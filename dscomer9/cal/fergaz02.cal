|FICHEROS;
     fergaz02 #0;
     fergam02 #2;
     fergam03 #3;
     fergam06 #6;
     fergam07 #7;
     fergam08 #8;
     fergam10 #10;
     fergam11 #11;
     fergaw03 #100;
     agifa010 #110;
     fergaw11 #111;
     referen@;
|FIN;

|VARIABLES;
     nUnidades = 0;
     nUniRepar = 0;
     nSwCuenta = 0;
     nCuenta1 = 0;
     nCuenta2 = 0;
     aQueBoton = "";
     nBotonAceptar = 0;
     nBotonCancelar = 0;
     aID = "";
     nActual = 0;
     nSal = 0;
     nId = 0;
     nPanta = 0;
     &Tempo = "";
     aTmp100 = "";
     nRango = 0;
     nGrid = 0;
     aAlfa = "";
     nTecla = 0;
     aEstado = "";
     fHoy = @;
     nLin = 0;
     aHora = "";
     nHay = 0;
     nError = 0;
     nHora1 = 0;
     nHora2 = 0;
     nMinu = 0;
     nHora = 0;
     nTiempo = 0;
     nTraba = 0;
     nNumTra = 0;
     nHoraTra = 0;
     nSw = 0;
     aFinalizada = "";
     nFabricadas = 0;
     aFin = "";
     nMaqui = 0;
     aArti = "";
|FIN;

|PROCESO MaquiLin;
     #11#0 = #7#0
     #11#1 = #7#1
     #11#2 = #3#4;
     |GRABA 000#11n; || Sin errores, se puede repetir
|FPRC;

|DEFBUCLE MaquiLin;
     #3#2;
     ;
     #7#2, #0#0;
     #7#2, #0#0;
     ;
     NULL, MaquiLin;
|FIN;

|PROCESO OrdenCompo;
     |BUCLE MaquiLin;
|FPRC;

|DEFBUCLE OrdenCompo;
     #7#1;
     ;
     #0#2, 001;
     #0#2, 999;
     ;
     NULL, OrdenCompo;
|FIN;

|PROCESO GrabaPartidas;
     |ABRE #11;
     |BUCLE OrdenCompo;
     |CIERRA #11;
|FPRC;

|PROCESO TiempoMaq;
     nHora = (A\(#8#5 % 102));
     nMinu = (A\(#8#5 % 402));
     nHora1 = nHora * 60 + nMinu;

     nHora = (A\(#8#6 % 102));
     nMinu = (A\(#8#6 % 402));
     nHora2 = nHora * 60 + nMinu;

     |SI nHora2 < nHora1;
          nTiempo = ((24*60 - nHora1) + nHora2) / 60;
     |SINO;
          nTiempo = (nHora2 - nHora1) / 60;
     |FINSI;
|FPRC;

|PROCESO TiempoTra;
     nHora = (A\(#10#3 % 102));
     nMinu = (A\(#10#3 % 402));
     nHora1 = nHora * 60 + nMinu;

     nHora = (A\(aHora % 102));
     nMinu = (A\(aHora % 402));
     nHora2 = nHora * 60 + nMinu;

     |SI nHora2 < nHora1;
          nTiempo = ((24*60 - nHora1) + nHora2) / 60;
     |SINO;
          nTiempo = (nHora2 - nHora1) / 60;
     |FINSI;
|FPRC;

|PROCESO ActTraba;
     |SI #10#4 = "        ";
          |HAZ TiempoTra;
     |SINO;
          nTiempo = #10#5;
     |FINSI;
     #10#6 = #10#6 + (nUnidades / nHoraTra * nTiempo);
     |GRABA 020#10a;
|FPRC;

|DEFBUCLE ActTraba;
     #10#1;
     ;
     #8#3, #8#4, 0001, "     ";
     #8#3, #8#4, 9999, "zzzzz";
     be;
     NULL, ActTraba;
|FIN;

|PROCESO CuentaTraba;
     |SI nTraba ! #10#2;
          nTraba = #10#2;
          nNumTra = nNumTra + 1;
     |FINSI;

     |SI #10#4 = "        ";
          |HAZ TiempoTra;
     |SINO;
          nTiempo = #10#5;
     |FINSI;
     nHoraTra = nHoraTra + nTiempo;
|FPRC;

|DEFBUCLE CuentaTraba;
     #10#1;
     ;
     #8#3, #8#4, 0001, "     ";
     #8#3, #8#4, 9999, "zzzzz";
     ;
     NULL, CuentaTraba;
|FIN;

|PROCESO ParaMaqui;
     aAlfa = #8#6; |QBF aAlfa;
     |SI #8#10 = "F"; |O aAlfa ! "";
          |ACABA;                  || Esta parada
     |FINSI;

     |SI nSwCuenta = 1;
          nCuenta1 = nCuenta1 + 1;
     |SINO;
          nCuenta2 = nCuenta2 + 1;
          nNumTra = 0;
          nHoraTra = 0;
          nTraba = -1;
          |BUCLE CuentaTraba;
          #8#6 = aHora;
          |HAZ TiempoMaq;
          #8#7 = nTiempo;
          #8#8 = nNumTra;
          #8#9 = nUnidades;
          nUniRepar = nUniRepar + #8#9;
          |SI nCuenta1 = nCuenta2; || Acumula el resto en la ultima
               #8#9 = #8#9 + (#0#4 + #0#5 + #0#7 + #0#8 - nUniRepar);
          |FINSI;

          |BUCLE ActTraba;

          |SI #0#3 = "F";
               #8#10 = "S";
          |FINSI;
          |GRABA 020#8a;
     |FINSI;
|FPRC;

|DEFBUCLE ParaMaqui;
     #8#2;
     ;
     #0#0, #6#0;
     #0#0, #6#0;
     be;
     NULL, ParaMaqui;
|FIN;

|PROCESO TodasFinalizadas;
     aAlfa = #8#6; |QBF aAlfa;
     |SI #8#10 = "N"; |Y aAlfa = "";
          aFinalizada = "N";
          |ERROR10;
     |FINSI;
     nFabricadas = nFabricadas + #8#9;
|FPRC;

|DEFBUCLE TodasFinalizadas;
     #8#1;
     ;
     #6#0, 001, 001;
     #6#0, 999, 999;
     ;
     NULL, TodasFinalizadas;
|FIN;

|PROCESO OrdenesPara;
     nCuenta1 = 0;
     nCuenta2 = 0;
     nSwCuenta = 1; |BUCLE ParaMaqui;

     nUnidades = (#0#4 + #0#5 + #0#7 + #0#8) / nCuenta1;
     nUniRepar = 0;
     nSwCuenta = 2; |BUCLE ParaMaqui;

     |SI #0#3 = "F";
/*
          nFabricadas = 0;
          aFinalizada = "S";
          |BUCLE TodasFinalizadas;
          #6#7 = nFabricadas;
*/
          |SI #2#2 = "S";     || Elabora
               #6#7 = #6#7 + #0#4;
          |FINSI;
          |SI #2#3 = "S";     || Armado Kg
               #6#11 = #6#11 + #0#5;
          |FINSI;
          |SI #2#4 = "S";     || Armados Puntos
               #6#16 = #6#16 + #0#7;
          |FINSI;
          |SI #2#5 = "S";     || Pilotes
               #6#17 = #6#17 + #0#8;
          |FINSI;

          |SI aFinalizada = "S";
/*
               #6#8 = "S";
               |HAZ AdjPedido;
               #6#9 = #110#52;
               #6#10 = #110#0;
*/
          |FINSI;
          |GRABA 020#6a;

     |FINSI;
|FPRC;

|DEFBUCLE OrdenesPara;
     #6#2;
     ;
     "N", #0#2;
     "N", #0#2;
     be;
     NULL, OrdenesPara;
|FIN;

|PROCESO IniciaMaqui;
     #6#0 = #111#0;
     |LEE 000#6=;
     |SI FSalida = 0; |Y #6#8 = "N";
          #8#0 = #111#0;
          #8#1 = #111#1;
          #8#2 = 999;
          |LEE 000#8];
          |SI FSalida = 0;
               |LEE 000#8a;
          |SINO;
               |LEE 000#8u;
          |FINSI;
          |SI FSalida = 0; |Y #8#0 = #111#0; |Y #8#1 = #111#1;
               nLin = #8#2 + 1;
          |SINO;
               nLin = 1;
          |FINSI;
          |DDEFECTO #8;
          #8#0 = #111#0;
          #8#1 = #111#1;
          #8#2 = nLin;
          #8#3 = fHoy;
          #8#4 = #0#0;
          #8#5 = aHora;
          |GRABA 020#8n;
          nHay = 1;
          aArti = #111#2;
     |FINSI;
|FPRC;

|DEFBUCLE TmpComponentes;
     #111#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, IniciaMaqui;
|FIN;

|PROCESO Componentes;
     #111#0 = #7#0;
     #111#1 = #7#1;
     #111#2 = #7#2;
     |GRABA 000#111n;
|FPRC;

|DEFBUCLE Componentes;
     #7#2;
     ;
     #0#2, #3#2;
     #0#2, #3#2;
     ;
     NULL, Componentes;
|FIN;

|PROCESO MaquinasLin;
     |BUCLE Componentes;
|FPRC;

|DEFBUCLE MaquinasLin;
     #3#1;
     ;
     #0#0, 001;
     #0#0, 999;
     ;
     NULL, MaquinasLin;
|FIN;

|PROCESO OrdenMaqui;
     aAlfa = #8#6; |QBT aAlfa;
     |SI aAlfa = "";
          aEstado = "I";
     |SINO;
          aEstado = "P";
     |FINSI;
     |SI #8#10 = "S";
          aFin = "S";
     |FINSI;
|FPRC;

|DEFBUCLE OrdenMaqui;
     #8#2;
     ;
     #0#0, #0#2;
     #0#0, #0#2;
     ;
     NULL, OrdenMaqui;
|FIN;

|PROCESO Chequea;
     nError = 0;
     |ABRE #6;
     #6#0 = #0#2;
     |LEE 000#6=;
     |SI FSalida = 0;
          |SI #6#8 = "S";
               |MENAV "0000Orden de producci¢n finalizada...";
               nError = 1;
          |FINSI;
     |FINSI;
     |CIERRA #6;
     |SI nError ! 0; |ACABA; |FINSI;

     aEstado = "";
     aFin = "N";
     |BUCLE OrdenMaqui;
     |SI aFin = "S";
        ||  |MENAV "0000La m quina ya esta finalizada...";
        ||  nError = 1;
     |SINO;
          |SI #0#3 = "I"; |Y aEstado = "I";
               |MENAV "0000La m quina ya esta iniciada...";
               nError = 1;
          |FINSI;
          |SI #0#3 = "P"; |O #0#3 = "F";
                |SI aEstado = "P";
                    |MENAV "0000La m quina ya esta parada...";
                    nError = 1;
               |FINSI;
          |FINSI;
          |SI #0#3 = "P"; |O #0#3 = "F";
               |SI aEstado = "";
                    |MENAV "0000La m quina no se ha iniciado...";
                    nError = 1;
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO IniElaborada;
     #8#0 = #0#2;
     #8#1 = 999;
     #8#2 = 999;
     |LEE 000#8];
     |SI FSalida = 0;
          |LEE 000#8a;
     |SINO;
          |LEE 000#8u;
     |FINSI;
     |SI FSalida = 0; |Y #8#0 = #0#2; |Y #8#1 = 999;
          nLin = #8#2 + 1;
     |SINO;
          nLin = 1;
     |FINSI;
     |DDEFECTO #8;
     #8#0 = #0#2;
     #8#1 = 999;
     #8#2 = nLin;
     #8#3 = fHoy;
     #8#4 = #0#0;
     #8#5 = aHora;
     |GRABA 020#8n;

     |DDEFECTO #7;
     #7#0 = #0#2;
     #7#1 = 999;
     #7#3 = "ELABORACION";
     |GRABA 000#7n;

     aArti = #7#2;
|FPRC;

|PROCESO T3; |TIPO 3;
     fHoy = ~;
     |HORA aHora;

     |HAZ Chequea;
     |SI nError ! 0; |ACABA; |FINSI;

     |ABRE #2;
     #2#0 = #0#0;
     |LEE 020#2=;
     |CIERRA #2;

     |SI #0#3 = "I";
          |HAZ GrabaPartidas;

          |SI #2#3 = "S"; |O #2#4 = "S"; |O #2#5 = "S";
               |ABRE #8;
               |ABRE #7;
               |HAZ IniElaborada;
               |CIERRA #7;
               |CIERRA #8;
          |SINO;
               nHay = 0;
               |ABRE #6;
               |ABRE #8;
               |HAZ CreaTempo; |NOME_DAT #111 Tempo; |DELINDEX #111;

               |ABRE #111;
               |BUCLE MaquinasLin;
               |CIERRA #111;
               |BUCLE TmpComponentes;
               |HAZ BoraTempo; |DELINDEX #111;
               |CIERRA #6;
               |CIERRA #8;
               |SI nHay = 0;
                    |MENAV "0000La orden no tiene art¡culos a fabricar en esta m quina...";
               |FINSI;
          |FINSI;
     |FINSI;
     |SI #0#3 = "P"; |O #0#3 = "F";
          |BUCLE OrdenesPara;
     |FINSI;

||    |HAZ CargaOrden;
|| ahora no refresca los datos, solo marca la orden
||
     #100#0 = #0#2;
     |LEE 000#100=;
     |SI FSalida = 0;
          |SI #0#3 = "I";
               |HORA aAlfa;
               #100#3 = aAlfa;
               #100#4 = "ACTIVA";
               |GRABA 020#100a;
          |FINSI;
          |SI #0#3 = "P";
               #100#4 = "PARADA";
               |GRABA 020#100a;
          |FINSI;
          |SI #0#3 = "F";
               |BORRA 000#100c;
          |FINSI;
     |SINO;
          #100#0 = #0#2;
          #100#1 = #6#4; || aArti;
          #100#2 = #6#6;
          #100#3 = aHora;
          #100#4 = "ACTIVA";
          |GRABA 020#100n;
     |FINSI;
     |REFRESCACONTROL nGrid;
|FPRC;

|PROCESO Unidad; TIPO 0;
     |SI FSalida = 0; |Y #0#3 = "F";
          |C_M #0Campo, 0, aAlfa;
          |SI aAlfa = "S";
               |PTEC 806;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO MiraFinMaqui;
     |SI #8#10 = "S";
          aFin = "S";
     |FINSI;
|FPRC;

|DEFBUCLE MiraFinMaqui;
     #8#2;
     ;
     #0#0, #0#2;
     #0#0, #0#2;
     ;
     NULL, MiraFinMaqui;
|FIN;

|PROCESO Orden0; |TIPO 0;
     |SI FSalida ! 0; |ACABA; |FINSI;
     |ABRE #6;
     #6#0 = #0#2;
     |LEE 000#6=;
     |SI FSalida = 0;
          |SI #6#8 = "S";
               |MENAV "0000Orden de producci¢n finalizada...";
               |ERROR;
          |SINO;
/*
               aFin = "N";
               |BUCLE MiraFinMaqui;
               |SI aFin = "S";
                    |MENAV "0000La m quina ya esta finalizada...";
                    |ERROR;
               |FINSI;
*/
          |FINSI;
     |SINO;
          |MENAV "0000No existe orden de producci¢n...";
          |ERROR;
     |FINSI;
     |CIERRA #6;
|FPRC;

|PROCESO OrdenesMaqui;
     |SI #8#10 = "N";
          aAlfa = #8#6; |QBF aAlfa;
          #100#0 = #6#0;
          |LEE 101#100=;
          |SI FSalida ! 0; |GRABA 020#100b; |FINSI;
          #100#1 = #6#4;
          #100#2 = #6#6;
          #100#3 = #8#5;
          |SI aAlfa = "";
               #100#4 = "ACTIVA";
          |SINO;
               #100#4 = "PARADA";
          |FINSI;
          |GRABA 000#100a;
          |LIBERA #100;
     |SINO;
          #100#0 = #6#0;
          |BORRA 000#100c;
     |FINSI;
|FPRC;

|DEFBUCLE OrdenesMaqui;
     #8#2;
     ;
     #0#0, #6#0;
     #0#0, #6#0;
     ;
     NULL, OrdenesMaqui;
|FIN;

|PROCESO Ordenes;
     |SI nSw = 2;
          |SALVA_DATOS #6;
          |REPON_DATOS #referen@;
          |GRABA 020#referen@.n;
     |SINO;
          |BUCLE OrdenesMaqui;
     |FINSI;
|FPRC;

|DEFBUCLE Ordenes;
     #6#2;
     ;
     "N", 000001;
     "N", 999999;
     ;
     NULL, Ordenes;
|FIN;

|PROCESO Orden66; |TIPO 66;
     |DDEF aAlfa;
     aAlfa = aAlfa + "fergam06";
     |CARGA_DEF aAlfa, referen@;
     |SI FSalida = 0;
          |HAZ CreaTempo;
          |NOME_DAT #referen@#Tempo#; |DELINDEX #referen@; |ABRE #referen@;
          nSw = 2; |BUCLE Ordenes;
          #referen@#0 = #0#2;
          |LEE 000#referen@.];
          |CONSULTA_CLAVE #1#referen@;
          |SI FSalida = 0;
               #0#2 = #referen@#0; |PINTA #0#2;
          |SINO;
               |ERROR;
          |FINSI;
          |CIERRA #referen@; |HAZ BoraTempo; |DELINDEX #referen@;
          |DESCARGA_DEF #referen@;
     |FINSI;
|FPRC;

|PROCESO CargaOrden;
     nActual = #100#0;
     |CIERRA #100; |DELINDEX #100; |ABRE #100;

     nSw = 1; |BUCLE Ordenes;

     #100#0 = nActual;
     |LEE 000#100=;

     |REFRESCACONTROL nGrid;
|FPRC;

|PROCESO Maqui7; |TIPO 7;
     nMaqui = #0#0;
/*
     aQueBoton = "|X000";
     aID = #0#aQueBoton#;
     nBotonAceptar = aID;
     |ESTADO_CONTROL nBotonAceptar, "HIDE";
*/
     aQueBoton = "|X001";
     aID = #0#aQueBoton#;
     nBotonCancelar = aID;
     |ESTADO_CONTROL nBotonCancelar, "HIDE";
|FPRC;

|PROCESO Maqui0; |TIPO 0;
     |SI nSal = 1; |ACABA; |FINSI;

     |SI nMaqui = #0#0; |ACABA; |FINSI;

     |ABRE #2;
     #2#0 = #0#0;
     |LEE 000#2=;
     |SI FSalida = 0;
          #0#1 = #2#1; |PINTA #0#1;
          |SI #2#2 = "S"; |O #2#3 = "S"; |O #2#4 = "S"; |O #2#5 = "S";
               |HAZ CargaOrden;
          |SINO;
               |MENAV "0000La maqu¡na no es de producci¢n/elaboraci¢n...";
               |ERROR;
          |FINSI;
     |SINO;
          |MENAV "0000No existe maqu¡na...";
          |ERROR;
     |FINSI;
     |CIERRA #2;
|FPRC;

|PROCESO Maquinas;
     |SI #2#2 = "S"; |O #2#3 = "S"; |O #2#4 = "S"; |O #2#5 = "S";
          |SALVA_DATOS #2;
          |REPON_DATOS #referen@;
          |GRABA 020#referen@.n;
     |FINSI;
|FPRC;

|DEFBUCLE Maquinas;
     #2#1;
     ;
     001;
     999;
     ;
     NULL, Maquinas;
|FIN;

|PROCESO Maqui66; |TIPO 66;
     |DDEF aAlfa;
     aAlfa = aAlfa + "fergam02";
     |CARGA_DEF aAlfa, referen@;
     |SI FSalida = 0;
          |HAZ CreaTempo;
          |NOME_DAT #referen@#Tempo#; |DELINDEX #referen@; |ABRE #referen@;
          |BUCLE Maquinas;
          #referen@#0 = #0#0;
          |LEE 000#referen@.];
          |CONSULTA_CLAVE #1#referen@;
          |SI FSalida = 0;
               #0#0 = #referen@#0; |PINTA #0#0;
          |SINO;
               |ERROR;
          |FINSI;
          |CIERRA #referen@; |HAZ BoraTempo; |DELINDEX #referen@;
          |DESCARGA_DEF #referen@;
     |FINSI;
|FPRC;

|PROCESO Evento;
     |SI FSalida = 0;
          |LEE 000#100=;
     |FINSI;
     |SI FSalida = 4;
          #0#2 = #100#0; |PINTA #0#2;
     |FINSI;
|FPRC;

|PROCESO T80; |TIPO 80;
     FSalida = 2499;
|FPRC;

|PROCESO BtnInicia;
     #0#3 = "I";
     #0#6 = "INICIA"; |PINTA #0#6;
     |C_M #0#4, 1, "N"; |PINTA #0#4;
     |C_M #0#5, 1, "N"; |PINTA #0#5;
     |C_M #0#7, 1, "N"; |PINTA #0#7;
     |C_M #0#8, 1, "N"; |PINTA #0#8;

     |HAZ Chequea;
     |SI nError = 0; |PTEC 806; |FINSI;
|FPRC;

|PROCESO BtnPara;
     #0#3 = "P";
     #0#6 = "PARA"; |PINTA #0#6;
     |C_M #0#4, 1, "N"; |PINTA #0#4;
     |C_M #0#5, 1, "N"; |PINTA #0#5;
     |C_M #0#7, 1, "N"; |PINTA #0#7;
     |C_M #0#8, 1, "N"; |PINTA #0#8;

     |HAZ Chequea;
     |SI nError = 0; |PTEC 806; |FINSI;
|FPRC;

|PROCESO BtnFinaliza;
     #0#3 = "F";
     #0#6 = "FINALIZA"; |PINTA #0#6;
     |HAZ Chequea;

     |SI nError = 0;
          |SI #2#2 = "S";
               |C_M #0#4, 1, "S"; |PINTA #0#4;
               aAlfa = "|C004WID";
               aAlfa = #0#aAlfa#;
               nId = aAlfa;
          |FINSI;
          |SI #2#3 = "S";
               |C_M #0#5, 1, "S"; |PINTA #0#5;
               aAlfa = "|C005WID";
               aAlfa = #0#aAlfa#;
               nId = aAlfa;
          |FINSI;
          |SI #2#4 = "S";
               |C_M #0#7, 1, "S"; |PINTA #0#7;
               aAlfa = "|C007WID";
               aAlfa = #0#aAlfa#;
               nId = aAlfa;
          |FINSI;
          |SI #2#5 = "S";
               |C_M #0#8, 1, "S"; |PINTA #0#8;
               aAlfa = "|C008WID";
               aAlfa = #0#aAlfa#;
               nId = aAlfa;
          |FINSI;
          |FOCOTECLADO nId;
     |FINSI;
|FPRC;

|PROCESO Tecla; |TIPO 6;
     |SI FSalida = 13;
          nSal = 1;
          |PTEC 807;
     |FINSI;
|FPRC;

|PROCESO BtnFin;
     nSal = 1;
     |PTEC 807;
|FPRC;

|PROGRAMA;
     |PINPA #0#0; nPanta = FSalida;
     |PINDA #0#0;
     |CONTROL_SIMPLE nPanta, "BOTON,Inicia", 2026, 2236, BtnInicia;
     |CONTROL_SIMPLE nPanta, "BOTON,Para", 2038, 2248, BtnPara;
     |CONTROL_SIMPLE nPanta, "BOTON,Finaliza", 2050, 2260, BtnFinaliza;
     |CONTROL_SIMPLE nPanta, "BOTON,SALIR", 0585, 0699, BtnFin;

     |HAZ CreaTempo;
     |NOME_DAT #100 Tempo; aTmp100 = Tempo; |DELINDEX #100; |ABRE #100;
     |PARA; |SI; |HACIENDO;
          #0#2 = 0; |PINTA #0#2;
          #0#4 = 0; |PINTA #0#4;
          #0#5 = 0; |PINTA #0#5;
          #0#6 = ""; |PINTA #0#6;
          #0#7 = 0; |PINTA #0#7;
          #0#8 = 0; |PINTA #0#8;
          |C_M #0#4, 1, "N"; |PINTA #0#4;
          |C_M #0#5, 1, "N"; |PINTA #0#4;
          |C_M #0#7, 1, "N"; |PINTA #0#7;
          |C_M #0#8, 1, "N"; |PINTA #0#8;
          nRango = 4 + 8 + 16 + 32;
          |SI nGrid ! 0; |DESTRUYECONTROL nGrid; |FINSI;
          |LINEAL_SIMPLE #1#100, nRango, 1203, 1998, NULL, NULL, Evento;
          nGrid = FSalida;
          |ENDATOS #1#0;
          |SI nSal = 1; |SAL; |FINSI;
     |SIGUE;
     |CIERRA #100; |DELINDEX #100; Tempo = aTmp100; |HAZ BoraTempo;
|FPRO;
