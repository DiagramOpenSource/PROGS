|FICHEROS;
     vigz0024 #0;
     vigm0001 #1;
     vigm0002 #2;
     vigm0003 #3;
     vigm0018 #18;
     agifa019 #19,MANTE;
|FIN;

|VARIABLES;
     aVar = "";
     aOrg = "";
     aDes = "";
     aAlfa = "";
     aAlf1 = "";
     aAlf2 = "";
     aAlf3 = "";
     aRuta = "";
     aBrill = "";
     aDato = "";
     aArt = "";
     aNum = "";
     aFormula = "";
     nBoton1 = 0;
     nBoton2 = 0;
     nLin = 0;
     nError = 0;
     nHandle1 = 0;
     nHandle2 = 0;
     nLinFormula = 0;
|FIN;

|PROCESO LinFormula;
     nLinFormula = nLinFormula + 1;
     aArt = aDato % 2305;
     aNum = aDato % 1110;
     aVar = aDato % 2801;
     |DDEFECTO #19;
     #19#0 = aArt;
     |LEE 000#19=;
     |SI FSalida ! 0;
          |HAZ AltaArti;
     |FINSI;
     |DDEFECTO #1;
     #1#0 = #19#0;
     |LEE 000#1=;
     |DDEFECTO #3;
     #3#0 = #2#0;
     #3#1 = nLinFormula;
     #3#2 = #19#0;
     #3#3 = #19#1;
     #3#4 = aNum;
     #3#6 = #1#9;
     #3#7 = aVar;
     |GRABA 020#3n;
|FPRC;

|PROCESO BorraLin;
     |BORRA 020#3a;
|FPRC;

|DEFBUCLE BorraLin;
     #3#1;
     ;
     #2#0, 001;
     #2#0, 999;
     be;
     NULL, BorraLin;
|FIN;

|PROCESO RecalFormula;
     #2#3 = #2#3 + #3#4;
|FPRC;

|DEFBUCLE RecalFormula;
     #3#1;
     ;
     #2#0, 001;
     #2#0, 999;
     be;
     NULL, RecalFormula;
|FIN;

|PROCESO CabFormula;
     |SI aFormula ! "";
          #2#3 = 0;
          |BUCLE RecalFormula;
          |GRABA 020#2a;
          |LIBERA #2;
     |FINSI;
     |DDEFECTO #2;
     #2#0 = #18#2;
     |LEE 101#2=;
     |SI FSalida = 0;
          |CIERRA #3;
          |BUCLE BorraLin;
          |ABRE #3;
     |SINO;
          |GRABA 020#2b;
     |FINSI;

     aAlfa = "F" + (("000" + #2#0) % -103);
     #2#1 = aAlfa;

     |DDEFECTO #19;
     #19#0 = #2#1;
     |LEE 000#19=;
     |SI FSalida ! 0;
          |HAZ AltaArti;
     |FINSI;
     #2#2 = #19#1;

     |GRABA 020#2a;
     |LIBERA #2;

     nLinFormula = 0;
     aFormula = #2#0;
|FPRC;

|PROCESO AltaArti;
     aAlfa = "0000No existe Articulo:" + #19#0;
     |MENAV aAlfa;
     |PUSHV 05012380;
     |PINPA #0#19;
     |PINDA #0#19;
     |PARA; |SI; |HACIENDO;
          |ENDATOS #1#19;
          |SI FSalida = 0;
               |GRABA 020#19n;
               nError = FSalida;
               |SAL;
          |SINO;
               nError = 1;
               |CONFI "0000¿Abandonar la importacion?";
               |SI FSalida = 0; |SAL; |FINSI;
          |FINSI;
     |SIGUE;
     |POPV;
|FPRC;

|PROCESO AltaBrill;
     aAlfa = "0000No existe codigo Brill:" + aBrill;
     |MENAV aAlfa;

     |SELECT #1#18;
     |LEE 000#18u;
     |SI FSalida = 0;
          nLin = #18#0 + 1;
     |SINO;
          nLin = 1;
     |FINSI;
     |PUSHV 05012380;
     |DDEFECTO #18;
     #18#0 = nLin;
     #18#1 = aBrill;
     |PINPA #0#18;
     |PINDA #0#18;
     |C_M #18#0, 1, "N";
     |C_M #18#1, 1, "N";
     |PARA; |SI; |HACIENDO;
          |ENDATOS #1#18;
          |SI FSalida = 0;
               |GRABA 020#18n;
               nError = FSalida;
               |SAL;
          |SINO;
               nError = 1;
               |CONFI "0000¿Abandonar la importacion?";
               |SI FSalida = 0; |SAL; |FINSI;
          |FINSI;
     |SIGUE;
     |POPV;
|FPRC;

|PROCESO Importar;
     |PARA; |SI; |HACIENDO;
          |XLEE nHandle1, 250, aDato;
          |SI FSalida < 0; |SAL; |FINSI;
          aAlfa = aDato % 102;
          |SI aAlfa = "FS";
               aBrill = aDato % 808;
               |SELECT #2#18;
               #18#1 = aBrill;
               |LEE 000#18=;
               |SI FSalida ! 0;
                    |HAZ AltaBrill;
               |FINSI;
               |SI nError ! 0; |SAL; |FINSI;
               aAlf1 = aDato % 107;
               aAlf3 = aDato % 16;
               aAlf2 = #18#2; |QBF aAlf2;
               aAlf2 = (aAlf2 + "       ") % 107;
               aAlf2 = #18#3 + aAlf2;
               aDato = aAlf1 + aAlf2 + aAlf3;
               |HAZ CabFormula;
          |SINO;
               aAlfa = aDato % 102;
               |SI aAlfa = "FI";
                    |HAZ LinFormula;
               |FINSI;
          |FINSI;
          |SI nError ! 0; |SAL; |FINSI;
          |XGRABA nHandle2, aDato;
     |SIGUE;
     |SI aFormula ! "";
          #2#3 = 0;
          |BUCLE RecalFormula;
          |GRABA 020#2a;
          |LIBERA #2;
     |FINSI;
|FPRC;

|PROCESO Tipo3; |TIPO 3;
     |ABRE #19;
     |ABRE #18;
     |ABRE #1;
     |ABRE #2;
     |ABRE #3;

     aOrg = #0#1; |QBF aOrg;
     aDes = #0#2; |QBF aDes;
     |SI aOrg = aDes;
          |MENAV "0000El origen y destino no puede ser el mismo...";
          |ACABA;
     |FINSI;
     |XABRE "C", aOrg, nHandle1;
     |SI FSalida < 0;
          |MENAV "0000Error al abrir fichero origen...";
     |SINO;
          |XABRE "E", aDes, 0;
          |SI FSalida = 0;
               |CONFI "0000NEl destino existe. ¿Sobreescribir?";
               |SI FSalida = 0; FSalida = 1; |SINO; FSalida = 0; |FINSI;
          |FINSI;
          |SI FSalida ! 0;
               |XABRE "WC", aDes, nHandle2;
               |SI FSalida < 0;
                    |MENAV "0000Error al abrir fichero destino...";
               |SINO;
                    |HAZ Importar;
                    |XCIERRA nHandle2;
               |FINSI;
          |FINSI;
          |XCIERRA nHandle1;
     |FINSI;

     |CIERRA #3;
     |CIERRA #2;
     |CIERRA #1;
     |CIERRA #18;
     |CIERRA #19;
|FPRC;

|PROCESO Quita; |TIPO 0;
     |SI FSalida = 10;
          aRuta = "F*.*";
          |BROWSE aRuta;
          aRuta = aRuta % 2;
          |SI aRuta = "F"; |ERROR; |ACABA; |FINSI;
          #0Campo = aRuta; |PINTA #0Campo;
     |FINSI;

     |SI Campo = 1;
          |ESTADO_CONTROL nBoton1, "DISABLE";
     |SINO;
          |ESTADO_CONTROL nBoton2, "DISABLE";
     |FINSI;
|FPRC;

|PROCESO Pone; |TIPO 7;
     |SI Campo = 1;
          |ESTADO_CONTROL nBoton1, "ENABLE";
     |SINO;
          |ESTADO_CONTROL nBoton2, "ENABLE";
     |FINSI;
|FPRC;

|PROCESO Boton;
     |PTEC 824;
|FPRC;

|PROGRAMA;
     |CONTROL_SIMPLE 0, "BOTON,Fichero Origen", 1011, 1027, Boton;
     nBoton1 = FSalida;
     |CONTROL_SIMPLE 0, "BOTON,Fichero Destino", 1211, 1227, Boton;
     nBoton2 = FSalida;
     |ESTADO_CONTROL nBoton1, "DISABLE";
     |ESTADO_CONTROL nBoton2, "DISABLE";

     |CLS;
     |ABRE #0;
     |LEE 101#0p;
     |SI FSalida ! 0; |GRABA 020#0b; |FINSI;
     |PINPA #0#0;
     |PINDA #0#0;
     |ENDATOS #1#0;
     |SI FSalida = 0;
          |GRABA 020#0a;
     |FINSI;
     |CIERRA #0;

     |FIN_CONTROL_WINDOWS nBoton1;
     |FIN_CONTROL_WINDOWS nBoton2;
     |PULSATECLA;
|FPRO;
