|FICHEROS;
     gopre001 #0;
     gopre002 #1;
     gopre003 #2;
     gopre004 #3;
     goprenun #5;
     goapaive #6;
     gosubive #7;
|FIN;
----------------------------------------------------------------------
|VARIABLES;
     aEsAlfa = "";
     aAlfa = "";
     aAlfa1 = "";
     aAlfa2 = "";
     aAlfa3 = "";
     aAlfa4 = "";
     nLinNume = 0;
     nNume = 0;

     &cSerie   = "";
     &cCapitu  = "";

     &nNumero  = 0;

     BAutoma   = 0;
     nLinea    = 0;
     nModo     = 0;
     nCampo    = 0;

     cUsuario  = "";
     cFichero  = "";

|FIN;
----------------------------------------------------------------------
|PROCESO GrabaPartida;
     |PARA nCampo = 0; |SI nCampo [ 24; |HACIENDO nCampo = nCampo + 1;
          #5nCampo = #2nCampo;
     |SIGUE;
     aAlfa = #2#25; |QBF aAlfa;
     |SI aAlfa = "";
          #5#26 = ("0000" + #2#3) % -104;
     |SINO;
          #5#26 = #2#25;
     |FINSI;
     #5#28 = #5#26;
     #5#29 = #5#27;
     #5#30 = #5#28;
     #5#31 = #5#29;
     |GRABA 020#5c;
|FPRC;
-------------------------------------------------------------------------------
|DEFBUCLE TomaCapitulo;
     #2#1;
     ;                                          ||Campos Extras.
     cSerie, nNumero, cCapitu,   1;
     cSerie, nNumero, cCapitu, 999;
     ;                                          ||b/e Bolqueo-Ordenacion.
     NULL, GrabaPartida;
|FIN;
-------------------------------------------------------------------------------
|PROGRAMA;
     aAlfa = "|NC";
     aAlfa = #2#aAlfa#;
     |SI aAlfa ! 30;
          |MENAV "0000ERROR, la estructura del gopre003 ha cambiado, consulte con su distribuidor";
          |ACABA;
     |FINSI;

     cFichero = Usuario;
     |NOME_DAT #5 cFichero;
     |DELINDEX #5;
     |ABRE #5;
     |BUCLE TomaCapitulo;
     |CONTROL_SIMPLE 0, "BOTON, Renumeación Automatica", 2725, 2760, BAutoma;
     BAutoma = FSalida;
     |ESTADO_CONTROL BAutoma, "SHOW";
     nModo = 23;

     |PARA; |SI nModo ] 23; |HACIENDO;
          |HAZ TipoSalida;
     |SIGUE;

     |HAZ GrabCamb;
     |CIERRA #5;
     |DELINDEX #5;
|FPROG;
-------------------------------------------------------------------------------
|PROCESO FinPrograma; |TIPO 90;
     |PULSATECLA;
     |FIN_CONTROL_WINDOWS BAutoma;
     |PULSATECLA;
|FPRC;
-------------------------------------------------------------------------------
|PROCESO Renum;
     nLinea = nLinea + 1;
     #5#26 = ("0000" + nLinea) % -104;
     |GRABA 020#5a;
|FPRC;
-------------------------------------------------------------------------------
|DEFBUCLE Renum;
     #5#2;
     ;                                          ||Campos Extras.
     INICIO;
     FINAL;
     ;                                          ||b/e Bolqueo-Ordenacion.
     NULL, Renum;
|FIN;
-------------------------------------------------------------------------------
|PROCESO BAutoma;
     FSalida = 10;
     nLinea = 0;
     |BUCLE Renum;
     |PTEC 806;
     nModo = 23;
|FPRC;
-------------------------------------------------------------------------------
|PROCESO MinPres;
     #5#3 = 1;
|FPRC;
-------------------------------------------------------------------------------
|PROCESO MaxPres;
     #5#3 = 999;
|FPRC;
-------------------------------------------------------------------------------
|PROCESO TipoSalida;
     nModo = 0;
     |PINPA #0#5;
     |ENTLINEAL #1#5, -1, 2, 21, 0, MinPres, MaxPres;
|FPRC;
-------------------------------------------------------------------------------
|PROCESO ActRen;
     |PARA nCampo = 0; |SI nCampo [ 24; |HACIENDO nCampo = nCampo + 1;
          |SI nCampo = 3;
               |SI aEsAlfa = "S";
                    #2nCampo = #5#27;
                    #2#25 = #5#26;
               |SINO;
                    #2nCampo = #5#26;
                    #2#25 = ("0000" + #5#26) % -104;
               |FINSI;
          |SINO;
               #2nCampo = #5nCampo;
          |FINSI;
     |SIGUE;
     #2#26 = #5#28;
     #2#27 = #5#29;
     #2#28 = #5#30;
     #2#29 = #5#31;
     |GRABA 020#2c;
|FPRC;
-------------------------------------------------------------------------------
|DEFBUCLE ActRen;
     #5#3;
     ;                                          ||Campos Extras.
     INICIO;
     FINAL;
     ;                                          ||b/e Bolqueo-Ordenacion.
     NULL, ActRen;
|FIN;
-------------------------------------------------------------------------------
|PROCESO BorraLin;
     |BORRA 020#2a;
|FPRC;
-------------------------------------------------------------------------------
|DEFBUCLE BorraLin;
     #2#1;
     ;                                          ||Campos Extras.
     cSerie, nNumero, cCapitu,   1;
     cSerie, nNumero, cCapitu, 999;
     ;                                          ||b/e Bolqueo-Ordenacion.
     NULL, BorraLin;
|FIN;

|PROCESO MiraAlfa;
     nLinNume = nLinNume + 1;
     #5#27 = nLinNume;
     |GRABA 020#5a;
     |LIBERA #5;

     aAlfa1 = #5#26 % 101;
     aAlfa2 = #5#26 % 201;
     aAlfa3 = #5#26 % 301;
     aAlfa4 = #5#26 % 401;
     |SI aAlfa1 [ "0"; |O aAlfa1 ] "9"; aEsAlfa = "S"; |FINSI;
     |SI aAlfa2 [ "0"; |O aAlfa2 ] "9"; aEsAlfa = "S"; |FINSI;
     |SI aAlfa3 [ "0"; |O aAlfa3 ] "9"; aEsAlfa = "S"; |FINSI;
     |SI aAlfa4 [ "0"; |O aAlfa4 ] "9"; aEsAlfa = "S"; |FINSI;
     nNume = #5#26;
     |SI nNume > 999; aEsAlfa = "S"; |FINSI;
|FPRC;

|DEFBUCLE MiraAlfa;
     #5#3;
     ;
     INICIO;
     FINAL;
     be;
     NULL, MiraAlfa;
|FIN;

|PROCESO GrabCamb;
     |CONFI "0000S¿DESEA ACTUALIZAR LA RENUMERACION?";

     |SI FSalida = 0;
          |BUCLE BorraLin;
          nLinNume = 0;
          aEsAlfa = "N";
          |BUCLE MiraAlfa;
          |ABRE #2;
          |BUCLE ActRen;
          |CIERRA #2;
     |FINSI;

|FPRC;
-------------------------------------------------------------------------------
|PROCESO Intro; |TIPO 0;
     |PTEC 802;
|FPRC;
-------------------------------------------------------------------------------
|PROCESO BSnum; |TIPO 7;
     |ESTADO_CONTROL BAutoma, "SHOW";
|FPRC;
-------------------------------------------------------------------------------
|PROCESO BHnum; |TIPO 7;
     |ESTADO_CONTROL BAutoma, "HIDE";
|FPRC;
-------------------------------------------------------------------------------
