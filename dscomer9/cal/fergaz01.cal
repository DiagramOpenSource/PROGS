|FICHEROS;
     fergaz01 #0;
     fergam01 #1;
     fergam02 #2;
     fergam05 #5;
     fergam06 #6;
     fergam10 #10;
     fergaw02 #100;  ||Tmp Traba
     referen@;
|FIN;

|VARIABLES;
     aQueBoton = "";
     nBotonAceptar = 0;
     nBotonCancelar = 0;
     aID = "";


     nPanta = 0;
     nSal = 0;
     &Tempo = "";
     fHoy = @;
     aTmp100 = "";
     aLon = "";
     aHora = "";
     aAlf1 = "";
     aAlf2 = "";
     aAlf3 = "";
     aAlfa = "";
     nNum = 0;
     nGrid = 0;
     nRango = 0;
     nSw = 0;
     nMaqui = 0;
     nHora1 = 0;
     nHora2 = 0;
     nMinu = 0;
     nHora = 0;
     nTiempo = 0;
|FIN;

|PROCESO Tiempo;
     nHora = (A\(#10#3 % 102));
     nMinu = (A\(#10#3 % 402));
     nHora1 = nHora * 60 + nMinu;

     nHora = (A\(#10#4 % 102));
     nMinu = (A\(#10#4 % 402));
     nHora2 = nHora * 60 + nMinu;

     |SI nHora2 < nHora1;
          nTiempo = ((24*60 - nHora1) + nHora2) / 60;
     |SINO;
          nTiempo = (nHora2 - nHora1) / 60;
     |FINSI;
|FPRC;

|PROCESO CambiaMaquina;
     |HAZ FinJornada;
     |HAZ InicioJornada;
|FPRC;

|PROCESO FinJornada;
     |LEE 101#10=;
     #10#4 = #0#4;
     |HAZ Tiempo;
     #10#5 = nTiempo;
     |GRABA 020#10a;
     |LIBERA #10;
|FPRC;

|PROCESO InicioJornada;
     |DDEFECTO #10;
     #10#0 = fHoy;
     #10#1 = #0#0;
     #10#2 = #0#2;
     #10#3 = #0#4;
     |GRABA 020#10n;
|FPRC;

|PROCESO Marcajes;
     |SI nSw = 1;
          |SI #0#0 = #10#1;
               #100#0 = #10#2;
               |LEE 000#100=;
               |SI FSalida ! 0;
                    #1#0 = #10#2;
                    |LEE 000#1=;
                    |SI FSalida = 0;
                         #100#1 = #1#1;
                    |FINSI;
                    |GRABA 020#100n;
               |FINSI;
          |FINSI;
     |SINO;
          |SI #0#2 = #10#2;      ||Traba
               |SI #0#0 = #10#1; ||Maqui
                    |HAZ FinJornada;
               |SINO;
                    |HAZ CambiaMaquina;
               |FINSI;

               nMaqui = #10#1;
               |ERROR10;
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE Marcajes;
     #10#2;
     ;
     "     ", "01.01.0000", 001, 0001;
     "     ", "31.12.9999", 999, 9999;
     ;
     NULL, Marcajes;
|FIN;

|PROCESO T3; |TIPO 3;
     |ABRE #10;
     nMaqui = 0;
     nSw = 2; |BUCLE Marcajes;
     |SI nMaqui = 0;
          |HAZ InicioJornada;
     |FINSI;
     |CIERRA #10;
|FPRC;

|PROCESO Traba;
     |SALVA_DATOS #1;
     |REPON_DATOS #referen@;
     |GRABA 020#referen@.n;
|FPRC;

|DEFBUCLE Traba;
     #1#1;
     3;
     0001, "S";
     9999, "S";
     ;
     NULL, Traba;
|FIN;

|PROCESO Traba66; |TIPO 66;
     |DDEF aAlfa;
     aAlfa = aAlfa + "fergam01";
     |CARGA_DEF aAlfa, referen@;
     |SI FSalida = 0;
          |HAZ CreaTempo; |NOME_DAT #referen@#Tempo#; |DELINDEX #referen@;
          |ABRE #referen@;
          |BUCLE Traba;
          #referen@#0 = #0#2;
          |LEE 000#referen@.];
          |CONSULTA_CLAVE #1#referen@;
          |SI FSalida = 0;
               #0#2 = #referen@#0; |PINTA #0#2;
          |SINO;
               |ERROR;
          |FINSI;
          |CIERRA #referen@; |HAZ BoraTempo;
          |DESCARGA_DEF #referen@;
     |FINSI;
|FPRC;

|PROCESO Traba0; |TIPO 0;
     |SI FSalida ! 0; |ACABA; |FINSI;
     |ABRE #1;
     #1#0 = #0#2;
     |LEE 000#1=;
     |SI FSalida = 0;
          #0#3 = #1#1; |PINTA #0#3;
          |SI #1#3 = "N";
               |MENAV "0000El trabajador no esta activo...";
               |ERROR;
          |FINSI;
     |SINO;
          |MENAV "0000No existe trabajador...";
          |ERROR;
     |FINSI;
     |CIERRA #1;
|FPRC;

|PROCESO Maqui7; |TIPO 7;
/*
     aQueBoton = "|X000";
     aID = #0#aQueBoton#;
     nBotonAceptar = aID;
     |ESTADO_CONTROL nBotonAceptar, "HIDE";
*/
     aQueBoton = "|X001";
     aID = #0#aQueBoton#;
     nBotonCancelar = aID;
     |ESTADO_CONTROL nBotonCancelar, "HIDE";
|FPRC;

|PROCESO Maqui0; |TIPO 0;
     |SI nSal = 1; |ACABA; |FINSI;

     |ABRE #2;
     #2#0 = #0#0;
     |LEE 000#2=;
     |SI FSalida = 0;
          #0#1 = #2#1; |PINTA #0#1;
          |HAZ CargaTraba;
          |HORA aAlfa; #0#4 = aAlfa; |PINTA #0#4;
     |SINO;
          |MENAV "0000No existe maquina...";
          |ERROR;
     |FINSI;
     |CIERRA #2;
|FPRC;

|PROCESO FormaHora;
     aLon = aHora % A0;
     |SI aLon = 5;
          aAlf1 = aHora % 102;
          aAlf2 = aHora % 402;
          nNum = aAlf1 $ 24; aAlf1 = nNum;
          nNum = aAlf2 $ 60; aAlf2 = nNum;
          aHora = (("00" + aAlf1)%-102)+":"+(("00" + aAlf2)%-102);
     |SINO;
          aAlf1 = aHora % 102;
          aAlf2 = aHora % 402;
          aAlf3 = aHora % 702;
          nNum = aAlf1 $ 24; aAlf1 = nNum;
          nNum = aAlf2 $ 60; aAlf2 = nNum;
          nNum = aAlf3 $ 60; aAlf3 = nNum;
          aHora = (("00" + aAlf1)%-102)+":"+(("00" + aAlf2)%-102)+":"+(("00" + aAlf3)%-102);
     |FINSI;
|FPRC;

|PROCESO Hora; |TIPO 0;
     |SI FSalida = 0;
          |PTEC 806;
     |FINSI;
     aHora = #0Campo;
     |HAZ FormaHora;
     #0Campo = aHora; |PINTA #0Campo;
|FPRC;

|PROCESO CargaTraba;
     |CIERRA #100; |DELINDEX #100; |ABRE #100;

     |ABRE #1;
     nSw = 1; |BUCLE Marcajes;
     |CIERRA #1;

     |ABRE #100;
     |REFRESCACONTROL nGrid;
|FPRC;

|PROCESO Evento;
     |SI FSalida = 0;
          |LEE 000#100=;
     |FINSI;
     |SI FSalida = 4;
          #0#2 = #100#0; |PINTA #0#2;
          #0#3 = #100#1; |PINTA #0#3;
          |HORA aAlfa; #0#4 = aAlfa; |PINTA #0#4;
     |FINSI;
|FPRC;

|PROCESO T80; |TIPO 80;
     FSalida = 2499;
|FPRC:

|PROCESO Tecla; |TIPO 6;
     |SI FSalida = 13;
          nSal = 1;
          |PTEC 807;
     |FINSI;
|FPRC;

|PROCESO BtnFin;
     nSal = 1;
     |PTEC 807;
|FPRC;

|PROGRAMA;
     |PINPA #0#0; nPanta = FSalida;
     |PINDA #0#0;
     |CONTROL_SIMPLE nPanta, "BOTON,SALIR", 0585, 0699, BtnFin;

     fHoy = ~;
     |HAZ CreaTempo;
     |NOME_DAT #100 Tempo; |DELINDEX #100; aTmp100 = Tempo; |ABRE #100;
     |PARA; |SI; |HACIENDO;
          #0#0 = 0; |PINTA #0#0;
          #0#1 = ""; |PINTA #0#1;
          #0#2 = 0; |PINTA #0#2;
          #0#3 = ""; |PINTA #0#3;
          |HORA aAlfa; #0#4 = aAlfa; |PINTA #0#4;
          nRango = 4 + 8 + 16 + 32;
          |SI nGrid ! 0; |DESTRUYECONTROL nGrid; |FINSI;
          |LINEAL_SIMPLE #1#100, nRango, 1203, 2098, NULL, NULL, Evento;
          nGrid = FSalida;
          |ENDATOS #1#0;
          |SI nSal = 1; |SAL; |FINSI;
     |SIGUE;
     |CIERRA #100; |DELINDEX #100; Tempo = aTmp100; |HAZ BoraTempo;
|FPRO;
