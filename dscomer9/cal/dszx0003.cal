|FICHEROS;
     dszx0003 #0;
     dsm00003 #3,MANTE;       || Por problema con relaciones
     dszx0009 #9;
     agifa010 #10;
     agifa019 #19;
     agifa024 #24;
     agifa071 #71;
     agifa104 #104,MANTE;
     agifa073 #73,MANTE;
     dsm00113 #113;
     agifa142 #142;
|FIN;

|VARIABLES;
     &enTiva = 0;
     &efFiva = @;
     &eaDiva = "";
     &enIva = 0;
     &enRec = 0;
     &num_alb = 0;
     &ser_alb = "";
     &abono = "";
     &aDivisa = "";
     &aMoneda = "";
     &eaSerNuevo = "";
     &enNumNuevo = 0;
     &enForma = 0;
     &eaCliente = "";
     &efFecha = @;
     &enImpCliPed = 0;
     &eaTag = "";
     &eaSerPed = "";
     &enNumPed = 0;
     &Tempo = "";
     &enSwMenu = 0;
     &ser_ped = "";
     &num_ped = 0;
     aTmp0 = "";
     aTmp9 = "";
     aAlfa = "";
     aAccion = "";
     aSerie = "";
     aCodigo = "";
     aCliente = "";
     aDirEnt = "";
     nHay = 0;
     aMensaje = "";
     aSwServ = "";
     nImpo = 0;
     nCalc = 0;
     fmes = "";
     mes = 0;
     aTempo = "";
     nHandle = 0;
|FIN;

|PROCESO GraTmp;
     |DFICE aAlfa;
     aAlfa = aAlfa + aTempo;
     |XABRE "W", aAlfa, nHandle;
     |SI FSalida = 0;
          aAlfa = eaSerNuevo + (("000000" + enNumNuevo) % -106);
          |XGRABA nHandle, aAlfa;
          |XCIERRA nHandle;
     |FINSI;
|FPRC;

|PROCESO ActuCli;
     #19#0 = #73#2;
     |LEE 020#19=;

     #24#0 = #104#4;
     |LEE 121#24=;
     |SI FSalida = 0;
          |SI #19#194 = 2;
               nImpo = ((((#73#44 * #73#6) < #73#8) < #73#29) < #104#5) < #104#17;
          |SINO;
               nImpo = ((((#73#5 * #73#6) < #73#8) < #73#29) < #104#5) < #104#17;
          |FINSI;
          |SI #142#115 = "S";
               nImpo = nImpo < #104#6;
          |FINSI;
          nImpo = nImpo * enForma;
          fmes = #104#1 % A402;
          mes = fmes - 1;
          mes = mes * 3;
          mes = mes + 114;
          #24mes = #24mes   + nImpo;     || VENTA.
          #24#150 = #24#150 + nImpo;
          |GRABA 020#24a;

          eaCliente = #agifa104#4;
          efFecha = #agifa104#1;
          enImpCliPed = nImpo;
          |HAZ AcumulaCli;
     |FINSI;
|FPRC;

|PROCESO CalcuConIva;
     #113#0 = #104#4;
     #113#1 = #73#14;
     |LEE 000#113=;
     |SI FSalida = 0;
          nCalc = #73#11 % #113#3;
          |SI #24#47 = "S";
               nCalc = nCalc + (#73#11 % #113#4);
          |FINSI;
     |SINO;
          enTiva = #73#14;
          efFiva = #104#1;
          |HAZ SacaIVA;
          nCalc = #73#11 % enIva;
          |SI #24#47 = "S";
               nCalc = nCalc + (#73#11 % enRec);
          |FINSI;
     |FINSI;
     nImpo = nImpo + (#73#11 + nCalc);
|FPRC;

|PROCESO BorraLinPed;
     |BORRA 020#73a;
     |HAZ AcumulaPV;
|FPRC;

|PROCESO MiraServido;
     |SI #73#43 ! 0; |O #73#49 ! 0;
          aSwServ = "S";
     |FINSI;
|FPRC;

|PROCESO MiraParBlan;
     #19#0 = #73#2;
     |LEE 000#19=;
     |SI #19#180 = "S";
          aAlfa = #73#45; |QBF aAlfa;
          |SI aAlfa = "";
               aMensaje = "0000Linea: " + #73#1 + " con partida en blanco.";
               |MENAV aMensaje;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO AdjuPedido;
     |SI #142#242 = "N";
          aMensaje = "";
          |ABRE #19;
          |BUCLELIN 2 MiraParBlan #0;
          |CIERRA #19;
          |SI aMensaje ! ""; |MENAV "0000No se Adjudica el Pedido..."; |ACABA; |FINSI;
     |FINSI;

     ser_ped = #104#25;
     num_ped = #104#0;
     |SUB_EJECUTA_NP "dsz00067";
|FPRC;

|PROCESO Tecla; |TIPO 11;
     |SI FSalida = 10;
          |ENTLINEAL #1#9, 1, 4, 22, 0, Min9, Max9;
          |ERROR;
     |FINSI;
     |SI FSalida = 11;
          |PUSHV 0101 2380;
          |CLS;
          aAlfa = "dszx0004;V" + #0#0 + (("000000"+#0#1)%-106);
          |SUB_EJECUTA_NP aAlfa;
          |POPV;
          |ERROR;
     |FINSI;
     |SI FSalida = 14;
          |ABRE #10;
          #10#52 = #0#0;
          #10#0 = #0#1;
          |LEE 020#10=;
          |SI FSalida = 0;
               ser_alb = #10#52;
               num_alb = #10#0;
               abono = #10#43;
               |SUB_EJECUTA_NP "agifa014";
          |FINSI;
          |CIERRA #10;
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO Tipo7; |TIPO 7;
     |ENTLINEAL #1#9, 1, 7, 22, 0, Min9, Max9;
|FPRC;

|PROCESO Min0;
     #0#0 = "     ";
     #0#1 = 000000;
|FPRC;

|PROCESO Max0;
     #0#0 =  "zzzzz";
     #0#1 = 999999;
|FPRC;

|PROCESO Min9;
     #9#0 = #0#0;
     #9#1 = #0#1;
     #9#2 = 001;
|FPRC;

|PROCESO Max9;
     #9#0 = #0#0;
     #9#1 = #0#1;
     #9#2 = 999;
|FPRC;

|PROCESO Servicios;
     #10#52 = #71#29;
     #10#0 = #71#0;
     |LEE 020#10=;
     |SI FSalida = 0;
          aDivisa = #10#68;
          aMoneda = #10#69;
          |HAZ Divisas010;

          nHay = nHay + 1;
          #0#0 = #10#52;
          #0#1 = #10#0;
          #0#2 = #10#1;
          #0#3 = #10#3;
          #0#4 = #10#4;
          #0#5 = #104#25;
          #0#6 = #104#0;
          #0#7 = #10#78;
          |GRABA 000#0n;

          #9#0 = #71#29;
          #9#1 = #71#0;
          #9#2 = #71#1;
          #9#3 = #71#2;
          #9#4 = #71#4;
          #9#5 = #71#5;
          |GRABA 020#9n;
     |FINSI;
|FPRC;

|DEFBUCLE Servicios;
     #71#4;
     ;
     #104#25, #104#0, 001;
     #104#25, #104#0, 999;
     ;
     NULL, Servicios;
|FIN;

|PROCESO VerServicios;
     |HAZ CreaTempo; |NOME_DAT #0 Tempo; |DELINDEX #0; aTmp0 = Tempo;
     |HAZ CreaTempo; |NOME_DAT #9 Tempo; |DELINDEX #9; aTmp9 = Tempo;
     |ABRE #0;
     |ABRE #9;
     |ABRE #10;
     |BUCLE Servicios;
     |CIERRA #10;
     |CIERRA #9;
     |CIERRA #0;
     |SI nHay = 0;
          |MENAV "0000No existen albaranes servidos de este pedido...";
     |SINO;
          |CLS;
          |ENTLINEAL #1#9, 1, 7, 22, 1, Min9, Max9;
          |ENTLINEAL #1#0, 1, 4, 13, 0, Min0, Max0;
     |FINSI;
     |DELINDEX #0; Tempo = aTmp0; |HAZ BoraTempo;
     |DELINDEX #9; Tempo = aTmp9; |HAZ BoraTempo;
|FPRC;

|PROGRAMA;
     aAccion = PARAMETRO % 101;
     aSerie = PARAMETRO % 205;
     aCodigo = PARAMETRO % 706;
     aCliente = PARAMETRO % 1306;
     aDirEnt = PARAMETRO % 1904;
     aTempo = PARAMETRO % 2308;

     |ABRE #142; |LEE 000#142p; |CIERRA #142;

     |CLS;
     |ABRE #104;
     #104#25 = aSerie;
     #104#0 = aCodigo;
     |SI aAccion = "I";
          |LEE 020#104=;
          |SI FSalida = 0;
               ser_ped = #104#25;
               num_ped = #104#0;
               |SUB_EJECUTA_NP "agifa105";
          |FINSI;
     |FINSI;
     |SI aAccion = "V";
          |LEE 020#104=;
          |SI FSalida = 0;
               |PINPA #0#104;
               |PINDA #0#104;
               |ENDATOS #4#104;
          |FINSI;
     |FINSI;
     |SI aAccion = "M";
          |LEE 121#104=;
          |SI FSalida = 0;
               |PINPA #0#104;
               |PINDA #0#104;
               |ENDATOS #2#104;
               |SI FSalida = 0;
                    |GRABA 020#104a;
               |FINSI;
          |FINSI;
     |FINSI;
     |SI aAccion = "N";
          |SI #142#185 = "S";
               #104#25 = #142#186;
          |FINSI;
          |PINPA #0#104;
          |PINDA #0#104;
          |ENCAMPO #25#104;
          |SI FSalida = 0;
               FEntrada = 101;
               |HAZ ContaPV7;
               FEntrada = 0;
               |ENCAMPO #0#104;
               |SI FSalida = 0;
                    |LEE 101#104=;
                    |SI FSalida = 0;
                         |PINDA #0#104;
                         |MENAV "0000Registro existente...";
                         |PAUSA;
                    |SINO;
                         #104#4 = aCliente; |PINTA #104#4;
                         #104#57 = aDirEnt; |PINTA #104#57;
                         |GRABA 020#104b;
                         |ENDATOS #1#104;
                         |SI FSalida = 0;
                              eaSerNuevo = #104#25;
                              enNumNuevo = #104#0;
                              |GRABA 020#104a;
                              |HAZ GraTmp;
                         |SINO;
                              |BORRA 020#104c;
                         |FINSI;
                    |FINSI;
               |FINSI;
          |FINSI;
     |FINSI;
     |SI aAccion = "S";  || Ver servicios
          |LEE 020#104=;
          |SI FSalida = 0;
               |HAZ VerServicios;
          |FINSI;
     |FINSI;
     |SI aAccion = "A";  || Adjudicar
          |LEE 020#104=;
          |SI FSalida = 0;
               |HAZ AdjuPedido;
          |FINSI;
     |FINSI;
     |SI aAccion = "B";  || Anular
          |LEE 121#104=;
          |SI FSalida = 0;
               aSwServ = "N";
               |ABRE #19;
               |BUCLELIN 2 MiraServido #104;
               |CIERRA #19;
               |SI aSwServ = "N";
                    enForma = -1;
                    |BUCLELIN 0 BorraLinPed #104;
                    |BORRA 020#104a;

                    |ABRE #24;
                    |ABRE #19;
                    |BUCLELIN 2 ActuCli #104;
                    |CIERRA #24;
                    |CIERRA #19;

                    |ABRE #113;
                    nImpo = 0; |BUCLELIN 2 CalcuConIva #104;
                    |CIERRA #113;
                    eaTag = "PE";  |HAZ Riesgos;
               |SINO;
                    eaSerPed = #0#0;
                    enNumPed = #0#1;
                    |SUB_EJECUTA_NP "dsz00006";
               |FINSI;
          |FINSI;
     |FINSI;
     |CIERRA #104;
|FPRO;
