|FICHEROS;
     dsz00111 #0;
     agifa010 #10;
     agifa059 #59;
     agifa071 #71;
     agifa134 #134;
     refe010@;
     refe059@;
     refe071@;
     refe134@;
|FIN;

|VARIABLES;
     &aDivisa = "";
     &aMoneda = "";
     &Tempo = "";
     &enB1 = 0;
     &enB2 = 0;
     &enB3 = 0;
     &enB4 = 0;
     &enB5 = 0;
     aPath = "";
     aAlfa = "";
     aTmp010 = "";
     aTmp059 = "";
     aTmp071 = "";
     aTmp134 = "";
     nPrime = 0;
     nImpre = -1;
     nInfor = -1;
     aSerAlb = "";
     nNumAlb = 0;
     aSerFac = "";
     nNumFac = 0;
|FIN;

|PROCESO Imprime;
     |SI nPrime = 0;
          nPrime = 1;
          |IMPRE 0;
          nImpre = FSalida;
          |SI FSalida = 0;
               |INFOR "dsz00111";
               nInfor = FSalida;
          |FINSI;
     |FINSI;
     |SI nInfor = 0; |PRINT; |FINSI;
|FPRC;

|PROCESO agifa071;
     |SALVA_DATOS #refe071@;
     |REPON_DATOS #71;
     |GRABA 000#71n;
|FPRC;

|DEFBUCLE agifa071;
     #refe071@#1003;
     ;
     aSerAlb, nNumAlb, 001;
     aSerAlb, nNumAlb, 999;
     ;
     NULL, agifa071;
|FIN;

|PROCESO agifa010;
     |BUCLE agifa071;
     |SALVA_DATOS #refe010@;
     |REPON_DATOS #10;
     |GRABA 000#10n;
     |SI FSalida ! 0;
          aAlfa = "0000Albaran incluido en mas de una factura:" + #10#52 + "/" + (("000000" + #10#0)% -106);
          |MENAV aAlfa;
     |FINSI;
|FPRC;

|DEFBUCLE agifa010;
     #refe010@#1002;
     ;
     aSerAlb, nNumAlb;
     aSerAlb, nNumAlb;
     ;
     NULL, agifa010;
|FIN;

|PROCESO agifa059;
     aSerAlb = #refe059@#14;
     nNumAlb = #refe059@#2;
     |BUCLE agifa010;
     |SALVA_DATOS #refe059@;
     |REPON_DATOS #59;
     |GRABA 020#59n;
|FPRC;

|DEFBUCLE agifa059;
     #refe059@#1003;
     ;
     aSerFac, nNumFac, 001;
     aSerFac, nNumFac, 999;
     ;
     NULL, agifa059;
|FIN;

|PROCESO agifa134;
     aSerFac = #refe134@#49;
     nNumFac = #refe134@#0;
     |BUCLE agifa059;
     |SALVA_DATOS #refe134@;
     |REPON_DATOS #134;
     |GRABA 020#134b;

     aDivisa = #134#58;
     aMoneda = #134#60;
     |HAZ Divisas134;

     |HAZ CalCabAgifa134;
     |GRABA 020#134a;
     |LIBERA #134;

     |SI #refe134@#92 ! #134#92;
               |O #refe134@#95 ! #134#95;
               |O #refe134@#98 ! #134#98;
               |O #refe134@#103 ! #134#103;
               |O #refe134@#106 ! #134#106;
          enB1 = #refe134@#92;
          enB2 = #refe134@#95;
          enB3 = #refe134@#98;
          enB4 = #refe134@#103;
          enB5 = #refe134@#106;
          |HAZ Imprime;
     |FINSI;
|FPRC;

|DEFBUCLE agifa134;
     #refe134@#3004;
     ;
     #0#6, #0#4, #0#0, #0#2;
     #0#7, #0#5, #0#1, #0#3;
     ;
     NULL, agifa134;
|FIN;

|PROCESO Tipo3; |TIPO 3;
     |DDEF aPath;
     aAlfa = aPath + "agifa010"; |CARGA_DEF aAlfa, refe010@;
     aAlfa = aPath + "agifa059"; |CARGA_DEF aAlfa, refe059@;
     aAlfa = aPath + "agifa071"; |CARGA_DEF aAlfa, refe071@;
     aAlfa = aPath + "agifa134"; |CARGA_DEF aAlfa, refe134@;

     |HAZ CreaTempo; aTmp010 = Tempo; |NOME_DAT #10Tempo; |DELINDEX #10;
     |HAZ CreaTempo; aTmp059 = Tempo; |NOME_DAT #59Tempo; |DELINDEX #59;
     |HAZ CreaTempo; aTmp071 = Tempo; |NOME_DAT #71Tempo; |DELINDEX #71;
     |HAZ CreaTempo; aTmp134 = Tempo; |NOME_DAT #134Tempo; |DELINDEX #134;

     |ABRET #10;
     |ABRET #59;
     |ABRET #71;
     |ABRET #134;
     |BUCLE agifa134;
     |CIERRAT #134;
     |CIERRAT #71;
     |CIERRAT #59;
     |CIERRAT #10;

     Tempo = aTmp010; |HAZ BoraTempo; |DELINDEX #10;
     Tempo = aTmp059; |HAZ BoraTempo; |DELINDEX #59;
     Tempo = aTmp071; |HAZ BoraTempo; |DELINDEX #71;
     Tempo = aTmp134; |HAZ BoraTempo; |DELINDEX #134;
     |DESCARGA_DEF #refe134@;
     |DESCARGA_DEF #refe071@;
     |DESCARGA_DEF #refe059@;
     |DESCARGA_DEF #refe010@;

     |SI nPrime = 0;
          |MENAV "0000No se ha encontrado errores...";
     |SINO;
          |SI nInfor = 0; |PIEINF; |FININF; |FINSI;
          |SI nImpre = 0; |FINIMP; |FINSI;
     |FINSI;
|FPRC;
