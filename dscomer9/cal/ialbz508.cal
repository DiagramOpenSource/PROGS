     RECIBOS

|FICHEROS;
     ialbz008 #0;
     dscam045@;
     dscam003@;
     dscam004@;
     referec@;
     referel@;
|FIN;

|VARIABLES;
     &eaEstadoBtn = "";
     &eaTmp = "";
     &eaPestana = "";
     &eaSwCarga5 = "";
     &eaBtnAcota5 = "";
     &eaSer5 = "";
     &eaNum5 = "";
     &eaLin5 = "";

     aProg = "";
     aTmp = "";
     aAlfa = "";
     nRango = 0;
     nPos1 = 0;
     nPos2 = 0;
     nPestana = 0;
     aPath = "";
     nPos = 0;
     nDDir = 0;
     nHDir = 0;
     aDFac = "";
     aHFac = "";
     aEmpre = "";
     aPathDat = "";
     aPathDef = "";
     aNumEmp = "";
     nNumEmp = 0;
     nOk = 0;
     aExisteCarte = "";

     nBtnPdtCob5 = 0;
     nBtnFactu5 = 0;
     nBtnTodos5 = 0;
     nBtnImpre5 = 0;
     nBtnSalir5 = 0;
     nGridC = -1;
     nGridL = -1;
     nPanta = 0;
     nEvtFoco = 0;
     nBtnVerRec5 = 0;
|FIN;

|PROCESO dscam045;
     |SI #dscam045@#2 = "V";
          aEmpre = ("00000" + #dscam045@#6) % -105;
          aPathDat = aPath + aEmpre + "/";
          |ERROR10;
     |FINSI;
|FPRC;

|DEFBUCLE dscam045;
     #dscam045@#1002;
     ;
     nNumEmp, 001;
     nNumEmp, 999;
     ;
     NULL, dscam045;
|FIN;

|PROCESO CargaCartera;
     |DBASS aPath;
     aPath = aPath + "dscarter/";
     aPathDef = aPath + "def/";
     aAlfa = aPathDef + "dscam045";
     |CARGA_DEF aAlfa, dscam045@;
     |SI FSalida = 0;
          |DFICE aAlfa;
          aNumEmp = aAlfa % -205;
          nNumEmp = aNumEmp;
          aPath = aPath + "fich/";
          |PATH_DAT #dscam045@#aPath#;
          |BUCLE dscam045;
          |DESCARGA_DEF #dscam045@;
          aExisteCarte = "S";

          aAlfa = aPathDef + "dscam003";
          |CARGA_DEF aAlfa, dscam003@;
          |SI FSalida ! 0; |MENAV "0000Error al cargar 'dscam003'"; |FINSI;
          aAlfa = aPathDef + "dscam004";
          |CARGA_DEF aAlfa, dscam004@;
          |SI FSalida ! 0; |MENAV "0000Error al cargar 'dscam004'"; |FINSI;
          |SI aPathDat = "";
               |MENAV "0000Error, no encuentro empresa cartera";
          |SINO;
               |PATH_DAT #dscam003@#aPathDat#;
               |PATH_DAT #dscam004@#aPathDat#;
          |FINSI;
     |SINO;
          aExisteCarte = "N";
     |FINSI;
|FPRC;

|PROCESO DescargaCartera;
     |SI aExisteCarte = "S";
         |DESCARGA_DEF #dscam004@;
         |DESCARGA_DEF #dscam003@;
     |FINSI;
|FPRC;

//----------------//
|PROCESO Existe5;
     |ABRE #dscam003@;
     #dscam003@#40 = #referec@#40;
     |LEE 000#dscam003@.=;
     aAlfa = FSalida;
     |CIERRA #dscam003@;
     FSalida = aAlfa;
|FPRC;

|PROCESO BtnVerRec5;
     |HAZ Existe5;
     |SI FSalida ! 0; |ACABA; |FINSI;

     aProg = "*dszx0006;"+ "V" + aEmpre + (("000000000"+#referec@#40)%-109);
     |SUB_EJECUTA aProg;
|FPRC;

//----------------//
|PROCESO CargaDatos5;
     |SI eaSwCarga5 ! "S"; |Y aExisteCarte = "S";
          |HAZ DestruyePan5;
          |HAZ Descarga5;
          |SI #0#0 = "000000";
               |REFERENCIA_DEF referec@, #dscam003@;
          |FINSI;
          |REFERENCIA_DEF referel@, #dscam004@;
          |HAZ PintaPanta5;
     |FINSI;
|FPRC;

|PROCESO CogePosicion5;
     eaSer5 = "";
     eaNum5 = ("000000000" + #referec@#40) % -109;
     eaLin5 = ("00" + #referel@#3) % -102;
|FPRC;

|PROCESO Posiciona5;
     #referel@#17 = eaNum5;
     #referel@#3 = eaLin5;
     |LEE 000#referel@.];

     #referec@#40 = eaNum5;
     |LEE 000#referec@.=;
     |REFRESCACONTROL nGridC;

     |SI eaPestana = "5"; |FOCOTECLADO nGridC; |FINSI;
|FPRC;

|PROCESO EstadoBtn5;
     |SI #0#0 = "000000";
          |ESTADO_CONTROL nBtnPdtCob5, "DISABLE";
          |ESTADO_CONTROL nBtnFactu5, "DISABLE";
          |ESTADO_CONTROL nBtnTodos5, "DISABLE";
     |SINO;
          |SI eaBtnAcota5 = "1";
               |ESTADO_CONTROL nBtnPdtCob5, "DISABLE";
               |ESTADO_CONTROL nBtnFactu5, "ENABLE";
               |ESTADO_CONTROL nBtnTodos5, "ENABLE";
          |FINSI;
          |SI eaBtnAcota5 = "2";
               |ESTADO_CONTROL nBtnPdtCob5, "ENABLE";
               |ESTADO_CONTROL nBtnFactu5, "DISABLE";
               |ESTADO_CONTROL nBtnTodos5, "ENABLE";
          |FINSI;
          |SI eaBtnAcota5 = "3";
               |ESTADO_CONTROL nBtnPdtCob5, "ENABLE";
               |ESTADO_CONTROL nBtnFactu5, "ENABLE";
               |ESTADO_CONTROL nBtnTodos5, "DISABLE";
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO BtnPdtCob5;
     eaBtnAcota5 = "1";
     eaSwCarga5 = "N";
     |HAZ EventoFoco5;
|FPRC;

|PROCESO BtnCobrado5;
     eaBtnAcota5 = "2";
     eaSwCarga5 = "N";
     |HAZ EventoFoco5;
|FPRC;

|PROCESO BtnTodos5;
     eaBtnAcota5 = "3";
     eaSwCarga5 = "N";
     |HAZ EventoFoco5;
|FPRC;

|PROCESO BtnImpre5;
     |HAZ Existe5;
     |SI FSalida ! 0; |ACABA; |FINSI;

     aProg = "*dszx0006;"+ "I" + aEmpre + (("000000000"+#referec@#40)%-109);
     |SUB_EJECUTA aProg;
|FPRC;

|PROCESO EventoC5;
     |SI FSalida = 1; |O FSalida = 2;
          |LEE 000#referec@.=;
          |REFRESCACONTROL nGridL;
     |FINSI;
|FPRC;

|PROCESO EventoL5;
     |SI FSalida = 1; |O FSalida = 2;
          |LEE 000#referel@.=;
     |FINSI;
|FPRC;

|PROCESO EventoFoco5;
     |HAZ CargaDatos5;
     eaPestana = "5";
|FPRC;

|PROCESO CargaTmp5;
     nOk = 0;
     |SI eaBtnAcota5 = "1";
          |SI #dscam003@#14 ! "L"; |O #dscam003@#65 ! 0; nOk = 1; |FINSI;
          |SI #dscam003@#14 = "R"; |O #dscam003@#14 = "A";
                    |O #dscam003@#14 = "D";
               nOk = 0;
          |FINSI;
     |FINSI;
     |SI eaBtnAcota5 = "2";
          |SI #dscam003@#14 = "L"; |Y #dscam003@#65 = 0; nOk = 1; |FINSI;
     |FINSI;
     |SI nOk = 1; |O eaBtnAcota5 = "3";
          |SALVA_DATOS #dscam003@;
          |REPON_DATOS #referec@;
          |GRABA 020#referec@.n;
     |FINSI;
|FPRC;

|DEFBUCLE CargaTmp5;
     #dscam003@#11003;
     91;
     #0#0, "01.01.0000", "     ", nDDir;
     #0#0, "31.12.9999", "zzzzz", nHDir;
     ;
     NULL, CargaTmp5;
|FIN;

|PROCESO NomeDat5;
     |SI #0#0 = "000000";
          aTmp = "dscam003";
          |NOME_DAT #referec@#aTmp#;
     |SINO;
          nPos = ((8 * 4 + 1) * 100 + 8);
          aTmp = eaTmp % nPos;
          |NOME_DAT #referec@#aTmp#;
          |DELINDEX #referec@;
     |FINSI;
|FPRC;

|PROCESO MinL5;
     #referel@#17 = #referec@#40
     #referel@#3 = 1;
|FPRC;

|PROCESO MaxL5;
     #referel@#17 = #referec@#40
     #referel@#3 = 99;
|FPRC;

|PROCESO MinC5;
     #referec@#40 = 0;
|FPRC;

|PROCESO MaxC5;
     #referec@#40 = 999999999;
|FPRC;

|PROCESO EstadoIniBtnPan5;
     |ESTADO_CONTROL nBtnPdtCob5, eaEstadoBtn;
     |ESTADO_CONTROL nBtnFactu5, eaEstadoBtn;
     |ESTADO_CONTROL nBtnTodos5, eaEstadoBtn;
     |ESTADO_CONTROL nBtnImpre5, eaEstadoBtn;
     |ESTADO_CONTROL nBtnSalir5, eaEstadoBtn;
|FPRC;

|PROCESO DestruyePan5;
     |DESTRUYECONTROL nGridL;
     |DESTRUYECONTROL nGridC;

     |FIN_CONTROL_WINDOWS nEvtFoco;

     |FIN_CONTROL_WINDOWS nBtnPdtCob5;
     |FIN_CONTROL_WINDOWS nBtnFactu5;
     |FIN_CONTROL_WINDOWS nBtnTodos5;
     |FIN_CONTROL_WINDOWS nBtnImpre5;
     |FIN_CONTROL_WINDOWS nBtnSalir5;

     |FIN_CONTROL_WINDOWS nBtnVerRec5;
|FPRC;

|PROCESO Descarga5;
     |CIERRAT #referec@;
     |CIERRAT #referel@;
     |DESCARGA_DEF #referel@;
     |DESCARGA_DEF #referec@;
|FPRC;

|PROCESO PintaPanta5;
     |SI aExisteCarte ! "S"; |ACABA; |FINSI;

     aAlfa = aPathDef + "dscam003";
     |CARGA_DEF aAlfa, referec@;
     aAlfa = aPathDef + "dscam004";
     |CARGA_DEF aAlfa, referel@;

     |PATH_DAT #referec@#aPathDat#;
     |PATH_DAT #referel@#aPathDat#;
     |HAZ NomeDat5;

     #referec@#40 = 0;    || Para que no salga nada si esta vacio

     |ABRET #referec@;;
     |ABRET #referel@;
     |SI #0#0 ! "000000";
          |SI #0#1 = 0;
               nDDir = 0; nHDir = 9999;
          |SINO;
               nDDir = #0#1; nHDir = #0#1;
          |FINSI;
          |BUCLE CargaTmp5;
          |LEE 000#referec@.p;
     |FINSI;
     eaSwCarga5 = "S";

     |PINPA #5#0;  nPanta = FSalida;

     nRango = 4 + 8 + 16 + 32;
     nPos1 = 2402;
     nPos2 = 3398;
     |SI nGridL ! -1; |DESTRUYECONTROL nGridL; |FINSI;
     |LINEAL_SIMPLE #1#referel@, nRango, nPos1, nPos2, MinL5, MaxL5, EventoL5;
     nGridL = FSalida;

     nRango = 4 + 8 + 16 + 32;
     nPos1 = 0602;
     nPos2 = 2298;
     |SI nGridC ! -1; |DESTRUYECONTROL nGridC; |FINSI;
     |LINEAL_SIMPLE #1#referec@, nRango, nPos1, nPos2, MinC5, MaxC5, EventoC5;
     nGridC = FSalida;

     |CONTROL_SIMPLE nPanta, "EVENTOPAN", 0000, 0000, EventoFoco5;
     nEvtFoco = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,Ptes. Cobro", 0501, 0513, BtnPdtCob5;
     nBtnPdtCob5 = FSalida;
     |CONTROL_SIMPLE nPanta, "BOTON,Cobrados", 0514, 0526, BtnCobrado5;
     nBtnFactu5 = FSalida;
     |CONTROL_SIMPLE nPanta, "BOTON,Todos", 0527, 0539, BtnTodos5;
     nBtnTodos5 = FSalida;
     |CONTROL_SIMPLE nPanta, "BOTON,{{205}}Imprimir", 0579, 0591, BtnImpre5;
     nBtnImpre5 = FSalida;

||нннн Lo oculto, no esta hecha la impresion en el dszx0006 !!!!!
     |ESTADO_CONTROL nBtnImpre5, "HIDE";

     |CONTROL_SIMPLE nPanta, "BOTON,Ver Recibo", 2301, 2313, BtnVerRec5;
     nBtnVerRec5 = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,{{207}} Salir", 3488, 3498, BtnSalir;
     nBtnSalir5 = FSalida;

     |HAZ EstadoBtn5;
|FPRC;
