|FICHEROS;
     diaw0003 #0;
     diam0001 #1,MANTE;
     diam0003 #3;
     diaw0004 #4,MANTE;   ||Tmp
     diam0005 #5;
     agifa025 #25;
     agifa104 #104;
     dstelm02@ #20;
     dstelm03@ #30;
     dsparm01@ #100;
     dsparm05@ #500;
|FIN;

|VARIABLES;
     &eaR1 = ""; &eaR2 = ""; &eaR3 = ""; &eaR4 = ""; &eaR5 = "";
     &eaR6 = ""; &eaR7 = ""; &eaR8 = ""; &eaR9 = ""; &eaR0 = "";
     &eaM1 = ""; &eaM2 = ""; &eaM3 = ""; &eaM4 = ""; &eaM5 = "";
     &eaM6 = ""; &eaM7 = ""; &eaM8 = ""; &eaM9 = ""; &eaM0 = "";
     &enR1 = 0; &enR2 = 0; &enR3 = 0; &enR4 = 0; &enR5 = 0;
     &enR6 = 0; &enR7 = 0; &enR8 = 0; &enR9 = 0; &enR0 = 0;
     &enM1 = 0; &enM2 = 0; &enM3 = 0; &enM4 = 0; &enM5 = 0;
     &enM6 = 0; &enM7 = 0; &enM8 = 0; &enM9 = 0; &enM0 = 0;
     &eaCli = "";
     &eaTiquet = "";
     aFicMail = "";
     aPath     = "";
     aDefh     = "";
     aBase     = "";
     aDef      = "";
 {-1}aMenu     = "";
     aMenu1    = "2400";
     aMenu2    = "1";
     aMenu3    = " ";
     aMenu4    = "RAC";
     aMenu5    = " Repasar ";
     aMenu6    = " Aceptar ";
     aMenu7    = " Cancelar ";
     aAlfa     = "";
     nCerrado  = 0;
     i         = 0;
     nHay      = 0;
     nCuantos  = 0;
     aIP       = "";
     aServidor = "";
     aFrom     = "";
     aTo       = "";
     aSubject  = "";
     aMensaje  = "";
     aDjunto   = "Sin asunto";
     aNume     = "";
     aAlfa1    = "";
     nSwNoCierres = 0;
|FIN;

|PROCESO diam0005_3;
     #20#5 = #104#4;
     #20#0 = #5#9;
     |LEE 000#20=;
     |SI FSalida = 0;|Y #5#9 ! 0;
          eaTiquet = ("00000" + #20#0) % -105;
          eaCli = #20#5;
          eaM1 = #20#11; |QBF eaM1; |SI eaM1 = ""; enM1 = 0; |SINO enM1 = 1; |FINSI;
          eaM2 = #20#12; |QBF eaM2; |SI eaM2 = ""; enM2 = 0; |SINO enM2 = 1; |FINSI;
          eaM3 = #20#13; |QBF eaM3; |SI eaM3 = ""; enM3 = 0; |SINO enM3 = 1; |FINSI;
          eaM4 = #20#19; |QBF eaM4; |SI eaM4 = ""; enM4 = 0; |SINO enM4 = 1; |FINSI;
          eaM5 = #20#20; |QBF eaM5; |SI eaM5 = ""; enM5 = 0; |SINO enM5 = 1; |FINSI;
          eaM6 = #20#21; |QBF eaM6; |SI eaM6 = ""; enM6 = 0; |SINO enM6 = 1; |FINSI;
          eaM7 = #20#22; |QBF eaM7; |SI eaM7 = ""; enM7 = 0; |SINO enM7 = 1; |FINSI;
          eaM8 = #20#23; |QBF eaM8; |SI eaM8 = ""; enM8 = 0; |SINO enM8 = 1; |FINSI;
          eaM9 = #20#24; |QBF eaM9; |SI eaM9 = ""; enM9 = 0; |SINO enM9 = 1; |FINSI;
          eaM0 = #20#25; |QBF eaM0; |SI eaM0 = ""; enM0 = 0; |SINO enM0 = 1; |FINSI;

          eaR1 = #20#14; |QBF eaR1; |SI eaR1 = ""; enR1 = 0; |SINO enR1 = 1; |FINSI;
          eaR2 = #20#15; |QBF eaR2; |SI eaR2 = ""; enR2 = 0; |SINO enR2 = 1; |FINSI;
          eaR3 = #20#16; |QBF eaR3; |SI eaR3 = ""; enR3 = 0; |SINO enR3 = 1; |FINSI;
          eaR4 = #20#26; |QBF eaR4; |SI eaR4 = ""; enR4 = 0; |SINO enR4 = 1; |FINSI;
          eaR5 = #20#27; |QBF eaR5; |SI eaR5 = ""; enR5 = 0; |SINO enR5 = 1; |FINSI;
          eaR6 = #20#28; |QBF eaR6; |SI eaR6 = ""; enR6 = 0; |SINO enR6 = 1; |FINSI;
          eaR7 = #20#29; |QBF eaR7; |SI eaR7 = ""; enR7 = 0; |SINO enR7 = 1; |FINSI;
          eaR8 = #20#30; |QBF eaR8; |SI eaR8 = ""; enR8 = 0; |SINO enR8 = 1; |FINSI;
          eaR9 = #20#31; |QBF eaR9; |SI eaR9 = ""; enR9 = 0; |SINO enR9 = 1; |FINSI;
          eaR0 = #20#32; |QBF eaR0; |SI eaR0 = ""; enR0 = 0; |SINO enR0 = 1; |FINSI;
          |PRINT;
     |FINSI;
|FPRC;

|DEFBUCLE diam0005_3;
     #5#1;
     ;
     #4#0, #4#1, #4#2, 0000;
     #4#0, #4#1, #4#2, 9999;
     ;
     NULL, diam0005_3;
|FIN;

|PROCESO FicMail;
     Impresora = aFicMail;
     |IMPRE -77;
     |SI FSalida = 0;
          |INFOR "diaw0003";
          |SI FSalida = 0;
               |BUCLE diam0005_3;
               |PIEINF;
               |FININF;
          |FINSI;
          |FINIMP;
     |FINSI;
|FPRC

|PROCESO diam0003_1;
     |SI #3#6 ! "C";
          nCerrado = 0;
          |ERROR10;
     |FINSI;
|FPRC;

|DEFBUCLE diam0003_1;
     #3#1;
     ;
     #4#0, #4#1, 0000;
     #4#0, #4#1, 9999;
     ;
     NULL, diam0003_1;
|FIN;

|PROCESO CorreoRepre;
     |SELECT #3#500;
     aAlfa = Usuario % 810;
     |QBF aAlfa;
     #500#2 = aAlfa;
     |LEE 000#500=;
     |SI FSalida ! 0;
          |MENAV "0000No tiene ficha de tecnico segun su usuario DS.";
          |ACABA;
     |SINO;
          aFrom = #500#3;     |QBF aFrom;
          |SI aFrom = "";
               |MENAV "0000No tiene correo informado en su ficha de tecnico.";
               |ACABA;
          |FINSI;
     |FINSI;

     aTo = "";
     |ABRE #25;
     #25#0 = #104#7;
     |LEE 000#25=;
     |SI FSalida = 0;
          aTo = #25#62; |QBF aTo;
     |FINSI;
     |CIERRA #25;

     |SI aTo = "";
          aAlfa = "0000Representante " + #25#0 + " no tiene el correo informado en su ficha.";
          |MENAV aAlfa;
     |SINO;
          aNume = ("000000" + #4#1) % -106;
          aSubject = "Visado cerrado en pedido: " + #4#0 + "." + aNume;
          aSubject = aSubject + " Tecnico: [" + #4#2 + "]" + #4#3;
          aMensaje = "$$$$" + aFicMail;

          aServidor = #100#7;      |QBF aServidor;
          aIP = #100#8;            |QBF aIP;

          |DSCORREO_ENVIA aIP, aServidor, aFrom, aTo, aSubject, aMensaje, aDjunto;
          |SI FSalida ! 0;
               |MENAV "0000Ha habido un error durante el envio al representante.";
          |SINO;
               aAlfa1 = "0000Notificacion [" + #104#25 + "." + #104#0 + "] cerrado enviada al representante [" + #25#0 + "]";
               |MENAV aAlfa1;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Correo;
     |SELECT #3#500;
     aAlfa = Usuario % 810;
     |QBF aAlfa;
     #500#2 = aAlfa;
     |LEE 000#500=;
     |SI FSalida ! 0;
          |MENAV "0000No tiene ficha de tecnico segun su usuario DS.";
          |ACABA;
     |SINO;
          aFrom = #500#3;     |QBF aFrom;
          |SI aFrom = "";
               |MENAV "0000No tiene correo informado en su ficha de tecnico.";
               |ACABA;
          |FINSI;
     |FINSI;

     |SELECT #1#500;
     #500#0 = #100#12;
     |LEE 000#500=;
     |SI FSalida ! 0;
          |MENAV "0000No existe la ficha del tecnico para notificaciones.";
          |ACABA;
     |SINO;
          aTo = #500#3; |QBF aTo;
          |SI aTo = "";
               aTo = #500#4;
          |SINO;
               aAlfa = #500#4; |QBF aAlfa;
               |SI aAlfa ! "";
                    aTo = aTo + ";" + #500#4;
               |FINSI;
          |FINSI;
          |QBF aTo;
          |SI aTo = "";
               |MENAV "0000No tiene correo informado el tecnico de pre-pedidos.";
               |ACABA;
          |FINSI;

          aNume = ("000000" + #4#1) % -106;
          aSubject = "Visados cerrados en pedido: " + #4#0 + "." + aNume;
          aMensaje = aSubject;

          aServidor = #100#7;      |QBF aServidor;
          aIP = #100#8;            |QBF aIP;

          |DSCORREO_ENVIA aIP, aServidor, aFrom, aTo, aSubject, aMensaje, aDjunto;
          |SI FSalida ! 0;
               |MENAV "0000Ha habido un error durante el envio.";
          |SINO;
               aAlfa1 = "0000Notificacion [" + #104#25 + "." + #104#0 + "] cerrado enviada.";
               |MENAV aAlfa1;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO El3Cerrado;
     nCerrado = 1;
     |BUCLE diam0003_1;
     |SI nCerrado = 1;
          aDef = aDefh + "dsparm01";
          |CARGA_DEF aDef, dsparm01@;
          |SI FSalida = 0;
               |PATH_DAT #100 aPath;
               |ABRE #100;
               |ABRE #500;
               |LEE 121#100p;
               |SI FSalida = 0;
                    |HAZ Correo;
               |FINSI;
               |LIBERA #100;
               |CIERRA #100;
               |CIERRA #500;
          |FINSI;
          |DESCARGA_DEF #dsparm01@;
     |SINO;
          aAlfa1 = "0000Pedido [" + #104#25 + "." + #104#0 + "] pendiente aun de otros tecnicos.";
          |MENAV aAlfa1;
     |FINSI;

     aDef = aDefh + "dsparm01";
     |CARGA_DEF aDef, dsparm01@;
     |SI FSalida = 0;
          |PATH_DAT #100 aPath;
          |ABRE #100;

          |LEE 121#100p;
          |SI FSalida = 0;
               |ABRE #500;
               |HAZ CorreoRepre;
               |CIERRA #500;
          |FINSI;
          |LIBERA #100;
          |CIERRA #100;
     |FINSI;
     |DESCARGA_DEF #dsparm01@;
|FPRC;

|PROCESO dstelm03;
     nCuantos = nCuantos + 1;
|FPRC;

|DEFBUCLE dstelm03;
     #30#1003;
     ;
     #4#8, #5#9, "01.01.0000";
     #4#8, #5#9, "31.12.9999";
     ;
     NULL, dstelm03;
|FIN;

|PROCESO diam0005_1;
     #20#5 = #104#4;
     #20#0 = #5#9;
     |LEE 000#20=;
     |SI FSalida = 0;|Y #5#9 ! 0;
          |SI #20#7 = "C";
               nCuantos = 0;
               |BUCLE dstelm03;
               #5#10 = nCuantos;
               |GRABA 020#5a;
               |LIBERA #5;
          |SINO;
               aAlfa1 = "0000Tiquet [" + #20#5 + "." + #20#0 + "] aun abierto.";
               |MENAV aAlfa1;

               nSwNoCierres = 1;
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE diam0005_1;
     #5#1;
     ;
     #4#0, #4#1, #4#2, 0000;
     #4#0, #4#1, #4#2, 9999;
     be;
     NULL, diam0005_1;
|FIN;

|PROCESO diaw0004;
     #104#25 = #4#0;
     #104#0 = #4#1;
     |LEE 000#104=;
     |SI FSalida = 0;
          nSwNoCierres = 0;
          |BUCLE diam0005_1;
          |SI nSwNoCierres = 0;
               #3#0 = #4#0;
               #3#1 = #4#1;
               #3#2 = #4#2;
               |LEE 121#3=;
               |SI FSalida = 0;
                    #3#6 = "C";
                    |GRABA 020#3a;
               |FINSI;
               |LIBERA #3;

               |DFICE aAlfa;
               aFicMail = aAlfa + "visado.txt";
               |HAZ FicMail;
               |HAZ El3Cerrado;
               |FBORRA aAlfa;
          |SINO;
               aAlfa1 = "0000El pedido [" + #104#25 + "." + #104#0 + "] no se cierra.";
               |MENAV aAlfa1;
          |FINSI;
     |SINO;
          aAlfa1 = "0000Pedido [" + #104#25 + "." + #104#0 + "] inaccesible.";
          |MENAV aAlfa1;
     |FINSI;
|FPRC;

|DEFBUCLE diaw0004;
     #4#1003;
     7;
     "     ", 000000, 0000, "*";
     "zzzzz", 999999, 9999, "*";
     ;
     NULL, diaw0004;
|FIN;

|PROCESO diam0005;
     nCerrado = 0;

     |SI #5#9 = 0;  |ACABA;   |FINSI;

     #20#5 = #104#4;          || cliente
     #20#0 = #5#9;            || tiquet
     |LEE 000#20=;
     |SI FSalida = 0;|Y #20#7 = "A";
          nCerrado = 1;
          |ERROR10;
     |FINSI;
|FPRC;

|DEFBUCLE diam0005;
     #5#1;
     ;
     #3#0, #3#1, #3#2, 0000;
     #3#0, #3#1, #3#2, 9999;
     ;
     NULL, diam0005;
|FIN;

|PROCESO diam0003;
     #104#25 = #3#0;
     #104#0 = #3#1;
     |LEE 000#104=;
     |SI FSalida ! 0;
          aAlfa1 = "0000Pedido [" + #104#25 + "." + #104#0 + "] inaccesible.";
          |MENAV aAlfa1;
          |ACABA;
     |FINSI;

     nCerrado = -1;
     |BUCLE diam0005;
     |SI nCerrado ! 1;
          nHay = 1;
          |PARA i = 0; |SI i [ 6; |HACIENDO i = i + 1;
               #4i = #3i;
          |SIGUE;
          #4#8 = #104#4;
          |GRABA 020#4n;
     |FINSI;
|FPRC;

|DEFBUCLE diam0003;
     #3#1;
     4, 6;
     "     ", 000000, #0#1, "S", "A";
     "zzzzz", 999999, #0#1, "S", "A";
     ;
     NULL, diam0003;
|FIN;

|PROCESO Min;
     #4#0 = "     ";
     #4#1 = 1;
     #4#2 = 1;
|FPRC;

|PROCESO Max;
     #4#0 = "zzzzz";
     #4#1 = 999999;
     #4#2 = 9999;
|FPRC;

|PROCESO Tecla0; |TIPO 0;
     |SI FSalida = 0; |Y #4Campo = Contenido;
          |SI #4#7 = "*";
               #4#7 = "";
          |SINO;
               #4#7 = "*";
          |FINSI;
          |GRABA 020#4a;
          |PINTA #4#7;
     |FINSI;
|FPRC;

|PROCESO Tecla11; |TIPO 11;
     |SI FSalida = 13;
          |PUSHV 0501 2380;
          |ABRE #104;
          #104#25 = #4#0;
          #104#0 = #4#1;
          |LEE 020#104=;
          |SI FSalida = 0;
               |PINPA #0#104;
               |PINDA #0#104;
               |PAUSA;
          |FINSI;
          |CIERRA #104;
          |POPV;
          |ERROR;
     |FINSI;
     |SI FSalida = 14;
          |PUSHV 0501 2380;
          |ABRE #1; |SELECT #2#1
          #1#14 = #4#0;
          #1#15 = #4#1;
          |LEE 020#1=;
          |SI FSalida = 0;
               |PINPA #0#1;
               |PINDA #0#1;
               |ENDATOS #4#1;
          |FINSI;
          |CIERRA #1;
          |POPV;
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO Tipo3; |TIPO 3;
     |DBASS aBase;  |QBF aBase;
     aDefh = aBase + "dspartes/def/";
     aPath = aBase + "dspartes/fich/";

     |SI #0#0 = "SI";
          aDef = aDefh + "dstelm02";
          |CARGA_DEF aDef, dstelm02@;
          |SI FSalida = 0;
               |PATH_DAT #20 aPath;

               aDef = aDefh + "dstelm03";
               |CARGA_DEF aDef, dstelm03@;
               |SI FSalida = 0;
                   |PATH_DAT #30 aPath;

                   aAlfa = Usuario; |QBF aAlfa;
                   |NOME_DAT #4 aAlfa;
                   |DELINDEX #4;

                   |ABRE #4;
                   |ABRE #20;
                   |ABRE #104;
                   |BUCLE diam0003;
                   |CIERRA #104;
                   |CIERRA #20;
                   |CIERRA #4;

                   |SI nHay = 1;
                        |PINPA #0#4;
                        |PARA; |SI aMenu2 = 1; |HACIENDO;
                             |ENTLINEAL #1#4, -3, 4, 22, 0, Min, Max;
                             |MENU aMenu;
                             aMenu2 = FSalida;
                        |SIGUE;

                        |SI aMenu2 = 2;
                             |ABRE #20;
                             |ABRE #104;
                             |ABRET #3;
                             |BUCLE diaw0004;
                             |CIERRAT #3;
                             |CIERRA #104;
                             |CIERRA #20;
                        |FINSI;
                   |SINO;
                        |MENAV "0000No hay tiquets...";
                   |FINSI;

                   |DELINDEX #4;

                   |DESCARGA_DEF #dstelm02@;
                   |DESCARGA_DEF #dstelm03@;
               |SINO;
                   aDef = "0000Error al cargar:" + aDef;
                   |MENAV aDef;
                   |DESCARGA_DEF #dstelm02@;
               |FINSI;
          |SINO;
               aDef = "0000Error al cargar:" + aDef;
               |MENAV aDef;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Tipo66; |TIPO 66;
     |ABRE #500;
     #500#0 = #0#1;
     |LEE 000#500];
     |CONSULTA_CLAVE #1#500;
     |SI FSalida = 0;
          #0#1 = #500#0;
          |PINTA #0#1;
     |FINSI;
     |CIERRA #500;
|FPRC;

|PROCESO Tipo0; |TIPO 0;
     |ABRE #500;
     #500#0 = #0#1;
     |LEE 000#500=;
     |SI FSalida = 0;
          #0#2 = #500#1;
          |PINTA #0#2;
     |SINO;
          |MENAV "0000No existe tecnico...";
          |ERROR;
     |FINSI;
     |CIERRA #500;
|FPRC;

|PROCESO Tipo7; |TIPO 7;
     |ABRE #500;
     |SELECT #3#500;
     aAlfa = Usuario % 810;
     #500#2 = aAlfa;
     |LEE 000#500=;
     |SI FSalida = 0;
          #0#1 = #500#0; |PINTA #0#1;
          #0#2 = #500#1; |PINTA #0#2;
     |FINSI;
     |CIERRA #500;
|FPRC;

|PROGRAMA;
     |DBASS aBase;  |QBF aBase;
     aDefh = aBase + "dspartes/def/";
     aPath = aBase + "dspartes/fich/";

     aDef = aDefh + "dsparm05";
     |CARGA_DEF aDef, dsparm05@;
     |SI FSalida = 0;
          |PATH_DAT #500 aPath;
          |MANTE #0;
          |DESCARGA_DEF #dsparm05@;
     |FINSI;
|FPRO;
