|FICHEROS;
    iber0107 #0;

    iber0108 #10;   || movimientos cabs.
    iber0109 #11;   || movimientos lins.

    iber0102 #20;   || codigos de tarjetas
    iber0103 #21;   || codigos de incidencias
    iber0100 #22;   || codigos de terminal
    iber0016 #23;   || empresas
    iber0101 #24;   || trabajadores

    iberw001 #51;   || tmp. errores repetidos
|FIN;

|VARIABLES;
    &Tempo = "";
    aTempo51 = "";
    alfa1 = "";
    alfa2 = "";
    alfa3 = "";
    alfa4 = "";
    nume1 = 0;
    nume2 = 0;
    nume3 = 0;
    nume4 = 0;
    para1 = 0;
    para2 = 0;
    para3 = 0;
    para4 = 0;
    fech1 = @;
    fech2 = @;
    fech3 = @;
    fech4 = @;
    elsw1 = 0;
    elsw2 = 0;
    elsw3 = 0;
    elsw4 = 0;
    FEstado = 0;
    seleccio = 0;
    modo = 0;
    sw_rup = 0;

    fichero = "";
    fabre = "";
    aVariable = "";
    fcanl = "";
    bar = "";
    tar = "";
    any = "";
    mes = "";
    dia = "";
    hor = "";
    min = "";
    seg = "";
    ter = "";
    nregi = 0;
    topemes = 0;
    bisiesto = 0;
    nerro = 0;
    linea = 0;

    aAbre   = "";
    nHandle = 0;
    nNume   = 0;
    aRuta   = "";
|FIN;

|PROCESO Browse;
     |SI FSalida ! 10;  |ACABA;  |FINSI;

     aRuta = #0#2;  |QBF aRuta;
     |SI aRuta = "";
         aRuta = "C:\*.*";
     |FINSI;
     aRuta = "F" + aRuta;

     |BROWSE aRuta;

     #0#2 = aRuta % 260;  |PINTA #0#2;
|FPRC;

____________________________________/
|PROCESO GrabaLins;
     #11#0 = #10#0;
     #11#1 = #10#1;
     #11#3 = #10#2;
     #11#2 = 999999;
     |LEE 000#11];
     |SI FSalida = 0;
         |LEE 000#11a;
     |SINO;
         |LEE 000#11u;
     |FINSI;
     |SI FSalida = 0;|Y #11#0 = #10#0;|Y #11#1 = #10#1;|Y #11#3 = #10#2;
         linea = #11#2 + 1;
     |SINO;
         linea = 1;
     |FINSI;

     |DDEFECTO #11;
     #11#0 = #10#0;       || a¤o
     #11#1 = #10#1;       || mes
     #11#2 = linea;       || linea
     #11#3 = #10#2;       || dia
     #11#4 = hor;         || hora
     #11#5 = min;         || minutos
     #11#6 = seg;         || segundos
     #11#7 = bar;         || codigo tarjeta
     #11#8 = tar;         || codigo incidencia
     |GRABA 020#11n;
     |SI FSalida = 0;
         nregi = nregi + 1;
     |FINSI;
|FPRC;

|PROCESO GrabaCabs;
     #10#0 = any;
     #10#1 = mes;
     #10#2 = dia;
     |LEE 000#10=;
     |SI FSalida ! 0;
         |GRABA 020#10n;
     |FINSI;
|FPRC;

|PROCESO ImprimeError;
     #51#0 = alfa1;
     |LEE 000#51=;
     |SI FSalida ! 0;
         |GRABA 020#51n;
         |IMPRIME alfa1;
     |FINSI;
     nerro = nerro + 1;
|FPRC;

|PROCESO DiasMes;
     nume1 = any;
     bisiesto = nume1 $ 4;
     |SI bisiesto ! 0;   bisiesto = 0;  |SINO;    bisiesto = 1;  |FINSI;
     nume1 = mes;
     |SI nume1 = 1; topemes = 31;  |FINSI;
     |SI nume1 = 2; topemes = 28 + bisiesto; |FINSI;
     |SI nume1 = 3; topemes = 31;  |FINSI;
     |SI nume1 = 4; topemes = 30;  |FINSI;
     |SI nume1 = 5; topemes = 31;  |FINSI;
     |SI nume1 = 6; topemes = 30;  |FINSI;
     |SI nume1 = 7; topemes = 31;  |FINSI;
     |SI nume1 = 8; topemes = 31;  |FINSI;
     |SI nume1 = 9; topemes = 30;  |FINSI;
     |SI nume1 = 10; topemes = 31;  |FINSI;
     |SI nume1 = 11; topemes = 30;  |FINSI;
     |SI nume1 = 12; topemes = 31;  |FINSI;
|FPRC;

|PROCESO CompruHor;
     nume1 = any;
     |SI nume1 < 0;|O nume1 > 9999;
         alfa1 = "    A¤o [" + any + "] err¢neo ...";  |HAZ ImprimeError;
     |FINSI;

     nume1 = mes;
     |SI nume1 < 1;|O nume1 > 12;
         alfa1 = "    Mes [" + mes + "] err¢neo ...";  |HAZ ImprimeError;
     |FINSI;

     |HAZ DiasMes;
     nume1 = dia;
     |SI nume1 < 1;|O nume1 > topemes;
         alfa1 = "    Dia [" + dia + "] err¢neo ...";  |HAZ ImprimeError;
     |FINSI;

     nume1 = hor;
     |SI nume1 < 0;|O nume1 > 23;
         alfa1 = "    Hora [" + hor + "] err¢nea ...";  |HAZ ImprimeError;
     |FINSI;

     nume1 = min;
     |SI nume1 < 0;|O nume1 > 59;
         alfa1 = "    Minutos [" + min + "] err¢neos ...";  |HAZ ImprimeError;
     |FINSI;

     nume1 = seg;
     |SI nume1 < 0;|O nume1 > 59;
         alfa1 = "    Segundos [" + seg + "] err¢neos ...";  |HAZ ImprimeError;
     |FINSI;
|FPRC;

|PROCESO CompruTer;
     #22#0 = ter;
     |LEE 000#22=;
     |SI FSalida ! 0;
         alfa1 = "Terminal [" + #22#0 + "] inexistente.";
         |HAZ ImprimeError;
     |FINSI;
|FPRC;

|PROCESO CompruTar;
     #21#0 = tar;
     |LEE 000#21=;
     |SI FSalida ! 0;
         alfa1 = "Incidencia [" + #21#0 + "] inexistente.";
         |HAZ ImprimeError;
     |FINSI;
|FPRC;

|PROCESO CompruBar;
     #20#0 = bar;
     |LEE 000#20=;
     |SI FSalida = 0;
         #23#0 = #20#1;
         |LEE 000#23=;
         |SI FSalida ! 0;
             alfa1 = "Centro [" + #23#0 + "] inexistente.";
             |HAZ ImprimeError;
         |SINO;
              #24#0 = #20#1;
              #24#1 = #20#2;
              |LEE 000#24=;
              |SI FSalida ! 0;
                  alfa1 = "Operario [" + #24#1 + "] inexistente.";
                  |HAZ ImprimeError;
              |FINSI;
         |FINSI;
     |SINO;
         alfa1 = "C¢digo de Tarjeta [" + #20#0 + "] inexistente.";
         |HAZ ImprimeError;
     |FINSI;
|FPRC;

|PROCESO Separa;
     bar = aVariable % 304;        || codigo tarjeta
     dia = aVariable % 702;        || dia
     mes = aVariable % 902;        || mes
     any = aVariable % 1102;       || a¤o
     hor = aVariable % 1302;       || hora
     min = aVariable % 1502;       || minutos
     seg = "00";               || segundos
     tar = aVariable % 1702;       || incidencia
     ter = "001";              || terminal

     nume1 = any;
     |SI nume1 < 80; nume1 = 2000 + nume1; |SINO; nume1 = 1900 + nume1; |FINSI;
     any = nume1;
|FPRC;

|PROCESO Registro;
     |HAZ Separa;
     |HAZ CompruBar;
     |HAZ CompruTar;
     |HAZ CompruTer;
     |HAZ CompruHor;

     |HAZ GrabaCabs;
     |HAZ GrabaLins;
|FPRC;

|PROCESO Tipo3;
     aAbre = #0#2;   |QBT fichero;
     |XABRE "CU", aAbre, nHandle;
     |SI FSalida < 0;
         |MENAV "     No puedo abrir el fichero a Importar";
         |ACABA;
     |FINSI;

     |PARA; |SI; |HACIENDO;
            nNume     = 250;
            |XLEE nHandle, nNume, aVariable;
            |SI FSalida < 0;  |SAL;  |FINSI;
            |HAZ Registro;
     |SIGUE;
     |XCIERRA nHandle;

     |SI #0#4 = "S";
         |RFBORRA fichero;
     |FINSI;

     alfa1 = "    Importados [" + nregi + "] registros ...";
     |MENAV alfa1;
|FPRC;

|PROGRAMA;
     |CLS;
     |CABEZA "Importar Descarga";
     |PINPA #0#0;
     |DDEFECTO #0;
     |ABRE #0;
     |LEE 101#0p;
     |SI FSalida ! 0;    |GRABA 020#0b; |FINSI;
     #0#1 = "NO";
     |PINDA #0#0;
     |ENDATOS #1#0;

     |SI FSalida ! 0;  |CIERRA #0;  |ACABA;  |FINSI;
     |SI #0#1 ! "SI";  |CIERRA #0;  |ACABA;  |FINSI;

     |IMPRE 0;
     |SI FSalida ! 0;  |CIERRA #0;  |ACABA;  |FINSI;


     |ABRE #10;       || movtos. cabs.
     |ABRE #11;       || movtos. lins.
     |ABRE #20;       || cod.tarjetas
     |ABRE #21;       || cod.incidencias
     |ABRE #22;       || cod.terminales
     |ABRE #23;       || Centros
     |ABRE #24;       || Operarios

     |HAZ CreaTempo; aTempo51 = Tempo;
     |NOME_DAT #51 aTempo51; |DELINDEX #51;

     |ABRE #51;
     |HAZ Tipo3;
     |CIERRA #51;
     |DELINDEX #51; Tempo = aTempo51; |HAZ BoraTempo;
     |CIERRA #10;
     |CIERRA #11;
     |CIERRA #20;
     |CIERRA #21;
     |CIERRA #22;
     |CIERRA #23;
     |CIERRA #24;

     alfa1 = "----------------------------"; |IMPRIME alfa1;
     alfa1 = "N£mero de errores : " + nerro; |IMPRIME alfa1;
     alfa1 = &12;                            |IMPRIME alfa1;

     |GRABA 020#0a;
     |LIBERA #0;
     |CIERRA #0;
|FPRO;
