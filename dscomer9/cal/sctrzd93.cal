    BuscaSerIda

|FICHEROS;
     sctrz093 #0;
     sctrm003 #3
     sctrm008 #8
     sctrm012 #12
     sctrw013 #13;  ||Tmp Trayectos Servicios
     sctrw027 #27;  ||Tmp servicios
     sctrw028 #28,MANTE;  ||Tmp resultado
     sctrm024 #24;
     sctrm026 #26;
     sctrm029 #29;
     sctrm030 #30;
     sctrm031 #31;
     sctrm069 #69;
     sctrm048 #148;
     referen@;

     sctrm052 #52;
     sctrm053 #53;
|FIN;

|VARIABLES;
     &enError = 0;
     aTmp13 = "";
     aMensa = "";
     aClave = "";
     nReg = 0;
     &Tempo = "";
     aTmp13b = "";
     aDias = "";
     nSwVuelta = 0;
     nSwBusServi = 0;
     nSwSugeridos = 0;
     aDiaSemanaN = "";
     aDiaSemanaL = "";
     fFecha = @;
     aAlfa = "";
     aTemporada = "";
     i = 0;
     j = 0;
     nHay = 0;
     aPobDes = "";
     aPobOrg = "";
     nLin = 0;
     nConta = 0;
     fDesde = @;
     fHasta = @;
     nError = 0;
     aLon = "";
     nPos = 0;
     aDia = "";
     nNume = 0;
     nHora = 0;
     aHora = "";
     aMinu = "";
     nMinu = 0;
     nLinea = 0;
     aSentido = "";
     fFechaViaje = @;
     nPax = 0;
     aBloqueado = "";
     nFechaHasta = 0;
     nFechaDesde = 0;
     nNumFec = 0;
     fFechaSalida = @;
     aIdaVuelta = "";
|FIN;

|PROCESO CtrlViajerosCheqX;
     |ABRE #69;
     #69#0 = nLinea;
     #69#1 = aSentido;
     #69#2 = fFechaViaje;
     |LEE 000#69=;
     |SI FSalida ! 0; |DDEFECTO #69; |FINSI;
     |CIERRA #69;
     nPax = #69#3 - #69#4;
     aBloqueado = #69#5;
|FPRC;

|PROCESO FormaHoraX;
     aHora = aAlfa % 102; nHora = aHora; nHora = nHora $ 24;
     aMinu = aAlfa % 402; nMinu = aMinu; nMinu = nMinu $ 60;
     aAlfa = (("00" + nHora)%-102) + ":" + (("00" + nMinu)%-102);
|FPRC;

|PROCESO DiaSemanaX;
     |SI fFecha = "01.01.0000";
          aDiaSemanaN = ""; aDiaSemanaL = "";
     |SINO;
          fFecha = fFecha % A1; aAlfa = fFecha % 1102;
          |SI aAlfa = "Lu"; aDiaSemanaN = "1"; aDiaSemanaL = "L"; |FINSI;
          |SI aAlfa = "Ma"; aDiaSemanaN = "2"; aDiaSemanaL = "M"; |FINSI;
          |SI aAlfa = "Mi"; aDiaSemanaN = "3"; aDiaSemanaL = "X"; |FINSI;
          |SI aAlfa = "Ju"; aDiaSemanaN = "4"; aDiaSemanaL = "J"; |FINSI;
          |SI aAlfa = "Vi"; aDiaSemanaN = "5"; aDiaSemanaL = "V"; |FINSI;
          |SI aAlfa = "Sa"; aDiaSemanaN = "6"; aDiaSemanaL = "S"; |FINSI;
          |SI aAlfa = "Do"; aDiaSemanaN = "7"; aDiaSemanaL = "D"; |FINSI;
     |FINSI;
|FPRC;

|PROCESO CambiaDiasX;     || Le suma el N§ dias trayecto
     aAlfa = "";
     aLon = aDias % A0;
     |PARA i = 1; |SI i [ aLon; |HACIENDO i = i + 1;
          nPos = i * 100 + 1;
          aDia = aDias % nPos;
          nNume = aDia + #13#11 - 1;
          nNume = nNume - 1;
          nNume = nNume $ 7;
          nNume = nNume + 1;
          aAlfa = aAlfa + nNume;
     |SIGUE;
     aDias = aAlfa;
|FPRC;

|PROCESO LeeEl29X;
     |ABRE #29;
     #29#0 = #31#0;
     #29#2 = #31#1;
     #29#4 = #31#2;
     #29#5 = #31#3;
     #29#7 = #31#4;
     |LEE 000#29=;
     |SI FSalida = 0; |Y #29#9 = "S"; |Y #0#2 ] #29#8; |Y #0#2 [ #29#10;
          aAlfa = "0";
     |SINO;
          aAlfa = "1";
     |FINSI;
     |CIERRA #29;

     |SI aAlfa = "0"; |Y #0#3 ! "01.01.0000";
          |SI #0#3 < #29#8; |O #0#3 > #29#10;
               aAlfa = "1";
          |FINSI;
     |FINSI;

     FSalida = aAlfa;
|FPRC;

|PROCESO RFreTrayectosX;
     |PARA j = 12; |SI j [ 21; |HACIENDO j = j + 1;
          |SI #referen@#j# ! "     "; |Y j = i;
               #13#0 = #31#0;
               #13#1 = #31#1;
               #13#2 = #31#2;
               #13#3 = #30#6;
               #13#4 = #30#7;
               #13#5 = #31i;
               #13#6 = #referen@#j#;
               #13#7 = #referen@#11; || #30#14;
               #13#8 = #29#8;
               #13#9 = #29#10;
               #13#11 = #31#11;
               |GRABA 000#13n;
               nHay = 1;
          |FINSI;
     |SIGUE;
|FPRC;

|DEFBUCLE RFreTrayectosX;
     #referen@#1006;
     9, 6;
     #31#0, #31#1, #31#2, #31#3, #31#4, 000, "D", aPobDes;
     #31#0, #31#1, #31#2, #31#3, #31#4, 999, "D", aPobDes;
     ;
     NULL, RFreTrayectosX;
|FIN;

|PROCESO FreTrayectosX;
     |PARA i = 12; |SI i [ 21; |HACIENDO i = i + 1;
          |SI #31i ! "     ";
               nLin = i - 11;
               |HAZ LeeEl29X;
               |SI FSalida = 0;
                    #30#0 = #31#0;
                    #30#1 = #31#1;
                    #30#2 = #31#2;
                    #30#3 = #31#3;
                    #30#4 = #31#4;
                    #30#5 = nLin;
                    |LEE 000#30=;
                    |SI FSalida = 0;
                         nHay = 0;
                         |BUCLE RFreTrayectosX;
                         ||SI nHay ! 0; |SAL; |FINSI;
                    |FINSI;
               |FINSI;
          |FINSI;
     |SIGUE;
|FPRC;

|DEFBUCLE FreTrayectosX;
     #31#1;
     9, 6;
     "          ", 000, " " , 000, 00, 000, "O", aPobOrg;
     "zzzzzzzzzz", 999, "z" , 999, 99, 999, "O", aPobOrg;
     ;
     NULL, FreTrayectosX;
|FIN;

|PROCESO PeriServiX;
     #13#8 = #24#5;
     #13#9 = #24#6;
     #13#10 = #24#7;

     |SALVA_DATOS #13;
     |REPON_DATOS #referen@;
     nConta = nConta + 1;
     #referen@#12 = nConta;
     |GRABA 020#referen@.n;
|FPRC;

|DEFBUCLE PeriServiX;
     #24#1;
     8;
     #13#0, #13#1, #13#2, #13#3, 00, "S";
     #13#0, #13#1, #13#2, #13#3, 99, "S";
     ;
     NULL, PeriServiX;
|FIN;

|PROCESO TmpTrayectosX;
     |BUCLE PeriServiX;
|FPRC;

|DEFBUCLE TmpTrayectosX;
     #13#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, TmpTrayectosX;
|FIN;

|PROCESO CargaTrayectosX;
     |CIERRA #13; |DELINDEX #13; |ABRE #13;
     |SI nSwVuelta = 0;
          aPobOrg = #0#7; aPobDes = #0#8;
     |SINO;
          aPobOrg = #0#8; aPobDes = #0#7;
     |FINSI;
     |DDEF aAlfa;
     aAlfa = aAlfa + "sctrm031";
     |CARGA_DEF aAlfa, referen@;
     |SI FSalida = 0;
          |ABRE #30;
          |BUCLE FreTrayectosX;
          |CIERRA #30;
          |DESCARGA_DEF #referen@;
          |SI nSwBusServi = 0;
               || sctrm024 puede contener mas de 1 registro por cada uno del sctrw013
               || por eso se regraba el sctrw013 con todos los posibles

               |DDEF aAlfa; aAlfa = aAlfa + "sctrw013";
               |CARGA_DEF aAlfa, referen@;
               |SI FSalida = 0;
                    |HAZ CreaTempo; |NOME_DAT #referen@#Tempo#; aTmp13b = Tempo; |DELINDEX #referen@;
                    |ABRE #referen@;

                    nConta = 0;
                    |BUCLE TmpTrayectosX;

                    |CIERRA #13; |DELINDEX #13; |ABRE #13;
                    |LEE 000#referen@.p;
                    |PARA; |SI FSalida = 0; |HACIENDO;
                         |SALVA_DATOS #referen@;
                         |REPON_DATOS #13;
                         |GRABA 020#13n;
                         |LEE 000#referen@.s;
                    |SIGUE;

                    |CIERRA #referen@;
                    Tempo = aTmp13b; |HAZ BoraTempo; |DELINDEX #referen@;
                    |DESCARGA_DEF #referen@;
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO BuscaServiX;
     fDesde = #13#8 + (#13#11 - 1);
     fHasta = #13#9 + (#13#11 - 1);
     |SI fFechaSalida ] fDesde; |Y fFechaSalida [ fHasta;
          aDias = #13#10; |QBF aDias;
          |SI aDias = "*";
               nHay = nHay + 1;
          |SINO;
               |HAZ CambiaDiasX;
               aAlfa = aDias - aDiaSemanaN;
               |SI FSalida > 0;
                    nHay = nHay + 1;
               |FINSI;
          |FINSI;
     |FINSI;
     |SI nHay ] 1;
          |HAZ AsignaServiX;
     |FINSI;
|FPRC;

|DEFBUCLE BuscaServiX;
     #13#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, BuscaServiX;
|FIN;

|PROCESO AsignaServiX;
     |PARA i = 0; |SI i [ 12; |HACIENDO i = i + 1;
          #27i = #13i;
     |SIGUE;
     #27#13 = aIdaVuelta;
     #27#14 = fFechaSalida;

     |SI #13#11 = 2;
          #27#15 = fFechaSalida;
     |SINO;
          nNume = fFechaSalida;
          nNume = nNume + (#13#7 - 1);
          #27#15 = nNume;
     |FINSI;

     |ABRE #12;
     #12#0 = #13#1;
     |LEE 000#12=;
     |SI FSalida ! 0; |DDEFECTO #12; |FINSI;
     |CIERRA #12;

     #27#17 = #12#1;

     ||horas viaje
     nNume = (#27#15 - fFechaSalida) * 24 * 60;
     |SI nNume = 0;
          aAlfa = #13#6; |HAZ FormaHoraX;
          nNume = nMinu + (nHora * 60);
          aAlfa = #13#5; |HAZ FormaHoraX;
          nNume = nNume - (nMinu + (nHora * 60));
     |SINO;
          nNume = nNume - (24 * 60);
          aAlfa = #13#6; |HAZ FormaHoraX;
          nNume = nNume + nMinu + (nHora * 60);
          aAlfa = #13#5; |HAZ FormaHoraX;
          nNume = nNume + (24 * 60) - (nMinu + (nHora * 60));
     |FINSI;
     nMinu = nNume $ 60;
     nHora = (nNume - nMinu) / 60;
     aAlfa = (("00" + nHora) % -102) + ":" + (("00" + nMinu) % -102);
     #27#16 = aAlfa;
     ||.............
     nLinea = #13#1;
     aSentido = #13#2;
     nNume = fFechaSalida - #13#11 + 1;
     fFechaViaje = nNume;
     |HAZ CtrlViajerosCheqX;
     #27#18 = aBloqueado;
     #27#19 = nPax;
     |GRABA 000#27n;     || Sin errores porque hay duplicados
|FPRC;

|PROCESO BuscaSerIda;
     |HAZ CreaTempo; |NOME_DAT #13 Tempo; |DELINDEX #13; aTmp13 = Tempo; |ABRE #13;

     nSwVuelta = 0;
     aTemporada = #52#96;

     |HAZ CargaTrayectosX;

     nHay = 0; aDias = "";
     fFechaSalida = #52#20;
     fFecha = fFechaSalida; |HAZ DiaSemanaX;
     aIdaVuelta = "I"; |BUCLE BuscaServiX;

     |CIERRA #13; |DELINDEX #13; Tempo = aTmp13; |HAZ BoraTempo;
|FPRC;
