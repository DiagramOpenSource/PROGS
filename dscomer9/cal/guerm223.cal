|FICHEROS;
     guerm223 #0;
     guerm224 #1;
     agifa019 #19;
     agifa024 #24;
     agifa025 #25;
     guerm059 #59;  || Aux. Fac. Directa
     agifa069 #69;
     agifa084 #84;
     guerm207 #207;
     guerm208 #208;
     agifa321 #321;
     agifa324 #324;
     agifa142 #142;
     refecab@;
     refelin@;
     referen@;
|FIN;

|VARIABLES;
     &enErrCarte = 0;
     &eaProg = "";
     &serie = "";
     &numero = 0;
     &eaTag = "";
     fmes = "";
     mes = 0;
     nImpo = 0;
     nMargen = 0;
     modo = 0;
     imneto = 0;
     nDto = 0;
     retencion = 0;
     nError = 0;
     nModoCab = 0;
     nModo = 0;
     nForma = 0;
     aSql = "";
     aDef = "";
     aAlfa = "";
     nConta = 0;
     nSwConta = 0;
     nCerti = 0;
     nPresu = 0;
     nSwEntra = 0;
|FIN;

|INCLUYE i_varart;

|PROCESO ContaFac7; |TIPO 7;
     |SI #0#17 = 0; |Y #0#12 = "S";
          #84#48 = #0#16;
          #84#0 = #0#17;
          aAlfa = FEntrada;
          FEntrada = 101;
          |HAZ ContaFD7
          FEntrada = aAlfa;
          #0#17 = #84#0;
          |PINTA #0#17;
          nSwConta = 1;
     |FINSI;
|FPRC;

|PROCESO ContaFac0; |TIPO 0;
     |SI nSwConta = 1;
          #84#48 = #0#16;
          #84#0 = #0#17;
          aAlfa = FEntrada;
          FEntrada = 101;
          |HAZ ContaFD0;
          FEntrada = aAlfa;
     |FINSI;
     nSwConta = 0;
|FPRC;

|PROCESO Contador; |TIPO 7;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 1;
          |SALVA_DATOS #0;
          nPresu = #0#1;
          #0#0 = 999999;
          |LEE 000#0];
          |SI FSalida = 0;
               |LEE 000#0a;
          |SINO;
               |LEE 000#0u;
          |FINSI;
          |SI FSalida = 0; |Y #0#1 = nPresu;
               nConta = #0#0 + 1;
          |SINO;
               nConta = 1;
          |FINSI;
          |REPON_DATOS #0;
          #0#1 = nPresu;
          #0#0 = nConta;
     |FINSI;
|FPRC;

|PROCESO Facturar7; |TIPO 7;
     |C_M #0#12, 1, "N";
     |SI nSwEntra = 1; |O #0#12 = "S";
          |C_M #0#12, 1, "S";
     |FINSI;
|FPRC;

|PROCESO ModiFactu; |TIPO 0;
     |SI #0Campo ! Contenido; |Y #0#12 = "N"; |Y #0#17 ! 0;
          |CONFI "0000NBorrar Factura?";
          |SI FSalida = 0;
               nError = 0;
               |HAZ BorraFactura;
               |SI nError = 0;
                    #0#13 = ""; |PINTA #0#13;
                    #0#15 = "01.01.0000"; |PINTA #0#15;
                    #0#16 = ""; |PINTA #0#16;
                    #0#17 = 0; |PINTA #0#17;
                    #0#19 = "N";
                    |GRABA 020#0a;
               |SINO;
                    |ERROR; |ACABA;
               |FINSI;
          |SINO;
               |ERROR; |ACABA;
          |FINSI;
     |FINSI;

     aAlfa = #0#12;
     |SI #0#19 = "S";
          aAlfa = "N";
     |FINSI;
     |C_M #0#13, 1, aAlfa;
     |C_M #0#14, 1, aAlfa;
     |C_M #0#15, 1, aAlfa;
     |C_M #0#16, 1, aAlfa;
     |C_M #0#17, 1, aAlfa;
|FPRC;

|PROCESO QuitaSinPre;
     |SI #referen@#9 = 0;
          |BORRA 020#referen@.a;
     |FINSI;
     |LIBERA #referen@;
|FPRC;

|DEFBUCLE QuitaSinPre;
     #referen@#1001;
     ;
     000001;
     999999;
     be;
     NULL, QuitaSinPre;
|FIN;

|PROCESO Presupu66; |TIPO 66;
     aSql = "SELECT * FROM guerm207 INTO sq" + Usuario + ".def WHERE ";
     aSql = aSql + "11 == C";

     |SQL aSql;
     |SI FSalida = 0;
          |DDEF aDef; |QBF aDef;
          aDef = aDef + "sq" + Usuario + ".mas";
          |CARGA_DEF aDef, referen@;
          |SI FSalida = 0;
               |BUCLE QuitaSinPre;||Chapuza la sql no pirula con "AND 9 > 0"
               |ABRE #referen@;
               #referen@#0 = #0#1;
               |LEE 000#referen@.];
               |CONSULTA_CLAVE #1#referen@;
               |SI FSalida = 0;
                    #0#1 = #referen@#0;
                    |PINTA #0#1;
                    |ERROR;
               |SINO;
                    |ERROR;
               |FINSI;
               |CIERRA #referen@;
               |DELINDEX #referen@;
               |DESCARGA_DEF #referen@;
               |FBORRA aDef;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Presupu0; |TIPO 0;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 4; |ACABA; |FINSI;
     |ABRE #207;
     #207#0 = #0#1;
     |LEE 000#207=;
     |SI #207#11 ! "C";
          |MENAV "0000El presupuesto no es del tipo 'C'";
          |ERROR;
     |SINO;
          |SI #207#9 = 0;
               |MENAV "0000El presupuesto no esta asignado a pedido...";
               |ERROR;
          |SINO;
               |SI #0#12 = "S"; |Y nModo = 1;
                    |MENAV "0000Atencion certificacion facturada...";
               |FINSI;
          |FINSI;
     |FINSI;
     |CIERRA #207;
|FPRC;

|PROCESO CargaLin;
     #1#9 = #0#1;
     #1#0 = #0#0;
     #1#1 = #208#1;
     #1#2 = #208#2;
     #1#3 = #208#3;
     #1#4 = #208#7;
     #1#5 = 0;
     #1#6 = 0;
     #1#7 = 0;
     #1#8 = #208#6; || Precio
     #1#10 = #208#9;   || Precio con Dto
     #1#11 = #208#8;   ||Dto
     |GRABA 020#1n;
|FPRC;

|DEFBUCLE CargaLin;
     #208#1;
     ;
     #0#1, 001;
     #0#1, 999;
     ;
     NULL, CargaLin;
|FIN;

|PROCESO CargaCerLin;
     #1#0 = #0#0;
     #1#1 = #refelin@#1;
     |LEE 101#1=;
     |SI FSalida = 0;
          #1#5 = #1#5 + #refelin@#6;
          |GRABA 020#1a;
     |FINSI;
     |LIBERA #1;
|FPRC;

|DEFBUCLE CargaCerLin;
     #refelin@#1003;
     ;
     nPresu, nCerti, 001;
     nPresu, nCerti, 999;
     ;
     NULL, CargaCerLin;
|FIN;

|PROCESO CargaCer;
     nCerti = #refecab@#0;
     nPresu = #refecab@#1;
     |BUCLE CargaCerLin;
|FPRC;

|DEFBUCLE CargaCer;
     #refecab@#1002;
     ;
     #0#1, 001;
     #0#1, 999;
     ;
     NULL, CargaCer;
|FIN;

|PROCESO Tipo5; |TIPO 5;
     nModoCab = (FEntrada / 100) ! 0;
     |SI nModoCab = 1;
          |ABRE #1;
          |BUCLE CargaLin;
          |DDEF aDef;
          aAlfa = aDef + "guerm223";
          |CARGA_DEF aAlfa, refecab@;
          |SI FSalida = 0;
               aAlfa = aDef + "guerm224";
               |CARGA_DEF aAlfa, refelin@;
               |SI FSalida = 0;
                    |BUCLE CargaCer;
                    |DESCARGA_DEF #refelin@;
               |FINSI;
               |DESCARGA_DEF #refecab@;
          |FINSI;
          |CIERRA #1;
     |FINSI;
     |SI nModoCab = 2;
          |SI #0#12 = "S";
               |PTEC 807;     ||No entra en las lineas
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Total; |TIPO 0;
     #1#7 = #1#10 * #1#6;
     |PINTA #1#7;
|FPRC;

|PROCESO Tipo2Lin; |TIPO 2;
     nModo = (FEntrada / 100) ! 0;
     nForma = 0 +. 1;
     |SI nModoCab ! 3;
          |SI nModo = 1; |O nModo = 3;
               |MENAV "0000Solo se puede modificar la linea.";
               |ERROR; |ACABA;
          |FINSI;
          |SI nModo = 2; |Y #0#12 = "S";
               |SI nForma = -1;
                    |MENAV "0000No se puede modificar, esta facturado";
               |FINSI;
               |ERROR; |ACABA;
          |FINSI;
          |HAZ Total;
          #0#18 = #0#18 +. #1#7;
          |PINTA #0#18;
     |FINSI;
|FPRC;

|PROCESO EntraCampoFac;
     |ABRE #24;
     #24#0 = #0#2;
     |LEE 000#24=;
     |CIERRA #24;

   |ET E1;
     |ENCAMPO #12#0;
     |SI FSalida ! 0; |Y FSalida ! 3; |VETE Fin; |FINSI;
     |SI #0#12 = "S";
          #0#13 = #24#20; |PINTA #0#13;
          #0#15 = ~; |PINTA #0#15;
     |FINSI;
   |ET E2;
     |ENCAMPO #13#0;
     |SI FSalida = 2; |VETE E1; |FINSI;
     |SI FSalida ! 0; |Y FSalida ! 3; |VETE Fin; |FINSI;
   |ET E3;
     |ENCAMPO #14#0;
     |SI FSalida = 2; |VETE E2; |FINSI;
     |SI FSalida ! 0; |Y FSalida ! 3; |VETE Fin; |FINSI;
   |ET E4;
     |ENCAMPO #15#0;
     |SI FSalida = 2; |VETE E3; |FINSI;
     |SI FSalida ! 0; |Y FSalida ! 3; |VETE Fin; |FINSI;
   |ET E5;
     |ENCAMPO #16#0;
     |SI FSalida = 2; |VETE E4; |FINSI;
     |SI FSalida ! 0; |Y FSalida ! 3; |VETE Fin; |FINSI;
     |ENCAMPO #17#0;
   |ET Fin;
|FPRC;

|PROCESO Tipo3; |TIPO 3;
     |SI #0#12 = "N"; |Y #0#17 = 0;
          nSwEntra = 1;
          |HAZ EntraCampoFac;
          nSwEntra = 0;
     |FINSI;

     |CONFI "0000NImprimir Certificacion";
     |SI FSalida = 0;
          |HAZ Tipo4;
     |FINSI;
     |SI #0#12 = "S"; |Y #0#19 = "N";
          |SI #0#17 = 0;
               nError = 1;
          |SINO;
               nError = 0;
               |HAZ CreaFactura;
          |FINSI;
          |SI nError = 0;
               #0#19 = "S";
          |SINO;
               #0#12 = "N"; |PINTA #0#12;
               #0#13 = ""; |PINTA #0#13;
               #0#15 = "01.01.000"; |PINTA #0#15;
               #0#16 = ""; |PINTA #0#16;
               #0#17 = 0; |PINTA #0#17;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO CreaFactura;
     enForma = 1;
     |ABRE #142; |LEE 000#142p; |CIERRA #142;
     |ABRE #321; |LEE 000#321p; |CIERRA #321;
     |ABRE #324;
     |ABRE #25;
     |ABRE #24;
     |ABRE #84;
     |ABRE #69;
     |ABRE #59;
     |DDEFECTO #84;
     #84#48 = #0#16;
     #84#0 = #0#17;
     |LEE 000#84=;
     |SI FSalida = 0;
          |MENAV "0000La factura ya existe...";
          nError = 1;
     |SINO;
          |GRABA 020#84b;

          |DDEFECTO #24;
          #24#0 = #0#2;
          |LEE 000#24=;
          |DDEFECTO #25;
          #25#0 = #24#25;
          |LEE 000#25=;

          #84#1 = #0#15;
          #84#3 = #0#2;
          #84#4 = #0#4;
          #84#6 = #24#25;
          #84#8 = #0#13;
          #84#28 = #0#4;
          #84#30 = #24#8;
          #84#31 = #24#9;
          #84#33 = #24#10;
          #84#34 = #25#7;
          #84#35 = #24#4;
          #84#36 = #24#47;
          #84#40 = #24#16;
          #84#60 = #24#15;
          #84#64 = #24#183;
          #84#65 = #321#9;

          #324#0 = #84#65;
          |LEE 000#324=;
          #84#66 = #324#2;

          #84#67 = #24#193;

          #84#68 = #321#9;
          #84#69 = #324#2;

          #84#88 = #24#22;
          #84#89 = #24#23;
          #84#90 = #24#24;

          |HAZ LimCabAgifa084;
          |HAZ LinFactura;

          aAlfa = (("000000"+#0#1)%-106) + "-" + (("000"+#0#0)%-103);
          #84#87 = aAlfa;

          |GRABA 020#84a;

          nForma = 1;
          |HAZ Actualiza;

          |DDEFECTO #59;
          #59#0 = #84#48;
          #59#1 = #84#0;
          |LEE 101#59=;
          |SI FSalida ! 0; |GRABA 020#59b; |FINSI;
          #59#2 = #84#3;
          #59#3 = #0#3;
          #59#4 = #0#1;
          |GRABA 020#59a;
          |CONFI "0000NImprimir Factura";
          |SI FSalida = 0;
               serie = #84#48;
               numero = #84#0;
               |SUB_EJECUTA_NP "agifa086";
          |FINSI;
     |FINSI;
     |CIERRA #59;
     |CIERRA #69;
     |CIERRA #84;
     |CIERRA #24;
     |CIERRA #25;
     |CIERRA #324;
|FPRC;

|PROCESO LinFactura;
     |DDEFECTO #69;
     #69#20 = #84#48;
     #69#0 = #84#0;
     #69#1 = 1;
     #69#2 = "CERTIFICACION";
     #69#4 = "Certificacion n: " + (("000000" + #0#0) % -106);
     #69#5 = 1;
     #69#6 = #0#18;
     #69#11 = #0#14;
     #69#15 = #84#3;
     #69#16 = #25#7;
     #69#17 = #24#4;
     #69#26 = #0#18;
     enForma = 1;
     |HAZ AcumulaFC;
     |HAZ TotalLin69;
     |HAZ CalCabAgifa084_2;
     |GRABA 020#69n;
|FPRC;
---------------------

|PROCESO ActArti;
     fmes = #84#1 % A402;
     mes = fmes - 1;
     mes = mes * 5;
     mes = mes + 35;
     #19#0 = #69#2;
     |LEE 101#19=;
     |SI FSalida = 0;
          |SI #19#194 = 2;
               nImpo = ((((#69#30 * #69#6) < #69#8) < #69#21) < #84#5) < #84#32;
          |SINO;
               nImpo = ((((#69#5 * #69#6) < #69#8) < #69#21) < #84#5) < #84#32;
          |FINSI;
          |SI #142#115 = "S";
               nImpo = nImpo < #84#7;
          |FINSI;
          nMargen = nImpo - #69#14;
          #19mes = #19mes + (nImpo * nForma);
          #19#95 = #19#95 + (nImpo * nForma);
          mes = mes + 1;
          #19mes = #19mes + (nMargen * nForma);
          #19#96 = #19#96 + (nMargen * nForma);
          |GRABA 020#19a;
     |FINSI;
     |LIBERA #19;

     efFecha = #84#1;
     eaArticulo = #69#2;
     enImpVta = (nImpo * nForma);
     enImpMar = (nMargen * nForma);
     |HAZ AcumulaArt;
|FPRC;

|CALCULO Actualiza;
     modo = (FEntrada / 100) ! 0;
     enForma = nForma;

     |ABRE #24;
     #24#0 = #84#3;
     |LEE 121#24=;
     |SI 0 = FSalida;
          fmes = #84#1 % A402;
          mes = fmes - 1;
          mes = mes * 4;
          mes = mes + 54;
          imneto = #84#15 - #84#25;
          |SI #142#115 = "N";
               nImpo = (imneto * 100) / (100 - #84#7);
               nDto = #84#25 + (imneto - nImpo);
          |SINO;
               nImpo = imneto;
               nDto = #84#25;
          |FINSI;
          #24mes = #24mes +. nImpo; || imneto;
          mes = mes + 1;
          #24mes = #24mes +. nDto;  || #84#25;
          mes = mes + 1;
          #24mes = #24mes +. #84#37;
          #24#102 = #24#102 + (nImpo * nForma); || imneto;
          #24#103 = #24#103 + (nDto * nForma);  || #84#25;
          #24#104 = #24#104 + (#84#37 * nForma);
          mes = FEntrada / 100;
          mes = mes ! 0;
          |SI 1 = mes;
               #24#45 = #84#1;
               #24#46 = imneto;
          |FINSI;
          #24#110 = #24#110 + (imneto * nForma);
          imneto = imneto + #84#24;
          |GRABA 020#24a;
          |LIBERA #24;

          enImpCliCom = nImpo * nForma;
          enImpCliDto = nDto * nForma;
          enImpCliMar = #84#37 * nForma;
          enImpCliFac = imneto * nForma;
          eaCliente = #84#3;
          efFecha = #84#1;
          |HAZ AcumulaCli;
     |FINSI;
     |CIERRA #24;

     |ABRE #25;
     #25#0 = #84#6;
     |LEE 121#25=;
     |SI 0 = FSalida;
           imneto = #84#15 - #84#25;
           fmes = #84#1 % A402;
           mes = fmes - 1;
           mes = mes * 2;
           mes = mes + 11;
           #25mes = #25mes + (#84#26 * nForma);
           retencion = #25mes % #25#39;
           mes = mes + 1;
           #25mes = #25mes + (imneto * nForma);
           #25#35 = #25#35 + (#84#26 * nForma);
           #25#36 = #25#36 + (imneto * nForma);
           mes = fmes - 1;
           mes = mes + 40;
           #25mes = retencion;
           retencion = #25#35 % #25#39;
           #25#52 = retencion;
           |GRABA 020#25a;
           |LIBERA #25;

          enImpRepComi = #84#26 * nForma;
          enImpRepVta = imneto * nForma;
          enImpRepRete = retencion * nForma;
          eaRepre = #84#6;
          efFecha = #84#1;
          |HAZ AcumulaRep;
     |FINSI;
     |CIERRA #25;
     ||***************** CALCULO DEL RIESGO;
     |ABRE #19;
     |BUCLELIN 2 ActArti #0;
     |CIERRA #19;
     |SI #142#47 = "S";  ||컴컴컴컴컴 Acumula en riesgos Si Crea Recibo
          eaTag = "FC";  |HAZ Riesgos;
     |FINSI;
|FCAL;

|PROCESO Imprime;
     |PRINT;
|FPRC;

|PROCESO Tipo4; |TIPO 4;
     |IMPRE 0;
     |SI FSalida = 0;
          |INFOR "guerm223";
          |SI FSalida = 0;
               |BUCLELIN 2Imprime#0;
               |PIEINF;
               |FININF;
          |SINO;
               |MENAV "0000Error en informe 'guerm223'";
          |FINSI;
          |FINIMP;
     |FINSI;
|FPRC;

|PROCESO BorLinFac;
     enForma = -1;
     |HAZ AcumulaFC;
     |BORRA 020#69a;
|FPRC;

|PROCESO BorraFactura;
     |ABRE #84;
     #84#48 = #0#16;
     #84#0 = #0#17;
     |LEE 101#84=;
     |SI FSalida = 0;
          |SI #84#39 = "S";
               nError = 1;
               |MENAV "0000Factura contabilizada...";
          |SINO;
               eaProg = "agifa084";
               |HAZ MiraRecVenta;
               |SI enErrCarte ! 0;
                    |MENAV "0000Recibos con movimientos: NO SE DARA DE BAJA";
                    nError = 1;
               |SINO;
                    nForma = -1;
                    |HAZ Actualiza;
                    |BUCLELIN 2 BorLinFac #84;
                    |BORRA 020#84a;
                    eaProg = "agifa084";
                    |HAZ BorraRecVenta;
                    |HAZ BorraVenciDir;
               |FINSI;
          |FINSI;
     |FINSI;
     |CIERRA #84;
|FPRC;

|PROCESO Tipo2Cab; |TIPO 2;
     nModo = (FEntrada / 100) ! 0;
     nForma = 0 +. 1;
     |SI nForma = -1; |Y nModo = 2; |Y #0#12 = "S";
          |MENAV "0000Atencion certificacion facturada...";
     |FINSI;
|FPRC;

|PROCESO NoEscape; |TIPO 6;
     |SI #0#12 = "S";
          |SI FSalida = 1;
               |AVISO;
               |ERROR;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO IVA223_66; |TIPO 66;
     enTiva = #0Campo;
     efFiva = #0#15;
     |HAZ ConsuIVA;
     |SI eaDiva = "";
          |ERROR;
     |SINO;
          #0Campo = enTiva; |PINTA #0Campo;
     |FINSI;
|FPRC;

|PROCESO IVA223_0; |TIPO 0;
     |SI #0#12 = "S";
          enTiva = #0Campo;
          efFiva = #0#15;
          |HAZ SacaIVA;
          |SI eaDiva = "";
               |MENAV "0000No existe tipo iva para esta fecha...";
               |ERROR; |ACABA;
          |FINSI;
     |FINSI;
|FPRC;
