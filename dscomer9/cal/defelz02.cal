|FICHEROS;
     defelz02 #0;
     defelm01 #1;
     defelw02 #2;
     defelw03 #3;
     defelw04 #4;
     agifa007 #7;
     agi00009 #9;
     agifa049 #49;
     agifa090 #90;
     agifa074 #74;
     dsm00052 #52;
     agifa142 #142;
     dsm00005 #905;
|FIN;

|INCLUYE i_varart;

|VARIABLES;
     &enTipo = 0;
     &enOrden  = 0;
     &eaReimp = "";
     &Tempo = "";
     aInforme = "defelz02";
     aAlfa = "";
     aTmp2 = "";
     aTmp3 = "";
     aTmp4 = "";
     nLin = 0;
     nLin1 = 0;
     nLin2 = 0;
     nLin3 = 0;
     nLinV = 0;
     aCampo = "";
     aTalla = "";
     i = 0;
     nLinMax = 0;
     nSal2 = 0;
     nSal3 = 0;
     nSal4 = 0;
     aArt = "";
     aArtAnt = "";
     nLinAnt = 0;
     aSerAnt = "";
     nNumAnt = 0;
     aImg = "";
     aOrg = "";
     aDes = "";
     nExiste = 0;
     aIP = "";
     nHandle = 0;
|FIN;

|PROCESO CopiaImg;
     #9#6 = "A";
     #9#0 = aArt;
     #9#1 = 0;
     |LEE 000#9];
     |SI FSalida = 0; |Y #9#0 = aArt; |Y #9#6 = "A";
          aImg = #9#2; |QBF aImg;
     |SINO;
          aImg = "vacio.bmp";
     |FINSI;
     aOrg = #142#134; |QBF aOrg;
     aOrg = aOrg + "/" + aImg;
     aDes = "/DOCUMENTOS/DSCOMER9/"; |MKDIR aDes;
     aDes = aDes + "imagen.bmp";

     |XABRE "E", aOrg, 0;
     nExiste = FSalida;

     aIP = "";
     |IP_REMOTO aIP;
     |SI aIP = "";
          |SI nExiste < 0;         ||Lo crea vacio
               |XABRE "WB", aDes, nHandle;
               |XCIERRA nHandle;
          |SINO;
               |COPIA_FICHERO aOrg, aDes;
          |FINSI;
     |SINO;
          |SI nExiste < 0;         ||Lo crea vacio
               |XABRE "CWB", aDes, nHandle;
               |XCIERRA nHandle;
          |SINO;
               |ENVIA_FICHERO aOrg, aDes;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO VerticalPed;    || Tallas por pedidos
     |SELECT #2#4;
     |DDEFECTO #4;
     #4#0 = #74#2;
     #4#6 = #1#0;
     #4#7 = #1#1;
     |LEE 101#4=;
     |SI FSalida ! 0;
          nLin3 = nLin3 + 1;
          #4#1 = nLin3;
          |GRABA 020#4b;
     |FINSI;

     aTalla = #905#4; |QBF aTalla;
     |PARA i = 8; |SI i [ 27; |HACIENDO i = i + 1;
          aCampo = #0i; |QBF aCampo;
          |SI aCampo = aTalla; |SAL; |FINSI;
     |SIGUE;
     |SI i > 27;
          |PARA i = 8; |SI i [ 27; |HACIENDO i = i + 1;
               aCampo = #0i; |QBF aCampo;
               |SI aCampo = ""; |SAL |FINSI;
          |SIGUE;
     |FINSI;
     |SI i [ 27;
          #0i = #905#4;
          #4i = #4i + #905#6;
     |FINSI;
     |GRABA 020#4a;
     |LIBERA #4;
|FPRC;

|DEFBUCLE VerticalPed;
     #905#1;
     ;
     "PV", #1#0, #1#1, nLinV, "      ";
     "PV", #1#0, #1#1, nLinV, "zzzzzz";
     ;
     NULL, VerticalPed;
|FIN;

|PROCESO Pedido;
     nLinV = #1#2;
     |BUCLE VerticalPed;
|FPRC;

|DEFBUCLE Pedido;
     #1#2;
     ;
     #74#9, #74#0, #74#1;
     #74#9, #74#0, #74#1;
     ;
     NULL, Pedido;
|FIN;

|PROCESO LinEscan;  || Componentes tipo2
     |SELECT #2#3;
     |DDEFECTO #3;
     #3#0 = #74#2;
     #3#2 = #49#2;
     |LEE 101#3=;
     |SI FSalida ! 0;
          nLin2 = nLin2 + 1;
          #3#1 = nLin2;
          |GRABA 020#3b;
     |FINSI;
     #3#3 = #49#3;
     |GRABA 020#3a;
     |LIBERA #3;
|FPRC;

|DEFBUCLE LinEscan;
     #49#1;
     10;
     #74#2, 001, 2;
     #74#2, 999, 2;
     ;
     NULL, LinEscan;
|FIN;

|PROCESO Vertical;      || Totales Talla
     |SELECT #2#2;
     |DDEFECTO #2;
     #2#0 = #74#2;
     #2#2 = #905#4;
     |LEE 101#2=;
     |SI FSalida ! 0;
          nLin1 = nLin1 + 1;
          #2#1 = nLin1;
          |GRABA 020#2b;
     |FINSI;
     #2#3 = #2#3 + #905#6;
     |GRABA 020#2a;
     |LIBERA #2;
|FPRC;

|DEFBUCLE Vertical;
     #905#1;
     ;
     "T5", #74#9, #74#0, nLinV, "      ";
     "T5", #74#9, #74#0, nLinV, "zzzzzz";
     ;
     NULL, Vertical;
|FIN;

|PROCESO Imprimelo;
     |SALVA_FICHA #90;
     |SALVA_FICHA #74;
     #90#20 = aSerAnt;
     #90#0 = nNumAnt;
     |LEE 020#90=;
     #74#9 = aSerAnt;
     #74#0 = nNumAnt;
     #74#1 = nLinAnt;
     |LEE 020#74=;

     |INFOR aInforme;
     |SI FSalida ! 0;
          aAlfa = "0000Error informe:" + aInforme;
          |MENAV aAlfa;
          |ERROR10; |ACABA;
     |FINSI;

     |ABRE #2;
     |ABRE #3;
     |ABRE #4;
     |PARA nLin = 1; |SI nLin [ nLinMax; |HACIENDO nLin = nLin + 1;
          |DDEFECTO #2;
          #2#0 = aArtAnt;
          #2#1 = nLin;
          |LEE 000#2=;
          |SI FSalida ! 0; |GRABA 020#2n; |FINSI;
          |DDEFECTO #3;
          #3#0 = aArtAnt;
          #3#1 = nLin;
          |LEE 000#3=;
          |SI FSalida ! 0; |GRABA 020#3n; |FINSI;
          |DDEFECTO #4;
          #4#0 = aArtAnt;
          #4#1 = nLin;
          |LEE 000#4=;
          |SI FSalida ! 0; |GRABA 020#4n; |FINSI;
     |SIGUE;

     aArt = aArtAnt; |HAZ CopiaImg;
     enTipo = 0; |PRINT;

     |LEE 000#2p; nSal2 = 0;
     |LEE 000#3p; nSal3 = 0;
     |LEE 000#4p; nSal4 = 0;
     |PARA; |SI nSal2 = 0; |Y nSal3 = 0; |Y nSal4 = 0; |HACIENDO;
          #4#2 = #2#2;
          #4#3 = #2#3;
          #4#4 = #3#2;
          #4#5 = #3#3;
          enTipo = 1; |PRINT;
          |LEE 000#2s; nSal2 = FSalida;
          |LEE 000#3s; nSal3 = FSalida;
          |LEE 000#4s; nSal4 = FSalida;
     |SIGUE;
     |PIEINF;
     |FININF;

     |CIERRA #2; |DELINDEX #2;
     |CIERRA #3; |DELINDEX #3;
     |CIERRA #4; |DELINDEX #4;
     |PDEFECTO #0, 8, 28;
     nLinMax = 0;
     nLin1 = 0;
     nLin2 = 0;
     nLin3 = 0;

     |REPON_FICHA #74;
     |REPON_FICHA #90;
|FPRC;

|PROCESO agifa074;
     |SI aArtAnt ! #74#2; |Y aArtAnt ! "";
          |HAZ Imprimelo;
     |FINSI;
     aArtAnt = #74#2;
     nLinAnt = #74#1;
     aSerAnt = #90#20;
     nNumAnt = #90#0;

     |ABRE #2;
     |ABRE #3;
     |ABRE #4;
     nLinV = #74#1;
     |BUCLE Vertical; |SI nLinMax < nLin1; nLinMax = nLin1; |FINSI;
     |BUCLE LinEscan; |SI nLinMax < nLin2; nLinMax = nLin2; |FINSI;
     |BUCLE Pedido; |SI nLinMax < nLin3; nLinMax = nLin3; |FINSI;
     |CIERRA #2;
     |CIERRA #3;
     |CIERRA #4;
|FPRC;

|DEFBUCLE agifa074;
     #74#1;
     ;
     #90#20, #90#0, 001;
     #90#20, #90#0, 999;
     ;
     NULL, NULL, NULL, NULL, NULL, agifa074;
     #74#2;
|FIN;

|PROCESO agifa090;
     |DDEFECTO #7;
     #7#26 = #90#20;
     |LEE 000#7=;
     aInforme = #7#51; |QBF aInforme;

     |BUCLE agifa074;
     #90#9 = "S";
     |GRABA 020#90a;
     |LIBERA #90;
|FPRC;

|DEFBUCLE agifa090;
     #90#2;
     9;
     #0#2, #0#6, #0#0, #0#4;
     #0#3, #0#7, #0#1, #0#4;
     be;
     NULL, agifa090;
|FIN;

|PROCESO Tipo3; |TIPO 3;
     |ABRE #142; |LEE 000#142p; |CIERRA #142;

     |HAZ CreaTempo; aTmp2 = Tempo; |NOME_DAT #2 Tempo;
     |HAZ CreaTempo; aTmp3 = Tempo; |NOME_DAT #3 Tempo;
     |HAZ CreaTempo; aTmp4 = Tempo; |NOME_DAT #4 Tempo;

     Impresora = "CrystalReport";
     |IMPRE -1;
     |SI FSalida  = 0;
          |ABRE #7;
          |ABRE #9;
          |BUCLE agifa090;
          |ABRE #90;
          |ABRE #74;
          |SI aArtAnt ! ""; |HAZ Imprimelo; |FINSI;
          |CIERRA #74;
          |CIERRA #90;
          |CIERRA #7;
          |CIERRA #9;
          |FINIMP;
     |FINSI;

     Tempo = aTmp2; |CIERRA #2; |DELINDEX #2; |HAZ BoraTempo;
     Tempo = aTmp3; |CIERRA #3; |DELINDEX #3; |HAZ BoraTempo;
     Tempo = aTmp4; |CIERRA #4; |DELINDEX #4; |HAZ BoraTempo;
|FPRC;

|PROGRAMA;
     |ABRE #142; |LEE 000#142p; |CIERRA #142;

     |HAZ MiraLogos; || Copia Logos para el Crystal
     |SI enOrden = 0;
          |MANTE #0;
     |SINO;
          #0#6 = eaSerie;
          #0#7 = eaSerie;
          #0#0 = enOrden;
          #0#1 = enOrden;
          #0#2 = "01.01.0000";
          #0#3 = "31.12.9999";
          #0#4 = eaReimp;
          |HAZ Tipo3;
     |FINSI;
|FPRO;
