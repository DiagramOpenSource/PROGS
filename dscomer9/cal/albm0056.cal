|FICHEROS;
     albm0056 #0;
     albm0001 #1;
     albm0002 #2;
     albm0003 #3;
     albm0006 #6;
     albm0044 #44;
     albm0046 #46;
     albm0047 #47;
     albm0052 #52;
     albm0053 #53;
     albm0054 #54;
     albm0055 #55;

     agifa007 #7;
     agifa017 #17;
     agifa019 #19;
     agif0041 #41;
     agifa067 #67;
     agifa081 #81;
     agifa112 #112;
     agifa142 #142;
     dsm00239 #239;
     albm0013 #13;
     albw0026 #726;
     albw0027 #727;
     agifa029 #29;

     agifa321;
     agifa322;
     agifa323;
     agifa324;

     albm0056@ #1056;         ||Ordenes de Tintado. CARGA_DEF
|FIN;

|INCLUYE i_varart;

|VARIABLES;
     aDesProceso = "";
     aMas = "";
     nSwTintado = 0;
     nTintado = 0;
     aPartida = "";
     &eaImpresora = "";
     nVentanaI = 0;
     nBotonI1 = 0;
     nBotonI2 = 0;
     nBotonI3 = 0;

     &enAlbeAlma = 0;
     nSwModifica = 0;         ||Ticket 3036/337.
                              ||Al dar a Modificar, en el tipo 7, aumentaba en 1 el numero de pedido.
     aMenu = "";

     &enTipoCal = 0;
     &efDFecha = @;
     &efHFecha = @;
     nUni1 = 0;
     nUni2 = 0;
     nDias = 0;
     nNume = 0;
     fFecha = @;

     &eaDefImpre = "";
     &ser_ped       = "";
     &num_ped       = 0;
     aParametro = "";
     aProve    = "";
     aArticulo = "";
     aAlmacen  = "";
     aUnidades = "";
     aFechaEnt = "";
     aFechaPre = "";
     aHora     = "";
     aAlfa     = "";
     aBase     = "";

     nBoton1   = 0;
     nBoton2   = 0;
     nBoton3   = 0;
     nBoton4   = 0;
     nBoton5   = 0;
     nBoton6   = 0;
     nBoton7   = 0;
     nBoton8   = 0;
     nBoton9   = 0;
     nTinta    = 0;

     cambiomb     = 0;
     cambio       = 0;
     div_act      = 0;
     nfecha       = 0;
     nCodEmp      = 0;
     nImpDiv      = 0;
     nPend        = 0;
     nImpo        = 0;
     mes          = 0;
     nPPort1      = 0;
     nPPort2      = 0;
     nNume7       = 0;
     nNume11      = 0;
     importe      = 0;
     importeD     = 0;
     nPendiente   = 0;
     nPendiente2  = 0;
     nVaria1      = 0;
     nVaria2      = 0;
     nMes         = 0;
     nError       = 0;

     aDCerrado    = "";
     aHCerrado    = "";
     monba        = "";
     montra       = "";
     aDatos       = "";
     aDirEmp      = "";
     aMes         = "";

     fDFecha      = @;
     fHFecha      = @;
     fechasis     = @;
     fmes1        = "  ";

     &efFecVto        = @;

     &decim_base      = 0;
     &precio_base     = 0;

     &decim_divisa    = 0;  || Decimales de la divisa
     &precio_divisa   = 0; || Decimales del precio en divisa
     &prov_divisa     = "";
     &enAlbaran       = 0;
     &enTintado       = 0;

     &nDeci_Base      = 2;   || Decimales en Totales de la moneda base
     &nDeci_PreBase   = 2;   || Decimales en el Precio en moneda base
     &nDeci_BaseMxP   = 2;   || Decimales en Totales de la moneda base (Maxima precision)
     &nDeci_BaseMx    = 2;   || Decimales en Totales de la moneda base (Maxima precision)
     &nDeci_PreBasMxP = 2;   || Decimales en el Precio en moneda base (Maxima precision)
     &nDeci_PreBaseMx = 2;   || Decimales en el Precio en moneda base (Maxima precision)
     &nDeci_Div       = 2;   || Decimales en Totales de la divisa
     &nDeci_DivP      = 2;   || Decimales en Totales de la divisa
     &nDeci_PreDivP   = 2;  || Decimales en el Precio en divisa
     &nDeci_PreDiv    = 2;  || Decimales en el Precio en divisa
     &nDeci_TotVisP   = 2;  || Decimales en Totales visibles (el de la moneda de trabajo)
     &nDeci_TotNoVisP = 2;  || Decimales en Totales no visibles (el de la otra moneda)
     &nDeci_PreVisP   = 2;  || Decimales en el Precio visual. (lineas)
     &nDeci_ImpVisP   = 2;  || Decimales en el Importe visual. (lineas)
     &nDeci_DivF      = 2;   || Decimales en Totales de la divisa
     &nDeci_PreDivF   = 2;   || Decimales en el Precio en divisa
     &nDeci_TotVisF   = 2;   || Decimales en Totales visibles (el de la moneda de trabajo)
     &nDeci_TotNoVisF = 2;   || Decimales en Totales no visibles (el de la otra moneda)
     &nDeci_PreVisF   = 2;   || Decimales en el Precio visual. (lineas)
     &nDeci_ImpVisF   = 2;   || Decimales en el Importe visual. (lineas)
     &EURODB          = 2;
     &eaHoraPed = "";
     {-1}get = 0;
     get1 = 0;
     get2 = 0;
     aMensaje = "";
     aFechaOk = "";
     fFechaOk = @;

     nPrecio1 = 0;
     nPrecio2 = 0;
     nPtec    = 0;
     aTecla   = "";
|FIN;

|PROCESO Salirm56;  |TIPO 0;
     |PTEC 806;
|FPRC;

|PROCESO Tipo7m56;  |TIPO 7;
     |SI aParametro = "MODIFICA";  |ACABA;  |FINSI;
     |SI nSwModifica = 1; |ACABA; |FINSI;

     |ABRE #81;
     #81#25 = #0#1;
     #81#0  = 999999;
     |LEE 040#81];
     |SI FSalida = 0;
         |LEE 040#81a;
     |SINO;
         |LEE 040#81u;
     |FINSI;

     #0#2 = 1;
     |SI FSalida = 0;  |Y  #81#25 = #0#1;
         #0#2 = #81#0 + 1;
     |FINSI;

     |CIERRA #81;

     |PINTA #0#2;
|FPRC;

|PROCESO Tipo0m56;  |TIPO 0;
     |SI aParametro = "MODIFICA";  |ACABA;  |FINSI;
     |SI nSwModifica = 1; |ACABA; |FINSI;

     |ABRE #81;
     #81#25 = #0#1;
     #81#0  = #0#2;
     |LEE 040#81=;
     |SI FSalida = 0;
         |MENAV "0000 El Pedido ya existe.";
         |ERROR;
     |FINSI;
     |CIERRA #81;
|FPRC;

|PROCESO MiraCalidadProv;  |TIPO 0;
     |ABRE #52;
     |LEE 000#52p;
     |SI FSalida ! 0;  |DDEFECTO #52;  |FINSI;
     |CIERRA #52;

     |ABRE #53;
     #53#0 = #0#6;
     |LEE 000#53=;
     |SI FSalida ! 0;  |DDEFECTO #53;  |FINSI;
     |CIERRA #53;

     |SI #53#10 [ #52#55;
         aAlfa = "0000NEl proveedor tiene una valoracion de " + #53#10 + " puntos, " + #53#18;
         |QBF aAlfa;
         aAlfa = aAlfa + ". Desea continuar ";

         |CONFI aAlfa;
         |SI FSalida ! 0;  |ERROR;  |FINSI;
     |FINSI;
|FPRC;

|PROCESO Baja46;
     #46#1 = #0#12;
     #46#2 = "";
|FPRC;

|PROCESO Alta46;
     #46#1 = #0#12;
     #46#2 = "zzzzzzzzzzzz";
|FPRC;

|PROCESO Partida66;  |TIPO 66;
     |ABRE #46;
     #46#1 = #0#12;
     #46#2 = #0Campo;
     |LEE 000#46];

     |CONSULTA_F_CLAVE #1#46, Baja46, Alta46;
     |SI FSalida = 0;
         #0Campo = #46#2;  |PINTA #0Campo;
     |FINSI;
     |CIERRA #46;
|FPRC;

|PROCESO Partida0;  |TIPO 0;
     |SI Campo = 19;
         |SI #0Campo = "               ";
             |C_M #0#20, 1, "N";  #0#20 = 0;  |PINTA #0#20;
             |C_M #0#21, 1, "N";  #0#21 = 0;  |PINTA #0#21;
             |ACABA;
         |SINO;
            |C_M #0#20, 1, "S";
            |C_M #0#21, 1, "S";
         |FINSI;
     |FINSI;

     |ABRE #46;
     #46#1 = #0#12;
     #46#2 = #0Campo;
     |LEE 000#46=;
     |SI FSalida ! 0;
         |MENAV "0000 Partida Inexistente.";
         |CIERRA #46;
         |ACABA;
     |FINSI;

     |SI Campo = 16;
         #0#22 = #46#0;
         #0#53 = #46#3;

         |ABRE #112;
         #112#0 = #0#53;
         |LEE 000#112=;
         |SI FSalida ! 0;  |DDEFECTO #112;  |FINSI;
         |CIERRA #112;

         #0#54 = #112#1;
     |SINO;
         |SI #46#0 ! #0#22;
             |MENAV "0000 El codigo del Articulo no coincide con el de la primera partida.";
             |ERROR;
             |ACABA;
         |FINSI;
     |FINSI;

     |ABRE #19;
     #19#0 = #0#22;
     |LEE 000#19=;
     |SI FSalida ! 0;  |DDEFECTO #19;  |FINSI;
     |CIERRA #19;

     aAlfa = #19#195;  |QBF aAlfa;
     |SI aAlfa = "";  aAlfa = #19#1;  |FINSI;
     #0#23 = aAlfa;

     |PINTA #0Campo;
     |PINTA #0#22;
     |PINTA #0#23;
     |PINTA #0#53;
     |PINTA #0#54;
|FPRC;

|PROCESO CompruebaPar;
     |ABRE #46;
     |SI Campo = 18;
         #46#1 = #0#12;
         #46#2 = #0#16;
         |LEE 000#46=;
         |SI FSalida ! 0;  |DDEFECTO #46;  |FINSI;

         |SI #46#32 < #0#17;  |O #46#31 < #0#18;
             aAlfa = "0000 En la partida disponemos de " + #46#32 + " balas y de " + #46#31 + " kilos.";
             |MENAV aAlfa;
         |FINSI;
     |FINSI;

     |SI Campo = 21;  |Y #0#19 ! "               ";
         #46#1 = #0#12;
         #46#2 = #0#19;
         |LEE 000#46=;
         |SI FSalida ! 0;  |DDEFECTO #46;  |FINSI;

         |SI #46#32 < #0#20;  |O #46#31 < #0#21;
             aAlfa = "0000 En la partida disponemos de " + #46#32 + " balas y de " + #46#31 + " kilos.";
             |MENAV aAlfa;
         |FINSI;
     |FINSI;

     |CIERRA #46;
|FPRC;

|PROCESO TotalPar;
     #0#24 = #0#17 + #0#20;  |PINTA #0#24;
     #0#25 = #0#18 + #0#21;  |PINTA #0#25;

     #0#34 = #0#24;  |PINTA #0#34;
     #0#35 = #0#25;  |PINTA #0#35;

     #0#55 = #0#56 - #0#35; |PINTA #0#55;
|FPRC;

|PROCESO PintaArticulo;
     |ABRE #19;
/*
     #19#0 = #0#14;
     |LEE 000#19=;
     |SI FSalida ! 0;  |DDEFECTO #19;  |FINSI;
     #0#15 = #19#1;
*/
     |ABRE #albm0055;
     #albm0055#0 = #0#14;
     |LEE 000#albm0055.=;
     |SI FSalida ! 0;  |DDEFECTO #albm0055;  |FINSI;
     #0#15 = #albm0055#1;
     |CIERRA #albm0055;

     #19#0 = #0#28;
     |LEE 000#19=;
     |SI FSalida ! 0;  |DDEFECTO #19;  |FINSI;
     |CIERRA #19;

     aAlfa = #19#195;  |QBF aAlfa;
     |SI aAlfa = "";  aAlfa = #19#1;  |FINSI;
     #0#29 = aAlfa;

     |ABRE #44;
     #44#0 = #0#6;
     #44#1 = #0#28;
     |LEE 101#44=;
     |SI FSalida ! 0;
         |DDEFECTO #44;
         #44#0 = #0#6;
         #44#1 = #0#28;
         |GRABA 020#44n;
         |LIBERA #44;
     |FINSI;
     |CIERRA #44;

     |SI aParametro ! "MODIFICA";
          #0#32 = #44#2;
          #0#33 = #44#3;
     |SINO;
          ||ARA
          #0#32 = #3#15;
          #0#33 = #3#16;
     |FINSI;

     |PINTA #0#29;
     |PINTA #0#32;
     |PINTA #0#33;
|FPRC;

|PROCESO GrabaArtProv;
     |ABRE #44;
     #44#0 = #0#6;
     #44#1 = #0#28;
     |LEE 101#44=;
     |SI FSalida = 0;
         |SI #0#32 ! #44#2;
             |CONFI "0000SEl codigo del Articulo proveedor a cambiado. Desea Guardarlo.";
             |SI FSalida = 0;
                 #44#2 = #0#32;
                 #44#3 = #0#33;
                 |GRABA 020#44a;
             |FINSI;
         |FINSI;
         |SI #0#33 ! #44#3;
             |CONFI "0000SLa descripcion del Articulo proveedor a cambiado. Desea Guardarlo.";
             |SI FSalida = 0;
                 #44#2 = #0#32;
                 #44#3 = #0#33;
                 |GRABA 020#44a;
             |FINSI;
         |FINSI;
     |FINSI;
     |LIBERA #44;
     |CIERRA #44;
|FPRC;

|PROCESO CalcuPrecio;
     |ABRE #46;
     #46#1 = #0#12;
     #46#2 = #0#16;
     |LEE 000#46=;
     |SI FSalida ! 0;  |DDEFECTO #46;  |FINSI;

     nPrecio2 = 0;

     nPrecio1 = #46#38 + #0#26 + ((#46#38 + #0#26) % #0#27);

     |SI #0#19 ! "               ";
         #46#1 = #0#12;
         #46#2 = #0#19;
         |LEE 000#46=;
         |SI FSalida ! 0;  |DDEFECTO #46;  |FINSI;

         |SI #46#38 ! 0;
             nPrecio2 = #46#38 + #0#26 + ((#46#38 + #0#26) % #0#27);
         |FINSI;
     |FINSI;

     #0#36 = 0;
     |SI nPrecio1 ! 0;  |Y nPrecio2 ! 0;
         #0#36 = ((nPrecio1 + nPrecio2) / 2) ! 2;
     |SINO;
         |SI nPrecio1 ! 0;  #0#36 = nPrecio1 ! 2;  |FINSI;
         |SI nPrecio2 ! 0;  #0#36 = nPrecio2 ! 2;  |FINSI;
     |FINSI;
     |CIERRA #46;

     |PINTA #0#36;
|FPRC;

|PROCESO TotalPedido;
     #0#39 = (((#0#35 * #0#36) < #0#37) < #0#38);
     |PINTA #0#39;
|FPRC;

|PROCESO FechaV;  |TIPO 0;
     #81#4  = #0#6;
     #81#2  = #0#40;
     #81#9  = #112#17;
     |HAZ AlbePedFec2;
     #0#41 = efFecVto;
     |PINTA #0#41;
|FPRC;

|PROCESO PideDias;
     |PUSHV 0501 2395;
     nNume = 1;
     E_sup = 9999;
     E_inf = 1;
     |PINTA 3223, aAlfa;
     nDias = 3228 ? 1;
     |POPV;
|FPRC;

|PROCESO Boton1;
     |ESTADO_CONTROL nBoton1, "DISABLE";
     |ESTADO_CONTROL nBoton2, "DISABLE";

     |REFERENCIA_DEF get1, #albm0056;
     get2 = 40;                         ||Numero de campo
     Iget = get1<-;
     |ESPECIFICA "GETDATACONTROL",get;
     aFechaOk = FSalida;
     fFechaOk = aFechaOk;

     |SI nDias = 0;
          |ABRE #albm0013;
          |DDEFECTO #albm0013;
          |LEE 000#albm0013.p;
          |CIERRA #albm0013;
          nDias = #albm0013#1;
     |FINSI;

     aAlfa = "Dias"; |HAZ PideDias;
     nNume = fFechaOk - ((nDias/2)!0);
     fDFecha = nNume;
     nNume = nNume + nDias;
     fHFecha = nNume;

     #67#13  = fFechaOk;
     #67#2   = #0#14;
     |HAZ FecEntrega;
     |ESTADO_CONTROL nBoton1, "ENABLE";
     |ESTADO_CONTROL nBoton2, "ENABLE";
     |ENCAMPO #40#albm0056;
|FPRC;

|PROCESO Boton2;
     |ESTADO_CONTROL nBoton1, "DISABLE";
     |ESTADO_CONTROL nBoton2, "DISABLE";

     |REFERENCIA_DEF get1, #albm0056;
     get2 = 41;                         ||Numero de campo
     Iget = get1<-;
     |ESPECIFICA "GETDATACONTROL",get;
     aFechaOk = FSalida;
     fFechaOk = aFechaOk;

     |SI nDias = 0;
          |ABRE #albm0013;
          |DDEFECTO #albm0013;
          |LEE 000#albm0013.p;
          |CIERRA #albm0013;
          nDias = #albm0013#1;
     |FINSI;

     aAlfa = "Vtos"; |HAZ PideDias;
     nNume = fFechaOk - ((nDias/2)!0);
     efDFecha = nNume;
     nNume = nNume + nDias;
     efHFecha = nNume;

     enTipoCal = 1;
     |HAZ PedComVal;
     enTipoCal = 0;
     |ESTADO_CONTROL nBoton1, "ENABLE";
     |ESTADO_CONTROL nBoton2, "ENABLE";
     |ENCAMPO #41#albm0056;
|FPRC;

|| --------------------------------------------------------------------------
|| Procesos de Creacion del Pedido
|| --------------------------------------------------------------------------

|PROCESO MiraMonedaBase;
     |ABRE #agifa321;
     |ABRE #agifa324;

     |LEE 010#agifa321.p;   ||Leo parametros de divisa
     monba = #agifa321#9;  ||Cojo la Moneda Base de parametros

     #agifa324#0 = #agifa321#9;
     |LEE 010#agifa324.=;         ||Leo la ficha de la moneda base en Divisas
     cambiomb    = #agifa324#3;  ||Asigno el cambio de la moneda base con el euro
     decim_base  = #agifa324#7;    ||Asigno los decimales de la moneda base
     precio_base = #agifa324#9;   ||Asigno los decimales del precio en la mon. base

     |CIERRA #agifa324;
     |CIERRA #agifa321;
|FPRC;

|PROCESO Param_Divisas; |TIPO 0;
     |ABRE #agifa324;
     #agifa324#0 = #112#81;
     |LEE 001#agifa324.=;
     |CIERRA #agifa324;

     decim_divisa  = #agifa324#7;
     precio_divisa = #agifa324#9;
     nDeci_DivP    = decim_divisa;
     nDeci_PreDivP = precio_divisa;
     cambio        = #agifa324#3; ||Cambio divisa

     |SI montra = "B";
         nDeci_PreVisP   = precio_divisa;
         nDeci_ImpVisP   = decim_divisa;
         nDeci_Base      = decim_base;
         nDeci_PreBase   = precio_base;
         nDeci_BaseMxP   = decim_base;
         nDeci_BaseMx    = decim_base;
         nDeci_PreBasMxP = precio_base;
         nDeci_PreBaseMx = precio_base;
         nDeci_TotVisP   = decim_base;
         nDeci_TotNoVisP = decim_divisa;
     |SINO;
         nDeci_PreVisP   = precio_base;
         nDeci_ImpVisP   = decim_base;
         nDeci_Base      = decim_base;
         nDeci_PreBase   = precio_base;
         |SI cambio = 1;
              nDeci_BaseMxP   = decim_base;    || sin triangulacion
              nDeci_BaseMx    = decim_base;    || sin triangulacion
              nDeci_PreBasMxP = precio_base;
              nDeci_PreBaseMx = precio_base;
         |SINO;
              nDeci_BaseMxP   = #agifa321#10;  || con triangulacion
              nDeci_BaseMx    = #agifa321#10;  || con triangulacion
              nDeci_PreBasMxP = #agifa321#11;
              nDeci_PreBaseMx = #agifa321#11;
         |FINSI;
         nDeci_TotVisP   = decim_divisa;
         nDeci_TotNoVisP = decim_base;
     |FINSI;
|FPRC;

|PROCESO CambioDivisa; |TIPO 0;
      |SI #agifa112#80 = "S"; || Si el proveedor es de divisa pactada
          |ABRE #agifa323;
          |ABRE #agifa322;
          #agifa322#0 = #agifa081#4;
          #agifa322#2 = #agifa081#27;
          #agifa322#7 = "N";
          |SELECT #2#agifa322;  || Con esta clave ...
          |LEE 011#agifa322.=;  || ... busco periodos no finalizados de esa divisa
          |SI FSalida = 0;  || Si existe un periodo no finalizado
              |PARA div_act = 0; |SI div_act = 0; |HACIENDO;
                 #agifa081#28 = #agifa322#3;        || ...y el cambio
                 |LEE 001#agifa322.=;   || Leo las lineas de Proveed/Divisas
                 nfecha = fechasis - #agifa322#6;  || Resto la fecha de fin de periodo a la actual
                 |SI nfecha > 0;  || Si la fecha actual supera a la del periodo asignado ...
                     |MENAV "0000ATENCION deberia Actualizar primero la divisa en Proveedores/Divisas";
                     prov_divisa = #agifa081#4;  || Le envio el cod. de proveedor a "agifa323"
                     |SUB_EJECUTA_NP "agifa323.tab";  || Ejecuto Proveedores/Divisas
                     |ERROR;
                 |SINO;
                     div_act = 1;  || Para salir del PARA
                 |FINSI;
              |SIGUE;
           |SINO;  || Si no existe un periodo no finalizado para esa divisa
              |MENAV "0000No existe un periodo activo para la divisa en Proveedores / Divisas";
              prov_divisa = #agifa081#4;  || Le paso a "agifa323" el proveedor
              |SUB_EJECUTA_NP "agifa323.tab";  || Ejecuto Proveedores / Divisas
              |ERROR;
           |FINSI;
           |SELECT #1#agifa322;
           |CIERRA #agifa322;
           |CIERRA #agifa323;
      |SINO; || Si el Proveedor no es de Divisa pactada ...
           |ABRE #agifa324; || ...miro directamente en divisas
           #agifa324#0 = #agifa081#27;
           |LEE 001#agifa324.=;
           |SI FSalida = 0; ||Si existe la divisa...
               #agifa081#28 = #agifa324#3; || la recojo
               |SI #agifa324#6 ! -1; || Si los dias de margen de actualizacion no
                                  || son = -1 (indefinidos)
                   |PARA div_act = 0; |SI div_act = 0; |HACIENDO;
                      #agifa081#28 = #agifa324#3;        ||Cargo el valor del cambio
                      |LEE 001#agifa324.=;  || Leo la divisa
                      nfecha = fechasis - #agifa324#4;
                      |SI nfecha > #agifa324#6; |Y #agifa324#6 ! -1;  || Si la fecha actual
                          |MENAV "0000ATENCION deberia Actualizar primero la divisa";
                          |SUB_EJECUTA_NP "agifa326.tab";
                          |ERROR;
                      |SINO;
                          div_act = 1;
                      |FINSI;
                    |SIGUE;
                |FINSI;
            |FINSI;
            |CIERRA #agifa324;
      |FINSI;
|FPRC;

|PROCESO CreaCabeceraPed;
     #112#0 = #0#6;
     |LEE 000#112=;

     |ABRE #6;
     #6#0 = #0#1;
     #6#1 = #0#2;
     |LEE 101#6=;
     |SI FSalida ! 0;
         |DDEFECTO #6;
         #6#0 = #0#1;
         #6#1 = #0#2;
         |GRABA 020#6b;
     |FINSI;

     #6#2 = #0#41;
     #6#3 = #0#42;
     |GRABA 020#6a;
     |LIBERA #6;
     |CIERRA #6;

     |DDEFECTO #81;

     #81#25 = #0#1;
     #81#0  = #0#2;
     #81#1  = #0#3;                  || FECHA PEDIDO
     #81#2  = #0#40;                 || FECHA ENTREGA
     #81#4  = #0#6;                  || PROVEEDOR.
     #81#8  = #112#2;                || NOMBRE.
     #81#9  = #112#17;               || FORMA DE PAGO.
     #81#15 = #0#43;                 || OBSERVACIONES
     #81#26 = #81#25 + "/" + #81#0;  || REFERENCIA INTERNA.
     #81#29 = "B";                   || MONEDA DE TRABAJO
     #81#27 = #112#81;               || DIVISA
     #81#32 = monba;                 || MONEDA BASE
     #81#33 = cambiomb;              || CAMBIO MON. BASE A EURO
     #81#5  = #112#24;               || DTO1 del proveedor
     #81#6  = #112#25;               || DTO P.P. del proveedor
     #81#17 = #112#41;                || DTO Acumulado del proveedor

     |DFICE aDirEmp;            || tomo el directorio de la empresa
     |DFICB aDatos;             || tomo el directorio de los datos
     aDirEmp = aDirEmp % -205;  || me quedo con los 5 digitos de la empresa
     nCodEmp = aDirEmp;         || y los paso a una variable numerica
     |PATH_DAT #41 aDatos;      || pongo el path al fichero agifa041

     |ABRE #41;                 || abre el fichero agifa041
     #41#0 = nCodEmp;
     |LEE 020#41=;              || y busco la empresa en la que estoy
     |SI FSalida = 0;
          #81#19 = #41#1;        || nombre de la empresa
          #81#18 = #41#2;        || direccion de la empresa
          #81#20 = #41#3;        || poblacion de la empresa
          #81#21 = #41#4;        || provincia de la empresa
     |FINSI;
     |CIERRA #41;               || cierro el fichero agifa041

     |HAZ Param_Divisas;
     |HAZ CambioDivisa;

     |GRABA 021#81b;
|FPRC;

|PROCESO TotalLinea;
     #19#0 = #0#28;
     |LEE 000#19=;

     #67#6  = #0#26;
     #67#33 = #0#26;
     #67#8  = #0#37;
     #67#27 = #0#38;

     |HAZ TotalLin67;
|FPRC;

|PROCESO CreaLineaPed;      || CREA LAS LINEAS DEL PEDIDO DE COMPRAS...................
     |DDEFECTO #67;
     #67#0  = #81#0;           || N.PEDIDO.
     #67#1  = 1;               || N.LINEA.
     #67#2  = #0#14;           || ARTICULO.
     #67#3  = #0#30;           || ALMACEN ENTRADA

     aDesProceso = #0#15;
     |QBF aDesProceso;
     |SI aDesProceso = "";
          |ABRE #albm0055;
          #albm0055#0 = #0#14;
          |LEE 000#albm0055.=;
          |SI FSalida ! 0;  |DDEFECTO #albm0055;  |FINSI;
          #0#15 = #albm0055#1;
          |CIERRA #albm0055;
     |FINSI;

     #67#4  = #0#15;           || DESCRIPCION.
     #67#5  = #0#35;           || UNIDADES.
     #67#41 = #0#34;           || uni2

     #19#0 = #0#14;
     |LEE 101#19=;
     |SI FSalida ! 0;
         |DDEFECTO #19;
         #19#0 = #0#14;
         #19#1 = #0#15;
         |GRABA 020#19n;
     |FINSI;

     |HAZ TotalLinea;

     #67#14 = #19#30;          || TIPO DE IVA.
     #67#13 = #0#40;           || FECHA ENTREGA.
     #67#20 = #0#6;            || PROVEEDOR.
     #67#26 = #81#9;           || FORMA DE PAGO.
     #67#28 = #81#25;          || SERIE PEDIDO.
     #67#37 = #67#5;
     #67#45 = #67#41;

     |GRABA 021#67n;

     |DDEFECTO #67;
     #67#0  = #81#0;           || N.PEDIDO.
     #67#1  = 2;               || N.LINEA.
     #67#4  = #0#23;           || DESCRIPCION.
     #67#28 = #81#25;          || SERIE PEDIDO.
     |GRABA 021#67n;
|FPRC;

|PROCESO VisualTotales;
   |SI montra = "D";
       #81#37 = #81#34;
       #81#38 = #81#35;
       #81#39 = #81#36;
       #81#40 = #81#11;
       #81#41 = #81#12;
       #81#42 = #81#13;
   |SINO;
       #81#37 = #81#11;
       #81#38 = #81#12;
       #81#39 = #81#13;
       #81#40 = #81#34;
       #81#41 = #81#35;
       #81#42 = #81#36;
   |FINSI;
|FPRC;

|PROCESO TotalizaPed;
     || ACTUALIZA LA CABECERA DEL PEDIDO DE COMPRAS..........................
     #81#11 = #81#11 + #67#9;              || IMPORTE TOTAL CABECERA Base.
     #81#12 = #81#12 + #67#11;             || IMPORTE POR RECIBIR Base.
     #81#13 = #81#11 - #81#12;             || IMPORTE RECIBIDO

     |SI #19#196 = 2;
         nPend = (#67#41 - #67#38) * #67#33;
     |SINO;
         nPend = (#67#5 - #67#38) * #67#33;
     |FINSI;
     nPend  = (((nPend < #67#8) < #81#5) < #81#17) < #81#6;

     #81#34 = #81#34 + #67#34;             || IMPORTE TOTAL CAB
     #81#35 = #81#35 + nPend;
     #81#36 = #81#34 - #81#35;             || IMPORTE RECIBIDO CAB

     |HAZ VisualTotales;

     |GRABA 021#81a;
     |LIBERA #81;
|FPRC;

|PROCESO Desactualiza;
     #81#25 = #0#1;
     #81#0  = #0#2;
     |LEE 101#81=;
     |SI FSalida = 0;
         |BORRA 020#81a;
         |LIBERA #81;
     |FINSI;

     #67#28 = #0#1;
     #67#0  = #0#2;
     #67#1  = 1;
     |LEE 101#67=;
     |SI FSalida = 0;
          |BORRA 020#67a;
          |LIBERA #67;
     |FINSI;

     #67#28 = #0#1;
     #67#0  = #0#2;
     #67#1  = 2;
     |LEE 101#67=;
     |SI FSalida = 0;
          |BORRA 020#67a;
          |LIBERA #67;
     |FINSI;

     |ABRE #2;
     #2#0 = #0#1;
     #2#1 = #0#2;
     |LEE 101#2=;
     |SI FSalida = 0;
         |BORRA 020#2a;
         |LIBERA #2;
     |FINSI;
     |CIERRA #2;

     |ABRE #3;
     #3#0 = #0#1;
     #3#1 = #0#2;
     |LEE 101#3=;
     |SI FSalida = 0;
         |BORRA 020#3a;
         |LIBERA #3;
     |FINSI;
     |CIERRA #3;

     |ABRE #112;
     |ABRE #19;
     #112#0 = #0#6;
     |LEE 121#112=;
     |SI FSalida = 0;
          fmes1 = #81#1 % A402;
          mes = fmes1 + 42;

          #19#0 = #67#2;
          |LEE 000#19=;

          |SI #19#196 = 2;
               nImpo = ((((#67#41 * #67#6) < #67#8) < #67#27) < #81#5) < #81#17;
          |SINO;
               nImpo = ((((#67#5 * #67#6) < #67#8) < #67#27) < #81#5) < #81#17;
          |FINSI;
          |SI #142#114 = "S";          || si DTO PP a PCM
               nImpo = nImpo < #81#6;
          |FINSI;
          #112mes = #112mes - nImpo;
          #112#55 = #112#55 - nImpo;
          |GRABA 020#112a;
          |LIBERA #112;

          enImpPrvPed = -nImpo;
          efFecha     = #81#1;
          eaProve     = #81#4;
          |HAZ AcumulaPrv;
     |FINSI;
     |CIERRA #112;

     enForma = -1;
     eaDocu  = "agifa067";
     |HAZ StockEntra;
|FPRC;

|PROCESO DesactualizaPart;
     |ABRE #46;

     #46#1 = #0#12;
     #46#2 = #0#16;
     |LEE 101#46=;
     |SI FSalida = 0;
/*
     ||Tiquet 3026/774
         #46#43 = #46#43 - #0#18;
         #46#31 = #46#31 + #0#18;

         #46#44 = #46#44 - #0#17;
         #46#32 = #46#32 + #0#17;

         |GRABA 020#46a;
*/
         |LIBERA #46;
     |FINSI;

     |SI #0#19 ! "               ";
         #46#1 = #0#12;
         #46#2 = #0#19;
         |LEE 101#46=;
         |SI FSalida = 0;
/*
     ||Tiquet 3026/774
             #46#43 = #46#43 - #0#21;
             #46#31 = #46#31 + #0#21;

             #46#44 = #46#44 - #0#20;
             #46#32 = #46#32 + #0#20;

             |GRABA 020#46a;
*/
             |LIBERA #46;
         |FINSI;
     |FINSI;

     |CIERRA #46;
|FPRC;

|PROCESO ActualizaPart;
     |ABRE #46;

     #46#1 = #0#12;
     #46#2 = #0#16;
     |LEE 101#46=;
     |SI FSalida = 0;
/*
     ||Tiquet 3026/774
         #46#43 = #46#43 + #0#18;
         #46#31 = #46#31 - #0#18;

         #46#44 = #46#44 + #0#17;
         #46#32 = #46#32 - #0#17;

         |GRABA 020#46a;
*/
         |LIBERA #46;
     |FINSI;

     |SI #0#19 ! "               ";
         #46#1 = #0#12;
         #46#2 = #0#19;
         |LEE 101#46=;
         |SI FSalida = 0;
/*
     ||Tiquet 3026/774
             #46#43 = #46#43 + #0#21;
             #46#31 = #46#31 - #0#21;

             #46#44 = #46#44 + #0#20;
             #46#32 = #46#32 - #0#20;

             |GRABA 020#46a;
*/
             |LIBERA #46;
         |FINSI;
     |FINSI;

     |CIERRA #46;
|FPRC;

|PROCESO ActualizaPed;
     |SI aParametro = "MODIFICA"; |O nSwModifica = 1;
         |HAZ Desactualiza;

         |HAZ CreaCabeceraPed;
         |HAZ CreaLineaPed;
         |HAZ TotalizaPed;
     |FINSI;

     ||ACTUALIZA LA FICHA DE PROVEEDORES...

     |ABRE #19;
     |ABRE #112;
     #112#0 = #0#6;
     |LEE 121#112=;
     |SI 0 = FSalida;
          fmes1 = #81#1 % A402;
          mes = fmes1 + 42;

          #19#0 = #67#2;
          |LEE 000#19=;

          |SI #19#196 = 2;
               nImpo = ((((#67#41 * #67#6) < #67#8) < #67#27) < #81#5) < #81#17;
          |SINO;
               nImpo = ((((#67#5 * #67#6) < #67#8) < #67#27) < #81#5) < #81#17;
          |FINSI;
          |SI #142#114 = "S";          || si DTO PP a PCM
               nImpo = nImpo < #81#6;
          |FINSI;
          #112mes = #112mes + nImpo;
          #112#55 = #112#55 + nImpo;
          |GRABA 020#112a;
          |LIBERA #112;

          enImpPrvPed = nImpo;
          efFecha = #81#1;
          eaProve = #81#4;
          |HAZ AcumulaPrv;
     |FINSI;
     |CIERRA #112;
     |CIERRA #19;

     enForma = 1;
     eaDocu  = "agifa067";
     |HAZ StockEntra;

     |ABRE #2;
     |DDEFECTO #2;
     #2#0  = #0#1;
     #2#1  = #0#2;
     #2#2  = #0#3;
     #2#3  = #0#30;
     #2#4  = #0#28;
     #2#5  = #0#29;
     #2#6  = #0#35;
     #2#7  = #0#6;
     #2#8  = #0#43;
     #2#9  = #0#40;
     #2#10 = "N";
     #2#13 = #0#41;
     #2#16 = (("00" + #0#4) % -102) + ":" + (("00" + #0#5) % -102);
     #2#17 = #0#42;
     #2#18 = #112#1;
     |GRABA 020#2n;
     |CIERRA #2;

     |ABRE #3;
     |DDEFECTO #3;
     #3#0  = #0#1;
     #3#1  = #0#2;
     #3#2  = #0#3;
     #3#3  = #0#30;
     #3#4  = #0#28;
     #3#5  = #0#29;
     #3#6  = #0#35;
     #3#7  = #0#6;
     #3#8  = #0#36;
     #3#9  = (("00" + #0#4) % -102) + ":" + (("00" + #0#5) % -102);
     #3#12 = #112#1;
     #3#13 = #0#0;
     #3#14 = #0#40;
     #3#15 = #0#32;
     #3#16 = #0#33;
     |GRABA 020#3n;
     |CIERRA #3;


     |SI aParametro ! "MODIFICA"; |Y aParametro ! "MANUAL";
         |ABRE #1;
         #1#0 = aFechaPre;
         #1#1 = aArticulo;
         #1#2 = aAlmacen;
         |LEE 101#1=;
         |SI FSalida = 0;
             |BORRA 020#1a;
         |FINSI;
         |CIERRA #1;
    |FINSI;
|FPRC;

|PROCESO Tipo12m56;  |TIPO 12;
     ||Solo se sale si se presiona Ctrl + Q.  O luego desde Cancelar del menu.

     |SI FSalida = 7;  |ACABA; |FINSI;

     |ABRE #13;     |LEE 000#13p;  |CIERRA #13;
     |ABRE #142;    |LEE 020#142p; |CIERRA #142;

     |HAZ MiraMonedaBase;

     |ABRE #81;
     |ABRE #67;
     |ABRE #112;
     |ABRET #19;

     |SI aParametro ! "MODIFICA"; |Y nSwModifica = 0;
         |HAZ CreaCabeceraPed;
         |HAZ CreaLineaPed;
         |HAZ TotalizaPed;
     |FINSI;

     |ESTADO_CONTROL nBoton3, "ENABLE";
     |ESTADO_CONTROL nBoton4, "ENABLE";
     |ESTADO_CONTROL nBoton5, "ENABLE";
     |ESTADO_CONTROL nBoton6, "ENABLE";
     |ESTADO_CONTROL nBoton7, "ENABLE";
     |ESTADO_CONTROL nBoton8, "ENABLE";
     |ESTADO_CONTROL nBoton9, "ENABLE";

     |PARA aMenu = ""; |SI aMenu = ""; |HACIENDO;
           |LEETECLA aTecla;

           |SI aMenu = "1"; |O aMenu = "5"; |O aMenu = "6"; |O aMenu = "7";        ||Imprimir
               |SI aParametro = "MODIFICA"; |O nSwModifica = 1;
                   |HAZ Desactualiza;

                   |HAZ CreaCabeceraPed;
                   |HAZ CreaLineaPed;
                   |HAZ TotalizaPed;
               |FINSI;

               ||Al imprimir no estaban grabadas las observaciones.
               |GRABA 020#0a;

               nTinta     = enTintado;
               eaHoraPed  = (("00" + #0#4) % -102) + ":" + (("00" + #0#5) % -102);
               eaDefImpre = #13#6;
               ser_ped    = #0#1;
               num_ped    = #0#2;
               enTintado  = #0#0;
               enAlbeAlma = #0#30;
               ||SUB_EJECUTA_NP "agifa100";
               ||HAZ MenuImpre;

               |SI aMenu = "1";
                    eaImpresora = "";
                    |SUB_EJECUTA_NP "agifa100";
               |FINSI;
               |SI aMenu = "5";
                    |ABRE #albm0013;
                    |DDEFECTO #albm0013;
                    |LEE 000#albm0013.p;
                    |CIERRA #albm0013;
                    eaImpresora = #albm0013#6;
                    |SUB_EJECUTA_NP "agifa100";
               |FINSI;
               |SI aMenu = "6";
                    |SUB_EJECUTA_NP "agifa100;FAX";
               |FINSI;
               |SI aMenu = "7";
                    |SUB_EJECUTA_NP "agifa100;EMAIL";
               |FINSI;

               aMenu      = "";
               enTintado  = nTinta;
           |SINO;
               |SI aMenu ! "";  |SAL;  |FINSI;
           |FINSI;
     |SIGUE;

     |SI aMenu = "3";     ||Aceptar
         |HAZ ActualizaPed;
         |HAZ ActualizaPart;
     |FINSI;

     |SI aMenu = "4";     ||Cancelar
         |SI aParametro ! "MODIFICA";
             #81#25 = #0#1;
             #81#0  = #0#2;
             |LEE 101#81=;
             |SI FSalida = 0;
                 |BORRA 020#81a;
             |FINSI;

             #67#28 = #0#1;
             #67#0  = #0#2;
             #67#1  = 1;
             |LEE 101#67=;
             |SI FSalida = 0;
                 |BORRA 020#67a;
             |FINSI;

             #67#28 = #0#1;
             #67#0  = #0#2;
             #67#1  = 2;
             |LEE 101#67=;
             |SI FSalida = 0;
                 |BORRA 020#67a;
             |FINSI;
         |FINSI;
     |FINSI;

     |CIERRA #81;
     |CIERRA #67;
     |CIERRA #112;
     |CIERRAT #19;
|FPRC;

|PROCESO Tipo80_m0056;  |TIPO 80;
     FSalida = 3599;
|FPRC;

컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴 Calculo F.Valor 컴컴컴컴컴컴컴컴컴컴
|PROCESO Baja027;
     #727#0 = #726#0;
     #727#1 = "     ";
     #727#2 = 0;
     #727#3 = 0;
|FPRC;

|PROCESO Alta027;
     #727#0 = #726#0;
     #727#1 = "zzzzz";
     #727#2 = 999999;
     #727#3 = 999;
|FPRC;

|PROCESO EntPediFe;
     nUni1 = #67#5 - #67#38;
     nUni2 = #67#41 - #67#46;
     |SI nUni1 ! 0; |O nUni2 ! 0;
          |DDEFECTO #726;
          #726#0 = #67#13;
          |LEE 101#726=;
          |SI FSalida ! 0; |GRABA 020#726b; |FINSI;
          #726#1 = #726#1 + nUni1;
          #726#2 = #726#2 + nUni2;
          #726#3 = #726#3 + 1;
          |GRABA 020#726a;
          |LIBERA #726;

          #727#0 = #726#0;
          #727#1 = #67#28;
          #727#2 = #67#0;
          #727#3 = #67#1;
          |LEE 101#727=;
          |SI FSalida ! 0;
              |DDEFECTO #727;
              #727#0 = #726#0;
              #727#1 = #67#28;
              #727#2 = #67#0;
              #727#3 = #67#1;
              #727#6 = #81#4;
              #727#7 = #81#8;
              |GRABA 020#727b;
          |FINSI;
          #727#4 = #727#4 + nUni1;
          #727#5 = #727#5 + nUni2;
          |GRABA 020#727a;
          |LIBERA #727;
     |FINSI;
|FPRC;

|DEFBUCLE EntPediFe;
     #67#2;
     ;
     "      ", fDFecha,     0,     0,     0, "  ", "     ", 000001;
     "zzzzzz", fHFecha, 99.99, 99.99, 99.99, "zz", "zzzzz", 999999;
     ;
     NULL, EntPediFe;
|FIN;

|PROCESO FecEntrega;
     aAlfa = Usuario; aAlfa = "3" + aAlfa;
     |NOME_DAT #726 aAlfa; |DELINDEX #726; |ABRE #726;

     aAlfa = Usuario; aAlfa = "4" + aAlfa;
     |NOME_DAT #727 aAlfa; |DELINDEX #727; |ABRE #727;

     |BUCLE EntPediFe;
     |PARA fFecha = fDFecha; |SI fFecha [ fHFecha; |HACIENDO fFecha = fFecha + 1;
          |DDEFECTO #726;
          #726#0 = fFecha;
          |LEE 000#726=;
          |SI FSalida ! 0; |GRABA 020#726n; |FINSI;
     |SIGUE;

     |LEE 000#726p;
     |PARA;  |SI;  |HACIENDO;
          |CONSULTA_CLAVE #1#726;
          |SI FSalida ! 0;  |SAL;  |FINSI;
          |CONSULTA_F_CLAVE #1#727, Baja027, Alta027;
     |SIGUE;

     |CIERRA #726; |DELINDEX #726;
     |CIERRA #727; |DELINDEX #727;
|FPRC;
컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴

|PROCESO Boton3;
     aMenu = "1";
     nPtec  = &"A";  |PTEC nPtec;
|FPRC;

|PROCESO Boton7;
     aMenu = "5";
     nPtec  = &"A";  |PTEC nPtec;
|FPRC;

|PROCESO Boton8;
     aMenu = "6";
     nPtec  = &"A";  |PTEC nPtec;
|FPRC;

|PROCESO Boton9;
     aMenu = "7";
     nPtec  = &"A";  |PTEC nPtec;
|FPRC;


|PROCESO Boton4;
     nSwModifica = 1;
     aMenu = "2";
     nPtec  = &"A";  |PTEC nPtec;
|FPRC;

|PROCESO Boton5;
     nSwModifica = 0;
     aMenu = "3";
     nPtec  = &"A";  |PTEC nPtec;
|FPRC;

|PROCESO Boton6;
     nSwModifica = 0;
     aMenu = "4";
     nPtec  = &"A";  |PTEC nPtec;
|FPRC;

|PROCESO albm0047;
     |SI #albm0047#17 = "TI";
          nTintado = #albm0047#19;
          nSwTintado = 1;
          |ERROR10;
     |FINSI;
|FPRC;

|DEFBUCLE albm0047;
 #albm0047#1;
 ;
 #albm0046#1, #albm0046#2, 001;
 #albm0046#1, #albm0046#2, 999;
 ;
 NULL, albm0047;
|FIN;

|PROGRAMA;
     nSwModifica = 0;
     |ABRE #142;    |LEE 020#142p; |CIERRA #142;

     aParametro = PARAMETRO;  |QBF aParametro;

     |ABRE #0;

     |SI aParametro = "MANUAL";
         |LEE 000#0u;
         |SI FSalida ! 0;  |DDEFECTO #0;  #0#0 = 0;  |FINSI;

         enTintado = #0#0 + 1;
         |DDEFECTO #0;
         #0#0 = enTintado;
         |GRABA 020#0b;
         enTintado = 0;

         |C_M #0#2, 1, "S";
     |FINSI;

     |SI aParametro = "MODIFICA";
         #0#0 = enTintado;
         |LEE 101#0=;
         |SI FSalida ! 0;
             |MENAV "0000 Orden de Tintado no encontrada.";
             |CIERRA #0;
             |ACABA;
         |FINSI;

         |HAZ DesactualizaPart;

         |C_M #0#2, 1, "N";
     |FINSI;

     |SI aParametro ! "MANUAL"; |Y aParametro ! "MODIFICA";
         |LEE 000#0u;
         |SI FSalida ! 0;  |DDEFECTO #0;  #0#0 = 0;  |FINSI;

         enTintado = #0#0 + 1;
         |DDEFECTO #0;
         #0#0 = enTintado;
         |GRABA 020#0b;
         enTintado = 0;

         |C_M #0#2, 1, "S";

         aProve    = aParametro % 106;
         aArticulo = aParametro % 720;
         aAlmacen  = aParametro % 2705;
         aUnidades = aParametro % 3215;
         aFechaEnt = aParametro % 4710;
         aFechaPre = aParametro % 5710;

          ||ARA

         aPartida = "";
         |ABRE #albm0001;
         |DDEFECTO #albm0001;
         #albm0001#0 = aFechaPre;
         #albm0001#1 = aArticulo;
         #albm0001#2 = aAlmacen;
         |LEE 000#albm0001.=;
         |SI FSalida = 0;
               aPartida = #albm0001#9;
               |QBF aPartida;
         |FINSI;
         |CIERRA #albm0001;

         nSwTintado = 0;
         |SI aPartida ! "";
               |ABRE #albm0046;
               |DDEFECTO #albm0046;
               #albm0046#1 = 1;
               #albm0046#2 = aPartida;
               |LEE 000#albm0046.=;
               |SI FSalida = 0;
                     |ABRE #agifa081;
                     |DDEFECTO #agifa081;
                     #agifa081#25 = #albm0046#35;
                     #agifa081#0 = #albm0046#36;
                     |LEE 000#agifa081.=;
                     |SI FSalida = 0;
                          #0#6 = #agifa081#4;
                          |ABRE #agifa067;
                          |DDEFECTO #agifa067;
                          #agifa067#28 = #agifa081#25;
                          #agifa067#0 = #agifa081#0;
                          #agifa067#1 = 1;
                          |LEE 000#agifa067.=;
                          |SI FSalida = 0;
                               #0#12 = #agifa067#3;
                               #0#14 = #agifa067#2;
                          |SINO;
                               #0#12 = aAlmacen;
                          |FINSI;
                          |CIERRA #agifa067;
                     |SINO;
                          #0#6 = aProve;
                          #0#12 = aAlmacen;
                     |FINSI;
                     |CIERRA #agifa081;

                     |BUCLE albm0047;
               |SINO;
                     #0#6 = aProve;
                     #0#12 = aAlmacen;
               |FINSI;
               |CIERRA #albm0046;
         |SINO;
               #0#6 = aProve;
               #0#12 = aAlmacen;
         |FINSI;

         |SI nSwTintado = 1;
               ||Buscamos el tintado nTintado. CargaDef
               |DBASE aAlfa; |QBF aAlfa;
               aMas = aAlfa + "def/albm0056.mas";
               |CARGA_DEF aMas, albm0056@;
               |SI FSalida ! 0; |ACABA; |FINSI;

               |ABRE #albm0056@;
               #albm0056@#0 = nTintado;
               |LEE 000#albm0056@.=;
               |SI FSalida = 0;
                    |ABRE #albm0046;
                    |DDEFECTO #albm0046;
                    #albm0046#1 = #albm0056@#12;
                    #albm0046#2 = #albm0056@#16;
                    |LEE 000#albm0046.=;
                    |SI FSalida = 0;
                         #0#53 = #albm0046#3;
                         #0#22 = #albm0046#0;
                         |ABRE #112;
                         #112#0 = #0#53;
                         |LEE 000#112=;
                         |SI FSalida ! 0;  |DDEFECTO #112;  |FINSI;
                         |CIERRA #112;
                         #0#54 = #112#1;

                         |ABRE #19;
                         #19#0 = #0#22;
                         |LEE 000#19=;
                         |SI FSalida ! 0; |DDEFECTO #19; |FINSI;
                         |CIERRA #19;
                         #0#23 = #19#1;
                    |FINSI;
                    |CIERRA #albm0046;
               |FINSI;
               |CIERRA #albm0056@;

               |DESCARGA_DEF #albm0056@;
         |FINSI;

         #0#28 = aArticulo;
         #0#35 = aUnidades;
         #0#56 = aUnidades;
         #0#55 = #0#56 - #0#35;
         #0#40 = aFechaEnt;
         #0#3 = aFechaPre;

         |ABRE #112;
         #112#0 = #0#6;
         |LEE 040#112=;
         |SI FSalida ! 0;  |DDEFECTO #112;  |FINSI;
         |CIERRA #112;

         |ABRE #29;
         #29#0 = #0#12;
         |LEE 000#29=;
         |SI FSalida ! 0;  |DDEFECTO #29;  |FINSI;
         #0#13 = #29#1;

         #29#0 = #0#30;
         |LEE 000#29=;
         |SI FSalida ! 0;  |DDEFECTO #29;  |FINSI;
         #0#31 = #29#1;
         |CIERRA #19;

         |ABRE #17;
         #17#0 = #0#28;
         #17#1 = #0#30;
         |LEE 040#17=;
         |SI FSalida ! 0;  |DDEFECTO #17;  |FINSI;
         |CIERRA #17;

         #0#7  = #112#1;
         #0#8  = #112#23;
         #0#9  = #112#6;
         #0#10 = #112#7;
         #0#11 = #112#83;

         |HAZ PintaArticulo;
     |FINSI;


     |HORA aHora;
     aAlfa = aHora % 102;  #0#4  = aAlfa;
     aAlfa = aHora % 402;  #0#5  = aAlfa;

     |PINPA #0#0;
     |PINDA #0#0;

     |CONTROL_SIMPLE 1,"BOTON,{{155}} ",  3032, 3035, Boton1;
     nBoton1  = FSalida;

     |CONTROL_SIMPLE 1,"BOTON,{{155}} ",  3132, 3135, Boton2;
     nBoton2  = FSalida;

     |CONTROL_SIMPLE 1,"BOTON,{{205}} Imprimir",  3405, 3420, Boton7;
     nBoton7  = FSalida;

     |CONTROL_SIMPLE 1,"BOTON,{{205}} FAX",  3425, 3440, Boton8;
     nBoton8  = FSalida;

     |CONTROL_SIMPLE 1,"BOTON,{{205}} Mail",  3445, 3460, Boton9;
     nBoton9  = FSalida;

     |CONTROL_SIMPLE 1,"BOTON,{{205}} Listar",  3465, 3480, Boton3;
     nBoton3  = FSalida;

     |CONTROL_SIMPLE 1,"BOTON,{{219}} Modificar",  3625, 3640, Boton4;
     nBoton4  = FSalida;

     |CONTROL_SIMPLE 1,"BOTON,{{218}} Aceptar",  3645, 3660, Boton5;
     nBoton5  = FSalida;

     |CONTROL_SIMPLE 1,"BOTON,{{217}} Cancelar",  3665, 3680, Boton6;
     nBoton6  = FSalida;

     |PARA aMenu = "2"; |SI aMenu = "2"; |HACIENDO;
          |ESTADO_CONTROL nBoton3, "DISABLE";
          |ESTADO_CONTROL nBoton4, "DISABLE";
          |ESTADO_CONTROL nBoton5, "DISABLE";
          |ESTADO_CONTROL nBoton6, "DISABLE";
          |ESTADO_CONTROL nBoton7, "DISABLE";
          |ESTADO_CONTROL nBoton8, "DISABLE";
          |ESTADO_CONTROL nBoton9, "DISABLE";

          aMenu = "";
          |ENDATOS #2#0;
          |SI FSalida ! 0;
              |SI aParametro ! "MODIFICA";
                  |BORRA 020#0a;
              |SINO;
                  |HAZ ActualizaPart;
              |FINSI;
          |SINO;
              |SI aMenu ! "4";
                  |GRABA 020#0a;
              |SINO;
                  |SI aParametro ! "MODIFICA";
                      |BORRA 020#0a;
                  |SINO;
                      |HAZ ActualizaPart;
                  |FINSI;
              |FINSI;
          |FINSI;
          |LIBERA #0;

          |C_M #0#2, 1, "N";
     |SIGUE;

     |CIERRA #0;
     |DESTRUYECONTROL nBoton1;
     |DESTRUYECONTROL nBoton2;
     |DESTRUYECONTROL nBoton3;
     |DESTRUYECONTROL nBoton4;
     |DESTRUYECONTROL nBoton5;
     |DESTRUYECONTROL nBoton6;
     |DESTRUYECONTROL nBoton7;
     |DESTRUYECONTROL nBoton8;
     |DESTRUYECONTROL nBoton9;
     |PULSATECLA;
|FPRO;
컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴횵mpresion컴컴컴컴컴컴컴컴컴컴컴컴
|PROCESO BotonI1;
     |SUB_EJECUTA_NP "agifa100";
|FPRC;

|PROCESO BotonI2;
     |SUB_EJECUTA_NP "agifa100;FAX";
|FPRC;

|PROCESO BotonI3;
     |SUB_EJECUTA_NP "agifa100;EMAIL";
|FPRC;

|PROCESO MenuImpre;
     |VENTANA 0835, 2065, -1, -1, "";
     nVentanaI = FSalida;

     |CONTROL_SIMPLE 0,"BOTON,{{205}}Imprimir", 0936, 0964,  BotonI1;
     nBotonI1 = FSalida;

     |CONTROL_SIMPLE 0,"BOTON,{{205}}Envio Fax", 1136, 1164,  BotonI2;
     nBotonI2 = FSalida;

     |CONTROL_SIMPLE 0,"BOTON,{{205}}Envio Email", 1336, 1364,  BotonI3;
     nBotonI3 = FSalida;

     |ESTADO_CONTROL nBotonI1, "ENABLE";
     |ESTADO_CONTROL nBotonI2, "ENABLE";
     |ESTADO_CONTROL nBotonI3, "ENABLE";

     |LEETECLA aTecla;

     |FIN_CONTROL_WINDOWS nBotonI1;
     |FIN_CONTROL_WINDOWS nBotonI2;
     |FIN_CONTROL_WINDOWS nBotonI3;

     |SI nVentanaI ! -1;  |FINVENTANA nVentanaI;  |FINSI;

     |PULSATECLA;
|FPRC;
컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
