|FICHEROS;
     dsm00044 #0;   || Pantalla
     dsm00043 #43;  || Pantalla
     agifa019 #1;   || Articulos
     agifa017 #2;   || Stock almacenes
     agifa029 #3;   || Almacen
     agifa027 #4;
     dsl00001 #6;   ||Codigos de barras multiples
     dsm00024 #24;
     agifa142 #42;  || Parametros generales
     dsm00005 #905;
     dsm00010 #910;
     dsm00014 #914;
     referen@;
|FIN;

|VARIABLES;
     &enSwGesAltaPar = 0;
     aDef = "";
     &enAlma3774 = 0;    || del guerz079, modulo 000con
     &Tempo = "";
     nLin = 0;
     nTecla = 0;
     aAlfa = "";
     &r_arti   = "";
     nModo     = 0;
     nSwBarra  = 1;
|FIN;

|INCLUYE i_varart;
|INCLUYE i_cdbarr;

|PROCESO ActuaCompo;
     eaTipo       = "T3";
     eaSerie      = #0#21;
     enCodigo     = #0#18;
     enLinea      = #0#8;
     eaArticulo   = #0#0;
     enAlmacen    = #0#1;
     enUnidades   = #905#6 * enForma;
     eaComponente = #905#4;
     efFecha      = #0#5;
     enCodHisto   = #0#18;
     |SI #0#7 = "S";
          eaOpeHisto = "SS";
     |SINO;
          eaOpeHisto = "ES";
     |FINSI;
     |LIBERA #905;
     |SUB_EJECUTA_NP "dsz00002;ERE";
|FPRC;

|DEFBUCLE dsm00005A;
     #905#1;
     ;
     "T3", #0#21, #0#18, #0#8, "      ";
     "T3", #0#21, #0#18, #0#8, "zzzzzz";
     be;
     NULL, ActuaCompo;
|FIN;

|PROCESO BorraCompo;
     |BORRA 020#905a;
|FPRC;

|DEFBUCLE BorraCompo;
     #905#1;
     ;
     "T3", #0#21, #0#18, #0#8, "      ";
     "T3", #0#21, #0#18, #0#8, "zzzzzz";
     be;
     NULL, BorraCompo;
|FIN;

|PROCESO Tipo2; |TIPO 2;
     |ABRE #42; |LEE 001#42p; |CIERRA #42;
     nModo = (FEntrada / 100) ! 0;
     enForma = 0 +. 1;
     enSwGesAltaPar = 0;
     |SI nSwBarra = 0; |Y nModo = 1; |Y enForma = 1; |Y #1#176 = "S";
          |HAZ GrabaElemento;
     |FINSI;

     #0#5 = #43#1;            ||Fecha

     enCodigo = #0#18;

     |HAZ AcumulaES_SS;

     #0#3 = #2#39;            || Disponible que queda
     #0#19 = #2#5;
     |BUCLE dsm00005A;

     |SI nModo =  3;
          |BUCLE BorraCompo;
          |HAZ BorraKit;
          |HAZ EsGesenvia;
          |SI FSalida = 0;
               eaSerie = #0#21;
               enCodigo = #0#18;
               enLinea = #0#8;
               |SUB_EJECUTA_NP "gesem002;BAJA";
          |FINSI;
          |HAZ EsGestral;
          |SI FSalida = 0; |Y #0#7 = "E";
               |HAZ GesBajaPar;
          |FINSI;
          |HAZ EsGesisat;
          |SI FSalida = 0;
               |HAZ GesiBorMaqui;
          |FINSI;
     |FINSI;
|FPRC;


|PROCESO decim; |TIPO 0;
     #0#4 = #0#4 ! #42#8;
     |PINTA #0#4;

     |SI enAlma3774 ! 0;
          |ABRE #2;
          #2#0 = #0#0;
          #2#1 = #0#1;
          |LEE 020#2=;
          |CIERRA #2;
          |SI #0#4 > #2#39;
               aAlfa = "0000Se superan las unidades disponibles:" + #2#39;
               |MENAV aAlfa;
               |ERROR;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO leealmacen;
     |ABRE #3;
     |DDEFECTO #3;
     #3#0 = #0#1;
     |LEE 001#3=;
     |CIERRA #3;
|FPRC;

|PROCESO Tip6; |TIPO 6;
     |ABRE #6;
     |SELECT #2#6;
     #6#3=#0#0;
     |LEE 010#6=;
     |SI FSalida=0; |Y #0#0 ! #6#1;
        #0#0=#6#1;
        |ERROR;
        |PINTA #0#0;
     |FINSI;
     |CIERRA #6;
     r_arti = "";
|FPRC;

|PROCESO CogeValores045;  |TIPO 7;
     |ABRE #42;
     |LEE 001#42p;
     |SI FSalida ! 0; |DDEFECTO #42; |FINSI;
     |CIERRA #42;

     |SI Campo = 1;
         |C_M #0Campo, 1, "S";
         |SI #42#99 = "S";
             |C_M #0Campo, 1, "N";
             #0Campo = 1;  |PINTA #0Campo;
         |FINSI;
     |FINSI;

     |SI Campo = 4;
          |HAZ DobleUni;
          |SI eaSw2Stock = "S";
               |C_M #0#4, 1, "N";
               |SI #agifa142#72 = "S";
                    |C_M #0#4, 1, "S";
               |FINSI;
               |ACABA;
          |FINSI;
     |FINSI;

     |SI #1#176 = "N";  |ACABA;  |FINSI;

     |SI Campo = 1;
         |SI enAlmacen ! 0;
             #0#1 = enAlmacen;  |PINTA #0#1;
         |FINSI;
     |FINSI;

|FPRC;

|PROCESO PideArt045;  |TIPO 7;
     |C_M #0#0, 1, "S";
     |C_M #0#1, 1, "S";

     |SI enAlma3774 ! 0; |C_M #0#1, 1, "S";  |FINSI;

     nModo = (FEntrada / 100) ! 0;
     |SI nSwBarra = 0; |Y nModo = 1; |ACABA; |FINSI;

     |SI #0#0 ! "                    ";
         |ABRE #1;
         #1#0 = #0#0;
         |LEE 000#1=;
         |SI FSalida ! 0;  |DDEFECTO #1;  |FINSI;
         |CIERRA #1;
         |SI #1#176 = "S";
             |C_M #0#0, 1, "N";
             |C_M #0#1, 1, "N";
             |ACABA;
         |FINSI;
     |FINSI;

     |ABRE #910;
     |LEE 040#910p;
     |SI FSalida ! 0;  |DDEFECTO #910;  |FINSI;
     |CIERRA #910;

     |SI #910#2 [ 2;    |ACABA;  |FINSI;

     eAlfa = #0Campo;
     |SUB_EJECUTA_NP "dsz99990";                 || Por Pantalla

     |SI eAlfa = "-1";  |PTEC 807;  |ACABA;  |FINSI;
     |SI eAlfa = "-2";              |ACABA;  |FINSI;

     eaArticulo = eAlfa;      ||  700000-4215

     #0Campo = eAlfa;
     |PINTA #0Campo;
     |PTEC 802;
|FPRC;

|PROCESO Calc6045;  |TIPO 6;
     |C_M #0#4,  1, "S";
     nModo = (FEntrada / 100) ! 0;
     |SI #0Campo ! Contenido; |Y Campo = 0;
          eaArticulo = #0#0;
          |HAZ DefectoMediC;
          #0#15 = enMedida1;
          #0#16 = enMedida2;
          #0#17 = enMedida3;
     |FINSI;
     |SI nSwBarra = 0; |Y nModo = 1;
          eaArti = #0#0;
          |HAZ HallaArti;
          |SI eaArti = ""; |ERROR; |ACABA; |FINSI;
          |SI #1#176 = "S"; |Y eaEleme = "";
               |MENAV "0000Codigo incompleto...";
               |ERROR; |ACABA;
          |FINSI;
          #0#0 = eaArti; |PINTA #0#0;
          |ACABA;
     |FINSI;


     enAlta = 1;
     eaArticulo = #0Campo;
     |HAZ LlenaArticulo;
     |SI FSalida = 0;
          #0Campo = eaArticulo;
          |PINTA #0Campo;
     |SINO;
          |ERROR; |ACABA;
     |FINSI;

     |ABRE #910;|LEE 000#910p;|CIERRA #910;

     |C_M #0#4,  1, "S";
     |SI #910#1 ! "S";
         |C_M #0#1,  1, "S";
         |C_M #0#4,  1, "S";
     |FINSI;

     |ABRE #1;
     #1#0 = #0#0;
     |LEE 000#1=;
     |SI FSalida ! 0;  |DDEFECTO #1;  |FINSI;
     |CIERRA #1;

     |SI #1#176 = "S";
         eaTipo      = "T3";
         eaSerie     = #0#21;
         enCodigo    = #0#18;
         enLinea     = #0#8;
         eaArticulo  = #0#0;
         enAlmacen   = #0#1;
         enTarifa    = 1;
         enUnidades  = #0#4;
         enCodHisto   = #0#18;

         |SUB_EJECUTA_NP "dsz00002";
         #0#4 = enUnidades; |PINTA #0#4;
         #0#1 = enAlmacen; |PINTA #0#1;

         |C_M #0#1,  1, "N";
         |C_M #0#4,  1, "N";
     |FINSI;

     |HAZ Tip6;

     |HAZ EsGesenvia;
     |SI FSalida = 0;
          |HAZ SacaKit;
     |FINSI;
|FPRC;

|PROCESO Tipo0045; |TIPO 0;
     nModo = (FEntrada / 100) ! 0;

     |ABRE #1;
     #1#0 = #0#0;
     |LEE 020#1=;
     |SI FSalida = 0;
          #0#2 = #1#1; |PINTA #0#2;
     |FINSI;
     |CIERRA #1;

     |SI nSwBarra = 0; |Y nModo = 1; |ACABA; |FINSI;

     |QBF r_arti;
     |SI r_arti ! "";
          #0#0 = r_arti;
          |PINTA #0#0;
     |FINSI;
     ||#0#6 = #1#6;     ||Total
     ||#0#20 = #1#104;  ||Dispo
|FPRC;

|PROCESO Tipo11045;  |TIPO 11;
     nModo = (FEntrada / 100) ! 0;
     |SI FSalida = 7;  |Y #1#176 = "S";
         |MENAV "      Imposible abortar;  Termine de introducir la linea y efectue una Baja.";
         |ERROR;
     |FINSI;
|FPRC;

|PROCESO MinAlma44;
     #2#0 = #0#0;
     #2#1 = 1;
|FPRC;

|PROCESO MaxAlma44;
     #2#0 = #0#0;
     #2#1 = 99999;
|FPRC;

|PROCESO cierre; |TIPO 0;
     |SI FSalida = 13; || F5;
          |ABRE #2;
          #2#0 = #0#0;
          #2#1 = #0#1;
          |LEE 000#2];
          |CONSULTA_F_CLAVE #1#2, MinAlma44, MaxAlma44;
          |SI FSalida = 0;
               #0#1 = #2#1; |PINTA #0#3;
          |SINO;
               |ERROR; |ACABA;
          |FINSI;
          |CIERRA #2;
     |FINSI;

     |HAZ Tipo44_13;
     efFec = #0#5;
     enAlma = #0#1;
     |HAZ MiraHisL;
     |SI enErrHis = 0; |HAZ MiraStockIni; |FINSI;
     |SI enErrHis ! 0;
          |ERROR;
          |ACABA;
     |FINSI;
|FPRC;

|PROCESO Tipo0Lin; |TIPO 0;
     |SI enAlma3774 ! 0;      || viene del guerz079
          #0#7 = "S";
          #0#1 = enAlma3774;
          |C_M #0#7, 1, "N";
          |C_M #0#1, 1, "N";
     |FINSI;
|FPRC;

|PROCESO Tipo7Lin; |TIPO 7;
     |HAZ PintaMedidaC;
     |HAZ EsGesenvia;
     |SI FSalida = 0;
          |ATRI I; |HAZ PinGesenvia; |ATRI 0;
     |FINSI;
|FPRC;

|PROCESO Tipo11; |TIPO 11;
     nTecla = FSalida;
     |SI nTecla = 13;
          |HAZ VerDobleUni;
          |HAZ VerKit;
          |ERROR;
     |FINSI;
     |SI nTecla = 25; || Ctrl-F7
          |HAZ CreaTempo;
          eaFichero = Tempo;
          |SUB_EJECUTA_NP "psion066";
          |HAZ BoraTempo;
          |SI enError = 0; |HAZ ImportaPDA44; |FINSI;
          |ERROR; |ACABA;
     |FINSI;
     |SI nTecla = 33; || SHIFT + F5
          |ABRE #1; #1#0 = #0#0; |LEE 000#1=; |CIERRA #1;
          |SI #1#176 = "S";
               eaTipo      = "T3";
               eaSerie     = #0#21;
               enCodigo    = #0#18;
               enLinea     = #0#8;
               eaArticulo  = #0#0;
               enAlmacen   = #0#1;
               enTarifa    = 1;
               enUnidades  = #0#4;
               enCodHisto   = #0#18;
               |SUB_EJECUTA_NP "dsz00002;CONSU";
          |FINSI;
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO Tipo5; |TIPO 5;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo [ 2; |Y nSwBarra = 1;
          |CONFI "0000NIntroduccion por Codigo de Barras";
          nSwBarra = FSalida;
          |ATRI S;
          |SI FSalida = 0;
               |PINTA 0533, "Introduccion por codigo de barras  ";
          |SINO;
               |PINTA 0533, "Introduccion por codigo de articulo";
          |FINSI;
          |ATRI 0;
     |FINSI;
     |HAZ SinBarra;
|FPRC;

|PROCESO GrabaElemento;
     |ABRE #914;
     #914#0 = eaEleme;
     |LEE 000#914=;
     |CIERRA #914;

     |ABRE #905;
     |DDEFECTO #905;
     #905#0 = "T3";
     #905#1 = #0#21;
     #905#2 = #0#18;
     #905#3 = #0#8;
     #905#4 = eaEleme;
     #905#5 = #914#1;
     #905#6 = #0#4;
     |GRABA 020#905n;
|FPRC;

|PROCESO Tipo44_13; |TIPO 13;
     |HAZ Tipo13_43;

     |DDEFECTO #1;
     |DDEFECTO #2;
     |ABRE #1;
     |ABRE #2;
     #1#0 = #0#0
     |LEE 000#1=;
     #2#0 = #0#0;
     #2#1 = #0#1;
     |LEE 000#2=;
     |CIERRA #2;
     |CIERRA #1;

     #0#3 = #2#39;  |PINTA #0#3;
     #0#19 = #2#5;  |PINTA #0#19;
     #0#6 = #1#6; |PINTA #0#6;
     #0#20 = #1#104;  |PINTA #0#20;

     #0#22 = #1#185; |PINTA #0#22;
     #0#23 = #1#193; |PINTA #0#23;
     #0#24 = #2#47;  |PINTA #0#24;
     #0#25 = #2#48;  |PINTA #0#25;

     |HAZ EsGesenvia;
     |SI FSalida = 0;
          |ATRI 0; |PINTA 1539, "³                                        ³";
          |ATRI R; |PINTA 1540, "Bien Produccion:";
          |ATRI I; |HAZ PinGesenvia;
          |ATRI 0;
     |FINSI;
|FPRC;

|PROCESO ImportaPDA44;
     |DDEF aAlfa; aAlfa = aAlfa + "dsm00044";
     |CARGA_DEF aAlfa, referen@;
     |SI FSalida ! 0; |ACABA; |FINSI;
     |ABRE #referen@;
     #referen@#21 = #43#4;
     #referen@#18 = #43#0;
     #referen@#8 = 99999;
     |LEE 000#referen@.];
     |SI FSalida = 0;
          |LEE 000#referen@.a;
     |SINO;
          |LEE 000#referen@.u;
     |FINSI;
     |SI FSalida = 0; |Y #referen@#21 = #43#4; |Y #referen@#18 = #43#0;
          nLin = #referen@#8 + 1;
     |SINO;
          nLin = 1;
     |FINSI;
     |CIERRA #referen@;
     |DESCARGA_DEF #referen@;

     |DDEF aAlfa; aAlfa = aAlfa + "psion067";
     |CARGA_DEF aAlfa, referen@;
     |SI FSalida ! 0; |ACABA; |FINSI;
     |NOME_DAT #referen@#eaFichero#;
     |ABRE #referen@;
     |LEE 000#referen@.p;
     |PARA; |SI FSalida = 0; |HACIENDO;
          #0#21 = #43#4;
          #0#18 = #43#0;
          #0#8 = nLin;
          |GRABA 020#0b;
          #0#0 = #referen@#0; ||Arti
          #0#2 = #referen@#1; ||Des
          #0#1 = #referen@#2; ||Alma
          #0#4 = #referen@#3; ||Uni
          #0#7 = #referen@#4; ||Ent/Sal
          #0#13 = #referen@#9; ||Parti
          FEntrada = 100;     ||simula alta
          |HAZ Tipo44_13;
          |HAZ Tipo2;
          |GRABA 020#0a;
          |LIBERA #0;
          nLin = nLin + 1;
          |LEE 000#referen@.s;
     |SIGUE;
     |CIERRA #referen@;
     |DELINDEX #referen@;
     |DESCARGA_DEF #referen@;
     |PONCONTROL 23, "SI"
     |PTEC 809;
     |PTEC 808;
|FPRC;

|PROCESO Alma44_66; |TIPO 66;
     |ABRE #3;
     #3#0 = #0#1;
     |LEE 000#3];
     |CONSULTA_CLAVE #1#3;
     |SI FSalida = 0;
          #0#1 = #3#0;
          |PINTA #0#1;
     |SINO;
          |ERROR;
     |FINSI;
     |CIERRA #3;
|FPRC;

|PROCESO AlmaConStock;
     |SI #2#39 > 0;
          #1#0 = #2#0;
          |LEE 000#1=;
          |SI FSalida = 0; |Y #1#137 = "S";
               |SALVA_DATOS #2;
               |REPON_DATOS #referen@;
               |GRABA 020#referen@.n;
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE AlmaConStock;
     #2#1;
     ;
     "                    ", enAlma3774;
     "zzzzzzzzzzzzzzzzzzzz", enAlma3774;
     ;
     NULL, AlmaConStock;
|FIN;

|PROCESO ConsuArti44; |TIPO 66;
     |SI enAlma3774 = 0;
          |ABRE #1;
          #1#0 = #0#0;
          |LEE 000#1];
          |CONSULTA_CLAVE #1#1;
          |SI FSalida = 0;
               #0#0 = #1#0; |PINTA #0#0;
          |SINO;
               |ERROR;
          |FINSI;
          |CIERRA #1;
     |SINO;
          |DDEF aAlfa;
          aAlfa = aAlfa + "agifa017";
          |CARGA_DEF aAlfa, referen@;
          |SI FSalida = 0;
               aAlfa = Usuario;
               |NOME_DAT #referen@#aAlfa#;
               |DELINDEX #referen@;
               |ABRE #1;
               |ABRE #referen@;
               |BUCLE AlmaConStock;
               |LEE 000#referen@.];
               #referen@#0 = #0#0;
               #referen@#1 = enAlma3774;
               |LEE 000#referen@.];
               |CONSULTA_CLAVE #1#referen@;
               |SI FSalida = 0;
                    #0#0 = #referen@#0;
               |SINO;
                    |ERROR;
               |FINSI;
               |CIERRA #referen@;
               |CIERRA #1;
               |DELINDEX #referen@;
               |DESCARGA_DEF #referen@;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Observa; |TIPO 7;
     |HAZ EsInforAlba;
     |SI FSalida = 0;
          |ABRE #1;
          #1#0 = #0#0;
          |LEE 000#1=;
          |SI FSalida = 0; |Y #1#174 = "S ";
               eaArticulo = #0#0;
               eaMatri = #0#13;
               |SUB_EJECUTA_NP "ialbz001;RE";
               #0#13 = eaMatri;
          |SINO;
               #0#13 = "";
          |FINSI;
          |CIERRA #1;
          |ATRI I; |PINTA 1165, #0#13; |ATRI 0;
     |FINSI;
     |HAZ EsGesisat;
     |SI FSalida = 0;
          |ABRE #1;
          #1#0 = #0#0;
          |LEE 000#1=;
          |SI FSalida = 0; |Y #1#174 = "S ";
               eaArticulo = #0#0;
               efFecha = #0#5;
               aAlfa = "gesiz019;RE" + #0#21 + (("000000"+#0#18)%-106) + #0#8;
               |SUB_EJECUTA_NP aAlfa;
               |SI eaMatri = ""; |ERROR; |FINSI;
          |SINO;
               |HAZ GesiBorMaqui;
          |FINSI;
          |CIERRA #1;
          aAlfa = #0#13; #0#13 = eaMatri;
          |ATRI I; |PINTA 1165, #0#13; |ATRI 0;
          #0#13 = aAlfa;
     |FINSI;
|FPRC;

|PROCESO Obs3; |TIPO 0;
     |HAZ EsGesenvia;
     |SI FSalida = 0; |Y #0#7 = "S";
          eaSerie = #0#21;
          enCodigo = #0#18;
          enLinea = #0#8;
          enAlmacen = #0#1;
          |SUB_EJECUTA_NP "gesem002";
          #0#1 = enAlmacen; |PINTA #0#1;
     |FINSI;
|FPRC;

|PROCESO PinGesenvia;
     |DDEF aAlfa; aAlfa = aAlfa + "gesem002";
     |CARGA_DEF aAlfa, referen@;
     |SI FSalida = 0;
          |ABRE #referen@;
          #referen@#0 = #0#21;
          #referen@#1 = #0#18;
          #referen@#2 = #0#8;
          |LEE 000#referen@.=;
          |SI FSalida ! 0; |DDEFECTO #referen@; |FINSI;
          |CIERRA #referen@;
          |PINTA 1557, #referen@#3;
          |PINTA 1564, #referen@#4;
          |DESCARGA_DEF #referen@;
     |FINSI;
|FPRC;

|PROCESO GesBajaPar;
     |ABRE #24;
     #24#0 = #0#0;
     #24#1 = #0#1;
     #24#2 = #0#13;
     |LEE 101#24=;
     |SI FSalida = 0; |Y #24#9 = 0; |Y #24#10 = 0; |Y #24#11 = 0;
               |Y #24#20 = 0; |Y #24#21 = 0; |Y #24#22 = 0;
               |Y #24#23 = 0; |Y #24#48 = 0; |Y #24#49 = 0;
               |Y #24#16 = 0; |Y #24#17 = 0; |Y #24#29 = 0;
               |Y #24#30 = 0; |Y #24#31 = 0; |Y #24#32 = 0;
               |Y #24#12 = 0; |Y #24#43 = 0; |Y #24#44 = 0;
          |CONFI "0000SDar de baja la partida generada S/N:";
          |SI FSalida = 0;
               |BORRA 020#24a;
          |FINSI;
     |FINSI;
     |CIERRA #24;
|FPRC;

|PROCESO Precio44; |TIPO 0;
     |SI FSalida = 2; |Y enSwGesAltaPar = 1;  || Modulo Gestral
          |ERROR; |ACABA;
     |FINSI;
     |HAZ Observa;
|FPRC;

|PROCESO NoCtrC; |TIPO 11;
     |SI FSalida = 7; |Y enSwGesAltaPar = 1;   || Modulo Gestral
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO GesiBorMaqui;
     |DDEF aAlfa;
     aAlfa = aAlfa + "gesim024";
     |CARGA_DEF aAlfa, referen@;
     |SI FSalida = 0;
          |ABRE #referen@;
          #referen@#0 = "RE";
          #referen@#1 = #0#21;
          #referen@#2 = #0#18;
          #referen@#3 = #0#8;
          |LEE 101#referen@.=;
          |SI FSalida = 0;
               |BORRA 020#referen@.a;
          |FINSI;
          |CIERRA #referen@;
          |DESCARGA_DEF #referen@;
     |FINSI;
|FPRC;

|PROCESO Arti44_0; |TIPO 0;
     |ABRE #1;
     #1#0 = #0#0;
     |LEE 000#1=;
     |SI FSalida ! 0; |DDEFECTO #1; |FINSI;
     |CIERRA #1;
     #0#0 = #1#133; |PINTA #0#0;
|FPRC;
