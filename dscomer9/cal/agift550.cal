|FICHEROS;
     agift550 #0;

     expor024@ #1;        ||Cliente a importar
     agifa024 #11;

     expor019@ #2;        ||Articulo a importar
     agifa019 #12;

     expor112@ #3;        ||Proveedor a importar
     agifa112 #13;

     expor025@ #4;        ||Comisionista a importar
     agifa025 #14;

     expor065@ #5;        ||Historico Movimientos.
     agifa065 #15;

     expor001@ #6;        ||Cod.Barras
     dsl00001 #16;

     dsm00030 #300;       || Tarifas a importar
     dsm00031 #301;       || Tarifas a importar
     agift555 #50;        || Control camino export/import
     dsm00032 #100;
     agifa324 #324;
     agifa321 #321;

     expord10@ #30;
     dsm00010 #40;

     expord06@ #31;
     dsm00006 #41;

     expord14@ #33;
     dsm00014 #43;

     expord15@ #35;
     dsm00015 #45;

     expord16@ #34;
     dsm00016 #44;

     expor023@ #103;
     agifa023 #203;

     ||컴컴컴컴컴컴컴컴컴컴횺arhuenda
     expor334@ #334;
     marhu334@ #434;
     expor335@ #335;
     marhu335@ #435;
     expor362@ #362;
     marhu362@ #462;
     expor363@ #363;
     marhu363@ #463;
     expor361@ #361;
     marhu361@ #461;
     ||컴컴컴컴컴컴컴컴컴컴

     dsw00011 #999; || Usado de temporal para el directorio
|FIN;

|VARIABLES;
     &eaNomDef = "";
     &enCamposDef = 0;
     &Tempo    = "";
     aTempo999 = "";
     nCampos   = 0;
     nSwMarhu  = 0;
     nSal      = 0;
     nHay      = 0;
     nHandle   = 0;
     aExt      = "";
     nLon      = 0;
     a20       = "                    ";
     b20       = "zzzzzzzzzzzzzzzzzzzz";
     aGz       = "";
     aOrg      = "";
     aParam    = "";
     nSwHisto  = 0;
     nSwGraba  = 0;
     nSwImpre  = 0;
     nLin      = 0;
     aHora     = "";

     &enCaja   = 0;
     &efFecha  = @;

     n024 = 0; n019 = 0; n112 = 0; n025 = 0; n001 = 0;
     nd10 = 0; nd06 = 0; nd14 = 0; nd16 = 0; nd15 = 0;
     ddx = "";
     fich = "";
     org = "";
     num = 0;
     bufer = 0;
     err = 0;
     fdes = "";
     des = "";
     fdef = "";
     def = "";
     exp = 0;
     nLinea = 0;

     desdec="      ";
     hastac="zzzzzz";
     desdea="                    ";
     hastaa="zzzzzzzzzzzzzzzzzzzz";
     desdep="    ";
     hastap="zzzz";
     desdecom="  ";
     hastacom="zz";
     i=0;
     sist="";
     mandato="";
     mandato1="";
     mensaje1= "0000ATENCION No puedo abrir fichero de control EXPORT/IMPORT";
     mensaje2= "0000ATENCION Debe informar del CONTROL CAMINO EXPORT/IMPORT";
     &path="";
     no="N";
     si="S";
     blanco="                         ";
     blanco1="                    ";
     nUltima = 0;
     aAlfa     = "";
     aAlf1     = "";
     aFic      = "";
     fFecha    = @;

     swErr    = 0;
     aMensa   = "";
     aPath    = "";
     aDef     = "";
     aDef1    = "";
     aDef2    = "";
     aDef3    = "";
     aDef4    = "";
     aDef5    = "";
     aDef6    = "";
     aDef7    = "";
     aDef8    = "";
     aDef9    = "";
     aDef10   = "";
     aDef11   = "";
     aDef12   = "";
     aDef13   = "";
     aDef14   = "";
     aDef15   = "";
     aDef16   = "";
     aDef17   = "";
     aMensaje = "";
|FIN;
컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
|PROCESO agifa023;
     aAlfa = "|NC";
     aAlfa = #103#aAlfa#;
     nCampos = aAlfa; nCampos = nCampos - 1;
     |PARA i = 0; |SI i [ nCampos; |HACIENDO i = i + 1;
          #203i = #103i;
     |SIGUE;
     |LEE 101#203=;
     |PARA i = 0; |SI i [ nCampos; |HACIENDO i = i + 1;
          #203i = #103i;
     |SIGUE;
     |SI FSalida = 0;
          |GRABA 020#203a;
          |LIBERA #203;
     |SINO;
          |GRABA 020#203n;
     |FINSI;
|FPRC;

|DEFBUCLE agifa023;
     #103#1002;
     ;
     "      ", "                    ";
     "zzzzzz", "zzzzzzzzzzzzzzzzzzzz";
     ;
     NULL, agifa023;
|FIN;
컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴Marhuenda
|PROCESO marhu335;
     aAlfa = "|NC";
     aAlfa = #435#aAlfa#;
     nCampos = aAlfa; nCampos = nCampos - 1;
     |PARA i = 0; |SI i [ nCampos; |HACIENDO i = i + 1;
          #435i = #335i;
     |SIGUE;
     |LEE 101#435=;
     |PARA i = 0; |SI i [ nCampos; |HACIENDO i = i + 1;
          #435i = #335i;
     |SIGUE;
     |SI FSalida = 0;
          |GRABA 020#435a;
          |LIBERA #435;
     |SINO;
          |GRABA 020#435n;
     |FINSI;
|FPRC;

|DEFBUCLE marhu335;
     #335#1003;
     ;
     #334#0, #334#1, 001;
     #334#0, #334#1, 999;
     ;
     NULL, marhu335;
|FIN;

|PROCESO marhu334;
     |DDEFECTO #434;
     #434#0 = #334#0;
     #434#1 = #334#1;
     |LEE 101#434=;
     |SI FSalida ! 0;
          aAlfa = "|NC";
          aAlfa = #434#aAlfa#;
          nCampos = aAlfa; nCampos = nCampos - 1;
          |PARA i = 0; |SI i [ nCampos; |HACIENDO i = i + 1;
               #434i = #334i;
          |SIGUE;
          |GRABA  020#434b;
     |FINSI;
     #434#5 = #334#5;
     #434#6 = #334#6;
     #434#8 = #334#8;
     |GRABA 020#434a;
     |LIBERA #434;
     |BUCLE marhu335;
|FPRC;

|DEFBUCLE marhu334;
     #334#1002;
     ;
     00001, 000001;
     99999, 999999;
     be;
     NULL, marhu334;
|FIN;

|PROCESO marhu361;
     aAlfa = "|NC";
     aAlfa = #461#aAlfa#;
     nCampos = aAlfa; nCampos = nCampos - 1;
     |PARA i = 0; |SI i [ nCampos; |HACIENDO i = i + 1;
          #461i = #361i;
     |SIGUE;
     |LEE 101#461=;
     |PARA i = 0; |SI i [ nCampos; |HACIENDO i = i + 1;
          #461i = #361i;
     |SIGUE;
     |SI FSalida = 0;
          |GRABA 020#461a;
          |LIBERA #461;
     |SINO;
          |GRABA 020#461n;
     |FINSI;
|FPRC;

|DEFBUCLE marhu361;
     #361#1001;
     ;
     "      ";
     "zzzzzz";
     ;
     NULL, marhu361;
|FIN;

|PROCESO marhu363;
     aAlfa = "|NC";
     aAlfa = #463#aAlfa#;
     nCampos = aAlfa; nCampos = nCampos - 1;
     |PARA i = 0; |SI i [ nCampos; |HACIENDO i = i + 1;
          #463i = #363i;
     |SIGUE;
     |LEE 101#463=;
     |PARA i = 0; |SI i [ nCampos; |HACIENDO i = i + 1;
          #463i = #363i;
     |SIGUE;
     |SI FSalida = 0;
          |GRABA 020#463a;
          |LIBERA #463;
     |SINO;
          |GRABA 020#463n;
     |FINSI;
|FPRC;

|DEFBUCLE marhu363;
     #363#1002;
     ;
     #362#0, "                    ";
     #362#0, "zzzzzzzzzzzzzzzzzzzz";
     ;
     NULL, marhu363;
|FIN;

|PROCESO marhu362;
     |DDEFECTO #462;
     #462#0 = #362#0;
     |LEE 101#462=;
     |SI FSalida ! 0;
          aAlfa = "|NC";
          aAlfa = #462#aAlfa#;
          nCampos = aAlfa; nCampos = nCampos - 1;
          |PARA i = 0; |SI i [ nCampos; |HACIENDO i = i + 1;
               #462i = #362i;
          |SIGUE;
          |GRABA  020#462b;
     |FINSI;
     |GRABA 020#462a;
     |LIBERA #462;
     |BUCLE marhu363;
|FPRC;

|DEFBUCLE marhu362;
     #362#1001;
     ;
     "      ";
     "zzzzzz";
     be;
     NULL, marhu362;
|FIN;
컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
|PROCESO dsm00014;
     |PARA i = 0; |SI i [ nd14; |HACIENDO i = i + 1;
          #43i = #33i;
     |SIGUE;
     |BORRA 000#43c;
     |PARA i = 0; |SI i [ nd14; |HACIENDO i = i + 1;
          #43i = #33i;
     |SIGUE;
     |GRABA 020#43n;
|FPRC;

|DEFBUCLE dsm00014;
     #33#1001;
     ;
     "                    ";
     "zzzzzzzzzzzzzzzzzzzz";
     ;
     NULL, dsm00014;
|FIN;

|PROCESO dsm00016;
     |PARA i = 0; |SI i [ nd16; |HACIENDO i = i + 1;
          #44i = #34i;
     |SIGUE;
     |BORRA 000#44c;
     |PARA i = 0; |SI i [ nd16; |HACIENDO i = i + 1;
          #44i = #34i;
     |SIGUE;
     |GRABA 020#44n;
|FPRC;

|DEFBUCLE dsm00016;
     #34#1005;
     ;
     a20,a20,a20,a20,a20;
     b20,b20,b20,b20,b20;
     ;
     NULL, dsm00016;
|FIN;

|PROCESO dsm00015;
     |PARA i = 0; |SI i [ nd15; |HACIENDO i = i + 1;
          #45i = #35i;
     |SIGUE;
     |BORRA 000#45c;
     |PARA i = 0; |SI i [ nd15; |HACIENDO i = i + 1;
          #45i = #35i;
     |SIGUE;
     |GRABA 020#45n;
|FPRC;

|DEFBUCLE dsm00015;
     #35#1004;
     ;
     a20,a20,a20,a20;
     b20,b20,b20,b20;
     ;
     NULL, dsm00015;
|FIN;

|PROCESO dsm00010;
     |ABRE #30;
     |LEE 000#30p;
     |CIERRA #30;

     |LEE 000#40p;
     |SI FSalida ! 0; |GRABA 020#40b; |FINSI;
     |PARA i = 0; |SI i [ nd10; |HACIENDO i = i + 1;
          #40i = #30i;
     |SIGUE;
     |GRABA 020#40a;
     |LIBERA #40;
|FPRC;

|PROCESO CodBarra;
     |PINTA 1863 , blanco1;
     |PINTA 1863 , #6#1;
     |DDEFECTO #16;
     |SELECT #2#16;
     #16#3 = #6#3;
     |LEE 000#16=;
     |SI FSalida ! 0;
          |SELECT #1#16;
          |LEE 000#16u;
          |SI FSalida = 0;
               nUltima = #16#0 + 1;
          |SINO;
               nUltima = 1;
          |FINSI;
          |PARA i = 1; |SI i [ n001; |HACIENDO i = i + 1;
              #16i = #6i;
          |SIGUE;
          #16#0 = nUltima;
          #16#4 = "S";
          #16#5 = #0#4;
          |GRABA 020#16n;
     |FINSI;
|FPRC;

|PROCESO miracli;
     |PINTA 1863 , blanco1;
     |PINTA 1863 , #1#0;
     |DDEFECTO #11;
     #11#0 = #1#0;    ||igualo el fichero de clientes
     |LEE 101#11=;  ||Miro si ya existe el cliente
     |SI FSalida ! 0; ||Si el cliente ya existe
          |GRABA 021#11b;
     |FINSI;
     |PARA i = 1; |SI i [ 44; |HACIENDO i = i + 1;
          #11i = #1i;
     |SIGUE;
     #11#47=#1#47;  ||Regimen equivalencia
     #11#52=#1#52;  ||Capacidad estimada de compra
     #11#53=#1#53;  ||Observaciones1
     |PARA i = 156; |SI i [ 174; |HACIENDO i = i + 1;
          #11i = #1i;
     |SIGUE;
     #11#175="S";
     #11#176=#0#4;
     |PARA i = 178; |SI i [ n024; |HACIENDO i = i + 1;
          #11i = #1i;
     |SIGUE;
     |GRABA 011#11a;
     |LIBERA #11;
|FPRC;

|PROCESO miraarti;
     |PINTA 1863 , blanco1;
     |PINTA 1863 , #2#0;
     |DDEFECTO #12;

     nSwGraba = 0;

     #12#0 = #2#0;
     |LEE 101#12=;
     |SI FSalida ! 0;
          |GRABA 021#12b;
          nSwGraba = 1;
     |FINSI;
     #12#1 = #2#1;
     #12#2 = #2#2;
     #12#3 = #2#3;
     #12#4 = #2#4;
     #12#5 = #2#5;

     #12#9 = #2#9;    ||P.C.M

     ||컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴Tarifas
     #301#3 = #12#0;
     |PDEFECTO #301, 6, 18;
     |SI #300#1 = "S";
          |SI #12#18 ! #2#18;
               #301#6 = #12#18;
               #301#12 = #2#18;
               #12#18 = #2#18;
               nSwGraba = 1;
          |FINSI;
     |FINSI;
     |SI #300#2 = "S";
          |SI #12#19 ! #2#19;
               #301#7 = #12#19;
               #301#13 = #2#19;
               #12#19 = #2#19;
               nSwGraba = 1;
          |FINSI;
     |FINSI;
     |SI #300#3 = "S";
          |SI #12#20 ! #2#20;
               #301#8 = #12#20;
               #301#14 = #2#20;
               #12#20 = #2#20;
               nSwGraba = 1;
          |FINSI;
     |FINSI;
     |SI #300#4 = "S";
          |SI #12#147 ! #2#147;
               #301#9 = #12#147;
               #301#15 = #2#147;
               #12#147 = #2#147;
               nSwGraba = 1;
          |FINSI;
     |FINSI;
     |SI #300#5 = "S";
          |SI #12#148 ! #2#148;
               #301#10 = #12#148;
               #301#16 = #2#148;
               #12#148 = #2#148;
               nSwGraba = 1;
          |FINSI;
     |FINSI;
     |SI #300#6 = "S";
          |SI #12#149 ! #2#149;
               #301#11 = #12#149;
               #301#17 = #2#149;
               #12#149 = #2#149;
               nSwGraba = 1;
          |FINSI;
     |FINSI;
     ||컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
     #12#21 = #2#21;
     #12#22 = #2#22;
     #12#23 = #2#23;
     #12#27 = #2#27;
     #12#28 = #2#28;
     #12#29 = #2#29;
     #12#30 = #2#30;       ||Tipo de iva
     #12#31 = #2#31;       ||%comision
     #12#102 = #2#102;     ||Peso por caja
     #12#103 = #2#103;     ||%royalti
     |PARA i = 105; |SI i [ 146; |HACIENDO i = i + 1;
          #12i = #2i;
     |SIGUE;
     |PARA i = 150; |SI i [ n019; |HACIENDO i = i + 1;
          #12i = #2i;
     |SIGUE;
     #12#123 = "S";
     #12#124 = #0#4;
     |GRABA 000#12a;
     |SI nSwGraba ! 0;
          nSwImpre = 1;
          |GRABA 000#301n;
     |FINSI;
     |LIBERA #12;
|FPRC;

|PROCESO miraprove;
     |PINTA 1863 , blanco1;
     |PINTA 1863 , #3#0;
     |DDEFECTO #13;
     #13#0 = #3#0;    ||igualo el fichero de proveedores
     |LEE 101#13=;  ||Miro si ya existe el proveedor
     |SI FSalida ! 0; ||Si el cliente ya existe
          |GRABA 021#13b;
     |FINSI;
     |PARA i = 1; |SI i [ 26; |HACIENDO i = i + 1;
          #13i = #3i;
     |SIGUE;
     #13#41=#3#41;  ||Dto acumulado
     #13#42=#3#42;  ||Codigo de grupo
     |PARA i = 57; |SI i [ n112; |HACIENDO i = i + 1;
          #13i = #3i;
     |SIGUE;
     #13#71="S";
     #13#72=#0#4;
     |GRABA 011#13a;
     |LIBERA #13;
|FPRC;

|PROCESO miracomis;
     |PINTA 1863 , blanco1;
     |PINTA 1863 , #4#0;
     |DDEFECTO #14;
     #14#0 = #4#0;    ||igualo el fichero de comision
     |LEE 101#14=;  ||Miro si ya existe la comision
     |SI FSalida ! 0;
          |GRABA 021#14b;
     |FINSI;
     |PARA i = 1; |SI i [ 10; |HACIENDO i = i + 1;
          #14i = #4i;
     |SIGUE;
     #14#39 = #4#39;  ||%IRPF
     #14#54 = "S";
     #14#55 = #0#4;
     |PARA i = 56; |SI i [ n025; |HACIENDO i = i + 1;
          #14i = #4i;
     |SIGUE;
     |GRABA 011#14a;
     |LIBERA #14;
|FPRC;

|PROCESO HistoEleme;
     |PARA i = 0; |SI i [ nd06; |HACIENDO i = i + 1;
          #41i = #31i;
     |SIGUE;
     |GRABA 000#41n;
|FPRC;

|DEFBUCLE HistoEleme;
     #31#1008;
     ;
     #15#0, #15#5, #15#1, #15#2, #15#11, #15#3, #15#4, "      ";
     #15#0, #15#5, #15#1, #15#2, #15#11, #15#3, #15#4, "zzzzzz";
     ;
     NULL, HistoEleme;
|FIN;

|PROCESO mirahis;
     |PINTA 1763 , blanco1;
     |PINTA 1763 , #5#0;
     |DDEFECTO #15;
     |PARA i = 0; |SI i [ 16; |HACIENDO i = i + 1;
          #15i = #5i;
     |SIGUE;
     #15#15 = "S";
     #15#16 = #0#4;
     |GRABA 000#15n;
     |BUCLE HistoEleme;
|FPRC;

|DEFBUCLE 0;        ||CLientes #1
#1#1001;            || Leo por la primera clave = codigo cliente, todos los
;                   || que haya
desdec;
hastac;
e;
NULL,miracli;
|FIN;

|DEFBUCLE 1;        ||Articulos #2
#2#1001;            || Seleccionando la clave codigo, todos.
;
desdea;
hastaa;
e;
NULL,miraarti;
|FIN;

|DEFBUCLE 2;        ||Proveedor #3
#3#1001;
;
desdep;
hastap;
e;
NULL,miraprove;
|FIN;

|DEFBUCLE 3;        ||Comisionista #4
#4#1001;
;
desdecom;
hastacom;
e;
NULL,miracomis;
|FIN;

|DEFBUCLE 4;        ||Historico
#5#1006;
;
"                    ", "01.01.1900", "  ", 000000, 00000, "     ";
"zzzzzzzzzzzzzzzzzzzz", "31.12.9999", "zz", 999999, 99999, "zzzzz";
e;
NULL,mirahis;
|FIN;

|DEFBUCLE CodBarra;
     #6#1001;
     ;
     0;
     9999999;
     e;
     NULL, CodBarra;
|FIN;
컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
|PROCESO UltimaLinea;
     #301#0 = #0#1;
     #301#1 = #0#4;
     #301#2 = 999;
     #301#3 = "";
     |LEE 000#301];
     |SI FSalida = 0;
          |LEE 000#301a;
     |SINO;
          |LEE 000#301u;
     |FINSI;
     |SI FSalida = 0; |Y #301#0 = #0#1; |Y #301#1 = #0#4;
          nLin = #301#2 + 1;
     |SINO;
          nLin = 1;
     |FINSI;
     |DDEFECTO #301;
|FPRC;
컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
|PROCESO BusCampos;
     |DFICE org; |QBF org;
     |DBASE des; |QBF des;
     |DDEF  def; |QBF def;
     eaNomDef = "agifa024"; |HAZ CamposDef; n024 = enCamposDef;
     eaNomDef = "agifa019"; |HAZ CamposDef; n019 = enCamposDef;
     eaNomDef = "agifa112"; |HAZ CamposDef; n112 = enCamposDef;
     eaNomDef = "agifa025"; |HAZ CamposDef; n025 = enCamposDef;
     eaNomDef = "dsl00001"; |HAZ CamposDef; n001 = enCamposDef;
     eaNomDef = "dsm00010"; |HAZ CamposDef; nd10 = enCamposDef;
     eaNomDef = "dsm00006"; |HAZ CamposDef; nd06 = enCamposDef;
     eaNomDef = "dsm00014"; |HAZ CamposDef; nd14 = enCamposDef;
     eaNomDef = "dsm00015"; |HAZ CamposDef; nd15 = enCamposDef;
     eaNomDef = "dsm00016"; |HAZ CamposDef; nd16 = enCamposDef;
|FPRC;
컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
|PROCESO Importa;
     ||Clientes
     |SI #0#5="S";     || Si he elegido pasar clientes

          |PATH_DAT #1 path;   || Camino marcado en la pantalla para clientes
          |ABRE #1;
          |SI FSalida=0;      ||Clientes
               |ABRE #11;
               |PINTA 1824,blanco;
               |PINTA 1824,"Clientes";
               |BUCLE 0;
               |LIBERA #11;
               |CIERRA #11;
          |FINSI;
          |CIERRA #1;

          |PATH_DAT #103 path;
          |ABRE #203;
          |PINTA 1824,blanco;
          |PINTA 1824,"Precio Cli/Art";
          |BUCLE agifa023;
          |CIERRA #203;
     |FINSI;

     ||Articulo
     |SI #0#6="S";
          |ABRE #300;
          #300#0 = #0#1;
          |LEE 000#300=;
          |CIERRA #300;

          |PATH_DAT #2 path;     || Camino de articulos
          |ABRE #2;
          |SI FSalida=0;
               |PINTA 1824,blanco;
               |PINTA 1824,"Articulos";
               |HORA aHora;
               |ABRE #12;
               |ABRE #301;
               |HAZ UltimaLinea;
               #301#0 = #0#1;       || Solo los cargo una vez
               #301#1 = #0#4;
               #301#2 = nLin;
               #301#4 = aHora;
               #301#5 = Usuario % 810;
               |BUCLE 1;
               |CIERRA #12;
               |CIERRA #301;
          |FINSI;
          |CIERRA #2;
     |FINSI;

     ||Proveedores
     |SI #0#7="S";

          |PATH_DAT #3 path;     || Camino de Proveedores

          |ABRE #3;
          |SI FSalida=0;
               |ABRE #13;
               |PINTA 1824,blanco;
               |PINTA 1824,"Proveedores";

               |BUCLE 2;
               |LIBERA #13;
               |CIERRA #13;
          |FINSI;
          |CIERRA #3;
     |FINSI;

     ||Comisionistas
     |SI #0#8="S";

          |PATH_DAT #4 path;     || Camino de Comisionistas
          |ABRE #4;
          |SI FSalida=0;
               |ABRE #14;
               |PINTA 1824,blanco;
               |PINTA 1824,"Comisionistas";

               |BUCLE 3;
               |LIBERA #14;
               |CIERRA #14;
          |FINSI;
          |CIERRA #4;
     |FINSI;
     ||Cod.Barras
     |SI #0#10="S";

          |PATH_DAT #6 path;
          |ABRE #6;
          |SI FSalida=0;
               |ABRE #16;
               |PINTA 1824,blanco;
               |PINTA 1824,"Cod. Barras";

               |BUCLE CodBarra;
               |LIBERA #16;
               |CIERRA #16;
          |FINSI;
          |CIERRA #6;
     |FINSI;
     |SI #0#9="S";
          |PATH_DAT #5 path;     || Camino de Historico
          |PATH_DAT #31 path;     || Camino de Historico
          |ABRE #41;
          |ABRE #15;
          |PINTA 1824,blanco;
          |PINTA 1824,"Historico    ";
          |BUCLE 4;
          |CIERRA #41;
          |CIERRA #15;
          nSwHisto = 1;
     |FINSI;
     |SI #0#11 = "S";
          |PINTA 1824,blanco;
          |PINTA 1824,"Div.Vertical";
          |PATH_DAT #30 path;
          |PATH_DAT #31 path;
          |PATH_DAT #33 path;
          |PATH_DAT #34 path;
          |PATH_DAT #35 path;
          |ABRE #40;
          |ABRE #43;
          |ABRE #44;
          |ABRE #45;
          |HAZ dsm00010;
          |BUCLE dsm00014;
          |BUCLE dsm00016;
          |BUCLE dsm00015;
          |CIERRA #40;
          |CIERRA #41;
          |CIERRA #44;
          |CIERRA #45;
     |FINSI;
     |SI nSwMarhu = 1;
          |PATH_DAT #335 path;
          |PATH_DAT #334 path;
          |ABRE #434;
          |ABRE #435;
          |PINTA 1824,blanco;
          |PINTA 1824,"Pedidos Reposicion";
          |BUCLE marhu334;
          |CIERRA #434;
          |CIERRA #435;

          |PATH_DAT #363 path;
          |PATH_DAT #362 path;
          |ABRE #462;
          |ABRE #463;
          |PINTA 1824,blanco;
          |PINTA 1824,"Tarifas Articulos";
          |BUCLE marhu362;
          |CIERRA #463;
          |CIERRA #462;

          |PATH_DAT #361 path;
          |ABRE #461;
          |BUCLE marhu361;
          |CIERRA #461;
     |FINSI;
|FPRC;
컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
|PROCESO ActHisto;
     |ABRE #100;
     |SELECT #1#100;
     #100#0 = "I";
     #100#1 = fFecha;
     #100#2 = 999;
     |LEE 000#100];
     |SI FSalida = 0;
          |LEE 000#100a;
     |SINO;
          |LEE 000#100u;
     |FINSI;
     |SI FSalida = 0; |Y #100#0 = "I"; |Y #100#1 = fFecha;
          nLin = #100#2 + 1;
     |SINO;
          nLin = 1;
     |FINSI;
     |HORA aAlfa;
     |DDEFECTO #100;
     #100#0 = "I";
     #100#1 = fFecha;
     #100#2 = nLin;
     #100#3 = aAlfa;
     #100#4 = Usuario % 810;
     #100#5 = aFic % -115;
     |GRABA 020#100n;
     |CIERRA #100;
|FPRC;

|PROCESO BorraExpor;
     aFic = path;
     |_PDIR aFic;
     |PARA; |SI FSalida = 0; |HACIENDO;
          aAlfa = aFic % -103;
          |SI aAlfa = "ddx"; |O aAlfa = "ixx"; |O aAlfa = "dat";
               aAlfa = aFic % -0805;
               |SI aAlfa = "expor"; |FBORRA aFic; |FINSI;
               aAlfa = aFic % -0507;
               |SI aAlfa = "testigo"; |FBORRA aFic; |FINSI;
          |FINSI;
          |_SDIR aFic;
     |SIGUE;
|FPRC;

|PROCESO DesComprime;
     ||---------Chequeo del fichero (debe se todo con numeros)
     ||         DETAR se cuelga si el fichero no es un tar
     err = 0;
     |QBF aFic;
     aAlfa = aFic % -115;
     |PARA i = 101; |SI i [ 1500; |HACIENDO i = i + 100;
          aAlf1 = (aAlfa % i);
          |SI aAlf1 < "0"; |O aAlf1 > "9";
               |SI i = 1201; |Y aAlf1 = ".";
               |SINO;
                    err = 1;
                    |SAL;
               |FINSI;
          |FINSI;
     |SIGUE;
     ||----------
     |SI err = 0;
          aAlfa = aFic % A0;
          nLon = aAlfa;
          nLon = nLon + 100 - 4;
          aOrg = aFic % nLon;
          aExt = aFic % -104;
          aGz  = aOrg + ".gz";

          |RENOMBRA_FICHERO aFic, aGz;
          |DESCOMPRIME aGz;
          |RENOMBRA_FICHERO aOrg, aFic;

          |XABRE "B", aFic, nHandle;
          |XLEE nHandle, 1, aAlfa;
          nHay = FSalida;
          |XCIERRA nHandle;

          |SI nHay > 0;
               |DETAR aFic, path;
               nSal = FSalida;
               |FBORRA aFic;
               |RENOMBRA_FICHERO aGz, aFic;
          |FINSI;

          |SI nSal = 0;
               err = 0;
          |SINO;
               err = 1;
               aAlfa = "0000Error en... [" + aFic + "]";
               |MENAV aAlfa;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO BuscaFichero;
     |HAZ BorraExpor;         || -> por si existe
     err = 1;
     |ABRE #100;
     |SELECT #2#100;
     aFic = path;
     |_PDIR aFic;
     |PARA; |SI FSalida = 0; |HACIENDO;
          aAlfa = aFic % -115;
          |SELECT #2#100;
          #100#0 = "I";
          #100#5 = aAlfa;
          |LEE 000#100=;
          |SI FSalida ! 0;
               #999#4 = aAlfa;
               aAlfa = (aAlfa % 0502) + (aAlfa % 0302) + (aAlfa % 0102) + (aAlfa % -109);
               #999#1 = aAlfa;
               |GRABA 020#999n;
          |FINSI;
          |_SDIR aFic;
     |SIGUE;
     |CIERRA #100;
     ||컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
     |LEE 000#999p;
     |PARA; |SI FSalida = 0; |HACIENDO;
          aFic = path + #999#4;
          |HAZ DesComprime;
          |SI err = 0;
               |HAZ Importa;
               |HAZ ActHisto;
          |FINSI;
          |LEE 000#999s;
     |SIGUE;
     |HAZ BorraExpor;
|FPRC;

|PROCESO inicio;|TIPO 3;

     |HAZ BusCampos;
     |SI err ! 0;
          |MENAV "0000Proceso Anulado...";
          |ACABA;
     |FINSI;

     |ABRE #50; |LEE 001#50p; |CIERRA #50;
     path = #50#1; |QBF path;
     |SI path = "";
          |MENAV mensaje2;
          |ERROR;
          |ACABA;
     |FINSI;

     |HAZ  CargaDef;
     |SI swErr ! 0;
          aMensa = "0000Error al cargar:" + aDef;
          |MENAV aMensa;
          |HAZ DesCargaDef;
          |ACABA;
     |FINSI;
     |SI #0#12 = "S";
           |CONFI "0000SRecoger Ficheros de central ahora: ";
           |SI FSalida = 0;
               |MENAV "0000Atencion!! Debe estar conectado a internet";
               aMensaje = "dsm00206;tpv"
               |SUB_EJECUTA aMensaje;
           |FINSI;
     |FINSI;
     |DELINDEX #999;
     |ABRE #999;
     |HAZ BuscaFichero;
     |CIERRA #999;

     |HAZ DesCargaDef;

     |SI nSwImpre ! 0;
          |SI #300#7 = "S"; |O #300#8 = "S"; |O #300#9 = "S";
                    |O #300#10 = "S"; |O #300#11 = "S"; |O #300#12 = "S";
               |SUB_EJECUTA_NP "dsy00005";
          |FINSI;
     |FINSI;
     |SI nSwHisto ! 0;
          |CONFI "    NRecalcular stocks a continuacion (S/N): ";
          |SI FSalida=0;
              |SUB_EJECUTA_NP "agifa068.tab";
          |FINSI;
     |FINSI;
|FPRC;
컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
|PROCESO Marhuenda;
     aAlfa = ""; |DBIN aAlfa;
     aAlfa = aAlfa + "marhu334.tab";
     |XABRE "E", aAlfa, 0;
     |SI FSalida = 0
          |DBASE aPath;
          aAlfa = aPath + "def/marhu334";
          |CARGA_DEF aAlfa, marhu334@;
          |SI FSalida = 0;
               aAlfa = aPath + "def/marhu335";
               |CARGA_DEF aAlfa, marhu335@;
               |SI FSalida = 0;
                    nSwMarhu = 1;
               |SINO;
                    aAlfa = "0000Error al cargar " + aAlfa;
                    |MENAV aAlfa;
               |FINSI;
          |SINO;
               aAlfa = "0000Error al cargar " + aAlfa;
               |MENAV aAlfa;
          |FINSI;
          |SI nSwMarhu = 1;
               aAlfa = aPath + "def/marhu362";
               |CARGA_DEF aAlfa, marhu362@;
               |SI FSalida = 0;
                    aAlfa = aPath + "def/marhu363";
                    |CARGA_DEF aAlfa, marhu363@;
                    |SI FSalida = 0;
                         nSwMarhu = 1;
                    |SINO;
                         aAlfa = "0000Error al cargar " + aAlfa;
                         |MENAV aAlfa;
                    |FINSI;
               |SINO;
                    aAlfa = "0000Error al cargar " + aAlfa;
                    |MENAV aAlfa;
               |FINSI;
          |FINSI;
          |SI nSwMarhu = 1;
               aAlfa = aPath + "def/marhu361";
               |CARGA_DEF aAlfa, marhu361@;
               |SI FSalida = 0;
                    nSwMarhu = 1;
               |SINO;
                    aAlfa = "0000Error al cargar " + aAlfa;
                    |MENAV aAlfa;
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|PROGRAMA;
     |HAZ Marhuenda;

     |HAZ CreaTempo; aTempo999 = Tempo;
     |NOME_DAT #999 aTempo999; |DELINDEX #999;

     aParam = PARAMETRO; |QBF aParam;

     |ABRE #0;
     |LEE 101#0p;
     |SI FSalida ! 0; |GRABA 020#0b; |FINSI;

     #0#4 = ~;
     fFecha = ~;

     |SI enCaja ! 0;  |Y aParam = "";        || Viene del agifa507
          #0#1 = enCaja;
          #0#4 = efFecha;
          |HAZ inicio;
     |SINO;
          |SI aParam = "";
               |CLS;
               |CABEZA "Importacion";
               |PINPA #0#0;
               |PINDA #0#0;
               |ENDATOS #1#0;
               |SI FSalida = 0;
                    |GRABA 020#0a;
               |FINSI;
          |SINO;
               #0#1 = enCaja;
               #0#4 = fFecha;
               |ABRE #50; |LEE 001#50p; |CIERRA #50;
               path = #50#1; |QBF path;
               |HAZ BusCampos;
               |SI err ! 0;
                    |MENAV "0000Proceso Anulado...";
                    |DELINDEX #999; Tempo = aTempo999; |HAZ BoraTempo;
                    |ACABA;
               |FINSI;
               |HAZ  CargaDef;
               |SI swErr ! 0;
                    aMensa = "0000Error al cargar:" + aDef;
                    |MENAV aMensa;
                    |HAZ DesCargaDef;
                    |DELINDEX #999; Tempo = aTempo999; |HAZ BoraTempo;
                    |ACABA;
               |FINSI;
               aFic = path + aParam;
               |HAZ DesComprime;
               |SI err = 0;
                    |HAZ Importa;
                    |HAZ ActHisto;
               |FINSI;
               |HAZ DesCargaDef;
          |FINSI;
     |FINSI;
     |CIERRA #0;
     |DELINDEX #999; Tempo = aTempo999; |HAZ BoraTempo;
     |SI nSwMarhu = 1;
          |DESCARGA_DEF #marhu361@;
          |DESCARGA_DEF #marhu363@;
          |DESCARGA_DEF #marhu362@;
          |DESCARGA_DEF #marhu335@;
          |DESCARGA_DEF #marhu334@;
     |FINSI;
|FPRO;

|PROCESO CargaDef;
     |DBASE aPath;

     aDef1 = aPath + "def/" + "agifa024";
     aDef  = aDef1;
     |CARGA_DEF aDef, expor024@;
     |SI FSalida ! 0;
          swErr = 1;
          aDef1 = "";
          |ACABA;
     |FINSI;
     aDef2 = aPath + "def/" + "agifa019";
     aDef  = aDef2;
     |CARGA_DEF aDef, expor019@;
     |SI FSalida ! 0;
          swErr = 1;
          aDef2 = "";
          |ACABA;
     |FINSI;
     aDef3 = aPath + "def/" + "agifa112";
     aDef  = aDef3;
     |CARGA_DEF aDef, expor112@;
     |SI FSalida ! 0;
          swErr = 1;
          aDef3 = "";
          |ACABA;
     |FINSI;
     aDef4 = aPath + "def/" + "agifa025";
     aDef  = aDef4;
     |CARGA_DEF aDef, expor025@;
     |SI FSalida ! 0;
          swErr = 1;
          aDef4 = "";
          |ACABA;
     |FINSI;
     aDef5 = aPath + "def/" + "agifa065";
     aDef  = aDef5;
     |CARGA_DEF aDef, expor065@;
     |SI FSalida ! 0;
          swErr = 1;
          aDef5 = "";
          |ACABA;
     |FINSI;
     aDef6 = aPath + "def/" + "dsl00001";
     aDef  = aDef6;
     |CARGA_DEF aDef, expor001@;
     |SI FSalida ! 0;
          swErr = 1;
          aDef6 = "";
          |ACABA;
     |FINSI;
     aDef7 = aPath + "def/" + "dsm00010";
     aDef  = aDef7;
     |CARGA_DEF aDef, expord10@;
     |SI FSalida ! 0;
          swErr = 1;
          aDef7 = "";
          |ACABA;
     |FINSI;
     aDef8 = aPath + "def/" + "dsm00006";
     aDef  = aDef8;
     |CARGA_DEF aDef, expord06@;
     |SI FSalida ! 0;
          swErr = 1;
          aDef8 = "";
          |ACABA;
     |FINSI;
     aDef9 = aPath + "def/" + "dsm00014";
     aDef  = aDef9;
     |CARGA_DEF aDef, expord14@;
     |SI FSalida ! 0;
          swErr = 1;
          aDef9 = "";
          |ACABA;
     |FINSI;
     aDef10 = aPath + "def/" + "dsm00016";
     aDef  = aDef10;
     |CARGA_DEF aDef, expord16@;
     |SI FSalida ! 0;
          swErr = 1;
          aDef10 = "";
          |ACABA;
     |FINSI;
     aDef11 = aPath + "def/" + "dsm00015";
     aDef  = aDef11;
     |CARGA_DEF aDef, expord15@;
     |SI FSalida ! 0;
          swErr = 1;
          aDef11 = "";
          |ACABA;
     |FINSI;
     aDef14 = aPath + "def/" + "agifa023";
     aDef  = aDef14;
     |CARGA_DEF aDef, expor023@;
     |SI FSalida ! 0;
          swErr = 1;
          aDef14 = "";
          |ACABA;
     |FINSI;
     |SI nSwMarhu = 1;
          aDef12 = aPath + "def/" + "marhu334";
          aDef  = aDef12;
          |CARGA_DEF aDef, expor334@;
          |SI FSalida ! 0;
               swErr = 1;
               aDef12 = "";
               |ACABA;
          |FINSI;
          aDef13 = aPath + "def/" + "marhu335";
          aDef  = aDef13;
          |CARGA_DEF aDef, expor335@;
          |SI FSalida ! 0;
               swErr = 1;
               aDef13 = "";
               |ACABA;
          |FINSI;
          aDef15 = aPath + "def/" + "marhu362";
          aDef  = aDef15;
          |CARGA_DEF aDef, expor362@;
          |SI FSalida ! 0;
               swErr = 1;
               aDef15 = "";
               |ACABA;
          |FINSI;
          aDef16 = aPath + "def/" + "marhu363";
          aDef  = aDef16;
          |CARGA_DEF aDef, expor363@;
          |SI FSalida ! 0;
               swErr = 1;
               aDef16 = "";
               |ACABA;
          |FINSI;
          aDef17 = aPath + "def/" + "marhu361";
          aDef  = aDef17;
          |CARGA_DEF aDef, expor361@;
          |SI FSalida ! 0;
               swErr = 1;
               aDef17 = "";
               |ACABA;
          |FINSI;
     |FINSI;
     |HAZ NomeDat;
|FPRC;

|PROCESO DesCargaDef;
     |SI nSwMarhu = 1;
          |SI aDef17 ! ""; |DESCARGA_DEF #expor361@; |FINSI;
          |SI aDef16 ! ""; |DESCARGA_DEF #expor363@; |FINSI;
          |SI aDef15 ! ""; |DESCARGA_DEF #expor362@; |FINSI;
          |SI aDef13 ! ""; |DESCARGA_DEF #expor335@; |FINSI;
          |SI aDef12 ! ""; |DESCARGA_DEF #expor334@; |FINSI;
     |FINSI;
     |SI aDef14 ! ""; |DESCARGA_DEF #expor023@; |FINSI;
     |SI aDef11 ! ""; |DESCARGA_DEF #expord15@; |FINSI;
     |SI aDef10 ! ""; |DESCARGA_DEF #expord16@; |FINSI;
     |SI aDef9  ! ""; |DESCARGA_DEF #expord14@; |FINSI;
     |SI aDef8  ! ""; |DESCARGA_DEF #expord06@; |FINSI;
     |SI aDef7  ! ""; |DESCARGA_DEF #expord10@; |FINSI;
     |SI aDef6  ! ""; |DESCARGA_DEF #expor001@; |FINSI;
     |SI aDef5  ! ""; |DESCARGA_DEF #expor065@; |FINSI;
     |SI aDef4  ! ""; |DESCARGA_DEF #expor025@; |FINSI;
     |SI aDef3  ! ""; |DESCARGA_DEF #expor112@; |FINSI;
     |SI aDef2  ! ""; |DESCARGA_DEF #expor019@; |FINSI;
     |SI aDef1  ! ""; |DESCARGA_DEF #expor024@; |FINSI;
|FPRC;

|PROCESO NomeDat;
     |SI nSwMarhu = 1;
          |NOME_DAT #361 "expor361";
          |NOME_DAT #363 "expor363";
          |NOME_DAT #362 "expor362";
          |NOME_DAT #335 "expor335";
          |NOME_DAT #334 "expor334";
     |FINSI;
     |NOME_DAT #103 "expor023";
     |NOME_DAT #35 "expord15";  || 11
     |NOME_DAT #34 "expord16";  || 10
     |NOME_DAT #33 "expord14";  || 9
     |NOME_DAT #31 "expord06";  || 8
     |NOME_DAT #30 "expord10";  || 7
     |NOME_DAT #6  "expor001";  || 6
     |NOME_DAT #5  "expor065";  || 5
     |NOME_DAT #4  "expor025";  || 4
     |NOME_DAT #3  "expor112";  || 3
     |NOME_DAT #2  "expor019";  || 2
     |NOME_DAT #1  "expor024";  || 1
|FPRC;
