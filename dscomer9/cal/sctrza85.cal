|FICHEROS;
     sctrm003 #3;
     sctrm008 #8;
     sctrm007 #7;
     sctrw013 #13
     sctrm012 #12;
     sctrw014 #14;
     sctrm017 #17;
     sctrm018 #18;
     sctrm019 #19;
     sctrm020 #20;
     sctrm024 #24;
     sctrm026 #26;
     sctrm028 #28;
     sctrm029 #29;
     sctrm030 #30;
     sctrm031 #31;
     sctrm036 #36;
     sctrm038 #38;
     sctrm046 #46;
     sctrm052 #52;
     sctrm050 #50;
     sctrm053 #53;
     sctrm068 #68;
     sctrm069 #69;
     sctrm070 #70;
     sctrm001 #100;
     referen@;
|FIN;

|VARIABLES;
     &enHay = 0;
     &Tempo = "";
     &enSwVuelta = 0;  || Org -> Des 0 , Des->Org 1
     &enLinea = 0;
     &eaSentido = "";
     &efFechaViaje = @;
     &eaBloqueado = "";
     &enTotBillete = "";
     &enDtoAuto = 0;
     &enIncBillete = 0;
     &enPax = 0;
     aTipoAct = "";
     nForma = 0;
     aTmp14 = "";
     nLin = 0;
     nImpDtoAuto = 0;
     aSwAplicaSuple = "";
     nSwSuple = 0;
     nOk = 0;
     nImpoV = 0;
     nImpoS = 0;
     aDiaV = "";
     aDiaS = "";
     nSwVuelta = "";
     aTemporada = "";
     aAlfa = "";
     nConta = 0;
     nHay = 0;
     aDias = "";
     aTmp13 = "";
     aTmp13b = "";
     i = 0;
     j = 0;
     fFecha = @;
     fDesde = @;
     fHasta = @;
     aDiaSemanaN = "";
     aDiaSemanaL = "";
     nPos = 0;
     aDia = "";
     aLon = "";
     nNume = 0;
     nHora = 0;
     nMinu = 0;
     aHora = "";
     aMinu = "";
     aPobDes = "";
     aPobOrg = "";
     nSwTempoFRese = 0;
|FIN;

|PROCESO CtrlViajerosAct;
     |DDEFECTO #69;
     |ABRE #69;
     #69#0 = enLinea;
     #69#1 = eaSentido;
     #69#2 = efFechaViaje;
     |LEE 101#69=;
     |SI FSalida ! 0; |GRABA  020#69b; |FINSI;
     #69#4 = #69#4 + enPax;
     |GRABA 020#69a;
     |CIERRA #69;
|FPRC;

|PROCESO Actualiza;
     nForma = 1;

     |SI #52#6 = 1; |O #52#6 = 2;
          #52#56 = #100#30;
          #52#57 = #100#31;
     |FINSI;

     |ABRE #50;
     |ABRE #26;
     #26#0 = #52#96;
     #26#2 = #52#75;
     #26#4 = #52#101;
     #26#5 = #52#22;
     nNume = #52#20 - #52#99 + 1;
     #26#7 = nNume;
     |LEE 121#26=;
     |SI FSalida = 0;
          |SI #52#52 = "L";
               #26#38 = #26#38 + (#52#19 * nForma);
          |SINO;
               #26#26 = #26#26 + (#52#19 * nForma);
          |FINSI;
          |SI #26#26 ] #26#20;
               #26#24 = "C";
          |SINO;
               #26#24 = "A";
          |FINSI;
          |GRABA 020#26a;
          |SI #26#27 ! 0; |Y #52#74 = 0; |Y #52#128 = "S";
               #50#0 = #26#7;
               #50#1 = #26#27;
               |LEE 101#50=;
               |SI FSalida = 0;
                    #50#20 = #50#20 + (#52#19 * nForma);
                    |GRABA 020#50a;
                    |LIBERA #50;

                    |SI nForma = -1; |Y #52#52 = "A";
                         #52#73 = "01.01.0000";
                         #52#74 = 0;
                         #52#77 = 0;
                         #52#103 = 0;
                         #52#122 = 0;
                    |SINO;
                         |SI #52#52 ! "L";
                              #52#73 = #50#0;
                              #52#74 = #50#1;
                              #52#77 = #50#2;
                              |SI #52#103 = 0; |Y nForma = 1;
                                   || eaAlfa = "Intercambiador ida";
                                   || |HAZ PideIntercambiador;
                                   || #52#103 = enIntercambiador;
                              |FINSI;
                         |FINSI;
                    |FINSI;
               |FINSI;
          |FINSI;
     |FINSI;
     |SI #52#17 > 1; |Y #52#26 ! "01.01.0000";
          #26#0 = #52#105;
          #26#2 = #52#75;
          #26#4 = #52#102;
          #26#5 = #52#28; || #52#22;
          nNume = #52#26 - #52#100 + 1;
          #26#7 = nNume;
          |LEE 121#26=;
          |SI FSalida = 0;
               |SI #52#52 = "L";
                    #26#38 = #26#38 + (#52#19 * nForma);
               |SINO;
                    #26#26 = #26#26 + (#52#19 * nForma);
               |FINSI;
               |SI #26#26 ] #26#20;
                    #26#24 = "C";
               |SINO;
                    #26#24 = "A";
               |FINSI;
               |GRABA 020#26a;
               |SI #26#27 ! 0; |Y #52#79 = 0; |Y #52#129 = "S";
                    #50#0 = #26#7;
                    #50#1 = #26#27;
                    |LEE 101#50=;
                    |SI FSalida = 0;
                         #50#20 = #50#20 + (#52#19 * nForma);
                         |GRABA 020#50a;
                         |LIBERA #50;

                         |SI nForma = -1; |Y #52#53 = "A";
                              #52#78 = "01.01.0000";
                              #52#79 = 0;
                              #52#82 = 0;
                              #52#104 = 0;
                              #52#123 = 0;
                         |SINO;
                              |SI #52#53 ! "L";
                                   #52#78 = #50#0;
                                   #52#79 = #50#1;
                                   #52#82 = #50#2;
                                   |SI #52#104 = 0; |Y nForma = 1;
                                        || eaAlfa = "Intercambiador vuelta";
                                        || |HAZ PideIntercambiador;
                                        || #52#104 = enIntercambiador;
                                   |FINSI;
                              |FINSI;
                         |FINSI;
                    |FINSI;
               |FINSI;
          |FINSI;
     |FINSI;
     |CIERRA #26;
     |CIERRA #50;

     |ABRE #36;
     #36#1 = #52#43;
     |LEE 000#36=;
     |SI FSalida ! 0; |DDEFECTO #36; |FINSI;
     |CIERRA #36;
|FPRC;

|PROCESO Comision;
     #52#55 = 0;
     #52#121 = 0;
     |SI #52#6 ] 2; |O #46#21 = "S";
          |ABRE #38;
          #38#0 = #52#43;
          #38#2 = #52#75;
          |LEE 000#38=;
          |SI FSalida = 0;
               #52#55 = #38#4;
          |SINO;
               |ABRE #12;
               #12#0 = #52#75;
               |LEE 000#12=;
               |SI FSalida = 0;
                    #52#55 = #12#9;
               |FINSI;
               |CIERRA #12;
          |FINSI;
          |CIERRA #38;
     |FINSI;

     |ABRE #12;
     #12#0 = #52#75;
     |LEE 000#12=;
     |SI FSalida = 0;
          #52#121 = #12#20;
     |FINSI;
     |CIERRA #12;
|FPRC;

|PROCESO sctrm070;
     |SI #70#1 = "R"; |O #70#1 = "T";
          |SI #52#6 = #70#2; |O #70#2 = 9;
               |SI #52#1 ] #70#3; |Y #52#1 [ #70#4;
                    |SI #52#20 ] #70#5; |Y #52#20 [ #70#6;
                         enDtoAuto = #70#7;
                    |FINSI;
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE sctrm070;
     #70#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, sctrm070;
|FIN;

|PROCESO DtoAuto;
     enDtoAuto = 0;
     |BUCLE sctrm070;
|FPRC;

|PROCESO sctrm068;
     |SI #68#2 ! 0;
          |SI #68#2 ! #52#75; |ACABA; |FINSI;                    || Linea
          |SI #52#22 < #68#3; |O #52#22 > #68#4; |ACABA; |FINSI; || Servicio
     |FINSI;
     |SI #52#1 < #68#5; |O #52#1 > #68#7;; |ACABA; |FINSI;       || F.Rese
     |SI #52#2 < #68#6; |O #52#2 > #68#8;; |ACABA; |FINSI;       || H.Rese

     enIncBillete = enIncBillete + ((enTotBillete * #68#10 / 100)!0);
|FPRC;

|DEFBUCLE sctrm068;
     #68#2;
     11;
     #52#20, #52#96, 0000000, #52#101;
     #52#20, #52#96, 9999999, #52#101;
     ;
     NULL, sctrm068;
|FIN;

|PROCESO IncDisBillete;
     enIncBillete = 0;
     |BUCLE sctrm068;
|FPRC;

|PROCESO BuscaTempo;
     |SI nSwTempoFRese = 1;
          |SI #52#1 ] #8#2; |Y #52#1 [ #8#3;
               aTemporada = #8#0;
               |ERROR10;
          |FINSI;
     |SINO;
          |SI nSwVuelta = 0;
               |SI #52#20 ] #8#2; |Y #52#20 [ #8#3;
                    aTemporada = #8#0;
                    |ERROR10;
               |FINSI;
          |SINO;
               |SI #52#26 ] #8#2; |Y #52#26 [ #8#3;
                    aTemporada = #8#0;
                    |ERROR10;
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE BuscaTempo;
     #8#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, BuscaTempo;
|FIN;

|PROCESO CtrlViajerosCheq;
     |ABRE #69;
     #69#0 = enLinea;
     #69#1 = eaSentido;
     #69#2 = efFechaViaje;
     |LEE 000#69=;
     |SI FSalida ! 0; |DDEFECTO #69; |FINSI;
     |CIERRA #69;
     enPax = #69#3 - #69#4;
     eaBloqueado = #69#5;
|FPRC;

|PROCESO DiaSemana;
     |SI fFecha = "01.01.0000";
          aDiaSemanaN = ""; aDiaSemanaL = "";
     |SINO;
          fFecha = fFecha % A1; aAlfa = fFecha % 1102;
          |SI aAlfa = "Lu"; aDiaSemanaN = "1"; aDiaSemanaL = "L"; |FINSI;
          |SI aAlfa = "Ma"; aDiaSemanaN = "2"; aDiaSemanaL = "M"; |FINSI;
          |SI aAlfa = "Mi"; aDiaSemanaN = "3"; aDiaSemanaL = "X"; |FINSI;
          |SI aAlfa = "Ju"; aDiaSemanaN = "4"; aDiaSemanaL = "J"; |FINSI;
          |SI aAlfa = "Vi"; aDiaSemanaN = "5"; aDiaSemanaL = "V"; |FINSI;
          |SI aAlfa = "Sa"; aDiaSemanaN = "6"; aDiaSemanaL = "S"; |FINSI;
          |SI aAlfa = "Do"; aDiaSemanaN = "7"; aDiaSemanaL = "D"; |FINSI;
     |FINSI;
|FPRC;

|PROCESO FormaHora;
     aHora = aAlfa % 102; nHora = aHora; nHora = nHora $ 24;
     aMinu = aAlfa % 402; nMinu = aMinu; nMinu = nMinu $ 60;
     aAlfa = (("00" + nHora)%-102) + ":" + (("00" + nMinu)%-102);
|FPRC;

|PROCESO CambiaDias;     || Le suma el N§ dias trayecto
     aAlfa = "";
     aLon = aDias % A0;
     |PARA i = 1; |SI i [ aLon; |HACIENDO i = i + 1;
          nPos = i * 100 + 1;
          aDia = aDias % nPos;
          nNume = aDia + #13#11 - 1;
          nNume = nNume - 1;
          nNume = nNume $ 7;
          nNume = nNume + 1;
          aAlfa = aAlfa + nNume;
     |SIGUE;
     aDias = aAlfa;
|FPRC;

|PROCESO LeeEl29;
     |ABRE #29;
     #29#0 = #31#0;
     #29#2 = #31#1;
     #29#4 = #31#2;
     #29#5 = #31#3;
     #29#7 = #31#4;
     |LEE 000#29=;
     |SI nSwVuelta = 0;
          |SI FSalida = 0; |Y #29#9 = "S"; |Y #52#20 ] #29#8; |Y #52#20 [ #29#10;
               aAlfa = "0";
          |SINO;
               aAlfa = "1";
          |FINSI;
     |SINO;
          |SI FSalida = 0; |Y #29#9 = "S"; |Y #52#26 ] #29#8; |Y #52#26 [ #29#10;
               aAlfa = "0";
          |SINO;
               aAlfa = "1";
          |FINSI;
     |FINSI;
     |CIERRA #29;
     FSalida = aAlfa;
|FPRC;

|PROCESO AsignaServi;
     |SI nSwVuelta = 0;
          #52#21 = #13#5;
          #52#22 = #13#3;

          #52#99 = #13#11;
          #52#101 = #13#2;

          |SI #52#99 = 2;
               #52#23 = #52#20;
          |SINO;
               nNume = #52#20;
               nNume = nNume + (#13#7 - 1);
               fFecha = nNume;
               #52#23 = fFecha;
          |FINSI;
          #52#24 = #13#6;
          #52#75 = #13#1;
          |ABRE #12;
          #12#0 = #52#75;
          |LEE 000#12=;
          |SI FSalida ! 0; |DDEFECTO #12; |FINSI;
          |CIERRA #12;
          #52#76 = #12#1;
          ||horas viaje
          nNume = (#52#23 - #52#20) * 24 * 60;
          |SI nNume = 0;
               aAlfa = #52#24; |HAZ FormaHora;
               nNume = nMinu + (nHora * 60);
               aAlfa = #52#21; |HAZ FormaHora;
               nNume = nNume - (nMinu + (nHora * 60));
          |SINO;
               nNume = nNume - (24 * 60);
               aAlfa = #52#24; |HAZ FormaHora;
               nNume = nNume + nMinu + (nHora * 60);
               aAlfa = #52#21; |HAZ FormaHora;
               nNume = nNume + (24 * 60) - (nMinu + (nHora * 60));
          |FINSI;
          nMinu = nNume $ 60;
          nHora = (nNume - nMinu) / 60;
          aAlfa = (("00" + nHora) % -102) + ":" + (("00" + nMinu) % -102);
          #52#25 = aAlfa;
     |SINO;
          #52#27 = #13#5;
          #52#28 = #13#3;

          #52#100 = #13#11;
          #52#102 = #13#2;

          |SI #52#100 = 2;
               #52#29 = #52#26;
          |SINO;
               nNume = #52#26;
               nNume = nNume + (#13#7 - 1);
               fFecha = nNume;
               #52#29 = fFecha;
          |FINSI;
          #52#30 = #13#6;
          #52#80 = #13#1;
          |ABRE #12;
          #12#0 = #52#75;
          |LEE 000#12=;
          |SI FSalida ! 0; |DDEFECTO #12; |FINSI;
          |CIERRA #12;
          #52#81 = #12#1;
          ||horas viaje
          nNume = (#52#29 - #52#26) * 24 * 60;
          |SI nNume = 0;
               aAlfa = #52#30; |HAZ FormaHora;
               nNume = nMinu + (nHora * 60);
               aAlfa = #52#27; |HAZ FormaHora;
               nNume = nNume - (nMinu + (nHora * 60));
          |SINO;
               nNume = nNume - (24 * 60);
               aAlfa = #52#30; |HAZ FormaHora;
               nNume = nNume + nMinu + (nHora * 60);
               aAlfa = #52#27; |HAZ FormaHora;
               nNume = nNume + (24 * 60) - (nMinu + (nHora * 60));
          |FINSI;
          nMinu = nNume $ 60;
          nHora = (nNume - nMinu) / 60;
          aAlfa = (("00" + nHora) % -102) + ":" + (("00" + nMinu) % -102);
          |SI #52#30 = "     ";
               #52#31 = "";
          |SINO;
               #52#31 = aAlfa;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO BuscaServi;
     |SI nSwVuelta = 0;
          fFecha = #52#20;
     |SINO;
          fFecha = #52#26;
     |FINSI;

     fDesde = #13#8 + (#13#11 - 1);
     fHasta = #13#9 + (#13#11 - 1);
     |SI fFecha ] fDesde; |Y fFecha [ fHasta;
          aDias = #13#10; |QBF aDias;
          |SI aDias = "*";
               nHay = nHay + 1;
          |SINO;
               |HAZ CambiaDias;
               aAlfa = aDias - aDiaSemanaN;
               |SI FSalida > 0;
                    nHay = nHay + 1;
               |FINSI;
          |FINSI;
     |FINSI;
     |SI nHay ] 1;
          |HAZ AsignaServi;
          |ERROR10;
     |FINSI;
|FPRC;

|DEFBUCLE BuscaServi;
     #13#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, BuscaServi;
|FIN;

|PROCESO RFreTrayectos;
     |PARA j = 12; |SI j [ 21; |HACIENDO j = j + 1;
          |SI #referen@#j# ! "     "; |Y j = i;
               #13#0 = #31#0;
               #13#1 = #31#1;
               #13#2 = #31#2;
               #13#3 = #30#6;
               #13#4 = #30#7;
               #13#5 = #31i;
               #13#6 = #referen@#j#;
               #13#7 = #referen@#11; || #30#14;
               #13#11 = #31#11;
               |GRABA 000#13n;
               nHay = 1;
          |FINSI;
     |SIGUE;
|FPRC;

|DEFBUCLE RFreTrayectos;
     #referen@#1006;
     9, 6;
     #31#0, #31#1, #31#2, #31#3, #31#4, 000, "D", aPobDes;
     #31#0, #31#1, #31#2, #31#3, #31#4, 999, "D", aPobDes;
     ;
     NULL, RFreTrayectos;
|FIN;

|PROCESO FreTrayectos;
     |PARA i = 12; |SI i [ 21; |HACIENDO i = i + 1;
          |SI #31i ! "     ";
               nLin = i - 11;
               |HAZ LeeEl29;
               |SI FSalida = 0;
                    #30#0 = #31#0;
                    #30#1 = #31#1;
                    #30#2 = #31#2;
                    #30#3 = #31#3;
                    #30#4 = #31#4;
                    #30#5 = nLin;
                    |LEE 000#30=;
                    |SI FSalida = 0;
                         nHay = 0;
                         |BUCLE RFreTrayectos;
                         ||SI nHay ! 0; |SAL; |FINSI;
                    |FINSI;
               |FINSI;
          |FINSI;
     |SIGUE;
|FPRC;

|DEFBUCLE FreTrayectos;
     #31#1;
     9, 6;
     "          ", 000, " " , 000, 00, 000, "O", aPobOrg;
     "zzzzzzzzzz", 999, "z" , 999, 99, 999, "O", aPobOrg;
     ;
     NULL, FreTrayectos;
|FIN;

|PROCESO PeriServi;
     nHay = 0;
     |SI nSwVuelta = 0;  || Org -> Des
          |SI #52#20 = "01.01.0000";
               fFecha = #52#1;
          |SINO;
               fFecha = #52#20;
          |FINSI;
          |SI fFecha ] #24#5; |Y fFecha [ #24#6;
               nHay = 1;
               #13#8 = #24#5;
               #13#9 = #24#6;
               #13#10 = #24#7;
          |FINSI;
     |SINO;    || Des -> Org
          fFecha = #52#26;
          |SI fFecha ] #24#5; |Y fFecha [ #24#6;
               nHay = 1;
               #13#8 = #24#5;
               #13#9 = #24#6;
               #13#10 = #24#7;
          |FINSI;
     |FINSI;

     |SI nHay = 1;
          |SALVA_DATOS #13;
          |REPON_DATOS #referen@;
          nConta = nConta + 1;
          #referen@#12 = nConta;
          |GRABA 020#referen@.n;
     |FINSI;
|FPRC;

|DEFBUCLE PeriServi;
     #24#1;
     8;
     #13#0, #13#1, #13#2, #13#3, 00, "S";
     #13#0, #13#1, #13#2, #13#3, 99, "S";
     ;
     NULL, PeriServi;
|FIN;

|PROCESO TmpTrayectos;
     |BUCLE PeriServi;
|FPRC;

|DEFBUCLE TmpTrayectos;
     #13#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, TmpTrayectos;
|FIN;

|PROCESO CargaTrayectos;
     |SI nSwVuelta = 0;
          aPobOrg = #52#8; aPobDes = #52#12;
     |SINO;
          aPobOrg = #52#12; aPobDes = #52#8;
     |FINSI;

     |DDEF aAlfa;
     aAlfa = aAlfa + "sctrm031";
     |CARGA_DEF aAlfa, referen@;
     |SI FSalida = 0;
          |ABRE #30;
          |BUCLE FreTrayectos;
          |CIERRA #30;
          |DESCARGA_DEF #referen@;

          || sctrm024 puede contener mas de 1 registro por cada uno del sctrw013
          || por eso se regraba el sctrw013 con todos los posibles

          |DDEF aAlfa; aAlfa = aAlfa + "sctrw013";
          |CARGA_DEF aAlfa, referen@;
          |SI FSalida = 0;
               |HAZ CreaTempo; |NOME_DAT #referen@#Tempo#; aTmp13b = Tempo; |DELINDEX #referen@;
               |ABRE #referen@;

               nConta = 0;
               |BUCLE TmpTrayectos;

               |CIERRA #13; |DELINDEX #13; |ABRE #13;
               |LEE 000#referen@.p;
               |PARA; |SI FSalida = 0; |HACIENDO;
                    |SALVA_DATOS #referen@;
                    |REPON_DATOS #13;
                    |GRABA 020#13n;
                    |LEE 000#referen@.s;
               |SIGUE;

               |CIERRA #referen@;
               Tempo = aTmp13b; |HAZ BoraTempo; |DELINDEX #referen@;
               |DESCARGA_DEF #referen@;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO DatosSalida;
     nSwVuelta = enSwVuelta;

     |BUCLE BuscaTempo;
     #52#96 = aTemporada;
     fFecha = #52#20; |HAZ DiaSemana;

     |HAZ CreaTempo; |NOME_DAT #13 Tempo; aTmp13 = Tempo; |DELINDEX #13;
     |ABRE #13;
     |HAZ CargaTrayectos;
     nHay = 0; aDias = "";
     |BUCLE BuscaServi;
     |CIERRA #13;
     Tempo = aTmp13; |HAZ BoraTempo; |DELINDEX #13;
     enHay = nHay;
|FPRC;

|PROCESO DatosVuelta;
     nSwVuelta = enSwVuelta;

     |BUCLE BuscaTempo;
     #52#105 = aTemporada;
     fFecha = #52#26; |HAZ DiaSemana;

     |HAZ CreaTempo; |NOME_DAT #13 Tempo; aTmp13 = Tempo; |DELINDEX #13;
     |ABRE #13;
     |HAZ CargaTrayectos;
     nHay = 0; aDias = "";
     |BUCLE BuscaServi;
     |CIERRA #13;
     Tempo = aTmp13; |HAZ BoraTempo; |DELINDEX #13;
     enHay = nHay;
|FPRC;
=============================================================
|PROCESO TariSuple;
     |SI nSwSuple = 1; |Y #18#14 = "S"; |ACABA; |FINSI;
     |SI nSwSuple = 2; |Y #18#14 = "N"; |ACABA; |FINSI;

     |SI #52#17 < #18#15; |O #52#17 > #18#16; |ACABA; |FINSI;


     |SI #52#26 = "01.01.0000";
          |SI #52#101 ! #18#13;
               |ACABA;
          |FINSI;
          |SI #52#20 < #18#11; |O #52#20 > #18#12;
               |ACABA;
          |FINSI;
          nOk = 1;
     |SINO;
          nOk = 0;
          |SI #52#20 ] #18#11; |Y #52#20 [ #18#12; |Y #52#101 = #18#13;
               nOk = 1;
          |FINSI;
          |SI #52#26 ] #18#11; |Y #52#26 [ #18#12; |Y #52#102 = #18#13;
                     |Y #52#17 ! 2;
               nOk = 2;
          |FINSI;
          |SI nOk = 0; |ACABA; |FINSI;
     |FINSI;

     nHay = 0;
     |SI #18#7 = "I"; |O #18#7 = "T";
          aDias = #18#8; |QBF aDias;
          |SI aDias = "*";
               nHay = 1;
               nImpoS = nImpoS + #18#9;
          |SINO;
               |SI nOk = 2; |Y #18#7 = "T";
                    aAlfa = aDias - aDiaV;
                    |SI FSalida > 0; nHay = 2; |FINSI;
               |FINSI;

               |SI nOk = 1;
                    aAlfa = aDias - aDiaS;
                    |SI FSalida > 0; nHay = 1; |FINSI;
               |FINSI;

               |SI nHay = 1; nImpoS = nImpoS + #18#9; |FINSI;
               |SI nHay = 2; nImpoV = nImpoV + #18#9; |FINSI;
          |FINSI;
     |FINSI;

     |SI #18#7 = "V"; |Y #52#17 = 3;   || ida y vuelta Cerrada
          aDias = #18#8; |QBF aDias;
          |SI aDias = "*";
               nHay = 1;
          |SINO;
               aAlfa = aDias - aDiaV;
               |SI FSalida > 0; nHay = 1; |FINSI;
          |FINSI;
          |SI nHay = 1; nImpoV = nImpoV + #18#9; |FINSI;
     |FINSI;

     |SI #18#7 = "O"; |Y #52#17 = 2;   || ida cerrada y vuelta Abierta
          aDias = #18#8; |QBF aDias;
          |SI aDias = "*";
               nHay = 1;
          |SINO;
               aAlfa = aDias - aDiaV;
               |SI FSalida > 0; nHay = 1; |FINSI;
          |FINSI;
          |SI nHay = 1; nImpoV = nImpoV + #18#9; |FINSI;
     |FINSI;

     |SI nSwSuple = 1; |Y nHay ! 0;
          |ERROR10;
     |FINSI;
|FPRC;

|DEFBUCLE TariSuple;
     #18#1;
     ;
     #20#0, #20#1, #20#2, 000;
     #20#0, #20#1, #20#2, 999;
     ;
     NULL, TariSuple;
|FIN;

|PROCESO TipTariFec;
     nNume = #52#20 - #52#99 + 1;
     fFecha = nNume;
     |SI fFecha ] #19#8; |Y fFecha [ #19#9;
          aDias = #19#7; |QBF aDias;
          |SI aDias = "*";
               nHay = 1;
          |SINO;
               aAlfa = aDias - aDiaS;
               |SI FSalida > 0; nHay = 1; |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE TipTariFec;
     #19#1;
     ;
     #20#0, #20#1, #20#2, 000;
     #20#0, #20#1, #20#2, 999;
     ;
     NULL, TipTariFec;
|FIN;

|PROCESO TariTrayectos;
     #17#0 = #20#0;
     #17#2 = #20#1;
     #17#4 = #20#2;
     |LEE 000#17=;
     |SI FSalida ! 0; |ACABA; |FINSI;

     |SI #17#9 = "N"; |ACABA; |FINSI;

     |SI #52#17 = 1; |O #52#17 = 3;
          |SI #17#14 = "A"; |ACABA; |FINSI;
     |SINO;
          |SI #17#14 = "C"; |ACABA; |FINSI;
     |FINSI;

     |SI #17#11 ! 0;
          nNume = #52#20 - #52#1;
          |SI nNume > #17#11; |ACABA; |FINSI;
     |FINSI;

     |SI #52#17 ! 1; |Y #17#7 = "S"; |ACABA; |FINSI;

     nHay = 0;
     |BUCLE TipTariFec;
     |SI nHay = 0; |ACABA; |FINSI;

     #7#0 = #17#2;
     |LEE 000#7=;
     |SI FSalida ! 0; |DDEFECTO #7; |FINSI;

     aAlfa = (("00" + #20#1) % -102) + "." + (("0000" + #20#2) % -104);
     #14#0 = aAlfa;
     #14#1 = #7#1;
     |SI #52#17 = 1;
          #14#2 = #20#7;
     |SINO;
          #14#2 = #20#8;
     |FINSI;
     nImpoS = 0; nImpoV = 0;
     |SI aSwAplicaSuple = "S";
          nSwSuple = 1; |BUCLE TariSuple;
          nSwSuple = 2; |BUCLE TariSuple;
     |FINSI;

     #14#3 = nImpoS;
     #14#4 = nImpoV;
     #14#5 = #14#2;

     |SI #52#20 ] #12#22; |Y #52#20 [ #12#23;
          #14#6 = #12#18;
     |SINO;
          #14#6 = 0;
     |FINSI;
     #14#6 = #14#6 + #14#3 + #14#4;

     #14#7 = #14#5 + #14#6;
     |GRABA 000#14n;
|FPRC;

|DEFBUCLE TariTrayectos;
     #20#1;
     ;
     aTemporada, 00, 0000, 000, #52#94, #52#8, #52#12;
     aTemporada, 99, 9999, 999, #52#94, #52#8, #52#12;
     ;
     NULL, TariTrayectos;
|FIN;

|PROCESO CargaTari;
     nNume = #52#26 - #52#100 + 1;
     fFecha = nNume; |HAZ DiaSemana; aDiaV = aDiaSemanaL;
     nNume = #52#20 - #52#99 + 1;
     fFecha = nNume; |HAZ DiaSemana; aDiaS = aDiaSemanaL;

     |ABRE #12;
     #12#0 = #52#75;
     |LEE 000#12=;
     |SI FSalida ! 0; |DDEFECTO #12; |FINSI;
     |CIERRA #12;

     aSwAplicaSuple = "S";
     |ABRE #3;
     #3#0 = #52#8;
     |LEE 020#3=;
     |SI #3#7 = "N";
          aSwAplicaSuple = "N";
     |SINO;
          #3#0 = #52#12;
          |LEE 020#3=;
          |SI #3#7 = "N";
               aSwAplicaSuple = "N";
          |FINSI;
     |FINSI;
     |CIERRA #3;

     nSwTempoFRese = 1;
     |BUCLE BuscaTempo;
     nSwTempoFRese = 0;

     |ABRE #17;
     |ABRE #7;
     |BUCLE TariTrayectos;
     |CIERRA #7;
     |CIERRA #17;
|FPRC;

|PROCESO Linea;
     #53#0 = #52#0;
     |LEE 000#14p;
     |PARA; |SI FSalida = 0; |HACIENDO;
          aAlfa = #14#0 % 102;
          |SI #53#7 = aAlfa;
               aAlfa = #14#0 % 404;
               #53#8 = aAlfa;
               #53#9 = #14#5;
               #53#10 = #14#6;
               #53#11 = #14#7;
               enTotBillete = #53#11; |HAZ IncDisBillete;

               |HAZ DtoAuto;
               nImpDtoAuto = #53#9 * enDtoAuto / 100;

               #53#13 = enIncBillete - nImpDtoAuto;
               #53#11 = #53#11 + #53#13;
               |SAL;
          |FINSI;
          |LEE 000#14s;
     |SIGUE;
     |GRABA 020#53n;
|FPRC;
