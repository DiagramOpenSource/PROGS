|FICHEROS;
     malpe126 #0;
     malpe127 #1,MANTE;
     malpe128 #2;
     malpe129 #3,MANTE;
     malpe004 #4;
     malpe005 #5;
     agifa019 #19;
     dsm00024 #24;
     agifa029 #29;
     malpe130 #130;

     referen@ #100;
|FIN;

|VARIABLES;
     nBorra = 0;
     aMatriPos = "";
     nRefresca = 0;
     nTotalPale = 0;
     nEntraPale = 0;
     nNume = 0;
     aAlfa = "";
     nSalida = 0;
     nBoton1 = 0;
     nBoton2 = 0;
     nBoton3 = 0;
     nBoton4 = 0;
     nBoton5 = 0;
     nBoton6 = 0;
     nBoton7 = 0;
     nBoton8 = 0;
     &Tempo = "";
     nForma = 0;
     nModo = 0;
     aTempo1 = "";
     aTempo3 = "";
     aArt = "";
     aLote = "";
     nSw = 0;
     nHay = 0;
     aFic = "";
     aAlma = "";
     aLector = "";
     nError = 0;
     nHandle = 0;
|FIN;

|PROCESO LeeTxt;
     |ABRE #130; |LEE 000#130=; |CIERRA #130;
     aAlma = "";
     aLector = "";
     nError = 0;
     aFic = #130#3; |QBF aFic; aFic = "C:/" + aFic + ".txt";
     |XABRE "CU", aFic, nHandle;
     |SI FSalida ] 0;
          |XLEE nHandle, 250, aAlma;
          |SI FSalida > 0;
               |XLEE nHandle, 250, aLector;
               |SI FSalida [ 0; nError = 1; |FINSI;
          |SINO;
               nError = 1;
          |FINSI;
          |XCIERRA nHandle;
     |SINO;
          nError = 1;
     |FINSI;

     |SI nError ! 0;
          |MENAV "0000ERROR al leer el fichero de configuracion";
     |SINO;
          #0#0 = aAlma;
          #0#2 = aLector;
          |PINDA #0#0;
          |HAZ PintaAlma;
     |FINSI;
     FSalida = nError;
|FPRC;
컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
|PROCESO ActuOrdenLin;
     |SI #3#8 = 0;
          |ACABA;
     |FINSI;
     #5#28 = #3#0;
     #5#0 = #3#1;
     #5#1 = #3#2;
     |LEE 121#5=;
     |SI FSalida = 0;
          #5#39 = #0#2; ||Carretilla
          #5#37 = #3#8; ||Palets Reales
          |GRABA 020#5a;
          |LIBERA #5;
     |FINSI;
|FPRC;

|DEFBUCLE ActuOrdenLin;
     #3#1;
     ;
     #1#0, #1#1, 001;
     #1#0, #1#1, 999;
     ;
     NULL, ActuOrdenLin;
|FIN;

|PROCESO ActuOrdenCab0;
     aAlfa = "S";
|FPRC;

|PROCESO ActuOrdenCab1;
     |SI #5#39 = 0;
          aAlfa = "N";
     |FINSI;
|FPRC;

|PROCESO ActuOrdenCab2;
     #4#22 = aAlfa;
     |GRABA 020#4a;
     |LIBERA #4;
|FPRC;

|DEFBUCLE ActuOrdenCab;
     #4#1;
     ;
     #1#0, #1#1;
     #1#0, #1#1;
     be;
     NULL, ActuOrdenCab0, ActuOrdenCab1, ActuOrdenCab2;
|FIN;

컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
|PROCESO Recarga;
     |SI #5#3 ! #0#0; |O #5#39 ! 0;
          |ACABA;
     |FINSI;
     |DDEFECTO #24;
     #24#0 = #5#2;
     #24#1 = #5#3;
     #24#2 = #5#31;
     |LEE 000#24=;
     |SI FSalida ! 0; |DDEFECTO #24; |FINSI;

     |SELECT #1#100;
     |DDEFECTO #100;
     #100#0 = #5#28;
     #100#1 = #5#0;
     #100#2 = #5#1;
     |LEE 101#100=;
     |SI FSalida = 0;
          |SI #100#7 ! #5#30; || Se ha modificado
               #100#7 = #5#30;
               nNume = #100#7 ! 0;
               |SI #100#7 ! nNume; |Y nNume [ #100#7;
                    nNume = nNume + 1;
               |FINSI;
               nTotalPale = nTotalPale - #100#9;
               #100#9 = nNume;  || El valor se pasa a entero
               nTotalPale = nTotalPale + #100#9;
               |GRABA 020#100a;
          |FINSI;
          |LIBERA #100;
     |SINO;                   || Se ha creado una linea nueva
          #100#0 = #5#28;
          #100#1 = #5#0;
          #100#2 = #5#1;
          #100#3 = #5#2;
          #100#4 = #5#31;
          #100#5 = #24#28;
          #100#6 = #5#5;
          #100#7 = #5#30;
          #100#8 = 0;

          nNume = #100#7 ! 0;
          |SI #100#7 ! nNume; |Y nNume [ #100#7;
               nNume = nNume + 1;
          |FINSI;
          #100#9 = nNume;  || El valor se pasa a entero
          |GRABA 020#100n;

          nTotalPale = nTotalPale + #100#9;
     |FINSI;
|FPRC;

|DEFBUCLE Recarga;
     #5#1;
     ;
     #1#0, #1#1, 001;
     #1#0, #1#1, 999;
     ;
     NULL, Recarga;
|FIN;
컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
|PROCESO CuentaPale;
     nTotalPale = nTotalPale + #3#9;
|FPRC;

|DEFBUCLE CuentaPale;
     #3#1;
     ;
     #1#0, #1#1, 001;
     #1#0, #1#1, 999;
     ;
     NULL, CuentaPale;
|FIN;
컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
|PROCESO Actu129;
     |SELECT #3#100;
     #100#3 = aArt;
     #100#4 = aLote;
     #100#0 = #3#0;
     #100#1 = #3#1;
     |LEE 101#100=;
     |SI FSalida ! 0;
          aAlfa = "0000N0 EXISTE"+&13 + "Articulo:" + aArt +&13+ "Lote:" + aLote;
          |MENAV aAlfa;
          |ACABA;
     |FINSI;
     #100#8 = #100#8 + 1;
     |SI #100#8 > #100#9;
          aAlfa = "0000COMPETADA CARGA"+&13 + "Articulo:" + aArt +&13+ "Lote:" + aLote;
          |MENAV aAlfa;
     |SINO;
          |GRABA 020#100a;
          nEntraPale = nEntraPale + 1;
     |FINSI;
     |LIBERA #100;
|FPRC;

|PROCESO LeeCB; |TIPO 0;
     |SI #3Campo ! Contenido; |Y FSalida = 0;
||          aLote = #1#1 % 105;
||          aArt = #1#1 % 608;
          aLote = #3#3 % 105;
          aArt = #3#3 % 608;
          |HAZ Actu129;
          #3Campo = Contenido;
          |SI nEntraPale ] nTotalPale;  || Por si se modifica la  Orden
               |BUCLE Recarga;          || mientras que actualiza la carretilla
               |SI nEntraPale ] nTotalPale;
                    |MENAV "0000Carga Finalizada, pulse para actualizar.";
                    |PTEC 807;
               |FINSI;
          |FINSI;
          |PONCONTROL 23, "SI";
     |FINSI;
|FPRC;

|PROCESO Min3;
     #3#0 = #1#0;
     #3#1 = #1#1;
     #3#5 = " " * 30;
     #3#3 = " " * 20;
|FPRC;

|PROCESO Max3;
     #3#0 = #1#0;
     #3#1 = #1#1;
     #3#5 = "z" * 30;
     #3#3 = "z" * 20;
|FPRC;

|PROCESO PonBoton2;
     |ESTADO_CONTROL nBoton5, aAlfa;
     |ESTADO_CONTROL nBoton6, aAlfa;
     |ESTADO_CONTROL nBoton7, aAlfa;
|FPRC;

|PROCESO PonBoton1;
     |ESTADO_CONTROL nBoton1, aAlfa;
     |ESTADO_CONTROL nBoton2, aAlfa;
     |ESTADO_CONTROL nBoton3, aAlfa;
     |ESTADO_CONTROL nBoton4, aAlfa;
     |ESTADO_CONTROL nBoton8, aAlfa;
|FPRC;

|PROCESO Matri7; |TIPO 7;
     aMatriPos = #1#2;
|FPRC;

|PROCESO Matri11; |TIPO 11;
     |SI FSalida = 10; ||F2
          nRefresca = 1;
          |PTEC 807;
     |FINSI;
|FPRC;

|PROCESO Matri0; |TIPO 0;
     aAlfa = "HIDE"; |HAZ PonBoton1;
     aAlfa = "SHOW"; |HAZ PonBoton2;
     |PUSHV 0501 2380;
     #2#0 = #1#2;
     #2#1 = #1#4;
     #2#2 = #1#0;
     #2#3 = #1#1;
     #2#4 = #1#5;
     |PINPA #0#2;
     |PINDA #0#2;
     nTotalPale = 0;
     |BUCLE CuentaPale;
     |ABRE #100;
     |ENTLINEAL #2#3, 4, 4, 20, 0, Min3, Max3;
     |CIERRA #100;
     |POPV;
     aAlfa = "HIDE"; |HAZ PonBoton2;

     |ABRE #5;
     |BUCLE ActuOrdenLin;
     |CIERRA #5;
     |ABRE #4;
     |BUCLE ActuOrdenCab;
     |CIERRA #4;

     aAlfa = "SHOW"; |HAZ PonBoton1;

     ||***Lo saco del entlineal
     nRefresca = 1;
     |PTEC 807;
|FPRC;

|PROCESO FinCab;
     |SI nSw = 1;
          |DDEFECTO #1;
          #1#0 = #4#0;
          #1#1 = #4#1;
          |LEE 101#1=;
          |SI FSalida ! 0; |GRABA 020#1b; |FINSI;
          #1#2 = #4#9;
          #1#3 = #4#10;
          #1#4 = #4#11;
          #1#5 = #4#5;
          |GRABA 020#1a;
          |LIBERA #1;
     |FINSI;
|FPRC;

|PROCESO Lin;
     |SI #5#3 ! #0#0; |O #5#39 = 9999;
          |ACABA;
     |FINSI;
     nHay = 1;
     nSw = 1;

     |DDEFECTO #24;
     #24#0 = #5#2;
     #24#1 = #5#3;
     #24#2 = #5#31;
     |LEE 000#24=;
     |SI FSalida ! 0; |DDEFECTO #24; |FINSI;

     |DDEFECTO #3;
     #3#0 = #5#28;
     #3#1 = #5#0;
     #3#2 = #5#1;
     |LEE 101#3=;
     |SI FSalida ! 0; |GRABA 020#3b; |FINSI;
     #3#3 = #5#2;
     #3#4 = #5#31;
     #3#5 = #24#28;
     #3#6 = #5#5;
     #3#7 = #5#30;
     #3#8 = #5#37;

     nNume = #3#7 ! 0;
     |SI #3#7 ! nNume; |Y nNume [ #3#7;
          nNume = nNume + 1;
     |FINSI;
     #3#9 = nNume;  || El valor se pasa a entero

     |GRABA 020#3a;
     |LIBERA #3;
|FPRC;

|PROCESO Cab;
     nSw = 0;
|FPRC;

|DEFBUCLE OrdCarga;
     #4#2;
     ;
     "               ", "N", "      ";
     "zzzzzzzzzzzzzzz", "N", "zzzzzz";
     ;
     NULL, Cab, Lin, FinCab;
|FIN;

|PROCESO BorraLin;
     #5#27 = #3#0;
     #5#0 = #3#1;
     #5#1 = #3#2;
     |LEE 000#5=;
     |SI FSalida ! 0; |O nBorra = 1;
          |BORRA 020#3a;
     |FINSI;
|FPRC;

|DEFBUCLE BorraLin;
     #3#1;
     ;
     #1#0, #1#1, 001;
     #1#0, #1#1, 999;
     be;
     NULL, BorraLin;
|FIN;

|PROCESO Borra;
     nBorra = 0;
     #4#0 = #1#0;
     #4#1 = #1#1;
     |LEE 000#4=;
     |SI FSalida ! 0; |O #4#15 = "S";
          |BORRA 020#1a;
          nBorra = 1;
     |FINSI;
     |BUCLE BorraLin;
|FPRC;

|DEFBUCLE Borra;
     #1#1;
     ;
     INICIO;
     FINAL;
     be;
     NULL, Borra;
|FIN;

|PROCESO Refresca;
     |ABRE #1;
     |ABRE #3;
     |ABRE #24;
     |BUCLE OrdCarga;
     |CIERRA #1;
     |CIERRA #3;
     |CIERRA #24;

     |ABRE #4;
     |ABRE #5;
     |BUCLE Borra;
     |CIERRA #5;
     |CIERRA #4;
|FPRC;

|PROCESO BotonActua;
     nSalida = 1;
     |PTEC 807;
|FPRC;

|PROCESO BotonAcepta;
     |PTEC 802;
|FPRC;

|PROCESO BotonRefres;
     |PTEC 824; ||F2
|FPRC;

|PROCESO BotonFin;
     |PTEC 807;
|FPRC;

|PROCESO BotonAvPag;
     |PTEC 809;
|FPRC;

|PROCESO BotonRePag;
     |PTEC 808;
|FPRC;

|PROCESO Tipo12; |TIPO 12;
|FPRC;

|PROCESO PintaAlma; |TIPO 0;
     |ABRE #29;
     #29#0 = #0#0;
     |LEE 000#29=;
     |CIERRA #29;
     #0#1 = #29#1; |PINTA #0#1;
|FPRC;

|PROCESO Min;
     #1#2 = " " * 15;
     |SI FSalida = "POSICION";
          #1#2 = aMatriPos;
     |FINSI;
|FPRC;

|PROCESO Max;
     #1#2 = "z" * 15;
|FPRC;

|PROGRAMA;
     |DDEF aAlfa; aAlfa = aAlfa + "malpe129";
     |CARGA_DEF aAlfa, referen@;
     |SI FSalida ! 0;
          |MENAV "0000ERROR al cargar 'malpe129'";
          |ACABA;
     |FINSI;

     |HAZ CreaTempo; aTempo1 = Tempo;
     |NOME_DAT #1 aTempo1; |DELINDEX #1;

     |HAZ CreaTempo; aTempo3 = Tempo;
     |NOME_DAT #3 aTempo3; |DELINDEX #3;
     |NOME_DAT #100 aTempo3;

     |CLS;
     |PINPA #0#0;
     |PINDA #0#0;
     |ENTLINEAL #1#1, 1, 7, 20, 0, Min, Max;

     |CONTROL_SIMPLE 0, "BOTON,ARRIBA", 1072, 1480, BotonRePag;
     nBoton1 = FSalida;
     |ESTADO_CONTROL nBoton1, "SHOW";

     |CONTROL_SIMPLE 0, "BOTON,ABAJO", 1672, 2080, BotonAvPag;
     nBoton2 = FSalida;
     |ESTADO_CONTROL nBoton2, "SHOW";
||****
     |CONTROL_SIMPLE 0, "BOTON,FIN", 2206, 2526, BotonFin;
     nBoton3 = FSalida;
     |ESTADO_CONTROL nBoton3, "SHOW";

     |CONTROL_SIMPLE 0, "BOTON,ACEPTAR", 2228, 2548, BotonAcepta;
     nBoton4 = FSalida;
     |ESTADO_CONTROL nBoton4, "SHOW";

     |CONTROL_SIMPLE 0, "BOTON,REFRESCAR", 2250, 2570, BotonRefres;
     nBoton8 = FSalida;
     |ESTADO_CONTROL nBoton8, "SHOW";
||****
     |CONTROL_SIMPLE 0, "BOTON,FIN", 2206, 2526, BotonFin;
     nBoton5 = FSalida;
     |ESTADO_CONTROL nBoton5, "HIDE";

     |CONTROL_SIMPLE 0, "BOTON,ARRIBA", 2228, 2548, BotonRePag;
     nBoton6 = FSalida;
     |ESTADO_CONTROL nBoton6, "HIDE";

     |CONTROL_SIMPLE 0, "BOTON,ABAJO", 2250, 2570, BotonAvPag;
     nBoton7 = FSalida;
     |ESTADO_CONTROL nBoton7, "HIDE";

     |||ENDATOS #1#0;
     |||SI FSalida = 0;
     |HAZ LeeTxt;
     |SI FSalida = 0
          |ABRE #1;
          |ABRE #3;
          |ABRE #24;
          |BUCLE OrdCarga;
          |CIERRA #1;
          |CIERRA #3;
          |CIERRA #24;
          |SI nHay = 0;
               |MENAV "0000Seleccion vacia...";
          |SINO;
               nSalida = 0;
               |PARA nRefresca = 1; |SI nRefresca = 1; |HACIENDO;
                    nRefresca = 0;
                    |ENTLINEAL #2#1, 1, 4, 20, 0, Min, Max;
                    |SI nRefresca = 1;
                         aAlfa = "DISABLE"; |HAZ PonBoton1;
                         |HAZ Refresca;
                         aAlfa = "ENABLE"; |HAZ PonBoton1;
                    |FINSI;
               |SIGUE;
               |SI nSalida = 1;
                    aAlfa = "DISABLE"; |HAZ PonBoton1;
                    |MENAV "0000Actualizando...";
               |FINSI;
          |FINSI;
     |FINSI;

     |DELINDEX #1; Tempo = aTempo1; |HAZ BoraTempo;
     |DELINDEX #3; Tempo = aTempo3; |HAZ BoraTempo;

     |DESCARGA_DEF #referen@;

     |FIN_CONTROL_WINDOWS nBoton7;
     |FIN_CONTROL_WINDOWS nBoton6;
     |FIN_CONTROL_WINDOWS nBoton5;
     |FIN_CONTROL_WINDOWS nBoton8;
     |FIN_CONTROL_WINDOWS nBoton4;
     |FIN_CONTROL_WINDOWS nBoton3;
     |FIN_CONTROL_WINDOWS nBoton2;
     |FIN_CONTROL_WINDOWS nBoton1;
     |PULSATECLA;
|FPRO;
