|FICHEROS;
     sctrz029 #0;
     sctrm036 #36;
     sctrm037 #37;
     sctrm057 #57;
     sctrm048 #148;
     sctrm001 #100;
     sctrz030 #130,MANTE;
     referen@;
|FIN;

|VARIABLES;
     aInf = "";
     &eaCliente = "";
     &eaProg = "";
     &eaNomLogo = "";
     &enPantaLogo = 0;
     &enCtrFin = 0;
     aAlfa = "";
     nModo = 0;
     nPanta = 0;
     nRango = 0;
     nGrid = 0;
     nBtnAlta = 0;
     nBtnModi = 0;
     nBtnBaja = 0;
     nBtnCons = 0;
     nBtnImpr = 0;
     nBtnTodos = 0;
     aReg0 = "";
     nReg2 = 0;
     nBloquea = 0;
     i = 0;
     nId = 0;
     aConte = "";
     nLin = 0;
     nSwFin = 0;
     aClave = "";
     fHoy = @;
|FIN;

|INCLUYE i_random;

|PROCESO GenClave;
     |HAZ Random;
     aClave = ("0000" + rnd) % -104;
     |HAZ Random;
     aAlfa = ("0000" + rnd) % -103;
     aClave = aClave + aAlfa;
     fHoy = ~;
     fHoy = fHoy % A1;
     aClave = (fHoy % 1101) + aClave;
|FPRC;

|PROCESO ActWeb;
     aAlfa = #37#0 + (("000" + #37#2) % -103);
     |ABRE #148;
     #148#0 = aAlfa;
     |LEE 101#148=;
     |SI #37#9 = "S";
          |SI FSalida = 0;
               #148#1 = #37#3;
          |SINO;
               |GRABA 020#148b;
               |HAZ GenClave;
               #148#1 = #37#3;
               #148#2 = aClave;
               #148#4 = #37#0;
               |ABRE #36;
               #36#0 = #37#0;
               |LEE 020#36=;
               |SI FSalida = 0;
                    #148#5 = #36#44;
               |FINSI;
               |CIERRA #36;
          |FINSI;
          |GRABA 020#148a;
     |SINO;
          |SI FSalida = 0;
               |BORRA 020#148a;
          |FINSI;
     |FINSI;
     |CIERRA #148;
|FPRC;

|PROCESO Chequea; |TIPO 0;
     |SI nModo = 1; |Y nBloquea = 0;
          |DDEFECTO #37;
          #37#0 = #130#0;
          #37#2 = #130#2;
          |GRABA 000#37b;
          |SI FSalida ! 0;
               |MENAV "0000Registro existente...";
               |ERROR;
          |SINO;
               |C_M #130#0, 1, "N"; |PINTA #130#0;
               |C_M #130#2, 1, "N"; |PINTA #130#2;
               nBloquea = 1;

               aAlfa = "|X000"; aAlfa = #130aAlfa; nId = aAlfa;
               |ESTADO_CONTROL nId, "ENABLE";
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Conta7; |TIPO 7;
     |SI nModo = 1; |Y nBloquea = 0;
          #37#0 = #130#0;
          #37#2 = 999;
          |LEE 000#37];
          |SI FSalida = 0;
               |LEE 000#37a;
          |SINO;
               |LEE 000#37u;
          |FINSI;
          |SI FSalida = 0; |Y #37#0 = #130#0;
               nLin = #37#2 + 1;
          |SINO;
               nLin = 1;
          |FINSI;
          #130#2 = nLin; |PINTA #130#2;
     |FINSI;
|FPRC;

|PROCESO Cliente; |TIPO 0;
     |ABRE #36;
     #36#1 = #130#0;
     |LEE 000#36=;
     |SI FSalida = 0;
          #130#1 = #36#2; |PINTA #130#1;

          |SI #36#43 = "S";
               |C_M #130#9, 1, "S"; |PINTA #130#9;
          |SINO;
               |C_M #130#9, 1, "N"; |PINTA #130#9;
          |FINSI;
     |SINO;
          |MENAV "0000No existe c¢digo de cliente...";
          |ERROR;
     |FINSI;
     |CIERRA #36;
|FPRC;

|PROCESO Conte; |TIPO 7;
     aConte = #130Campo;
|FPRC;

|PROCESO Valida; |TIPO 7;
     |SI nModo = 1;
          aAlfa = "|X000"; aAlfa = #130aAlfa; nId = aAlfa;
          |ESTADO_CONTROL nId, "DISABLE";
     |FINSI;
|FPRC;

|PROCESO Edita;
     aReg0 = #37#0;
     nReg2 = #37#2;

     |SI nModo = 1; |VENTANA 1801, 2898, -1, 17, "Nuevo Contacto"; |FINSI;
     |SI nModo = 2; |VENTANA 1801, 2898, -1, 17, "Modifica Contacto"; |FINSI;
     |SI nModo = 4; |VENTANA 1801, 2898, -1, 17, "Consulta Contacto"; |FINSI;
     |HAZ PushVenModal;

     |SI nModo = 1;
          |DDEFECTO #130;
          |SI eaCliente ! "";
               |C_M #130#0, 1, "N";
               #130#0 = eaCliente;
               |HAZ Cliente;
          |SINO;
               |C_M #130#0, 1, "S";
          |FINSI;
          |C_M #130#2, 1, "S";
     |SINO;
          |PARA i = 0; |SI i [ 9; |HACIENDO i = i + 1;
               #130i = #37i;
          |SIGUE;
          |C_M #130#0, 1, "N";
          |C_M #130#2, 1, "N";
     |FINSI;

     |PINPA #0#130;
     |PINDA #0#130;

     nBloquea = 0;
     |SI nModo = 1; |ENDATOS #1#130; |FINSI;
     |SI nModo = 2; |ENDATOS #2#130; |FINSI;
     |SI nModo = 4; |PAUSA; |FINSI;
     |SI FSalida = 0; |Y nModo ! 4;
          #37#0 = #130#0;
          #37#2 = #130#2;
          |LEE 121#37=;
          |PARA i = 0; |SI i [ 9; |HACIENDO i = i + 1;
               #37i = #130i;
          |SIGUE;
          |GRABA 020#37a;
          aReg0 = #37#0;
          nReg2 = #37#2;
          |HAZ ActWeb;
     |SINO;
          |SI nBloquea = 1;
               #37#0 = #130#0;
               #37#2 = #130#2;
               |BORRA 020#37c;
          |FINSI;
     |FINSI;
     |LIBERA #37;

     |HAZ PopVenModal;

     #37#0 = aReg0;
     #37#2 = nReg2;
     |LEE 000#37=;
     |REFRESCACONTROL nGrid;
|FPRC;

|PROCESO BtnAlta;
     nModo = 1; |HAZ Edita;
     |HAZ TieneLineas;
|FPRC;

|PROCESO BtnModi;
     |LEE 100#37=;
     |SI FSalida ! 0; |ACABA; |FINSI;
     nModo = 2; |HAZ Edita;
|FPRC;

|PROCESO BtnBaja;
     |LEE 100#37=;
     |SI FSalida ! 0; |ACABA; |FINSI;
     aAlfa = "0000N¨Borrar contacto: " + #37#0 + ".-" + #37#1;
     |QBF aAlfa;
     aAlfa = aAlfa + "?";
     |CONFI aAlfa;
     |SI FSalida = 0;
          |SI #37#9 = "S";
               aAlfa = #37#0 + (("000" + #37#2) % -103);
               |ABRE #148;
               #148#0 = aAlfa;
               |LEE 101#148=;
               |SI FSalida = 0;
                    |BORRA 020#148a;
               |FINSI;
               |CIERRA #148;
          |FINSI;
          |BORRA 020#37a;
          |HAZ TieneLineas;
          |REFRESCACONTROL nGrid;
     |FINSI;
     |LIBERA #37;
|FPRC;

|PROCESO BtnCons;
     |LEE 000#37=;
     |SI FSalida ! 0; |ACABA; |FINSI;
     nModo = 4; |HAZ Edita;
|FPRC;

|PROCESO Imprime;
     Impresora = "CrystalReport";
     |IMPRE -1;
     |SI FSalida = 0;
          aInf = #57#5; |QBF aInf;
          |INFOR aInf;
          |SI FSalida = 0;
               |PRINT;
               |PIEINF;
               |FININF;
          |SINO;
               aAlfa = "0000Error en in forme:" + aInf;
               |MENAV aAlfa;
          |FINSI;
          |FINIMP;
     |FINSI;
|FPRC;

|PROCESO BtnImpr;
     |LEE 000#37=;
     |SI FSalida ! 0; |ACABA; |FINSI;
     |HAZ Imprime;
|FPRC;

|PROCESO Evento;
     |SI FSalida = 1; |O FSalida = 2;
          |SELECT #1#37;
          |LEE 000#37=;
          |HAZ TieneLineas;
     |FINSI;
     |SI FSalida = 4;
          |HAZ BtnCons;
     |FINSI;
|FPRC;

|PROCESO TieneLineas;
     |DDEF aAlfa;
     aAlfa = aAlfa + "sctrm037";
     |CARGA_DEF aAlfa, referen@;
     |SI FSalida = 0;
          |ABRE #referen@;
          |SI eaCliente = "";
               |LEE 000#referen@.p;
          |SINO;
               #referen@#0 = eaCliente;
               #referen@#1 = 000;
               |LEE 000#referen@.];
               |SI FSalida ! 0; |O #referen@#0 ! eaCliente;
                    FSalida = "1";
               |FINSI;
          |FINSI;
          |SI FSalida = 0;
               |ESTADO_CONTROL nBtnModi, "ENABLE";
               |ESTADO_CONTROL nBtnBaja, "ENABLE";
               |ESTADO_CONTROL nBtnCons, "ENABLE";
               |ESTADO_CONTROL nBtnImpr, "ENABLE";
               |ESTADO_CONTROL nBtnImpr, "ENABLE";
          |SINO;
               |ESTADO_CONTROL nBtnModi, "DISABLE";
               |ESTADO_CONTROL nBtnBaja, "DISABLE";
               |ESTADO_CONTROL nBtnCons, "DISABLE";
               |ESTADO_CONTROL nBtnImpr, "DISABLE";
          |FINSI;
          |CIERRA #referen@;
          |DESCARGA_DEF #referen@;
     |FINSI;
|FPRC;

|PROCESO LogoPanta;
     eaNomLogo = #100#1;
     |HAZ TraeLogoPan;
     enPantaLogo = nPanta;
     |HAZ PintaLogoPan;
|FPRC;

|PROCESO BtnTodos;
     nSwFin = 1;
     eaCliente = "";
     |PTEC 807;
|FPRC;

|PROCESO Min;
     #37#0 = eaCliente;
     #37#2 = 000;
|FPRC;

|PROCESO Max;
     #37#0 = eaCliente;
     #37#2 = 999;
|FPRC;

|PROGRAMA;
     |ABRE #100; |LEE 000#100p; |CIERRA #100;
     |ABRE #57; |LEE 000#57p; |CIERRA #57;

     |PINPA #0#0; nPanta = FSalida;
     |PINDA #0#0;
     |HAZ LogoPanta;

     |CONTROL_SIMPLE nPanta, "BOTON,{{193}}Alta", 1002, 1012, BtnAlta;
     nBtnAlta = FSalida;
     |CONTROL_SIMPLE nPanta, "BOTON,{{197}}Modificar", 1014, 1024, BtnModi;
     nBtnModi = FSalida;
     |CONTROL_SIMPLE nPanta, "BOTON,{{199}}Baja", 1026, 1037, BtnBaja;
     nBtnBaja = FSalida;
     |CONTROL_SIMPLE nPanta, "BOTON,{{155}}Consulta", 1038, 1048, BtnCons;
     nBtnCons = FSalida;
     |CONTROL_SIMPLE nPanta, "BOTON,{{205}}Impresi¢n", 1050, 1060, BtnImpr;
     nBtnImpr = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,{{230}}Todos", 1088, 1098, BtnTodos;
     nBtnTodos = FSalida;

     |ABRET #37;
     |PARA nSwFin = 1; |SI nSwFin = 1; |HACIENDO;
          |HAZ TieneLineas;
          nSwFin = 0;
          nRango = 4 + 8 + 16 + 32;
          |SI eaCliente = "";
               |ESTADO_CONTROL nBtnTodos, "DISABLE";
               |LINEAL_SIMPLE #1#37, nRango, 1102, 2999, NULL, NULL, Evento;
          |SINO;
               |LINEAL_SIMPLE #1#37, nRango, 1102, 2999, Min, Max, Evento;
          |FINSI;
          nGrid = FSalida;
          enCtrFin = nGrid;
          |HAZ EsperaFin;
          |PULSATECLA;
          |DESTRUYECONTROL nGrid;
     |SIGUE;
     |CIERRAT #37;
|FPRO;
