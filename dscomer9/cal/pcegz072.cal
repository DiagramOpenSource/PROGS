|FICHEROS;
     pcegz072 #0;
     agifa019 #19;
     agifa029 #29;
     pcegm038 #38;
     pcegm050 #50;
|FIN;

|VARIABLES;
     nAlma = 0;
     aAlfa = "";
     aPath = "";
     aDato = "";
     nHandle = 0;
     aOrg = "";
     aDes = "";
     nCampo    = 0;
     aLon      = "";
     i         = 0;
     nPos      = 0;
     aLetra    = "";
     aReg      = "";
     aTab      = "";
 {99}Valor     = "";
|FIN;

|PROCESO Procesa;
     aAlfa = Valor1 - &34;
     aAlfa = aAlfa - &34;
     |DDEFECTO #50;
     #50#0 = aAlfa;
     #50#1 = nAlma;
     |LEE 101#50=;
     |SI FSalida ! 0; |GRABA 020#50b; |FINSI;

     #19#0 = #50#0;
     |LEE 000#19=;
     |SI FSalida ! 0; |DDEFECTO #19; |FINSI;

     #50#2 = #19#1;
     #50#3 = #29#1;
     #50#4 = Valor2;
     |GRABA 020#50a;
     |LIBERA #50;
|FPRC;

|PROCESO Dato;
     aTab = ";";
     nCampo = 1;
     IValor = Valor1<-;
     aLon = aDato % A0;
     aReg = "";
     |PARA i = 1; |SI i [ aLon; |HACIENDO i = i + 1;
          nPos = i * 100 + 1;
          aLetra = aDato % nPos;
          |SI aLetra = aTab;
               Valor = aReg;
               aReg = "";
               nCampo = nCampo + 1;
               IValor = IValor + 1;
          |SINO;
               |SI aLetra ] " "; |Y aLetra [ "~"; || Quita caracteres invalidos
                    aReg = aReg + aLetra;
               |FINSI;
          |FINSI;
          |SI nCampo > 90; |SAL; |FINSI;     || Por si.... :)
     |SIGUE;
     Valor = aReg;
|FPRC;

|PROGRAMA;
     |SI PARAMETRO = "menu";
          |CONFI "0000NRealizar importación stock?";
          |SI FSalida ! 0; |ACABA; |FINSI;
     |FINSI;

     |ABRE #38; |LEE 000#38p; |CIERRA #38;
     nAlma = #38#9;

     |DFICE aDes;
     aDes = aDes + "impor.csv";

     aOrg = #38#11; |QBF aOrg;

     aAlfa = "";
     |IP_REMOTO aAlfa;
     |SI aAlfa = "";
          |COPIA_FICHERO aOrg, aDes;
     |SINO;
          |RECIBE_FICHERO aOrg, aDes;
     |FINSI;

     |ABRE #29;
     #29#0 = nAlma;
     |LEE 000#29=;
     |CIERRA #29;

     |ABRE #50;
     |ABRE #19;
     |XABRE "", aDes, nHandle;

     |SI FSalida ] 0;
          |XLEE nHandle, 250, aDato;
          |PARA; |SI FSalida ] 0; |HACIENDO;
               |HAZ Dato;
               |HAZ Procesa;
               |XLEE nHandle, 250, aDato;
          |SIGUE;
          |XCIERRA nHandle;
     |FINSI;
     |CIERRA #19;
     |CIERRA #50;

     |FBORRA aDes;
|FPRO;
