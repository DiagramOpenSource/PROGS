|FICHEROS;
     gesim002 #0;
     gesim003 #3;
     gesim004 #4;
     gesim003 #3;
     agifa010 #10;
     agifa019 #19;
     agifa024 #24;
     agifa025 #25;
     dsm00003 #30;
     agifa077 #77;
     agifa084 #84;
     agifa112 #112;
     agifa501 #501;
     condm000@;
     condm001@;
     condm009@;
|FIN;

|VARIABLES;
     &enModo = 0;
     &eaAlfa = "";
     &eaMaquina = "";
     &eaArticulo = "";
     nModo = 0;
     aConte = "";
     nBtnHisto = -1;
     nRango = 0;
     nPanta = 0;
     nConta = 0;
     aAlfa = "";
     aBase = "";
     aEmp = "";
     aEmpGes = "";
     {10}aPopup = "";
     i = 0;
|FIN;

|PROCESO DefArti; |TIPO 7;
|FPRC;

|PROCESO Maq13; |TIPO 13;
     |ABRE #19;
     #19#0 = #0#20;
     |LEE 000#19=;
     |SI FSalida ! 0; |DDEFECTO #19; |FINSI;
     |CIERRA #19;
     |PINTA 1248, #19#1;
     |ABRE #25;
     #25#0 = #0#10;
     |LEE 000#25=;
     |SI FSalida ! 0; |DDEFECTO #25; |FINSI;
     |CIERRA #25;
     |PINTA 2535, #25#1;
     |ABRE #25;
     #25#0 = #0#11;
     |LEE 000#25=;
     |SI FSalida ! 0; |DDEFECTO #25; |FINSI;
     |CIERRA #25;
     |PINTA 2635, #25#1;
     |ABRE #3;
     #3#0 = #0#28;
     |LEE 000#3=;
     |SI FSalida ! 0; |DDEFECTO #3; |FINSI;
     |CIERRA #3;
     |PINTA 2836, #3#1;

     |SI nBtnHisto = -1;
          |PINPA #0#0; nPanta = FSalida;
          |CONTROL_SIMPLE nPanta, "BOTON,{{137}}Hist¢rico", 882, 898, BtnHisto;
          nBtnHisto = FSalida;
     |FINSI;
|FPRC;

|PROCESO Min4;
     #4#1 = #0#0;
     #4#3 = "      ";
     #4#0 = 0;
|FPRC;

|PROCESO Max4;
     #4#1 = #0#0;
     #4#3 = "zzzzzz";
     #4#0 = 9999999;
|FPRC;

|PROCESO BtnHisto;
     |VENTANA 1101, 3198, -1, 17, "Hist¢rico m quinas";
     |HAZ PushVenModal;

     |ABRE #4;
     nRango = 4 + 8 + 16 + 32;
     |LINEAL_SIMPLE #2#4, nRango, 1202, 2998, Min4, Max4, NULL;
     |PAUSA;
     |CIERRA #4;

     |HAZ PopVenModal;
|FPRC;

|PROCESO Conte; |TIPO 7;
     aConte = #0Campo;
|FPRC;

|PROCESO Clien; |TIPO 0;
     nModo = (FEntrada / 100) ! 0;
     |ABRE #24;
     #24#0 = #0#6;
     |LEE 000#24=;
     |SI FSalida = 0;
          |SI nModo = 1; |O #0Campo ! aConte;
               |SI #0Campo ! aConte; |Y nModo = 2;
                    |CONFI "0000SEl cambio de cliente implica un nuevo registro. ¨Continuar?";
                    |SI FSalida ! 0; |ERROR; |ACABA; |FINSI;
                    #0#15 = ""; |PINTA #0#15; #0#16 = 0; |PINTA #0#16;
                    #0#17 = ""; |PINTA #0#17; #0#18 = 0; |PINTA #0#18;
                    #0#22 = ""; |PINTA #0#22; #0#23 = 0; |PINTA #0#23;
                    #0#29 = 0;
               |FINSI;
               #0#7 = #24#1; |PINTA #0#7;
               #0#32 = #24#2; |PINTA #0#32;
               #0#21 = #24#17; |PINTA #0#21;
               #0#26 = #24#196; |PINTA #0#26;
               #0#27 = #24#197; |PINTA #0#27;
               #0#10 = #24#25; |PINTA #0#10;
               #0#11 = #24#26; |PINTA #0#11;

               |HAZ Maq13;
          |FINSI;
     |SINO;
          |C_M #0#6, 0, aAlfa;
          |SI aAlfa = "S";
               |MENAV "0000No existe cliente...";
               |ERROR;
          |FINSI;
     |FINSI;
     |CIERRA #24;
|FPRC;

|PROCESO Prove; |TIPO 0;
     |ABRE #112;
     #112#0 = #0#2;
     |LEE 000#112=;
     |SI FSalida = 0;
          |SI nModo = 1; |O #0Campo ! aConte;
               #0#3 = #112#1; |PINTA #0#3;
               |HAZ Maq13;
          |FINSI;
     |SINO;
          |C_M #0#2, 0, aAlfa;
          |SI aAlfa = "S";
               |MENAV "0000No existe proveedor...";
               |ERROR;
          |FINSI;
     |FINSI;
     |CIERRA #112;
|FPRC;

|PROCESO Repre; |TIPO 0;
     |ABRE #25;
     #25#0 = #0#10;
     |LEE 000#25=;
     |SI FSalida = 0;
          |HAZ Maq13;
     |SINO;
          |MENAV "0000No existe representante...";
          |ERROR;
     |FINSI;
     |CIERRA #25;
|FPRC;

|PROCESO Tecnico; |TIPO 0;
     |ABRE #3;
     #3#0 = #0#28;
     |LEE 000#3=;
     |SI FSalida = 0;
          |HAZ Maq13;
     |SINO;
          |MENAV "0000No existe tecnico...";
          |ERROR;
     |FINSI;
     |CIERRA #3;
|FPRC;

|PROCESO Arti; |TIPO 0;
     |ABRE #19;
     #19#0 = #0#20;
     |LEE 000#19=;
     |SI FSalida = 0;
          |HAZ Maq13;
     |SINO;
          |MENAV "0000No existe art¡culo...";
          |ERROR;
     |FINSI;
     |CIERRA #19;
|FPRC;

|PROCESO AlbCom; |TIPO 0;
     |SI FSalida = 2; |ACABA; |FINSI;
     |SI #0#19 ! 0;
          |ABRE #77;
          #77#50 = #0#25;
          #77#0 = #0#19;
          |LEE 000#77=;
          |SI FSalida ! 0;
               |MENAV "0000No existe documento...";
               |ERROR;
          |FINSI;
          |CIERRA #77;
     |FINSI;
|FPRC;

|PROCESO AlbVta; |TIPO 0;
     |SI FSalida = 2; |ACABA; |FINSI;
     |SI #0#16 ! 0;
          |ABRE #10;
          #10#52 = #0#15;
          #10#0 = #0#16;
          |LEE 000#10=;
          |SI FSalida ! 0;
               |MENAV "0000No existe documento...";
               |ERROR;
          |FINSI;
          |CIERRA #10;
     |FINSI;
|FPRC;

|PROCESO Factu; |TIPO 0;
     |SI FSalida = 2; |ACABA; |FINSI;
     |SI #0#18 ! 0;
          |ABRE #84;
          #84#48 = #0#17;
          #84#0 = #0#18;
          |LEE 000#84=;
          |SI FSalida ! 0;
               |MENAV "0000No existe documento...";
               |ERROR;
          |FINSI;
          |CIERRA #84;
     |FINSI;
|FPRC;

|PROCESO Tique; |TIPO 0;
     |SI FSalida = 2; |ACABA; |FINSI;
     |SI #0#23 ! 0;
          |ABRE #501; |SELECT #2#501;
          #501#35 = "T";
          #501#57 = #0#22;
          #501#58 = #0#23;
          |LEE 000#501=;
          |SI FSalida ! 0;
               |MENAV "0000No existe documento...";
               |ERROR;
          |FINSI;
          |CIERRA #501;
     |FINSI;
|FPRC;

|PROCESO Tique66; |TIPO 66;
     |ABRE #501; |SELECT #2#501;
     #501#35 = "T";
     #501#57 = #0#22;
     #501#58 = #0#23;
     |LEE 000#501];
     |CONSULTA_CLAVE #2#501;
     |SI FSalida = 0;
          #0#22 = #501#57; |PINTA #0#22;
          #0#23 = #501#58; |PINTA #0#23;
     |SINO;
          |MENAV "0000No existe documento...";
     |FINSI;
     |CIERRA #501;
|FPRC;

|PROCESO Maq2; |TIPO 2;
     |ABRE #24;
     #24#0 = #0#6;
     |LEE 101#24=;
     |SI FSalida = 0;
          #24#1 = #0#7;
          #24#17 = #0#21;
          #24#196 = #0#26;
          #24#197 = #0#27;
          #24#25 = #0#10;
          #24#26 = #0#11;
          |GRABA 020#24a;

          |DDEFECTO #30;
          |ABRE #30;
          #30#0 = #24#0;
          #30#1 = 1;
          |LEE 101#30=;
          |SI FSalida ! 0; |GRABA 020#30b; |FINSI;
          #30#13 = #24#17;
          #30#20 = #24#196;
          #30#24 = #24#197;
          #30#14 = #24#25;
          |GRABA 020#30a;
          |CIERRA #30;
     |FINSI;
     |CIERRA #24;

     |ABRE #4;
     #4#0 = #0#29;
     |LEE 101#4=;
     |SI #0#29 = 0; |O FSalida ! 0;
          |LEE 000#4u;
          |SI FSalida = 0
               nConta = #4#0 + 1;
          |SINO;
               nConta = 1;
          |FINSI;
          #4#0 = nConta;
          |GRABA 020#4b;
          #0#29 = #4#0;
     |FINSI;
     #4#1 = #0#0;
     #4#2 = #0#1;
     #4#3 = #0#6;
     #4#4 = #0#7;
     #4#5 = #0#8;
     #4#6 = #0#9;
     #4#7 = #0#10;
     #4#8 = #0#11;
     #4#9 = #0#12;
     #4#10 = #0#13;
     #4#11 = #0#14;
     #4#12 = #0#15;
     #4#13 = #0#16;
     #4#14 = #0#17;
     #4#15 = #0#18;
     #4#16 = #0#19;
     #4#17 = #0#21;
     #4#18 = #0#22;
     #4#19 = #0#23;
     #4#20 = #0#24;
     #4#21 = #0#25;
     #4#22 = #0#26;
     #4#23 = #0#27;
     #4#24 = #0#32;
     |GRABA 020#4a;
     |CIERRA #4;

     enModo = (FEntrada / 100) ! 0;
     eaMaquina = #0#0;
     eaAlfa = #0#1;
     eaArticulo = #0#20;
     |SUB_EJECUTA_NP "cafe0026;GESIART";
|FPRC;

|PROCESO BusEmpresa;
     |DFICE aEmpGes; aEmpGes = aEmpGes % -205
     aEmp = "";
     |DBASS aBase;
     aAlfa = aBase + "condepo/def/condm000";
     |CARGA_DEF aAlfa, condm000@;
     |SI FSalida = 0;
          aAlfa = aBase + "condepo/def/condm001";
          |CARGA_DEF aAlfa, condm001@;
          |SI FSalida = 0;
               aAlfa = aBase + "condepo/fich/";
               |PATH_DAT #condm000@#aAlfa#;
               |ABRE #condm000@;
               |LEE 000#condm000@.p;
               |PARA; |SI FSalida = 0; |HACIENDO;
                    aAlfa = aBase + "condepo/fich/" + (("00000" + #condm000@#0) % -105) + "/";
                    |PATH_DAT #condm001@#aAlfa#;
                    |ABRE #condm001@;
                    |LEE 0000#condm001@.p;
                    |SI FSalida = 0;
                         |SI #condm001@#2 = aEmpGes;
                              aEmp = ("00000" + #condm000@#0) % -105;
                         |FINSI;
                    |FINSI;
                    |CIERRA #condm001@;
                    |LEE 000#condm000@.s;
               |SIGUE;
               |CIERRA #condm000@;
               |DESCARGA_DEF #condm001@;
          |SINO;
               |MENAV "0000Error al cargar 'condm001'";
          |FINSI;
          |DESCARGA_DEF #condm000@;
     |SINO;
          |MENAV "0000Error al cargar 'condm000'";
     |FINSI;
|FPRC;

|PROCESO TipoDepo0; |TIPO 0;
     |C_M #0#30, 0, aAlfa;
     |SI aAlfa = "N"; |ACABA; |FINSI;

     |SI #0#30 = 0;
          |AVISO; |ERROR; |ACABA;
     |FINSI;

     |HAZ BusEmpresa;
     |SI aEmp = "";
          |MENAV "0000Empresa m quina no encontrada..";
          |ERROR; |ACABA;
     |FINSI;


     aAlfa = aBase + "condepo/def/condm009.mas";
     |CARGA_DEF aAlfa, condm009@;
     |SI FSalida = 0;
          aAlfa = aBase + "condepo/fich/" + aEmp + "/";
          |PATH_DAT #condm009@#aAlfa#;
          |ABRE #condm009@;
          |LEE 000#condm009@.p;
          |SI FSalida ! 0; |DDEFECTO #condm009@; |FINSI;
          |CIERRA #condm009@;

          |SI #0#30 = 1; #0#31 = #condm009@#1; |FINSI;
          |SI #0#30 = 2; #0#31 = #condm009@#2; |FINSI;
          |SI #0#30 = 3; #0#31 = #condm009@#3; |FINSI;
          |SI #0#30 = 4; #0#31 = #condm009@#4; |FINSI;
          |SI #0#30 = 5; #0#31 = #condm009@#5; |FINSI;
          |SI #0#30 = 6; #0#31 = #condm009@#6; |FINSI;
          |SI #0#30 = 7; #0#31 = #condm009@#7; |FINSI;
          |PINTA #0#31;

          |DESCARGA_DEF #condm009@;
     |FINSI;
|FPRC;

|PROCESO TipoDepo7; |TIPO 7;
     nModo = (FEntrada / 100) ! 0;

     |DBASS aAlfa;
     aAlfa = aAlfa + "condepo/def/condm009.mas";
     |XABRE "E", aAlfa, 0;
     |SI FSalida ] 0; |Y nModo = 1;
          |C_M #0#30, 1, "S"; |PINTA #0#30;
     |SINO;
          |C_M #0#30, 1, "N"; |PINTA #0#30;
     |FINSI;
|FPRC;

|PROCESO TipoDepo66; |TIPO 66;
     |HAZ BusEmpresa;
     |SI aEmp = "";
          |MENAV "0000Empresa m quina no encontrada..";
          |ERROR; |ACABA;
     |FINSI;

     aAlfa = aBase + "condepo/def/condm009.mas";
     |CARGA_DEF aAlfa, condm009@;
     |SI FSalida = 0;
          aAlfa = aBase + "condepo/fich/" + aEmp + "/";
          |PATH_DAT #condm009@#aAlfa#;
          |ABRE #condm009@;
          |LEE 000#condm009@.p;
          |SI FSalida ! 0; |DDEFECTO #condm009@; |FINSI;
          |CIERRA #condm009@;

          aPopup1 = "0"
          aPopup2 = "7"
          aPopup3 =  "1.-" + #condm009@#1;
          aPopup4 =  "2.-" + #condm009@#2;
          aPopup5 =  "3.-" + #condm009@#3;
          aPopup6 =  "4.-" + #condm009@#4;
          aPopup7 =  "5.-" + #condm009@#5;
          aPopup8 =  "6.-" + #condm009@#6;
          aPopup9 =  "7.-" + #condm009@#7;
          IaPopup = aPopup1<-;
          |ESPECIFICA "TRACKPOPUP", aPopup;
          |SI FSalida ! 0;
               IaPopup = aPopup2<-;
               IaPopup  = IaPopup + FSalida;
               #0Campo = aPopup; |PINTA #0Campo;
          |FINSI;

          |DESCARGA_DEF #condm009@;
     |FINSI;
|FPRC;
