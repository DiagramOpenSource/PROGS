|FICHEROS;
     clipr001 #1;
     clipr007 #7;
     cliprw04 #4,MANTE;
     agifa019 #19;
     dsm00020 #20;
     agifa024 #24;
     agifa025 #25;
     agifa069 #69;
     clipr076 #76;  || Delega
     clipr078 #78;  || Delega/Serie
     clipr081 #81;  || Historico
     agifa084 #84;
     agifa142 #142;
     agifa321 #321;
     agifa324 #324;
     agifa220 #220;
     dsm00053 #53;
|FIN;

|VARIABLES;
     &aCon_Serie = "";
     &aCon_Clave = "";
     &nCon_Conta = 0;
     &aDivisa = "";
     &aMoneda = "";
     aNom = "";
     nTipoFac = 0;
     aTxt = "";
     nTotal = 0;
     nFacturado = 0;
     nLin = 0;
     nError = 0;
     nNume = 0;
     aFactu = "";
     nEspe = 0;
     nPorce = 0;
     aDes = "";
     aFactura = "";
     aTxtRepre = "";
     aRepre = "";
     i = 0;
     aAlfa = "";

     mes = 0;
     fmes = "";
     imneto = 0;
     retencion = 0;
     &serie = "";
     &numero = 0;
     &reimpr = "";
|FIN;

|INCLUYE i_varart;

|PROCESO ActRepre;
     |ABRE #25;
     #25#0 = #84#6;
     |LEE 121#25=;
     |SI 0 = FSalida;
          imneto = #84#15 - #84#25;
          fmes = #84#1 % A402;
          mes = fmes - 1;
          mes = mes * 2;
          mes = mes + 11;
          #25mes = #25mes +. #84#26;
          retencion = #25mes % #25#39;
          mes = mes + 1;
          #25mes = #25mes +. imneto;
          #25#35 = #25#35 +. #84#26;
          #25#36 = #25#36 +. imneto;
          mes = fmes - 1;
          mes = mes + 40;
          #25mes = retencion;
          retencion = #25#35 % #25#39;
          #25#52 = retencion;
          |GRABA 020#25a;
          |LIBERA #25;

          enImpRepComi = #84#26;
          enImpRepVta = imneto;
          enImpRepRete = retencion;
          eaRepre = #84#6;
          efFecha = #84#1;
          |HAZ AcumulaRep;
     |FINSI;
|FPRC;

|PROCESO DesAmpli;
     #53#0 = "F";
     #53#1 = #69#20;
     #53#2 = #69#0;
     #53#3 = #69#1;
     #53#4 = #220#1;
     #53#5 = #220#2;
     |GRABA 020#53n;
|FPRC;

|DEFBUCLE DesAmpli;
     #220#1;
     ;
     #69#2, 0001;
     #69#2, 9999;
     ;
     NULL, DesAmpli;
|FIN;

|PROCESO LinFactu;
     |DDEFECTO #19;
     |ABRE #19;
     #19#0 = #78#3;
     |LEE 000#19=;
     |CIERRA #19;

     |ABRE #53;
     |SI aFactu = "N";
          nPorce = (#4#4 * 100 / #4#2) ! 3;
          |SI nPorce = 100;
               aDes = #19#1;
          |SINO;
               aDes = "Anticipo de " + nPorce + "% " + #19#1;
          |FINSI;
          |DDEFECTO #69;
          #69#20 = #84#48;
          #69#0 = #84#0;
          #69#1 = 1;
          #69#2 = #78#3;
          #69#3 = 1;
          #69#4 = aDes;
          #69#5 = 1;
          #69#6 = #4#4;
          #69#11 = #7#11;
          #69#13 = #19#2;
          #69#15 = #84#3;
          #69#16 = #25#7;
          #69#17 = #24#4;
          #69#26 = #69#6;
          #69#28 = #69#6;
          |HAZ TotalLin69;
          enForma = 1;
          |HAZ AcumulaFC;
          #69#14 = enCoste;
          |HAZ CalCabAgifa084;
          |BUCLE DesAmpli;
          |GRABA 020#69n;
     |SINO;    || aFactu = P
          |DDEFECTO #69;
          #69#20 = #84#48;
          #69#0 = #84#0;
          #69#1 = 1;
          #69#2 = #78#3;
          #69#3 = 1;
          #69#4 = #19#1;
          #69#5 = 1;
          #69#6 = #4#2;
          #69#11 = #7#11;
          #69#13 = #19#2;
          #69#15 = #84#3;
          #69#16 = #25#7;
          #69#17 = #24#4;
          #69#26 = #69#6;
          #69#28 = #69#6;
          |HAZ TotalLin69;
          enForma = 1;
          |HAZ AcumulaFC;
          #69#14 = enCoste;
          |HAZ CalCabAgifa084;
          |BUCLE DesAmpli;
          |GRABA 020#69n;

          nPorce = (nFacturado * 100 / #4#2) ! 3;
          |SI nPorce = 100;
               aDes = "Seg£n factura " + aFactura;
          |SINO;
               aDes = "Anticipo del " + nPorce + "% seg£n factura " + aFactura;
          |FINSI;
          |DDEFECTO #69;
          #69#20 = #84#48;
          #69#0 = #84#0;
          #69#1 = 2;
          #69#2 = #78#3;
          #69#3 = 1;
          #69#4 = aDes;
          #69#5 = -1;
          #69#6 = nFacturado;
          #69#11 = #7#11;
          #69#13 = #19#2;
          #69#15 = #84#3;
          #69#16 = #25#7;
          #69#17 = #24#4;
          #69#26 = #69#6;
          #69#28 = #69#6;
          |HAZ TotalLin69;
          enForma = 1;
          |HAZ AcumulaFC;
          #69#14 = enCoste;
          |HAZ CalCabAgifa084;
     ||     |BUCLE DesAmpli;
          |GRABA 020#69n;

          #84#95 = 2;
     |FINSI;
     |CIERRA #53;
|FPRC;

|PROCESO LaFactura;
     |ABRE #1;
     #1#0 = #7#3;
     |LEE 000#1=;
     |CIERRA #1;

     |HAZ BuscaRepre;

     |DDEFECTO #24;
     |ABRE #24;
     #24#0 = #1#58;
     |LEE 000#24=;
     |SI FSalida = 0;
          aNom = #24#1;
     |SINO;
          aNom = #1#4;
     |FINSI;
     |CIERRA #24;

     |ABRE #25;
     #25#0 = aRepre;
     |LEE 000#25=;
     |CIERRA #25;

     |ABRE #20;
     #20#0 = #24#35;
     |LEE 000#20=;
     |CIERRA #20;

     |DDEFECTO #84;
     #84#48 = #4#6;
     #84#0 = #4#7;
     |GRABA 000#84b;
     |SI FSalida ! 0;
          |MENAV "0000La factura ya existe....";
          nError = 1;
          |ACABA;
     |FINSI;
     #84#1 = #4#5;
     #84#3 = #24#0;
     #84#4 = aNom;
     #84#6 = #25#0;
     #84#8 = #24#20;
     #84#9 = #20#1;
     #84#29 = #24#1;

     #84#30 = #1#14; ||direccion
     #84#31 = #1#15; ||poblacion
     #84#33 = #1#19; ||provincia

     #84#34 = #25#7;
     #84#35 = #24#4;
     #84#36 = #24#47;
     #84#40 = #1#49; ||cuenta contable
     #84#60 = #1#9;  ||cif
     #84#64 = #1#18;
     #84#65 = #24#192;

     |ABRE #321; |LEE 000#321p; |CIERRA #321;
     |ABRE #324;
     #324#0 = #84#65;
     |LEE 000#324=;
     #84#66 = #324#2;

     #84#67 = #24#193;
     #84#68 = #321#9;

     #324#0 = #84#68;
     |LEE 000#324=;
     #84#69 = #324#2;
     |CIERRA #324;

     #84#78 = #20#0;
     #84#88 = #1#51;
     #84#89 = #1#52;
     #84#90 = #1#53;

     #84#93 = #7#0;
     #84#94 = ("0000000" + #7#1) % -107;

     aDivisa = #84#65;
     aMoneda = #84#67;
     |HAZ Divisas084;

     |HAZ LimCabAgifa084;
     |HAZ LinFactu;
     |GRABA 020#84a;

     |HAZ ActRepre;
     |HAZ VenciDir;
|FPRC;

|PROCESO ContaFac0; |TIPO 0;
|FPRC;

|PROCESO ContaFac7; |TIPO 7;
     |ABRE #84;
     |DDEFECTO #84;
     aCon_Serie = #4#6;
     #84#48 = #4#6;
     |SI #142#137 = "F";
          aCon_Clave = "facturas";
          |HAZ ContadorFicClip;
     |SINO;
          #84#48 = aCon_Serie;
          #84#0 = 999999;
          |LEE 000#agifa084.];
          |SI FSalida = 0;
               |LEE 000#agifa084.a;
          |SINO;
               |LEE 000#agifa084.u;
          |FINSI;
          |SI FSalida = 0; |Y #84#48 = aCon_Serie;
               nCon_Conta = #84#0 + 1;
          |SINO;
               nCon_Conta = 1;
          |FINSI;
     |FINSI;
     |CIERRA #84;
     #4#7 = nCon_Conta; |PINTA #4#7;
|FPRC;

|PROCESO Delegacion; |TIPO 0;
|FPRC;

|PROCESO AFacturar; |TIPO 0;
     |SI #4#4 > #4#2;
          |MENAV "0000Se supera el importe total...";
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO Facturado;
     nFacturado = nFacturado + #81#7;
     aFactura = #81#5 + "/" + (("000000" + #81#6) % -106);
|FPRC;

|DEFBUCLE Facturado;
     #81#1;
     ;
     #7#0, #7#1, nTipoFac, 1;
     #7#0, #7#1, nTipoFac, 99999;
     ;
     NULL, Facturado;
|FIN;

|PROCESO FactuInicial;
     nTipoFac = FSalida;
     nError = 0;
     |SI nTipoFac = 1;
          aTxtRepre = "SEGURIDAD INICIAL";
          aTxt = "SEGURIDAD ";
          nEspe = #7#102;
          nTotal = #7#68;
          aFactu = #7#90;
     |FINSI;
     |SI nTipoFac = 2;
          aTxtRepre = "ERGONOMIA INICIAL";
          aTxt = "ERGONOMIA ";
          nEspe = #7#103;
          nTotal = #7#70;
          aFactu = #7#91;
     |FINSI;
     |SI nTipoFac = 3;
          aTxtRepre = "HIGIENE";
          aTxt = "HIGIENE ";
          nEspe = #7#104;
          nTotal = #7#72;
          aFactu = #7#92;
     |FINSI;
     |SI nTipoFac = 4;
          aTxtRepre = "PISICOLOGIA";
          aTxt = "PISICOLOGIA ";
          nEspe = #7#105;
          nTotal = #7#73;
          aFactu = #7#93;
     |FINSI;
     |SI nTipoFac = 5;
          aTxtRepre = "PLAN PREVENCION";
          aTxt = "PLAN PREVENCION ";
          nEspe = #7#106;
          nTotal = #7#75;
          aFactu = #7#94;
     |FINSI;

     |SI aFactu = "S";
          nError = 1;
          |MENAV "0000Evaluacion inicial facturada...";
          |ACABA;
     |FINSI;

     |DDEFECTO #78;
     |ABRE #78;
     #78#0 = #7#112;
     #78#1 = nEspe;
     |LEE 000#78=;
     |SI FSalida ! 0;
          |MENAV "0000No existe relacion delegacion/especialidad/serie";
          |ERROR; |ACABA;
     |FINSI;
     |CIERRA #78;

     nFacturado = 0;
     |C_M #4#4, 1, "S";
     |SI aFactu = "P";
          aFactura = "";
          |BUCLE Facturado;
          |C_M #4#4, 1, "N";
     |FINSI;

     |ABRE #76;
     #76#0 = #7#112;
     |LEE 020#76=;
     |CIERRA #76;

     |DDEFECTO #4;
     #4#2 = nTotal;
     #4#3 = nFacturado;
     |SI aFactu = "P";
          #4#4 = nTotal - nFacturado;
     |SINO;
          #4#4 = nTotal * #7#12 / 100;
     |FINSI;
     #4#6 = #78#2;
     #4#0 = #76#0;
     #4#1 = #76#1;

     |PUSHV 0501 2380;
     |BLANCO 1113 2367;
     |PINPA #0#4;
     |ATRI R; |PINTA 1246, aTxt;
     |PINDA #0#4;
     |ENDATOS #1#4;
     |SI FSalida = 0;
          |ABRE #142; |LEE 000#142p; |CIERRA #142;
          |ABRE #84;
          |ABRE #69;

          |HAZ LaFactura;
          |SI nError = 0;
               |ABRE #81;
               #81#0 = #7#0;
               #81#1 = #7#1;
               #81#2 = nTipoFac;
               #81#3 = 99999;
               |LEE 000#81];
               |SI FSalida ! 0;
                    |LEE 000#81u;
               |SINO;
                    |LEE 000#81a;
               |FINSI;
               |SI FSalida = 0; |Y #81#2 = nTipoFac; |Y #81#1 = #7#1; |Y #81#0 = #7#0;
                    nLin = #81#3 + 1;
               |SINO;
                    nLin = 1;
               |FINSI;

               #81#0 = #7#0;
               #81#1 = #7#1;
               #81#2 = nTipoFac;
               #81#3 = nLin;
               #81#4 = aTxt;
               #81#5 = #84#48;
               #81#6 = #84#0;
               #81#7 = #4#4;
               #81#8 = #84#1;
               |GRABA 020#81n;

               nNume = nTotal - nFacturado;
               |SI nNume = #4#4;
                    aFactu = "S";
               |SINO;
                    |SI aFactu = "P";   || Si se ha facturado antes algo
                         aFactu = "S";  || porque solo se crean 2 facturas max
                    |SINO;
                         aFactu = "P";
                    |FINSI;
               |FINSI;

               |SI nTipoFac = 1; #7#90 = aFactu; |FINSI;
               |SI nTipoFac = 2; #7#91 = aFactu; |FINSI;
               |SI nTipoFac = 3; #7#92 = aFactu; |FINSI;
               |SI nTipoFac = 4; #7#93 = aFactu; |FINSI;
               |SI nTipoFac = 5; #7#94 = aFactu; |FINSI;
               |GRABA 020#7a;

               |CONFI "0000NImprimir factura?";
               |SI FSalida = 0;
                    |LIBERA #69;
                    |LIBERA #84;
                    serie = #84#48;
                    numero = #84#0;
                    reimpr = #84#10;
                    |SUB_EJECUTA_NP "agifa086";
               |FINSI;
          |FINSI;
          |CIERRA #69;
          |CIERRA #84;
     |FINSI;
     |POPV;

     |PINTA #7#90;
     |PINTA #7#91;
     |PINTA #7#92;
     |PINTA #7#93;
     |PINTA #7#94;
|FPRC;

|PROCESO BuscaRepre;
     aRepre = "00";
     |PARA i = 66; |SI i [ 126; |HACIENDO i = i + 5;
          aAlfa = #1i;
          aAlfa = aAlfa - aTxtRepre;
          |SI FSalida > 0;
               i = i + 3;
               aRepre = #1i;
               |SAL;
          |FINSI;
     |SIGUE;
|FPRC;
