|FICHEROS;
     malpe165 #0;
     malpe154 #154; || Tabajadores
     malpe156 #156; || Partes
     malpe158 #158; || Fabricas
     malpe166 #166,MANTE; || Tmp1
     malpe167 #167,MANTE; || Tmp2
|FIN;

|VARIABLES;
     fDia = @;
     &Tempo = "";
     nForma = 0;
     nModo = 0;
     aDia = "";
     aTmp166 = "";
     aTmp167 = "";
     aTraba = "";
     i = 0;
     nDiasMes = 0;
     fFecha = @;
     aDFecha = "";
     aHFecha = "";
     aMes = "";
     aA¤o = "";
     aAlfa = "";
     nDia = 0;
     nCampo = 0;
|FIN;

|PROCESO CheqHora; |TIPO 0;
     |SI #167#4 ! 0; |Y #167#5 ! 0;
          |MENAV "0000No se puede indicar de los dos tipos de horas en un mismo parte";
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO BorraHoras;
     |BORRA 020#167a;
     aDia = (("00" + #166#3) % -102) + "." + (("00" + #0#3) % -102) + "." + #0#2;
     |DDEFECTO #156;
     #156#0 = #167#0;
     #156#1 = aDia;
     #156#2 = #167#1;
     #156#3 = #167#2;
     |BORRA 000#156c;
|FPRC;

|DEFBUCLE BorraHoras;
     #167#1;
     ;
     #166#0, #166#2, #166#4, 01;
     #166#0, #166#2, #166#4, 99;
     be;
     NULL, BorraHoras;
|FIN;

|PROCESO Min167;
     #167#0 = #166#0;
     #167#1 = #166#2;
     #167#2 = #166#4;
     #167#3 = 1;
|FPRC;

|PROCESO Max167;
     #167#0 = #166#0;
     #167#1 = #166#2;
     #167#2 = #166#4;
     #167#3 = nDiasMes;
|FPRC;

|PROCESO Min166;
     #166#0 = 000000;
     #166#2 = 00;
     #166#4 = " ";
|FPRC;

|PROCESO Max166;
     #166#0 = 999999;
     #166#2 = 99;
     #166#4 = "z";
|FPRC;

|PROCESO TipoHora; |TIPO 7;
     |SI #166#4 = "T"; #166#5 = "Trabajadas"; |FINSI;
     |SI #166#4 = "L"; #166#5 = "Licenciadas"; |FINSI;
     |SI #166#4 = "J"; #166#5 = "Sin Justificante"; |FINSI;
     |SI #166#4 = "S"; #166#5 = "Sabado Tarde"; |FINSI;
     |PINTA #166#5;
|FPRC;

|PROCESO DetalleTipo2; |TIPO 2;
     #166#6 = #166#6 +. (#167#4 + #167#5);

     aDia = (("00" + #167#3) % -102) + "." + (("00" + #0#3) % -102) + "." + #0#2;
     |DDEFECTO #156;
     #156#0 = #167#0;
     #156#1 = aDia;
     #156#2 = #167#1;
     #156#3 = #167#2;
     |LEE 101#156=;
     |SI FSalida ! 0; |GRABA 020#156b; |FINSI;
     |SI #167#4 ! 0;
          #156#4 = #167#4;
          #156#5 = 1;
     |FINSI;
     |SI #167#5 ! 0;
          #156#4 = #167#5;
          #156#5 = 2;
     |FINSI;
     |SI nModo = 3;
          |BORRA 020#156c;
     |SINO;
          |GRABA 020#156a;
          |LIBERA #156;
     |FINSI;
|FPRC;

|PROCESO Tipo2; |TIPO 2;
     nForma = 0 +. 1;
     nModo = (FEntrada / 100) ! 0;
     |SI nForma = 1;
          |PUSHV 0501 2380;
          |ENTCOLUMNA #1#167, 4, 2, 7, 1, Min167, Max167;
          |POPV;
          |PINTA #166#6;
     |FINSI;
     |SI nModo = 3;
          |BUCLE BorraHoras;
     |FINSI;
|FPRC;

|PROCESO Partes;
     |DDEFECTO #154;
     #154#0 = #156#0;
     |LEE 000#154=;
     |DDEFECTO #158;
     #158#0 = #156#2;
     |LEE 000#158=;

     |DDEFECTO #166;
     #166#0 = #154#0;
     #166#2 = #158#0;
     #166#4 = #156#3;
     |LEE 101#166=;
     |SI FSalida ! 0; |GRABA 020#166b; |FINSI;
     #166#1 = #154#1;
     #166#3 = #158#1;
     #166#4 = #156#3;
     |SI #156#3 = "T"; #166#5 = "Trabajadas"; |FINSI;
     |SI #156#3 = "L"; #166#5 = "Licenciadas"; |FINSI;
     |SI #156#3 = "J"; #166#5 = "Sin Justificante"; |FINSI;
     |SI #156#3 = "S"; #166#5 = "Sabado Tarde"; |FINSI;
     #166#6 = #166#6 + #156#4;
     |GRABA 020#166a;
     |LIBERA #166;

     aDia = #156#1 % 102;
     |DDEFECTO #167;
     #167#0 = #166#0;
     #167#1 = #166#2
     #167#2 = #166#4;
     #167#3 = aDia;
     |LEE 101#167=;
     |SI FSalida ! 0; |GRABA 020#167b; |FINSI;
     |SI #156#5 = 1;
          #167#4 = #167#4 + #156#4;
     |SINO;
          #167#5 = #167#5 + #156#4;
     |FINSI;
     fDia = #156#1 % 1;
     aDia = fDia % 1101;
     #167#6 = aDia;
     |GRABA 020#167a;
     |LIBERA #167;
|FPRC;

|DEFBUCLE Partes;
     #156#1;
     ;
     #0#0, aDFecha, 00, " ";
     #0#1, aHFecha, 99, "z";
     ;
     NULL, Partes;
|FIN;

|PROCESO Defecto; |TIPO 7;
     |SI #0#3 = 0;
          fFecha = ~;
          aAlfa = fFecha % 402;
          #0#3 = aAlfa;
          aAlfa = fFecha % 704;
          #0#2 = aAlfa;
          |PINTA #0#2;
          |PINTA #0#3;
     |FINSI;
|FPRC;

|PROCESO SacaMes;
     aMes = aAlfa % 402;
     aA¤o = aAlfa % 704;
     aDFecha = "01." + aMes + "." + aA¤o;
     aHFecha = "27." + aMes + "." + aA¤o;
     |PARA; |SI; |HACIENDO;
          fFecha = aHFecha;
          fFecha = fFecha + 1;
          aAlfa = fFecha % 402;
          |SI aMes ! aAlfa; |SAL; |FINSI;
          aHFecha = fFecha;
     |SIGUE;
|FPRC;

|PROCESO LlenaDias;
     |PARA i = 1; |SI i [ nDiasMes; |HACIENDO i = i + 1;
          |DDEFECTO #167;
          #167#0 = #166#0;
          #167#1 = #166#2;
          #167#2 = #166#4;
          #167#3 = i;
          |LEE 000#167=;
          aDia = (("00" + i) % -102) + "." + (("00" + #0#3) % -102) + "." + #0#2;
          fDia = aDia;
          fDia = fDia % 1;
          aDia = fDia % 1101;
          #167#6 = aDia;
          |SI FSalida ! 0; |GRABA 020#167n; |FINSI;
     |SIGUE;
|FPRC;

|DEFBUCLE LlenaDias;
     #166#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, LlenaDias;
|FIN;

|PROCESO Tipo3; |TIPO 3;
     aAlfa = "01." + (("00" + #0#3) % -102) + "." + (("0000" + #0#2) % -104);
     |HAZ SacaMes;
     |PARA fFecha = aDFecha; |SI fFecha [ aHFecha; |HACIENDO fFecha = fFecha + 1;
          nDiasMes = nDiasMes + 1;
     |SIGUE;

     |HAZ CreaTempo; aTmp166 = Tempo; |NOME_DAT #166#Tempo#; |DELINDEX #166;
     |HAZ CreaTempo; aTmp167 = Tempo; |NOME_DAT #167#Tempo#; |DELINDEX #167;

     |ABRE #166;
     |ABRE #167;
     |ABRE #154;
     |ABRE #158;
     |BUCLE Partes;
     |CIERRA #158;
     |CIERRA #154;
     |CIERRA #166;
     |BUCLE LlenaDias;
     |CIERRA #167;

     |ABRE #156;
     |ENTLINEAL #1#166, 1, 2, 22, 1, Min166, Max166;
     |CIERRA #156;

     |DELINDEX #166; aTmp166 = Tempo; |HAZ BoraTempo;
     |DELINDEX #167; aTmp167 = Tempo; |HAZ BoraTempo;
|FPRC;
