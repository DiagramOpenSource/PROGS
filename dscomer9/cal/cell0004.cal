|FICHEROS;
     cell0004 #0;
     cell0001 #1;
     cell0002 #2;
|FIN;

|VARIABLES;
     nPos      = 0;
     nCam      = 0;
     i         = 0;
     aLon      = "";
     aTab      = "";
     nBoton1   = 0;
     nHandle   = 0;
     nConta    = 0;
     nLin      = 0;
     aRuta     = "";
     aReg      = "";
     aCb       = "";
     aFic      = "";
     aArt      = "";
     aDes      = "";
     aAlfa     = "";
|FIN;

|PROCESO Graba;
     nCam = 0; aCb = ""; aArt = ""; aDes = "";
     aLon = aReg % A0;
     |PARA i = 1; |SI i [ aLon; |HACIENDO i = i + 1;
          nPos = i * 100 + 1;
          aAlfa = aReg % nPos;
          |SI aAlfa = aTab;
               nCam = nCam + 1;
          |SINO;
               |SI nCam = 0; aCb  = aCb  + aAlfa; |FINSI;
               |SI nCam = 1; aArt = aArt + aAlfa; |FINSI;
               |SI nCam = 2; aDes = aDes + aAlfa; |FINSI;
          |FINSI;
     |SIGUE;

     |SI nConta = 0;
          |LEE 000#1u;
          |SI FSalida = 0;
               nConta = #1#0 + 1;
          |SINO;
               nConta = 1;
          |FINSI;
          |DDEFECTO #1;
          #1#0 = nConta;
          #1#1 = ~;
          |GRABA 020#1n;
     |FINSI;
     |DDEFECTO #2;
     nLin = nLin + 1;
     #2#0 = nConta;
     #2#1 = nLin;
     #2#2 = aArt;
     #2#3 = aCb;
     #2#4 = aDes;
     #2#5 = "S";
     |GRABA 020#2n;
|FPRC;

|PROCESO Tipo3; |TIPO 3;
     aFic = #0#0; |QBF aFic;
     |XABRE "U", aFic, nHandle;
     |SI FSalida < 0;
          |MENAV "0000No puedo crear el fichero...";
     |SINO;
          |ABRE #1;
          |ABRE #2;
          |PARA; |SI; |HACIENDO;
               |XLEE nHandle, 250, aReg;
               |SI FSalida < 0; |SAL; |FINSI;
               |HAZ Graba;
          |SIGUE;
          |XCIERRA nHandle;
          |CIERRA #1;
          |CIERRA #2;
          aAlfa = "0000Importado, Registro: " + (("000000" + nConta) % -106);
          |MENAV aAlfa;
     |FINSI;
|FPRC;

|PROCESO Boton1;
     |PTEC 824;
|FPRC;

|PROCESO Browser;
     |SI FSalida ! 10; |ACABA; |FINSI;

     aRuta = #0#0;  |QBF aRuta;
     |SI aRuta = "";
         aRuta = "\*.*";
     |FINSI;
     aRuta = "F" + aRuta;

     |BROWSE aRuta;

     aRuta = aRuta % 299;
     #0#0 = aRuta;
     |PINTA #0#0;
|FPRC;

|PROCESO Quita; |TIPO 0;
     |ESTADO_CONTROL nBoton1, "DISABLE";
|FPRC;

|PROCESO Pone; |TIPO 7;
     |ESTADO_CONTROL nBoton1, "ENABLE";
|FPRC;

|PROGRAMA;
     aTab = &9;
     |CONTROL_SIMPLE 0, "BOTON, Fichero", 1220, 1227, Boton1;
     nBoton1 = FSalida;
     |HAZ Quita;

     |CLS;
     |ABRE #0;
     |LEE 101#0p;
     |SI FSalida ! 0; |GRABA 020#0b; |FINSI;
     |PINPA #0#0;
     |PINDA #0#0;
     |ENDATOS #1#0;
     |SI FSalida = 0;
          |HAZ Tipo3;
          |GRABA 020#0a;
     |FINSI;
     |CIERRA #0;

     |FIN_CONTROL_WINDOWS nBoton1;
     |PULSATECLA;
|FPRO;
