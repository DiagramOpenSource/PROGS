|FICHEROS;
     aparz001 #0;

     agifa010 #10;
     otroref@ #999;
|FIN;

|VARIABLES;
     aMensaje = "";
     aInforme = "";
     nSwPie = 0;

     &enNumDocErp = 0;

     nSwCargaDef = 0;
     nDocAsociados = 0;
     aAplicaErp = "";
     nEmpErp = 0;
     aDefErp = "";
     aClaveErp = "";
     aCien = "";
     aBass = "";
     aErp = "";
     aPathErp = "";
|FIN;

|PROCESO DocErp;
     nDocAsociados = nDocAsociados + 1;
|FPRC;

|DEFBUCLE DocErp;
 #otroref@#1005;
 ;
 aAplicaErp, nEmpErp, aDefErp, aClaveErp, 01;
 aAplicaErp, nEmpErp, aDefErp, aClaveErp, 99;
 ;
 NULL, DocErp;
|FIN;

|PROCESO agifa010;
     ||Cliente
     |SI #agifa010#3 < #aparz001#4; |O #agifa010#3 > #aparz001#5;
          |ACABA;
     |FINSI;

     ||Fecha
     |SI #agifa010#1 < #aparz001#6; |O #agifa010#1 > #aparz001#7;
          |ACABA;
     |FINSI;

     ||Facturados
     |SI #aparz001#9 = "S";
          |SI #agifa010#38 ! "S"; |ACABA; |FINSI;
     |SINO;
          |SI #aparz001#9 = "N";
               |SI #agifa010#38 ! "N"; |ACABA; |FINSI;
          |FINSI;
     |FINSI;

     ||Numero de Documentos.
     nDocAsociados = 0;
     |SI nSwCargaDef = 1;
          aCien = " " * 100;
          nEmpErp = #48#0;
          aAplicaErp = "dscomer9";
          aDefErp = "agifa010";
          aClaveErp = #agifa010#52 + ("000000" + #agifa010#0) % -106;
          |QBF aClaveErp;
          aClaveErp = (aClaveErp + aCien) % 199;
          aClaveErp = aClaveErp + " ";
          |BUCLE DocErp;
     |FINSI;

     |SI #aparz001#8 = "S";
          |SI nDocAsociados = 0; |ACABA; |FINSI;
     |SINO;
          |SI #aparz001#8 = "N";
               |SI nDocAsociados ! 0; |ACABA; |FINSI;
          |FINSI;
     |FINSI;

     enNumDocErp = nDocAsociados;
     |PRINT;
     nSwPie = 1;
|FPRC;

|DEFBUCLE agifa010;
 #agifa010#1;
 ;
 #aparz001#0, #aparz001#2;
 #aparz001#1, #aparz001#3;
 ;
 NULL, agifa010;
|FIN;

|PROCESO CargaDef;
     |DBASS aBass;
     |QBF aBass;
     aErp = aBass + "dserp/def/dserpm09.mas";
     |XABRE "E", aErp, 0;
     |SI FSalida ] 0;
          |CARGA_DEF aErp, otroref@;
          |SI FSalida = 0;
                 aPathErp = aBass + "dserp/fich/";
                 |PATH_DAT #999 aPathErp;
                 nSwCargaDef = 1;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Principal;
     nSwCargaDef = 0;
     |IMPRE 0;
     |SI FSalida = 0;
          aInforme = "aparz001";
          |INFOR aInforme;
          |SI FSalida = 0;
               |HAZ CargaDef;
               |BUCLE agifa010;
               |SI nSwCargaDef = 1;
                    |DESCARGA_DEF #otroref@;
               |FINSI;
               |SI nSwPie = 1;
                    |PIEINF;
               |FINSI;
               |FININF;
          |SINO;
               aMensaje = "0000Informe " + aInforme + " inaccesible.";
               |MENAV aMensaje;
          |FINSI;
          |FINIMP;
     |SINO;
          |MENAV "0000Proceso cancelado.";
     |FINSI;
|FPRC;

|PROGRAMA;
     |CLS;
     |PINPA #0#0;
     |PINDA #0#0;
     |ENDATOS #1#0;
     |SI FSalida = 0;
          |HAZ Principal;
     |FINSI;
|FPRO;
