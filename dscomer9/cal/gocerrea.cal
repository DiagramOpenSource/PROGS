|FICHEROS;
     gocerrea #0;
     gocerrec #1,MANTE;
     gocerrel #2,MANTE;
     gomantho #3;
     gomantli #4;
     gopre001 #5;
     gopre003 #6;
     agifa007 #7;
     gocerhis #9;
     gocerdes #10;
     agifa024 #24;
     agifa025 #25;
     agifa084 #84;
     agifa091 #91;
     goparame #100;
     agifa142 #142;
     gocercab #200;
     referen@;
|FIN;

|VARIABLES;
     &serie = "";
     &numero = 0;
     &enSwPrint = 0;
     &enError = 0;
     &eaSerie = "";
     &enNuPre = 0;
     &enLinea = 0;
     &enTiva = 0;
     &efFiva = @;
     &eaDiva = "";
     &cCodMonTrab = "";
     &Tempo = "";
     nModo = 0;
     nForma = 0;
     aAlfa = "";
     nSwCheq = 0;
     aGuarda = 0;
     aSwFac = "";
     nHay = 0;
     aSerOE = "";
     nNumOE = 0;
     nCerti = 0;
     fiac           = "  ";
     guarda         = "  ";
     relleno        = "                                       ";
     aponer         = "  ";
|FIN;

|PROCESO Tipo14; |TIPO 14;
     cCodMonTrab = #0#4;
     |HAZ MonBas;
|FPRC;

|PROCESO ExpeLin;
     #5#0 = #4#3;
     #5#1 = #4#4;
     |LEE 020#5=;
     |SI FSalida = 0;
          |SALVA_DATOS #5;
          |REPON_DATOS #referen@;
          |GRABA 020#referen@.n;
     |FINSI;
|FPRC;

|DEFBUCLE ExpeLin;
     #4#1;
     ;
     #0#2, #0#3, 001;
     #0#2, #0#3, 999;
     ;
     NULL, ExpeLin;
|FIN;

|PROCESO Tipo0_0; |TIPO 0;
     nModo = (FEntrada / 100) ! 0;
     |SI FSalida = 13;
          |HAZ SelePresu;
     |FINSI;
|FPRC;

|PROCESO SelePresu;
     |DDEF aAlfa;
     aAlfa = aAlfa + "gopre001";
     |CARGA_DEF aAlfa, referen@;
     |SI FSalida = 0;
          |HAZ CreaTempo; |NOME_DAT #referen@#Tempo#; |DELINDEX #referen@;
          |ABRE #referen@;
          |ABRE #5;
          |BUCLE ExpeLin;
          |CIERRA #5;
          |LEE 000#referen@.p;
          |CONSULTA_CLAVE #1#referen@;
          |SI FSalida = 0;
               #0#0 = #referen@#0; |PINTA #0#0;
               #0#1 = #referen@#1; |PINTA #0#1;

               |ABRE #200;
               #200#0 = #0#0;
               #200#1 = #0#1;
               |LEE 000#200=;
               |SI FSalida = 0;
                    |SI nModo = 1;
                         |MENAV "0000ERROR. O.E. introducida por certificaciones...";
                         |ERROR;
                    |FINSI;
               |SINO;
                    |SI nModo = 1; |PTEC 815; |FINSI;
               |FINSI;
               |CIERRA #200;
          |SINO;
               |ERROR;
          |FINSI;
          |CIERRA #referen@;
          |HAZ BoraTempo; |DELINDEX #referen@;
          |DESCARGA_DEF #referen@;
     |FINSI;
|FPRC;

|PROCESO Tipo7_0; |TIPO 7;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 1;
          |HAZ SelePresu;
     |FINSI;
|FPRC;

|PROCESO Tipo0_2; |TIPO 0;
     |SI FSalida ! 13; |ACABA; |FINSI;
     |ABRE #3;
     #3#0 = #0#2;
     #3#1 = #0#3;
     |LEE 000#3];
     |CONSULTA_CLAVE #1#3;
     |SI FSalida = 0;
          #0#2 = #3#0; |PINTA #0#2;
          #0#3 = #3#1; |PINTA #0#3;
     |SINO;
          |ERROR;
     |FINSI;
     |CIERRA #3;
|FPRC;

|PROCESO Minc;
     #1#0 = #0#0;
     #1#1 = #0#1;
     #1#2 = 1;
|FPRC;

|PROCESO Maxc;
     #1#0 = #0#0;
     #1#1 = #0#1;
     #1#2 = 999;
|FPRC;

|PROCESO Minl;
     #2#0 = #1#0;
     #2#1 = #1#1;
     #2#2 = #1#2;
     #2#3 = 1;
|FPRC;

|PROCESO Maxl;
     #2#0 = #1#0;
     #2#1 = #1#1;
     #2#2 = #1#2;
     #2#3 = 999;
|FPRC;

|PROCESO Tipo13a; |TIPO 13;
     |ENTLINEAL #1#1, 3, 7, 13, 0, Minc, Maxc;
     |ENTLINEAL #1#2, 4, 7, 22, 0, Minl, Maxl;
|FPRC;

|PROCESO BorraHis;
     |BORRA 020#9a;
|FPRC;

|DEFBUCLE BorraHis;
     #9#1;
     ;
     #1#0, #1#1, #1#2, 001;
     #1#0, #1#1, #1#2, 999;
     be;
     NULL, BorraHis;
|FIN;


|PROCESO BorraDes;
     |BORRA 020#10a;
|FPRC;

|DEFBUCLE BorraDes;
     #10#1;
     ;
     #1#0, #1#1, #1#2, 001;
     #1#0, #1#1, #1#2, 999;
     be;
     NULL, BorraDes;
|FIN;

|PROCESO Borra2;
     |BORRA 020#2a;
|FPRC;

|DEFBUCLE Borra2;
     #2#1;
     ;
     #1#0, #1#1, #1#2, 001;
     #1#0, #1#1, #1#2, 999;
     be;
     NULL, Borra2;
|FIN;

|PROCESO Borra1;
     |SI nSwCheq = 1;
          |SI #1#8 = "S";
               |MENAV "0000Hay certifiaciones facturadas...";
               |ERROR10;
               enError = 1;
          |FINSI;
     |SINO;
          |BORRA 020#1a;
          |BUCLE Borra2;
          |BUCLE BorraDes;
          |BUCLE BorraHis;
     |FINSI;
|FPRC;

|DEFBUCLE Borra1;
     #1#1;
     ;
     #0#0, #0#1, 001;
     #0#0, #0#1, 999;
     be;
     NULL, Borra1;
|FIN;

|PROCESO Tipo12a; |TIPO 12;
|FPRC;

|PROCESO Tipo2a; |TIPO 2;
     nForma = 0 +. 1;
     nModo = (FEntrada / 100) ! 0;
     |ABRE #100; |LEE 000#100p; |CIERRA #100;
     |ABRE #142; |LEE 000#142p; |CIERRA #142;
     |SI nModo = 3;
          enError = 0;
          nSwCheq = 1; |BUCLE Borra1;
          |SI enError = 0;
               nSwCheq = 0; |BUCLE Borra1;
          |SINO;
               |ERROR;
          |FINSI;
     |SINO;
          |SI nForma = 1;
               |HAZ Tipo14;
               |ENTLINEAL #1#1, 3, 2, 13, 0, Minc, Maxc;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO CheqPres; |TIPO 0;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 1;
          |ABRE #4;
          #4#0 = #0#2;
          #4#1 = #0#3;
          #4#3 = #0#0;
          #4#4 = #0#1;
          |LEE 000#4=;
          |SI FSalida ! 0;
               |MENAV "ERROR. La O.E. no pertenece a la O.T. ...";
               |ERROR;
          |FINSI;
          |CIERRA #4;

          |ABRE #200;
          #200#0 = #0#0;
          #200#1 = #0#1;
          |LEE 000#200=;
          |SI FSalida = 0;
               |MENAV "0000ERROR. O.E. introducida por certificaciones...";
               |ERROR;
          |FINSI;
          |CIERRA #200;
     |FINSI;
|FPRC;

|PROCESO LinCer7; |TIPO 7;
     |MENSA "0000<F2> Copiar    <F6> Descripcion   <F7> Imprimir";
     |ENTLINEAL #1#2, 4, 7, 22, 0, Minl, Maxl;
|FPRC;

|PROCESO LinCer0; |TIPO 0;
     nModo = (FEntrada / 100) ! 0;
     |SI FSalida = 10;
          |SI nModo = 1;
               |HAZ CopiaCerti;
          |SINO;
               |MENAV "0000Opcion solo disponible en alta...";
          |FINSI;
          |ERROR;
     |FINSI;
     |SI FSalida = 14;
          eaSerie = #1#0;
          enNuPre = #1#1;
          enLinea = #1#2;
          |SUB_EJECUTA_NP "gocerdes";
          |ERROR;
     |FINSI;
     |SI FSalida = 15;
          |CONFI "0000N¿Imprimir certificación ?";
          |SI FSalida = 0;
               |HAZ ImpCerti;
          |FINSI;
          |SI #1#8 = "S";
               |CONFI "0000N¿Imprimir factura ?";
               |SI FSalida = 0;
                    serie = #1#14;
                    numero = #1#15;
                    |SUB_EJECUTA_NP "agifa086";
               |FINSI;
          |FINSI;
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO Tipo2c; |TIPO 2;
     nModo = (FEntrada / 100) ! 0;
     #1#3 = #0#2;
     #1#4 = #0#3;
     |SI nModo = 3;
          |SI #1#8 = "S";
               |MENAV "0000Certificación facturada...";
               |ERROR;
          |SINO;
               |BUCLE Borra2;
               |BUCLE BorraDes;
               |HAZ Actualiza;
               |BUCLE BorraHis;
          |FINSI;
     |SINO;
          |HAZ GocerHis;
          |HAZ Actualiza;
     |FINSI;
|FPRC;

|PROCESO Actualiza;
     |ABRE #9;
     #9#0 = #1#0;
     #9#1 = #1#1;
     #9#2 = #1#2;
     #9#3 = 1;
     |LEE 121#9=;
     |SI FSalida = 0;
          |ABRE #6;
          #6#0 = #9#0;                                 ||Serie Pres.
          #6#1 = #9#1;                                 ||N.Presupuesto
          #6#2 = #9#14;
          #6#3 = #9#22;                                 ||Partida.
          |LEE 020#6=;
          |SI FSalida = 0;
               #6#13 = #6#13 +. #9#10;                  ||Importe Certificado.
               #6#14 = #6#14 +. #9#7;                   ||Unidades Certificadas.
               #6#15 = #6#7  -  #6#14;                  ||Unidades Pendientes.
               #6#19 = #6#19 -. #9#12;                  ||% Pendiente.
               #6#20 = #6#10 -  #6#13;                  ||Importe Pendiente.
               |GRABA 020#6a;
               #9#9  = #6#10;
               #9#11 = #6#20;
               |GRABA 020#9a;
          |FINSI;
          |CIERRA #6;
     |FINSI;
     |CIERRA #9;
|FPRC;

|PROCESO NoCancela11; |TIPO 11;
     |SI FSalida = 7;
          |AVISO; |ERROR;
     |FINSI;
|FPRC;

|PROCESO Titulo; |TIPO 0;
     |SI FSalida = 0;
          |SI #1#8 = "S";
               |ENTLINEAL #1#2, 4, 4, 22, 0, Minl, Maxl;
          |SINO;
               |ENTLINEAL #1#2, 4, 2, 22, 0, Minl, Maxl;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Factu; |TIPO 0;
     aSwFac = "N";
     |C_M #1#10, 1, "N";
     |C_M #1#11, 1, "N";
     |C_M #1#12, 1, "N";
     |C_M #1#13, 1, "N";
     |C_M #1#14, 1, "N";
     |C_M #1#15, 1, "N";
     |SI #1Campo ! Contenido;
          |SI #1#8 = "N";
               |SI #1#15  ! 0;
                    |CONFI "0000N¿Borrar Factura?";
                    |SI FSalida = 0;
                         enError = 0;
                         |HAZ BorraFactura;
                         |SI enError = 0;
                              #1#13 = "01.01.0000"; |PINTA #1#13;
                              #1#14 = ""; |PINTA #1#14;
                              #1#15 = 0; |PINTA #1#15;
                         |SINO;
                              #1#8 = "S";
                              |ERROR;
                         |FINSI;
                    |SINO;
                         #1#8 = "S";
                         |ERROR;
                    |FINSI;
               |FINSI;
          |SINO;
               aSwFac = "S";
               |ABRE #5;
               #5#0 = #1#0;
               #5#1 = #1#1;
               |LEE 020#5=;
               |CIERRA #5;
               |SI #5#18 = "P"; |Y #100#139 = "S";
                    #1#10 = 0;
                    |C_M #1#10, 1, "N";
               |SINO;
                    #1#10 = 2;
                    |C_M #1#10, 1, "S";
               |FINSI;
               |PINTA #1#10;
          ||     #1#9 = #5#2; |PINTA #1#9;
               |ABRE #24;
               #24#0 = #1#9;
               |LEE 020#24=;
               |CIERRA #24;
               #1#11 = #24#20; |PINTA #1#11;
               #1#12 = #24#25; |PINTA #1#12;
               #1#13 = ~; |PINTA #1#13;
               #1#14 = #100#13; |PINTA #1#14;
               |C_M #1#10, 1, "S";
               |C_M #1#11, 1, "S";
               |C_M #1#12, 1, "S";
               |C_M #1#13, 1, "S";
               |C_M #1#14, 1, "S";
               |C_M #1#15, 1, "S";
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Cliente7; |TIPO 7;
     |SI #1#9 = "      ";
          |ABRE #5;
          #5#0 = #1#0;
          #5#1 = #1#1;
          |LEE 020#5=;
          |CIERRA #5;
          #1#9 = #5#2; |PINTA #1#9;
     |FINSI;
     |SI #1#8 = "S";
          |C_M #1#9, 1, "N";
     |SINO;
          |C_M #1#9, 1, "S";
     |FINSI;
|FPRC;

|PROCESO Cliente0; |TIPO 0;
     |ABRE #24;
     #24#0 = #1#9;
     |LEE 000#24=;
     |SI FSalida ! 0;
          |MENAV "0000No exite cliente...";
          |ERROR;
     |SINO;
          |SI #1Campo ! Contenido;
               #1#11 = #24#20; |PINTA #1#11;
               #1#12 = #24#25; |PINTA #1#12;
          |FINSI;
     |FINSI;
     |CIERRA #24;
|FPRC;

|PROCESO IVA071_0; |TIPO 0;
     |SI aSwFac = "S";
          enTiva = #1Campo;
          efFiva = #1#5;
          |HAZ SacaIVA;
          |SI eaDiva = "";
               |MENAV "0000No existe tipo iva para esta fecha...";
               |ERROR; |ACABA;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO IVA071_66; |TIPO 66;
     enTiva = #1Campo;
     efFiva = #1#5;
     |HAZ ConsuIVA;
     |SI eaDiva = "";
          |ERROR;
     |SINO;
          #1Campo = enTiva; |PINTA #1Campo;
     |FINSI;
|FPRC;

|PROCESO Repre; |TIPO 0;
     |SI aSwFac = "S";
          |ABRE #25;
          #25#0 = #1#12;
          |LEE 000#25=;
          |SI FSalida ! 0;
               |MENAV "0000No existe representante...";
               |ERROR;
          |FINSI;
          |CIERRA #25;
     |FINSI;
|FPRC;

|PROCESO Forpa; |TIPO 0;
     |SI aSwFac = "S";
          |ABRE #91;
          #91#0 = #1#11;
          |LEE 000#91=;
          |SI FSalida ! 0;
               |MENAV "0000No existe forma de pago...";
               |ERROR;
          |FINSI;
          |CIERRA #91;
     |FINSI;
|FPRC;

|PROCESO Serie; |TIPO 0;
     |SI aSwFac = "S";
          |ABRE #7;
          #7#26 = #1#14;
          |LEE 000#7=;
          |SI FSalida ! 0;
               |MENAV "0000No existe serie...";
               |ERROR;
          |FINSI;
          |CIERRA #7;
     |FINSI;
|FPRC;

|PROCESO Numero7; |TIPO 7;
     |SI aSwFac = "S";
          aGuarda = FEntrada;
          FEntrada = 100;
          |ABRE #84;
          #84#48 = #1#14;
          |HAZ ContaFD7;
          #1#15 = #84#0; |PINTA #1#15;
          |CIERRA #84;
          FEntrada = aGuarda;
     |FINSI;
|FPRC;

|PROCESO Numero0; |TIPO 0;
     |SI aSwFac = "S";
          |SI FSalida = 0;
               |DDEFECTO #84;
               |ABRE #84;
               #84#48 = #1#14;
               #84#0 = #1#15;
               |GRABA 000#84b;
               |SI FSalida = 0;
                    |HAZ CreaFactura;
               |SINO;
                    |MENAV "0000Factura existente...";
                    |ERROR;
               |FINSI;
               |CIERRA #84;
          |SINO;
               |ERROR;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Tipo2l; |TIPO 2;
     #1#7 = #1#7 +. #2#9; |PINTA #1#7;
|FPRC;

|PROCESO Total; |TIPO 0;
     #2#6 = #2#6 ! #142#8; |PINTA #2#6;
     #2#9 = (#2#6 * #2#7) <  #2#8; |PINTA #2#9;
|FPRC;

|PROCESO DefArti; |TIPO 7;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 1;
          #2#4 = #100#31;
     |FINSI;
|FPRC;

|PROCESO Descri;
     enSwPrint = 1;
     nHay = 1;
     |PRINT;
|FPRC;

|DEFBUCLE Descri;
     #10#1;
     ;
     #1#0, #1#1, #1#2, 01;
     #1#0, #1#1, #1#2, 99;
     ;
     NULL, Descri;
|FIN;

|PROCESO Conceptos;
     enSwPrint = 2;
     nHay = 1;
     |PRINT;
|FPRC;

|DEFBUCLE Conceptos;
     #2#1;
     ;
     #1#0, #1#1, #1#2, 001;
     #1#0, #1#1, #1#2, 999;
     ;
     NULL, Conceptos;
|FIN;

|PROCESO ImpCerti;
     Impresora = "CrystalReport";
     |IMPRE -1;
     |SI FSalida = 0;
          |INFOR "gocerrea";  || El rpt esta renombrado a 'gocerrea_rpt'
          |SI FSalida = 0;
               nHay = 0;
               |DDEFECTO #2;
               |BUCLE Descri;
               |DDEFECTO #10;
               |BUCLE Conceptos;
               |SI nHay = 0; |PRINT; |FINSI;
               |PIEINF;
               |FININF;
               |HAZ ActFactu;
          |FINSI;
          |FINIMP;
     |FINSI;
|FPRC;

|PROCESO ActFactu;
     |SI #1#8 = "S";
          |ABRE #84;
          #84#48 = #1#14;
          #84#0 = #1#15;
          |LEE 121#84=;
          |SI FSalida = 0;
               |SI #100#123 = "S"; |Y #84#10 = "N";
                    |SI #142#47 = "S";
                         |SI #142#146 ! "N"; |O #84#73 ! 0;
                              |HAZ CreaRecibos;
                         |FINSI;
                    |FINSI;
               |FINSI;
               #84#10 = "S";
               |GRABA 020#84a;
               |CIERRA #84;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO GocerHis;
     |ABRE #6;
     #6#0 = #1#0;
     #6#1 = #1#1;
     #6#2 = "";
     #6#3 = 0;
     |LEE 000#6];
     |SI FSalida ! 0; |O #6#0 ! #1#0; |O #6#1 ! #1#1;
          |DDEFECTO #6;
     |FINSI;
     |CIERRA #6;

     |DDEFECTO #9;
     |ABRE #9;
     #9#0 = #1#0;
     #9#1 = #1#1;
     #9#2 = #1#2;
     #9#3 = 1;
     |LEE 101#9=;
     |SI FSalida ! 0; |GRABA 020#9b; |FINSI;

     #9#4 = #6#4;   ||Partida.
     #9#5  = #6#5;  ||Descripcion.
     #9#9  = #6#10; ||Importe Partida.
     #9#11 = #6#10 - #6#13; ||Pendiente Cerif.
     #9#14 = #6#2;  ||Capitulo.
     #9#18 = #6#7;  ||Cantidad Pres.
     #9#22 = #6#3;  ||LINEA

     #9#10 = #2#7;                ||Importe certificado
     #9#8 = #2#7;
     #9#7 = 1;
     #9#12 = (#9#10 * 100) / #9#9;||%Certif

     |GRABA 020#9a;
     |CIERRA #9;
|FPRC;

|PROCESO poncontr;
     fiac = Control % A109;
     fiac = fiac + relleno;
     guarda = fiac % A109;
     fiac = Control % A1001;
     Control = Control + fiac;
     Control = Control + relleno;
     Control = Control % A120;
     Control = Control + aponer;
|FPRC;

|PROCESO CopiaLin;
     #2#0 = #1#0;
     #2#1 = #1#1;
     #2#2 = #1#2;
     |GRABA 020#2n;
     #1#7 = #1#7 + #2#9;
|FPRC;

|DEFBUCLE CopiaLin;
     #2#1;
     ;
     aSerOE, nNumOE, nCerti, 001;
     aSerOE, nNumOE, nCerti, 999;
     ;
     NULL, CopiaLin;
|FIN;

|PROCESO CopiaCerti;
     aponer = Asino; |HAZ poncontr;

     |DDEF aAlfa;
     aAlfa = aAlfa + "gocerrec";
     |CARGA_DEF aAlfa, referen@;
     |SI FSalida = 0;
          |ABRE #referen@;
          #referen@#0 = #1#0;
          #referen@#1 = #1#1;
          #referen@#2 = #1#2;
          |LEE 000#referen@.];
          |SI FSalida = 0;
               |LEE 000#referen@.a;
          |SINO;
               |LEE 000#referen@.u;
          |FINSI;
          |CONSULTA_CLAVE #1#referen@;
          |SI FSalida = 0;
               #1#3 = #0#2;
               #1#4 = #0#3;
               #1#5 = ~;
               #1#6 = #referen@#6;

               aSerOE = #referen@#0;
               nNumOE = #referen@#1;
               nCerti = #referen@#2;
               |ABRE #2;
               |BUCLE CopiaLin;
               |CIERRA #2;

               |GRABA 020#1n;

               |HAZ GocerHis;
               |HAZ Actualiza;
          |FINSI;
          |CIERRA #referen@;
          |DESCARGA_DEF #referen@;
     |FINSI;
     aponer = Anosi; |HAZ poncontr;
     |PTEC 809; |PTEC 808;
|FPRC;

|PROCESO NoEsc; |TIPO 6;
     |SI FSalida ! 0; |Y FSalida ! 3; |Y FSalida ! 2;
          |AVISO; |ERROR;
     |FINSI;
|FPRC;
