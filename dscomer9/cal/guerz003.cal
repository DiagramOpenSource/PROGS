|FICHEROS;
   guerz003 #0;
   agifa024 #1;
   agifa025 #2;
   agifa184 #3;
   agifa091 #4;
   agifa112 #5;

   dscam003@ #200;
   dscam008@ #201;
|FIN;

|VARIABLES;
   aAlfa   = "";
   aAlfa1  = "";
   aAlfa2  = "";
   aCadena = "";
   aLong   = "";
   Cadena  = "";
   CR      = "";
   LF      = "";

   aNombreBanco = "";

   nPos    = 0;
   nCont   = 0;
   nLong   = 0;
   Handle  = 0;
   nCalc   = 0;
   nTecla  = 0;
   nNumDoc = 0;
|FIN;

|PROCESO TrataCobros;

   aAlfa = Cadena % 1208;
   aAlfa1 = aAlfa % 0301; |SI aAlfa1 ! "-"; |ACABA; |FINSI;
   aAlfa1 = aAlfa % 0601; |SI aAlfa1 ! "-"; |ACABA; |FINSI;
   aAlfa1 = aAlfa % 0101; |SI aAlfa1 < "0"; |O aAlfa1 > "9"; |ACABA; |FINSI;
/*
   aAlfa1 = Cadena % 0108;      |PINTA 0505, aAlfa1; || Fecha emision
   aAlfa1 = Cadena % 1208;      |PINTA 0605, aAlfa1; || Fecha vencimiento
   aAlfa1 = Cadena % 2112;      |PINTA 0705, aAlfa1; || N§ Recibo
   aAlfa1 = Cadena % 3607;      |PINTA 0805, aAlfa1; || Codigo Cliente
   aAlfa1 = Cadena % 4435;      |PINTA 0905, aAlfa1; || Nombre Cliente
   aAlfa1 = Cadena % 8015;      |PINTA 1005, aAlfa1; || Importe cobro
   aAlfa1 = Cadena % 11615;     |PINTA 1105, aAlfa1; || Importe ¨¨ cobrado ??

   |PAUSA;
   nTecla = FSalida;
*/
   aAlfa1 = Cadena % 3706;
   #agifa024#0 = aAlfa1;        |LEE 000#agifa024.=;
   |SI FSalida ! 0; |DDEFECTO #agifa024; #agifa024#0 = "NOESTA"; |FINSI;

   #agifa025#0 = #agifa024#25;  |LEE 000#agifa025.=;
   |SI FSalida ! 0; |DDEFECTO #agifa025; |FINSI;

   #agifa184#0 = #agifa024#203; |LEE 000#agifa184.=;
   |SI FSalida ! 0; |DDEFECTO #agifa184; |FINSI;

   #agifa091#0 = #agifa024#20;  |LEE 000#agifa091.=;
   |SI FSalida ! 0; |DDEFECTO #agifa091; |FINSI;

   |LEE 000#dscam003@.u; nNumDoc = #dscam003@#40 + 1;
   |DDEFECTO #dscam003@;
   #dscam003@#40 = nNumDoc;
   |GRABA 020#dscam003@.b;
   aAlfa1 = Cadena % 3002; #dscam003@#0 = aAlfa1;    || Serie
   aAlfa1 = Cadena % 2306; #dscam003@#1 = aAlfa1;    || N§ Factura
   aAlfa1 = Cadena % 3302; #dscam003@#2 = aAlfa1;    || N§ Recibo
   aAlfa1 = Cadena % 1210; aAlfa1 = (aAlfa1 % 0102) + "/" + (aAlfa1 % 0402) + "/" + (aAlfa1 % -104);
   #dscam003@#3 = aAlfa1;                            || Fecha de vencimiento
   aAlfa1 = Cadena % 0110; aAlfa1 = (aAlfa1 % 0102) + "/" + (aAlfa1 % 0402) + "/" + (aAlfa1 % -104);
   #dscam003@#4 = aAlfa1;                            || Fecha de emision
   #dscam003@#5  = #agifa024#16;                     || Cuenta de cliente
   #dscam003@#6  = #agifa024#1;                      || Nombre de cliente
   #dscam003@#7  = #agifa024#29;                     || Banco del cliente
   #dscam003@#8  = #agifa024#32;                     || Entidad
   #dscam003@#9  = #agifa024#33;                     || Sucursal
   #dscam003@#10 = #agifa024#31;                     || DC
   #dscam003@#11 = #agifa024#30;                     || Cuenta
   #dscam003@#14 = "P";                              || Estado del recibo
   #dscam003@#15 = "Pendiente de cobro";             || Desc.estado del recibo
   aAlfa1 = Cadena % 8015; |HAZ CambiaPuntos;
   #dscam003@#22 = aAlfa1;                           || Importe del recibo
   #dscam003@#30 = #dscam003@#22;                    || Total del recibo
   #dscam003@#31 = #dscam003@#22;                    || Pendiente recibo
   aAlfa1 = Cadena % 3607; #dscam003@#32 = aAlfa1;   || Codigo de cliente
   #dscam003@#33 = #agifa025#0;                      || Codigo representante
   #dscam003@#34 = #agifa025#1;                      || Nombre representante
   #dscam003@#39 = #agifa024#3;                      || Zona cliente
   #dscam003@#46 = #guerz003#4;                      || Empresa gestion
   #dscam003@#51 = #agifa024#203;                    || Banco remesas
   #dscam003@#52 = #agifa184#1;                      || Nombre banco remesas
   #dscam003@#57 = "EUR";                            || Divisa
   #dscam003@#58 = "EUROS";                          || Nombre Divisa
   #dscam003@#59 = 1;                                || Cambio Divisa en M.Base
   #dscam003@#60 = #dscam003@#22;                    || Importe del recibo BASE
   #dscam003@#64 = #dscam003@#22;                    || Total del recibo BASE
   #dscam003@#65 = #dscam003@#22;                    || Total pendiente BASE
   #dscam003@#80 = #agifa024#20;                     || Forma de pago
   #dscam003@#81 = #agifa091#1;                      || Descripcion forma de pago
   |GRABA 020#dscam003@.a; |LIBERA #dscam003@;
|FPRC;

|PROCESO TrataPagos;

   aAlfa = Cadena;
   aAlfa = aAlfa - "en EUR";
   |SI FSalida ! 0; aNombreBanco = aAlfa % 0399; |QBF aNombreBanco; |ACABA; |FINSI;

   aAlfa = Cadena % 4408;
   aAlfa1 = aAlfa % 0301; |SI aAlfa1 ! "-"; |ACABA; |FINSI;
   aAlfa1 = aAlfa % 0601; |SI aAlfa1 ! "-"; |ACABA; |FINSI;
   aAlfa1 = aAlfa % 0101; |SI aAlfa1 < "0"; |O aAlfa1 > "9"; |ACABA; |FINSI;
/*
   aAlfa1 = Cadena % 0340;      |PINTA 0505, aAlfa1; || Nombre proveedor
   aAlfa1 = Cadena % 4408;      |PINTA 0605, aAlfa1; || Fecha Vencimiento
   aAlfa1 = Cadena % 5312;      |PINTA 0705, aAlfa1; || Referencia
   aAlfa1 = Cadena % 6615;      |PINTA 0805, aAlfa1; || Su factura
   aAlfa1 = Cadena % 8208;      |PINTA 0905, aAlfa1; || Fecha emision
   aAlfa1 = Cadena % 9111;      |PINTA 1005, aAlfa1; || Importe
   aAlfa1 = Cadena % 10402;     |PINTA 1105, aAlfa1; || TPAG
   aAlfa1 = Cadena % 10902;     |PINTA 1205, aAlfa1; || Bco.
   aAlfa1 = Cadena % 11303;     |PINTA 1305, aAlfa1; || Sit
   aAlfa1 = Cadena % 11711;     |PINTA 1405, aAlfa1; || Importe pagado

   |PAUSA;
   nTecla = FSalida;
*/
   aAlfa1 = Cadena % 0340;
   |SELECT #3#agifa112;
   #agifa112#2 = aAlfa1;        |LEE 000#agifa112.=;
   |SI FSalida ! 0; |DDEFECTO #agifa112; #agifa112#0 = "NOESTA"; |FINSI;
   |SELECT #1#agifa112;

   #agifa091#0 = #agifa112#17;  |LEE 000#agifa091.=;
   |SI FSalida ! 0; |DDEFECTO #agifa091; |FINSI;

   |LEE 000#dscam008@.u; nNumDoc = #dscam008@#40 + 1;
   |DDEFECTO #dscam008@;
   #dscam008@#40 = nNumDoc;
   |GRABA 020#dscam008@.b;
   aAlfa1 = Cadena % 5302; #dscam008@#27 = aAlfa1;    || Libro
   aAlfa1 = Cadena % 6302; #dscam008@#0  = aAlfa1;    || Serie
   aAlfa1 = Cadena % 5606; #dscam008@#1  = aAlfa1;    || N§ Factura
   #dscam008@#2  = 1;                                 || N§ Recibo
   aAlfa1 = Cadena % 4408; aAlfa1 = (aAlfa1 % 0106) + "20" + (aAlfa1 % -102);
   #dscam008@#3  = aAlfa1;    || Fecha de vencimiento
   aAlfa1 = Cadena % 8208; aAlfa1 = (aAlfa1 % 0106) + "20" + (aAlfa1 % -102);
   #dscam008@#4  = aAlfa1;    || Fecha de emision
   #dscam008@#5  = #agifa112#10;                       || Cuenta de proveedor
   #dscam008@#6  = #agifa112#2;                        || Nombre de proveedor
   #dscam008@#7  = aNombreBanco;                       || Banco
   #dscam008@#8  = #agifa112#14;                       || Entidad
   #dscam008@#9  = #agifa112#15;                       || Sucursal
   #dscam008@#10 = #agifa112#13;                       || DC
   #dscam008@#11 = #agifa112#12;                       || Cuenta
   aAlfa1 = Cadena % 10304; |QBT aAlfa1;
   |SI aAlfa1 = "G";  #dscam008@#12 = #guerz003#5; |FINSI;
   |SI aAlfa1 = "PN"; #dscam008@#12 = #guerz003#6; |FINSI;
   |SI aAlfa1 = "CO"; #dscam008@#12 = #guerz003#7; |FINSI;
   #dscam008@#14 = "P";                              || Estado del recibo
   #dscam008@#15 = "Pendiente de pago";              || Desc.estado del recibo
   aAlfa1 = Cadena % 9111;  |HAZ CambiaPuntos;
   #dscam008@#22 = aAlfa1;                           || Importe del recibo
   #dscam008@#25 = #dscam008@#22;                    || Total del recibo
   #dscam008@#31 = #dscam008@#22;                    || Pendiente recibo
   #dscam008@#32 = #agifa112#0;                      || Codigo de proveedor
   #dscam008@#46 = #guerz003#4;                      || Empresa gestion
   #dscam008@#53 = #dscam008@#22;                    || Importe del recibo BASE
   #dscam008@#56 = #dscam008@#22;                    || Total del recibo BASE
   || aAlfa1 = Cadena % 11711;  |HAZ CambiaPuntos;
   || #dscam008@#57 = aAlfa1;                        || Importe pagado recibo
   #dscam008@#57 = 0;                                || Importe pagado recibo
   #dscam008@#58 = #dscam008@#56 - #dscam008@#57;    || Total pendiente BASE
   #dscam008@#59 = "EUR";                            || Divisa
   #dscam008@#60 = "EUROS";                          || Nombre Divisa
   #dscam008@#61 = 1;                                || Cambio Divisa en M.Base
   aAlfa1 = Cadena % 6615; #dscam008@#63 = aAlfa1;   || Referencia
   #dscam008@#67 = #agifa091#0;                     || Forma de pago
   #dscam008@#68 = #agifa091#1;                     || Desc.forma de pago
   |GRABA 020#dscam008@.a; |LIBERA #dscam008@;
|FPRC;

|PROCESO importacion;
   aAlfa = #guerz003#0; |QBF aAlfa;
   aAlfa = "rb  " + aAlfa;
   |FABRE aAlfa;
   |SI FSalida ] 0;
      Handle = FSalida;
      aAlfa = Handle + " " + 1; |FLEE aAlfa; nCalc = &aAlfa;
      |PARA; |SI FSalida ! 0; |HACIENDO;
         |SI aAlfa = LF;
            |SI #0#1 = "P"; |HAZ TrataPagos;  |FINSI;
            |SI #0#1 = "C"; |HAZ TrataCobros; |FINSI;
            Cadena = "";
            |SI nTecla = 1; |O nTecla = 7; |SAL; |FINSI;
            aAlfa = Handle + " " + 1; |FLEE aAlfa; nCalc = &aAlfa;
            |PARA; |SI aAlfa = CR; |Y FSalida = 0; |HACIENDO;
               aAlfa = Handle + " " + 1; |FLEE aAlfa; nCalc = &aAlfa;
               |SI FSalida ! 0; |SAL; |FINSI;
            |SIGUE;
         |FINSI;
         Cadena = Cadena + aAlfa;
         aAlfa = Handle + " " + 1; |FLEE aAlfa; nCalc = &aAlfa;
      |SIGUE;
      FSalida = Handle; |FCIERRA FSalida;
   |FINSI;
|FPRC;

|PROCESO CambiaPuntos;
   aAlfa = aAlfa1; |QBF aAlfa; aCadena = "";
   aLong = aAlfa % 0; nLong = aLong;
   |PARA nCont = 1; |SI nCont [ nLong; |HACIENDO nCont = nCont + 1;
      nPos = (nCont * 100) + 1;
      aAlfa2 = aAlfa % nPos;
      |SI aAlfa2 = ",";
         aAlfa2 = "";
      |SINO;
         |SI aAlfa2 = "."; aAlfa2 = ","; |FINSI;
      |FINSI;
      aCadena = aCadena + aAlfa2;
   |SIGUE;
   aAlfa1 = aCadena;
|FPRC;

|PROCESO Tipo;
   |SI #guerz003#1 = "P"; aAlfa = "S"; |SINO; aAlfa = "N"; |FINSI;

   |C_M #guerz003#5,1,aAlfa;
   |C_M #guerz003#6,1,aAlfa;
   |C_M #guerz003#7,1,aAlfa;
|FPRC;

|PROGRAMA;
   CR = &13; LF = &10;
   |CLS;
   |CABEZA " Importacion cartera ";

   |ABRE #guerz003; |LEE 000#guerz003.p;
   |SI FSalida ! 0; |DDEFECTO #guerz003; |GRABA 020#guerz003.c; |FINSI;

   |PINPA #0#0; |PINDA #0#0;
   |ENDATOS #1#0;
   |SI FSalida = 0;
      aAlfa = #guerz003#3; |QBF aAlfa;
      |SI #guerz003#1 = "C";
         aAlfa1 = aAlfa + "def/dscam003.mas"; |CARGA_DEF aAlfa1, dscam003@;
      |SINO;
         aAlfa1 = aAlfa + "def/dscam008.mas"; |CARGA_DEF aAlfa1, dscam008@;
      |FINSI;
      |SI FSalida ! 0;
         |MENAV "    Error al cargar el def de cartera"; |ERROR; |ACABA;
      |FINSI;
      aAlfa1 = aAlfa + "fich/" + (("00000" + #guerz003#4) % -105) + "/";
      |SI #guerz003#1 = "C";
         |PATH_DAT #200 aAlfa1; |ABRE #dscam003@;
      |SINO;
         |PATH_DAT #201 aAlfa1; |ABRE #dscam008@;
      |FINSI;
      |ABRE #agifa112; |ABRE #agifa024; |ABRE #agifa025;
      |ABRE #agifa184; |ABRE #agifa091;
      |CLS; |HAZ importacion;
      |CIERRA #agifa091; |CIERRA #agifa184; |CIERRA #agifa025;
      |CIERRA #agifa024; |CIERRA #agifa112;
      |SI #guerz003#1 = "C";
         |CIERRA #dscam003@; |DESCARGA_DEF #dscam003@;
      |SINO;
         |CIERRA #dscam008@; |DESCARGA_DEF #dscam008@;
      |FINSI;
   |FINSI;

   |GRABA 020#guerz003.a; |CIERRA #guerz003;
|FPRO;
