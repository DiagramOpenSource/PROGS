|FICHEROS;
     iber0076 #0;
     agifa048 #8;
     dsm00018 #918;
     agifa046 #46;
|FIN;

|VARIABLES;
     aPathImg       = "";   || Directorio base + '/recetas' (Ver Tipo8)

     &nDeci_EsCab   = 0;
     &nDeci_EsLin   = 0;
     &enReceta      = 0;
     aIP            = "";
     aDes           = "";
     aAlf1          = "";
     aAlf2          = "";
     aFic           = "";
     aRuta          = "";
     nBoton1        = 0;
     nBoton2        = -1;
     nConta         = 0;
     aDirectorio    = "";
     aNFichero      = "";
     aVariableCarga = "";
     aFichero       = "";
     aAlfa          = "";
     nCampo         = 0;
     nModo          = 0;

     nVariable      = 0;
     nBaffer        = 0;
     nTotalLinea    = 0;
     nTope          = 0;
     nVari1         = 0;
     nVari2         = 0;

     eaCodigo      = "";
     eaTipoCodigo  = "";
     eaNombreList  = "";
     enCuadroIni   = 0;
     enCuadroFin   = 0;
     enCampoAncho  = 0;
     enModoEdita   = 0;
     enTotalVaria  = 0;
     aExt          = "";
     aLetra        = "";
     aLon          = "";
     i             = 0;
     nPos          = 0;

{100}&epPuntEdita   = "";
|FIN;

|PROCESO BorraNota;
     |BORRA 020#918a;
|FPRC;

|DEFBUCLE BorraNota;
     #918#1;
     ;
     eaCodigo, "IB", 0;
     eaCodigo, "IB", 999;
     be;
     NULL, BorraNota;
|FIN;

|PROCESO Tipo2; |TIPO 2;
     #0#15 = "N";
     #0#5 = ~; |PINTA #0#5;

     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 3;
          eaCodigo = ("000000" + #0#0) % -106;
          |BUCLE BorraNota;
     |FINSI;
|FPRC;

|PROCESO Contador; |TIPO 7;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 1;
          |LEE 000#0u;
          |SI FSalida = 0;
               nConta = #0#0 + 1;
          |SINO;
               nConta = 1;
          |FINSI;
          |DDEFECTO #0;
          #0#0 = nConta;
     |FINSI;
|FPRC;

|PROCESO GrabaSecuencial;
     IepPuntEdita  = epPuntEdita1 <-;
     |PARA nTotalLinea = 0;  |SI nTotalLinea [ enTotalVaria;  |HACIENDO nTotalLinea = nTotalLinea + 1;
          |AL_BUF nBaffer, nTotalLinea, epPuntEdita;
          IepPuntEdita = IepPuntEdita + 1;
     |SIGUE;
|FPRC;

|PROCESO Edicion;
     nTope  = nTotalLinea - 1;
     nVari1 = 0;
     nVari2 = 0;
     enModoEdita = enModoEdita + 100;
     |EDITA nBaffer, 1, nTope, enCuadroIni, enCuadroFin, enModoEdita, eaNombreList, enCampoAncho, nVari1, nVari2;
     enModoEdita = enModoEdita $ 100;
|FPRC;

|PROCESO CogePuntero;
     IepPuntEdita  = epPuntEdita1 <-;
     |PARA nTotalLinea = 0;  |SI nTotalLinea [ enTotalVaria;  |HACIENDO nTotalLinea = nTotalLinea + 1;
          |DEL_BUF nBaffer, nTotalLinea, epPuntEdita;
          IepPuntEdita = IepPuntEdita + 1;
     |SIGUE;
|FPRC;

|PROCESO Edita;
     |PUSHV 0501 2380;
     |DIMENSIONA  200, nBaffer, 0;

     |HAZ GrabaSecuencial;
     |HAZ Edicion;
     |HAZ CogePuntero;

     |FINDIM nBaffer;
     |POPV;
|FPRC;
컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
|PROCESO IniciaEdicion;
     |ABRE #918;
     |PARA nVariable = 1;  |SI nVariable [ 100;  |HACIENDO nVariable = nVariable + 1;
           IepPuntEdita  = epPuntEdita1 <-;
           IepPuntEdita = IepPuntEdita + (nVariable - 1);

           #918#0 = eaCodigo;
           #918#1 = nVariable;
           #918#3 = eaTipoCodigo;
           |LIBERA #918;
           |LEE 101#918=;
           |SI FSalida ! 0;
               epPuntEdita  = "";
           |SINO;
               epPuntEdita  = #918#2;
               |BORRA 020#918a;
           |FINSI;
     |SIGUE;

     |PUSHV  0501 2380;
     |BLANCO 1502 2349;
     |CUADRO 1502 2349;
     |HAZ Edita;
     |POPV;

     |PARA nVariable = 1;  |SI nVariable [ 100;  |HACIENDO nVariable = nVariable + 1;
           IepPuntEdita = epPuntEdita1 <-;
           IepPuntEdita = IepPuntEdita + (nVariable - 1);

           |DDEFECTO #918;
           #918#0 = eaCodigo;
           #918#1 = nVariable;
           #918#3 = eaTipoCodigo;
           |LIBERA #918;
           |LEE 1010#918=;
           |SI FSalida ! 0;  |GRABA 020#918b;  |FINSI;
           #918#2 = epPuntEdita;
           |GRABA 020#918a;

           |SI nVariable [ 9; |Y nVariable > 1;
               nCampo = nVariable + 5;
               #0nCampo = epPuntEdita;
           |FINSI;
     |SIGUE;

     |PARA nVariable = 100;  |SI nVariable ] 1;  |HACIENDO nVariable = nVariable - 1;
           #918#0 = eaCodigo;
           #918#1 = nVariable;
           #918#3 = eaTipoCodigo;
           |LIBERA #918;
           |LEE 101#918=;
           aAlfa = #918#2;  |QBF aAlfa;

           |SI aAlfa ! "";  |SAL;  |FINSI;

           |BORRA 020#918a;
     |SIGUE;
     |CIERRA #918;
|FPRC;

|PROCESO Nota;
     enTotalVaria  = 100;
     eaNombreList  = "";
     enCuadroIni   = 1503;
     enCuadroFin   = 2248;
     enCampoAncho  = 60;
     enModoEdita   = 0;

     eaCodigo = ("000000" + #0#0) % -106;
     eaTipoCodigo = "IB";
     |HAZ IniciaEdicion;
     |PARA nCampo = 6; |SI nCampo [ 14; |HACIENDO nCampo = nCampo + 1;
          |PINTA #0nCampo;
     |SIGUE;
|FPRC;

|PROCESO Activa; |TIPO 7;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo ! 4;
          |ESTADO_CONTROL nBoton1, "ENABLE";
          |SI nBoton2 ! -1;
               |FIN_CONTROL_WINDOWS nBoton2;
               nBoton2 = -1;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Desactiva; |TIPO 3;
     |ESTADO_CONTROL nBoton1, "DISABLE";
     |HAZ Tipo14;
|FPRC;

|PROCESO CopiaImagen;
     aAlf1 = aRuta % 299;
     aAlf2 = aRuta % 10099;
     aRuta = aAlf1 + aAlf2;
     aExt = "";
     aLon = aRuta % A0;
     |PARA i = aLon; |SI i > 0; |HACIENDO i = i - 1;
          nPos =  (i * 100 + 1)
          aLetra = aRuta % nPos;
          |SI aLetra = ".";  |SAL; |FINSI;
          aExt = aLetra + aExt;
     |SIGUE;
     |SI aLetra = ".";
          aFic =  (("000000" + #0#0) % -106) + "." + aExt;
          aFic = aFic < "";
          aDes = aPathImg + aFic;
          aDes = aDes < "";
          aIP = "";
          |IP_REMOTO aIP;
          |SI aIP = "";
               |COPIA_FICHERO aRuta, aDes;
          |SINO;
               |RECIBE_FICHERO aRuta, aDes;
          |FINSI;
          #0#16 = aFic;
          #0#6 = aRuta;
     |SINO;
          |MENAV "0000Fichero INCORRECTO, no tiene extension";
     |FINSI;
|FPRC;

|PROCESO Boton1;
     |ESTADO_CONTROL nBoton1, "DISABLE";
     |PUSHV 0501 2380;
     aRuta = #0#6;  |QBF aRuta;
     |SI aRuta = ""; aRuta = "\*.*"; |FINSI;
     aRuta = "F" + aRuta;
     |BROWSE aRuta; |QBF aRuta;
     |POPV;
     |SI aRuta ! "F";
          |HAZ CopiaImagen;
     |FINSI;

     |ESTADO_CONTROL nBoton1, "ENABLE";
     |HAZ Tipo14;
|FPRC;

|PROCESO Tipo9;  |TIPO 9;
     |FIN_CONTROL_WINDOWS nBoton1;
     |FIN_CONTROL_WINDOWS nBoton2;
     |PULSATECLA;
|FPRC;

|PROCESO Tipo8;  |TIPO 8;
     |DBASE aPathImg;
     aPathImg = aPathImg + "recetas/";
     |MKDIR aPathImg;

     |ABRE #46; |LEE 000#46p; |CIERRA #46;
     nDeci_EsCab = #46#15;
     nDeci_EsLin = #46#16;

     |CONTROL_SIMPLE 0, "BOTON, Copiar Imagen", 1303, 1315, Boton1;
     nBoton1 = FSalida;
     |ESTADO_CONTROL nBoton1, "DISABLE";
     |PULSATECLA;
|FPRC;

|PROCESO Tipo14; |TIPO 14;
     |SI nBoton2 ! -1;
          |FIN_CONTROL_WINDOWS nBoton2;
          |BLANCO 0749 2279;
          nBoton2 = -1;
     |FINSI;

     aFic = #0#16; |QBF aFic;
     |SI aFic ! "";
          aFic = aPathImg + aFic;
          |XABRE "E", aFic, 0;
          |SI FSalida = 0;
               |IP_REMOTO aIP;
               |SI aIP ! "";
                    aAlfa = "c:\tmp\"; |RMKDIR aAlfa;
                    aDes = "c:\tmp\" + #0#16; |QBF aDes;
                    |ENVIA_FICHERO aFic, aDes;
               |SINO;
                    aDes = aFic;
               |FINSI;
               aFic = "IMAGEN," + aDes;
               |CONTROL_SIMPLE 0, aFic , 0749, 2279, NULL;
               nBoton2 = FSalida;
               |ESTADO_CONTROL nBoton2, "ENABLE";
               |PULSATECLA;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Tipo30; |TIPO 30;
     |CONFI "0000NImprimir Documento";
     |SI FSalida = 0;
          |HAZ Tipo4;
     |FINSI;
|FPRC;

|PROCESO Tipo4; |TIPO 4;
     enReceta = #0#0;
     |SUB_EJECUTA_NP "iber0077";
|FPRC;
