|FICHEROS;
     aparm030 #0;
     aparm031 #31;
|FIN;


|VARIABLES;
     &eaVarDni = "";
     aNifQbf1 = "";
     aNifQbf2 = "";
     nPuntero = 0;
     aAlfa = "";
     nBuffer = 0;
     nLineas = 0;
     nModo = 0;
     nLin = 0;
     nCol = 0;
|FIN;

|PROCESO CdPostal30; |TIPO 0;
     aAlfa = (("00" + #0#4) % -102) + (("000" + #0#5) % -103);
     #0#6 = aAlfa;
|FPRC;

|PROCESO CogeLineas30;
     nPuntero = 1;
     |PARA nPuntero = 1; |SI nPuntero [ 999; |HACIENDO nPuntero = nPuntero + 1;
          #31#0 = #0#0;
          #31#1 = nPuntero;
          |LEE 000#31=;
          |SI FSalida = 0;
               aAlfa = #31#2; |AL_BUF nBuffer, nPuntero, aAlfa;
               |BORRA 020#31a;
          |SINO;
               |SAL;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO PoneLineas30;
     |PARA nPuntero = 999; |SI nPuntero ] 0; |HACIENDO nPuntero = nPuntero - 1;
          |DEL_BUF nBuffer, nPuntero, aAlfa;
          |SI aAlfa ! ""; nLineas = nPuntero; |SAL; |FINSI;
     |SIGUE;
     |PARA nPuntero = 1; |SI nPuntero [ nLineas; |HACIENDO nPuntero = nPuntero + 1;
          |DEL_BUF nBuffer, nPuntero, aAlfa;
          #31#0 = #0#0;
          #31#1 = nPuntero;
          #31#2 = aAlfa;
          |GRABA 020#31n;
     |SIGUE;
|FPRC;

|PROCESO Observa30;
     |ABRE #31;
     |PINPA #0#31;
     |DIMENSIONA 200, nBuffer, 0;
     |HAZ CogeLineas30;
     nModo = 100; nLin = 10; nCol = 78;
     |EDITA nBuffer, 1, 10, 1002, 2279, nModo, "", 0, nLin, nCol;
     |HAZ PoneLineas30;
     |FINDIM nBuffer;
     |CIERRA #31;
|FPRC;

|PROCESO Tipo30_20; |TIPO 20;
     |SI FSalida = "OPCION";
          FSalida = "Observa.";
     |SINO;
          |LEE 101#0=;
          |SI FSalida ! 0; |ACABA; |FINSI;

          |PUSHV 0501 2380;
          |HAZ Observa30;
          |POPV;

          |LIBERA #0;
     |FINSI;
|FPRC;

|PROCESO Borra31;
     |BORRA 020#31a;
|FPRC;

|DEFBUCLE Borra31;
     #31#1;
     ;
     #0#0, 001;
     #0#0, 999;
     be;
     NULL,  Borra31;
|FIN;


|PROCESO Tipo30_2; |TIPO 2;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 3;
          |BUCLE Borra31;
     |FINSI;
|FPRC;


|PROCESO LetraNif30; |TIPO 6;
||     eaPais =

     |SI FSalida = 9;
          eaVarDni  = #0Campo;
          |HAZ CalculoNIFCIF;
          #0Campo = eaVarDni;
          |PINTA #0Campo;
          |ERROR; |ACABA;
     |FINSI;

     eaVarDni  = #0Campo;
     |HAZ CalculoNIFCIF;
     |SI FSalida = 9;
          eaVarDni  = #0Campo;
          |HAZ CalculoNIFCIF;
          #0Campo = eaVarDni;
          |PINTA #0Campo;
          |ERROR; |ACABA;
     |FINSI;

     eaVarDni  = #0Campo;
     |HAZ CalculoNIFCIF;

     aNifQbf1 = #0Campo;   |QBF aNifQbf1;
     aNifQbf2 = eaVarDni;  |QBF aNifQbf2;

     |SI aNifQbf1 ! aNifQbf2; |Y aNifQbf1 ! "";
          aAlfa = "0000NEl NIF puede ser erróneo."+&13+"¿Desea calcularlo?";
          |CONFI aAlfa;
          |SI FSalida = 0;
               #0Campo = eaVarDni; |PINTA #0Campo;
          |FINSI;
     |FINSI;
|FPRC;
