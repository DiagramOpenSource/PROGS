|FICHEROS;
     evarz034 #0;
     evarw005 #5;   || Tmp
     evarw006 #6;   || Tmp
     agifa010 #10;
     agifa019 #19;
     agifa024 #24;
     agifa025 #25;
     evarm043 #43;
     agifa060 #60;
     agi00063 #63;
     agifa069 #69;
     agifa071 #71;
     agifa073 #73;
     agifa084 #84;
     agifa104 #104;
|FIN;

|VARIABLES;
     &Tempo = "";
     aInf = "";
     aAlfa = "";
     aTmp5 = "";
     aTmp6 = "";
     aMarca = "";
     nCampo = 0;
     nCamUni = 0;
     nCamImp = 0;
     nHayFam = 0;
     nHayMar = 0;
     nError = 0;
     nError1 = 0;
     i = 0;
     aTipo = "";
|FIN;

|PROCESO Tmp5;
     #25#0 = #5#0;
     |LEE 000#25=;
     |SI FSalida ! 0; |DDEFECTO #25; |FINSI;

     #24#0 = #5#1;
     |LEE 000#24=;
     |SI FSalida ! 0; |DDEFECTO #24; |FINSI;

     |PRINT;
|FPRC;

|DEFBUCLE Tmp5;
     #5#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, Tmp5;
|FIN;

|PROCESO Campo;
     |PARA nCampo = 11; |SI nCampo [ 30; |HACIENDO nCampo = nCampo + 1;
          |SI #6#0 = #0nCampo; |SAL; |FINSI;
          |SI #0nCampo = "   "; |Y nCampo > 11; |SAL; |FINSI;
     |SIGUE;
     |SI nCampo > 30;
          nCampo = 0;
          nError = nError + 1;
          |SI nError = 1;
               |MENAV "0000Se supera el máximo de marcas (20).";
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO CampoX;
     |SI #6#5 = "I";
          |PARA nCampo = 11; |SI nCampo [ 18; |HACIENDO nCampo = nCampo + 1;
               |SI #6#0 = #0nCampo; |SAL; |FINSI;
               |SI #0nCampo = "   "; |Y nCampo > 11; |SAL; |FINSI;
          |SIGUE;
          |SI nCampo > 18;
               nCampo = 0;
               nError = nError + 1;
               |SI nError = 1;
                    |MENAV "0000Se supera el máximo de marcas inicial (8).";
               |FINSI;
          |FINSI;
     |SINO;
          |PARA nCampo = 21; |SI nCampo [ 28; |HACIENDO nCampo = nCampo + 1;
               |SI #6#0 = #0nCampo; |SAL; |FINSI;
               |SI #0nCampo = "   "; |Y nCampo > 21; |SAL; |FINSI;
          |SIGUE;
          |SI nCampo > 28;
               nCampo = 0;
               nError1 = nError1 + 1;
               |SI nError1 = 1;
                    |MENAV "0000Se supera el máximo de marcas repetición (8).";
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Tmp6;
     |SI aTipo ! #6#5; aMarca = ""; |FINSI;
     aTipo = #6#5;

     |SI aMarca = "";
          |SI #0#0 = "P"; |Y #0#1 = "A";
               |SI #6#5 = "I";
                    nCampo = 11;
               |SINO;
                    nCampo = 21;
               |FINSI;
          |SINO;
               nCampo = 11;
          |FINSI;
     |SINO;
          |SI aMarca ! #6#0;
               |SI #0#0 = "P"; |Y #0#1 = "A";
                    |HAZ CampoX;
               |SINO;
                    |HAZ Campo;
               |FINSI;
          |FINSI;
     |FINSI;
     aMarca = #6#0;

     |SI nCampo = 0; |ACABA; |FINSI;

     |SI #6#0 = "   ";
          #0nCampo = "___";
     |SINO;
          #0nCampo = #6#0;
     |FINSI;

     nCamUni = nCampo - 11 + 2;
     nCamImp = nCampo - 11 + 22;

     |DDEFECTO #5;
     #5#0 = #6#1;
     #5#1 = #6#2;
     |LEE 101#5=;
     |SI FSalida ! 0; |GRABA 020#5b; |FINSI;
     #5nCamUni = #5nCamUni + #6#3;
     #5nCamImp = #5nCamImp + #6#4;
     |GRABA 020#5a;
     |LIBERA #5;
|FPRC;

|DEFBUCLE Tmp6;
     #6#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, Tmp6;
|FIN;

|PROCESO CheqFam;
     FSalida = 1;
     |SI nHayFam = 1;
          |PARA i = 32; |SI i [ 41; |HACIENDO i = i + 1;
               |SI #0i = #19#2; FSalida = 0; |SAL; |FINSI;
          |SIGUE;
     |SINO;
          |SI #19#2 ] #0#2; |Y #19#2 [ #0#3; FSalida = 0; |FINSI;
     |FINSI;
|FPRC;

|PROCESO CheqMar;
     FSalida = 1;
     |SI nHayMar = 1;
          |PARA i = 42; |SI i [ 49; |HACIENDO i = i + 1;
               |SI #0i = #19#126; FSalida = 0; |SAL; |FINSI;
          |SIGUE;
     |SINO;
          |SI #19#126 ] #0#4; |Y #19#126 [ #0#5; FSalida = 0; |FINSI;
     |FINSI;
|FPRC;

|PROCESO LinFactura;
     #19#0 = #69#2;
     |LEE 000#19=;
     |SI FSalida ! 0; |DDEFECTO #19; |FINSI;

     |HAZ CheqFam;
     |SI FSalida ! 0; |ACABA; |FINSI;

     |HAZ CheqMar;
     |SI FSalida ! 0; |ACABA; |FINSI;

     |DDEFECTO #6;
     #6#0 = #19#126;
     #6#1 = #84#6;
     #6#2 = #84#3;
     |LEE 101#6=;
     |SI FSalida ! 0; |GRABA 020#6b; |FINSI;
     #6#3 = #6#3 + #69#5;
     #6#4 = #6#4 + #69#9;
     |GRABA 020#6a;
     |LIBERA #6;
|FPRC;

|DEFBUCLE Factura;
     #84#1;
     1;
     "     ", 000001, #0#6;
     "zzzzz", 999999, #0#7;
     ;
     NULL, NULL, LinFactura;
|FIN;

|PROCESO LinAlbaran;
     #19#0 = #71#2;
     |LEE 000#19=;
     |SI FSalida ! 0; |DDEFECTO #19; |FINSI;

     |HAZ CheqFam;
     |SI FSalida ! 0; |ACABA; |FINSI;

     |HAZ CheqMar;
     |SI FSalida ! 0; |ACABA; |FINSI;

     |DDEFECTO #6;
     #6#0 = #19#126;
     #6#1 = #10#6;
     #6#2 = #10#3;
     |LEE 101#6=;
     |SI FSalida ! 0; |GRABA 020#6b; |FINSI;
     #6#3 = #6#3 + #71#5;
     #6#4 = #6#4 + #71#9;
     |GRABA 020#6a;
     |LIBERA #6;
|FPRC;

|DEFBUCLE Albaran;
     #10#7;
     ;
     #0#6, "     ", 000001;
     #0#7, "zzzzz", 999999;
     ;
     NULL, NULL, LinAlbaran;
|FIN;

|PROCESO LinPedido;
     #19#0 = #73#2;
     |LEE 000#19=;
     |SI FSalida ! 0; |DDEFECTO #19; |FINSI;

     |HAZ CheqFam;
     |SI FSalida ! 0; |ACABA; |FINSI;

     |HAZ CheqMar;
     |SI FSalida ! 0; |ACABA; |FINSI;
     |SI #19#2 < #0#2; |O #19#2 > #0#3; |ACABA; |FINSI;
     |SI #19#126 < #0#4; |O #19#126 > #0#5; |ACABA; |FINSI;

     |DDEFECTO #6;
     #6#0 = #19#126;
     #6#1 = #104#7;
     #6#2 = #104#4;
     #6#5 = #43#3;
     |LEE 101#6=;
     |SI FSalida ! 0; |GRABA 020#6b; |FINSI;
     #6#3 = #6#3 + #73#5;
     #6#4 = #6#4 + #73#9;
     |GRABA 020#6a;
     |LIBERA #6;
|FPRC;

|PROCESO Pedido;
     #43#0 = #104#25;
     #43#1 = #104#0;
     |LEE 000#43=;
     |SI FSalida ! 0; |DDEFECTO #43; |FINSI;

     |SI #0#1 ! "A";
          |SI #43#3 ! #0#1; |ERROR; |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE Pedido;
     #104#7;
     7;
     #0#6, "      ", 0001, "     ", 000001, #0#8;
     #0#7, "zzzzzz", 9999, "zzzzz", 999999, #0#9;
     ;
     NULL, Pedido, LinPedido;
|FIN;

|PROCESO PedAlb; |TIPO 0;
     |SI #0#0 = "P";
          |C_M #0#1, 1, "S";
     |SINO;
          |C_M #0#1, 1, "N";
     |FINSI;
|FPRC;

|PROCESO Familia; |TIPO 66;
     |ABRE #60;
     #60#0 = #0Campo;
     |LEE 000#60];
     |CONSULTA_CLAVE #1#60;
     |SI FSalida = 0;
          #0Campo = #60#0; |PINTA #0Campo;
     |SINO;
          |ERROR;
     |FINSI;
     |CIERRA #60;
|FPRC;

|PROCESO Marca; |TIPO 66;
     |ABRE #63;
     #63#0 = #0Campo;
     |LEE 000#63];
     |CONSULTA_CLAVE #1#63;
     |SI FSalida = 0;
          #0Campo = #63#0; |PINTA #0Campo;
     |SINO;
          |ERROR;
     |FINSI;
     |CIERRA #63;
|FPRC;

|PROCESO T3; |TIPO 3;
     |PARA i = 32; |SI i [ 41; |HACIENDO i = i + 1;
          |SI #0i ! "___"; nHayFam = 1; |FINSI;
     |SIGUE;

     |PARA i = 42; |SI i [ 49; |HACIENDO i = i + 1;
          |SI #0i ! "___"; nHayMar = 1; |FINSI;
     |SIGUE;

     |HAZ CreaTempo; |NOME_DAT #5 Tempo; |DELINDEX #5; aTmp5 = Tempo;
     |HAZ CreaTempo; |NOME_DAT #6 Tempo; |DELINDEX #6; aTmp6 = Tempo;

     |IMPRE 0;
     |SI FSalida = 0;
          |SI #0#0 = "P";
               |SI #0#1 = "A";
                    aInf = "evarzx34";
               |SINO;
                    aInf = "evarzp34";
               |FINSI;
          |SINO;
               aInf = "evarza34";
          |FINSI;
          |INFOR aInf;
          |SI FSalida = 0;
               |ABRE #19;
               |ABRE #6;
               |SI #0#0 = "P";
                    |ABRE #43;
                    |BUCLE Pedido;
                    |CIERRA #43;
               |SINO;
                    |BUCLE Albaran;
                    |BUCLE Factura;
               |FINSI;
               |CIERRA #6;
               |CIERRA #19;

               |ABRE #5;
               |BUCLE Tmp6;
               |CIERRA #5;

               |ABRE #24;
               |ABRE #25;
               |BUCLE Tmp5;
               |CIERRA #25;
               |CIERRA #24;

               |PIEINF;
               |FININF;
          |SINO;
               aAlfa = "0000Error en informe: " + aInf;
               |MENAV aAlfa;
          |FINSI;
          |FINIMP;
     |FINSI;

     Tempo = aTmp5; |HAZ BoraTempo; |DELINDEX #5;
     Tempo = aTmp6; |HAZ BoraTempo; |DELINDEX #6;
|FPRC;
