|FICHEROS;
   con00002 #0;
   agifa142 #1;

   RefAnal@ #100;
   RefCtas@ #101;
|FIN;

|VARIABLES;
   aAlfa   = "";
   aAlfa1  = "";
   aAlfa2  = "";

   nTecla  = 0;
   nCalc   = 0;
   nMaxDig = 0;
|FIN;

|PROCESO ConsCta;
   nTecla = FSalida;

   |ABRE #agifa142;
   |LEE 000#agifa142.p;
   |SI FSalida ! 0; |DDEFECTO #agifa142; |FINSI;

   aAlfa = #agifa142#151; |QBF aAlfa;
   aAlfa = aAlfa + "def/canempre.mas";
   |CARGA_DEF aAlfa, RefCtas@;
   |SI FSalida = 0;
      aAlfa = #agifa142#151; |QBF aAlfa;
      aAlfa = aAlfa + "fich/"; |PATH_DAT #RefCtas@#aAlfa#;
      |ABRE #RefCtas@;
      aAlfa = #agifa142#152; |QBF aAlfa; aAlfa = ("000000" + aAlfa) % -106;
      aAlfa1 = aAlfa % 0105; #RefCtas@#2 = aAlfa1;
      aAlfa1 = aAlfa % -101; #RefCtas@#3 = aAlfa1;
      |LEE 000#RefCtas@.=;
      |SI FSalida ! 0; |DDEFECTO #RefCtas@; |FINSI;
      nMaxDig = 0;
      |SI #RefCtas@#14 ] nMaxDig; nMaxDig = #RefCtas@#14; |FINSI;
      |SI #RefCtas@#15 ] nMaxDig; nMaxDig = #RefCtas@#15; |FINSI;
      |SI #RefCtas@#16 ] nMaxDig; nMaxDig = #RefCtas@#16; |FINSI;
      |SI #RefCtas@#17 ] nMaxDig; nMaxDig = #RefCtas@#17; |FINSI;
      |SI #RefCtas@#18 ] nMaxDig; nMaxDig = #RefCtas@#18; |FINSI;
      |SI #RefCtas@#19 ] nMaxDig; nMaxDig = #RefCtas@#19; |FINSI;

      |CIERRA #RefCtas@; |DESCARGA_DEF #RefCtas@;
   |FINSI;

   aAlfa = #con00002#Campo#; |QBF aAlfa;
   aAlfa = aAlfa - ".";
   |PARA; |SI FSalida ! 0; |HACIENDO;
      nCalc = FSalida + 98;               aAlfa2 = aAlfa % nCalc;
      nCalc = ((FSalida / 100) ! 0) + 99; aAlfa1 = aAlfa % nCalc;
      aAlfa = aAlfa1 + aAlfa2; aAlfa = aAlfa % 0;
      nCalc = aAlfa; nCalc = nMaxDig - nCalc;
      aAlfa = aAlfa1 + ("0" * nCalc) + aAlfa2;
      aAlfa = aAlfa - ".";
   |SIGUE;
   #con00002#Campo# = aAlfa; |PINTA #con00002#Campo#;

   aAlfa = #agifa142#151; |QBF aAlfa;
   aAlfa = aAlfa + "def/cacuenta.mas";
   |CARGA_DEF aAlfa, RefCtas@;
   |SI FSalida = 0;
      aAlfa = #agifa142#151; |QBF aAlfa;
      aAlfa = aAlfa + "fich/" + (("000000" + #agifa142#152) % -106);
      |QBF aAlfa; aAlfa = aAlfa + "/"; |PATH_DAT #RefCtas@#aAlfa#;
      |ABRE #RefCtas@;
      #RefCtas@#0 = #con00002#Campo#;
      |SI nTecla = 10;
         |LEE 000#RefCtas@.];
         |CONSULTA_CLAVE #1#RefCtas@;
         |SI FSalida = 0;
            #con00002#Campo# = #RefCtas@#0; |PINTA #con00002#Campo#;
         |FINSI;
         |ERROR;
      |SINO;
         |LEE 000#RefCtas@.=;
         |SI FSalida ! 0;
            |MENAV "    Cuenta contable inexistente"; |ERROR;
         |SINO;
            #con00002#Campo# = #RefCtas@#0; |PINTA #con00002#Campo#;
         |FINSI;
      |FINSI;
      |CIERRA #RefCtas@; |DESCARGA_DEF #RefCtas@;
   |FINSI;
   |CIERRA #agifa142;
|FPRC;

|PROCESO ConsAnalit;
   nTecla = FSalida;

   |ABRE #agifa142;
   |LEE 000#agifa142.p;
   |SI FSalida ! 0; |DDEFECTO #agifa142; |FINSI;

   aAlfa = #agifa142#151; |QBF aAlfa;
   aAlfa = aAlfa + "def/anm00001.mas";
   |CARGA_DEF aAlfa, RefAnal@;
   |SI FSalida = 0;
      aAlfa = #agifa142#151; |QBF aAlfa;
      aAlfa = aAlfa + "fich/" + (("000000" + #agifa142#152) % -106);
      |QBF aAlfa; aAlfa = aAlfa + "/"; |PATH_DAT #RefAnal@#aAlfa#;
      |ABRE #RefAnal@;
      #RefAnal@#0 = #con00002#6;
      |SI nTecla = 10;
         |LEE 000#RefAnal@.];
         |CONSULTA_CLAVE #1#RefAnal@;
         |SI FSalida = 0;
            #con00002#6 = #RefAnal@#0; |PINTA #con00002#6;
         |FINSI;
         |ERROR;
      |SINO;
         |LEE 000#RefAnal@.=;
         |SI FSalida ! 0;
            |MENAV "    Cuenta analitica inexistente"; |ERROR;
         |SINO;
            #con00002#6 = #RefAnal@#0; |PINTA #con00002#6;
         |FINSI;
      |FINSI;
      |CIERRA #RefAnal@; |DESCARGA_DEF #RefAnal@;
   |FINSI;
   |CIERRA #agifa142;
|FPRC;
