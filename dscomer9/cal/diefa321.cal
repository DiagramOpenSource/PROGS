|FICHEROS;
     diefa321 #0; ||Este Def.
     agifa010 #1; ||Albaranes (C).
     agifa071 #2; ||Albaranes (L).
     agifa019 #3; ||Articulos.
     agifa025 #4; ||Representantes.
     diefz321 #5; ||Temporal
     agifa084 #11;||Facturas (C).
     agifa069 #22;||Facturas (L).
     agifa024 #33;||Clientes
|FIN;

|VARIABLES;
     &nApi1 = 0;
     &nApi2 = 0;
     aEmp  = "";
     nAno  = 0;
     nFec  = 0;
     aFec  = "";
     fFec  = "";
     aAlfa = "";
     aAno  = "";
     aFich = "";
     nSal  = 0;
     nImp  = 0;
     swImp = 0;
     aRep  = "";
     &nTip = 0;
|FIN;

|PROCESO regulariza;
     #5#5 = aAno;
     |LEE 000#5=;
     |SI FSalida = 0; |ACABA; |FINSI;
     #5#2 = 0;
     #5#3 = 0;
     #5#4 = 0;
     |GRABA 020#5n;
|FPRC;

|DEFBUCLE regulariza;
     #5#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL,regulariza;
|FIN;

|PROCESO diefz321;
     |SI swImp = 0;
          swImp = 1;
          aRep = #5#0;
          |DDEFECTO #4;
          #4#0 = aRep;
          |LEE 000#4=;
     |FINSI;
     |SI aRep ! #5#0;
          |PIEINF;
          #0#10 = 0;
          #0#11 = 0;
          #0#12 = 0;
          #0#16 = 0;
          #0#17 = 0;
          #0#18 = 0;
          aRep = #5#0;
          |DDEFECTO #4;
          #4#0 = aRep;
          |LEE 000#4=;
     |FINSI;
     |DDEFECTO #33;
     #33#0 = #5#1;
     |LEE 000#33=;
     |PRINT;
     |SI #5#5 = nApi1;
          #0#10 = #0#10 + #5#2;
          #0#11 = #0#11 + #5#3;
          #0#12 = #0#12 + #5#4;
          #0#13 = #0#13 + #5#2;
          #0#14 = #0#14 + #5#3;
          #0#15 = #0#15 + #5#4;
     |FINSI;
     |SI #5#5 = nApi2;
          #0#16 = #0#16 + #5#2;
          #0#17 = #0#17 + #5#3;
          #0#18 = #0#18 + #5#4;
          #0#19 = #0#19 + #5#2;
          #0#20 = #0#20 + #5#3;
          #0#21 = #0#21 + #5#4;
     |FINSI;
|FPRC;

|DEFBUCLE diefz321;
     #5#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL,diefz321;
|FIN;

|PROCESO agifa069;
     |DDEFECTO #3;
     #3#0 = #22#2;
     |LEE 000#3=;
     |DDEFECTO #5;
     #5#0 = #11#6;
     #5#1 = #11#3;
     #5#5 = aAno;
     |LEE 000#5=;
     nSal = FSalida;
     #5#2 = #5#2 + #22#5;
     #5#3 = #5#3 + (#22#5 * #3#102);
     nImp = #22#9 < #11#5;
     nImp = nImp < #11#32;
     nImp = nImp < #11#7;
     #5#4 = #5#4 + nImp;
     |SI nSal = 0; |GRABA 020#5a; |FINSI;
     |SI nSal ! 0; |GRABA 020#5n; |FINSI;
|FPRC;

|PROCESO agifa084;
     aAlfa = #0#4;
     |QBF aAlfa;
     |SI aAlfa = ""; |ACABA; |FINSI;
     |SI #11#48 ! #0#4; |Y #11#48 ! #0#5; |Y #11#48 ! #0#6;
      |Y #11#48 ! #0#7; |Y #11#48 ! #0#8; |Y #11#48 ! #0#9;
          |ERROR;
     |FINSI;
|FPRC;

|DEFBUCLE agifa084;
     #11#4;
     6;
     #0#22,#0#25,000000,#0#2;
     #0#23,#0#26,999999,#0#3;
     ;
     NULL,agifa084,agifa069;
|FIN;

|PROCESO agifa071;
     |DDEFECTO #3;
     #3#0 = #2#2;
     |LEE 000#3=;
     |DDEFECTO #5;
     #5#0 = #1#6;
     #5#1 = #1#3;
     #5#5 = aAno;
     |LEE 000#5=;
     nSal = FSalida;
     #5#2 = #5#2 + #2#5;
     #5#3 = #5#3 + (#2#5 * #3#102);
     nImp = #2#9 < #1#5;
     nImp = nImp < #1#32;
     nImp = nImp < #1#7;
     #5#4 = #5#4 + nImp;
     |SI nSal = 0; |GRABA 020#5a; |FINSI;
     |SI nSal ! 0; |GRABA 020#5n; |FINSI;
|FPRC;

|PROCESO agifa010;
     aAlfa = #0#4;
     |QBF aAlfa;
     |SI aAlfa = ""; |ACABA; |FINSI;
     |SI #1#52 ! #0#4; |Y #1#52 ! #0#5; |Y #1#52 ! #0#6;
      |Y #1#52 ! #0#7; |Y #1#52 ! #0#8; |Y #1#52 ! #0#9;
          |ERROR;
     |FINSI;
|FPRC;

|DEFBUCLE agifa010;
     #1#4;
     6;
     #0#22,#0#25,000000,#0#2;
     #0#23,#0#26,999999,#0#3;
     ;
     NULL,agifa010,agifa071;
|FIN;

|PROCESO ejec; |TIPO 3;
     |IMPRE 0;
     |SI FSalida ! 0; |ACABA; |FINSI;
     |INFOR "diefa321";
     |SI FSalida ! 0; |FINIMP; |ACABA; |FINSI;
     aFich = Usuario;
     |NOME_DAT #5 aFich; |ABRE #5; |CIERRA #5; |DELINDEX #5;

     aAno = #0#1 % -104;
     nApi1 = aAno;
     #0#25 = #0#0;
     #0#26 = #0#1;

     |ABRE #3;
     |ABRE #5;
     |BUCLE agifa010;
     |BUCLE agifa084;
     |CIERRA #3;
     |CIERRA #5;
     ||-------- Montar A¤o Anterior.
     nAno  = aAno;
     nAno  = nAno - 1;
     aAno  = nAno;
     aAno  = ("0000" + aAno) % -104;
     nApi2 = aAno;

     |BUCLE regulariza;

     aFec  = #0#0 % -104;
     nFec  = aFec;
     nFec  = nFec - 1;
     aFec  = nFec;
     aFec  = ("0000" + aFec) % -104;
     fFec  = #0#0 % 105;
     fFec  = fFec + "." + aFec;
     #0#25 = fFec;
     aFec  = #0#1 % -104;
     nFec  = aFec;
     nFec  = nFec - 1;
     aFec  = nFec;
     aFec  = ("0000" + aFec) % -104;
     fFec  = #0#1 % 105;
     fFec  = fFec + "." + aFec;
     #0#26 = fFec;
     ||-----------------
     |DBASE aFich;
     aEmp = #0#24;
     |QBF aEmp;
     aEmp = ("00000" + aEmp) % -105;
     aFich = aFich + "fich/" + aEmp + "/";
     |PATH_DAT #1 aFich;
     |PATH_DAT #2 aFich;
     |PATH_DAT #3 aFich;
     |PATH_DAT #11 aFich;
     |PATH_DAT #22 aFich;
     |ABRE #3;
     |ABRE #5;
     |BUCLE agifa010;
     |BUCLE agifa084;
     |CIERRA #3;
     |CIERRA #5;
     ||----------------------------------------------------
     |ABRE #4;
     |ABRE #33;
     |BUCLE diefz321;
     |CIERRA #4;
     |CIERRA #33;
     |SI swImp = 1;
          nTip = 1;
          |PIEINF;
     |FINSI;
     |FININF;
     |FINIMP;
     |DELINDEX #5;
|FPRC;
