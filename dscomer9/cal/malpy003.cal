|FICHEROS;
     malpy003 #0;
     malpe087 #87;
     malpe088 #88;

     :/cmi/def/manm0012 #12; || Carpeta Remotas
|FIN;

|VARIABLES;
     aCliAnt      = "";
     aAlfa        = "";
     aAlfa1       = "";
     aAlfa2       = "";
     aNFichero    = "";
     aAbre        = "";
     aHost        = "";
     aOrigen      = "";
     aDestino     = "";
     aPathEnvio   = "";
     aBaseCrystal = "";
     aDocRemoto   = "";
     aDocServidor = "";
     aIPRemoto    = "";
     aBase        = "";

     nObraAnt     = 0;
     nError       = 0;
     nHandle      = 0;
     nPdte        = 0;
     nPrint       = 0;

  {1}aBaseLocal   = "";
  {1}aContiene    = "";
|FIN;

|PROCESO Tipo66Fmalpy3;  |TIPO 66;
     |ABRE #12;
     |LEE 000#12p;

     |CONSULTA_CLAVE #1#12;
     |SI FSalida = 0;
         #0#2 = #12#0;   |PINTA #0#2;
         #0#3 = #12#1;   |PINTA #0#3;
     |FINSI;
     |CIERRA #12;

     |ERROR;
|FPRC;

|PROCESO Tipo0C2Fmalpy3;
     |SI #0#1 = "V";  |ACABA;  |FINSI;

     |ABRE #12;
     #12#0 = #0#2;
     |LEE 000#12=;
     |SI FSalida ! 0;
         |MENAV "0000 No existe el Codigo de la Carpeta Remota.";
         |ERROR;
     |SINO;
         #0#3 = #12#1;   |PINTA #0#3;
     |FINSI;
     |CIERRA #12;
|FPRC;

|PROCESO Tipo0C1Fmalpy3;
     |C_M #0#2, 1, "S";
     |SI #0#1 = "V";
         |C_M #0#2, 1, "N";
     |FINSI;
|FPRC;

|PROCESO AbreImpresora;
     nError = 0;

     |SI #0#1 = "V";
         Impresora = "CrystalReport";
     |SINO;
         aAlfa1    = #87#4;  |QBT aAlfa1;
         aAlfa1    = (aAlfa1 + "------") % 106;
         aAlfa2    = ("0000" + #87#63) % -104;
         aAlfa     = "CrystalReport;" + aBaseCrystal;
         aNFichero = aAlfa1 + aAlfa2 + "PEN.pdf";
         aAlfa     = aAlfa + aNFichero;
         Impresora = aAlfa;
     |FINSI;

     |IMPRE -1;
     nError = FSalida;

     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |INFOR "malpy003";
     nError = FSalida;

     |SI FSalida ! 0;  |ACABA;  |FINSI;
|FPRC;

|PROCESO CierraImpresora;
     |PIEINF;
     |FININF;
     |FINIMP;

     |SI #0#1 = "E";
         |ABRE #12;
         #12#0 = #0#2;
         |LEE 000#12=;
         |SI FSalida = 0;
             aAbre = aBaseCrystal + "ftp.txt";
             |XABRE "WBC", aAbre, nHandle;
             |SI FSalida < 0;  |ACABA;  |FINSI;

             aAlfa = #12#2 + &13 + &10;                  |XGRABA nHandle, aAlfa;
             aAlfa = #12#3 + &13 + &10;                  |XGRABA nHandle, aAlfa;
             aAlfa = "lcd " + aBaseCrystal + &13 + &10;  |XGRABA nHandle, aAlfa;
             aAlfa = "cd " + #12#4;                      |QBF aAlfa;
             aAlfa = aAlfa + " " + &13 + &10;            |XGRABA nHandle, aAlfa;
             aAlfa = "bin " + &13 + &10;                 |XGRABA nHandle, aAlfa;
             aAlfa = "put " + aNFichero + &13 + &10;     |XGRABA nHandle, aAlfa;
             aAlfa = "bye " + &13 + &10;                 |XGRABA nHandle, aAlfa;

             |XCIERRA nHandle;

             aHost  = #12#1;  |QBF aHost;
             aAlfa  = "cmd " + &34 + "/c START /WAIT " + aBaseCrystal + "dsftp " + aAbre + " " + aHost + &34;
             |RSYSTEM aAlfa;
         |FINSI;
         |CIERRA #12;

         aAlfa = aBaseCrystal + aNFichero;
         |RFBORRA aAlfa;
     |FINSI;
|FPRC;

|PROCESO malpe088;
     nPdte = #88#5 - #88#43;
     |SI nPdte > 0;
         |PRINT;

         nPrint = 1;
     |FINSI;
|FPRC;

|DEFBUCLE malpe088;
     #88#1;
     ;
     #87#25, #87#0, 1;
     #87#25, #87#0, 999;
     ;
     NULL, malpe088;
|FIN;

|PROCESO malpe087;
     |SI nObraAnt ! -1;
         |SI aCliAnt ! #87#4; |O nObraAnt ! #87#63;  |Y nPrint = 1;
             |SI #0#1 = "V";
                 |PIEINF;
             |SINO;
                 |HAZ CierraImpresora;
                 |HAZ AbreImpresora;
             |FINSI;
          |FINSI;
          nPrint   = 0;
     |SINO;
          |HAZ AbreImpresora;
          nPrint   = 0;
     |FINSI;

     aCliAnt  = #87#4;
     nObraAnt = #87#63;
     |BUCLE malpe088;
|FPRC;

|DEFBUCLE malpe087;
     #87#6;
     ;
     "      ", 0000;
     "zzzzzz", 9999;
     ;
     NULL, malpe087;
|FIN;

|PROCESO Tipo3Fmalpy3;  |TIPO 3;
     nObraAnt = -1;

     |BUCLE malpe087;
     |HAZ CierraImpresora;
|FPRC;

|PROCESO TraeDsFtp;
     aOrigen  = aPathEnvio  + "/dsftp.exe";
     aDestino = aBaseCrystal + "dsftp.exe";

     aContiene1 = aDestino;
     |ESPECIFICA "REMOTOMD5", aContiene;
     aDocRemoto = FSalida;  |QBF aDocRemoto;

     aContiene1 = aOrigen;
     |ESPECIFICA "MD5", aContiene;
     aDocServidor = FSalida;  |QBF aDocServidor;

     |SI aDocRemoto ! aDocServidor;
         aIPRemoto = "";
         |IP_REMOTO aIPRemoto;  |QBT aIPRemoto;

         |SI aIPRemoto ! "";
             |ENVIA_FICHERO aOrigen, aDestino;
         |SINO;
             |COPIA_FICHERO aOrigen, aDestino;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO TraeDscristal;
     aOrigen  = aPathEnvio  + "/dscristal.dll";
     aDestino = aBaseLocal1 + "bin/dscristal.dll";

     aContiene1 = aDestino;
     |ESPECIFICA "REMOTOMD5", aContiene;
     aDocRemoto = FSalida;  |QBF aDocRemoto;

     aContiene1 = aOrigen;
     |ESPECIFICA "MD5", aContiene;
     aDocServidor = FSalida;  |QBF aDocServidor;

     |SI aDocRemoto ! aDocServidor;
         aIPRemoto = "";
         |IP_REMOTO aIPRemoto;  |QBT aIPRemoto;

         |SI aIPRemoto ! "";
             |ENVIA_FICHERO aOrigen, aDestino;
         |SINO;
             |COPIA_FICHERO aOrigen, aDestino;
         |FINSI;
     |FINSI;
|FPRC;

|PROGRAMA;
     aBaseLocal1 = "";
     |ESPECIFICA "RBASS", aBaseLocal;

     aBaseCrystal = aBaseLocal1 + "crystal/";

     |DBASE aBase;  |QBF aBase;
     aPathEnvio = aBase + "001254";  |MKDIR aPathEnvio;

     |HAZ TraeDsFtp;
     |HAZ TraeDscristal;

     |ABRE #0;
     |SI PARAMETRO = "";
         |CLS;
         |LEE 101#0p;
         |SI FSalida ! 0;
             |DDEFECTO #0;
             |GRABA 020#0b;
         |FINSI;

         |PINPA #0#0;
         |PINDA #0#0;

         |ENDATOS #1#0;
         |SI FSalida = 0;
             |GRABA 020#0a;
             |LIBERA #0;
         |FINSI;
     |SINO;
         |LEE 000#0p;
         |SI FSalida ! 0;  |ACABA;  |FINSI;
         |SI #0#2 = "  ";  |ACABA;  |FINSI;

         #0#1 = "E";
         |HAZ Tipo3Fmalpy3;
     |FINSI;
     |CIERRA #0;
|FPRO;
