|FICHEROS;
     iberw006  #6;  ||Tmp
     iberw006@ #16; ||Tmp
     iber0018  #18;
     iber0018@ #180;
|FIN;

|VARIABLES;
     &Tempo    = "";
     &enSwCheq = 0;
     aTempo6   = "";
     aTempo16  = "";
     aAlfa     = "";
     aDef      = "";
     nHay      = 0;
     nCentro   = 0;
     nSw       = 0;
|FIN;

|INCLUYE i_escank;

|PROCESO LinKit;
     #180#0 = nCentro;
     #180#1 = aEscan;
     |LEE 000#180=;
     |SI FSalida ! 0;
          enSwCheq = 1;
          aAlfa = "0000Centro: "+#180#0+" Producto: "+#180#1+"."+&13+"NO EXISTE";
          |MENAV aAlfa;
     |SINO;
          |SI #180#4 = "S"; |Y #180#7 = "S";
               |SI nSw = 0;
                    #6#0 = #180#0;
                    #6#1 = #180#1;
                    |LEE 000#6=;
                    |SI FSalida ! 0;
                         nHay = 1;
                         |GRABA 020#6n;
                    |FINSI;
               |FINSI;
               |SI nSw = 1;
                    #16#0 = #180#0;
                    #16#1 = #180#1;
                    |LEE 000#16=;
                    |SI FSalida ! 0;
                         nHay = 1;
                         |GRABA 020#16n;
                    |FINSI;
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Temporal16;
     #180#0 = #16#0;
     #180#1 = #16#1;
     |LEE 000#180=;
     |SI #180#4 = "S"; |Y #180#7 = "S";
          nCentro = #180#0;
          aEscan = #180#1;
          nCantiEscan = 1;
          nMaxNivel = 1;
          |HAZ DesglosaKit;
     |FINSI;
|FPRC;

|DEFBUCLE Temporal16;
     #16#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, Temporal16;
|FIN;

|PROCESO Temporal6;
     #180#0 = #6#0;
     #180#1 = #6#1;
     |LEE 000#180=;
     |SI #180#4 = "S"; |Y #180#7 = "S";
          nCentro = #180#0;
          aEscan = #180#1;
          nCantiEscan = 1;
          nMaxNivel = 1;
          |HAZ DesglosaKit;
     |FINSI;
|FPRC;

|DEFBUCLE Temporal6;
     #6#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, Temporal6;
|FIN;

|PROCESO Productos;
     |SI #18#4 = "S"; |Y #18#7 = "S";
          nCentro = #18#0;
          aEscan = #18#1;
          nCantiEscan = 1;
          nMaxNivel = 1;
          |HAZ DesglosaKit;
     |FINSI;
|FPRC;

|DEFBUCLE Productos;
     #18#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, Productos;
|FIN;

|PROCESO Pegote;
     |ABRE #180;
     |BUCLE Productos;
     |PARA; |SI nHay ! 0; |HACIENDO;
          nHay = 0; nSw = 1;
          |DELINDEX #16;
          |ABRE #16;
          |BUCLE Temporal6;
          |CIERRA #16;

          nHay = 0; nSw = 0;
          |DELINDEX #6;
          |ABRE #6;
          |BUCLE Temporal16;
          |CIERRA #6;
     |SIGUE;
     |CIERRA #180;
|FPRC;

|PROCESO Chequea;
     |DDEF aDef;
     aAlfa = aDef + "iber0018";
     |CARGA_DEF aAlfa, iber0018@;
     |SI FSalida = 0;
          aAlfa = aDef + "iber0018";
          |CARGA_DEF aAlfa, iberw006@;
          |SI FSalida = 0;
               |HAZ CreaTempo; aTempo6 = Tempo;
               |NOME_DAT #6 aTempo6; |DELINDEX #6;
               |HAZ CreaTempo; aTempo16 = Tempo;
               |NOME_DAT #16 aTempo16; |DELINDEX #16;

               |HAZ Pegote;

               |DELINDEX #6; Tempo = aTempo6; |HAZ BoraTempo;
               |DELINDEX #16; Tempo = aTempo16; |HAZ BoraTempo;

               |DESCARGA_DEF #iberw006@;
          |SINO;
               aDef = "0000Error al cargar " + aDef;
               |MENAV aDef;
          |FINSI;
          |DESCARGA_DEF #iber0018@;
     |SINO;
          aDef = "0000Error al cargar " + aDef;
          |MENAV aDef;
     |FINSI;
|FPRC;
