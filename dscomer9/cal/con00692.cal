|FICHEROS;
   con00692 #0;
   con00904 #10;
   agifa019 #11;
   agifa722 #12;
   con00002 #13;
   con00006 #14;

   con00693 #100;
   dsm00054 #101;
   dsm00055 #102;

   caconas@ #1003;
   canempr@ #1004;
   camasic@ #1005;
   caenlac@ #1006;

   goparti@ #5000;
|FIN;

|VARIABLES;
   &fich_alta = "";
   &enNoModif = 0;
   &PRMNT_enVer   = 0;

   nLinDoc   = 0;
   nBusca    = 0;
   nTecla    = 0;
   nMaxDigit = 0;
   nCalc     = 0;
   nSwHay    = 0;
   nDiario   = 0;
   nDocum    = 0;
   nDocgpt   = 0;
   nLinea    = 0;
   nPerAnt   = 0;
   FEstado   = 0;

   fFecha    = @;

   aCuenta   = "";
   aDiario   = "";
   aConcepto = "";

   aAlfa     = "";
   aAlfa1    = "";
   aAlfa2    = "";
   aSerie    = "";
   aNumero   = "";
   aLinea    = "";

   aAlfa1a   = "";
   aAlfa2a   = "";
   aAlfa3a   = "";
   aAlfa4a   = "";
   aAlfa1b   = "";
   aAlfa2b   = "";
   aAlfa3b   = "";
   aAlfa4b   = "";
|FIN;

|PROCESO dsm00055;
   #agifa019#0 = #dsm00055#0;
   |LEE 000#agifa019.=;
   |SI FSalida ! 0; |DDEFECTO #agifa019; |FINSI;

   |DDEFECTO #agifa722;
   #agifa722#0 = "MV";
   #agifa722#1 = #dsm00055#28;
   #agifa722#2 = #dsm00055#27;
   #agifa722#3 = "";
   ||#agifa722#3 = #dsm00055#11;
   |LEE 000#agifa722.];
   FEstado = FSalida;
   aAlfa1a = #agifa722#0; aAlfa2a = #agifa722#1;  aAlfa3a = #agifa722#2;  aAlfa4a = #agifa722#3;
   aAlfa1b = "MV";        aAlfa2b = #dsm00055#28; aAlfa3b = #dsm00055#27; aAlfa4b = #dsm00055#11;
   |QBF aAlfa1a; |QBF aAlfa2a; |QBF aAlfa3a; |QBF aAlfa4a;
   |QBF aAlfa1b; |QBF aAlfa2b; |QBF aAlfa3b; |QBF aAlfa4b;
   |SI FEstado = 0; |Y aAlfa1a = aAlfa1b; |Y aAlfa2a = aAlfa2b;
    |Y aAlfa3a = aAlfa3b; ||Y aAlfa4a = aAlfa4b;
       |ACABA;
   |FINSI;

   |DDEFECTO #con00904;
   #con00904#0 = #dsm00055#28;
   #con00904#1 = #dsm00055#27;
   #con00904#2 = #dsm00055#11;
   #con00904#3 = #dsm00055#0;
   aAlfa = #dsm00055#1; |QBF aAlfa; aAlfa = (("00000" + aAlfa) % -105);
   #con00904#4 = aAlfa;
   #con00904#5 = #dsm00055#4;
   #con00904#6 = #dsm00055#4 * #agifa019#9; || Precio coste medio
   #con00904#7 = #dsm00054#1;
   aAlfa = #dsm00055#5; |QBF aAlfa; aAlfa = (("00000" + aAlfa) % -105);
   #con00904#8 = aAlfa;
   |GRABA 020#con00904.n;

   nSwHay = 1;
|FPRC;

|DEFBUCLE dsm00055;
#dsm00055#1;
;
#dsm00054#2, #dsm00054#0, 00001;
#dsm00054#2, #dsm00054#0, 99999;
;
NULL,dsm00055;
|FIN;

|PROCESO dsm00054;
   |BUCLE dsm00055;
|FPRC;

|DEFBUCLE dsm00054;
#dsm00054#1;
1;
#con00692#0, #con00692#1, #con00692#4;
#con00692#0, #con00692#3, #con00692#5;
;
NULL,dsm00054;
|FIN;

|PROCESO GrabaAsto;
   nDiario = 1;
   fFecha  = #con00904#7;
   |SI aSerie ! #con00904#0; |O nDocgpt ! #con00904#1; ||O nLinDoc ! #con00904#2;
      #caenlac@#9 = -#con00904#6;
      |LEE 101#caconas@.p;
      |SI FSalida ! 0;
         |DDEFECTO #caconas@;
         |GRABA 020#caconas@.n;
         |LEE 101#caconas@.p;
      |FINSI;

      #caconas@#1 = #caconas@#1 + 1;
      aAlfa = "|NC"; nCalc = #caconas@#aAlfa#;
      |SI nCalc > 2; #caconas@#2 = #caconas@#2 + 1; |FINSI;
      nDocum = #caconas@#1;
      |GRABA 020#caconas@.a;
      |LIBERA #caconas@;

      aSerie = #con00904#0; nDocgpt = #con00904#1; nLinDoc = #con00904#2;
      nLinea = 10;
   |FINSI;

   #camasic@#0 = nDiario;
   #camasic@#1 = fFecha;
   #camasic@#2 = nDocum;
   |LEE 000#camasic@.=;
   |SI FSalida = 0;
      #camasic@#0 = nDiario;
      #camasic@#1 = fFecha;
      #camasic@#2 = 999999;
      |LEE 000#camasic@.];
      |SI FSalida = 0;
         |SI #camasic@#0 = nDiario; |Y #camasic@#1 = fFecha;
            nDocum = 1;
         |SINO;
            |LEE 000#camasic@.a;
            |SI FSalida = 0;
               |SI #camasic@#0 = nDiario; |Y #camasic@#1 = fFecha;
                   nDocum = #camasic@#2 + 1;
               |SINO;
                  nDocum = 1;
               |FINSI;
            |SINO;
               nDocum = 1;
            |FINSI;
         |FINSI;
      |SINO;
         |LEE 000#camasic@.u;
         |SI FSalida = 0;
            |SI #camasic@#0 = nDiario; |Y #camasic@#1 = fFecha;
               nDocum = #camasic@#2 + 1;
            |SINO;
               nDocum = 1;
            |FINSI;
         |SINO;
            nDocum = 1;
         |FINSI;
      |FINSI;
   |FINSI;

   #agifa722#0 = "MV";
   #agifa722#1 = #con00904#0;
   #agifa722#2 = #con00904#1;
   #agifa722#3 = "";
   ||#agifa722#3 = #con00904#2;
   |LEE 000#agifa722.];
   FEstado = FSalida;
   aAlfa1a = #agifa722#0; aAlfa2a = #agifa722#1; aAlfa3a = #agifa722#2; aAlfa4a = #agifa722#3;
   aAlfa1b = "MV";        aAlfa2b = #con00904#0; aAlfa3b = #con00904#1; aAlfa4b = #con00904#2;
   |QBF aAlfa1a; |QBF aAlfa2a; |QBF aAlfa3a; |QBF aAlfa4a;
   |QBF aAlfa1b; |QBF aAlfa2b; |QBF aAlfa3b; |QBF aAlfa4b;
   |SI FEstado ! 0; |O aAlfa1a ! aAlfa1b; |O aAlfa2a ! aAlfa2b;
    |O aAlfa3a ! aAlfa3b; ||O aAlfa4a ! aAlfa4b;
      |DDEFECTO #agifa722;
      #agifa722#0 = "MV";
      #agifa722#1 = #con00904#0;
      #agifa722#2 = #con00904#1;
      #agifa722#3 = "";
      ||#agifa722#3 = #con00904#2;
      #agifa722#4 = nDiario;
      #agifa722#5 = fFecha;
      #agifa722#6 = nDocum;
      #agifa722#7 = 10;
      aAlfa1 = ("000000" + #con00693#2) % -106;
      aAlfa2 = aAlfa1 % -101; aAlfa1 = aAlfa1 % 0105;
      #agifa722#8 = aAlfa1;
      #agifa722#9 = aAlfa2;
      #agifa722#11 = 1;
      |GRABA 020#agifa722.n;
   |FINSI;

   aAlfa1 = ("000000" + #con00693#2) % -106;
   aAlfa2 = aAlfa1 % -101; aAlfa1 = aAlfa1 % 0105;
   nPerAnt = aAlfa2;
   #canempr@#2 = aAlfa1;
   #canempr@#3 = aAlfa2;
   |LEE 000#canempr@.=;
   |SI FSalida = 0;
      nBusca = 0;
      aAlfa = fFecha; |QBF aAlfa;
      aAlfa = aAlfa % -102; nCalc = aAlfa;
      |SI nCalc ! #canempr@#26;
         |PARA; |SI FSalida = 0; |Y #canempr@#2 = aAlfa1; |HACIENDO;
            |LEE 000#canempr@.s;
            |SI nCalc = #canempr@#26; nBusca = 1; |SAL; |FINSI;
         |SIGUE;
         |SI nBusca = 1;
            nPerAnt = aAlfa2;
         |FINSI;
      |FINSI;
   |FINSI;

   nMaxDigit = 0;
   |SI #canempr@#14 ! 0; |Y #canempr@#14 ] nMaxDigit; nMaxDigit = #canempr@#14; |FINSI;
   |SI #canempr@#15 ! 0; |Y #canempr@#15 ] nMaxDigit; nMaxDigit = #canempr@#15; |FINSI;
   |SI #canempr@#16 ! 0; |Y #canempr@#16 ] nMaxDigit; nMaxDigit = #canempr@#16; |FINSI;
   |SI #canempr@#17 ! 0; |Y #canempr@#17 ] nMaxDigit; nMaxDigit = #canempr@#17; |FINSI;
   |SI #canempr@#18 ! 0; |Y #canempr@#18 ] nMaxDigit; nMaxDigit = #canempr@#18; |FINSI;
   |SI #canempr@#19 ! 0; |Y #canempr@#19 ] nMaxDigit; nMaxDigit = #canempr@#19; |FINSI;

   #con00006#0 = #con00904#4;
   |LEE 000#con00006.=;
   |SI FSalida ! 0; |DDEFECTO #con00006; |FINSI;

   #con00002#0 = #con00006#1;
   |LEE 000#con00002.=;
   |SI FSalida ! 0; |DDEFECTO #con00002; |FINSI;
   |DDEFECTO #caenlac@;
   #caenlac@#0 = nDiario;
   #caenlac@#1 = fFecha;
   #caenlac@#2 = nDocum;
   #caenlac@#3 = nLinea; nLinea = nLinea + 10;
   aAlfa = #con00002#0; |QBF aAlfa; aAlfa = "1" + aAlfa;
   aAlfa1 = #con00692#6; |QBF aAlfa1;
   aAlfa1 = aAlfa1 + aAlfa; aAlfa1 = aAlfa1 % 0; nCalc = aAlfa1;
   nCalc = nMaxDigit - nCalc; |SI nCalc < 0; nCalc = 0; |FINSI;
   aAlfa = #con00692#6; |QBF aAlfa;
   aAlfa1 = #con00002#0;  |QBF aAlfa1; aAlfa1 = "1" + aAlfa1;
   aAlfa = aAlfa + (("0" * nCalc)) + aAlfa1;
   aAlfa = #con00002#2; #caenlac@#4 = aAlfa;
   #caenlac@#5 = 0;
   #caenlac@#6 = 1;

   aAlfa = "MOVTO.ALMACENES" + #con00904#0 + " " + (("000000" + #con00904#1) % -106);

   #caenlac@#7  = aAlfa;
   #caenlac@#8 = "H";
   #caenlac@#9 = #con00904#6;
   aAlfa1 = ("000000" + #con00693#2) % -106;
   aAlfa2 = aAlfa1 % -101; aAlfa1 = aAlfa1 % 0105;
   #caenlac@#37 = aAlfa1;
   #caenlac@#38 = aAlfa2;
   #caenlac@#39 = #con00002#6;
   |GRABA 020#caenlac@.n;

   #con00006#0 = #con00904#8;
   |LEE 000#con00006.=;
   |SI FSalida ! 0; |DDEFECTO #con00006; |FINSI;

   #con00002#0 = #con00006#1;
   |LEE 000#con00002.=;
   |SI FSalida ! 0; |DDEFECTO #con00002; |FINSI;

   |DDEFECTO #caenlac@;
   #caenlac@#0 = nDiario;
   #caenlac@#1 = fFecha;
   #caenlac@#2 = nDocum;
   #caenlac@#3 = nLinea; nLinea = nLinea + 10;
   aAlfa = #con00002#0;  |QBF aAlfa; aAlfa = "1" + aAlfa;
   aAlfa1 = #con00692#6; |QBF aAlfa1;
   aAlfa1 = aAlfa1 + aAlfa; aAlfa1 = aAlfa1 % 0; nCalc = aAlfa1;
   nCalc = nMaxDigit - nCalc; |SI nCalc < 0; nCalc = 0; |FINSI;
   aAlfa = #con00692#6; |QBF aAlfa;
   aAlfa1 = #con00002#0;  |QBF aAlfa1; aAlfa1 = "1" + aAlfa1;
   aAlfa = aAlfa + (("0" * nCalc)) + aAlfa1;
   aAlfa = #con00002#2; #caenlac@#4 = aAlfa;
   #caenlac@#5 = 0;
   #caenlac@#6 = 1;

   aAlfa = "MOVTO.ALMACENES" + #con00904#0 + " " + (("000000" + #con00904#1) % -106);

   #caenlac@#7  = aAlfa;
   #caenlac@#8 = "D";
   #caenlac@#9 = #con00904#6;
   aAlfa1 = ("000000" + #con00693#2) % -106;
   aAlfa2 = aAlfa1 % -101; aAlfa1 = aAlfa1 % 0105;
   #caenlac@#37 = aAlfa1;
   #caenlac@#38 = aAlfa2;
   #caenlac@#39 = #con00002#6;
   |GRABA 020#caenlac@.n;

   nSwHay = 1;
|FPRC;

|DEFBUCLE Asientos;
#con00904#2;
;
INICIO;
FINAL;
;
NULL, GrabaAsto;
|FIN;

|PROCESO Asientos;
     |ABRE #con00693;
     #con00693#0 = #con00692#0;
     |LEE 000#con00693.=;
     |SI FSalida ! 0;
        aAlfa = "    No se ha definido la serie '" + #con00692#0 + "' en el control de paths. Proceso cancelado";
        |MENAV aAlfa;
        |CIERRA #con00693; |ACABA;
     |FINSI;
     |CIERRA #con00693;

     aAlfa = "t1" + Usuario; |NOME_DAT #con00904#aAlfa#;
     |DELINDEX #con00904; |ABRE #con00904;

     |ABRE #agifa722; |ABRE #agifa019;

     |SI PARAMETRO = "";
        aAlfa = "SELECT * FROM dsm00054 INTO ds" + Usuario + " WHERE ";
        aAlfa = aAlfa + "2 BETWEEN '" + #con00692#0 + "','" + #con00692#0 + "' AND ";
        aAlfa = aAlfa + "0 BETWEEN " + #con00692#1 + "," + #con00692#3 + " AND ";
        aAlfa = aAlfa + "1 BETWEEN " + #con00692#4 + "," + #con00692#5;
        |SQL aAlfa;
        |SI FSalida = 0;
           aAlfa = "ds" + Usuario; |NOME_DAT #dsm00054#aAlfa#;
        |FINSI;

        nSwHay = 0; |BUCLE dsm00054;

        |SI nSwHay = 0;
           |CIERRA #con00904; |CIERRA #agifa722; |ACABA;
        |FINSI;
    |SINO;
        |BUCLE dsm00055;
    |FINSI;

     aAlfa = #con00693#1; |QBF aAlfa;
     aAlfa = aAlfa + "def/caenlace.mas";
     |CARGA_DEF aAlfa, caenlac@;
     |SI FSalida = 0;
        aAlfa = #con00693#1; |QBF aAlfa;
        aAlfa = aAlfa + "def/caconasi.mas";
        |CARGA_DEF aAlfa, caconas@;
        |SI FSalida = 0;
           aAlfa = #con00693#1; |QBF aAlfa;
           aAlfa = aAlfa + "def/camaasic.mas";
           |CARGA_DEF aAlfa, camasic@;
           |SI FSalida = 0;
              aAlfa = #con00693#1; |QBF aAlfa;
              aAlfa = aAlfa + "def/canempre.mas";
              |CARGA_DEF aAlfa, canempr@;
              |SI FSalida = 0;
                 aAlfa = #con00693#1; |QBF aAlfa;
                 aAlfa = aAlfa + "fich/";
                 |PATH_DAT #canempr@#aAlfa#;
                 aAlfa = #con00693#1; |QBF aAlfa;
                 aAlfa = aAlfa + "fich/" + (("000000" + #con00693#2) % -106) + "/";
                 |PATH_DAT #caenlac@#aAlfa#; |PATH_DAT #camasic@#aAlfa#;
                 |PATH_DAT #caconas@#aAlfa#;
                 aAlfa = "pu" + Usuario; |QBF aAlfa; |NOME_DAT #caenlac@#aAlfa#;
                 |DELINDEX #caenlac@; |ABRE #caenlac@;
                 |ABRE #caconas@; |ABRE #camasic@; |ABRE #canempr@;
                 nSwHay = 0;

                 |ABRE #con00002; |ABRE #con00006;
                 |BUCLE Asientos;
                 |CIERRA #con00002; |CIERRA #con00006;

                 |SI nSwHay = 1;
                    |HAZ CogeVersion;
                    aAlfa = #con00693#1; |QBF aAlfa;
                    aAlfa = aAlfa + "fich/" + (("000000" + #con00693#2) % -106) + "/";
                    fich_alta = aAlfa;
                    aAlfa1 = #con00693#1; |QBF aAlfa1;
                    aAlfa1 = aAlfa1 - "contagen";
                    |SI FSalida ! 0;
                       aAlfa1 = ":contagen/dsapunte.tab;";
                    |SINO;
                       aAlfa1 = ":agicont/dsapunte.tab;";
                    |FINSI;
                    enNoModif = 100;
                    aAlfa = aAlfa1 + aAlfa + ",pu" + Usuario;
                    |CIERRA #caenlac@;
                    |SUB_EJECUTA_NP aAlfa;
                    |ABRE #caenlac@; |LEE 000#caenlac@.c;
                 |FINSI;
              |FINSI;
              |CIERRA #canempr@; |DESCARGA_DEF #canempr@;
              |CIERRA #camasic@; |DESCARGA_DEF #camasic@;
              |CIERRA #caconas@; |DESCARGA_DEF #caconas@;
              |CIERRA #caenlac@; |DELINDEX #caenlac@; |DESCARGA_DEF #caenlac@;
           |FINSI;
        |FINSI;
     |SINO;
        |CIERRA #con00904; |CIERRA #agifa722; |ACABA;
     |FINSI;
     |CIERRA #agifa722; |CIERRA #agifa019;

     |CIERRA #con00904; |DELINDEX #con00904;
|FPRC;

|PROCESO MiraSerie;
  |ABRE #con00693;
  #con00693#0 = #con00692#0;
  |LEE 000#con00693.=;
  |SI FSalida ! 0;
     aAlfa = "    No se ha definido la serie '" + #con00692#0 + "' en el control de paths. Proceso cancelado";
     |MENAV aAlfa; |ERROR;
  |FINSI;
  |CIERRA #con00693;
|FPRC;

|PROGRAMA;
   |SI PARAMETRO = "";
      |CLS;
      |CABEZA "Contabilizacion traspaso almacenes";
      |PINPA #0#0; |PINDA #0#0;
      |ENDATOS #1#0;
      |SI FSalida = 0; |HAZ Asientos; |FINSI;
   |SINO;
      |ABRE #dsm00054; |ABRE #dsm00055;
      aAlfa = PARAMETRO;
      aAlfa = aAlfa - ",";
      |SI FSalida ! 0;
         nCalc = ((FSalida / 100) ! 0) + 99; aSerie  = aAlfa % nCalc;
         nCalc = FSalida + 98;               aAlfa   = aAlfa % nCalc;
         aAlfa = aAlfa - ",";
         |SI FSalida ! 0;
            nCalc = FSalida + 98;               aLinea  = aAlfa % nCalc;
            nCalc = ((FSalida / 100) ! 0) + 99; aNumero = aAlfa % nCalc;
         |SINO;
            aNumero = aAlfa;
         |FINSI;
         #dsm00054#2 = aSerie;
         #dsm00054#0 = aNumero;
         |LEE 000#dsm00054.=;
         |SI FSalida ! 0; |DDEFECTO #dsm00054; |FINSI;
         |HAZ Asientos;
      |FINSI;
      |CIERRA #dsm00054; |CIERRA #dsm00055;
   |FINSI;
|FPRO;
