|FICHEROS;
     agifa010 #0;
     agifa071 #3;
     agifa019 #2;
     agifa024 #4;
     agifa038 #13;
     agifa048 #15;
     agifa049 #16;
     agifa007 #40;
     agifa142 #41;
     agifa027 #42;  || Contador.              || VIC Puesto el 15/07/98
     agifa321 #35;  || Parametros de Divisas
     agifa328 #36;  || Clientes/Divisas
     agifa329 #37;  || Lineas de Clientes/Divisas
     agifa324 #38;  || Divisas
     dsm00003 #39,MANTE;
     agifa048 #148;
     agifa049 #149;
     agifa204 #204;
     dsm00279 #279;
     referen@;
|FIN;

|VARIABLES;
     nModo = 0;
     &enGuerAuto = 0;
     nSal = 0;
     aSinError = "";
     aSql = "";
     aDef = "";
     &enModo036 = 0;
     nForma = 0;
     &nRieTotal = 0;
     &nRieUtili = 0;
     &aRieBloqu = "";
     &aBloquead = "";
     &eaTag    = "";

     aArti     = "";
     aOpera    = "";
     &cli      = "";
     &ser      = "";
     &ser_alb  = "";
     &num_alb  = 0;
     &abono    = "";   || Lo utilizo en agifa014
     &eaCodigo     = "";
     &eaTipoCodigo = "CL";

     nTecla     = 0;
     gsalida    = 0;

     &nDeci_Base     = 0;   || Decimales en Totales de la moneda base
     &nDeci_PreBase  = 0;   || Decimales en el Precio en moneda base
     &nDeci_BaseMx   = 0;   || Decimales en Totales de la moneda base (Maxima precision)
     &nDeci_PreBaseMx= 0;   || Decimales en el Precio en moneda base (Maxima precision)
     &nDeci_Div      = 0;   || Decimales en Totales de la divisa
     &nDeci_PreDiv   = 0;   || Decimales en el Precio en divisa
     &nDeci_TotVis   = 0;   || Decimales en Totales visibles (el de la moneda de trabajo)
     &nDeci_TotNoVis = 0;   || Decimales en Totales no visibles (el de la otra moneda)
     &nDeci_PreVis   = 0;   || Decimales en el Precio visual. (lineas)
     &nDeci_ImpVis   = 0;   || Decimales en el Importe visual. (lineas)
     &nDeci_BaseMxD  = 0;   || Decimales en Totales de la moneda base (Maxima precision)
     &nDeci_PreBasMxD= 0;   || Decimales en el Precio en moneda base (Maxima precision)
     &nDeci_DivD     = 0;   || Decimales en Totales de la divisa
     &nDeci_PreDivD  = 0;   || Decimales en el Precio en divisa
     &nDeci_TotVisD  = 0;   || Decimales en Totales visibles (el de la moneda de trabajo)
     &nDeci_TotNoVisD= 0;   || Decimales en Totales no visibles (el de la otra moneda)
     &nDeci_PreVisD  = 0;   || Decimales en el Precio visual. (lineas)
     &nDeci_ImpVisD  = 0;   || Decimales en el Importe visual. (lineas)
     nfecha    = 0;
     &ac_divisa = "N";   || Vale "S" cuando las divisas en ventas estan
                         || activadas y la serie es de divisas
     &pintado = 0;       || Sirve para controlar los pintados de las lineas
     &decim_divisa = 0;  || Decimales de la divisa
     &precio_divisa = 0; || Decimales del precio en divisa
     &decim_base = 0;    || Decimales de la moneda base
     &precio_base = 0;   || Decimales del precio en moneda base
     div_act = 0;        || Divisa actualizada. Me sirve en un PARA para que
                         || no salga hasta que este actualizada (div_act=1)
     &cli_divisa = "";   || Codigo de Cliente. Se pasa a CLIENTES/DIVISAS
     &divisa = "";       || Codigo de Divisa. Se pasa a ACTUAL. de DIVISAS
     nModo032 = 0;

     aAlfa = "";

     totalba = 0;
     imneto = 0;
     tf = 0;
     mes = 0;
     con = 0;
     &Cie_pasa      = 0;
     &Cie_fech      = @;
     &Cie_alma      = 0;
     margen = 0;
     importe = 0;
     indtope = 0;
     ind = 0;
     cant = 0;
     control = 0;
     mensaje01 = "0423Actualizando linea:";
     mensaje10 = "0000Este albaran contiene lineas provenientes de Pedidos";
     mensaje11 = "0000Este albaran contiene lineas provenientes de Depositos";
     mensaje12 = "0000Este albaran esta FACTURADO";
     mensaje13 = "0000Debe Eliminar previamente las lineas de Pedidos";
     mensaje14 = "0000Este albaran esta en modo Exportacion";
     mensajeri = "0000Se supera el Riesgo Limite del cliente";
     mensajei  = "2400NDesea imprimir este albaran? ";
     informe = "";
     informe1 = "agifa010";   ||no valorado
     informe2 = "agif1010";   ||valorado
     &iva1 = 0;
     &iva2 = 0;
     &iva3 = 0;
     &iva4 = 0;
     &iva5 = 0;
     &re1 = 0;
     &re2 = 0;
     &re3 = 0;
     &re4 = 0;
     &re5 = 0;
     &codigo="";
     &descrip="";
     &precio=0;
     &dto=0;
     &dto1 = 0;
     &unid=0;
     &imptot=0;
     &tiva=0;
     &linea=0;
     &supedino="";
     vacio="";
     &n_dec = 0;
     &n_pre = 0;
     &bl  = "                              ";
     puente    = "";
     n_copia   = 0;
     contenido = 0;
     iva_ant   = 0;
     modo = 0;
     alfa = "";
     cont     = "";
     contador = 0;
     caltot = 0;
     recalpre = 0;
     nImpDiv  = 0;

    || MENU PARA INTRODUCIR EL CODIGO DEL CLIENTE DE FORMA AUTOMATICA.
 {-1}meng           = "";                   || VIC Puesto 15/07/98
     meng1          = "2400";
     meng2          = "1";
     meng3          = "Contador:  ";
     meng4          = "AM";
     meng5          = " Automatico ";
     meng6          = " Manual ";
     so             = "";
     no             = "";
|FIN;

|CAMPOS;
|FINSI;

|INCLUYE i_varart;

|PROCESO Entra33; |TIPO 7;
     nModo = (FEntrada / 100) ! 0;
     |HAZ ControlUsuDV;
     |SI enErr ! 0; |PTEC 807; |ACABA; |FINSI;
     |ABRE #agifa142; |LEE 000#agifa142.p; |CIERRA #agifa142;
     |SI #agifa142#185 = "S"; #0#52 = #agifa142#188; |FINSI;

     |C_P #0#3, 0, enPosCli;
     |HAZ DefSerVta;
     |SI nModo = 1; |Y #279#24 = "S";
          |SI eaSerCli ! "";
               #0#52 = eaSerCli; |PTEC 802;
          |SINO;
               |PTEC 807; |PTEC 807; |ERROR; |ACABA;
          |FINSI;
     |FINSI;
|FPRC;

|CALCULO fecdep;|TIPO 7;
modo = (FEntrada / 100) ! 0;
|SI modo = 2;
  |CAMPO_MODIFICA #0#1 , 1 , "N";
|SINO;
  |CAMPO_MODIFICA #0#1 , 1 , "S";
|FINSI;
|FCAL;

|CALCULO cogecon0;|TIPO 0;
     control = 0;
|FCAL;


|CALCULO pedcon0;|TIPO 6;
     con = (FEntrada / 100) ! 0;
     |SI con = 2;|Y #0Campo ! Contenido;
          |SI #0#41 > 0;|O #0#42 > 0;|O #0#38 = AS; |O #0#39 = 999999;
               #0Campo = Contenido;
               |PINTA #0Campo;
               |SI #0#41 > 0; |MENAV mensaje10; |FINSI;
               |SI #0#42 > 0; |MENAV mensaje11; |FINSI;
               |SI #0#38 = AS; |MENAV mensaje12; |FINSI;
               |SI #0#39 = 999999; |MENAV mensaje14; |FINSI;
               |ERROR;
          |SINO;
               control = 1;
          |FINSI;
     |FINSI;
|FCAL;


|CALCULO dcon;|TIPO 6;
con = (FEntrada / 100) ! 0;
|SI con = 2;|Y #0Campo ! Contenido;
     |SI #0#38 = AS; |O #0#39 = 999999;
          |SI #0#38 = "S";
               |MENAV mensaje12;
          |SINO;
               |MENAV mensaje14;
          |FINSI;
        |ERROR;
      |FINSI;
|FINSI;
|FCAL;

|CALCULO acacp;|TIPO 2;
     |HAZ ControlUsuDV;
     |SI enErr ! 0; |ERROR; |ACABA; |FINSI;

     con = (FEntrada / 100) ! 0;
     nForma = 0 +. 1;
     enModo036 = con;
     |SI con ! 1;
          |SI #0#42 = 0; |O #0#41 ! 0;
               |MENAV "0000El albaran no proviene de depositos...";
               |ERROR; |ACABA;
          |FINSI;
     |FINSI;
     |SI con = 3;
          eaSer = #0#52;
          enDocu = #0#0;
          efFec = #0#1;
          |HAZ MiraHisV;
          |SI enErrHis ! 0;
               |ERROR;
               |ACABA;
          |FINSI;
     |FINSI;
     |SI con = 3;|Y #0#41 > 0;|O #0#38 = AS; |O #0#39 = 999999;
         |SI #0#41 > 0;
               |MENAV mensaje13;
         |FINSI;
         |SI #0#38 = AS;
               |MENAV mensaje12;
         |FINSI;
         |SI #0#39 = 999999;
               |MENAV mensaje14;
         |FINSI;
         |ERROR; |ACABA;
     |FINSI;

     |SI #0#69 = 0; || chequea el cambio
          |ABRE #agifa324;
          #agifa324#0 = #0#68;
          |LEE 020#agifa324.=;
          |SI FSalida = 0;
               #0#69 = #agifa324#2;
          |FINSI;
          |CIERRA #agifa324;
     |FINSI;

     |SI nForma = -1;
          |HAZ EsGuerola;
          |SI FSalida = 0;
               |CONFI "0000SLa adjudicacion se ha hecho de forma MANUAL?";
               enGuerAuto = FSalida;
          |FINSI;
     |FINSI;


     |SI control = 1;
          |HAZ actal;
     |FINSI;
     |HAZ actpf;

     |HAZ EsWeb;
     |SI FSalida = 0;
          |HAZ DocWeb;
     |FINSI;
|FCAL;

|CALCULO acttl;|TIPO 0;
     |SI caltot = 0;
          |PINTA 445,#3#1;
          #3#15 = #0#3;
          #3#16 = #0#34;
          #3#17 = #0#35;
          |GRABA 020#3a;
     |SINO;
          |SI recalpre = 1;
               |HAZ TotalLin71;
          |FINSI;
     |FINSI;

     |HAZ CalCabAgifa010;
|FCAL;

|DEFBUCLE agifa071;
     #3#1;
     ;
     #0#52, #0#0, 001;
     #0#52, #0#0, 999;
     ;
     NULL, acttl;
|FIN;

|CALCULO actal;|TIPO 0;
     |HAZ LimCabAgifa010;
     |BUCLE agifa071;
|FCAL;

|CALCULO siimpre;|TIPO 3;
     enGuerAuto = 0;
     |BUCLELIN 2 ModiHistoAV #0;
     |CONFI mensajei;
     |SI 0 = FSalida;
          |HAZ impreal;
     |FINSI;
|FCAL;

|PROCESO T33_4; |TIPO 4;
     |SELECT #1#0;
     |LEE 000#0=;
     |SI FSalida ! 0;
          |MENAV "0000Por favor, consulte el registro correcto antes de imprimir.";
          |ACABA;
     |SINO;
          |PINDA #0#0;
          FEntrada = 402;
     |FINSI;
     |HAZ impreal;
|FPRC;

|CALCULO impreal;
     modo = (FEntrada / 100) ! 0;
     |SI modo = 1; |O modo = 2;
          |GRABA 021#0a;
          |LIBERA #0;
     |FINSI;
     ser_alb = #0#52;
     num_alb = #0#0;
     |CIERRAT #0;
     abono = #0#43;                 || Abono S/N para discriminar los formatos
                                    || en agifa014
     |SUB_EJECUTA_NP "agifa014";       || Impresion de albaranes
     |ABRET #0;
     #0#52 = ser_alb;
     #0#0 = num_alb;
     |SI modo = 4;
          |LEE 001#0=;
     |SINO;
          |LEE 101#0=;
     |FINSI;
|FCAL;

|CALCULO alriesgo;|TIPO 0;
     eaSerie = #agifa010#52; eaTag = "AV"; |HAZ SacaRiesgo;

     |SI aRieBloqu = "S";
          |HAZ BloqCart;
          |ERROR; |ACABA;
     |FINSI;

     |SI #41#66 = "S";
          |SI nRieUtili ] nRieTotal; |Y nRieTotal ! 0;
               |SI #agifa142#167 = "S"; |AVISO; |FINSI;
               |MENSA mensajeri;
               |SI #agifa142#167 = "S"; |PAUSA; |BLIN 4; |FINSI;
          |FINSI;
     |FINSI;

     |SI #agifa142#14 = "S";
          puente = #agifa024#53;     |QBF puente;
          |SI puente ! "";
               |PUSHV 1008 1370;
               |SI #agifa142#20 = "S"; |AVISO; |FINSI;
               |BLANCO 1009 1369;
               |CUADRO 1008 1370;
               |ATRI R;
               |PINTA 1110 , #agifa024#53;
               |ATRI 0;
               |PAUSA;
               |POPV;
          |FINSI;
     |FINSI;
|FCAL;

|CALCULO valeya;|TIPO 3;
|FCAL;

|CALCULO actpf;|TIPO 0;
     cant = #0#15 < #0#5;
     cant = cant < #0#32;
     cant = cant < #0#7;
     |ABRE #4;
     #4#0 = #0#3;
     |LEE 121#4=;
     |SI 0 = FSalida;
        #4#50 = #4#50 +. cant;
        |GRABA 020#4a;
        |LIBERA #4;
     |FINSI;
     |CIERRA #4;

     enImpCliPen = cant * nForma;
     eaCliente = #0#3;
     efFecha = #0#1;
     |HAZ AcumulaCli;

     |HAZ riesgo_cli;
|FCAL;

|CALCULO totdep3;|TIPO 14;
|PINTA 0577,"  ";
|SI #0#41 = 0;|Y #0#42 = 0;
     |PINTA 0577,"AV";
|SINO;
     |SI #0#41 ! 0;
          |PINTA 0577,"PV";
     |SINO;
          |SI #0#42 ! 0;
               |PINTA 0577,"AD";
          |FINSI;
     |FINSI;
|FINSI;
|FCAL;

|PROCESO mira_par;
     |ABRE #41;
     #41#0 = "A";
     |LEE 001#41=;
     |SI FSalida ! 0; |DDEFECTO #41; |FINSI;
     |CIERRA #41;
     n_dec = #41#8;
     n_pre = #41#9;
     |HAZ LeeMonBase4;
     |HAZ LeeParamDiv4;
|FPRC;

|PROCESO mirar_de;
     #0#2 = #0#2 ! n_dec;
     |PINTA #0#2;
|FPRC;

|PROCESO riesgo_cli;
   eaTag = "AV"; |HAZ Riesgos;
|FPRC;

_____________________________________/ BUSCA UN CODIGO DE CLIENTE AUTOMATICO.
|PROCESO altacc; |TIPO 6;
     nTecla = FSalida;
                            || VIC Puesto por vicente 15/07/98
     alfa = #0#3;
     |QBF alfa;
     |SI alfa ! "";
          |ABRE #4;
          #4#0 = #0#3;
          |LEE 000#4=;
          |SI FSalida ! 0;
               |ABRE #41;
               |LEE 000#41p;
               |SI #41#1 = "S";
                    |HAZ Menusc;
               |FINSI;
               |CIERRA #41;
          |FINSI;
          |CIERRA #4;
     |FINSI;
|FPRC;

|PROCESO Menusc;
     |MENU meng;
     |SI FSalida = 1;    ||Automatico
          |ABRE #42;
          #42#2 = "";
          #42#0 = "clientes";
          |LEE 101#42=;
          |SI FSalida = 0;
               #42#1 = #42#1 + 1;
               contador = #42#1;
               |GRABA 021#42a;
          |SINO;
               contador = 1;
               #42#1 = 1;
               |GRABA 021#42n;
          |FINSI;
          cont = contador;
          cont = ("000000" + cont) % -106;
          #0Campo = cont;
          |PINTA #0Campo;
          |LIBERA #42;
          |CIERRA #42;
     |FINSI;
|FPRC;

___________/ VIC 15-07-98


|PROCESO LeeMonBase4; ||Lee los parametros de la moneda base
     |ABRE #agifa321;
     |ABRE #agifa324;
     |LEE 010#agifa321.p;   ||Leo parametros de divisa
     #agifa010#73 = #agifa321#9;  ||Cojo la Moneda Base de parametros
     #agifa324#0 = #agifa321#9;
     |LEE 010#agifa324.=;         ||Leo la ficha de la moneda base en Divisas
     #agifa010#74 = #agifa324#2;  ||Asigno el cambio de la moneda base con el euro
     decim_base = #agifa324#7;    ||Asigno los decimales de la moneda base
     precio_base = #agifa324#9;   ||Asigno los decimales del precio en la mon. base
     |CIERRA #agifa324;
     |CIERRA #agifa321;
|FPRC;

|PROCESO Param_Divisas4; |TIPO 0;
     pintado = 0;  ||Controla pintados de las lineas

     |ABRE #agifa324;
     #agifa324#0 = #agifa010#68;
     |LEE 001#agifa324.=;   || Leo la divisa
     |CIERRA #agifa324;

     decim_divisa = #agifa324#7;  || Decimales de la divisa
     precio_divisa = #agifa324#9;  || Decimales en el precio de la divisa
     nDeci_Div = decim_divisa;   || Decimales de la divisa
     nDeci_PreDiv = precio_divisa;  || Decimales en el precio de la divisa
     nDeci_DivD    = decim_divisa;   || Decimales de la divisa
     nDeci_PreDivD = precio_divisa;  || Decimales en el precio de la divisa
     |SI #0#70 = "B";   || Si trabajo con la moneda base ...
          nDeci_Base     = decim_base;
          nDeci_PreBase  = precio_base;
          nDeci_PreVis   = precio_divisa;
          nDeci_ImpVis   = decim_divisa;
          nDeci_BaseMx  = decim_base;
          nDeci_PreBaseMx = precio_base;
          nDeci_TotVis   = decim_base;
          nDeci_TotNoVis = decim_divisa;
          nDeci_PreVisD  = precio_divisa;
          nDeci_ImpVisD  = decim_divisa;
          nDeci_BaseMxD  = decim_base;
          nDeci_PreBasMxD= precio_base;
          nDeci_TotVisD  = decim_base;
          nDeci_TotNoVisD= decim_divisa;
     |SINO;     || Si trabajo con la divisa ...
          nDeci_Base     = decim_base; || Max. precision en los decimales de la moneda base
          nDeci_PreBase  = precio_base;  || Max. precision en los dec. del precio de la moneda base
          nDeci_PreVis   = precio_base;
          nDeci_ImpVis   = decim_base;
         |SI #0#69 = 1;
              nDeci_BaseMx    = decim_base;    || sin triangulacion
              nDeci_PreBaseMx = precio_base;
         |SINO;
              nDeci_BaseMx    = #agifa321#10;  || con triangulacion
              nDeci_PreBaseMx = #agifa321#11;
         |FINSI;
          nDeci_TotVis   = decim_divisa;
          nDeci_TotNoVis = decim_base;
          nDeci_PreVisD   = precio_base;
          nDeci_ImpVisD   = decim_base;
         |SI #0#69 = 1;
              nDeci_BaseMxD   = decim_base;    || sin triangulacion
              nDeci_PreBasMxD = precio_base;
         |SINO;
              nDeci_BaseMxD   = #agifa321#10;  || con triangulacion
              nDeci_PreBasMxD = #agifa321#11;
         |FINSI;
          nDeci_TotVisD   = decim_divisa;
          nDeci_TotNoVisD = decim_base;
     |FINSI;
     nDeci_BaseMx = nDeci_BaseMxD;
     nDeci_Div = nDeci_DivD;
     nDeci_ImpVis = nDeci_ImpVisD;
     |HAZ Deci_IVA;
|FPRC;

|PROCESO LeeParamDiv4; |TIPO 0;
     |ABRE #agifa321;
     |LEE 001#agifa321.p;    || Leo parametros de divisas
     |ABRE #agifa007;
     #agifa007#26 = #agifa010#52;
     |LEE 001#agifa007.=;        || Leo la serie

     |SI #agifa007#39 = "N"; |O #agifa321#1 = "N";
          #0#70 = "B";
     |FINSI;

     |SI #0#70 = "D";      || Si la mon. de trabajo es la divisa...
       || ...escondo los campos de la moneda base :
       |CAMPO_POSICION #3#6 , 1 , 1009;
       |CAMPO_POSICION #3#9 , 1 ,1026;
       |CAMPO_VISUAL #3#6 , 1 , "N";
       |CAMPO_VISUAL #3#9 , 1 , "N";
       |CAMPO_LINEAL #3#6 , 1 , "N";
       |CAMPO_LINEAL #3#9 , 1 , "N";
       |CAMPO_MODIFICA #3#6 , 1 , "N";
       || ...y pongo en pantalla los de divisa
       |CAMPO_POSICION #3#36 , 1 , 1644;
       |CAMPO_POSICION #3#37 , 1 ,1668;
       |CAMPO_VISUAL #3#36 , 1 , "S";
       |CAMPO_VISUAL #3#37 , 1 , "S";
       |CAMPO_MODIFICA #3#36 , 1 , "S";
       |CAMPO_LINEAL #3#36 , 1 , "S";
       |CAMPO_LINEAL #3#37 , 1 , "S";
       |SI #agifa007#39 = "S"; |Y #agifa321#1 = "S"; ||Si la serie es de ...
          ac_divisa = "S";  || ... divisas, y en parametros estan activadas ...
          || ... pongo como modificables los campos relacionados con divisas
          |CAMPO_MODIFICA #0#68 , 1 , "S";
          |CAMPO_MODIFICA #0#69 , 1 , "S";
          |CAMPO_MODIFICA #0#70 , 1 , "S";
          |CAMPO_MODIFICA #0#71 , 1 , "S";
          |CAMPO_MODIFICA #0#72 , 1 , "S";
          |SI #agifa321#3 = "S";  || Si la visualizacion de parametros esta activada
            || ... pongo los campos visual. de lineas como visualizables
            |CAMPO_VISUAL #3#38, 1 , "S";
            |CAMPO_VISUAL #3#39 , 1 , "S";
          |SINO;
            || ... los pongo como no visualizables
            |CAMPO_VISUAL #3#38 , 1 , "N";
            |CAMPO_VISUAL #3#39 , 1 , "N";
          |FINSI;
       |SINO; || Si la serie no es de divisas, o no estan activados parametros
          ac_divisa = "N";
          #0#70 = "B";  || La moneda de trabajo es por fuerza la moneda base
          |PINTA #0#70;
          || Pongo como no modificables los campos relacionados con divisas
          |CAMPO_MODIFICA #0#70 , 1 , "N";
          |CAMPO_MODIFICA #0#71 , 1 , "N";
          |CAMPO_MODIFICA #0#72 , 1 , "N";
          |CAMPO_MODIFICA #0#68 , 1 , "N";
          |CAMPO_MODIFICA #0#69 , 1 , "N";
          || Pongo como no visualizables los campos visual. de lineas
          |CAMPO_VISUAL #3#38, 1 , "N";
          |CAMPO_VISUAL #3#39, 1 , "N";
       |FINSI;
     |SINO;  || Si la moneda de trabajo es la moneda base
       || ...escondo los campos en divisa :
       |CAMPO_POSICION #3#36 , 1 , 1009;
       |CAMPO_POSICION #3#37 , 1 ,1026;
       |CAMPO_VISUAL #3#36 , 1 , "N";
       |CAMPO_VISUAL #3#37 , 1 , "N";
       |CAMPO_MODIFICA #3#36 , 1 , "N";
       |CAMPO_LINEAL #3#36 , 1 , "N";
       |CAMPO_LINEAL #3#37 , 1 , "N";

       || ...coloco los campos en moneda base:
       |CAMPO_POSICION #3#6 , 1 , 1644;
       |CAMPO_POSICION #3#9 , 1 ,1668;
       |CAMPO_VISUAL #3#6 , 1 , "S";
       |CAMPO_VISUAL #3#9 , 1 , "S";
       |CAMPO_MODIFICA #3#6 , 1 , "S";
       |CAMPO_LINEAL #3#6 , 1 , "S";
       |CAMPO_LINEAL #3#9 , 1 , "S";
       |SI #agifa007#39 = "S"; |Y #agifa321#1 = "S"; || Si la serie es de divisas...
          ac_divisa = "S"; || ... y divisas estan activadas en parametros
          || Pongo como modificables los campos relacionados con divisas
          |CAMPO_MODIFICA #0#68 , 1 , "S";
          |CAMPO_MODIFICA #0#69 , 1 , "S";
          |CAMPO_MODIFICA #0#70 , 1 , "S";
          |CAMPO_MODIFICA #0#71 , 1 , "S";
          |CAMPO_MODIFICA #0#72 , 1 , "S";
          |SI #agifa321#3 = "S";  || Si la visualizacion esta activada en parametros ...
            || Pongo como visualizables los campos visual. de las lineas
            |CAMPO_VISUAL #3#38, 1 , "S";
            |CAMPO_VISUAL #3#39 , 1 , "S";
          |SINO; || Los pongo como no visualizables
            |CAMPO_VISUAL #3#38 , 1 , "N";
            |CAMPO_VISUAL #3#39 , 1 , "N";
          |FINSI;
       |SINO;  || Si la serie no es de divisas o no estan activados los parametros
          ac_divisa = "N";
          #0#70 = "B";  || La moneda de trabajo es por fuerza la moneda base
          || Pongo como no modificables los campos relacionados con divisas
          |CAMPO_MODIFICA #0#70 , 1 , "N";
          |CAMPO_MODIFICA #0#71 , 1 , "N";
          |CAMPO_MODIFICA #0#72 , 1 , "N";
          |CAMPO_MODIFICA #0#68 , 1 , "N";
          |CAMPO_MODIFICA #0#69 , 1 , "N";
          || Pongo como no visualizables los campos visual. de lineas
          |CAMPO_VISUAL #3#38, 1 , "N";
          |CAMPO_VISUAL #3#39, 1 , "N";
       |FINSI;
     |FINSI;
     |CIERRA #agifa007;
|CIERRA #agifa321;
|HAZ Param_Divisas4;
|FPRC;

|PROCESO CambioDivisa4; |TIPO 0;
nModo032 = (FEntrada / 100) ! 0;
|SI #0Campo ! Contenido; |Y nModo032 = 1;   ||Si cambia el valor o es alta
  |SI Campo = 3; || Si llamo al proceso desde el cod. de cliente ...
    |LEE 001#agifa024.=;
    #0#68 = #agifa024#192;  || Asigno la divisa por defecto del cliente
    #0#70 = #agifa024#193;  || Asigno la moneda de trabajo del cliente
    |PINTA #0#70;
  |FINSI;
  #agifa007#26 = #agifa010#52;
  |LEE 001#agifa007.=;  || Leo la serie
   |SI #agifa007#39 = "S"; |Y #agifa321#1 = "S";
      |SI #agifa024#191 = "S"; || Si el cliente es de divisa pactada
         |ABRE #agifa328;
         |ABRE #agifa329;
         #agifa329#0 = #agifa010#3;
         #agifa329#2 = #agifa010#68;
         #agifa329#7 = "N";
         |SELECT #2#agifa329;  || Con esta clave ...
         |LEE 011#agifa329.=;  || ... busco periodos no finalizados de esa divisa
         |SI FSalida = 0;  || Si existe un periodo no finalizado
           |PARA div_act = 0; |SI div_act = 0; |HACIENDO;
             #agifa010#68 = #agifa329#2;        || Cargo la divisa ...
             #agifa010#69 = #agifa329#3;        || ...y el cambio
             |LEE 001#agifa329.=;   || Leo las lineas de Clientes/Divisas
             nfecha = #agifa010#1 - #agifa329#6;  || Resto la fecha de fin de periodo a la actual
             |SI nfecha > 0;  || Si la fecha actual supera a la del periodo asignado ...
                |MENAV "0000ATENCION deberia Actualizar primero la divisa en Clientes/Divisas";
                cli_divisa = #agifa010#3;  || Le envio el cod. de cliente a "agifa328"
                |SUB_EJECUTA_NP "agifa328.tab";  || Ejecuto Clientess/Divisas
                |ERROR;
             |SINO;
                div_act = 1;  || Para salir del PARA
             |FINSI;
           |SIGUE;
         |SINO;  || Si no existe un periodo no finalizado para esa divisa
           |MENAV "0000No existe un periodo activo para la divisa en Clientes / Divisas";
           cli_divisa = #agifa010#3;  || Le paso a "agifa328" el cliente
           |SUB_EJECUTA_NP "agifa328.tab";  || Ejecuto Clientes / Divisas
           |ERROR;
           ||#agifa010#68 = #agifa010#73;
           ||#agifa010#69 = #agifa010#74;
         |FINSI;
         |SELECT #1#agifa329;
         |CIERRA #agifa329;
         |CIERRA #agifa328;
      |SINO; || Si el Cliente no es de Divisa pactada ...
         |ABRE #agifa324; || ...miro directamente en divisas
         #agifa324#0 = #agifa010#68;
         |LEE 001#agifa324.=;
         |SI FSalida = 0; ||Si existe la divisa...
            #agifa010#69 = #agifa324#2; || la recojo
            |SI #agifa324#5 ! -1; || Si los dias de margen de actualizacion no
                                  || son = -1 (indefinidos)
              |PARA div_act = 0; |SI div_act = 0; |HACIENDO;
                 #agifa010#68 = #agifa324#0;        ||Cargo el valor de la divisa
                 #agifa010#69 = #agifa324#2;        ||Cargo el valor del cambio
                 |LEE 001#agifa324.=;  || Leo la divisa
                 nfecha = #agifa010#1 - #agifa324#4;
                 |SI nfecha > #agifa324#5; |Y #agifa324#5 ! -1;  || Si la fecha actual
                    |MENAV "0000ATENCION deberia Actualizar primero la divisa";
                    divisa = #agifa010#68;
                    |SUB_EJECUTA_NP "agifa326.tab";
                    |ERROR;
                 |SINO;
                    div_act = 1;
                 |FINSI;
              |SIGUE;
            |FINSI;
         |FINSI;
         |CIERRA #agifa324;
      |FINSI;
   |SINO;
     #agifa010#68 = #agifa010#73;
     #agifa010#69 = #agifa010#74;
   |FINSI;
|PINTA #agifa010#68;
|PINTA #agifa010#69;
|SI Campo = 3; || Si llamo al proceso desde el cod. de cliente ...
  |HAZ LeeParamDiv4;
|SINO;
  |HAZ Param_Divisas4;
|FINSI;
|FINSI;
|FPRC;

|PROCESO convportes; |TIPO 6;  || Convierte los portes a la mon. base
|SI #0#70 = "B";  || Si la moneda de trabajo es la moneda base ...
  #0#11 = #0#81;  || ... basta con asignarlo
|SINO;    || Si la moneda de trabajo es la divisa
  #0#11 = #0#81 / #0#69 * #0#74;   || ... lo convierto
|FINSI;
|HAZ dcon;
|FPRC;

|PROCESO convvarios; |TIPO 6;  || Convierte los varios a la mon. base
|SI #0#70 = "B";  || Si la moneda de trabajo es la moneda base ...
  #0#13 = #0#82;  || ... basta con asignarlo
|SINO;    || Si la moneda de trabajo es la divisa ...
  #0#13 = #0#82 / #0#69 * #0#74;  || ... lo convierto
|FINSI;
|HAZ dcon;
|FPRC;


|PROCESO LeeAlb; |TIPO 13;  || Sirve para cargar los valores cada vez que
                            || cambio de ficha
pintado = 0;   || controla pintado de las lineas
|HAZ LeeMonBase4;
|HAZ LeeParamDiv4;
|FPRC;

|CALCULO cambfecha4;
|SI #0#71 ! "O";
  #0#72 = "        ";
  |PINTA #0#72;
  |C_M #0#72,1,"N";
  |C_V #0#72,1,"N";
|SINO;
  |C_M #0#72,1,"S";
  |C_V #0#72,1,"S";
|FINSI;
|FCAL;

|CALCULO modomod4; |TIPO 6;
   |SI nModo032 = 2; |Y #0Campo ! Contenido;
       |MENAV "0000No se puede modificar el campo.";
       #0Campo = Contenido;
       |PINTA #0Campo;
       |ERROR;
   |FINSI;
   |SI #0Campo ! Contenido; |Y Campo = 70;
     |HAZ LeeParamDiv4;
   |FINSI;
|FCAL;

|PROCESO caltotal4; |TIPO 0; ||Proceso especial para casos determinados como
                            ||modificar los campos de cabecera en dtos, cambio divisas etc
                            ||Recalcula total depositos, descuentos y ivas de lineas
                            ||En el caso de cambiar el cambio de divisas recalcula tambien precio... de lineas

   |SI #0Campo ! Contenido;||Solo si cambia
          caltot = 1;
          |SI Campo = 69; ||Si viene del cambio divisa
              recalpre = 1; ||Recalcular tambien precios linea
          |FINSI;
          |HAZ convportes;
          |HAZ convvarios;
          |HAZ actal;

          |PINTA #0#78;
          caltot = 0;
          recalpre = 0;
   |FINSI;
|FPRC;

|PROCESO linfer;
  #39#0 = #0#3;
  #39#1 = 1;
|FPRC;

|PROCESO lsuper;
  #39#0 = #0#3;
  #39#1 = 9999;
|FPRC;

|PROCESO Ce; |TIPO 0;  ||Direccion de entrega
     gsalida = FSalida;
     |ABRE #41; |LEE 000#41p; |SI FSalida ! 0;  |DDEFECTO #41;  |FINSI;
     nModo032 = (FEntrada / 100) ! 0;

     |ABRE #4;
     #4#0 = #0#3;
     |LEE 000#4=;
     |CIERRA #4;

     |SI nModo032 = 1;
          |ABRE #39;
          #39#0 = #0#3;
          #39#1 = #0#84;
          |LEE 000#39=;
          |CIERRA #39;
          |HAZ AsignaDir033;
     |FINSI;
     |SI gsalida = 10;
          #0#64 = #41#100;  |PINTA #0#64;
          |ABRE #39;
          #39#0 = #0#3;
          #39#1 = 1;
          |LEE 101#39=;
          |SI FSalida = 0;
             |LEE 101#39s;
             |SI FSalida = 0;
                |SI #39#0 = #0#3; ||El cliente tiene mas de una direccion de entrega
                   |CONSULTA_F_CLAVE #1#39,linfer,lsuper;
                   |SI FSalida ] 0;
                      #0#4 = #39#10;   |PINTA #0#4;
                      #0#29 = #39#10;  |PINTA #0#29;
                      #0#30 = #39#2; |PINTA #0#30;
                      #0#31 = #39#3; |PINTA #0#31;
                      #0#33 = #39#7; |PINTA #0#33;
                      #0#62 = #39#6; |PINTA #0#62;
                      #0#84 = #39#1;
                      |SI #39#14 = "  ";
                          #0#6 = #4#25;
                      |SINO;
                          #0#6 = #39#14;
                      |FINSI;
                      |PINTA #0#6;
                   |FINSI;
                |FINSI;
             |FINSI;
         |FINSI;
         |CIERRA #39;
     |FINSI;

     |SI nTecla = 11; |O #41#102 = "S"; |O #41#102 = "X";
          eaCodigo = #0#3;
          eaFuerza = "N";
          |SI nTecla = 11; eaFuerza = "S"; |FINSI;
          |SUB_EJECUTA_NP "dsz99994";
          |SI #41#102 = "N";  |Y nModo032 ! 2; |ERROR;  |FINSI;
     |FINSI;

     |HAZ Pago7_33;
|FPRC;

|PROCESO s; |TIPO 0;              || VIC 20/07/98
     cli=#0#3;
     ser=#0#52;
     |SUB_EJECUTA_NP "agifv011.tab;por_ejemplo"
|FPRC;

|PROCESO pinta_obs; |TIPO 14;
     |ABRE #204; |LEE 000#204p; |CIERRA #204;
     puente = #agifa204#6; |QBF puente;
     |SI puente ! "";
         |ATRI I;  |PINTA 2005 , puente;    |ATRI 0;
         |C_M #0#55 , 1 , "S";
     |FINSI;

     puente = #agifa204#7; |QBF puente;
     |SI puente ! "";
         |ATRI I;  |PINTA 2030 , puente;    |ATRI 0;
         |C_M #0#56 , 1 , "S";
     |FINSI;

     puente = #agifa204#8; |QBF puente;
     |SI puente ! "";
         |ATRI I;  |PINTA 2055 , puente;    |ATRI 0;
         |C_M #0#57 , 1 , "S";
     |FINSI;

     |ATRI I;
|FPRC;

|PROCESO AsignaDir033;
     #0#4 = #39#10;   |PINTA #0#4;
     #0#29 = #39#10;  |PINTA #0#29;
     #0#30 = #39#2; |PINTA #0#30;
     #0#31 = #39#3; |PINTA #0#31;
     #0#33 = #39#7; |PINTA #0#33;
     #0#62 = #39#6; |PINTA #0#62;
     #0#84 = #39#1; |PINTA #0#84;
     |SI #39#14 = "  ";
         #0#6 = #4#25;
     |SINO;
         #0#6 = #39#14;
     |FINSI;
     |PINTA #0#6;
|FPRC;

|PROCESO MinDir033;
     #39#0 = #0#3;
     #39#1 = 1;
|FPRC;

|PROCESO MaxDir033;
     #39#0 = #0#3;
     #39#1 = 9999;
|FPRC;

|PROCESO Dir033_0; |TIPO 0;
     |SI FSalida = 2; |ACABA; |FINSI;
     nTecla = FSalida;
     |SI nTecla = 12;    || F4 Alta direccion
          |PUSHV 0501 2380;
          |ENTLINEAL #1#39, -2, 1, 22, 1, MinDir033, MaxDir033;
          |POPV;
          nTecla = 18;
     |FINSI;
     |ABRE #39;
     |SI nTecla = 18;   || F10  Todas las direcciones
          #39#0 = #0#3;
          #39#1 = #0#84;
          |LEE 000#39];
          |CONSULTA_F_CLAVE #1#39, MinDir033, MaxDir033;
          |SI FSalida = 0;
               |HAZ AsignaDir033;
          |SINO;
               |ERROR; |ACABA;
          |FINSI;
     |FINSI;
     #39#0 = #0#3;
     #39#1 = #0#84;
     |LEE 000#39=;
     |SI FSalida = 0;
          |SI #39#18 = "N";
               |CONFI "0000NDireccion de entrega desactiva. ¿Continuar?";
               |SI FSalida ! 0; |ERROR; |ACABA; |FINSI;
          |FINSI;
     |SINO;
          |MENAV "0000No existe direccion de entrega...";
          |ERROR; |ACABA;
     |FINSI;
     |CIERRA #39;
     |SI #0Campo ! Contenido;
          |HAZ AsignaDir033;
     |FINSI;
|FPRC;

|PROCESO Dir033_7; |TIPO 7;
     |SI #41#71 = "S";
          |ABRE #39;
          #39#0 = #0#3;
          #39#1 = 1;
          |LEE 000#39];
          |SI FSalida = 0; |Y #39#0 = #0#3;
               |LEE 000#39s;
               |SI FSalida = 0; |Y #39#0 = #0#3;
                   aSinError = "S";
                   |HAZ Dir033_66;
                   aSinError = "N";
                   |SI nSal = 0; |ERROR; |FINSI;
               |FINSI;
          |FINSI;
          |CIERRA #39;
     |FINSI;
|FPRC;

|PROCESO Dir033_66; |TIPO 66;
     nModo = (FEntrada / 100) ! 0;
     |ABRE #39;
     aSql = "SELECT * FROM dsm00003 INTO sq" + Usuario + ".def WHERE ";
     aSql = aSql + "0 == " + #0#3 + " AND 18 == S";
     |SQL aSql;
     |SI FSalida = 0;
          |DDEF aDef; |QBF aDef;
          aDef = aDef + "sq" + Usuario + ".mas";
          |CARGA_DEF aDef, referen@;
          |SI FSalida = 0;
               |ABRE #referen@;
               #referen@#0 = #0#3;
               #referen@#1 = #0#84;
               |LEE 000#referen@.]
               |CONSULTA_CLAVE #1#referen@;
               nSal = FSalida;
               |SI FSalida = 0;
                    |SI nModo ! 2; |O #referen@#0 ! #0#3; |O #referen@#1 ! #0#84;
                         #39#0 = #referen@#0;
                         #39#1 = #referen@#1;
                         |LEE 020#39=;
                         |HAZ AsignaDir033;
                    |FINSI;
               |SINO;
                    |SI aSinError ! "S";
                         |ERROR;
                    |FINSI;
               |FINSI;
               |CIERRA #referen@;
               |DELINDEX #referen@;
               |DESCARGA_DEF #referen@;
               |FBORRA aDef;
          |FINSI;
     |FINSI;
     |CIERRA #39;
|FPRC;

|PROCESO Cli33_7; |TIPO 7;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 1;
          |C_M #0#3, 1, "S";
     |SINO;
          |C_M #0#3, 1, "N";
     |FINSI;

     |SI #279#24 = "S";
          |C_M #0#3, 1, "N";
          |SI nModo = 1;
               |SI eaSerCli ! ""; #0#3 = eaCliSer; |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Pago7_33; |TIPO 7;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 1;
          |HAZ EsTagloma;
          |SI FSalida = 0;
               eaAlfa = #0#52;
               |HAZ TagloDefFP;
               |SI eaAlfa ! ""; #0#8 = eaAlfa; |PINTA #0#8; |FINSI;
               |SI eaQuitaDto = "S";
                    #0#5 = 0; |PINTA #0#5;
                    #0#32 = 0; |PINTA #0#32;
                    #0#7 = 0; |PINTA #0#7;
               |FINSI;
          |FINSI;
          |HAZ EsGestral;
          |SI FSalida = 0;
               eaAlfa = #0#52;
               |HAZ GestrDefFP;
               |SI eaAlfa ! ""; #0#8 = eaAlfa; |PINTA #0#8; |FINSI;
               |SI eaQuitaDto = "S";
                    #0#5 = 0; |PINTA #0#5;
                    #0#32 = 0; |PINTA #0#32;
                    #0#7 = 0; |PINTA #0#7;
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO IVA033_66; |TIPO 66;
     enTiva = #0Campo;
     efFiva = #0#1;
     |HAZ ConsuIVA;
     |SI eaDiva = "";
          |ERROR;
     |SINO;
          #0Campo = enTiva; |PINTA #0Campo;
     |FINSI;
|FPRC;

|PROCESO IVA033_0; |TIPO 0;
     enTiva = #0Campo;
     efFiva = #0#1;
     |HAZ SacaIVA;
     |SI eaDiva = "";
          |MENAV "0000No existe tipo iva para esta fecha...";
          |ERROR; |ACABA;
     |SINO;
          |HAZ caltotal4;
     |FINSI;
|FPRC,

|PROCESO DocWeb;
     |DDEF aAlfa;
     aAlfa = aAlfa + "coima038";
     |CARGA_DEF aAlfa, referen@;
     |SI FSalida = 0;
          |ABRE #referen@;
          #referen@#0 = "AV";
          #referen@#1 = #0#52;
          #referen@#2 = #0#0;
          |SI enModo036 = 3;
               |BORRA 000#referen@.c;
          |SINO;
               |LEE 101#referen@.=;
               |SI FSalida ! 0; |GRABA 020#referen@.b; |FINSI;
               #referen@#3 = #0#1;
               #referen@#4 = "N";
               |GRABA 020#referen@.a;
          |FINSI;
          |CIERRA #referen@;
          |DESCARGA_DEF #referen@;
     |FINSI;
|FPRC;
