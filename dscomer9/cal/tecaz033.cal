|FICHEROS;
     tecaz033 #0;
     agifa010 #10;
     tecaw023 #23,MANTE; ||TMP
     tecaw024 #24; || TMP
     tecam036 #36; ||Aux. Agencias Trans.
     tecam038 #38; ||Aux.Albaranes
|FIN;

|VARIABLES;
     &etiqueta1 = 0;
     aAlfa = "";
     &enFin = 0;
     &eaEtiqueta = "";
     &enEtiqueta = 0;
     &ser_alb = "";
     &num_alb = 0;
     &Tempo = "";
     aInforme = "";
     nLinea = 0;
     nConta = 0;
     nLin = 0;
     aCliente = 0;
     nHay = 0;
     nRenum = 0;
     nUltima = 0;
     aTempo23 = "";
     aTempo24 = "";
     nBultos = 0;
     nUltimo = 0;
|FIN;

|PROCESO Reimprimir;
     aAlfa = #0#12;
     |C_M #0#14, 1, aAlfa;
     |C_M #0#15, 1, aAlfa;
     |C_M #0#16, 1, aAlfa;
     |C_M #0#17, 1, aAlfa;
     |SI aAlfa = "N";
          #0#14 = 0; |PINTA #0#14;
          #0#15 = 99999999; |PINTA #0#15;
          #0#16 = 1; |PINTA #0#16;
          #0#17 = 999; |PINTA #0#17;
     |FINSI;
|FPRC;

|PROCESO Imprime;
     |DDEFECTO #10;
     #10#52 = #24#1;
     #10#0 = #24#2;
     |LEE 000#10=;

     #23#3 = #24#1;
     #23#4 = #24#2;
     |LEE 020#23=;  || Por la Clave2

     aInforme = #23#8; |QBF aInforme;
     |INFOR aInforme;  ||"tecam036";
     |SI FSalida = 0;
          nBultos = #24#3;
          nUltimo = nBultos;
          |SI #0#17 < nUltimo; nUltimo = #0#17; |FINSI;
          |SI #0#13 = "S"; nBultos = 1; |FINSI;
          |PARA enEtiqueta = 1; |SI enEtiqueta [ nBultos; |HACIENDO enEtiqueta = enEtiqueta + 1;
               |SI enEtiqueta ] #0#16; |Y enEtiqueta [ #0#17;
                    eaEtiqueta = ("0000" + enEtiqueta) % -104;
                    |SI nUltima = #24#0; |Y enEtiqueta = nUltimo;
                         enFin = 1; || Carga variable en la ultima etiqueta
                    |FINSI;
                    etiqueta1 = enEtiqueta;
                    |PRINT;
                    |PIEINF;
               |FINSI;
          |SIGUE;
          |FININF;
     |FINSI;
|FPRC;

|DEFBUCLE Imprime;
     #24#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, Imprime;
|FIN;

|PROCESO Agrupa;
     |DDEFECTO #38;
     #38#0 = #23#3;
     #38#1 = #23#4;
     |LEE 101#38=;
     |SI #0#12 = "N";
          |SI FSalida ! 0; |GRABA 020#38b; |FINSI;
          #38#7 = "S";
          #38#9 = #23#5;
          #38#10 = #0#11;
          |GRABA 020#38a;
     |FINSI;
     |LIBERA #38;

     #10#52 = #23#3;
     #10#0 = #23#4;
     |LEE 020#10=;

     |DDEFECTO #24;
     #24#0 = #38#9;
     |LEE 101#24=;
     |SI FSalida ! 0; |GRABA 020#24b; |FINSI;
     #24#1 = #10#52;
     #24#2 = #10#0;
     #24#3 = #24#3 + #10#2;
     #24#4 = #24#4 + #38#2;
     #24#5 = #24#5 + #38#3;
     |SI #38#4 = "S";
          #24#6 = #24#6 + #10#67;
     |FINSI;
     #24#7 = #24#7 + #38#5;
     |GRABA 020#24a;
     |LIBERA #24;

     |SI nUltima < #24#0; nUltima = #24#0; |FINSI;
|FPRC;

|DEFBUCLE Agrupa;
     #23#2;
     ;
     INICIO;
     FINAL;
     ;
     NULL, Agrupa;
|FIN;

|PROCESO Impresion;
     |IMPRE 1;
     |SI FSalida = 0;
          |ABRE #10;
          |ABRE #38;
          |ABRE #24;
          |BUCLE Agrupa;
          |ABRE #23; |SELECT #2#23;
          |BUCLE Imprime;
          |CIERRA #23;
          |CIERRA #24;
          |CIERRA #38;
          |CIERRA #10;
          |FINIMP;
     |FINSI;
|FPRC;

|PROCESO Renumerar;
     #23#5 = #23#5 + nRenum;
     |GRABA 020#23a;
     |LIBERA #23;
|FPRC;

|DEFBUCLE Renumerar;
     #23#1;
     ;
     nLinea;
     99999;
     be;
     NULL, Renumerar;
|FIN;

|PROCESO Renumera; |TIPO 0;
     |SI #23Campo ! Contenido;
          |GRABA 020#23a;
          |CONFI "N0000Renumerar:";
          |SI FSalida = 0;
               nLinea = #23#0 + 1;
               nRenum = #23#5 - Contenido;
               |PTEC 807;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Modifica; |TIPO 0;
     |SI #23Campo = Contenido; |Y FSalida = 0;
          |ENCAMPO #5#23;
     |FINSI;
|FPRC;

|PROCESO Contador;
     |DDEFECTO #36;
     #36#0 = #10#88;
     |LEE 101#36=;
     |SI FSalida ! 0;
          |GRABA 020#36b;
          #36#1 = 1;
          nConta = 1;
     |SINO;
          nConta = #36#1;
     |FINSI;
     #36#1 = nConta + 1;
     |GRABA 020#36a;

     |LIBERA #36;
|FPRC;

|PROCESO Agifa010;
     |SI #10#2 = 0;
          |ACABA;
     |FINSI;

     |DDEFECTO #38;
     #38#0 = #10#52;
     #38#1 = #10#0;
     |LEE 000#38=;
     |SI #38#7 ! #0#12; |ACABA; |FINSI;

     |SI #0#12 = "S";
          |DDEFECTO #36;
          #36#0 = #10#88;
          |LEE 000#36=;
          nConta = #38#9;
          |SI #38#9 < #0#14; |O #38#9 > #0#15;
               |ACABA;
          |FINSI;
     |SINO;
          |SI #0#10 = "S";
               |SI aCliente ! #10#3;
                    aCliente = #10#3;
                    |HAZ Contador;
               |FINSI;
          |SINO;
               |HAZ Contador;
          |FINSI;
     |FINSI;

     |DDEFECTO #36;
     #36#0 = #10#88;
     |LEE 000#36=;

     nLin = nLin + 1;
     |DDEFECTO #23;
     #23#0 = nLin;
     #23#1 = #10#3;
     #23#2 = #10#84;
     #23#3 = #10#52;
     #23#4 = #10#0;
     #23#5 = nConta;
     #23#6 = #10#4;
     #23#7 = #10#29;
     #23#8 = #36#2;
     |GRABA 020#23n;
     nHay = 1;
|FPRC;

|DEFBUCLE Agifa010;
     #10#3;
     43, 88;
     #0#0 , #0#4 , #0#6 , #0#2, "N", #0#8;
     #0#1 , #0#5 , #0#7 , #0#3, "N", #0#8;
     ;
     NULL, NULL, NULL, NULL, NULL, Agifa010;
     #10#3, #10#84;
     || Agen, Cli, Dir Ent
|FIN;

|PROCESO Min;
     #23#0 = 1;
     |SI FSalida = "POSICION";
          #23#0 = nLinea - 1;
     |FINSI;
|FPRC;

|PROCESO Max;
     #23#0 = 99999;
|FPRC;

|CALCULO Tipo3;
     |HAZ CreaTempo; aTempo23 = Tempo;
     |NOME_DAT #23 Tempo; |DELINDEX #23;
     |HAZ CreaTempo; aTempo24 = Tempo;
     |NOME_DAT #24 Tempo; |DELINDEX #24;

     |ABRE #23;
     |ABRE #36;
     |ABRE #38;
     |BUCLE Agifa010;
     |CIERRA #38;
     |CIERRA #36;
     |CIERRA #23;
     |SI nHay = 0;
          |MENAV "0000Seleccion Vacia...";
     |SINO;
          |PINPA #0#23;
          |PARA nRenum = 1; |SI nRenum ! 0; |HACIENDO;
               nRenum = 0;
               |ENTLINEAL #1#23, 1, 4, 20, 0, Min, Max;
               |SI nRenum ! 0;
                    |BUCLE Renumerar;
               |FINSI;
          |SIGUE;
          |CONFI "S0000Imprimir Etiquetas";
          |SI FSalida = 0;
               |ABRE #10;
               |HAZ Impresion;
               |CIERRA #10;
          |FINSI;
     |FINSI;
     aTempo23 = Tempo; |DELINDEX #23; |HAZ BoraTempo;
     aTempo24 = Tempo; |DELINDEX #24; |HAZ BoraTempo;
|FCAL;

|PROGRAMA;
     |HAZ SacaIVA;
     |HAZ MiraLogos; || Copia Logos para el Crystal

     |SI ser_alb = ""; |Y num_alb = 0;
         |CLS;
         |CABEZA "Emitir Etiquetas Envio";
         |PINPA #0#0;
         |PINDA #0#0;
         |ENDATOS #1#0;
         |SI FSalida = 0;  |HAZ Tipo3;  |FINSI;
     |SINO;
         |DDEFECTO #0;
         #0#6  = ser_alb;
         #0#7  = ser_alb;
         #0#2  = num_alb;
         #0#3  = num_alb;
         #0#4 = "01.01.0000";
         #0#5 = "31.12.9999";

         |ABRE #10;
         #10#52 = ser_alb;
         #10#0 = num_alb;
         |LEE 020#10=;
         |CIERRA #10;
         |ABRE #38;
         #38#0 = #10#52;
         #38#1 = #10#0;
         |LEE 000#38=;
         |CIERRA #38;

         #0#8 = #10#88;
         #0#12 = #38#7;
         |HAZ Tipo3;
     |FINSI;
|FPRO;
