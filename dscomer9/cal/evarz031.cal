|FICHEROS;
     evarz031 #0;
     evarm004 #4;
     agifa010 #10;
     evarm021 #21;
     evarm022 #22;
     agifa104 #104;
     agifa071 #71;
     agifa073 #73;
|FIN;

|VARIABLES;
     &enCB = 0;
     &enTalla = 0;
     nPrime = 0;
     nInfor = -1;
     nImpre = -1;
     nUni = 0;
     nConta = 0;
     aServi = "";
     nContaServi = 0;
     aAlfa = "";
     i = 0;
     nSwCorrige = 0;
     nUniAlb = 0;
|FIN;

|PROCESO GraServida;
     |DDEFECTO #22;
     #22#0 = #71#29;
     #22#1 = #71#0;
     #22#2 = #71#1;
     #22#3 = #71#2;
     #22#5 = #71#31;
     #22#6 = #71#12;
     #22#7 = #71#21;
     #22#9 = #10#1;
     #22#10 = #10#3;
     #22#4 = #21#3;  || modelo
     #22#8 = #21#7;  || referencia

     aAlfa = #21#0; |QBF aAlfa;
     |SI aAlfa = "";
          #22#11 = #73#5;
     |SINO;
          #22#11 = 1;
          #22#12 = #21#2; || talla
          #22#13 = #21#0; || CB
     |FINSI;
     |GRABA 020#22n;
|FPRC;

|PROCESO evarm0021;
     |SI nSwCorrige = 2;
          |SI #21#11 = "S";
               nUniAlb = nUniAlb - 1;
          |SINO;
               |SI nUniAlb > 0;
                    |HAZ GraServida;
                    nUniAlb = nUniAlb - 1;
                    #21#11 = "S";
                    |GRABA 020#21a;
               |FINSI;
          |FINSI;
     |FINSI;

     |SI nSwCorrige = 1;
          aAlfa = #21#0; |QBF aAlfa;
          |SI nUni = 0;              || no hay tallas
               |SI aAlfa ! "";
                    |BORRA 020#21a;  || es con tallas
               |FINSI;
          |SINO;
               |SI aAlfa = "";
                    |BORRA 020#21a;  || es sin tallas
               |FINSI;
          |FINSI;
     |FINSI;

     |SI nSwCorrige = 0;
          |SI aServi = "S";        || Linea pedido servida
               |SI #21#11 = "S";   || Unidad Servida
                    |SI nUniAlb > 0;
                         |HAZ GraServida;
                         nContaServi = nContaServi + #22#11;
                         nUniAlb = nUniAlb - #22#11;
                    |FINSI;
               |FINSI;
          |FINSI;
          nConta = nConta + 1;
     |FINSI;
|FPRC;

|DEFBUCLE evarm0021;
     #21#1;
     ;
     #73#28, #73#0, #73#1, "            ";
     #73#28, #73#0, #73#1, "zzzzzzzzzzzz";
     be;
     NULL, evarm0021;
|FIN;

|PROCESO evarm0022;
     |BORRA 020#22a;
|FPRC;

|DEFBUCLE evarm0022;
     #22#1;
     ;
     #71#29, #71#0, #71#1, "            ";
     #71#29, #71#0, #71#1, "zzzzzzzzzzzz";
     be;
     NULL, evarm0022;
|FIN;

|PROCESO LinPedi;
     |SI #73#2 ] #0#2; |Y #73#2 [ #0#3;
          |SELECT #4#71;
          #71#31 = #73#28;
          #71#12 = #73#0;
          #71#21 = #73#1;
          |LEE 000#71=;
          |SI FSalida = 0;
               aServi = "S";
               nUniAlb = #71#5;

               #10#52 = #71#29;
               #10#0 = #71#0;
               |LEE 000#10=;
          |SINO;
               aServi = "N";
               nUniAlb = 0;
          |FINSI;

          #4#30 = #73#28;
          #4#31 = #73#0;
          #4#32 = #73#1;
          |LEE 000#4=;
          |SI FSalida = 0;
               nUni = #4#27;
               |PARA i = 3; |SI i [ 15; |HACIENDO i = i + 1;
                    nUni = nUni + #4i;
               |SIGUE;
          |SINO;
               nUni = 0;
          |FINSI;

          nConta = 0; nContaServi = 0;
          |SI aServi = "S";
               |BUCLE evarm0022;
          |FINSI;

          nSwCorrige = 1;
          |BUCLE evarm0021;

          nSwCorrige = 0;
          |BUCLE evarm0021;

          |SI aServi = "S"; |Y nContaServi ! #71#5;
               nUniAlb = #71#5;
               nSwCorrige = 2;
               |BUCLE evarm0021;
               nSwCorrige = 0;
          |FINSI;

          |SI nUni ! 0;
               |SI nUni ! nConta; |O nUni ! #73#5;
                    aAlfa = "0000Pedido con unidades incorrectas:" + &13+ #73#28 + "/" + #73#0 + "/" + #73#1;
                    ||MENAV aAlfa;
                    enTalla = nUni;
                    enCB = nConta;
                    |HAZ Imprime;
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE Pedidos;
     #104#2;
     1;
     #0#0, #0#4, #0#6, #0#8;
     #0#1, #0#5, #0#7, #0#9;
     ;
     NULL, NULL, LinPedi;
|FIN;

|PROCESO Imprime;
     |SI nPrime = 0;
          nPrime = 1;
          |IMPRE 0;
          nImpre = FSalida;
          |SI nImpre = 0;
               |INFOR "evarz031";
               nInfor = FSalida;
          |FINSI;
     |FINSI;
     |SI nImpre = 0; |Y nInfor = 0; |PRINT; |FINSI;
|FPRC;

|PROCESO T3; |TIPO 3;
     |ABRE #4;
     |ABRE #10;
     |ABRE #22;
     |ABRE #71;
     |BUCLE Pedidos;
     |CIERRA #71;
     |CIERRA #22;
     |CIERRA #10;
     |CIERRA #4;

     |SI nInfor = 0; |PIEINF;  |FININF; |FINSI;
     |SI nImpre = 0; |FINIMP; |FINSI;
|FPRC;
