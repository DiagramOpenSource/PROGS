||Para controlar las facturas ya Enviadas, voy a utilizar el campo 117
||del agifa134, que NO se utilizar Para absolutamente NADA.


|FICHEROS;
     pcegz077 #0;

     pcegm053 #553;

     agifa024 #24;
     agifa134 #134;
     agifa059 #59;
     agifa071 #71;
|FIN;

|VARIABLES;
     ||Para el QuitarRaros
     aAlfa1 = "";
     aCadena = "";
     aLen = "";
     nLen = 0;
     nCaracter = 0;
     aSinRaros = "";
     nCampo = 0;
     nPosi = 0;
     aCaracter = "";

     nPrecio = 0;
     aPrefijo = "";
     aSerie = "";
     aUltimo = "";
     aIPRemoto = "";
     aNombreFic = "";
     aOrigen = "";
     aDestino = "";

     nExiste = 0;
     aMensaje = "";
     aRespuesta = "";

     nSwPrimeraLinea = 0;
     aBase = "";
     aPathEnvios = "";
     nHandle2 = 0;

     fFecha = @;
     aFecha = "";
     aDia = "";
     aMes = "";
     aAnyo = "";

     aHora = "";
     aHoras = "";
     aMinutos = "";
     aFicDestino = "";

     aTexto = "";
     aReturn = "";
     aRut = "";

     ||DSSELECT
     aPathDif = "";
     &enError = 0;
     {1}aLocal = "";
     aDirLocal = "";
     aPathDSSelect = "";
     aAux = "";
     aBorra = "";
     aParaIni = "";
     aCarpetaCaptura = "";
     &eaUnidadBasico = "";
     aTextoIni = "";
     nHandle = 0;
     aEjecuta = "";
     aPrograma = "";
     aAbre = "";
     aPath = "";
     nError = 0;
     aFichero = "";
     aLong = "";
     nLong = 0;
     nCarac = 0;
     aCarac = "";
     aProg = "";
     &eaUnidad = "";
     aAlfa = "";
|FIN;

|PROCESO MeteTitulos;
     aTexto = &34 + "TIPO (33: Factura electrónica, 34:Factura exenta electrónica, 46: Factura de compra electrónica, 56: Nota débito electrónica,";
     |XGRABA nHandle2, aTexto;

     aTexto = " 61:Nota de crédito electrónica, 39: Boleta electrónica; 41: Boleta exenta electrónica )" + &34;
     |XGRABA nHandle2, aTexto;

     aTexto = ";FOLIO;SECUENCIA;FECHA;RUT;RAZONSOCIAL;GIRO;COMUNA;DIRECCION;AFECTO;PRODUCTO;DESCRIPCION;CANTIDAD;PRECIO;PORCENTDSCTO;EMAIL;";
     |XGRABA nHandle2, aTexto;

     aTexto = &34 + "TIPOSERVICIO (1: Boletas de servicios periódico ó Facturas de servicios periódicos domiciliarios; ";
     |XGRABA nHandle2, aTexto;

     aTexto = "2:Boletas de servicios periódicos domiciliarios ó Facturas de otros servicios periódicos; ";
     |XGRABA nHandle2, aTexto;

     aTexto = "3:Boletas de venta y servicios ó Factura de servicios)" + &34;
     |XGRABA nHandle2, aTexto;
     aTexto = ";PERIODODESDE;PERIODOHASTA;FECHAVENCIMIENTO;CODSUCURSAL;VENDEDOR;CODRECEPTOR;CODITEM;";
     |XGRABA nHandle2, aTexto;

     aTexto = "UNIDADMEDIDA(HR, KG, M3, MR, PM, PP, QTAL, TBDM, TBDU, TON, UNID);";
     |XGRABA nHandle2, aTexto;

     aTexto = "PORCENTDSCTO2;PORCENTDSCTO3;CODIGOIMP;MONTOIMP;";
     |XGRABA nHandle2, aTexto;
     aTexto = "INDICADORTRASLADO(1:Operacion constituye venta,2:Ventas por efectuar,3:Consignaciones,";
     |XGRABA nHandle2, aTexto;

     aTexto = "4:Entrega gratuita,5:Traslados internos,6:Otros traslados no venta,7:Guia de devolucion);";
     |XGRABA nHandle2, aTexto;

     aTexto = "FORMAPAGO(1:Contado, 2:Credito, 3:Sin costo);";
     |XGRABA nHandle2, aTexto;

     aTexto = "MEDIOPAGO(CH:Cheque,CF:Cheque a fecha,LT:letra,EF:Efectivo,PE:Pago a cta.cte,TC:Tarjeta Credito,OT:Otro);";
     |XGRABA nHandle2, aTexto;

     aTexto = "TERMINOSPAGOSDIAS;TERMINOSPAGOCODIGO;COMUNADESTINO;RUTSOLICITANTEFACTURA; ";
     |XGRABA nHandle2, aTexto;

     aTexto = "PRODUCTOCAMBIOSUJETO; CANTIDADMONTOCAMBIOSUJETO;";
     |XGRABA nHandle2, aTexto;

     aTexto = " TIPOGLOBALAFECTO (1:DSCTO%, 2:DSCTO$, 3:RCGO%, 4:RCGO$);VALORGLOBALAFECTO;";
     |XGRABA nHandle2, aTexto;

     aTexto = "TIPOGLOBALEXENTO (1:DSCTO%, 2:DSCTO$, 3:RCGO%, 4:RCGO$);VALORGLOBALEXENTO;";
     |XGRABA nHandle2, aTexto;

     aTexto = aReturn;
     |XGRABA nHandle2, aTexto;
|FPRC;

|PROCESO MeteLinea;

     aTexto = #pcegz077#1 + ";";
     |XGRABA nHandle2, aTexto;

     aPrefijo = "0000";

     aSerie = #agifa134#49;
     |QBF aSerie;
     |SI aSerie ! "";
          |SI #agifa134#49 = #pcegm053#1;
               aPrefijo = #pcegm053#2 + "000";
          |FINSI;
          |SI #agifa134#49 = #pcegm053#3;
               aPrefijo = #pcegm053#4 + "000";
          |FINSI;
          |SI #agifa134#49 = #pcegm053#5;
               aPrefijo = #pcegm053#6 + "000";
          |FINSI;
          |SI #agifa134#49 = #pcegm053#7;
               aPrefijo = #pcegm053#8 + "000";
          |FINSI;
          |SI #agifa134#49 = #pcegm053#9;
               aPrefijo = #pcegm053#10 + "000";
          |FINSI;
          |SI #agifa134#49 = #pcegm053#11;
               aPrefijo = #pcegm053#12 + "000";
          |FINSI;
          |SI #agifa134#49 = #pcegm053#13;
               aPrefijo = #pcegm053#14 + "000";
          |FINSI;
          |SI #agifa134#49 = #pcegm053#15;
               aPrefijo = #pcegm053#16 + "000";
          |FINSI;
          |SI #agifa134#49 = #pcegm053#17;
               aPrefijo = #pcegm053#18 + "000";
          |FINSI;
          |SI #agifa134#49 = #pcegm053#19;
               aPrefijo = #pcegm053#20 + "000";
          |FINSI;
     |FINSI;
     ||Folio
     aAux = aPrefijo + ("000000" + #agifa134#0) % -106;
     aTexto = aAux + ";";
     |XGRABA nHandle2, aTexto;

     ||Secuencia
     aTexto = #agifa071#1 + ";";
     |XGRABA nHandle2, aTexto;

     ||Fecha
     aFecha = #agifa134#1;
     |QBF aFecha;
     aDia = aFecha % 102;
     aMes = aFecha % 402;
     aAnyo = aFecha % -104;
     aAux = aDia + "/" + aMes + "/" + aAnyo;
     aTexto = aAux + ";";
     |XGRABA nHandle2, aTexto;

     ||RUT
     ||Rut de la empresa receptora del documento de venta.
     ||Debe ser ingresado sin puntos, con guion y digito verificador.
     aRut = #agifa024#15;
     |QBF aRut;
     aRut = aRut - "." - ".";
     aTexto = aRut + ";";
     |XGRABA nHandle2, aTexto;

     ||Razon Social
     aAux = #agifa134#3 % 150;
     |QBF aAux;
     aTexto = aAux;
     |HAZ QuitaRarosNubex;
     aAux = aSinRaros;
     aTexto = aAux + ";";
     |XGRABA nHandle2, aTexto;

     ||Giro
     aAux = #agifa024#53 % 140;
     |QBF aAux;
     aTexto = aAux;
     |HAZ QuitaRarosNubex;
     aAux = aSinRaros;
     aTexto = aAux + ";";
     |XGRABA nHandle2, aTexto;

     ||Comuna
     aAux = #agifa024#9;
     |QBF aAux;
     aTexto = aAux;
     |HAZ QuitaRarosNubex;
     aAux = aSinRaros;
     aTexto = aAux + ";";
     |XGRABA nHandle2, aTexto;

     ||Direccion
     aAux = #agifa024#8;
     |QBF aAux;
     aTexto = aAux;
     |HAZ QuitaRarosNubex;
     aAux = aSinRaros;
     aTexto = aAux + ";";
     |XGRABA nHandle2, aTexto;

     ||Afecto (si tiene cuota de iva)
     |SI #agifa134#29 ! 0;
          aAux = "si";
     |SINO;
          aAux = "no";
     |FINSI;
     aTexto = aAux + ";";
     |XGRABA nHandle2, aTexto;

     ||Producto
     aAux = #agifa071#2;
     |QBF aAux;
     aTexto = aAux;
     |HAZ QuitaRarosNubex;
     aAux = aSinRaros;
     aTexto = aAux + ";";
     |XGRABA nHandle2, aTexto;

     ||Descripcion;
     aAux = #agifa071#4;
     |QBF aAux;
     aTexto = aAux;
     |HAZ QuitaRarosNubex;
     aAux = aSinRaros;
     aTexto = aAux + ";";
     |XGRABA nHandle2, aTexto;

     ||Cantidad
     aTexto = #agifa071#5 + ";";
     |XGRABA nHandle2, aTexto;

     ||Precio
     nPrecio = #agifa071#6 ! 0;
     aTexto = nPrecio;
     aTexto = aTexto - "." - "." - ".";
     aTexto = aTexto - "," - "," - ",";
     aTexto = aTexto + ";";
     |XGRABA nHandle2, aTexto;

     ||Por Dto. 1
     aTexto = #agifa071#8 + ";";
     |XGRABA nHandle2, aTexto;

     ||Email.
     aAux = #agifa024#197;
     |QBF aAux;
     aTexto = aAux + ";";
     |XGRABA nHandle2, aTexto;

     ||A partir de aqui van TODOS vacios.

     aTexto = (";" * 26);
     |XGRABA nHandle2, aTexto;

     aTexto = aReturn;
     |XGRABA nHandle2, aTexto;
|FPRC;

|PROCESO agifa071;
     |SI nSwPrimeraLinea = 1;
          nSwPrimeraLinea = 0;

          |DBASE aBase;
          |QBF aBase;

          aPathEnvios = aBase + "envios/";
          |MKDIR aPathEnvios;

          fFecha = ~;
          aFecha = fFecha;
          aDia = aFecha % 102;
          aMes = aFecha % 402;
          aAnyo = aFecha % -104;

          |HORA aHora;
          |QBF aHora;
          aHoras = aHora % 102;
          aMinutos = aHora % 402;

          aNombreFic = "facturas_" + aAnyo + aMes + aDia + "_" + aHoras + aMinutos + ".csv";
          aFicDestino = aPathEnvios + aNombreFic;

          aOrigen = aFicDestino;

          |XABRE "WB", aFicDestino, nHandle2;
          |SI FSalida ] 0;
               |HAZ MeteTitulos;
          |FINSI;
     |FINSI;

     |HAZ MeteLinea;
|FPRC;

|DEFBUCLE agifa071;
 #agifa071#1;
 ;
 #agifa059#15, #agifa059#2,   1;
 #agifa059#15, #agifa059#2, 999;
 ;
 NULL, agifa071;
|FIN;

|PROCESO agifa059;
     |BUCLE agifa071;
|FPRC;

|DEFBUCLE agifa059;
 #agifa059#1;
 ;
 #agifa134#49, #agifa134#0,   1;
 #agifa134#49, #agifa134#0, 999;
 ;
 NULL, agifa059;
|FIN;

|PROCESO agifa134;
     |SI #pcegz077#11 = "N";
          |SI #agifa134#117 = 1;
               |ACABA;
          |FINSI;
     |FINSI;

     |DDEFECTO #agifa024;
     #agifa024#0 = #agifa134#2;
     |LEE 000#agifa024.=;
     |SI FSalida ! 0;
          |DDEFECTO #agifa024;
     |FINSI;

     |BUCLE agifa059;

     #agifa134#117 = 1;
     |GRABA 020#agifa134.a;
|FPRC;

|DEFBUCLE agifa134;
 #agifa134#3;
 ;
 #pcegz077#9,  #pcegz077#7, #pcegz077#3, #pcegz077#5;
 #pcegz077#10, #pcegz077#8, #pcegz077#4, #pcegz077#6;
 be;
 NULL, agifa134;
|FIN;


|PROCESO Principal;
     aReturn = &13 + &10;
     nSwPrimeraLinea = 1;
     |ABRE #agifa024;
     |BUCLE agifa134;
     |CIERRA #agifa024;

     |SI nSwPrimeraLinea = 1;
          aMensaje = "0000No hay nada para enviar. Selecci¢n vacia";
          |MENAV aMensaje;
     |SINO;
          |XCIERRA nHandle2;

          aIPRemoto = "";
          |IP_REMOTO aIPRemoto;  |QBT aIPRemoto;

          aDestino = #pcegz077#13;
          |QBF aDestino;
          |SI aDestino ! "";
               |RMKDIR aDestino;
          |FINSI;

          aUltimo = aDestino % -101;
          |SI aUltimo ! "\";
               aDestino = aDestino + "\" + aNombreFic;
          |SINO;
               aDestino = aDestino + aNombreFic;
          |FINSI;

          |SI aIPRemoto ! "";
              |ENVIA_FICHERO aOrigen, aDestino;
          |SINO;
              |COPIA_FICHERO aOrigen, aDestino;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Tipo0C13; |TIPO 0;
     |SI FSalida = 13; ||F5

          |HAZ Traete_dsselect;
          |SI enError = 1;
              |MENAV "0000No puedo instala el dsselect.exe, consulte con su administrador";
          |FINSI;

          |ESPECIFICA "RBASS", aLocal;
          aDirLocal = aLocal;

          |HAZ DimeUnidad;
          aAux      = eaUnidad + ":\DOCUMENTOS\DSCOMER9\";
          aPrograma = aAux + "dsselect.exe";

          ||Donde ejecuto el dsselect.exe
          aPathDSSelect = aAux;
          aBorra = aPathDSSelect + "dsselect.txt";
          |RFBORRA aBorra;

          aParaIni = aAux + "dsselect.ini";
          |RFBORRA aParaIni;

          aCarpetaCaptura = #pcegz077#13;
          |QBF aCarpetaCaptura;
          |SI aCarpetaCaptura = "";
               aCarpetaCaptura = eaUnidadBasico;
          |FINSI;

          aTextoIni = &34 + aCarpetaCaptura + &34 + " " + &34 + aBorra + &34 + " C";
          |XABRE "CW", aParaIni, nHandle;
          |SI FSalida ] 0;
               |XGRABA nHandle, aTextoIni;
          |FINSI;
          |XCIERRA nHandle;

          aEjecuta = aPrograma;
          |RSYSTEM aEjecuta;

          aAbre = aPathDSSelect + "dsselect.txt";
          |XABRE "CU", aAbre, nHandle;
          |SI FSalida < 0;
             aPath = "";
             nError = 1;
             |ACABA;
          |FINSI;

          |XLEE nHandle, 250, aPath;
          |SI FSalida < 0
              aPath = "";
              nError = 1;
              |ACABA;
          |FINSI;

          |QBF aPath;
          |SI aPath ! "";
               #pcegz077#13 = aPath + "\";
               |PINTA #pcegz077#13;
          |FINSI;

          |XCIERRA nHandle;
     |FINSI;
|FPRC;

|PROCESO QuitaRarosNubex;
     aAlfa1 = aTexto;
     |QBF aAlfa1;
     aCadena = "";
     aLen    = aAlfa1 % 0;
     nLen    = aLen;
     |PARA nCampo = 1;  |SI nCampo [ nLen;  |HACIENDO nCampo = nCampo + 1;
           nPosi = (100 * nCampo) + 1;
           aCaracter = aAlfa1 % nPosi;
           |SI aCaracter = "¥";  aCaracter = &209; |FINSI;
           |SI aCaracter = "¤";  aCaracter = &241; |FINSI;
           |SI aCaracter = " ";  aCaracter = &225; |FINSI;
           |SI aCaracter = "‚";  aCaracter = &233; |FINSI;
           |SI aCaracter = "¡";  aCaracter = &237; |FINSI;
           |SI aCaracter = "¢";  aCaracter = &243; |FINSI;
           |SI aCaracter = "£";  aCaracter = &250; |FINSI;
           |SI aCaracter = "§";  aCaracter = &176; |FINSI;
           |SI aCaracter = "¦";  aCaracter = &170; |FINSI;
           |SI aCaracter = "°";  aCaracter = &176; |FINSI;  ||Euro
/*
Esto es para importar !!!
           nCaracter = &aCaracter;
           |SI nCaracter = 192;  aCaracter = "A";  |FINSI;
           |SI nCaracter = 193;  aCaracter = "A";  |FINSI;
           |SI nCaracter = 200;  aCaracter = "E";  |FINSI;
           |SI nCaracter = 201;  aCaracter = "E";  |FINSI;
           |SI nCaracter = 204;  aCaracter = "I";  |FINSI;
           |SI nCaracter = 205;  aCaracter = "I";  |FINSI;
           |SI nCaracter = 209;  aCaracter = "¥";  |FINSI;
           |SI nCaracter = 210;  aCaracter = "O";  |FINSI;
           |SI nCaracter = 211;  aCaracter = "O";  |FINSI;
           |SI nCaracter = 217;  aCaracter = "U";  |FINSI;
           |SI nCaracter = 218;  aCaracter = "U";  |FINSI;
           |SI nCaracter = 186;  aCaracter = "§";  |FINSI;
*/
           aCadena = aCadena + aCaracter;
     |SIGUE;
     aAlfa1 = aCadena;
     aSinRaros = aAlfa1;
|FPRC;

|PROGRAMA;
     |ABRE #pcegm053;
     |LEE 000#pcegm053.p;
     |SI FSalida ! 0;
          aMensaje = "0000Fichero de Equivalencias Series Num. Factura Nubox, no definido";
          |MENAV aMensaje;
          |ACABA;
     |FINSI;

     |CIERRA #pcegm053;

     |CLS;
     |ABRE #0;
     |DDEFECTO #0;
     |LEE 101#0p;
     nExiste = FSalida;
     |PINPA #0#0;
     |PINDA #0#0;
     |ENDATOS #1#0;
     |SI FSalida = 0;
          aRespuesta = #pcegz077#12;
          #pcegz077#12 = "NO";
          |SI nExiste = 0;
               |GRABA 020#0a;
          |SINO;
               |GRABA 020#0n;
          |FINSI;
          |SI aRespuesta = "SI";
               |HAZ Principal;
          |FINSI;
     |FINSI;
     |LIBERA #0;
     |CIERRA #0;
|FPRO;
