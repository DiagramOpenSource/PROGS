|FICHEROS;
     gomaz001 #0;

     camasil@ #1;  || Apuntes Lins
     cacuent@ #2;  || Cuentas contables
     cactaan@ #3;  || Cuentas Analiticas
     cagrual@ #4;  || Lineas Grupos analiticas
     cagrapt@ #5;  || Asientos de Grupos para Contagen


     canempr@ #30;  || Empresas
     camoned@ #31;  || Moneda empresas

     goman001 #10;
     goman002 #11;
|FIN;

|VARIABLES;

   &cCodMonBase   = "";
   &cDesMonBase   = "";
   &cCodMonTrab   = "";
   &eaCtaAna      = "";
   &enMEntAna     = 0;
   &enSwDatos     = 0;
   aMonedaC       = "";
   aMonedaA       = "";
   impMoneda      = 0;
   mulMoneda      = 0;

   nEstado  = 0;
   aDef30   = "";
   aDef31   = "";
   aDef1    = "";
   aDef2    = "";
   aDef3    = "";
   aDef4    = "";
   aDef5    = "";

   nSwError = 0;
   aMensa   = "";
   aAlfa1   = "";
   aAlfa2   = "";
   aAlfa3   = "";
   nNume1   = 0;
   nNume2   = 0;
   aDirInicio = "";
   aDirBase   = "";
   aPathDef   = "";
   aPathFic0  = "";
   aPathFic00 = "";
   aDirFich   = "";

   aQueQuiero = "";
   aNumCampos = "";
   nNumCampos = 0;
   nVer       = 0;
   nNumCAsto  = 0;

   nInkey = 0;
   nDEmp  = 0;
   nHEmp  = 0;

   nCta      = 0;
   aCtaAna   = "";
   nSwGrupo  = 0;
   nSwGApte  = 0;
   nDebe     = 0;
   nHaber    = 0;
   eaCuenta  = "";
   enNivel   = 0;
   eaNomCta  = "";
   eaNomCtaA = "";
   aGrupoC   = "";
   nGrupoD   = 0;
   nGrupoH   = 0;
   nAcDebe   = 0;
   nAcHaber  = 0;
   aCtaUltima = "";

   &EuroDec  = 0;
|FIN;

|| /////////////////// Calculos de Pantalla

|CALCULO Defecto; |TIPO 7;
    |DBASS aDirInicio; |QBF aDirInicio;
    |SI nEstado = 1;
        aDirBase = aDirInicio + "agicont/";
        #0#1 = aDirBase;
        |PINTA #0#1;
    |FINSI;

    |HAZ DesCargaDef;
|FCAL;

|CALCULO PonBarra; |TIPO 0;
     aAlfa1 = #0Campo;
     |QBF aAlfa1;
     aAlfa2 = aAlfa1 % -101;
     |SI aAlfa2 ! "/";|Y aAlfa2 ! "\";
          #0Campo = aAlfa1 + "/";
          |PINTA #0Campo;
     |FINSI;
     |HAZ CargaEmpresa;
     |SI nSwError = 1;
         |MENAV aMensa;
         |ERROR;
     |FINSI;
|FCAL;

|RUTINA infA2;
   #30#2 = nDEmp;
   #30#3 = 0;
|FRUT;

|RUTINA supA2;
   #30#2 = nHEmp;
   #30#3 = 9;
|FRUT;


|CALCULO LeeEmpresa; |TIPO 0;
  nInkey = FSalida;

  aDirBase = #0#1; |QBF aDirBase;
  aPathFic0  = aDirBase + "fich/";
  |PATH_DAT #30 aPathFic0;

  |SI nInkey = 10;
     nDEmp = 0; nHEmp = 99999;
     |ABRE #30;
     |CONSULTA_F_CLAVE #1#30,infA2,supA2;
     #0#2 = #30#2; |PINTA #0#2;
     #0#3 = #30#3; |PINTA #0#3;
     |CIERRA #30;
     |PTEC 802;
     |ACABA;
  |FINSI;

  |ABRE #30;
  #30#2 = #0#2;
  #30#3 = 0;
  |LEE 011#30];
  |SI FSalida ! 0;  |O #30#2 ! #0#2;
      |MENAV "     NO Existe la empresa Contable";
      |ERROR;
  |SINO;
      #0#4 = #30#1; |PINTA #0#4;
  |FINSI;
  |CIERRA #30;
|FCAL;

|CALCULO LeeEmpresaP; |TIPO 0;
  nInkey = FSalida;
  |SI nInkey = 2; |ACABA; |FINSI;

  aDirBase = #0#1; |QBF aDirBase;
  aPathFic0  = aDirBase + "fich/";
  aPathFic00 = aDirBase + "fich/000000/";
  |PATH_DAT #30 aPathFic0;
  |PATH_DAT #31 aPathFic00;

  |SI nInkey = 10;
     nDEmp = #0#2; nHEmp = #0#2;
     |ABRE #30;
     |CONSULTA_F_CLAVE #1#30,infA2,supA2;
     #0#3 = #30#3; |PINTA #0#3;
     |CIERRA #30;
  |FINSI;

  aMonedaC = "E";
  |ABRE #30;
  #30#2 = #0#2;
  #30#3 = #0#3;
  |LEE 011#30=;
  |SI FSalida ! 0;
      |MENAV "     NO Existe Periodo en la Empresa Contable";
      |ERROR;
  |SINO;
      #0#4   = #30#1;  |PINTA #0#4;
      |SI #30#26 ] 80;
          #0#5 = 1900 + #30#26;
      |SINO;
          #0#5 = 2000 + #30#26;
      |FINSI;
      |PINTA #0#5;
      |ABRE #31;
      #31#0 = #0#2;
      #31#1 = #0#3;
      |LEE 000#31=;
      |SI FSalida = 0; aMonedaC = #31#2; |FINSI;
      |CIERRA #31;
  |FINSI;
  |CIERRA #30;
|FCAL;

|CALCULO ana_h; |TIPO 0;
 |SI #0#7 < #0#6;
     |ERROR;
 |FINSI;
|FCAL;

||//////////////////////////////////////////////////////////////////////////
|PROCESO LeeLaCta;  || Lee Cuenta Contable
 |ABRE #2;
 |DDEFECTO #2;
 #2#0 = eaCuenta;
 #2#1 = enNivel;
 |LEE 001#2=;
 |CIERRA #2;
 eaNomCta = #2#2;
|FPRC;

|PROCESO LeeLaCtaA;  || Lee Cuenta Analitica
 |ABRE #3;
 |DDEFECTO #3;
 #3#0 = aCtaAna;
 |LEE 001#3=;
 |CIERRA #3;
 eaNomCtaA = #3#1;
|FPRC;

|PROCESO GrabaSaldo;

 |SI aCtaAna < #0#6; |O aCtaAna > #0#7;
     |ACABA;
 |FINSI;

 #10#0 = aCtaAna;
 |LEE 101#10=;
 |SI FSalida ! 0;
     |DDEFECTO #10;
     #10#0 = aCtaAna;
     |HAZ LeeLaCtaA;
     #10#1 = eaNomCtaA;
     |GRABA 020#10b;
 |FINSI;

 #11#0 = #10#0;
 #11#1 = eaCuenta;
 |LEE 101#11=;
 |SI FSalida ! 0;
     |DDEFECTO #11;
     #11#0 = #10#0;
     #11#1 = eaCuenta;
     |HAZ LeeLaCta;
     #11#2 = eaNomCta;
     |GRABA 020#11b;
 |FINSI;

 #11#3 = #11#3 + nDebe;
 #11#4 = #11#4 + nHaber;
 |GRABA 020#11a;
 |LIBERA #11;

 #10#7 = #10#7 + nDebe;
 #10#8 = #10#8 + nHaber;
 #10#9 = #10#8 - #10#7;
 |GRABA 020#10a;
 |LIBERA #10;

 enSwDatos = 1;
|FPRC;

||................./// Especial Contagen
|PROCESO GruposA;
 aCtaAna = #5#6;
 |SI #1#8 = "D";
     nDebe  = #5#9;
     nHaber = 0;
 |SINO;
     nDebe = 0;
     nHaber = #5#9;
 |FINSI;

 |SI aMonedaC ! aMonedaA;
     impMoneda = nDebe;  |HAZ ElCambioMoneda; nDebe  = impMoneda;
     impMoneda = nHaber; |HAZ ElCambioMoneda; nHaber = impMoneda;
 |FINSI;

 |HAZ GrabaSaldo;
 nSwGApte = 1;
|FPRC;

|DEFBUCLE LeeGrApte;
 #5#1006;
 ;
 #1#0, #1#1, #1#2, #1#3, aGrupoC, 000;
 #1#0, #1#1, #1#2, #1#3, aGrupoC, 999;
 e;
 NULL, GruposA;
|FIN;

|| ... Los Grupos
|PROCESO ggrupos;
 aCtaAna = #4#2;
 nDebe = 0;
 nHaber = 0;
 |SI nGrupoD ! 0; nDebe  = (nGrupoD * #4#4 / 100) ! EuroDec; |FINSI;
 |SI nGrupoH ! 0; nHaber = (nGrupoH * #4#4 / 100) ! EuroDec; |FINSI;

 |SI aMonedaC ! aMonedaA;
     impMoneda = nDebe;  |HAZ ElCambioMoneda; nDebe  = impMoneda;
     impMoneda = nHaber; |HAZ ElCambioMoneda; nHaber = impMoneda;
 |FINSI;

 |HAZ GrabaSaldo;

 nSwGrupo   = 1;
 aCtaUltima = #4#2;
 nAcDebe    = nAcDebe  + nDebe;
 nAcHaber   = nAcHaber + nHaber;
|FPRC;

|PROCESO fingrupo;

 |SI nSwGrupo = 0; |ACABA; |FINSI;
 |SI nAcDebe = nGrupoD; |Y nAcHaber = nGrupoH; |ACABA; |FINSI;

 aCtaAna = aCtaUltima;
 nDebe   = nGrupoD - nAcDebe;
 nHaber  = nGrupoH - nAcHaber;
 |HAZ GrabaSaldo;
|FPRC;

|DEFBUCLE LeeGrupo;
 #4#1002;
 ;
 aGrupoC, 000;
 aGrupoC, 999;
 e;
 NULL, ggrupos, NULL, NULL, fingrupo;
|FIN;

||-----------------------------------------------------
|PROCESO LosAsientos;

 |SI #1#9 = 0; |ACABA; |FINSI;

 aCtaAna = #1nCta;
 |SI aCtaAna [ "       "; |ACABA; |FINSI;

 |SI #1#8 = "D";
     nDebe = #1#9;
     nHaber = 0;
 |SINO;
     nDebe = 0;
     nHaber = #1#9;
 |FINSI;
 eaCuenta = #1#4;
 enNivel  = #1#5;

 |SI aMonedaC ! aMonedaA;
     impMoneda = nDebe;  |HAZ ElCambioMoneda; nDebe  = impMoneda;
     impMoneda = nHaber; |HAZ ElCambioMoneda; nHaber = impMoneda;
 |FINSI;

 aAlfa3 = aCtaAna % 103;
 |SI aAlfa3 ! "GRU";
     |HAZ GrabaSaldo;
 |SINO;
     nSwGApte = 0;
     |SI nVer = 1;
         |BUCLE LeeGrApte;
     |FINSI;
     |SI nSwGApte = 0;
         aCtaAna = #1nCta;
         aGrupoC = aCtaAna; nSwGrupo = 0;
         nGrupoD = nDebe;   nAcDebe  = 0;
         nGrupoH = nHaber;  nAcHaber = 0;
         aCtaUltima = "";
         |BUCLE LeeGrupo;
     |FINSI;
 |FINSI;

|FPRC;

|DEFBUCLE LosAsientos;
 #1#1004;
 4;
 00,#0#10, 000001, 0001, "6           ";
 99,#0#11, 999999, 9999, "799999999999";
 e;
 NULL, LosAsientos;
|FIN;

|| .....................
|PROCESO BPrimero;
 |BUCLELIN 1NULL#10;
 #10#7 = 0;
 #10#8 = 0;
 #10#9 = 0;
 |GRABA 020#10a;
 |LIBERA #10;
|FPRC;

|DEFBUCLE BPrimero;
 #10#1;
 ;
 #0#6;
 #0#7;
 be;
 NULL, BPrimero;
|FIN;
|| .....................


|PROCESO ElPase;
  |HAZ CargaResto;
  |SI nSwError = 1;
      aMensa = aMensa + ". Proceso Abortado";
      |MENAV aMensa;
      |ACABA;
  |FINSI;

  |PATH_DAT #1 aDirFich;
  |PATH_DAT #2 aDirFich;
  |PATH_DAT #3 aDirFich;
  |PATH_DAT #4 aDirFich;
  |SI nVer = 1;  |PATH_DAT #5 aDirFich; |FINSI;

  |SI #0#8 = "I";
      |BUCLE BPrimero;
  |FINSI;

  |ABRE #10;
  |ABRE #11;

  |BUCLE LosAsientos;

  |CIERRA #11;
  |CIERRA #10;

|FPRC;

|| =====================================================================
|| =====================================================================
|PROCESO DesCargaDef;
     |SI aDef30 ! "";  |DESCARGA_DEF #canempr@; aDef30 = ""; |FINSI;
     |SI aDef31 ! "";  |DESCARGA_DEF #camoned@; aDef31 = ""; |FINSI;
     |SI aDef1 ! "";   |DESCARGA_DEF #camasil@; aDef1 = ""; |FINSI;
     |SI aDef2 ! "";   |DESCARGA_DEF #cacuent@; aDef2 = ""; |FINSI;
     |SI aDef3 ! "";   |DESCARGA_DEF #cactaan@; aDef3 = ""; |FINSI;
     |SI aDef4 ! "";   |DESCARGA_DEF #cagrual@; aDef4 = ""; |FINSI;
     |SI aDef5 ! "";   |DESCARGA_DEF #cagrapt@; aDef5 = ""; |FINSI;
|FPRC;

|PROCESO CargaEmpresa;

     nSwError = 0;
     aMensa   = "";

     aDirBase = #0#1; |QBF aDirBase;
     aPathDef = aDirBase + "def/";

     aDef30 = aPathDef + "canempre";
     |CARGA_DEF aDef30, canempr@;
     |SI FSalida ! 0;
         aMensa   = "0000Error en:" + aDef30;
         aDef30    = "";
         nSwError = 1;
         |ACABA;
     |FINSI;

|| ... Averiguar la Version de Contabilidad
||
||   Hasta el momento, todos tienen "Contagen" !!!
||
     aQueQuiero = "|NC";
     aNumCampos = #canempr@#aQueQuiero#;
     nNumCampos = aNumCampos;
     nVer = 0;
     |SI nNumCampos = 36; nVer = 1; nCta = 29; |FINSI;     ||Version Contagen
     |SI nNumCampos = 34; nVer = 2; nCta = 24; |FINSI;     ||Version 6.60 Analitica
     |SI nVer = 0;
         aMensa = "0000Error, Version incorrecta de Contabilidad";
         nSwError = 1;
         |HAZ DesCargaDef;
         |ACABA;
     |FINSI;

     aDef31 = aPathDef + "camoneda";
     |CARGA_DEF aDef31, camoned@;
     |SI FSalida ! 0;
         aMensa   = "0000Error en:" + aDef31;
         aDef31    = "";
         nSwError = 1;
     |FINSI;
|FPRC;

|PROCESO CargaResto;
|| ... Carga Defs de la contabilidad y Directorio
  aDirBase  = #0#1; |QBF aDirBase;
  aPathDef  = aDirBase + "def/";
  aPathFic0 = aDirBase + "fich/";

  aAlfa1 = #0#2; |QBF aAlfa1; aAlfa1 = ("00000" + aAlfa1) % -105;
  aAlfa2 = #0#3; |QBF aAlfa2; aAlfa2 = ("0"     + aAlfa2) % -101;

  aDirFich = aPathFic0 + aAlfa1 + aAlfa2 + "/";

  nSwError = 0;
  aDef1 = aPathDef + "camaasil";
  |CARGA_DEF aDef1, camasil@;
  |SI FSalida ! 0;
      aMensa = "0000Error en:" + aDef1;
      aDef1  = "";
      nSwError = 1;
  |SINO;
      aQueQuiero = "|NC";
      aNumCampos = #camasil@#aQueQuiero#;
      nNumCAsto  = aNumCampos;

      aDef2 = aPathDef + "cacuenta";
      |CARGA_DEF aDef2, cacuent@;
      |SI FSalida ! 0;
          aMensa = "0000Error en:" + aDef2;
          aDef2  = "";
          nSwError = 1;
      |SINO;
          |SI nVer = 1;
              aDef3 = aPathDef + "anm00001";   ||Contagen
          |SINO;
              aDef3 = aPathDef + "cactaana";   ||Agicont 6.60
          |FINSI;
          |CARGA_DEF aDef3, cactaan@;
          |SI FSalida ! 0;
              aMensa = "0000Error en:" + aDef3;
              aDef3  = "";
              nSwError = 1;
          |SINO;
              |SI nVer = 1;
                  aDef4 = aPathDef + "anm00003";   ||Contagen
              |SINO;
                  aDef4 = aPathDef + "cagruanl";   ||Agicont 6.60
              |FINSI;
              |CARGA_DEF aDef4, cagrual@;
              |SI FSalida ! 0;
                  aMensa = "0000Error en:" + aDef4;
                  aDef4  = "";
                  nSwError = 1;
              |SINO;
                  |SI nVer = 1;
                      aDef5 = aPathDef + "anm00013";   ||Contagen
                      |CARGA_DEF aDef5, cagrapt@;
                      |SI FSalida ! 0;
                          aMensa = "0000Error en:" + aDef5;
                          aDef5  = "";
                          nSwError = 1;
                      |FINSI;
                  |FINSI;
              |FINSI;
          |FINSI;
      |FINSI;
  |FINSI;
|FPRC;

|| **************************************************************************
|| **************************************************************************
|| **************************************************************************

|PROGRAMA;

   nEstado = 0;
   |ABRE #0;
   |LEE 101#0p;
   |SI FSalida ! 0;
       |DDEFECTO #0;
       |GRABA 020#0b;
       nEstado = 1;
   |FINSI;

   |CLS;
   |CABEZA "Cargar Saldos de Cuentas";

   |PINPA #0#0;

   |SI eaCtaAna ! "";
       #0#6 = eaCtaAna; |C_M #0#6,1,"N";
       #0#7 = eaCtaAna; |C_M #0#7,1,"N";
       |SI enMEntAna = 1;
           #0#8 = "I"; |C_M #0#8,1,"N";
       |FINSI;
       #0#9 = "S";
   |SINO;
       |HAZ MonBas;
       #0#9 = "N";
   |FINSI;
   |SI cCodMonBase = "EUR";
       aMonedaA = "E";
       EuroDec = 2;
   |SINO;
       aMonedaA = "P";
       EuroDec = 0;
   |FINSI;

   |PINDA #0#0;
   |ENDATOS #1#0;
   |SI FSalida = 0;
       |SI #0#9 = "S";
           |HAZ ElPase;
       |FINSI;
       #0#6 = "";
       #0#7 = "zzzzzzz";
       #0#9 = "N";
       |GRABA 020#0a;
   |FINSI;
   |LIBERA #0;
   |CIERRA #0;
   |HAZ DesCargaDef;
|FPRO;

|| ********************************************************************

|PROCESO ElCambioMoneda;

 |SI impMoneda = 0; |ACABA; |FINSI;

 |SI aMonedaC = "P";                  || pesetas
     mulMoneda = 1 / 166.386;
 |FINSI;
 |SI aMonedaC = "E";                  || euros
     mulMoneda = 166.386;
 |FINSI;
 impMoneda = (impMoneda * mulMoneda) ! EuroDec;
|FPRC;
