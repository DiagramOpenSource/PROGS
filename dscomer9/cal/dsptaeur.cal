||dsptaeur.

|FICHEROS;
     dsptaeur #0;
     fichero@ #1;   ||Para cargar los def's
     defline@ #100; ||Para los def de lineas
     agifa321 #2;   ||Divisas - Parametros
     agifa324 #3;   ||Divisas - Divisas

     dsptaeu2 #10;  ||Auxiliar Pase a Euro
     dsptaeu3 #11;  ||Auxiliar Def's Pase a Euro

     tempo@   #200; ||Temporal resultado de la select
     dsptaw01 #51;  ||Temporal con los campos de las cabeceras.
     dsptaw02 #52;  ||Temporal con los campos de las lineas.
|FIN;

|VARIABLES;
     aAlfa = "";
     &Tempo     = "";
     aTempo51   = "";
     aTempo52   = "";
     i=0;
     j=0;
     k=0;
     nLinea=0;
     nModo=0;
     nSeguir=0;
     nEURO=166.386;
     aBUSCA="";
     &EURODB=0;

     aCadena1="";
     aCadena2="";
     aCadena3="";
     aMensaje="";
     aDirectorio="";
     aDef="";
     aPath="";

     nConvertidos=0;
     nTotalFicheros=0;
     nNumCampos=0;

|| variables para llenar de ceros
     aCadena="";
     r_alfa = "";
     r_long = "";
     r_num  = 0;
     r_masc = "000";
     nSwAcaba = 0;
     aBase = "";
     nSwEncontrada = 0;
     nSwDivEuro = 0;
     aCodDivisa = "";
     aDivVtas = "";
     aDivCpas = "";
     aFichero = "";
     nSwControlado = 0;
     aTipoDef = "";
     nDivisa = 0;
     aDivisa = "";
     nMoneda = 0;
     aMoneda = "";
     aDivisaBase = "";
     aDirDef = "";
     aDefLineas = "";
     aFicLineas = "";
     nTipoCab = 0;
     nTipoLin = 0;
     aPathOri = "";
     nCont = 0;
     nSwDescargaLin = 0;
     aCadLin1="";
     aCadLin2="";
     nNumCamLin=0;
     aComodin = "";
     aQueQuiero = "";
     aAlfa1 = "";
     nTCompo = 0;
     nCompo = 0;
     nPosi = 0;
     nNume = 0;
     aSQL = "";
     aTempo = "";
     aPathEuro = "";
     aEmpresa = "";
     aCodEmp = "";
     aClave = "";
     aPathBase = "";
     aPathDatos = "";
     nCampo = 0;
     nCampoLin = 0;
     nSwEncontrado = 0;
     nSwEncontradoLin = 0;
     aDiviPara = "";
     nCampoDivisa = 0;
     nCampoMoneda = 0;
     nCampoCambio = 0;
|FIN;


|DEFBUCLE BConvertirDatos;
     #0#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL,ConvertirDatos;
|FIN;

|PROCESO F11;|TIPO 66;
     |CONFI "00000Quieres Continuar con el Proceso de Conversion?";
     |SI FSalida=0;

          |HAZ CreaTempo; aTempo51 = Tempo;
          |NOME_DAT #51 aTempo51; |DELINDEX #51;

          |HAZ CreaTempo; aTempo52 = Tempo;
          |NOME_DAT #52 aTempo52; |DELINDEX #52;

          nConvertidos=0;
          nTotalFicheros=0;
          |BUCLE BConvertirDatos;

          ||TODO CCC. Cuando termine esto descomentar la linea de abajo
          |SI nTotalFicheros ! 0;
               |HAZ ActParametros;
          |FINSI;
          aCadena1=nTotalFicheros;
          aCadena2=nConvertidos;
          aMensaje="- Ficheros Examinados: "+aCadena1+"       - Ficheros Convertidos: "+aCadena2;
          |BLIN 23;
          |PINTA 2305,aMensaje;
          |PAUSA;
          |DELINDEX #51;
          |DELINDEX #52;
          Tempo = aTempo51; |HAZ BoraTempo;
          Tempo = aTempo52; |HAZ BoraTempo;
     |FINSI;
|FPRC;

|PROCESO llena_ceros;
     |QBF aCadena;
     r_alfa= aCadena;
     |QBF r_alfa;
     r_long = aCadena % A0;
     r_alfa = r_masc + r_alfa;
     r_num = -103;
     aCadena = r_alfa % r_num;
|FPRC;

|PROCESO CuantosCampos;
     aCadena1="|NC";
     aCadena2=#1#aCadena1#;
     nNumCampos=aCadena2;    || numero de campos en el def
|FPRC;

|PROCESO dsptaeu2;
     aBUSCA=#dsptaeu2#0; ||Palabra que buscamos
     |QBF aBUSCA;
     |HAZ BuscaCadena;
     |SI nSwEncontrada = 1;
          ||Si encuentra algun campo se sale de este defbucle
          |ERROR10;
     |FINSI;
|FPRC;

|DEFBUCLE Buscar;
 #dsptaeu2#1;
 ;
 INICIO;
 FINAL;
 ;
 NULL, dsptaeu2;
|FIN;

|PROCESO BuscaCamposEuro;
     |PARA j=0;|SI j<nNumCampos; |HACIENDO j=j+1;
          aCadena=j;
          |HAZ llena_ceros; || en aCadena esta el resultado

          nSwEncontrada = 0;
          |BUCLE Buscar;
          |SI nSwEncontrada = 1;
               |SAL;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO min;
     #0#0=001;
|FPRC;

|PROCESO max;
     #0#0=999;
|FPRC;

|PROCESO CargaDef;
     aCadena1=#0#1;
     |QBF aCadena1;
     aCadena2=#0#2;
     |QBF aCadena2;

     aCadena3=aDirectorio-aCadena2;
     aCadena3=aCadena3-".dat";
     aDef=aCadena1+"def/"+aCadena3+".mas";

     |CARGA_DEF aDef, fichero@;
     |SI FSalida ! 0;
          nSeguir=-1;
     |FINSI;
|FPRC;

|PROCESO DescargaDef;
     |DESCARGA_DEF #fichero@;
|FPRC;

|PROCESO EstableceRutaDatos;
     aPath=#0#2;
     |QBF aPath; || ruta para path dat

     |PATH_DAT #1 aPath;
|FPRC;

|PROCESO Literales;
     |SI #dsptaw01#2 [ nTipoCab;
          aBUSCA=#dsptaw01#1; ||Palabra que buscamos
          |QBF aBUSCA;
          aCadena = #dsptaw01#0;
          |HAZ llena_ceros;

          |HAZ CambiaDatos;
     |FINSI;
|FPRC;

|PROCESO dsptaw01;
     aCadena=#dsptaw01#0;
     |HAZ llena_ceros; || en aCadena esta el resultado
     |HAZ Literales;
|FPRC;

|DEFBUCLE dsptaw01;
 #dsptaw01#1;
 ;
 INICIO;
 FINAL;
 ;
 NULL, dsptaw01;
|FIN;

|PROCESO ConviertePesetasEuros;
     |SI nSwControlado = 0;
          nTipoCab = 3;
     |SINO;
          aTipoDef = #dsptaeu3#6;
          |SI aTipoDef = "V"; |Y aDivVtas = "N";
               nTipoCab = 3;
                nTipoLin = 3;
          |FINSI;
          |SI aTipoDef = "V"; |Y aDivVtas = "S";
               |HAZ VerDivMoneda;
          |FINSI;
          |SI aTipoDef = "C"; |Y aDivCpas = "N";
               nTipoCab = 3;
               nTipoLin = 3;
          |FINSI;
          |SI aTipoDef = "C"; |Y aDivCpas = "S";
               |HAZ VerDivMoneda;
          |FINSI;
     |FINSI;
     |BUCLE dsptaw01;
|FPRC;

|PROCESO ConvertirDatos; || funcion principal
     |PINTA 2320,"Convirtiendo Datos a Euros, Espere un Momento......";

     aCadena1=#0#2;
     |QBF aCadena1;
     aPathOri = aCadena1;

     aDirectorio=aCadena1+"*.dat";
     |_PDIR aDirectorio;    || extrae el primero directorio
                            || con |_SDIR aDirectorio cogemos los siguientes


     |SI FSalida<0;
          aCadena1=#0#2;
          |QBF aCadena1;
          aPathOri = aCadena1;
          aMensaje="0000El Directorio de Datos "+aCadena1+" no Existe!";
          |MENAV aMensaje;
     |FINSI;

     |PARA i=0;|SI FSalida]0; |HACIENDO i=i+1;
          aFichero = aDirectorio;
          aFichero = aFichero - aPathOri;
          aFichero = aFichero - ".dat";

          aMensaje = "2313 Procesando fichero de Datos: " + aFichero + "               ";
          |MENSA aMensaje;

          nTotalFicheros=nTotalFicheros+1;
          nSeguir=0;
          |HAZ CargaDef;

          |SI nSeguir=0;
               |HAZ EstableceRutaDatos;
               |HAZ CuantosCampos;  || nNumCampos tiene el numero de campos
               nSeguir=-1;

               |QBF aFichero;
               #dsptaeu3#0 = aFichero;
               |LEE 000#dsptaeu3.=;
               |SI FSalida = 0;
                    nSwControlado = 1;
                    |SI #dsptaeu3#4 = "S";   ||Activo = "SI"
                         nSeguir = 0;
                    |SINO;
                         nSeguir = -1;
                    |FINSI;
               |SINO;
                    nSwControlado = 0;
                    nSeguir = -1;
                    |HAZ BuscaCamposEuro; || mira si hay campos con Euro o no
               |FINSI;

               |SI nSwControlado = 1;
                    nSwDescargaLin = 0;
                    aDefLineas = #dsptaeu3#3;
                    |QBF aDefLineas;
                    |SI aDefLineas ! "";
                         aDirDef = #0#1;
                         |QBF aDirDef;
                         aFicLineas = aDirDef + "def/" + aDefLineas + ".mas";
                         |CARGA_DEF aDefLineas, defline@;
                         |SI FSalida = 0;
                              nSwDescargaLin = 1;
                              nConvertidos=nConvertidos+1;
                              aCadLin1="|NC";
                              aCadLin2=#100#aCadLin1#;
                              nNumCamLin=aCadLin2;    || num campos def Lineas
                              |PATH_DAT #100 aPath;
                         |FINSI;
                    |FINSI;
               |FINSI;

               |SI nSeguir=0;

                    ||Creare 2 temporales con los numeros de campos
                    ||a tener en cuenta.

                    |DELINDEX #dsptaw01;
                    |ABRE #dsptaw01;
                    |HAZ RellenaTmpCab;

                    nConvertidos=nConvertidos+1;
                    || buscamos en todos los registros
                    |ABRE #1;
                    |LEE 000#1p;
                    |PARA;|SI FSalida=0; |HACIENDO;
                         |HAZ ConviertePesetasEuros;
                         |SI nSwDescargaLin = 1;
                              |DELINDEX #dsptaw02;
                              |ABRE #dsptaw02;
                              |HAZ Lineas;
                              |CIERRA #dsptaw02;
                         |FINSI;
                         |LEE 000#1s;
                    |SIGUE;
                    |CIERRA #1;
                    |CIERRA #dsptaw01;
               |FINSI;
               |HAZ DescargaDef;
               |SI nSwDescargaLin = 1;
                    nSwDescargaLin = 0;
                    |DESCARGA_DEF #defline@;
               |FINSI;
          |FINSI;
          |_SDIR aDirectorio;
     |SIGUE;
|FPRC;

|PROGRAMA;
     |HAZ ElPath;
     |ABRE #dsptaeu3;
     nEURO=166.386;
     EURODB=2;

     |CLS;
     |ABRE #0;
     |DDEFECTO #0;
     #0#0 = 001;
     |LEE 101#dsptaeur.=;
     |SI FSalida ! 0; |GRABA 020#dsptaeur.b; |FINSI;
     |DBASE aPathBase;
     |QBF aPathBase;
     #dsptaeur#1 = aPathBase;
     |PINTA #dsptaeur#1;

     |DFICE aPathDatos;
     |QBF aPathDatos;
     #dsptaeur#2 = aPathDatos;
     |PINTA #dsptaeur#2;
     |GRABA 020#dsptaeur.a;
     |LIBERA #dsptaeur;
     |PINPA #0#0;
     |PINDA #0#0;

     |HAZ MiraEuro;
     |SI nSwAcaba = 1;
          |ACABA;
     |FINSI;

     |HAZ F11;
     |CIERRA #0;
     |CIERRA #dsptaeu3;

     |DDEF aAlfa; aAlfa = aAlfa + "tempo.mas"; |FBORRA aAlfa;
|FPRO;

|PROCESO agifa324;
     aDiviPara = #agifa324#0;
     |QBF aDiviPara;
     |SI #agifa324#2 = 1; |Y #agifa324#3 = 1; |Y aDiviPara ! "";
          nSwDivEuro = 1;
          aCodDivisa = #agifa324#0;          ||Divisa Euro
          |ERROR10;
     |FINSI;
|FPRC;

|DEFBUCLE agifa324;
 #agifa324#1;
 ;
 INICIO;
 FINAL;
 ;
 NULL, agifa324;
|FIN;

|PROCESO MiraEuro;
     |ABRE #agifa321;
     |LEE 000#agifa321.p;
     |SI FSalida = 0;
          aDivVtas = #agifa321#1;
          aDivCpas = #agifa321#2;
          aBase = #agifa321#9;
          |ABRE #agifa324;
          #agifa324#0 = aBase;
          |LEE 000#agifa324.=;
          |SI FSalida = 0;
               |SI #agifa324#2 = nEURO; |Y #agifa324#3 = nEURO;
                    aDivisaBase = aBase;
                    ||Todo correcto.
               |SINO;
                    aMensaje = "0000Error: La moneda Base debe ser Pesetas, para poder realizar la conversion a Euros.";
                    |MENAV aMensaje;
                    nSwAcaba = 1;
                    |ACABA;
               |FINSI;
          |SINO;
               aMensaje = "0000Error: La moneda Base debe ser Pesetas, para poder realizar la conversion a Euros.";
               |MENAV aMensaje;
               nSwAcaba = 1;
               |ACABA;
          |FINSI;
          |CIERRA #agifa324;
     |SINO;
          aMensaje = "0000Error: Parametros Divisa -No- Definidos";
          |MENAV aMensaje;
          nSwAcaba = 1;
          |ACABA;
     |FINSI;
     |CIERRA #agifa321;

     |BUCLE agifa324;
     |SI nSwDivEuro = 0;
          aCodDivisa = "EUR";
     |FINSI;
|FPRC;

|PROCESO ActParametros;
     ||La idea es que despues de realizar el proceso
     ||Cambiemos en Parametros Generales y pongamos divisa "EURO".
     nSwDivEuro = 0;
     |BUCLE agifa324;
     |ABRE #agifa321;
     |SI nSwDivEuro = 1;
          |DDEFECTO #agifa321;
          |LEE 101#agifa321.p;
          |SI FSalida ! 0; |GRABA 020#agifa321.b; |FINSI;
          #agifa321#9 = aCodDivisa;
          |GRABA 020#agifa321.a;
          |LIBERA #agifa321;
     |SINO;
          |ABRE #agifa324;
          |DDEFECTO #agifa324;
          #agifa324#0 = "EUR";
          |LEE 101#agifa324.=;
          |SI FSalida ! 0;
               |GRABA 020#agifa324.b;
               #agifa324#1 = "Euro";
          |FINSI;
          #agifa324#2 = 1;    ||1 Euro = 1
          #agifa324#3 = 1;    ||1 Euro = 1
          #agifa324#7 = 2;    ||2 decimales
          #agifa324#9 = 2;    ||2 decimales
          |GRABA 020#agifa324.a;
          |LIBERA #agifa324;
          |CIERRA #agifa324;
          aCodDivisa = #agifa324#0;
          |DDEFECTO #agifa321;
          |LEE 101#agifa321.p;
          |SI FSalida ! 0; |GRABA 020#agifa321.b; |FINSI;
          #agifa321#9 = aCodDivisa;
          |GRABA 020#agifa321.a;
          |LIBERA #agifa321;
     |FINSI;
     |CIERRA #agifa321;
|FPRC;

|PROCESO BuscaCadena;
     aCadena1="|C"+aCadena+"TIPO";  || buscamos el tipo
     aCadena2=#1#aCadena1#;
     |SI aCadena2="N";
          aCadena1="|C"+aCadena+"MINIMO";  || buscamos en la cadena
          aCadena2=#1#aCadena1#;
          aCadena3=aCadena2-aBUSCA;
          |SI aCadena2!aCadena3;
               nSwEncontrada = 1;
               nSeguir=0;
               |ACABA;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO CambiaDatos;
     aCadena1="|C"+aCadena+"TIPO";  || buscamos el tipo
     aCadena2=#1#aCadena1#;
     |SI aCadena2="N";
          aCadena1="|C"+aCadena+"MINIMO";  || buscamos en la cadena
          aCadena2=#1#aCadena1#;
          aCadena3=aCadena2-aBUSCA;
          |SI aCadena2!aCadena3;   || convertir a euros
               |SI nSwControlado = 1;
                    nCampoDivisa = #dsptaeu3#1;
                    nCampoMoneda = #dsptaeu3#2;
                    nCampoCambio = nCampoMoneda - 1;
                    |SI #1nCampoMoneda = "B";
                         #1nCampoDivisa = aCodDivisa;
                    |FINSI;
                    ||TODO CCC
                    |SI #dsptaeu3#0 ! "agifa024"; |Y #dsptaeu3#0 ! "agifa112";
                         #1nCampoCambio = 1;
                    |FINSI;
               |FINSI;
               j=#dsptaw01#0;
               #1j=#1j / nEURO;
               |GRABA 000#1a;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO VerDivMoneda;
     nDivisa = #dsptaeu3#1;
     nMoneda = #dsptaeu3#2;
     aDivisa = #1nDivisa;
     aMoneda = #1nMoneda;
     |SI aDivisa = aDivisaBase;
          nTipoCab = 3;
          nTipoLin = 3;
     |SINO;
          |SI aMoneda = "D";
               ||Cambia tipo Base y Visualizacion
               nTipoCab = 1;
               nTipoLin = 2;
          |SINO;
               ||Cambia tipo Base
               nTipoCab = 2;
               nTipoLin = 1;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO CambiaDatosLin;
     aCadena1="|C"+aCadena+"TIPO";  || buscamos el tipo
     aCadena2=#100#aCadena1#;
     |SI aCadena2="N";
          aCadena1="|C"+aCadena+"MINIMO";  || buscamos el la cadena EURO
          aCadena2=#100#aCadena1#;
          aCadena3=aCadena2-aBUSCA;
          |SI aCadena2!aCadena3;   || convertir a euros
               j=#dsptaw02#0;
               #100j=#100j / nEURO;
               |GRABA 000#100a;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO LiteralesLin;
     |SI #dsptaw02#2 [ nTipoLin;
          aBUSCA=#dsptaw02#1; ||Palabra que buscamos
          |QBF aBUSCA;
          aCadena = #dsptaw02#0;
          |HAZ llena_ceros;

          |HAZ CambiaDatosLin;
     |FINSI;
|FPRC;

|PROCESO Lineas;
     |HAZ Claves;
     |HAZ SentSQL;
     |HAZ CargaTmp;
|FPRC;

/*
Listado de Las variables que se usan en dscomer9, para el tema del control
de los decimales en pantalla y en los informes.

Ahora estos datos estan en dsptaeu2. Clasificados en 3 tipos:
1 - Moneda Base
2 - Divisa
3 - Moneda Visualizacion

Este listado se ha obtenido a partir del cal : dsz99985. (+ info Juan)
*/

|PROCESO Claves;
     aComodin = "";
     aQueQuiero = "|K000";
     aAlfa1     = #100#aQueQuiero# % 103;
     nTCompo    = aAlfa1;
     nTCompo = nTCompo - 1;
     |PARA nCompo = 1;  |SI nCompo [ nTCompo;  |HACIENDO nCompo = nCompo + 1;
           |SI nCompo ! 1;
               aComodin = aComodin + " AND ";
           |FINSI;
           nPosi    = (nCompo * 300) + 103;
           aAlfa1   = #100#aQueQuiero# % nPosi;
           nNume    = aAlfa1;
           nNume    = nNume;
           aComodin = aComodin + nNume + " == ";
           aAlfa1   = #1#aQueQuiero# % nPosi;
           nNume    = aAlfa1;
           nNume    = nNume;
           aComodin = aComodin + #1#nNume#;
     |SIGUE;
|FPRC;
                                                                                                                                                                                                                                                      ;
|PROCESO SentSQL;
     aSQL = "SELECT * FROM " + aDefLineas + " INTO sqtempo.def WHERE " + aComodin;
     |SQL aSQL;
|FPRC;

|PROCESO CargaTmp;
     aTempo = "tempo";
     |CARGA_DEF aTempo, tempo@;
     |SI FSalida = 0;
          |PATH_DAT #200 aPath;
          |ABRE #200;

          |HAZ RellenaTmpLin;
          |LEE 000#200.p;
          |PARA;|SI FSalida=0; |HACIENDO;
               |HAZ Act_Lin;
               |LEE 000#200s;
          |SIGUE;
          |CIERRA #200;
     |FINSI;
     |DESCARGA_DEF #tempo@;
|FPRC;

|PROCESO dsptaw02;
     |HAZ LiteralesLin;
|FPRC;

|DEFBUCLE dsptaw02;
 #dsptaw02#1;
 ;
 INICIO;
 FINAL;
 ;
 NULL, dsptaw02;
|FIN;

|PROCESO Act_Lin;
     |ABRE #100;
     aQueQuiero = "|K000";
     aAlfa1     = #200#aQueQuiero# % 103;
     nTCompo    = aAlfa1;
     nTCompo = nTCompo;
     |PARA nCompo = 1;  |SI nCompo [ nTCompo;  |HACIENDO nCompo = nCompo + 1;
           nPosi    = (nCompo * 300) + 103;
           aAlfa1   = #200#aQueQuiero# % nPosi;
           nNume    = aAlfa1;
           nNume    = nNume;
           #100#nNume# = #200#nNume#;
     |SIGUE;
     |LEE 000#100=;
     |SI FSalida = 0;
          |BUCLE dsptaw02;
     |FINSI;
     |CIERRA #100
|FPRC;

|PROCESO ElPath;
     |DBASE aPathEuro;
     |QBF aPathEuro;
     aPathEuro = aPathEuro + "fich/";
     |PATH_DAT #10 aPathEuro;
     |PATH_DAT #11 aPathEuro;
|FPRC;

|PROCESO RellenaTmpCab;
     |PARA j=0;|SI j<nNumCampos; |HACIENDO j=j+1;
          aCadena=j;
          |HAZ llena_ceros; || en aCadena esta el resultado
          nCampo = aCadena;
          |HAZ VerLiterales;
     |SIGUE;
|FPRC;

|PROCESO VerLiterales;
     |ABRE #dsptaeu2;
     |LEE 000#dsptaeu2.p;
     |PARA; |SI FSalida = 0; |HACIENDO;
          nSwEncontrado = 0;
          |HAZ AnotaLiterales;
          |SI nSwEncontrado = 1;
               |SAL;
          |FINSI;
          |LEE 000#dsptaeu2.s;
     |SIGUE;
     |CIERRA #dsptaeu2;
|FPRC;

|PROCESO AnotaLiterales;
     aBUSCA=#dsptaeu2#0; ||Palabra que buscamos
     |QBF aBUSCA;
     |HAZ BuscaDatos;
     |SI nSwEncontrado = 1;
          |DDEFECTO #dsptaw01;
          #dsptaw01#0 = nCampo;
          |LEE 101#dsptaw01.=;
          |SI FSalida ! 0; |GRABA 020#dsptaw01.b; |FINSI;
          #dsptaw01#1 = aBUSCA;
          #dsptaw01#2 = #dsptaeu2#1;
          |GRABA 020#dsptaw01.a;
          |LIBERA #dsptaw01;
          ||aMensaje = "Acado de grabar : " + #dsptaw01#0 + "-" + #dsptaw01#1 + "/" + #dsptaw01#2;
          ||MENAV aMensaje;
     |FINSI;
|FPRC;

|PROCESO BuscaDatos;
     aCadena1="|C"+aCadena+"TIPO";  || buscamos el tipo
     aCadena2=#1#aCadena1#;
     |SI aCadena2="N";
          aCadena1="|C"+aCadena+"MINIMO";  || buscamos en la cadena
          aCadena2=#1#aCadena1#;
          aCadena3=aCadena2-aBUSCA;
          |SI aCadena2!aCadena3;   || convertir a euros
               nSwEncontrado = 1;
          |FINSI;
     |FINSI;
|FPRC;


||************************* L I N E A S *****************************

|PROCESO RellenaTmpLin;
     aMensaje = "2313 Procesando fichero de Datos: " + aFichero + " / " + #dsptaeu3#3;
     |MENSA aMensaje;

     ||PROCESO Similar al RellenaTmpCab;
     |PARA j=0;|SI j<nNumCamLin; |HACIENDO j=j+1;
          aCadena=j;
          |HAZ llena_ceros; || en aCadena esta el resultado
          nCampoLin = aCadena;
          |HAZ VerLiteralesLin;
     |SIGUE;
|FPRC;

|PROCESO VerLiteralesLin;
     |ABRE #dsptaeu2;
     |LEE 000#dsptaeu2.p;
     |PARA; |SI FSalida = 0; |HACIENDO;
          nSwEncontradoLin = 0;
          |HAZ AnotaLiteralesLin;
          |SI nSwEncontradoLin = 1;
               |SAL;
          |FINSI;
          |LEE 000#dsptaeu2.s;
     |SIGUE;
     |CIERRA #dsptaeu2;
|FPRC;

|PROCESO AnotaLiteralesLin;
     aBUSCA=#dsptaeu2#0; ||Palabra que buscamos
     |QBF aBUSCA;
     |HAZ BuscaDatosLin;
     |SI nSwEncontradoLin = 1;
          |DDEFECTO #dsptaw02;
          #dsptaw02#0 = nCampoLin;
          |LEE 101#dsptaw02.=;
          |SI FSalida ! 0; |GRABA 020#dsptaw02.b; |FINSI;
          #dsptaw02#1 = aBUSCA;
          #dsptaw02#2 = #dsptaeu2#1;
          |GRABA 020#dsptaw02.a;
          |LIBERA #dsptaw02;
          ||aMensaje = "Acado de grabar : " + #dsptaw02#0 + "-" + #dsptaw02#1 + "/" + #dsptaw02#2;
          ||MENAV aMensaje;
     |FINSI;
|FPRC;

|PROCESO BuscaDatosLin;
     aCadena1="|C"+aCadena+"TIPO";  || buscamos el tipo
     aCadena2=#100#aCadena1#;
     |SI aCadena2="N";
          aCadena1="|C"+aCadena+"MINIMO";  || buscamos en la cadena
          aCadena2=#100#aCadena1#;
          aCadena3=aCadena2-aBUSCA;
          |SI aCadena2!aCadena3;   || convertir a euros
               nSwEncontradoLin = 1;
          |FINSI;
     |FINSI;
|FPRC;
