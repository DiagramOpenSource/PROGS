|FICHEROS;
     inmom001 #0;
     inmom009 #9,MANTE;
     inmow011 #11,MANTE;
     inmom009@;
|FIN;

|VARIABLES;
     nRegistro = 0;
     nPrime = 0;
     aNivel = "";
     Tempo = "";
     nTecla = 0;
     nNivel = 0;
     nClave = 0;
     nConta = 0;
     aAlfa = "";
     i = 0;
     aDObr = ""; aHObr = "";
     aDSeg = ""; aHSeg = "";
     aDSer = ""; aHSer = "";
     aDCod = ""; aHCod = "";
|FIN;

|PROCESO Baja;
     #9#0 = nRegistro;
     |BORRA 020#9c;
|FPRC;

|PROCESO Modi;
     |PUSHV 0501 2380;

     #9#0 = nRegistro;
     |LEE 121#9=;
     |SI FSalida ! 0; |ACABA; |FINSI;

     |PARA i = 0; |SI i [ 9; |HACIENDO i = i + 1;
          #11i = #9i;
     |SIGUE;

     |PINPA #0#11;
     |PINDA #0#11;
     |ENDATOS #2#11;
     |SI FSalida = 0;
          |PARA i = 1; |SI i [ 9; |HACIENDO i = i + 1;
               #9i = #11i;
          |SIGUE;
          |GRABA 020#9a;
     |FINSI;
     |LIBERA #9;

     |POPV;
|FPRC;

|PROCESO Alta;
     |PUSHV 0501 2380;

     |DDEFECTO #11;
     #11#0 = nConta;
     #11#1 = Usuario % 810;
     #11#2 = ~;
     #11#5 = #0#9;
     #11#6 = #0#10;
     #11#7 = #0#11;
     #11#8 = #0#0;
     |PINPA #0#11;
     |PINDA #0#11;
     |ENDATOS #1#11;
     |SI FSalida = 0;
          |LEE 000#9u;
          |SI FSalida = 0;
               nConta = #9#0 + 1;
          |SINO;
               nConta = 1;
          |FINSI;
          |DDEFECTO #9;
          |PARA i = 1; |SI i [ 9; |HACIENDO i = i + 1;
               #9i = #11i;
          |SIGUE;
          |SI #9#4 = 1;
               #9#5 = #0#9;
          |FINSI;
          |SI #9#4 = 2;
               #9#5 = #0#9; #9#6 = #0#10;
          |FINSI;
          |SI #9#4 = 3;
               #9#5 = #0#9; #9#7 = #0#11;
          |FINSI;
          |SI #9#4 = 4;
               #9#8 = #0#0;
          |FINSI;
          #9#0 = nConta;
          |GRABA 020#9n;
     |FINSI;

     |POPV;
     |HAZ Mensa;
|FPRC;

|PROCESO Tipo11; |TIPO 11;
     |SI FSalida ] 10; |Y FSalida [ 14;
          |SI FSalida = 11;
               |CONFI "0000NBorrar registro?";
               |SI FSalida ! 0; |ERROR; |ACABA; |FINSI;
               nTecla = 11;
          |SINO;
               |SI nTecla = 0; nTecla = FSalida; |FINSI;
          |FINSI;
          nRegistro = #9#0;
          |PTEC 807;
     |FINSI;
|FPRC;

|PROCESO Tipo20_001; |TIPO 20;
     |SI FSalida = "OPCION";
          FSalida = "Observa.";
     |SINO;
          |LEE 000#0=;
          |SI FSalida = 0;
               |HAZ Observa;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Mensa;
     |MENSA "0000<F2> Alta  <F3> Baja <F4> Modifica  <F5> Clave  <F6> Nivel  <F11> Grid";
|FPRC;

|PROCESO CargaTmp;
     |SALVA_DATOS #inmom009@;
     |REPON_DATOS #9;
     |GRABA 040#9n;
|FPRC;

|DEFBUCLE CargaTmp;
     #inmom009@#2006;
     ;
     " ", "01.01.0000", aDObr, aDSeg, aDSer, aDCod;
     "z", "31.12.9999", aHObr, aHSeg, aHSer, aHCod;
     ;
     NULL, CargaTmp;
|FIN;

|PROCESO Min;
     #9#2 = "01.01.0000";
     #9#3 = nNivel;
     #9#4 = 00;
     |SI nNivel = 1;
          #9#5 = #0#9;
          #9#6 = "    ";
          #9#7 = "    ";
          #9#8 = "                    ";
     |FINSI;
     |SI nNivel = 2;
          #9#5 = #0#9;
          #9#6 = #0#10;
          #9#7 = "    ";
          #9#8 = "                    ";
     |FINSI;
     |SI nNivel = 3;
          #9#5 = #0#9;
          #9#6 = "    ";
          #9#7 = #0#11;
          #9#8 = "                    ";
     |FINSI;
     |SI nNivel = 4;
          #9#5 = "    ";
          #9#6 = "    ";
          #9#7 = "    ";
          #9#8 = #0#0;
     |FINSI;
     |SI nNivel = 5;
          #9#3 = " ";
          #9#5 = "    ";
          #9#6 = "    ";
          #9#7 = "    ";
          #9#8 = "                    ";
     |FINSI;
|FPRC;

|PROCESO Max;
     #9#2 = "31.12.9999";
     #9#3 = nNivel;
     #9#4 = 99;
     |SI nNivel = 1;
          #9#5 = #0#9;
          #9#6 = "zzzz";
          #9#7 = "zzzz";
          #9#8 = "zzzzzzzzzzzzzzzzzzzz";
     |FINSI;
     |SI nNivel = 2;
          #9#5 = #0#9;
          #9#6 = #0#10;
          #9#7 = "zzzz";
          #9#8 = "zzzzzzzzzzzzzzzzzzzz";
     |FINSI;
     |SI nNivel = 3;
          #9#5 = #0#9;
          #9#6 = "zzzz";
          #9#7 = #0#11;
          #9#8 = "zzzzzzzzzzzzzzzzzzzz";
     |FINSI;
     |SI nNivel = 4;
          #9#5 = "zzzz";
          #9#6 = "zzzz";
          #9#7 = "zzzz";
          #9#8 = #0#0;
     |FINSI;
     |SI nNivel = 5;
          #9#3 = "z";
          #9#5 = "zzzz";
          #9#6 = "zzzz";
          #9#7 = "zzzz";
          #9#8 = "zzzzzzzzzzzzzzzzzzzz";
     |FINSI;
|FPRC;

|PROCESO Nivel;
     |SI nNivel = 1; aNivel = nNivel + " obra         "; |FINSI;
     |SI nNivel = 2; aNivel = nNivel + " obra+seguidor"; |FINSI;
     |SI nNivel = 3; aNivel = nNivel + " obra+serie   "; |FINSI;
     |SI nNivel = 4; aNivel = nNivel + " cod.barras   "; |FINSI;
     |SI nNivel = 5; aNivel = nNivel + " todos        "; |FINSI;
|FPRC;

|PROCESO Observa;
     |DDEF aAlfa;
     aAlfa = aAlfa + "inmom009";
     |CARGA_DEF aAlfa, inmom009@;
     |SI FSalida ! 0; |ACABA; |FINSI;

     Tempo = Usuario;

     |PUSHV 0501 2380;
     nNivel = 5;
     nClave = 2;
     nPrime = 0;
     nTecla = -1;
     |PARA; |SI; |HACIENDO;
          |SI nPrime ! 0; nTecla = 0; |FINSI;
          |PINPA #0#9;
          |HAZ Mensa;
          |HAZ Nivel;
          |SI nClave = 2; |Y nPrime ! 0;
               |C_M #9#2, 1, "S";
               aAlfa = " Nivel: " + aNivel +  "   Orden: fecha";
               |PINTA 1002, aAlfa;
               |ATRI S; |PINTA 1114, " Fecha    "; |ATRI 0;
               |ENTLINEAL #2#9, 2, 4, 22, 0, Min, Max;
          |FINSI;
          |SI nClave = 3; |Y nPrime ! 0;
               |C_M #9#2, 1, "N";
               aAlfa = " Nivel: " + aNivel +  "   Orden: tipo+fecha";
               |PINTA 1002, aAlfa;
               |ATRI S; |PINTA 1114, " Fecha    "; |ATRI 0;
               |ATRI S; |PINTA 1127, "Ti"; |ATRI 0;
               |ENTLINEAL #3#9, 2, 4, 22, 0, Min, Max;
          |FINSI;
          |SI nTecla = 0; |SAL; |FINSI;
          |SI nTecla = 13;
               nClave = nClave + 1;
               |SI nClave > 3; nClave = 2; |FINSI;
          |FINSI;
          |SI nTecla = 14;
               nNivel = nNivel + 1;
               |SI nNivel > 5; nNivel = 1; |FINSI;
          |FINSI;
          |SI nTecla = 10; |O nTecla = 11; |O nTecla = 12;
               |NOME_DAT #9 "inmom009";
               |ABRE #9;
               |SI nTecla = 10; |HAZ Alta; |FINSI;
               |SI nTecla = 11; |HAZ Baja; |FINSI;
               |SI nTecla = 12; |HAZ Modi; |FINSI;
               |CIERRA #9;
          |FINSI;

          |SI nNivel = 5;
               |NOME_DAT #9 Tempo;
               |DELINDEX #9;

               |ABRE #9;
               aDObr = #0#9; aHObr = #0#9;
               aDSeg = "    "; aHSeg = "zzzz";
               aDSer = "    "; aHSer = "zzzz";
               aDCod = "                    "; aHCod = "zzzzzzzzzzzzzzzzzzzz";
               |BUCLE CargaTmp;

               aDObr = #0#9; aHObr = #0#9;
               aDSeg = #0#10; aHSeg = #0#10;
               aDSer = "    "; aHSer = "zzzz";
               aDCod = "                    "; aHCod = "zzzzzzzzzzzzzzzzzzzz";
               |BUCLE CargaTmp;

               aDObr = #0#9; aHObr = #0#9;
               aDSeg = "    "; aHSeg = "zzzz";
               aDSer = #0#11; aHSer = #0#11;
               aDCod = "                    "; aHCod = "zzzzzzzzzzzzzzzzzzzz";
               |BUCLE CargaTmp;

               aDObr = "    "; aHObr = "zzzz";
               aDSeg = "    "; aHSeg = "zzzz";
               aDSer = "    "; aHSer = "zzzz";
               aDCod = #0#0; aHCod = #0#0;
               |BUCLE CargaTmp;
               |CIERRA #9;
          |SINO;
               |NOME_DAT #9 "inmom009";
          |FINSI;
          nPrime = 1;
     |SIGUE;
     |POPV;

     |DESCARGA_DEF #inmom009@;

     |NOME_DAT #9 Tempo;
     |DELINDEX #9;
     |NOME_DAT #9 "inmom009"; || restaura al final
     |MENSA "0000_";
|FPRC;

|PROCESO Tipo66; |TIPO 66;
     |SI nNivel = 5;
          |SI nClave = 2;
               |CONSULTA_F_CLAVE #2#9, Min, Max;
          |SINO;
               |CONSULTA_F_CLAVE #3#9, Min, Max;
          |FINSI;
     |SINO;
          |MENAV "0000Opcion exclusiva para nivel 5";
     |FINSI;
|FPRC;
