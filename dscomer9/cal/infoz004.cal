|FICHEROS;
     infoz004 #0;
     infom005 #5;
     infom006 #6;
     infom000 #100;
     agifa134 #134;
     agifa142 #142;
     tmp00000 #300;
|FIN;

|VARIABLES;
     &eaMensaje = "";
     &enSwInfoz004 = 0;
     &eaImprime = "";
     &eaMenuImpre = "";
     &ser_fac = "";
     &num_fac = 0;
     &reimp = "";
     &eaOrdenado = "";
     {1}aBaseLocal = "";
     aAlfa = "";
     aCar = "";
     aIP = "";
     aFic = "";
     aBase = "";
     nHay = 0;
     aSer = "";
     fHoy = @;
     nNum = 0;
     aParam = "";
|FIN;

|PROCESO Subcarpeta;
     |SI #100#21 = "S";
          nNum = #134#0 - (#134#0 $ #100#22);
          aAlfa = aAlfa + nNum + "/";
     |FINSI;
|FPRC;

|PROCESO Facturas;
     |SI #0#8 = "N";
          #5#0 = "F";
          #5#1 = #134#49;
          #5#2 = #134#0;
          |LEE 000#5=;
          |SI FSalida = 0; |ACABA; |FINSI;
     |FINSI;

     aAlfa = #0#6; |QBF aAlfa;
     aCar = aAlfa % -101;
     |SI aCar ! "/"; |Y aCar ! "\"; aAlfa = aAlfa + "/"; |FINSI;
     |HAZ Subcarpeta;
     |MKDIR aAlfa;
     aSer = #134#49;

     enSwInfoz004 = 1;
     aIP = ""; |IP_REMOTO aIP;
     |SI aIP = "";
          aFic = aAlfa + aSer + (("000000" + #134#0)%-106)+ ".pdf";
          |QBT aFic;
          eaImprime = "CrystalReport;" + aFic;
          ser_fac = #134#49;
          num_fac = #134#0;
          reimp = #134#45;
          eaOrdenado = #142#141;
          |SUB_EJECUTA_NP "agifa136";
     |SINO;
          aFic = aAlfa + aSer + (("000000" + #134#0)%-106)+ ".pdf";
          |QBT aFic;
          |ESPECIFICA "RBASS", aBaseLocal;
          aAlfa = aBaseLocal1 + aSer + (("000000" + #134#0)%-106)+ ".pdf";
          |QBT aAlfa;
          eaImprime = "CrystalReport;" + aAlfa;
          ser_fac = #134#49;
          num_fac = #134#0;
          reimp = #134#45;
          eaOrdenado = #142#141;
          |SUB_EJECUTA_NP "agifa136";
          |RECIBE_FICHERO aAlfa, aFic;
          |RFBORRA aAlfa;
     |FINSI;
     nHay = 1;
|FPRC;

|PROCESO Facturas0;
     aAlfa = "[1]0000Envia factura:" + #134#49 + "/" + #134#0;
     ||MENAV aAlfa;

     |LEE 140#134=;          ||bloqueando sin errores ni reintentos
     |SI FSalida = 0;
          |LIBERA #134;
          |HAZ Facturas;
     |FINSI;
|FPRC;

|DEFBUCLE Facturas;
     #134#2;
     ;
     "      ", #0#4, #0#0, #0#2;
     "zzzzzz", #0#5, #0#1, #0#3;
     ;
     NULL, Facturas0;
|FIN;

|PROCESO Sync;
     |SI nHay = 1;
          |MENSA "0000Sincronizando con servidor web...";
          |DBASS aBase;
          aAlfa = "/bin/chmod 775 " + aBase + "dscomer9/001980/sync_inforpor.sh"
          |SYSTEM aAlfa;
          aAlfa = aBase + "dscomer9/001980/sync_inforpor.sh " + aBase + "dscomer9/001980/";
          |SYSTEM aAlfa;
     |FINSI;
|FPRC;

|PROCESO Tipo3; |TIPO 3;
     |ABRE #142; |LEE 000#142p; |CIERRA #142;
     |ABRE #5;
     |BUCLE Facturas;
     |CIERRA #5;
|FPRC;

|PROCESO NoEmitidos;
     #134#49 = #6#1;
     #134#0 = #6#2;
     |LEE 140#134=;
     |SI FSalida = 0;
          |LIBERA #134;
          |SI #134#45 = "S";
               aAlfa = "[2]0000Envia factura:" + #134#49 + "/" + #134#0;
               ||MENAV aAlfa;

               |HAZ Facturas;
               |BORRA 020#6a;
          |FINSI;
     |SINO;
          |BORRA 020#6a;
     |FINSI;
     |LIBERA #6;
|FPRC;

|DEFBUCLE NoEmitidos;
     #6#1;
     ;
     "F", "     ", 000001;
     "F", "zzzzz", 999999;
     be;
     NULL, NoEmitidos;
|FIN;

|PROCESO Tmp300;
     |SI #300#2 = "N";
          #134#49 = #300#0;
          #134#0 = #300#1;
          |LEE 140#134=;
          |SI FSalida = 0;
               |LIBERA #134;
               |SI #134#1 ! fHoy;
                    aAlfa = "[3]0000Envia factura:" + #134#49 + "/" + #134#0;
                    ||MENAV aAlfa;
                    |HAZ Facturas;
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE Tmp300;
     #300#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, Tmp300;
|FIN;

|PROGRAMA;
     aParam = PARAMETRO; aParam = aParam > "";
     |ABRE #100; |LEE 000#100p; |CIERRA #100;
     |ABRE #0;
     |LEE 101#0p;
     |SI FSalida ! 0; |GRABA 020#0b; |FINSI;

     |SI aParam = "MENU";
          |PINPA #0#0;
          |PINDA #0#0;
          |ENDATOS #1#0;
          |SI FSalida = 0;
               |HAZ Sync;
               |GRABA 020#0a;
          |FINSI;
     |SINO;
          |LIBERA #0;
          |SI aParam = "PROG"; |O aParam = "PROGREIMP";
               eaMensaje = "Comienzo envio facturas";
               ||HAZ EnviaMail;

               #0#0 = "";
               #0#1 = "zzzzz";
               #0#2 = 1;
               #0#3 = 999999;
               #0#4 = ~;
               #0#5 = ~;
               #0#8 = "N";
               |SI aParam = "PROGREIMP"; #0#8 = "S"; |FINSI;
               |HAZ Tipo3;
               |ABRE #134;
               |ABRE #5;
               |BUCLE NoEmitidos;
               |CIERRA #5;
               |CIERRA #134;
               |HAZ Sync;

               eaMensaje = "Fin proceso facturas";
               ||HAZ EnviaMail;
          |SINO;
               #0#8 = "N";
               fHoy = ~;
               |NOME_DAT #300 aParam;
               |ABRE #134;
               |ABRE #5;
               |BUCLE Tmp300;
               |CIERRA #5;
               |CIERRA #134;
               |HAZ Sync;
          |FINSI;
     |FINSI;
     |CIERRA #0;
|FPRO;
