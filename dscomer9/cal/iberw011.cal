|FICHEROS;
   iberw011 #0;
|FIN;

|VARIABLES;
   aPath   = "";
   aAlfa   = "";
   aFecha  = "";

   DFecha = @;
   HFecha = @;
   fFecha = @;

   nCalc    = 0;
   Salir    = 0;
   nCanal   = 0;
   nCont    = 0;
   nCentro  = 0;
   nImporte = 0;
|FIN;

|PROCESO Carga;
   aAlfa = "rb  " + aPath;
   |FABRE aAlfa;
   |SI FSalida < 0;
      |MENAV "    Problemas para abrir el fichero origen "; |ACABA;
   |FINSI;
   nCanal = FSalida; nCont = 1;
   |PARA; |SI; |HACIENDO;
      aAlfa = " Importando registros : " + nCont + "        ";
      |ATRI I; |PINTA 2320, aAlfa; |ATRI N; nCont = nCont + 1;
      aAlfa = nCanal + " 10"; |FLEE aAlfa; nCentro = aAlfa; nCentro = (nCentro / 100) ! 0;
      |SI FSalida ! 10; |SAL; |FINSI;
      aAlfa = nCanal + " 10"; |FLEE aAlfa; aFecha  = aAlfa;
      aAlfa = nCanal + " 23"; |FLEE aAlfa; nImporte = aAlfa;
      aAlfa = nCanal + " 2";  |FLEE aAlfa;

      aFecha = (aFecha % 0102) + "." + (aFecha % 0402) + "." + (aFecha % 0704);
      fFecha = aFecha;
      |SI DFecha [ fFecha; |Y fFecha [ HFecha;
         |DDEFECTO #iberw011;
         #iberw011#0 = nCentro;
         #iberw011#1 = aFecha;
         |LEE 000#iberw011.=;
         |SI FSalida ! 0;
            |DDEFECTO #iberw011;
            #iberw011#0 = nCentro;
            #iberw011#1 = aFecha;
            #iberw011#2 = nImporte;
            |GRABA 020#iberw011.n;
         |FINSI;
      |FINSI;
   |SIGUE;
   FSalida = nCanal; |FCIERRA FSalida;
|FPRC;

|PROCESO Reinicia;
   #iberw011#3 = 0; #iberw011#4 = 0; #iberw011#5 = 0; #iberw011#6  = 0;
   #iberw011#7 = 0; #iberw011#8 = 0; #iberw011#9 = 0; #iberw011#10 = 0;
   |GRABA 020#iberw011.a;
|FPRC;

|DEFBUCLE Reinicia;
#iberw011#1;
;
INICIO;
FINAL;
;
NULL,Reinicia;
|FIN;

|PROCESO CargaAscii; |TIPO 11;
   |SI FSalida = 11;
      |SALVA_FICHA #iberw011;
      |BUCLE Reinicia;
      |REPON_FICHA #iberw011;
      |PTEC 815; |PTEC 814;
   |FINSI;
   |SI FSalida = 10;
      |CONFI "    NEfectuar la carga desde un fichero ASCII ? ";
      |SI FSalida = 0;
         |PUSHV 1904 2176;
         |BLANCO 2005 2176;
         |ATRI R; |PINTA 1905, " Seleccione el intervalo de fechas "; |ATRI N;
         |PINTA 2005, " Desde fecha           hasta fecha ";
         Salir = 0;
         |PARA; |SI Salir = 0; |HACIENDO;
            E_sup = 10; E_inf = ""; aFecha = DFecha;
            aFecha = 2018 ? 1;
            |SI FSalida ! 7; |Y FSalida ! 1;
               DFecha = aFecha; nCalc = DFecha; DFecha = nCalc; aAlfa = DFecha; |QBF aAlfa;
               |SI aAlfa = aFecha; Salir = 1; |FINSI;
            |SINO;
               Salir = -1;
            |FINSI;
         |SIGUE;
         |SI Salir < 0; |VETE Salida; |FINSI;
         DFecha = aFecha; Salir = 0;
         |PARA; |SI Salir = 0; |HACIENDO;
            E_sup = 10; E_inf = ""; aFecha = HFecha;
            aFecha = 2040 ? 1;
            |SI FSalida ! 7; |Y FSalida ! 1;
               HFecha = aFecha; nCalc = HFecha; HFecha = nCalc; aAlfa = HFecha; |QBF aAlfa;
               |SI aAlfa = aFecha; Salir = 1; |FINSI;
            |SINO;
               Salir = -1;
            |FINSI;
         |SIGUE;
         |SI Salir < 0; |VETE Salida; |FINSI;
         HFecha = aFecha;
         |BLANCO 2005 2176;
         |ATRI R; |PINTA 1905, " Introduzca el path del fichero origen "; |ATRI N;
         |PARA; |SI; |HACIENDO;
            E_sup = 72; E_inf = ""; aPath = "";
            aPath = 2005 ? 1;
            |SI FSalida = 7; |O FSalida = 1; aPath = ""; |FINSI;
            |QBF aPath;
            |SI aPath = ""; |SAL; |FINSI;
            |XABRE "E", aPath, nCanal;
            |SI FSalida < 0;
               |MENAV "    No es encuentra el fichero. Revise el camino ";
            |SINO;
               |SAL;
            |FINSI;
         |SIGUE;
         |SI aPath ! "";
            |CIERRA #iberw011; |DELINDEX #iberw011; |ABRE #iberw011;
            |HAZ Carga;
         |FINSI;
|ET Salida;
         |POPV;
      |FINSI;
      |ERROR;
      |PONCONTROL 23,"si";
   |FINSI;
|FPRC;

|RUTINA inf;
   #iberw011#0 = 000;
   #iberw011#1 = "01.01.0000";
|FRUT;

|RUTINA sup;
   #iberw011#0 = 999;
   #iberw011#1 = "31.12.9999";
|FRUT;

|PROGRAMA;
   |CLS;
   |CABEZA "Datos Tarjetas Iber";
   |PINPA #0#0; |PINDA #0#0;
   |ABRE #iberw011;
   |ENTLINEAL #1#iberw011,-2,4,18,1,inf,sup;
   |CIERRA #iberw011;
|FPRO;
