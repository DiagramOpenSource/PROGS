|FICHEROS;
     con00702 #0;
     con00686 #2;  ||Temporal de aparcadas
     con00625 #1;
     con00626 #3; ||Lineas de O.R.
     con00642 #4; ||lineas de factura
     agifa019 #5;
     con00637 #6; ||facturas
     agifa024 #7;
     con00603 #8; ||Maestro M.Obra
     con00606 #9; ||Maestro Ser. Exteriores
     agifa059 #11;
     agifa134 #12;
     agifa027 #13;
     agifa091 #14;
     dsz99984 #16;  ||Recibos
     con01625 #18;  ||Texto inicial O.R.
     con00683 #19;  ||Control aplicacion
     con00634 #21;  ||texto explicativo
     con00680 #22;
     agifa007 #23;  ||series

     dsz99984 #100;
     agifa133 #101;
|FIN;

|VARIABLES;
     &EURODB = 0;
 r_alfa = "";
 r_long = "";
 r_num = 0;
 mensaje = "";
 sal = 0;
 sw = 0;
 cliente = "";
 lin = 0;
 tipo_iva = 0;
 lfac = 1;
 cuenta_conta = "";
 numf = "";
 fmes = "  ";
 mes = 0;
 imneto = 0;
 num = 0;
 importe = 0;
 tipo_imp = 0;
 resto = 0;
 redondeo = 0;
 em = 0;
 fecha_fac = @;
 i = 0;
 j = 0;
 k = 0;
 fechae = @;
 mes_venci = "";
 mes_ven = 0;
 fecha = "         ";
 dia_venci = "";
 any_venci = "";
 fechas = "";
 cafe = "";
 diafe = "";
 dianu = "";
 expor = "";
 dia1 = 0;
 dia2 = 0;
 dia3 = 0;
 &nCuantos = 0;
 aMenav = "";
|FIN;

|PROCESO cambia_fecha;
  dia_venci = fechae % A102;
  mes_venci = fechae % A402;
  mes_ven = mes_venci + 1;
  mes_venci = ("00" + mes_ven) % -102;
  any_venci = fechae % A704;
  |SI mes_ven > 12;
    mes_venci = "01";
    mes_ven = any_venci + 1;
    any_venci = mes_ven;
  |FINSI;
  fechas = dia_venci + "." + mes_venci + "." + any_venci;
  fechae = fechas;
|FPRC;

|PROCESO cambios;
   fecha = fechae;
   cafe = fecha;
   diafe = fecha % A102;
   dianu = diafe;
|FPRC;

|PROCESO fijos;
  |SI #7#22 = 0;
    |VETE finfijos;
  |FINSI;
  |HAZ cambios;
  |SI dianu > #7#22;
    |VETE fijos2;
  |FINSI;
  |ET fijos1;
  |SI dianu = #7#22;
     |VETE finfijos;
  |FINSI;
  fechae = fechae + 1;
  |HAZ cambios;
  |VETE fijos1;
  |ET fijos2;
  |SI #7#23 = 0;|O dianu [ #7#22;
    |VETE fijos1;
  |FINSI;
  |SI dianu > #7#23;
     |VETE fijos3;
  |FINSI;
  |SI dianu = #7#23;
     |VETE finfijos;
  |FINSI;
  fechae = fechae + 1;
  |HAZ cambios;
  |VETE fijos2;
  |ET fijos3;
  |SI #7#24 = 0;|O dianu > #7#24;|O dianu [ #7#22;|O dianu [ #7#23;
    |VETE fijos1;
  |FINSI;
  |SI dianu = #7#24;
     |VETE finfijos;
  |FINSI;
  fechae = fechae + 1;
  |HAZ cambios;
  |VETE fijos3;
  |ET finfijos;
  fecha = fechae;
|FPRC;

|PROCESO vencical;
  |DDEFECTO #16;
  |ABRE #16;
  j = #14k / em;
  |SI j ! 0; |O #7#22 ! 0;|O #7#23 ! 0;|O #7#24 ! 0;
     fechae = fecha_fac + j;
     |HAZ fijos;
     mes_venci = fechae % A402;
     mes_ven = mes_venci;
     |SI mes_ven = #7#21;  ||si mes de no pago
          |HAZ cambia_fecha;
          fecha = fechae;
     |FINSI;
     #16#0 = #6#1;
     #16#1 = i;
     #16#7 = #6#2;
     #16#21 = #14#69;
     #16#8 = fecha;
     #16#9 = importe * tipo_imp;
     |SI i = num;
        #16#9 = #16#9 + (resto * tipo_imp);
     |FINSI;
     #16#17 = #6#0;
  |SINO;
     #16#0 = #6#1;
     #16#1 = i;
     #16#7 = #6#2;
     #16#21 = #14#69;
     #16#8 = #6#2;
     #16#9 = importe * tipo_imp;
     #16#17 = #6#0;
  |FINSI;
  |SI EURODB = 2; #16#9 = (#16#9 / 100) ! EURODB; |FINSI;
  #16#2 = #6#3;          || codigo cliente
  ||#16#3 = #6#4;          || nombre del cliente (desde con00637)
  #16#3 = #agifa024#1;       || nombre del cliente (desde agifa024)
  #16#4 = #6#21;
  #16#12 = cuenta_conta;
  |SI FSalida = 0;
       nCuantos = nCuantos + 1;
  |FINSI;

  #dsz99984#12 = #agifa024#16;
  #dsz99984#23 = num;
  #dsz99984#24 = #agifa091#0;
  #dsz99984#25 = #agifa091#1;
  #dsz99984#26 = #agifa024#32;
  #dsz99984#27 = #agifa024#33;
  #dsz99984#28 = #agifa024#31;
  #dsz99984#29 = #agifa024#30;

  |ABRE #agifa133;
  |DDEFECTO #agifa133;
  #agifa133#0 = #dsz99984#0;
  #agifa133#1 = #dsz99984#1;
  #agifa133#2 = #dsz99984#9;
  #agifa133#3 = #dsz99984#8;
  #agifa133#4 = #dsz99984#17;
  #agifa133#8 = #dsz99984#1;
  |GRABA 020#agifa133.n;
  |CIERRA #agifa133;

  |HAZ AbreRecVenta;
  |HAZ RecVenta;
  |HAZ CierraRecVenta;
|FPRC;

|PROCESO crea_recibo;
    |DDEFECTO #7;
    |ABRE #7;
    #7#0 = #6#3;
    |LEE 000#7=;
    |SI FSalida ! 0;
         aMenav = "0000Cliente [" + #7#0 + "] inaccesible.";
         |MENAV aMenav;
    |FINSI;
    |CIERRA #7;

    |ABRE #14;
    #14#0 = #6#20;
    |LEE 000#14=;
    |SI FSalida ! 0;
         aMenav = "0000Forma de pago [" + #14#0 + "] inaccesible.";
         |MENAV aMenav;
    |FINSI;
    |CIERRA #14;

    num = #14#3;
    importe = #6#11;
    |SI importe < 0;
     tipo_imp = -1;
     importe = -importe;
    |SINO;
     tipo_imp = 1;
    |FINSI;
    importe = importe ! EURODB;
    |SI EURODB = 2; importe = (importe * 100) ! 0; |FINSI;
    resto = importe $ num;
    importe = importe / num;
    redondeo = importe ! 0;
    |SI redondeo ! importe;    ||trunca el importe
      |SI redondeo > importe;
        importe = redondeo - 1;
      |SINO;
        importe = redondeo;
      |FINSI;
    |FINSI;
    |SI #14#5 = "N";
       em = 1;
    |SINO;
       em = 1000;
    |FINSI;
    fecha_fac = #6#2;
    |PARA i = 1;|SI i [ num;|HACIENDO i = i + 1;
      k = i + 5;
      |HAZ vencical;
    |SIGUE;
|FPRC;
