|FICHEROS;
     champ014 #0;
     champ004 #4;
     champ013 #13;
     agifa010 #10;
     agifa019 #19;
     agifa071 #71;
|FIN;

|VARIABLES;
     &eaAlfa = "";
     &enComision = 0;
     &enCoste = 0;
     &enForma = 0;
     &enDescuento1 = 0;
     &enDescuento2 = 0;
     &eaTComision = "";
     &eaCliente = "";
     &eaArticulo = "";
     &eaRepre = "";
     &enDirEnt = 0;
     &enAlmacen = 0;
     aAlfa = "";
     nHandle = 0;
     aDato = "";
     aIP = "";
     aFic = "";
  {1}aContiene = "";
  {1}aLocal = "";
     aDes = "";
     aOrg = "";
     aMd5Org = "";
     aMd5Des = "";
     aTgz = "";
     aBase = "";
     aExe = "";
     aLon = "";
     aCar = "";
     nPos = 0;
     aArt = "";
     aBul = "";
     aKil = "";
     aPre = "";
     nLinFic = 0;
     nLin = 0;
     nEsta = 0;
     nLinAlb = 0;
     aVal = "";
     i = 0;
|FIN;

|PROCESO BusArti;
     aAlfa = #71#2; |QBF aAlfa;
     |SI aAlfa = aArt;
          nEsta = 1;
          |ERROR10;
     |FINSI;
|FPRC;

|PROCESO QuitaPunto;
     aVal = "";
     aLon = aAlfa % A0;
     |PARA i = 1; |SI i [ aLon; |HACIENDO i = i + 1;
          nPos = i * 100 + 1;
          aCar = aAlfa % nPos;
          |SI aCar ! ".";
               aVal = aVal + aCar;
          |FINSI;
     |SIGUE;
     aAlfa = aVal;
|FPRC;

|PROCESO Dato;
     nLinFic = nLinFic + 1;
     eaAlfa = aDato; |HAZ QuitaRarosImp; aDato = eaAlfa;
     aArt = aDato % 3205; |QBF aArt;
     aDes = aDato % 5740;
     aBul = aDato % 9803;
     aKil = aDato % 10211; |QBF aKil;
     aPre = aDato % 11409;

     aAlfa = aBul; |HAZ QuitaPunto; aBul = aAlfa;
     aAlfa = aKil; |HAZ QuitaPunto; aKil = aAlfa;
     aAlfa = aPre; |HAZ QuitaPunto; aPre = aAlfa;

     |SI aKil ! ""; |Y aArt ! ""; |Y nLinFic > 7;
          #13#0 = aArt;
          |LEE 000#13=;
          |SI FSalida ! 0;
               nEsta = 0;
               |BUCLELIN 2 BusArti #10;
               |SI nEsta = 0;
                    |ABRE #19;
                    #19#0 = aArt;
                    |LEE 000#19=;
                    |SI FSalida ! 0;
                         #19#1 = aDes;
                         #19#18 = aPre;
                         #19#19 = aPre;
                         #19#20 = aPre;
                         #19#147 = aPre;
                         #19#148 = aPre;
                         #19#149 = aPre;
                         |GRABA 020#19n;
                    |FINSI;
                    |CIERRA #19;

                    #71#29 = #10#52;
                    #71#0 = #10#0;
                    #71#1 = 999;
                    |LEE 000#71];
                    |SI FSalida = 0;
                         |LEE 000#71a;
                    |SINO;
                         |LEE 000#71u;
                    |FINSI;
                    |SI FSalida = 0; |Y #71#29 = #10#52; |Y #71#0 = #10#0;
                         nLin = #71#1 + 1;
                    |SINO;
                         nLin = 1;
                    |FINSI;
                    |DDEFECTO #71;
                    #71#29 = #10#52;
                    #71#0 = #10#0;
                    #71#1 = nLin;
                    #71#2 = aArt;
                    #71#3 = 1;
                    #71#4 = aDes;
                    #71#5 = aKil;
                    #71#6 = aPre;
                    #71#11 = #19#30;
                    #71#13 = #19#2;
                    #71#15 = #10#3;
                    #71#16 = #10#34;
                    #71#17 = #10#35;
                    #71#36 = aPre;

                    enDescuento1 = #71#8;
                    enDescuento2 = #71#33;
                    eaTComision  = #71#16;
                    eaCliente    = #10#3;
                    eaArticulo   = #71#2;
                    eaRepre      = #10#6;
                    enDirEnt = #10#84;
                    enAlmacen = #71#3;
                    |HAZ Comisiones;
                    |HAZ ComiMarhu;
                    #71#10 = enComision;

                    |HAZ TotalLin71;

                    enForma = 1;
                    |HAZ AcumulaAV;
                    #71#14 = enCoste;
                    |GRABA 020#71n;

                    #4#0 = #71#29;
                    #4#1 = #71#0;
                    #4#2 = #71#1;
                    #4#3 = aBul;
                    #4#4 = aKil;
                    #4#5 = 0;
                    |GRABA 020#4n;

                    nLinAlb = nLinAlb + 1;
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO CopiaTgz;
     aTgz = "pdftotext.tgz";
     |DBASE aAlfa;
     aOrg = aAlfa + "plugins/" + aTgz;
     |SI aIP = "";
          |DBASS aBase;
          aDes = aBase + aTgz;

          aContiene = aOrg;
          |ESPECIFICA "MD5", aContiene;
          aMd5Org = FSalida;
          aContiene = aDes;
          |ESPECIFICA "MD5", aContiene;
          aMd5Des = FSalida;
          |SI aMd5Org ! aMd5Des;
               |COPIA_FICHERO aOrg, aDes;
               |DESCOMPRIME aDes;
               |DETAR aDes, aBase;
          |FINSI;
     |SINO;
          |ESPECIFICA "RBASS", aLocal;
          aBase = aLocal;
          aDes = aBase + aTgz;

          aContiene = aOrg;
          |ESPECIFICA "MD5", aContiene;
          aMd5Org = FSalida;
          aContiene = aDes;
          |ESPECIFICA "REMOTOMD5", aContiene;
          aMd5Des = FSalida;
          |SI aMd5Org ! aMd5Des;
               |ENVIA_FICHERO aOrg, aDes;
               |RDESCOMPRIME aDes;
               |RDETAR aDes, aBase;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Pdf2Txt;
     aOrg = aBase + Usuario + ".txt";
     aExe = aBase + "pdftotext/pdftotext.exe -f 1 -layout " + aFic + " " + aOrg;
     |SI aIP = "";
          |SYSTEM aExe;
          aDes = aOrg;
     |SINO;
          |RSYSTEM aExe;
          |DFICE aDes; aDes = aDes + Usuario;
          |RECIBE_FICHERO aOrg, aDes;
          |RFBORRA aOrg;
     |FINSI;
     aFic = aDes;
|FPRC;

|PROCESO Tipo3; |TIPO 3;
     aFic = #0#2; |QBF aFic;
     |SI aFic ! "";
          aIP = ""; |IP_REMOTO aIP;
          |HAZ CopiaTgz;
          |HAZ Pdf2Txt;

          |ABRE #4;
          |ABRE #10;
          |ABRE #13;
          |ABRE #71;
          #10#52 = #0#0;
          #10#0 = #0#1;
          |LEE 121#10=;
          |SI FSalida = 0;
               |XABRE "", aFic, nHandle;
               |SI FSalida ] 0;
                    |XLEE nHandle, 250, aDato;
                    |PARA; |SI FSalida ] 0; |HACIENDO;
                         |HAZ Dato;
                         |XLEE nHandle, 250, aDato;
                    |SIGUE;
                    |XCIERRA nHandle;
               |SINO;
                    |MENAV "0000Error al abrir fichero...";
               |FINSI;
               |FBORRA aFic;

               |HAZ LimCabAgifa010;
               |BUCLELIN 0 CalCabAgifa010 #10;
               |GRABA 020#10a;

               aAlfa = "0000" + nLinAlb + " Lineas añadidas...";
               |MENAV aAlfa;
          |FINSI;
          |CIERRA #71;
          |CIERRA #13;
          |CIERRA #10;
          |CIERRA #4;
     |FINSI;
|FPRC;

|PROCESO Browse; |TIPO 0;
     |SI FSalida ! 13; |ACABA; |FINSI;

     aAlfa = "F*.pdf";

     |BROWSE aAlfa;

     aAlfa = aAlfa % 2; |QBF aAlfa;
     |SI aAlfa = "F"; aAlfa = ""; |FINSI;

     |QBF aAlfa;
     |SI aAlfa = "";
          |ERROR;
     |SINO;
          #0#2 = aAlfa; |PINTA #0#2;
     |FINSI;
|FPRC;

|PROGRAMA;
     |CABEZA "Importacion marcufresco";
     |ABRE #0;
     |LEE 101#0p;
     |SI FSalida ! 0; |GRABA 020#0b; |FINSI;
     |PINPA #0#0;
     |PINDA #0#0;
     |ENDATOS #1#0;
     |SI FSalida = 0; |GRABA 020#0a; |FINSI;
     |CIERRA #0;
|FPRO;
