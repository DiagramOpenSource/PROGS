|FICHEROS;
     tlfz0002 #0;   ||semat001
     agifa134 #134;
     tlfz0003 #3,MANTE;   ||semat002
     agifa024 #4;
     agifa142 #6;
     tlfm0022 #22;

     agifa324 #324;
     agifa509 #509;
     agifa515 #515;
     agifa516 #516;

     :/dscarter/def/dscam003 #30;
     :/dscarter/def/dscam004 #40;
     :/dscarter/def/dscam045 #45;
|FIN;

|VARIABLES;
     nLinCob = 0;
     nCobro = 0;
     nCambio = 0;
     aEstaCobrado = "";
     aHayReci = "";
     nEmpCarte = 0;
     nEmpresa = 0;

     aAlfa = "";
     nImpa     = 0;
     aImpa     = "";
     &eaTag          = "";
     pro = "";
     {-1}menu = "";
     menu1 = "2400";
     menu2 = "1";
     menu3 = "Entrada:  ";
     menu4 = "FVMC";
     menu5 = " Aut. Factura ";
     menu6 = " Aut. Vencimiento ";
     menu7 = " Manual ";
     menu8 = " Cliente ";
     lin = 0;
     fmes = "";
     mes = 0;
     nombre = "";
     salmenu = 0;
     aEmp = "";
     aBase = "";
|FIN;

|PROCESO LlenaReci;
     #0#0 = #30#1;
     #0#1 = #30#2;
     #0#2 = #30#32;
     #0#3 = #30#6;
     #0#4 = #30#64;
     #0#5 = ~;
     #0#7 = "N";
     #0#8 = #30#0;
     #0#9 =  #6#13;
     #0#11 = #30#23;
     #0#13 = #30#99;
     #0#14 = #30#65;
     #0#15 = #30#12;
     #0#18 = aEmp;
     #0#19 = #30#40;
     #0#20 = #30#57;
|FPRC;

|PROCESO Recibos;
     |SI #30#14 = "C"; |O #30#14 = "P";
          |DDEFECTO #0;
          lin = lin + 1;
          #0#6 = lin;    ||Clave linea
          |HAZ LlenaReci;
          |GRABA 021#0n;
          |SI lin = 9999; |ERROR10; |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE Recibos11;
     #30#11;
     ;
     "      ", "01.01.0000", "     ";
     "zzzzzz", "31.12.9999", "zzzzz";
     ;
     NULL, Recibos;
|FIN;

|DEFBUCLE Recibos2;
     #30#2;
     ;
     "01.01.0000", 000000001;
     "31.12.9999", 999999999;
     ;
     NULL, Recibos;
|FIN;

|DEFBUCLE Recibos12;
     #30#12;
     ;
     "     ", 000001, 000, "01.01.0000", "            ", "      ";
     "zzzzz", 999999, 999, "31.12.9999", "zzzzzzzzzzzz", "zzzzzz";
     ;
     NULL, Recibos;
|FIN;

|PROCESO ElRecibo;
     |ABRE #30;
     |SELECT #12#30;
     #30#0 = #0#8;
     #30#1 = #0#0;
     #30#2 = #0#1;
     #30#4 = "01.01.0000";
     #30#5 = "";
     #30#32 = "";
     |LEE 000#30];
     |PARA; |SI FSalida = 0; |Y #30#0 = #0#8; |Y #30#1 = #0#0; |Y #30#2 = #0#1; |HACIENDO;
        |SI #30#69 = " ";
           |SAL;
        |SINO;
           |LEE 000#30s;
        |FINSI;
     |SIGUE;

     |SI FSalida = 0; |Y #30#0 = #0#8; |Y #30#1 = #0#0; |Y #30#2 = #0#1;
           aHayReci = "S";
           |SI #30#14 = "C"; |O #30#14 = "P";
               |HAZ LlenaReci;
           |SINO;
               aEstaCobrado = "S";
           |FINSI;
     |FINSI;
     |CIERRA #30;
|FPRC;

|PROCESO dscam045;
     |SI nEmpCarte ! #45#6;
          nEmpCarte = #45#6;

          aEmp = ("00000" + #45#6) % -105;
          aAlfa = aBase + "dscarter/fich/" + aEmp + "/";
          |PATH_DAT #30#aAlfa#;
          |SI salmenu = 1; |BUCLE Recibos12; |FINSI;
          |SI salmenu = 2; |BUCLE Recibos2; |FINSI;
          |SI salmenu = 4; |BUCLE Recibos11; |FINSI;

          |SI salmenu = 3; |HAZ ElRecibo; |ERROR10; |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE dscam045;
     #45#1;
     ;
     nEmpresa, 001;
     nEmpresa, 999;
     ;
     NULL, NULL, NULL, NULL, NULL, dscam045;
     #45#6;
|FIN;

|PROCESO tmp117;
     |DDEFECTO #3;
     #3#0 = #0#4;        ||importe
     #3#1 = #0#14;       ||Importe Pendiente
     #3#2 = #0#14;       ||Importe A cobrar
     #3#3 = #0#12;
     #3#5 = "N";
     #3#7 = "01.01.0000"; ||#0#5;        ||Fecha de Cobro
     #3#6 = #0#7;
     #3#9 = #0#15;       ||Tipo de Recibo
     #3#8 = #0#9;
     #3#10 = #0#16;
     #3#11 = #0#17;
     #3#12 = #0#21;
     #3#13 = #0#22;
     |PUSHV 0501 2380;
     |BLANCO 0745 2280;
     |ATRI R;
     |PINDA #0#0;
     |ATRI 0;
     |PINPA #0#3;
     |PINDA #0#3;
     |ENDATOS #1#3;
     |SI FSalida = 0;
          #0#5 = #3#7;
          #0#7 = #3#6;
          #0#9 = #3#8;
          #0#10 = #3#5;
          #0#11 = #3#2;
          #0#12 = #3#3;
          #0#15 = #3#9;       ||Tipo de recibo
          #0#16 = #3#10;
          #0#17 = #3#11;
          #0#21 = #3#12;
          #0#22 = #3#13;
     |FINSI;
     |POPV;
     |PINDA #0#0;
|FPRC;

|PROCESO consulta;
     |SI pro = "M"; |ACABA; |FINSI;
     |SI FSalida = 0;    ||SE ha pulsado Return
          |LEE 101#0=;        ||Leo el registro actual corriente bloqueando
          |HAZ tmp117;
          |GRABA 021#0a;
          |LIBERA #0;
     |FINSI;
|FPRC;

|PROCESO Factura;
     |PUSHV 0501 2380;
     |ABRE #134;
     #134#49 = #0#8;  ||serie factura
     #134#0 = #0#0;
     |LEE 000#134=;
     |SI FSalida ! 0;
          |MENAV "0000No se ha encontrado la Factura";
     |SINO;
          |PINPA #0#134;
          |PINDA #0#134;
          |PAUSA;
     |FINSI;
     |CIERRA #134;
     |POPV;
|FPRC;

|PROCESO Recibo;
     aAlfa = aBase + "dscarter/fich/" + #0#18 + "/";
     |PATH_DAT #30#aAlfa#;
     aAlfa = aBase + "dscarter/pan/";
     |PATH_PAN #30#aAlfa#;

     |PUSHV 0501 2380;
     |ABRE #30;
     #30#40 = #0#19;  ||serie factura
     |LEE 000#30=;
     |SI FSalida ! 0;
          |MENAV "0000No se ha encontrado el recibo";
     |SINO;
          |PINPA #0#30;
          |PINDA #0#30;
          |PAUSA;
     |FINSI;
     |CIERRA #30;
     |POPV;
|FPRC;

|PROCESO reci;
     |SI pro = "A"; |ACABA; |FINSI;

     |DFICE aAlfa;
     aAlfa = aAlfa % -205;
     nEmpresa = aAlfa;
     nEmpCarte = 0;

     aHayReci = "N";
     aEstaCobrado = "N";
     |BUCLE dscam045;

     |SI aHayReci = "N";
          |MENAV "0000No existe el recibo...";
          |ERROR;
     |SINO;
          |SI aEstaCobrado = "S";
               |MENAV "0000El recibo esta cobrado...";
               |ERROR;
          |SINO;
               lin = lin + 1;
               |PINDA #0#0;
               |HAZ tmp117;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO entra;|TIPO 11;
     |SI FSalida = 10;
          |HAZ Factura;
     |FINSI;
     |SI FSalida = 11;
          |HAZ Recibo;
     |FINSI;
|FPRC;

|PROCESO autom;
     |DFICE aAlfa;
     aAlfa = aAlfa % -205;
     nEmpresa = aAlfa;
     nEmpCarte = 0;

     |ABRE #0;
     |BUCLE dscam045;
     |CIERRA #0;

     |BLIN 4;
     |SI lin = 0;
          |MENAV "0000NO hay recibos Pendientes de COBRO";
          |ACABA;
     |FINSI;

     |ENTLINEAL #1#0 , -1 , 4 , 22 , 0 , pro1 , pro2;
|FPRC;

|PROCESO pro1;
     #0#6 = 1;
|FPRC;

|PROCESO pro2;
     #0#6 = 9999;
|FPRC;

|PROCESO manual;
     |ENTLINEAL #1#0 , -1 , 1 , 22 , 0 , pro1 , pro2;
|FPRC;

|PROCESO actual;
     |DDEFECTO #324;
     #324#0 = #0#20;
     |LEE 000#324=;
     nCambio = #324#2;

     aAlfa = aBase + "dscarter/fich/" + #0#18 + "/";
     |PATH_DAT #30#aAlfa#;
     |PATH_DAT #40#aAlfa#;
     |ABRE #30;
     |ABRE #40;
     #30#40 = #0#19;
     |LEE 121#30=;
     |SI FSalida = 0;
          #40#17 = #0#19;
          #40#3 = 99;
          |LEE 000#40];
          |SI FSalida = 0;
               |LEE 000#40a;
          |SINO;
               |LEE 0000#40u;
          |FINSI;
          |SI FSalida = 0; |Y #40#17 = #0#19;
               nCobro = #40#3 + 1;
          |SINO;
               nCobro = 1;
          |FINSI;
          |DDEFECTO #40;
          #40#17 = #0#19;
          #40#3 = nCobro;
          #40#0 = #0#8;
          #40#1 = #0#0;
          #40#2 = #0#1;
          #40#4 = #0#9;
          #40#5 = "COB"; || Operacion
          #40#6 = #0#5;

          #40#7 =  #0#11; || Liquidado
          #40#25 = #40#7 * nCambio;

          #40#8 = #0#11;     || Cobrado
          #40#26 = #40#8 * nCambio;

          #40#10 = #0#12;     || Gastos
          #40#28 = #40#10 * nCambio;

          #40#12 = #0#12;     || Gastos nego
          #40#29 = #40#12 * nCambio;

          #40#33 = nCambio;
          #40#36 = #0#16; ||***** Caja *******
          |GRABA 020#40n;

          #30#23 = #30#23 + #0#11;      || Liquidado
          #30#61 = #30#23 * nCambio;

          #30#24 = #30#24 + #0#12;      || Gastos
          #30#62 = #30#23 * nCambio;

          #30#31 = #30#31 - #0#11;      || Pendiente
          #30#65 = #30#31 * nCambio;

          |SI #30#31 [ 0;
               #30#14 = "L";
               #30#15 = "Recibo Liquidado";
          |FINSI;
          #30#26 = #0#5;
          |GRABA 020#30a;
     |FINSI;
     |CIERRA #30;
     |CIERRA #40;

     |SI #0#16 > 0;
          |DFICE aAlfa;
          aAlfa = aAlfa % -205;
          |ABRE #509;
          #509#63 = aAlfa;
          #509#0 = #0#16;
          #509#1 = #0#17;
          |LEE 101#509=;
          |SI FSalida = 0;
               |SI #0#21 = "E";    || Efectivo
                    #509#52 = #509#52 + #0#11 + #0#12;
                    #509#55 = #509#55 + #0#11 + #0#12;
                    #509#4 = #509#4 + #0#11 + #0#12;
               |SINO;              || Tarjeta
                    #509#53 = #509#53 + #0#11 + #0#12;
                    #509#4 = #509#4 + #0#11 + #0#12;
                    |DDEFECTO #515;
                    |ABRE #515;
                    #515#0 = #0#22;
                    |LEE 101#515=;
                    |SI FSalida ! 0; |GRABA 020#515b; |FINSI;
                    #515#2 = #515#2 + #0#11 + #0#12;
                    #515#3 = #0#5;
                    #515#4 = #0#11 + #0#12;
                    |GRABA 020#515a;
                    |CIERRA #515;
                    |ABRE #516;
                    #516#0 = #0#22;
                    #516#1 = #0#5;
                    |LEE 101#516=;
                    |SI FSalida ! 0; |GRABA 020#516b; |FINSI;
                    #516#2 = #516#2 + #0#11 + #0#12;
                    |GRABA 020#516a;
                    |CIERRA #516;
               |FINSI;
               |GRABA 020#509a;
          |FINSI;
          |CIERRA #509;

          #22#0 = #0#8;
          #22#1 = #0#0;
          #22#2 = #0#1;
          #22#3 = 999;
          |LEE 000#22];
          |SI FSalida = 0;
               |LEE 000#22a;
          |SINO;
               |LEE 000#22u;
          |FINSI;
          |SI FSalida = 0; |Y #22#0 = #0#8; |Y #22#1 = #0#0; |Y #22#2 = #0#1;
               nLinCob = #22#3 + 1;
          |SINO;
               nLinCob = 1;
          |FINSI;
          |DDEFECTO #22;
          #22#0 = #0#8;
          #22#1 = #0#0;
          #22#2 = #0#1;
          #22#3 = nLinCob;
          #22#4 = #0#2;
          #22#6 = #0#5;
          #22#7 = #0#4;
          #22#8 = #0#11 + #0#12;
          #22#9 = #0#11;
          #22#11 = #0#12;
          #22#16 = #0#13;
          #22#18 = #0#15;
          #22#20 = #0#16;
          #22#21 = #0#17;
          #22#22 = #0#21;
          #22#23 = #0#22;
          |GRABA 020#22n;
     |FINSI;
|FPRC;

|DEFBUCLE actual;
     #0#2;
     ;
     "            " , "S";
     "zzzzzzzzzzzz" , "S";
     ;
     NULL , actual;
|FIN;

|PROCESO actu;
     |ABRE #22;
     |ABRE #324;
     |BUCLE actual;
     |CIERRA #324;
     |CIERRA #22;
|FPRC;

|PROGRAMA;
     |HAZ DecimalesBase;

     |DBASS aBase;
     aAlfa = aBase + "dscarter/fich/";
     |PATH_DAT #45 aAlfa;

     |CLS;
     |CABEZA "Control Cobros";
     |PINPA #0#0;
     |ENTLINEAL #1#0 , -1 , 7 , 22 , 1 , pro1 , pro2;
     nombre = Usuario;
     |NOME_DAT #0 nombre;
     |ABRE #0; |CIERRA #0; |DELINDEX #0;
     |ABRE #6; |LEE 000#6p; |CIERRA #6;
     |MENU menu;
     salmenu = FSalida;
     |SI salmenu = 1; |O salmenu = 2; |O salmenu = 4;
          pro = "A";
          |MENSA "0000Pulse ESPACIO para detener el Proceso";
          |HAZ autom;
     |FINSI;
     |SI salmenu = 3;
          pro = "M";
          |HAZ manual;
     |FINSI;
     |SI salmenu ] 1; |Y salmenu [ 4; |Y lin > 0;
          |CONFI "2400SSon Correctos los valores Entrados : ";
          |SI FSalida = 0;
               |HAZ actu;
          |FINSI;
     |FINSI;
     |DELINDEX #0;
|FPRO;
