|| En este fichero se montaran los parametros para trabajar con los marcos

|FICHEROS;
     foto0006 #0;
     agifa019 #1;   || Articulos
|FIN;

|VARIABLES;
     aCodigo        = "";
     aCodArt        = "";

     sal            = 0;
     i              = 0;
     nPos           = 0;
     nCampo         = 0;
     nCol           = 0;
     nPosIni        = 0;
     nTecla         = 0;
     nuCampo        = 0;
     nPosCon        = 0;
     nlargo         = 0;
     nancho         = 0;
     n              = 0;
     sw_tamano      = 0;
     lonta          = 0;
     car            ="";
     ancho          ="";
     largo          ="";
{10}tamano    = ""; || tabla con los tama¤os que hay de fotos por si los cambian
    T=0;
|FIN;

|PROCESO ExisteArti;|TIPO 0
     || Los tipos 14 que hay para pintar me pasan directamente por estos
     || campos aunque en realidad no esten en el campo en concreto.
     |SI Campo = 77; |O Campo = 78; |O Campo = 82; |O Campo = 83;
     |O  Campo = 87; |O Campo = 88; |O Campo = 92; |O Campo = 93;
     |O  Campo = 97; |O Campo = 98; |O Campo =102; |O Campo =103;
     |O  Campo =107; |O Campo =108; |O Campo =112; |O Campo =113;
     |O  Campo =117; |O Campo =118; |O Campo =122; |O Campo =123;
          |ABRET #agifa019;
          #agifa019#0 = #foto0006#Campo#;
          |LEE 001#agifa019.=;
          |SI FSalida ! 0;
              |MENAV "0000ATENCION!!! El articulo no existe";
              |ERROR;
          |FINSI;
          |CIERRAT #agifa019;
     |FINSI;
|FPRC;

|PROGRAMA;
     |PINPA #0#foto0006;
     |ABRE #foto0006;
     #foto0006#0="A";
     |LEE 101#foto0006.=;
     sal=FSalida;
     |PINDA #0#0;
     |ENDATOS #1#0;
     |SI FSalida=0;
          |SI sal=0;
               |GRABA 001#foto0006.a;
          |SINO;
               |GRABA 001#foto0006.n;
          |FINSI;
     |FINSI;
     |LIBERA #foto0006;
     |CIERRA #foto0006;

|FPRO;

/*    || Esther he quitado el proceso, ahora el iva es opcional
|| Comprueba que los articulos aqui informados tengan el I.V.A. incluido.
|| De cara a los calculos que existen en la creacion del tiquet
|PROCESO IvaIncluido;
     |SI #agifa019#130 ! "S";
          |MENAV "0000ATENCION!!!Estos articulos deben tener el I.V.A incluido";
          |ERROR;
     |FINSI;
|FPRC;
*/

|| Pinta los tama¤os definidos en la pantalla de RA-4 1
|PROCESO PintaTam1;|TIPO 14;
     |ATRI R;
     nCol    = 0;
     nPosIni = 1027;

     |PARA i = 1;|SI i [10;|HACIENDO i = i + 1;
          nPos   = nPosIni + (nCol * 10);
          nCampo =  i + 32;
          |PINTA nPos,#foto0006#nCampo#;
          nCol = nCol + 1;
          |SI nCol > 4;
               nCol    = 0;
               nPosIni = 1427;
          |FINSI;
     |SIGUE;
     |ATRI r;
|FPRC;

|| Pinta los tama¤os definidos en la pantalla de RA-4 2 y calcula consumos
|PROCESO PintaTam3;|TIPO 14;

     nPosCon = 74; || posici¢n de campo consumo positivos - 5, por que luego se
                   || suma 5 nada m s entrar

     |PARA i = 1;|SI i [10;|HACIENDO i = i + 1;
          |SI Campo = 131;
               nPos   = ((i + 11) * 100 ) + 6;
          |SINO;
               nPos   = ((i + 12) * 100 ) + 15;
          |FINSI;
          nCampo =  i + 32;
          |ATRI R;
          |PINTA nPos,#foto0006#nCampo#;
          |ATRI r;
          || Esther 22/07/98
          || Se consigue del campo tama¤o alfanum‚rico leer el ancho y el alto
          || para calcular el consumo en m2.
          nlargo=0;
          nancho=0;
          largo="";
          ancho="";
          car="";
          sw_tamano=0; ||utilizado para saber cuando se cambia de ancho a alto
          |PARA lonta=1; |SI lonta [ 5; |HACIENDO lonta = lonta + 1;
               n=(lonta*100)+1;
               car=#foto0006#nCampo# %n;
               |SI car ! "x"; |Y car ! "X"
                    |SI sw_tamano = 0;
                         ancho=ancho+car;
                         nancho=ancho;
                    |SINO;
                         largo=largo+car;
                         nlargo=largo;
                    |FINSI;
               |SINO;
                    sw_tamano=1;
               |FINSI;
          |SIGUE;
          || Pasar el tama¤o de cm a m2
          nancho=nancho/100;  ||pasar a m
          nlargo=nlargo/100;  ||pasar a m
          nPosCon=nPosCon+5;

          || Si el consumo es 0 suponemos que es la 1§ vez que se entra y
          || calcula consumo. Si el tama¤o se ha cambiado tambi‚n se recalcula
          || el consumo, pero sino es ninguna de estas dos cosas deja el valor
          || introducido por el usuario.
          Itamano=tamano1<-;
          Itamano=Itamano+i-1;

          |SI #foto0006#nCampo# ! tamano; |O #foto0006#nPosCon# = 0;
               #foto0006#nPosCon#=nancho*nlargo;
          |FINSI;
          |PINTA #foto0006#nPosCon#;
     |SIGUE;

|FPRC;


|PROCESO ConsultaArt; |TIPO 6;
     aCodArt = Contenido;
     nTecla = FSalida;
     nCampo = Campo;
     |ABRE #agifa019;
     #agifa019#0 = #foto0006#nCampo#;
     |LEE 001#agifa019.=;
     |SI FSalida = 0;
          |SI nTecla = 18;   ||Se ha Pulsado F10
               |CONSULTA_CLAVE #2#agifa019;  ||Si existe se consulta
               |SI FSalida ] 0;
                    #foto0006#nCampo# = #agifa019#0;
                    |PINTA #foto0006#nCampo#;
               |FINSI;
          |FINSI;
     |SINO;
          |HAZ EntradaArt;
     |FINSI;
     |CIERRA #agifa019;
|FPRC;

|PROCESO EntradaArt;
     |PUSHV 0501 2380;
     |DDEFECTO #agifa019;
     #agifa019#0 = #foto0006#nCampo#;
     |PINPA #0#agifa019;
     |PINDA #0#agifa019;
     |CONFI "2400SConfirma Alta (S/N) ..: ";
     |SI FSalida = 0;
          |GRABA 021#agifa019.b;
          |ENDATOS #1#agifa019;
          |SI FSalida = 0;
               |GRABA 021#agifa019.a;
          |SINO;
               |BORRA 021#agifa019.c;
               #foto0006#nCampo# = aCodArt;
               |ERROR;
          |FINSI;
          |LIBERA #agifa019;
     |SINO;
          #foto0006#nCampo# = aCodArt;
          |ERROR;
     |FINSI;
     |POPV;
     |PINTA #foto0006#nCampo#;
|FPRC;

|PROCESO Tomardato;
                 ||Si es un cambio o alta toma los datos
     aCodArt = Contenido;           || por defecto del articulo.
     nTecla = FSalida;
     nCampo = Campo;
     |SI aCodArt ! #foto0006#nCampo#;
          |SI nCampo = 1; || #foto0006#1 Cod. Revelado.
               #foto0006#4   = #agifa019#18;
               #foto0006#197 = #agifa019#19;
               #foto0006#198 = #agifa019#20;
               #foto0006#199 = #agifa019#130;
               #foto0006#202 = #agifa019#131;
               #foto0006#203 = #agifa019#132;
               |PINTA #foto0006#4;
               |PINTA #foto0006#197;
               |PINTA #foto0006#198;
               |PINTA #foto0006#199;
               |PINTA #foto0006#202;
               |PINTA #foto0006#203;
          |FINSI;
          |SI nCampo = 2; || #foto0006#2 cod. 1§ copias
               #foto0006#200 = #agifa019#130;
               |PINTA #foto0006#4;
          |FINSI;
          |SI nCampo = 3; || #foto0006#3 cod. 2¦ copias
               #foto0006#201 = #agifa019#130;
               |PINTA #foto0006#4;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO leeTamano; |TIPO 14;
     |SI FSalida = 5; |Y FSalida ! 0;
     || Con este proceso introducimos en una tabla los tama¤os del fichero
     || para luego compararlos con los del fichero por si se modifican volver
     || a calcular el consumo
     nuCampo=nCampo; || guardo el valor de nCampo para luego dejarlo como estaba por si acaso.
     nCampo=33;
     Itamano=tamano1<-;
     |PARA T=1; |SI T [ 10; |HACIENDO T=T+1;
             tamano=#foto0006#nCampo#;
             nCampo=nCampo+1;
             Itamano=Itamano+1;
     |SIGUE;
     nCampo=nuCampo;
     |FINSI;
|FPRC;
