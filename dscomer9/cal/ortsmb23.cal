     Importa Fab

|FICHEROS;
     ortsz013 #13,MANTE;
     ortsm017 #17;
     ortsm018 #18;
     ortsm024 #24;
|FIN;

|VARIABLES;
     aColor = "";
     &enError = 0;
     &eaRuta = "";
     {1}aLocal = "";
     aXls = "";
     aAlf1 = "";
     aAlf2 = "";
     aAlfa = "";
     aAlff = "";
     aAlfv = "";
     aAlfw = "";
     aAlfx = "";
     aAlfd = "";
     aAlfe = "";
     aAlfc = "";
     aAlfad = "";
     aAlfae = "";
     aAlfaf = "";
     aAlfag = "";
     aAlfah = "";

     aBase = "";
     aOrigen = "";
     aDestino = "";
     nReg = 0;
     nXls = 0;
     nHandle = 0;
     a0D = "";
     a0A = "";
     aCar = "";
     aLinea = "";
     aTmp = "";
     nCampo = "";
     {90}Valor = "";
     aReg = "";
     aLon = "";
     aDato = "";
     i = 0;
     aLetra = "";
     nPos = "";
     aExt = "";
|FIN;

|PROCESO Linea;
     nXls = nXls + 1;
     |SI nXls = 1; |ACABA; |FINSI;

     ||Optimizador A
     aAlfa = Valor1;

     ||Largo   C
     aAlfc = Valor3;

     ||Anchura  D
     aAlfd = Valor4;

     ||Color Pieza
     aAlfe = Valor5;

     ||Grosor F
     aAlff = Valor6;

     ||Serie V
     aAlfv = Valor22;

     ||Lote W
     aAlfw = Valor23;

     ||Lin Lote X
     aAlfx = Valor24;

     ||Tablero
     aAlf1 = Valor6;

     ||Cantidad
     aAlf2 = Valor7;


     ||Medidas AD
     aAlfad = Valor8;

     ||Chapado exterior AE
     aAlfae = Valor9;

     ||Observaciones AF
     aAlfaf = Valor10;

     ||Obs 1 AG
     aAlfag = Valor11;

     ||Grupo AH
     aAlfah = Valor20;

     |HAZ ImportaFabXlsLin;
|FPRC;

|PROCESO Caracter;
     |SI aCar = a0D; |O aCar = a0A;
          |SI aCar = a0D;
               Valor = aReg;
               |HAZ Linea;
               IValor = Valor1<-;
               aReg = "";
          |FINSI;
     |FINSI;
     |SI aCar ! a0D; |Y aCar ! a0A;
          |SI aCar = ";";
               Valor = aReg;
               IValor = IValor + 1;
               aReg = "";
          |SINO;
               aReg = aReg + aCar;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO ImportaFabCSV;
     aReg = "";
     IValor = Valor1<-;
     nXls = 0;
     aLinea = "";
     aOrigen = #13#1; |QBF aOrigen;
     aExt = aOrigen % -104; aExt = aExt < "";
     |SI aExt ! ".csv";
          aOrigen = aOrigen + ".csv";
     |FINSI;

     |DFICE aDestino;
     aDestino = aDestino + Usuario;
     |RECIBE_FICHERO aOrigen, aDestino;
     |SI FSalida ! 0;
          |MENAV "0000Error al enviar fichero al servidor...";
     |SINO;
          |XABRE "B", aDestino, nHandle;
          |SI FSalida ] 0;
               |XLEE nHandle, 250, aTmp;
               |PARA; |SI FSalida > 0; |HACIENDO;
                    |PARA; |SI aTmp ! ""; |HACIENDO;
                         aCar = aTmp % 101;
                         |SI aTmp = aCar;
                              aTmp = ""; ||con un caracter falla: "aTmp = aTmp % 2"
                         |SINO;
                              aTmp = aTmp % 2;
                         |FINSI;
                         |HAZ Caracter;
                    |SIGUE;
                    |XLEE nHandle, 250, aTmp;
               |SIGUE;
               |XCIERRA nHandle;
               |FBORRA aDestino;
          |SINO;
               |MENAV "0000Error al abrir el fichero...";
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO ImportaFabXlsLin;
     |SI aAlfa = "S"; |O aAlfa = "N"; |O aAlfa = "X";
     |SINO;
          aAlfa = "0000Linea " + nXls + " con valores incorrectos en el optimizador...";
          |MENAV aAlfa;
          |ACABA;
     |FINSI;
     |SI aAlfa = "S";
          |SI aAlfd = ""; |O aAlfc = "";
               aAlfa = "0000Linea " + nXls + " no tiene valores Anchura/Largo...";
               |MENAV aAlfa;
               |ACABA;
          |FINSI;
     |FINSI;

     #24#13 = aAlfv;
     #24#0 = aAlfw;
     #24#1 = aAlfx;
     |LEE 121#24=;
     |SI FSalida = 0;
          #24#8 = aAlfe;
          #24#9 = aAlfa;
          #24#12 = aAlfd;
          #24#11 = aAlfc;
          #24#14 = aAlf1;
          #24#15 = aAlf2;
          #24#16 = aAlfad;
          #24#17 = aAlfae;
          #24#18 = aAlfaf;
          #24#19 = aAlfag;
          #24#20 = aAlfah;

          aColor = "";
          |SI #24#6 = 0;
               #17#0 = #24#2;
               #17#1 = #24#3;
               #17#2 = #24#4;
               #17#3 = #24#5;
               |LEE 000#17=;
               |SI FSalida = 0;
                    aColor = #17#6;
               |FINSI;
          |SINO;
               #18#0 = #24#2;
               #18#1 = #24#3;
               #18#2 = #24#4;
               #18#3 = #24#5;
               #18#4 = #24#6;
               |LEE 000#18=;
               |SI FSalida = 0;
                    aColor = #18#5;
               |FINSI;
          |FINSI;
          |SI aColor ! #24#8;
               aAlfa = "0000La linea del lote " + #24#1 + " revisar combinaciones pedidos.";
               |MENAV aAlfa;
          |FINSI;

          |GRABA 020#24a;
          |LIBERA #24;
     |FINSI;
|FPRC;

|PROCESO ImportaFabXls;
     aXls = #13#1; |QBF aXls;
     aAlfa = aXls % -104;
     |SI aAlfa ! ".xls"; aXls = aXls + ".xls"; |FINSI;

     |XABRE "CE", aXls, 0;
     |SI FSalida < 0
          |MENAV "0000No puedo abrir el fichero a importar";
     |SINO;
          |CARGADLL "agixl97.dll";
          |SI FSalida < 0;
               |MENAV "0000No se puede usar agixl97.dll";
          |SINO;
               |ALFACALLDLL "agixl97.dll", "carga_book", aXls;
               |ALFACALLDLL "agixl97.dll", "selecciona_hoja", "Hoja1";
               |SI FSalida = 0;
                    |PARA nXls = 2; |SI nXls < 65536; |HACIENDO nXls = nXls + 1;
                         aAlfa = "Importando:" + nXls;
                         |PINTA 1903, aAlfa;

                         ||Optimizador
                         aAlfa = "A" + nXls; |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfa;
                         |SI FSalida ! 0; |SAL; |FINSI;

                         ||Largo
                         aAlfc = "C" + nXls; |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfc;
                         |SI FSalida ! 0; |SAL; |FINSI;

                         ||Anchura
                         aAlfd = "D" + nXls; |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfd;
                         |SI FSalida ! 0; |SAL; |FINSI;

                         ||Color Pieza
                         aAlfe = "E" + nXls; |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfe;
                         |SI FSalida ! 0; |SAL; |FINSI;

                         ||Grosor
                         aAlff = "F" + nXls; |ALFACALLDLL "agixl97.dll", "peek_dato", aAlff;
                         |SI FSalida ! 0; |SAL; |FINSI;

                         ||Serie
                         aAlfv = "V" + nXls; |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfv;
                         |SI FSalida ! 0; |SAL; |FINSI;

                         ||Lote
                         aAlfw = "W" + nXls; |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfw;
                         |SI FSalida ! 0; |SAL; |FINSI;

                         ||Lin Lote
                         aAlfx = "X" + nXls; |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfx;
                         |SI FSalida ! 0; |SAL; |FINSI;


                         ||Tablero
                         aAlf1 = "f" + nXls; |ALFACALLDLL "agixl97.dll", "peek_dato", aAlf1;
                         |SI FSalida ! 0; |SAL; |FINSI;


                         ||Cantidad
                         aAlf2 = "g" + nXls; |ALFACALLDLL "agixl97.dll", "peek_dato", aAlf2;
                         |SI FSalida ! 0; |SAL; |FINSI;

                         ||Medidas
                         aAlfad = "H" + nXls; |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfad;
                         |SI FSalida ! 0; |SAL; |FINSI;

                         ||Chapado exterior
                         aAlfae = "I" + nXls; |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfae;
                         |SI FSalida ! 0; |SAL; |FINSI;

                         ||Observaciones
                         aAlfaf = "J" + nXls; |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfaf;
                         |SI FSalida ! 0; |SAL; |FINSI;

                         ||Obs 1
                         aAlfag = "K" + nXls; |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfag;
                         |SI FSalida ! 0; |SAL; |FINSI;

                         ||Grupo
                         aAlfah = "T" + nXls; |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfah;
                         |SI FSalida ! 0; |SAL; |FINSI;

                         |QBT aAlfx;
                         |SI aAlfx = ""; |SAL; |FINSI;

                         |HAZ ImportaFabXlsLin;
                    |SIGUE;
                    ||||ALFACALLDLL "agixl97.dll", "fin_book", "salva";
                    |ALFACALLDLL "agixl97.dll", "fin_book", "";
               |FINSI;
          |FINSI;
          |DESCARGADLL "agixl97.dll";
     |FINSI;
|FPRC;

|PROCESO Fichero13; |TIPO 0;
     |SI FSalida = 13;
          |HAZ DsSelect;
          |SI enError = 0;
               #13Campo = eaRuta; |PINTA #13Campo;
          |FINSI;
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO ImportaFab;
     a0D = &13;
     a0A = &10;
     |ABRE #13;
     |LEE 101#13=;
     |SI FSalida ! 0; |GRABA 020#13b; |FINSI;
     |PINPA #0#13;
     |PINDA #0#13;
     |ENDATOS #1#13;
     |SI FSalida = 0;
          |GRABA 020#13a;
          |ABRE #24;
          |ABRE #17;
          |ABRE #18;
          |SI #13#2 = "X";
               |HAZ ImportaFabXls;
          |SINO;
               |HAZ ImportaFabCSV;
          |FINSI;
          |CIERRA #18;
          |CIERRA #17;
          |CIERRA #24;
          |LIBERA #13;
     |FINSI;
     |CIERRA #13;
|FPRC;
