|FICHEROS;
     agist006 #0;
     agist002 #1;      || entrada Stock lins.
     agist001 #2;      || entrada Stock cabs.

     dsm00044 #11;     || Regularicacion lins.
     dsm00043 #22;     || Regularizacion Cabs.

     agifa017 #5;      || articulo-almacen
     agifa019 #7;      || articulos
     agifa142 #42;     || parametros
     agifa065 #65;     || historico

     dsm00004 #904;
     dsm00005 #905;
     dsm00006 #906;
     dsm00016 #916;
     dsm00010 #910;
|FIN;

|VARIABLES;
     aAlfa1 = "";
     nCodigo = 0;
     bloque  = "";
     sw      = 0;
     est_lin = 0;
     linea   = 0;
     swno    = 0;
     alfa1 = "";
     nume1 = 0;
     nStockHisto = 0;
     nConta    = 0;
     nHay   = 0;
|FIN;

|INCLUYE i_varart;

|PROCESO Defecto; |TIPO 0;
     |SI FSalida = 2; |ACABA; |FINSI;

     |ABRE #2;
     #2#2 = #0#11;
     #2#0 = #0#12;
     |LEE 000#2=;
     |SI FSalida = 0;
          |C_M #0#6, 1, "S";
          |C_M #0#8, 1, "N";
          |C_M #0#9, 1, "N";
          #0#8 = #2#3; |PINTA #0#8;
          #0#9 = #2#1; |PINTA #0#9;
          |SI #2#4 = "S";
               |MENAV "0000Registro Actualizado...";
               |ERROR;
          |FINSI;
     |SINO;
          |C_M #0#6, 1, "N";
          |C_M #0#8, 1, "S";
          |C_M #0#9, 1, "S";
          #0#8 = Usuario % 810; |PINTA #0#8;
     |FINSI;
     |CIERRA #2;
|FPRC;

|PROCESO StockSi; |TIPO 7;
     |SI #0#4 = "S";
          |C_M #0#10, 1, "S";
     |SINO;
          |C_M #0#10, 1, "N";
     |FINSI;
|FPRC;

|| **************************************************************************

|PROCESO agifa065_1;
     nStockHisto = nStockHisto + #65#6;
|FPRC;

|DEFBUCLE agifa065;
     #65#1;
     5;
     #5#0, "01.01.1900", "  ",      0,     0, "     ", #5#1;
     #5#0, "31.12.2999", "zz", 999999, 99999, "zzzzz", #5#1;
     ;
     NULL, agifa065_1;
|FIN;

|PROCESO dsm00006;
     nStockHisto = nStockHisto + #906#9;
|FPRC;

|DEFBUCLE dsm00006;
     #906#1;
     ;
     #5#0, #5#1, "01.01.0001", "  ", "     ",      0,     0, #905#4;
     #5#0, #5#1, "31.12.9999", "zz", "zzzzz", 999999, 99999, #905#4;
     ;
     NULL, dsm00006;
|FIN;

|PROCESO GrabaCompo;
     |DDEFECTO #905;
     |SI #0#13 ! "R";
          #905#0 = "T2";
          #905#1 = #1#13;
          #905#2 = #1#0;
          #905#3 = #1#1;
          #905#4 = #904#2;
          #905#5 = #904#3;
          |SI #0#4 = "S";
               |SI #0#10 = "A";
                    #905#6 = #904#12;             || stock actual
               |SINO;
                    nStockHisto = 0;
                    |BUCLE dsm00006;
                    #905#6 = #904#12 - nStockHisto;
               |FINSI;
          |FINSI;
     |SINO;
          #905#0 = "T2";
          #905#1 = #11#21;
          #905#2 = #11#18;
          #905#3 = #11#8;
          #905#4 = #904#2;
          #905#5 = #904#3;
          |SI #0#4 = "S";
               |SI #0#10 = "A";
                    #905#6 = #904#12;             || stock actual
               |SINO;
                    nStockHisto = 0;
                    |BUCLE dsm00006;
                    #905#6 = #904#12 - nStockHisto;
               |FINSI;
          |FINSI;
     |FINSI;

     |SI #7#176 = "S"; |Y #910#44 = "T";
          aAlfa1  = #7#177;  |QBF aAlfa1;
          aAlfa1  = aAlfa1 + ((" " * 20) % 120);
          #916#0 = aAlfa1;
          #916#1 = "";
          #916#2 = "";
          #916#3 = #905#4;
          #916#15 = "";
          |LEE 000#916=;
          |SI FSalida ! 0; |ACABA; |FINSI;
     |FINSI;

     |GRABA 020#905n;
|FPRC;

|DEFBUCLE dsm00004;
     #904#1;
     ;
     #5#0, #5#1, "      ";
     #5#0, #5#1, "zzzzzz";
     be;
     NULL, GrabaCompo;
|FIN;

|PROCESO versiexiste;
     |SELECT #2#1;
     #1#13 = #0#11;
     #1#0 = #0#12;
     #1#2 = #5#0;
     #1#3 = #5#1;
     |LEE 000#1=;
     |SI FSalida = 0;
          swno = 1;
     |FINSI;
     |SELECT #1#1;
|FPRC;

|PROCESO versiexiste2;
     |SELECT #2#11;
     #11#21 = #0#11;
     #11#18 = #0#12;
     #11#0  = #5#0;
     #11#1  = #5#1;
     |LEE 000#11=;
     |SI FSalida = 0; |Y #11#21 = #0#11; |Y #11#18 = #0#12; |Y #11#0 = #5#0; |Y #11#1 = #5#1;
          swno = 1;
     |FINSI;
     |SELECT #1#11;
|FPRC;

|PROCESO grabar;
     efFec = #0#9;
     enAlma = 0;
     |HAZ MiraHisT;
     |SI enErrHis ! 0;
          |ERROR;
          |ACABA;
     |FINSI;

     || Seleccion de las Operaciones
     |SI #0#13 ! "R";
          swno = 0;
          |HAZ versiexiste;
          |SI swno = 0;    || Si no existe incorporar una linea
               linea = linea + 1;
               #1#1 = linea;
          |SINO;
               |SI #0#6 = "S";
                   |ACABA;   || No lo actualiza
               |FINSI;
               || ...... #1#1 la linea que se ha leido
          |FINSI;

          |PINTA 2224, #5#0;     || ...  Articulo
          |PINTA 2242, #5#1;     || ...  Almacen

          #7#0 = #5#0;
          |LEE 000#7=;
          |SI FSalida ! 0;
               #7#1 = #5#40;
               #7#9 = 0;
          |FINSI;

          #1#13 = #0#11;
          #1#0 = #0#12;
          #1#2 = #5#0;
          #1#3 = #5#1;
          #1#4 = 0;
          |SI #0#4 = "S";
               |SI #0#10 = "A";
                    #1#4 = #5#5;             || stock actual
               |SINO;
                    nStockHisto = 0;
                    |BUCLE agifa065;
                    #1#4 = #5#5 - nStockHisto;
               |FINSI;
          |FINSI;
          #1#5 = 0;
          |SI #0#5 = "S";
              #1#5 = #7#9;
          |FINSI;
          #1#6 = #7#1;                                    || Descripcion
          |SALVA_DATOS #1;

          |LEE 101#1=;
          est_lin = FSalida;
          |REPON_DATOS #1;
          |SI est_lin = 0; |GRABA 020#1a; |FINSI;
          |SI est_lin ! 0; |GRABA 020#1n; |FINSI;
          |LIBERA #1;

          |ABRE #905;
          |ABRE #916;
          |BUCLE dsm00004;
          |CIERRA #916;
          |CIERRA #905;

          nHay = 1;
     |SINO;
          swno = 0;
          |HAZ versiexiste2;
          |SI swno = 0;    || Si no existe incorporar una linea
               linea = linea + 1;
               #11#8 = linea;
          |SINO;
               |SI #0#6 = "S";
                   |ACABA;   || No lo actualiza
               |FINSI;
               || ...... #1#1 la linea que se ha leido
          |FINSI;

          |PINTA 2224, #5#0;     || ...  Articulo
          |PINTA 2242, #5#1;     || ...  Almacen

          #7#0 = #5#0;
          |LEE 000#7=;
          |SI FSalida ! 0;
               #7#1 = #5#40;
               #7#9 = 0;
          |FINSI;

          #11#21 = #0#11;
          #11#18 = #0#12;
          #11#0  = #5#0;
          #11#1  = #5#1;
          #11#4  = 0;
          #11#5  = #0#9;

          |SI #0#4 = "S";
               |SI #0#10 = "A";
                    #11#4 = #5#5;             || stock actual
               |SINO;
                    nStockHisto = 0;
                    |BUCLE agifa065;
                    #11#4 = #5#5 - nStockHisto;
               |FINSI;
          |FINSI;

          |SI #11#4 > 0;
               #11#7 = "S";
               ||#11#4 = #11#4 * -1;
               #11#4 = #11#4;
          |SINO;
               #11#7 = "E";
               #11#4 = #11#4 * -1;
          |FINSI;


          #1#5 = 0;
          #11#2 = #7#1;                                  || Descripcion

          |SALVA_DATOS #11;

          |LEE 101#11=;
          est_lin = FSalida;
          |REPON_DATOS #11;
          |SI est_lin = 0; |GRABA 020#11a; |FINSI;
          |SI est_lin ! 0; |GRABA 020#11n; |FINSI;
          |LIBERA #11;

          enForma = 1;

          |HAZ AcumulaES_SS;

          |ABRE #905;
          |BUCLE dsm00004;
          |CIERRA #905;

          nHay = 1;
     |FINSI;
|FPRC;

|DEFBUCLE pasa;
     #5#1;
     ;
     #0#2, #0#0;
     #0#3, #0#1;
     ;
     NULL, grabar;
|FIN;

|| ************************************************************************
|PROCESO buscalin;
     linea = #1#1;
|FPRC;

|DEFBUCLE buscalin;
     #2#1;
     ;
     #0#11, #0#12;
     #0#11, #0#12;
     ;
     NULL, NULL, buscalin;
|FIN;

|| ************************************************************************

|PROCESO BorraCompo;
     |BORRA 020#905a;
     |LIBERA #905;
|FPRC;

|DEFBUCLE dsm00005;
     #905#1;
     ;
     "T2", #1#13, #1#0, #1#1, "      ";
     "T2", #1#13, #1#0, #1#1, "zzzzzz";
     be;
     NULL, BorraCompo;
|FIN;

|PROCESO borraCab;
     |BORRA 020#2a;
|FPRC;

|PROCESO borraLin;
     |BORRA 020#1a;
     |BUCLE dsm00005;
|FPRC;

|DEFBUCLE borra;
     #2#1;
     ;
     #0#11, #0#12;
     #0#11, #0#12;
     be;
     NULL, borraCab, borraLin;
|FIN;
|| ***************************************************************************

|PROCESO cargar; |TIPO 3;
     |SI #0#7 = "NO"; |ACABA; |FINSI;

     |ABRE #910; |LEE 000#910p; |CIERRA #910;
     |HAZ DecimalesBase;

     |D_CUADRO 2109, 2353;
     |PINTA 2210 , " Procesando :                              ";

     |SI #0#13 = "R"; |HAZ Regularizacion; |ACABA; |FINSI;

     linea = 0;
     |SI #0#6 = "N"; |BUCLE borra; |FINSI;
     |SI #0#6 = "S"; |BUCLE buscalin;  |FINSI;

     |ABRE #2;
     |SI #0#12 = 0;
          #2#2 = #0#11;
          #2#0 = 999999;
          |LEE 000#2];
          |SI FSalida = 0;
               |LEE 000#2a;
          |SINO;
               |LEE 000#2u;
          |FINSI;
          |SI FSalida = 0; |Y #2#2 = #0#11;
               nConta = #2#0 + 1;
          |SINO;
               nConta = 1;
          |FINSI;
          |DDEFECTO #2;
          #2#0 = nConta;
          #2#1 = #0#9;
          #2#2 = #0#11;
          #2#3 = #0#8;
          |GRABA 020#2n;
          #0#12 = nConta;
     |SINO;
          #2#2 = #0#11;
          #2#0 = #0#12;
          |LEE 101#2=;
          |SI FSalida ! 0; |GRABA 020#2b; |FINSI;
          #2#1 = #0#9;
          #2#2 = #0#11;
          |GRABA 020#2a;
     |FINSI;
     |CIERRA #2;

     |ABRE #1;
     |ABRE #7;
     |BUCLE pasa;
     |CIERRA #7;
     |CIERRA #1;
     |SI nHay = 0; |Y #0#12 = 0;
          |ABRE #2;
          #2#2 = nConta;
          #2#0 = #0#12;
          |BORRA 020#2c;
          |CIERRA #2;
     |FINSI;
|FPRC;

|PROCESO Regularizacion;
     |ABRE #22;
     |SI #0#12 = 0;
          #22#4 = #0#11;
          #22#0 = 999999;
          |LEE 000#22];
          |SI FSalida = 0;
               |LEE 000#22a;
          |SINO;
               |LEE 000#22u;
          |FINSI;
          |SI FSalida = 0; |Y #22#4 = #0#11;
               nConta = #22#0 + 1;
          |SINO;
               nConta = 1;
          |FINSI;
          |DDEFECTO #22;
          #22#0 = nConta;
          #22#1 = #0#9;
          #22#4 = #0#11;
          |GRABA 020#22n;
          #0#12 = nConta;
     |SINO;
          #22#4 = #0#11;
          #22#0 = #0#12;
          |LEE 101#22=;
          |SI FSalida ! 0; |GRABA 020#22b; |FINSI;
          #22#1 = #0#9;
          #22#4 = #0#11;
          |GRABA 020#22a;
     |FINSI;
     |CIERRA #22;
     |ABRE #11;
     |ABRE #7;
     |BUCLE pasa;
     |CIERRA #7;
     |CIERRA #11;
     |SI nHay = 0; |Y #0#12 = 0;
          |ABRE #22;
          #22#4 = nConta;
          #22#0 = #0#12;
          |BORRA 020#22c;
          |CIERRA #22;
     |FINSI;
|FPRC;

|PROCESO Llena006; |TIPO 6;
     eaArticulo = #0Campo;
     |HAZ LlenaArticulo;
     |SI FSalida = 0;
          #0Campo = eaArticulo;
          |PINTA #0Campo;
     |SINO;
          |ERROR; |ACABA;
     |FINSI;
|FPRC;
