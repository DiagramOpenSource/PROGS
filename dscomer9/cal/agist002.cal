|FICHEROS;
     agist002 #0;   || Lineas Entrada Stocks Inicial
     agist001 #1;   || Cabeceras Entrada Stocks Inicial
     agifa019 #2;   || Articulos

     agifa142 #42;  || Parametros generales
     dsl00001 #6;   ||Codigos de barras multiples

     referen@ #100;

     dsm00005 #905;
     dsm00010 #910;
     dsm00014 #914;
     agifa017 #8;
     agifa029 #29;
|FIN;

|VARIABLES;
     nTecla = 0;
     nLin = 0;
     &Tempo = "";
     aAlfa = "";
     &nDeci_Base = 0;
     &nDeci_PreBase = 0;
     &precio_base = 0;
     nSwBarra       = 1;
     nForma         = 1;
     &Cie_pasa      = 0;
     &Cie_fech      = @;
     &Cie_alma      = 0;
   fichero   = "";
   nCodigo   = 0;
   nModo     = 0;

   &bloque   = "";

   &fecuno   = @;
   &linuno   = 0;

   &inkey    = 0;
   &esnuevo  = 0;

   alfa1     = "";
   alfa2     = "";
   alfa3     = "";
   alfa4     = "";
   nume1     = 0;
   nume2     = 0;
   nume3     = 0;
   nume4     = 0;

   &arant    = "";
   &alant    = 1;
   modo      = 0;
   articulo  = "";
   almacen   = 01;
   linea     = 0001;
   sw_igual  = 0;
   estado    = 0;
   swpaso    = 0;
   swborrar  = 0;

     &enUni1 = 0;
     &enUni2 = 0;
     &enAlbForma = 0;
|FIN;

|INCLUYE i_varart;
|INCLUYE i_cdbarr;

||***************************************************************************
|| ********* Procesos de Pantalla

|PROCESO articulo7; |TIPO 7;
     modo = (FEntrada / 100) ! 0;
     |SI modo ! 1; |ACABA; |FINSI;
     #0#2 = arant;
    |PINTA #0#2;
|FPRC;

|PROCESO articulo0; |TIPO 0;
 arant = #0Campo;
 #0#6 = #2#1; |PINTA #0#6;
 modo = (FEntrada / 100) ! 0;
 |SI modo ! 1; |ACABA; |FINSI;
 #0#5 = #2#9; |PINTA #0#5;
|FPRC;

|PROCESO alma7; |TIPO 7;
 modo = (FEntrada / 100) ! 0;
 |SI modo ! 1; |ACABA; |FINSI;
 #0#3 = alant;
 |PINTA #0#3;
|FPRC;

|PROCESO alma0; |TIPO 0;
 alant = #0#3;
|FPRC;

|PROCESO decim; |TIPO 0;
     #0Campo = #0Campo ! #42#8;
     |PINTA #0#4;
|FPRC;

|PROCESO HayActu;
     aAlfa = #0#14;
     |SI aAlfa = "S"; |ERROR10; |FINSI;
|FPRC;

|DEFBUCLE HayActu;
     #100#1003;
     ;
     #0#13, #0#0, 00001;
     #0#13, #0#0, 99999;
     ;
     NULL, HayActu;
|FIN;

|PROCESO funcio11; |TIPO 11;
     nTecla = FSalida;
     |SI nTecla = 9;
          nModo = (FEntrada / 100) ! 0;
          |SI nModo = 2;
               |DDEF aAlfa;
               aAlfa = aAlfa + "agist002";
               |CARGA_DEF aAlfa, referen@;
               |SI FSalida = 0;
                    |BUCLE HayActu;
                    |SI aAlfa = "S";
                         |C_M #1#1, 1, "N";
                         |MENAV "0000Hay articulos actualizados...";
                    |SINO;
                         |C_M #1#1, 1, "S";
                    |FINSI;
                    |DESCARGA_DEF #referen@;
               |FINSI;
          |FINSI;
          |ENCAMPO #1#1;
          |GRABA 020#1a;
          |ERROR;
          |ACABA;
     |FINSI;
     |SI nTecla = 11;
          swborrar = 0;
          |PUSHV 1301 1680;
          |D_CUADRO 1323, 1658;
          |ATRI R;
          |PINTA 1424, " BORRAR LA ENTRADA CODIGO ";
          |PINTA 1450, bloque;
          |PINTA 1524, "         CORRECTO S/N             ";
          |CONFI "1524S          CORRECTO S/N ";
          |SI FSalida = 0;
               swborrar = 1;
               |PTEC 807;
          |FINSI;
          |ATRI 0;
          |POPV;
          |ERROR;
     |FINSI;
     |SI nTecla = 25; || Ctrl-F7
          |HAZ CreaTempo;
          eaFichero = Tempo;
          |SUB_EJECUTA_NP "psion066";
          |HAZ BoraTempo;
          |SI enError = 0; |HAZ ImportaPDA002; |FINSI;
          |ERROR; |ACABA;
     |FINSI;
     |SI nTecla = 13;
          |HAZ VerDobleUni;
          |ERROR; |ACABA;
     |FINSI;
|FPRC;

|PROCESO BorraCompo;
     |BORRA 020#905a;
     |LIBERA #905;
|FPRC;

|DEFBUCLE dsm00005;
     #905#1;
     ;
     "T2", #0#13, #0#0, #0#1, "      ";
     "T2", #0#13, #0#0, #0#1, "zzzzzz";
     be;
     NULL, BorraCompo;
|FIN;

|PROCESO CogeValorest002;  |TIPO 7;
     |ABRE #42;
     |LEE 001#42p;
     |SI FSalida ! 0; |DDEFECTO #42; |FINSI;
     |CIERRA #42;

     |SI Campo = 3;
         |C_M #0Campo, 1, "S";
         |SI #42#99 = "S";
             |C_M #0Campo, 1, "N";
             #0Campo = 1;  |PINTA #0Campo;
         |FINSI;
     |FINSI;

     |SI Campo = 4;
          |HAZ DobleUni;
          |SI eaSw2Stock = "S";
               |C_M #0#4, 1, "N";
               |SI #agifa142#72 = "S";
                    |C_M #0#4, 1, "S";
               |FINSI;
          |SINO;
               |C_M #0#4, 1, "S";
               |ACABA;
          |FINSI;
     |FINSI;

     |SI #2#176 = "N";  |ACABA;  |FINSI;

     |SI Campo = 3;
         |SI enAlmacen ! 0;
             #0#3 = enAlmacen;  |PINTA #0#3;
         |FINSI;
     |FINSI;

     |SI Campo = 4;
         |SI enUnidades ! 0;
             |SI #0#4 ! enUnidades;  Contenido = #0#4;  |FINSI;
             #0#4 = enUnidades;  |PINTA #0#4;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO PideArtt002;  |TIPO 7;
     |C_M #0#2, 1, "S";
     |C_M #0#3, 1, "N";

     nModo = (FEntrada / 100) ! 0;
     |SI nSwBarra = 0; |Y nModo = 1; |C_M #0#3, 1, "S"; |ACABA; |FINSI;

     |SI #0#2 ! "                    ";
         |ABRE #2;
         #2#0 = #0#2;
         |LEE 000#2=;
         |SI FSalida ! 0;  |DDEFECTO #2;  |FINSI;
         |CIERRA #2;
         |SI #2#176 = "S";
             |C_M #0#2, 1, "N";
             |C_M #0#3, 1, "N";
             |ACABA;
         |FINSI;
     |FINSI;

     |HAZ articulo7;

     |ABRE #910;
     |LEE 040#910p;
     |SI FSalida ! 0;  |DDEFECTO #910;  |FINSI;
     |CIERRA #910;

     |SI #910#2 [ 2;    |ACABA;  |FINSI;

     eAlfa = #0Campo;
     |SUB_EJECUTA_NP "dsz99990";                 || Por Pantalla

     |SI eAlfa = "-1";  |PTEC 807;  |ACABA;  |FINSI;
     |SI eAlfa = "-2";              |ACABA;  |FINSI;

     eaArticulo = eAlfa;      ||  700000-4215

     #0Campo = eAlfa;
     |PINTA #0Campo;
     |PTEC 802;

|FPRC;

|PROCESO Calc6t002;  |TIPO 6;
     nModo = (FEntrada / 100) ! 0;
     |SI #0Campo ! Contenido;
          eaArticulo = #0#2; |Y Campo = 2;
          |HAZ DefectoMediC;
          #0#10 = enMedida1;
          #0#11 = enMedida2;
          #0#12 = enMedida3;
     |FINSI;
     |SI nSwBarra = 0; |Y nModo = 1;
          eaArti = #0#2;
          |HAZ HallaArti;
          |SI eaArti = ""; |ERROR; |ACABA; |FINSI;
          |SI #2#176 = "S"; |Y eaEleme = "";
               |MENAV "0000Codigo incompleto...";
               |ERROR; |ACABA;
          |FINSI;
          #0#2 = eaArti; |PINTA #0#2;
          |ACABA;
     |FINSI;

     enAlta = 1;
     eaArticulo = #0Campo;
     |HAZ LlenaArticulo;
     |SI FSalida = 0;
          #0Campo = eaArticulo;
          |PINTA #0Campo;
     |SINO;
          |ERROR; |ACABA;
     |FINSI;

     |ABRE #2;
     #2#0 = #0#2;
     |LEE 000#2=;
     |SI FSalida ! 0;  |DDEFECTO #2;  |FINSI;
     |CIERRA #2;

     |SI #2#176 = "S";
         eaTipo      = "T2";
         eaSerie     = #0#13;
         enCodigo    = #0#0;
         enLinea     = #0#1;
         eaArticulo  = #0#2;
         enAlmacen   = #0#3;
         enUnidades  = #0#4;
         enTBruto    = 0;
         |SUB_EJECUTA_NP "dsz00002";
         #0#3 = enAlmacen;
         #0#4 = enUnidades;
         |SI #42#99 = "N"; |PTEC 802; |FINSI;
         |PTEC 802;
     |FINSI;
|FPRC;

|PROCESO Tipo0t002;
     nModo = (FEntrada / 100) ! 0;
     |SI nSwBarra = 0; |Y nModo = 1; |ACABA; |FINSI;
     |HAZ articulo0;
|FPRC;

|PROCESO Tipo11t002;  |TIPO 11;
     nModo = (FEntrada / 100) ! 0;
     |SI FSalida = 7;  |Y #2#176 = "S";
         |MENAV "      Imposible abortar;  Termine de introducir la linea y efectue una Baja.";
         |ERROR;
     |FINSI;
|FPRC;

|PROCESO Tipo2;  |TIPO 2;
     nModo = (FEntrada / 100) ! 0;
     nForma = 0 +. 1;

     |ABRE #2;
     #2#0 = #0#2;
     |LEE 000#2=;
     |SI FSalida ! 0;  |DDEFECTO #2;  |FINSI;
     |CIERRA #2;

     |SI nSwBarra = 0; |Y nModo = 1; |Y nForma = 1; |Y #2#176 = "S";
          |HAZ GrabaElemento;
     |FINSI;
     |SI nModo = 3;
          |BUCLE dsm00005;
     |FINSI;

     |HAZ EsAlbero;
     |SI FSalida = 0;
          enUni1 = #0#4;
          enUni2 = #0#7;
          enAlbForma = nForma;
          |HAZ AlberoActPartidasSA;
     |FINSI;
|FPRC;

|PROCESO MinAlma02;
     #8#0 = #0#2;
     #8#1 = 1;
|FPRC;

|PROCESO MaxAlma02;
     #8#0 = #0#2;
     #8#1 = 99999;
|FPRC;

|PROCESO cierre; |TIPO 0;
     |SI FSalida = 13; || F5;
          |ABRE #8;
          #8#0 = #0#2;
          #8#1 = #0#3;
          |LEE 000#8];
          |CONSULTA_F_CLAVE #1#8, MinAlma02, MaxAlma02;
          |SI FSalida = 0;
               #0#3 = #8#1; |PINTA #0#3;
          |SINO;
               |ERROR; |ACABA;
          |FINSI;
          |CIERRA #8;
     |FINSI;

     efFec = #1#1;
     enAlma = #0#3;
     |HAZ MiraHisL;
     |SI enErrHis = 0; |HAZ MiraStockIni; |FINSI;
     |SI enErrHis ! 0;
          |ERROR;
          |ACABA;
     |FINSI;
|FPRC;

|PROCESO Tipo5; |TIPO 5;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 4; |ACABA; |FINSI;

     |SI nSwBarra = 1;
          |CONFI "0000NIntroduccion por Codigo de Barras";
          nSwBarra = FSalida;
     |FINSI;
     |ATRI S;
     |SI FSalida = 0;
          |PINTA 0533, "Introduccion por codigo de barras  ";
     |SINO;
          |PINTA 0533, "Introduccion por codigo de articulo";
     |FINSI;
     |ATRI 0;
|FPRC;

|PROCESO GrabaElemento;
     |ABRE #914;
     #914#0 = eaEleme;
     |LEE 000#914=;
     |CIERRA #914;

     |ABRE #905;
     |DDEFECTO #905;
     #905#0 = "T2";
     #905#1 = #0#13;
     #905#2 = #0#0;
     #905#3 = #0#1;
     #905#4 = eaEleme;
     #905#5 = #914#1;
     #905#6 = #0#4;
     |GRABA 020#905n;
|FPRC;

|PROCESO ImportaPDA002;
     |DDEF aAlfa; aAlfa = aAlfa + "agist002";
     |CARGA_DEF aAlfa, referen@;
     |SI FSalida ! 0; |ACABA; |FINSI;
     |ABRE #referen@;
     #referen@#13 = #1#2;
     #referen@#0 = #1#0;
     #referen@#1 = 99999;
     |LEE 000#referen@.];
     |SI FSalida = 0;
          |LEE 000#referen@.a;
     |SINO;
          |LEE 000#referen@.u;
     |FINSI;
     |SI FSalida = 0; |Y #referen@#13 = #1#2; |Y #referen@#0 = #1#0;
          nLin = #referen@#1 + 1;
     |SINO;
          nLin = 1;
     |FINSI;
     |CIERRA #referen@;
     |DESCARGA_DEF #referen@;

     |DDEF aAlfa; aAlfa = aAlfa + "psion067";
     |CARGA_DEF aAlfa, referen@;
     |SI FSalida ! 0; |ACABA; |FINSI;
     |NOME_DAT #referen@#eaFichero#;
     |ABRE #referen@;
     |LEE 000#referen@.p;
     |PARA; |SI FSalida = 0; |HACIENDO;
          #0#13 = #1#2;
          #0#0 = #1#0;
          #0#1 = nLin;
          |GRABA 020#0b;
          #0#2 = #referen@#0; ||Arti
          #0#6 = #referen@#1; ||Des
          #0#3 = #referen@#2; ||Alma
          #0#4 = #referen@#3; ||Uni
          #0#8 = #referen@#9; ||Parti

          |ABRE #2;
          #2#0 = #0#2;
          |LEE 000#2=;
          |CIERRA #2;
          #0#5 = #2#9;

          FEntrada = 100;     ||simula alta
          |HAZ Tipo2;
          |GRABA 020#0a;
          |LIBERA #0;
          nLin = nLin + 1;
          |LEE 000#referen@.s;
     |SIGUE;
     |CIERRA #referen@;
     |DELINDEX #referen@;
     |DESCARGA_DEF #referen@;
     |PONCONTROL 23, "SI"
     |PTEC 809;
     |PTEC 808;
|FPRC;

|PROCESO Alma02_66; |TIPO 66;
     |ABRE #29;
     #29#0 = #0#3;
     |LEE 000#29];
     |CONSULTA_CLAVE #1#29;
     |SI FSalida = 0;
          #0#3 = #29#0;
          |PINTA #0#3;
     |SINO;
          |ERROR;
     |FINSI;
     |CIERRA #29;
|FPRC;
