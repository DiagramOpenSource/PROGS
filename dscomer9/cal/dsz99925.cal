|FICHEROS;
     dsz99998 #998;
     agifa134 #134;
     agifa189 #189;
     agifa071 #71;
     agifa010 #10;
     agifa019 #19;
     grsol001@ #1;
     grsol002@ #2;
     grsol003@ #3;
     grsol004@ #4;
|FIN;

|VARIABLES;
     &efFecha = @;
     &eaAbono = "";
     &eaSerie = "";
     &enNumero = 0;
     &enLinea = 0;
     &enUniSol = 0;
     &eaTmpSol = "";
     &Tempo = "";
     aDef = "";
     aSerFab = "";
     nUniFab = "";
     aArti = "";
     aAlfa = "";
|FIN;

|PROCESO ParametroArti;
     |DDEF aDef;
     aDef = aDef + "grsol001";
     |CARGA_DEF aDef, grsol001@;
     |SI FSalida = 0;
          |DDEFECTO #1;
          |ABRE #1;
          #1#0 = aArti;
          |LEE 101#1=;
          |CIERRA #1;
          aSerFab = #1#1;
          nUniFab = #1#2;
          |DESCARGA_DEF #grsol001@;
     |FINSI;
|FPRC;

=============== Llamadas del agifa019 =============================

|PROCESO GrSolPinArt;
     |DDEF aDef;
     aDef = aDef + "grsol001";
     |CARGA_DEF aDef, grsol001@;
     |SI FSalida = 0;
          |DDEFECTO #1;
          |ABRE #1;
          #1#0 = #19#0;
          |LEE 000#1=;
          |CIERRA #1;
          |ATRI 0;
          |PINTA 2048, "N§ Serie: @  Uni.Stock: @";
          |ATRI I;
          |PINTA 2058, #1#1;
          |PINTA 2072, #1#2;
          |DESCARGA_DEF #grsol001@;
     |FINSI;
|FPRC;

|PROCESO GrSolEntArt;
     |DDEF aDef;
     aDef = aDef + "grsol001";
     |CARGA_DEF aDef, grsol001@;
     |SI FSalida = 0;
          |DDEFECTO #1;
          |ABRE #1;
          #1#0 = #19#0;
          |LEE 101#1=;
          |SI FSalida ! 0; |GRABA 020#1b; |FINSI;
          |ENCAMPO #1#1;
          |SI #19#179 = "S";
               |ENCAMPO #2#1;
          |FINSI;
          |GRABA 020#1a;
          |CIERRA #1;
          |DESCARGA_DEF #grsol001@;
     |FINSI;
|FPRC;

|PROCESO GrSolBorArt;
     |DDEF aDef;
     aDef = aDef + "grsol001";
     |CARGA_DEF aDef, grsol001@;
     |SI FSalida = 0;
          |DDEFECTO #1;
          |ABRE #1;
          #1#0 = #19#0;
          |BORRA 101#1c;
          |CIERRA #1;
          |DESCARGA_DEF #grsol001@;
     |FINSI;
|FPRC;

==================== Llamadas del agifa189 ========================
|PROCESO GrSolBorraTmp;
     |SI eaTmpSol ! "";
          |DELINDEX #grsol003@; Tempo = eaTmpSol; |HAZ BoraTempo;
          |DESCARGA_DEF #grsol003@;
     |FINSI;
|FPRC;

|PROCESO GrSolCreaTmp;
     |DDEF aDef;
     aDef = aDef + "grsol003";
     |CARGA_DEF aDef, grsol003@;
     |SI FSalida ! 0; |ACABA; |FINSI;

     |HAZ CreaTempo;
     eaTmpSol = Tempo;
     |NOME_DAT #grsol003@#eaTmpSol#; |DELINDEX #grsol003@;
|FPRC;

|PROCESO GrSolFabrica;
     aArti = #189#2;;
     |HAZ ParametroArti;
     |SI aSerFab = "S";
          eaSerie = #189#11;
          enNumero = #189#0;
          enLinea = #189#1;
          enUniSol = #189#7;
          |SUB_EJECUTA_NP "grsol003";
          #189#7 = enUniSol;
          |PINTA #189#7;

          |C_M #189#7, 1, "N";

     |SINO;
          |C_M #189#7, 1, "S";
     |FINSI;
|FPRC;

|PROCESO ActSerFab;
     |DDEFECTO #2;
     #2#0 = #3#4;
     #2#1 = #3#0;
     #2#2 = #3#1;
     #2#3 = #189#9;
     #2#4 = #3#5;
     #2#5 = #3#6;
     |GRABA 020#2n;
|FPRC;

|DEFBUCLE ActSerFab;
     #3#1004;
     ;
     #189#11, #189#0, #189#1, 001;
     #189#11, #189#0, #189#1, 999;
     ;
     NULL, ActSerFab;
|FIN;

|PROCESO GrSolActFab;
     |DDEF aDef;
     aDef = aDef + "grsol002";
     |CARGA_DEF aDef, grsol002@;
     |SI FSalida = 0;
          |ABRE #2;
          |BUCLE ActSerFab;
          |CIERRA #2;
          |DESCARGA_DEF #grsol002@;
     |FINSI;
|FPRC;
=============== Lmamadas del agifa010 ==========================
|PROCESO GrSolUniAlb;
     aArti = #71#2;
     |HAZ ParametroArti;
     |SI aSerFab = "S";
          |C_M #71#5, 1, "N";

          eaSerie = #71#29;
          enNumero = #71#0;
          enLinea = #71#1;
          enUniSol = #71#5;
          efFecha = #10#1;
          eaAbono = #10#43;
          |SUB_EJECUTA_NP "grsol004";
          #71#5 = enUniSol;
          |PINTA #71#5;
     |FINSI;
|FPRC;

|PROCESO BorSerAlb;
     |BORRA 020#4a;
     #2#0 = #4#4;
     |LEE 101#2=;
     |SI FSalida = 0;
          #2#6 = "";
          #2#7 = "";
          #2#8 = "";
          #2#9 = "";
          #2#10 = "";
          |GRABA 020#2a;
          |LIBERA #2;
     |FINSI;
|FPRC;

|DEFBUCLE BorSerAlb;
     #4#1004;
     ;
     #71#29, #71#0, #71#1, 001;
     #71#29, #71#0, #71#1, 999;
     be;
     NULL, BorSerAlb;
|FIN;

|PROCESO GrSolBorAlb;
     |DDEF aDef;
     aAlfa = aDef + "grsol004";
     |CARGA_DEF aAlfa, grsol004@;
     |SI FSalida = 0;
          aAlfa = aDef + "grsol002";
          |CARGA_DEF aAlfa, grsol002@;
          |SI FSalida = 0;
               |ABRE #2;
               |BUCLE BorSerAlb;
               |CIERRA #2;
               |DESCARGA_DEF #grsol002@;
          |FINSI;
          |DESCARGA_DEF #grsol004@;
     |FINSI;
|FPRC;

=============== Lmamadas del i_dobsto ==========================
|PROCESO GrSolUniAlb1;
     aArti = #71#2;
     |HAZ ParametroArti;
     |SI aSerFab = "S"; |Y nUniFab = 1;
          |C_M #998#1, 1, "N";

          eaSerie = #71#29;
          enNumero = #71#0;
          enLinea = #71#1;
          enUniSol = #71#5;
          efFecha = #10#1;
          eaAbono = #10#43;
          |SUB_EJECUTA_NP "grsol004";

          #998#1 = enUniSol;
          |PINTA #998#1;
     |FINSI;
|FPRC;

|PROCESO GrSolUniAlb2;
     aArti = #71#2;
     |HAZ ParametroArti;
     |SI aSerFab = "S"; |Y nUniFab = 2;
          |C_M #998#0, 1, "N";

          eaSerie = #71#29;
          enNumero = #71#0;
          enLinea = #71#1;
          enUniSol = #71#5;
          efFecha = #10#1;
          eaAbono = #10#43;
          |SUB_EJECUTA_NP "grsol004";

          #998#0 = enUniSol;
          |PINTA #998#0;
     |FINSI;
|FPRC;
====================== Al facturar ================================
|PROCESO ActSerFactu;
     #2#0 = #4#4;
     |LEE 101#2=;
     |SI FSalida = 0;
          #2#11 = #10#53;
          #2#12 = #10#39;
          #2#13 = #134#1;
          |GRABA 020#2a;
          |LIBERA #2;
     |FINSI;
|FPRC;

|DEFBUCLE ActSerFactu;
     #4#1004;
     ;
     #10#52, #10#0, 001, 001;
     #10#52, #10#0, 999, 999;
     be;
     NULL, ActSerFactu;
|FIN;

|PROCESO GrSolMarcaFac;
     |DDEF aDef;
     aDef = aDef + "grsol004";
     |CARGA_DEF aDef, grsol004@;
     |SI FSalida = 0;
          |DDEF aDef;
          aDef = aDef + "grsol002";
          |CARGA_DEF aDef, grsol002@;
          |SI FSalida = 0;
               |ABRE #2;
               |BUCLE ActSerFactu;
               |CIERRA #2;
               |DESCARGA_DEF #grsol002@;
          |FINSI;
          |DESCARGA_DEF #grsol004@;
     |FINSI;
|FPRC;
