|FICHEROS;
     albw0013 #0,MANTE;
     agifa104 #104;
     agifa024 #1;
     agifa038 #13;
     agifa019 #19;
     agifa007 #40;
     agifa142 #41;  || Parametros Generales
     agifa027 #42;  || Contador.
     agis0002 #90;  || Recibos adelantados.
     agifa321 #35;  || Parametros de Divisas
     agifa328 #36;  || Clientes/Divisas
     agifa329 #37;  || Lineas de Clientes/Divisas
     agifa324 #38;  || Divisas
     dsm00003 #39,MANTE;
     agifa204 #204;
     dsm00117 #117;
     dsm00246 #140; ||Control Riesgo Por Usuarios.
     dsm00247 #141; ||Pantalla Informe Riesgo.
     dsm00248 #143; ||Auditoria.
|FIN;

|VARIABLES;
     nSwAlbero  = 0;
     nBoton     = 0;
     nPendiente = 0;
     nPendiente2= 0;
     nNume11    = 0;
     nModo      = 0;
     nNume7     = 0;
     nCalc      = 0;
     nConAu     = 0;
     aUs        = "";
     &nMasRies  = 0;
     nTries     = 0;
     nAudito    = 0;
     &nRieTotal = 0;
     &nRieUtili = 0;
     &aRieBloqu = "";
     &aBloquead = "";
     &eaTag     = "";
     nMult      = 0;
     aProg      = "";
     nForma     = 0;
     &nImpo     = 0;
     &cli       = "";
     nPosi      = 0;
     nfecha     = 0;
     nTecla     = 0;
     nSwVersion = 0;

     &enSw      = 0;

     &nDeci_Base     = 0;   || Decimales en Totales de la moneda base
     &nDeci_PreBase  = 0;   || Decimales en el Precio en moneda base
     &nDeci_BaseMxP  = 0;   || Decimales en Totales de la moneda base (Maxima precision)
     &nDeci_PreBasMxP= 0;   || Decimales en el Precio en moneda base (Maxima precision)
     &nDeci_DivP     = 0;   || Decimales en Totales de la divisa
     &nDeci_PreDivP  = 0;   || Decimales en el Precio en divisa
     &nDeci_TotVisP  = 0;   || Decimales en Totales visibles (el de la moneda de trabajo)
     &nDeci_TotNoVisP = 0;  || Decimales en Totales no visibles (el de la otra moneda)
     &nDeci_PreVisP  = 0;   || Decimales en el Precio visual. (lineas)
     &nDeci_ImpVisP  = 0;   || Decimales en el Importe visual. (lineas)
     &nDeci_PreVis  = 0;    || para agifa023
     &nDeci_ImpVis  = 0;    ||
     &ac_divisa = "N";   || Vale "S" cuando las divisas en ventas estan
                         || activadas y la serie es de divisas
     &pintado = 0;       || Sirve para controlar los pintados de las lineas
     &decim_divisa = 0;  || Decimales de la divisa
     &precio_divisa = 0; || Decimales del precio en divisa
     &decim_base = 0;    || Decimales de la moneda base
     &precio_base = 0;   || Decimales del precio en moneda base
     div_act = 0;        || Divisa actualizada. Me sirve en un PARA para que
                         || no salga hasta que este actualizada (div_act=1)
     &cli_divisa = "";   || Codigo de Cliente. Se pasa a CLIENTES/DIVISAS
     &divisa = "";       || Codigo de Divisa. Se pasa a ACTUAL. de DIVISAS
     nModo104 = 0;
     sw_reserva     = 0;
     alfa           = "";
     contador       = 0;
     cont           = "";
     &ext1          = "";
     &ser_ped       = "";
     &num_ped       = 0;
     &bl            = "                              ";
     dto_prom       = "N";
     def_exp        = "N";
     avisa_st       = "N";
     recoge         = "N";    || Lo utilizo en agifa073
     fmes           = "  ";
     mensaje10      = "0000El Pedido ya esta Servido en parte o totalmente";
     mensaje11      = "0423Actualizando linea:";
     mensaje1       = "2400NDesea imprimir el recibo? ";
     informe        = "";
     puente         = "";
     &cant          = 0;
     &tolin         = 0;
     n_dec          = 0;
     n_pre          = 0;
     def_alm        = 0;
     con            = 0;
     indtope        = 0;
     ind            = 0;
     mes            = 0;
     control        = 0;
     modo           = 0;
     nEntrada       = 0;
     aAlfa          = "";
     || Variables de comunicacion con agifa218
     &aSeriePedido  = "";
     &aNumeroPedido = 0;
     &SPedido       = "";
     &NPedido       = 0;
     &BAdela        = 0;
     &TipoCta       = "P";
     &ser           = "";
     &doc           = 0;
     &eaCodigo      = "";
     &eaTipoCodigo  = "CL";
     &enDesdeX      = 1;
     &enSwEntrar    = 0;

     || MENU PARA INTRODUCIR EL CODIGO DEL CLIENTE DE FORMA AUTOMATICA.
 {-1}menu           = "";
     menu1          = "2400";
     menu2          = "1";
     menu3          = "Contador:  ";
     menu4          = "AM";
     menu5          = " Automatico ";
     menu6          = " Manual ";
     so             = "";
     no             = 0;
     ser_a          = "";
     num_a          = 0;
     lin_a          = 0;
     art_a          = "";
     uni_a          = 0;
     serie          = "";
     numero         = 0;
     caltot         = 0;
     recalpre       = 0;
     nDirEnt        = 0;
     nImpDiv        = 0;
     nPend          = 0;
|FIN;

|INCLUYE i_varart;

|PROCESO LeeMonBase; ||Lee los parametros de la moneda base
     |ABRE #agifa321;
     |ABRE #agifa324;
     |LEE 010#agifa321.p;   ||Leo parametros de divisa
     #albw0013#40 = #agifa321#9;  ||Cojo la Moneda Base de parametros
     #agifa324#0 = #agifa321#9;
     |LEE 010#agifa324.=;         ||Leo la ficha de la moneda base en Divisas
     #albw0013#41 = #agifa324#2;  ||Asigno el cambio de la moneda base con el euro
     decim_base = #agifa324#7;    ||Asigno los decimales de la moneda base
     precio_base = #agifa324#9;   ||Asigno los decimales del precio en la mon. base
     |CIERRA #agifa324;
     |CIERRA #agifa321;
|FPRC;

|PROCESO Param_Divisas; |TIPO 0;
     pintado = 0;  ||Controla pintados de las lineas

     |ABRE #agifa324;
     #agifa324#0 = #albw0013#35;
     |LEE 001#agifa324.=;   || Leo la divisa
     |CIERRA #agifa324;

     decim_divisa = #agifa324#7;  || Decimales de la divisa
     precio_divisa = #agifa324#9;  || Decimales en el precio de la divisa
     nDeci_DivP = decim_divisa;   || Decimales de la divisa
     nDeci_PreDivP = precio_divisa;  || Decimales en el precio de la divisa
     |SI #0#37 = "B";   ||Si trabajo con la moneda base ...
          nDeci_PreVis    = precio_divisa;
          nDeci_ImpVis    = decim_divisa;
          nDeci_PreVisP   = precio_divisa;
          nDeci_ImpVisP   = decim_divisa;
          nDeci_Base     = decim_base;
          nDeci_PreBase  = precio_base;
          nDeci_BaseMxP  = decim_base;
          nDeci_PreBasMxP = precio_base;
          nDeci_TotVisP   = decim_base;
          nDeci_TotNoVisP = decim_divisa;
     |SINO;     ||Si trabajo con la divisa ...
          nDeci_PreVis    = precio_base;
          nDeci_ImpVis    = decim_base;
          nDeci_PreVisP   = precio_base;
          nDeci_ImpVisP   = decim_base;
          nDeci_Base      = decim_base; || Max. precision en los decimales de la moneda base
          nDeci_PreBase   = precio_base;  || Max. precision en los dec. del precio de la moneda base
         |SI #0#36 = 1;
              nDeci_BaseMxP   = decim_base;    || sin triangulacion
              nDeci_PreBasMxP = precio_base;
         |SINO;
              nDeci_BaseMxP   = #agifa321#10;  || con triangulacion
              nDeci_PreBasMxP = #agifa321#11;
         |FINSI;
          nDeci_TotVisP   = decim_divisa;
          nDeci_TotNoVisP = decim_base;
     |FINSI;
|FPRC;

|PROCESO LeeParamDiv; |TIPO 0;
     |ABRE #agifa321;
     |LEE 001#agifa321.p;    || Leo parametros de divisas
     |ABRE #agifa007;
     #agifa007#26 = #albw0013#25;
     |LEE 001#agifa007.=;        || Leo la serie

     |SI #agifa007#39 = "N"; |O #agifa321#1 = "N";
          #0#37 = "B";
     |FINSI;

     |SI #0#37 = "D";      || Si la mon. de trabajo es la divisa...
       |SI #agifa007#39 = "S"; |Y #agifa321#1 = "S"; ||Si la serie es de ...
          ac_divisa = "S";  || ... divisas, y en parametros estan activadas ...
          || ... pongo como modificables los campos relacionados con divisas
          |CAMPO_MODIFICA #0#35 , 1 , "S";
          |CAMPO_MODIFICA #0#36 , 1 , "S";
          |CAMPO_MODIFICA #0#37 , 1 , "S";
          |CAMPO_MODIFICA #0#38 , 1 , "S";
          |CAMPO_MODIFICA #0#39 , 1 , "S";
       |SINO; || Si la serie no es de divisas, o no estan activados parametros
          ac_divisa = "N";
          #0#37 = "B";  || La moneda de trabajo es por fuerza la moneda base
          |PINTA #0#37;
          || Pongo como no modificables los campos relacionados con divisas
          |CAMPO_MODIFICA #0#37 , 1 , "N";
          |CAMPO_MODIFICA #0#38 , 1 , "N";
          |CAMPO_MODIFICA #0#39 , 1 , "N";
          |CAMPO_MODIFICA #0#35 , 1 , "N";
          |CAMPO_MODIFICA #0#36 , 1 , "N";
       |FINSI;
     |SINO;  || Si la moneda de trabajo es la moneda base
       |SI #agifa007#39 = "S"; |Y #agifa321#1 = "S"; || Si la serie es de divisas...
          ac_divisa = "S"; || ... y divisas estan activadas en parametros
          || Pongo como modificables los campos relacionados con divisas
          |CAMPO_MODIFICA #0#35 , 1 , "S";
          |CAMPO_MODIFICA #0#36 , 1 , "S";
          |CAMPO_MODIFICA #0#37 , 1 , "S";
          |CAMPO_MODIFICA #0#38 , 1 , "S";
          |CAMPO_MODIFICA #0#39 , 1 , "S";
       |SINO;  || Si la serie no es de divisas o no estan activados los parametros
          ac_divisa = "N";
          #0#37 = "B";  || La moneda de trabajo es por fuerza la moneda base
          || Pongo como no modificables los campos relacionados con divisas
          |CAMPO_MODIFICA #0#37 , 1 , "N";
          |CAMPO_MODIFICA #0#38 , 1 , "N";
          |CAMPO_MODIFICA #0#39 , 1 , "N";
          |CAMPO_MODIFICA #0#35 , 1 , "N";
          |CAMPO_MODIFICA #0#36 , 1 , "N";
       |FINSI;
     |FINSI;
     |CIERRA #agifa007;
     |CIERRA #agifa321;
     |HAZ Param_Divisas;
|FPRC;

|PROCESO CambioDivisa; |TIPO 0;
nModo104 = (FEntrada / 100) ! 0;
|SI #0Campo ! Contenido; |Y nModo104 = 1;   ||Si cambia el valor o es alta
  |SI Campo = 4; || Si llamo al proceso desde el cod. de cliente ...
    |LEE 001#agifa024.=;
    #0#35 = #agifa024#192;  || Asigno la divisa por defecto del cliente
    #0#37 = #agifa024#193; || Asigno la moneda de trabajo del cliente
    |PINTA #0#37;
  |FINSI;
  #agifa007#26 = #albw0013#25;
  |LEE 001#agifa007.=;  || Leo la serie
   |SI #agifa007#39 = "S"; |Y #agifa321#1 = "S";
      |SI #agifa024#191 = "S"; || Si el cliente es de divisa pactada
         |ABRE #agifa328;
         |ABRE #agifa329;
         #agifa329#0 = #albw0013#4;
         #agifa329#2 = #albw0013#35;
         #agifa329#7 = "N";
         |SELECT #2#agifa329;  || Con esta clave ...
         |LEE 011#agifa329.=;  || ... busco periodos no finalizados de esa divisa
         |SI FSalida = 0;  || Si existe un periodo no finalizado
           |PARA div_act = 0; |SI div_act = 0; |HACIENDO;
             #albw0013#35 = #agifa329#2;        || Cargo la divisa ...
             #albw0013#36 = #agifa329#3;        || ...y el cambio
             |LEE 001#agifa329.=;   || Leo las lineas de Clientes/Divisas
             nfecha = #albw0013#1 - #agifa329#6;  || Resto la fecha de fin de periodo a la actual
             |SI nfecha > 0;  || Si la fecha actual supera a la del periodo asignado ...
                |MENAV "0000ATENCION deberia Actualizar primero la divisa en Clientes/Divisas";
                cli_divisa = #albw0013#4;  || Le envio el cod. de cliente a "agifa328"
                |SUB_EJECUTA_NP "agifa328.tab";  || Ejecuto Clientes/Divisas
                |ERROR;
             |SINO;
                div_act = 1;  || Para salir del PARA
             |FINSI;
           |SIGUE;
         |SINO;  || Si no existe un periodo no finalizado para esa divisa
           |MENAV "0000No existe un periodo activo para la divisa en Clientes / Divisas";
           cli_divisa = #albw0013#4;  || Le paso a "agifa328" el cliente
           |SUB_EJECUTA_NP "agifa328.tab";  || Ejecuto Clientes / Divisas
           |ERROR;
           ||#albw0013#35 = #albw0013#40;
           ||#albw0013#36 = #albw0013#41;
         |FINSI;
         |SELECT #1#agifa329;
         |CIERRA #agifa329;
         |CIERRA #agifa328;
      |SINO; || Si el cliente no es de Divisa pactada ...
         |ABRE #agifa324; || ...miro directamente en divisas
         #agifa324#0 = #albw0013#35;
         |LEE 001#agifa324.=;
         |SI FSalida = 0; ||Si existe la divisa...
            #albw0013#36 = #agifa324#2; || la recojo
            |SI #agifa324#6 ! -1; || Si los dias de margen de actualizacion no
                                  || son = -1 (indefinidos)
              |PARA div_act = 0; |SI div_act = 0; |HACIENDO;
                 #albw0013#35 = #agifa324#0;        ||Cargo el valor de la divisa
                 #albw0013#36 = #agifa324#2;        ||Cargo el valor del cambio
                 |LEE 001#agifa324.=;  || Leo la divisa
                 nfecha = #albw0013#1 - #agifa324#4;
                 |SI nfecha > #agifa324#6; |Y #agifa324#6 ! -1;  || Si la fecha actual
                    |MENAV "0000ATENCION deberia Actualizar primero la divisa";
                    divisa = #albw0013#35;
                    |SUB_EJECUTA_NP "agifa326.tab";
                    |ERROR;
                 |SINO;
                    div_act = 1;
                 |FINSI;
              |SIGUE;
            |FINSI;
         |FINSI;
         |CIERRA #agifa324;
      |FINSI;
   |SINO;
     #albw0013#35 = #albw0013#40;
     #albw0013#36 = #albw0013#41;
   |FINSI;
|PINTA #albw0013#35;
|PINTA #albw0013#36;
|SI Campo = 4; || Si llamo al proceso desde el cod. de cliente ...
  |HAZ LeeParamDiv;
  |HAZ Param_Divisas;
|SINO;
  |HAZ Param_Divisas;
|FINSI;
|FINSI;
|FPRC;

|PROCESO VerSerie;  |TIPO 7;
     |SI enSwEntrar = 1;  |ACABA;  |FINSI;

     |C_M #0#25, 1, "S";
     |C_M #0#0,  1, "S";
|FPRC;

|PROCESO VerContaPV7;  |TIPO 7;
     |SI enSwEntrar = 1;  |ACABA;  |FINSI;

     |DDEFECTO #104;
     |ABRE #104;
     #104#25 = #0#25;
     #104#0  = #0#0;
     |HAZ ContaPV7;
     #0#25   = #104#25;  |PINTA #0#25;
     #0#0    = #104#0;   |PINTA #0#0;
     |CIERRA #104;
|FPRC;

|PROCESO VerContaPV0;  |TIPO 0;
     |SI enSwEntrar = 1;  |ACABA;  |FINSI;

     |ABRE #104;
     |HAZ ContaPV0;

     #104#25 = #0#25;
     #104#0  = #0#0;
     |LEE 101#104=;
     |SI FSalida = 0;
         |MENAV "0000 El Pedido ya Existe.";
         |ERROR;
     |SINO;
         |DDEFECTO #104;
         #104#25 = #0#25;
         #104#0  = #0#0;
         |GRABA 020#104n;
         |LIBERA #104;

         |C_M #0#25, 1, "N";
         |C_M #0#0,  1, "N";
         enSwEntrar = 1;
     |FINSI;
     |CIERRA #104;
|FPRC;

|PROCESO modomod; |TIPO 6;
   |SI nModo104 = 2; |Y #0Campo ! Contenido;
       |MENAV "0000No se puede modificar el campo.";
       #0Campo = Contenido;
       |PINTA #0Campo;
       |ERROR;
   |FINSI;
  |SI #0Campo ! Contenido; |Y Campo = 37;
    |HAZ LeeParamDiv;
    |HAZ Param_Divisas;
  |FINSI;
|FPRC;

|PROCESO mirar_d; |TIPO 6;
     control = 0;

     |ABRE #41; |LEE 001#41p; |CIERRA #41;

     n_dec    = #41#8;
     n_pre    = #41#9;
     dto_prom = #41#18;
     avisa_st = #41#35;

     |ABRE #agifa007;
     #agifa007#26 = #albw0013#25;
     |LEE 001#agifa007.=;
     |CIERRA #agifa007;

     def_alm = #agifa007#29;
     def_exp = #agifa007#30;
     recoge  = #agifa142#38;
     |HAZ LeeMonBase;
     |HAZ LeeParamDiv;

     ||컴컴컴컴컴컴컴컴컴
|FPRC;

|PROCESO ser55; |TIPO 0;
     cli = #0#4;
     ser = #0#25;
     |SUB_EJECUTA_NP "agifv011.tab;por_ejemplo"
|FPRC;

|PROCESO linf;
     #39#0 = #0#4;
     #39#1 = 1;
|FPRC;

|PROCESO lsup;
     #39#0 = #0#4;
     #39#1 = 9999;
|FPRC;

|PROCESO DirEntrega;
     |SI nTecla = 10;
          |ABRE #39;
          #39#0 = #0#4;
          #39#1 = 1;
          |LEE 101#39=;
          |SI FSalida = 0;
               |LEE 101#39s;
               |SI FSalida = 0; |Y #39#0 = #0#4; ||El cliente tiene mas de una direccion de entrega
                    |CONSULTA_F_CLAVE #1#39,linf,lsup;
                    |SI FSalida ] 0;
                         #0#57 = #39#1; |PINTA #0#57;
                         #0#19 = #39#10;|PINTA #0#19;
                         #0#14 = #39#2; |PINTA #0#14;
                         #0#20 = #39#3; |PINTA #0#20;
                         #0#21 = #39#7; |PINTA #0#21;
                         #0#59 = #39#6; |PINTA #0#59;
                         nDirEnt = #39#1;
                         |SI #39#14 = "  ";
                             #0#7 = #1#25;
                         |SINO;
                             #0#7 = #39#14;
                         |FINSI;
                         |PINTA #0#7;
                    |FINSI;
               |SINO;
                    |SI nTecla = 10;
                         |MENAV "0000El cliente solo tiene una direccion de entrega...";
                    |FINSI;
               |FINSI;
          |SINO;
               |SI nTecla = 10;
                    |MENAV "0000El cliente solo tiene una direccion de entrega...";
               |FINSI;
          |FINSI;
          |CIERRA #39;
     |FINSI;

     |SI nTecla = 11; |O #41#102 = "S"; |O #41#102 = "X";
          eaCodigo = #0#4;
          eaFuerza = "N";
          |SI nTecla = 11; eaFuerza = "S"; |FINSI;
          |SUB_EJECUTA_NP "dsz99994;alquelotoquelecortolosguevos";
          |SI #41#102 = "N"; |Y con ! 2; |ERROR;  |FINSI;
     |FINSI;
|FPRC;

|PROCESO SacaEntrega;
     nTecla  = FSalida;
|FPRC;

|PROCESO Cliente6; |TIPO 6;
     nTecla  = FSalida;
     |HAZ mirar_d;
     |HAZ DirEntrega;
|FPRC;

|PROCESO alriesgo; |TIPO 0;

     ||컴컴컴컴컴컴컴컴컴컴컴컴컴 Observaciones Clientes.

     |SI #agifa142#14 = "S";
          puente = #agifa024#53;     |QBF puente;
          |SI puente ! "";
               |PUSHV 1008 1370;
               |SI #agifa142#20 = "S"; |AVISO; |FINSI;
               |BLANCO 1008 1370;
               |CUADRO 1008 1370;
               |ATRI R;
               |PINTA 1110 , #agifa024#53;
               |ATRI 0;
               |PAUSA;
               |POPV;
          |FINSI;
     |FINSI;
     ||컴컴컴컴컴컴컴컴컴컴컴컴컴 Control Riesgo Cliente.
     nMasRies   = 0;
     eaSerie = #0#25; eaTag = "PE";  |HAZ SacaRiesgo;
     |SI #agifa142#166 = "S";
          |SI nRieUtili ] nRieTotal; |Y nRieTotal ! 0;
               |SI #agifa142#167 = "N";
                    |MENAV "0000Superado Riesgo Cliente";
                    |BLIN 4;
               |FINSI;
               |SI #agifa142#167 = "S";
                    |HAZ AvisaRiesgo;
               |FINSI;
               |SI #agifa142#167 = "P";
                    |ABRE #140;
                    aUs = Usuario % 0810;
                    #140#0 = "P";
                    #140#1 = aUs;
                    |LEE 000#140=;
                    |SI FSalida ! 0;
                         |CIERRA #140;
                         |HAZ mira_riesgop;
                         |ERROR; |ACABA;
                    |FINSI;
                    |CIERRA #140;
                    |DDEFECTO #141;
                    #141#0 = nRieTotal;
                    #141#1 = nRieUtili;
                    #141#2 = #140#2;
                    #141#5 = nRieUtili;
                    #141#3 = (nRieTotal % #140#2);
                    #141#4 = #140#3;
                    |SI #141#3 < #141#4;|Y #141#3 ! 0;
                         #141#6 = nRieTotal + #141#3;
                         nMasRies  = #141#3;
                    |SINO;
                         #141#6 = nRieTotal + #141#4;
                         nMasRies  = #141#4;
                    |FINSI;
                    |SI #141#6 [ #141#5;
                         #141#7 = "DENEGADA ";
                         nAudito = 1;
                         nMasRies = 0;
                    |SINO;
                         #141#7 = "PERMITIDA";
                         nAudito = 0;
                         |ABRE #143;
                         |LEE 000#143u;
                         |SI FSalida = 0;
                              nConAu = #143#0 + 1;
                         |SINO;
                              nConAu = 1;
                         |FINSI;
                         |DDEFECTO #143;
                         #143#0 = nConAu;
                         #143#12 = "PEDIDO";
                         #143#1 = #0#25;
                         #143#2 = #0#0;
                         #143#3 = #0#1;
                         #143#4 = #0#4;
                         #143#5 = #0#8;
                         #143#6 = aUs;
                         #143#7 = #141#0;
                         #143#8 = #141#1;
                         #143#9 = #141#2;
                         #143#10 = #141#3;
                         #143#11 = #141#4;
                         |GRABA 020#143n;
                         |CIERRA #143;
                    |FINSI;
                    |PUSHV 0501 2380;
                    |PINPA #0#141;
                    |PINDA #0#141;
                    |PAUSA;
                    |POPV;
                    |SI nAudito = 1;
                         |ERROR;
                    |FINSI;
               |FINSI;
          |FINSI;
     |FINSI;
     |HAZ mira_riesgop;
|FPRC;

|PROCESO mira_riesgop;
     |SI aRieBloqu = "S";
          |HAZ BloqCart;
          |ERROR; |ACABA;
     |FINSI;
     |SI #agifa142#166 = "S";
          |SI nRieTotal ! 0;
               |PUSHV 0501 2380;
               |BLANCO 0948 1672;
               |CUADRO 1148 1672;
               |CUADRO 0948 1672;
               |ATRI R; |PINTA 1049, "  < CONTROL RIESGOS >  "; |ATRI 0;
               |ATRI I;
               |PINTA 1250, "Concedido:";
               |PINTA 1350, "Total ...:";
               |PINTA 1261, nRieTotal;
               |PINTA 1361, nRieUtili;
               |ATRI 0;
               |PAUSA;
               |POPV;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Cliente0; |TIPO 0;
     #0#7 = #1#25;

     |HAZ ser55;
     |HAZ CambioDivisa;
     |HAZ alriesgo;

     |ABRE #1;
     #1#0 = #0#4;
     |LEE 000#1=;
     |CIERRA #1;

     |SI nDirEnt ! 0;    ||Porque pierde el los valores en Alta
          |ABRE #39;     ||ya que toma los defectos del agifa024
          #39#0 = #0#4;
          #39#1 = nDirEnt;
          |LEE 101#39=;
          |SI FSalida = 0;
               #0#57 = #39#1;  |PINTA #0#57;
               #0#19 = #39#10; |PINTA #0#19;
               #0#14 = #39#2;  |PINTA #0#14;
               #0#20 = #39#3;  |PINTA #0#20;
               #0#21 = #39#7;  |PINTA #0#21;
          |FINSI;
          |CIERRA #39;
          nDirEnt = 0;
     |FINSI;
|FPRC;

|PROCESO Promo104_7; |TIPO 7;
     |SI #41#129 = "S";
          |C_M #0#56, 1, "S";
     |FINSI;
|FPRC;

|PROCESO Promo104_0; |TIPO 0;
     |SI #0#56 ! 0;
          |ABRE #117;
          #117#0 = #0#56;
          |LEE 000#117=;
          |SI FSalida ! 0;
               |MENAV "0000Promocion Inexistente...";
               |ERROR;
          |FINSI;
          |CIERRA #117;
     |FINSI;
|FPRC;
