|FICHEROS;
     pcegz056 #56;  || Este

     pcegz050 #0;
     pcegz051 #1;
     pcegz052 #2;
     pcegm004 #4;
     pcegm033 #33;
     pcegm035 #35;
     agifa142 #142;
     :/dscarter/def/dscam003 #3;
     :/dscarter/def/dscam004 #104;
     :/dscarter/def/dscam045 #45;
     pcegz050@;
     pcegz051@;
     pcegz052@;
     gestempo;
|FIN;

|VARIABLES;
     nPanta0 = 0;
     nClave = 0;
     nGrid0 = 0;
     nGrid1 = 0;
     nGrid2 = 0;
     nBtnAsigna = 0;
     nBtnPedido = 0;
     nBtnRecibo = 0;
     nLabel = 0;
     aEsc1 = "";
     aEsc2 = "";
     aRetu = "";
     aTecla = "";

     &Tempo = "";
     i = 0;
     aTmp0 = "";
     aTmp1 = "";
     aTmp2 = "";
     nHayPed = 0;
     nHayCob = 0;
     nHayRec = 0;
     nSwAsigna = 0;
     nSal = 0;
     aAlfa = "";
     nCodCob = 0;
     nCodPed = 0;
     aSerPed = "";
     nCodRec = 0;
     aEmpRec = "";
     aEmpCarte = "";
     nEmpCarte = 0;
     nEmpresa = 0;
     nSwVer = 0;
     aBase = "";
     aEmpCar = "";
     nRango = 0;
     nPos1 = 0;
     nPos2 = 0;
     aFich = "";
     aEmp = "";
     aFichero = "";
     nHandle = 0;
     aTmp = "";
     nTipo = 0;
|FIN;

|PROCESO AsignaRec;
     #0#0 = nCodCob;
     |BORRA 020#0c;
     #2#0 = aEmpRec;
     #2#1 = nCodRec;
     |BORRA 020#2c;
     |REFRESCACONTROL nGrid0;
     |REFRESCACONTROL nGrid2;

     |ABRE #56;
     #56#0 = nCodCob;
     #56#1 = "R";
     #56#4 = aEmpRec;
     #56#5 = nCodRec;
     |GRABA 020#56n;
     |CIERRA #56;
     nCodCob = 0;
     nCodRec = 0;
|FPRC;

|PROCESO AsignaPed;
     #0#0 = nCodCob;
     |BORRA 020#0c;
     #1#1 = aSerPed;
     #1#2 = nCodPed;
     |BORRA 020#1c;
     |REFRESCACONTROL nGrid0;
     |REFRESCACONTROL nGrid1;

     |ABRE #56;
     #56#0 = nCodCob;
     #56#1 = "P";
     #56#2 = aSerPed;
     #56#3 = nCodPed;
     |GRABA 020#56n;
     |CIERRA #56;
     nCodCob = 0;
     nCodPed = 0;
|FPRC;

|PROCESO Recibos;
     nHayRec = 1;
     #2#0 = aEmpCarte;
     #2#1 = #3#40;
     #2#2 = #3#0;
     #2#3 = #3#1;
     #2#4 = #3#2;
     #2#5 = #3#32;
     #2#6 = #3#6;
     #2#7 = #3#99;
     #2#8 = #3#65;
     #2#10 = #3#3;
     |GRABA 020#2n;
|FPRC;

|DEFBUCLE Recibos;
     #3#1;
     14, 12;
     000000001, "P", #33#11;
     999999999, "P", #33#11;
     be;
     NULL, Recibos;
|FIN;

|PROCESO dscam045;
     |SI nEmpCarte ! #45#6;
          nEmpCarte = #45#6;

          aEmpCarte = ("00000" + #45#6) % -105;
          aAlfa = aBase + "dscarter/fich/" + aEmpCarte + "/";
          |PATH_DAT #3#aAlfa#;
          |BUCLE Recibos;
     |FINSI;
|FPRC;

|DEFBUCLE dscam045;
     #45#1;
     ;
     nEmpresa, 001;
     nEmpresa, 999;
     ;
     NULL, NULL, NULL, NULL, NULL, dscam045;
     #45#6;
|FIN;

|PROCESO LlenaRecPen;
     |DBASS aBase;
     aAlfa = aBase + "dscarter/fich/";
     |PATH_DAT #45 aAlfa;

     nEmpresa = aEmp;
     nEmpCarte = 0;

     |BUCLE dscam045;
|FPRC;

|PROCESO QuitaMarca2;
     #pcegz052@#9 = "";
     |GRABA 020#pcegz052@.a;
|FPRC;

|DEFBUCLE QuitaMarca2;
     #pcegz052@#1002;
     ;
     "     ", 000000001;
     "zzzzz", 999999999;
     ;
     NULL, QuitaMarca2;
|FIN;

|PROCESO Desmarca2;
     |DDEF aAlfa;
     aAlfa = aAlfa + "pcegz052";
     |CARGA_DEF aAlfa, pcegz052@;
     |SI FSalida = 0;
          |PATH_DAT #pcegz052@#aFich#;
          |NOME_DAT #pcegz052@#aTmp2#;
          |BUCLE QuitaMarca2;
          |DESCARGA_DEF #pcegz052@;
     |FINSI;
|FPRC;

|PROCESO QuitaMarca1;
     #pcegz051@#6 = "";
     |GRABA 020#pcegz051@.a;
|FPRC;

|DEFBUCLE QuitaMarca1;
     #pcegz051@#1002;
     ;
     "     ", 000001;
     "zzzzz", 999999;
     ;
     NULL, QuitaMarca1;
|FIN;

|PROCESO Desmarca1;
     |DDEF aAlfa;
     aAlfa = aAlfa + "pcegz051";
     |CARGA_DEF aAlfa, pcegz051@;
     |SI FSalida = 0;
          |PATH_DAT #pcegz051@#aFich#;
          |NOME_DAT #pcegz051@#aTmp1#;
          |BUCLE QuitaMarca1;
          |DESCARGA_DEF #pcegz051@;
     |FINSI;
|FPRC;

|PROCESO QuitaMarca0;
     #pcegz050@#16 = "";
     |GRABA 020#pcegz050@.a;
|FPRC;

|DEFBUCLE QuitaMarca0;
     #pcegz050@#1001;
     ;
     000000;
     999999;
     ;
     NULL, QuitaMarca0;
|FIN;

|PROCESO Desmarca0;
     |DDEF aAlfa;
     aAlfa = aAlfa + "pcegz050";
     |CARGA_DEF aAlfa, pcegz050@;
     |SI FSalida = 0;
          |PATH_DAT #pcegz050@#aFich#;
          |NOME_DAT #pcegz050@#aTmp0#;
          |BUCLE QuitaMarca0;
          |DESCARGA_DEF #pcegz050@;
     |FINSI;
|FPRC;

|PROCESO EventoGrid2;
     |SI FSalida = 4;
          |LEE 000#2=;
          |SI FSalida ! 0; |ACABA; |FINSI;

          |SI #2#9 = "*";
               #2#9 = "";
               nCodRec = 0;
          |SINO;
               |SI nCodRec ! 0;
                    |HAZ Desmarca2;
               |FINSI;
               #2#9 = "*";
               aEmpRec = #2#0;
               nCodRec = #2#1;
          |FINSI;
          |GRABA 020#2a;
          |REFRESCACONTROL nGrid2;
    |FINSI;
|FPRC;

|PROCESO EventoGrid1;
     |SI FSalida = 4;
          |LEE 000#1=;
          |SI FSalida ! 0; |ACABA; |FINSI;

          |SI #1#6 = "*";
               #1#6 = "";
               nCodPed = 0;
          |SINO;
               |SI nCodPed ! 0;
                    |HAZ Desmarca1;
               |FINSI;
               #1#6 = "*";
               nCodPed = #1#2;
               aSerPed = #1#1;
          |FINSI;
          |GRABA 020#1a;
          |REFRESCACONTROL nGrid1;
     |FINSI;
|FPRC;

|PROCESO EventoGrid0;
     |SI FSalida = 4;
          |LEE 000#0=;
          |SI FSalida ! 0; |ACABA; |FINSI;

          |SI #0#16 = "*";
               #0#16 = "";
               nCodCob = 0;
          |SINO;
               |SI nCodCob ! 0;
                    |HAZ Desmarca0;
               |FINSI;
               #0#16 = "*";
               nCodCob = #0#0;
          |FINSI;
          |GRABA 020#0a;
          |REFRESCACONTROL nGrid0;
     |FINSI;
|FPRC;


|PROCESO Pedidos;
     #1#0 = #4#11;
     #1#1 = #4#25;
     #1#2 = #4#0;
     #1#3 = #4#1;
     #1#4 = #4#4;
     #1#5 = #4#8;
     |GRABA 020#1n;
     nHayPed = 1;
|FPRC;

|DEFBUCLE Pedidos;
     #4#4;
     9;
     "P", "      ", 0001, "     ", 000000, #33#12;
     "P", "zzzzzz", 9999, "zzzzz", 999999, #33#12;
     ;
     NULL, Pedidos;
|FIN;

|PROCESO Cobros;
     |PARA i = 0; |SI i [ 13; |HACIENDO i = i + 1;
          #0i = #35i;
     |SIGUE;
     #0#17 = #35#17;
     |GRABA 020#0n;
     nHayCob = 1;
|FPRC;

|DEFBUCLE Cobros;
     #35#5;
     ;
     "     ", 000000, 000000000;
     "     ", 000000, 000000000;
     ;
     NULL, Cobros;
|FIN;

|PROCESO Min2;
     #2#8 = -99999999;
     #2#1 = 000000001;
|FPRC;

|PROCESO Max2;
     #2#8 = 99999999;
     #2#1 = 999999999;
|FPRC;

|PROCESO Min1;
     #1#0 = -99999999;
     #1#1 = "     ";
     #1#2 = 000001;
|FPRC;

|PROCESO Max1;
     #1#0 = 99999999;
     #1#1 = "zzzzz";
     #1#2 = 999999;
|FPRC;

|PROCESO Min0;
     #0#0 = 000001;
     #0#2 = "01.01.00000";
|FPRC;

|PROCESO Max0;
     #0#0 = 999999;
     #0#2 = "31.12.99999";
|FPRC;

|PROCESO BtnAsigna;
     |SI nTipo = 1;
          |SI nCodCob = 0; |O nCodPed = 0;
               |SI nCodCob = 0; |Y nCodPed = 0;
                    |MENAV "0000Debe de marcar el cobro y el pedido";
                    |ACABA;
               |SINO;
                    |SI nCodCob = 0;
                         |MENAV "0000Debe de marcar el cobro";
                         |ACABA;
                    |FINSI;
                    |SI nCodPed = 0;
                         |MENAV "0000Debe de marcar el pedido";
                         |ACABA;
                    |FINSI;
               |FINSI;
          |FINSI;
          |CONFI "0000N¿Asignar cobro al pedido?";
          |SI FSalida = 0;
               |HAZ AsignaPed;
          |FINSI;
     |SINO;
          |SI nCodRec = 0; |O nCodPed = 0;
               |SI nCodCob = 0; |Y nCodRec = 0;
                    |MENAV "0000Debe de marcar el cobro y el recibo";
                    |ACABA;
               |SINO;
                    |SI nCodCob = 0;
                         |MENAV "0000Debe de marcar el cobro";
                         |ACABA;
                    |FINSI;
                    |SI nCodRec = 0;
                         |MENAV "0000Debe de marcar el recibo";
                         |ACABA;
                    |FINSI;
               |FINSI;
          |FINSI;
          |CONFI "0000N¿Asignar cobro al recibo?";
          |SI FSalida = 0;
               |HAZ AsignaRec;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO BtnRecibo;
     nTipo = 2;
     |ESTADO_CONTROL nGrid1, "HIDE";
     |ESTADO_CONTROL nGrid2, "SHOW";
     |ESTADO_CONTROL nBtnPedido, "ENABLE";
     |ESTADO_CONTROL nBtnRecibo, "DISABLE";

     |SI nLabel ! -1; |FIN_CONTROL_WINDOWS nLabel; |FINSI;
     |CONTROL_SIMPLE nPanta0, "LABEL,{{001}}Recibos", 2102, 2110, NULL;
     nLabel = FSalida;
|FPRC;

|PROCESO BtnPedido;
     nTipo = 1;
     |ESTADO_CONTROL nGrid1, "SHOW";
     |ESTADO_CONTROL nGrid2, "HIDE";
     |ESTADO_CONTROL nBtnPedido, "DISABLE";
     |ESTADO_CONTROL nBtnRecibo, "ENABLE";

     |SI nLabel ! -1; |FIN_CONTROL_WINDOWS nLabel; |FINSI;
     |CONTROL_SIMPLE nPanta0, "LABEL,{{001}}Pedidos", 2102, 2110, NULL;
     nLabel = FSalida;
|FPRC;

|PROCESO Tipo80; |TIPO 80;
     FSalida = 3499;
|FPRC;

|PROCESO LeeTxt;
     |DBASE aFich;
     aFich = aFich + "fich/";
     aFichero = aFich + "R43" + (Usuario % 810);
     |XABRE "", aFichero, nHandle;

     |XLEE nHandle, 250, aEmp;
     |XLEE nHandle, 250, aTmp;
     |XCIERRA nHandle;
|FPRC;

|PROGRAMA;
     |CLS;
     |PINPA #0#56;  nPanta0 = FSalida;
     |PINDA #0#56;
     |HAZ LeeTxt;

     |NOME_DAT #56 aTmp;

     |ABRE #33; |LEE 000#33p; |CIERRA #33;
     |ABRE #142; |LEE 000#142p; |CIERRA #142;

     |HAZ CreaTempo; |NOME_DAT #0 Tempo; aTmp0 = Tempo; |DELINDEX #0;
     |HAZ CreaTempo; |NOME_DAT #1 Tempo; aTmp1 = Tempo; |DELINDEX #1;
     |HAZ CreaTempo; |NOME_DAT #2 Tempo; aTmp2 = Tempo; |DELINDEX #2;

     |ABRE #0
     |BUCLE Cobros;
     |CIERRA #0;
     |ABRE #1;
     |BUCLE Pedidos;
     |CIERRA #1;

     |ABRE #2;
     |HAZ LlenaRecPen;
     |CIERRA #2;

     |SI nHayPed ! 0 ;
          |SI nHayCob ! 0; |O nHayRec ! 0;
               |ABRET #0;
               |ABRET #1;
               |ABRET #2;
               |LEE 000#0p;
               |LEE 000#1p;
               |LEE 000#2p;

               nRango = 4 + 8 + 16 + 32;
               nPos1 = 0902;
               nPos2 = 1999;
               nClave = 4;
               |LINEAL_SIMPLE #2#0, nRango, nPos1, nPos2, Min0, Max0, EventoGrid0;
               nGrid0 = FSalida;

               nRango = 4 + 8 + 16 + 32;
               nPos1 = 2201;
               nPos2 = 3299;
               |LINEAL_SIMPLE #2#1, nRango, nPos1, nPos2, Min1, Max1, EventoGrid1;
               nGrid1 = FSalida;

               nRango = 4 + 8 + 16 + 32;
               nPos1 = 2201;
               nPos2 = 3299;
               |LINEAL_SIMPLE #2#2, nRango, nPos1, nPos2, Min2, Max2, EventoGrid2;
               nGrid2 = FSalida;

               |CONTROL_SIMPLE nPanta0, "BOTON,{{193}}Asignar", 3302, 3312, BtnAsigna;
               nBtnAsigna = FSalida;

               |CONTROL_SIMPLE nPanta0, "BOTON,Pedidos", 3314, 3324, BtnPedido;
               nBtnPedido = FSalida;

               |CONTROL_SIMPLE nPanta0, "BOTON,Recibos", 3326, 3336, BtnRecibo;
               nBtnRecibo = FSalida;

               |HAZ BtnPedido;

               aEsc1 = &255 + "806";
               aEsc2 = &255 + "807";
               aRetu = &255 + "802";
               |PARA; |SI;  |HACIENDO;
                   FSalida = "::{{001}}Salir," + nGrid0;
                   |LEETECLA aTecla;
                   |SI aTecla = aEsc1; |SAL;  |FINSI;
                   |SI aTecla = aEsc2; |SAL;  |FINSI;
                   |SI aTecla = aRetu; |SAL;  |FINSI;
               |SIGUE;

               |CIERRAT #2;
               |CIERRAT #1;
               |CIERRAT #0;

               |DESTRUYECONTROL nGrid0;
               |DESTRUYECONTROL nGrid1;
               |DESTRUYECONTROL nGrid2;
               |FIN_CONTROL_WINDOWS nBtnAsigna;
               |FIN_CONTROL_WINDOWS nBtnPedido;
               |FIN_CONTROL_WINDOWS nBtnRecibo;
               |SI nLabel ! -1; |FIN_CONTROL_WINDOWS nLabel; |FINSI;
          |SINO;
               |MENAV "0000No hay cobros ni recibos pendientes";
          |FINSI;
     |SINO;
          |MENAV "0000No hay pedidos pendientes";
     |FINSI;
     Tempo = aTmp0; |HAZ BoraTempo; |DELINDEX #0;
     Tempo = aTmp1; |HAZ BoraTempo; |DELINDEX #1;
     Tempo = aTmp2; |HAZ BoraTempo; |DELINDEX #2;
     |PULSATECLA;
|FPRO;
