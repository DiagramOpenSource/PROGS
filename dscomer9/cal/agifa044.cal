|FICHEROS;
     agifa044 #44;    ||ESTE
     agifa048 #0;
     agifa049 #1;
     agifa019 #2;
     agifa017 #17;
     dsw00038 #38;    ||Escandallo + Nivel
     dsw00039 #39;    ||Escandallo Calculado
     agifa046 #46;
     agifa142 #142;
     dsm00241 #241;
     agifa048@;
     agifa321;
     agifa324;
     agifa142 #142;
     dsm00238 #238;
     referen@;
|FIN;

|VARIABLES;
     &enTiva = 0;
     &efFiva = @;
     &eaDiva = "";
     &enIva = 0;
     &enRec = 0;
     aFilm = "";
     nSwMecanogra = 0;
     nUni = 0;
     nSwRodacal = 0;
     &eaEscan = "";
     &eaArticulo = "";
     &nDeci_Base = 0;
     &nDeci_PreBase = 0;
     &Tempo    = "";
     &nDeci_EsCab = 0;
     &nDeci_EsLin = 0;
     aTempo38  = "";
     aTempo39  = "";
     aDef      = "";
     aAlfa     = "";
     impcompone = 0;
     nCampo    = 0;
     nNume     = 0;
     nNume1    = 0;
     nPvp      = 0;
     nFactor   = 0;
     nTotal    = 0;
     nSwImp    = 0;
     nSwCorri  = 0;
     nSwCheq   = 0;
 {-1}aMenu  = "";
     aMenu1 = "2400";
     aMenu2 = "1";
     aMenu3 = "Opciones : ";
     aMenu4 = "ACI";
     aMenu5 = " Anular ";
     aMenu6 = " Corregir ";
     aMenu7 = " Imprimir ";
     nPvpSin = 0;
     aParam  = "";
     nImpo = 0;
     nImpo2 = 0;
|FIN;

|INCLUYE i_escand;

|PROCESO LeeMulti;
     |SI #2#204 = "S";
          |DDEFECTO #241;
          |ABRE #241; |SELECT #2#241;
          #241#0 = #2#5;
          #241#2 = #1#14;
          |LEE 000#241=;
          |CIERRA #241;
          |SI #241#6 = "D";
               nFactor = 1 / #241#5;
          |SINO;
               nFactor = 1 * #241#5;
          |FINSI;
     |SINO;
          nFactor = 1;
     |FINSI;
|FPRC;

|PROCESO Total;
     |SI #0#36 = "S";
          |SI #1#16 = "D";
               nTotal = (nUni * #1#5) > #1#13;
          |SINO;
               nTotal = (nUni * #1#5) / ((100 - #1#13) / 100);
          |FINSI;
          nTotal = nTotal > #1#21;
     |SINO;
          |SI #1#16 = "D";
               nImpo = (nUni * #1#5) > #1#13;
          |SINO;
               nImpo = (nUni * #1#5) / ((100 - #1#13) / 100);
          |FINSI;
          nImpo = nImpo - (nUni * #1#5);
          nImpo2 = (nUni * #1#5) % #1#21;
          nTotal = (nUni * #1#5) + nImpo + nImpo2;
     |FINSI;
|FPRC;

|PROCESO EscanLin;
     |SI nSwMecanogra = 0;
          |HAZ MecanoFilm;
          |SI aFilm = "S"; |ACABA; |FINSI;
     |FINSI;

     |DDEFECTO #2;
     #2#0 = #1#2;
     |LEE 000#2=;

     |HAZ LeeMulti;

     nUni = #1#4;
     |SI #2#179 = "S";   ||Doble stock
          |ABRE #238;
          #238#0 = #2#201;
          |LEE 000#238=;
          |SI FSalida = 0; |Y #238#6 = 2;    || Factura uni2
               nUni = #1#17;
          |FINSI;
          |CIERRA #238;
     |FINSI;
     |SI #0#33 ! "K"; nUni = #1#4; |FINSI;

     |HAZ Total;
     #1#6 = nTotal;

     impcompone = #1#6 / #0#6;
     |SI #1#10 = 1; #0#8 = #0#8 + impcompone;    |FINSI;   ||Componentes
     |SI #1#10 = 2; #0#2 = #0#2 + impcompone;    |FINSI;   ||Mano Obra
     |SI #1#10 = 3; #0#3 = #0#3 + impcompone;    |FINSI;   ||Maquinaria
     |SI #1#10 = 4; #0#4 = #0#4 + impcompone;    |FINSI;   ||Otros

     |SI #1#10 = 1;
         |SI #0#17 = "S";
             |SI #0#26 = "D";
                 #0#21 = #0#21 + (#1#6 > #0#16);
             |SINO;
                 #0#21 = #0#21 + (#1#6 / ((100 - #0#16) / 100));
             |FINSI;
         |SINO;
             #0#21 = #0#21 + #1#6;
         |FINSI;
     |FINSI;

     |SI #1#10 = 2;
         |SI #0#19 = "S";
             |SI #0#28 = "D";
                 #0#21 = #0#21 + (#1#6 > #0#24);
             |SINO;
                 #0#21 = #0#21 + (#1#6 / ((100 - #0#24) / 100));
             |FINSI;
         |SINO;
             #0#21 = #0#21 + #1#6;
         |FINSI;
     |FINSI;

     |SI #1#10 = 3;
         |SI #0#18 = "S";
             |SI #0#27 = "D";
                 #0#21 = #0#21 + (#1#6 > #0#23);
             |SINO;
                 #0#21 = #0#21 + (#1#6 / ((100 - #0#23) / 100));
             |FINSI;
         |SINO;
             #0#21 = #0#21 + #1#6;
         |FINSI;
     |FINSI;

     |SI #1#10 = 4;
         |SI #0#20 = "S";
             |SI #0#29 = "D";
                 #0#21 = #0#21 + (#1#6 > #0#25);
             |SINO;
                 #0#21 = #0#21 + (#1#6 / ((100 - #0#25) / 100));
             |FINSI;
         |SINO;
             #0#21 = #0#21 + #1#6;
         |FINSI;
     |FINSI;

     #0#5 = #0#2 + #0#3 + #0#4;         || total otros costes
     |SI #0#36 = "S";
           #0#11 = (#0#5 + #0#8) > #0#7;      || total serie
           #0#11 = #0#11 > #0#34;
     |SINO;
           nImpo = (#0#5 + #0#8) % #0#7;
           nImpo2 = (#0#5 + #0#8) % #0#34;
           #0#11 = (#0#5 + #0#8) + nImpo + nImpo2;
     |FINSI;
     #0#9 = #0#11 / #0#12;              || total unitario
     |SI nSwRodacal = 0;
          #0#9 = #0#11 * #0#12;              || total unitario
     |FINSI;

     |GRABA 020#1a;
     |LIBERA #1;
|FPRC;


|PROCESO CalcuPrecio;
     |PINTA 1720, #39#0;
     #0#0 = #39#0;
     |LEE 101#0=;
     |SI FSalida = 0;
          #0#2  = 0;
          #0#3  = 0;
          #0#4  = 0;
          #0#8  = 0;
          #0#21 = 0;
          #0#5  = 0;
          #0#11 = 0;
          #0#9  = 0;
          |BUCLELIN 2 EscanLin #0;
          |DDEFECTO #2;
          #2#0 = #0#0;
          |LEE 101#2=;
          |SI FSalida ! 0; |GRABA 020#2b; |FINSI;

          enTiva = #2#30;
          efFiva = ~;
          |HAZ SacaIVA;

          nPvp = #0#21;
          |SI #46#13 = "S";
               nPvp = nPvp > enIva;
          |FINSI;

          |SI #46#14 = "S";
               nNume = 1;
               |PARA nCampo = 1; |SI nCampo [ nDeci_EsCab; |HACIENDO nCampo = nCampo + 1;
                    nNume = nNume * 10;
               |SIGUE;
               nPvp = nPvp * nNume;

               |SI #2#109 > 0; nPvp = nPvp + (#2#109-1); |FINSI;
               nNume1 = (nPvp $ #2#109);
               |SI nNume1 ! 0;
                    nPvp = nPvp - nNume1;
               |FINSI;

               nPvp = nPvp / nNume;
          |FINSI;
          #0#21 = nPvp; |PINTA #0#21;

          |SI #44#3 = 3;
               nPvpSin = #0#21 / ( 1 + (enIva/100));
               #0#30 = 100 - ((#0#11 * 100) / nPvpSin);
               #0#31 = #0#11 /  ((100 - #0#30) / 100);
               #0#31 = #0#31 > enIva;
          |FINSI;
          |SI #44#3 = 2;
               nPvpSin = #0#31 / ( 1 + (enIva/100));
               #0#30 = 100 - ((#0#11 / nPvpSin) * 100);
          |FINSI;
          |SI #44#3 = 1;
               #0#31 = #0#11 /  ((100 - #0#30) / 100);
               #0#31 = #0#31 > enIva;
          |FINSI;

||          #0#30 = 100 - ((#0#11 * 100) / #0#21);
||          #0#31 = #0#11 /  ((100 - #0#30) / 100);
          |SI #46#13 = "N"
               nPvp = #0#31;
             ||  |SI #46#13 = "S";
                    nPvp = nPvp > enIva;
             ||  |FINSI;

               |SI #46#14 = "S";
                    nNume = 1;
                    |PARA nCampo = 1; |SI nCampo [ nDeci_EsCab; |HACIENDO nCampo = nCampo + 1;
                         nNume = nNume * 10;
                    |SIGUE;
                    nPvp = nPvp * nNume;

                    |SI #2#109 > 0; nPvp = nPvp + (#2#109-1); |FINSI;
                    nNume1 = (nPvp $ #2#109);
                    |SI nNume1 ! 0;
                         nPvp = nPvp - nNume1;
                    |FINSI;

                    nPvp = nPvp / nNume;
               |FINSI;
               #0#32 = nPvp;
          |SINO;
               #0#32 = #0#31;
          |FINSI;

          |GRABA 020#0a;
          |LIBERA #0;

          |ABRE #1; |SELECT #2#1;
          #1#2 = #0#0;
          #1#0 = "";
          |LEE 101#1];
          |PARA; |SI FSalida = 0; |Y #1#2 = #0#0; |HACIENDO;
               |HAZ LeeMulti;
               #1#5 = #0#11;          || Precio = Coste Serie
               #1#15 = #1#5 / nFactor;

               |HAZ Total;
               #1#6 = nTotal;
               |GRABA 020#1a;
               |LIBERA #1;
               |LEE 101#1s;
          |SIGUE;
          |LIBERA #1;
          |CIERRA #1;

          #2#9 = #0#11;
          |SI nSwRodacal = 0;
               #2#9 = #2#9 * #2#4;
          |FINSI;

          |GRABA 020#2a;
          |LIBERA #2;
     |FINSI;
|FPRC;

|PROCESO dsw00038;
     #39#0 = #38#0;
     |LEE 000#39=;
     |SI FSalida ! 0;
          |GRABA 020#39n;
          |HAZ CalcuPrecio;
     |FINSI;
|FPRC;

|DEFBUCLE dsw00038;
     #38#2;
     ;
     INICIO;
     FINAL;
     ;
     NULL, dsw00038;
|FIN;

|PROCESO LinEscan;
     #0#0 = aEscan;
     |LEE 000#0=;
     |SI FSalida = 0;
          #38#0 = aEscan;
          #38#1 = nNivelEscan;
          |LEE 000#38=;
          |SI FSalida ! 0;
               |GRABA 020#38n;
          |FINSI;
     |SINO;
          |SI #44#2 = "S";
               |HAZ EsTecatel;
               |SI FSalida = 0; |Y #142#9 = "S";
                    #17#0 = #1#2;
                    #17#1 = #1#9;
                    |LEE 000#17=;
                    |SI FSalida = 0; |Y #17#69 > 0;
                         |HAZ LeeMulti;
                         |LEE 101#1=;
                         #1#5 = #17#69;
                         #1#15 = #1#5 / nFactor;
                         |GRABA 020#1a;
                         |LIBERA #1;
                    |FINSI;
               |SINO;
                    #2#0 = #1#2;
                    |LEE 000#2=;
                    |SI FSalida = 0; |Y #2#9 > 0;
                         |HAZ LeeMulti;
                         |LEE 101#1=;
                         |SI nSwRodacal = 0;
                              |SI #2#9 ! 0; #1#5 = #2#9; |FINSI;
                              |SI #2#133 ! 0; #1#5 = #2#133; |FINSI;
                         |SINO;
                              #1#5 = #2#9; || La estandar
                         |FINSI;
                         #1#15 = #1#5 / nFactor;
                         |GRABA 020#1a;
                         |LIBERA #1;
                    |FINSI;
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO agifa048;
     |DELINDEX #38;
     |ABRE #38;
     aEscan = #agifa048@#0;
     |HAZ DesglosaEscan;
     |CIERRA #38;
     |BUCLE dsw00038;

     #39#0 = aEscan;
     |LEE 000#39=;
     |SI FSalida ! 0;
          |HAZ CalcuPrecio;
          |GRABA 020#39n;
     |FINSI;
|FPRC;

|DEFBUCLE agifa048;
     #agifa048@#1001;
     ;
     #44#0;
     #44#1;
     ;
     NULL, agifa048;
|FIN;

|PROCESO Chequeo;
     #2#0 = #1#2;
     |LEE 101#2=;
     |SI FSalida = 0;
          |SI #2#5 = #2#207; |O #2#204 = "N";
               |SI #1#12 ! #1#4;
                    |SI nSwImp = 1;
                         |PRINT;
                    |SINO;
                         |SI nSwCorri = 1;
                              #1#4 = #1#12;    || Corrige Unidades
                              |GRABA 020#1a;
                              |LIBERA #1;
                              |SI #2#204 = "N"; || Corrige Unidad en Articulo
                                   #2#205 = #2#5;
                                   #2#206 = #2#5;
                                   #2#207 = #2#5;
                                   |GRABA 020#2a;
                              |FINSI;
                         |SINO;
                              |ERROR10;
                              nSwCheq = 1;
                         |FINSI;
                    |FINSI;
               |FINSI;
          |FINSI;
     |FINSI;
     |LIBERA #2;
|FPRC;

|DEFBUCLE Chequeo;
     #1#1;
     ;
     INICIO;
     FINAL;
     be;
     NULL, Chequeo;
|FIN;

|PROCESO Cheque;
     nSwCheq = 0;
     |ABRE #2;
     |BUCLE Chequeo;
     |CIERRA #2;
     |SI nSwCheq = 1;
          |ATRI S;
          |PINTA 2236, "ERROR EN UNIDADES....";
          |PINTA 2336, "컴컴컴컴컴컴컴컴컴컴";
          |ATRI 0;
          |AVISO;
          |MENU aMenu;
          aMenu2 = FSalida;
          |SI aMenu2 = 2;
               nSwCorri = 1;
               |ABRE #2;
               |BUCLE Chequeo;
               |CIERRA #2;
               nSwCorri = 0;
               nSwCheq = 0;
          |FINSI;
          |SI aMenu2 = 3;
               |IMPRE 0;
               |SI FSalida = 0;
                    |INFOR "agifa044";
                    |SI FSalida = 0;
                         nSwImp = 1;
                         |ABRE #2;
                         |BUCLE Chequeo;
                         |CIERRA #2;
                         nSwImp = 0;
                         |PIEINF;
                         |FININF;
                    |FINSI;
                    |FINIMP;
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Tipo3; |TIPO 3;
     |SI aParam ! "rodacal"; |Y aParam ! "mecanogra";
          |HAZ Cheque;
     |FINSI;

     |SI nSwCheq = 1; |ACABA; |FINSI;

     |HAZ CreaTempo; aTempo38 = Tempo;
     |NOME_DAT #38 aTempo38; |DELINDEX #38;

     |HAZ CreaTempo; aTempo39 = Tempo;
     |NOME_DAT #39 aTempo39; |DELINDEX #39;

     |DDEF aDef;
     aDef = aDef + "agifa048";
     |CARGA_DEF aDef, agifa048@;
     |SI FSalida = 0;
          |ABRE #46; |LEE 000#46p; |CIERRA #46;
          nDeci_EsCab = #46#15;
          nDeci_EsLin = #46#16;

          |ABRE #2;
          |ABRE #39;
          |ABRE #0;
          |ABRE #17;
          |BUCLE agifa048;
          |CIERRA #17;
          |CIERRA #0;
          |CIERRA #39;
          |CIERRA #2;
          |DESCARGA_DEF #agifa048@;
     |SINO;
          aAlfa = "0000Error al cargar " + aDef;
          |MENAV aAlfa;
     |FINSI;

     |DELINDEX #38; Tempo = aTempo38; |HAZ BoraTempo;
     |DELINDEX #39; Tempo = aTempo39; |HAZ BoraTempo;
|FPRC;

|PROGRAMA;
     aParam = PARAMETRO; |QBF aParam;

     |HAZ EsRodacal;
     nSwRodacal = FSalida;

     |HAZ EsMecanogra;
     nSwMecanogra = FSalida;

     |ABRE #142; |LEE 000#142p; |CIERRA #142;
     |ABRE #agifa324;
     |ABRE #agifa321;
     |LEE 001#agifa321.p;
     #agifa324#0 = #agifa321#9;    ||Igualo a moneda base
     |LEE 001#agifa324.=;
     nDeci_Base = #agifa324#7;     ||Decimales moneda base
     nDeci_PreBase = #agifa324#9;  ||Decimales precio de moneda base
     |CIERRA #agifa321;
     |CIERRA #agifa324;
     |SI aParam = "";
          |MANTE #44;
     |SINO;
          |SI aParam = "rodacal"; |O aParam = "tagz0009";
               #44#0 = eaEscan;
               #44#1 = eaEscan;
          |FINSI;
          |SI aParam = "mecanogra";
               #44#0 = eaEscan;
               #44#1 = eaEscan;
               #44#2 = "S";
          |FINSI;
          |HAZ Tipo3;
     |FINSI;
|FPRO;

|PROCESO Llena044; |TIPO 6;
     eaArticulo = #44Campo;
     |HAZ LlenaArticulo;
     |SI FSalida = 0;
          #44Campo = eaArticulo;
          |PINTA #44Campo;
     |SINO;
          |ERROR; |ACABA;
     |FINSI;
|FPRC;

|PROCESO MecanoFilm;
     |DDEF aAlfa;
     aAlfa = aAlfa + "mecam004";
     |CARGA_DEF aAlfa, referen@;
     |SI FSalida = 0;
          |DDEFECTO #referen@;
          |ABRE #referen@;
          #referen@#0 = #1#0;
          #referen@#1 = #1#1;
          |LEE 000#referen@.=;
          |CIERRA #referen@;
          aFilm = #referen@#2;
          |DESCARGA_DEF #referen@;
     |FINSI;
|FPRC;
