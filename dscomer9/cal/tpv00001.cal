******* ­­­ EL ARTICULO blanco  NO SE PUEDE USAR !!! *******

******
****** P R O G R A M A  COMUN PARA tpv00001 y tpv00006
|FICHEROS;
     tpv00001 #0;        || Este def.
     tpv00002 #1,MANTE;  || Temporal Mesa
     agifa019 #2;        || Articulos.
     agifa505 #3;        || Parametros caja.
     agifa514 #4;        || Cajeros.
     tpa00001 #5,MANTE;  || Pantalla cobros.
     tpv00003 #6,MANTE;  || Tiquet Impresos
     agifa007 #7;        || Series.
     agifa027 #8;        || Contadores.
     tpv00031 #9;        || Bloqueo
     tpv00012 #10;       || Tarifas Especiales

     agifa017 #11;       || Articulos por almacen.
     agifa065 #12;       || Historico.
     agifa401 #13;||=agifa501 Tiquet(C).
     agifa502 #14;       || Tiquet(L).

     agifa510 #17;       || Horario
     agifa509 #18;       || Resumen
     agifa515 #20;       || Tarjetas
     agifa516 #21;       || Diario Tarjetas
     tpv00005 #22,MANTE; || Panta Ser/Num
     agifa024 #23;       || Cliente
     tpa00002 #24,MANTE; || Pantalla Cliente
     agifa571 #25;       || Creditos (C)
     agifa572 #26;       || Creditos (L)
     tpv00013 #27;       || Cargos Habi (C)
     tpv00014 #28;       || Cargos Habi (L)
     tpv00016 #30,MANTE; || Igual a este def mas campo de pase.
     tpv00017 #31;       || Pantalla aviso cierre.
     agifa517 #32;       || Parametros T.P.V
     agifa142 #142;      || Parametros Generales
     tpv00032 #33;       || Tarifas Mesas
     dsl00001 #40;       || C.B multi
     agifa321;
     agifa324;

     referen@ #100;
     tpv01001 #1001;     || Pantalla para alimentacion
     tpa00003 #55;
|FIN;

|VARIABLES;
     aGuarda = "";
     nSwPrecio = 0;
     nUltimo = 0;
     aSesion = "";
     nLin = 0;
     &enPreTPV  = 0; || PRECIO TPV
     &eaIVAsn   = "";|| IVA incluido S/N
     FicPant = "";
     nSW_CB = 0;
     aLon = "";
     nLon = 0;
     nLon1= 0;
     aAlf1 = "";
     aAlf2 = "";
     &nDeci_PreDiv  = 0;
     &nDeci_Base = 0;
     &nDeci_PreBase = 0;
     &nDeci_PreVis = 0;

     &eaTiVisor= "";
     &eaParametro = "";       ||Parametro
     aAlfa     = "";
     aEsEuro   = "";
     nNume     = 0;
     sw_ctrl_c = 0;
     &Tempo    = "";
     aTempo30  = "";
     aTempo1   = "";
     nTOTAL    = 0;
     nTOTAL_IVA= 0;
     nLINEAS   = 0;
     nNoPasa   = 0;
     nMesaBloq = -1;
     nPenLin   = 0;
     nSwCajon  = 0;
     &nDeci_UniTpv = 0;
     aIvaSN    = "";
     nGuarda   = 0;
     nMargen   = 0;
     nTipoIVA  = 0;
     nTota     = 0;
     nPrecio   = 0;
     nPreNeto  = 0;
     nBase     = 0;
     nCuota    = 0;
     fi      = "";
     Mensa  = "N";                 || Mensajes       (para pruebas)
     nImpo   = 0;
     mes    = 0;
     fmes   = "";
     nSwMesa = 0;

     TCOBIMP = 9;   || <F1>
     TCAJON  = 10;  || <F2>
     TSERVI  = 11;  || <F3>
     TBORLIN = 12;  || <F4>
     TIMPRE  = 13;  || <F5>       || IMPRIME SIN COBRAR.
     TTIQUET = 14;  || <F6>
     TMESAS  = 15;  || <F7>
     TCREDI  = 16;  || <F8>
     TIQIMP  = 17;  || <F9>
     TCONSUL = 18;  || <F10>
     TCOBIMS = 20;  || <F2> + Control.
     TIMPRPA = 23;  || <F5> + Control.   || IMPRIME S/COB Y PASA A TICKET.
|||||TSALIV  = 21;  || <F3> + Control.
     &TVisor  = 0;
     &cVisor  = "";

     &impr_tpv = "";
     &impr_dis = "";
     &abre_dis = "";
     &mens_dis = "";
     &cier_dis = "";
     &forma    = ""

     &acaja = 0;
     &afecha = "";
     &empresa = 0;
     &camarero = 0;
     &mesas = "";
     mensaje = "";
     &xhabita = 0;
     &xnombre = "";
     &xhuespe = "";
     &xhotel = 0;

     aMensa  = "";
     emp  = "";
     apli = "";
     minu_ant = 0;
     fichero = "";
     path = "";
     swvic = 0;
     vic = 0;

     nabo = 0;
     totpavi = 0;
     lindesco = 0;
     tiim    = 0;
     nNumero = 0;
     aSerie = "";
     cGuarda = "";
     nTecla = 0;
     nEstado = 0;
     nTotal = 0;
     nDife = 0;
     nEfe = 0;
     nTar = 0;
     nChe = 0;
     nDeu = 0;
     nHab = 0;
     nRot = 0;
     nIca = 0;
     nIcl = 0;
     nCobrar = 0;
     nCambio = 0;
     nMes = 0;

     nMesaAnt = 0;
     MesaElige = 0;
     ModoLineal = 0;
     ClaveLinea = 1;

     campo = 0;
     err = 0;
     sw = 0;
     i = 0;
     abo = 0;
     precio = 0;
     base = 0;
     iva   = 0;
     total = 0;
     linea = 0;
     lincre = 0;
     periodo = 0;
     nuevo = 0;
     alf1 = "";
     informe = "";
     informl = "";
     informp = "";
     almacen = 0;
     nModo = 0;
     nTipo = 0;
     nMesa = 0;
     nComensal = 0;
     cTex1 = "";
     cTex2 = "";
     cTex3 = "";
     cHora = "";
     cHH = "";
     cMM = "";
     nHora = 0;

     form0 = "";
     nume1 = 0;
     nume2 = 0;
     bet0 = 0;
     pos0 = 0;
     lon0 = 0;
     longi = "";
     cam0 = 0;
     text0 = "";
     texto = "";
     fechasis = @;
     vhora = "";
     vh = 0;
     vha = "";
     swcierra = 0;
{-1}menu = "";
    menu1 = "2400";
    menu2 = "1";
    menu3 = "Elija: ";
    menu4 = "CRMB";
    menu5 = " Cobrar ";
    menu6 = " Reimprimir ";
    menu7 = " Modificar ";
    menu8 = " Borrar ";

{-1}menuSN  = "";
    menuSN1 = "2400";
    menuSN2 = "2";
    menuSN3 = " CONFIRMAR.:";
    menuSN4 = "SN";
    menuSN5 = " Si ";
    menuSN6 = " No ";

{-1}menub = "";
    menub1 = "2400";
    menub2 = "1";
    menub3 = "Elija: ";
    menub4 = "ARC";
    menub5 = " Anula ";
    menub6 = " Reintenta ";
    menub7 = " Continua ";
    &EURODB = 0;
    aMensaje = "";
    cabeza    = 0;
    infocab                  = "";
    infolin                  = "";
    infopie                  = "";
    aParam = "";
    aBarra = "";
    aContenido = "";
    aUni = "";
    aCod = "";
    aSW  = "";
    aUniCod = "";

     &enMensaje = 0;
     &enPromoTPV1 = 0;
     &enPromoTPV2 = 0;
     &uni_dt = 0;
     &fed_dt = @;   || Fecha....
     &art_dt = "";  || Articulo.
     &cli_dt = "";  || Cliente..
     &fam_dt = "";  || Familia.
     &iva_dt = 0;   || Tipo Iva.
     &dtos_2 = 0;   || Descuento 2.
     &dto_pt = 0;   || Descuento Ptas.
     &dtos_1 = 0;   || Descuento 1.
     &pvp_ve = 0;   || Precio Venta.
     &gru_dt = "";  || Grupo Cliente.
     &tarifa = 0;   || Tarifa Cliente.
     &presel = 0;   || tarifa Seleccion
     &dtosel = 0;   || dto    Seleccion
     aValorAntiguo = "";
     aValorIni = "";
     long = 0;
     beta = 0;
     alfa = "";
|FIN;

|INCLUYE i_varart;
-----------------------------------------------------------------------------
|PROCESO DefectoCaja;              ||FUERZA CAMPOS NO MODIFICABLES
     |SI eaParametro = "alimenta";
          #3#15 = "N"; ||PVP Modi
          #3#10 = "N"; ||Uni Modi
          #3#14 = "N"; ||Des Modi
          #3#32 = "S"; ||Imp. Directo
     |FINSI;
|FPRC;

|PROCESO tipo0_lin; |TIPO 0;
     |SI FSalida = 12; |Y eaParametro ! "alimenta"; ||F4 Borra Linea
          |PTEC 802;
          |PTEC 811;
     |SINO;
          |HAZ Funciones;
     |FINSI;
|FPRC;

|PROCESO tipo11_lin; |TIPO 11;
     |SI FSalida = 7; |Y sw_ctrl_c = 0;
          |ERROR;
          |PTEC 806;          || SIMULA ESC
     |FINSI;
     sw_ctrl_c = 0;
|FPRC;

|PROCESO tipo7_lin; |TIPO 7;
     nModo = (FEntrada / 100) ! 0;
     |C_M #0#0, 1, "N";
     |SI nModo = 1; |Y FSalida ! 7;
          #0#7 = camarero;
          #0#12 = #3#127; || Uni2
          #0#13 = #3#126; ||Partida
          TVisor = nTOTAL;
          |HAZ Numero;
     |FINSI;
     |SI nModo = 2;
          |SI #3#10 = "S"; |C_M #0#4, 1, "S"; |FINSI;
          |SI #3#14 = "S"; |C_M #0#3, 1, "S"; |FINSI;
          |SI #3#15 = "S"; |C_M #0#5, 1, "S"; |FINSI;
          |SI #3#17 = "S"; |C_M #0#6, 1, "S"; |FINSI;
     |SINO;
          |SI #3#10 = "S";
               |C_M #0#4, 1, "S";
          |SINO;
               |C_M #0#4, 1, "N";
          |FINSI;
          |C_M #0#3, 1, "N";
          |C_M #0#5, 1, "N";
          |C_M #0#6, 1, "N";
     |FINSI;

     |SI eaParametro = "alimenta";
          |ABRE #2;
          #2#0 = #0#2;
          |LEE 000#2=;
          |CIERRA #2;
          |ATRI S;
          |PINTA 0746, #2#128; ||aBarra;
          |ATRI 0;
     |FINSI;
|FPRC;

|PROCESO tipo2_lin; |TIPO 2;
     #0#16 = nComensal;
     cGuarda = FSalida;
     nModo = (FEntrada / 100) ! 0;
     nTipo = 0 +. 1;

     |SI nTipo = -1; |Y #agifa505#32 = "S";
          aMensaje = "0000Modificacion de linea no permitida. Cree una linea en negativo.";
          |HAZ DesactivaScaner;
          |MENAV aMensaje;
          |HAZ ActivaScaner;
          |ERROR;
          |ACABA;
     |FINSI;
     |SI nModo = 1; |Y #0#5 = 0;
          |AVISO;
          |C_M #0#5, 1, "S";
          nSwPrecio = 1; || Para que no se quede pillao.... :)
          |PARA FSalida = 1; |SI FSalida ! 0; |HACIENDO;
               |ENCAMPO #5#0;
          |SIGUE;
          nSwPrecio = 0;
     |FINSI;

     |SI nTipo = 1;
          aAlfa = #0#2; |QBF aAlfa;
          |SI #agifa505#32 = "S"; |Y nModo = 1; |Y aAlfa ! "";
               |HAZ abre_impre;
               |HAZ ImpLinDir;     ||Impresion Directa Linea
          |FINSI;
          |SI nModo = 1;
               |PTEC 802;          ||Salta Linea
          |FINSI;
     |FINSI;
     nabo = 0 +. 1; |HAZ ActMesa;
     |SI nTipo = 1;
          |HAZ lin_display;
     |FINSI;
     TVisor = nTOTAL;
     |HAZ Numero;
     FSalida = cGuarda;
|FPRC;
-----------------------------------------------------------------------------
|PROCESO tipo0; |TIPO 0;
     cGuarda = FSalida;
     |SI Campo = 2;
          |SI eaParametro = "alimenta";
               fed_dt = afecha; || Fecha....
               art_dt = #0#2;   || Articulo.
               cli_dt = #3#36;  || Cliente..
               fam_dt = #2#2;   || Familia.
               iva_dt = #2#30;  || Tipo Iva.
               dtos_2 = 0;      || Descuento 2.
               dto_pt = 0;      || Descuento Ptas.
               dtos_1 = 0;      || Descuento 1.
               pvp_ve = 0;      || Precio Venta.
               tarifa = #23#39; || Tarifa Cliente....
               gru_dt = #23#158;|| Grupo Cliente.
               presel = 0;      || Precio seleccion.
               dtosel = 0;      || Dto      '' ''
               uni_dt = #0#4;
               enMensaje = 0;
               aParam = "";
               eaSerie = #3#105;
               |SI #3#146 = "S";  enPromoTPV1 = 1; |FINSI;
               |SI #3#147 = "S";  enPromoTPV2 = 1; |FINSI;
               enDirEnt = 0;
               enAlmacen = #0#9;
               |HAZ calcdtos;
               precio = enPreTPV - (enPreTPV * dtos_1 / 100);
               aIvaSN = eaIVAsn;
               |SI #3#165 = "S";
                    #0#11 = #2#30;
               |SINO;
                    #0#11 = #3#128;
               |FINSI;
          |SINO;
               precio = #2#18;
               |SI #3#125 = 1; precio = #2#18; aIvaSN = #2#130; |FINSI;
               |SI #3#125 = 2; precio = #2#19; aIvaSN = #2#131; |FINSI;
               |SI #3#125 = 3; precio = #2#20; aIvaSN = #2#132; |FINSI;
               |SI #3#125 = 4; precio = #2#147; aIvaSN = #2#153; |FINSI;
               |SI #3#125 = 5; precio = #2#148; aIvaSN = #2#154; |FINSI;
               |SI #3#125 = 6; precio = #2#149; aIvaSN = #2#155; |FINSI;
               |SI #3#165 = "S";
                    #0#11 = #2#30;
               |SINO;
                    #0#11 = #3#128;
               |FINSI;
               |ABRE #33;
               #33#0 = nMesa;
               |LEE 000#33=;
               |SI FSalida = 0;
                    |SI #33#1 = 1; precio = #2#18;  aIvaSN = #2#130; |FINSI;
                    |SI #33#1 = 2; precio = #2#19;  aIvaSN = #2#131; |FINSI;
                    |SI #33#1 = 3; precio = #2#20;  aIvaSN = #2#132; |FINSI;
                    |SI #33#1 = 4; precio = #2#147; aIvaSN = #2#153; |FINSI;
                    |SI #33#1 = 5; precio = #2#148; aIvaSN = #2#154; |FINSI;
                    |SI #33#1 = 6; precio = #2#149; aIvaSN = #2#155; |FINSI;
               |FINSI;
               |CIERRA #33;
          |FINSI;
          |SI #0Campo ! Contenido; |Y nSW_CB = 0;
               |HAZ MiraPrecio;
               |HAZ IVAPrecio;
               #0#5 = precio; |PINTA #0#5;
          |FINSI;

          alfa = #0#3; |QBF alfa;
          |SI alfa = ""; |O #0Campo ! Contenido;
               #0#3 = #2#1; |PINTA #0#3;
          |FINSI;
     |FINSI;
     |SI Campo = 5;
          #0#6 = #0#4 * #0#5; |PINTA #0#6;
          aAlfa = Contenido; |QBT aAlfa;
          |SI aAlfa ! #0#5; |O nSW_CB = 1;
               |SI aIvaSN = "S";
                    #0#17 = #0#5;
                    #0#18 = #0#5 / (1 + (enIva / 100));
               |SINO;
                    #0#18 = #0#5;
                    #0#17 = #0#5 > enIva;
               |FINSI;
               |SI #3#109 = "S";
                    #0#5 = #0#17;
               |SINO;
                    #0#5 = #0#18;
               |FINSI;
               |PINTA #0#5;
          |FINSI;
     |FINSI;
     FSalida = cGuarda;
|FPRC;

|PROCESO tipo7; |TIPO 7;
     |SI Campo = 2;
          nSW_CB = 0;
     |FINSI;
     |SI Campo = 4; |Y nModo = 1;
          #0#4 = #3#11;
          |PINTA #0#4;
     |FINSI;
|FPRC;

|PROCESO UniCod;
     aLon = (aUniCod % A0) + "01";
     aUni = "";
     aCod = "";
     aSW  = "";
     |PARA i = 101; |SI i [ aLon; |HACIENDO i = i + 100;
          aAlfa = aUniCod % i;
          |SI aAlfa = "*"; |O aAlfa = "+";
               aSW = aAlfa;
          |SINO;
               |SI aSW = "";
                    aUni = aUni + aAlfa;
               |SINO;
                    aCod = aCod + aAlfa;
               |FINSI;
          |FINSI;
     |SIGUE;
     |QBF aCod;
|FPRC;

|PROCESO CodBarras;
     aMensaje = "";
     |ABRE #40;
     |ABRE #2;
     |SELECT #4#2;
     #2#128 = #0#2;
     |LEE 000#2=;
     |SI FSalida = 0;
          aBarra = #0#2; ||Es un codigo de Barras
          #0#2 = #2#0;
          |PINTA #0#2;
          |SELECT #2#40;
          #40#3 = #2#128;
          |LEE 000#40=;
          |SI FSalida = 0;
               |SI #40#6 ! 0;
                    #0#4 = #0#4 * #40#6;
                    |PINTA #0#4;
               |FINSI;
          |FINSI;
     |SINO;
          |SELECT #1#2;
          |SELECT #2#40;
          #40#3 = #0#2;
          |LEE 000#40=;
          |SI FSalida = 0;
               aBarra = #0#2; ||Es un codigo de Barras
               #0#2 = #40#1;
               |SI #40#6 ! 0;
                    #0#4 = #0#4 * #40#6;
                    |PINTA #0#4;
               |FINSI;
               |PINTA #0#2;
               #2#0 = #0#2;
               |LEE 000#2=;
          |SINO;
               #2#0 = #0#2;
               |LEE 101#2=;
               |SI FSalida = 0;
                    aBarra = #2#128;
               |SINO;
                    aMensaje = "0000El articulo [" + #0#2 + "] no existe en la Base de Datos.";
                    |HAZ DesactivaScaner;
                    |MENAV aMensaje;
                    |HAZ ActivaScaner;
                    |ERROR;
                    |ACABA;
               |FINSI;
          |FINSI;
     |FINSI;
     |CIERRA #40;
     |CIERRA #2;
|FPRC;

|PROCESO MuestraPrecio;
     |PUSHV 0501 2380;
     |BLANCO 15022126;
     |CUADRO 15022126;
     |ATRI R;
     |PINTA 1603, " CONSULTA PRECIO C.B.  ";
     |PINTA 1803, " C.B.:";
     |PINTA 2003, " Precio:";
     |ATRI 0;
     E_sup = 13;
     E_inf = "";
     #0#2 = 1810  ? 1;
     |HAZ CodBarras;
     |SI aMensaje = "";
          fed_dt = afecha; || Fecha....
          art_dt = #0#2;   || Articulo.
          cli_dt = #3#36;  || Cliente..
          fam_dt = #2#2;   || Familia.
          iva_dt = #2#30;  || Tipo Iva.
          dtos_2 = 0;      || Descuento 2.
          dto_pt = 0;      || Descuento Ptas.
          dtos_1 = 0;      || Descuento 1.
          pvp_ve = 0;      || Precio Venta.
          tarifa = #23#39; || Tarifa Cliente....
          gru_dt = #23#158;|| Grupo Cliente.
          presel = 0;      || Precio seleccion.
          dtosel = 0;      || Dto      '' ''
          uni_dt = #0#4;
          enMensaje = 0;
          aParam = "";
          eaSerie = #3#105;
          |SI #3#146 = "S";  enPromoTPV1 = 1; |FINSI;
          |SI #3#147 = "S";  enPromoTPV2 = 1; |FINSI;
          |HAZ calcdtos;
          precio = enPreTPV;
          aIvaSN = eaIVAsn;
          precio = precio ! EURODB;
          |ATRI S;
          |PINTA 2012, precio;
          |ATRI 0;
          |PAUSA;
     |FINSI;
     |POPV;
|FPRC;

|PROCESO tipo6; |TIPO 6;
     |SI nSwPrecio ! 0;     ||  Para que no se quede pillao.... :)
          |SI FSalida ! 0;  ||  solo permite ENTER
               |ACABA;
          |FINSI;
     |FINSI;

     |SI nModo = 1; |Y #0#2 = "                    ";
               |Y Campo = 2; |Y FSalida = 0;
          |AVISO;
          |ERROR;
          |ACABA;
     |FINSI;
     |SI Campo = 2; |Y eaParametro = "alimenta";
          nGuarda = FSalida;
          |SI FSalida = 12;  ||F4;
               |HAZ MuestraPrecio;
               |ERROR;
               |ACABA;
          |FINSI;
          aUniCod = #0#2; |QBT aUniCod;
          aAlfa = aUniCod - "*";
          |SI aAlfa ! aUniCod;
               |HAZ UniCod;
               #0#4 = aUni;
               #0#2 = aCod;
               |PINTA #0#4;
               |PINTA #0#2;
               |SI aCod = ""; |ERROR;   |FINSI;
          |SINO;
               aAlfa = aUniCod - "+";
               |SI aAlfa ! aUniCod;
                    |HAZ UniCod;
                    #0#4 = aUni;
                    #0#2 = aCod;
                    |PINTA #0#4;
                    |PINTA #0#2;
                    |SI aCod = ""; |ERROR;   |FINSI;
               |FINSI;
          |FINSI;
          FSalida = nGuarda;
     |FINSI;
     |SI Campo = 2;
          aContenido = Contenido;
          nGuarda = FSalida;
          |HAZ CB_Compuesto;
          |SI nSW_CB = 0;
               |HAZ CodBarras;
               |SI eaParametro ! "alimenta";
                    |HAZ DobleArtiV;    || alimentacion sin doble stock
               |FINSI;
               enAlta = 1;
               eaArticulo = #0Campo;
               |HAZ LlenaArticulo;
               |SI FSalida = 0;
                    #0Campo = eaArticulo;
                    |PINTA #0Campo;
               |SINO;
                    |ERROR; |ACABA;
               |FINSI;
          |FINSI;
          FSalida = nGuarda;
     |SINO;
          aContenido = "";
     |FINSI;

     |SI FSalida = 5;                           || Re-Pag
          |SI #0#2 = "                    ";
               |ERROR;
          |FINSI;
     |FINSI;
     |SI FSalida = 4;                            || Av-Pag
          |SI #0#2 = "                    ";
               |ERROR;
               |PTEC 808;                        || Simula Flecha Arriba
          |FINSI;
     |FINSI;
     |SI FSalida = 1;                          || Esc = Cobro de Servicio.
          FSalida = TCOBIMP;
     |FINSI;
     |SI FSalida = 2;                             || Flecha Arriba
          |ERROR;
          |PTEC 808;               ||       -> Arriba
          |PTEC 807;               ||       -> Ctrl-C
          sw_ctrl_c = 1;
     |FINSI;
     |SI FSalida = 3; |Y #0#2 ! "                    "; || Flecha Abajo
          |C_M #0#4, 1, "N";
          |C_M #0#2, 1, "N";
          |C_M #0#3, 1, "N";
          |C_M #0#5, 1, "N";
          |C_M #0#6, 1, "N";
     |FINSI;
     |HAZ Funciones;
|FPRC;

|PROCESO Funciones;
     |SI FSalida = TCAJON;
          |HAZ AbreCajon;                        || 2 veces
          |HAZ AbreCajon;
          |ERROR;
          |ACABA;
     |FINSI;
     |SI FSalida = TSERVI;
          |SI nMesa = 0; |O mesas = "N";
               |SI nLINEAS > 0;
                    |MENAV "0000DEBE COBRAR EL SERVICIO";
                    |ERROR;
                    |ACABA;
               |FINSI;
          |FINSI;
     |FINSI;
     |SI FSalida = TCONSUL;
          |PUSHV 0101 2380;
          |CLS;
          |BLANCO 0501 2380;
          |ABRE #13;
          |DDEFECTO #13;
          |SELECT #3#13;
          #13#36 = aSerie;
          #13#14 = afecha;
          |LEE 000#13];
          |SELECT #1#13;
          |LEE 000#13=;
          |PINPA #0#13;
          |PINDA #0#13;
          |CONSULTA #0#13;
          |CIERRA #13;
          |POPV;
          |ERROR;
          |ACABA;
     |FINSI;
     |SI FSalida = TMESAS;
          |SI eaParametro = "alimenta";
               |HAZ DesactivaScaner;
               |PUSHV 05012380;
               |BLANCO 1344 1969;
               |ATRI I; |PINTA 1648, "B L O Q U E A D O "; |ATRI 0;
               |PARA aAlfa = ""; |SI aAlfa ! 829;|HACIENDO;
                    |LEETECLA aAlfa;
                    aAlfa = aAlfa % 203;
               |SIGUE;
               |POPV;
               |HAZ ActivaScaner;
               |ERROR;
          |SINO;
               |HAZ MesaOcupa;
               |ERROR;
               |SI MesaElige ! -1;
                    |PTEC 825;       || Simula F3   Cambio de Servicio
               |FINSI;
          |FINSI;
          |ACABA;
     |FINSI;
     |SI FSalida = TCREDI;
          |PTEC 802;
          |PTEC 811;
          |SUB_EJECUTA_NP "agifa571";
          |ERROR;
          |ACABA;
     |FINSI;
     |SI FSalida ] 9; |Y FSalida [ 23;
          |ERROR;
          |PTEC 806;   ||       -> Escape
          |PTEC 807;   ||       -> Ctrl-C
          nTecla = FSalida;
          ClaveLinea = #0#0;
          sw_ctrl_c = 1;
     |FINSI;
|FPRC;
-----------------------------------------------------------------------------
|PROCESO IVAPrecio;
     nTipoIVA = #0#11;
     |HAZ LeeIVA;

     |SI #3#109 = "N";        || Ver precio sin iva
          |SI aIvaSN = "S";
               #0#17 = precio;
               precio = precio / (1 + (enIva / 100));
               #0#18 = precio;
          |SINO;
               #0#18 = precio;
               #0#17 = precio > enIva;
          |FINSI;
     |SINO;                   || Ver precio con iva
          |SI aIvaSN = "N";
               #0#18 = precio;
               precio = precio > enIva;
               #0#17 = precio;
          |SINO;
               #0#17 = precio;
               #0#18 = precio / (1 + (enIva / 100));
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO MiraPrecio;
     #0#10 = "N";
     |HORA cHora;
     cHH = cHora % 102;
     cMM = cHora % 402;
     cHora = cHH + cMM;
     nHora = cHora;
     |SI #3#97 ! "S"; |ACABA; |FINSI;        || No usa tarifa especial
     |ABRE #10;
     #10#0 = #0#2;
     #10#1 = #3#98;
     |LEE 000#10=;
     |SI FSalida = 0;
          #0#10 = "S";
          precio = #10#2;
          |SI #10#3 = "S";                    || hora feliz
               nuevo = 0;
               |SI nHora ] #10#4; |Y nHora [ #10#5;
                    nuevo = #10#6;
               |SINO;
                    |SI nHora ] #10#7; |Y nHora [ #10#8;
                         nuevo = #10#9;
                    |SINO;
                         |SI nHora ] #10#10; |Y nHora [ #10#11;
                              nuevo = #10#12;
                         |SINO;
                              |SI nHora ] #10#13; |Y nHora [ #10#14;
                                   nuevo = #10#15;
                              |FINSI;
                         |FINSI;
                    |FINSI;
               |FINSI;
               |SI nuevo ! 0;
                    |SI #3#99 = "S";
                         |PUSHV 0501 2380;
                         |COLOR 0, 0; |ATRI 0;
                         |BLANCO 1128 1552;
                         |COLOR 14, 0; |ATRI 0;
                         |CUADRO 1128 1552;
                         |COLOR 14, 0; |ATRI P;
                         |PINTA 1332, "PRECIO HAPPY HOUR";
                         |ATRI 0;
                  |ET OtroSN;
                         |MENU menuSN;
                         |SI FSalida < 1; |VETE OtroSN; |FINSI;
                         |SI FSalida = 1;
                              Contenido = "";
                              precio = nuevo;
                              #0#10 = "H";
                         |FINSI;
                         |POPV;
                    |SINO;
                         Contenido = "";
                         precio = nuevo;
                         #0#10 = "H";
                    |FINSI;
               |FINSI;
          |FINSI;
     |FINSI;
     |CIERRA #10;
|FPRC;
-----------------------------------------------------------------------------
|PROCESO ini_display; |TIPO 0;
     texto = abre_dis + mens_dis + cier_dis;
     |SI mens_dis ! ""; |Y impr_dis ! "";
          Impresora = impr_dis;
          |IMPRE -1;
          |IMPRIMESIN texto;
          |FINIMP;
     |FINSI;
|FPRC;

|PROCESO total_display;
     |SI impr_dis ! "";
          texto = " " * #3#67;
          texto = texto + nCobrar;
          alfa = "-"  + forma;
          texto = texto % alfa;
          nume1 = forma; nume1 = nume1 - 7;
          alfa = nume1;  alfa = "-" + alfa;
          texto = texto % alfa;
          texto = "TOTAL: " + texto;
          texto = abre_dis + texto + cier_dis;
          Impresora = impr_dis;
          |IMPRE -1;
          |IMPRIMESIN texto;
          |FINIMP;
     |FINSI;
|FPRC;

|PROCESO cobro_display;
     |SI #3#68 > 1; |Y impr_dis ! ""; || Si tiene mas de 1 linea
          beta = #3#67/#3#68;
          longi = "00" + beta;
          form0 = "1" + (longi % -102);
          texto = " " * beta;
          texto = texto + nCobrar;
          alfa = "-"  + form0;
          texto = texto % alfa;
          nume1 = form0; nume1 = nume1 - 7;
          alfa = nume1;  alfa = "-" + alfa;
          texto = texto % alfa;
          text0 = "TOTAL: " + texto;
          texto = " " * beta;
          texto = texto + nCambio;
          alfa = "-"  + form0;
          texto = texto % alfa;
          nume1 = form0; nume1 = nume1 - 8;
          alfa = nume1;  alfa = "-" + alfa;
          texto = texto % alfa;
          text0 = text0 + "CAMBIO: " + texto;
          texto = abre_dis + text0 + cier_dis;
          Impresora = impr_dis;
          |IMPRE -1;
          |IMPRIMESIN texto;
          |FINIMP;
     |FINSI;
|FPRC;

|PROCESO lin_display;
     |SI impr_dis ! "";
          texto = "";
          |PARA beta = 69; |SI beta [ 81; |HACIENDO beta = beta + 3;
               |SI #3beta = 0; |SAL; |FINSI;
               pos0 = beta + 1; lon0 = beta + 2;
               longi = texto % A0; long = longi;
               bet0 = #3pos0 - long - 1;
               alfa = " " * bet0;
               texto = texto + alfa;
               alfa = " " * #3#67;
               |SI #3beta = 1; cam0 = 2; |FINSI;     || Cod
               |SI #3beta = 2; cam0 = 4; |FINSI;     || Uni
               |SI #3beta = 3; cam0 = 3; |FINSI;     || Des
               |SI #3beta = 4; cam0 = 5; |FINSI;     || Pre
               |SI #3beta = 5; cam0 = 6; |FINSI;     || Imp Linea
               |SI #3beta = 6;
                    bet0 = nTOTAL;
                    alfa = bet0 + alfa;              || Imp parcial
               |SINO;
                    alfa = #0cam0 + alfa;
               |FINSI;
               longi = "00" + #3lon0;
               form0 = "1" + (longi % -102);
               alfa = alfa % form0;
               texto = texto + alfa;
          |SIGUE;
          alfa = " " * #3#67;
          texto = texto + alfa;
          texto = texto % forma;
          texto = abre_dis + texto + cier_dis;
          Impresora = impr_dis;
          |IMPRE -1;
          |IMPRIMESIN texto;
          |FINIMP;
     |FINSI;
|FPRC;
-----------------------------------------------------------------------------
|PROCESO Limpia;
     |SI Mensa = "S"; |MENSA "2405Borrando mesa..."; |FINSI;
     |ABRE #0;
     #0#0 = 0;
     #0#1 = nMesa;
     #0#9 = almacen;
     |LEE 000#0];
     |PARA; |SI FSalida = 0; |Y #0#1 = nMesa; |Y #0#9 = almacen; |HACIENDO;
          |BORRA 020#0a;
          |LEE 000#0s;
     |SIGUE;
     |CIERRA #0;
     |SI Mensa = "S"; |BLIN 24; |FINSI;
|FPRC;

|PROCESO FichaBloqueada;
     |PUSHV  1601 2331;
     |BLANCO 1602 2231;
     |CUADRO 1602 2231;
     |COLOR 6, 1;
     |ATRI I;
     cTex1 = "LA MESA  " + nMesa +  " SE ENCUENTRA";
     cTex2 = "BLOQUEADA POR " + #9#2;
     |PINTA  1703, cTex1;
     |PINTA  1803, cTex2;
     |ATRI 0;
     |AVISO;
     |BLIN 24;
     |CONFI "2400NCONTINUAR  'asegurese de ser el unico Usuario con esta mesa' (S/N).: ";
     |SI FSalida = 0;
          nEstado = 0;
     |SINO;
          nEstado = 1;
     |FINSI;
     |POPV;
|FPRC;
-----------------------------------------------------------------------------
|PROCESO BorraLinea;
     |ABRE #0;
     #0#0 = ClaveLinea;
     #0#1 = nMesa;
     #0#9 = almacen;
     |LEE 000#0=;
     |SI FSalida = 0;
          |BORRA 000#0a;
          nabo = -1; |HAZ ActMesa;
          TVisor = nTOTAL;
          |HAZ Numero;
     |FINSI;
     |LEE 000#0a;
     |SI FSalida = 0; |Y #0#1 = nMesa; |Y #0#9 = almacen;
          ClaveLinea = #0#0;
     |SINO;
          ClaveLinea = 1;
     |FINSI;
     |CIERRA #0;
|FPRC;
-----------------------------------------------------------------------------
|PROCESO ActCab;
     #13#76 = #13#76 + #14#5; || T.Unidades
     #13#3 = #13#3 + #14#21;                     || TOTAL BRUTO.
     #13#4 = #13#4 + #14#19;                     || TOTAL IVA.
     #13#9 = #13#9 + #14#7;                      || TOTAL TIQUET.
     |SI #14#13 = 0;
          #13#30 = #13#30 + #14#21;               || BASE Exenta
     |FINSI;
     |SI #14#13 = 1;
          #13#20 = #13#20 + #14#21;               || BASE IVA 1.
          #13#21 = #13#21 + #14#19;               || CUOTA IVA 1.
     |FINSI;
     |SI #14#13 = 2;
          #13#23 = #13#23 + #14#21;               || BASE IVA 2.
          #13#24 = #13#24 + #14#19;               || CUOTA IVA 2.
     |FINSI;
     |SI #14#13 = 3;
          #13#26 = #13#26 + #14#21;               || BASE IVA 3.
          #13#27 = #13#27 + #14#19;               || CUOTA IVA 3.
     |FINSI;
     |SI #14#13 = 4;
          #13#48 = #13#48 + #14#21;               || BASE IVA 4.
          #13#49 = #13#49 + #14#19;               || CUOTA IVA 4.
     |FINSI;
     |SI #14#13 = 5;
          #13#51 = #13#51 + #14#21;               || BASE IVA 5.
          #13#52 = #13#52 + #14#19;               || CUOTA IVA 5.
     |FINSI;
     |SI #3#109 = "N";
          enTiva = #agifa502#13;
          |HAZ Recal501_Sin;
     |FINSI;
|FPRC;

|PROCESO CopiaCab;
     |DDEFECTO #20;
     |ABRE #20;
     #20#0 = #5#4;
     |LEE 000#20=;
     |CIERRA #20;

     |DDEFECTO #13;
     #13#0  = nUltimo;
     #13#36 = aSesion;
     #13#1  = #24#0;                         || CLIENTE.
     #13#10 = nEfe + nTar + nChe;            || ENTREGA.
     #13#11 = nCambio;                       || CAMBIO.
     #13#12 = acaja;                         || CAJA.
     #13#14 = afecha;                        || FECHA.
     |SI sw = 1;
          #13#15 = 0;
     |SINO;
          #13#15 = nDife;                    || Resto
     |FINSI;
     |HAZ FormaPago;
     #13#33 = camarero;                      || CAJERO.
     #13#34 = cHora;                         || HORA.
     #13#35 = "T"                            || TIPO.
     #13#37 = "";
     #13#38 = nEfe - nCambio;                || COBRADO EFECTIVO.
     #13#39 = nChe;                          || Cobrado Cheque
     #13#40 = nTar;                          || Cobra Tarjeta
     #13#41 = nDeu;                          || Deuda
     |SI nTar ! 0;
          #13#42 = #20#5;                    || Cuenta Contable Tarjeta
     |FINSI;
     #13#59 = nHab;                          || Cargo Hab.
     #13#46 = #5#4;                          || Codi Tarjeta
     #13#56 = nMesa;                         || N§ Mesa
     #13#55 = nComensal;                     || N§ Comensales.
     #13#57 = aSerie;
     #13#58 = nNumero;
     #13#60 = xhotel;                        || Hotel si es con cargo a  la  Habitacion.
     #13#61 = xhabita;                       || Habit '' ''  ''  ''   '' ''   ''  ''
     #13#65 = #24#6;                         || Cuenta Contable....

     |SI #32#20 = "S"; |Y nTar ! 0;
          #13#65 = #20#5;
     |FINSI;
     |GRABA 041#13b;
|FPRC;

|PROCESO CopiaLin;
     #2#0 = #0#2;
     |LEE 021#2=;

     nTipoIVA = #0#11;
     |HAZ LeeIVA;

     |SI #3#109 = "S";
          nPrecio = #0#5;
          nPreNeto = #0#5 / (1 + (enIva / 100));
     |SINO;
          nPreNeto = #0#5;
          nPrecio = #0#5 > enIva;
     |FINSI;
     nBase = #0#4 * nPreNeto;
     nTota = #0#4 * nPrecio;
     nCuota = nTota - nBase;

     linea = linea + 1;
     |DDEFECTO #14;
     #14#0  = #13#0;                         || NUMERO TIQUET.
     #14#20 = #13#36;                        || SERIE.
     #14#1  = linea;                         || LINEA.

     #14#2  = #0#2;                          || ARTICULO.
     #14#3  = #3#87;                         || ALMACEN.
     #14#4 =  #13#1;                         || CLIENTE
     #14#5  = #0#4;                          || UNIDADES.
     #14#6  = nPrecio;                       || PRECIO.
     #14#7  = nTota;                         || IMPORTE LINEA.
     #14#8  = #0#3;                          || DESCRIPCION.
     #14#9  = #13#14;                        || FECHA.

     #14#10 = nPreNeto;                      || PRECIO NETO.
     #14#11 = #2#2;                          || FAMILIA.
     #14#12 = #2#3;                          || SUBFAMILIA.
     #14#13 = #0#11;                         || TIPO DE IVA.
     #14#15 = acaja;                         || NUMERO DE CAJA.
     #14#16 = cHora;                         || HORA.
     #14#17 = #0#7;                          || CAJERO.
     #14#18 = "T";                           || TIPO.
     #14#21 = nBase;                         || BASE IMPONIBLE.
     #14#19 = nCuota;                        || CUOTA IVA.
     #14#22 = #2#128;                        || CODIGO DE BARRAS.
     #14#23 = #0#4 * #2#9;                   || COSTE.
     #14#26 = #0#10;                         || Tarifa Especial
     #14#29 = #0#12;                         || Uni2
     #14#30 = #0#13;                         || Partida
     #14#31 = #0#14;                         || Merma
     |GRABA 020#14n;
     |HAZ ActCab;
     |GRABA 020#13a;
|FPRC;

|PROCESO Copia;
     |SI Mensa = "S"; |MENSA "2405Copiando..."; |FINSI;
     |HAZ CopiaCab;
     #0#0 = 0;
     #0#1 = nMesa;
     #0#9 = almacen;
     |LEE 000#0];
     |PARA; |SI FSalida = 0; |Y #0#1 = nMesa; |Y #0#9 = almacen; |HACIENDO;
          |HAZ CopiaLin;
          |LEE 000#0s;
     |SIGUE;
     |SI Mensa = "S"; |BLIN 24; |FINSI;
|FPRC;
-----------------------------------------------------------------------------
|PROCESO tpa_12; |TIPO 12;
|FPRC;

|PROCESO CliDefecto;
     |DDEFECTO #23;
     |ABRE #23;
     #23#0 = #3#36;
     |LEE 001#23=;
     #24#0 = #23#0;       || Cod
     #24#1 = #23#1;       || Nom
     #24#2 = #23#15;      || NIF
     #24#3 = #23#8;       || Dir
     #24#4 = #23#9;       || Pob
     #24#5 = #23#10;      || Prov
     #24#6 = #23#16;      || Cuenta Contable.......
     |CIERRA #23;
|FPRC;

|PROCESO PideCliente;
     |PUSHV 0501 2380;
     |BLANCO 1136 1878;
     |PINPA #0#24;
     |PINDA #0#24;
     |ENDATOS #1#24;
     nEstado = FSalida;
     |POPV;
|FPRC;

|PROCESO conta_tick;
     |HORA cHora;
     nEstado = 0;
     |SI #3#39 = "S";
          |PUSHV 0101 2380;
          |D_CUADRO 0501, 1131; |BLANCO 1201 2330;
          |MENSA "0000<F1> Datos del Cliente";
          |BLANCO 1806 2025;
          |D_CUADRO 1806, 2025;
          |ATRI R;|PINTA 1706, "    SERIE TIQUET    "; |ATRI 0;
          |ATRI R; |PINTA 1907, " SERIE.:"; |ATRI 0;
        |ET OtraSerie;
          E_sup = 5;
          E_inf = "";
          aSerie = 1916 ? 1;
          nEstado = FSalida;
          |SI FSalida = 9;
               |HAZ PideCliente;
               |SI nEstado = 0;
                    |VETE OtraSerie;
               |SINO;
                    |HAZ CliDefecto;
               |FINSI;
          |FINSI;
          |SI nEstado ! 0; |POPV; |ACABA; |FINSI;
          |ABRE #7;
          #7#26 = aSerie;
          |LEE 000#7=;
          |SI FSalida ! 0;
               |MENAV "0000­­­ LA SERIE NO EXISTE... !!!";
               |VETE OtraSerie;
          |FINSI;
          |POPV;
     |FINSI;

     |SI #142#137 = "F";
          |ABRE #8;
          #8#0 = "vtiquet";
          #8#2 = aSerie;         ||Serie para tickets parametros.
          |LEE 101#8=;
          |SI FSalida ! 0;
               #8#1 = 1;
               |GRABA 020#8b;
          |FINSI;
          nNumero = #8#1;
          #8#1 = #8#1 + 1;
          |GRABA 020#8a;
          |CIERRA #8;
     |SINO;
          |SALVA_DATOS #13;
          |SELECT #2#13;
          #13#57 = aSerie;
          aSerie = #13#57;
          #13#35 = "T";
          #13#58 = 999999;
          |LEE 000#13];
          |SI FSalida = 0;
               |LEE 000#13a;
          |SINO;
               |LEE 000#13u;
          |FINSI;
          |SI FSalida = 0; |Y #13#57 = aSerie; |Y #13#35 = "T";
               nNumero = (#13#58 + 1);
          |SINO;
               nNumero = 1;
          |FINSI;
          |SELECT #1#13;
          |REPON_DATOS #13;
     |FINSI;

     |SALVA_DATOS #13;

     aSesion = #3#112;
     #13#36 = aSesion;
     #13#0 = 999999;
     |LEE 000#13];
     |SI FSalida = 0;
          |LEE 000#13a;
     |SINO;
          |LEE 000#13u;
     |FINSI;
     |SI FSalida = 0; |Y #13#36 = aSesion;
          nUltimo = #13#0 + 1;
     |SINO;
          nUltimo = 1;
     |FINSI;
     |REPON_DATOS #13;
     |LEE 101#13=;
|FPRC;

|PROCESO Pasa501_502;
     |HAZ conta_tick;
     |SI nEstado = 0;
          |ABRE #14; |ABRE #2;
          linea = 0;
          |SI vic = 0;
               |HAZ Copia;
          |SINO;
              |HAZ Copia2;
          |FINSI;
          |CIERRA #14; |CIERRA #2;
     |FINSI;
     |HAZ CliDefecto;
|FPRC;
-----------------------------------------------------------------------------
|PROCESO imprime;
     |PRINT;
|FPRC;

|DEFBUCLE imprime;
     #agifa502#1;
     ;
     #13#36, #13#0, 001;
     #13#36, #13#0, 999;
     ;
     NULL, imprime;
|FIN;

|PROCESO ImpreTiquet;
     |SI nSwCajon = 0;
          |HAZ AbreCajon;
     |FINSI;
     |SI #3#21 ! "S"; |ACABA; |FINSI;
     |SI Mensa = "S"; |MENSA "2405Imprimiendo..."; |FINSI;

     |HAZ CogeInformes;

     |SI #3#110 = "S";
          |CONFI "0000SImprimir Tiquet S/N.: ";
          |SI FSalida = 0;
               Impresora = impr_tpv;
               |IMPRE 0;
          |SINO;
               FSalida = 2;
          |FINSI;
     |SINO;
          Impresora = impr_tpv;
          |IMPRE -1;
     |FINSI;
     |SI FSalida = 0;                 ||TODO CCC
          |SI #agifa505#32 = "N";
               |SI #3#90 = 1;
                    |INFOR informe;
                    |SI FSalida = 0;
                         #13#13 = "S";
                         |GRABA 000#13a;
                         |PRINT;
                         |PIEINF; |FININF;
                    |FINSI;
                    |INFOR informl;
                    |SI FSalida = 0;
                         |BUCLE imprime;
                         |||BUCLELIN 0 imprime #13;
                         |PIEINF; |FININF;
                    |FINSI;
                    |INFOR informp;
                    |SI FSalida = 0;
                         |PRINT;
                         |PIEINF; |FININF;
                    |FINSI;
                    |FINIMP;
               |FINSI;
               |SI #3#90 = 2;
                    |INFOR informe;
                    |SI FSalida = 0;
                         #13#13 = "S";
                         |GRABA 000#13a;
                         |||BUCLELIN 0 imprime #13;
                         |HAZ imprime;
                         |PIEINF;
                         |FININF;
                    |FINSI;
                    |FINIMP;
               |FINSI;
          |SINO;
               |HAZ fin_imp;
               |SI #agifa505#33 = "S";            || Imp. Cabecera al Final
                    |HAZ abre_impre;
                |FINSI;
          |FINSI;
     |SINO;
          |SI FSalida ! 2;
               |MENAV "0000No se encuentra activada la impresora";
          |FINSI;
     |FINSI;
     |SI Mensa = "S"; |BLIN 24; |FINSI;
|FPRC;
-----------------------------------------------------------------------------
|PROCESO QueTiquet;
     |DDEFECTO #22;
     #22#0 = #3#112;
     aAlfa = ("000000" + nUltimo) % -106; || por defecto el anterior
     #22#1 = aAlfa;
     |PINPA #0#22;
     |PINDA #0#22;

     |SI #3#39 = "S";
          |C_M #22#0, 1, "S";
     |SINO;
          |C_M #22#0, 1, "N";
     |FINSI;
     |ENDATOS #1#22;

     |ABRE #13;
     #13#36 = #22#0;
     #13#0 = #22#1;
     |LEE 000#13=;
     nEstado = FSalida;
     |SI FSalida ! 0;
          |MENAV "0000TIQUET INEXISTENTE....";
     |SINO;
          |SI #13#14 ! afecha;
               alfa = "0000EL TIQUET INTRODUCIDO ES DE LA FECHA " + #13#14;
               |MENAV alfa;
               nEstado = -1;
          |FINSI;
          TVisor = #13#9;
          |HAZ Numero;
     |FINSI;
     |CIERRA #13;
|FPRC;

|PROCESO CobraTiquet;
     |ABRE #6;
     #6#0 = #13#36;
     #6#1 = #13#0;
     |LEE 000#6=;
     |SI FSalida ! 0;
          |MENAV "0000 EL TIQUET INTRODUCIDO ESTA COBRADO...";
     |SINO;
          |ABRET #13;
          |LEE 021#13=;
          |SI FSalida = 0;
               nCobrar = #13#9;
               |HAZ Cobrar;
               |SI nEstado = 0;
                    |BORRA 021#6a;                || Borra impreso
                    |HAZ ActFicheros;
                    |SI #3#21 = "S";
                         |CONFI "0000NImprimir Tiquet (S/N).: ";
                         |SI FSalida = 0;
                              |HAZ ImpreTiquet;
                         |SINO;
                              |HAZ AbreCajon;
                         |FINSI;
                    |SINO;
                         |HAZ AbreCajon;
                    |FINSI;
                    |HAZ PintaCobro;
               |FINSI;
          |FINSI;
          |CIERRAT #13;
     |FINSI;
     |CIERRA #6;
|FPRC;

|PROCESO Llena;
     linea = linea + 1;
     #0#0 = linea;  || Linea
     #0#1 = nMesa;  || Mesa
     #0#9 = almacen;
     #0#2 = #14#2;  || Arti
     #0#3 = #14#8;  || Des
     #0#4 = #14#5;  || Uni
     |SI #agifa505#109 = "S";
          #0#5 = #14#6;  || Precio con IVA
          #0#6 = #14#7;  || Importe con IVA
     |SINO;
          #0#5 = #14#10; || Precio sin IVA
          #0#6 = #14#21; || Importe sin IVA
     |FINSI;
     #0#7 = #14#17; || Cama
     #0#10 = #14#26;|| Tarifa Especial
     #0#11 = #14#13; || Tipo IVA
     #0#12 = #14#29; || Uni2
     #0#13 = #14#30; || Partida
     #0#14 = #14#31; || Merma
     #0#16 = #13#55;
     #0#17 = #14#6;
     #0#18 = #14#10;

     |GRABA 020#0n;
     nabo = 1; |HAZ ActMesa;
     |BORRA 020#14a;
|FPRC;

|PROCESO UltimaLinea;
     |PARA; |SI FSalida = 0; |HACIENDO;
          |SI #0#1 ! nMesa;
               |SAL;
          |FINSI;
          linea = #0#0;
          |LEE 000#0s;
     |SIGUE;
|FPRC;

|DEFBUCLE Llena;
     #agifa502#1;
     ;
     #13#36, #13#0, 001;
     #13#36, #13#0, 999;
     ;
     NULL, Llena;
|FIN;

|PROCESO ModiTiquet;
     linea = 0;
     |ABRE #0;
     #0#0 = 0;
     #0#1 = nMesa;
     #0#9 = almacen;
     |LEE 000#0];
     |SI FSalida = 0; |Y #0#1 = nMesa; |Y #0#9 = almacen;
          |HAZ UltimaLinea;
          |AVISO;
          |CONFI "0000NA¤adir tiquet a la mesa actual (S/N).: ";
          nEstado = FSalida;
     |SINO;
          nEstado = 0;
     |FINSI;
     |SI Mensa = "S"; |MENSA "2405Copiando..."; |FINSI;
     |SI nEstado = 0;
          |ABRE #6; |ABRET #13; |ABRE #14;
          |LEE 021#13=;
          |SI FSalida = 0;
               #6#0 = #13#36;
               #6#1 = #13#0;
               |LEE 000#6=;
               |SI FSalida ! 0;       || Si esta cobrado
                    |HAZ DesActFicheros;
               |SINO;
                    |BORRA 021#6a;     || Tiquet Impreso y no cobrado
               |FINSI;
               |||BUCLELIN 0 Llena #13;
               |BUCLE Llena;
               |BORRA 021#13a;
          |FINSI;
          |CIERRA #6; |CIERRAT #13; |CIERRA #14;
     |FINSI;
     |CIERRA #0;
     |SI Mensa = "S"; |BLIN 24; |FINSI;
|FPRC;

|PROCESO BorraLin;
     |BORRA 020#agifa502.a;
|FPRC;

|DEFBUCLE BorraLin;
     #agifa502#1;
     ;
     #13#36, #13#0, 001;
     #13#36, #13#0, 999;
     be;
     NULL, BorraLin;
|FIN;

|PROCESO BajaTiquet;
     |SI Mensa = "S"; |MENSA "2405Borra tiquet..."; |FINSI;
     |ABRE #6; |ABRET #13; |ABRE #14;
     #6#0 = #13#36;
     #6#1 = #13#0;
     |LEE 000#6=;
     |SI FSalida = 0;              || Si impreso no esta cobrado
          |LEE 021#13=;
          |SI FSalida = 0;
               |||BUCLELIN 1 NULL #13;
               |BUCLE BorraLin;
               |BORRA 021#13a;
               |BORRA 021#6a;      || Tiquets Impresos
          |FINSI;
     |SINO;                        || Si cobrado Actu. ficheros
         |HAZ DesActFicheros;
         |||BUCLELIN 1 NULL #13;
         |BUCLE BorraLin;
         |BORRA 021#13c;
     |FINSI;
     |CIERRA #6; |CIERRAT #13; |CIERRA #14;
     |SI Mensa = "S"; |BLIN 24; |FINSI;
|FPRC;
-----------------------------------------------------------------------------
|PROCESO LeeIVA;
     enTiva = nTipoIVA;
     efFiva = afecha;
     |HAZ SacaIVA;
     iva = enIva;
|FPRC;

|PROCESO PintaCobro;
     |PUSHV 0501 2380;
     |BLANCO 1502 2336;
     |PINVE 1502, 2336, FicPant;
     |FBORRA FicPant;
     |PAUSA;
     |POPV;
|FPRC;

|PROCESO CobraPtas;
     |C_P #5#3, 1, 2225;
     |C_M #5#1, 1, "N";
     |C_M #5#2, 1, "N";

     |SI #3#19 = "S";               || Variacion a Pagar
          |C_M #5#5, 1, "S";
     |SINO;
          |C_M #5#5, 1, "N";
     |FINSI;

     nEfe = 0; nTar = 0; nChe = 0; nDeu = 0; nCambio = 0;
     nDife = 0; nHab = 0; nRot = 0; nIca = 0; nIcl = 0;
     nTotal = nCobrar;
     |DDEFECTO #5;
 |ET OtroCobroPtas;
     #5#1 = nTotal / 166.386;
     #5#5 = nTotal;
     |BLANCO 1502 2336;
     |PINPA #0#5;
     |PINDA #0#5;
     |ENDATOS #1#5;
     nEstado = FSalida;
     |SI FSalida ! 0;
       |||   |PTEC 802;
     |SINO;
          nDife = nDife + nTotal - #5#5;
          nTotal = #5#5 - #5#6;
          |SI #5#0 = "E"; |O #5#0 = "D"; |O #5#0 = "H";
               nEfe = nEfe + #5#6;
          |FINSI;
          |SI #5#0 = "T"; nTar = nTar + #5#6; |FINSI;
          |SI #5#0 = "C"; nChe = nChe + #5#6; |FINSI;
          |SI #5#0 = "D"; nDeu = nTotal;      |FINSI;
          |SI #5#0 = "H"; nHab = nTotal;      |FINSI;
          |SI #5#0 = "R"; nRot = nRot + #5#6; |FINSI;
          |SI #5#0 = "A"; nIca = nIca + #5#6; |FINSI;
          |SI #5#0 = "L"; nIcl = nIcl + #5#6; |FINSI;
          nCambio = #5#3;
          |SI nTotal [ 0; |O #5#0 = "D"; |O #5#0 = "H"; |O #5#0 = "R"; |O #5#0 ="A"; |O #5#0 ="L";
               |HAZ cobro_display;
               |HAZ AbreCajon;
               FicPant = ".pan"; |TEMPO FicPant;
               |GRABAVE 1502, 2336, FicPant;
          |SINO;
               |DDEFECTO #5;
               |SI #4#4 = "S"; #5#0 = "D"; |FINSI;
               |SI #4#5 = "S"; #5#0 = "H"; |FINSI;
               |VETE OtroCobroPtas;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO CobraEuros;
     |SI #3#19 = "S";               || Variacion a Pagar
          |C_M #5#1, 1, "S";
          |C_M #5#5, 1, "S";
     |SINO;
          |C_M #5#1, 1, "N";
          |C_M #5#5, 1, "N";
     |FINSI;

     nEfe = 0; nTar = 0; nChe = 0; nDeu = 0; nCambio = 0;
     nDife = 0; nHab = 0; nRot = 0; nIca = 0; nIcl = 0;
     nTotal = nCobrar;
     |DDEFECTO #5;
 |ET OtroCobroEuro;
     #5#1 = nTotal;
     #5#5 = nTotal * 166.386;
     |BLANCO 1502 2336;
     |PINPA #0#5;
     |SI #32#24 = "N";
          |PINPA #0#55;
          |C_P #5#1, 1, 2019;
          |C_P #5#2, 1, 2119;
          |C_P #5#3, 1, 2219;
          |C_V #5#5, 1, "N";
          |C_V #5#6, 1, "N";
     |FINSI;
     |PINDA #0#5;
     |ENDATOS #1#5;
     nEstado = FSalida;
     |SI FSalida ! 0;
        ||  |PTEC 802;
     |SINO;
          nDife = nDife + nTotal - #5#1;
          nTotal = #5#1 - (#5#2 + ((#5#6/166.386) ! EURODB));
          |SI #5#0 = "E"; |O #5#0 = "D"; |O #5#0 = "H";
               nEfe = nEfe + #5#2 + ((#5#6/166.386) ! EURODB);
          |FINSI;
          |SI #5#0 = "T"; nTar = nTar + #5#2 + ((#5#6/166.386)!EURODB); |FINSI;
          |SI #5#0 = "C"; nChe = nChe + #5#2 + ((#5#6/166.386)!EURODB); |FINSI;
          |SI #5#0 = "D"; nDeu = nTotal; |FINSI;
          |SI #5#0 = "H"; nHab = nTotal; |FINSI;
          |SI #5#0 = "R"; nRot = nRot + #5#2 + ((#5#6/166.386)!EURODB); |FINSI;
          |SI #5#0 = "A"; nIca = nIca + #5#2 + ((#5#6/166.386)!EURODB); |FINSI;
          |SI #5#0 = "L"; nIcl = nIcl + #5#2 + ((#5#6/166.386)!EURODB); |FINSI;
          nCambio = #5#3;
          |SI nTotal [ 0; |O #5#0 = "D"; |O #5#0 = "H"; |O #5#0 = "R"; |O #5#0 ="A"; |O #5#0 ="L";
               |HAZ cobro_display;
               |HAZ AbreCajon;
               FicPant = ".pan"; |TEMPO FicPant;
               |GRABAVE 1502, 2336, FicPant;
          |SINO;
               |DDEFECTO #5;
               |SI #4#4 = "S"; #5#0 = "D"; |FINSI;
               |SI #4#5 = "S"; #5#0 = "H"; |FINSI;
               |VETE OtroCobroEuro;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Cobrar;
     TVisor = nCobrar;
     |HAZ Numero;
     |HAZ total_display;
     |PUSHV 0501 2380;
     |D_CUADRO 0501, 1131; |BLANCO 1502 2336;

     |SI #4#2 = "S"; |O #4#3 = "S"; |O #4#4 = "S"; |O #4#5 = "S";
          |C_M #5#0, 1, "S";
     |SINO;
          |C_M #5#0, 1, "N";       || Solo Efectivo
     |FINSI;

     |SI #32#24 = "N";
          |HAZ CobraEuros;
     |SINO;
          |SI aEsEuro = "S";
               |HAZ CobraEuros;
          |SINO;
               |HAZ CobraPtas;
          |FINSI;
     |FINSI;

     |POPV;
|FPRC;
-----------------------------------------------------------------------------
|PROCESO inf;
     |SI FSalida = "POSICION";
          #0#0 = ClaveLinea;
          #0#1 = nMesa;
          #0#9 = almacen;
     |SINO;
          #0#0 = 1;
          #0#1 = nMesa;
          #0#9 = almacen;
     |FINSI;
|FPRC;

|PROCESO sup;
     #0#0 = 999;
     #0#1 = nMesa;
     #0#9 = almacen;
|FPRC;

|PROCESO ActMesa;
     nLINEAS = nLINEAS + nabo;
     nTOTAL = nTOTAL + (#0#6 * nabo);
|||     nTipoIVA = #3#128;
|||     |HAZ LeeIVA;
     nTOTAL_IVA = nTOTAL_IVA + ((#0#17 * #0#4) * nabo);
|FPRC;

|PROCESO LiberaMesa;
     |SI nMesaBloq ! -1;
          #9#0 = almacen;
          #9#1 = nMesaBloq;
          |BORRA 000#9c;
          |SI FSalida ! 0;
               |MENAV "0000Mesa manipulada por otro Usuario...";
          |FINSI;
          nMesaBloq = -1;
     |FINSI;
|FPRC;

|PROCESO IniTotal;
     nTOTAL = 0;
     nTOTAL_IVA = 0;
     nLINEAS = 0;
|||     nTipoIVA = #3#128;
|||     |HAZ LeeIVA;
|FPRC;

|PROCESO Total;
     nLINEAS = nLINEAS + 1;
     nTOTAL = nTOTAL + #0#6;
     nTOTAL_IVA = nTOTAL_IVA + (#0#17 * #0#4);
|FPRC;

|DEFBUCLE Total;
     #0#1;
     ;
     almacen, nMesa, 1;
     almacen, nMesa, 999;
     ;
     IniTotal, Total;
|FIN;

|PROGRAMA;
     aParam = eaParametro;
     eaTiVisor= "restaurante"; || PARA EL DISPLAY DE NUMEROS

     |HAZ DecimalesBase;
     |HAZ CliDefecto;
     |SI #23#193 = "B";
          nDeci_PreVis = nDeci_PreBase;
     |SINO;
          nDeci_PreVis = nDeci_PreDiv;
     |FINSI;

     |HAZ CogeInformes;

     |SI #agifa324#2 = 1;
          aEsEuro = "S";
     |SINO;
          aEsEuro = "N";
     |FINSI;

     |ABRE #32;  |LEE 001#32p; |CIERRA #32;
     |ABRE #142; |LEE 000#142p; |CIERRA #142;
     nDeci_UniTpv = #32#18;

     |HAZ CreaTempo; aTempo30 = Tempo; |NOME_DAT #30 aTempo30; |DELINDEX #30;
     |HAZ CreaTempo; aTempo1  = Tempo; |NOME_DAT #1 aTempo1;   |DELINDEX #1;

     |SI eaParametro = "alimenta";
          aAlfa = Usuario;    |QBF aAlfa;
          aAlfa = "0" + aAlfa;
          |NOME_DAT #0 aAlfa;
          aAlfa = Usuario;    |QBF aAlfa;
          aAlfa = "9" + aAlfa;
          |NOME_DAT #9 aAlfa;
     |FINSI;

  |ET cierravic;
     |CLS;
     |CABEZA "Gestion de TPV";
     |SI eaParametro = "alimenta";
          |PINPA #0#1001;
          |SI acaja > 999;
               |MENAV "0000La caja no puede ser superior a 999...";
               |ACABA;
          |FINSI;
     |SINO;
          |PINPA #0#0;
     |FINSI;
     nMesa = -1;
     |ENTLINEAL #1#0, 3, 7, 22, 0, inf, sup;  || Solo pinta
     |PINTA 936, #48#1;

     |ABRE #3
     #3#0 = acaja;
     |LEE 021#3=;
     |CIERRA #3;
     |HAZ DefectoCaja;

     |SI #3#143 = "N"; TCOBIMS = 999; |FINSI;
     |SI #3#144 = "N"; TIMPRPA = 999; |FINSI;

     |ATRI R;
     alfa = #3#1 % 115;
     |PINTA 2334, "                           ";
     |PINTA 2334, afecha;
     |PINTA 2350, #3#0;
     |PINTA 2356, alfa;
     |ATRI 0;
     |SI eaParametro = "alimenta";
          nMesa = acaja;
     |SINO;
          nMesa = #3#93;
     |FINSI;
     aSerie = #3#89;
     cVisor = #3#86;
     almacen = #3#87;
     #0#9 = almacen;        || igualo almacen para grabar por al/mesa/lin.
     |HAZ InicioVisor;

     fichero = Usuario; |QBF fichero;
     |HAZ PathHotel;
     |HAZ CargaHabi;

     |ABRE #9;
  |ET Inicio;
     |HAZ LiberaMesa;
     fechasis = ~;
     |SI fechasis > afecha; |Y #3#129 = "S";
          |HAZ miraora;
          |SI swcierra = 1; swcierra = 0; |VETE cierravic; |FINSI;
     |FINSI;
     nEstado = 0;
     |HAZ PideCamarero;
     |SI nEstado ! 0;
          |SI eaParametro ! "alimenta";
               |HAZ MiraPendientes;
          |FINSI;
          |SI MesaElige = -1;
               |CONFI "2400NSalir (S/N).: ";
               |SI FSalida = 0;
                    |HAZ LiberaMesa;
                    |CIERRA #9;
                    |DELINDEX #30; Tempo = aTempo30; |HAZ BoraTempo;
                    |DELINDEX #1;  Tempo = aTempo1;  |HAZ BoraTempo;
                    |ACABA;
               |SINO;
                    |VETE Inicio;
               |FINSI;
          |SINO;
               |SI MesaElige ] 1000;
                    MesaElige = MesaElige - 1000;
               |SINO;
                    nMesa = MesaElige;
               |FINSI;
               |PTEC 802;
               |SI mesas = "S"; |PTEC 802; |FINSI;
               |VETE Inicio;
          |FINSI;
     |FINSI;
     |HAZ ini_display;

     |SI mesas = "S";
          |SI eaParametro ! "alimenta";
               |HAZ PideMesa;
               |SI nEstado ! 0; |VETE Inicio; |FINSI;
          |FINSI;
     |SINO;
          |ATRI I; |PINTA 0746, nMesa; |ATRI 0;
     |FINSI;

     #9#0 = almacen;
     #9#1 = nMesa;
     |LEE 000#9=;
     |SI FSalida ! 0;
          #9#2 = Usuario % 810;
          |GRABA 020#9n;
     |SINO;
          |SI eaParametro ! "alimenta";
               |HAZ FichaBloqueada;
               |SI nEstado ! 0;  |VETE Inicio; |FINSI;
          |FINSI;
          #9#2 = Usuario % 810;
          |GRABA 020#9a;
     |FINSI;
     nMesaBloq = nMesa;

  |ET Otra;
     ModoLineal = 1;
     ClaveLinea = 1;
     MesaElige = -1;

  |ET Lineal;
     |BUCLE Total;
     |PTEC 802;
     |ENTLINEAL #1#0, -3, ModoLineal, 22, 0, inf, sup;

     |HAZ CargaHabi;

     ||TODO CCC. No se permite el borrado de lineas si estamos en impresion
     ||          directa. Ni a¤adir MESA y otras cosas relacionadas con MESAS

     |SI eaParametro ! "alimenta";
          |SI nTecla = TBORLIN;             || Borra linea
               |HAZ BorraLinea;
               ModoLineal = 2;
               |ERROR;
               |VETE Lineal;
          |FINSI;
          |SI nTecla = TSERVI;              || Otro Servicio
               |SI MesaElige ! -1;          || Cambio de Mesa por consulta
                    |SI MesaElige ] 1000;   || A¤ade a Mesa actual
                         MesaElige = MesaElige - 1000;
                         |HAZ SumaMesa;
                         |SI mesas = "S"; |PTEC 802; |FINSI;
                    |FINSI;
                    nMesa = MesaElige;
                    |PTEC 802;
                    |SI mesas = "S"; |PTEC 802; |FINSI;
               |FINSI;
               |VETE Inicio;
          |FINSI;
     |FINSI;

     |SI nTecla = TIQIMP;              || Consulta Tiq. Impresos
          |HAZ TiqImpreso;
     |FINSI;
     |SI nTecla = TCOBIMP;             || Cobro + Impresion
          |ABRE #0; |ABRET #13;
          #0#0 = 0;
          #0#1 = nMesa;
          #0#9 = almacen;
          |LEE 000#0];
          |SI FSalida = 0; |Y #0#1 = nMesa;
               nCobrar = nTOTAL_IVA;
               |HAZ Cobrar;
               |SI nEstado = 0;
                    nSwCajon = 1;
                    |HAZ Pasa501_502;
                    |SI nEstado = 0;
                         |HAZ ImpreTiquet;
                         |HAZ Limpia;
                         |HAZ ActFicheros;
                    |FINSI;
                    nSwCajon = 0;
                    |HAZ PintaCobro;
               |FINSI;
          |FINSI;
          |CIERRA #0; |CIERRAT #13;
          |SI nEstado = 0 ; |VETE Inicio; |FINSI;
     |FINSI;
     |SI nTecla = TIMPRE;               || Imprime y no cobra
          |SI eaParametro = "alimenta";
               |HAZ AbonaLinea;
               nTecla = 0;
               |VETE Otra;
          |SINO;
               |ABRE #0; |ABRET #13;
               #0#0 = 0;
               #0#1 = nMesa;
               #0#9 = almacen;
               |LEE 000#0];
               |SI FSalida = 0; |Y #0#1 = nMesa;
                    nEfe = 0; nTar = 0; nChe = 0; nDeu = 0; nCambio = 0;
                    nDife = 0; nHab = 0;
                    |HAZ ImpreTiqueA;
               |FINSI;
               |CIERRA #0; |CIERRAT #13;
               |VETE Inicio;
          |FINSI;
     |FINSI;
     |SI nTecla = TIMPRPA;               || Imprime y no cobra y pasa a Tick
          |ABRE #0; |ABRET #13;
          #0#9 = almacen;
          #0#1 = nMesa;
          #0#0 = 0;
          |LEE 000#0];
          |SI FSalida = 0; |Y #0#1 = nMesa; |Y #0#9 = almacen;
               nEfe = 0; nTar = 0; nChe = 0; nDeu = 0; nCambio = 0;
               nDife = 0; nHab = 0;
               sw = 1;
               |HAZ Pasa501_502;               || vic
               |SI nEstado ! 0;
                    |CIERRA #0; |CIERRAT #13;
                    |VETE Otra;
               |FINSI;
               sw = 0;
               nSwCajon = 1;
               |HAZ ImpreTiquet;
               nSwCajon = 0;
               |HAZ Limpia;
               |ABRE #6;
               #6#0 = #13#36;
               #6#1 = #13#0;
               #6#2 = #13#9;
               #6#3 = #13#56;
               |GRABA 020#6n;           || Tiquets Impresos
               |CIERRA #6;
          |FINSI;
          |CIERRA #0; |CIERRAT #13;
          |VETE Inicio;
     |FINSI;
     |SI nTecla = TTIQUET;              || Cobra/Imp/Modi/Borra
          |PUSHV 0501 2380;
          |D_CUADRO 0501, 1131; |BLANCO 1201 2330;
          |HAZ QueTiquet;
          |SI nEstado = 0;
               |HAZ MenuTiquet;
          |SINO;
               |POPV;
          |FINSI;
          nEstado = 0;
     |FINSI;

     |SI eaParametro ! "alimenta";
          |SI nTecla = TCOBIMS;             || Cobro + Impresion  (PARCIAL)
               |ABRE #0; |ABRET #13;
               #0#0 = 0;
               #0#1 = nMesa;
               #0#9 = almacen;
               |LEE 000#0];
               |SI FSalida = 0; |Y #0#1 = nMesa;
                    |HAZ cargapa;
                    |SI totpavi = 0;
                         nEstado = 1;
                    |SINO;
                         nCobrar = totpavi;
                         |HAZ Cobrar;
                    |FINSI;
                    |SI nEstado = 0;
                         vic = 1;
                         |HAZ Pasa501_502;
                         vic = 0;
                         |SI nEstado = 0;
                              |HAZ ImpreTiquet;
                              |HAZ Limpia2;
                              |HAZ ActFicheros;
                         |FINSI;
                         |HAZ PintaCobro;
                    |FINSI;
               |FINSI;
               |CIERRA #0; |CIERRAT #13;
               |SI nEstado = 0 ; |VETE Inicio; |FINSI;
          |FINSI;
     |FINSI;

     |VETE Otra;
|FPRO;
-----------------------------------------------------------------------------
|PROCESO MenuTiquet;
     menu2 = "1";
     |MENU menu;
     |POPV;
     menu2 = FSalida;
     |SI menu2 = 1;
          |HAZ CobraTiquet;
     |FINSI;
     |SI menu2 = 2;
          aGuarda = #agifa505#32;
          #agifa505#32 = "N";
          |HAZ ImpreTiquet;
          #agifa505#32 = aGuarda;
     |FINSI;
     |SI menu2 = 3;
          |SI eaParametro ! "alimenta";
               |HAZ ModiTiquet;
          |SINO;
               |MENAV "0000Modificacion no permitida...";
          |FINSI;
     |FINSI;
     |SI menu2 = 4;
          |HAZ BajaTiquet;
     |FINSI;
|FPRC;
-----------------------------------------------------------------------------
|PROCESO mesa_suma; |TIPO 11;
     |SI FSalida = 9; |Y nNoPasa = 0; |Y nMesa !  #1#0;
          #9#0 = almacen;
          #9#1 = #1#0;
          |LEE 000#9=;
          nEstado = FSalida;
          |SI nEstado = 0;
               |HAZ FichaBloqueada;
               |SI nEstado ! 0; |ERROR; |ACABA; |FINSI;
               |BORRA 000#9a;
          |FINSI;
          #9#2 = Usuario % 810;
          |GRABA 020#9n;
          nEstado = 0;
          |PTEC 806;
          MesaElige = #1#0 + 1000;
     |FINSI;
|FPRC;

|PROCESO mesa_elige; |TIPO 0;
     |SI FSalida = 0;
          |PTEC 806;
          |PTEC 806;
          MesaElige = #1#0;                  || Elige mesa
          nSwMesa = 1;
     |FINSI;
|FPRC;
-----------------------------------------------------------------------------
|PROCESO CargaMesas;
     |SALVA_FICHA #0;
     nMesaAnt = -1;
     |DELINDEX #1;
     |ABRE #1;
     |DDEFECTO #0;
     #0#9 = almacen;
     |LEE 000#0];
     |PARA; |SI FSalida = 0; |Y #0#9 = almacen; |HACIENDO;
          |SI #0#1 ! nMesaAnt;
               nMesaAnt = #0#1;
               nPenLin = 1;
          |SINO;
               nPenLin = nPenLin + 1;
          |FINSI;
          |DDEFECTO #1;
          #1#4 = #0#9;
          #1#0 = #0#1;
          |LEE 101#1=;
          |SI FSalida ! 0; |GRABA 020#1b; |FINSI;
          #1#2 = #1#2 + #0#6;
          #1#3 = nPenLin;
          |GRABA 020#1a;
          |LIBERA #1;
          |LEE 000#0s;
     |SIGUE;
     |CIERRA #1;

     |REPON_FICHA #0;
|FPRC;

|PROCESO MiraPendientes;
     MesaElige = -1;
     nEstado = 0;
     |ABRE #0;
     |DDEFECTO #0;
     #0#9 = almacen;
     |LEE 000#0];
     |SI FSalida = 0; |Y #0#9 = almacen;
          |HAZ CargaMesas;
          |PUSHV 0501 2380;
          MesaElige = -1;
          nNoPasa = 1;
          |MENSA "0000<INTRO> Elige Mesa";
          |ENTLINEAL #1#1, -2, 4, 21, 1, inf1, sup1;
          |POPV;
     |FINSI;
     |CIERRA #0;
|FPRC;

|PROCESO PideCamarero;
     |SI eaParametro ! "alimenta";
          |MENSA "0000<F7> Elige Mesa";
     |FINSI;

  |ET OtroCama;
     |DDEFECTO #4;
     |ABRE #4;
     |SI #3#34 = "S";
          |MODULO alfa; |QBF alfa;
          E_sup = 99;
          E_inf = 1;
     |SINO;
          E_sup = #3#20;
          E_inf = #3#20;
     |FINSI;
     camarero  = 0646 ? 1;
     nEstado = FSalida;
     |SI FSalida = 0;
          #4#0 = camarero;
          |LEE 040#4=;
          |PINTA 0649, #4#1;
          |SI FSalida ! 0;
               |MENAV "0000NO EXISTE CAMARERO...";
               |VETE OtroCama;
          |FINSI;
     |FINSI;
     |CIERRA #4;
|FPRC;
-----------------------------------------------------------------------------
|PROCESO impreso_elige; |TIPO 0;
     |SI FSalida = 0;
          nEstado = 0;
          #22#0 = #6#0;
          #22#1 = #6#1;
          TVisor = #6#2;
          #13#36 = #6#0;
          #13#0 = #6#1;
          |PTEC 806;
     |FINSI;
|FPRC;

|PROCESO inf6;
     #6#0 = "  ";
     #6#1 = "      ";
|FPRC;

|PROCESO sup6;
     #6#0 = "zz";
     #6#1 = "zzzzzz";
|FPRC;

|PROCESO TiqImpreso;
     |ABRE #6;
     |LEE 000#6p;
     |SI FSalida ! 0;
          |MENAV "0000NO HAY TIQUETS PENDIENTES DE COBRAR...";
     |SINO;
          cGuarda = FEntrada;
          |PUSHV 0501 2380;
          |BLANCO 0747 2179;
          nEstado = -1;
          |ENTLINEAL #1#6, -1, 4, 19, 1, inf6, sup6;
          |POPV;
          |SI nEstado = 0;
               |HAZ Numero;
               |PUSHV 0501 2380;
               |BLANCO 1201 2330;
               |PINPA #0#22;
               |PINDA #0#22;
               |HAZ MenuTiquet;
          |FINSI;
          FEntrada = cGuarda;
     |FINSI;
     |CIERRA #6;
|FPRC;
-----------------------------------------------------------------------------
|PROCESO ActComensal;
     |SI #0#16 ! nComensal;
          #0#16 = nComensal;
          |GRABA 000#0a;
          |LIBERA #0;
     |FINSI;
|FPRC;

|DEFBUCLE ActComensal;
     #0#1;
     ;
     almacen, nMesa, 001;
     almacen, nMesa, 999;
     ;                     || Sin Bloqueos !!!
     NULL, ActComensal;
|FIN;

|PROCESO inf1;
     #1#0 = 0;
     #1#3 = 1;
     #1#4 = almacen;
|FPRC;

|PROCESO sup1;
     #1#0 = 999;
     #1#3 = 999;
     #1#4 = almacen;
|FPRC;

|PROCESO MesaOcupa;
     cGuarda = FEntrada;
     MesaElige = -1;
     nNoPasa = 0;
     |HAZ CargaMesas;
     |PUSHV 0101 2380;
     |MENSA "0000<INTRO> Elige Mesa <F12> Pasa Articulos de la Mesa a la Actual.";
     |ENTLINEAL #1#1, -2, 4, 21, 1, inf1, sup1;
     |POPV;
     FEntrada = cGuarda;
|FPRC;

|PROCESO PideMesa;
     |MENSA "0000<F7> Mesas Ocupadas <F8> Consulta Creditos <F10> Consulta Tiquets";
  |ET PideMesa1;
     E_sup = 999;
     E_inf = 0;
     |SI nSwMesa = 0;
          |SI #3#92 = "S"; nMesa = #3#93; |FINSI;
     |FINSI;
     nSwMesa = 0;
     nMesa = 0746 ? 1;
     nEstado = FSalida;
     |SI nEstado ! 0;
          |SI nEstado = 1; |O nEstado = 7;
               |PTEC 807;
          |FINSI;
          |SI nEstado = TMESAS;
               |ABRE #0;
               |HAZ MesaOcupa;
               |CIERRA #0;
               |SI MesaElige ! -1;
                    |SI MesaElige ] 1000;
                         MesaElige = MesaElige - 1000;
                    |FINSI;
                    nMesa = MesaElige;
                    |PTEC 802;
               |FINSI;

               |VETE PideMesa1;
          |FINSI;
          |SI FSalida = TCREDI;      || VIC
               |PTEC 802;
               |PTEC 811;
               |SUB_EJECUTA_NP "agifa571";
               |ERROR;
               |ACABA;
          |FINSI;
          |SI FSalida = TCONSUL;
               |PUSHV 0101 2380;
               |CLS;
               |ABRE #13;
               |DDEFECTO #13;
               |SELECT #3#13;
               #13#36 = aSerie;
               #13#14 = afecha;
               |LEE 000#13];
               |SELECT #1#13;
               |LEE 000#13=;
               |PINPA #0#13;
               |PINDA #0#13;
               |CONSULTA #0#13;
               |CIERRA #13;
               |POPV;
               |ERROR;
               |ACABA;
          |FINSI;
     |FINSI;

     |MENSA "0000 ";
     |ABRE #0;
     #0#9 = almacen;
     #0#1 = nMesa;
     #0#0 = 0;
     |LEE 000#0];
     |SI FSalida = 0; |Y #0#9 = almacen; |Y #0#1 = nMesa;
          nComensal = #0#16;
     |FINSI;
     |CIERRA #0;
     E_sup = 999;
     E_inf = 0;
     nComensal = 0773 ? 1;

     #9#0 = almacen;
     #9#1 = nMesa;
     |LEE 000#9=;
     |SI FSalida = 0;
          |HAZ FichaBloqueada;
          |SI nEstado ! 0; |ACABA; |FINSI;
          #9#2 = Usuario % 810;
          |BORRA 000#9a;
     |FINSI;
     |BUCLE ActComensal;
|FPRC;
-----------------------------------------------------------------------------
|PROCESO SumaMesa;
     |SI nMesa = MesaElige; |ACABA; |FINSI;
     |HAZ pregunta;               || Pregunta si la copia es total/parcial...
     |SI tiim=1;
          |HAZ presenta;
     |SINO;
          |HAZ carprese;
     |FINSI;
     |SI Mensa = "S"; |MENSA "2405Copiando mesa..."; |FINSI;
     linea = 0;
     |ABRE #0;
     |ABRE #30;
     #0#0 = 0;
     #0#1 = nMesa;
     #0#9 = almacen;
     |LEE 000#0];
     |SI FSalida = 0; |Y #0#1 = nMesa;
          |HAZ UltimaLinea;
     |FINSI;
     #30#0 = 0;
     #30#1 = MesaElige;
     #30#10 = almacen;
     |LEE 101#30];

     #0#0 = 0;
     #0#1 = MesaElige;
     #0#9 = almacen;
     |LEE 101#0];
     |PARA; |SI FSalida = 0; |Y #0#1 = MesaElige; |HACIENDO;
          |SI #30#9 = "*";
               |BORRA 021#0a;
               lindesco = lindesco - 1;
               linea = linea + 1;
               #0#0 = linea;
               #0#1 = nMesa;
               #0#9 = almacen;
               |GRABA 020#0n;
               nabo = 1; |HAZ ActMesa;
          |FINSI;
          |LIBERA #0;
          |LEE 101#0s;
          |LEE 000#30s;
     |SIGUE;
     |CIERRA #0;
     |CIERRA #30;
     ||ÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄQuita Bloqueo
     |ABRE #9;
     #9#0 = almacen;
     #9#1 = MesaElige;
     |BORRA 000#9c;
     |SI FSalida ! 0;
          |MENAV "0000Mesa manipulada por otro Usuario...";
     |FINSI;
     |CIERRA #9;

     MesaElige = nMesa;
     |SI Mensa = "S"; |BLIN 24; |FINSI;
|FPRC;
-----------------------------------------------------------------------------
|PROCESO horario;
     |ABRE #17;
     alfa = (#13#34 % 102) + (#13#34 % 402);
     beta = alfa;
     #17#0 = #3#18;
     |LEE 000#17=;
     periodo = 0;
     |PARA i = 1; |SI i [ 19; |HACIENDO i = i + 2;
          campo = i + 1;
          periodo = periodo + 1;
          |SI beta ] #17i; |Y beta [ #17campo;
               |SAL;
          |FINSI;
     |SIGUE;
     |CIERRA #17;
|FPRC;
-----------------------------------------------------------------------------
|PROCESO ActCarHabi;
     |SI #13#59 = 0; |ACABA; |FINSI;    || -> No hay Cargo habi
     |ABRE #27;
     |ABRE #28;
     |SI abo = 1;
          |DDEFECTO #27;
          #27#1 = xhotel;
          #27#0 = xhabita;
          |LEE 000#27=;
          |SI FSalida ! 0;
               #27#1 = xhotel;
               #27#0 = xhabita;
               |GRABA 020#27b;
          |FINSI;
          #28#15 = #27#1;
          #28#0  = #27#0;
          #28#1  = 999;
          |LEE 000#28];
          |SI FSalida = 0;
               |LEE 000#28a;
          |SINO;
               |LEE 000#28u;
          |FINSI;
          |SI FSalida = 0; |Y #28#15 = #27#1; |Y #28#0 = #27#0;
               lincre = #28#1 + 1;
          |SINO;
               lincre = 1;
          |FINSI;
          |DDEFECTO #28;
          #28#15 = #27#1;
          #28#0  = #27#0;
          #28#1  = lincre;

          alfa = (Usuario % 810);

          #28#2  = #13#37;  || Documento.
          #28#3  = #13#14;  || Fecha.
          #28#4  = #13#12;  || Caja.
          #28#5  = #13#59;  || Importe.
          #28#6  = xhuespe; || Cod.Huesp.
          #28#7  = xnombre; || Nom.Huesp.
          #28#8  = "X";     || Tipo Cargo.
          #28#9  = "REST";  || Concepto.
          #28#10 = 1;       || Unidades.
          #28#11 = alfa;    || Usuario.
          #28#12 = "";      || Servicio.
          #28#13 = 0;       || Tipo Iva.         (no hace falta)
          #28#14 = "S";     || Iva Incluido. S/N (tampoco)
          |GRABA 020#28n;
     |SINO;
          #27#1 = #13#60;
          #27#0 = #13#61;
          |LEE 020#27=;
          |SI FSalida = 0;
               #28#15 = #27#1;
               #28#0  = #27#0;
               #28#1  = 0;
               |LEE 101#28];
               |PARA; |SI FSalida = 0; |Y #27#1 = #28#15; |Y #27#0 = #28#0; |HACIENDO;
                    |SI #28#2 = #13#37; |Y #28#4 = #13#12;
                         |BORRA 021#28a;
                         |SAL;
                    |FINSI;
                    |LIBERA #28;
                    |LEE 101#28s;
               |SIGUE;
          |FINSI;
     |FINSI;
     |CIERRA #27;
     |CIERRA #28;
|FPRC;

|PROCESO ActCredito;
     |SI #13#41 = 0; |ACABA; |FINSI;    || -> No hay Credito
     |ABRE #25;
     |ABRE #26;
     |SI abo = 1;
          |DDEFECTO #25;
          #25#0 = #13#1;
          |LEE 000#25=;
          |SI FSalida ! 0;
               |ABRE #23;
               #23#0 = #13#1;
               |LEE 000#23=;
               |SI FSalida ! 0; |DDEFECTO #23; |FINSI;
               |CIERRA #23;
               #25#1 = #23#1;
               |GRABA 020#25b;
          |FINSI;
          #25#2 = #25#2 + #13#41;  || T.Credi
          #25#4 = #25#4 + #13#41;  || T.Pendiente
          |GRABA 020#25a;
          #26#0 = #25#0;
          #26#1 = 9999;
          |LEE 000#26];
          |SI FSalida = 0;
               |LEE 000#26a;
          |SINO;
               |LEE 000#26u;
          |FINSI;
          |SI FSalida = 0; |Y #26#0 = #25#0 ;
               lincre = #26#1 + 1;
          |SINO;
               lincre = 1;
          |FINSI;
          |DDEFECTO #26;
          #26#0 = #25#0;
          #26#1 = lincre;
          #26#2 = #13#57;     || Se
          #26#3 = #13#0;      || Reg
          #26#4 = #13#58;     || Doc
          #26#5 = #13#14;     || Fecha
          #26#6 = #13#12;     || Caja
          #26#7 = #13#33;     || Ope
          #26#8 = #13#41;     || Credito
          #26#9 = #13#41;     || Pendiente
          #26#10= #13#9;      || Imp.Docu
          #26#13 = #13#36;
          |GRABA 020#26n;
     |SINO;
          #25#0 = #13#1;
          |LEE 020#25=;
          |SI FSalida = 0;
               #26#0 = #25#0;
               #26#1 = 0;
               |LEE 101#26];
               |PARA; |SI FSalida = 0; |Y #26#0 = #25#0; |HACIENDO;
                    |SI #26#2 = #13#57; |Y #26#4 = #13#38;
                         |BORRA 021#26a;
                         |SI FSalida = 0;
                              #25#2 = #25#2 - #26#8;  || T.Cred
                              #25#3 = #25#3 - #26#12; || T.Pag
                              #25#4 = #25#4 - #26#9;  || T.Pend
                              |GRABA 020#25a;
                         |FINSI;
                         |SAL;
                    |FINSI;
                    |LIBERA #26;
                    |LEE 101#26s;
               |SIGUE;
          |FINSI;
     |FINSI;
     |CIERRA #25;
     |CIERRA #26;
|FPRC;

|PROCESO ActResumen;
     |ABRE #18;
     |DDEFECTO #18;
     #18#63 = empresa;
     #18#0 = acaja;
     #18#1 = #13#14;
     |LEE 101#18=;
     |SI FSalida ! 0;
          |GRABA 020#18b;
     |FINSI;
     |SI #13#31 ! "R"; |Y #13#31 ! "A"; |Y #13#31 ! "L";
          #18#24 = #18#24 + (#13#9*abo);   ||Venta con imp.
          #18#25 = #18#25 + (#13#4*abo);   ||Total iva
          #18#26 = #18#26+ ((#13#22+#13#25+#13#47+#13#50+#13#53)*abo) ;
          #18#27 = #18#27+ ((#13#20+#13#23+#13#26+#13#30+#13#48+#13#51)*abo);
          #18#28 = #18#28 + (#13#28*abo);  ||Total dto
          #18#29 = #18#29 + (#13#3*abo);   ||Total bruto
          #18#6 = #18#6 + (1*abo);
          #18#15 = #18#15 + (#13#9*abo);
          |HAZ horario;
          campo = periodo + 29;
          #18campo = #18campo + (1*abo);
          campo = campo + 10;
          #18campo = #18campo + (#13#9*abo);
          |SI #3#163 = "S"; |O #13#9 > 0;
               #18#52 = #18#52 + (#13#38*abo);        ||Efectivo
               #18#55 = #18#55 + ((#13#38 + #13#40 + #13#39)*abo);
          |FINSI;
          #18#53 = #18#53 + (#13#40*abo);            ||Tarjeta
          #18#54 = #18#54 + (#13#39*abo);            ||Cheque
          #18#58 = #18#58 + (#13#41*abo) + (#13#59*abo); ||Pendiente de credito
          #18#57 = #18#57 + (#13#15*abo);            ||Restos no cobrados
     |FINSI;
     #18#67 = #18#67 + #13#62;                  ||Roturas.
     #18#68 = #18#68 + #13#63;                  ||Invitaciones Casa.
     #18#69 = #18#69 + #13#64;                  ||Invitaciones Clientes.
     |GRABA 020#18a;
     |CIERRA #18;
|FPRC;

|PROCESO ActTarjeta;
     |SI #13#40 ! 0;
          |ABRE #20;
          #20#0 = #13#46;
          |LEE 101#20=;
          |SI FSalida ! 0;
               |GRABA 020#20b;
          |FINSI;
          #20#2 = #20#2 + (#13#40*abo);
          #20#3 = #13#14;
          #20#4 = (#13#40*abo);
          |GRABA 020#20a;
          |CIERRA #20;

          |ABRE #21;
          |DDEFECTO #21;
          #21#0 = #13#46
          #21#1 = #13#14;
          |LEE 101#21=;
          |SI FSalida ! 0;
               |GRABA 020#21b;
          |FINSI;
          #21#2 = #21#2 + (#13#40*abo);
          |GRABA 020#21a;
          |CIERRA #21;
     |FINSI;
|FPRC;

|PROCESO ActCliente;
     |ABRE #23;
     #23#0 = #13#1;
     |LEE 101#23=;
     |SI FSalida ! 0;
          |GRABA 020#23b;
     |FINSI;
     #23#102 = #23#102 + (#13#3*abo);     ||Total compras
     #23#110 = #23#110 + (#13#3*abo);     ||Total Faturado
     alfa = #13#14 % 402;
     campo = ((alfa - 1) * 4) + 54;
     #23campo = #23campo + (#13#3*abo);   ||Compras Parcial
     #23#46 = #13#3*abo;                  ||Importe Ultcompra
     |GRABA 020#23a;
     |CIERRA #23;

     enImpCliCom = (#13#3*abo);
     enImpCliFac = (#13#3*abo);
     eaCliente = #13#1;
     efFecha = #13#14;
     |HAZ AcumulaCli;
|FPRC;

|PROCESO ActImpoArti;
     fmes = #13#14 % A402;
     mes = ((fmes - 1) * 5) + 35;
     #2#0 = #14#2;
     |LEE 101#2=;
     |SI FSalida = 0;
          nImpo = ((((#14#5 * #14#10) < #14#14) < #13#17) < #13#19);
          |SI #142#115 = "S";
               nImpo = nImpo < #13#18;
          |FINSI;
          nMargen = nImpo - #14#23;
          #2mes = #2mes + (nImpo * abo);
          #2#95 = #2#95 + (nImpo * abo);
          mes = mes + 1;
          #2mes = #2mes + (nMargen * abo);
          #2#96 = #2#96 + (nMargen * abo);
          |GRABA 020#2a;

          efFecha = #13#14;
          eaArticulo = #14#2;
          enImpVta = (nImpo * abo);
          enImpMar = (nMargen * abo);
          |HAZ AcumulaArt;
     |FINSI;
     |LIBERA #2;
|FPRC;

|PROCESO Actualiza;

     enForma = abo;
     |HAZ AcumulaTPVR;

     #14#23 = enCoste;
     #13#8 = #13#8 + (#14#21 - enCoste);
     |GRABA 020#14a;

     |HAZ ActImpoArti;
|FPRC;
-----------------------------------------------------------------------------
|PROCESO FormaPago;
     |SI nEfe ! 0; #13#31 = "E"; |FINSI;
     |SI nTar ! 0; #13#31 = "T"; |FINSI;
     |SI nChe ! 0; #13#31 = "C"; |FINSI;
     |SI nDeu ! 0; #13#31 = "D"; |FINSI;
     |SI nHab ! 0; #13#31 = "H"; |FINSI;
     |SI nRot ! 0; #13#31 = "R"; |FINSI;
     |SI nIca ! 0; #13#31 = "A"; |FINSI;
     |SI nIcl ! 0; #13#31 = "L"; |FINSI;
|FPRC;

|DEFBUCLE Actualiza;
     #agifa502#1;
     ;
     #13#36, #13#0, 001;
     #13#36, #13#0, 999;
     ;
     NULL, Actualiza;
|FIN;

|PROCESO ActFicheros;
     |SI Mensa = "S"; |MENSA "2405Actualizando..."; |FINSI;
     |LEE 121#13=;
     |SI FSalida = 0;
          abo = 1;
          |ABRE #2; |ABRE #11; |ABRE #12;
          #13#8 = 0;
          |||BUCLELIN 0 Actualiza #13;
          |BUCLE Actualiza;
          |CIERRA #2; |CIERRA #11; |CIERRA #12;
          #13#10 = nEfe + nTar + nChe;
          #13#11 = nCambio;
          #13#15 = nDife;
          |HAZ FormaPago;
          #13#38 = nEfe - nCambio;
          #13#39 = nChe;
          #13#40 = nTar;
          #13#41 = nDeu;
          #13#59 = nHab;
          #13#46 = #5#4;          || Codi Tarjeta
          #13#59 = nHab;
          #13#62 = nRot;          || Roturas.
          #13#63 = nIca;          || Invitacion Casa.
          #13#64 = nIcl;          || Invitacion clientes.
          |GRABA 020#13a;
          |HAZ ActResumen;
          |HAZ ActTarjeta;
          |HAZ ActCliente;
          |HAZ ActCredito;
          |HAZ ActCarHabi;
     |FINSI;
     |LIBERA #13;
     |SI Mensa = "S"; |BLIN 24; |FINSI;
|FPRC;
-----------------------------------------------------------------------------
|PROCESO DesActFicheros;
     |SI Mensa = "S"; |MENSA "2405Desactualizando..."; |FINSI;
     |LEE 121#13=;
     |SI FSalida = 0;
          |ABRE #2; |ABRE #11; |ABRE #12;
          abo = -1;
          |||BUCLELIN 0 Actualiza #13;
          |BUCLE Actualiza;
          |HAZ ActResumen;
          |HAZ ActTarjeta;
          |HAZ ActCliente;
          |HAZ ActCredito;
          |HAZ ActCarHabi;
          |CIERRA #2; |CIERRA #11; |CIERRA #12;
     |FINSI;
     |LIBERA #13;
     |SI Mensa = "S"; |BLIN 24; |FINSI;
|FPRC;
--------------------------------------PANTALLA DE COBRO
|PROCESO Tipo12; |TIPO 12;
     || no quitar
|FPRC;

|PROCESO TotaPaga; |TIPO 0;
     nNume = Contenido;
     |SI nNume ! #5Campo;
          |SI Campo = 1;
               #5#5 = #5#1 * 166.386;
               |PINTA #5#5;
          |SINO;
               #5#1 = #5#5 / 166.386;
               |PINTA #5#1;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO soloenter; |TIPO 6;
     |SI FSalida ! 0;
          |SI FSalida = 1;
               |PTEC 807;
          |SINO;
               |SI FSalida ! 2;
                    |ERROR;
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO forpa; |TIPO 0;
     #5#0 = #5#0 > ""; |PINTA #5#0;
     |SI #5#0 = "T"; |Y #4#3 = "N";
          |MENAV "0000Tipo Cobro NO PERMITIDO para este Cajero...";
          |ERROR;
     |FINSI;
     |SI #5#0 = "C"; |Y #4#2 = "N";
          |MENAV "0000Tipo Cobro NO PERMITIDO para este cajero...";
          |ERROR;
     |FINSI;
     |SI #5#0 = "D"; |Y #4#4 = "N";
          |MENAV "0000Tipo Cobro NO PERMITIDO para este cajero...";
          |ERROR;
     |FINSI;
     |SI #5#0 = "H"; |Y #4#5 = "N";
          |MENAV "0000Tipo Cobro NO PERMITIDO para este cajero...";
          |ERROR;
     |FINSI;
     |SI #5#0 = "T";
          |C_M #5#4, 1, "S";
     |SINO;
          |C_M #5#4, 1, "N";
     |FINSI;
     |SI #5#0 = "R"; |O #5#0 = "A"; |O #5#0 = "L";
          |PTEC 802;
     |FINSI;
|FPRC;

|PROCESO entrega7; |TIPO 7;
     |SI #5#0 = "D"; |O #5#0 = "H";
          #5#2 = 0;
          #5#6 = 0;
     |SINO;
          |SI aEsEuro = "S"; |Y Campo = 2;
               #5#2 = #5#1;
               #5#6 = 0;
          |FINSI;
          |SI aEsEuro = "N"; |Y Campo = 6;
               #5#6 = #5#5;
               #5#2 = 0;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO entrega0; |TIPO 0;
     err = 0;
     #5#3 = (#5#2+((#5#6/166.386)!EURODB)) - #5#1;
     |SI #5#3 < 0; #5#3 = 0; |FINSI;
     |PINTA #5#3;
     |SI aEsEuro = "S"; |Y Campo = 2;
          |SI #5#0 = "D";
               nNume = #5#2 + ((#5#6/166.386)!EURODB);
               |SI nNume ] #5#1;
                    #5#0 = "E"; |PINTA #5#0;
               |SINO;
                    |AVISO;
                    err = #5#1 - nNume;
                    alfa = "2400SImporte DEUDA de: " + err + ". Es Correcto (S/N).: ";
                    |CONFI alfa;
                    err = FSalida;
                    |SI err = 0;
                         |HAZ PideCliente;
                         |PTEC 802; ||para que no entre en la ptas
                    |FINSI;
               |FINSI;
          |FINSI;
          |SI #5#0 = "H";
               nNume = #5#2 + ((#5#6/166.386)!EURODB);
               |SI nNume ] #5#1;
                    #5#0 = "E"; |PINTA #5#0;
               |SINO;
                    |SI #3#101 = 0;
                         alfa  = ":" + apli + "/tabusagr taempre1 " + emp;
                    |SINO;
                         alfa = "tabusagr" + &59 + fichero;
                    |FINSI;
                    |SUB_EJECUTA_NP alfa;
                    |SI xhabita = 0; err = 1; |FINSI;
               |FINSI;
          |FINSI;
          |SI #5#0 = "E";
               |SI #5#3 = 0;
                    #5#6 = (#5#1 - #5#2) * 166.386;
                    |SI #32#24 = "N";
                         #5#6 = 0;
                    |FINSI;
               |FINSI;
          |FINSI;
     |FINSI;
     |SI aEsEuro = "N"; |Y Campo = 6;
          #5#3 = #5#6 - #5#5;
          |SI #5#3 < 0; #5#3 = 0; |FINSI;
          |PINTA #5#3;
          |SI #5#0 = "D";
               |SI #5#6 ] #5#5;
                    #5#0 = "E"; |PINTA #5#0;
               |SINO;
                    |AVISO;
                    err = #5#6 - #5#5;
                    alfa = "2400SImporte DEUDA de: " + err + ". Es Correcto (S/N).: ";
                    |CONFI alfa;
                    err = FSalida;
               |FINSI;
          |FINSI;
          |SI #5#0 = "H";
               |SI #5#6 ] #5#5;
                    #5#0 = "E"; |PINTA #5#0;
               |SINO;
                    |SI #3#101 = 0;
                         alfa  = ":" + apli + "/tabusagr taempre1 " + emp;
                    |SINO;
                         alfa = "tabusagr" + &59 + fichero;
                    |FINSI;
                    |SUB_EJECUTA_NP alfa;
                    |SI xhabita = 0; err = 1; |FINSI;
               |FINSI;
          |FINSI;
     |FINSI;
     |SI err ! 0;
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO PathHotel;
     alfa = #3#100; |QBF alfa;
     emp = alfa % -202;
     alf1 = alfa % -109;
     alfa = alfa - alf1;
     alf1 = "";
     apli = "";
     |PARA i = -101; |SI i ] -10000; |HACIENDO i = i - 100;
          alf1 = alfa % i;
          |SI alf1 = "\"; |O alf1 = "/"; |SAL; |FINSI;
          apli = alf1 + apli;
     |SIGUE;
|FPRC;

|PROCESO CargaHabi;
     |SI #3#101 = 0; |ACABA; |FINSI;
     |HORA alfa;
     alf1 = alfa % 102;
     beta = alf1 * 60;
     alf1 = alfa % 402;
     beta = beta + alf1;

     bet0 = beta - minu_ant;
     |SI minu_ant = 0; |O bet0 < 0; |O bet0 ] #3#101;
          |PUSHV 0501 2380;
          |COLOR 0, 0; |ATRI 0;
          |BLANCO 1128 1560;
          |COLOR 14, 0; |ATRI 0;
          |CUADRO 1128 1560;
          |COLOR 14, 0; |ATRI P;
          |PINTA 1332, "ACTUALIZANDO HABITACIONES";
          |ATRI 0;
          |DFICE path; |QBF path;
          minu_ant = beta;
          alfa = ":" + apli + "/tabusagr taempre1 " + emp;
          alfa = alfa + &59 + path + fichero;
          |SUB_EJECUTA_NP alfa;
          |POPV;
     |FINSI;
|FPRC;

|PROCESO imprimeA;
     |SI tiim=1; |Y #0#8 = "*"; |ACABA; |FINSI;
     |HAZ CopLinFan;
     |PRINT;
     #0#8 = "*";
     |GRABA 020#0a;
|FPRC;

|DEFBUCLE imprmes;
     #0#1;
     ;
     almacen,nMesa,1;
     almacen,nMesa,999;
     be;
     NULL,imprimeA;
|FIN;

|PROCESO ImpreTiqueA;

     |CONFI "0000SImpresion Total (S/N).: ";
     |SI FSalida = 0;
          tiim=0;
     |SINO;
          tiim=1;
     |FINSI;

     |SI #3#21 ! "S"; |ACABA; |FINSI;
     |SI Mensa = "S"; |MENSA "2405Imprimiendo..."; |FINSI;

     |ABRE #7;
     #7#26 = #13#36;
     |LEE 001#7=;
     |SI FSalida ! 0; |DDEFECTO #7; |FINSI;
     |CIERRA #7;

     |SI #3#64 = "S"; |Y nMesa ] #3#91;
          informe = #7#34;  |QBF informe;
          informl = #7#35;  |QBF informl;
          informp = #7#36;  |QBF informp;
          ||-> puede que no esten definidos (versiones anteriores)
          |SI informe = ""; informe = #7#21; |QBF informe; |FINSI;
          |SI informl = ""; informl = #7#32; |QBF informl; |FINSI;
          |SI informp = ""; informp = #7#33; |QBF informp; |FINSI;
     |SINO;
          informe = #7#21;   |QBF informe;
          informl = #7#32;   |QBF informl;
          informp = #7#33;   |QBF informp;
     |FINSI;

     Impresora = impr_tpv;   || VIC
     |IMPRE -1;
     |SI FSalida = 0;
          |SI #3#90 = 1;
               |INFOR informe;
               |SI FSalida = 0;
                 |HAZ CopCabFan;
                    |PRINT;
                    |PIEINF; |FININF;
               |FINSI;
               |INFOR informl;
               |SI FSalida = 0;
                    |BUCLE imprmes;
                    |PIEINF; |FININF;
               |FINSI;
               |INFOR informp;
               |SI FSalida = 0;
                    |PRINT;
                    |PIEINF; |FININF;
               |FINSI;
               |FINIMP;
          |FINSI;
          |SI #3#90 = 2;
               |INFOR informe;
               |SI FSalida = 0;
                    |HAZ CopCabFan;
                    |BUCLE imprmes;
                    |PIEINF;
                    |FININF;
               |FINSI;
               |FINIMP;
          |FINSI;
     |SINO;
          |MENAV "0000No se encuentra activada la impresora";
     |FINSI;
     |SI Mensa = "S"; |BLIN 24; |FINSI;
|FPRC;

|PROCESO CopCabFan;   || Copia Fantasma Tiquetk Para Impresion.
     |DDEFECTO #13;
     #13#0  = "Avance";                      || NUMERO TICKET.
     #13#36 = aSesion;                       || SERIE.
     #13#1  = #24#0;                         || CLIENTE.
     #13#10 = nEfe + nTar + nChe;            || ENTREGA.
     #13#11 = nCambio;                       || CAMBIO.
     #13#12 = acaja;                         || CAJA.
     #13#14 = afecha;                        || FECHA.
     |SI sw = 1;
          #13#15 = 0;
     |SINO;
          #13#15 = nDife;                    || Resto
     |FINSI;
     |HAZ FormaPago;
     #13#33 = camarero;                      || CAJERO.
     #13#34 = cHora;                         || HORA.
     #13#35 = "T"                            || TIPO.
     #13#37 = "";
     #13#38 = nEfe - nCambio;                || COBRADO EFECTIVO.
     #13#39 = nChe;                          || Cobrado Cheque
     #13#40 = nTar;                          || Cobra Tarjeta
     #13#41 = nDeu;                          || Deuda
     #13#59 = nHab;                          || Cargo Hab.
     #13#46 = #5#4;                          || Codi Tarjeta
     #13#56 = nMesa;                         || N§ Mesa
     #13#55 = nComensal;                     || N§ Comensales...
     #13#57 = aSerie;
     #13#58 = nNumero;
     #13#60 = xhotel;                        || Hotel si es con cargo a  la  Habitacion.
     #13#61 = xhabita;                       || Habit '' ''  ''  ''   '' ''   ''  ''
     #13#65 = #24#6;                         || Cuenta Cta.
||   |GRABA 041#13b;   || No Graba Por eso es fantasma je je....
|FPRC;

|PROCESO CopLinFan;      || Copia Fantasma de lineas.....
     nTipoIVA = #0#11;
     |HAZ LeeIVA;

     |SI #3#109 = "S";
          nPrecio = #0#5;
          nPreNeto = #0#5 / (1 + (enIva / 100));
     |SINO;
          nPreNeto = #0#5;
          nPrecio = #0#5 > enIva;
     |FINSI;
     nBase = #0#4 * nPreNeto;
     nTota = #0#4 * nPrecio;
     nCuota = nTota - nBase;
     |DDEFECTO #14;
     #14#0  = #13#0;                         || NUMERO TIQUET.
     #14#20 = #13#36;                        || SERIE.
     #14#1  = #0#0;                          || LINEA.

     #14#2  = #0#2;                          || ARTICULO.
     #14#4 =  #13#1;                         || CLIENTE
     #14#5  = #0#4;                          || UNIDADES.
     #14#6  = nPrecio;                       || PRECIO.
     #14#7  = nTota;                         || IMPORTE LINEA.
     #14#8  = #0#3;                          || DESCRIPCION.
     #14#9  = #13#14;                        || FECHA.

     #14#10 = nPreNeto;                      || PRECIO NETO.
     #14#15 = acaja;                         || NUMERO DE CAJA.
     #14#16 = cHora;                         || HORA.
     #14#17 = #0#7;                          || CAJERO.
     #14#18 = "T";                           || TIPO.
     #14#19 = nCuota;                        || CUOTA IVA.
     #14#21 = nBase;                         || BASE IMPONIBLE.
     #14#26 = #0#10;                         || Tarifa Especial
     #14#29 = #0#12;                         || Uni2
     #14#30 = #0#13;                         || Partida
     #14#31 = #0#14;                         || Merma
     |HAZ ActCab;
|FPRC;

|PROCESO pregunta;
     |CONFI "0000NPasar todos los servicios (S/N).: ";
     |SI FSalida = 0;
          tiim = 0;
     |SINO;
          tiim = 1;
     |FINSI;
|FPRC;

|PROCESO inf2;
     #30#0 = 0;
     #30#1 = 1;
     #30#10 = almacen;
|FPRC;

|PROCESO sup2;
     #30#0 = 999;
     #30#1 = 999;
     #30#10 = almacen;
|FPRC;


|PROCESO presenta;
     |DELINDEX #30;
     |ABRE #30;
     lindesco = 0;
     linea = 0;
     |ABRE #0;
     #0#0 = 0;
     #0#1 = MesaElige;
     #0#9 = almacen;
     |LEE 101#0];
     |PARA; |SI FSalida = 0; |Y #0#1 = MesaElige; |HACIENDO;
          linea = linea + 1;
          #30#0 = #0#0;
          #30#1 = #0#1;
          #30#10 = almacen;
          #30#2 = #0#2;
          #30#3 = #0#3;
          #30#4 = #0#4;
          #30#5 = #0#5;
          #30#6 = #0#6;
          #30#7 = #0#7;
          #30#8 = #0#8;
          #30#9 = " ";
          #30#11 = #0#10;
          #30#12 = #0#11;
          #30#13 = #0#12;                         || Uni2
          #30#14 = #0#13;                         || Partida
          #30#15 = #0#14;                         || Merma
          #30#16 = #0#16;
          #30#17 = #0#17;
          #30#18 = #0#18;
          |GRABA 020#30n;
          |LEE 000#0s;
     |SIGUE;
     lindesco = linea;
     |PUSHV 0501 2380;
     |MENSA "0000- TRASPASO SERVICIOS - <INTRO> -> Seleccion Servicio";
     |ENTLINEAL #1#30, -1, 4, 21, 1, inf2, sup2;
     |POPV;
     |CIERRA #30;
|FPRC;

|PROCESO marca; |TIPO 0;
    |SI FSalida = 0;
      |SI #30#9 ! "*";
             #30#9 = "*";
      |SINO;
             #30#9 = " ";
      |FINSI;
      |PINTA #30#9;
      |GRABA 020#30a;
    |FINSI;
|FPRC;

|PROCESO carprese;
     |DELINDEX #30;
     |ABRE #30;
     lindesco = 0;
     linea = 0;
     |ABRE #0;
     #0#0 = 0;
     #0#1 = MesaElige;
     #0#9 = almacen;
     |LEE 101#0];
     |PARA; |SI FSalida = 0; |Y #0#1 = MesaElige; |HACIENDO;
          linea = linea + 1;
          #30#0 = #0#0;
          #30#1 = #0#1;
          #30#10 = almacen;
          #30#2 = #0#2;
          #30#3 = #0#3;
          #30#4 = #0#4;
          #30#5 = #0#5;
          #30#6 = #0#6;
          #30#7 = #0#7;
          #30#8 = #0#8;
          #30#9 = "*";
          #30#11 = #0#10;
          #30#12 = #0#11;
          #30#13 = #0#12;                         || Uni2
          #30#14 = #0#13;                         || Partida
          #30#15 = #0#14;                         || Merma
          #30#16 = #0#16;
          #30#17 = #0#17;
          #30#18 = #0#18;
          |GRABA 020#30n;
          |LEE 000#0s;
     |SIGUE;
     lindesco = linea;
     |CIERRA #30;
|FPRC;

|PROCESO cargapa;
     |DELINDEX #30;
     |ABRE #30;
     totpavi = 0;
     #0#0 = 0;
     #0#1 = nMesa;
     #0#9 = almacen;
     |LEE 101#0];
     |PARA; |SI FSalida = 0; |Y #0#1 = nMesa; |HACIENDO;
          #30#0 = #0#0;
          #30#1 = #0#1;
          #30#10 = almacen;
          #30#2 = #0#2;
          #30#3 = #0#3;
          #30#4 = #0#4;
          #30#5 = #0#5;
          #30#6 = #0#6;
          #30#7 = #0#7;
          #30#8 = #0#8;
          #30#9 = " ";
          #30#11 = #0#10;
          #30#12 = #0#11;
          #30#13 = #0#12;                         || Uni2
          #30#14 = #0#13;                         || Partida
          #30#15 = #0#14;                         || Merma
          #30#16 = #0#16;
          #30#17 = #0#17;
          #30#18 = #0#18;
          |GRABA 020#30n;
          |LEE 000#0s;
     |SIGUE;

     |PUSHV 0501 2380;
     |MENSA "0000- COBRO PARCIAL - <INTRO> -> Seleccion Servicios a Cobrar";
     |ENTLINEAL #1#30, -1, 4, 21, 1, inf2, sup2;

     |POPV;
     totpavi = 0;
     |ABRE #30;
     #30#0 = 0;
     #30#1 = nMesa;
     #30#10 = almacen;
     |LEE 101#30];
     |PARA; |SI FSalida = 0; |Y #30#1 = nMesa; |HACIENDO;
          |SI #30#9 = "*";
               |SI #3#109 = "S";
                    totpavi = totpavi + #30#6;
               |SINO;
                    nTipoIVA = #30#12;
                    |HAZ LeeIVA;
                    totpavi = totpavi + (#30#6 > enIva);
               |FINSI;
          |FINSI;
          |LEE 000#30s;
     |SIGUE;
     |CIERRA #30;
|FPRC;
-----------------------------------------------------------------------------
|PROCESO CopiaCab2;
     |DDEFECTO #13;
     #13#0  = nUltimo;                       || NUMERO TICKET.
     #13#36 = aSesion;                        || SERIE.
     #13#1  = #24#0;                         || CLIENTE.
     #13#10 = nEfe + nTar + nChe;            || ENTREGA.
     #13#11 = nCambio;                       || CAMBIO.
     #13#12 = acaja;                         || CAJA.
     #13#14 = afecha;                        || FECHA.
     |SI sw = 1;
          #13#15 = 0;
     |SINO;
          #13#15 = nDife;                    || Resto
     |FINSI;
     |HAZ FormaPago;
     #13#33 = camarero;                      || CAJERO.
     #13#34 = cHora;                         || HORA.
     #13#35 = "T"                            || TIPO.
     #13#37 = "";
     #13#38 = nEfe - nCambio;                || COBRADO EFECTIVO.
     #13#39 = nChe;                          || Cobrado Cheque
     #13#40 = nTar;                          || Cobra Tarjeta
     #13#41 = nDeu;                          || Deuda
     #13#59 = nHab;                          || Cargo Hab.
     #13#46 = #5#4;                          || Codi Tarjeta
     #13#56 = nMesa;                         || N§ Mesa
     #13#55 = nComensal;                     || Comensales....
     #13#57 = aSerie;
     #13#58 = nNumero;
     #13#60 = xhotel;                        || Hotel si es con cargo a  la  Habitacion.
     #13#61 = xhabita;                       || Habit '' ''  ''  ''   '' ''   ''  ''
     #13#65 = #24#6;                         || Cta.Contable....

     |SI #32#20 = "S"; |Y nTar ! 0;
          |ABRE #20;
          #20#0 = #5#4;
          |LEE 000#20=;
          |SI FSalida = 0;
               #13#65 = #20#5;
          |FINSI;
          |CIERRA #20;
     |FINSI;
     |GRABA 041#13b;
|FPRC;

|PROCESO CopiaLin2;
     #2#0 = #30#2;
     |LEE 021#2=;

     nTipoIVA = #0#11;
     |HAZ LeeIVA;

     |SI #3#109 = "S";
          nPrecio = #30#5;
          nPreNeto = #30#5 / (1 + (enIva / 100));
     |SINO;
          nPreNeto = #30#5;
          nPrecio = #30#5 > enIva;
     |FINSI;
     nBase = #30#4 * nPreNeto;
     nTota = #30#4 * nPrecio;
     nCuota = nTota - nBase;

     linea = linea + 1;
     |DDEFECTO #14;
     #14#0  = #13#0;                         || NUMERO TIQUET.
     #14#20 = #13#36;                        || SERIE.
     #14#1  = linea;                         || LINEA.

     #14#2  = #30#2;                          || ARTICULO.
     #14#3  = #3#87;                          || ALMACEN.
     #14#4 =  #13#1;                          || CLIENTE
     #14#5  = #30#4;                          || UNIDADES.
     #14#6  = nPrecio;                        || PRECIO.
     #14#7  = nTota;                          || IMPORTE LINEA.
     #14#8  = #30#3;                          || DESCRIPCION.
     #14#9  = #13#14;                         || FECHA.

     #14#10 = nPreNeto;                      || PRECIO NETO.
     #14#11 = #2#2;                          || FAMILIA.
     #14#12 = #2#3;                          || SUBFAMILIA.
     #14#13 = #0#11;                        || TIPO DE IVA.
     #14#15 = acaja;                         || NUMERO DE CAJA.
     #14#16 = cHora;                         || HORA.
     #14#17 = #30#7;                         || CAJERO.
     #14#18 = "T";                           || TIPO.
     #14#19 = nCuota;                        || CUOTA IVA.
     #14#21 = nBase;                         || BASE IMPONIBLE.
     #14#22 = #2#128;                        || CODIGO DE BARRAS.
     #14#23 = #30#4 * #2#9;                  || COSTE.
     #14#26 = #30#11;                        || Tarifa Especial
     #14#29 = #30#13;                         || Uni2
     #14#30 = #30#14;                         || Partida
     #14#31 = #30#15;                         || Merma
     |GRABA 020#14n;
     |HAZ ActCab;
     |GRABA 020#13a;
|FPRC;

|PROCESO Copia2;
     |SI Mensa = "S"; |MENSA "2405Copiando..."; |FINSI;
     |ABRE #30;
     swvic = 0;
     #30#0 = 0;
     #30#1 = nMesa;
     #30#10 = almacen;
     |LEE 000#30];
     |PARA; |SI FSalida = 0; |Y #30#1 = nMesa; |HACIENDO;
          |SI #30#9 = "*";
               |SI swvic = 0;
                    |HAZ CopiaCab2;
                     swvic = 1;
               |FINSI;
               |HAZ CopiaLin2;
           |FINSI;
           |LEE 000#30s;
     |SIGUE;
     |CIERRA #30;
     |SI Mensa = "S"; |BLIN 24; |FINSI;
|FPRC;
-----------------------------------------------------------------------------
|PROCESO Limpia2;
     |SI Mensa = "S"; |MENSA "2405Borrando mesa..."; |FINSI;
     |ABRE #30;
     |ABRE #0;
     #30#0 = 0;
     #30#1 = nMesa;
     #30#10 = almacen;
     |LEE 101#30];
     #0#0 = 0;
     #0#1 = nMesa;
     #0#9 = almacen;
     |LEE 000#0];
     |PARA; |SI FSalida = 0; |Y #0#1 = nMesa; |Y #0#9 = almacen; |HACIENDO;
          |SI #30#9 = "*";
               |BORRA 021#0a;
               nabo = -1; |HAZ ActMesa;
          |FINSI;
          |LEE 000#0s;
          |LEE 000#30s;
     |SIGUE;
     |CIERRA #0;
     |CIERRA #30;
     |SI Mensa = "S"; |BLIN 24; |FINSI;
|FPRC;

|PROCESO salde; |TIPO 0;
  |SI FSalida = 9;
          |PTEC 807;   ||       -> Ctrl-C
  |FINSI;
|FPRC;

|PROCESO miraora;
     |ABRE #18;
     |DDEFECTO #18;
     #18#63 = empresa;
     #18#0 = acaja;
     #18#1 = fechasis;
     |LEE 000#18=;
     |SI FSalida = 0;
          |CIERRA #18;
          |SI #18#56 = "N";
               aMensa = "0000Cierre Auto. CANCELADO, Resumen " + #18#0 + "/" + #18#1 + " YA CERRADO";
               |MENAV aMensa;
          |FINSI;
          |ACABA;
     |FINSI;
     |CIERRA #18;

     |HORA vhora;
     vha = vhora % 102;
     vh  = vha;
     swcierra = 0;
     |SI vh ] 8;
          |ABRE #31;
          |PINPA #0#31;
          |MENAV "0000 Cierre Automatico...";
          |CIERRA #4;
          ||------------ Primero el diario
          |ABRE #18;      || Fichero diario
          #18#63 = empresa;
          #18#0  = acaja;     || Numero caja fichero cobro
          #18#1  = afecha;
          |LEE 101#18=;
          #18#56 = "N";  || Marco el diario como cerrado
          |GRABA 101#18a;
          |LIBERA #18;
          |CIERRA #18;
          ||----------- Despues el de configuracion
          |ABRE #3;      || Fichero caja (configuracion)
          #3#0 = acaja;     || Numero caja (cierre)
          |LEE 101#3=;
          #3#23 = "N";  || Marco como cerrada
          #3#24 = afecha;|| Fecha del cierre
          |GRABA 101#3a;
          |LIBERA #3;
          |CIERRA #3;
          afecha = fechasis;     || le doy la nueva fecha al sistema para trabajar...
          swcierra = 1;
          ||----------Verifico que no exista el resumen diario
          |ABRE #18;
          |DDEFECTO #18;
          #18#63 = empresa;
          #18#0 = acaja;
          #18#1 = afecha;
          |ABRE #3;         ||Compruebo Existencia parametros caja
          #3#0 = acaja;
          |LEE 101#3=;
          #18#2 = #3#26;              ||Provision
          #18#55 = #18#55 + #18#2;
          #18#56 = "S";           ||Caja abierta en diario
          #3#23 = "S";            ||caga abierta en Parametros
          #3#24 = afecha;         ||Fecha de Apertura
          |GRABA 020#18n;
          |GRABA 020#3a;
          |CIERRA #18;
          |CIERRA #3;
          |HAZ DefectoCaja;
     |FINSI;
|FPRC;

|PROCESO ImpLinDir;     ||Impresion Directa Linea
     |HAZ imprelineas;
|FPRC;

|PROCESO imprelineas;
     Impresora = impr_tpv;
     |IMPRE -1;
     |DDEFECTO #agifa502;
     #agifa502#2 = #tpv00001#4;    ||Arti
     #agifa502#5 = #tpv00001#4;    ||Uni
     #agifa502#8 = #tpv00001#3;    ||Des
     #agifa502#6 = #tpv00001#5;    ||PVP con
     #agifa502#7 = #tpv00001#6;    ||Total con
     |INFOR infolin;  |PRINT; |PIEINF; |FININF;
     |FINIMP;
|FPRC;

|PROCESO CogeInformes;
     |ABRE #7;
     |SI #3#39 = "S"; |Y #3#32 = "N";
          #7#26 = #13#36;
     |SINO;
          #7#26 = #3#105;
     |FINSI;
     |LEE 001#7=;
     |SI FSalida ! 0; |DDEFECTO #7; |FINSI;
     |CIERRA #7;
     |SI #3#64 = "S";
          informe = #7#34;  |QBF informe;
          informl = #7#35;  |QBF informl;
          informp = #7#36;  |QBF informp;
          ||-> puede que no esten definidos (versiones anteriores)
          |SI informe = ""; informe = #7#21; |QBF informe; |FINSI;
          |SI informl = ""; informl = #7#32; |QBF informl; |FINSI;
          |SI informp = ""; informp = #7#33; |QBF informp; |FINSI;

          infocab = informe;
          infolin = informl;
          infopie = informp;
     |SINO;
          informe = #7#21;   |QBF informe;
          informl = #7#32;   |QBF informl;
          informp = #7#33;   |QBF informp;
          infocab = informe;
          infolin = informl;
          infopie = informp;
     |FINSI;
|FPRC;

|PROCESO abre_impre;
     |SI cabeza ! 1;
          cabeza = 1;
          Impresora = impr_tpv;
          |IMPRE -1;
          |INFOR infocab; |PRINT; |PIEINF; |FININF;
          |FINIMP;
     |FINSI;
|FPRC;

|PROCESO fin_imp;
     cabeza = 0;
     Impresora = impr_tpv;
     |IMPRE -1;
     |INFOR infopie;    |PRINT; |PIEINF; |FININF;
     |FINIMP;
|FPRC;

|PROCESO CB_Compuesto;
     |SI #3#148 ! "S"; |ACABA; |FINSI;

     nModo = (FEntrada / 100) ! 0;
     |SI nModo ! 1; |ACABA; |FINSI;

     aBarra = #0#2;

     aAlf2 = #3#162; |QBF aAlf2;
     |SI aAlf2 ! "";
          aLon = aAlf2 % A0;
          nLon = aLon;
          nLon = 100 + nLon;
          aAlfa = #0#2 % nLon;
          |SI aAlfa ! aAlf2; |ACABA; |FINSI;
          |SI #3#164 = "N";
               nLon = nLon - 100;
               aAlfa = #0#2; |QBF aAlfa;
               aLon = aAlfa % A0;
               nLon1 = aLon;
               nLon = (100 * (nLon+1)) + (nLon1 - nLon);
               aAlf2 = #0#2 % nLon;
          |SINO;
               aAlf2 = #0#2;      || prefijo incluido en el c.b.
          |FINSI;
     |SINO;
          aAlf2 = #0#2;
     |FINSI;

     aAlfa = (("00" + #3#149) % -102) + (("00" + #3#150) % -102);
     #0#2 = aAlf2 % aAlfa;

     |SI #3#152 = 0;
          #0#4 = 1;
     |SINO;
          aAlfa = (("00" + #3#151) % -102) + (("00" + #3#152) % -102);
          #0#4 = (A\(aAlf2 % aAlfa));
     |FINSI;

     aAlfa = (("00" + #3#158) % -102) + (("00" + #3#159) % -102);
     aAlf1 = (A\(aAlf2 % aAlfa));

     |SI #3#161 ! 0;
          aAlfa = (("00" + #3#160) % -102) + (("00" + #3#161) % -102);
          aAlfa = aAlf2 % aAlfa;
     |SINO;
          aAlfa = "";
     |FINSI;

     aAlfa = aAlf1 + "." + aAlfa;
     #0#5 = aAlfa;

     aAlfa = #0#2; |QBT aAlfa;
     |SI aAlfa ! "";
          |ABRE #2;
          #2#0 = #0#2;
          |LEE 000#2=;
          |SI FSalida = 0;
               nSW_CB = 1;
          |SINO;
               nSW_CB = 0;
          |FINSI;
          |CIERRA #2;
     |FINSI;
     |PINTA #0#2;
     |PINTA #0#4;
     |PINTA #0#5;
|FPRC;

|PROCESO DesactivaScaner;
     aValorIni = "TECLA_SCANNER=0";
     aValorAntiguo = "";
     |VALOR_INI aValorIni, aValorAntiguo;
|FPRC;

|PROCESO ActivaScaner;
     aValorIni = "TECLA_SCANNER=" + aValorAntiguo;
     aValorAntiguo = "";
     |VALOR_INI aValorIni, aValorAntiguo;
|FPRC;

|PROCESO AbonaLinea;
     |DDEF aAlfa;
     aAlfa = aAlfa + "tpv00001";
     |CARGA_DEF aAlfa, referen@;
     |SI FSalida = 0;
          aAlfa = Usuario;    |QBF aAlfa;
          aAlfa = "0" + aAlfa;
          |NOME_DAT #100 aAlfa;
          |ABRE #100;
          #100#9 = #0#9;
          #100#1 = #0#1;
          #100#0 = 9999;
          |LEE 000#100];
          |SI FSalida = 0;
               |LEE 000#100a;
          |SINO;
               |LEE 000#100u;
          |FINSI;
          |SI FSalida = 0;
               nLin = #100#0 + 1;
               #100#9 = #0#9;
               #100#1 = #0#1;
               #100#0 = #0#0;
               |LEE 000#100=;
               #100#0 = nLin;
               #100#4 = #100#4 * -1;
               #100#6 = #100#6 * -1;
               |GRABA 020#100n;
          |FINSI;
          |CIERRA #100;

          |SALVA_DATOS #referen@;
          |REPON_DATOS #tpv00001;

          |DESCARGA_DEF #referen@;

          |SI #agifa505#32 = "S";
               |HAZ abre_impre;
               |HAZ ImpLinDir;
          |FINSI;

          nabo = 1; |HAZ ActMesa;

          |HAZ lin_display;
          TVisor = nTOTAL;
          |HAZ Numero;

          |PTEC 807;
     |SINO;
          |ERROR;
     |FINSI;
|FPRC;

|| Chapuza motivada por:
|| Al elegir el articulo con F11 daba FSalida = 1 y se salida del programa
|| Tue Nov 21 10:51:06 CET 2006
|PROCESO Tipo66; |TIPO 66;
     |ABRE #2;
     #2#0 = #0#2;
     |LEE 000#2];
     |CONSULTA_CLAVE #1#2;
     |SI FSalida = 0;
          #0#2 = #2#0; |PINTA #0#2;
     |SINO;
          |ERROR;
     |FINSI;
     |CIERRA #2;
     FSalida = 0;
|FPRC;
