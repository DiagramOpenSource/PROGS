|FICHEROS;
     diaw0001 #0;
     diam0001 #7;
     diam0002 #8;
     diam0015 #15;
     diam0016 #16;
     diaz0001 #10,MANTE;
     agifa019 #19;
     agifa024 #24;
     agifa025 #25;
     agifa073 #73;
     agifa104 #104;
     agifa321 #321;
     agifa324 #324;
     diaz0004 #4,MANTE;
     diam0003 #3;

     &dsparm05@ #100;
     dsparm01@ #101;
|FIN;

|VARIABLES;
     &enTiva = 0;
     &efFiva = @;
     &eaDiva = "";
     &enIva = 0;
     &enRec = 0;
     nSw       = 0;
     aDat      = "";
     aDef1     = "";
     aDef2     = "";
     &aMoneda  = "";
     &aDivisa  = "";
     &enSwMenu = 0;
     &enForma  = 1;
     nLin      = 0;
     nCambio   = 0;
     i         = 0;
     nHay      = 0;
     aAlfa     = "";
     aFic1     = "";
     aFic2     = "";
     aIP       = "";
     aServidor = "";
     aFrom     = "";
     aTo       = "";
     aSubject  = "";
     aMensaje  = "";
     aDjunto   = "Sin asunto";
     aNume     = "";
     aBase     = "";
     aDHora    = "";
     aHHora    = "";
     aFamilia  = "";

     nTecnico  = 0;
     nDPedido  = 0;
     nHPedido  = 0;

     &eaCliente      = "";
     &eaFecha        = "";
     &eaSerie        = "";
     &eaFechaPedido  = "";
     &eaHora         = "";
     &eaFechaEntrega = "";
     &enTipoIVA      = 0;
     &enPedido       = 0;
|FIN;

|PROCESO Correo;
     #100#0 = #101#12;
     |LEE 020#100=;
     aFrom = #100#3; |QBF aFrom;

     aNume = ("000000" + #104#0) % -106;
     aSubject = "Visado pedido: " + #104#25 + "/" + aNume + " " + #104#8;
     aMensaje = aSubject;
     aServidor = #101#7;
     aIP = #101#8;
     |QBF aIP;
     |QBF aServidor;
     |DSCORREO_ENVIA aIP, aServidor, aFrom, aTo, aSubject, aMensaje, aDjunto;
     |SI FSalida ! 0;
          |MENAV "0000Ha habido un error durante el envio.";
     |FINSI;
|FPRC                                                                        ;

|PROCESO Min4;
     #4#0 = "";
     #4#1 = #10#19;
     #4#2 = 1;
|FPRC;

|PROCESO Max4;
     #4#0 = "";
     #4#1 = #10#19;
     #4#2 = 9999;
|FPRC;

|PROCESO diaz0004;
     |SI nSw = 1;
          nHay = 1;
          |ERROR10;
          |ACABA;
     |FINSI;

     #3#0 = #104#25;
     #3#1 = #104#0;
     #3#2 = #4#2;
     |LEE 101#3=;
     |SI FSalida ! 0;
         |DDEFECTO #3;
         #3#0 = #104#25;
         #3#1 = #104#0;
         #3#2 = #4#2;
         |GRABA 020#3b;
     |FINSI;

     #3#3 = #4#3;
     #3#4 = #4#4;
     #3#5 = #4#5;
     #3#6 = #4#6;
     |GRABA 020#3a;
     |LIBERA #3;

     #100#0 = #4#2;
     |LEE 000#100=;
     |SI FSalida = 0;
          |SI aTo = "";
               aTo = #100#3;
          |SINO;
               aTo = aTo + ";" + #100#3;
          |FINSI;
          |QBF aTo;
     |FINSI;
|FPRC;

|DEFBUCLE diaz0004;
     #4#1003;
     ;
     "     ", #10#19, 0001;
     "     ", #10#19, 9999;
     ;
     NULL, diaz0004;
|FIN;

|PROCESO GrabaTecnico;
     |SI #15#1  = aFamilia;  nTecnico = #15#4;  |FINSI;
     |SI #15#5  = aFamilia;  nTecnico = #15#8;  |FINSI;
     |SI #15#9  = aFamilia;  nTecnico = #15#12; |FINSI;
     |SI #15#13 = aFamilia;  nTecnico = #15#16; |FINSI;
     |SI #15#17 = aFamilia;  nTecnico = #15#20; |FINSI;
     |SI #15#21 = aFamilia;  nTecnico = #15#24; |FINSI;

     #100#0 = nTecnico;
     |LEE 040#100=;
     |SI FSalida ! 0;  |DDEFECTO #100;  |FINSI;

     #4#0 = "";
     #4#1 = #10#19;
     #4#2 = nTecnico;
     |LEE 101#4=;
     |SI FSalida ! 0;
         |DDEFECTO #4;
         #4#0 = "";
         #4#1 = #10#19;
         #4#2 = nTecnico;
         #4#3 = #100#1;
         #4#4 = "N";
         |GRABA 020#4b;
     |FINSI;
     #4#7 = "N";
     |GRABA 020#4a;
     |LIBERA #4;
|FPRC;

|PROCESO MiraTecnicos;
     |ABRE #16;
     #16#0  = #10#0;
     #16#1  = #10#1;
     #16#13 = #10#13;
     |LEE 040#16=;
     |SI FSalida ! 0;  |CIERRA #16;  |ACABA;  |FINSI;
     |CIERRA #16;

     aAlfa = #16#1 % -104;
     |ABRE #15;
     #15#71 = aAlfa;
     |LEE 040#15=;
     |SI FSalida ! 0;  |DDEFECTO #15;  |FINSI;
     |CIERRA #15;

     |ABRE #4;

     nTecnico = 0;

     |SI #16#34 ! 0;  aFamilia = #16#32;  |HAZ GrabaTecnico;  |FINSI;
     |SI #16#39 ! 0;  aFamilia = #16#37;  |HAZ GrabaTecnico;  |FINSI;
     |SI #16#44 ! 0;  aFamilia = #16#42;  |HAZ GrabaTecnico;  |FINSI;
     |SI #16#49 ! 0;  aFamilia = #16#47;  |HAZ GrabaTecnico;  |FINSI;
     |SI #16#54 ! 0;  aFamilia = #16#52;  |HAZ GrabaTecnico;  |FINSI;
     |SI #16#59 ! 0;  aFamilia = #16#57;  |HAZ GrabaTecnico;  |FINSI;

     |CIERRA #4;
|FPRC;

|PROCESO Tipo0; |TIPO 0;
     |SI FSalida = 0; |Y #10Campo = Contenido;
          |SI #10#17 = "*";
               #10#17 = " ";  |PINTA #10#17;
               |GRABA 020#10a;
          |SINO;
               |ET E1;
               |ENCAMPO #16#10;
               |ENCAMPO #18#10;
               |SI FSalida = 2; |VETE E1; |FINSI;

               |HAZ MiraTecnicos;

               |PUSHV 0501 2380;
               |ENTLINEAL #1#4, -3, 2, 22, 1, Min4, Max4;
               |POPV;

               nHay = 0; nSw = 1;  |BUCLE diaz0004;    nSw = 0;
               |SI nHay = 0;
                    |MENAV "0000Es obligado informar los visadores para dejarlo marcado.";
                    |ERROR;
               |SINO;
                    #10#17 = "*";  |PINTA #10#17;
                    |GRABA 020#10a;
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO LeeDiv;
     |ABRE #324;
     #324#0 = aDivisa;
     |LEE 000#324=;
     |CIERRA #324;
     nCambio = #324#2;
|FPRC;

|PROCESO FinCab;
     |HAZ CalCabAgifa104;
     |GRABA 020#104a;

     #7#14 = #104#25;
     #7#15 = #104#0;
     |GRABA 020#7a;

     |ABRE #16;
     #16#0  = #7#0;
     #16#1  = #7#1;
     #16#13 = #7#13;
     |LEE 101#16=;
     |SI FSalida = 0;
         #16#14  = #104#25;
         #16#15  = #104#0;
         #16#104 = "SI";
         |GRABA 020#16a;
         |LIBERA #16;
     |FINSI;
     |CIERRA #16;
|FPRC;


|PROCESO Lin;
     nLin = nLin + 1;

     |DDEFECTO #19;
     |ABRE #19;
     #19#0 = #8#4;
     |LEE 000#19=;
     |CIERRA #19;

     #73#28 = #104#25;
     #73#0 = #104#0;
     #73#1 = nLin;
     #73#2 = #19#0;                || articulo
     #73#3 = 1;                    || almacen
     #73#4 = #8#5;                 || descripcion
     #73#5 = #8#6;                 || unidades
     #73#42 = #8#6;                || unidades pedidas
     #73#6 = #8#8;                 || precio
     #73#14 = #0#6;                || tipo de IVA
     #73#38 = #8#8;                || precio-DIV
     #73#13 = #10#18;
     |GRABA 020#73n;
     enForma = 1;
     |HAZ AcumulaPV;
|FPRC;

|PROCESO Cab;
     |DDEFECTO #24;
     #24#0 = #7#0;
     |LEE 000#24=;

     |DDEFECTO #25;
     #25#0 = #24#25;
     |LEE 000#25=;

     aDivisa = #24#192;
     aMoneda = #24#193;
     |HAZ Divisas104;

|ET Otro;
     nLin = 0;
     #104#25 = #10#16;
     |SI enPedido = 0;  |Y eaSerie = "     ";
         enSwMenu = 1;
         |HAZ ContaPV7;
     |SINO;
         #104#0 = enPedido;
         #104#2 = eaFechaEntrega;
     |FINSI;
     |GRABA 000#104b;
     |SI FSalida = 100; |VETE Otro; |FINSI;

     #104#1  = #diaw0001#5;         || fecha pedido
     #104#4  = #24#0;
     #104#7  = #7#18;
     #104#8  = #24#1;
     #104#9  = #7#32;
     #104#19 = #7#4;
     #104#14 = #7#5;
     #104#20 = #7#6;
     #104#59 = #7#8;
     #104#15 = #7#9;

     #104#5  = #24#37;
     #104#6  = #24#38;
     #104#16 = #25#7;
     #104#17 = #24#156;
     #104#18 = #24#4;
     #104#23 = #24#41;
     #104#35 = aDivisa;
     |HAZ LeeDiv;
     #104#36 = nCambio;
     #104#37 = aMoneda;
     aDivisa = #agifa321#9;
     |HAZ LeeDiv;
     #104#40 = aDivisa;
     #104#41 = nCambio;
|FPRC;

|PROCESO diaz0001;
     |SI #10#17 = "*";
          nHay = 0; nSw = 1;  |BUCLE diaz0004;    nSw = 0;
          |SI nHay = 0;
               aAlfa = "0000Cliente: "+ #10#0 + " [" + #10#1 + "]" + &13 + "sin visadores.";
               |MENAV aAlfa;
               |ACABA;
          |FINSI;

          #7#0 = #10#0;
          #7#1 = #10#1;
          #7#13 = #10#13;
          |LEE 121#7=;
          |SI FSalida = 0; |Y #7#15 = 0;
               |HAZ Cab;
               |BUCLELIN 2 Lin #7;
               |HAZ FinCab;
               |LIBERA #7;

               aTo = "";
               |BUCLE diaz0004;
               |SI aTo ! "";
                    |CARGA_DEF aDef2, dsparm01@;
                    |SI FSalida = 0;
                         |PATH_DAT #101 aDat;
                         |ABRE #101; |LEE 000#101p; |CIERRA #101;
                         |HAZ  Correo;
                         |DESCARGA_DEF #dsparm01@;
                    |SINO;
                         |MENAV "     No puedo cargar el DEF dsparm01";
                    |FINSI;
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE diaz0001;
     #10#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, diaz0001;
|FIN;

|PROCESO diam0001;
     |PARA i = 0; |SI i [ 13; |HACIENDO i = i + 1;
          #10i = #7i;
     |SIGUE;
     #10#16 = #0#4;
     #10#19 = #10#19 + 1;
     #10#18 = #7#28;

     |SI eaSerie ! "     ";  |O enPedido ! 0;
         #10#17 = "*";
     |FINSI;

     |GRABA 020#10n;
     nHay = 1;
|FPRC;

|DEFBUCLE diam0001;
     #7#1;
     15;
     #0#0, #0#2, aDHora, nDPedido;
     #0#1, #0#3, aHHora, nHPedido;
     ;
     NULL, diam0001;
|FIN;

|PROCESO Min;
     #10#0 = "     ";
     #10#1 = "01.01.0000";
     #10#13 = "        ";
|FPRC;

|PROCESO Max;
     #10#0 = "zzzzzz";
     #10#1 = "31.12.9999";
     #10#13 = "zzzzzzzz";
|FPRC;

|PROCESO Tipo3; |TIPO 3;
     |DBASS aBase; |QBF aBase;

     aDef1 = aBase + "dspartes/def/dsparm05";
     aDef2 = aBase + "dspartes/def/dsparm01";
     aDat  = aBase + "dspartes/fich/";

     |ABRE #agifa321;
     |LEE 000#agifa321.p;
     |CIERRA #agifa321;

     aAlfa = Usuario;
     aFic1 = "1" + aAlfa;
     aFic2 = "2" + aAlfa;
     |NOME_DAT #10 aFic1; |DELINDEX #10;
     |NOME_DAT #4  aFic2; |DELINDEX #4;

     |ABRE #10;
     |BUCLE diam0001;
     |CIERRA #10;

     |SI nHay = 0;
          |MENAV "0000Seleccion vacia...";
     |SINO;
          |CARGA_DEF aDef1, dsparm05@;
          |SI FSalida = 0;
               |PATH_DAT #100 aDat;
               |ABRE #100;

               |SI eaSerie ! "     ";  |O enPedido ! 0;
                    |HAZ MiraTecnicos;
                    |PARA;  |SI;  |HACIENDO;
                         |PUSHV 0501 2380;
                         |ENTLINEAL #1#4, -3, 2, 22, 1, Min4, Max4;
                         |POPV;

                         nHay = 0;
                         nSw  = 1;
                         |BUCLE diaz0004;
                         nSw  = 0;
                         |SI nHay = 0;
                             |MENAV "0000Es obligado informar los visadores para dejarlo marcado.";
                         |SINO;
                             |SAL;
                         |FINSI;
                    |SIGUE;

                    |ABRE #104;
                    |ABRE #73;
                    |ABRE #24;
                    |ABRE #25;
                    |ABRE #7;
                    |ABRE #3;
                    |BUCLE diaz0001;
                    |CIERRA #3;
                    |CIERRA #7;
                    |CIERRA #25;
                    |CIERRA #24;
                    |CIERRA #73;
                    |CIERRA #104;
               |SINO;
                    |PINPA #0#10;
                    |ENTLINEAL #1#10, -1, 4, 22, 0, Min, Max;
                    |CONFI "0000NCrear pedidos";
                    |SI FSalida = 0;
                        |ABRE #104;
                        |ABRE #73;
                        |ABRE #24;
                        |ABRE #25;
                        |ABRE #7;
                        |ABRE #3;
                        |BUCLE diaz0001;
                        |CIERRA #3;
                        |CIERRA #7;
                        |CIERRA #25;
                        |CIERRA #24;
                        |CIERRA #73;
                        |CIERRA #104;
                    |FINSI;
               |FINSI;

               |CIERRA #100;
               |DESCARGA_DEF #dsparm05@;
          |SINO;
               |MENAV "     No puedo cargar el DEF dsparm05";
          |FINSI;
     |FINSI;
     |DELINDEX #4;
     |DELINDEX #10;
|FPRC;

|PROGRAMA;
     |SI eaSerie ! "     ";  |O enPedido ! 0;
         #0#0     = eaCliente;
         #0#1     = eaCliente;
         #0#2     = eaFecha;
         #0#3     = eaFecha;
         #0#4     = eaSerie;
         #0#5     = eaFechaPedido;
         #0#6     = enTipoIVA;
         nDPedido = 0;
         nHPedido = 999999;
         aDHora   = eaHora;
         aHHora   = eaHora;
         |HAZ Tipo3;
     |SINO;
         nDPedido = 0;
         nHPedido = 0;
         aDHora   = "        ";
         aDHora   = "zzzzzzzz";
         |MANTE #0;
     |FINSI;
|FPRO;

|PROCESO IVA001_0; |TIPO 0;
     enTiva = #0Campo;
     efFiva = #0#5;
     |HAZ SacaIVA;
     |SI eaDiva = "";
          |MENAV "0000No existe tipo iva para esta fecha...";
          |ERROR; |ACABA;
     |FINSI;
|FPRC;

|PROCESO IVA010_66; |TIPO 66;
     enTiva = #0Campo;
     efFiva = #0#5;
     |HAZ ConsuIVA;
     |SI eaDiva = "";
          |ERROR;
     |SINO;
          #0Campo = enTiva; |PINTA #0Campo;
     |FINSI;
|FPRC;
