|FICHEROS;
     dsm00034 #0;   || escandallo cabs.
     dsm00035 #1;   || escandallo lins.
     agifa019 #2;
     dsl00001 #36;       || Codigos de barras multiples
     agifa142 #142;
     dsm00238 #238;
     dsm00010 #910;
     agifa029 #29;
|FIN;

|VARIABLES;
     nModo = 0;
     nCodigo = 0;
     nPos = 0;
     aAlfa = "";
|FIN;

|INCLUYE i_varart;

|PROCESO AlEntrar1; |TIPO 7;
     nModo = (FEntrada/100) ! 0;
     |SI nModo = 1;
          |LEE 001#0u;
          |SI FSalida = 0;
              nCodigo = #0#0+1;
              |DDEFECTO #0;
              #0#0 = nCodigo;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO PideArt34;  |TIPO 7;
     nModo = (FEntrada / 100) ! 0;

     |SI #0#2 ! "                    ";
         |ABRE #2;
         #2#0 = #0#2;
         |LEE 000#2=;
         |SI FSalida ! 0;  |DDEFECTO #2;  |FINSI;
         |CIERRA #2;
         |SI #2#176 = "S";
             |C_M #0#2, 1, "N";
             |C_M #0#3, 1, "N";
             |ACABA;
         |FINSI;
     |FINSI;

     |ABRE #910;
     |LEE 040#910p;
     |SI FSalida ! 0;  |DDEFECTO #910;  |FINSI;
     |CIERRA #910;

     |SI #910#2 [ 2;    |ACABA;  |FINSI;

     eAlfa = #0Campo;
     |SUB_EJECUTA_NP "dsz99990";                 || Por Pantalla

     |SI eAlfa = "-1";  |PTEC 807;  |ACABA;  |FINSI;
     |SI eAlfa = "-2";              |ACABA;  |FINSI;

     eaArticulo = eAlfa;      ||  700000-4215

     #0Campo = eAlfa;
     |PINTA #0Campo;
     |PTEC 802;
|FPRC;

|PROCESO Calc634;  |TIPO 6;
     enAlta = 1;
     eaArticulo = #0Campo;
     |HAZ LlenaArticulo;
     |SI FSalida = 0;
          #0Campo = eaArticulo;
          |PINTA #0Campo;
     |SINO;
          |ERROR; |ACABA;
     |FINSI;

     |ABRE #142; |LEE 000#142=; |CIERRA #142;

     |SI #142#55 = "S";  || Codigos de Barras
          |ABRE #36;
          |SELECT #2#36;
          #36#3 = #0#2;
          |LEE 010#36=;
          |SI FSalida=0; |Y #0#2 ! #36#1;
               #0#2 = #36#1;
               |PINTA #0#2;
          |FINSI;
          |CIERRA #36;
     |FINSI;

     |ABRE #2;
     #2#0 = #0#2;
     |LEE 000#2=;
     |CIERRA #2;
|FPRC;

|PROCESO Tipo13_34; |TIPO 13;
     |C_P #0#6, 0, nPos;
     |C_P #0#7, 1, nPos;

     |ATRI R; |PINTA 1003, "KILOS   :"; |ATRI 0;
     |ABRE #2;
     #2#0 = #0#2;
     |LEE 000#2=;
     |SI FSalida = 0; |Y #2#179 = "S";
          |DDEFECTO #238;
          |ABRE #238;
          #238#0 = #2#201;
          |LEE 000#238=;
          |SI FSalida = 0;
               aAlfa = #238#2; |QBT aAlfa;
               aAlfa = ((aAlfa + "        ") % 108) + ":";
               |ATRI R; |PINTA 1003, aAlfa; |ATRI 0;
          |FINSI;
          |CIERRA #238;
          |SI #238#6 = 1;
               |C_V #0#6, 1, "S";
               |C_V #0#7, 1, "N";
          |SINO;
               |C_V #0#6, 1, "N";
               |C_V #0#7, 1, "S";
          |FINSI;
     |SINO;
          |C_V #0#6, 1, "S";
          |C_V #0#7, 1, "N";
     |FINSI;
     |CIERRA #2;

     |ABRE #29;
     #29#0 = #0#3;
     |LEE 000#29=;
     |CIERRA #29;
     |ATRI I; |PINTA 920, #29#1; |ATRI 0;
|FPRC;

|PROCESO DobleUni34; |TIPO 7;
     |C_M #0#6, 1, "S";
     eaProgVta = "dsm00034";
     |HAZ DobleUniF;
     |SI eaSw2Stock = "S";
          |C_M #0#6, 1, "N";
     |FINSI;
     |HAZ Tipo13_34;
     |C_V #0#6, 0, aAlfa;
     |SI aAlfa = "S";
          |PINTA #0#6;
     |SINO;
          |PINTA #0#7;
     |FINSI;
|FPRC;
