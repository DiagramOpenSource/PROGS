|FICHEROS;
     coimz006 #0;
     agifa019 #19;
     coima022 #22; ||Param .Correo
     coima023 #23; ||Bandeja
     coima024 #24; ||Pedidos
     agifa024 #240;
     dsm00003 #3;
|FIN;

|VARIABLES;
     nCampo = 0;
     aLetra = "";
     aReg = "";
     aTab = "";
 {99}Valor = "";
     nSw = 0;
     nHandle = 0;
     aDato = "";
     aFic = "";
     aLon = "";
     aNombre = "";
     nPos = 0;
     i = 0;
     aAlfa = "";
     nLin = 0;
     aBase = "";
     aDir = "";
     aDSCorreo = "";
     aUsu = "";
     aPwd = "";
     aPop3 = "";
     nError = 0;
     nCorreos = 0;
|FIN;

/*
o    1-Código Cliente
o    2- Nº dirección de entrega.
o    3-Representante.
o    4-Código Articulo
o    5-Unidades.
o    6-Pedido Completo.
o    7-Colección Completa.
o    8-E-mail
     9-Observa_1
     10-Observa_2
     11-Observa_2
     12-Su pedido
     13-F.Entrega
*/

|PROCESO Dato;
     aAlfa = aTab * 11;
     aDato = aDato + aTab;

     nCampo = 1;
     IValor = Valor1<-;
     aLon = aDato % A0;
     aReg = "";
     aTab = &9;
     |PARA i = 1; |SI i [ aLon; |HACIENDO i = i + 1;
          nPos = i * 100 + 1;
          aLetra = aDato % nPos;
          |SI aLetra = aTab;
               Valor = aReg;
               aReg = "";
               nCampo = nCampo + 1;
               IValor = IValor + 1;
          |SINO;
               aReg = aReg + aLetra;
          |FINSI;
          |SI nCampo > 90; |SAL; |FINSI;     || Por si.... :)
     |SIGUE;
     Valor = aReg;
|FPRC;

|PROCESO ActMail;
     |ABRE #3;
     #3#0 = #24#2;
     #3#1 = #24#3;
     |LEE 101#3=;
     |SI FSalida = 0;
          #3#24 = #24#11;
          |GRABA 020#3a;
          |SI #3#1 = 1;
               |ABRE #240;
               #240#0 = #24#2;
               |LEE 101#240=;
               |SI FSalida = 0;
                    #240#214 = #24#11;
                    |GRABA 020#240a;
               |FINSI;
               |CIERRA #240;
          |FINSI;
     |FINSI;
     |CIERRA #3;
|FPRC;

|PROCESO ProcesarCorreo;
     |ABRE #24;
     aFic = aNombre;
     |XABRE "U", aFic, nHandle;
     |SI FSalida ! 0;
          aAlfa = "0000Problemas procesando correo: " + aFic;
          |SI nSw = 0; |MENAV aAlfa; |FINSI;
     |SINO;
          nHandle = FSalida;
          nLin = 0
          |XLEE nHandle, 250, aDato;
          |PARA; |SI FSalida ] 0; |HACIENDO;
               |QBF aDato;
               |SI aDato ! "";
                    nLin = nLin + 1;
                    |DDEFECTO #24;
                    #24#0 = #23#0;
                    #24#1 = nLin;
                    |HAZ Dato;
                    #24#2 = Valor1;
                    #24#3 = Valor2;
                    #24#4 = Valor3;
                    #24#5 = Valor4;
                    #24#6 = Valor5;
                    #24#7 = Valor6;
                    #24#8 = Valor7;
                    #24#11 = Valor8;
                    #24#12 = Valor9;
                    #24#13 = Valor10;
                    #24#14 = Valor11;
                    #24#15 = Valor12;
                    #24#16 = Valor13;
                    |SI #24#8 = "S";
                         |DDEFECTO #19;
                         #19#0 = #24#5;
                         |LEE 000#19=;
                         #24#10 = #19#3;
                    |FINSI;
                    |ABRE #240;
                    #240#0 = #24#2;
                    |LEE 000#240=;
                    |SI FSalida = 0;
                         |GRABA 020#24n;
                         aAlfa = #24#11; |QBF aAlfa;
                         |SI aAlfa ! "";
                             |HAZ ActMail;
                         |FINSI;
                    |FINSI;
                    |CIERRA #240;
               |FINSI;
               |XLEE nHandle, 250, aDato;
          |SIGUE;
          |XCIERRA nHandle;
     |FINSI;
     |CIERRA #24;
|FPRC;

|PROCESO SacaNom;
     aFic = "";
     aLon = aNombre % A0;
     nPos = -101;
     |PARA i = aLon; |SI i > 0; |HACIENDO i = i - 1;
          aAlfa = aNombre % nPos;
          |SI aAlfa = "/"; |O aAlfa = "\"; |SAL; |FINSI;
          aFic = aAlfa + aFic;
          nPos = nPos -100;
     |SIGUE;
|FPRC;

|PROCESO Historico;
     |HAZ SacaNom;
     |SI aFic = "_cuerpo_.txt";
          nLin = 0; |ACABA;
     |FINSI;

     |ABRE #23;
     |SELECT #2#23;
     #23#1 = aFic;
     |LEE 000#23=;
     |SI FSalida = 0;
          nLin = 0;
     |SINO;
          |SELECT #1#23;
          |LEE 000#23u;
          |SI FSalida = 0;
               nLin = #23#0 + 1;
          |SINO;
               nLin = 1;
          |FINSI;

          |HORA aAlfa;
          |DDEFECTO #23;
          #23#0 = nLin;
          #23#1 = aFic;
          #23#2 = ~;
          #23#3 = aAlfa;
          #23#4 = Usuario % 810;
          |GRABA 020#23n;

          |DBASE aBase;
          aAlfa = aBase + "004082"; |MKDIR aAlfa;
          aAlfa = aBase + "004082/adjuntos"; |MKDIR aAlfa;
          aAlfa  = aBase + "004082/adjuntos/" + aFic;
          |COPIA_FICHERO aNombre, aAlfa;
     |FINSI;
     |CIERRA #23;
|FPRC;

|PROCESO Recoge;
     aDir = #22#4; |QBF aDir;
     |MKDIR aDir

     |SI #22#8 = "F";
          aDSCorreo = #22#7;  |QBF aDSCorreo;
     |SINO;
          |SI #22#8 = "L"; |IP_REMOTO aDSCorreo; |FINSI;
          |SI #22#8 = "S"; |IP_LOCAL aDSCorreo; |FINSI;
     |FINSI;
     aUsu = #22#2; |QBF aUsu;
     aPwd = #22#6; |QBF aPwd;
     aPop3 = #22#1; |QBF aPop3;
     aNombre = "";

     aAlfa = "0000Recibiendo [" + aDSCorreo + "]";
     |MENSA aAlfa;
     |DSCORREO_RECIBE aDSCorreo, aPop3, aUsu, aPwd, aDir, aNombre;

     nError = FSalida;

     |PARA aAlfa = aNombre; |SI; |HACIENDO;
          aNombre = aNombre - ";";
          |SI aNombre = aAlfa; |SAL; |FINSI;
          aAlfa = aNombre;
     |SIGUE;

     |BLIN 4;
     |SI nError < 0;
          aAlfa = "    Problemas al recibir correo ..." + nError;
          |SI nSw = 0; |MENAV aAlfa; |FINSI;
     |FINSI;
     |SI nError = 0;
          |HAZ SacaNom;
          aAlfa = "    Correo recibido con exito [" + nError + "][" + aFic + "]";
          |MENSA aAlfa;
     |FINSI;
     |SI nError = 1;
          |SI nCorreos = 1;
               |SI nSw = 0; |MENAV "    No hay correo ..."; |FINSI;
          |SINO;
               nCorreos = nCorreos - 1;
               aAlfa = "0000" + nCorreos + " Correos recibidos...";
               |SI nSw = 0; |MENAV aAlfa; |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Captura; |TIPO 0; || Para pruebas;
     |SI FSalida = 13; ||F5;
          aAlfa = "F*.txt";
          |BROWSE aAlfa;
          aAlfa = aAlfa % 2; |QBF aAlfa;
          |SI aAlfa = "F"; aAlfa = ""; |FINSI;
          |SI aAlfa ! "";
               aNombre = "/tmp/" + Usuario;
               |RECIBE_FICHERO aAlfa, aNombre;
               |HAZ ProcesarCorreo;
               |SUB_EJECUTA_NP "coimz007";  ||Pase pedidos a gestion
          |FINSI;
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO Tipo3; |TIPO 3;
     |SI #0#0 ! "SI"; |ACABA; |FINSI;

     |ABRE #22; |LEE 000#22p; |CIERRA #22;
     |ABRE #19;
     nCorreos = 0;
     |PARA; |SI; |HACIENDO;
          nError = 0;
          nCorreos = nCorreos + 1;
          |HAZ Recoge;

          |SI nError ! 0; |SAL; |FINSI;

          |HAZ Historico;
          |SI nLin ! 0;
               |HAZ ProcesarCorreo;
          |FINSI;
     |SIGUE;
     |CIERRA #19;
     |SUB_EJECUTA_NP "coimz007";  ||Pase pedidos a gestion
|FPRC;

|PROGRAMA;
     |SI PARAMETRO = "";
          nSw = 1;
          #0#0 = "SI";
          |HAZ Tipo3;
     |SINO;
          |CLS;
          |DDEFECTO #0;
          |PINPA #0#0;
          |PINDA #0#0;
          |ENDATOS #1#0;
     |FINSI;
|FPRO;
