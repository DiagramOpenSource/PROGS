|FICHEROS;
     agifa112 #0;
     agifa200 #5;
     agif0041 #6;
     agifa208 #8;
     refprv@  #4;
|FIN;

|VARIABLES;
     &registro = "";

     base      = "";
     nombre    = "";
     path      = "";

     &modo     = 0;
     &mes      = 0;

     i         = 0;
     j         = 0;
     aMas      = "";
     aDirBase  = "";
     aAlfa     = "";
|FIN;

|PROCESO actualiza;
     |SI modo = 2; |Y #8#4 = "S";
          j = #8#1;
          #4j = #0j
     |SINO;
          |SI modo = 1; |Y #8#3 = "S";
               j = #8#1;
               #4j = #0j
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE alta_pro;
     #8#2;
     ;
     "agifa112" ,   0 , "S";
     "agifa112" , 999 , "S";
     ;
     NULL , actualiza;
|FIN;

|DEFBUCLE modi_pro;
     #8#3;
     ;
     "agifa112" ,   0 , "S";
     "agifa112" , 999 , "S";
     ;
     NULL , actualiza;
|FIN;

|PROCESO grabar4;
     #4#0 = #0#0;
     |LEE 101#4=;
     |SI FSalida ! 0; |DDEFECTO #4; #4#0 = #0#0; |GRABA 021#4b; |FINSI;
|FPRC;

|PROCESO mira_200;
     #6#0 = #5#0;
     |LEE 000#6=;
     |SI FSalida = 0;         ||La empresa Existe
          #0#0 = registro;
          |LEE 000#0=;
          |SI FSalida ! 0; |DDEFECTO #0; |FINSI;

          path = base + (("00000" + #5#0) % -105) + "/";
          |PATH_DAT #4 path;

          |ABRE #4;
          |SI modo = 1; |Y #5#7 = "S";
               |HAZ grabar4;
               |BUCLE alta_pro;
               |GRABA 021#4a;
          |FINSI;
          |SI modo = 2; |Y mes = 1; |Y #5#8 = "S";
               |HAZ grabar4;
               |BUCLE modi_pro;
               |GRABA 021#4a;
          |FINSI;
          |SI modo = 3; |Y #5#9 = "S";
               #4#0 = #0#0;
               |BORRA 021#4c;
          |FINSI;
          |CIERRA #4;
     |FINSI;
|FPRC;

|DEFBUCLE lee_200;
     #5#1;
     ;
     00001;
     99999;
     e;
     NULL , mira_200;
|FIN;

|PROGRAMA;
     |ATRI P;
     |MENSA "0000Actualizando Proveedor ... ";
     |ATRI p;

     |DBASE aDirBase;
     aMas = aDirBase + "def/" + "agifa112";
     |CARGA_DEF aMas, refprv@;
     |SI FSalida ! 0;
          aAlfa = "0000NO PUEDO CARGAR " + aMas;
          |MENAV aAlfa;
     |SINO;
          |DFICB base; |QBF base;
          |PATH_DAT #6 base;

          |ABRE #0;
          |ABRE #6;
          |BUCLE lee_200;
          |CIERRA #6;
          |CIERRA #0;
     |FINSI;

     |BLIN 4;
|FPRO;
