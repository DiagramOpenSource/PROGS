|FICHEROS;
     sctrz057 #157,MANTE; ||Pan Rese
     sctrz058 #158,MANTE; ||Pan Rese
     sctrz071 #171,MANTE; ||Pan Intercambiador
     sctrz072 #172,MANTE; ||Pan Cobro Reserva
     sctrm012 #12;
     sctrm029 #29;
     sctrm046 #46;
     sctrm031 #31;
     sctrm036 #36;
     sctrm046 #46;
     sctrm047 #47;
     sctrm052 #52;
     sctrm068 #68;
     sctrm069 #69;
     sctrm070 #70;
     sctrm076 #76;
     sctrm078 #78;
     agifa324 #324;
     agifa325 #325;
     sctrz092 #192;
|FIN;

|VARIABLES;
     &eaAgeApi = "";
     &enLinApi = 0;
     &enDtoAuto = 0;
     &enTotBillete = 0;
     &enIncBillete = 0;
     &efFechaViaje = @;
     &enLinea = 0;
     &eaSentido = "";
     &enPax = 0;
     &eaBloqueado = "";

     &enSwVuelta = 0;
     &eaAlfa = "";
     &enIntercambiador = 0;
     &eaCamCab = "";
     &enModoCab = 0;
     i = 0;
     aAlfa = "";
     nInc = 0;
     nHayLin = 0;
|FIN;

|PROCESO TipoVenta; |TIPO 0;
     |SI #157#6 = 4; |O #157#6 = 5;
          |C_M #157#150, 1, "S";
          #157#150 = 1; |PINTA #157#150;
          |HAZ NumApi;
     |SINO;
          |C_M #157#150, 1, "N";
          #157#150 = 0; |PINTA #157#150;
          #157#151 = "";
          #157#152 = "";
          #157#153 = 0;
     |FINSI;
|FPRC;

|PROCESO PideIntercambiador;
     |VENTANA 1237, 1867, -1, 17, eaAlfa;
     |HAZ PushVenModal;
     |DDEFECTO #171;
     |PINPA #0#171;
     |PINDA #0#171;
     |ENDATOS #1#171;
     |SI FSalida = 0;
          enIntercambiador = #171#0;
     |SINO;
          enIntercambiador = 0;
     |FINSI;
     |HAZ PopVenModal;
|FPRC;

|PROCESO Entrega172; |TIPO 0;
     |SI #172#114 > #172#32;
          |MENAV "0000Importe incorrecto, supera al total localizador...";
          |ERROR;
     |SINO;
          #172#115 = #172#32 - #172#114; |PINTA #172#115;
     |FINSI;
|FPRC;

|PROCESO FPago172; |TIPO 0;
     |SI #172#113 = "T";
          |C_M #172#118, 1, "S"; |PINTA #172#118;
          |C_M #172#119, 1, "S"; |PINTA #172#119;
          |C_M #172#120, 1, "S"; |PINTA #172#120;
     |SINO;
          |C_M #172#118, 1, "N"; |PINTA #172#118;
          |C_M #172#119, 1, "N"; |PINTA #172#119;
          |C_M #172#120, 1, "N"; |PINTA #172#120;
     |FINSI;
|FPRC;

|PROCESO BtnCobroRese;
     |SI #157#127 = 0;
          #157#127 = #47#3; || Oficina
     |FINSI;

     |ABRE #46;
     #46#0 = #157#127;
     |LEE 000#46=;
     |SI FSalida ! 0; |Y #47#2 = 1;
          aAlfa = "0000Oficina cobro:" + #46#0 + " inexistente...";
          |MENAV aAlfa;
          |ACABA;
     |FINSI;
     |CIERRA #46;

     |VENTANA 2301, 3398, -1, 17, "Cobro Reserva";
     |HAZ PushVenModal;
     |PARA i = 0; |SI i < eaCamCab; |HACIENDO i = i + 1;
          #172i = #157i;
     |SIGUE;
     |SI #172#116 = " "; #172#116 = "M"; |FINSI; || -> Pone M por defecto

     aAlfa = #172#160; |QBF aAlfa;
     |SI aAlfa = "";
          aAlfa = Usuario % 810;
          #172#160 = aAlfa;
     |FINSI;
     |PINPA #0#172;
     |PINDA #0#172;
     |HAZ OfiCobInd;
     |SI enModoCab = 4;
          |PAUSA;
     |SINO;
          |SI #172#117 = "01.01.0000";
               #172#117 = ~; |PINTA #172#117;
          |FINSI;

          |SI #47#2 [ 9;
               |C_M #172#160, 1, "S";
          |SINO;
               |C_M #172#160, 1, "N";
          |FINSI;
          |PINTA #172#160;

          |SI #47#2 = 1;
               |C_M #172#127, 1, "N";
          |SINO;
               |C_M #172#127, 1, "S";
          |FINSI;
          |PINTA #172#127;

          |ENDATOS #1#172;
          |SI FSalida = 0;
               |PARA i = 0; |SI i < eaCamCab; |HACIENDO i = i + 1;
                    #157i = #172i;
               |SIGUE;
          |FINSI;
     |FINSI;
     |HAZ PopVenModal;
|FPRC;

|PROCESO OfiCobInd; |TIPO 0;
     |ABRE #46;
     #46#0 = #172#127;
     |LEE 000#46=;
     |SI FSalida ! 0; |DDEFECTO #46; |FINSI;
     |CIERRA #46;
     |PINTA 1354, #46#1;
|FPRC;

|PROCESO LeeEl29;
     |ABRE #29;
     #29#0 = #31#0;
     #29#2 = #31#1;
     #29#4 = #31#2;
     #29#5 = #31#3;
     #29#7 = #31#4;
     |LEE 000#29=;
     |SI enSwVuelta = 0;
          |SI FSalida = 0; |Y #29#9 = "S"; |Y #157#20 ] #29#8; |Y #157#20 [ #29#10;
               aAlfa = "0";
          |SINO;
               aAlfa = "1";
          |FINSI;
     |SINO;
          |SI FSalida = 0; |Y #29#9 = "S"; |Y #157#26 ] #29#8; |Y #157#26 [ #29#10;
               aAlfa = "0";
          |SINO;
               aAlfa = "1";
          |FINSI;
     |FINSI;
     |CIERRA #29;
     FSalida = aAlfa;
|FPRC;

|PROCESO sctrm068;
     |SI #68#2 ! 0;
          |SI #68#2 ! #157#75; |ACABA; |FINSI;                    || Linea
          |SI #157#22 < #68#3; |O #157#22 > #68#4; |ACABA; |FINSI; || Servicio
     |FINSI;
     |SI #157#1 < #68#5; |O #157#1 > #68#7;; |ACABA; |FINSI;       || F.Rese
     |SI #157#2 < #68#6; |O #157#2 > #68#8;; |ACABA; |FINSI;       || H.Rese

     enIncBillete = enIncBillete + ((enTotBillete * #68#10 / 100)!0);
|FPRC;

|DEFBUCLE sctrm068;
     #68#2;
     11;
     #157#20, #157#96, 0000000, #157#101;
     #157#20, #157#96, 9999999, #157#101;
     ;
     NULL, sctrm068;
|FIN;

|PROCESO IncDisBillete;
     enIncBillete = 0;
     |BUCLE sctrm068;
|FPRC;

|PROCESO CtrlViajerosCheq;
     |ABRE #69;
     #69#0 = enLinea;
     #69#1 = eaSentido;
     #69#2 = efFechaViaje;
     |LEE 000#69=;
     |SI FSalida ! 0; |DDEFECTO #69; |FINSI;
     |CIERRA #69;
     enPax = #69#3 - #69#4;
     eaBloqueado = #69#5;
|FPRC;

|PROCESO CtrlViajerosAct;
     |DDEFECTO #69;
     |ABRE #69;
     #69#0 = enLinea;
     #69#1 = eaSentido;
     #69#2 = efFechaViaje;
     |LEE 101#69=;
     |SI FSalida ! 0; |GRABA  020#69b; |FINSI;
     #69#4 = #69#4 + enPax;
     |GRABA 020#69a;
     |CIERRA #69;
|FPRC;

|PROCESO sctrm078;
     |SI #78#1 = #157#75;                       || Servicio
          |SI #78#2 = #158#7; |O #78#2 = 0;     || Grupo

               aAlfa = #78#5; |QBF aAlfa;
               |SI #78#5 = #157#96; |O aAlfa = ""; || Temporada

                    aAlfa = #78#6; |QBF aAlfa;
                    |SI #78#6 = #157#101; |O aAlfa = ""; || Sentido

                         |SI #78#7 = #157#22; |O #78#7 = 0; || Servicio
                              nHayLin = 1;
                         |FINSI;
                    |FINSI;
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE sctrm078;
     #78#1;
     ;
     #70#0, 000, 00, "          ", " ", 000000;
     #70#0, 999, 99, "zzzzzzzzzz", "z", 999999;
     ;
     NULL, sctrm078;
|FIN;

|PROCESO sctrm070;
     |SI #70#1 = "R"; |O #70#1 = "T";
          |SI #157#6 = #70#2; |O #70#2 = 9;
               |SI #157#1 ] #70#3; |Y #157#1 [ #70#4;
                    |SI #157#20 ] #70#5; |Y #157#20 [ #70#6;
                         nHayLin = 0;
                         |BUCLE sctrm078
                         |SI nHayLin = 1;
                              enDtoAuto = #70#7;
                         |FINSI;
                    |FINSI;
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE sctrm070;
     #70#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, sctrm070;
|FIN;

|PROCESO DtoAuto;
     enDtoAuto = 0;
     |BUCLE sctrm070;
|FPRC;

|PROCESO HistoDivisa;
     #157#136 = #325#2;
|FPRC;

|DEFBUCLE HistoDivisa;
     #325#2;
     ;
     #157#94, "01.01.0000", 0;
     #157#94, "31.12.9999", 99999999;
     ;
     NULL, HistoDivisa;
|FIN;

|PROCESO ValorDivisa;
     |ABRE #324;
     #324#0 = #157#94;
     |LEE 000#324=;
     |SI FSalida ! 0; |DDEFECTO #324; |FINSI;
     |CIERRA #324;

     #157#136 = #324#2;
     |BUCLE HistoDivisa;
|FPRC;

|PROCESO NumApi; |TIPO 0;
     |C_M #157#150, 0, aAlfa;
     |SI aAlfa ! "S"; |ACABA; |FINSI;

     |ABRE #76;
     #76#0 = #157#150;
     |LEE 000#76=;
     |SI FSalida = 0;
          |ABRE #36;
          #36#1 = #157#151;
          |LEE 000#36=;
          |SI FSalida ! 0; |DDEFECTO #36; |FINSI;
          |CIERRA #36;

          #157#151 = #76#1;
          #157#152 = #36#2;

          |ABRE #12;
          #12#0 = #76#3;
          |LEE 000#12=;
          |SI FSalida ! 0; |DDEFECTO #12; |FINSI;
          |CIERRA #12;
          |SI #157#17 = 1;
               #157#75 = #76#3;
               #157#76 = #12#1;
          |SINO;
               #157#75 = #76#3;
               #157#76 = #12#1;
               |SI #157#26 ! "01.01.0000";
                    #157#80 = #76#3;
                    #157#81 = #12#1;
               |FINSI;
          |FINSI;
     |SINO;
          |MENAV "0000No existe c¢digo api...";
          |ERROR;
     |FINSI;
     |CIERRA #76;
|FPRC;

|PROCESO LeeApi;
     |ABRE #76;
     #76#0 = #157#150;
     |LEE 000#76=;
     |SI FSalida ! 0; |DDEFECTO #76; |FINSI;
     |CIERRA #76;
     eaAgeApi = #76#1;
     enLinApi = #76#3;
|FPRC;

|PROCESO BtnDatoGen2;
     |PARA i = 0; |SI i < 160; |HACIENDO i = i + 1;
          #192i = #52i;
     |SIGUE;
     |VENTANA 0901, 2898, -1, 17, "Datos Generales";
     |HAZ PushVenModal;
     |PINPA #0#192;
     |PINDA #0#192;
     |PAUSA;
     |HAZ PopVenModal;
|FPRC;
