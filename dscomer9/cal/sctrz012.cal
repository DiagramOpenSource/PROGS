|FICHEROS;
     sctrz012 #0;
     sctrm003 #3;
     sctrm005 #5;
     sctrm012 #12;
     sctrm013 #13;
     sctrm057 #57;

     sctrm021 #21;
     sctrm022 #22,MANTE;
     sctrm001 #100;
     sctrz013 #113,MANTE;     ||Pan Cab
     referen@;
|FIN;

|VARIABLES;
     aInf = "";
     &enLinServi = 0;
     &eaNomLogo = "";
     &enPantaLogo = 0;
     &enCtrFin = 0;
     &Tempo = "";
     aAlfa = "";
     nModoCab = 0;
     nModoKms = 0;
     nPanta = 0;
     nPantaCab = 0;
     nRango = 0;
     nGridCab = 0;
     nGridKms = 0;
     nBtnAlta = 0;
     nBtnModi = 0;
     nBtnBaja = 0;
     nBtnCons = 0;
     nBtnImpr = 0;
     nBtnTodos = 0;
     i = 0;
     nReg0 = 0;
     aReg2 = "";
     aReg4 = "";
     nBloquea = 0;
     nId = 0;
     nSwFin = 0;
     nLin = 0;
     nKms = 0;
|FIN;

|PROCESO SumaKms;
     nKms = nKms + #referen@#6;
|FPRC;

|DEFBUCLE SumaKms;
     #referen@#1004;
     ;
     #22#0, #22#1, #22#2, 0;
     #22#0, #22#1, #22#2, nLin;
     ;
     NULL, SumaKms;
|FIN;

|PROCESO Kms0; |TIPO 0;
     #22#7 = #22#6 * 100 / #113#6; |PINTA #22#7;
|FPRC;

|PROCESO Kms7; |TIPO 7;
     |SI #22#6 = 0;
          |DDEF aAlfa;
          aAlfa = aAlfa + "sctrm022";
          |CARGA_DEF aAlfa, referen@;
          |SI FSalida = 0;
               nKms = 0;
               nLin = #22#3 - 1;
               |BUCLE SumaKms;
               #22#6 = #113#6 - nKms; |PINTA #22#6;
               |DESCARGA_DEF #referen@;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Pais66; |TIPO 66;
     |ABRE #5;
     #5#0 = #22#4;
     |LEE 000#5];
     |CONSULTA_CLAVE #1#5;
     |SI FSalida = 0;
          #22#4 = #5#0; |PINTA #22#4;
     |SINO;
          |ERROR;
     |FINSI;
     |CIERRA #5;
|FPRC;

|PROCESO Pais0; |TIPO 0;
     |ABRE #5;
     #5#0 = #22#4;
     |LEE 000#5=;
     |SI FSalida = 0;
          #22#5 = #5#1; |PINTA #22#5;
          |ABRE #13;
          #13#0 = #22#0;
          #13#1 = #22#4;
          |LEE 000#13=;
          |SI FSalida ! 0;
               |MENAV "0000El Pa¡s no pertenece a la l¡nea de servicio...";
               |ERROR;
          |FINSI;
          |CIERRA #13;
     |SINO;
          |MENAV "0000No existe c¢digo pa¡s...";
          |ERROR;
     |FINSI;
     |CIERRA #5;
|FPRC;

|PROCESO DefeLin22; |TIPO 7;
     nModoKms = (FEntrada / 100) ! 0;
     |SI nModoKms = 1;
          #22#0 = #113#0;
          #22#1 = #113#2;
          #22#2 = #113#4;
          #22#3 = 9999;
          |LEE 000#22];
          |SI FSalida = 0;
               |LEE 000#22a;
          |SINO;
               |LEE 000#22u;
          |FINSI;
          |SI FSalida = 0; |Y #22#0 = #113#0; |Y #22#1 = #113#2;
                    |Y #22#2 = #113#4;
               nLin = #22#3 + 1;
          |SINO;
               nLin = 1;
          |FINSI;
          |DDEFECTO #22;
          #22#0 = #113#0;
          #22#1 = #113#2;
          #22#2 = #113#4;
          #22#3 = nLin;
          |C_M #22#4, 1, "S";
     |SINO;
          |C_M #22#4, 1, "N";
     |FINSI;
|FPRC;

|PROCESO LinServi; |TIPO 0;
     |ABRE #12;
     #12#0 = #113#0;
     |LEE 000#12=;
     |SI FSalida = 0;
          #113#1 = #12#1; |PINTA #113#1;
     |SINO;
          |MENAV "0000No existe c¢digo l¡nea servicio...";
          |ERROR;
     |FINSI;
     |CIERRA #12;
|FPRC;

|PROCESO PobOrg; |TIPO 0;
     |ABRE #3;
     #3#0 = #113#2;
     |LEE 000#3=;
     |SI FSalida = 0;
          #113#3 = #3#1; |PINTA #113#3;
     |SINO;
          |MENAV "0000No existe c¢digo poblaci¢n...";
          |ERROR;
     |FINSI;
     |CIERRA #3;
|FPRC;

|PROCESO CargaPaises;
     #22#0 = #113#0;
     #22#1 = #113#2;
     #22#2 = #113#4;
     #22#3 = 9999;
     |LEE 000#22];
     |SI FSalida = 0;
          |LEE 000#22a;
     |SINO;
          |LEE 000#22u;
     |FINSI;
     |SI FSalida = 0; |Y #22#0 = #113#0; |Y #22#1 = #113#2;
               |Y #22#2 = #113#4;
          nLin = #22#3 + 1;
     |SINO;
          nLin = 1;
     |FINSI;
     |DDEFECTO #22;
     #22#0 = #113#0;
     #22#1 = #113#2;
     #22#2 = #113#4;
     #22#3 = nLin;
     #22#4 = #13#1;
     #22#5 = #13#2;
     |GRABA 020#22n;
|FPRC;

|DEFBUCLE CargaPaises;
     #13#1;
     ;
     #113#0, "    ";
     #113#0, "zzzz";
     ;
     NULL, CargaPaises;
|FIN;

|PROCESO PobDes; |TIPO 0;
     |ABRE #3;
     #3#0 = #113#4;
     |LEE 000#3=;
     |SI FSalida = 0;
          #113#5 = #3#1; |PINTA #113#5;
          |SI nModoCab = 1; |Y nBloquea = 0;
               |DDEFECTO #21;
               #21#0 = #113#0;
               #21#2 = #113#2;
               #21#4 = #113#4;
               |GRABA 000#21b;
               |SI FSalida ! 0;
                    |MENAV "0000Registro existente...";
                    |ERROR;
               |SINO;
                    |C_M #113#0, 1, "N";
                    |C_M #113#2, 1, "N";
                    |C_M #113#4, 1, "N";
                    nBloquea = 1;

                    aAlfa  = "|X000"; aAlfa = #113aAlfa; nId = aAlfa;
                    |ESTADO_CONTROL nId, "ENABLE";

                    |BUCLE CargaPaises;
                    #22#0 = #113#0;
                    #22#1 = #113#2;
                    #22#2 = #113#4;
                    #22#3 = 0;
                    |LEE 000#22];
                    |REFRESCACONTROL nGridKms;
               |FINSI;
          |FINSI;
     |SINO;
          |MENAV "0000No existe c¢digo poblaci¢n...";
          |ERROR;
     |FINSI;
     |CIERRA #3;
|FPRC;

|PROCESO Valida7; |TIPO 7;
     |SI nModoCab = 1;
          aAlfa  = "|X000"; aAlfa = #113aAlfa; nId = aAlfa;
          |ESTADO_CONTROL nId, "DISABLE";
     |FINSI;
|FPRC;

|PROCESO PintaCampos;
|FPRC;

|PROCESO Borra22;
     |BORRA 020#22a;
|FPRC;

|DEFBUCLE Borra22;
     #22#1;
     ;
     #21#0, #21#2, #21#4, 0000
     #21#0, #21#2, #21#4, 9999
     be;
     NULL, Borra22;
|FIN;

|PROCESO SumaTotalKms;
     nKms = nKms + #22#6;
|FPRC;

|DEFBUCLE SumaTotalKms;
     #22#1;
     ;
     #21#0, #21#2, #21#4, 0000
     #21#0, #21#2, #21#4, 9999
     be;
     NULL, SumaTotalKms;
|FIN;

|PROCESO Min22;
     #22#0 = #113#0;
     #22#1 = #113#2;
     #22#2 = #113#4;
     #22#3 = 0000;
|FPRC;

|PROCESO Max22;
     #22#0 = #113#0;
     #22#1 = #113#2;
     #22#2 = #113#4;
     #22#3 = 999;
|FPRC;

|PROCESO Edita;
     nReg0 = #21#0;
     aReg2 = #21#2;
     aReg4 = #21#4;

     |SI nModoCab = 1; |VENTANA 0901, 2898, -1, 17, "Nuevo Trayectos/Kil¢metros"; |FINSI;
     |SI nModoCab = 2; |VENTANA 0901, 2898, -1, 17, "Modifica Trayectos/Kil¢metros"; |FINSI;
     |SI nModoCab = 4; |VENTANA 0901, 2898, -1, 17, "Consulta Trayectos/Kil¢metros"; |FINSI;
     |HAZ PushVenModal;

     |SI nModoCab = 1;
          |DDEFECTO #113;
          |C_M #113#0, 1, "S";
          |C_M #113#2, 1, "S";
          |C_M #113#4, 1, "S";
     |SINO;
          |PARA i = 0; |SI i [ 6; |HACIENDO i = i + 1;
               #113i = #21i;
          |SIGUE;
          |C_M #113#0, 1, "N";
          |C_M #113#2, 1, "N";
          |C_M #113#4, 1, "N";
     |FINSI;

     |PINPA #0#113; nPantaCab = FSalida;
     |PINDA #0#113;
     |HAZ PintaCampos;

     |SI nModoCab = 4;
          nRango = 4 + 8 + 16 + 32;
     |SINO;
          nRango = 1 + 4 + 8 + 16 + 32;
     |FINSI;
     |LINEAL_SIMPLE #1#22, nRango, 1102, 2398, Min22, Max22, NULL;
     nGridKms = FSalida;

     nBloquea = 0;
 |ET Otra;
     |SI nModoCab = 1; |ENDATOS #1#113; |FINSI;
     |SI nModoCab = 2; |ENDATOS #2#113; |FINSI;
     |SI nModoCab = 4; |PAUSA; |FINSI;
     |SI FSalida = 0; |Y nModoCab ! 4;
          #21#0 = #113#0;
          #21#2 = #113#2;
          #21#4 = #113#4;
          |LEE 121#21=;

          nKms = 0;
          |BUCLE SumaTotalKms;
          |SI nKms ! #113#6;
               aAlfa = "0000Los kilometros de las l¡neas:" + nKms + " no coincide con el total...";
               |MENAV aAlfa;
               |VETE Otra;
          |FINSI;

          |PARA i = 0; |SI i [ 6; |HACIENDO i = i + 1;
               #21i = #113i;
          |SIGUE;
          |GRABA 020#21a;
          nReg0 = #21#0;
          aReg2 = #21#2;
          aReg4 = #21#4;
     |SINO;
          |SI nBloquea = 1;
               #21#0 = #113#0;
               #21#2 = #113#2;
               #21#4 = #113#4;
               |BORRA 020#21c;
               |BUCLE Borra22;
          |FINSI;
     |FINSI;
     |LIBERA #21;

     |HAZ PopVenModal;

     #21#0 = nReg0;
     #21#2 = aReg2;
     #21#4 = aReg4;
     |LEE 000#21=;
     |REFRESCACONTROL nGridCab;
|FPRC;

|PROCESO BtnAlta;
     nModoCab = 1; |HAZ Edita;
     |HAZ TieneLineas;
|FPRC;

|PROCESO BtnModi;
     |LEE 100#21=;
     |SI FSalida ! 0; |ACABA; |FINSI;
     nModoCab = 2; |HAZ Edita;
|FPRC;

|PROCESO BtnBaja;
     |LEE 100#21=;
     |SI FSalida ! 0; |ACABA; |FINSI;
     aAlfa = "0000N¨Borrar trayectos/kilometros: " + #21#0 + "/" + #21#2 + "/" + #21#4 + "?";
     |CONFI aAlfa;
     |SI FSalida = 0;
          |BORRA 020#21a;
          |BUCLE Borra22;
          |HAZ TieneLineas;
          |REFRESCACONTROL nGridCab;
     |FINSI;
     |LIBERA #21;
|FPRC;

|PROCESO BtnCons;
     |LEE 000#21=;
     |SI FSalida ! 0; |ACABA; |FINSI;
     nModoCab = 4; |HAZ Edita;
|FPRC;

|PROCESO sctrm022;
     |PRINT;
|FPRC;

|DEFBUCLE sctrm022;
     #22#1;
     ;
     #21#0, #21#2, #21#4, 0000;
     #21#0, #21#2, #21#4, 9999;
     ;
     NULL, sctrm022;
|FIN;

|PROCESO Impresion;
     Impresora = "CrystalReport";
     |IMPRE -1;
     |SI FSalida = 0;
          aInf = #57#1; |QBF aInf;
          |INFOR aInf;
          |SI FSalida = 0;
               |BUCLE sctrm022;
               |PIEINF;
               |FININF;
          |SINO;
               aInf = "0000Error en in forme:" + aInf;
               |MENAV aInf;
          |FINSI;
          |FINIMP;
     |FINSI;
|FPRC;

|PROCESO BtnImpr;
     |LEE 000#21=;
     |SI FSalida ! 0; |ACABA; |FINSI;
     |HAZ Impresion;
|FPRC;

|PROCESO Evento21;
     |SI FSalida = 1; |O FSalida = 2;
          |SELECT #1#21;
          |LEE 000#21=;
          |HAZ TieneLineas;
     |FINSI;
     |SI FSalida = 4;
          |HAZ BtnCons;
     |FINSI;
|FPRC;

|PROCESO TieneLineas;
     |DDEF aAlfa;
     aAlfa = aAlfa + "sctrm021";
     |CARGA_DEF aAlfa, referen@;
     |SI FSalida = 0;
          |ABRE #referen@;
          |SI enLinServi = 0;
               |LEE 000#referen@.p;
          |SINO;
               |DDEFECTO #referen@;
               #referen@#0 = enLinServi;
               |LEE 000#referen@.];
               |SI FSalida ! 0; |O #referen@#0 ! enLinServi;
                    FSalida = "1";
               |FINSI;
          |FINSI;
          |SI FSalida = 0;
               |ESTADO_CONTROL nBtnModi, "ENABLE";
               |ESTADO_CONTROL nBtnBaja, "ENABLE";
               |ESTADO_CONTROL nBtnCons, "ENABLE";
               |ESTADO_CONTROL nBtnImpr, "ENABLE";
          |SINO;
               |ESTADO_CONTROL nBtnModi, "DISABLE";
               |ESTADO_CONTROL nBtnBaja, "DISABLE";
               |ESTADO_CONTROL nBtnCons, "DISABLE";
               |ESTADO_CONTROL nBtnImpr, "DISABLE";
          |FINSI;
          |CIERRA #referen@;
          |DESCARGA_DEF #referen@;
     |FINSI;
|FPRC;

|PROCESO LogoPanta;
     eaNomLogo = #100#1;
     |HAZ TraeLogoPan;
     enPantaLogo = nPanta;
     |HAZ PintaLogoPan;
|FPRC;

|PROCESO BtnTodos;
     nSwFin = 1;
     enLinServi = 0;
     |PTEC 807;
|FPRC;

|PROCESO Min21;
     |SI enLinServi = 0;
          #21#0 = 0;
     |SINO;
          #21#0 = enLinServi;
     |FINSI;
     #21#2 = "    ";
     #21#4 = "    ";
|FPRC;

|PROCESO Max21;
     |SI enLinServi = 0;
          #21#0 = 999;
     |SINO;
          #21#0 = enLinServi;
     |FINSI;
     #21#2 = "zzzz";
     #21#4 = "zzzz";
|FPRC;

|PROGRAMA;
     |ABRE #100; |LEE 000#100p; |CIERRA #100;
     |ABRE #57; |LEE 000#57p; |CIERRA #57;

     |PINPA #0#0; nPanta = FSalida;
     |PINDA #0#0;
     |HAZ LogoPanta;

     |CONTROL_SIMPLE nPanta, "BOTON,{{193}}Alta", 1002, 1012, BtnAlta;
     nBtnAlta = FSalida;
     |CONTROL_SIMPLE nPanta, "BOTON,{{197}}Modificar", 1014, 1024, BtnModi;
     nBtnModi = FSalida;
     |CONTROL_SIMPLE nPanta, "BOTON,{{199}}Baja", 1026, 1036, BtnBaja;
     nBtnBaja = FSalida;
     |CONTROL_SIMPLE nPanta, "BOTON,{{155}}Consulta", 1038, 1048, BtnCons;
     nBtnCons = FSalida;
     |CONTROL_SIMPLE nPanta, "BOTON,{{205}}Impresi¢n", 1050, 1060, BtnImpr;
     nBtnImpr = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,{{230}}Todos", 1088, 1098, BtnTodos;
     nBtnTodos = FSalida;

     |ABRET #21;
     |ABRET #22;
     |PARA nSwFin = 1; |SI nSwFin = 1; |HACIENDO;
          |HAZ TieneLineas;
          nSwFin = 0;
          nRango = 4 + 8 + 16 + 32;
          |SI enLinServi = 0;
               |ESTADO_CONTROL nBtnTodos, "DISABLE";
          |FINSI;
          |LINEAL_SIMPLE #1#21, nRango, 1102, 2999, Min21, Max21, Evento21;
          nGridCab = FSalida;
          enCtrFin = nGridCab;
          |HAZ EsperaFin;
          |PULSATECLA;
          |DESTRUYECONTROL nGridCab;
     |SIGUE;
     |CIERRAT #22;
     |CIERRAT #21;
|FPRO;
