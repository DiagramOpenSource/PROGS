|FICHEROS;
     extz0016 #0;
     extm0001 #1;
     extm0019 #19;
     agifa071 #71;
     referen@;
|FIN;

|VARIABLES;
     &Tempo = "";
     &enModo = 0;
     &eaArticulo = "";
     &eaSerie = "";
     &enCodigo = 0;
     &enLinea = 0;
     &enUnidades = 0;
     aAlfa = "";
     nForma = 0;
     nLin = 0;
     nUni = 0;
     nDif = 0;
     nError = 0;
     aNum = "";
     aTmp0 = "";
     nSwAdj = 0;
     nSwCheq = 0;
|FIN;

|PROCESO T2; |TIPO 2;
     nForma = 0 +. 1;
     nUni = nUni +. 1;
     |ABRE #19;
     #19#0 = eaArticulo;
     #19#1 = #0#1;
     |LEE 121#19=;
     |SI FSalida = 0;
          |SI nForma = 1;
               #19#8 = eaSerie;
               #19#9 = enCodigo;
               #19#10 = enLinea;
          |SINO;
               #19#8 = "";
               #19#9 = 0;
               #19#10 = 0;
          |FINSI;
          |GRABA 020#19a;
     |FINSI;
     |CIERRA #19;
|FPRC;

|PROCESO Min19;
     #19#0 = eaArticulo;
     #19#1 = "                    ";
|FPRC;

|PROCESO Max19;
     #19#0 = eaArticulo;
     #19#1 = "zzzzzzzzzzzzzzzzzzzz";
|FPRC;

|PROCESO LlenaTmp;
     |SALVA_DATOS #19;
     |REPON_DATOS #referen@;
     |GRABA 020#referen@.n;
|FPRC;

|DEFBUCLE LlenaTmp;
     #19#1;
     9;
     eaArticulo, "                    ", 0;
     eaArticulo, "zzzzzzzzzzzzzzzzzzzz", 0;
     ;
     NULL, LlenaTmp;
|FIN;

|PROCESO T66; |TIPO 66;
     |DDEF aAlfa;
     aAlfa = aAlfa + "extm0019";
     |CARGA_DEF aAlfa, referen@;
     |SI FSalida = 0;
          |HAZ CreaTempo; |NOME_DAT #referen@#Tempo#;
          |DELINDEX #referen@; |ABRE #referen@;
          |BUCLE LlenaTmp;
          |LEE 000#referen@.p;
          |SI FSalida = 0;
               |CONSULTA_CLAVE #1#referen@;
               |SI FSalida = 0;
                    #0#1 = #referen@#1; |PINTA #0#1;
               |SINO;
                    |ERROR;
               |FINSI;
          |SINO;
               |MENAV "0000No hay números de serie pendientes de este artículo...";
               |ERROR;
          |FINSI;
          |CIERRA #19;
          |CIERRA #referen@; |HAZ BoraTempo; |DELINDEX #referen@;
          |DESCARGA_DEF #referen@;
     |FINSI;
|FPRC;

|PROCESO T0; |TIPO 0;
     |SALVA_FICHA #0;
     |SELECT #2#0;
     nError = 0;
     aNum = #0#1;
     nLin = #0#0;
     |LEE 000#0];
     |PARA; |SI FSalida = 0; |Y aNum = #0#1; |HACIENDO;
          |SI #0#0 ! nLin;
               aAlfa = "0000Número de serie introducido en la linea:" + #0#0;
               |MENAV aAlfa;
               nError = 1; |SAL;
          |FINSI;
          |LEE 000#0s;
     |SIGUE;
     |SELECT #1#0;
     |REPON_FICHA #0;
     |SI nError = 1; |ERROR; |ACABA; |FINSI;

     |ABRE #19;
     #19#0 = eaArticulo;
     #19#1 = #0#1;
     |LEE 000#19=;
     |SI FSalida ! 0;
          |MENAV "0000No existe número de serie...";
          |ERROR;
     |SINO;
          |SI #19#9 ! 0;
               |SI #19#8 ! eaSerie; |O #19#9 ! enCodigo; |O #19#10 ! enLinea;
                    aAlfa = "0000Nº serie vendido en albarán:" + #19#8 + "/" + (("000000" + #19#9)%-106) + "-" + (("000" + #19#10)%-103);
                    |MENAV aAlfa;
                    |ERROR;
               |FINSI;
          |FINSI;
     |FINSI;
     |CIERRA #19;
|FPRC;

|PROCESO Desasigna;
     #19#8 = "";
     #19#9 = 0;
     #19#10 = 0;
     |GRABA 020#19a;
|FPRC;

|DEFBUCLE Desasigna;
     #19#3;
     ;
     eaSerie, enCodigo, enLinea;
     eaSerie, enCodigo, enLinea;
     be;
     NULL, Desasigna;
|FIN;

|PROCESO CargaTmp;
     nUni = nUni + 1;
     #0#0 = nUni;
     #0#1 = #19#1;
     |GRABA 020#0n;
|FPRC;

|DEFBUCLE CargaTmp;
     #19#3;
     ;
     eaSerie, enCodigo, enLinea;
     eaSerie, enCodigo, enLinea;
     be;
     NULL, CargaTmp;
|FIN;

|PROCESO T7; |TIPO 7;
     #0#2 = eaArticulo; |PINTA #0#2;
|FPRC;

|PROCESO T12; |TIPO 12;
|FPRC;

|PROCESO Min;
     #0#0 = 1;
|FPRC;

|PROCESO Max;
     #0#0 = 999;
|FPRC;

|PROCESO LinAlbaran;
     |ABRE #1;
     #1#0 = eaArticulo;
     |LEE 000#1=;
     |SI FSalida = 0;
          |SI #1#6 = "S"; |O enModo = 3;
               |SI enModo = 3;
                    |BUCLE Desasigna;
               |SINO;
                    |HAZ CreaTempo; |NOME_DAT #0 Tempo; |DELINDEX #0; aTmp0 = Tempo;
                    |ABRE #0;
                    nUni = 0;
                    |BUCLE CargaTmp;
                    |ABRE #19;
                    |PARA; |SI; |HACIENDO;
                         |ENTLINEAL #1#0, 1, 2, 22, 1, Min, Max;
                         |SI nUni = enUnidades;
                              |SAL;
                         |SINO;
                              nDif = enUnidades - nUni;
                              |SI nSwAdj = 0;
                                   aAlfa = "0000Unidades albarán:" + enUnidades + " introducidas:" + nUni + ". Diferencia:" + nDif;
                                   |MENAV aAlfa;
                              |SINO;
                                   aAlfa = "0000NUnidades albarán:" + enUnidades + " introducidas:" + nUni + ". Diferencia:" + nDif + ". ¿Continuar?";
                                   |CONFI aAlfa;
                                   |SI FSalida = 0; |SAL; |FINSI;
                              |FINSI;
                         |FINSI;
                    |SIGUE;
                    |CIERRA #19;
                    |CIERRA #0;
                    Tempo = aTmp0; |HAZ BoraTempo; |DELINDEX #0;
               |FINSI;
          |FINSI;
     |FINSI;
     |CIERRA #1;
|FPRC;

|PROCESO CuentaUni;
     nUni = nUni + 1;
|FPRC;

|DEFBUCLE CuentaUni;
     #19#3;
     ;
     eaSerie, enCodigo, #71#1;
     eaSerie, enCodigo, #71#1;
     be;
     NULL, CuentaUni;
|FIN;

|PROCESO Adjudicacion;
     |SI nSwCheq = 0;
          enLinea = #71#1;
          eaArticulo = #71#2;
          enUnidades = #71#5;
          |HAZ LinAlbaran;
     |SINO;
          |ABRE #1;
          #1#0 = #71#2;
          |LEE 000#1=;
          |SI FSalida = 0; |Y #1#6 = "S";
               nUni = 0;
               |BUCLE CuentaUni;
               |SI nUni ! #71#5;
                    nDif = #71#5 - nUni;
                    aAlfa = "0000Artículo:" + #71#2; |QBF aAlfa;
                    aAlfa = aAlfa + " unidades:" + enUnidades + " introducidas:" + nUni + ". Diferencia:" + nDif;
                    |MENAV  aAlfa;
                    enCodigo = 0;
               |FINSI;
          |FINSI;
          |CIERRA #1;
     |FINSI;
|FPRC;

|DEFBUCLE Adjudicacion;
     #71#1;
     ;
     eaSerie, enCodigo, 001;
     eaSerie, enCodigo, 999;
     ;
     NULL, Adjudicacion;
|FIN;

|PROGRAMA;
     |SI PARAMETRO = "ADJ";
          nSwAdj = 1;
          nSwCheq = 0;
          |BUCLE Adjudicacion;
          nSwCheq = 1;
          |BUCLE Adjudicacion;
     |SINO;
          |HAZ LinAlbaran;
     |FINSI;
|FPRO;
