|FICHEROS;
   agif0708 #0;

   agif0702 #10;
   agif0704 #11;
   agif0717 #12;

   agifw708 #100;
   agifw709 #101;
   agifw700 #102;
   agifw799 #199;

   Referen@ #1000;
|FIN;

|VARIABLES;
   &eaResultado = "";

   aAlfa    = "";
   aAlfa1   = "";
   aAlfa2   = "";
   aCodEnl  = "";
   aTipoEnl = "";
   aTipoCmp = "";

   aLong    = "";
   nLong    = 0;
   nCont    = 0;

   nDato    = 0;

   aFichero = "";
   aCampo   = "";
   aDirRec  = "";
   nDigRec  = 0;
   aValor   = "";

   nNumAsto = 0;
   nNumLin  = 0;
   nCalc    = 0;
   nPant    = 0;
   nPanta   = 0;
   nPantFlt = 0;
   nVent    = 0;
   nVentFlt = 0;
   nRango   = 0;
   nPos1    = 0;
   nPos2    = 0;

   fFecha   = @;

   nBotonInc = 0;
   nBotonEli = 0;
   nBotonRec = 0;
   nBotonVF  = 0;
   nLabel    = 0;

   aEsc1   = "";
   aEsc2   = "";
   aRetu   = "";
   aTecla  = "";

   nGridA  = 0;
   nGridB  = 0;
   || NO METER VARIABLES ENTRE ESTE BLOQUE
{-1}aVent      = "";
    aVent1     = 0;
    aVent2     = 0;
    aVent3     = 0;
    aVent4     = 0;
    aVent5     = "";
   || NO METER VARIABLES ENTRE ESTE BLOQUE
|FIN;

|PROCESO RecogeConceptos;
   |SI aTipoCmp ! "";
      |SI #agif0702#5 ! aTipoCmp; |ACABA; |FINSI;
   |SINO;
      |DDEFECTO #agifw708;
      |SELECT #2#agifw708;
      #agifw708#2 = #agif0702#1;
      #agifw708#4 = #agif0702#2;
      |LEE 000#agifw708.];
      |SI FSalida = 0; |Y #agifw708#2 = #agif0702#1; |Y #agifw708#4 = #agif0702#2;
         |SELECT #1#agifw708; |ACABA;
      |SINO;
         |SELECT #1#agifw708;
      |FINSI;
   |FINSI;

   |DDEF aAlfa; |QBF aAlfa;
   aAlfa = aAlfa + #agif0702#1; |QBF aAlfa;
   aAlfa = aAlfa + ".mas";
   |CARGA_DEF aAlfa, Referen@;
   |SI FSalida = 0;
      aAlfa = "|TITULO"; aAlfa = #Referen@#aAlfa#; |QBF aAlfa;
      |DESCARGA_DEF #Referen@;
   |SINO;
      |ACABA;
   |FINSI;

   |DDEFECTO #agifw708;
   #agifw708#0 = #agif0702#0;
   #agifw708#1 = aAlfa;
   #agifw708#2 = #agif0702#1;
   #agifw708#3 = #agif0702#3;
   #agifw708#4 = #agif0702#2;
   |GRABA 020#agifw708.n;
|FPRC;

|DEFBUCLE RecogeConceptos;
#agif0702#3;
;
"        ", 000, aTipoEnl;
"zzzzzzzz", 999, aTipoEnl;
;
NULL, RecogeConceptos;
|FIN;

|PROCESO RecogeMemorias;
   |LEE 000#agifw708.u;
   nCalc = #agifw708#0 + 1;

   |DDEFECTO #agifw708;
   #agifw708#0 = nCalc;
   aAlfa = "MEMO " + #agif0717#1;
   #agifw708#1 = aAlfa;
   #agifw708#5 = "49,106,197;255,255,255";
   |GRABA 020#agifw708.n;
|FPRC;

|DEFBUCLE RecogeMemorias;
#agif0717#1;
;
#agif0704#0, "               ";
#agif0704#0, "zzzzzzzzzzzzzzz";
;
NULL, RecogeMemorias;
|FIN;

|PROCESO EvtoGridA;
   |SI FSalida = 1; |O FSalida = 2;
      |LEE 000#agifw708.c;

      |ESTADO_CONTROL nBotonInc, "ENABLE";
   |FINSI;
   |SI FSalida = 4; |HAZ Incluye; |FINSI;
|FPRC;

|PROCESO EvtoGridB;
   |SI FSalida = 1; |O FSalida = 2;
      |LEE 000#agifw709.c;

      |ESTADO_CONTROL nBotonEli, "ENABLE";
      |ESTADO_CONTROL nBotonRec, "ENABLE";
   |FINSI;
   |SI FSalida = 4;
      |SI #agifw709#1 ! "        ";
         |HAZ Elimina;
      |SINO;
         aValor = #agifw709#5; |HAZ EditaValor;
         #agifw709#5 = aValor; |GRABA 020#agifw709.a;
         |REFRESCACONTROL nGridB;
      |FINSI;
   |FINSI;
|FPRC;

|PROCESO Incluye;
   |LEE 000#agifw709.u;
   |SI FSalida ! 0; #agifw709#0 = 0; |FINSI;
   nCalc = #agifw709#0 + 1;
   |DDEFECTO #agifw709;

   aAlfa = #agifw708#1; aAlfa = aAlfa - "MEMO ";
   |SI FSalida = 0;
      #agifw709#0 = nCalc;
      #agifw709#1 = #agifw708#2;
      #agifw709#2 = #agifw708#4;
      aAlfa = "[" + #agifw708#1; |QBF aAlfa;
      aAlfa = aAlfa + "][" + #agifw708#3 + "]";
      #agifw709#5 = aAlfa;
      |GRABA 020#agifw709.n;
   |SINO;
      |QBF aAlfa;
      #agifw709#0 = nCalc;
      #agifw709#6 = aAlfa;
      aAlfa = "[" + aAlfa + "]"; |QBF aAlfa;
      #agifw709#5 = aAlfa;
      |GRABA 020#agifw709.n;
   |FINSI;

   |REFRESCACONTROL nGridB;
|FPRC;

|PROCESO Elimina;
   |BORRA 020#agifw709.a;
   |LEE 000#agifw709.c;
   |SI FSalida ! 0;
      |ESTADO_CONTROL nBotonEli, "DISABLE";
      |ESTADO_CONTROL nBotonRec, "DISABLE";
   |FINSI;

   |REFRESCACONTROL nGridB;
|FPRC;

|PROCESO Recorta;
   |VENTANA 1228, 1672, -1, 17, "Recortar valor campo"; nVentFlt = FSalida;
   aVent1 = -1;
   aVent2 = -1;
   aVent3 = 0;
   aVent4 = nVentFlt;
   aVent5 = "MODAL";
   |ESPECIFICA "ESTADO_VENTANA", aVent;

   |DDEFECTO #agifw799;
   #agifw799#0 = #agifw709#3;
   #agifw799#1 = #agifw709#4;
   |PINPA #0#agifw799; nPantFlt = FSalida;
   |PINDA #0#agifw799;
   |PTEC 802; |PAUSA;

   |ENDATOS #1#agifw799;
   |SI FSalida = 0;
      #agifw709#3 = #agifw799#0;
      #agifw709#4 = #agifw799#1;
      |GRABA 020#agifw709.a;
   |FINSI;

   aVent4 = nVentFlt;
   aVent5 = "MODELESS";
   |ESPECIFICA "ESTADO_VENTANA", aVent;
   |FINVENTANA nVentFlt;
|FPRC;

|PROCESO EditaValor;
   |VENTANA 1224, 1476, -1, 17, "Valor fijo"; nVentFlt = FSalida;
   aVent1 = -1;
   aVent2 = -1;
   aVent3 = 0;
   aVent4 = nVentFlt;
   aVent5 = "MODAL";
   |ESPECIFICA "ESTADO_VENTANA", aVent;

   |PINPA #0#agifw700; nPanta = FSalida;
   |PTEC 802; |PAUSA;

   nPos1 = 0602; nPos2 = 0612;
   |CONTROL_SIMPLE nPanta, "LABEL,{{2}}Valor : ", nPos1, nPos2, NULL;
   nLabel = FSalida;
   |PULSATECLA;

   E_sup = 40; E_inf = "";
   aValor = 0610 ? 1;
   |SI FSalida ! 0; aValor = ""; |FINSI;

   |FIN_CONTROL_WINDOWS nLabel;

   aVent4 = nVentFlt;
   aVent5 = "MODELESS";
   |ESPECIFICA "ESTADO_VENTANA", aVent;
   |FINVENTANA nVentFlt;
|FPRC;

|PROCESO ValorFijo;
   aValor = "";
   |HAZ EditaValor;
   |SI aValor ! "";
      |LEE 000#agifw709.u;
      |SI FSalida ! 0; #agifw709#0 = 0; |FINSI;
      nCalc = #agifw709#0 + 1;

      |DDEFECTO #agifw709;
      #agifw709#0 = nCalc;
      #agifw709#5 = aValor;
      |GRABA 020#agifw709.n;
      |REFRESCACONTROL nGridB;
   |FINSI;
|FPRC;

|PROCESO ObtenResultado;
   |SI #agifw709#1 ! "        ";
      eaResultado = eaResultado + "[";
      |SI #agifw709#3 ! " ";
         eaResultado = eaResultado + "%";
         |SI #agifw709#3 = "A";
            eaResultado = eaResultado + "-";
            eaResultado = eaResultado + (("00" + #agifw709#4) % -102);
         |SINO;
            eaResultado = eaResultado + (("000" + #agifw709#4) % -103);
         |FINSI;
      |FINSI;
      eaResultado = eaResultado + #agifw709#1 + "][" + (("000" + #agifw709#2) % -103) + "]";
   |SINO;
      |SI #agifw709#6 ! "               ";
         eaResultado = eaResultado + "[" + #agifw709#6; |QBF eaResultado;
         eaResultado = eaResultado + "]";
      |SINO;
         eaResultado = eaResultado + #agifw709#5; |QBF eaResultado;
      |FINSI;
   |FINSI;
|FPRC;

|DEFBUCLE ObtenResultado;
   #agifw709#1;
   ;
   INICIO;
   FINAL;
   ;
   NULL, ObtenResultado;
|FIN;

|PROCESO AnalizaResultado;
   aAlfa = eaResultado; |QBF aAlfa;

   aLong = aAlfa % 0; nLong = aLong;

   aAlfa2 = ""; nDato = 0;
   |PARA nCont = 1; |SI nCont [ nLong; |HACIENDO nCont = nCont + 1;
      nCalc = (nCont * 100) + 1;
      aAlfa1 = aAlfa % nCalc;
      |SI nDato = 0;
         |SI aAlfa1 = "[";
            |SI aAlfa2 ! "";
               |LEE 000#agifw709.u;
               |SI FSalida ! 0; #agifw709#0 = 0; |FINSI;
               nCalc = #agifw709#0 + 1;
               |DDEFECTO #agifw709;
               #agifw709#0 = nCalc;
               #agifw709#5 = aAlfa2;
               |GRABA 020#agifw709.n;
               aAlfa2 = "";
            |FINSI;
            aAlfa1 = ""; nDato = 1;
         |SINO;
            aAlfa2 = aAlfa2 + aAlfa1;
         |FINSI;
      |FINSI;
      |SI nDato = 1;
         |SI aAlfa1 = "]";
            nCont = nCont + 1;
            nCalc = (nCont * 100) + 1;
            aAlfa1 = aAlfa % nCalc;
            |SI aAlfa1 = "[";            || Si despues de encontrar una cadena
               aAlfa1 = ""; nDato = 2;   || [XXXXXXX] hay un '[' estamos ante
            |SINO;                       || un campo del tipo [fichero][campo]
               aAlfa2 = aAlfa1;
               aAlfa1 = aFichero % 0101; || Si no, tendremos en aFichero el
               |SI aAlfa1 = "%";         || nombre de una memoria. La grabamos
                  aAlfa1 = aFichero % 0201;
                  |SI aAlfa1 = "-";
                     aDirRec = "A";
                     aAlfa1 = aFichero % 0302; nDigRec = aAlfa1;
                     aAlfa1 = "%-" + (("00" + nDigRec) % -102);
                  |SINO;
                     aDirRec = "D";
                     aAlfa1 = aFichero % 0203; nDigRec = aAlfa1;
                     aAlfa1 = "%" + (("000" + nDigRec) % -103);
                  |FINSI;
                  aFichero = aFichero - aAlfa1;
               |FINSI;

               |LEE 000#agifw709.u;
               |SI FSalida ! 0; #agifw709#0 = 0; |FINSI;
               nCalc = #agifw709#0 + 1;
               |DDEFECTO #agifw709;
               #agifw709#0 = nCalc;
               #agifw709#1 = "";
               #agifw709#2 = 0;
               #agifw709#3 = aDirRec;
               #agifw709#4 = nDigRec;
               #agifw709#5 = "[" + aFichero + "]";
               #agifw709#6 = aFichero;
               |GRABA 020#agifw709.n;
               aFichero = ""; aCampo   = ""; nDato = 0;
            |FINSI;
         |SINO;
            aFichero = aFichero + aAlfa1;
         |FINSI;
      |FINSI;
      |SI nDato = 2;
         |SI aAlfa1 = "]";
            aAlfa1 = aFichero % 0101;
            |SI aAlfa1 = "%";
               aAlfa1 = aFichero % 0201;
               |SI aAlfa1 = "-";
                  aDirRec = "A";
                  aAlfa1 = aFichero % 0302; nDigRec = aAlfa1;
                  aAlfa1 = "%-" + (("00" + nDigRec) % -102);
               |SINO;
                  aDirRec = "D";
                  aAlfa1 = aFichero % 0203; nDigRec = aAlfa1;
                  aAlfa1 = "%" + (("000" + nDigRec) % -103);
               |FINSI;
               aFichero = aFichero - aAlfa1;
            |FINSI;

            |LEE 000#agifw709.u;
            |SI FSalida ! 0; #agifw709#0 = 0; |FINSI;
            nCalc = #agifw709#0 + 1;
            |DDEFECTO #agifw709;
            #agifw709#0 = nCalc;
            #agifw709#1 = aFichero;
            #agifw709#2 = aCampo;
            #agifw709#3 = aDirRec;
            #agifw709#4 = nDigRec;

            |DDEF aAlfa1; |QBF aAlfa1;
            aAlfa1 = aAlfa1 + aFichero; |QBF aAlfa1;
            aAlfa1 = aAlfa1 + ".mas";
            |CARGA_DEF aAlfa1, Referen@;
            |SI FSalida = 0;
               aAlfa1 = "|TITULO"; aAlfa1 = #Referen@#aAlfa1#; |QBF aAlfa1;
               aAlfa2 = "|C" + aCampo + "NOMBRE"; aAlfa2 = #Referen@#aAlfa2#; |QBF aAlfa2;
               |DESCARGA_DEF #Referen@;
               aAlfa1 = "[" + aAlfa1 + "][" + aAlfa2 + "]";
               #agifw709#5 = aAlfa1;
            |FINSI;
            |GRABA 020#agifw709.n;
            aFichero = ""; aCampo   = ""; nDato = 0; aAlfa2 = "";
         |SINO;
            aCampo = aCampo + aAlfa1;
         |FINSI;
      |FINSI;
   |SIGUE;
   |QBF aAlfa2;

   |SI aAlfa2 ! "";
      |LEE 000#agifw709.u;
      |SI FSalida ! 0; #agifw709#0 = 0; |FINSI;
      nCalc = #agifw709#0 + 1;
      |DDEFECTO #agifw709;
      #agifw709#0 = nCalc;
      #agifw709#5 = aAlfa2;
      |GRABA 020#agifw709.n;
      aAlfa2 = "";
   |FINSI;
|FPRC;

|PROGRAMA;
   aAlfa = "tm1"; |NOME_DAT #agifw708#aAlfa#;
   |DELINDEX #agifw708; |ABRE #agifw708;
   aAlfa = "tm2"; |NOME_DAT #agifw709#aAlfa#;
   |DELINDEX #agifw709; |ABRE #agifw709;

   aAlfa = PARAMETRO; |QBF aAlfa;

   aCodEnl = aAlfa % 0104;
   aAlfa1  = aAlfa % 0506; nNumAsto = aAlfa1;
   aAlfa1  = aAlfa % 1104; nNumLin  = aAlfa1;
   aAlfa1  = aAlfa % 1501; aTipoCmp = aAlfa1;

   |ABRE #agif0704;
   #agif0704#0 = aCodEnl;
   |LEE 000#agif0704.=;
   |SI FSalida = 0; aTipoEnl = #agif0704#2; |FINSI;
   |CIERRA #agif0704;

   |BUCLE RecogeConceptos;
   |BUCLE RecogeMemorias;
   |HAZ AnalizaResultado;

   |SI aTipoCmp = "C"; aAlfa = "Selecci¢n de campos para cuenta contable"; |FINSI;
   |SI aTipoCmp = "D"; aAlfa = "Selecci¢n de campos para descripcion de concepto"; |FINSI;
   |SI aTipoCmp = "I"; aAlfa = "Selecci¢n de campos para importe"; |FINSI;
   |VENTANA 0628, 2372, -1, 17, aAlfa; nVent = FSalida;
   aVent1 = -1;
   aVent2 = -1;
   aVent3 = 0;
   aVent4 = nVent;
   aVent5 = "MODAL";
   |ESPECIFICA "ESTADO_VENTANA", aVent;

   |PINPA #0#agif0708; nPant = FSalida;
   |PINDA #0#agif0708;

   nPos1 = 1334; nPos2 = 1344;
   |CONTROL_SIMPLE nPant, "BOTON,{{231}}", nPos1, nPos2, Elimina;
   nBotonEli = FSalida; |ESTADO_CONTROL nBotonEli, "DISABLE";

   nPos1 = 1322; nPos2 = 1332;
   |CONTROL_SIMPLE nPant, "BOTON,{{232}}", nPos1, nPos2, Incluye;
   nBotonInc = FSalida; |ESTADO_CONTROL nBotonInc, "DISABLE";

   nPos1 = 2205; nPos2 = 2229;
   |CONTROL_SIMPLE nPant, "BOTON,{{197}}Usar parte del valor del campo", nPos1, nPos2, Recorta;
   nBotonRec = FSalida; |ESTADO_CONTROL nBotonRec, "DISABLE";

   nPos1 = 2231; nPos2 = 2244;
   |CONTROL_SIMPLE nPant, "BOTON,{{197}}Nuevo valor fijo", nPos1, nPos2, ValorFijo;
   nBotonVF = FSalida;

   nRango = 4 + 8 + 16 + 32;
   nPos1 = 0602; nPos2 = 1144;
   |LINEAL_SIMPLE #1#agifw708, nRango, nPos1, nPos2, NULL, NULL, EvtoGridA;
   nGridA = FSalida;

   nPos1 = 1502; nPos2 = 2044;
   |LINEAL_SIMPLE #1#agifw709, nRango, nPos1, nPos2, NULL, NULL, EvtoGridB;
   nGridB = FSalida;
   |PULSATECLA;

   aEsc1  = &255 + "806";
   aEsc2  = &255 + "807";
   aRetu  = &255 + "802";
   |PARA; |SI;  |HACIENDO;
      FSalida = "::{{001}}Salir," + nGridA;
      |LEETECLA aTecla;
      |SI aTecla = aEsc1; |SAL; |FINSI;
      |SI aTecla = aEsc2; |SAL; |FINSI;
      |SI aTecla = aRetu; |SAL; |FINSI;
   |SIGUE;

   |DESTRUYECONTROL nGridA;
   |DESTRUYECONTROL nGridB;
   |FIN_CONTROL_WINDOWS nBotonInc;
   |FIN_CONTROL_WINDOWS nBotonEli;
   |FIN_CONTROL_WINDOWS nBotonRec;

   eaResultado = "";
   |BUCLE ObtenResultado;

   |CIERRA #agifw709; |DELINDEX #agifw709;
   |CIERRA #agifw708; |DELINDEX #agifw708;

   aVent4 = nVent;
   aVent5 = "MODELESS";
   |ESPECIFICA "ESTADO_VENTANA", aVent;
   |FINVENTANA nVent;
|FPRO;
