     ALBARANES

|FICHEROS;
     ialbz008 #0;
     agifa010 #10;
     ialbz027 #27;
     agifa071 #71;
     agifa142 #142;
     referec@;
     referel@;
|FIN;

|VARIABLES;
     &aDivisa = "";
     &aMoneda = "";
     &eaAlfa = "";
     &eaRegNuevo = "";
     &eaProg = "";
     &eaEstadoBtn = "";
     &eaTmp = "";
     &eaPestana = "";
     &eaSwCarga3 = "";
     &eaBtnAcota3 = "";
     &eaSer3 = "";
     &eaNum3 = "";
     &eaLin3 = "";

     aProg = "";
     aTmp = "";
     aAlfa = "";
     nRango = 0;
     nPos1 = 0;
     nPos2 = 0;
     nPestana = 0;
     aPath = "";
     nPos = 0;
     nDDir = 0;
     nHDir = 0;
     aDFac = "";
     aHFac = "";
     aSer = "";
     aNum = "";
     nSw = 0;
     nNume = 0;

     nBtnPdtFac3 = 0;
     nBtnFactu3 = 0;
     nBtnTodos3 = 0;
     nBtnNuevo3 = 0;
     nBtnImpre3 = 0;
     nBtnSalir3 = 0;
     nGridC = -1;
     nGridL = -1;
     nPanta = 0;
     nEvtFoco = 0;
     nBtnFacturar13 = -1;
     nBtnVerPed13 = -1;
     nBtnModifi13 = -1;
     nBtnVerPed23 = -1;
     nBtnVerFac23 = -1;

     nVentana = 0;
 {-1}aVent = "";
     aVent1 = 0;
     aVent2 = 0;
     aVent3 = 0;
     aVent4 = 0;
     aVent5 = "";
     aTipo = "";
     nSwRefre = 0;
|FIN;

|PROCESO ActLineal3;
     |SI #0#0 ! "000000";
          |ABRE #10;
          #10#52 = aSer;
          #10#0 = aNum;
          |LEE 000#10=;
          |SI FSalida = 0;
               nSw = 0;
               |SI eaBtnAcota3 = "1"; |Y #10#38 = "N"; nSw = 1; |FINSI;
               |SI eaBtnAcota3 = "2"; |Y #10#83 = "S"; nSw = 1; |FINSI;
               |SI eaBtnAcota3 = "3"; nSw = 1; |FINSI;
               #referec@#52 = #10#52;
               #referec@#0 = #10#0;
               |LEE 000#referec@.=;
               |SI nSw = 1;   || si debe de estar
                    |SI FSalida = 0;
                         |BORRA 020#referec@.c;
                    |FINSI;
                    |SALVA_DATOS #10;
                    |REPON_DATOS #referec@;
                    |GRABA 000#referec@.n;
                    |LEE 000#referec@.=;
               |SINO;         || no debe de estar
                    |SI FSalida = 0
                         |BORRA 020#referec@.c;
                    |FINSI;
               |FINSI;
          |FINSI;
          |CIERRA #10;
     |FINSI;
     |REFRESCACONTROL nGridC;
|FPRC;
//----------------//
|PROCESO Existe3;
     |ABRE #10;
     #10#52 = #referec@#52;
     #10#0 = #referec@#0;
     |LEE 000#10=;
     aAlfa = FSalida;
     |CIERRA #10;
     FSalida = aAlfa;
|FPRC;

|PROCESO BtnVerPed23;
     |HAZ BtnVerPed13;
|FPRC;

|PROCESO BtnVerFac23;
     |HAZ Existe3;
     |SI FSalida ! 0; |ACABA; |FINSI;

     aProg = "*dszx0004;"+ "F" + #referec@#52 + (("000000" + #referec@#0)%-106);
     |SUB_EJECUTA aProg;
|FPRC;

|PROCESO PideFecha;
     |VENTANA 1324, 2074, -1, 17, "Facturar";
     nVentana = FSalida;
     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVentana;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |DDEFECTO #27;
     #27#1 = #referec@#52
     #27#2 = #referec@#0;
     |PINPA #0#27;
     |PINDA #0#27;
     |ENDATOS #1#27;
     |SI FSalida = 0;
          aAlfa = #27#0;
     |SINO;
          aAlfa = "";
     |FINSI;
     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVentana;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;
     |FINVENTANA nVentana;
     |PULSATECLA;
|FPRC;

|PROCESO BtnFacturar13;
     |HAZ Existe3;
     |SI FSalida ! 0; |ACABA; |FINSI;

     |HAZ PideFecha;
     |SI aAlfa ! "";
          aProg = "*dszx0004;"+ "A" + #referec@#52 + (("000000" + #referec@#0)%-106) + aAlfa;
          |SUB_EJECUTA aProg;
          aSer = #referec@#52;
          aNum = #referec@#0;
          aTipo = "A"; |HAZ ActLineal3;
     |FINSI;
|FPRC;

|PROCESO BtnVerPed13;
     |HAZ Existe3;
     |SI FSalida ! 0; |ACABA; |FINSI;

     |SI #10#41 = 0;
          |MENAV "0000El albar n no proviene de pedido....";
          |ACABA;
     |FINSI;

     aProg = "*dszx0004;"+ "P" + #referec@#52 + (("000000" + #referec@#0)%-106);
     |SUB_EJECUTA aProg;
|FPRC;

|PROCESO BtnModifi13;
     |HAZ Existe3;
     |SI FSalida ! 0; |ACABA; |FINSI;

     aProg = "*dszx0004;"+ "M" + #referec@#52 + (("000000" + #referec@#0)%-106);
     |SUB_EJECUTA aProg;
     aSer = #referec@#52;
     aNum = #referec@#0;
     aTipo = "M"; |HAZ ActLineal3;
|FPRC;
//----------------//
|PROCESO CargaDatos3;
     |SI eaSwCarga3 ! "S";
          |HAZ DestruyePan3;
          |HAZ Descarga3;
          |SI #0#0 = "000000";
               |REFERENCIA_DEF referec@, #10;
          |FINSI;
          |REFERENCIA_DEF referel@, #71;
          |HAZ PintaPanta3;
     |FINSI;
|FPRC;

|PROCESO CogePosicion3;
     eaSer3 = #referec@#52;
     eaNum3 = ("000000" + #referec@#0) % -106;
     eaLin3 = ("000" + #referel@#1) % -103;
|FPRC;

|PROCESO Posiciona3;
     #referel@#29 = eaSer3;
     #referel@#0 = eaNum3;
     #referel@#1 = eaLin3;
     |LEE 000#referel@.=;

     #referec@#52 = eaSer3;
     #referec@#0 = eaNum3;
     |LEE 000#referec@.=;
     |REFRESCACONTROL nGridC;

     |SI eaPestana = "3"; |FOCOTECLADO nGridC; |FINSI;
|FPRC;

|PROCESO EstadoBtn3;
     |SI #0#0 = "000000";
          |ESTADO_CONTROL nBtnPdtFac3, "DISABLE";
          |ESTADO_CONTROL nBtnFactu3, "DISABLE";
          |ESTADO_CONTROL nBtnTodos3, "DISABLE";
     |SINO;
          |SI eaBtnAcota3 = "1";
               |ESTADO_CONTROL nBtnPdtFac3, "DISABLE";
               |ESTADO_CONTROL nBtnFactu3, "ENABLE";
               |ESTADO_CONTROL nBtnTodos3, "ENABLE";
          |FINSI;
          |SI eaBtnAcota3 = "2";
               |ESTADO_CONTROL nBtnPdtFac3, "ENABLE";
               |ESTADO_CONTROL nBtnFactu3, "DISABLE";
               |ESTADO_CONTROL nBtnTodos3, "ENABLE";
          |FINSI;
          |SI eaBtnAcota3 = "3";
               |ESTADO_CONTROL nBtnPdtFac3, "ENABLE";
               |ESTADO_CONTROL nBtnFactu3, "ENABLE";
               |ESTADO_CONTROL nBtnTodos3, "DISABLE";
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO BtnPdtFac3;
     eaBtnAcota3 = "1";
     eaSwCarga3 = "N";
     |HAZ EventoFoco3;
|FPRC;

|PROCESO BtnServidos3;
     eaBtnAcota3 = "2";
     eaSwCarga3 = "N";
     |HAZ EventoFoco3;
|FPRC;

|PROCESO BtnTodos3;
     eaBtnAcota3 = "3";
     eaSwCarga3 = "N";
     |HAZ EventoFoco3;
|FPRC;

|PROCESO BtnNuevo3;
     eaProg = "*dszx0007;"+ "N" + "     000000" + #0#0 + (("0000"+#0#1)%-104);
     |HAZ EjecutaProg;
     aSer = eaAlfa % 105;
     aNum = eaAlfa % 606;
     aTipo = "N"; |HAZ ActLineal3;
|FPRC;

|PROCESO BtnImpre3;
     |HAZ Existe3;
     |SI FSalida ! 0; |ACABA; |FINSI;

     aProg = "*dszx0004;"+ "I" + #referec@#52 + (("000000"+#referec@#0)%-106);
     |SUB_EJECUTA aProg;
|FPRC;

|PROCESO EventoC3;
     |SI FSalida = 1; |O FSalida = 2;
          |LEE 000#referec@.=;
          |SI FSalida = 0;
               aDivisa = #referec@#68;
               aMoneda = #referec@#70;
               |HAZ Divisas010;
          |FINSI;
          |REFRESCACONTROL nGridL;
     |FINSI;
|FPRC;

|PROCESO EventoL3;
     |SI FSalida = 1; |O FSalida = 2;
          |LEE 000#referel@.=;
     |FINSI;
|FPRC;

|PROCESO EventoFoco3;
     |HAZ CargaDatos3;
     eaPestana = "3";
|FPRC;

|PROCESO CargaTmp3;
     |SI eaBtnAcota3 = "1";
          |SI #10#51 = "N"; |ACABA; |FINSI;
          nNume = #10#67 ! 2;
          |SI nNume = 0; |ACABA; |FINSI;
     |FINSI;

     |SALVA_DATOS #10;
     |REPON_DATOS #referec@;
     |GRABA 020#referec@.n;
|FPRC;

|DEFBUCLE CargaTmp3;
     #10#8;
     ;
     #0#0, nDDir, aDFac;
     #0#0, nHDir, aHFac;
     ;
     NULL, CargaTmp3;
|FIN;

|PROCESO NomeDat3;
     |SI #0#0 = "000000";
          aTmp = "agifa010";
          |NOME_DAT #referec@#aTmp#;
     |SINO;
          nPos = ((8 * 2 + 1) * 100 + 8);
          aTmp = eaTmp % nPos;
          |NOME_DAT #referec@#aTmp#;
          |DELINDEX #referec@;
     |FINSI;
|FPRC;

|PROCESO MinL3;
     #referel@#29 = #referec@#52;
     #referel@#0 = #referec@#0;
     #referel@#1 = 1;
|FPRC;

|PROCESO MaxL3;
     #referel@#29 = #referec@#52;
     #referel@#0 = #referec@#0;
     #referel@#1 = 999;
|FPRC;

|PROCESO MinC3;
     #referec@#52 = "     ";
     #referec@#0 = 0;
|FPRC;

|PROCESO MaxC3;
     #referec@#52 = "zzzzz";
     #referec@#0 = 999999;
|FPRC;

|PROCESO EstadoIniBtnPan3;
     |ESTADO_CONTROL nBtnPdtFac3, eaEstadoBtn;
     |ESTADO_CONTROL nBtnFactu3, eaEstadoBtn;
     |ESTADO_CONTROL nBtnTodos3, eaEstadoBtn;
     |ESTADO_CONTROL nBtnNuevo3, eaEstadoBtn;
     |ESTADO_CONTROL nBtnImpre3, eaEstadoBtn;
     |ESTADO_CONTROL nBtnSalir3, eaEstadoBtn;
|FPRC;

|PROCESO DestruyePan3;
     |DESTRUYECONTROL nGridL;
     |DESTRUYECONTROL nGridC;

     |FIN_CONTROL_WINDOWS nEvtFoco;

     |FIN_CONTROL_WINDOWS nBtnPdtFac3;
     |FIN_CONTROL_WINDOWS nBtnFactu3;
     |FIN_CONTROL_WINDOWS nBtnTodos3;
     |FIN_CONTROL_WINDOWS nBtnNuevo3;
     |FIN_CONTROL_WINDOWS nBtnImpre3;
     |FIN_CONTROL_WINDOWS nBtnSalir3;
     |SI nBtnFacturar13 ! -1; |FIN_CONTROL_WINDOWS nBtnFacturar13; |FINSI;
     |SI nBtnVerPed13 ! -1; |FIN_CONTROL_WINDOWS nBtnVerPed13; |FINSI;
     |SI nBtnModifi13 ! -1; |FIN_CONTROL_WINDOWS nBtnModifi13; |FINSI;
     nBtnFacturar13 = -1;
     nBtnVerPed13 = -1;
     nBtnModifi13 = -1;
     |SI nBtnVerPed23 ! -1; |FIN_CONTROL_WINDOWS nBtnVerPed23; |FINSI;
     |SI nBtnVerFac23 ! -1; |FIN_CONTROL_WINDOWS nBtnVerFac23; |FINSI;
     nBtnVerPed23 = -1;
     nBtnVerFac23 = -1;
|FPRC;

|PROCESO Descarga3;
     |CIERRAT #referec@;
     |CIERRAT #referel@;
     |DESCARGA_DEF #referel@;
     |DESCARGA_DEF #referec@;
|FPRC;

|PROCESO PintaPanta3;
     |DDEF aPath;
     aAlfa = aPath + "agifa010";
     |CARGA_DEF aAlfa, referec@;
     aAlfa = aPath + "agifa071";
     |CARGA_DEF aAlfa, referel@;

     |HAZ NomeDat3;
     #referec@#0 = 0;    || Para que no salga nada si esta vacio

     |ABRET #referec@;;
     |ABRET #referel@;
     |SI #0#0 ! "000000";
          |SI #0#1 = 0;
               nDDir = 0; nHDir = 9999;
          |SINO;
               nDDir = #0#1; nHDir = #0#1;
          |FINSI;
          |SI eaBtnAcota3 = "1"; aDFac = "N"; aHFac = "N"; |FINSI;
          |SI eaBtnAcota3 = "2"; aDFac = "S"; aHFac = "S"; |FINSI;
          |SI eaBtnAcota3 = "3"; aDFac = " "; aHFac = "z"; |FINSI;
          |BUCLE CargaTmp3;
          |LEE 000#referec@.p;
     |FINSI;
     eaSwCarga3 = "S";

     |PINPA #3#0;  nPanta = FSalida;

     nRango = 4 + 8 + 16 + 32;
     nPos1 = 2402;
     nPos2 = 3398;
     |SI nGridL ! -1; |DESTRUYECONTROL nGridL; |FINSI;
     |LINEAL_SIMPLE #1#referel@, nRango, nPos1, nPos2, MinL3, MaxL3, EventoL3;
     nGridL = FSalida;

     nRango = 4 + 8 + 16 + 32;
     nPos1 = 0602;
     nPos2 = 2298;
     |SI nGridC ! -1; |DESTRUYECONTROL nGridC; |FINSI;
     |LINEAL_SIMPLE #1#referec@, nRango, nPos1, nPos2, MinC3, MaxC3, EventoC3;
     nGridC = FSalida;

     |CONTROL_SIMPLE nPanta, "EVENTOPAN", 0000, 0000, EventoFoco3;
     nEvtFoco = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,Ptes. Facturar", 0501, 0513, BtnPdtFac3;
     nBtnPdtFac3 = FSalida;
     |CONTROL_SIMPLE nPanta, "BOTON,Facturados", 0514, 0526, BtnServidos3;
     nBtnFactu3 = FSalida;
     |CONTROL_SIMPLE nPanta, "BOTON,Todos", 0527, 0539, BtnTodos3;
     nBtnTodos3 = FSalida;
     |CONTROL_SIMPLE nPanta, "BOTON,{{193}}Nuevo", 0566, 0578, BtnNuevo3;
     nBtnNuevo3 = FSalida;
     |CONTROL_SIMPLE nPanta, "BOTON,{{205}}Imprimir", 0579, 0591, BtnImpre3;
     nBtnImpre3 = FSalida;

     |SI #0#0 ! "000000";
          |SI eaBtnAcota3 = "1";
               |CONTROL_SIMPLE nPanta, "BOTON,Facturar", 2301, 2313, BtnFacturar13;
               nBtnFacturar13 = FSalida;
               |CONTROL_SIMPLE nPanta, "BOTON,Ver Pedido", 2314, 2326, BtnVerPed13;
               nBtnVerPed13 = FSalida;
               |CONTROL_SIMPLE nPanta, "BOTON,Modificar", 2327, 2339, BtnModifi13;
               nBtnModifi13 = FSalida;
          |FINSI;
          |SI eaBtnAcota3 = "2";
               |CONTROL_SIMPLE nPanta, "BOTON,Ver Pedido", 2301, 2313, BtnVerPed23;
               nBtnVerPed23 = FSalida;
               |CONTROL_SIMPLE nPanta, "BOTON,Ver Factura", 2314, 2326, BtnVerFac23;
               nBtnVerFac23 = FSalida;
          |FINSI;
     |FINSI;

     |CONTROL_SIMPLE nPanta, "BOTON,{{207}} Salir", 3488, 3498, BtnSalir;
     nBtnSalir3 = FSalida;

     |HAZ EstadoBtn3;
|FPRC;
