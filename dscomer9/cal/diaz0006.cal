|FICHEROS;
     diaz0006 #0;
     diam0003 #3;
     diam0005 #5;
     agifa073 #73;
     agifa104 #104;
     dsparm01@ #100;
     dstelm02@ #200;
     dsparm05@ #500;
|FIN;

|VARIABLES;
     aDat      = "";
     aDef1     = "";
     aDef2     = "";
     aDef3     = "";
     aBase     = "";
     aFichero  = "";
     nHay      = 0;
     fFecha    = @;
     nNume     = 0;
     aAlfa     = "";
     aIP       = "";
     aServidor = "";
     aFrom     = "";
     aTo       = "";
     aSubject  = "";
     aMensaje  = "";
     aDjunto   = "vacio.txt";
     aNume     = "";
     nTodosCerrados = 0;
     aToPrepe = "";
|FIN;

|PROCESO CorreoLista;
     fFecha = ~;
     aSubject = fFecha + ": Relacion Visados Abiertos con sus Tiquets Cerrados.";
     aMensaje = "$$$$" + aFichero;
     aDjunto = aFichero;
     aServidor = #100#7;
     aIP = #100#8;
     |QBF aIP;
     |QBF aServidor;

     |DSCORREO_ENVIA aIP, aServidor, aFrom, aToPrepe, aSubject, aMensaje, aDjunto;

     |COPIA_FICHERO "/tmp/fichero.txt", "/tmp/diaz0006.txt";
|FPRC;

|PROCESO Correo;
     aNume = ("000000" + #104#0) % -106;
     aSubject = "Visado Abierto con sus tiquets cerrados: " + #104#25 + "/" + aNume + " " + #104#8;
     aMensaje = aSubject;
     aServidor = #100#7;
     aIP = #100#8;
     |QBF aIP;
     |QBF aServidor;

     |DSCORREO_ENVIA aIP, aServidor, aFrom, aTo, aSubject, aMensaje, aDjunto;
|FPRC;

|PROCESO diam0005;
     |SI #5#9 = 0;  |ACABA;   |FINSI;

     nTodosCerrados = 0;

     #104#25 = #5#0;
     #104#0 = #5#1;
     |LEE 000#104=;
     |SI FSalida ! 0;    |ACABA;   |FINSI;

     #200#5 = #104#4;
     #200#0 = #5#9;
     |LEE 000#200=;
     |SI #200#7 = "A";|Y FSalida = 0;
          nTodosCerrados = 1;
          |ERROR10;
     |FINSI;
|FPRC;

|DEFBUCLE diam0005;
     #5#1;
     ;
     #3#0, #3#1, #3#2, 0000;
     #3#0, #3#1, #3#2, 9999;
     ;
     NULL, diam0005;
|FIN;

|PROCESO diam0003;
     nTodosCerrados = -1;
     |BUCLE diam0005;
     |SI nTodosCerrados = 0;
          #500#0 = #3#2;
          |LEE 000#500=;
          aTo = #500#3; |QBF aTo;
          |SI aTo = "";
               aTo = #500#4;
          |SINO;
               aAlfa = #500#4; |QBF aAlfa;
               |SI aAlfa ! "";
                    aTo = aTo + ";" + #500#4;
               |FINSI;
          |FINSI;

          |QBF aTo;
          |HAZ Correo;

          |PRINT;
          nHay = 1;
     |FINSI;
|FPRC;

|DEFBUCLE diam0003;
     #3#2;
     6;
     0000, "S", "A";          || visados SI y abiertos SI
     9999, "S", "A";
     ;
     NULL, diam0003;
|FIN;

|PROCESO Inicio;
     |DBASS aBase;  |QBF aBase;
     aFichero  = aBase + "dspartes/correos";  |MKDIR aFichero;
     aFichero  = aFichero + "/" + Usuario + ".txt";
     |FBORRA aFichero;

     Impresora = aFichero;
     |IMPRE -77;
     |SI FSalida = 0;
          |INFOR "diaz0006";
          |SI FSalida ! 0;
               |FINIMP;
          |SINO;
               |ABRE #104;
               |BUCLE diam0003;
               |CIERRA #104;
               |PIEINF;
               |FININF;
          |FINSI;
          |FINIMP;
     |FINSI;
     |SI nHay ! 0; |HAZ CorreoLista; |FINSI;

     |FBORRA aFichero;
|FPRC;

|PROCESO Tipo3; |TIPO 3;
     |SI #0#0 ! "SI"; |ACABA; |FINSI;

     |CARGA_DEF aDef1, dsparm05@;
     |SI FSalida = 0;
          |CARGA_DEF aDef2, dsparm01@;
          |SI FSalida = 0;
               |CARGA_DEF aDef3, dstelm02@;
               |SI FSalida = 0;
                    |PATH_DAT #500 aDat;
                    |PATH_DAT #100 aDat;
                    |PATH_DAT #200 aDat;
                    |ABRE #100;
                    |LEE 000#100p;      || para no concurrir en "/tmp/fichero.txt"
                    |SI FSalida = 0;
                         |ABRE #500;
                         #500#0 = #100#12;
                         |LEE 000#500=;
                         aFrom = #500#3; |QBF aFrom;
                         aToPrepe = #500#3; |QBF aToPrepe;
                         |SI aToPrepe = "";
                              aToPrepe = #500#4;
                         |SINO;
                              aAlfa = #500#4; |QBF aAlfa;
                              |SI aAlfa ! "";
                                   aToPrepe = aToPrepe + ";" + #500#4;
                              |FINSI;
                         |FINSI;
                         |QBF aToPrepe;
                         |ABRE #200;

                         |HAZ Inicio;
                         |CIERRA #200;
                         |CIERRA #500;
                    |FINSI;
                    |CIERRA #100;
                    |DESCARGA_DEF #dstelm02@;
               |SINO;
                    aDef3 = "0000Error al cargar:" + aDef3;
                    |MENAV aDef3;
               |FINSI;
               |DESCARGA_DEF #dsparm01@;
          |SINO;
               aDef2 = "0000Error al cargar:" + aDef2;
               |MENAV aDef2;
          |FINSI;
          |DESCARGA_DEF #dsparm05@;
     |SINO;
          aDef3 = "0000Error al cargar:" + aDef3;
          |MENAV aDef3;
     |FINSI;
|FPRC;

|PROGRAMA;
     |DBASS aBase; |QBF aBase;

     aDef1 = aBase + "dspartes/def/dsparm05";
     aDef2 = aBase + "dspartes/def/dsparm01";
     aDef3 = aBase + "dspartes/def/dstelm02";
     aDat  = aBase + "dspartes/fich/";

     |SI PARAMETRO = "";
          |MANTE #0;
     |SINO;
          #0#0 = "SI";
          |HAZ Tipo3;
     |FINSI;
|FPRO;
