|FICHEROS;
     agifa166 #0;  ||  Pantalla de pase.
     agifa143 #1;  ||  Temporal pase Facturas Proforma.
     agifa146 #2;  ||  Cabec. Facturas Proforma.
     agifa147 #3;  ||  Lineas Facturas Proforma.
     agifa104 #4;  ||  Cabec. Pedidos Clientes.
     agifa073 #5;  ||  Lineas Pedidos Clientes.

     agifa024 #7;  ||  Clientes.
     agifa019 #8;  ||  Articulos.
     agifa017 #9;  ||  Almacenes
     dsm00024 #24;
     agifa321 #35;  || Parametros de Divisas
     agifa328 #36;  || Clientes/Divisas
     agifa329 #37;  || Lineas de Clientes/Divisas
     agifa324 #38;  || Divisas
     agifa049 #49;
     dsm00053 #53;
     dsm00113 #113;

     agifa142 #142;

     dsm00005 #905;
     dsm00050 #950;
     dsm00046 #946;

     dsm00247 #141; ||Pantalla Informe Riesgo.
     dsm00248 #143; ||Auditoria.

     tagm0056@;
     mecam001@;
     mecam004@;
     mecam005@;
     mecam006@;
     mecam006a@;
     mecam018@;
     feldo001@;

     referen@;
|FIN;

|VARIABLES;
     nSwChampiEmar = 0;
     &ser_ped = "";
     &num_ped = 0;
     &num_alb = 0;
     &fec_alb = @;
     &eaSerPedido = "";
     &enNumPedido = 0;

     aParam = "";
     aSeccion = "";
     nSwGestral = 0;
     nSwTecatel = 0;
     nSwMecanogra = 0;
     aPath = "";
     fFechaEntrega = @;
     nFactor = 0;
     nUni = 0;
     aFaltaStock = "";
     &nRieUtili = 0;
     &nRieTotal = 0;

     nSwTagloma = 0;
     &eaTipAdj = "";
     nCalc = 0;
     &nImpo = 0;
     nLinV           = 0;
     &eaSer1         = "";
     &enDocu1        = 0;
     &eaTag          = "";
     &aRieBloqu      = "";
     &aBloquead      = "";
     &nMasRies       = 0;

     &enSwMenu       = 0;
     aFichero        = "";
     &nDeci_Base     = 0;   || Decimales en Totales de la moneda base
     &nDeci_PreBase  = 0;   || Decimales en el Precio en moneda base
     &nDeci_BaseMx   = 0;   || Decimales en Totales de la moneda base (Maxima precision)
     &nDeci_PreBasMx = 0;   || Decimales en el Precio en moneda base (Maxima precision)
     &nDeci_Div      = 0;   || Decimales en Totales de la divisa
     &nDeci_PreDiv   = 0;   || Decimales en el Precio en divisa
     &nDeci_TotVis   = 0;   || Decimales en Totales visibles (el de la moneda de trabajo)
     &nDeci_TotNoVis = 0;   || Decimales en Totales no visibles (el de la otra moneda)
     &nDeci_PreVis   = 0;   || Decimales en el Precio visual. (lineas)
     &nDeci_ImpVis   = 0;   || Decimales en el Importe visual. (lineas)
     &nDeci_BaseMxP  = 0;   || Decimales en Totales de la moneda base (Maxima precision)
     &nDeci_PreBasMxP= 0;   || Decimales en el Precio en moneda base (Maxima precision)
     &nDeci_DivP     = 0;   || Decimales en Totales de la divisa
     &nDeci_PreDivP  = 0;   || Decimales en el Precio en divisa
     &nDeci_TotVisP  = 0;   || Decimales en Totales visibles (el de la moneda de trabajo)
     &nDeci_TotNoVisP = 0;  || Decimales en Totales no visibles (el de la otra moneda)
     &nDeci_PreVisP  = 0;   || Decimales en el Precio visual. (lineas)
     &nDeci_ImpVisP  = 0;   || Decimales en el Importe visual. (lineas)
     &decim_divisa = 0;  || Decimales de la divisa
     &precio_divisa = 0; || Decimales del precio en divisa
     &decim_base = 0;    || Decimales de la moneda base
     &precio_base = 0;   || Decimales del precio en moneda base
     asto = 0;
     contador = "";
     fmes = "";
     aAlfa = "";
     mes  = 0;
     nConAu = 0;
     &eaCodCliente = "";
     &eaNomCliente = "";
|FIN;

|INCLUYE i_varart;

|PROCESO ChampiEmarPorte;
     |DDEF aPath;
     aAlfa = aPath + "champi14";
     |CARGA_DEF aAlfa, referen@;
     |SI FSalida = 0;
          |DDEFECTO #referen@;
          |ABRE #referen@;
          #referen@#0 = #4#25;
          #referen@#1 = #4#0;
          |LEE 101#referen@.=;
          |SI FSalida ! 0; |GRABA 020#referen@.b; |FINSI;
          #referen@#2 = #2#11;
          |GRABA 020#referen@.a;
          |CIERRA #referen@;
          |DESCARGA_DEF #referen@;
     |FINSI;
|FPRC;

|PROCESO FeldoPortes;
     |DDEF aAlfa;
     aAlfa = aAlfa + "feldo001";
     |CARGA_DEF aAlfa, feldo001@;
     |SI FSalida = 0;
          |ABRE #feldo001@;
          #feldo001@#0 = #2#48;
          #feldo001@#1 = #2#0;
          |LEE 101#feldo001@.=;
          |SI FSalida ! 0; |GRABA 020#feldo001@.b; |FINSI;
          #feldo001@#2 = #4#25;
          #feldo001@#3 = #4#0;
          #feldo001@#4 = #2#11;
          #feldo001@#5 = #2#12;
          |GRABA 020#feldo001@.a;
          |CIERRA #feldo001@;
          |DESCARGA_DEF #feldo001@;
     |SINO;
          |MENAV "0000Error al cargar 'feldo001'";
     |FINSI;
|FPRC;

|PROCESO Riesgo166;
     nMasRies = 0;
     eaCodCliente = #2#3;
     eaNomCliente = #2#4;
     eaSerie = #1#7; eaTag = "PROFO";  |HAZ SacaRiesgo;
     |SI #agifa142#166 = "S"; |Y eaExisteCliRie = "S";
          eaDocu = "C";
          eaTipo = "P";
          enImpoDoc = #2#41;
          eaMuestra = "S";
          |HAZ ControlRiesgo;
          |SI enErrRie ! 0;
               |ACABA;
          |FINSI;
          |SI enAudito = 0;
               |ABRE #143;
               |LEE 000#143u;
               |SI FSalida = 0;
                    nConAu = #143#0 + 1;
               |SINO;
                    nConAu = 1;
               |FINSI;
               |DDEFECTO #143;
               #143#0 = nConAu;
               #143#12 = "PEDIDO";
               #143#1 = #2#48;
               #143#2 = #2#0;
               #143#3 = #2#1;
               #143#4 = #2#3;
               #143#5 = #2#4;
               #143#6 = Usuario % 810;
               #143#7 = #141#0;
               #143#8 = #141#1;
               #143#9 = #141#2;
               #143#10 = #141#3;
               #143#11 = #141#4;
               |GRABA 020#143n;
               |CIERRA #143;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO MecaRiesgo;
/*
     eaSerie = #4#25; eaTag = "PE";  |HAZ SacaRiesgo;
     |SI nRieUtili ] nRieTotal;
          |DDEF aAlfa;
          aAlfa = aAlfa + "mecam005";
          |CARGA_DEF aAlfa, mecam005@;
          |SI FSalida = 0;
               |DDEFECTO #mecam005@;
               |ABRE #mecam005@;
               #mecam005@#0 = #4#25;
               #mecam005@#1 = #4#0;
               |LEE 101#mecam005@.=;
               |SI FSalida ! 0; |GRABA 020#mecam005@.b; |FINSI;

               aAlfa = #4#25 + (("000000" + #4#0) % -106);
               |SI #mecam005@#3 = "01.01.0000";
                    |SI #mecam005@#2 = "S"
                         aAlfa = "0000Pedido " + aAlfa + " denegado financieramente por limite de riesgo";
                         |MENAV aAlfa;
                         #mecam005@#2 = "N";
                         |GRABA 020#mecam005@.a;
                    |FINSI;
               |SINO;
                    aAlfa = "0000Pedido " + aAlfa + " aceptado financieramente por direccion con cambios que superan el riesgo";
                    |MENAV aAlfa;
               |FINSI;

               |CIERRA #mecam005@;
               |DESCARGA_DEF #mecam005@;
          |FINSI;
     |FINSI;
*/
|FPRC;

|PROCESO MecaGraAuxi;
     |DDEF aPath;

     aAlfa = aPath + "mecam018";
     |CARGA_DEF aAlfa, mecam018@;
     |SI FSalida ! 0; |ACABA; |FINSI;

     |DDEFECTO #mecam018@;
     |ABRE #mecam018@;
     #mecam018@#0 = #3#20;
     #mecam018@#1 = #3#0;
     #mecam018@#2 = #3#1;
     |LEE 000#mecam018@.=;
     |CIERRA #mecam018@;

     aAlfa = aPath + "mecam005";
     |CARGA_DEF aAlfa, mecam005@;
     |SI FSalida = 0;
          aAlfa = aPath + "mecam006";
          |CARGA_DEF aAlfa, mecam006@;
          |SI FSalida = 0;
               |ABRE #mecam005@;
               |ABRE #mecam006@;
               |DDEFECTO #mecam005@;
               #mecam005@#0 = #5#28;
               #mecam005@#1 = #5#0;
               |LEE 000#mecam005@.=;
               |SI FSalida ! 0; |GRABA 020#mecam005@.b; |FINSI;

               #mecam005@#2 = "S";
               #mecam005@#3 = ~;
               #mecam005@#4 = "Aceptacion de proforma";
               #mecam005@#5 = "S";
               #mecam005@#6 = ~;
               #mecam005@#7 = "Aceptacion de proforma";
               |GRABA 020#mecam005@.a;

               |DDEFECTO #mecam006@;
               #mecam006@#0 = #5#28;
               #mecam006@#1 = #5#0;
               #mecam006@#2 = #5#1;
               |LEE 101#mecam006@.=;
               |SI FSalida ! 0; |GRABA 020#mecam006@.b; |FINSI;
               #mecam006@#3 = #mecam018@#3;
               #mecam006@#4 = "S";
               #mecam006@#5 = ~;
               #mecam006@#6 = "S";
               #mecam006@#7 = ~;
               #mecam006@#11 = #mecam018@#4;
               #mecam006@#12 = #mecam018@#5;
               #mecam006@#13 = #mecam018@#6;
               fFechaEntrega = #mecam018@#7;

               |GRABA 020#mecam006@.a;
               |CIERRA #mecam006@;
               |CIERRA #mecam005@;
               |DESCARGA_DEF #mecam006@;
          |FINSI;
          |DESCARGA_DEF #mecam005@;
     |FINSI;
     |DESCARGA_DEF #mecam018@;
|FPRC;

|PROCESO MecaEscanLin;
     |DDEFECTO #mecam004@;
     #mecam004@#0 = #49#0;
     #mecam004@#1 = #49#1;
     |LEE 000#mecam004@.=;
     |SI #mecam004@#2 = "S"; || Film = S
          |SI #mecam006a@#3 = "N"; |ACABA; |FINSI;
          nFactor = 1;
          |SI #mecam006a@#3 = "A"; nFactor = 2; |FINSI;
     |SINO;
          nFactor = 1;
     |FINSI;

     |SI #5#44 ! 0;
          nUni = #49#12 * nFactor * #5#44;
     |SINO;
          nUni = #49#12 * nFactor * #5#5;
     |FINSI;

     #8#0 = #49#2;
     |LEE 101#8=;
     |SI FSalida = 0;
          #8#12 = #8#12 + nUni;
          #8#104 = #8#104 - nUni;

          #9#0 = #49#2;
          #9#1 = #49#9;
          |LEE 101#9=;
          |SI FSalida = 0;
               #9#8 = #9#8 + nUni;
               #9#39 = #9#39 - nUni;
               |SI #8#180 = "S";
                    |DDEFECTO #24;
                    #24#0 = #49#2;
                    #24#1 = #49#9;
                    #24#2 = "";
                    |LEE 101#24=;
                    |SI FSalida ! 0; |GRABA 020#24b; |FINSI;
                    #24#24 = #24#24 + nUni;
                    #24#22 = #24#22 - nUni;
                    |HAZ ValoraStock;
                    |GRABA 020#24a;
                    |LIBERA #24;
               |FINSI;
               |HAZ ValoraStock;
               |GRABA 020#9a;
               |LIBERA #9;
          |FINSI;
          |GRABA 020#8a;
          |LIBERA #8;
     |FINSI;
|FPRC;

|DEFBUCLE MecaEscanLin;
     #49#1;
     ;
     #0#2, 001;
     #0#2, 999;
     ;
     NULL, MecaEscanLin;
|FIN;

|PROCESO MecaReservaStock;
     |DDEF aPath;
     aAlfa = aPath + "mecam001";
     |CARGA_DEF aAlfa, mecam001@;
     |SI FSalida = 0;
          |ABRE #mecam001@;
          #mecam001@#0 = #5#2;
          |LEE 000#mecam001@.=;
          |CIERRA #mecam001@;
          aAlfa = #mecam001@#3;    ||Articulo Fabricacion
          |DESCARGA_DEF #mecam001@;
     |FINSI;
     |SI aAlfa = "N"; |ACABA; |FINSI;

     aAlfa = aPath + "mecam004";
     |CARGA_DEF aAlfa, mecam004@;
     |SI FSalida = 0;
          aAlfa = aPath + "mecam006";
          |CARGA_DEF aAlfa, mecam006a@;
          |SI FSalida = 0;
               |ABRE #mecam004@;
               |ABRE #mecam006a@;
               |DDEFECTO #mecam006a@;
               #mecam006a@#0 = #5#28;
               #mecam006a@#1 = #5#0;
               #mecam006a@#2 = #5#1;
               |LEE 101#mecam006a@.=;
               |SI FSalida ! 0; |GRABA 020#mecam006a@.b; |FINSI;

               |ABRE #8;
               |ABRE #9;
               |ABRE #24;
               |BUCLE MecaEscanLin;
               |CIERRA #24;
               |CIERRA #9;
               |CIERRA #8;

               |CIERRA #mecam006a@;
               |CIERRA #mecam004@;
               |DESCARGA_DEF #mecam006a@;
          |FINSI;
          |DESCARGA_DEF #mecam004@;
     |FINSI;
|FPRC;

|PROCESO MecaEscanLin2;
     |DDEFECTO #9;
     #9#0 = #49#2;
     #9#1 = #49#9;
     |LEE 000#9=;
     |SI #9#39 < 0;
          aFaltaStock = "S";
     |FINSI;
|FPRC;

|DEFBUCLE MecaEscanLin2;
     #49#1;
     ;
     #5#2, 001;
     #5#2, 999;
     ;
     NULL, MecaEscanLin2;
|FIN;

|PROCESO MecaAceptaPro;
/*
     |DDEF aAlfa;
     aAlfa = aAlfa + "mecam006";
     |CARGA_DEF aAlfa, mecam006a@;
     |SI FSalida = 0;
          |DDEFECTO #mecam006a@;
          |ABRE #mecam006a@;
          #mecam006a@#0 = #5#28;
          #mecam006a@#1 = #5#0;
          #mecam006a@#2 = #5#1;
          |LEE 101#mecam006a@.=;
          |SI FSalida ! 0; |GRABA 020#mecam006a@.b; |FINSI;
          aFaltaStock = "N";
          |ABRE #9;
          |BUCLE MecaEscanLin2;
          |CIERRA #9;
          |SI #mecam006a@#5 = "01.01.0000"; |Y aFaltaStock = "S";
               #mecam006a@#4 = "N";
               |GRABA 020#mecam006a@.a;
               aAlfa = #5#28 + "/" + (("000000" + #5#0) % -106);
               aAlfa =  "0000Pedido " + aAlfa + " denegado en produccion por falta de stock de:" + #5#2;
               |MENAV aAlfa;
          |FINSI;
          |GRABA 020#mecam006a@.a;
          |CIERRA #mecam006a@;
          |DESCARGA_DEF #mecam006a@;
     |FINSI;
*/
|FPRC;

|PROCESO MecaAltaPaquete;
     eaSerie = #5#28;
     enDocu = #5#0;
     enLinea = #5#1;
     eaArticulo = #5#2;
     enUnidades = #5#5;
     enMedida1 = #5#50;
     |SUB_EJECUTA_NP "mecam007;ALTA";
|FPRC;
----------------------------------------------------
|PROCESO TagPosFormados;
     |DDEF aAlfa;
     aAlfa = aAlfa + "tagm0056";
     |CARGA_DEF aAlfa, tagm0056@;
     |SI FSalida = 0;
          |ABRE #tagm0056@;

          #tagm0056@#0 = "PR";
          #tagm0056@#1 = #3#20;
          #tagm0056@#2 = #3#0;
          #tagm0056@#3 = #3#1;
          |LEE 000#tagm0056@.=;
          |SI FSalida = 0;
               #tagm0056@#0 = "PV";
               #tagm0056@#1 = #5#28;
               #tagm0056@#2 = #5#0;
               #tagm0056@#3 = #5#1;
               |GRABA 020#tagm0056@.n;
          |FINSI;

          |CIERRA #tagm0056@;
          |DESCARGA_DEF #tagm0056@;
     |FINSI;
|FPRC;
----------------------------------------------------

|PROCESO LineasKit;
     #946#0 = #5#28;          || serie pedido
     #946#1 = #5#0;           || nro. pedido
     #946#2 = #5#1;           || linea pedido
     #946#6 = #950#6;         || linea kit
     #946#3 = #950#3;         || articulo
     #946#4 = #950#4;         || almacen
     #946#5 = #950#5;         || unidades
     #946#7 = #950#7;         || observaciones
     #946#8 = #950#8;         || precio coste medio
     #946#9 = #950#9;
     #946#10 = #950#10;
     #946#11 = #950#11;
     #946#12 = #950#12;
     |GRABA 020#946n;
|FPRC;

|DEFBUCLE LineasKit;
     #950#1;
     ;
     #3#20, #3#0, #3#1, 001;
     #3#20, #3#0, #3#1, 999;
     ;
     NULL, LineasKit;
|FIN;

|PROCESO graba_cabped;
     |DDEFECTO #7;
     #7#0 = #2#3;
     |LEE 020#7=;

     #4#1 = #1#5; ||Fecha
     |SI nSwTagloma = 0;
           #4#3 = #2#91;
     |FINSI;
     #4#4  = #2#3;      ||  Cliente.
     #4#5  = #2#5;      ||  Dto.
     #4#6  = #2#7;      ||  Dto.P.P.
     #4#7  = #2#6;      ||  Representante 1.
     #4#8  = #2#4;      ||  Nombre Cliente.
     #4#9  = #2#8;      ||  Forma de pago.
     #4#12 = #4#11;     ||  Importe por servir.
     #4#14 = #2#30;     ||  Direccion Entrega.
     #4#16 = #2#34;     ||  Tipo de comision.
     #4#17 = #2#32;     ||  Dto.Acumulado.
     #4#18 = #2#35;     ||  Tipo de cliente.
     #4#19 = #2#29;     ||  Entregar a.
     #4#20 = #2#31;     ||  Poblacion Entrega.
     #4#21 = #2#33;     ||  Provincia Entrega.
     #4#22 = #2#36;     ||  Regimen de equivalencia S/N.
     #4#23 = #7#41;
     #4#15 = "PROFORMA: " + #2#48 + "/" + #2#0;

     |SI nSwMecanogra = 0; #4#15 = ""; |FINSI;

     #4#57 = #2#80;     || Dir Ent.
     #4#59 = #2#81;     || C.P

     #4#35 = #2#65;
     #4#36 = #2#66;
     #4#37 = #2#67;
     #4#40 = #2#68;
     #4#41 = #2#69;
     #4#43 = #4#42;
     #4#61 = #2#85;
     #4#64 = #2#86;
     #4#60 = #2#78;
     |HAZ visual_totales;
     |GRABA 020#4a;
     |LIBERA #4;

     |SI nSwTecatel = 0;
           eaSer = #2#48;
           enDocu = #2#0;
           eaTipo = "RE";
           eaSer1 = #4#25;
           enDocu1 = #4#0;
           |HAZ TecaTransPrPe;
     |FINSI;

     |SI nSwTagloma = 0;
           eaAlfa = #4#25;
           |HAZ TagloDefFP;
           |SI eaAlfa ! ""; #4#9 = eaAlfa; |FINSI;
           |SI eaQuitaDto = "S";
                #4#5 = 0;
                #4#17 = 0;
                #4#6 = 0;
           |FINSI;
     |FINSI;
     |SI nSwGestral = 0;
           eaAlfa = #4#25;
           |HAZ GestrDefFP;
           |SI eaAlfa ! ""; #4#9 = eaAlfa; |FINSI;
           |SI eaQuitaDto = "S";
                #4#5 = 0;
                #4#17 = 0;
                #4#6 = 0;
           |FINSI;
     |FINSI;
     |SI nSwChampiEmar = 0;
          |HAZ ChampiEmarPorte;
     |FINSI;
|FPRC;

|PROCESO GrabaCompoN;
     |SALVA_DATOS #905;

     #905#0 = "PV";
     #905#1 = #5#28;
     #905#2 = #5#0;
     #905#3 = #5#1;
     #905#9 = #905#6;
     |GRABA 020#905n;
     |LIBERA #905;

     eaTipo        = "PV";
     eaSerie       = #5#28;
     enCodigo      = #5#0;
     enLinea       = #5#1;
     eaArticulo    = #5#2;
     enAlmacen     = #5#3;
     eaComponente  = #905#4;
     enUnidades    = #5#5;
     enTBruto      = #5#7;
     eaAbono       = "N";
     enTarifa      = #7#39;
     enCamBas      = #4#36;
     eaMoneda      = #4#37;
     enCamDiv      = #4#41;

     |SUB_EJECUTA_NP "dsz00002;PVC";

     |REPON_DATOS #905;
     |LEE 040#905=;
|FPRC;

|DEFBUCLE dsm00005N;
     #905#1;
     ;
     "FP", #3#20, #3#0, nLinV, "      ";
     "FP", #3#20, #3#0, nLinV, "zzzzzz";
     ;
     NULL, GrabaCompoN;
|FIN;

|PROCESO dsm00053;
     #53#0 = "P";
     #53#1 = #5#28;
     #53#2 = #5#0;
     #53#3 = #5#1;
     |GRABA 020#53n;
|FPRC;

|DEFBUCLE dsm0053;
     #53#1;
     ;
     "X", #3#20, #3#0, #3#1, 0001;
     "X", #3#20, #3#0, #3#1, 9999;
     ;
     NULL, dsm00053;
|FIN;

|PROCESO graba_linped;
     #4#35 = #2#65;
     #4#36 = #2#66;
     #4#37 = #2#67;
     #4#40 = #2#68;
     #4#41 = #2#69;
     |HAZ LeeMonBase;
     |HAZ Param_Divisas;

     |DDEFECTO #5;
     #5#28 = #4#25;     ||  Serie.
     #5#0  = #4#0;
     #5#1  = #3#1;     ||  Numero de linea.
     #5#2  = #3#2;     ||  Codigo articulo.
     #5#3  = #3#3;     ||  Numero de almacen.
     #5#4  = #3#4;     ||  Descripcion articulo.
     #5#5  = #3#5;     ||  Unidades.
     #5#6  = #3#6;     ||  Precio.
     #5#7  = #3#7;     ||  Importe.
     #5#8  = #3#8;     ||  Dto.1 Linea.
     #5#9  = #3#9;     ||  Importe total
     #5#39 = #3#25;    ||  Importe total Divisas
     #5#10 = #3#10;    ||  % Comision.
     #5#11 = #5#9;     ||  Importe Pendiente.
     #5#14 = #3#11;    ||  Tipo de IVA.
     #5#16 = #2#6;     ||  Representante 1.
     #5#17 = #3#16;    ||  Tipo comision.
     #5#18 = #3#13;    ||  Familia.
     #5#19 = #3#17;    ||  Tipo cliente.
     #5#20 = #3#15;    ||  Cliente.
     #5#22 = #142#227;
     #5#23 = #2#5;     ||  Dto.
     #5#24 = #2#7;     ||  Dto.P.P.
     #5#25 = #2#32;    ||  Dto.Acumulado.
     #5#26 = #2#8;    ||  Forma de pago.
     #5#27 = #3#18;    ||  Ampliacion de descripcion.
     #5#29 = #3#21;    ||  % Dto. 2
     #5#35 = #3#22;
     #5#36 = #3#23;
     #5#38 = #3#24;  || PRECIO EN DIVISA
     #5#42 = #5#5;
     #5#44 = #3#28;
     #5#45 = #3#29;
     #5#46 = #3#30;
     #5#48 = #5#44;
     #5#50 = #3#31;
     #5#51 = #3#32;
     #5#52 = #3#33;
     #5#53 = #3#34;
     #5#54 = #3#35;
     #5#55 = #3#36;
     |HAZ prec;
     |HAZ visual_importe;
     |GRABA 020#5b;
     #4#11 = #4#11 + #5#9;  ||  Importe Total (Cabecera).
     #4#42 = #4#42 + #5#39; ||  Importe Total (Cabecera) Divisas.
     #4#63 = #4#63 + #5#54; ||  Punto Verde
     |BUCLE dsm0053;

     eaArticulo = #3#2;
     |HAZ EsKit;
     |SI FSalida = 0;
           |ABRE #946;
           |BUCLE LineasKit;
           |CIERRA #946;
     |FINSI;

     eaProgVta = "agifa104";
     enForma = 1;
     eaReservado = #5#22;
     |HAZ AcumulaPV;
     nLinV = #3#1;
     |BUCLE dsm00005N;

     |HAZ EsOrts;
     |SI FSalida = 0;
          eaSeriePedido = #agifa147#20;
          enCodigPedido = #agifa147#0;
          enLineaPedido = #agifa147#1;
          eaSerie = #agifa073#28;
          enCodigo = #agifa073#0;
          enLinea = #agifa073#1;
          |SUB_EJECUTA_NP "dsz99905;PROPED";
     |FINSI;

     |HAZ EsAlbadis; ||*
     |SI FSalida = 0;
           eaSerie = #3#20;
           enCodigo = #3#0;
           enLinea = #3#1;
           eaSeriePedido = #5#28;
           enCodigPedido = #5#0;
           enLineaPedido = #5#1;
           |HAZ BseProPed;
     |FINSI;

     |SI nSwTagloma = 0;
         eaTipAdj = "P";
         |HAZ TagAdjUniFami;
         |HAZ TagPosFormados;
     |FINSI;

     |SI nSwMecanogra = 0;
          |HAZ MecaGraAuxi;
          #5#13 = fFechaEntrega;
          |GRABA 020#5a;
          |HAZ MecaReservaStock;
          |HAZ MecaAceptaPro;
          |HAZ MecaAltaPaquete;
     |FINSI;

     |LIBERA #5;
|FPRC;

|PROCESO lee_contador;
     #4#25 = #1#7;       ||Serie
     enSwMenu = 1;
     |HAZ ContaPV7;
     enSwMenu = 0;
     contador = #4#0;
|FPRC;

|| ------------------------ PROCESOS DE ACTUALIZACION -----------------------
|PROCESO ActuaCli;
     |ABRE #8;
     #8#0 = #5#2;
     |LEE 000#8=;
     |CIERRA #8;

     |SI #8#194 = 2;
          nImpo = ((((#5#44 * #5#6) < #5#8) < #5#29) < #4#5) < #4#17;
     |SINO;
          nImpo = ((((#5#5 * #5#6) < #5#8) < #5#29) < #4#5) < #4#17;
     |FINSI;
     |SI #agifa142#115 = "S"; nImpo = nImpo < #4#6; |FINSI;

     |DDEFECTO #7;
     #7#0 = #4#4;
     |LEE 121#7=;
     |SI FSalida = 0;
         fmes = #4#1 % A402;
         mes = fmes - 1;
         mes = mes * 3;
         mes = mes + 114;
         #7mes  = #7mes  + nImpo;
         #7#150 = #7#150 + nImpo;
         |GRABA 020#7a;
     |FINSI;
     |LIBERA #7;

     eaCliente = #agifa104#4;
     efFecha = #agifa104#1;
     enImpCliPed = nImpo;
     |HAZ AcumulaCli;
|FPRC;

|PROCESO actua_cliente;
         |DDEFECTO #7;
         #7#0 = #4#4;
         |LEE 121#7=;
         |SI FSalida = 0;
             fmes = #4#1 % A402;
             mes = fmes - 1;
             mes = mes * 3;
             mes = mes + 114;
             #7mes  = #7mes  + #4#11;
             #7#150 = #7#150 + #4#11;
             |GRABA 020#7a;
         |FINSI;
         |LIBERA #7;

     eaCliente = #4#4;
     efFecha = #4#1;
     enImpCliPed = nImpo;
     |HAZ AcumulaCli;
|FPRC;

|PROCESO LeeMonBase; ||Lee los parametros de la moneda base
     |ABRE #agifa321;
     |ABRE #agifa324;
     |LEE 010#agifa321.p;   ||Leo parametros de divisa
     #agifa324#0 = #agifa321#9;
     |LEE 010#agifa324.=;         ||Leo la ficha de la moneda base en Divisas
     decim_base = #agifa324#7;    ||Asigno los decimales de la moneda base
     precio_base = #agifa324#9;   ||Asigno los decimales del precio en la mon. base
     |CIERRA #agifa324;
     |CIERRA #agifa321;
|FPRC;

|PROCESO Param_Divisas; |TIPO 0;
     |ABRE #agifa324;
     #agifa324#0 = #agifa104#35;
     |LEE 001#agifa324.=;   || Leo la divisa
     |CIERRA #agifa324;

     decim_divisa = #agifa324#7;  || Decimales de la divisa
     precio_divisa = #agifa324#9;  || Decimales en el precio de la divisa
     nDeci_Div    = decim_divisa;   || Decimales de la divisa
     nDeci_PreDiv = precio_divisa;  || Decimales en el precio de la divisa
     nDeci_DivP   = decim_divisa;   || Decimales de la divisa
     nDeci_PreDivP = precio_divisa;  || Decimales en el precio de la divisa
     |SI #agifa104#37 = "B";   ||Si trabajo con la moneda base ...
          nDeci_Base     = decim_base;
          nDeci_PreBase  = precio_base;
          nDeci_PreVis   = precio_divisa;
          nDeci_ImpVis   = decim_divisa;
          nDeci_BaseMx   = decim_base;
          nDeci_PreBasMx = precio_base;
          nDeci_TotVis   = decim_base;
          nDeci_TotNoVis = decim_divisa;
          nDeci_PreVisP   = precio_divisa;
          nDeci_ImpVisP   = decim_divisa;
          nDeci_BaseMxP  = decim_base;
          nDeci_PreBasMxP = precio_base;
          nDeci_TotVisP   = decim_base;
          nDeci_TotNoVisP = decim_divisa;
     |SINO;     ||Si trabajo con la divisa ...
          nDeci_Base     = decim_base; || Max. precision en los decimales de la moneda base
          nDeci_PreBase  = precio_base;  || Max. precision en los dec. del precio de la moneda base
          nDeci_PreVis   = precio_base;
          nDeci_ImpVis   = decim_base;
         |SI #agifa104#36 = 1;
              nDeci_BaseMx    = decim_base;    || sin triangulacion
              nDeci_PreBasMx  = precio_base;
         |SINO;
              nDeci_BaseMx    = #agifa321#10;  || con triangulacion
              nDeci_PreBasMx  = #agifa321#11;
         |FINSI;
          nDeci_TotVis   = decim_divisa;
          nDeci_TotNoVis = decim_base;
          nDeci_PreVisP  = precio_base;
          nDeci_ImpVisP  = decim_base;
         |SI #agifa104#36 = 1;
              nDeci_BaseMxP   = decim_base;    || sin triangulacion
              nDeci_PreBasMxP = precio_base;
         |SINO;
              nDeci_BaseMxP   = #agifa321#10;  || con triangulacion
              nDeci_PreBasMxP = #agifa321#11;
         |FINSI;
          nDeci_TotVisP   = decim_divisa;
          nDeci_TotNoVisP = decim_base;
     |FINSI;
     |HAZ Deci_IVA;
|FPRC;

|PROCESO prec;
|SI #4#37 = "D";  || Si la moneda de trabajo es la divisa ...
  #5#6 = #5#38 / #4#36 * #4#41;  || ... recalculo en funcion de la divisa
|SINO;
  #5#38 = #5#6 / #4#41 * #4#36; || .. recalculo en funcion de la moneda base
|FINSI;
|HAZ visual_precio; || Visualiza el precio
|FPRC;


|PROCESO visual_precio; || Pinta el precio visual.
|SI #4#37 = "D";  || Si la moneda de trabajo es la divisa ...
  #5#40 = #5#6;   || ... el campo visualiz. es el precio en moneda base
|SINO;            || Si la moneda de trabajo es la moneda base ...
  #5#40 = #5#38;  || ... el campo visual. es el precio en divisa
|FINSI;
|FPRC;

|PROCESO visual_importe;  || Pinta el importe visual
|SI #4#37 = "D";  || Si la moneda de trabajo es la divisa ...
  #5#41 = #5#9;   || el campo visualiz. es el importe en moneda base
|SINO;            || Si la moneda de trabajo es la moneda base ...
  #5#41 = #5#39;  || el campo visualiz. es el importe en divisa
|FINSI;
|FPRC;

|PROCESO visual_totales;
   |SI #4#37 = "D";
     #4#45 = #4#42;
     #4#46 = #4#43;
     #4#47 = #4#44;
     #4#48 = #4#11;
     #4#49 = #4#12;
     #4#50 = #4#13;
   |SINO;
     #4#45 = #4#11;
     #4#46 = #4#12;
     #4#47 = #4#13;
     #4#48 = #4#42;
     #4#49 = #4#43;
     #4#50 = #4#44;
   |FINSI;
|FPRC;

|PROCESO CalcConIva;
     |ABRE #dsm00113;
     #dsm00113#0 = #agifa104#4;
     #dsm00113#1 = #agifa073#14;
     |LEE 000#dsm00113.=;
     |SI FSalida = 0;
        nCalc = #agifa073#11 % #dsm00113#3;
        |SI #agifa024#47 = "S";
           nCalc = nCalc + (#agifa073#11 % #dsm00113#4);
        |FINSI;
     |SINO;
        enTiva =  #agifa073#14;
        efFiva = #agifa104#1;
        |HAZ SacaIVA;

        nCalc = #agifa073#11 % enIva;
        |SI #agifa024#47 = "S";
              nCalc = nCalc + (#agifa073#11 % enRec);
        |FINSI;
     |FINSI;
     |CIERRA #dsm00113;

     nImpo = nImpo + (#agifa073#11 + nCalc);
|FPRC;

|DEFBUCLE CalcConIva;
     #agifa073#1;
     ;
     #agifa104#25, #agifa104#0,001;
     #agifa104#25, #agifa104#0,999;
     ;
     NULL,CalcConIva;
|FIN;

|PROCESO Contador;
     |HAZ EsMecanogra;
     |SI FSalida = 0;
          |DDEFECTO #4;
          #4#25 = #2#48;
          #4#0 = #2#0;

          #0#2 = #2#48;
          #0#3 = #2#0;
          |ACABA;
     |FINSI;

     |HAZ lee_contador;
     #0#2 = #1#7;
     #0#3 = contador;
     |DDEFECTO #4;
     #4#25 = #1#7;       ||Serie
     #4#0 = contador;
|FPRC;

|PROCESO factura;
     nMasRies = 0;
     #4#4 = #1#1;
     #4#8 = #1#4;
     eaSerie = #1#7; eaTag = "PE";  |HAZ SacaRiesgo;
     |SI aRieBloqu = "S";
         |HAZ BloqCart;
         |ERROR; |ACABA;
     |FINSI;

     #0#0 = #1#2;
     #0#1 = #1#3;
     |DDEFECTO #2;
     #2#48 = #1#2;  ||  Serie.
     #2#0  = #1#3;  ||  Factura.
     |LEE 101#2=;
     |SI FSalida = 0;
          |HAZ Riesgo166;
          |SI enErrRie = 0;
               |HAZ Contador;
               |GRABA 040#4b;
               |SI FSalida ! 0;
                    aAlfa = "0000Ya existe el pedido: "+#4#25+"/"+(("000000"+#4#0)%-106);
                    |MENAV aAlfa; |ACABA;
               |FINSI;

               |BUCLELIN 0graba_linped#2;
               |HAZ graba_cabped;
               |HAZ CalCabAgifa104;
               ||HAZ actua_cliente;
               |BUCLELIN 0ActuaCli#2;

               #2#60 = #4#25;  ||  Serie.
               #2#61 = ("000000" + #4#0) % -106;   ||  Factura.
               #2#62 = "P";    ||  Tipo de documento asignado (Pedido).
               |GRABA 020#2a;

               |HAZ EsFeldo;
               |SI FSalida = 0; |Y #2#11 ! 0;
                    |HAZ FeldoPortes;
               |FINSI;

               nImpo = 0; |BUCLE CalcConIva;

               |SI nSwMecanogra = 0;
                    |HAZ MecaRiesgo;
               |FINSI;

               eaTag = "PE";  |HAZ Riesgos;
               |PINDA #0#0;
          |FINSI;
     |SINO;
          #0#2 = #1#7;
          #0#3 = contador;
          |PINDA #0#0;
          |PAUSA;
     |FINSI;
     |LIBERA #2;
|FPRC;

|DEFBUCLE lin_tmp;
     #1#1;
     8;
     001,"*";
     999,"*";
     e;
     NULL,factura;
|FIN;

|PROCESO PasaTagloma;
     #2#48 = eaSerie;
     #2#0 = enCodigo;
     |LEE 121#2=;
     |SI FSalida = 0;
          |C_V #0#0, 1, "N";
          |C_V #0#1, 1, "N";
          |C_V #0#2, 1, "N";
          |C_V #0#3, 1, "N";
          #1#1 = #2#3;
          #1#2 = #2#48;
          #1#3 = #2#0;
          #1#4 = #2#1;
          #1#5 = ~;
          #1#6 = #2#73;
          #1#7 = #142#186;
          |HAZ factura;

          #2#83 = "S";
          |GRABA 020#2a;
          |LIBERA #2;

          |DDEF aPath;
          aAlfa = aPath + "tagm0091";
          |CARGA_DEF aAlfa, referen@;
          |SI FSalida = 0;
               |ABRE #referen@;
               #referen@#0 = #2#48;
               #referen@#1 = #2#0;
               |LEE 000#referen@.=;
               |CIERRA #referen@;
               aSeccion = #referen@#2;
               |DESCARGA_DEF #referen@;
          |FINSI;

          aAlfa = aPath + "tagm0079";
          |CARGA_DEF aAlfa, referen@;
          |SI FSalida = 0;
               |ABRE #referen@;
               #referen@#0 = #4#25;
               #referen@#1 = #4#0;
               |LEE 000#referen@.=;
               |SI FSalida ! 0; |GRABA 020#referen@.b; |FINSI;
               #referen@#3 = aSeccion;
               |GRABA 020#referen@.a;
               |CIERRA #referen@;
               |DESCARGA_DEF #referen@;
          |FINSI;

          num_alb = 0;
          fec_alb = #4#1;
          eaSerPedido = #4#25;
          enNumPedido = #4#0;
/*
          |HAZ TagloFacPed;
*/
          |CONFI "0000N¿Asignar procesos al pedido?";
          |SI FSalida = 0;
               ser_ped = #4#25;
               num_ped = #4#0;
               |SUB_EJECUTA_NP "tagz0018;N";
          |FINSI;

          |CONFI "2400NDesea imprimir el pedido? ";
          |SI FSalida = 0;
               ser_ped = #4#25;
               num_ped = #4#0;
               |SUB_EJECUTA_NP "agifa105.run";
          |FINSI;
     |FINSI;
|FPRC;

|PROGRAMA;
     aParam = PARAMETRO;

     |HAZ EsTecatel; nSwTecatel = FSalida;
     |HAZ EsTagloma; nSwTagloma = FSalida;
     |HAZ EsMecanogra; nSwMecanogra = FSalida;
     |HAZ EsGestral; nSwGestral = FSalida;
     |HAZ EsChampiEmar; nSwChampiEmar = FSalida;

     |ABRE #142; |LEE 000#142p; |CIERRA #142;

     |ABRE #2;
     |ABRE #4;
     |ABRET #5;
     |ABRE #7;
     |ABRET #8;
     |ABRE #9;
     |SI aParam = "TAGLOMA";
          |HAZ PasaTagloma;
     |SINO;
          |PINPA #0#0; |PINTA 1446,"Pedido Clientes";

          aFichero = PARAMETRO; |QBF aFichero;
          |NOME_DAT #1 aFichero;
          |BUCLE lin_tmp;
          |DELINDEX #1;
     |FINSI;
     |CIERRA #2;
     |CIERRA #4;
     |CIERRAT #5;
     |CIERRA #7;
     |CIERRAT #8;
     |CIERRA #9;
|FPRO;
