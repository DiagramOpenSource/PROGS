     CALCULO COMISIONES

|FICHEROS;
     agifa019 #19;
     agifa024 #24;
     agifa142 #142;
     agifa128 #128;
     agifa129 #129,MANTE;
     dsm00022 #922;
     agifa023 #23;
     agifa290 #290;
     dsm00123 #123;
|FIN;

|VARIABLES;
     nSwDir         = 0;
     &enDirEnt      = 0;
     &enUniMarhu    = 0;
     &enDescuento1  = 0;
     &enDescuento2  = 0;
     &enComision    = 0;
     &eaRepre       = "";
     &eaCliente     = "";
     &eaArticulo    = "";
     &enAlmacen = 0;
     &eaTComision   = "";
     &efFecha       = @;
     &uni_dt        = 0;

     nSwError       = 0;

      aCero         = "";
      nCampo        = 0;
      nRepre        = 0;
      nPasada       = 0;
      nPrior        = 0;
      nComision1    = 0;
      nComision2    = 0;
      nComision3    = 0;
      nComision4    = 0;
      nComision5    = 0;
      nDescuento    = 0;
      nCCom         = 0;
      nCDto         = 0;
      nDescu        = 0;
      nMenor        = 0;
      nPrimera      = 0;
      aAlfa         = "";
      pa            = 0;
      pa1           = 0;
|FIN;

|PROCESO PorPromo;
     |ABRE #290;
     #290#0 = #19#0;
     #290#41 = #24#158
     |LEE 000#290=;
     |SI FSalida = 0;
          pa1 = 41;
          |PARA pa = 1;|SI pa [ 15;|HACIENDO pa = pa + 2;
               pa1 = pa1 + 1;
               |SI #290pa ! 0;
                     |SI uni_dt ] #290pa;
                         |SI #290pa1 = 0;
                             |SI aCero = "S";  nComision5 = #290pa1; |FINSI;
                         |SINO;
                             nComision5 = #290pa1;
                         |FINSI;
                     |FINSI;
               |SINO;
                     |SAL;
               |FINSI;
          |SIGUE;
     |FINSI;
     |CIERRA #290;
|FPRC;

|PROCESO PorTCliente;
     |ABRE #128;
     #128#0 = #24#4;
     |LEE 040#128=;
     |SI FSalida ! 0;  |CIERRA #128;  |ACABA; |FINSI;
     |CIERRA #128;

     |SI #128#1 = 0;
         |SI aCero = "S";  nComision1 = #128#1;  |FINSI;
     |SINO;
         nComision1 = #128#1;
     |FINSI;
|FPRC;

|PROCESO PorDtos;
     |ABRE #129;
     #129#0 = eaTComision;
     |LEE 000#129=;
     |SI FSalida ! 0;  |CIERRA #129;  |ACABA;  |FINSI;
     |CIERRA #129;

     |SI #129#41 = 1;  nDescuento = enDescuento1;  |FINSI;
     |SI #129#41 = 2;  nDescuento = enDescuento2;  |FINSI;
     |SI #129#41 = 3;
         nDescuento = enDescuento1;
         |SI nDescuento < enDescuento2;  nDescuento = enDescuento2;  |FINSI;
     |FINSI;
     |SI #129#41 = 4;
         nDescuento = enDescuento1;
         |SI nDescuento > enDescuento2;  nDescuento = enDescuento2;  |FINSI;
     |FINSI;
     |SI #129#41 = 5;  nDescuento = enDescuento1 + enDescuento2;     |FINSI;
     nMenor = 0;
     |PARA nCDto = 1;  |SI nCDto [ 39;|HACIENDO nCDto = nCDto + 2;
           nCCom = nCDto + 1;
           |SI nDescuento ] nMenor; |Y nDescuento [ #129nCDto;
               nComision2 = #129nCCom;
               |SAL;
           |FINSI;
           nMenor = #129nCDto;
     |SIGUE;
     |SI nComision2 = 0;
         |SI aCero = "N";  nComision2 = -1;  |FINSI;
     |FINSI;
|FPRC;

|PROCESO CogeGrupo;
     |SI #19#2   < #922#4;   |ACABA;  |FINSI;
     |SI #19#2   > #922#5;   |ACABA;  |FINSI;
     |SI #19#3   < #922#6;   |ACABA;  |FINSI;
     |SI #19#3   > #922#7;   |ACABA;  |FINSI;
     |SI #19#0   < #922#8;   |ACABA;  |FINSI;
     |SI #19#0   > #922#9;   |ACABA;  |FINSI;
     |SI #19#125 < #922#35;  |ACABA;  |FINSI;
     |SI #19#125 > #922#36;  |ACABA;  |FINSI;

     |SI #922#10 = "N";
         |SI eaRepre < #922#11;  |ACABA;  |FINSI;
         |SI eaRepre > #922#12;  |ACABA;  |FINSI;
     |SINO;
         nRepre = 0;
         |PARA nCampo = 13;  |SI nCampo [ 34;  |HACIENDO nCampo = nCampo + 1;
               |SI #922nCampo = eaRepre;  nRepre = 1;  |SAL;  |FINSI;
         |SIGUE;
         |SI nRepre = 0;  |ACABA;  |FINSI;
     |FINSI;

     |SI #922#3 = 0;
         |SI aCero = "S";  nComision3 = #922#3;  |FINSI;
     |SINO;
         nComision3 = #922#3;
     |FINSI;
|FPRC;

|DEFBUCLE dsm00022;
     #922#1;
     ;
     #24#158, 001;
     #24#158, 999;
     ;
     NULL, CogeGrupo;
|FIN;

|PROCESO PorGrupo;
     |BUCLE dsm00022;
|FPRC;

|PROCESO PorArticulo;
     |ABRE #23;
     #23#0 = eaCliente;
     #23#1 = eaArticulo;
     |LEE 000#23=;
     |SI FSalida = 0;
          |SI efFecha ] #23#28; |Y efFecha [ #23#29;
               |SI #23#34 = 0;
                    |SI aCero = "S";
                         #19#31 = #23#34;
                    |FINSI;
               |SINO;
                    #19#31 = #23#34;
               |FINSI;
          |FINSI;
     |FINSI;
     |CIERRA #23;

     |SI #19#31 = 0;
          |SI aCero = "S";
               nComision4 = #19#31;
          |FINSI;
     |SINO;
          nComision4 = #19#31;
     |FINSI;
|FPRC;

|PROCESO PorDirArticulo;
     nSwDir = 0;

     |ABRE #123;
     #123#0 = eaCliente;
     #123#1 = enDirEnt;
     #123#2 = eaArticulo;
     |LEE 000#123=;
     |SI FSalida = 0;
          |SI efFecha ] #123#7; |Y efFecha [ #123#8;
               |SI #123#5 = 0;
                    |SI aCero = "S";
                         #19#31 = #123#5;
                         nSwDir = 1;
                    |FINSI;
               |SINO;
                    #19#31 = #123#5;
                    nSwDir = 1;
               |FINSI;
          |FINSI;
     |FINSI;
     |CIERRA #123;

     |SI nSwDir = 0;
          |HAZ PorArticulo;
     |SINO;
          |SI #19#31 = 0;
               |SI aCero = "S";
                    nComision4 = #19#31;
               |FINSI;
          |SINO;
               nComision4 = #19#31;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Comisiones;
     |ABRE #142; |LEE 000#142p; |CIERRA #142;

     |ABRE #19;
     #19#0 = eaArticulo;
     |LEE 040#19=;
     |SI FSalida ! 0;  |DDEFECTO #19;  |FINSI;
     |CIERRA #19;

     |ABRE #24;
     #24#0 = eaCliente;
     |LEE 040#24=;
     |SI FSalida ! 0;  |DDEFECTO #24;  |FINSI;
     |CIERRA #24;

     enComision = 0;
     nComision1 = -1;
     nComision2 = -1;
     nComision3 = -1;
     nComision4 = -1;
     nComision5 = -1;

     |SI #142#105 ! 0; aCero = #142#106; |HAZ PorTCliente;  |FINSI;
     |SI #142#107 ! 0; aCero = #142#108; |HAZ PorDtos;      |FINSI;
     |SI #142#109 ! 0; aCero = #142#110; |HAZ PorGrupo;     |FINSI;
     |SI #142#111 ! 0; aCero = #142#112; |HAZ PorDirArticulo;  |FINSI;
     |SI #142#180 ! 0; aCero = #142#181; |HAZ PorPromo;     |FINSI;

     |SI #142#113 = 1;
         |PARA nPasada = 5; |SI nPasada ] 1; |HACIENDO nPasada = nPasada - 1;
               |PARA nPrior = 105; |SI nPrior [ 111; |HACIENDO nPrior = nPrior + 2;
                     |SI #142nPrior = nPasada;
                         |SI nPrior = 105;
                             |SI nComision1 ! -1;  enComision = nComision1;  |FINSI;
                         |FINSI;
                         |SI nPrior = 107;
                             |SI nComision2 ! -1;  enComision = nComision2;  |FINSI;
                         |FINSI;
                         |SI nPrior = 109;
                             |SI nComision3 ! -1;  enComision = nComision3;  |FINSI;
                         |FINSI;
                         |SI nPrior = 111;
                             |SI nComision4 ! -1;  enComision = nComision4;  |FINSI;
                         |FINSI;
                    |FINSI;
                    |SI #142#180 = nPasada;
                         |SI nComision5 ! -1;  enComision = nComision5;  |FINSI;
                    |FINSI;
               |SIGUE;
          |SIGUE;
     |SINO;
          nMenor = -1;
          |SI nComision1 ! -1;  nMenor = nComision1;  |FINSI;
          |SI nComision2 ! -1;
              |SI nMenor = -1;
                  nMenor = nComision2;
              |SINO;
                  |SI nMenor > nComision2;  nMenor = nComision2;  |FINSI;
              |FINSI;
          |FINSI;
          |SI nComision3 ! -1;
              |SI nMenor = -1;
                  nMenor = nComision3;
              |SINO;
                  |SI nMenor > nComision3;  nMenor = nComision3;  |FINSI;
              |FINSI;
          |FINSI;
          |SI nComision4 ! -1;
              |SI nMenor = -1;
                  nMenor = nComision4;
              |SINO;
                  |SI nMenor > nComision4;  nMenor = nComision4;  |FINSI;
              |FINSI;
          |FINSI;
          |SI nComision5 ! -1;
              |SI nMenor = -1;
                  nMenor = nComision5;
              |SINO;
                  |SI nMenor > nComision5;  nMenor = nComision5;  |FINSI;
              |FINSI;
          |FINSI;

          |SI nMenor = -1;
              enComision = 0;
          |SINO;
              enComision = nMenor;
          |FINSI;
     |FINSI;
|FPRC;
