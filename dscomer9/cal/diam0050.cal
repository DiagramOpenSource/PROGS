|FICHEROS;
     diam0050;

     diaw0029,MANTE;
|FIN;

|VARIABLES;
     n0Vtn            = -1;
     n1Vtn            = -1;
     nBtnPres         = -1;
     nBtnBorr         = -1;
     nBtnNuev         = -1;
     nBtnModi         = -1;
     nRango           = 0;
     nGrid            = 0;
     nLinea           = 0;
     nSubPanta        = 0;
     nHand            = 0;
     nError           = 0;
     nLong            = 0;
     nCarac           = 0;
     nPos             = 0;

     aTecla           = "";
     aAlfa            = "";
     aEsc1            = "";
     aEsc2            = "";
     aRetu            = "";
     aExt             = "";
     aHora            = "";
     aBase            = "";
     aPathSer         = "";
     aPathLoc         = "";
     aPathPlu         = "";
     aFic             = "";
     aRuta            = "";
     aProg            = "";
     aTxt             = "";
     aIni             = "";
     aLong            = "";
     aCarac           = "";
     aOri             = "";
     aDes             = "";

     {-1}aVent        = "";
         aVent1       = 0;
         aVent2       = 0;
         aVent3       = 0;
         aVent4       = 0;
         aVent5       = "";

     &enError         = 0;
     &eaDef           = "";
     &eaClv           = "";
     &eaUnidad        = "";
     &eaParametro1    = "";
     &eaParametro2    = "";
     &eaParametro3    = "";
     &eaParametro4    = "";
     &eaParametro5    = "";
|FIN;

|PROCESO Editar;
     |VENTANA 0501, 2999, n0Vtn, 16, "Presentando documento";
     n1Vtn = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n1Vtn;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |PTEC 802;  |PAUSA;
     |PTEC 802;  |PAUSA;

     aFic  = #diam0050#0;
     aFic  = aFic + (("0000" + #diam0050#2) % -104);
     aFic  = aFic + #diam0050#1;                    |QBF aFic;
     aFic  = aFic + "." + #diam0050#10;             |QBF aFic;

     aOri = aPathSer + aFic;
     aDes = aPathLoc + aFic;
     |ENVIA_FICHERO aOri, aDes;

     eaParametro1 = aPathLoc + "dsabreextension";
     eaParametro2 = aPathLoc + aFic;
     eaParametro3 = aPathPlu;
     eaParametro4 = aPathLoc;
     eaParametro5 = aPathLoc;

     |HAZ EjecutaDSAbreExtension;

     aOri = aPathLoc + aFic;
     aDes = aPathSer + aFic;
     |RECIBE_FICHERO aOri, aDes;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n1Vtn;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA n1Vtn;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n0Vtn;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;
|FPRC;

|PROCESO Borrar;
     |CONFI "0000NSeguro que quiere BORRAR el Documento.";
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |LEE 101#diam0050.=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     aFic  = #diam0050#0;
     aFic  = aFic + (("0000" + #diam0050#2) % -104);
     aFic  = aFic + #diam0050#1;                    |QBF aFic;
     aFic  = aFic + "." + #diam0050#10;             |QBF aFic;

     |BORRA 020#diam0050.a;
     |LIBERA #diam0050;

     aAlfa = aPathSer + aFic;
     |FBORRA aAlfa;

     |REFRESCACONTROL nGrid;  |PULSATECLA;
     |FOCOTECLADO nGrid;
|FPRC;

|PROCESO Modificar;
     |LEE 101#diam0050.=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |VENTANA 0501, 1599, n0Vtn, 16, "Modificaci¢n";
     n1Vtn = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n1Vtn;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |PINPA #0#diaw0029;  nSubPanta = FSalida;

     #diaw0029#0 = #diam0050#3;
     #diaw0029#1 = #diam0050#2;

     |PINDA #0#diaw0029;

     |ENDATOS #1#diaw0029;
     |SI FSalida = 0;
         #diam0050#3  = #diaw0029#0;
         |GRABA 020#diam0050.a;
     |FINSI;
     |LIBERA #diam0050;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n1Vtn;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA n1Vtn;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n0Vtn;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |REFRESCACONTROL nGrid;  |PULSATECLA;
     |FOCOTECLADO nGrid;
|FPRC;

|PROCESO Adjuntar;
     aRuta = "";
     |HAZ Traete_dsselect;
     |SI enError = 1;
         |MENAV "0000No puedo instala el dsselect.exe, consulte con su administrador";
     |FINSI;

     aProg = aPathLoc + "dsselect.exe";
     aTxt  = aPathLoc + "dsselect.txt";
     aIni  = aPathLoc + "dsselect.ini";

     |RFBORRA aTxt;
     |RFBORRA aIni;

     aAlfa = &34 + aPathLoc + &34 + " " + &34 + aTxt + &34;
     |XABRE "CW", aIni, nHand;
     |SI FSalida ] 0;
          |XGRABA nHand, aAlfa;
     |FINSI;
     |XCIERRA nHand;

     |RSYSTEM aProg;

     |XABRE "CU", aTxt, nHand;
     |SI FSalida < 0;
        aRuta  = "";
        nError = 1;

        |ACABA;
     |FINSI;

     |XLEE nHand, 250, aRuta;
     |SI FSalida < 0
         |XCIERRA nHand;
         aRuta  = "";
         nError = 1;
         |ACABA;
     |FINSI;
     |XCIERRA nHand;

     |QBF aRuta;
     |SI aRuta ! "";
         aAlfa    = aRuta;
         aLong    = aAlfa % 0;
         nLong    = aLong;
         aExt     = "";
         aRuta    = "";
         |PARA nCarac = 1;  |SI nCarac [ nLong;  |HACIENDO nCarac = nCarac + 1;
               nPos   = (nCarac * 100) + 1;
               aCarac =  aAlfa % nPos;

               |SI aCarac = ".";
                   aExt = "";
               |SINO;
                   aExt = aExt + aCarac;
               |FINSI;

               aRuta = aRuta + aCarac;
         |SIGUE;
     |FINSI;

     |RFBORRA aTxt;

     aExt = aExt < "";
|FPRC;

|PROCESO Nuevo;
     nLinea = 1;

     #diam0050#0 = eaDef;
     #diam0050#1 = eaClv;
     #diam0050#2 = 9999;
     |LEE 000#diam0050.];
     |SI FSalida = 0;
         |LEE 000#diam0050.a;
     |SINO;
         |LEE 000#diam0050.u;
     |FINSI;

     |SI FSalida = 0;  |Y #diam0050#0 = eaDef;
                       |Y #diam0050#1 = eaClv;
          nLinea = #diam0050#2 + 1;
     |FINSI;

     |HORA aHora;

     |DDEFECTO #diam0050;
     #diam0050#0  = eaDef;
     #diam0050#1  = eaClv;
     #diam0050#2  = nLinea;
     #diam0050#3  = "";
     #diam0050#4  = ~;
     #diam0050#5  = aHora;
     #diam0050#6  = Usuario % 810;
     #diam0050#7  = ~;
     #diam0050#8  = aHora;
     #diam0050#9  = Usuario % 810;
     #diam0050#10 = "";
     |GRABA 020#diam0050.b;

     |VENTANA 0501, 1599, n0Vtn, 16, "Nuevo documento";
     n1Vtn = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n1Vtn;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |DDEFECTO #diaw0029;
     |PINPA #0#diaw0029;  nSubPanta = FSalida;

     |CONTROL_SIMPLE nSubPanta, "BOTON,{{197}}Adjuntar ", 1142, 1355, Adjuntar;

     #diaw0029#1 = nLinea;

     |PINDA #0#diaw0029;

     |ENDATOS #1#diaw0029;
     |SI FSalida = 0;
         #diam0050#3  = #diaw0029#0;
         #diam0050#10 = aExt;
     |SINO;
         aRuta = "";
     |FINSI;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n1Vtn;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA n1Vtn;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n0Vtn;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |SI aRuta = "";
         |MENAV "0000Proceso cancelado. No se ha adjuntado documento.";

         |BORRA 020#diam0050.a;
         |LIBERA #diam0050;
         |ACABA;
     |FINSI;

     aFic  = #diam0050#0;
     aFic  = aFic + (("0000" + #diam0050#2) % -104);
     aFic  = aFic + #diam0050#1;  |QBF aFic;

     aOri = aRuta;
     aDes = aPathSer + aFic + "." + aExt;
     |RECIBE_FICHERO aOri, aDes;

     |GRABA 020#diam0050.a;
     |LIBERA #diam0050;

     |REFRESCACONTROL nGrid;  |PULSATECLA;
     |FOCOTECLADO nGrid;
|FPRC;

|PROCESO Evento;
     |SI FSalida = 1;  |O FSalida = 2;
         |LEE 000#diam0050.=;

         |SI nBtnPres ! -1;  |ESTADO_CONTROL nBtnPres, "ENABLE";  |FINSI;
         |SI nBtnBorr ! -1;  |ESTADO_CONTROL nBtnBorr, "ENABLE";  |FINSI;
         |SI nBtnModi ! -1;  |ESTADO_CONTROL nBtnModi, "ENABLE";  |FINSI;
     |FINSI;

     |SI FSalida = 4;
         |HAZ Editar;
     |FINSI;
|FPRC;

|PROCESO CreaBotones;
     |CONTROL_SIMPLE 0, "BOTON,{{217}}Presentar Documento", 2605, 2717, Editar;
     nBtnPres = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,{{199}}Borrar Documento", 2619, 2732, Borrar;
     nBtnBorr = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,{{197}}Modificar Descripci¢n", 2634, 2750, Modificar;
     nBtnModi = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,{{193}}Nuevo Documento", 2652, 2766, Nuevo;
     nBtnNuev = FSalida;

     |SI nBtnPres ! -1;  |ESTADO_CONTROL nBtnPres, "DISABLE";  |FINSI;
     |SI nBtnBorr ! -1;  |ESTADO_CONTROL nBtnBorr, "DISABLE";  |FINSI;
     |SI nBtnBorr ! -1;  |ESTADO_CONTROL nBtnModi, "DISABLE";  |FINSI;
|FPRC;

|PROCESO DestruyeControles;
     |DESTRUYECONTROL nGrid;

     |SI nBtnPres ! -1;  |FIN_CONTROL_WINDOWS nBtnPres;  |FINSI;
     |SI nBtnBorr ! -1;  |FIN_CONTROL_WINDOWS nBtnBorr;  |FINSI;
     |SI nBtnNuev ! -1;  |FIN_CONTROL_WINDOWS nBtnNuev;  |FINSI;
     |SI nBtnNuev ! -1;  |FIN_CONTROL_WINDOWS nBtnModi;  |FINSI;

     |PULSATECLA;
|FPRC;

|PROCESO Baja;
     #diam0050#0 = eaDef;
     #diam0050#1 = eaClv;
     #diam0050#2 = 1;
|FPRC;

|PROCESO Alta;
     #diam0050#0 = eaDef;
     #diam0050#1 = eaClv;
     #diam0050#2 = 9999;
|FPRC;

|PROGRAMA;
     aAlfa = "Documentos";
     |VENTANA 0503, 2997, -1, 16, aAlfa;
     n0Vtn = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n0Vtn;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     nRango = 4 + 8 + 16 + 32 + 128;

     |LINEAL_SIMPLE #1#diam0050, nRango, 0503, 2597, Baja, Alta, Evento;
     nGrid = FSalida;

     |HAZ CreaBotones;

     aEsc1  = &255 + "806";
     aEsc2  = &255 + "807";
     aRetu  = &255 + "802";
     |PARA; |SI;  |HACIENDO;
         FSalida = "::{{001}}Salir," + nGrid;
         |LEETECLA aTecla;
         |SI aTecla = aEsc1; |SAL;  |FINSI;
         |SI aTecla = aEsc2; |SAL;  |FINSI;
         |SI aTecla = aRetu; |SAL;  |FINSI;
     |SIGUE;

     |HAZ DestruyeControles;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n0Vtn;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA n0Vtn;
|FPRO;

|PROCESO T80;  |TIPO 80;
     |DBASE aBase;  |QBF aBase;
     aPathSer = aBase + "000577";               |MKDIR aPathSer;
     aPathSer = aBase + "000577/documentos";    |MKDIR aPathSer;
     aPathSer = aBase + "000577/documentos/";

     aPathPlu = aBase + "plugins/";

     |HAZ DimeUnidad;
     aPathLoc = eaUnidad  + ":\DOCUMENTOS";     |RMKDIR aPathLoc;
     aPathLoc = aPathLoc + "\DSCOMER9";         |RMKDIR aPathLoc;
     aPathLoc = aPathLoc + "\";

     |ABRE #diam0050;
|FPRC;

|PROCESO T90;  |TIPO 90;
     |CIERRA #diam0050;
|FPRC;
