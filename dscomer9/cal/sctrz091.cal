     Ver *** la acotacion para cargar las reservas, no se si es correcta

|FICHEROS;
     sctrz091 #0;
     sctrw029 #29;  ||Tmp
     sctrm046 #46;
     sctrm047 #47;
     sctrm050 #50;
     sctrm052 #52;
     sctrm053 #53;
     sctrm001 #100;
|FIN;

|VARIABLES;
     &eaMensaje = "";
     &Tempo = "";
     &enPantaLogo = 0;
     &eaNomLogo = "";
     &enCtrFin = 0;
     nRango = 0;
     nGrid = 0;
     nPanta = 0;
     aTmp29 = "";
     aColor = "";
     aMarca = "";
     aAlfa = "";
     nSwPtec = 0;
     nSigue = 0;
     nConta = 0;
     nBtnMarca = 0;
     nBtnFPago = 0;
     nBtnActualiza = 0;
|FIN;

|PROCESO ActReserva;
     |SI #29#5 = "*";
          #52#0 = #29#0;
          |LEE 121#52=;
          |SI FSalida = 0;
               aAlfa = Usuario % 810;

               #52#114 = #52#114 + #29#8;
               #52#115 = 0;
               #52#116 = #29#6;
               #52#117 = #0#7;
               #52#126 = "N";
               #52#127 = #0#2;
               #52#160 = aAlfa;
               |GRABA 020#52a;
               |LIBERA #52;
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE ActReserva;
     #29#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, ActReserva;
|FIN;

|PROCESO BtnActualiza;
     |ABRE #52;
     |BUCLE ActReserva;
     |CIERRA #52;
     |PTEC 807;
|FPRC;

|PROCESO Reservas;
     |SI #52#73 = #0#1; |Y #52#74 = #0#0;|| *** Esta acotacion no se si es correcta
          |SI #52#115 > 0;
               #53#0 = #52#0;
               #53#1 = 1;
               |LEE 000#53=;
               |SI FSalida ! 0; |DDEFECTO #53; |FINSI;

               #29#0 = #52#0;
               #29#1 = #53#3;
               #29#2 = #52#32;
               #29#3 = #52#114;
               #29#4 = #52#115;
               #29#5 = "";
               #29#6 = #52#113;
               |SI #29#6 = " ";    ||las de la web este campo llega vacio
                    #29#6 = "M";
               |FINSI;
               #29#8 = #52#115;
               |GRABA 020#29n;

               #0#4 = #0#4 + #29#2;
               #0#5 = #0#5 + #29#3;
               #0#6 = #0#6 + #29#4;
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE Reservas;
     #52#1;
     ;
     0000000;
     9999999;
     ;
     NULL, Reservas;
|FIN;

|PROCESO T3; |TIPO 3;
     |ABRE #50;
     |SELECT #4#50;
     #50#2 = #0#0;
     #50#0 = #0#1;
     |LEE 000#50=;
     |SI FSalida ! 0;
          #0#4 = 0;
          #0#5 = 0;
          #0#6 = 0;

          eaMensaje = "Calculando..."; |HAZ IniMensaje;

          |ABRE #53;
          |BUCLE Reservas
          |CIERRA #53;

          |HAZ FinMensaje;

          |REFRESCACONTROL nGrid;
          |PINTA #0#4;
          |PINTA #0#5;
          |PINTA #0#6;
          enCtrFin = nGrid;
          |HAZ EsperaFin;
     |SINO;
          |MENAV "0000No existe la hoja de ruta...";
     |FINSI;
     |CIERRA #50;
|FPRC;

|PROCESO BtnFPago;
     |SELECT #1#29;
     |LEE 000#29=;
     |SI #29#6 = "M";
          #29#6 = "T";
     |SINO;
          #29#6 = "M";
     |FINSI;
     |GRABA 000#29a;
     |REFRESCACONTROL nGrid;
|FPRC;

|PROCESO BtnMarca;
     #0#5 = 0;
     #0#6 = 0;
     |SALVA_FICHA #29;
     |LEE 000#29p;
     |SI #29#5 = "*"; aMarca = ""; |SINO; aMarca = "*"; |FINSI;
     |PARA; |SI FSalida = 0; |HACIENDO;
          #29#5 = aMarca;
          |SI aMarca = "*";
               #29#7 = aColor;
               #29#3 = #29#3 + #29#8;
               #29#4 = 0;
          |SINO;
               #29#7 = "";
               #29#3 = #29#3 - #29#8;
               #29#4 = #29#8;
          |FINSI;
          #0#5 = #0#5 + #29#3;
          #0#6 = #0#6 + #29#4;
          |GRABA 020#29a;
          |LEE 000#29s;
     |SIGUE;
     |REPON_FICHA #29;
     |REFRESCACONTROL nGrid;
     |PINTA #0#5;
     |PINTA #0#6;
|FPRC;

|PROCESO Evt;
     |SI FSalida = 1; |O FSalida = 2;
          |SELECT #1#29;
          |LEE 000#29=;
     |FINSI;
     |SI FSalida = 4;
          |SI #29#5 = "*";
               #0#5 = #0#5 - #29#8;
               #0#6 = #0#6 + #29#8;
               #29#5 = "";
               #29#7 = "";
               #29#3 = #29#3 - #29#8;
               #29#4 = #29#8;
          |SINO;
               #0#5 = #0#5 + #29#8;
               #0#6 = #0#6 - #29#8;
               #29#5 = "*";
               #29#7 = aColor;
               #29#3 = #29#3 + #29#8;
               #29#4 = 0;
          |FINSI;
          |GRABA 020#29a;
          |REFRESCACONTROL nGrid;

          |PINTA #0#5;
          |PINTA #0#6;
     |FINSI;
|FPRC;

|PROCESO FecHoja; |TIPO 6;
     #0#7 = #0#1; |PINTA #0#7;
|FPRC;

|PROCESO Ptec; |TIPO 0;
     |SI FSalida = 0; |Y nSwPtec = 0;
          |C_M #0#2, 0, aAlfa;
          |SI aAlfa = "S";
               |SI Campo = 2;
                    |PTEC 806;
                    nSwPtec = 1;
               |FINSI;
          |SINO;
               |PTEC 806;
               nSwPtec = 1;
          |FINSI;
     |FINSI;
|FPRC;

|PROGRAMA;
     aColor = "0,0,0,192,231,250";

     |ABRE #100; |LEE 000#100p; |CIERRA #100;
     |PINPA #0#0; nPanta = FSalida;
     eaNomLogo = #100#1;
     |HAZ TraeLogoPan;
     enPantaLogo = nPanta;
     |HAZ PintaLogoPan;

     aAlfa = Usuario % 810;
     |ABRE #47;
     #47#0 = aAlfa;
     |LEE 000#47=;
     |SI FSalida ! 0;
          |MENAV "0000Usuario no registrado...";
          |ACABA;
     |FINSI;
     |CIERRA #47;

     #0#2 = #47#3;

     |ABRE #46;
     #46#0 = #0#2;
     |LEE 000#46=;
     |CIERRA #46;
     #0#3 = #46#1;

     |SI #47#2 = 0;
          |C_M #0#2, 1, "S";
     |SINO;
          |C_M #0#2, 1, "N";
     |FINSI;


     |PINPA #0#0; nPanta = FSalida;
     |PINDA #0#0;
     |CONTROL_SIMPLE nPanta, "BOTON,{{137}}Marca/Desmarcar todos", 1355, 1378, BtnMarca;
     nBtnMarca = FSalida;
     |CONTROL_SIMPLE nPanta, "BOTON,{{137}}Cambia Forma Pago", 1380, 1398, BtnFPago;
     nBtnFPago = FSalida;
     |CONTROL_SIMPLE nPanta, "BOTON,{{137}}Actualizar", 1085, 1198, BtnActualiza;
     nBtnActualiza = FSalida;

     |PARA nSigue = 0; |SI nSigue = 0; |HACIENDO;
          |ESTADO_CONTROL nBtnMarca, "DISABLE";
          |ESTADO_CONTROL nBtnFPago, "DISABLE";
          |ESTADO_CONTROL nBtnActualiza, "DISABLE";
          |HAZ CreaTempo; |NOME_DAT #29 Tempo; |DELINDEX #29; aTmp29 = Tempo; |ABRE #29;
          nRango = 4 + 8 + 16 + 32;
          |LINEAL_SIMPLE #1#29, nRango, 1402, 2798, NULL, NULL, Evt;
          nGrid = FSalida;
          nSwPtec = 0;
          |ENDATOS #1#0;
          nSigue = FSalida;
          |SI FSalida = 0;
               |ESTADO_CONTROL nBtnMarca, "ENABLE";
               |ESTADO_CONTROL nBtnFPago, "ENABLE";
               |ESTADO_CONTROL nBtnActualiza, "ENABLE";
               |HAZ T3;
          |FINSI;
          |DESTRUYECONTROL nGrid;
          |CIERRA #29; |DELINDEX #29; Tempo = aTmp29; |HAZ BoraTempo;
     |SIGUE;
|FPRO;
