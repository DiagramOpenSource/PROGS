|FICHEROS;
     dsz00120 #0;
     dsz00121 #1,MANTE;   ||Pan C
     dsz00122 #2,MANTE;   ||Pan P
     dsz00123 #3,MANTE;   ||Pan L
     dsz00124 #4,MANTE;   ||Pan R

     agifa019 #19;
     dsm00023 #23;
     agifa112 #112;
     agifa142 #142;
     dsm00149 #149;
     dsm00150 #150;
     dsm00151 #151;
     dsm00152 #152;
|FIN;

|INCLUYE i_varart;

|VARIABLES;
     &eaArtiAnt = "";
     &enCtrFin = 0;
     {-1}aVent = "";
         aVent1 = 0;
         aVent2 = 0;
         aVent3 = 0;
         aVent4 = 0;
         aVent5 = "";
     nVenC = 0;
     nVenP = 0;
     nVenL = 0;
     nVenR = 0;
     nPanta = 0;
     nRango = 0;
     nGridC = 0;
     nGridL = 0;
     nGridP = 0;
     nGridR = 0;
     nBtnAltaC = 0;
     nBtnModiC = 0;
     nBtnBajaC = 0;
     nBtnConsC = 0;
     nBtnAltaP = 0;
     nBtnModiP = 0;
     nBtnBajaP = 0;
     nBtnConsP = 0;
     nBtnAltaL = 0;
     nBtnModiL = 0;
     nBtnBajaL = 0;
     nBtnConsL = 0;
     nBtnAltaR = 0;
     nBtnModiR = 0;
     nBtnBajaR = 0;
     nBtnConsR = 0;
     nBtnImp = 0;
     aAlfa = "";
     nModoC = 0;
     nModoP = 0;
     nModoL = 0;
     nModoR = 0;
     aDArt = "";
     aHArt = "";
     aDPrv = "";
     aHPrv = "";
     nBloquea = 0;
     nConta = 0;
     nSw = 0;
     i = 0;
     m = 999.999;
     nError = 0;
|FIN;

|PROCESO dsm00151;
     |PRINT;
|FPRC;

|DEFBUCLE dsm00151;
     #151#1;
     ;
     #149#0, "                    ";
     #149#0, "zzzzzzzzzzzzzzzzzzzz";
     ;
     NULL, dsm00151;
|FIN;

|PROCESO dsm00150;
     |BUCLE dsm00151;
     |PIEINF;
|FPRC;

|DEFBUCLE dsm00150;
     #150#1;
     ;
     #149#0, "      ";
     #149#0, "zzzzzz";
     ;
     NULL, dsm00150;
|FIN;

|PROCESO BtnImp;
     |LEE 000#149=;
     |SI FSalida ! 0; |ACABA; |FINSI;

     |SALVA_DATOS #150;
     |SALVA_DATOS #151;
     |IMPRE 0;
     |SI FSalida = 0;
          |INFOR "dsm00149";
          |SI FSalida = 0;
               |BUCLE dsm00150;
               |FININF;
          |SINO;
               |MENAV "0000Error en informe 'dsm00149'";
          |FINSI;
          |FINIMP;
     |FINSI;
     |REPON_FICHA #151;
     |REPON_FICHA #150;
|FPRC;

|PROCESO BorraP;
     |BORRA 020#150a;
|FPRC;

|DEFBUCLE BorraP;
     #150#1;
     ;
     #149#0, "      ";
     #149#0, "zzzzzz";
     be;
     NULL, BorraP;
|FIN;

|PROCESO BorraL;
     |BORRA 020#151a;
|FPRC;

|DEFBUCLE BorraL;
     #151#1;
     ;
     #149#0, "                    ";
     #149#0, "zzzzzzzzzzzzzzzzzzzz";
     be;
     NULL, BorraL;
|FIN;

|PROCESO BorraR;
     |BORRA 020#152a;
|FPRC;

|DEFBUCLE BorraR;
     #152#1;
     ;
     #149#0, aDPrv, aDArt;
     #149#0, aDPrv, aDArt;
     be;
     NULL, BorraR;
|FIN;

|PROCESO DobleUniR; |TIPO 7;
     |SI FSalida = 2; |ACABA; |FINSI;
     |C_M #4#3, 1, "S";

     |ABRE #19;
     #19#0 = #4#2;
     |LEE 000#19=;
     |SI FSalida = 0; |Y #19#179 = "S";
          ||eaArtiAnt  (se cargar en el tipo 0 de la linea)
          eaArticulo = #4#2;
          enAlmacen = 0;
          enUnidad1 = #4#3;
          enUnidad2 = #4#4;
          enMedida1 = #4#6;
          enMedida2 = #4#7;
          enMedida3 = #4#8;
          eaPartida = #4#5;
          enModoVta = nModoR;
          eaProgVta = "dsz00120";
          |SUB_EJECUTA_NP "dsz99907";
          |SI eaSw2Stock = "S";
               |C_M #4#3, 1, "N";
               #4#3 = enUnidad1; |PINTA #4#3;
               #4#4 = enUnidad2;
               #4#5 = eaPartida;
               #4#6 = enMedida1;
               #4#7 = enMedida2;
               #3#8 = enMedida3;
          |SINO;
               #4#4 = 0;
               #4#5 = "";
               #4#6 = 0;
               #4#7 = 0;
               #4#8 = 0;
          |FINSI;
     |FINSI;
     |CIERRA #19;
|FPRC;

|PROCESO ArtiR0; |TIPO 0;
     |ABRE #19;
     #19#0 = #4#2;
     |LEE 000#19=;
     |SI FSalida = 0;
          #4#14 = #19#1; |PINTA #4#14;
     |SINO;
          |SI nSw = 0;
               |MENAV "0000No existe art¡culo...";
               |ERROR;
          |FINSI;
     |FINSI;
     |CIERRA #19;
     eaArtiAnt = #4#2;
|FPRC;

|PROCESO ArtiR6; |TIPO 6;
     enAlta = 0;
     eaArticulo = #4Campo;
     |HAZ LlenaArticulo;
     |SI FSalida = 0;
          #4Campo = eaArticulo;
          |PINTA #4Campo;
     |SINO;
          |ERROR; |ACABA;
     |FINSI;
|FPRC;

|PROCESO ProvR; |TIPO 0;
     |ABRE #112;
     #112#0 = #4#1;
     |LEE 000#112=;
     |SI FSalida = 0;
          #4#13 = #112#1; |PINTA #4#13;
     |SINO;
          |SI nSw = 0;
               |MENAV "0000No existe proveedor...";
               |ERROR;
          |FINSI;
     |FINSI;
     |CIERRA #112;
|FPRC;

|PROCESO CheqPrv; |TIPO 0;
     nError = 0;
     |SALVA_FICHA #150;
     #150#0 = #4#0;
     #150#1 = #4#1;
     |LEE 000#150=;
     |SI FSalida ! 0;
          |MENAV "0000Proveedor no informado...";
          nError = 1;
     |FINSI;
     |REPON_FICHA #150;
     |SI nError = 1; |ERROR; |FINSI;
|FPRC;


|PROCESO ChequeaR; |TIPO 0;
     nError = 0;
     |SALVA_FICHA #151;
     #151#0 = #4#0;
     #151#1 = #4#2;
     |LEE 000#151=;
     |SI FSalida ! 0;
          |MENAV "0000Art¡culo no informado...";
          nError = 1;
     |FINSI;
     |REPON_FICHA #151;
     |SI nError = 1; |ERROR; |ACABA; |FINSI;

     |SI nModoR = 1; |Y nBloquea = 0;
          #152#0 = #4#0;
          #152#1 = #4#1;
          #152#2 = #4#2;
          |GRABA 000#152b;
          |SI FSalida ! 0;
               |MENAV "0000Registro existente...";
               |ERROR;
          |SINO;
               |C_M #4#1, 1, "N"; |PINTA #4#1;
               |C_M #4#2, 1, "N"; |PINTA #4#2;
               nBloquea = 1;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO EditaR;
     |SI nModoR = 1; |VENTANA 2301, 3199, -1, 17, "Nuevo art¡culo recibido"; |FINSI;
     |SI nModoR = 2; |VENTANA 2301, 3199, -1, 17, "Modifica art¡culo recibido"; |FINSI;
     |SI nModoR = 4; |VENTANA 2301, 3199, -1, 17, "Consulta art¡culo recibido"; |FINSI;
     nVenR = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVenR;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |PINPA #0#4;
     |SI nModoR = 1;
          |DDEFECTO #4;
          #4#0 = #149#0;
     ||     #4#1 = #150#1;
     ||     #4#2 = #151#1;
          |C_M #4#1, 1, "S";
          |C_M #4#2, 1, "S";
     |SINO;
          |C_M #4#1, 1, "N";
          |C_M #4#2, 1, "N";
          |PARA i = 0; |SI i [ 12; |HACIENDO i = i + 1;
               #4i = #152i;
          |SIGUE;
     |FINSI;
     nBloquea = 0;
     nSw = 1;
     |HAZ ArtiR0;
     |HAZ ProvR;
     nSw = 0;
     |PINDA #0#4;
     |SI nModoR = 1; |ENDATOS #1#4; |FINSI;
     |SI nModoR = 2; |ENDATOS #2#4; |FINSI;
     |SI nModoR = 4; |PAUSA; |FINSI;
     |SI nModoR = 1; |Y nBloquea = 0; FSalida = 1; |FINSI;
     |SI FSalida = 0; |Y nModoR ! 4;
          |PARA i = 0; |SI i [ 12; |HACIENDO i = i + 1;
               #152i = #4i;
          |SIGUE;
          |GRABA 020#152a;
          |REFRESCACONTROL nGridR;
     |SINO;
          |SI nModoR = 1; |Y nBloquea = 1;
               |BORRA 020#152c;
          |FINSI;
          |REFRESCACONTROL nGridR;
     |FINSI;
     |LIBERA #152;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVenR;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;
     |FINVENTANA nVenR;
     |PULSATECLA;
|FPRC;

|PROCESO DobleUniL; |TIPO 7;
     |C_M #3#3, 1, "S";

     |ABRE #19;
     #19#0 = #3#1;
     |LEE 000#19=;
     |SI FSalida = 0; |Y #19#179 = "S";
          ||eaArtiAnt  (se cargar en el tipo 0 de la linea)
          eaArticulo = #3#1;
          enAlmacen = 0;
          enUnidad1 = #3#3;
          enUnidad2 = #3#4;
          enMedida1 = #3#6;
          enMedida2 = #3#7;
          enMedida3 = #3#8;
          eaPartida = #3#5;
          enModoVta = nModoL;
          eaProgVta = "dsz00120";
          |SUB_EJECUTA_NP "dsz99907";
          |SI eaSw2Stock = "S";
               |C_M #3#3, 1, "N";
               #3#3 = enUnidad1; |PINTA #3#3;
               #3#4 = enUnidad2;
               #3#5 = eaPartida;
               #3#6 = enMedida1;
               #3#7 = enMedida2;
               #3#8 = enMedida3;
          |SINO;
               #3#4 = 0;
               #3#5 = "";
               #3#6 = 0;
               #3#7 = 0;
               #3#8 = 0;
          |FINSI;
     |FINSI;
     |CIERRA #19;
|FPRC;

|PROCESO ArtiL; |TIPO 6;
     enAlta = 0;
     eaArticulo = #3Campo;
     |HAZ LlenaArticulo;
     |SI FSalida = 0;
          #3Campo = eaArticulo;
          |PINTA #3Campo;
     |SINO;
          |ERROR; |ACABA;
     |FINSI;
|FPRC;

|PROCESO ChequeaL; |TIPO 0;
     |SI nModoL = 1; |Y nBloquea = 0;
          #151#0 = #3#0;
          #151#1 = #3#1;
          |GRABA 000#151b;
          |SI FSalida ! 0;
               |MENAV "0000Registro existente...";
               |ERROR;
          |SINO;
               |C_M #3#1, 1, "N"; |PINTA #3#1;
               nBloquea = 1;
          |FINSI;
     |FINSI;
     eaArtiAnt = #3#1;
|FPRC;

|PROCESO EditaL;
     |SI nModoL = 1; |VENTANA 2501, 3199, -1, 17, "Nuevo art¡culo"; |FINSI;
     |SI nModoL = 2; |VENTANA 2501, 3199, -1, 17, "Modifica art¡culo"; |FINSI;
     |SI nModoL = 4; |VENTANA 2501, 3199, -1, 17, "Consulta art¡culo"; |FINSI;
     nVenL = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVenL;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |PINPA #0#3;
     |SI nModoL = 1;
          |DDEFECTO #3;
          #3#0 = #149#0;
          |C_M #3#1, 1, "S";
     |SINO;
          |C_M #3#1, 1, "N";
          |PARA i = 0; |SI i [ 8; |HACIENDO i = i + 1;
               #3i = #151i;
          |SIGUE;
     |FINSI;
     nBloquea = 0;
     nSw = 1;
     nSw = 0;
     |PINDA #0#3;
     |SI nModoL = 1; |ENDATOS #1#3; |FINSI;
     |SI nModoL = 2; |ENDATOS #2#3; |FINSI;
     |SI nModoL = 4; |PAUSA; |FINSI;
     |SI FSalida = 0; |Y nModoL ! 4;
          |PARA i = 0; |SI i [ 8; |HACIENDO i = i + 1;
               #151i = #3i;
          |SIGUE;
          |GRABA 020#151a;
          |REFRESCACONTROL nGridL;
     |SINO;
          |SI nModoL = 1; |Y nBloquea = 1;
               |BORRA 020#151c;
          |FINSI;
          |REFRESCACONTROL nGridL;
     |FINSI;
     |LIBERA #151;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVenL;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;
     |FINVENTANA nVenL;
     |PULSATECLA;
|FPRC;

|PROCESO ChequeaP; |TIPO 0;
     |SI nModoP = 1; |Y nBloquea = 0;
          #150#0 = #2#0;
          #150#1 = #2#1;
          |GRABA 000#150b;
          |SI FSalida ! 0;
               |MENAV "0000Registro existente...";
               |ERROR;
          |SINO;
               |C_M #2#1, 1, "N"; |PINTA #2#1;
               nBloquea = 1;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO EditaP;
     |SI nModoP = 1; |VENTANA 2601, 3199, -1, 17, "Nueva proveedor"; |FINSI;
     |SI nModoP = 2; |VENTANA 2601, 3199, -1, 17, "Modifica proveedor"; |FINSI;
     |SI nModoP = 4; |VENTANA 2601, 3199, -1, 17, "Consulta proveedor"; |FINSI;
     nVenP = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVenP;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |PINPA #0#2;
     |SI nModoP = 1;
          |DDEFECTO #2;
          #2#0 = #149#0;
          |C_M #2#1, 1, "S";
     |SINO;
          |C_M #2#1, 1, "N";
          |PARA i = 0; |SI i [ 2; |HACIENDO i = i + 1;
               #2i = #150i;
          |SIGUE;
     |FINSI;
     nBloquea = 0;
     nSw = 1;
     nSw = 0;
     |PINDA #0#2;
     |SI nModoP = 1; |ENDATOS #1#2; |FINSI;
     |SI nModoP = 2; |ENDATOS #2#2; |FINSI;
     |SI nModoP = 4; |PAUSA; |FINSI;
     |SI FSalida = 0; |Y nModoP ! 4;
          |PARA i = 0; |SI i [ 2; |HACIENDO i = i + 1;
               #150i = #2i;
          |SIGUE;
          |GRABA 020#150a;
          |REFRESCACONTROL nGridP;
     |SINO;
          |SI nModoP = 1; |Y nBloquea = 1;
               |BORRA 020#150c;
          |FINSI;
          |REFRESCACONTROL nGridP;
     |FINSI;
     |LIBERA #150;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVenP;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;
     |FINVENTANA nVenP;
     |PULSATECLA;
|FPRC;

|PROCESO EditaC;
     |SI nModoC = 1; |VENTANA 2101, 3199, -1, 17, "Nueva oferta"; |FINSI;
     |SI nModoC = 2; |VENTANA 2101, 3199, -1, 17, "Modifica oferta"; |FINSI;
     |SI nModoC = 4; |VENTANA 2101, 3199, -1, 17, "Consulta oferta"; |FINSI;
     nVenC = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVenC;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |PINPA #0#1;
     |SI nModoC = 1;
          |LEE 000#149u;
          |SI FSalida = 0;
               nConta = #149#0 + 1;
          |SINO;
               nConta = 1;
          |FINSI;
          |DDEFECTO #1;
          #1#0 = nConta;

          #149#0 = nConta;
          |GRABA 020#149b;
     |SINO;
          |PARA i = 0; |SI i [ 6; |HACIENDO i = i + 1;
               #1i = #149i;
          |SIGUE;
     |FINSI;
     nSw = 1;
     nSw = 0;
     |PINDA #0#1;
     |SI nModoC = 1; |ENDATOS #1#1; |FINSI;
     |SI nModoC = 2; |ENDATOS #2#1; |FINSI;
     |SI nModoC = 4; |PAUSA; |FINSI;
     |SI FSalida = 0; |Y nModoC ! 4;
          |PARA i = 0; |SI i [ 6; |HACIENDO i = i + 1;
               #149i = #1i;
          |SIGUE;
          |GRABA 020#149a;
          |REFRESCACONTROL nGridC;
     |SINO;
          |SI nModoC = 1;
               |BORRA 020#149c;
          |FINSI;
          |REFRESCACONTROL nGridC;
     |FINSI;
     |LIBERA #149;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVenC;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;
     |FINVENTANA nVenC;
     |PULSATECLA;
|FPRC;

|PROCESO BtnAltaC;
     nModoC = 1;
     |HAZ EditaC;
|FPRC;

|PROCESO BtnModiC;
     |LEE 101#149=;
     |SI FSalida = 0;
          nModoC = 2;
          |HAZ EditaC;
     |FINSI;
     |LIBERA #149;
|FPRC;

|PROCESO BtnConsC;
     |LEE 000#149=;
     |SI FSalida = 0;
          nModoC = 4;
          |HAZ EditaC;
     |FINSI;
|FPRC;

|PROCESO BtnBajaC;
     |LEE 101#149=;
     |SI FSalida = 0;
          aAlfa = "0000N¨Borrar Oferta n§:" + (("000000" + #149#0) % -106) + "?";
          |CONFI aAlfa;
          |SI FSalida = 0;
               |BORRA 020#149a;
               |BUCLE BorraP;
               |BUCLE BorraL;
               aDPrv = "      "; aHPrv = "zzzzzz";
               aDArt = "                    "; aDArt = "zzzzzzzzzzzzzzzzzzzz";
               |BUCLE BorraR;
               #149#0 = 0;
               |REFRESCACONTROL nGridR;
               |REFRESCACONTROL nGridL;
               |REFRESCACONTROL nGridP;
               |REFRESCACONTROL nGridC;
          |FINSI;
     |FINSI;
     |LIBERA #149;
|FPRC;

|PROCESO BtnAltaP;
     |LEE 000#149=;
     |SI FSalida = 0;
          nModoP = 1;
          |HAZ EditaP;
     |FINSI;
|FPRC;

|PROCESO BtnModiP;
     |LEE 101#150=;
     |SI FSalida = 0; |Y #149#0 = #150#0;
          nModoP = 2;
          |HAZ EditaP;
     |FINSI;
     |LIBERA #150;
|FPRC;

|PROCESO BtnConsP;
     |LEE 000#150=;
     |SI FSalida = 0; |Y #149#0 = #150#0;
          nModoP = 4;
          |HAZ EditaP;
     |FINSI;
|FPRC;

|PROCESO BtnBajaP;
     |LEE 101#150=;
     |SI FSalida = 0; |Y #149#0 = #150#0;
          aAlfa = "0000N¨Borrar Oferta n§/proveedor:" + (("000000" + #150#0) % -106) + "/" + #150#1 + "?";
          |CONFI aAlfa;
          |SI FSalida = 0;
               |BORRA 020#150a;
               aDPrv = #150#1; aHPrv = #150#1;
               aDArt = "                    "; aDArt = "zzzzzzzzzzzzzzzzzzzz";
               |BUCLE BorraR;
               #150#0 = 0;
               |REFRESCACONTROL nGridR;
               |REFRESCACONTROL nGridP;
          |FINSI;
     |FINSI;
     |LIBERA #150;
|FPRC;

|PROCESO BtnAltaL;
     |LEE 101#149=;
     |SI FSalida = 0;
          nModoL = 1;
          |HAZ EditaL;
     |FINSI;
     |LIBERA #149;
|FPRC;

|PROCESO BtnModiL;
     |LEE 101#151=;
     |SI FSalida = 0; |Y #149#0 = #151#0;
          nModoL = 2;
          |HAZ EditaL;
     |FINSI;
     |LIBERA #151;
|FPRC;

|PROCESO BtnConsL;
     |LEE 000#151=;
     |SI FSalida = 0; |Y #149#0 = #151#0;
          nModoL = 4;
          |HAZ EditaL;
     |FINSI;
|FPRC;

|PROCESO BtnBajaL;
     |LEE 101#151=;
     |SI FSalida = 0; |Y #149#0 = #151#0;
          aAlfa = "0000N¨Borrar Oferta n§/art¡culo:" + (("000000" + #151#0) % -106) + "/" + #151#1;
          |QBF aAlfa; aAlfa = aAlfa + "?";
          |CONFI aAlfa;
          |SI FSalida = 0;
               |BORRA 020#151a;
               aDPrv = "      "; aHPrv = "zzzzzz";
               aDArt = #151#1; aDArt = #151#1;
               |BUCLE BorraR;
               #151#0 = 0;
               |REFRESCACONTROL nGridR;
               |REFRESCACONTROL nGridL;
          |FINSI;
     |FINSI;
     |LIBERA #151;
|FPRC;

|PROCESO BtnAltaR;
     |LEE 000#149=;
     |SI FSalida = 0;
          nModoR = 1;
          |HAZ EditaR;
     |FINSI;
|FPRC;

|PROCESO BtnModiR;
     |LEE 101#152=;
     |SI FSalida = 0; |Y #149#0 = #152#0;
          nModoR = 2;
          |HAZ EditaR;
     |FINSI;
     |LIBERA #152;
|FPRC;

|PROCESO BtnConsR;
     |LEE 000#152=;
     |SI FSalida = 0; |Y #149#0 = #152#0;
          nModoR = 4;
          |HAZ EditaR;
     |FINSI;
|FPRC;

|PROCESO BtnBajaR;
     |LEE 101#152=;
     |SI FSalida = 0; |Y #149#0 = #152#0;
          aAlfa = "0000N¨Borrar Oferta n§/proveedor/art¡culo:" + (("000000" + #152#0) % -106) + "/"+ #152#1;
          aAlfa = aAlfa + "/" + #152#2;
          |QBF aAlfa; aAlfa = aAlfa + "?";
          |CONFI aAlfa;
          |SI FSalida = 0;
               |BORRA 020#152a;
               |REFRESCACONTROL nGridR;
          |FINSI;
     |FINSI;
     |LIBERA #152;
|FPRC;

|PROCESO EvtC;
     |SI FSalida = 1; |O FSalida = 2;
          |LEE 000#149=;
          |REFRESCACONTROL nGridP;
          |REFRESCACONTROL nGridL;
          |REFRESCACONTROL nGridR;
     |FINSI;
     |SI FSalida = 4;
          |HAZ BtnModiC;
     |FINSI;
|FPRC;

|PROCESO EvtP;
     |SI FSalida = 1; |O FSalida = 2;
          |LEE 000#150=;
          |REFRESCACONTROL nGridR;
     |FINSI;
     |SI FSalida = 4;
          |HAZ BtnModiP;
     |FINSI;
|FPRC;

|PROCESO EvtL;
     |SI FSalida = 1; |O FSalida = 2;
          |LEE 000#151=;
          |REFRESCACONTROL nGridR;
     |FINSI;
     |SI FSalida = 4;
          |HAZ BtnModiL;
     |FINSI;
|FPRC;

|PROCESO EvtR;
     |SI FSalida = 1; |O FSalida = 2;
          |LEE 000#152=;
     |FINSI;
     |SI FSalida = 4;
          |HAZ BtnModiR;
     |FINSI;
|FPRC;

|PROCESO MinL;
     #151#0 = #149#0;
     #151#1 = "                    ";
|FPRC;

|PROCESO MaxL;
     #151#0 = #149#0;
     #151#1 = "zzzzzzzzzzzzzzzzzzzz";
|FPRC;

|PROCESO MinP;
     #150#0 = #149#0;
     #150#1 = "      ";
|FPRC;

|PROCESO MaxP;
     #150#0 = #149#0;
     #150#1 = "zzzzzz";
|FPRC;

|PROCESO MinR;
     #152#0 = #149#0;
     #152#1 = "      ";
     #152#2 = "                    ";
|FPRC;

|PROCESO MaxR;
     #152#0 = #149#0;
     #152#1 = "zzzzzz";
     #152#2 = "zzzzzzzzzzzzzzzzzzzz";
|FPRC;

|PROGRAMA;
     |ABRE #142; |LEE 000#142p; |CIERRA #142;
     |ABRE #23;
     #23#0 = "PO";
     |LEE 000#23=;
     |CIERRA #23;

     |PINPA #0#0; nPanta = FSalida;
     |PINDA #0#0;

     |ABRET #149;
     |ABRET #150;
     |ABRET #151;
     |ABRET #152;

     |CONTROL_SIMPLE nPanta, "BOTON,{{205}}Imprimir", 0688, 0698, BtnImp;

     nRango = 4 + 8 + 16 + 32;
     |CONTROL_SIMPLE nPanta, "BOTON,{{193}}Alta", 2150, 2160, BtnAltaR;
     nBtnAltaR = FSalida;
     |CONTROL_SIMPLE nPanta, "BOTON,{{197}}Modificar", 2161, 2171, BtnModiR;
     nBtnModiR = FSalida;
     |CONTROL_SIMPLE nPanta, "BOTON,{{199}}Baja", 2172, 2182, BtnBajaR;
     nBtnBajaR = FSalida;
     |CONTROL_SIMPLE nPanta, "BOTON,{{155}}Consulta", 2183, 2193, BtnConsR;
     |LINEAL_SIMPLE #1#152, nRango, 2250, 3299, MinR, MaxR, EvtR;
     nGridR = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,{{193}}Alta", 2101, 2111, BtnAltaL;
     nBtnAltaL = FSalida;
     |CONTROL_SIMPLE nPanta, "BOTON,{{197}}Modificar", 2112, 2122, BtnModiL;
     nBtnModiL = FSalida;
     |CONTROL_SIMPLE nPanta, "BOTON,{{199}}Baja", 2123, 2133, BtnBajaL;
     nBtnBajaL = FSalida;
     |CONTROL_SIMPLE nPanta, "BOTON,{{155}}Consulta", 2134, 2144, BtnConsL;
     nBtnConsL = FSalida;
     |LINEAL_SIMPLE #1#151, nRango, 2201, 3248, MinL, MaxL, EvtL;
     nGridL = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,{{193}}Alta", 850, 860, BtnAltaP;
     nBtnAltaP = FSalida;
     |CONTROL_SIMPLE nPanta, "BOTON,{{197}}Modificar", 861, 871, BtnModiP;
     nBtnModiP = FSalida;
     |ESTADO_CONTROL nBtnModiP, "DISABLE";
     |CONTROL_SIMPLE nPanta, "BOTON,{{199}}Baja", 872, 882, BtnBajaP;
     nBtnBajaP = FSalida;
     |CONTROL_SIMPLE nPanta, "BOTON,{{155}}Consulta", 883, 893, BtnConsP;
     |LINEAL_SIMPLE #1#150, nRango, 950, 1999, MinP, MaxP, EvtP;
     nGridP = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,{{193}}Alta", 801, 811, BtnAltaC;
     nBtnAltaC = FSalida;
     |CONTROL_SIMPLE nPanta, "BOTON,{{197}}Modificar", 812, 822, BtnModiC;
     nBtnModiC = FSalida;
     |CONTROL_SIMPLE nPanta, "BOTON,{{199}}Baja", 823, 833, BtnBajaC;
     nBtnBajaC = FSalida;
     |CONTROL_SIMPLE nPanta, "BOTON,{{155}}Consulta", 834, 844, BtnConsC;
     nBtnConsC = FSalida;
     |LINEAL_SIMPLE #1#149, nRango, 901, 1948, NULL, NULL, EvtC;
     nGridC = FSalida;

     enCtrFin = nGridC;
     |HAZ EsperaFin;

     |CIERRAT #152;
     |CIERRAT #151;
     |CIERRAT #150;
     |CIERRAT #149;
|FPRO;
