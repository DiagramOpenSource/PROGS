|FICHEROS;
     okm00009 #0;
     okm00010 #1;
     agifa019 #19;
     okm00001 #100;
|FIN;

|VARIABLES;
     nModo = 0;
     aArti = "";
     nLin = 0;
     nId = 0;
     aAlfa = "";
     {10}aPopup = "";
|FIN;

|PROCESO DefeLin; |TIPO 7;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 1;
          #1#2 = #0#2;
          #1#3 = 9999999999;
          |LEE 000#1];
          |SI FSalida = 0;
               |LEE 000#1a;
          |SINO;
               |LEE 000#1u;
          |FINSI;
          |SI FSalida = 0; |Y #1#2 = #0#2;
               nLin = #1#3 + 1;
          |SINO;
               nLin = 1;
          |FINSI;
          |DDEFECTO #1;
          #1#2 = #0#2;
          #1#3 = nLin; |PINTA #1#3;
          aAlfa  = "|X000"; aAlfa = #0aAlfa; nId = aAlfa;
          |ESTADO_CONTROL nId, "ENABLE";
     |FINSI;
|FPRC;

|PROCESO Total; |TIPO 0;
     #1#8 = #1#6 * #1#7; |PINTA #1#8;
|FPRC;

|PROCESO Arti7; |TIPO 7;
     aArti = #1#4;
|FPRC;

|PROCESO Arti0; |TIPO 0;
     |SI aArti ! #1#4;
          |ABRE #19;
          #19#0 = #1#4;
          |LEE 000#19=;
          |SI FSalida = 0;
               #1#5 = #19#1; |PINTA #1#5;
          |SINO;
               |MENAV "0000No existe c¢digo de art¡culo...";
               |ERROR;
          |FINSI;
          |CIERRA #19;
     |FINSI;
|FPRC;

|PROCESO Arti66; |TIPO 66;
     |ABRE #19;
     #19#0 = #1#4;
     |LEE 000#19];
     |CONSULTA_CLAVE #1#19;
     |SI FSalida = 0;
          #1#4 = #19#0; |PINTA #1#4;
     |FINSI;
     |CIERRA #19;
|FPRC;

|PROCESO Almacen; |TIPO 0;
     |SI #1#9 = #100#10;
          #1#10 = "C";
     |SINO;
          #1#10 = "P";
     |FINSI;
     |PINTA #1#10;
|FPRC;

|PROCESO T2Lin; |TIPO 2;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 3;
          |SI #0#18 = "S";
               |MENAV "0000Prepedido en web...";
               |ERROR;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO TipoAlma; |TIPO 66;
     aPopup1  = "0";
     aPopup2 = 2;
     aPopup3 = "C.- Almacen central";
     aPopup4 = "P.- Almacen propio";

     IaPopup = aPopup1<-;
     |ESPECIFICA "TRACKPOPUP", aPopup;
     |SI FSalida ! 0;
          IaPopup = aPopup2<-;
          IaPopup  = IaPopup + FSalida;
          #1Campo = aPopup; |PINTA #1Campo;
     |FINSI;
|FPRC;
