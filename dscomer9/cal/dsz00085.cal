          Basado en el agifa218

|FICHEROS;
     dsz00085 #0;
     agifa019 #19;
     agifa017 #17;
     agifa010 #10; ||agifa104 #104;  || Pedidos de Venta Cab.
     agifa071 #71; ||agifa073 #73;  || Pedidos de Venta Lin.

     agifa081 #81;  || cab ped.compra
     agifa067 #67;  || lin ped compra

     agifa112 #35;  || Proveedores.
     expor071@ #41;
     agifa029 #29;  ||Almace
     dsm00041 #50;  || Alma Dependientes
     agifa007 #7;
     agifa142;
     dsm00005 #5;   ||Entrada vertical

     agi00029 #1,MANTE;   || Tmp Consulta Prv
     agifa080 #80;  ||Albaran Compras Lineas
     agifa077 #77;  ||Albaran Compras Cab
     agifa041 #11;  || datos empresa
     agifa321 #321;
     agifa324 #324;
|FIN;

|VARIABLES;
     &nDeci_PreBase = 0;
     &enPrecioB = 0;
     &enDeciB = 0;
     aDirEmp = "";
     aDatos = "";
     nCodEmp = 0;
     nNume = 0;
     nPre = 0;
     nDto = 0;
     aPro = "";
     linea = 0;
     aTempo1 = "";
     nLinV     = 0;
     &eaDocu   = "";
     &enPrecio = 0;
     &enPreDiv = 0;
     &enDto1   = 0;
     &enDto2   = 0;
     &enDto3   = 0;
     &Tempo    = "";
     aTempo0   = "";
     aTempo41  = "";
     aDef1     = "";
     aPath     = "";
     nPrecio = 0
     aMensa    = "";
     nModo          = 0;
     nAlma          = 0;
     nCampo         = 0;
     nLinea         = 0;

     ||-- Variables de comunicacion
     &enSwMenu      = 0;
     &eaSerie  = "";
     &enCodigo = 0;
     &enForma       = 0;

     aProve         = "";
     nCampos        = 52;
     nLinVenta      = 0;
     nLinTmp        = 0;
     nSwPrimera     = 0;
     nUni           = 0;
     nDispo         = 0;
     nFalta         = 0;
     nConta         = 0;
     i              = 0;
     j              = 0;
     aAlfa          = "";
     aNomFich    = "";
|FIN;

|PROCESO Contador;
     #77#50 = eaSerie;
     enSwMenu = 1;
     |HAZ ContaAC7;
     nConta = #77#0;
     |PARA; |SI FSalida = 0; |HACIENDO;
          #77#0  = nConta;
          #77#50 = eaSerie;
          |LEE 000#77=;
          |SI FSalida = 0; nConta  = nConta + 1; |FINSI;
     |SIGUE;
     |DDEFECTO #77;
     #77#0  = nConta;
     #77#50 = eaSerie;
|FPRC;

|PROCESO CabeAlbCompra;
     |ABRE #321; |LEE 000#321p; |CIERRA #321;
     |ABRE #324;
     |DDEFECTO #77;
     |HAZ Contador;
     |GRABA 021#77b;
     #77#1 = ~;
     #77#3 = #35#0;
     #77#4 = #35#1;
     #77#8 = #35#17;

     |DFICE aDirEmp;
     |DFICB aDatos;
     aDirEmp = aDirEmp % -205;
     nCodEmp = aDirEmp;
     |PATH_DAT #11 aDatos;
     |ABRE #11;
     #11#0 = nCodEmp;
     |LEE 020#11=;
     |CIERRA #11;

     #77#29 = #11#1;        || nombre de la empresa
     #77#30 = #11#2;        || direccion de la empresa
     #77#31 = #11#3;        || poblacion de la empresa
     #77#33 = #11#4;        || provincia de la empresa
     #77#36 = #35#58;
     #77#56 = #35#81;

     #324#0 = #77#56;
     |LEE 000#324=;

     #77#57 = #324#3;
     #77#58 = #35#81;
     #77#61 = #321#9;

     #324#0 = #77#61;
     |LEE 000#324=;
     #77#62 = #324#3;

     |GRABA 020#77a;
     |CIERRA #324;

     |HAZ Divisas077;
|FPRC;

|PROCESO GrabaEntVert;
     #dsm00005#0 = "AC";
     #dsm00005#1 = #80#27;
     #dsm00005#2 = #80#0;
     #dsm00005#3 = #80#1;
     |GRABA 020#dsm00005.n;
|FPRC;

|DEFBUCLE GrabaEntVert;
     #dsm00005#1;
     ;
     "AV", #71#29, #71#0, nLinV,"      ";
     "AV", #71#29, #71#0, nLinV,"zzzzzz";
     be;
     NULL, GrabaEntVert;
|FIN;

|PROCESO PcmLinea;
     enPrecioB = #agifa324#9;
     enDeciB = nDeci_PreBase;
     enForma = 1;
     eaDocu = "agifa077";
     |HAZ StockEntra;
|FPRC;

|PROCESO Recalcula077;
     |HAZ LimCabAgifa077;
     |BUCLELIN 0 CalCabAgifa077 #77;

     |BUCLELIN 0 PcmLinea #77;
     |GRABA 020#77a;
     |LIBERA #77;
|FPRC;

|PROCESO GrabaCompra;
     #71#29 = #10#52;
     #71#0 = #10#0;
     #71#1 = #0#8;
     |LEE 020#71=;

     |HAZ LeeArti;
     |HAZ LeeProveedor;
     |SI aProve ! #35#0;
          |SI aProve ! ""; |HAZ Recalcula077; |FINSI;
          aProve = #35#0;
          |HAZ CabeAlbCompra;
     |FINSI;

     |DDEFECTO #80;
     #80#27 = #77#50;
     #80#0 = #77#0;
     #80#1 = #71#1;
     #80#2 = #71#2;
     #80#4 = #71#4;

     #80#6 = #0#10;  || Precio
     #80#30 = #0#13; || Precio Div
     #80#8 = #0#9;   || Dto1
     #80#26 = #0#12; || Dto2
     #80#45 = #0#14; || Dto3

     |ABRE #7; #7#26 = #80#27; |LEE 020#7=; |CIERRA #7; ||SERIE

     nFalta = #0#6;

     #80#3 = #7#31;          || Almacen
     #80#5 = nFalta;         || Unidades pdtes= unidades

     #80#11 = #71#11;
     #80#13 = #71#13;
     #80#15 = aProve;

     #80#34 = #71#34;
     #80#35 = #71#35;
     #80#39 = #71#51;
     #80#40 = #71#52;
     #80#41 = #71#53;
     |HAZ TotalLin80;
     |GRABA 020#80n;

     nLinV = #71#1;
     |BUCLE GrabaEntVert;

     enForma = 1;
     eaDocu = "agifa080";
     |HAZ StockEntra;
|FPRC;

|DEFBUCLE GrabaCompra;
     #0#2;
     ;
     "    ", "*", 001;
     "zzzz", "*", 999;
     ;
     NULL, GrabaCompra;
|FIN;

|PROCESO RecalcuVenta;
     enForma = 1; |HAZ AcumulaAV;
     |HAZ CalCabAgifa010;
|FPRC;

|PROCESO GrabaVenta;
     |SI #41#0 = "*"; |ACABA; |FINSI;   || Es para Compras

     |PARA i = 0; |SI i < nCampos; |HACIENDO i = i + 1;
          #71i = #41i;
     |SIGUE;
     nLinVenta = nLinVenta + 1;
     #71#1 = nLinVenta;

     #19#0 = #71#2;
     |LEE 020#19=;
     #71#6 = #19#28;
     #71#38 = #19#28;

     |GRABA 020#71n;
|FPRC;

|DEFBUCLE GrabaVenta;
     #41#1003;
     ;
     "     ", 000001, 001;
     "zzzzz", 999999, 999;
     ;
     NULL, GrabaVenta;
|FIN;

|PROCESO LeeProveedor;
     |ABRE #35;
     #35#0 = #0#7;
     |LEE 000#35=;
     |SI FSalida ! 0; |DDEFECTO #35; |FINSI;
     |CIERRA #35;
|FPRC;

|PROCESO LeeArti;
     |ABRE #19;
     #19#0 = #71#2;
     |LEE 021#19=;
     |SI FSalida ! 0; |DDEFECTO #19; |FINSI;
     |CIERRA #19;
|FPRC;

|PROCESO LeeAlmacen;
     |ABRE #29;
     #29#0 = nAlma;
     |LEE 000#29=;
     |SI FSalida ! 0; |DDEFECTO #29; |FINSI;
     |CIERRA #29;
|FPRC;

|PROCESO GrabaTmpC;
     nAlma = #71#3;
     |HAZ LeeAlmacen;
     |HAZ LeeArti;

     nLinea = nLinea + 1;
     |ABRE #0;
     |DDEFECTO #0;
     #0#0 = "*";
     #0#1 = nLinea;
     #0#2 = #71#2; || Arti
     #0#3 = #71#3; || Alma
     #0#4 = #71#4; ||Des Arti
     #0#5 = #29#1; ||des Alma
     #0#6 = nFalta;
     #0#7 = #19#27;
     #0#8 = #71#1;

     ||Para Hallar el Precio
     |DDEFECTO #80;
     |PARA i = 0;|SI i [ 7;|HACIENDO i = i + 1;
          #80i = #71i;
     |SIGUE;

     eaDocu = "agifa080";
     |HAZ LeePreDtoCom;
     #0#10 = enPrecio;
     #0#13 = enPreDiv;
     #0#9 = enDto1;
     #0#12 = enDto2;
     #0#14 = enDto3;

     |GRABA 020#0n;
     |CIERRA #0;
|FPRC;

|PROCESO GrabaTmpV;
     nAlma = #41#3;
     |HAZ LeeAlmacen;

     nLinea = nLinea + 1;
     |ABRE #0;
     |DDEFECTO #0;
     #0#0 = "";
     #0#1 = nLinea;
     #0#2 = #41#2; || Arti
     #0#3 = #41#3; || Alma
     #0#4 = #41#4; ||Des Arti
     #0#5 = #29#1; ||des Alma
     #0#6 = #41#5; ||Uni
     #0#7 = "";
     #0#8 = #41#1;
     |GRABA 020#0n;
     |CIERRA #0;
|FPRC;

|PROCESO MiraDepeParcial;
     |PARA i = 11; |SI i [ 20; |Y nFalta > 0; |HACIENDO i = i + 1;
          nCampo = i - 10;
          |SI #50i = "P";
               #17#1 = #50nCampo;
               |LEE 000#17=;
               nDispo = #17#43 + #17#5 - #17#42;
               |SI nDispo > 0;
                    |PARA j = 0; |SI j < nCampos; |HACIENDO j = j + 1;
                         #41j = #71j;
                    |SIGUE;
                    nLinTmp = nLinTmp + 1;
                    #41#3 = #17#1;           ||Almacen
                    #41#1 = nLinTmp;

                    nUni = nDispo - nFalta;
                    |SI nUni ] 0;
                         #71#5 = #71#5 - nFalta;
                         |SI #71#5 = 0;
                              |BORRA 020#71a;
                         |SINO;
                              |GRABA 020#71a;
                         |FINSI;
                         #41#5 = nFalta;
                         |GRABA 020#41n;
                         |HAZ GrabaTmpV;
                         nFalta = 0;
                    |SINO;
                         #71#5 = #71#5 - nDispo;
                         |SI #71#5 = 0;
                              |BORRA 020#71a;
                         |SINO;
                              |GRABA 020#71a;
                         |FINSI;
                         #41#5 = nDispo;
                         |GRABA 020#41n;
                         |HAZ GrabaTmpV;
                         nFalta = nFalta - nDispo;
                    |FINSI;
               |FINSI;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO MiraDepeTotal;
     |PARA i = 11; |SI i [ 20; |Y nFalta > 0; |HACIENDO i = i + 1;
          nCampo = i - 10;
          |SI #50i = "T";
               #17#1 = #50nCampo;
               |LEE 000#17=;
               nDispo = #17#43 + #17#5 - #17#42;
               |SI nDispo > nFalta;
                    |PARA j = 0; |SI j < nCampos; |HACIENDO j = j + 1;
                         #41j = #71j;
                    |SIGUE;
                    nLinTmp = nLinTmp + 1;
                    #41#3 = #17#1;           ||Almacen
                    #41#1 = nLinTmp;

                    #71#5 = #71#5 - nFalta;
                    |SI #71#5 = 0;
                         |BORRA 020#71a;
                    |SINO;
                         |GRABA 020#71a;
                    |FINSI;
                    #41#5 = nFalta;
                    |GRABA 020#41n;
                    |HAZ GrabaTmpV;
                    nFalta = 0;
               |FINSI;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO GrabaTmp;
     enForma = -1;
     |HAZ AcumulaAV;

     |ABRE #50;
     |ABRE #17;     ||Almacen
     |ABRE #19;
     #17#0 = #71#2;
     #17#1 = #71#3;
     |LEE 000#17=;
     |SI #agifa142#254 = "N";           || teniendo en cuenta el stock
                 ||P.Rec    Tota    P.Ser
          nFalta = #17#43 + #17#5 - #17#42 - #71#5;
          |SI nFalta < 0;
               nFalta = -nFalta;
               |SI nFalta > #71#5; nFalta = #71#5; |FINSI;

               #50#0 = #17#1;
               |LEE 000#50=;   ||Alma Depen
               |SI FSalida = 0;
                    |HAZ MiraDepeTotal;
                    |SI nFalta > 0;
                         |HAZ MiraDepeParcial;
                    |FINSI;
               |FINSI;
               |SI nFalta > 0;
                    |HAZ GrabaTmpC;
               |FINSI;
          |FINSI;
     |SINO;                             || sin tener en cuenta el stock
          nFalta = #71#5;

          #50#0 = #17#1;
          |LEE 000#50=;   ||Alma Depen
          |SI FSalida = 0;
               |HAZ MiraDepeTotal;
               |SI nFalta > 0;
                    |HAZ MiraDepeParcial;
               |FINSI;
          |FINSI;
          |SI nFalta > 0;
               |HAZ GrabaTmpC;
          |FINSI;
     |FINSI;
     |CIERRA #17;
     |CIERRA #19;
     |CIERRA #50;
     nLinVenta = #71#1;
|FPRC;

|DEFBUCLE GrabaTmp;
     #71#1;
     ;
     #10#52, #10#0, 001;
     #10#52, #10#0, 999;
     ;
     NULL, LeeArti, NULL, NULL, NULL, GrabaTmp;
     #19#27;
|FIN;

|PROCESO Min;
     #0#1 = 1;
|FPRC;

|PROCESO Max;
     #0#1 = 999;
|FPRC;

|PROCESO TeclaEnter; |TIPO 0;
     |SI #1Campo = Contenido; |Y FSalida = 0;
          aPro = #1#0;
          nPre = #1#2;
          nDto = #1#5;
          |ERROR;
          |PTEC 806;
     |FINSI;
|FPRC;

|PROCESO ProveArtiLin; |TIPO 11;
     |SI FSalida = 13;
          |HAZ ProveArti;
     |FINSI;
|FPRC;

|PROCESO GrabaAlba;
     linea = linea + 1;
     #1#0 = #35#0;                  || PROVEEDOR.
     |LEE 101#1=;
     |SI FSalida ! 0;
          #1#1 = #35#1;              || NOMBRE.
          #1#2 = #80#6;             || PRECIO.
          #1#3 = #80#5;             || CANTIDAD.
          #1#4 = #77#1;             || FECHA
          #1#5 = #80#8;             || % DTO.
          #1#6 = #1#2 < #1#5;       || PRECIO NETO.
          |GRABA 020#1n;
     |SINO;
          |SI #80#8 > #1#5;
               #1#1 = #35#1;              || NOMBRE.
               #1#2 = #80#6;             || PRECIO.
               #1#3 = #80#5;             || CANTIDAD.
               #1#4 = #77#1;             || FECHA
               #1#5 = #80#8;             || % DTO.
               #1#6 = #1#2 < #1#5;       || PRECIO NETO.
               |GRABA 020#1a;
          |FINSI;
     |FINSI;
     |LIBERA #1;
|FPRC;

|PROCESO mini;
     #1#0 = "      ";
     #1#6 = 0;
|FPRC;

|PROCESO maxi;
     #1#0 = "zzzzzz";
     #1#6 = 99999999;
|FPRC;

|PROCESO BuscaAlba;
     |MENSA "2403Buscando....";
     |LEE 000#80p;
     |PARA; |SI FSalida = 0; |HACIENDO;
          |SI #0#2 = #80#2;
               #77#50 = #80#27;
               #77#0 = #80#0;
               |LEE 000#77=;
               |SI FSalida = 0;
                    #35#0 = #77#3;
                    |LEE 000#35=;
                    |SI FSalida ! 0; |DDEFECTO #35; |FINSI;
                    |HAZ GrabaAlba;
               |FINSI;
          |FINSI;
          |LEE 000#80s;
     |SIGUE;
     |BLIN 24;
|FPRC;

|PROCESO ProveArti; |TIPO 0;
     |SI FSalida ! 13; |ACABA; |FINSI; || F5

     |HAZ CreaTempo; aTempo1 = Tempo;
     |NOME_DAT #1 aTempo1; |DELINDEX #1;
     |ABRE #1; |ABRE #35; |ABRE #80; |ABRE #77;
     linea = 1;
     |HAZ BuscaAlba;
     |SI linea ! 1;
          aPro = "";
          |PUSHV 0501 2380;
          ||ENTLINEAL #1#1,1,4,22,1,mini,maxi;     || por proveedor
          |ENTLINEAL #2#1,-1,4,22,1,mini,maxi;     || por precio neto
          |POPV;
          |SI aPro ! "";
               #0#7 = aPro;             || PROVEEDOR.
               #0#10 = nPre;             || PRECIO.
               #0#9 = nDto;             || % DTO.
               |GRABA 020#0a;
               |PINTA #0#7;
               |PINTA #0#10;
               |PINTA #0#9;
          |FINSI;
     |SINO;
          |MENAV "0000No se encontraron compras anteriores.";
     |FINSI;
     |CIERRA #1; |CIERRA #35; |CIERRA #77; |CIERRA #80;
     |DELINDEX #1;
     Tempo = aTempo1; |HAZ BoraTempo;
     |ERROR;
|FPRC;

|PROCESO Tipo2; |TIPO 2;
     |SI #0#0 = "*"; |ACABA; |FINSI;

     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 1; |ERROR; |ACABA; |FINSI;

     |ABRE #41;
     #41#28 = #10#52;
     #41#0 = #10#0;
     #41#1 = #0#8;
     |LEE 020#41=;
     |SI FSalida = 0;
          |SI nModo = 3;
               |BORRA 020#41a;
          |SINO;
               #41#3 = #0#3;
               #41#5 = #0#6;
               |GRABA 020#41a;
          |FINSI;
     |FINSI;
     |CIERRA #41;
|FPRC;

|PROGRAMA;
     |MENSA "0000Albaran de Compra Automatico...";

     || 컴컴컴컴컴컴컴컴 Carga Def.
     |DBASE aPath;
     aMensa = ""; aDef1 = "";

     aDef1 = aPath + "def/" + "agifa071";
     |CARGA_DEF aDef1, expor071@;
     |SI FSalida ! 0;
          aMensa = "0000Error al cargar:" + aDef1;
          aDef1 = "";
          |MENAV aMensa; |ACABA;
     |FINSI;

     aAlfa = "|NC";
     aAlfa = #71#aAlfa#;
     nCampos = aAlfa;

     |HAZ CreaTempo; aTempo0 = Tempo;
     |NOME_DAT #0 aTempo0; |DELINDEX #0;

     |HAZ CreaTempo; aTempo41 = Tempo;
     |NOME_DAT #41 aTempo41; |DELINDEX #41;

     |ABRE #agifa142;
     |LEE 000#agifa142.p;
     |CIERRA #agifa142;

     |ABRE #41;
     |ABRE #10;
     #10#0 = enCodigo;
     #10#52 = eaSerie;
     |LEE 101#10=;
     |SI FSalida = 0;
          |ABRE #77;
          |ABRE #80;
          |BUCLE GrabaTmp;
          |CIERRA #80;
          |CIERRA #77;

          |ABRE #0;
          |LEE 000#0p;
          |SI FSalida = 0;
               #10#95 = "S";     ||Lo Marco
               |GRABA 020#10a;

               |PINPA #0#10;
               |PINDA #0#10;
               |ENTLINEAL #1#0, -2, 2, 20, 1, Min, Max;
          |FINSI;
          |CIERRA #0;

          |ABRE #71;
          |BUCLE GrabaVenta;
          |CIERRA #71;

          |HAZ LimCabAgifa010;
          |BUCLELIN 0 RecalcuVenta #10;
          |GRABA 020#10a;

          |ABRE #77;
          |ABRE #80;
          |ABRE #71;
          |ABRE #19;
          aProve = "";
          |BUCLE GrabaCompra;
          |SI aProve ! ""; |HAZ Recalcula077; |FINSI;
          |CIERRA #77;
          |CIERRA #80;
          |CIERRA #71;
          |CIERRA #19;
     |FINSI;
     |CIERRA #10;

     |CIERRA #41;
     |CIERRA #0;
     |DELINDEX #41;
     |DELINDEX #0;
     Tempo = aTempo0;  |HAZ BoraTempo;
     Tempo = aTempo41; |HAZ BoraTempo;

     ||컴컴컴컴컴컴 Descarga Def.
     |SI aDef1 ! ""; |DESCARGA_DEF #expor071@; |FINSI;
|FPRO;
