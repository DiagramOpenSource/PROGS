|FICHEROS;
     dsm00122 #0;
     dsm00123 #1;
     agifa024 #24;
     dsm00003 #3,MANTE;
     agifa321;
     agifa324;
     dsw00070 #70,MANTE;
|FIN;

|INCLUYE i_varart;

|VARIABLES;
     &nDeci_PreVis  = 0;
     &precio_divisa  = 0;
     &precio_base    = 0;
     &decim_base     = 0;
|FIN;

|PROCESO Tipo13; |TIPO 13;
     |DDEFECTO #24;
     |ABRE #24;
     #24#0 = #0#0;
     |LEE 000#24=;
     |CIERRA #24;

     |DDEFECTO #3;
     |ABRE #3;
     #3#0 = #0#0;
     #3#1 = #0#1;
     |LEE 000#3=;
     |CIERRA #3;

     |PINTA 723, #24#1;
     |PINTA 823, #3#10;
|FPRC;

|PROCESO Min;
     #3#0 = #0#0;
     #3#1 = 1;
|FPRC;

|PROCESO Max;
     #3#0 = #0#0;
     #3#1 = 9999;
|FPRC;

|PROCESO Tipo66; |TIPO 66;
     |ABRE #3;
     #3#0 = #0#0;
     #3#1 = #0#1;
     |LEE 000#3];
     |CONSULTA_F_CLAVE #1#3, Min, Max;
     |SI FSalida = 0;
          #0#1 = #3#1; |PINTA #0#1;
          |ERROR;
     |FINSI;
     |CIERRA #3;
|FPRC;

|PROCESO Tipo0; |TIPO 0;
     |ABRE #3;
     #3#0 = #0#0;
     #3#1 = #0#1;
     |LEE 000#3=;
     |SI FSalida ! 0;
          |MENAV "0000Direccion inexistente...";
          |ERROR;
     |SINO;
          |HAZ Tipo13;
     |FINSI;
     |CIERRA #3;
|FPRC;


|PROCESO LeeMon0; |TIPO 0; ||Lee los parametros de la moneda base
     |HAZ LeeMon;
|FPRC;

|PROCESO LeeMon; |TIPO 13; ||Lee los parametros de la moneda base
     |ABRE #24;
     #24#0 = #0#0;
     |LEE 000#24=;
     |CIERRA #24;

     |HAZ LeeMonBase;
     |ABRE #agifa324;
     #agifa324#0 = #24#192;
     |LEE 001#agifa324.=;          || Leo la divisa
     |CIERRA #agifa324;
     precio_divisa = #agifa324#9;  || Decimales en el precio de la divisa

     |SI #24#193 = "B";           || Si trabajo con la moneda base ...
          nDeci_PreVis   = precio_divisa;
     |SINO;                        || Si trabajo con la divisa ...
          nDeci_PreVis   = precio_base;
     |FINSI;
|FPRC;

|PROCESO LeeMonBase; ||Lee los parametros de la moneda base
     |ABRE #agifa321;
     |ABRE #agifa324;
     |LEE 010#agifa321.p;         ||Leo parametros de divisa
     #agifa324#0 = #agifa321#9;
     |LEE 010#agifa324.=;         ||Leo la ficha de la moneda base en Divisas
     decim_base = #agifa324#7;    ||Asigno los decimales de la moneda base
     precio_base = #agifa324#9;   ||Asigno los decimales del precio en la mon. base
     |CIERRA #agifa324;
     |CIERRA #agifa321;
|FPRC;

|PROCESO Llena123; |TIPO 6;
     enAlta = 1;
     eaArticulo = #1Campo;
     |HAZ LlenaArticulo;
     |SI FSalida = 0;
          #1Campo = eaArticulo;
          |PINTA #1Campo;
     |SINO;
          |ERROR; |ACABA;
     |FINSI;
|FPRC;

|PROCESO Copia;
     #1#0 = #70#4;
     #1#1 = #70#6;
     |GRABA  020#1n;
|FPRC;

|PROCESO Tipo122_20; |TIPO 20;
     |SI FSalida = "OPCION";
          FSalida = " Duplicar ";
     |SINO;
          |PUSHV 05012380;
          |BLANCO 7101874;
          |DDEFECTO #70;
          #70#0 = #0#0;
          #70#2 = #0#1;
          #70#4 = #0#0;
          |PINPA #0#70;
          |PINDA #0#70;
          |ENDATOS #1#70;
          |SI FSalida = 0;
               #0#0 = #70#0;
               #0#1 = #70#2;
               |LEE 121#0=;
               |SI FSalida = 0;
                    |ABRE #1;
                    |BUCLELIN 2Copia#0;
                    |CIERRA #1;
                    #0#0 = #70#4;
                    #0#1 = #70#6;
                    |GRABA 020#0n;
                    |SI FSalida = 0;
                         |MENAV "0000Tarifa Duplicada...";
                    |FINSI;
               |FINSI;
          |FINSI;
          |POPV;
     |FINSI;
|FPRC;
