|FICHEROS;
     pcegz019 #0;
     dsm00003 #3;   ||Dir
     pcegm004 #4;
     pcegm006 #6;
     agifa010 #10;  ||Alb Vta
     pcegm015 #15;  ||Aux Age
     pcegm020 #20;  ||Parametros
     pcegm021 #21;  ||Aux CP
     pcegm022 #22;  ||Aux Paises
     pcegm023 #23;  ||Etiquetas
     agifa024 #24;
     pcegm042 #42;
     agifa071 #71;
     agifa077 #77;  ||Alb Com
     agifa081 #81;  ||Ped Com
     agifa112 #112;
     pcegm024 #124;
|FIN;

|VARIABLES;
     &enDigito = 0;
     &eaCodigo = "";
     &eaPaquete = "";
     &eaBulto = "";
     &eaNumBulto = "";
     aParam = "";
     aProvin = "";
     i = 0;
     aObs1 = "";
     aObs2 = "";
     aRefeCli = "";
     aClavePor = "";
     aDir = "";
     aPobla = "";
     aCP = "";
     aNombre = "";
     aPais = "";
     fFecha = @;
     aTipo = "";
     nExpedicion = 0;
     aSerie = "";
     nNumero = 0;
     &SaltoLinea = "";
     &eaTipo = "";
     &eaSerie = "";
     &enDocu = 0;
     &eaPregunta = "";
     nConta = 0;
     aInforme = "";
     aAlfa = "";
     nAgencia = 0;
     aTlf = "";
     aContacto = "";
     nBultos = 0;
     nKilos = 0;
     aCliente = "";
     nDirEnt = 0;
     TC = "";
     CC = "";
     NB = "";
     CP = "";
     nTotal = 0;
     aFP = "";

     aNume = "";
     aCar = "";
     nSuma = 0;
     nSup = 0;
     nNum = 0;
     nDig = 0;
     nPos = 0;
     j = 0;
|FIN;

|PROCESO DigControl;     || entra=aNume, salida=nDig
     aAlfa = aNume;
     nSuma = 0;
     |PARA j = 1; |SI j [ 22; |HACIENDO j = j + 1;
          nPos = j * 100 + 1;
          aCar = aAlfa % nPos;
          nNum = j $ 2;
          |SI nNum = 0;
               nNum = aCar;
               nSuma = nSuma + (nNum * 3);
          |SINO;
               nNum = aCar;
               nSuma = nSuma + nNum;
          |FINSI;
     |SIGUE;
     |PARA nSup = nSuma; |SI; |HACIENDO nSup = nSup + 1;
          nNum = nSup $ 10;
          |SI nNum = 0; |SAL; |FINSI;
     |SIGUE;
     nDig = nSup - nSuma;
|FPRC;

|PROCESO Etiqueta;
     |DDEFECTO #23;
     #23#0 = aTipo;
     #23#1 = aSerie;
     #23#2 = nNumero;
     #23#3 = nAgencia;
     #23#4 = nExpedicion;
     #23#5 = fFecha;

     |SI nAgencia = #20#2;    || Chronoexpres
          #124#0 = aPais;
          |LEE 000#124=;
          |SI FSalida = 0;
               aPais = #124#1;
          |SINO;
               aPais = "";
          |FINSI;
     |FINSI;

     #23#6 = aPais;
     |QBF aPais;

     aAlfa = aCP ! 102;
     |SI aAlfa = "35"; |O aAlfa = "38";
          |SI aPais = "ES";
               #23#7 = "A";
          |FINSI;
     |FINSI;

     #23#8 = aNombre;
     #23#9 = aDir;
     #23#10 = aPobla;

     |SI nAgencia = #20#2;    || Chronoexpres
          #23#11 = aCP;
          #23#12 = aProvin;
          #23#13 = "";

          |SI aFP = #20#13; |O aFP = #20#14; |O aFP = #20#15;
               #23#20 = nTotal;
               #23#21 = "P";
          |FINSI;
     |SINO;
          #23#11 = aCP;

          |SI aPais = "ES"; |O aPais = "PT";
               |DDEFECTO #21;
               #21#0 = aPais;
               |SI aPais = "PT";
                    #21#1 = "7" + aCP;
               |SINO;
                    #21#1 = aCP;
               |FINSI;
               |LEE 000#21=;
               #23#12 = #21#2;
               #23#13 = #21#3;
          |SINO;
               |DDEFECTO #22;
               #22#0 = aPais;
               |LEE 000#22=;
               #23#12 = #22#1;
               #23#13 = aPais;
          |FINSI;
          |SI aFP = #20#10; |O aFP = #20#11; |O aFP = #20#12;
               #23#20 = nTotal;
               #23#21 = "P";
          |FINSI;
     |FINSI;

     #23#14 = aTlf;
     #23#15 = aContacto;
     #23#16 = aClavePor;
     #23#17 = aRefeCli;

     |SI nBultos = 0; nBultos = 1; |FINSI;
     |SI nKilos = 0; nKilos = 1; |FINSI;

     #23#18 = nBultos;   ||?
     #23#19 = nKilos;    ||?
     #23#22 = "";        ||?
     #23#23 = "";        ||?
     #23#24 = "";        ||?
     #23#25 = aObs1;
     #23#26 = aObs2;
     #23#27 = "";        ||?
     #23#28 = "";        ||?
     |SI aPais = "ES";
          #23#29 = "102";
     |SINO;
          #23#29 = "110";
     |FINSI;
     #23#30 = "";        ||?
     #23#31 = "EUR";      ||
     #23#32 = aCliente;
     #23#33 = nDirEnt;

     |SI nAgencia = #20#2;    || Chronoexpres
          aAlfa = ("00000000" + nExpedicion) % -108;
          #23#36 = aAlfa;

          |SI aClavePor = "P";
               #23#35 = #20#5;
          |SINO;
               #23#35 = #20#6;
          |FINSI;

          ||TC=2, CC=4, NB=9, Bultos=2, CP=5
          TC = #23#35;
          CC = #20#7;
          NB = ("000000000000" + #23#4) % -109;
          |SI aPais = "";
               CP = aCP % 105;
          |SINO;
               CP = "99999";
          |FINSI;

          #23#34 = TC + CC + NB + "01" + CP;

          aNume = (#23#34 % 115) + (("00" + #23#18) % -102) + (#23#34 % 1805);
          |HAZ DigControl;
          #23#36 = (#23#34 % 115) + nDig;
     |SINO; || Dhl
          aAlfa = ("00000000" + nExpedicion) % -107;
          aAlfa = "02" + aAlfa + "0";
          #23#36 = aAlfa;
     |FINSI;
|FPRC;

|PROCESO Imprimela;
     |DDEFECTO #15;
     #15#0 = nAgencia;
     |LEE 000#15=;
     Impresora = #15#1; |QBF Impresora;
     |SI Impresora = "";
          aAlfa = "0000No esta definida la impresora para la agencia:" + #15#0;
          |MENAV aAlfa;
     |FINSI;

     |IMPRE -1;
     |SI FSalida ! 0;
          aAlfa = "0000Error al abrir la impresora " + Impresora;
          |MENAV aAlfa;
          |ERROR; |ACABA;
     |FINSI;

     aInforme = #15#5; |QBF aInforme;
     |INFOR aInforme;
     |SI FSalida = 0;
          nBultos = #23#18;
          eaPaquete = ("00000000" + #23#4) % -108;
          eaBulto = ("000" + nBultos) % -103;
          |PARA i = 1; |SI i [ nBultos; |HACIENDO i = i + 1;
               eaNumBulto = ("000" + i) % -103;

               aNume = (#23#34 % 115) + (("00" + i) % -102) + (#23#34 % 1805);
               |HAZ DigControl;
               eaCodigo = aNume + nDig;
               enDigito = nDig;

               |PRINT;
               |PIEINF;
          |SIGUE;
          |FININF;
     |SINO;
          aAlfa = "0000Error en informe '" + aInforme + "', agencia: " + #15#0;
          |MENAV aAlfa;
     |FINSI;
     |FINIMP;
|FPRC;

|PROCESO Imprime;
     |SI nAgencia ! #20#1; |Y nAgencia ! #20#2;
          #23#0 = aTipo;
          #23#1 = aSerie;
          #23#2 = nNumero;
          |LEE 101#23=;
          |SI FSalida = 0;
               |BORRA 020#23a;
          |FINSI;
          |ACABA;
     |FINSI;

     |SI eaPregunta = "S";
          aAlfa = "0000NImprimir etiquetas envio:" + eaSerie + "/" + (("000000" + enDocu) % -106);
          |CONFI aAlfa;
          |SI FSalida ! 0; |ACABA; |FINSI;
     |FINSI;

     #23#0 = aTipo;
     #23#1 = aSerie;
     #23#2 = nNumero;
     |LEE 000#23=;
     |SI FSalida = 0;
          |SI #0#4 = "N"; |ACABA; |FINSI;
          |SI nAgencia = #23#3; |Y nBultos = #23#18;
               |HAZ Imprimela; || si existe la etiqueta y no se ha cambiado
               |ACABA;         || no se regenera
          |FINSI;
     |SINO;
          |SI #0#4 = "S"; |ACABA; |FINSI;
     |FINSI;
     |SI FSalida = 0;
          |LEE 121#23=;
          |SI nAgencia = #23#3;
               nExpedicion = #23#4;
          |SINO;
               nExpedicion = -1;
          |FINSI;
          |BORRA 020#23a;
          |LIBERA #23;
     |SINO;
          nExpedicion = -1;
     |FINSI;

     #15#0 = nAgencia;
     |LEE 101#15=;
     |SI FSalida = 0; |Y #15#4 ! 0;
          |SI nExpedicion = -1;
               nExpedicion = #15#4;
               #15#4 = #15#4 + 1;
               |SI #15#4 > #15#3;
                    #15#4 = #15#2;
               |FINSI;
               |GRABA 020#15a;
               |LIBERA #15;
          |FINSI;
     |FINSI;

     |HAZ Etiqueta;
     |GRABA 020#23n;
     |HAZ Imprimela;
|FPRC;

|PROCESO agifa081;
     |DDEFECTO #112;
     #112#0 = #81#4;
     |LEE 000#112=;

     aTipo = "PC";
     aSerie = #81#25;
     nNumero = #81#0;
     fFecha = #81#1;
     nAgencia = #81#45;
     aPais = #112#89;
     aNombre = #81#8;
     aDir = #112#3;
     aPobla = #112#4;
     aProvin = #112#5;
     aCP = #112#108;
     aTlf = #112#6;
     aContacto = #112#23;
     nBultos = 1;
     aCliente = "";
     nDirEnt = 0;
     aClavePor = "P";
     aRefeCli = #81#25 + "/"+ (("000000" + #81#0) % -106);
     aObs1 = "";
     aObs2 = "";
     nTotal = 0;
     aFP = "";
     |HAZ Imprime;
     |LIBERA #81;
|FPRC;

|DEFBUCLE agifa081;
     #81#1;
     ;
     #0#0, #0#2;
     #0#1, #0#3;
     ;
     NULL, agifa081;
|FIN;

|PROCESO agifa077;
     |DDEFECTO #112;
     #112#0 = #77#3;
     |LEE 000#112=;

     aTipo = "AC";
     aSerie = #77#50;
     nNumero = #77#0;
     fFecha = #77#1;
     nAgencia = #77#72;
     aPais = #112#89;
     aNombre = #77#4;
     aDir = #112#3;
     aPobla = #112#4;
     aProvin = #112#5;
     aCP = #112#108;
     aTlf = #112#6;
     aContacto = #112#23;
     nBultos = 1;
     aCliente = "";
     nDirEnt = 0;
     aClavePor = "";
     aRefeCli = #77#50 + "/" + (("000000" + #77#0) % -106);
     aObs1 = "";
     aObs2 = "";
     nTotal = 0;
     aFP = "";
     |HAZ Imprime;
     |LIBERA #77;
|FPRC;

|DEFBUCLE agifa077;
     #77#1;
     ;
     #0#0, #0#2;
     #0#1, #0#3;
     ;
     NULL, agifa077;
|FIN;

|PROCESO LeePedido;
     aTlf = "";
     aContacto = "";
     aObs1 = "";
     aObs2 = "";
     |ABRE #71;
     #71#29 = #10#52;
     #71#0 = #10#0;
     #71#1 = 1;
     |LEE 000#71];
     |SI FSalida = 0; |Y #71#29 = #10#52; |Y #71#0 = #10#0;
          |ABRE #4;
          #4#25 = #71#31;
          #4#0 = #71#12;
          |LEE 000#4=;
          |SI FSalida = 0;
               aTlf = #4#77; |QBF aTlf;
               aContacto = #4#73; |QBF aContacto;
               aClavePor = #4#61;
               aPais = #4#74;
               aProvin = #4#21;
               aObs1 = ""; aObs2 = "";
               |ABRE #6;
               #6#0 = #4#25;
               #6#1 = #4#0;
               #6#2 = 1;
               |LEE 000#6=;
               |SI FSalida = 0;
                    aObs1 = #6#3; |QBF aObs1;
               |FINSI;
               #6#2 = 2;
               |LEE 000#6=;
               |SI FSalida = 0;
                    aObs2 = #6#3; |QBF aObs2;
               |FINSI;
               |CIERRA #6;
          |FINSI;
          |CIERRA #4;
     |FINSI;
     |CIERRA #71;
|FPRC;

|PROCESO agifa010;
     |DDEFECTO #3;
     #3#0 = #10#3;
     #3#1 = #10#84;
     |LEE 000#3=;

     aTipo = "AV";
     aSerie = #10#52;
     nNumero = #10#0;
     fFecha = #10#1;
     nAgencia = #10#88;
     aPais = #3#19;
     aProvin = #3#7;
     aNombre = #10#29;
     aDir = #10#30;
     aPobla = #10#31;
     aCP = #10#62;
     aClavePor = #10#64;
     |HAZ LeePedido;
     |SI aTlf = ""; aTlf = #3#13; |FINSI;
     |SI aContacto = ""; aContacto = #3#22; |FINSI;
     nBultos = #10#2;
     aCliente = #10#3;
     nDirEnt = #10#84;
     aRefeCli = #10#52 + "/" + (("000000" + #10#0) % -106);
     nTotal = #10#67;
     aFP = #10#8;
     |HAZ Imprime;
     |LIBERA #10;
|FPRC;

|DEFBUCLE agifa010;
     #10#1;
     ;
     #0#0, #0#2;
     #0#1, #0#3;
     ;
     NULL, agifa010;
|FIN;

|PROCESO pcegm042;
     |DDEFECTO #3;
     #3#0 = #42#3;
     #3#1 = #42#5;
     |LEE 000#3=;

     |DDEFECTO #24;
     #24#0 = #42#3;
     |LEE 000#24=;

     aTipo = "RE";
     aSerie = #42#0;
     nNumero = #42#1;
     fFecha = ~; ||    #42#2;
     nAgencia = #42#20;
     nTotal = 0;
     aFP = "";
     nBultos = 1;

     |ABRE #4; |SELECT #10#4; ||Coge la agencia del albaran
     #4#95 = #42#0;
     #4#96 = #42#1;
     |LEE 000#4=;
     |SI FSalida = 0;
          |ABRE #71; |SELECT #4#71;
          #71#31 = #4#25;
          #71#12 = #4#0;
          #71#21 = 1;
          |LEE 000#71];
          |SI FSalida = 0; |Y #71#31 = #4#25; |Y #71#12 = #4#0;
               |ABRE #10;
               #10#52 = #71#29;
               #10#0 = #71#0;
               |LEE 000#10=;
               |SI FSalida = 0;
                    nAgencia = #10#88;
                    nTotal = #10#67;
                    aFP = #10#8;
                    nBultos = #10#2;
                    |SI nBultos = 0; nBultos = 1; |FINSI;
               |FINSI;
               |CIERRA #10;
          |FINSI;
          |CIERRA #71;
     |SINO;
          |ABRE #10;
          #10#52 = #42#25;
          #10#0 = #42#26;
          |LEE 000#10=;
          |SI FSalida = 0;
               nAgencia = #10#88;
               nTotal = #10#67;
               aFP = #10#8;
               nBultos = #10#2;
               |SI nBultos = 0; nBultos = 1; |FINSI;
          |FINSI;
          |CIERRA #10;
     |FINSI;
     |CIERRA #4;

     |ABRE #4;
     |SELECT #10#4;
     #4#95 = #42#0;
     #4#96 = #42#1;
     |LEE 000#4=;
     |SI FSalida = 0;
          aPais = #4#74;
          aNombre = #4#19;
          aDir = #4#14;
          aPobla = #4#20;
          aProvin = #4#21;
          aCP = #4#59;
          aTlf = #4#77;
          aContacto = #4#73;
          aCliente = #4#4;
          nDirEnt = #4#57;
     |SINO;
          aPais = #3#19;
          aNombre = #24#1;
          aDir = #3#2;
          aPobla = #3#3;
          aProvin = #3#7;
          aCP = (("00"  + #3#4) % -102) + (("000" + #3#5) % -103);
          aTlf = #24#17;
          aContacto = #3#22;
          aCliente = #3#0;
          nDirEnt = #3#1;
     |FINSI;
     |CIERRA #4;


     aClavePor = "P";
     aObs1 = "";
     aObs2 = "";
     aRefeCli = #42#0 + "/" + (("000000" + #42#1) % -106);
     |HAZ Imprime;
     |LIBERA #42;
|FPRC;

|DEFBUCLE pcegm042;
     #42#1;
     22;
     #0#0, #0#2, "C";
     #0#1, #0#3, "C";
     ;
     NULL, pcegm042;
|FIN;

|PROCESO Tipo3; |TIPO 3;
     |ABRE #20; |LEE 000#20p; |CIERRA #20;
     |ABRE #3;
     |ABRE #15;
     |ABRE #21;
     |ABRE #22;
     |ABRE #23;
     |ABRE #24;
     |ABRE #112;
     |ABRE #124;
     |SI #0#5 = "AV"; |BUCLE agifa010; |FINSI;
     |SI #0#5 = "AC"; |BUCLE agifa077; |FINSI;
     |SI #0#5 = "PC"; |BUCLE agifa081; |FINSI;
     |SI #0#5 = "RE"; |BUCLE pcegm042; |FINSI;
     |CIERRA #124;
     |CIERRA #112;
     |CIERRA #24;
     |CIERRA #23;
     |CIERRA #22;
     |CIERRA #21;
     |CIERRA #15;
     |CIERRA #3;
|FPRC;

|PROGRAMA;
     SaltoLinea = &13;
     SaltoLinea = SaltoLinea + &10;
     |SI enDocu = 0;
          |MANTE #0;
     |SINO;
          #0#0 = eaSerie;
          #0#1 = eaSerie;
          #0#2 = enDocu;
          #0#3 = enDocu;
          #0#4 = "X";
          #0#5 = eaTipo;
          aParam = PARAMETRO; |QBF aParam;
          |SI aParam = "BORRA";
               |ABRE #23;
               #23#0 = eaTipo;
               #23#1 = eaSerie;
               #23#2 = enDocu;
               |LEE 101#23=;
               |SI FSalida = 0;
                    |BORRA 020#23a;
               |FINSI;
               |CIERRA #23;
          |SINO;
               |HAZ Tipo3;
          |FINSI;
     |FINSI;
|FPRO;
