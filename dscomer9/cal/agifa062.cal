|FICHEROS;
 agifa062 #0;
 agifa065 #1;  || Historico
 agifa010 #2;  || Albaranes / Abonos Ventas
 agifa077 #3;  || Albaranes / Abonos Compras
 agifa084 #4;  || Facturas contado
 agifa007 #5;  || Series Para Saber si es mov. interno
|FIN;

|VARIABLES;
  informe = "agifa062";
  &oper = "";
  &fecha = @;
  &codi = "";
  &num  = 0;

  swim = 0;
|FIN;

|PROCESO mayus;
 #0Campo = #0Campo > " ";
 |PINTA #0Campo;
|FPRC;



|PROCESO documento;
 swim = 0;
 oper = "";
 codi  = #1#2;
 fecha = #1#1;
|| ....... Lee la Serie por si es Interno
 |DDEFECTO #5;
 #5#26 = #1#11; ||Serie
 |LEE 001#5=;
 |SI #5#37 = "S";
     |SI codi = "AC"; codi = "AV"; |FINSI;
     |SI codi = "BC"; codi = "BV"; |FINSI;
 |FINSI;


 |SI codi = "AV";|O codi = "BV";||Venta;
     |DDEFECTO #2;
     #2#0  = #1#3; ||Documento;
     #2#52 = #1#11;||Serie;
     |LEE 000#2=;
     |SI FSalida ! 0;
         oper = "NO EXISTE ALBARAN DE VENTA.";
         swim = 1;
     |SINO;
         |SI #2#1 ! #1#1;
             oper  = "FECHA CAMBIADA EN ALBARAN DE VENTA.";
             fecha = #2#1;
             swim  = 1;
             |SI #0#1 = "S";
                 |BORRA 021#1a;
                 #1#1 = #2#1;
                 |GRABA 040#1n;
                 |LIBERA #1;
             |FINSI;
         |FINSI;
     |FINSI;
 |FINSI;

 |SI codi = "AC";|O codi = "BC";||Compras;
     |DDEFECTO #3;
     #3#0  = #1#3; ||Documento;
     #3#50 = #1#11;||Serie;
     |LEE 000#3=;
     |SI FSalida ! 0;
         oper = "NO EXISTE ALBARAN DE COMPRA. ";
         swim  = 1;
     |SINO;
         |SI #3#1 ! #1#1;
             oper  = "FECHA CAMBIADA EN ALBARAN DE COMPRA.";
             fecha = #3#1;
             swim  = 1;
             |SI #0#1 = "S";
                 |BORRA 021#1a;
                 #1#1 = #3#1;
                 |GRABA 040#1n;
                 |LIBERA #1;
             |FINSI;
          |FINSI;
     |FINSI;
 |FINSI;

 |SI codi = "FC"; ||Facturas Contado;
     |DDEFECTO #4;
     #4#0  = #1#3;  ||Documento;
     #4#48 = #1#11; ||Serie;
     |LEE 000#4=;
     |SI FSalida ! 0;
         oper  = "NO EXISTE FACTURA CONTADO.";
         swim = 1;
     |SINO;
         |SI #4#1 ! #1#1;
             oper  = "FECHA CAMBIADA EN FACTURA CONTADO.";
             fecha = #4#1;
             swim = 1;
             |SI #0#1 = "S";
                 |BORRA 021#1a;
                 #1#1 = #4#1;
                 |GRABA 040#1n;
                 |LIBERA #1;
             |FINSI;
         |FINSI;
     |FINSI;
 |FINSI;

 |SI swim = 1;
     |SI #5#37 = "S";
         |SI codi ! #1#2; codi = #1#2; |FINSI;
         codi = codi + "(In.)";
     |FINSI;
     |PRINT;
     num = num + 1;
 |FINSI;
|FPRC;

|DEFBUCLE hist;
     #1#1;
     ;
     INICIO;
     FINAL;
     be;
     NULL,documento;
|FIN;

|PROCESO comprueba;|TIPO 3;

 |SI #0#0 ! "S"; |ACABA; |FINSI;

 |IMPRE 0;
 |SI FSalida ! 0; |ACABA; |FINSI;

 |INFOR informe;
 |SI FSalida ! 0; |FINIMP; |ACABA; |FINSI;

 |ABRE #2;
 |ABRE #3;
 |ABRE #4;
 |ABRE #5;
 |BUCLE hist;
 |CIERRA #2;
 |CIERRA #3;
 |CIERRA #4;
 |CIERRA #5;

 |SI num ! 0; |PIEINF; |FINSI;

 |FININF;
 |FINIMP;
|FPRC;
