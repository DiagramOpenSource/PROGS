|FICHEROS;
     diam0100 #100;
     diam0101 #101;
     diam0102 #102;
     diam0103 #103;
     diam0104 #104;

     diaw0013 #913,MANTE;
     diaw0014 #914,MANTE;
     diaw0016 #916,MANTE;
     diaw0017 #917,MANTE;

     agifa019 #19;
     agifa077 #77;
     agifa072 #72;
     agifa079 #79;
     agifa080 #80;
     agifa112 #112;
     agif0041 #41;
     agifa321 #321;
     agifa324 #324;

     :/dspartes/def/dsparm05 #500;
||     :/dserp/def/dserpm09 #600;

     dsparm01@ #110;
     dstelw01@ #111;
     dstelm02@ #200;
     dstelm03@ #300;
|FIN;

|VARIABLES;
     nImpo = 0;
     nTota = 0;
     &enTiva = 0;
     &efFiva = @;
     &eaDiva = "";
     &enIva = 0;
     &enRec = 0;
     nPanta       = 0;
     nPosi1       = 0;
     nPosi2       = 0;
     nRango2      = 0;
     nRango       = 0;
     nGrid917     = 0;
     nGrid100     = 0;
     nGrid101     = 0;
     nBoton       = 0;
     nBoton1      = 0;
     nBoton2      = 0;
     nBoton3      = 0;
     nBoton4      = 0;
     nBoton5      = 0;
     nBoton6      = 0;
     nBoton7      = 0;
     nBoton8      = 0;
     nVentana     = 0;
     nErr         = 0;
     nTiquet      = 0;
     nLin         = 0;
     nPPort1      = 0;
     nPPort2      = 0;
     nImpDiv      = 0;
     nImporte     = 0;
     nHandle      = 0;
     nVto         = 0;
     nVto1        = 0;
     nNumAnt      = 0;
     nComodNum    = 0;
     nLinea       = 0;
     nModo        = 0;
     nForma       = 0;
     nNeto        = 0;
     nBruto       = 0;
     nRetencion   = 0;

     aTecla       = "";
     aEsc1        = "";
     aEsc2        = "";
     aRetu        = "";
     aIPRemoto    = "";
     aBase        = "";
     aFichero     = "";
     aDirDocu     = "";
     aOrigen      = "";
     aDestino     = "";
     aAlfa        = "";
     aDef1        = "";
     aDef2        = "";
     aDef3        = "";
     aDef10       = "";
     aDat         = "";
     aDirOri      = "";
     aDirDes      = "";
     aDSCorreo    = "";
     aProxy       = "";
     aTitulo      = "";
     aTexto       = "";
     aFic         = "";
     aHora        = "";
     aDirEmp      = "";
     aDatos       = "";
     aArticulo    = "";
     aDocRemoto   = "";
     aDocServidor = "";
     aPathRemoto  = "";
     aPathDSErp   = "";
     aDirDocs     = "";
     aComodin     = "";
     aExtension   = "";
     aNomeDat     = "";
     aDoc         = "";

     &enForma      = 0;
     &eaDocu       = "";
     &nDeci_PreDiv = 0;
     &enSwMenu     = 1;
     &aDivisa      = "";
     &aMoneda      = "";
     &cCodMonTrab  = "";
     &qSerie       = "";
     &qSerieF      = "";
     &qAlbar       = 0;
     &qFecha       = @;
     &qAbono       = "";
     &qSufac       = "";
     &enError      = 0;
     &enErrCarte   = 0;
     &enContaFac   = 0;

 {-1}aMenu = "";
     aMenu1 = "2240";
     aMenu2 = "1";
     aMenu3 = "3";
     aMenu4 = " Modificar ";
     aMenu5 = " Aceptar ";
     aMenu6 = " Cancelar ";

   {1} aContiene     = "";
|FIN;

|| ------------------------------
|| Procesos del diaw0017
|| ------------------------------

|PROCESO Tipo7w17;  |TIPO 7;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 1;
         |MENAV "0000 No se puede dar de Alta, cree los Artculos en el Control de Proveedores";
         |PTEC 806;
     |FINSI;
|FPRC;

|PROCESO Tipo2w17;  |TIPO 2;
     #913#9 = #913#9 +. #917#4;

     |PINTA #913#9;
|FPRC;

|PROCESO TLinea;  |TIPO 0;
     |ABRE #19;
     #19#0 = #917#1;
     |LEE 000#19=;
     |SI FSalida ! 0;  |DDEFECTO #19;  |FINSI;
     |CIERRA #19;
     |ABRE #112;
     #112#0 = #913#0;
     |LEE 000#112=;
     |SI FSalida ! 0;  |DDEFECTO #112;  |FINSI;
     |CIERRA #112;

     enTiva = #19#30;
     efFiva = #100#3;
     |HAZ SacaIVA;

     #917#4 = #917#3 > enIva;

     nBruto =  #917#3;
     nNeto =  #917#4;
     nRetencion  = 0;
     |SI #913#10 ! 0;
          |SI #19#30 = 0;
               |SI #112#93 = "S";
                    |SI #112#86 = "B";
                       nRetencion = nBruto % #913#10;
                    |SINO;
                       nRetencion = nNeto  % #913#10;
                    |FINSI;
               |FINSI;
          |SINO;
               |SI #112#86 = "B";
                  nRetencion = nBruto % #913#10;
               |SINO;
                  nRetencion = nNeto  % #913#10;
               |FINSI;
          |FINSI;
     |FINSI;
     #917#4 = nNeto - nRetencion;

     |PINTA #917#4;
|FPRC;

|| ------------------------------
|| Procesos del diaw0016
|| ------------------------------

|PROCESO Tipo12w16;  |TIPO 12;
     aMenu2 = "1";
     |MENUG aMenu;
     aMenu2 = FSalida;
|FPRC;

|| ------------------------------
|| Procesos del diaw0014
|| ------------------------------

|PROCESO Tipo12w14;  |TIPO 12;
     aMenu2 = "1";
     |MENUG aMenu;
     aMenu2 = FSalida;
|FPRC;

|PROCESO FechaInicio;  |TIPO 7;
     |SI #914#1 = "01.01.0000";
         #914#1 = #100#3;
         #914#2 = #914#1 + 0.000001;
         |PINTA #914#1;
         |PINTA #914#2;
     |FINSI;
|FPRC;

|| ------------------------------
|| Procesos del diaw0013
|| ------------------------------

|PROCESO Tipo12w13;  |TIPO 12;
     |SI #913#12 ! 0;
         |ABRE #79;
         #79#66 = #913#11;
         #79#0  = #913#12;
         |LEE 000#79=;
         |SI FSalida = 0;
             |MENAV "0000 La factura compra ya existe.";
             |CIERRA #79;
             aMenu2 = "1";
             |ACABA;
         |FINSI;
     |FINSI;

     aMenu2 = "1";
     |MENUG aMenu;
     aMenu2 = FSalida;

     |SI #913#5 = "      ";  |Y aMenu2 = "2";
         |MENAV "       Introduzca un Codigo de Cliente.";
         aMenu2 = "1";
     |FINSI;
|FPRC;

|PROCESO LeeSerie;
     |SI nBoton = 4;  |ACABA;  |FINSI;

     #101#0 = #100#0;
     #101#1 = #100#1;
     #101#2 = #100#2;
     #101#3 = #914#0;
     |LEE 000#101=;
     |SI FSalida = 0;
         |MENAV "0000 La Serie ya Existe.";
         |ERROR;
     |FINSI;
|FPRC;

|PROCESO MiraCliente;
     |SI #913#5 = "      ";
         |MENAV "       Introduzca un Codigo de Cliente.";
         |ERROR;
         |ACABA;
     |FINSI;
|FPRC;

|PROCESO Baja79;
     #79#66 = #913#11;
     #79#0  = 1;
|FPRC;

|PROCESO Alta79;
     #79#66 = #913#11;
     #79#0  = 999999;
|FPRC;

|PROCESO Tipo66C12Fw13;  |TIPO 66;
     |ABRE #79;
     #79#66 = #913#11;
     #79#0  = #913#12;
     |LEE 000#79];

     |CONSULTA_F_CLAVE #1#79, Baja79, Alta79;
     |CIERRA #79;
|FPRC;

|PROCESO Tipo0C12Fw13;  |TIPO 0;
     |SI #913#12 = 0;  |ACABA;  |FINSI;

     |ABRE #79;
     #79#66 = #913#11;
     #79#0  = #913#12;
     |LEE 000#79=;
     |SI FSalida = 0;
         |MENAV "0000 La factura compra ya existe.";
         |ERROR;
     |FINSI;
     |CIERRA #79;
|FPRC;

|| ------------------------------
|| Procesos del diam0100
|| ------------------------------

|PROCESO ClaveBaja100_1;
     #100#4 = "!";
     #100#0 = "";
     #100#1 = "";
     #100#2 = 01;
|FPRC;

|PROCESO ClaveAlta100_1;
     #100#4 = "zzzzzz";
     #100#0 = "zzzzzz";
     #100#1 = "zzzzzzzzzzzzzzzzzzzz";
     #100#2 = 99;
|FPRC;

|PROCESO ClaveBaja100_2;
     #100#4 = "";
     #100#0 = "";
     #100#1 = "";
     #100#2 = 01;
|FPRC;

|PROCESO ClaveAlta100_2;
     #100#4 = "";
     #100#0 = "zzzzzz";
     #100#1 = "zzzzzzzzzzzzzzzzzzzz";
     #100#2 = 99;
|FPRC;

|PROCESO ClaveBaja101;
     #101#0 = #100#0;
     #101#1 = #100#1;
     #101#2 = #100#2;
     #101#3 = "";
|FPRC;

|PROCESO ClaveAlta101;
     #101#0 = #100#0;
     #101#1 = #100#1;
     #101#2 = #100#2;
     #101#3 = "zzzzzzzzzzzzzzzzzzzz";
|FPRC;

|PROCESO Evento100;
     |SI FSalida = 1;
         |LEE 000#100=;
         |SI FSalida = 0;
             |ESTADO_CONTROL nBoton1, "ENABLE";
             |ESTADO_CONTROL nBoton3, "ENABLE";
             |ESTADO_CONTROL nBoton5, "ENABLE";
             |ESTADO_CONTROL nBoton2, "ENABLE";

             |REFRESCACONTROL nGrid101;

             #101#0 = #100#0;
             #101#1 = #100#1;
             #101#2 = #100#2;
             #101#3 = "";
             |LEE 000#101];
             |SI FSalida = 0;  |Y  #101#0 = #100#0;  |Y #101#1 = #100#1;
              |Y #101#2 = #100#2;
                 |ESTADO_CONTROL nBoton4, "ENABLE";
                 |ESTADO_CONTROL nBoton6, "ENABLE";
             |SINO;
                 |ESTADO_CONTROL nBoton4, "DISABLE";
                 |ESTADO_CONTROL nBoton6, "DISABLE";
             |FINSI;
         |SINO;
             |ESTADO_CONTROL nBoton1, "DISABLE";
             |ESTADO_CONTROL nBoton2, "DISABLE";
             |ESTADO_CONTROL nBoton3, "DISABLE";
             |ESTADO_CONTROL nBoton4, "DISABLE";
             |ESTADO_CONTROL nBoton5, "DISABLE";
             |ESTADO_CONTROL nBoton6, "DISABLE";
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO Evento101;
     |SI FSalida = 1;
         |LEE 000#101=;
         |SI FSalida = 0;
             |ESTADO_CONTROL nBoton4, "ENABLE";
             |ESTADO_CONTROL nBoton6, "ENABLE";
         |SINO;
             |ESTADO_CONTROL nBoton4, "DISABLE";
             |ESTADO_CONTROL nBoton6, "DISABLE";
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO Boton1;
     |LEE 000#100=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |SI #100#0 = "      ";  |ACABA;  |FINSI;

     aPathRemoto = "C:/DOCUMENTOS";              |RMKDIR aPathRemoto;
     aPathRemoto = "C:/DOCUMENTOS/PDFCOMPRAS";   |RMKDIR aPathRemoto;

     aIPRemoto = "";
     |IP_REMOTO aIPRemoto;  |QBF aIPRemoto;

     |DBASE aBase;  |QBT aBase;
     aFichero = #100#10; |QBT aFichero;
     aDirDocu = aBase + "000577/pdfcompras/" + (("00000" + #48#0) % -105) + "/";
     aDestino = aPathRemoto + "/" + aFichero;  |QBF aDestino;
     aOrigen  = aDirDocu + aFichero;

     aContiene1 = aDestino;
     |ESPECIFICA "REMOTOMD5", aContiene;
     aDocRemoto = FSalida;  |QBF aDocRemoto;

     aContiene1 = aOrigen;
     |ESPECIFICA "MD5", aContiene;
     aDocServidor = FSalida;  |QBF aDocServidor;

     |SI aDocRemoto ! aDocServidor;
         |SI aIPRemoto ! "";
             |ENVIA_FICHERO aOrigen, aDestino;
         |SINO;
             |COPIA_FICHERO aOrigen, aDestino;
         |FINSI;
     |SINO;
         FSalida = 0;
     |FINSI;

     |SI FSalida = 0;
         aAlfa = "start " + aDestino;
         |RSYSTEM aAlfa;
     |FINSI;
|FPRC;

|PROCESO CargaDef100;
     |SI nErr = 1; |ACABA; |FINSI;

     |DBASS aBase; |QBF aBase;

     aDef1  = aBase + "dspartes/def/dstelw01";
     aDef2  = aBase + "dspartes/def/dstelm02";
     aDef3  = aBase + "dspartes/def/dstelm03";
     aDef10 = aBase + "dspartes/def/dsparm01";
     aDat   = aBase + "dspartes/fich/";

     |CARGA_DEF aDef1, dstelw01@;
     |SI FSalida ! 0;
          aAlfa = "0000Error al cargar " + aDef1;
          |MENAV aAlfa; nErr = 1;
     |FINSI;

     |CARGA_DEF aDef2, dstelm02@;
     |SI FSalida ! 0;
          aAlfa = "0000Error al cargar " + aDef2;
          |MENAV aAlfa; nErr = 1;
     |FINSI;

     |CARGA_DEF aDef3, dstelm03@;
     |SI FSalida ! 0;
          aAlfa = "0000Error al cargar " + aDef3;
          |MENAV aAlfa; nErr = 1;
     |FINSI;

     |CARGA_DEF aDef10, dsparm01@;
     |SI FSalida ! 0;
          aAlfa = "0000Error al cargar " + aDef10;
          |MENAV aAlfa; nErr = 1;
     |FINSI;

     |SI nErr = 1;  |ACABA;  |FINSI;

     |PATH_DAT #110 aDat;
     |PATH_DAT #200 aDat;
     |PATH_DAT #300 aDat;
     |PATH_DAT #111 aDat;

     |ABRE #110; |LEE 020#110p; |CIERRA #110;
|FPRC;

|PROCESO DescargaDef100;
     |DESCARGA_DEF #dstelw01@;
     |DESCARGA_DEF #dstelm02@;
     |DESCARGA_DEF #dstelm03@;
     |DESCARGA_DEF #dsparm01@;
|FPRC;

|PROCESO CreaTiquet;
     |HAZ CargaDef100;
     |SI nErr = 1; |ACABA; |FINSI;

     |ABRE #200;
     #200#5 = #913#5;
     #200#0 = 99999;
     |LEE 000#200];
     |SI FSalida = 0;
          |LEE 000#200a;
     |SINO;
          |LEE 000#200u;
     |FINSI;
     |SI FSalida = 0; |Y #200#5 = #913#5;
          nTiquet = #200#0 + 1;
     |SINO;
          nTiquet = 1;
     |FINSI;
     |HORA aHora;
     aHora = aHora % 105;

     |DDEFECTO #200;
     #200#5 = #913#5;
     #200#0 = nTiquet;
     #200#1 = ~;
     #200#2 = aHora;
     #200#3 = #102#9;
     #200#5 = #913#5;
     #200#6 = #913#6;
     #200#38 = "N";

     /*  Segun Subtiquet nro. 00764.0001 Cliente: 000577 */
     #200#40 = "I";
     #200#10 = "s-ts";
     #200#11 = "Asignada factura compra al cliente.";
     aAlfa  = "Factura Numero: " + #913#1;
     #200#12 = aAlfa;
     aAlfa  = "Fecha Factura: " + #913#2;
     #200#13 = aAlfa;
     /* ... */

     nLin = 10;
     |ABRE #111;

     #111#5 = #200#5;
     #111#0 = nTiquet;
     #111#1 = "M";
     #111#4 = 1;
     #111#2 = 1;
     #111#3 = "Asignada factura compra al cliente.";
     |GRABA 020#111n;

     #111#5 = #200#5;
     #111#0 = nTiquet;
     #111#1 = "M";
     #111#4 = 1;
     #111#2 = 2;
     aAlfa  = "Factura Numero: " + #913#1;
     #111#3 = aAlfa;
     |GRABA 020#111n;

     #111#5 = #200#5;
     #111#0 = nTiquet;
     #111#1 = "M";
     #111#4 = 1;
     #111#2 = 3;
     aAlfa  = "Fecha Factura: " + #913#2;
     #111#3 = aAlfa;
     |GRABA 020#111n;

     #111#5 = #200#5;
     #111#0 = nTiquet;
     #111#1 = "O";
     #111#4 = 1;
     #111#2 = 1;
     #111#3 = "Asignada factura compra al cliente.";
     |GRABA 020#111n;

     #111#5 = #200#5;
     #111#0 = nTiquet;
     #111#1 = "O";
     #111#4 = 1;
     #111#2 = 2;
     aAlfa  = "Factura Numero: " + #913#1;
     #111#3 = aAlfa;
     |GRABA 020#111n;

     #111#5 = #200#5;
     #111#0 = nTiquet;
     #111#1 = "O";
     #111#4 = 1;
     #111#2 = 3;
     aAlfa  = "Fecha Factura: " + #913#2;
     #111#3 = aAlfa;
     |GRABA 020#111n;

     |CIERRA #111;

     |GRABA 020#200n;
     |LIBERA #200;

     |DDEFECTO #300;
     |ABRE #300;
     #300#8 = #200#5;
     #300#0 = #200#0;
     #300#1 = 1;
     #300#2 = ~;

     |HORA aAlfa; aAlfa = aAlfa % 105;

     #300#3 = aAlfa;
     #300#4 = #102#9;
     #300#5 = #102#9;
     #300#6 = #102#10;
     #300#7 = #102#10;
     #300#8 = #200#5;
     #300#9 = "A";
     |GRABA 020#300n;
     |CIERRA #300;
     |HAZ DescargaDef100;

     aAlfa = ":dspartes/dsparz06;" + #913#5 + (("00000" + nTiquet) % -105) + "0001";
     |SUB_EJECUTA_NP aAlfa;
|FPRC;

|PROCESO LeeEmpre;
     |DFICE aDirEmp;
     |DFICB aDatos;
     aDirEmp = aDirEmp % -205;
     |PATH_DAT #41 aDatos;

     |DDEFECTO #41;
     |ABRE #41;
     #41#0 = aDirEmp;
     |LEE 020#41=;
     |CIERRA #41;
|FPRC;

|PROCESO Divisas;
     aDivisa = "EUR";
     aMoneda = "B";
     |HAZ Divisas077;
|FPRC;

|PROCESO Total80;
     |SI #77#58 = "D";                      || Si mon. de trabajo es divisa ...
          #80#6    = #80#30 / #77#57 * #77#62; || Recalculo precio en mon. base
          #80#32   = #80#6;                  || Precio visual, en mon. base
          nPPort1  = #80#42 / #77#57 * #77#62;
          nPPort2  = #80#42;
     |SINO;                               || Si mon. de trabajo es mon. base
          #80#30   = #80#6 / #77#62 * #77#57; || Recalculo precio en divisa
          #80#32   = #80#30;                || Precio visual, en divisa
          nPPort1  = #80#42;
          nPPort2  = #80#42 / #77#62 * #77#57;
     |FINSI;

     |SI #19#196 = 2;
          #80#7    = #80#36 * #80#6;
          nImpDiv  = (#80#36 * #80#30) ! nDeci_PreDiv;
     |SINO;
          #80#7    = #80#5  * #80#6;
          nImpDiv  = (#80#5  * #80#30) ! nDeci_PreDiv;
     |FINSI;

     #80#9  = ((#80#7    < #80#8) < #80#26) < #80#45;
     #80#31 = ((nImpDiv < #80#8) < #80#26) < #80#45;

     |SI #19#196 = 2;
          #80#9  = #80#9  + (#80#36 * nPPort1);  || Portes Linea
          #80#31 = #80#31 + (#80#36 * nPPort2);  || Portes Linea
     |SINO;
          #80#9  = #80#9  + (#80#5  * nPPort1);  || Portes Linea
          #80#31 = #80#31 + (#80#5  * nPPort2);  || Portes Linea
     |FINSI;

     |SI #77#58 = "D";     || Si mon. de trabajo es divisa ...
          #80#33 = #80#9;   || Importe visual, en mon. base
     |SINO;               || Si mon. de trabajo es mon. base ...
          #80#33 = #80#31;  || Importe visual, en divisa
     |FINSI;
|FPRC;

|PROCESO GrabaLineas;
     |ABRE #19;
     #19#0 = aArticulo;
     |LEE 000#19=;  ||Arti
     |CIERRA #19;

     |DDEFECTO #80;
     #80#27 = #77#50;
     #80#0  = #77#0;
     #80#1  = nLin;
     #80#2  = #19#0;
     #80#3  = 1;           ||Almacen
     #80#4  = #19#1;
     #80#5  = 1;           ||Uni
     #80#6  = nImporte;    ||Pre
     #80#30 = #80#6;       ||Pre
     #80#11 = #19#30;
     #80#13 = #19#2;
     #80#15 = #77#3;
     #80#43 = #19#5;

     |HAZ Total80;
     |HAZ CalCabAgifa077;

     enForma = 1;
     eaDocu  = "agifa080";
     |HAZ StockEntra;
     |GRABA 020#80n;
     |LIBERA #80;
|FPRC;

|PROCESO diaw0017;
     |SI #917#3 = 0;  |ACABA;  |FINSI;

     nLin      = #917#0;
     aArticulo = #917#1;
     nImporte  = #917#3;
     |HAZ GrabaLineas;
|FPRC;

|DEFBUCLE diaw0017;
     #917#1;
     ;
     01;
     99;
     ;
     NULL, diaw0017;
|FIN;

|PROCESO CreaAlbaran;
|DEBUG;
     |HAZ LeeEmpre;
     |HAZ Divisas;

     |DDEFECTO #112;
     |ABRE #112;
     #112#0 = #100#0; ||Prv = Contratista
     |LEE 000#112=;
     |CIERRA #112;

     |PARA;  |SI;  |HACIENDO;
          enSwMenu = 1;
          |DDEFECTO #77;
          #77#50 = #103#2;
          |HAZ ContaAC7;
          |LEE 000#77=;
          |SI FSalida ! 0;  |SAL;  |FINSI;
     |SIGUE;
     |GRABA 020#77b;

     #77#1  = #913#2;
     #77#3  = #112#0;
     #77#4  = #112#1;
     #77#5  = #112#24;
     #77#7  = #112#25;
     #77#32 = 0;
     #77#8  = #112#17;
     #77#29 = #41#1;
     #77#30 = #41#2;
     #77#31 = #41#3;
     #77#33 = #41#4;
     #77#36 = #112#58;
     #77#52 = #913#10;

     |ABRE #321; |LEE 000#321p; |CIERRA #321;

     |ABRE #324;
     #77#56 = "EUR";            ||Divisa
     #324#0 = "EUR";
     |LEE 020#324=;

     #77#57 = #324#3;     ||Cambio
     #77#58 = "B";             ||Moneda Tra
     #324#0 = #321#9;
     |LEE 020#324=;
     #77#61 = #321#9;     ||Moneda Base
     #77#62 = #324#3;     ||Cambio a EUR

     #77#72 = #112#87;

     |ABRE #80;
     |BUCLE diaw0017;
     |CIERRA #80;

     |GRABA 020#77a;
     |LIBERA #77;
     |CIERRA #324;
|FPRC;

|PROCESO CreaFactura;
     qSerie           = #77#50;
     qAlbar           = #77#0;
     qFecha           = #77#1;
     qAbono           = "N";
     qSufac           = #100#1;
     qSerieF          = #77#50;
     enContaFac       = #913#12;
     |SUB_EJECUTA_NP "agifa229";

     |LEE 000#77=;
     |SI FSalida ! 0;  |DDEFECTO #77;  |FINSI;
|FPRC;

|PROCESO SumaVto;   |TIPO 0;
     #916#13 = 0;
     |PARA nVto = 1; |SI nVto [ 12; |HACIENDO nVto = nVto + 2;
          nVto1 = nVto - 1;
          #916#13 = #916#13 + #916nVto;
          |SI #916nVto = 0;
               #916nVto1 = "01.01.0000";
               |PINTA #916nVto1;
          |FINSI;
     |SIGUE;

     |PARA nVto = 15; |SI nVto [ 25; |HACIENDO nVto = nVto + 2;
          nVto1 = nVto - 1;
          #916#13 = #916#13 + #916nVto;
          |SI #916nVto = 0;
               #916nVto1 = "01.01.0000";
               |PINTA #916nVto1;
          |FINSI;
     |SIGUE;

     |PINTA #916#13;
|FPRC;

|PROCESO SacaVencimientos;
     |ABRE #79;
     #79#66 = #77#50;
     #79#0  = #77#39;
     |LEE 101#79=;
     |SI FSalida ! 0;  |CIERRA #79;  |ACABA;  |FINSI;

     |VENTANA 0501, 2380, -1, -1, "";
     nVentana = FSalida;

     #916#12 = #79#118;
     #916#0  = #79#68;
     #916#1  = #79#33;
     #916#2  = #79#69;
     #916#3  = #79#34;
     #916#4  = #79#70;
     #916#5  = #79#35;
     #916#6  = #79#71;
     #916#7  = #79#36;
     #916#8  = #79#72;
     #916#9  = #79#37;
     #916#10 = #79#73;
     #916#11 = #79#38;
     #916#14 = #79#129;
     #916#15 = #79#39;
     #916#16 = #79#130;
     #916#17 = #79#40;
     #916#18 = #79#131;
     #916#19 = #79#41;
     #916#20 = #79#132;
     #916#21 = #79#42;
     #916#22 = #79#133;
     #916#23 = #79#43;
     #916#24 = #79#134;
     #916#25 = #79#44;

     |HAZ SumaVto;

     |PINPA #0#916;
     |PINDA #0#916;

     |PARA;  |SI;  |HACIENDO;
          |ENDATOS #1#916;
          |SI aMenu2 ! "1";  |SAL;  |FINSI;
     |SIGUE;

     |FINVENTANA nVentana;

     |SI aMenu2 = "2";
         #79#68  = #916#0;
         #79#33  = #916#1;
         #79#69  = #916#2;
         #79#34  = #916#3;
         #79#70  = #916#4;
         #79#35  = #916#5;
         #79#71  = #916#6;
         #79#36  = #916#7;
         #79#72  = #916#8;
         #79#37  = #916#9;
         #79#73  = #916#10;
         #79#38  = #916#11;
         #79#129 = #916#14;
         #79#39  = #916#15;
         #79#130 = #916#16;
         #79#40  = #916#17;
         #79#131 = #916#18;
         #79#41  = #916#19;
         #79#132 = #916#20;
         #79#42  = #916#21;
         #79#133 = #916#22;
         #79#43  = #916#23;
         #79#134 = #916#24;
         #79#44  = #916#25;
         |GRABA 020#79a;
     |FINSI;

     |LIBERA #79;
     |CIERRA #79;
|FPRC;

|PROCESO PonEnErp;
/*
     |DBASS aBase; |QBF aBase;

     aPathDSErp = aBase + "dserp/fich/";
     |PATH_DAT #600 aPathDSErp;

     |ABRE #600;
     #600#0 = "agifa079";
     #600#1 = #79#66 + #79#0;
     #600#2 = 1;

     aDirDocs = aBase + "dserp/documentos";
     |MKDIR aDirDocs;

     aDestino = aDirDocs + "/*";
     |_PDIR aDestino;
     nNumAnt = 0;
     |PARA; |SI FSalida = 0; |HACIENDO;
        aComodin  = aDestino % -509;
        nComodNum = aComodin;
        |SI nComodNum > nNumAnt;
            nNumAnt = nComodNum;
        |FINSI;
        |_SDIR aDestino;
     |SIGUE;
     aComodin = aDestino % -101;

     aExtension = #100#10;  |QBF aExtension;
     aExtension = (aExtension % -104) > "";
     nComodNum  = nNumAnt;
     |SI aComodin = "*";
        aDestino = aDirDocs + "/DOCUMENTO000000000" + aExtension;
     |SINO;
        nComodNum = nComodNum + 1;
        aDestino  = aDirDocs + "/DOCUMENTO" + (("000000000" + nComodNum) % -109) + aExtension;
     |FINSI;

     aComodin = aDirDocs + "/";
     #600#3 = aDestino - aComodin;
     #600#4 = Usuario % 0810;
     |GRABA 020#600n;
     |LIBERA #600;
     |CIERRA #600;

     |DBASE aBase;  |QBT aBase;
     aFichero = #100#10; |QBT aFichero;
     aDirDocu = aBase + "000577/pdfcompras/" + (("00000" + #48#0) % -105) + "/";
     aOrigen  = aDirDocu + aFichero;
     |COPIA_FICHERO aOrigen, aDestino;
*/
|FPRC;

|PROCESO Actualizar;
     |SI #102#6 ! #913#5;
         |HAZ CreaTiquet;
     |FINSI;

     |ABRE #77;
     |HAZ CreaAlbaran;
     |HAZ CreaFactura;
     |HAZ SacaVencimientos;
     ||    |HAZ PonEnErp;
     |CIERRA #77;

     |LEE 101#100=;

     #100#3  = #913#2;
     #100#4  = #913#5;
     #100#5  = #913#7;
     #100#6  = #913#3;
     #100#7  = #913#4;
     #100#8  = #77#50;
     #100#9  = #77#0;
     #100#11 = #77#39;
     #100#12 = nTiquet;
     #100#14 = #913#10;
     |GRABA 020#100a;
     |LIBERA #100;

     |REFRESCACONTROL nGrid100;
|FPRC;

|PROCESO diam0104;
     #917#0 = #104#1;
     #917#1 = #104#2;
     #917#2 = #104#3;
     #917#3 = 0;
     |SI #917#0 = 1;  #917#3 = #100#6;  |FINSI;
     |SI #917#0 = 2;  #917#3 = #100#7;  |FINSI;

     |ABRE #19;
     #19#0 = #917#1;
     |LEE 000#19=;
     |SI FSalida ! 0;  |DDEFECTO #19;  |FINSI;
     |CIERRA #19;

     enTiva = #19#30;
     efFiva = #100#3;
     |HAZ SacaIVA;

     #917#4 = #917#3 > enIva;

     |GRABA 020#917n;
     |LIBERA #917;

     nLinea = #917#0;
     nBruto = nBruto +. #917#3;
     nNeto  = nNeto  +. #917#4;

     |ABRE #112;
     #112#0 = #913#0;
     |LEE 000#112=;
     |SI FSalida ! 0;  |DDEFECTO #112;  |FINSI;
     |CIERRA #112;

     nRetencion = 0;
     |SI #913#10 ! 0;
         |SI #112#86 = "B";
             nRetencion = nBruto % #913#10;
         |SINO;
             nRetencion = nNeto % #913#10;
         |FINSI;
     |FINSI;

     #913#9 = nNeto - nRetencion;

     |PINTA #913#9;
|FPRC;

|DEFBUCLE diam0104;
     #104#1;
     ;
     #103#0, 01;
     #103#0, 99;
     ;
     NULL, diam0104;
|FIN;

|PROCESO Recal;
/*
     nBruto = nBruto + #917#3;
     nNeto  = nNeto  + #917#4;

     |ABRE #112;
     #112#0 = #913#0;
     |LEE 000#112=;
     |SI FSalida ! 0;  |DDEFECTO #112;  |FINSI;
     |CIERRA #112;

     |ABRE #19;
     #19#2 = #917#1;
     |LEE 020#19=;
     |CIERRA #19;

     nRetencion  = 0;
     |SI #913#10 ! 0;
          |SI #19#30 = 0;
               |SI #112#93 = "S";
                    |SI #112#86 = "B";
                       nRetencion = nBruto % #913#10;
                    |SINO;
                       nRetencion = nNeto  % #913#10;
                    |FINSI;
               |FINSI;
          |SINO;
               |SI #112#86 = "B";
                  nRetencion = nBruto % #913#10;
               |SINO;
                  nRetencion = nNeto  % #913#10;
               |FINSI;
          |FINSI;
     |FINSI;

     #913#8 = nNeto - nRetencion;
*/
     #913#8 = #913#8 + #917#4;
|FPRC;

|DEFBUCLE Recal;
     #917#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, Recal;
|FIN;

|PROCESO Baja917;
     #917#0 = 1;
|FPRC;

|PROCESO Alta917;
     #917#0 = nLinea;
|FPRC;

|PROCESO Boton2_1;
     |LEE 000#100=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |SI #100#13 = 0;
         |LEE 101#100=;
         |SI FSalida = 0;
             #100#13 = (#100#6 > 16) + #100#7;
             |GRABA 020#100a;
             |LIBERA #100;
         |FINSI;
     |FINSI;

     |SI #100#0 = "      ";      |ACABA;  |FINSI;
     |SI #100#5 ! "01.01.0000";  |ACABA;  |FINSI;

     |CONFI "0000NLas Garantias estan completadas";
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |ABRE #103;
     #103#0 = #100#0;
     |LEE 000#103=;
     |SI FSalida ! 0;
         |MENAV "0000 No existe en el Control de Proveedores este Proveedor. Creelo.";
         |CIERRA #103;
         |ACABA;
     |FINSI;
     |CIERRA #103;

     |SI #100#6 ! 0;  |Y #103#3 = "                    ";
         |MENAV "0000 No existe Articulo 1 en el Fichero Control de Proveedores. Creelo.";
         |ACABA;
     |FINSI;

     |SI #100#7 ! 0;  |Y #103#5 = "                    ";
         |MENAV "0000 No existe Articulo 2 en el Fichero Control de Proveedores. Creelo.";
         |ACABA;
     |FINSI;

     |DDEFECTO #913;
     #913#0  = #100#0;
     #913#1  = #100#1;
     #913#2  = #100#3;
     #913#3  = #100#6;
     #913#4  = #100#7;
     #913#8  = #100#13;
     #913#9  = 0;
     nBruto  = 0;
     nNeto   = 0;

     |SI #100#11 ! 0;
         #913#11 = #100#8;
         #913#12 = #100#11;
     |SINO;
         #913#11 = #103#2;
     |FINSI;

     |VENTANA 0501, 3399, -1, -1, "";
     nVentana = FSalida;

     aNomeDat = ("03" + Usuario + "zzzzzzzz") % 108;
     |NOME_DAT #917 aNomeDat;
     |ABRE #917;  |CIERRA #917;  |DELINDEX #917;

     |ABRE #917;
     |BUCLE diam0104;

     |PINPA #0#913;

     |C_M #913#2, 1, "S";

     |PINDA #0#913;

     nRango2 = 1 + 4 + 8 + 16 + 32 + 128;
     |LINEAL_SIMPLE #1#917, nRango2, 2202, 3298, Baja917, Alta917, NULL;
     nGrid917 = FSalida;

     |PARA;  |SI;  |HACIENDO;
          |ENDATOS #1#913;
          |SI aMenu2 ! "1";  |SAL;  |FINSI;
     |SIGUE;

     |CIERRA #917;
     |DESTRUYECONTROL nGrid917;
     |FINVENTANA nVentana;

     |SI aMenu2 = "2";
         nBruto = 0;
         nNeto = 0;
         #913#8  = 0;
         |BUCLE Recal;
         |SI #913#8 ! #913#9;
             |MENAV "0000 El Total Factura no coincide con el Total en Lineas. Cancelo operacion ";
             |ABRE #917;  |CIERRA #917;  |DELINDEX #917;
             |ACABA;
         |FINSI;

         |SI #913#8 = 0;
             |MENAV "0000 No asignado importe factura. Cancelo operacion ";
             |ABRE #917;  |CIERRA #917;  |DELINDEX #917;
             |ACABA;
         |FINSI;

         |SI #913#2 = "01.01.0000";
             |MENAV "0000 No asignado Fecha Factura. Cancelo operacion ";
             |ABRE #917;  |CIERRA #917;  |DELINDEX #917;
             |ACABA;
         |FINSI;

         |HAZ Actualizar;
     |FINSI;
     |ABRE #917;  |CIERRA #917;  |DELINDEX #917;
|FPRC;

|PROCESO agifa072B;
     |BORRA 020#72a;
     |LIBERA #72;
|FPRC;

|DEFBUCLE agifa072B;
     #72#1;
     ;
     #79#66, #79#0, 001;
     #79#66, #79#0, 999;
     be;
     NULL, agifa072B;
|FIN;

|PROCESO agifa080B;
     enForma = -1;
     eaDocu  = "agifa080";
     |HAZ StockEntra;

     |BORRA 020#80a;
     |LIBERA #80;
|FPRC;

|DEFBUCLE agifa080B;
     #80#1;
     ;
     #77#50, #77#0, 001;
     #77#50, #77#0, 999;
     be;
     NULL, agifa080B;
|FIN;

|PROCESO Boton2_2;
     |LEE 000#100=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |SI #100#0 = "      ";      |ACABA;  |FINSI;
     |SI #100#5 = "01.01.0000";  |ACABA;  |FINSI;

     |CONFI "0000NSeguro de Desactualizar esta Factura";
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |VENTANA 1450, 1560, -1, -1, "";
     nVentana = FSalida;

     |ATRI S;
     |PINTA 1450, " PASSWORD ";
     |ATRI s;

     E_inf = "";
     E_sup = "10";
     aAlfa = "";
     aAlfa = 1550 ? -1;  |QBF aAlfa;
     |FINVENTANA nVentana;

     |SI aAlfa ! "<MYPASWD>";
         |MENAV "0000 Password incorrecto.";
         |ACABA;
     |FINSI;

     enError = 0;
     aAlfa   = ":dspartes/dsparz06;" + #100#4 + (("00000" + #100#12) % -105) + "0001C";
     |SUB_EJECUTA_NP aAlfa;

     |SI enError = 1;
         |MENAV "0000 Hay varios Subtiquets en Proceso. No puedo desactualizar.";
         |ACABA;
     |FINSI;

     |ABRE #79;
     #79#66 = #100#8;
     #79#0  = #100#11;
     |LEE 000#79=;
     |SI FSalida ! 0;  |DDEFECTO #79;   |FINSI;
     |CIERRA #79;

     enErrCarte = 0;
     |HAZ MiraRecCompra;
     |SI enErrCarte ! 0;
         |MENAV "0000Recibos con movimientos. No puedo desactualizar.";
         |ACABA;
     |FINSI;

     |SI #79#48 = "S";
         |MENAV "0000NEsta Factura Esta Contabilizada. No puedo desactualizar.";
         |ACABA;
     |FINSI;

     aAlfa   = ":dspartes/dsparz06;" + #100#4 + (("00000" + #100#12) % -105) + "0001B";
     |SUB_EJECUTA_NP aAlfa;

     |DBASS aBase; |QBF aBase;

/*
     aPathDSErp = aBase + "dserp/fich/";
     |PATH_DAT #600 aPathDSErp;

     |ABRE #600;
     #600#0 = "agifa079";
     #600#1 = #79#66 + #79#0;
     #600#2 = 1;
     |LEE 101#600=;
     |SI FSalida = 0;
         |BORRA 020#600a;
         |LIBERA #600;

         aDoc = aBase + "dserp/documentos/" + #600#3;  |QBF aDoc;
         |FBORRA aDoc;
     |FINSI;
     |CIERRA #600;
*/

     |ABRE #79;
     #79#66 = #100#8;
     #79#0  = #100#11;
     |LEE 101#79=;
     |SI FSalida = 0;
         |BUCLE agifa072B;

         |BORRA 020#79a;
         |LIBERA #79;
     |FINSI;
     |CIERRA #79;

     |ABRE #77;
     #77#50 = #100#8;
     #77#0  = #100#9;
     |LEE 101#77=;
     |SI FSalida = 0;
         |BUCLE agifa080B;

         |BORRA 020#77a;
         |LIBERA #77;
     |FINSI;
     |CIERRA #77;

     #100#4  = "";
     #100#5  = "01.01.0000";
||     #100#8  = "";
     #100#9  = 0;
||     #100#11 = 0;
     #100#12 = 0;
     #100#14 = 0;
     |GRABA 020#100a;
     |LIBERA #100;

     |REFRESCACONTROL nGrid100;
|FPRC;

|PROCESO diam0101B;
     |BORRA 020#101a;
     |LIBERA #101;
|FPRC;

|DEFBUCLE diam0101B;
     #101#1;
     ;
     #100#0, #100#1, #100#2, "                                        ";
     #100#0, #100#1, #100#2, "zzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzz";
     be;
     NULL, diam0101B;
|FIN;

|PROCESO Boton3;
     |CONFI "0000NSeguro de Borrar el Registro ";
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |LEE 101#100=;
     |SI FSalida = 0;
         |BUCLE diam0101B;

         |BORRA 020#100a;
         |LIBERA #100;

         |DBASE aBase;  |QBT aBase;
         aFichero = #100#10; |QBT aFichero;
         aDirDocu = aBase + "000577/pdfcompras/" + (("00000" + #48#0) % -105) + "/" + aFichero;
         |FBORRA aDirDocu;

         |REFRESCACONTROL nGrid100;
     |FINSI;
|FPRC;

|PROCESO Boton4;
     |LEE 000#101=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |DDEFECTO #914;
     #914#0  = #101#3;
     #914#1  = #101#4;
     #914#2  = #101#5;
     #914#3  = #101#6;

     |VENTANA 0501, 2380, -1, -1, "";
     nVentana = FSalida;

     |PINPA #0#914;

     |C_M #914#0, 1, "S";
     aAlfa = #914#0;  |QBF aAlfa;
     |SI aAlfa ! "";
         |C_M #914#0, 1, "N";
     |FINSI;

     |PINDA #0#914;

     |PARA;  |SI;  |HACIENDO;
          nBoton = 4;
          |ENDATOS #1#914;
          |SI aMenu2 ! "1";  |SAL;  |FINSI;
     |SIGUE;

     |FINVENTANA nVentana;

     |SI aMenu2 = "2";
         |LEE 101#101=;
         |SI FSalida = 0;
             #101#4 = #914#1;
             #101#5 = #914#2;
             #101#6 = #914#3;
             |GRABA 020#101a;
             |LIBERA #101;

             |REFRESCACONTROL nGrid101;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO Boton5;
     |DDEFECTO #914;

     |VENTANA 0501, 2380, -1, -1, "";
     nVentana = FSalida;

     |PINPA #0#914;

     |C_M #914#0, 1, "S";
     aAlfa = #914#0;  |QBF aAlfa;
     |SI aAlfa ! "";
         |C_M #914#0, 1, "N";
     |FINSI;

     |PINDA #0#914;

     |PARA;  |SI;  |HACIENDO;
          nBoton = 5;
          |ENDATOS #1#914;
          |SI aMenu2 ! "1";  |SAL;  |FINSI;
     |SIGUE;

     |FINVENTANA nVentana;

     aAlfa = #914#0;  |QBF aAlfa;
     |SI aMenu2 = "2";  |Y aAlfa ! "";
         #101#0 = #100#0;
         #101#1 = #100#1;
         #101#2 = #100#2;
         #101#3 = #914#0;
         #101#4 = #914#1;
         #101#5 = #914#2;
         #101#6 = #914#3;
         |GRABA 020#101n;
         |LIBERA #101;

         |REFRESCACONTROL nGrid101;
     |FINSI;
|FPRC;

|PROCESO Boton6;
     |LEE 101#101=;
     |SI FSalida = 0;
         |CONFI "0000NSeguro de Borrar la Garantia ";
         |SI FSalida ! 0;  |ACABA;  |FINSI;

         |BORRA 020#101a;
         |LIBERA #101;

         |REFRESCACONTROL nGrid101;
     |FINSI;
|FPRC;

|PROCESO Boton7;
     |ESTADO_CONTROL nBoton7, "DISABLE";
     |ESTADO_CONTROL nBoton8, "ENABLE";
     |FIN_CONTROL_WINDOWS nBoton2;

     |DESTRUYECONTROL nGrid100;
     |FIN_CONTROL_WINDOWS nBoton2;

     |CONTROL_SIMPLE nPanta, "BOTON,{{218}} Completar Datos y Actualizar", 0850, 0978, Boton2_1;
     nBoton2 = FSalida;

     |ESTADO_CONTROL nBoton1, "DISABLE";
     |ESTADO_CONTROL nBoton2, "DISABLE";
     |ESTADO_CONTROL nBoton3, "DISABLE";
     |ESTADO_CONTROL nBoton4, "DISABLE";
     |ESTADO_CONTROL nBoton5, "DISABLE";
     |ESTADO_CONTROL nBoton6, "DISABLE";

     |SELECT #2#100;
     #100#4 = "";
     #100#0 = "";
     #100#1 = "";
     #100#2 = 01;
     |LEE 000#100];
     |SI FSalida ! 0;  |O #100#4 ! "      ";
         |DDEFECTO #100;
     |FINSI;

     nRango = 2 + 4 + 8 + 16 + 32;
     nPosi1 = 1003;
     nPosi2 = 2397;
     |LINEAL_SIMPLE #2#100, nRango, nPosi1, nPosi2, ClaveBaja100_2, ClaveAlta100_2, Evento100;
     nGrid100 = FSalida;

     |REFRESCACONTROL nGrid101;
|FPRC;

|PROCESO Boton8;
     |ESTADO_CONTROL nBoton8, "DISABLE";
     |ESTADO_CONTROL nBoton7, "ENABLE";

     |DESTRUYECONTROL nGrid100;
     |FIN_CONTROL_WINDOWS nBoton2;

     |SELECT #2#100;
     #100#4 = "!";
     #100#0 = "";
     #100#1 = "";
     #100#2 = 01;
     |LEE 000#100];
     |SI FSalida ! 0;
         |DDEFECTO #100;
         #100#4 = "!";
     |FINSI;

     |CONTROL_SIMPLE nPanta, "BOTON,{{218}} Desactualizar", 0850, 0978, Boton2_2;
     nBoton2 = FSalida;

     |DDEFECTO #101;
     |REFRESCACONTROL nGrid101;

     |ESTADO_CONTROL nBoton1, "DISABLE";
     |ESTADO_CONTROL nBoton2, "DISABLE";
     |ESTADO_CONTROL nBoton3, "DISABLE";
     |ESTADO_CONTROL nBoton4, "DISABLE";
     |ESTADO_CONTROL nBoton5, "DISABLE";
     |ESTADO_CONTROL nBoton6, "DISABLE";

     nRango = 2 + 4 + 8 + 16 + 32;
     nPosi1 = 1003;
     nPosi2 = 2397;
     |LINEAL_SIMPLE #2#100, nRango, nPosi1, nPosi2, ClaveBaja100_1, ClaveAlta100_1, Evento100;
     nGrid100 = FSalida;
|FPRC;

|PROCESO ExpCSV;
     |SUB_EJECUTA "diaz0027";
     |REFRESCACONTROL nGrid100;
|FPRC;

|PROGRAMA;
     |PINPA #0#100;
     nPanta = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,{{137}} Lineal Pendientes ", 0803, 0823, Boton7;
     nBoton7 = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,{{137}} Lineal Facturados ", 0903, 0923, Boton8;
     nBoton8 = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,{{217}} Ver Documento Origen ", 0825, 0948, Boton1;
     nBoton1 = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,{{218}} Completar Datos y Actualizar", 0850, 0978, Boton2_1;
     nBoton2 = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,{{207}} Borrar Registro", 0880, 0997, Boton3;
     nBoton3 = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,{{218}} Completar Garantias", 2403, 2525, Boton4;
     nBoton4 = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,{{219}} Garantia Nueva", 2430, 2550, Boton5;
     nBoton5 = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,{{207}} Borrar Registro", 2480, 2597, Boton6;
     nBoton6 = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,Exportaci¢n documentos", 3202, 3220, ExpCSV;

     |ESTADO_CONTROL nBoton1, "DISABLE";
     |ESTADO_CONTROL nBoton2, "DISABLE";
     |ESTADO_CONTROL nBoton3, "DISABLE";
     |ESTADO_CONTROL nBoton4, "DISABLE";
     |ESTADO_CONTROL nBoton5, "DISABLE";
     |ESTADO_CONTROL nBoton6, "DISABLE";
     |ESTADO_CONTROL nBoton7, "DISABLE";

     |ABRET #100;
     |ABRET #101;

     |ABRE #102;
     |LEE 000#102p;
     |SI FSalida ! 0;  |DDEFECTO #102;  |FINSI;
     |CIERRA #102;

     nRango = 2 + 4 + 8 + 16 + 32;

     nPosi1 = 1003;
     nPosi2 = 2397;
     |LINEAL_SIMPLE #2#100, nRango, nPosi1, nPosi2, ClaveBaja100_2, ClaveAlta100_2, Evento100;
     nGrid100 = FSalida;

     |SELECT #2#100;
     |LEE 000#100p;

     nPosi1 = 2603;
     nPosi2 = 3297;
     |LINEAL_SIMPLE #1#101, nRango, nPosi1, nPosi2, ClaveBaja101, ClaveAlta101, Evento101;
     nGrid101 = FSalida;

     aEsc1  = &255 + "806";
     aEsc2  = &255 + "807";
     aRetu  = &255 + "802";
     |PARA; |SI;  |HACIENDO;
         FSalida = "::Salir";
         |LEETECLA aTecla;
         |SI aTecla = aEsc1; |SAL;  |FINSI;
         |SI aTecla = aEsc2; |SAL;  |FINSI;
         |SI aTecla = aRetu; |SAL;  |FINSI;
     |SIGUE;
     aTecla = "";

     |CIERRAT #100;

     |FIN_CONTROL_WINDOWS nBoton1;
     |FIN_CONTROL_WINDOWS nBoton2;
     |FIN_CONTROL_WINDOWS nBoton3;
     |FIN_CONTROL_WINDOWS nBoton4;
     |FIN_CONTROL_WINDOWS nBoton5;
     |FIN_CONTROL_WINDOWS nBoton6;
     |FIN_CONTROL_WINDOWS nBoton7;
     |FIN_CONTROL_WINDOWS nBoton8;

     |DESTRUYECONTROL nGrid100;
     |DESTRUYECONTROL nGrid101;

     |PULSATECLA;
|FPRO;

|PROCESO Tipo80;  |TIPO 80;
     FSalida = 3299;
|FPRC;
