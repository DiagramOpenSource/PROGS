|FICHEROS;
     defelz01 #0;
     defelm01 #1;
     defelw01 #10,MANTE; ||Tmp
     agifa017 #17;
     agifa019 #19;
     agifa049 #49;
     dsm00052 #52;
     agifa073 #73;
     agifa074 #74;
     agifa104 #104;
     agifa142 #142;
     dsm00238 #238;
     dsm00279 #279;
     agifa048 #480;
     dsm00005 #950;
     agifa104@;
|FIN;

|VARIABLES;
     &Tempo = "";
     aTmp10 = "";
     aAlfa = "";
     aMarca = "";
     nHay = 0;
     {-1}aMenu = "";
     aMenu1 = "2400";
     aMenu2 = "1";
     aMenu3 = "Opciones:  ";
     aMenu4 = "RAC";
     aMenu5 = " Repasar ";
     aMenu6 = " Aceptar ";
     aMenu7 = " Cancelar ";
     aSer = "";
     nNum = 0;
     nLin = 0;
     nLinV = 0;
     nSubLin = 0;
     nFactor = 0;
     nFaltaStock = 0;
     nForma = 0;
     nSwCheq = 0;
     nPdte = 0;
|FIN;

|INCLUYE i_varart;
|INCLUYE i_escank;

|PROCESO GraVertical;
     #950#0 = "T5";
     #950#1 = #1#5;
     #950#2 = #1#6;
     #950#3 = #1#7;
     #950#7 = 0;
     #950#8 = 0;
     #950#9 = 0;
     #950#10 = "";
     #950#11 = 0;
     #950#12 = 0;
     #950#13 = 0;
     #950#14 = 0;
     #950#15 = 0;
     |GRABA 020#950n;
|FPRC;

|DEFBUCLE GraVertical;
     #950#1;
     ;
     "PV", #1#0, #1#1, nLinV, "      ";
     "PV", #1#0, #1#1, nLinV, "zzzzzz";
     ;
     NULL, GraVertical;
|FIN;

|PROCESO LeeCompo;
     |DDEFECTO #19;
     #19#0 = aEscan;
     |LEE 101#19=;
     |SI FSalida ! 0; |GRABA 020#19b; |FINSI;
     |DDEFECTO #17;
     #17#0 = aEscan;
     #17#1 = nAlmaEscan;
     |LEE 101#17=;
     |SI FSalida ! 0; |GRABA 020#17b; |FINSI;

     nFactor = 0;
     |SI #19#179 = "S";
          |ABRE #238;
          |DDEFECTO #238;
          #238#0 = #19#201;
          |LEE 000#238=;
          |CIERRA #238;
          nFactor = 1;
          |SI #238#8 ] 1; nFactor = #238#9; |FINSI;
          |SI #238#8 ] 2; nFactor = nFactor * #238#10; |FINSI;
          |SI #238#8 = 3; nFactor = nFactor * #238#11; |FINSI;
     |FINSI;
|FPRC;

|PROCESO ActCompo;
     #19#12 = #19#12 + (#52#4 * nForma);
     #19#104 = #19#104 - (#52#4 * nForma);
     #19#193 = #19#193 - (#52#9 * nForma);

     #17#8 = #17#8 + (#52#4 * nForma);
     #17#39 = #17#39 - (#52#4 * nForma);
     #17#48 = #17#48 - (#52#9 * nForma);

     |HAZ ValoraStock;
     |GRABA 020#17a;
     |GRABA 020#19a;
     |LIBERA #17;
     |LIBERA #19;
|FPRC;

|PROCESO LinKit;
     |HAZ LeeCompo;

     nSubLin = nSubLin + 1;

     |DDEFECTO #52;
     #52#6 = #74#9;
     #52#0 = #74#0;
     #52#1 = #74#1;
     #52#2 = nSubLin;
     #52#3 = aEscan;
     #52#4 = nCantiEscan;
     #52#5 = #49#9;
     #52#9 = #52#4 * nFactor;
     #52#11 = #49#0;
     |SI #279#18 = "S";
          #52#4 = #52#4 / #480#12;
          #52#9 = #52#9 / #480#12;
     |FINSI;
     |SI nForma = 1;
          |GRABA 020#52n;
     |SINO;
          |BORRA 020#52c;
     |FINSI;

     |HAZ ActCompo;

     |SI #17#39 < 0; |Y nSwCheq = 0;
          |SI #142#223 = "S"; |O #142#223 = "P";
               aAlfa = "0000Falta Stock: " + #19#0 + &13 + "Unidades:" + #17#39;
               |MENAV aAlfa;
          |FINSI;
          |SI #142#223 = "P";
               nFaltaStock = 1;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO CreaOrd;
     |SI #10#9 = "*";
          #1#0 = #10#0;
          #1#1 = #10#1;
          #1#2 = #10#2;
          |LEE 000#1=;
          |SI FSalida = 0; |ACABA; |FINSI;

          #74#9 = aSer;
          #74#0 = nNum;
          #74#1 = 999;
          |LEE 000#74];
          |SI FSalida = 0;
               |LEE 000#74a;
          |SINO;
               |LEE 000#74u;
          |FINSI;
          |SI FSalida = 0; |Y #74#9 = aSer; |Y #74#0 = nNum;
               nLin = #74#1 + 1;
          |SINO;
               nLin = 1;
          |FINSI;
          |DDEFECTO #74;
          #74#9 = aSer;
          #74#0 = nNum;
          #74#1 = nLin;
          #74#2 = #10#5;
          #74#3 = #10#6;
          #74#4 = #10#8;
          #74#7 = 1;
          #74#8 = ~;
          #74#11 = #74#4;

          nSwCheq = 0;
          nSubLin = 0;
          nForma = 1;
          nFaltaStock = 0;
          aEscan = #74#2;
          nCantiEscan = #74#4;
          |SI #142#159 = "N"; nMaxNivel = 1; |FINSI;
          |HAZ DesglosaKit;
          |SI nFaltaStock = 1;
               nSwCheq = 1;
               nSubLin = 0;
               nForma = -1;
               aEscan = #74#2;
               nCantiEscan = #74#4;
               |SI #142#159 = "N"; nMaxNivel = 1; |FINSI;
               |HAZ DesglosaKit;
          |SINO;
               #1#3 = #74#2;
               #1#4 = #74#4;
               #1#5 = #74#9;
               #1#6 = #74#0;
               #1#7 = #74#1;
               |GRABA 020#1n;
               |GRABA 020#74n;

               nLinV = #1#2;
               |BUCLE GraVertical;

               |DDEFECTO #19;
               #19#0 = #74#2;
               |LEE 101#19=;
               |SI FSalida ! 0; |GRABA 020#19b; |FINSI;
               |DDEFECTO #17;
               #17#0 = #74#2;
               #17#1 = #74#7;
               |LEE 101#17=;
               |SI FSalida ! 0; |GRABA 020#17b; |FINSI;

               #19#13 = #19#13 + #74#4;
               #17#9 = #17#9 + #74#4;
               |HAZ ValoraStock;
               |GRABA 020#17a;
               |GRABA 020#19a;
               |LIBERA #17;
               |LIBERA #19;
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE CreaOrd;
     #10#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, CreaOrd;
|FIN;

|PROCESO Todos;
     #10#9 = aMarca;
     |GRABA 020#10a;
|FPRC;

|DEFBUCLE Todos;
     #10#1;
     ,
     INICIO;
     FINAL;
     ;
     NULL, Todos;
|FIN;

|PROCESO Tipo0; |TIPO 0;
     |SI FSalida = 0; |Y #10Campo = Contenido;
          |SI #10#9 = "*";
               #10#9 = "";
          |SINO;
               #10#9 = "*";
          |FINSI;
          |GRABA 020#10a;
          |PINTA #10#9;
     |FINSI;
|FPRC;

|PROCESO Tipo11; |TIPO 11;
     |SI FSalida = 12;
          aMarca = "*"; |PTEC 807;
     |FINSI;
     |SI FSalida = 13;
          aMarca = " "; |PTEC 807;
     |FINSI;
|FPRC;

|PROCESO Pedidos;
     |SI #73#13 ] #0#4; |Y #73#13 [ #0#5;
          #1#0 = #73#28;
          #1#1 = #73#0;
          #1#2 = #73#1;
          |LEE 000#1=;
          |SI FSalida = 0; |ACABA; |FINSI;

          #10#0 = #73#28;
          #10#1 = #73#0;
          #10#2 = #73#1;
          #10#3 = #104#4;
          #10#4 = #104#8;
          #10#5 = #73#2;
          #10#6 = #73#4;
          #10#7 = #73#13;
          #10#8 = #73#5;
          #10#9 = "*";
          |GRABA 020#10n;
          nHay = 1;
     |FINSI;
|FPRC;

|DEFBUCLE Pedidos;
     #104#2;
     ;
     #0#6, #0#0, #0#2;
     #0#7, #0#1, #0#3;
     ;
     NULL, NULL, Pedidos;
|FIN;

|PROCESO Min;
     #10#0 = "     ";
     #10#1 = 000001;
     #10#2 = 001;
|FPRC;

|PROCESO Max;
     #10#0 = "zzzzz";
     #10#1 = 999999;
     #10#2 = 999;
|FPRC;

|PROCESO Fin;
     |SI nPdte ! 0;
          nHay = 1;
          |SALVA_DATOS #104;
          |REPON_DATOS #agifa104@;
          |GRABA 020#agifa104@.n;
     |FINSI;
|FPRC;

|PROCESO Lin;
     #1#0 = #73#28;
     #1#1 = #73#0;
     #1#2 = #73#1;
     |LEE 000#1=;
     |SI FSalida ! 0; nPdte = 1; |FINSI;
|FPRC;

|PROCESO Cab;
     nPdte = 0;
|FPRC;

|DEFBUCLE PedPdte;
     #104#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, Cab, Lin, Fin;
|FIN;

|PROCESO Selec; |TIPO 66;
     |DDEF aAlfa;
     aAlfa = aAlfa + "agifa104";
     |CARGA_DEF aAlfa, agifa104@;
     |SI FSalida = 0;
          |HAZ CreaTempo; |NOME_DAT #agifa104@#Tempo#; |DELINDEX #agifa104@;
          |ABRE #agifa104@;
          nHay = 0;
          |ABRE #1;
          |BUCLE PedPdte;
          |CIERRA #1;
          |SI nHay = 0;
               |MENAV "0000No hay pedidos pendientes...";
               |ERROR;
          |SINO;
               |SI Campo = 0; |O Campo = 2;
                    #agifa104@#25 = #0#0;
                    #agifa104@#0 = #0#2;
               |SINO;
                    #agifa104@#25 = #0#1;
                    #agifa104@#0 = #0#3;
               |FINSI;
               |LEE 000#agifa104@.];
               |CONSULTA_CLAVE #1#agifa104@;
               |SI FSalida = 0;
                    |SI Campo = 0; |O Campo = 2;
                         #0#0 = #agifa104@#25; |PINTA #0#0;
                         #0#2 = #agifa104@#0; |PINTA #0#2;
                         #0#4 = #agifa104@#2; |PINTA #0#4;
                         #0#6 = #agifa104@#4; |PINTA #0#6;
                    |SINO;
                         #0#1 = #agifa104@#25; |PINTA #0#1;
                         #0#3 = #agifa104@#0; |PINTA #0#3;
                         #0#5 = #agifa104@#2; |PINTA #0#5;
                         #0#7 = #agifa104@#4; |PINTA #0#7;
                    |FINSI;
               |SINO;
                    |ERROR;
               |FINSI;
          |FINSI;
          |CIERRA #agifa104@;
          |HAZ BoraTempo; |DELINDEX #agifa104@;
          |DESCARGA_DEF #agifa104@;
     |FINSI;
|FPRC;

|PROCESO Tipo3; |TIPO 3;
     |HAZ CreaTempo; |NOME_DAT #10 Tempo; |DELINDEX #10; aTmp10 = Tempo;

     |ABRE #1;
     |ABRE #10;
     |BUCLE Pedidos;
     |CIERRA #10;
     |SI nHay = 0;
          |MENAV "0000No hay pedidos pendientes...";
     |SINO;
          |PINPA #0#10;
          |PARA; |SI; |HACIENDO;
               |PARA aMarca = " "; |SI aMarca ! ""; |HACIENDO;
                    aMarca = "";
                    |ENTLINEAL #1#10, 1, 4, 19, 0, Min, Max;
                    |SI aMarca ! ""; |BUCLE Todos; |FINSI;
               |SIGUE;
               |MENU aMenu;
               |SI FSalida = 2;
                    |ABRE #1;
                    |ABRE #17;
                    |ABRE #19;
                    |ABRE #52;
                    |ABRE #74;
                    |BUCLE CreaOrd;
                    |CIERRA #74;
                    |CIERRA #52;
                    |CIERRA #19;
                    |CIERRA #17;
                    |CIERRA #1;
                    |SAL;
               |FINSI;
               |SI FSalida = 3; |SAL; |FINSI;
          |SIGUE;
     |FINSI;
     |CIERRA #1;

     Tempo = aTmp10; |HAZ BoraTempo; |DELINDEX #10;
|FPRC;

|PROGRAMA;
     |ABRE #142; |LEE 000#142p; |CIERRA #142;
     |ABRE #279; |LEE 000#279p; |CIERRA #279;
     aAlfa = PARAMETRO % 105; aSer = aAlfa;
     aAlfa = PARAMETRO % 606; nNum = aAlfa;
     |MANTE #0;
|FPRO;
