|FICHEROS;
     golis021 #0;
     gomantho #1;
     gomantl1 #2;
     gopre001 #3;
     gopre002 #4;
     gopre003 #5;
     gopartia #10;
     goparame #11;
     goceradm #12; || = gocerhis
|FIN;

|VARIABLES;
     &enCapitulo = 0;
     &enPartida = 0;
     &enPre=0;
     &enEje=0;
     &enCer=0;

     nHay = 0;
     nHayGopartia = 0;
     nNume = 0;
     aSerAdm = "";
     nNumAdm = 0;
     nHayNoPresup = 0;
     nPrime = 0;
     nGuardaCapi = 0;
     nGuardaParti = 0;
     nCantAdm = 0;
     nSwAdm = 0;
|FIN;

|PROCESO NoPresup;
     |SALVA_DATOS #4;
     |SALVA_DATOS #5;
     |DDEFECTO #4;
     |DDEFECTO #5;
     #4#13 = "CAPITULO NO ASIGNADO";
     #5#5 = "PARTIDA NO ASIGNADA";
     |SI nSwAdm = 1;
          #5#13 = enCer;
          #5#14 = nCantAdm;
     |FINSI;
     nGuardaCapi = enCapitulo;
     nGuardaParti = enPartida;
     enCapitulo = 0;
     enPartida = 0;

     enEje = #10#14;
     |PRINT;

     enCapitulo = nGuardaCapi;
     enPartida = nGuardaParti;
     |REPON_DATOS #5;
     |REPON_DATOS #4;
     nHayNoPresup = 1;
|FPRC;

|DEFBUCLE NoPresup;
     #10#3;
     ;
     #0#0, #0#2, "     ", 000001, "                    ", "                    ";
     #0#1, #0#3, "zzzzz", 999999, "                    ", "                    ";
     ;
     NULL, NoPresup;
|FIN;

|PROCESO gopartia;
     enEje = #10#14;
     |PRINT;
     enPre = 0;
     enCer = 0;
     nHayGopartia = 1;
|FPRC;

|DEFBUCLE gopartia;
     #10#3;
     ;
     #1#0, #1#1, #3#0, #3#1, #5#2, #5#4;
     #1#0, #1#1, #3#0, #3#1, #5#2, #5#4;
     ;
     NULL, gopartia;
|FIN;

|PROCESO HisAdm;
     enCer = enCer + #12#9;
     nCantAdm = nCantAdm + #12#18;
|FPRC;

|DEFBUCLE HisAdm;
     #12#1;
     ;
     #5#0, nNumAdm, 001, 001;
     #5#0, nNumAdm, 999, 999;
     ;
     NULL, HisAdm;
|FIN;

|PROCESO Partidas;
     nHay = 1;
     enPartida = enPartida + 1;

     |SI nPrime = 0;
          nSwAdm = 1;
          nPrime = 1;
          enEje = 0;
          enPre = 0;
          enCer = 0;
          nCantAdm = 0;
          |SI #3#18 = "A";
               aSerAdm = #5#0;
               nNumAdm = #5#1;
               |BUCLE HisAdm;
               |SI #11#55 = "S";
                    nNumAdm = #5#1 - #11#54;
                    |BUCLE HisAdm;
               |FINSI;
          |FINSI;
          nHayNoPresup = 0;
          |BUCLE NoPresup;
          |SI nHayNoPresup = 0; |Y enCer ! 0;
               |DDEFECTO #10;
               |HAZ NoPresup;
          |FINSI;
          nSwAdm = 0;
     |FINSI;

     enPre = #5#11;
     |SI #3#18 ! "A";
          enCer = #5#13;
          |SI #11#55 = "S";
               nNume = #5#1 - #11#54;
               |SALVA_FICHA #5;
               #5#1 = nNume;
               |LEE 000#5=;
               |SI FSalida = 0;
                    enCer = enCer + #5#13;
               |FINSI;
               |REPON_FICHA #5;
          |FINSI;
     |FINSI;
     enEje = 0;
     nHayGopartia = 0;
     |BUCLE gopartia;
     |SI nHayGopartia = 0;
          |DDEFECTO #10;
          #10#28 = "01.01.0000";
          #10#6 = 0;
          |PRINT;
     |FINSI;
|FPRC;

|DEFBUCLE Partidas;
     #5#1;
     ;
     #4#0, #4#1, #4#12, 001;
     #4#0, #4#1, #4#12, 999;
     ;
     NULL, Partidas;
|FIN;

|PROCESO LinPresupu;
     enCapitulo = enCapitulo + 1;
     enPartida = 0;
     |BUCLE Partidas;
|FPRC;

|PROCESO CabPresupu;
     nPrime = 0;
|FPRC;

|DEFBUCLE Presupuesto;
     #3#1;
     ;
     #2#3, #2#4;
     #2#3, #2#4;
     ;
     NULL, CabPresupu, LinPresupu;
|FIN;

|PROCESO LinExpediente;
     enCapitulo = 0;
     |BUCLE Presupuesto;
|FPRC;

|PROCESO FinExpediente;
     |PIEINF;
|FPRC;

|DEFBUCLE Expediente;
     #1#1;
     ;
     #0#0, #0#2;
     #0#1, #0#3;
     ;
     NULL, NULL, LinExpediente, FinExpediente;
|FIN;

|PROCESO Tipo3; |TIPO 3;
     |ABRE #11; |LEE 000#11p; |CIERRA #11;

     Impresora = "Crystal Reports";
     |IMPRE 0;
     |SI FSalida = 0;
          |SI Impresora = "CrystalReport";
               |SI #0#4 = "F";
                    |INFOR "golis021";
               |SINO;
                    |INFOR "golisb21";
               |FINSI;
               |SI FSalida = 0;
                    |BUCLE Expediente;
                    |SI nHay = 0;
                         |MENAV "0000No hay registros...";
                    |FINSI;
                    |FININF;
               |FINSI;
          |SINO;
               |MENAV "0000Informe confeccionado solo para Crystal Reports";
          |FINSI;
          |FINIMP;
     |FINSI;
|FPRC;
