|FICHEROS;
     conz0005 #0;
     agifa065 #1;
     dsw00024 #2; ||Tmp Art/Alma
     dsw00025 #3; ||Tmp Art/Alma/Fecha
     agifa077 #4;
     agifa080 #5;
     dsw00041 #8;
     agifa017 #17;
     agifa019 #19;
     agifa142 #142;
     dsm00241 #241;
     agifa321;
     agifa324;
|FIN;

|CAMPOS;
     #19#15  art_pser;  #17#42  alm_pser;
     #19#16  art_prec;  #17#43  alm_prec;
     #19#17  art_pval;  #17#4   alm_pval;
     #19#11  art_nece;  #17#50  alm_nece;
     #19#12  art_rese;  #17#8   alm_rese;
     #19#14  art_depo;  #17#10  alm_depo;
     #19#13  art_fabr;  #17#9   alm_fabr;
     #19#10  art_vsto;  #17#3   alm_vsto;
     #19#7   art_mini;  #17#6   alm_mini;
     #19#8   art_maxi;  #17#7   alm_maxi;
     #19#6   art_tota;  #17#5   alm_tota;
     #19#104 art_disp;  #17#39  alm_disp;
     #19#9   art_pcm;
     #19#185 art_tota2; #17#47  alm_tota2;
     #19#193 art_disp2; #17#48 alm_disp2;
|FIN;

|VARIABLES;
     FINICIAL = "01.01.0000";
     &eaUsar   = "";
     &Tempo    = "";
     &aDivisa  = "";
     &aMoneda  = "";
     aParam    = "";
     nFactor   = 0;
     aTempo2   = "";
     aTempo3   = "";
     aTempo8   = "";
     aInforme  = "conz0005";
     aAlfa     = "";
     aFichero  = "";
     nNeto     = 0;
     nCoste    = 0;
     nPcm      = 0;
     nPm       = 0;
     nSwImpre  = 0;
     nValor    = 0;
     nUni      = 0;
     nImporte  = 0;
     nImneto   = 0;
     nMargen   = 0;
     nPortes   = 0;
     nPrecio   = 0;
     nSw       = 0;
|FIN;

|PROCESO dsw00025;
     |PRINT;
|FPRC;

|DEFBUCLE dsw00025;
     #3#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, dsw00025;
|FIN;

|PROCESO agifa019;
     #3#0 = #19#0;
     #3#1 = "01.01.0000";
     |LEE 000#3];
     |SI FSalida = 0; |Y #3#0 = #19#0;
          || existe...
     |SINO;
          #3#0 = #19#0;
          #3#1 = #19#26;
          #3#2 = #19#133;
          |GRABA 020#3n;
     |FINSI;
|FPRC;

|DEFBUCLE agifa019;
     #19#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, agifa019;
|FIN;
컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
|PROCESO agifa019PCM;
     #17#0 = #19#0;
     #17#1 = 1;
     |LEE 101#17=;
     |PARA; |SI FSalida = 0; |Y #17#0 = #19#0; |HACIENDO;
          |HAZ ValoraStock;
          |GRABA 020#17a;
          |LIBERA #17;
          |LEE 101#17s;
     |SIGUE;
     |GRABA 020#19a;
     |LIBERA #19;
     |LIBERA #17;
|FPRC;

|DEFBUCLE agifa019PCM;
     #19#1;
     ;
     "                    ";
     "zzzzzzzzzzzzzzzzzzzz";
     be;
     NULL, agifa019PCM;
|FIN;
컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
|PROCESO agifa065Movi;
     #3#0 = #1#0;
     #3#1 = #1#1;
     |LEE 000#3];
     |SI FSalida ! 0; |LEE 000#3u; |FINSI;

     |SI #3#0 ! #1#0; |O #3#1 ! #1#1;
          |LEE 000#3a;
          |SI #3#0 ! #1#0; |O FSalida ! 0;
               ||ACABA;        || Esto no debe pasar nunca
                               || ...pero si pasa
               |DDEFECTO #19;  || Pues le pongo
               #19#0 = #1#0;   || el PCM del articulo
               |LEE 000#19=;
               #3#2 = #19#9;
          |FINSI;
     |FINSI;
     #1#8 = #3#2;
     |GRABA 020#1a;
     |LIBERA #1;
|FPRC;

|DEFBUCLE agifa065Movi;
     #1#4;
     2;
     "01.01.0000", 00001, "                    ", "OR";
     "31.12.9999", 99999, "zzzzzzzzzzzzzzzzzzzz", "OR";
     be;
     NULL, agifa065Movi;
|FIN;
컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
|PROCESO Coste;
     nCoste = 0;
     nNeto = nUni * nPrecio;
     |SI nNeto = 0; |ACABA; |FINSI;

     |SI #1#2 = "AC";
          #4#50 = #1#11;
          #4#0 = #1#3;
          |LEE 000#4=;
          |SI FSalida ! 0; |DDEFECTO #4; |FINSI;

          #5#27 = #agifa065#11;
          #5#0 = #agifa065#3
          #5#1 = #agifa065#4
          |LEE 000#5=;
          |SI FSalida ! 0; |DDEFECTO #5; |FINSI;

          |SI #19#196 = 2;
               nNeto = nNeto + ( #5#36 * #5#42);  || Portes Linea
          |SINO;
               nNeto = nNeto + ( #5#5 * #5#42);  || Portes Linea
          |FINSI;

          |SI #142#114 = "S";
               nNeto = nNeto < #4#7;
          |FINSI;

          nPm = 0;
          |SI #142#104 = "S";
               nPm = (#4#11 + #4#13) / #4#15;
          |FINSI;
          |SI nPm ! 0;
               nPm = (nNeto * nPm) + nNeto;
          |SINO;
               nPm = nNeto;
          |FINSI;
          nCoste = nPm;
     |SINO;
          nCoste = nNeto;
     |FINSI;
|FPRC;

|PROCESO Pcm;
     |SI #1#2 ! "AC"; |Y #1#2 ! "EV"; |Y #1#2 ! "SA";
                      |Y #1#2 ! "ES"; |Y #1#2 ! "SS";
                      |Y #1#2 ! "ME"; |Y #1#2 ! "MS";
          |ACABA;
     |FINSI;

     |DDEFECTO #19;
     #19#0 = #1#0;
     |LEE 101#19=;
     |SI #19#196 = 2;
          nUni = #1#17;
     |SINO;
          nUni = #1#6;
     |FINSI;

     |SI nUni = 0; |LIBERA #19; |ACABA; |FINSI;

     |SI #19#204 = "N";
          nFactor = 1;
     |SINO;
          #241#0 = #19#5;
          #241#2 = #1#20;
          |LEE 000#241=;
          |SI FSalida ! 0; |DDEFECTO #241; |FINSI;
          |SI #241#6 = "D";
              nFactor = 1 * #241#5;
          |SINO;
              nFactor = 1 / #241#5;
          |FINSI;
     |FINSI;

     |DDEFECTO #2;
     #2#0 = #1#0;
     |LEE 101#2=;
     |SI FSalida ! 0; |GRABA 020#2b; |FINSI;
     |GRABA 020#2a;

     |SI #1#2 = "AC"; |O #1#2 = "EV";
          nPrecio = ((#1#9 < #1#12) < #1#13) < #1#21;
     |SINO;
          #8#0 = #1#0;
          #8#1 = #1#1;
          |LEE 000#8=;
          |SI FSalida ! 0;
               |LEE 000#8a;
          |FINSI;
          |SI FSalida ! 0; |DDEFECTO #8; |FINSI;

          |SI #1#2 = "SA"; |O #0#1 = "P";
               nPrecio = #2#3;       || PCM calculado
          |SINO;
               |SI #8#0 ! #1#0; |O FSalida ! 0;
                    nPrecio = #2#3;  || PCM calculado
               |SINO;
                    nPrecio = #8#2;  || Ultimo Precio Compra
               |FINSI;
          |FINSI;
          |SI nPrecio = 0;
               nPrecio = #19#133;    || Ult. Pre Comp con Dto
          |FINSI;

          |SI nPrecio = 0; |Y #1#2 = "SA";
               nPrecio = #1#9;           || Precio
               |SI nPrecio = 0;
                    nPrecio = #19#9;     || PCM
               |FINSI;
          |FINSI;
     |FINSI;

     |HAZ Coste;
     nCoste = nCoste / nFactor;
     |SI nCoste = 0; |ACABA; |FINSI;

     |SI #1#1 < FINICIAL;
          #2#1 = #2#1 + nUni;
          #2#2 = #2#2 + nCoste;
          #2#3 = #2#2 / #2#1;
     |SINO;
          |SI #2#2 > 0;
               nPcm = (#2#2 + (nCoste)) / (#2#1 + nUni);
          |SINO;
               nPcm = nCoste / nUni;
          |FINSI;

          #2#1 = #2#1 + nUni;
          #2#2 = nPcm * #2#1;
          #2#3 = nPcm;
          |GRABA 020#2a;

          |SI #2#3 > 0;
               |DDEFECTO #3;
               #3#0 = #1#0;
               #3#1 = #1#1;
               |LEE 101#3=;
               #3#2 = #2#3;
               |SI FSalida = 0;
                    |GRABA 020#3a;
               |SINO;
                    |GRABA 020#3n;
               |FINSI;
          |FINSI;
     |FINSI;
     |LIBERA #2;
     |LIBERA #3;
|FPRC;

|PROCESO UltPc;
     |SI #1#2 ! "AC"; |Y #1#2 ! "EV";
          |ACABA;
     |FINSI;
     |SI #1#6 = 0; |ACABA; |FINSI;

     |DDEFECTO #8;
     #8#0 = #1#0;
     #8#1 = #1#1;
     |LEE 101#8=;
     |SI FSalida ! 0; |GRABA 020#8b; |FINSI;
     #8#2 = ((#1#9 < #1#12) < #1#13) < #1#21;
     |GRABA 020#8a;
     |LIBERA #8;
|FPRC;

|PROCESO agifa065;
     |SI nSw = 1;
          |HAZ UltPc;
     |SINO;
          |HAZ Pcm;
     |FINSI;
|FPRC;

|DEFBUCLE agifa065;
     #1#4;
     ;
     "01.01.0000", 00001, "                    ";
     "31.12.9999", 99999, "zzzzzzzzzzzzzzzzzzzz";
     ;
     NULL, agifa065;
|FIN;
컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
|PROCESO Tipo3; |TIPO 3;
     |SI #0#0 = "SI";
          |HAZ DecimalesBase;

          |ABRE #142; |LEE 000#142p; |CIERRA #142;

          |SI nSwImpre = 0;
               |CONFI "0000SImprimir PCM calculado...";
               nSwImpre = FSalida;
          |FINSI;

          |HAZ CreaTempo; aTempo2 = Tempo;|NOME_DAT #2 aTempo2; |DELINDEX #2;
          |HAZ CreaTempo; aTempo3 = Tempo;|NOME_DAT #3 aTempo3; |DELINDEX #3;
          |HAZ CreaTempo; aTempo8 = Tempo;|NOME_DAT #8 aTempo8; |DELINDEX #8;

          |ABRE #2;
          |ABRE #3;
          |ABRE #4;
          |ABRE #5;
          |ABRE #8;

          |ABRE #19;
          |ABRE #241; |SELECT #2#241;
          nSw = 1;
          |BUCLE agifa065;  || Calcula Ultimo Precio Compra
          nSw = 2;
          |BUCLE agifa065;  || Calcula PCM
          |CIERRA #241;
          |CIERRA #19;

          |BUCLE agifa019; ||Rellena con los articulos sin movimientos

          |CIERRA #2;
          |CIERRA #3;
          |CIERRA #4;
          |CIERRA #5;
          |CIERRA #8;

          |ABRE #3;
          |ABRE #19;
          |BUCLE agifa065Movi; || Graba PCM en Historico "OR";
          |CIERRA #19;
          |CIERRA #3;

          |SI nSwImpre = 0;
               |IMPRE 0;
               |SI FSalida = 0;
                    |INFOR aInforme;
                    |SI FSalida = 0;
                         |BUCLE dsw00025;
                         |PIEINF;
                         |FININF;
                    |FINSI;
                    |FINIMP;
               |FINSI;
          |FINSI;
          |DELINDEX #2; Tempo = aTempo2; |HAZ BoraTempo;
          |DELINDEX #3; Tempo = aTempo3; |HAZ BoraTempo;
          |DELINDEX #8; Tempo = aTempo8; |HAZ BoraTempo;
     |FINSI;
|FPRC;
