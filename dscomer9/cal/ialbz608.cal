     CONTRATOS

|FICHEROS;
     ialbz008 #0;
     ialbm004 #4;
     ialbm005 #5;
     ialbm009 #9;
     ialbm010 #10;
     referec@;
     referel@;
|FIN;

|VARIABLES;
     &eaEstadoBtn = "";
     &eaTmp = "";
     &eaPestana = "";
     &eaSwCarga6 = "";
     &eaBtnAcota6 = "";
     &eaCli6 = "";
     &eaMat6 = "";
     &eaArt6 = "";
     &eaLin6 = "";

     aProg = "";
     aTmp = "";
     aAlfa = "";
     nRango = 0;
     nPos1 = 0;
     nPos2 = 0;
     nPestana = 0;
     aPath = "";
     nPos = 0;
     nDDir = 0;
     nHDir = 0;
     aSwPdte = "";
     nOk = 0;

     nBtnCopia6 = 0;
     nBtnCarton6 = 0;
     nBtnNuevo6 = 0;
     nBtnImpre6 = 0;
     nBtnSalir6 = 0;
     nGridC = -1;
     nGridL = -1;
     nPanta = 0;
     nEvtFoco = 0;
     nBtnModCto6 = 0;
     nBtnVeCto6 = 0;
|FIN;

//----------------//
|PROCESO Existe6;
     |SI eaBtnAcota6 = "1";
          |ABRE #4;
          #4#0 = #referec@#0;
          #4#1 = #referec@#1;
          #4#2 = #referec@#2;
          |LEE 000#4=;
          aAlfa = FSalida;
          |CIERRA #4;
     |SINO;
          |ABRE #9;
          #9#0 = #referec@#0;
          |LEE 000#9=;
          aAlfa = FSalida;
          |CIERRA #9;
     |FINSI;
     FSalida = aAlfa;
|FPRC;

|PROCESO BtnModCto6;
     |HAZ Existe6;
     |SI FSalida ! 0; |ACABA; |FINSI;

     |SI eaBtnAcota6 = "1";
          aProg = "*ialbz011;"+ "F" + "M" + #referec@#0 + #referec@#1 + #referec@#2 + ".";
     |SINO;
          aProg = "*ialbz011;"+ "C" + "M" + #referec@#0 + ".";
     |FINSI;
     |SUB_EJECUTA aProg;
     |REFRESCACONTROL nGridC;
|FPRC;

|PROCESO BtnVeCto6;
     |HAZ Existe6;
     |SI FSalida ! 0; |ACABA; |FINSI;

     |SI eaBtnAcota6 = "1";
          aProg = "*ialbz011;"+ "F" + "V" + #referec@#0 + #referec@#1 + #referec@#2 + ".";
     |SINO;
          aProg = "*ialbz011;"+ "C" + "V" + #referec@#0 + ".";
     |FINSI;
     |SUB_EJECUTA aProg;
|FPRC;
//----------------//
|PROCESO CargaDatos6;
     |SI eaSwCarga6 ! "S";
          |HAZ DestruyePan6;
          |HAZ Descarga6;
          |SI eaBtnAcota6 = "1";
               |REFERENCIA_DEF referec@, #4;
               |REFERENCIA_DEF referel@, #5;
          |SINO;
               |REFERENCIA_DEF referec@, #9;
               |REFERENCIA_DEF referel@, #10;
          |FINSI;
          |HAZ PintaPanta6;
     |FINSI;
|FPRC;

|PROCESO CogePosicion6;
     |SI eaBtnAcota6 = "1";
          eaCli6 = #referec@#0;
          eaMat6 = #referec@#1;
          eaArt6 = #referec@#2;
          eaLin6 = ("000" + #referel@#3) % -103;
     |SINO;
          eaCli6 = #referec@#0;
          eaMat6 = "               ";
          eaArt6 = "                    ";
          eaLin6 = ("000" + #referel@#0) % -103;
     |FINSI;
|FPRC;

|PROCESO Posiciona6;
     |SI eaBtnAcota6 = "1";
          #referel@#0 = eaCli6;
          #referel@#1 = eaMat6;
          #referel@#2 = eaArt6;
          #referel@#3 = eaLin6;
          |LEE 000#referel@.=;

          #referec@#0 = eaCli6;
          #referec@#1 = eaMat6;
          #referec@#2 = eaArt6;
          |LEE 000#referec@.=;
     |SINO;
          #referel@#0 = eaLin6;
          #referel@#1 = eaCli6;
          |LEE 000#referel@.=;

          #referec@#0 = eaCli6;
          |LEE 000#referec@.=;
     |FINSI;
     |REFRESCACONTROL nGridC;

     |SI eaPestana = "6"; |FOCOTECLADO nGridC; |FINSI;
|FPRC;

|PROCESO EstadoBtn6;
     |SI eaBtnAcota6 = "1";
          |ESTADO_CONTROL nBtnCopia6, "DISABLE";
          |ESTADO_CONTROL nBtnCarton6, "ENABLE";
          |ESTADO_CONTROL nBtnNuevo6, "ENABLE";
     |SINO;
          |ESTADO_CONTROL nBtnCopia6, "ENABLE";
          |ESTADO_CONTROL nBtnCarton6, "DISABLE";
          |SI #0#0 = "000000";
               |ESTADO_CONTROL nBtnNuevo6, "ENABLE";
          |SINO;
               |ESTADO_CONTROL nBtnNuevo6, "DISABLE";
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO BtnCopia6;
     eaBtnAcota6 = "1";
     eaSwCarga6 = "N";
     |HAZ EventoFoco6;
|FPRC;

|PROCESO BtnCarton6;
     eaBtnAcota6 = "2";
     eaSwCarga6 = "N";
     |HAZ EventoFoco6;
|FPRC;

|PROCESO BtnNuevo6;
     |SI eaBtnAcota6 = "1";
          aProg = "*ialbz011;"+ "F" + "N" + #0#0 + ".";
     |SINO;
          aProg = "*ialbz011;"+ "C" + "N" + #0#0 + ".";
     |FINSI;
     |SUB_EJECUTA aProg;
     |REFRESCACONTROL nGridC;
|FPRC;

|PROCESO BtnImpre6;
     |HAZ Existe6;
     |SI FSalida ! 0; |ACABA; |FINSI;

     |SI eaBtnAcota6 = "1";
          aProg = "*ialbz011;"+ "F" + "I" + #referec@#0 + #referec@#1 + #referec@#2 + ".";
     |SINO;
          aProg = "*ialbz011;"+ "C" + "I" + #referec@#0 + ".";
     |FINSI;
     |SUB_EJECUTA aProg;
|FPRC;

|PROCESO EventoC6;
     |SI FSalida = 1; |O FSalida = 2;
          |LEE 000#referec@.=;
          |REFRESCACONTROL nGridL;
     |FINSI;
|FPRC;

|PROCESO EventoL6;
     |SI FSalida = 1; |O FSalida = 2;
          |LEE 000#referel@.=;
     |FINSI;
|FPRC;

|PROCESO EventoFoco6;
     |HAZ CargaDatos6;
     eaPestana = "6";
|FPRC;

|PROCESO NomeDat6;
     |SI eaBtnAcota6 = "1";
          aTmp = "ialbm004";
     |SINO;
          aTmp = "ialbm009";
     |FINSI;
     |NOME_DAT #referec@#aTmp#;
|FPRC;

|PROCESO MinL6;
     |SI eaBtnAcota6 = "1";
          #referel@#0 = #referec@#0;
          #referel@#1 = #referec@#1;
          #referel@#2 = #referec@#2;
          #referel@#3 = 1;
     |SINO;
          #referel@#0 = 1;
          #referel@#1 = #referec@#0;
     |FINSI;
|FPRC;

|PROCESO MaxL6;
     |SI eaBtnAcota6 = "1";
          #referel@#0 = #referec@#0;
          #referel@#1 = #referec@#1;
          #referel@#2 = #referec@#2;
          #referel@#3 = 999;
     |SINO;
          #referel@#0 = 999;
          #referel@#1 = #referec@#0;
     |FINSI;
|FPRC;

|PROCESO MinC6;
     |SI eaBtnAcota6 = "1";
          |SI #0#0 = "000000";
               #referec@#0 = "      ";
          |SINO;
               #referec@#0 = #0#0;
          |FINSI;
           #referec@#16 = nDDir;
           #referec@#1 = "               ";
           #referec@#2 = "                    ";
     |SINO;
          |SI #0#0 = "000000";
               #referec@#0 = "      ";
          |SINO;
               #referec@#0 = #0#0;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO MaxC6;
     |SI eaBtnAcota6 = "1";
          |SI #0#0 = "000000";
               #referec@#0 = "zzzzzz";
          |SINO;
               #referec@#0 = #0#0;
          |FINSI;
           #referec@#16 = nHDir;
           #referec@#1 = "zzzzzzzzzzzzzzz";
           #referec@#2 = "zzzzzzzzzzzzzzzzzzzz";
     |SINO;
          |SI #0#0 = "000000";
               #referec@#0 = "zzzzzz";
          |SINO;
               #referec@#0 = #0#0;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO EstadoIniBtnPan6;
     |ESTADO_CONTROL nBtnCopia6, eaEstadoBtn;
     |ESTADO_CONTROL nBtnCarton6, eaEstadoBtn;
     |ESTADO_CONTROL nBtnNuevo6, eaEstadoBtn;
     |ESTADO_CONTROL nBtnImpre6, eaEstadoBtn;
     |ESTADO_CONTROL nBtnSalir6, eaEstadoBtn;
|FPRC;

|PROCESO DestruyePan6;
     |DESTRUYECONTROL nGridL;
     |DESTRUYECONTROL nGridC;

     |FIN_CONTROL_WINDOWS nEvtFoco;

     |FIN_CONTROL_WINDOWS nBtnCopia6;
     |FIN_CONTROL_WINDOWS nBtnCarton6;
     |FIN_CONTROL_WINDOWS nBtnNuevo6;
     |FIN_CONTROL_WINDOWS nBtnImpre6;
     |FIN_CONTROL_WINDOWS nBtnSalir6;

     |FIN_CONTROL_WINDOWS nBtnModCto6;
     |FIN_CONTROL_WINDOWS nBtnVeCto6;
|FPRC;

|PROCESO Descarga6;
     |CIERRAT #referec@;
     |CIERRAT #referel@;
     |DESCARGA_DEF #referel@;
     |DESCARGA_DEF #referec@;
|FPRC;

|PROCESO PintaPanta6;
     |DDEF aPath;
     |SI eaBtnAcota6 = "1";
          aAlfa = aPath + "ialbm004";
          |CARGA_DEF aAlfa, referec@;
          aAlfa = aPath + "ialbm005";
          |CARGA_DEF aAlfa, referel@;
     |SINO;
          aAlfa = aPath + "ialbm009";
          |CARGA_DEF aAlfa, referec@;
          aAlfa = aPath + "ialbm010";
          |CARGA_DEF aAlfa, referel@;
     |FINSI;

     |HAZ NomeDat6;
     #referec@#0 = 0;    || Para que no salga nada si esta vacio

     |ABRET #referec@;;
     |ABRET #referel@;
     |SI #0#1 = 0;
          nDDir = 0; nHDir = 9999;
     |SINO;
          nDDir = #0#1; nHDir = #0#1;
     |FINSI;
     eaSwCarga6 = "S";

     |PINPA #6#0;  nPanta = FSalida;

     nRango = 4 + 8 + 16 + 32;
     nPos1 = 2402;
     nPos2 = 3398;
     |SI nGridL ! -1; |DESTRUYECONTROL nGridL; |FINSI;
     |LINEAL_SIMPLE #1#referel@, nRango, nPos1, nPos2, MinL6, MaxL6, EventoL6;
     nGridL = FSalida;

     nRango = 4 + 8 + 16 + 32;
     nPos1 = 0602;
     nPos2 = 2298;
     |SI nGridC ! -1; |DESTRUYECONTROL nGridC; |FINSI;
     |SI eaBtnAcota6 = "1";
          |LINEAL_SIMPLE #2#referec@, nRango, nPos1, nPos2, MinC6, MaxC6, EventoC6;
     |SINO;
          |LINEAL_SIMPLE #1#referec@, nRango, nPos1, nPos2, MinC6, MaxC6, EventoC6;
     |FINSI;
     nGridC = FSalida;

     |CONTROL_SIMPLE nPanta, "EVENTOPAN", 0000, 0000, EventoFoco6;
     nEvtFoco = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,Copiadoras", 0501, 0513, BtnCopia6;
     nBtnCopia6 = FSalida;
     |CONTROL_SIMPLE nPanta, "BOTON,Cartones", 0514, 0526, BtnCarton6;
     nBtnCarton6 = FSalida;
     |CONTROL_SIMPLE nPanta, "BOTON,{{193}}Nuevo", 0566, 0578, BtnNuevo6;
     nBtnNuevo6 = FSalida;
     |CONTROL_SIMPLE nPanta, "BOTON,{{205}}Imprimir", 0579, 0591, BtnImpre6;
     nBtnImpre6 = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,Modificar", 2301, 2313, BtnModCto6;
     nBtnModCto6 = FSalida;
     |CONTROL_SIMPLE nPanta, "BOTON,Ver Contrato", 2314, 2326, BtnVeCto6;
     nBtnVeCto6 = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,{{207}} Salir", 3488, 3498, BtnSalir;
     nBtnSalir6 = FSalida;

     |HAZ EstadoBtn6;
|FPRC;
