|FICHEROS;
     mecam014 #0;
     mecam015 #1;
     mecam006 #6;
     mecam007 #7;
     mecam008 #8;
     mecam013 #13;
     agifa019 #19;
     mecaw020 #20,MANTE;
     mecaw026 #26;
     agifa104 #104;
|FIN;

|VARIABLES;
     &Tempo = "";
     aTmp20 = "";
     nModo = 0;
     aSerie = "";
     nConta = 0;
     nForma = 0;
     nHay = 0;
     nError = 0;
     aFinaliza = "";
     aAlfa = "";
|FIN;

|PROCESO ContaOrd; |TIPO 7;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 1;
          aSerie = #0#0;
          #0#1 = 999999;
          |LEE 0000#0];
          |SI FSalida = 0;
               |LEE 000#0a;
          |SINO;
               |LEE 000#0u;
          |FINSI;
          |SI FSalida = 0; |Y #0#0 = aSerie;
               nConta = #0#1 + 1;
          |SINO;
               nConta = 1;
          |FINSI;
          |DDEFECTO #0;
          #0#0 = aSerie;
          #0#1 = nConta;
     |FINSI;
|FPRC;

|PROCESO MiraSer;
     |SI #7#7 = "N";
          nHay = 1;
          |ERROR10;
     |FINSI;
|FPRC;

|DEFBUCLE MiraSer;
     #7#1;
     ;
     #0#3, #0#4, 0001;
     #0#3, #0#4, 9999;
     ;
     NULL, MiraSer;
|FIN;

|PROCESO CheqPed; |TIPO 0;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 1;
          nHay = 0;
          |BUCLE MiraSer;
          |SI nHay = 0;
               |MENAV "0000No quedan paquetes pendientes de servir";
               |ERROR;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO MiraFinaliza;
     #6#0 = #8#0;
     #6#1 = #8#1;
     #6#2 = #8#3;
     |LEE 000#6=;
     |SI #6#10 = "N";
          aFinaliza = "N";
     |FINSI;
|FPRC;

|DEFBUCLE MiraFinaliza;
     #8#1;
     ;
     #7#0, #7#1, #7#2, 001;
     #7#0, #7#1, #7#2, 999;
     ;
     NULL, MiraFinaliza;
|FIN;

|PROCESO CheConfir;
     |SI #1#7 = "S";
          nError = 1;
     |FINSI;
|FPRC;

|PROCESO Tipo2Cab; |TIPO 2;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 3;
          nError = 0;
          |BUCLELIN 0CheConfir#0;
          |SI nError ! 0;
               |MENAV "0000Orden con lineas confirmadas";
               |ERROR; |ACABA;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO OrdTipo2; |TIPO 2;
     nModo = (FEntrada / 100) ! 0;
     nForma = 0 +. 1;

     |SI #1#7 = "S";
          |SI nForma = -1;
               |MENAV "0000Linea confirmada...";
          |FINSI;
          |ERROR; |ACABA;
     |FINSI;

     #1#3 = #0#3;
     #1#4 = #0#4;

     |ABRE #7;
     #7#0 = #1#3;
     #7#1 = #1#4;
     #7#2 = #1#2;
     |LEE 121#7=;
     |SI FSalida = 0;
          nError = 0;
          |SI nForma = 1;
               || Chequeos en alta
               |SI #7#7 = "S";
                    |MENAV "0000El paquete esta servido";
                    nError = 1;
               |SINO;
                    aFinaliza = "S";
                    |ABRE #6;
                    |BUCLE MiraFinaliza;
                    |CIERRA #6;
                    |SI aFinaliza = "N";
                         |MENAV "0000El paquete con lineas sin finalizar";
                    |FINSI;
               |FINSI;
               ||---------------------
               #7#7 = "S";
          |SINO;
               #7#7 = "N";
          |FINSI;
          |SI nError = 0;
               #1#5 = #7#3; |PINTA #1#5;
               #1#6 = #7#4; |PINTA #1#6;
               |GRABA 020#7a;
          |SINO;
               |ERROR;
          |FINSI;
     |FINSI;
     |CIERRA #7;
|FPRC;

|PROCESO OrdTipo5; |TIPO 5;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 1;
          |HAZ Seleccion;
     |FINSI;
|FPRC;

|PROCESO Min8;
     #8#0 = #1#3;
     #8#1 = #1#4;
     #8#2 = #1#2;
     #8#3 = 1;
|FPRC;

|PROCESO Max8;
     #8#0 = #1#3;
     #8#1 = #1#4;
     #8#2 = #1#2;
     #8#3 = 999;
|FPRC;

|PROCESO OrdTipo11; |TIPO 11;
     nModo = (FEntrada / 100) ! 0;
     |SI FSalida = 13; ||F5
          |HAZ Seleccion;
          |SI nHay = 0; |ERROR; |FINSI;
     |FINSI;
     |SI FSalida = 14; ||F6;
          |ABRE #8;
          |CONSULTA_F_CLAVE #1#8, Min8, Max8;
          |CIERRA #8;
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO CargaSele;
     |SI #20#3 ! "*";
          |ACABA;
     |FINSI;

     |DDEFECTO #1;
     #1#0 = #0#0;
     #1#1 = #0#1;
     #1#2 = #20#0; ||Paquete
     #1#3 = #0#3;
     #1#4 = #0#4;
     #1#5 = #20#1; ||Arti
     #1#6 = #20#2; ||Canti

     #7#0 = #1#3;
     #7#1 = #1#4;
     #7#2 = #1#2;
     |LEE 121#7=;
     |SI FSalida = 0;
          #7#7 = "S";
          |GRABA 020#7a;
          |SI FSalida = 0;
               |GRABA 020#1n;
               nHay = 1;
          |FINSI;
          |LIBERA #7;
     |FINSI;
|FPRC;

|DEFBUCLE CargaSele;
     #20#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, CargaSele;
|FIN;

|PROCESO Pendientes;
     aFinaliza = "S";
     |BUCLE MiraFinaliza;

     |DDEFECTO #20;
     #20#0 = #7#2;
     #20#1 = #7#3;
     #20#2 = #7#4;
     #20#3 = "";
     #20#4 = aFinaliza;
     |GRABA 020#20n;
     nHay = 1;
|FPRC;

|DEFBUCLE Pendientes;
     #7#1;
     7;
     #0#3, #0#4, 0001, "N";
     #0#3, #0#4, 9999, "N";
     ;
     NULL, Pendientes;
|FIN;

|PROCESO Min20;
     #20#0 = 1;
|FPRC;

|PROCESO Max20;
     #20#0 = 9999;
|FPRC;

|PROCESO Seleccion;
     |HAZ CreaTempo; aTmp20 = Tempo; |NOME_DAT #20 Tempo; |DELINDEX #20;
     nHay = 0;
     |ABRE #20;
     |ABRE #6;
     |BUCLE Pendientes;
     |CIERRA #6;
     |CIERRA #20;
     |SI nHay = 0;
          |MENAV "0000No hay paquetes pendientes";
     |SINO;
          |PUSHV 0501 2380;
          |ENTLINEAL #1#20, 1, 4, 22, 1, Min20, Max20;
          |POPV;
          |ABRE #7;
          nHay = 0;
          |BUCLE CargaSele;
          |CIERRA #7;
     |FINSI;
     Tempo = aTmp20; |HAZ BoraTempo; |DELINDEX #20;
|FPRC;

|PROCESO Marca; |TIPO 0;
     |SI nModo = 4;
          |MENAV "0000Opcion no valida en consulta";
          |ACABA;
     |FINSI;
     |SI #20#4 = "N";
          |MENAV "0000Solo se pueden marcar los paquetes terminados...";
          |ERROR; |ACABA;
     |FINSI;
     |SI #20Campo = Contenido; |Y FSalida = 0;
          |SI #20#3 = "*";
               #20#3 = "";
          |SINO;
               #20#3 = "*";
          |FINSI;
          |GRABA 020#20a;
          |PINTA #20#3;
     |FINSI;
|FPRC;

|PROCESO Tipo20; |TIPO 20;
     |SI FSalida = "OPCION";
          FSalida = "Imprimir";
     |SINO;
          |HAZ Imprime;
     |FINSI;
|FPRC;

|PROCESO Tipo3; |TIPO 3;
     |CONFI "0000NImprimir orden de carga?";
     |SI FSalida = 0;
          |HAZ Imprime;
     |FINSI;
|FPRC;

|PROCESO Tmp;
     |SI #26#0 = 2;
          |ACABA;         || No imprime las lineas de pedido
                          || quitar el acaba para imprimir
     |FINSI;

     |DDEFECTO #19;
     #19#0 = #26#2;
     |LEE 000#19=;

     |PRINT;
|FPRC;

|DEFBUCLE Tmp;
     #26#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, Tmp;
|FIN;

|PROCESO LinPaquete;
     |DDEFECTO #26;
     #26#0 = 1;
     #26#1 = #1#2; ||paquete
     #26#2 = #1#5; ||articulo
     #26#3 = #8#4; ||medida
     |LEE 101#26=;
     |SI FSalida ! 0; |GRABA 020#26b; |FINSI;
     #26#4 = #26#4 + #8#5;
     |GRABA 020#26a;
     |LIBERA #26;

     |DDEFECTO #26;
     #26#0 = 2;
     #26#1 = #8#3; ||linea pedido
     #26#2 = #1#5; ||articulo
     #26#3 = #8#4; ||medida
     |LEE 101#26=;
     |SI FSalida ! 0; |GRABA 020#26b; |FINSI;
     #26#4 = #26#4 + #8#5;
     |GRABA 020#26a;
     |LIBERA #26;
|FPRC;

|DEFBUCLE LinPaquete;
     #8#1;
     ;
     #0#3, #0#4, #1#2, 001;
     #0#3, #0#4, #1#2, 999;
     ;
     NULL, LinPaquete;
|FIN;

|PROCESO LinCarga;
     |BUCLE LinPaquete;
|FPRC;

|PROCESO Imprime;
     |ABRE #104;
     #104#25 = #0#3;
     #104#0 = #0#4;
     |LEE 000#104=;
     |CIERRA #104;

     |ABRE #13;
     #13#0 = #0#5;
     |LEE 000#13=;
     |CIERRA #13;

     Impresora = "Crystal Reports";
     |IMPRE 0;
     |SI FSalida = 0;
          |INFOR "mecam014";
          |SI FSalida = 0;
               aAlfa = Usuario;
               |NOME_DAT #26 aAlfa; |DELINDEX #26;

               |ABRE #26;
               |BUCLELIN 0 LinCarga #0;
               |CIERRA #26;

               |ABRE #19;
               |BUCLE Tmp;
               |CIERRA #19;

               |PIEINF;
               |FININF;

               |DELINDEX #26
          |SINO;
               |MENAV "0000Error en informe 'mecam014'";
          |FINSI;
          |FINIMP;
     |FINSI;
|FPRC;
