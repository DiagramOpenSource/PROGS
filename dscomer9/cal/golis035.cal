GJ003/1

|FICHEROS;
     golis035 #0;
     gomantho #1;
     gomantl1 #2;
     gopartia #3;
     gocerlin #4;
     gocercab #5;
     gopre001 #10;
     guerm035 #35;
     agifa080 #80;
     agifa084 #84;
     gotmp004 #100;
     gotmp006 #101;
     agifa142 #142;
     agifa724 #724;
     cacuenta@;
|FIN;

|VARIABLES;
     &enTipo = 0;
     &Tempo = "";
     aD = "                    ";
     aH = "zzzzzzzzzzzzzzzzzzzz";
     nSw = 0;
     aDef = "";
     aPath = "";
     aAlfa = "";
     aTipo = "";
     aTmp100 = "";
     aTmp101 = "";
|FIN;

|PROCESO gocerlin2;
     |SI #4#7 = "S";
          |PRINT;
     |FINSI;
|FPRC;

|DEFBUCLE gocercab;
     #5#2;
     0, 1;
     #0#0, #0#1, #0#10, #0#12;
     #0#0, #0#1, #0#11, #0#13;
     ;
     NULL, NULL, gocerlin2;
|FIN;

|PROCESO FactuDir;
     #101#0 = #84#48;
     #101#1 = #84#0;
     |LEE 000#101=;
     |SI FSalida = 0; |ACABA; |FINSI; || es factura de certificacion

     |SI nSw = 1;
          aAlfa = #0#0; |QBF aAlfa; aAlfa = aAlfa + "/";
          aAlfa = #84#87 - aAlfa;
          |SI FSalida > 0;
               #0#14 = #0#14 + #84#72;
          |FINSI;
     |FINSI;
     |SI nSw = 3;
          |PRINT;
     |FINSI;
|FPRC;

|DEFBUCLE FactuDir;
     #84#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, FactuDir;
|FIN;

|PROCESO Tmp;
     |SI aDef ! "";
          |DDEFECTO #cacuenta@;
          #cacuenta@#0 = #100#3;
          |LEE 000#cacuenta@.=;
          #100#6 = #cacuenta@#2;
     |FINSI;
     |PRINT;
|FPRC;

|DEFBUCLE Tmp;
     #100#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, Tmp;
|FIN;

|PROCESO gopartia;
     |SI nSw = 1; |O nSw = 3;
          |SI nSw = 1;
               #0#4 = #0#4 + #3#14;
          |FINSI;

          || tmp cuentas
          #1#0 = #3#2;
          #1#1 = #3#3;
          |LEE 020#1=;
          |SI FSalida = 0;
               #724#0 = #3#20;
               |LEE 000#724=;
               |SI FSalida ! 0; |DDEFECTO #724; |FINSI;

               |SI #3#34 = "A"; |O #3#34 = "T";
                    aTipo = "I";
               |SINO;
                    aTipo = "C";
                    #80#27 = #3#4;
                    #80#0 = #3#5;
                    #80#1 = #3#6;
                    |LEE 000#80=;
                    |SI FSalida = 0;
                         #35#0 = #80#15;
                         #35#2 = #80#2;
                         |LEE 000#35=;
                         |SI FSalida = 0;
                              aTipo = "M";
                         |FINSI;
                     |FINSI;
               |FINSI;

               |DDEFECTO #100;
               #100#0 = #2#0;
               #100#1 = #2#1;
               #100#2 = aTipo;
               #100#3 = #724#3;
               |SI nSw = 1;
                    |LEE 101#100=;
                    |SI FSalida ! 0; |GRABA 020#100b; |FINSI;
                    #100#4 = #100#4 + #3#25;
                    |GRABA 020#100a;
                    |LIBERA #100;
               |SINO;
                    #100#4 = #3#25;
                    |PRINT;
               |FINSI;
          |FINSI;
     |SINO;
          |PRINT;
     |FINSI;
|FPRC;

|DEFBUCLE gopartia;
     #3#3;
     ;
     #0#0, #0#1, #0#10, #0#12, aD, aD;
     #0#0, #0#1, #0#11, #0#13, aH, aH;
     ;
     NULL, gopartia;
|FIN;

|PROCESO gocerlin;
     #0#5 = #0#5 + #4#6;
     |SI #4#7 = "S";
          #0#6 = #0#6 + #4#6;

          #101#0 = #4#4;
          #101#1 = #4#5;
          |GRABA 020#101n;
     |FINSI;
|FPRC;

|DEFBUCLE gocerlin;
     #4#1;
     ;
     #2#3, #2#4, 001;
     #2#3, #2#4, 999;
     ;
     NULL, gocerlin;
|FIN;

|PROCESO gomantl1;
     |SI #2#3 < #0#10; |O #2#3 > #0#11; |ACABA; |FINSI;
     |SI #2#4 < #0#12; |O #2#4 > #0#13; |ACABA; |FINSI;
     |DDEFECTO #10;
     #10#0 = #2#3;
     #10#1 = #2#4;
     |LEE 000#10=;
     #0#3 = #0#3 + #10#5;

     |SI #0#8 = "      ";
          |SI #1#2 = "000000";     ||si el cliente es el 000000
               #0#8 = #10#2;       ||le asigno el del presupuesto
               #0#9 = #10#3;
          |SINO;
               #0#8 = #1#2;
               #0#9 = #1#8;
          |FINSI;
     |FINSI;
     |BUCLE gocerlin;
|FPRC;

|DEFBUCLE gomantho;
     #1#1;
     ;
     #0#0, #0#1;
     #0#0, #0#1;
     ;
     NULL, NULL, gomantl1;
|FIN;

|PROCESO Min;
     #2#0 = #0#0;
     #2#1 = #0#1;
     #2#3 = "     ";
     #2#4 = 000000;
|FPRC;

|PROCESO Max;
     #2#0 = #0#0;
     #2#1 = #0#1;
     #2#3 = "zzzzz";
     #2#4 = 999999;
|FPRC;

|PROCESO Tipo66D; |TIPO 66;
     |ABRE #2;
     |SELECT #2#2;
     #2#0 = #0#0;
     #2#1 = #0#1;
     #2#3 = #0#10;
     #2#4 = #0#12;
     |CONSULTA_F_CLAVE #2#2, Min, Max;
     |SI FSalida = 0;
          #0#10 = #2#3; |PINTA #0#10;
          #0#12 = #2#4; |PINTA #0#12;
     |SINO;
          |ERROR;
     |FINSI;
     |CIERRA #2;
|FPRC;

|PROCESO Tipo66H; |TIPO 66;
     |ABRE #2;
     |SELECT #2#2;
     #2#0 = #0#0;
     #2#1 = #0#1;
     #2#3 = #0#11;
     #2#4 = #0#13;
     |CONSULTA_F_CLAVE #2#2, Min, Max;
     |SI FSalida = 0;
          #0#11 = #2#3; |PINTA #0#11;
          #0#13 = #2#4; |PINTA #0#13;
     |SINO;
          |ERROR;
     |FINSI;
     |CIERRA #2;
|FPRC;

|PROCESO Tipo3; |TIPO 3;
     |ABRE #142; |LEE 000#142p; |CIERRA #142;
     aDef = #142#151; |QBF aDef;
     aDef = aDef + "/def/cacuenta";
     |CARGA_DEF aDef, cacuenta@;
     |SI FSalida ! 0;
          aAlfa = "0000Error al cargar:" + aDef;
          aDef = "";
          |MENAV aAlfa;
     |SINO;
          aPath = #142#151; |QBF aPath;
          aPath = aPath + "/fich/" + (("000000" + #142#152) % -106) + "/";
          |PATH_DAT #cacuenta@#aPath#;
          |ABRE #cacuenta@;
     |FINSI;

     Impresora = "Crystal Reports";
     |IMPRE 0;
     |SI FSalida = 0;
          |INFOR "golis035";
          |SI FSalida = 0;
               |HAZ CreaTempo; aTmp100 = Tempo;
               |HAZ CreaTempo; aTmp101 = Tempo;
               |NOME_DAT #100 aTmp100; |DELINDEX #100; |ABRE #100;
               |NOME_DAT #101 aTmp101; |DELINDEX #101; |ABRE #101;
               |ABRE #10;
               |BUCLE gomantho;
               |CIERRA #10;
               |ABRE #1;
               |ABRE #35;
               |ABRE #80;
               |ABRE #724;
               nSw = 1; |BUCLE gopartia;     || Calcula totales cab
               |BUCLE FactuDir;
               #0#7 = #0#5 - #0#4;
               enTipo = 1;
               nSw = 2; |BUCLE gopartia;     || Impre. Tipos de Gastos
               enTipo = 2;
               |BUCLE Tmp;                   || Impre. Cta Contables
               |SI #0#2 = "S";
                    enTipo = 3;
                    nSw = 3; |BUCLE gopartia;     || Impre. Desglose Cta
                    enTipo = 4;
                    |BUCLE gocercab;
                    enTipo = 5;
                    |BUCLE FactuDir;
               |FINSI;
               |CIERRA #724;
               |CIERRA #80;
               |CIERRA #35;
               |CIERRA #1;

               |CIERRA #101; |DELINDEX #101;
               |CIERRA #100; |DELINDEX #100;
               Tempo = aTmp100; |HAZ BoraTempo;
               Tempo = aTmp101; |HAZ BoraTempo;
               |PIEINF;
               |FININF;
          |SINO;
               |MENAV "0000Error en informe 'golis035'";
          |FINSI;
          |FINIMP;
     |FINSI;

     |SI aDef ! "";
          |CIERRA #cacuenta@;
          |DESCARGA_DEF #cacuenta@;
     |FINSI;
|FPRC;
