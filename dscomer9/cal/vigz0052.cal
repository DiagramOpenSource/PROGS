|FICHEROS;
     vigz0052 #0;
     vigm0037 #37;
     vigm0038 #38;
     vigm0019 #19;
     vigm0020 #20;
|FIN;

|VARIABLES;
     i = 0;
     nLin = 0;
     nNueva = 0;
|FIN;

|PROCESO EnsaFab; |TIPO 0;
     |PARA i = 1; |SI i [ 10; |HACIENDO i = i + 1;
          |C_M #0i, 1, "N";
     |SIGUE;
     |BLIN 19;
     |BLIN 20;
     |BLIN 21;
     |BLIN 22;

     |SI #0#0 = "E";
          |C_M #0#8, 1, "S";
          |C_M #0#9,1, "S";
          |C_M #0#10, 1, "S";

          |C_V #0#12, 1, "S";
          |C_V #0#13, 1, "S";
          |C_V #0#14, 1, "S";
          |C_V #0#15, 1, "S";
          |C_V #0#16, 1, "S";
          |C_V #0#17, 1, "S";
          |ATRI R; |PINTA 2103, "³Serie³Numero³Lin³ Articulo    ³ Descripcion                   ³    Cantidad³";
          |ATRI 0; |PINTA 2203, "³     ³      ³   ³             ³                               ³            ³";
     |SINO;
||          |C_M #0#1, 1, "S";
          |C_M #0#2, 1, "S";
          |C_M #0#3, 1, "S";
          |C_M #0#4, 1, "S";
          |C_M #0#5, 1, "S";
||          |C_M #0#6, 1, "S";
||          |C_M #0#7, 1, "S";

          |C_V #0#19, 1, "S";
          |C_V #0#20, 1, "S";
          |C_V #0#21, 1, "S";
          |C_V #0#22, 1, "S";
          |C_V #0#23, 1, "S";
          |C_V #0#24, 1, "S";
          |C_V #0#25, 1, "S";
          |C_V #0#26, 1, "S";
          |C_V #0#27, 1, "S";
          |ATRI R; |PINTA 1903, "³ Articulo           ³Maq³Alma ³    Cantidad ³Pienso              ³Granel³F³";
          |ATRI 0; |PINTA 2003, "³                    ³   ³     ³             ³                    ³      ³ ³";
          |ATRI R; |PINTA 2103, "³Des:                                                    Fab:              ³"; |ATRI 0;
     |FINSI;
|FPRC;

|PROCESO Min38;
     #38#0 = #0#8;
     #38#1 = 1;
|FPRC;

|PROCESO Max38;
     #38#0 = #0#8;
     #38#1 = 99999;
|FPRC;

|PROCESO Ensaque66; |TIPO 66;
     |ABRE #38;
     #38#0 = #0#8;
     #38#1 = #0#9;
     |LEE 000#38];
     |CONSULTA_F_CLAVE #1#38, Min38, Max38;
     |SI FSalida = 0;
          #0#9 = #38#1;
     |SINO;
          |ERROR;
     |FINSI;
     |CIERRA #38;
|FPRC;

|PROCESO Ensaque0; |TIPO 0;
     |SI #0#0 = "F"; |O FSalida = 2; |ACABA; |FINSI;
     |ABRE #38;
     #38#0 = #0#8;
     #38#1 = #0#9;
     |LEE 000#38=;
     |SI FSalida ! 0;
          |MENAV "0000No existe parte de ensaque...";
          |ERROR;
     |SINO;
          #0#12 = #38#2; |PINTA #0#12;
          #0#13 = #38#3; |PINTA #0#13;
          #0#14 = #38#4; |PINTA #0#14;
          #0#15 = #38#5; |PINTA #0#15;
          #0#16 = #38#6; |PINTA #0#16;
          #0#17 = #38#7; |PINTA #0#17;
     |FINSI;
     |CIERRA #38;
|FPRC;

|PROCESO Fabrica0; |TIPO 0;
     |SI #0#0 = "E"; |O FSalida = 2; |ACABA; |FINSI;

     |ABRE #19;
     #19#20 = #0#2;
     #19#0 = #0#3;
     |LEE 000#19=;
     |SI FSalida ! 0;
          |MENAV "0000No existe orden de fabricacion";
          |ERROR; |ACABA;
     |FINSI;
     |CIERRA #19;

     #0#1 = #19#1; |PINTA #0#1;

     |ABRE #20;
     #20#7 = #0#2;
     #20#0 = #0#3;
     #20#1 = #0#4;
     |LEE 000#20=;
     |SI FSalida ! 0;
          |MENAV "0000No existe orden de fabricacion";
          |ERROR;
     |SINO;
          #0#19 = #20#2; |PINTA #0#19;
          #0#20 = #20#3; |PINTA #0#20;
          #0#21 = #20#4; |PINTA #0#21;
          #0#22 = #20#5; |PINTA #0#22;
          #0#23 = #20#6; |PINTA #0#23;
          #0#24 = #20#8; |PINTA #0#24;
          #0#25 = #20#9; |PINTA #0#25;
          #0#26 = #20#10; |PINTA #0#26;
          #0#27 = #20#11; |PINTA #0#27
     |FINSI;
     |CIERRA #20;
|FPRC;

|PROCESO OrdenDes;
     |SI #0#0 = "E"; |O FSalida = 2; |ACABA; |FINSI;

     |C_M #0#6, 1, "N";

     |DDEFECTO #19;
     |ABRE #19;
     |SELECT #2#19;
     #19#1 = #0#5;
     |LEE 000#19];
     |SI FSalida ! 0; |O #19#1 ! #0#5;
          |SELECT #1#19;
          |AVISO;
          |CONFI "0000NNo existe orden de fabricacion. Crear nueva?";
          |SI FSalida ! 0; |ERROR; |ACABA; |FINSI;
          |C_M #0#6, 1, "S";
          #0#7 = 0; |PINTA #0#7;
     |SINO;
          #0#6 = #19#20; |PINTA #0#6;
          #0#7 = #19#0; |PINTA #0#7;
     |FINSI;
     |CIERRA #19;
|FPRC;

|PROCESO EnsaqueDes;
     |SI #0#0 ! "E"; |O FSalida = 2; |ACABA; |FINSI;

     |ABRE #37;
     #37#0 = #0#10;
     |LEE 000#37=;
     |SI FSalida ! 0;
          |AVISO;
          |CONFI "0000NNo existe parte de ensaque. Crear nuevo?";
          |SI FSalida ! 0; |ERROR; |ACABA; |FINSI;
     |FINSI;
     |CIERRA #37;
|FPRC;

|PROCESO EnsaqueNuevo;
     |SALVA_DATOS #38;

     |DDEFECTO #37;
     #37#0 = #0#10;
     |LEE 101#37=;
     |SI FSalida ! 0; |GRABA 020#37b; |FINSI;
     #38#0 = #0#10;
     #38#1 = 99999;
     |LEE 000#38];
     |SI FSalida = 0;
          |LEE 000#38a;
     |SINO;
          |LEE 000#38u;
     |FINSI;
     |SI FSalida = 0; |Y #38#0 = #0#10;
          nLin = #38#1 + 1;
     |SINO;
          nLin = 1;
     |FINSI;

     |REPON_DATOS #38;
     #38#0 = #0#10;
     #38#1 = nLin;
     |GRABA 020#38n;

     #37#1 = #37#1 + #38#7;
     |GRABA 020#37a;
|FPRC;

|PROCESO Ensaque;
     |ABRE #37;
     |ABRE #38;

     #37#0 = #0#8;
     |LEE 121#37=;
     |SI FSalida = 0;
          #38#0 = #0#8;
          #38#1 = #0#9;
          |LEE 121#38=;
          |SI FSalida = 0;
               |BORRA 020#38a;
               |LIBERA #38;

               #37#1 = #37#1 - #38#7;
               |GRABA 020#37a;

               |HAZ EnsaqueNuevo;
          |FINSI;
          |LIBERA #37;
     |FINSI;
     |CIERRA #38;
     |CIERRA #37;
|FPRC;

|PROCESO FabricaNuevo;
     |SALVA_DATOS #20;

     #19#20 = #0#6;
     #19#0 = #0#7;
     |LEE 000#19=;
     |SI FSalida ! 0; |O #0#7 = 0;
          #19#20 = #0#6;
          #19#0 = 999999;
          |LEE 000#19];
          |SI FSalida = 0;
               |LEE 000#19a;
          |SINO;
               |LEE 000#19u;
          |FINSI;
          |SI FSalida = 0; |Y #19#20 = #0#6;
               nNueva = #19#0 + 1;
          |SINO;
               nNueva = 1;
          |FINSI;
          |DDEFECTO #19;
          #19#20 = #0#6;
          #19#0 = nNueva;
          #19#1 = #0#5;
          #19#2 = #0#5;
          #19#3 = #0#5;
          |GRABA 020#19n;
     |FINSI;

     #20#7 = #19#20;
     #20#0 = #19#0;
     #20#1 = 999;
     |LEE 000#20];
     |SI FSalida = 0;
          |LEE 000#20a;
     |SINO;
          |LEE 000#20u;
     |FINSI;
     |SI FSalida = 0; |Y #20#7 = #19#20; |Y #20#0 = #19#0;
          nLin = #20#1 + 1;
     |SINO;
          nLin = 1;
     |FINSI;

     |REPON_DATOS #20;
     #20#7 = #19#20;
     #20#0 = #19#0;
     #20#1 = nLin;
     |GRABA 020#20n;
|FPRC;

|PROCESO Fabrica;
     |ABRE #19;
     |ABRE #20;
     #19#20 = #0#2;
     #19#0 = #0#3;
     |LEE 121#19=;
     |SI FSalida = 0;
          #20#7 = #0#2;
          #20#0 = #0#3;
          #20#1 = #0#4;
          |LEE 121#20=;
          |SI FSalida = 0;
               |BORRA 020#20a;
               |HAZ FabricaNuevo;
          |FINSI;
     |FINSI;
     |CIERRA #19;
     |CIERRA #20;
|FPRC;

|PROCESO Min20;
     #20#7 = #0#2;
     #20#0 = #0#3;
     #20#1 = 1;
|FPRC;

|PROCESO Max20;
     #20#7 = #0#2;
     #20#0 = #0#3;
     #20#1 = 999;
|FPRC;

|PROCESO FabriLin66; |TIPO 66;
     |ABRE #20;
     #20#7 = #0#2;
     #20#0 = #0#3;
     #20#1 = #0#4;
     |LEE 000#20];
     |CONSULTA_F_CLAVE #1#20, Min20, Max20;
     |SI FSalida = 0;
          #0#4 = #20#1; |PINTA #0#4;
     |SINO;
          |ERROR;
     |FINSI;
     |CIERRA #20;
|FPRC;

|PROCESO Fecha66; |TIPO 66;
     |DDEFECTO #19;
     |ABRE #19;
     |SELECT #2#19;
     #19#1 = #0#5;
     |LEE 000#19];
     |CONSULTA_CLAVE #2#19;
     |SI FSalida = 0;
          #0#5 = #19#1; |PINTA #0#5;
          #0#6 = #19#20; |PINTA #0#6;
          #0#7 = #19#0; |PINTA #0#7;
     |SINO;
          |ERROR;
     |FINSI;
     |CIERRA #19;
|FPRC;

|PROCESO Tipo3; |TIPO 3;
     |SI #0#0 = "E";
          |HAZ Ensaque;
     |SINO;
          |HAZ Fabrica;
     |FINSI;
|FPRC;

|PROGRAMA;
     |PARA; |SI; |HACIENDO;
          |DDEFECTO #0;
          |PINPA #0#0;
          |PINDA #0#0;
          |BLANCO 1901 2380;
          |ENDATOS #1#0;
          |SI FSalida ! 0; |SAL; |FINSI;
     |SIGUE;
|FPRO;
