|FICHEROS;
   gocerenc #0;

   goparame #10;
   concept@ #1000;
   cacuent@ #1001;
   canempr@ #1002;
   diarios@ #1003;
|FIN;

|VARIABLES;
   aAlfa  = "";
   aAlfa1 = "";

   nCalc  = 0;
   nTecla = 0;
   nMaxDigit = 0;
|FIN;

|PROCESO CompCuenta;
   aAlfa = #gocerenc#Campo#; |QBF aAlfa;
   aAlfa = aAlfa - ".";
   |PARA; |SI FSalida !0; |HACIENDO;
      aAlfa1 = aAlfa % 0; nCalc = aAlfa1;
      nCalc = nMaxDigit - nCalc;
      aAlfa1 = "0" * nCalc;
      nCalc = FSalida + 98; aAlfa1 = aAlfa1 + (aAlfa % nCalc);
      nCalc = ((FSalida / 100) ! 0) + 99;
      aAlfa1 = (aAlfa % nCalc) + aAlfa1; aAlfa = aAlfa1;
      aAlfa = aAlfa - ".";
   |SIGUE;
   #gocerenc#Campo# = aAlfa; |PINTA #gocerenc#Campo#;

   aAlfa = #gocerenc#Campo#; |QBF aAlfa;
   aAlfa = aAlfa % 0; nCalc = aAlfa;
   |SI nCalc = #canempr@#14; #cacuent@#1 = 1; |FINSI;
   |SI nCalc = #canempr@#15; #cacuent@#1 = 2; |FINSI;
   |SI nCalc = #canempr@#16; #cacuent@#1 = 3; |FINSI;
   |SI nCalc = #canempr@#17; #cacuent@#1 = 4; |FINSI;
   |SI nCalc = #canempr@#18; #cacuent@#1 = 5; |FINSI;
   |SI nCalc = #canempr@#19; #cacuent@#1 = 6; |FINSI;
   #cacuent@#0 = #gocerenc#Campo#;
   |LEE 000#cacuent@.=;
   |SI FSalida ! 0;
       |MENAV "    Cuenta inexistente";
       |ERROR; |ACABA;
   |SINO;
      |SI #cacuent@#3 = "N";
         aAlfa = "    NEsta seleccionando una cuenta sin pase directo. " + &191 + " Continuar ? ";
         |CONFI aAlfa;
         |SI FSalida ! 0; |ERROR; |FINSI;
      |FINSI;
   |FINSI;

   |CIERRA #canempr@; |DESCARGA_DEF #canempr@;
   |CIERRA #cacuent@; |DESCARGA_DEF #cacuent@;
|FPRC;

|PROCESO Cuentas;
     nTecla = FSalida;

     aAlfa = #gocerenc#Campo#;
     |QBF aAlfa;
     |SI aAlfa = "CLI";
          |ACABA;
     |SINO;
          aAlfa = aAlfa % -103;
          |SI aAlfa = "COD";
               |ACABA;
          |FINSI;
     |FINSI;

   |SI #gocerenc#2 = "N"; |ACABA; |FINSI;

   |ABRE #goparame;
   |LEE 000#goparame.p;
   |SI FSalida ! 0;
      |MENAV "    No existe el control de parametros";
      |CIERRA #goparame;
      |ERROR; |ACABA;
   |FINSI;
   |CIERRA #goparame;

   aAlfa = #goparame#84; |QBF aAlfa;
   aAlfa1 = aAlfa + "def/cacuenta.mas";
   |CARGA_DEF aAlfa1, cacuent@;
   |SI FSalida ! 0;
      |MENAV "    No encuentro el fichero de plan contable";
      |ERROR; |ACABA;
   |SINO;
      aAlfa1 = aAlfa + "def/canempre.mas";
      |CARGA_DEF aAlfa1, canempr@;
      |SI FSalida ! 0;
         |DESCARGA_DEF #cacuent@;
         |MENAV "    No encuentro el fichero de empresas";
         |ERROR; |ACABA;
      |FINSI;
   |FINSI;

   aAlfa1 = aAlfa + "fich/"; |PATH_DAT #1002#aAlfa1#;
   aAlfa1 = aAlfa + "fich/" + (("00000" + #goparame#85) % -105) + #goparame#86 + "/";
   |PATH_DAT #1001#aAlfa1#;

   |ABRE #canempr@;
   #canempr@#2 = #goparame#85;
   #canempr@#3 = #goparame#86;
   |LEE 000#canempr@.=;
   |SI FSalida ! 0; |DDEFECTO #canempr@; |FINSI;

   nMaxDigit = 0;
   |SI #canempr@#14 ] nMaxDigit; nMaxDigit = #canempr@#14; |FINSI;
   |SI #canempr@#15 ] nMaxDigit; nMaxDigit = #canempr@#15; |FINSI;
   |SI #canempr@#16 ] nMaxDigit; nMaxDigit = #canempr@#16; |FINSI;
   |SI #canempr@#17 ] nMaxDigit; nMaxDigit = #canempr@#17; |FINSI;
   |SI #canempr@#18 ] nMaxDigit; nMaxDigit = #canempr@#18; |FINSI;
   |SI #canempr@#19 ] nMaxDigit; nMaxDigit = #canempr@#19; |FINSI;

   |ABRE #cacuent@;
   |SI nTecla = 10;
      #cacuent@#0 = #gocerenc#Campo#;
      |LEE 000#cacuent@.];
      |CONSULTA_CLAVE #1#cacuent@;
      |SI FSalida = 0;
         #gocerenc#Campo# = #cacuent@#0; |PINTA #gocerenc#Campo#;
      |FINSI;
   |FINSI;

     |HAZ CompCuenta;
|FPRC;

|PROCESO MiraCpts;
   nTecla = FSalida;

   |SI #gocerenc#2 = "N"; |ACABA; |FINSI;

   |ABRE #goparame;
   |LEE 000#goparame.p;
   |SI FSalida ! 0;
      |MENAV "    No existe el control de parametros";
      |CIERRA #goparame;
      |ERROR; |ACABA;
   |FINSI;
   |CIERRA #goparame;

   aAlfa = #goparame#84; |QBF aAlfa;
   aAlfa = aAlfa - "contagen/";
   |SI FSalida ! 0;
      aAlfa = aAlfa + "modelos/";
      aAlfa1 = aAlfa + "def/dsmom030.mas";
      |CARGA_DEF aAlfa1, concept@;
      |SI FSalida ! 0;
         |MENAV "    No encuentro el fichero de conceptos";
         |ERROR; |ACABA;
      |SINO;
         aAlfa1 = aAlfa + "fich/"; |PATH_DAT #1000#aAlfa1#;
      |FINSI;
   |SINO;
      aAlfa = aAlfa - "contagen\";
      |SI FSalida ! 0;
         aAlfa = aAlfa + "modelos/";
         aAlfa1 = aAlfa + "def/dsmom030.mas";
         |CARGA_DEF  aAlfa1, concept@;
         |SI FSalida ! 0;
            |MENAV "    No encuentro el fichero de conceptos";
            |ERROR; |ACABA;
         |SINO;
            aAlfa1 = aAlfa + "fich/"; |PATH_DAT #1000#aAlfa1#;
         |FINSI;
      |SINO;
         aAlfa1 = aAlfa + "/def/caconcep.mas";
         |CARGA_DEF  aAlfa1, concept@;
         |SI FSalida ! 0;
            |MENAV "    No encuentro el fichero de conceptos";
            |ERROR; |ACABA;
         |SINO;
            aAlfa1 = aAlfa + "fich/" + (("00000" + #goparame#85) % -105) + #goparame#86 + "/";
            |PATH_DAT #1000#aAlfa1#;
         |FINSI;
      |FINSI;
   |FINSI;

   |ABRE #concept@;
   |SI nTecla = 10;
      #concept@#0 = #gocerenc#5;
      |LEE 000#concept@.];
      |CONSULTA_CLAVE #1#concept@;
      |SI FSalida = 0;
         #gocerenc#5 = #concept@#0; |PINTA #gocerenc#5;
         #gocerenc#6 = #concept@#1; |PINTA #gocerenc#6;
      |FINSI;
   |FINSI;
   #concept@#0 = #gocerenc#5;
   |LEE 000#concept@.=;
   |SI FSalida ! 0;
      |MENAV "    Concepto inexistente";
      |ERROR;
   |SINO;
      #gocerenc#6 = #concept@#1; |PINTA #gocerenc#6;
   |FINSI;
   |CIERRA #concept@; |DESCARGA_DEF #concept@;
|FPRC;

|PROCESO Codigo;
   |SI #gocerenc#2 = "S";
      |C_M #gocerenc#3, 1, "S"; |C_M #gocerenc#4, 1, "S";
      |C_M #gocerenc#5, 1, "S";
   |SINO;
      #gocerenc#3 = ""; |PINTA #gocerenc#3;
      #gocerenc#4 = ""; |PINTA #gocerenc#4;
      #gocerenc#5 = ""; |PINTA #gocerenc#5;
      #gocerenc#6 = ""; |PINTA #gocerenc#6;
      |C_M #gocerenc#3, 1, "N"; |C_M #gocerenc#4, 1, "N";
      |C_M #gocerenc#5, 1, "N";
   |FINSI;
|FPRC;

|PROCESO MiraDiarios;
   nTecla = FSalida;

   |SI #gocerenc#2 = "N"; |ACABA; |FINSI;

   |ABRE #goparame;
   |LEE 000#goparame.p;
   |SI FSalida ! 0;
      |MENAV "    No existe el control de parametros";
      |CIERRA #goparame;
      |ERROR; |ACABA;
   |FINSI;
   |CIERRA #goparame;

   aAlfa = #goparame#84; |QBF aAlfa;
   aAlfa1 = aAlfa + "def/camandia.mas";
   |CARGA_DEF aAlfa1, diarios@;
   |SI FSalida ! 0;
      |MENAV "    No encuentro el fichero de diarios";
      |ERROR; |ACABA;
   |SINO;
      aAlfa1 = aAlfa + "fich/" + (("00000" + #goparame#85) % -105) + #goparame#86 + "/";
      |PATH_DAT #1003#aAlfa1#;
   |FINSI;

   |ABRE #diarios@;
   |SI nTecla = 10;
      #diarios@#0 = #gocerenc#7;
      |LEE 000#diarios@.];
      |CONSULTA_CLAVE #1#diarios@;
      |SI FSalida = 0;
         #gocerenc#7 = #diarios@#0; |PINTA #gocerenc#7;
         #gocerenc#8 = #diarios@#1; |PINTA #gocerenc#8;
      |FINSI;
   |FINSI;
   #diarios@#0 = #gocerenc#7;
   |LEE 000#diarios@.=;
   |SI FSalida ! 0;
      |MENAV "    Diario inexistente";
      |ERROR;
   |SINO;
      #gocerenc#8 = #diarios@#1; |PINTA #gocerenc#8;
   |FINSI;
   |CIERRA #diarios@; |DESCARGA_DEF #diarios@;
|FPRC;
