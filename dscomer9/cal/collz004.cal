|FICHEROS;
     collz004 #0;
     collm001 #1;
     collm002 #2;
     collm004 #4;
     collm011 #11;
     collw002 #20,MANTE;
     collw003 #30;
|FIN;

|VARIABLES;
     &Tempo = "";
     aTmp20 = "";
     aTmp30 = "";
     nTecla = 0;
     nClave = 0;
     fFecha = @;
     aAlfa = "";
     nHay = 0;
     i = 0;
     nCampo = 0;
     nXls = 0;
     aAlf1 = "";
     aAlf2 = "";
     aAlf3 = "";
     aAlf4 = "";
     aAlf5 = "";
     aAlf6 = "";
     aAlf7 = "";
     aAlf8 = "";
     aRuta = "";
     aDato     = "";
     nHandle   = 0;
     aLon      = "";
     nPos      = 0;
     aLetra    = "";
     aReg      = "";
     aTab      = "";
 {99}Valor     = "";
     nLinea = 0;
     nPrime = 0;
     nInfor = 0;
     nImpre = 0;
|FIN;

|PROCESO ActualizaImp;
     |SELECT #2#20;
     #20#3 = #30#0;
     |LEE 121#20=;
     |SI FSalida = 0;
          #20#8 = #30#2;
          #20#9 = #30#1;
          #20#10 = #20#8 - #20#6;
          |GRABA 020#20a;

          |SELECT #1#11;
          #11#0 = #20#0;
          #11#1 = #20#1;
          #11#2 = #20#2;
          #11#3 = #20#3;
          #11#5 = #20#5;
          #11#4 = #20#4;
          |LEE 121#11=;
          |SI FSalida = 0;
               |HAZ Actua11;
               |LIBERA #11;
          |FINSI;
          |LIBERA #20;
     |FINSI;
|FPRC;

|DEFBUCLE ActualizaImp;
     #30#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, ActualizaImp;
|FIN;

|PROCESO Imprime2;
     |SI nPrime = 0;
          nPrime = 1;
          |IMPRE 0;
          nImpre = FSalida;
          |SI FSalida = 0;
               |INFOR "collz0b4";
               nInfor = FSalida;
               |SI FSalida ! 0;
                    |MENAV "0000Error en informe 'collz0b4'";
               |FINSI;
          |FINSI;
     |FINSI;
     |SI nImpre = 0; |Y nInfor = 0;
          |PRINT;
     |FINSI;
|FPRC;

|PROCESO Imprime1;
     |SI nPrime = 0;
          nPrime = 1;
          |IMPRE 0;
          nImpre = FSalida;
          |SI FSalida = 0;
               |INFOR "collz0a4";
               nInfor = FSalida;
               |SI FSalida ! 0;
                    |MENAV "0000Error en informe 'collz0a4'";
               |FINSI;
          |FINSI;
     |FINSI;
     |SI nImpre = 0; |Y nInfor = 0;
          |PRINT;
     |FINSI;
|FPRC;

|PROCESO Chequea2;
     |SELECT #4#11;
     #11#0 = #0#0;
     #11#1 = #0#1;
     #11#3 = #30#0;
     #11#4 = #0#2;
     #11#5 = #0#3;
     |LEE 000#11=;
     |SI #11#8 ! 0;
          |HAZ Imprime2;
     |FINSI;
|FPRC;

|DEFBUCLE Chequea2;
     #30#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, Chequea2;
|FIN;

|PROCESO Chequea1;
     |SELECT #2#20;
     #20#3 = #30#0;
     |LEE 000#20=;
     |SI FSalida ! 0;
          |HAZ Imprime1;
     |FINSI;
|FPRC;

|DEFBUCLE Chequea1;
     #30#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, Chequea1;
|FIN;

|PROCESO Dato;
     nCampo = 1;
     IValor = Valor1<-;
     aLon = aDato % A0;
     aReg = "";
     |PARA i = 1; |SI i [ aLon; |HACIENDO i = i + 1;
          nPos = i * 100 + 1;
          aLetra = aDato % nPos;
          |SI aLetra = aTab;
               Valor = aReg;
               aReg = "";
               nCampo = nCampo + 1;
               IValor = IValor + 1;
          |SINO;
               aReg = aReg + aLetra;
          |FINSI;
          |SI nCampo > 90; |SAL; |FINSI;     || Por si.... :)
     |SIGUE;
     Valor = aReg;
|FPRC;

|PROCESO ImportaTxt;
     nLinea = 0;
     aTab = &9;
     |XABRE "C", aRuta, nHandle;
     |SI FSalida ] 0
          |XLEE nHandle, 250, aDato;    || la 1§ se desprecia
          |PARA; |SI FSalida ] 0; |HACIENDO;
               nLinea = nLinea + 1;
               aAlfa = "Importando:" + nLinea;
               |PINTA 2307, aAlfa;

               |XLEE nHandle, 250, aDato;
               |SI FSalida < 0; |SAL; |FINSI;
               |HAZ Dato; #30#0 = Valor2;

               |XLEE nHandle, 250, aDato;
               |SI FSalida < 0; |SAL; |FINSI;

               |XLEE nHandle, 250, aDato;
               |SI FSalida < 0; |SAL; |FINSI;
               |HAZ Dato; #30#1 = Valor1; #30#2 = Valor3;

               |GRABA 000#30n;

               FSalida = 0;
          |SIGUE;
          |XCIERRA nHandle;
     |SINO;
          |MENAV "0000Error al abrir el fichero...";
     |FINSI;
|FPRC;

|PROCESO ImportaXLS;
     aTab = &9;
     |XABRE "CE", aRuta, nHandle;
     |SI FSalida ] 0
          |CARGADLL "agixl97.dll";
          |SI FSalida < 0;
               |MENAV "0000No se puede usar agixl97.dll";
          |SINO;
               |ALFACALLDLL "agixl97.dll", "carga_book", aRuta;
               |ALFACALLDLL "agixl97.dll", "selecciona_hoja", "Hoja1";
               |SI FSalida = 0;
                    |PARA nXls = 2; |SI nXls < 65536; |HACIENDO;
                         aAlfa = "Importando:" + nXls;
                         |PINTA 2307, aAlfa;

                         aAlf2 = "B" + nXls; |ALFACALLDLL "agixl97.dll", "peek_dato", aAlf2;
                         |SI FSalida ! 0; |SAL; |FINSI;
                         |QBF aAlf2;
                         |SI aAlf2 = ""; |SAL; |FINSI;

                         aAlf3 = "K" + nXls; |ALFACALLDLL "agixl97.dll", "peek_dato", aAlf3;
                         |SI FSalida ! 0; |SAL; |FINSI;

                         aAlf4 = "C" + nXls; |ALFACALLDLL "agixl97.dll", "peek_dato", aAlf4;
                         |SI FSalida ! 0; |SAL; |FINSI;

                         #30#0 = aAlf2;
                         #30#1 = aAlf3;
                         #30#2 = aAlf4;
                         |GRABA 020#30n;

                         nXls = nXls + 1;

                    |SIGUE;
                    |ALFACALLDLL "agixl97.dll", "fin_book", "salva";
               |FINSI;
          |FINSI;
          |DESCARGADLL "agixl97.dll";
     |SINO;
          |MENAV "0000Error al abrir el fichero...";
     |FINSI;
|FPRC;

|PROCESO ImportaFic;
     |DELINDEX #30;
     |ABRE #30;
     aRuta = "F*.*";
     |BROWSE aRuta;
     aRuta = aRuta % 2;
     |SI aRuta ! "F";
          aAlfa = aRuta % -103;
          aAlfa = aAlfa < "";
          |SI aAlfa = "txt";
               |HAZ ImportaTxt;
          |SINO;
               |SI aAlfa = "xls";
                    |HAZ ImportaXLS;
               |SINO;
                    |MENAV "0000Fichero con extension incorrecta, las posibles son .xls y .txt";
               |FINSI;
          |FINSI;
     |FINSI;
     |CIERRA #30;
|FPRC;

|PROCESO Actua11;
     #11#8 = #20#8;
     #11#9 = #20#9;
     #11#10 = #20#10;
     |GRABA 020#11a;
|FPRC;

|PROCESO Lectura; |TIPO 0;
     |SI Campo = 8;
          |SI #20#8 < #20#6;
               |MENAV "0000La lectura no puede ser menor al anterior";
               |ERROR;
          |FINSI;
     |SINO;
          |SI #20#9 < #20#7;
               |MENAV "0000La lectura no puede ser menor al anterior";
               |ERROR;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Tecla0; |TIPO 0;
     |SI #20Campo = Contenido; |Y FSalida = 0; |Y Campo = nCampo;
          |SELECT #1#11;
          #11#0 = #20#0;
          #11#1 = #20#1;
          #11#2 = #20#2;
          #11#3 = #20#3;
          #11#5 = #20#5;
          #11#4 = #20#4;
          |LEE 101#11=
          |SI FSalida = 0;
               |SI #11#12 ! 0;
                    |MENAV "0000Factura de realizada...";
               |SINO;
                    |ENCAMPO #8#20;
                    |SI FSalida = 0;
                         |SI #20#9 = "01.01.0000"; #20#9 = ~; |FINSI;
                         |ENCAMPO #9#20;
                         |SI FSalida = 0;
                              #20#10 = #20#8 - #20#6;
                              |HAZ Actua11;
                              |GRABA 020#20a;
                         |FINSI;
                    |FINSI;
               |FINSI;
               |LEE 020#20=;
               |PINTA #20#8;
               |PINTA #20#9;
               |PINTA #20#10;
          |FINSI;
          |LIBERA #11;
     |FINSI;
|FPRC;

|PROCESO Tecla11; |TIPO 11;
     |SI nTecla ! 0; |ACABA; |FINSI;

     |SI FSalida = 10;   || F2
          nTecla = FSalida;
          nClave = (nClave + 1) $ 2;
          |PTEC 807;
     |FINSI;
     |SI FSalida = 12;   || F4
          nTecla = FSalida;
          |PTEC 807;
     |FINSI;
|FPRC;

|PROCESO Min;
     #20#0 = #0#0;
     #20#1 = #0#1;
     #20#2 = "      ";
     #20#3 = "          ";
     #20#4 = #0#2;
     #20#5 = #0#3;
|FPRC;

|PROCESO Max;
     #20#0 = #0#0;
     #20#1 = #0#1;
     #20#2 = "zzzzzz";
     #20#3 = "zzzzzzzzzz";
     #20#4 = #0#2;
     #20#5 = #0#3;
|FPRC;

|PROCESO CargaTmp;
     |PARA i = 0; |SI i [ 12; |HACIENDO i = i + 1;
          #20i = #11i;
     |SIGUE;
     |DDEFECTO #2;
     #2#0 = #11#2;
     |LEE 000#2=;
     #20#13 = #2#1;
     #20#14 = #11#13;
     |GRABA 020#20n;
     nHay = 1;
|FPRC;

|DEFBUCLE CargaTmp;
     #11#1;
     8;
     #0#0, #0#1, "      ", "          ", #0#3, #0#2, 0;
     #0#0, #0#1, "zzzzzz", "zzzzzzzzzz", #0#3, #0#2, 0;
     ;
     NULL, CargaTmp;
|FIN;

|PROCESO Tipo3; |TIPO 3;
     |HAZ CreaTempo; aTmp20 = Tempo; |NOME_DAT #20#Tempo#; |DELINDEX #20;
     |HAZ CreaTempo; aTmp30 = Tempo; |NOME_DAT #30#Tempo#; |DELINDEX #30;

     |ABRE #20;
     |ABRE #2;
     |BUCLE CargaTmp;
     |CIERRA #2;
     |CIERRA #20;
     |SI nHay = 0;
          |MENAV "0000No hay registros...";
     |SINO;
          |ABRE #4;
          #4#0 = #0#0;
          |LEE 000#4=;
          |CIERRA #4;
          |PINPA #0#20;
          |PINTA 651, #4#1;

          |ABRE #11;
          nClave = 0;
          |PARA nTecla = 1; |SI nTecla ! 0; |HACIENDO;
               nTecla = 0;
               |SI nClave = 0;
                    nCampo = 2;
                    |C_M #20#2, 1, "S";
                    |C_M #20#3, 1, "N";
                    |ENTLINEAL #1#20, 3, 4, 19, 0, Min, Max;
               |SINO;
                    nCampo = 3;
                    |C_M #20#2, 1, "N";
                    |C_M #20#3, 1, "S";
                    |ENTLINEAL #2#20, 1, 4, 19, 0, Min, Max;
               |FINSI;
               |SI nTecla = 12; || F4
                    |HAZ ImportaFic;
/*
|ABRE #30;
|CONSULTA_CLAVE #1#30;
|CIERRA #30;
*/
                    nPrime = 0;
                    nInfor = -1;
                    nImpre = -1;

                    |ABRE #20;
                    |ABRE #11;
                    |BUCLE Chequea1;
                    |SI nPrime = 0;
                         |BUCLE Chequea2;
                         |SI nPrime = 0;
                              |BUCLE ActualizaImp;
                         |FINSI;
                    |FINSI;
                    |CIERRA #20;
                    |CIERRA #11;

                    |SI nPrime ! 0;
                         |SI nInfor = 0; |PIEINF; |FININF; |FINSI;
                         |SI nImpre = 0; |FINIMP; |FINSI;
                    |FINSI;
               |FINSI;
          |SIGUE;
          |CIERRA #11;
     |FINSI;

     |DELINDEX #20;
     |DELINDEX #30;
|FPRC;

|PROGRAMA;
     |ABRE #1; |LEE 000#1p; |CIERRA #1;
     #0#1 = #1#1;
     fFecha = ~;
     aAlfa = fFecha % 402;
     #0#2 = aAlfa;
     aAlfa = fFecha % 704;
     #0#3 = aAlfa;
     |PINPA #0#0;
     |PINDA #0#0;
     |ENDATOS #1#0;
|FPRO;
