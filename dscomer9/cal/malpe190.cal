|FICHEROS;
     malpe190 #0;
     malpe191 #1;
     malpe003 #3;
     malpe006 #6; ||Trans
     malpe001 #10;
     dsm00024 #24;
     malpe081 #81;
     malpe085 #85;
     malpe090 #90;
     malpe091 #91;
     malpe109 #109;
     malpe192 #192;
     malpe193 #193,MANTE;
     malpe197 #197;
     malpe198 #198,MANTE;
     malpe200 #200;
     malpe201 #201;
     malpe203 #203; || Agen
     referen@;
|FIN;

|VARIABLES;
     &enTiva = 0;
     &efFiva = @;
     &eaDiva = "";
     &enIva = 0;
     &enRec = 0;
     aDSer1 = "";
     aDSer2 = "";
     aDSer3 = "";
     aDSer4 = "";
     aDSer5 = "";
     aDSer6 = "";
     aHSer1 = "";
     aHSer2 = "";
     aHSer3 = "";
     aHSer4 = "";
     aHSer5 = "";
     aHSer6 = "";
     &eaTipo = "";
     &enLiquida = 0;
     {-1}aMenu = "";
     aMenu1 = "2400";
     aMenu2 = "1";
     aMenu3 = "Tipo impresion:  ";
     aMenu4 = "PM";
     aMenu5 = " Papel ";
     aMenu6 = " Mail ";
     nError = 0;
     nForma = 0;
     aNom = "";
     nConta = 0;
     nModo = 0;
     aAlfa = "";
     aTipo = "";
     aTipoLin = "";
     nHay = 0;
     nLin = 0;
     aTran = "";
     aAgen = "";
     nAlma = 0;
     aMatri = "";
     nPeso = 0;
     nPrecio = 0;
     aCod = "";
     nFactu = 0;
     nEmp = 0;
     nLiqui = 0;
     fiac           = "  ";
     guarda         = "  ";
     relleno        = "                                       ";
     aponer         = "  ";
     vacio          = "                    ";
|FIN;

|PROCESO Tipo13; |TIPO 13;
     |HAZ PinTraAge;
|FPRC;

|PROCESO PinTraAge; |TIPO 0;
     |SI #0#5 = "T";
          |DDEFECTO #6;
          |ABRE #6;
          #6#0 = #0#6;
          |LEE 000#6=;
          |CIERRA #6;
          |PINTA 1016, #6#1;
          |PINTA 1066, #6#18;
     |SINO;
          |DDEFECTO #203;
          |ABRE #203;
          #203#0 = #0#6;
          |LEE 000#203=;
          |CIERRA #203;
          |PINTA 1016, #203#1;
          |PINTA 1066, #203#15;
     |FINSI;
|FPRC;

|PROCESO TraAgen0; |TIPO 0;
     nModo = (FEntrada / 100) ! 0;
     |SI #0#5 = "T";
          |ABRE #6;
          #6#0 = #0#6;
          |LEE 000#6=;
          |SI FSalida = 0;
               |SI nModo = 1; |O #0Campo ! Contenido;
                    #0#8 = #6#12; |PINTA #0#8;    || %Inc
                    #0#12 = #6#13; |PINTA #0#12;  || %Rete
                    #0#13 = #6#14; |PINTA #0#13;  || B/T
                    #0#10 = #6#15; |PINTA #0#10;  || IVA
                    #0#2 = #6#16; |PINTA #0#2;    || Forma
               |FINSI;
               |HAZ PinTraAge;
          |SINO;
               |MENAV "0000No existe transportista";
               |ERROR;
          |FINSI;
          |CIERRA #6;
     |SINO;
          |ABRE #203;
          #203#0 = #0#6;
          |LEE 000#203=;
          |SI FSalida = 0;
               |SI nModo = 1; |O #0Campo ! Contenido;
                    #0#8 = #203#9; |PINTA #0#8;
                    #0#12 = #203#10; |PINTA #0#12;
                    #0#13 = #203#11; |PINTA #0#13;
                    #0#10 = #203#12; |PINTA #0#10;
                    #0#2 = #203#13; |PINTA #0#2;
               |FINSI;
               |HAZ PinTraAge;
          |SINO;
               |MENAV "0000No existe agencia de transporte";
               |ERROR;
          |FINSI;
          |CIERRA #203;
     |FINSI;
|FPRC;

|PROCESO TraAgen66; |TIPO 66;
     |SI #0#5 = "T";
          |ABRE #6;
          #6#0 = #0#6;
          |LEE 000#6];
          |CONSULTA_CLAVE #1#6;
          |SI FSalida = 0;
               #0#6 = #6#0; |PINTA #0#6;
               |HAZ PinTraAge;
               #0#8 = #6#12; |PINTA #0#8;
               #0#12 = #6#13; |PINTA #0#12;
               #0#13 = #6#14; |PINTA #0#13;
               #0#10 = #6#15; |PINTA #0#10;
               #0#2 = #6#16; |PINTA #0#2;
          |SINO;
               |ERROR;
          |FINSI;
          |CIERRA #6;
     |SINO;
          |ABRE #203;
          #203#0 = #0#6;
          |LEE 000#203];
          |CONSULTA_CLAVE #1#203;
          |SI FSalida = 0;
               #0#6 = #203#0; |PINTA #0#6;
               |HAZ PinTraAge;
               #0#8 = #203#9; |PINTA #0#8;
               #0#12 = #203#10; |PINTA #0#12;
               #0#13 = #203#11; |PINTA #0#13;
               #0#10 = #203#12; |PINTA #0#10;
               #0#2 = #203#13; |PINTA #0#2;
          |SINO;
               |ERROR;
          |FINSI;
          |CIERRA #203;
     |FINSI;
|FPRC;

|PROCESO ContaFa; |TIPO 7;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 1;
          |LEE 000#0u;
          |SI FSalida = 0;
               nConta = #0#0 + 1;
          |SINO;
               nConta = 1;
          |FINSI;
          |DDEFECTO #0;
          #0#0 = nConta;
     |FINSI;
|FPRC;

|PROCESO KiloTone; |TIPO 0;
     |SI #1Campo ! Contenido;
          |SI Campo = 10;
               #1#11 = #1#10 / 1000;
               |PINTA #1#11;
          |SINO;
               #1#10 = #1#11 * 1000;
               |PINTA #1#10;
          |FINSI;
          |HAZ TotalLin;
     |FINSI;
|FPRC;

|PROCESO TotalLin; |TIPO 0;
     |SI #1#2 ! "S";
          #1#13 = #1#12 * #1#11;
          |PINTA #1#13;
     |FINSI;
|FPRC;

|PROCESO TotalFac;
     enTiva = #0#10;
     efFiva = #0#1;
     |HAZ SacaIVA;

     #0#9 = #0#7 + (#0#7 * #0#8 / 100); |PINTA #0#9;
     #0#11 = (#0#9 * enIva) / 100; |PINTA #0#11;
     |SI #0#13 = "B";
          #0#14 = #0#9 * #0#12 / 100;
     |SINO;
          #0#14 = (#0#9 + #0#11) * #0#12 / 100;
     |FINSI;
     |PINTA #0#14;
     #0#15 = #0#9 + #0#11 - #0#14; |PINTA #0#15;
|FPRC;

|PROCESO BorraAux;
     |BORRA 020#192a;
|FPRC;

|DEFBUCLE BorraAux;
     #192#1;
     ;
     #0#0, 00001;
     #0#0, 99999;
     be;
     NULL, BorraAux;
|FIN;

|PROCESO Tipo2Cab; |TIPO 2;
     nModo = (FEntrada / 100) ! 0;
     nForma = 0 +. 1;

     |SI nError = 1;
          nError = 0; |ERROR |ACABA;
     |FINSI;

     |SI nForma < 0; |Y #0#21 ! 0;
          |AVISO;
          |CONFI "0000NSi se borra/modifica debe de borrar/modificar el asiento contable a mano. ¿Continuar?";
          |SI FSalida ! 0;
               |SI nModo = 2; nError = 1; |FINSI;
               |ERROR; |ACABA;
          |FINSI;
     |FINSI;

     |SI nModo = 3;
          |BUCLE BorraAux;
     |FINSI;
|FPRC;

|PROCESO Tipo2Lin; |TIPO 2;
     nModo = (FEntrada / 100) ! 0;
     nForma = 0 +. 1;

     |SI nModo = 3;
          |SI #1#2 = "S";
               |AVISO;
               |CONFI "0000N¿Seguro de borrar el suplemento?";
               |SI FSalida = 0;
                    |AVISO;
                    |CONFI "0000N¿Confirma que realmente desea borrar el suplemento?";
                    |SI FSalida = 0;
                         |ABRE #109;
                         #109#0 = #1#6;
                         |BORRA 000#109c;
                         |CIERRA #109;
                    |FINSI;
               |FINSI;
          |FINSI;
     |FINSI;

     |HAZ TotalLin;
     #0#7 = #0#7 +. #1#13; |PINTA #0#7;
     |HAZ TotalFac;

     |DDEFECTO #192;
     |ABRE #192;
     #192#0 = #1#0;
     #192#1 = #1#9;
     |LEE 101#192=;
     |SI FSalida ! 0; |GRABA 020#192b; |FINSI;
     #192#2 = #192#2 +. #1#13;
     |GRABA 020#192a;
     |LIBERA #192;

     |SI nForma < 0;
          nLiqui = 0;
     |SINO;
          nLiqui = #0#0;
     |FINSI;

     |SI #1#2 = "A";
          |ABRE #91;
          #91#29 = #1#5;
          #91#0 = #1#6;
          #91#1 = #1#7;
          |LEE 101#91=;
          |SI FSalida = 0;
               #91#78 = nLiqui;
               |GRABA 020#91a;
          |FINSI;
          |CIERRA #91;
     |FINSI;
     |SI #1#2 = "T";
          aAlfa = ("000000" + #1#6) % -106;
          |ABRE #201;
          #201#0 = #1#7;
          #201#1 = aAlfa;
          |LEE 101#201=;
          |SI FSalida = 0;
               #201#10 = nLiqui;
               |GRABA 020#201a;
          |FINSI;
          |CIERRA #201;
     |FINSI;
     |SI #1#2 = "S";
          |ABRE #109;
          #109#0 = #1#6;
          |LEE 101#109=;
          |SI FSalida = 0;
               #109#16 = nLiqui;
               |GRABA 020#109a;
          |FINSI;
          |CIERRA #109;
     |FINSI;
|FPRC;

|PROCESO RecalLin;
     |HAZ Tipo2Lin;
     |GRABA 020#1a;
     |LIBERA #1;
|FPRC;

|DEFBUCLE RecalLin;
     #1#1;
     ;
     #0#0, 00001;
     #0#0, 99999;
     be;
     NULL, RecalLin;
|FIN;

|PROCESO RecalFac; |TIPO 0;
     |SI #0Campo ! Contenido;
          #0#7 = 0;
          |BUCLE BorraAux;
          |BUCLE RecalLin;
          |PINTA #0#7;
     |FINSI;
|FPRC;

|PROCESO TipoLin; |TIPO 0;
     |C_M #1#5, 1, "S";
     |C_M #1#6, 1, "S";
     |C_M #1#7, 1, "S";
     |C_M #1#8, 1, "S";
     |C_M #1#9, 1, "S";
     |C_M #1#10, 1, "S";
     |C_M #1#13, 1, "N";
     |SI #1#2 = "T";
          |C_M #1#5, 1, "N";
     |FINSI;
     |SI #1#2 = "S";
          |C_M #1#5, 1, "N";
          |C_M #1#7, 1, "N";
          |C_M #1#8, 1, "N";
          |C_M #1#9, 1, "N";
          |C_M #1#10, 1, "N";
          |C_M #1#11, 1, "N";
          |C_M #1#12, 1, "N";
          |C_M #1#13, 1, "S";
     |FINSI;
|FPRC;

|PROCESO poncontr;
     fiac = Control % A109;
     fiac = fiac + relleno;
     guarda = fiac % A109;
     fiac = Control % A1001;
     Control = Control + fiac;
     Control = Control + relleno;
     Control = Control % A120;
     Control = Control + aponer;
|FPRC;

|PROCESO Tipo11; |TIPO 11;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 4; |ACABA; |FINSI;
     |SI FSalida = 10;
          aTipoLin = "A";
          |HAZ Carga;
     |FINSI;
     |SI FSalida = 11;
          aTipoLin = "S";
          |HAZ Carga;
     |FINSI;
     |SI FSalida = 12;
          aTipoLin = "T";
          |HAZ Carga;
     |FINSI;
     |SI FSalida = 13;
          |HAZ RedondeoFac;
     |FINSI;
|FPRC;

|PROCESO Min;
     #193#0 = "     ";
     #193#1 = 1;
     #193#2 = 0;
|FPRC;

|PROCESO Max;
     #193#0 = "zzzzz";
     #193#1 = 999999;
     #193#2 = 999;
|FPRC;

|PROCESO Tecla; |TIPO 0;
     |SI #193Campo = Contenido; |Y FSalida = 0;
          |SI #193#10 = "*";
               nHay = nHay - 1;
               #193#10 = "";
          |SINO;
               nHay = nHay + 1;
               #193#10 = "*";
          |FINSI;
          |GRABA 020#193a;
     |FINSI;
|FPRC;

|PROCESO Descarga;
     |SI #193#10 ! "*";
          |ACABA;
     |FINSI;
     #1#0 = #0#0;
     #1#1 = 99999;
     |LEE 000#1];
     |SI FSalida = 0;
          |LEE 000#1a;
     |SINO;
          |LEE 000#1u;
     |FINSI;
     |SI FSalida = 0; |Y #1#0 = #0#0;
          nLin = #1#1 + 1;
     |SINO;
          nLin = 1;
     |FINSI;
     |DDEFECTO #1;
     #1#0 = #0#0;
     #1#1 = nLin;
     #1#2 = aTipoLin;
     #1#3 = #193#6;
     #1#4 = #193#7;
     #1#5 = #193#0;
     #1#6 = #193#1;
     #1#7 = #193#2;
     #1#8 = #193#11;
     #1#9 = #193#8;
     #1#10 = #193#15;
     #1#11 = #193#13;
     #1#12 = #193#14;
     #1#13 = #193#9;
     |GRABA 020#1n;
     |HAZ Tipo2Lin;
|FPRC;

|DEFBUCLE Descarga;
     #193#1;
     ;
     "     ", 000001, 000;
     "zzzzz", 999999, 999;
     ;
     NULL, Descarga;
|FIN;

|PROCESO Cabecera;
     |QBF aAgen;
     |SI aAgen = "";
          |DDEFECTO #6;
          #6#0 = aTran;
          |LEE 000#6=;
          aCod = #6#0;
          aNom = #6#1;
          aTipo = "T";
          nFactu = #6#16;
     |SINO;
          |DDEFECTO #203;
          #203#0 = aAgen;
          |LEE 000#203=;
          aCod = #203#0;
          aNom = #203#1;
          aTipo = "A";
          nFactu = #203#13;
     |FINSI;

     |SI nFactu = 1;
          aMatri = "";
          nAlma = 0;
     |FINSI;
     |SI nFactu = 2;
          aMatri = "";
     |FINSI;
     |SI nFactu = 3;
          nAlma = 0;
     |FINSI;

     aAlfa = #0#3; |QBF aAlfa;
     |SI #0#5 = aTipo; |Y #0#2 = nFactu; |Y #0#6 = aCod; |Y aAlfa = aMatri;
               |Y #0#4 = nAlma; |Y #0#22 = nEmp;
          FSalida = 0;
     |SINO;
          FSalida = 1;
     |FINSI;
|FPRC;

|PROCESO PesoAlb;
     #24#0 = #91#2;
     #24#1 = #91#3;
     #24#2 = #91#49;
     |LEE 000#24=;
     |SI FSalida ! 0 ; |DDEFECTO #24; |FINSI;

     #85#0 = #24#0;
     #85#1 = #24#1;
     #85#2 = #24#2;
     |LEE 000#85=;
     |SI FSalida ! 0; |DDEFECTO #85; |FINSI;
     nPeso = #85#3;;
|FPRC;

|PROCESO PrecioAlb;
     #10#0 = #90#3;  ||Cli
     #10#1 = #90#84; ||Obra
     |LEE 000#10=;
     |SI FSalida ! 0 ; |DDEFECTO #1; |FINSI;
     #3#0 = #10#22;  ||Tipo Trans
     #3#3 = #90#1;  ||Fecha Alba
     |LEE 000#3];
     |SI FSalida ! 0;
          |LEE 000#3u;
     |SINO;
          |LEE 000#3a;
     |FINSI;
     |SI FSalida ! 0 ; |O #3#0 ! #10#22;
          |DDEFECTO #3;
     |FINSI;
     nPrecio = #3#2;
|FPRC;

|PROCESO Albaranes;
     #90#52 = #91#29;
     #90#0 = #91#0;
     |LEE 000#90=;
     |SI FSalida ! 0; |O #90#64 ! "P"; |O #90#1 > #0#1; |ACABA; |FINSI;

     |SI #91#66 = "R"; ||SI es una rotura, ni suma ni resta
          |ACABA;
     |FINSI;
     |SI #91#78 ! 0; |ACABA; |FINSI;    ||Ya esta calculado

     |SI #90#52 ] aDSer1; |Y #90#52 [ aHSer1; |ACABA; |FINSI;
     |SI #90#52 ] aDSer2; |Y #90#52 [ aHSer2; |ACABA; |FINSI;
     |SI #90#52 ] aDSer3; |Y #90#52 [ aHSer3; |ACABA; |FINSI;
     |SI #90#52 ] aDSer4; |Y #90#52 [ aHSer4; |ACABA; |FINSI;
     |SI #90#52 ] aDSer5; |Y #90#52 [ aHSer5; |ACABA; |FINSI;
     |SI #90#52 ] aDSer6; |Y #90#52 [ aHSer6; |ACABA; |FINSI;

     |SI #90#52 ] "PC   "; |Y #90#52 [ "PCZZZ";
          nEmp = 2;
     |SINO;
          nEmp = 1;
     |FINSI;

     aTran = #90#100;
     aAgen = #90#104;
     nAlma = #91#3;
     aMatri = #90#98;
     |HAZ Cabecera;
     |SI FSalida ! 0; |ACABA; |FINSI;

     |HAZ PesoAlb;
     |HAZ PrecioAlb;

     |DDEFECTO #193;
     #193#0 = #91#29;
     #193#1 = #91#0;
     #193#2 = #91#1;
     #193#3 = aTipo;
     #193#4 = aCod;
     #193#5 = aNom;
     #193#6 = #90#1;
     #193#7 = #90#98;
     #193#8 = #91#3;
     #193#11 = #91#2;
     #193#12 = #91#5;              ||Cantidad
     |SI #91#2 = #81#18; || Art. autodescarga
          #193#15 = #91#5 / #81#20 * 1000;
          #193#13 = #91#5 / #81#20;
          #193#14 = #81#19;
          #193#9 = #193#14 * #193#13;
     |SINO;
          #193#14 = nPrecio;
          #193#15 = nPeso * #91#5;      ||Peso Kilos
          #193#13 = #193#15 / 1000;     ||Peso Tn
          #193#9 = #193#14 * #193#13;   ||Importe
     |FINSI;
     |GRABA 020#193n;
     nHay = 1;
|FPRC;

|DEFBUCLE Albaranes;
     #91#6;
     ;
     0;
     0;
     be;
     NULL, Albaranes;
|FIN;

|PROCESO PesoTras;
     |DDEFECTO #85;
     #85#0 = #201#2;
     #85#1 = #201#7;
     #85#2 = #201#9;
     |LEE 000#85=;
     nPeso = #85#3;
|FPRC;

|PROCESO PrecioTras;
     #3#0 = #200#13; || Tipo Trans
     #3#3 = #200#7;   || Fecha
     |LEE 000#3];
     |SI FSalida ! 0;
          |LEE 000#3u;
     |SINO;
          |LEE 000#3a;
     |FINSI;
     |SI FSalida ! 0 ; |O #3#0 ! #200#13;
          |DDEFECTO #3;
     |FINSI;
     nPrecio = #3#2;
|FPRC;

|PROCESO Traspasos;
     |SI #201#10 ! 0; |ACABA; |FINSI; ||Ya esta calculado
     aTran = #200#14;
     aAgen = #200#15;
     nAlma = #201#7;
     aMatri = #200#11;
     |HAZ Cabecera;
     |SI FSalida ! 0; |ACABA; |FINSI;

     |HAZ PesoTras;
     |HAZ PrecioTras;
     |DDEFECTO #193;
     #193#1 = #201#1;
     #193#2 = #201#0;
     #193#3 = aTipo;
     #193#4 = aCod;
     #193#5 = aNom;
     #193#6 = #200#7;
     #193#7 = #200#11;
     #193#8 = #201#7;
     #193#11 = #201#2;
     #193#12 = #201#4;
     #193#14 = nPrecio;
     #193#15 = nPeso * #201#4;
     #193#13 = #193#15 / 1000;
     #193#9 = #193#14 * #193#13;
     |GRABA 020#193n;
     nHay = 1;
|FPRC;

|DEFBUCLE Traspasos;
     #200#3;
     ;
     "01.01.0000";
     #0#1;
     be;
     NULL, NULL, Traspasos;
|FIN;

|PROCESO Suplementos;
     aTran = #109#1;
     aAgen = #109#15;
     nAlma = #109#13;
     aMatri = #109#12;
     nEmp = #109#17;
     |HAZ Cabecera;
     |SI FSalida ! 0; |ACABA; |FINSI;

     |HAZ PesoTras;
     |HAZ PrecioTras;
     |DDEFECTO #193;
     #193#1 = #109#0;
     #193#3 = aTipo;
     #193#4 = aCod;
     #193#5 = aNom;
     #193#6 = #109#9;
     #193#7 = #109#12;
     #193#8 = #109#13;
     #193#9 = #109#14;
     |GRABA 020#193n;
     nHay = 1;
|FPRC;

|DEFBUCLE Suplementos;
     #109#2;
     16;
     "01.01.0000", 0;
     #0#1, 0;
     be;
     NULL, Suplementos;
|FIN;

|PROCESO Carga;
     |ABRE #197; |LEE 000#197p; |CIERRA #197;
     |ABRE #81; |LEE 000#81p; |CIERRA #81;

     aDSer1 = #197#1; |QBF aDSer1;
     aDSer2 = #197#2; |QBF aDSer2;
     aDSer3 = #197#3; |QBF aDSer3;
     aDSer4 = #197#4; |QBF aDSer4;
     aDSer5 = #197#5; |QBF aDSer5;
     aDSer6 = #197#6; |QBF aDSer6;
     aHSer1 = (aDSer1 + "zzzzzz") % 105;
     aHSer2 = (aDSer2 + "zzzzzz") % 105;
     aHSer3 = (aDSer3 + "zzzzzz") % 105;
     aHSer4 = (aDSer4 + "zzzzzz") % 105;
     aHSer5 = (aDSer5 + "zzzzzz") % 105;
     aHSer6 = (aDSer6 + "zzzzzz") % 105;
     aDSer1 = (aDSer1 + "      ") % 105;
     aDSer2 = (aDSer2 + "      ") % 105;
     aDSer3 = (aDSer3 + "      ") % 105;
     aDSer4 = (aDSer4 + "      ") % 105;
     aDSer5 = (aDSer5 + "      ") % 105;
     aDSer6 = (aDSer6 + "      ") % 105;

     aAlfa = Usuario;
     |NOME_DAT #193 aAlfa; |DELINDEX #193;
     nHay = 0;

     |ABRE #3;
     |ABRE #6;
     |ABRE #10;
     |ABRE #24;
     |ABRE #85;
     |ABRE #193;
     |ABRE #203;
     |SI aTipoLin = "A";
          |ABRE #90;
          |BUCLE Albaranes;
          |CIERRA #90;
     |FINSI;
     |SI aTipoLin = "S";
          |BUCLE Suplementos;
     |FINSI;
     |SI aTipoLin = "T";
          nEmp = 1;
          |BUCLE Traspasos;
     |FINSI;
     |CIERRA #203;
     |CIERRA #193;
     |CIERRA #85;
     |CIERRA #24;
     |CIERRA #10;
     |CIERRA #6;
     |CIERRA #3;

     |SI nHay = 0;
          |MENAV "0000No existen registros...";
     |SINO;
          nHay = 0;
          |PUSHV 0501 2380;
          |PINPA #0#193;
          |ENTLINEAL #1#193, 1, 4, 20, 0, Min, Max;
          |POPV;
          |SI nHay ! 0;
               |CONFI "0000N¿Conforme con lo seleccionado?";
               |SI FSalida = 0;
                    aponer = Asino; |HAZ poncontr;
                    |BUCLE Descarga;
                    aponer = Anosi; |HAZ poncontr;
                    |PTEC 816;
                    |PTEC 812;
               |FINSI;
          |FINSI;
     |FINSI;
     |DELINDEX #193;
     |ERROR;
|FPRC;

|PROCESO Tipo20; |TIPO 20;
     |SI FSalida = "OPCION";
          FSalida = "Imprimir";
     |SINO;
          |SELECT #1#0;
          |LEE 000#0=;
          |SI FSalida ! 0; |ACABA; |FINSI;

          |MENU aMenu;
          |SI FSalida = 1;
               enLiquida = #0#0;
               eaTipo = "P";
               |SUB_EJECUTA_NP "malpe194";
          |FINSI;
          |SI FSalida = 2;
               enLiquida = #0#0;
               eaTipo = "M";
               |SUB_EJECUTA_NP "malpe194";
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO AlmaLin;
     |SI #198#1 = #referen@#9;
          nHay = 1;
          |ERROR10;
     |FINSI;
|FPRC;

|DEFBUCLE AlmaLin;
     #referen@#1002;
     ;
     #0#0, 00001;
     #0#0, 99999;
     ;
     NULL, AlmaLin;
|FIN;

|PROCESO CheqAlma;
     nHay = 0;
     |DDEF aAlfa;
     aAlfa = aAlfa + "malpe191";
     |CARGA_DEF aAlfa, referen@;
     |SI FSalida = 0;
          |BUCLE AlmaLin;
          |DESCARGA_DEF #referen@;
     |FINSI;
     |SI nHay = 0;
          |MENAV "0000Almacen inexistente en las lineas de las facturas...";
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO RedondeoFac;
     |PUSHV 0501 2380;
     |BLANCO 1725 2357;
     |DDEFECTO #198;
     |PINPA #0#198;
     |PINDA #0#198;
     |ENDATOS #1#198;
     |SI FSalida = 0;
          |ABRE #109
          |LEE 000#109u;
          |SI FSalida = 0;
               nConta = #109#0 + 1;
          |SINO;
               nConta = 1;
          |FINSI;
          |DDEFECTO #109;
          #109#0 = nConta;
          #109#9 = #0#1;
          #109#14 = #198#0;
          #109#12 = #0#3;
          #109#13 = #198#1;
          |SI #0#5 = "T";
               |ABRE #6;
               #6#0 = #0#6;
               |LEE 000#6=;
               |CIERRA #6;
               #109#1 = #6#0;
               #109#2 = #6#1;
               #109#3 = #6#2;
               #109#4 = #6#3;
               #109#5 = #6#4;
               #109#6 = #6#6;
               #109#7 = #6#7;
               #109#8 = #6#8;
          |SINO;
               #109#15 = #0#6;
          |FINSI;
          #109#17 = #0#22;
          |GRABA 020#109n;
          |CIERRA #109;

          |ABRE #6;
          |ABRE #203;
          aTran = #109#1;
          aAgen = #109#15;
          nAlma = #109#13;
          aMatri = #109#12;
          nEmp = #109#17;
          |HAZ Cabecera;
          ||SI FSalida ! 0; |ACABA; |FINSI;

          |DDEFECTO #193;
          #193#1 = #109#0;
          #193#3 = aTipo;
          #193#4 = aCod;

          #193#5 = aNom;
          #193#6 = #109#9;
          #193#7 = #109#12;
          #193#8 = #109#13;
          #193#9 = #109#14;
          #193#10 = "*";
          |CIERRA #6;
          |CIERRA #203;
          aTipoLin = "S";

          aponer = Asino; |HAZ poncontr;
          |HAZ Descarga;
          aponer = Anosi; |HAZ poncontr;
          |PTEC 816;
          |PTEC 812;
     |FINSI;
     |POPV;
     |PINDA #0#0;
|FPRC;

|PROCESO IVA190_66; |TIPO 66;
     enTiva = #0Campo;
     efFiva = #0#1;
     |HAZ ConsuIVA;
     |SI eaDiva = "";
          |ERROR;
     |SINO;
          #0Campo = enTiva; |PINTA #0Campo;
     |FINSI;
|FPRC;

|PROCESO IVA190_0; |TIPO 0;
     enTiva = #0Campo;
     efFiva = #0#1;
     |HAZ SacaIVA;
     |SI eaDiva = "";
          |MENAV "0000No existe tipo iva para esta fecha...";
          |ERROR; |ACABA;
     |SINO;
          |HAZ RecalFac;
     |FINSI;
|FPRC;
