|FICHEROS;
     coimz043 #0;
     coima003 #3;
     agifa019 #19;
     agifa027 #27;
     agifa090 #90;
     dsm00052 #52;
     coima067 #67;
     coima068 #68;
     agifa074 #74;
     agifa142 #142;
|FIN;

|VARIABLES;
     &eaOrdenIni = "";
     &eaSerie = "";
     &eaOrden = "";
     &Tempo = "";
     nOrden = 0;
 {-1}aMenu = "";
     aMenu1 = "2400";
     aMenu2 = "1";
     aMenu3 = "Opciones:  ";
     aMenu4 = "RAC";
     aMenu5 = " Repasar ";
     aMenu6 = " Aceptar ";
     aMenu7 = " Cancelar ";
     aSerie = "";
     nConta = 0;
     nLin = 0;
     nModo = 0;
     nSw = 0;
     nContador = 0;
     nSigue = 0;
     nSwSerBlanca = 0;
|FIN;

|PROCESO NoAlta; |TIPO 0;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 1;
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO Componente;
     |BORRA 020#52a;

     #52#6 = aSerie;
     #52#0 = nConta;
     #52#1 = nLin;
     |GRABA 020#52n;
|FPRC;

|DEFBUCLE Componente;
     #52#1;
     ;
     #0#0, #0#1, #0#2, 001;
     #0#0, #0#1, #0#2, 999;
     ;
     NULL, Componente;
|FIN;

|PROCESO CabOrden;
     nConta = #68#1;

     #90#20 = #0#0;
     #90#0 = #0#1;
     |LEE 020#90=;

     #90#20 = aSerie;
     #90#0 = nConta;
     |GRABA 020#90n;
|FPRC;

|PROCESO OrdenSerie;
     |SI nSw = 1;
          #90#20 = #0#5;
          #90#0 = #68#1;
          |LEE 000#90=;
          |SI FSalida = 0;
               #68#1 = #68#1 + 1;
               |SI #68#1 > 999999;
                    nSigue = 2;
               |SINO;
                    nSigue = 1;
               |FINSI;
               |ERROR10;
          |FINSI;

          |ACABA;
     |FINSI;

     |SI aSerie ! #0#5;
          aSerie = #0#5;
          nLin = 0;
          |HAZ CabOrden;
     |FINSI;

     nLin = nLin + 1;

     #74#9 = #0#0;
     #74#0 = #0#1;
     #74#1 = #0#2
     |LEE 121#74=;
     |SI FSalida = 0;
          #3#0 = "F";
          #3#1 = #74#9;
          #3#2 = #74#0;
          #3#3 = #74#1;
          |LEE 121#3=;
          |SI FSalida = 0;
               |BORRA 020#3a;
               |LIBERA #3;
               #3#1 = aSerie;
               #3#2 = nConta;
               #3#3 = nLin;
               |GRABA 020#3n;
          |FINSI;

          |BUCLE Componente;
          |BORRA 020#74a;
          |LIBERA #74;

          #74#9 = aSerie;
          #74#0 = nConta
          #74#1 = nLin;
          |GRABA 020#74n;
     |FINSI;

     |BORRA 020#0a;

     eaSerie = #90#20;
     eaOrden = ("000000" + #90#0) % -106;
     |PRINT;
|FPRC;

|DEFBUCLE OrdenSerie;
     #0#2;
     ;
     INICIO;
     FINAL;
     ;
     NULL, OrdenSerie;
|FIN;

|PROCESO Aceptar;
     |IMPRE 0;
     |SI FSalida = 0;
          |INFOR "coimz043";
          |SI FSalida = 0;
               |ABRE #142; |LEE 000#142p; |CIERRA #142;

               |ABRE #90;
               |ABRE #74;
               |ABRE #27;
               |ABRE #3;
               |ABRE #68;

               |LEE 101#68=;
               |SI FSalida ! 0; |GRABA 020#68b; |FINSI;

               |PARA; |SI; |HACIENDO;
                    nSigue = 0;
                    nSw = 1; |BUCLE OrdenSerie;
                    |SI nSigue ! 1; |SAL; |FINSI;
               |SIGUE;

               |SI nSigue = 0;
                    nSw = 2; |BUCLE OrdenSerie;
                    #68#1 = #68#1 + 1;
                    |GRABA 020#68a;

                    #90#20 = "";
                    #90#0 = nOrden;
                    |BORRA 020#90c;
               |FINSI;

               |CIERRA #68;
               |CIERRA #3;
               |CIERRA #27;
               |CIERRA #74;
               |CIERRA #90;

               |SI nSigue = 2;
                    |MENAV "0000Superado contador orden: 999999";
               |FINSI;

               |PIEINF;
               |FININF;
          |SINO;
               |MENAV "0000Error informe 'coimz043'.";
          |FINSI;
          |FINIMP;
     |FINSI;
|FPRC;

|PROCESO Min;
     #0#2 = 1;
|FPRC;

|PROCESO Max;
     #0#2 = 999;
|FPRC;

|PROCESO Lineas;
     #19#0 = #74#2;
     |LEE 020#19=;
     |SI FSalida ! 0; |DDEFECTO #19; |FINSI;

     #67#0 = #19#2;
     |LEE 000#67=;
     |SI FSalida ! 0; |DDEFECTO #67; |FINSI;

     #0#0 = #74#9;
     #0#1 = #74#0;
     #0#2 = #74#1;
     #0#3 = #74#2;
     #0#4 = #74#3;
     #0#5 = #67#1;
     |GRABA 020#0n;
|FPRC;

|PROCESO CheqSerBlanca;
     |SI #0#5 = "     ";
          nSwSerBlanca = 1;
     |FINSI;
|FPRC;

|DEFBUCLE CheqSerBlanca;
     #0#2;
     ;
     INICIO;
     FINAL;
     ;
     NULL, CheqSerBlanca;
|FIN;

|PROGRAMA;
     nOrden = PARAMETRO;
     |HAZ CreaTempo; |NOME_DAT #0 Tempo; |DELINDEX #0;

     eaOrdenIni = ("000000" + nOrden) % -106;
     |ABRE #90;
     #90#20 = "";
     #90#0 = nOrden;
     |LEE 121#90=;
     |SI FSalida = 0;
          |ABRE #0;
          |ABRE #19;
          |ABRE #67;
          |BUCLELIN 2 Lineas #90;
          |CIERRA #67;
          |CIERRA #19;
          |CIERRA #0;

          |PINPA #0#0;
          |ENTLINEAL #1#0, 1, 7, 22, 0, Min, Max;
          |PARA; |SI; |HACIENDO;
               |ATRI I;
               |PINTA 532, #90#20;
               |PINTA 538, #90#0;
               |ATRI 0;
               |MENU aMenu;
               aMenu2 = FSalida;
               |SI aMenu2 = 2;
                    nSwSerBlanca = 0;
                    |BUCLE CheqSerBlanca;
                    |SI nSwSerBlanca = 0;
                         |HAZ Aceptar; |SAL;
                    |SINO;
                         |MENAV "0000Debe de asignar serie a todos los articulos...";
                    |FINSI;
               |FINSI;
               |SI aMenu2 = 0; |O aMenu2 = 3; |SAL; |FINSI;
               |ENTLINEAL #1#0, 1, 2, 22, 0, Min, Max;
          |SIGUE;
     |FINSI;
     |CIERRA #90;

     |DELINDEX #0; |HAZ BoraTempo;
|FPRO;
