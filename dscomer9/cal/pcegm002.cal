|FICHEROS;
     pcegm002 #0;
     agifa024 #6;
     pcegm003 #3;
     agifa019 #2;
     pcegm007 #7;
     agifa025 #8;
     agifa038 #13;
     agifa048 #15;
     agifa049 #16;
     agifa007 #40;
     agifa142 #41;
     agifa321 #35;  || Parametros de Divisas
     agifa328 #36;  || Clientes/Divisas
     agifa329 #37;  || Lineas de Clientes/Divisas
     agifa324 #38;  || Divisas
     pcegm040 #400;
     dsm00003 #49,MANTE;
     dsm00134 #134;
     referen@;
     dsm00087@;
     pcegm001 #100;
     agifm006 #106;
     pcegz033 #33,MANTE;
     dsm00020 #20;
     pcegm020 #120;
     pcegm024 #24;
|FIN;

|VARIABLES;
     &enTiva = 0;
     &efFiva = @;
     &eaDiva = "";
     &enIva = 0;
     &enRec = 0;
     nLineas = 0;
     nDir = 0;
     &enOferta = 0;
     nCampo = 0;
     nCol = 0;
     nPuntero = 0;
     nBuffer = 0;
     nBtnPro = 0;
     nBoton1 = 0;
     nBoton2 = 0;
     aHayCal = "";
     aAlf1 = "";
     &eaCodigoNum = "";
     &enLinea = 0;
     aCliente = "";
     nDirEnt = 0;
     &enSwMenu = "";
     &eaPais = "";
     &eaTipoImp = "";
 {-1}aMenuImp = "";
     aMenuImp1 = "2400";
     aMenuImp2 = "1";
     aMenuImp3 = "Impresion: ";
     aMenuImp4 = "PMF";
     aMenuImp5 = "   Papel  ";
     aMenuImp6 = "   Mail   ";
     aMenuImp7 = "   Fax    ";
     aTieneLin = "N";
     aSinError = "";
     nSal = 0;
     aDef = "";
     aSql = "";
     aUsu = "";
     nContaLog = 0;
     aSer = "";
     aNum = "";
     aTipo = "";
     nTipo = 0;
     nModo = 0;
     nLin = 0;
     nResto = 0;
     nUniPromo = 0;
     &uni_dt = 0;   || Unidades
     &fed_dt = @;   || Fecha....
     &art_dt = "";  || Articulo.
     &cli_dt = "";  || Cliente..
     &fam_dt = "";  || Familia.
     &iva_dt = 0;   || Tipo Iva.
     &dtos_2 = 0;   || Descuento 2.
     &dto_pt = 0;   || Descuento Ptas.
     &dtos_1 = 0;   || Descuento 1.
     &pvp_ve = 0;   || Precio Venta.
     &tarifa = 0;   || Tarifa Cliente.
     &quedto = 0;   || 1=cli./art, 2=cli./familia, 3=cli./subfam.
     &gru_dt = "";  || Grupo Cliente.
     &nPromo = 0;
     &enMensaje = 0;
     &aParam    = "";
     &enModo146 = 0;
     &eaSerie = "";
     &enDirEnt = 0;
     &enAlmacen = 0;
     aAlfa = "";
     &eaSer = "";
     &enDocu = 0;
     &eaTipo = "";
     nPos         = 0;
     &eaCli       = "";

     &cli            = "";
     &ser            = "";
     nTecla          = 0;
     &eaCodigo       = "";
     &eaTipoCodigo   = "CL";
     &eaFuerza       = "";

     &nDeci_Base     = 0;   || Decimales en Totales de la moneda base
     &nDeci_PreBase  = 0;   || Decimales en el Precio en moneda base
     &nDeci_BaseMx   = 0;   || Decimales en Totales de la moneda base (Maxima precision)
     &nDeci_PreBaseMx= 0;   || Decimales en el Precio en moneda base (Maxima precision)
     &nDeci_Div      = 0;   || Decimales en Totales de la divisa
     &nDeci_PreDiv   = 0;   || Decimales en el Precio en divisa
     &nDeci_TotVis   = 0;   || Decimales en Totales visibles (el de la moneda de trabajo)
     &nDeci_TotNoVis = 0;   || Decimales en Totales no visibles (el de la otra moneda)
     &nDeci_PreVis   = 0;   || Decimales en el Precio visual. (lineas)
     &nDeci_ImpVis   = 0;   || Decimales en el Importe visual. (lineas)
     nfecha    = 0;
     &ac_divisa = "N";   || Vale "S" cuando las divisas en ventas estan
                         || activadas y la serie es de divisas
     &pintado = 0;       || Sirve para controlar los pintados de las lineas
     &decim_divisa = 0;  || Decimales de la divisa
     &precio_divisa = 0; || Decimales del precio en divisa
     &decim_base = 0;    || Decimales de la moneda base
     &precio_base = 0;   || Decimales del precio en moneda base
     div_act = 0;        || Divisa actualizada. Me sirve en un PARA para que
                         || no salga hasta que este actualizada (div_act=1)
     &cli_divisa = "";   || Codigo de Cliente. Se pasa a CLIENTES/DIVISAS
     &divisa = "";       || Codigo de Divisa. Se pasa a ACTUAL. de DIVISAS
     &nModo146 = 0;
     &numero = 0;
     &serie  = "";
     &reimpr = "";
     importe = 0;

     imneto = 0;
     tf = 0;
     retencion = 0;
     con = 0;
     margen = 0;
     indtope = 0;
     ind = 0;
     control = 0;
     mensaje01 = "0423Actualizando linea:";
     fmes = "  ";
     mes = 0;
     mensajei  = "2400NDesea imprimir esta oferta ?";
     informe = "";
     informe1 = "agifa146";
     &iva1 = 0;&iva2 = 0;&iva3 = 0;&re1 = 0;&re2 = 0;&re3 = 0;
     &iva4 = 0;&iva5 = 0;&re4 = 0;&re5 = 0;
     &codigo="";
     &descrip="";
     &precio=0;
     &dto=0;
     &dto1=0;
     &unid=0;
     &imptot=0;
     &tiva=0;
     &linea=0;
     vacio="";
     modo = 0;
     &n_dec = 0;
     &n_pre = 0;
     &bl  = "                              ";
     &def_alm = 0;
     &def_exp = "N";
     iva_ant   = 0;
     contenido = 0;
     &codiBarra = "";
     recalpre = 0;
     caltot = 0;
     nImpDiv = 0;

     aNifQbf1   = "";
     aNifQbf2   = "";

     &eaVarDni  = "";
     &enCalcCif = 0;
 {-1}aMenu = "";
     aMenu1 = "2400";
     aMenu2 = "1";
     aMenu3 = "Opciones: ";
||     aMenu4 = "ICPT";
     aMenu4 = "ICP";
     aMenu5 = " Imprimir ";
     aMenu6 = " Calibración ";
     aMenu7 = " Propuesta ";
||     aMenu8 = " Texto mail ";
|FIN;

|CAMPOS;
  #0#41 totfa;
|FIN;

ÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ
|PROCESO CalculaNif;
     nSal = FSalida;

     |ABRE #6;
     #6#0 = #0#3;
     |LEE 000#6=;
     |CIERRA #6;
     eaPais = #6#205;

     |SI nSal = 9;
         eaVarDni   = #0Campo;
         enCalcCif  = 1;
         |HAZ CalculoNIFCIF;
         #0Campo = eaVarDni;
         |PINTA #0Campo;
         |ERROR;
         |ACABA;
     |FINSI;

     eaVarDni  = #0Campo;
     enCalcCif = 0;
     |HAZ CalculoNIFCIF;

     aNifQbf1 = #0Campo;   |QBF aNifQbf1;
     aNifQbf2 = eaVarDni;  |QBF aNifQbf2;

     |SI aNifQbf1 ! aNifQbf2; |Y aNifQbf1 ! "";
         |CONFI "0000SEl Nif puede ser Erroneo. Desea Calcularlo.";
         |SI FSalida = 0;
             #0Campo = eaVarDni;  |PINTA #0Campo;
             |ERROR;
         |FINSI;
     |FINSI;
|FPRC;

|CALCULO fcporte;|TIPO 0;
     imneto = #0#11 - Contenido;
     |SI #0#11 ! Contenido; control = 1; |FINSI;
|FCAL;

|CALCULO fcvario;|TIPO 0;
     imneto = #0#13 - Contenido
     |SI #0#13 ! Contenido; control = 1; |FINSI;
|FCAL;

|CALCULO cogecon0;|TIPO 0;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 1;
          aAlfa = Usuario % 810;
          #0#98 = aAlfa; |PINTA #0#98;
     |FINSI;


     control = 0;
|FCAL;

|CALCULO pedcon0;|TIPO 6;
con = FEntrada / 100;
con = con ! 0;
|SI con = 2;|Y #0Campo ! Contenido;
  control = 1;
|FINSI;
|FCAL;

|CALCULO acttl;|TIPO 0;
     |SI caltot = 0;
          |PINTA 445,#3#1;
          #3#15 = #0#3;
          #3#16 = #0#34;
          #3#17 = #0#35;
          |GRABA 020#3a;
     |SINO;
          |SI recalpre = 1;
               |HAZ TotalLin002;
               |GRABA 001#3a;
          |FINSI;
     |FINSI;

     |HAZ CalCabpcegm002;

     |ABRE #2;
     #2#0 = #3#2;
     |LEE 000#2=;
     |SI FSalida = 0;
          #0#105 = #0#105 + (#2#102 * #3#5);
     |FINSI;
     |CIERRA #2;

     aAlf1 = #3#2;
     aAlfa = aAlf1 - "CAL";
     |SI aAlfa ! aAlf1;
          aHayCal = "S";
     |FINSI;
|FCAL;

|DEFBUCLE pcegm002;
     #3#1;
     ;
     #0#48, #0#0, 001;
     #0#48, #0#0, 999;
     ;
     NULL, acttl;
|FIN;

|PROCESO RecalOfe;
     |HAZ LimCabpcegm002;
|FPRC;

|PROCESO TieneLin146;
     aTieneLin = "S";
|FPRC;

|CALCULO siimpre;|TIPO 3;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 4; |ACABA; |FINSI;

     aTieneLin = "N";
     |BUCLELIN 2 TieneLin146 #0;
     |SI aTieneLin = "N";
          |BORRA 020#0c;
          |ERROR; |ACABA;
     |FINSI;

     |HAZ LimCabpcegm002;

     #0#105 = 0;
     aHayCal = "N";
     |BUCLE pcegm002;

     |SI aHayCal = "S";
          |HAZ Calibracion;
     |FINSI;

     |CONFI mensajei;
     |SI 0 = FSalida; |HAZ impreal; |FINSI;

     |PINTA #0#105;
|FCAL;

|CALCULO impreal;
     |MENU aMenuImp;
     |SI FSalida [ 0; |ACABA; |FINSI;
     |SI FSalida = 1; eaTipoImp = "P"; |FINSI;
     |SI FSalida = 2; eaTipoImp = "M"; |FINSI;
     |SI FSalida = 3; eaTipoImp = "F"; |FINSI;

     modo = (FEntrada / 100) ! 0;
     |SI modo [ 2;
          |GRABA 021#0a;
          |LIBERA #0;
     |FINSI;
     |CIERRAT #0;

     serie = #0#48;
     numero = #0#0;
     reimpr = #0#10;
     |SUB_EJECUTA_NP "pcegz002";
     #0#10 = "S";
     |PINTA #0#10;

     |ABRET #0;
     #0#48 = serie;
     #0#0 = numero;
     |SI modo = 4;
          |LEE 000#0=;
     |SINO;
          |LEE 101#0=;
     |FINSI;
|FCAL;

|PROCESO mira_d;
     nModo = (FEntrada / 100) ! 0;
     |ABRE #41; |LEE 001#41p; |CIERRA #41;
     n_dec = #41#8;
     n_pre = #41#9;
     codiBarra = #41#56; || El mismo que la Factura
     |ABRE #agifa007;
     #agifa007#26 = #pcegm002#48;
     |LEE 001#agifa007.=;
     |SI FSalida ! 0; |DDEFECTO #agifa007; |FINSI;
     |CIERRA #agifa007;
     def_alm = #agifa007#29;
     def_exp = #agifa007#30;
     |HAZ LeeMonBase;
     |HAZ LeeParamDiv;
     ||----------------Aplica S -> Tarifa normal N-> con dtos y promociones
     #0#79 = #41#171;

     |SI nModo = 1;
          #0#103 = ~; |PINTA #0#103;
          |HORA aAlfa;
          #0#104 = aAlfa; |PINTA #0#104;
     |FINSI;
|FPRC;

|PROCESO LeeMonBase; ||Lee los parametros de la moneda base
     |ABRE #agifa321;
     |ABRE #agifa324;
     |LEE 010#agifa321.p;   ||Leo parametros de divisa
     #pcegm002#68 = #agifa321#9;  ||Cojo la Moneda Base de parametros
     #agifa324#0 = #agifa321#9;
     |LEE 010#agifa324.=;         ||Leo la ficha de la moneda base en Divisas
     #pcegm002#69 = #agifa324#2;  ||Asigno el cambio de la moneda base con el euro
     decim_base = #agifa324#7;    ||Asigno los decimales de la moneda base
     precio_base = #agifa324#9;   ||Asigno los decimales del precio en la mon. base
     |CIERRA #agifa324;
     |CIERRA #agifa321;
|FPRC;

|PROCESO Param_Divisas; |TIPO 0;
     |ABRE #agifa324;
     pintado = 0;  ||Controla pintados de las lineas
     #agifa324#0 = #pcegm002#65;
     |LEE 001#agifa324.=;   || Leo la divisa
     |CIERRA #agifa324;

     decim_divisa = #agifa324#7;  || Decimales de la divisa
     precio_divisa = #agifa324#9;  || Decimales en el precio de la divisa
     nDeci_Div = decim_divisa;   || Decimales de la divisa
     nDeci_PreDiv = precio_divisa;  || Decimales en el precio de la divisa
     |SI #0#67 = "B";   || Si trabajo con la moneda base ...
          nDeci_PreVis   = precio_divisa;
          nDeci_ImpVis   = decim_divisa;
          nDeci_Base     = decim_base;
          nDeci_PreBase  = precio_base;
          nDeci_BaseMx  = decim_base;
          nDeci_PreBaseMx = precio_base;
          nDeci_TotVis   = decim_base;
          nDeci_TotNoVis = decim_divisa;
     |SINO;     || Si trabajo con la divisa ...
          nDeci_PreVis   = precio_base;
          nDeci_ImpVis   = decim_base;
          nDeci_Base     = decim_base;     || Max. precision en los decimales de la moneda base
          nDeci_PreBase  = precio_base;    || Max. precision en los dec. del precio de la moneda base
          nDeci_BaseMx    = #agifa321#10;  || Max. precision en los decimales de la moneda base
          nDeci_PreBaseMx = #agifa321#11;  || Max. precision en los dec. del precio de la moneda base
          nDeci_TotVis   = decim_divisa;
          nDeci_TotNoVis = decim_base;
     |FINSI;
     |HAZ Deci_IVA;
|FPRC;

|PROCESO LeeParamDiv; |TIPO 0;
     |ABRE #agifa321;
     |LEE 001#agifa321.p;    || Leo parametros de divisas
     |CIERRA #agifa321;

     |ABRE #agifa007;
     #agifa007#26 = #pcegm002#48;
     |LEE 001#agifa007.=;        || Leo la serie
     |CIERRA #agifa007;

     |SI #agifa007#39 = "N"; |O #agifa321#1 = "N";
          #0#67 = "B";
     |FINSI;

     |SI #0#67 = "D";      || Si la mon. de trabajo es la divisa...
          || ...escondo los campos de la moneda base :
          |C_P #3#6 , 1 , 1009;
          |C_P #3#9 , 1 ,1026;
          |C_V #3#6 , 1 , "N";
          |C_V #3#9 , 1 , "N";
          |C_L #3#6 , 1 , "N";
          |C_L #3#9 , 1 , "N";
          |C_M #3#6 , 1 , "N";
          || ...y pongo en pantalla los de divisa
          |C_P #3#24 , 1 , 1542;
          |C_P #3#25 , 1 ,1568;
          |C_V #3#24 , 1 , "S";
          |C_V #3#25 , 1 , "S";
          |C_M #3#24 , 1 , "S";
          |C_L #3#24 , 1 , "S";
          |C_L #3#25 , 1 , "S";
          |SI #agifa007#39 = "S"; |Y #agifa321#1 = "S"; ||Si la serie es de ...
               ac_divisa = "S";  || ... divisas, y en parametros estan activadas ...
               || ... pongo como modificables los campos relacionados con divisas
               |C_M #0#65 , 1 , "S";
               |C_M #0#66 , 1 , "S";
               |C_M #0#67 , 1 , "S";
               |SI #agifa321#3 = "S";  || Si la visualizacion de parametros esta activada
                    || ... pongo los campos visual. de lineas como visualizables
                    |C_V #3#26, 1 , "S";
                    |C_V #3#27 , 1 , "S";
               |SINO;
                    || ... los pongo como no visualizables
                    |C_V #3#26 , 1 , "N";
                    |C_V #3#27 , 1 , "N";
               |FINSI;
          |SINO; || Si la serie no es de divisas, o no estan activados parametros
               ac_divisa = "N";
               #0#67 = "B";  || La moneda de trabajo es por fuerza la moneda base
               |PINTA #0#67;
               || Pongo como no modificables los campos relacionados con divisas
               |C_M #0#65 , 1 , "N";
               |C_M #0#66 , 1 , "N";
               |C_M #0#67 , 1 , "N";
               || Pongo como no visualizables los campos visual. de lineas
               |C_V #3#26, 1 , "N";
               |C_V #3#27, 1 , "N";
          |FINSI;
     |SINO;  || Si la moneda de trabajo es la moneda base
          || ...escondo los campos en divisa :
          |C_P #3#24 , 1 , 1009;
          |C_P #3#25 , 1 ,1026;
          |C_V #3#24 , 1 , "N";
          |C_V #3#25 , 1 , "N";
          |C_M #3#24 , 1 , "N";
          |C_L #3#24 , 1 , "N";
          |C_L #3#25 , 1 , "N";
          || ...coloco los campos en moneda base:
          |C_P #3#6 , 1 , 1542;
          |C_P #3#9 , 1 ,1568;
          |C_V #3#6 , 1 , "S";
          |C_V #3#9 , 1 , "S";
          |C_M #3#6 , 1 , "S";
          |C_L #3#6 , 1 , "S";
          |C_L #3#9 , 1 , "S";
          |SI #agifa007#39 = "S"; |Y #agifa321#1 = "S"; || Si la serie es de divisas...
               ac_divisa = "S"; || ... y divisas estan activadas en parametros
               || Pongo como modificables los campos relacionados con divisas
               |C_M #0#65 , 1 , "S";
               |C_M #0#66 , 1 , "S";
               |C_M #0#67 , 1 , "S";
               |SI #agifa321#3 = "S";  || Si la visualizacion esta activada en parametros ...
                    || Pongo como visualizables los campos visual. de las lineas
                    |C_V #3#26, 1 , "S";
                    |C_V #3#27 , 1 , "S";
               |SINO; || Los pongo como no visualizables
                    |C_V #3#26 , 1 , "N";
                    |C_V #3#27 , 1 , "N";
               |FINSI;
          |SINO;  || Si la serie no es de divisas o no estan activados los parametros
               ac_divisa = "N";
               #0#67 = "B";  || La moneda de trabajo es por fuerza la moneda base
               || Pongo como no modificables los campos relacionados con divisas
               |C_M #0#65 , 1 , "N";
               |C_M #0#66 , 1 , "N";
               |C_M #0#67 , 1 , "N";
               || Pongo como no visualizables los campos visual. de lineas
               |C_V #3#26, 1 , "N";
               |C_V #3#27, 1 , "N";
          |FINSI;
     |FINSI;
     ||ÄÄÄÄÄÄÄÄPortes/Varios
     |C_P #0#76, 0, nPos; |C_P #0#11, 1, nPos;
     |C_P #0#77, 0, nPos; |C_P #0#13, 1, nPos;
     |C_V #0#11, 1, "N";
     |C_V #0#13, 1, "N";
     |C_V #0#76, 1, "N";
     |C_V #0#77, 1, "N";
     |SI #0#67 = "D";
          |C_V #0#76, 1, "S";
          |C_V #0#77, 1, "S";
     |SINO;
          |C_V #0#11, 1, "S";
          |C_V #0#13, 1, "S";
     |FINSI;
     |HAZ Param_Divisas;
|FPRC;

|PROCESO CambioDivisa; |TIPO 0;
nModo146 = (FEntrada / 100) ! 0;
|SI #0#3 = "      ";
 #0#3 = "";
 Contenido = "      ";
|FINSI;
|SI nModo146 = 1;  ||Si es alta
  |SI Campo = 3; || Si llamo al proceso desde el cod. de cliente ...
    |ABRE #agifa024;
    #agifa024#0 = #0#3;
    |LEE 001#agifa024.=;
    #0#65 = #agifa024#192;  || Asigno la divisa por defecto del cliente
    #0#67 = #agifa024#193;  || Asigno la moneda de trabajo del cliente
    |PINTA #0#67;
  |FINSI;
  #agifa007#26 = #pcegm002#48;
  |LEE 001#agifa007.=;  || Leo la serie
   |SI #agifa007#39 = "S"; |Y #agifa321#1 = "S";
      |SI #agifa024#191 = "S"; || Si el cliente es de divisa pactada
         |ABRE #agifa328;
         |ABRE #agifa329;
         #agifa329#0 = #0#3;
         #agifa329#2 = #pcegm002#65;
         #agifa329#7 = "N";
         |SELECT #2#agifa329;  || Con esta clave ...
         |LEE 011#agifa329.=;  || ... busco periodos no finalizados de esa divisa
         |SI FSalida = 0;  || Si existe un periodo no finalizado
           |PARA div_act = 0; |SI div_act = 0; |HACIENDO;
             #pcegm002#65 = #agifa329#2;        || Cargo la divisa ...
             #pcegm002#66 = #agifa329#3;        || ...y el cambio
             |LEE 001#agifa329.=;   || Leo las lineas de Clientes/Divisas
             nfecha = #0#1 - #agifa329#6;  || Resto la fecha de fin de periodo a la actual
             |SI nfecha > 0;  || Si la fecha actual supera a la del periodo asignado ...
                |MENAV "0000ATENCION deberia Actualizar primero la divisa en Clientes/Divisas";
                cli_divisa = #0#3;  || Le envio el cod. de cliente a "agifa328"
                |SUB_EJECUTA_NP "agifa328.tab";  || Ejecuto Clientess/Divisas
                |ERROR;
             |SINO;
                div_act = 1;  || Para salir del PARA
             |FINSI;
           |SIGUE;
         |SINO;  || Si no existe un periodo no finalizado para esa divisa
           |MENAV "0000No existe un periodo activo para la divisa en Clientes / Divisas";
           cli_divisa = #0#3;  || Le paso a "agifa328" el cliente
           |SUB_EJECUTA_NP "agifa328.tab";  || Ejecuto Clientes / Divisas
           |ERROR;
           ||#pcegm002#65 = #pcegm002#68;
           ||#pcegm002#66 = #pcegm002#69;
         |FINSI;
         |SELECT #1#agifa329;
         |CIERRA #agifa329;
         |CIERRA #agifa328;
      |SINO; || Si el Cliente no es de Divisa pactada ...
         |ABRE #agifa324; || ...miro directamente en divisas
         #agifa324#0 = #pcegm002#65;
         |LEE 001#agifa324.=;
         |SI FSalida = 0; ||Si existe la divisa...
            #pcegm002#66 = #agifa324#2; || la recojo
            |SI #agifa324#5 ! -1; || Si los dias de margen de actualizacion no
                                  || son = -1 (indefinidos)
              |PARA div_act = 0; |SI div_act = 0; |HACIENDO;
                 #pcegm002#65 = #agifa324#0;        ||Cargo el valor de la divisa
                 #pcegm002#66 = #agifa324#2;        ||Cargo el valor del cambio
                 |LEE 001#agifa324.=;  || Leo la divisa
                 nfecha = #0#1 - #agifa324#4;
                 |SI nfecha > #agifa324#5; |Y #agifa324#5 ! -1;  || Si la fecha actual
                    |MENAV "0000ATENCION deberia Actualizar primero la divisa";
                    divisa = #pcegm002#65;
                    |SUB_EJECUTA_NP "agifa326.tab";
                    |ERROR;
                 |SINO;
                    div_act = 1;
                 |FINSI;
              |SIGUE;
            |FINSI;
         |FINSI;
         |CIERRA #agifa324;
       |FINSI;
     |CIERRA #agifa024;
     |SINO;
       #pcegm002#65 = #pcegm002#68;
       #pcegm002#66 = #pcegm002#69;
     |FINSI;
  |PINTA #pcegm002#65;
  |PINTA #pcegm002#66;

  |SI Campo = 3; || Si llamo al proceso desde el cod. de cliente ...
    |HAZ LeeParamDiv;
  |SINO;
    |HAZ Param_Divisas;
  |FINSI;
|FINSI;
|FPRC;

|PROCESO convportes; |TIPO 6;
     |SI #0#67 = "D";
          #0#11 = #0#76 / #0#66 * #0#69;
     |SINO;
          #0#76 = #0#11 * #0#66 / #0#69;
     |FINSI;
     |HAZ fcporte;
|FPRC;

|PROCESO convvarios; |TIPO 6;
     |SI #0#67 = "D";
          #0#13 = #0#77 / #0#66 * #0#69;
     |SINO;
          #0#77 = #0#13 * #0#66 / #0#69;
     |FINSI;
     |HAZ fcvario;
|FPRC;


|PROCESO LeeFac; |TIPO 13;  || Sirve para cargar los valores cada vez que
                            || cambio de ficha
pintado = 0;   || controla pintado de las lineas
|HAZ LeeMonBase;
|HAZ LeeParamDiv;
|FPRC;

|CALCULO modomod; |TIPO 6;
     |SI nModo146 = 2; |Y #0Campo ! Contenido;
          |SI Campo = 65; |Y Contenido = "   "; |ACABA; |FINSI;
          |SI Campo = 67; |Y Contenido = " "; |ACABA; |FINSI;
          |MENAV "0000No se puede modificar el campo.";
          #0Campo = Contenido;
          |PINTA #0Campo;
          |ERROR;
     |FINSI;
     |SI #0Campo ! Contenido; |Y Campo = 67;
          |HAZ LeeParamDiv;
     |FINSI;
|FCAL;

|PROCESO caltotal; |TIPO 0; ||Proceso especial para casos determinados como
                            ||modificar los campos de cabecera en dtos, cambio divisas etc
                            ||Recalcula total factpro, descuentos y ivas de lineas
                            ||En el caso de cambiar el cambio de divisas recalcula tambien precio... de lineas

    |SI #0Campo ! Contenido;||Solo si cambia
          caltot = 1;
          |SI Campo = 66; ||Si viene del cambio divisa
              recalpre = 1; ||Recalcular tambien precios linea
          |FINSI;
          |HAZ convportes;
          |HAZ convvarios;
          |HAZ RecalOfe;

          |PINTA #0#73;
          caltot = 0;
          recalpre = 0;
     |FINSI;
|FPRC;


|PROCESO linf;
  #49#0 = #0#3;
  #49#1 = 1;
|FPRC;

|PROCESO lsup;
  #49#0 = #0#3;
  #49#1 = 9999;
|FPRC;

|PROCESO Dir146;  ||Direccion de entrega
     nTecla = FSalida;
     nModo146 = (FEntrada / 100) ! 0;
     |ABRE #6;
     #6#0 = #0#3;
     |LEE 000#6=;
     |CIERRA #6;

     |SI nTecla = 11; |O #41#102 = "S"; |O #41#102 = "X";
          eaCodigo = #0#3;
          eaFuerza = "N";
          |SI nTecla = 11; eaFuerza = "S"; |FINSI;
          |SUB_EJECUTA_NP "dsz99994";
          |SI #41#102 = "N"; |Y nModo146 ! 2; |ERROR;  |FINSI;
     |FINSI;
|FPRC;

|PROCESO ser66; |TIPO 0;              || VIC 20/07/98
     cli=#0#3;
     ser=#0#48;
     |SUB_EJECUTA_NP "agifv011.tab;por_ejemplo"
|FPRC;

|PROCESO Cli146_6; |TIPO 6;
     nModo = (FEntrada / 100) ! 0;
     |ABRE #6;
     #6#0 = #0#3;
     |LEE 000#6=;
     |CIERRA #6;
     |SI nModo = 1; |O #0Campo ! Contenido;
          |ABRE #49;
          #49#0 = #0#3;
          #49#1 = #0#80;
          |LEE 000#49=;
          |CIERRA #49;
          |HAZ AsignaDir146;
     |FINSI;

     |HAZ CambioDivisa;
|FPRC;

|PROCESO Cli146_0; |TIPO 0;
     nModo = (FEntrada / 100) ! 0;
     |SI #0Campo ! Contenido; |Y nModo = 2;
          |SI #41#250 = "S"; |HAZ ProModLin; |FINSI;
     |FINSI;
     |HAZ Dir146;
     |HAZ mira_d;
     |HAZ ser66;
     |SI nModo = 1; |Y #6#28 = "S"; || Cliente Exporacion
          #0#12 = 0; |PINTA #0#12;
     |FINSI;
|FPRC;

|PROCESO T146_2; |TIPO 2;
     enModo146 = (FEntrada / 100) ! 0;
|FPRC;

|PROCESO PrecioCab146;
     |ABRE #2;
     #2#0 = #3#2;
     |LEE 020#2=;
     |CIERRA #2;

     |ABRE #6;
     #6#0 = #0#3;
     |LEE 000#6=;
     |CIERRA #6;

     fed_dt = #0#1;   || Fecha....
     art_dt = #3#2;   || Articulo.
     cli_dt = #3#15;  || Cliente..
     fam_dt = #3#13;  || Familia.
     iva_dt = #2#30;  || Tipo Iva.
     dtos_2 = 0;      || Descuento 2.
     dto_pt = 0;      || Descuento Ptas.
     dtos_1 = 0;      || Descuento 1.
     pvp_ve = 0;      || Precio Venta.
     tarifa = #6#39;  || Tarifa Cliente....
     gru_dt = #6#158; || Grupo Cliente.
     uni_dt = #3#5;
     enMensaje = 0;
     aParam = "";
     eaSerie = #3#20;
     enDirEnt = #0#80;
     |SI #2#176 = "S";  enMensaje = 1;  |FINSI;
     enAlmacen = #3#3;
     |HAZ calcdtos;    || Calculo Dtos.....
     #3#6   = pvp_ve;  || Precio Venta. sin iva.
     #3#8   = dtos_1;  || 1¦ Dto.
     #3#21  = dtos_2;  || 2§ Dto.
|FPRC;

|PROCESO CreaLinProCab146;
     |ABRE #referen@;
     #referen@#20 = #3#20;
     #referen@#0 = #3#0;
     #referen@#1 = #3#36;
     |LEE 000#referen@.=;
     |SI FSalida = 0; |O #3#36 = 0;
          #referen@#1 = 999;
          |LEE 000#referen@.];
          |SI FSalida = 0;
               |LEE 000#referen@.a;
          |SINO;
               |LEE 000#referen@.u;
          |FINSI;
          |SI FSalida = 0; |Y #referen@#20 = #3#20;  |Y #referen@#0 = #3#0;
               nLin = #referen@#1 + 1;
          |SINO;
               nLin = 1;
          |FINSI;
          |SI nLin = #0#1; nLin = nLin + 1; |FINSI;
          #referen@#20 = #3#20;
          #referen@#0 = #3#0;
          #referen@#1 = nLin;
          |GRABA 020#referen@.b;
     |SINO;
          nLin = #3#36;
          |GRABA 020#referen@.b;
     |FINSI;
     |SALVA_DATOS #3;
     |REPON_DATOS #referen@;

     #referen@#1 = nLin;
     #referen@#5 = #3#39;
     #referen@#6 = 0;
     #referen@#7 = 0;
     #referen@#8 = 0;
     #referen@#9 = 0;
     #referen@#24 = 0;
     #referen@#25 = 0;
     #referen@#26 = 0;
     #referen@#27 = 0;
     #referen@#36 = -1;

     |GRABA 020#referen@.a;
     |LIBERA #referen@;
|FPRC;

|PROCESO ProModLinea;
     #134#0 = #0#3;
     #134#1 = #3#2;
     |LEE 000#134=;
     |SI FSalida = 0; |Y #0#1 ] #134#6; |Y #0#1 [ #134#7;
          #3#15 = #0#3; || cliente
          |HAZ PrecioCab146;
          nUniPromo = #3#5 / #134#2;
          nResto = #3#5 $ #134#2;
          nResto = nResto / #134#2;
          nUniPromo = nUniPromo  - nResto;
          #3#6 = (#3#6 - #134#4) ! nDeci_Base;
          #3#37 = #134#4;
          #3#38 = #134#5;
          #3#39 = nUniPromo * #134#3;
          #3#8 = #134#5;
          |HAZ TotalLin002;
          |SI #3#39 = 0;
               #3#36 = 0;
          |SINO;
               |HAZ CreaLinProCab146;
               #3#36 = nLin;
          |FINSI;
          |GRABA 020#3a;
     |SINO;    || No tiene promocion
          #3#36 = 0;
          #3#37 = 0;
          #3#38 = 0;
          #3#39 = 0;
          |HAZ TotalLin002;
          |GRABA 020#3a;
     |FINSI;
     |LIBERA #3;
|FPRC;

|PROCESO BorraLinPromo146;
     |SI #3#36 = -1;
          |BORRA 020#3a;
     |FINSI;
|FPRC;

|PROCESO ProModLin;
     |CONFI "0000SActualizar promociones para el nuevo cliente?";
     |SI FSalida = 0;
          |DDEF aAlfa;
          aAlfa = aAlfa + "pcegm002";
          |CARGA_DEF aAlfa, referen@;
          |SI FSalida = 0;
               |ABRE #134;
               |ABRE #referen@;
               |BUCLELIN 0 BorraLinPromo146 #0;
               |BUCLELIN 0 ProModLinea #0;
               |CIERRA #referen@;
               |CIERRA #134;
               |DESCARGA_DEF #referen@;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO AsignaDir146;
     #0#4 = #49#10;   |PINTA #0#4;
     #0#29 = #49#10;  |PINTA #0#29;
     #0#30 = #49#2; |PINTA #0#30;
     #0#31 = #49#3; |PINTA #0#31;
     #0#33 = #49#7; |PINTA #0#33;
     #0#80 = #49#1; |PINTA #0#80;
     |SI #49#14 = "  ";
         #0#6 = #6#25;
     |SINO;
         #0#6 = #49#14;
     |FINSI;
     |PINTA #0#6;
     #0#87 = #49#22; |PINTA #0#87;
     #0#106 = #49#22; |PINTA #0#106;
     #0#81 = #49#23; |PINTA #0#81;
     #0#89 = #49#21; |PINTA #0#89;
     #0#88 = #49#24; |PINTA #0#88;
     #0#109 = #49#13; |PINTA #0#109;
     #0#107 = #49#19; |PINTA #0#107;
     #0#108 = #49#11; |PINTA #0#108;
     #0#125 = #49#25; |PINTA #0#125;
     #0#126 = #49#26; |PINTA #0#126;
|FPRC;

|PROCESO MinDir146;
     #49#0 = #0#3;
     #49#1 = 1;
|FPRC;

|PROCESO MaxDir146;
     #49#0 = #0#3;
     #49#1 = 9999;
|FPRC;

|PROCESO Dir146_0; |TIPO 0;
     |SI FSalida = 2; |ACABA; |FINSI;
     nTecla = FSalida;
     |SI nTecla = 12;    || F4 Alta direccion
          |PUSHV 0501 2380;
          |ENTLINEAL #1#49, -2, 1, 22, 1, MinDir146, MaxDir146;
          |POPV;
          nTecla = 18;
     |FINSI;
     |ABRE #49;
     |SI nTecla = 18;   || F10  Todas las direcciones
          #49#0 = #0#3;
          #49#1 = #0#80;
          |LEE 000#49];
          |CONSULTA_F_CLAVE #1#49, MinDir146, MaxDir146;
          |SI FSalida = 0;
               |HAZ AsignaDir146;
          |SINO;
               |ERROR; |ACABA;
          |FINSI;
     |FINSI;
     #49#0 = #0#3;
     #49#1 = #0#80;
     |LEE 000#49=;
     |SI FSalida = 0;
          |SI #49#18 = "N";
               |CONFI "0000NDireccion de entrega desactiva. ¨Continuar?";
               |SI FSalida ! 0; |ERROR; |ACABA; |FINSI;
          |FINSI;
     |SINO;
          |MENAV "0000No existe direccion de entrega...";
          |ERROR; |ACABA;
     |FINSI;
     |CIERRA #49;
     |SI #0Campo ! Contenido;
          |HAZ AsignaDir146;
     |FINSI;
|FPRC;

|PROCESO Dir146_7; |TIPO 7;
     |SI #41#71 = "S";
          |ABRE #49;
          #49#0 = #0#3;
          #49#1 = 1;
          |LEE 000#49];
          |SI FSalida = 0; |Y #49#0 = #0#3;
               |LEE 000#49s;
               |SI #49#0 = #0#3;
                    aSinError = "S";
                    |HAZ Dir146_66;
                    aSinError = "N";
                    |SI nSal = 0; |ERROR; |FINSI;
               |FINSI;
          |FINSI;
          |CIERRA #49;
     |FINSI;
|FPRC;

|PROCESO Dir146_66; |TIPO 66;
     nModo = (FEntrada / 100) ! 0;
     |ABRE #49;
     aSql = "SELECT * FROM dsm00003 INTO sq" + Usuario + ".def WHERE ";
     aSql = aSql + "0 == " + #0#3 + " AND 18 == S";
     |SQL aSql;
     |SI FSalida = 0;
          |DDEF aDef; |QBF aDef;
          aDef = aDef + "sq" + Usuario + ".mas";
          |CARGA_DEF aDef, referen@;
          |SI FSalida = 0;
               |ABRE #referen@;
               #referen@#0 = #0#3;
               #referen@#1 = #0#80;
               |LEE 000#referen@.]
               |CONSULTA_CLAVE #1#referen@;
               nSal = FSalida;
               |SI FSalida = 0;
                    |SI nModo ! 2; |O #referen@#0 ! #0#3; |O #referen@#1 ! #0#80;
                         #49#0 = #referen@#0;
                         #49#1 = #referen@#1;
                         |LEE 020#49=;
                         |HAZ AsignaDir146;
                    |FINSI;
               |SINO;
                    |SI aSinError ! "S";
                         |ERROR;
                    |FINSI;
               |FINSI;
               |CIERRA #referen@;
               |DELINDEX #referen@;
               |DESCARGA_DEF #referen@;
               |FBORRA aDef;
          |FINSI;
     |FINSI;
     |CIERRA #49;
|FPRC;

|PROCESO CalZona; |TIPO 0;
     |SI #0Campo = Contenido; |ACABA; |FINSI;
     |ABRE #7;
     #7#0 = #0#107;
     #7#1 = #0#81;
     |LEE 000#7=;
     |SI FSalida = 0;
          #0#108 = #7#2;
     |SINO;
          aAlfa = #0#81 % 102;
          #7#1 = aAlfa;
          |LEE 000#7.=;
          |SI FSalida = 0;
               #0#108 = #7#2;
          |SINO;
               #7#1 = "";
               |LEE 000#7.=;
               |SI FSalida = 0;
                    #0#108 = #7#2;
               |FINSI;
          |FINSI;
     |FINSI;
     |CIERRA #7;
     |PINTA #0#108;
|FPRC;

|PROCESO Contacto; |TIPO 0;
     |SI FSalida = 13;   || F5
          nCampo = Campo;
          eaCodigoNum = #0#3;
          |SUB_EJECUTA_NP "agifm006;SELECCIONA";
          |SI enLinea ! 0;
               |ABRE #106;
               #106#0 = #0#3;
               #106#1 = enLinea;
               |LEE 000#106=;
               |SI FSalida = 0;
                    |SI nCampo = 106;
                         #0#106 = #106#3; |PINTA #0#106;
                    |SINO;
                         #0#87 = #106#3; |PINTA #0#87;
                         #0#88 = #106#6; |PINTA #0#88;
                         #0#89 = #106#8; |PINTA #0#89;
                         #0#109 = #106#4; |PINTA #0#109;
                    |FINSI;
               |FINSI;
               |CIERRA #106;
          |FINSI;
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO Opciones2; |TIPO 20;
     |SI FSalida ! "OPCION";
          |SELECT #1#0;
          |LEE 000#0=;
          |SI FSalida ! 0;
               |MENAV "0000Por favor, consulte el registro correcto antes de imprimir.";
               |ACABA;
          |SINO;
               |PINDA #0#0;
          |FINSI;
     |FINSI;
     |HAZ T2_20;
|FPRC;

|PROCESO T2_20;
     |SI FSalida = "OPCION";
          FSalida = "Opciones";
          |ACABA;
     |FINSI;

     |LEE 101#0=;
     |SI FSalida ! 0; |ACABA; |FINSI;

     |MENU aMenu;
     |SI FSalida = 1;
          |HAZ impreal;
     |FINSI;
     |SI FSalida = 2;
          |HAZ Calibracion;
     |FINSI;
     |SI FSalida = 3;
          |HAZ Propuesta;
          |GRABA 020#0a;
     |FINSI;
     |SI FSalida = 4;
          eaSerie = #0#48;
          enOferta = #0#0;
          |SUB_EJECUTA_NP "pcegm025";
     |FINSI;
     |LIBERA #0;
|FPRC;

|PROCESO PF2;
     |PTEC 824;
|FPRC;

|PROCESO PF3;
     |PTEC 825;
|FPRC;

|PROCESO Tecla33; |TIPO 0;
     |SI FSalida = 10;   ||F2
          |DDEFECTO #6;
          |ABRE #6;
          #6#0 = #0#3;
          |LEE 000#6=;
          |CIERRA #6;
          #33#0 = #6#2; |PINTA #33#0;
          #33#1 = #6#8; |PINTA #33#1;
          #33#2 = #6#211; |PINTA #33#2;
          #33#3 = #6#9; |PINTA #33#3;
          #33#4 = #6#10; |PINTA #33#4;
          |ERROR;
     |FINSI;
     |SI FSalida = 11;   ||F3
          #33#0 = #0#29; |PINTA #33#0;
          #33#1 = #0#30; |PINTA #33#1;
          #33#2 = #0#81; |PINTA #33#2;
          #33#3 = #0#31; |PINTA #33#3;
          #33#4 = #0#33; |PINTA #33#4;
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO Calibracion;
     |PUSHV 0501 2380;
     #33#0 = #0#111;
     #33#1 = #0#112;
     #33#2 = #0#113;
     #33#3 = #0#114;
     #33#4 = #0#115;
     |PINPA #0#33;
     |PINDA #0#33;
     |CONTROL_SIMPLE 0, "BOTON,F2.-Dir. Factura", 1033, 1045, PF2; nBoton1 = FSalida;
     |CONTROL_SIMPLE 0, "BOTON,F3.-Dir. Envio", 1047, 1059, PF3; nBoton2 = FSalida;
     |ENDATOS #1#33;
     |SI FSalida = 0;
          #0#111 = #33#0;
          #0#112 = #33#1;
          #0#113 = #33#2;
          #0#114 = #33#3;
          #0#115 = #33#4;
          |GRABA 020#0a;
     |FINSI;
     |POPV;
     |FIN_CONTROL_WINDOWS nBoton1;
     |FIN_CONTROL_WINDOWS nBoton2;
     |PULSATECLA;
|FPRC;

|PROCESO NonAge; |TIPO 0;
     |ABRE #20;
     #20#0 = #0#78;
     |LEE 000#20=;
     |SI FSalida = 0;
          #0#9 = #20#1; |PINTA #0#9;
     |FINSI;
     |CIERRA #20;
|FPRC;

|PROCESO CheqAgen; |TIPO 0;
     |ABRE #120; |LEE 000#120p; |CIERRA #120;

     |SI #0#78 = #120#2;
          |ABRE #24;
          #24#0 = #0#107;
          |LEE 000#24=;
          |SI FSalida ! 0;
               |MENAV "0000No existe el pais en el auxiliar paises CHRONOEXPRES";
               |ERROR;
          |FINSI;
          |CIERRA #24;
     |FINSI;
|FPRC;

|PROCESO BtnPro;
     |PTEC 824;
|FPRC;

|PROCESO Oferta8; |TIPO 8;
     |CONTROL_SIMPLE 0, "BOTON,F2.- Propuesta", 1442, 1460, BtnPro;
     nBtnPro = FSalida;
     |ESTADO_CONTROL nBtnPro, "HIDE";
|FPRC;

|PROCESO Oferta9; |TIPO 9;
     |FIN_CONTROL_WINDOWS nBtnPro;
     |PULSATECLA;
|FPRC;

|PROCESO Propuesta7; |TIPO 7;
     |ESTADO_CONTROL nBtnPro, "SHOW";
|FPRC;

|PROCESO Propuesta0; |TIPO 0;
     |SI FSalida = 10; ||F2;
          |HAZ Propuesta;
          |ERROR;
     |SINO;
          |ESTADO_CONTROL nBtnPro, "HIDE";
     |FINSI;
|FPRC;

|PROCESO CogeLineas;
     nPuntero = 1;
     |PARA nPuntero = 1; |SI nPuntero [ 999; |HACIENDO nPuntero = nPuntero + 1;
          #400#0 = #0#48;
          #400#1 = #0#0;
          #400#2 = nPuntero;
          |LEE 000#400=;
          |SI FSalida = 0;
               aAlfa = #400#3; |AL_BUF nBuffer, nPuntero, aAlfa;
               |BORRA 020#400a;
          |SINO;
               |SAL;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO PoneLineas;
     |PARA nPuntero = 999; |SI nPuntero ] 0; |HACIENDO nPuntero = nPuntero - 1;
          |DEL_BUF nBuffer, nPuntero, aAlfa;
          |SI aAlfa ! ""; nLineas = nPuntero; |SAL; |FINSI;
     |SIGUE;
     |PARA nPuntero = 1; |SI nPuntero [ nLineas; |HACIENDO nPuntero = nPuntero + 1;
          |DEL_BUF nBuffer, nPuntero, aAlfa;
          #400#0 = #0#48;
          #400#1 = #0#0;
          #400#2 = nPuntero;
          #400#3 = aAlfa;
          |GRABA 020#400n;
     |SIGUE;
|FPRC;

|PROCESO Propuesta;
     |ABRE #400;
     |PUSHV 0501 2380;
     |ATRI R; |PINTA 1302, "                        P R O P U E S T A                     ";
     |PINTA 1360, "                    "; |ATRI 0;
     |DIMENSIONA 200, nBuffer, 0;
     |HAZ CogeLineas;
     nModo = 100; nLin = 10; nCol = 60;
     |EDITA nBuffer, 1, 10, 1409, 2271, nModo, "", 0, nLin, nCol;
     |HAZ PoneLineas;
     |FINDIM nBuffer;
     |POPV;
     |CIERRA #400;
|FPRC;

|PROCESO Oferta7; |TIPO 7;
     nModo = (FEntrada / 100) ! 0;
     aAlfa = #0#92; |QBF aAlfa;
     |SI nModo = 1; |Y aAlfa = ""; || El defecto tambien esta en ContaOf0
          #0#92 = "Importante: Este equipo no cuenta con protección ATEX, por lo que no debe ser usado en ambientes explosivos.";
     |FINSI;
|FPRC;

|PROCESO Entrega0; |TIPO 0;
     nTecla = FSalida;
     |SI nTecla ! 12; |Y nTecla ! 18; |ACABA; |FINSI;

     |SI nTecla = 12;    || F4 Alta direccion
          |PUSHV 0501 2380;
          |ENTLINEAL #1#49, -2, 1, 22, 1, MinDir146, MaxDir146;
          |POPV;
          nTecla = 18;
     |FINSI;
     |ABRE #49;
     |SI nTecla = 18;   || F10  Todas las direcciones
          #49#0 = #0#3;
          #49#1 = 0;
          |LEE 000#49];
          |CONSULTA_F_CLAVE #1#49, MinDir146, MaxDir146;
          |SI FSalida = 0;
               nDir = #49#1;
               |HAZ AsignaDir146_2;
          |SINO;
               |ERROR; |ACABA;
          |FINSI;
     |FINSI;
     #49#0 = #0#3;
     #49#1 = nDir;
     |LEE 000#49=;
     |SI FSalida = 0;
          |SI #49#18 = "N";
               |CONFI "0000NDireccion de entrega desactiva. ¨Continuar?";
               |SI FSalida ! 0; |ERROR; |ACABA; |FINSI;
          |FINSI;
     |SINO;
          |MENAV "0000No existe direccion de entrega...";
          |ERROR; |ACABA;
     |FINSI;
     |CIERRA #49;
|FPRC;

|PROCESO Entrega66; |TIPO 66;
     nModo = (FEntrada / 100) ! 0;
     |ABRE #49;
     aSql = "SELECT * FROM dsm00003 INTO sq" + Usuario + ".def WHERE ";
     aSql = aSql + "0 == " + #0#3 + " AND 18 == S";
     |SQL aSql;
     |SI FSalida = 0;
          |DDEF aDef; |QBF aDef;
          aDef = aDef + "sq" + Usuario + ".mas";
          |CARGA_DEF aDef, referen@;
          |SI FSalida = 0;
               |ABRE #referen@;
               #referen@#0 = #0#3;
               #referen@#1 = #0#80;
               |LEE 000#referen@.]
               |CONSULTA_CLAVE #1#referen@;
               nSal = FSalida;
               |SI FSalida = 0;
                    |SI nModo ! 2; |O #referen@#0 ! #0#3; |O #referen@#1 ! #0#80;
                         #49#0 = #referen@#0;
                         #49#1 = #referen@#1;
                         |LEE 020#49=;
                         |HAZ AsignaDir146_2;
                    |FINSI;
               |SINO;
                    |SI aSinError ! "S";
                         |ERROR;
                    |FINSI;
               |FINSI;
               |CIERRA #referen@;
               |DELINDEX #referen@;
               |DESCARGA_DEF #referen@;
               |FBORRA aDef;
          |FINSI;
     |FINSI;
     |CIERRA #49;
|FPRC;

|PROCESO AsignaDir146_2;
     ||#0#4 = #49#10;   |PINTA #0#4;
     #0#29 = #49#10;  |PINTA #0#29;
     #0#30 = #49#2; |PINTA #0#30;
     #0#31 = #49#3; |PINTA #0#31;
     #0#33 = #49#7; |PINTA #0#33;
     ||#0#80 = #49#1; |PINTA #0#80;
     |SI #49#14 = "  ";
         #0#6 = #6#25;
     |SINO;
         #0#6 = #49#14;
     |FINSI;
     |PINTA #0#6;
     #0#87 = #49#22; |PINTA #0#87;
     #0#106 = #49#22; |PINTA #0#106;
     #0#81 = #49#23; |PINTA #0#81;
     #0#89 = #49#21; |PINTA #0#89;
     #0#88 = #49#24; |PINTA #0#88;
     #0#109 = #49#13; |PINTA #0#109;
     #0#107 = #49#19; |PINTA #0#107;
     #0#108 = #49#11; |PINTA #0#108;
     #0#125 = #49#25; |PINTA #0#125;
     #0#126 = #49#26; |PINTA #0#126;
|FPRC;

|PROCESO IVA002_66; |TIPO 66;
     enTiva = #0Campo;
     efFiva = #0#1;
     |HAZ ConsuIVA;
     |SI eaDiva = "";
          |ERROR;
     |SINO;
          #0Campo = enTiva; |PINTA #0Campo;
     |FINSI;
|FPRC;

|PROCESO IVA002_0; |TIPO 0;
     enTiva = #0Campo;
     efFiva = #0#1;
     |HAZ SacaIVA;
     |SI eaDiva = "";
          |MENAV "0000No existe tipo iva para esta fecha...";
          |ERROR; |ACABA;
     |SINO;
          |HAZ caltotal;
     |FINSI;
|FPRC;
