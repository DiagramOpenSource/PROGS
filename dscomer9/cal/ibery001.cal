|FICHEROS;
     ibery001 #0;

     iber0109 #11;   || movtos. lins.
     iber0105 #12;   || ajustes de marcado

     iber0102 #20;   || tarjetas
     iber0103 #21;   || incidencias
     iber0016 #23;   || empresas
     iber0101 #24;   || trabajadores
     iber0111 #25;   || calendario laboral

     iberw002 #50;   || def de impresion del detalle
|FIN;

|VARIABLES;
     alfa1 = "";
     alfa2 = "";
     alfa3 = "";
     alfa4 = "";
     nume1 = 0;
     nume2 = 0;
     nume3 = 0;
     nume4 = 0;
     para1 = 0;
     para2 = 0;
     para3 = 0;
     para4 = 0;
     fech1 = @;
     fech2 = @;
     fech3 = @;
     fech4 = @;
     elsw1 = 0;
     elsw2 = 0;
     elsw3 = 0;
     elsw4 = 0;
     FEstado = 0;
     seleccio = 0;
     modo = 0;
     sw_rup = 0;

     p_mes = "";
     dia_ant = 0;
     bar_ant = 0;
     hor_ant = 0;
     min_ant = 0;
     lin_ant = 0;
     nhor = 0;
     nmin = 0;
     &shor = 0;
     &sext = 0;
     &dhor = 0;
     &dext = 0;
     &ghor = 0;
     &gext = 0;
     &error = "";
     &swlin = 0;
     swentra = 0;
     dhora = 0;
     hhora = 0;
     chora = 0;
     dminu = 0;
     hminu = 0;
     cminu = 0;
     desde = 0;
     hasta = 0;
     rajus = 0;
     hajus = 0;
     majus = 0;
     lectu = 0;
     rhora = 0;
     dlect = 0;
     hlect = 0;
     aju_cod = 0;
     aju_dia = @;
     elmes = 0;
     eldia = 0;
     fehas = @;
     emp_ant = 0;
     tra_ant = 0;
|FIN;
____________________________________/
|PROCESO defecto; |TIPO 7; ||calcula el a¤o por defecto segun fecha sistema
     fech1 = ~;
     alfa1 = fech1;
     alfa1 = alfa1 % -104;
     #0Campo = alfa1;
     |PINTA #0Campo;
|FPRC;

|PROCESO mirames; |TIPO 7; ||CALCULA EL MES POR DEFECTO SEGUN FECHA SISTEMA
     fech1 = ~;
     alfa1 = fech1;
     alfa1 = alfa1 % 402;
     #0Campo = alfa1;
     |PINTA #0Campo;
|FPRC;

|PROCESO pintames; |TIPO 0;      || PINTA EL MES LITERAL
     |SI #0Campo =  1;  p_mes = "ENERO     ";  |FINSI;
     |SI #0Campo =  2;  p_mes = "FEBRERO   ";  |FINSI;
     |SI #0Campo =  3;  p_mes = "MARZO     ";  |FINSI;
     |SI #0Campo =  4;  p_mes = "ABRIL     ";  |FINSI;
     |SI #0Campo =  5;  p_mes = "MAYO      ";  |FINSI;
     |SI #0Campo =  6;  p_mes = "JUNIO     ";  |FINSI;
     |SI #0Campo =  7;  p_mes = "JULIO     ";  |FINSI;
     |SI #0Campo =  8;  p_mes = "AGOSTO    ";  |FINSI;
     |SI #0Campo =  9;  p_mes = "SEPTIEMBRE";  |FINSI;
     |SI #0Campo = 10;  p_mes = "OCTUBRE   ";  |FINSI;
     |SI #0Campo = 11;  p_mes = "NOVIEMBRE ";  |FINSI;
     |SI #0Campo = 12;  p_mes = "DICIEMBRE ";  |FINSI;
     |PINTA 1136, p_mes;
|FPRC;

|PROCESO dias; |TIPO 7;
     |SI #0#2 = #0#3;
         #0#12 = "N";
         |PINTA #0#12;
         |C_M #0#12, 1, "N";
     |SINO;
         |C_M #0#12, 1, "S";
     |FINSI;
|FPRC;
____________________________________/
|PROCESO h_extras;
     #24#0 = emp_ant;
     #24#1 = tra_ant;
     |LEE 000#24=;
     |SI FSalida = 0;
         |DDEFECTO #25;
         #25#0  = #24#20;
         #25#13 = #0#0;
         |LEE 000#25=;
         elmes = #0#1;
         eldia = (#50#0 * 100) + 1;
         alfa1 = #25elmes % eldia;
         |SI alfa1 = "1";
             dext = dhor;
         |SINO;
             |SI dhor > 8;
                 dext = dhor - 8;
             |SINO;
                 dext = 0;
             |FINSI;
        |FINSI;
    |FINSI;

    sext = sext + dext;       || total tarjeta extras
    gext = gext + dext;       || total general extras
|FPRC;

|PROCESO ajustes1;
     aju_cod = #12#0;
     aju_dia = #12#142;
|FPRC;

|DEFBUCLE ajustes;
     #12#1;
     ;
     #20#3, "01.01.1900";
     #20#3, fehas;
     ;
     NULL, ajustes1;
|FIN;

|PROCESO lee_ajuste;
     alfa1 = #11#3;  alfa1 = ("00" + alfa1) % -102;
     alfa2 = #0#1;   alfa2 = ("00" + alfa2) % -102;
     alfa3 = alfa1 + "." + alfa2 + "." + #0#0;
     fehas = alfa3;

     aju_cod = -1;
     |BUCLE ajustes;

     |DDEFECTO #12;
     |SI aju_cod ! -1;
         #12#0   = aju_cod;
         #12#142 = aju_dia;
         |LEE 020#12=;
     |FINSI;
|FPRC;

|PROCESO descuentos;
     |HAZ lee_ajuste;

     rajus = 0;
     dlect = (#50#5 * 100) + #50#6;
     hlect = (#50#7 * 100) + #50#8;
     |PARA para1 = 122; |SI para1 [ 125; |HACIENDO para1 = para1 + 1;
           dhora = para1;         dminu = para1 + 4;
           hhora = para1 + 8;     hminu = para1 + 12;
           rhora = para1 + 16;

           desde = (#12dhora * 100) + #12dminu;
           hasta = (#12hhora * 100) + #12hminu;
           |SI desde ! 0;|Y hasta ! 0;
               |SI hlect ] desde;|Y dlect [ hasta;
                   rajus = #12rhora;
               |FINSI;
           |FINSI;
     |SIGUE;
|FPRC;

|PROCESO ajustes;
     |HAZ lee_ajuste;

     rajus = 0;
     lectu = (hajus * 100) + majus;
     |PARA para1 = 2; |SI para1 [ 21; |HACIENDO para1 = para1 + 1;
           dhora = para1;         dminu = para1 + 20;
           hhora = para1 + 40;    hminu = para1 + 60;
           chora = para1 + 80;    cminu = para1 + 100;

           desde = (#12dhora * 100) + #12dminu;
           hasta = (#12hhora * 100) + #12hminu;
           |SI lectu ] desde;|Y lectu [ hasta;
               nhor = #12chora - hajus;
               nmin = #12cminu - majus;
               |SI nmin < 0;     nhor = nhor - 1;    nmin = 60 + nmin;   |FINSI;
               rajus = (nhor * 60) + nmin;

               hajus = #12chora;
               majus = #12cminu;
               |SAL;
           |FINSI;
     |SIGUE;
|FPRC;

|PROCESO detalle;
     #50#0 = #11#3;       || dia
     #50#1 = hor_ant;     || desde hora (M)
     #50#2 = min_ant;     || hasta minutos (M)
     #50#3 = #11#4;       || hasta hora (M)
     #50#4 = #11#5;       || hasta minutos (M)
     #50#5 = hor_ant;     || desde hora (C)
     #50#6 = min_ant;     || hasta minutos (C)
     #50#7 = #11#4;       || hasta hora (C)
     #50#8 = #11#5;       || hasta minutos (C)
     #50#9 = 0;           || minutos ajustados
     #50#10 = 0;          || fraccion horas
     #50#11 = 0;          || fraccion minutos
     #50#12 = 0;          || tiempo en minutos
     #50#13 = 0;          || tiempo en horas

     hajus = #50#5;  majus = #50#6;
     |HAZ ajustes;                      || ajusta el desde
     #50#9 = #50#9 - rajus;
     #50#5 = hajus;  #50#6 = majus;

     hajus = #50#7;  majus = #50#8;

     |HAZ ajustes;                      || ajusta el hasta

     #50#9 = #50#9 + rajus;
     #50#7 = hajus;  #50#8 = majus;

     nhor = #50#7 - #50#5;
     nmin = #50#8 - #50#6
     |SI nmin < 0;    nhor = nhor - 1;    nmin = 60 + nmin;   |FINSI;

     |HAZ descuentos;

     #50#15 = rajus;
     nmin = nmin + #50#15;
     |SI nmin < 0;    nhor = nhor - 1;    nmin = 60 + nmin;   |FINSI;

     |SI nmin < 0;  nmin = 0; |FINSI;
     |SI nhor < 0;  nhor = 0; |FINSI;

     #50#10 = nhor;
     #50#11 = nmin;
     #50#12 = (nhor * 60) + nmin;
     #50#13 = nhor + (nmin / 60);
     #50#14 = #11#2;       || linea

     shor = shor + #50#13;     || total por tarjeta
     dhor = dhor + #50#13;     || total por dia
     ghor = ghor + #50#13;     || total general
|FPRC;

|PROCESO rup_bar;
     |DDEFECTO #20;   #20#0 = bar_ant;              |LEE 000#20=;     || tarjeta
     |DDEFECTO #23;   #23#0 = #20#1;                |LEE 000#23=;     || empresa
     |DDEFECTO #24;   #24#0 = #20#1; #24#1 = #20#2; |LEE 000#24=;     || trabajador

     swlin = 1;
     |PRINT;

     shor = 0;
     sext = 0;
     bar_ant = #11#7;
     swentra = 0;
|FPRC;

|PROCESO rup_dia;
     |SI swentra = 1;
         error = "Tarjeta [" + bar_ant + "." + lin_ant + "] sin registro de salida. [" + hor_ant + ":" + min_ant + "]";
         swlin = 3;
         |PRINT;
     |FINSI;

     |HAZ h_extras;

     |SI #0#12 = "S";
         swlin = 2;
         |PRINT;
     |FINSI;

     dhor = 0;
     dia_ant = #11#3;
     swentra = 0;
|FPRC;

|PROCESO movimientos1;
     |DDEFECTO #20; #20#0 = #11#7; |LEE 000#20=;
     |SI #20#1 < #0#8; |O #20#1 > #0#9;      |ACABA;   |FINSI;   || empresas
     |SI #20#2 < #0#10;|O #20#2 > #0#11;     |ACABA;   |FINSI;   || trabajadores

     |SI sw_rup = 0;
         sw_rup = 1;
         bar_ant = #11#7;
         dia_ant = #11#3;
     |FINSI;

     |SI bar_ant ! #11#7;
         |HAZ rup_dia;
         |HAZ rup_bar;
     |SINO;
         |SI dia_ant ! #11#3;
             |HAZ rup_dia;
         |FINSI;
     |FINSI;

     |SI swentra = 1;
         swentra = 0;
         |HAZ detalle;

         swlin = 0;
         |PRINT;
     |SINO;
         swentra = 1;

         hor_ant = #11#4;
         min_ant = #11#5;
         lin_ant = #11#2;
     |FINSI;

     emp_ant = #20#1;
     tra_ant = #20#2;
|FPRC;

|DEFBUCLE movimientos;
     #11#2;
     9;
     #0#0, #0#1, #0#4, #0#2,  0,  0,  0, #0#13;
     #0#0, #0#1, #0#5, #0#3, 23, 59, 59, #0#14;
     ;
     NULL, movimientos1;
|FIN;

|PROCESO tipo3; |TIPO 3;
     |IMPRE 0;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |ABRET #12;
     |ABRE #20;
     |ABRE #21;
     |ABRE #23;
     |ABRE #24;
     |ABRE #25;
     |INFOR "ibery001";
     |BUCLE movimientos;
     |SI sw_rup ! 0;
         |HAZ rup_dia;
         |HAZ rup_bar;
         |PIEINF;
     |SINO;
         |MENAV "    Seleccion vacia ...";
     |FINSI;
     |FININF;
     |CIERRAT #12;
     |CIERRA #20;
     |CIERRA #21;
     |CIERRA #23;
     |CIERRA #24;
     |CIERRA #25;
     |FINIMP;
|FPRC;
