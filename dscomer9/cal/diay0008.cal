|FICHEROS;
     diay0008 #0;
     diam0003 #3;
     agifa010 #10;
     agifa071 #71;
     agifa073 #73;
     agifa104 #104;
     agifa134 #134;
     agifa025 #25;
     agifa024 #24;
     agifa133 #133;
|FIN;

|VARIABLES;
     nHay = 0;
     aCerrado = "";
     aFacturado = "";
     aSerie = "";
     nFactu = "";
     &eaEstado = "";
     &eaFactu = "";
     &eaFVto = "";
     fHoy = @;
     aSerUlt = "";
     nAlbUlt = 0;
     fFecUlt = @;
     aParcial = "";
     nPendiente = 0;
     nAdjTodo = 0;
     nCuentaLin = 0;
     aReferen = "";
|FIN;

|PROCESO BuscaVtoProx;
     fHoy = ~;
     |ABRE #133;
     #133#4 = #134#49;
     #133#0 = #134#0;
     #133#1 = 1;
     |LEE 000#133];
     |PARA; |SI FSalida = 0; |Y #133#4 = #134#49; |Y #133#0 = #134#0; |HACIENDO;
          |SI #133#3 ] fHoy;
               eaFVto = #133#3;
               |SAL;
          |FINSI;
          |LEE 000#133s;
     |SIGUE;
     |CIERRA #133;
|FPRC;

|PROCESO Fin;
     eaFVto = "";
     |DDEFECTO #134;
     |SI aFacturado = "S";
          #134#49 = aSerie;
          #134#0 = nFactu;
          |LEE 000#134=;
          |SI FSalida ! 0;
               aFacturado = "N";
          |SINO;
               eaFVto = "SIN MAS VTO";
               |HAZ BuscaVtoProx;
          |FINSI;
     |FINSI;

     aReferen = #agifa104#26;
     |QBF aReferen;
     |SI aReferen = "FACTURADO";   || para casos especiales que no se
          aFacturado = "S";        ||/facturan pero es como si lo estuvieran
     |FINSI;                       ||/se fuerza a "S"

     |SI #0#10 ! "T";
          |SI #0#10 = "A";
               |SI aCerrado = "S"; |ACABA; |FINSI;
          |SINO;
               |SI aCerrado = "N"; |ACABA; |FINSI;
          |FINSI;
     |FINSI;
     |SI #0#11 ! "T";
          |SI #0#11 = "F";
               |SI aFacturado = "N"; |ACABA; |FINSI;
          |SINO;
               |SI aFacturado = "S"; |ACABA; |FINSI;
          |FINSI;
     |FINSI;

     |SI aCerrado = "N";
          eaEstado = "A";
     |SINO;
          eaEstado = "C";
     |FINSI;
     eaFactu = aFacturado;

     |DDEFECTO #25;
     #25#0 = #104#7;
     |LEE 000#25=;

     |SI aFacturado = "N"; || El defecto es 1, lo pongo a 0
          #134#0 = 0;
     |FINSI;

     |PRINT;
|FPRC;


|PROCESO UltimaFactu;
     #10#52 = #71#29;
     #10#0 = #71#0;
     |LEE 000#10=;
     |SI FSalida = 0;
          #134#49 = #10#53;
          #134#0 = #10#39;
          |LEE 000#134=;
          |SI FSalida = 0;
               |SI #134#1 ] fFecUlt;
                    fFecUlt = #134#1;
                    aSerUlt = #10#52;
                    nAlbUlt = #10#0;
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE UltimaFactu;
     #71#4;
     ;
     #73#28, #73#0, 001;
     #73#28, #73#0, 999;
     ;
     NULL, UltimaFactu;
|FIN;

|PROCESO agifa073;
     |SI #73#5 = 0;
          |ACABA;
     |FINSI;
     |SI aParcial = "X"; || cuando es parcial y ya ha entrado una vez
          |ACABA;
     |FINSI;

     |SI aParcial = "S";
          fFecUlt = "01.01.0000";
          aFacturado = "N";
          |BUCLE UltimaFactu;

          |DDEFECTO #10;
          #10#52 = aSerUlt;
          #10#0 = nAlbUlt;
          |LEE 000#10=;

          |SI aSerUlt ! "";
               aFacturado = "S";
               aSerie = #10#53;
               nFactu = #10#39;
          |FINSI;
          aParcial = "X";     || para que no entre mas
     |SINO;
          |DDEFECTO #71;
          |SELECT #4#71;
          #71#31 = #73#28;
          #71#12 = #73#0;
          #71#21 = #73#1;
          |LEE 000#71=;

          |DDEFECTO #10;
          #10#52 = #71#29;
          #10#0 = #71#0;
          |LEE 000#10=;

          |SI #10#38 = "N";
               aFacturado = "N";
          |FINSI;
          aSerie = #10#53;
          nFactu = #10#39;
     |FINSI;
|FPRC;

|PROCESO ParcialAdj;
     |SI #73#5 = 0;
          |ACABA;
     |FINSI;

     nCuentaLin = nCuentaLin + 1;
     nPendiente = #73#5 - #73#43;
     |SI nPendiente = 0;
          nAdjTodo = nAdjTodo + 1;
     |FINSI;
|FPRC;

|PROCESO diam0003;
     |SI #3#6 ! "C";
          aCerrado = "N";
     |FINSI;
     nHay = 1;
|FPRC;

|DEFBUCLE diam0003;
     #3#1;
     ;
     #104#25, #104#0, 0000;
     #104#25, #104#0, 9999;
     ;
     NULL, diam0003;
|FIN;

|PROCESO agifa104;
     |DDEFECTO #24;
     #24#0 = #104#4;
     |LEE 000#24=;
     |SI #24#3 ] #0#12; |Y #24#3 [ #0#13;
          aFacturado = "S";
          aCerrado = "S";
          nHay = 0;
          |BUCLE diam0003;
          |SI nHay = 0;
               aCerrado = "N";
          |FINSI;

          nAdjTodo = 0;
          nCuentaLin = 0;
          |BUCLELIN 2ParcialAdj#104;
          |SI nCuentaLin ! nAdjTodo; |Y nAdjTodo ! 0;
               aParcial = "S";
          |SINO;
               aParcial = "N";
          |FINSI;
          aSerUlt = "";
          nAlbUlt = 0;
     |SINO;
          |ERROR;
     |FINSI;
|FPRC;

|DEFBUCLE agifa104;
     #104#1;
     1, 4, 7;
     #0#0, #0#2, #0#4, #0#6, #0#8;
     #0#1, #0#3, #0#5, #0#7, #0#9;
     ;
     NULL, agifa104, agifa073, Fin;
|FIN;

|PROCESO Tipo3; |TIPO 3;
     Impresora = "Crystal Reports";
     |IMPRE 0;
     |SI FSalida = 0;
          |INFOR "diay0008";
          |SI FSalida = 0;
               |ABRE #10;
               |ABRET #71;
               |ABRE #134;
               |ABRE #25;
               |ABRE #24;
               |BUCLE agifa104;
               |CIERRA #24;
               |CIERRA #25;
               |CIERRA #134;
               |CIERRAT #71;
               |CIERRA #10;
               |PIEINF;
               |FININF;
          |SINO;
               |MENAV "0000Error en informe 'diay0008'";
          |FINSI;
          |FINIMP;
     |FINSI;
|FPRC;
