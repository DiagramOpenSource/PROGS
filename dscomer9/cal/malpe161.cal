|FICHEROS;
     malpe161 #0;
     malpe081 #81;
     malpe154 #154;
     malpe156 #156;
     malpe157 #157;
     malpe158 #158;
|FIN;

|VARIABLES;
     nSal = 0;
     aAlfa = "";
     aModiFab = "";
     fFecha = @;
     nHora = 0;
     nMinu = 0;
     nDesde = 0;
     nHasta = 0;
|FIN;

|PROCESO Fabrica0; |TIPO 0;
     |SI FSalida = 2; |ACABA; |FINSI;
     |ABRE #158;
     #158#0 = #0#2;
     |LEE 000#158=;
     |SI FSalida ! 0;
          |MENAV "0000No existe fabrica...";
          |ERROR;
     |FINSI;
     |CIERRA #158;
|FPRC;

|PROCESO Horas0; |TIPO 0;
     |SI FSalida = 2; |ACABA; |FINSI;
     |SI #0#4 = 0;
          |MENAV "0000Debe de introducir un valor...";
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO Trabajador66; |TIPO 66;
     |ERROR;
     || para anular el F11
|FPRC;

|PROCESO Tipo; |TIPO 0;
     |SI #0#3 = "T";
          #0#8 = "Trabajadas";
     |SINO;
          #0#8 = "Sabado Tarde";
     |FINSI;
     |PINTA #0#8;
|FPRC;

|PROCESO Turno; |TIPO 0;
     |SI #0#5 = 1;
          #0#7 = "Normal";
     |SINO;
          #0#7 = "Nocturno";
     |FINSI;
     |PINTA #0#7;
|FPRC;

|PROCESO Horas7; |TIPO 7;
     |C_M #0#2, 1, aModiFab;
     |C_M #0#5, 1, "S";
     fFecha = #0#1 % 1;
     aAlfa = fFecha % 1101;
     |SI aAlfa = "S";
          |C_M #0#3, 1, "S";
     |FINSI;
|FPRC;

|PROCESO Trabajador0; |TIPO 0;
     |ABRE #154;
     #154#0 = #0#0;
     |LEE 000#154=;
     |CIERRA #154;
     #0#2 = #154#12; |PINTA #0#2;
     aModiFab = #154#13;
     |SI #0#1 < #154#2; |O #0#1 > #154#3;
          aAlfa = "0000Fecha fuera de periodo de calculo" + #154#2 +"-"+#154#3;
          |MENAV aAlfa; |ERROR;
     |FINSI;
     fFecha = #0#1 % 1;
     aAlfa = fFecha % 1101;
     |SI aAlfa = "S";
          |HORA aAlfa;
          nHora = aAlfa % 102;
          nMinu = aAlfa % 402;
          nHora = nHora * 60 + nMinu;
          nDesde = #81#14 * 60 + #81#15;
          nHasta = #81#16 * 60 + #81#17;
          |SI nHora ] nDesde; |Y nHora [ nHasta;
               #0#3 = "S"; |PINTA #0#3;
          |FINSI;
     |FINSI;
     |C_M #0#2, 1, "N";
     |C_M #0#3, 1, "N";
     |C_M #0#5, 1, "N";
     |ABRE #158;
     #158#0 = #0#2;
     |LEE 000#158=;
     |SI FSalida ! 0; |C_M #0#2, 1, "S"; |FINSI;
     |CIERRA #158;

     |HORA aAlfa;
     nHora = aAlfa % 102;
     nMinu = aAlfa % 402;
     nHora = nHora * 60 + nMinu;
     nDesde = #81#9 * 60 + #81#10;
     nHasta = #81#11 * 60 + #81#12;
     |SI nDesde < nHasta;
          |SI nHora ] nDesde; |Y nHora [ nHasta;
               #0#5 = 1;
          |SINO;
               #0#5 = 2;
          |FINSI;
     |SINO; || SI el Hasta es menor
          nHasta = 60 * 60;
          |SI  nHora ] nDesde; |Y nHora [ nHasta;
               #0#5 = 1;
          |SINO;
               nDesde = 0;
               nHasta = #81#11 * 60 + #81#12;
               |SI  nHora ] nDesde; |Y nHora [ nHasta;
                    #0#5 = 1;
               |SINO;
                     #0#5 = 2;
               |FINSI;
          |FINSI;
     |FINSI;
     |PINTA #0#5;
|FPRC;


|PROCESO Trabajador6; |TIPO 6;
     |SI FSalida = 1;
          |ERROR; |PTEC 807;
     |FINSI;
|FPRC;

|PROCESO Tipo12; |TIPO 12;
     |SI FSalida = 1; |O FSalida = 7;
          nSal = 1; |ACABA;
     |FINSI;
     |SI #0#4 = 0; |ACABA; |FINSI;
     |CONFI "0000SEs Correcto?";
     |SI FSalida ! 0; |ACABA; |FINSI;

     |DDEFECTO #156;
     |ABRE #156;
     #156#0 = #0#0;
     #156#1 = #0#1;
     #156#2 = #0#2;
     #156#3 = #0#3;
     |LEE 101#156=;
     |SI FSalida ! 0;
          #156#4 = #0#4;
          #156#5 = #0#5;
          #156#7 = ~;
          |HORA aAlfa;
          #156#8 = aAlfa;
          |GRABA 020#156n;
          |MENAV "0000Parte de trabajo archivado.";
     |SINO;
          aAlfa = "0000No se graba la marcada. Ya existe una anterior de " + #156#4 + " horas.";
          |MENAV aAlfa;
     |FINSI;
     |CIERRA #156;
|FPRC;

|PROGRAMA;
     |ABRE #81; |LEE 000#81p; |CIERRA #81;

     |CLS;

  |ET Sigue;
     |PINPA #0#0;
     |PARA nSal = 0; |SI nSal = 0; |HACIENDO;
          |DDEFECTO #0;
          |PINDA #0#0;
          |ENDATOS #1#0;
     |SIGUE;

     |PUSHV 2401 2480;
     |ATRI S;
     |PINTA 2425, " Introduzca Clave:             ";
     |ATRI 0;
     E_sup = 10;
     E_inf = "";
     aAlfa = "";
     aAlfa = 2445 ? -1;
     |SI FSalida ! 0; aAlfa = ""; |FINSI;
     |POPV;
     |SI aAlfa ! #81#13;
          |MENAV "0000Clave incorrecta...";
          |VETE Sigue;;
     |FINSI;
|FPRO;
