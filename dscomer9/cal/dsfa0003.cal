|FICHEROS;
     dsfa0003 #0;
     agifa010 #1;        || albaranes cabs.
     agifa104 #2;        || pedidos cabs.
     agifa024 #4;        || clientes
     dsfa0004 #10,MANTE; || tmp. de edicion
     agis0002 #11;       || recibos adelantados
     agifa024 #24;
     agifa025;
     dsm00003 #30;
     agifa073 #73;
     agifa321 #321;
     agifa324 #324;
     dsm00289 #289;
     dsm00279 #279;

     dscamrec@ #600;
     agis0002@ #3;       || recibos adelantados

     cafe0000@ #20;
     cafe0011@;          || Solo para pintar la pantalla
     cafe0014@;
|FIN;


|VARIABLES;
     aDFactu = "";
     aHFactu = "";
     &eaModifica = ""
     n1 = 0;
     n2 = 0;
     &enTiva = 0;
     &efFiva = @;
     &eaDiva = "";
     &enIva = 0;
     &enRec = 0;
     &nDeci_BaseMx = 0;
     &eaJamaEntPend = "N";
     &enJamaImpPend = 0;
     nHayEntrega = 0;

     &eaSerie = "";

     aDef14 = "";
     nAbo = 0;
     aCobro = "";
     nTipo = 0;
     nTotalAlba = 0;
     nTotaJama = 0;
     nSwJamaica = 0;
     &eaDEjer       = "";
     &eaHEjer       = "";
     &Tempo         = "";
     aTempo600      = "";
     aTempo10       = "";
     &aCPath        = ""; &aCNome = "";
     aSubProg       = ":dscarter/dsrecibo";

     &nRieTotal     = 0;
     &nRieUtili     = 0;
     &eaTag         = "";

     &eaSer         = "";
     &enNum         = 0;
     &eaTipoEntre   = "";
     &enTotaEntre   = 0;
     &enNumRec      = 0;

     &nDeci_Base    = 0;

     nDoc           = 0;
     Comodin        = "";
     aMensa = "";
     aDef   = "";
     aPath  = "";
     aParam = "";
     aAlfa  = "";
     ext1   = "";
     aInf   = "dsfa0003";
     nSwImp =  0;
     nTPedi = 0;
     aAlfa1 = "";
     aAlfa2 = "";
     aAlfa3 = "";
     aAlfa4 = "";
     nNume1 = 0;
     nNume2 = 0;
     nNume3 = 0;
     nNume4 = 0;
     nPara1 = 0;
     nPara2 = 0;
     nPara3 = 0;
     nPara4 = 0;
     fFech1 = @;
     fFech2 = @;
     fFech3 = @;
     fFech4 = @;
     nFEstado = 0;
     nSeleccio = 0;
     nModo = 0;

     &ser = "";
     &doc = 0;
     &TipoCta = "";
     &Cliente = "";
     &SPedido = "";
     &NPedido = 0;
     &FPedido = @;
     &PPedido = "";
     &BAdela  = 0;
     &aTmp148 = "";

     nACuenta = 0;
     nCobro = 0;
     nRecibo = 0;
     nFalta = 0;
     nInkey = 0;
     i = 0;
|FIN;

____________________________________/
|PROCESO Tipo; |TIPO 0;
     |SI #0#0 = "P";
          |C_M #0#12, 1, "S";
          |C_M #0#13, 1, "S";
     |SINO;
          |C_M #0#12, 1, "N";
          |C_M #0#13, 1, "N";
     |FINSI;
|FPRC;
____________________________________/
|PROCESO Min10;
     #10#0 = "     ";
     #10#1 = 1;
|FPRC;

|PROCESO Max10;
     #10#0 = "zzzzz";
     #10#1 = 999999;
|FPRC;

|PROCESO PintaTotales;
     |ATRI I;
     |PINTA 2240, #0#9;
     |PINTA 2252, #0#10;
     |PINTA 2264, #0#11;
     |ATRI 0;
|FPRC;

|PROCESO Imprime;
     |SI #0#0 = "A";
          #1#52 = #10#0;
          #1#0 = #10#1;
          |LEE 000#1=;
     |SINO;
          #2#25 = #10#0;
          #2#0 = #10#1;
          |LEE 000#2=;
     |FINSI;

     |PRINT;
|FPRC;

|DEFBUCLE Imprime;
     #10#1;
     ;
     "     ", 000000;
     "zzzzz", 999999;
     ;
     NULL, Imprime;
|FIN;

|PROCESO Impresion;
     |IMPRE 0;
     |SI FSalida = 0;
          |INFOR aInf;
          |SI FSalida = 0;
               |ABRE #1;
               |ABRE #2;
               |BUCLE Imprime;
               |CIERRA #1;
               |CIERRA #2;
               |PIEINF;
               |FININF;
          |FINSI;
          |FINIMP;
     |FINSI;
|FPRC;

|PROCESO PreImprime;
     |ABRE #11;
     #11#30 = #0#0;
     #11#20 = #10#0;
     #11#17 = #10#1;
     #11#18 = 0;
     |LEE 000#11];
     |SI FSalida = 0; |Y #11#30 = #0#0; |Y #11#20 = #10#0; |Y #11#17 = #10#1;
          |CONFI "0000NDesea imprimir el recibo? ";
          |SI FSalida = 0;
               ser = #10#0;
               doc = #10#1;
               TipoCta = #0#0;
               |SUB_EJECUTA_NP "agifa149";
          |FINSI;
     |FINSI;
     |CIERRA #11;
|FPRC;
____________________________________/ DESDE LA EDICION
|PROCESO Pulsa; |TIPO 11;
     nInkey = FSalida;

     |SI nInkey = 13;         || F5 (ver documento)
          |SI #10#11 = "S";
               |MENAV "0000Linea de entrega pendiente";
               |ERROR; |ACABA;
          |FINSI;
          |SI #0#0 = "A";               || albaran
               |ABRE #1;
               #1#52 = #10#0;
               #1#0 = #10#1;
               |LEE 020#1=;
               |SI FSalida = 0;
                    |PUSHV 0101 2380;
                    |CLS;
                    |HAZ CambioDivisa_010;
                    |PINPA #0#1;
                    |PINDA #0#1;
                    |ENDATOS #4#1;
                    |POPV;
               |FINSI;
               |CIERRA #1;
          |FINSI;
          |SI #0#0 = "P";               || pedido
               |ABRE #2;
               #2#25 = #10#0;
               #2#0 = #10#1;
               |LEE 020#2=;
               |SI FSalida = 0;
                    |PUSHV 0501 2380;
                    |HAZ CambioDivisa_104;
                    |PINPA #0#2;
                    |PINDA #0#2;
                    |ENDATOS #4#2;
                    |POPV;
               |FINSI;
               |CIERRA #2;
          |FINSI;
     |FINSI;

     |SI nInkey = 14;         || F6 (editar entregas a cuenta)
          |SI #10#11 = "S";
               |MENAV "0000Linea de entrega pendiente";
               |ERROR; |ACABA;
          |FINSI;
          |PUSHV 0501 2380;
          TipoCta = #0#0;
          Cliente = #10#3;
          SPedido = #10#0;
          NPedido = #10#1;
          FPedido = #10#2;
          PPedido = #10#2;
          BAdela  = 0;
          eaModifica = "N";
          |SUB_EJECUTA_NP "agis0002";
          aAlfa = "agis0002"
          |NOME_DAT #agis0002#aAlfa#;

          eaSer         = #10#0;
          enNum         = #10#1;
          eaTipoEntre   = #0#0;
          eaDEjer       = #10#2;
          eaHEjer       = #10#2;
          |HAZ TotalEntrega;
          nACuenta = enTotaEntre;

          |SI nACuenta ! #10#5;
               #0#9 = #0#9 - #10#5;
               #0#11 = #0#11 + #10#5;
               #10#5 = nACuenta; |PINTA #10#5;
               #10#8 = #10#6 - #10#5; |PINTA #10#8;
               #0#9 = #0#9 + #10#5;
               #0#11 = #0#11 - #10#5;
               |SI nSwJamaica = 0;
                    |HAZ CobroJamaica;
               |FINSI;
               |GRABA 020#10a;
               |HAZ PintaTotales;

               |HAZ PreImprime;

               |SI #0#0 = "A";
                    |ABRE #1;
                    #1#52 = #10#0;
                    #1#0 = #10#1;
                    |LEE 101#1=;
                    |SI FSalida = 0;
                         #1#61 = nACuenta;
                         |GRABA 020#1a;
                    |FINSI;
                    |LIBERA #1;
                    |CIERRA #1;
               |FINSI;
          |SINO;
               |SI eaModifica = "S";
                    |HAZ PreImprime;
               |FINSI;
          |FINSI;
          |POPV;
          |HAZ PintaTotales;
     |FINSI;

     |SI nInkey = 15;         || F7 (entregas a cuenta masivas)
          |SI #0#0 = "A";
               |PUSHV 0501 2380;
               TipoCta = #0#0;
               Cliente = #10#3;
               SPedido = #10#0;
               NPedido = #10#1;
               FPedido = #10#2;
               PPedido = #10#2;
               BAdela  = 2;
               eaJamaEntPend = #10#11;
               enJamaImpPend = #10#6;
               |SUB_EJECUTA_NP "agis0002";
               |POPV;
               |PTEC 806;
          |SINO;
               |MENAV "    Opcion no implementada para pedidos ...";
               |ERROR;
          |FINSI;

     |FINSI;

     |SI nInkey = 16;
          |SI #10#11 = "S";
               |MENAV "0000Linea de entrega pendiente";
               |ERROR; |ACABA;
          |FINSI;
          |SI #0#0 = "A";
               |ABRE #1;
               #1#52 = #10#0;
               #1#0 = #10#1;
               |LEE 020#1=;
               |SI FSalida = 0;
                    eaSerie = #agifa010#52; eaTag = "AV"; |HAZ SacaRiesgo;
               |FINSI;
               |CIERRA #1;
          |SINO;
               |ABRE #2;
               #2#25 = #10#0;
               #2#0 = #10#1;
               |LEE 020#2=;
               |SI FSalida = 0;
                    eaSerie = #agifa104#25; eaTag = "PE"; |HAZ SacaRiesgo;
               |FINSI;
               |CIERRA #2;
          |FINSI;

          |PUSHV 0101 2380;
          |BLANCO 0948 1672;
          |CUADRO 1148 1672;
          |CUADRO 0948 1672;
          |ATRI R; |PINTA 1049, "  < CONTROL RIESGOS >  "; |ATRI 0;
          |ATRI I;
          |PINTA 1250, "Concedido:";
          |PINTA 1350, "Total ...:";
          |PINTA 1261, nRieTotal;
          |PINTA 1361, nRieUtili;
          |ATRI 0;
          |PAUSA;
          |POPV;
     |FINSI;

     |SI nInkey = 17;
          |SI #10#11 = "S";
               |MENAV "0000Linea de entrega pendiente";
               |ERROR; |ACABA;
          |FINSI;
          nSwImp = 1;
          |PTEC 807;
     |FINSI;

     |SI nInkey = 18;
          |ABRE #4;
          #4#0 = #10#3;
          |LEE 000#4=;
          |CIERRA #4;

          ext1 = #4#16;
          |SUB_EJECUTA_NP "caextrap.run";
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO GrabaEnlace;
     |SI nSwJamaica = 0; |Y #3#22 = 2;
          |ACABA;   || Los del tipo 2 no pasan a cartera
     |FINSI;

     |ABRE #30;
     #30#0 = #1#3;
     #30#1 = #1#84;
     |LEE 000#30=;
     |SI FSalida ! 0; |DDEFECTO #30; |FINSI;
     |CIERRA #30;

     |DDEFECTO #agifa025;
     |ABRE #agifa025;
     #agifa025#0 = #1#6;
     |LEE 000#agifa025.=;
     |CIERRA #agifa025;

     nDoc = nDoc + 1;
     |DDEFECTO #dscamrec@;
   ||#600#0  =              ||Libro
     #600#1  = #10#0;       ||Serie
     #600#2  = #10#1;       ||Fact
     #600#3  = nCobro;      ||N§ Rec
     #600#4  = #11#8;       ||F.V
     #600#5  = #11#7;       ||F.E
     #600#6  = #4#16;       ||Cuenta Cli
     #600#7  = #4#1;        ||Nom Cli
     #600#8  = #4#29;       ||Nom Banco
 ||  #600#9  = #4#32;       ||Entidad
     #600#10 = #4#33;       ||Sucursal
     #600#11 = #4#31;       ||DC
     #600#12 = #4#30;       ||Cuenta
     #600#13 = #11#22;      ||Tipo Rec
   ||#600#14 =              ||Departa
     #600#15 = nRecibo;     ||Importe
     #600#16 = #11#15;      ||A aceptar
     #600#17 = #11#6;       ||Aceptado
     #600#18 = #4#0;        ||Cliente
     #600#19 = #agifa025#0;
     #600#20 = #agifa025#1;
     #600#21 = #4#3;        ||Zona
     #600#22 = nDoc;        ||Documento
   ||#600#23 =              ||Empresa Contable
   ||#600#24 =              ||Periodo
     #600#25 = Comodin;     ||Empresa ges
   ||#600#26 =              ||A cuenta
     #600#27 = "V";         ||Tipo
     #600#28 = "A";         ||Ope
     #600#29 = #agifa324#0;      ||Divisa
     #600#30 = #agifa324#1;      ||Nombre
     #600#31 = #1#69;            ||Cambio Divisa
     #600#32 = #1#74;            ||Cambio Base
     #600#33 = #4#203;
     #600#34 = 0;
     #600#35 = TipoCta;
     #600#36 = #11#28;
     |SI #11#32 = "N"; #600#37 = #11#33; |FINSI;
     #600#38 = #11#16;
     #600#42 = #1#84;         ||Dir. Entr
     #600#43 = #3#10;

     aAlfa = "|NC"; aAlfa = #600#aAlfa#;
     |SI aAlfa > 48;
          #600#48 = #agifa024#215;
          #600#49 = #agifa024#216;
     |FINSI;
     |SI aAlfa > 50;
          #600#50 = #agifa024#217;
          |ABRE #289;
          #289#0 = #agifa024#0;
          |LEE 000#289=;
          |SI FSalida ! 0; |DDEFECTO #289; |FINSI;
          |CIERRA #289;
          #600#51 = #289#1;
     |FINSI;

     |GRABA 020#600n;
|FPRC;

|PROCESO GrabaEntrega;
     |DFICE Comodin;
     Comodin = Comodin % -106;
     Comodin = Comodin % 0105;
     |ABRE #1;
     #1#52 = #10#0;
     #1#0 = #10#1;
     |LEE 020#1=;
     |CIERRA #1;

     |DDEFECTO #3;
     #3#30 = #0#0;       || tipo documento
     #3#20 = #10#0;      || serie
     #3#17 = #10#1;      || documento
     #3#18 = 99;         || numero
     |LEE 000#3];
     |SI FSalida = 0;
          |LEE 000#3a;
     |SINO;
          |LEE 000#3u;
     |FINSI;
     |SI FSalida = 0;|Y #3#30 = #0#0;
                     |Y #3#20 = #10#0;
                     |Y #3#17 = #10#1;
          nCobro = #3#18 + 1;
     |SINO;
          nCobro = 1;
     |FINSI;
     |DDEFECTO #3;
     #3#30 = #0#0;       || tipo documento
     #3#20 = #10#0;      || serie
     #3#17 = #10#1;      || documento
     #3#18 = nCobro;     || numero
     |GRABA 020#3b;
     |SI FSalida = 0;
          #3#0 = #1#39;  || factura
          #3#2 = #4#0;   || codigo cliente
          #3#3 = #4#1;   || nombre cliente
          #3#4 = #4#29;  || banco
          #3#5 = #11#5;  || domiciliado
          #3#6 = #11#6;  || aceptado
          #3#7 = #11#7;  || fecha emision
          #3#8 = #11#8;  || fecha vencimiento
          #3#9 = nRecibo;|| importe
          #3#10 = #11#10;|| impreso
          #3#11 = #11#11;|| contabilizado
          #3#12 = #4#16; || cuenta contable cliente
          #3#13 = #11#13;|| cobrado
          #3#14 = #11#14;|| impagado
          #3#15 = #11#15;|| a aceptar
          #3#16 = #11#16;|| fecha de cobro
          #3#19 = #1#53; || serie factura
          #3#21 = #11#21;|| afecta a factura
          #3#22 = #11#22;|| tipo de recibo
          #3#23 = #11#23;|| nro.remesa
          #3#24 = #11#24;|| forma de pago
          ||#3#25 = #3#9;  || cobrado       NO SE USA
          #3#26 = #11#26;|| impagado
          #3#27 = #11#27;|| situacion
          #3#28 = #11#28;|| cuenta ingreso
          #3#29 = #11#29;|| texto
          #3#31 = #11#31;|| Referencia
          #3#32 = #11#32;|| Tipo documento
          #3#33 = #11#33;
          |GRABA 020#3a;
          |LIBERA #3;
          |HAZ GrabaEnlace;
     |FINSI;
|FPRC;

|PROCESO dsfa0004_1;
     |SI #279#25 = "N"; || Solo albaranes no facturados
          |SI #10#7 = "S"; |ACABA; |FINSI;
     |FINSI;


     |SI #10#11 = "S";
          || Es una linea de entrega pendiente del modulo 001901
          |ACABA;
     |FINSI;

     |SI nACuenta = 0;   |ACABA;   |FINSI;

     |SI #0#0 = "A";
          |ABRE #1;
          #1#52 = #10#0;
          #1#0 = #10#1;
          |LEE 101#1=;
          |SI FSalida = 0;
               nFalta = #1#78 - #1#61;
               |SI nFalta [ nACuenta;
                    #1#61 = #1#61 + nFalta;
                    nRecibo = nFalta;
                    nACuenta = nACuenta - nFalta;
               |SINO;
                    #1#61 = #1#61 + nACuenta;
                    nRecibo = nACuenta;
                    nACuenta = 0;
               |FINSI;

               |GRABA 020#1a;
               |SI FSalida = 0;|Y nRecibo ! 0;
                    |HAZ GrabaEntrega;
                    #10#5 = #10#5 + nRecibo;
                    #0#9 = #0#9 + nRecibo;
                    #10#8 = #10#6 - #10#5;
                    #0#11 = #0#10 - #0#9;
                    |SI nSwJamaica = 0;
                         |HAZ CobroJamaica;
                    |FINSI;
                    |GRABA 020#10a;
               |FINSI;
          |FINSI;
          |LIBERA #1;
          |CIERRA #1;
     |FINSI;
     |LIBERA #10;
|FPRC;

|DEFBUCLE dsfa0004;
     #10#1;
     3;
     "     ",      1, #11#2;
     "zzzzz", 999999, #11#2;
     be;
     NULL, dsfa0004_1;
|FIN;

|PROCESO aTmp148_1;
     nACuenta = #11#9;
     |BUCLE dsfa0004;
     nACuenta = nACuenta ! nDeci_BaseMx;
     |SI nACuenta ! 0;
          aAlfa1 = "    Quedan [" + nACuenta + "] euros sin asignar ...";
          |MENAV aAlfa1;

          |SI nSwJamaica = 0;
               |HAZ JamaExceso;
          |FINSI;
     |FINSI;
     nHayEntrega = 1;
|FPRC;

|DEFBUCLE aTmp148;
     #11#1;
     ;
     #0#0, "     ",      1,  1;
     #0#0, "zzzzz", 999999, 99;
     ;
     NULL, aTmp148_1;
|FIN;

|PROCESO EntregaMasiva;
     |DDEFECTO #4;
     |ABRE #4;
     #4#0 = Cliente;
     |LEE 000#4=;
     |CIERRA #4;

     |DBASS aPath;
     aPath = aPath + "dscarter/";
     aDef = aPath + "def/dscamrec";
     |CARGA_DEF aDef, dscamrec@;
     |SI FSalida = 0;
          |HAZ CreaTempo; aTempo600 = Tempo;
          |PATH_DAT #600 aPath; |NOME_DAT #600 aTempo600; |DELINDEX #600;
          aCPath = aPath; aCNome = aTempo600;
          |NOME_DAT #11 aTmp148;
          |ABRE #600;
          |ABRE #3;
          nHayEntrega = 0;
          |BUCLE aTmp148;
          |CIERRA #600;
          |CIERRA #3;
          |DELINDEX #11;
          Tempo = aTmp148; |HAZ BoraTempo;
          |SUB_EJECUTA_NP aSubProg;

          |DELINDEX #600; Tempo = aTempo600; |HAZ BoraTempo;

          |DESCARGA_DEF #dscamrec@;

          |SI nHayEntrega ! 0; |Y eaJamaEntPend = "S";
               |HAZ JamaQuitaExceso;
          |FINSI;

     |SINO;
        aMensa = "0000Error al cargar:" + aDef;
        |MENAV aMensa;
     |FINSI;
|FPRC;

|PROCESO TotalPedi;
     enTiva = #73#14;
     efFiva = #2#1;
     |HAZ SacaIVA;
     |SI #24#47 = "S";
          nTPedi = nTPedi + #73#9 + (#73#9 * (enIva + enRec) / 100);
     |SINO;
          nTPedi = nTPedi + #73#9 + (#73#9 * enIva / 100);
     |FINSI;
|FPRC;

|PROCESO agifa104_1;

     |HAZ CambioDivisa_104;    || Sirve para cargar los valores cada vez que

     |SI #0#12 ! "A";
          |SI #0#12 = "S"; |Y #2#12 ! 0; |ACABA; |FINSI;
          |SI #0#12 = "N"; |Y #2#12 = 0; |ACABA; |FINSI;
     |FINSI;

     eaSer         = #2#25;
     enNum         = #2#0;
     eaTipoEntre   = "P";
     eaDEjer       = #2#1;
     eaHEjer       = #2#1;
     |HAZ TotalEntrega;
     nACuenta = enTotaEntre;

     nTPedi = 0;
     |BUCLELIN 2 TotalPedi #2;
     |SI #0#13 ! "A";
          |SI #0#13 = "L";
               |SI nACuenta < nTPedi; |ACABA; |FINSI;
          |SINO;
               |SI nACuenta ] nTPedi; |ACABA; |FINSI;
          |FINSI;
     |FINSI;

     nSeleccio = 1;
     |DDEFECTO #10;
     #10#0 = #2#25;                || serie
     #10#1 = #2#0;                 || numero
     #10#2 = #2#1;                 || fecha
     #10#3 = #2#4;                 || cliente
     #10#4 = #2#8;                 || nombre
     #10#5 = nACuenta;             || a cuenta
     #10#6 = nTPedi;                || total
     #10#7 = "N";                  || albaran facturado S/N
     #10#8 = #10#6 - #10#5;        || saldo
     |SI nSwJamaica = 0;
          |HAZ CobroJamaica;
     |FINSI;
     |SI #0#16 = "T";
          |GRABA 020#10n;
     |SINO;
          |SI #10#6 > 0;
               n1 = #10#5; n2 = #10#6;
          |SINO;
               n1 = -#10#5; n2 = -#10#6;
          |FINSI;
          |SI #0#16 = "P"; |Y n1 < n2;
               |GRABA 020#10n;
          |FINSI;
          |SI #0#16 = "C"; |Y n1 ] n2;
               |GRABA 020#10n;
          |FINSI;
     |FINSI;

     #0#9 = #0#9 + #10#5;          || totales
     #0#10 = #0#10 + #10#6;
     #0#11 = #0#10 - #0#9;
|FPRC;

|DEFBUCLE agifa104;
     #2#1;
     1, 4, 7;
     #0#1, #0#3, #0#5, #0#7, #0#14;
     #0#2, #0#4, #0#6, #0#8, #0#15;
     ;
     NULL, agifa104_1;
|FIN;

|PROCESO agifa010_1;
     |HAZ CambioDivisa_010;

     eaSer         = #1#52;
     enNum         = #1#0;
     eaTipoEntre   = "A";
     eaDEjer       = #1#1;
     eaHEjer       = #1#1;

     |HAZ TotalEntrega;
     nACuenta = enTotaEntre;

     nSeleccio = 1;
     |DDEFECTO #10;
     #10#0 = #1#52;                || serie
     #10#1 = #1#0;                 || numero
     #10#2 = #1#1;                 || fecha
     #10#3 = #1#3;                 || cliente
     #10#4 = #1#4;                 || nombre
     #10#5 = nACuenta;             || a cuenta
     #10#6 = #1#78;                || total
     #10#7 = #1#38;                || albaran facturado S/N
     #10#8 = #10#6 - #10#5;        || saldo
     |SI nSwJamaica = 0;
          |HAZ CobroJamaica;
     |FINSI;
     |SI #0#16 = "T";
          |GRABA 020#10n;
     |SINO;
          |SI #10#6 > 0;
               n1 = #10#5; n2 = #10#6;
          |SINO;
               n1 = -#10#5; n2 = -#10#6;
          |FINSI;
          |SI #0#16 = "P"; |Y n1 < n2;
               |GRABA 020#10n;
          |FINSI;
          |SI #0#16 = "C"; |Y n1 ] n2;
               |GRABA 020#10n;
          |FINSI;
     |FINSI;

     #0#9 = #0#9 + #10#5;          || totales
     #0#10 = #0#10 + #10#6;
     #0#11 = #0#10 - #0#9;
|FPRC;

|DEFBUCLE agifa010;
     #1#1;
     1, 3, 6, 38;
     #0#1, #0#3, #0#5, #0#7, #0#14, aDFactu;
     #0#2, #0#4, #0#6, #0#8, #0#15, aHFactu;
     ;
     NULL, agifa010_1;
|FIN;

|PROCESO JamaCargaExeso;
     |DDEFECTO #24;
     #24#0 = #cafe0014@#2;
     |LEE 000#24=;

     |DDEFECTO #10;
     #10#0 = "*****";
     #10#1 = #cafe0014@#0;
     #10#2 = #cafe0014@#1;
     #10#3 = #cafe0014@#2;
     #10#4 = #agifa024#1;
     #10#5 = #cafe0014@#3;
     #10#6 = #cafe0014@#3;
     #10#7 = "N";
     #10#8 = 0;
     #10#11 = "S";
     |SI #0#16 = "T";
          |GRABA 020#10n;
     |SINO;
          |SI #10#6 > 0;
               n1 = #10#5; n2 = #10#6;
          |SINO;
               n1 = -#10#5; n2 = -#10#6;
          |FINSI;
          |SI #0#16 = "P"; |Y n1 < n2;
               |GRABA 020#10n;
          |FINSI;
          |SI #0#16 = "C"; |Y n1 ] n2;
               |GRABA 020#10n;
          |FINSI;
     |FINSI;

     #0#9 = #0#9 + #10#5;          || totales
     #0#10 = #0#10 + #10#6;
     #0#11 = #0#10 - #0#9;
|FPRC;

|DEFBUCLE JamaCargaExeso;
     #cafe0014@#2002;
     ;
     #0#7, "N";
     #0#8, "N";
     ;
     NULL, JamaCargaExeso;
|FIN;


|PROCESO Tipo3; |TIPO 3;
     |SI #0#17 = "T";
          aDFactu = " ";
          aHFactu = "z";
     |SINO;
          aDFactu = "N";
          aHFactu = "N";
     |FINSI;


     |HAZ LeeMonBase;
     |DELINDEX #10;
     |ABRE #10;
     |SI #0#0 = "A";     |BUCLE agifa010;    |FINSI;
     |SI #0#0 = "P";
          |ABRE #24;
          |BUCLE agifa104;
          |CIERRA #24;
     |FINSI;
     |SI nSwJamaica = 0;
          |ABRE #24;
          |BUCLE JamaCargaExeso;
          |CIERRA #24;
     |FINSI;
     |CIERRA #10;

     |SI nSeleccio = 0;
          |MENAV "    Seleccion vacia ...";
     |SINO;
          |SI nSwJamaica = 0;    || Pinta la pantalla del modulo
               |DDEF aAlfa; aAlfa = aAlfa + "cafe0011";
               |CARGA_DEF aAlfa, cafe0011@;
               |SI FSalida = 0;
                    |C_V #10#10, 1, "S"; || y el campo
                    |PINPA #0#cafe0011@;
                    |DESCARGA_DEF #cafe0011@;
               |SINO;
                   |PINPA #0#10;
               |FINSI;
          |SINO;             || pinta la pantalla estandar
               |PINPA #0#10;
          |FINSI;
          |ATRI R;
          |SI #0#0 = "A";     |PINTA 0643, "Albaranes. ";   |FINSI;
          |SI #0#0 = "P";     |PINTA 0643, "Pedidos. ";     |FINSI;
          |ATRI 0;
          |PARA nSwImp = 1; |SI nSwImp = 1; |HACIENDO;
               nSwImp = 0;
               |HAZ PintaTotales;
               |PTEC 812;
               |PTEC 815;
               |ENTLINEAL #1#10, -2, 4, 20, 0, Min10, Max10;
               |SI nSwImp = 1;
                    |HAZ Impresion;
               |SINO;
                    |SI BAdela = 2;
                         |HAZ EntregaMasiva;
                         BAdela = 0;
                         nSwImp = 1;
                         eaJamaEntPend = "N";
                    |FINSI;
              |FINSI;
          |SIGUE;
     |FINSI;
     Tempo = aTempo10; |HAZ BoraTempo;
|FPRC;

|PROCESO Tipo7; |TIPO 7;
     #0#0 = aParam;
|FPRC;

|PROCESO Docu; |TIPO 0;
     |SI #0#0 = "A";
          |C_M #0#17, 1, "S";
     |SINO;
          |C_M #0#17, 1, "N";
     |FINSI;
|FPRC;

|PROGRAMA;
     |ABRE #279; |LEE 000#279p; |CIERRA #279;

     |HAZ DecimalesBase;

     |HAZ EsJamaica;
     nSwJamaica = FSalida;
     |SI nSwJamaica = 0;
          |DDEF aDef14; aDef14 = aDef14 + "cafe0014";
          |CARGA_DEF aDef14, cafe0014@;
          |SI FSalida ! 0;
               aDef14 = "";
               |MENAV "0000Error al cargar 'cafe0014'";
          |FINSI;
     |FINSI;

     |HAZ CreaTempo; aTempo10 = Tempo;
     |NOME_DAT #10 aTempo10; |DELINDEX #10;
     aParam = PARAMETRO;
     |DDEF aDef;
     aDef = aDef + "agis0002";
     |CARGA_DEF aDef, agis0002@;
     |SI FSalida = 0;
          |MANTE #0;
          |DESCARGA_DEF #agis0002@;
     |SINO;
          aDef = "0000Error al cargar " + aDef;
          |MENAV aDef;
     |FINSI;
     |DELINDEX #10; Tempo = aTempo10; |HAZ BoraTempo;

     |SI nSwJamaica = 0; |Y aDef14 ! "";
          |DESCARGA_DEF #cafe0014@;
     |FINSI;
|FPRO;

|PROCESO JamaEntrega;
     |SI #20#13 = "N";
          enTotaEntre = enTotaEntre + #20#9;
     |FINSI;
|FPRC;

|DEFBUCLE JamaEntrega;
     #20#1004;
     22;
     eaTipoEntre, eaSer, enNum, 01, 2;
     eaTipoEntre, eaSer, enNum, 99, 2;
     ;
     NULL, JamaEntrega;
|FIN;

|PROCESO TotalEntrega;
     |HAZ TotaRecEntrega;

     |SI nSwJamaica = 0; || Sumamos las de tipo 2 que no pasan a cartera
          |DDEF aAlfa;   || y que no esten cobradas
          aAlfa = aAlfa + "agis0002";
          |CARGA_DEF aAlfa, cafe0000@;
          |SI FSalida = 0;
               |BUCLE JamaEntrega;
               |DESCARGA_DEF #cafe0000@;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO MiraCobro;
     |SI #20#32 = "N";
          aCobro = "N";
     |SINO;
          |SI #20#22 ! 3; |Y #20#22 ! 4; || A¤adido Tiquet:1901/1142
               nTotaJama = nTotaJama - (#20#9 * nAbo);
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE MiraCobro;
     #20#1004;
     ;
     #0#0, #10#0, #10#1, 01;
     #0#0, #10#0, #10#1, 99;
     ;
     NULL, MiraCobro;
|FIN;

|PROCESO CobroJamaica;
     |DDEF aAlfa;
     aAlfa = aAlfa + "agis0002";
     |CARGA_DEF aAlfa, cafe0000@;
     |SI FSalida = 0;
          nTotalAlba = #10#6 ! 2;
          nTotaJama = nTotalAlba;
          nAbo = 1;
          |SI nTotaJama < 0; nAbo = -1; |FINSI;
          nTotaJama = nTotaJama * nAbo;
          aCobro = "S";
          |BUCLE MiraCobro;
          |DESCARGA_DEF #cafe0000@;

          || El #10#10 Solo se usa para el modulo

          nTotaJama = nTotaJama ! 2;
          |SI aCobro = "S"; |Y nTotaJama [ 0;
               #10#10 = "S";
          |SINO;
               nTotaJama = nTotaJama * nAbo;
               |SI nTotaJama = nTotalAlba;
                    #10#10 = "N";
               |SINO;
                    #10#10 = "P";
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO JamaExceso;
     |ABRE #cafe0014@;
     |LEE 000#cafe0014@.u;
     |SI FSalida = 0;
          #cafe0014@#0 = #cafe0014@#0 + 1;
     |SINO;
          #cafe0014@#0 = 1;
     |FINSI;
     #cafe0014@#1 = ~;
     #cafe0014@#2 = Cliente;
     #cafe0014@#3 = nACuenta;
     |GRABA 020#cafe0014@.n;
     |CIERRA #cafe0014@;

     |ABRE #24;
     |ABRE #10;
     |HAZ JamaCargaExeso;
     |CIERRA #10;
     |CIERRA #24;
|FPRC;

|PROCESO JamaQuitaExceso;
     |ABRE #cafe0014@;
     #cafe0014@#0 = NPedido;
     |LEE 121#cafe0014@.=;
     |SI FSalida = 0;
          #cafe0014@#4 = "S";
          |GRABA 020#cafe0014@.a;
     |FINSI;
     |CIERRA #cafe0014@;

     |ABRE #10;
     #10#0 = SPedido;
     #10#1 = NPedido;
     |BORRA 020#10c;
     |CIERRA #10;
|FPRC;

|PROCESO Tipo66; |TIPO 66;
     |SI #0#0 = "P";
          |ABRE #2;
          |SI Campo = 3;
               #2#25 = #0#1;
               #2#0 = #0#3;
               |LEE 000#2];
               |CONSULTA_CLAVE #1#2;
               |SI FSalida = 0;
                    #0#1 = #2#25; |PINTA #0#1;
                    #0#3 = #2#0; |PINTA #0#3;
               |SINO;
                    |ERROR;
               |FINSI;
          |SINO;
               #2#25 = #0#2;
               #2#0 = #0#4;
               |LEE 000#2];
               |CONSULTA_CLAVE #1#2;
               |SI FSalida = 0;
                    #0#2 = #2#25; |PINTA #0#2;
                    #0#4 = #2#0; |PINTA #0#4;
               |SINO;
                    |ERROR;
               |FINSI;
          |FINSI;
          |CIERRA #2;
     |SINO;
          |ABRE #1;
          |SI Campo = 3;
               #1#52 = #0#1;
               #1#0 = #0#3;
               |LEE 000#1];
               |CONSULTA_CLAVE #1#1;
               |SI FSalida = 0;
                    #0#1 = #1#52; |PINTA #0#1;
                    #0#3 = #1#0; |PINTA #0#3;
               |SINO;
                    |ERROR;
               |FINSI;
          |SINO;
               #1#52 = #0#2;
               #1#0 = #0#4;
               |LEE 000#1];
               |CONSULTA_CLAVE #1#1;
               |SI FSalida = 0;
                    #0#2 = #1#52; |PINTA #0#2;
                    #0#4 = #1#0; |PINTA #0#4;
               |SINO;
                    |ERROR;
               |FINSI;
          |FINSI;
          |CIERRA #1;
     |FINSI;
|FPRC;
