     NOTA: Si no existe el cliente no se graba en la bandeja pedidos

|FICHEROS;
     coimz026 #0;
     coima035 #35; ||Param .Correo
     coima023 #23; ||Bandeja
     coima049 #49; ||Pedidos
     agifa024 #24;
     dsm00003 #3;
|FIN;

|VARIABLES;
     aObs2 = "";
     aTmp = "";
     a0D = "";
     a0A = "";
     aCar = "";
     nCampo = 0;
     aLetra = "";
     aReg = "";
     aTab = "";
 {99}Valor = "";
     nSw = 0;
     nHandle = 0;
     aDato = "";
     aFic = "";
     aLon = "";
     aNombre = "";
     nPos = 0;
     i = 0;
     aAlfa = "";
     nLin = 0;
     aBase = "";
     aDir = "";
     aDSCorreo = "";
     aUsu = "";
     aPwd = "";
     aPop3 = "";
     nError = 0;
     nCorreos = 0;
|FIN;

/*
     1-C¢digo Cliente
     2-N§ direcci¢n de entrega.
     3-NULO
     4-C¢digo Art¡culo
     5-Unidades.
     6-NULO
     7-NULO
     8-E-mail
     9-Observa_1
     10-Observa_2
     11-Observa_3
     12-Su pedido
     13-F.Entrega

     A¤adidos Tarea: T3377
     14-Partida
     15-Importe Portes
     16-Envase
     17-Bultos
     18-Bruto
*/

|PROCESO Dato;
     aTab = &9;
     aAlfa = aTab * 18;
     aDato = aDato + aTab;

     nCampo = 1;
     IValor = Valor1<-;
     aLon = aDato % A0;
     aReg = "";
     |PARA i = 1; |SI i [ aLon; |HACIENDO i = i + 1;
          nPos = i * 100 + 1;
          aLetra = aDato % nPos;
          |SI aLetra = aTab;
               Valor = aReg;
               aReg = "";
               nCampo = nCampo + 1;
               IValor = IValor + 1;
          |SINO;
               aReg = aReg + aLetra;
          |FINSI;
          |SI nCampo > 90; |SAL; |FINSI;     || Por si.... :)
     |SIGUE;
     Valor = aReg;
|FPRC;

|PROCESO ActMail;
     |ABRE #3;
     #3#0 = #49#2;
     #3#1 = #49#3;
     |LEE 101#3=;
     |SI FSalida = 0;
          #3#24 = #49#11;
          |GRABA 020#3a;
          |SI #3#1 = 1;
               |ABRE #24;
               #24#0 = #49#2;
               |LEE 101#24=;
               |SI FSalida = 0;
                    #24#214 = #49#11;
                    |GRABA 020#24a;
               |FINSI;
               |CIERRA #24;
          |FINSI;
     |FINSI;
     |CIERRA #3;
|FPRC;

|PROCESO Grabalo;
     nLin = nLin + 1;
     |DDEFECTO #49;
     #49#0 = #23#0;
     #49#1 = nLin;
     #49#2 = Valor1;
     #49#3 = Valor2;
||     #49#4 = Valor3;
     #49#5 = Valor4;
     #49#6 = Valor5;
 ||    #49#7 = Valor6;
 ||    #49#8 = Valor7;
     #49#11 = Valor8;
     #49#12 = Valor9;
     #49#13 = Valor10;
     #49#14 = Valor11;


     aAlfa = Valor10 + Valor11; |QBF aAlfa;
     |SI aAlfa = "";
          #49#13 = aObs2;
     |FINSI;

     #49#15 = Valor12;
     #49#16 = Valor13;

     #49#17 = Valor14;   ||Partida
     #49#18 = Valor15;   ||Importe
     #49#19 = Valor16;   ||Envase
     #49#20 = Valor17;   ||Bultos
     #49#21 = Valor18;   ||Bruto

     |ABRE #24;
     #24#0 = #49#2;
     |LEE 000#24=;
     |SI FSalida = 0;
          #49#4 = #24#25;

          aAlfa = #49#11; |QBF aAlfa;
          |SI aAlfa = "";
               #49#11 = #24#197;
          |FINSI;

          |GRABA 020#49n;
          aAlfa = #49#11; |QBF aAlfa;
          |SI aAlfa ! "";
              |HAZ ActMail;
          |FINSI;
     |FINSI;
     |CIERRA #24;
|FPRC;

|PROCESO Caracter;
     |SI aCar = a0D; |O aCar = a0A;
          |SI aDato ! "";
               |HAZ Dato;
               |HAZ Grabalo;
          |FINSI;
          aDato = ""; aObs2 = "";
     |FINSI;
     |SI aCar ! a0D; |Y aCar ! a0A;
          aLon = aDato % A0;
          |SI aLon < 250;
               aDato = aDato + aCar;
          |SINO;
               aObs2  = aObs2 + aCar;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO ProcesarCorreo;
     |ABRE #49;
     aFic = aNombre;
     |XABRE "B", aFic, nHandle;
     |SI FSalida ! 0;
          aAlfa = "0000Problemas procesando correo: " + aFic;
          |SI nSw = 0; |MENAV aAlfa; |FINSI;
     |SINO;
          nHandle = FSalida;
          nLin = 0
          |XLEE nHandle, 250, aTmp;
          |PARA; |SI FSalida > 0; |HACIENDO;
               |PARA; |SI aTmp ! ""; |HACIENDO;
                    aCar = aTmp % 101;
                    |SI aTmp = aCar;
                         aTmp = ""; ||con un caracter falla: "aTmp = aTmp % 2"
                    |SINO;
                         aTmp = aTmp % 2;
                    |FINSI;
                    |HAZ Caracter;
               |SIGUE;
               |XLEE nHandle, 250, aTmp;
          |SIGUE;
          |XCIERRA nHandle;
     |FINSI;
     |CIERRA #49;
|FPRC;

|PROCESO SacaNom;
     aFic = "";
     aLon = aNombre % A0;
     nPos = -101;
     |PARA i = aLon; |SI i > 0; |HACIENDO i = i - 1;
          aAlfa = aNombre % nPos;
          |SI aAlfa = "/"; |O aAlfa = "\"; |SAL; |FINSI;
          aFic = aAlfa + aFic;
          nPos = nPos -100;
     |SIGUE;
|FPRC;

|PROCESO Historico;
     |HAZ SacaNom;
     |SI aFic = "_cuerpo_.txt";
          nLin = 0; |ACABA;
     |FINSI;

     |ABRE #23;
     |SELECT #2#23;
     #23#1 = aFic;
     |LEE 000#23=;
     |SI FSalida = 0;
          nLin = 0;
     |SINO;
          |SELECT #1#23;
          |LEE 000#23u;
          |SI FSalida = 0;
               nLin = #23#0 + 1;
          |SINO;
               nLin = 1;
          |FINSI;

          |HORA aAlfa;
          |DDEFECTO #23;
          #23#0 = nLin;
          #23#1 = aFic;
          #23#2 = ~;
          #23#3 = aAlfa;
          #23#4 = Usuario % 810;
          |GRABA 020#23n;

          |DBASE aBase;
          aAlfa = aBase + "004082"; |MKDIR aAlfa;
          aAlfa = aBase + "004082/adjuntos"; |MKDIR aAlfa;
          aAlfa  = aBase + "004082/adjuntos/" + aFic;
          |COPIA_FICHERO aNombre, aAlfa;
     |FINSI;
     |CIERRA #23;
|FPRC;

|PROCESO Recoge;
     aDir = #35#4; |QBF aDir;
     |MKDIR aDir

     |SI #35#8 = "F";
          aDSCorreo = #35#7;  |QBF aDSCorreo;
     |SINO;
          |SI #35#8 = "L"; |IP_REMOTO aDSCorreo; |FINSI;
          |SI #35#8 = "S"; |IP_LOCAL aDSCorreo; |FINSI;
     |FINSI;
     aUsu = #35#2; |QBF aUsu;
     aPwd = #35#6; |QBF aPwd;
     aPop3 = #35#1; |QBF aPop3;
     aNombre = "";

     aAlfa = "0000Recibiendo [" + aDSCorreo + "]";
     |MENSA aAlfa;
     |DSCORREO_RECIBE aDSCorreo, aPop3, aUsu, aPwd, aDir, aNombre;

     nError = FSalida;

     |PARA aAlfa = aNombre; |SI; |HACIENDO;
          aNombre = aNombre - ";";
          |SI aNombre = aAlfa; |SAL; |FINSI;
          aAlfa = aNombre;
     |SIGUE;

     |BLIN 4;
     |SI nError < 0;
          aAlfa = "    Problemas al recibir correo ..." + nError;
          |SI nSw = 0; |MENAV aAlfa; |FINSI;
     |FINSI;
     |SI nError = 0;
          |HAZ SacaNom;
          aAlfa = "    Correo recibido con exito [" + nError + "][" + aFic + "]";
          |MENSA aAlfa;
     |FINSI;
     |SI nError = 1;
          |SI nCorreos = 1;
               |SI nSw = 0; |MENAV "    No hay correo ..."; |FINSI;
          |SINO;
               nCorreos = nCorreos - 1;
               aAlfa = "0000" + nCorreos + " Correos recibidos...";
               |SI nSw = 0; |MENAV aAlfa; |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Captura; |TIPO 0; || Para pruebas;
     |SI FSalida = 13; ||F5;
          aAlfa = "F*.txt";
          |BROWSE aAlfa;
          aAlfa = aAlfa % 2; |QBF aAlfa;
          |SI aAlfa = "F"; aAlfa = ""; |FINSI;
          |SI aAlfa ! "";
               aNombre = "/tmp/" + Usuario;
               |RECIBE_FICHERO aAlfa, aNombre;
               |HAZ ProcesarCorreo;
               |CONFI "0000SPasar a gestion";
               |SI FSalida = 0;
                    |SUB_EJECUTA_NP "coimz027";  ||Pase pedidos a gestion
               |FINSI;
          |FINSI;
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO Tipo3; |TIPO 3;
     |SI #0#0 ! "SI"; |ACABA; |FINSI;

     |ABRE #35; |LEE 000#35p; |CIERRA #35;
     nCorreos = 0;
     |PARA; |SI; |HACIENDO;
          nError = 0;
          nCorreos = nCorreos + 1;
          |HAZ Recoge;

          |SI nError ! 0; |SAL; |FINSI;

          |HAZ Historico;
          |SI nLin ! 0;
               |HAZ ProcesarCorreo;
          |FINSI;
     |SIGUE;
     |SUB_EJECUTA_NP "coimz027";  ||Pase pedidos a gestion
|FPRC;

|PROGRAMA;
     a0D = &13;
     a0A = &10;
     |SI PARAMETRO = "";
          nSw = 1;
          #0#0 = "SI";
          |HAZ Tipo3;
     |SINO;
          |CLS;
          |DDEFECTO #0;
          |PINPA #0#0;
          |PINDA #0#0;
          |ENDATOS #1#0;
     |FINSI;
|FPRO;
