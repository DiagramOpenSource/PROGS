|FICHEROS;
     coimy003;

     coima018;
     coima019;
     coima020;
|FIN;

|VARIABLES;
     i                   = 0;
     nPos                = 0;
     nHand               = 0;
     nLin                = 0;

     aFoto               = "";
     aOri                = "";
     aDes                = "";
     aNom                = "";
     aLon                = "";
     aCar                = "";
     aLin                = "";
     aAlfa               = "";
     aBase               = "";
     aPathSer            = "";
     aPathPlu            = "";
     aPathPla            = "";
     aPathLoc            = "";
     aPathTra            = "";
     aPathFot            = "";
     aFicGuion           = "";

     &enModoExterno      = 1;
     &eaUnidad           = "";
     &eaOrigen           = "";
     &eaDestino          = "";
     &eaParametro1       = "";
     &eaParametro2       = "";
     &eaParametro3       = "";
     &eaParametro4       = "";
     &eaParametro5       = "";
     &eaAlfa             = "";
     &eaAsigna           = "";
|FIN;

|PROCESO SacaNom;
     aLon = aFoto % 0;
     aNom = "";
     |PARA i = 1; |SI i [ aLon; |HACIENDO i = i + 1;
          nPos = -(i * 100 + 1)
          aCar = aFoto % nPos;
          |SI aCar = "\"; |O aCar = "/"; |SAL; |FINSI;
          aNom = aCar + aNom;
     |SIGUE;
|FPRC;

|PROCESO Bajalo;
     |QBF aFoto;
     |HAZ SacaNom;

     eaOrigen = aPathFot + aNom;
     |XABRE "E", eaOrigen, 0;
     |SI FSalida < 0;
         aNom = "";
         |ACABA;
     |FINSI;

     eaDestino = aPathTra + aNom;
     |HAZ EnviaFicheroConMD5;
|FPRC;

|PROCESO coima020;
     #coima018#0 = #coima020#0;
     |LEE 000#coima018.=;
     |SI FSalida ! 0;  |DDEFECTO #coima018;  |FINSI;

     #coima019#0 = #coima020#0;
     #coima019#1 = #coima020#1;
     |LEE 000#coima019.=;
     |SI FSalida ! 0;  |DDEFECTO #coima019;  |FINSI;

     aFoto = #coima020#5;  |HAZ Bajalo;

     nLin  = nLin + 1;
     |SI nLin > 1;
         eaAlfa   = "1";
         eaAsigna = "T}1}1}1}+F";   |HAZ AsignaTabla;

/*
         eaAsigna = "T}1}2}1}+G";   |HAZ AsignaTabla;
         eaAsigna = "T}1}1}1}+P";   |HAZ AsignaTabla;
*/
     |FINSI;

     eaAsigna = "T}1}" + nLin + "}1}";
     eaAlfa   = ("000" + #coima020#0) % -103;
     eaAlfa   = eaAlfa + " - " + #coima018#1;  |QBF eaAlfa;
     |HAZ QuitaRaros;
     |HAZ AsignaTabla;

     eaAsigna = "T}1}" + nLin + "}2}";
     eaAlfa   = ("000" + #coima020#1) % -103;
     eaAlfa   = eaAlfa + " - " + #coima019#2;  |QBF eaAlfa;
     |HAZ QuitaRaros;
     |HAZ AsignaTabla;

     eaAsigna = "T}1}" + nLin + "}3}";
     eaAlfa   = ("000" + #coima020#2) % -103;
     |HAZ AsignaTabla;

     eaAsigna = "T}1}" + nLin + "}4}";
     eaAlfa   = ("000" + #coima020#3) % -103;
     |HAZ AsignaTabla;

     aAlfa = #coima020#4;  |QBF aAlfa;
     |SI aAlfa ! "";
         eaAlfa = aAlfa;
     |FINSI;

     aAlfa = #coima020#11;  |QBF aAlfa;
     |SI aAlfa ! "";
         eaAlfa = eaAlfa + " " + aAlfa;
     |FINSI;

     aAlfa = #coima020#12;  |QBF aAlfa;
     |SI aAlfa ! "";
         eaAlfa = eaAlfa + " " + aAlfa;
     |FINSI;

     aAlfa = #coima020#13;  |QBF aAlfa;
     |SI aAlfa ! "";
         eaAlfa = eaAlfa + " " + aAlfa;
     |FINSI;

     aAlfa = #coima020#16;  |QBF aAlfa;
     |SI aAlfa ! "";
         eaAlfa = eaAlfa + " " + aAlfa;
     |FINSI;

     eaAsigna = "T}1}" + nLin + "}5}";
     |HAZ AsignaTabla;

     |SI aNom ! "";
          eaAsigna     = "T}1}" + nLin + "}6}";
          eaAlfa       = "#" + aNom + "#";
          |HAZ AsignaTabla;
     |FINSI;

     eaAlfa   = eaDestino;
     eaAsigna = "I#" + aNom + "#";
     |HAZ AsignaWord;
|FPRC;

|DEFBUCLE coima020;
     #coima020#1;
     ;
     #coimy003#0, #coimy003#2, #coimy003#4, 001;
     #coimy003#1, #coimy003#3, #coimy003#5, 999;
     ;
     NULL, coima020;
|FIN;

|PROCESO T3; |TIPO 3;
     eaOrigen  = aPathPla + "coima020.xml";
     eaDestino = aPathLoc + "coima020.xml";
     |HAZ EnviaFicheroConMD5;

     aFicGuion = aPathTra + "guion.txt";
     |XABRE "WC", aFicGuion, nHand;
     |SI FSalida < 0;
         |MENAV "0000Problemas en crear el guion.";
         |ACABA;
     |FINSI;

     aLin = "[FICHERO]#" + aPathLoc + "coima020.xml#" + aPathTra + "coima020.xml";
     |XGRABA nHand, aLin;

     nLin = 0;

     |ABRE #coima018;
     |ABRE #coima019;
     |BUCLE coima020;
     |CIERRA #coima018;
     |CIERRA #coima019;

     aLin = "[IMPRESORA]#P";
     |XGRABA nHand, aLin;
     |XCIERRA nHand;

     eaParametro1 = aPathLoc + "dsxml.exe";
     eaParametro2 = aFicGuion;
     eaParametro3 = aPathPlu;
     eaParametro4 = aPathLoc;
     eaParametro5 = aPathLoc;

     |HAZ EjecutaDSXML;
|FPRC;

|PROCESO EditaPlan;
     eaOrigen  = aPathPla + "coima020.xml";
     eaDestino = aPathLoc + "coima020.xml";
     |HAZ EnviaFicheroConMD5;

     aFicGuion = aPathLoc + "guion.txt";
     |XABRE "WC", aFicGuion, nHand;
     |SI FSalida ] 0;
         aLin = "[FICHERO]#" + aPathLoc + "coima020.xml#" + aPathLoc + "coima020.xml";
         |XGRABA nHand, aLin;

         aLin = "[IMPRESORA]#P";
         |XGRABA nHand, aLin;

         |XCIERRA nHand;
     |FINSI;

     eaParametro1 = aPathLoc + "dsxml.exe";
     eaParametro2 = aFicGuion;
     eaParametro3 = aPathPlu;
     eaParametro4 = aPathLoc;
     eaParametro5 = aPathLoc;

     |HAZ EjecutaDSXML;

     eaOrigen  = aPathPla + "coima020.xml";
     eaDestino = aPathLoc + "coima020.xml";
     |HAZ RecibeFichero;
|FPRC;

|PROGRAMA;
     |SI PARAMETRO = "LISTADO";
         |MANTE #coimy003;
         |ACABA;
     |FINSI;

     |SI PARAMETRO = "PLANTILLA";
         |HAZ EditaPlan;
     |FINSI;

     |SI PARAMETRO = "RECUPERAR";
         |CONFI "0000NEsta seguro de recuperar la plantilla de la de fabrica";
         |SI FSalida ! 0;  |ACABA;  |FINSI;

         aOri = aPathPla + "plantilla_coima020.xml";
         aDes = aPathPla + "coima020.xml";
         |COPIA_FICHERO aOri, aDes;

         |MENAV "0000Plantilla recuperada";
     |FINSI;
|FPRO;

|PROCESO T80;  |TIPO 80;
     |DBASE aBase;

     enModoExterno = 1;

     aPathPlu = aBase + "plugins/";
     aPathPla = aBase + "000web/plantillas/";
     aPathFot = aBase + "000web/fotos/";

     |HAZ DimeUnidad;
     aPathLoc = eaUnidad  + ":\DOCUMENTOS";     |RMKDIR aPathLoc;
     aPathLoc = aPathLoc + "\DSCOMER9";         |RMKDIR aPathLoc;
     aPathLoc = aPathLoc + "\";

     aPathTra = aPathLoc + Usuario;             |RMKDIR aPathTra;
     aPathTra = aPathTra + "\";

     aOri = aPathPla + "coima020.xml";
     |XABRE "E", aOri, 0;
     |SI FSalida < 0;
         aOri = aPathPla + "plantilla_coima020.xml";
         aDes = aPathPla + "coima020.xml";
         |COPIA_FICHERO aOri, aDes;
     |FINSI;
|FPRC;

|PROCESO T90;  |TIPO 90;
     aAlfa = aPathTra + "*.*";
     |R_PDIR aAlfa;
     |PARA;  |SI FSalida = 0;  |HACIENDO;
          |RFBORRA aAlfa;
          |R_SDIR aAlfa;
     |SIGUE;
|FPRC;
