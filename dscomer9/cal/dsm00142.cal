|FICHEROS;
     dsm00138 #0;
     dsm00139 #3;
     agifa019 #2;
     agifa017 #8;
     agifa112 #4;
     agif0041 #11;  || datos empresa
     agifa007 #40;
     agifa142 #41;
     agifa010 #50;
     agifa321 #45;  || Parametros de Divisas
     agifa322 #46;  || Lineas de Proveedores/Divisas
     agifa323 #47;  || Proveedores/Divisas
     agifa324 #42;  || Divisas
     referen@;
|FIN;

|VARIABLES;
     aUsu = "";
     nContaLog = 0;
     aSer = "";
     aNum = "";
     aTipo = "";
     nTipo = 0;
     &enModoCab = 0;
     nSal = 0;
     &eaSerAlb = "";
     &enNumAlb = 0;
     &eaImpreAlb = "";
     &eaFecConstru = "";
     &enSwAct   = 0;

     &eaSerJefe = "";
     &enEvento  = 0;
     &enEnvio   = 0;
     &eaOrigen  = "";
     &eaDestino = "";
     &eaAsunto  = "";

     aSwModiCon = "";
     nSwFlag        = 0;
     &qTprov        = 0;
     &qImpoa        = 0;
     &qSerieF       = "";
     &qSerie        = "";
     &qAlbar        = 0;
     &qFecha        = @;
     &qAbono        = "";
     &qFactu        = 0;
     &qSufac        = "";
     nModo          = 0;
     nSwPorVar      = 0;
     aSerie         = "";
     nNume          = 0;
     aAlfa          = "";
     aDatos         = "";
     aDirEmp        = "";
     nCodEmp        = 0;
     aProv          = "";
     nForma    = 0;
     nImpo     = 0;
     nMes      = 0;
     aMes      = "";
     nNetoDto  = 0;
     &ser_ped       = "";
     &num_ped       = 0;

     &nDeci_Base     = 0;   || Decimales en Totales de la moneda base
     &nDeci_PreBase  = 0;   || Decimales en el Precio en moneda base
     &nDeci_BaseMx   = 0;   || Decimales en Totales de la moneda base (Maxima precision)
     &nDeci_PreBaseMx= 0;   || Decimales en el Precio en moneda base (Maxima precision)
     &nDeci_Div      = 0;   || Decimales en Totales de la divisa
     &nDeci_PreDiv   = 0;   || Decimales en el Precio en divisa
     &nDeci_TotVis   = 0;   || Decimales en Totales visibles (el de la moneda de trabajo)
     &nDeci_TotNoVis = 0;   || Decimales en Totales no visibles (el de la otra moneda)
     &nDeci_PreVis   = 0;   || Decimales en el Precio visual. (lineas)
     &nDeci_ImpVis   = 0;   || Decimales en el Importe visual. (lineas)
     nfecha    = 0;
     &ac_divisa = "N";   || Vale "S" cuando las divisas en compras estan
                         || activadas y la serie es de divisas
     &pintado = 0;       || Sirve para controlar los pintados de las lineas
     &decim_divisa = 0;  || Decimales de la divisa
     &precio_divisa = 0; || Decimales del precio en divisa
     &decim_base = 0;    || Decimales de la moneda base
     &precio_base = 0;   || Decimales del precio en moneda base
     div_act = 0;        || Divisa actualizada. Me sirve en un PARA para que
                         || no salga hasta que este actualizada (div_act=1)
     &prov_divisa = "";  || Codigo de Proveedor. Se pasa a PROVEED/DIVISAS
     &divisa = "";       || Codigo de Divisa. Se pasa a ACTUAL. de DIVISAS
     nModo077 = 0;
     &ser_alb = "";
     &num_alb = 0;
     t_totalb = 0;
     param     = "";
     mensa     = "";
     base      = "";
     alfa      = "";
     alfa1     = "";
     direc     = "";
     asto      = 0;
     &bl       = "                              ";

     mensaje01 = "0423Actualizando linea:";
     mensajei  = "2400NDesea imprimir este deposito? ";
     mensaje   = "0000No puede cambiar este campo !";
     puente    = "";
     &iva1     = 0;
     &iva2     = 0;
     &iva3     = 0;
     &iva4     = 0;
     &iva5     = 0;
     &re1      = 0;
     &re2      = 0;
     &re3      = 0;
     &re4      = 0;
     &re5      = 0;
     &def_alm  = 0;
     &n_dec    = 0;
     imneto    = 0;
     tf        = 0;
     xmes      = 0;
     con       = 0;
     margen    = 0;
     control   = 0;
     pm        = 0;
     de        = 0;
     pe        = 0;
     m1        = 0;
     m2        = 0;
     m3        = 0;
     m4        = 0;
     m5        = 0;
     m6        = 0;
     pcm       = 0;
     tipalb    = 1;
     contenido = 0;
     modo = 0;
     impret = 0;
     &codiBarra = "";
     precio = 0;
     pvp1   = 0;
     pvp2   = 0;
     pvp3   = 0;
     pvp4   = 0;
     pvp5   = 0;
     pvp6   = 0;
     aConfir = "";
     varios = 0;
     portes = 0;
     mens = 0;
     caltot = 0;
     recalpre = 0;
     factordec = 0;
     i = 0;

     nTecla        = 0;
     &eaCodigo     = "";
     &factur = 0;
     &prov   = "";
     &fpago  = "";
     &dto    = 0;
     &dtoa   = 0;
     &dtop   = 0;

     nImpDiv = 0;
     nPPort1 = 0;
     nPPort2 = 0;
     aA¤o = "";
|FIN;

|INCLUYE i_varart;

|CALCULO prov;|TIPO 0;
     enModoCab = (FEntrada / 100) ! 0;
     |SI factur = 1;|O factur = 2;
       |SI factur = 2;
         #0#42 = "S";
         |PINTA #0#42;
       |FINSI;
       #0#3 = prov;
       #0#8 = fpago;
       #0#5 = dto;
       #0#32 = dtoa;
       #0#7 = dtop;
       |PINTA #0#3;
       |PINTA #0#8;
       |PINTA #0#5;
       |PINTA #0#32;
       |PINTA #0#7;
     |FINSI;
|FCAL;

|PROCESO con0; |TIPO 6;
     nTecla = FSalida;
     modo = (FEntrada / 100) ! 0;
     |SI Campo = 3;
          nSal = FSalida;
          enError = 0;
          |ABRE #4;
          #4#0 = #0#3;
          |LEE 000#4=;
          |SI FSalida ! 0;
               eaProve = #0#3;
               |SUB_EJECUTA_NP "dsz99921";   || Por exceso de ficheros
               |SI enError ! 0; |ERROR; |ACABA; |FINSI;
          |FINSI;
          |CIERRA #4;
          FSalida = nSal;
     |FINSI;
|FPRC;


|PROCESO serie7; |TIPO 7;
     modo = (FEntrada / 100) ! 0;

     |HAZ ControlUsuDC;
     |SI enErr ! 0; |PTEC 807; |ACABA; |FINSI;

     |HAZ DefSerCom;
|FPRC;

|PROCESO Prv7; |TIPO 7;
     |HAZ prov;
|FPRC;


|CALCULO fecalbc; |TIPO 7;
     modo = (FEntrada / 100) ! 0;
     |SI modo = 2;
          |CAMPO_MODIFICA #0#1 , 1 , "N";
     |SINO;
          |CAMPO_MODIFICA #0#1 , 1 , "S";
     |FINSI;
|FCAL;


|PROCESO Tipo1_138; |TIPO 1;
     nSwFlag = 0;
|FPRC;

|CALCULO acacpc;|TIPO 2;
     nForma = 0 +. 1;
     con = (FEntrada / 100) ! 0;
     enModoCab = con;

     |SI con = 1; nSwFlag = 0; |FINSI;
     |SI nSwFlag = 1; |ERROR; |ACABA; |FINSI;

     |HAZ ControlUsuDC;
     |SI enErr ! 0;
          nSwFlag = 1; |ERROR; |ACABA;
     |FINSI;

     |SI #0#57 = 0; || chequea el cambio
          |ABRE #agifa324;
          #agifa324#0 = #0#56;
          |LEE 020#agifa324.=;
          |SI FSalida = 0;
               #0#57 = #agifa324#3;
          |FINSI;
          |CIERRA #agifa324;
     |FINSI;

     |C_M #0#54, 1, "S";

     |SI #0#75 = "S"; |Y nForma = -1;
          |MENAV "0000Deposito Exportado, no se puede Modificar...";
          nSwFlag = 1; |ERROR; |ACABA;
     |FINSI;
     |SI #0#26 = 0; |Y nForma = -1;
          |MENAV "0000El deposito no proviene de pedido...";
          nSwFlag = 1; |ERROR; |ACABA;
     |FINSI;
     |SI #0#41 ! 0; |Y nForma = -1;
          |MENAV "0000El deposito esta total o parcialmente servido...";
          nSwFlag = 1; |ERROR; |ACABA;
     |FINSI;

     #0#75 = "N";   ||EXPORTADO

     |HAZ actalc;

     |SI enModoCab = 3;
          |HAZ EsDesanch;
          |SI FSalida = 0;
               aAlfa = "desan016;DB" + #0#50 + (("000000" + #0#0) % -106);
               |SUB_EJECUTA aAlfa;
          |FINSI;
     |FINSI;
|FCAL;

|CALCULO dsm00138;
     |SI caltot = 0; ||Si no viene del proceso caltotal
          |PINTA 445,#3#1;
          #3#15 = #0#3;
          |GRABA 020#3a;
     |SINO;
         |SI recalpre = 1;
               |HAZ TotalLin139;
               |GRABA 001#3a;   ||Grabo lineas
         |FINSI;
     |FINSI;

     ||------------Actualiza Cabecera
     |HAZ CalCabDsm00138;
|FCAL;

|DEFBUCLE dsm00138;
     #3#1;
     ;
     #0#50, #0#0, 001;
     #0#50, #0#0, 999;
     ;
     NULL, dsm00138;
|FIN;

|CALCULO actalc;|TIPO 0;
     |HAZ LimCabDsm00138;
     |BUCLE dsm00138;
|FCAL;

|CALCULO siimprec;|TIPO 3;
     |CONFI mensajei;
     |SI 0 = FSalida; |HAZ imprealc; |FINSI;
|FCAL;

|PROCESO T142_4; |TIPO 4;
     |SELECT #1#0;
     |LEE 000#0=;
     |SI FSalida ! 0;
          |MENAV "0000Por favor, consulte el registro correcto antes de imprimir.";
          |ACABA;
     |SINO;
          |PINDA #0#0;
          FEntrada = 402;
     |FINSI;
     |HAZ imprealc;
|FPRC;

|CALCULO imprealc;
     modo = (FEntrada / 100) ! 0;
     |SI modo = 1; |O modo = 2;
          |GRABA 000#0a;
          |SI FSalida = 112;  ||cuando viene de otro programa por ENDATOS
               |GRABA 020#0n; ||por ejemplo del dsz99957
          |FINSI;
          |LIBERA #0;
     |FINSI;
     ser_ped = #0#50;
     num_ped = #0#0;
     |CIERRAT #0;

     |SUB_EJECUTA_NP "dsy00012";

     |ABRET #0;
     #0#50 = ser_ped;
     #0#0  = num_ped;
     |SI modo = 4;
          |LEE 001#0=;
     |SINO;
          |LEE 101#0=;
     |FINSI;
|FCAL;

|PROCESO Defectos77;
     |ABRE #41; |LEE 001#41p; |CIERRA #41;
     n_dec     = #41#8;
     codiBarra = #41#58;
     aConfir = #41#76;
     |ABRE #agifa007;
     #agifa007#26 = #0#50;
     |LEE 001#agifa007.=;
     |SI FSalida ! 0; |DDEFECTO #agifa007; |FINSI;
     |CIERRA #agifa007;
     def_alm = #agifa007#31;
|FPRC;

|PROCESO mira_deci;
     control = 0;
     |HAZ Defectos77;

     |SI #agifa007#30 = "S";
          #0#12 = 0;
          #0#14 = 0;
          |PINTA #0#12;
          |PINTA #0#14;
     |FINSI;
     |HAZ LeeMonBase;
     |HAZ LeeParamDiv;
|FPRC;

|PROCESO prov_ob; |TIPO 0;
     eaProve = #0#3;
     qTprov = 0;
     qImpoa = 0;

     nModo077 = (FEntrada / 100) ! 0;
     |SI nTecla = 11; |O #41#102 = "S"; |O #41#102 = "X";
          eaCodigo = #0#3;
          eaFuerza = "N";
          |SI nTecla = 11; eaFuerza = "S"; |FINSI;
          |SUB_EJECUTA_NP "dsz99994";
          |SI #41#102 = "N"; |Y nModo077 ! 2; |ERROR;  |FINSI;
     |FINSI;

     |SI #agifa142#15 = "S";
          puente = #agifa112#26;     |QBF puente;
          |SI puente ! "";
               |PUSHV 1008 1370;
               |SI #agifa142#21 = "S"; |AVISO; |FINSI;
               |BLANCO 1008 1370;
               |CUADRO 1008 1370;
               |ATRI R;
               |PINTA 1110 , #agifa112#26;
               |ATRI 0;
               |PAUSA;
               |POPV;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO LeeMonBase; ||Lee los parametros de la moneda base
     |ABRE #agifa321;
     |ABRE #agifa324;
     |LEE 010#agifa321.p;   ||Leo parametros de divisa
     #0#61 = #agifa321#9;  ||Cojo la Moneda Base de parametros
     #agifa324#0 = #agifa321#9;
     |LEE 010#agifa324.=;         ||Leo la ficha de la moneda base en Divisas
     #0#62 = #agifa324#3;  ||Asigno el cambio de la moneda base con el euro
     decim_base = #agifa324#7;    ||Asigno los decimales de la moneda base
     precio_base = #agifa324#9;   ||Asigno los decimales del precio en la mon. base
     |CIERRA #agifa324;
     |CIERRA #agifa321;
|FPRC;

|PROCESO Param_Divisas; |TIPO 0;
     pintado = 0;  ||Controla pintados de las lineas

     |ABRE #agifa324;
     #agifa324#0 = #0#56;
     |LEE 001#agifa324.=;   || Leo la divisa
     |CIERRA #agifa324;

     decim_divisa = #agifa324#7;  || Decimales de la divisa
     precio_divisa = #agifa324#9;  || Decimales en el precio de la divisa
     nDeci_Div = decim_divisa;   || Decimales de la divisa
     nDeci_PreDiv = precio_divisa;  || Decimales en el precio de la divisa
     |SI #0#58 = "B";   ||Si trabajo con la moneda base ...
          nDeci_PreVis   = precio_divisa;
          nDeci_ImpVis   = decim_divisa;
          nDeci_Base     = decim_base;
          nDeci_PreBase  = precio_base;
          nDeci_BaseMx  = decim_base;
          nDeci_PreBaseMx = precio_base;
          nDeci_TotVis   = decim_base;
          nDeci_TotNoVis = decim_divisa;
     |SINO;     ||Si trabajo con la divisa ...
          nDeci_PreVis   = precio_base;
          nDeci_ImpVis   = decim_base;
          nDeci_Base     = decim_base;
          nDeci_PreBase  = precio_base;
         |SI #0#57 = 1;
              nDeci_BaseMx    = decim_base;    || sin triangulacion
              nDeci_PreBaseMx = precio_base;
         |SINO;
              nDeci_BaseMx    = #agifa321#10;  || con triangulacion
              nDeci_PreBaseMx = #agifa321#11;
         |FINSI;
          nDeci_TotVis     = decim_divisa;
          nDeci_TotNoVis   = decim_base;
     |FINSI;
     |HAZ Deci_IVAC;
|FPRC;

|PROCESO LeeParamDiv; |TIPO 0;
     |ABRE #agifa321;
     |LEE 001#agifa321.p;    || Leo parametros de divisas
     |ABRE #agifa007;
     #agifa007#26 = #0#50;
     |LEE 001#agifa007.=;        || Leo la serie
     |SI #0#58 = "D";      || Si la mon. de trabajo es la divisa...
       || ...escondo los campos de la moneda base :
       |CAMPO_POSICION #3#6 , 1 , 1009;
       |CAMPO_POSICION #3#9 , 1 ,1026;
       |CAMPO_VISUAL #3#6 , 1 , "N";
       |CAMPO_VISUAL #3#9 , 1 , "N";
       |CAMPO_LINEAL #3#6 , 1 , "N";
       |CAMPO_LINEAL #3#9 , 1 , "N";
       |CAMPO_MODIFICA #3#6 , 1 , "N";
       || ...y pongo en pantalla los de divisa
       |CAMPO_POSICION #3#30 , 1 , 1644;
       |CAMPO_POSICION #3#31 , 1 ,1662;
       |CAMPO_VISUAL #3#30 , 1 , "S";
       |CAMPO_VISUAL #3#31 , 1 , "S";
       |CAMPO_MODIFICA #3#30 , 1 , "S";
       |CAMPO_LINEAL #3#30 , 1 , "S";
       |CAMPO_LINEAL #3#31 , 1 , "S";
       |SI #agifa007#39 = "S"; |Y #agifa321#2 = "S"; ||Si la serie es de ...
          ac_divisa = "S";  || ... divisas, y en parametros estan activadas ...
          || ... pongo como modificables los campos relacionados con divisas
          |CAMPO_MODIFICA #0#56 , 1 , "S";
          |CAMPO_MODIFICA #0#57 , 1 , "S";
          |CAMPO_MODIFICA #0#58 , 1 , "S";
          |CAMPO_MODIFICA #0#59 , 1 , "S";
          |CAMPO_MODIFICA #0#60 , 1 , "S";
          |SI #agifa321#4 = "S";  || Si la visualizacion de parametros esta activada
            || ... pongo los campos visual. de lineas como visualizables
            |CAMPO_VISUAL #3#32, 1 , "S";
            |CAMPO_VISUAL #3#33 , 1 , "S";
          |SINO;
            || ... los pongo como no visualizables
            |CAMPO_VISUAL #3#32 , 1 , "N";
            |CAMPO_VISUAL #3#33 , 1 , "N";
          |FINSI;
       |SINO; || Si la serie no es de divisas, o no estan activados parametros
          ac_divisa = "N";
          #0#58 = "B";  || La moneda de trabajo es por fuerza la moneda base
          |PINTA #0#58;
          || Pongo como no modificables los campos relacionados con divisas
          |CAMPO_MODIFICA #0#58 , 1 , "N";
          |CAMPO_MODIFICA #0#59 , 1 , "N";
          |CAMPO_MODIFICA #0#60 , 1 , "N";
          |CAMPO_MODIFICA #0#56 , 1 , "N";
          |CAMPO_MODIFICA #0#57 , 1 , "N";
          || Pongo como no visualizables los campos visual. de lineas
          |CAMPO_VISUAL #3#32, 1 , "N";
          |CAMPO_VISUAL #3#33, 1 , "N";
       |FINSI;
     |SINO;  || Si la moneda de trabajo es la moneda base
       || ...escondo los campos en divisa :
       |CAMPO_POSICION #3#30 , 1 , 1009;
       |CAMPO_POSICION #3#31 , 1 ,1026;
       |CAMPO_VISUAL #3#30 , 1 , "N";
       |CAMPO_VISUAL #3#31 , 1 , "N";
       |CAMPO_MODIFICA #3#30 , 1 , "N";
       |CAMPO_LINEAL #3#30 , 1 , "N";
       |CAMPO_LINEAL #3#31 , 1 , "N";
       || ...coloco los campos en moneda base:
       |CAMPO_POSICION #3#6 , 1 , 1644;
       |CAMPO_POSICION #3#9 , 1 ,1662;
       |CAMPO_VISUAL #3#6 , 1 , "S";
       |CAMPO_VISUAL #3#9 , 1 , "S";
       |CAMPO_MODIFICA #3#6 , 1 , "S";
       |CAMPO_LINEAL #3#6 , 1 , "S";
       |CAMPO_LINEAL #3#9 , 1 , "S";
       |SI #agifa007#39 = "S"; |Y #agifa321#2 = "S"; || Si la serie es de divisas...
          ac_divisa = "S"; || ... y divisas estan activadas en parametros
          || Pongo como modificables los campos relacionados con divisas
          |CAMPO_MODIFICA #0#56 , 1 , "S";
          |CAMPO_MODIFICA #0#57 , 1 , "S";
          |CAMPO_MODIFICA #0#58 , 1 , "S";
          |CAMPO_MODIFICA #0#59 , 1 , "S";
          |CAMPO_MODIFICA #0#60 , 1 , "S";
          |SI #agifa321#4 = "S";  || Si la visualizacion esta activada en parametros ...
            || Pongo como visualizables los campos visual. de las lineas
            |CAMPO_VISUAL #3#32, 1 , "S";
            |CAMPO_VISUAL #3#33 , 1 , "S";
          |SINO; || Los pongo como no visualizables
            |CAMPO_VISUAL #3#32 , 1 , "N";
            |CAMPO_VISUAL #3#33 , 1 , "N";
          |FINSI;
       |SINO;  || Si la serie no es de divisas o no estan activados los parametros
          ac_divisa = "N";
          #0#58 = "B";  || La moneda de trabajo es por fuerza la moneda base
          || Pongo como no modificables los campos relacionados con divisas
          |CAMPO_MODIFICA #0#58 , 1 , "N";
          |CAMPO_MODIFICA #0#59 , 1 , "N";
          |CAMPO_MODIFICA #0#60 , 1 , "N";
          |CAMPO_MODIFICA #0#56 , 1 , "N";
          |CAMPO_MODIFICA #0#57 , 1 , "N";
          || Pongo como no visualizables los campos visual. de lineas
          |CAMPO_VISUAL #3#32, 1 , "N";
          |CAMPO_VISUAL #3#33, 1 , "N";
       |FINSI;
     |FINSI;
     |CIERRA #agifa007;
|CIERRA #agifa321;
|HAZ Param_Divisas;
|FPRC;

|PROCESO CambioDivisa; |TIPO 0;
nModo077 = (FEntrada / 100) ! 0;
|SI #0Campo ! Contenido; |Y nModo077 = 1;   ||Si cambia el valor o es alta
  |SI Campo = 3; || Si llamo al proceso desde el cod. de proveedor ...
    |LEE 001#agifa112.=;
    #0#56 = #agifa112#81;  || Asigno la divisa por defecto del proveedor
    #0#58 = #agifa112#82; || Asigno la moneda de trabajo del proveedor
    |PINTA #0#58;
  |FINSI;
  #agifa007#26 = #0#50;
  |LEE 001#agifa007.=;  || Leo la serie
   |SI #agifa007#39 = "S"; |Y #agifa321#2 = "S";
      |SI #agifa112#80 = "S"; || Si el proveedor es de divisa pactada
         |ABRE #agifa323;
         |ABRE #agifa322;
         #agifa322#0 = #0#3;
         #agifa322#2 = #0#56;
         #agifa322#7 = "N";
         |SELECT #2#agifa322;  || Con esta clave ...
         |LEE 011#agifa322.=;  || ... busco periodos no finalizados de esa divisa
         |SI FSalida = 0;  || Si existe un periodo no finalizado
           |PARA div_act = 0; |SI div_act = 0; |HACIENDO;
             #0#56 = #agifa322#2;        || Cargo la divisa ...
             #0#57 = #agifa322#3;        || ...y el cambio
             |LEE 001#agifa322.=;   || Leo las lineas de Proveed/Divisas
             nfecha = #0#1 - #agifa322#6;  || Resto la fecha de fin de periodo a la actual
             |SI nfecha > 0;  || Si la fecha actual supera a la del periodo asignado ...
                |MENAV "0000ATENCION deberia Actualizar primero la divisa en Proveedores/Divisas";
                prov_divisa = #0#3;  || Le envio el cod. de proveedor a "agifa323"
                |SUB_EJECUTA_NP "agifa323.tab";  || Ejecuto Proveedores/Divisas
                |ERROR;
             |SINO;
                div_act = 1;  || Para salir del PARA
             |FINSI;
           |SIGUE;
         |SINO;  || Si no existe un periodo no finalizado para esa divisa
           |MENAV "0000No existe un periodo activo para la divisa en Proveedores / Divisas";
           prov_divisa = #0#3;  || Le paso a "agifa323" el proveedor
           |SUB_EJECUTA_NP "agifa323.tab";  || Ejecuto Proveedores / Divisas
           |ERROR;
           ||#0#56 = #0#61;
           ||#0#57 = #0#62;
         |FINSI;
         |SELECT #1#agifa322;
         |CIERRA #agifa322;
         |CIERRA #agifa323;
      |SINO; || Si el Proveedor no es de Divisa pactada ...
         |ABRE #agifa324; || ...miro directamente en divisas
         #agifa324#0 = #0#56;
         |LEE 001#agifa324.=;
         |SI FSalida = 0; ||Si existe la divisa...
            #0#57 = #agifa324#3; || la recojo
            |SI #agifa324#6 ! -1; || Si los dias de margen de actualizacion no
                                  || son = -1 (indefinidos)
              |PARA div_act = 0; |SI div_act = 0; |HACIENDO;
                 #0#56 = #agifa324#0;        ||Cargo el valor de la divisa
                 #0#57 = #agifa324#3;        ||Cargo el valor del cambio
                 |LEE 001#agifa324.=;  || Leo la divisa
                 nfecha = #0#1 - #agifa324#4;
                 |SI nfecha > #agifa324#6; |Y #agifa324#6 ! -1;  || Si la fecha actual
                    |MENAV "0000ATENCION deberia Actualizar primero la divisa";
                    divisa = #0#56;
                    |SUB_EJECUTA_NP "agifa326.tab";
                    |ERROR;
                 |SINO;
                    div_act = 1;
                 |FINSI;
              |SIGUE;
            |FINSI;
         |FINSI;
         |CIERRA #agifa324;
      |FINSI;
   |SINO;
     #0#56 = #0#61;
     #0#57 = #0#62;
   |FINSI;
   |PINTA #0#56;
   |PINTA #0#57;
 |SI Campo = 3; || Si llamo al proceso desde el cod. de proveedor ...
  |HAZ LeeParamDiv;
 |SINO;
  |HAZ Param_Divisas;
 |FINSI;
|FINSI;
|FPRC;

|PROCESO convportes; |TIPO 6;  || Convierte los portes a la mon. base
|SI #0#58 = "B";  || Si la moneda de trabajo es la moneda base ...
  #0#11 = #0#69;  || ... basta con asignarlo
|SINO;    || Si la moneda de trabajo es la divisa
  #0#11 = #0#69 / #0#57 * #0#62;   || ... lo convierto
|FINSI;
|FPRC;

|PROCESO convvarios; |TIPO 6;  || Convierte los varios a la mon. base
|SI #0#58 = "B";  || Si la moneda de trabajo es la moneda base ...
  #0#13 = #0#70;  || ... basta con asignarlo
|SINO;    || Si la moneda de trabajo es la divisa ...
  #0#13 = #0#70 / #0#57 * #0#62;  || ... lo convierto
|FINSI;
|FPRC;


|PROCESO LeeAlb; |TIPO 13;  || Sirve para cargar los valores cada vez que
                            || cambio de ficha
     |HAZ Defectos77;

     pintado = 0;   || controla pintado de las lineas
     |HAZ LeeMonBase;
     |HAZ LeeParamDiv;
     |ABRE #41; |LEE 000#41p; |CIERRA #41;
     |SI #41#148 = "N";
          |C_V #0#73, 1, "N";
          |C_V #3#42, 1, "N";
     |SINO;
          |C_V #0#73, 1, "S";
          |C_V #3#42, 1, "S";
          |PINTA 1403, "Portes Linea Facturables:";
     |FINSI;
     |HAZ EsDesanch;
     |SI FSalida = 0;
          |HAZ PinDesanch142;
     |FINSI;
|FPRC;

|CALCULO cambfecha;
     |SI FSalida = 0;
          |HAZ EsDesanch;
          |SI FSalida = 0;
               aAlfa = "desan016;DA" + #0#50 + (("000000" + #0#0) % -106);
               |SUB_EJECUTA aAlfa;
               |HAZ PinDesanch142;
          |FINSI;
     |FINSI;
     |SI #0#59 ! "O";
         #0#60 = "        ";
         |PINTA #0#60;
         |C_M #0#60,1,"N";
         |C_V #0#60,1,"N";
     |SINO;
         |C_M #0#60,1,"S";
         |C_V #0#60,1,"S";
     |FINSI;
|FCAL;

|CALCULO modomod; |TIPO 6;
  |SI nModo077 = 2; |Y #0Campo ! Contenido;
    |MENAV "0000No se puede modificar el campo.";
    #0Campo = Contenido;
    |PINTA #0Campo;
    |ERROR;
  |FINSI;
  |SI #0Campo ! Contenido; |Y Campo = 58;
    |HAZ LeeParamDiv;
  |FINSI;
|FCAL;

|PROCESO caltotal; |TIPO 0; ||Proceso especial para casos determinados como
                            ||modificar los campos de cabecera en dtos, cambio divisas etc
                            ||Recalcula total albaran, descuentos y ivas de lineas
                            ||En el caso de cambiar el cambio de divisas recalcula tambien precio... de lineas
     |SI #0Campo ! Contenido; |O nSwPorVar ! 0;||Solo si cambia
          nSwPorVar = 0;
          caltot = 1;
          |SI Campo = 57; ||Si viene del cambio divisa
              recalpre = 1; ||Recalcular tambien precios linea
          |FINSI;
          |HAZ convportes;
          |HAZ convvarios;
          |HAZ actalc;

          |PINTA #0#66;
          caltot = 0;
          recalpre = 0;
     |FINSI;
|FPRC;

|PROCESO DatosEmpresa;   |TIPO 7;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo ! 1; |ACABA; |FINSI;
     |DFICE aDirEmp;            || tomo el directorio de la empresa
     |DFICB aDatos;             || tomo el directorio de los datos
     aDirEmp = aDirEmp % -205;  || me quedo con los 5 digitos de la empresa
     nCodEmp = aDirEmp;         || y los paso a una variable numerica
     |PATH_DAT #11 aDatos;      || pongo el path al fichero agifa041
     |ABRE #11;                 || abre el fichero agifa041
     #11#0 = nCodEmp;
     |LEE 020#11=;              || y busco la empresa en la que estoy
     |SI FSalida = 0;
          #0#29 = #11#1;        || nombre de la empresa
          #0#30 = #11#2;        || direccion de la empresa
          #0#31 = #11#3;        || poblacion de la empresa
          #0#33 = #11#4;        || provincia de la empresa
          |PINTA #0#29;
          |PINTA #0#30;
          |PINTA #0#31;
          |PINTA #0#33;
     |FINSI;
     |CIERRA #11;               || cierro el fichero agifa041
|FPRC;

|PROCESO SuAlbaran; |TIPO 0;
     aAlfa = #0#54; |QBF aAlfa;
     |SI aAlfa = ""; |ACABA; |FINSI;
     |SI #agifa142#173 = "S"; |ACABA; |FINSI;

     aAlfa = #0#54;
     aSerie = #0#50;
     aProv = #0#3;
     nNume = #0#0;
     aA¤o = #0#1 % -104;
     mensa = "";
     |SALVA_FICHA #0;
     |SELECT #4#0;
     |LEE 000#0];
     |PARA; |SI FSalida = 0; |Y #0#3 = aProv; |Y #0#54 = aAlfa; |HACIENDO;
          |SI #0#50 ! aSerie; |O nNume ! #0#0;
               aAlfa = #0#1 % -104;
               |SI aA¤o = aAlfa;
                    aAlfa = ("000000" + #0#0) % -106;
                    mensa = "0000Codigo existente en Deposito:" + #0#50 + "/" + aAlfa;
               |FINSI;
               |SAL;
          |FINSI;
          |LEE 000#0s;
     |SIGUE;
     |SELECT #1#0;
     |REPON_FICHA #0;
     |SI mensa ! "";
          |MENAV mensa;
          |ERROR;
     |FINSI;
|FPRC;

|CALCULO TipoDepo;|TIPO 14;
     |SI #0#26 = 0;
          |PINTA 0577,"DC";
     |SINO;
          |PINTA 0577,"PC";
     |FINSI;
|FCAL;

|PROCESO IVA142_66; |TIPO 66;
     enTiva = #0Campo;
     efFiva = #0#1;
     |HAZ ConsuIVA;
     |SI eaDiva = "";
          |ERROR;
     |SINO;
          #0Campo = enTiva; |PINTA #0Campo;
     |FINSI;
|FPRC;

|PROCESO IVA142_0; |TIPO 0;
     enTiva = #0Campo;
     efFiva = #0#1;
     |HAZ SacaIVA;
     |SI eaDiva = "";
          |MENAV "0000No existe tipo iva para esta fecha...";
          |ERROR; |ACABA;
     |SINO;
          |HAZ caltotal;
     |FINSI;
|FPRC;

|PROCESO PinDesanch142;
     |DDEF aAlfa;
     aAlfa = aAlfa + "desan016";
     |CARGA_DEF aAlfa, referen@;
     |SI FSalida = 0;
          |DDEFECTO #referen@;
          |ABRE #referen@;
          #referen@#0 = "D";
          #referen@#1 = #0#50;
          #referen@#2 = #0#0;
          |LEE 000#referen@.=;
          |CIERRA #referen@;
          |PINTA 1403, "%Dto2:   %Dto3:  ";
          |ATRI I;
          |PINTA 1410, #referen@#3;
          |PINTA 1419, #referen@#4;
          |ATRI 0;
          |DESCARGA_DEF #referen@;
     |FINSI;
|FPRC;
