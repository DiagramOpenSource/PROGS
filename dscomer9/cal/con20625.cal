|FICHEROS;
     con01637 #20;
     con00613 #613,MANTE; || Tmp partes
     con00625 #625;
     con00626 #626,MANTE;
     con00634 #634,MANTE;
     con00637 #637;
     con00642 #642;
     con00633 #633;
     con00696 #696;

     con00680 #680;
     con00681 #681;
|FIN;

|VARIABLES;
     &Tempo = "";
     &eaTParte = "";  || contiene la cadena de los tipos a imprimir
                      || o "TodoS" para que se imprima todo
     &enSwFactu = 0;
     &enTxtGaran = 0;
     &enTxtInter = 0;
     &enTxtNorma = 0;
     &enTxtSinca = 0;
     &enSwCoste  = 0;
     nBtn1 = 0;
     nBtn2 = 0;
     nBtn3 = 0;
     nBtn4 = 0;
     nBtn5 = 0;
     aAlfa = "";
     aMarca = "";
     nSwTallerSC = 0;
|FIN;

|PROCESO LinORFac;
     |DDEFECTO #633;
     #633#0 = #626#10;
     |LEE 000#633=;

     |DDEFECTO #613;
     #613#0 = #626#10;
     #613#1 = #633#1;
     #613#3 = #633#2;
     |LEE 000#613=;
     |SI FSalida ! 0; |GRABA 020#613n; |FINSI;
|FPRC;

|DEFBUCLE LinORFac;
     #626#1;
     36;
     #625#0, #625#1, 0001, 0;
     #625#0, #625#1, 9999, 0;
     ;
     NULL, LinORFac;
|FIN;

|PROCESO LinOR;
     |DDEFECTO #633;
     #633#0 = #626#10;
     |LEE 000#633=;

     |DDEFECTO #613;
     #613#0 = #626#10;
     #613#1 = #633#1;
     #613#3 = #633#2;
     |LEE 000#613=;
     |SI FSalida ! 0; |GRABA 020#613n; |FINSI;
|FPRC;

|DEFBUCLE LinOR;
     #626#1;
     ;
     #625#0, #625#1, 0001;
     #625#0, #625#1, 9999;
     ;
     NULL, LinOR;
|FIN;

|PROCESO LinFacOR;
     #626#0 = #696#3;
     #626#1 = #696#4;
     #626#11 = #696#5;
     |LEE 020#626=;
     |SI FSalida = 0;
          |HAZ LinOR;
     |FINSI;
|FPRC;

|DEFBUCLE LinFacOR;
     #696#1;
     ;
     #642#0, #642#1, #642#2, 0000;
     #642#0, #642#1, #642#2, 9999;
     ;
     NULL, LinFacOR;
|FIN;

|PROCESO Textos;
     |SI #634#4 = "G"; enTxtGaran = 1; |FINSI;
     |SI #634#4 = "I"; enTxtInter = 1; |FINSI;
     |SI #634#4 = "N"; enTxtNorma = 1; |FINSI;
     |SI #634#4 = "S"; enTxtSinca = 1; |FINSI;
|FPRC;

|DEFBUCLE Textos;
     #634#1;
     ;
     #625#0, #625#1, 001;
     #625#0, #625#1, 999;
     ;
     NULL, Textos;
|FIN;

|PROCESO LinFactu;
     #625#0 = #642#3;
     #625#1 = #642#4;
     |BUCLE Textos;

     |ABRE #626;
     |BUCLE LinFacOR;
     |CIERRA #626;
|FPRC;

|DEFBUCLE LinFactu;
     #642#1;
     ;
     #637#0, #637#1, 001;
     #637#0, #637#1, 999;
     ;
     NULL, LinFactu;
|FIN;

|PROCESO Masiva;
     |BUCLE LinFactu;
|FPRC;

|DEFBUCLE MasivaN;
     #637#1;
     2, 12;
     #20#0, #20#2, #20#4, "N";
     #20#1, #20#3, #20#5, "N";
     ;
     NULL, NULL, Masiva;
|FIN;

|DEFBUCLE Masiva;
     #637#1;
     2;
     #20#0, #20#2, #20#4;
     #20#1, #20#3, #20#5;
     ;
     NULL, NULL, Masiva;
|FIN;

|PROCESO SelecTexto;
     |HAZ CreaTempo; |NOME_DAT #613 Tempo; |DELINDEX #613;
     |ABRE #613;
     |ABRE #633;

     enTxtGaran = 0;
     enTxtInter = 0;
     enTxtNorma = 0;
     enTxtSinca = 0;
     |SI enSwFactu = 0;       || Desde la OR
          |BUCLE Textos;
          |BUCLE LinOR;
     |FINSI;
     |SI enSwFactu = 1;       || Desde Factura
          |BUCLE LinFactu;
     |FINSI;
     |SI enSwFactu = 2;       || Desde Imp Factu Masiva
          |SI #20#7 = "S";
               |BUCLE Masiva;
          |SINO;
               |BUCLE MasivaN;
          |FINSI;
     |FINSI;
     |SI enSwFactu = 3;       || Desde la OR a facturar
          |BUCLE LinORFac;
     |FINSI;

     |HAZ EsTallerSC;
     nSwTallerSC = FSalida;

     eaTParte = "TodoS";
     |SI nSwTallerSC = 0;
          |LEE 000#613p;
          |SI FSalida = 0; |HAZ TParte; |FINSI;
     |FINSI;
     |CIERRA #633;
     |CIERRA #613;
     |HAZ BoraTempo; |DELINDEX #613;

     |SI nSwTallerSC = 0; |ACABA; |FINSI;

     |SI enTxtGaran ! 0; |O enTxtInter ! 0; |O enTxtNorma ! 0;
               |O enTxtSinca ! 0;
          |HAZ Texto;
     |FINSI;
|FPRC;

|PROCESO PintaEstado;
     |SI enTxtGaran = 0; |PINTA 2110, "NO"; |SINO; |PINTA 2110, "SI"; |FINSI;
     |SI enTxtInter = 0; |PINTA 2127, "NO"; |SINO; |PINTA 2127, "SI"; |FINSI;
     |SI enTxtNorma = 0; |PINTA 2144, "NO"; |SINO; |PINTA 2144, "SI"; |FINSI;
     |SI enTxtSinca = 0; |PINTA 2161, "NO"; |SINO; |PINTA 2161, "SI"; |FINSI;
|FPRC;

|PROCESO B1;
     enTxtGaran = (enTxtGaran + 1) $ 2; |HAZ PintaEstado;
|FPRC;

|PROCESO B2;
     enTxtInter = (enTxtInter + 1) $ 2; |HAZ PintaEstado;
|FPRC;

|PROCESO B3;
     enTxtNorma = (enTxtNorma + 1) $ 2; |HAZ PintaEstado;
|FPRC;

|PROCESO B4;
     enTxtSinca = (enTxtSinca + 1) $ 2; |HAZ PintaEstado;
|FPRC;

|PROCESO B5;
     |PTEC 806;
|FPRC;

|PROCESO Texto;
     |PUSHV 0501 2380;
     |BLANCO 1701 2380;
     |CUADRO 1701 2380;
     |ATRI I; |PINTA 1825, "SELECCION IMPRESION LINEAS DE TEXTO OR"; |ATRI 0;
     |CUADRO 1902 2279;

     |CONTROL_SIMPLE 0, "BOTON, F5.- Garantia", 2003, 2018, B1; nBtn1 = FSalida;
     |CONTROL_SIMPLE 0, "BOTON, F6.- Interno", 2020, 2035, B2; nBtn2 = FSalida;
     |CONTROL_SIMPLE 0, "BOTON, F7.- Normal", 2037, 2052, B3; nBtn3 = FSalida;
     |CONTROL_SIMPLE 0, "BOTON, F8.- Sin Cargo", 2054, 2069, B4; nBtn4 = FSalida;
     |CONTROL_SIMPLE 0, "BOTON, ESC. Continuar", 2071, 2178, B5; nBtn5 = FSalida;

     |SI enTxtGaran = 0; |ESTADO_CONTROL nBtn1, "DISABLE"; |FINSI;
     |SI enTxtInter = 0; |ESTADO_CONTROL nBtn2, "DISABLE"; |FINSI;
     |SI enTxtNorma = 0; |ESTADO_CONTROL nBtn3, "DISABLE"; |FINSI;
     |SI enTxtSinca = 0; |ESTADO_CONTROL nBtn4, "DISABLE"; |FINSI;

     |HAZ PintaEstado;

     |PARA; |SI; |HACIENDO;
          |LEETECLA aAlfa;
          aAlfa = aAlfa % -103;
          |SI aAlfa = 807; |SAL; |FINSI;
          |SI aAlfa = 806; |SAL; |FINSI;
          |SI aAlfa = 827; |HAZ B1; |FINSI;
          |SI aAlfa = 828; |HAZ B2; |FINSI;
          |SI aAlfa = 829; |HAZ B3; |FINSI;
          |SI aAlfa = 830; |HAZ B4; |FINSI;
     |SIGUE;

     |FIN_CONTROL_WINDOWS nBtn1;
     |FIN_CONTROL_WINDOWS nBtn2;
     |FIN_CONTROL_WINDOWS nBtn3;
     |FIN_CONTROL_WINDOWS nBtn4;
     |FIN_CONTROL_WINDOWS nBtn5;
     |POPV;
|FPRC;
--------------------------------------------------------------
|PROCESO Marca11; |TIPO 11;
     |SI FSalida = 13;
          aMarca = "*";
          |PTEC 807;
     |FINSI;
     |SI FSalida = 14;
          aMarca = " ";
          |PTEC 807;
     |FINSI;
|FPRC;

|PROCESO Marca0; |TIPO 0;
     |SI FSalida = 0; |Y #613Campo = Contenido;
          |SI #613#2 = "*";
               #613#2 = " ";
          |SINO;
               #613#2 = "*";
          |FINSI;
          |GRABA 020#613a;
          |PINTA #613#2;
     |FINSI;
|FPRC;

|PROCESO Marcalo;
     |LEE 000#613p;
     |PARA; |SI FSalida = 0; |HACIENDO;
          #613#2 = aMarca;
          |GRABA 020#613a;
          |LEE 000#613s;
     |SIGUE;
|FPRC;

|PROCESO Max613;
     #613#0 = "zz";
|FPRC;

|PROCESO Min613;
     #613#0 = "  ";
|FPRC;

|PROCESO TParte;
     |PUSHV 0501 2380;
     |BLANCO 509 2373;
     |PINPA #0#613;
     |SI nSwTallerSC = 0; |Y enSwFactu = 3;
          |ATRI I; |PINTA 617, "SELECCION FACTURAR LINEAS SEGUN TIPO DE PARTES "; |ATRI 0;
     |FINSI;
     |PARA aMarca = " "; |SI aMarca ! ""; |HACIENDO;
          aMarca = "";
          |ENTLINEAL #1#613, 1, 4, 21, 0, Min613, Max613;
          |SI aMarca ! ""; |HAZ Marcalo; |FINSI;
     |SIGUE;
     |POPV;

     enTxtGaran = 0;
     enTxtInter = 0;
     enTxtNorma = 0;
     enTxtSinca = 0;
     eaTParte = "";
     |LEE 000#613p;
     |PARA; |SI FSalida = 0; |HACIENDO;
          |SI #613#2 = "*";
               eaTParte = eaTParte + "_" + #613#0;
               |SI #613#3 = "G"; enTxtGaran = 1; |FINSI;
               |SI #613#3 = "I"; enTxtInter = 1; |FINSI;
               |SI #613#3 = "N"; enTxtNorma = 1; |FINSI;
               |SI #613#3 = "S"; enTxtSinca = 1; |FINSI;
          |FINSI;
          |LEE 000#613s;
     |SIGUE;
|FPRC;

|PROCESO SacaCoste;
   |ABRE #con00681;
   #con00681#0 = #con00680#4;
   |LEE 000#con00681.=;
   |SI FSalida ! 0; |DDEFECTO #con00681; |FINSI;
   |CIERRA #con00681;

   enSwCoste = #con00681#3;
|FPRC;
