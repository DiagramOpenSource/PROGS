|FICHEROS;
     agifa321 #1;
     agifa324 #2;
     gosubcoc;
|FIN;
------------------------------------------------------------------------------
|VARIABLES;
     &cCodMonBase   = "";
     &cCodMonTrab   = "";
     &cCodMonDe     = "";
     &cCodMonA      = "";
     &cCodMonConv   = "";
     &cCodMonActu   = "";
     &cDesMonBase   = "";
     &cDesMonTrab   = "";

     &nDeci_STrab   = 0;
     &nDeci_PBase   = 0;
     &nDeci_TBase   = 0;
     &nDeci_PTrab   = 0;
     &nDeci_ITrab   = 0;
     &nDeci_TTrab   = 0;
     &nDeci_TotVis  = 0;
     &nEurComTrab   = 0;
     &nEurComBase   = 0;
     &nEurVenTrab   = 0;
     &nEurVenBase   = 0;
     &nPreComTrab   = 0;
     &nPreComBase   = 0;
     &nPreVenTrab   = 0;
     &nPreVenBase   = 0;
     &nPrecioComp   = 0;
     &nPrecioVent   = 0;

     cParam          = "";

     nModo          = 0;
     nActVenTrab     = 0;
     nActComTrab     = 0;
     nDeVenta       = 0;
     nAVenta        = 0;
     nDeCompra      = 0;
     nACompra       = 0;
|FIN;
------------------------------------------------------------------------------
|PROCESO MonBas;
     nDeci_STrab = #gosubcoc#15;

     |ABRE #1;
     #1#0 = "A";
     |LEE 000#1=;

     |SI FSalida ! 0;
          |CONFI "0000SParametros de divisas inexistentes. Definir?";

          |SI FSalida = 0;
               |PUSHV 01012380;
               |CLS;
               |PINPA #0#1;
               |PINDA #0#1;
               |ENDATOS #1#1;
               |POPV;

               |SI FSalida = 0;
                    |GRABA 020#1c;
               |SINO;
                    |ERROR;
                    |ACABA;
               |FINSI;

          |SINO;
               |ERROR;
               |ACABA;
          |FINSI;

     |FINSI;

     |ABRE #2;
     #2#0 = #1#9;
     |LEE 000#2=;

     |SI FSalida = 0;
          cCodMonBase = #2#0;
          cDesMonBase = #2#1;
          |QBF cCodMonBase;
          |QBF cDesMonBase;
          nDeci_PBase = #2#9;
          nDeci_TBase = #2#7;
          nEurVenBase = #2#2;
          nEurComBase = #2#3;
     |FINSI;

     |QBF cCodMonTrab;

     /* SI NO HEMOS PASADO MONEDA DE TRABAJO SE ASUME LA MONEDA BASE*/

     |SI cCodMonTrab = "";
          cCodMonTrab = #1#9;
     |FINSI;

     #2#0 = cCodMonTrab;
     |LEE 000#2=;

     |SI FSalida = 0;
          cDesMonTrab = #2#1;
          |QBF cDesMonTrab;
          nDeci_PTrab  = #2#9;
          nDeci_TTrab  = #2#7;
          nDeci_ITrab  = #2#7;
          nDeci_TotVis = #2#7;
          nEurVenTrab  = #2#2;
          nEurComTrab  = #2#3;
     |FINSI;

     |CIERRA #2;

     |CIERRA #1;
     |HAZ Conver;
|FPRC;
------------------------------------------------------------------------------
|PROCESO Conver;
     |ABRE #2;
     |QBF cCodMonActu;

     |SI cCodMonActu = "";
          cCodMonActu = cCodMonTrab;
     |FINSI;

     #2#0 = cCodMonActu;
     |LEE 000#2=;

     |SI FSalida = 0;
          nActComTrab = #2#3;
          nActVenTrab = #2#2;
     |FINSI;

     |QBF cCodMonConv;

     |SI cCodMonConv ! "";
          #2#0 = cCodMonConv;
          |LEE 000#2=;

          |SI FSalida = 0;
               nPreComTrab = nPrecioComp / nActComTrab;     ||Euros Doc. Comp. Trab.
               nPreComBase = nPreComTrab * nEurComBase;     ||Prec. Con. Comp. Base.
               nPreComTrab = nPreComTrab * #2#3;            ||Prec. Con. Comp. Trab.
               nPreVenTrab = nPrecioVent  / nActVenTrab;    ||Euros Doc. Vent. Trab.
               nPreVenBase = nPreVenTrab * nEurVenBase;     ||Prec. Con. Vent. Base.
               nPreVenTrab = nPreVenTrab * #2#2;            ||Prec. Con. Vent. Trab.
          |FINSI;

          |CIERRA #2;
          cCodMonConv = "";
          cCodMonActu = "";
     |FINSI;

|FPRC;
------------------------------------------------------------------------------
|PROCESO DemonAmon;

     |ABRE #2;
     #2#0 = cCodMonDe;
     |LEE 000#2=;

     |SI FSalida = 0;
          nDeVenta  = #2#3;                       ||Valor Euro a Venta.
          nDeCompra = #2#2;                       ||Valor Euro a Compra.
     |SINO;
          |MENAV "0000NO EXISTE LA MONEDA";
          |ACABA;
     |FINSI;

     #2#0 = cCodMonA;

     |LEE 000#2=;

     |SI FSalida = 0;
          nAVenta  = #2#3;                        ||Valor Euro a Venta.
          nACompra = #2#2;                        ||Valor Euro a Compra.
     |SINO;
          |MENAV "0000NO EXISTE LA MONEDA";
          |ACABA;
     |FINSI;

     /* precios de compra */

     |SI nPrecioComp ! 0;
          nPrecioComp = nPrecioComp / nDeCompra;  ||Euros Doc. Comp. Trab.
          nPrecioComp = nPrecioComp * nACompra;   ||Prec. Con. Comp. Trab.
     |FINSI;

     /* precios de Venta*/

     |SI nPrecioVent ! 0;
          nPrecioVent = nPrecioVent / nDeVenta;   ||Euros Doc. Comp. Trab.
          nPrecioVent = nPrecioVent * nAVenta;    ||Prec. Con. Comp. Trab.
     |FINSI;

|FPRC;
------------------------------------------------------------------------------
