|FICHEROS;
     vigz0046 #0;  || Pantalla    = piens078
     agifa019 #19;  || Articulos
     agifa017 #17;  || Stock almacenes
     agifa027 #27;
     agifa065 #65;  || Historico
     agifa142 #142;  || Parametros generales
|FIN;

|VARIABLES;
     {-1}aMenu = "";
     aMenu1 = "2400";
     aMenu2 = "1";
     aMenu3 = "Opciones:  ";
     aMenu4 = "RAIC";
     aMenu5 = " Repasar ";
     aMenu6 = " Aceptar ";
     aMenu7 = " Imprimir ";
     aMenu8 = " Cancelar ";
     &Tempo = "";
     aTmp0 = "";
     aImpreso = "N";
     aOpe = "";
     nConta = 0;
     nContaSale = 0;
     nContaEntra = 0;
     aClaConta = "";
     nEntrada = 0;
     nSalida = 0;
     aMes = "";
     nMes = 0;
     nForma = 0;
|FIN;

|PROCESO Actualiza;
     |DDEFECTO #19;
     #19#0 = #0#0;
     |LEE 121#19=;
     |SI FSalida ! 0; |GRABA 020#19b; |FINSI;

     |DDEFECTO #17;
     #17#0 = #0#0;
     #17#1 = #0#1;
     |LEE 121#17=;
     |SI FSalida ! 0; |GRABA 020#17b; |FINSI;

     aMes = #0#5 % 402;
     nMes = aMes - 1;
     nMes = nMes * 2;
     |SI #0#7 = "E";
          aOpe = "FB";
          nConta = nContaEntra;
          nForma = 1;
          nMes = nMes + 12;
          #17#36 = #17#36 + #0#4;
     |SINO;
          aOpe = "SF";
          nConta = nContaSale;
          nForma = -1;
          nMes = nMes + 11;
          #17#35 = #17#35 + #0#4;
     |FINSI;

     #19#6 = #19#6 + (#0#4 * nForma);
     #19#104 = #19#104 + (#0#4 * nForma);

     #17#5 = #17#5 + (#0#4 * nForma);
     #17#39 = #17#39 + (#0#4 * nForma);

     #17nMes = #17nMes + (#0#4 * nForma);

     |HAZ ValoraStock;
     |GRABA 020#19a;
     |LIBERA #19;

     |GRABA 020#17a;
     |LIBERA #17;

     |DDEFECTO #65;
     #65#0 = #0#0;
     #65#1 = #0#5;
     #65#2 = aOpe;
     #65#3 = nConta;
     #65#4 = #0#8;
     #65#5 = #0#1;
     |SI aOpe = "FB";
          #65#6 = #0#4;
     |SINO;
          #65#6 = -#0#4;
     |FINSI;
     #65#7 = #19#6;
     #65#8 = #19#9;
     #65#11 = "";
     |GRABA 020#65n;
|FPRC;

|DEFBUCLE Actualiza;
     #0#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, Actualiza;
|FIN;

|PROCESO Contador;
     |ABRE #27;
     #27#2 = "";
     #27#0 = aClaConta;
     |LEE 101#27=;
     |SI FSalida = 0;
          nConta = #27#1;
          #27#1 = #27#1 + 1;
          |GRABA 020#27a;
     |SINO;
          nConta = 1;
          #27#1 = 2;
          |GRABA 020#27n;
     |FINSI;
     |CIERRA #27;
|FPRC;

|PROCESO Imprime;
     |PRINT;
|FPRC;

|DEFBUCLE Imprime;
     #0#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, Imprime;
|FIN;

|PROCESO Min;
     #0#8 = 001;
|FPRC;

|PROCESO Max;
     #0#8 = 999;
|FPRC;

|PROCESO Imprimir;
     |IMPRE 0;
     |SI FSalida = 0;
          |INFOR "vigz0046";
          |SI FSalida = 0;
               |BUCLE Imprime;
               |PIEINF;
               |FININF;
          |SINO;
               |MENAV "0000Error en informe 'vigz0046'";
          |FINSI;
          |FINIMP;
     |FINSI;
     aImpreso = "S";
|FPRC;

|PROCESO Tipo2; |TIPO 2;
     |SI #0#7 = "E";
          nEntrada = nEntrada +. 1;
     |SINO;
          nSalida = nSalida +. 1;
     |FINSI;
|FPRC;

|PROGRAMA;
     |HAZ CreaTempo; |NOME_DAT #0 Tempo; aTmp0 = Tempo; |DELINDEX #0;

     |CLS;
     |CABEZA "Reg. Existencias";
     |PINPA #0#0;
     |PARA; |SI; |HACIENDO;
          |SI aMenu2 ! 3;
               |ENTLINEAL #1#0, 1, 1, 20, 0, Min, Max;
          |FINSI;
          |MENU aMenu;
          aMenu2 = FSalida;
          |SI aMenu2 = 3;
               |HAZ Imprimir;
          |SINO;
               |SI aMenu2 ! 1; |SAL; |FINSI;
          |FINSI;
     |SIGUE;

     |SI aMenu2 = 2;
          |SI nEntrada > 0;
               aClaConta = "entrada";
               |HAZ Contador;
               nContaEntra = nConta;
          |FINSI;
          |SI nSalida > 0;
               aClaConta = "salida";
               |HAZ Contador;
               nContaSale = nConta;
          |FINSI;

          |ABRE #65;
          |ABRE #17;
          |ABRE #19;
          |BUCLE Actualiza;
          |CIERRA #19;
          |CIERRA #17;
          |CIERRA #65;

          |SI aImpreso = "N";
               |CONFI "0000NDesea Impresion de los Movimientos Realizados:";
          |SINO;
               FSalida = 1;
          |FINSI;
          |SI FSalida = 0;
               |HAZ Imprime;
          |FINSI;
     |FINSI;

     Tempo = aTmp0; |HAZ BoraTempo; |DELINDEX #0;
|FPRO;
