|FICHEROS;
     gofase04 #0;
     gofase05 #1;
     gofase06 #2,MANTE;
     gofase07 #3,MANTE;
     gopre001;
     gofase01 #10;
     gofase10 #20;
     goparame #100;
|FIN;
----------------------------------------------------------------------
|VARIABLES;
     &Tempo = "";
     aTmp20 = "";
     {-1}aUtil = "";
     aUtil1    = "2400";
     aUtil2    = "1";
     aUtil3    = "Ejecucion Partidas : ";
     aUtil4    = "AS";
     aUtil5    = " AUTOMATICA ";
     aUtil6    = " SELECTIVA  ";

     &eaTipo = "";
     &enNum  = 0;
     &eaSer  = "";

     cEstado   = "";
     cActual   = "";
     cEstA     = "";
     cEstB     = "";
     cEstC     = "";
     cCapitulo = "";

     fFecIni   = @;
     fFecFin   = @;
     fFecha    = @;

     nHay      = 0;
     nBotAmpli = 0;
     nBotParal = 0;
     nBotGantt = 0;
     nBotGanttC = 0;
     nCambio   = 0;
     nModo     = 0;
     &aTeclaF = "";
|FIN;
----------------------------------------------------------------------
|PROCESO PinFas04; |TIPO 7;
     |HAZ FasIni04;
|FPRC;
-------------------------------------------------------------------------------
|PROCESO FasIni04; |TIPO 14;
     |ENTLINEAL #1#2, -4, 7, 22, 0, cFasParMin04, cFasParMax04;
|FPRC;
-------------------------------------------------------------------------------
|PROCESO cFasParMin04;
     #2#0 = #1#0;
     #2#1 = #1#1;
     #2#2 = #1#3;
     #2#3 = 1;
|FPRC;
-------------------------------------------------------------------------------
|PROCESO cFasParMax04;
     #2#0 = #1#0;
     #2#1 = #1#1;
     #2#2 = #1#3;
     #2#3 = 999;
|FPRC;
-------------------------------------------------------------------------------
|PROCESO EstPart;

     |SI cEstado = "P"; |Y #2#15 = "E";
          #2#15 = cEstado;
          #2#13 = #1#12;                          ||Fecha de Fin Cap.
     |SINO;
          #2#15 = cEstado;
          #2#12 = #1#11;                          ||Fecha de Inicio Cap.
          #2#13 = #1#12;                          ||Fecha de Fin Cap.
     |FINSI;

     |GRABA 020#2a;
|FPRC;
-------------------------------------------------------------------------------
|DEFBUCLE EstPart;
     #2#1;
     ;                                          ||Campos Extras.
     #1#0, #1#1, #1#3, 1;
     #1#0, #1#1, #1#3, 999;
     ;                                          ||b/e Bolqueo-Ordenacion.
     NULL, EstPart;
|FIN;
-------------------------------------------------------------------------------
|PROCESO CamEst; |TIPO 3;

     |SI nCambio = 0;
          |ACABA;
     |FINSI;

     |SI #1#14 = "E";
          cEstado = "E";
          |BUCLE EstPart;
     |FINSI;

     |SI #1#14 = "F";
          cEstado = "F";
          |BUCLE EstPart;
     |FINSI;

     |SI #1#14 = "P";
          cEstado = "P";
          |BUCLE EstPart;
     |FINSI;


|FPRC;
-------------------------------------------------------------------------------
|PROCESO MiraEjePar;
     nHay = 1;
|FPRC;
-------------------------------------------------------------------------------
|DEFBUCLE MiraEjePar;
     #2#1;
     15;                                          ||Campos Extras.
     #1#0, #1#1, #1#3,   1, "P";
     #1#0, #1#1, #1#3, 999, "P";
     ;                                          ||b/e Bolqueo-Ordenacion.
     NULL, MiraEjePar;
|FIN;
-------------------------------------------------------------------------------
|PROCESO BorraSeg;
     |BORRA 020#3a;
|FPRC;
-------------------------------------------------------------------------------
|DEFBUCLE BorraSeg;
     #3#1;
     ;                                          ||Campos Extras.
     #1#0, #1#1, #1#3,   1;
     #1#0, #1#1, #1#3, 999;
     ;                                          ||b/e Bolqueo-Ordenacion.
     NULL, BorraSeg;
|FIN;
-------------------------------------------------------------------------------
|PROCESO MirEst; |TIPO 0;

     |C_M #1#11, 1, "N";
     |C_M #1#12, 1, "N";

     |SI #1Campo ! Contenido;

          nCambio = 1;

          |SI #1#14 = "E";

               |SI Contenido = "F";
                    |CONFI "0000N¡ATENCION! EL CAPITULO ESTABA FINALIZADO. ¿CONTINUAR?";

                    |SI FSalida ! 0;
                         |ERROR10;
                         |ACABA;
                    |FINSI;

               |FINSI;

               |SI Contenido = "P"; |Y #1#11 ! "01.01.0000";
                    |HAZ HisCam;
                    |ACABA;
               |FINSI;


               |C_M #1#11, 1, "S";
               #1#11 = ~;
               #1#12 = "01.01.0000";
               |PINTA #1#12;

          |FINSI;

          |SI #1#14 = "F";

               |SI Contenido ! "E";
                    |MENAV "0000EL CAPITULO NO ESTA EN EJECUCION";
                    |ERROR10;
                    |ACABA;
               |FINSI;

               nHay = 0;
               |BUCLE MiraEjePar;

               |SI nHay = 1;
                    |MENAV "0000HAY PARTIDAS PARALIZADAS.";
                    |ERROR10;
                    |ACABA;
               |FINSI;

               |C_M #1#12, 1, "S";
               #1#12 = ~;

          |FINSI;

          |SI #1#14 = "P";

               |SI Contenido = "F";
                    |CONFI "0000N¡ATENCION! EL CAPITULO ESTABA FINALIZADO. ¿CONTINUAR?";

                    |SI FSalida ! 0;
                         |ERROR10;
                         |ACABA;
                    |FINSI;

               |FINSI;

               |SI Contenido = "E";
                    #1#12 = "01.01.0000";
                    |HAZ HisCam;
               |SINO;
                    #1#11 = "01.01.0000";
                    #1#12 = "01.01.0000";
                    |BUCLE BorraSeg;
               |FINSI;

               |PINTA #1#12;
               |PINTA #1#11;

          |FINSI;

     |SINO;
          nCambio = 0;
     |FINSI;

|FPRC;
-------------------------------------------------------------------------------
|PROCESO BI004; |TIPO 80;
     |CLS;
     |CONTROL_SIMPLE 0, "BOTON,F2 - Partidas", 2405, 2420, F8004;
     nBotAmpli = FSalida;
     |ESTADO_CONTROL nBotAmpli, "HIDE";           ||"SHOW""HIDE""ENABLE""DISABLE".

     |CONTROL_SIMPLE 0, "BOTON,F3 - Seguimientos", 2423, 2437, F3004;
     nBotParal = FSalida;
     |ESTADO_CONTROL nBotParal, "HIDE";           ||"SHOW""HIDE""ENABLE""DISABLE".

     |CONTROL_SIMPLE 0, "BOTON,F4 - Grafico Gantt", 2440, 2455, F4004;
     nBotGantt = FSalida;
     |ESTADO_CONTROL nBotGantt, "HIDE";           ||"SHOW""HIDE""ENABLE""DISABLE".

     |CONTROL_SIMPLE 0, "BOTON,F5 - Grafico Gantt Comp", 2458, 2475, F5004;
     nBotGanttC = FSalida;
     |ESTADO_CONTROL nBotGanttC, "HIDE";           ||"SHOW""HIDE""ENABLE""DISABLE".
|FPRC;
------------------------------------------------------------------------------
|PROCESO BF004; |TIPO 90;
     |PULSATECLA;
     |FIN_CONTROL_WINDOWS nBotAmpli;
     |PULSATECLA;
     |FIN_CONTROL_WINDOWS nBotParal;
     |PULSATECLA;
     |FIN_CONTROL_WINDOWS nBotGantt;
     |PULSATECLA;
     |FIN_CONTROL_WINDOWS nBotGanttC;
     |PULSATECLA;
|FPRC;
------------------------------------------------------------------------------
|PROCESO BS004; |TIPO 7;
     cCapitulo = #1#3;
     |QBF cCapitulo;

     |SI cCapitulo = ""; |Y #1#2 ! 1;
          |PTEC 808;
     |FINSI;

     |ESTADO_CONTROL nBotAmpli, "SHOW"; ||"SHOW""HIDE""ENABLE""DISABLE".
     |ESTADO_CONTROL nBotParal, "SHOW"; ||"SHOW""HIDE""ENABLE""DISABLE".
     |ESTADO_CONTROL nBotGantt, "SHOW"; ||"SHOW""HIDE""ENABLE""DISABLE".
     |ESTADO_CONTROL nBotGanttC, "SHOW"; ||"SHOW""HIDE""ENABLE""DISABLE".
|FPRC;
-------------------------------------------------------------------------------
|PROCESO BH004; |TIPO 0;
     |ESTADO_CONTROL nBotAmpli, "HIDE";           ||"SHOW""HIDE""ENABLE""DISABLE".
     |ESTADO_CONTROL nBotParal, "HIDE";           ||"SHOW""HIDE""ENABLE""DISABLE".
     |ESTADO_CONTROL nBotGantt, "HIDE";           ||"SHOW""HIDE""ENABLE""DISABLE".
     |ESTADO_CONTROL nBotGanttC, "HIDE";           ||"SHOW""HIDE""ENABLE""DISABLE".
|FPRC;
-------------------------------------------------------------------------------
|PROCESO F8004;
     |PTEC 824;
|FPRC;
-------------------------------------------------------------------------------
|PROCESO F3004;
     |PTEC 825;
|FPRC;

|PROCESO F4004;
    |PTEC 826;
|FPRC;

|PROCESO F5004;
    |PTEC 827;
|FPRC;

-------------------------------------------------------------------------------
|PROCESO MiraCamPar;

     |SI #2#15 = "E";

          |SI #2#12 [ fFecIni;
               fFecIni = #2#12;
          |FINSI;

          cEstA = "E";
     |FINSI;

     |SI #2#15 = "F";

          |SI #2#12 [ fFecIni;
               fFecIni = #2#12;
          |FINSI;

          |SI #2#13 ] fFecFin;
               fFecFin = #2#13;
          |FINSI;

          cEstB = "F";
     |FINSI;

     |SI #2#15 = "P";
          cEstC = "P";
     |FINSI;

|FPRC;
-------------------------------------------------------------------------------
|DEFBUCLE MiraCamPar;
     #2#1;
     ;                                          ||Campos Extras.
     #1#0, #1#1, #1#3,   1;
     #1#0, #1#1, #1#3, 999;
     ;                                          ||b/e Bolqueo-Ordenacion.
     NULL, MiraCamPar;
|FIN;
-------------------------------------------------------------------------------
|PROCESO EntLinPar; |TIPO 11;

     |SI FSalida = 10;
          |HAZ BH004;
          |ENTLINEAL #1#2, -4, 2, 22, 0, cFasParMin04, cFasParMax04;
          cEstA = "";
          cEstB = "";
          cEstC = "";
          fFecIni = "31.12.9999";
          fFecFin = "01.01.0000";
          |BUCLE MiraCamPar;
          cEstado = cEstA+cEstB+cEstC

          /*TODAS LAS PARTIDAS ESTAN FINALIZADAS.*/

          |SI cEstado = "F";
               #1#14 = "F";
               #1#11 = fFecIni;
               #1#12 = fFecFin;
               |GRABA 020#1a;
          |FINSI;

          /*ALGUNA DE LAS PARTIDAS ESTA EN EJECUCION.*/

          |SI cEstado = "E"; |O cEstado = "EF"; |O cEstado = "EFP"; |O cEstado = "EP";

               |SI #1#14 = "P";
                    #1#14 = "E";
                    |HAZ HisCam;
               |FINSI;

               #1#14 = "E";
               #1#11 = fFecIni;
               #1#12 = "01.01.0000";
               |GRABA 020#1a;
          |FINSI;

          /*LAS PARTIZAS SE ENCUENTRA PARALIZADAS O FINALIZADAS.*/

          |SI cEstado = "P"; |O cEstado = "FP";
               |SI #1#14 = "E";
                    #1#12 = "01.01.0000";
                    #1#14 = "P";
                    |HAZ HisCam;
               |SINO;
                    #1#11 = "01.01.0000";
                    #1#12 = "01.01.0000";
                    #1#14 = "P";
               |FINSI;
               |GRABA 020#1a;
          |FINSI;

          |PINTA #1#11;|PINTA #1#12;|PINTA #1#14;
          |HAZ BS004;
          |ERROR;
     |FINSI;

     |SI FSalida = 11;
          ||HAZ BH004;
          |PUSHV 01012380;
          |ENTLINEAL #1#3, -4, 2, 18, 1, MinCap4, MaxCap4;
          |POPV;
          |HAZ BS004;
          |ERROR;
     |FINSI;
     |SI FSalida = 12; |O FSalida = 13;
          |SI FSalida = 13;
               aTeclaF = "5";
          |SINO;
               aTeclaF = "4";
          |FINSI;
          |HAZ BH004;
          ||HAZ Gantt;

          |HAZ ImpreRPT;
          |ERROR;
          |HAZ BS004;
     |FINSI;

     |SI FSalida = 7;
          |HAZ BH004;
     |FINSI;
|FPRC;
-------------------------------------------------------------------------------
|PROCESO MinCap4;
     #3#0 = #1#0;
     #3#1 = #1#1;
     #3#2 = #1#3;
     #3#3 = 1;
|FPRC;
-------------------------------------------------------------------------------
|PROCESO MaxCap4;
     #3#0 = #1#0;
     #3#1 = #1#1;
     #3#2 = #1#3;
     #3#3 = 999;
|FPRC;
-------------------------------------------------------------------------------
|PROCESO HisCam;
     |PUSHV 01012380;
     |DDEFECTO #3;
     |ENTLINEAL #1#3, -4, 1, 18, 1, MinCap4, MaxCap4;
     |POPV;
|FPRC;
-------------------------------------------------------------------------------
|PROGRAMA;
    |HAZ CreaTempo; aTmp20 = Tempo;
     |NOME_DAT #20 aTmp20; |DELINDEX #20;

     |CLS;
     |ABRE #0;
     |PINPA #0#0;
     |PINDA #0#0;
     |ENCLAVE #0,4,0;
     |LEE 000#0=;
     |SI FSalida = 0;
          |PINDA #0#0;
          |ENDATOS #2#0;
          fFecIni = "31.12.9999";
          fFecFin = "01.01.0000";
          |BUCLELIN 2 Cabecera #0;
          |GRABA 020#0a;
     |FINSI;

     |DELINDEX #20;
|FPROG;
----------------------------------------------------------------------
|PROCESO Cabecera;

     |SI #1#11 < fFecIni; |Y #1#11 ! "01.01.0000";
          #0#8 = #1#11;
          fFecIni = #1#11;
     |FINSI;

     |SI #1#12 > fFecFin; |Y #1#12 ! "01.01.0000";
          #0#9 = #1#12;
          fFecFin = #1#12;
     |FINSI;

|FPRC;
-------------------------------------------------------------------------------
|PROCESO MiraFecha; |TIPO 0;

     |SI #1#12 ! "01.01.0000"; |Y #1#12 < #1#11;
          |MENAV "0000LA FECHA FIN, NO PUEDE SER INFERIOR A LA DE INICIO";
          |ERROR10;
     |FINSI;

|FPRC;
-------------------------------------------------------------------------------

|PROCESO ModPresup; |TIPO 6;
   |ABRE #gopre001;
   eaTipo = "F"; eaSer = #gofase04#0; enNum = #gofase04#1; |HAZ ModifPres;
   |SI enNum = -1; |ERROR; |ACABA; |FINSI;
   #gofase04#1 = enNum; |LEE 000#gofase04.=; |PINTA #gofase04#1;
   |CIERRA #gopre001;
|FPRC;

|PROCESO Tipo13; |TIPO 13;
     |ABRE #100; |LEE 000#100p; |CIERRA #100;
     |SI #100#141 = "O";
          |ATRI I; |PINTA 502, "O.E........:"; |ATRI 0;
     |FINSI;
|FPRC;
