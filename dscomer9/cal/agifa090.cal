|FICHEROS;
     agifa090 #0;
     agifa074 #1;
     agifa017 #17;
     agifa019 #19;
     dsm00024 #24;
     agifa029 #29;
     agifa048 #480;
     agifa049 #49;
     agifa142 #142;
     dsm00052 #52,MANTE; || Componentes
     dsm00067 #67; || Cantidad Fabricada por Partida
     dsm00238 #238;
     dsm00279 #279;
     dsm00010 #900;

     refere1@;
     gomantl1@;
     gopre025@;

     coima003 #300;
|FIN;

|INCLUYE i_varart;

|VARIABLES;
 {-1}aMenu = "";
     aMenu1 = "2400";
     aMenu2 = "1";
     aMenu3 = "Opciones:  ";
     aMenu4 = "ID";
     aMenu5 = " Imprimir ";
     aMenu6 = " Desglosar por familias ";
     nSwCoimasa = 0;
     nSw = 0;
     aPro = "";
     aNom = "";
     nSwPin = 0;
     &eaSerExpe = "";
     &enNumExpe = 0;
     &eaArt00con = "";
     &enUni00con = 0;
     aDesde = "                    ";
     aHasta = "zzzzzzzzzzzzzzzzzzzz";
     nHay = 0;
     nUni = 0;
     aPath = "";
     nUniAnt = 0;
     aArtAnt = "";
     aUsu = "";
     nContaLog = 0;
     aSer = "";
     aNum = "";
     aTipo = "";
     nTipo = 0;
     nFaltaStock = 0;
     aAlfa      = "";
     &Tempo     = "";
     &nDeci_Uni= 0;           || Decimales Unidades
     nTecla         = 0;

     &enOrden  = 0;
     &eaReimp  = "";
     &eaCompo  = "";

     informe   =  "agifa090" ;
 {-1}menu      = "";
     menu1     = "2400";
     menu2     = "1";
     menu3     = " Imprimir Componentes: ";
     menu4     = "SN";
     menu5     = " NO ";
     menu6     = " SI ";

     nForma    = 0;
     nModo     = 0;
     nSwErr    = 0;
     nSubLin   = 0;
     nFactor   = 0;
     nPartiBlan = 0;
     nNume = 0;
     fFechaPrev = @;
     nBtnCoimaDesglosa = 0;
|FIN;

|INCLUYE i_escank;

|PROCESO BtnCoimaDesglosa
     aAlfa = #0#20; |QBF aAlfa;
     |SI aAlfa ! "";
          |MENAV "0000Solo se puede desglosar la serie vacia.";
     |SINO;
          |SI #0#10 = "S";
               |MENAV "0000Orden finalizada.";
          |SINO;
               |LEE 000#0=;
               |SI FSalida = 0;
                    aAlfa = "coimz043;" + #0#0;
                    |PUSHV 0501 2380;
                    |SUB_EJECUTA_NP aAlfa;
                    |POPV;
               |SINO;
                    |MENAV "0000No existe orden de fabricacion.";
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Tipo90_8; |TIPO 8;
     |ABRE #142; |LEE 000#142p; |CIERRA #142;
     nDeci_Uni = #142#8;

     |SI #142#12 ! "N"; |Y #142#12 ! "X";
          |MENAV "0000Opcion no configurada en Parametros Generales";
          |PTEC 807;
     |FINSI;
|FPRC;

|PROCESO Miralin;
     |SI #1#5 ! 0; nSwErr = 1; |FINSI;
|FPRC;

|PROCESO Tipo90_2; |TIPO 2;
     nModo = (FEntrada / 100) ! 0;
     nForma = 0 +. 1;

     |SI #0#21 = "S";
          |SI nForma = -1;
               |MENAV "0000Orden Exportada, no se puede modificar...";
          |FINSI;
          |ERROR; |ACABA;
     |FINSI;

     |SI nForma = -1; fFechaPrev = #0#2; |FINSI;

     |SI nModo ! 1;
          |SI #0#10 = "S";
               |SI nForma = -1; |MENAV "0000Orden finalizada..."; |FINSI;
               |ERROR; |ACABA;
          |SINO;
               |SI nModo = 3;
                    nSwErr = 0;
                    |BUCLELIN 2 Miralin #0;
                    |SI nSwErr ! 0;
                         |SI nForma = -1; |MENAV "0000Orden en curso..."; |FINSI;
                         |ERROR; |ACABA;
                    |FINSI;
               |FINSI;
          |FINSI;
     |FINSI;

     |SI nModo = 3;
          |HAZ EsGestral;
          |SI FSalida = 0;
               eaSerie = #0#20;
               enOrden = #0#0;
               |SUB_EJECUTA_NP "gestrm11;BAJA";
          |FINSI;
     |FINSI;

     |SI nModo = 2; |Y #0#2 ! fFechaPrev;
          |HAZ EsCoimasa;
          |SI FSalida = 0;
               |HAZ CoimaFecPrev;
          |FINSI;
     |FINSI;
|FPRC;
컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
|PROCESO LeeCompo;
     |DDEFECTO #19;
     #19#0 = aEscan;
     |LEE 101#19=;
     |SI FSalida ! 0; |GRABA 020#19b; |FINSI;
     |DDEFECTO #17;
     #17#0 = aEscan;
     #17#1 = nAlmaEscan;
     |LEE 101#17=;
     |SI FSalida ! 0; |GRABA 020#17b; |FINSI;

     |DDEFECTO #24;
     |SI nSw = 1;
          |DDEFECTO #52;
     |FINSI;

     |SI #19#180 = "S"; |Y #142#118 = "S";
          aAlfa = #52#8; |QBF aAlfa;
          |DDEFECTO #24;
          #24#0 = aEscan;
          #24#1 = nAlmaEscan;
          #24#2 = #52#8;
          |LEE 101#24=;
          |SI FSalida ! 0
               |SI nSw = 0;
                    |SI FSalida ! 0; |GRABA 020#24b; |FINSI;
               |SINO;
                    |SI aAlfa = "";     || busca la 1
                         |LEE 000#24];
                         |SI FSalida = 0; |Y #24#0 = aEscan; |Y #24#1 = nAlmaEscan;

                         |SINO;
                              |DDEFECTO #24;
                              #24#0 = aEscan;
                              #24#1 = nAlmaEscan;
                              #24#2 = #52#8;
                             |SI FSalida ! 0; |GRABA 020#24b; |FINSI;
                         |FINSI;
                    |SINO;
                         |SI FSalida ! 0; |GRABA 020#24b; |FINSI;
                    |FINSI;
               |FINSI;
          |FINSI;
     |FINSI;

     nFactor = 0;
     |SI #19#179 = "S";
          |ABRE #238;
          |DDEFECTO #238;
          #238#0 = #19#201;
          |LEE 000#238=;
          |CIERRA #238;
          nFactor = 1;
          |SI #238#8 ] 1; nFactor = #238#9; |FINSI;
          |SI #238#8 ] 2; nFactor = nFactor * #238#10; |FINSI;
          |SI #238#8 = 3; nFactor = nFactor * #238#11; |FINSI;
     |FINSI;
|FPRC;

|PROCESO ActCompo;
     #19#12 = #19#12 + (#52#4 * nForma);
     #19#104 = #19#104 - (#52#4 * nForma);
     #19#193 = #19#193 - (#52#9 * nForma);

     #17#8 = #17#8 + (#52#4 * nForma);
     #17#39 = #17#39 - (#52#4 * nForma);
     #17#48 = #17#48 - (#52#9 * nForma);

     |HAZ ValoraStock;
     |GRABA 020#17a;
     |GRABA 020#19a;
     |SI #19#180 = "S";
          |GRABA 020#24a;
          |LIBERA #24;
     |FINSI;
     |LIBERA #17;
     |LIBERA #19;
|FPRC;

|PROCESO LinKit;
     |SI nSwCoimasa = 0;
          |SI #49#10 ! 1; |ACABA; |FINSI;
     |FINSI;


     |ABRE #279; |LEE 000#279p; |CIERRA #279;
     nSw = 1;
     |HAZ LeeCompo;
     nSw = 0;

     nSubLin = nSubLin + 1;
     |DDEFECTO #52;
     #52#6 = #1#9;
     #52#0 = #1#0;
     #52#1 = #1#1;
     #52#2 = nSubLin;
     #52#3 = aEscan;
     #52#4 = nCantiEscan;
     #52#5 = #49#9;
     #52#8 = #24#2;
     ||#52#9 = #52#4 * nFactor;
     #52#9 = (#52#4 * nFactor) / #49#4 * #49#17;
     #52#11 = #49#0;
     #52#12 = #49#1;

     |SI #279#18 = "S";
          #52#4 = #52#4 / #480#12;
          #52#9 = #52#9 / #480#12;
     |FINSI;

     |GRABA 020#52n;

     |HAZ ActCompo;

     |SI #17#39 < 0;
          |SI #142#223 = "S"; |O #142#223 = "P";
               aAlfa = "0000Falta Stock: " + #19#0 + &13 + "Unidades:" + #17#39;
               |MENAV aAlfa;
          |FINSI;
          |SI #142#223 = "P";
               nFaltaStock = 1;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO T90_4; |TIPO 4;
     |SELECT #1#0;
     |LEE 000#0=;
     |SI FSalida ! 0;
          |MENAV "0000Por favor, consulte el registro correcto antes de imprimir.";
          |ACABA;
     |SINO;
          |PINDA #0#0;
          FEntrada = 402;
     |FINSI;
     |HAZ Tipo90_4;
|FPRC;

|PROCESO Tipo90_4;|TIPO 4;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 1; |O nModo = 2;
          |GRABA 020#0a;
          |LIBERA #0;
     |FINSI;

     ||컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
     eaSerie = "";
     |HAZ EsBerria;
     |SI FSalida = 0;
          eaSerie  = #0#20;
          enOrden  = #0#0;
          eaReimp  = #0#9;
          |CIERRAT #0;
          |SUB_EJECUTA "berz0006";
          |ABRET #0;
     |FINSI;
     |HAZ EsDefelino;
     |SI FSalida = 0;
          eaSerie  = #0#20;
          enOrden  = #0#0;
          eaReimp  = #0#9;
          |CIERRAT #0;
          |SUB_EJECUTA "defelz02";
          |ABRET #0;
     |FINSI;
     |HAZ EsGestral;
     |SI FSalida = 0;
          eaSerie  = #0#20;
          enOrden  = #0#0;
          |CIERRAT #0;
          |SUB_EJECUTA "gestrw01";
          |ABRET #0;
          |SI #0#10 = "N"; eaSerie = ""; |FINSI;
     |FINSI;

     |SI eaSerie = "";   || la estandar
          |MENU menu;
          |SI FSalida > 0;
               |SI FSalida = 2;
                    eaCompo  = "S";
               |SINO;
                    eaCompo  = "N";
               |FINSI;
               eaSerie  = #0#20;
               enOrden  = #0#0;
               eaReimp  = #0#9;
               |CIERRAT #0;
               |SUB_EJECUTA "agifa137";
               |ABRET #0;
          |FINSI;
     |FINSI;
     ||컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
     #0#0 = enOrden;
     #0#20 = eaSerie;
     |SI nModo = 4;
          |LEE 000#0=;
     |SINO;
          |LEE 101#0=;
     |FINSI;
     |PINTA #0#9;
|FPRC;
컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
|PROCESO Tipo90_3;|TIPO 3;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo ! 4;
          |SI nModo = 2; |Y #0#10 = "S";
               |HAZ EsGestral;
               |SI FSalida = 0;
                    |ACABA;
               |FINSI;
          |FINSI;

          |CONFI "0000SImprimir Orden...";
          |SI FSalida = 0;
              |HAZ Tipo90_4;
          |FINSI;
     |FINSI;
|FPRC;
컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
|PROCESO Borra67;
     |BORRA 020#67a;
|FPRC;

|DEFBUCLE Borra67;
     #67#1;
     ;
     #1#9, #1#0, #1#1, "                              ";
     #1#9, #1#0, #1#1, "zzzzzzzzzzzzzzzzzzzzzzzzzzzzzz";
     be;
     NULL, Borra67;
|FIN;

|PROCESO Borra52;
     aEscan = #52#3;
     nAlmaEscan = #52#5;
     |HAZ LeeCompo;
     |HAZ ActCompo;
     |BORRA 020#52a;
|FPRC;

|DEFBUCLE Borra52;
     #52#1;
     ;
     #1#9, #1#0, #1#1, 001;
     #1#9, #1#0, #1#1, 999;
     be;
     NULL, Borra52;
|FIN;

|PROCESO MiraPartiBlan;
     aAlfa = #52#8; |QBF aAlfa;
     |SI aAlfa = "";
          nPartiBlan = 1;
          |ERROR10;
     |FINSI;
|FPRC;

|DEFBUCLE MiraPartiBlan;
     #52#1;
     ;
     #1#9, #1#0, #1#1, 001;
     #1#9, #1#0, #1#1, 999;
     ;
     NULL, MiraPartiBlan;
|FIN;

|PROCESO Tipo74_2; |TIPO 2;
     nForma = 0 +. 1;
     nModo = (FEntrada / 100) ! 0;
     |SI #1#5 ! 0;
          |SI nForma = -1;
               |MENAV "0000Linea orden con unidades fabricadas...";
          |FINSI;
          |ERROR;
     |FINSI;
     |ABRE #17;
     |ABRE #19;
     |ABRE #24;
     |DDEFECTO #19;
     #19#0 = #1#2;
     |LEE 101#19=;
     |SI FSalida ! 0; |GRABA 020#19b; |FINSI;
     |DDEFECTO #17;
     #17#0 = #1#2;
     #17#1 = #1#7;
     |LEE 101#17=;
     |SI FSalida ! 0; |GRABA 020#17b; |FINSI;

     #19#13 = #19#13 +. #1#4;
     #17#9 = #17#9 +. #1#4;
     |HAZ ValoraStock;
     |GRABA 020#17a;
     |GRABA 020#19a;
     |LIBERA #17;
     |LIBERA #19;

     |SI nModo = 1; |HAZ CreaCompo; |FINSI;

     |SI #agifa142#118 = "S"; |Y nForma = 1; |Y nModo < 3;
          |PUSHV 0501 2380;
          |PARA; |SI; |HACIENDO;
               |ENTLINEAL #1#52, -4, nModo, 20, 1, Min52, Max52;
               |SI #142#243 = "N";
                    nPartiBlan = 0;
                    |BUCLE MiraPartiBlan;
                    |SI nPartiBlan = 0;
                         |SAL;
                    |SINO;
                         |MENAV "0000Debe de indicar la partida de los componentes...";
                    |FINSI;
               |SINO;
                    |SAL;
               |FINSI;
          |SIGUE;
          |POPV;
     |FINSI;
     #1#11 = #1#4; || Unidades Originales
     #1#15 = #1#13;

     |SI nModo = 3;
          |BUCLE Borra52;
          |BUCLE Borra67;
          eaTipo      = "T5";
          eaSerie     = #1#9;
          enCodigo    = #1#0;
          enLinea     = #1#1;
          eaArticulo  = #1#2;
          enAlmacen   = #1#7;
          enUnidades  = #1#4;
          efFecha     = #0#1;
          |SUB_EJECUTA_NP "dsz00002;BAJA";
          |HAZ EsDefelino;
          |SI FSalida = 0;
               |HAZ BorDefelino;
          |FINSI;
     |FINSI;
     |CIERRA #19;
     |CIERRA #17;
     |CIERRA #24;

     |HAZ EsCoimasa;
     |SI FSalida = 0;
          |HAZ CoimaHisto;
     |FINSI;
|FPRC;

|PROCESO Tipo74_5; |TIPO 5;
     FSalida = "SINBARRA";
     |SI eaArt00con ! "";     || Lo llama el guerz080 del modulo 000con
          |HAZ CreaLin00con;
     |FINSI;
|FPRC;

|PROCESO Min52;
     #52#6 = #1#9;
     #52#0 = #1#0;
     #52#1 = #1#1;
     #52#2 = 1;
|FPRC;

|PROCESO Max52;
     #52#6 = #1#9;
     #52#0 = #1#0;
     #52#1 = #1#1;
     #52#2 = 999;
|FPRC;

|PROCESO Min67;
     #67#0 = #1#9;
     #67#1 = #1#0;
     #67#2 = #1#1;
     #67#3 = " " * 15;
|FPRC;

|PROCESO Max67;
     #67#0 = #1#9;
     #67#1 = #1#0;
     #67#2 = #1#1;
     #67#3 = "z" * 15;
|FPRC;

|PROCESO Compo0; |TIPO 6;
     |HAZ Compo13;
|FPRC;

|PROCESO MinPar;
     #24#0 = #52#3;
     #24#1 = #52#5;
     #24#2 = " " * 30;
|FPRC;

|PROCESO MaxPar;
     #24#0 = #52#3;
     #24#1 = #52#5;
     #24#2 = "z" * 30;
|FPRC;

|PROCESO Parti6; |TIPO 6;
     |SI FSalida = 18; ||F10
          |ABRE #24;
          #24#0 = #52#3;
          #24#1 = #52#5;
          #24#2 = #52#8;
          |LEE 000#24];
          |CONSULTA_F_CLAVE #1#24, MinPar, MaxPar;
          |SI FSalida = 0;
               #52#8 = #24#2; |PINTA #52#8;
          |SINO;
               |ERROR;
          |FINSI;
          |CIERRA #24;
     |FINSI;
     |SI FSalida = 9; ||F2;
          eaArticulo = #52#3;
          enAlmacen = #52#5;
          efFecha = #0#1;
          |HAZ ConsultaPartida;
          |SI eaPartida = "";
               |ERROR;
          |SINO;
               #52#8 = eaPartida; |PINTA #52#8;
          |FINSI;
     |FINSI;

     aAlfa = #52#8; |QBF aAlfa;
     |SI #19#180 = "S"; |Y #142#243 = "N"; |Y aAlfa = "";
          |AVISO; |ERROR;
     |FINSI;

     |SI aAlfa ! "";
          |ABRE #24;
          #24#0 = #52#3;
          #24#1 = #52#5;
          #24#2 = #52#8;
          |LEE 000#24=;
          |SI FSalida ! 0;
               |MENAV "0000No existe partida...";
               |ERROR;
          |FINSI;
          |CIERRA #24;
     |FINSI;
|FPRC;

|PROCESO Compo13; |TIPO 13;
     |ABRE #19;
     #19#0 = #52#3;
     |LEE 000#19=;
     |CIERRA #19;
     |ABRE #238;
     #238#0 = #19#201;
     |LEE 000#238=;
     |CIERRA #238;

     aAlfa = #19#179;
     |C_V #52#9, 1, aAlfa;
     |C_V #52#10, 1, aAlfa;
     aAlfa = #19#180;
     |C_M #52#8, 1, aAlfa;

     aAlfa = #238#4; |QBF aAlfa;
     |SI aAlfa ! "";
          |PINTA 1662, #238#5;
          |PINTA 1962, #238#5;
          |SI #19#179 = "S";
               |PINTA 1762, #238#4;
               |PINTA 2062, #238#4;
          |SINO;
               |PINTA 1755, "                        ";
               |PINTA 2055, "                        ";
          |FINSI;
     |SINO;
          |PINTA 1662, "...";
          |PINTA 1962, "...";
          |PINTA 1755, "                        ";
          |PINTA 2055, "                        ";
     |FINSI;
     |PINTA 2217, #19#1;
|FPRC;

|PROCESO MiraDesglose; |TIPO 11;
     nModo = (FEntrada / 100) ! 0;
     nTecla = FSalida;
     |SI nTecla = 10;
          |PUSHV 0501 2380;
          |ENTLINEAL #1#52, -4, nModo, 20, 1, Min52, Max52;
          |POPV;
          |ERROR; |ACABA;
     |FINSI;
     |SI nTecla = 11;
          |PUSHV 0501 2380;
          |ENTLINEAL #1#67, -4, 4, 22, 1, Min67, Max67;
          |POPV;
          |ERROR; |ACABA;
     |FINSI;
     |SI nTecla = 12;
          |SI nModo = 1;
               |HAZ EsDefelino;
               |SI FSalida = 0;
                    aAlfa = "defelz01;" + #0#20 + (("000000" + #0#0) % -106);
                    |SUB_EJECUTA_NP aAlfa;
                    |PTEC 816;
                    |PTEC 812;
               |FINSI;
          |SINO;
               |MENAV "0000Opcion solo disponible en alta...";
          |FINSI;
          |ERROR; |ACABA;
     |FINSI;
     |SI nTecla = 13;
          |HAZ VerDobleUni;
          |ERROR; |ACABA;
     |FINSI;
     |ABRE #900; |LEE 000#900p; |CIERRA #900;
     |SI nTecla = 13; |Y #900#1 = "S";
          eaTipo      = "T55";
          eaSerie     = #1#9;
          enCodigo    = #1#0;
          enLinea     = #1#1;
          eaArticulo  = #1#2;
          enAlmacen   = #1#7;
          enUnidades  = #1#4;
          efFecha     = #0#1;
          |SUB_EJECUTA_NP "dsz00002";
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO Partida7; |TIPO 7;
     |ABRE #19;
     #19#0 = #1#2;
     |LEE 000#19=;
     |CIERRA #19;
     aAlfa = #19#180;
     |C_M #1#10, 1, aAlfa;
|FPRC;

|PROCESO Partida0; |TIPO 0;
     aAlfa = #1#10; |QBF aAlfa;
     |SI aAlfa = ""; |ACABA; |FINSI;

     |SI #142#122 = "S";
          aAlfa = #142#121 * 30;
          nNume = -(#142#120 + 100);
          aAlfa = (aAlfa + #1#10); |QBF aAlfa;
          aAlfa = aAlfa % nNume;
          #1#10 = aAlfa; |PINTA #1#10;
     |FINSI;

     |ABRE #24;
     #24#0 = #1#2;
     #24#1 = #1#7;
     #24#2 = #1#10;
     |LEE 000#24=;
     |SI FSalida ! 0;
          |MENAV "0000No existe partida...";
          |ERROR;
     |FINSI;
     |CIERRA #24;
|FPRC;

|PROCESO MinPar1;
     #24#0 = #1#2;
     #24#1 = #1#7;
     #24#2 = " " * 30;
|FPRC;

|PROCESO MaxPar1;
     #24#0 = #1#2;
     #24#1 = #1#7;
     #24#2 = "z" * 30;
|FPRC;

|PROCESO Partida6; |TIPO 6;
     |SI FSalida = 18; ||F10
          |ABRE #24;
          #24#0 = #1#2;
          #24#1 = #1#7;
          #24#2 = #1#10;
          |LEE 000#24];
          |CONSULTA_F_CLAVE #1#24, MinPar1, MaxPar1;
          |SI FSalida = 0;
               #1#10= #24#2; |PINTA #1#10;
          |SINO;
               |ERROR;
          |FINSI;
          |CIERRA #24;
     |FINSI;
     |SI FSalida = 9; ||F2;
          eaArticulo = #1#2;
          enAlmacen = #1#7;
          efFecha = #0#1;
          |HAZ ConsultaPartida;
          |SI eaPartida = "";
               |ERROR;
          |SINO;
               #1#10 = eaPartida; |PINTA #1#10;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Llena074; |TIPO 6;
     enAlta = 1;
     eaArticulo = #1Campo;
     |HAZ LlenaArticulo;
     |SI FSalida = 0;
          #1Campo = eaArticulo;
          |PINTA #1Campo;

          |ABRE #19;
          #19#0 = #1#2;
          |LEE 000#19=;
          |SI FSalida ! 0; |DDEFECTO #19; |FINSI;
          |CIERRA #19;

          |SI #19#176 = "S";
               eaTipo      = "T5";
               eaSerie     = #1#9;
               enCodigo    = #1#0;
               enLinea     = #1#1;
               eaArticulo  = #1#2;
               enAlmacen   = #1#7;
               enUnidades  = #1#4;
               efFecha     = #0#1;
               |SUB_EJECUTA_NP "dsz00002";
               #1#7 = enAlmacen; |PINTA #1#7;
               #1#4 = enUnidades; |PINTA #1#4;
               |C_M #1#7, 1, "N";
               |C_M #1#4, 1, "N";
          |SINO;
               |C_M #1#7, 1, "S";
               |C_M #1#4, 1, "S";
          |FINSI;
     |SINO;
          |ERROR; |ACABA;
     |FINSI;
|FPRC;

|PROCESO Guarda; |TIPO 7;
     nUniAnt = #1#4;
     aArtAnt = #1#2;
|FPRC;

|PROCESO TipoEscan; |TIPO 0;
     nModo = (FEntrada / 100) ! 0;
     |ABRE #142; |LEE 000#142p; |CIERRA #142;
     |SI #142#12 = "X"; |Y nModo = 1;
          |ABRE #480;
          #480#0 = #1#2;
          |LEE 000#480=;
          |CIERRA #480;

          |SI #480#33 = "K";
               |MENAV "0000El escandallo es un KIT";
               |ERROR; |ACABA;
          |FINSI;
     |FINSI;
     |SI #1#2 ! aArtAnt; |Y nModo = 2;
          |ABRE #17;
          |ABRE #19;
          |HAZ BorraCompo;
          nFaltaStock = 0;
          |HAZ CreaCompo;
          |SI nFaltaStock ! 0;
               |HAZ BorraCompo;
               #1#2 = aArtAnt;
               |HAZ CreaCompo;
               |ERROR; |ACABA;
          |FINSI;
          |CIERRA #19;
          |CIERRA #17;
     |FINSI;
|FPRC;

|PROCESO Fabricar; |TIPO 0;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 1;
          |ABRE #17;
          |ABRE #19;
          |ABRE #24;
          nFaltaStock = 0;
          |HAZ CreaCompo;
          |HAZ BorraCompo;
          |SI nFaltaStock ! 0; |Y #142#223 = "P";
               |ERROR;
          |FINSI;
          |CIERRA #24;
          |CIERRA #19;
          |CIERRA #17;
     |FINSI;
     |SI #1#4 ! nUniAnt; |Y nModo = 2;
          |ABRE #17;
          |ABRE #19;
          |ABRE #24;
          |HAZ BorraCompo;
          nFaltaStock = 0;
          |HAZ CreaCompo;
          |SI nFaltaStock ! 0;
               |HAZ BorraCompo;
               #1#4 = nUniAnt;
               |HAZ CreaCompo;
               |ERROR; |ACABA;
          |FINSI;
          |CIERRA #24;
          |CIERRA #19;
          |CIERRA #17;
     |FINSI;
|FPRC;

|PROCESO Compo2; |TIPO 2;
     |SI #52#7 ! 0; |O #52#10 ! 0;
          |MENAV "0000No se puede modificar, hay entradas...";
          |ERROR;
     |SINO;
          nForma = 0 +. 1;
          |ABRE #19;
          |ABRE #17;
          |ABRE #24;
          aEscan = #52#3;
          nAlmaEscan = #52#5;
          |HAZ LeeCompo;
          |HAZ ActCompo;
          |CIERRA #24;
          |CIERRA #17;
          |CIERRA #19;
     |FINSI;
|FPRC;

|PROCESO StockCompo; |TIPO 0;
     |SI #142#223 = "N"; |ACABA; |FINSI;
     |ABRE #19;
     #19#0 = #52#3;
     |LEE 000#19=;
     |CIERRA #19;
     |SI #19#180 = "N";
          |ABRE #17;
          #17#0 = #52#3;
          #17#1 = #52#5;
          |LEE 000#17=;
          |CIERRA #17;
          |SI #17#39 < #52#4;
               |SI #142#223 = "S"; |O #142#223 = "P";
                    aAlfa = "0000Falta Stock, unidades:" + #17#39;
                    |MENAV aAlfa;
                    |SI #142#223 = "P"; |ERROR; |FINSI;
               |FINSI;
          |FINSI;
     |SINO;
          |ABRE #24;
          #24#0 = #52#3;
          #24#1 = #52#5;
          #24#2 = #52#8;
          |LEE 000#24=;
          |CIERRA #24;
          |SI #24#31 < #52#4;
               |SI #142#223 = "S"; |O #142#223 = "P";
                    aAlfa = "0000Falta Stock, unidades:" + #24#31;
                    |MENAV aAlfa;
                    |SI #142#223 = "P"; |ERROR; |FINSI;
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO T090_15; |TIPO 15;
     nTipo = 15; |HAZ Log090;
|FPRC;

|PROCESO T090_16; |TIPO 16;
     nTipo = 16; |HAZ Log090;
|FPRC;

|PROCESO T090_17; |TIPO 17;
     nTipo = 17; |HAZ Log090;
|FPRC;

|PROCESO Log090;
     |DDEF aAlfa;
     aAlfa = aAlfa + "dsm00087";
     |CARGA_DEF aAlfa, refere1@;
     |SI FSalida ! 0; |ACABA; |FINSI;

     aSer = #0#20;
     aNum = ("000000" + #0#0) % -106;
     aTipo = "OR";

     |ABRE #refere1@;
     |DDEFECTO #refere1@;
     |SELECT #2#refere1@;
     #refere1@#1 = aSer;
     #refere1@#2 = aNum;
     #refere1@#3 = aTipo;
     |LEE 101#refere1@.=;
     |SI FSalida ! 0;
          |SELECT #1#refere1@;
          |LEE 000#refere1@.u;
          |SI FSalida = 0;
               nContaLog = #refere1@#0 + 1;
          |SINO;
               nContaLog = 1;
          |FINSI;
          |DDEFECTO #refere1@;
          #refere1@#0 = nContaLog;
          #refere1@#1 = aSer;
          #refere1@#2 = aNum;
          #refere1@#3 = aTipo;
          |GRABA 020#refere1@.b;
     |FINSI;
     |SI FSalida = 0;
          |HORA aAlfa;
          aUsu = Usuario % 810;
          |SI nTipo = 15;
               #refere1@#4 = aUsu;
               #refere1@#5 = ~;
               #refere1@#6 = aAlfa;
               #refere1@#7 = "";
               #refere1@#8 = "01.01.0000";
               #refere1@#9 = ""
               #refere1@#10 = "";
               #refere1@#11 = "01.01.0000";
               #refere1@#12 = ""
          |FINSI;
          |SI nTipo = 16;
               #refere1@#7 = aUsu;
               #refere1@#8 = ~;
               #refere1@#9 = aAlfa;
          |FINSI;
          |SI nTipo = 17;
               #refere1@#10 = aUsu;
               #refere1@#11 = ~;
               #refere1@#12 = aAlfa;
          |FINSI;
          |GRABA 020#refere1@.a;
     |FINSI;
     |CIERRA #refere1@;
     |DESCARGA_DEF #refere1@;
|FPRC;
---------------------
|PROCESO BorraCompo;
     nForma = -1;
     |BUCLE Borra52;
     |BUCLE Borra67;
|FPRC;

|PROCESO CreaCompo;
     |SI eaArt00con ! "";     || Lo llama el guerz080 del modulo 000con
          |HAZ CreaCompo00con;
          |ACABA;
     |FINSI;

     |HAZ EsCoimasa; nSwCoimasa = FSalida;

     nFaltaStock = 0;
     nForma = 1;
     |ABRE #52;
     aEscan = #1#2;
     nCantiEscan = #1#4;
     nSubLin = 0;
     |SI #142#159 = "N";
          nMaxNivel = 1;
     |FINSI;
     enSwResta = 1;
     |HAZ DesglosaKit;
     |CIERRA #52;
|FPRC;

|PROCESO CreaLin00con;   || Del modulo 000con
     |ABRE #480;
     #480#0 = eaArt00con;
     |LEE 020#480=;
     |CIERRA #480;

     #1#9 = #0#20;
     #1#0 = #0#0;
     #1#1 = 1;
     #1#2 = #480#0;
     #1#3 = #480#1;
     #1#4 = enUni00con;
     #1#7 = #480#10;
     #1#11 = #1#4;
     |HAZ Tipo74_2;
     |GRABA 020#1n;
|FPRC;

|PROCESO CompoPresu;
     aEscan = #gopre025@#2;
     nCantiEscan = #gopre025@#4;
     |SI #agifa142#165 = "S";
          |SI #gopre025@#16 = "D";
               nCantiEscan = #gopre025@#4 > #gopre025@#13;
          |SINO;
               nCantiEscan = #gopre025@#4 / ((100 - #gopre025@#13) / 100) ;
          |FINSI;
     |FINSI;
     nCantiEscan = nCantiEscan * nUni;
     |HAZ LinKit;
     nHay = 1;
|FPRC;

|DEFBUCLE CompoPresu;
     #gopre025@#1007;
     ;
     #gomantl1@#3, #gomantl1@#4, aDesde, aDesde, 001, eaArt00con, 001;
     #gomantl1@#3, #gomantl1@#4, aHasta, aHasta, 999, eaArt00con, 999;
     ;
     NULL, CompoPresu;
|FIN;

|PROCESO Expediente;
     |BUCLE CompoPresu;
     |SI nHay = 1; |ERROR10; |FINSI;
|FPRC;

|DEFBUCLE Expediente;
     #gomantl1@#1003;
     ;
     eaSerExpe, enNumExpe, 001;
     eaSerExpe, enNumExpe, 999;
     ;
     NULL, Expediente;
|FIN;

|PROCESO CreaCompo00con;
     nUni = #1#4;
     nHay = 0;
     |DDEF aPath;
     aAlfa = aPath + "gomantl1";
     |CARGA_DEF aAlfa, gomantl1@;
     |SI FSalida = 0;
          aAlfa = aPath + "gopre025";
          |CARGA_DEF aAlfa, gopre025@;
          |SI FSalida = 0;
               |ABRE #52;
               |BUCLE Expediente;
               |CIERRA #52;
               |DESCARGA_DEF #gopre025@;
          |SINO;
               |MENAV "0000Error al cargar 'gopre025'";
          |FINSI;
          |DESCARGA_DEF #gomantl1@;
     |SINO;
          |MENAV "0000Error al cargar 'gomantl1'";
     |FINSI;
|FPRC;

|PROCESO CoimaHisto;
     aPro = "000000"; aNom = "";
     |HAZ EsGestral;
     |SI FSalida = 0;
          |DDEF aAlfa;
          aAlfa = aAlfa + "gestrm11";
          |CARGA_DEF aAlfa, refere1@;
          |SI FSalida = 0;
               |ABRE #refere1@;
               #refere1@#0 = #0#20;
               #refere1@#1 = #0#0;
               |LEE 000#refere1@.=;
               |SI FSalida ! 0; |DDEFECTO #refere1@; |FINSI;
               |CIERRA #refere1@;
               aPro = #refere1@#2;
               aNom = #refere1@#3;
               |DESCARGA_DEF #refere1@;
          |FINSI;
     |FINSI;

     |DDEF aAlfa;
     aAlfa = aAlfa + "coima003";
     |CARGA_DEF aAlfa, refere1@;
     |SI FSalida = 0;
          |DDEFECTO #refere1@;
          |ABRE #refere1@;
          #refere1@#0 = "F";
          #refere1@#1 = #1#9;
          #refere1@#2 = #1#0;
          #refere1@#3 = #1#1;
          |SI nForma = -1;
               |LEE 101#refere1@.];
               |PARA; |SI FSalida = 0; |Y #refere1@#0 = "F"; |Y #refere1@#1 = #1#9;
                         |Y #refere1@#2 = #1#0; |Y #refere1@#3 = #1#1; |HACIENDO;
                    |BORRA 020#refere1@.a;
                    |LIBERA #refere1@;
                    |LEE 101#refere1@.s;
               |SIGUE;
          |SINO;
               #refere1@#7 = #1#4;
               #refere1@#9 = #0#1;
               #refere1@#10 = #0#2;
               #refere1@#13 = #1#2;
               #refere1@#14 = #1#3;
               #refere1@#16 = #1#7;
               #refere1@#11 = aPro;
               #refere1@#12 = aNom;
               |GRABA 020#refere1@.n;
          |FINSI;
          |CIERRA #refere1@;
          |DESCARGA_DEF #refere1@;
     |SINO;
          |MENAV "0000Error al cargar 'coima003'";
     |FINSI;
|FPRC;

|PROCESO Tipo74_7; |TIPO 7;
     aAlfa = "0000<F2> Consulta Detalle Componentes   <F3> Consulta Entrada Partidas";
     |HAZ EsDefelino;
     |SI FSalida = 0;
          aAlfa = "0000<F2>Consulta Componentes  <F3>Consulta Entrada Partidas <F4>Pedidos Pdte";
     |FINSI;
     |HAZ EsGestral;
     |SI FSalida = 0;
          aAlfa = "0000<F2>Consulta Componentes <F3>Consulta Entrada Partidas <F5>Doble Stock";
     |FINSI;
     |MENSA aAlfa;
|FPRC;

|PROCESO BorDefelino;
     |DDEF aAlfa;
     aAlfa = aAlfa + "defelm01";
     |CARGA_DEF aAlfa, refere1@;
     |SI FSalida = 0;
          |ABRE #refere1@;
          |SELECT #2#refere1@;
          #refere1@#5 = #1#9;
          #refere1@#6 = #1#0;
          #refere1@#7 = #1#1;
          |BORRA 000#refere1@.c;
          |CIERRA #refere1@;
          |DESCARGA_DEF #refere1@;
     |FINSI;
|FPRC;

|PROCESO NuePrv; |TIPO 0;
     |HAZ EsGestral;
     |SI FSalida = 0;
          eaSerie = #0#20;
          enOrden = #0#0;
          |SUB_EJECUTA_NP "gestrm11";
          nSwPin = 1; |HAZ PinGestral;
     |FINSI;
|FPRC;

|PROCESO T90_13; |TIPO 13;
     |HAZ EsGestral;
     |SI FSalida = 0;
          nSwPin = 0; |HAZ PinGestral;
     |FINSI;
     |HAZ EsCoimasa; nSwCoimasa = FSalida;
|FPRC

|PROCESO PinGestral;
     |DDEF aAlfa;
     aAlfa = aAlfa + "gestrm11";
     |CARGA_DEF aAlfa, refere1@;
     |SI FSalida = 0;
          |ABRE #refere1@;
          #refere1@#0 = #0#20;
          #refere1@#1 = #0#0;
          |LEE 000#refere1@.=;
          |SI FSalida ! 0; |DDEFECTO #refere1@; |FINSI;
          |CIERRA #refere1@;
          |SI nSwPin = 0; |PINPA #0#refere1@; |FINSI;
          |PINDA #0#refere1@;
          |DESCARGA_DEF #refere1@;
     |FINSI;
|FPRC;

|PROCESO VerDobleUni;
     |HAZ EsGestral;
     |SI FSalida = 0;         || Solo para el modulo
          eaArticulo = #1#2;
          enUnidad1 = #1#4;
          enUnidad2 = #1#13;
          enMedida1 = #1#17;
          enMedida2 = #1#18;
          enMedida3 = #1#19;

          eaProgVta = "agifa090";
          |SUB_EJECUTA_NP "dsz99919;V";
     |FINSI;
|FPRC;

|PROCESO EntraUni; |TIPO 7;
     |HAZ EsGestral;
     |SI FSalida = 0;         || Solo para el modulo
          enAlmacen = #1#7;
          eaCliente = "";
          efFecha = #0#1;
      ||***
          eaArticulo = #1#2;
          enUnidad1 = #1#4;
          enUnidad2 = #1#13;
          enMedida1 = #1#17;
          enMedida2 = #1#18;
          enMedida3 = #1#19;

          eaProgVta = "agifa090";
          enModoVta = (FEntrada / 100) ! 0;
          |SUB_EJECUTA_NP "dsz99919";

          |C_M #1#4, 1, "S";
          |SI eaSw2Stock = "S";
               |C_M #1#4, 1, "N";
               #1#4 = enUnidad1; |PINTA #1#4;
               #1#13 = enUnidad2;
               #1#17 = enMedida1;
               #1#18 = enMedida2;
               #1#19 = enMedida3;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO coima003;
     #refere1@#10 = #0#2;
     |GRABA 020#refere1@.a;
|FPRC;

|DEFBUCLE coima003;
     #refere1@#1006;
     6, 15;
     "F", #0#20, #0#0, 001, 0, 0, 0, "N"
     "F", #0#20, #0#0, 999, 0, 0, 0, "N"
     be;
     NULL, coima003;
|FIN;

|PROCESO CoimaFecPrev;
     |DDEF aAlfa;
     aAlfa = aAlfa + "coima003";
     |CARGA_DEF aAlfa, refere1@;
     |SI FSalida = 0;
          |BUCLE coima003;
          |DESCARGA_DEF #refere1@;
     |FINSI;
|FPRC;

|PROCESO T90_20; |TIPO 20;
     |SI nSwCoimasa = 0;
          |SI FSalida = "OPCION";
               FSalida = "Opciones";
          |SINO;
               |MENU aMenu;
               |SI FSalida = 1; |HAZ T90_4; |FINSI;
               |SI FSalida = 2; |HAZ BtnCoimaDesglosa; |FINSI;
          |FINSI;
     |SINO;
          |SI FSalida = "OPCION";
               FSalida = "Imprimir";
          |SINO;
               |HAZ T90_4;
          |FINSI;
     |FINSI;
|FPRC;
