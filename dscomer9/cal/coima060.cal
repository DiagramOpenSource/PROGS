|FICHEROS;
     coima060;
|FIN;

|VARIABLES;
     nPan                = 0;
     nBtn1               = -1;
     nBtn2               = -1;
     nOk                 = -1;
     nHandle             = -1;
     nLong               = -1;
     nCarac              = -1;
     nPos                = -1;

     aAlfa               = "";
     aLong               = "";
     aBase               = "";
     aImag               = "";
     aAbre               = "";
     aRuta               = "";
     aPrograma           = "";
     aBorra              = "";
     aParaIni            = "";
     aCarpCap            = "";
     aTextoIni           = "";
     aEjecuta            = "";
     aCarac              = "";
     aLee                = "";
     aProg               = "";
     aFicPNG             = "";
     aPathSer            = "";
     aPathPlu            = "";
     aPathLoc            = "";
     aOri                = "";
     aDes                = "";

     {1}aLocal           = "";

     &eaArticulo         = "";
     &eaUnidad           = "";
     &enError            = 0;
     &eaParametro1       = "";
     &eaParametro2       = "";
     &eaParametro3       = "";
     &eaParametro4       = "";
     &eaParametro5       = "";
|FIN;

|PROCESO T12;  |TIPO 12;
|FPRC;

|PROCESO T0C6;  |TIPO 0;
     |SI FSalida ! 10;
         |ACABA;
     |FINSI;

     |SUB_EJECUTA "coimz040;SACAIDIOMAS";
|FPRC;

|PROCESO Recoger;
     nOk   = 1;
     aRuta = "";
     |HAZ Traete_dsselect;
     |SI enError = 1;
         nOk = 1;
         |MENAV "0000No puedo instalar el dsselect.exe, consulte con su administrador";
     |FINSI;

     |ESPECIFICA "RBASS", aLocal;

     |HAZ DimeUnidad;
     aPrograma = aPathLoc + "dsselect.exe";

     aBorra = aPathLoc + "dsselect.txt";
     |RFBORRA aBorra;

     aParaIni = aPathLoc + "dsselect.ini";
     |RFBORRA aParaIni;

     aCarpCap = "/*.jpg";

     aTextoIni = &34 + aCarpCap + &34 + " " + &34 + aBorra + &34;
     |XABRE "CW", aParaIni, nHandle;
     |SI FSalida ] 0;
          |XGRABA nHandle, aTextoIni;
     |FINSI;
     |XCIERRA nHandle;

     aEjecuta = aPrograma;
     |RSYSTEM aEjecuta;

     aAbre = aPathLoc + "dsselect.txt";
     |XABRE "CU", aAbre, nHandle;
     |SI FSalida < 0;
        nOk      = 0;
        |ACABA;
     |FINSI;

     |XLEE nHandle, 250, aLee;
     |SI FSalida < 0
         nOk      = 0;
         |ACABA;
     |FINSI;

     |QBF aLee;
     |SI aLee ! "";
         aAlfa    = aLee;
         aLong    = aAlfa % 0;
         nLong    = aLong;
         aRuta    = "";
         |PARA nCarac = 1;  |SI nCarac [ nLong;  |HACIENDO nCarac = nCarac + 1;
               nPos   = (nCarac * 100) + 1;
               aCarac =  aAlfa % nPos;
               |SI aCarac = "/";  |O aCarac = "\";
                   aProg = "";
               |SINO;
                   aProg = aProg + aCarac;
               |FINSI;

               aRuta = aRuta + aCarac;
         |SIGUE;
     |FINSI;
     |XCIERRA nHandle;

     aBorra = aPathLoc + "dsselect.txt";
     |RFBORRA aBorra;

     aFicPNG = aRuta < "";

     aAlfa = aFicPNG - ".jpg";
     |SI FSalida = 0;
         aAlfa = "0000El fichero debera tener extesion JPG";
         |MENAV aAlfa;
         nOk = 0;

         |ACABA;
     |FINSI;

     aOri = aRuta;
     aDes = aPathSer + aImag;
     |RECIBE_FICHERO aOri, aDes;

     |HAZ BtnImagen;
|FPRC;

|PROCESO Editar;
     aAbre = aPathSer + aImag;

     aOri = aPathSer + aImag;
     aDes = aPathLoc + aImag;
     |ENVIA_FICHERO aOri, aDes;

     eaParametro1 = aPathLoc + "dsabreextension";
     eaParametro2 = aPathLoc + aImag;
     eaParametro3 = aPathPlu;
     eaParametro4 = aPathLoc;
     eaParametro5 = aPathLoc;

     |HAZ EjecutaDSAbreExtension;

     aOri = aPathLoc + aImag;
     aDes = aPathSer + aImag;
     |RECIBE_FICHERO aOri, aDes;
|FPRC;

|PROCESO Borrar;
     |CONFI "0000NSeguro que quiere borrar la imagen";
     |SI FSalida ! 0;
         |ACABA;
     |FINSI;

     aAbre = aPathSer + aImag;
     |FBORRA aAbre;

     |HAZ BtnImagen;
|FPRC;

|PROCESO BtnImagen;
     |SI nBtn1 ! -1;
         |FIN_CONTROL_WINDOWS nBtn1;
         nBtn1 = -1;
     |FINSI;

     |SI nBtn2 ! -1;
         |FIN_CONTROL_WINDOWS nBtn2;
         nBtn2 = -1;
     |FINSI;

     aAbre = aPathSer + aImag;
     |XABRE "E", aAbre, 0;
     |SI FSalida ] 0;
         |CONTROL_SIMPLE nPan, "BOTON,{{197}}EDITAR IMAGEN DESPIECE A FABRICAR", 1650, 1675, Editar;
         nBtn1 = FSalida;

         |CONTROL_SIMPLE nPan, "BOTON,{{197}}BORRAR IMAGEN DESPIECE A FABRICAR", 1750, 1775, Borrar;
         nBtn2 = FSalida;
     |SINO;
         |CONTROL_SIMPLE nPan, "BOTON,{{197}}RECOGER IMAGEN DESPIECE A FABRICAR", 1750, 1775, Recoger;
         nBtn1 = FSalida;
     |FINSI;
|FPRC;

|PROGRAMA;
     |SI PARAMETRO = "BAJA";
         #coima060#0 = eaArticulo;
         |LEE 101#coima060.=;
         |SI FSalida = 0;
             |BORRA 020#coima060.a;
             |LIBERA #coima060;
         |FINSI;

         |ACABA;
     |FINSI;

     |SI PARAMETRO = "SINOEXISTE";
         #coima060#0 = eaArticulo;
         |LEE 000#coima060.=;
         |SI FSalida = 0;
             |LIBERA #coima060;
             |ACABA;
         |FINSI;
     |FINSI;

     #coima060#0 = eaArticulo;
     |LEE 101#coima060.=;
     |SI FSalida ! 0;
         |DDEFECTO #coima060;
         #coima060#0 = eaArticulo;
         |GRABA 020#coima060.b;
     |FINSI;

     |SI #coima060#6 = "  ";
         aAlfa = eaArticulo;  |QBF aAlfa;
         aAlfa = aAlfa % -102;

         #coima060#6 = aAlfa;
     |FINSI;

     |PINPA #0#coima060;  nPan = FSalida;
     |PINDA #0#coima060;

     |HAZ BtnImagen;

     |ENDATOS #1#coima060;
     |SI FSalida = 0;
         |GRABA 020#coima060.a;
     |FINSI;
     |LIBERA #coima060;

     |SI nBtn1 ! -1;  |FIN_CONTROL_WINDOWS nBtn1;  |FINSI;
     |SI nBtn2 ! -1;  |FIN_CONTROL_WINDOWS nBtn2;  |FINSI;
|FPRO;

|PROCESO T80;  |TIPO 80;
     |ABRET #coima060;

     |DBASE aBase;
     aPathSer = aBase + "000web";            |MKDIR aPathSer;
     aPathSer = aBase + "000web/fotos";      |MKDIR aPathSer;
     aPathSer = aBase + "000web/fotos/";

     aPathPlu = aBase + "plugins/";

     |HAZ DimeUnidad;
     aPathLoc = eaUnidad + ":\DOCUMENTOS";               |RMKDIR aPathLoc;
     aPathLoc = eaUnidad + ":\DOCUMENTOS\DSCOMER9";      |RMKDIR aPathLoc;
     aPathLoc = eaUnidad + ":\DOCUMENTOS\DSCOMER9\";

     |QBF eaArticulo;
     aImag = eaArticulo + ".jpg";
|FPRC;

|PROCESO T90;  |TIPO 90;
     |CIERRAT #coima060;
|FPRC;
