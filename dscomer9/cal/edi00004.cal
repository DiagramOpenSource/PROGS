|FICHEROS;
     edi00004 #0;
     agifa019 #2;
     agifa010 #11;
     agifa071 #12;
     dsm00109 #109;

     edi00002 #80;  || Empresa (EDI).
     edi00003 #91;  || Codigos Edi Cliente...
|FIN;

|VARIABLES;
     nAlfa        = 0;
     nVic         = 0;
     swNo         = 0;
     aCamp        = "";
     fiche        = "";
     desde        = "                              ";
     hasta        = "zzzzzzzzzzzzzzzzzzzzzzzzzzzzzz";
     nTu          = 0;
     handle1      = 0;
     handle2      = 0;
     qDir         = "";
     aAlfa1       = "";
     aAlfa2       = "";
     aAlfa        = "";
     aSe          = "#";
     contenido    = "";
|FIN;

||INC-LUYE llena_c1;

|PROCESO agifa071;
     |DDEFECTO #2;

     #109#0 = #11#3;
     #109#2 = #12#2;
     |LEE 000#109=;
     |SI FSalida = 0;
          #2#0 = #109#3;
     |SINO;
          #2#0 = #12#2;
     |FINSI;
     |LEE 000#2=;
     ||
     ||---------- Graba Linea Albaran
     ||
     aAlfa = aAlfa2;                              || Identificador Cabecera.
     aAlfa = aAlfa + aSe + " ";                   || Identificador Cabecera Enbalaje
     aAlfa = aAlfa + aSe + #12#1;                 || Identificador N§ Linea.
     aAlfa = aAlfa + aSe + #2#128;                || Codigo EAN-13 Cod.Articulo
     aAlfa = aAlfa + aSe + " ";                   || Valor de la variable promocional
     aAlfa = aAlfa + aSe + #2#0;                  || Codigo Articulo proveedor
     aAlfa = aAlfa + aSe + " ";                   || Codigo Articulo Comprador
     aAlfa = aAlfa + aSe + " ";                   || Numero de Serie.
     aAlfa = aAlfa + aSe + " ";                   || Numero de Unidad de Expedicion
     aAlfa = aAlfa + aSe + #12#4;                 || Descripcion del producto
     aAlfa = aAlfa + aSe + "TU";                  || Descripcion Producto Codificada TU = aAlfa + Unidad Comerciada
     aAlfa = aAlfa + aSe + #12#5;                 || Cantidad Enviada Total.
     aAlfa = aAlfa + aSe + "0";                   || Cantidad Enviada Gratuita.
     aAlfa = aAlfa + aSe + "0";                   || Cantidad de unidades en agrupacion superior
     aAlfa = aAlfa + aSe + " ";                   || Numero de Unidades de consumo
     aAlfa = aAlfa + aSe + " ";                   || Unidad de medida
     aAlfa = aAlfa + aSe + " ";                   || Informacion de texto en formato libre
     aAlfa = aAlfa + aSe + " ";                   || idem
     aAlfa = aAlfa + aSe + " ";                   || idem
     aAlfa = aAlfa + aSe + " ";                   || idem
     aAlfa = aAlfa + aSe + " ";                   || idem
     aAlfa = aAlfa + aSe + #11#83;                || Numero de pedido
     aAlfa = aAlfa + aSe + aAlfa2;                || Numero de albaran
     ||
     ||--------- Graba
     ||
     |XGRABA handle2 , aAlfa;
     ||
     ||
|FPRC;

|PROCESO agifa071u;
     nTu = nTu + #12#5;
|FPRC;

|PROCESO Pasalos;
     swNo = 0;
     |HAZ parrillacli;
     |SI swNo = 0; |ACABA; |FINSI;
     swNo = 0;
     |HAZ parrillaalb;
     |SI swNo = 0; |ACABA; |FINSI;
     |DDEFECTO #91;
     #91#0 = #11#3;
     |LEE 000#91=;
     ||
     ||--------- Graba Cabecera de Albaran.
     ||
     aAlfa2 = #11#0;
     aAlfa2 = ("000000" + aAlfa2 ) % -106;

     aAlfa = aAlfa2;                      || Identificador Cabecera.
     aAlfa = aAlfa + aSe + aAlfa2;        || Numero Aviso Expedicion.
     aAlfa = aAlfa + aSe + "351";         || Tipo Documento  351 = Aviso de Expedicion
     aAlfa = aAlfa + aSe + "9";           || Funcion del Mensaje 9 = Original
     aAlfa = aAlfa + aSe + #11#1;         || Fecha Aviso Expedicion
     aAlfa = aAlfa + aSe + #11#1;         || Fecha Entrega Mercancia.
     aAlfa = aAlfa + aSe + " ";           || Fecha Entrega Solicitada.
     aAlfa = aAlfa + aSe + " ";           || Fecha Programado para la entrega en o antes
     aAlfa = aAlfa + aSe + " ";           || Fecha Programado para la entrega en o despues
     aAlfa = aAlfa + aSe + " ";           || Condiciones especiales.
     aAlfa = aAlfa + aSe + aAlfa2;        || Numero Albaran.
     aAlfa = aAlfa + aSe + #11#1;         || Fecha Albaran
     aCamp = #11#56;
     |QBF aCamp;
     aAlfa = aAlfa + aSe + aCamp;         || Numero Pedido.
     aCamp = #11#55;
     |QBF aCamp;
     aAlfa = aAlfa + aSe + aCamp;         || Fecha Pedido.
     aAlfa = aAlfa + aSe + " ";           || Numero de Lista en Picking
     aAlfa = aAlfa + aSe + " ";           || Fecha lista Picking.
     aAlfa = aAlfa + aSe + #80#6;         || Codigo EAN-13 Origen Mensaje.
     aAlfa = aAlfa + aSe + #91#3;         || Codigo EAN-13 Destino Mensaje.
     aAlfa = aAlfa + aSe + #80#0;         || Codigo EAN-13 Proveedor Mercancia.
     aAlfa = aAlfa + aSe + #91#7;         || Codigo EAN-13 Comprador
     aCamp = #11#57;
     |QBF aCamp;
     aAlfa = aAlfa + aSe + aCamp;         || Codigo Departamento que pide Mercancia.
     aAlfa = aAlfa + aSe + #91#1;         || Codigo EAN-13 Receptor Mercancia.
     aAlfa = aAlfa + aSe + " ";           || Codigo EAN-13 Muelle
     aAlfa = aAlfa + aSe + " ";           || Codigo EAN-13 Desde donde se envia
     aAlfa = aAlfa + aSe + " ";           || Codigo EAN-13 Expedidor
     aAlfa = aAlfa + aSe + " ";           || Codigo EAN-13 Ultimo consignatario
     aAlfa = aAlfa + aSe + " ";           || Numero de Entrega
     aAlfa = aAlfa + aSe + " ";           || Numero de Reposicion
     aAlfa = aAlfa + aSe + " ";           || Portes pagador / Debidos
     aAlfa = aAlfa + aSe + " ";           || Condiciones de Entrega
     aAlfa = aAlfa + aSe + " ";           || Calificador de Tipo de Transporte
     aAlfa = aAlfa + aSe + " ";           || identificador EAN Transportista
     aAlfa = aAlfa + aSe + " ";           || matricula Transportista
     ||--------- Comprobar Total Unidades.
     nTu = 0;
     |BUCLELIN 0 agifa071u #11;
     aAlfa1 = nTu;
     |QBF aAlfa1;
     aAlfa = aAlfa + aSe + aAlfa1;        || Sumatorio Cantidades
     aAlfa = aAlfa + aSe + #11#2;         || Bultos.
     ||
     ||--------- Graba
     ||
     |XGRABA handle1 , aAlfa;
     ||
     ||
     |BUCLELIN 0 agifa071 #11;
     ||
     ||
|FPRC;

|DEFBUCLE agifa010;
     #11#3;
     43;
     #0#0, #0#4,  #0#9, #0#2, #0#8;
     #0#1, #0#5, #0#10, #0#3, #0#8;
     ;
     NULL, Pasalos;
|FIN;


|PROCESO Tipo3;
     |HAZ AbreEmpr;
     |HAZ AbreFich;
     |ABRE #109; |SELECT #2#109;
     |ABRE #2;
     |ABRE #91;
     |BUCLE agifa010;
     |CIERRA #2;
     |CIERRA #91;
     |CIERRA #109;
     |HAZ CierFich;
|FPRC;

|PROGRAMA;

     |CLS;
     |CABEZA "Pase Albaranes a EDI";
     |PINPA #0#0;
     |PINDA #0#0;
     |ENDATOS #1#0;
     |SI FSalida = 0;  |HAZ Tipo3;  |FINSI;
|FPRO;

|PROCESO AbreFich;

  |DFICB qDir;
  fiche = qDir + "cabalb.txt";
  |QBF fiche;
  |XABRE "W" , fiche , handle1;

  fiche = qDir + "linalb.txt";
  |QBF fiche;
  |XABRE "W" , fiche , handle2;

|FPRC;

|PROCESO CierFich;

  |XCIERRA handle1;
  |XCIERRA handle2;

|FPRC;

|PROCESO AbreEmpr;
     |ABRE #80;
     |LEE 000#80p;
     |SI FSalida ! 0;
          |DDEFECTO #80;
     |FINSI;
     |CIERRA #80;
|FPRC;

|PROCESO parrillacli;
     aAlfa = #0#21;
     |QBF aAlfa;
     nAlfa = aAlfa;
     |SI aAlfa = ""; |O nAlfa = 0; swNo = 1; |ACABA; |FINSI;
     |PARA nVic = 21; |SI nVic [ 40; |HACIENDO nVic = nVic + 1;
          |SI #0nVic = #11#3;
               swNo = 1;
               |SAL;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO parrillaalb;
     |SI #0#41 = 0; swNo = 1; |ACABA; |FINSI;
     |PARA nVic = 41; |SI nVic [ 60; |HACIENDO nVic = nVic + 1;
          |SI #0nVic = #11#0;
               swNo = 1;
               |SAL;
          |FINSI;
     |SIGUE;
|FPRC;
