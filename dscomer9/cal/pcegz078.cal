|FICHEROS;
     pcegz078 #0;
     agifa010 #10
     agifa019 #19;
     pcegw020 #20;       || Tmp
     pcegw021 #21;       || Tmp
     pcegw023 #23;       || Tmp
     agifa024 #24;
     agifa071 #71;
     pcegm054 #54;
     pcegm055 #55;
     pcegm056 #56;
     pcegm057 #57;
     pcegm058 #58;
     pcegm059 #59;
|FIN;

|VARIABLES;
     &Tempo = "";
     &eaAlfa = "";
     nMax = 0;
     nNume = 0;
     aColUni = "";
     aColPeso = "";
     nTUni = 0;
     nTPeso = 0;
     nTrimestre = 0;
     i = 0;
     f = @;
     fFecha = @;
     nMes = 0;
     aMes = "";
     aAlfa = "";
     nTotaUni = 0;
     aBat = "";
     nUni = 0;
     nError = 0;
     aExcelDes = "";
     aExcelOrg = "";
     aIP = "";
     aOrg = "";
     {1}aLocal = "";
     aBase = "";
     aPath = "";
     nFila = 0;
     nPeso = 0;
     aTmp20 = "";
     aTmp21 = "";
     aTmp22 = "";
     aTmp23 = "";
     nTotaPeso = 0;
     aDFecha = "";
     aHFecha = "";
     nTipo = 0;
     aTipo = "";
     nKgs = 0;
|FIN;

|PROCESO TmpHistoLin;
     |SI #21#3 = aTipo;
          aAlfa = "C" + nFila + "," + #21#5; |HAZ Poke;
          aAlfa = "Q" + nFila + "," + #21#1; |HAZ Poke;
          aAlfa = "S" + nFila + "," + #21#2; |HAZ Poke;

          nFila = nFila + 1;
     |FINSI;
|FPRC;

|DEFBUCLE TmpHistoLin;
     #21#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, TmpHistoLin;
|FIN;

|PROCESO TotalHistoLin;
     |SI #23#4 = aTipo;
          aAlfa = aColUni + nFila + "," + #23#2; |HAZ Poke;
          nNume = (#23#3 / 1000) ! 4;
          aAlfa = aColPeso + nFila + "," + nNume; |HAZ Poke;

          nFila = nFila + 1;
     |FINSI;
|FPRC;

|DEFBUCLE TotalHistoLin;
     #23#2;
     ;
     INICIO;
     FINAL;
     ;
     NULL, TotalHistoLin;
|FIN;

|PROCESO HistoLin;
     #55#0 = #58#1;
     |LEE 000#55=;
     |SI FSalida ! 0; |DDEFECTO #55; |FINSI;

     aAlfa = "C" + nFila + "," + #55#1; |HAZ Poke;

     aAlfa = aColUni + nFila + "," + #58#2; |HAZ Poke;

     nNume = (#58#3 / 1000) ! 4;
     aAlfa = aColPeso + nFila + "," + nNume; |HAZ Poke;

     nFila = nFila + 1;

     |DDEFECTO #23;
     #23#0 = #58#1;
     |LEE 101#23=;
     |SI FSalida ! 0; |GRABA 020#23b; |FINSI;
     #23#1 = #55#1;
     #23#2 = #23#2 + #58#2;
     #23#3 = #23#3 + #58#3;
     #23#4 = aTipo;
     |GRABA 020#23a;
     |LIBERA #23;
|FPRC;

|PROCESO Orden;
     #55#0 = #58#1;
     |LEE 000#55=;
     |SI FSalida ! 0; |DDEFECTO #55; |FINSI;
|FPRC;

|DEFBUCLE HistoLin;
     #58#1;
     4;
     fFecha, "  ", aTipo;
     fFecha, "zz", aTipo;
     ;
     NULL, Orden, NULL, NULL, NULL, HistoLin;
     #55#0;
|FIN;

|PROCESO LeeTrimestre
     |HAZ FecTrimestre;
     |ABRE #57;
     #57#0 = fFecha;
     |LEE 000#57=;
     |SI FSalida = 0;
          nUni = #57#4;
          nPeso = #57#3;
     |SINO;
          nUni = 0;
          nPeso = 0;
     |FINSI;
     |CIERRA #57;
|FPRC;

|PROCESO AgrupaHisto;
     |DDEFECTO #55;
     #55#0 = #58#1;
     |LEE 000#55=;

     |DDEFECTO #21;
     #21#0 = #58#1;
     |LEE 101#21=;
     |SI FSalida ! 0; |GRABA 020#21b; |FINSI;

     aAlfa = #21#4; |QBF aAlfa;
     |SI aAlfa = "";
          aAlfa = #58#5;
     |SINO;
          aAlfa = aAlfa + #58#5;
     |FINSI;

     #21#1 = #21#1 + #58#2;
     #21#2 = #21#2 + #58#3;
     #21#3 = #58#4;
     #21#4 = aAlfa;
     #21#5 = #55#1;
     |GRABA 020#21a;
     |LIBERA #21;

     nTUni = nTUni + #58#2;
     nTPeso = nTPeso + #58#3;
|FPRC;

|DEFBUCLE AgrupaHisto;
     #57#1;
     ;
     aDFecha;
     aHFecha;
     ;
     NULL, NULL, AgrupaHisto;
|FIN;

|PROCESO HistoPT;
     |SI #58#4 ! aTipo;
          |ACABA;
     |FINSI;
     #55#0 = #58#1;
     |LEE 020#55=;

     nKgs = (#58#3 / 1000) ! 4;

     aAlfa = "D" + nFila + "," + #58#5; |HAZ Poke;
     aAlfa = "E" + nFila + "," + #55#1; |HAZ Poke;
     aAlfa = "F" + nFila + "," + #58#2; |HAZ Poke;
     aAlfa = "G" + nFila + ", Uds"; |HAZ Poke;
     aAlfa = "H" + nFila + "," + #55#2; |HAZ Poke;
     aAlfa = "I" + nFila + ", grs"; |HAZ Poke;
     aAlfa = "J" + nFila + "," + nKgs; |HAZ Poke;
     aAlfa = "K" + nFila + ", Kgrs"; |HAZ Poke;

     nFila = nFila + 1;
|FPRC;

|DEFBUCLE HistoPT;
     #57#1;
     ;
     fFecha;
     fFecha;
     ;
     NULL, NULL, HistoPT;
|FIN;

|PROCESO TmpPT0;
     aAlfa = "A" + nFila + "," + #20#0; |HAZ Poke;
     aAlfa = "B" + nFila + "," + #20#1; |HAZ Poke;
     nFila = nFila + 1;
|FPRC;

|DEFBUCLE TmpPT0;
     #20#2;
     ;
     nTipo, "        ";
     nTipo, "zzzzzzzz";
     ;
     NULL, TmpPT0;
|FIN;

|PROCESO Tmp;
     #54#0 = #20#0;
     |LEE 000#54=;
     |SI FSalida ! 0; |DDEFECTO #54; |FINSI;

     nFila = nFila + 1;

     aAlfa = "A" + nFila + "," + #20#0; |HAZ Poke;
     aAlfa = "B" + nFila + "," + "3"; |HAZ Poke;

     aAlfa = #20#1;
     aAlfa = "C" + nFila + "," + aAlfa; |HAZ Poke;

     nPeso = #20#1 * #54#2;
     aAlfa = "D" + nFila + "," + nPeso; |HAZ Poke;
|FPRC;

|DEFBUCLE Tmp;
     #20#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, Tmp;
|FIN;

|PROCESO FinExcel;
     |ALFACALLDLL "agixl97.dll", "fichero_destino", aExcelDes;
     ||ALFACALLDLL "agixl97.dll", "fin_book", "salva";      || graba
     ||ALFACALLDLL "agixl97.dll", "fin_book", "imprime";    || imprime
     |ALFACALLDLL "agixl97.dll", "fin_book", "";            || visualiza
     |DESCARGADLL "agixl97.dll";

     |SI aIP = "";
          |FBORRA aExcelOrg;
     |SINO;
          |RFBORRA aExcelOrg;
     |FINSI;
|FPRC;

|PROCESO IniExcel;
     |CARGADLL "agixl97.dll";
     |SI FSalida < 0;
         |MENAV "0000Error al cargar agixl97.dll...";
         nError = 1;
     |SINO;
          |DBASE aOrg;
          |SI #0#0 = "P";
               |SI #0#1 = "T";
                    aOrg = aOrg + "/plugins/pce_trimestral.xls";
               |SINO;
                    aOrg = aOrg + "/plugins/pce_anual.xls";
               |FINSI;
          |SINO;
               aOrg = aOrg + "/plugins/pce_esp.xls";
          |FINSI;
          |SI aIP = "";
               |COPIA_FICHERO aOrg, aExcelOrg;
          |SINO;
               |ENVIA_FICHERO aOrg, aExcelOrg;
          |FINSI;
          |SI FSalida = 0;
               aAlfa = aExcelOrg;
               |ALFACALLDLL "agixl97.dll", "carga_book", aAlfa; || modifica aAlfa
               |ALFACALLDLL "agixl97.dll", "selecciona_hoja", "Hoja1";
               nError = FSalida;
          |SINO;
               |MENAV "0000Error al crear Libro excel...";
               nError = 1;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Poke;
     eaAlfa = aAlfa;
||     |HAZ QuitaRarosExp;
     |ALFACALLDLL "agixl97.dll", "poke_dato", eaAlfa;
|FPRC;

|PROCESO Recal;
     #57#2 = #57#2 + #58#2;
     #57#3 = #57#3 + #58#3;
|FPRC;

|PROCESO MaxPeso;
     #54#0 = #56#1;
     |LEE 020#54=;
     |SI FSalida ! 0; |DDEFECTO #54; |FINSI;
     |SI #54#2 > nPeso;
          nPeso = #54#2;
     |FINSI;
|FPRC;

|PROCESO GrabaPT;
     |SELECT #2#56;
     nPeso = 0;
     #56#1 = #20#0;
     #56#0 = "";
     |LEE 101#56];
     |SI FSalida ! 0; |O #56#1 ! #20#0;
          |DDEFECTO #55;
     |SINO;
          #55#0 = #56#0;
          |LEE 020#55=;
          |BUCLELIN 2 MaxPeso #55;
     |FINSI;

     |DDEFECTO #58;
     #58#0 = #57#0;
     #58#1 = #55#0;
     |LEE 101#58=;
     |SI FSalida ! 0; |GRABA 020#58b; |FINSI;

     #58#2 = #58#2 + #20#1;
     #58#3 = #58#3 + (#20#1 * nPeso);
     #58#4 = #55#3;
     aAlfa = #58#5; |QBF aAlfa;
     |SI aAlfa = "";
          aAlfa = #20#0;
     |SINO;
          aAlfa = aAlfa + " " + #20#0;
     |FINSI;
     #58#5 = aAlfa;
     |GRABA 020#58a;
     |LIBERA #58;

     |DDEFECTO #59;
     #59#0 = #58#0;
     #59#1 = #58#1;
     #59#2 = #20#0;
     |LEE 101#59=;
     |SI FSalida ! 0; |GRABA 020#59b; |FINSI;
     #59#3 = #59#3 + #20#1;
     #59#4 = #59#4 + (#20#1 * nPeso);
     |GRABA 020#59a;
     |LIBERA #59;
|FPRC;

|DEFBUCLE GrabaPT;
     #20#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, GrabaPT;
|FIN;

|PROCESO GrabaBatePT;
     |DDEFECTO #58;
     #58#0 = #57#0;
     #58#1 = #55#0;
     #58#4 = #55#3;
     |LEE 000#58=;
     |SI FSalida ! 0; |GRABA 020#58n; |FINSI;
|FPRC;

|DEFBUCLE GrabaBatePT;
     #55#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, GrabaBatePT;
|FIN;

|PROCESO GrabaBateria;
     |DDEFECTO #20;
     #20#0 = aBat;
     |LEE 101#20=;
     |SI FSalida ! 0; |GRABA 020#20b; |FINSI;
     #20#1 = #20#1 + (nUni * #71#5);
     #20#2 = nTipo;
     |GRABA 020#20a;
     |LIBERA #20;
|FPRC;

|PROCESO AlbaLin;
     #19#0 = #71#2;
     |LEE 020#19=;
     |SI FSalida ! 0; |DDEFECTO #19; |FINSI;

     || si es 1 es que es electr¢nico y puede contener una bateria
     |SI #19#119 ! 1; |ACABA; |FINSI;

     aBat = #19#110; |QBF aBat;
     nUni = #19#116;
     nTipo = 1;
     |SI aBat ! ""; |HAZ GrabaBateria; |FINSI;

     aBat = #19#111; |QBF aBat;
     nUni = #19#117;
     nTipo = 2;
     |SI aBat ! ""; |HAZ GrabaBateria; |FINSI;

     nTotaUni = nTotaUni + #71#5;
|FPRC;

|PROCESO Albaranes;
     #24#0 = #10#3;
     |LEE 000#24=;
     |SI FSalida ! 0; |DDEFECTO #24; |FINSI;

     |SI #24#205 ! #0#8; |ERROR; |FINSI;
|FPRC;

|DEFBUCLE Albaranes;
     #10#4;
     ;
     #0#4, #0#6, 000001;
     #0#5, #0#7, 999999;
     ;
     NULL, Albaranes, AlbaLin;
|FIN;

|PROCESO Tipo; |TIPO 0;
     |SI #0#1 = "A";
          |C_M #0#3, 1, "N";
          |C_M #0#4, 1, "N";
          |C_M #0#5, 1, "N";
          |C_M #0#6, 1, "N";
          |C_M #0#7, 1, "N";
          |C_M #0#8, 1, "N";
     |SINO;
          |SI #0#0 = "E";
               |C_M #0#3, 1, "N";
          |SINO;
               |C_M #0#3, 1, "S";
          |FINSI;
          |C_M #0#4, 1, "S";
          |C_M #0#5, 1, "S";
          |C_M #0#6, 1, "S";
          |C_M #0#7, 1, "S";
          |C_M #0#8, 1, "S";
     |FINSI;
|FPRC;

|PROCESO Pais; |TIPO 0;
     |SI #0#0 = "E";
          |C_M #0#1, 1, "N";
          |C_M #0#2, 1, "N";
          |C_M #0#3, 1, "N";
          #0#1 = "T"; |PINTA #0#1;
     |SINO;
          |C_M #0#1, 1, "S";
          |C_M #0#2, 1, "S";
          |C_M #0#3, 1, "S";
     |FINSI;
|FPRC;

|PROCESO FecTrimestre;
     nMes = nTrimestre * 3;
     aAlfa = "01." + (("00" + nMes) % -102) + "." + (("0000" + #0#2) % -104);
     |PARA f = aAlfa; |SI; |HACIENDO f = f + 1;
          aMes = f % 402;
          |SI aMes ! nMes; |SAL; |FINSI;
          fFecha = f;
     |SIGUE;
|FPRC;

|PROCESO A¤o; |TIPO 7;
     |SI #0#2 = 0;
     f = ~;
     aAlfa = f % 704;
     #0#2 = aAlfa; |PINTA #0#2;
     |FINSI;
|FPRC;

|PROCESO T3; |TIPO 3;
     nTrimestre = #0#3;
     |HAZ FecTrimestre;

     |SI #0#0 = "P"; |Y #0#1 = "T";
          |ABRE #57;
          #57#0 = fFecha;
          |LEE 000#57=;
          |SI FSalida = 0; |Y #57#1 = "S";;
               |MENAV "0000Registro bloqueado....";
               |ACABA;
          |FINSI;
          |CIERRA #57;
     |FINSI;

     aIP = ""; |IP_REMOTO aIP;

     |HAZ CreaTempo; |NOME_DAT #20 Tempo; |DELINDEX #20; aTmp20 = Tempo;
     |HAZ CreaTempo; |NOME_DAT #21 Tempo; |DELINDEX #21; aTmp21 = Tempo;
     |HAZ CreaTempo; |NOME_DAT #23 Tempo; |DELINDEX #23; aTmp23 = Tempo;

     |SI #0#1 ! "A";
          |ABRE #20;
          |ABRE #24;
          |ABRE #19;
          |BUCLE Albaranes;
          |CIERRA #19;
          |CIERRA #24;
     |FINSI;

     |SI #0#0 = "P";
          |SI #0#1 = "A";
               aDFecha = "01.01." + (("0000" + #0#2) % -104);
               aHFecha = "31.12." + (("0000" + #0#2) % -104);
               |ABRE #21;
               |ABRE #55;
               |BUCLE AgrupaHisto;
               |CIERRA #55;
               |CIERRA #21;
          |SINO;
               aDFecha = fFecha;
               aHFecha = fFecha;

               |ABRE #55;
               |ABRE #56;
               |ABRE #57;
               |ABRE #58;
               |ABRE #59;
               #57#0 = fFecha;
               |LEE 101#57=;
               |SI FSalida ! 0; |GRABA 020#57b; |FINSI;
               #57#4 = nTotaUni;
               |BUCLE GrabaBatePT;
               |ABRE #54;
               |BUCLE GrabaPT;
               |CIERRA #54;

               |BUCLELIN 0 Recal #57;
               |GRABA 020#57a;
               |CIERRA #59;
               |CIERRA #58;
               |CIERRA #57;
               |CIERRA #56;
               |CIERRA #55;
          |FINSI;
     |FINSI;

     |ESPECIFICA "RBASS", aLocal;
     aBase = aLocal;
     aPath = aBase + "tmp/"; |RMKDIR aPath;

     f = ~;
     aAlfa = #0#8; |QBF aAlfa;
     aAlfa = (f % 704) + (f % 402) + (f % 102) + "_" + aAlfa + "_" + (Usuario % 810);

     aExcelDes = aPath + aAlfa + ".xls";
     aExcelOrg = aPath + "Libro1.xls";
     |RFBORRA aExcelDes;

     |HAZ IniExcel;
     |SI nError = 0;
          |SI #0#0 = "E";
               nFila = 4;
               aAlfa = "A" + nFila + ",2204"; |HAZ Poke;
               aAlfa = "B" + nFila + ",3"; |HAZ Poke;
               aAlfa = "C" + nFila + "," + nTotaUni; |HAZ Poke;
               nPeso = (nTotaUni * 0.3) ! 0;
               aAlfa = "D" + nFila + "," + nPeso; |HAZ Poke;

               |ABRE #54;
               |BUCLE Tmp;
               |CIERRA #54;
          |SINO;
               |SI #0#1 = "T";
                    |ALFACALLDLL "agixl97.dll", "selecciona_hoja", "ID of the Producer";
                    nFila = 10;
                    aAlfa = "E" + nFila + "," + #0#2; |HAZ Poke;

                    nTUni = 0; nTPeso = 0;
                    |ALFACALLDLL "agixl97.dll", "selecciona_hoja", "EEE";
                    |PARA nTrimestre = 1; |SI nTrimestre [ #0#3; |HACIENDO nTrimestre = nTrimestre + 1;
                         nFila = 12;
                         |SI nTrimestre = 1;
                              |HAZ LeeTrimestre;
                              nNume = (nUni * 0.3) ! 4;
                              aAlfa = "G" + nFila + "," + nUni; |HAZ Poke;
                              aAlfa = "I" + nFila + "," + nNume; |HAZ Poke;
                         |FINSI;
                         |SI nTrimestre = 2;
                              |HAZ LeeTrimestre;
                              nNume = (nUni * 0.3) ! 4;
                              aAlfa = "K" + nFila + "," + nUni; |HAZ Poke;
                              aAlfa = "M" + nFila + "," + nNume; |HAZ Poke;
                         |FINSI;
                         |SI nTrimestre = 3;
                              |HAZ LeeTrimestre;
                              nNume = (nUni * 0.3) ! 4;
                              aAlfa = "O" + nFila + "," + nUni; |HAZ Poke;
                              aAlfa = "Q" + nFila + "," + nNume; |HAZ Poke;
                         |FINSI;
                         |SI nTrimestre = 4;
                              |HAZ LeeTrimestre;
                              nNume = (nUni * 0.3) ! 4;
                              aAlfa = "S" + nFila + "," + nUni; |HAZ Poke;
                              aAlfa = "U" + nFila + "," + nNume; |HAZ Poke;
                         |FINSI;
                         nTUni = nTUni + nUni;
                         nTPeso = nTPeso + nPeso;
                    |SIGUE;
                    nNume = (nTUni / 1000) ! 4;
                    aAlfa = "W" + nFila + "," + nTUni; |HAZ Poke;
                    aAlfa = "Y" + nFila + "," + nNume; |HAZ Poke;

                    |ABRE #55;
                    |ABRE #23;
                    |ALFACALLDLL "agixl97.dll", "selecciona_hoja", "B&A";
                    |PARA nTipo = 1; |SI nTipo [ 2; |HACIENDO nTipo = nTipo + 1;
                         |PARA nTrimestre = 1; |SI nTrimestre [ #0#3; |HACIENDO nTrimestre = nTrimestre + 1;
                              |SI nTipo = 1;
                                   aTipo = "P";
                                   nFila = 12;
                              |SINO;
                                   aTipo = "I";
                                   nFila = 21;
                              |FINSI;
                              |SI nTrimestre = 1;
                                   aColUni = "F"; aColPeso = "H";
                                   |HAZ FecTrimestre;
                                   |BUCLE HistoLin;
                              |FINSI;
                              |SI nTrimestre = 2;
                                   aColUni = "J"; aColPeso = "L";
                                   |HAZ FecTrimestre;
                                   |BUCLE HistoLin;
                              |FINSI;
                              |SI nTrimestre = 3;
                                   aColUni = "N"; aColPeso = "P";
                                   |HAZ FecTrimestre;
                                   |BUCLE HistoLin;
                              |FINSI;
                              |SI nTrimestre = 4;
                                   aColUni = "R"; aColPeso = "T";
                                   |HAZ FecTrimestre;
                                   |BUCLE HistoLin;
                              |FINSI;
                         |SIGUE;
                    |SIGUE;
                    |CIERRA #23;
                    |CIERRA #55;

                    |PARA nTipo = 1; |SI nTipo [ 2; |HACIENDO nTipo = nTipo + 1;
                         |SI nTipo = 1;
                              aTipo = "P";
                              nFila = 12;
                         |SINO;
                              aTipo = "I";
                              nFila = 21;
                         |FINSI;
                         aColUni = "V"; aColPeso = "X";
                         |BUCLE TotalHistoLin;
                    |SIGUE;
               |SINO;         || Anual
                    |ALFACALLDLL "agixl97.dll", "selecciona_hoja", "ID of the Producer";
                    nFila = 10;
                    aAlfa = "E" + nFila + "," + #0#2; |HAZ Poke;

                    |ALFACALLDLL "agixl97.dll", "selecciona_hoja", "EEE";
                    aAlfa = "V" + 17 + "," + #0#2; |HAZ Poke;
                    aAlfa = "V" + 23 + "," + nTUni; |HAZ Poke;
                    aAlfa = "X" + 23 + "," + nTPeso; |HAZ Poke;

                    |ALFACALLDLL "agixl97.dll", "selecciona_hoja", "B&A";
                    |PARA nTipo = 1; |SI nTipo [ 2; |HACIENDO nTipo = nTipo + 1;
                         |SI nTipo = 1;
                              aTipo = "P";
                              nFila = 19;
                         |SINO;
                              aTipo = "I";
                              nFila = 29;
                         |FINSI;
                         |BUCLE TmpHistoLin;
                    |SIGUE;
               |FINSI;
          |FINSI;

          |HAZ FinExcel;
     |FINSI;

     |CIERRA #20; |DELINDEX #20; Tempo = aTmp20; |HAZ BoraTempo;
     |CIERRA #21; |DELINDEX #21; Tempo = aTmp21; |HAZ BoraTempo;
     |CIERRA #23; |DELINDEX #23; Tempo = aTmp23; |HAZ BoraTempo;
|FPRC;
