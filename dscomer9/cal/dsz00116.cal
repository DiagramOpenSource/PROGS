|FICHEROS;
     dsz00116 #0;
     agifa007 #7;
     agifa019 #19;
     agifa069 #69;
     agifa084 #84;
     agifa142 #142;
     agifa321 #321;
|FIN;

|VARIABLES;
     &enTiva = 0;
     &efFiva = @;
     &enIva = 0;
     &aDivisa = "";
     &aMoneda = "";
     &Tempo = "";
     aTmp69 = "";
     aTmp84 = "";
     nInfor = 0;
     nImpre = 0;
     aInf = "";
     aAlfa = "";
     aPath = "";
     aFic = "";
     i = 0;
     nPos = 0;
     aLetra = "";
     aReg = "";
     aTab = "";
 {99}Valor = "";
     aDato = "";
     nCampo = 0;
     aLon = "";
     nHandle = 0;
     nPrecio = 0;
     nBruto = 0;
     nLin = 0;
|FIN;

|PROCESO ImpCab;
     nInfor = -1;
     nImpre = -1;

     |DDEFECTO #7;
     #7#26 = #84#48;
     |LEE 000#7=;
     aInf = #7#11; |QBF aAlfa;
     Impresora = "CrystalReport";
     |IMPRE -1;
     nImpre = FSalida;
     |SI FSalida ! 0; |ERROR; |ACABA; |FINSI;
     |INFOR aInf;
     nInfor = FSalida;
     |SI FSalida ! 0; |ERROR; |FINSI;
|FPRC;

|PROCESO FinCab;
     |SI nInfor = 0; |PIEINF; |FININF; |FINSI;
     |SI nImpre = 0; |FINIMP; |FINSI;
|FPRC;

|PROCESO ImpLin;
     |PRINT;
|FPRC;

|DEFBUCLE Imprime;
     #84#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, ImpCab, ImpLin, FinCab;
|FIN;

|PROCESO Dato;
     nCampo = 1;
     IValor = Valor1<-;
     aLon = aDato % A0;
     aReg = "";
     aTab = &9;
     |PARA i = 1; |SI i [ aLon; |HACIENDO i = i + 1;
          nPos = i * 100 + 1;
          aLetra = aDato % nPos;
          |SI aLetra = aTab;
               Valor = aReg;
               aReg = "";
               nCampo = nCampo + 1;
               IValor = IValor + 1;
          |SINO;
               aReg = aReg + aLetra;
          |FINSI;
          |SI nCampo > 90; |SAL; |FINSI;     || Por si.... :)
     |SIGUE;
     Valor = aReg;
|FPRC;

|PROCESO RecalFin;
     nBruto = #84#15 / (1+(enIva/100));
     #84#15 = nBruto;
     ||#84#25 = #84#15 % #84#5;
     #84#19 = #84#15 - #84#25;
     #84#20 = #84#41 - #84#19;
     |GRABA 020#84a;
|FPRC;

|PROCESO RecalLin;
     #19#0 = #69#2;
     |LEE 000#19=;
     |SI FSalida = 0;
          enTiva = #19#30;
     |SINO;
          enTiva = #142#239;
     |FINSI;
     efFiva = #84#1;
     |HAZ SacaIVA;

     nPrecio = #69#6 / (1+(enIva/100));
     #69#6 = nPrecio;
     #69#7 = #69#5 *  #69#6;
     |GRABA 020#69a;

     #84#15 = #84#15 + #69#9;
|FPRC;

|PROCESO RecalCab;
     #84#15 = 0;
|FPRC;

|DEFBUCLE Recalcula;
     #84#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, RecalCab, RecalLin, RecalFin;
|FIN;

1.FechaTiquet
2.TipoCobro
3.SerieAux
4.DocumentoAux
5.CliNombre
6.CliCif
7.CliDireccion
8.CliPostal
9.clipoblacion
10.CliProvincia
11.total
12.tdescuentoesto
|PROCESO GrabaCab;
     #84#1 = Valor1;
     #84#8 = Valor2;
     #84#48 = Valor3;
     #84#0 = Valor4;
     #84#4 = Valor5;
     #84#60 = Valor6;
     #84#30 = Valor7;
     #84#64 = Valor8;
     #84#31 = Valor9;
     #84#33 = Valor10;
     #84#41 = Valor11;   || esta repetido
     #84#25 = Valor12;
     |GRABA 020#84n;
|FPRC;

1.serie
2.documento
3.linea
4.CodArticulo
5.Unidades
6.pvp
7.importe
8.descripcion
|PROCESO GrabaLin;
     #69#20 = Valor1;
     #69#0 = Valor2;
     nLin = Valor3; nLin = nLin + 1;
     #69#1 = nLin;
     #69#2 = Valor4;
     #69#5 = Valor5;
     #69#6 = Valor6;
     #69#9 = Valor7;
     #69#4 = Valor8;
     |GRABA 020#69n;
|FPRC;

|PROCESO Importa;
     aAlfa = aFic % -110; aAlfa = aAlfa < "";
     |SI aAlfa ! "cabfac.txt"; |Y aAlfa ! "linfac.txt"; |ACABA; |FINSI;

     |XABRE "C", aFic, nHandle;
     |SI FSalida ] 0;
          |XLEE nHandle, 250, aDato;
          |PARA; |SI FSalida ] 0; |HACIENDO;
               |HAZ Dato;

               aAlfa = aFic % -110; aAlfa = aAlfa < "";
               |SI aAlfa = "cabfac.txt";
                    |HAZ GrabaCab;
               |FINSI;
               |SI aAlfa = "linfac.txt";
                    |HAZ GrabaLin;
               |FINSI;
               |XLEE nHandle, 250, aDato;
          |SIGUE;
          |XCIERRA nHandle;
     |FINSI;

     |RFBORRA aFic;
|FPRC;

|PROGRAMA;
     |ABRE #0;
     |SI PARAMETRO = "menu";
          |LEE 101#0p;
          |SI FSalida ! 0; |GRABA 020#0b; |FINSI;
          |PINPA #0#0;
          |PINDA #0#0;
          |ENDATOS #1#0;
          |SI FSalida = 0; |GRABA 020#0a; |FINSI;
     |SINO;
          |HAZ CreaTempo; aTmp84 = Tempo; |NOME_DAT #84 Tempo; |DELINDEX #84;
          |HAZ CreaTempo; aTmp69 = Tempo; |NOME_DAT #69 Tempo; |DELINDEX #69;

          |ABRE #142; |LEE 000#142p; |CIERRA #142;
          |ABRE #321; |LEE 000#321p; |CIERRA #321;

          |HAZ DecimalesBase;
          aDivisa = #321#9;
          aMoneda = "B";
          |HAZ Divisas084;

          |ABRE #84;
          |ABRE #69;
          |LEE 000#0p;
          aPath = #0#1; |QBF aPath;
          aAlfa = aPath % -101;
          |SI aAlfa ! "/"; |Y aAlfa ! "\"; aPath = aPath + "/"; |FINSI;
          |R_PDIR aPath;
          |PARA; |SI FSalida = 0; |HACIENDO;
               aFic = aPath;
               |HAZ Importa;
               |R_SDIR aPath;
          |SIGUE;
          |CIERRA #69;
          |CIERRA #84;

          |ABRE #19;
          |BUCLE Recalcula;
          |CIERRA #19;

          |ABRE #7;
          |BUCLE Imprime;
          |CIERRA #7;

          Tempo = aTmp84; |HAZ BoraTempo; |DELINDEX #84;
          Tempo = aTmp69; |HAZ BoraTempo; |DELINDEX #69;
     |FINSI;
     |CIERRA #0;
|FPRO;
