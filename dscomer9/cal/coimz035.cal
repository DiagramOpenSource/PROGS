|FICHEROS;
     dsm00003 #3;
     agifa010 #10;
     agifa019 #19;
     agifa024 #24;
     agifa025 #25;
     coima035 #35;
     agifa040 #40;
     coima049 #49;
     dsm00047 #47;
     coima053 #53;
     agifa071 #71;
     agifa142 #142;
     dsm00053 #153;
     agifa324 #324;
     agifa321 #321;
     agifa048 #480;

     refere1@;
     refere3@;
|FIN;

|INCLUYE i_varart;

|VARIABLES;
     aDef = "";
     nSwChampiEmar = 0;
     &eaTag = "";
     &eaImpresora = "";
     &ser_alb = "";
     &num_alb = 0;
     &abono = "";

     &enSwMenu = 0;
     &aDivisa = "";
     &aMoneda = "";

     &enDescuento1 = 0;
     &enDescuento2 = 0;
     &eaTComision  = "";
     &enComision   = 0;

     &uni_dt = 0;   || Unidades
     &uni_dt2 = 0;   || Unidades2
     &fed_dt = @;   || Fecha....
     &art_dt = "";  || Articulo.
     &cli_dt = "";  || Cliente..
     &fam_dt = "";  || Familia.
     &iva_dt = 0;   || Tipo Iva.
     &dtos_2 = 0;   || Descuento 2.
     &dto_pt = 0;   || Descuento Ptas.
     &dtos_1 = 0;   || Descuento 1.
     &pvp_ve = 0;   || Precio Venta.
     &tarifa = 0;   || Tarifa Cliente.
     &quedto = 0;   || 1=cli./art, 2=cli./familia, 3=cli./subfam.
     &gru_dt = "";  || Grupo Cliente.
     &nPromo = 0;
     &enMensaje = 0;
     &aParam    = "";

     {1}aBaseLocal = "";
     nDeci = 0;
     aFormato2 = "";
     aEmail = "";
     aMailRep1 = "";
     aMailRep2 = "";
     nPedido = 0;
     aCli = "";
     nLin = 0;
     aAlfa = "";
     aPob = "";
     aArti = "";
     nLinKit = 0;
     aIP = "";
     aServidor = "";
     aFrom = "";
     aTo = "";
     aSubject = "";
     aMensaje = "";
     aDjunto = "";
     aPath = "";
     importe = 0;
     cant = 0;
|FIN;

|PROCESO ChampiEmarEnvase;
     #refere3@#0 = #49#19;
     |LEE 000#refere3@.=;
     |SI FSalida ! 0; |DDEFECTO #refere3@; |FINSI;

     |DDEFECTO #refere1@;
     #refere1@#0 = #71#29;
     #refere1@#1 = #71#0;
     #refere1@#2 = #71#1;
     |LEE 101#refere1@.=;
     |SI FSalida ! 0; |GRABA 020#refere1@.b; |FINSI;

     #refere1@#3 = #49#20;                  ||bultos
     #refere1@#4 = #49#21;                  ||bruto
     #refere1@#5 = #49#20 * #refere3@#2;    ||tara
     #refere1@#6 = #49#19;                  ||envase
     |GRABA 020#refere1@.a;
     |LIBERA #refere1@;

     #71#5 = (#refere1@#4 - #refere1@#5) ! nDeci;
|FPRC;

|PROCESO Kit;
     nLinKit = nLinKit + 1;
     |DDEFECTO #47;
     #47#0 = #71#29;
     #47#1 = #71#0;
     #47#2 = #71#1;
     #47#3 = #49#2;
     #47#4 = #49#9;
     #47#5 = #49#4;
     #47#6 = nLinKit;
     #47#8 = #19#9;
     |GRABA 020#47n;
|FPRC;

|PROCESO precio_divisa;
     #71#36 = #71#6 / #10#74 * #10#69;  || Calculo el precio en divisa
     |SI #10#70 = "D";  || Si la moneda de trabajo es la divisa ...
          #71#6 = #71#36 / #10#69 * #10#74;  || ... recalculo en funcion de la divisa
     |SINO;
          #71#36 = #71#6 / #10#74 * #10#69; || .. recalculo en funcion de la moneda base
     |FINSI;
     |SI #10#70 = "D";
          #71#38 = #71#6;
     |SINO;
          #71#38 = #71#36;
     |FINSI;
|FPRC;

|PROCESO Comi;
     enDescuento1 = #71#8;
     enDescuento2 = #71#33;
     eaTComision  = #71#16;
     eaCliente    = #10#3;
     eaArticulo   = #71#2;
     eaRepre      = #10#6;
     efFecha      = #10#1;
     enDirEnt = #10#84;
     enAlmacen = #71#3;
     |SI #71#35 ! 0;
          uni_dt = #71#35;
     |SINO;
          uni_dt = #71#5;
     |FINSI;
     uni_dt2 = #71#48;
     |HAZ Comisiones;
|FPRC;

|PROCESO Precio;
     fed_dt = #10#1;   || Fecha....
     art_dt = #71#2;   || Articulo.
     cli_dt = #71#15;  || Cliente..
     fam_dt = #71#13;  || Familia.
     iva_dt = #19#30;  || Tipo Iva.
     dtos_2 = 0;      || Descuento 2.
     dto_pt = 0;      || Descuento Ptas.
     dtos_1 = 0;      || Descuento 1.
     pvp_ve = 0;      || Precio Venta.
     tarifa = #24#39;  || Tarifa Cliente....
     gru_dt = #24#158; || Grupo Cliente.
     nPromo = #10#89;
     |SI #71#35 ! 0;
          uni_dt = #71#35;
     |SINO;
          uni_dt = #71#5;
     |FINSI;
     uni_dt2 = #71#48;
     enMensaje = 0;
     aParam = "";
     eaSerie = #71#29;
     enDirEnt = #10#84;
     enAlmacen = #71#3;
     ||SI #2#176 = "S";  enMensaje = 1;  |FINSI;
     |HAZ calcdtos;
|FPRC;

|PROCESO acttl;|TIPO 0;
     #19#0 = #71#2;
     |LEE 000#19=;

     #71#15 = #10#3;
     #71#16 = #10#34;
     #71#17 = #10#35;
     |HAZ TotalLin71;
     |GRABA 020#71a;

     |HAZ CalCabAgifa010;
     |LIBERA #71;
|FPRC;

|DEFBUCLE agifa071;
     #71#1;
     ;
     #10#52, #10#0, 001;
     #10#52, #10#0, 999;
     be;
     NULL, acttl;
|FIN;

|PROCESO actpf;
     |SI #10#15 ] 0;
          importe = 1;
     |SINO;
          importe = -1;
          #10#15 = -#10#15;
     |FINSI;
     cant = #10#15 < #10#5;
     cant = cant < #10#32;
     cant = cant < #10#7;
     cant = cant * importe;
     #10#15 = #10#15 * importe;
     |ABRE #24;
     #24#0 = #10#3;
     |LEE 101#24=;
     |SI FSalida = 0;
        |SI #10#43 = AN;
           #24#50 = #24#50 + cant;    || es albaran
        |SINO;
           #24#50 = #24#50 - cant;    || es abono
        |FINSI;
        |GRABA 020#24a;
        |LIBERA #24;
     |FINSI;
     |CIERRA #24;

     |SI #10#43 = "N";
          enImpCliPen = cant;
     |SINO;
          enImpCliPen = -cant;
     |FINSI;
     eaCliente = #10#3;
     efFecha = #10#1;
     |HAZ AcumulaCli;

     eaTag = "AV";  |HAZ Riesgos;
|FPRC;

|PROCESO Final;
     |SI aCli = ""; |ACABA; |FINSI;

     |HAZ LimCabAgifa010;
     |ABRE #19;
     |BUCLE agifa071;
     |CIERRA #19;
     |GRABA 020#10a;

     |HAZ actpf;

     |SI #35#16 = "N"; |ACABA; |FINSI;

     aIP = ""; |IP_REMOTO aIP;
     |SI aIP = "";
          |DFICE aAlfa;
          aDjunto = aAlfa + #10#52 + "_" + (("000000" + #10#0)%-106)+ ".pdf";
          |QBT aDjunto;
          eaImpresora = "CrystalReport;" + aDjunto;
          ser_alb = #10#52;
          num_alb = #10#0;
          abono = #10#43;
          |SUB_EJECUTA_NP "agifa014";
     |SINO;
          |DFICE aAlfa;
          aDjunto = aAlfa + #10#52 + "_" + (("000000" + #10#0)%-106)+ ".pdf";
          |QBT aDjunto;
          |ESPECIFICA "RBASS", aBaseLocal;
          aAlfa = aBaseLocal1 + #10#52 + "_" + (("000000" + #10#0)%-106)+ ".pdf";
          |QBT aAlfa;
          eaImpresora = "CrystalReport;" + aAlfa;
          ser_alb = #10#52;
          num_alb = #10#0;
          abono = #10#43;
          |SUB_EJECUTA_NP "agifa014";
          |RECIBE_FICHERO aAlfa, aDjunto;
          |RFBORRA aAlfa;
     |FINSI;

     aIP = "";
     |SI #35#8 = "L";
          |IP_REMOTO aIP;
     |SINO;
          |SI #35#8 = "S";
               |IP_LOCAL aIP;
          |SINO;
               aIP = #35#7; |QBF aIP;
          |FINSI;
     |FINSI;
     aServidor = #35#14; |QBF aServidor;
     aFrom = #35#15; |QBF aFrom;
     aTo = aEmail;
     aSubject = "Confirmación pedido web.";
     aMensaje = "Adjunto pedido:" + #10#52 + "/" + (("000000" + #10#0)%-106);

     aMensaje = "Le adjuntamos PDF con la confirmacion de su pedido realizado a traves de nuestra web.";
     aMensaje = aMensaje + &13 + "Reciba un cordial saludo.";
     aMensaje = aMensaje + &13 + #35#18;
     |QBF aMensaje;

     |SI aTo ! "";
          |DSCORREO_ENVIA aIP, aServidor, aFrom, aTo, aSubject, aMensaje, aDjunto;
     |FINSI;

     aTo = aMailRep1; |QBF aTo;
     |SI aTo ! "";
          |DSCORREO_ENVIA aIP, aServidor, aFrom, aTo, aSubject, aMensaje, aDjunto;
     |FINSI;
     aTo = aMailRep2; |QBF aTo;
     |SI aTo ! "";
          |DSCORREO_ENVIA aIP, aServidor, aFrom, aTo, aSubject, aMensaje, aDjunto;
     |FINSI;

     aTo = #35#19; |QBF aTo;
     |SI aTo ! "";
          aSubject = "Copia de pedido web.";
          aMensaje = "Adjunto pedido:" + #10#52 + "/" + (("000000" + #10#0)%-106);

          aMensaje = "Adjuntamos PDF con la copia del pedido realizado a traves de la web.";
          aMensaje = aMensaje + &13 + "Reciba un cordial saludo.";
          aMensaje = aMensaje + &13 + #35#18;

          |DSCORREO_ENVIA aIP, aServidor, aFrom, aTo, aSubject, aMensaje, aDjunto;
     |FINSI;
|FPRC;

|PROCESO Lineas;
     #53#2 = #49#5;
     |LEE 000#53=;
     |SI FSalida = 0;
          aArti = #53#0;
     |SINO;
          aArti = #49#5;
     |FINSI;

     |DDEFECTO #19;
     |ABRE #19;
     #19#0 = aArti;
     |LEE 000#19=;
     |CIERRA #19;

     nLin = nLin + 1;
     |DDEFECTO #71;
     #71#29 = #10#52
     #71#0 = #10#0;
     #71#1 = nLin;
     #71#2 = #19#0;
     #71#3 = #35#11;
     #71#4 = #19#1;

     |SI nSwChampiEmar = 0;
          |HAZ ChampiEmarEnvase;
     |SINO;
           #71#5 = #49#6
     |FINSI;

     |HAZ Precio;
     |SI aFormato2 = "S";
          #71#6 = #49#31;
          #71#8 = #49#23;
          #71#33 = 0;
     |SINO;
          #71#6 = pvp_ve - dto_pt;       || Precio
          #71#8 = dtos_1;
          #71#33 = dtos_2;
     |FINSI;
     |HAZ precio_divisa;
     |HAZ Comi;
     #71#10 = enComision;      || Comi

     |SI #24#28 = "S";   || Exportacion
          #71#11 = 0;
     |SINO;
          #71#11 = #19#30;
     |FINSI;
     #71#49 = #49#17;
     |GRABA 020#71n;

     #480#0 = #71#2;
     |LEE 000#480=;
     |SI FSalida = 0; |Y #480#33 = "K";      || solo para kits
          |ABRE #47;
          nLinKit = 0;
          |BUCLELIN 2 Kit #480;
          |CIERRA #47;
     |FINSI;

     enForma = 1;
     |HAZ AcumulaAV;

     |SI nSwChampiEmar = 0;
          aAlfa = "-LOTE: " + #49#32;
          |ABRE #153;
          #153#0 = "A";
          #153#1 = #71#29;
          #153#2 = #71#0;
          #153#3 = #71#1;
          #153#4 = 1;
          #153#5 = aAlfa;
          |GRABA 020#153n;
          |CIERRA #153;
     |FINSI;
|FPRC;

|PROCESO Cabecera;
     |DDEFECTO #3;
     |ABRE #3;
     #3#0 = #49#2;
     #3#1 = #49#3;
     |LEE 000#3=;
     |CIERRA #3;

     |LIBERA #10;
     |DDEFECTO #10;
     #10#52 = #35#10;
     enSwMenu = 1;
     |HAZ ContaAV7;

     #10#2 = 0;
     #10#3 = #49#2;
     #10#4 = #24#2;
     #10#5 = #24#37;
     #10#6 = #25#0;
     #10#7 = #24#38;
     #10#8 = #24#20;
     #10#9 = #24#35;
     #10#29 = #3#10;
     #10#30 = #3#2;
     #10#31 = #3#3;
     #10#33 = #3#7;
     #10#34 = #25#7;
     #10#35 = #24#4;
     #10#36 = #24#47;
     #10#40 = #24#41;
     #10#62 = #3#6;
     #10#63 = #24#15;
     #10#68 = #24#192;
     |ABRE #324;
     #324#0 = #10#68;
     |LEE 000#324=;
     #10#69 = #324#2;
     #10#70 = #24#193;
     #10#73 = #321#9;
     #324#0 = #10#73;
     |LEE 000#324=;
     #10#74 = #324#2;
     |CIERRA #324;
     #10#84 = #3#1;
     #10#88 = #24#202;
     #10#91 = #24#206;

     #10#1 = #49#16;
     #10#83 = #49#15;
     #10#55 = #49#12;
     #10#56 = #49#13;

     |SI aFormato2 = "S";
          #10#5 = #49#22;    ||Dto Espe
          #10#13 = #49#30;    ||Varios
          #10#29 = #49#25;   ||Entregar
          #10#30 = #49#26;   ||Dir
          #10#31 = #49#27;   ||Pob
          #10#62 = #49#28;   ||CP
          #10#63 = #49#29;   ||NIF

          |ABRE #40;
          aPob = #10#62 % 102;
          #40#0 = aPob;
          |LEE 000#40=;
          |SI FSalida = 0;
               #10#33 =  #40#1;
          |FINSI;
          |CIERRA #40;

          |SI nSwChampiEmar = 0;
               #10#11 = #49#18;
               #10#81 = #49#18;
               #10#8 = #49#14;     || FPago = Obs3
          |SINO;
               #10#57 = #49#14;
          |FINSI;
     |SINO;
          #10#57 = #49#14;
     |FINSI;

     |GRABA 020#10b;

     aDivisa = #10#68;
     aMoneda = #10#70;
     |HAZ Divisas010;

     aAlfa = #10#55 + #10#56 + #10#57; |QBT aAlfa;
     |SI aAlfa ! "";
          |HAZ EnviaMail;
     |FINSI;
|FPRC;

|PROCESO coima049;
     |ABRE #24;
     #24#0 = #49#2;
     |LEE 000#24=;
     |SI FSalida ! 0; |DDEFECTO #24; |FINSI;
     |CIERRA #24;

     |DDEFECTO #25;
     |ABRE #25;
     #25#0 = #49#4;
     |LEE 000#25=;
     |SI #25#57 = "S";
          |DDEFECTO #25;
          #25#0 = #24#25;
          |LEE 000#25=;
     |FINSI;
     |CIERRA #25;

     |SI nPedido ! #49#0;
          |HAZ Final;

          aMailRep1 = #25#62;
          aMailRep2 = #25#64;

          |ABRE #24;
          #24#0 = #49#2;
          |LEE 000#24=;
          |SI FSalida ! 0; |DDEFECTO #24; |FINSI;
          |CIERRA #24;
          |HAZ Cabecera;

          nPedido = #49#0;
          aEmail = #49#11; |QBF aEmail
          aCli = #49#2;
          nLin = 0;
     |FINSI;

     |HAZ Lineas;

     #49#9 = "S";
     #49#33 = "A";
     #49#34 = #10#52;
     #49#35 = #10#0;
     |GRABA 020#49a;
     |LIBERA #49;
|FPRC;

|DEFBUCLE coima049;
     #49#2;
     ;
     00000001, 000001, "N";
     99999999, 999999, "N";
     be;
     NULL, coima049;
|FIN;

|PROGRAMA;
     |SI PARAMETRO = "formato2";
          aFormato2 = "S";
     |FINSI;

     |HAZ EsChampiEmar; nSwChampiEmar = FSalida;

     |SI nSwChampiEmar = 0;
          |DDEF aDef;
          aAlfa = aDef + "champi04"; |CARGA_DEF aAlfa, refere1@; |ABRE #refere1@;
          aAlfa = aDef + "champi06"; |CARGA_DEF aAlfa, refere3@; |ABRE #refere3@;
     |FINSI;

     |ABRE #142; |LEE 000#142p; |CIERRA #142;
     |ABRE #321; |LEE 000#321p; |CIERRA #321;
     |ABRE #35; |LEE 000#35p; |CIERRA #35;

     nDeci = #142#8;

     |ABRET #10;
     |ABRET #71;
     |ABRE #480;
     |ABRE #47;
     |ABRE #53; |SELECT #2#53;
     |BUCLE coima049;
     |HAZ Final;
     |CIERRA #53;
     |CIERRA #47;
     |CIERRA #480;
     |CIERRAT #71;
     |CIERRAT #10;

     |SI nSwChampiEmar = 0;
          |CIERRA #refere3@; |DESCARGA_DEF #refere3@;
          |CIERRA #refere1@; |DESCARGA_DEF #refere1@;
     |FINSI;
|FPRO;
