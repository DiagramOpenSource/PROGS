|FICHEROS;
     diam0003 #0,MANTE;
     diam0005 #5;
     diam0006 #6;
     agifa104 #104;
     agifa077 #77;
     dsparm05@ #100;
     dsparm01@ #101;
     dsasis04@ #102;
     agifa211 #211;
     diam0001 #1;
     agifa010 #10;

     agifa134 #134;
     agifa059 #59;
     agifa071 #71;
     agifa104 #104;
     diam0001 #201;
     diam0018 #218;
     diam0007 #7;
     diam0008 #8;
     diam0016 #16;
|FIN;

|VARIABLES;
     nHay      = 0;
     aDat      = "";
     aDef1     = "";
     aDef2     = "";
     aDef3     = "";
     aVisa     = "";
     aIP       = "";
     aServidor = "";
     aFrom     = "";
     aTo       = "";
     aSubject  = "";
     aMensaje  = "";
     aDjunto   = "Sin asunto";
     aNume     = "";
     nSw       = 0;
     nDLin     = 0;
     nHLin     = 0;
     nModo     = 0;
     aAlfa     = "";
     aBase     = "";
|FIN;
컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
|PROCESO diam0003;
     #100#0 = #0#2;
     |LEE 000#100=;
     |SI FSalida = 0;
          |SI aTo = "";
               aTo = #100#3;
          |SINO;
               aTo = aTo + ";" + #100#3;
          |FINSI;
          |QBF aTo;
     |FINSI;
|FPRC;

|DEFBUCLE diam0003;
     #0#1003;
     4;
     #104#25, #104#0, 0001, "N";
     #104#25, #104#0, 9999, "N";
     ;
     NULL, diam0003;
|FIN;

|PROCESO EnviaCorreo;
     |CARGA_DEF aDef2, dsparm01@;
     |SI FSalida = 0;

          |PATH_DAT #101 aDat;
          |ABRE #101; |LEE 000#101p; |CIERRA #101;

          #100#0 = #101#12;
          |LEE 020#100=;

          aFrom = #100#3; |QBF aFrom;

          aTo = "";
          |BUCLE diam0003;
          |SI aTo ! ""; |Y aFrom ! "";
               aNume = ("000000" + #104#0) % -106;
               aSubject = "Visado pedido: " + #104#25 + "/" + aNume + " " + #104#8;
               aMensaje = aSubject;
               aServidor = #101#7;
               aIP = #101#8;
               |QBF aIP;
               |QBF aServidor;
               |DSCORREO_ENVIA aIP, aServidor, aFrom, aTo, aSubject, aMensaje, aDjunto;
               |SI FSalida ! 0;
                    |MENAV "0000Ha habido un error durante el envio.";
               |FINSI;
          |FINSI;
          |DESCARGA_DEF #dsparm01@;
     |SINO;
          aDef2 = "0000Error al cargar:" + aDef2;
          |MENAV aDef2;
     |FINSI;
|FPRC;
컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
|PROCESO NomTecnico; |TIPO 0;
     |DDEFECTO #100;
     #100#0 = #0#2;
     |LEE 000#100=;
     |SI FSalida ! 0;
          |MENAV "0000No existe...";
          |ERROR;
     |SINO;
          #0#3 = #100#1;
          |PINTA #0#3;
          |C_M #0#3, 1, "N";
     |FINSI;
|FPRC;

|PROCESO Min005;
     #5#0 = #0#0;
     #5#1 = #0#1;
     #5#2 = #0#2;
     #5#3 = 0001;
|FPRC;

|PROCESO Max005;
     #5#0 = #0#0;
     #5#1 = #0#1;
     #5#2 = #0#2;
     #5#3 = 9999;
|FPRC;

|PROCESO DiagTecnico; |TIPO 11;
     |SI FSalida = 10;
          |CONSULTA_CLAVE #1#100;
          |SI FSalida = 0;
               |C_M #0#3, 1, "S";
               #0#2 = #100#0; |PINTA #0#2;
               #0#3 = #100#1; |PINTA #0#3;
               |PTEC 802;
          |SINO;
               |ERROR;
          |FINSI;
     |FINSI;
     |SI FSalida = 11;
          |PUSHV 0501 2380;
          |ENTLINEAL #1#5, 4, 2, 22, 1, Min005, Max005;
          |POPV;
     |FINSI;
|FPRC;
컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
|PROCESO Bordiam0006;
     |BORRA 020#6a;
|FPRC;

|DEFBUCLE Bordiam0006;
     #6#1;
     ;
     #104#25, #104#0, nDLin, 0001, 001;
     #104#25, #104#0, nHLin, 9999, 999;
     be;
     NULL, Bordiam0006;
|FIN;

|PROCESO Bordiam0005;
     |BORRA 020#5a;
|FPRC;

|DEFBUCLE Bordiam0005;
     #5#1;
     ;
     #104#25, #104#0, nDLin, 0001;
     #104#25, #104#0, nHLin, 9999;
     be;
     NULL, Bordiam0005;
|FIN;

|PROCESO Bordiam0003;
     |BORRA 020#0a;
|FPRC;

|DEFBUCLE Bordiam0003;
     #0#1;
     ;
     #104#25, #104#0, 0001;
     #104#25, #104#0, 9999;
     be;
     NULL, Bordiam0003;
|FIN;

|PROCESO Miradiam0005;
     |SI #5#9 ! 0;
          nSw = 1;
          |ERROR10;
     |FINSI;
|FPRC;

|DEFBUCLE Miradiam0005;
     #5#1;
     ;
     #104#25, #104#0, nDLin, 0001;
     #104#25, #104#0, nHLin, 9999;
     be;
     NULL, Miradiam0005;
|FIN;
컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
|PROCESO Min003;
     #0#0 = #104#25;
     #0#1 = #104#0;
     #0#2 = 1;
|FPRC;

|PROCESO Max003;
     #0#0 = #104#25;
     #0#1 = #104#0;
     #0#2 = 9999;
|FPRC;

|PROCESO Miradiam0003;
     |SI #0#4 = "N";
          aVisa = "";
          |ERROR10;
     |FINSI;
|FPRC;

|DEFBUCLE Miradiam0003;
     #0#1;
     ;
     #104#25, #104#0, 0001;
     #104#25, #104#0, 9999;
     ;
     NULL, Miradiam0003;
|FIN;

|PROCESO BajaVisa; |TIPO 2;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 3;
          nSw = 0;
          nDLin = #0#2;
          nHLin = #0#2;
          |BUCLE Miradiam0005;
          |SI nSw = 0;
               |BUCLE Bordiam0005;
               |BUCLE Bordiam0006;
          |SINO;
               |MENAV "0000Hay Tiquets creados...";
               |ERROR;
          |FINSI;
     |FINSI;
|FPRC;

컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴횱XTERNAS컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
|PROCESO DiagBajAlb;
     |CARGA_DEF aDef3, dsasis04@;
     |SI FSalida = 0;
          |PATH_DAT #102 aDat;
          |ABRE #102;
          |SELECT #4#102;
          #102#105 = #10#52;
          #102#106 = #10#0;
          |LEE 101#102=;
          |SI FSalida = 0;
               |PDEFECTO #102, 96, 97;       || facturado/abierto
               |PDEFECTO #102, 105, 107;     || serie/albaran
               |GRABA 020#102a;
          |FINSI;
          |LIBERA #102;
          |CIERRA #102;

          |DESCARGA_DEF #dsasis04@;
     |SINO;
          aDef3 = "0000Error al cargar:" + aDef3;
          |MENAV aDef3;
     |FINSI;
|FPRC;

|PROCESO DiagBajPed;
     nSw = 0;
     nDLin = 0;
     nHLin = 9999;
     |BUCLE Miradiam0005;
     |SI nSw = 0;
          |BUCLE Bordiam0003;
          |BUCLE Bordiam0005;
          |BUCLE Bordiam0006;

          |ABRE #1;
          |SELECT #2#1;
          #1#14 = #104#25;
          #1#15 = #104#0;
          |LEE 101#1=;
          |SI FSalida = 0;
               #1#14 = "";
               #1#15 = "";
               |GRABA 020#1a;

               |ABRE #16;
               #16#0  = #1#0;
               #16#1  = #1#1;
               #16#13 = "";
               |LEE 101#16];
               |SI FSalida = 0;  |Y #16#0  = #1#0;  |Y  #16#1  = #1#1;
                   #16#14  = "";
                   #16#15  = "";
                   #16#104 = "NO";
                   |GRABA 020#16a;
                   |LIBERA #16;
               |FINSI;
               |CIERRA #16;
          |FINSI;
          |LIBERA #1;
          |CIERRA #1;

          FSalida = 0;
     |SINO;
          |MENAV "0000Hay Tiquets creados...";
          FSalida = 1;
     |FINSI;
|FPRC;

|PROCESO DiagFinPed;
     |CARGA_DEF aDef1, dsparm05@;
     |SI FSalida = 0;
          |PATH_DAT #100 aDat;
          |ABRE #100;

          |PUSHV 0501 2380;
          |BLANCO 821 2380;
          |ENTLINEAL #1#0, 3, 2, 22, 1, Min003, Max003;
          |POPV;

          aVisa = "VISADO";
          |BUCLE Miradiam0003;
          #104#51 = aVisa;

          |CONFI "0000NEnviar Correos";
          |SI FSalida = 0;
               |HAZ EnviaCorreo;
          |FINSI;

          |CIERRA #100;
          |DESCARGA_DEF #dsparm05@;
     |SINO;
          aDef1 = "0000Error al cargar:" + aDef1;
          |MENAV aDef1;
     |FINSI;
|FPRC;

|PROCESO DiagEntAlb;
     |PUSHV 0501 2380;
     |BLANCO 07250941;
     |CUADRO 07250941;
     |ATRI R; |PINTA 0725, "                 "; |ATRI 0;
     |C_P #77#6, 1, 0834;
     |PINTA 0826, "Visado: ";
     |SI #77#6 = "  ";
          #77#6 = "NO";
     |FINSI;

     |PARA; |SI; |HACIENDO;
          |ENCAMPO #6#77;
          #77#6 = #77#6 > "";
          |SI #77#6 = "SI"; |O #77#6 = "NO";
               |SAL;
          |FINSI;
     |SIGUE;
     |POPV;
|FPRC;
컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
|PROCESO diam0003Adj;
     |SI #0#4 ! "S";
          nSw = 1;
          |MENAV "0000Pedido no Visado...";
          |ERROR10;
     |FINSI;
|FPRC;

|DEFBUCLE diam0003Adj;
     #0#1003;
     ;
     #211#31, #211#12, 0001;
     #211#31, #211#12, 9999;
     ;
     NULL, diam0003Adj;
|FIN;

|PROCESO DiagAdjPed;
     nSw = 0;
     |BUCLE diam0003Adj;
     FSalida = nSw;
|FPRC;

|PROCESO DiagObsAdj;
     |ABRE #1;
     |SELECT #2#1;
     #1#14 = #71#31;
     #1#15 = #71#12;
     |LEE 000#1=;
     |SI FSalida = 0;
          |PUSHV 0501 2380;
          |BLANCO 717 1571;
          |CUADRO 717 1571;
          |CUADRO 818 1470;
          |ATRI I; |PINTA 820, " O B S E R V A C I O N E S "; |ATRI 0;
          |ATRI S;
          aAlfa = #1#23; |PINTA  919, aAlfa;
          aAlfa = #1#24; |PINTA 1019, aAlfa;
          aAlfa = #1#25; |PINTA 1119, aAlfa;
          aAlfa = #1#26; |PINTA 1219, aAlfa;
          aAlfa = #1#27; |PINTA 1319, aAlfa;
          |ATRI 0;
          |PAUSA;
          |POPV;
     |FINSI;
     |CIERRA #1;
|FPRC;

|PROCESO Tipo80;  |TIPO 80;
     |DBASS aBase; |QBF aBase;

     aDef1 = aBase + "dspartes/def/dsparm05";
     aDef2 = aBase + "dspartes/def/dsparm01";
     aDef3 = aBase + "dspartes/def/dsasis04";
     aDat  = aBase + "dspartes/fich/";
|FPRC;
컴컴컴컴컴컴컴컴컴컴컴컴컴컴
|VARIABLES;
     &unid = 0;
     &codigo = "";
     &descrip = "";
     &linea = 0;
|FIN;

|PROCESO diam0008;
     linea   = #8#2;
     unid    = #8#4;
     descrip = #8#3;
     codigo  = #8#7; |QBF codigo;

     |SI codigo = "";
          |ABRE #71;
          #71#29 = #59#15;
          #71#0  = #59#2;
          #71#1  = #8#2 / 10;
          |LEE 000#71=;
          |SI FSalida = 0; codigo = #71#2; |FINSI;
          |CIERRA #71;
     |FINSI;

     #71#36  = #8#5;  || precio
     #71#37  = #8#6;  || importe
     |PRINT;
     nHay = 0;
|FPRC;

|PROCESO diam0018;
     #7#0 = #134#49;
     #7#1 = #134#0;
     |LEE 101#7=;
     |SI FSalida ! 0;
         |DDEFECTO #7;
         #7#0 = #134#49;
         #7#1 = #134#0;
         |GRABA 020#7b;
     |FINSI;

     #7#2 = #7#2 + #218#7;
     |GRABA 020#7a;
     |LIBERA #7;

     #8#0 = #7#0;
     #8#1 = #7#1;
     #8#2 = #218#3;
     #8#3 = #218#4;
     #8#4 = #218#5;
     #8#5 = #218#6;
     #8#6 = #218#7;
     #8#7 = #218#8;
     |GRABA 020#8n;
     |LIBERA #8;
|FPRC;

|DEFBUCLE diam0018;
     #218#1;
     ;
     #201#0, #201#1, #201#13, 001;
     #201#0, #201#1, #201#13, 999;
     ;
     NULL, diam0018;
|FIN;

|PROCESO FacDetalle;
     nHay = 1;
     |ABRE #7;
     #7#0 = #134#49;
     #7#1 = #134#0;
     |LEE 000#7=;
     |SI FSalida = 0;
          |SI #7#2 ! #134#88;
               aAlfa = "0000Fra.: [" + #134#49 + "." + #134#0 + "] con TOTAL DETALLE erroneo."
               |MENAV aAlfa;
          |FINSI;
          |BUCLELIN 2 diam0008 #7;
          |CIERRA #7;
          FSalida = nHay;
          |ACABA;
     |FINSI;

     |ABRE #71;
     #71#29 = #59#15;
     #71#0  = #59#2;
     #71#1  = 1;
     |LEE 040#71];
     |SI FSalida ! 0;  |O  #71#29 ! #59#15;  |O #71#0 ! #59#2;
         |DDEFECTO #71;
     |FINSI;
     |CIERRA #71;

     |DDEFECTO #104;
     |ABRE #104;
     #104#25 = #71#31;
     #104#0  = #71#12;
     |LEE 040#104=;
     |SI FSalida ! 0;
          FSalida = nHay;    ||Si no hay pedidos finaliza Tiq: 577/1509
          |ACABA;
     |FINSI;
     |CIERRA #104;

     |ABRE #201;
     |SELECT #2#201;
     #201#14 = #104#25;
     #201#15 = #104#0;
     |LEE 040#201=;
     |SI FSalida ! 0;  |DDEFECTO #201;  |FINSI;
     |CIERRA #201;

     |ABRE #8;
     |BUCLE diam0018;
     |CIERRA #8;

     #7#0 = #134#49;
     #7#1 = #134#0;
     |LEE 000#7=;
     |SI FSalida = 0;
          |BUCLELIN 2 diam0008 #7;
     |FINSI;
     |CIERRA #7;

     FSalida = nHay;
|FPRC;

|PROCESO Recal8;
      #8#6 = #8#4 * #8#5;
      #7#2 = #7#2 + #8#6;
|FPRC;

|PROCESO DiagramRecal;
     |ABRE #7;
     #7#0 = #134#49;
     #7#1 = #134#0;
     |LEE 101#7=;
     |SI FSalida = 0;
          #7#2 = 0;
          |BUCLELIN 2 Recal8 #7;
          |GRABA 020#7a;


          |SI #7#2 ! #134#88;
               aAlfa = "0000Fra.: [" + #134#49 + "." + #134#0 + "] con TOTAL DETALLE erroneo."
               |MENAV aAlfa;
               FSalida = "1"; |ACABA;
          |FINSI;
     |FINSI;
     |CIERRA #7;
     FSalida = 0;
|FPRC;
