|FICHEROS;
     tagz0021 #0;
     agifa019 #19;
     dsm00020 #20;
     tagm0030 #30;
     tagw0043 #43,MANTE;
     agifa079 #79;
     agifa073 #73;
     tagm0086 #86;  || Asig. Rutas
     agifa080 #80;
     agifa104 #104;
     agifa142 #142;
     referen@;
|FIN;

|VARIABLES;
     &Tempo = "";
     nBtnImprime = 0;
     nBtnAcepta = 0;
     nGrid = 0;
     nRango = 0;
     nPanta = 0;
     aAlfa = "";
     aEsc1 = "";
     aEsc2 = "";
     aRetu = "";
     aTecla = "";
     fHoy = @;
     nAge = 0;
     aSwPdte = "";
     nVentana = 0;
 {-1}aVent = "";
     aVent1 = 0;
     aVent2 = 0;
     aVent3 = 0;
     aVent4 = 0;
     aVent5 = "";
     i = 0;
|FIN;

|PROCESO AsignaAge;
     |VENTANA 1101, 2398, -1, 17, "Asignar agencia";
     nVentana = FSalida;
     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVentana;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |PARA i = 0; |SI i [ 12; |HACIENDO i = i + 1;
          #43i = #86i;
     |SIGUE;
     |PINPA #0#43;
     |PINDA #0#43;
     |ENDATOS #2#43;
     |SI FSalida = 0;
          #86#9 = #43#9;
          #86#12 = #43#12;
          |GRABA 020#86a;
          |REFRESCACONTROL nGrid;
     |FINSI;
     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVentana;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;
     |FINVENTANA nVentana;
     |PULSATECLA;
|FPRC;

|PROCESO tagm0086;
     |PRINT;
|FPRC;

|DEFBUCLE tagm0086;
     #86#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, tagm0086;
|FIN;

|PROCESO BtnImprime;
     |SALVA_FICHA #86;
     |IMPRE 0;
     |SI FSalida = 0;
          |INFOR "tagm0086";
          |SI FSalida = 0;
               |BUCLE tagm0086;
               |PIEINF;
               |FININF;
          |SINO;
               |MENAV "0000Error en informe 'tagm0086'";
          |FINSI;
          |FINIMP;
     |FINSI;
     |REPON_FICHA #86;
|FPRC;

|PROCESO AsignaRuta;
     |SI #86#2 = "N";
          #104#25 = #86#0;
          #104#0 = #86#1;
          |LEE 121#104=;
          |SI FSalida = 0;
               #104#60 = #86#9;
               |GRABA 020#104a;
               |LIBERA #104;
          |FINSI;
     |SINO;
          #30#0 = #86#0;
          #30#1 = #86#1;
          |LEE 121#30=;
          |SI FSalida = 0;
               #30#31 = #86#9;
               |GRABA 020#30a;
               |LIBERA #30;
          |FINSI;
     |FINSI;

     #referen@#0 = #86#0;
     #referen@#1 = #86#1;
     #referen@#2 = #86#2;
     |LEE 101#referen@.=;
     |SI FSalida ! 0; |GRABA 020#referen@.b; |FINSI;
     |SALVA_DATOS #86;
     |REPON_DATOS #referen@;
     |GRABA 020#referen@.a;
     |LIBERA #referen@;
|FPRC;

|DEFBUCLE AsignaRuta;
     #86#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, AsignaRuta;
|FIN;

|PROCESO BtnAcepta;
     |ESTADO_CONTROL nBtnImprime, "DISABLE";
     |ESTADO_CONTROL nBtnAcepta, "DISABLE";
     |ABRE #104;
     |ABRE #30;
     |ABRE #referen@;
     |BUCLE AsignaRuta;
     |CIERRA #referen@;
     |CIERRA #30;
     |CIERRA #104;

     |PTEC 807;
|FPRC;

|PROCESO tagm0030;
     #80#0 = #30#0;
     #80#1 = #30#1;
     |LEE 000#80=;
     |SI FSalida = 0; |Y #80#2 = "S";
          |ACABA;
     |FINSI;
     |SI #30#29 [ 1; |ACABA; |FINSI;

     |DDEFECTO #86;
     #86#0 = #30#0;
     #86#1 = #30#1;
     #86#2 = "S";
     #86#3 = #30#2;
     #86#4 = #30#3;
     #86#5 = #30#6;
     #86#6 = #30#4;
     #86#7 = 1;
     #86#8 = #30#23;

     #referen@#0 = #86#0;
     #referen@#1 = #86#1;
     #referen@#2 = "S";
     |LEE 000#referen@.=;
     |SI FSalida = 0;
          nAge = #referen@#9;
     |SINO;
          nAge = #30#1;
     |FINSI;

     #86#9 = nAge;
     |SI #0#1 = "S"; |Y nAge ! 99;
          #86#9 = 0;
     |FINSI;

     #86#10 = #30#19;
     #86#11 = "";
     |DDEFECTO #20;
     #20#0 = #86#9;
     |LEE 000#20=;
     #86#12 = #20#1;

     |GRABA 020#86n;
|FPRC;

|DEFBUCLE tagm0030;
     #30#4;
     ;
     #0#0, " ", "     ", 000000;
     fHoy, " ", "zzzzz", 999999;
     ;
     NULL, tagm0030;
|FIN

|PROCESO CargaTmp;
     |SI aSwPdte = "S";
          |DDEFECTO #86;
          #86#0 = #104#25;
          #86#1 = #104#0;
          #86#2 = "N";
          #86#3 = #104#1;
          #86#4 = #104#4;
          #86#5 = #104#8;
          #86#6 = #104#7;
          #86#7 = #104#57;
          #86#8 = #104#59;

          #referen@#0 = #86#0;
          #referen@#1 = #86#1;
          #referen@#2 = "N";
          |LEE 000#referen@.=;
          |SI FSalida = 0;
               nAge = #referen@#9;
          |SINO;
               nAge = #104#60;
          |FINSI;

          #86#9 = nAge;
          |SI #0#1 = "S"; |Y nAge ! 99;
               #86#9 = 0;
          |FINSI;

          #86#10 = #104#20;
          #86#11 = #104#19;

          |DDEFECTO #20;
          #20#0 = #86#9;
          |LEE 000#20=;
          #86#12 = #20#1;

          |GRABA 020#86n;
     |FINSI;
|FPRC;

|PROCESO agifa073;
     #19#0 = #73#2;
     |LEE 000#19=;
     |SI #73#2 ! "                    "; |Y FSalida = 0;
          |SI #142#224 = "B";
               |SI #19#179 = "S";
                    |SI #73#43 = #73#5; |Y #73#49 = #73#44; |ACABA; |FINSI;
               |SINO;
                    |SI #73#43 = #73#5;  |ACABA; |FINSI;
               |FINSI;
          |SINO;
               |SI #73#43 = #73#5;  |ACABA; |FINSI;
               |SI #19#179 = "S";
                    |SI #73#49 = #73#44; |ACABA; |FINSI;
               |FINSI;
          |FINSI;
     |FINSI;
     aSwPdte = "S";
|FPRC;

|PROCESO agifa104;
     aSwPdte = "N";
     |SI #104#12 [ 1; |ERROR; |FINSI;
     #79#0 = #104#25;
     #79#1 = #104#0;
     |LEE 000#79=;
     |SI FSalida = 0; |Y #79#2 = "S"; |ERROR; |FINSI;
|FPRC;

|DEFBUCLE agifa104;
     #104#7;
     ;
     #0#0, "      ", 0001, "     ", 000001;
     fHoy  , "zzzzzz", 9999, "zzzzz", 999999;
     ;
     NULL, agifa104, agifa073, CargaTmp;
|FIN;

|PROCESO Evento;
     |SI FSalida = 1; |O FSalida = 2;
          |LEE 000#0=;
     |FINSI;
     |SI FSalida = 4;
          |HAZ AsignaAge;
     |FINSI;
|FPRC;

|PROCESO Min;
     #86#6 = "  ";
     #86#4 = "      ";
     #86#0 = "     ";
     #86#1 = 1;
|FPRC;

|PROCESO Max;
     #86#6 = "zz";
     #86#4 = "zzzzzz";
     #86#0 = "zzzzz";
     #86#1 = 999999;
|FPRC;

|PROCESO Tipo12; |TIPO 12;
|FPRC;

|PROCESO Tipo80; |TIPO 80;
     FSalida = 3299;
|FPRC;

|PROGRAMA;
     |ABRE #142; |LEE 000#142p; |CIERRA #142;

     |CLS;
     |PINPA #0#0; nPanta = FSalida;
     |PINDA #0#0;

     |DDEF aAlfa;
     aAlfa = aAlfa + "tagm0086";
     |CARGA_DEF aAlfa, referen@;

     |HAZ CreaTempo; |NOME_DAT #86 Tempo; |DELINDEX #86;
     |ABRET #86;

     nRango = 4 + 8 + 16 + 32;
     |LINEAL_SIMPLE #2#86, nRango, 1002, 3098, Min, Max, Evento;
     nGrid = FSalida;

     |ENDATOS #1#0;
     |SI FSalida = 0;
          fHoy = ~;
          |ABRE #referen@
          |ABRE #20;
          |ABRE #79;
          |ABRE #19;
          |BUCLE agifa104;
          |CIERRA #19;
          |CIERRA #79;
          |ABRE #80;
          |BUCLE tagm0030;
          |CIERRA #80;
          |CIERRA #20;
          |CIERRA #referen@;
          |LEE 000#86p;

          |DESTRUYECONTROL nGrid;
          |LINEAL_SIMPLE #2#86, nRango, 1002, 3098, Min, Max, Evento;
          nGrid = FSalida;

          |CONTROL_SIMPLE nPanta, "BOTON,{{205}}Imprimir", 3102, 3114, BtnImprime;
          nBtnImprime = FSalida;
          |CONTROL_SIMPLE nPanta, "BOTON,{{206}}Aceptar", 3116, 3128, BtnAcepta;
          nBtnAcepta = FSalida;
          aEsc1  = &255 + "806";
          aEsc2  = &255 + "807";
          aRetu  = &255 + "802";
          |PARA; |SI;  |HACIENDO;
               FSalida = "::{{207}}Cancelar";
               |LEETECLA aTecla;
               |SI aTecla = aEsc1; |SAL;  |FINSI;
               |SI aTecla = aEsc2; |SAL;  |FINSI;
               |SI aTecla = aRetu; |SAL;  |FINSI;
          |SIGUE;
     |FINSI;
     |CIERRAT #86;
     |HAZ BoraTempo; |DELINDEX #86;

     |FIN_CONTROL_WINDOWS nBtnImprime;
     |FIN_CONTROL_WINDOWS nBtnAcepta;
     |DESTRUYECONTROL nGrid;
     |PULSATECLA;

     |DESCARGA_DEF #referen@;
|FPRO;
