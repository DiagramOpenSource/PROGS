|FICHEROS;
   godiaact #0;

   godiacaj #10;
   godiacal #11;
   agifa077 #12;
   agifa080 #13;
   goparame #14;
   agifa019 #15;
   agifa324 #16;
   agifa112 #17;
   agifa321 #18;
   agifa079 #19;
   agifa091 #20;
   agifa177 #21;
   dsz99984 #22;
   godiatas #23;
   agifa716 #24;
   godiatmp #25;
   godiatm5 #26;
   agifa704 #27;
   agifa029 #28;
   godiafpa #29;

   gopartia #30;
   agifa722 #31;
   gosubaux #32;

   agifa730 #50;
   tmp00078 #78;
|FIN;

|VARIABLES;
     aA쨚 = "";
     j = 0;
     &enContaFac = 0;
     &Tempo = "";
     &aTmp1 = "";
     &aTmp2 = "";
   &enModiFac = 0;
   &enNoModif = 0;
   &efFecha = @;
   &cCodMonTrab = "";
   &qSufac = "";

   &eaDocu    = "";
   &eaUsuario = "";
   &eaFecha   = "";
   &qSerie    = "";
   &qSerieF   = "";
   &qAlbar    = 0;
   &qFecha    = @;
   &qAbono    = "";

   &decim_divisa    = 0; || Decimales de la divisa
   &precio_divisa   = 0; || Decimales del precio en divisa
   &nDeci_Div       = 0; || Decimales en Totales de la divisa
   &nDeci_PreVis    = 0; || Decimales en el Precio visual. (lineas)
   &nDeci_ImpVis    = 0; || Decimales en el Importe visual. (lineas)
   &nDeci_Base      = 0; || Decimales en Totales de la moneda base
   &decim_base      = 0;  || Decimales de la moneda base
   &precio_base     = 0; || Decimales del precio en moneda base
   &nDeci_PreDiv    = 0;
   &nDeci_PreBase   = 0;
   &nDeci_BaseMx    = 0;
   &nDeci_PreBaseMx = 0;
   &nDeci_TotVis    = 0;
   &nDeci_TotNoVis  = 0;

   &enForma   = 0;
   &enDDocum  = 0;
   &enHDocum  = 0;
   &enSwMenu  = 0;

   &eaNombre  = "";
   &eaCif     = "";

   nCalc = 0;
   nCont = 0;

   SerB = ""; SerZ = "";
   NumB = 0;  NumZ = 0;
   Bl   = ""; ZZ   = "";
   Primero = 1;

   nLinea  = 0;
   nPPort1 = 0;
   nPPort2 = 0;
   nImpDiv = 0;
   nSwLin = 0;

   aAntUsuario = "";
   fAntFecha   = @;
   nAntDocum   = 0;
   aAntSExp    = "";

   i           = 0;
   nCamImp     = 0;
   nCamFec     = 0;
   nNumRecibos = 0;
   nNumFac     = 0;

   aAlfa       = "";
   aSerieFac   = "";
   aBase          = "";
   aPathCorreo    = "";
   aFichero       = "";
   nHandleCorreo  = 0;
   aTo            = "";
   aIP            = "";
   aSalida        = "";
   aFrom          = "";
   aAsunto        = "";
   aMensaje       = "";
   &eaToIncidencia = "";
|FIN;

|PROCESO Param_Divisas; |TIPO 0;
     |ABRE #agifa321;
     |LEE 000#agifa321.p;
     |SI FSalida ! 0; |DDEFECTO #agifa321; |FINSI;
     |CIERRA #agifa321;

     |ABRE #agifa324;
     #agifa324#0 = #agifa321#9;
     |LEE 001#agifa324.=;   || Leo la moneda base

     decim_base = #agifa324#7;    ||Asigno los decimales de la moneda base
     precio_base = #agifa324#9;   ||Asigno los decimales del precio en la mon. base
     #agifa077#61 = #agifa321#9;
     #agifa077#62 = #agifa324#3;

     #agifa324#0 = #agifa077#56;
     |LEE 001#agifa324.=;   || Leo la divisa
     |CIERRA #agifa324;

     decim_divisa = #agifa324#7;  || Decimales de la divisa
     precio_divisa = #agifa324#9;  || Decimales en el precio de la divisa
     nDeci_Div = decim_divisa;   || Decimales de la divisa
     nDeci_PreDiv = precio_divisa;  || Decimales en el precio de la divisa
     |SI #agifa077#58 = "B";   ||Si trabajo con la moneda base ...
          nDeci_PreVis   = precio_divisa;
          nDeci_ImpVis   = decim_divisa;
          nDeci_Base     = decim_base;
          nDeci_PreBase  = precio_base;
          nDeci_BaseMx  = decim_base;
          nDeci_PreBaseMx = precio_base;
          nDeci_TotVis   = decim_base;
          nDeci_TotNoVis = decim_divisa;
     |SINO;     ||Si trabajo con la divisa ...
          nDeci_PreVis   = precio_base;
          nDeci_ImpVis   = decim_base;
          nDeci_Base     = decim_base;
          nDeci_PreBase  = precio_base;
         |SI #agifa077#57 = 1;
              nDeci_BaseMx    = decim_base;    || sin triangulacion
              nDeci_PreBaseMx = precio_base;
         |SINO;
              nDeci_BaseMx    = #agifa321#10;  || con triangulacion
              nDeci_PreBaseMx = #agifa321#11;
         |FINSI;
          nDeci_TotVis     = decim_divisa;
          nDeci_TotNoVis   = decim_base;
     |FINSI;
|FPRC;

|PROCESO GrabaGopartia;
   |SI #agifa077#58 = "B";
      cCodMonTrab = #agifa077#61;
   |SINO;
      cCodMonTrab = #agifa077#56;
   |FINSI;
   |HAZ MonBas;

   |ABRE #agifa029;
   #agifa029#0 = #godiacal#24;
   |LEE 000#agifa029.=;
   |SI FSalida ! 0; |DDEFECTO #agifa029; |FINSI;
   |CIERRA #agifa029;

   |DDEFECTO #30;
   #30#0  = #godiacal#7;
   #30#1  = #godiacal#8;
   #30#2  = #godiacal#5;
   #30#3  = #godiacal#6;
   #30#4  = #godiacal#19;
   #30#5  = #godiacal#20;
   #30#6  = #agifa080#1;
   #30#7  = 1;
   #30#8  = #godiacal#10;
   #30#9  = #godiacal#14;
   #30#10 = #godiacal#26;
/*
   #30#11 = #godiacal#11;
   #30#12 = #godiacal#11;
   #30#13 = #godiacal#11;
   #30#14 = #godiacal#11;
*/
   #30#15 = #godiacal#22;
   #30#16 = #godiacal#23;
   #30#17 = #godiacal#24;

   #30#18 = #agifa029#1;
   #30#20 = #godiacal#3;
   #30#21 = #agifa077#61;
/*
   #30#22 = #godiacal#11;
   #30#23 = #godiacal#11;
   #30#24 = #godiacal#11;
   #30#25 = #godiacal#11;
*/
   #30#27 = "01.01.0000";
   #30#28 = #agifa077#1;
   || #30#29 = "C";      antes era asi, ahora se le crea un campito para pedir
                     ||  el tipo de articulo (MANGEL: 15/05/2006)
   #30#29 = #godiacal#25;

   #30#30 = #godiacal#4;
   #30#33 = #godiacal#9;
   #30#34 = "C";
   #30#36 = #godiacal#26;

     #30#37 = #godiacal#27;
     #30#38 = 0;
     #30#39 = 0;
     #30#40 = 0;
     #30#41 = #godiacal#27;
     #30#42 = 0;
     #30#43 = 0;
     #30#44 = 0;

     |SI #godiacal#30 = "S";
          #30#37 = #godiacal#29;
          #30#45 = #godiacal#29;
     |FINSI;

     |HAZ CalcuGopartia;

     |SI #14#70 = "F"; |HAZ PreForestales; |FINSI;
     |GRABA 020#30n;

     |ABRE #32;
     #32#0 = #30#34;
     #32#1 = #30#4;
     #32#2 = #30#5;
     #32#3 = #30#6;
     #32#4 = #30#7;
     #32#5 = #godiacal#28;
     #32#6 = "N";
     #32#7 = #30#28;
     #32#8 = #30#14;
     #32#9 = 0;
     #32#13 = 1;
     |GRABA 020#32n;
     |CIERRA #32;
|FPRC;

|PROCESO SerieFactura;
     |ABRE #godiatas;
     #godiatas#0 = #godiacaj#3;
     |LEE 000#godiatas.=;
     |SI FSalida = 0;
        |SI #godiatas#2 = "S";
           enNoModif = 100;
        |SINO;
           |SI #godiatas#7 = "N";
              enNoModif = 110;
           |FINSI;
        |FINSI;
        qSerieF = #godiatas#5;
     |SINO;
          qSerieF = "";
     |FINSI;
     |CIERRA #godiatas;
|FPRC;

|PROCESO CreaAlbaranes;
   |SI aAntUsuario ! #godiacal#0; |O fAntFecha ! #godiacal#15; |O nAntDocum ! #godiacal#1; |O aAntSExp ! #godiacal#5;
      |SI Primero = 1;
         Primero = 0;
      |SINO;
         || Ejecuto el proceso de recalculo de la cabecera del albaran
         |HAZ CalCabAgifa077;
         qSerie = #agifa077#50;
         qAlbar = #agifa077#0;
         qFecha = #agifa077#1;
         qAbono = #agifa077#42;

         |HAZ SerieFactura;

         |GRABA 020#agifa077.a; |LIBERA #agifa077;
         #agifa730#0 = #agifa077#50; #agifa730#1 = #agifa077#0; |GRABA 020#agifa730.n;
      |FINSI;
      aAntUsuario = #godiacal#0;
      fAntFecha   = #godiacal#15;
      nAntDocum   = #godiacal#1;
      aAntSExp    = #godiacal#5;

      || Recojo el contador del albaran ........
      #agifa077#50 = #godiacal#5;
      enSwMenu = 1; |HAZ ContaAC7;
      #agifa077#1 = #godiacal#15;
      || Grabo la cabecera del albaran con los datos basicos
      ||   (Proveedor, forma pago, etc). El resto de campos luego se recalculan ......

      #agifa112#0 = #godiacaj#4;
      |LEE 000#agifa112.=;

      #agifa077#56 = #agifa112#81;
      #agifa077#58 = #agifa112#82;
      || Recojo los campos decimales para divisas .....
      |HAZ Param_Divisas;
      #agifa077#3  = #godiacaj#4;
      #agifa077#4  = #godiacaj#37;
      #agifa077#5  = #agifa112#24;
      #agifa077#7  = #agifa112#25;
      #agifa077#8  = #godiacaj#39;
      #agifa077#29 = #48#1;
      #agifa077#30 = #48#2;
      #agifa077#31 = #48#3;
      #agifa077#32 = #agifa112#41;
      #agifa077#33 = #48#4;
      #agifa077#34 = "AS";
      #agifa077#36 = #agifa112#58;
      #agifa077#52 = #godiacaj#36;
      #agifa077#53 = #godiacaj#12;
      #agifa077#54 = #godiacaj#7;
      #agifa077#57 = #agifa324#3;
      #agifa077#72 = #agifa112#87;
      |GRABA 020#agifa077.b;
      nLinea = 1;
   |FINSI;

   || Grabo las lineas del albaran con el articulo, almacen, unidades y precio.
   ||   El resto de campos se recalculan luego ......
   |DDEFECTO #agifa080;
   #agifa080#27 = #agifa077#50;
   #agifa080#0  = #agifa077#0;
   #agifa080#1  = nLinea; nLinea = nLinea + 1;
   #agifa080#2  = #godiacal#3;
   #agifa080#3  = #goparame#22;
   #agifa080#4  = #godiacal#4;
   #agifa080#5  = #godiacal#26;
   #agifa080#6  = #godiacal#27;
   #agifa080#7  = #godiacal#11;
   #agifa080#11 = #godiacal#12;
   #agifa080#15 = #godiacaj#4;
   |GRABA 020#agifa080.b;

   || Recalculo la linea ....
   |ABRE #agifa019;
   #agifa019#0 = #agifa080#2;
   |LEE 020#agifa019.=;
   |CIERRA #agifa019;

   |SI #agifa077#58 = "D";                      || Si mon. de trabajo es divisa ...
        #agifa080#6  = #agifa080#30 / #agifa077#57 * #agifa077#62; || Recalculo precio en mon. base
        #agifa080#32 = #agifa080#6;                  || Precio visual, en mon. base
        nPPort1 = #agifa080#42 / #agifa077#57 * #agifa077#62;
        nPPort2 = #agifa080#42;
   |SINO;                               || Si mon. de trabajo es mon. base
        #agifa080#30 = #agifa080#6 / #agifa077#62 * #agifa077#57; || Recalculo precio en divisa
        #agifa077#32 = #agifa077#30;                || Precio visual, en divisa
        nPPort1 = #agifa080#42;
        nPPort2 = #agifa080#42 / #agifa077#62 * #agifa077#57;
   |FINSI;

   |SI #agifa019#196 = 2;
        #agifa080#7 = #agifa080#36 * #agifa080#6;
        nImpDiv = (#agifa080#36 * #agifa080#30) ! nDeci_PreDiv;
   |SINO;
        #agifa080#7 = #agifa080#5 * #agifa080#6;
        nImpDiv = (#agifa080#5  * #agifa080#30) ! nDeci_PreDiv;
   |FINSI;

   #agifa080#9  = ((#agifa080#7    < #agifa080#8) < #agifa080#26) < #agifa080#45;
   #agifa080#31 = ((nImpDiv < #agifa080#8) < #agifa080#26) < #agifa080#45;

   |SI #agifa019#196 = 2;
        #agifa080#9  = #agifa080#9  + (#agifa080#36 * nPPort1);  || Portes Linea
        #agifa080#31 = #agifa080#31 + (#agifa080#36 * nPPort2);  || Portes Linea
   |SINO;
        #agifa080#9  = #agifa080#9  + (#agifa080#5  * nPPort1);  || Portes Linea
        #agifa080#31 = #agifa080#31 + (#agifa080#5  * nPPort2);  || Portes Linea
   |FINSI;

   |SI #agifa077#58 = "D";     || Si mon. de trabajo es divisa ...
        #agifa080#33 = #agifa080#9;   || Importe visual, en mon. base
   |SINO;               || Si mon. de trabajo es mon. base ...
        #agifa080#33 = #agifa080#31;  || Importe visual, en divisa
   |FINSI;

   |GRABA 020#agifa080.a; |LIBERA #agifa080;

   #godiatmp#0 = #agifa080#27;
   #godiatmp#1 = #agifa080#0;
   #godiatmp#2 = #agifa080#1;
   |LEE 101#godiatmp.=;
   |SI FSalida ! 0;
      |DDEFECTO #godiatmp;
      #godiatmp#0 = #agifa080#27;
      #godiatmp#1 = #agifa080#0;
      #godiatmp#2 = #agifa080#1;
      |GRABA 020#godiatmp.b;
   |FINSI;
   #godiatmp#3 = #godiacal#0;
   #godiatmp#4 = #godiacal#15;
   #godiatmp#5 = #godiacal#1;
   #godiatmp#6 = #godiacal#2;
   |GRABA 020#godiatmp.a; |LIBERA #godiatmp;

   #godiacal#19 = #agifa080#27;
   #godiacal#20 = #agifa080#0;
   #godiacal#21 = #agifa080#1;
   |GRABA 020#godiacal.a;

   || Ejecuto el proceso de recalculo de stocks
   eaDocu = "agifa080"; enForma = 1; |HAZ StockEntra;
   eaDocu = "agifa077"; enForma = 1; |HAZ StockEntra;
   |HAZ GrabaGopartia;
|FPRC;

|DEFBUCLE CreaAlbaranes;
#godiacal#2;
;
SerB,NumB,SerB,NumB,Bl,Bl,Bl,#10#0,#10#2,#10#1;
SerZ,NumZ,SerZ,NumZ,ZZ,ZZ,ZZ,#10#0,#10#2,#10#1;
;
NULL,CreaAlbaranes;
|FIN;

|PROCESO CreaAlbs;
   Primero = 1;
   aAntUsuario = ""; fAntFecha = @; nAntDocum = 0; aAntSExp = "";
   |BUCLE CreaAlbaranes;

   || Ejecuto el proceso de recalculo de la cabecera del albaran
   |HAZ CalCabAgifa077;
   qSerie = #agifa077#50;
   qAlbar = #agifa077#0;
   qFecha = #agifa077#1;
   qAbono = #agifa077#42;

   |ABRE #godiatas;
   #godiatas#0 = #godiacaj#3;
   |LEE 000#godiatas.=;
   |SI FSalida = 0;
      |SI #godiatas#2 = "S";
         enNoModif = 100;
      |SINO;
         |SI #godiatas#7 = "N";
            enNoModif = 110;
         |FINSI;
      |FINSI;
      qSerieF = #godiatas#5;
   |SINO;
      qSerieF = "";
   |FINSI;
   |CIERRA #godiatas;
   |GRABA 020#agifa077.a; |LIBERA #agifa077;

   #agifa730#0 = #agifa077#50; #agifa730#1 = #agifa077#0; |GRABA 020#agifa730.n;

   #agifa112#0 = #agifa077#3;
   |LEE 101#agifa112.=;
   |SI FSalida = 0;
      aAlfa = #agifa077#1; |QBF aAlfa;
      aAlfa = aAlfa % 0402; nCalc = aAlfa;
      nCalc = nCalc + 26;
      #agifa112#nCalc# = #agifa112#nCalc# + #agifa077#65;
      |GRABA 020#agifa112.a; |LIBERA #agifa112;
   |FINSI;

|FPRC;

|PROCESO AdjFactura;
   enContaFac = #godiacaj#33;
   qSufac = #agifa077#54;
   |PUSHV 0101 2480;
   aAlfa = "agifa229;ta" + Usuario + ",NoVtos";
   |SUB_EJECUTA_NP aAlfa;       ||Facturar Albaranes
   |POPV;
   |CIERRA #agifa730;
   aAlfa = "ta" + Usuario; |NOME_DAT #50 aAlfa;
   |DELINDEX #agifa730; |ABRE #agifa730;
|FPRC;

|PROCESO RellenaVtos;
   nCalc = nCont + 67;
   |SI nCalc > 73; nCalc = nCalc + 55; |FINSI;
   #agifa079#nCalc# = #godiafpa#2;
   nCalc = nCont + 32;
   #agifa079#nCalc# = #godiafpa#3;
   nCont = nCont + 1;
|FPRC;

|DEFBUCLE RellenaVtos;
#godiafpa#1;
;
#godiacaj#1,01;
#godiacaj#1,99;
;
NULL,RellenaVtos;
|FIN;

|PROCESO CreaRecibos;
   #godiatm5#0 = #agifa077#51;
   #godiatm5#1 = #agifa077#39;
   |LEE 101#godiatm5.=;
   |SI FSalida ! 0;
      |DDEFECTO #godiatm5;
      #godiatm5#0 = #agifa077#51;
      #godiatm5#1 = #agifa077#39;
      |GRABA 020#godiatm5.b;
   |FINSI;
   #godiatm5#2 = #godiacaj#0;
   #godiatm5#3 = #godiacaj#2;
   #godiatm5#4 = #godiacaj#1;
   |GRABA 020#godiatm5.a; |LIBERA #godiatm5;

   #agifa079#66 = #agifa077#51;
   #agifa079#0  = #agifa077#39;
   |LEE 101#agifa079.=;
   |SI FSalida = 0;
      #agifa112#0 = #godiacaj#4;
      |LEE 000#agifa112.=;
      |SI FSalida = 0;
         |HAZ EsMinana; || Aqui es el primer punto que salta en Mi쨅na PEPE
         |SI #agifa079#12 ! #agifa112#10; |Y FSalida = 0;
            #agifa079#12 = #agifa112#10;
         |FINSI;
      |SINO;
         aAlfa = "    No encuentro la ficha del proveedor " + #godiacaj#4;
         |MENAV aAlfa;
      |FINSI;

      |PARA j = 33; |SI j [ 44; |HACIENDO j=j+1; #agifa079#j# = 0; |SIGUE;
      |PARA j = 68; |SI j [ 73; |HACIENDO j=j+1; #agifa079#j# = "01.01.0000"; |SIGUE;
      |PARA j = 129; |SI j [ 134; |HACIENDO j=j+1; #agifa079#j# = "01.01.0000"; |SIGUE;
      nCont = 1; |BUCLE RellenaVtos;
      |GRABA 020#agifa079.a; |LIBERA #agifa079;

      #agifa112#0 = #godiacaj#4;
      |LEE 000#agifa112.=;
      |SI FSalida = 0;
          ||
      |SINO;
         aAlfa = "    No encuentro la ficha del proveedor " + #godiacaj#4;
         |MENAV aAlfa;
      |FINSI;

      |ABRE #agifa091;
      #agifa091#0 = #agifa079#11;
      |LEE 000#agifa091.=;
      |CIERRA #agifa091;

      |DDEFECTO #dsz99984;
      #dsz99984#0  = #agifa079#0;
      #dsz99984#17 = #agifa079#66;
      |PARA i = 1;|SI i [ 12; |HACIENDO i = i + 1;
          nCamImp = i + 32;
          |SI #agifa079#nCamImp# ! 0; nNumRecibos = i; |FINSI;
      |SIGUE;

      |HAZ AbreRecCompra;
      |PARA i = 1;|SI i [ 12; |HACIENDO i = i + 1;
          nCamImp = i + 32;
          ||SI #agifa079#nCamImp# = 0; |O i > 12; |ACABA; |FINSI;
          |SI #agifa079#nCamImp# = 0; |O i > 12; |SAL; |FINSI;
          |SI i [ 6;
               nCamFec = i + 67;
          |SINO;
               nCamFec = i + 121;
          |FINSI;
          #dsz99984#7 = #agifa079#1;
          #dsz99984#8 = #agifa079#nCamFec#;
          #dsz99984#9 = #agifa079#nCamImp#;
          #dsz99984#1 = i;
          #dsz99984#2 = #agifa079#2;
          #dsz99984#3 = #agifa079#3;
          #dsz99984#4 = #agifa079#9;
          #dsz99984#12 = #agifa079#12;
          #dsz99984#18 = #agifa091#69;      ||Tipo de Recibo
          #dsz99984#21 = #agifa091#69;
          #dsz99984#23 = nNumRecibos;
          #dsz99984#24 = #agifa091#0;
          #dsz99984#25 = #agifa091#1;

          |ABRE #agifa177;
          #agifa177#0 = #dsz99984#18;
          |LEE 000#agifa177.=;
          |CIERRA #agifa177;

          #dsz99984#15 = #agifa177#3;      ||Cuenta
          |HAZ RecCompra;
      |SIGUE;
      |HAZ CierraRecCompra;
   |FINSI;
|FPRC;

|PROCESO EnlaceCont;
   |ABRE #agifa704;
   #agifa704#0 = #godiatas#4;
   |LEE 000#agifa704.=;
   |SI FSalida = 0;
      |SI #agifa704#8 = "S";
         nSwLin = 1;
      |SINO;
         nSwLin = 0;
      |FINSI;
   |FINSI;
   |CIERRA #agifa704;

   aAlfa = "ac" + Usuario; |NOME_DAT #agifa716#aAlfa#;
   |DELINDEX #agifa716; |ABRE #agifa716;
   |DDEFECTO #agifa716;
   #agifa716#15 = #godiatas#4; #agifa716#0 = 1;
   #agifa716#11 = "A";         #agifa716#12 = 5;
   |SI nSwLin = 0;
      #agifa716#13 = "agifa079";  #agifa716#14 = 66;
   |SINO;
      #agifa716#13 = "agifa080";  #agifa716#14 = 28;
   |FINSI;
   #agifa716#3 = #agifa079#66; #agifa716#4 = #agifa079#66;
   |GRABA 020#agifa716.n;
   |DDEFECTO #agifa716;
   #agifa716#15 = #godiatas#4; #agifa716#0 = 2;
   #agifa716#11 = "N";         #agifa716#12 = 6;
   |SI nSwLin = 0;
      #agifa716#13 = "agifa079";  #agifa716#14 = 0;
   |SINO;
      #agifa716#13 = "agifa080";  #agifa716#14 = 25;
   |FINSI;
   #agifa716#5 = #agifa079#0; #agifa716#6 = #agifa079#0; || PEPE1
   |GRABA 020#agifa716.n;
   |LIBERA #agifa716; |CIERRA #agifa716;
   eaNombre = #godiacaj#37;
   eaCif    = #godiacaj#38;

   aAlfa = #godiatas#4; |QBF aAlfa;
   aAlfa = aAlfa + ("*" + (#godiacaj#2 % -104));
   aAlfa = "agifa711.tab;" + aAlfa + ",CAJA";
   |SUB_EJECUTA_NP aAlfa;
   eaNombre = "";
   eaCif    = "";
|FPRC;

|PROCESO Incidencia;
     |MENSA "0000 Enviando incidencia.";

     |DBASE aBase;
     aPathCorreo = aBase + "/correo";  |MKDIR aPathCorreo;
     aFichero    = aPathCorreo + "/incidencia.txt";
     |XABRE "W", aFichero, nHandleCorreo;
     |SI FSalida ] 0;
         aAlfa = "Incidencia en la Actualizacion datos Diario de Caja" + &13 + &10;
         |XGRABA nHandleCorreo, aAlfa;

         aAlfa = "" + &13 + &10;                   |XGRABA nHandleCorreo, aAlfa;

         aAlfa = "SERIE: "     + #agifa079#66  + &13 + &10;  |XGRABA nHandleCorreo, aAlfa;
         aAlfa = "ALBARAN: "   + #agifa079#0   + &13 + &10;  |XGRABA nHandleCorreo, aAlfa;
         aAlfa = "PROVEEDOR: " + #agifa079#2 + &13 + &10;    |XGRABA nHandleCorreo, aAlfa;
         aAlfa = "NOMBRE PROVEEDOR: " + #agifa079#3 + &13 + &10;   |XGRABA nHandleCorreo, aAlfa;

         aAlfa = "" + &13 + &10;                   |XGRABA nHandleCorreo, aAlfa;

         aAlfa = "CUENTA CONTABLE EN FACTURA: "   + #agifa079#12 + &13 + &10;   |XGRABA nHandleCorreo, aAlfa;
         aAlfa = "CUENTA CONTABLE EN PROVEEDOR: " + #agifa112#10 + &13 + &10;     |XGRABA nHandleCorreo, aAlfa;

         aAlfa = "" + &13 + &10;                   |XGRABA nHandleCorreo, aAlfa;
     |FINSI;
     |XCIERRA nHandleCorreo;

     aIP      = "xdscorreo.dv.diagram.net";
     aSalida  = "asmtp.diagram.es";  |QBF aSalida;
     aFrom    = "soporte@diagram.es";
     aTo      = eaToIncidencia;
     aAsunto  = "Incidencia en la Actualizacion datos Diario de Caja (diagram014 /v/ase0823)";
     aMensaje = &13 + &10 + "Leer fichero adjunto.";

     |DSCORREO_ENVIA aIP, aSalida, aFrom, aTo, aAsunto, aMensaje, aFichero;

     |MENSA "       ";
|FPRC;

|PROCESO Actualiza;
   |SI #godiacaj#33 ! 0;
        |HAZ SerieFactura;
        #agifa079#66 = qSerieF;
        #agifa079#0 = #godiacaj#33;
        |LEE 000#agifa079.=;
        |SI FSalida = 0;
             |MENAV "0000ERROR. No se puede actualizar el documento, existe la factura";
             aAlfa = "0000Documento:" + #godiacaj#1 +&13+" Factura:" + qSerieF + "/" + (("000000"+#godiacaj#33)%-106);
             |MENAV aAlfa; |ACABA;
        |FINSI;
   |FINSI;

     aAlfa = #10#7; |QBF aAlfa;
     |SI aAlfa ! "";
          aA쨚 = #10#2;
          |SELECT #4#19;
          #19#2 = #10#4;
          #19#74 = #10#7;
          |LEE 000#19];
          |PARA; |SI FSalida = 0; |Y #19#2 = #10#4; |Y #19#74 = #10#7; |HACIENDO;
               aAlfa = #19#1 % -104;
               |SI aAlfa = aA쨚;
                    aAlfa = ("000000" + #19#0) % -106;
                    aAlfa = "0000Codigo existente:" + #10#17 + "  en Factura:" + #19#66 + "/" + aAlfa;
                    |MENAV aAlfa; |ACABA;
               |FINSI;
               |LEE 000#19s;
          |SIGUE;
          |SELECT #1#19;
     |FINSI;

   |HAZ CreaAlbs;
   |HAZ AdjFactura;

   #agifa077#50 = qSerie;
   #agifa077#0  = qAlbar;
   |LEE 000#agifa077.=;
   aSerieFac = #agifa077#51; nNumFac = #agifa077#39;
   |SI enModiFac = 1;   || Llamado por godiaact con F2
          |CONFI "0000NModificar totales factura:";
          |SI FSalida = 0;
               |HAZ ModiImportes;
          |FINSI;
   |FINSI;
   |HAZ CreaRecibos;
   |HAZ EnlaceCont;

   #agifa079#66 = aSerieFac;
   #agifa079#0  = nNumFac;
   |LEE 101#agifa079.=;
   |SI FSalida = 0;
      aAlfa = #agifa079#1; |QBF aAlfa;
      aAlfa = aAlfa % 0704;
      |ABRE #agifa722;
      #agifa722#0 = "FC";
      #agifa722#1 = aSerieFac;
      #agifa722#2 = nNumFac;
      #agifa722#3 = aAlfa;
      |LEE 000#agifa722.];
      |SI FSalida = 0; |Y #agifa722#0 = "FC"; |Y #agifa722#1 = aSerieFac;
        |Y #agifa722#2 = nNumFac; |Y #agifa722#3 = aAlfa;
           #agifa079#48 = "S";
           |GRABA 020#agifa079.a;
      |FINSI;
      |CIERRA #agifa722;
      |LIBERA #agifa079;
   |FINSI;

   #agifa112#0 = #godiacaj#4;
   |LEE 000#agifa112.=;
   |SI FSalida = 0;
      |SI #agifa079#12 ! #agifa112#10;
         aAlfa = "    La cuenta contable del proveedor no coincide en factura y albaran.Factura [";
         aAlfa = aAlfa + aSerieFac + "/" + (("000000" + nNumFac) % -106) + "]";
         |MENAV aAlfa;

         ||컴컴컴컴컴컴컴횺i쨅na
         |HAZ EsMinana;
         |SI FSalida = 0;  |Y  eaToIncidencia ! "";
             |HAZ Incidencia;
         |FINSI;
      |FINSI;
   |SINO;
      aAlfa = "    No encuentro la ficha del proveedor " + #godiacaj#4;
      |MENAV aAlfa;
   |FINSI;

   #godiacaj#32 = aSerieFac;
   #godiacaj#33 = nNumFac;
   #godiacaj#35 = "S";
   |GRABA 020#godiacaj.a;
|FPRC;

|DEFBUCLE Actualiza;
#godiacaj#2;
;
#godiaact#0, #godiaact#1, #godiaact#2, "N";
#godiaact#3, #godiaact#4, #godiaact#5, "N";
be;
NULL,Actualiza;
|FIN;

|PROGRAMA;
   enNoModif = 0;
   |ABRE #goparame;
   |LEE 000#goparame.p;
   |CIERRA #goparame;

   SerB = "     ";   SerZ = "zzzzz";
   NumB = 0;         NumZ = 999999;
   Bl = " " * 20;    ZZ = "z" * 20;

   |ABRE #godiatmp; |ABRE #godiatm5;

   aAlfa = "ta" + Usuario; |NOME_DAT #50 aAlfa;
   |DELINDEX #agifa730; |ABRE #agifa730;
   |ABRE #30;
   |ABRE #godiacaj; |ABRE #godiacal;
   |ABRE #agifa077; |ABRE #agifa080;
   |ABRE #agifa112; |ABRE #agifa079;
   |SI enDDocum ! 0; |O enHDocum ! 0;
      #godiaact#0 = eaUsuario; #godiaact#3 = eaUsuario;
      #godiaact#1 = eaFecha;   #godiaact#4 = eaFecha;
      #godiaact#2 = enDDocum;  #godiaact#5 = enHDocum;
      |BUCLE Actualiza;
   |SINO;
      |CLS;
      |CABEZA " Actualizacion diario caja ";
      |PINPA #0#0; |PINDA #0#0;
      |ENDATOS #1#0;
      |SI FSalida = 0;
          ||컴컴컴컴컴컴컴횮ontrol Cierre
          efFecha = #0#1;
          |HAZ EstaCerrado;
          |SI FSalida ! 0;
               |SI FSalida = 0; |BUCLE Actualiza; |FINSI;
          |FINSI;
      |FINSI;
   |FINSI;
   |CIERRA #godiatmp; |CIERRA #godiatm5;
   |CIERRA #30; |CIERRA #agifa730;
   |CIERRA #agifa079; |CIERRA #agifa112;
   |CIERRA #agifa077; |CIERRA #agifa080;
   |CIERRA #godiacaj; |CIERRA #godiacal;
|FPRO;
