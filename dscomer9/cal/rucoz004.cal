|FICHEROS;
     rucoz004 #0;
     rucom001 #1;
     rucom005 #5;
     rucom006 #6;
     agifa007 #7;
     rucom009 #9;
     rucom010 #10;
     rucom014 #14;
     agifa019 #19;
     agifa024 #24;
     rucoz003 #103,MANTE;
     agifa112 #112;
     agifa142 #142;
     agifa321 #321;
|FIN;

|INCLUYE i_varart;

|VARIABLES;
     &enSwCheq = 0;
     &enCtrFin = 0;
     nGrid = 0;
     nPanta = 0;
     nPanEdita = 0;
     nBtnAlta = 0;
     nBtnModi = 0;
     nBtnBaja = 0;
     nBtnCons = 0;
 {-1}aVent = "";
     aVent1 = 0;
     aVent2 = 0;
     aVent3 = 0;
     aVent4 = 0;
     aVent5 = "";
     nVenEdita = 0;
     nModo = 0;
     aAlfa = "";
     nRango = 0;
     nLin = 0;
     nSw = 0;
     i = 0;
     aConte = "";

     &uni_dt = 0;   || Unidades
     &fed_dt = @;   || Fecha....
     &art_dt = "";  || Articulo.
     &cli_dt = "";  || Cliente..
     &fam_dt = "";  || Familia.
     &iva_dt = 0;   || Tipo Iva.
     &dtos_2 = 0;   || Descuento 2.
     &dto_pt = 0;   || Descuento Ptas.
     &dtos_1 = 0;   || Descuento 1.
     &pvp_ve = 0;   || Precio Venta.
     &tarifa = 0;   || Tarifa Cliente.
     &quedto = 0;   || 1=cli./art, 2=cli./familia, 3=cli./subfam.
     &gru_dt = "";  || Grupo Cliente.
     &nPromo = 0;
     &enMensaje = 0;
     &aParam    = "";
     nOk = 0;
|FIN;

|PROCESO PrecioV;
     aAlfa = #103#3; |QBF aAlfa;
     |SI aAlfa ! "";
          |ABRE #19;
          #19#0 = #103#17;
          |LEE 020#19=;
          |CIERRA #19;
          |ABRE #24;
          #24#0 = #103#3;
          |LEE 020#24=;
          |CIERRA #24;

          fed_dt = #14#16;   || Fecha....
          art_dt = #19#0;   || Articulo.
          cli_dt = #24#0;  || Cliente..
          fam_dt = #19#2;  || Familia.
          iva_dt = #19#30;  || Tipo Iva.
          dtos_2 = 0;      || Descuento 2.
          dto_pt = 0;      || Descuento Ptas.
          dtos_1 = 0;      || Descuento 1.
          pvp_ve = 0;      || Precio Venta.
          tarifa = #24#39;  || Tarifa Cliente....
          gru_dt = #24#158; || Grupo Cliente.
          nPromo = 0;
          uni_dt = #14#8;
          enMensaje = 0;
          aParam = "";
          eaSerie = #14#5;
          enDirEnt = 1;
          enAlmacen = 1;
          |HAZ calcdtos;
          #103#19 = pvp_ve; |PINTA #103#19;
     |FINSI;
|FPRC;

|PROCESO Precio;
     aAlfa = #103#1; |QBF aAlfa;
     |SI aAlfa ! "";
          |ABRE #19;
          #19#0 = #103#7;
          |LEE 020#19=;
          |CIERRA #19;
          #103#4 = #19#28; |PINTA #103#4;
     |FINSI;
|FPRC;

|PROCESO Actualiza;
     aAlfa = #14#1; |QBF aAlfa;
     |SI aAlfa ! ""; |HAZ AlbCom; |FINSI;
     aAlfa = #14#3; |QBF aAlfa;
     |SI aAlfa ! ""; |HAZ AlbVta; |FINSI;

     |ABRE #5;
     #5#0 = #14#10;
     |LEE 121#5=;
     |SI FSalida = 0;
          #5#23 = #5#23 + #14#12;
          |GRABA 020#5a;
          |DDEFECTO #10;
          |ABRE #10;
          #10#0 = #14#10;
          #10#1 = #14#6;
          |LEE 101#10=;
          |SI FSalida ! 0; |GRABA 020#10b; |FINSI;
          #10#2 = #10#2 + #14#12;
          #10#3 = #10#3 + #14#13;
          |GRABA 020#10a;
          |CIERRA #10;
     |FINSI;
     |CIERRA #9;
|FPRC;

|PROCESO Desactualiza;
     enError = 0;

     enSwCheq = 1;
     |SI #14#14 ! 0; |HAZ BajaCom; |FINSI;
     |SI enError ! 0; |ACABA; |FINSI;
     |SI #14#15 ! 0; |HAZ BajaVta; |FINSI;
     |SI enError ! 0; |ACABA; |FINSI;

     enSwCheq = 0;
     |SI #14#14 ! 0; |HAZ BajaCom; |FINSI;
     |SI #14#15 ! 0; |HAZ BajaVta; |FINSI;

     #14#14 = 0;
     #14#15 = 0;

     |ABRE #5;
     #5#0 = #14#10;
     |LEE 121#5=;
     |SI FSalida = 0;
          #5#23 = #5#23 - #14#12;
          |GRABA 020#5a;
          |DDEFECTO #10;
          |ABRE #10;
          #10#0 = #14#10;
          #10#1 = #14#6;
          |LEE 101#10=;
          |SI FSalida = 0;
               #10#2 = #10#2 - #14#12;
               #10#3 = #10#3 - #14#13;
               |SI #10#2 [ 0; |Y #10#3 [ 0;
                    |BORRA 020#10a;
               |SINO;
                    |GRABA 020#10a;
               |FINSI;
          |FINSI;
          |CIERRA #10;
     |FINSI;
     |CIERRA #9;
|FPRC;

|PROCESO Canti; |TIPO 0;
     #14#8 = #14#8 ! #142#8; |PINTA #14#8;
|FPRC;

|PROCESO Pro6; |TIPO 6;
     aAlfa = #103#1; |QBF aAlfa;
     |SI aAlfa ! "";
          aAlfa = ("000000" + aAlfa) % -106;
          #103#1 = aAlfa; |PINTA #103#1;
     |FINSI;
|FPRC;

|PROCESO Pro0; |TIPO 0;
     aAlfa = #103#1; |QBF aAlfa;
     |SI aAlfa = "";
          #103#16 = "01.01.0000";
          |C_M #103#6, 1, "N"; |PINTA #103#6;
          |C_M #103#2, 1, "N"; |PINTA #103#2;
          |C_M #103#7, 1, "N"; |PINTA #103#7;
          |C_M #103#8, 1, "N"; |PINTA #103#8;
          |C_M #103#4, 1, "N"; |PINTA #103#4;
     |SINO;
          |SI #103#6 = "01.01.0000"; #103#6 = ~; |FINSI;
          |C_M #103#6, 1, "S"; |PINTA #103#6;
          |C_M #103#2, 1, "S"; |PINTA #103#2;
          |C_M #103#7, 1, "S"; |PINTA #103#7;
          |C_M #103#8, 1, "S"; |PINTA #103#8;
          |C_M #103#4, 1, "S"; |PINTA #103#4;

          |ABRE #112;
          #112#0 = #103#1;
          |LEE 000#112=;
          |SI FSalida = 0;
               #103#20 = #112#1; |PINTA #103#20;
               |SI aConte ! #103#1; |Y nSw = 0;
                    |HAZ Precio;
               |FINSI;
          |SINO;
               |SI nSw = 0;
                    |MENAV "0000No existe c¢digo proveedor...";
                    |ERROR;
               |FINSI;
          |FINSI;
          |CIERRA #112;
     |FINSI;
|FPRC;

|PROCESO SerCom; |TIPO 0;
     |ABRE #7;
     #7#26 = #103#2;
     |LEE 000#7=;
     |SI FSalida = 0;
          #103#21 = #7#27; |PINTA #103#21;
     |SINO;
          |SI nSw = 0;
               |MENAV "0000No existe c¢digo serie...";
               |ERROR;
          |FINSI;
     |FINSI;
     |CIERRA #7;
|FPRC;

|PROCESO Cli6; |TIPO 6;
     aAlfa = #103#3; |QBF aAlfa;
     |SI aAlfa ! "";
          aAlfa = ("000000" + aAlfa) % -106;
          #103#3 = aAlfa; |PINTA #103#3;
     |FINSI;
|FPRC;

|PROCESO Cli0; |TIPO 0;
     aAlfa = #103#3; |QBF aAlfa;
     |SI aAlfa = "";
          #103#16 = "01.01.0000";
          |C_M #103#16, 1, "N"; |PINTA #103#16;
          |C_M #103#5, 1, "N"; |PINTA #103#5;
          |C_M #103#17, 1, "N"; |PINTA #103#17;
          |C_M #103#18, 1, "N"; |PINTA #103#18;
          |C_M #103#19, 1, "N"; |PINTA #103#19;
     |SINO;
          |SI #103#16 = "01.01.0000"; #103#16 = ~; |FINSI;
          |C_M #103#16, 1, "S"; |PINTA #103#16;
          |C_M #103#5, 1, "S"; |PINTA #103#5;
          |C_M #103#17, 1, "S"; |PINTA #103#17;
          |C_M #103#18, 1, "S"; |PINTA #103#18;
          |C_M #103#19, 1, "S"; |PINTA #103#19;
          |ABRE #24;
          #24#0 = #103#3;
          |LEE 000#24=;
          |SI FSalida = 0;
               #103#22 = #24#1; |PINTA #103#22;
               |SI aConte ! #103#3; |Y nSw = 0;
                    |HAZ PrecioV;
               |FINSI;
          |SINO;
               |SI nSw = 0;
                    |MENAV "0000No existe c¢digo cliente...";
                    |ERROR;
               |FINSI;
          |FINSI;
          |CIERRA #24;
     |FINSI;
|FPRC;

|PROCESO SerVta; |TIPO 0;
     |ABRE #7;
     #7#26 = #103#5;
     |LEE 000#7=;
     |SI FSalida = 0;
          #103#23 = #7#27; |PINTA #103#23;
     |SINO;
          |SI nSw = 0;
               |MENAV "0000No existe c¢digo serie...";
               |ERROR;
          |FINSI;
     |FINSI;
     |CIERRA #7;
|FPRC;

|PROCESO Arti6; |TIPO 6;
     enAlta = 0;
     eaArticulo = #103#7;
     |HAZ LlenaArticulo;
     |SI FSalida = 0;
          #103#7 = eaArticulo;
          |PINTA #103#7;
     |SINO;
          |ERROR; |ACABA;
     |FINSI;
|FPRC;

|PROCESO Arti0; |TIPO 0;
     |ABRE #19;
     #19#0 = #103#7;
     |LEE 000#19=;
     |SI FSalida = 0;
          #103#24 = #19#1; |PINTA #103#24;
          |SI aConte ! #103#7; |Y nSw = 0;
               |HAZ Precio;
          |FINSI;
     |SINO;
          |SI nSw = 0;
               |MENAV "0000No existe c¢digo art¡culo...";
               |ERROR;
          |FINSI;
     |FINSI;
     |CIERRA #19;
|FPRC;

|PROCESO ArtiV6; |TIPO 6;
     enAlta = 0;
     eaArticulo = #103#17;
     |HAZ LlenaArticulo;
     |SI FSalida = 0;
          #103#17 = eaArticulo;
          |PINTA #103#17;
     |SINO;
          |ERROR; |ACABA;
     |FINSI;
|FPRC;

|PROCESO ArtiV0; |TIPO 0;
     |ABRE #19;
     #19#0 = #103#17;
     |LEE 000#19=;
     |SI FSalida = 0;
          #103#28 = #19#1; |PINTA #103#28;
          |SI aConte ! #103#17; |Y nSw = 0;
               |HAZ PrecioV;
          |FINSI;
     |SINO;
          |SI nSw = 0;
               |MENAV "0000No existe c¢digo art¡culo...";
               |ERROR;
          |FINSI;
     |FINSI;
     |CIERRA #19;
|FPRC;

|PROCESO Conductor66; |TIPO 66;
     |ABRE #1;
     #1#0 = #103#9;
     |LEE 000#1];
     |CONSULTA_CLAVE #1#1;
     |SI FSalida = 0;
          #103#9 = #1#0; |PINTA #103#9;
     |SINO;
          |ERROR;
     |FINSI;
     |CIERRA #1;
|FPRC;

|PROCESO Conductor0; |TIPO 0;
     |ABRE #1;
     #1#0 = #103#9;
     |LEE 000#1=;
     |SI FSalida = 0;
          #103#25 = #1#1; |PINTA #103#25;
          |SI aConte ! #103#9; |Y nSw = 0;
               #103#10 = #1#13; |PINTA #103#10;
               #103#11 = #1#15; |PINTA #103#11;
               nSw = 1;
               |HAZ Vehiculo;
               |HAZ Remolque0;
               nSw = 0;
          |FINSI;
     |SINO;
          |SI nSw = 0;
               |MENAV "0000No existe c¢digo conductor...";
               |ERROR;
          |FINSI;
     |FINSI;
     |CIERRA #1;
|FPRC;

|PROCESO Vehiculo; |TIPO 0;
     |ABRE #5;
     #5#0 = #103#10;
     |LEE 000#5=;
     |SI FSalida = 0;
          #103#26 = #5#1; |PINTA #103#26;
     |SINO;
          |SI nSw = 0;
               |MENAV "0000No existe c¢digo veh¡culo...";
               |ERROR;
          |FINSI;
     |FINSI;
     |CIERRA #5;
|FPRC;

|PROCESO Remolque0; |TIPO 0;
     |ABRE #6;
     #6#0 = #103#11;
     |LEE 000#6=;
     |SI FSalida = 0;
          #103#27 = #6#1; |PINTA #103#27;
     |SINO;
          |SI nSw = 0;
               |MENAV "0000No existe c¢digo remolque....";
               |ERROR;
          |FINSI;
     |FINSI;
     |CIERRA #6;
|FPRC;

|PROCESO Remolque66; |TIPO 66;
     |ABRE #6;
     #6#0 = #103#11;
     |LEE 000#6];
     |CONSULTA_CLAVE #1#6;
     |SI FSalida = 0;
          #103#11 = #6#0; |PINTA #103#11;
     |SINO;
          |ERROR;
     |FINSI;
     |CIERRA #6;
|FPRC;

|PROCESO Conte; |TIPO 7;
     aConte = #103Campo;
|FPRC;

|PROCESO Edita;
     enError = 0;
     |SI nModo = 2;
          enSwCheq = 1;
          |SI #14#14 ! 0; |HAZ BajaCom; |FINSI;
          |SI enError ! 0; |ACABA; |FINSI;
          |SI #14#15 ! 0; |HAZ BajaVta; |FINSI;
          |SI enError ! 0; |ACABA; |FINSI;
     |FINSI;

     |SI nModo = 1; |VENTANA 0901, 3198, -1, 17, "Nuevo Registro"; |FINSI;
     |SI nModo = 2; |VENTANA 0901, 3198, -1, 17, "Modifica Registro"; |FINSI;
     |SI nModo = 4; |VENTANA 0901, 3198, -1, 17, "Consulta Registro"; |FINSI;
     nVenEdita = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVenEdita;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |SI nModo = 1;
          |LEE 000#14u;
          |SI FSalida = 0;
               nLin = #14#0 + 1;
          |SINO;
               nLin = 1;
          |FINSI;
          |DDEFECTO #14;
          #14#0 = nLin;
          |GRABA 020#14b;

          |DDEFECTO #103;
          #103#0 = nLin;
          |SI #142#185 = "S";
               #14#2 = #142#193;
               #14#5 = #142#188;
          |FINSI;
     |SINO;
          |PARA i = 0; |SI i [ 19; |HACIENDO i = i + 1;
               #103i = #14i;
          |SIGUE;
     |FINSI;
     nSw = 1;
     |HAZ Pro0;
     |HAZ SerCom;
     |HAZ Cli0;
     |HAZ SerVta;
     |HAZ Arti0;
     |HAZ ArtiV0;
     |HAZ Conductor0;
     |HAZ Vehiculo;
     |HAZ Remolque0;
     nSw = 0;
     |PINPA #0#103; nPanEdita = FSalida;
     |PINDA #0#103;
     |SI nModo = 1; |ENDATOS #1#103; |FINSI;
     |SI nModo = 2; |ENDATOS #2#103; |FINSI;
     |SI nModo = 4; |PAUSA; |FINSI;
     |SI FSalida = 0; |Y nModo ! 4;
          nOk = 0;
          |SI nModo = 2; || Comprueba que se ha modificado algo
               nOk = 1;
               |PARA i = 0; |SI i [ 19; |HACIENDO i = i + 1;
                    |SI #14i ! #103i;
                         nOk = 0; |SAL;
                    |FINSI;
               |SIGUE;
          |FINSI;
          |SI nOk = 0;
               |SI nModo = 2; |HAZ Desactualiza; |FINSI;
               |SI enError = 0;
                    |PARA i = 0; |SI i [ 19; |HACIENDO i = i + 1;
                         #14i = #103i;
                    |SIGUE;
                    |HAZ Actualiza;
                    |GRABA 020#14a;
                    |REFRESCACONTROL nGrid;
               |FINSI;
          |FINSI;
     |SINO;
          |SI nModo = 1;
               |BORRA 020#14c;
               |REFRESCACONTROL nGrid;
          |FINSI;
     |FINSI;
     |LIBERA #14;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVenEdita;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA nVenEdita;
     |PULSATECLA;
|FPRC;

|PROCESO BtnAlta;
     nModo = 1;
     |HAZ Edita;
|FPRC;

|PROCESO BtnModi;
     |LEE 101#14=;
     |SI FSalida ! 0; |ACABA; |FINSI;
     nModo = 2;
     |HAZ Edita;
     |LIBERA #14;
|FPRC;

|PROCESO BtnBaja;
     |LEE 101#14=;
     |SI FSalida ! 0; |ACABA; |FINSI;
     aAlfa = "0000N¿Borrar movimiento: " + (("000000" + #14#0) % -106) + "?";
     |CONFI aAlfa;
     |SI FSalida = 0;
          |HAZ Desactualiza;
          |SI enError = 0;
               |BORRA 020#14a;
               |REFRESCACONTROL nGrid;
          |FINSI;
     |FINSI;
     |LIBERA #14;
|FPRC;

|PROCESO BtnCons;
     |LEE 000#14=;
     |SI FSalida ! 0; |ACABA; |FINSI;
     nModo = 4;
     |HAZ Edita;
|FPRC;

|PROCESO Evt;
     |SI FSalida = 1; |O FSalida = 2;
          |LEE 000#14=;
     |FINSI;
     |SI FSalida = 4;
          |HAZ BtnModi;
     |FINSI;
|FPRC;

|PROCESO Min;
     #14#0 = 000001;
|FPRC;

|PROCESO Max;
     #14#0 = 999999;
|FPRC;

|PROGRAMA;
     |ABRE #142; |LEE 000#142p; |CIERRA #142;
     |ABRE #321; |LEE 000#321p; |CIERRA #321;

     |PINPA #0#0; nPanta = FSalida;
     |CONTROL_SIMPLE nPanta, "BOTON,{{193}}Alta", 1002, 1012, BtnAlta;
     nBtnAlta = FSalida;
     |CONTROL_SIMPLE nPanta, "BOTON,{{197}}Modificar", 1014, 1024, BtnModi;
     nBtnModi = FSalida;
     |CONTROL_SIMPLE nPanta, "BOTON,{{199}}Baja", 1026, 1036, BtnBaja;
     nBtnBaja = FSalida;
     |CONTROL_SIMPLE nPanta, "BOTON,{{155}}Consulta", 1038, 1048, BtnCons;
     nBtnCons = FSalida;

     |ABRET #14; |LEE 000#14u;
     nRango = 4 + 8 + 16 + 32;
     |LINEAL_SIMPLE #1#14, nRango, 1102, 3299, Min, Max, Evt;
     nGrid = FSalida;
     enCtrFin = nGrid;
     |HAZ EsperaFin;
     |CIERRAT #14;

     |DESTRUYECONTROL nGrid;
     |FIN_CONTROL_WINDOWS nBtnAlta;
     |FIN_CONTROL_WINDOWS nBtnModi;
     |FIN_CONTROL_WINDOWS nBtnBaja;
     |FIN_CONTROL_WINDOWS nBtnCons;
|FPRO;
