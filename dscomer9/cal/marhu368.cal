|FICHEROS;
   marhu368 #0;

   agifa019 #1;
   dsl00001 #2;
|FIN;

|VARIABLES;
   &SwLinea = 0;
   &CabArt = 0;
   &CabLin = 0;

   Calc   = 0;
   Calc1  = 0;
   nCont  = 0;
   nPar   = 0;
   nImpar = 0;
   nLong  = 0;

   aLong    = "";
   Comodin  = "";
   Comodin1 = "";
   Comodin2 = "";
|FIN;

|PROCESO RecalculaDig;
   |QBF Comodin; aLong = Comodin % 0; nLong = aLong; Calc = 99 + nLong;
   Comodin = Comodin % Calc;
   Comodin = ("000000000000" + Comodin) % -112;
   nPar = 0; nImpar = 0;
   |PARA nCont = 12; |SI nCont ] 1; |HACIENDO nCont = nCont - 1;
      Calc = (nCont * 100) + 1;
      Comodin1 = Comodin % Calc;
      nCont = 13 - nCont;
      Calc = ((nCont/ 2) ! 0) * 2;
      Calc1 = Comodin1;
      |SI Calc = nCont;
         nPar = nPar + Calc1;
      |SINO;
         nImpar = nImpar + Calc1;
      |FINSI;
      nCont = 13 - nCont;
   |SIGUE;
   nImpar = nImpar * 3;
   Calc   = nPar + nImpar;
   Calc1  = (((Calc / 10) + .5) ! 0) * 10;
   Calc   = Calc1 - Calc;
   |SI Calc = 10; Calc = 0; |FINSI;
   Comodin = Comodin + Calc;
|FPRC;

|PROCESO Articulos;
   SwLinea = 0; CabLin = 0;
   Comodin2 = #agifa019#128; |QBF Comodin2;
   aLong = Comodin2 % 0; nLong = aLong;
   |SI nLong < 13;
      Comodin2 = #agifa019#128; |QBF Comodin2;
      Comodin2 = ("0000000000000" + Comodin2) % -113;
      Comodin = #agifa019#128; |HAZ RecalculaDig;
      |SI Comodin = Comodin2;
         #agifa019#128 = Comodin; |GRABA 020#agifa019.a;
      |SINO;
         SwLinea = 1; |PRINT;
         |SI CabArt = 0; CabArt = 1; |FINSI;
      |FINSI;
   |FINSI;

   #dsl00001#1 = #agifa019#0;
   #dsl00001#3 = "";
   |LEE 000#dsl00001.];
   |SI FSalida = 0; |Y #dsl00001#1 = #agifa019#0;
      |PARA; |SI FSalida = 0; |Y #dsl00001#1 = #agifa019#0; |HACIENDO;
         Comodin2 = #dsl00001#3; |QBF Comodin2;
         aLong = Comodin2 % 0; nLong = aLong;
         |SI nLong < 13;
            Comodin2 = #dsl00001#3; |QBF Comodin2;
            Comodin2 = ("0000000000000" + Comodin2) % -113;
            Comodin = #dsl00001#3; |HAZ RecalculaDig;
            |SI Comodin = Comodin2;
               #dsl00001#3 = Comodin; |GRABA 020#dsl00001.a;
            |SINO;
               SwLinea = 2; |PRINT;
               |SI CabLin = 0; CabLin = 1; CabArt = 0; |FINSI;
            |FINSI;
         |FINSI;
         |LEE 000#dsl00001.s;
      |SIGUE;
   |FINSI;

   |SI SwLinea = 2; SwLinea = 3; |PRINT; |FINSI;

|FPRC;

|DEFBUCLE Articulos;
#agifa019#1;
;
INICIO;
FINAL;
be;
NULL,Articulos;
|FIN;

|PROGRAMA;
   |CLS;
   |CABEZA "Recalculo codigo de barras";
   |PINPA #0#0; |PINDA #0#0;
   |ENDATOS #1#0;
   |SI FSalida = 0; |Y #0#0 = "SI";
      |IMPRE 0;
      |SI FSalida < 0; |ACABA; |FINSI;
      |INFOR "marhu368";
      |SI FSalida < 0; |ACABA; |FINSI;
      |ABRE #dsl00001; |SELECT #3#dsl00001;
      |BUCLE Articulos;
      |SELECT #1#dsl00001; |CIERRA #dsl00001;
      |PIEINF;
      |FININF;
      |FINIMP;
   |FINSI;
|FPRO;
