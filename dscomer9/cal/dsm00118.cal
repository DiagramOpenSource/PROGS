|FICHEROS;
     dsm00118 #0;
     origen@  #1;
     destin@  #2;
     agifa142 #142;
|FIN;

|VARIABLES;
               || NomFic, Campo Expor.
 {-1}MFichero   = "";
     MFichero1 = "agifa019,123";        || F.Pago
     MFichero2 = "agifa553,000";        || Linea
     MFichero3 = "agifa060,000";        || Familia
     MFichero4 = "agifa120,000";        || Subfamilia
     MFichero5 = "agi00063,000";        || Marcas
     MFichero6 = "dsm00116,000";        || Grupo
     MFichero7 = "FIN";

{100}MSelec    = "";
{100}MConte    = "";
{100}MParri    = "";

     nSe       = 0;
     nCo       = 0;
     nPa       = 0;
     nPos      = 0;
     i         = 0;
     j         = 0;
     nError    = 0;
     nCampos   = 0;
     nCamExpo  = 0;
     aDesde    = "";
     aHasta    = "";
     aLetra    = "";
     aAlfa     = "";
     aLon      = "";
     aLon1     = "";
     aCod      = "";
     aCadena   = "";
     aCampo    = "";
     aDef      = "";
     aDef1     = "";
     aDef2     = "";
     aPath     = "";
     aFichero  = "";
     nValido   = "";
|FIN;

|PROCESO Seleccion;
     aCod = "";
     aDesde = "";
     aLon1 = aCadena % A0;
     |PARA j = 2; |SI j [ aLon1; |HACIENDO j = j + 1;
          nPos = j * 100 + 1;
          aLetra = aCadena % nPos;
          |SI aLetra = ",";
               MSelec = aDesde;
               IMSelec = IMSelec + 1;
               MSelec = aCod;
               IMSelec = IMSelec + 1;
               aCod = ""; aDesde = "";
          |SINO;
               |SI aLetra = ":";
                    aDesde = aCod;
                    aCod = "";
               |SINO;
                    aCod = aCod + aLetra;
               |FINSI;
          |FINSI;
     |SIGUE;
     |SI aDesde ! "";
          MSelec = aDesde;
          IMSelec = IMSelec + 1;
          MSelec = aCod;
          IMSelec = IMSelec + 1;
     |FINSI;
|FPRC;

|PROCESO Contenido;
     aCod = "";
     aLon1 = aCadena % A0;
     |PARA j = 2; |SI j [ aLon1; |HACIENDO j = j + 1;
          nPos = j * 100 + 1;
          aLetra = aCadena % nPos;
          |SI aLetra = ",";
               MConte = aCod;
               IMConte = IMConte + 1;
               aCod = "";
          |SINO;
               aCod = aCod + aLetra;
          |FINSI;
     |SIGUE;
     |SI aCod ! "";
          MConte = aCod;
          IMConte = IMConte + 1;
     |FINSI;
|FPRC;

|PROCESO Parrilla;
     aCod = "";
     aLon1 = aCadena % A0;
     |PARA j = 2; |SI j [ aLon1; |HACIENDO j = j + 1;
          nPos = j * 100 + 1;
          aLetra = aCadena % nPos;
          |SI aLetra = ",";
               MParri = aCod;
               IMParri = IMParri + 1;
               aCod = "";
          |SINO;
               aCod = aCod + aLetra;
          |FINSI;
     |SIGUE;
     |SI aCod ! "";
          MParri = aCod;
          IMParri = IMParri + 1;
     |FINSI;
|FPRC;

|PROCESO SacaCod;
     aLetra = aCadena % 101;
     |SI aLetra = "P"; |HAZ Parrilla; |ACABA; |FINSI;
     |SI aLetra = "C"; |HAZ Contenido;|ACABA; |FINSI;
     |SI aLetra = "S"; |HAZ Seleccion;|ACABA; |FINSI;
     nError = 1;
|FPRC;

|PROCESO Chequea;
     aCampo = #0Campo;
     |HAZ CargaMatriz;
|FPRC;

|PROCESO CargaMatriz;
     nSe = 0; nCo = 0; nPa = 0; nError = 0;
     aCadena = "";
     IMSelec = MSelec1<-;
     IMParri = MParri1<-;
     IMConte = MConte1<-;

     |QBF aCampo;
     aLon = aCampo % A0;

     |PARA i = 1; |SI i [ aLon; |Y nError = 0; |HACIENDO i = i + 1;
          nPos = i * 100 + 1;
          aLetra = aCampo % nPos;
          |SI aLetra = "/";
               |HAZ SacaCod;
               aCadena = "";
          |SINO;
               aCadena = aCadena + aLetra;
          |FINSI;
     |SIGUE;
     |SI aCadena ! ""; |Y nError = 0; |HAZ SacaCod; |FINSI;

     |SI nError ! 0;
          aAlfa = "0000Error en parametros...";
          |MENAV aAlfa;
          |ERROR;
     |FINSI;

     MSelec = "FIN";
     MParri = "FIN";
     MConte = "FIN";
|FPRC;
컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
|PROCESO CargaDef;
     aDef1 = aDef + "/" + aFichero;
     |CARGA_DEF aDef1, origen@;
     |SI FSalida = 0;
          aDef2 = aDef + "/" + aFichero;
          |CARGA_DEF aDef2, destin@;
          |SI FSalida ! 0;
               aAlfa = "0000 Error al cargar " + aDef2;
               |MENAV aAlfa;
               aDef2 = "";
          |FINSI;
     |SINO;
          aAlfa = "0000 Error al cargar " + aDef1;
          |MENAV aAlfa;
          aDef1 = "";
     |FINSI;
|FPRC;

|PROCESO DescargaDef;
     |SI aDef1 ! ""; |DESCARGA_DEF #destin@; |FINSI;
     |SI aDef2 ! ""; |DESCARGA_DEF #origen@; |FINSI;
|FPRC;

|PROCESO NomeDat;
     aAlfa = aFichero % -106;
     aAlfa = "ex" + aAlfa;
     |NOME_DAT #2 aAlfa;
     |PATH_DAT #2 aPath;
     |DELINDEX #2;
|FPRC;
컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
|PROCESO Valido;
     nValido = 1;
     IMSelec = MSelec1<-;
     IMParri = MParri1<-;
     IMConte = MConte1<-;

     |PARA; |SI MSelec ! "FIN"; |HACIENDO;
          aDesde = MSelec; IMSelec = IMSelec + 1;
          aHasta = MSelec; IMSelec = IMSelec + 1;
          |SI #1#0 ] aDesde; |Y #1#0 [ aHasta;
               nValido = 0; |ACABA;
          |FINSI;
     |SIGUE;

     |PARA; |SI MParri ! "FIN"; |HACIENDO;
          aAlfa = #1#0; |QBF aAlfa;
          |SI aAlfa = MParri;
               nValido = 0; |ACABA;
          |FINSI;
          IMParri = IMParri + 1;
     |SIGUE;

     |PARA; |SI MConte ! "FIN"; |HACIENDO;
          aAlfa = #1#0 - MConte;
          |SI aAlfa ! #1#0;
               nValido = 0; |ACABA;
          |FINSI;
          IMConte = IMConte + 1;
     |SIGUE;

|FPRC;
컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
|PROCESO GrabaExpor;
     |SI aFichero = "agifa019";         || F.Pago
          aCampo = #0#1;
          |HAZ CargaMatriz;
     |FINSI;
     |SI aFichero = "agifa553";        || Linea
          aCampo = #0#2;
          |HAZ CargaMatriz;
     |FINSI;
     |SI aFichero = "agifa060";        || Familia
          aCampo = #0#3;
          |HAZ CargaMatriz;
     |FINSI;
     |SI aFichero = "agifa120";        || Subfamilia
          aCampo = #0#4;
          |HAZ CargaMatriz;
     |FINSI;
     |SI aFichero = "agi00063";        || Marcas
          aCampo = #0#5;
          |HAZ CargaMatriz;
     |FINSI;
     |SI aFichero = "dsm00116";        || Grupo
          aCampo = #0#6;
          |HAZ CargaMatriz;
     |FINSI;

     |SI nError ! 0; |ACABA; |FINSI;

     aAlfa = "|NC";
     aAlfa = #1#aAlfa#;
     nCampos = aAlfa; nCampos = nCampos - 1;

     |LEE 101#1p;
     |PARA; |SI FSalida = 0; |HACIENDO;
          |HAZ Valido;
          |SI nValido = 0;
               |PARA i = 0; |SI i [ nCampos; |HACIENDO i = i + 1;
                    #2i = #1i;
               |SIGUE;
               |SI nCamExpo ! 0
                    #1nCamExpo = "S";        || Exportado
                    |GRABA 020#1a;
               |FINSI;
               |PINTA 2315, #1#0;
        ||     aAlfa = "0000" + #1#0; |MENAV aAlfa;
               |GRABA 020#2n;
          |FINSI;
          |LIBERA #1;
          |LEE 101#1s;
     |SIGUE;
|FPRC;

|PROCESO Exportacion;
     |DDEF aDef;
     IMFichero = MFichero1<-;
     |PARA; |SI MFichero ! "FIN"; |HACIENDO;
          aFichero = MFichero % 108;
          nCamExpo = MFichero % -103;
          |PINTA 2305, aFichero;
          |HAZ CargaDef;
          |SI FSalida = 0;
               |HAZ NomeDat;
               |ABRE #2;
               |ABRE #1;
               |HAZ GrabaExpor;
               |CIERRA #1;
               |CIERRA #2;
          |FINSI;
          |HAZ DescargaDef;
          IMFichero = IMFichero + 1;
     |SIGUE;
|FPRC;
컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
|PROGRAMA;
     |ABRE #142; |LEE 000#142p; |CIERRA #142;
     aPath = #142#150; |QBF aPath;

     |CLS;
     |ABRE #0;
     |LEE 101#0p;
     |SI FSalida ! 0; |GRABA 020#0b; |FINSI;
     |PINPA #0#0;
     |PINDA #0#0;
     |ENDATOS #2#0;
     |SI FSalida = 0;
          |HAZ Exportacion;
          |GRABA 020#0a;
     |FINSI;
     |CIERRA #0;
|FPRO;
