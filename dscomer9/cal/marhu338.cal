|FICHEROS;
     marhu338 #0;
     marhu340 #1;
     agifa134 #134;
     agifa059 #59;
     agifa010 #10;
     agifa071 #71;
     agifa024 #24;
|FIN;

|VARIABLES;
     &Tempo    = "";
     aTempo1   = "";
     aSer      = "";
     &nTip     = 0;
     nHay      = 0;
     aAlfa     = "";
     aInforme  = "marhu338";
     nBruto    = 0;
     nDto      = 0;
     i         = 0;
     aDSer     = "";
     aHSer     = "";
     nAbo      = 0;
|FIN;

|PROCESO Serie; |TIPO 0;
     |SI #0#6 = "L";
          |C_M #0#0, 1, "S";
          |C_M #0#1, 2, "S";
          |PARA i = 7; |SI i [ 16; |HACIENDO i = i + 1;
               |C_M #0i, 1, "N";
          |SIGUE;
     |SINO;
          |C_M #0#0, 1, "N";
          |C_M #0#1, 2, "N";
          |PARA i = 7; |SI i [ 16; |HACIENDO i = i + 1;
               |C_M #0i, 1, "S";
          |SIGUE;
     |FINSI;
|FPRC;

|PROCESO Tmp;
     |SI nHay = 0;
          nHay = 1;
          aSer = #1#0;
     |FINSI;
     |SI aSer ! #1#0;
          nTip = 1;
          |PRINT;
          aSer = #1#0;
          #0#17 = 0;
          #0#18 = 0;
          #0#19 = 0;
     |FINSI;
     |DDEFECTO #24;
     #24#0 = #1#2;
     |LEE 000#24=;
     nTip = 0;
     |PRINT;
     #0#17 = #0#17 + #1#3;
     #0#18 = #0#18 + #1#4;
     #0#19 = #0#19 + #1#5;
|FPRC;

|DEFBUCLE Tmp;
     #1#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, Tmp;
|FIN;

|PROCESO agifa071;
     nBruto = #71#5 * #71#6;
     nDto = ((nBruto < #71#8) < #71#33);
     nDto = (((nDto < #10#5) < #10#32) < #10#7);
     nDto = nBruto - nDto;

     |DDEFECTO #1;
     #1#0 = #134#49;
     #1#1 = #134#0;
     |LEE 101#1=;
     #1#2 = #134#2;         ||Cli
     #1#3 = #1#3 + (nBruto * nAbo);
     #1#4 = #1#4 + (nDto * nAbo);
     #1#5 = #1#5 + #71#14;  ||Coste;
     |SI FSalida = 0;
          |GRABA 020#1a;
     |SINO;
          |GRABA 020#1n;
     |FINSI;
     |LIBERA #1;
|FPRC;

|PROCESO agifa059;
     #10#52 = #59#15;
     #10#0 = #59#2;
     |LEE 000#10=;
     |SI FSalida = 0;
          |SI #10#43 = "S";
               nAbo = -1;
          |SINO;
               nAbo = 1;
          |FINSI;
          |BUCLELIN 2 agifa071 #10;
     |FINSI;
|FPRC;

|DEFBUCLE agifa134;
     #134#3;
     ;
     "      ", #0#4, aDSer, #0#2;
     "zzzzzz", #0#5, aHSer, #0#3;
     ;
     NULL, NULL, agifa059;
|FIN;

|PROCESO Tipo3; |TIPO 3;
     |HAZ CreaTempo; aTempo1 = Tempo;
     |NOME_DAT #1 aTempo1; |DELINDEX #1;

     |IMPRE 0;
     |SI FSalida = 0;
          |INFOR aInforme;
          |SI FSalida = 0;
               |ABRE #10;
               |ABRE #1;
               |SI #0#6 = "L";
                    aDSer = #0#0;
                    aHSer = #0#1;
                    |BUCLE agifa134;
               |SINO;
                    |PARA i = 7; |SI i [ 16; |HACIENDO  i = i + 1;
                         aDSer = #0i;
                         aHSer = #0i;
                         |BUCLE agifa134;
                    |SIGUE;
               |FINSI;
               |CIERRA #1;
               |CIERRA #10;

               |ABRE #24;
               |BUCLE Tmp;
               |CIERRA #24;

               |SI nHay ! 0;
                    nTip = 1;
                    |PRINT;
                    |PIEINF;
               |FINSI;
               |FININF;
          |FINSI;
          |FINIMP;
     |FINSI;

     |DELINDEX #1; Tempo = aTempo1; |HAZ BoraTempo;
|FPRC;
