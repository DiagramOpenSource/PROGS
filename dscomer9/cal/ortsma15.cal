     ImportaImpos-> Boton Importacion IMOS

|FICHEROS;
     ortsm015 #15;
     ortsw007 #7;   || Tmp Imos
     ortsw007@;
     ortsm016 #16;
     ortsm017 #17,MANTE;
     ortsm018 #18,MANTE;
     agifa019 #19;
     ortsm020 #20;
     ortsm027 #27;
     ortsm028 #28;
     ortsm029 #29,MANTE;      || Herrajes
     ortsm030 #30,MANTE;      || Canteados
     ortsm031 #31,MANTE;
     ortsm034 #34;
     ortsm036 #36;
     ortsm037 #37;
     agifa049 #49;
|FIN;

|VARIABLES;
     &Tempo = "";
     nHay = 0;
     aOrderId = "";
     nRegistro = 0;
     aTmp7 = "";
     nError = 0;
     aArti = "";
     aTyp = "";
     nSw = 0;
     nLin = 0;
     aArtOpt = "";
     aTxt = "";
     aAlfa1 = "";
     aPos = "";
     aOrden = "";
     nSwActualiza = 0;
     nCoste = 0;
     nTCoste = 0;

     aTab = "";
     aAscii10 = "";
     aAscii13 = "";
     aDsDestino = "";
     {1}aLocal = "";
     {1}aContiene = "";
     aAlfa = "";
     nHandle = 0;
     aIP = "";
     aLon = "";
     nPos = 0;
     aCar = "";
     aPath = "";
     aBat = "";
     aDat = "";
     aFin = "";
     i = 0;
     aOrg = "";
     aDes = "";
     aMd5Org = "";
     aMd5Des = "";
     nCampo = 0;
     aReg = "";
     aDato = "";
     aLetra = "";
 {99}Valor = "";
     aCampo = "";
     aTmp = "";
     nPadre = 0;
     nHerraje = 0;
     nGraba18 = 0;
|FIN;

1 ORDERID
2 TYP
3 PARTTYPE
4 MAINLINE
5 NAME
6 NAME2
7 KP
8 LENGTH
9 WIDTH
10 THICKNESS
11 CLENGTH
12 CWIDTH
13 ID
14 PARENTID
15 CNT
16 ARTICLE_ID
17 CNC_BARCODE1
18 CNC_BARCODE2
19 EDGE_ID
20 EDGE_TRANS
21 BOM_FLAG
22 INFO1
23 INFO2
24 INFO3
25 INFO4
26 INFO5
27 RENDERPMAT
28 TEXT1
29 WEIGHT


|PROCESO BuscaLinPed;
     nHay = 0;
     |SELECT #2#ortsw007@;
     #ortsw007@#1 = 3;
     #ortsw007@#13 = #7#13;
     |LEE 000#ortsw007@.=;
     |SI FSalida = 0;
          |SELECT #6#17;
          #17#0 = #16#28;
          #17#1 = #16#0;
          #17#2 = #ortsw007@#3;
          #17#37 = #ortsw007@#12;
          |LEE 000#17=;
          |SI FSalida = 0;
               #29#2 = #17#2;
               #29#3 = #17#3;
               nHay = 1;
          |SINO;
               |SELECT #4#17;
               #17#0 = #16#28;
               #17#1 = #16#0;
               #17#2 = #ortsw007@#3;
               #17#26 = #ortsw007@#25;
               |LEE 000#17=;
               |SI FSalida = 0;
                     #29#2 = #17#2;
                     #29#3 = #17#3;
                     nHay = 1;
               |FINSI;
          |FINSI;
     |FINSI;
     |SI nHay = 0;
           |SELECT #3#ortsw007@;
           #ortsw007@#1 = 3;
           #ortsw007@#12 = #7#13;
           |LEE 000#ortsw007@.=;
           |SI FSalida = 0;
                 |SELECT #6#17;
                 #17#0 = #16#28;
                 #17#1 = #16#0;
                 #17#2 = #ortsw007@#3;
                 #17#37 = #ortsw007@#12;
                 |LEE 000#17=;
                 |SI FSalida = 0;
                      #29#2 = #17#2;
                      #29#3 = #17#3;
                      nHay = 1;
                 |SINO;
                      |SELECT #4#17;
                      #17#0 = #16#28;
                      #17#1 = #16#0;
                      #17#2 = #ortsw007@#3;
                      #17#26 = #ortsw007@#25;
                      |LEE 000#17=;
                      |SI FSalida = 0;
                          #29#2 = #17#2;
                          #29#3 = #17#3;
                          nHay = 1;
                     |FINSI;
               |FINSI;
          |FINSI;
     |FINSI;
     |SI nHay = 0;
          |SELECT #3#ortsw007@;
          #ortsw007@#1 = 2;
          #ortsw007@#12 = #7#13;
          |LEE 000#ortsw007@.=;
          |SI FSalida = 0;
               |SELECT #3#ortsw007@;
               #ortsw007@#1 = 1;
               #ortsw007@#12 = #ortsw007@#13;
               |LEE 000#ortsw007@.=;
               |SI FSalida = 0;
                    #29#2 = #ortsw007@#3;
                    #29#3 = 99;
                    nHay = 2;
               |SINO;
                    aAlfa = "0000Revisar herraje " + #7#12;
                    |MENAV aAlfa;
                    |ACABA;
               |FINSI;
          |SINO;
               aAlfa = "0000Revisar herraje " + #7#12;
               |MENAV aAlfa;
               |ACABA;
          |FINSI;
     |FINSI;
|FPRC;


|PROCESO ImosMultipieza;
     nCoste = #7#24;
     |SI nCoste = 0;
          aAlfa = "0000Elmento 0 en la pieza " + #17#5 + " del art¡culo: " + #7#5; |QBF aAlfa;
          |MENAV aAlfa;
          |ACABA;
     |FINSI;

     |SELECT #6#17;
     #17#0 = #15#25;
     #17#1 = #15#0;
     #17#2 = #7#3;
     #17#37 = #7#13;
     |LEE 101#17=;
     |SI FSalida = 0;
          #18#0 = #17#0;
          #18#1 = #17#1;
          #18#2 = #17#2;
          #18#3 = #17#3;
          #18#4 = nCoste;
          #18#5 = #17#6;
          #18#6 = #17#11;
          #18#7 = #7#16;
          #18#8 = #7#17;
          #18#9 = #7#25;
          #18#10 = #7#23;
          #18#13 = #7#7 * #36#7 / #36#8;
          #18#12 = #7#8 * #36#7 / #36#8;
          #18#15 = #7#10 * #36#7 / #36#8;
          #18#14 = #7#11 * #36#7 / #36#8;
          #18#16 = #7#9;
          |GRABA 000#18b;
          nGraba18 = FSalida;
          |SI FSalida ! 0;
               aAlfa = "0000Revisar multipieza " + #17#5 + " del art¡culo: " + #7#5; |QBF aAlfa;
               |MENAV aAlfa;
          |FINSI;

          aOrden = "";
          nPadre = #18#4;

          #16#28 = #17#0;
          #16#0 = #17#1;
          #16#1 = #17#2;
          |HAZ B_Imos_7;       || Aux. Pedidos Canteados

          |SI nGraba18 = 0;
               #18#11 = aOrden
               |GRABA 020#18a;
          |FINSI;
          |LIBERA #18;
/*
          |HAZ B_Imos_8;       || Aux. Pedidos Herrajes
*/

          #17#8 = "S";
          |SI nCoste > #17#7;
               #17#7 = nCoste;
          |FINSI;

          |SELECT #2#ortsw007@;
          #ortsw007@#1 = 4;
          #ortsw007@#13 = #7#12;
          |LEE 000#ortsw007@.=;
          |SI FSalida ! 0; |DDEFECTO #ortsw007@; |FINSI;

          aArtOpt = #ortsw007@#4;

          #17#4 = #ortsw007@#5;
          nHay = 0;
          |SELECT #2#ortsw007@;
          #ortsw007@#1 = 8;
          #ortsw007@#13 = #7#13;
          |LEE 000#ortsw007@.];
          |PARA; |SI FSalida = 0; |Y #ortsw007@#1 = 8; |Y #ortsw007@#13 = #7#13; |HACIENDO;
               |SI #ortsw007@#2 = 16;
                    nHay = 1; |SAL;
               |FINSI;
               |LEE 000#ortsw007@.s;
          |SIGUE;
          |SI nHay = 0; |DDEFECTO #ortsw007@; |FINSI;

          aAlfa = #ortsw007@#4 - "_";
          |SI FSalida > 0;
               nPos = (FSalida - 1) / 100 + 1;
               aAlfa = #ortsw007@#4 % nPos;
          |SINO;
               aAlfa = "";
          |FINSI;
          #17#11 = aAlfa;
          #17#15 = "S";
          #17#23 = aArtOpt;
          #17#27 = #7#21;
          #17#28 = "B" + #7#22;

          #17#34 = "1";

          |SI #7#30 = 1;
               #17#35 = "S";
          |SINO;
               #17#35 = "N";
          |FINSI;
          #17#38 = #7#31;

          |GRABA 020#17a;
          |LIBERA #17;
     |FINSI;
|FPRC;


|DEFBUCLE ImosMultipieza;
     #7#2;
     32;
     3, "          ", "S";
     3, "zzzzzzzzzz", "S";
     ;
     NULL, ImosMultipieza;
|FIN;

|PROCESO ortsm030PV;
     #16#63 = #16#63 + #30#14;
|FPRC;

|DEFBUCLE ortsm030PV;
     #30#1;
     ;
     #16#28, #16#0, #16#1, 001, 0, " ", 00;
     #16#28, #16#0, #16#1, 999, 9, "z", 99;
     ;
     NULL, ortsm030PV;
|FIN;

|PROCESO ortsm029PV;
     #16#63 = #16#63 + #29#11;
|FPRC;

|DEFBUCLE ortsm029PV;
     #29#1;
     ;
     #16#28, #16#0, #16#1, 001, "                    ";
     #16#28, #16#0, #16#1, 999, "zzzzzzzzzzzzzzzzzzzz";
     ;
     NULL, ortsm029PV;
|FIN;

|PROCESO ortsm017PV;
     #16#63 = #16#63 + #17#32;
     #16#64 = #16#64 + #17#33;
|FPRC;

|DEFBUCLE ortsm017PV;
     #17#1;
     ;
     #16#28, #16#0, #16#1, 001;
     #16#28, #16#0, #16#1, 999;
     ;
     NULL, ortsm017PV;
|FIN;

|PROCESO PesoVolumen;
     #16#63 = 0;
     #16#64 = 0;
     |BUCLE ortsm017PV;
     |BUCLE ortsm029PV;
     |BUCLE ortsm030PV;
     |GRABA 020#16a;
|FPRC;

|DEFBUCLE PesoVolumen;
     #16#1;
     ;
     #15#25, #15#0, 001;
     #15#25, #15#0, 999;
     be;
     NULL, PesoVolumen;
|FIN;


|PROCESO PedidoLineas;
     |HAZ CalConsumoLin;
     |GRABA 020#16a;
|FPRC;

|DEFBUCLE PedidoLineas;
     #16#1;
     ;
     #15#25, #15#0, 001;
     #15#25, #15#0, 999;
     be;
     NULL, PedidoLineas;
|FIN;

|PROCESO PedidoHerrajes;
     #16#28 = #29#0;
     #16#0 = #29#1;
     #16#1 = #29#2;
     |LEE 020#16=;

     |DDEFECTO #27;
     #27#0 = #16#2;
     #27#1 = #49#1;
     #27#2 = #29#4;
     #27#3 = #29#5;
     #27#4 = #29#6;
     #27#5 = #29#7;
     #27#6 = #29#8;
     #27#7 = #29#9;
     #27#8 = #29#10;
     #27#9 = #29#11;
     |GRABA 020#27n;

     nCoste = nCoste + (#27#4 * #27#8);
|FPRC;

|DEFBUCLE PedidoHerrajes;
     #29#1;
     ;
     #17#0, #17#1, #17#2, #17#3, "                    ";
     #17#0, #17#1, #17#2, #17#3, "zzzzzzzzzzzzzzzzzzzz";
     ;
     NULL, PedidoHerrajes;
|FIN;

|PROCESO PedidoCanteado;
     #16#28 = #30#0;
     #16#0 = #30#1;
     #16#1 = #30#2;
     |LEE 020#16=;

     |DDEFECTO #28;
     #28#0 = #16#2;
     #28#1 = #49#1;
     #28#2 = #30#4;
     #28#3 = #30#5;
     #28#4 = #30#6;
     #28#6 = #30#8;
     #28#7 = #30#9;
     #28#8 = #30#10;
     #28#9 = #30#11;
     #28#10 = #30#12;
     #28#11 = #30#13;
     #28#12 = #30#14;
     #28#13 = #30#15;
     #28#14 = #30#16;
     #28#15 = #30#17;
     |GRABA 020#28n;

     nCoste = nCoste + (#28#11 * #28#13);
|FPRC;

|DEFBUCLE PedidoCanteado;
     #30#1;
     ;
     #17#0, #17#1, #17#2, #17#3, 0, " ", 00;
     #17#0, #17#1, #17#2, #17#3, 9, "z", 99;
     ;
     NULL, PedidoCanteado;
|FIN;

|PROCESO UltLin49;
     #49#0 = #16#2;
     #49#1 = 999;
     |LEE 000#49];
     |SI FSalida = 0;
          |LEE 000#49a;
     |SINO;
          |LEE 000#49u;
     |FINSI;
     |SI FSalida = 0; |Y #49#0 = #16#2;
          nLin = #49#1 + 1;
     |SINO;
          nLin = 1;
     |FINSI;

     |DDEFECTO #49;
     #49#0 = #16#2;
     #49#1 = nLin;   || #17#3;
     |LEE 101#49=;
     |SI FSalida ! 0; |GRABA 020#49b; |FINSI;
|FPRC;

|PROCESO PedidoCompoElemento;
     #37#0 = #20#0;
     #37#1 = #20#1;
     #37#2 = #18#4;
     #37#3 = #18#7;
     #37#4 = #18#8;
     #37#5 = #18#9;
     #37#6 = #18#10;
     #37#7 = #18#11;
     #37#8 = #18#12;
     #37#9 = #18#13;
     #37#10 = #18#14;
     #37#11 = #18#15;
     #37#12 = #18#16;
     |GRABA 020#37n;
|FPRC;

|DEFBUCLE PedidoCompoElemento;
     #18#1;
     ;
     #17#0, #17#1, #17#2, #17#3, 0;
     #17#0, #17#1, #17#2, #17#3, 999;
     ;
     NULL, PedidoCompoElemento;
|FIN;

|PROCESO PedidoCompo;
     #16#28 = #17#0;
     #16#0 = #17#1;
     #16#1 = #17#2;
     |LEE 020#16=;

     |HAZ UltLin49;
     #49#2 = #17#4;
     #49#3 = #17#5;
     #49#4 = (#17#16 * #36#9 / #36#7) * (#17#17 * #36#9 / #36#7) * #17#7;

     #19#0 = #49#2;
     |LEE 000#19=;
     |SI FSalida ! 0; |DDEFECTO #19; |FINSI;
     #49#5 = #19#9; || Precio de coste medio del articulo.

     #49#13 = #36#19;
     nCoste = #49#4 + (#49#4 * #49#13 / 100);
     #49#6 = nCoste * #49#5;
     #49#9 = 1;
     #49#10 = 1;
     #49#12 = #49#4;
     #49#15 = #19#9;
     #49#16 = "D";
     |SI #17#12 = "H";
          #49#4 = #17#7;
          #49#12 = #17#7;
     |FINSI;
     |GRABA 020#49a;
     |LIBERA #49;

     |DDEFECTO #20;
     #20#0 = #49#0;
     #20#1 = #49#1;
     |LEE 101#20=;
     |SI FSalida ! 0; |GRABA 020#20b; |FINSI;

     |SI #17#12 = "H";  |O #17#12 = "N";
          #20#2 = #17#12;
     |SINO;
          aAlfa = #17#11; |QBF aAlfa;
          |SI aAlfa = "";
               #20#2 = "S";
          |SINO;
               #20#2 = "T";
          |FINSI;
     |FINSI;

     #20#3 = #17#24;
     #20#4 = #17#14;
     #20#5 = #17#13;
     #20#6 = #17#18;
     #20#7 = #17#16;
     #20#8 = #17#17;
     #20#9 = #17#23;
     #20#10 = #17#19;
     #20#11 = #17#20;
     #20#12 = #17#21;
     #20#13 = #17#22;
     #20#14 = #17#26;
     #20#15 = #17#27;
     #20#16 = #17#28;
     #20#17 = #17#30;
     #20#18 = #17#31;
     #20#19 = #17#32;
     #20#20 = #17#33;
     #20#21 = #17#8;
     #20#22 = #17#7;
     #20#23 = #17#35;
     #20#24 = #17#36
     #20#25 = #17#37
     #20#26 = #17#38
     |GRABA 020#20a;
     |LIBERA #20;
     |SI #20#21 = "S";
          |BUCLE PedidoCompoElemento;
     |FINSI;

     nCoste = 0;
     |BUCLE PedidoCanteado;

     nCoste = 0;
     |BUCLE PedidoHerrajes;
|FPRC;

|DEFBUCLE PedidoCompo;
     #17#5;
     ;
     #15#25, #15#0, 001, 0;
     #15#25, #15#0, 999, 9;
     ;
     NULL, PedidoCompo;
|FIN;

|PROCESO Bortsm017;
     |BORRA 020#17a;
|FPRC;
|PROCESO Bortsm018;
     |BORRA 020#18a;
|FPRC;
|PROCESO Bortsm029;
     |BORRA 020#29a;
|FPRC;
|PROCESO Bortsm030;
     |BORRA 020#30a;
|FPRC;
|PROCESO Bortsm0_31;
     |BORRA 020#31a;
|FPRC;

|DEFBUCLE Bortsm017;
     #17#1;
     ;
     #16#28, #16#0, #16#1, 001;
     #16#28, #16#0, #16#1, 999;
     ;
     NULL, Bortsm017;
|FIN;

|DEFBUCLE Bortsm018;
     #18#1;
     ;
     #16#28, #16#0, #16#1, 001, 001;
     #16#28, #16#0, #16#1, 999, 999;
     ;
     NULL, Bortsm018;
|FIN;

|DEFBUCLE Bortsm029;
     #29#1;
     ;
     #16#28, #16#0, #16#1, 001, "                    ";
     #16#28, #16#0, #16#1, 999, "zzzzzzzzzzzzzzzzzzzz";
     ;
     NULL, Bortsm029;
|FIN;

|DEFBUCLE Bortsm030;
     #30#1;
     ;
     #16#28, #16#0, #16#1, 001, 0, " ", 00;
     #16#28, #16#0, #16#1, 999, 9, "z", 99;
     ;
     NULL, Bortsm030;
|FIN;

|DEFBUCLE Bortsm0_31;
     #31#1;
     ;
     #16#28, #16#0, #16#1, "                    ";
     #16#28, #16#0, #16#1, "zzzzzzzzzzzzzzzzzzzz";
     ;
     NULL, Bortsm0_31;
|FIN;
|PROCESO Bortsm028;
     |BORRA 020#28a;
|FPRC;

|DEFBUCLE Bortsm028;
     #28#1;
     ;
     #49#0, #49#1, 0, " ", 0;
     #49#0, #49#1, 9, "z", 99;
     be;
     NULL, Bortsm028;
|FIN;

|PROCESO Bortsm027;
     |BORRA 020#27a;
|FPRC;

|DEFBUCLE Bortsm027;
     #27#1;
     ;
     #49#0, #49#1, "                    ";
     #49#0, #49#1, "zzzzzzzzzzzzzzzzzzzz";
     be;
     NULL, Bortsm027;
|FIN;

|PROCESO Bortsm037;
     |BORRA 020#37a;
|FPRC;

|DEFBUCLE Bortsm037;
     #37#1;
     ;
     #49#0, #49#1, 1;
     #49#0, #49#1, 99;
     be;
     NULL, Bortsm037;
|FIN;

|PROCESO agifa049_1;
     |SI nSw = 0;
          nHay = 1;
          |ERROR10;
     |SINO;
          #20#0 = #49#0;
          #20#1 = #49#1;
          |LEE 101#20=;
          |SI FSalida = 0; |Y #49#10 ! 2;
               |BORRA 020#20a;
               |LIBERA #20;

               |BORRA 020#49a;
               |BUCLE Bortsm027;
               |BUCLE Bortsm028;
               |BUCLE Bortsm037;
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE agifa049_1;
     #49#1;
     10;
     #7#4, 001, 1;
     #7#4, 999, 4;
     be;
     NULL, agifa049_1;
|FIN;

|PROCESO Imos_8;
     |HAZ BuscaLinPed;
     |SI nHay = 0; |ACABA; |FINSI;

     |SI nHay = 2;
          #16#1 = #29#2;
     |SINO;
          #16#1 = #17#2;
     |FINSI;

     aAlfa = #7#35; |QBF aAlfa;
     |SI aAlfa = "PIEZA";
          nHerraje = 1;
          |HAZ Tipo3;
          nHerraje = 0;
     |SINO;
          |DDEFECTO #29;
          #29#0 = #16#28;
          #29#1 = #16#0;
          #29#2 = #16#1;
          #29#3 = #17#3;
          |SI nHay = 0;
               || nada
          |SINO;
               |SI nHay = 2;
                    #29#3 = 99;

                    |DDEFECTO #17;
                    |SELECT #1#17;
                    #17#0 = #16#28;
                    #17#1 = #16#0;
                    #17#2 = #29#2;
                    #17#3 = 99;
                    |LEE 000#17=;
                    |SI FSalida ! 0;
                         #17#4 = #36#27;
                         #17#5 = "Accesorios";
                         #17#7 = 1;
                         #17#12 = "N";
                         #17#25 = 0;
                         #17#26 = #36#27;
                         #17#34 = 9;
                         #17#36 = #7#0;
                         |GRABA 020#17n;
                    |FINSI;
               |FINSI;
               #29#4 = #7#15;
               |LEE 101#29=;
               |SI FSalida ! 0; |GRABA 020#29b; |FINSI;

               #29#6 = #29#6 + 1;
               #29#7 = #7#7 * #36#7 / #36#8;
               |SI #7#2 = 16;
                    #29#8 = "S";
               |SINO;
                    #29#8 = "N";
               |FINSI;

               #19#0 = #29#4;
               |LEE 000#19=;
               |SI FSalida ! 0; |DDEFECTO #19; |FINSI;

               |SI #36#23 = "N";
                    #29#5 = #19#1;
               |SINO;
                    #29#5 = #7#5;
               |FINSI;

               aAlfa = #29#5;
               |QBF aAlfa;
               |SI aAlfa = ""; #29#5 = #19#1; |FINSI;

               #29#10 = #19#9;
               #29#11 = #19#102;

               |GRABA 020#29a;
               |LIBERA #29;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Imos_7;
     |DDEFECTO #30;
     #30#0 = #16#28;
     #30#1 = #16#0;
     #30#2 = #16#1;
     #30#3 = #17#3;
     #30#4 = #ortsw007@#18; ||EDGE_ID
     #30#5 = #ortsw007@#19; ||EDGE_TRANS
     #30#16 = nPadre;
     |LEE 101#30=;
     |SI FSalida ! 0; |GRABA 020#30b; |FINSI;

     #30#6 = #ortsw007@#5;  ||NAME2
     #30#12 = #ortsw007@#4;  ||NAME

     #19#0 = #30#6;
     |LEE 000#19=;
     |SI FSalida ! 0; |DDEFECTO #19; |FINSI;
     #30#8 = #19#1;

     #30#9 = #ortsw007@#7 * #36#7 / #36#8;
     #30#10 = #ortsw007@#8 * #36#7 / #36#8;
     #30#11 = #ortsw007@#9;

     #34#0 = #30#6;
     #34#1 = #17#6;
     |LEE 000#34=;
     |SI FSalida = 0;
          #19#0 = #34#3;
     |SINO;
          #19#0 = #30#6;
     |FINSI;
     |LEE 000#19=;
     |SI FSalida ! 0; |DDEFECTO #19; |FINSI;

     #30#13 = #19#9;
     #30#14 = #19#102 * #17#16 / 100;
     #30#15 = #30#15 + 1;
     #30#16 = nPadre;
     #30#17 = #7#34;
     |GRABA 020#30a;
     |LIBERA #30;

     |SI #7#33 = 0;
          aLetra = "";
          |SI #ortsw007@#18 = 1; aLetra = #36#10; |FINSI;
          |SI #ortsw007@#18 = 2; aLetra = #36#11; |FINSI;
          |SI #ortsw007@#18 = 3; aLetra = #36#12; |FINSI;
          |SI #ortsw007@#18 = 4; aLetra = #36#13; |FINSI;
          |SI aOrden = "";
               aOrden = aLetra + #ortsw007@#9;
          |SINO;
               aOrden = aOrden + "-" + aLetra + #ortsw007@#9;
          |FINSI;

          aAlfa = #7#24; ||INFO4
          |QBF aAlfa;
          aPos = aAlfa % A0;
          nSw = 0;
          aTxt = "";
          |PARA i = 1; |SI i [ aPos; |HACIENDO i = i + 1;
               nPos = i * 100 + 1;
               aCar = aAlfa % nPos;
               |SI aCar = #7#9; |Y nSw = 0;
                    nSw = 1;
               |SINO;
                    |SI nSw = 1;
                         aAlfa1 = "0123456789" - aCar;
                         |SI FSalida = 0;
                              aTxt = aTxt + aCar;
                         |SINO;
                              |SAL;
                         |FINSI;
                    |FINSI;
               |FINSI;
          |SIGUE;
          aOrden  = aOrden + aTxt;
     |SINO;
          |SI #ortsw007@#34 = 1;
               aTxt = #ortsw007@#18 + ":" + #ortsw007@#9 + " ";
               aOrden = aOrden + aTxt
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE Imos_8_Todos;
     #7#2;
     20;
     8, "          ", 1;
     8, "zzzzzzzzzz", 1;
     ;
     NULL, Imos_8;
|FIN;

|PROCESO Tipo3;
     |SELECT #4#17;
     #17#0 = #16#28;
     #17#1 = #16#0;
     #17#2 = #16#1;
||     #17#26 = #7#25;
||     |SI nHerraje = 1; #17#26 = #7#15; |FINSI;
     || Cuando son estanterias, cajas. Pasa a grabar unitariamente
     #17#26 = "zzzzz";
     |LEE 101#17=;
     |SI FSalida ! 0;
          |SELECT #1#17;
          #17#0 = #16#28;
          #17#1 = #16#0;
          #17#2 = #16#1;
          #17#3 = 999;
          |LEE 000#17];
          |SI FSalida = 0;
               |LEE 000#17a;
          |SINO;
               |LEE 000#17u;
          |FINSI;
          |SI FSalida = 0; |Y #17#0 = #16#28; |Y #17#1 = #16#0; |Y #17#2 = #16#1;
               nLin = #17#3 + 1;
          |SINO;
               nLin = 1;
          |FINSI;
          |DDEFECTO #17;
          #17#0 = #16#28;
          #17#1 = #16#0;
          #17#2 = #16#1;
          #17#3 = nLin;
          |GRABA 020#17b;
     |FINSI;

     |SI nHerraje = 0;
          |SELECT #2#ortsw007@;
          #ortsw007@#1 = 4;
          #ortsw007@#13 = #7#12;
          |LEE 000#ortsw007@.=;
          |SI FSalida ! 0; |DDEFECTO #ortsw007@; |FINSI;

          aArtOpt = #ortsw007@#4;

          #17#4 = #ortsw007@#5;
          aAlfa = #7#27; |QBF aAlfa; ||info1
          |SI aAlfa = "";
               aAlfa = #7#4; |QBF aAlfa;   ||name
               aAlfa = aAlfa + " " + #7#6; ||kp
          |FINSI;
          #17#5 = aAlfa;
          ||-----------------
          nHay = 0;
          |SELECT #2#ortsw007@;
          #ortsw007@#1 = 8;
          #ortsw007@#13 = #7#13;
          |LEE 000#ortsw007@.];
          |PARA; |SI FSalida = 0; |Y #ortsw007@#1 = 8; |Y #ortsw007@#13 = #7#13; |HACIENDO;
               |SI #ortsw007@#2 = 16;
                    nHay = 1; |SAL;
               |FINSI;
               |LEE 000#ortsw007@.s;
          |SIGUE;
          |SI nHay = 0; |DDEFECTO #ortsw007@; |FINSI;

          aAlfa = #ortsw007@#4 - "_";
          |SI FSalida > 0;
               nPos = (FSalida - 1) / 100 + 1;
               aAlfa = #ortsw007@#4 % nPos;
          |SINO;
              aAlfa = "";
          |FINSI;
          #17#11 = aAlfa;
     |SINO;
          #17#4 = #7#15;
          #17#5 = #7#5;
     |FINSI;

     #17#6 = #7#26;
     aAlfa = #17#6; |QBF aAlfa;
     |SI aAlfa = "";
           #17#6 = #16#59;
     |FINSI;

     #17#7 = #17#7 + 1;
     #17#8 = "N";
     #17#9 = 1;
     #17#10 = 1;

     nHay = 0;
     |SELECT #2#ortsw007@;
     #ortsw007@#1 = 8;
     #ortsw007@#13 = #7#13;
     |LEE 000#ortsw007@.];
     |PARA; |SI FSalida = 0; |Y #ortsw007@#1 = 8; |Y #ortsw007@#13 = #7#13; |HACIENDO;
          |SI #ortsw007@#2 = 16;
               nHay = 1; |SAL;
          |FINSI;
          |LEE 000#ortsw007@.s;
     |SIGUE;
     |SI nHay = 0; |DDEFECTO #ortsw007@; |FINSI;

     aAlfa = #ortsw007@#4 - "_";
     |SI FSalida > 0;
           nPos = (FSalida - 1) / 100 + 1;
          aAlfa = #ortsw007@#4 % nPos;
     |SINO;
          aAlfa = "";
     |FINSI;
     #17#11 = aAlfa;

     |SI nHerraje  = 0;
          #17#12 = "S";
          #17#13 = #7#8 * #36#7 / #36#8;
          #17#14 = #7#7 * #36#7 / #36#8;
          #17#15 = "S";
          #17#16 = #7#10 * #36#7 / #36#8;
          #17#17 = #7#11 * #36#7 / #36#8;
          #17#18 = #7#9;
          #17#26 = #7#25;

          nHay = 0;
          |SELECT #2#ortsw007@;
          #ortsw007@#1 = 8;
          #ortsw007@#13 = #7#13;
          |LEE 000#ortsw007@.];
          |PARA; |SI FSalida = 0; |Y #ortsw007@#1 = 8; |Y #ortsw007@#13 = #7#13; |HACIENDO;
               |SI #ortsw007@#2 = 16;
                    nHay = 1; |SAL;
               |FINSI;
               |LEE 000#ortsw007@.s;
          |SIGUE;
          |SI nHay = 0; |DDEFECTO #ortsw007@; |FINSI;

          aAlfa = #ortsw007@#4 - "_";
          |SI FSalida > 0;
               nPos = (FSalida - 1) / 100 + 1;
               aAlfa = #ortsw007@#4 % nPos;
          |SINO;
               aAlfa = "";
          |FINSI;
          #17#11 = aAlfa;
     |SINO;
          #17#12 = "H";
          #17#13 = 0;
          #17#15 = "N";
          #17#17 = 0;
          #17#18 = 0;
          #17#26 = #7#15;
          #17#11 = "";
          #17#8 = "N";
     |FINSI;

     |SI #16#61 = "S"; || Medidas Especiales
          |SI #7#29 = 1;
               #17#20 = #7#16;
               #17#21 = #7#17;
          |FINSI;
     |SINO;
          nHay = 0;
          #20#0 = #16#2;
          #20#1 = 1;
          |LEE 000#20];
          |PARA; |SI FSalida = 0; |Y #20#0 = #16#2; |HACIENDO;
               |SI #20#14 = #17#26;
                    nHay = 1; |SAL;
               |FINSI;
               |LEE 000#20s;
          |SIGUE;
          |SI nHay = 0;
               |SI #7#29 = 1;
                    #17#20 = #7#16;
                    #17#21 = #7#17;
               |FINSI;
          |SINO;
               |SI #7#29 = 1;
                    #20#0 = #49#0;
                    #20#1 = #49#1;
                    |LEE 000#20=;
                    |SI FSalida ! 0; |DDEFECTO #20; |FINSI;
                    #17#20 = #20#11;
                    #17#21 = #20#12;
               |FINSI;
          |FINSI;
     |FINSI;
     #17#22 = #7#24;
     #17#23 = aArtOpt;
     #17#25 = 0;

     #17#27 = #7#21;
     #17#28 = "B" + #7#22;
     aAlfa = #7#4; |QBF aAlfa;
     aAlfa = aAlfa + #7#6;
     #17#30 = aAlfa;
     #17#31 = #7#23;
     #17#32 = #7#28;
     #17#33 = #17#16 / 100 * #17#17 / 100 * #17#18 / 1000;

     #19#0 = #17#4
     |LEE 000#19=;
     |SI FSalida ! 0; |DDEFECTO #19; |FINSI;

     |SI nHerraje = 1;
          aAlfa = #17#5;
          |QBF aAlfa;
          |SI aAlfa = ""; #17#5 = #19#1;|FINSI;
          #17#14 = #7#7 * #36#7 / #36#8;
          #17#16 = #7#7 * #36#7 / #36#8;
     |FINSI;

     aAlfa = #17#11; |QBF aAlfa;
     |SI aAlfa ! "";
          #17#34 = 1;
     |SINO;
          aAlfa = #19#125; |QBF aAlfa;
          |SI aAlfa = "K";
               #17#34 = 2;
          |SINO;
               #17#34 = 3;
          |FINSI;
     |FINSI;
     |SI nHerraje  = 1; #17#34 = 2; |FINSI;

     |SI #7#30 = 1;
          #17#35 = "S";
     |SINO;
          #17#35 = "N";
     |FINSI;
     #17#36 = #7#0
     #17#37 = #7#12
     #17#38 = #7#31
     |SI nHerraje = 1;
          #17#20 = "";
          #17#22 = "";
          #17#26 = #17#4;
          #17#30 = #17#5;
          #17#32 = 0;
          #17#35 = "N";
     |FINSI;
     |GRABA 020#17a;
     |LIBERA #17;
|FPRC;

|PROCESO Tipo1;
     |SI #16#61 = "N";
          #19#0 = #16#2;
          |LEE 101#19=;
          |SI FSalida = 0;
               #19#102 = #7#28;
               |GRABA 020#19a;
               |LIBERA #19;
          |FINSI;

          nSw = 0; nHay = 0;
          |BUCLE agifa049_1;
          |SI #36#14 = "S";
               nSwActualiza = 1;
               nSw = 1; |BUCLE agifa049_1;
               |BUCLE Bortsm017;
               |BUCLE Bortsm018;
               |BUCLE Bortsm029;
               |BUCLE Bortsm030;
               |BUCLE Bortsm0_31;
          |FINSI;
     |FINSI;
     |GRABA 020#16a;
|FPRC;

|DEFBUCLE Imos_7;
     #ortsw007@#4004;
     ;
     7, #7#12, " ", 0;
     7, #7#12, "z", 999999999;
     ;
     NULL, Imos_7;
|FIN;

|PROCESO B_Imos_7;
     |BUCLE Imos_7;
|FPRC;

|DEFBUCLE Imos_8;
     #ortsw007@#2002;
     20;
     8, #7#13, 1;
     8, #7#13, 1;
     ;
     NULL, Imos_8;
|FIN;

|PROCESO B_Imos_8;
     |BUCLE Imos_8;
|FPRC;

|PROCESO Imos;
     aArti = #7#4; |QBF aArti;

     |SI #7#1 = 1; |O #7#1 = 3;
          #16#28 = #15#25
          #16#0 = #15#0;
          #16#1 = #7#3;
          |LEE 121#16=;
     |FINSI;

     |SI #7#1 = 1; |Y aTyp = "1";       || Actualiza Escandallo
          |HAZ Tipo1;
     |FINSI;
     |SI #7#1 = 3; |Y aTyp = "3"; |Y #7#20 ! 0; || Aux. Pedidos Combinaciones
          |SELECT #3#ortsw007@;
          #ortsw007@#1 = 3;
          #ortsw007@#12 = #7#13;
          |LEE 000#ortsw007@.=;
          |SI FSalida = 0; |Y #ortsw007@#20 = 1;  || BombFlag padre = 1
               #7#32 = "S";
               |GRABA 020#7a;
          |SINO;
               |HAZ Tipo3;

               aOrden = "";
               nPadre = 0;
               |BUCLE Imos_7;                || Aux. Pedidos Canteados

               #17#19 = aOrden;              ||Orden Canteado
               |GRABA 020#17a;
               |LIBERA #17;
/*
               |BUCLE Imos_8;                || Aux. Pedidos Herrajes
*/
          |FINSI;
     |FINSI;

     |LIBERA #16;
|FPRC;

|DEFBUCLE Imos;
     #7#3;
     ;
     1, "          ";
     3, "zzzzzzzzzz";
     be;
     NULL, Imos;
|FIN;

|PROCESO CheqEscan;
     nHay = 1;
     |ERROR10;
|FPRC;

|DEFBUCLE CheqEscan;
     #49#1;
     ;
     #7#4, 001;
     #7#4, 999;
     ;
     NULL, CheqEscan;
|FIN;

|PROCESO ChequeaImos;
     #16#28 = #15#25
     #16#0 = #15#0;
     #16#1 = #7#3;
     |LEE 000#16=;
     |SI FSalida ! 0;
          aAlfa = "0000No existe la línea de pedido:" + #7#3;
          |MENAV aAlfa;
          nError = 1;
     |SINO;
          aAlfa = #16#2; |QBF aAlfa;
          aArti = #7#4; |QBF aArti;
          |SI aArti ! aAlfa;
               aAlfa = "0000Línea de pedido " +#7#3+ " con distinto articulo: " + aArti;
               |MENAV aAlfa;
               nError = 1;
          |FINSI;
          |SI #16#5 ! #7#14;
               aAlfa = "0000Línea de pedido " + #7#3 + " con distintas unidades: " + #7#14;
               |MENAV aAlfa;
               nError = 1;
          |FINSI;
          nHay = 0;
          |BUCLE CheqEscan;
          |SI nHay = 0;
               aAlfa = "0000Escandallo inexistente o incompleto: " + aArti;
               |MENAV aAlfa;
               nError = 1;
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE ChequeaImos;
     #7#2;
     ;
     1, "          ";
     1, "zzzzzzzzzz";
     ;
     NULL, ChequeaImos;
|FIN;

|PROCESO Registro;
     nRegistro = nRegistro + 1;
     |SI nRegistro = 1; |ACABA; |FINSI; || los nombres de los campos
     #7#0 = Valor1;
     #7#1 = Valor2;
     #7#2 = Valor3;
     #7#3 = Valor4;
     #7#4 = Valor5;
     #7#5 = Valor6;
     #7#6 = Valor7;
     #7#7 = Valor8;
     #7#8 = Valor9;
     #7#9 = Valor10;
     #7#10 = Valor11;
     #7#11 = Valor12;
     #7#12 = Valor13;
     #7#13 = Valor14;
     #7#14 = Valor15;
     #7#15 = Valor16;
     #7#16 = Valor17;
     #7#17 = Valor18;
     #7#18 = Valor19;
     #7#19 = Valor20;
     #7#20 = Valor21;
     #7#21 = Valor22;
     #7#22 = Valor23;
     #7#23 = Valor24;
     #7#24 = Valor25;
     #7#25 = Valor26;
     #7#26 = Valor27;
     #7#27 = Valor28;
     #7#28 = Valor29;
     #7#29 = Valor30;
     #7#30 = Valor31;
     #7#31 = Valor32;
     ||#7#32 - este no
     #7#33 = Valor33;
     #7#34 = Valor34;
     #7#35 = Valor35;
     |GRABA 000#7n;      || Sin error de duplicados
|FPRC;

|PROCESO CopiaExe;
     |DBASE aAlfa;
     aOrg = aAlfa + "plugins/sqlserver.exe";
     aDes = aPath + aDsDestino + "/sqlserver.exe";
     aContiene = aOrg;
     |ESPECIFICA "MD5", aContiene;
     aMd5Org = FSalida;
     aContiene = aDes;
     |SI aIP = "";
          |ESPECIFICA "MD5", aContiene;
     |SINO;
          |ESPECIFICA "REMOTOMD5", aContiene;
     |FINSI;
     aMd5Des = FSalida;
     |SI aMd5Org ! aMd5Des;
          |SI aIP = "";
               |COPIA_FICHERO aOrg, aDes;
          |SINO;
               |ENVIA_FICHERO aOrg, aDes;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO CheqDat;
     |XABRE "C", aDat, nHandle;
     |SI FSalida ] 0;
          |XLEE nHandle, 250, aAlfa;
          |XCIERRA nHandle;
     |FINSI;
     aAlfa = aAlfa % 107;
     |SI aAlfa = "ORDERID";
          FSalida = "0";
     |SINO;
          FSalida = "1";
     |FINSI;
|FPRC;

|PROCESO CambiaBarra;
     aAlfa = "";
     aLon = aPath % A0;
     |PARA i = 1; |SI i [ aLon; |HACIENDO i = i + 1;
          nPos = i * 100 + 1;
          aCar = aPath % nPos;
          |SI aCar = "/"; aCar = "\"; |FINSI;
          aAlfa = aAlfa + aCar;
     |SIGUE;
     aPath = aAlfa;
|FPRC;

|PROCESO CreaBat;
     |XABRE "CWB", aBat, nHandle;
     aAlfa = "@echo off " + aFin;
     |XGRABA nHandle, aAlfa;

     aAlfa = aPath + "tmp\sqlserver " + #36#2; |QBF aAlfa;
     |XGRABA nHandle, aAlfa;

     aAlfa = " " + #36#5; |QBF aAlfa;
     |XGRABA nHandle, aAlfa;

     aAlfa = " " + #36#6; |QBF aAlfa;
     |XGRABA nHandle, aAlfa;

     aAlfa = " " + #36#3; |QBF aAlfa;
     |XGRABA nHandle, aAlfa;

     aAlfa = " " + &34 + "SELECT ORDERID,TYP,PARTTYPE,MAINLINE,NAME,NAME2,";
     aAlfa = aAlfa + "KMS,LENGTH,WIDTH,THICKNESS,CLENGTH,CWIDTH,ID,PARENTID,";
     aAlfa = aAlfa + "CNT,ARTICLE_ID,CNC_BARCODE1,CNC_BARCODE2,EDGE_ID,";
     |XGRABA nHandle, aAlfa;

     aAlfa = "EDGE_TRANS,BOM_FLAG,INFO1,INFO2,INFO3,INFO4,INFO5,";
     aAlfa = aAlfa + "RENDERPMAT,TEXT1,WEIGHT,NC_FLAG,CUT_FLAG,MATID,ISPEC,EDGECNC,PRODUCER";
     |XGRABA nHandle, aAlfa;

     aAlfa = " FROM " + #36#4; |QBF aAlfa;
     |XGRABA nHandle, aAlfa;

     aOrderId = #15#25; |QBF aOrderId;
     aOrderId = aOrderId + "_" + (("000000" + #15#0) % -106);
     aAlfa = " WHERE ORDERID='" + aOrderId; |QBF aAlfa;
     aAlfa = aAlfa + "' ORDER BY ORDERID, TYP" + &34;
     |XGRABA nHandle, aAlfa;

     aAlfa = " >" + aDat + aFin;
     |XGRABA nHandle, aAlfa;

     aAlfa = "exit";
     |XGRABA nHandle, aAlfa;

     |XCIERRA nHandle;
|FPRC;

|PROCESO Caracter;
     |SI aCar = aTab;
          Valor = aCampo;
          aCampo = "";
          IValor = IValor + 1;
          nCampo = nCampo + 1;
          |SI nCampo > 90; |ACABA; |FINSI; || Por si.... :)
     |SINO;
          |SI aCar = aAscii10; |O aCar = aAscii13;
               Valor = aCampo;
               |SI nCampo > 0; |HAZ Registro; |FINSI;
               IValor = Valor1<-;
               nCampo = 0;
               aCampo = "";
          |SINO;
               aCampo = aCampo + aCar;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO ImportaImpos;
     |ABRE #36; |LEE 000#36p; |CIERRA #36;

     aTab = &9;
     aAscii10 = &10;
     aAscii13 = &13;
     aFin = aAscii13 + aAscii10;

     |QBF aIP;
     |IP_REMOTO aIP;
     |SI aIP = "";
          |DBASS aPath;
     |SINO;
          |ESPECIFICA "RBASS", aLocal;
          aPath = aLocal;
     |FINSI;

     aDsDestino = "tmp"; |HAZ CopiaExe;
     aDsDestino = "bin"; |HAZ CopiaExe;

     |HAZ CambiaBarra;
     aBat = aPath + "tmp\sql" + Usuario + ".bat";
     aDat = aPath + "tmp\dat" + Usuario + ".txt";

     |HAZ CreaBat;

     aBat  = "cmd " + &34 + "/c START /MIN /WAIT " + aBat + &34;
     |SI aIP = "";
          |SYSTEM aBat;
     |SINO;
          |RSYSTEM aBat;
     |FINSI;

     |HAZ CheqDat;
     |SI FSalida ! 0;
          |MENAV "0000Error en la comunicación con el servidor...";
     |SINO;
          |HAZ CreaTempo; |NOME_DAT #7 Tempo; aTmp7 = Tempo; |DELINDEX #7;
          |ABRE #7;

          |DFICE aDes; aDes = aDes + Usuario;
          |RECIBE_FICHERO aDat, aDes;
          |SI FSalida ! 0;
               |MENAV "0000Error al recibir el fichero...";
          |SINO;
               aDat = aDes;
               nRegistro = 0;
               IValor = Valor1<-;
               |XABRE "B", aDat, nHandle;
               |SI FSalida ] 0;
                    |XLEE nHandle, 250, aTmp;
                    |PARA; |SI FSalida > 0; |HACIENDO;
                         |PARA; |SI aTmp ! ""; |HACIENDO;
                              aCar = aTmp % 101;
                              |SI aTmp = aCar;
                                   aTmp = ""; ||con un caracter falla: "aTmp = aTmp % 2"
                              |SINO;
                                   aTmp = aTmp % 2;
                              |FINSI;
                              |HAZ Caracter;
                         |SIGUE;
                         |XLEE nHandle, 250, aTmp;
                    |SIGUE;
                    |XCIERRA nHandle;
               |FINSI;
          |FINSI;
          |CIERRA #7;

          |ABRE #16;
          |BUCLE ChequeaImos;
          |CIERRA #16;
          |SI nError = 0;
               |DDEF aAlfa; aAlfa = aAlfa + "ortsw007";
               |CARGA_DEF aAlfa, ortsw007@;
               |SI FSalida = 0;
                    |NOME_DAT #ortsw007@#aTmp7#;
                    |ABRET #ortsw007@;

                    nSwActualiza = 0;
                    |ABRE #16;
                    |ABRE #20;
                    |ABRE #19;
                    aTyp = "1"; |BUCLE Imos;
                    |CIERRA #19;
                    |CIERRA #20;

                    |ABRE #17;
                    |ABRE #19;
                    |ABRE #20;
                    |ABRE #29;
                    |ABRE #30;
                    |ABRE #49;
                    |ABRE #34;

                    aTyp = "3"; |BUCLE Imos;
                    |ABRE #19;
                    |ABRE #18;
                    |BUCLE ImosMultipieza;
                    |BUCLE Imos_8_Todos;
                    |ABRE #18;
                    |CIERRA #19;

                    |CIERRA #34;
                    |CIERRA #49;
                    |CIERRA #30;
                    |CIERRA #29;
                    |CIERRA #20;
                    |CIERRA #19;
                    |CIERRA #17;
                    |CIERRA #16;

                    |BUCLE PedidoLineas; || Consumos

                    |SI nSwActualiza = 1;
                         |ABRE #16;
                         |ABRE #20;
                         |ABRE #28;
                         |ABRE #27;
                         |ABRE #49;
                         |ABRE #19;
                         |ABRE #37;
                         |BUCLE PedidoCompo;
                         |CIERRA #37;
                         |CIERRA #19;
                         |CIERRA #49;
                         |CIERRA #27;
                         |CIERRA #28;
                         |CIERRA #20;
                         |CIERRA #16;
                    |FINSI;

                    |CIERRAT #ortsw007@;
                    |DESCARGA_DEF #ortsw007@;
               |FINSI;

               ||Calculo Peso y Volumen
               |BUCLE PesoVolumen;

               |MENAV "0000Importación finalizada...";
          |FINSI;
          |DELINDEX #7; Tempo = aTmp7; |HAZ BoraTempo;
     |FINSI;
|FPRC;
