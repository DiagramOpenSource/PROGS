|FICHEROS;
     agifa077 #0;
     agifa080 #3;
     agifa019 #2;
     agifa017 #8;
     agifa112 #4;
     agifa065 #10;
     agifa041 #11;  || datos empresa
     agifa007 #40;
     agifa142 #41;
     agifa010 #50;
     agifa321 #45;  || Parametros de Divisas
     agifa322 #46;  || Lineas de Proveedores/Divisas
     agifa323 #47;  || Proveedores/Divisas
     agifa324 #42;  || Divisas
     fries003 #33;  || Serie Por Defecto.......
|FIN;

|VARIABLES;
     &PRMNT_aHisto = "";

     &enSwAct   = 0;
     nSwFlag = 0;
     nModo          = 0;
     nSwPorVar      = 0;
     aSerie         = "";
     nNume          = 0;
     aAlfa          = "";
     aDatos         = "";
     aDirEmp        = "";
     nCodEmp        = 0;
     aProv          = "";
     nForma    = 0;
     nImpo     = 0;
     nMes      = 0;
     aMes      = "";
     nNetoDto  = 0;
     &ser_ped       = "";
     &num_ped       = 0;

     &nDeci_Base     = 0;   || Decimales en Totales de la moneda base
     &nDeci_PreBase  = 0;   || Decimales en el Precio en moneda base
     &nDeci_BaseMx   = 0;   || Decimales en Totales de la moneda base (Maxima precision)
     &nDeci_PreBaseMx= 0;   || Decimales en el Precio en moneda base (Maxima precision)
     &nDeci_Div      = 0;   || Decimales en Totales de la divisa
     &nDeci_PreDiv   = 0;   || Decimales en el Precio en divisa
     &nDeci_TotVis   = 0;   || Decimales en Totales visibles (el de la moneda de trabajo)
     &nDeci_TotNoVis = 0;   || Decimales en Totales no visibles (el de la otra moneda)
     &nDeci_PreVis   = 0;   || Decimales en el Precio visual. (lineas)
     &nDeci_ImpVis   = 0;   || Decimales en el Importe visual. (lineas)
     nfecha    = 0;
     &ac_divisa = "N";   || Vale "S" cuando las divisas en compras estan
                         || activadas y la serie es de divisas
     &pintado = 0;       || Sirve para controlar los pintados de las lineas
     &decim_divisa = 0;  || Decimales de la divisa
     &precio_divisa = 0; || Decimales del precio en divisa
     &decim_base = 0;    || Decimales de la moneda base
     &precio_base = 0;   || Decimales del precio en moneda base
     div_act = 0;        || Divisa actualizada. Me sirve en un PARA para que
                         || no salga hasta que este actualizada (div_act=1)
     &prov_divisa = "";  || Codigo de Proveedor. Se pasa a PROVEED/DIVISAS
     &divisa = "";       || Codigo de Divisa. Se pasa a ACTUAL. de DIVISAS
     nModo077 = 0;
     &ser_alb = "";
     &num_alb = 0;
     t_totalb = 0;
     param     = "";
     mensa     = "";
     base      = "";
     alfa      = "";
     alfa1     = "";
     direc     = "";
     asto      = 0;
     &bl       = "                              ";

     mensaje01 = "0423Actualizando linea:";
     mensaje10 = "0000Este albaran contiene lineas provenientes de Pedidos";
     mensaje12 = "0000Este albaran esta FACTURADO";
     mensajei  = "2400NDesea imprimir este albaran? ";
     mensaje   = "0000No puede cambiar este campo !";
     informe1  = "agifa077";
     informe2  = "agif1077";
     informe   = "";
     puente    = "";
     &iva1     = 0;
     &iva2     = 0;
     &iva3     = 0;
     &iva4     = 0;
     &iva5     = 0;
     &re1      = 0;
     &re2      = 0;
     &re3      = 0;
     &re4      = 0;
     &re5      = 0;
     &def_alm  = 0;
     &recoge   = ""; || Lo utilizo en agifa080
     &n_dec    = 0;
     imneto    = 0;
     tf        = 0;
     xmes      = 0;
     con       = 0;
     margen    = 0;
     control   = 0;
     pm        = 0;
     de        = 0;
     pe        = 0;
     m1        = 0;
     m2        = 0;
     m3        = 0;
     m4        = 0;
     m5        = 0;
     m6        = 0;
     pcm       = 0;
     tipalb    = 1;
     contenido = 0;
     modo = 0;
     impret = 0;
     &codiBarra = "";
     calpre = 0;
     precio = 0;
     pvp1   = 0;
     pvp2   = 0;
     pvp3   = 0;
     pvp4   = 0;
     pvp5   = 0;
     pvp6   = 0;
     aConfir = "";
     varios = 0;
     portes = 0;
     mens = 0;
     caltot = 0;
     recalpre = 0;
     factordec = 0;
     i = 0;

     nTecla        = 0;
     &eaCodigo     = "";
     &eaTipoCodigo = "PR";
     &factur = 0;
     &prov   = "";
     &fpago  = "";
     &dto    = 0;
     &dtoa   = 0;
     &dtop   = 0;

     nImpDiv = 0;
     nOk = 0;
     nPos = 0;
     aCar = "";
|FIN;

|INCLUYE i_varart;

|CALCULO prov;|TIPO 0;
     |SI factur = 1;|O factur = 2;
       |SI factur = 2;
         #0#42 = "S";
         |PINTA #0#42;
       |FINSI;
       #0#3 = prov;
       #0#8 = fpago;
       #0#5 = dto;
       #0#32 = dtoa;
       #0#7 = dtop;
       |PINTA #0#3;
       |PINTA #0#8;
       |PINTA #0#5;
       |PINTA #0#32;
       |PINTA #0#7;
     |FINSI;
|FCAL;

|PROCESO con0; |TIPO 6;
     nTecla = FSalida;
     modo = (FEntrada / 100) ! 0;
     |SI FSalida = 9;
          |SI modo ! 1;
               |CONSULTA #3#0;
               |PTEC 802;
               |PTEC 802;
          |SINO;
               |ERROR;
          |FINSI;
     |FINSI;
|FPRC;


|PROCESO serie7; |TIPO 7;
     |HAZ ControlUsuAC;
     |SI enErr ! 0; |PTEC 807; |ACABA; |FINSI;


     || SI CONSULTO DESDE agifa383 , num_alb ! 0;
     |SI PRMNT_aHisto ! "";
          ser_alb = PRMNT_aHisto % 105;
          num_alb = (A\(PRMNT_aHisto % 6));

         #0#50 = ser_alb;
         #0#0 = num_alb;
         |PTEC 802;
         |PTEC 802;
         |PINTA #0#50;
       |PINTA #0#0;
     |FINSI;

     param = PARAMETRO; |QBF param;     || LLamado por agivf015
     |SI param ! "";
          #0#50 = param % 102;
          alfa  = param % 306;  #0#0 = alfa;
     |FINSI;
|FPRC;

|PROCESO serie30; |TIPO 30;
     |SI param ! "";                    || LLamado por agivf015
          |PTEC 807;
          |PTEC 807;
     |FINSI;
|FPRC;

|CALCULO fecalbc; |TIPO 7;
modo = (FEntrada / 100) ! 0;
|SI modo = 2;
  |CAMPO_MODIFICA #0#1 , 1 , "N";
|SINO;
  |CAMPO_MODIFICA #0#1 , 1 , "S";
|FINSI;
|FCAL;

|CALCULO cogeconc;|TIPO 0;
control = 0;
|FCAL;

|CALCULO pedconc;|TIPO 6;
con = FEntrada / 100;
con = con ! 0;
|SI con = 2;|Y #0Campo ! Contenido;
    |SI #0#41 > 0;|O #0#38 = AS;
       #0Campo = Contenido;
       |PINTA #0Campo;
       |SI #0#41 > 0;
         |MENAV mensaje10;
       |FINSI;
       |SI #0#38 = AS;
          |MENAV mensaje12;
       |FINSI;
       |ERROR;
    |SINO;
       control = 1;
    |FINSI;
|FINSI;
|FCAL;

|CALCULO dcon;|TIPO 6;
con = FEntrada / 100;
con = con ! 0;
|SI con = 2;|Y #0Campo ! Contenido;
    |SI #0#38 = AS;
       |MENAV mensaje12;
       |ERROR;
    |SINO;
       nSwPorVar = 1;
       control = 1;
    |FINSI;
|FINSI;
|FCAL;

|PROCESO Tipo1_77; |TIPO 1;
     nSwFlag = 0;
|FPRC;

|CALCULO acacpc;|TIPO 2;
     nForma = 0 +. 1;
     con = (FEntrada / 100) ! 0;

     |SI con = 1; nSwFlag = 0; |FINSI;
     |SI nSwFlag = 1; |ERROR; |ACABA; |FINSI;

     |HAZ ControlUsuAC;
     |SI enErr ! 0;
          nSwFlag = 1;
          |ERROR; |ACABA;
     |FINSI;

     |C_M #0#54, 1, "S";
     |SI con = 2;
          mensa = "";
          |HAZ MiraPasadoVenta;
          |SI mensa ! "";
               nSwFlag = 1;
               |ERROR; |ACABA;
          |FINSI;
     |FINSI;
     |SI con = 3;
          eaSer = #0#50;
          enDocu = #0#0;
          efFec = #0#1;
          |HAZ MiraHisC;
          |SI enErrHis ! 0;
               nSwFlag = 1;
               |ERROR; |ACABA;
          |FINSI;
     |FINSI;
     |SI con = 3;|O con = 2;
         |SI #0#41 > 0;
               |MENAV mensaje10;
               nSwFlag = 1;
               |ERROR; |ACABA;
         |FINSI;
         |SI #0#38 = AS;
               |MENAV mensaje12;
               nSwFlag = 1;
               |ERROR;|ACABA;
         |FINSI;
     |FINSI;

     |SI #0#75 = "S"; |Y nForma = -1;
          |HAZ EsIber21;
          |SI FSalida = 0; |Y #41#164 = "D";
               |MENAV "0000Albaran Exportado...";
               |HAZ ClaveAlbIber;
               |SI FSalida ! 0;
                    nSwFlag = 1;
                    |ERROR; |ACABA;
               |FINSI;
          |SINO;
               |MENAV "0000Albaran Exportado, no se puede Modificar...";
               nSwFlag = 1;
               |ERROR; |ACABA;
          |FINSI;
     |FINSI;

     #0#75 = "N";   ||EXPORTADO

     |HAZ actalc;

     pcm    = 0;
     pe     = 0;
     de     = 0;
     pm     = 0;
     enSwAct = -2;
     |BUCLELIN 0pcmlinea#0;
     enSwAct = 0;
|FCAL;

|CALCULO acttlc;|TIPO 0;
     |SI caltot = 0; ||Si no viene del proceso caltotal
          |PINTA 445,#3#1;
          #3#15 = #0#3;
          |GRABA 020#3a;
     |SINO;
         |SI recalpre = 1;
               |HAZ TotalLin80;
               |GRABA 001#3a;   ||Grabo lineas
         |FINSI;
     |FINSI;

     ||------------Actualiza Cabecera
     |HAZ CalCabAgifa077;
|FCAL;

|DEFBUCLE agifa080;
     #3#1;
     ;
     #0#50, #0#0, 001;
     #0#50, #0#0, 999;
     ;
     NULL, acttlc;
|FIN;

|CALCULO actalc;|TIPO 0;
     |HAZ LimCabAgifa077;
     |BUCLE agifa080;
|FCAL;

|CALCULO siimprec;|TIPO 3;
  |CONFI mensajei;
|SI 0 = FSalida;
  |HAZ imprealc;
|FINSI;
|FCAL;

|CALCULO imprealc;|TIPO 4;
     modo = (FEntrada / 100) ! 0;
     |SI modo = 1; |O modo = 2;
          |GRABA 021#0a;
          |LIBERA #0;
     |FINSI;
     ser_ped = #0#50;
     num_ped = #0#0;
     |CIERRAT #0;

     |SUB_EJECUTA_NP "agifa016";

     |ABRET #0;
     #0#50 = ser_ped;
     #0#0  = num_ped;
     |SI modo = 4;
          |LEE 001#0=;
     |SINO;
          |LEE 101#0=;
     |FINSI;
|FCAL;

|CALCULO abo;|TIPO 0;
con = FEntrada / 100;
con = con ! 0;
|SI con = 2;|Y #0#42 ! Contenido;
   |MENAV mensaje;
   |ERROR;
|FINSI;
|FCAL;

|CALCULO totcomp;|TIPO 14;
|SI #0#41 = 0;
     |PINTA 0577,"AC";
|SINO;
     |PINTA 0577,"PC";
|FINSI;
|FCAL;

|PROCESO pcmlinea;
     ||컴컴컴컴컴컴 Doble/Multi
     enPrecioB  = precio_base;
     enDeciB    = nDeci_PreBase;
     enForma = nForma;
     eaDocu = "agifa077";
     |HAZ StockEntra;
     ||컴컴컴컴컴컴컴컴컴컴컴컴

     ||------------------------------------Prov
     |ABRE #4;
     nImpo = (#3#9 < #0#5) < #0#32;
     |SI #41#114 = "S";
          nImpo = nImpo < #0#7;
     |FINSI;
     #4#0 = #0#3;
     |LEE 101#4=;
     |SI FSalida = 0;
         aMes = #0#1 % A402;
         nMes = aMes + 26;
         |SI #0#42 = "N";
             #4nMes = #4nMes + (nImpo * nForma);
             #4#39 = #4#39 + (nImpo * nForma);
         |SINO;
             #4nMes = #4nMes - (nImpo * nForma);
             #4#39 = #4#39 - (nImpo * nForma);
         |FINSI;
         |GRABA 020#4a;
         |LIBERA #4;
     |FINSI;
     |CIERRA #4;

     |SI #0#42 = "N";
          enImpPrvCom = nImpo * nForma;
     |SINO;
          enImpPrvCom = -nImpo * nForma;
     |FINSI;
     efFecha = #0#1;
     eaProve = #0#3;
     |HAZ AcumulaPrv;
|FPRC;

|PROCESO mira_deci;
     |ABRE #41; |LEE 001#41p; |CIERRA #41;
     n_dec     = #41#8;
     codiBarra = #41#58;
     calpre = #41#60;
     aConfir = #41#76;
     |ABRE #agifa007;
     #agifa007#26 = #agifa077#50;
     |LEE 001#agifa007.=;
     |SI FSalida ! 0; |DDEFECTO #agifa007; |FINSI;
     |CIERRA #agifa007;
     def_alm = #agifa007#31;
     |SI #agifa007#30 = "S";
          #0#12 = 0;
          #0#14 = 0;
          |PINTA #0#12;
          |PINTA #0#14;
     |FINSI;
     |HAZ LeeMonBase77;
     |HAZ LeeParamDiv;
     || Me llevo a agifa088 si puedo o no recoger un albaran de compra con F4
     recoge = #agifa142#36;
|FPRC;

|PROCESO prov_ob; |TIPO 0;
     nModo077 = (FEntrada / 100) ! 0;
     |SI nTecla = 11;  |O #41#102 = "S";
         eaCodigo = #0#3;
         |SUB_EJECUTA_NP "dsz99994";
         |SI #41#102 = "N"; |Y nModo077 ! 2; |ERROR;  |FINSI;
     |FINSI;

     |SI #agifa142#15 = "S";
          puente = #agifa112#26;     |QBF puente;
          |SI puente ! "";
               |PUSHV 1008 1370;
               |SI #agifa142#21 = "S"; |AVISO; |FINSI;
               |BLANCO 1008 1370;
               |CUADRO 1008 1370;
               |ATRI R;
               |PINTA 1110 , #agifa112#26;
               |ATRI 0;
               |PAUSA;
               |POPV;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO SuAlbOk; || chequea #0#54 con 5 caractes numericos
     nOk = 1;
     aAlfa = #0#54; |QBF aAlfa;
     aAlfa = aAlfa % A0;
     |SI aAlfa = 5;
          aAlfa = #0#54;
          |PARA i = 1; |SI i [ 5; |HACIENDO i = i + 1;
               nPos = i * 100 + 1;
               aCar = aAlfa % nPos;
               aCar = aCar - "0123456789";
               |SI FSalida < 0; |SAL; |FINSI;
          |SIGUE;
          |SI i = 6; nOk = 0; |FINSI;
     |FINSI;
     FSalida = nOk;
|FPRC;

|PROCESO MiraPasadoVenta;
     xmes = 0 +. 1;
     |HAZ SuAlbOk;
     |SI FSalida = 0;
          |DBASE base; |QBF base;
          alfa = base + "fich/" + ((#0#54) % 102);
          |MKDIR alfa;
          alfa  = alfa + "/";
          |PATH_DAT #50 alfa;
          |ABRE #50;
          |LEE 040#50p;
          |SI FSalida ! 0;
              |CIERRA #50;
              |PATH_DAT #41 alfa;
              |ABRE #41;
              |LEE 040#41p;
              |SI FSalida ! 0;
                  |CIERRA #41;

                  |DELINDEX #41; |DELINDEX #50; |RMDIR alfa;

                  |DFICE alfa; |QBF alfa;
                  |PATH_DAT #50 alfa;
                  |PATH_DAT #41 alfa;

                  |ACABA;
              |FINSI;
              |CIERRA #41;
          |FINSI;

          |C_M #0#54, 1, "S";
          #50#52 = #0#54 % 302;
          #50#0 = #0#54 % 506;
          |LEE 040#50=;
          |SI FSalida = 0;
               |SI xmes = -1;
                    |AVISO;
                    mensa = "2400NAlbaran pasado de Ventas. Continuar (S/N).: ";
                    |CONFI mensa;
                    |SI FSalida = 0;
                         mensa = "";
                    |FINSI;
               |FINSI;
               |C_M #0#54, 1, "N";
          |FINSI;
          |CIERRA #50;

          |DFICE alfa; |QBF alfa;
          |PATH_DAT #50 alfa;
          |PATH_DAT #41 alfa;
     |FINSI;
|FPRC;

|PROCESO LeeMonBase77; ||Lee los parametros de la moneda base
|ABRE #agifa321;
|ABRE #agifa324;
|LEE 010#agifa321.p;   ||Leo parametros de divisa
#agifa077#61 = #agifa321#9;  ||Cojo la Moneda Base de parametros
#agifa324#0 = #agifa321#9;
|LEE 010#agifa324.=;         ||Leo la ficha de la moneda base en Divisas
#agifa077#62 = #agifa324#3;  ||Asigno el cambio de la moneda base con el euro
decim_base = #agifa324#7;    ||Asigno los decimales de la moneda base
precio_base = #agifa324#9;   ||Asigno los decimales del precio en la mon. base
|CIERRA #agifa324;
|CIERRA #agifa321;
|FPRC;

|PROCESO Param_Divisas; |TIPO 0;
     |ABRE #agifa324;
     pintado = 0;  ||Controla pintados de las lineas
     #agifa324#0 = #agifa077#56;
     |LEE 001#agifa324.=;   || Leo la divisa
     decim_divisa = #agifa324#7;  || Decimales de la divisa
     precio_divisa = #agifa324#9;  || Decimales en el precio de la divisa
     nDeci_Div = decim_divisa;   || Decimales de la divisa
     nDeci_PreDiv = precio_divisa;  || Decimales en el precio de la divisa
     |SI #0#58 = "B";   ||Si trabajo con la moneda base ...
       nDeci_PreVis   = precio_divisa;
       nDeci_ImpVis   = decim_divisa;
       nDeci_Base     = decim_base;
       nDeci_PreBase  = precio_base;
       nDeci_BaseMx  = decim_base;
       nDeci_PreBaseMx = precio_base;
       nDeci_TotVis   = decim_base;
       nDeci_TotNoVis = decim_divisa;
     |SINO;     ||Si trabajo con la divisa ...
       nDeci_PreVis   = precio_base;
       nDeci_ImpVis   = decim_base;
       nDeci_Base     = decim_base;
       nDeci_PreBase  = precio_base;
       |SI #0#57 = 1;
            nDeci_BaseMx    = decim_base;    || sin triangulacion
            nDeci_PreBaseMx = precio_base;
       |SINO;
            nDeci_BaseMx    = 5;             || con triangulacion
            nDeci_PreBaseMx = 5;
       |FINSI;
       nDeci_TotVis     = decim_divisa;
       nDeci_TotNoVis   = decim_base;
     |FINSI;
     |CIERRA #agifa324;
     |HAZ Deci_IVA;
|FPRC;

|PROCESO LeeParamDiv; |TIPO 0;
     |ABRE #agifa321;
     |LEE 001#agifa321.p;    || Leo parametros de divisas
     |ABRE #agifa007;
     #agifa007#26 = #agifa077#50;
     |LEE 001#agifa007.=;        || Leo la serie
     |SI #0#58 = "D";      || Si la mon. de trabajo es la divisa...
       || ...escondo los campos de la moneda base :
       |CAMPO_POSICION #3#6 , 1 , 1009;
       |CAMPO_POSICION #3#9 , 1 ,1026;
       |CAMPO_VISUAL #3#6 , 1 , "N";
       |CAMPO_VISUAL #3#9 , 1 , "N";
       |CAMPO_LINEAL #3#6 , 1 , "N";
       |CAMPO_LINEAL #3#9 , 1 , "N";
       |CAMPO_MODIFICA #3#6 , 1 , "N";
       || ...y pongo en pantalla los de divisa
       |CAMPO_POSICION #3#30 , 1 , 1544;
       |CAMPO_POSICION #3#31 , 1 ,1568;
       |CAMPO_VISUAL #3#30 , 1 , "S";
       |CAMPO_VISUAL #3#31 , 1 , "S";
       |CAMPO_MODIFICA #3#30 , 1 , "S";
       |CAMPO_LINEAL #3#30 , 1 , "S";
       |CAMPO_LINEAL #3#31 , 1 , "S";
       |SI #agifa007#39 = "S"; |Y #agifa321#2 = "S"; ||Si la serie es de ...
          ac_divisa = "S";  || ... divisas, y en parametros estan activadas ...
          || ... pongo como modificables los campos relacionados con divisas
          |CAMPO_MODIFICA #0#56 , 1 , "S";
          |CAMPO_MODIFICA #0#57 , 1 , "S";
          |CAMPO_MODIFICA #0#58 , 1 , "S";
          |CAMPO_MODIFICA #0#59 , 1 , "S";
          |CAMPO_MODIFICA #0#60 , 1 , "S";
          |SI #agifa321#4 = "S";  || Si la visualizacion de parametros esta activada
            || ... pongo los campos visual. de lineas como visualizables
            |CAMPO_VISUAL #3#32, 1 , "S";
            |CAMPO_VISUAL #3#33 , 1 , "S";
          |SINO;
            || ... los pongo como no visualizables
            |CAMPO_VISUAL #3#32 , 1 , "N";
            |CAMPO_VISUAL #3#33 , 1 , "N";
          |FINSI;
       |SINO; || Si la serie no es de divisas, o no estan activados parametros
          ac_divisa = "N";
          #0#58 = "B";  || La moneda de trabajo es por fuerza la moneda base
          |PINTA #0#58;
          || Pongo como no modificables los campos relacionados con divisas
          |CAMPO_MODIFICA #0#58 , 1 , "N";
          |CAMPO_MODIFICA #0#59 , 1 , "N";
          |CAMPO_MODIFICA #0#60 , 1 , "N";
          |CAMPO_MODIFICA #0#56 , 1 , "N";
          |CAMPO_MODIFICA #0#57 , 1 , "N";
          || Pongo como no visualizables los campos visual. de lineas
          |CAMPO_VISUAL #3#32, 1 , "N";
          |CAMPO_VISUAL #3#33, 1 , "N";
       |FINSI;
     |SINO;  || Si la moneda de trabajo es la moneda base
       || ...escondo los campos en divisa :
       |CAMPO_POSICION #3#30 , 1 , 1009;
       |CAMPO_POSICION #3#31 , 1 ,1026;
       |CAMPO_VISUAL #3#30 , 1 , "N";
       |CAMPO_VISUAL #3#31 , 1 , "N";
       |CAMPO_MODIFICA #3#30 , 1 , "N";
       |CAMPO_LINEAL #3#30 , 1 , "N";
       |CAMPO_LINEAL #3#31 , 1 , "N";
       || ...coloco los campos en moneda base:
       |CAMPO_POSICION #3#6 , 1 , 1544;
       |CAMPO_POSICION #3#9 , 1 ,1568;
       |CAMPO_VISUAL #3#6 , 1 , "S";
       |CAMPO_VISUAL #3#9 , 1 , "S";
       |CAMPO_MODIFICA #3#6 , 1 , "S";
       |CAMPO_LINEAL #3#6 , 1 , "S";
       |CAMPO_LINEAL #3#9 , 1 , "S";
       |SI #agifa007#39 = "S"; |Y #agifa321#2 = "S"; || Si la serie es de divisas...
          ac_divisa = "S"; || ... y divisas estan activadas en parametros
          || Pongo como modificables los campos relacionados con divisas
          |CAMPO_MODIFICA #0#56 , 1 , "S";
          |CAMPO_MODIFICA #0#57 , 1 , "S";
          |CAMPO_MODIFICA #0#58 , 1 , "S";
          |CAMPO_MODIFICA #0#59 , 1 , "S";
          |CAMPO_MODIFICA #0#60 , 1 , "S";
          |SI #agifa321#4 = "S";  || Si la visualizacion esta activada en parametros ...
            || Pongo como visualizables los campos visual. de las lineas
            |CAMPO_VISUAL #3#32, 1 , "S";
            |CAMPO_VISUAL #3#33 , 1 , "S";
          |SINO; || Los pongo como no visualizables
            |CAMPO_VISUAL #3#32 , 1 , "N";
            |CAMPO_VISUAL #3#33 , 1 , "N";
          |FINSI;
       |SINO;  || Si la serie no es de divisas o no estan activados los parametros
          ac_divisa = "N";
          #0#58 = "B";  || La moneda de trabajo es por fuerza la moneda base
          || Pongo como no modificables los campos relacionados con divisas
          |CAMPO_MODIFICA #0#58 , 1 , "N";
          |CAMPO_MODIFICA #0#59 , 1 , "N";
          |CAMPO_MODIFICA #0#60 , 1 , "N";
          |CAMPO_MODIFICA #0#56 , 1 , "N";
          |CAMPO_MODIFICA #0#57 , 1 , "N";
          || Pongo como no visualizables los campos visual. de lineas
          |CAMPO_VISUAL #3#32, 1 , "N";
          |CAMPO_VISUAL #3#33, 1 , "N";
       |FINSI;
     |FINSI;
     |CIERRA #agifa007;
|CIERRA #agifa321;
|HAZ Param_Divisas;
|FPRC;

|PROCESO CambioDivisa; |TIPO 0;
nModo077 = (FEntrada / 100) ! 0;
|SI #0Campo ! Contenido; |Y nModo077 = 1;   ||Si cambia el valor o es alta
  |SI Campo = 3; || Si llamo al proceso desde el cod. de proveedor ...
    |LEE 001#agifa112.=;
    #0#56 = #agifa112#81;  || Asigno la divisa por defecto del proveedor
    #0#58 = #agifa112#82; || Asigno la moneda de trabajo del proveedor
    |PINTA #0#58;
  |FINSI;
  #agifa007#26 = #agifa077#50;
  |LEE 001#agifa007.=;  || Leo la serie
   |SI #agifa007#39 = "S"; |Y #agifa321#2 = "S";
      |SI #agifa112#80 = "S"; || Si el proveedor es de divisa pactada
         |ABRE #agifa323;
         |ABRE #agifa322;
         #agifa322#0 = #agifa077#3;
         #agifa322#2 = #agifa077#56;
         #agifa322#7 = "N";
         |SELECT #2#agifa322;  || Con esta clave ...
         |LEE 011#agifa322.=;  || ... busco periodos no finalizados de esa divisa
         |SI FSalida = 0;  || Si existe un periodo no finalizado
           |PARA div_act = 0; |SI div_act = 0; |HACIENDO;
             #agifa077#56 = #agifa322#2;        || Cargo la divisa ...
             #agifa077#57 = #agifa322#3;        || ...y el cambio
             |LEE 001#agifa322.=;   || Leo las lineas de Proveed/Divisas
             nfecha = #agifa077#1 - #agifa322#6;  || Resto la fecha de fin de periodo a la actual
             |SI nfecha > 0;  || Si la fecha actual supera a la del periodo asignado ...
                |MENAV "0000ATENCION deberia Actualizar primero la divisa en Proveedores/Divisas";
                prov_divisa = #agifa077#3;  || Le envio el cod. de proveedor a "agifa323"
                |SUB_EJECUTA_NP "agifa323.tab";  || Ejecuto Proveedores/Divisas
                |ERROR;
             |SINO;
                div_act = 1;  || Para salir del PARA
             |FINSI;
           |SIGUE;
         |SINO;  || Si no existe un periodo no finalizado para esa divisa
           |MENAV "0000No existe un periodo activo para la divisa en Proveedores / Divisas";
           prov_divisa = #agifa077#3;  || Le paso a "agifa323" el proveedor
           |SUB_EJECUTA_NP "agifa323.tab";  || Ejecuto Proveedores / Divisas
           |ERROR;
           ||#agifa077#56 = #agifa077#61;
           ||#agifa077#57 = #agifa077#62;
         |FINSI;
         |SELECT #1#agifa322;
         |CIERRA #agifa322;
         |CIERRA #agifa323;
      |SINO; || Si el Proveedor no es de Divisa pactada ...
         |ABRE #agifa324; || ...miro directamente en divisas
         #agifa324#0 = #agifa077#56;
         |LEE 001#agifa324.=;
         |SI FSalida = 0; ||Si existe la divisa...
            #agifa077#57 = #agifa324#3; || la recojo
            |SI #agifa324#6 ! -1; || Si los dias de margen de actualizacion no
                                  || son = -1 (indefinidos)
              |PARA div_act = 0; |SI div_act = 0; |HACIENDO;
                 #agifa077#56 = #agifa324#0;        ||Cargo el valor de la divisa
                 #agifa077#57 = #agifa324#3;        ||Cargo el valor del cambio
                 |LEE 001#agifa324.=;  || Leo la divisa
                 nfecha = #agifa077#1 - #agifa324#4;
                 |SI nfecha > #agifa324#6; |Y #agifa324#6 ! -1;  || Si la fecha actual
                    |MENAV "0000ATENCION deberia Actualizar primero la divisa";
                    divisa = #agifa077#56;
                    |SUB_EJECUTA_NP "agifa326.tab";
                    |ERROR;
                 |SINO;
                    div_act = 1;
                 |FINSI;
              |SIGUE;
            |FINSI;
         |FINSI;
         |CIERRA #agifa324;
      |FINSI;
   |SINO;
     #agifa077#56 = #agifa077#61;
     #agifa077#57 = #agifa077#62;
   |FINSI;
   |PINTA #agifa077#56;
   |PINTA #agifa077#57;
 |SI Campo = 3; || Si llamo al proceso desde el cod. de proveedor ...
  |HAZ LeeParamDiv;
 |SINO;
  |HAZ Param_Divisas;
 |FINSI;
|FINSI;
|FPRC;

|PROCESO convportes; |TIPO 6;  || Convierte los portes a la mon. base
|SI #0#58 = "B";  || Si la moneda de trabajo es la moneda base ...
  #0#11 = #0#69;  || ... basta con asignarlo
|SINO;    || Si la moneda de trabajo es la divisa
  #0#11 = #0#69 / #0#57 * #0#62;   || ... lo convierto
|FINSI;
|HAZ dcon;
|FPRC;

|PROCESO convvarios; |TIPO 6;  || Convierte los varios a la mon. base
|SI #0#58 = "B";  || Si la moneda de trabajo es la moneda base ...
  #0#13 = #0#70;  || ... basta con asignarlo
|SINO;    || Si la moneda de trabajo es la divisa ...
  #0#13 = #0#70 / #0#57 * #0#62;  || ... lo convierto
|FINSI;
|HAZ dcon;
|FPRC;


|PROCESO LeeAlb; |TIPO 13;  || Sirve para cargar los valores cada vez que
                            || cambio de ficha
pintado = 0;   || controla pintado de las lineas
|HAZ LeeMonBase77;
|HAZ LeeParamDiv;
|FPRC;

|CALCULO cambfecha;
|SI #0#59 ! "O";
  #0#60 = "        ";
  |PINTA #0#60;
  |C_M #0#60,1,"N";
  |C_V #0#60,1,"N";
|SINO;
  |C_M #0#60,1,"S";
  |C_V #0#60,1,"S";
|FINSI;
|FCAL;

|CALCULO modomod; |TIPO 6;
  |SI nModo077 = 2; |Y #0Campo ! Contenido;
    |MENAV "0000No se puede modificar el campo.";
    #0Campo = Contenido;
    |PINTA #0Campo;
    |ERROR;
  |FINSI;
  |SI #0Campo ! Contenido; |Y Campo = 58;
    |HAZ LeeParamDiv;
  |FINSI;
|FCAL;

|PROCESO caltotal; |TIPO 0; ||Proceso especial para casos determinados como
                            ||modificar los campos de cabecera en dtos, cambio divisas etc
                            ||Recalcula total albaran, descuentos y ivas de lineas
                            ||En el caso de cambiar el cambio de divisas recalcula tambien precio... de lineas

     |SI #0Campo ! Contenido; |O nSwPorVar ! 0;||Solo si cambia
          nSwPorVar = 0;
          caltot = 1;
          |SI Campo = 57; ||Si viene del cambio divisa
              recalpre = 1; ||Recalcular tambien precios linea
          |FINSI;
          |HAZ convportes;
          |HAZ convvarios;
          |HAZ actalc;

          |PINTA #0#66;
          caltot = 0;
          recalpre = 0;
     |FINSI;
|FPRC;

|PROCESO DatosEmpresa;   |TIPO 7;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo ! 1; |ACABA; |FINSI;
     |DFICE aDirEmp;            || tomo el directorio de la empresa
     |DFICB aDatos;             || tomo el directorio de los datos
     aDirEmp = aDirEmp % -202;  || me quedo con los 2 digitos de la empresa
     nCodEmp = aDirEmp;         || y los paso a una variable numerica
     |PATH_DAT #11 aDatos;      || pongo el path al fichero agifa041
     |ABRE #11;                 || abre el fichero agifa041
     #11#0 = nCodEmp;
     |LEE 020#11=;              || y busco la empresa en la que estoy
     |SI FSalida = 0;
          #0#29 = #11#1;        || nombre de la empresa
          #0#30 = #11#2;        || direccion de la empresa
          #0#31 = #11#3;        || poblacion de la empresa
          #0#33 = #11#4;        || provincia de la empresa
          |PINTA #0#29;
          |PINTA #0#30;
          |PINTA #0#31;
          |PINTA #0#33;
     |FINSI;
     |CIERRA #11;               || cierro el fichero agifa041
|FPRC;

|PROCESO SuAlbaran; |TIPO 0;
     aAlfa = #0#54; |QBF aAlfa;
     |SI aAlfa = ""; |ACABA; |FINSI;

     aAlfa = #0#54;
     aSerie = #0#50;
     aProv = #0#3;
     nNume = #0#0;
     mensa = "";
     |SALVA_FICHA #0;
     |SELECT #4#0;
     |LEE 000#0];
     |PARA; |SI FSalida = 0; |Y #0#3 = aProv; |Y #0#54 = aAlfa; |HACIENDO;
          |SI #0#50 ! aSerie; |O nNume ! #0#0;
               aAlfa = ("000000" + #0#0) % -106;
               mensa = "0000Codigo existente en Albaran:" + #0#50 + "/" + aAlfa;
               |SAL;
          |FINSI;
          |LEE 000#0s;
     |SIGUE;
     |SELECT #1#0;
     |REPON_FICHA #0;
     |SI mensa ! "";
          |MENAV mensa;
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO sacaser;  |TIPO 7;
     |ABRE #33;
     #33#0 = 1;
     |LEE 101#33=;
     |SI FSalida = 0;
          #0#50 = #33#2;
          |PINTA #0#50;
     |FINSI;
     |CIERRA #33;
|FPRC;

|PROCESO IVA011_66; |TIPO 66;
     enTiva = #0Campo;
     efFiva = #0#1;
     |HAZ ConsuIVA;
     |SI eaDiva = "";
          |ERROR;
     |SINO;
          #0Campo = enTiva; |PINTA #0Campo;
     |FINSI;
|FPRC;

|PROCESO IVA011_0; |TIPO 0;
     enTiva = #0Campo;
     efFiva = #0#1;
     |HAZ SacaIVA;
     |SI eaDiva = "";
          |MENAV "0000No existe tipo iva para esta fecha...";
          |ERROR; |ACABA;
     |SINO;
          |HAZ caltotal;
     |FINSI;
|FPRC;
