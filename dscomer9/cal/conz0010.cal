|FICHEROS;
   conz0010 #0;
   con0g021 #1; || Lineas de observaciones
|FIN;

|VARIABLES;
   Puntero = 0;
   aClave  = "";
   Comodin = "";
   aAlfa   = "";
   aAlfa1  = "";
   nCalc   = 0;

   nBaffer = 0;
   nTotalLinea = 0; nTope = 0; nVari1 = 0; nVari2 = 0;

   &enModoEdita  = 0;
   &enTotalVaria = 0;
   &enCampoAncho = 0;
   &eaNombreList  = "";
   &enCuadroIni   = 0;
   &enCuadroFin   = 0;

{100}epPuntEdita   = "";
|FIN;

|PROCESO GrabaSecuencial;
     IepPuntEdita  = epPuntEdita1 <-;
     |PARA nTotalLinea = 0;  |SI nTotalLinea [ enTotalVaria;  |HACIENDO nTotalLinea = nTotalLinea + 1;
          |AL_BUF nBaffer, nTotalLinea, epPuntEdita;
          IepPuntEdita = IepPuntEdita + 1;
     |SIGUE;
|FPRC;

|PROCESO Edicion;
     nTope  = nTotalLinea - 1;
     nVari1 = 0;
     nVari2 = 0;
     enModoEdita = enModoEdita + 100;
     |EDITA nBaffer, 1, nTope, enCuadroIni, enCuadroFin, enModoEdita, eaNombreList, enCampoAncho, nVari1, nVari2;
     enModoEdita = enModoEdita $ 100;
|FPRC;

|PROCESO CogePuntero;
     IepPuntEdita  = epPuntEdita1 <-;
     |PARA nTotalLinea = 0;  |SI nTotalLinea [ enTotalVaria;  |HACIENDO nTotalLinea = nTotalLinea + 1;
          |DEL_BUF nBaffer, nTotalLinea, epPuntEdita;
          IepPuntEdita = IepPuntEdita + 1;
     |SIGUE;
|FPRC;

|PROCESO Edita;
     |PUSHV 0501 2380;
     |DIMENSIONA  200, nBaffer, 0;

     |HAZ GrabaSecuencial;
     |HAZ Edicion;
     |HAZ CogePuntero;

     |FINDIM nBaffer;
     |POPV;
|FPRC;

|PROGRAMA;
   aClave = PARAMETRO; |QBF aClave;

   enTotalVaria = 100;
   enModoEdita  = 0;
   enCampoAncho = 60;
   eaNombreList  = "";
   enCuadroIni   = 1002;
   enCuadroFin   = 1679;

   |ABRE #con0g021;
   |PARA Puntero = 1; |SI Puntero [ 100; |HACIENDO Puntero = Puntero + 1;
      IepPuntEdita  = epPuntEdita1 <-;
      IepPuntEdita = IepPuntEdita + (Puntero - 1);

      #con0g021#0 = aClave;
      #con0g021#1 = Puntero;
      |LEE 101#con0g021.=;
      |SI FSalida ! 0;
         epPuntEdita  = "";
      |SINO;
         epPuntEdita  = #con0g021#2;
      |FINSI;
   |SIGUE;
   |CIERRA #con0g021;

   |PUSHV  0501 2380;
   |BLANCO 0901 1679;
   aAlfa = " OBSERVACIONES                                                                ";
   |ATRI R;  |PINTA 0902, aAlfa; |ATRI 0;

   |HAZ Edita;
   |POPV;

   |ABRE #con0g021;
   |PARA Puntero = 1;  |SI Puntero [ 100;  |HACIENDO Puntero = Puntero + 1;
      IepPuntEdita  = epPuntEdita1 <-;
      IepPuntEdita = IepPuntEdita + (Puntero - 1);

      |DDEFECTO #con0g021;
      #con0g021#0 = aClave;
      #con0g021#1 = Puntero;
      |LEE 101#con0g021.=;
      |SI FSalida ! 0;
         Comodin = epPuntEdita; |QBF Comodin;
         |SI Comodin ! "";
            #con0g021#2 = epPuntEdita;
            |GRABA 020#con0g021.n;
         |FINSI;
      |SINO;
         Comodin = epPuntEdita; |QBF Comodin;
         |SI Comodin ! "";
            #con0g021#2 = epPuntEdita;
            |GRABA 020#con0g021.a;
         |SINO;
            |BORRA 020#con0g021.a;
         |FINSI;
         |LIBERA #con0g021;
      |FINSI;
   |SIGUE;
   |CIERRA #con0g021;
|FPRO;
