|FICHEROS;
     diaz0016 #0;
     diam0100 #100;
     diaw0015 #915,MANTE;
|FIN;

|VARIABLES;
     nPanta         = 0;
     nPanta915      = 0;
     nBoton1        = 0;
     nBoton2        = 0;
     nBoton3        = 0;
     nVentana       = 0;
     nError         = 0;
     nHoja          = 0;
     nNumHoja       = 0;
     nLinea         = 0;
     nHandle        = 0;
     nLong          = 0;
     nPosi          = 0;
     nCampo         = 0;

     aEsc1          = "";
     aEsc2          = "";
     aRetu          = "";
     aAlfa          = "";
     aAlfa1         = "";
     aFichero       = "";
     aFicheroP      = "";
     aFicheroT      = "";
     aFicheroTif    = "";
     aTecla         = "";
     aTipoScaneo    = "";
     aTipoScaneoT   = "";
     aPathRemoto    = "";
     aOrigen        = "";
     aDestino       = "";
     aDirCompras    = "";
     aIPRemoto      = "";
     aBase          = "";
     aCaracter      = "";
     aLong          = "";

 {-1}aMenu = "";
     aMenu1 = "2240";
     aMenu2 = "1";
     aMenu3 = "3";
     aMenu4 = " Modificar ";
     aMenu5 = " Aceptar ";
     aMenu6 = " Cancelar ";
|FIN;

|PROCESO Tipo12w15;  |TIPO 12;
     |XABRE "EC", aFichero, nHandle;
     |SI FSalida < 0;  aMenu2 = "3";  |ACABA;  |FINSI;

     aMenu2 = "1";
     |MENUG aMenu;
     aMenu2 = FSalida;
|FPRC;

|PROCESO TipoBandeja;
     |C_M #915#10, 1, "S";
     |SI #915#8 = "P";
         |C_M #915#10, 1, "N";  #915#10 = "N";  |PINTA #915#10;
     |FINSI;
|FPRC;

|PROCESO EscaneoPlana;
     |RFBORRA aFichero;

                        aTipoScaneo = "[SCANUI]";
     |SI #915#2 = "N";  aTipoScaneo = "[SCAN]";   |FINSI;

     nError = 0;
     |CARGADLL "dstwain.dll";
     |SI FSalida < 0;
         nError = 1;
         |MENAV "     No encuentro el driver : dstwain.dll";
         |ACABA;
     |FINSI;

     |PARA nHoja = 1;  |SI nHoja [ #915#1;  |HACIENDO nHoja = nHoja + 1;
           aTipoScaneoT = aTipoScaneo;

           |ALFACALLDLL "dstwain.dll","DsTwainEntry", aTipoScaneoT;
           |SI FSalida < 0;
               nError = 1;
               |MENAV "     No encuentro el SCANNER";
               |SAL;
           |FINSI;

           |SI aTipoScaneoT = "OK";
               aAlfa = "[SAVE:" + aFicheroP; |QBF aAlfa;
               aAlfa = aAlfa + "]";
               |ALFACALLDLL "dstwain.dll", "DsTwainEntry", aAlfa;
           |FINSI;

           |SI nHoja ! #915#1;
               nNumHoja = nHoja + 1;
               aAlfa = "0000 Introduzca la hoja numero: " + nNumHoja + " en el Escaner y pulse tecla para continuar";
               |MENAV aAlfa;
           |FINSI;
     |SIGUE;
     |DESCARGADLL "dstwain.dll";
|FPRC;

|PROCESO EscaneoTrasera;
     |RFBORRA aFichero;

                        aTipoScaneo = "[SCANUI]";
     |SI #915#2 = "N";  aTipoScaneo = "[SCAN]";   |FINSI;

     nError = 0;
     |CARGADLL "dstwain.dll";
     |SI FSalida < 0;
         nError = 1;
         |MENAV "     No encuentro el driver : dstwain.dll";
         |ACABA;
     |FINSI;

     aTipoScaneoT = aTipoScaneo;

     aAlfa = "[AUTOSAVE:" + aFicheroT; |QBF aAlfa;
     aAlfa = aAlfa + "]";
     |ALFACALLDLL "dstwain.dll","DsTwainEntry", aAlfa;

     |ALFACALLDLL "dstwain.dll","DsTwainEntry", aTipoScaneoT;
     |SI FSalida < 0;
         nError = 1;
         |MENAV "     No encuentro el SCANNER";
         |ACABA;
     |FINSI;

     |DESCARGADLL "dstwain.dll";
|FPRC;

|PROCESO Escaneo;
     |SI #915#8 = "P";  |HAZ EscaneoPlana;    |FINSI;
     |SI #915#8 = "T";  |HAZ EscaneoTrasera;  |FINSI;

     |PULSATECLA;
|FPRC;

|PROCESO GrabaFactura;
     |ABRE #100;
     #100#0 = #915#3;
     #100#1 = #915#4;
     #100#2 = 99;
     |LEE 000#100];
     |SI FSalida = 0;
         |LEE 000#100a;
     |SINO;
         |LEE 000#100u;
     |FINSI;
     nLinea = 1;
     |SI FSalida = 0;  |Y #100#0 = #915#3;  |Y #100#1 = #915#4;
         nLinea = #100#2 + 1;
     |FINSI;

     |DDEFECTO #100;
     #100#0  = #915#3;
     #100#1  = #915#4;
     #100#2  = nLinea;
     #100#3  = #915#5;
     #100#13 = #915#6;

     aAlfa   = #100#1;  |QBT aAlfa;
     aAlfa1  = "";
     aLong   = aAlfa % 0;
     nLong   = aLong;
     |PARA nCampo = 1;  |SI nCampo [ nLong;  |HACIENDO nCampo = nCampo + 1;
           nPosi     = (100 * nCampo) + 1;
           aCaracter = aAlfa % nPosi;
           |SI aCaracter = "/";  |O aCaracter = "\";
               aCaracter = "_";
           |FINSI;
           aAlfa1 = aAlfa1 + aCaracter;
     |SIGUE;

     aFicheroTif = #100#0 + aAlfa1 + (("00" + #100#2) % -102) + ".tif";
     |QBT aFicheroTif;
     #100#10     = aFicheroTif;
     |GRABA 020#100n;
     |LIBERA #100;
     |CIERRA #100;

     aOrigen     = aFichero;
     aDestino    = aDirCompras + aFicheroTif;
     aIPRemoto   = "";
     |IP_REMOTO aIPRemoto;
     |SI aIPRemoto ! "";
         |RECIBE_FICHERO aOrigen, aDestino;
     |SINO;
         |COPIA_FICHERO aOrigen, aDestino;
     |FINSI;
|FPRC;

|PROCESO Boton2;
     |SI #915#8 = "P";
         aAlfa = "0000 Introduzca la hoja numero: 1 en el Escaner y pulse tecla para continuar";
         |MENAV aAlfa;
     |FINSI;
     |HAZ Escaneo;

     |SI nError = 0;
         |C_M #915#3, 1, "S";  |PINTA #915#3;
         |C_M #915#4, 1, "S";  |PINTA #915#4;
         |C_M #915#5, 1, "S";  |PINTA #915#5;
         |C_M #915#6, 1, "S";  |PINTA #915#6;

         |ESTADO_CONTROL nBoton3, "ENABLE";
     |FINSI;
|FPRC;

|PROCESO Boton3;
     aAlfa = "start " + aFichero;
     |RSYSTEM aAlfa;
|FPRC;

|PROCESO Boton1;
     |ESTADO_CONTROL nBoton1, "HIDE";

     |LEE 101#915p;
     |SI FSalida ! 0;
         |DDEFECTO #915;
         |GRABA 020#915b;
     |FINSI;

     |VENTANA 0501, 2480, -1, 1, "";
     nVentana = FSalida;

     #915#1 = 1;
     #915#3 = "";
     #915#4 = "";
     #915#5 = "01.01.0000";
     #915#6 = 0;
     #915#9 = "";

     |C_M #915#3, 1, "N";
     |C_M #915#4, 1, "N";
     |C_M #915#5, 1, "N";
     |C_M #915#6, 1, "N";

     |PINPA #0#915;
     nPanta915 = FSalida;

     |PINDA #0#915;

     |CONTROL_SIMPLE nPanta915, "BOTON,{{218}} ESCANEAR FACTURA ", 1245, 1276, Boton2;
     nBoton2 = FSalida;

     |CONTROL_SIMPLE nPanta915, "BOTON,{{219}} VER DOCUMENTO ESCANEADO", 1345, 1376, Boton3;
     nBoton3 = FSalida;

     |ESTADO_CONTROL nBoton3, "DISABLE";

     |PARA;  |SI;  |HACIENDO;
          |ENDATOS #1#915;
          |SI aMenu2 ! "1";  |SAL;  |FINSI;
     |SIGUE;

     |SI aMenu2 = "2";
         |HAZ GrabaFactura;
     |FINSI;

     |GRABA 020#915a;
     |LIBERA #915;
     |FINVENTANA nVentana;
     |ESTADO_CONTROL nBoton1, "SHOW";

     |RFBORRA aFichero;

     |PULSATECLA;
|FPRC;

|PROCESO Tipo80;  |TIPO 80;
     FSalida = 2399;
|FPRC;

|PROGRAMA;
     |CLS;
     |CABEZA "Escanear Facturas";
     |PINPA #0#0;
     nPanta = FSalida;

     aPathRemoto = "C:/DOCUMENTOS";              |RMKDIR aPathRemoto;
     aPathRemoto = "C:/DOCUMENTOS/ESCANER";      |RMKDIR aPathRemoto;
     aFichero    = aPathRemoto + "/imagen.tif"
     aFicheroP   = aPathRemoto + "/imagen.tif"
     aFicheroT   = aPathRemoto + "/imagen.1tif"

     |DBASE aBase;  |QBF aBase;
     aDirCompras   = aBase + "000577/pdfcompras";  |MKDIR aDirCompras;
     aDirCompras   = aDirCompras + "/" + (("00000" + #48#0) % -105); |MKDIR aDirCompras;
     aDirCompras   = aDirCompras + "/";

     |ABRE #915;
     |CONTROL_SIMPLE nPanta, "BOTON,{{217}} NUEVA FACTURA ", 1127, 1553, Boton1;
     nBoton1 = FSalida;

     aEsc1  = &255 + "806";
     aEsc2  = &255 + "807";
     aRetu  = &255 + "802";
     |PARA; |SI;  |HACIENDO;
         FSalida = "::Salir";
         |LEETECLA aTecla;
         |SI aTecla = aEsc1; |SAL;  |FINSI;
         |SI aTecla = aEsc2; |SAL;  |FINSI;
         |SI aTecla = aRetu; |SAL;  |FINSI;
     |SIGUE;
     aTecla = "";
     |CIERRA #915;

     |RFBORRA aFichero;

     |FIN_CONTROL_WINDOWS nBoton1;
     |PULSATECLA;
|FPRO;
