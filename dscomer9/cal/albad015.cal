|FICHEROS;
     albad015 #0;
     agifa010 #10;
     agifa019 #19;
     agifa071 #71;
     agifa024 #24;
     agifa134 #134;
|FIN;

|VARIABLES;
     &enTiva = 0;
     &efFiva = @;
     &eaDiva = "";
     &enIva = 0;
     &enRec = 0;
     aTipo     = "";
     aAlf1     = "";
     fFecha    = @;
     nPos      = 0;
     nLon      = 0;
     nNume     = 0;
     nAbo      = 0;
     nNu1      = 0;
     nNu2      = 0;
     nDec      = 0;

     aAlfa = "";
     nHandle = 0;
     nConta = 0;
     aFin = "";
     aFic = "";
     aPath = "";
     nTipo = 0;
     fFecFactu = @;
     nTipoIVa = 0;
|FIN;

|PROCESO Relleno;
     |SI aTipo = "F";
          aAlfa = (aAlfa % -104) + (aAlfa % 402)  + (aAlfa % 102);
     |FINSI;

     |SI aTipo = "A";
          aAlf1 = " " * nLon;
          aAlfa = aAlfa + aAlf1;
          nPos = nLon + 100;
          aAlfa = aAlfa % nPos;
     |FINSI;

     |SI aTipo = "N";
          nNume = aAlfa;
          |SI nNume < 0; nAbo = -1; |SINO; nAbo = 1; |FINSI;
          |SI nNume < 0; nNume = nNume * - 1; |FINSI;
          nNu1 = nNume ! 0;
          |SI nNu1 > nNume; nNu1 = nNu1 - 1; |FINSI;
          nNu2 = nNume - nNu1;
          |SI nDec < 0;
               aAlfa = nNume;
          |SINO;
               |SI nDec = 0;
                    aAlfa = nNu1;
               |SINO;
                    aAlfa = nNu2;
                    aAlfa = aAlfa + "00000000000000000000000000";
                    nPos = nDec + 300;
                    aAlfa = aAlfa % nPos;
                    aAlfa = nNu1 + "." + aAlfa;
               |FINSI;
          |FINSI;
          aAlfa = "000000000000000000000000000000000000000000000" + aAlfa;
          |SI nAbo = 1;
               nPos = -1 * (nLon + 100);
               aAlfa = (aAlfa % nPos);
               |SI nDec > 0;
                    aAlfa = aAlfa + "+";
               |FINSI;
          |SINO;
               nPos = -1 * (nLon + 100);
               aAlfa = (aAlfa % nPos);
               |SI nDec > 0;
                    aAlfa = aAlfa + "-";
               |FINSI;
          |FINSI;
     |FINSI;

     |XGRABA nHandle, aAlfa;
/*
     aAlf1 = aAlfa;
     aAlfa = aAlfa % A0;
     aAlfa = "0000Valor:[" + aAlf1 + "] Longitud:" + aAlfa;
     |MENAV aAlfa;
*/
|FPRC;

|PROCESO Articulos;
     |SI #19#6 < 0; |Y #0#5 = "N";
          |ACABA;
     |FINSI;

     enTiva = #19#30;
     efFiva = ~;
     |HAZ SacaIVA;

     |SI enIva = 16; |O enIva = 18; nTipoIVa = 1; |FINSI;
     |SI enIva = 7; |O enIva = 8;  nTipoIVa = 2; |FINSI;
     |SI enIva = 4; nTipoIVa = 3; |FINSI;
     |SI enIva = 0; nTipoIVa = 4; |FINSI;

||Articulo  13
     aAlfa = #19#128;
     aTipo = "A"; nLon = 13; |HAZ Relleno;
||Pvp  13
     aAlfa = #19#18;
     aTipo = "N"; nDec = 2; nLon = 12; |HAZ Relleno;
||Tipo iva  1
     aAlfa = nTipoIVa;
     aTipo = "A"; nLon = 1; |HAZ Relleno;
||Existencia     13
     aAlfa = #19#6;
     aTipo = "N"; nDec = 2; nLon = 12; |HAZ Relleno;

     |XGRABA nHandle, aFin;
|FPRC;

|DEFBUCLE Articulos;
     #19#1;
     125;
     "             ", #0#2;
     "zzzzzzzzzzzzz", #0#2;
     ;
     NULL, Articulos;
|FIN;

|PROCESO LinAlb1;
||Articulo  13
     aAlfa = #19#128;
     aTipo = "A"; nLon = 13; |HAZ Relleno;
||Contador  9
     aAlfa = nConta;
     aTipo = "N"; nDec = 0; nLon = 9; |HAZ Relleno;
||Tipo Mov. 2
     aAlfa = nTipo;
     aTipo = "A"; nLon = 2; |HAZ Relleno;
||Cliente   9   Cuenta Contable
     aAlfa = #24#16;
     aTipo = "A"; nLon = 9; |HAZ Relleno;
||Albaran   9
     aAlfa = #10#0;
     aTipo = "N"; nDec = 0; nLon = 9; |HAZ Relleno;
||Fecha Albaran  8
     aAlfa = #10#1;
     aTipo = "F"; |HAZ Relleno;
||Factura   9
     aAlfa = #10#39;
     aTipo = "N"; nDec = 0; nLon = 9; |HAZ Relleno;
||Fecha Factura  8
     aAlfa = fFecFactu;
     aTipo = "F"; |HAZ Relleno;
     |SI nTipo = 10;
||Cantidad  13
          nNume = #71#5;
          |SI #19#114 ! 0;
               nNume = #71#5 * #19#114;
          |FINSI;
          aAlfa = nNume;
          aTipo = "N"; nDec = 2; nLon = 12; |HAZ Relleno;
||Precio    13
          aAlfa = #71#6;
          aTipo = "N"; nDec = 2; nLon = 12; |HAZ Relleno;
||Bonificacion en pvp 10
          aAlfa = 0;
          aTipo = "N"; nDec = 2; nLon = 9; |HAZ Relleno;
||Descuento %    7
          aAlfa = #71#8;
          aTipo = "N"; nDec = 2; nLon = 6; |HAZ Relleno;
||Importe Dto.   13
          nNume = (#71#5 * #71#6) - #71#9;
          aAlfa = nNume;
          aTipo = "N"; nDec = 2; nLon = 12; |HAZ Relleno;
||Importe   13
          aAlfa = #71#9;
          aTipo = "N"; nDec = 2; nLon = 12; |HAZ Relleno;
||SW   1
          aAlfa = "N";
          aTipo = "A"; nLon = 1; |HAZ Relleno;
     |SINO;
||Cantidad  13
          aAlfa = #71#66;
          aTipo = "N"; nDec = 2; nLon = 12; |HAZ Relleno;
||Precio    13
          aAlfa = 0;
          aTipo = "N"; nDec = 2; nLon = 12; |HAZ Relleno;
||Bonificacion en pvp 10
          aAlfa = 0;
          aTipo = "N"; nDec = 2; nLon = 9; |HAZ Relleno;
||Descuento %    7
          aAlfa = 0;
          aTipo = "N"; nDec = 2; nLon = 6; |HAZ Relleno;
||Importe Dto.   13
          aAlfa = 0;
          aTipo = "N"; nDec = 2; nLon = 12; |HAZ Relleno;
||Importe   13
          aAlfa = 0;
          aTipo = "N"; nDec = 2; nLon = 12; |HAZ Relleno;
||SW   1
          aAlfa = "S";
          aTipo = "A"; nLon = 1; |HAZ Relleno;
     |FINSI;

     |XGRABA nHandle, aFin;
|FPRC;

|PROCESO LinAlba;
     |DDEFECTO #19;
     #19#0 = #71#2;
     |LEE 000#19=;
     |SI #19#125 ! #0#2; |ACABA; |FINSI;

     nTipo = 10;
     nConta = nConta  + 1;
     |HAZ LinAlb1;
     |SI #71#35 ! 0;
          nTipo = 11;
          nConta = nConta  + 1;
          |HAZ LinAlb1;
     |FINSI;
|FPRC;

|PROCESO CabAlb;
     nConta = 0;
     |DDEFECTO #24;
     #24#0 = #10#3;
     |LEE 000#24=;

     #134#49 = #10#53;
     #134#0 = #10#39;
     |LEE 000#134=;
     |SI FSalida = 0;
          fFecFactu = #134#1;
     |SINO;
          fFecFactu = "01.01.0000";
     |FINSI;
|FPRC;

|DEFBUCLE Albaran;
     #10#3;
     ;
     "      ", #0#3, "     ", 000000;
     "zzzzzz", #0#4, "zzzzz", 999999;
     ;
     NULL, CabAlb, LinAlba;
|FIN;

|PROCESO Inicio;
     aFin = &13 + &10;
     aPath = #0#1; |QBF aPath;
     |SI #0#6 = "S";
          aFic = aPath + #0#7; |QBF aFic;
          |XABRE "CWB", aFic, nHandle;
          |SI FSalida < 0;
               aAlfa = "0000No puedo crear [" + aFic + "]";
               |MENAV aAlfa;
          |SINO;
               |ABRE #134;
               |ABRE #19;
               |ABRE #24;
               |BUCLE Albaran;
               |CIERRA #24;
               |CIERRA #19;
               |CIERRA #134;
               |XCIERRA nHandle;
          |FINSI;
     |FINSI;
     |SI #0#8 = "S";
          aFic = aPath + #0#9; |QBF aFic;
          |XABRE "CWB", aFic, nHandle;
          |SI FSalida < 0;
               aAlfa = "0000No puedo crear [" + aFic + "]";
               |MENAV aAlfa;
          |SINO;
               |BUCLE Articulos;
               |XCIERRA nHandle;
          |FINSI;
     |FINSI;
|FPRC;

|PROGRAMA;
     |ABRE #0;
     |LEE 101#0p;
     |SI FSalida ! 0; |GRABA 020#0b; |FINSI;
     |CLS;
     |PINPA #0#0;
     |PINDA #0#0;
     |ENDATOS #1#0;
     |SI FSalida = 0;
          |HAZ Inicio;
          |GRABA 020#0a;
     |FINSI;
     |CIERRA #0;
|FPRO;
