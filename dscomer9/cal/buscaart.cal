|FICHEROS;
     agifa142 #142;      || Parametros generales.
|FIN;

|VARIABLES;
     bcodart   = 0;
     balfa     = "";
     lt        = 0;
     se        = "";
     nu        = "";
     li        = 0;
     i_alfa    = "";
     bbeta     = 0;
     bprov     = "";
     bprov2    = "";
|FIN;


________________________/ RUTINA DE RELLENO PARA EL CODIGO DE ARTICULO, HECHA
________________________/ PARA LA OCA. R.AGUERA 20-02-98
________________________/ RELLENA EL CODIGO HASTA LA LONGITUD MAXIMA, Y BUSCA
________________________/ EL SIGUIENTE NUMERO DE ARTICULO LIBRE.

|PROCESO xrelleart; |TIPO 6;
     balfa = #0Campo;
     |QBF balfa;
     |SI balfa ! "";
          |ABRE #142;
          |LEE 000#142p;
          |SI FSalida = 0; |Y #142#65 = "S";
               bbeta = #142#44 + 100;             || LONG.INICIAL,DIGITOS PROV.
               bprov = (#0Campo % bbeta);         || GUARDA EL COD.PROVEEDOR.
               |LEE 000#0=;
               |SI FSalida ! 0;
                    balfa = bprov + "z";          || MONTA EL COD.ARTICULO
                    #0Campo = balfa;              || PARA QUE BUSQUE EL ULTIMO.
                    |LEE 000#0];
                    |SI FSalida = 0;
                         |LEE 000#0a;
                    |SINO;
                         |LEE 000#0u;
                    |FINSI;
                    bprov2 = (#0Campo % bbeta);
                    |SI FSalida = 0; |Y bprov = bprov2;
                         bbeta = #142#44 + 1;
                         bbeta = bbeta * 100 + #142#62;
                         balfa = #0Campo % bbeta;
                         bcodart = balfa;
                         bcodart = bcodart + 1;
                         balfa = bprov + bcodart;
                         |DDEFECTO #0;
                         #0Campo = balfa;
                         |PINTA #0Campo;
                    |SINO;
                         balfa = bprov + "1";
                         |DDEFECTO #0;
                         #0Campo = balfa;
                    |FINSI;
                    |HAZ llena;
               |FINSI;
          |FINSI;
          |CIERRA #142;
     |FINSI;
|FPRC;
