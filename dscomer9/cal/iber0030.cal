 Crea ficheros:
               ib__arti  -> Articulo
               ib__kits  -> Kits
               ib__ticd  -> Desglose Tiquets
               ib__tick  -> Tiquets
               ib__mesa  -> mesas
               ib__clie  -> Clientes
               ib__apre  -> Fijos
               ib__sala  -> salas
               ib__extr  -> Extra
               ib__rese  -> Reservas

               ib__fami  -> Orden
               ib__desg  -> Articulos Configurables
               ib__comp  -> Familia  Compuestos
               ib__coma  -> Articulos  ''
               ib__cama  -> Camareros
               ib__secc  -> Secciones
               ibtarjet  -> Tarjetas
               ib__tari  -> Tarifas

|FICHEROS;
     iber0030 #0;   || Este Def
     referen@ #2;   || referencia de iber0020
     iber0019 #119; || Prd. Centro
     iber0020 #120; || ''

     agifa024 #24;
     iber0016 #16;
     agifa019 #19;
     iber0056 #56;  || Orden
     iber0031 #31;  ||Config.
     iber0032 #32;  ||Fam.Compuesto
     iber0033 #33;  ||Compuesto
     iber0040 #40;  ||Cama
     iber0065 #65;  ||Seccion
     iber0066 #166;
     iber0067 #167;
     iber0070 #70;  ||Seccion PuntoVta
     iber0074 #74;  ||Salas centro
     iber0078 #78;  ||Reservas
     iber0079 #79;  || ''
     iber0080 #80;  || ''
     iber0085 #85;  || Mensa cocina
     iber0091 #91;
     iber0092 #92;
     ibtarjet #93;

     ib__tick;
     ib__ticd;
     ib__data;
     ib__cama #201;
     ib__mesa #200;
     ib__sala #402;
     ib__apre #203;
     ib__extr #204;
     ib__arti #219;
     ib__clie #224;
     ib__desg #231;
     ib__comp #232;
     ib__coma #233;
     ib__kits #249;
     ib__fami #260;
     ib__rese #278;
     ib__secc #265;
     ib__perm #292;
     ib__tari #300;
     agifa515 #515;
|FIN;

|VARIABLES;
     &enTiva = 0;
     &efFiva = @;
     &eaDiva = "";
     &enIva = 0;
     &enRec = 0;
     &uni_dt = 0;
     &fed_dt = @;   || Fecha....
     &art_dt = "";  || Articulo.
     &cli_dt = "";  || Cliente..
     &fam_dt = "";  || Familia.
     &iva_dt = 0;   || Tipo Iva.
     &dtos_2 = 0;   || Descuento 2.
     &dto_pt = 0;   || Descuento Ptas.
     &dtos_1 = 0;   || Descuento 1.
     &pvp_ve = 0;   || Precio Venta.
     &gru_dt = "";  || Grupo Cliente.
     &tarifa = 0;   || Tarifa Cliente.
     &presel = 0;   || tarifa Seleccion
     &dtosel = 0;   || dto    Seleccion
     &aParam        = "";
     &enMensaje     = 0;
     &enPromoTPV1   = 0;
     &eaIVAsn   = "";|| IVA incluido S/N
     &enPromoTPV2   = 0;
     &enDirEnt = 0;
     aIVAinc = "";
     &enPreTPV = 0;
     &EURODB = 0;

     nSw = 0;
     fFecha    = @;
     fFecRese  = @;

     aNota     = "";
     aDes      = "";
     aOrden    = "";
     aOrden1   = "";
     aTipo     = "";
     aAlfa     = "";
     aNomFic   = "";
     aNom      = "";
     aArti     = "";
     aTipoArt  = "";
     aDArt     = "";
     aHArt     = "";
     aSubFam   = "";
     aCentro   = "";
     aPath     = "";
     aOrg      = "";
     nCentro   = 0;
     nConta    = 0;
     nPutoVta  = 0;
     nPrime    = 0;
     nMesa     = 0;
     nLinea    = 0;
     nSwCompu  = 0;
|FIN;

|PROCESO agifa515;
     #93#0 = #515#1;
     |GRABA 000#93n;
|FPRC;

|DEFBUCLE agifa515;
     #515#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, agifa515;
|FIN;
컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
|PROCESO BuscaPrecio;
     |SI nSw = 0;
          nSw = 1;
          #166#0 = #167#0;
          #166#1 = #167#1;
          #166#2 = #167#2;
          #166#3 = #167#3;
          |LEE 000#166=;

          enPreTPV = #167#9;
          eaIVAsn = #166#7;
     |FINSI;
|FPRC;

|DEFBUCLE BuscaPrecio;
     #167#2;
     ;
     #119#0, #119#1, tarifa, #19#0, " ", fed_dt;
     #119#0, #119#1, tarifa, #19#0, "z", "01.01.0000";
     ;
     NULL, BuscaPrecio;
|FIN;

|PROCESO LeeIVA;
     |SI #iber0016#28 = 0;
          enTiva = #agifa019#30;
     |SINO;
          enTiva = #iber0016#28;
     |FINSI;
     efFiva = ~;
     |HAZ SacaIVA;
|FPRC;

|PROCESO Precio;
     |ABRE #24;
     #24#0 = "000000";
     |LEE 000#24=;
     |CIERRA #24;

     fed_dt = ~;            || Fecha....
     art_dt = #agifa019#0;  || Articulo.
     cli_dt = #agifa024#0;  || Cliente..
     fam_dt = #agifa019#2;  || Familia.
     |SI #iber0016#28 = 0;
          iva_dt = #agifa019#30;
     |SINO;
          iva_dt = #iber0016#28;
     |FINSI;
     iva_dt = #agifa019#30; || Tipo Iva.
     dtos_2 = 0;            || Descuento 2.
     dto_pt = 0;            || Descuento Ptas.
     dtos_1 = 0;            || Descuento 1.
     pvp_ve = 0;            || Precio Venta.
     tarifa = 1;
     gru_dt = #agifa024#158;|| Grupo Cliente.
     presel = 0;            || Precio seleccion.
     dtosel = 0;            || Dto      '' ''
     uni_dt = 1;
     enMensaje = 0;
     aParam = "";
     enDirEnt = 0;
     |HAZ calcdtos;
     |HAZ LeeIVA;
     |ABRE #166;
     nSw = 0;
     |BUCLE BuscaPrecio;
     |CIERRA #166;
     aIVAinc =  #iber0019#11;
     |SI eaIVAsn = "N";
          |SI aIVAinc = "S";
               enPreTPV = enPreTPV > enIva;
          |FINSI;
     |SINO;
          |SI aIVAinc = "N";
               enPreTPV = enPreTPV /(1+(enIva/100));
          |FINSI;
     |FINSI;
     enPreTPV = enPreTPV ! EURODB;

     |DDEFECTO #300;
     #300#0 = #219#1;
     |LEE 101#300=;
     #300#1 = enPreTPV;
     |SI FSalida ! 0; |GRABA 020#300b; |FINSI;

     |GRABA 020#300a;
     |LIBERA #300;
|FPRC;
컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
|PROCESO Existe;
     #2#0 = nCentro;
     #2#1 = nPutoVta;
     #2#2 = aArti;
     |LEE 000#2=;
|FPRC;
컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
|PROCESO BorraRese;
     |SI #278#3 < fFecRese;
          |BORRA 020#278a;
     |FINSI;
|FPRC;

|DEFBUCLE BorraRese;
     #278#1;
     ;
     INICIO;
     FINAL;
     be;
     NULL, BorraRese;
|FIN;
컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
|PROCESO iber0092;
     #292#0 = #92#0;
     #292#1 = #92#1;
     |GRABA 020#292n;
|FPRC;

|DEFBUCLE iber0091;
     #91#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, NULL, iber0092;
|FIN;
컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
|PROCESO iber0033;
     aArti = #33#2; |QBF aArti;
     |SI aArti = ""; |ACABA; |FINSI;

     |HAZ Existe;
     |SI FSalida ! 0; |ACABA; |FINSI;

     |HAZ Articulo;

     nSwCompu = 1;

     |DDEFECTO #233;     || ib__coma
     #233#0 = #33#0;
     #233#1 = aOrden;
     #233#2 = #33#2;
     |GRABA 000#233n;

/*
     |DDEFECTO #232;     || ib__comp
     #232#0 = aArti;
     #232#1 = aOrden;
     #232#2 = #32#3;
     |GRABA 000#232n;
*/

     |SI nPrime = 0;
          nPrime = 1;
/*
          |DDEFECTO #249;     || ib__kits
          #249#0 = #33#0;
          #249#1 = 1;
          #249#2 = #33#2;
          |GRABA 000#249n;
*/
     |FINSI;
|FPRC;

|DEFBUCLE iber0033;
     #33#1;
     ;
     #32#0, #32#1, aDArt;
     #32#0, #32#1, aHArt;
     ;
     NULL, iber0033;
|FIN;

|PROCESO iber0032;
     nPrime = 0;

     nSwCompu = 0;
     |BUCLE iber0033;
     |SI nSwCompu = 0; |ACABA; |FINSI;

     aArti = #32#0;

     |HAZ ArticuloCompuestos;

     |DDEFECTO #232;     || ib__comp
     #232#0 = aArti;
     #232#1 = aOrden;
     #232#2 = #32#3;
     |GRABA 000#232n;
|FPRC;

|DEFBUCLE iber0032;
     #32#1;
     ;
     #120#2, "   ";
     #120#2, "zzz";
     ;
     NULL, iber0032;
|FIN;

|PROCESO iber0031;
     aArti = #31#1; |QBF aArti;
     |SI aArti = ""; |ACABA; |FINSI;

     |HAZ Existe;
     |SI FSalida ! 0; |ACABA; |FINSI;

     |HAZ Articulo;

     |DDEFECTO #231;     || ib__desg
     #231#0 = #31#0;
     #231#1 = #31#1;
     #231#2 = aDes;
     |GRABA 000#231n;
     |SI nPrime = 0;
          nPrime = 1;
/*
          |DDEFECTO #249;     || ib__kits
          #249#0 = #31#0;
          #249#1 = 1;
          #249#2 = #31#1;
          |GRABA 000#249n;
*/
     |FINSI;
|FPRC;

|DEFBUCLE iber0031;
     #31#1;
     ;
     #120#2, aDArt;
     #120#2, aHArt;
     ;
     NULL, iber0031;
|FIN;

|PROCESO iber0040;
     |DDEFECTO #201;     || ib__cama
     #201#0 = #40#7;
     #201#1 = #40#8;
     #201#2 = #40#3;
     #201#3 = #40#9;
     #201#4 = #40#6;     || puede cobrar S/N
     #201#5 = #40#10;
     |GRABA 000#201n;
|FPRC;

|DEFBUCLE iber0040;
     #40#1;
     4;
     #119#0, "        ", #119#1;
     #119#0, "zzzzzzzz", #119#1;
     ;
     NULL, NULL, NULL, NULL, NULL, iber0040;
     #40#4;
|FIN;

|PROCESO agifa024;       || ib__clie
     #224#0 = #24#0;
     #224#1 = #24#1;
     #224#2 = #24#163;         || Alfanumerico1 = Alias
     #224#3 = #24#17;
     |GRABA 000#224n;
|FPRC;

|DEFBUCLE agifa024;
     #24#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, agifa024;
|FIN;

|PROCESO iber0065;
     #265#0 = #65#0;
     #265#1 = #65#1;
     |GRABA 020#265n;
|FPRC;

|DEFBUCLE iber0065;
     #65#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, iber0065;
|FIN;

|PROCESO ArticuloCompuestos;  || R.PRIEGO (10.08.2001)
     #19#0 = aArti;
     |LEE 000#19=;
     aDes = #19#110; |QBF aDes;
     |SI aDes = ""; aDes = #19#1; |FINSI;

     #56#0 = #120#0;
     #56#1 = #120#1;
     #56#3 = #19#3;    |||#32#1;      || subfamilia de los compuestos!!
     |LEE 000#56=;
     aOrden = ("   " + #56#2) % -103;

     aTipoArt = "N";
     |SI #120#5 = "F"; aTipoArt = "S"; |FINSI;
     |SI #120#5 = "P"; aTipoArt = "M"; |FINSI;

     |DDEFECTO #219;
     #219#0 = aOrden;
     #219#1 = aArti;
     #219#2 = aDes;
     #219#3 = aTipoArt;
     #219#5 = #120#6;  ||Plato
     |SI #120#8 = 0; #120#8 = 9; |FINSI;
     #219#6 = #120#8;  ||Prioridad
     |HAZ Precio;
     |GRABA 000#219n;

     |ABRE #70;
     |DDEFECTO #70;
     #70#0 = #119#0;  || Centro
     #70#1 = #19#3;           |||#32#1;        || Subf
     #70#2 = #119#1;  || Pvta
     |LEE 000#70=;
     |CIERRA #70;

     |DDEFECTO #260;
     #260#0 = aOrden;
     #260#1 = #56#4;      || Descripcion
     #260#2 = #70#3;      ||Seccion
     #260#3 = #19#3;
     |GRABA 000#260n;
||--
     #56#0 = #120#0;
     #56#1 = #120#1;
     #56#3 = #32#1;      || subfamilia de los compuestos!!
     |LEE 000#56=;
     aOrden = ("   " + #56#2) % -103;
|FPRC;

|PROCESO Articulo;
     #19#0 = aArti;
     |LEE 000#19=;
     aDes = #19#110; |QBF aDes;
     |SI aDes = ""; aDes = #19#1; |FINSI;

     #56#0 = #120#0;
     #56#1 = #120#1;
     #56#3 = #19#3;
     |LEE 000#56=;
     aOrden = ("   " + #56#2) % -103;

     aTipoArt = "N";
     |SI #120#5 = "F"; aTipoArt = "S"; |FINSI;
     |SI #120#5 = "P"; aTipoArt = "M"; |FINSI;

     |DDEFECTO #219;
     #219#0 = aOrden;
     #219#1 = aArti;
     #219#2 = aDes;
     #219#3 = aTipoArt;
     #219#5 = #120#6;  ||Plato
     |SI #120#8 = 0; #120#8 = 9; |FINSI;
     #219#6 = #120#8;  ||Prioridad
     |HAZ Precio;
     |GRABA 000#219n;

     |ABRE #70;
     |DDEFECTO #70;
     #70#0 = #119#0;  || Centro
     #70#1 = #19#3;   || Subf
     #70#2 = #119#1;  || Pvta
     |LEE 000#70=;
     |CIERRA #70;

     |DDEFECTO #260;
     #260#0 = aOrden;
     #260#1 = #56#4;
     #260#2 = #70#3;      ||Seccion
     #260#3 = #19#3;
     |GRABA 000#260n;
|FPRC;

|PROCESO iber0085;
     #56#0 = #119#0;
     #56#1 = #119#1;
     #56#3 = #85#0;
     |LEE 000#56=;
     aOrden1 = ("   " + #56#2) % -103;

     #70#0 = #119#0; ||Cent
     #70#1 = #85#0;  ||Sub
     #70#2 = #119#1; ||Pvta
     |LEE 000#70=;
     |SI FSalida ! 0; |DDEFECTO #70; |FINSI;
     |DDEFECTO #204;
     #204#0 = #85#1;     ||Cod.
     #204#1 = #85#3;     ||Des
     #204#2 = aOrden1;   ||Fami=Orden
     #204#3 = #85#4;     ||Precio
     #204#4 = #70#3;     ||Seccion
     |GRABA 040#204n;    ||
|FPRC;

|DEFBUCLE iber0085;
     #85#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, iber0085;
|FIN;

|PROCESO iber0019Fin;
     |SI aNomFic ! "";
          |CIERRA #ib__tick;
          |CIERRA #ib__ticd;
          |CIERRA #ib__data;
          |CIERRA #ib__cama;
          |CIERRA #ib__apre;
          |CIERRA #ib__extr;
          |CIERRA #ib__arti;
          |CIERRA #ib__clie;
          |CIERRA #ib__desg;
          |CIERRA #ib__comp;
          |CIERRA #ib__coma;
          |CIERRA #ib__kits;
          |CIERRA #ib__fami;
          |CIERRA #ib__rese;
          |CIERRA #ib__secc;
          |CIERRA #ib__perm;
          |CIERRA #ib__tari;
     |FINSI;
     aNomFic = "";
|FPRC;

|PROCESO iber0020Lin;
     nPutoVta = #120#1;
     aAlfa = "ib" + (("00" + #119#1) % -102);
     |SI aAlfa ! aNomFic;
          |HAZ iber0019Fin;
          aNomFic = aAlfa;
          aTipo = #119#2;

          aNom = aNomFic + "tick";
          |NOME_DAT #ib__tick#aNom#;
        ||  |DELINDEX #ib__tick;
          |ABRE #ib__tick;
          |CIERRA #ib__tick;

          aNom = aNomFic + "ticd"
          |NOME_DAT #ib__ticd#aNom#;
        ||  |DELINDEX #ib__ticd;
          |ABRE #ib__ticd;
          |CIERRA #ib__ticd;

          aNom = aNomFic + "data";
          |NOME_DAT #ib__data#aNom#;
          |DELINDEX #ib__data;
          |ABRE #ib__data;
          |CIERRA #ib__data;

          aNom = aNomFic + "cama";
          |NOME_DAT #ib__cama#aNom#;
          |DELINDEX #ib__cama;
          |ABRE #ib__cama;

          aNom = aNomFic + "apre";
          |NOME_DAT #ib__apre#aNom#;
          |DELINDEX #ib__apre;
          |ABRE #ib__apre;

          aNom = aNomFic + "extr";
          |NOME_DAT #ib__extr#aNom#;
          |DELINDEX #ib__extr;
          |ABRE #ib__extr;
          |DDEFECTO #ib__extr;
          |GRABA 020#ib__extr.n;

          aNom = aNomFic + "arti";
          |NOME_DAT #ib__arti#aNom#;
          |DELINDEX #ib__arti;
          |ABRE #ib__arti;

          aNom = aNomFic + "clie";
          |NOME_DAT #ib__clie#aNom#;
          |DELINDEX #ib__clie;
          |ABRE #ib__clie;

          aNom = aNomFic + "desg";
          |NOME_DAT #ib__desg#aNom#;
          |DELINDEX #ib__desg;
          |ABRE #ib__desg;

          aNom = aNomFic + "comp";
          |NOME_DAT #ib__comp#aNom#;
          |DELINDEX #ib__comp;
          |ABRE #ib__comp;

          aNom = aNomFic + "coma";
          |NOME_DAT #ib__coma#aNom#;
          |DELINDEX #ib__coma;
          |ABRE #ib__coma;

          aNom = aNomFic + "kits";
          |NOME_DAT #ib__kits#aNom#;
          |DELINDEX #ib__kits;
          |ABRE #ib__kits;

          aNom = aNomFic + "fami";
          |NOME_DAT #ib__fami#aNom#;
          |DELINDEX #ib__fami;
          |ABRE #ib__fami;

          aNom = aNomFic + "secc";
          |NOME_DAT #ib__secc#aNom#;
          |DELINDEX #ib__secc;
          |ABRE #ib__secc;

          aNom = aNomFic + "rese";
          |NOME_DAT #ib__rese#aNom#;
       ||   |DELINDEX #ib__rese;

          aNom = aNomFic + "perm";
          |NOME_DAT #ib__perm#aNom#;
          |DELINDEX #ib__perm;
          |ABRE #ib__perm;

          aNom = aNomFic + "tari";
          |NOME_DAT #ib__tari#aNom#;
          |DELINDEX #ib__tari;
          |ABRE #ib__tari;

          |BUCLE iber0091;

          |ABRE #ib__rese;
          |CIERRA #ib__rese;
          |BUCLE BorraRese;

          |BUCLE agifa024;

          |BUCLE iber0040;

          |ABRE #265;
          |BUCLE iber0065;
          |CIERRA #265;
          |ABRE #70;
          |BUCLE iber0085;
          |CIERRA #70;
          nLinea = 0;
     |FINSI;

     aArti = #120#2;
     |HAZ Articulo;

     |SI #120#5 = "J";
          nLinea = nLinea + 1;
          |DDEFECTO #203;
          #203#0 = nLinea;
          #203#1 = aOrden;
          #203#2 = aArti;
          #203#3 = 1;
          #203#4 = aDes;
          #203#5 = #120#6;
          |SI #120#7 = "M";
               #203#6 = "N";
          |SINO;
               #203#6 = "S";
          |FINSI;
          |GRABA 020#203n;
     |FINSI;
/*
     |SI #120#5 = "E";
          |DDEFECTO #204;
          #204#0 = aArti;
          #204#1 = aDes;
          #204#2 = aOrden;
          |GRABA 000#204n;
     |FINSI;
*/
     nPrime = 0;
     |SI #120#5 = "C";
          |BUCLE iber0031;
     |FINSI;
     |SI #120#5 = "P";
          |BUCLE iber0032;
     |FINSI;
|FPRC;

|DEFBUCLE iber0019;
     #119#2;
     ;
     nCentro, #0#1, " ";
     nCentro, #0#2, "z";
     ;
     NULL, NULL, iber0020Lin, NULL, iber0019Fin;
|FIN;
컴컴컴컴컴컴컴컴컴컴컴 SALAS 컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
|PROCESO iber0074Fin;
     |SI aNomFic ! "";
          |CIERRA #200;
          |CIERRA #402;
     |FINSI;
     aNomFic = "";
|FPRC;

|PROCESO iber0074;
     aAlfa = "ib" + (("00" + #74#1) % -102) + "mesa";
     |SI aAlfa ! aNomFic;
          |HAZ iber0074Fin;
          aNomFic = aAlfa;

          aNom = "ib" + (("00" + #74#1) % -102) + "mesa";
          |NOME_DAT #200 aNom;
       || |DELINDEX #200;  *** NO SE INICIALIZA
          |ABRE #200;

          aNom = "ib" + (("00" + #74#1) % -102) + "sala";
          |NOME_DAT #402 aNom;
          |DELINDEX #402;
          |ABRE #402;
     |FINSI;

     |DDEFECTO #402;
     #402#0 = #74#2;
     #402#1 = #74#3;
     |GRABA 020#402n;

     |SI #74#4 = "S";
          || Borra las anteriores
          |PARA nMesa = 0; |SI nMesa < #74#5; |HACIENDO nMesa = nMesa + 1;
               #200#5 = #74#2;
               #200#0 = nMesa;
               |BORRA 000#200c;
          |SIGUE;
          || Graba las actuales
          |PARA nMesa = #74#5; |SI nMesa [ #74#6; |HACIENDO nMesa = nMesa + 1;
               |DDEFECTO #200;
               #200#5 = #74#2;
               #200#0 = nMesa;
               |GRABA 000#200n;
          |SIGUE;
          || Borra las posteriores
          #200#5 = #74#2;
          #200#0 = #74#6 + 1;
          |LEE 101#200];
          |PARA; |SI FSalida = 0; |Y #200#5 = #74#2; |HACIENDO;
               |BORRA 020#200a;
               |LEE 101#200s;
          |SIGUE;
     |SINO;
         || No hay mesas, las elimina todas
          #200#5 = #74#2;
          #200#0 = 0;
          |LEE 101#200];
          |PARA; |SI FSalida = 0; |Y #200#5 = #74#2; |HACIENDO;
               |BORRA 020#200a;
               |LEE 101#200s;
          |SIGUE;
     |FINSI;
|FPRC;

|DEFBUCLE iber0074;
     #74#1;
     ;
     nCentro, #0#1, 01;
     nCentro, #0#2, 99;
     ;
     NULL, iber0074, NULL, NULL, iber0074Fin;
|FIN;
컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
|PROCESO Tipo12; |TIPO 12;
     |SI #0#0 ! "SI"; |ACABA; |FINSI;

     aDArt = " " * 20;
     aHArt = "z" * 20;


     |ABRE #19;
     |ABRE #56;
     |BUCLE iber0019;    || puntos de venta centro
     |CIERRA #56;
     |CIERRA #19;

     |BUCLE iber0074;    || salas puntos de venta centro

     ||컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴 Copia Dll Central a Puesto
     |DBASE aPath;  aPath = aPath + "tactil/";
     aAlfa = "";    |IP_REMOTO aAlfa;
     |SI aAlfa ! "";
          aOrg = aPath + "dscocina1.dll"; |ENVIA_FICHERO aOrg, "dscocina1.dll";
          aOrg = aPath + "dsrestau1.dll"; |ENVIA_FICHERO aOrg, "dsrestau1.dll";
          aOrg = aPath + "dsbar1.dll";    |ENVIA_FICHERO aOrg, "dsbar1.dll";
     |SINO;
          aOrg = aPath + "dscocina1.dll"; |COPIA_FICHERO aOrg, "dscocina1.dll";
          aOrg = aPath + "dsrestau1.dll"; |COPIA_FICHERO aOrg, "dsrestau1.dll";
          aOrg = aPath + "dsbar1.dll";    |COPIA_FICHERO aOrg, "dsbar1.dll";
     |FINSI;

     |DELINDEX #93;
     |SI #16#30 = "S";   ||Presentar Tarjetas
          |ABRE #93;
          |BUCLE agifa515;
          |CIERRA #93;
     |FINSI;
|FPRC;

|PROGRAMA;
     fFecRese  = ~;

     |DFICE aCentro;
     aCentro = aCentro % -403;
     nCentro = aCentro;

     |ABRE #16;
     #16#0 = nCentro;
     |LEE 000#16=;
     |CIERRA #16;

     |DDEF aAlfa;
     aAlfa = aAlfa + "/iber0020";
     |CARGA_DEF aAlfa, referen@;
     |SI FSalida = 0;
          |ABRE #referen@;
          |SI PARAMETRO ! "";
               #0#1 = PARAMETRO;
               #0#2 = #0#1;
               #0#0 = "SI";
               |HAZ Tipo12;
          |SINO;
               |MANTE #0;
          |FINSI;
          |CIERRA #referen@;
          |DESCARGA_DEF #referen@;
     |SINO;
          aAlfa = "0000Error al cargar " + aAlfa;
          |MENAV aAlfa;
     |FINSI;
|FPRO;
