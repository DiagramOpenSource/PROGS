|FICHEROS;
     agifa134 #0;   || Facturas(C).
     agifa059 #1;   || Facturas(L).
     agifa010 #2;   || Albaranes(C).
     agifa071 #3;   || Albaranes(L).
     agifa104 #8;   || Pedidos(C).
|FIN;

|VARIABLES;
     modo      = 0;
     repant    = "";
     pagant    = "";
     nGuarda = 0;
|FIN;

||---------------------------------------------------------------------------
|| GUARDA EL REPRESENTANTE Y LA FORMA DE PAGO.
||---------------------------------------------------------------------------
|PROCESO guarda; |TIPO 7;
     repant = #0#7;
     pagant = #0#11;
|FPRC;

||---------------------------------------------------------------------------
|| LEE LAS LINEAS DE LA FACTURA, Y CAMBIA EL REPRESENTANTE Y LA FORMA DE
|| PAGO E ALBARANES Y PEDIDOS.
||---------------------------------------------------------------------------
|PROCESO cambi2;
     #8#25 = #3#31;           || SERIE.
     #8#0  = #3#12;           || N§.PEDIDO.
     |LEE 101#8=;
     |SI FSalida = 0;
          #8#7 = #0#7;        || REPRESENTANTE.
          #8#9 = #0#11;       || F.PAGO.
          |GRABA 021#8a;
     |FINSI;
     |LIBERA #8;
|FPRC;

|DEFBUCLE lineasa;
     #3#1;
     ;
     #2#52,#2#0,001;
     #2#52,#2#0,999;
     ;
     NULL,cambi2;
|FIN;

|PROCESO cambi1;
     #2#52 = #1#15;           || SERIE ALBARAN.
     #2#0  = #1#2;            || N§.ALBARAN.
     |LEE 101#2=;
     |SI FSalida = 0;
          #2#6 = #0#7;        || REPRESENTANTE.
          #2#8 = #0#11;       || F.PAGO.
          |GRABA 021#2a;
          |BUCLE lineasa;     || LEE LINEAS ALBARANES.
     |FINSI;
     |LIBERA #2;
|FPRC;

|DEFBUCLE lineasf;
     #1#1;
     ;
     #0#49,#0#0,001;
     #0#49,#0#0,999;
     ;
     NULL,cambi1;
|FIN;

|| PRINCIPIO CAMBIO CODIGOS REPRE. Y F.PAGO..................................
|PROCESO cambia; |TIPO 0;
     nGuarda = FSalida;
     modo = (FEntrada / 100) ! 0;
     |SI modo = 2;
          |SI repant ! #0#7; |O pagant ! #0#11;
               |AVISO;
               |CONFI "0000N¿Cambiar Representante y F.Pago en Pedidos y Albaranes? (S/N) ";
               |SI FSalida = 0;
                    repant = #0#7;
                    pagant = #0#11;
                    |ABRE #2;
                    |ABRE #8;
                    |BUCLE lineasf;     || LEE LINEAS FACTURA.
                    |CIERRA #2;
                    |CIERRA #8;
               |FINSI;
          |FINSI;
     |FINSI;
     FSalida = nGuarda;
|FPRC;
