|FICHEROS;
     dsm00244 #0;
     dsm00245 #1;
     dsm00245@ #2;
     agifa019 #19;
|FIN;

||INC-LUYE llena_c1;

|VARIABLES;
     aDef      = "";
     aMensa    = "";
     nPre      = 0;
     nForma    = 0;
     aAlfa     = "";
|FIN;

|PROCESO Tipo2; |TIPO 2;
     nForma = 0 +. 1;
     |SI nForma ! 1; |ACABA; |FINSI;

     |DDEF aDef;
     aDef = aDef + "dsm00245";
     |CARGA_DEF aDef, dsm00245@;
     |SI FSalida = 0;
          aMensa = "";
          |ABRE #2;
          #2#0 = #1#0;
          #2#1 = #1#1;
          #2#2 = "";
          |LEE 000#2];
          |SI FSalida = 0; |Y #2#0 = #1#0; |Y #2#1 = #1#1;
               |ABRE #19;
               #19#0 = #2#2;
               |LEE 000#19=;
               nPre = #19#18;
               #19#0 = #1#2;
               |LEE 000#19=;
               |SI nPre ! #19#18;
                    aMensa = "0000Articulo con Precio de Compra distinto...";
               |FINSI;
               |CIERRA #19;
          |FINSI;
          |SELECT #2#2;
          #2#0 = #1#0;
          #2#2 = #1#2;
          |LEE 000#2=;
          |SI FSalida = 0; |Y #2#1 ! #1#1;
               aMensa = "0000Articulo existente en" + &13 + "Promocion:" + #2#1;
          |FINSI;
          |CIERRA #2;
          |DESCARGA_DEF #dsm00245@;
          |SI aMensa ! "";
               |MENAV aMensa;
               |ERROR; |ACABA;
          |FINSI;
     |SINO;
          aDef = "0000Error al cargar " + aDef;
          |MENAV aDef;
     |FINSI;
|FPRC;

|PROCESO DesArti; |TIPO 0;
     |ABRE #19;
     #19#0 = #1#2;
     |LEE 000#19=;
     |CIERRA #19;
     aAlfa = #19#195; |QBF aAlfa;
     |SI aAlfa = ""; aAlfa = #19#1; |FINSI;
     #1#3 = aAlfa; |PINTA #1#3;
|FPRC;
