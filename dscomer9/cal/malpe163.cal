|FICHEROS;
     malpe163 #0;
     malpe154 #154; || Tabajadores
     malpe156 #156; || Partes
     malpe158 #158; || Fabricas
     malpe159 #159; || Resumen
     malpe164 #164; || Tmp
|FIN;

|VARIABLES;
     &Tempo = "";
     aTraba = "";
     i = 0;
     nDiasMes = 0;
     fFecha = @;
     aDFecha = "";
     aHFecha = "";
     aMes = "";
     aA¤o = "";
     aAlfa = "";
     nCampo = 0;
     f = @;
     nDia = 0;
     aDia = "";
     &enTotal = 0;
|FIN;

|PROCESO PonDias;
     nCampo = 83;
     |PARA nDia = 1; |SI nDia [ 16; |HACIENDO nDia = nDia + 1;
          #164nCampo = nDia;
          nCampo = nCampo + 1;
     |SIGUE;

     aDFecha = "17." + aMes + "." + aA¤o;
     f = aDFecha;
     nCampo = 83;
     |PARA f = aDFecha; |SI f [ aHFecha; |HACIENDO f = f + 1;
          aDia = f % 102;
          aAlfa = #164nCampo; |QBF aAlfa;
          aAlfa = aAlfa + "/" + aDia;
          #164nCampo = aAlfa;
          nCampo = nCampo + 1;
     |SIGUE;
     |GRABA 020#164a;

     enTotal = enTotal + #164#35;
|FPRC;

|DEFBUCLE PonDias;
     #164#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, PonDias;
|FIN;

|PROCESO Partes;
     |DDEFECTO #154;
     #154#0 = #156#0;
     |LEE 000#154=;

     |SI #0#4 = 2;
          |SI #156#2 ! #0#5; |ACABA; |FINSI;
     |FINSI;

     |DDEFECTO #164;
     #164#0 = #156#0;
     #164#1 = #156#2;
||     #164#2 = #156#3;
     |LEE 101#164=;
     |SI FSalida ! 0; |GRABA 020#164b; |FINSI;
     #164#3 = nDiasMes;
     aAlfa = #156#1 % 102;    || Dia
     nCampo = aAlfa + 3;
     #164nCampo = #164nCampo + #156#4;
     nCampo = nCampo + 32;

     || 1. Dia Normal, 2. Sabado Tarde, 3. Noche, 4. Sabado Tarde + Noche

     |SI #156#3 = "S";
          |SI #164nCampo = 1; #164nCampo = 2; |FINSI;
     |FINSI;
     |SI #156#5 = 2;
          |SI #164nCampo = 1; #164nCampo = 3; |FINSI;
          |SI #164nCampo = 2; #164nCampo = 4; |FINSI;
     |FINSI;

     #164#35 = 0;
     |PARA i = 4; |SI i [ 34; |HACIENDO i = i + 1;
          #164#35 = #164#35 + #164i;
     |SIGUE;
     |GRABA 020#164a;
     |LIBERA #164;
|FPRC;

|DEFBUCLE Partes;
     #156#1;
     ;
     #0#0, aDFecha, 00, " ";
     #0#1, aHFecha, 99, "z";
     ;
     NULL, Partes;
|FIN;

|PROCESO Defecto; |TIPO 7;
     |SI #0#3 = 0;
          fFecha = ~;
          aAlfa = fFecha % 402;
          #0#3 = aAlfa;
          aAlfa = fFecha % 704;
          #0#2 = aAlfa;
          |PINTA #0#2;
          |PINTA #0#3;
     |FINSI;
|FPRC;

|PROCESO Temporal;
     |DDEFECTO #154;
     #154#0 = #164#0;
     |LEE 000#154=;

     |DDEFECTO #158;
     #158#0 = #164#1;
     |LEE 000#158=;

     |DDEFECTO #159;
     |SI aTraba ! #164#0;
          aTraba = #164#0;
          #159#0 = #164#0;
          #159#1 = #0#2;
          #159#2 = #0#3;
          |LEE 000#159=;
     |FINSI;

     |PRINT;
|FPRC;

|DEFBUCLE Temporal;
     #164#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, Temporal;
|FIN;

|PROCESO SacaMes;
     aMes = aAlfa % 402;
     aA¤o = aAlfa % 704;
     aDFecha = "01." + aMes + "." + aA¤o;
     aHFecha = "27." + aMes + "." + aA¤o;
     |PARA; |SI; |HACIENDO;
          fFecha = aHFecha;
          fFecha = fFecha + 1;
          aAlfa = fFecha % 402;
          |SI aMes ! aAlfa; |SAL; |FINSI;
          aHFecha = fFecha;
     |SIGUE;
|FPRC;

|PROCESO Fabrica;
     |SI #0#4 = 2;
          |ABRE #158;
          #158#0 = #0#5;
          |LEE 000#158=;
          |SI FSalida ! 0;
               |MENAV "0000No existe fabrica...";
               |ERROR;
          |FINSI;
          |CIERRA #158;
     |FINSI;
|FPRC;

|PROCESO Tipo; |TIPO 0;
     |SI #0#4 = 1;
          |C_M #0#5, 1, "N";
     |SINO;
          |C_M #0#5, 1, "S";
     |FINSI;
|FPRC;

|PROCESO Tipo3; |TIPO 3;
     aAlfa = "01." + (("00" + #0#3) % -102) + "." + (("0000" + #0#2) % -104);
     |HAZ SacaMes;
     |PARA fFecha = aDFecha; |SI fFecha [ aHFecha; |HACIENDO fFecha = fFecha + 1;
          nDiasMes = nDiasMes + 1;
     |SIGUE;

     |HAZ CreaTempo;
     |NOME_DAT #164#Tempo#;
     |DELINDEX #164;

     Impresora = "Crystal Reports";
     |IMPRE 0;
     |SI FSalida = 0;
          |INFOR "malpe163";
          |SI FSalida ! 0;
               |MENAV "0000No existe informe 'malpe163'";
          |SINO;
               |ABRE #154;
               |ABRE #164;
               |BUCLE Partes;
               |CIERRA #164;
               |ABRE #159;
               |ABRE #158;
               |BUCLE PonDias;
               |BUCLE Temporal;
               |CIERRA #158;
               |CIERRA #159;
               |CIERRA #154;
               |PIEINF;
               |FININF;
          |FINSI;
          |FINIMP;
     |FINSI;

     |DELINDEX #164;
     |HAZ BoraTempo;
|FPRC;
