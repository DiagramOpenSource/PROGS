|FICHEROS;
     tecaz008 #0;
     tecaw003 #3,MANTE;
     agifa017 #17;
     agifa019 #19;
     tecam032 #32;
     tecam033 #33;
     tecam034 #34;
     dsm00054 #54;
     dsm00055 #55;
|FIN;

|VARIABLES;
     nConta = 0;
     &enForma = 0;
     nLin = 0;
     nCabe = 0;
     i = 0;
     nHayPen = 0;
     aAlfa = "";
     {-1}aMenu = "";
     aMenu1 = "2400";
     aMenu2 = "1";
     aMenu3 = "Opciones:  ";
     aMenu4 = "RAIC";
     aMenu5 = " Repasar ";
     aMenu6 = " Aceptar ";
     aMenu7 = " Imprimir ";
     aMenu8 = " Cancelar ";
|FIN;

|PROCESO Impre;
     |PRINT;
|FPRC;

|DEFBUCLE Impre;
     #3#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, Impre;
|FIN;

|PROCESO Imprime;
     |IMPRE 0;
     |SI FSalida = 0;
          |INFOR "tecaz008";
          |SI FSalida ! 0;
               |MENAV "0000No existe informe 'tecaz032'";
          |SINO;
               |BUCLE Impre;
               |PIEINF;
               |FININF;
          |FINSI;
          |FINIMP;
     |FINSI;
|FPRC;

|PROCESO Stock;
     |DDEFECTO #34;
     #34#0 = #3#3;
     #34#1 = #3#5;
     |LEE 101#34=
     |SI FSalida ! 0; |GRABA 020#34b; |FINSI;

     #34#3 = #34#3 + #3#9;
     #34#4 = #34#4 - #3#9 - #3#11;
     |GRABA 020#34a;
     |LIBERA #34;
|FPRC;

|PROCESO Movimiento;
     |SI nCabe = 0;
          nCabe = 1;
          #54#2 = #0#0;
          #54#0 = 999999;
          |LEE 000#54];
          |SI FSalida = 0;
               |LEE 000#54a;
          |SINO;
               |LEE 000#54u;
          |FINSI;
          |SI FSalida = 0; |Y #54#2 = #0#0;
               nConta = #54#0 + 1;
          |SINO;
               nConta = 1;
          |FINSI;
          |DDEFECTO #54;
          #54#2 = #0#0;
          #54#0 = nConta;
          |GRABA 020#54n;
     |FINSI;

     nLin = nLin + 1;
     |DDEFECTO #55;
     #55#28 = #54#2;
     #55#27 = #54#0;
     #55#11 = nLin;

     #55#0 = #3#3;
     #55#1 = #3#10;
     #55#2 = #3#4;

     #17#0 = #55#0;
     #17#1 = #55#1;
     |LEE 000#17=;

     #55#3 = #17#39; || Stock Sal
     #55#4 = #3#9;
     #55#5 = #3#5;

     #17#0 = #55#0;
     #17#1 = #55#5;
     |LEE 000#17=;
     #19#0 = #55#0;
     |LEE 000#19=;

     #55#6 = #17#39; || Stock Entra
     #55#7 = #19#104; || Stock Artic
     #55#8 = #54#1;
     enForma = 1;
     |HAZ AcumulaME_MS;
     |GRABA 020#55n;
|FPRC;

|PROCESO Adjudica;
     |SI #3#10 = 0;
          |ACABA;
     |FINSI;
     #33#0 = #3#0;
     #33#1 = #3#1;
     #33#2 = #3#2;
     |LEE 121#33=;
     |SI FSalida = 0;
          |HAZ Stock;
          #33#7 = #33#7 + #3#9;
          #33#8 = #33#8 - #3#9 - #3#11;
          |GRABA 020#33a;
          |LIBERA #33;
          |HAZ Movimiento;
     |FINSI;
|FPRC;

|DEFBUCLE Adjudica;
     #3#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, Adjudica;
|FIN;

|PROCESO Aceptar;
     nLin = 0;
     |ABRE #54;
     |ABRE #55;
     |ABRE #33;
     |ABRE #34;
     |BUCLE Adjudica;
     |CIERRA #34;
     |CIERRA #33;
     |CIERRA #55;
     |CIERRA #54;
|FPRC;

|PROCESO Servir; |TIPO 0;
     |SI #3#9 > #3#8;
          |MENAV "0000No se pueden adjudicar mas de las pedidas...";
          |ERROR; |ACABA;
     |FINSI;
     #3#11 = 0;
     |SI #3#9 < #3#8;
           |CONFI "2400NEsta recibiendo menos de lo Pedido, Despreciar: ";
           |SI FSalida = 0;
               #3#11 = #3#8 - #3#9;
           |FINSI;
     |FINSI;
|FPRC;

|PROCESO Pulsa; |TIPO 0;
     |SI FSalida = 0; |Y #3Campo = Contenido;
       |ET Otra;
          |ENCAMPO #10#3;
          |SI FSalida = 0;
               |ENCAMPO #9#3;
               |SI FSalida = 2; |VETE Otra; |FINSI;
          |FINSI;
          |SI FSalida = 0;
               |GRABA 020#3a;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO CargaLin;
     |PARA i = 0; |SI i [ 8; |HACIENDO i = i + 1;
          #3i = #33i;
     |SIGUE;
     |SI #3#8 ! 0;
          nHayPen = 1;
          |GRABA 020#3n;
     |FINSI;
|FPRC;

|PROCESO Correcto; |TIPO 12;
     |SI FSalida ! 0; |ACABA; |FINSI;
     |ABRE #32;
     #32#0 = #0#0;
     #32#1 = #0#1;
     |LEE 000#32=;
     |SI FSalida ! 0;
          |MENAV "0000No existe Documento...";
     |SINO;
          |PARA i = 2; |SI i [ 10; |HACIENDO i = i + 1;
               #0i = #32i;
          |SIGUE;
          |PINDA #0#0;
          nHayPen = 0;
          |ABRE #3;
          |BUCLELIN 2CargaLin#32;
          |CIERRA #3;
          |SI nHayPen = 0;
               |MENAV "0000No quedan unidades pendientes...";
          |SINO;
               |PARA; |SI aMenu2 = 1; |O aMenu2 = 3; |HACIENDO;
                    |SI aMenu2 = 3;
                         |HAZ Imprime;
                    |SINO;
                         |ENTLINEAL #1#3, 3, 4, 22, 0, Min, Max;
                    |FINSI;
                    |MENU aMenu;
                    aMenu2 = FSalida;
               |SIGUE;
               |SI aMenu2 = 2;
                    |HAZ Aceptar;
               |FINSI;
          |FINSI;
     |FINSI;
     |CIERRA #32;
|FPRC;

|PROCESO Min;
     #3#0 = #0#0;
     #3#1 = #0#1;
     #3#2 = 1;
|FPRC;

|PROCESO Max;
     #3#0 = #0#0;
     #3#1 = #0#1;
     #3#2 = 999;
|FPRC;

|PROGRAMA;
     aAlfa = Usuario;
     |NOME_DAT #3 aAlfa;
     |DELINDEX #3;
     |CLS;
     |PINPA #0#0;
     |PINDA #0#0;
     |ENTLINEAL #1#3, 3, 7, 22, 0, Min, Max;
     |ENDATOS #1#0;
     |DELINDEX #3;
|FPRO;
