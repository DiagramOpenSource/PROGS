|FICHEROS;
     goartive #0;        ||Fichero de artidas
     godeivec #1;        ||Fichero de descripciones ampliadas CABS.
     godeivel #2;        ||Fichero de descripciones ampliadas LINS.
     goartdes #3,MANTE;  ||Fichero de Descomposicion.
     agifa321 #4;        ||Parametros Moneda.
     gofamive #5;        ||Familias IVE
     gosubive #6;        ||SubFamilias IVE
     goapaive #7;        ||Capitulos IVE
     goparame #8;        ||Parametros Obra.
     gogenpar #9,MANTE;
     duppart@ #10;
     dupparl@ #11;
     dupdesc@ #12;
     dupdesl@ #13;
     gomedive #16;
     goarticu@ #17;      || Por problemas con procesos duplicados
     dsm00238 #238;
|FIN;
-----------------------------------------------------------------------------
|VARIABLES;
     &enSw = 0;
     nPrecio = 0;
     &enDisInte = 0;
     &enDisTens = 0;
     &eaSeccion = "";
     &eaFamilia = "";
     &eaSubFamilia = "";
     &eaCapitulo = "";
     &eaPartida = "";
     &enBotonAmpli = 0;
     &PasoPorDes    = 0;
     &qAlta         = "";
     &cCodMonBase   = "";
     &cDesMonBase   = "";
     &cCodMonTrab   = "";
     &cDesMonTrab   = "";
     &nDeci_PBase   = 4;
     &nDeci_TBase   = 4;
     &nDeci_PTrab   = 4;
     &nDeci_TTrab   = 4;
     &nDeci_TotVis  = 4;
     &nEurComTrab   = 0;
     &nEurComBase   = 0;
     &nEurVenTrab   = 0;
     &nEurVenBase   = 0;
     &cCodigo       = "";
     &cDesc         = "";
     &eaCodigo      = "";

     cDesco         = "";
     Camino         = "";

     nModo          = 0;
     nBotAmpli      = 0;
     nBotFamilia    = 0;
     nBotSubFam     = 0;
     nBotCapitulo   = 0;
     nBotGenera     = 0;
     nConta         = 0;
     nLinea         = 0;
     nHay           = 0;
     aAlfa = "";
     aParam = "";
|FIN;

-----------------------------------------------------------------------------
|PROCESO GraArticu;
     |DDEF aAlfa;
     aAlfa = aAlfa + "agifa019";
     |CARGA_DEF aAlfa, goarticu@;
     |SI FSalida = 0;
          |DDEFECTO #17;
          |ABRE #17;
          #17#0 = #0#0;
          |LEE 101#17=;
          |SI FSalida ! 0; |GRABA 020#17b; |FINSI;

          #17#1 = #0#1;
          #17#125 = #0#7;     || Seccion
          #17#2 = #0#9;       || Familia
          #17#3 = #0#11;      || Subfam
          #17#127 = #0#13;    || Capi
          #17#5 = #0#2;       || Medida
          #17#201 = #0#30;    || Unidad Doble Stock
          |C_M #0#30, 0, aAlfa;
          #17#179 = aAlfa;
          #17#134 = #17#0;    || Articulo Proveedor
          |GRABA 020#17a;
          |CIERRA #17;

          |DESCARGA_DEF #goarticu@;
     |FINSI;
|FPRC;

|PROCESO Parti3; |TIPO 3;
     |HAZ GraArticu;
|FPRC;

|PROCESO UniMed66; |TIPO 66;
     |ABRE #238;
     #238#0 = #0#30;
     |LEE 000#238];
     |CONSULTA_CLAVE #1#238;
     |SI FSalida = 0;
          #0#30 = #238#0; |PINTA #0#30;
     |SINO;
          |ERROR;
     |FINSI;
     |CIERRA #238;
|FPRC;

|PROCESO UniMed0; |TIPO 0;
     |ABRE #238;
     #238#0 = #0#30;
     |LEE 000#238=;
     |SI FSalida ! 0;
          |MENAV "0000Código inexistente...";
          |ERROR;
     |FINSI;
     |CIERRA #238;
|FPRC;


|PROCESO Medida; |TIPO 0;
     |ABRE #16;
     #16#0 = #0#2;
     |LEE 000#16=;
     |SI FSalida = 0; |Y #16#12 = "S";
          |C_M #0#30, 1, "S";
     |SINO;
          |C_M #0#30, 1, "N";
     |FINSI;
     |CIERRA #16;
|FPRC;

|PROCESO Fin; |TIPO 11;
     |SI FSalida = 7;
          |HAZ Escape0;
     |FINSI;
|FPRC;

|PROCESO PulsaF02; |TIPO 0;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 1;
          |ESTADO_CONTROL nBotAmpli, "HIDE"; ||"SHOW""HIDE""ENABLE""DISABLE".
     |FINSI;
     |SI FSalida = 10;
          cDesc     = #0#1;
          eaCodigo  = #0#0;
          |ESTADO_CONTROL nBotAmpli, "HIDE"; ||"SHOW""HIDE""ENABLE""DISABLE".
          |HAZ Ampliadaive;
          |ESTADO_CONTROL nBotAmpli, "SHOW"; ||"SHOW""HIDE""ENABLE""DISABLE".
          PasoPorDes = 1;
          |ACABA;
     |FINSI;
     |ESTADO_CONTROL nBotAmpli, "HIDE"; ||"SHOW""HIDE""ENABLE""DISABLE".
|FPRC;
-----------------------------------------------------------------------------
|PROCESO MinDesPar;
     #3#0 = #0#0;
     #3#1 = 1;
|FPRC;
-----------------------------------------------------------------------------
|PROCESO MaxDesPar;
     #3#0 = #0#0;
     #3#1 = 999;
|FPRC;
-----------------------------------------------------------------------------
|PROCESO BorraCompo;
     |BORRA 020#3a;
|FPRC;
-----------------------------------------------------------------------------
|DEFBUCLE BorrComp;
     #3#1;
     ;
     #0#0,   1;
     #0#0, 999;
     ;
     NULL, BorraCompo;
|FIN;
-----------------------------------------------------------------------------
|PROCESO DesPar; |TIPO 0;
     |SI cDesco ! #0#4;
          |SI #0#4 = "S";
               |C_M #0#15, 1, "N";
               |ENTLINEAL #1#3, 2, 1, 22, 0, MinDesPar, MaxDesPar;
               |HAZ RecaPrec;
          |SINO;
               |C_M #0#15, 1, "S";
               |CONFI "0000N¨Desea eliminar los componentes de la partida?";
               |SI FSalida = 0;
                    |BUCLE BorrComp;
               |FINSI;
          |FINSI;
     |SINO;
          |SI #0#4 = "S";
               |C_M #0#15, 1, "N";
               |ENTLINEAL #1#3, 2, 2, 22, 0, MinDesPar, MaxDesPar;
               |HAZ RecaPrec;
          |SINO;
               |C_M #0#15, 1, "S";
          |FINSI;
     |FINSI;
|FPRC;
-----------------------------------------------------------------------------
|PROCESO MonTraArt; |TIPO 14;
     #0#5 = "";
     cCodMonBase = "";
     |HAZ MonBas;
     #0#5 = cCodMonBase;
     |ENTLINEAL #1#3, -2, 7, 22, 0, MinDesPar, MaxDesPar;
     |ATRI I;
|FPRC;
-----------------------------------------------------------------------------
|PROCESO DesgPar;
     #0#15 = #0#15 + #3#7;
|FPRC;
-----------------------------------------------------------------------------
|DEFBUCLE DesgPar;
     #3#1;
     ;
     #0#0, 1;
     #0#0, 999;
     ;
     NULL, DesgPar;
|FIN;
-----------------------------------------------------------------------------
|PROCESO RecaPrec;
     #0#15 = 0;
     |BUCLE DesgPar;
     |PINDA #0#0;
|FPRC;
-----------------------------------------------------------------------------
|PROCESO Mone; |TIPO 7;
     cDesco = #0#4;
     cCodMonTrab = #0#5;
     |HAZ MonBas;
|FPRC;
-------------------------------------------------------------------------------
|PROCESO BInicio; |TIPO 80;
     |ABRE #8; |LEE 000#8p; |CIERRA #8;
     |CONTROL_SIMPLE 0, "BOTON,F2 - Descripcion Ampliada", 2325, 2355, F2;
     nBotAmpli = FSalida;
     enBotonAmpli = FSalida;
     |ESTADO_CONTROL nBotAmpli, "HIDE"; ||"SHOW""HIDE""ENABLE""DISABLE".
     |CONTROL_SIMPLE 0, "BOTON,F2 - Consultar Familias", 2325, 2355, F2;
     nBotFamilia = FSalida;
     |ESTADO_CONTROL nBotFamilia, "HIDE"; ||"SHOW""HIDE""ENABLE""DISABLE".
     |CONTROL_SIMPLE 0, "BOTON,F2 - Consultar Subfamilias", 2325, 2355, F2;
     nBotSubFam = FSalida;
     |ESTADO_CONTROL nBotSubFam, "HIDE"; ||"SHOW""HIDE""ENABLE""DISABLE".
     |CONTROL_SIMPLE 0, "BOTON,F2 - Consultar Capitulos", 2325, 2355, F2;
     nBotCapitulo = FSalida;
     |ESTADO_CONTROL nBotCapitulo, "HIDE"; ||"SHOW""HIDE""ENABLE""DISABLE".
||     |CONTROL_SIMPLE 0, "BOTON,F2 - Generador Ref. Partidas", 2325, 2355, F2;
||     nBotGenera = FSalida;
||     |ESTADO_CONTROL nBotGenera, "HIDE"; ||"SHOW""HIDE""ENABLE""DISABLE".
|FPRC;
-------------------------------------------------------------------------------
|PROCESO BFin; |TIPO 90;
     |PULSATECLA;
     |FIN_CONTROL_WINDOWS nBotAmpli;
     |PULSATECLA;
     |FIN_CONTROL_WINDOWS nBotFamilia;
     |PULSATECLA;
     |FIN_CONTROL_WINDOWS nBotSubFam;
     |PULSATECLA;
     |FIN_CONTROL_WINDOWS nBotCapitulo;
     |PULSATECLA;
||     |FIN_CONTROL_WINDOWS nBotGenera;
||     |PULSATECLA;
|FPRC;
-------------------------------------------------------------------------------
|PROCESO F2;
     |PTEC 824;
|FPRC;
-------------------------------------------------------------------------------
|PROCESO BShow; |TIPO 7;
||     |SI Campo = 0;
||          nModo = (FEntrada/100)!0;
||          |SI nModo = 1;
||               |ESTADO_CONTROL nBotGenera, "SHOW"; ||"SHOW""HIDE""ENABLE""DISABLE".
||          |FINSI;
||     |FINSI;
     |SI Campo = 1;
          |ESTADO_CONTROL nBotAmpli, "SHOW"; ||"SHOW""HIDE""ENABLE""DISABLE".
     |FINSI;
     |SI Campo = 9;
          |ESTADO_CONTROL nBotFamilia, "SHOW"; ||"SHOW""HIDE""ENABLE""DISABLE".
     |FINSI;
     |SI Campo = 11;
          |ESTADO_CONTROL nBotSubFam, "SHOW"; ||"SHOW""HIDE""ENABLE""DISABLE".
     |FINSI;
     |SI Campo = 13;
          |ESTADO_CONTROL nBotCapitulo, "SHOW"; ||"SHOW""HIDE""ENABLE""DISABLE".
     |FINSI;
|FPRC;
-------------------------------------------------------------------------------
|PROCESO Escape0; |TIPO 0;
||     |ESTADO_CONTROL nBotGenera,   "HIDE"; ||"SHOW""HIDE""ENABLE""DISABLE".
     |ESTADO_CONTROL nBotAmpli,    "HIDE"; ||"SHOW""HIDE""ENABLE""DISABLE".
     |ESTADO_CONTROL nBotFamilia,  "HIDE"; ||"SHOW""HIDE""ENABLE""DISABLE".
     |ESTADO_CONTROL nBotSubFam,   "HIDE"; ||"SHOW""HIDE""ENABLE""DISABLE".
     |ESTADO_CONTROL nBotCapitulo, "HIDE"; ||"SHOW""HIDE""ENABLE""DISABLE".
|FPRC;
-------------------------------------------------------------------------------
|PROCESO ConsFam; |TIPO 0;
     nModo = (FEntrada / 100) ! 0;
     |SI FSalida ! 10;
          |ABRE #5;
          #5#0 = #0#9;
          |LEE 010#5=;
          |SI FSalida = 0;
               |SELECT #3#5;
               #5#0 = #0#9;
               #5#2 = #0#7;
               |LEE 010#5=;
               |SI FSalida = 0;
                    #0#10 = #5#1;
                    |PINTA #0#10;
               |SINO;
                    |MENAV "0000FAMILIA NO PERMITIDA";
                    |ERROR;
                    |ACABA;
               |FINSI;
          |SINO;
               |MENAV "0000FAMILIA INEXISTENTE";
               |ERROR;
               |ACABA;
          |FINSI;
          |CIERRA #5;
     |FINSI;
     |SI FSalida = 10;
          |ABRE #5;
          |DDEFECTO #5;
          |CONSULTA_F_CLAVE #4#5, SecMin, SecMax;
          |SI FSalida = 0;
               #0#9 = #5#0;
               #0#10 = #5#1;
               |PINTA #0#9;
               |PINTA #0#10;
          |SINO;
               |ERROR;
               |ACABA;
          |FINSI;
          |CIERRA #5;
     |FINSI;
     |ESTADO_CONTROL nBotFamilia, "HIDE";
|FPRC;
-------------------------------------------------------------------------------
|PROCESO Consulta_Familia;
|FPRC;
-------------------------------------------------------------------------------
|PROCESO SecMin;
     #5#2 = #0#7;
|FPRC;
-------------------------------------------------------------------------------
|PROCESO SecMax;
     #5#2 = #0#7;
|FPRC;
-------------------------------------------------------------------------------
|PROCESO ConsSubFam; |TIPO 0;
     nModo = (FEntrada / 100) ! 0;
     |SI FSalida ! 10;
          |ABRE #6;
          #6#0 = #0#11;
          |LEE 010#6=;
          |SI FSalida = 0;
               |SELECT #3#6;
               #6#0 = #0#11;
               #6#2 = #0#9;
               |LEE 010#6=;
               |SI FSalida = 0;
                    #0#12 = #6#1;
                    |PINTA #0#12;
               |SINO;
                    |MENAV "0000SUBFAMILIA NO PERMITIDA";
                    |ERROR;
                    |ACABA;
               |FINSI;
          |SINO;
               |MENAV "0000SUBFAMILIA INEXISTENTE";
               |ERROR;
               |ACABA;
          |FINSI;
          |CIERRA #6;
     |FINSI;
     |SI FSalida = 10;
          |ESTADO_CONTROL nBotSubFam, "HIDE";
          |HAZ Consulta_SubFamilia;
          |ESTADO_CONTROL nBotSubFam, "SHOW";
          |ACABA;
     |FINSI;
     |ESTADO_CONTROL nBotSubFam, "HIDE";
|FPRC;
-------------------------------------------------------------------------------
|PROCESO Consulta_SubFamilia;
     |ABRE #6;
     |DDEFECTO #6;
     |CONSULTA_F_CLAVE #4#6, FamMin, FamMax;
     |SI FSalida = 0;
          #0#11 = #6#0;
          #0#12 = #6#1;
          |PINTA #0#11;
          |PINTA #0#12;
     |FINSI;
     |CIERRA #6;
|FPRC;
-------------------------------------------------------------------------------
|PROCESO FamMin;
     #6#2 = #0#9;
|FPRC;
-------------------------------------------------------------------------------
|PROCESO FamMax;
     #6#2 = #0#9;
|FPRC;
-------------------------------------------------------------------------------
|PROCESO ConsCap; |TIPO 0;
     nModo = (FEntrada / 100) ! 0;
     |SI FSalida ! 10;
          |ABRE #7;
          #7#0 = #0#13;
          |LEE 010#7=;
          |SI FSalida = 0;
               |SELECT #3#7;
               #7#0 = #0#13;
               #7#2 = #0#11;
               |LEE 010#7=;
               |SI FSalida = 0;
                    #0#14 = #7#1;
                    |PINTA #0#14;
               |SINO;
                    |MENAV "0000CAPITULO NO PERMITIDO";
                    |ERROR;
                    |ACABA;
               |FINSI;
          |SINO;
               |MENAV "0000CAPITULO INEXISTENTE";
               |ERROR;
               |ACABA;
          |FINSI;
          |CIERRA #7;
     |FINSI;
     |SI FSalida = 10;
          |ABRE #7;
          |DDEFECTO #7;
          |CONSULTA_F_CLAVE #4#7, SubFMin, SubFMax;
          |SI FSalida = 0;
               #0#13 = #7#0;
               #0#14 = #7#1;
               |PINTA #0#13;
               |PINTA #0#14;
          |SINO;
               |ERROR;
               |ACABA;
          |FINSI;
          |CIERRA #7;
     |FINSI;
     |ESTADO_CONTROL nBotCapitulo, "HIDE";
|FPRC;
-------------------------------------------------------------------------------
|PROCESO SubFMin;
     #7#2 = #0#11;
|FPRC;
-------------------------------------------------------------------------------
|PROCESO SubFMax;
     #7#2 = #0#11;
|FPRC;
-------------------------------------------------------------------------------
|PROCESO BorraDescripcion;
     |BORRA 020#2a;
     nHay = 1;
|FPRC;
-------------------------------------------------------------------------------
|DEFBUCLE BorraDescripcion;
     #2#1;
     ;                                          ||Campos Extras.
     #1#0, 1;
     #1#0, 999;
     ;                                          ||b/e Bolqueo-Ordenacion.
     NULL, BorraDescripcion;
|FIN;
-------------------------------------------------------------------------------
|PROCESO BDesc; |TIPO 2;
     nModo = (FEntrada/100)!0;
     |SI nModo = 2;
          |C_M #0#5, 1, "N";
     |SINO;
          |C_M #0#5, 1, "S";
     |FINSI;
     |SI nModo = 3;
          |BUCLE BorrComp;
          |ABRE #1;
          #1#0 = #0#0;
          |LEE 000#1=;
          |SI FSalida = 0;
               |BUCLE BorraDescripcion;
               |SI nHay = 1;
                    |BORRA 020#1a;
               |FINSI;
          |FINSI;
          |CIERRA #1;
     |FINSI;
     PasoPorDes = 0;
|FPRC;
-------------------------------------------------------------------------------
|PROCESO Genera; |TIPO 7;
     |C_M #0#7,  1, "S";
     |C_M #0#9,  1, "S";
     |C_M #0#11, 1, "S";
     |C_M #0#13, 1, "S";

     nModo = (FEntrada/100)!0;
     |SI nModo = 1;
          #0#18 = Usuario % 810; |PINTA #0#18;

          |SI #8#32 ! "N";
               |PUSHV 01012380;
               |CLS;
               #9#9 = "";
               #9#0 = "";
               qAlta = "P";
               |PINPA #0#9;
               |PINDA #0#9;
               |ENDATOS #2#9;
               |SI FSalida = 0;
                    #0#0  = #9#0;            ||Codigo Partida.
                    #0#7  = #9#1;            ||Seccion;
                    #0#8  = #9#2;            ||Nombre Sec.
                    #0#9  = #9#3;            ||Familia.
                    #0#10 = #9#4;            ||Nombre Fam.
                    #0#11 = #9#5;            ||SubFamilia
                    #0#12 = #9#6;            ||Nombre Sub.
                    #0#13 = #9#7;            ||Capitulo.
                    #0#14 = #9#8;            ||Nombre Cap.
                    |C_M #0#7,  1, "N";
                    |C_M #0#9,  1, "N";
                    |C_M #0#11, 1, "N";
                    |C_M #0#13, 1, "N";
               |SINO;
                    |POPV;
                    |ERROR; |ACABA;
               |FINSI;

               |CONFI "0000N¿DESEA BASARSE EN UNA PARTIDA EXISTENTE?";
               |SI FSalida = 0;
                    |DBASE aAlfa;
                    Camino = aAlfa + "def/goartive.mas";
                    |CARGA_DEF Camino, duppart@;

                    Camino = aAlfa + "def/goartdes.mas";
                    |CARGA_DEF Camino, dupparl@;

                    Camino = aAlfa + "def/godeivec.mas";
                    |CARGA_DEF Camino, dupdesc@;

                    Camino = aAlfa + "def/godeivel.mas";
                    |CARGA_DEF Camino, dupdesl@;

                    Camino = aAlfa + "def/goarticu.mas";
                    |CARGA_DEF Camino, goarticu@;
                    |SI  FSalida ] 0;
                         |ABRE #10;
                         |CONSULTA_CLAVE #1#10;
                         |SI FSalida = 0;
                              |HAZ DupliParti;
                              |HAZ DupliDescr;
                         |FINSI;
                         |CIERRA #10;
                    |FINSI;
                    |DESCARGA_DEF #goarticu@;
                    |DESCARGA_DEF #dupdesl@;
                    |DESCARGA_DEF #dupdesc@;
                    |DESCARGA_DEF #dupparl@;
                    |DESCARGA_DEF #duppart@;
               |FINSI;
               |POPV;
               |PINTA #0#0;
               |PINTA #0#7;            ||Seccion;
               |PINTA #0#8;            ||Nombre Sec.
               |PINTA #0#9;            ||Familia.
               |PINTA #0#10;           ||Nombre Fam.
               |PINTA #0#11;           ||SubFamilia
               |PINTA #0#12;           ||Nombre Sub.
               |PINTA #0#13;           ||Capitulo.
               |PINTA #0#14;           ||Nombre Cap.
          |FINSI;
     |FINSI;
|FPRC;
-------------------------------------------------------------------------------
|PROCESO DupliDescr;
     |ABRE #1;
     |ABRE #2;
     |ABRE #12;
     #12#0 = #10#0;
     |LEE 000#12=;
     |SI FSalida = 0;
          |ABRE #13;
          nLinea = 1;
          #13#0 = #12#0;
          #13#1 = nLinea;
          |LEE 000#13=;
          |PARA; |SI FSalida = 0; |HACIENDO;
               #2#0 = #0#0;
               #2#1 = #13#1;
               #2#2 = #13#2;
               |GRABA 020#2n;
               nLinea = nLinea + 1;
               #13#1 = nLinea;
               |LEE 000#13=;
          |SIGUE;
          |CIERRA #13;
          #1#0 = #0#0;
          #1#1 = #12#1;
          #1#2 = #12#2;
          |GRABA 020#1n;
     |FINSI;
     |CIERRA #12;
     |CIERRA #1;
     |CIERRA #2;
|FPRC;
-------------------------------------------------------------------------------
|PROCESO DupliParti;
     enSw = 0;
     |ABRE #goarticu@;
     #goarticu@#0 = #10#0;
     |LEE 000#goarticu@.=;
     |SI FSalida = 0; |Y #goarticu@#200 = #8#116; |Y #8#119 = "S";
          |MENAV "0000PARTIDA GENÉRICA...";
          |SUB_EJECUTA_NP "gopargen";
     |FINSI;

     nPrecio = 0;
     |ABRE #3;
     |ABRE #11;
     #11#0 = #10#0;
     #11#1 = 0;
     |LEE 000#11];
     |PARA; |SI FSalida = 0; |HACIENDO;
          |SI #11#0 ! #10#0; |SAL; |FINSI;
          #3#0 = #0#0;
          |PARA nConta = 1; |SI nConta [ 9; |HACIENDO nConta = nConta + 1;
               #3nConta = #11nConta;
          |SIGUE;
          |SI #8#119 = "S";
               #goarticu@#0 = #3#2;
               |LEE 000#goarticu@.=;
               |SI FSalida = 0; |Y enSw = 1;
                    |SI #goarticu@#200 = #8#117;
                         #3#5 = #11#5 / enDisInte;
                    |FINSI;
                    |SI #goarticu@#200 = #8#118;
                         #3#5 = #11#5 / enDisTens;
                    |FINSI;
               |FINSI;
          |FINSI;
          #3#5 = #3#5 ! #8#115;
          #3#7 = #3#5 * #3#6;
          nPrecio = nPrecio + #3#7;
          |GRABA 020#3n;
          |LEE 000#11s;
     |SIGUE;
     #0#1 = #10#1; #0#2 = #10#2; #0#3 = #10#3; #0#4 = #10#4; #0#5 = #10#5; #0#6 = #10#6; #0#16 = #10#16; #0#15 = #10#15;
     #0#15 = nPrecio;
     |GRABA 020#0c;
     |PINDA #0#0;
     |CIERRA #3;
     |CIERRA #11;
     |CIERRA #goarticu@;
|FPRC;
-------------------------------------------------------------------------------
|PROCESO VerDes; |TIPO 0;
     |SI nModo = 1; |Y PasoPorDes = 0;
          cDesc     = #0#1;
          eaCodigo  = #0#0;
          FSalida = 10;
          |HAZ Ampliadaive;
          PasoPorDes = 1;
     |FINSI;
|FPRC;

|PROCESO ModiCampo;
     |ABRE #8; |LEE 000#8p; |CIERRA #8;
     |SI #8#90 = "S";
          |C_M #0#9, 1, "N";
          |C_M #0#13, 1, "N";
     |SINO;
          |C_M #0#9, 1, "S";
          |C_M #0#13, 1, "S";
     |FINSI;
|FPRC;

|PROCESO DefFam; |TIPO 7;
     |SI #8#90 = "S";
          #0#9 = #0#7; |PINTA #0#9;
          #0#10 = #0#8; |PINTA #0#10;
          |ABRE #5;
          #5#0 = #0#9;
          |LEE 000#5=;
          |SI FSalida ! 0;
               |DDEFECTO #5;
               #5#0 = #0#9;
               #5#1 = #0#10;
               #5#2 = #0#9;
               #5#3 = #0#10;
               |GRABA 020#5n;
          |FINSI;
          |CIERRA #5;
     |FINSI;
|FPRC;

|PROCESO DefCapi; |TIPO 7;
     |SI #8#90 = "S";
          #0#13 = #0#11; |PINTA #0#13;
          #0#14 = #0#12; |PINTA #0#14;
          |ABRE #7;
          #7#0 = #0#13;
          |LEE 000#7=;
          |SI FSalida ! 0;
               |DDEFECTO #7;
               #7#0 = #0#13;
               #7#1 = #0#14;
               #7#2 = #0#13;
               #7#3 = #0#14;
               |GRABA 020#7n;
          |FINSI;
          |CIERRA #7;
     |FINSI;
|FPRC;

|PROCESO GeneraPartida;
     |SI #8#90 = "S"; |Y #8#141 = "O";
          |C_M #9#3, 1, "N";
          |C_M #9#7, 1, "N";
     |FINSI;
     |CLS;
     |PUSHV 0501 2380;
     |DDEFECTO #9;
     |PINPA #0#9;
     |PINDA #0#9;
     |ENDATOS #2#9;
     |SI FSalida = 0;
          eaPartida = #9#0;
          eaSeccion = #9#1;
          eaFamilia = #9#3;
          eaSubFamilia = #9#5;
          eaCapitulo = #9#7;
          #0#0  = #9#0;            ||Codigo Partida.
          #0#7  = #9#1;            ||Seccion;
          #0#8  = #9#2;            ||Nombre Sec.
          #0#9  = #9#3;            ||Familia.
          #0#10 = #9#4;            ||Nombre Fam.
          #0#11 = #9#5;            ||SubFamilia
          #0#12 = #9#6;            ||Nombre Sub.
          #0#13 = #9#7;            ||Capitulo.
          #0#14 = #9#8;            ||Nombre Cap.
          |C_M #0#7,  1, "N";
          |C_M #0#9,  1, "N";
          |C_M #0#11, 1, "N";
          |C_M #0#13, 1, "N";
     |SINO;
          eaPartida = "";
     |FINSI;
     |POPV;

     |SI eaPartida = ""; |ACABA; |FINSI;
     |SI aParam = "goarticu"; |ACABA; |FINSI;


     |CONFI "0000N¿DESEA BASARSE EN UNA PARTIDA EXISTENTE?";
     |SI FSalida = 0;
          |DBASE aAlfa;
          Camino = aAlfa + "def/goartive.mas";
          |CARGA_DEF Camino, duppart@;

          Camino = aAlfa + "def/goartdes.mas";
          |CARGA_DEF Camino, dupparl@;

          Camino = aAlfa + "def/godeivec.mas";
          |CARGA_DEF Camino, dupdesc@;

          Camino = aAlfa + "def/godeivel.mas";
          |CARGA_DEF Camino, dupdesl@;

          Camino = aAlfa + "def/goarticu.mas";
          |CARGA_DEF Camino, goarticu@;
          |SI  FSalida ] 0;
               |ABRE #duppart@;
               |CONSULTA_CLAVE #1#duppart@;
               |SI FSalida = 0;
                    |HAZ DupliParti;
                    |HAZ DupliDescr;
               |FINSI;
               |CIERRA #duppart@;
          |FINSI;
          |DESCARGA_DEF #goarticu@;
          |DESCARGA_DEF #dupdesl@;
          |DESCARGA_DEF #dupdesc@;
          |DESCARGA_DEF #dupparl@;
          |DESCARGA_DEF #duppart@;
     |FINSI;
|FPRC;

|PROCESO Na; |TIPO 0;    || No quitar
|FPRC;

|PROGRAMA;
     aParam = PARAMETRO;

     |ABRE #8; |LEE 000#8p; |CIERRA #8;
     |SI #8#32 = "H";
          aAlfa = "goartihi;" + aParam;
          |SUB_EJECUTA_NP aAlfa;
          |ACABA;
     |SINO;
          |SI eaPartida = "";
               |MANTE #0;
          |SINO;
               |ABRE #0;
               |DDEFECTO #0;
               |QBF eaPartida;
               |SI eaPartida = "";
                    |HAZ GeneraPartida;
                    |SI eaPartida = ""; |ACABA; |FINSI;
                    |SI aParam = "goarticu"; |ACABA; |FINSI;

|| quitado segun Subtiquet nro. 00150.0002 Cliente: 003774
||                    |SI aParam = "ALTA"; |ACABA; |FINSI;
               |FINSI;

               |CLS;
               #0#0 = eaPartida;
               |PINPA #0#0;
               |PINDA #0#0;
               |ENDATOS #1#0;
               |SI FSalida = 0;
                    |GRABA 020#0n;
               |SINO;
                    eaPartida = "";
               |FINSI;
               |CIERRA #0;
          |FINSI;
     |FINSI;
|FPRO;
