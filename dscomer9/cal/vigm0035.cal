|FICHEROS;
     vigm0035 #0; || = agifa077
     agifa019 #2;
     vigm0036 #3; || = vigm0017
     agifa112 #4;
     agifa017 #8;
     agif0041 #11;  || datos empresa
     agifa007 #40;
     agifa142 #41;

     agifa321 #45;  || Parametros de Divisas
     agifa322 #46;  || Lineas de Proveedores/Divisas
     agifa323 #47;  || Proveedores/Divisas
     agifa324 #42;  || Divisas

     agifa067 #67;
     goparame@;
     agifa080 #80;
|FIN;

|VARIABLES;
     &enModoCab = 0;
     &enEsCto = 0;
     &enNumero = 0;
     &enSwAct = 0;
     aSwModiCon = "";
     nSwConstru = 0;

     &eaSerJefe = "";
     &enEvento  = 0;
     &enEnvio   = 0;
     &eaOrigen  = "";
     &eaDestino = "";
     &eaAsunto  = "";

     nModo      = 0;

     nSwFlag        = 0;
     mensa          = "";
     aSerie         = "";
     nNume          = 0;
     aProv          = "";

     &ser_ped       = "";
     &num_ped       = 0;
     nModo212       = 0;
     aDatos         = "";
     aDirEmp        = "";
     nCodEmp        = 0;

   &aSerAntes     = "";
   &nNumAntes     = 0;
   aAlfa          = "";
   aMes           = "";
   nForma         = 0;
   nImpo          = 0;
   nMes           = 0;
   nSwVersion     = 0;

   &enSw          = 0;

  &nDeci_Base     = 0;   || Decimales en Totales de la moneda base
  &nDeci_PreBase  = 0;   || Decimales en el Precio en moneda base
  &nDeci_BaseMx   = 0;   || Decimales en Totales de la moneda base (Maxima precision)
  &nDeci_PreBaseMx= 0;   || Decimales en el Precio en moneda base (Maxima precision)
  &nDeci_Div      = 0;   || Decimales en Totales de la divisa
  &nDeci_PreDiv   = 0;   || Decimales en el Precio en divisa
  &nDeci_TotVis   = 0;   || Decimales en Totales visibles (el de la moneda de trabajo)
  &nDeci_TotNoVis = 0;   || Decimales en Totales no visibles (el de la otra moneda)
  &nDeci_PreVis   = 0;   || Decimales en el Precio visual. (lineas)
  &nDeci_ImpVis   = 0;   || Decimales en el Importe visual. (lineas)
  &nDeci_BaseMxP  = 0;   || Decimales en Totales de la moneda base (Maxima precision)
  &nDeci_PreBasMxP= 0;   || Decimales en el Precio en moneda base (Maxima precision)
  &nDeci_DivP     = 0;   || Decimales en Totales de la divisa
  &nDeci_PreDivP  = 0;   || Decimales en el Precio en divisa
  &nDeci_TotVisP  = 0;   || Decimales en Totales visibles (el de la moneda de trabajo)
  &nDeci_TotNoVisP= 0;   || Decimales en Totales no visibles (el de la otra moneda)
  &nDeci_PreVisP  = 0;   || Decimales en el Precio visual. (lineas)
  &nDeci_ImpVisP  = 0;   || Decimales en el Importe visual. (lineas)
  nfecha    = 0;
  &ac_divisa = "N";   || Vale "S" cuando las divisas en compras estan
                      || activadas y la serie es de divisas
  &pintado = 0;       || Sirve para controlar los pintados de las lineas
  &decim_divisa = 0;  || Decimales de la divisa
  &precio_divisa = 0; || Decimales del precio en divisa
  &decim_base = 0;    || Decimales de la moneda base
  &precio_base = 0;   || Decimales del precio en moneda base
  div_act = 0;        || Divisa actualizada. Me sirve en un PARA para que
                      || no salga hasta que este actualizada (div_act=1)
  &prov_divisa = "";  || Codigo de Proveedor. Se pasa a PROVEED/DIVISAS
  &divisa = "";       || Codigo de Divisa. Se pasa a ACTUAL. de DIVISAS
  nModo077 = 0;
  param = "";
 asto = 0;
 imneto = 0;
 tf = 0;
 con = 0;
 margen = 0;
control = 0;
mensaje01 = "0423Actualizando linea:";
mensaje10 = "0000Este albaran contiene lineas provenientes de Pedidos";
mensaje12 = "0000Este albaran esta FACTURADO";
mensajei  = "2400NDesea imprimir este albaran? ";
canti2 = 0;
pm = 0;
pcm = 0;
pe = 0;
de = 0;
m1 = 0;
m2 = 0;
m3 = 0;
     m4        = 0;
     m5        = 0;
     m6        = 0;
&n_dec = 0;
&n_pre = 0;
     &bl  = "                              ";
     &serv_art = "N";
     &act_ent = "N";
     &sw_modo       = 0;
     puente    = "";
     iva_ant = 0;
     contenido = 0;
     modo = 0;
     impret = 0;
     calpre = 0;
     precio = 0;
     caltot = 0;
     recalpre = 0;
     pvp1   = 0;
     pvp2   = 0;
     pvp3   = 0;
     pvp4   = 0;
     pvp5   = 0;
     pvp6   = 0;
     factordec = 0;
     aConfir = "";
     i = 0;

     &eaCodigo     = "";
     &eaTipoCodigo = "PR";

     nImpDiv = 0;
     nPPort1 = 0;
     nPPort2 = 0;
     &eaSerAlb = "";
     &enNumAlb = 0;
|FIN;

|INCLUYE i_varart;
||INC-LUYE llena_c2;

|PROCESO Tipo8m212;  |TIPO 8;
     |HAZ EsConstru;
     nSwConstru = FSalida;
     |SI nSwConstru = 0;
          |DDEF aAlfa; aAlfa = aAlfa + "goparame";
          |CARGA_DEF aAlfa, goparame@;
          |SI FSalida = 0;
               |ABRE #goparame@;
               |LEE 000#goparame@.p;
               |CIERRA #goparame@;
               aSwModiCon = #goparame@#69;
               |DESCARGA_DEF #goparame@;
          |FINSI;
     |FINSI;
     ||컴컴컴컴컴컴컴컴횺odulo 003026
     |HAZ EsAlbero;
     |SI FSalida = 0;
          |HAZ AlbeAlbCom8;
     |FINSI;
     ||컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
     nSwVersion = 0;
     |ABRE #67;
     |LEE 040#67p;
     |SI FSalida = 0;
         |SI #67#37 = 0;  |Y #67#5 ! 0;
             nSwVersion = 1;
         |FINSI;
     |FINSI;
     |CIERRA #67;

     |SI nSwVersion = 1;  |SUB_EJECUTA_NP "dsz00005";  |FINSI;

     |SI enSw = 1;  |PTEC 807;  |FINSI;
|FPRC;

|PROCESO serie7; |TIPO 7;
     |HAZ ControlUsuAC;
     |SI enErr ! 0; |PTEC 807; |ACABA; |FINSI;
     |HAZ DefSerCom;

     param = PARAMETRO; |QBF param;     || LLamado por agivf015
     |SI param ! "";
          #0#50 = param % 102;
          aAlfa = param % 306;  #0#0 = aAlfa;
     |FINSI;
|FPRC;

|PROCESO serie30; |TIPO 30;
     ||컴컴컴컴컴컴Modulo 002743;
     |HAZ EsGuerola;
     |SI FSalida = 0;
          |HAZ GuerAdjPed;
     |FINSI;
     |HAZ EsConstru;
     |SI FSalida = 0;
          |GRABA 020#0a;
          |CIERRAT #0;
          eaSerAlb = #0#50;
          enNumAlb = #0#0;
          |HAZ AdjAutoExpe;
          |ABRET #0;
          |LEE 101#0=;
     |FINSI;

     |SI param ! "";                    || LLamado por agivf015
          |PTEC 807;
          |PTEC 807;
     |FINSI;
|FPRC;

|CALCULO fecalbc;|TIPO 7;
     modo = (FEntrada / 100) ! 0;
     |SI modo = 2;
          |C_M #0#1 , 1 , "N";    || Fecha Albaran
          |C_M #0#3 , 1 , "N";    || Cod. Proveedor
          |C_M #0#58, 1 , "N";    || Moneda Trabajo
          |C_M #0#56, 1 , "N";    || Divisa
          |C_M #0#57, 1 , "N";    || Cambio
          |C_M #0#59, 1 , "N";    ||
          |C_M #0#60, 1 , "N";    ||
     |SINO;
          |C_M #0#1 , 1 , "S";
          |C_M #0#3 , 1 , "S";
          |C_M #0#58, 1 , "S";    || Moneda Trabajo
          |C_M #0#56, 1 , "S";    || Divisa
          |C_M #0#57, 1 , "S";    || Cambio
          |C_M #0#59, 1 , "S";    ||
          |C_M #0#60, 1 , "S";    ||
     |FINSI;
|FCAL;

|CALCULO cogeconc;|TIPO 0;
control = 0;
|FCAL;

|CALCULO mirar_d;
     |ABRE #41; |LEE 000#41p; |CIERRA #41;
     n_dec = #41#8; n_pre = #41#9; act_ent = #41#34;
     calpre = #41#60;
     aConfir = #41#76;
     serv_art = #41#131;
     |ABRE #agifa007;
     #agifa007#26 = #0#50;
     |LEE 001#agifa007.=;
     |SI FSalida ! 0; |DDEFECTO #agifa007; |FINSI;
     |CIERRA #agifa007;
     |SI #agifa007#30 = "S";
          #0#12 = 0;
          #0#14 = 0;
          |PINTA #0#12;
          |PINTA #0#14;
     |FINSI;
     |HAZ LeeMonBase2;
     |HAZ LeeParamDiv2;
|FCAL;

|CALCULO pedconc;|TIPO 6;
con = (FEntrada / 100) ! 0;
|SI con = 2;|Y #0Campo ! Contenido;
  |SI #0#41 > 0;|O #0#38 = AS;
    |SI #0#41 > 0;
      |MENAV mensaje10;
    |FINSI;
    |SI #0#38 = AS;
      |MENAV mensaje12;
    |FINSI;
  |SINO;
    control = 1;
  |FINSI;
|FINSI;
|FCAL;

|CALCULO dcon;|TIPO 6;
con = FEntrada / 100;
con = con ! 0;
|SI con = 2;|Y #0Campo ! Contenido;
    |SI #0#38 = AS;
       |MENAV mensaje12;
       |ERROR;
    |FINSI;
|FINSI;
|FCAL;

|PROCESO Tipo1_212; |TIPO 1;
     nSwFlag = 0;
|FPRC;

|CALCULO acacpc;|TIPO 2;
     nForma = 0 +. 1;
     con = (FEntrada / 100) ! 0;
     enModoCab = con;
     |SI con = 1; nSwFlag = 0; |FINSI;
     |SI nSwFlag = 1; |ERROR; |ACABA; |FINSI;

     |HAZ ControlUsuAC;
     |SI enErr ! 0;
          nSwFlag = 1;
          |ERROR; |ACABA;
     |FINSI;

     ||컴컴컴컴컴컴컴횺odulo 00con
     |MODULO aAlfa;
     |SI con ! 1; |Y nSwConstru = 0; |Y #0#34 ! "PE"; |Y aAlfa = "agifa212";
          |SI aSwModiCon = "S";
               |MENAV "0000Albaran Asignado a Expediente. No se perimite Modificacion...";
               nSwFlag = 1;
               |ERROR; |ACABA;
          |SINO;
               |MENAV "0000Albaran Asignado a Expediente.";
               |MENAV "0000Si modifica el albaran debera posteriormente modificar la asignacion al expediente";
          |FINSI;
     |FINSI;
     ||컴컴컴컴컴컴컴컴컴컴컴컴컴

     |SI con ! 1; |Y #0#41 = 0;
          |MENAV "0000El Albaran no proviene de pedido...";
          nSwFlag = 1;
          |ERROR; |ACABA;
     |FINSI;

     |SI con = 3;
          eaSer = #0#50;
          enDocu = #0#0;
          efFec = #0#1;
          |HAZ MiraHisC;
          |SI enErrHis ! 0;
               nSwFlag = 1;
               |ERROR; |ACABA;
          |FINSI;
     |FINSI;
     |SI con = 2;|Y #0#38 = AS;
          |MENAV mensaje12;
          nSwFlag = 1;
          |ERROR; |ACABA;
     |FINSI;
     |SI con = 3;|Y #0#38 = AS;
          |SI #0#38 = AS;
               |MENAV mensaje12;
          |FINSI;
          nSwFlag = 1;
          |ERROR; |ACABA;
     |SINO;
          |SI control = 1;
               |HAZ actalc;
          |FINSI;
     |FINSI;

     |SI #0#75 = "S"; |Y nForma = -1;
          |HAZ EsIber21;
          |SI FSalida = 0; |Y #41#164 = "D";
               |MENAV "0000Albaran Exportado...";
               |HAZ ClaveAlbIber;
               |SI FSalida ! 0;
                    nSwFlag = 1;
                    |ERROR; |ACABA;
               |FINSI;
          |SINO;
               |MENAV "0000Albaran Exportado, no se puede Modificar...";
               nSwFlag = 1;
               |ERROR; |ACABA;
          |FINSI;
     |FINSI;
     ||컴컴컴컴컴컴컴컴컴컴컴 Modulo 00con
     |SI nSwConstru = 0; |Y con = 1;
          aAlfa = #0#34; |QBF aAlfa;
          |SI aAlfa = ""; #0#34 = "PE"; |FINSI;
     |FINSI;
     ||컴컴컴컴컴컴컴컴컴컴컴
     |SI con = 3;
          |HAZ EsAlbero;
          |SI FSalida = 0;
               |HAZ AlbeAlbBorra;
          |FINSI;
     |FINSI;
     ||컴컴컴컴컴컴컴컴컴컴컴

     #0#75 = "N"; || EXPORTADO

     pcm = 0;
     pm = 0;
     pe = 0;
     de = 0;
     |ABRE #2;
     |ABRE #8;
     |ABRE #4;
     enSwAct = -2;
     |ABRE #80;
     |BUCLELIN 0pcmlinea#0;
     |CIERRA #80;
     enSwAct = 0;
     |CIERRA #4;
     |CIERRA #8;
     |CIERRA #2;
|FCAL;

|CALCULO acttlc;|TIPO 0;
     |SI caltot = 0; ||Si no viene del proceso caltotal
         |PINTA 445,#3#1;
         #3#15 = #0#3;
         |GRABA 020#3a;
     |SINO;
         |SI recalpre = 1;
             |HAZ TotalLinVig80;
             |GRABA 001#3a; ||Grabo lineas
         |FINSI;
     |FINSI;

     |HAZ CalCabAgifa077;
|FCAL;

|DEFBUCLE agifa080;
     #3#1;
     ;
     #0#50, #0#0, 001;
     #0#50, #0#0, 999;
     ;
     NULL, acttlc;
|FIN;

|CALCULO actalc;|TIPO 0;
     |MENSA mensaje01;

     |HAZ LimCabAgifa077;
     |BUCLE agifa080;

     |SI caltot = 0;   |BLIN 4;  |FINSI;
|FCAL;

|CALCULO siimprec;|TIPO 3;
     ||컴컴컴컴컴컴컴컴컴컴Modulo 000323
     |HAZ EsTecatel;
     |SI FSalida = 0;
          eaSer = #0#50;
          enDocu = #0#0;
          eaCodigo = #0#3;
          eaTipo = "AI";
          |HAZ TecaTransAdjC;
     |FINSI;
     ||컴컴컴컴컴컴컴컴컴컴컴컴컴

     aSerAntes  = "";  || Inicializa variables anteriores
     nNumAntes  = 0;   || de Adj.

     |CONFI mensajei;
     |SI 0 = FSalida;
          |HAZ imprealc;
     |FINSI;

     ||컴컴컴컴컴컴컴컴컴횺odulo Contruccion
     |SI nSwConstru = 0;
          nModo = (FEntrada / 100) ! 0;
          |SI nModo = 1; |O nModo = 2;
               |GRABA 020#0a;
          |FINSI;
          |DBASS aAlfa;
          eaFicImpre= aAlfa + "spool/";
          aAlfa = Usuario;
          eaFicImpre = eaFicImpre + aAlfa;
          ser_ped = #0#50;
          num_ped = #0#0;
          |CIERRAT #0;
          aAlfa = "agifa016;" + eaFicImpre;
          |SUB_EJECUTA_NP aAlfa;
          |ABRET #0;
          #0#50 = ser_ped;
          #0#0  = num_ped;
          |LEE 101#0=;

          eaSerJefe = #0#50;
          eaPrograma = "agifa212"  + #0#50 + #0#0;
          |SI nModo = 1;
               enEvento = 3; eaTipo = "03";
               eaAsunto = "Alta albaran nro. '" + #0#50 + "/" + (("000000" + #0#0) % -106) + "'";
               eaOrigen = Usuario % 0810;
               |HAZ EnviaEvento;
          |FINSI;
          |SI nModo = 2;
               enEvento = 6; eaTipo = "06";
               eaAsunto = "Modificacion albaran nro. '" + #0#50 + "/" + (("000000" + #0#0) % -106) + "'";
               eaOrigen = Usuario % 0810;
               |HAZ EnviaEvento;
          |FINSI;
     |FINSI;
|FCAL;

|PROCESO T35_4; |TIPO 4;
     |SELECT #1#0;
     |LEE 000#0=;
     |SI FSalida ! 0;
          |MENAV "0000Por favor, consulte el registro correcto antes de imprimir.";
          |ACABA;
     |SINO;
          |PINDA #0#0;
          FEntrada = 402;
     |FINSI;
     |HAZ imprealc;
|FPRC;

|CALCULO imprealc;
     modo = (FEntrada / 100) ! 0;
     |SI modo = 1; |O modo = 2;
          |GRABA 021#0a;
          |LIBERA #0;
     |FINSI;
     ser_ped = #0#50;
     num_ped = #0#0;
     |CIERRAT #0;

     |SUB_EJECUTA_NP "agifa016";

     |ABRET #0;
     #0#50 = ser_ped;
     #0#0  = num_ped;
     |SI modo = 4;
          |LEE 001#0=;
     |SINO;
          |LEE 101#0=;
     |FINSI;
|FCAL;

|CALCULO totcomp;|TIPO 14;
|SI #0#41 = 0;
     |PINTA 0577,"AC";
|SINO;
     |PINTA 0577,"PC";
|FINSI;
|FCAL;

|PROCESO pcmlinea;
     #80#27 = #3#27;
     #80#0 = #3#0;
     #80#1 = #3#1;
     |LEE 020#80=;
     ||컴컴컴컴컴컴 Doble/Multi
     enPrecioB  = precio_base;
     enDeciB    = nDeci_PreBase;
     enForma = nForma;
     eaDocu = "agifa212";
     |HAZ StockEntra;
     ||컴컴컴컴컴컴컴컴컴컴컴컴
     ||-------------------------------Prov
     nImpo = (#3#9 < #0#5) < #0#32;
     |SI #41#114 = "S";
          nImpo = nImpo < #0#7;
     |FINSI;
     #4#0 = #0#3;
     |LEE 101#4=;
     |SI FSalida = 0;
         aMes = #0#1 % A402;
         nMes = aMes + 26;
         |SI #0#42 = "N";
             #4nMes = #4nMes + (nImpo * nForma);
             #4#39 = #4#39 +. (nImpo * nForma);
         |SINO;
             #4nMes = #4nMes -. (nImpo * nForma);
             #4#39 = #4#39 -. (nImpo * nForma);
         |FINSI;
         |GRABA 020#4a;
         |LIBERA #4;
     |FINSI;

     |SI #0#42 = "N";
          enImpPrvCom = nImpo * nForma;
     |SINO;
          enImpPrvCom = -nImpo * nForma;
     |FINSI;
     efFecha = #0#1;
     eaProve = #0#3;
     |HAZ AcumulaPrv;
|FPRC;

|PROCESO pr_obs; |TIPO 0;
     nModo212 = (FEntrada / 100) ! 0;
     |SI FSalida = 11; |O #41#102 = "S"; |O #41#102 = "X";
          eaCodigo = #0#3;
          eaFuerza = "N";
          |SI FSalida = 11; eaFuerza = "S"; |FINSI;
          |SUB_EJECUTA_NP "dsz99994";
          |SI #41#102 = "N"; |Y nModo212 ! 2; |ERROR;  |FINSI;
     |FINSI;

     |SI #agifa142#15 = "S";
          puente = #agifa112#26;     |QBF puente;
          |SI puente ! "";
               |PUSHV 1008 1370;
               |SI #agifa142#21 = "S"; |AVISO; |FINSI;
               |BLANCO 1009 1369;
               |CUADRO 1008 1370;
               |ATRI R;
               |PINTA 1110 , #agifa112#26;
               |ATRI 0;
               |PAUSA;
               |POPV;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO ant_fecha; |TIPO 7;
     sw_modo = (FEntrada / 100) ! 0;
|FPRC;

|PROCESO LeeMonBase2; ||Lee los parametros de la moneda base
     |ABRE #agifa321;
     |ABRE #agifa324;
     |LEE 010#agifa321.p;   ||Leo parametros de divisa
     #0#61 = #agifa321#9;  ||Cojo la Moneda Base de parametros
     #agifa324#0 = #agifa321#9;
     |LEE 010#agifa324.=;         ||Leo la ficha de la moneda base en Divisas
     #0#62 = #agifa324#3;  ||Asigno el cambio de la moneda base con el euro
     decim_base = #agifa324#7;    ||Asigno los decimales de la moneda base
     precio_base = #agifa324#9;   ||Asigno los decimales del precio en la mon. base
     |CIERRA #agifa324;
     |CIERRA #agifa321;
|FPRC;

|PROCESO Param_Divisas2; |TIPO 0;
     pintado = 0;  ||Controla pintados de las lineas

     |ABRE #agifa324;
     #agifa324#0 = #0#56;
     |LEE 001#agifa324.=;   || Leo la divisa
     |CIERRA #agifa324;

     decim_divisa = #agifa324#7;  || Decimales de la divisa
     precio_divisa = #agifa324#9;  || Decimales en el precio de la divisa
     nDeci_Div = decim_divisa;   || Decimales de la divisa
     nDeci_PreDiv = precio_divisa;  || Decimales en el precio de la divisa
     nDeci_DivP = decim_divisa;   || Decimales de la divisa
     nDeci_PreDivP = precio_divisa;  || Decimales en el precio de la divisa
     |SI #0#58 = "B";   ||Si trabajo con la moneda base ...
          nDeci_Base      = decim_base;
          nDeci_PreBase   = precio_base;
          nDeci_PreVis    = precio_divisa;
          nDeci_ImpVis    = decim_divisa;
          nDeci_BaseMx    = decim_base;
          nDeci_PreBaseMx = precio_base;
          nDeci_TotVis    = decim_base;
          nDeci_TotNoVis  = decim_divisa;
          nDeci_PreVisP   = precio_divisa;
          nDeci_ImpVisP   = decim_divisa;
          nDeci_BaseMxP   = decim_base;
          nDeci_PreBasMxP = precio_base;
          nDeci_TotVisP   = decim_base;
          nDeci_TotNoVisP = decim_divisa;
     |SINO;     ||Si trabajo con la divisa ...
          nDeci_Base     = decim_base; || Max. precision en los decimales de la moneda base
          nDeci_PreBase  = precio_base;  || Max. precision en los dec. del precio de la moneda base
          nDeci_PreVis   = precio_base;
          nDeci_ImpVis   = decim_base;
         |SI #0#57 = 1;
              nDeci_BaseMx    = decim_base;    || sin triangulacion
              nDeci_PreBaseMx = precio_base;
         |SINO;
              nDeci_BaseMx    = #agifa321#10;  || con triangulacion
              nDeci_PreBaseMx = #agifa321#11;
         |FINSI;
          nDeci_TotVis   = decim_divisa;
          nDeci_TotNoVis = decim_base;
          nDeci_PreVisP  = precio_base;
          nDeci_ImpVisP  = decim_base;
         |SI #0#57 = 1;
              nDeci_BaseMxP   = decim_base;    || sin triangulacion
              nDeci_PreBasMxP = precio_base;
         |SINO;
              nDeci_BaseMxP   = #agifa321#10;  || con triangulacion
              nDeci_PreBasMxP = #agifa321#11;
         |FINSI;
          nDeci_TotVisP  = decim_divisa;
          nDeci_TotNoVisP= decim_base;
     |FINSI;
     |HAZ Deci_IVAC;
|FPRC;

|PROCESO LeeParamDiv2; |TIPO 0;
     |ABRE #agifa321;
     |LEE 001#agifa321.p;    || Leo parametros de divisas
     |ABRE #agifa007;
     #agifa007#26 = #0#50;
     |LEE 001#agifa007.=;        || Leo la serie
     |SI #0#58 = "D";      || Si la mon. de trabajo es la divisa...
       || ...escondo los campos de la moneda base :
       |CAMPO_POSICION #3#6 , 1 , 1009;
       |CAMPO_POSICION #3#9 , 1 ,1026;
       |CAMPO_VISUAL #3#6 , 1 , "N";
       |CAMPO_VISUAL #3#9 , 1 , "N";
       |CAMPO_LINEAL #3#6 , 1 , "N";
       |C_M #3#6 , 1 , "N";
       || ...y pongo en pantalla los de divisa
       |CAMPO_POSICION #3#30 , 1 , 1556;
       |CAMPO_POSICION #3#31 , 1 ,1350;
       |CAMPO_VISUAL #3#30 , 1 , "S";
       |CAMPO_VISUAL #3#31 , 1 , "S";
       |C_M #3#30 , 1 , "S";
       |SI #agifa007#39 = "S"; |Y #agifa321#2 = "S"; ||Si la serie es de ...
          ac_divisa = "S";  || ... divisas, y en parametros estan activadas ...
          || ... pongo como modificables los campos relacionados con divisas
          |C_M #0#56 , 1 , "S";
          |C_M #0#57 , 1 , "S";
          |C_M #0#58 , 1 , "S";
          |C_M #0#59 , 1 , "S";
          |C_M #0#60 , 1 , "S";
          |SI #agifa321#4 = "S";  || Si la visualizacion de parametros esta activada
            || ... pongo los campos visual. de lineas como visualizables
            |CAMPO_VISUAL #3#32, 1 , "S";
            |CAMPO_VISUAL #3#33 , 1 , "S";
          |SINO;
            || ... los pongo como no visualizables
            |CAMPO_VISUAL #3#32 , 1 , "N";
            |CAMPO_VISUAL #3#33 , 1 , "N";
          |FINSI;
       |SINO; || Si la serie no es de divisas, o no estan activados parametros
          ac_divisa = "N";
          #0#58 = "B";  || La moneda de trabajo es por fuerza la moneda base
          |PINTA #0#58;
          || Pongo como no modificables los campos relacionados con divisas
          |C_M #0#58 , 1 , "N";
          |C_M #0#59 , 1 , "N";
          |C_M #0#60 , 1 , "N";
          |C_M #0#56 , 1 , "N";
          |C_M #0#57 , 1 , "N";
          || Pongo como no visualizables los campos visual. de lineas
          |CAMPO_VISUAL #3#32, 1 , "N";
          |CAMPO_VISUAL #3#33, 1 , "N";
       |FINSI;
     |SINO;  || Si la moneda de trabajo es la moneda base
       || ...escondo los campos en divisa :
       |CAMPO_POSICION #3#30 , 1 , 1009;
       |CAMPO_POSICION #3#31 , 1 ,1026;
       |CAMPO_VISUAL #3#30 , 1 , "N";
       |CAMPO_VISUAL #3#31 , 1 , "N";
       |C_M #3#30 , 1 , "N";
       || ...coloco los campos en moneda base:
       |CAMPO_POSICION #3#6 , 1 , 1556;
       |CAMPO_POSICION #3#9 , 1 ,1350;
       |CAMPO_VISUAL #3#6 , 1 , "S";
       |CAMPO_VISUAL #3#9 , 1 , "S";
       |C_M #3#6 , 1 , "S";
       |CAMPO_LINEAL #3#6 , 1 , "S";
       |SI #agifa007#39 = "S"; |Y #agifa321#2 = "S"; || Si la serie es de divisas...
          ac_divisa = "S"; || ... y divisas estan activadas en parametros
          || Pongo como modificables los campos relacionados con divisas
          |C_M #0#56 , 1 , "S";
          |C_M #0#57 , 1 , "S";
          |C_M #0#58 , 1 , "S";
          |C_M #0#59 , 1 , "S";
          |C_M #0#60 , 1 , "S";
          |SI #agifa321#4 = "S";  || Si la visualizacion esta activada en parametros ...
            || Pongo como visualizables los campos visual. de las lineas
            |CAMPO_VISUAL #3#32, 1 , "S";
            |CAMPO_VISUAL #3#33 , 1 , "S";
          |SINO; || Los pongo como no visualizables
            |CAMPO_VISUAL #3#32 , 1 , "N";
            |CAMPO_VISUAL #3#33 , 1 , "N";
          |FINSI;
       |SINO;  || Si la serie no es de divisas o no estan activados los parametros
          ac_divisa = "N";
          #0#58 = "B";  || La moneda de trabajo es por fuerza la moneda base
          || Pongo como no modificables los campos relacionados con divisas
          |C_M #0#58 , 1 , "N";
          |C_M #0#59 , 1 , "N";
          |C_M #0#60 , 1 , "N";
          |C_M #0#56 , 1 , "N";
          |C_M #0#57 , 1 , "N";
          || Pongo como no visualizables los campos visual. de lineas
          |CAMPO_VISUAL #3#32, 1 , "N";
          |CAMPO_VISUAL #3#33, 1 , "N";
       |FINSI;
     |FINSI;
     |CIERRA #agifa007;
|CIERRA #agifa321;
|HAZ Param_Divisas2;
|FPRC;

|PROCESO CambioDivisa2; |TIPO 0;
     nModo077 = (FEntrada / 100) ! 0;
     |SI #0Campo ! Contenido; |Y nModo077 = 1;   ||Si cambia el valor o es alta
          |SI Campo = 3; || Si llamo al proceso desde el cod. de proveedor ...
               |LEE 001#agifa112.=;
               #0#56 = #agifa112#81;  || Asigno la divisa por defecto del proveedor
               #0#58 = #agifa112#82;  || Asigno la moneda de trabajo del proveedor
               |PINTA #0#58;
          |FINSI;
          #agifa007#26 = #0#50;
          |LEE 001#agifa007.=;        || Leo la serie
          |SI #agifa007#39 = "S"; |Y #agifa321#2 = "S";
               |SI #agifa112#80 = "S"; || Si el proveedor es de divisa pactada
                    |ABRE #agifa323;
                    |ABRE #agifa322;
                    #agifa322#0 = #0#3;
                    #agifa322#2 = #0#56;
                    #agifa322#7 = "N";
                    |SELECT #2#agifa322;  || Con esta clave ...
                    |LEE 011#agifa322.=;  || ... busco periodos no finalizados de esa divisa
                    |SI FSalida = 0;  || Si existe un periodo no finalizado
                         |PARA div_act = 0; |SI div_act = 0; |HACIENDO;
                              #0#56 = #agifa322#2;        || Cargo la divisa ...
                              #0#57 = #agifa322#3;        || ...y el cambio
                              |LEE 001#agifa322.=;   || Leo las lineas de Proveed/Divisas
                              nfecha = #0#1 - #agifa322#6;  || Resto la fecha de fin de periodo a la actual
                              |SI nfecha > 0;  || Si la fecha actual supera a la del periodo asignado ...
                                   |MENAV "0000ATENCION deberia Actualizar primero la divisa en Proveedores/Divisas";
                                   prov_divisa = #0#3;  || Le envio el cod. de proveedor a "agifa323"
                                   |SUB_EJECUTA_NP "agifa323.tab";  || Ejecuto Proveedores/Divisas
                                   |ERROR;
                              |SINO;
                                   div_act = 1;  || Para salir del PARA
                              |FINSI;
                         |SIGUE;
                    |SINO;  || Si no existe un periodo no finalizado para esa divisa
                         |MENAV "0000No existe un periodo activo para la divisa en Proveedores / Divisas";
                         prov_divisa = #0#3;  || Le paso a "agifa323" el proveedor
                         |SUB_EJECUTA_NP "agifa323.tab";  || Ejecuto Proveedores / Divisas
                         |ERROR;
                    |FINSI;
                    |SELECT #1#agifa322;
                    |CIERRA #agifa322;
                    |CIERRA #agifa323;
               |SINO; || Si el Proveedor no es de Divisa pactada ...
                    |ABRE #agifa324; || ...miro directamente en divisas
                    #agifa324#0 = #0#56;
                    |LEE 001#agifa324.=;
                    |SI FSalida = 0; ||Si existe la divisa...
                         #0#57 = #agifa324#3; || la recojo
                         |SI #agifa324#6 ! -1; || Si los dias de margen de actualizacion no
                                               || son = -1 (indefinidos)
                              |PARA div_act = 0; |SI div_act = 0; |HACIENDO;
                                   #0#56 = #agifa324#0;        ||Cargo el valor de la divisa
                                   #0#57 = #agifa324#3;        ||Cargo el valor del cambio
                                   |LEE 001#agifa324.=;  || Leo la divisa
                                   nfecha = #0#1 - #agifa324#4;
                                   |SI nfecha > #agifa324#6; |Y #agifa324#6 ! -1;  || Si la fecha actual
                                        |MENAV "0000ATENCION deberia Actualizar primero la divisa";
                                        divisa = #0#56;
                                        |SUB_EJECUTA_NP "agifa326.tab";
                                        |ERROR;
                                   |SINO;
                                        div_act = 1;
                                   |FINSI;
                              |SIGUE;
                         |FINSI;
                    |FINSI;
                    |CIERRA #agifa324;
               |FINSI;
          |SINO;
               #0#56 = #0#61;
               #0#57 = #0#62;
          |FINSI;
          |PINTA #0#56;
          |PINTA #0#57;
          |SI Campo = 3; || Si llamo al proceso desde el cod. de proveedor ...
               |HAZ LeeParamDiv2;
          |SINO;
               |HAZ Param_Divisas2;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO convportes2; |TIPO 6;  || Convierte los portes a la mon. base
|SI #0#58 = "B";  || Si la moneda de trabajo es la moneda base ...
  #0#11 = #0#69;  || ... basta con asignarlo
|SINO;    || Si la moneda de trabajo es la divisa
  #0#11 = #0#69 / #0#57 * #0#62;   || ... lo convierto
|FINSI;
|HAZ dcon;
|FPRC;

|PROCESO convvarios2; |TIPO 6;  || Convierte los varios a la mon. base
|SI #0#58 = "B";  || Si la moneda de trabajo es la moneda base ...
  #0#13 = #0#70;  || ... basta con asignarlo
|SINO;    || Si la moneda de trabajo es la divisa ...
  #0#13 = #0#70 / #0#57 * #0#62;  || ... lo convierto
|FINSI;
|HAZ dcon;
|FPRC;


|PROCESO LeeAlb2; |TIPO 13;  || Sirve para cargar los valores cada vez que
                            || cambio de ficha
     pintado = 0;   || controla pintado de las lineas
     |HAZ LeeMonBase2;
     |HAZ LeeParamDiv2;
     ||TODO CCC
     |ABRE #41; |LEE 000#41p; |CIERRA #41;
     |SI #41#148 = "N";
          |C_V #0#73, 1, "N";
          |C_M #0#73, 1, "N";
     |SINO;
          |C_V #0#73, 1, "S";
          |C_M #0#73, 1, "S";
          |PINTA 1503, "Portes Linea Facturables:";
     |FINSI;
     |SI nSwConstru = 0;
          |ATRI I;
          |PINTA 1971, "Asig:";
          |ATRI 0;
          |C_V #0#34, 1, "S";
          |PINTA #0#34;
     |FINSI;
     |HAZ EsAlbero;
     |SI FSalida = 0;
          |HAZ AlbeAlbPinta;
     |FINSI;
|FPRC;

|CALCULO cambfecha2;
     |SI #0#59 ! "O";
       #0#60 = "        ";
       |PINTA #0#60;
       |C_M #0#60,1,"N";
       |C_V #0#60,1,"N";
     |SINO;
       |C_M #0#60,1,"S";
       |C_V #0#60,1,"S";
     |FINSI;
     |HAZ EsAlbero;
     |SI FSalida = 0;
          |HAZ AlbeAlbEntra;
     |FINSI;
|FCAL;

|CALCULO modomod2; |TIPO 6;
  |SI nModo077 = 2; |Y #0Campo ! Contenido;
    |MENAV "0000No se puede modificar el campo.";
    #0Campo = Contenido;
    |PINTA #0Campo;
    |ERROR;
  |FINSI;
  |SI #0Campo ! Contenido; |Y Campo = 58;
    |HAZ LeeParamDiv2;
  |FINSI;
|FCAL;

|PROCESO caltotala; |TIPO 0; ||Proceso especial para casos determinados como
                            ||modificar los campos de cabecera en dtos, cambio divisas etc
                            ||Recalcula total albaran, descuentos y ivas de lineas
                            ||En el caso de cambiar el cambio de divisas recalcula tambien precio... de lineas

     |SI #0Campo ! Contenido;||Solo si cambia
          caltot = 1;
          |SI Campo = 57; ||Si viene del cambio divisa
              recalpre = 1; ||Recalcular tambien precios linea
          |FINSI;
          |HAZ convportes2;
          |HAZ convvarios2;
          |HAZ actalc;

          |PINTA #0#66;
          caltot = 0;
          recalpre = 0;
     |FINSI;
|FPRC;

|PROCESO Fecha212; |TIPO 0;
|FPRC;

|PROCESO DatosEmpresa2;   |TIPO 7;
     nModo212 = (FEntrada / 100) ! 0;
     |SI nModo212 ! 1; |ACABA; |FINSI;
     |DFICE aDirEmp;            || tomo el directorio de la empresa
     |DFICB aDatos;             || tomo el directorio de los datos
     aDirEmp = aDirEmp % -205;  || me quedo con los 5 digitos de la empresa
     nCodEmp = aDirEmp;         || y los paso a una variable numerica
     |PATH_DAT #11 aDatos;      || pongo el path al fichero agifa041
     |ABRE #11;                 || abre el fichero agifa041
     #11#0 = nCodEmp;
     |LEE 020#11=;              || y busco la empresa en la que estoy
     |SI FSalida = 0;
          #0#29 = #11#1;        || nombre de la empresa
          #0#30 = #11#2;        || direccion de la empresa
          #0#31 = #11#3;        || poblacion de la empresa
          #0#33 = #11#4;        || provincia de la empresa
          |PINTA #0#29;
          |PINTA #0#30;
          |PINTA #0#31;
          |PINTA #0#33;
     |FINSI;
     |CIERRA #11;               || cierro el fichero agifa041
|FPRC;

|PROCESO SuAlbaran; |TIPO 0;
     aAlfa = #0#54; |QBF aAlfa;
     |SI aAlfa = ""; |ACABA; |FINSI;

     aAlfa = #0#54;
     aSerie = #0#50;
     aProv = #0#3;
     nNume = #0#0;
     mensa = "";
     |SALVA_FICHA #0;
     |SELECT #4#0;
     |LEE 000#0];
     |PARA; |SI FSalida = 0; |Y #0#3 = aProv; |Y #0#54 = aAlfa; |HACIENDO;
          |SI #0#50 ! aSerie; |O nNume ! #0#0;
               aAlfa = ("000000" + #0#0) % -106;
               mensa = "0000Codigo existente en Albaran:" + #0#50 + "/" + aAlfa;
               |SAL;
          |FINSI;
          |LEE 000#0s;
     |SIGUE;
     |SELECT #1#0;
     |REPON_FICHA #0;
     |SI mensa ! "";
          |MENAV mensa;
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO Tipo212_9; |TIPO 9;
     |HAZ EsAlbero;
     |SI FSalida = 0;
          |HAZ AlbeAlbCom9;
     |FINSI;
|FPRC;

|PROCESO Pago212_0; |TIPO 0;
     nModo = (FEntrada / 100) ! 0;
     |HAZ EsAlbero;
     |SI FSalida = 0; |Y nModo = 1;
          |HAZ AlbeAlbFec;
          |HAZ AlbeAlbPinta;
     |FINSI;
     |HAZ EsConstru;
     |SI FSalida = 0;
          ||컴컴컴컴컴컴컴횮ontrol Cierre
          efFecha = #0#1;
          |HAZ EstaCerrado;
          |SI FSalida = 0;
               |ERROR; |ACABA;
          |FINSI;
          ||컴컴컴컴컴컴컴컴컴컴컴컴컴컴
     |FINSI;
|FPRC;

|PROCESO IVA035_66; |TIPO 66;
     enTiva = #0Campo;
     efFiva = #0#1;
     |HAZ ConsuIVA;
     |SI eaDiva = "";
          |ERROR;
     |SINO;
          #0Campo = enTiva; |PINTA #0Campo;
     |FINSI;
|FPRC;

|PROCESO IVA035_0; |TIPO 0;
     enTiva = #0Campo;
     efFiva = #0#1;
     |HAZ SacaIVA;
     |SI eaDiva = "";
          |MENAV "0000No existe tipo iva para esta fecha...";
          |ERROR; |ACABA;
     |SINO;
          |HAZ caltotala;
     |FINSI;
|FPRC;
