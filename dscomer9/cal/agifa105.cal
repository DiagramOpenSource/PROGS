|FICHEROS;
     agifa105 #0;
     agifa104 #1;
     agifa073 #2;
     agifa024 #3;
     agifa038 #13;
     agifa048 #15;
     agifa049 #16;
     agifa019 #19;
     agifa007 #40;
     agifa142 #41;
     dsm00046 #46;
     dsm00053 #53;
     agifa010 #55;
     &marhu355@ #100;
     :/agitool/def/dsfax001 #101;
     dsm00109 #109;
     dsm00010 #910;

     refere1@ #201;
     refere2@ #202;
     refere3@;
     tagm0056@;
     tmp00000 #300; || Tmp Ped. que se imprimen
     dsm00156 #156;
     dsm00225 #225;
     agifa073@;
|FIN;

|INCLUYE i_varart;
|INCLUYE i_semana;

|VARIABLES;
     nSwEvaristo = 0;
     &eaObs1 = "";
     &eaObs2 = "";
     &eaObs3 = "";
     nSwSanchez = 0;
     nSalida    = 0;
     nCampos    = 0;
     i          = 0;

     &eaObsOrts = "";
     aArti = "";
     &enGraba300 = 0;
     &Tempo = "";
     aTmp300 = "";
     nSwCoimasa = 0;
     aEmp = "";
     nEmpresa = 0;
     &eaBloqueo = "";
     &enSwDes = 0;
     &enLin1 = 0;
     &enLin2 = 0;
     nSwOrts = 0;
     nNum = 0;
     nLin1 = 0;
     nLin2 = 0;
     &enOrtDesCombi = 1;
     &eaImpElige = "";
     &PRMNT_Imp = "";
     &eaArtIdioma = "";
     &eaCodArtIdioma = "";

     &enImpPorte = 0;
     &enIvaPorte = 0;
     nSwFeldo = 0;
     nSwSematel = 0;
     &eaIMEI      = "";
     &eaABO       = "";
     &eaICC       = "";
     &enSwIMEI    = 0;
     nSwTecatel = 0;
     nSwTagloma = 0;
     nSwAparecida = 0;
     &enSemana = 0;
     &enCliArt = 0;
     nPrecio = 0;
     nPrecioD = 0;
     nTotal = 0;
     nTotalD = 0;
     aEsPostFormaso = "N";
     &eaFicTmpCab = "";
     &eaFicTmpLin = "";
     nImpreso = 0;
   {1}aContiene = "";
   {1}aLocal = "";
     aFic = "";
     aAdjunto = "";
     aBaseLocal = "";
     aVersion1 = "";
     aVersion2 = "";
     aSalida = "";
     aFrom = "";
     aTo = "";
     aAsunto = "";
     aMensaje = "";
     aEmail = "";
     aNomFic = "";
     aBase = "";
     aPathEnvios = "";
     aOrigen = "";
     aDestino = "";
     aIPRemoto = "";
     aAlfa = "";
     aNumFax = "";
     aNumMail = "";
     aInforme = "";
     &enSwAnna = 0;
     nSwFax = 0;
     nSwMail = 0;
     &eaImpresora = "";
     &eaDefImpre = "";
     &enPideImpre = 0;
     aDRei        = "";
     aHRei        = "";
     &enSwMarhu   = 0;

     &aMoneda     = "";
     &aDivisa     = "";
     &enEscan     = 0;
     &enImpo      = 0;
     &enSwImpre   = 0;
     aParam      = "";
     &enCodig     = 0;
     &eaNArticulo = "";
     &enPantalla  = 0;
     &efFEntrega  = @;
     aPath = "";
     nSwPasa      = 0;

     indtope = 0;
     ind = 0;
     n_copia  = 0;
     informe   = "";
     &informe1 = "";
     &cant = 0;
     &tolin = 0;
     &iva1 = 0;
     &iva2 = 0;
     &iva3 = 0;
     &iva4 = 0;
     &iva5 = 0;
     &re1 = 0;
     &re2 = 0;
     &re3 = 0;
     &re4 = 0;
     &re5 = 0;
     &descrip="";
     &linea=0;
     vacio="";
     &bl  = "                              ";
     &ser_ped  = "";
     &num_ped  = 0;
     &unid     = 0;
     &codigo   = "";
     &descri   = "";
     &dto      = 0;
     &precio   = 0;
     &imptot   = 0;
     &tiva     = 0;

     &enSwLin  = 0;
     &eaDoc = "";
     nSwPackList = 0;
|FIN;

|PROCESO ImpEvaristo;
     |DDEF aAlfa;
     aAlfa = aAlfa + "evarm004";
     |CARGA_DEF aAlfa, refere1@;
     |SI FSalida = 0;
          |ABRE #refere1@;
          #refere1@#30 = #2#28;
          #refere1@#31 = #2#0;
          #refere1@#32 = #2#1;
          |LEE 000#refere1@.=;
          |SI FSalida = 0;
               eaObs1 = #refere1@#0; |QBF eaObs1;
               eaObs2 = #refere1@#1; |QBF eaObs2;
               eaObs3 = #refere1@#2; |QBF eaObs3;
               |SI eaObs1 ! ""; |O eaObs2 ! ""; |O eaObs3 ! "";
                    #2#5 = 0;
                    |PRINT;
               |FINSI;
               eaObs1 = ""; eaObs2 = ""; eaObs3 = "";
          |FINSI;
          |CIERRA #refere1@;
          |DESCARGA_DEF #refere1@;
     |FINSI;
|FPRC;

컴컴컴컴컴컴컴컴컴컴컴컴Modulo 002736컴컴컴컴컴컴컴컴컴컴컴컴
|PROCESO Agrupador;
     nSalida = 1;
     |LEE 000#2p;
     |PARA; |SI FSalida = 0; |HACIENDO;
          |SI #2#2 = #agifa073@#2; |Y #2#50 = #agifa073@#50; |Y #agifa073@#50!0;
               nSalida = 0; |SAL;
          |FINSI;
          |LEE 000#2s;
     |SIGUE;
     |SI nSalida = 0;
          #2#6 = #2#6 + #agifa073@#6;   ||Precio
          #2#7 = #2#7 + #agifa073@#7;   ||Impo
          #2#9 = #2#9 + #agifa073@#9;   ||Total
          #2#38 = #2#38 + #agifa073@#38;||Precio Div
          #2#39 = #2#39 + #agifa073@#39;||Total Div
          |GRABA 020#2a;
     |SINO;
          aAlfa = "|NC"; aAlfa = #2#aAlfa#;
          nCampos = aAlfa; nCampos = nCampos - 1;
          |PARA i = 0; |SI i [ nCampos; |HACIENDO i = i + 1;
               #2i = #agifa073@#i#;
          |SIGUE;
          |GRABA 020#2n;
     |FINSI;
|FPRC;

|DEFBUCLE Agrupador;
     #agifa073@#1003;
     ;
     #1#25, #1#0, 001;
     #1#25, #1#0, 999;
     ;
     NULL, Agrupador;
|FIN;

|PROCESO AgruArtiIni;
     |DDEF aAlfa;
     aAlfa = aAlfa + "agifa073";
     |CARGA_DEF aAlfa, agifa073@;
     nSwSanchez = FSalida;
     |SI FSalida ! 0; |ACABA; |FINSI;

     aAlfa = Usuario;
     |NOME_DAT #2 aAlfa;
     |DELINDEX #2;
     |ABRE #2;
     |BUCLE Agrupador;
     |CIERRA #2;
|FPRC;

|PROCESO AgruArtiFin;
     |DESCARGA_DEF #agifa073@;
     |DELINDEX #2;
|FPRC;
컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴

|PROCESO IdiomaArticulo;
     aAlfa = "";
     |ABRE #225;
     #225#0 = #3#205;
     |LEE 000#225=;
     |SI FSalida = 0;
          |ABRE #156;
          #156#0 = aArti;
          #156#1 = #225#5;
          |LEE 000#156=;
          |SI FSalida = 0;
               aAlfa = #156#2; |QBF aAlfa;
          |FINSI;
          |CIERRA #156;
     |FINSI;
     |CIERRA #225;
|FPRC;

|PROCESO ImpreFeldo;
     |DDEF aPath;
     aAlfa = aPath + "feldo001";
     |CARGA_DEF aAlfa, refere3@;
     |SI FSalida ! 0;
          |MENAV "0000Error al cargar 'feldo001'";
     |SINO;
          |ABRE #refere3@;
          |SELECT #2#refere3@;
          #refere3@#2 = #1#25;
          #refere3@#3 = #1#0;
          |LEE 000#refere3@.=;
          |SI FSalida = 0;
               enImpPorte = #refere3@#4;
               enIvaPorte = #refere3@#5;
          |SINO;
               enImpPorte = 0;
               enIvaPorte = 0;
          |FINSI;
          |CIERRA #refere3@;
          |DESCARGA_DEF #refere3@;
     |FINSI;
|FPRC;

|PROCESO ImpreIMEI;
     |DDEF aPath;
     aAlfa = aPath + "tlfm0001";
     |CARGA_DEF aAlfa, refere1@;
     |SI FSalida ! 0;
          |MENAV "0000Error al cargar 'tlfm0001'";
     |SINO;
          aAlfa = aPath + "tlfm0002"
          |CARGA_DEF aAlfa, refere2@;
          |SI FSalida ! 0;
               |MENAV "0000Error al cargar 'tlfm0002'";
          |SINO;
               eaIMEI = ""; eaABO = ""; eaICC = "";
               enSwIMEI = 0;
               |ABRE #201; |SELECT #10#201;
               |ABRE #202; |SELECT #8#202;
               #201#58 = #2#28;
               #201#59 = #2#0;
               #201#60 = #2#1;
               |LEE 000#201=;
               |SI FSalida = 0;
                    eaIMEI = #201#0;
               |FINSI;
               #202#51 = #2#28;
               #202#52 = #2#0;
               #202#53 = #2#1;
               |LEE 000#202=;
               |SI FSalida = 0;
                    eaABO = #202#3;
                    eaICC = #202#4;
               |FINSI;
               |CIERRA #201;
               |CIERRA #202;
               |SI eaIMEI ! ""; |O eaABO ! ""; |O eaICC ! "";
                    enSwIMEI = 1;
                    |PRINT;
               |FINSI;
               enSwIMEI = 0;
               |DESCARGA_DEF #refere2@;
          |FINSI;
          |DESCARGA_DEF #refere1@;
     |FINSI;
|FPRC;

|PROCESO ConverCliArt;
     |DDEFECTO #109;
     #109#0 = #1#4;
     #109#2 = #2#2;
     |LEE 000#109=;
     |SI FSalida = 0;
          enCliArt = 1;
     |SINO;
          enCliArt = 0;
          #109#3 = #109#2;    || pongo el original sino esta, asi no es
          #109#4 = #2#4;      ||/necesario condicionar la linea en el inf
     |FINSI;
|FPRC;

|PROCESO TagPostFormado;
     aEsPostFormaso = "N";
     |DDEF aAlfa;
     aAlfa = aAlfa + "tagm0056";
     |CARGA_DEF aAlfa, tagm0056@;
     |SI FSalida = 0;
          |ABRE #tagm0056@;
          #tagm0056@#0 = "PV";
          #tagm0056@#1 = #2#28;
          #tagm0056@#2 = #2#0;
          #tagm0056@#3 = #2#1;
          |LEE 000#tagm0056@.=;
          |SI FSalida = 0;
               |ABRE #19;
               #19#0 = #2#2;
               |LEE 000#19=;
               |CIERRA #19;

               descrip = #2#4;
               nTotal = #2#9;
               nTotalD = #2#39;
               |SALVA_FICHA #2;
               #2#1 = #tagm0056@#5;
               |LEE 000#2=;
               |SI FSalida = 0;
                    nTotal = nTotal + #2#9;
                    nTotalD = nTotalD + #2#39;
                    nPrecio = #2#6;
                    nPrecioD = #2#38;
               |FINSI;
               |REPON_FICHA #2;
               |SI #19#194 = 2;
                    nPrecio = nPrecio + (#2#6 * #2#44 / #2#5);
                    nPrecioD = nPrecioD + (#2#38 * #2#44 / #2#5);
               |SINO;
                    nPrecio = nPrecio + #2#6;
                    nPrecioD = nPrecioD + #2#38;
               |FINSI;
               #2#4 = descrip;
               #2#6 = nPrecio;
               #2#38 = nPrecioD;
               #2#9 = nTotal;
               #2#39 = nTotalD;
          |SINO;
               |SELECT #2#tagm0056@;
               #tagm0056@#5 = #2#1;
               |LEE 000#tagm0056@.=;
               |SI FSalida = 0;
                    aEsPostFormaso = "S";
               |FINSI;
          |FINSI;
          |CIERRA #tagm0056@;
          |DESCARGA_DEF #tagm0056@;
     |FINSI;
|FPRC;

|PROCESO CargaFax;
     |SI nImpreso = 0; |ACABA; |FINSI;
     |FINIMP;
     aDestino = "c:\dswinfax\" + aNomFic;
     |SI nSwFax = 1;
          |DBASS aAlfa; |QBF aAlfa;
          aAlfa = aAlfa + "/agitool/fich/";
          |PATH_DAT #100 aAlfa;
          |ABRE #100;
          |LEE 101#100p;
          |SI FSalida ! 0; |ACABA; |FINSI;
          #100#3 = #3#2;          || destinatario
          #100#4 = aNumFax;       || nro. de fax destino
          #100#7 = aNomFic;       || doc
          #100#10 = #100#10 + 1;  || contador
          #100#11 = aDestino;     || fichero
          |GRABA 020#100a;
          |CIERRA #100;
          |SUB_EJECUTA ":agitool/dsfax001;AUTO";
     |FINSI;
     |SI nSwMail = 1;         ||EMAIL
          ||TODO CCC es un DSCORREO de la propia maquina
          |HAZ MandaCorreo;
     |FINSI;
|FPRC;

|PROCESO MandaCorreo;
||Paso el adjunto (PDF) al servidor.
     |DBASE aBase; |QBF aBase;
     aPathEnvios = aBase + "envios/";
     |MKDIR aPathEnvios;
     aOrigen = aDestino;
     aDestino = aPathEnvios + aNomFic;
     aIPRemoto = "";
     |IP_REMOTO aIPRemoto;  |QBT aIPRemoto;
     |SI aIPRemoto ! "";
          |RECIBE_FICHERO aOrigen, aDestino;
     |SINO;
          |COPIA_FICHERO aOrigen, aDestino;
     |FINSI;
     aAdjunto = aDestino;

     |ESPECIFICA "RBASS", aLocal;
     aBaseLocal = aLocal;

     aDestino   = aBaseLocal + "bin/dscorreo.exe";
     aContiene1 = aDestino;
     |ESPECIFICA "REMOTOMD5", aContiene;
     aVersion1  = FSalida;  |QBF aVersion1;

     |DBASE aBase;  |QBF aBase;
     aOrigen    = aBase + "plugins/dscorreo.exe";
     aContiene1 = aOrigen;
     |ESPECIFICA "MD5", aContiene;
     aVersion2  = FSalida;  |QBF aVersion2;

     |SI aVersion1 ! aVersion2;
         aIPRemoto = "";
         |IP_REMOTO aIPRemoto;  |QBT aIPRemoto;

         |SI aIPRemoto ! "";
             |ENVIA_FICHERO aOrigen, aDestino;
         |SINO;
             |COPIA_FICHERO aOrigen, aDestino;
         |FINSI;
     |FINSI;

     aAlfa  = "start " + aDestino;
     |RSYSTEM aAlfa;
     |MENSA "         ";
     |PULSATECLA;
     |SLEEP 1;

     aIPRemoto = "";
     |IP_REMOTO aIPRemoto;  |QBT aIPRemoto;

     aSalida  = "MAPI";
     aFrom    = "MAPI";
     aTo      = aEmail;  |QBF aTo;
     aAsunto  = "Pedido de Venta " + aNomFic;
     aAsunto = aAsunto - ".pdf";
     |QBF aAsunto;
     aMensaje = aAsunto;

     |DSCORREO_ENVIA aIPRemoto, aSalida, aFrom, aTo, aAsunto, aMensaje, aAdjunto;
     |SI FSalida < 0;
         |MENAV "0000 Ha habido un error durante el envio.";
     |SINO;
         |MENAV "0000 El correo se encuentra en la Bandeja de Salida de su Gestor de Correos. Envielo desde alli.";
     |FINSI;

     |FBORRA aAdjunto;
|FPRC;

|PROCESO escanlinea;
     unid    = #16#4 * #2#5;
     descrip = #16#3;
     linea   = 1;

     aArti = #16#2;
     |HAZ IdiomaArticulo;
     |SI aAlfa ! "";
          #16#3 = aAlfa;
          descrip = aAlfa;
     |FINSI;

     |PRINT;
|FPRC;

|PROCESO xx;
     indtope=0
     |PARA ind=6;|SI ind>0;|HACIENDO ind=ind-1;
           descrip = #13ind;  |QBF descrip;
           |SI descrip ! vacio;
               indtope = ind + 1;
               ind     = 0;
           |FINSI;
     |SIGUE;

     |PARA ind = 1; |SI ind <i ndtope; |HACIENDO ind = ind + 1;
           descrip = #13ind;
           linea   = 0;
           |PRINT;
     |SIGUE;
|FPRC;

|PROCESO impdesam;|TIPO 0;
     |ABRE #13;
     #13#0=#2#27;
     |LEE 121#13=;
     |SI FSalida = 0;  |HAZ xx;  |FINSI;
     |LIBERA #13;
     |CIERRA #13;
|FPRC;

|PROCESO DesAmpli;
     |DDEFECTO #2;
     #2#3 = 0;      || Almacen
     #2#4 = #53#5;
     descrip = #2#4;

     enSwDA = 1;
     |PRINT;
     enSwDA = 0;
|FPRC;

|DEFBUCLE DesAmpli;
     #53#1;
     ;
     "P", #2#28, #2#0, #2#1, 0001;
     "P", #2#28, #2#0, #2#1, 9999;
     ;
     NULL, DesAmpli;
|FIN;

|PROCESO dsm00046;
     unid = #2#5 * #46#5;
     |PRINT;
|FPRC;

|DEFBUCLE dsm00046;
     #46#1;
     ;
     #2#28, #2#0, #2#1, 001;
     #2#28, #2#0, #2#1, 999;
     ;
     NULL, dsm00046;
|FIN;

|PROCESO ImpreNormal;
     tolin   = #2#5 * #2#6;
     tolin   = tolin < #2#8;
     cant    = cant + tolin;
     descrip = #2#4;
     linea   = 1;
     unid    = #2#5;
     codigo  = #2#2;
     enArtImp = #2#2; |QBF enArtImp;

     ||컴컴컴컴컴컴컴컴컴컴 Modulos
     |SI nSwTagloma = 0;
          |HAZ ImpUniFami;
          |HAZ TagPostFormado;
          |SI aEsPostFormaso = "S"; |ACABA; |FINSI;
     |FINSI;
     |SI nSwAparecida = 0;
          eaArticulo = #2#2;
          |HAZ ImpAparecida;
     |FINSI;
     |SI nSwSematel = 0;
          |HAZ ImpreIMEI;
     |FINSI;
     |SI nSwFeldo = 0;
          |HAZ ImpreFeldo;
     |FINSI;
     ||컴컴컴컴컴컴컴컴컴컴

     eaSerLog = #1#25;
     eaNumLog = (A\("000000" + #1#0) % -106);
     eaTipLog = "PV";
     |HAZ ImprimeLog;

     |HAZ ConverCliArt;

     |SI nSwCoimasa = 0;
          |HAZ RieCliBloq;
          eaCodArtIdioma = #agifa073#2;
          eaArtIdioma = "";
          |HAZ IdiomaPedidoCoimasa;
          |SI eaArtIdioma ! "";
               descrip = eaArtIdioma;
          |FINSI;
     |FINSI;

     aArti = #2#2;
     |HAZ IdiomaArticulo;
     |SI aAlfa ! "";
          #2#4 = aAlfa;
          descrip = aAlfa;
     |FINSI;

     |PRINT;
     |SI nSwOrts = 0;
          |HAZ OrtsObserva;
          |SI enOrtDesCombi = 0; |HAZ OrtImpre; |FINSI;
     |FINSI;
     |SI enSwMarhu = 0; |HAZ CargaTotP; |FINSI;

     |SI nSwEvaristo = 0;
          |HAZ ImpEvaristo;
     |FINSI;

     |SALVA_DATOS #2;
     |BUCLE DesAmpli;
     |REPON_DATOS #2;

     |HAZ impdesam;
     |SI #41#16 = "S";
          eaArticulo = #2#2;
          |HAZ EsEscandallo;
          |SI FSalida = 0;
               |HAZ EsKit;
               |SI FSalida = 0;
                    enEscan = 2;
                    |BUCLE dsm00046;
               |SINO;
                    enEscan = 1;
                    |BUCLELIN 0escanlinea#15;
               |FINSI;
               enEscan = 0;
          |FINSI;
          |CIERRA #15;
     |FINSI;
|FPRC;

|PROCESO ImpreHorizo;
     enArtImp = #2#2; |QBF enArtImp;
     eaTipo      = "PV";
     eaSerie     = #2#28;
     enCodig     = #2#0;
     enLinea     = #2#1;
     eaArticulo  = #2#2;
     eaNArticulo = #2#4;
     enUnidades  = #2#5;
     enPrecio    = #2#6;
     enImpo = #2#39;
     enDto1      = #2#8;
     enDto2      = #2#29;
     efFEntrega  = #2#13;
     enImpo      = #2#39;
     |HAZ TrataCompo;
|FPRC;

|PROCESO pedimprl;|TIPO 0;
     |SI #2#13 < #0#9;  |O #2#13 > #0#10;  |ACABA;  |FINSI;

     nSwPasa = 1;

     |SI #910#19 = "N";  |HAZ ImpreNormal;  |FINSI;
     |SI #910#19 = "S";  |HAZ ImpreHorizo;  |FINSI;
|FPRC;

|PROCESO Marhuenda;
     |DDEFECTO #55;
     #2#28 = #100#0;
     #2#0 = #100#1;
     #2#1 = #100#2;
     |LEE 020#2=;
     |HAZ pedimprl;
|FPRC;

|DEFBUCLE Marhuenda;
     #100#2004;
     ;
     "          ", #1#25, #1#0, 001;
     "zzzzzzzzzz", #1#25, #1#0, 999;
     ;
     NULL, Marhuenda;
|FIN;

|PROCESO IVAS;
     eaCli = #1#4;
     efFiva = #1#1;
     enTiva = 1; |HAZ IVACli; iva1 = enIva; re1 = enRec;
     enTiva = 2; |HAZ IVACli; iva1 = enIva; re1 = enRec;
     enTiva = 3; |HAZ IVACli; iva1 = enIva; re1 = enRec;
     enTiva = 4; |HAZ IVACli; iva1 = enIva; re1 = enRec;
     enTiva = 5; |HAZ IVACli; iva1 = enIva; re1 = enRec;
|FPRC;

|PROCESO pedimpr1;
     || Modulo Master Panel: 003679
     fFecha = #1#2; |HAZ CalculaSemana; enSemana = nSemana;

     |SI nSwPackList = 0;
          eaDoc = "PV" + #1#25 + #1#0;
          |HAZ PackListVar;
     |FINSI;
     ||컴컴컴컴컴컴컴컴컴
     |SI nSwTecatel = 0;
          |HAZ TecaImpPed;
     |FINSI;
     ||컴컴컴컴컴컴컴컴컴
     |HAZ IVAS;

     aMoneda = #agifa104#37;
     aDivisa = #agifa104#35;
     |HAZ Divisas104;

     |DDEFECTO #3;
     #3#0 = #1#4;
     |LEE 040#3=;
     aNumFax = #3#18; |QBF aNumFax;
     aNumMail = #3#197; |QBF aNumMail;
     |SI nSwFax = 1; |Y aNumFax = "";
          aAlfa = "0000No se ha indicado Numero de Fax en la ficha del Cliente:" + #3#0;
          |MENAV aAlfa;
          |ACABA;
     |FINSI;
     |SI nSwMail = 1; |Y aNumMail = "";
          aAlfa = "0000No se ha indicado Numero de Email en la ficha del Cliente: " + #3#0;
          |MENAV aAlfa;
          |ACABA;
     |FINSI;
     |SI nSwFax = 1; |O nSwMail = 1;
          aNomFic = #1#25 +(("000000"+#1#0)%-106)+".pdf";|QBT aNomFic;
          |DBASS aFic;

          aFic = "c:\dswinfax\" + aNomFic;
          Impresora = "CrystalReport;" + aFic;
          |IMPRE -1;
          |SI FSalida ! 0; |ERROR; |ACABA; |FINSI;
     |FINSI;

     |ABRE #40;
     #40#26 = #1#25;
     |LEE 001#40=;
     |SI FSalida ! 0;  |DDEFECTO #40;  |FINSI;
     |CIERRA #40;

     |SI aInforme = "";
          informe = #40#0; |QBF informe;
          aInforme = informe;
     |SINO;
          informe = aInforme;
     |FINSI;
     informe1 = (informe % 0104) + "5" + (informe % 0603); |QBF informe1;

     |INFOR aInforme;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |SI nSwSanchez = 0; |HAZ AgruArtiIni; |FINSI;

     |HAZ OrdenaMarhuP;
     |PARA n_copia = 0; |SI n_copia [ #3#188; |HACIENDO n_copia = n_copia + 1;
          nSwPasa = 0;
          |SI #910#19 = "S";  |HAZ AbreTemporales;  |FINSI;
          |SI enSwMarhu = 0;
               |ABRE #2;
               |BUCLE Marhuenda;
               |CIERRA #2;
          |SINO;
               |BUCLELIN 2pedimprl#1;
          |FINSI;
          |SI #910#19 = "S";  |HAZ ElRemate;  |FINSI;
          |SI nSwPasa = 1;  |PIEINF;  |FINSI;
          enSwImpre = 0;
          cant = 0;
          tolin = 0;
     |SIGUE;

     |SI nSwSanchez = 0; |HAZ AgruArtiFin; |FINSI;

     |FININF;
     #1#58 = "S";
     |GRABA 020#1a;

     |SI nSwTagloma = 0; |Y enGraba300 = 0;
          #300#0 = #1#25;
          #300#1 = #1#0;
          |GRABA 020#300n;
     |FINSI;

     nImpreso = 1;
|FPRC;

|DEFBUCLE agifa104e1;
     #1#1;
     58;
     #0#6 , #0#2,aDRei;
     #0#7 , #0#3,aHRei;
     e;
     NULL, pedimpr1;
|FIN;

|DEFBUCLE agifa104e2;
     #1#1;
     4, 1,58;
     #0#6 , #0#2 , #0#0 , #0#4,aDRei;
     #0#7 , #0#3 , #0#1 , #0#5,aHRei;
     e;
     NULL, pedimpr1;
|FIN;

|DEFBUCLE agifa104e3;
     #1#2;
     25, 0, 1,58;
     #0#0 , "     ",      0, #0#6 , #0#2 , #0#4,aDRei;
     #0#1 , "zzzzz", 999999, #0#7 , #0#3 , #0#5,aHRei;
     be;
     NULL, pedimpr1;
|FIN;

|PROCESO PideImpresora;
     |QBF eaImpresora;        || No pide
     |QBF eaFicImpre;         || a un fichero
     |QBF eaDefImpre;         || la por defecto
     || enPideImpre  (solo pide la impresora la primera vez)

     |SI eaFicImpre ! "";
          Impresora = eaFicImpre;
          |IMPRE -77;
     |SINO;
          |SI eaImpresora = "";
               |SI eaDefImpre = "";
                    |IMPRE 1;
                    |SI enPideImpre = 1;||carga la variable para que no la pida a la siguiente
                        eaImpresora = Impresora;
                        |SI FSalida ! 0;eaImpresora = "***";|FINSI;||ninguna
                    |FINSI;
               |SINO;
                    Impresora = eaDefImpre;
                    |IMPRE 0;
               |FINSI;
          |SINO;
               Impresora = eaImpresora;
               |IMPRE -1;
          |FINSI;
     |FINSI;
     eaImpElige = Impresora;
|FPRC;

|PROCESO Tipo3;
     |SI #0#11 = "S";
          aDRei = " ";
          aHRei = "z";
     |SINO;
          aDRei = "N";
          aHRei = "N";
     |FINSI;

     |SI nSwFax = 0; |Y nSwMail = 0;
          |HAZ PideImpresora;
          |SI FSalida ! 0;  |ACABA;  |FINSI;
     |FINSI;

     |ABRE #3;
     |ABRE #109; |SELECT #2#109;
     |SI ser_ped = ""; |Y num_ped = 0;
         |SI #0#8 = "C";
             |BUCLE agifa104e3;
         |SINO;
             |BUCLE agifa104e1;
         |FINSI;
     |SINO;
         |BUCLE agifa104e2;
     |FINSI;
     |CIERRA #109;
     |CIERRA #3;

     |SI nSwFax = 0; |Y nSwMail = 0;
          |FINIMP;
     |SINO;
          |HAZ CargaFax;
     |FINSI;
|FPRC;

|PROGRAMA;
     |SI PRMNT_Imp ! "";
          ser_ped = PRMNT_Imp % 105; aAlfa = PRMNT_Imp % 606;
          num_ped = aAlfa;
     |FINSI;
     |HAZ EsVigor;
     |SI FSalida = 0; |CORRE "vigz0032"; |FINSI;

     |HAZ EsSanchez;
     nSwSanchez = FSalida;

     |SI eaFicTmpCab ! ""; |NOME_DAT #agifa104#eaFicTmpCab#; |FINSI;
     |SI eaFicTmpLin ! ""; |NOME_DAT #agifa073#eaFicTmpLin#; |FINSI;

     aParam = PARAMETRO; |QBF aParam;
     |SI aParam = "ANNA";
          enSwAnna = 1; aParam = "";
     |FINSI;
     |SI aParam = "FAX";
          nSwFax = 1; aParam = "";
     |FINSI;
     |SI aParam = "EMAIL";
          nSwMail = 1; aParam = "";
     |FINSI;
     aInforme = aParam;
     /*----*/

     |HAZ MiraLogos; || Copia Logos para el Crystal

     enPantalla  = 0;

     |HAZ EsPackList; nSwPackList = FSalida;
     |HAZ EsSematel; nSwSematel = FSalida;
     |HAZ EsTecatel; nSwTecatel = FSalida;
     |HAZ EsTagloma; nSwTagloma = FSalida;
     |HAZ EsAparecida; nSwAparecida = FSalida;
     |HAZ EsFeldo; nSwFeldo = FSalida;
     |HAZ EsOrts; nSwOrts = FSalida;
     |HAZ EsCoimasa; nSwCoimasa = FSalida;
     |HAZ EsEvaristo; nSwEvaristo = FSalida;

     |HAZ IniMarhuP;

     |ABRE #41; |LEE 000#41p; |CIERRA #41;
     |ABRE #910; |LEE 000#910p; |CIERRA #910;

     |SI nSwTagloma = 0; |Y enGraba300 = 0;
          |HAZ CreaTempo; |NOME_DAT #300 Tempo; aTmp300 = Tempo;
          |DELINDEX #300; |ABRE #300;
     |FINSI;

     |SI ser_ped = ""; |Y num_ped = 0;
         eaImpresora = "";
         |CLS;
         |CABEZA "Imp.Pedidos";
         |DDEFECTO #0;
         |PINPA #0#0;
         |PINDA #0#0;
         |ENDATOS #1#0;
         |SI FSalida = 0;  |HAZ Tipo3;  |FINSI;
     |SINO;
         |SI eaImpresora = "***"; |ACABA; |FINSI;
         #0#6 = ser_ped;
         #0#7 = ser_ped;
         #0#2 = num_ped;
         #0#3 = num_ped;
         #0#4 = "01.01.0000";
         #0#5 = "31.12.9999";
         #0#9 = "01.01.0000";
         #0#10 = "31.12.9999";
         #0#11 = "S";
         |HAZ Tipo3;
     |FINSI;

     |HAZ FinMarhuP;

     |SI nSwTecatel = 0;
          |SUB_EJECUTA "tecaz005;algo";
     |FINSI;

     |SI nSwTagloma = 0; |Y enGraba300 = 0;
          aAlfa = "tagz0026;" + aTmp300;
          |SUB_EJECUTA_NP aAlfa;
          |CIERRA #300; Tempo = aTmp300; |DELINDEX #300;
     |FINSI;
|FPRO;

|PROCESO ortsm032;
     eaObsOrts = #refere1@#4;
     |PRINT;
|FPRC;

|DEFBUCLE ortsm032;
     #refere1@#1004;
     ;
     #2#28, nNum, nLin1, 000;
     #2#28, nNum, nLin1, 999;
     ;
     NULL, ortsm032;
|FIN;

|PROCESO OrtsObserva;
     |DDEF aPath;
     aAlfa = aPath + "ortsm032";
     |CARGA_DEF aAlfa, refere1@;
     |SI FSalida ! 0;
          |MENAV "0000Error al cargar 'ortsm032'";
     |SINO;
          |BUCLE ortsm032;
          |DESCARGA_DEF #refere1@;
     |FINSI;
|FPRC;

|PROCESO ortsm018;
     enLin2 = #refere2@#4;
     enSwDes = 2;
     |PRINT;
|FPRC;

|DEFBUCLE ortsm018;
     #refere2@#1005;
     ;
     #2#28, nNum, nLin1, nLin2, 1;
     #2#28, nNum, nLin1, nLin2, 999;
     ;
     NULL, ortsm018;
|FIN;

|PROCESO ortsm017;
     enLin1 = #refere1@#3;
     enSwDes = 1;
     |PRINT;
     |SI #refere1@#8 = "S";
          nLin2 = #refere1@#3;
          |BUCLE ortsm018;
     |FINSI;
|FPRC;

|DEFBUCLE ortsm017;
     #refere1@#1004;
     ;
     #2#28, nNum, nLin1, 000;
     #2#28, nNum, nLin1, 999;
     ;
     NULL, ortsm017;
|FIN;

|PROCESO OrtImpre;
     |DDEF aPath;
     aAlfa = aPath + "ortsm017";
     |CARGA_DEF aAlfa, refere1@;
     |SI FSalida ! 0;
          |MENAV "0000Error al cargar 'ortsm017'";
     |SINO;
          aAlfa = aPath + "ortsm018"
          |CARGA_DEF aAlfa, refere2@;
          |SI FSalida ! 0;
               |MENAV "0000Error al cargar 'ortsm018'";
          |SINO;
               nNum = #2#0; nLin1 = #2#1;
               |BUCLE ortsm017;
               enSwDes = 0;
               |DESCARGA_DEF #refere2@;
          |FINSI;
          |DESCARGA_DEF #refere1@;
     |FINSI;
|FPRC;

|PROCESO dscam038;
     |SI #1#25 ] #refere2@#3; |Y #1#25 [ #refere2@#4;
          aEmp = ("00000" + #refere2@#5) % -105;
          aAlfa = aBase + "dscarter/fich/" + aEmp + "/";
          |PATH_DAT #refere3@#aAlfa#;
          #refere1@#0 = #refere2@#0;
          |LEE 020#refere1@.=;
          |SI FSalida = 0;
               |ABRE #refere3@;
               #refere3@#0 = #1#4;
               |LEE 000#refere3@.=;
               |SI FSalida = 0; |Y #refere3@#9 = "S";
                    eaBloqueo = "S";
               |FINSI;
               |CIERRA #refere3@;
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE dscam038;
     #refere2@#1002;
     2;
     nEmpresa, 001, "A"; || lee el control albaranes
     nEmpresa, 999, "A";
     ;
     NULL, dscam038;
|FIN;

|PROCESO RieCliBloq;
     eaBloqueo = "N";
     |DBASS aBase;
     aAlfa = aBase + "dscarter/def/dscam037";
     |CARGA_DEF aAlfa, refere1@;
     |SI FSalida = 0;
          aAlfa = aBase + "dscarter/def/dscam038";
          |CARGA_DEF aAlfa, refere2@;
          |SI FSalida = 0;
               aAlfa = aBase + "dscarter/def/dscam023";
               |CARGA_DEF aAlfa, refere3@;
               |SI FSalida = 0;
                    |DFICE aAlfa; aAlfa = aAlfa % -205; nEmpresa = aAlfa;
                    aAlfa = aBase + "dscarter/fich/";
                    |PATH_DAT #refere2@#aAlfa#;
                    |PATH_DAT #refere1@#aAlfa#;
                    |ABRE #refere1@;
                    |BUCLE dscam038;
                    |CIERRA #refere1@;
                    |DESCARGA_DEF #refere3@;
               |FINSI;
               |DESCARGA_DEF #refere2@;
          |FINSI;
          |DESCARGA_DEF #refere1@;
     |FINSI;
|FPRC;
