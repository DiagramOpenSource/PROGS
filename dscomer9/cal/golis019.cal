|FICHEROS;
     golis019 #0;
     goman001 #1;        ||Cta Analitica
     gomantho #10;       ||Expedientes CAB.
     gomantl1 #11;       ||Expedientes LIN.
     gopre001 #21;       ||Presupuestos
     gopartia #22;
     guerw045 #45;  ||Tmp
     guerw046 #46;  ||Tmp
     agifa084 #84;
     goparame;
|FIN;

|VARIABLES;
     aAlfa = "";
     aInforme  = "";
     aInformes = "golis019"
     aInformec = "golis019";
     aInformeo = "golis019";
     nHay = 0;
     nSw0 = 0;
|FIN;

|PROCESO Tmp;
     |PRINT;
     nHay = 1;
|FPRC;

|DEFBUCLE Tmp3;
     #45#3;
     ;
     INICIO;
     FINAL;
     ;
     NULL, Tmp;
|FIN;

|DEFBUCLE Tmp2;
     #45#2;
     ;
     INICIO;
     FINAL;
     ;
     NULL, Tmp;
|FIN;

|DEFBUCLE Tmp1;
     #45#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, Tmp;
|FIN;

|PROCESO Factura;
     |SI #46#6 [ #0#18;
          #45#15 = #45#15 + #46#5;
     |SINO;
          #45#16 = #45#16 + #46#5;
     |FINSI;
|FPRC;

|DEFBUCLE Factura;
     #46#2;
     ;
     INICIO;
     FINAL;
     ;
     NULL, Factura;
|FIN;

|PROCESO gopartia;
     |SI #22#28 [ #0#18;
          |SI #22#34 = "T";                    || partes trabajo
               #45#8 = #45#8 + #22#14;
          |FINSI;
          |SI #22#34 = "A"; |O #22#34 = "M";   || albaranes y movimientos
               #45#9 = #45#9 + #22#14;
          |FINSI;
          |SI #22#34 = "C";                    || control caja
               #45#10 = #45#10 + #22#14;
          |FINSI;
     |SINO;
          |SI #22#34 = "T";                    || partes trabajo
               #45#11 = #45#11 + #22#14;
          |FINSI;
          |SI #22#34 = "A"; |O #22#34 = "M";   || albaranes y movimientos
               #45#12 = #45#12 + #22#14;
          |FINSI;
          |SI #22#34 = "C";                    || control caja
               #45#13 = #45#13 + #22#14;
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE gopartia;
     #22#3;
     ;
     #11#0, #11#1, #11#3, #11#4, "                    ", "                    ";
     #11#0, #11#1, #11#3, #11#4, "zzzzzzzzzzzzzzzzzzzz", "zzzzzzzzzzzzzzzzzzzz";
     ;
     NULL, gopartia;
|FIN;

|PROCESO gomantl1;
     #21#0 = #11#3;
     #21#1 = #11#4;
     |LEE 000#21=;

     #45#7 = #45#7 + (#21#35 - #21#34);
     |BUCLE gopartia;
     #45#14 = #45#14 + #21#35;

     |BUCLE Factura;
|FPRC;

|PROCESO gomantho;
     nSw0 = 0;
     |SI #0#14 = "S"; |Y #10#9 = "C"; nSw0 = 1; |FINSI;
     |SI #0#15 = "S"; |Y #10#9 = "P"; nSw0 = 1; |FINSI;
     |SI #0#16 = "S"; |Y #10#9 = "T"; nSw0 = 1; |FINSI;
     |SI #0#17 = "S"; |Y #10#9 = "E"; nSw0 = 1; |FINSI;
     |SI nSw0 = 0; |ERROR; |ACABA; |FINSI;

     |DDEFECTO #45;
     #45#0 = #10#0;
     #45#1 = #10#1;
     |LEE 101#45=;
     |SI FSalida ! 0; |GRABA 020#45b; |FINSI;
     #45#2 = #10#2;
     #45#3 = #10#8;
     #45#4 = #10#29;
     #45#5 = #10#30;
     #45#6 = #10#31;
     |BUCLELIN 2 gomantl1 #10;

     #45#17 = #45#14 - (#45#15 + #45#16);
     #45#18 = #45#7 - (#45#8 + #45#9 + #45#10 + #45#11 + #45#12 + #45#13);
     #45#19 = #45#14 - (#45#8 + #45#9 + #45#10 + #45#11 + #45#12 + #45#13);

     |GRABA 020#45a;
     |LIBERA #45;
|FPRC;

|DEFBUCLE gomantho;
     #10#1;
     2, 30, 24, 25;
     #0#1, #0#3, #0#5, #0#7, #0#9, #0#11;
     #0#2, #0#4, #0#6, #0#8, #0#10, #0#12;
     ;
     NULL, gomantho;
|FIN;

|| Para acelerar el proceso, ya que el curso no es clave
|PROCESO Carga46;
     #1#0 = #84#87;
     |LEE 000#1=;
     |SI FSalida = 0;
          #46#0 = #84#48;
          #46#1 = #84#0;
          #46#2 = #84#87;
          #46#3 = #1#2;
          #46#4 = #1#3;
          #46#5 = #84#41;
          #46#6 = #84#1;
          |GRABA 020#46n;
     |FINSI;
|FPRC;

|DEFBUCLE Carga46;
     #84#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, Carga46;
|FIN;

|PROCESO Inicio;
     aAlfa = "1" + Usuario;
     |NOME_DAT #45 aAlfa;
     |DELINDEX #45;

     aAlfa = "2" + Usuario;
     |NOME_DAT #46 aAlfa;
     |DELINDEX #46;

     |SI #0#13 = "S"; aInforme = aInformes; |FINSI;
     |SI #0#13 = "C"; aInforme = aInformec; |FINSI;
     |SI #0#13 = "O"; aInforme = aInformeo; |FINSI;
     |IMPRE 0;
     |SI FSalida = 0;
          |INFOR aInforme;
          |SI FSalida ! 0;
               aInforme = "0000No existe informe:" + aInforme;
               |MENAV aInforme;
          |SINO;
               |ABRE #46;
               |ABRE #1;
               |BUCLE Carga46;
               |CIERRA #1;
               |ABRE #21;
               |ABRE #45;
               |BUCLE gomantho;
               |CIERRA #45;
               |CIERRA #21;
               |CIERRA #46;
               |SI #0#13 = "S"; |BUCLE Tmp1; |FINSI;
               |SI #0#13 = "C"; |BUCLE Tmp2; |FINSI;
               |SI #0#13 = "O"; |BUCLE Tmp3; |FINSI;
               |SI nHay ! 0; |PIEINF; |FINSI;
               |FININF;
          |FINSI;
          |FINIMP;
     |FINSI;

     |DELINDEX #45;
     |DELINDEX #46;
|FPRC;

|PROGRAMA;
     |HAZ MiraLogos; || Copia Logos para el Crystal

     |ABRE #goparame; |LEE 000#goparame.p; |CIERRA #goparame;
     |CLS;
     |DDEFECTO #0;
     |ABRE #0;
     |LEE 101#0p;
     |SI FSalida ! 0; |GRABA 020#0b; |FINSI;
     |PINPA #0#0;
     |PINDA #0#0;
     |ENDATOS #1#0;
     |SI FSalida = 0;
          |GRABA 020#0a;
          |HAZ Inicio;
     |FINSI;
     |CIERRA #0;
|FPRO;
