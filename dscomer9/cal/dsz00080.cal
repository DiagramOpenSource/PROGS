|FICHEROS;
     dsz00080 #0;
     dsm00134 #134; || Promociones
     agifa324 #324; || Divisa
     agifa019 #19;
     agifa142 #142;
|FIN;

|VARIABLES;
     nSwAlbadist = 0;
     aParam = "";
     nOk = 0;
     nUniPromo = 0;
     nResto = 0;
     nPrePromo = 0;
     nDeci = 0;
     i = 0;

     &Envase = "";
     &Unidades1 = 0;
     &Unidades2 = 0;
     &VArticulo = "";
     &enDto = 0;
     nHayDto = 0;
|FIN;

|INCLUYE i_varart;

|PROCESO Uni_Base;
     |SI FSalida = 14; ||F6;
          enTipEnvase = 1;
          |HAZ Entra_Base;
          |ACABA;
     |FINSI;

     |SI #0Campo = Contenido; |ACABA; |FINSI;

     nUniPromo = #0#4 / #134#2;
     nResto = #0#4 $ #134#2;
     nResto = nResto / #134#2;
     nUniPromo = nUniPromo  - nResto;
     #0#5 = nUniPromo * #134#3;
     nPrePromo = #134#4;

     #0#7 = (#0#7 + #0#9 - nPrePromo) ! nDeci;
     #0#9 = nPrePromo;

     #0#6 = #0#4 + #0#5;
     |PINTA #0#6;
     |PINTA #0#7;
|FPRC;

|PROCESO Entra_Base; |TIPO 7;
     |C_M #0#4, 1, "S";
     |SI FSalida = 2; |ACABA; |FINSI;
     Envase = #0#11;
     Unidades1 = #0#10;
     Unidades2 = #0#4;
     VArticulo = eaArticulo;
     |SI eaEnvase = "S";
          |HAZ EntraEnvase;
          |C_M #0#4, 1, "S";
          |SI enEntraEnvase ! 0; |C_M #0#4, 1, "N";  |FINSI;
     |FINSI;
     #0#11 = Envase;
     #0#10 = Unidades1;
     #0#4 = Unidades2;
     |PINTA #0#4;
|FPRC;

|PROCESO Uni_Promo; |TIPO 0;
     |SI #0Campo = Contenido; |ACABA; |FINSI;
     #0#6 = #0#4 + #0#5;
     |PINTA #0#6;
|FPRC;

|PROCESO Imp_DtoPro;
     |SI #0Campo ! Contenido; |Y Campo = 8;
          #0#7 = #0#7 + Contenido;
          #0#7 = #0#7 - #0#8;
          |PINTA #0#7;
     |FINSI;
|FPRC;

|PROCESO Fin; |TIPO 12;
     |SI FSalida = 7;
          enError = FSalida;
     |SINO;
          enError = 0;
     |FINSI;
|FPRC;

|PROGRAMA;
     aParam = PARAMETRO;
     enError = 0;
     eaProCliArt = "N";

     |ABRE #142; |LEE 000#142p; |CIERRA #142;

     |ABRE #0;
     |ABRE #134;
     #134#0 = eaCli;
     #134#1 = eaArticulo;
     |LEE 000#134=;
     |SI FSalida = 0; |Y efFecha ] #134#6; |Y efFecha [ #134#7;
          nHayDto = 1;
          nOk = 1;
     |FINSI;

     |ABRE #19;
     #19#0 = eaArticulo;
     |LEE 000#19=;
     |CIERRA #19;

     |HAZ EsAlbadis;     || El modulo SIEMPRE entra en el calculo
     nSwAlbadist = FSalida;
     |SI FSalida = 0;
          nOk = 1;
          |C_V #0#12, 1, "S"; || Punto verde solo visual en el modulo
     |FINSI;

     |SI nOk = 1; |O #142#68 = "S";
          eaProCliArt = "S";
          |ABRE #324;
          #324#0 = eaDivisa;
          |LEE 000#324=;
          |CIERRA #324;
          nDeci = #324#9;

          #0#0 = eaDocu;
          #0#1 = eaSerie;
          #0#2 = enCodigo;
          #0#3 = enLinea;
          #0#4 = enUnid1;          || uni base
          #0#5 = enUnid2;          || uni promocion
          #0#6 = #0#4 + #0#5;      || uni total
          #0#7 = enPrecio;         || precio
          #0#8 = enImpDtoPromo;    || imp. dto promo
          #0#9 = enDtoPrePromo;    || dto precio promo
          #0#10 = enUnid3;         || uni conver
          #0#11 = Envase;          || envase
          #0#12 = #19#208;    || Punto verde
          |SI enModo = 1; |O eaArtiAntes ! eaArticulo;
               |SI enModo ! 4; |Y aParam ! "VER";
                    |SI nHayDto = 1; || a¤adido tiquet 1928/226
                         #0#8 = #134#4;
                         #0#9 = #134#4;
                         enDto = #134#5;
                         #0#7 = (enPrecio - #134#4) ! nDeci;
                    |FINSI;
               |FINSI;
          |FINSI;

          |SI #142#250 = "X"; |Y enModo = 2;
               #0#7 = #0#7 - #0#8;
          |FINSI;

          |BLANCO 12222155;
          |PINPA #0#0;
          |PINDA #0#0;

          |SI nSwAlbadist = 0;
               |ATRI I; |PINTA 1823, "³ Punto Verde.......:          ³"; |ATRI 0;
               |PINTA #0#12;
          |FINSI;

          |SI aParam = "VER";
               |PAUSA;
          |SINO;
               |SI enModo = 1;
                    |ENDATOS #1#0;
               |SINO;
                    |ENDATOS #2#0;
               |FINSI;
          |FINSI;
          |SI enError = 0;
               enUnid1 = #0#4; || Unidades linea actual
               enUnid2 = #0#5; || Unidades linea nueva
               enPrecio = #0#7;
               enUnid3 = #0#10;
               Envase = #0#11;
               enImpDtoPromo = #0#8;
               enDtoPrePromo = #0#9;
          |FINSI;
     |FINSI;
     |CIERRA #0;
     |CIERRA #134;
|FPRO;
