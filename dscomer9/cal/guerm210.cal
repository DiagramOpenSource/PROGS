|FICHEROS;
     guerm210 #0;
     guerm211 #1;
     agifa019 #19;
     agifa024 #24;
     agifa041 #41;
     agifa081 #81;
     agifa067 #67;
     agifa112 #112;
     agifa142 #142;
     guerm201 #201;
     guerm202 #202;
     guerm204 #204;
     guerm207 #207;
     guerm208 #208;
     agifa321 #321;
     agifa324 #324;
     guerm219 #219;
     guerm220 #220;
     guerm225 #225;
     referen@;
|FIN;

|VARIABLES;
     aAct = "";
     fDFecha = @;
     fHFecha = @;
     fFecFinAnt = @;
     aDef = "";
     aSql = "";
     nNume = 0;
     &enCreaLineas = 0;
     &eaSerie = "";
     &enNumero = 0;
     &enSwMenu = 0;
     &enImpPrvPed = 0;
     &efFecha = @;
     &eaProve = "";
     &ser_ped = "";
     &num_ped = 0;
     aAlfa = "";
     nModo = 0;
     aSerie = "";
     nConta = 0;
     aDirEmp = "";
     aDatos = "";
     nCodEmp = 0;
     fmes1 = "";
     mes = 0;
     nImpo = 0;
     nForma = 0;
     nLin = 0;
     &eaDocu = "";
|FIN;

|PROCESO Min207;
     #207#10 = "P";
     #207#0 = 0;
|FPRC;

|PROCESO Max207;
     #207#10 = "P";
     #207#0 = 999999;
|FPRC;

|PROCESO ConsCto; |TIPO 66;
     |ABRE #207;
     |LEE 000#207p;
     |CONSULTA_F_CLAVE #2#207, Min207, Max207;
     |SI FSalida = 0;
          #0#6 = #207#0; |PINTA #0#6;
     |SINO;
          |ERROR;
     |FINSI;
     |CIERRA #207;
|FPRC;

|PROCESO Subcontra;
     aAlfa = #0#12;
     |C_M #0#11, 1, aAlfa;
     |C_M #0#14, 1, aAlfa;
|FPRC;

|PROCESO CheqCto; |TIPO 0;
     nModo = (FEntrada/100) ! 0;
     |SI #0#5 = "S";
          |ABRE #207;
          #207#0 = #0#6;
          |LEE 000#207=;
          |SI FSalida ! 0; |Y nModo = 1;
               |MENAV "0000No existe el presupuesto...";
               |ERROR;
          |SINO;
               |SI #207#10 ! "P"; |Y nModo = 1;
                    |MENAV "0000El presupuesto no esta pendiente...";
                    |ERROR; |ACABA;
               |FINSI;
          |FINSI;
          |CIERRA #207;
     |FINSI;
|FPRC;

|PROCESO Fecha0; |TIPO 0;
     |SI FSalida = 2; |ACABA; |FINSI;
     |SI #0#5 = "S";
          |SI #0#9 = "01.01.0000";
               #0#9 = #0#2; |PINTA #0#9;
               nNume = #207#6 - #207#5;
               #0#10 = #0#2 + nNume; |PINTA #0#10;
          |FINSI;
     |SINO;
          |SI #0#9 = "01.01.0000";
               #0#9 = #0#2; |PINTA #0#9;
               #0#10 = #0#2; |PINTA #0#10;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO ProviCto7; |TIPO 7;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 1;
          |C_M #0#5, 1, "S";
     |SINO;
          |C_M #0#5, 1, "N";
     |FINSI;
|FPRC;

|PROCESO ProviCto0; |TIPO 0;
     nModo = (FEntrada / 100) ! 0;
     |SI #0#5 = "S";
          |SI nModo = 1;
               |C_M #0#6, 1, "S";
          |SINO;
               |C_M #0#6, 1, "N";
          |FINSI;
          |C_M #0#3, 1, "N";
          |C_M #0#7, 1, "N";
     |SINO;
          |C_M #0#6, 1, "N";
          |C_M #0#3, 1, "S";
          |C_M #0#7, 1, "S";
     |FINSI;

|FPRC;

|PROCESO ContaPed7; |TIPO 7;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 1;
          aSerie = #0#0;
          #0#1 = 999999;
          |LEE 000#0];
          |SI FSalida = 0;
               |LEE 000#0a;
          |SINO;
               |LEE 000#0u;
          |FINSI;
          |SI FSalida = 0; |Y #0#0 = aSerie;
               nConta = #0#1 + 1;
          |SINO;
               nConta = 1;
          |FINSI;
          |DDEFECTO #0;
          #0#0 = aSerie;
          #0#1 = nConta;
     |FINSI;
|FPRC;

|PROCESO PedArt0; |TIPO 0;
     |SI #1Campo ! Contenido;
          |ABRE #24;
          #24#0 = #0#3;
          |LEE 000#24=;
          |CIERRA #24;

          |ABRE #19;
          #19#0 = #1#3;
          |LEE 000#19=;
          |SI FSalida = 0;
               |SI #24#39 = 1; #1#7 = #19#18; #1#12 = #19#21; |FINSI;
               |SI #24#39 = 2; #1#7 = #19#19; #1#12 = #19#22; |FINSI;
               |SI #24#39 = 3; #1#7 = #19#20; #1#12 = #19#23; |FINSI;
               |SI #24#39 = 4; #1#7 = #19#147; #1#12 = #19#150; |FINSI;
               |SI #24#39 = 5; #1#7 = #19#148; #1#12 = #19#151; |FINSI;
               |SI #24#39 = 6; #1#7 = #19#149; #1#12 = #19#152; |FINSI;
          |FINSI;
          |CIERRA #19;
          |ABRE #204;
          #204#0 = #0#3;
          #204#1 = #0#7;
          #204#2 = #1#3;
          |LEE 000#204=;
          |SI FSalida = 0;
               #1#7 = #204#6;
               #1#12 = #204#7;
          |FINSI;
          |CIERRA #204;
          |PINTA #1#7;
          |PINTA #1#12;
     |FINSI;
|FPRC;

|PROCESO PedTipo3; |TIPO 3;
     |CONFI "0000NImprimir Pedido?";
     |SI FSalida = 0;
          |HAZ PedTipo4;
     |FINSI;
     |SI #0#12 = "S"; |Y #0#17 = 0;
          |CONFI "0000SCrear Pedido Subcontrata?";
          |SI FSalida = 0;
               |HAZ CreaPedido;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO PedLin;
     |PRINT;
|FPRC;

|PROCESO PedTipo4; |TIPO 4;
     |IMPRE 0;
     |SI FSalida = 0;
          |INFOR "guerm210";
          |SI FSalida ! 0;
               |MENAV "0000No existe informe 'guerm210'";
          |SINO;
               |BUCLELIN 2PedLin#0;
               |PIEINF;
               |FININF;
          |FINSI;
          |FINIMP;
     |FINSI;
|FPRC;

|PROCESO PedTipo2; |TIPO 2;
     nModo = (FEntrada / 100) ! 0;
     |SI #0#5 = "S";
          |ABRE #207;
          #207#0 = #0#6;
          |LEE 101#207=;
          |SI FSalida = 0;
               |SI nModo = 3;
                    #207#8 = "";
                    #207#9 = 0;
                    #207#13 = "01.01.0000";
               |SINO;
                    #207#8 = #0#0;
                    #207#9 = #0#1;
                    #207#13 = #0#2;
               |FINSI;
               |GRABA 020#207a;
          |FINSI;
          |CIERRA #207;
     |FINSI;
|FPRC;

|PROCESO CargaLin;
     |DDEFECTO #1;
     #1#0 = #0#0;
     #1#1 = #0#1;
     #1#2 = #208#1;
     #1#3 = #208#2;
     #1#4 = #208#3;
     #1#5 = #208#4;
     #1#6 = #207#11;
     #1#7 = #208#6;
     #1#8 = #208#7;
     #1#9 = #0#9;
     #1#10 = #0#10;
     #1#12 = #208#8;
     #1#19 = #208#10;
     |GRABA 020#1n;
|FPRC;

|PROCESO PedTipo5; |TIPO 5;
     nModo = (FEntrada / 100)! 0;
     |SI nModo ! 1; |ACABA; |FINSI;
     |SI #0#5 = "N"; |ACABA; |FINSI;
     |ABRE #207;
     #207#0 = #0#6;
     |LEE 101#207=;
     |SI FSalida = 0;
          |BUCLELIN 2CargaLin#207;
          #207#10 = "S";
          |GRABA 020#207a;
     |FINSI;
     |CIERRA #1;
|FPRC;

|PROCESO Defecto211; |TIPO 0;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 1;
          #1#9 = #0#9; |PINTA #1#9;
          #1#10 = #0#10; |PINTA #1#10;
     |FINSI;
|FPRC;

|PROCESO FIni211; |TIPO 0;
     |SI #1#9 < #0#9; |O #1#9 > #0#10;
          |MENAV "0000Periodo incorrecto...";
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO FFin211; |TIPO 0;
     |SI #1#10 < #0#9; |O #1#10 > #0#10; |O #1#10 < #1#9;
          |MENAV "0000Periodo incorrecto...";
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO Equipo0; |TIPO 0;
     |SI #0#12 = "S";
          |ABRE #201;
          #201#0 = #0#14;
          |LEE 000#201=;
          |SI FSalida = 0;
               |SI #201#11 = "N";
                    |MENAV "0000Equipo no subcontratado...";
                    |ERROR; |ACABA;
               |FINSI;
               |SI #201#12 ! #0#11; |Y #201#11 = "S";
                    aAlfa = "0000Equipo subcontratado a:" + #201#12;
                    |MENAV aAlfa;
                    |ERROR; |ACABA;
               |FINSI;
          |FINSI;
          |CIERRA #201;
     |FINSI;
|FPRC;

|PROCESO EquipoLin0; |TIPO 0;
     |SI #1#13 = "S";
          |ABRE #201;
          #201#0 = #1#15;
          |LEE 000#201=;
          |SI FSalida = 0;
               |SI #201#11 = "N";
                    |MENAV "0000Equipo no subcontratado...";
                    |ERROR; |ACABA;
               |FINSI;
               |SI #201#12 ! #1#14; |Y #201#11 = "S";
                    aAlfa = "0000Equipo subcontratado a:" + #201#12;
                    |MENAV aAlfa;
                    |ERROR; |ACABA;
               |FINSI;
          |FINSI;
          |CIERRA #201;
     |FINSI;
|FPRC;

|PROCESO Min201;
     #201#12 = #0#11;
|FPRC;

|PROCESO Max201;
     #201#12 = #0#11;
|FPRC;

|PROCESO Equipo66; |TIPO 66;
     |ABRE #201;
     |CONSULTA_F_CLAVE #3#201, Min201, Max201;
     |SI FSalida = 0;
          #0#14 = #201#0; |PINTA #0#14;
     |SINO;
          |ERROR;
     |FINSI;
     |CIERRA #201;
|FPRC;

|PROCESO MinLin201;
     #201#12 = #1#14;
|FPRC;

|PROCESO MaxLin201;
     #201#12 = #1#14;
|FPRC;

|PROCESO EquipoLin66; |TIPO 66;
     |ABRE #201;
     |CONSULTA_F_CLAVE #3#201, MinLin201, MaxLin201;
     |SI FSalida = 0;
          #1#15 = #201#0; |PINTA #1#15;
     |SINO;
          |ERROR;
     |FINSI;
     |CIERRA #201;
|FPRC;

|PROCESO RellenaObra210;
     #1#18 = #0#7;
     #1#20 = #0#3;
     |GRABA 020#1a;
     |LIBERA #1;
|FPRC;

|PROCESO Obra210_0; |TIPO 0;
     |ABRE #202;
     #202#0 = #0#3;
     #202#2 = #0#7;
     |LEE 000#202=;
     |SI #202#6 = "N";
          |MENAV "0000Obra Cerrada...";
          |ERROR;
     |SINO;
          |BUCLELIN 0 RellenaObra210 #1;
     |FINSI;
     |CIERRA #202;
|FPRC;

|PROCESO Obra210_66; |TIPO 66;
     aSql = "SELECT * FROM guerm202 INTO sq" + Usuario + ".def WHERE ";
     aSql = aSql + "6 == S AND 0 == " + #0#3;

     |SQL aSql;
     |SI FSalida = 0;
          |DDEF aDef; |QBF aDef;
          aDef = aDef + "sq" + Usuario + ".mas";
          |CARGA_DEF aDef, referen@;
          |SI FSalida = 0;
               |ABRE #referen@;
               |CONSULTA_CLAVE #1#referen@;
               |SI FSalida = 0;
                    #0#7 = #referen@#2;
                    #0#8 = #referen@#3;
                    |PINTA #0#7;
                    |PINTA #0#8;
               |SINO;
                    |ERROR;
               |FINSI;
               |CIERRA #referen@;
               |DELINDEX #referen@;
               |DESCARGA_DEF #referen@;
               |FBORRA aDef;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO PePrecio; |TIPO 0;
     |ABRE #204;
     #204#0 = #0#3;
     #204#1 = #0#7;
     #204#2 = #1#3;
     |LEE 101#204=;
     |SI FSalida ! 0;
          |ABRE #19;
          #19#0 = #1#3;
          |LEE 000#19=;
          |CIERRA #19;
          #204#3 = #0#4;
          #204#4 = #0#8;
          #204#5 = #19#1;
          |GRABA 020#204b;
     |FINSI;
     #204#6 = #1#7;
     #204#7 = #1#12;
     |GRABA 020#204a;
     |CIERRA #204;
|FPRC;

|PROCESO ActProv;
     |ABRE #19;
     #19#0 = #67#2;
     |LEE 020#19=;
     |CIERRA #19;

     fmes1 = #81#1 % A402;
     mes = fmes1 + 42;
     |SI #19#196 = 2;
          nImpo = (((((#67#41 * #67#6) < #67#8) < #67#27) < #67#57) < #81#5) < #81#17;
     |SINO;
          nImpo = (((((#67#5 * #67#6) < #67#8) < #67#27) < #67#57) < #81#5) < #81#17;
     |FINSI;
     |SI #142#114 = "S";
          nImpo = nImpo < #81#6;
     |FINSI;
     #112mes = #112mes + (nImpo * nForma);
     #112#55 = #112#55 + (nImpo * nForma);

     enImpPrvPed = nImpo * nForma;
     efFecha = #81#1;
     eaProve = #81#4;
     |HAZ AcumulaPrv;
|FPRC;

|PROCESO CreaLinPed;
     |ABRE #19;
     #19#0 = #1#3;
     |LEE 000#19=;
     |CIERRA #19;

     nLin = nLin + 1;
     |DDEFECTO #67;
     #67#28 = #81#25;
     #67#0 = #81#0;
     #67#1 = nLin;
     #67#2 = #19#0;
     #67#3 = 1;
     #67#4 = #1#4;
     #67#5 = #1#8;
     #67#6 = #1#7;
     #67#8 = #1#12;
     #67#14 = #19#30;
     #67#18 = #19#2;
     #67#20 = #112#0;
     #67#26 = #81#9;
     eaDocu = "agifa067";
     |HAZ StockEntra;
     |GRABA 020#67n;
|FPRC;

|PROCESO Actualiza;
     |SI #1#13 = "S"; |Y #1#17 = 0;
          #1#14 = #0#11;
          #1#15 = #0#14;
          #1#16 = #0#16;
          #1#17 = #0#17;
          |GRABA 020#1a;
     |FINSI;
     |LIBERA #1;
|FPRC;

|PROCESO CreaPedido;
     |ABRE #142; |LEE 000#142p; |CIERRA #142;

     |ABRE #112;
     #112#0 = #0#11;
     |LEE 000#112=;
     |CIERRA #112;

     |DFICE aDirEmp;
     |DFICB aDatos;
     aDirEmp = aDirEmp % -205;
     nCodEmp = aDirEmp;
     |PATH_DAT #41 aDatos;
     |ABRE #41;
     #41#0 = nCodEmp;
     |LEE 000#41=;
     |CIERRA #41;

     |ABRE #321; |LEE 000#321p; |CIERRA #321;

     |ABRE #81;
     |ABRE #67;
     |ABRE #324;
     #81#25 = #0#0;
     enSwMenu = 1;
     |HAZ ContaPC7;
     |GRABA 020#81b;
     |SI FSalida = 0;
          #81#4 = #112#0;
          #81#8 = #112#2;
          #81#9 = #112#17;
          #81#19 = #41#1;
          #81#18 = #41#2;
          #81#20 = #41#3;
          #81#21 = #41#4;
          #81#27 = #112#81;

          #324#0 = #81#27;
          |LEE 000#324=;

          #81#28 = #324#3;
          #81#29 = #112#82;
          #81#32 = #321#9;

          #324#0 = #81#32;
          |LEE 000#324=;

          #81#33 = #324#3;

          |BUCLELIN 2CreaLinPed#0;
          |HAZ CalCabAgifa081;
          |GRABA 020#81a;

          nForma = 1;
          |ABRE #112;
          #112#0 = #0#4;
          |LEE 101#112=;
          |SI FSalida = 0;
               |BUCLELIN 2 ActProv #0;
               |GRABA 020#112a;
          |FINSI;
          |CIERRA #112;

          #0#16 = #81#25; |PINTA #0#16;
          #0#17 = #81#0; |PINTA #0#17;
          #0#18 = #81#1;

          enCreaLineas = 0;
          eaSerie = #0#16;
          enNumero = #0#17;
          |SUB_EJECUTA_NP "guerm217";


          |BUCLELIN 2 Actualiza #0;
     |FINSI;
     |CIERRA #324;
     |CIERRA #67;
     |CIERRA #81;
|FPRC;

|PROCESO LinCreaPed;
     |SI #1#13 ! "S"; |ACABA; |FINSI;
     |ABRE #142; |LEE 000#142p; |CIERRA #142;

     |ABRE #112;
     #112#0 = #1#14;
     |LEE 000#112=;
     |CIERRA #112;

     |DFICE aDirEmp;
     |DFICB aDatos;
     aDirEmp = aDirEmp % -205;
     nCodEmp = aDirEmp;
     |PATH_DAT #41 aDatos;
     |ABRE #41;
     #41#0 = nCodEmp;
     |LEE 000#41=;
     |CIERRA #41;

     |ABRE #321; |LEE 000#321p; |CIERRA #321;

     |ABRE #81;
     |ABRE #67;
     |ABRE #324;
     #81#25 = #0#0;
     |SI #1#17 = 0;
          |C_M #1#17, 0, aAlfa;
          |SI aAlfa = "S";
               |CONFI "0000SCrear Pedido Subcontrata?";
               |SI FSalida = 0;
                    enSwMenu = 1;
                    |HAZ ContaPC7;
                    |GRABA 020#81b;
               |FINSI;
          |SINO;
               FSalida = 1;
          |FINSI;
     |SINO;
          #81#0 = #1#17;
          |LEE 000#81=
          |SI FSalida = 0;
               FSalida = 1; || Ya existe
          |SINO;
               |GRABA 020#81b; || Nuevo
          |FINSI;
     |FINSI;
     |SI FSalida = 0;
          #81#4 = #112#0;
          #81#8 = #112#2;
          #81#9 = #112#17;
          #81#19 = #41#1;
          #81#18 = #41#2;
          #81#20 = #41#3;
          #81#21 = #41#4;
          #81#27 = #112#81;

          #324#0 = #81#27;
          |LEE 000#324=;

          #81#28 = #324#3;
          #81#29 = #112#82;
          #81#32 = #321#9;

          #324#0 = #81#32;
          |LEE 000#324=;

          #81#33 = #324#3;

          |HAZ CreaLinPed;
          |HAZ CalCabAgifa081;
          |GRABA 020#81a;

          nForma = 1;
          |ABRE #112;
          #112#0 = #0#4;
          |LEE 101#112=;
          |SI FSalida = 0;
               |BUCLELIN 2 ActProv #0;
               |GRABA 020#112a;
          |FINSI;
          |CIERRA #112;

          #1#16 = #81#25; |PINTA #1#16;
          #1#17 = #81#0; |PINTA #1#17;

          enCreaLineas = 0;
          eaSerie = #1#16;
          enNumero = #1#17;
          |SUB_EJECUTA_NP "guerm217";
     |FINSI;
     |CIERRA #324;
     |CIERRA #67;
     |CIERRA #81;
|FPRC;

|PROCESO SubcontraLin; |TIPO 0;
     aAlfa = #1#13;
     |C_M #1#14, 1, aAlfa;
     |C_M #1#15, 1, aAlfa;
     |C_M #1#16, 1, aAlfa;
     |C_M #1#17, 1, aAlfa;
     |SI aAlfa = "N";
          #1#14 = "000000";
          |PINTA #1#14;
     |FINSI;
     |SI #1Campo ! Contenido; |Y aAlfa = "N";
          |ABRE #225;
          #225#0 = #1#14;
          #225#6 = #1#20;
          #225#1 = #1#18;
          #225#3 = #1#3;
          |LEE 000#225=;
          |SI FSalida = 0;
               #1#19 = #225#5;
               |PINTA #1#19;
          |FINSI;
          |CIERRA #225;
     |FINSI;
|FPRC;

|PROCESO Borra220;
     |BORRA 020#220a;
|FPRC;

|DEFBUCLE Borra220;
     #220#1;
     ;
     fDFecha, #1#0, #1#1, #1#2, 001;
     fHFecha, #1#0, #1#1, #1#2, 999;
     be;
     NULL, Borra220;
|FIN;

|PROCESO Check220;
     |SI #220#30 = "S";
          aAlfa = "0000Carga :" + #220#29 + " actualizada...";
          |MENAV aAlfa;
          aAct = "S";
     |FINSI;
|FPRC;

|DEFBUCLE Check220;
     #220#1;
     ;
     fDFecha, #1#0, #1#1, #1#2, 001;
     fHFecha, #1#0, #1#1, #1#2, 999;
     be;
     NULL, Check220;
|FIN;

|PROCESO Borra219;
     |BORRA 020#219a;
|FPRC;

|DEFBUCLE Borra219;
     #219#3;
     ;
     fDFecha, #1#0, #1#1, #1#2, 001;
     fHFecha, #1#0, #1#1, #1#2, 999;
     be;
     NULL, Borra219;
|FIN;

|PROCESO Tipo2Lin; |TIPO 2;
     nForma = 0 +. 1;
     nModo = (FEntrada / 100) ! 0;

     |SI #0#5 = "S";
          |ABRE #207;
          #207#0 = #0#6;
          |LEE 000#207=;
          |SI FSalida = 0;
               #1#6 = #207#11; || Tipo Facturacion
          |FINSI;
          |CIERRA #207;
     |FINSI;

     |SI nModo = 3;
          fDFecha = #1#9;
          fHFecha = #1#10;
          aAct = "N";
          |BUCLE Check220;
          |SI aAct = "S";
               |MENAV "0000No se modifica...";
               |ERROR;
          |SINO;
               |BUCLE Borra219;
               |BUCLE Borra220;
          |FINSI;
     |FINSI;
     |SI nModo = 2;
          |SI nForma = -1;
               fFecFinAnt = #1#10;
          |SINO;
               |SI fFecFinAnt > #1#10;
                    fDFecha = #1#10 + 1;
                    fHFecha = fFecFinAnt;
                    aAct = "N";
                    |BUCLE Check220;
                    |SI aAct = "S"
                         |MENAV "0000No se modifica...";
                         |ERROR;
                    |SINO;
                         |BUCLE Borra219;
                         |BUCLE Borra220;
                    |FINSI;
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO NomObra; |TIPO 0;
     nModo = (FEntrada % 100);
     |SI #0Campo ! Contenido; |O nModo = 1;
          |ABRE #202;
          #202#0 = #0#3;
          #202#1 = #0#7;
          |LEE 000#202=;
          |SI FSalida = 0;
               #0#8 = #202#3;
               |PINTA #0#8;
          |FINSI;
          |CIERRA #202;
     |FINSI;
|FPRC;

|PROCESO ActPreCos; |TIPO 0;
     |SI #1Campo ! Contenido;
          |ABRE #225;
          #225#0 = #1#14;
          #225#6 = #1#20;
          #225#1 = #1#18;
          #225#3 = #1#3;
          |LEE 000#225=;
          |SI FSalida = 0; |Y #1#19 ! #225#5;
               aAlfa = "0000Atención" + &13;
               aAlfa = aAlfa + "Cambio de precio de coste para el proveedor-articulo-obra";
               |MENAV aAlfa;
               |CONFI "0000SActualizar Precio de Coste?";
               |SI FSalida = 0;
                    #225#5 = #1#19;
                    |GRABA 020#225a;
               |FINSI;
          |FINSI;
          |CIERRA #225;
     |FINSI;
|FPRC;
