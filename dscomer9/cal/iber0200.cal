|FICHEROS;
     agifa505 #4;        ||Parametros CAJA
     iber0200 #6;        ||Acceso T.P.V
     agifa510 #7;        ||Fichero de Horarios
     agifa509 #8;        ||Resumen diario
     agifa066 #11; ||*
     iber0019 #19;
     agifa517 #30;       ||Parametros T.P.V
     dsm00032 #32;
     iber0040 #40;
     iber0041 #41;
     agifv029 #111;      ||Usuarios / Cajas.
     agifa142 #142;
     agift555 #555;
     iber0078 #78;
     iber0079 #79;
     iber0080 #80;
     iber0016;
     iber0042 #42;

     agifa007;
     agifa019;
     ib__arti;
     ib__kits;
     ib__ticd;
     ib__tick;
     ibxxtpas;
     ib__mesa;
     ib__clie;
     ib__apre;
     ib__sala;
     ib__extr;
     ib__rese;
     ib__fami;
     ib__desg;
     ib__comp;
     ib__coma;
     ib__cama;
     ib__faco;
     ib__secc;
     ib__perm;
     ib__data;
     ib__tari;
     ibxxtarj;
     ibxxtarl;
     ibxxbloq;
|FIN;

|VARIABLES;
     &eaClave="";

     &enEsIber        = 0;
     aAlf2            = "";
     &nElTicket       = 1;
     aImpreCPV        = "";
  {5}aValIni          = "";
     &eaIvaIber       = "";
     &enHayCentralCob = 0;
     &nImporteConPesetas = 0;      || mostrar columna pesetas 1-si 0-no
     &aTieneCaja2   = "";
     &enSwIber      = "";
     &aClaveNegativos = "";
     &aTarjeta      = "";
     &nPuntoDeVenta = 0;
     &aCentralCobro = "";
     &aIPCobro      = "";
     &aIPCocina     = "";
     &eaCentro      = "";
     &eaTipoTactil  = "";
     &aTipoTactil   = "";

     &eaNomDll      = "";
     &abre_dis      = "";
     &cier_dis      = "";
     &mens_dis      = "";

     &ferr_tarjeta  = "";      || Fichero de Error -> 'error'
     &oper_tarjeta  = "";      || Operacion
     &bat_tarjeta   = "";      || Fcihero Cobro
     &impr_tpv      = "";
     &empresa       = 0;
     &acaja         = 0;
     &afecha        = "";

     &enCaja        = 0;
     &efFecha       = @;

     nNume          = 0;
     aIP            = "";
     aEmp           = "";
     fNume          = 0;
     fAntes         = @;
     nErr           = 0;
     aAlf1          = "";
     aPath          = "";
     aAlfa          = "";
     aParam         = "";

     path           = "";
     camino         = "";
     mensaje        = "";
     puente         = "";
     i              = 0;
     nHay           = 0;
     fFecha         = @;
     aNom           = "";
     aNota          = "";
     nConta         = 0;
     nCentro        = 0;
     nSwTermi       = 0;

     aArtiB         = "";
     aTarjB         = "";
     aUnidB         = "";
     aPrecB         = "";
     aFic           = "";
     nTarjeta       = 0;
     nHandle        = 0;
     nLinB          = 0;
     nPrime         = 0;
     aExt           = "";
     aPathDll       = "";
     aOrg           = "";
     aPuertoPalm    = "";
     nPuertoPalm    = 0;
     &aFacturar     = "";     || se carga al entrar con 'X' para poder hacer albaranes
|FIN;
컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
 EL fichero 'tactil.ini' contiene:
  1. Usuario
  2. Clave
  3. Caja
  4. Provision
  5. Terminal
 Si alguna linea empieza por '#' no se tiene en cuenta
컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
|PROCESO LeeIni;
     |DFICE aAlfa;
     aAlfa = aAlfa % -205;
||     |DBASS aFic; |QBT aFic;
||     aFic = aFic + "bin/" + aAlfa + ".ini";

     aFic =  "/ds/bin/" + aAlfa + ".ini";

     IaValIni = aValIni1<-;
     |XABRE "CU", aFic, nHandle;
     |SI FSalida ] 0;
          |XLEE nHandle, 250, aAlfa;
          |PARA nConta = 0; |SI FSalida ] 0; |Y nConta < 5; |HACIENDO;
               |QBT aAlfa;
               aAlf1 = aAlfa % 101;
               |SI aAlf1 ! "#";
                    aValIni = aAlfa;
                    IaValIni = IaValIni + 1;
                    nConta = nConta + 1;
               |FINSI;
               |XLEE nHandle, 250, aAlfa;
          |SIGUE;
          |XCIERRA nHandle;
     |FINSI;
|FPRC;

|PROCESO DefIni; |TIPO 7;
     |C_M #6Campo, 0, aAlfa;
     |SI Campo = 4; |Y aValIni1 ! ""; |Y aAlfa = "S";
          #6Campo = aValIni1;
          |PTEC 802;
     |FINSI;
     |SI Campo = 7; |Y aValIni2 ! ""; |Y aAlfa = "S";
          #6Campo = aValIni2;
          |PTEC 802;
     |FINSI;
     |SI Campo = 0; |Y aValIni3 ! ""; |Y aAlfa = "S";
          #6Campo = aValIni3;
          |PTEC 802;
     |FINSI;
     |SI Campo = 11; |Y aValIni4 ! ""; |Y aAlfa = "S";
          #6Campo = aValIni4;
          |PTEC 802;
     |FINSI;
|FPRC;
컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
|PROCESO CopiaDll;
     |||| JUAN ... quitar tambien en iber0030
     fFecha = ~;
     |SI fFecha = "07.07.2003"; |ACABA; |FINSI;
     ||컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴

     |DBASE aPathDll;  aPathDll = aPathDll + "tactil/";

     aAlfa = ""; |IP_REMOTO aAlfa;

     |SI aAlfa ! "";
          aOrg = aPathDll + eaNomDll; |ENVIA_FICHERO aOrg, eaNomDll;
     |SINO;
          aOrg = aPathDll + eaNomDll; |COPIA_FICHERO aOrg, eaNomDll;
     |FINSI;
|FPRC;
컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
|PROCESO &NomeDat;

     aNom = "ib" + (("00" + nPuntoDeVenta) % -102);

     aAlfa= aNom + "arti";
     |CIERRAT  #ib__arti;
     |NOME_DAT #ib__arti#aAlfa#;
     |ABRE     #ib__arti;
     |CIERRA   #ib__arti;

     aAlfa= aNom + "kits";
     |CIERRAT  #ib__kits;
     |NOME_DAT #ib__kits#aAlfa#;
     |ABRE     #ib__kits;
     |CIERRA   #ib__kits;

     aAlfa= aNom + "ticd";
     |CIERRAT  #ib__ticd;
     |NOME_DAT #ib__ticd#aAlfa#;
     |ABRE     #ib__ticd;
     |CIERRA   #ib__ticd;

     aAlfa= aNom + "tick";
     |CIERRAT  #ib__tick;
     |NOME_DAT #ib__tick#aAlfa#;
     |ABRE     #ib__tick;
     |CIERRA   #ib__tick;

     aAlfa= aNom + "mesa";
     |CIERRAT  #ib__mesa;
     |NOME_DAT #ib__mesa#aAlfa#;
     |ABRE     #ib__mesa;
     |CIERRA   #ib__mesa;

     aAlfa= aNom + "clie";
     |CIERRAT  #ib__clie;
     |NOME_DAT #ib__clie#aAlfa#;
     |ABRE     #ib__clie;
     |CIERRA   #ib__clie;

     aAlfa= aNom + "apre";
     |CIERRAT  #ib__apre;
     |NOME_DAT #ib__apre#aAlfa#;
     |ABRE     #ib__apre;
     |CIERRA   #ib__apre;

     aAlfa= aNom + "sala";
     |CIERRAT  #ib__sala;
     |NOME_DAT #ib__sala#aAlfa#;
     |ABRE     #ib__sala;
     |CIERRA   #ib__sala;

     aAlfa= aNom + "extr";
     |CIERRAT  #ib__extr;
     |NOME_DAT #ib__extr#aAlfa#;
     |ABRE     #ib__extr;
     |CIERRA   #ib__extr;

     aAlfa= aNom + "rese";
     |CIERRAT  #ib__rese;
     |NOME_DAT #ib__rese#aAlfa#;
     |ABRE     #ib__rese;
     |CIERRA   #ib__rese;

     aAlfa= aNom + "fami";
     |CIERRAT  #ib__fami;
     |NOME_DAT #ib__fami#aAlfa#;
     |ABRE     #ib__fami;
     |CIERRA   #ib__fami;

     aAlfa= aNom + "desg";
     |CIERRAT  #ib__desg;
     |NOME_DAT #ib__desg#aAlfa#;
     |ABRE     #ib__desg;
     |CIERRA   #ib__desg;

     aAlfa= aNom + "comp";
     |CIERRAT  #ib__comp;
     |NOME_DAT #ib__comp#aAlfa#;
     |ABRE     #ib__comp;
     |CIERRA   #ib__comp;

     aAlfa= aNom + "coma";
     |CIERRAT  #ib__coma;
     |NOME_DAT #ib__coma#aAlfa#;
     |ABRE     #ib__coma;
     |CIERRA   #ib__coma;

     aAlfa= aNom + "cama";
     |CIERRAT  #ib__cama;
     |NOME_DAT #ib__cama#aAlfa#;
     |ABRE     #ib__cama;
     |CIERRA   #ib__cama;

     aAlfa= aNom + "faco";
     |CIERRAT  #ib__faco;
     |NOME_DAT #ib__faco#aAlfa#;
     |ABRE     #ib__faco;
     |CIERRA   #ib__faco;

     aAlfa= aNom + "secc";
     |CIERRAT  #ib__secc;
     |NOME_DAT #ib__secc#aAlfa#;
     |ABRE     #ib__secc;
     |CIERRA   #ib__secc;

     aAlfa= aNom + "perm";
     |CIERRAT  #ib__perm;
     |NOME_DAT #ib__perm#aAlfa#;
     |ABRE     #ib__perm;
     |CIERRA   #ib__perm;

     aAlfa= aNom + "data";
     |CIERRAT  #ib__data;
     |NOME_DAT #ib__data#aAlfa#;
     |ABRE     #ib__data;
     |CIERRA   #ib__data;

     aAlfa= aNom + "tari";
     |CIERRAT  #ib__tari;
     |NOME_DAT #ib__tari#aAlfa#;
     |ABRE     #ib__tari;
     |CIERRA   #ib__tari;
|FPRC;
컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
|PROCESO Usuario; |TIPO 0;
     |SI FSalida = 1; |PTEC 807; |FINSI;
     |DFICE aEmp;
     aEmp = aEmp % -205;
     aEmp = aEmp % 103;
     |ABRE #40;
     #40#0 = aEmp;
     #40#1 = #6#4;
     |LEE 000#40=;
     |SI FSalida ! 0; |ERROR; |FINSI;
     |CIERRA #40;
     |SI aEmp < 100;
          |MENAV "0000Numero Empresa INCORRECTO debe ser superior o igual a 100!";
     |FINSI;
     |SI PARAMETRO = "palm";
          |ABRE #42;
          #42#0 = aEmp;
          #42#1 = #6#4;
          |LEE 000#42=;
          |CIERRA #42;
          aAlf1 = #42#8; |QBF aAlf1;
          |IP_REMOTO aAlf2; |QBF aAlf2;
          |SI aAlf2 = ""; |IP_LOCAL aAlf2; |FINSI;
          |SI aAlf2 ! aAlf1;
               |MENAV "0000PC incorrecto...";
               |ERROR;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO UsuClave; |TIPO 0;
     |SI FSalida = 2; |ACABA; |FINSI;

     |SI FSalida = 1; |PTEC 807; |FINSI;

     |SI #6#7 ! #40#3;
          |AVISO;
          #6#7 = "";
          |ERROR;
     |SINO;
          eaClave = #40#3;
          #6#7 = "********"; |PINTA #6#7;
          #6#5 = #40#8; |PINTA #6#5;
          #6#8 = #40#4; |PINTA #6#8;
          #6#9 = #40#5; |PINTA #6#9;
          #6#6 = #40#6; |PINTA #6#6;
     |FINSI;
|FPRC;
컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
|PROCESO CargaFicTactil;
     |ABRE #19;
     #19#0 = eaCentro;
     #19#1 = nPuntoDeVenta;
     |LEE 140#19=;
     |SI FSalida ! 0;
          |PUSHV 0501 2380;
          |BLANCO 1316 1765;
          |CUADRO 1316 1765;
          |COLOR 2,0; |ATRI 0;
          |PINTA 1427, "CARGA FICHEROS EN PROCESO.";
          |PINTA 1527, "ESPERE UN MOMENTO...";
          |PARA FSalida = 1; |SI FSalida ! 0; |HACIENDO;
               |SLEEP 3;
               |LEE 140#19=;
          |SIGUE;
          |ATRI 0;
          |POPV;
     |SINO;
          |SI #19#9 = "N";
               |PUSHV 0501 2380;
               |BLANCO 1316 1765;
               |CUADRO 1316 1765;
               |COLOR 2,0; |ATRI 0;
               |PINTA 1427, "PROCESANDO FICHEROS.";
               |PINTA 1527, "ESPERE UN MOMENTO...";
               aAlfa = "iber0030" + &59 + #6#8;
               |SUB_EJECUTA_NP aAlfa;
               #19#9 = "S";
               |GRABA 020#19a;
               |POPV;
          |FINSI;
     |FINSI;
     |CIERRA #19;

     aImpreCPV = #19#12; |QBF aImpreCPV;
|FPRC;

|PROCESO MiraImporta;
     |ABRE #555; |LEE 000#555p; |CIERRA #555;
     aPath = #555#1; |QBF aPath;
     aAlfa = aPath % A0;
     |SI aAlfa [ 1; |ACABA; |FINSI;
     |ABRE #32;
     |SELECT #2#32;
     |_PDIR aPath;
     |PARA; |SI FSalida = 0; |HACIENDO;
          aAlfa = aPath % -112;
          #32#0 = "I";
          #32#5 = aAlfa;
          |LEE 000#32=;
          |SI FSalida ! 0;
               ||--------------------Chequeo el fichero debe se numerico
               nErr = 0;
               |PARA i = 101; |SI i [ 1200; |HACIENDO i = i + 100;
                    aAlf1 = (aAlfa % i);
                    |SI aAlf1 < "0"; |O aAlf1 > "9";
                         |SI i = 901; |Y aAlf1 = ".";
                         |SINO;
                              nErr = 1;
                              |SAL;
                         |FINSI;
                    |FINSI;
               |SIGUE;
               |SI nErr = 0;
                    nHay = 1; |SAL;
               |FINSI;
          |FINSI;
          |_SDIR aPath;
     |SIGUE;
     |CIERRA #32;
     |SI nHay = 1;
          |CONFI "0000SHay Ficheros a Importar. Ejecutar importacion S/N ";
          |SI FSalida = 0;
               enCaja = acaja;
               efFecha = afecha;
               |SUB_EJECUTA "agift550";
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO verif_iva;
     |ABRE #11;
     |PARA i = 0; |SI i [ 5; |HACIENDO i = i + 1;
          #11#0 = i;
          #11#4 = "01.01.0000";
          |LEE 001#11];
          |SI FSalida ! 0; |O #11#0 ! i;
               |DDEFECTO #11;
               #11#0 = i;
               |GRABA 021#11n;
          |FINSI;
     |SIGUE;
     |CIERRA #11;
|FPRC;

|PROCESO abre_caja;           ||Verifico que no exista el resumen diario
     |ABRE #8;
     |DDEFECTO #8;
     #8#63 = empresa;
     #8#0 = #6#0;
     #8#1 = #6#1;
     |LEE 001#8=;
     |SI FSalida = 0;
          mensaje = "0000ATENCION Caja cerrada, no puede introducir mas movimientos";
     |SINO;
          fFecha = ~;
          fNume = fFecha; fNume = fFecha - 1;
          fAntes = fNume;
          |SI #4#124 = "S";
               |SI #6#1 < fAntes; |O #6#1 > fFecha;
                    mensaje = "0000Fecha NO AUTORIZADA...";
                    |ACABA;
               |FINSI;
          |FINSI;

          |CUADRO 1915 2167;
          |ATRI I; |PINTA 2030, "Provision:"; |ATRI 0;
          |C_V #6#11, 1, "S";
          |ENCAMPO #11#6;

          |SI FSalida = 0;
               #8#2 = #6#11;              ||Provision
               #8#55 = #8#55 + #8#2;
               #8#56 = "S";           ||Caja abierta en diario
               #4#23 = "S";           ||caga abierta en Parametros
               #4#24 = #6#1;   ||Fecha de Apertura

               |GRABA 021#8n;
               |GRABA 021#4a;
          |SINO;
               mensaje = "0000Operacion Anulada por el Usuario";
          |FINSI;
     |FINSI;
     |CIERRA #8;
|FPRC;

|PROCESO al_entrar;
     |DFICE puente;      |QBF puente;
     puente = puente % -205;
     empresa = puente;
     |ABRE #30;              ||Compruebo existencia Parametros tpv
     |LEE 000#30p;
     |SI FSalida = 0;
          |ABRE #4;         ||Compruebo Existencia parametros caja
          #4#0 = #6#0;
          |LEE 101#4=;
          |SI FSalida = 0;
               oper_tarjeta = #30#8;        |QBF oper_tarjeta;
               path = #30#11;               |QBF path;
               bat_tarjeta = path + #30#9;  |QBF bat_tarjeta;
               ferr_tarjeta = path + "error";
               |HAZ Secuencias;
               |SI aImpreCPV ! "";
                    impr_tpv = aImpreCPV;
               |FINSI;
                              ||Caja abierta o de diferente fecha
               |SI #4#23 = "N";
                    |HAZ abre_caja;     ||Abro la caja en todos los ficheros
                    |SI mensaje = "";        ||Hasta ahora todo Correcto
                         |ABRE #7;    ||Verifico que exista el codigo
                         #7#0 = #4#18;   ||de horarios
                         |LEE 001#7=;
                         |SI FSalida ! 0;
                              mensaje = "0000ATENCION no existe el fichero de HORARIOS";
                         |SINO;
                              |HAZ verif_iva;
                         |FINSI;
                         |CIERRA #7;
                    |FINSI;
               |SINO;
                    #6#1 = #4#24;
               |FINSI;
          |SINO;
               mensaje = "0000El fichero PARAMETROS CAJA no existe";
          |FINSI;
          |LIBERA #4;
          |CIERRA #4;
     |SINO;
          mensaje = "0000El fichero PARAMETROS T.P.V no existe";
     |FINSI;
     |CIERRA #30;
|FPRC;

|PROCESO Inicio;
     |ABRE #142; |LEE 000#142p; |CIERRA #142;

     |HAZ al_entrar;
     |SI mensaje ! "";        ||Si ha habido algun error No deja continuar
          |MENAV mensaje;
     |SINO;
          acaja  = #6#0;
          afecha = #6#1;
          |SI impr_tpv ! "";
               |SI #4#84 = "S";
                    Impresora = "windows";
                    |IMPRE -1;
                    |IMPRIMESIN " ";
                    |FINIMP;
               |FINSI;
               |SI #4#21 = "S";
                    Impresora = impr_tpv;
                    |IMPRE -1;
                    |IMPRIMESIN " ";
                    |FINIMP;
               |FINSI;
          |FINSI;
          |SI #4#133 = "S";
               |HAZ MiraImporta;
          |FINSI;
          |HAZ NomeDat;
          nCentro = eaCentro;
          |HAZ CargaReserva;

          aFacturar = "";
          |SI  #4#43 = "N";         || Permite hacer albaranes
               aFacturar = "X";
          |FINSI;

          |HAZ IniciaTactil;

          |SI #4#95 = "S";
               aClaveNegativos = #4#153;
          |SINO;
               aClaveNegativos = "";
          |FINSI;

          enHayCentralCob = 0;
          |ABRE #19;
          #19#0 = eaCentro;
          #19#1 = 0;
          |LEE 000#19];
          |PARA; |SI  FSalida = 0; |Y #19#0 = eaCentro; |HACIENDO;
               |SI #19#10 = "S";
                    enHayCentralCob = 1;
               |FINSI;
               |LEE 000#19s;
          |SIGUE;
          |CIERRA #19;
          |ABRET #ibxxtarj;
          |ABRET #ibxxbloq;
          |HAZ Especifica;
          |CIERRAT #ibxxbloq;
          |CIERRAT #ibxxtarj;
     |FINSI;
|FPRC;

|PROCESO BuscaCocina;
     |ABRE #41;
     #41#0 = aEmp;
     #41#1 = "";
     |LEE 000#41];
     |PARA; |SI FSalida = 0; |Y #41#0 = aEmp; |HACIENDO;
          |SI #41#9 = "S";
               aIPCocina = #41#3 +"."+ #41#4 +"."+ #41#5 +"."+ #41#6;
               |SAL;
          |FINSI;
          |LEE 000#41s;
     |SIGUE;
     |CIERRA #41;
|FPRC;

|PROCESO BuscaCCobro;
     aIPCobro = "";
     |ABRE #41;
     #41#0 = aEmp;
     #41#1 = "";
     |LEE 000#41];
     |PARA; |SI FSalida = 0; |Y #41#0 = aEmp; |HACIENDO;
          |SI #41#20 = "S";
               aIPCobro = #41#3 +"."+ #41#4 +"."+ #41#5 +"."+ #41#6;
               |SAL;
          |FINSI;
          |LEE 000#41s;
     |SIGUE;
     |CIERRA #41;
|FPRC;

|PROCESO Tipo12; |TIPO 12;
     |SI FSalida ! 0; |ACABA; |FINSI;

     aIP = "";
     |IP_REMOTO aIP;  |QBT aIP;
     |SI aIP = "";
          |IP_LOCAL aIP;
     |FINSI;

     |SI aValIni5 ! "";  || Se informa el terminal en el .ini
          |ABRE #41;
          #41#0 = aEmp;
          #41#1 = aValIni5;
          |LEE 000#41=;
          |SI FSalida ! 0;
               aAlfa = "0000Terminal (.ini) incorrecto: [" + aValIni5 + "]";
               |MENAV aAlfa;
          |FINSI;
          |CIERRA #41;
     |SINO;
          |ABRE #41;
          #41#0 = aEmp;
          #41#1 = "";
          |LEE 000#41];
          aAlfa = "";
          nSwTermi = 0;
          |SI FSalida = 0; |Y #41#0 = aEmp;
               |PARA; |SI FSalida = 0; |Y #41#0 = aEmp; |HACIENDO;
                    aAlfa = #41#3 +"."+ #41#4 +"."+ #41#5 +"."+ #41#6;
                    |SI aAlfa = aIP;
                         nSwTermi =  1;
                         |SAL;
                    |FINSI;
                    |LEE 000#41s;
               |SIGUE;
          |SINO;
               |MENAV "0000No Existe Terminal...";
          |FINSI;
          |CIERRA #41;

          |SI nSwTermi > 1;
               |MENAV "0000Existen IP's iguales en Definicion de Terminales...";
               |ACABA;
          |FINSI;

          |SI nSwTermi = 0;
               |ABRE #41;
               #41#0 = aEmp;
               #41#1 = "";
               |LEE 000#41];
               |PARA; |SI FSalida = 0; |Y #41#0 = aEmp; |HACIENDO;
                    nNume = #41#3 +  #41#4 + #41#5 + #41#6;
                    |SI nNume = 0;
                         nSwTermi = 1;
                         |SAL;
                    |FINSI;
                    |LEE 000#41s;
               |SIGUE;
               |CIERRA #41;
               |SI nSwTermi = 0;
                    |MENAV "0000No existe Terminal...";
                    |ACABA;
               |FINSI;
          |FINSI;
     |FINSI;        || aValIni5 ! "";

     |SI #41#7 = "N";
          |MENAV "0000Terminal sin Caja...";
          |ACABA;
     |FINSI;
     #6#0 = #41#8; |PINTA #6#0;
     #6#10 = #41#1;|PINTA #6#10;

     |ABRE #19;
     #19#0 = aEmp;
     #19#1 = #6#8;
     |LEE 000#19=;
     |SI FSalida ! 0;
          |MENAV "0000No Existe Punto Venta...";
          |ACABA;
     |FINSI;
     |CIERRA #19;
     aCentralCobro = #19#10;
     eaTipoTactil = #19#2;
     eaIvaIber = #19#11;

     |SI eaTipoTactil = "C"; |O #iber0016#29 = "S";
          |C_M #6#0, 1, "S";
          |ENCAMPO #0#6;
          |SI FSalida ! 0; |ACABA; |FINSI;
     |FINSI;

     |SI PARAMETRO = "";
          aTipoTactil = eaTipoTactil;
          |SI #19#2 = "A"; |O #19#2 = "C"; |O #19#2 = "T";
               aTipoTactil = "T";
               eaNomDll = "dsbar1.dll";
          |FINSI;
          |SI #19#2 = "B"; eaNomDll = "dsbocata1.dll"; |FINSI;
          |SI #19#2 = "R"; eaNomDll = "dsrestau1.dll"; |FINSI;
     |SINO;
          eaNomDll = PARAMETRO;
          |SI eaNomDll = "palm";
               aPuertoPalm = #iber0200#4;
               |QBF aPuertoPalm;
               eaNomDll = "DSITCP:" + aPuertoPalm;

               nPuertoPalm = aPuertoPalm;
               |SI nPuertoPalm < 3300;|O nPuertoPalm > 3500;
                    |MENAV "0000Rango de puertos validos: 3300-3500";
                    |MENAV "0000Recuerde que el 'Usuario' es el puerto de comunicaciones.";
                    |PTEC 807;
               |FINSI;
          |FINSI;
     |FINSI;

     |HAZ CopiaDll;

     nPuntoDeVenta = #6#8;
     |DFICE eaCentro;
     eaCentro = eaCentro % -403;
     |HAZ CargaFicTactil;
     |HAZ BuscaCocina;
     |HAZ BuscaCCobro;
     |SI aCentralCobro = "S";
          aIPCobro = "";
     |FINSI;
     |HAZ Inicio;
|FPRC;

|PROGRAMA;
     |DFICE eaCentro;
     eaCentro = eaCentro % -403;
     |ABRE #iber0016;
     #iber0016#0 = eaCentro;
     |LEE 000#iber0016.=
     |CIERRA #iber0016;

     |HAZ EsIber21;
     enEsIber = FSalida;

     |HAZ Divisa;
     |HAZ LeeIni;

     |ABRE #30;
     |LEE 000#30p;
     |SI FSalida = 111;
          |MENAV "0000Parametros TPV no Definido...";
          |PTEC 807;
     |FINSI;
     |CIERRA #30;

     |DDEFECTO #agifa007;
     |ABRE #agifa007;
     #agifa007#26 = #30#1;
     |LEE 000#agifa007.=;
     |CIERRA #agifa007;
     aTieneCaja2 = #agifa007#37;

     |CLS;
     |PINPA #0#6;
     |PINDA #0#6;
     |ENDATOS #1#6;
|FPRO;
컴컴컴컴컴컴컴컴컴컴컴컴컴LLAMADAS DE LA DLL's
|PROCESO GrabaBascula;
     |SI nPrime = 0;
          nPrime = 1;
          aAlfa = aAlfa % 13;
     |FINSI;

     aArtiB = aAlfa % 2104;
     aTarjB = aAlfa % 2906;
     aUnidB = aAlfa % 3909;
     aPrecB = aAlfa % 4808;

     #ibxxtarj#0 = aTarjB;
     #ibxxtarj#1 = 9999;
     |LEE 000#ibxxtarj.];
     |SI FSalida = 0;
          |LEE 000#ibxxtarj.a;
     |SINO;
          |LEE 000#ibxxtarj.u;
     |FINSI;
     |SI FSalida = 0;|Y #ibxxtarj#0 = aTarjB;
          nLinB = #ibxxtarj#1 + 1;
     |SINO;
          nLinB = 1;
     |FINSI;

     |DDEFECTO #agifa019;
     #agifa019#128 = aArtiB;  || C.B
     |LEE 000#agifa019.=;

     aAlfa = #agifa019#110; |QBF aAlfa;
     |SI aAlfa = ""; aAlfa = #agifa019#1; |FINSI;

     |DDEFECTO #ibxxtarj;
     #ibxxtarj#0 = aTarjB;
     #ibxxtarj#1 = nLinB;
     #ibxxtarj#2 = #agifa019#0;
     ||#ibxxtarj#3 = aPrecB; #ibxxtarj#3 = #ibxxtarj#3 / 100;
     #ibxxtarj#3 = aPrecB; #ibxxtarj#3 = #ibxxtarj#3 / 10000;
     #ibxxtarj#4 = aUnidB; #ibxxtarj#4 = #ibxxtarj#4 / 1000;
     #ibxxtarj#5 = aFic - aPath;
     #ibxxtarj#7 = aAlfa;
     #ibxxtarj#11 = "B";
     |GRABA 020#ibxxtarj.n;
|FPRC;

|PROCESO BorraTarj;
     |LEE 101#ibxxtarj.p;
     |PARA; |SI FSalida = 0; |HACIENDO;
          |SI #ibxxtarj#11 = "B";
               |BORRA 020#ibxxtarj.a;
          |SINO;
               |LIBERA #ibxxtarj;
          |FINSI;
          |LEE 101#ibxxtarj.s;
     |SIGUE;
|FPRC;

|PROCESO &RecogeBascula;
     nLinB = 0;
     |HAZ BloqueaTarj;

     |HAZ BorraTarj;

     |ABRE #agifa019;
     |SELECT #4#agifa019;
     aPath = "/basculas/sxcash/";
     aFic = aPath;
     |_PDIR aFic;
     |PARA; |SI FSalida = 0; |HACIENDO;
          aExt = aFic % -103;
          |SI aExt ] 1; |Y aExt [ 366;
               nPrime = 0;
               |XABRE "U", aFic, nHandle;
               |SI FSalida = 0;
                    |XLEE nHandle, 250, aAlfa;
                    |PARA; |SI FSalida > 70; |HACIENDO;
                         |HAZ GrabaBascula;
                         |XLEE nHandle, 250, aAlfa;
                    |SIGUE;
                    |XCIERRA nHandle;
               |FINSI;
          |FINSI;
          |_SDIR aFic;
     |SIGUE;
     |CIERRA #agifa019;
|FPRC;

|PROCESO &LiberaBascula;
     aTarjeta = ("000000" + aTarjeta) % -106;

     aPath = "/basculas/sxcash/";

     #ibxxtarj#0 = aTarjeta;     ||---> aTarjeta lo manda la dll
     #ibxxtarj#1 = 0;
     |LEE 101#ibxxtarj.];
     |PARA; |SI FSalida = 0; |Y #ibxxtarj#0 = aTarjeta; |HACIENDO;
          aFic = #ibxxtarj#5; |QBF aFic;
          |SI aFic ! "";
               aFic = aPath + aFic;
               |FBORRA aFic;
          |FINSI;
          |BORRA 020#ibxxtarj.a;
          |LEE 101#ibxxtarj.s;
     |SIGUE;

     |ABRE #ibxxtarl;
     #ibxxtarl#0 = aTarjeta;     ||---> aTarjeta lo manda la dll
     #ibxxtarl#1 = 0;
     #ibxxtarl#4 = 0;
     |LEE 101#ibxxtarl.];
     |PARA; |SI FSalida = 0; |Y #ibxxtarl#0 = aTarjeta; |HACIENDO;
          |BORRA 020#ibxxtarl.a;
          |LEE 101#ibxxtarl.s;
     |SIGUE;
     |CIERRA #ibxxtarl;
|FPRC;
