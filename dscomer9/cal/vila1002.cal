     Factu1Pago  -> Creacion facturas/recibos/cobros 1§ Pago

|FICHEROS;
     vilam002 #0;
     vilam003 #1;
     vilaz003 #3,MANTE;
     vilam005 #5;
     agifa019 #19;
     dsm00020 #20;
     agifa024 #24;
     agifa025 #25;
     dsm00003 #49,MANTE;
     agifa069 #69;
     agifa084 #84;
     agifa091 #91;
     vilam001 #100;
     tempo134 #134;
     agifa142 #142;
     agifa321 #321;
     agifa324 #324;

     agifa133 #17;  || vencimientos
     agifa177 #99;  || tipos de recibo
     dsz99984 #11;

     :/dscarter/def/dscam003 #30;
     :/dscarter/def/dscam004 #40;
     :/dscarter/def/dscam045 #45;
|FIN;

|VARIABLES;
     &enTiva = 0;
     &efFiva = @;
     &eaDiva = "";
     &enIva = 0;
     &enRec = 0;
     &eaCuenta = "";
     &enErrCta = 0;
     &enSwMenu = 0;
     &enError = 0;
     &aDivisa = "";
     &aMoneda = "";
     &eaProg = "";
     &enDirEnt = 0;
     &Tempo = "";
     &eaFichero = "";
     &enForma = 0;
     &enCoste = 0;
     &enImpCliCom = 0;
     &enImpCliDto = 0;
     &enImpCliMar = 0;
     &enImpCliFac = 0;
     &enImpRepComi = 0;
     &enImpRepVta = 0;
     &enImpRepRete = 0;
     &eaRepre = "";
     &eaCliente = "";
     &efFecha = @;
     &enEmpCartera = 0;
     &enEmpGestion = 0;
     aTmp134 = "";
     retencion = 0;
     nPrecio = 0;
     nLin = 0;
     nCampo = 0;
     fmes = "";
     mes = 0;
     imneto = 0;
     nImpo = 0;
     nForma = 0;
     nDto = 0;
     nVto = 0;
     nRecibo = 0;
     nCambio = 0;
     nCobro = 0;
     nEmpCarte = 0;
     aEmp = "";
     aAlfa = "";
     aBase = "";
     nEmpresa = 0;
     nEmpresaGestion = 0;
     aAlfaImp = "";
     nImp = 0;
     fCob = @;
     aCta = "";
|FIN;

|PROCESO Recibo;
     |DDEFECTO #324;
     #324#0 = #84#65;
     |LEE 000#324=;
     nCambio = #324#2;

     |ABRE #30;
     |ABRE #40;
     |SELECT #12#30;
     #30#0 = #84#48;
     #30#1 = #84#0;
     #30#2 = 0;
     #30#4 = "01.01.0000";
     #30#5 = "";
     #30#32 = "";
     |LEE 000#30];
     |SI FSalida = 0; |Y #30#0 = #84#48; |Y #30#1 = #84#0;
          #40#17 = #30#40;
          #40#3 = 99;
          |LEE 000#40];
          |SI FSalida = 0;
               |LEE 000#40a;
          |SINO;
               |LEE 0000#40u;
          |FINSI;
          |SI FSalida = 0; |Y #40#17 = #30#40;
               nCobro = #40#3 + 1;
          |SINO;
               nCobro = 1;
          |FINSI;
          |DDEFECTO #40;
          #40#17 = #30#40;
          #40#0 = #30#0;
          #40#1 = #30#1;
          #40#2 = #30#2;
          #40#3 = nCobro;
          #40#4 = #0#15; || cta
          #40#5 = "COB"; || Operacion
          #40#6 = #84#1;

          #40#7 =  #84#41; || Liquidado
          #40#25 = #40#7 * nCambio;

          #40#8 = #84#41;     || Cobrado
          #40#26 = #40#8 * nCambio;

          #40#10 = 0;     || Gastos
          #40#28 = #40#10 * nCambio;

          #40#12 = 0;     || Gastos nego
          #40#29 = #40#12 * nCambio;

          #40#33 = nCambio;
          |GRABA 020#40n;

          #30#23 = #30#23 + #84#41;      || Liquidado
          #30#61 = #30#23 * nCambio;

          #30#24 = 0;      || Gastos
          #30#62 = #30#23 * nCambio;

          #30#31 = #30#31 - #84#41;      || Pendiente
          #30#65 = #30#31 * nCambio;

          |SI #30#31 [ 0;
               #30#14 = "L";
               #30#15 = "Recibo Liquidado";
          |FINSI;
          |GRABA 020#30a;
     |FINSI;
     |CIERRA #40;
     |CIERRA #30;
|FPRC;

|PROCESO CtrlRecibos;
     |SI nEmpCarte ! #45#6;
          nEmpCarte = #45#6;

          aEmp = ("00000" + #45#6) % -105;
          aAlfa = aBase + "dscarter/fich/" + aEmp + "/";
          |PATH_DAT #30#aAlfa#;
          |PATH_DAT #40#aAlfa#;
          |HAZ Recibo;
     |FINSI;
|FPRC;

|DEFBUCLE CtrlRecibos;
     #45#1;
     ;
     nEmpresa, 001;
     nEmpresa, 999;
     ;
     NULL, NULL, NULL, NULL, NULL, CtrlRecibos;
     #45#6;
|FIN;

|PROCESO CreaCobro;
     |DBASS aBase;
     aAlfa = aBase + "dscarter/fich/";
     |PATH_DAT #45 aAlfa;

     |DFICE aAlfa;
     aAlfa = aAlfa % -205;
     nEmpresa = aAlfa;
     nEmpCarte = 0;
     |BUCLE CtrlRecibos;
|FPRC;

|PROCESO CreaRecibo;
     |ABRE #17;
     #17#0 = #84#0;
     #17#4 = #84#48;
     #17#1 = 0;
     |LEE 001#17];
     nVto = 0;
     |SI FSalida ! 0; |O #17#0 ! #84#0;  |O #17#4 ! #84#48;
         |HAZ VenciDir;
     |FINSI;

     |ABRE #99;
     nVto = 0;
     #17#0 = #84#0;
     #17#4 = #84#48;
     #17#1 = 0;
     |LEE 001#17];
     |PARA ; |SI FSalida = 0; |Y #17#0 = #84#0;|Y #17#4 = #84#48; |HACIENDO;
          nVto = nVto + 1;
          |LEE 001#17s;
     |SIGUE;

     |HAZ AbreRecVenta;

     nRecibo = 0;
     #17#0 = #84#0;
     #17#4 = #84#48;
     #17#1 = 0;
     |LEE 001#17];
     |PARA ; |SI FSalida = 0; |Y #17#0 = #84#0;|Y #17#4 = #84#48; |HACIENDO;
          |DDEFECTO #11;
          #11#17 = #84#48;
          #11#0  = #84#0;
          #11#1  = #17#1;           || Linea;
          #11#2  = #84#3;            || cliente
          #11#3  = #84#4;            || nombre del cliente
          #11#4  = #24#29;           || Banco
          #11#5  = "S";             || domiciliado
          #11#6  = "N";             || aceptado
          #11#7  = #84#1;            || fecha de emision
          #11#8  = #17#3;           || Fecha de vencimiento
          #11#9  = #17#2;           || Importe
          #11#10 = "N";            || Impreso
          #11#11 = "N";            || Contabilizado
          #11#12 = #84#40;

          |DDEFECTO #91;
          |ABRE #91;
          #91#0 = #84#8;
          |LEE 000#91=;
          |CIERRA #91;

          |SI #142#63 = "S";
               #99#0 = #24#69;
               |LEE 000#99=;
               |SI FSalida = 0;
                    #11#12 = #99#3;     || NUMERO CTA.CONTABLE.
               |SINO;
                    #11#12 = #84#40;     || NUMERO CTA.CONTABLE.
               |FINSI;
          |SINO;
               #11#12 = #84#40;          || NUMERO CTA.CONTABLE.
          |FINSI;
          #11#13 = "N";            || Cobrado
          #11#14 = "N";            || Impagado
          #11#15 = #24#161;         || A aceptar
          #11#16 = "01.01.0000";   || fecha de cobro
          #11#18 = #84#40;          || Cta
          #11#21 = #91#69;          || Tipo de Recibo (forma de pago)
          nRecibo = nRecibo + 1;
          |SI nRecibo = nVto; |Y #24#70 ! 0;
               #11#21 = #91#73;     || Tipo de Recibo (Fianza retenida)
          |FINSI;
          #11#22 = "Pdte. Cobro";
          #11#23 = nVto;
          #11#24 = #91#0;
          #11#25 = #91#1;

          #11#26 = "";
          #11#27 = "";
          #11#29 = "";
          #11#28 = "";

          eaProg = "agifa084";
          |HAZ RecVenta;
          |LEE 001#17s;
     |SIGUE;
     |CIERRA #17;
     |CIERRA #99;
     enDirEnt = #agifa084#91; |HAZ CierraRecVenta;
|FPRC;

|PROCESO ActCliRep;
     |ABRE #24;
     #24#0 = #84#3;
     |LEE 121#24=;
     |SI 0 = FSalida;
          fmes = #84#1 % A402;
          mes = fmes - 1;
          mes = mes * 4;
          mes = mes + 54;
          imneto = #84#15 - #84#25;
          |SI #142#115 = "N";
               nImpo = (imneto * 100) / (100 - #84#7);
               nDto = #84#25 + (imneto - nImpo);
          |SINO;
               nImpo = imneto;
               nDto = #84#25;
          |FINSI;
          #24mes = #24mes +. nImpo; || imneto;
          mes = mes + 1;
          #24mes = #24mes +. nDto;  || #84#25;
          mes = mes + 1;
          #24mes = #24mes +. #84#37;
          #24#102 = #24#102 +. nImpo;
          #24#103 = #24#103 +. nDto;
          #24#104 = #24#104 +. #84#37;
          mes = FEntrada / 100;
          mes = mes ! 0;
          |SI 1 = mes;
               #24#45 = #84#1;
               #24#46 = imneto;
          |FINSI;
          #24#110 = #24#110 +. imneto;
          imneto = imneto + #84#24;
          |GRABA 020#24a;
          |LIBERA #24;

          enImpCliCom = nImpo * nForma;
          enImpCliDto = nDto * nForma;
          enImpCliMar = #84#37 * nForma;
          enImpCliFac = imneto * nForma;
          eaCliente = #84#3;
          efFecha = #84#1;
          |HAZ AcumulaCli;
     |FINSI;
     |CIERRA #24;

     |ABRE #25;
     #25#0 = #84#6;
     |LEE 121#25=;
     |SI 0 = FSalida;
           imneto = #84#15 - #84#25;
           fmes = #84#1 % A402;
           mes = fmes - 1;
           mes = mes * 2;
           mes = mes + 11;
           #25mes = #25mes +. #84#26;
           retencion = #25mes % #84#39;
           mes = mes + 1;
           #25mes = #25mes +. imneto;
           #25#35 = #25#35 +. #84#26;
           #25#36 = #25#36 +. imneto;
           mes = fmes - 1;
           mes = mes + 40;
           #25mes = retencion;
           retencion = #25#35 % #25#39;
           #25#52 = retencion;
           |GRABA 020#25a;
           |LIBERA #25;

          enImpRepComi = #84#26 * nForma;
          enImpRepVta = imneto * nForma;
          enImpRepRete = retencion * nForma;
          eaRepre = #84#6;
          efFecha = #84#1;
          |HAZ AcumulaRep;
     |FINSI;
     |CIERRA #25;
|FPRC;

|PROCESO LinFactu;
     |ABRE #19;
     #19#0 = #0#27;
     |LEE 000#19=;
     |CIERRA #19;

     nPrecio = #0#19 * #1#3 / 100;
     nPrecio = nPrecio / (1+(#3#3/100));     ||Quita IVA

     |PARA nLin = 1; |SI nLin [ 6; |HACIENDO nLin = nLin + 1;
          nCampo = nLin + 2;

          aAlfa = #0nCampo; |QBF aAlfa;
          |SI aAlfa ! "";
               |DDEFECTO #69;
               #69#20 = #84#48;
               #69#0 = #84#0;
               #69#1 = nLin;
               #69#2 = "";
               #69#3 = 1;
               #69#4 = #0nCampo;
               |SI nLin = 1;
                    #69#2 = #19#0;
                    #69#5 = 1;
                    #69#6 = nPrecio;
                    #69#26 = nPrecio;
               |FINSI;
               #69#11 = #3#2;
               #69#13 = #19#2;
               #69#15 = #84#3;
               #69#16 = #84#34;
               #69#17 = #84#35;
               |HAZ TotalLin69;
               enForma = 1;
               |HAZ AcumulaFC;
               #69#14 = enCoste;
               |GRABA 020#69n;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO CabFactu;
     #84#1 = #3#4;
     #84#3 = #24#0;
     #84#4 = #24#1;
     #84#6 = #25#0;
     #84#8 = #3#1;
     #84#9 = #20#1;
     #84#29 = #24#1;
     #84#40 = #24#16;
     #84#91 = 1;

     |ABRE #49;
     #49#0 = #84#3;
     #49#1 = #84#91;
     |LEE 000#49=;
     |CIERRA #49;

     #84#29 = #49#10;
     #84#30 = #49#2;
     #84#31 = #49#3;
     #84#33 = #49#7;
     #84#64 = #49#6;
     |SI #49#14 = "  ";
         #84#6 = #24#25;
     |SINO;
         #84#6 = #49#14;
     |FINSI;

     |SI #142#229 = "F";
          #84#30 = #24#8;
          #84#31 = #24#9;
          #84#64 = #24#183;
          #84#33 = #24#10;
     |FINSI;
     |SI #142#229 = "C";
          #84#30 = #24#11;
          #84#31 = #24#12;
          #84#64 = #24#186;
          #84#33 = #24#13;
     |FINSI;

     #84#34 = #25#7;
     #84#35 = #24#4;
     #84#60 = #24#15;

     #84#65 = #24#192;
     #324#0 = #84#65;
     |LEE 000#324=;
     #84#66 = #324#2;
     #84#67 = #24#193;
     #84#68 = #321#9;
     #324#0 = #84#68;
     |LEE 000#324=;
     #84#69 = #324#2;
     #84#78 = #20#0;
     #84#88 = #24#22;
     #84#89 = #24#23;
     #84#90 = #24#24;
     #84#88 = #24#22;
|FPRC;

|PROCESO CreaFac1Pago;
     |ABRE #24;
     #24#0 = #1#4;
     |LEE 020#24=;
     |CIERRA #24;
     |ABRE #25;
     #25#0 = #24#25;
     |LEE 020#25=;
     |CIERRA #25;
     |ABRE #20;
     #20#0 = #24#202;
     |LEE 000#20=;
     |CIERRA #20;

     #84#48 = #3#0;
     enSwMenu = 1;
     |HAZ ContaFD7;
     |GRABA 020#84b;
     |SI FSalida = 0;
          |HAZ LimCabAgifa084;
          |HAZ CabFactu;

          aDivisa = #84#65;
          aMoneda = #84#67;
          |HAZ Divisas084;

          |HAZ LinFactu;
          |BUCLELIN 0 CalCabAgifa084 #84;

          #84#10 = "S";  || Se pone como impresa porque aqui se crean
                         || los recibos
          |GRABA 020#84a;
          |LIBERA #84;

          nForma = 1;
          |HAZ ActCliRep;

          |HAZ CreaRecibo;

          |||HAZ CreaCobro;        ANULADO

          |DFICE aAlfa;
          nImp = #84#41;
          fCob = #84#1;
          aCta = #0#15;
          aEmp = aAlfa % -205;
          aAlfa = ":dscarter/dscaz001.tab;" + #84#48 + ":" + #84#0 + ":" + "1";
          aAlfa = aAlfa + ":" + #84#1 + ":" + nImp + ":" + fCob + ":" + aCta + ":" + aEmp;
          |SUB_EJECUTA aAlfa;

          ||Historico
          #5#0 = #0#0;
          #5#1 = #0#1;
          #5#2 = #84#3;
          #5#3 = 0;
          #5#4 = #84#48;
          #5#5 = #84#0;
          #5#6 = #84#1;
          #5#7 = #0#19 * #1#3 / 100;
          |GRABA 020#5n;

          #134#0 = #84#48;
          #134#1 = #84#0;
          #134#2 = #84#3;
          |GRABA 020#134n;
     |FINSI;
|FPRC;

|PROCESO ImprimeRecs;
   enEmpCartera = #dscam045#6;
   enEmpGestion = #dscam045#0;
   |DFICE aAlfaImp; |QBF aAlfaImp; aAlfaImp = aAlfaImp + aTmp134 + ".dat";
   aAlfaImp = ":dscarter/dscaz029.tab;" + aAlfaImp;
   |SUB_EJECUTA aAlfaImp;
|FPRC;

|DEFBUCLE ImprimeRecs;
#dscam045#1;
2;
nEmpresaGestion, 001, "V";
nEmpresaGestion, 999, "V";
;
NULL, ImprimeRecs;
|FIN;

|PROCESO CheqCta1; |TIPO 0;
     eaCuenta = #3#5;
     |HAZ CheqCta0;
     #3#5 = eaCuenta; |PINTA #3#5;
     |SI enErrCta ! 0; |ERROR; |FINSI;
|FPRC;

|PROCESO ConsuCta1; |TIPO 66;
     eaCuenta = #3#5;
     |HAZ ConsuCta0;
     #3#5 = eaCuenta; |PINTA #3#5;
     |SI enErrCta ! 0; |ERROR; |FINSI;
|FPRC;

|PROCESO FecFac; |TIPO 0;
     enTiva = #3#2;
     efFiva = #3#4;
     |HAZ SacaIVA;
     #3#3 = enIva; |PINTA #3#3;
|FPRC;

|PROCESO Factu1Pago;
     |ABRE #142; |LEE 000#142p; |CIERRA #142;
     |ABRE #100; |LEE 000#100p; |CIERRA #100;
     |ABRE #321; |LEE 000#321p; |CIERRA #321;

     enTiva = #100#3;
     efFiva = ~;
     |HAZ SacaIVA;

     |PUSHV 0501 2380;
     |BLANCO 1426 2356;
     |DDEFECTO #3;
     #3#0 = #100#1;
     #3#1 = #100#4;
     #3#2 = enTiva;
     #3#3 = enIva;
     #3#5 = #0#15;
     |PINPA #0#3;
     |PINDA #0#3;
     |ENDATOS #1#3;
     enError = FSalida;
     |SI FSalida = 0;
          |HAZ CreaTempo; |NOME_DAT #134 Tempo; |DELINDEX #134; aTmp134 = Tempo;

          |ABRE #84;
          |ABRE #69;
          |ABRE #5;
          |ABRE #134;
          |BUCLELIN 2 CreaFac1Pago #0;
          |CIERRA #134;
          |CIERRA #5;
          |CIERRA #69;
          |CIERRA #84;

          |CONFI "0000N¿Imprimir facturas?";
          |SI FSalida = 0;
               eaFichero = aTmp134;
               |SUB_EJECUTA_NP "agifa086";
          |FINSI;
          |CONFI "0000N¿Imprimir recibos de entrega?";
          |SI FSalida = 0;
               nEmpresaGestion = #48#0;
               |DBASS aAlfa; |QBF aAlfa;
               aAlfa = aAlfa + "dscarter/fich/"; |PATH_DAT #45 aAlfa;
               |ABRE #45;
               |BUCLE ImprimeRecs;
               |CIERRA #45;
          |FINSI;
          Tempo = aTmp134; |HAZ BoraTempo; |DELINDEX #134;
     |FINSI;
     |POPV;
|FPRC;
