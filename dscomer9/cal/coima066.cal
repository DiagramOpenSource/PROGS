|FICHEROS;
     coima065 #0;
     coima066 #1;
     agifa019 #2;   || articulos
     agifa017 #3;   || almacenes
     agifa142 #142;
     dsm00241 #241;
     dsm00238 #238;
     agifa048@ #480;
     coima002 #20;
     coima054 #54;
     coima059 #59;

     referen@;
|FIN;

|VARIABLES;
     &eaFamilia = "";
     &eaLinea = "";
     aPath = "";
     i = 0;
     aAlfa1 = "";
     nLin = 0;
     aArtiAnt = "";
     nLinea = 0;
     nCampo = 0;
     nImpo = 0;
     nImpo2 = 0;
     nUni = 0;
     nAlma = 0;
     nModo = 0;
     nUnidad = 0;
     &eaMate = "";
     &eaEscan = "";
     &enTecla = 0;
     &enTipo = 0;
     nForma = 0;
     aTipo = "";
     nNume1        = 0;
     nNume2        = 0;
     nNume         = 0;

     &nDeci_EsCab = 0;
     &nDeci_EsLin = 0;

     nMerma         = 0;
     swdes          = 0;
     &nModcab       = 0;
     &nDec          = 0;
     vacio          = " " * 20;
     impcompone     = 0 ;
     fiac           = " ";
     paac           =  0 ;
     &aLargo        = "N";
     &nUniLargo     = 0;
     &nLargo        = 0;
     &nAncho        = 0;
     &nGrosor       = 0;
     &nResultado    = 0;
     &nTipoLargo    = 0;
     &nExitAlterna  = 0;
     unifab         = 0;
     modo           = 0;
     resp           = 0;
     aAlfa          = "";
|FIN;

|INCLUYE i_varart

|PROCESO DefeUniPre;
     |HAZ LeeConverUni;
     |SI Campo = 9; |ACABA; |FINSI;
     |SI #241#6 = "D";
          nNume = #2#9 * #241#5;
          #1#15 = nNume;
     |SINO;
         nNume = #2#9 / #241#5;
         #1#15 = nNume;
     |FINSI;
     |PINTA #1#15;
|FPRC;

|PROCESO Llena049; |TIPO 6;
     enAlta = 1;
     eaArticulo = #1Campo;
     |HAZ LlenaArticulo;
     |SI FSalida = 0;
          #1Campo = eaArticulo;
          |PINTA #1Campo;
     |SINO;
          |ERROR; |ACABA;
     |FINSI;
|FPRC;

|PROCESO escartic; |TIPO 0;
     nModo = (FEntrada / 100) ! 0;

     |SI nModo = 1; |O #1Campo ! Contenido;
          |ABRE #2;
          #2#0 = #1#2;
          |LEE 020#2=;
          |CIERRA #2;

          |HAZ DefeUniPre;
          eaArticulo = #1#2;
          eaCliente = "";
          |SUB_EJECUTA_NP "dsz99919;D";
          #1#18 = enMedida1;
          #1#19 = enMedida2;
          #1#20 = enMedida3;

          |ABRE #59; |SELECT #2#59;
          #59#1 = #1#2;
          |LEE 000#59=;
          |SI FSalida = 0;
               #1#30 = #59#3; |PINTA #1#30;
          |FINSI;
          |CIERRA #59;
     |FINSI;
|FPRC;

|PROCESO Observa49; |TIPO 0;
     nModo = (FEntrada / 100) ! 0;
|FPRC;

|PROCESO TipoArti; |TIPO 0;
     |SI #1#10 < 1; |O #1#10 > 4;
          |AVISO; |ERROR;
     |FINSI;
|FPRC;

|PROCESO EntraUni49; |TIPO 7;
     ||SI #0#33 ! "K"; |ACABA; |FINSI;

     enAlmacen = #1#9;
     eaCliente = "";
     efFecha = "01.01.000";
 ||***
     eaArticulo = #1#2;
     enUnidad1 = #1#12;
     enUnidad2 = #1#17;
     enMedida1 = #1#18;
     enMedida2 = #1#19;
     enMedida3 = #1#20;

     ||HAZ DobleUniV;
     eaProgVta = "agifa048";
     enModoVta = (FEntrada / 100) ! 0;
     |SUB_EJECUTA_NP "dsz99919";

     |C_M #1#12, 1, "S";
     |SI eaSw2Stock = "S";
          |C_M #1#12, 1, "N";
          #1#12 = enUnidad1; |PINTA #1#12;
          #1#17 = enUnidad2;
          #1#18 = enMedida1;
          #1#19 = enMedida2;
          #1#20 = enMedida3;
     |FINSI;
|FPRC;

|PROCESO LargoAncho; |TIPO 0;
     |SI #1Campo ! Contenido;
          |ABRE #2;
          #2#0 = #1#2;
          |LEE 000#2=;
          |CIERRA #2;

          #1#12 = #1#25 * #1#26 * #1#27 / #2#4; |PINTA #1#12;
     |FINSI;
|FPRC;

|PROCESO Min20;
     #20#0 = #1#22;
     #20#1 = 00;
|FPRC;

|PROCESO Max20;
     #20#0 = #1#22;
     #20#1 = 99;
|FPRC;

|PROCESO Proceso; |TIPO 66;
     |ABRE #20;
     |SI Campo = 23;
          #20#0 = #1#22;
          #20#1 = #1#23;
          |LEE 000#20];
          |CONSULTA_F_CLAVE #1#20, Min20, Max20;
          |SI FSalida = 0;
               #1#23 = #20#1; |PINTA #1#23;
          |SINO;
               |ERROR;
          |FINSI;
     |SINO;
          #20#0 = #1#22;
          #20#1 = #1#24;
          |LEE 000#20];
          |CONSULTA_F_CLAVE #1#20, Min20, Max20;
          |SI FSalida = 0;
               #1#24 = #20#1; |PINTA #1#24;
          |SINO;
               |ERROR;
          |FINSI;
     |FINSI;
     |CIERRA #20;
|FPRC;

|PROCESO Min54;
     #54#2 = aAlfa;
     #54#0 = 0000;
|FPRC;

|PROCESO Max54;
     #54#2 = aAlfa;
     #54#0 = 9999;
|FPRC;

|PROCESO Pieza0; |TIPO 0;
     |SI #1#28 = 0; |ACABA; |FINSI;
     |ABRE #54;
     #54#0 = #1#28;
     |LEE 000#54=;
     |SI #54#2 = "S";
          |MENAV "0000El codigo es una subpieza...";
          |ERROR;
     |SINO;
          |SI #1Campo ! Contenido;
               aAlfa = #54#1; |QBF aAlfa;
               #54#0 = #1#29;
               |LEE 000#54=;
               |SI FSalida ! 0; |DDEFECTO #54; |FINSI;
               aAlfa = aAlfa + "#" + #54#1;
               #1#7 = aAlfa; |PINTA #1#7;
          |FINSI;
     |FINSI;
     |CIERRA #54;
|FPRC;

|PROCESO Pieza66; |TIPO 66;
     aAlfa = "N";
     |ABRE #54;
     #54#0 = #1#28;
     |LEE 000#54];
     |CONSULTA_F_CLAVE #2#54, Min54, Max54;
     |SI FSalida = 0;
          #1#28 = #54#0; |PINTA #1#28;
     |SINO;
          |ERROR;
     |FINSI;
     |CIERRA #54;
|FPRC;

|PROCESO Sub0; |TIPO 0;
     |SI #1#29 = 0; |ACABA; |FINSI;
     |ABRE #54;
     #54#0 = #1#29;
     |LEE 000#54=;
     |SI #54#2 = "N";
          |MENAV "0000El codigo es una pieza...";
          |ERROR;
     |SINO;
          |SI #1Campo ! Contenido;
               aAlfa1 = #54#1; |QBF aAlfa1;
               #54#0 = #1#28;
               |LEE 000#54=;
               |SI FSalida ! 0; |DDEFECTO #54; |FINSI;
               aAlfa = #54#1; |QBF aAlfa;
               aAlfa = aAlfa + "#" + aAlfa1;
               #1#7 = aAlfa; |PINTA #1#7;
          |FINSI;
     |FINSI;
     |CIERRA #54;
|FPRC;

|PROCESO Sub66; |TIPO 66;
     aAlfa = "S";
     |ABRE #54;
     #54#0 = #1#29;
     |LEE 000#54];
     |CONSULTA_F_CLAVE #2#54, Min54, Max54;
     |SI FSalida = 0;
          #1#29 = #54#0; |PINTA #1#29;
     |SINO;
          |ERROR;
     |FINSI;
     |CIERRA #54;
|FPRC;

|PROCESO CopiaLineas;
     |DDEF aPath;
     aAlfa = aPath + "coima066";
     |CARGA_DEF aAlfa, referen@;
     |SI FSalida = 0;
          |ABRE #referen@;
          #referen@#0 = eaFamilia;
          #referen@#31 = eaLinea;
          #referen@#1 = 1;
          |LEE 000#referen@.];
          |PARA; |SI FSalida = 0; |Y #referen@#0 = eaFamilia;
                    |Y #referen@#31 = eaLinea; |HACIENDO;
               #1#0 = #0#0;
               #1#31 = #0#1;
               #1#1 = 999;
               |LEE 000#1];
               |SI FSalida = 0;
                    |LEE 000#1a;
               |SINO;
                    |LEE 000#1u;
               |FINSI;
               |SI FSalida = 0; |Y #1#0 = #0#0; |Y  #1#31 = #0#1;
                    nLin = #1#1 + 1;
               |SINO;
                    nLin = 1;
               |FINSI;

               |SALVA_DATOS #referen@;
               |REPON_DATOS #1;
               #1#0 = #0#0;
               #1#31 = #0#1;
               #1#1 = nLin;
               |GRABA 020#1n;

               |LEE 000#referen@.s;
          |SIGUE;

          |CIERRA #referen@;

          |DESCARGA_DEF #referen@;
     |FINSI;
|FPRC;

|PROCESO Escan11; |TIPO 11;
     nModo = (FEntrada  / 100) ! 0;
     |SI FSalida = 12; |Y nModo ! 4;
          |PUSHV 0501 2380;
          |BLANCO 905 1676;
          |SUB_EJECUTA_NP "coimz042";
          |SI eaFamilia ! "";
               |SI eaFamilia = #0#0; |Y eaLinea = #0#1;
                    |MENAV "0000No se puede seleccionar el mismo defecto...";
                    |ERROR;
               |SINO;
                    |HAZ CopiaLineas;
                    |SI nModo ! 1;
                         |PONCONTROL 23, "SI";
                         |ERROR;
                    |FINSI;
                    |PTEC 809;
                    |PTEC 808;
               |FINSI;
          |SINO;
               |ERROR;
          |FINSI;
          |POPV;
     |FINSI;
|FPRC;M
