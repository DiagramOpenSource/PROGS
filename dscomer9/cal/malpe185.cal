|FICHEROS;
     malpe001 #1;
     agifa024 #24;
     malpe185 #185;
     malpe185@;
|FIN;

|VARIABLES;
     &eaCliente = "";
     &enObra = 0;
     &enModo = 0;
     nModo = 0;
     nIncidencia = 0;
     nClave = 1;
     nLinea = 0;
     nHay = 0;
     aAlfa = "";
     nBoton = 0;
     nBuffer = 0;
     nPuntero = 0;
     nLineas = 0;
     nLin = 0;
     nCol = 0;
     i = 0;
|FIN;

|PROCESO Boton;
     enModo = 1;
     |PTEC 807;
|FPRC;

|PROCESO CogeLineas;
     aAlfa = "Usuario: " + #185#3;
     aAlfa = aAlfa + " Fecha: " + #185#4;

     |AL_BUF nBuffer, nPuntero, aAlfa;
     nPuntero = nPuntero + 1;

     |SI #185#5 = "G";
          aAlfa = " Incidencia general";
     |FINSI;
     |SI #185#5 = "I";
          aAlfa = " Incidencia " + #185#6;
     |FINSI;
     |SI #185#5 = "C";
          aAlfa = " Continuacion incidencia " + #185#6;
     |FINSI;
     |SI #185#7 = "A";
          aAlfa = aAlfa + " ABIERTA";
     |SINO;
          aAlfa = aAlfa + " CERRADA";
     |FINSI;
     |AL_BUF nBuffer, nPuntero, aAlfa;
     nPuntero = nPuntero + 1;

     |PARA i = 8; |SI i [ 15; |HACIENDO i = i + 1;
          aAlfa = #185i; |QBF aAlfa;
          |SI aAlfa ! "";
               aAlfa = "    " + aAlfa;
               |AL_BUF nBuffer, nPuntero, aAlfa;
               nPuntero = nPuntero + 1;
          |FINSI;
     |SIGUE;

     aAlfa = aAlfa + "";
     |AL_BUF nBuffer, nPuntero, aAlfa;
     nPuntero = nPuntero + 1;
|FPRC;

|DEFBUCLE CogeLineas;
     #185#1;
     ;
     eaCliente, enObra, 001;
     eaCliente, enObra, 999;
     be;
     NULL, CogeLineas;
|FIN;

|PROCESO CuentaLineas;
     nLineas = nLineas + 3;
     |PARA i = 8; |SI i [ 15; |HACIENDO i = i + 1;
          aAlfa = #185i; |QBF aAlfa;
          |SI aAlfa ! "";
               nLineas = nLineas + 1;
          |FINSI;
     |SIGUE;
|FPRC;

|DEFBUCLE CuentaLineas;
     #185#1;
     ;
     eaCliente, enObra, 001;
     eaCliente, enObra, 999;
     be;
     NULL, CuentaLineas;
|FIN;

|PROCESO Tipo3; |TIPO 3;
     |SI #185#7 = "A"; |Y #185#5 = "G";
          |CONFI "0000SCerrar incidencia";
          |SI FSalida = 0;
               #185#7 = "C";
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Min185;
     #185#0 = eaCliente;
     #185#1 = enObra;
     #185#2 = 1;
     #185#4 = "01.01.0000";
     #185#6 = 0000;
|FPRC;

|PROCESO Max185;
     #185#0 = eaCliente;
     #185#1 = enObra;
     #185#2 = 999;
     #185#4 = "31.12.9999";
     #185#6 = 9999;
|FPRC;

|PROCESO Mensa; |TIPO 7;
     |SI enModo ! 4;
          |MENSA "0000<F5> Cerrar incidencia";
     |SINO;
          |MENSA "0000<F5>Cerrar incidencia  <F6>Orden Fecha  <F7>Orden Inc.  <F8>Modo Alta";
     |FINSI;
|FPRC;

|PROCESO Defusu; |TIPO 0;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 1;
          #185#3 = Usuario % 810;
          |PINTA #185#3;
          |C_M #185#3, 1, "S";
          |C_M #185#4, 1, "S";
          |C_M #185#5, 1, "S";
     |SINO;
          |C_M #185#3, 1, "N";
          |C_M #185#4, 1, "N";
          |C_M #185#5, 1, "N";
     |FINSI;
|FPRC;

|PROCESO UltimaInci;
     |SALVA_FICHA #185;
     |SELECT #2#185;

     #185#0 = eaCliente;
     #185#1 = enObra;
     #185#6 = 9999;
     |LEE 000#185];
     |SI FSalida = 0;
          |LEE 000#185a;
     |SINO;
          |LEE 000#185u;
     |FINSI;

     |SI FSalida = 0; |Y #185#0 = eaCliente; |Y #185#1 = enObra;
          nIncidencia = #185#6 + 1;
     |SINO;
          nIncidencia = 1;
     |FINSI;

     |SELECT #1#185;
     |REPON_FICHA #185;
|FPRC;

|PROCESO TipoObs; |TIPO 0;
     nModo = (FEntrada / 100) ! 0;
     |SI #185#5 ! "C";
          |C_M #185#6, 1, "N";
          |SI #185#5 = "I";
               |SI nModo = 1;
                    |HAZ UltimaInci;
                    #185#6 = nIncidencia;
               |FINSI;
          |SINO;
               #185#6 = 0;
          |FINSI;
          |PINTA #185#6;
     |SINO;
          |C_M #185#6, 1, "S";
     |FINSI;
     |SI #185#5 = "I";
          |C_M #185#7, 1, "S";
     |SINO;
          |C_M #185#7, 1, "N";
     |FINSI;
|FPRC;

|PROCESO Incidencia7; |TIPO 0;
     |SI enModo ! 4;
          |MENSA "0000<F11> Consulta incidencias";
     |FINSI;
|FPRC;

|PROCESO ExisteInci;
     |SI #malpe185@#2 ! #185#2; |Y #malpe185@#6 = #185#6;
          nHay = 1;
     |FINSI;
|FPRC;

|DEFBUCLE ExisteInci;
     #malpe185@#2004;
     ;
     #185#0, #185#1, 0000, "01.01.0000";
     #185#0, #185#1, 9999, "31.12.9999";
     ;
     NULL, ExisteInci;
|FIN;

|PROCESO Incidencia0; |TIPO 0;
     |SI #185#5 = "C"; |Y enModo ! 4;
          nHay = 0;
          |DDEF aAlfa;
          aAlfa = aAlfa + "malpe185";
          |CARGA_DEF aAlfa, malpe185@;
          |SI FSalida = 0;
               |SI PARAMETRO ! "";
                  |DFICB aAlfa; |QBF aAlfa;
                  aAlfa = aAlfa + PARAMETRO + "/";
                  |PATH_DAT #malpe185@#aAlfa#;
               |FINSI;
               |BUCLE ExisteInci;
               |DESCARGA_DEF #malpe185@;
          |FINSI;
          |SI nHay = 0;
               |MENAV "0000No existe incidencia...";
               |ERROR;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Fecha66; |TIPO 66;
     |SI enModo = 4;
          |CONSULTA_F_CLAVE #3#185, Min185, Max185;
          |SI FSalida ! 0;
               |ERROR;
               nLinea = 0;
          |SINO;
               nLinea = #185#2;
          |FINSI;

          |SI nLinea ! 0;
               |SELECT #1#185;
               #185#2 = nLinea;
               |LEE 000#185=;
               |PONCONTROL 23, "SI";
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Incidencia66; |TIPO 66;
     |SI enModo = 4;
          |CONSULTA_F_CLAVE #2#185, Min185, Max185;
          |SI FSalida ! 0;
               |ERROR;
               nLinea = 0;
          |SINO;
               nLinea = #185#2;
          |FINSI;

          |SI nLinea ! 0;
               |SELECT #1#185;
               #185#2 = nLinea;
               |LEE 000#185=;
               |PONCONTROL 23, "SI";
          |FINSI;
     |SINO;
          |SALVA_FICHA #185;

          nIncidencia = 0;
          #185#0 = eaCliente;
          #185#1 = enObra;
          #185#6 = 1;
          #185#4 = "01.01.0000";
          |LEE 000#185];
          |CONSULTA_F_CLAVE #2#185, Min185, Max185;
          |SI FSalida ! 0;
               |ERROR;
          |SINO;
               nLinea = #185#2;
               nIncidencia = #185#6;
          |FINSI;

          |REPON_FICHA #185;
          |SI nIncidencia ! 0;
               #185#6 = nIncidencia;
               |PINTA #185#6;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO CierraTodas;
     #185#7 = "C";
     |GRABA 020#185a;
|FPRC;

|DEFBUCLE CierraTodas;
     #185#2;
     ;
     eaCliente, enObra, nIncidencia, "01.01.0000";
     eaCliente, enObra, nIncidencia, "31.12.9999";
     be;
     NULL, CierraTodas;
|FIN;

|PROCESO Clave; |TIPO 11;
     |SI FSalida = 14;
          nClave = 1; |PTEC 807;
     |FINSI;
     |SI FSalida = 15;
          nClave = 2; |PTEC 807;
     |FINSI;
     |SI FSalida = 16;
          enModo = 1; |PTEC 807;
     |FINSI;
|FPRC;

|PROCESO CierraInc; |TIPO 11;
     |SI FSalida = 13;
          |SI #185#5 = "G";
               #185#7 = "C"; |PINTA #185#7;
               |GRABA 020#185a;
          |SINO;
               #185#7 = "C"; |PINTA #185#7;
               |GRABA 020#185a;
               |SALVA_FICHA #185;
               nIncidencia = #185#6;
               |BUCLE CierraTodas;
               |REPON_FICHA #185;
          |FINSI;
          |ERROR;
     |FINSI;
|FPRC;

|PROGRAMA;
     |SI PARAMETRO ! "";
          |DFICB aAlfa; |QBF aAlfa;
          aAlfa = aAlfa + PARAMETRO + "/";
          |PATH_DAT #malpe185#aAlfa#;
     |FINSI;

     |BLANCO 808 2375;

     |SI enModo = 4;
          enModo = 0;

          |PINPA #0#185;
          |BLANCO 1009 2274;

          |BUCLE CuentaLineas;
          nLineas = nLineas + 3;
          |SI nLineas < 20; nLineas = 20; |FINSI;

          nPuntero = 0;
          nLineas = nLineas -1;
          |DIMENSIONA nLineas, nBuffer, 0;
          |BUCLE CogeLineas;

          |CONTROL_SIMPLE 0, "BOTON, Modo alta", 909, 919, Boton;
          nBoton = FSalida;

          nModo = 100; nLin = 99; nCol = 80;
          |EDITA nBuffer, 1, nLineas, 1009, 2274, nModo, "", 0, nLin, nCol;

          |FIN_CONTROL_WINDOWS nBoton;
          |FINDIM nBuffer;

          |PULSATECLA;
          |SI enModo = 1;
               |MENSA "0000<F5> Cerrar incidencia";
               |ENTLINEAL #1#185, 3, enModo, 21, 1, Min185, Max185;
          |FINSI;
     |SINO;
          |MENSA "0000<F5> Cerrar incidencia";
          |ENTLINEAL #1#185, 3, enModo, 21, 1, Min185, Max185;
     |FINSI;

     |PULSATECLA;
|FPRO;



/*
          |PARA nClave = 1; |SI nClave ! 0; |HACIENDO;
               |PINPA #0#185;
               |MENSA "0000<F5>Cerrar incidencia  <F6>Orden Fecha  <F7>Orden Inc.  <F8>Modo Alta";
               |SI nClave = 1;
                    |ATRI S; |PINTA 1010, " Ordenado por fechas"; |ATRI s;
                    |C_M #185#4, 1, "S";
                     nClave = 0;
                    |ENTLINEAL #3#185, 3, enModo, 21, 0, Min185, Max185;
               |SINO;
                    |ATRI S; |PINTA 1010, " Ordenanado por incidencia y fecha"; |ATRI s;
                    |C_M #185#4, 1, "N";
                    nClave = 0;
                    |ENTLINEAL #2#185, 3, enModo, 21, 0, Min185, Max185;
               |FINSI;
          |SIGUE;
*/
