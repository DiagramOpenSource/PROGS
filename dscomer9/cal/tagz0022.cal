|FICHEROS;
     agifa010 #10;
     agifa071 #71;
     tagm0087 #87;
     tagm0088 #88;
|FIN;

|VARIABLES;
     &eaItem = "";
     aAlfa = "";
     nHandle = 0;
     aEmp = "";
     aSer = "";
     aNum = "";
     aNom = "";
     aFic = "";
     c = "";
     f = "";
     fFec = @;
     aHora = "";
     aFecha = "";
     aPath = "";
     aNomCli = "";
     aCodCli = "";
     aA¤o = "";
     aFec = "";
     aAbreBat = "";

     kk = 0;
     nPagina = 0;
     nLe = 0;
     nValor = 0;
     nWhash2 = 0;
     nPos = 0;
     nNume = 0;
     aDC = "";
     aPagina = "";
     aHashtotal = "";
     aSemihash1 = "";
     aSerhash = "";
     aNumhash = "";
     aWhash = "";
     aWwhash = "";
     aHash1 = "";
     aHash2 = "";
     aPed = "";
     aPedAnt = "";
     nReg = 0;
     nHay = 0;
|FIN;

|PROCESO FNSemihash2;
     aHash2 = "";
     |QBT aWhash;
     aWwhash = aWhash;
     |PARA; |SI; |HACIENDO;
          aAlfa = aWwhash % A0; nLe = aAlfa;
          |SI nLe < 2;
               nWhash2 = aWwhash;
               aWwhash = ("00" + nWhash2) % -102;
          |FINSI;
          |SI nLe > 2;
               nValor = 0;
               |PARA kk = 1; |SI kk [ nLe; |HACIENDO kk = kk + 1;
                    nPos = kk * 100 + 1;
                    aAlfa = aWhash % nPos;
                    nNume = aAlfa;
                    nValor = nValor + nNume;
               |SIGUE;
               aWwhash = nValor;
          |SINO;
               |SAL;
          |FINSI;
     |SIGUE;
     |QBT aWwhash;
     aHash2 = aWwhash;
|FPRC;

|PROCESO FNSemihash1;
     aSemihash1 = "";
     aAlfa = aWhash % A0; nLe = aAlfa;
     |PARA kk = 1; |SI kk [ nLe; |HACIENDO kk = kk + 1;
          nPos = kk * 100 + 1;
          aAlfa = aWhash % nPos;
          nValor = &aAlfa;
          nValor = nValor + kk + nPagina;
          |PARA; |SI; |HACIENDO;
               |SI nValor < 48; |O nValor > 57;
                    nNume = nValor - 58;
                    |SI nNume < 0; nNume = nNume * -1; |FINSI;
                    nValor = 48 + nNume;
               |SINO;
                    |SAL;
               |FINSI;
          |SIGUE;
          |QBT aSemihash1;
          aSemihash1 = aSemihash1 + &nValor;
     |SIGUE;
|FPRC;

|PROCESO DC;
     aSerhash = eaItem % 302;
     aNumhash = eaItem % 611;
     aPagina = eaItem % 1802;
     aHashtotal = aSerhash + aNumhash;

     aWhash = aHashtotal; nPagina = aPagina;
     |HAZ FNSemihash1;
     aHashtotal = aSemihash1;

     aWhash = aHashtotal;
     |HAZ FNSemihash2;
     aHash1 = aHash2;

     aHashtotal = nPagina;
     aWhash = aHashtotal; nPagina = 0;
     |HAZ FNSemihash1;
     aHashtotal = aSemihash1;

     aWhash = aHashtotal;
     |HAZ FNSemihash2;

     aDC = aHash1 + aHash2;
|FPRC;

|PROCESO Digito;
     eaItem = "--90-";
     eaItem = eaItem + "30";                                || Empresa
     eaItem = eaItem + (("00" + #88#1) % -102);             || Serie
     eaItem = eaItem + (("0000000" + #10#0) % -107);         || Albaran
     eaItem = eaItem + "-" + (("00" + nPagina) % -102)+ "-";|| Pagina
     |HAZ DC;
     eaItem = eaItem + aDC + "-";                           || DC
|FPRC;

|PROCESO Graba;
     aAlfa = aAlfa + f;
     |XGRABA nHandle, aAlfa;
|FPRC;

|PROCESO Fecha;
     fFec = #10#1;
     |HORA aHora;
     aFecha = (fFec % 102) + "/" + (fFec % 402) + "/" + (fFec % 704);
     aFecha = aFecha + " " + aHora;
     aA¤o = fFec % 704;
     aFec = (aFecha % 106) + (aA¤o % -102);
|FPRC;

|PROCESO Tif;
     aFic = aPath + aNom + ".tif";
     |XABRE "WBC", aFic, nHandle;
     |SI FSalida ] 0;
          |XCIERRA nHandle;
     |SINO;
          aAlfa = "0000Error al crear fichero:" + aFic;
          |MENAV aAlfa;
     |FINSI;
|FPRC;

|PROCESO SinPedido;
     aPed = "99/999999";
     aAlfa = "                                 ("+ nReg + ")(" + c + aPed + c + "," + c + "A" + c + "," + c + "9" + c + ");";
     |HAZ Graba;
|FPRC;

|PROCESO PonPedidos;
     |SI #71#12 = 0;
          nHay = 1;
     |SINO;
          aPed = "50/30" + ((#71#31) % 102) + (("000000" + #71#12) % -106);
          |SI aPed ! aPedAnt;
               aAlfa = "                                 ("+ nReg + ")(" + c + aPed + c + "," + c + "A" + c + "," + c + "9" + c + ");";
               |HAZ Graba;
               nReg = nReg + 1;
               aPedAnt = aPed;
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE PonPedidos;
     #71#1;
     ;
     #10#52, #10#0, 001;
     #10#52, #10#0, 999;
     ;
     NULL, NULL, NULL, NULL, NULL, PonPedidos;
     #71#31, #71#12;
|FIN;

|PROCESO Apo;
     aFic = aPath + aNom + ".apo";
     |XABRE "WBC", aFic, nHandle;
     |SI FSalida ] 0;
          aAlfa = "list" + f + "{" + f + "template(" + c + c + ");"+ f + "type("+ c + "90" + c + ");";
          |HAZ Graba;
          aAlfa = "date(" + c + aFecha + c + ");";
          |HAZ Graba;
          aAlfa = "       doc" + f + "               {";
          |HAZ Graba;
          aAlfa = "                       key(" + c + "90/" + aNom + c + ");";
          |HAZ Graba;
          aAlfa = "                       data" + f + "                               {";
          |HAZ Graba;
          aAlfa = "                                 (1)(" + c + c + "," + c + "A" + c + "," + c + "0" + c + ");";
          |HAZ Graba;
          aAlfa = "                                 (2)(" + c + c + "," + c + "A" + c + "," + c + "0" + c + ");";
          |HAZ Graba;
          aAlfa = "                                 (3)(" + c + c + "," + c + "A" + c + "," + c + "0" + c + ");";
          |HAZ Graba;
          aAlfa = "                                 (4)(" + c + aNomCli + c + "," + c + "A" + c + "," + c + (aNomCli%A0) + c + ");";
          |HAZ Graba;
          aAlfa = "                                 (5)(" + c + c + "," + c + "A" + c + "," + c + "0" + c + ");";
          |HAZ Graba;
          aAlfa = "                                 (6)(" + c + aCodCli + c + "," + c + "A" + c + "," + c + (aCodCli%A0) + c + ");";
          |HAZ Graba;
          aAlfa = aEmp + "." + aSer + "." + aNum;
          aAlfa = "                                 (7)(" + c + aAlfa + c + "," + c + "A" + c + "," + c + (aAlfa%A0) + c + ");";
          |HAZ Graba;
          aAlfa = "                                 (8)(" + c + aFec + c + "," + c + "A" + c + "," + c + (aFec%A0) + c + ");";
          |HAZ Graba;
          aAlfa = "                                 (9)(" + c + aA¤o + c + "," + c + "A" + c + "," + c + (aA¤o%A0) + c + ");";
          |HAZ Graba;
          aAlfa = "                                 (10)(" + c + c + "," + c + "A" + c + "," + c + "0" + c + ");";
          |HAZ Graba;
          aAlfa = "                                 (11)(" + c + aEmp + c + "," + c + "A" + c + "," + c + (aEmp%A0) + c + ");";
          |HAZ Graba;

          nReg = 12; aPedAnt = ""; nHay = 0;
          |BUCLE PonPedidos;
          |SI nHay ! 0; |HAZ SinPedido; |FINSI;

          aAlfa = "                               }" + f + "                       contents";
          |HAZ Graba;
          aAlfa = "                               {" + f + "                                            1;" + f;
          |HAZ Graba;
          aAlfa = "                               }" + f + "               }" + f + "}";
          |HAZ Graba;
          |XCIERRA nHandle;
     |SINO;
          aAlfa = "0000Error al crear fichero:" + aFic;
          |MENAV aAlfa;
     |FINSI;
|FPRC;

|PROCESO Txt;
     aFic = aPath + aNom + ".txt";
     |XABRE "WBC", aFic, nHandle;
     |SI FSalida ] 0;
          ||aAlfa = aSer + aNum + f + aFec + f + aCodCli + f + aNomCli;
          aAlfa = #10#0 + f + #10#1 + f + aCodCli + f + aNomCli;
          |HAZ Graba;
          |XCIERRA nHandle;
     |SINO;
          aAlfa = "0000Error al crear fichero:" + aFic;
          |MENAV aAlfa;
     |FINSI;
|FPRC;

|PROCESO Sal;
     aFic = aPath + aNom + ".sal";
     |XABRE "WBC", aFic, nHandle;
     |SI FSalida ] 0;
          aAlfa = "USER=" + (Usuario % 810);
          |HAZ Graba;
          aAlfa = "CONTENIDO=" + aPath + aNom + ".tif";
          |HAZ Graba;
          aAlfa = "PORTADA=" + aPath + aNom + ".apo";
          |HAZ Graba;
          aAlfa = "NOTRANSFORM=1" + f + "WHATTODO=2" + f + "TIFF=1" + f + "TIF=1" + f + "FAX= " + f + "PDF=0";
          |HAZ Graba;
          |XCIERRA nHandle;
     |SINO;
          aAlfa = "0000Error al crear fichero:" + aFic;
          |MENAV aAlfa;
     |FINSI;
|FPRC;

|PROCESO Comando;
     aAbreBat = aPath + "itemdoc.bat";
     |RFBORRA aAbreBat;

     |XABRE "WC", aAbreBat, nHandle;
     |SI FSalida ] 0;
          |XGRABA nHandle, "@ECHO OFF";
          aAlfa = aPath % 102;
          |XGRABA nHandle, aAlfa;
          aAlfa = "CD " + aPath;
          |XGRABA nHandle, aAlfa;
          aAlfa = #87#2; |QBF aAlfa; aAlfa = aAlfa + "towarial.exe " + aPath + aNom + ".txt";
          |XGRABA nHandle, aAlfa;;
          aAlfa = #87#2; |QBF aAlfa; aAlfa = aAlfa + "jafsmb.exe " + aPath + aNom + ".sal";
          |XGRABA nHandle, aAlfa;
          |XGRABA nHandle, "exit";
          |XCIERRA nHandle;

          aAlfa  = "cmd " + &34 + "/c START /MIN /WAIT " + aAbreBat + &34;
          |RSYSTEM aAlfa;
     |SINO;
          aAlfa = "0000Error al crear fichero:" + aAbreBat;
          |MENAV aAlfa;
     |FINSI;
|FPRC;

|PROGRAMA;
     f = &13; f = f + &10;
     c = &34;

     |ABRE #87; |LEE 000#87p; |CIERRA #87;

     |ABRE #10;
     aAlfa = PARAMETRO % 105; #10#52 = aAlfa;
     aAlfa = PARAMETRO % 606; #10#0 = aAlfa;
     aAlfa = PARAMETRO % 1204;
     |LEE 020#10=;
     |SI FSalida = 0;
          |DDEFECTO #88;
          |ABRE #88;
          #88#0 = #10#52;
          |LEE 000#88=;
          |CIERRA #88;

          |SI aAlfa = "DIGI";
               aAlfa = PARAMETRO % 1602;
               nPagina = aAlfa;
               |HAZ Digito;
          |SINO;
               aEmp = "30";
               aSer = ("00" + #88#1) % -102;
               aNum = ("0000000" + #10#0) % -107;
               aNom = aEmp + aSer + aNum;
               aCodCli = #10#3;
               aNomCli = #10#4; |QBF aNomCli;

               aPath = "c:\ds\item\";
               |RMKDIR aPath;

               |HAZ Fecha;

               |HAZ Tif;
               |HAZ Apo;
               |HAZ Txt;
               |HAZ Sal;
               |HAZ Comando;

               aFic = aPath + aNom + ".tif"; |RFBORRA aFic;
               aFic = aPath + aNom + ".apo"; |RFBORRA aFic;
               aFic = aPath + aNom + ".txt"; |RFBORRA aFic;
               aFic = aPath + aNom + ".sal"; |RFBORRA aFic;
               aFic = aPath + "itemdoc.bat"; |RFBORRA aFic;
          |FINSI;
     |FINSI;
     |CIERRA #10;
|FPRO;
