|FICHEROS;
     conz0016 #0;
     agifa019 #19;
|FIN;

|VARIABLES;
     nBtn = 0;
     aAlfa = "";
     nHandle = 0;
     aReg = "";
     aReg1 = "";
     aReg2 = "";
     aReg3 = "";
     aReg4 = "";
     aDes = "";
     aPvp = "";
     aDto = "";
     aOpt = "";
     aKey = "";
     aDato = "";
     nNume = 0;
     aIP = "";
     aFic = "";
  {1}aContiene = "";
  {1}aLocal = "";
     aOrg = "";
     aMd5Org = "";
     aMd5Des = "";
     aTgz = "";
     aBase = "";
     aExe = "";
     aLon = "";
     aCar = "";
     nPos = 0;
     i = 0;
|FIN;

|PROCESO Dato;
     aAlfa = aDato % 0118; |QBT aAlfa;
     |SI aAlfa = ""; |ACABA; |FINSI;
     |SI aAlfa = "CÓDIGO"; |ACABA; |FINSI;

     || la longitud no es fija, cojo 4 posible tama¤os 15, 16, 17 y 18
     aReg1 = aDato % 0118; |QBF aReg1;
     aReg2 = aDato % 0117; |QBF aReg2;
     aReg3 = aDato % 0116; |QBF aReg3;
     aReg4 = aDato % 0115; |QBF aReg4;
     nNume = &aAlfa;
     |SI nNume < 32; |ACABA; |FINSI;

     #19#0 = aReg1;
     |LEE 000#19=;
     |SI FSalida = 0;
          aReg = aReg1;
          aDato = aDato % 19; |QBF aDato;
     |SINO;
          #19#0 = aReg2;
          |LEE 000#19=;
          |SI FSalida = 0;
               aReg = aReg2;
               aDato = aDato % 18; |QBF aDato;
          |SINO;
               #19#0 = aReg3;
               |LEE 000#19=;
               |SI FSalida = 0;
                    aReg = aReg3;
                    aDato = aDato % 17; |QBF aDato;
               |SINO;
                    #19#0 = aReg4;
                    |LEE 000#19=;
                    |SI FSalida = 0;
                         aReg = aReg4;
                         aDato = aDato % 16; |QBF aDato;
                    |SINO;
                         |ACABA;
                    |FINSI;
               |FINSI;
          |FINSI;
     |FINSI;

     aDes = "";
     aPvp = "";
     aLon = aDato % A0;
     |PARA i = 1; |SI i [ aLon; |HACIENDO i = i + 1;
          nPos = -(i * 100 + 1);
          aCar = aDato % nPos;
          |SI aCar = " "; |O aDes ! "";
               aDes = aCar + aDes;
          |SINO;
               aPvp = aCar + aPvp;
          |FINSI;
     |SIGUE;
     ||quita blancos izq
     aLon = aDes % A0;
     aAlfa = "";
     |PARA i = 1; |SI i [ aLon; |HACIENDO i = i + 1;
          nPos = i * 100 + 1;
          aCar = aDato % nPos;
          |SI aCar ! " "; |O aAlfa ! "";
               aAlfa = aAlfa + aCar;
          |FINSI;
     |SIGUE;
     aDes = aAlfa;

     #19#0 = aReg;
     |LEE 101#19=;
     |SI FSalida = 0;
          |PINTA 1306, #19#0;
          #19#1 = aDes;
          #19#18 = aPvp;
          |GRABA 020#19a;
          |LIBERA #19;
     |FINSI;
|FPRC;

|PROCESO CopiaTgz;
     aTgz = "pdftotext.tgz";
     |DBASE aAlfa;
     aOrg = aAlfa + "plugins/" + aTgz;
     |SI aIP = "";
          |DBASS aBase;
          aDes = aBase + aTgz;

          aContiene = aOrg;
          |ESPECIFICA "MD5", aContiene;
          aMd5Org = FSalida;
          aContiene = aDes;
          |ESPECIFICA "MD5", aContiene;
          aMd5Des = FSalida;
          |SI aMd5Org ! aMd5Des;
               |COPIA_FICHERO aOrg, aDes;
               |DESCOMPRIME aDes;
               |DETAR aDes, aBase;
          |FINSI;
     |SINO;
          |ESPECIFICA "RBASS", aLocal;
          aBase = aLocal;
          aDes = aBase + aTgz;

          aContiene = aOrg;
          |ESPECIFICA "MD5", aContiene;
          aMd5Org = FSalida;
          aContiene = aDes;
          |ESPECIFICA "REMOTOMD5", aContiene;
          aMd5Des = FSalida;
          |SI aMd5Org ! aMd5Des;
               |ENVIA_FICHERO aOrg, aDes;
               |RDESCOMPRIME aDes;
               |RDETAR aDes, aBase;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Pdf2Txt;
     aOrg = aBase + Usuario + ".txt";
     aExe = aBase + "pdftotext/pdftotext.exe -f 1 -layout " + aFic + " " + aOrg;
     |SI aIP = "";
          |SYSTEM aExe;
          aDes = aOrg;
     |SINO;
          |RSYSTEM aExe;
          |DFICE aDes; aDes = aDes + Usuario;
          |RECIBE_FICHERO aOrg, aDes;
          |RFBORRA aOrg;
     |FINSI;
     aFic = aDes;
|FPRC;

|PROCESO Tipo3; |TIPO 3;
     aFic = #0#0; |QBF aFic;
     |SI aFic ! "";
          |IP_REMOTO aIP;
          |HAZ CopiaTgz;
          |HAZ Pdf2Txt;

          |ABRE #19;
          |XABRE "", aFic, nHandle;
          |SI FSalida ] 0;
               |XLEE nHandle, 250, aDato;
               |PARA; |SI FSalida ] 0; |HACIENDO;
                    |HAZ Dato;
                    |XLEE nHandle, 250, aDato;
               |SIGUE;
               |XCIERRA nHandle;
          |SINO;
               |MENAV "0000Error al abrir fichero...";
          |FINSI;
          |CIERRA #19;

          |FBORRA aFic;
     |FINSI;
|FPRC;

|PROCESO Browse; |TIPO 0;
     |SI FSalida ! 10; |ACABA; |FINSI;

     aAlfa = "F*.pdf";

     |BROWSE aAlfa;

     aAlfa = aAlfa % 2; |QBF aAlfa;
     |SI aAlfa = "F"; aAlfa = ""; |FINSI;

     |QBF aAlfa;
     |SI aAlfa = "";
          |ERROR;
     |SINO;
          #0#0 = aAlfa;
          |PINTA #0#0;
     |FINSI;
|FPRC;

|PROCESO Btn;
     |PTEC 824;
|FPRC;

|PROGRAMA;
     |CONTROL_SIMPLE 0, "BOTON,{{137}}Fichero", 1006, 1013, Btn;
     nBtn = FSalida;
     |PINPA #0#0;
     |PINDA #0#0;
     |ENDATOS #1#0;
     |FIN_CONTROL_WINDOWS nBtn;
|FPRO;
