|FICHEROS;
     golis007 #0;
     gomantho #10;       ||Expedientes CAB.
     gomantl1 #11;       ||Expedientes LIN.
     gopre001 #21;       ||Presupuestos
     gopartia #22;
     gocercab #23;
     gocerlin #24;
     guerm043 #43;
     agifa069 #69;
     agifa084 #84;
     go000023 #230;
     goparame;
|FIN;

|VARIABLES;
     nPorcentaje = 0;
     nSw0 = 0;
     aInformes = "golis0s7"
     aInformec = "golis0c7";
     aInformeo = "golis0o7";

     &enPre    = 0;
     &enPem    = 0;
     &enEje    = 0;
     &enCer    = 0;
     &enCos    = 0;
     &enMar    = 0;
     &enPer    = 0;
     &enExi    = 0;

     nNum      = 0;
     aDart     = "";
     aHart     = "";
     aInforme  = "";
     nHay      = 0;
     nSw       = 0;
     nTotalPEM = 0;
     nTotalTrasBaja = 0;
     nTotalTrasDto = 0;
     nPEM = 0;
     nDto = 0;
     nGG = 0;
     nBI = 0;
     nBaja = 0;
     nFacDirCer = 0;
     nFacDirCos = 0;
|FIN;

|PROCESO FacturasLin;
     nFacDirCos = nFacDirCos + #69#14;
|FPRC;

|PROCESO Facturas;
     #84#48 = #43#0;
     #84#0 = #43#1;
     |LEE 000#84=;
     |SI FSalida = 0;
          nFacDirCer = nFacDirCer + (#84#16+#84#19+#84#22+#84#28+#84#42+#84#45);
          |BUCLELIN 2FacturasLin#84;
     |FINSI;
|FPRC;

|DEFBUCLE Facturas;
     #43#2;
     15;
     #10#0, #10#1, "01.01.0000";
     #10#0, #10#1, #0#18;
     ;
     NULL, Facturas;
|FIN;

|PROCESO FactDir;
     nFacDirCos = 0;
     nFacDirCer = 0;
     |ABRE #84;
     |BUCLE Facturas;
     |CIERRA #84;
|FPRC;

|PROCESO NuevoTot; /* A¤adido Tue Jul 13 14:03:17 CEST 2004 */
     nDto = (enCer * #21#43 / 100) ! 2;
     nGG = ((enCer - nDto) * #21#26 / 100) ! 2;
     nBI = ((enCer - nDto) * #21#27 / 100) ! 2;
     nBaja = ((enCer + nGG + nBI - nDto) * #21#28 / 100) ! 2;

     enCer = enCer - nDto + nGG + nBI - nBaja;
|FPRC;

|PROCESO CalcuPen;
     nTotalPEM = 0;
     nTotalTrasBaja = #21#5 -(((#21#5 * #21#28)/100) ! 2);
     nTotalTrasDto = nTotalTrasBaja - (((nTotalTrasBaja * #21#43)/100) ! 2);
     nPEM = (nTotalTrasDto / (1 + (#21#45/100))) ! 2;
     nTotalPEM = nPEM;

     nPorcentaje = #21#45;
|FPRC;

|PROCESO gopartia;
     ||enEje = enEje + (#22#23 * #22#10);

     enEje = enEje + #22#14;
|FPRC;

|DEFBUCLE gopartia;
     #22#3;
     28;
     #10#0, #10#1, #11#3, #11#4, aDart, aDart,"00.00.0000";
     #10#0, #10#1, #11#3, #11#4, aHart, aHart, #0#18;
     ;
     NULL, gopartia;
|FIN;

|PROCESO gocerlin;
     |SI #24#3 [ #0#18;
          |SI #24#15 ! 0;
               enCer = enCer + #24#15;
          |SINO;
               enCer = enCer + #24#6;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO gomantl1;
     #gocercab#0 = #gomantl1#3;
     #gocercab#1 = #gomantl1#4;
     |LEE 000#gocercab.=;
     |SI FSalida = 0;
         |BUCLELIN 2 gocerlin #23;
     |SINO;
         |SI #goparame#52 = "S";
              #gocercab#1 = #gocercab#1 + #goparame#54;
              |LEE 000#gocercab.=;
              |SI FSalida = 0;
                  |BUCLELIN 2 gocerlin #23;
              |SINO;
                  #gocercab#1 = #gocercab#1 - #goparame#54 - #goparame#54;
                  |LEE 000#gocercab.=;
                  |SI FSalida = 0;
                      |BUCLELIN 2 gocerlin #23;
                  |SINO;
                      #gocercab#1 = #gocercab#1 + #goparame#56;
                      |LEE 000#gocercab.=;
                      |SI FSalida = 0;
                          |BUCLELIN 2 gocerlin #23;
                      |SINO;
                          #gocercab#1 = #gocercab#1 - #goparame#56 - #goparame#56;
                          |LEE 000#gocercab.=;
                          |SI FSalida = 0;
                              |BUCLELIN 2 gocerlin #23;
                          |FINSI;
                      |FINSI;
                  |FINSI;
              |FINSI;
          |FINSI;
      |FINSI;

     |DDEFECTO #gopre001;
     #gopre001#0 = #gomantl1#3;
     #gopre001#1 = #gomantl1#4;
     |LEE 000#gopre001.=;


     enPre = enPre + #11#9;

     |HAZ CalcuPen;
     enPem = enPem + nTotalPEM;

     |BUCLE gopartia;

     |HAZ NuevoTot;
|FPRC;

|PROCESO gomantho;
     |SI #0#19 ! "T";
          #230#0 = #gomantho#0;
          #230#1 = #gomantho#1;
          |LEE 000#230=;
          |SI FSalida = 0; |Y #0#19 = "N"; |ERROR; |ACABA; |FINSI;
          |SI FSalida ! 0; |Y #0#19 = "S"; |ERROR; |ACABA; |FINSI;
     |FINSI;

     nSw0 = 0;
     |SI #0#14 = "S"; |Y #10#9 = "C"; nSw0 = 1; |FINSI;
     |SI #0#15 = "S"; |Y #10#9 = "P"; nSw0 = 1; |FINSI;
     |SI #0#16 = "S"; |Y #10#9 = "T"; nSw0 = 1; |FINSI;
     |SI #0#17 = "S"; |Y #10#9 = "E"; nSw0 = 1; |FINSI;
     |SI nSw0 = 0;
          |ERROR;
          |ACABA;
     |FINSI;

     enPre    = 0;
     enPem    = 0;
     enEje    = 0;
     enCer    = 0;
     enCos    = 0;
     enMar    = 0;
     enPer    = 0;
     enExi    = 0;

     |HAZ FactDir

     nPorcentaje = 19; || Defecto

     |BUCLELIN 2 gomantl1 #10;
     enCer = enCer + nFacDirCer;
     enCos = enCer - (enCer / nPorcentaje);

     |SI #10#25 < #0#18; |Y #10#9 = "T";
          ||nNum = enPem - enEje;
          nNum = enCos - enEje;
          |SI nNum > 0;
               enMar = nNum;
          |SINO;
               enPer = -nNum;
          |FINSI;
     |SINO;
          enExi = enEje - enCos;
          |SI enExi < 0;
               enExi = 0;
          |FINSI;
     |FINSI;

     |PRINT;
     nHay = 1;
|FPRC;

|DEFBUCLE gomantho_s;
     #gomantho#1;
     2, 30, 24, 25;
     #golis007#1, #golis007#3, #golis007#5, #golis007#7, #golis007#9, #golis007#11;
     #golis007#2, #golis007#4, #golis007#6, #golis007#8, #golis007#10, #golis007#12;
     ;
     NULL, gomantho;
|FIN;

|DEFBUCLE gomantho_c;
     #gomantho#1;
     2, 30, 24, 25;
     #golis007#1, #golis007#3, #golis007#5, #golis007#7, #golis007#9, #golis007#11;
     #golis007#2, #golis007#4, #golis007#6, #golis007#8, #golis007#10, #golis007#12;
     ;
     NULL, NULL, NULL, NULL, NULL, gomantho;
     #gomantho#2;
|FIN;

|DEFBUCLE gomantho_o;
     #gomantho#1;
     2, 30, 24, 25;
     #golis007#1, #golis007#3, #golis007#5, #golis007#7, #golis007#9, #golis007#11;
     #golis007#2, #golis007#4, #golis007#6, #golis007#8, #golis007#10, #golis007#12;
     ;
     NULL, NULL, NULL, NULL, NULL, gomantho;
     #gomantho#30;
|FIN;

|PROCESO Inicio;
     |SI #0#13 = "S"; aInforme = aInformes; |FINSI;
     |SI #0#13 = "C"; aInforme = aInformec; |FINSI;
     |SI #0#13 = "O"; aInforme = aInformeo; |FINSI;
     |IMPRE 0;
     |SI FSalida = 0;
          |INFOR aInforme;
          |SI FSalida ! 0;
               aInforme = "0000No existe informe:" + aInforme;
               |MENAV aInforme;
          |SINO;
               |ABRE #21;
               |ABRE #23;
               |ABRE #230;
               |SI #0#13 = "S"; |BUCLE gomantho_s; |FINSI;
               |SI #0#13 = "C"; |BUCLE gomantho_c; |FINSI;
               |SI #0#13 = "O"; |BUCLE gomantho_o; |FINSI;
               |CIERRA #230;
               |CIERRA #23;
               |CIERRA #21;
               |SI nHay ! 0; |PIEINF; |FINSI;
               |FININF;
          |FINSI;
          |FINIMP;
     |FINSI;
|FPRC;

|PROGRAMA;
     aDart = " " * 20;
     aHart = "z" * 20;
     |HAZ MiraLogos; || Copia Logos para el Crystal
     |ABRE #goparame; |LEE 000#goparame.p; |CIERRA #goparame;
     |CLS;
     |ABRE #0;
     |DDEFECTO #0;
     |LEE 101#0p;
     |SI FSalida ! 0; |GRABA 020#0b; |FINSI;
     |PINPA #0#0;
     |PINDA #0#0;
     |ENDATOS #1#0;
     |SI FSalida = 0;
          |GRABA 020#0a;
          |HAZ Inicio;
     |FINSI;
     |LIBERA #0;
     |CIERRA #0;
|FPRO;
