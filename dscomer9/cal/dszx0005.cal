|FICHEROS;
     dszx0005 #0;
     dszx0008 #8,MANTE;
     agifa134 #134,MANTE;
     agifa010 #10;
     agifa059 #59;
     agifa142 #142;
     agifa704 #704;
     agifa722 #722;
     dscam045@;
     dscam003@;
     camaasic@;
     camaasil@;
|FIN;

|VARIABLES;
     &Tempo = "";
     &ser_fac = "";
     &num_fac = 0;
     &reimp = "";
     &eaOrdenado = "";
     aAcc = "";
     aSer = "";
     aFac = "";
     aAlfa = "";
     aSerAlb = "";
     nNumAlb = "";
     aPath = "";
     aEmpre = "";
     aPathDat = "";
     aPathDef = "";
     aPathPan = "";
     aNumEmp = "";
     nNumEmp = 0;
     aA¤o = "";
     nNum = 0;
     nSal = 0;
     nHay = 0;
|FIN;

|PROCESO Tecla; |TIPO 0;
     |SI FSalida = 0; |Y #0Campo = Contenido;
          |PINPA #0#dscam003@;
          |PINDA #0#dscam003@;
          |PAUSA;
     |FINSI;
|FPRC;

|PROCESO MinRec;
     #0#0 = 1;
|FPRC;

|PROCESO MaxRec;
     #0#0 = 999999999;
|FPRC;

|PROCESO MinAsi;
     #camaasil@#0 = #camaasic@#0;
     #camaasil@#1 = #camaasic@#1;
     #camaasil@#2 = #camaasic@#2;
     #camaasil@#3 = 1;
|FPRC;

|PROCESO MaxAsi;
     #camaasil@#0 = #camaasic@#0;
     #camaasil@#1 = #camaasic@#1;
     #camaasil@#2 = #camaasic@#2;
     #camaasil@#3 = 999;
|FPRC;

|PROCESO VerAsiento;
     aA¤o = #134#1; aA¤o = aA¤o % -104;
     |DDEFECTO #722;
     |ABRE #722;
     #722#0 = "FV";
     #722#1 = #134#49;
     #722#2 = #134#0;
     #722#11 = 1;
     |LEE 000#722];
     nSal = FSalida; aSer = #722#1 % 105; nNum = #722#2; FSalida = nSal;
     |PARA; |SI FSalida = 0; |Y #722#0 = "FV"; aSer = #134#49;
               |Y nNum = #134#0; |HACIENDO;
          aAlfa = #722#3; |QBF aAlfa;
          |SI aAlfa = aA¤o; |SAL; |FINSI;
          |LEE 000#722s;
          nSal = FSalida; aSer = #722#1 % 105; nNum = #722#2; FSalida = nSal;
     |SIGUE;
     |SI aAlfa ! aA¤o; |ACABA; |FINSI;

     |ABRE #704;
     #704#0 = #722#10;
     |LEE 020#704=;
     |SI FSalida = 0;
          aAlfa = #704#6; |QBF aAlfa;
          aPathDef = aAlfa + "def/";
          aPathPan = aAlfa + "pan/";
          aPathDat = aAlfa + "fich/" + (("00000" + #722#8)%-105) + #722#9 +"/";
          aAlfa = aPathDef + "camaasic";
          |CARGA_DEF aAlfa, camaasic@;
          |SI FSalida = 0;
               aAlfa = aPathDef + "camaasil";
               |CARGA_DEF aAlfa, camaasil@;
               |SI FSalida = 0;
                    |PATH_DAT #camaasic@#aPathDat#;
                    |PATH_DAT #camaasil@#aPathDat#;
                    |PATH_PAN #camaasic@#aPathPan#;
                    |PATH_PAN #camaasil@#aPathPan#;
                    |ABRE #camaasic@;
                    #camaasic@#0 = #722#4;
                    #camaasic@#1 = #722#5;
                    #camaasic@#2 = #722#6;
                    |LEE 020#camaasic@.=;
                    |SI FSalida = 0;
                         |CLS;
                         |PINPA #0#camaasic@;
                         |PINDA #0#camaasic@;
                         |ENTLINEAL #1#camaasil@, 1, 4, 19, 0, MinAsi, MaxAsi;
                    |FINSI;
                    |CIERRA #camaasic@;
                    |DESCARGA_DEF #camaasil@;
               |FINSI;
               |DESCARGA_DEF #camaasic@;
          |FINSI;
     |FINSI;
     |CIERRA #704;
|FPRC;

|PROCESO _dscam045;
     |SI #dscam045@#2 = "V";
          aEmpre = ("00000" + #dscam045@#6) % -105;
          aPathDat = aPath + aEmpre + "/";
          |ERROR10;
     |FINSI;
|FPRC;

|DEFBUCLE _dscam045;
     #dscam045@#1002;
     ;
     nNumEmp, 001;
     nNumEmp, 999;
     ;
     NULL, _dscam045;
|FIN;

|PROCESO VerRecibo;
     |DBASS aPath;
     aPath = aPath + "dscarter/";
     aPathDef = aPath + "def/";
     aPathPan = aPath + "pan/";
     aAlfa = aPathDef + "dscam045";
     |CARGA_DEF aAlfa, dscam045@;
     |SI FSalida = 0;
          |DFICE aAlfa;
          aNumEmp = aAlfa % -205;
          nNumEmp = aNumEmp;
          aPath = aPath + "fich/";
          |PATH_DAT #dscam045@#aPath#;
          |BUCLE _dscam045;
          |DESCARGA_DEF #dscam045@;

          aAlfa = aPathDef + "dscam003";
          |CARGA_DEF aAlfa, dscam003@;
          |SI FSalida = 0;
               |SI aPathDat = "";
                    |MENAV "0000Error, no encuentro empresa cartera";
               |SINO;
                    |PATH_DAT #dscam003@#aPathDat#;
                    |PATH_PAN #dscam003@#aPathPan#;

                    |HAZ CreaTempo;
                    |NOME_DAT #0#Tempo#; |DELINDEX #0;
                    |ABRE #0;
                    |ABRE #dscam003@;
                    |SELECT #12#dscam003@;
                    #dscam003@#0 = aSer;
                    #dscam003@#1 = aFac;
                    |LEE 020#dscam003@.];
                    |PARA; |SI FSalida = 0; |Y #dscam003@#0 = aSer;
                              |Y #dscam003@#1 = aFac; |HACIENDO;
                         nHay = nHay + 1;
                         #0#0 = #dscam003@#40;
                         #0#1 = #dscam003@#99;
                         #0#2 = #dscam003@#32;
                         #0#3 = #dscam003@#6;
                         #0#4 = aSer;
                         #0#5 = aFac;
                         |GRABA 020#0n;
                         |LEE 000#dscam003@.s;
                    |SIGUE;
                    |CIERRA #0;
                    |SI nHay = 0;
                         |MENAV "0000La factura no tiene recibos...";
                    |SINO;
                         |SI nHay = 1;
                              |PINPA #0#dscam003@;
                              |PINDA #0#dscam003@;
                              |PAUSA;
                         |SINO;
                              |ENTLINEAL #1#0, 1, 4, 22, 1, MinRec, MaxRec;
                         |FINSI;
                    |FINSI;
                    |HAZ BoraTempo; |DELINDEX #0;
                    |CIERRA #dscam003@;
               |FINSI;
               |DESCARGA_DEF #dscam003@;
          |SINO;
               |MENAV "0000Error al cargar 'dscam003'";
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO TeclaAlb; |TIPO 0;
     |SI FSalida = 0; |Y #8Campo = Contenido;
          |SI #10#41 = 0;
               aAlfa = "dszx0007;V" + #8#0 + (("000000"+#8#1)%-106);
          |SINO;
               aAlfa = "dszx0004;V" + #8#0 + (("000000"+#8#1)%-106);
          |FINSI;
          |SUB_EJECUTA_NP aAlfa;
     |FINSI;
|FPRC;

|PROCESO MinAlb;
     #8#0 = "     ";
     #8#1 = 000001;
|FPRC;

|PROCESO MaxAlb;
     #8#0 = "zzzzz";
     #8#1 = 999999;
|FPRC;

|PROCESO BusAlb;
     #10#52 = #59#15;
     #10#0 = #59#2;
     |LEE 020#10=;
     |SI FSalida = 0;
          nHay = nHay + 1;
          #8#0 = #10#52;
          #8#1 = #10#0;
          #8#2 = #10#1;
          #8#3 = #10#3;
          #8#4 = #10#4;
          #8#5 = #134#49;
          #8#6 = #134#0;
          |GRABA 020#8n;
     |FINSI;
|FPRC;

|PROCESO VerAlbaran;
     |HAZ CreaTempo; |NOME_DAT #8 Tempo; |DELINDEX #8;
     |ABRE #8;
     |ABRE #10;
     |BUCLELIN 2 BusAlb #134;
     |CIERRA #10;
     |CIERRA #8;
     |SI nHay = 0;
          |MENAV "0000No encuentro albaranes para esta factura...";
     |SINO;
          |SI nHay = 1;
               |SI #10#41 = 0;
                    aAlfa = "dszx0007;V" + #8#0 + (("000000"+#8#1)%-106);
               |SINO;
                    aAlfa = "dszx0004;V" + #8#0 + (("000000"+#8#1)%-106);
               |FINSI;
               |SUB_EJECUTA_NP aAlfa;
          |SINO;
               |ENTLINEAL #1#8, 1, 4, 22, 1, MinAlb, MaxAlb;
          |FINSI;
     |FINSI;
     |HAZ BoraTempo; |DELINDEX #8;
|FPRC;

|PROGRAMA;
     aAcc = PARAMETRO % 101;
     aSer = PARAMETRO % 205;
     aFac = PARAMETRO % 706;

     |CLS;
     |ABRE #142; |LEE 000#142p; |CIERRA #142;

     |ABRE #134;
     #134#49 = aSer;
     #134#0 = aFac;
     |LEE 020#134=;
     |SI FSalida = 0;
          |SI aAcc = "V";     ||Ver Fac
               |PINPA #0#134;
               |PINDA #0#134;
               |ENDATOS #4#134;
          |FINSI;
          |SI aAcc = "A";     ||Ver Alb
               |HAZ VerAlbaran;
          |FINSI;
          |SI aAcc = "R";     ||Ver Rec
               aAlfa = "0000Recibo de la factura;" + aSer + "/" + aFac;
               ||MENAV aAlfa;
               |HAZ VerRecibo;
          |FINSI;
          |SI aAcc = "C";     ||Ver Asiento
               aAlfa = "0000Asiento contable de la factura;" + aSer + "/" + aFac;
               ||MENAV aAlfa;
               |HAZ VerAsiento;
          |FINSI;
          |SI aAcc = "I";     ||Imprimir;
               ser_fac = #134#49;
               num_fac = #134#0;
               reimp = #134#45;
               eaOrdenado = #142#141;
               |SUB_EJECUTA_NP "agifa136";
          |FINSI;
     |FINSI;
     |CIERRA #134;
|FPRO;
