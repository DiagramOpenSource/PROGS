|FICHEROS;
     mecam001 #1;
     mecam004 #4;
     mecam006 #6;
     mecam009 #9;
     mecam011 #11;
     mecam012 #12;

     agifa017 #17;
     agifa019 #19;
     dsm00024 #24;
     agifa049 #49;
     agifa073 #73;
     agifa104 #104;
|FIN;

|VARIABLES;
     aAlfa = "";
     &eaDarti = "";
     &eaHarti = "";
     &efDfecha = @;
     &efHfecha = @;
     nUni = 0;
     nUni1 = 0;
     nUni2 = 0;
     nForma = 0;
     nFactor = 0;
     nRese = 0;
     aParti = "";
     aArti = "";
     nAlma = 0;
     nReser = 0;
     nDispo = 0;
     nTotal = 0;
|FIN;

|PROCESO MecaEscanLin;
     |DDEFECTO #4;
     #4#0 = #49#0;
     #4#1 = #49#1;
     |LEE 000#4=;
     |SI #4#2 = "S"; || Film = S
          |SI #6#3 = "N"; |ACABA; |FINSI;
          nFactor = 1;
          |SI #6#3 = "A"; nFactor = 2; |FINSI;
     |SINO;
          nFactor = 1;
     |FINSI;
     |SI #73#44 ! 0;
          nUni = #49#12 * nFactor * #73#44 * nForma;
     |SINO;
          nUni = #49#12 * nFactor * #73#5 * nForma;
     |FINSI;

     |SI #49#2 < eaDarti; |O #49#2 > eaHarti; |ACABA; |FINSI;

     #19#0 = #49#2;
     |LEE 101#19=;
     |SI FSalida = 0;
          #19#12 = #19#12 + nUni;
          #19#104 = #19#104 - nUni;

          #17#0 = #49#2;
          #17#1 = #49#9;
          |LEE 101#17=;
          |SI FSalida = 0;
               #17#8 = #17#8 + nUni;
               #17#39 = #17#39 - nUni;
               |SI #19#180 = "S";
                    |DDEFECTO #24;
                    #24#0 = #49#2;
                    #24#1 = #49#9;
                    #24#2 = "";
                    |LEE 101#24=;
                    |SI FSalida ! 0; |GRABA 020#24b; |FINSI;
                    #24#24 = #24#24 + nUni;
                    #24#22 = #24#22 - nUni;
                    |HAZ ValoraStock;
                    |GRABA 020#24a;
                    |LIBERA #24;
               |FINSI;
               |HAZ ValoraStock;
               |GRABA 020#17a;
               |LIBERA #17;
          |FINSI;
          |GRABA 020#19a;
          |LIBERA #19;
     |FINSI;
|FPRC;

|DEFBUCLE MecaEscanLin;
     #49#1;
     ;
     #73#2, 001;
     #73#2, 999;
     ;
     NULL, MecaEscanLin;
|FIN;

|PROCESO StockFab;
     |SI #73#2 < eaDarti; |O #73#2 > eaHarti;
          |ACABA;
     |FINSI;

     nUni = #73#44;
     |SI nUni = 0; nUni = #73#5; |FINSI;

     #19#0 = #73#2;
     |LEE 101#19=;
     |SI FSalida = 0;
          #19#13 = #19#13 + nUni;

          #17#0 = #73#2;
          #17#1 = #73#3;
          |LEE 101#17=;
          |SI FSalida = 0;
               #17#9 = #17#9 + nUni;

               |HAZ ValoraStock;
               |GRABA 020#17a;
               |LIBERA #17;
          |FINSI;
          |GRABA 020#19a;
          |LIBERA #19;
     |FINSI;
|FPRC;

|PROCESO agifa073;
     |DDEFECTO #1;
     #1#0 = #73#2;
     |LEE 000#1=;

     |SI #1#3 = "N"; |ACABA; |FINSI; ||Articulo Fabricacion

     |DDEFECTO #6;
     #6#0 = #73#28;
     #6#1 = #73#0;
     #6#2 = #73#1;
     |LEE 000#6=;
     |SI FSalida ! 0; |GRABA 020#6n; |FINSI;
     |SI #6#10 = "N";    ||Orden Finalizada
          |SI #6#8 = 0;
               |BUCLE MecaEscanLin;
          |SINO;
               |HAZ StockFab;
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE agifa104;
     #104#1;
     1;
     "     ", 000001, efDfecha;
     "zzzzz", 999999, efHfecha;
     ;
     NULL, NULL, agifa073;
|FIN;

|PROCESO StockConsu;
     #19#0 = aArti;
     |LEE 101#19=;
     |SI FSalida = 0;
          #19#12 = #19#12 + nReser;
          #19#6 = #19#6 - nTotal;
          #19#104 = #19#104 - nDispo;

          #17#0 = aArti;
          #17#1 = nAlma;
          |LEE 101#17=;
          |SI FSalida = 0;
               #17#8 = #17#8 + nReser;
               #17#5 = #17#5 - nTotal;
               #17#39 = #17#39 - nDispo;
               |SI aParti ! "";
                    #24#0 = aArti;
                    #24#1 = nAlma;
                    #24#2 = aParti;
                    |LEE 101#24=;
                    |SI FSalida = 0;
                         #24#11 = #24#11 - nTotal;
                         #24#31 = #24#31 - nTotal;
                         #24#24 = #24#24 + nReser;
                         |HAZ ValoraStock;
                         |GRABA 020#24a;
                    |FINSI;
               |SINO;
                    |HAZ ValoraStock;
               |FINSI;
               |GRABA 020#17a;
          |FINSI;
          |GRABA 020#19a;
     |FINSI;
|FPRC;

|PROCESO mecam012;
     |DDEFECTO #9;
     #9#0 = #12#0;
     #9#1 = #12#1;
     |LEE 000#9=;
     |SI #9#2 < efDfecha; |O #9#2 > efHfecha;
          |ACABA;
     |FINSI;

     aParti = #12#6;
     aArti = #12#3;
     nAlma = #12#4;
     |SI #12#10 = "N";
          nReser = #12#7;
          nDispo = #12#7;
          nTotal = 0;
          |HAZ StockConsu;
     |FINSI;
|FPRC;

|DEFBUCLE mecam012;
     #12#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, mecam012;
|FIN;

|PROCESO mecam011;
     |DDEFECTO #9;
     #9#0 = #11#0;
     #9#1 = #11#1;
     |LEE 000#9=;
     |SI #9#2 < efDfecha; |O #9#2 > efHfecha;
          |ACABA;
     |FINSI;

     |SI #11#6 = "N";
          aParti = "";
          aArti = #11#3;
          nAlma = #11#4;
          |SI #11#9 = "N";
               nReser = #11#7;
               nDispo = #11#7;
               nTotal = 0;
               |HAZ StockConsu;
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE mecam011;
     #11#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, mecam011;
|FIN;

|PROGRAMA;
     nForma = 1;
     |ABRE #1;
     |ABRE #4;
     |ABRE #6;
     |ABRE #9;
     |ABRE #19;
     |ABRE #17;
     |ABRE #24;
     |BUCLE agifa104;
     |BUCLE mecam011;
     |BUCLE mecam012;
     |CIERRA #24;
     |CIERRA #17;
     |CIERRA #19;
     |CIERRA #9;
     |CIERRA #6;
     |CIERRA #4;
     |CIERRA #1;
|FPRO;
