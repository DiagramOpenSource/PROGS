|FICHEROS;
     goartive #14;
     goartdes #15,MANTE;
     goparame #17;
     godeivec #18;                                ||Desc. Ampliada (CAB)
     godeivel #19;                                ||Desc. Ampliada (LIN)
     goparam1 #30,MANTE;                          ||Seleccion Parametrico (1)
     goparam2 #31,MANTE;                          ||Seleccion Parametrico (2)
     goparam3 #32,MANTE;                          ||Seleccion Parametrico (3)
     goparam4 #33,MANTE;                          ||Seleccion Parametrico (4)
     agifa019 #50;
     agifa321 #51;
|FIN;
--------------------------------------------------------------------------------
|VARIABLES;
     aAlfa = "";
     &cPart         = "";
     &cPartAct      = "";
     &cTitulo1      = "";
     &cTitulo2      = "";
     &cTitulo3      = "";
     &cTitulo4      = "";
     &cCombina      = "";

     &cCodMonBase   = "";
     &cCodMonTrab   = "";
     &cCodMonDe     = "";
     &cCodMonA      = "";
     &cCodMonConv   = "";
     &cCodMonActu   = "";
     &cDesMonBase   = "";
     &cDesMonTrab   = "";
     &nDeci_PBase   = 0;
     &nDeci_TBase   = 0;
     &nDeci_PTrab   = 0;
     &nDeci_TTrab   = 0;
     &nDeci_TotVis  = 0;
     &nEurComTrab   = 0;
     &nEurComBase   = 0;
     &nEurVenTrab   = 0;
     &nEurVenBase   = 0;
     &nPreComTrab   = 0;
     &nPreComBase   = 0;
     &nPreVenTrab   = 0;
     &nPreVenBase   = 0;
     &nPrecioComp   = 0;
     &nPrecioVent   = 0;
     &nErrorPar     = 0;

     cCadena        = "";
     cOrigen        = "";
     cParam         = "";
     cFich1         = "";
     cFich2         = "";
     cFich3         = "";
     cFich4         = "";
     cLong          = "";

     f              = 0;
     h              = 0;
     i              = 0;
     j              = 0;
     k              = 0;
     r              = 0;
     nCarac         = 0;
     nSalida        = 0;
     nPrecio        = 0;
     nParametrico   = 0;
     nLinea         = 0;
     nElem          = 0;
     nUltimo        = 0;
     nAntMadr       = 0;
     nAntHija       = 0;

     {500}fiebdc    = "";
     {20}PartHija   = "";
     {20}PartMadr   = "";

|FIN;
-------------------------------------------------------------------------------------
|PROCESO Parametrico;
     |HAZ SelParam;
     nUltimo = 1;
     IPartMadr = PartMadr1<-;
     IPartHija = PartHija1<-;
     |QBF cParam;
     |QBF cPart;
     PartMadr = cPart;
     PartHija = cParam;

     |PARA nElem = 0; |SI nElem < nUltimo; |HACIENDO nElem = nElem + 1;

          |SI nElem > 0;
               IPartMadr = IPartMadr + 1;
               IPartHija = IPartHija + 1;
          |FINSI;

          |HAZ MiraParam;

          |SI nErrorPar = 0;
               |HAZ CreaPart;
               |HAZ DescAmpl;
               |HAZ CreaDesc;
          |FINSI;

     |SIGUE;

|FPRC;
-------------------------------------------------------------------------------
|PROCESO SelParam;
     cCombina = "";
     cTitulo1 = "";cTitulo2 = "";cTitulo3 = "";cTitulo4 = "";
     cParam = cPart;
     Ifiebdc = fiebdc1<-;
     fiebdc1 = "PARAMETROS";
     fiebdc2 = 400;
     fiebdc3 = cParam;
     |ESPECIFICA "FIEBDC",fiebdc;

     |SI FSalida < 0;
          |MENAV "0000Error o no es parametrico!";
     |SINO;
          cFich1 = Usuario % 108;
          cFich1 = "A"+cFich1;
          cFich2 = Usuario % 108;
          cFich2 = "B"+cFich1;
          cFich3 = Usuario % 108;
          cFich3 = "C"+cFich1;
          cFich4 = Usuario % 108;
          cFich4 = "D"+cFich1;
          |QBF cFich1;|QBF cFich2;|QBF cFich3;|QBF cFich4
          |NOME_DAT #30 cFich1;|NOME_DAT #31 cFich2;
          |NOME_DAT #32 cFich3;|NOME_DAT #33 cFich4;
          |CIERRA #30;|DELINDEX #30;
          |CIERRA #31;|DELINDEX #31;
          |CIERRA #32;|DELINDEX #32;
          |CIERRA #33;|DELINDEX #33;
          |ABRE #30;|ABRE #31;|ABRE #32;|ABRE #33;
          Ifiebdc = fiebdc5<-;

          |PARA i = 0;|SI i < fiebdc4;|HACIENDO i = i + 1;
               k = fiebdc;
               Ifiebdc = Ifiebdc  + 1;

               |PARA j = 0;|SI j < k;|HACIENDO j = j + 1;
                    nCarac = 96+j;

                    |SI j = 0;
                         |SI i = 0;cTitulo1 = fiebdc;|FINSI;
                         |SI i = 1;cTitulo2 = fiebdc;|FINSI;
                         |SI i = 2;cTitulo3 = fiebdc;|FINSI;
                         |SI i = 3;cTitulo4 = fiebdc;|FINSI;
                    |SINO;
                         Ifiebdc = Ifiebdc + 1;

                         |SI i = 0;
                              #30#0 = " ";#30#1 = &nCarac;
                              #30#2 = fiebdc;|GRABA 020#30c;
                         |FINSI;

                         |SI i = 1;
                              #31#0 = " ";#31#1 = &nCarac;
                              #31#2 = fiebdc;|GRABA 020#31c;
                         |FINSI;

                         |SI i = 2;
                              #32#0 = " ";#32#1 = &nCarac;
                              #32#2 = fiebdc;|GRABA 020#32c;
                         |FINSI;

                         |SI i = 3;
                              #33#0 = " ";#33#1 = &nCarac;
                              #33#2 = fiebdc;|GRABA 020#33c;
                         |FINSI;

                    |FINSI;

               |SIGUE;

               Ifiebdc = Ifiebdc + 1;

          |SIGUE;

          |PUSHV 0101 2380;
          |CLS;
          |ENTLINEAL #1#30, 1, 4, 14, 1, MinFic, MaxFic;
          |POPV;
          |CIERRA #30;||DELINDEX #30;|CIERRA #31;||DELINDEX #31;
          |CIERRA #32;||DELINDEX #32;|CIERRA #33;||DELINDEX #33;
          cParam = cParam - "$";
          |QBF cParam;
          |QBF cCombina;
          cParam = cParam + cCombina;
          cPartAct = cParam;
     |FINSI;

|FPRC;
--------------------------------------------------------------------------------
|PROCESO MiraParam;
     Ifiebdc = fiebdc1<-;
     fiebdc1 = "PARAMETRICO";
     fiebdc2 = 400;
     fiebdc3 = PartHija;
     |ESPECIFICA "FIEBDC",fiebdc;

     |SI FSalida < 0;

          |SI nElem = 0;
               aAlfa = "0000Error en parametrico! ("+fiebdc4+")"+fiebdc5;
               |MENAV aAlfa;
          |FINSI;

          nErrorPar = 1;
     |SINO;
          nErrorPar = 0;
     |FINSI;
|FPRC;
-------------------------------------------------------------------------------
|PROCESO CreaPart;

     |ABRE #14;
     #14#0 = PartMadr;                            ||Partida Madre.
     |LEE 101#14=;

     |SI FSalida = 0;
          |SALVA_FICHA #14;
          #14#0 = fiebdc3;                        ||Partida Hija.
          |LEE 101#14=;

          |SI FSalida ! 0;
               |REPON_FICHA #14;
               #14#0  = fiebdc3;                  ||Referencia.
               #14#1  = fiebdc27;                 ||Descripcion.
               #14#15 = fiebdc28;                 ||Precio Compra.

               |SI fiebdc4 ! 0;
                    #14#4 = "S";                  ||Tiene Descomposicion.
               |SINO;
                    #14#4 = "N";                  ||No tiene descomposicion.
               |FINSI;

               |GRABA 020#14c;
          |FINSI;

     |FINSI;

     |CIERRA #14;
|FPRC;
-------------------------------------------------------------------------------
|PROCESO CreaDesc;
     nLinea = 1;
     Ifiebdc = fiebdc29<-;
     |ABRE #15;

     |PARA i = 0;|SI i < fiebdc4;|HACIENDO i = i + 1;
          |DDEFECTO #15;
          #15#0 = PartHija;             ||Codigo Partida.
          #15#1 = nLinea + i;           ||Linea.
          |LEE 000#15=;

          |SI FSalida ! 0;
               |GRABA 020#15c;
          |FINSI;

          #15#2 = fiebdc;               ||Codigo Articulo Descomposicion.
          Ifiebdc = Ifiebdc + 1;
          #15#5 = fiebdc;               ||Cantidad.
          Ifiebdc = Ifiebdc + 1;
          nPrecio = fiebdc;
          |ABRE #17;
          #17#0 = "A";
          |LEE 000#17=;
          |SI FSalida = 0;
               |HAZ MonBas;
               cCodMonDe = #17#40;      ||Moneda Goartive
               cCodMonA  = cCodMonBase; ||Moneda Base
               nPrecioComp = nPrecio;   ||Precio Compra
               |HAZ DemonAmon;
          |FINSI;
          #15#6 = nPrecioComp;          ||Precio.
          #15#7 = #15#5*#15#6;          ||Importe.
          |ABRE #14;
          #14#0 = #15#2;                ||Partida-Articulo del Descompuesto.
          |LEE 000#14=;

          |SI FSalida = 0;
               #15#3 = #14#1;           ||Descripcion.
               #15#4 = #14#2;           ||U.M.
               #15#8 = #14#6;           ||Tipo Componente.
               #15#9 = #14#16;          ||Descricpcion Tipo.
          |SINO;
               |HAZ BusParam;
          |FINSI;

          |CIERRA #14;
          |GRABA 020#15a;
          |HAZ CreaElArti;
          Ifiebdc = Ifiebdc +  1;
          ||HAZ DescAmpl;
          |CIERRA #17;
     |SIGUE;

     |CIERRA #15;

|FPRC;
-------------------------------------------------------------------------------
|PROCESO CreaElArti;
     |ABRE #50;
     #50#0 = #15#2;
     |LEE 000#50=;

     |SI FSalida ! 0;
          #50#1   = #15#3;                        ||Descripcion del articulo.
          #50#125 = #14#7;                        ||Seccion.
          #50#2   = #14#9;                        ||Familia.
          #50#3   = #14#11;                       ||SubFamilia.
          #50#127 = #14#13;                       ||Capitulo.
          #50#5   = #14#2;                        ||Unidad Medida.
          #50#126 = #14#6;                        ||Tipo de Componente
          #50#28  = nPrecioComp;                  ||Ultimo Precio Proveedor.
          #50#29  = 0;                            ||Ultimo Dto% 1
          #50#129 = 0;                            ||Ultimo Dto% 2
          #50#209 = 0;                            ||Ultimo Dto% 3
          #50#133 = #50#28 < #50#29;                ||Ultimo Precio Neto.
          #50#133 = (#50#133 < #50#129) < #50#209;
          #50#9   = nPrecioComp;                  ||Precio Medio de Compra.
          #50#108 = "S";
          #50#105 = #17#21;                       ||% Aumento PVP1.
          #50#18 = #50#9 > #17#21;                ||Precio Venta 1.
          |GRABA 020#50c;
     |FINSI;

     |CIERRA #50;

|FPRC;
-------------------------------------------------------------------------------
|PROCESO BusParam;
     r = 0;

     |PARA j = 101; |SI j < 601; |HACIENDO j = j + 100;
          k = -j
          r = r + 1;
          cPart = #14#0;
          |QBF cPart;
          cPart = cPart % k;

          |SI cPart ] "0"; |Y cPart [ "9";
               cLong = #14#0;
               |QBF cLong;
               cLong = cLong % 0;
               k = cLong;
               k = (k - r) + 1;
               k = k + 100;
               cPart = #14#0 % k;
               cPart = cPart + "$";
               |SAL;
          |FINSI;

     |SIGUE;

     cParam = #14#0;
     |QBF cParam;
     |QBF cPart;
     #14#0 = cPart;
     |LEE 000#14=;

     |SI FSalida = 0;
          #15#3 = #14#1;             ||Descripcion.
          #15#4 = #14#2;             ||U.M.
          #15#8 = #14#6;             ||Tipo Componente.
          #15#9 = #14#16;            ||Descricpcion Tipo.
          nAntMadr = IPartMadr;
          nAntHija = IPartHija;
          IPartMadr = IPartMadr + nUltimo;
          IPartHija = IPartHija + nUltimo;
          nUltimo = nUltimo + 1;
          PartMadr = cPart;
          PartHija = cParam;
          IPartMadr = nAntMadr;
          IPartHija = nAntHija;
     |FINSI;

|FPRC;
--------------------------------------------------------------------------------
|PROCESO DesAmpIve;
     Ifiebdc = fiebdc1<-;
     fiebdc1 = "PARAMETRICO";
     fiebdc2 = 400;
     ||cParam = #3#5; pedro
     |QBF cParam;
     fiebdc3 = cParam;
     |ESPECIFICA "FIEBDC",fiebdc;

     |SI FSalida ] 0;
          |HAZ DescAmpl;
     |FINSI;

|FPRC;
--------------------------------------------------------------------------------
|PROCESO DescAmpl;
     |ABRE #18;
     #18#0 = fiebdc3;
     #18#1 = fiebdc27;
     |LEE 000#18=;

     |SI FSalida ! 0;
          nLinea = 0;
          |ABRE #19;
          Ifiebdc = fiebdc6<-;

          |PARA r = 1; |SI r [ 20; |HACIENDO r = r + 1;
               |DDEFECTO #19;
               cCadena = fiebdc;
               |QBF cCadena;

               |SI cCadena = "";
                    |SAL;
               |FINSI;

               nLinea = nLinea + 1;
               #19#0 = #18#0;
               #19#1 = nLinea;
               #19#2 = cCadena;
               |GRABA 020#19c;
               Ifiebdc = Ifiebdc + 1;

          |SIGUE;

          |CIERRA #19;
          |GRABA 020#18c;
     |FINSI;

     |GRABA 020#18a;
     |CIERRA #18;
|FPRC;
--------------------------------------------------------------------------------
|PROCESO MinFic;
     #30#1 = " ";
|FPRC;
--------------------------------------------------------------------------------
|PROCESO MaxFic;
     #30#1 = "z";
|FPRC;
--------------------------------------------------------------------------------
|PROCESO CargaIve;
     |ABRE #17;
     #17#0 = "A";
     |LEE 000#17=;
     |SI FSalida = 0;
          cOrigen = #17#24;
          |QBF cOrigen;
          fiebdc2 = cOrigen;
     |FINSI;

     |CIERRA #17;
     Ifiebdc = fiebdc1<-;
     fiebdc1 = "CARGA";
     fiebdc3 = 0;
     |ESPECIFICA "FIEBDC",fiebdc;

     |SI FSalida < 0;|Y #17#61 = "S";
          |MENAV "0000No se carga ive99.bc3!";
     |FINSI;

     Ifiebdc = fiebdc1<-;
     fiebdc1 = "PROVINCIA";
     fiebdc2 = "0";
     |ESPECIFICA "FIEBDC",fiebdc;
|FPRC;
-------------------------------------------------------------------------------
|PROCESO LiberaIve;
     Ifiebdc = fiebdc1<-;
     fiebdc1 = "LIBERA";
     fiebdc2 = "";
     fiebdc3 = 0;
     |ESPECIFICA "FIEBDC",fiebdc;

     |SI FSalida < 0;
        |MENAV "0000No se libera ive99.bc3!";
     |FINSI;

|FPRC;
-------------------------------------------------------------------------------
