|FICHEROS;
     agifa010 #10,MANTE;
     agifa071 #71;
     agifa142 #142;
|FIN;

|VARIABLES;
     &eaSerNuevo = "";
     &enNumNuevo = 0;
     &eaSerie = "";
     &enCodigo = 0;
     &ser_alb = "";
     &num_alb = 0;
     &abono = "";
     &fec_alb = @;
     &abo_alb = "";
     aAcc = "";
     aSer = "";
     aAlb = "";
     aCliente = "";
     aDirEnt = "";
     nHandle = 0;
     aTempo = "";
     aAlfa = "";
|FIN;

|PROCESO GraTmp;
     |DFICE aAlfa;
     aAlfa = aAlfa + aTempo;
     |XABRE "W", aAlfa, nHandle;
     |SI FSalida = 0;
          aAlfa = eaSerNuevo + (("000000" + enNumNuevo) % -106);
          |XGRABA nHandle, aAlfa;
          |XCIERRA nHandle;
     |FINSI;
|FPRC;

|PROGRAMA;
     aAcc = PARAMETRO % 101;
     aSer = PARAMETRO % 205;
     aAlb = PARAMETRO % 706;
     aCliente = PARAMETRO % 1306;
     aDirEnt = PARAMETRO % 1904;
     aTempo = PARAMETRO % 2308;

     |CLS;
     |ABRE #142; |LEE 000#142p; |CIERRA #142;

     |ABRET #10;
     #10#52 = aSer;
     #10#0 = aAlb;
     |SI aAcc = "V";     ||Ver Alb
          |LEE 020#10=;
          |SI FSalida = 0;
               |PINPA #0#10;
               |PINDA #0#10;
               |ENDATOS #4#10;
          |FINSI;
     |FINSI;
     |SI aAcc = "I";     ||Imprimir;
          |LEE 020#10=;
          |SI FSalida = 0;
               ser_alb = #10#52;
               num_alb = #10#0;
               abono = #10#43;
               |SUB_EJECUTA_NP "agifa014";
          |FINSI;
     |FINSI;
     |SI aAcc = "N";     ||Nuevo
          |SI #142#185 = "S";
               #10#52 = #142#188;
          |FINSI;
          |PINPA #0#10;
          |PINDA #0#10;
          |ENCAMPO #52#10;
          |SI FSalida = 0;
               FEntrada = 101;
               |HAZ ContaAV7;
               FEntrada = 0;
               |ENCAMPO #0#10;
               |SI FSalida = 0;
                    |LEE 101#10=;
                    |SI FSalida = 0;
                         |PINDA #0#10;
                         |MENAV "0000Registro existente...";
                         |PAUSA;
                    |SINO;
                         #10#3 = aCliente; |PINTA #10#3;
                         #10#84 = aDirEnt; |PINTA #10#84;
                         |GRABA 020#10b;
                         |ENDATOS #1#10;
                         |SI FSalida = 0;
                              |GRABA 020#10a;
                              eaSerNuevo = #10#52;
                              enNumNuevo = #10#0;
                              |LIBERA #10;
                              |HAZ GraTmp;
                              |HAZ Final;
                         |SINO;
                              |BORRA 020#10c;
                         |FINSI;
                    |FINSI;
               |FINSI;
          |FINSI;
     |FINSI;
     |CIERRAT #10;
|FPRO;

|PROCESO Final; ||Como el tipo 30
     |HAZ EsInforAlba;
     |SI FSalida = 0;
          eaSerie = #10#52;
          enCodigo = #10#0;
          |SUB_EJECUTA_NP "ialbz005";
     |FINSI;

     |CONFI "2400NDesea imprimir este albaran? ";
     |SI FSalida = 0;
          ser_alb = #10#52;
          num_alb = #10#0;
          abono = #10#43;
          |SUB_EJECUTA_NP "agifa014";
     |FINSI;

     |SI #142#17 = "S"; |Y #10#51 = "S";
          |SI #142#19 = "S";
               |CONFI "2400NDesea Facturar este Albaran : ";
               |SI FSalida = 0;
                    ser_alb = #10#52;
                    num_alb = #10#0;
                    fec_alb = #10#1;
                    abo_alb = #10#43;
                    |HAZ EsSematel;
                    |SI FSalida = 0; fec_alb = ~; |FINSI;

                    |SUB_EJECUTA_NP "agifa057";       ||Facturar Albaranes
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;
