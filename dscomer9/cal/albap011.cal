|FICHEROS;
     agifa104 #0;   || Cabecera Pedidos.
     agifa024 #1;   || Clientes.
     agifa073 #3;   || Lineas Pedidos.
     agifa038 #13;  || Descripcion Ampliada.
     agifa007 #40;  || Fichero de Series.
     agifa142 #41;  || Parametros Generales.
     albap004 #99;  || Cabecera de revision.
     agifa027 #43;  || Contadores;
|FIN;

|VARIABLES;
     &enTiva = 0;
     &efFiva = @;
     &eaDiva = "";
     &enIva = 0;
     &enRec = 0;
     con = 0;
     indtope = 0;
     ind = 0;
     fmes = "  ";
     mes = 0;
     control = 0;
     mensaje10 = "0000El Pedido ya esta Servido en parte o totalmente";
     mensaje11 = "0423Actualizando linea:";
     mensajei  = "2400NDesea imprimir este pedido? ";
     informe1 = "agifa104";
     informe = "";
     &iva1 = 0;
     &iva2 = 0;
     &iva3 = 0;
     &iva4 = 0;
     &iva5 = 0;
     &re1 = 0;
     &re2 = 0;
     &re3 = 0;
     &re4 = 0;
     &re5 = 0;
     &codigo="";
     &descrip="";
     &precio=0;
     &dto=0;
     &dto1=0;
     &unid=0;
     &imptot=0;
     &tiva=0;
     &linea=0;
     vacio="";
     &cant = 0;
     &tolin = 0;
     &n_dec = 0;
     &n_pre = 0;
     &ext1 = "";
     modo = 0;
     &bl  = "                              ";
     numero = "";
     tipo = "vpedido";
|FIN;

|CALCULO cogecon;|TIPO 0;
     control = 0;
|FCAL;

|CALCULO mirar_d;
     |ABRE #41;
     #41#0 = "A";
     |LEE 001#41=;
     |LIBERA #41;
     |SI FSalida ! 0; |DDEFECTO #41; |FINSI;
     |LIBERA #41;
     |CIERRA #41;
     n_dec = #41#8;
     n_pre = #41#9;
|FCAL;

|CALCULO pedcon;|TIPO 6;
     con = FEntrada / 100;
     con = con ! 0;
     |SI con ! 1; |Y #0Campo ! Contenido;
          |SI con ! 4;
               |SI #0#24 > 0;
                    #0Campo = Contenido;
||                    |PINTA #0Campo;
||                    |MENAV mensaje10;
                    |ERROR;
                    |ACABA;
               |SINO;
                    control = 1;
               |FINSI;
          |FINSI;
     |FINSI;
|FCAL;

|CALCULO pedaccli;|TIPO 2;
     con = FEntrada / 100;
     con = con ! 0;
     |SI con = 3;|Y #0#24 > 0;
||          |MENAV mensaje10;
          |ERROR;
     |SINO;
          |ABRE #1;
          #1#0 = #0#4;
          |LEE 121#1=;
          |LIBERA #1;
          |SI 0 = FSalida;
               fmes = #0#1 % A402;
               mes = fmes - 1;
               mes = mes * 3;
               mes = mes + 114;
               #1mes = #1mes +. #0#11;
               #1#150 = #1#150 +. #0#11;
               |GRABA 020#1a;
               |LIBERA #1;
          |FINSI;
          |LIBERA #1;
          |CIERRA #1;
          |SI control = 1;
               control = 0;
               |HAZ baallpp;
          |FINSI;
     |FINSI;
|FCAL;

|CALCULO aallpp;|TIPO 0;
||     |PINTA 445,#3#1;
     #0#11 = #0#11 - #3#9;
     #0#12 = #0#11;
     #3#19 = #1#4;
     #3#16 = #0#7;
     #3#17 = #0#16;
     #3#20 = #0#4;
     #3#23 = #0#5
     #3#24 = #0#6
     #3#25 = #0#17
     #3#26 = #0#9
     #3#13 = #0#2;  ||Fecha primera entrega
     #3#34 = #3#5 * #3#33;
     #3#7 = #3#5 * #3#6;
     #3#7 = #3#7 + #3#34;
     #3#9 = #3#7 < #3#8;
     #3#9 = #3#9 < #3#29;
     #3#9 = #3#9 < #0#5;
     #3#9 = #3#9 < #0#17;
     #3#9 = #3#9 < #0#6;
     #3#11 = #3#9;
     #0#11 = #0#11 + #3#9;
     #0#12 = #0#11;
     #0#13 = #0#11 - #0#12;
     #0#26 = #0#26 + #3#34;
||     |PINTA 1448,#0#12;|PINTA 1459,#0#13;|PINTA 1469,#0#11;
     |GRABA 020#3a;
     |LIBERA #3;
|FCAL;

|CALCULO baallpp;|TIPO 0;
     |MENSA mensaje11;
     |BUCLELIN 0aallpp#0;
     |BLIN 4;
|FCAL;

|CALCULO siimpre1;|TIPO 3;
     |CONFI mensajei;
     |SI 0 = FSalida;
          |HAZ impreal1;
     |FINSI;
|FCAL;

|CALCULO imprelim1;|TIPO 0;
     codigo=#3#2;
     descrip=#3#4;
     dto=#3#8;
     dto1=#3#29;
     precio=#3#6;
     unid=#3#5;
     tolin = #3#5 * #3#6;
     tolin = tolin < #3#8;
     tolin = tolin < #3#29;
     cant = cant + tolin;
     imptot=#3#9;
     tiva =#3#14;
     linea=1;
     |PRINT;
     |HAZ impdesa;
|FCAL;

|CALCULO impdesa;|TIPO 0;
     |ABRE #13;
     #13#0 = #3#27;
     |LEE 101#13=;
     |LIBERA #13;
     |SI 0 = FSalida;
          |HAZ xx;
     |FINSI;
     |LIBERA #13;
     |CIERRA #13;
|FCAL;

|CALCULO xx;
     indtope = 0;
     |PARA ind = 6;|SI ind>0;|HACIENDO ind = ind-1;
          descrip = #13ind;
          |QBF descrip;
          |SI descrip ! vacio;
               indtope=ind+1;
               ind=0;
          |FINSI;
     |SIGUE;

     |PARA ind = 1;|SI ind < indtope;|HACIENDO ind = ind + 1;
          descrip=#13ind;
          linea=0;
          |PRINT;
     |SIGUE;
|FCAL;

|CALCULO impreal1;|TIPO 4;
     |IMPRE 1;
     |SI 0 = FSalida;
          |HAZ mirainf;
          |INFOR informe;
          |SI 0 = FSalida;
               enTiva = 1; efFiva = #0#1; |HAZ SacaIVA;
               iva1 = enIva;re1 = enRec;

               enTiva = 2; efFiva = #0#1; |HAZ SacaIVA;
               iva2 = enIva;re2 = enRec;

               enTiva = 3; efFiva = #0#1; |HAZ SacaIVA;
               iva3 = enIva;re3 = enRec;

               enTiva = 4; efFiva = #0#1; |HAZ SacaIVA;
               iva4 = enIva;re4 = enRec;

               enTiva = 1; efFiva = #0#1; |HAZ SacaIVA;
               iva5 = enIva;re5 = enRec;

               |SI #0#4 ! #1#0;
                    |ABRE #1;
                    #1#0 = #0#4;
                    |LEE 040#1=;
                    |SI 0 ! FSalida ;|DDEFECTO #1;|FINSI;
                    |LIBERA #1;
                    |CIERRA #1;
               |FINSI;
               |BUCLELIN 2imprelim1#0;
               |PIEINF;
               cant = 0;
               |FININF;
          |FINSI;
          |FINIMP;
     |FINSI;
|FCAL;

|PROCESO alriesgo;
     |SI #1#49 ] #1#48; |Y #41#6 = "S";
||          |MENAV "0000Se supera el Riesgo Limite del Cliente";
     |FINSI;
|FPRC;

|PROCESO mirainf;
     |ABRE #40;
     #40#26 = #0#25;
     |LEE 001#40=;
     |SI FSalida = 0;
          informe = #40#0; |QBF informe;
     |SINO;
          informe = informe1;
     |FINSI;
     |LIBERA #40;
     |CIERRA #40;
|FPRC;

|PROCESO lee_tecla2;
     |SI FSalida = 13;   ||F5
          modo = (FEntrada / 100) ! 0;
          |SI modo = 4;
               |ABRE #1;
               #1#0 = #0#4;
               |LEE 001#1=;
               |SI FSalida ! 0; |DDEFECTO #1; |FINSI;
               |LIBERA #1;
               |CIERRA #1;
          |FINSI;
          |PUSHV 0501 2380;
          ext1 = #1#16;
          |SUB_EJECUTA "caextrap.run";
          |POPV;
     |FINSI;
|FPRC;

|PROCESO pasa;
     |DDEFECTO #0;
     numero = "000000" + #99#1;
     numero = numero % -106;
     #0#0 = numero;                || Numero Pedido.
     #0#25 = #99#2;                || Serie Pedido.
     #0#1 = #99#3;                 || Fecha Pedido.
     #0#4 = #99#4;                 || Cod. Cliente.
     |ABRE #1;
          #1#0 = #99#4;
          |LEE 000#1=;
     |LIBERA #1;
     |CIERRA #1;
     #0#8 = #1#2;                  || Nombre Cliente.
     #0#7 = #1#25;                 || Representante.
     #0#5 = #1#37;                 || Dto.
     #0#6 = #1#38;                 || Dto. P.P.
     #0#9 = #1#20;                 || Forma de Pago.
     #0#19 = #1#1;                 || Entregar a.
     #0#14 = #1#5;                 || Direccion entraga.
     #0#20 = #1#6;                 || Pob. entre.
     #0#21 = #1#7;                 || Prov. entre.
     #0#17 = 0;                    || Dto. acumulado.
     #0#22 = #1#47;                || Regimen equiva.
     #0#11 = #99#8;                || Total Pedido.
     #0#12 = #99#8;                || Pendiente Servir.
     #0#26 = #99#10;               || Importe Envase.
     |HAZ mirar_d;
     |HAZ cogecon;
     |HAZ pedcon;
     |HAZ alriesgo;
     #0#0 = numero;                || Numero Pedido.
     #0#25 = #99#2;                || Serie Pedido.
     |ABRE #43;
     #43#0 = tipo;
     #43#2 = #0#25;
     |LEE 010#43=;
     |SI FSalida ! 0;
       #43#1 = #0#0 + 1;
       |GRABA 010#43n;
       |LIBERA #43;
     |SINO;
       #43#1 = #0#0 + 1;
       |GRABA 010#43a;
       |LIBERA #43;
     |FINSI;
     |LIBERA #43;
     |CIERRA #43;
     |GRABA 121#0n;
     |LIBERA #0;
|FPRC;

|DEFBUCLE lee_cab;
     #99#1;
     ;
     "P","  ",000000;
     "P","zz",999999;
     be;
     NULL,pasa;
|FIN;

|PROGRAMA;
     modo = 1;
     |ABRE #0;
     |BUCLE lee_cab;
     |LIBERA #0;
     |CIERRA #0;
|FPRO;
