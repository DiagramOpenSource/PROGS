|FICHEROS;
     legim003 #0;
     legim004 #2;
     agifa027 #41;       || Contador.

     legim005 #5;        ||Formatos por series
     legim015 #15;       ||Incidencias
     legim016 #16;       ||Medidas correctoras
     legim017 #17;
     agifa019 #19;
     agifa219 #219;
     agifa220 #220;
|FIN;

|VARIABLES;
     &enSwDA = 0;
     nForma = 0;
     nUltimo = 0;
     nPuntero = 0;
     aAlfa = "";
     nBuffer = 0;
     nLineas = 0;
     nLin = 0;
     nCol = 0;
     nTipo = 0;

     qMod = 0;
     aSerie = "";
     nNumero = 0;


  {-1}aDato  = "";
  aDato1 = "2400";
  aDato2 = "1";
  aDato3 = " ";
  aDato4 = "IIM";
  aDato5 = "Imprimir";
  aDato6 = "Incidencias";
  aDato7 = "Med.Correctoras";

     aParametro = "";
     &eaSerieParte = "";
     &enNumeroParte = 0;

 {-1}menu           = "";
     menu1          = "2400";
     menu2          = "1";
     menu3          = "Contador:  ";
     menu4          = "AM";
     menu5          = " Automatico ";
     menu6          = " Manual ";

     nModo = 0;
     contador = 0;
     cont = "";

     aMensaje = "";
     aAlfa1 = "";

     aInforme = "";
     nSwPie = 0;
|FIN;

|PROCESO Totales003;
     #legim004#9 = #legim004#7 * #legim004#8;
     #legim004#12 = #legim004#9 < #legim004#10;
     #legim004#12 = #legim004#12 < #legim004#11;
     |PINTA #legim004#9;
     |PINTA #legim004#12;
|FPRC;


|PROCESO Borra15;
     |BORRA 020#15a;
|FPRC;

|DEFBUCLE Borra15;
     #15#1;
     ;
     #0#0, #0#1, 001;
     #0#0, #0#1, 999;
     be;
     NULL,  Borra15;
|FIN;

|PROCESO Borra16;
     |BORRA 020#16a;
|FPRC;

|DEFBUCLE Borra16;
     #16#1;
     ;
     #0#0, #0#1, 001;
     #0#0, #0#1, 999;
     be;
     NULL,  Borra16;
|FIN;

|PROCESO Tipo2legim003; |TIPO 2;
     nModo = (FEntrada/100) ! 0;
     |SI nModo = 3;
          |BUCLE Borra15;
          |BUCLE Borra16;
     |FINSI;
|FPRC;


|PROCESO Tipo20legim003;  |TIPO 20;
     aAlfa1 = FSalida;
     |SI aAlfa1 = "OPCION";
         FSalida = "Opciones";
         |ACABA;
     |FINSI;

     |SELECT #1#0;

     |LEE 101#0=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     qMod = (FEntrada / 100) ! 0;

     aSerie = #0#0;
     nNumero = #0#1;

     |PARA;  |SI;  |HACIENDO;
          |MENU aDato;
          aDato2 = FSalida;
          |SI aDato2 = "1";  |HAZ Imprimir003;  |FINSI;
          |SI aDato2 = "2";  |HAZ Incidencias;  |FINSI;
          |SI aDato2 = "3";  |HAZ MedCorrectoras;  |FINSI;
          |SI aDato2 < "1"; |O aDato2 > "3";  |SAL;  |FINSI;
     |SIGUE;
     |LIBERA #0;
|FPRC;


|PROCESO Incidencias;
     nTipo = 1;
     |HAZ EditaTexto;
|FPRC;

|PROCESO MedCorrectoras;
     nTipo = 2;
     |HAZ EditaTexto;
|FPRC;



|PROCESO CogeLineas15y16;
     nPuntero = 1;
     |PARA nPuntero = 1; |SI nPuntero [ 999; |HACIENDO nPuntero = nPuntero + 1;
          |SI nTipo = 1;
               #15#0 = #0#0;
               #15#1 = #0#1;
               #15#2 = nPuntero;
               |LEE 000#15=;
          |SINO;
               #16#0 = #0#0;
               #16#1 = #0#1;
               #16#2 = nPuntero;
               |LEE 000#16=;
          |FINSI;
          |SI FSalida = 0;
               |SI nTipo = 1;
                    aAlfa = #15#3; |AL_BUF nBuffer, nPuntero, aAlfa;
                    |BORRA 020#15a;
               |SINO;
                    aAlfa = #16#3; |AL_BUF nBuffer, nPuntero, aAlfa;
                    |BORRA 020#16a;
               |FINSI;
          |SINO;
               |SAL;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO PoneLineas15y16;
     |PARA nPuntero = 999; |SI nPuntero ] 0; |HACIENDO nPuntero = nPuntero - 1;
          |DEL_BUF nBuffer, nPuntero, aAlfa;
          |SI aAlfa ! ""; nLineas = nPuntero; |SAL; |FINSI;
     |SIGUE;
     |PARA nPuntero = 1; |SI nPuntero [ nLineas; |HACIENDO nPuntero = nPuntero + 1;
          |DEL_BUF nBuffer, nPuntero, aAlfa;
          |SI nTipo = 1;
               #15#0 = #0#0;
               #15#1 = #0#1;
               #15#2 = nPuntero;
               #15#3 = aAlfa;
               |GRABA 020#15n;
               |LIBERA #15;
          |SINO;
               #16#0 = #0#0;
               #16#1 = #0#1;
               #16#2 = nPuntero;
               #16#3 = aAlfa;
               |GRABA 020#16n;
               |LIBERA #16;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO Edita15y16;
     |SI nTipo = 1;
          |ABRE #15;
          |PINPA #0#15;
     |SINO;
          |ABRE #16;
          |PINPA #0#16;
     |FINSI;

     |DIMENSIONA 200, nBuffer, 0;
     |HAZ CogeLineas15y16;
     nModo = 100; nLin = 10; nCol = 78;
     |EDITA nBuffer, 1, 10, 1002, 2279, nModo, "", 0, nLin, nCol;
     |HAZ PoneLineas15y16;
     |FINDIM nBuffer;
     |SI nTipo = 1;
          |CIERRA #15;
     |SINO;
          |CIERRA #16;
     |FINSI;
|FPRC;

|PROCESO EditaTexto;
     |PUSHV 0501 2380;
     |HAZ Edita15y16;
     |POPV;
|FPRC;

|PROCESO ImpreDA;
     enSwDA = 1;
     |PRINT;
     enSwDA = 0;
|FPRC;

|DEFBUCLE ImpreDA;
     #17#1;
     ;
     "P", #2#0, #2#1, #2#2, 001;
     "P", #2#0, #2#1, #2#2, 999;
     ;
     NULL, ImpreDA;
|FIN;

|PROCESO legim004;
     |PRINT;
     nSwPie = 1;
     |BUCLE ImpreDA;
|FPRC;

|DEFBUCLE legim004;
 #legim004#1;
 ;
 #legim003#0, #legim003#1, 001;
 #legim003#0, #legim003#1, 999;
 ;
 NULL, legim004;
|FIN;

|PROCESO Imprimir003;
     |IMPRE 0;
     |SI FSalida = 0;
          |ABRE #legim005;
          |DDEFECTO #legim005;
          #legim005#0 = #legim003#0;
          |LEE 000#legim005.=;
          aInforme = #legim005#3;
          |QBF aInforme;
          |CIERRA #legim005

          |INFOR aInforme;
          |SI FSalida = 0;
               |BUCLE legim004;
               |SI nSwPie = 1;
                    |PIEINF;
               |FINSI;
               |FININF;

               |LEE 101#legim003.=;
               |SI FSalida = 0;
                    #legim003#16 = "S";
                    |PINTA #legim003#16;
                    |GRABA 020#legim003.a;
                    |LIBERA #legim003;
               |FINSI;
          |SINO;
               aMensaje = "0000Informe " + aInforme + " inaccesible.";
               |MENAV aMensaje;
          |FINSI;
          |FINIMP;
     |SINO;
          |MENAV "0000Proceso cancelado.";
     |FINSI;
|FPRC;

|PROCESO Tipo7C1_legim003; |TIPO 7;
     nModo = (FEntrada/100) ! 0;
     |SI nModo = 1;
          |MENU menu;
          |SI FSalida = 1;    ||Automatico
               aSerie = #0#0;

               |DDEFECTO #legim003;
               #legim003#0 = aSerie;
               #legim003#1 = 999999;
               |LEE 000#legim003.];
               |SI FSalida = 0;
                    |LEE 000#legim003.a;
               |SINO;
                    |LEE 000#legim003.u;
               |FINSI;
               |SI FSalida = 0; |Y #legim003#0 = aSerie;
                    nUltimo = #legim003#1 + 1;
               |SINO;
                    nUltimo = 1;
               |FINSI;

               |ABRE #41;
               #41#2 = aSerie;
               #41#0 = "legparte";
               |LEE 101#41=;
               |SI FSalida = 0;
                    contador = #41#1;
                    |SI contador < nUltimo;
                         contador = nUltimo;
                    |FINSI;
                    #41#1 = contador + 1;
                    |GRABA 021#41a;
               |SINO;
                    contador = 1;
                    |SI contador < nUltimo;
                         contador = nUltimo;
                    |FINSI;
                    #41#1 = contador + 1;
                    |GRABA 021#41n;
               |FINSI;
               cont = contador;
               cont = ("000000" + cont) % -106;
               |DDEFECTO #legim003;
               #0#0 = aSerie;
               #0#1 = cont;
               |PINTA #0#0;
               |PINTA #0#1;
               |LIBERA #41;
               |CIERRA #41;
          |FINSI;
     |FINSI;
|FPRC;

|PROGRAMA;
     aParametro = PARAMETRO;
     |QBF aParametro;

     |SI aParametro = "";
          |MANTE #0;
     |SINO;
          |SI aParametro = "MODI";
               |CLS;
               |ABRE #legim003;

               |DDEFECTO #legim003;
               #legim003#0 = eaSerieParte;
               #legim003#1 = enNumeroParte;
               |LEE 000#legim003.=;

               |PINPA #0#legim003;
               |PINDA #0#legim003;
               |ENDATOS #2#legim003;
               |SI FSalida = 0;
                    |GRABA 020#legim003.a;
                    |LIBERA #legim003;

                    aMensaje = "0000NImprimir Parte?";
                    |CONFI aMensaje;
                    |SI FSalida = 0;
                         |HAZ Imprimir003;
                    |FINSI;
               |FINSI;

               |CIERRA #legim003;
          |SINO;
               |SI aParametro = "CONSU";
                    |CLS;
                    |ABRE #legim003;
                    |DDEFECTO #legim003;
                    #legim003#0 = eaSerieParte;
                    #legim003#1 = enNumeroParte;
                    |LEE 000#legim003.=;
                    |PINPA #0#legim003;
                    |PINDA #0#legim003;
                    |PAUSA;
                    |CIERRA #legim003;
               |SINO;
                    |MANTE #0;
               |FINSI;
          |FINSI;
     |FINSI;
|FPRO;

|PROCESO LlenaDA;
     #17#0 = "P";
     #17#1 = #2#0;
     #17#2 = #2#1;
     #17#3 = #2#2;
     #17#4 = #220#1;
     #17#5 = #220#2;
     |GRABA 020#17n;
|FPRC;

|PROCESO BorraDA;
     |BORRA 020#17a;
|FPRC;

|DEFBUCLE BorraDA;
     #17#1;
     ;
     "P", #2#0, #2#1, #2#2, 001;
     "P", #2#0, #2#1, #2#2, 999;
     ;
     NULL, BorraDA;
|FIN;

|PROCESO Min17;
     #17#0 = "P";
     #17#1 = #2#0;
     #17#2 = #2#1;
     #17#3 = #2#2;
     #17#4 = 001;
|FPRC;

|PROCESO Max17;
     #17#0 = "P";
     #17#1 = #2#0;
     #17#2 = #2#1;
     #17#3 = #2#2;
     #17#4 = 999;
|FPRC;

|PROCESO Tipo2Lin; |TIPO 2;
     nModo = (FEntrada / 100) ! 0;
     nForma = 0 +. 1;
     |SI nModo = 1;
          |ABRE #219;
          #219#0 = #2#3;
          |LEE 000#219=;
          |SI FSalida = 0; |Y #219#2 = "S";
               |BUCLE BorraDA;
               |ABRE #17;
               |BUCLELIN 2 LlenaDA #219;
               |CIERRA #17;
          |FINSI;
          |CIERRA #219;
     |FINSI;
     |SI nForma = 1;
          |PUSHV 0501 2380;
          |ENTLINEAL #1#17, 5, 2, 22, 1, Min17, Max17;
          |POPV;
     |FINSI;
     |SI nModo = 3;
          |BUCLE BorraDA;
     |FINSI;
|FPRC;

|PROCESO T11_legim004; |TIPO 11;
     |SI FSalida = 16;
          |PUSHV 0501 2380;
          |ENTLINEAL #1#17, 5, 4, 22, 1, Min17, Max17;
          |POPV;
     |FINSI;
|FPRC;
