|FICHEROS;
     tpv00018 #0;
     agifa501 #1;
     agifa502 #2;
     temp0018 #3;
     agifa505 #4;
|FIN;

|VARIABLES;
     &EURODB  = 0;
     &Tempo   = "";
     aTempo3  = "";
     aDimp    = "";
     aHimp    = "";
     &eFecha  = "";
     &eCaja   = 0;
     k        = 0;
     ll       = "";
     js       = "";
     jj       = "";
     hor      = 0;
     salida   = 0;
     fichero  = "";
     horaen   = "";
     horab    = 0;
     dh1      = 0;
     hh1      = 0;
     dh2      = 0;
     hh2      = 0;
     informe  = "retihod";
     informe1 = "retihor";
     hora     = "";
     &fei      = "";
     caj      = 0;
     &cai      = "";
     &tip      = 0;
     &tota     = 0;
     &totg     = 0;
     &totn     = 0;
     &totm     = 0;
     &toti     = 0;
     sw       = 0;
     fec      = @;
     aDtipo    = "";
     aHtipo    = "";
|FIN;


|PROCESO butemp;

        |SI sw = 0;
          fei = #3#0;
          caj = #3#1;
          cai = #3#1;
          fec = #3#0;
          sw = 1;
        |FINSI;

     |SI caj ! #3#1;  |O fec ! #3#0;
      tip = 1;
      |PRINT;
      tip = 0;
      tota = 0;
      cai = #3#1;
      caj = #3#1;
      fei = #3#0;
      fec = #3#0;
     |FINSI;

      tota = tota + (#3#5 ! EURODB);
      totg = totg + (#3#5 ! EURODB);
      totn = totn + 1;
      tip = 0;
      |PRINT;
      fei = "   '' ";
      cai = "''";

|FPRC;

|DEFBUCLE leetemp;
    #3#1;
    ;
#0#4,#0#12,0;
#0#5,#0#13,999;
;
NULL,butemp;
|FIN;

|PROCESO butemp2;

        |SI sw = 0;
          caj = #3#1;
          fec = #3#0;
          fei = #3#0;
          sw = 1;
        |FINSI;
     |SI fec ! #3#0;
         |ABRE #4;
         #4#0 = caj;
         |LEE 000#4=;
         tip = 0;
         |PRINT;
         |CIERRA #4;
         tip = 1;
         |PRINT;
         tota = 0;
         toti = 0;
         caj = #3#1;
         fec = #3#0;
         fei = #3#0;
     |FINSI;

     |SI caj ! #3#1;
         |ABRE #4;
         #4#0 = caj;
         |LEE 000#4=;
         tip = 0;
         |PRINT;
         |CIERRA #4;
         tota = 0;
         caj = #3#1;
     |FINSI;

     tota = tota + (#3#5 ! EURODB);
     toti = toti + (#3#5 ! EURODB);
     totg = totg + (#3#5 ! EURODB);
     totn = totn + 1;

|FPRC;

|DEFBUCLE leetemp2;
    #3#1;
    ;
#0#4,#0#12,0;
#0#5,#0#13,999;
;
NULL,butemp2;
|FIN;


---------------------------------------------------------------------------
|PROCESO tiqdesC501;
  |SI #1#31 = "R"; |O #1#31 = "A"; |O #1#31 = "L"; |ERROR; |ACABA; |FINSI;
     hora = #1#34; |QBF hora;
     hora = hora - ":";
     hora = hora % 104;
     horaen = hora % 102;
     |HAZ mirahora;
     |SI horab = 0;  |ERROR; |ACABA; |FINSI;

     || -------------------- Grabacion Fichero Temporal....

     hor = horaen;

     |SI hor > 0; |Y  hor < 10;
           hor = 200 + hor;
     |SINO;
           hor = 100 + hor;
     |FINSI;

     #3#0 = #1#14;
     #3#1 = #1#12;
     #3#2 = hor;
     |LEE 000#3=;
     salida = FSalida;

     |SI salida = 0;
            #3#5 = #3#5 + #1#9;
            |GRABA 020#3a;
     |FINSI;
     |SI salida ! 0;
            jj = #1#34 % 102;
            #3#3 = jj + ":00";
            jj = #1#34 % 102;
            #3#4 = jj + ":59";
            #3#5 = #1#9;
            |GRABA 020#3n;
     |FINSI;
|FPRC;

|DEFBUCLE tiq501;
     #1#3;
     33, 12, 13;
     #0#4, "      ", aDtipo, #0#0, #0#2, #0#6, #0#12, aDimp;
     #0#5, "zzzzzz", aHtipo, #0#1, #0#3, #0#7, #0#13, aHimp;
     ;
     NULL, tiqdesC501;
|FIN;
---------------------------------------------------------------------------
|PROCESO inicio; |TIPO 3;
     |SI #0#17 = "T";
          aDtipo = " ";
          aHtipo = "z";
     |SINO;
          aDtipo = #0#17;
          aHtipo = #0#17;
     |FINSI;

     |SI #0#16 = "A";
          aDimp = " ";
          aHimp = "z";
     |SINO;
          aDimp = #0#16;
          aHimp = #0#16;
     |FINSI;

     |HAZ horapre;
     |HAZ CreaTempo; aTempo3 = Tempo;
     |NOME_DAT #3 aTempo3; |DELINDEX #3;
     |ABRE #3;
     |HAZ calhora;
     |IMPRE 0;
     |SI FSalida = 0;
            |SI #0#11 = "D";
                    |INFOR informe;
               |SI FSalida = 0;
                    |BUCLE tiq501;
                    |BUCLE leetemp;
                |SI sw ! 0;
                    tip = 1;
                    |PRINT;
                    tip = 33;
                    totm = totg / totn;
                    |PRINT;
                    |PIEINF;
                    |FININF;
                |FINSI;
               |FINSI;
               |FINIMP;
            |FINSI;
            |SI #0#11 = "R";
                    |INFOR informe1;
               |SI FSalida = 0;
                    |BUCLE tiq501;
                    |BUCLE leetemp2;
                |SI sw ! 0;
                    |ABRE #4;
                    #4#0 = caj;
                    |LEE 000#4=;
                    tip = 0;
                    |PRINT;
                    |CIERRA #4;
                    tip = 1;
                    |PRINT;
                    tip = 33;
                    totm = totg / totn;
                    |PRINT;
                    |PIEINF;
                    |FININF;
                |FINSI;
               |FINSI;
               |FINIMP;
            |FINSI;
     |FINSI;
     |CIERRA #3;
     |DELINDEX #3;  Tempo = aTempo3; |HAZ BoraTempo;
|FPRC;

|PROCESO calhora;

        |SI #0#8 > #0#9;

         dh1 = #0#8;
         hh1 = 2400;
         dh2 = 0000;
         hh2 = #0#9;

        |FINSI;

|FPRC;

|PROCESO mirahora;
        horab = 0;
        |SI #0#8 [ #0#9;
            |SI hora > #0#8; |Y hora < #0#9; horab = 1; |ACABA; |FINSI;
        |SINO;
        |SI hora > dh1; |Y hora < hh1; horab = 1; |ACABA; |FINSI;
        |SI hora > dh2; |Y hora < hh2; horab = 1; |ACABA; |FINSI;
        |FINSI;
|FPRC;

|CALCULO horapre;
             k = 10000 + #0#8;
            js = k;
            jj = js % 202;
            ll = js % -102;
            #0#14 = jj + ":" + ll;
             k = 10000 + #0#9;
            js = k;
            jj = js % 202;
            ll = js % -102;
            #0#15 = jj + ":" + ll;
|FCAL;

|PROGRAMA;
     |SI eFecha ! "";
          |DDEFECTO #0;
          #0#4 = eFecha;
          #0#5 = eFecha;
          |SI eCaja ! 0;
               #0#12 = eCaja;
               #0#13 = eCaja;
               #0#17 = "T";
          |FINSI;
          |HAZ inicio;
     |SINO;
          |MANTE #0;
     |FINSI;
|FPRO;
