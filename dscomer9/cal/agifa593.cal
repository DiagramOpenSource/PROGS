|FICHEROS;
     agifa593 #100;  ||Este
     agifa501 #0;
     agifa502 #1;
     agifa019 #2;
     agifa505 #4;
     agifa509 #8;
     agifa510 #7;
     agifa065 #9;
     agifa024 #14;
     agifa515 #19;
     agifa516 #20;
     agifa517 #30;
     agifa142 #52;
|FIN;

|VARIABLES;
     tipo      = 1;
     operacion = "";
     beta      = 0;
     actua_dia = "";
     i         = 0;
     j         = 0;
     empresa   = 0;
     periodo   = 0;
     aAlfa     = "";
     nNume     = 0;
     nSwGraba  = 0;
     nDto      = 0;
     aMes      = "";
     nMes      = 0;
     nImpo     = 0;
     nImneto   = 0;
     actu_dia  = "";
|FIN;

|INCLUYE i_varart;

|PROCESO actua_tarj;               ||Actualiza las tarjetas
     |SI #0#40 ! 0;
          |ABRE #19;
          |DDEFECTO #19;
          #19#0 = #0#46;
          |LEE 101#19=;
          |SI FSalida ! 0;    |GRABA 021#19b; |FINSI;
          #19#2 = #19#2 + (#0#40*tipo);    ||Total Ptas cargadas
          #19#3 = #0#14;                  ||Fecha Ult. Oper.
          #19#4 = #0#40;                  ||Ptas Cargadas
          |GRABA 021#19a;
          |CIERRA #19;

          |ABRE #20;
          |DDEFECTO #20;
          #20#0 = #0#46;
          #20#1 = #0#14;
          |LEE 101#20=;
          |SI FSalida ! 0;    |GRABA 021#20b; |FINSI;
          #20#2 = #20#2 + (#0#40*tipo);    ||Total Ptas cargadas
          |GRABA 021#20a;
          |CIERRA #20;
     |FINSI;
|FPRC;

|PROCESO horario;
     periodo = 0;
     |PARA i = 1; |SI i [ 19; |HACIENDO i = i + 2;
          j = i + 1;
          periodo = periodo + 1;
          |SI nNume ] #7#i#; |Y nNume [ #7#j#;
               |SAL;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO actua_dia;
     #7#0 = #4#18;
     |LEE 000#7=;

     aAlfa = (#0#34 % 102) + (#0#34 % 402);
     nNume = aAlfa;

     |HAZ horario;
     nNume = periodo + 29;
     #8#nNume# = #8#nNume# + tipo;
     nNume = nNume + 10;
     #8#nNume# = #8#nNume# + (#0#9 *tipo);
     #8#52 = #8#52 + (#0#38*tipo);            ||Efectivo
     #8#53 = #8#53 + (#0#40*tipo);            ||Tarjeta
     #8#54 = #8#54 + (#0#39*tipo);            ||Cheque
     #8#58 = #8#58 + (#0#41*tipo);            ||Pendiente de credito
     #8#55 = #8#55 + ((#0#38 + #0#40 + #0#39)*tipo);
     #8#57 = #8#57 + (#0#15*tipo);            ||Restos no cobrados
|FPRC;

|PROCESO agifa501;
     |SI nSwGraba = 1; nSwGraba = 0; |ACABA; |FINSI;

     ||컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
     |SI #0#3 > 0; beta = 1; |SINO; beta = -1; |FINSI;
     #0#3 = #0#3 * beta;
     nDto = (((#0#3 < #0#17) < #0#19) < #0#18) * beta;
     #0#3 = #0#3 * beta;
     nDto = #0#3 - nDto;   || Total Dto Cabecera
     ||컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
     ||                Cliente
     |DDEFECTO #14;
     #14#0 = #0#1;
     |LEE 101#14=;
     |SI FSalida ! 0; |GRABA 020#14b; |FINSI;

     aMes = #0#14 % A402;
     nMes = ((aMes - 1) * 4) + 54;
     nImneto = #0#3 - nDto;
     |SI #0#35 = "A";
          #14#50 = #14#50 +. nImneto;
          enImpCliPen = nImneto;
     |SINO;
          |SI #52#115 = "N";
               nImpo = (nImneto * 100) / (100 - #0#18);
               nDto = nDto + (nImneto - nImpo);
          |SINO;
               nImpo = nImneto;
          |FINSI;
          #14nMes = #14nMes +. nImpo;
          nMes = nMes + 1;
          #14nMes = #14nMes +. nDto;
          nMes = nMes + 1;
          #14nMes = #14nMes +. #0#8;
          #14#102 = #14#102 +. nImpo;
          #14#103 = #14#103 +. nDto;
          #14#104 = #14#104 +. #0#8;
          #14#45 = #0#14;
          #14#46 = #14#46 + nImneto;
          #14#110 = #14#110 +. nImneto;

          enImpCliCom = nImpo;
          enImpCliDto = nDto;
          enImpCliMar = #0#8;
          enImpCliFac = nImneto;
     |FINSI;
     eaCliente = #0#1;
     efFecha = #0#14;
     |HAZ AcumulaCli;
     |GRABA 020#14a;
     ||컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
     ||                    Resumen
     |SI #0#35 = "T"; |O #0#35 = "F";
          |SI actu_dia = "N";
               #8#24 = #8#24 +. #0#9;   ||Venta con imp.
               #8#25 = #8#25 +. #0#4;   ||Total iva
               #8#26 = #8#26 +. (#0#22+#0#25+#0#47+#0#50+#0#53);
               #8#27 = #8#24 - #8#25 - #8#26;
               #8#28 = #8#28 +. #0#28;  ||Total dto
               #8#29 = #8#29 +. #0#3;   ||Total bruto
          |FINSI;
     |FINSI;
     |SI actu_dia = "N";
         |HAZ actua_dia;                    ||Actualiza el resumen diario
     |FINSI;
     |HAZ actua_tarj;                       ||Actualiza la tarjeta

     |GRABA 020#0a;
     |GRABA 020#8a;
|FPRC;

|PROCESO ActImpoArti;
     aMes = #0#14 % A402;
     nMes = ((aMes - 1) * 5) + 35;
     #2#0 = #1#2;
     |LEE 101#2=;
     |SI FSalida = 0;
          |SI #2#194 = 2;
               nImpo = ((((#1#29 * #1#10) < #1#14) < #0#17) < #0#19);
          |SINO;
               nImpo = ((((#1#5 * #1#10) < #1#14) < #0#17) < #0#19);
          |FINSI;
          |SI #52#115 = "S";
               nImpo = nImpo < #0#18;
          |FINSI;
          enCoste = nImpo - #1#23;
          #2nMes = #2nMes + (nImpo * tipo);
          #2#95 = #2#95 + (nImpo * tipo);
          nMes = nMes + 1;
          #2nMes = #2nMes + (enCoste * tipo);
          #2#96 = #2#96 + (enCoste * tipo);
          |GRABA 020#2a;
          |LIBERA #2;

          efFecha = #0#14;
          eaArticulo = #1#2;
          enImpVta = (nImpo * tipo);
          enImpMar = (enCoste * tipo);
          |HAZ AcumulaArt;
     |FINSI;
|FPRC;

|PROCESO agifa502;
     |SI #0#35 = "T"; operacion = "TI"; |FINSI;
     |SI #0#35 = "A"; operacion = "AV"; |FINSI;
     |SI #0#35 = "F"; operacion = "FC"; |FINSI;
     ||컴컴컴컴컴컴컴컴컴컴컴컴Actualiza Historico, Articulo y Almacen
     #9#0 = #1#2;
     #9#1 = #0#14;
     #9#2 = operacion;
     #9#3 = #0#58;
     #9#4 = #1#1;
     #9#11 = #0#57;
     |LEE 000#9=;
     |SI FSalida = 0;
          nSwGraba = 1;
          |ACABA;
     |FINSI;

     enForma = 1;
     |HAZ AcumulaTPVR;

     |SI #1#21 > 0;
          nNume = 1;
     |SINO;
          nNume = -1;
          #1#21 =  -#1#21;
     |FINSI;
     nImneto = ((((#1#21 < #0#17) < #0#19) < #0#18)) * nNume;

     #1#23 = enCoste;
     #0#8 = #0#8 + (nImneto - enCoste);
     |GRABA 020#1a;
     ||컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
     |SI #0#35 ! "A"; |HAZ ActImpoArti; |FINSI;
|FPRC;

|DEFBUCLE agifa502;
     #agifa502#1;
     ;
     #agifa501#36, #agifa501#0, 001;
     #agifa501#36, #agifa501#0, 999;
     ;
     NULL, agifa502;
|FIN;

|PROCESO Cab;
     |DDEFECTO #8;
     #8#63 = empresa;
     #8#0 = #0#12;
     #8#1 = #0#14;
     |LEE 101#8=;
     |SI FSalida ! 0; |GRABA 021#8b; |FINSI;

     |DDEFECTO #4;
     #4#0 = #0#12;
     |LEE 000#4=;

     |BUCLE agifa502;
     |HAZ agifa501;
|FPRC;


|DEFBUCLE agifa501;
     #0#1;
     14, 12;
     #100#0, 000001, #100#2, #100#4;
     #100#1, 999999, #100#3, #100#5;
     ;
     NULL, Cab;
|FIN;

|PROCESO Tipo3; |TIPO 3;
     |ABRE #30;  |LEE 000#30p; |CIERRA #30;
     |ABRE #52;  |LEE 000#52p; |CIERRA #52;

     actu_dia = #30#7;
     |DFICE aAlfa;  |QBF aAlfa;
     aAlfa = aAlfa % -205;
     empresa = aAlfa;

     |ABRE #9;
     |ABRE #2;
     |ABRE #14;
     |ABRE #7;
     |ABRE #8;
     |ABRE #4;
     |BUCLE agifa501;
     |CIERRA #9;
     |CIERRA #2;
     |CIERRA #14;
     |CIERRA #7;
     |CIERRA #8;
     |CIERRA #4;
|FPRC;


|PROGRAMA;
     |CLS;
     |CABEZA "Actualizacion";

     |ABRE #100;
     |LEE 000#100p;
     |SI FSalida ! 0; |GRABA 020#100n; |FINSI;
     |LEE 101#100p; || Lo Bloqueo

     |PINPA #0#100;
     |PINDA #0#100;
     |ENDATOS #1#100;
     |SI FSalida = 0;
          |GRABA 020#100a;
     |FINSI;

     |CIERRA #100;
|FPRO;
