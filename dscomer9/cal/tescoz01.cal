|FICHEROS;
     tescoz01 #0;
     tescom01 #1; ||Histo
     agifa104 #104;
     agifa073 #73;
     agifa024 #24;
     agifa025 #25;
     agifa104@ #100; ||Temporal
     dsm00053 #53;
     agifa019 #19;
     agifa324 #324;
     agifa321 #321;
|FIN;

|VARIABLES;
     &enTiva = 0;
     &efFiva = @;
     &eaDiva = "";
     &enIva = 0;
     &enRec = 0;
     &enDirEnt      = 0;
     &enUniMarhu    = 0;
     &enDescuento1  = 0;
     &enDescuento2  = 0;
     &enComision    = 0;
     &eaRepre       = "";
     &eaCliente     = "";
     &eaArticulo    = "";
     &eaTComision   = "";
     &efFecha       = @;
     &eaSerie       = "";
     &uni_dt        = 0;
     nCalc     = 0;
     &nImpo    = 0;
     &eaTag    = "";
 {100}aCampo   = "";
     &aDivisa  = "";
     &aMoneda  = "";
     &enForma  = 0;
     &nDeci_PreDivP = 0;
     nHandle   = 0;
     aReg      = "";
     aAlfa     = "";
     aPath     = "";
     aFichero  = "";
     aDef      = "";
     aLetra    = "";
     nPosi     = 0;
     i         = 0;
     aLon      = "";
     aDato     = "";
     nErr      = 0;
     nCambio   = 0;
     nMult     = 0;
     nNume7    = 0;
     nNume11   = 0;
     nImpDiv   = 0;
|FIN;

|PROCESO ElRiesgo;
     |DDEFECTO #24;
     #24#0 = #104#4;
     |LEE 000#24=;

     nImpo = 0;
     enTiva = #agifa073#14;
     efFiva = #agifa104#1;
     |HAZ SacaIVA;


     nCalc = #agifa073#39 % enIva;
     |SI #agifa024#37 = "S";
          nCalc = nCalc + (#agifa073#39 % enRec);
     |FINSI;

     nImpo = nImpo + (#agifa073#39 + nCalc);
     nImpo = nImpo * enForma;
     eaTag = "PE";  |HAZ Riesgos;
|FPRC;

|PROCESO LeeDiv;
     |ABRE #324;
     #324#0 = aDivisa;
     |LEE 000#324=;
     |CIERRA #324;
     nCambio = #324#2;
|FPRC;

|PROCESO DefPedido;
     |DDEFECTO #24;
     #24#0 = #104#4;
     |LEE 000#24=;

     |DDEFECTO #25;
     #25#0 = #104#7;
     |LEE 000#25=;

     #104#8 = #24#1;
     #104#16 = #25#7;
     #104#18 = #24#4;
     aDivisa = #24#192;
     |HAZ LeeDiv;
     #104#35 = aDivisa;
     #104#36 = nCambio;
     #104#37 = #24#193;       ||Moneda Tra
     aDivisa = #agifa321#9;
     |HAZ LeeDiv;
     #104#40 = aDivisa;
     #104#41 = nCambio;
|FPRC;


|PROCESO DefPedLin;
     |DDEFECTO #19;
     |ABRE #19;
     #19#0 = #73#2;
     |LEE 000#19=;
     |CIERRA #19;

     #73#3 = 1;          ||Almacen
     #73#14 = #19#30;
     #73#16 = #104#7;
     #73#18 = #19#2;
     #73#19 = #104#18;
     #73#20 = #104#4;
|FPRC;

|PROCESO Cliente;
||Empresa
     ||aCampo1;
||Cliente
     |DDEFECTO #24;
     aCampo2 = aCampo2 % -106;
     #24#0 = aCampo2;
     |LEE 101#24=;
     |SI FSalida ! 0; |GRABA 020#24b; |FINSI;
||Razon Social
     #24#1 = aCampo3;
||CIF-DNI
     #24#15 = aCampo4;
||Codigo Tipo Servicio
     ||aCampo5;
||Codigo Forma Pago
     #24#20 = aCampo6;
||Calle
     #24#5 = aCampo7;
||Ciudad
     #24#6 = aCampo8;
||Provincia
     #24#7 = aCampo9;
||CodPostal
     aCampo10 = ("00000" + aCampo10) % -105;
     #24#180 = aCampo10;
     #24#178 = aCampo10 % 102;
     #24#179 = aCampo10 % -103;
||Pais
     #24#205 = aCampo11;
||Telefono
     #24#17 = aCampo12;
||Fax
     #24#18 = aCampo13;
||Email
     #24#197 = aCampo14;
||Zona
     #24#3 = aCampo15;
||Moneda
     #24#192 = aCampo16;
||Dto Cab
     #24#37 = aCampo17;
||Dto Lin
     #24#204 = aCampo18;
||Incoterm
     ||aCampo19;

     |GRABA 020#24a;
     |LIBERA #24;
|FPRC;

|PROCESO PedidoLin;
     |DDEFECTO #73;
||Empresa
     ||aCampo1
||Periodo
     ||aCampo2
||Serie Pedido
     #73#28 = aCampo3;
||Codigo Pedido
     #73#0 = aCampo4;
||Linea Pedido
     #73#1 = aCampo5;
     |LEE 101#73=
     |SI FSalida = 0;
          enForma = -1;
          |HAZ ElRiesgo;
     |SINO;
          |GRABA 020#73b;
     |FINSI;
||Codigo Articulo
     #73#2 = aCampo6;
||Descripcion
     #73#4 = aCampo7;
||Precio
     #73#6 = aCampo8;
     #73#38 = aCampo8;
||Cantidad
     #73#5 = aCampo9;
     #73#42 = #73#5;
||Importe Neto
     ||aCampo10;
||%Dto
     #73#8 = aCampo11;
||Observa
     aAlfa = aCampo12;
     |QBF aAlfa;
     |SI aAlfa ! "";
          #53#0 = "P";
          #53#1 = #73#28;
          #53#2 = #73#0;
          #53#3 = #73#1;
          #53#4 = 1;
          |LEE 101#53=;
          #53#5 = aAlfa;
          |SI FSalida = 0;
               |GRABA 121#53a;
               |LIBERA #53;
          |SINO;
               |GRABA 020#53n;
          |FINSI;
     |FINSI;

     |HAZ DefPedLin;
     |HAZ TotalLin73;
     |GRABA 020#73a;
     |LIBERA #73;

     enForma = 1;
     |HAZ AcumulaPV;
     |HAZ ElRiesgo;
|FPRC;

|PROCESO Pedido;
     |DDEFECTO #104;
||Empresa
     ||aCampo1
||Periodo
     ||aCampo2
||Ser Pedido
     #104#25 = aCampo3;
||Cod Pedido
     #104#0 = aCampo4;
     |LEE 101#104=;
     |SI FSalida ! 0; |GRABA 020#104b; |FINSI;
||Fecha dd/mm/aa
     aCampo5 = (aCampo5 % 102)+"."+(aCampo5 % 402)+".20"+(aCampo5 % 702);
     #104#1 = aCampo5;
||Cod Cli
     aCampo6 = aCampo6 % -106;
     #104#4 = aCampo6;
||Cod Tipo Servicio
     ||aCampo7;
||Cod Forma Pago
     #104#9 = aCampo8;
||Tipo N-nacional E-exportacion I-intracomuntario
     || aCampo9;
||Cod Agente
     |QBF aCampo10;
     aCampo10 = ("00" + aCampo10) % -102;
     #104#7 = aCampo10;
||Total Pedido
     ||aCampo11;
||Observa
     #104#53 = aCampo12;
||Fecha Servicio
     |QBF aCampo13;
     |SI aCampo13 = "";
          #104#2 = #104#1;
     |SINO;
          aCampo13 = (aCampo13 % 102)+"."+(aCampo13 % 402)+".20"+(aCampo13 % 702);
          #104#2 = aCampo13;
     |FINSI;
||%Dto
     #104#5 = aCampo14;
||Domicilio Env
     #104#19 = aCampo15;
||Pobla Env
     #104#20 = aCampo16;
||CPostal Env
     #104#59 = aCampo17;
||Prov Env
     #104#20 = aCampo18;
||Incoterm
     ||aCampo19;
||Puerto Destino
     ||aCampo20;

     |HAZ DefPedido;
     |GRABA 020#104a;
     |LIBERA #104;
|FPRC;

|PROCESO Registro;
     IaCampo = aCampo1<-;
     |PARA i = 1; |SI i [ 100; |HACIENDO i = i + 1;
          aCampo = "";
          IaCampo = IaCampo + 1;
     |SIGUE;
     IaCampo = aCampo1<-;
     aLon = aReg % A0;
     aDato = "";
     |PARA i = 1; |SI i [ aLon; |HACIENDO i = i + 1;
          nPosi = i * 100 + 1;
          aAlfa = aReg % nPosi;
          |SI aAlfa = "|";
               aCampo = aDato;
               IaCampo = IaCampo + 1;
               aDato = "";
          |SINO;
               aDato = aDato + aAlfa;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO Procesa;
     |PINTA 2301, aFichero;
     nErr = 0;
     |XABRE "CU", aPath, nHandle;
     |SI FSalida ] 0;
          |XLEE nHandle, 250, aReg;
          |PARA; |SI FSalida ] 0; |HACIENDO;
               |HAZ Registro;
               aAlfa = aFichero > "";
               aAlfa = aAlfa % 104;
               |SI aAlfa = "PEDI";
                    |HAZ Pedido;
                    #100#25 = #104#25;
                    #100#0 = #104#0;
                    |GRABA 000#100n;
                    nErr = 1;
               |FINSI;
               |SI aAlfa = "PEDL";
                    |HAZ PedidoLin;
                    #100#25 = #73#28;
                    #100#0 = #73#0;
                    |GRABA 000#100n;
                    nErr = 1;
               |FINSI;
               |SI aAlfa = "CLIE";
                    |HAZ Cliente;
                    nErr = 1;
               |FINSI;
               |XLEE nHandle, 250, aReg;
          |SIGUE;
          |XCIERRA nHandle;
          |SI nErr = 1;
               |HAZ Historico;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO RecalLinea;
     #73#16 = #104#7;
     #73#17 = #104#16;
     #73#20 = #104#4;

     |SI #73#36 ! 0;
           uni_dt = #73#36;
     |SINO;
           uni_dt = #73#5;
     |FINSI;
     enDescuento1 = #73#8;
     enDescuento2 = #73#29;
     eaTComision  = #73#17;
     eaCliente    = #104#4;
     eaRepre      = #104#7;
     eaArticulo   = #73#2;
     efFecha      = #104#1;
     eaSerie      = #73#28;
     enDirEnt = #104#57;
     |HAZ Comisiones;
     ||컴컴컴컴컴컴컴횺arhuenda
     enUniMarhu = #73#5;
     |HAZ ComiMarhu;
     #73#10 = enComision;

     |GRABA 020#73a
     |LIBERA #73;
|FPRC;

|PROCESO Recalcula;
     #104#25 = #100#25;
     #104#0 = #100#0;
     |LEE 121#104=;
     |SI FSalida = 0;
          |DDEFECTO #25;
          #25#0 = #104#7;
          |LEE 000#25=;
          #100#16 = #25#7;
          |BUCLELIN 0 RecalLinea #104;

          aDivisa = #104#35;
          aMoneda = #104#37;
          |HAZ Divisas104;
          |HAZ CalCabAgifa104;
          |GRABA 020#104a;
          |LIBERA #104;
     |FINSI;
|FPRC;

|DEFBUCLE Recalcula;
     #100#1002;
     ;
     "     ", 000000;
     "zzzzz", 999999;
     ;
     NULL, Recalcula;
|FIN;

|PROCESO Historico;
     aAlfa = Usuario % 810;
     #1#0 = aFichero;
     #1#1 = ~;
     #1#2 = aAlfa;
     |GRABA 020#1n;
|FPRC;

|PROCESO Fichero;
     aFichero = "";
     |PARA i = 1; |SI i [ 200; |HACIENDO i = i + 1;
          nPosi = (i * -100) - 1;
          aLetra = aPath % nPosi;
          |SI aLetra = "\"; |O aLetra = "/";
               |SAL;
          |FINSI;
          aFichero = aLetra + aFichero;
     |SIGUE;
     aFichero = aFichero - ".txt";
     aFichero = aFichero - ".TXT";
     aLon = aFichero % A0;
     |SI aLon ! 8; aFichero = ""; |FINSI;
|FPRC;

|PROCESO Tipo3; |TIPO 3;
     |SI #0#2 = "SI";
          |ABRE #321; |LEE 000#321p; |CIERRA #321;

          aPath = #0#1; |QBF aPath;
          |ABRE #1;
          |ABRE #100;
          |ABRE #104;
          |ABRE #73;
          |ABRE #24;
          |ABRE #53;
          |ABRE #25;
          |R_PDIR aPath;
          |PARA; |SI FSalida = 0; |HACIENDO;
               |HAZ Fichero;
               |SI aFichero ! "";
                    #1#0 = aFichero;
                    |LEE 000#1=;
                    |SI FSalida ! 0;
                         |HAZ Procesa;
                    |FINSI;
               |FINSI;
               |R_SDIR aPath;
          |SIGUE;
          |CIERRA #25;
          |CIERRA #53;
          |CIERRA #24;
          |CIERRA #73;
          |CIERRA #104;
          |CIERRA #1;
          |CIERRA #100;
          |ABRE #104;
          |ABRE #24;
          |BUCLE Recalcula;
          |CIERRA #24;
          |CIERRA #104;
     |FINSI;
|FPRC;

|PROGRAMA;
     aCampo5 = "21/05/03";


     |DDEF aDef;
     aDef = aDef + "agifa104";
     |CARGA_DEF aDef, agifa104@;
     aAlfa = Usuario; |QBF aAlfa;
     |NOME_DAT #100 aAlfa;
     |DELINDEX #100;

     |CLS;
     |ABRE #0;
     |LEE 101#0p;
     |SI FSalida ! 0; |GRABA 020#0b; |FINSI;
     #0#2 = "NO";
     |PINPA #0#0;
     |PINDA #0#0;
     |ENDATOS #1#0;
     |SI FSalida = 0;
          |GRABA 020#0a;
     |FINSI;
     |CIERRA #0;

     |DELINDEX #100;
     |DESCARGA_DEF #agifa104@;
|FPRO;
