|FICHEROS;
con00655 #1,MANTE;
con00656 #2;
con00657 #3,MANTE;
con00653 #4;
con00650 #5;
con00659 #6,MANTE;
con00660 #7;
con00661 #8,MANTE;
con00652 #10,MANTE;
con00625 #20;
con00626 #21;
con00663 #30; || Facturas Coches Nuevos
contmp01 #50; || tmp ver sin facturar
con00662 #62;
con00637 #637;
con00691 #691;
|FIN;

|VARIABLES;
     &enTiva = 0;
     &efFiva = @;
     &eaDiva = "";
     &enIva = 0;
     &enRec = 0;
     nModo = 0;
     nForma = 0;
     nDto = 0;
     &Tempo = "";
     aTmp50 = "";
     aAlfa = "";
     &EURODB = 0;
&sw_inf = 0;
&texto1 = "";
&texto11 = "";
&texto2 = "";
&texto22= "";
&texto3 =  "------------------------------------------------------------";
&texto33 = "------------------------------------------------------------";
hay_nuevos = "";
aAlfa1 = "";
nMod = 0;
mensaje = "";
base = 0;
iva = 0;
basti = "";
basti1 = "";
modo = 0;
opcion = 0;
fondo      = "°";
sw = 0;
x = 0;
posici = 0;
horizo = "";
 {-1} menu      = "";
    menu1       = "2400";
    menu2       = "1";
    menu3       = "";
    menu4       = "ECIVOTR"
    menu5       = "Economicos";
    menu6       = "Cliente ";
    menu7       = "Incidencia";
    menu8       = "V.O";
    menu9       = "Ordenes R.";
    menu10      = "Totales";
    menu11      = "Resumen ";
|FIN;

|PROCESO calcula_perdida;
#8#5 = #8#3 - #8#4;
|PINTA #8#5;
|FPRC;

|PROCESO pasacampos;
|SI #3#10 = "S";
     #3#6 = #5#31;
     #3#8 = #5#30;
     |HAZ lee_impuestos;
     #3#7 = enIva;
     #3#9 = #62#2;
     |PINTA #3#6;
     |PINTA #3#7;
     |PINTA #3#8;
     |PINTA #3#9;
|SINO;
     |SI #3#10 ! "A";
          #3#6 = 0;
          #3#8 = "";
          #3#7 = 0;
          #3#9 = 0;
         |PINTA #3#6;
         |PINTA #3#7;
         |PINTA #3#8;
         |PINTA #3#9;
     |FINSI;
|FINSI;
|FPRC;

|PROCESO borra_del;
|BORRA 001#3a;
|LIBERA #3;
|FPRC;

|DEFBUCLE borra_de;
#3#1;
;
#5#0 , 001;
#5#0 , 999;
;
NULL , borra_del;
|FIN;

|PROCESO b_lino1;
|BORRA 021#21a;
|LIBERA #21;
|FPRC;

|DEFBUCLE b_linor1;
#21#1;
;
#20#0 , #20#1 , 001;
#20#0 , #20#1 , 999;
;
NULL , b_lino1;
|FIN;


|PROCESO busca_or1;
|BUCLE b_linor1;
|BORRA 021#20a;
|LIBERA #20;
|FPRC;

|DEFBUCLE borra_o1;
#20#1;
;
#6#2 , #6#3;
#6#2 , #6#3;
;
NULL , busca_or1;
|FIN;

|PROCESO borra_orl;
     aAlfa = "con00802;" + #6#2 + #6#3;
     |SUB_EJECUTA_NP aAlfa;

     |BUCLE borra_o1;
     |BORRA 001#6a;
     |LIBERA #6;
|FPRC;

|DEFBUCLE borra_or;
#6#1;
;
#5#0 , 001;
#5#0 , 999;
;
NULL , borra_orl;
|FIN;

|PROCESO borra_incil;
|BORRA 001#2a;
|LIBERA #2;
|FPRC;

|DEFBUCLE borra_inci;
#2#1;
;
#5#0 , 001;
#5#0 , 999;
;
NULL , borra_incil;
|FIN;

|PROCESO borra_vul;
|BORRA 001#8a;
|LIBERA #8;
|FPRC;

|DEFBUCLE borra_vu;
#8#1;
;
#5#0 , 001;
#5#0 , 999;
;
NULL , borra_incil;
|FIN;

|PROCESO borra_clil;
|BORRA 001#1a;
|LIBERA #1;
|FPRC;

|DEFBUCLE borra_cli;
#1#1;
;
#5#0 ;
#5#0 ;
;
NULL , borra_clil;
|FIN;

|PROCESO borra_total;
|BORRA 001#7a;
|LIBERA #7;
|FPRC;

|DEFBUCLE borra_totales;
#7#1;
;
#5#0 ;
#5#0 ;
;
NULL , borra_total;
|FIN;

|PROCESO tipo22; |TIPO 2;
nForma = 0 +. 1;
modo = (FEntrada / 100) ! 0;

|SI modo = 2; |O modo = 3;
     |SI #5#24 ! 0;
          |SI nForma = -1;
               |MENAV "0000Vehiculo facturado...";
          |FINSI;
          |ERROR; |ACABA;
     |FINSI;
|FINSI;

|SI modo = 3;
     |BUCLE borra_de;
     |BUCLE borra_cli;
     |BUCLE borra_inci;
     |BUCLE borra_vu;
     |BUCLE borra_or;
     |BUCLE borra_totales;
|FINSI;
|FPRC;

|PROCESO graba_tmp;
#50#0 = #5#0;
#50#1 = #5#28;
#50#2 = #5#1;
#50#3 = #5#2;
#50#4 = #5#3;
#50#5 = #5#4;
#50#6 = #5#22;
|GRABA 000#50n;
|LIBERA #50;
hay_nuevos = "S";
|FPRC;

|DEFBUCLE crea_tmp;
#5#3;
;
"  " , 000000 , "                         ";
"  " , 000000 , "zzzzzzzzzzzzzzzzzzzzzzzzz";
;
NULL , graba_tmp;
|FIN;

|PROCESO efe11; |TIPO 66;
  |CONFI "0000SSolo Pendientes de Facturar";
  |SI FSalida = 0;
       |HAZ CreaTempo; aTmp50 = Tempo;
       |NOME_DAT #50 aTmp50; |DELINDEX #50;
       |ABRE #50;
       hay_nuevos = "N";
       |BUCLE crea_tmp;
       |SI hay_nuevos = "S";
            |CONSULTA_CLAVE #1#50;
            |SI FSalida = 0;
               #5#0 = #50#0;
               |LEE 000#5=;
               |PINDA #0#5;
            |SINO;
               |ERROR;
            |FINSI;
       |SINO;
          |MENAV "0000No existen vehiculos pendientes de facturar";
       |FINSI;
       |CIERRA #50;
       Tempo = aTmp50; |HAZ BoraTempo; |DELINDEX #50;
  |SINO;
       |CONSULTA_F_CLAVE #1#con00650 , min_vehi , max_vehi;
  |FINSI;
|FPRC;

|PROCESO min_vehi;
#5#0 = "";
|FPRC;

|PROCESO max_vehi;
#5#0 = "zzzzzzzzzzzzzzzzzzzzzzzzz";
|FPRC;

|PROCESO min_vehis;
#50#0 = "                         ";
|FPRC;
|PROCESO max_vehis;
#50#0 = "zzzzzzzzzzzzzzzzzzzzzzzzz";
|FPRC;


|PROCESO inciden;
|PUSHV 0501 2380;
|SI #5#24 ! 0; |Y #691#4 = "N";
     |ENTLINEAL #1#2,2,4,22,1,min,max;
|SINO;
     |ENTLINEAL #1#2,2,1,22,1,min,max;
|FINSI;
|POPV;
|FPRC;

|PROCESO imprime;
|PRINT;
|FPRC;

|DEFBUCLE lee_economicos;
#3#1;
;
#5#0 , 001;
#5#0 , 999;
;
NULL , imprime;
|FIN;

|PROCESO lee_clientes;
|ABRE #1;
#1#0 = #5#0;
|LEE 000#1=;
|SI FSalida = 0;
     |PRINT;
|FINSI;
|FPRC;

|DEFBUCLE lee_inci;
#2#1;
;
#5#0 , 001;
#5#0 , 999;
;
NULL , imprime;
|FIN;

|DEFBUCLE lee_usados;
#8#1;
;
#5#0 , 001;
#5#0 , 999;
;
NULL , imprime;
|FIN;

|DEFBUCLE lee_lineas_or;
#21#1;
;
#6#2 , #6#3 , 0001;
#6#2 , #6#3 , 9999;
;
NULL , imprime;
|FIN;

|PROCESO saca_lineas_or;
|BUCLE lee_lineas_or;
|FPRC;

|DEFBUCLE lee_or;
#6#1;
;
#5#0 , 001;
#5#0 , 999;
;
NULL , saca_lineas_or;
|FIN;

|PROCESO resumen;
|IMPRE 0 ;
|SI FSalida = 0;
     |INFOR "con06501";
     |SI FSalida = 0;
          sw_inf = 0;
          texto1 = "";
          texto11 = "";
          texto2 = "Descripcion                    Precio Venta   Importe Dto     Coste   %IV";
          texto22= "A %Mat  Beneficio Factura";
          |PRINT;
          sw_inf = 1;
          |BUCLE lee_economicos;
          sw_inf = 11;
          |PRINT;
          sw_inf = 0;
          texto2 = "                                  Datos de Clientes";
          texto22 = "";
          |PRINT;
          sw_inf = 2;
          |HAZ lee_clientes;
          sw_inf = 0;
          texto2 = "                                  Comentarios e Incidencias";
          texto22 = " ";
          |PRINT;
          sw_inf = 5;
          |BUCLE lee_inci;
          sw_inf = 0;
          texto1 = "                       VEHICULOS USADOS ";
          texto11 = "";
          texto2 = "Matricula   Valor pasado     Valor Real   Exceso Valor    F.Entrada";
          texto22= "";
          |PRINT;
          sw_inf = 7;
          |BUCLE lee_usados;
          sw_inf = 0;
          texto1 = "                       ORDENES DE REPARACION";
          texto11 = "";
          texto2 = "Orden  Referencia Articulo  Descripcion                    ";
          texto22 = "Cantidad      Precio %Dto.  Imp.Dto.  Imp. Total";
          |PRINT;
          sw_inf = 9;
          |BUCLE lee_or;
          |PUSHV 0501 2380;
          |ABRE #7;
          #7#0 = #5#0;
          |LEE 001#7=;
          |SI FSalida ! 0 ; |DDEFECTO #7; |FINSI;
          |CIERRA #7;
          |PIEINF;
     |FINSI;
     |FININF;
|FINSI;
|FINIMP;
|FPRC;

|PROCESO usados;
|PUSHV 0501 2380;
|SI #5#24 ! 0; |Y #691#4 = "N";
     |ENTLINEAL #1#8,2,4,22,1,min5,max5;
|SINO;
     |ENTLINEAL #1#8,2,1,22,1,min5,max5;
|FINSI;
|POPV;
|FPRC;

|PROCESO min5;
#8#0 = #5#0;
#8#1 = 1;
|FPRC;

|PROCESO max5;
#8#0 = #5#0;
#8#1 = 999;
|FPRC;

|PROCESO min;
#2#0 = #5#0;
#2#3 = 1;
|FPRC;

|PROCESO max;
#2#0 = #5#0;
#2#3 = 999;
|FPRC;

|PROCESO lee_impuestos;
|ABRE #62;
#62#0 = #3#8;
|LEE 000#62=;
|CIERRA #62;

enTiva = #3#6;
efFiva = ~;
|HAZ SacaIVA;
|FPRC;

|PROCESO alta_precio;
|ABRE #10; || Modelos
#10#0 = #5#2;
#10#1 = #5#3;
|LEE 001#10=;
|SI FSalida ! 0; |DDEFECTO #10; |FINSI;
|LIBERA #10;
|CIERRA #10;
|DDEFECTO #3;
#3#0 = #5#0;
#3#1 = 1;
#3#3 = "IMPORTE VEHICULO";
#3#4 = #10#3;
#3#5 = #10#4;
#3#6 = #5#31;
#3#8 = #5#30;
|HAZ lee_impuestos;
#3#7 = enIva;
#3#9 = #62#2;
|GRABA 021#3n;
|LIBERA #3;
|FPRC;

|PROCESO alta_color;
|ABRE #4;
#4#0 = #5#4;
#4#4 = #5#28;
#4#5 = #5#1;
#4#6 = #5#2;
#4#7 = #5#3;
|LEE 001#4=;
|SI FSalida ! 0 ; |DDEFECTO #4; |FINSI;
|LIBERA #4;
|CIERRA #4;
|DDEFECTO #3;
#3#0 = #5#0;
#3#1 = 2;
#3#2 = #5#4;
#3#3 = #5#17;
#3#4 = #4#2;
#3#5 = #4#3;
#3#6 = #5#31;
#3#8 = #5#30;
#3#7 = enIva;
#3#9 = #62#2;
|GRABA 021#3n;
|LIBERA #3;
|SI #4#9 ! 0;
     |DDEFECTO #3;
     #3#0 = #5#0;
     #3#1 = 4;
     #3#2 = "";
     #3#3 = #4#8;
     #3#4 = #4#9;
     #3#5 = #4#10;
     #3#6 = #5#31;
     #3#8 = #5#30;
     #3#7 = enIva;
     #3#9 = #62#2;
     |GRABA 021#3n;
     |LIBERA #3;
|FINSI;
|FPRC;

|PROCESO alta_trans;
|DDEFECTO #3;
#3#0 = #5#0;
#3#1 = 3;
#3#2 = #10#5;
#3#3 = "TRANSPORTE";
#3#4 = #10#6;
#3#5 = #10#12;
#3#6 = #5#31;
#3#8 = #5#30;
#3#7 = enIva;
#3#9 = #62#2;
|GRABA 021#3n;
|LIBERA #3;
|FPRC;

|PROCESO EconomicoFac; |TIPO 0;
     |SI #5#24 ! 0; |Y #3#10 = "S";
          |MENAV "0000Vehiculo facturado...";
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO Economico2; |TIPO 2;
     nModo = (FEntrada / 100) ! 0;
     nForma = 0 +. 1;
     |SI #5#24 ! 0; |Y nModo ! 1; |Y #3#10 = "S";
          |SI nForma = -1;
               |MENAV "0000No se puede modificar, esta facturado...";
          |FINSI;
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO datosec;
|PUSHV 0501 2380;
|ABRE #3;
#3#0 = #5#0;
#3#1 = 1;
|LEE 001#3];
basti = #3#0;
basti1 = #5#0;
|QBF basti;
|QBF basti1;
|SI FSalida ! 0 ; |O basti ! basti1;
     |HAZ alta_precio;
     |HAZ alta_color;
     |HAZ alta_trans;
|FINSI;
|LIBERA #3;
|CIERRA #3;
|SI #5#24 ! 0; |Y #691#4 = "N";
     |ENTLINEAL #1#3,2,4,21,1,min2,max2;
|SINO;
     |ENTLINEAL #1#3,2,1,21,1,min2,max2;
|FINSI;
|POPV;
|FPRC;

|PROCESO min2;
#3#0 = #5#0;
#3#1 = 1;
|FPRC;

|PROCESO max2;
#3#0 = #5#0;
#3#1 = 999;
|FPRC;

|PROCESO cliente;
|PUSHV 0501 2380;
|ABRE #1;
#1#0 = #5#0;
|LEE 101#1=;
|SI FSalida ! 0;
     |DDEFECTO #1;
     #1#0 = #5#0;
     |GRABA 021#1c;
|FINSI;
|PINPA #0#1;
|PINDA #0#1;
|SI #5#24 = 0;
     |ENDATOS #1#1;
     |SI FSalida = 0;
          |GRABA 021#1a;
     |FINSI;
|SINO;
     |PAUSA;
|FINSI;
|LIBERA #1;
|CIERRA #1;
|POPV;
|FPRC;

|PROCESO OrdenR2; |TIPO 2;
     nModo = (FEntrada / 100) ! 0;
     nForma = 0 +. 1;
     |SI #5#24 ! 0; |Y nModo ! 1;
          |SI nForma = -1;
               |MENAV "0000No se puede modificar, esta facturado...";
          |FINSI;
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO orden_r;
|PUSHV 0501 2380;
|SI #5#24 ! 0; |Y #691#4 = "N";
     |ENTLINEAL #1#6,2,4,22,1,min3,max3;
|SINO;
     |ENTLINEAL #1#6,2,1,22,1,min3,max3;
|FINSI;
|POPV;
|FPRC;

|PROCESO min3;
#6#0 = #5#0;
#6#1 = 1;
|FPRC;

|PROCESO max3;
#6#0 = #5#0;
#6#1 = 999;
|FPRC;

|PROCESO suma_ord;
#7#2 = #7#2 + #6#4;
|FPRC;

|DEFBUCLE suma_or;
#6#1;
;
#5#0 , 001;
#5#0 , 999;
;
NULL , suma_ord;
|FIN;

|PROCESO suma_dec;
     #7#27 = #7#27 + #3#11;
     #7#2 = #7#2 + #3#5;
     |SI #3#10 = "S";
          |SI #3#7 ! 0;
              base = #3#6 + 3;
              iva = base + 3;
              #7base = #7base + #3#4;
              #7iva = #3#7;
          |SINO;
              #7#13 = #7#13 + #3#4;
          |FINSI;
     |FINSI;
     |SI #3#10 = "A";
          |SI #3#7 ! 0;
               base = #3#6 + 15;
               iva = base + 3;
               #7base = #7base + #3#4;
               #7iva = #3#7;
          |SINO;
               #7#25 = #7#25 + #3#4;
          |FINSI;
     |FINSI;
     #7#15 = #7#15 + ((#3#4 % #3#9)!EURODB);

|FPRC;

|DEFBUCLE suma_de;
#3#1;
;
#5#0 , 001;
#5#0 , 999;
;
NULL , suma_dec;
|FIN;

|PROCESO suma_usa;
#7#2 = #7#2 + #8#5;
|FPRC;

|DEFBUCLE suma_usados;
#8#1;
;
#5#0 ,   1;
#5#0 , 999;
;
NULL , suma_usa;
|FIN;

|PROCESO RestaDto;
     |SI #3#6 = 1; #7#4 = #7#4 - #3#11; |FINSI;
     |SI #3#6 = 2; #7#5 = #7#5 - #3#11; |FINSI;
     |SI #3#6 = 3; #7#6 = #7#6 - #3#11; |FINSI;
|FPRC;

|DEFBUCLE RestaDto;
     #3#1;
     ;
     #5#0 , 001;
     #5#0 , 999;
     ;
     NULL , RestaDto;
|FIN;


|PROCESO suma_totales;
  ||   |BUCLE suma_or;
     |BUCLE suma_de;
||     |BUCLE suma_usados;
     |BUCLE RestaDto;
     #7#1 = #7#4 + #7#5 + #7#6 + #7#13 + #7#16 + #7#17 + #7#18 + #7#25;
     #7#3 = #7#1 - #7#2; || - #7#27;
     #7#10 = (#7#4 % #7#7) ! EURODB;
     #7#11 = (#7#5 % #7#8) ! EURODB;
     #7#12 = (#7#6 % #7#9) ! EURODB;
     #7#22 = (#7#16 % #7#19) ! EURODB;
     #7#23 = (#7#17 % #7#20) ! EURODB;
     #7#24 = (#7#18 % #7#21) ! EURODB;
     #7#14 = #7#4 + #7#5 + #7#6 + #7#10 + #7#11 + #7#12 + #7#13;
     #7#26 = #7#16 + #7#17 + #7#18 + #7#22 + #7#23 + #7#24 + #7#25;
|FPRC;



|PROCESO total;
|ABRE #7;
#7#0 = #5#0;
|LEE 001#7=;
|SI FSalida = 0; |BORRA 001#7a; |FINSI;
|LIBERA #7;
|DDEFECTO #7;
#7#0 = #5#0;
|GRABA 001#7c;
|LIBERA #7;
|HAZ suma_totales;
|GRABA 001#7a;
|LIBERA #7;
|SI opcion = 6;
    |PUSHV 0501 2380;
    |BLANCO 901 2380;
    |PINPA #0#7;
    |PINDA #0#7;
    |PAUSA;
    |POPV;
|FINSI;
|CIERRA #7;
|FPRC;

|PROCESO opciones; |TIPO 20;
     aAlfa1 = FSalida;

     |SI aAlfa1 = "OPCION";
         FSalida = "Opciones";
         |ACABA;
     |FINSI;
     |ABRE #691; |LEE 000#691p; |CIERRA #691;

|PUSHV 0501 2380;
opcion = 1;
sw = 0;
|PARA; |SI sw = 0; |HACIENDO;
   menu2  = opcion;

   |MENU menu;
   opcion = FSalida;
   |SI opcion ] 1; |Y opcion [ 8;
       |SI opcion = 1; |HAZ datosec; |FINSI;
       |SI opcion = 2; |HAZ cliente; |FINSI;
       |SI opcion = 3; |HAZ inciden; |FINSI;
       |SI opcion = 4; |HAZ usados; |FINSI;
       |SI opcion = 5; |HAZ orden_r; |FINSI;
       |SI opcion = 6; |HAZ total;   |FINSI;
       |SI opcion = 7; |HAZ resumen;   |FINSI;
   |SINO;
       |HAZ total;
       sw = 1;
   |FINSI;
|SIGUE;
|POPV;
|FPRC;

|PROCESO pinta_pan;
|SI modo = 4; |ACABA ; |FINSI;
|PINPA #0#5;
|PINDA #0#5;
|FPRC;

|PROCESO tipo33; |TIPO 3;
modo = (FEntrada / 100) ! 0;
|SI modo = 1;
     |HAZ opciones;
|FINSI;
|FPRC;

|PROCESO IntroDto; |TIPO 0;
     |SI FSalida = 13; || F5
          |BLIN 24;
          |ATRI I;
          |PINTA 2430, "Introduzca el %Dto: ";
          |ATRI 0;
          nDto = 0;
          E_sup = 99.99;
          E_inf = 0;
          nDto = 2450 ? 1;
          #3#11 = #3#4 * nDto / 100;
          |PINTA #3#11;
          |BLIN 24;
     |FINSI;
|FPRC;

|PROCESO IVA650_0; |TIPO 0;
     enTiva = #5Campo;
     efFiva = ~;
     |HAZ SacaIVA;
     |SI eaDiva = "";
          |MENAV "0000No existe tipo iva para esta fecha...";
          |ERROR; |ACABA;
     |FINSI;
|FPRC;

|PROCESO IVA650_66; |TIPO 66;
     enTiva = #5Campo;
     efFiva = ~;
     |HAZ ConsuIVA;
     |SI eaDiva = "";
          |ERROR;
     |SINO;
          #5Campo = enTiva; |PINTA #5Campo;
     |FINSI;
|FPRC;
