|FICHEROS;
     pcegm033 #33;
     pcegm034 #34;
|FIN;

|VARIABLES;
     &enSwAuto = 0;
     aIP = "";
     nHandle = 0;
     a0D = "";
     a0A = "";
     aLon = "";
     aTab = "";
     aCar0 = "";
     aCar1 = "";
     aEsOk = "";
     aLinea = "";
     aTmp = "";
     aTmp1 = "";
     nSal = 0;
     nError = 0;
     aAlfa = "";
     {99}Valor = "";
     aCar = "";
     aCampo = "";
     nCampo = "";
     nNume  = 0;
     aCopia = "";
     aBasketNum = "";
     aIzq = "";
     aDer = "";
     nPos = 0;
     i = 0;

     aNom = "";
     aFic = "";
     j = 0;
     aPath  = "";
     aDes  = "";
     nReg  = 0;
|FIN;

|PROCESO SacaBasketNum;
     aBasketNum = "";
     aAlfa = aCopia - "basketnumber:";
     |SI FSalida ! 0;
          aAlfa = ("     " + FSalida) % -105;
          aIzq = aAlfa % 103;
          aDer = aAlfa % 402;
          nPos = aIzq;
          nPos = nPos + aDer;
          aLon = aCopia % A0;
          |PARA i = nPos; |SI i [ aLon; |HACIENDO i = i + 1;
               nPos = i * 100 + 1;
               aCar = aCopia % nPos;
               aBasketNum = aBasketNum + aCar;
          |SIGUE;
     |FINSI;
     |QBT aBasketNum;
|FPRC;

|PROCESO Registro;
     |HAZ SacaBasketNum;

     |DDEFECTO #34;
     #34#0 = Valor1;
     aAlfa = (Valor2 % 902) + "." + (Valor2 % 602) + "." + (Valor2 % 104);
     #34#1 = aAlfa;
     aAlfa = Valor2 % 1208;
     #34#2 = aAlfa;
     #34#3 = Valor3;
     #34#4 = Valor4;
     #34#5 = Valor5;
     #34#6 = Valor6;
     #34#7 = Valor7;
     #34#8 = Valor8;
     #34#9 = ~;
     |HORA aAlfa;
     #34#10 = aAlfa;
     ||#34#16 = Valor9;
     #34#16 = aBasketNum;
     |OEM_ANSI #34#5, #34#5;

     |GRABA 000#34n;
     |SI FSalida ! 0;
          aAlfa = "0000Registro existente: " + Valor1;
          |SI enSwAuto ! 0; |MENAV aAlfa; |FINSI;
     |FINSI;
|FPRC;

|PROCESO Caracter1;
     |SI aCar = aTab;
          Valor = aCampo;
          aCampo = "";
          IValor = IValor + 1;
          nCampo = nCampo + 1;
          |SI nCampo > 90; |ACABA; |FINSI; || Por si.... :)
     |SINO;
          aCampo = aCampo + aCar;
     |FINSI;
|FPRC;

|PROCESO SacaNomb;
     aNom = "";
     aLon = aFic % A0;
     |PARA j = 1; |SI j [ aLon; |HACIENDO j = j + 1;
          nPos = -(j * 100 + 1);
          aAlfa = aFic % nPos;
          |SI aAlfa = "/"; |O aAlfa = "\"; |SAL; |FINSI;
          aNom = aAlfa + aNom;
     |SIGUE;
|FPRC;

|PROCESO ProcesaLinea;
     aCopia = aLinea;
     IValor = Valor1<-;
     nCampo = 0;
     aCampo = "";
     |PARA; |SI aLinea ! ""; |HACIENDO;
          aCar = aLinea % 101;
          |SI aLinea = aCar;
               aLinea = ""; ||con un caracter falla: "aLinea = aLinea % 2"
          |SINO;
               aLinea = aLinea % 2;
          |FINSI;
          |HAZ Caracter1;
     |SIGUE;
     |HAZ Registro;

     aLinea = ""; aEsOk = "N";
|FPRC;

|PROCESO Caracter;
     |SI aCar0 = a0D; |O aCar0 = a0A;
          |SI aEsOk = "S";
               |HAZ ProcesaLinea;
          |FINSI;
          aLinea = ""; aEsOk = "N";
     |FINSI;
     |SI aCar0 ! a0D; |Y aCar0 ! a0A;
          aLon = aLinea % A0;
          |SI aLon < 250;
               aLinea = aLinea + aCar0;
          |FINSI;
     |FINSI;

     aLinea = aLinea - "Purchase"; || cadena buscar para saber la linea OK
     |SI FSalida > 0;
          aEsOk = "S";
     |FINSI;
|FPRC;

|PROCESO ImportaFic;
     aLinea = "";

     |ABRE #34;
     aTab = &9;
     a0D = &13;
     a0A = &10;
     aIP = "";
     |IP_REMOTO aIP;
     |SI aIP ! "";
          |XABRE "BC", aFic, nHandle;
     |SINO;
          |XABRE "B", aFic, nHandle;
     |FINSI;

     |SI FSalida ] 0;
          |XLEE nHandle, 250, aTmp;
          |PARA; |SI FSalida > 0; |HACIENDO;
               |PARA; |SI aTmp ! ""; |HACIENDO;
                    aCar0 = aTmp % 101;
                    aCar1 = aTmp % 201;
                    |SI aTmp = aCar0;
                         aTmp = ""; ||con un caracter falla: "aTmp = aTmp % 2"
                         |XLEE nHandle, 250, aTmp1;
                         nSal = FSalida;
                         aCar1 = aTmp1 % 101;
                    |SINO;
                         aTmp = aTmp % 2;
                    |FINSI;
                    |HAZ Caracter;
                    |SI nError ! 0; |SAL; |FINSI;
               |SIGUE;
               aTmp = aTmp1;
               FSalida = nSal;
               |SI nError ! 0; |SAL; |FINSI;
          |SIGUE;
          |SI aEsOk = "S"; |HAZ ProcesaLinea; |FINSI;

          |XCIERRA nHandle;
     |SINO;
          aAlfa = aFic % -160;
          aAlfa = "0000ERROR al abrir:" + aAlfa;
          |SI enSwAuto ! 0; |MENAV aAlfa; |FINSI;
     |FINSI;
     |CIERRA #34;
     nReg  = nReg + 1;
|FPRC;

|PROCESO Importalos;
     aPath = #33#1; |QBF aPath;
     aPath = aPath + "*.*";

     aIP = "";
     |IP_REMOTO aIP;
     |SI aIP = "";
          |_PDIR aPath;
          |PARA; |SI FSalida = 0; |HACIENDO;
               aFic = aPath;
               |BLIN 23; |PINTA 2301, aFic;
               |HAZ ImportaFic;
               |SI nError = 0;
                    |HAZ SacaNomb;
                    aDes = #33#2; |QBF aDes;
                    aDes = aDes + aNom;
                    |RENOMBRA_FICHERO aFic, aDes;
               |FINSI;
               |_SDIR aPath;
          |SIGUE;
     |SINO;
          |R_PDIR aPath;
          |PARA; |SI FSalida = 0; |HACIENDO;
               aFic = aPath;
               |BLIN 23; |PINTA 2301, aFic;
               |HAZ ImportaFic;
               |SI nError = 0;
                    |HAZ SacaNomb;
                    aDes = #33#2; |QBF aDes;
                    aDes = aDes + aNom;
                    |RRENOMBRA_FICHERO aFic, aDes;
               |FINSI;
               |R_SDIR aPath;
          |SIGUE;
     |FINSI;

     aAlfa = "0000Procesados " + nReg + " ficheros.";
     |SI enSwAuto = 0; |MENAV aAlfa;  |FINSI;
|FPRC;
