|FICHEROS;
     iber0217 #0;   || Pantalla
     iber0216 #43;   || Pantalla
     agifa019 #1;   || Articulos
     agifa017 #2;   || Stock almacenes
     agifa029 #3;   || Almacen
     agifa027 #4;
     agifa065 #9;   || Historico
     agifa142 #42;  || Parametros generales
     dsl00001 #6;   ||Codigos de barras multiples
     dsm00005 #905;
     dsm00010 #910;
     dsm00014 #914;
|FIN;

|VARIABLES;
     nLinV     = 0;
     &r_arti   = "";
     nModo     = 0;
     nSwBarra  = 1;
|FIN;

|INCLUYE i_varart;
|INCLUYE i_cdbarr;

|PROCESO ActuaCompo;
     eaTipo       = "T3";
     eaSerie      = #0#21;
     enCodigo     = #0#18;
     enLinea      = #0#8;
     eaArticulo   = #0#0;
     enAlmacen    = #0#1;
     enUnidades   = #905#6;
     eaComponente = #905#4;
     efFecha      = #0#5;
     enCodHisto   = #0#18;
     |SI #0#7 = "S";
          eaOpeHisto = "SS";
     |SINO;
          eaOpeHisto = "ES";
     |FINSI;
     |LIBERA #905;
     |SUB_EJECUTA_NP "dsz00002;ERE";
|FPRC;

|DEFBUCLE dsm00005A;
     #905#1;
     ;
     "T3", #0#21, #0#18, nLinV, "      ";
     "T3", #0#21, #0#18, nLinV, "zzzzzz";
     be;
     NULL, ActuaCompo;
|FIN;

|PROCESO BorraCompo;
     |BORRA 020#905a;
|FPRC;

|DEFBUCLE BorraCompo;
     #905#1;
     ;
     "T3", #0#21, #0#18, nLinV, "      ";
     "T3", #0#21, #0#18, nLinV, "zzzzzz";
     be;
     NULL, BorraCompo;
|FIN;

|PROCESO Tipo2; |TIPO 2;
     |ABRE #42; |LEE 001#42p; |CIERRA #42;
     nModo = (FEntrada / 100) ! 0;
     enForma = 0 +. 1;
     |SI nSwBarra = 0; |Y nModo = 1; |Y enForma = 1; |Y #1#176 = "S";
          |HAZ GrabaElemento;
     |FINSI;

     #0#5 = #43#1;            ||Fecha

     enCodigo = #0#18;

     |HAZ AcumulaES_SS_iber;

     #0#3 = #2#39;            || Disponible que queda
     #0#19 = #2#5;
     nLinV = #0#8;
     |BUCLE dsm00005A;

     |SI nModo =  3;
          nLinV = #0#8;
          |BUCLE BorraCompo;
     |FINSI;
|FPRC;

|PROCESO decim; |TIPO 0;
     #0#4 = #0#4 ! #42#8;
     |PINTA #0#4;
|FPRC;

|PROCESO leealmacen;
     |ABRE #3;
     |DDEFECTO #3;
     #3#0 = #0#1;
     |LEE 001#3=;
     |CIERRA #3;
|FPRC;

|PROCESO Tip6; |TIPO 6;
     |ABRE #6;
     |SELECT #2#6;
     #6#3=#0#0;
     |LEE 010#6=;
     |SI FSalida=0; |Y #0#0 ! #6#1;
        #0#0=#6#1;
        |ERROR;
        |PINTA #0#0;
     |FINSI;
     |CIERRA #6;
     r_arti = "";
|FPRC;

|PROCESO CogeValores045;  |TIPO 7;
     |ABRE #42;
     |LEE 001#42p;
     |SI FSalida ! 0; |DDEFECTO #42; |FINSI;
     |CIERRA #42;

     |SI Campo = 1;
         |C_M #0Campo, 1, "S";
         |SI #42#99 = "S";
             |C_M #0Campo, 1, "N";
             #0Campo = 1;  |PINTA #0Campo;
         |FINSI;
     |FINSI;

     |SI Campo = 4;
          |HAZ DobleUni;
          |SI eaSw2Stock = "S";
               |C_M #0#4, 1, "N";
               |ACABA;
          |FINSI;
     |FINSI;

     |SI #1#176 = "N";  |ACABA;  |FINSI;

     |SI Campo = 1;
         |SI enAlmacen ! 0;
             #0#1 = enAlmacen;  |PINTA #0#1;
         |FINSI;
     |FINSI;

|FPRC;

|PROCESO PideArt045;  |TIPO 7;
     |C_M #0#0, 1, "S";
     |C_M #0#1, 1, "S";

     nModo = (FEntrada / 100) ! 0;
     |SI nSwBarra = 0; |Y nModo = 1; |ACABA; |FINSI;

     |SI #0#0 ! "                    ";
         |ABRE #1;
         #1#0 = #0#0;
         |LEE 000#1=;
         |SI FSalida ! 0;  |DDEFECTO #1;  |FINSI;
         |CIERRA #1;
         |SI #1#176 = "S";
             |C_M #0#0, 1, "N";
             |C_M #0#1, 1, "N";
             |ACABA;
         |FINSI;
     |FINSI;

     |ABRE #910;
     |LEE 040#910p;
     |SI FSalida ! 0;  |DDEFECTO #910;  |FINSI;
     |CIERRA #910;

     |SI #910#2 [ 2;    |ACABA;  |FINSI;

     eAlfa = #0Campo;
     |SUB_EJECUTA_NP "dsz99990";                 || Por Pantalla

     |SI eAlfa = "-1";  |PTEC 807;  |ACABA;  |FINSI;
     |SI eAlfa = "-2";              |ACABA;  |FINSI;

     eaArticulo = eAlfa;      ||  700000-4215

     #0Campo = eAlfa;
     |PINTA #0Campo;
     |PTEC 802;
|FPRC;

|PROCESO Calc6045;  |TIPO 6;
     |C_M #0#4,  1, "S";
     nModo = (FEntrada / 100) ! 0;
     |SI nSwBarra = 0; |Y nModo = 1;
          eaArti = #0#0;
          |HAZ HallaArti;
          |SI eaArti = ""; |ERROR; |ACABA; |FINSI;
          |SI #1#176 = "S"; |Y eaEleme = "";
               |MENAV "0000Codigo incompleto...";
               |ERROR; |ACABA;
          |FINSI;
          #0#0 = eaArti; |PINTA #0#0;
          |ACABA;
     |FINSI;

     enAlta = 1;
     eaArticulo = #0Campo;
     |HAZ LlenaArticulo;
     |SI FSalida = 0;
          #0Campo = eaArticulo;
          |PINTA #0Campo;
     |SINO;
          |ERROR; |ACABA;
     |FINSI;

     |ABRE #910; |LEE 000#910p; |CIERRA #910;
     |C_M #0#4,  1, "S";
     |SI #910#1 ! "S";
         |C_M #0#1,  1, "S";
         |C_M #0#4,  1, "S";
     |FINSI;

     |ABRE #1;
     #1#0 = #0#0;
     |LEE 000#1=;
     |SI FSalida ! 0;  |DDEFECTO #1;  |FINSI;
     |CIERRA #1;

     |SI #1#176 = "S";
         eaTipo      = "T3";
         eaSerie     = #0#21;
         enCodigo    = #0#18;
         enLinea     = #0#8;
         eaArticulo  = #0#0;
         enAlmacen   = #0#1;
         enTarifa    = 1;
         enUnidades  = #0#4;
         enCodHisto   = #0#18;

         |SUB_EJECUTA_NP "dsz00002";
         #0#4 = enUnidades; |PINTA #0#4;
         #0#1 = enAlmacen; |PINTA #0#1;

         |C_M #0#1,  1, "N";
         |C_M #0#4,  1, "N";
     |FINSI;

     |HAZ Tip6;
|FPRC;

|PROCESO Tipo0045;
     nModo = (FEntrada / 100) ! 0;
     |SI nSwBarra = 0; |Y nModo = 1; |ACABA; |FINSI;

     |QBF r_arti;
     |SI r_arti ! "";
          #0#0 = r_arti;
          |PINTA #0#0;
     |FINSI;
     #0#6 = #1#104;
     #0#20 = #1#6;
|FPRC;

|PROCESO Tipo11045;  |TIPO 11;
     nModo = (FEntrada / 100) ! 0;
     |SI FSalida = 7;  |Y #1#176 = "S";
         |MENAV "      Imposible abortar;  Termine de introducir la linea y efectue una Baja.";
         |ERROR;
     |FINSI;
|FPRC;

|PROCESO cierre; |TIPO 0;
     |HAZ Tipo44_13;
     efFec = #0#5;
     enAlma = #0#1;
     |HAZ MiraHisL;
     |SI enErrHis = 0; |HAZ MiraStockIni; |FINSI;
     |SI enErrHis ! 0;
          |ERROR;
          |ACABA;
     |FINSI;
|FPRC;

|PROCESO Tipo7Lin; |TIPO 7;
     |HAZ PintaMedidaC;
|FPRC;

|PROCESO Tipo11; |TIPO 11;
     |SI FSalida = 13;
          |HAZ VerDobleUni;
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO Tipo5; |TIPO 5;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo [ 2; |Y nSwBarra = 1;
          |CONFI "0000NIntroduccion por Codigo de Barras";
          nSwBarra = FSalida;
          |ATRI S;
          |SI FSalida = 0;
               |PINTA 0533, "Introduccion por codigo de barras  ";
          |SINO;
               |PINTA 0533, "Introduccion por codigo de articulo";
          |FINSI;
          |ATRI 0;
     |FINSI;
     |HAZ SinBarra;
|FPRC;

|PROCESO GrabaElemento;
     |ABRE #914;
     #914#0 = eaEleme;
     |LEE 000#914=;
     |CIERRA #914;

     |ABRE #905;
     |DDEFECTO #905;
     #905#0 = "T3";
     #905#1 = #0#21;
     #905#2 = #0#18;
     #905#3 = #0#8;
     #905#4 = eaEleme;
     #905#5 = #914#1;
     #905#6 = #0#4;
     |GRABA 020#905n;
|FPRC;

|PROCESO Tipo44_13; |TIPO 13;
     |DDEFECTO #1;
     |DDEFECTO #2;
     |ABRE #1;
     |ABRE #2;
     #1#0 = #0#0
     |LEE 000#1=;
     #2#0 = #0#0;
     #2#1 = #0#1;
     |LEE 000#2=;
     |CIERRA #2;
     |CIERRA #1;

     #0#3 = #2#39;  |PINTA #0#3;
     #0#19 = #2#5;  |PINTA #0#19;
     #0#6 = #1#104; |PINTA #0#6;
     #0#20 = #1#6;  |PINTA #0#20;
|FPRC;
