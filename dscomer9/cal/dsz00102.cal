|FICHEROS;
     dsz00102 #0;
     agifa041 #41;
     dsw00099 #99;  || Tmp chequeo contador fac vta=fac dir
     dsm00281 #281,MANTE;

     agifa079 #79;
     agifa084 #84;
     agifa134 #134;
     agifa142 #142;
     referen@;
     crontcom@;
|FIN;

|VARIABLES;
     &Tempo = "";
     nErrores = 0;
     nActual = 0;
     nErrorHay = 0;
     &eaEmpresa = "";
     aMin = "";
     aHora = "";
     aMes = "";
     aDia_mes = "";
     aDia_Sem = "";
     aComando = "";
     nCanal = 0;
     aAbre = "";
     aDBass = "";
     aNumEmpre = "";
     aShell = "";
     nHandle = 0;
     aDirBase = "";
     aLong = "";
     nLong = 0;
     nPos = 0;
     aAplicacion = "";
     aDefEmpre = "";
     aTAB = "";
     aParam = "";
     aAlfa2 = "";
     nLinea = 0;
     nUltima = 0;
     aDirBass = "";
     aFicCron = "";
     aDatos = "";

     &enHay = 0;
     aDef = "";
     aAlfa = "";
     aDjunto = "";
     aDirDestino = "";
     aServidor = "";
     aFrom = "";
     aTo = "";
     aSubject = "";
     aMensaje = "";
     aIP = "";
     aEmp = "";
     nError1 = 0;
     nError2 = 0;
     nHay = 0;
     nTipo = 0;
     fFecha = @;
     aSerAnt = "";
     nNumAnt = 0;
     fFecAnt = @;
     aSerie = "";
     nNume = 0;
     nNume1 = 0;
     nNume2 = 0;
     i = 0;
     nPrime = 0;
|FIN;

|PROCESO FechaSig;
     |SI #referen@#1 ] fFecAnt;
          #0#15 = #referen@#1;
          |ERROR10;
     |FINSI;
|FPRC

|DEFBUCLE FechaSig;
     #referen@#1002;
     ;
     aSerie, nNume;
     aSerie, 999999;
     ;
     NULL, FechaSig;
|FIN;

|PROCESO Comprueba;
     nError1 = 0;
     nError2 = 0;
     |SI aSerAnt ! aSerie; |O nPrime = 0;
          aSerAnt = aSerie;
          nNumAnt = nNume;
          fFecAnt = fFecha;
          nPrime = 1;
     |SINO;
          nNume1 = nNumAnt + 1;
          |SI nNume1 ! nNume;
               nError1 = 1;
          |FINSI;
          |SI fFecha < fFecAnt;
               nError2 = 1;
          |FINSI;
     |FINSI;

     |SI nError1 ! 0; |O nError2 ! 0;
          |SI nHay = 0;  || La primera vez
               nHay = 1;
               |SI nTipo = 1; enHay = 1; |FINSI;
               |SI nTipo = 2; enHay = 2; |FINSI;
               |SI nTipo = 3; enHay = 3; |FINSI;
               |SI nTipo = 4; enHay = 1; |FINSI;
               |PRINT;
          |FINSI;
          |SI nError1 ! 0; || error hueco
               enHay = 4;
               nNume2 = nNume - 1;
               |PARA i = nNume1; |SI i [ nNume2; |HACIENDO i = i + 1;
                    #0#12 = aSerie;
                    #0#13 = i;
                    |PRINT;
               |SIGUE;
          |FINSI;
          |SI nError2 ! 0; || error fecha
               |SI nTipo = 1; aDef = "agifa134"; |FINSI;
               |SI nTipo = 2; aDef = "agifa084"; |FINSI;
               |SI nTipo = 3; aDef = "agifa079"; |FINSI;
               |SI nTipo = 4; aDef = "dsw00099"; |FINSI;
               enHay = 5;
               #0#12 = aSerie;
               #0#13 = nNume;
               #0#14 = fFecAnt;
               #0#15 = "31.12.9999";
               |DDEF aAlfa;
               aAlfa = aAlfa + aDef;
               |CARGA_DEF aAlfa, referen@;
               |SI FSalida = 0;
                    |SI nTipo = 4; |NOME_DAT #referen@#Tempo#; |FINSI;
                    |BUCLE FechaSig;
                    |DESCARGA_DEF #referen@;
               |FINSI;
               |PRINT;
          |FINSI;
     |FINSI;
     nNumAnt = nNume;
     |SI fFecha ] fFecAnt;
          fFecAnt = fFecha;
     |FINSI;
|FPRC;

|PROCESO agifa079;
     nNume = #79#0;
     fFecha = #79#1;
     aSerie = #79#66;
     nTipo = 3;
     |HAZ Comprueba;
|FPRC;

|PROCESO agifa084;
     nNume = #84#0;
     fFecha = #84#1;
     aSerie = #84#48;
     |SI #142#26 = "S";
          #99#0 = aSerie;
          #99#1 = fFecha;
          #99#2 = nNume;
          |GRABA 000#99n;
     |SINO;
          nTipo = 2;
          |HAZ Comprueba;
     |FINSI;
|FPRC;

|PROCESO agifa134;
     nNume = #134#0;
     fFecha = #134#1;
     aSerie = #134#49;
     |SI #142#26 = "S";
          #99#0 = aSerie;
          #99#1 = fFecha;
          #99#2 = nNume;
          |GRABA 000#99n;
     |SINO;
          nTipo = 1;
          |HAZ Comprueba;
     |FINSI;
|FPRC;

|PROCESO Tmp99;
     nNume = #99#2;
     fFecha = #99#1;
     aSerie = #99#0;
     nTipo = 4;
     |HAZ Comprueba;
|FPRC;

|DEFBUCLE Tmp99;
     #99;
     ;
     INICIO;
     FINAL;
     ;
     NULL, Tmp99;
|FIN;

|DEFBUCLE agifa079;
     #79#1;
     1;
     #0#6, 000001, #0#1;
     #0#7, 999999, #0#2;
     ;
     NULL, agifa079;
|FIN;

|DEFBUCLE agifa084;
     #84#1;
     1;
     #0#6, 000001, #0#1;
     #0#7, 999999, #0#2;
     ;
     NULL, agifa084;
|FIN;

|DEFBUCLE agifa134;
     #134#1;
     1;
     #0#6, 000001, #0#1;
     #0#7, 999999, #0#2;
     ;
     NULL, agifa134;
|FIN;

|PROCESO Chequeo;
     #0#16 = aEmp;

     |DBASE aAlfa;
     aAlfa = aAlfa + "fich/" + aEmp + "/";
     |PATH_DAT #134 aAlfa;
     |PATH_DAT #84 aAlfa;
     |PATH_DAT #79 aAlfa;

     |SI #142#26 = "S";
          |SI #0#8 = "S"; |O #0#9 = "S";
               |HAZ CreaTempo; |NOME_DAT #99 Tempo; |DELINDEX #99;
               |ABRE #99;
               |BUCLE agifa134;
               |BUCLE agifa084;
               |CIERRA #99;
               |SI nHay ! 0; enHay = 6; |PRINT; |FINSI;
               nPrime = 0;
               nHay = 0;
               aSerAnt = "";
               |BUCLE Tmp99;
               |SI nHay ! 0; nErrorHay = 1; |FINSI;

               |DELINDEX #99; |HAZ BoraTempo;
          |FINSI;
     |SINO;
          |SI #0#8 = "S";
               |SI nHay ! 0; enHay = 6; |PRINT; |FINSI;
               nPrime = 0;
               nHay = 0;
               aSerAnt = "";
               |BUCLE agifa134;
               |SI nHay ! 0; nErrorHay = 1; |FINSI;
          |FINSI;
          |SI #0#9 = "S";
               |SI nHay ! 0; enHay = 6; |PRINT; |FINSI;
               nPrime = 0;
               nHay = 0;
               aSerAnt = "";
               |BUCLE agifa084;
               |SI nHay ! 0; nErrorHay = 1; |FINSI;
          |FINSI;
     |FINSI;
     |SI #0#10 = "S";
          |SI nHay ! 0; enHay = 6; |PRINT; |FINSI;
          nPrime = 0;
          nHay = 0;
          aSerAnt = "";
          |BUCLE agifa079;
          |SI nHay ! 0; nErrorHay = 1; |FINSI;
     |FINSI;
|FPRC;

|PROCESO Empresas;
     nErrorHay = 0;
     aEmp = ("00000" + #281#0) % -105;
     |HAZ Chequeo;
     #281#2 = nErrorHay;
     |GRABA 020#281a;
     |LIBERA #281;
|FPRC;

|DEFBUCLE Empresas;
     #281#1;
     ;
     INICIO;
     FINAL;
     be;
     NULL, Empresas;
|FIN;

|PROCESO EmpresasOK;
     |SI #281#2 = 0;
          |PRINT;
     |SINO;
          nErrores = 1;
     |FINSI;
|FPRC;

|DEFBUCLE EmpresasOK;
     #281#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, EmpresasOK;
|FIN;

|PROCESO EnviaMail;
     |SI aDirDestino = ""; |ACABA; |FINSI;

     aIP = "xdscorreo.dv.diagram.net";
     aServidor = "asmtp.diagram.es";
     aFrom = aDirDestino;
     aTo = aDirDestino;
     |SI nErrores = 0;
          aSubject = "Huecos facturacion (correcto)";
          aMensaje = "Correcto.";
     |SINO;
          aSubject = "Huecos facturacion (con errores)";
          aMensaje = "Adjunta errores encontrados.";
     |FINSI;

     |DSCORREO_ENVIA aIP, aServidor, aFrom, aTo, aSubject, aMensaje, aDjunto;
|FPRC;

|PROCESO EnviaMails;
     aDirDestino = #0#5; |QBF aDirDestino; |HAZ EnviaMail;
     aDirDestino = #0#20; |QBF aDirDestino; |HAZ EnviaMail;
     aDirDestino = #0#21; |QBF aDirDestino; |HAZ EnviaMail;
     aDirDestino = #0#22; |QBF aDirDestino; |HAZ EnviaMail;
     aDirDestino = #0#23; |QBF aDirDestino; |HAZ EnviaMail;
|FPRC;

|PROCESO Tipo3; |TIPO 3;
     fFecAnt = "01.01.0000";
     |SI #0#4 = "M";
          |DFICE aDjunto;
          aDjunto = aDjunto + "huecos_fac.txt";
          Impresora = aDjunto;
          |IMPRE -77;
          |SI FSalida ! 0;
               |SI PARAMETRO ! "";
                    |MENAV "0000Error al crear el fichero";
               |FINSI;
               |ACABA;
          |FINSI;
     |SINO;
          |IMPRE 0;
          |SI FSalida ! 0; |ACABA; |FINSI;
     |FINSI;
     |INFOR "dsz00102";
     |SI FSalida = 0;
          |DFICE aEmp;
          aEmp = aEmp %- 205;
          eaEmpresa = aEmp;
          |HAZ Chequeo;
          nActual = enHay;
          |SI #0#3 = "S";
               |BUCLE Empresas;
          |FINSI;

          || imprime las que no tienen errores
          nErrores = 0;
          enHay = 0;
          |SI nActual = 0;
               |DFICE aEmp;
               aEmp = aEmp %- 205;
               #281#0 = aEmp;
               |PRINT;
          |SINO;
               nErrores = 1;
          |FINSI;
          |BUCLE EmpresasOK;

          |PIEINF;
          |FININF;
     |SINO;
          |SI PARAMETRO ! "";
               |MENAV "0000Error en informe 'dsz00102'";
          |FINSI;
     |FINSI;
     |FINIMP;
     |SI #0#4 = "M";
          |HAZ EnviaMails;
          |FBORRA aDjunto;
     |FINSI;
|FPRC;
  ||--------------------------------------------------||
|PROCESO GrabSRV;
     aMin     = #crontcom@#2;    |QBF aMin;
     aHora    = #crontcom@#3;    |QBF aHora;
     aDia_mes = #crontcom@#4;    |QBF aDia_mes;
     aMes     = #crontcom@#5;    |QBF aMes;
     aDia_Sem = #crontcom@#6;    |QBF aDia_Sem;
     aComando = #crontcom@#7;    |QBF aComando;

     |SI #crontcom@#8 = "S";
          aAlfa = aMin + " " + aHora + " " + aDia_mes + " " + aMes + " " + aDia_Sem + " " + aComando + &13 + &10;
     |SINO;
          aAlfa = "#" + aMin + " " + aHora + " " + aDia_mes + " " + aMes + " " + aDia_Sem + " " + aComando + &13 + &10;
     |FINSI;
     |XGRABA nCanal, aAlfa;
|FPRC;

|DEFBUCLE GrabSRV;
     #crontcom@#1002;
     ;
     00, 000;
     00, 999;
     ;
     NULL, GrabSRV;
|FIN;

|PROCESO Graba_SRV;
     |DBASS aAbre;
     aAbre = aAbre + "bin/tareas.srv";
     |XABRE "WB", aAbre, nCanal;
     |DBASS aDBass; |QBF aDBass;
     aAlfa = aDBass + "cron";
     |MKDIR aAlfa;
     |BUCLE GrabSRV;
     |XCIERRA nCanal;
|FPRC;

|PROCESO Shell;
     |DBASS aDBass; |QBF aDBass;
     |DFICE aNumEmpre;
     aNumEmpre = aNumEmpre % -205;
     aShell = aDBass + "bin/cron" + aNumEmpre  + ".sh";
     |XABRE "WB", aShell, nHandle;
     aDirBase = aDBass;
     aLong = aDirBase % 0;
     nLong = aLong;
     aAlfa = aDirBase;
     nPos = 100 + nLong - 1;
     aDirBase = aAlfa % nPos;
     aAplicacion = "dscomer9";
     aNumEmpre = aNumEmpre;
     aDefEmpre = "agif0041";
     aTAB = "dsz00102";
     aParam = "";

     |SI #0#18 = "N";
          aAlfa2 = &10;
     |SINO;
          aAlfa = "echo Iniciando la ejecucion:";
          aAlfa2 = " >>" + aDBass + "bin/cron" + nLinea + ".log 2>>" + aDBass + "bin/cron" + nLinea + ".err" + &10;
          aAlfa = aAlfa + aAlfa2;
          |XGRABA nHandle, aAlfa;

          aAlfa = "date";
          aAlfa = aAlfa + aAlfa2;
          |XGRABA nHandle, aAlfa;
     |FINSI;

     aAlfa = aDBass + "bin/serverds @" + aDirBase + " " + &34 + "/!:" + aAplicacion +"/"+ aTAB;
     |SI aDefEmpre ! "";
          aAlfa = aAlfa + " " + aDefEmpre;
     |FINSI;
     |SI aNumEmpre ! "";
          aAlfa = aAlfa + " " + aNumEmpre;
     |FINSI;
     |SI aParam ! "";
          aAlfa = aAlfa + ";" + aParam;
     |FINSI;
     aAlfa = aAlfa + &34 + aAlfa2;
     |XGRABA nHandle, aAlfa;

     |SI #0#18 = "S";
          aAlfa = "echo Finalizada la ejecucion:";
          aAlfa = aAlfa + aAlfa2;
          |XGRABA nHandle, aAlfa;

          aAlfa = "date";
          aAlfa = aAlfa + aAlfa2;
          |XGRABA nHandle, aAlfa;
     |FINSI;

     |XCIERRA nHandle;
     aAlfa = "chmod 777 " + aShell;
     |SYSTEM aAlfa;
|FPRC;

|PROCESO Bat;
     |DBASS aDBass; |QBF aDBass;
     |DFICE aNumEmpre;
     aNumEmpre = aNumEmpre % -205;
     aShell = aDBass + "bin/cron" + aNumEmpre  + ".bat";
     |XABRE "WB", aShell, nHandle;
     aDirBase = aDBass;
     aLong = aDirBase % 0;
     nLong = aLong;
     aAlfa = aDirBase;
     nPos = 100 + nLong - 1;
     aDirBase = aAlfa % nPos;
     aAplicacion = "dscomer9";
     aDefEmpre = "agif0041";
     aNumEmpre = aNumEmpre;
     aTAB = "dsz00102";
     aParam = "";

     aAlfa = "@echo off" + &13 + &10;
     |XGRABA nHandle, aAlfa;

     aAlfa = "echo Iniciando la ejecucion:";
     aAlfa2 = " >>" + aDBass + "bin/cron" + nLinea + ".log 2>>" + aDBass + "bin/cron" + nLinea + ".err" + &13 + &10;
     aAlfa = aAlfa + aAlfa2;
     |XGRABA nHandle, aAlfa;

     aAlfa = "time /T";
     aAlfa = aAlfa + aAlfa2;
     |XGRABA nHandle, aAlfa;

     aAlfa = aDBass + "bin/diagramx.exe /DS:" + &34 + "@" + aDirBase + " $$$batch,batch# //(" + aAplicacion +")"+ aTAB;
     |SI aDefEmpre ! "";
          aAlfa = aAlfa + "&" + aDefEmpre;
     |FINSI;
     |SI aNumEmpre ! "";
          aAlfa = aAlfa + "&" + aNumEmpre;
     |FINSI;
     |SI aParam ! "";
          aAlfa = aAlfa + ";" + aParam;
     |FINSI;
     aAlfa = aAlfa + &34 + aAlfa2;
     |XGRABA nHandle, aAlfa;

     aAlfa = "echo Finalizada la ejecucion:";
     aAlfa = aAlfa + aAlfa2;
     |XGRABA nHandle, aAlfa;

     aAlfa = "time /T";
     aAlfa = aAlfa + aAlfa2;
     |XGRABA nHandle, aAlfa;

     |XCIERRA nHandle;
|FPRC;

|PROCESO crontcom;
     nUltima = #crontcom@#1;
     aAlfa = #crontcom@#22; |QBF aAlfa;
     aAlfa2 = "Huecos facturacion empresa:" + aNumEmpre;
     |SI aAlfa = aAlfa2;
          nLinea = #crontcom@#1;
     |FINSI;
|FPRC;

|DEFBUCLE crontcom;
     #crontcom@#1002;
     ;
     00, 000;
     00, 999;
     ;
     NULL, crontcom;
|FIN;

|PROCESO Programacion;
     |DBASS aDirBass;
     |QBF aDirBass;
     aFicCron = aDirBass + "agitool/def/crontcom";
     |CARGA_DEF aFicCron, crontcom@;
     |SI FSalida ! 0;
          aMensaje = "0000Problemas con el CargaDef " + aFicCron;
          |MENAV aMensaje;
          |ACABA;
     |FINSI;

     aDatos = aDirBass + "agitool/fich/";
     |PATH_DAT #crontcom@#aDatos#;
     |SI #0#17 = 0;
          |DFICE aNumEmpre;
          aNumEmpre = aNumEmpre % -205;
          nLinea = 0;
          |BUCLE crontcom;
          |SI nLinea > 0;
               |ABRE #crontcom@;
               #crontcom@#0 = 0;
               #crontcom@#1 = nLinea;
               |BORRA 020#crontcom@.c;
               |CIERRA #crontcom@;
          |FINSI;
          |HAZ Graba_SRV;
     |SINO;
          nLinea = 0;
          |BUCLE crontcom;
          |ABRE #crontcom@;
          |SI nLinea = 0;
               #crontcom@#0 = 0;
               #crontcom@#1 = nUltima + 1;
               |GRABA 020#crontcom@.b;
          |SINO;
               #crontcom@#0 = 0;
               #crontcom@#1 = nLinea;
               |LEE 121#crontcom@.=;
          |FINSI;
          |SI FSalida = 0;
               nLinea = #crontcom@#1;
               |QUE_SISTEMA aAlfa;
               |SI aAlfa = "ESDOS";
                    |HAZ Bat;
               |SINO;
                    |HAZ Shell;
               |FINSI;
/*
               ||para minutos
               aAlfa = "0-59/" + #0#17;
               #crontcom@#2 = aAlfa;
               #crontcom@#3 = "*";
               #crontcom@#4 = "*";
               #crontcom@#5 = "*";
               #crontcom@#6 = "*";
*/
               ||para dias
               aAlfa = "0-6/" + #0#17;
               #crontcom@#2 = "*";
               #crontcom@#3 = "*";
               #crontcom@#4 = aAlfa;
               #crontcom@#5 = "*";
               #crontcom@#6 = "*";
               #crontcom@#7 = aShell;
               #crontcom@#8 = "S";
               #crontcom@#9 = #crontcom@#2;
               #crontcom@#10 = #crontcom@#3;
               #crontcom@#11 = #crontcom@#4;
               #crontcom@#12 = #crontcom@#5;
               #crontcom@#13 = #crontcom@#6;
               #crontcom@#14 = #crontcom@#7;
               #crontcom@#15 = "N";
               aAlfa2 = "Huecos facturacion empresa:" + aNumEmpre;
               #crontcom@#22 = aAlfa2;
               #crontcom@#23 = "S";
               #crontcom@#24 = #0#18;
               #crontcom@#25 = #0#18;
               |GRABA 020#crontcom@.a;
               |HAZ Graba_SRV;
          |FINSI;
     |FINSI;
     |CIERRA #crontcom@;

     |DESCARGA_DEF #crontcom@;
|FPRC;
  ||--------------------------------------------------||
|PROCESO Min;
     #281#0 = 1;
|FPRC;

|PROCESO Max;
     #281#0 = 99999;
|FPRC;

|PROCESO OtrasEmp; |TIPO 0;
     |SI #0#3 = "S";
          |PUSHV 0501 2380;
          |ENTLINEAL #1#281, 1, 2, 22, 1, Min, Max;
          |POPV;
     |FINSI;
|FPRC;


|PROCESO CheqEmp; |TIPO 0;
     |DFICE aAlfa;
     aAlfa = aAlfa % -205;
     |SI #281#0 = aAlfa;
          |MENAV "0000Esta es la empresa actual...";
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO Mail; |TIPO 0;
     |SI #0#4 = "M";
          |C_M #0#5, 1, "S";
          |C_M #0#20, 1, "S";
          |C_M #0#21, 1, "S";
          |C_M #0#22, 1, "S";
          |C_M #0#23, 1, "S";
          |C_M #0#17, 1, "S";
     |SINO;
          |C_M #0#5, 1, "N";
          |C_M #0#20, 1, "N";
          |C_M #0#21, 1, "N";
          |C_M #0#22, 1, "N";
          |C_M #0#23, 1, "N";
          |C_M #0#17, 1, "N";
          #0#17 = 0; |PINTA #0#17;
     |FINSI;
|FPRC;

|PROGRAMA;
     |DBASE aAlfa; aAlfa = aAlfa + "fich/";
     |PATH_DAT #41 aAlfa;
     |SALVA_FICHA #41;

     |ABRE #142; |LEE 000#142p; |CIERRA #142;

     |ABRE #0;
     |LEE 101#0p;
     |SI FSalida ! 0; |GRABA 020#0b; |FINSI;
     |SI PARAMETRO ! ""; || llamada desde el menu
          |CLS;
          |PINPA #0#0;
          |PINDA #0#0;
          |ENDATOS #1#0;
          |SI FSalida = 0;
               |HAZ Programacion;
               |GRABA 020#0a;
          |FINSI;
     |SINO;
          |HAZ Tipo3;
     |FINSI;
     |CIERRA #0;

     |REPON_FICHA #41;
|FPRO;
