|FICHEROS;
     dscsvw01;

     agifa019;
     agifa024;
     agifa069;
     agifa084;
     agifa724;
|FIN;

|VARIABLES;
     nLong               = 0;
     nCarac              = 0;
     nPos                = 0;
     nCmp                = 0;
     nReg                = 0;
     nTotCmp             = 0;
     nTCampos            = 0;
     nHandle             = 0;
     nCampo              = 0;
     nTValor             = 0;
     nPorce              = 0;
     nOk                 = 0;
     nLinea              = 0;

     aFicBrowse          = "";
     aAlfa               = "";
     aLong               = "";
     aFic                = "";
     aLee                = "";
     aVar                = "";
     aC10                = "";
     aC13                = "";
     aCarac              = "";
     aCmp1               = "";
     aCmp2               = "";
     aCmp3               = "";
     aCmp4               = "";
     aCmp5               = "";
|FIN;

|INCLUYE i_varart;

|PROCESO dscsvw01Campos;
     nTCampos  = nTCampos + 1;
|FPRC;

|DEFBUCLE dscsvw01Campos;
     #dscsvw01#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, dscsvw01Campos;
|FIN;

|PROCESO dscsvw01Carga;
     nCampo = #dscsvw01#0;

     |SI nCampo = 1;   aCmp1  = #dscsvw01#1;  |FINSI;
     |SI nCampo = 2;   aCmp2  = #dscsvw01#1;  |FINSI;
     |SI nCampo = 3;   aCmp3  = #dscsvw01#1;  |FINSI;
     |SI nCampo = 4;   aCmp4  = #dscsvw01#1;  |FINSI;
     |SI nCampo = 5;   aCmp5  = #dscsvw01#1;  |FINSI;
|FPRC;

|DEFBUCLE dscsvw01Carga;
     #dscsvw01#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, dscsvw01Carga;
|FIN;

|PROCESO GrabaLinea;
     #agifa069#20 = #agifa084#48;
     #agifa069#0  = #agifa084#0;
     #agifa069#1  = 999;
     |LEE 000#agifa069.];
     |SI FSalida = 0;
         |LEE 000#agifa069.a;
     |SINO;
         |LEE 000#agifa069.u;
     |FINSI;
     nLinea = 1;
     |SI FSalida = 0; |Y #agifa069#20 = #agifa084#48;  |Y #agifa069#0 = #agifa084#0;
         nLinea = #agifa069#1 + 1;
     |FINSI;

     |ABRE #agifa019;
     #agifa019#0 = aCmp1;
     |LEE 101#agifa019.=;
     |SI FSalida ! 0;
         |DDEFECTO #agifa019;
         #agifa019#0  = aCmp1;
         #agifa019#1  = aCmp2;
         #agifa019#30 = aCmp5;
         |GRABA 020#agifa019.n;
     |FINSI;
     |LIBERA #agifa019;
     |CIERRA #agifa019;

     |ABRE #agifa724;
     #agifa724#0 = aCmp1;
     |LEE 101#agifa724.=;
     |SI FSalida ! 0;
         |DDEFECTO #agifa724;
         #agifa724#0  = aCmp1;
         #agifa724#1  = #agifa019#1;
         |GRABA 020#agifa724.n;
         |LIBERA #agifa724;
     |FINSI;
     |CIERRA #agifa724;

     |DDEFECTO #agifa069;
     #agifa069#20 = #agifa084#48;
     #agifa069#0  = #agifa084#0;
     #agifa069#1  = nLinea;
     #agifa069#2  = aCmp1;
     #agifa069#3  = #agifa084#48;         || Almacen
     #agifa069#4  = aCmp2;                || Descripcion articulo
     #agifa069#5  = aCmp3;                || Unidades
     #agifa069#6  = aCmp4;                || Precio
     #agifa069#11 = aCmp5;                || Tipo iva
     #agifa069#13 = #agifa019#2;          || Familia
     #agifa069#15 = #agifa084#3;          || Cliente
     #agifa069#16 = #agifa084#34;         || Tipo comision
     #agifa069#17 = #agifa024#4;          || Tipo cliente

     enForma = 1;

     |HAZ AcumulaFC;
     |HAZ TotalLin69;
     |HAZ CalCabAgifa084_2;

     |GRABA 020#agifa069.n;
     |LIBERA #agifa069;
|FPRC;

|PROCESO GrabaDatos;
     |CIERRA #dscsvw01;
     nTCampos = 0;
     |BUCLE dscsvw01Campos;

     nOk      = 0;
     nTValor  = nTotCmp;
     |SI nTCampos < nTValor;
         |DELINDEX #dscsvw01;
         |ACABA;
     |FINSI;

     nPorce = ((nTValor * 100) / nTCampos) ! 0;
     |SI nPorce < 70;
         |DELINDEX #dscsvw01;
         |ACABA;
     |FINSI;

     nReg = nReg + 1;

     aCmp1 = "";
     aCmp2 = "";
     aCmp3 = "";
     aCmp4 = "";
     aCmp5 = "";

     |BUCLE dscsvw01Carga;

     |HAZ GrabaLinea;

     |DELINDEX #dscsvw01;
     |PULSATECLA;
|FPRC;

|PROCESO Grabaw01;
     #dscsvw01#0 = nCmp;
     #dscsvw01#1 = aVar;

     |GRABA 020#dscsvw01.n;
     |LIBERA #dscsvw01;
|FPRC;

|PROCESO SeparaDatos;
     aLong  = aLee % 0;
     nLong  = aLong;
     aC10   = &10;
     aC13   = &13;
     |PARA nCarac = 1;  |SI nCarac [ nLong;  |HACIENDO nCarac = nCarac + 1;
           nPos   = (nCarac * 100) + 1;
           aCarac = aLee % nPos;

           |SI aCarac = aC13;
               aCarac = ";";
           |FINSI;

           |SI aCarac = ";";
               nCmp   = nCmp + 1;
               |HAZ Grabaw01;

               aVar   = "";
               aCarac = "";
           |FINSI;

           |SI aCarac = aC10;
               aCarac = "";
           |FINSI;

           |SI nCmp = nTotCmp;  |Y nCmp ! 0;
               |HAZ GrabaDatos;

               |ABRE #dscsvw01;

               nCmp = 0;
           |FINSI;

           aVar = aVar + aCarac;
      |SIGUE;
|FPRC;

|PROCESO RecogeCSV;
     |CONFI "0000SQuiere importar los datos de la facturas de un documentos CSV";
     |SI FSalida ! 0;
         |ACABA;
     |FINSI;

     |MENAV "0000Ahora seleccione el documento CSV a importar";

     aFicBrowse = "F*.csv";
     |BROWSE aFicBrowse;

     aAlfa      = aFicBrowse;   |QBF aAlfa;
     aFicBrowse = "";
     aLong      = aAlfa % 0;
     nLong      = aLong;
     |PARA nCarac = 2;  |SI nCarac [ nLong;  |HACIENDO nCarac = nCarac + 1;
           nPos  = (nCarac * 100) + 1;
           aFicBrowse = aFicBrowse + (aAlfa % nPos);
     |SIGUE;

     |SI aFicBrowse = "";
         |ACABA;
     |FINSI;

     aAlfa = (aFicBrowse % -103) < "";
     |SI aAlfa ! "csv";
         |MENAV "0000El documento debe ser CSV";
         |ACABA;
     |FINSI;

     aFic = "dscsvw01" + Usuario;  |NOME_DAT #dscsvw01#aFic#;
     |ABRE #dscsvw01;  |CIERRA #dscsvw01;  |DELINDEX #dscsvw01;

     |ABRE #dscsvw01;

     nTotCmp = 5;
     nCmp    = 0;
     nReg    = 0;
     |XABRE "BC", aFicBrowse, nHandle;
     |PARA;  |SI;  |HACIENDO;
          |XLEE nHandle, 255, aLee;
          |SI FSalida [ 0;  |SAL;  |FINSI;

          |HAZ SeparaDatos;
     |SIGUE;
     |XCIERRA nHandle;

     |LEE 000#dscsvw01.p;
     |SI FSalida = 0;
         nCmp   = nCmp + 1;

         |HAZ Grabaw01;
         |HAZ GrabaDatos;
     |FINSI;
|FPRC;
