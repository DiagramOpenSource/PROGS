|FICHEROS;
     mecaz004 #0;
     mecam001 #1;
     mecaw004 #4,MANTE;
     mecaw005 #5,MANTE;
     mecaw006 #6,MANTE;
     mecaw007 #7,MANTE; || Ver por Lin pedido
     mecaw008 #8; || ''
     mecaw009 #9,MANTE;

     mecam007 #70;
     mecam008 #80;
     mecam006 #60;
     agifa104 #104;
     agifa073 #73;
     mecaw004@;
     mecaw005@;
     mecaw005a@;
     mecam007@;
|FIN;

|VARIABLES;
     nSw = 0;
     &eaSerie = "";
     &enDocu = 0;
     aEntrada = "";
     nSwBorra4 = 0;
     nHayLin = 0;
     nErrorMueve = 0;
     aPath = "";
     aTemp4 = "";
     aTemp5 = "";
     aTmp4 = "";
     aTmp5 = "";
     aTmp7 = "";
     aTmp8 = "";
     i = 0;
     nErrRenum = 0;
     nPaquete = 0;
     nConta = 0;
     nRecal = 0;
     aOrden = "";
     aArti = "";
     nErrorJunta = 0;
     &Tempo = "";
     nForma = 0;
     nHay = 0;
     aDSerOrd = "";
     aHSerOrd = "";
     nDNumOrd = 0;
     nHNumOrd = 0;
     aConsulta = "N";
     nModo = 0;

     {-1}Menu = "";
     Menu1 = "2400";
     Menu2 = "1";
     Menu3 = "Opciones:";
     Menu4 = "RAC";
     Menu5 = " Repasar ";
     Menu6 = " Aceptar ";
     Menu7 = " Cancelar ";
     nError = 0;
     nUni = 0;
     aAlfa = "";
     nPaquetes = 0;
     nUnidades = 0;
|FIN;

|PROCESO Cuentala;
     #4#2 = #4#2 + #mecaw005@#3;
|FPRC;

|DEFBUCLE Cuentala;
     #mecaw005@#1002;
     ;
     nPaquete, 001;
     nPaquete, 999;
     ;
     NULL, Cuentala;
|FIN;

|PROCESO CuentaUni;
     |DDEF aPath;
     aAlfa = aPath + "mecaw005"
     |CARGA_DEF aAlfa, mecaw005@;
     |SI FSalida = 0;
          |NOME_DAT #mecaw005@#aTmp5#;
          nSw = 0;
          |SI #4#0 ! nPaquete;
               nSw = 1;
               |SALVA_FICHA #4;
          |FINSI;

          #4#0 = nPaquete;
          |LEE 101#4=;
          |SI FSalida = 0;
               #4#2 = 0;
               |BUCLE Cuentala;
               |GRABA 020#4a;
               |LIBERA #4;
          |FINSI;

          |SI nSw = 1;
               |REPON_FICHA #4;
          |FINSI;
          |DESCARGA_DEF #mecaw005@;
     |FINSI;
|FPRC;
----------------------------------
|PROCESO CopPaqLin;
     |DDEFECTO #73;
     #73#28 = #0#0;
     #73#0 = #0#1;
     #73#1 = #5#1;
     |LEE 000#73=;

     |DDEFECTO #7;
     #7#0 = #5#1;
     |LEE 000#7=;
     |SI FSalida ! 0;
          #7#1 = #4#1;
          #7#2 = #73#5;
          #7#3 = #73#50;
          #7#4 = #4#3;
          #7#5 = #4#4;
          |GRABA 020#7n;
     |FINSI;
     |DDEFECTO #8;
     #8#0 = #7#0;
     #8#1 = #4#0;
     #8#2 = #5#3;
     |GRABA 020#8n;
|FPRC;

|DEFBUCLE CopPaqLin;
     #5#1;
     ;
     #4#0, 001;
     #4#0, 999;
     ;
     NULL, CopPaqLin;
|FIN;

|PROCESO CopPaquete;
     |BUCLE CopPaqLin;
|FPRC;

|DEFBUCLE CopPaquete;
     #4#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, CopPaquete;
|FIN;

|PROCESO Min8;
     #8#0 = #7#0;
     #8#1 = 1;
|FPRC;

|PROCESO Max8;
     #8#0 = #7#0;
     #8#1 = 9999;
|FPRC;

|PROCESO Min7;
     #7#0 = 001;
|FPRC;

|PROCESO Max7;
     #7#0 = 999;
|FPRC;

|PROCESO Tipo7_7; |TIPO 7;
     |ENTLINEAL #1#8, 2, 7, 22, 0, Min8, Max8;
|FPRC;

|PROCESO Tipo7_0; |TIPO 0;
     |SI FSalida = 0; |Y #7Campo = Contenido;
          |ENTLINEAL #1#8, 2, 4, 22, 0, Min8, Max8;
     |FINSI;
|FPRC;

|PROCESO PorLinPedido;
     |PUSHV 0501 2380;
     |HAZ CreaTempo; |NOME_DAT #7#Tempo#; |DELINDEX #7; aTmp7 = Tempo;
     |HAZ CreaTempo; |NOME_DAT #8#Tempo#; |DELINDEX #8; aTmp8 = Tempo;
     |ABRE #73;
     |ABRE #7;
     |ABRE #8;
     |BUCLE CopPaquete;
     |CIERRA #8;
     |CIERRA #7;
     |CIERRA #73;
     |ENTLINEAL #1#7, 1, 4, 22, 1, Min7, Max7;

     Tempo = aTmp7; |HAZ BoraTempo; |DELINDEX #7;
     Tempo = aTmp8; |HAZ BoraTempo; |DELINDEX #8;
     |POPV;
|FPRC;
----------------------------------
|PROCESO RenumLin1;
     |BORRA 020#5a;
     #mecaw005@#0 = nPaquete;
     #mecaw005@#1 = #5#1;
     #mecaw005@#2 = #5#2;
     #mecaw005@#3 = #5#3;
     |GRABA 020#mecaw005@.n;
|FPRC;

|DEFBUCLE RenumLin1;
     #5#1;
     ;
     #4#0, 001;
     #4#0, 999;
     ;
     NULL, RenumLin1;
|FIN;

|PROCESO RenumCab1;
     nPaquete = nPaquete + 1;
     |BUCLE RenumLin1;
     |BORRA 020#4a;
     #mecaw004@#0 = nPaquete;
     #mecaw004@#1 = #4#1;
     #mecaw004@#2 = #4#2;
     #mecaw004@#3 = #4#3;
     #mecaw004@#4 = #4#4;
     |GRABA 020#mecaw004@.n;
|FPRC;

|DEFBUCLE RenumCab1;
     #4#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, RenumCab1;
|FIN;

|PROCESO RenumLin2;
     #5#0 = #mecaw005@#0;
     #5#1 = #mecaw005@#1;
     #5#2 = #mecaw005@#2;
     #5#3 = #mecaw005@#3;
     |GRABA 020#5n;
|FPRC;

|DEFBUCLE RenumLin2;
     #mecaw005@#1002;
     ;
     nPaquete, 001;
     nPaquete, 999;
     ;
     NULL, RenumLin2;
|FIN;

|PROCESO RenumCab2
     nPaquete = #mecaw004@#0;
     |BUCLE RenumLin2
     #4#0 = #mecaw004@#0;
     #4#1 = #mecaw004@#1;
     #4#2 = #mecaw004@#2;
     #4#3 = #mecaw004@#3;
     #4#4 = #mecaw004@#4;
     |GRABA 020#4n;
|FPRC;

|DEFBUCLE RenumCab2;
     #mecaw004@#1001;
     ;
     1;
     9999;
     ;
     NULL, RenumCab2;
|FIN;

|PROCESO CheqRenum;
     #60#0 = #73#28;
     #60#1 = #73#0;
     #60#2 = #73#1;
     |LEE 000#60=;
     |SI FSalida = 0;
          |SI #60#8 ! 0;
               aAlfa = "0000Linea " + #73#1 + ", con orden de fabricacion:" + #60#14 + "/" + #60#8;
               |MENAV aAlfa;
               nErrRenum = 1;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Renumera;
     nErrRenum = 0;
     |ABRE #60;
     |BUCLELIN 2CheqRenum#104;
     |CIERRA #60;
     |SI nErrRenum ! 0;
          |MENAV "0000Opcion cancelada...";
     |SINO;
          |DDEF aPath;
          aAlfa = aPath + "mecaw004";
          |CARGA_DEF aAlfa, mecaw004@;
          |SI FSalida = 0;
               aAlfa = aPath + "mecaw005"
               |CARGA_DEF aAlfa, mecaw005@;
               |SI FSalida = 0;
                    |HAZ CreaTempo; |NOME_DAT #mecaw004@#Tempo#; aTemp4 = Tempo;
                    |HAZ CreaTempo; |NOME_DAT #mecaw005@#Tempo#; aTemp5 = Tempo;
                    |DELINDEX #mecaw004@;
                    |DELINDEX #mecaw005@;
                    |ABRE #mecaw004@;
                    |ABRE #mecaw005@;
                    nPaquete = 0;
                    |BUCLE RenumCab1;
                    |CIERRA #mecaw005@;
                    |CIERRA #mecaw004@;
                    |ABRE #4;
                    |ABRE #5;
                    |BUCLE RenumCab2;
                    |CIERRA #5;
                    |CIERRA #4;
                    |DELINDEX #mecaw004@;
                    |DELINDEX #mecaw005@;
                    Tempo = aTemp4; |HAZ BoraTempo;
                    Tempo = aTemp5; |HAZ BoraTempo;
                    |DESCARGA_DEF #mecaw005@;
               |FINSI;
               |DESCARGA_DEF #mecaw004@;
          |FINSI;
          |MENAV "0000Renumeracion finalizada.";
     |FINSI;
|FPRC;
----------------------------------
|PROCESO GraLin;
     |DDEFECTO #80;
     #80#0 = #0#0;
     #80#1 = #0#1;
     #80#2 = #5#0;
     #80#3 = #5#1;
     #80#4 = #5#2;
     #80#5 = #5#3;
     |GRABA 020#80n;
|FPRC;

|DEFBUCLE GraLin;
     #5#1;
     ;
     #4#0, 001;
     #4#0, 999;
     ;
     NULL, GraLin;
|FIN;

|PROCESO GraPaquete;
     |BUCLE GraLin;
     |DDEFECTO #70;
     #70#0 = #0#0;
     #70#1 = #0#1;
     #70#2 = #4#0;
     #70#3 = #4#1;
     #70#4 = #4#2;
     #70#5 = #4#3;
     #70#6 = #4#4;
     #70#8 = #4#5;
     |GRABA 020#70n;
|FPRC;

|DEFBUCLE GraPaquete;
     #4#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, GraPaquete;
|FIN;

|PROCESO BorraLinPaquete;
     |BORRA 020#80a;
     |LIBERA #80;
|FPRC;

|DEFBUCLE BorraLinPaquete;
     #80#1;
     ;
     #70#0, #70#1, #70#2, 001;
     #70#0, #70#1, #70#2, 999;
     be;
     NULL, BorraLinPaquete;
|FIN;

|PROCESO BorraPaquete;
     |BUCLE BorraLinPaquete;
     |BORRA 020#70a;
     |LIBERA #70;
|FPRC;

|DEFBUCLE BorraPaquete;
     #70#1;
     5, 6;
     #0#0, #0#1, 001, aDSerOrd, nDNumOrd;
     #0#0, #0#1, 999, aHSerOrd, nHNumOrd;
     be;
     NULL, BorraPaquete;
|FIN;

|PROCESO Actualiza;
     |BUCLE BorraPaquete;
     |ABRE #70;
     |ABRE #80;
     |BUCLE GraPaquete;
     |CIERRA #80;
     |CIERRA #70;
|FPRC;
-----------------------------
|PROCESO Contador;
     |PARA nConta = 1; |SI nConta [ 999; |HACIENDO nConta = nConta + 1;
          #4#0 = nConta;
          |LEE 000#4=;
          |SI FSalida ! 0;
               #mecam007@#0 = #0#0;
               #mecam007@#1 = #0#1;
               #mecam007@#2 = nConta;
               |LEE 000#mecam007@.=;
               |SI FSalida ! 0;
                    |SAL;
               |FINSI;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO Recalcula;
     |DDEFECTO #60;
     #60#0 = #73#28;
     #60#1 = #73#0;
     #60#2 = #73#1;
     |LEE 000#60=;
     |SI #60#8 ! 0;  ||Orden Fab.
          |ACABA;
     |FINSI;

     |DDEFECTO #1;
     #1#0 = #73#2;
     |LEE 000#1=;

     nUnidades = #73#5;
     |SI nUnidades < 0;
          nUnidades = -nUnidades;
     |FINSI;

     |DDEFECTO #4;
     |DDEFECTO #5;
     nPaquetes = nUnidades / #1#1;
     nUni = 0;
     |PARA i = 1; |SI i [ nPaquetes; |HACIENDO i = i + 1;
          |HAZ Contador;
          #4#0 = nConta;
          #4#1 = #73#2;
          #4#2 = #1#1;
          #4#3 = #60#14;
          #4#4 = #60#8;
          |GRABA 020#4n;
          #5#0 = #4#0;
          #5#1 = #73#1;
          #5#2 = #73#50;
          #5#3 = #1#1;
          |GRABA 020#5n;
          nUni = nUni + #1#1;
     |SIGUE;
     |SI nUni ! nUnidades; |Y nUnidades ! 0; |Y #1#1 ! 0;
          |HAZ Contador;
          #4#0 = nConta;
          #4#1 = #73#2;
          #4#2 = nUnidades - nUni;
          #4#3 = #60#14;
          #4#4 = #60#8;
          |GRABA 020#4n;
          #5#0 = #4#0;
          #5#1 = #73#1;
          #5#2 = #73#50;
          #5#3 = nUnidades - nUni;
          |GRABA 020#5n;
     |FINSI;
|FPRC;

|PROCESO CopiaOtros;
     |SI #70#5 = #0#2; |Y #70#6 = #0#3;
          |ACABA;
     |FINSI;
     |PARA i = 0; |SI i [ 6; |HACIENDO i = i + 1;
          #mecam007@#i# = #70i;
     |SIGUE;
     |GRABA 020#mecam007@.n;
|FPRC;

|DEFBUCLE CopiaOtros;
     #70#1;
     ;
     #0#0, #0#1, 0001;
     #0#0, #0#1, 9999;
     ;
     NULL, CopiaOtros;
|FIN;

|PROCESO RecalEstandar;
     |DDEF aAlfa;
     aAlfa = aAlfa + "mecam007";
     |CARGA_DEF aAlfa, mecam007@;
     |SI FSalida ! 0; |ACABA; |FINSI;
     |HAZ CreaTempo; |NOME_DAT #mecam007@#aTmp7#;
     |ABRE #mecam007@;

     |BUCLE CopiaOtros;

     |DELINDEX #4;
     |DELINDEX #5;
     |ABRE #4;
     |ABRE #5;
     |ABRE #1;
     |ABRE #60;
     |BUCLELIN 2Recalcula#104;
     |CIERRA #60;
     |CIERRA #1;
     |CIERRA #5;
     |CIERRA #4;

     |CIERRA #mecam007@;
     |HAZ BoraTempo; |DELINDEX #mecam007@;
     |DESCARGA_DEF #mecam007@;

     |MENAV "0000Recalculo finalizado.";
|FPRC;
-----------------------------
|PROCESO mecaw005;
     nHay = 1;
     nUni = nUni + #5#3;
|FPRC;

|DEFBUCLE mecaw005;
     #5#1;
     ;
     #4#0, #73#1;
     #4#0, #73#1;
     ;
     NULL, mecaw005;
|FIN;

|PROCESO mecaw004;
     |BUCLE mecaw005;
|FPRC;

|DEFBUCLE mecaw004;
     #4#1;
     1;
     0001, #73#2;
     9999, #73#2;
     ;
     NULL, mecaw004;
|FIN;

|PROCESO LinPedido;
     |DDEFECTO #60;
     #60#0 = #73#28;
     #60#1 = #73#0;
     #60#2 = #73#1;
     |LEE 000#60=;
     |SI #60#14 ! #0#2; |O #60#8 ! #0#3;
          |ACABA;
     |FINSI;

     nUni = 0;
     nHay = 0;
     |BUCLE mecaw004;
     |SI nHay = 0;
          nError = 1;
          aAlfa = "0000Articulo " + #73#2 + " sin paquete";
          |MENAV aAlfa;
     |SINO;
          |SI nUni ! #73#5;
               nError = 1;
               aAlfa = "0000Articulo " + #73#2 + " con unidades distintas"
               |MENAV aAlfa;
               aAlfa = "0000Unidades paquete:" +  nUni + ", en pedido:" + #73#5;
               |MENAV aAlfa;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Chequea;
     |ABRE #60;
     |BUCLELIN 2LinPedido#104;
     |CIERRA #60;
     FSalida = nError;
     |SI nError ! 0;
          |MENAV "0000Opcion anulada...";
     |FINSI;
|FPRC;

|PROCESO LinPed0; |TIPO 0;
     |ABRE #73;
     #73#28 = #0#0;
     #73#0 = #0#1;
     #73#1 = #5#1;
     |LEE 000#73=;
     |SI FSalida = 0;
          |SI #73#2 ! #4#1;
               aAlfa = "0000La linea de pedido es del articulo:" + #73#2;
               |MENAV aAlfa;
               |ERROR;
          |SINO;
               #5#2 = #73#50; |PINTA #5#2;
          |FINSI;
     |SINO;
          |MENAV "0000No existe linea pedido...";
          |ERROR;
     |FINSI;
     |CIERRA #73;
|FPRC;

|PROCESO BusArt;
     |SI #73#2 = #4#1;
          nHay = 1;
     |FINSI;
|FPRC;

|PROCESO Arti0; |TIPO 0;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 1;
          nHay = 0;
          |BUCLELIN 2BusArt#104;
          |SI nHay = 0;
               |MENAV "0000No existe articulo en el pedido";
               |ERROR;
          |SINO;
               |DDEFECTO #1;
               |ABRE #1;
               #1#0 = #4#1;
               |LEE 000#1=;
               |CIERRA #1;
               #4#2 = #1#1; |PINTA #4#2;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Min5;
     #5#0 = #4#0;
     #5#1 = 1;
|FPRC;

|PROCESO Max5;
     #5#0 = #4#0;
     #5#1 = 9999;
|FPRC;

|PROCESO BorraLin;
     |BORRA 020#5a;
|FPRC;

|DEFBUCLE BorraLin;
     #5#1;
     ;
     #4#0, 001;
     #4#0, 999;
     ;
     NULL, BorraLin;
|FIN;

|PROCESO Juntando;
     #5#0 = #6#0;
     #5#1 = #mecaw005@#1;
     |LEE 101#5=;
     |SI FSalida = 0;
          #5#3 = #5#3 + #mecaw005@#3;
          |GRABA 020#5a;
          |LIBERA #5;
     |SINO;
          #5#1 = #mecaw005@#1;
          #5#2 = #mecaw005@#2;
          #5#3 = #mecaw005@#3;
          |GRABA 020#5n;
     |FINSI;
     |BORRA 020#mecaw005@.a;
     |LIBERA #mecaw005@;

     nErrorJunta = 0;
|FPRC;

|DEFBUCLE Juntando;
     #mecaw005@#1002;
     ;
     #6#1, 0001;
     #6#1, 9999;
     be;
     NULL, Juntando;
|FIN;

|PROCESO Juntalo;
     |DDEF aAlfa;
     aAlfa = aAlfa + "mecaw005";
     |CARGA_DEF aAlfa, mecaw005@;
     |SI FSalida = 0;
          |NOME_DAT #mecaw005@#aTmp5#;
          |ABRE #5;
          |BUCLE Juntando;
          |CIERRA #5;
          |DESCARGA_DEF #mecaw005@;

     |FINSI;
|FPRC;

|PROCESO Juntar;
     aEntrada = FEntrada;
     nErrorJunta = 1;
     |PUSHV 0501 2380;
     |BLANCO 1327 2055;
     #6#1 = #4#0;
     |PINPA #0#6;
     |PINDA #0#6;
     |ENDATOS #1#6;
     |SI FSalida = 0;
          |SALVA_FICHA #4;
          #4#0 = #6#1;
          |LEE 000#4=;
          aOrden = #4#3 + #4#4;
          aArti = #4#1;
          #4#0 = #6#0;
          |LEE 000#4=;
          |SI FSalida ! 0;
               |MENAV "0000No existe paquete";
          |SINO;
               aAlfa = #4#3 + #4#4;
               |SI aAlfa ! aOrden;
                    |MENAV "0000Las ordenes deben ser iguales";
               |SINO;
                    |SI aArti ! #4#1;
                         |MENAV "0000Los articulos deben ser iguales";
                    |SINO;
                         |SI #6#0 ! #6#1;
                              |HAZ Juntalo;
                         |FINSI;
                    |FINSI;
               |FINSI;
          |FINSI;
          |REPON_FICHA #4;

     |FINSI;
     |POPV;

     FEntrada = aEntrada;
|FPRC;
----------------------------
|PROCESO Moviendo;
     #mecaw005a@#0 = #9#0;
     #mecaw005a@#1 = #mecaw005@#1;
     |LEE 101#mecaw005a@.=;
     |SI FSalida = 0;
          #mecaw005a@#3 = #mecaw005a@#3 + #9#2;
          |GRABA 020#mecaw005a@.a;
          |LIBERA #5;
     |SINO;
          #mecaw005a@#1 = #mecaw005@#1;
          #mecaw005a@#2 = #mecaw005@#2;
          #mecaw005a@#3 = #9#2;
          |GRABA 020#mecaw005a@.n;
     |FINSI;
     #mecaw005@#3 = #mecaw005@#3 - #9#2;
     |GRABA 020#mecaw005@.a;
     |LIBERA #mecaw005@;

     nErrorMueve = 0;
|FPRC;

|DEFBUCLE Moviendo;
     #mecaw005@#1002;
     ;
     #9#1, #5#1;
     #9#1, #5#1;
     be;
     NULL, Moviendo;
|FIN;

|PROCESO Mueve;
     |DDEF aPath;
     aAlfa = aPath + "mecaw005";
     |CARGA_DEF aAlfa, mecaw005@;
     |SI FSalida = 0;
          |NOME_DAT #mecaw005@#aTmp5#;

          aAlfa = aPath + "mecaw005";
          |CARGA_DEF aAlfa, mecaw005a@;
          |SI FSalida = 0;
               |NOME_DAT #mecaw005a@#aTmp5#;
               |ABRE #mecaw005a@;
               |BUCLE Moviendo;
               |CIERRA #mecaw005a@;
               |DESCARGA_DEF #mecaw005a@;
          |FINSI;

          |DESCARGA_DEF #mecaw005@;

     |FINSI;
|FPRC;

|PROCESO CreaNuevo;
     |SALVA_FICHA #4;
     |SALVA_FICHA #5;

     #4#0 = #9#1;
     |LEE 000#4=;

     #4#0 = #9#0;
     |GRABA 020#4n;

     #5#0 = #4#0;
     #5#3 = 0;
     |GRABA 020#5n;

     |REPON_FICHA #5;
     |REPON_FICHA #4;
|FPRC;

|PROCESO MueveUni;
     aEntrada = FEntrada;
     nErrorMueve = 1
     |PUSHV 0501 2380;
     |BLANCO 1327 2155;
     #9#1 = #4#0;
     #9#2 = #5#3;
     |PINPA #0#9;
     |PINDA #0#9;
     |ENDATOS #1#9;
     |SI FSalida = 0;
          aOrden = #4#3 + #4#4;
          aArti = #4#1;
          |SALVA_FICHA #4;
          #4#0 = #9#0;
          |LEE 000#4=;
          |SI FSalida ! 0;
               |SI #9#0 ! #9#1; |HAZ CreaNuevo; |HAZ Mueve; |FINSI;
          |SINO;
               aAlfa = #4#3 + #4#4;
               |SI aAlfa ! aOrden;
                    |MENAV "0000Las ordenes deben ser iguales";
               |SINO;
                    |SI aArti ! #4#1;
                         |MENAV "0000Los articulos deben ser iguales";
                    |SINO;
                         |SI #9#0 ! #9#1; |HAZ Mueve; |FINSI;
                    |FINSI;
               |FINSI;
          |FINSI;
          |REPON_FICHA #4;

          nPaquete = #9#0;
          |HAZ CuentaUni;

          nPaquete = #9#1;
          |HAZ CuentaUni;

     |FINSI;
     |POPV;
     FEntrada = aEntrada;
|FPRC;

----------------------------
|PROCESO MiraHay;
     nHayLin = nHayLin + 1;
|FPRC;

|DEFBUCLE MiraHay;
     #mecaw005@#1002;
     ;
     #4#0, 0001;
     #4#0, 9999;
     ;
     NULL, MiraHay;
|FIN;

|PROCESO MiraHay0;
     |DDEF aPath;
     aAlfa = aPath + "mecaw005";
     |CARGA_DEF aAlfa, mecaw005@;
     |SI FSalida = 0;
          |NOME_DAT #mecaw005@#aTmp5#;
          |BUCLE MiraHay;
          |DESCARGA_DEF #mecaw005@;
     |FINSI;
|FPRC;

|PROCESO Tipo5_11; |TIPO 11;
     |SI FSalida = 10;   ||F2 lineas
          |HAZ MueveUni;
          |SI nErrorMueve = 0;
               nUni = #5#3 - #9#2;
               |SI nUni = 0;
                    nHayLin = 0;
                    |HAZ MiraHay0;
                    |BORRA 020#5a;
                    |SI nHayLin = 1;
                         nSwBorra4 = #4#0;
                         |PTEC 807;
                         |PONCONTROL 23, "SI";
                    |SINO;
                         |ERROR;
                         |PTEC 809; || abajo
                         |PONCONTROL 23, "SI";
                    |FINSI;
               |SINO;
                    |PONCONTROL 23, "SI";
               |FINSI;
          |SINO;
               |ERROR;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Tipo11; |TIPO 11;
     |SI FSalida = 10;   || F2
          |SI aConsulta = "S";
               |MENAV "0000Funcion no disponible en modo consulta";
               |ERROR; |ACABA;
          |FINSI;
          |HAZ Juntar;
          |SI nErrorJunta = 0;
               nPaquete = #6#0;
               |HAZ CuentaUni;

               #4#0 = #6#1;
               |BORRA 040#4c;
               |ERROR;
               |PTEC 809; || abajo
               |PONCONTROL 23, "SI";
          |SINO;
               |ERROR;
          |FINSI;
     |FINSI;
     |SI FSalida = 11;   || F3
          |SI aConsulta = "S";
               |MENAV "0000Funcion no disponible en modo consulta";
               |ERROR; |ACABA;
          |FINSI;
          |SI #0#2 = "     "; |Y #0#3 = 0;
               nRecal = 1;
               |PTEC 807;
          |SINO;
               |MENAV "0000Registros con orden de fabricacion. Cancelado";
               |ERROR;
          |FINSI;
     |FINSI;
     |SI FSalida = 12;   || F4
          |SI aConsulta = "S";
               |MENAV "0000Funcion no disponible en modo consulta";
               |ERROR; |ACABA;
          |FINSI;
          |SI #0#2 = "     "; |Y #0#3 = 0;
               nRecal = 2;
               |PTEC 807;
          |SINO;
               |MENAV "0000Registros con orden de fabricacion. Cancelado";
               |ERROR;
          |FINSI;
     |FINSI;
     |SI FSalida = 13;   || F5
          |SALVA_FICHA #4;
          |HAZ PorLinPedido;
          |REPON_FICHA #4;
          |ERROR; |ACABA;
     |FINSI;
|FPRC;

|PROCESO Tipo12; |TIPO 12;
|FPRC;

|PROCESO Tipo2; |TIPO 2;
     nForma = 0 +. 1;
     nModo = (FEntrada / 100) ! 0;
     |SI nForma = 1;
          ||ENTLINEAL #1#5, 2, 2, 22, 0, Min5, Max5;
          |ENTLINEAL #1#5, 2, 4, 22, 0, Min5, Max5;
     |FINSI;
     |SI nModo = 3;
          |BUCLE BorraLin;
     |FINSI;
|FPRC;

|PROCESO Tipo0; |TIPO 0;
     nModo = (FEntrada / 100) ! 0;
     |SI FSalida = 0; |Y #4Campo = Contenido;
          |SI aConsulta = "S";
               |ENTLINEAL #1#5, 2, 4, 22, 0, Min5, Max5;
          |FINSI;
     |FINSI;

     |C_M #4#1, 1, "N";
     |SI nModo = 1;
          |C_M #4#1, 1, "S";
          |ABRE #70;
          #70#0 = #0#0;
          #70#1 = #0#1;
          #70#2 = #4#0;
          |LEE 000#70=;
          |SI FSalida = 0;
               aAlfa = "0000El paquete existe, con orden:" +&13+ #70#5 + "/" + #70#6;
               |MENAV aAlfa;
               |ERROR;
          |FINSI;
          |CIERRA #70;
     |FINSI;
|FPRC;

|PROCESO Tipo7; |TIPO 7;

     |ENTLINEAL #1#5, 1, 7, 22, 0, Min5, Max5;

     |SI nSwBorra4 ! 0;
          |SALVA_DATOS #0;
          #4#0 = nSwBorra4;
          |BORRA 020#4c;
          |REPON_DATOS #0;
          nSwBorra4 = 0;
          |PTEC 808; || arriba
          |PTEC 809; || abajo
          |PTEC 809; || abajo
          |PONCONTROL 23, "SI";
     |FINSI;
|FPRC;

|PROCESO Min4;
     #4#0 = 1;
|FPRC;

|PROCESO Max4;
     #4#0 = 9999;
|FPRC;

|PROCESO PaqueteLin;
     #5#0 = #80#2;
     #5#1 = #80#3;
     #5#2 = #80#4;
     #5#3 = #80#5;
     |GRABA 020#5n;
|FPRC;

|DEFBUCLE PaqueteLin;
     #80#1;
     ;
     #70#0, #70#1, #70#2, 001;
     #70#0, #70#1, #70#2, 999;
     ;
     NULL, PaqueteLin;
|FIN;

|PROCESO Paquetes;
     |BUCLE PaqueteLin;

     #4#0 = #70#2;
     #4#1 = #70#3;
     #4#2 = #70#4;
     #4#3 = #70#5;
     #4#4 = #70#6;
     #4#5 = #70#8;
     |GRABA 020#4n;

     nHay = 1;
|FPRC;

|DEFBUCLE Paquetes;
     #70#1;
     5, 6;
     #0#0, #0#1, 001, aDSerOrd, nDNumOrd;
     #0#0, #0#1, 999, aHSerOrd, nHNumOrd;
     ;
     NULL, Paquetes;
|FIN;

|PROCESO Inicio;
     |HAZ CreaTempo; aTmp4 = Tempo; |NOME_DAT #4 Tempo; |DELINDEX #4;
     |HAZ CreaTempo; aTmp5 = Tempo; |NOME_DAT #5 Tempo; |DELINDEX #5;

     |SI #0#3 = 999999;
          aDSerOrd = "     ";
          aHSerOrd = "zzzzz";
          nDNumOrd = 0;
          nHNumOrd = 999999;
          aConsulta = "S";
     |SINO;
          aDSerOrd = #0#2;
          aHSerOrd = #0#2;
          nDNumOrd = #0#3;
          nHNumOrd = #0#3;
     |FINSI;

     |ABRE #4;
     |ABRE #5;
     |BUCLE Paquetes;
     |CIERRA #4;
     |CIERRA #5

     |SI nHay = 0;
          |MENAV "0000No hay registros...";
     |SINO;
          |PINPA #0#4;
          |SI aConsulta = "S";
               |ATRI I; |PINTA 521, "(Consulta)"; |ATRI 0;
               |ENTLINEAL #1#4, 1, 4, 20, 0, Min4, Max4;
          |SINO;
               |PARA; |SI; |HACIENDO;
                    |PARA nRecal = 1; |SI nRecal ! 0; |HACIENDO;
                         nRecal = 0;
                         |ENTLINEAL #1#4, 1, 2, 20, 0, Min4, Max4;
                         |SI nRecal = 1; |HAZ RecalEstandar; |FINSI;
                         |SI nRecal = 2; |HAZ Renumera; |FINSI;
                    |SIGUE;
                    |MENU Menu;
                    |SI FSalida = 3; |O FSalida [ 0; |SAL; |FINSI;
                    |SI FSalida = 2;
                         |HAZ Chequea;
                         |SI FSalida = 0;
                              |HAZ Actualiza;
                              |SAL;
                         |FINSI;
                    |FINSI;
               |SIGUE;
          |FINSI;
     |FINSI;

     Tempo = aTmp4; |HAZ BoraTempo; |DELINDEX #4;
     Tempo = aTmp5; |HAZ BoraTempo; |DELINDEX #5;

|FPRC;

|PROCESO Tipo3; |TIPO 3;
     |ABRE #104;
     #104#25 = #0#0;
     #104#0 = #0#1;
     |LEE 101#104=;
     |SI FSalida ! 0; |ACABA; |FINSI;
     |HAZ Inicio;
     |CIERRA #104;
|FPRC;

|PROGRAMA;
     |SI enDocu ! 0;
          #0#0 = eaSerie;
          #0#1 = enDocu;
          #0#3 = 999999;
          |HAZ Inicio;
     |SINO;
          |MANTE #0;
     |FINSI;
|FPRO;
