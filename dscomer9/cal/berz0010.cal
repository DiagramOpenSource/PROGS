|FICHEROS;
     berz0010 #0;
     berm0001 #1;
     berm0002 #2;
     berm0005 #5;
     berm0010 #10;
     agifa019 #19;
     referen@;
|FIN;

|VARIABLES;
     &Tempo = "";
     nCamTermi = 0;
     nCamFondo = 0;
     nColor = 0;
     nCamPega = 0;
     nCamTalla = 0;
     nNume = 0;
     aAlfa = "";
     nNum = 0;
     nLin = 0;
     aDes = "";
     aFam = "";
     aArti = "";
     aTalla = "";
     aColor = "";
|FIN;

|PROCESO Grabalo;
     nNum = nCamPega - 36;
     nColor = #1nCamTermi + #1nCamFondo + nNum;
     aColor = ("000" + nColor) % -103;
     aTalla = ("000" + #1nCamTalla) % -103;

     aArti = #2#2; |QBF aArti;
     aAlfa = aArti % -101;
     |SI aAlfa ! "."; aArti = aArti + "."; |FINSI;
     aArti = aArti + aTalla + "." + aColor;

     #5#0 = #1nCamFondo;
     |LEE 000#5=;
     |SI FSalida ! 0; |DDEFECTO #5; |FINSI;

     #10#0 = #1nCamTermi;
     |LEE 000#10=;
     |SI FSalida ! 0; |DDEFECTO #10; |FINSI;

     aDes = #1#1; |QBF aDes;
     aDes = aDes + " " + #5#1; |QBF aDes;
     aDes = aDes + " " + #10#1; |QBF aDes;

     #5#0 = #1nCamPega;
     |LEE 000#5=;
     |SI FSalida ! 0; |DDEFECTO #5; |FINSI;

     aDes = aDes + " " + #5#1; |QBF aDes;

     #19#0 = #2#2;
     |LEE 000#19=;
     |SI FSalida ! 0; |DDEFECTO #19; |FINSI;
     aFam = #19#2;

     |DDEFECTO #19;
     #19#0 = aArti;
     |LEE 000#19=;
     |SI FSalida = 0;
          aAlfa = "0000Art¡culo -" + #19#0; |QBF aAlfa;
          aAlfa = aAlfa + "- existente...";
          |MENAV aAlfa;
     |SINO;
          #19#1 = aDes;
          #19#2 = aFam;
          #19#180 = "S"; ||partida
          |GRABA 020#19n;

          |SALVA_DATOS #19;
          |REPON_DATOS #referen@;
          |GRABA 020#referen@.n;
     |FINSI;
|FPRC;

|PROCESO Crealo;
     |PARA nCamTermi = 60; |SI nCamTermi [ 61; |HACIENDO nCamTermi = nCamTermi + 1;
          |PARA nCamFondo = 55; |SI nCamFondo [ 59; |HACIENDO nCamFondo = nCamFondo + 1;
               nNume = #1nCamFondo;
               |SI nNume ! 0;
                    |PARA nCamPega = 37; |SI nCamPega [ 46; |HACIENDO nCamPega = nCamPega + 1;
                         nNume = #1nCamPega;
                         |SI nNume ! 0;
                              |PARA nCamTalla = 64; |SI nCamTalla [ 71; |HACIENDO nCamTalla = nCamTalla + 1;
                                   nNume = #1nCamTalla;
                                   |SI nNume ! 0;
                                        |HAZ Grabalo;
                                   |FINSI;
                              |SIGUE;
                         |FINSI;
                    |SIGUE;
               |FINSI;
          |SIGUE;
     |SIGUE;
|FPRC;

|PROCESO BusLinCompo1;
     |SI #2#22 = 1;
          nLin = #2#1;
          |ERROR10;
     |FINSI;
|FPRC;

|DEFBUCLE BusLinCompo1;
     #2#1;
     ;
     #1#0, 001;
     #1#0, 999;
     ;
     NULL, BusLinCompo1;
|FIN;

|PROCESO Tipo3; |TIPO 3;
     |DDEF aAlfa;
     aAlfa = aAlfa + "agifa019";
     |CARGA_DEF aAlfa, referen@;
     |HAZ CreaTempo; |NOME_DAT #referen@#Tempo#; |DELINDEX #referen@;
     |ABRE #referen@;
     |CIERRA #referen@;
     |HAZ BoraTempo; |DELINDEX #referen@;
     |DESCARGA_DEF #referen@;


     |ABRE #1;
     #1#0 = #0#0;
     |LEE 020#1=;
     |SI FSalida = 0;
          nLin = 0;
          |BUCLE BusLinCompo1;
          |SI nLin = 0;
               |MENAV "0000No existe linea escandallo del tipo componente 1 ...";
          |SINO;
               |ABRE #2;
               #2#0 = #0#0;
               #2#1 = nLin;
               |LEE 020#2=;
               |CIERRA #2;

               |DDEF aAlfa;
               aAlfa = aAlfa + "agifa019";
               |CARGA_DEF aAlfa, referen@;
               |HAZ CreaTempo; |NOME_DAT #referen@#Tempo#; |DELINDEX #referen@;
               |ABRE #referen@;

               |ABRE #5;
               |ABRE #10;
               |ABRE #19;
               |HAZ Crealo;
               |CIERRA #19;
               |CIERRA #10;
               |CIERRA #5;

               |LEE 000#referen@.p;
               |CONSULTA_CLAVE #1#referen@;
               |CIERRA #referen@;
               |HAZ BoraTempo; |DELINDEX #referen@;
               |DESCARGA_DEF #referen@;
          |FINSI;
     |FINSI;
     |CIERRA #1;
|FPRC;
