|FICHEROS;
     agi00031 #0;        || Este def.
     agifa073 #1;        || Pedidos de Ventas(L).
     agifa067 #2;        || Pedidos de Compras(L).
|FIN;

|VARIABLES;
     &Tempo    = "";
     aTempo0   = "";
     linea     = 0;
     alfa      = "";
     usu       = "";
     fichero   = "";
     informe   = "";
     art_ant   = "";
     dserie    = "";
     hserie    = "";
     dnumero   = 0;
     hnumero   = 0;
     dfecha    = "";
     hfecha    = "";
|FIN;

________________________/ IMPRIME EL INFORME COMPARATIVO DE COMPRAS Y VENTAS.
|PROCESO imprime;
     |PRINT;
|FPRC;

|DEFBUCLE temporal;
     #0#2;
     ;
     "                    ","             ","      ";
     "zzzzzzzzzzzzzzzzzzzz","zzzzzzzzzzzzz","zzzzzz";
     ;
     NULL,imprime;
|FIN;


______________________/ LEE Y RELLENA EL TEMPORAL CON LOS PEDIDOS DE COMPRAS.
|PROCESO graba_ventas;
     |SI #0#16 = "S";
          #0#7  = #1#2;
          #0#10 = #1#13;
          |LEE 141#0=;
          |SI FSalida ! 0;
               |DDEFECTO #0;
               #0#7  = #1#2;
               #0#8  = #1#28;      || SERIE.
               #0#9  = #1#0;       || PEDIDO.
               #0#10 = #1#13;      || FECHA ENTREGA.
               #0#11 = #1#20;      || CLIENTE.
               #0#15 = #1#2;       || ARTICULO.
               |GRABA 021#0b;
          |FINSI;
          #0#12 = #0#12 + #1#12;      || UNIDADES PENDIENTES DE SERVIR.
          |GRABA 021#0a;
          |LIBERA #0;
     |SINO;
          alfa  = linea;
          #0#7  = alfa;
          #0#8  = #1#28;      || SERIE.
          #0#9  = #1#0;       || PEDIDO.
          #0#10 = #1#13;      || FECHA ENTREGA.
          #0#11 = #1#20;      || CLIENTE.
          #0#12 = #1#12;      || UNIDADES PENDIENTES DE SERVIR.
          #0#13 = 0;
          #0#14 = 0;
          #0#15 = #1#2;       || ARTICULO.
          |GRABA 021#0n;
          |LIBERA #0;
          linea = linea + 1;
     |FINSI;
|FPRC;

|DEFBUCLE lee_ventas;
     #1#1;
     13;
     dserie,dnumero,001,dfecha;
     hserie,hnumero,999,hfecha;
     ;
     NULL,graba_ventas;
|FIN;


_______________________/ LEE Y RELLENA EL TEMPORAL CON LOS PEDIDOS DE VENTAS.
|PROCESO graba_compras;
     |SI #0#16 = "S";
          #0#7  = #2#2;
          #0#10 = #2#13;
          |LEE 141#0=;
          |SI FSalida ! 0;
               |DDEFECTO #0;
               #0#7 = #2#2;
               #0#8  = #2#28;      || SERIE.
               #0#9  = #2#0;       || PEDIDO.
               #0#10 = #2#13;      || FECHA ENTREGA.
               #0#11 = #2#20;      || PROVEEDOR.
               #0#15 = #2#2;       || ARTICULO.
               |GRABA 021#0b;
          |FINSI;
          #0#13 = #0#13 + (#2#5 - #2#38);
          |GRABA 021#0a;
          |LIBERA #0;
     |SINO;
          alfa = linea;
          #0#7  = alfa;
          #0#8  = #2#28;      || SERIE.
          #0#9  = #2#0;       || PEDIDO.
          #0#10 = #2#13;      || FECHA ENTREGA.
          #0#11 = #2#20;      || PROVEEDOR.
          #0#12 = 0;
          #0#13 = (#2#5 - #2#38);      || UNIDADES PENDIENTES DE RECIBIR.
          #0#14 = 0;
          #0#15 = #2#2;       || ARTICULO.
          |GRABA 021#0n;
          |LIBERA #0;
          linea = linea + 1;
     |FINSI;
|FPRC;

|DEFBUCLE lee_compras;
     #2#1;
     13;
     dserie,dnumero,001,dfecha;
     hserie,hnumero,999,hfecha;
     ;
     NULL,graba_compras;
|FIN;


_________________________________________________________________/ PRINCIPIO.
|PROGRAMA;
     |HAZ CreaTempo; aTempo0 = Tempo;
     |NOME_DAT #0 aTempo0; |DELINDEX #0;
     |ABRE #0;
     |CLS;
     |CABEZA "INF.COMPARATIVO C/V";
     |PINPA #0#0;
     |DDEFECTO #0;
     |PINDA #0#0;
     |ENDATOS #1#0;
     |SI FSalida = 0;
          dserie  = #0#0;
          hserie  = #0#1;
          dnumero = #0#2;
          hnumero = #0#3;
          dfecha  = #0#4;
          hfecha  = #0#5;
          linea = 1;
          |BUCLE lee_ventas;
          |BUCLE lee_compras;
          |SI #0#16 = "S";
               informe = "agi00031";
          |SINO;
               informe = "agi00b31";
          |FINSI;
          |IMPRE 0;
          |SI FSalida = 0;
               |INFOR informe;
               |SI FSalida = 0;
                    |BUCLE temporal;
                    |PIEINF;
                    |FININF;
               |FINSI;
               |FINIMP;
          |FINSI;
     |FINSI;
     |CIERRA #0;
     |DELINDEX #0; Tempo = aTempo0; |HAZ BoraTempo;
|FPRO;
