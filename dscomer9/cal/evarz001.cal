|FICHEROS;
     evarz001 #0;
     evarm001 #1;
     evarm002 #2;

     agifa017 #17;
     agifa019 #19;
     evarz002 #20,MANTE;
     evarz003 #21,MANTE;
     evarz021 #22,MANTE;
     evarz023 #23;
     evarz024 #24;
     evarz025 #25;
     evarz026 #26;
     evarz027 #27;
     evarz028 #28;
     agifa029 #29;
     dsm00071 #71; || Nivel_1
     dsm00072 #72; || Nivel_1
     dsm00073 #73; || Nivel_1
     agifa321 #321;
     agifa324 #324;
     dsm00004 #904;
     dsm00010 #910;
     dsm00014 #914;
     dsm00015 #915;
     dsm00016 #916;
     dsm00016 #145;
|FIN;

|VARIABLES;
     &Tempo = "";
     &eaTmp1 = "";
     &eaTmp2 = "";
     &eaTmp3 = "";
     &eaTmp4 = "";
     &eaTmp5 = "";
     &eaTmp6 = "";
     &eaTmp7 = "";
     &eaTmp8 = "";
     &eaTmp9 = "";
     &eaModelo = "";
     &nDeci_PreBase = 0;
     &enSwMalo = 0;
     nNume     = 0;
     aAlfa     = "";
     nBoton1   = 0;
     nBoton2   = 0;
     nBoton3   = 0;
     aTitulo1  = "";
     aTitulo2  = "";
     aTitulo3  = "";
     aEstado   = "";
     nPosi     = 0;
     nSw       = 0;
     aDes1     = "";
     aDes2     = "";
     aDes3     = "";
     aDes4     = "";
     aDes      = "";
     aCod1     = "";
     aCod2     = "";
     aCod3     = "";
     aCod4     = "";
     aCod      = "";
     aBlanco   = "";
     i         = 0;
     j         = 0;
     aDesde    = "";
     aHasta    = "";
     aEspacio  = "";
     nSal1 = 0;
     nSal19 = 0;
     nBtn1 = 0;
     nBtn2 = 0;
     nPre2 = 0;
     nPre3 = 0;
     nSwPre = 0;
     nSwPre20 = 0;
     nSwPre21 = 0;
     nSwPre23 = 0;
     nSwPre26 = 0;
     nTabla = 0;
|FIN;

|INCLUYE i_varart;

|PROCESO Tabla66; |TIPO 66;
     |ABRE #915;
     #915#0 = #0#21;
     #915#1 = "";
     #915#2 = "";
     #915#5 = "";
     |LEE 000#915];
     |CONSULTA_CLAVE #1#915;
     |SI FSalida = 0;
          #0#21 = #915#0; |PINTA #0#21;
     |SINO;
          |ERROR;
     |FINSI;
     |CIERRA #915;
|FPRC;

|PROCESO Tabla0; |TIPO 0;
     |SI #0#21 ! "000";
          |ABRE #915;
          #915#0 = #0#21;
          #915#1 = "";
          #915#2 = "";
          #915#5 = "";
          |LEE 000#915=;
          |SI FSalida ! 0;
               |MENAV "0000No existe tabla...";
               |ERROR;
          |SINO;
               |ESTADO_CONTROL nBtn1, "DISABLE";
               |ESTADO_CONTROL nBtn2, "DISABLE";
          |FINSI;
          |CIERRA #915;
     |SINO;
          |ESTADO_CONTROL nBtn1, "ENABLE";
          |ESTADO_CONTROL nBtn2, "ENABLE";
     |FINSI;
|FPRC;

|PROCESO CodigoBarra;
     |SI #910#33 = "S";
         eAlfa = #19#0;
         |SUB_EJECUTA_NP "dsz99990;B";
         #19#128 = eAlfa;
     |SINO;
         #19#128 = #19#0;
     |FINSI;
|FPRC;

|PROCESO PreCaliCol;
     |DDEFECTO #2;
     #2#0 = #1#0;
     #2#1 = #28#3;
     |LEE 101#2=;
     #2#2 = #28#4;
     #2#3 = #28#5;
     #2#4 = #28#6;
     #2#5 = #28#7;
     #2#6 = #28#8;
     #2#7 = #28#9;
     #2#8 = #28#10;
     |SI FSalida = 0;
          |SI #0#1 = "S"; |GRABA 020#2a; |FINSI;
     |SINO;
          |GRABA 020#2n;
     |FINSI;
     |LIBERA #2;
|FPRC;

|DEFBUCLE PreCaliCol;
     #28#1;
     ;
     aCod1, aCod2, aCod3, "      ";
     aCod1, aCod2, aCod3, "zzzzzz";
     ;
     NULL, PreCaliCol;
|FIN;

|PROCESO PreCali;
     |DDEFECTO #2;
     #2#0 = #1#0;
     #2#1 = #25#2;
     |LEE 101#2=;
     #2#2 = #25#3;
     #2#3 = #25#4;
     #2#4 = #25#5;
     #2#5 = #25#6;
     #2#6 = #25#7;
     #2#7 = #25#8;
     #2#8 = #25#9;
     |SI FSalida = 0;
          |SI #0#1 = "S"; |GRABA 020#2a; |FINSI;
     |SINO;
          |GRABA 020#2n;
     |FINSI;
     |LIBERA #2;
|FPRC;

|DEFBUCLE PreCali;
     #25#1;
     ;
     aCod1, aCod2, "      ";
     aCod1, aCod2, "zzzzzz";
     ;
     NULL, PreCali;
|FIN;

|PROCESO Tallas;
     |DDEFECTO #2;
     #2#0 = #1#0;
     #2#1 = #914#0;
     |LEE 101#2=;
     #2#2 = #914#1;
     |SI nSwPre = 1;
          #2#3 = nPre2;
          #2#4 = nPre2;
          #2#5 = nPre2;
          #2#6 = nPre2;
          #2#7 = nPre2;
          #2#8 = nPre2;
     |SINO;
          #2#3 = nPre3;
          #2#4 = nPre3;
          #2#5 = nPre3;
          #2#6 = nPre3;
          #2#7 = nPre3;
          #2#8 = nPre3;
     |FINSI;
     |SI FSalida = 0;
          |SI #0#1 = "S"; |GRABA 020#2a; |FINSI;
     |SINO;
          |GRABA 020#2n;
     |FINSI;
     |LIBERA #2;
|FPRC;

|DEFBUCLE Tallas;
     #914#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, Tallas;
|FIN;

|PROCESO dsm00016;
     |DDEFECTO #904;
     #904#0 = aCod;
     #904#1 = 1;
     #904#2 = #916#3;
     |LEE 101#904=;
     #904#3 = aDes;
     |SI FSalida = 0;
          |SI #0#1 = "S"; |GRABA 020#904a; |FINSI;
     |SINO;
          |GRABA 020#904n;
     |FINSI;
     |LIBERA #904;
|FPRC;

|DEFBUCLE dsm00015;
     #915#1;
     ;
     aCod1, aDesde, aDesde, aDesde;
     aCod1, aHasta, aHasta, aHasta;
     ;
     NULL, NULL, dsm00016;
|FIN;

|PROCESO Descripcion;
     nSw = 1;
     aDes = "";
     |SI #0#6 = 0; aDes1 = ""; |SINO; nPosi = #0#6 + 100; aDes1 = (aDes1 + aBlanco) % nPosi; |FINSI;
     |SI #0#7 = 0; aDes2 = ""; |SINO; nPosi = #0#7 + 100; aDes2 = (aDes2 + aBlanco) % nPosi; |FINSI;
     |SI #0#8 = 0; aDes3 = ""; |SINO; nPosi = #0#8 + 100; aDes3 = (aDes3 + aBlanco) % nPosi; |FINSI;
     |SI #0#9 = 0; aDes4 = ""; |SINO; nPosi = #0#9 + 100; aDes4 = (aDes4 + aBlanco) % nPosi; |FINSI;
     |SI #0#19 = "S";
          |QBF aDes1;
          |QBF aDes2;
          |QBF aDes3;
          |QBF aDes4;
     |FINSI;
     |PARA; j = 2; |SI j [ 5; |HACIENDO j = j + 1;
          |PARA; i = 1; |SI i [ 4; |HACIENDO i = i + 1;
               |SI #0j = i;
                    |SI aDes = ""; aEspacio = ""; |SINO; aEspacio = " "; |FINSI;
                    |SI i = 1; aDes = aDes + aEspacio + aDes1; |FINSI;
                    |SI i = 2; aDes = aDes + aEspacio + aDes2; |FINSI;
                    |SI i = 3; aDes = aDes + aEspacio + aDes3; |FINSI;
                    |SI i = 4; aDes = aDes + aEspacio + aDes4; |FINSI;
               |FINSI;
          |SIGUE;
     |SIGUE;
     |SI #0#6 = 0; aCod1 = ""; |SINO; nPosi = 100 + #910#4; aCod1 = aCod1 % nPosi; |FINSI;
     |SI #0#7 = 0; aCod1 = ""; |SINO; nPosi = 100 + #910#8; aCod2 = aCod2 % nPosi; |FINSI;
     |SI #0#8 = 0; aCod1 = ""; |SINO; nPosi = 100 + #910#12; aCod3 = aCod3 % nPosi; |FINSI;
     |SI #0#9 = 0; aCod1 = ""; |SINO; nPosi = 100 + #910#16; aCod4 = aCod4 % nPosi; |FINSI;

     aCod = aCod1 + aCod2 + aCod3 + aCod4;

     |DDEFECTO #19;
     #19#0 = aCod;
     |LEE 101#19=;
     nSal19 = FSalida;
     #19#1 = aDes;
     #19#125 = #0#10; ||Linea
     #19#2   = #0#11; ||Familia
     #19#3   = #0#12; ||Subfam
     #19#126 = #0#13; ||Marca
     #19#134 = #0#14; ||Art Prv
     #19#27  = #0#15; ||Prv
     #19#30  = #0#16; ||IVA
     #19#130 = #0#17; ||IVA S/N
     #19#131 = #0#17; ||IVA S/N
     #19#132 = #0#17; ||IVA S/N
     #19#153 = #0#17; ||IVA S/N
     #19#154 = #0#17; ||IVA S/N
     #19#155 = #0#17; ||IVA S/N
     nTabla = #0#21;
     #19#175 = "S";
     #19#176 = #910#19;
     |HAZ CodigoBarra;

     #29#1 = 1;
     |LEE 000#29=;

     |DDEFECTO #17;
     #17#0 = aCod;
     #17#1 = 1;
     |LEE 101#17=;
     #17#40 = aDes;
     #17#41 = #29#1;
     |SI FSalida = 0;
          |SI #0#1 = "S"; |GRABA 020#17a; |FINSI;
     |SINO;
          |GRABA 020#17n;
     |FINSI;
     |LIBERA #17;

     |SI #0#22 = "S";
          aDesde = " " * 20;
          aHasta = "z" * 20;
          |BUCLE dsm00015;

          |SI nSwPre ! 0;
               |DDEFECTO #1;
               #1#0 = #19#0;
               |LEE 101#1=;
               nSal1 = FSalida;
               #1#1 = #19#1;

               eAlfa = #1#0;
               |SUB_EJECUTA_NP "dsz99990;V";
               #1#2 = eAlfa1;
               #71#0 = #1#2;
               |LEE 000#71=;
               |SI FSalida ! 0; |DDEFECTO #71; |FINSI;
               #1#3 = #71#1;

               #1#4 = eAlfa2;
               #72#0 = #1#4;
               |LEE 000#72=;
               |SI FSalida ! 0; |DDEFECTO #72; |FINSI;
               #1#5 = #72#1;

               #1#6 = eAlfa3;
               #73#0 = #1#6;
               |LEE 000#73=;
               |SI FSalida ! 0; |DDEFECTO #73; |FINSI;
               #1#7 = #73#1;

               |SI nSal1 = 0;
                    |SI #0#1 = "S"; |GRABA 020#1a; |FINSI;
               |SINO;
                    |GRABA 020#1n;
               |FINSI;
               |LIBERA #1;
               |SI nSwPre23 = 1;
                    |BUCLE PreCali;
                    |ABRE #23;
                    #23#0 = #0#18;
                    |LEE 020#23=
                    |CIERRA #23;
                    nTabla = #23#2;
               |SINO;
                    |SI nSwPre26 = 1;
                         |ABRE #26;
                         #26#0 = #0#18;
                         |LEE 020#26=
                         |CIERRA #26;
                         nTabla = #26#2;
                         |BUCLE PreCaliCol;
                    |SINO;
                         |BUCLE Tallas;
                    |FINSI;
               |FINSI;
          |FINSI;
          #19#177 = ("000" + nTabla) % -103;
     |SINO;    || Sin elementos
          #19#176 = "N";
          #19#177 = "";
          #19#18 = #0#20;
          |SI nSwPre20 ! 0; #19#18 = nPre2; |FINSI;
          |SI nSwPre21 ! 0; #19#18 = nPre3; |FINSI;
     |FINSI;

     |SI nSal19 = 0;
          |SI #0#1 = "S"; |GRABA 020#19a; |FINSI;
     |SINO;
          |GRABA 020#19n;
     |FINSI;
     |LIBERA #19;
|FPRC;

|PROCESO Nivel_4;
     aCod4 = #22#0;
     aDes4 = #22#1;
     |HAZ Descripcion;
|FPRC;

|DEFBUCLE Nivel_4;
     #22#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, Nivel_4;
|FIN;

|PROCESO Nivel_3;
     aCod3 = #21#0;
     aDes3 = #21#1;
     nPre3 = #21#2;
     nSw = 0;
     |BUCLE Nivel_4;
     |SI nSw = 0;
          |HAZ Descripcion;
     |FINSI;
|FPRC;

|DEFBUCLE Nivel_3;
     #21#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, Nivel_3;
|FIN;

|PROCESO Nivel_2;
     aCod2 = #20#0;
     aDes2 = #20#1;
     nPre2 = #20#2;
     nSw = 0;
     |BUCLE Nivel_3;
     |SI nSw = 0;
          |HAZ Descripcion;
     |FINSI;
|FPRC;

|DEFBUCLE Nivel_2;
     #20#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, Nivel_2;
|FIN;

|PROCESO Tipo3;
     aAlfa = "";
     |ABRE #71;
     #71#0 = #0#18;
     |LEE 000#71=;
     |SI FSalida ! 0;
          aAlfa = "0000Codigo Nivel_1 no definido...";
     |SINO;
          |SI #910#8 ! 0;
               |ABRE #20;
               |LEE 000#20p;
               |SI FSalida ! 0; aAlfa = "0000Codigos Nivel_2 no definidos"; |FINSI;
               |CIERRA #20;
               |SI #0#21 = 0;
                    |ABRE #23;
                    #23#0 = #0#18;
                    |LEE 000#23=;
                    |SI FSalida = 0; aAlfa = ""; |FINSI;
                    |CIERRA #23;
                    |ABRE #26;
                    #26#0 = #0#18;
                    |LEE 000#26=;
                    |SI FSalida = 0; aAlfa = ""; |FINSI;
                    |CIERRA #26;
               |FINSI;
          |FINSI;
          |SI #910#12 ! 0;
               |ABRE #21;
               |LEE 000#21p;
               |SI FSalida ! 0; aAlfa = "0000Codigos Nivel_3 no definidos"; |FINSI;
               |CIERRA #21;
               |SI #0#21 = 0;
                    |ABRE #23;
                    #23#0 = #0#18;
                    |LEE 000#23=;
                    |SI FSalida = 0; aAlfa = ""; |FINSI;
                    |CIERRA #23;
                    |ABRE #26;
                    #26#0 = #0#18;
                    |LEE 000#26=;
                    |SI FSalida = 0; aAlfa = ""; |FINSI;
                    |CIERRA #26;
               |FINSI;
          |FINSI;
          |SI #910#16 ! 0;
               |ABRE #22;
               |LEE 000#22p;
               |SI FSalida ! 0; aAlfa = "0000Codigos Nivel_4 no definidos"; |FINSI;
               |CIERRA #22;
          |FINSI;
     |FINSI;
     |CIERRA #71;

     |HAZ TienePre;
     nSwPre = nSwPre20 + nSwPre21 + nSwPre23 + nSwPre26;
     |SI nSwPre > 1;
          aAlfa = "0000ERROR, se ha indicado mas de una tabla con precio...";
     |FINSI;

     |SI aAlfa ! "";
          |MENAV aAlfa;
     |SINO;
          |SI nSwPre = 0; |Y #0#20 = 0;
               aAlfa = "0000NNo se ha indicado ningun precio. Continuar S/N:";
               |CONFI aAlfa;
               |SI FSalida ! 0; |ACABA; |FINSI;
          |FINSI;

          |ABRE #1;
          |ABRE #2;
          |ABRE #17;
          |ABRE #19;
          |ABRE #29;
          |ABRE #71;
          |ABRE #72;
          |ABRE #73;
          |ABRE #904;
          |ABRE #915;
          aBlanco = " " * 20;
          aCod1 = #71#0;
          aDes1 = #71#1;
          |BUCLE Nivel_2;
          |CIERRA #915;
          |CIERRA #904;
          |CIERRA #73;
          |CIERRA #72;
          |CIERRA #71;
          |CIERRA #29;
          |CIERRA #19;
          |CIERRA #17;
          |CIERRA #2;
          |CIERRA #1;
     |FINSI;
|FPRC;
컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
|PROCESO Caracter20; |TIPO 0;
     |SI #910#5 ! "N";  |ACABA;  |FINSI;

     eAlfa = #20Campo;  |QBF eAlfa;  |SUB_EJECUTA_NP "dsz99990;C";
     |SI enSwMalo = 1;
         |MENAV "     Introduccion Erronea";
         |ERROR;
     |FINSI;
|FPRC;

|PROCESO Caracter21; |TIPO 0;
     |SI #910#5 ! "N";  |ACABA;  |FINSI;

     eAlfa = #21Campo;  |QBF eAlfa;  |SUB_EJECUTA_NP "dsz99990;C";
     |SI enSwMalo = 1;
         |MENAV "     Introduccion Erronea";
         |ERROR;
     |FINSI;
|FPRC;

|PROCESO Caracter22; |TIPO 0;
     |SI #910#5 ! "N";  |ACABA;  |FINSI;

     eAlfa = #22Campo;  |QBF eAlfa;  |SUB_EJECUTA_NP "dsz99990;C";
     |SI enSwMalo = 1;
         |MENAV "     Introduccion Erronea";
         |ERROR;
     |FINSI;
|FPRC;

|PROCESO Min;
     #20#0 = " " * 20;
     #21#0 = " " * 20;
     #22#0 = " " * 20;
     #20#3 = #0#18;
     #21#3 = #0#18;
     #22#2 = #0#18;
|FPRC;

|PROCESO Max;
     #20#0 = "z" * 20;
     #21#0 = "z" * 20;
     #22#0 = "z" * 20;
     #20#3 = #0#18;
     #21#3 = #0#18;
     #22#2 = #0#18;
|FPRC;

|PROCESO EstadoBoton;
     |ESTADO_CONTROL nBoton1, aEstado;
     |ESTADO_CONTROL nBoton2, aEstado;
     |ESTADO_CONTROL nBoton3, aEstado;
|FPRC;

|PROCESO Pre21;
     |SI #21#2 ! 0;
          nSwPre21 = 1;
     |FINSI;
|FPRC;

|DEFBUCLE Pre21;
     #21#1;
     ;
     #0#18, "                    ";
     #0#18, "zzzzzzzzzzzzzzzzzzzz";
     ;
     NULL, Pre21;
|FIN;

|PROCESO Pre20;
     |SI #20#2 ! 0;
          nSwPre20 = 1;
     |FINSI;
|FPRC;

|DEFBUCLE Pre20;
     #20#1;
     ;
     #0#18, "                    ";
     #0#18, "zzzzzzzzzzzzzzzzzzzz";
     ;
     NULL, Pre20;
|FIN;

|PROCESO TienePre;
     nSwPre20 = 0; nSwPre21 = 0; nSwPre23 = 0; nSwPre26 = 0;
     |BUCLE Pre20;
     |BUCLE Pre21;
     |SI #0#21 = 0;
          |ABRE #23;
          #23#0 = #0#18;
          |LEE 000#23=;
          |SI FSalida = 0; nSwPre23 = 1; |FINSI;
          |CIERRA #23;
          |ABRE #26;
          #26#0 = #0#18;
          |LEE 000#26=;
          |SI FSalida = 0; nSwPre26 = 1; |FINSI;
          |CIERRA #26;
     |FINSI;
|FPRC;

|PROCESO Pvp7; |TIPO 7;
     |HAZ TienePre;
     |SI nSwPre20 = 0; |Y nSwPre21 = 0; |Y nSwPre23 = 0; |Y nSwPre26 = 0;
          |C_M #0#20, 1, "S";
     |SINO;
          #0#20 = 0; |PINTA #0#20;
          |C_M #0#20, 1, "N";
     |FINSI;
|FPRC;

|PROCESO Tecla; |TIPO 6;
     |SI FSalida = 13;
          |HAZ TienePre;
          |SI nSwPre20 = 0; |Y nSwPre21 = 0; |Y nSwPre26 = 0;
               eaModelo = #0#18; |SUB_EJECUTA_NP "evarz023";
          |SINO;
               |MENAV "0000Ya se han definido precios en otro nivel...";
          |FINSI;
          |ERROR; |ACABA;
     |FINSI;
     |SI FSalida = 14;
          |HAZ TienePre;
          |SI nSwPre20 = 0; |Y nSwPre21 = 0; |Y nSwPre23 = 0;
               eaModelo = #0#18; |SUB_EJECUTA_NP "evarz026";
          |SINO;
               |MENAV "0000Ya se han definido precios en otro nivel...";
          |FINSI;
          |ERROR; |ACABA;
     |FINSI;

     |SI FSalida ! 10; |Y FSalida ! 11; |Y FSalida ! 12; |ACABA; |FINSI;
     |SI #910#8 = 0; |Y FSalida = 10; |ACABA; |FINSI;
     |SI #910#12 = 0; |Y FSalida = 11; |ACABA; |FINSI;
     |SI #910#16 = 0; |Y FSalida = 12; |ACABA; |FINSI;

     aEstado = "DISABLE"; |HAZ EstadoBoton;
     |PUSHV 0501 2380;
     aAlfa = "";
     |SI FSalida = 10;
          |HAZ TienePre;
          |SI nSwPre21 = 0; |Y nSwPre23 = 0; |Y nSwPre26 = 0;
               |C_M #20#2, 1, "S";
          |SINO;
               |C_M #20#2, 1, "N";
          |FINSI;
          |PINPA #0#20;
          nPosi = 1639 + #910#8;
          |C_P #20#1, 1, nPosi; nPosi = nPosi -200 + 9;
          |ATRI R; |PINTA 1439, #910#7; |PINTA nPosi, "Descripcion      Precio"; |ATRI 0;
          nPosi = 1639 + #910#12 + 21; |C_P #20#2, 1, nPosi;
          |ENTLINEAL #1#20, -1, 3, 22, 0, Min, Max;
     |FINSI;
     |SI FSalida = 11;
          |HAZ TienePre;
          |SI nSwPre20 = 0; |Y nSwPre23 = 0; |Y nSwPre26 = 0;
               |C_M #21#2, 1, "S";
          |SINO;
               |C_M #21#2, 1, "N";
          |FINSI;
          |PINPA #0#21;
          nPosi = 1639 + #910#12;
          |C_P #21#1, 1, nPosi; nPosi = nPosi -200 + 9;
          |ATRI R; |PINTA 1439, #910#11; |PINTA nPosi, "Descripcion      Precio"; |ATRI 0;
          nPosi = 1639 + #910#12 + 21; |C_P #21#2, 1, nPosi;
          |ENTLINEAL #1#21, -1, 3, 22, 0, Min, Max;
     |FINSI;

     |SI FSalida = 12;
          |PINPA #0#22;
          nPosi = 1639 + #910#16;
          |C_P #22#1, 1, nPosi; nPosi = nPosi -200 + 9;
          |ATRI R; |PINTA 1439, #910#15; |PINTA nPosi, "Descripcion"; |ATRI 0;
          |ENTLINEAL #1#22, -1, 3, 22, 0, Min, Max;
     |FINSI;

     |POPV;
     aEstado = "ENABLE"; |HAZ EstadoBoton;
     |ERROR;
|FPRC;

|PROCESO PulsaF2;
     |PTEC 824;
|FPRC;
|PROCESO PulsaF3;
     |PTEC 825;
|FPRC;
|PROCESO PulsaF4;
     |PTEC 826;
|FPRC;

|PROCESO Caracter19;  |TIPO 0;
     |SI #910#5 ! "N";  |ACABA;  |FINSI;

     eAlfa = #0Campo;  |QBF eAlfa;  |SUB_EJECUTA_NP "dsz99990;C";
     |SI enSwMalo = 1;
         |MENAV "     Introduccion Erronea";
         |ERROR;
     |FINSI;
|FPRC;

|PROCESO IVA019_66; |TIPO 66;
     enTiva = #0Campo;
     efFiva = ~;
     |HAZ ConsuIVA;
     |SI eaDiva = "";
          |ERROR;
     |SINO;
          #0Campo = enTiva; |PINTA #0Campo;
     |FINSI;
|FPRC;

|PROCESO IVA019_0; |TIPO 0;
     enTiva = #0Campo;
     efFiva = ~;
     |HAZ SacaIVA;
     |SI eaDiva = "";
          |MENAV "0000No existe tipo iva para esta fecha...";
          |ERROR; |ACABA;
     |FINSI;
|FPRC;

|PROCESO Btn1;
     |PTEC 827;
|FPRC;

|PROCESO Btn2;
     |PTEC 828;
|FPRC;

|PROGRAMA;
     |HAZ CreaTempo; |NOME_DAT #20#Tempo#; eaTmp1 = Tempo; |DELINDEX #20;
     |HAZ CreaTempo; |NOME_DAT #21#Tempo#; eaTmp2 = Tempo; |DELINDEX #21;
     |HAZ CreaTempo; |NOME_DAT #22#Tempo#; eaTmp3 = Tempo; |DELINDEX #22;

     |HAZ CreaTempo; |NOME_DAT #23#Tempo#; eaTmp4 = Tempo; |DELINDEX #23;
     |HAZ CreaTempo; |NOME_DAT #24#Tempo#; eaTmp5 = Tempo; |DELINDEX #24;
     |HAZ CreaTempo; |NOME_DAT #25#Tempo#; eaTmp6 = Tempo; |DELINDEX #25;
     |HAZ CreaTempo; |NOME_DAT #26#Tempo#; eaTmp7 = Tempo; |DELINDEX #26;
     |HAZ CreaTempo; |NOME_DAT #27#Tempo#; eaTmp8 = Tempo; |DELINDEX #27;
     |HAZ CreaTempo; |NOME_DAT #28#Tempo#; eaTmp9 = Tempo; |DELINDEX #28;

     |ABRE #910; |LEE 040#910p; |CIERRA #910;
     |ABRE #321; |LEE 000#321p; |CIERRA #321;
     |ABRE #324;
     #324#0 = #321#9;
     |LEE 000#324=;
     |SI FSalida ! 0;  |DDEFECTO #324;  |FINSI;
     |CIERRA #324;
     nDeci_PreBase = #324#9;

     |CLS;

     |SI #910#8 = 0; |C_V #0#3, 1, "N"; |C_V #0#7, 1, "N"; |FINSI;
     |SI #910#12 = 0; |C_V #0#4, 1, "N"; |C_V #0#8, 1, "N"; |FINSI;
     |SI #910#16 = 0; |C_V #0#5, 1, "N"; |C_V #0#9, 1, "N"; |FINSI;

     nNume = #910#4;
     aAlfa = "\" + #910#6 + "+#";
     |CAMPO_ANCHO  #0#18, 1, nNume;
     |CAMPO_MAXIMO #0#18, 1, aAlfa;

     nNume = #910#8;
     aAlfa = "\" + #910#10 + "+#";
     |CAMPO_ANCHO  #20#0, 1, nNume;
     |CAMPO_MAXIMO #20#0, 1, aAlfa;

     nNume = #910#12;
     aAlfa = "\" + #910#14 + "+#";
     |CAMPO_ANCHO  #21#0, 1, nNume;
     |CAMPO_MAXIMO #21#0, 1, aAlfa;

     nNume = #910#16;
     aAlfa = "\" + #910#18 + "+#";
     |CAMPO_ANCHO  #22#0, 1, nNume;
     |CAMPO_MAXIMO #22#0, 1, aAlfa;

     |ABRE #0;
     |LEE 101#0p;
     |SI FSalida ! 0; |GRABA 020#0b; |FINSI;

     #0#20 = 0;
     #0#22 = #910#19;

     #0#18 = "";
     #0#2 = #910#29;
     #0#3 = #910#30;
     #0#4 = #910#31;
     #0#5 = #910#32;

     aTitulo1 = "LBOTON,F2. Edita " + #910#7;  |QBF aTitulo1;
     aTitulo2 = "LBOTON,F3. Edita " + #910#11; |QBF aTitulo2;
     aTitulo3 = "LBOTON,F4. Edita " + #910#15; |QBF aTitulo3;

     |CONTROL_SIMPLE 0, aTitulo1, 1047, 1060, PulsaF2; nBoton1 = FSalida;
     |CONTROL_SIMPLE 0, aTitulo2, 1147, 1160, PulsaF3; nBoton2 = FSalida;
     |CONTROL_SIMPLE 0, aTitulo3, 1247, 1260, PulsaF4; nBoton3 = FSalida;

     |PINPA #0#0;
     |PINDA #0#0;

     |SI #910#8 = 0;
          |ESTADO_CONTROL nBoton1, "HIDE";
     |FINSI;
     |SI #910#12 = 0;
          |ESTADO_CONTROL nBoton2, "HIDE";
     |FINSI;
     |SI #910#16 = 0;
          |ESTADO_CONTROL nBoton3, "HIDE";
     |FINSI;

     |CONTROL_SIMPLE 0, "BOTON,{{000}}F5. Precio", 1062, 1070, Btn1; nBtn1 = FSalida;
     |CONTROL_SIMPLE 0, "BOTON,{{000}}F6. Precio", 1162, 1170, Btn2; nBtn2 = FSalida;
     |ESTADO_CONTROL nBtn1, "DISABLE";
     |ESTADO_CONTROL nBtn2, "DISABLE";

     aAlfa = #910#3;
     |PINTA 0714, aAlfa
     |ENDATOS #2#0;
     |SI FSalida = 0;
          |HAZ Tipo3;
          |GRABA 020#0a;
     |FINSI;
     |CIERRA #0;
     |FIN_CONTROL_WINDOWS nBoton3;
     |FIN_CONTROL_WINDOWS nBoton2;
     |FIN_CONTROL_WINDOWS nBoton1;
     |FIN_CONTROL_WINDOWS nBtn1;
     |FIN_CONTROL_WINDOWS nBtn2;
     |PULSATECLA;

     Tempo = eaTmp1; |HAZ BoraTempo; |DELINDEX #20;
     Tempo = eaTmp2; |HAZ BoraTempo; |DELINDEX #21;
     Tempo = eaTmp3; |HAZ BoraTempo; |DELINDEX #22;
     Tempo = eaTmp4; |HAZ BoraTempo; |DELINDEX #23;
     Tempo = eaTmp5; |HAZ BoraTempo; |DELINDEX #24;
     Tempo = eaTmp6; |HAZ BoraTempo; |DELINDEX #25;
     Tempo = eaTmp7; |HAZ BoraTempo; |DELINDEX #26;
     Tempo = eaTmp8; |HAZ BoraTempo; |DELINDEX #27;
     Tempo = eaTmp9; |HAZ BoraTempo; |DELINDEX #28;
|FPRO;
