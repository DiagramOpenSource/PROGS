     Copiado de star3/n43recep

|FICHEROS;
     pcegz049 #0;
     pcegm035 #4;  || n43datos
     pcegw011 #11; || n43infor
     pcegw010 #44; || n43auxil
|FIN;

|VARIABLES;
     aTipo = "";
     &Tempo = "";
     aTmp11 = "";
     aTmp44 = "";
     nBoton = 0;

    aFichero = "";
    fiche    = "";
    handle   = 0;

    aEl10    = "";
    aEl13    = "";
    aAlfa  = "";
    aAlfa1 = "";
    aAlfa2 = "";

    nCalc  = 0;
    n_Conta = 0;
    n_salan = 0;
    n_sigsa = "";
    dfec = "01.01.0000";
    dia  = 0;
    hfec = @;
    sali = 0;
    salf = 0;
    nSal = 0;
    descon  = "";
    sistema = "";
    fich    = "";
    alfa = "";
    tipo = "";
    vic  = 0;
    salida = 0;
    unida = "";
    banco = "";            || clave 11
    oficina = "";
    cuenta = "";
    fecini = "";
    fecfin = "";
    dhcta = "";
    a_saldo = "";
    descrip = "";

    fecope = "";           || clave 22
    d_abajo = 0;
    h_abajo = 0;
    fecval = "";
    concep = "";
    dhmov = "";
    a_importe = "";
    documen = "";
    n_Regd  = 0;
    n_Regr  = 0;
    concep1 = "";          || clave 23
    concep2 = "";

    dhfinal = "";
    a_slfinal = "";


    banco_t = "";
    ofici_t = "";
    cuent_t = "";
    dhfin_t = "";
    contable = "";
    digitoco = 0;

    tranum1 = 0;
    tranum2 = 0;
    traalf1 = "";
    traalf2 = "";
    traalf3 = "";
    fectra = "";
    jn = 0;
   f = "";
   espacio = " ";
   menos = -1;
   reg = 0;
   saldo_ini1 = "";
   saldo_ini2 = 0;
       valida = "0000NContinuar S/N "
        swini = 0;
      salcued = 0;
      salcueh = 0;
       salcue = 0;
   dhsald_ini = "";
      importe = 0;
     importe2 = 0;
     ultimo = 0;
     sw_eof = 0;
     swcuenta = 0;
     totreg_t = 0;
     saldo = 0;
     apund_t = 0;
     impod_t = 0;
     apunh_t = 0;
     impoh_t = 0;
     slfin_t = 0;
     apunh = 0;
     impoh = 0;
     apund = 0;
     impod = 0;
     slfinal = 0;
     totreg = 0;
     aAn    = "";
     nVic   = 0;

     aMas    = "";
     aDef    = "";
     nSwDigo = 0;
     sw      = 0;
     nReg = 0;
|FIN;

|PROCESO n43auxil;
     |SI #44#4 = #0#1;
          aAlfa = #44#1;
          aAlfa = aAlfa + #44#2;
          aAlfa = aAlfa + #44#3;
          aAlfa = aAlfa + (("000" + #44#4) % -103);
          aAlfa = aAlfa + #44#5;
          aAlfa = aAlfa + #44#6;
          aAlfa = aAlfa + (("000000000000" + #44#7) % -112);
          aAlfa = aAlfa + #44#8;
          aAlfa = aAlfa + #44#9;
          aAlfa = aAlfa + #44#10;
          aAlfa = aAlfa + #44#11;
          aAlfa = aAlfa + #44#12;

          |SELECT #4#4;
          #4#13 = aAlfa;
          |LEE 000#4=;
          |SI FSalida = 0; |ACABA; |FINSI;

          |DDEFECTO #4;
          |SELECT #1#4;

          ultimo = ultimo + 1;
          #4#00 = ultimo;
          #4#01 = #44#1;
          #4#02 = #44#2;
          #4#03 = #44#3;
          #4#04 = #44#4;
          #4#05 = #44#5;
          #4#06 = #44#6;
          #4#07 = #44#7;
          #4#08 = #44#8;
          #4#09 = "N";
          #4#10 = #44#10;
          #4#11 = "N";
          #4#12 = #44#12;
          #4#13 = aAlfa;
          #4#17 = #44#14;
          |OEM_ANSI #4#5, #4#5;
          |OEM_ANSI #4#10, #4#10;
          |GRABA 020#4n;
     |FINSI;
|FPRC;

|DEFBUCLE n43auxil;
     #44#2;
     ;
     #11#3,#11#20,1
     #11#3,#11#20,999999;
     ;
     NULL,n43auxil;
|FIN;

|PROCESO n43aux;
     |BUCLE n43auxil;
|FPRC;

|DEFBUCLE n43infor;
     #11#1;
     ;
     "          ",  1;
     "zzzzzzzzzz",999;
     ;
     NULL,n43aux;
|FIN;

|PROCESO carga;
     |ABRE #4;
     ultimo = 0;
     |LEE 040#4u;
     |SI FSalida ! 0;
          ultimo = 0;
     |SINO;
          ultimo = #4#0;
     |FINSI;
     |BUCLE n43infor;
     |CIERRA #4;
|FPRC;

|CALCULO cambia_fec;
    traalf1 = fectra % 102;
    traalf2 = fectra % 302;
    traalf3 = fectra % 502;
    nVic    = traalf3;
    |SI nVic ] 90;
         aAn = ".19";
    |SINO;
         aAn = ".20";
    |FINSI;
    fectra  = traalf3 + "." + traalf2 + aAn + traalf1;
|FCAL;

|PROCESO n43datos;
  n_Regr = n_Regr + 1;
  |SI #4#6 = "D";
      apund_t = apund_t + #4#7;
  |FINSI;
  |SI #4#6 = "H";
      apunh_t = apunh_t + #4#7;
  |FINSI;
|FPRC;

|DEFBUCLE n43datos;
     #4#2;
     2;
     #11#3,000001,#11#5;
     #11#3,999999,#11#6;
     ;
     NULL,n43datos;
|FIN;

|PROCESO importado;
     apund_t = 0;
     apunh_t = 0;
     n_Regr  = 0;
     |BUCLE n43datos;
|FPRC;

|PROCESO sacasal;
     n_salan = 0;
     n_sigsa = "";
|FPRC;

|PROCESO extracto;
     d_abajo = 0;
     h_abajo = 0;
     dia = #11#5;
     dia = dia -1;
     hfec = dia;
|FPRC;

|CALCULO tipo_11;
    banco   = alfa % 0304;
    oficina = alfa % 0704;
    cuenta  = alfa % 1110;
    aAlfa   = banco + oficina;

    contable = "";      || no ha encontrado la cta contable de esta
    digitoco = 0;       || cuenta corriente del banco

    fecini  = alfa % 2106; fectra = fecini; |HAZ cambia_fec; fecini=fectra;
    fecfin  = alfa % 2706; fectra = fecfin; |HAZ cambia_fec; fecfin=fectra;
    dhcta   = alfa % 3301;
    a_saldo = alfa % 3414; saldo = a_saldo; saldo = saldo / 100;
    descrip = alfa % 5226;
    |SI dhcta = "1";  dhcta = "H";  |FINSI;
    |SI dhcta = "2";  dhcta = "D";  |FINSI;
    ||____________________ Comprobacion Saldo.

    nSwDigo = 0;
    nCalc   = 0;
    impod_t = 0;
    impoh_t = 0;
    n_Regd  = 0;
    n_Conta = n_Conta + 1;

    |SI sw_eof = 0; sw = 1; |FINSI;
|FCAL;

|CALCULO tipo_22;
    fecope  = alfa % 1106;  fectra = fecope; |HAZ cambia_fec; fecope = fectra;
    fecval  = alfa % 1706;  fectra = fecval; |HAZ cambia_fec; fecval = fectra;
    concep  = alfa % 2503;
    descon  = alfa % 5328;  || Descripcion Posible Banco.
    dhmov   = alfa % 2801;
    a_importe=alfa % 2914;  importe = a_importe; importe2 = importe / 100;
    documen = alfa % 4310;
    |SI dhmov = "1";  dhmov = "H";  |FINSI;
    |SI dhmov = "2";  dhmov = "D";  |FINSI;

    |SI dhmov = "D";
       impod_t = impod_t + importe;
       |SI dhcta = "D";
           slfin_t = slfin_t + importe;
       |SINO;
           slfin_t = slfin_t - importe;
       |FINSI;
   |FINSI;
   |SI dhmov = "H";
       impoh_t = impoh_t + importe;
       |SI dhcta = "H";
           slfin_t = slfin_t + importe;
       |SINO;
           slfin_t = slfin_t - importe;
       |FINSI;
   |FINSI;

  |SI dhmov = "D"; |O dhmov = "H";
       n_Regd = n_Regd + 1;
       ultimo = ultimo + 1;
       #44#00 = ultimo;
       #44#01 = contable;
       #44#12 = digitoco;
       #44#02 = fecope;
       #44#03 = fecval;
       #44#04 = concep;
       #44#05 = descon;
       #44#06 = dhmov;
       #44#07 = importe2;
       #44#08 = documen;
       #44#09 = "N";
       #44#10 = descrip;
       #44#13 = n_Conta;
       |GRABA 020#44n;
   |FINSI;
|FCAL;

|CALCULO tipo_23;
     |SI aTipo = "2301"; |O aTipo = "2303";
          concep1 = alfa % 0538; |QBF concep1;
          concep2 = alfa % 4338;
          |SI ultimo > 0;
               #44#00 = ultimo;
               |LEE 001#44=;
               |SI FSalida = 0;
                    |SI aTipo = "2301";
                         #44#05 = concep1 + " " + concep2;
                    |SINO;
                         #44#14 = concep1 + " " + concep2;
                    |FINSI;
                    |GRABA 020#44a;
               |FINSI;
          |FINSI;
     |FINSI;
|FCAL;

|CALCULO tipo_33;
    dhfinal = alfa % 5901;
    a_slfinal=alfa % 6014;  slfinal = a_slfinal;  slfinal = slfinal / 100;

    |SI dhfinal = "1";  dhfinal = "H";  |FINSI;
    |SI dhfinal = "2";  dhfinal = "D";  |FINSI;

    #11#0 = cuenta;
    #11#20 = n_Conta;
    |LEE 101#11=;
    nSal = FSalida;
    #11#1 = banco;
    #11#2 = oficina;
    #11#3 = contable;
    #11#4 = digitoco;
    #11#5 = fecini;
    #11#6 = fecfin;
    #11#7 = dhcta;
    #11#8 = saldo;
    #11#9 = dhfinal;
    #11#10 = slfinal;
    #11#12 = "";
    #11#14 = n_Regd;
    #11#13 = impod_t - impoh_t;
    #11#13 = #11#13 / 100;
    |HAZ importado;
    #11#15 = n_Regr;
    #11#11 = apund_t - apunh_t;
    |HAZ extracto;
    #11#16 = d_abajo - h_abajo;
    |SI #11#16 < 0;
          #11#16 = #11#16 * -1;
          #11#17 = "H";
    |SINO;
          #11#17 = "D";
    |FINSI;
    |HAZ sacasal;
    #11#18 = n_salan;
    #11#19 = n_sigsa;
    |SI nSal = 0; |GRABA 020#11a; |FINSI;
    |SI nSal ! 0; |GRABA 020#11n; |FINSI;
    |LIBERA #11;
|FCAL;

|PROCESO PasoRegistro;
     tipo = alfa % 102;
     aTipo = alfa % 104;
     |SI tipo = "11"; |HAZ tipo_11;  |FINSI;
     |SI tipo = "22"; |HAZ tipo_22;  |FINSI;
     |SI tipo = "23"; |HAZ tipo_23;  |FINSI;
     |SI tipo = "33"; |HAZ tipo_33;  |FINSI;
|FPRC;

|PROCESO Tipo3; |TIPO 3;
     |HAZ CreaTempo; |NOME_DAT #44 Tempo; |DELINDEX #44; aTmp44 = Tempo;
     |HAZ CreaTempo; |NOME_DAT #11 Tempo; |DELINDEX #11; aTmp11 = Tempo;

     |ABRE #11;
     |ABRE #44;

     nReg = 0;
     aEl13 = &13;
     aEl10 = &10;
     fiche = #0#0; |QBF fiche;
     |XABRE "C", fiche, handle;
     |SI FSalida ] 0;
         |PARA; |SI; |HACIENDO;
               |XLEE handle, 250, alfa;
               |SI FSalida < 0; |SAL; |FINSI;
               nReg = nReg + 1;
               aAlfa = "Linea " + nReg;
               |PINTA 1407, aAlfa;
               |HAZ PasoRegistro;
         |SIGUE;
         |XCIERRA handle;
     |FINSI;

     |HAZ carga;

     |CIERRA #44;
     |CIERRA #11;


     Tempo = aTmp44; |HAZ BoraTempo; |DELINDEX #44;
     Tempo = aTmp11; |HAZ BoraTempo; |DELINDEX #11;
|FPRC;

|PROCESO Browse; |TIPO
     |SI FSalida ! 10; |ACABA; |FINSI;

     aAlfa = "F*.*";

     |BROWSE aAlfa;

     aAlfa = aAlfa % 2; |QBF aAlfa;
     |SI aAlfa = "F"; aAlfa = ""; |FINSI;

     |QBF aAlfa;
     |SI aAlfa = "";
          |ERROR;
     |SINO;
          #0Campo = aAlfa;
          |PINTA #0Campo;
     |FINSI;
|FPRC;

|PROCESO Boton;
     |PTEC 824;
|FPRC;

|PROGRAMA;
     |CLS;
     |CONTROL_SIMPLE 0, "BOTON,Fichero", 0907, 0914, Boton;
     nBoton = FSalida;

     |PINPA #0#0;
     |PINDA #0#0;
     |ENDATOS #1#0;

     |FIN_CONTROL_WINDOWS nBoton;
     |PULSATECLA;
|FPRO;
