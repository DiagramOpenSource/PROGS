|FICHEROS;
     dsz00083 #0;
     dsm00088 #88; ||Parametros
     agitp001 #12;
     agifa019 #19;
     agifa017 #17;
     dsm00056 #56;
     dsm00057 #57;
     agifa553 #553;  || Linea
     agifa112 #112;
     agifa321 #321;
     dsm00255 #255;
     dsm00256 #256;
     agifa321 #321;
|FIN;

|VARIABLES;
     &enEtiq = 0;
     &Tempo = "";
     nContaVal = 0;
     nContaLinVal = 0;
     aSerie = "";
     fHoy = @;
     nConta = 0;
     aAlfa = "";
     nPos = 0;
     &enImpPrvCom = 0;
     &efFecha = @;
     &eaProve = "";
     pelas = 0;
     &enCodigo = 0;
     fmes = "";
     mes = 0;
     &enForma = 0;
     fecha = @;
     i = 0;
|FIN;

|PROCESO acarpr;
     |DDEFECTO #12;
     #12#0 = #57#5;            || Proveedor.
     #12#1 = #57#0;            || Articulo.
     |LEE 101#12=;
     |SI FSalida ! 0;
          #12#4 = #57#7;           || ultimo descuento de compra
          #12#5 = #57#20;          || ultimo descuento de compra
          #12#10 = #57#30;
          #12#6 = #57#12;          || ultima fecha de compra
          #12#7 = #56#1;           || Descripcion Articulo.
          #12#8 = #57#18;          || Divisa
          #12#9 = #57#6;           || ultimo precio de compra
          #12#3 = #57#19;          || upc en divisa
          |GRABA 020#12n;
      |SINO;
          #12#4 = #57#7;           || ultimo descuento de compra
          #12#5 = #57#20;          || ultimo descuento de compra
          #12#10 = #57#30;          || ultimo descuento de compra
          #12#6 = #57#12;          || ultima fecha de compra
          #12#7 = #56#1;           || Descripcion Articulo.
          #12#8 = #57#18;          || Divisa
          #12#9 = #57#6;           || ultimo precio de compra
          #12#3 = #57#19;          || upc en divisa
          |GRABA 020#12a;
     |FINSI;
     |LIBERA #12;
|FPRC;


|PROCESO Articulo;
     #19#1 = #0#1;
     #19#128 = #0#2;
     #19#18 = #0#3;
     #19#130 = #88#13;
     #19#28 = #0#4;
     #19#133 = #0#4;
     #19#163 = #0#4;
     #19#164 = #0#4;
     #19#125 = #0#5;
     #19#27 = #0#7;
     #19#165 = #321#9;
     |GRABA 020#19a;
|FPRC;

|PROCESO ArtiTactil;
     |DDEFECTO #255;
     #255#0 = #0#0;
     |GRABA 020#255n;
     |SI FSalida = 0;
          |DDEFECTO #256;
          #256#0 = #255#0;
          #256#1 = 1;
          #256#5 = #0#3;
          |GRABA 020#256n;
     |FINSI;
|FPRC;

|PROCESO Valorada;
     |SI nContaVal = 0; |O nContaLinVal > 99999;
          #56#2 = "";     || Serie;
          aSerie = #56#2;
          #56#0 = 999999;
          |LEE 000#56];
          |SI FSalida = 0;
               |LEE 000#56a;
          |SINO;
               |LEE 000#56u;
          |FINSI;
          |SI FSalida = 0; |Y #56#2 = aSerie;
               nContaVal = #56#0 + 1;
          |SINO;
               nContaVal = 1;
          |FINSI;
          |DDEFECTO #56;
          #56#2 = aSerie;
          #56#0 = nContaVal;
          |GRABA 020#56b;
          nContaLinVal = 0;
     |FINSI;

     nContaLinVal = nContaLinVal + 1;
     |DDEFECTO #57;
     #57#28 = #56#2;
     #57#27 = #56#0;
     #57#14 = nContaLinVal;
     |GRABA 020#57b;
     |SI FSalida = 0;
          #57#0 = #0#0;
          #57#1 = 1;     || almacen
          #57#2 = #0#1;
          #57#4 = #0#6;
          #57#5 = #0#7;
          #57#6 = #0#4;
          #57#19 = #0#4;
          #57#11 = #0#4;
          #57#12 = fHoy;
          #57#18 = #321#9;
          #57#15 = "Entrada rapida articulos";
          |GRABA 020#57a;
          |LIBERA #57;

          eaProve = #0#7;
          enCodigo = #57#27;
          enForma = 1;
          |HAZ AcumulaEV;

          |SI #57#4 = 0;
               pelas = 0;
          |SINO;
               |SI #19#196 = 1;
                    pelas = #57#4 * #57#11;
               |SINO;
                    pelas = #57#21 * #57#11;
               |FINSI;
          |FINSI;

          |ABRE #112;
          #112#0 = #0#7;
          |LEE 101#112=;
          |SI FSalida = 0;
               fecha = #57#12;
               fmes = fecha % A402;
               mes = fmes - 1;
               mes = mes + 27;
               #112mes = #112mes +. pelas;
               #112#39 = #112#39 +. pelas;
               |GRABA 020#112a;
               |LIBERA #112;

               enImpPrvCom = pelas;
               efFecha = #57#12;
               eaProve = #57#5;
               |HAZ AcumulaPrv;
          |FINSI;
          |CIERRA #112;
     |FINSI;
|FPRC;

|PROCESO Impresion;
     |CONFI "0000SImprimir Etiquetas";
     |SI FSalida = 0;
          |IMPRE 0;
          |SI FSalida = 0;
               aAlfa = #88#12; |QBF aAlfa;
               |INFOR aAlfa;
               |SI FSalida = 0;
                    |PARA i = 1; |SI i [ #0#6; |HACIENDO i = i + 1;
                         enEtiq = i;
                         |PRINT;
                         |PIEINF;
                    |SIGUE;
                    |FININF;
               |SINO;
                    aAlfa =  "0000Error en informe " + aAlfa;
                    |MENAV aAlfa;
               |FINSI;
               |FINIMP;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Tipo3; |TIPO 3;
     fHoy = ~;
     |HAZ Articulo;
     |HAZ ArtiTactil;
     |HAZ Valorada;
     |HAZ Impresion;
|FPRC;

|PROGRAMA;
     |CLS;

     |HAZ DecimalesBase;

     |ABRE #88; |LEE 000#88p; |CIERRA #88;

     |ABRE #56;
     |ABRE #57;
     |ABRE #255;
     |ABRE #256;
     |ABRE #12;

     |PINPA #0#0;
     |PARA; |SI; |HACIENDO;
          |ABRE #19;
          |LEE 000#19u;
          |SI FSalida = 0;
               nConta = #19#0 + 1;
          |SINO;
               nConta = 1;
          |FINSI;
          |PARA; |SI; |HACIENDO;
               aAlfa = "0" * #88#1;
               nPos = -1 * (#88#1 + 100);
               aAlfa = (aAlfa + nConta) % nPos;
               |DDEFECTO #19;
               #19#0 = aAlfa;
               |GRABA 000#19b;
               |SI FSalida = 0; |SAL; |FINSI;
               nConta = nConta + 1;
          |SIGUE;
          |DDEFECTO #0;
          #0#0 = #19#0;
          |PINDA #0#0;
          |ENDATOS #1#0;
          |SI FSalida ! 0;
               #19#0 = #0#0;
               |BORRA 020#19c; |SAL;
          |FINSI;
          |LIBERA #19;
          |CIERRA #19;
     |SIGUE;
     |CIERRA #12;
     |CIERRA #256;
     |CIERRA #255;
     |CIERRA #57;
     |CIERRA #56;
|FPRO;
