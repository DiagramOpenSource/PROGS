|FICHEROS;
     guiz0004 #0;
     guiw0002 #2,MANTE; || Tmp Adj
     agifa019 #19;
     agifa073 #73;
     agifa104 #104;
     agifa024 #24;
     agifa025 #25;
|FIN;

|VARIABLES;
     &eaTmpGui = "";
     &eaCliente = "";
     &eaRepre = "";
     aAlfa = "";
     nExiste = 0;
     nLin = 0;
     nPend = 0;

     &uni_dt = 0;   || Unidades
     &fed_dt = @;   || Fecha....
     &art_dt = "";  || Articulo.
     &cli_dt = "";  || Cliente..
     &fam_dt = "";  || Familia.
     &iva_dt = 0;   || Tipo Iva.
     &dtos_2 = 0;   || Descuento 2.
     &dto_pt = 0;   || Descuento Ptas.
     &dtos_1 = 0;   || Descuento 1.
     &pvp_ve = 0;   || Precio Venta.
     &tarifa = 0;   || Tarifa Cliente.
     &quedto = 0;   || 1=cli./art, 2=cli./familia, 3=cli./subfam.
     &gru_dt = "";  || Grupo Cliente.
     &nPromo = 0;
     &enMensaje = 0;
     &eaSerie = "";
     &eaDocu = "";
     &enDescuento1 = 0;
     &enDescuento2 = 0;
     &eaTComision  = "";
     &enComision   = 0;
     &eaArticulo = "";
|FIN;

|PROCESO Tipo2; |TIPO 2;
     |DDEFECTO #73;
     #2#7 = #0#0;
     #2#8 = #0#1;

     |ABRE #73;
     #73#28 = #2#7;
     #73#0 = #2#8;
     #73#1 = #2#3;
     |LEE 000#73=;
     |SI FSalida = 0;
          #2#9 = #73#2;
          #2#10 = #73#6;
          #2#11 = #73#8;
          #2#12 = #73#10;
          #2#13 = #73#14;
          #2#14 = #73#29;
     |SINO;
          |ABRE #19; |SELECT #4#19;
          #19#128 = #2#1;
          |LEE 000#19=;
          |CIERRA #19;
          #2#9 = #19#0;
          #2#13 = #19#30;

          fed_dt = #104#1;   || Fecha....
          art_dt = #2#9;   || Articulo.
          cli_dt = eaCliente;  || Cliente..
          fam_dt = #19#2;  || Familia.
          iva_dt = #19#30;  || Tipo Iva.
          dtos_2 = 0;      || Descuento 2.
          dto_pt = 0;      || Descuento Ptas.
          dtos_1 = 0;      || Descuento 1.
          pvp_ve = 0;      || Precio Venta.
          tarifa = #24#39;  || Tarifa Cliente....
          gru_dt = #24#158; || Grupo Cliente.
       |||   nPromo = #1#89;
          uni_dt = #2#2;
          enMensaje = 0;
          eaSerie = #2#7;
          |SI #19#176 = "S";  enMensaje = 1;  |FINSI;
          eaDocu = "agifa071";
          |HAZ calcdtos;
          #2#10  = pvp_ve - dto_pt;
          #2#11  = dtos_1;
          #2#14 = dtos_2;

         enDescuento1 = #2#11;
         enDescuento2 = #2#14;
         eaTComision  = #25#7;
         ||eaCliente
         eaArticulo   = #2#9;
         ||eaRepre

         |HAZ Comisiones;
         #2#12 = enComision;
     |FINSI;
     |CIERRA #73;
|FPRC;

|PROCESO CheqUni; |TIPO 0;
     |SI #2#3 = 0;
          |ACABA;
     |FINSI;

     #2#5 = "N";
     |SI #2#2 >  #2#6;
          aAlfa = "0000NSe superan las unidades pendientes:" + #2#6 +"."+&13+ "¿Correcto?";
          |CONFI aAlfa;
          |SI FSalida ! 0; |ERROR; |FINSI;
          #2#5 = "S";
     |FINSI;
     |SI #2#2 < #2#6;
          aAlfa = "0000NUnidades inferior a las pendientes:" + #2#6 +"."+&13+ "¿Despreciar unidades?";
          |CONFI aAlfa;
          |SI FSalida = 0;
               #2#5 = "S";
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO ExisteArt;
     |SI #2#9 = #73#2;
          nExiste = 1;
          #2#3 = #73#1;
          #2#6 = #73#5 - #73#43;
          #2#4 = #73#4;
          |ERROR10;
     |FINSI;
|FPRC;

|PROCESO CheqCod; |TIPO 0;
     |ABRE #19; |SELECT #4#19;
     #19#128 = #2#1;
     |LEE 000#19=;
     |SI FSalida ! 0;
          |MENAV "0000No existe Articulo....";
          |ERROR;
     |SINO;
          #2#9 = #19#0;
          nExiste = 0;
          |BUCLELIN 2ExisteArt#104;
          |SI nExiste = 0;
               #2#3 = 0;
               #2#6 = 0;
               #2#4 = #19#1;
               |MENAV "0000No existe articulo en Pedido...";
          |FINSI;
          |PINTA #2#4;
     |FINSI;
     |CIERRA #19;
|FPRC;

|PROCESO Min;
     #2#0 = 1;
|FPRC;

|PROCESO Max;
     #2#0 = 999;
|FPRC;

|PROCESO CheqPedido;
     nPend = #73#5 - #73#43;
     |SELECT #2#2;
     #2#3 = #73#1;
     |LEE 000#2=;
     |SI FSalida ! 0; |Y nPend > 0;
          aAlfa = "0000NArticulo " + #73#2 + " no servido."+&13+"¿Despreciar?";
          |CONFI aAlfa;
          |SI FSalida = 0;
               |SELECT #1#2;
               |LEE 000#2u;
               |SI FSalida = 0;
                    nLin = #2#0 + 1;
               |SINO;
                    nLin = 1;
               |FINSI;
               |DDEFECTO #2;
               #2#0 = nLin;
               #2#3 = #73#1;
               #2#4 = #73#4;
               #2#5 = "S";
               #2#7 = #0#0;
               #2#8 = #0#1;
               #2#9 = #73#2;
               #2#10 = #73#6;
               #2#11 = #73#8;
               #2#12 = #73#10;
               #2#13 = #73#14;
               #2#14 = #73#29;
               |GRABA 020#2n;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Tipo12; |TIPO 12;
     |SI FSalida ! 0; |ACABA; |FINSI;

     |ABRE #24;
     #24#0 = eaCliente;
     |LEE 000#24=;
     |CIERRA #24;

     |ABRE #25;
     #25#0 = eaRepre;
     |LEE 000#25=;
     |CIERRA #25;

     |ABRE #104;
     #104#25 = #0#0;
     #104#0 = #0#1;
     |LEE 000#104=;
     |SI FSalida ! 0;
          |MENAV "0000No existe Pedido...";
     |SINO;
          |SI eaCliente ! #104#4;
               aAlfa = "0000Pedido del cliente " + #104#4;
               |MENAV aAlfa; |ERROR;
          |SINO;
               |ENTLINEAL #1#2, 1, 1, 22, 0, Min, Max;
               |ABRE #2;
               |BUCLELIN 2CheqPedido#104;
               |CIERRA #2;
          |FINSI;
     |FINSI;
     |CIERRA #104;
|FPRC;

|PROGRAMA;
     |NOME_DAT #2#eaTmpGui#;
     |PINPA #0#0;
     |ENTLINEAL #1#2, 1, 7, 22, 1, Min, Max;
     |PINDA #0#0;
     |ENDATOS #1#0;
|FPRO;
