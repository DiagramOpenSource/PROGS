|FICHEROS;
     pcegm023 #0;
     pcegm015 #15;  ||Aux Age
     agifa010 #10;  ||Alb Vta
     agifa077 #77;  ||Alb Com
     agifa081 #81;  ||Ped Com
|FIN;

|VARIABLES;
     &enDigito = 0;
     &eaCodigo = "";
     &eaPaquete = "";
     &eaBulto = "";
     &eaNumBulto = "";
     aAlfa = "";
     aInforme = "";
     i = 0;

     aNume = "";
     aCar = "";
     nSuma = 0;
     nSup = 0;
     nNum = 0;
     nDig = 0;
     nPos = 0;
     j = 0;
|FIN;

|PROCESO DigControl;     || entra=aNume, salida=nDig
     aAlfa = aNume;
     nSuma = 0;
     |PARA j = 1; |SI j [ 22; |HACIENDO j = j + 1;
          nPos = j * 100 + 1;
          aCar = aAlfa % nPos;
          nNum = j $ 2;
          |SI nNum = 0;
               nNum = aCar;
               nSuma = nSuma + (nNum * 3);
          |SINO;
               nNum = aCar;
               nSuma = nSuma + nNum;
          |FINSI;
     |SIGUE;
     |PARA nSup = nSuma; |SI; |HACIENDO nSup = nSup + 1;
          nNum = nSup $ 10;
          |SI nNum = 0; |SAL; |FINSI;
     |SIGUE;
     nDig = nSup - nSuma;
|FPRC;

|PROCESO Tipo4; |TIPO 4;
     |SI #0#0 = "AV";
          |ABRE #10;
          #10#52 = #0#1;
          #10#0 = #0#2;
          |LEE 000#10=;
          |CIERRA #10;
     |FINSI;
     |SI #0#0 = "AC";
          |ABRE #77;
          #77#51 = #0#1;
          #77#0 = #0#2;
          |LEE 000#77=;
          |CIERRA #77;
     |FINSI;
     |SI #0#0 = "PC";
          |ABRE #81;
          #81#25 = #0#1;
          #81#0 = #0#2;
          |LEE 000#81=;
          |CIERRA #81;
     |FINSI;


     |ABRE #15;

     |DDEFECTO #15;
     #15#0 = #0#3;
     |LEE 000#15=;
     Impresora = #15#1; |QBF Impresora;
     |SI Impresora = "";
          aAlfa = "0000No esta definida la impresora para la agencia:" + #15#0;
          |MENAV aAlfa;
     |FINSI;

     |IMPRE -1;
     |SI FSalida ! 0; |ERROR; |ACABA; |FINSI;


     eaPaquete = ("00000000" + #0#4) % -108;

     aInforme = #15#5; |QBF aInforme;
     |INFOR aInforme;
     |SI FSalida = 0;
          eaBulto = ("000" + #0#18) % -103;
          |PARA i = 1; |SI i [ #0#18; |HACIENDO i = i + 1;
               eaNumBulto = ("000" + i) % -103;

               aNume = (#0#34 % 115) + (("00" + i) % -102) + (#0#34 % 1805);
               |HAZ DigControl;
               eaCodigo = aNume + nDig;
               enDigito = nDig;

               |PRINT;
               |PIEINF;
          |SIGUE;
          |FININF;
     |SINO;
          aAlfa = "0000Error en informe '" + aInforme + "', agencia: " + #15#0;
          |MENAV aAlfa;
     |FINSI;
     |FINIMP;

     |CIERRA #15;
|FPRC;
