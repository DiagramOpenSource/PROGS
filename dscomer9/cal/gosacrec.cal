|FICHEROS;
     gosacrec #0;
     gopartia #1;
     gosaccon #2;
     goalbpa1 #3;
     gomantca #4;
     goparame #5;
|FIN;
-------------------------------------------------------------------------------
|VARIABLES;
     nNumLin   = 0;
     nLinea    = 0;
     nEsAbono  = 0;
|FIN;
----------------------------------------------------------------------
|PROCESO MiraPalet;
     |DDEFECTO #2;
     |SELECT #3#2;
     #2#1  = #1#2;                                ||Serie Expediente.
     #2#2  = #1#3;                                ||Nume. Expediente.
     #2#3  = #1#0;                                ||Serie Presupuesto.
     #2#4  = #1#1;                                ||Nume. Presupuesto.
     #2#5  = #1#4;                                ||Serie Albaran.
     #2#6  = #1#5;                                ||Numer Albaran.
     #2#17 = #1#11;                               ||Precio Venta Trab.
     #2#13 = #1#20;                               ||Articulo.
     |LEE 000#2=;
     |SI FSalida = 0;
        nNumLin = #2#0;
     |SINO;
        nNumLin = 0;
     |FINSI;
     |SELECT #1#2;

     #2#0 = nNumLin;
     |LEE 000#2=;
     |SI FSalida ! 0;
          nLinea = nLinea + 1;
          #2#0 = nLinea;
          #2#1  = #1#2;                                ||Serie Expediente.
          #2#2  = #1#3;                                ||Nume. Expediente.
          #2#3  = #1#0;                                ||Serie Presupuesto.
          #2#4  = #1#1;                                ||Nume. Presupuesto.
          #2#5  = #1#4;                                ||Serie Albaran.
          #2#6  = #1#5;                                ||Numer Albaran.
          #2#17 = #1#11;                               ||Precio Venta Trab.
          #2#13 = #1#20;                               ||Articulo.
          |GRABA 020#2n; |LEE 000#2=;
     |FINSI;

     |ABRE #3;
     #3#0 = #1#4;
     #3#1 = #1#5;
     |LEE 000#3=;

     |SI FSalida = 0;
          #2#7 = #3#3;                                 ||Proveedor.
          #2#8 = #3#4;                                 ||Nombre.
          #2#12 = #3#2;                                ||Fecha Albaran.
          #2#25 = #3#7;                                ||Su Albaran.
     |FINSI;

     |CIERRA #3;
     |ABRE #4;
     #4#0 = #2#1;
     #4#1 = #2#2;
     |LEE 000#4=;

     |SI FSalida = 0;
          #2#9  = #4#2;                           ||Cliente.
          #2#10 = #4#8;                           ||Nombre.
          #2#11 = #4#3;                           ||Direccion Obra.
     |FINSI;

     |CIERRA #4;
     #2#13 = #1#20;                               ||Codigo Articulo.
     #2#14 = #1#30;                               ||Descripcion.

     |SI #1#12 ] 0; |Y #1#10 ] 0;
          #2#15 = "C";
          nEsAbono = 1;                           ||Tipo Cargo
     |FINSI;

     |SI #1#10 < 0;
          #2#15 = "A";
          nEsAbono = 1;
     |FINSI;

     |SI #1#12 < 0;
          #2#15 = "A";                            ||Tipo Abono.
          nEsAbono = -1;
     |FINSI;

     #2#16 = #2#16 + (#1#10*nEsAbono);           ||Cantidad.

     |SI #2#16 = 0;
          |BORRA 020#2a;
          |ACABA;
     |FINSI;

     #2#17 = #1#11;                               ||Precio Vent. Trab.
     #2#18 = #1#22;                               ||Precio Vent. Base.
     #2#19 = #1#12;                               ||precio Cost. Trab.
     #2#20 = #1#23;                               ||Precio Cost. Base.
     #2#21 = (#2#16*#2#17*nEsAbono);              ||Total Venta Traba.
     #2#22 = (#2#16*#2#18*nEsAbono);              ||Total Venta Base.
     #2#23 = (#2#16*#2#19*nEsAbono);              ||Total Coste Traba.
     #2#24 = (#2#16*#2#20*nEsAbono);              ||Total Coste Base.
     |GRABA 020#2a;
|FPRC;
-------------------------------------------------------------------------------
|DEFBUCLE MiraPalet;
     #1#1;
     26;                                          ||Campos Extras.
     " ", "     ",      1,   1,   1, #5#37;
     "z", "zzzzz", 999999, 999, 999, #5#37;
     ;                                          ||b/e Bolqueo-Ordenacion.
     NULL, MiraPalet;
|FIN;
-------------------------------------------------------------------------------
|PROCESO BorraPalet;
     |SI #2#15 ! "D";
          |BORRA 020#2a;
     |SINO;
          nLinea = #2#0;
     |FINSI;
|FPRC;
-------------------------------------------------------------------------------
|DEFBUCLE BorraPalet;
     #2#1;
     ;                                          ||Campos Extras.
     1;
     9999;
     ;                                          ||b/e Bolqueo-Ordenacion.
     NULL, BorraPalet;
|FIN;
-------------------------------------------------------------------------------
|PROCESO Recalcula; |TIPO 0;
     |ABRE #5;
     #5#0 = "A";
     |LEE 000#5=;

     |SI FSalida = 0;

          |SI #0#0 = "S";
               |BUCLE BorraPalet;
               |ABRE #2;
               nLinea = 0;
               |BUCLE MiraPalet;
               |CIERRA #2;
          |FINSI;

     |FINSI;

     |CIERRA #5;
     |PTEC 802;
|FPRC;
-------------------------------------------------------------------------------
