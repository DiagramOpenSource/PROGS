|FICHEROS;
     dsz00043 #0;
     agifv030 #3;
     agifa019 #19;
     agifa017 #17;
     agifv030 #30;
     dsm00043 #43; || Regularizacion
     dsm00044 #44;
     dsm00056 #56; || Valorada
     dsm00057 #57;
     agifa065 #65;
     dsm00120 #120;
     dsm00121 #121;
     agifa321 #321;
     agifa324 #324;
     dsm00005 #905;
     dsm00006 #906;
     dsm00014 #914;
     dsm00154 #154;
|FIN;

|INCLUYE i_varart;

|VARIABLES;
     nTipoV = 0;
     &efFecha1 = @;
     &efFecha2 = @;
     fFecha = @;
     dArt = "                    ";
     hArt = "zzzzzzzzzzzzzzzzzzzz";
     nUni = 0;
     aActS = "";
     aActN = "";
     nDAlma = 0;
     nHAlma = 0;
     &nDeci_PreBaseMx = 0;
     &nDeci_PreBase = 0;
     nSw       = 0;
     nNum      = 0;
     nLin      = 0;
     nUni2 = 0;
|FIN;

|PROCESO PonCeroSinRecV;
     #154#5 = 0;
     #154#6 = #154#5 - #154#4;
     |GRABA 020#154a;
     |LIBERA #154;
|FPRC;

|DEFBUCLE PonCeroSinRecV;
     #154#1;
     ;
     #121#0, #121#1, #121#3, "      ";
     #121#0, #121#1, #121#3, "zzzzzz";
     ;
     NULL, PonCeroSinRecV;
|FIN;

|PROCESO PonCeroSinRec;
     |SI #121#21 = "N";
          #121#5 = 0;
          #121#13 = 0;
          #121#6 = #121#5 - #121#4;
          #121#14 = #121#13 - #121#12;
          |GRABA 020#121a;
          |BUCLE PonCeroSinRecV;
     |FINSI;
|FPRC;
ÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ
|PROCESO LinPosteriores;
     |HAZ ReActualiza;
|FPRC;

|DEFBUCLE LinPosteriores;
     #121#1;
     ;
     #120#0, "                    ", 00000, "               ";
     #120#0, "zzzzzzzzzzzzzzzzzzzz", 99999, "zzzzzzzzzzzzzzz";
     ;
     NULL, LinPosteriores;
|FIN;

|PROCESO Posteriores;
     |SI #120#5 ! 0;
          |BUCLE LinPosteriores;
     |FINSI;
|FPRC;

|DEFBUCLE Posteriores;
     #120#1;
     ;
     fFecha;
     "31.12.9999";
     be;
     NULL, Posteriores;
|FIN;

|PROCESO ActVertiValor;
     eaTipo       = "W1";
     eaSerie      = #57#28;
     enCodigo     = #57#27;
     enLinea      = #57#14;
     eaArticulo   = #57#0;
     enAlmacen    = #57#1;
     enUnidades   = #905#6 * enForma;
     eaComponente = #905#4;
     efFecha      = #57#12;
     enCamBas    =  #324#3; ||=cambio_div
     enCamDiv    =  #324#3; ||=cambio_base;
     |SI #57#18 = #321#9;
          eaMoneda = "B";
     |SINO;
          eaMoneda = "D";
     |FINSI;
     |SUB_EJECUTA_NP "dsz00002;EVS";
|FPRC;

|PROCESO ActVertiRegu;
     eaTipo       = "T3";
     eaSerie      = #44#21;
     enCodigo     = #44#18;
     enLinea      = #44#8;
     eaArticulo   = #44#0;
     enAlmacen    = #44#1;
     enUnidades   = #905#6 * enForma;
     eaComponente = #905#4;
     efFecha      = #44#5;
     enCodHisto   = #44#18;
     |SI #44#7 = "S";
          eaOpeHisto = "SS";
     |SINO;
          eaOpeHisto = "ES";
     |FINSI;
     |LIBERA #905;
     |SUB_EJECUTA_NP "dsz00002;ERE";
|FPRC;

|PROCESO InventarioV;
     nUni = #154#5 - #154#4;
     |SI nUni ! #154#6;
          |SI #120#8 = "V";
               #905#0 = "W1";
               #905#1 = #120#6;
               #905#2 = #120#7;
               #905#3 = #121#10;
               |LEE 101#905=;
               |SI FSalida = 0;
                    enForma = -1; |HAZ ActVertiValor;
                    #905#6 = nUni;
                    enForma = 1; |HAZ ActVertiValor;
                    |GRABA 020#905a;
                    |LIBERA #905;
               |FINSI;
          |SINO;
               #905#0 = "T3";
               #905#1 = #120#6;
               #905#2 = #120#7;
               #905#3 = #121#10;
               |LEE 101#905=;
               |SI FSalida = 0;
                    enForma = -1; |HAZ ActVertiRegu;
                    #905#6 = nUni;
                    enForma = 1; |HAZ ActVertiRegu;
                    |GRABA 020#905a;
                    |LIBERA #905;
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE InventarioV;
     #154#1;
     ;
     #121#0, #121#1, #121#3, "      ";
     #121#0, #121#1, #121#3, "zzzzzz";
     ;
     NULL, InventarioV;
|FIN;

|PROCESO Inventario;
     nUni = #121#5 - #121#4;
     nUni2 = #121#13 - #121#12;
     |SI nUni ! #121#6; |O nUni2 ! #121#14;
          |SI #120#8 = "V";
               #57#28 = #120#6;
               #57#27 = #120#7;
               #57#14 = #121#10;
               |LEE 101#57=;
               |SI FSalida = 0;
                    eaProve = #57#5;
                    enCodigo = #57#27;
                    enForma = -1;
                    |HAZ AcumulaEV;
                    #57#4 = nUni;
                    #57#21 = nUni2;
                    |GRABA 020#57a;
                    enForma = 1;
                    |HAZ AcumulaEV;
                    |LIBERA #57;

                    |ABRE #905;
                    |BUCLE InventarioV;
                    |CIERRA #905;
               |FINSI;
          |SINO;
               #44#21 = #120#6;
               #44#18 = #120#7;
               #44#8 = #121#10;
               |LEE 101#44=;
               |SI FSalida = 0;
                    enCodigo = #44#18;
                    enForma = -1;
                    |HAZ AcumulaES_SS;
                    |SI nUni > 0;
                         #44#7 = "E";
                         #44#4 = nUni;
                         #44#12 = nUni2;
                    |SINO;
                         #44#7 = "S";
                         #44#4 = -nUni;
                         #44#12 = -nUni2;
                    |FINSI;
                    |GRABA 020#44a;
                    enForma = 1;
                    |HAZ AcumulaES_SS
                    |LIBERA #44;

                    |ABRE #905;
                    |BUCLE InventarioV;
                    |CIERRA #905;
               |FINSI;
          |FINSI;
          #121#6 = nUni;
          #121#14 = nUni2;
     |FINSI;
     #121#9 = "S";
     |GRABA 020#121a;
     |LIBERA #121;


     #3#0 = "I";
     #3#1 = #121#3;
     #3#2 = #121#0;
     #3#3 = #121#6;
     #3#4 = #121#7;
     |LEE 000#3=;
     |SI FSalida ! 0; |GRABA 020#3n; |FINSI;
|FPRC;

|DEFBUCLE Inventario;
     #121#1;
     ;
     #120#0, "                    ", 00000, "               ";
     #120#0, "zzzzzzzzzzzzzzzzzzzz", 99999, "zzzzzzzzzzzzzzz";
     be;
     NULL, Inventario;
|FIN;

|PROCESO HistoVertical;
     #154#0 = #120#0;
     #154#1 = #65#0;
     #154#2 = #121#3;
     #154#3 = #906#7;
     |LEE 101#154=;
     |SI FSalida = 0;
          #154#4 = #154#4 + #906#9;
          |GRABA 020#154a;
          |LIBERA #154;
     |FINSI;
|FPRC;

|DEFBUCLE HistoVertical;
     #906#1;
     ;
     #65#0, #65#5, #65#1, #65#2, #65#11, #65#3, #65#4, "      ";
     #65#0, #65#5, #65#1, #65#2, #65#11, #65#3, #65#4, "zzzzzz";
     ;
     NULL, HistoVertical;
|FIN;

|PROCESO Historico;
     #19#0 = #65#0;
     |LEE 000#19=;
     |SI FSalida ! 0; |ACABA; |FINSI;

     || para que No contemple los movimientos realizados  de regularización
     || en la actualización anterior
     |SI #65#2 = "ES"; |O #65#2 = "SS";
          |SI #65#11 = #120#6; |Y #65#3 = #120#7;
               |ACABA;
          |FINSI;
     |FINSI;

     |DDEFECTO #121;
     #121#0 = #120#0;
     #121#1 = #65#0;
     |SI #120#3 = "S";
          #121#3 = #120#4;
     |SINO;
          #121#3 = #65#5;
     |FINSI;
     |SI #120#3 = "P";
          #121#11 = #65#18;
     |FINSI;
     |LEE 101#121=;
     |SI FSalida = 0;
          #121#4 = #121#4 + #65#6;
          #121#12 = #121#12 + #65#17;
          |GRABA 020#121a;
     |FINSI;
     |LIBERA #121;

     |SI #19#176 = "S";
          |ABRE #154;
          |BUCLE HistoVertical;
          |CIERRA #154;
     |FINSI;
|FPRC;

|DEFBUCLE Historico;
     #65#1;
     5;
     dArt, "01.01.1900",  "  ", 000000, 00000,"     ", nDAlma;
     hArt, #120#0,        "zz", 999999, 99999,"zzzzz", nHAlma;
     ;
     NULL, Historico;
|FIN;

|PROCESO PonCero;
     #121#4 = 0;
     #121#12 = 0;
     |GRABA 020#121a;
     |LIBERA #121;
|FPRC;

|DEFBUCLE PonCero;
     #121#1;
     ;
     #120#0, "                    ", 00000, "               ";
     #120#0, "zzzzzzzzzzzzzzzzzzzz", 99999, "zzzzzzzzzzzzzzz";
     be;
     NULL, PonCero;
|FIN;

|PROCESO PonCeroV;
     #154#4 = 0;
     |GRABA 020#154a;
     |LIBERA #154;
|FPRC;

|DEFBUCLE PonCeroV;
     #154#1;
     ;
     #120#0, "                    ", 00000, "      ";
     #120#0, "zzzzzzzzzzzzzzzzzzzz", 99999, "zzzzzz";
     be;
     NULL, PonCeroV;
|FIN;

|PROCESO ReActualiza;
     |BUCLE PonCero;
     |BUCLE PonCeroV;
     |ABRE #154;
     |ABRE #121;
     |ABRE #19;
     |SI #120#3 = "N";
          nDAlma = #120#4;
          nHAlma = #120#4;
     |SINO;
          nDAlma = 1;
          nHAlma = 99999;
     |FINSI;

     |BUCLE Historico;
     |ABRE #65;
     |ABRE #57;
     |ABRE #56;
     |ABRE #43;
     |ABRE #44;
     |ABRE #3;
     |BUCLE Inventario;
     |CIERRA #3;
     |CIERRA #44;
     |CIERRA #43;
     |CIERRA #56;
     |CIERRA #57;
     |CIERRA #65;
     |CIERRA #19;
     |CIERRA #121;
     |CIERRA #154;

     |ABRE #30;
     #30#0 = "I";
     #30#1 = #0#4;
     #30#2 = #0#0;
     |LEE 101#30=;
     |SI FSalida ! 0; |GRABA 020#30b; |FINSI;
     #30#3 = #120#6;
     #30#4 = #120#7;
     |GRABA 020#30a;
     |CIERRA #30;
|FPRC;
ÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ
|PROCESO Vertical;
     |SI nTipoV = 1; ||Regula
          |ABRE #914;
          #914#0 = #154#3;
          |LEE 000#914=;
          |CIERRA #914;

          |DDEFECTO #905;
          #905#0 = "T3";
          #905#1 = #44#21;
          #905#2 = #44#18;
          #905#3 = #44#8;
          #905#4 = #154#3;
          #905#5 = #914#1;
          |SI #44#7 = "E";
               #905#6 = #154#6;
          |SINO;
               #905#6 = -#154#6;
          |FINSI;
          |GRABA 020#905n;
          enForma = 1; |HAZ ActVertiRegu;
     |SINO;          ||Valorada;
          |ABRE #914;
          #914#0 = #154#3;
          |LEE 000#914=;
          |CIERRA #914;

          |DDEFECTO #905;
          #905#0 = "W1";
          #905#1 = #57#28;
          #905#2 = #57#27;
          #905#3 = #57#14;
          #905#4 = #154#3;
          #905#5 = #914#1;
          #905#6 = #154#6;
          #905#7 = #57#19;
          #905#8 = #905#6 * #905#7;
          |GRABA 020#905n;
          enForma = 1; |HAZ ActVertiValor;
     |FINSI;
|FPRC;

|DEFBUCLE Vertical;
     #154#1;
     ;
     #121#0, #121#1, #121#3, "      ";
     #121#0, #121#1, #121#3, "zzzzzz";
     ;
     NULL, Vertical;
|FIN;

|PROCESO dsm00121Val;
     #121#9 = "S";
     |GRABA 020#121a;

     |DDEFECTO #19;
     |ABRE #19;
     #19#0 = #121#1;
     |LEE 000#19=;
     |CIERRA #19;
     |SI #19#137 = "N"; |ACABA; |FINSI;

     |SI #0#5 = "S"; |HAZ PonCeroSinRec; |FINSI;

     |SI #121#6 = 0;
          |LIBERA #121;
          |ACABA;
     |FINSI;

     |DDEFECTO #17;
     #17#0 = #121#1;
     #17#1 = #121#3;
     |LEE 000#17=;
     |CIERRA #17;

     nLin = nLin + 1;
     |DDEFECTO #57;
     #57#0 = #121#1;
     #57#1 = #121#3;
     #57#2 = #121#2;
     #57#3 = #17#39;
     #57#4 = #121#6;     || Uni Entrada
     #57#5 = #19#27;
     #57#6 = #19#28;
     #57#7 = #19#29;       ||Dto1
     #57#20 = #19#129;     ||Dto2
     #57#30 = #19#209;     ||Dto3
     #57#11 = (((((#57#6 < #57#7) < #57#20) < #57#30) < #57#8) < #57#9) < #57#10;
     #57#12 = #0#0;
     #57#13 = #19#6;
     #57#14 = nLin;
     #57#15 = "ACTUALIZACION INVENTARIO";
     #57#18 = #19#165;
     #57#19 = #19#164;
     #57#27 = #56#0; ||Doc
     #57#28 = #56#2; ||Ser
     #57#29 = #121#7;
     #57#22 = #121#11;
     #57#21 = #121#14;
     #57#24 = #121#18;
     #57#25 = #121#19;
     #57#26 = #121#20;
     |GRABA 020#57n;
     eaProve = #57#5;
     enCodigo = #57#27;
     enForma = 1;
     |HAZ AcumulaEV;

     #3#0 = "I";
     #3#1 = #57#1;
     #3#2 = #56#1;
     #3#3 = #56#2;
     #3#4 = #56#0;
     |LEE 000#3=;
     |SI FSalida ! 0; |GRABA 020#3n; |FINSI;
     #121#10 = nLin;
     |GRABA 020#121a;
     |LIBERA #121;

     |ABRE #905;
     nTipoV = 2; |BUCLE Vertical
     |CIERRA #905;

||ÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄChapuza pa Iber :(
     |DDEFECTO #19;
     |ABRE #19;
     #19#0 = #121#1;
     |LEE 000#19=;
     |CIERRA #19;
|FPRC;

|PROCESO FinCabVal;
     nSw = 1;

     |CIERRA #3;
     |CIERRA #56;
     |CIERRA #57;
|FPRC;

|DEFBUCLE dsm00121Val;
     #121#1;
     9;
     #120#0, "                    ", nDAlma, "               ", "N";
     #120#0, "zzzzzzzzzzzzzzzzzzzz", nHAlma, "zzzzzzzzzzzzzzz", "N";
     be;
     NULL, dsm00121Val;
|FIN;

|PROCESO IniVal;
     nNum = #120#7;
     |SI #120#1 = "S";
          |ACABA;
     |FINSI;
     |ABRE #3;
     |ABRE #56;
     |ABRE #57;
     #56#2 = #0#3;
     #56#0 = 999999;
     |LEE 000#56];
     |SI FSalida = 0;
          |LEE 000#56a;
     |SINO;
          |LEE 000#56u;
     |FINSI;
     |SI FSalida = 0; |Y #56#2 = #0#3;
          nNum = #56#0 + 1;
     |SINO;
          nNum = 1;
     |FINSI;
     |DDEFECTO #56;
     #56#0 = nNum;
     #56#1 = #0#0;
     #56#2 = #0#3;
     |GRABA 020#56b;
     |BUCLE dsm00121Val;
     |HAZ FinCabVal;
|FPRC;

|DEFBUCLE dsm00120Val;
     #120#1;
     ;
     #0#0;
     #0#0;
     ;
     NULL, IniVal;
|FIN;
ÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ
|PROCESO dsm00121;
     #121#9 = "S";
     |GRABA 020#121a;

     |ABRE #19;
     #19#0 = #121#1;
     |LEE 000#19=;
     |CIERRA #19;
     |SI #19#137 = "N"; |ACABA; |FINSI;

     |SI #0#5 = "S"; |HAZ PonCeroSinRec; |FINSI;

     |SI #121#6 = 0;
          |LIBERA #121;
          |ACABA;
     |FINSI;

     nLin = nLin + 1;
     |DDEFECTO #44;
     #44#21 = #43#4;
     #44#18 = #43#0;
     #44#8 = nLin;

     |SI #121#6 > 0;
          #44#7 = "E";
          #44#4 = #121#6;
          #44#12 = #121#14;
     |SINO;
          #44#7 = "S";
          #44#4 = -#121#6;
          #44#12 = -#121#14;
     |FINSI;
     #44#0 = #121#1;
     #44#1 = #121#3;
     #44#2 = #121#2;
     #44#5 = #43#1;
     #44#13 = #121#11;
     #44#15 = #121#18;
     #44#16 = #121#19;
     #44#17 = #121#20;

     enCodigo = #44#18;
     enForma = 1;
     |HAZ AcumulaES_SS;

     #44#3 = #17#39;            || Disponible que queda
     #44#19 = #17#5;

     |GRABA 020#44n;

     #3#0 = "I";
     #3#1 = #44#1;
     #3#2 = #43#1;
     #3#3 = #43#4;
     #3#4 = #43#0;
     |LEE 000#3=;
     |SI FSalida ! 0; |GRABA 020#3n; |FINSI;

     #121#10 = nLin;
     |GRABA 020#121a;
     |LIBERA #121;

     |ABRE #905;
     nTipoV = 1; |BUCLE Vertical
     |CIERRA #905;
|FPRC;

|PROCESO FinCab;
     nSw = 1;
     |CIERRA #3;
     |CIERRA #43;
     |CIERRA #44;
|FPRC;

|DEFBUCLE dsm00121;
     #121#1;
     9;
     #120#0, "                    ", nDAlma, "               ", "N";
     #120#0, "zzzzzzzzzzzzzzzzzzzz", nHAlma, "zzzzzzzzzzzzzzz", "N";
     be;
     NULL, dsm00121;
|FIN;

|PROCESO Ini;
     nNum = #120#7;
     |SI #120#1 = "S";
          |ACABA;
     |FINSI;

     |ABRE #3;
     |ABRE #43;
     |ABRE #44;
     #43#4 = #0#3;      ||SERIE
     #43#0 = 999999;
     |LEE 000#43];
     |SI FSalida = 0;
          |LEE 000#43a;
     |SINO;
          |LEE 000#43u;
     |FINSI;
     |SI FSalida = 0; |Y #43#4 = #0#3;
          nNum = #43#0 + 1;
     |SINO;
          nNum = 1;
     |FINSI;
     |DDEFECTO #43;
     #43#0 = nNum;
     #43#1 = #0#0;
     #43#4 = #0#3;
     #43#2 = "REGULARIZACION INVENTARIO";
     |GRABA 020#43b;
     |BUCLE dsm00121;
     |HAZ FinCab;
|FPRC;

|DEFBUCLE dsm00120;
     #120#1;
     ;
     #0#0;
     #0#0;
     ;
     NULL, Ini;
|FIN;

|PROCESO CheqAct;
     |SI #121#9 = "S"; aActS = "S"; |FINSI;
     |SI #121#9 = "N"; aActN = "S"; |FINSI;
|FPRC;

|DEFBUCLE CheqAct;
     #121#1;
     ;
     #0#0, "                    ", 00001, "               ";
     #0#0, "zzzzzzzzzzzzzzzzzzzz", 99999, "zzzzzzzzzzzzzzz";
     ;
     NULL, CheqAct;
|FIN;

|PROCESO ActPrecio;
     |DDEFECTO #19;
     #19#0 = #121#1;
     |LEE 000#19=;

     #121#15 = #19#28;    || Ult Pre Com
     #121#16 = #19#9;     || PCM
     #121#17 = #19#133;   || Pre con Dto

     |DDEFECTO #65;
     #65#0 = #121#1;
     #65#5 = #121#3;
     #65#1 = "31.12.9999";
     |LEE 000#65];
     |SI FSalida = 0;
          |LEE 000#65a;
     |SINO;
          |LEE 000#65u;
     |FINSI;
     |PARA; |SI #65#0 = #121#1; |Y #65#1 = #121#3; |HACIENDO;
          |SI #65#2 = "AC"; |O #65#2 = "PC"; |O #65#2 = "EV";
                #121#15 = #65#9;     || Ult Pre Com
                #121#16 = #65#8;     || PCM
                #121#17 = #65#14;    || Pre con Dto
               |SAL;
          |FINSI;
          |LEE 000#65a;
     |SIGUE;
     |GRABA 020#121a;
     |LIBERA #121;
|FPRC;

|DEFBUCLE ActPrecio;
     #121#1;
     9;
     #120#0, "                    ", nDAlma, "               ", "N";
     #120#0, "zzzzzzzzzzzzzzzzzzzz", nHAlma, "zzzzzzzzzzzzzzz", "N";
     be;
     NULL, ActPrecio;
|FIN;
ÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ
|PROCESO HistoricoRecal;
     #19#0 = #65#0;
     |LEE 000#19=;
     |SI FSalida ! 0; |ACABA; |FINSI;
     |SI #19#137 = "N"; |ACABA; |FINSI;

     |DDEFECTO #121;
     #121#0 = #120#0;
     #121#1 = #65#0;
     |SI #120#3 = "N";
          #121#3 = #120#4;
     |SINO;
          #121#3 = #65#5;
     |FINSI;
     |SI #120#3 = "P";
          #121#11 = #65#18;
     |FINSI;
     |LEE 101#121=;
     |SI FSalida = 0;
          #121#4 = #121#4 + #65#6;
          #121#6 = #121#5 - #121#4;

          #121#12 = #121#12 + #65#17;
          #121#14 = #121#13 - #121#12;
          |GRABA 020#121a;
     |SINO;
          #121#2 = #19#1;
          #121#4 = #65#6;
          #121#5 = 0;
          #121#6 = #65#6;
          #121#7 = #19#5;

          #121#12 = #65#17;
          #121#13 = 0;
          #121#14 = #65#17;

          #121#15 = #19#28;
          #121#16 = #19#9;
          #121#17 = #19#133;
          |GRABA 020#121n;
     |FINSI;
     |LIBERA #121;
|FPRC;

|DEFBUCLE HistoricoRecal;
     #65#1;
     5;
     dArt, "01.01.1900",  "  ", 000000, 00000,"     ", nDAlma;
     hArt, #120#0,        "zz", 999999, 99999,"zzzzz", nHAlma;
     ;
     NULL, HistoricoRecal;
|FIN;

|PROCESO Recalcula;
     |MENSA "0000Recalculando...";
     dArt = " " * 20;
     hArt = "z" * 20;
     |ABRE #120;
     #120#0 = #0#0;
     |LEE 121#120=;
     |SI FSalida = 0;
          |BUCLE PonCero;
          |BUCLE PonCeroV;
          |ABRE #121;
          |ABRE #19;
          |SI #120#3 = "N";
               nDAlma = #120#4;
               nHAlma = #120#4;
          |SINO;
               nDAlma = 1;
               nHAlma = 99999;
          |FINSI;
          |BUCLE Historico;
          |CIERRA #19;
          |CIERRA #121;
     |FINSI;
     |CIERRA #120;
|FPRC;
ÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ
|PROCESO Tipo_3; |TIPO 3;
     |HAZ DecimalesBase;
     nDeci_PreBaseMx = nDeci_PreBase;

     |SI #0#1 = "SI";
          |ABRE #120;
          #120#0 = #0#0;
          |LEE 000#120=;
          |SI FSalida ! 0;
               |MENAV "0000No existe inventario...";
               |ACABA;
          |FINSI;
          |CIERRA #120;

          |SI #0#5 = "S";
               |CONFI "0000N¿Esta seguro de regularizar a 0 los artículos no inventariados?";
               |SI FSalida ! 0;
                    |MENAV "0000Proceso anulado...";
                    |ACABA;
               |FINSI;
          |FINSI;


          |SI #120#1 = "S";
               |MENSA "0000EL INVENTARIO ESTA ACTUALIZADO";
               |ACABA;
          |FINSI;

          |HAZ Recalcula;

          |SI #0#4 = 0;
               nDAlma = 1;
               nHAlma = 99999;
          |SINO;
               nDAlma = #0#4;
               nHAlma = #0#4;
          |FINSI;

          |ABRE #19;
          |ABRE #65; |SELECT #5#65;
          |BUCLE ActPrecio;  ||Busca ultimo precio en historico y actualiza
          |CIERRA #65;
          |CIERRA #19;

          |SI #120#5 = 0;
               |SI #0#2 = "R";
                    |BUCLE dsm00120;
               |SINO;
                    |BUCLE dsm00120Val;
               |FINSI;
          |FINSI;

          |BUCLE CheqAct;
          |ABRE #120;
          #120#0 = #0#0;
          |LEE 121#120=;
          |SI FSalida = 0;
               |SI #120#5 = 0;
                    |SI aActS = "S"; |Y aActN = "S";
                         #120#1 = "P";
                    |SINO;
                         |SI aActS = "S"; #120#1 = "S"; |FINSI;
                         |SI aActN = "S"; #120#1 = "N"; |FINSI;
                    |FINSI;
                    |SI #120#1 = "S";
                         #120#5 = #120#5 + 1;
                    |FINSI;
                   || #120#5 = #120#5 + 1;
                    #120#6 = #0#3;
                    #120#7 = nNum;
                    #120#8 = #0#2;
               |SINO;
                    #120#5 = #120#5 + 1;
                    #120#1 = "S";
               |FINSI;
               |GRABA 020#120a;
               |SI #120#5 > 1;
                    #120#1 = "S";
                    |GRABA 020#120a;
                    |HAZ ReActualiza;
                    fFecha = #0#0 + 1;
                    |BUCLE Posteriores;
               |FINSI;
          |FINSI;

          |HAZ EsVipei;
          |SI FSalida = 0;
               efFecha1 = #120#0;
               |LEE 000#120a;
               |SI FSalida = 0;
                    efFecha2 = #120#0;
                    |SUB_EJECUTA_NP "dsz00079;algo";
               |FINSI;
          |FINSI;

          |CIERRA #120;
          |SI nSw = 0;
               |MENAV "0000Este Inventario esta actualizado...";
          |FINSI;
     |FINSI;
|FPRC;
