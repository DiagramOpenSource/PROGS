|FICHEROS;
     gomed010 #0;
     gomed011 #1;
     gomed012 #2,MANTE;
     gotmpme1 #3;
     gofamive #4;
     gosubive #5;
     goartive #6,MANTE;
     gotmpme2 #7;
     gotmpart #8;
|FIN;
----------------------------------------------------------------------
|VARIABLES;
     &Tempo = "";
     aTmp3 = "";
     nModo     = 0;
     nConta    = 0;
     nBotLPar  = 0;
     nBotPart  = 0;
     cFich     = "";
     nLinea    = 0;
     &cParam   = "";
     &cPart    = "";
     &cPartAct = "";
|FIN;
----------------------------------------------------------------------
|PROCESO BorraPar;
     |BORRA 020#2a;
|FPRC;
-------------------------------------------------------------------------------
|DEFBUCLE BorraPar;
     #2#1;
     ;
     #1#0,Contenido,  1;
     #1#0,Contenido,999;
     ;
     NULL,BorraPar;
|FIN;
-------------------------------------------------------------------------------
|PROCESO Baja; |TIPO 2;
     nModo = (FEntrada/100)!0;
     |SI nModo = 3;
          |BUCLE BorraPar;
     |FINSI;
|FPRC;
-------------------------------------------------------------------------------
|PROCESO CamCap; |TIPO 0;
     |SI Contenido ! #1#2;
          |BUCLE BorraPar;
     |FINSI;
|FPRC;
-------------------------------------------------------------------------------
|PROCESO Contador; |TIPO 7;
     nModo = (FEntrada/100)!0;
     |SI nModo = 1;
          |LEE 000#0u;
          |SI FSalida = 0;
               nConta = #0#0 + 1;
          |SINO;
               nConta = 1;
          |FINSI;
          |DDEFECTO #0;
          #0#0 = nConta;
          |PINTA #0#0;
     |FINSI;
|FPRC;
-------------------------------------------------------------------------------
|PROCESO Inicio; |TIPO 13;
     |ENTLINEAL #1#2,-3,7,21,0,MinMed,MaxMed;
|FPRC;
-------------------------------------------------------------------------------
|PROCESO MinMed;
     #2#0 = #0#0;
     #2#1 = #1#2;
     #2#2 = 1;
|FPRC;
-------------------------------------------------------------------------------
|PROCESO MaxMed;
     #2#0 = #0#0;
     #2#1 = #1#2;
     #2#2 = 999;
|FPRC;
-------------------------------------------------------------------------------
|PROCESO TomaSubFam;
     |DDEFECTO #3;
     #3#0 = #5#0;
     #3#1 = #5#1;
     |GRABA 020#3c;
|FPRC;
-------------------------------------------------------------------------------
|DEFBUCLE TomaSubFam;
     #5#4;
     ;
     #4#0;
     #4#0;
     ;
     NULL,TomaSubFam;
|FIN;
-------------------------------------------------------------------------------
|PROCESO TomaFami;
     |BUCLE TomaSubFam
|FPRC;
-------------------------------------------------------------------------------
|DEFBUCLE TomaFami;
     #4#4;
     ;
     #0#2;
     #0#2;
     ;
     NULL, TomaFami;
|FIN;
-------------------------------------------------------------------------------
|PROCESO TmpCap; |TIPO 0;
     |DELINDEX #3;
     |ABRE #3;
     |BUCLE TomaFami;
     |CIERRA #3;
|FPRC;
-------------------------------------------------------------------------------
|PROCESO Partida; |TIPO 0;
     |SI FSalida = 11;
          |ABRE #8;
          |PUSHV 0101 2380;
          |CONSULTA_F_CLAVE #8#8,MinCapi,MaxCapi;
          |POPV;
          |SI FSalida = 0;
               #2#3 = #8#0;             ||PARTIDA
               #2#4 = #8#1;             ||DESCRIPCION
               #2#8 = #8#2;             ||UNIDAD DE MEDIDA
               |PINTA #2#3;
               |PINTA #2#4;
               |PINTA #2#8;
          |SINO;
               |ESTADO_CONTROL nBotLPar, "SHOW";
               |ERROR10;
          |FINSI;
          |CIERRA #8;
     |SINO;
          |ABRE #8;
          |SELECT #8#8;
          #8#11 = #1#2;
          #8#0 = #2#3;
          |LEE 000#8=;
          |SI FSalida ! 0;
               |MENAV "0000PARTIDA NO ENCONTRADA EN ESTE CAPITULO";
               |ESTADO_CONTROL nBotLPar, "SHOW";
               |ERROR10;
          |SINO;
               #2#4 = #8#1;             ||DESCRIPCION
               #2#8 = #8#2;             ||UNIDAD DE MEDIDA
               |PINTA #2#3;
               |PINTA #2#4;
               |PINTA #2#8;
          |FINSI;
          |CIERRA #8;
     |FINSI;
|FPRC;
-------------------------------------------------------------------------------
|PROCESO MinCapi;
     #8#11 = #1#2;
     #8#0 = " "*20;
|FPRC;
-------------------------------------------------------------------------------
|PROCESO MaxCapi;
     #8#11 = #1#2;
     #8#0 = "z"*20;
|FPRC;
-------------------------------------------------------------------------------
|PROCESO BI004; |TIPO 80;
     |CLS;
     |CONTROL_SIMPLE 0, "BOTON,F2 - Partida", 2430, 2450, FDos;
     nBotPart = FSalida;
     |ESTADO_CONTROL nBotPart, "HIDE";
     |CONTROL_SIMPLE 0, "BOTON,F3 - Listado Partidas", 2430, 2450, F3;
     nBotLPar = FSalida;
     |ESTADO_CONTROL nBotLPar, "HIDE";

     |HAZ CreaTempo; aTmp3 = Tempo;
     |NOME_DAT #3 aTmp3; |DELINDEX #3;
|FPRC;
------------------------------------------------------------------------------
|PROCESO BF004; |TIPO 90;
     Tempo = aTmp3; |HAZ BoraTempo; |DELINDEX #3;

     |PULSATECLA;
     |FIN_CONTROL_WINDOWS nBotLPar;
     |PULSATECLA;
     |FIN_CONTROL_WINDOWS nBotPart;
     |PULSATECLA;
|FPRC;
------------------------------------------------------------------------------
|PROCESO BS004; |TIPO 7;
     |ESTADO_CONTROL nBotPart, "SHOW";
|FPRC;
-------------------------------------------------------------------------------
|PROCESO BS005; |TIPO 7;
     |ESTADO_CONTROL nBotLPar, "SHOW";
|FPRC;
-------------------------------------------------------------------------------
|PROCESO BH004; |TIPO 0;
     |ESTADO_CONTROL nBotPart, "HIDE";
     |ESTADO_CONTROL nBotLPar, "HIDE";
|FPRC;
-------------------------------------------------------------------------------
|PROCESO BH0004; |TIPO 11;
     |ESTADO_CONTROL nBotLPar, "HIDE";
     |ESTADO_CONTROL nBotPart, "HIDE";
|FPRC;
-------------------------------------------------------------------------------
|PROCESO FDos;
     |PTEC 824;
|FPRC;
-------------------------------------------------------------------------------
|PROCESO F3;
     |PTEC 825;
|FPRC;
-------------------------------------------------------------------------------
|PROCESO PinPar; |TIPO 7;
     |HAZ Inicio;
|FPRC;
-------------------------------------------------------------------------------
|PROCESO LinPar; |TIPO 11;
     nModo = (FEntrada/100)!0;
     |SI FSalida = 10;
          |SI #1#2 = "   ";
               |MENAV "0000DEBE INTRODUCIR UN CAPITULO";
               |ERROR10;
               |ACABA;
          |FINSI;
          |ESTADO_CONTROL nBotPart, "HIDE";
          |SI nModo = 1; |O nModo = 2;
               |ENTLINEAL #1#2,-3,2,21,0,MinMed,MaxMed;
               |ERROR10;
          |FINSI;
          |SI nModo = 4;
               |ENTLINEAL #1#2,-3,4,21,0,MinMed,MaxMed;
               |ERROR10;
          |FINSI;
     |FINSI;
|FPRC;
-------------------------------------------------------------------------------
|PROCESO BusPar; |TIPO 0;
     |HAZ Parametros;
     |SALVA_FICHA #2;
     nLinea = #2#2;
     |SELECT #2#2;
     |LEE 000#2=;
     |SI FSalida = 0; |Y nLinea ! #2#2;
          |MENAV "0000PARTIDA REPETIDA. ALTA NO PERMITIDA";
          |ESTADO_CONTROL nBotLPar, "SHOW";
          |ERROR10;
     |FINSI;
     |SELECT #1#2;
     |REPON_FICHA #2;
|FPRC;
-------------------------------------------------------------------------------
|PROCESO Parametros;
     cParam = #2#3;                     ||PARTIDA
     |QBF cParam;
     cPart = cParam;
     cParam = cParam % -101;
     |SI cParam = "$";                  ||COMPRUEBO SI ES PARAMETRICO
          |HAZ CargaIve;
          |SI FSalida ] 0;
               |HAZ Parametrico;
               #2#3 = cPartAct;
               |ABRE #6;                ||BUSCO DESCRIPCION Y UNIDAD MEDIDA
               #6#0 = #2#3;
               |LEE 000#6=;
               |SI FSalida = 0;
                    #2#4 = #6#1;        ||DESCRIPCION
                    #2#8 = #6#2;        ||UNIDAD DE MEDIDA
                    |PINTA #2#3;
                    |PINTA #2#4;
                    |PINTA #2#8;
               |FINSI;
               |CIERRA #6;
          |FINSI;
          |HAZ LiberaIve;
     |FINSI;
|FPRC;
-------------------------------------------------------------------------------
