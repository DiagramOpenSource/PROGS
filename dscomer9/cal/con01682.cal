|FICHEROS;
     con01682 #0;
     con00626 #1; || lineas O.R.
     con00682 #2;
     con00680 #3; ||Operarios
     con00625 #4; ||Cabecera O.R.
|FIN;

|VARIABLES;
aAlfa = "";
linea = 0;
orden = "";
precio = 0;
dato = "";
r_alfa="";
r_num = 0;
r_long = "";
operario = "";
|FIN;

|PROCESO busca_linea_siguiente;
     r_alfa = #2#5;
     |QBF r_alfa;
     orden = "000000";
     r_long = orden % A0;
     r_alfa = "000000" + r_alfa;
     r_num = -(r_long + 100);
     orden = r_alfa % r_num;
     #1#0 = #2#4;
     #1#1 = orden;
     #1#11 = 999;
     |LEE 000#1];
     |SI FSalida = 0;
          |LEE 000#1a;
     |SINO;
          |LEE 000#1u;
     |FINSI;
     |SI #1#0 = #2#4; |Y #1#1 = orden;
          linea = #1#11 + 1;
     |SINO;
          linea = 1;
     |FINSI;
|FPRC;

|PROCESO busca_importe_hora;
     #3#0 = #2#0;
     |LEE 001#3=;
     |SI FSalida ! 0 ; |DDEFECTO #3;|FINSI;
     precio = #3#2;
|FPRC;

|PROCESO graba_linea;
     |DDEFECTO #1;
     #1#0 =#2#4;||Serie O.R.
     #1#1 =orden;||N§ O.R.
     #1#11 =linea;||LINEA

     #4#0 = #1#0;
     #4#1 = #1#1;
     |LEE 000#4=;
     |SI FSalida ! 0 ; |DDEFECTO #4; |FINSI;

     #1#2 ="";||REFERENCIA (VACIO)
     operario = #2#0;
     #1#3 ="M.O. Operario: " + operario;||DESCRIP
     #1#4 =#2#10;||CANTIDAD
     #1#5 =precio;||PRECIO
     #1#6 =0;||DTO
     #1#7 =0;||IMP.DTO
     #1#8 =#1#4 * #1#5;||iMP.tOTAL
     #1#9 =1;||TIPO LINEA
     #1#10 =#4#7;||TIPO DE PARTE
     #1#12 =0;||LIN.M.OBRA REFER
     #1#13 =#2#6;||CANTIDAD REAL
     #1#14 =#4#12;||TIPO PARTE REAL
     #1#15 = 0;
     #1#16 = 0;
     #1#17 = 0;
     #1#18 = 0;
     #1#19 = 0;
     #1#20 = "N";
     #1#21 =#2#0 ;||operario
     #1#22 =#2#1 ;||Fecha
     #1#23 = #2#14;
     |GRABA 020#1n;
|FPRC;

|PROCESO lee_l;
     |HAZ busca_linea_siguiente;
     |SI linea > 9999;
          aAlfa = "0000 Se supera 9999 lineas, Orden:" + #2#4 + "/" + orden;
          |MENAV aAlfa;
     |SINO;
          |HAZ busca_importe_hora;
          |HAZ graba_linea;

          #2#11 = "S";
          |GRABA 020#2a;
          |LIBERA #2;
     |FINSI;
|FPRC;

|DEFBUCLE lee_lineas;
     #2#1;
     3,10,11;
     "    " , #0#0 , 01 , 1 , 0.01 , "N";
     "zzzz" , #0#1 , 99 , 1 ,99.99 , "N";
     be;
     NULL , lee_l;
|FIN;

|PROCESO tipo3; |TIPO 3;
     |ABRE #1;
     |ABRE #3;
     |ABRE #4;

     |BUCLE lee_lineas;
     |LIBERA #1;
     |LIBERA #3;
     |LIBERA #4;
     |CIERRA #1;
     |CIERRA #3;
     |CIERRA #4;

|FPRC;
