|FICHEROS;
     diefaz15 #0;
     referen@ #1;
     diefaw19 #19;
|FIN;

|VARIABLES;
     nOk = 0;
     i = 0;
     nLonCam = 0;
     nCamClave = 0;
     nPosCam = 0;
     nNume = 0;
     nPos = 0;
     nCampo = 0;
     aValor = "";
     aCamClave = "";
     aValClave = "";
     aCampo = "";
     aBase = "";
     aPath = "";
     aDes = "";
     aFic = "";
     aAlfa = "";
     aOrg = "";
     aEmpAnt = "";
     aDefAnt = "";
     aEmp = "";
     aDef = "";
     aLon = "";
     aPathApli1 = "";
     aPathApli2 = "";
     aFicDef = "";
     nSwCarga = 0;
     nConta = 0;
|FIN;

|PROCESO LeeReg;
     aValClave = #19#7;
     aAlfa = "|K000";
     aCamClave = #1aAlfa;
     aAlfa = aCamClave % 103;
     nCamClave = aAlfa;
     nPos = 4;
     nPosCam = 1;
     |PARA i = 1; |SI i [ nCamClave; |HACIENDO i = i + 1;
          nNume = nPos * 100 + 3;
          aCampo = aCamClave % nNume;
          nCampo = aCampo;

          aAlfa = "|C" + aCampo + "LONGITUD";
          aAlfa = #1aAlfa;
          nLonCam = aAlfa;
          nNume = nPosCam * 100 + nLonCam;
          aValor = aValClave % nNume;

          #1nCampo = aValor;
          nPos = nPos + 3;
          nPosCam = nPosCam + nLonCam + 1;
     |SIGUE;
     |LEE 101#1=;
     |SI FSalida ! 0; |GRABA 020#1b; |FINSI;
|FPRC;

|PROCESO CargaDef;
     aAlfa = aPathApli1 + "/def/" +  aFicDef;
     |CARGA_DEF aAlfa, referen@;
     |SI FSalida ! 0; |Y aPathApli2 ! "";
          aAlfa = aPathApli2 + "/def/" + aFicDef;
          |CARGA_DEF aAlfa, referen@;
          aPathApli1 = aPathApli2;
     |FINSI;
     nSwCarga = FSalida;

     |BLIN 21; |PINTA 2102, aAlfa;
|FPRC;

|PROCESO Log;
     nOk = 0;
     |SI #19#3 = "C"; nOk = 1; |FINSI;
     |SI #19#3 = "R"; |Y #19#4 = "32"; nOk = 1; |FINSI;
     |SI nOk = 0; |ACABA; |FINSI;

     aFicDef = #19#5; |QBF aFicDef;
     aEmp = #19#9;    |QBF aEmp;

     aLon = aEmp % A0;
     |SI aLon = 5;  || Cartera/Dscomer9
          aPathApli1 = aBase + "dscomer9";
          aPathApli2 = aBase + "dscarter";
     |FINSI;
     |SI aLon = 6;  || Agicont
          aPathApli1 = aBase + "agicont";
          aPathApli2 = "";
     |FINSI;
     |SI aLon = 2;  || Dscomer8
          aPathApli1 = aBase + "dscomer8";
          aPathApli2 = "";
     |FINSI;

     |SI aEmp ! aEmpAnt; |O aFicDef ! aDefAnt;
          |SI nSwCarga = 0;
               |CIERRA #1;
               |DESCARGA_DEF #1;
          |FINSI;
          aEmpAnt = aEmp;
          aDefAnt = aFicDef;
          |HAZ CargaDef;
          |SI nSwCarga = 0;
               aAlfa = aPathApli1 + "/fich/" + aEmp + "/";
               |PATH_DAT #1 aAlfa;
               |ABRE #1;
               |BLIN 22; |PINTA 2202, aAlfa;
          |FINSI;
     |FINSI;
     |SI nSwCarga ! 0; |ACABA; |FINSI;

     |DDEFECTO #1;
     |HAZ LeeReg;
     |SI #19#3 = "C";               || Modifica campo
          nCampo = #19#6;
          #1nCampo = #19#12;
          |GRABA 020#1a;
     |FINSI;
     |SI #19#3 = "R";               || Baja
          |BORRA 020#1a;
     |FINSI;
     |LIBERA #1;
|FPRC;

|DEFBUCLE Log;
     #19#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, NULL, NULL, NULL, NULL, Log;
     #19#9, #19#5, #19#1, #19#2; ||Empresa, Def, Fecha, Hora
|FIN;

|PROCESO Descomprime;
     aDes = aBase + "spool/ficlog.gz";
     |COPIA_FICHERO aFic, aDes;
     |DESCOMPRIME aDes;
     aFic = aDes - ".gz";
     aDes = aBase + "spool/ficlog.dat";
     |RENOMBRA_FICHERO aFic, aDes;
|FPRC;

|PROCESO Copia;
     aDes = aBase + "spool/ficlog";
     |COPIA_FICHERO aFic, aDes;
     aFic = aDes;
     aDes = aBase + "spool/ficlog.dat";
     |RENOMBRA_FICHERO aFic, aDes;
|FPRC;

|PROCESO Tipo3; |TIPO 3;
     |DBASS aBase;
     aPath = #0#0; |QBF aPath;
     |_PDIR aPath;
     |PARA; |SI FSalida = 0; |HACIENDO;
          aFic = aPath; |QBF aFic;
          aAlfa = aFic % -103;
          |SI aAlfa ! "tar";
               |BLIN 20; |PINTA 2002, aFic;
               |SI aAlfa = ".gz";
                    |HAZ Descomprime;
               |SINO;
                    |HAZ Copia;
               |FINSI;

               aAlfa = aBase + "spool/";
               |PATH_DAT #19 aAlfa;
               |NOME_DAT #19 "ficlog";
               aEmpAnt = "";
               nSwCarga = -1;
               |BUCLE Log;
               |DELINDEX #19;
               |SI nSwCarga = 0;
                    |CIERRA #1;
                    |DESCARGA_DEF #1;
               |FINSI;
          |FINSI;
          |_SDIR aPath;
          nConta = nConta + 1;
          ||SI nConta ] 3; |ACABA; |FINSI;
     |SIGUE;
|FPRC;
