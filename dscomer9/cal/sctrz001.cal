|FICHEROS;
     sctrz001 #0;
     sctrm005 #5;
     sctrm010 #10;
     sctrm011 #11;
     sctrm012 #12;       ||Lineas
     sctrm013 #13,MANTE; ||Lineas Paises
     sctrm014 #14,MANTE; ||Lineas Socios
     sctrm001 #100;
     sctrz002 #102,MANTE;||Pan Lineas
     agifa324 #324;
     referen@;
|FIN;

|VARIABLES;
     &enLinServi = 0;
     &eaProg = "";
     &enFinVen = 0;
     &eaNomLogo = "";
     &enPantaLogo = 0;
     &enCtrFin = 0;
     aAlfa = "";
     nModoLin = 0;
     nPanta = 0;
     nPantaLin = 0;
     nRango = 0;
     nGridLin = 0;
     nBtnAlta = 0;
     nBtnModi = 0;
     nBtnBaja = 0;
     nBtnCons = 0;
     nBtnReducciones = 0;
     nBtnTipoTarifas = 0;
     nBtnTariTrayectos = 0;
     nBtnTrayectosKms = 0;
     nBtnServiTempo = 0;
     nBtnFrecuServi = 0;
     nBtnPaises = 0;
     nBtnSocios = 0;
     nBloquea = 0;
     nId = 0;
     i = 0;
     nReg0 = 0;
     nLin = 0;
|FIN;

|PROCESO BtnReducciones;
     enFinVen = 3099;
     eaProg = "sctrm015";
     |HAZ EjecutaProg;
|FPRC;

|PROCESO BtnTipoTarifas;
     enFinVen = 3099;
     eaProg = "sctrz003";
     |HAZ EjecutaProg;
|FPRC;

|PROCESO BtnTariTrayectos;
     enLinServi = #12#0;
     enFinVen = 3099;
     eaProg = "sctrz007";
     |HAZ EjecutaProg;
|FPRC;

|PROCESO BtnTrayectosKms;
     enLinServi = #12#0;
     enFinVen = 3099;
     eaProg = "sctrz012";
     |HAZ EjecutaProg;
|FPRC;

|PROCESO BtnServiTempo;
     enLinServi = #12#0;
     enFinVen = 3099;
     eaProg = "sctrz015";
     |HAZ EjecutaProg;
|FPRC;

|PROCESO BtnFrecuServi;
     enLinServi = #12#0;
     enFinVen = 3099;
     eaProg = "sctrz017";
     |HAZ EjecutaProg;
|FPRC;

|PROCESO Socio; |TIPO 0;
     |ABRE #10;
     #10#0 = #14#1;
     |LEE 000#10=;
     |SI FSalida = 0;
          #14#2 = #10#1; |PINTA #14#2;
     |SINO;
          |MENAV "0000No existe c¢digo de socio...";
          |ERROR;
     |FINSI;
     |CIERRA #10;
|FPRC;

|PROCESO Min14;
     #14#0 = #102#0;
     #14#1 = "   ";
|FPRC;

|PROCESO Max14;
     #14#0 = #102#0;
     #14#1 = "zzz";
|FPRC;

|PROCESO BtnSocios;
     |VENTANA 1001, 2898, -1, 17, "Socios Grupo";
     |HAZ PushVenModal;

     |ABRE #14;
     |SI nModoLin = 4;
          nRango = 4 + 8 + 16 + 32;
     |SINO;
          nRango = 1 + 4 + 8 + 16 + 32;
     |FINSI;
     |LINEAL_SIMPLE #1#14, nRango, 1102, 2697, Min14, Max14, NULL;
     enCtrFin = FSalida;
     |HAZ EsperaFin;
     |CIERRA #14;

     |HAZ PopVenModal;
|FPRC;

|PROCESO Pais; |TIPO 0;
     |ABRE #5;
     #5#0 = #13#1;
     |LEE 000#5=;
     |SI FSalida = 0;
          #13#2 = #5#1; |PINTA #13#2;
     |SINO;
          |MENAV "0000No existe c¢digo de pais...";
          |ERROR;
     |FINSI;
     |CIERRA #5;
|FPRC;

|PROCESO Min13;
     #13#0 = #102#0;
     #13#1 = "    ";
|FPRC;

|PROCESO Max13;
     #13#0 = #102#0;
     #13#1 = "zzzz";
|FPRC;

|PROCESO BtnPaises;
     |VENTANA 1001, 2898, -1, 17, "Paises";
     |HAZ PushVenModal;

     |ABRE #13;
     |SI nModoLin = 4;
          nRango = 4 + 8 + 16 + 32;
     |SINO;
          nRango = 1 + 4 + 8 + 16 + 32;
     |FINSI;
     |LINEAL_SIMPLE #1#13, nRango, 1102, 2697, Min13, Max13, NULL;
     enCtrFin = FSalida;
     |HAZ EsperaFin;
     |CIERRA #13;

     |HAZ PopVenModal;
|FPRC;

|PROCESO Divisa;
     |ABRE #324;
     #324#0 = #102Campo;
     |LEE 000#324=;
     |SI FSalida ! 0;
          |MENAV "0000No existe c¢digo divisas...";
     |SINO;
          |HAZ PintaCampos;
     |FINSI;
     |CIERRA #324;
|FPRC;

|PROCESO Partenair;
     |ABRE #10;
     #10#0 = #102#10;
     |LEE 000#10=;
     |SI FSalida ! 0;
          |MENAV "0000No existe c¢digo partenair...";
          |ERROR;
     |SINO;
          #102#11 = #10#1; |PINTA #102#11;
     |FINSI;
     |CIERRA #10;
|FPRC;

|PROCESO Plantilla;
     |ABRE #11;
     #11#0 = #102#7;
     |LEE 000#11=;
     |SI FSalida ! 0;
          |MENAV "0000No existe c¢digo plantilla...";
     |SINO;
          |HAZ PintaCampos;
     |FINSI;
     |CIERRA #11;
|FPRC;

|PROCESO Chequea; |TIPO 0;
     |SI nModoLin = 1; |Y nBloquea = 0;
          |DDEFECTO #12;
          #12#0 = #102#0;
          |GRABA 000#12b;
          |SI FSalida ! 0;
               |MENAV "0000Registro existente...";
               |ERROR;
          |SINO;
               |ESTADO_CONTROL nBtnPaises, "ENABLE";
               |ESTADO_CONTROL nBtnSocios, "ENABLE";
               |C_M #102#0, 1, "N"; |PINTA #102#0;
               nBloquea = 1;

               aAlfa = "|X000"; aAlfa = #102aAlfa; nId = aAlfa;
               |ESTADO_CONTROL nId, "ENABLE";
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Valida7; |TIPO 7;
     |SI nModoLin = 1;
          aAlfa = "|X000"; aAlfa = #102aAlfa; nId = aAlfa;
          |ESTADO_CONTROL nId, "DISABLE";
     |FINSI;
|FPRC;

|PROCESO PintaCampos;
     |ABRE #11;
     #11#0 = #102#7;
     |LEE 000#11=;
     |SI FSalida ! 0; |DDEFECTO #11; |FINSI;
     |CIERRA #11;
     |PINTA 1530, #11#1;

     |ABRE #324;
     #324#0 = #102#13;
     |LEE 000#324=;
     |SI FSalida ! 0; |DDEFECTO #324; |FINSI;
     |CIERRA #324;
     |PINTA 2160, #324#1;

     |ABRE #324;
     #324#0 = #102#14;
     |LEE 000#324=;
     |SI FSalida ! 0; |DDEFECTO #324; |FINSI;
     |CIERRA #324;
     |PINTA 2260, #324#1;
|FPRC;

|PROCESO Borra14;
     |BORRA 020#14a;
|FPRC;

|DEFBUCLE Borra14;
     #14#1;
     ;
     #12#0, "   ";
     #12#0, "zzz";
     be;
     NULL, Borra14;
|FIN;

|PROCESO Borra13;
     |BORRA 020#13a;
|FPRC;

|DEFBUCLE Borra13;
     #13#1;
     ;
     #12#0, "    ";
     #12#0, "zzzz";
     be;
     NULL, Borra13;
|FIN;

|PROCESO Edita;
     nReg0 = #12#0;

     |SI nModoLin = 1; |VENTANA 0601, 2898, -1, 17, "Nueva L¡nea Servicio Viajeros"; |FINSI;
     |SI nModoLin = 2; |VENTANA 0601, 2898, -1, 17, "Modifica L¡nea Servicio Viajeros"; |FINSI;
     |SI nModoLin = 4; |VENTANA 0601, 2898, -1, 17, "Consulta L¡nea Servicio Viajeros"; |FINSI;
     |HAZ PushVenModal;

     |SI nModoLin = 1;
          |DDEFECTO #102;
          |C_M #102#0, 1, "S";
          |LEE 000#12u;
          |SI FSalida = 0;
               nLin = #12#0 + 1;
          |SINO;
               nLin = 1;
          |FINSI;
          #102#0 = nLin;
     |SINO;
          |PARA i = 0; |SI i [ 24; |HACIENDO i = i + 1;
               #102i = #12i;
          |SIGUE;
          |C_M #102#0, 1, "N";
     |FINSI;

     |PINPA #0#102; nPantaLin = FSalida;
     |PINDA #0#102;
     |HAZ PintaCampos;

     |CONTROL_SIMPLE nPantaLin, "BOTON,{{137}}Paises", 0680, 0698, BtnPaises;
     nBtnPaises = FSalida;
     |CONTROL_SIMPLE nPantaLin, "BOTON,{{137}}Socios Grupo", 0880, 0898, BtnSocios;
     nBtnSocios = FSalida;
     |SI nModoLin = 1;
          |ESTADO_CONTROL nBtnPaises, "DISABLE";
          |ESTADO_CONTROL nBtnSocios, "DISABLE";
     |FINSI;

     nBloquea = 0;
     |SI nModoLin = 1; |ENDATOS #1#102; |FINSI;
     |SI nModoLin = 2; |ENDATOS #2#102; |FINSI;
     |SI nModoLin = 4; |PAUSA; |FINSI;
     |SI FSalida = 0; |Y nModoLin ! 4;
          #12#0 = #102#0;
          |LEE 121#12=;
          |PARA i = 0; |SI i [ 24; |HACIENDO i = i + 1;
               #12i = #102i;
          |SIGUE;
          |GRABA 020#12a;
          nReg0 = #12#0;
     |SINO;
          |SI nBloquea = 1;
               #12#0 = #102#0;
               |BORRA 020#12c;
               |BUCLE Borra13;
               |BUCLE Borra14;
          |FINSI;
     |FINSI;
     |LIBERA #12;

     |HAZ PopVenModal;

     #12#0 = nReg0;
     |LEE 000#12=;
     |REFRESCACONTROL nGridLin;
|FPRC;

|PROCESO BtnAlta;
     nModoLin = 1; |HAZ Edita;
     |HAZ TieneLineas;
|FPRC;

|PROCESO BtnModi;
     |LEE 100#12=;
     |SI FSalida ! 0; |ACABA; |FINSI;
     nModoLin = 2; |HAZ Edita;
|FPRC;

|PROCESO BtnBaja;
     |LEE 100#12=;
     |SI FSalida ! 0; |ACABA; |FINSI;
     aAlfa = "0000N¨Borrar l¡nea: " + #12#0 + ".-" + #12#1; |QBF aAlfa;
     aAlfa = aAlfa + "?";
     |CONFI aAlfa;
     |SI FSalida = 0;
          |BORRA 020#12a;
          |BUCLE Borra13;
          |BUCLE Borra14;
          |HAZ TieneLineas;
          |REFRESCACONTROL nGridLin;
     |FINSI;
     |LIBERA #12;
|FPRC;

|PROCESO BtnCons;
     |LEE 000#12=;
     |SI FSalida ! 0; |ACABA; |FINSI;
     nModoLin = 4; |HAZ Edita;
|FPRC;

|PROCESO Evento12;
     |SI FSalida = 1; |O FSalida = 2;
          |SELECT #1#12;
          |LEE 000#12=;
          |HAZ TieneLineas;
     |FINSI;
     |SI FSalida = 4;
          |HAZ BtnCons;
     |FINSI;
|FPRC;

|PROCESO TieneLineas;
     |DDEF aAlfa;
     aAlfa = aAlfa + "sctrm012";
     |CARGA_DEF aAlfa, referen@;
     |SI FSalida = 0;
          |ABRE #referen@;
          |LEE 000#referen@.p;
          |SI FSalida = 0;
               |ESTADO_CONTROL nBtnModi, "ENABLE";
               |ESTADO_CONTROL nBtnBaja, "ENABLE";
               |ESTADO_CONTROL nBtnCons, "ENABLE";
               |ESTADO_CONTROL nBtnReducciones, "ENABLE";
               |ESTADO_CONTROL nBtnTipoTarifas, "ENABLE";
               |ESTADO_CONTROL nBtnTariTrayectos, "ENABLE";
               |ESTADO_CONTROL nBtnTrayectosKms, "ENABLE";
               |ESTADO_CONTROL nBtnServiTempo, "ENABLE";
               |ESTADO_CONTROL nBtnFrecuServi, "ENABLE";
          |SINO;
               |ESTADO_CONTROL nBtnModi, "DISABLE";
               |ESTADO_CONTROL nBtnBaja, "DISABLE";
               |ESTADO_CONTROL nBtnCons, "DISABLE";
               |ESTADO_CONTROL nBtnReducciones, "DISABLE";
               |ESTADO_CONTROL nBtnTipoTarifas, "DISABLE";
               |ESTADO_CONTROL nBtnTariTrayectos, "DISABLE";
               |ESTADO_CONTROL nBtnTrayectosKms, "DISABLE";
               |ESTADO_CONTROL nBtnServiTempo, "DISABLE";
               |ESTADO_CONTROL nBtnFrecuServi, "DISABLE";
          |FINSI;
          |CIERRA #referen@;
          |DESCARGA_DEF #referen@;
     |FINSI;
|FPRC;

|PROCESO LogoPanta;
     eaNomLogo = #100#1;
     |HAZ TraeLogoPan;
     enPantaLogo = nPanta;
     |HAZ PintaLogoPan;
|FPRC;

|PROGRAMA;
     |ABRE #100; |LEE 000#100p; |CIERRA #100;

     |PINPA #0#0; nPanta = FSalida;
     |PINDA #0#0;
     |HAZ LogoPanta;

     |CONTROL_SIMPLE nPanta, "BOTON,{{193}}Alta", 1002, 1012, BtnAlta;
     nBtnAlta = FSalida;
     |CONTROL_SIMPLE nPanta, "BOTON,{{197}}Modificar", 1014, 1024, BtnModi;
     nBtnModi = FSalida;
     |CONTROL_SIMPLE nPanta, "BOTON,{{199}}Baja", 1026, 1036, BtnBaja;
     nBtnBaja = FSalida;
     |CONTROL_SIMPLE nPanta, "BOTON,{{155}}Consulta", 1038, 1048, BtnCons;
     nBtnCons = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,{{137}}Reducciones", 2802, 2820, BtnReducciones;
     nBtnReducciones = FSalida;
     |CONTROL_SIMPLE nPanta, "BOTON,{{137}}Tipos Tarifas", 2822, 2840, BtnTipoTarifas;
     nBtnTipoTarifas = FSalida;
     |CONTROL_SIMPLE nPanta, "BOTON,{{137}}Tarifas Trayectos", 2842, 2860, BtnTariTrayectos;
     nBtnTariTrayectos = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,{{137}}Trayectos/Kil¢metros", 2902, 2920, BtnTrayectosKms;
     nBtnTrayectosKms = FSalida;
     |CONTROL_SIMPLE nPanta, "BOTON,{{137}}Servicios Temporada", 2922, 2940, BtnServiTempo;
     nBtnServiTempo = FSalida;
     |CONTROL_SIMPLE nPanta, "BOTON,{{137}}Frecuencia Servicios", 2942, 2960, BtnFrecuServi;
     nBtnFrecuServi = FSalida;

     |HAZ TieneLineas;
     |ABRET #12;
     nRango = 4 + 8 + 16 + 32;
     |LINEAL_SIMPLE #1#12, nRango, 1102, 2699, NULL, NULL, Evento12;
     nGridLin = FSalida;
     enCtrFin = nGridLin;
     |HAZ EsperaFin;
     |PULSATECLA;
     |CIERRAT #12;
|FPRO;
