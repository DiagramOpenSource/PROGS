     PreVto -> Creacion de prevencimientos

|FICHEROS;
     vilam002 #0;
     vilam003 #1;
     vilam004 #4;
     agifa084 #84;
     agifa091 #91;
|FIN;

|VARIABLES;
     &nDeci_Base = 0;
     fFecVto = @;
     i = 0;
     nEuro = 0;
     nResto = 0;
     nImpo = 0;
     nVtos = 0;
     nEntero = 0;
     nContaVto = 0;
     nA¤o = 0;
     aEuro = "";
     aAlfa = "";
     nCon = 0;
     f = @;
     aMes1 = "";
     aMes2 = "";
     nMes = 0;
|FIN;

|PROCESO UltDiaMes;
     f = fFecVto;
     aMes1 = f % 402;
     |PARA; |SI; |HACIENDO;
          f = f + 1;
          aMes2 = f % 402;
          |SI aMes1 ! aMes2; |SAL; |FINSI;
          fFecVto = f;
     |SIGUE;
|FPRC;

|PROCESO CalVto;
     |SI #0#29 = "A";
          nCon = nContaVto $ 1;
          aAlfa = "31.12.";
     |FINSI;
     |SI #0#29 = "S";
          nCon = nContaVto $ 2;
          |SI nCon = 0; aAlfa = "30.06."; |FINSI;
          |SI nCon = 1; aAlfa = "31.12."; |FINSI;
     |FINSI;
     |SI #0#29 = "C";
          nCon = nContaVto $ 3;
          |SI nCon = 0; aAlfa = "30.04."; |FINSI;
          |SI nCon = 1; aAlfa = "31.08."; |FINSI;
          |SI nCon = 2; aAlfa = "31.12."; |FINSI;
     |FINSI;
     |SI #0#29 = "T";
          nCon = nContaVto $ 4;
          |SI nCon = 0; aAlfa = "31.03."; |FINSI;
          |SI nCon = 1; aAlfa = "30.06."; |FINSI;
          |SI nCon = 2; aAlfa = "30.09."; |FINSI;
          |SI nCon = 3; aAlfa = "31.12."; |FINSI;
     |FINSI;
     |SI #0#29 = "B";
          nCon = nContaVto $ 6;
          |SI nCon = 0; aAlfa = "28.02."; |FINSI;
          |SI nCon = 1; aAlfa = "30.04."; |FINSI;
          |SI nCon = 2; aAlfa = "30.06."; |FINSI;
          |SI nCon = 3; aAlfa = "31.08."; |FINSI;
          |SI nCon = 4; aAlfa = "31.10."; |FINSI;
          |SI nCon = 5; aAlfa = "31.12."; |FINSI;
     |FINSI;
     |SI #0#29 = "M";
          nCon = nContaVto $ 12;
          nMes = nCon + 1;
          aAlfa = "28." + (("00" + nMes) % -102) + ".";
     |FINSI;
     |SI nContaVto ! 0;
          |SI nCon = 0; nA¤o = nA¤o + 1; |FINSI;
     |FINSI;
     aAlfa = aAlfa + nA¤o;
     fFecVto = aAlfa;
     |HAZ UltDiaMes;
     nContaVto = nContaVto + 1;
|FPRC;

|PROCESO CreaPreVto;
     nImpo = #0#20 - #0#19;
     nImpo = nImpo * #1#3 / 100;
     nVtos = #0#31;

     aEuro = "0" * nDeci_Base;
     aEuro = "1" + aEuro;
     nEuro = aEuro;
     nImpo = nImpo * nEuro;
     nImpo = nImpo ! 0;

     nResto = nImpo $ nVtos;
     nImpo = nImpo / nVtos;
     nEntero = nImpo ! 0;
     |SI nEntero ! nImpo;     ||* ...sino esta comparacion falla
          |SI nEntero > nImpo;
               nImpo = nEntero - 1;
          |SINO;
               nImpo = nEntero;
          |FINSI;
     |FINSI;
     nImpo = nImpo / nEuro;
     nResto = nResto / nEuro;


     aAlfa = #0#2 % 704;
     nA¤o = aAlfa;
     nContaVto = 0;
     |PARA i = 1; |SI i [ nVtos; |HACIENDO i = i + 1;
          |DDEFECTO #4;
          #4#0 = #0#0;
          #4#1 = #0#1;
          #4#2 = #1#4;
          #4#3 = i;
          |LEE 101#4=;
          |SI FSalida ! 0; |GRABA 021#4b; |FINSI;
          #4#4 = #1#5;
          #4#5 = #0#2;
          #4#12 = #0#15;

          |PARA; |SI; |HACIENDO;
               |HAZ CalVto;
               |SI fFecVto ] #0#2; |SAL; |FINSI;
          |SIGUE;

          |SI i = nVtos;
               nImpo = nImpo + nResto;
          |FINSI;

          #4#6 = fFecVto;
          #4#9 = nImpo;
          #4#11 = nImpo;

          |GRABA 021#4a;
          |LIBERA #4;
     |SIGUE;
|FPRC;

|PROCESO BorraPreVto;
     |BORRA 020#4a;
|FPRC;

|DEFBUCLE BorraPreVto;
     #4#1;
     ;
     #0#0, #0#1, "      ", 01;
     #0#0, #0#1, "zzzzzz", 99;
     be;
     NULL, BorraPreVto;
|FIN;

|PROCESO PreVto;
     |BUCLE BorraPreVto;

     |HAZ DecimalesBase;

     |ABRE #91;
     #91#0 = #0#17;
     |LEE 000#91=;
     |CIERRA #91;

     |ABRE #4;
     |BUCLELIN 0 CreaPreVto #0;
     |CIERRA #4;

     #0#23 = "S";
     |GRABA 020#0a;
|FPRC;
