|FICHEROS;
     diefaz13 #0;
     diefa007 #7;
     diefaw17 #17; ||Tmp
     agifa019 #19;
     agifa073 #73;
     agifa104 #104;
|FIN;

|VARIABLES;
     nPen = 0;
     aAlfa = "";
     nHay = 0;
|FINSI;

|PROCESO Tmp;
     |DDEFECTO #19;
     #19#0 = #17#1;
     |LEE 000#19=;

     #17#4 = #19#6 - #17#2 - #17#3;  || Nece
     #17#5 = #17#4 - #19#7;          || Nece Minimo

     |SI #0#8 = 2;
          |SI #17#4 ] 0; |ACABA; |FINSI;
     |FINSI;
     |SI #0#8 = 3;
          |SI #17#5 ] 0; |ACABA; |FINSI;
     |FINSI;

     #17#4 = -#17#4;
     #17#5 = -#17#5;

     nHay = 1;
     |PRINT;
|FPRC;

|DEFBUCLE Tmp;
     #17#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, Tmp;
|FIN;

|PROCESO Faltas;
     |SI #7#4 < #0#9; |O #7#4 > #0#10; ||Arti
          |ACABA;
     |FINSI;
     |DDEFECTO #19;
     #19#0 = #7#4;
     |LEE 000#19=;
     |SI #19#2 < #0#2; |O #19#2 > #0#3; |ACABA; |FINSI;  ||Familia
     |SI #19#125 < #0#0; |O #19#125 > #0#1; |ACABA; |FINSI; ||Linea
     |SI #19#27 < #0#11; |O #19#27 > #0#12; |ACABA; |FINSI; ||Proveedor

     |DDEFECTO #17;
     #17#0 = #19#125;
     #17#1 = #19#0;
     |LEE 101#17=;
     |SI FSalida ! 0; |GRABA 020#17b; |FINSI;
     #17#3 = #17#3 + #7#8;
     |GRABA 020#17a;
     |LIBERA #17;
|FPRC;

|DEFBUCLE Faltas;
     #7#3;
     13;
     "                    ", #0#6, "N";
     "zzzzzzzzzzzzzzzzzzzz", #0#7, "N";
     ;
     NULL, Faltas;
|FIN;

|PROCESO Pedidos;
     #104#25 = #73#28;
     #104#0 = #73#0;
     |LEE 000#104=;

     |SI #73#2 < #0#9; |O #73#2 > #0#10; |ACABA; |FINSI; || Arti
     |DDEFECTO #19;
     #19#0 = #73#2;
     |LEE 000#19=;
     |SI #19#2 < #0#2; |O #19#2 > #0#3; |ACABA; |FINSI;  ||Familia
     |SI #19#125 < #0#0; |O #19#125 > #0#1; |ACABA; |FINSI; ||Linea
     |SI #19#27 < #0#11; |O #19#27 > #0#12; |ACABA; |FINSI; ||Proveedor

     nPen = #73#5 - #73#43;
     |SI nPen > 0;
          |DDEFECTO #17;
          #17#0 = #19#125;
          #17#1 = #19#0;
          |LEE 101#17=;
          |SI FSalida ! 0; |GRABA 020#17b; |FINSI;
          #17#2 = #17#2 + nPen;
          |GRABA 020#17a;
          |LIBERA #17;
     |FINSI;
|FPRC;

|DEFBUCLE Pedidos;
     #73#2;
     ;
     "      ", #0#4, 0, 0, 0, "  ", "  ", 000001;
     "zzzzzz", #0#5, 99.99, 99.99, 99.99, "zz", "zz", 999999;
     ;
     NULL, Pedidos;
|FIN;

|PROCESO Tipo3; |TIPO 3;
     |IMPRE 0;
     |SI FSalida = 0;
          |INFOR "diefaz13";
          |SI FSalida ! 0;
               |MENAV "0000No existe informe 'diefaz13'";
          |SINO;
               aAlfa = Usuario;
               |NOME_DAT #17 aAlfa; |DELINDEX #17;
               |ABRE #19;
               |ABRE #104;
               |ABRE #17;
               |BUCLE Pedidos;
               |BUCLE Faltas;
               |CIERRA #17;
               |CIERRA #104;
               |BUCLE Tmp;
               |CIERRA #19;
               |SI nHay ! 0; |PIEINF; |FINSI;
               |FININF;
               |DELINDEX #17;
          |FINSI;
          |FINIMP;
     |FINSI;
|FPRC;
