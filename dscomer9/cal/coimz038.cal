|FICHEROS;
     coimz038 #0;
     coima002 #2;
     coima003 #3;
     agifa049 #49;
     coima057 #57;
     agifa048 #480;
     agifa019 #19;
|FIN;

|VARIABLES;
     {1}aLocal = "";
     aBaseLocal = "";
     aDes = "";
     aAlfa = "";
     nCoste = 0;
     aFic = "";
     nHandle = 0;
     aLF = "";
     aLon = "";
     aDato = "";
     nLon = 0;
     i = 0;
     nPos = 0;
     aCar = "";
     nNume = 0;
     nUniPro = 0;
     nTCoste = 0;
     nCostePro = 0;
     nDispo = 0;
     nInven = 0;
|FIN;

|PROCESO Punto_a_Coma;
     |QBF aAlfa;
     aAlfa = aAlfa;
     aLon = aAlfa % A0;
     aDato = "";
     |PARA i = 0; |SI i [ aLon; |HACIENDO i = i + 1;
          nPos = (i + 1) * 100 + 1;
          aCar = aAlfa % nPos;
          |SI aCar = "."; aCar = ","; |FINSI;
          aDato = aDato + aCar;
     |SIGUE;
     aAlfa = aDato;
|FPRC;

|PROCESO QuitaRarosExp;
     |QBF aAlfa;
     aAlfa = aAlfa;
     aLon = aAlfa % A0;
     aDato = "";
     |PARA i = 0; |SI i [ aLon; |HACIENDO i = i + 1;
          nPos = (i + 1) * 100 + 1;
          aCar = aAlfa % nPos;
          |SI aCar = "¥"; aCar = &209; |FINSI;
          |SI aCar = "¤"; aCar = &241; |FINSI;
          |SI aCar = " "; aCar = &225; |FINSI;
          |SI aCar = "‚"; aCar = &233; |FINSI;
          |SI aCar = "¡"; aCar = &237; |FINSI;
          |SI aCar = "¢"; aCar = &243; |FINSI;
          |SI aCar = "£"; aCar = &250; |FINSI;
          |SI aCar = "§"; aCar = &176; |FINSI;
          |SI aCar = "¦"; aCar = &170; |FINSI;
          |SI aCar = "°"; aCar = &176; |FINSI;  ||Euro

          |SI aCar = ";"; aCar = ","; |FINSI;
          aDato = aDato + aCar;
     |SIGUE;
     aAlfa = aDato;
|FPRC;

|PROCESO Graba;
     |HAZ QuitaRarosExp;
     aAlfa = aAlfa + ";";
     |XGRABA nHandle, aAlfa;
|FPRC;

|PROCESO agifa049;
     |SELECT #2#57;
     #57#0 = #3#0;
     #57#1 = #3#6;
     #57#3 = #49#2;
     |LEE 000#57=;
     |SI FSalida = 0;
          nCoste = #49#6 - (#49#6 * #49#13 / 100);
          #0#12 = #0#12 + nCoste;
     |FINSI;
|FPRC;

|DEFBUCLE agifa049;
     #49#1;
     ;
     #480#0, 001;
     #480#0, 999;
     ;
     NULL, agifa049;
|FIN;

|PROCESO HistoProceso;
     #480#0 = #3#13;
     |LEE 000#480=;
     |SI FSalida ! 0; |DDEFECTO #480; |FINSI;

     #2#0 = #3#0;
     #2#1 = #3#6;
     |LEE 000#2=;
     |SI FSalida ! 0; |DDEFECTO #2; |FINSI;

     #19#0 = #3#13;
     |LEE 000#19=;
     |SI FSalida ! 0; |DDEFECTO #19; |FINSI;

     #0#12 = 0;
     |BUCLE agifa049;

     |SI #0#11 = "S";
          ||Proceso
          aAlfa = #3#6; |HAZ Graba;

          ||Descripción Proceso
          aAlfa = #2#2; |HAZ Graba;

          ||Artículo
          aAlfa = #3#13; |HAZ Graba;

          ||Descripción Artículo
          aAlfa = #3#14; |HAZ Graba;

          ||Unidades Ini
          aAlfa = #3#7; |HAZ Punto_a_Coma; |HAZ Graba;

          ||Unidades Fin
          aAlfa = #3#8; |HAZ Punto_a_Coma; |HAZ Graba;

          ||Unidades Proceso
          nUniPro = #3#7 - #3#8;
          aAlfa = nUniPro; |HAZ Punto_a_Coma; |HAZ Graba;

          ||Coste Materia
          aAlfa = #480#8; |HAZ Punto_a_Coma; |HAZ Graba;

          ||Otros Costes
          aAlfa = #0#12; |HAZ Punto_a_Coma; |HAZ Graba;

          ||Total Costes
          nTCoste = #480#8 + #0#12;
          aAlfa = nTCoste; |HAZ Punto_a_Coma; |HAZ Graba;

          ||Total Coste Proceso
          nCostePro = nUniPro * nTCoste;
          aAlfa = nCostePro; |HAZ Punto_a_Coma; |HAZ Graba;

          ||Total Disponible
          nDispo = #19#6 * #19#9;
          aAlfa = nDispo; |HAZ Punto_a_Coma; |HAZ Graba;

          ||Total Inventario
          nInven = nCostePro + nDispo;
          aAlfa = nInven; |HAZ Punto_a_Coma; |HAZ Graba;

          |XGRABA nHandle, aLF;
     |SINO;
          |PRINT;
     |FINSI;
|FPRC;

|DEFBUCLE HistoProceso;
     #3#1;
     9, 6, 13, 15, 11;
     "F", "     ", 000001, 001, 000000, 000, #0#1, #0#5, #0#3, "N", #0#7;
     "F", "zzzzz", 999999, 999, 999999, 999, #0#2, #0#6, #0#4, "N", #0#8;
     ;
     NULL, HistoProceso;
|FIN;

|PROCESO T3; |TIPO 3;
     aLF = &13; aLF = aLF + &10;

     |ABRE #480;
     |ABRE #57;
     |ABRE #2;
     |ABRE #19;

     |SI #0#11 = "S";
          |DFICE aFic;
          aFic = aFic + Usuario;
          |XABRE "BW", aFic, nHandle;
          |SI FSalida ] 0;
               aAlfa = "Proceso;Descripción Proceso;Artículo;Descripción Artículo;";
               |XGRABA nHandle, aAlfa;
               aAlfa = "Unidades Iniciales;Unidades Finales;Unidades Proceso;Coste Materia;Otros Costes;Total Costes;";
               |XGRABA nHandle, aAlfa;
               aAlfa = "Total Coste Proceso;Total Disponible;Valor Inventario";
               |XGRABA nHandle, aAlfa;
               |XGRABA nHandle, aLF;

               |BUCLE HistoProceso;
               |XCIERRA nHandle;

               |ESPECIFICA "RBASS", aLocal;
               aBaseLocal = aLocal;
               aDes = aLocal + "tmp/" + Usuario + ".csv";
               |ENVIA_FICHERO aFic, aDes;
               |SI FSalida = 0;
                    aAlfa = "cmd " + &34 + " /c START /WAIT " + aDes + &34;
                    |RSYSTEM aAlfa;
               |SINO;
                    |MENAV "0000Error al enviar fichero...";
               |FINSI;
          |SINO;
               |MENAV "0000Error al crear temporal...";
          |FINSI;
     |SINO;
          aAlfa = #0#9; |QBF aAlfa;
          Impresora = "CrystalReport";
          |IMPRE -1;
          |SI FSalida = 0;
               |INFOR aAlfa;
               |SI FSalida = 0;
                    |BUCLE HistoProceso;
                    |PIEINF;
                    |FININF;
               |SINO;
                    |MENAV "0000Error en informe o no existe.";
               |FINSI;
               |FINIMP;
          |FINSI;
     |FINSI;

     |CIERRA #19;
     |CIERRA #2;
     |CIERRA #57;
     |CIERRA #480;
|FPRC;

|PROCESO Min;
     #2#0 = "F";
     #2#1 = 00;
|FPRC;

|PROCESO Max;
     #2#0 = "F";
     #2#1 = 99;
|FPRC;

|PROCESO T66; |TIPO 66;
     |ABRE #2;
     #2#0 = #0#10;
     #2#1 = #0Campo;
     |LEE 000#2=;
     |CONSULTA_F_CLAVE #1#2, Min, Max;
     |SI FSalida = 0;
          #0Campo = #2#1; |PINTA #0Campo;
     |SINO;
          |ERROR;
     |FINSI;
     |CIERRA #2;
|FPRC;

|PROCESO Excel; |TIPO 0;
     |SI #0#11 = "S";
          |C_M #0#9, 1, "N";
     |SINO;
          |C_M #0#9, 1, "S";
     |FINSI;
     |PINTA #0#11;
|FPRC;

|PROGRAMA;
     |ABRE #0;
     |LEE 101#0p;
     |SI FSalida ! 0; |GRABA 020#0b; |FINSI;
     |PINPA #0#0;
     |PINDA #0#0;
     |ENDATOS #1#0;
     |SI FSalida = 0; |GRABA 020#0a; |FINSI;
     |CIERRA #0;
|FPRO;
