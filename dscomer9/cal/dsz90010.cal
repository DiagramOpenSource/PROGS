||PLB Coimasa

|FICHEROS;
     agifa019 #19;
     agifa024 #24;

     coima043@ #543;
     coima016@ #516;

     coima001@ #501;      ||Parametros
     coima027@ #927;      ||Aux.Articulo/Almacen

     coima006@ #506;
     coimw012@ #912;      ||Tempo Kit Prod.Final
     coimw013@ #913;      ||Tempo Inter. Kit

     agifa048 #148;      ||Escandallos Cabs.
     agifa049 #49;       ||Escandallos Lins.

     agifa073 #73;       ||Pedidos Ventas Lins.
     agifa071 #71;       ||Albaranes Ventas Lins.
     dsm00225 #225;

     agifa073@ #973;     ||Para cargadef linea de pedido. (Para nUdVarPed)
|FIN;

|VARIABLES;
     nSwVengoCambioPrio = 0;

     nAux = 0;
     &enPrioAntes = 0;
     &enPrioAhora = 0;
     &eaSerPed = "";
     &enNumPed = 0;

     aAux = "";
     nUdVarPed = 0;
     aPathDef = "";
     aSeriePed = "";
     nNumeroPed = 0;
     nLinPed = 0;

     aAlfa = "";
     aPathCD = "";

     aTempo912 = "";
     aTempo913 = "";
     &Tempo = "";

     &eaSwCtrlStockReseCoimasa = "";
     &enReservadasPrio = 0;
     &enCoimaPrio = 0;
     &enCoimaPrioPed = 0;

     &eaArtCoimasa = "";
     &eaCliCoimasa = "";
     &eaRepCoimasa = "";

     &enPrioridadBajaPed = 0;
     aMensaje = "";

     &enModoCoimasa = 0;
     &enFormaCoimasa = 0;
     aArticulo = "";
     nUnidades = 0;
     nAlmacen = 0;
     nSwEsKit = 0;
     aArtStock = "";
     nStock = 0;
     nSwTempoKit = 0;
     aArtBucle = "";
     nCanBucle = 0;
     nDesglose = 0;

     &eaCodArtIdioma = "";
     &eaArtIdioma = "";
     aPais = "";
     aIdioma = "";
|FIN;

|PROCESO CtrlPrioridaReservarPed;          ||DESDE PEDIDOS. (LIN)
     |SI enModoCoimasa = 5; |O enModoCoimasa = 6;
          nUnidades = #agifa073#5 - #agifa073#43;
          |SI enModoCoimasa = 5;       ||Recalculo. (Alta)
              enModoCoimasa = 1;       ||Es como si fuera una alta
          |SINO;
              enModoCoimasa = 3;       ||Es como si fuera una baja
          |FINSI;
     |SINO;
          nUnidades = #agifa073#5;
     |FINSI;

     aArticulo = #agifa073#2;
     nAlmacen = #agifa073#3;
     aSeriePed = #agifa073#28;
     nNumeroPed = #agifa073#0;
     ||nUdVarPed = #agifa073#12;
     nUdVarPed = 0;      ||Desde pedidos. No se deben de tener en cuenta la variacion
                         ||solo desde albaranes. Pq. en pedidos si hay variacion
                         ||las unidades ya tiene el valor real (con la variacion)

     |HAZ CtrlPrioridaReservar;
|FPRC;

|PROCESO CtrlPrioridaReservarAlb;          ||DESDE ALBARANES. (LIN)
     aArticulo = #agifa071#2;
     nUnidades = #agifa071#5;
     nAlmacen = #agifa071#3;
     aSeriePed = #agifa071#31;
     nNumeroPed = #agifa071#12;
     nLinPed = #agifa071#21;

     |DDEF aPathDef;
     |QBF aPathDef;
     aAlfa = aPathDef + "agifa073";
     |CARGA_DEF aAlfa, agifa073@;
     |SI FSalida ! 0;
          |MENAV "0000Error al cargar 'agifa073@'";
          |ACABA;
     |FINSI;

     ||Falta el nUdVarPed.  (CargaDef del agifa073)
     |ABRE #agifa073@;
     #agifa073@#28 = aSeriePed;
     #agifa073@#0 = nNumeroPed;
     #agifa073@#1 = nLinPed;
     |LEE 000#agifa073@.=;
     |SI FSalida = 0;
          nUdVarPed = #agifa073@#12;
     |SINO;
          nUdVarPed = 0;
     |FINSI;
     |CIERRA #agifa073@;
     |DESCARGA_DEF #agifa073@;

     |HAZ CtrlPrioridaReservar;
|FPRC;

|PROCESO CtrlPrioridaReservar;
     |DDEF aAlfa;
     aPathCD = aAlfa;
     aAlfa = aPathCD + "coima001";
     |CARGA_DEF aAlfa, coima001@;
     |SI FSalida ! 0;
          |MENAV "0000Error al cargar 'coima001'";
          |ACABA;
     |FINSI;

     aAlfa = aPathCD + "coima027";
     |CARGA_DEF aAlfa, coima027@;
     |SI FSalida ! 0;
          |MENAV "0000Error al cargar 'coima027'";
          |DESCARGA_DEF #coima001@;
          |ACABA;
     |FINSI;

     aAlfa = aPathCD + "coima006";
     |CARGA_DEF aAlfa, coima006@;
     |SI FSalida ! 0;
          |MENAV "0000Error al cargar 'coima006'";
          |DESCARGA_DEF #coima027@;
          |DESCARGA_DEF #coima001@;
          |ACABA;
     |FINSI;

     aAlfa = aPathCD + "coimw012";
     |CARGA_DEF aAlfa, coimw012@;
     |SI FSalida ! 0;
          |MENAV "0000Error al cargar 'coimw012'";
          |DESCARGA_DEF #coima006@;
          |DESCARGA_DEF #coima027@;
          |DESCARGA_DEF #coima001@;
          |ACABA;
     |FINSI;

     aAlfa = aPathCD + "coimw013";
     |CARGA_DEF aAlfa, coimw013@;
     |SI FSalida ! 0;
          |MENAV "0000Error al cargar 'coimw013'";
          |DESCARGA_DEF #coimw012@;
          |DESCARGA_DEF #coima006@;
          |DESCARGA_DEF #coima027@;
          |DESCARGA_DEF #coima001@;
          |ACABA;
     |FINSI;

     |ABRE #coima001@;
     |DDEFECTO #coima001@;
     |LEE 000#coima001@.p;
     |CIERRA #coima001@;

     |ABRE #coima027@;
     |ABRE #coima006@;
     |DDEFECTO #coima006@;
     #coima006@#0 = aSeriePed;
     #coima006@#1 = nNumeroPed;
     |LEE 000#coima006@.=;
     |SI FSalida = 0;
          |SI #coima006@#2 [ #coima001@#27; |O nSwVengoCambioPrio = 1;
               |HAZ ActualizaStockReservado;
          |FINSI;
     |SINO;
          |SI enPrioridadBajaPed ! 0;
               |SI enPrioridadBajaPed [ #coima001@#27;
                    |HAZ ActualizaStockReservado;
               |FINSI;
               enPrioridadBajaPed = 0;
          |FINSI;
     |FINSI;
     |CIERRA #coima006@;
     |CIERRA #coima027@;

     |DESCARGA_DEF #coimw013@;
     |DESCARGA_DEF #coimw012@;
     |DESCARGA_DEF #coima006@;
     |DESCARGA_DEF #coima027@;
     |DESCARGA_DEF #coima001@;
|FPRC;

|PROCESO agifa049;
     nDesglose = nDesglose + 1;

     |DDEFECTO #913;
     #913#0 = #agifa049#2;
     |LEE 101#913.=;
     |SI FSalida ! 0; |GRABA 020#913.b; |FINSI;
     #913#1 = #913#1 + (nCanBucle * #agifa049#4);
     |GRABA 020#913.a;
     |LIBERA #913;
|FPRC;

|DEFBUCLE agifa049;
 #agifa049#1;
 ;
 aArtBucle,   1;
 aArtBucle, 999;
 ;
 NULL, agifa049;
|FIN;

|PROCESO Tempo912;
     aArtStock = #912#0;
     nStock = #912#1;
     |HAZ MeteStock;
|FPRC;

|DEFBUCLE Tempo912;
 #912#1001;
 ;
 "                    ";
 "zzzzzzzzzzzzzzzzzzzz";
 ;
 NULL, Tempo912;
|FIN;

|PROCESO ActualizaStockReservado;
     nSwEsKit = 0;
     |ABRE #agifa048;
     |DDEFECTO #agifa048;
     #agifa048#0 = aArticulo;
     |LEE 000#agifa048.=;
     |SI FSalida = 0;
          |SI #agifa048#33 = "K";
               nSwEsKit = 1;
          |FINSI;
     |FINSI;

     |SI nSwEsKit = 1;
          |HAZ CreaTempo;
          aTempo912 = Tempo;
          |NOME_DAT #912 aTempo912;
          |DELINDEX #912;
          |ABRE #912;

          |HAZ CreaTempo;
          aTempo913 = Tempo;
          |NOME_DAT #913 aTempo913;
          |DELINDEX #913;
          |ABRE #913;

          |DDEFECTO #913;
          #913#0 = aArticulo;
          |LEE 101#913.=;
          |SI FSalida ! 0; |GRABA 020#913.b; |FINSI;
          #913#1 = #913#1 + nUnidades - nUdVarPed;
          |GRABA 020#913.a;
          |LIBERA #913;

          |DDEFECTO #912;
          #912#0 = aArticulo;
          |LEE 101#912.=;
          |SI FSalida ! 0; |GRABA 020#912.b; |FINSI;
          #912#1 = nUnidades - nUdVarPed;
          |GRABA 020#912.a;
          |LIBERA #912;

          nDesglose = 0;
          |PARA; |SI; |HACIENDO;
               |LEE 101#913.p;
               |SI FSalida ! 0; |SAL; |FINSI;

               nSwTempoKit = 0;
               |DDEFECTO #agifa048;
               #agifa048#0 = #913#0;
               |LEE 000#agifa048.=;
               |SI FSalida = 0;
                    |SI #agifa048#33 = "K";
                         nSwTempoKit = 1;
                    |FINSI;
               |FINSI;
               |SI nSwTempoKit = 1;
                    aArtBucle = #913#0;
                    nCanBucle = #913#1;
                    |LIBERA #913;
                    |BUCLE agifa049;

                    #913#0 = aArtBucle;
                    |LEE 101#913.=;
                    |BORRA 020#913.a;
               |SINO;
                    |DDEFECTO #912;
                    #912#0 = #913#0;
                    |LEE 101#912.=;
                    |SI FSalida ! 0; |GRABA 020#912.b; |FINSI;
                    #912#1 = #912#1 + #913#1;
                    |GRABA 020#912.a;
                    |LIBERA #912;

                    |BORRA 020#913.a;
               |FINSI;
               |LIBERA #913;

               |SI nDesglose ] 100;
                    aMensaje = "0000Articulo con escandallo en bucle infinito";
                    |MENAV aMensaje;
                    |SAL;
               |FINSI;
          |SIGUE;
          ||ARA

          |CIERRA #913;
          |BUCLE Tempo912;
          |CIERRA #912;

          |DELINDEX #913; Tempo = aTempo913; |HAZ BoraTempo;
          |DELINDEX #912; Tempo = aTempo912; |HAZ BoraTempo;
     |SINO;
          aArtStock = aArticulo;
          nStock = nUnidades - nUdVarPed;
          |HAZ MeteStock;
     |FINSI;
     |CIERRA #agifa048;
|FPRC;

|PROCESO MeteStock;
     |DDEFECTO #coima027@;
     #coima027@#0 = aArtStock;
     #coima027@#1 = nAlmacen;
     |LEE 101#coima027@.=;
     |SI FSalida ! 0; |GRABA 020#coima027@.b; |FINSI;

     |SI enModoCoimasa = 1;
          #coima027@#6 = #coima027@#6 + nStock;
     |FINSI;
     |SI enModoCoimasa = 3;
          #coima027@#6 = #coima027@#6 - nStock;
     |FINSI;
     |SI enModoCoimasa = 2;
          |SI enFormaCoimasa = 1;
               #coima027@#6 = #coima027@#6 + nStock;
          |SINO;
               #coima027@#6 = #coima027@#6 - nStock;
          |FINSI;
     |FINSI;
     |GRABA 020#coima027@.a;
     |LIBERA #coima027@;
|FPRC;

|PROCESO IdiomaPedidoCoimasa;
     |DDEF aAlfa;
     aAlfa = aAlfa + "coima043";
     |CARGA_DEF aAlfa, coima043@;
     |SI FSalida = 0;
          aPais = #agifa024#205;

          |ABRE #dsm00225;
          |DDEFECTO #dsm00225;
          #dsm00225#0 = aPais;
          |LEE 000#dsm00225.=;
          |SI FSalida = 0;
               aIdioma = #dsm00225#5;
               |SI aIdioma ! "";
                    |ABRE #coima043@;
                    |DDEFECTO #coima043@;
                    #coima043@#0 = aIdioma;
                    #coima043@#1 = eaCodArtIdioma;
                    |LEE 000#coima043@.=;
                    |SI FSalida = 0;
                         eaArtIdioma = #coima043@#4;
                    |FINSI;
                    |CIERRA #coima043@;
               |FINSI;
          |FINSI;
          |CIERRA #dsm00225;
          |DESCARGA_DEF #coima043@;
     |SINO;
          |MENAV "0000Error al cargar 'coima043'";
     |FINSI;
|FPRC;

|PROCESO StockReservadoCoimasa;
     |DDEF aAlfa;
     aPathCD = aAlfa;
     aAlfa = aPathCD + "coima001";
     |CARGA_DEF aAlfa, coima001@;
     |SI FSalida ! 0;
          |MENAV "0000Error al cargar 'coima001'";
          |ACABA;
     |FINSI;

     aAlfa = aPathCD + "coima027";
     |CARGA_DEF aAlfa, coima027@;
     |SI FSalida ! 0;
          |MENAV "0000Error al cargar 'coima027'";
          |DESCARGA_DEF #coima001@;
          |ACABA;
     |FINSI;

     |ABRE #coima001@;
     |DDEFECTO #coima001@;
     |LEE 000#coima001@.p;

     eaSwCtrlStockReseCoimasa = #coima001@#28;
     enCoimaPrio = #coima001@#27;
     |QBF eaSwCtrlStockReseCoimasa;
     |SI eaSwCtrlStockReseCoimasa = "S"; |O eaSwCtrlStockReseCoimasa = "X";
          |ABRE #coima027@;
          |DDEFECTO #coima027@;
          #coima027@#0 = #agifa073#2;
          #coima027@#1 = #agifa073#3;
          |LEE 000#coima027@.=;
          |SI FSalida = 0;
               enReservadasPrio = #coima027@#6;
          |FINSI;
          |CIERRA #coima027@;
     |FINSI;
     |CIERRA #coima001@;

     |DESCARGA_DEF #coima027@;
     |DESCARGA_DEF #coima001@;

     |DDEF aAlfa;
     aAlfa = aAlfa + "coima006";
     |CARGA_DEF aAlfa, coima006@;
     |SI FSalida = 0;
           |ABRE #coima006@;
           #coima006@#0 = #agifa073#28;
           #coima006@#1 = #agifa073#0;
           |LEE 000#coima006@.=;
           |CIERRA #coima006@;
           enCoimaPrioPed = #coima006@#2;
           |DESCARGA_DEF #coima006@;
     |FINSI;
|FPRC;

|PROCESO CtrlRepCoimasa;
     |DDEF aAlfa;
     aAlfa = aAlfa + "coima016";
     |CARGA_DEF aAlfa, coima016@;
     |SI FSalida = 0;
          |ABRE #agifa019;
          |DDEFECTO #agifa019;
          #agifa019#0 = eaArtCoimasa;
          |LEE 000#agifa019.=;
          |SI FSalida = 0;
               |ABRE #coima016@;
               |DDEFECTO #coima016@;
               #coima016@#0 = #agifa019#125;
               |LEE 000#coima016@.=;
               |SI FSalida = 0;
                    |ABRE #agifa024;
                    |DDEFECTO #agifa024;
                    #agifa024#0 = eaCliCoimasa;
                    |LEE 000#agifa024.=;
                    |SI FSalida = 0;
                         |SI #coima016@#1 = 1; |Y eaRepCoimasa ! #agifa024#25;
                              aMensaje = "0000El representante deberia ser el 1: " + #agifa024#25;
                              |QBF aMensaje;
                              |MENAV aMensaje;
                         |FINSI;
                         |SI #coima016@#1 = 2; |Y eaRepCoimasa ! #agifa024#26;
                              aMensaje = "0000El representante deberia ser el 2: " + #agifa024#26;
                              |QBF aMensaje;
                              |MENAV aMensaje;
                         |FINSI;
                    |FINSI;
                    |CIERRA #agifa024;
               |FINSI;

               |CIERRA #coima016@;
          |FINSI;
          |CIERRA #agifa019;
          |DESCARGA_DEF #coima016@;
     |SINO;
          |MENAV "0000Error al cargar 'coima016'";
     |FINSI;
|FPRC;

|PROCESO agifa073_PrioReserva;
     nAux = #agifa073#5 - #agifa073#43;
     |SI nAux ! 0;
          |HAZ CtrlPrioridaReservarPed;
     |FINSI;
|FPRC;

|DEFBUCLE agifa073_PrioReserva;
 #agifa073#1;
 ;
 eaSerPed, enNumPed,   1;
 eaSerPed, enNumPed, 999;
 ;
 NULL, agifa073_PrioReserva;
|FIN;

|PROCESO CambioPrioReservaPed;
     nSwVengoCambioPrio = 1;
     |BUCLE agifa073_PrioReserva;
     nSwVengoCambioPrio = 0;
|FPRC;
