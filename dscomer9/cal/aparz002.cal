|FICHEROS;
     aparz002 #0;
     aparw003 #3,MANTE;
     aparw006 #6,MANTE;
     aparm016 #16;
     aparm017 #17;
     aparm021 #21;  || Histo Vehiculo
     aparm032 #32;  || Surtidor
     aparm034 #34;  || Histo Surtidor
     aparm035 #35;  || Hisot Kmts
|FIN;

|VARIABLES;
     &Tempo = "";
     nModo = 0;
     aTmp3 = "";
     aTmp6 = "";
     nSwLineal = 0;
     nForma = 0;
     nSurtidor = 0;
     nCod = 0;
     nUni = 0;
     i = 0;
     aAlfa = "";
     nLin = 0;
     nEntra = 0;
     nSale = 0;
     nHora = 0;
     nMinu = 0;
     nTiempo = 0;
     {-1}aMenu = "";
     aMenu1 = "2400";
     aMenu2 = "1";
     aMenu3 = "Opciones:";
     aMenu4 = "RAC";
     aMenu5 = " Repasar ";
     aMenu6 = " Aceptar ";
     aMenu7 = " Cancelar ";
|FIN;

|PROCESO ActSurtidor;
     #32#0 = #34#1;
     |LEE 101#32=;
     |SI FSalida = 0;
          #32#2 = #32#2 - (#34#4 * nForma);
          |GRABA 020#32a;
          |LIBERA #32;
          |SI #32#2 < #32#4; |Y nForma = 1;
               |SI nSurtidor ! #32#0;
                    nSurtidor = #32#0;
                    aAlfa = "0000Surtidor: '" + #32#0 + "' bajo mínimos...";
                    |MENAV aAlfa;
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Tmp6;
     #32#0 = #6#2;
     |LEE 020#32=;

     #34#0 = #6#4;
     |LEE 101#34=;
     |SI FSalida = 0;
          nForma = -1; |HAZ ActSurtidor;
          |SI #6#3 = 0;
               |BORRA 020#34a;
          |SINO;
               #34#1 = #6#2;
               #34#7 = #6#1;
               #34#4 = #6#3;
               |GRABA 020#34a;
               nForma = 1; |HAZ ActSurtidor;
          |FINSI;
     |SINO;
          |LEE 000#34u;
          |SI FSalida = 0;
               nLin = #34#0 + 1;
          |SINO;
               nLin = 1;
          |FINSI;
          |DDEFECTO #34;
          #34#0 = nLin;
          #34#1 = #6#2;
          #34#2 = "G";
          #34#3 = #0#0;
          #34#4 = #6#3;
          #34#5 = #32#5 * #34#4;
          #34#6 = #0#1;
          #34#7 = #6#1;
          |GRABA 020#34b;
          nForma = 1; |HAZ ActSurtidor;
     |FINSI;

     |LIBERA #34;
|FPRC;

|DEFBUCLE Tmp6;
     #6#2;
     ;
     INICIO;
     FINAL;
     ;
     NULL, Tmp6;
|FIN;

|PROCESO Tmp3;
     #21#0 = #3#0;
     #21#1 = #3#1;
     #21#2 = #3#2;
     |LEE 121#21=;
     |SI FSalida = 0;
          #21#7 = #3#7;
          #21#8 = #3#8;
          #21#9 = #3#9;
          #21#10 = #3#10;
          #21#11 = #3#11;
          |GRABA 020#21a;
          |LIBERA #21;
     |FINSI;

     #16#0 = #3#1;
     #16#1 = #3#2;
     |LEE 121#16=;
     |SI FSalida = 0;
          #16#11 = #3#7;
          #16#12 = #3#8;
          #16#13 = #3#9;
          #16#14 = #3#10;
          #16#15 = #3#11;
          |GRABA 020#16a;
          |LIBERA #16;
     |FINSI;
|FPRC;

|DEFBUCLE Tmp3;
     #3#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, Tmp3;
|FIN;

|PROCESO Tiempo; |TIPO 0;
     |SI #3Campo ! Contenido;
          nEntra = #3#7 * 60 + #3#8;
          nSale = #3#9 * 60 + #3#10;
          |SI nSale < nEntra; || Si es menor es la hora del dia siguiente
               nSale = nSale + (24 * 60);
          |FINSI;
          nTiempo = nSale - nEntra;
          nMinu = nTiempo $ 60;
          nHora = (nTiempo - nMinu) / 60;
          ||#3#11 = nHora + (nMinu / 100);
          #3#11 = nHora + (nMinu / 60);
          |PINTA #3#11;
     |FINSI;
|FPRC;

|PROCESO Tipo6_2; |TIPO 2;
     nModo = (FEntrada / 100) ! 0;
     nForma = 0 +. 1;
     |SI nModo = 3;
          |SI nForma = -1;
               |MENAV "0000Se modifican los litros con '0'";
               #6#3 = 0; |GRABA 020#6a;
          |FINSI;
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO Tecla6_11; |TIPO 11;
     |SI FSalida = 12;
          nSwLineal = 1;
          |PTEC 807;
     |FINSI;
     |SI FSalida = 16;
          #0#2 = #0#2 - #0#3;
          |C_M #0#3, 1, "S";
          |ENCAMPO #3#0;
          |C_M #0#3, 1, "N";
          #0#2 = #0#2 + #0#3; |PINTA #0#2;
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO Tecla3_11; |TIPO 11;
     |SI FSalida = 12;
          nSwLineal = 2;
          |PTEC 807;
     |FINSI;
     |SI FSalida = 16;
          #0#2 = #0#2 - #0#3;
          |C_M #0#3, 1, "S";
          |ENCAMPO #3#0;
          |C_M #0#3, 1, "N";
          #0#2 = #0#2 + #0#3; |PINTA #0#2;
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO Tecla3_0; |TIPO 0;
     |SI #3Campo = Contenido; |Y FSalida = 0;
          |ET E1;
               |ENCAMPO #7#3;
               |SI FSalida = 2; |VETE E1; |FINSI;
               |SI FSalida ! 0; |Y FSalida ! 3; |VETE F1; |FINSI;
          |ET E2;
               |ENCAMPO #8#3;
               |SI FSalida = 2; |VETE E1; |FINSI;
               |SI FSalida ! 0; |Y FSalida ! 3; |VETE F1; |FINSI;
          |ET E3;
               |ENCAMPO #9#3;
               |SI FSalida = 2; |VETE E2; |FINSI;
               |SI FSalida ! 0; |Y FSalida ! 3; |VETE F1; |FINSI;
          |ET E4;
               |ENCAMPO #10#3;
               |SI FSalida = 2; |VETE E3; |FINSI;
               |SI FSalida ! 0; |Y FSalida ! 3; |VETE F1; |FINSI;
          |ET E5;
               |ENCAMPO #11#3;
               |SI FSalida = 2; |VETE E4; |FINSI;
               |SI FSalida ! 0; |Y FSalida ! 3; |VETE F1; |FINSI;
               |GRABA 020#3a;
          |ET F1;
     |FINSI;
|FPRC;

|PROCESO Min3;
     #3#0 = #0#1;
     #3#1 = "     ";
     #3#2 = 000001;
|FPRC;

|PROCESO Max3;
     #3#0 = #0#1;
     #3#1 = "zzzzz";
     #3#2 = 999999;
|FPRC;

|PROCESO Min6;
     #6#0 = #0#1;
     #6#1 = "   ";
     #6#2 = 000;
|FPRC;

|PROCESO Max6;
     #6#0 = #0#1;
     #6#1 = "zzz";
     #6#2 = 999;
|FPRC;

|PROCESO HistoGasoil;
     #6#0 = #34#6;
     #6#1 = #34#7;
     #6#2 = #34#1;
     #6#3 = #34#4;
     #6#4 = #34#0;
     |GRABA 020#6n;
|FPRC;

|DEFBUCLE HistoGasoil;
     #34#2;
     ;
     "G", #0#1, #0#0;
     "G", #0#1, #0#0;
     ;
     NULL, HistoGasoil;
|FIN;

|PROCESO HistoVta;
     |PARA i = 0; |SI i [ 13; |HACIENDO i = i + 1;
          #3i = #21i;
     |SIGUE;
     |GRABA 020#3n;
|FPRC;

|DEFBUCLE HistoVta;
     #21#2;
     ;
     #0#1, #0#0;
     #0#1, #0#0;
     ;
     NULL, HistoVta;
|FIN;

|PROCESO Tipo3; |TIPO 3;
     |ABRE #35;
     #35#0 = #0#1;
     #35#1 = #0#0;
     |LEE 000#35=;
     |SI FSalida = 0;
          #0#3 = #35#2; |PINTA #0#3;
     |FINSI;
     |CIERRA #35;

     |HAZ CreaTempo; |NOME_DAT #3#Tempo#; aTmp3 = Tempo; |DELINDEX #3;
     |HAZ CreaTempo; |NOME_DAT #6#Tempo#; aTmp6 = Tempo; |DELINDEX #6;
     |ABRE #3;
     |ABRE #6;
     |BUCLE HistoVta;
     |BUCLE HistoGasoil;
     |CIERRA #6;
     |CIERRA #3;
     |SI i = 0;
          |CONFI "0000SNo hay registros. ¿Introducir Kms?";
          |SI FSalida = 0;
               |C_M #0#3, 1, "S";
               |ENCAMPO #3#0;
               |SI FSalida = 0;
                    |ABRE #17;
                    #17#0 = #0#1;
                    |LEE 121#17=;
                    |SI FSalida = 0;
                         |DDEFECTO #35;
                         |ABRE #35;
                         #35#0 = #0#1;
                         #35#1 = #0#0;
                         |LEE 101#35=;
                         #17#38 = #17#38 - #35#2;
                         |SI FSalida ! 0; |GRABA 020#35b; |FINSI;
                         #35#2 = #0#3;
                         #17#38 = #17#38 + #35#2;
                         |GRABA 020#17a;
                         |GRABA 020#35a;
                         |CIERRA #35;
                    |FINSI;
                    |CIERRA #17;
               |FINSI;
          |FINSI;
     |SINO;
          |ENTLINEAL #1#6, 2, 7, 20, 0, Min6, Max6;
          |PARA; |SI; |HACIENDO;
               |PARA nSwLineal = 1; |SI nSwLineal = 1; |HACIENDO;
                    |SI nSwLineal = 1;
                         nSwLineal = 0;
                         |ENTLINEAL #1#3, 2, 4, 20, 0, Min3, Max3;
                    |FINSI;
                    |SI nSwLineal = 2;
                         nSwLineal = 0;
                         |ENTLINEAL #1#6, 2, 2, 20, 0, Min6, Max6;
                    |FINSI;
               |SIGUE;
               |MENU aMenu;
               aMenu2 = FSalida;
               |SI aMenu2 = 2; |O aMenu2 = 3; |SAL; |FINSI;
          |SIGUE;
          |SI aMenu2 = 2;
               |ABRE #21;
               |ABRE #34;
               |ABRE #32;
               |ABRE #16;
               |BUCLE Tmp3;
               |BUCLE Tmp6;
               |CIERRA #16;
               |CIERRA #32;
               |CIERRA #34;
               |CIERRA #21;

               |ABRE #17;
               #17#0 = #0#1;
               |LEE 121#17=;
               |SI FSalida = 0;
                    |DDEFECTO #35;
                    |ABRE #35;
                    #35#0 = #0#1;
                    #35#1 = #0#0;
                    |LEE 101#35=;
                    |SI FSalida ! 0; |GRABA 020#35b; |FINSI;
                    #17#38 = #17#38 - #35#2;
                    #35#2 = #0#3;
                    #17#38 = #17#38 + #35#2;
                    |GRABA 020#17a;
                    |GRABA 020#35a;
                    |CIERRA #35;
               |FINSI;
               |CIERRA #17;

          |FINSI;
     |FINSI;
     Tempo = aTmp3; |HAZ BoraTempo; |DELINDEX #3;
     Tempo = aTmp6; |HAZ BoraTempo; |DELINDEX #6;
|FPRC;

|PROCESO Tipo12; |TIPO 12;
|FPRC;

|PROGRAMA;
     |C_A #17#1, 1, 32;
     |CLS;
     |PINPA #0#0;
     |PINDA #0#0;
     |ENTLINEAL #1#3, 2, 7, 20, 0, Min3, Max3;
     |ENTLINEAL #1#6, 2, 7, 20, 0, Min6, Max6;
     |ENDATOS #1#0;
|FPRO;
