|FICHEROS;
     tabusagr #0;
|FIN;

|VARIABLES;
     &xnombre = "";
     &xhuespe = "";
     &xhabita = 0;
     &xhotel  = 0;

     tempo     = "";
     param     = "";
     agr       = 0;
     alfa      = "";
     clave     = 0;
     fecha     = @;
     hay       = 0;
     alf1      = "";
     BuscaConte = "";
     tecla     = 0;
     c1        = "";
     c2        = 0;
     c9        = 0;
     actual    = "";
     valor     = "";
     antes     = "";

     sw_busca  = 0;
     des       = "";
     org       = "";
     path      = "";
|FIN;
------------------------------------------
|PROCESO BuscaConte;
     alfa = #0#1;
     alfa = alfa > "";
     alf1 = alfa - BuscaConte;
     |SI alf1 ! alfa;
          hay = 1;
     |SINO;
          |BORRA 021#0a;
     |FINSI;
|FPRC;

|DEFBUCLE BuscaConte;
     #0#1;
     ;
     "      ", 00000, 00;
     "zzzzzz", 99999, 99;
     ;
     NULL, BuscaConte;
|FIN;
------------------------------------------
|PROGRAMA;
     |CLS;
     |CABEZA "Consulta";

     tempo = Usuario; |QBF tempo;
     tempo = ("HT" + tempo) % 108;

     param = PARAMETRO; |QBF param;
     |NOME_DAT #0 param;

     |ABRE #0;           || Comprueba si hay mas de un hotel
     |SELECT #2#0;
     |DDEFECTO #0;
     |LEE 000#0];
     agr = #0#9;
     #0#9 = #0#9 + 1;
     |LEE 000#0];
     |SI FSalida = 0; |Y #0#9 > agr;
          clave = 1; agr = 1;
     |SINO;
          clave = 2; agr = 0;
     |FINSI;
     |CIERRA #0;

     xhabita = 0;
     xhuespe = "";
     xnombre = "";
     xhotel = 0;
     BuscaConte = "";
     |PARA; |SI clave > 0; |HACIENDO;
          |SI clave = 1;
               |C_M #0#1, 1, "N"; |C_M #0#2, 1, "N"; |C_M #0#9, 1, "S";
               |PINPA #0#0;
               |ATRI S; |PINTA 0839, "Ht"; |ATRI 0;
               clave = 0;
               |ENTLINEAL #2#0, 1, 4, 20, 0, inf, sup;
          |FINSI;
          |SI clave = 2;
               |C_M #0#1, 1, "N"; |C_M #0#2, 1, "S"; |C_M #0#9, 1, "N";
               |PINPA #0#0;
               |ATRI S; |PINTA 0842, "Habi."; |ATRI 0;
               clave = 0;
               |ENTLINEAL #3#0, 1, 4, 20, 0, inf, sup;
          |FINSI;
          |SI clave = 3;
               |C_M #0#1, 1, "S"; |C_M #0#2, 1, "N"; |C_M #0#9, 1, "N";
               |PINPA #0#0;
               |ATRI S; |PINTA 0811, " Nombre                    "; |ATRI 0;
               clave = 0;
               |ENTLINEAL #4#0, 1, 4, 20, 0, inf, sup;
          |FINSI;
          |SI BuscaConte ! "";
               |SI sw_busca = 0;
                    sw_busca = 1;
                    |DFICE path; |QBF path;
                    org = path + param + ".ixx"; des = path + tempo + ".ixx";
                    |COPIA_FICHERO org, des;
                    org =  path + param + ".dat"; des = path + tempo + ".dat";
                   |COPIA_FICHERO org, des;
               |FINSI;
               hay = 0;
               |BUCLE BuscaConte;
               BuscaConte = "";
               |SI hay = 0;
                    |MENAV "0000ннн No se encuentran coincidencias... !!!";
                    |SAL;
               |FINSI;
          |FINSI;
     |SIGUE;
     |SI sw_busca = 1;
          |DFICE path; |QBF path;
          org = path + tempo + ".ixx"; des = path + param + ".ixx";
          |COPIA_FICHERO org, des;
          org = path + tempo + ".dat"; des = path + param + ".dat";
          |COPIA_FICHERO org, des;
     |FINSI;
|FPRO;

|PROCESO inf;
     |SI FSalida = "POSICION";
          #0#1 = c1;
          #0#2 = c2;
          #0#9 = c9;
     |SINO;
          #0#1 = "                              ";
          #0#2 = 00001;
          #0#9 = 01;
     |FINSI;
|FPRC;

|PROCESO sup;
     #0#1 = "zzzzzzzzzzzzzzzzzzzzzzzzzzzzzz";
     #0#2 = 99999;
     #0#9 = 99;
|FPRC;

|PROCESO intro; |TIPO 0;
     alfa = #0Campo % 102;
     |SI Campo = 1; |Y alfa = "\\"; |Y FSalida = 0;
          clave = 3;
          BuscaConte = #0Campo - "\\";
          |QBF BuscaConte;
          BuscaConte = BuscaConte > "";
          |PTEC 807;
     |SINO;
          |C_M #0Campo, 0, alfa;
          |SI alfa = "S"; BuscaConte = ""; |FINSI;
     |FINSI;
     valor = #0Campo;    |QBT valor;
     antes = Contenido;  |QBT antes;
     |C_M #0Campo, 0, alfa;
     |SI FSalida = 0; |Y valor = antes; |Y alfa = "S";
          xhuespe = #0#0;
          xnombre = #0#1;
          xhabita = #0#2;
          xhotel  = #0#9;
          |PTEC 806;
     |FINSI;
|FPRC;

|PROCESO tecla; |TIPO 11;
     tecla = FSalida;
     |SI tecla = 9; |O tecla = 10;
          |SI tecla = 9;
               |SI Campo = 9; clave = 3; |FINSI;
               |SI Campo = 2; |Y agr = 1; clave = 1; |FINSI;
               |SI Campo = 2; |Y agr ! 1; clave = 3; |FINSI;
               |SI Campo = 1; clave = 2; |FINSI;
          |SINO;
               |SI Campo = 9; clave = 2; |FINSI;
               |SI Campo = 2; clave = 3; |FINSI;
               |SI Campo = 1; |Y agr = 1; clave = 1; |FINSI;
               |SI Campo = 1; |Y agr ! 1; clave = 2; |FINSI;
          |FINSI;
          c1 = #0#1;
          c2 = #0#2;
          c9 = #0#9;
          |PTEC 807;
     |FINSI;
|FPRC;
