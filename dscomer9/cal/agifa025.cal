|FICHEROS;
     agifa025 #0;
     agifa142 #1;
     agifa321 #5;
     agifa324 #6;
     referen@;
|FIN;

|VARIABLES;
     &endigC   = 0;
     &eaCta    = "";
     &eswCta   = 0;
     nSal = 0;
     &nDeci_Base = 0;
     &nDeci_PreBase = 0;
     mes = 0; tot = 0; i = 0;
     informe = "agifa025";
     puente = "";
     puente1 = "";
     modo = 0;
     cuen = "";
     quitar = 0;
     cod    = 0;
     &aClave   = "";
     &aMenClave= "";
     aAlfa     = "";

     aNifQbf1   = "";
     aNifQbf2   = "";

     &eaVarDni  = "";
     &enCalcCif = 0;
|FIN;

|PROCESO letra_com;
     |SI FSalida = 9;
         eaVarDni   = #0Campo;
         enCalcCif  = 1;
         |HAZ CalculoNIFCIF;
         #0Campo = eaVarDni;
         |PINTA #0Campo;
         |ERROR;
         |ACABA;
     |FINSI;

     eaVarDni  = #0Campo;
     enCalcCif = 0;
     |HAZ CalculoNIFCIF;
     aNifQbf1 = #0Campo;   |QBF aNifQbf1;
     aNifQbf2 = eaVarDni;  |QBF aNifQbf2;

     |SI aNifQbf1 ! aNifQbf2; |Y aNifQbf1 ! "";
         |CONFI "0000SEl Nif puede ser Erroneo. Desea Calcularlo.";
         |SI FSalida = 0;
             #0Campo = eaVarDni;  |PINTA #0Campo;
             |ERROR;
         |FINSI;
     |FINSI;
|FPRC;

|CALCULO totalcom;|TIPO 0;
mes = Campo - 11;
i = mes $ 2;
tot = 35 + i;
#0tot = #0tot - Contenido + #0Campo;
|PINTA #0tot;

|SI i = 0;
mes = (mes / 2) + 40;
#0#52 = #0#52 - #0mes;
#0mes = #0Campo % #0#39;
#0#52 = #0#52 + #0mes;
|PINTA #0mes;|PINTA #0#52;
|FINSI;
|FCAL;

|CALCULO todoirpf;|TIPO 0;
|SI #0Campo ! Contenido;

#0#52 = 0;
|PARA i = 0;|SI i < 12;|HACIENDO i = i + 1;
     tot = i * 2;tot = tot + 11;
     mes = i + 40;
     #0mes = #0tot % #0#39;
     #0#52 = #0#52 + #0mes;
     |PINTA #0mes;
|SIGUE;
|PINTA #0#52;
|FINSI;
|FCAL;

|PROCESO cp3;
     puente = #0#58; puente = "00" + puente; puente = puente % -102;
     puente1 = puente;
     puente = #0#59; puente = "000" + puente; puente = puente % -103;
     puente1 = puente1 + puente;
     #0#60 = puente1;
|FPRC;

|PROCESO cuenta1; |TIPO 0;
     modo = (FEntrada / 100) ! 0;
     |SI modo = 1;
          cod = #0#0;
          |SI cod ! 0;
               |ABRE #1; |LEE 000#1p; |CIERRA #1;
               cuen = "000000000000" + cod;
               |QBT cuen;
               quitar = -(100 + (#1#11 - 2));
               cuen = cuen % quitar;
               cuen = "44" + cuen;
               #0#56 = cuen;
               |PINTA #0#56;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO MCta25; |TIPO 0;
  eaCta = #0Campo;
  |HAZ MFiltraPtos;
  #0Campo = eaCta; |PINTA #0Campo;

  |HAZ MCtaContable;
  |SI eswCta = 1;
      |MENAV "    Error. Longitud de Cuenta fuera de Nivel";
      |ERROR;
  |FINSI;
|FPRC;

|PROCESO tipo3; |TIPO 3;
     #0#54 = "N";
     #0#55 = "00.00.0000";

     |ABRE #1; |LEE 000#1p; |CIERRA #1;

     |SI #1#135 ! "S"; |ACABA; |FINSI;

     |MENSA "0000Clave para Modificacion de Documentos.";
     |SI #0#63 = 0;
          aMenClave = "          Entre la CLAVE:";
          |HAZ Clave;
          aAlfa = aClave;
          aMenClave = "        Reentre la CLAVE:";
          |HAZ Clave;
          |SI aAlfa ! aClave;
               |MENAV "0000CLAVE incorrecta";
          |SINO;
               #0#63 = aClave;
          |FINSI;
     |SINO;
          aAlfa = #0#63;
          aMenClave = "          Entre la CLAVE:";
          |HAZ Clave;
          |SI aAlfa ! aClave;
               |MENAV "0000Clave incorrecta...";
          |SINO;
               |CONFI "2400NModificar CLAVE (S/N).: ";
               |SI FSalida = 0;
                    aMenClave = "   Entre la Nueva CLAVE :";
                    |HAZ Clave;
                    aAlfa = aClave;
                    aMenClave = "       Reentre la CLAVE :";
                    |HAZ Clave;
                    |SI aAlfa ! aClave;
                         |MENAV "0000CLAVE incorrecta, NO SE MODIFICA...";
                    |SINO;
                         #0#63 = aClave;
                    |FINSI;
               |FINSI;
          |FINSI;
     |FINSI;
     |BLIN 4;
|FPRC;

|PROCESO LeeDecimBase25; |TIPO 13;
     |ABRE #agifa321;
     |ABRE #agifa324;
     |LEE 001#agifa321.p;
     #agifa324#0 = #agifa321#9; ||Igualo a moneda base
     |LEE 001#agifa324.=;
     nDeci_Base = #agifa324#7;  ||Decimales moneda base
     nDeci_PreBase = #agifa324#9;  ||Decimales precio de moneda base
     |CIERRA #agifa321;
     |CIERRA #agifa324;

     |HAZ EsVigor;
     |SI FSalida = 0;
          |CUADRO 2106 2374;
          |PINTA 2208, "Importe Complemento Fijo Comision:";
          |DDEF aAlfa; aAlfa  = aAlfa + "vigm0029";
          |CARGA_DEF aAlfa, referen@;
          |SI FSalida = 0;
               |ABRE #referen@;
               |DDEFECTO #referen@;
               #referen@#0 = #0#0;
               |LEE 000#referen@.=;
               |CIERRA #referen@;
               |ATRI I; |PINTA 2244, #referen@#1; |ATRI 0;
               |DESCARGA_DEF #referen@;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO DatoModulo; |TIPO 0;
     nSal = FSalida;

     |HAZ EsVigor;
     |SI FSalida = 0; |Y nSal = 0;
          |DDEF aAlfa; aAlfa  = aAlfa + "vigm0029";
          |CARGA_DEF aAlfa, referen@;
          |SI FSalida = 0;
               |ABRE #referen@;
               |DDEFECTO #referen@;
               #referen@#0 = #0#0;
               |LEE 101#referen@.=;
               |SI FSalida ! 0; |GRABA 020#referen@.b; |FINSI;
               |ENCAMPO #1#referen@;
               |GRABA 020#referen@.a;
               |CIERRA #referen@;
               |DESCARGA_DEF #referen@;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Alta25; |TIPO 7;
     modo = (FEntrada / 100) ! 0;
     |SI modo = 1;
          |HAZ LeeDecimBase25;
     |FINSI;
|FPRC;
