|FICHEROS;
     diaw0002 #0;
     diam0001 #1;
     diam0003 #3;
     diam0005 #5,MANTE;
     diam0006 #6;
     agifa024 #24;
     agifa104 #104;
     agifa007 #7;
     agifa073 #73;
     agifa321;
     agifa324;

     agifa104@ #1004;
     dsparm01@ #100;
     dstelw01@ #111;
     dstelm02@ #200;
     dsparm05@ #500;
     dstelm03@ #300;
     dmaplica@;
|FIN;

|VARIABLES;
     &nDeci_Base     = 0;   || Decimales en Totales de la moneda base
     &nDeci_PreBase  = 0;   || Decimales en el Precio en moneda base
     &nDeci_BaseMxP  = 0;   || Decimales en Totales de la moneda base (Maxima precision)
     &nDeci_PreBasMxP= 0;   || Decimales en el Precio en moneda base (Maxima precision)
     &nDeci_DivP     = 0;   || Decimales en Totales de la divisa
     &nDeci_PreDivP  = 0;   || Decimales en el Precio en divisa
     &nDeci_TotVisP  = 0;   || Decimales en Totales visibles (el de la moneda de trabajo)
     &nDeci_TotNoVisP = 0;  || Decimales en Totales no visibles (el de la otra moneda)
     &nDeci_PreVisP  = 0;   || Decimales en el Precio visual. (lineas)
     &nDeci_ImpVisP  = 0;   || Decimales en el Importe visual. (lineas)
     &nDeci_PreVis  = 0;    || para agifa023
     &nDeci_ImpVis  = 0;    ||
     &ac_divisa = "N";   || Vale "S" cuando las divisas en ventas estan
                         || activadas y la serie es de divisas
     &pintado = 0;       || Sirve para controlar los pintados de las lineas
     &decim_divisa = 0;  || Decimales de la divisa
     &precio_divisa = 0; || Decimales del precio en divisa
     &decim_base = 0;    || Decimales de la moneda base
     &precio_base = 0;   || Decimales del precio en moneda base
     div_act = 0;        || Divisa actualizada. Me sirve en un PARA para que
                         || no salga hasta que este actualizada (div_act=1)
     &cli_divisa = "";   || Codigo de Cliente. Se pasa a CLIENTES/DIVISAS
     &divisa = "";       || Codigo de Divisa. Se pasa a ACTUAL. de DIVISAS

     nSwTodo   = 0;
     aDat      = "";
     aDefa     = "";
     aDef5     = "";
     aDef2     = "";
     aDef3     = "";
     aDef1     = "";
     aDef10    = "";
     aBase     = "";
     aVisado   = "";
     aAlfa1    = "";
     nSiHay    = 0;
     &eaSer    = "";
     &enNum    = 0;
     &enTec1   = 0;
     &enTec2   = 0;
     nTiquet   = 0;
     nModo     = 0;
     aAlfa     = "";
     nLin      = 0;
     aHora     = "";
     nErr      = 0;
     aDirOri   = "";
     aDirDes   = "";
     aTitulo   = "";
     aTexto    = "";
     aFic      = "";
     aDSCorreo = "";
     aProxy    = "";
     nSwVisa   = 0;
     nNum1     = 0;
     nNum2     = 0;
     nHora     = 0;
     nMinu     = 0;
     nDias     = 0;
     nResto    = 0;
     nFecha    = 0;
     fFecha    = @;

     clave = "";
     nume1 = 0;
     camuf = 0;
     camufz = "";
     para1 = 0;
     para2 = 0;
     alfa1 = "";
     alfa2 = "";

     aIP       = "";
     aServidor = "";
     aFrom     = "";
     aTo       = "";
     aSubject  = "";
     aMensaje  = "";
     aDjunto   = "Sin asunto";
     aNume     = "";

     nTecnico  = 0;
     nTecPrepe = 0;
     nTecRepre = 0;
     nBucle    = 0;
|FIN;
컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
|PROCESO LaClave;
     |BLIN 24;
     |PINTA 2418, "Entre la clave...:";

     clave = "";
     E_sup = 10;
     E_inf = "";
     clave = 2439 ? -1;
     |BLIN 24;
     |HAZ Encripta;
     |SI camufz ! #500#5;
          |MENAV "0000Clave incorrecta...";
          |ERROR;
     |FINSI;
|FPRC;
컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
|PROCESO LaPutaHora;
     nHora = (A\(#300#11 % 102));
     nMinu = (A\(#300#11 % -102));
     nMinu = nHora * 60 + nMinu;
     nMinu = nMinu + #300#5 * 60;
     nMinu = nMinu - 540;            || - 09:00

     nResto = nMinu $ 480;           || $ Jornada
     nDias = ((nMinu - nResto) / 480); || Inc de Dias
     |SI nResto > 300;               || 5h. ma쨅na
          nResto = nResto + 120;     || 2h. para la tarde
     |FINSI;
     nResto = nResto + 540;          || + 09:00
     nFecha = #300#6;
     nFecha = nFecha + nDias;

     nHora = (nResto - (nResto $ 60)) / 60;
     nMinu = nResto - (nHora * 60);

     fFecha = fFecha % A1;
     aAlfa = fFecha % 1101;

     |SI aAlfa = "S";
          nFecha = nFecha + 2;
     |FINSI;
     |SI aAlfa = "D";
          nFecha = nFecha + 1;
     |FINSI;
     fFecha = nFecha;

     |SI fFecha = "01.01.0000";
          fFecha = ~;
     |FINSI;

     #5#6 = fFecha;
     #5#11 = (("00" + nHora) % -102) + ":" + (("00" + nMinu) % -102);
|FPRC;

|PROCESO CalHora; |TIPO 0;
     |ACABA;
          || en deshuso
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 1;
          FSalida = 0;
      ||  |CONFI "0000SCalular agenda...";
          |SI FSalida = 0;
               |HAZ CargaDef;
               |ABRE #300; |SELECT #2#300;
               #300#9 = "A";
               #300#5 = #3#3;
               #300#10 = "31.12.9999";
               #300#11 = "23:59";
               |LEE 000#300];
               |SI FSalida = 0;
                    |LEE 000#300a;
               |SINO;
                    |LEE 000#300u;
               |FINSI;
               |SI FSalida = 0; |Y #300#5 = #3#3; |Y #300#9 = "A";
                    |HAZ LaPutaHora;
               |SINO;
                    |HORA aHora;
                    #5#6 = ~;
                    #5#11 = aHora;
               |FINSI;
               |PINTA #5#6; |PINTA #5#11;
               |CIERRA #300;
               |HAZ DescargaDef;
          |FINSI;
     |FINSI;
|FPRC;


|PROCESO Hora0; |TIPO 0;
     |ACABA;
          || en deshuso
     nNum1 = (A\(#5#11 % 102)) $ 24;
     nNum2 = (A\(#5#11 %-102)) $ 60;
     #5#11 = (("00" + nNum1) % -102) + ":" + (("00" + nNum2) % -102)
     |PINTA #5#11;
|FPRC;
컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴

|PROCESO Maildiam0003;
     |SI nBucle = 1;
         |SI #3#2 = nTecPrepe;  |ACABA;  |FINSI;
         |SI #3#2 = nTecRepre;  |ACABA;  |FINSI;

         nTecnico = #3#2;
     |FINSI;

     |DDEFECTO #500;
     #500#0 = #0#2;
     |LEE 020#500=;
     aDirOri = #500#3; |QBF aDirOri;

     |DDEFECTO #500;
     #500#0 = nTecnico;
     |LEE 020#500=;
     aDirDes = #500#4; |QBF aDirDes;
     |SI aDirDes = "";
          aDirDes = #500#3;
     |SINO;
          aDirDes = aDirDes + ";" + #500#3; |QBF aDirDes;
     |FINSI;
     |QBF aDirDes;

     |SI aDirDes = "";  |ACABA;  |FINSI;

     aDSCorreo = #100#8; |QBF aDSCorreo;
     aProxy    = #100#7; |QBF aProxy;

     aTitulo = "Pedido Bloqueado: " + #104#25 + "." + #104#0 + " " + #104#4 + " " + #104#8;
     |QBF aTitulo;
     aTexto = aTitulo + " ---> " + #diaw0002#6;
     |QBF aTexto;
     aFic = "vacio";

     aAlfa = "2405Enviando [" + aDSCorreo + "] ..."; |MENSA aAlfa;
     |DSCORREO_ENVIA aDSCorreo, aProxy, aDirOri, aDirDes, aTitulo, aTexto, aFic;
     |BLIN 24;
     |SI FSalida < 0;
          aAlfa = "000Problemas al enviar correo, tecnico:" + nTecnico;
          |MENAV aAlfa;
     |FINSI;
|FPRC;

|DEFBUCLE Maildiam0003;
     #3#1;
     ;
     #0#0, #0#1, 0001;
     #0#0, #0#1, 9999;
     ;
     NULL, Maildiam0003;
|FIN;

|PROCESO EnvioInvalidez;
     nBucle    = 0;
     nTecRepre = 0;
     nTecPrepe = #100#12;

     |ABRE #500;
     |SELECT #4#500;
     #500#6 = #104#7;
     |LEE 000#500=;
     |SI FSalida = 0;
          nTecRepre = #500#0;
     |FINSI;
     |SELECT #1#500;

     |SI nTecPrepe ! 0;
         nTecnico  = nTecPrepe;  |HAZ Maildiam0003;
     |FINSI;

     |SI nTecRepre ! 0;
         nTecnico  = nTecRepre;  |HAZ Maildiam0003;
     |FINSI;

     nBucle    = 1;
     |BUCLE Maildiam0003;
|FPRC;

컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
|PROCESO Correo;
     |DDEFECTO #500;
     #500#0 = #0#2;
     |LEE 020#500=;
     aDirOri = #500#3; |QBF aDirOri;

     |DDEFECTO #500;
     #500#0 = #5#3;
     |LEE 020#500=;
     aDirDes = #500#4; |QBF aDirDes;
     |SI aDirDes = "";
          aDirDes = #500#3;
     |SINO;
          aDirDes = aDirDes + ";" + #500#3; |QBF aDirDes;
     |FINSI;
     |QBF aDirDes;

     aDSCorreo = #100#8; |QBF aDSCorreo;
     aProxy    = #100#7; |QBF aProxy;

     aTitulo = "Consultar AGENDA: Asignado Tiquet ";
     aAlfa = ("00000" + #300#0) % -105; aTitulo = aTitulo + aAlfa;
     aAlfa = ("00000" + #300#1) % -104; aTitulo = aTitulo + "." + aAlfa;
     aTitulo = aTitulo + " de " + #104#4 + " " + #104#8;
     aTexto = aTitulo;
     aFic = "fichero.txt";

     aAlfa = "2405Enviando [" + aDSCorreo + "] ..."; |MENSA aAlfa;
     |DSCORREO_ENVIA aDSCorreo, aProxy, aDirOri, aDirDes, aTitulo, aTexto, aFic;
     |BLIN 24;
     |SI FSalida < 0;
          |MENAV "    Problemas al enviar correo. Revisar conexion.";
     |SINO;
          |MENAV "    Envio correo completado.";
     |FINSI;
|FPRC;

|PROCESO diam0006;
     |SI nLin < 61;
          nLin = nLin + 1;
          |SI nLin = 14; nLin = 19; |FINSI;
          |SI nLin = 26; nLin = 52; |FINSI;
          #200nLin = #6#5;
     |FINSI;

     #111#5 = #104#4;
     #111#0 = nTiquet;
     #111#1 = "M";
     #111#4 = 1;
     #111#2 =  #6#4-1;

     #111#3 = #6#5;
     |GRABA 020#111n;
|FPRC;

|DEFBUCLE diam0006;
     #6#1;
     ;
     #5#0, #5#1, #5#2, #5#3, 2;
     #5#0, #5#1, #5#2, #5#3, 99;
     ;
     NULL, diam0006;
|FIN;

|PROCESO CargaDef;
     |SI nErr = 1; |ACABA; |FINSI;

     |CARGA_DEF aDef1, dstelw01@;
     |SI FSalida ! 0;
          aAlfa = "0000Error al cargar " + aDef1;
          |MENAV aAlfa; nErr = 1;
     |FINSI;

     |CARGA_DEF aDef2, dstelm02@;
     |SI FSalida ! 0;
          aAlfa = "0000Error al cargar " + aDef2;
          |MENAV aAlfa; nErr = 1;
     |FINSI;

     |CARGA_DEF aDef3, dstelm03@;
     |SI FSalida ! 0;
          aAlfa = "0000Error al cargar " + aDef3;
          |MENAV aAlfa; nErr = 1;
     |FINSI;

     |CARGA_DEF aDef10, dsparm01@;
     |SI FSalida ! 0;
          aAlfa = "0000Error al cargar " + aDef10;
          |MENAV aAlfa; nErr = 1;
     |FINSI;

     |CARGA_DEF aDefa, dmaplica@;
     |SI FSalida ! 0;
          aAlfa = "0000Error al cargar " + aDefa;
          |MENAV aAlfa; nErr = 1;
     |FINSI;

     |SI nErr = 0;
          |PATH_DAT #100 aDat;
          |PATH_DAT #200 aDat;
          |PATH_DAT #300 aDat;
          |PATH_DAT #111 aDat;
          |PATH_DAT #dmaplica@#aDat#;

          |ABRE #100; |LEE 020#100p; |CIERRA #100;
     |FINSI;
|FPRC;

|PROCESO DescargaDef;
     |DESCARGA_DEF #dmaplica@;
     |DESCARGA_DEF #dsparm01@;
     |DESCARGA_DEF #dstelm03@;
     |DESCARGA_DEF #dstelm02@;
     |DESCARGA_DEF #dstelw01@;
|FPRC;

|PROCESO CreaTiquet;
     |HAZ CargaDef;
     |SI nErr = 1; |ACABA; |FINSI;

     |ABRE #104;
     #104#25 = #5#0;
     #104#0 = #5#1;
     |LEE 020#104=;
     |CIERRA #104;

     |ABRE #24;
     #24#0 = #104#4;
     |LEE 000#24=;
     |CIERRA #24;

     |ABRE #200;
     #200#5 = #104#4;
     #200#0 = 99999;
     |LEE 000#200];
     |SI FSalida = 0;
          |LEE 000#200a;
     |SINO;
          |LEE 000#200u;
     |FINSI;
     |SI FSalida = 0; |Y #200#5 = #104#4;
          nTiquet = #200#0 + 1;
     |SINO;
          nTiquet = 1;
     |FINSI;
     |HORA aHora;
     aHora = aHora % 105;

     |DDEFECTO #200;
     #200#5 = #104#4;
     #200#0 = nTiquet;
     #200#1 = ~;
     #200#2 = aHora;
     #200#3 = #0#2;
     #200#5 = #24#0;
     #200#6 = #24#1;
     #200#10 = #5#12;
     aAlfa1 = "Pedido: " + #diaw0002#0;   |QBF aAlfa1;
     aAlfa1 = aAlfa1 + "." + #diaw0002#1;
     #200#11 = aAlfa1;                            || pedido en motivo1
     #200#37 = #5#5;                              || horas previstas
     #200#38 = "N";                               || a web no
     #200#40 = "P";                               || tipo tiquet
     #200#41 = "U";                               || los pedidos siempre urgen
     #200#42 = 90;
     #200#44 = #0#0;
     #200#45 = #0#1;
     #200#73 = #0#7;
     nLin    = 10;
     |ABRE #111;
     |BUCLE diam0006;
     |CIERRA #111;

     |GRABA 020#200n;
     |CIERRA #200;

     |DDEFECTO #300;
     |ABRE #300;
     #300#8 = #200#5;
     #300#0 = #200#0;
     #300#1 = 1;
     #300#2 = ~;
     |HORA aAlfa; aAlfa = aAlfa % 105;
     #300#3 = aAlfa;
     #300#4 = #0#2;
     #300#5 = #5#3;
     #300#6 = #0#3;

     |DDEFECTO #500;
     #500#0 = #5#3;
     |LEE 000#500=;

     #300#7 = #500#1;
     #300#8 = #104#4;
     #300#9 = "A";
     #300#10 = #5#6;
     #300#11 = #5#11;
     #300#12 = #5#5;
     #300#17 = #200#40;       || tipo de tiquet
     #300#19 = #200#10;
     |GRABA 020#300n;
     |CIERRA #300;

     |HAZ Correo;

     |HAZ DescargaDef;
|FPRC;
컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
|PROCESO TodoVisado;
     |SI #3#4 ! "S";
          nSwTodo = 0;
          |ERROR10;
     |FINSI;
|FPRC;

|DEFBUCLE TodoVisado;
     #3#1;
     ;
     #0#0, #0#1, 0001;
     #0#0, #0#1, 9999;
     ;
     NULL, TodoVisado;
|FIN;

|PROCESO VisaPedido;
     |SI #3#4 ! "S";
          aVisado = "";
          |ERROR10;
     |SINO;
          |SI #3#4 = "I";
               aVisado = "BLOQUEADO";
               |ERROR10;
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE VisaPedido;
     #3#1;
     ;
     #0#0, #0#1, 0001;
     #0#0, #0#1, 9999;
     ;
     NULL, VisaPedido;
|FIN;

|PROCESO Tipo12; |TIPO 12;
     |SI FSalida = 1;|O FSalida = 7;    |ACABA;   |FINSI;

     #3#0 = #0#0;
     #3#1 = #0#1;
     #3#2 = #0#2;
     |LEE 101#3=;
     |SI FSalida = 0;
          |SI #diaw0002#5 = "S";   |CONFI "0000SVisar pedido?";       |FINSI;
          |SI #diaw0002#5 = "I";   |CONFI "0000SBloquear pedido?";    |FINSI;
          |SI FSalida = 0;
               |SI #3#4 = #0#5;|Y #diaw0002#5 = "S";
                    |MENAV "0000El pedido ya estaba visado por este tecnico.";
               |FINSI;
               #3#4 = #0#5;        || visado
               #3#5 = ~;           || fecha visado
               |SI #0#4 = "N";
                    #3#6 = "C";    || se cierra si va sin tecnicos
               |FINSI;
               #3#7 = #0#7;
               |GRABA 020#3a;

               |SI #diaw0002#5 = "S";
                    aVisado = "VISADO";
                    |BUCLE VisaPedido;
               |FINSI;
               |SI #diaw0002#5 = "I";
                    aVisado = "BLOQUEADO";
               |FINSI;
               |ABRE #104;
               #104#25 = #0#0;
               #104#0 = #0#1;
               |LEE 121#104=;
               |SI FSalida = 0;
                    #104#51 = aVisado;

                    |ABRE #500;
                    |SELECT #1#500;
                    #500#0 = #3#2;
                    |LEE 000#500=;
                    |SI FSalida = 0;  |Y #500#7 = "SOP";
                        #104#62 = #0#7;
                    |FINSI;
                    |CIERRA #500;

                    |GRABA 020#104a;
                    |LIBERA #104;
               |FINSI;
               |CIERRA #104;

               |SI #0#5 = "I";
                    |HAZ CargaDef;
                    |SI nErr = 0;
                         |HAZ EnvioInvalidez;
                         |MENAV "    Envio correo completado.";
                    |FINSI;
                    |HAZ DescargaDef;
               |FINSI;

               |SI #0#4 = "S";|Y #0#5 = "S";
                    |HAZ SacaLin;
               |FINSI;

               nSwTodo = 1;
               |BUCLE TodoVisado;
               |SI nSwTodo = 1;
                    |HAZ CargaDef;
                    |HAZ CorreoPrepe;
                    |HAZ DescargaDef;
               |FINSI;
          |FINSI;
     |SINO;
          |SI #0#1 ! 0;
               |MENAV "0000Tecnico inexistente es este pedido...";
          |FINSI;
     |FINSI;
     |LIBERA #3;
|FPRC;

|PROCESO diam0003;
     #agifa104@#25 = #diam0003#0;
     #agifa104@#0 = #diam0003#1;
     |LEE 000#agifa104@.=;
     |SI FSalida ! 0;
          #agifa104#25 = #diam0003#0;
          #agifa104#0 = #diam0003#1;
          |LEE 000#agifa104.=;
          |SI FSalida = 0;
               |SALVA_DATOS #agifa104;
               |REPON_DATOS #agifa104@;
               |GRABA 020#agifa104@.n;
               nSiHay = 1;
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE diam0003;
     #diam0003#2;
     ;
     #diaw0002#2, "N";
     #diaw0002#2, "N";
     ;
     NULL, diam0003;
|FIN;

|PROCESO ConsultaPedidos;
     aAlfa1 = "";
     |DDEF aAlfa1;
     aAlfa1 = aAlfa1 + "agifa104.mas";
     |CARGA_DEF aAlfa1, agifa104@;
     |SI FSalida = 0;
          nSiHay = 0;
          aAlfa1 = Usuario;
          |QBF aAlfa1;
          |NOME_DAT #agifa104@#aAlfa1#;
          |DELINDEX #agifa104@;
          |ABRE #agifa104@;
          |ABRE #agifa104;
          |BUCLE diam0003;
          |CIERRA #agifa104@;
          |CIERRA #agifa104;
          |SI nSiHay ! 0;
               |ABRE #agifa104@;
               |CONSULTA_CLAVE #1#agifa104@;
               |SI FSalida = 0;
                    #diaw0002#0 = #agifa104@#25;  |PINTA #diaw0002#0;
                    #diaw0002#1 = #agifa104@#0;   |PINTA #diaw0002#1;
               |FINSI;
               |CIERRA #agifa104@;
          |SINO;
               |MENAV "0000No se encuentran pedidos pendientes de visar.";
          |FINSI;
     |FINSI;
     |DESCARGA_DEF #agifa104@;
|FPRC;

|PROCESO Sale; |TIPO 6;
     |SI FSalida = 1;
          |ERROR;
          |PTEC 807;
     |FINSI;
     |SI FSalida = 13;
          |HAZ ConsultaPedidos;
          |ERROR;
     |FINSI;
     |SI FSalida = 14;
          |PUSHV 05012380;
          |ABRE #104;
          #104#25 = #0#0;
          #104#0 = #0#1;
          |LEE 000#104=;
          |SI FSalida = 0;
               |PINPA #0#104;
               |PINDA #0#104;
               |HAZ LeeMonBase;
               |HAZ LeeParamDiv;
               |HAZ Param_Divisas;
               |ENDATOS #4#104;
          |SINO;
               |MENAV "0000No existe el pedido...";
          |FINSI;
          |CIERRA #104;
          |POPV;
          |ERROR;
     |FINSI;
     |SI FSalida = 15;
          |PUSHV 05012380;
          |ABRE #1; |SELECT #2#1;
          #1#14 = #0#0;
          #1#15 = #0#1;
          |LEE 000#1=;
          |SI FSalida = 0;
               |PINPA #0#1;
               |PINDA #0#1;
               |ENDATOS #4#1;
          |SINO;
               |MENAV "0000Pedido sin Pre-pedido...";
          |FINSI;
          |CIERRA #1;
          |POPV;
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO DiagTecnico; |TIPO 66;
     |HAZ NomTecnico;
     |CONSULTA_CLAVE #1#500;
     |SI FSalida = 0;
          #0#2 = #500#0; |PINTA #0#2;
          #0#3 = #500#1; |PINTA #0#3;
     |SINO;
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO NomTecnico; |TIPO 0;
     |DDEFECTO #500;
     #500#0 = #0#2;
     |LEE 000#500=;
     |SI FSalida ! 0;
          |MENAV "0000No existe...";
          |ERROR;
     |SINO;
          #0#3 = #500#1;
          |PINTA #0#3;
          |C_M #0#2, 0, aAlfa;
          |SI aAlfa = "S";
                |HAZ LaClave;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Invalida; |TIPO 0;
     |SI #diaw0002#5 = "I";
          |C_M #diaw0002#4, 1, "N";
          |C_M #diaw0002#6, 1, "S";
     |SINO;
          |C_M #diaw0002#4, 1, "S";
          |C_M #diaw0002#6, 1, "N";
     |FINSI;
|FPRC;

|PROCESO DefTecnico; |TIPO 7;
     |SELECT #3#dsparm05@;
     aAlfa1 = Usuario % 810;
     #dsparm05@#2 = aAlfa1;
     |LEE 000#dsparm05@.=;
     |SI FSalida = 0;
          #diaw0002#2 = #dsparm05@#0;
          |PINTA #diaw0002#2;
          |PTEC 802;
     |FINSI;
     |SELECT #1#dsparm05@;
|FPRC;


|PROCESO Min5;
     #5#0 = #0#0;
     #5#1 = #0#1;
     #5#2 = #0#2;
     #5#3 = 1;
|FPRC;

|PROCESO Max5;
     #5#0 = #0#0;
     #5#1 = #0#1;
     #5#2 = #0#2;
     #5#3 = 9999;
|FPRC;

|PROCESO MiraVisa;
     |SI #3#4 ! "S";
          nSwVisa = 1;
          aAlfa1 = "0000Tecnico [" + #3#2 + "] pendiente de visar.";
     |FINSI;
|FPRC;

|DEFBUCLE MiraVisa;
     #3#1;
     ;
     #0#0, #0#1, 0001;
     #0#0, #0#1, 9999;
     ;
     NULL, MiraVisa;
|FIN;

|PROCESO Tecla; |TIPO 11;
     nModo = (FEntrada / 100) ! 0;
     |SI FSalida = 14;   ||Crea T.
          |SI nModo = 1; |ERROR; |ACABA; |FINSI;
          |SI #5#9 = 0;
               nSwVisa = 0;
               |BUCLE MiraVisa;
               |SI nSwVisa = 0;
                    |HAZ CreaTiquet;
                    #5#9 = nTiquet; |PINTA #5#9;
                    |GRABA 020#5a;
               |SINO;
                    |CONFI "0000NCrear tiquet aunque haya/n visado/s pendiente/s?";
                    |SI FSalida = 0;
                         |HAZ CreaTiquet;
                         #5#9 = nTiquet; |PINTA #5#9;
                         |GRABA 020#5a;
                    |FINSI;
               |FINSI;
          |SINO;
               |MENAV "0000Ya existe tiquet asignado ...";
          |FINSI;
          |ERROR; |ACABA;
     |FINSI;
     |SI FSalida = 13;        ||Obs
          |SI nModo = 1; |ERROR; |ACABA; |FINSI;
          eaSer = #5#0;
          enNum = #5#1;
          enTec1 = #5#2;
          enTec2 = #5#3;
          |SUB_EJECUTA_NP "diam0006";
          |ERROR; |ACABA;
     |FINSI;
     |SI FSalida = 10; ||Consulta
          |ABRE #500;
          |LEE 000#500p;
          |CONSULTA_CLAVE #1#500;
          |SI FSalida = 0;
               #5#3 = #500#0; |PINTA #5#3;
               #5#4 = #500#1; |PINTA #5#4;
          |SINO;
               |ERROR;
          |FINSI;
          |CIERRA #500;
     |FINSI;
|FPRC;

|PROCESO Tecnico; |TIPO 0;
     |DDEFECTO #500;
     |ABRE #500;
     #500#0 = #5#3;
     |LEE 000#500=;
     |SI FSalida ! 0;
          |MENAV "0000No existe Tecnico...";
          |ERROR;
          |ACABA;
     |FINSI;
     |CIERRA #500;
     #5#4 = #500#1; |PINTA #5#4;
|FPRC;

|PROCESO SacaLin;
     |PUSHV 0501 2380;
     |ENTLINEAL #1#5, -4, 1, 20, 1, Min5, Max5;
     |POPV;
|FPRC;

|PROCESO BorraObs;
     |BORRA 020#6a;
|FPRC;

|DEFBUCLE BorraObs;
     #6#1;
     ;
     #5#0, #5#1, #5#2, #5#3, 1;
     #5#0, #5#1, #5#2, #5#3, 999;
     be;
     NULL, BorraObs;
|FIN;

|PROCESO Tipo2_5; |TIPO 2;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 3;
          |SI #5#9 ! 0;
               |MENAV "0000Tiquet Creado...";
               |ERROR;
          |SINO;
               |BUCLE BorraObs;
          |FINSI;
     |FINSI;
|FPRC;

|PROGRAMA;
     |CLS;
     |PINPA #0#0;
     |PINDA #0#0;

     |CARGA_DEF aDef5, dsparm05@;
     |SI FSalida = 0;
          |PATH_DAT #500 aDat;
          |ABRET #3;
          |ABRET #500;
          |PARA FSalida = 0; |SI FSalida = 0; |HACIENDO;
               |ENDATOS #1#0;
               |C_M #0#2, 1, "N";
          |SIGUE;
          |CIERRAT #3;
          |CIERRAT #500;
          |DESCARGA_DEF #dsparm05@;
     |SINO;
          aDef5 = "0000Error al cargar: " + aDef5;
          |MENAV aDef5;
     |FINSI;
|FPRO;

|PROCESO Desencripta;
     camufz = "";
     |PARA para1 = 1; |SI para1 [ 20; |HACIENDO para1 = para1 + 2;
          nume1 = (para1 * 100) + 1;
          alfa1 = clave % nume1;
          nume1 = ((para1 + 1) * 100) + 1;
          alfa2 = clave % nume1;

          nume1 = &alfa2;
          nume1 = nume1 - 65;

          camuf = &alfa1;
          camuf = camuf - 65;
          camuf = ((camuf * 26) + nume1) / 2;

          camufz = camufz + &camuf;
     |SIGUE;
|FPRC;

|PROCESO Encripta;
     camufz = "";
     |PARA para1 = 1; |SI para1 [ 10; |HACIENDO para1 = para1 + 1;
          nume1 = (para1 * 100) + 1;
          alfa1 = clave % nume1;

          camuf = &alfa1;
          camuf = camuf * 2;

          nume1 = ((camuf - (camuf $ 26)) / 26 ) + 65;
          camufz = camufz + &nume1;

          nume1 = (camuf $ 26) + 65;
          camufz = camufz + &nume1;
     |SIGUE;
|FPRC;

|PROCESO CorreoPrepe;
     |ABRE #500;

     #500#0 = #100#12;
     |LEE 020#500=;
     aFrom = #500#3; |QBF aFrom;
     |SI aFrom = "";
          aFrom = #500#4;
     |SINO;
          aAlfa = #500#4; |QBF aAlfa;
          |SI aAlfa ! "";
               aFrom = aFrom + ";" + #500#4;
          |FINSI;
     |FINSI;
     |QBF aFrom;
     aTo = aFrom;
     aFrom = #500#3; |QBF aFrom;
     aNume = ("000000" + #0#1) % -106;
     aSubject = "Completado Visado pedido: " + #0#0 + "/" + aNume;
     aMensaje = aSubject;
     aServidor = #100#7;
     aIP = #100#8;
     |QBF aIP;
     |QBF aServidor;
     |DSCORREO_ENVIA aIP, aServidor, aFrom, aTo, aSubject, aMensaje, aDjunto;
     |SI FSalida ! 0;
          |MENAV "0000Ha habido un error durante el envio.";
     |FINSI;
     |CIERRA #500;
|FPRC;

|PROCESO Tipo80;  |TIPO 80;
     |DBASS aBase; |QBF aBase;

     aDef1  = aBase + "dspartes/def/dstelw01";
     aDef2  = aBase + "dspartes/def/dstelm02";
     aDef3  = aBase + "dspartes/def/dstelm03";
     aDef5  = aBase + "dspartes/def/dsparm05";
     aDef10 = aBase + "dspartes/def/dsparm01";
     aDefa = aBase + "dspartes/def/dmaplica";

     aDat  = aBase + "dspartes/fich/";
|FPRC;
컴컴컴컴컴컴컴컴컴컴컴
|PROCESO LeeMonBase; ||Lee los parametros de la moneda base
     |ABRE #agifa321;
     |ABRE #agifa324;
     |LEE 010#agifa321.p;   ||Leo parametros de divisa
     #agifa104#40 = #agifa321#9;  ||Cojo la Moneda Base de parametros
     #agifa324#0 = #agifa321#9;
     |LEE 010#agifa324.=;         ||Leo la ficha de la moneda base en Divisas
     #agifa104#41 = #agifa324#2;  ||Asigno el cambio de la moneda base con el euro
     decim_base = #agifa324#7;    ||Asigno los decimales de la moneda base
     precio_base = #agifa324#9;   ||Asigno los decimales del precio en la mon. base
     |CIERRA #agifa324;
     |CIERRA #agifa321;
|FPRC;

|PROCESO Param_Divisas; |TIPO 0;
     |ABRE #agifa324;
     #agifa324#0 = #agifa104#35;
     |LEE 001#agifa324.=;   || Leo la divisa
     |CIERRA #agifa324;

     decim_divisa = #agifa324#7;  || Decimales de la divisa
     precio_divisa = #agifa324#9;  || Decimales en el precio de la divisa
     nDeci_DivP = decim_divisa;   || Decimales de la divisa
     nDeci_PreDivP = precio_divisa;  || Decimales en el precio de la divisa
     |SI #104#37 = "B";   ||Si trabajo con la moneda base ...
          nDeci_PreVis    = precio_divisa;
          nDeci_ImpVis    = decim_divisa;
          nDeci_PreVisP   = precio_divisa;
          nDeci_ImpVisP   = decim_divisa;
          nDeci_Base     = decim_base;
          nDeci_PreBase  = precio_base;
          nDeci_BaseMxP  = decim_base;
          nDeci_PreBasMxP = precio_base;
          nDeci_TotVisP   = decim_base;
          nDeci_TotNoVisP = decim_divisa;
     |SINO;     ||Si trabajo con la divisa ...
          nDeci_PreVis    = precio_base;
          nDeci_ImpVis    = decim_base;
          nDeci_PreVisP   = precio_base;
          nDeci_ImpVisP   = decim_base;
          nDeci_Base      = decim_base; || Max. precision en los decimales de la moneda base
          nDeci_PreBase   = precio_base;  || Max. precision en los dec. del precio de la moneda base
         |SI #104#36 = 1;
              nDeci_BaseMxP   = decim_base;    || sin triangulacion
              nDeci_PreBasMxP = precio_base;
         |SINO;
              nDeci_BaseMxP   = #agifa321#10;  || con triangulacion
              nDeci_PreBasMxP = #agifa321#11;
         |FINSI;
          nDeci_TotVisP   = decim_divisa;
          nDeci_TotNoVisP = decim_base;
     |FINSI;
|FPRC;

|PROCESO LeeParamDiv; |TIPO 0;
     |ABRE #agifa321;
     |LEE 001#agifa321.p;    || Leo parametros de divisas
     |ABRE #agifa007;
     #agifa007#26 = #agifa104#25;
     |LEE 001#agifa007.=;        || Leo la serie

     |SI #agifa007#39 = "N"; |O #agifa321#1 = "N";
          #104#37 = "B";
     |FINSI;

     |SI #104#37 = "D";      || Si la mon. de trabajo es la divisa...
       || ...escondo los campos de la moneda base :
       |CAMPO_POSICION #73#6 , 1 , 1009;
       |CAMPO_POSICION #73#9 , 1 ,1026;
       |CAMPO_VISUAL #73#6 , 1 , "N";
       |CAMPO_VISUAL #73#9 , 1 , "N";
       |CAMPO_LINEAL #73#6 , 1 , "N";
       |CAMPO_LINEAL #73#9 , 1 , "N";
       |CAMPO_MODIFICA #73#6 , 1 , "N";
       || ...y pongo en pantalla los de divisa
       |CAMPO_POSICION #73#38 , 1 , 1544;
       |CAMPO_POSICION #73#39 , 1 ,1568;
       |CAMPO_VISUAL #73#38 , 1 , "S";
       |CAMPO_VISUAL #73#39 , 1 , "S";
       |CAMPO_MODIFICA #73#38 , 1 , "S";
       |CAMPO_LINEAL #73#38 , 1 , "S";
       |CAMPO_LINEAL #73#39 , 1 , "S";
       |SI #agifa007#39 = "S"; |Y #agifa321#1 = "S"; ||Si la serie es de ...
          ac_divisa = "S";  || ... divisas, y en parametros estan activadas ...
          || ... pongo como modificables los campos relacionados con divisas
          |CAMPO_MODIFICA #104#35 , 1 , "S";
          |CAMPO_MODIFICA #104#36 , 1 , "S";
          |CAMPO_MODIFICA #104#37 , 1 , "S";
          |CAMPO_MODIFICA #104#38 , 1 , "S";
          |CAMPO_MODIFICA #104#39 , 1 , "S";
          |SI #agifa321#3 = "S";  || Si la visualizacion de parametros esta activada
            || ... pongo los campos visual. de lineas como visualizables
            |CAMPO_VISUAL #73#40, 1 , "S";
            |CAMPO_VISUAL #73#41 , 1 , "S";
          |SINO;
            || ... los pongo como no visualizables
            |CAMPO_VISUAL #73#40 , 1 , "N";
            |CAMPO_VISUAL #73#41 , 1 , "N";
          |FINSI;
       |SINO; || Si la serie no es de divisas, o no estan activados parametros
          ac_divisa = "N";
          #104#37 = "B";  || La moneda de trabajo es por fuerza la moneda base
          |PINTA #104#37;
          || Pongo como no modificables los campos relacionados con divisas
          |CAMPO_MODIFICA #104#37 , 1 , "N";
          |CAMPO_MODIFICA #104#38 , 1 , "N";
          |CAMPO_MODIFICA #104#39 , 1 , "N";
          |CAMPO_MODIFICA #104#35 , 1 , "N";
          |CAMPO_MODIFICA #104#36 , 1 , "N";
          || Pongo como no visualizables los campos visual. de lineas
          |CAMPO_VISUAL #73#40, 1 , "N";
          |CAMPO_VISUAL #73#41, 1 , "N";
       |FINSI;
     |SINO;  || Si la moneda de trabajo es la moneda base
       || ...escondo los campos en divisa :
       |CAMPO_POSICION #73#38 , 1 , 1009;
       |CAMPO_POSICION #73#39 , 1 ,1026;
       |CAMPO_VISUAL #73#38 , 1 , "N";
       |CAMPO_VISUAL #73#39 , 1 , "N";
       |CAMPO_MODIFICA #73#38 , 1 , "N";
       |CAMPO_LINEAL #73#38 , 1 , "N";
       |CAMPO_LINEAL #73#39 , 1 , "N";
       || ...coloco los campos en moneda base:
       |CAMPO_POSICION #73#6 , 1 , 1544;
       |CAMPO_POSICION #73#9 , 1 ,1568;
       |CAMPO_VISUAL #73#6 , 1 , "S";
       |CAMPO_VISUAL #73#9 , 1 , "S";
       |CAMPO_MODIFICA #73#6 , 1 , "S";
       |CAMPO_LINEAL #73#6 , 1 , "S";
       |CAMPO_LINEAL #73#9 , 1 , "S";
       |SI #agifa007#39 = "S"; |Y #agifa321#1 = "S"; || Si la serie es de divisas...
          ac_divisa = "S"; || ... y divisas estan activadas en parametros
          || Pongo como modificables los campos relacionados con divisas
          |CAMPO_MODIFICA #104#35 , 1 , "S";
          |CAMPO_MODIFICA #104#36 , 1 , "S";
          |CAMPO_MODIFICA #104#37 , 1 , "S";
          |CAMPO_MODIFICA #104#38 , 1 , "S";
          |CAMPO_MODIFICA #104#39 , 1 , "S";
          |SI #agifa321#3 = "S";  || Si la visualizacion esta activada en parametros ...
            || Pongo como visualizables los campos visual. de las lineas
            |CAMPO_VISUAL #73#40, 1 , "S";
            |CAMPO_VISUAL #73#41 , 1 , "S";
          |SINO; || Los pongo como no visualizables
            |CAMPO_VISUAL #73#40 , 1 , "N";
            |CAMPO_VISUAL #73#41 , 1 , "N";
          |FINSI;
       |SINO;  || Si la serie no es de divisas o no estan activados los parametros
          ac_divisa = "N";
          #104#37 = "B";  || La moneda de trabajo es por fuerza la moneda base
          || Pongo como no modificables los campos relacionados con divisas
          |CAMPO_MODIFICA #104#37 , 1 , "N";
          |CAMPO_MODIFICA #104#38 , 1 , "N";
          |CAMPO_MODIFICA #104#39 , 1 , "N";
          |CAMPO_MODIFICA #104#35 , 1 , "N";
          |CAMPO_MODIFICA #104#36 , 1 , "N";
          || Pongo como no visualizables los campos visual. de lineas
          |CAMPO_VISUAL #73#40, 1 , "N";
          |CAMPO_VISUAL #73#41, 1 , "N";
       |FINSI;
     |FINSI;
     |CIERRA #agifa007;
     |CIERRA #agifa321;
|FPRC;

|PROCESO Aplica66; |TIPO 66;
     |CARGA_DEF aDefa, dmaplica@;
     |SI FSalida ! 0;
          aAlfa = "0000Error al cargar " + aDefa;
          |MENAV aAlfa;
     |SINO;
          |PATH_DAT #dmaplica@#aDat#;
          |ABRE #dmaplica@;
          #dmaplica@#0 = #5#12;
          |LEE 000#dmaplica@.];
          |CONSULTA_CLAVE #1#dmaplica@;
          |SI FSalida = 0;
               #5#12 = #dmaplica@#0; |PINTA #5#12;
          |SINO;
               |ERROR;
          |FINSI;
          |CIERRA #dmaplica@;
          |DESCARGA_DEF #dmaplica@;
     |FINSI;
|FPRC;
