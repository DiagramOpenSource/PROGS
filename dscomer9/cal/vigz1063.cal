Solo se divide por toneladas los de traspasos

(el vigz0063 tiene el cal anterior)

|FICHEROS;
     vigz0063 #0;
     agifa007 #7;
     agifa019 #19;
     vigw0034 #34;
     vigw0039 #39;
     vigm0043 #43;
     agifa065 #65;
|FIN;

|VARIABLES;
     &enD1 = 0;
     &enD2 = 0;
     nUni = 0;
     nTotaKg1 = 0;
     nTotaKg2 = 0;
     nImp1 = 0;
     nImp2 = 0;
     nImp3 = 0;
     nImp4 = 0;
     &enT1 = 0;
     &enT2 = 0;
     &enT3 = 0;
     &enT4 = 0;
     &Tempo = "";
     fDFecha = @;
     fHFecha = @;
     aMes = "";
     aA¤o = "";
     aAlfa = "";
     nMes = 0;
     nAno = 0;
     nPre = 0;
     nPreTon = 0;
     nKg = 0;
     nPcmV = 0;
     nPcmF = 0;
     f = @;
     aArt = "";
     nPrecio = 0;
     aTmp34 = "";
     aTmp39 = "";
|FIN;

|PROCESO &Redo;
     enD1 = enD1 ! 4;
     enD2 = enD2 ! 4;
|FPRC;

|PROCESO PreTon;
     nPreTon = 1;
     |DDEFECTO #19;
     #19#0 = #34#0;
     |LEE 000#19=;
     aAlfa = #19#110; |QBF aAlfa;   || Alfanumerico1
     aAlfa = aAlfa > "";
     |SI aAlfa = "S"; nPreTon = 1000; |FINSI;
|FPRC;

|PROCESO BuscaAntAV;
     nPrecio = 0;
     aAlfa = "01." + (("00" + #34#2) % -102) + "." + (("0000" + #34#1) % -104);
     #65#0 = #34#0;
     #65#1 = aAlfa;
     #65#2 = "AV";
     #65#3 = 0;
     #65#4 = 0;
     #65#11 = "";
     |LEE 000#65];
     |SI FSalida = 0;
          |LEE 000#65a;
     |SINO;
          |LEE 000#65u;
     |FINSI;
     |PARA; |SI FSalida = 0; |Y #65#0 = #34#0; |HACIENDO;
          |SI #65#2 = "AV";
               |DDEFECTO #7;
               #7#26 = #65#11;
               |LEE 000#7=;
               |SI #65#14 ! 0; |Y #7#37 = "N";
                    nPrecio = #65#14;
                    |SAL;
               |FINSI;
          |FINSI;
          |LEE 000#65a;
     |SIGUE;
||     |HAZ PreTon;
||     nPrecio = nPrecio / nPreTon;
|FPRC;

|PROCESO BuscaAntEntra;
     nPrecio = 0;
     aAlfa = "01." + (("00" + #34#2) % -102) + "." + (("0000" + #34#1) % -104);
     #65#0 = #34#0;
     #65#1 = aAlfa;
     #65#2 = "AC";
     #65#3 = 0;
     #65#4 = 0;
     #65#11 = "";
     |LEE 000#65];
     |SI FSalida = 0;
          |LEE 000#65a;
     |SINO;
          |LEE 000#65u;
     |FINSI;
     |PARA; |SI FSalida = 0; |Y #65#0 = #34#0; |HACIENDO;
          |SI #65#2 = "AC"; |O #65#2 = "EV";
               |DDEFECTO #7;
               #7#26 = #65#11;
               |LEE 000#7=;
               |SI #65#14 ! 0;  |Y #7#37 = "N";
                    nPrecio = #65#14;
                    |SAL;
               |FINSI;
          |FINSI;
          |LEE 000#65a;
     |SIGUE;
     |HAZ PreTon;
     |SI #65#2 = "EV"; nPreTon = 1; |FINSI;
     nPrecio = nPrecio / nPreTon;
|FPRC;

|PROCESO BuscaAntFB;
     nPrecio = 0;
     aAlfa = "01." + (("00" + #34#2) % -102) + "." + (("0000" + #34#1) % -104);
     #65#0 = #34#0;
     #65#1 = aAlfa;
     #65#2 = "FB";
     #65#3 = 0;
     #65#4 = 0;
     #65#11 = "";
     |LEE 000#65];
     |SI FSalida = 0;
          |LEE 000#65a;
     |SINO;
          |LEE 000#65u;
     |FINSI;
     |PARA; |SI FSalida = 0; |Y #65#0 = #34#0; |HACIENDO;
          |SI #65#2 = "FB";
               |DDEFECTO #7;
               #7#26 = #65#11;
               |LEE 000#7=;
               |SI #65#9 ! 0; |Y #7#37 = "N";
                    nPrecio = #65#9;
                    |SAL;
               |FINSI;
          |FINSI;
          |LEE 000#65a;
     |SIGUE;
||     |HAZ PreTon;
||     nPrecio = nPrecio / nPreTon;
|FPRC;

|PROCESO BuscaAnt;
     |HAZ BuscaAntFB;
     |SI nPrecio = 0; |HAZ BuscaAntEntra; |FINSI;
     |SI nPrecio = 0; |HAZ BuscaAntAV; |FINSI;
|FPRC;

|PROCESO Tmp;
     |SI #34#8 = 0;
          |HAZ BuscaAnt;
          #34#8 = nPrecio;
     |FINSI;
     |SI #34#7 = 0;
          |HAZ BuscaAntAV;
          #34#7 = nPrecio;
     |FINSI;

     |SALVA_FICHA #34;
     aArt = #34#0;
     nAno = #34#1;
     nMes = #34#2;
     nMes = nMes - 1;
     |SI nMes [ 0; nMes = 12; nAno = nAno - 1; |FINSI;
     |DDEFECTO #34;
     #34#0 = aArt;
     #34#1 = nAno;
     #34#2 = nMes;
     |LEE 000#34=;
     |SI #34#8 = 0;
          |HAZ BuscaAnt;
          #34#8 = nPrecio;
     |FINSI;
     |SI #34#7 = 0;
          |HAZ BuscaAntAV;
          #34#7 = nPrecio;
     |FINSI;
     nKg = #34#3;
     nPcmV = #34#7;
     nPcmF = #34#8;
     |REPON_FICHA #34;
     #34#9 = nKg;
     #34#10 = nPcmV;
     #34#11 = nPcmF;
     #34#12 = nAno;
     #34#13 = nMes;

     nTotaKg1 = nTotaKg1 + #34#3;
     nTotaKg2 = nTotaKg2 + #34#9;
     nImp1 = nImp1 + (#34#3 * #34#7);
     nImp2 = nImp2 + (#34#3 * #34#8);
     nImp3 = nImp3 + (#34#9 * #34#10);
     nImp4 = nImp4 + (#34#9 * #34#11);
     enT1 = nImp1 / nTotaKg1;
     enT2 = nImp2 / nTotaKg1;
     enT3 = nImp3 / nTotaKg2;
     enT4 = nImp4 / nTotaKg2;
     |PRINT;
|FPRC;

|DEFBUCLE Tmp;
     #34#1;
     ;
     #0#6, #0#0, "                    ", "      ";
     #0#7, #0#1, "zzzzzzzzzzzzzzzzzzzz", "zzzzzz";
     ;
     NULL, Tmp;
|FIN;

|PROCESO Traspaso;
     aA¤o = #43#6 % 704;
     aMes = #43#6 % 402;
     |DDEFECTO #34;
     #34#0 = #43#12;
     #34#1 = aA¤o;
     #34#2 = aMes;
     |LEE 101#34=;
     |SI FSalida ! 0; |GRABA 020#34b; |FINSI;

     |DDEFECTO #19;
     #19#0 = #43#12;
     |LEE 000#19=;
     nPreTon = 1;
     aAlfa = #19#110; |QBF aAlfa;   || Alfanumerico1
     aAlfa = aAlfa > "";
     |SI aAlfa = "S"; nPreTon = 1000; |FINSI;
     nPre = #43#14 / nPreTon;

     #34#3 = #34#3 + #43#13;
     #34#4 = #34#4 + (nPre * #43#13);
     #34#7 = #34#4 / #34#3;
     |GRABA 020#34a;
     |LIBERA #34;
|FPRC;

|DEFBUCLE Traspaso;
     #43#3;
     12, 17;
     fDFecha, #0#2, #0#4;
     fHFecha, #0#3, #0#5;
     ;
     NULL, Traspaso;
|FIN;

|PROCESO Historico2;
     |DDEFECTO #7;
     #7#26 = #65#11;
     |LEE 000#7=;
     |SI #7#37 = "S"; |ACABA; |FINSI;

     |DDEFECTO #19;
     #19#0 = #65#0;
     |LEE 000#19=;
     nPreTon = 1;
     aAlfa = #19#110; |QBF aAlfa;   || Alfanumerico1
     aAlfa = aAlfa > "";
     |SI aAlfa = "S"; nPreTon = 1000; |FINSI;

     |SI #65#2 = "AC"; |O #65#2 = "BC"; |O #65#2 = "EV";
          aA¤o = #65#1 % 704;
          aMes = #65#1 % 402;
          |DDEFECTO #34;
          #34#0 = #65#0;
          #34#1 = aA¤o;
          #34#2 = aMes;

          #39#0 = #34#0;
          #39#1 = #34#1;
          #39#2 = #34#2;
          |LEE 000#39=;
          |SI FSalida = 0; |ACABA; |FINSI;

          |SI #65#2 = "EV"; nPreTon = 1; |FINSI;

          |SI #65#2 = "AC"; |O #65#2 = "BC"; |O #65#2 = "EV";
               |LEE 101#34=;
               |SI FSalida ! 0; |GRABA 020#34b; |FINSI;
               nUni = #65#6;
               #34#5 = #34#5 + nUni;
               #34#6 = #34#6 + (nUni * #65#9 / nPreTon);
          |FINSI;
          #34#7 = #34#4 / #34#3;
          #34#8 = #34#6 / #34#5;
          |GRABA 020#34a;
          |LIBERA #34;
     |FINSI;
|FPRC;

|DEFBUCLE Historico2;
     #65#1;
     5;
     #0#2, fDFecha, "  ", 000000, 00000, "     ", #0#4;
     #0#3, fHFecha, "zz", 999999, 99999, "zzzzz", #0#5;
     ;
     NULL, Historico2;
|FIN;

|PROCESO Historico1;
     |DDEFECTO #7;
     #7#26 = #65#11;
     |LEE 000#7=;
     |SI #7#37 = "S"; |ACABA; |FINSI;

     |DDEFECTO #19;
     #19#0 = #65#0;
     |LEE 000#19=;
     nPreTon = 1;
     aAlfa = #19#110; |QBF aAlfa;   || Alfanumerico1
     aAlfa = aAlfa > "";
||     |SI aAlfa = "S"; nPreTon = 1000; |FINSI;

     |SI #65#2 = "AV"; |O #65#2 = "BV"; |O #65#2 = "FB";
          aA¤o = #65#1 % 704;
          aMes = #65#1 % 402;
          |DDEFECTO #34;
          #34#0 = #65#0;
          #34#1 = aA¤o;
          #34#2 = aMes;

          |SI #65#2 = "AV"; |O #65#2 = "BV";
               |LEE 101#34=;
               |SI FSalida ! 0; |GRABA 020#34b; |FINSI;
               nUni = -#65#6;
               #34#3 = #34#3 + nUni;
               #34#4 = #34#4 + (nUni * #65#14 / nPreTon);
          |FINSI;
          |SI #65#2 = "FB";
               |LEE 101#34=;
               |SI FSalida ! 0; |GRABA 020#34b; |FINSI;
               nUni = #65#6;
               #34#5 = #34#5 + nUni;
               #34#6 = #34#6 + (nUni * #65#9 / nPreTon);

               #39#0 = #34#0;
               #39#1 = #34#1;
               #39#2 = #34#2;
               |LEE 000#39=;
               |SI FSalida ! 0; |GRABA 000#39n; |FINSI;
          |FINSI;
          #34#7 = #34#4 / #34#3;
          #34#8 = #34#6 / #34#5;
          |GRABA 020#34a;
          |LIBERA #34;
     |FINSI;
|FPRC;

|DEFBUCLE Historico1;
     #65#1;
     5;
     #0#2, fDFecha, "  ", 000000, 00000, "     ", #0#4;
     #0#3, fHFecha, "zz", 999999, 99999, "zzzzz", #0#5;
     ;
     NULL, Historico1;
|FIN;

|PROCESO Fecha;
     nMes = nMes - 1;
     |SI nMes = 0; nMes = 12; nAno = nAno - 1; |FINSI;
     aAlfa = "01." + (("00" + nMes) % -102) + "." + (("0000" + nAno) % -104);
     fDFecha = aAlfa;
     aAlfa = "27." + (("00" + #0#1) % -102) + "." + (("0000" + #0#7) % -104);
     fHFecha = aAlfa;
     aMes = fHFecha % 402;
     |PARA; |SI; |HACIENDO;
          f = fHFecha + 1;
          aAlfa = f % 402;
          |SI aMes ! aAlfa; |SAL; |FINSI;
          fHFecha = f;
     |SIGUE;
|FPRC;

|PROCESO Tipo7; |TIPO 7;
     f = ~;
     aMes = f % 402;
     aA¤o = f % 704;
     |SI #0#0 ! 0; |ACABA; |FINSI;

     #0#0 = aMes; |PINTA #0#0;
     #0#1 = aMes; |PINTA #0#1;
     #0#6 = aA¤o; |PINTA #0#6;
     #0#7 = aA¤o; |PINTA #0#7;
|FPRC;

|PROCESO Tipo3; |TIPO 3;
     |IMPRE 0;
     |SI FSalida = 0;
          |INFOR "vigz0063";
          |SI FSalida = 0;
               |HAZ CreaTempo; |NOME_DAT #34 Tempo; |DELINDEX #34; aTmp34 = Tempo;
               |HAZ CreaTempo; |NOME_DAT #39 Tempo; |DELINDEX #39; aTmp39 = Tempo;
               nAno = #0#6;
               nMes = #0#0;
               |HAZ Fecha;
               |ABRE #39;
               |ABRE #34;
               |ABRE #7;
               |ABRE #19;
               |BUCLE Historico1;
               |BUCLE Historico2;
               |BUCLE Traspaso;
               |CIERRA #19;
               |CIERRA #7;
               |CIERRA #34;
               |CIERRA #39;

               |ABRE #7;
               |ABRE #65;
               |ABRE #19;
               |BUCLE Tmp;
               |CIERRA #19;
               |CIERRA #65;
               |CIERRA #7;
               |PIEINF;
               |FININF;
               Tempo = aTmp34; |HAZ BoraTempo; |DELINDEX #34;
               Tempo = aTmp39; |HAZ BoraTempo; |DELINDEX #39;
          |SINO;
               |MENAV "0000Error en informe 'vigz0063'";
          |FINSI;
          |FINIMP;
     |FINSI;
|FPRC;
