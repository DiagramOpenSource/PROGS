|FICHEROS;
     ortoy001 #0;
     agifa077 #77;
     agifa080 #80;
     dsm00138 #138;
     dsm00139 #139;
     agifa081 #81;
     agifa067 #67;
     agifa065 #65;
|FIN;

|VARIABLES;
     aOpe = "";
     nHay = 0;
     nOk = 0;
     nHayLin = 0;
     i = 0;
|FIN;

|PROCESO HayLin;
     nHayLin = 1;
|FPRC;

|PROCESO BorraHis;
     |SI #77#42 = "S";
          aOpe = "BC";
     |SINO;
          aOpe = "AC";
     |FINSI;
     #65#0 = #80#2;
     #65#1 = #77#1;
     #65#2 = aOpe;
     #65#3 = #80#0;
     #65#4 = #80#1;
     #65#11 = #80#27;
     |BORRA 000#65c;
     |LIBERA #65;
|FPRC;

|PROCESO CopiaLin;
     |PARA i = 0; |SI i [ 45; |HACIENDO i = i + 1;
          #139i = #80i;
     |SIGUE;
     |HAZ TotalLin139;
     |HAZ CalCabDsm00138;

     #138#26 = #138#26 + 1;   || Control P

     |GRABA 020#139n;

     |HAZ BorraHis;
     |BORRA 020#80a;
|FPRC;

|PROCESO FinAlb;
     |SI nHay = 0;
          |ACABA;
     |FINSI;
     |HAZ Chequea;
     |SI nOk ! 0; |ACABA; |FINSI;

     |PARA i = 0; |SI i [ 78; |HACIENDO i = i + 1;
          #138i = #77i;
     |SIGUE;
     |GRABA 020#138b;
     |HAZ LimCabDsm00138;

     |BUCLELIN 0 CopiaLin #77;

     |GRABA 020#138a;
     |LIBERA #138;

     nHayLin = 0;
     |BUCLELIN 2 HayLin #77;
     |SI nHayLin = 0;
          |BORRA 020#77a;
     |SINO;
          |HAZ LimCabAgifa077;
          |BUCLELIN 2 CalCabAgifa077 #77;
          |GRABA 020#77a;
     |FINSI;

     |LIBERA #77;
|FPRC;

|PROCESO Chequea;
     nOk = 1;
     #81#25 = #80#29;
     #81#0 = #80#12;
     |LEE 000#81=;
     |SI FSalida = 0;
          |SI #80#29 ] #0#0; |Y #80#29 [ #0#1; || Serie
               |SI #80#0 ] #0#2; |Y #80#0 [ #0#3; || Numero
                    |SI #81#1 ] #0#6; |Y #81#1 [ #0#7; || fecha
                         nOk = 0;
                         nHay = 1;
                    |FINSI;
               |FINSI;
          |FINSI;
     |SINO;
          nOk = 0;
          nHay = 1;
     |FINSI;
|FPRC;

|PROCESO LinAlb;
     |HAZ Chequea;
     |SI nOk = 0;
          nHay = 1;
     |FINSI;
|FPRC;

|PROCESO CabAlb;
     |SI #77#38 = "S";   || facturado
          |ERROR;
     |FINSI;
     nHay = 0;
|FPRC;

|DEFBUCLE Albaran;
     #77#4;
     ;
     #0#4, "             ";
     #0#5, "zzzzzzzzzzzzz";
     be;
     NULL, CabAlb, LinAlb, FinAlb;
|FIN;

|PROCESO Tipo3; |TIPO 3;
     |ABRE #138;
     |ABRE #139;
     |ABRE #81;
     |ABRE #67;
     |ABRE #65;
     |BUCLE Albaran;
     |CIERRA #65;
     |CIERRA #67;
     |CIERRA #81;
     |CIERRA #138;
     |CIERRA #139;
|FPRC;
