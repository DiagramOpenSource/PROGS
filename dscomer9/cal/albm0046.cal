|FICHEROS;
     albm0046 #0;
     albm0047 #47;       ||Lineas en formato GRID. LinealSimple
     albm0063 #63;       ||PesoMtsPzas OrdenFab
     albm0064 #64;       ||Ordenes Partida

     agifa019 #1;
     dsm00238 #2;

     albm0035 #35,MANTE;
     agifa019 #19;       ||Articulos
     agifa029 #29;       ||Datos Almacenes
     agifa112 #112;      ||Proveedor

     albm0049 #49;       ||Aux.Impresion Etiq.

     dsm00024 #24;       ||Art./Alm./Partida
     referen@; || = albm0046
     agifa324 #324;
|FIN;

|VARIABLES;
     aEjecuta = "";
     nPrecioAnt = 0;

     &enPrecioPar = 0;
     &eaParRecalculo;
     &enAlmRecalculo;

     nRango = 0;
     aSerieDes = "";
     nNumDes = "";
     aPartidaDes = "";
     aAlfa2 = "";
     aAux = "";
     aNumDes = "";

     aAlmacen = "";
     nAlmacen = 0;
     aPartida = "";

     nConta = 0;
     nCarga = 0;
     aParam = "";
     nSwArtDOS = 0;
     nNueva = 0;
     &EURODB = 0;
     aAlfa = "";
     nModoLin = 0;
     nPos1 = 0;
     nPos2 = 0;
     nGrid = 0;

     aMensaje = "";
     nBotonDes = 0;
     nBotonEti = 0;
     nBotonVer = 0;
     nBotonPed = 0;
     nSwPrimero = 1;

     aParametro = "";
     nVentana1 = 0;
     aTecla = "";
     aBase    = "";
     aAbre    = "";
     nHandle  = 0;
     nBlancos = 0;
     nEtiq = 0;
     aCodigo = "";
     nModo = 0;
     nSwCambio = 0;

     &enAlberoDentro = 0;
     aParOri = "";
     aGuardaArt = "";
|FIN;

|PROCESO TipoParti14; |TIPO 14;
     ||Esto lo utilizare para el doble stock.

     |ABRE #1;
     #1#0 = #0#0;
     |LEE 000#1=;
     |CIERRA #1;

     |ATRI N;
     |SI #1#179 = "S";
          |ABRE #2;
          #2#0 = #1#201;
          |LEE 000#2=;
          |CIERRA #2;

          aAlfa = #2#4 + " .....";
          |PINTA 1403, aAlfa;
          aAlfa = #2#5 + " .....";
          |PINTA 1303, aAlfa;
     |SINO;
          |PINTA 1303, "         ";
          |PINTA 1403, "         ";
     |FINSI;
     |ATRI I;
|FPRC;

|PROCESO Inferior47;
     ||#albm0047#0 = #albm0046#0;
     #albm0047#1 = #albm0046#1;
     #albm0047#2 = #albm0046#2;
     #albm0047#3 = 001;
|FPRC;

|PROCESO Superior47;
     ||#albm0047#0 = #albm0046#0;
     #albm0047#1 = #albm0046#1;
     #albm0047#2 = #albm0046#2;
     #albm0047#3 = 999;
|FPRC;

|PROCESO Inferior63;
     #albm0063#0 = aSerieDes;
     #albm0063#1 = nNumDes;
     #albm0063#3 = 00;
     #albm0063#4 = 0001;
|FPRC;

|PROCESO Superior63;
     #albm0063#0 = aSerieDes;
     #albm0063#1 = nNumDes;
     #albm0063#3 = 99;
     #albm0063#4 = 9999;
|FPRC;


|PROCESO ElGrid; |TIPO 13;

     |ABRE #19;
     |DDEFECTO #19;
     #19#0 = #albm0046#0;
     |LEE 000#19=;
     |CIERRA #19;

     |PINTA 0833, #19#1;

     |ABRE #29;
     |DDEFECTO #29;
     #29#0 = #albm0046#1;
     |LEE 000#29=;
     |CIERRA #29;

     |PINTA 0711, #29#1;

     |SI nSwPrimero = 1;
          nSwPrimero = 0;

          aParametro = PARAMETRO;
          aParam = aParametro % 101;
          |SI aParam ! "V";
               |CONTROL_SIMPLE 1, "LBOTON,{{137}} Ver Desglose", 1261, 1274, Desglose;
               nBotonDes = FSalida;
               |ESTADO_CONTROL nBotonDes, "ENABLE";
               |ESTADO_CONTROL nBotonDes, "SHOW";
          |SINO;
               |CONTROL_SIMPLE 1, "LBOTON,{{137}} Pedidos", 1114, 1127, Pedidos;
               nBotonPed = FSalida;
               |ESTADO_CONTROL nBotonPed, "ENABLE";
               |ESTADO_CONTROL nBotonPed, "SHOW";
          |FINSI;

          |CONTROL_SIMPLE 1, "LBOTON,{{205}} Etiquetas", 1361, 1374, Etiquetas;
          nBotonEti = FSalida;
          |ESTADO_CONTROL nBotonEti, "ENABLE";
          |ESTADO_CONTROL nBotonEti, "SHOW";
          |CONTROL_SIMPLE 1, "LBOTON,{{137}} Ver Incidencia", 1461, 1474, Incidencia;
          nBotonVer = FSalida;
          |ESTADO_CONTROL nBotonVer, "ENABLE";
          |ESTADO_CONTROL nBotonVer, "SHOW";
     |FINSI;

     aParametro = PARAMETRO;
     aParam = aParametro % 101;
     |SI aParam = "V";
          |ABRE #albm0063;

          aSerieDes = "";
          nNumDes = "";

          aPartidaDes = #albm0046#2;
          |QBF aPartidaDes;
          aAlfa2 = aPartidaDes % -301;
          |SI aAlfa2 = "/";
               aSerieDes = aPartidaDes % -102;
               aAux = aPartidaDes % -103;
               aNumDes = aPartidaDes - aAux;
               nNumDes = aNumDes;
          |FINSI;

          |SI nGrid ! 0;
               |REFRESCACONTROL nGrid;
               |ACABA;
          |FINSI;

          nModoLin = 2 + 4 + 8 + 16 + 32;
          nPos1 = 1702;
          nPos2 = 2279;
          |LINEAL_SIMPLE #1#63, nModoLin, nPos1, nPos2, Inferior63, Superior63, NULL;
          nGrid = FSalida;
          |CIERRA #albm0063;

          aAlfa = "Adjudicado";
          |PINTA 1101, aAlfa;


          aAlfa = "LOTE";
          |PINTA 0625, aAlfa;
     |SINO;
          aAlfa = "             ";
          |PINTA 1101, aAlfa;
          aAlfa = "PARTIDA";
          |PINTA 0622, aAlfa;

          |ABRE #albm0047;
          |SI nGrid ! 0;
               |REFRESCACONTROL nGrid;
               |ACABA;
          |FINSI;


          nModoLin = 2 + 4 + 8 + 16 + 32;
          nPos1 = 1702;
          nPos2 = 2279;
          |LINEAL_SIMPLE #1#47, nModoLin, nPos1, nPos2, Inferior47, Superior47, NULL;
          nGrid = FSalida;
          |CIERRA #albm0047;
     |FINSI;

|FPRC;

|PROCESO Tipo9; |TIPO 9;
     aParametro = PARAMETRO;
     aParam = aParametro % 101;
     |SI aParam ! "V";
          |DESTRUYECONTROL nBotonDes;
     |SINO;
          |DESTRUYECONTROL nBotonPed;
     |FINSI;

     |DESTRUYECONTROL nBotonEti;
     |DESTRUYECONTROL nBotonVer;

     |PULSATECLA;
     |PULSATECLA;

     |SI aParam ! "V";
          |FIN_CONTROL_WINDOWS nBotonDes;
     |SINO;
          |FIN_CONTROL_WINDOWS nBotonPed;
     |FINSI;
     |FIN_CONTROL_WINDOWS nBotonEti;
     |FIN_CONTROL_WINDOWS nBotonVer;
     |PULSATECLA;
     |PULSATECLA;
|FPRC;

|PROCESO Desglose;
     |HAZ AlberoDesgloseOF;
|FPRC;

|PROCESO Etiquetas;
     |PUSHV  1131 1447;
     |PINPA #0#albm0049;
     |PINDA #0#albm0049;
     |ENDATOS #1#albm0049;
     |SI FSalida = 0;
          |HAZ ImprimeBarras;
     |FINSI;
     |POPV;
|FPRC;

|PROCESO Incidencia;
     |HAZ Tipo9;

     |HAZ ModIncidencia;
     ||HAZ VerIncidencia;

     nSwPrimero = 1;
     |HAZ ElGrid;
|FPRC;

|PROCESO ModIncidencia;
      |ABRE #albm0035;
      |VENTANA 0501, 3299, -1, -1, "";
      nVentana1 = FSalida;

      |ABRE #19;
      #19#0 = #albm0046#0;
      |LEE 040#19=;
      |SI FSalida ! 0;  |DDEFECTO #19;  |FINSI;
      |CIERRA #19;

      |ABRE #29;
      #29#0 = #albm0046#1;
      |LEE 040#29=;
      |SI FSalida ! 0;  |DDEFECTO #29;  |FINSI;
      |CIERRA #29;

      |ABRE #112;
      #112#0 = #albm0046#3;
      |LEE 040#112=;
      |SI FSalida ! 0;  |DDEFECTO #112;  |FINSI;
      |CIERRA #112;

      nSwArtDOS = 0;
      nNueva = 0;
      aParOri = #albm0046#45;
      |QBF aParOri;
      |SI aParOri ! "";
           #35#0 = #albm0046#45;
           #35#2 = #albm0046#1;
           #35#4 = #albm0046#2;
           |LEE 000#35=;
           |SI FSalida ! 0;
                #35#0 = #albm0046#0;
                #35#2 = #albm0046#1;
                #35#4 = #albm0046#2;
                |LEE 000#35=;
           |SINO;
               nSwArtDOS = 1;
           |FINSI;
      |SINO;
           #35#0 = #albm0046#0;
           #35#2 = #albm0046#1;
           #35#4 = #albm0046#2;
           |LEE 000#35=;
      |FINSI;

      |SI FSalida ! 0;
         |DDEFECTO #albm0035;
         |SI nSwArtDOS = 0;
              #albm0035#0 = #albm0046#0;
         |SINO;
              #albm0035#0 = #albm0046#45;
         |FINSI;
         #albm0035#2 = #albm0046#1;
         #albm0035#4 = #albm0046#2;
         |GRABA 020#albm0035.b;
         nNueva = 1;
      |FINSI;

      #albm0035#1  = #19#1;
      #albm0035#3  = #29#1;
      #albm0035#5  = #albm0046#3;
      #albm0035#6  = #112#1;

      |PINPA #0#35; |PINDA #0#35;
      |ENDATOS #2#35;
      |SI FSalida = 0;
         |GRABA 020#albm0035.a;
         |SI #albm0035#40 = "S"; |PTEC 807; |FINSI;
      |SINO;
         |SI nNueva = 1; |BORRA 020#albm0035.a; |FINSI;
      |FINSI;
      |LIBERA #albm0035;
      |CIERRA #albm0035;

      |FINVENTANA nVentana1;
|FPRC;

|PROCESO VerIncidencia;
     |ABRE #35;
     |VENTANA 0501, 3299, -1, -1, "";
     nVentana1 = FSalida;

     |ABRE #19;
     #19#0 = #albm0046#0;
     |LEE 040#19=;
     |SI FSalida ! 0;  |DDEFECTO #19;  |FINSI;
     |CIERRA #19;

     |ABRE #29;
     #29#0 = #albm0046#1;
     |LEE 040#29=;
     |SI FSalida ! 0;  |DDEFECTO #29;  |FINSI;
     |CIERRA #29;

     |ABRE #112;
     #112#0 = #albm0046#3;
     |LEE 040#112=;
     |SI FSalida ! 0;  |DDEFECTO #112;  |FINSI;
     |CIERRA #112;

     |DDEFECTO #35;

     ||ARA
     aParOri = #albm0046#45;
     |QBF aParOri;
     |SI aParOri ! "";
          #35#0 = #albm0046#45;
          #35#2 = #albm0046#1;
          #35#4 = #albm0046#2;
          |LEE 000#35=;
          |SI FSalida ! 0;
               #35#0 = #albm0046#0;
               #35#2 = #albm0046#1;
               #35#4 = #albm0046#2;
               |LEE 000#35=;
          |FINSI;
     |SINO;
          #35#0 = #albm0046#0;
          #35#2 = #albm0046#1;
          #35#4 = #albm0046#2;
          |LEE 000#35=;
     |FINSI;

     #35#1  = #19#1;
     #35#3  = #29#1;
     #35#5  = #albm0046#3;
     #35#6  = #112#1;

     |PINPA #0#35;
     |PINDA #0#35;
     |LEETECLA aTecla;
     |CIERRA #35;
     |FINVENTANA nVentana1;
|FPRC;


|PROCESO ImprimeBarras;
     aBase = "\BARRAS\";
     aAbre = aBase + "barcods0.exe";
     |XABRE "EC", aAbre, nHandle;
     |SI FSalida < 0;
         |MENAV "0000 No tiene instalado el Programa de Emision de Codigo de Barras. Pongase en contacto con su Distribuidor.";
         |ACABA;
     |FINSI;

     aBase = "\BARRAS\";
     aAbre = aBase + "BARRAS.TXT";
     |XABRE "CW", aAbre, nHandle;
     |SI FSalida < 0;  |ACABA;  |FINSI;

     nBlancos = #albm0049#1 - 1;
     |SI nBlancos ] 1;
          aAlfa = "            #      #      #";
          aAlfa = aAlfa + " " + &13 + &10;
          |PARA nEtiq = 1; |SI nEtiq [ nBlancos; |HACIENDO nEtiq = nEtiq + 1;
               |XGRABA nHandle, aAlfa;
          |SIGUE;
     |FINSI;
     aCodigo = #albm0046#2;
     |QBT aCodigo;
     aCodigo = aCodigo - "/";
     aCodigo = aCodigo - "\";
     aAlfa = (("000000000000" + aCodigo) % -112) + "#";
     aAlfa = aAlfa + (("000000" + aCodigo) % -106) + "#";
     aAlfa = aAlfa + #albm0046#3;             |QBF aAlfa;

     |ABRE #19;
     #19#0 = #albm0046#0;
     |LEE 040#19=;
     |SI FSalida ! 0;  |DDEFECTO #19;  |FINSI;
     |CIERRA #19;

     aAlfa = aAlfa + "#" + (#19#195 % 131);     |QBF aAlfa;
     aAlfa = aAlfa + " " + &13 + &10;

     |PARA nEtiq = 1; |SI nEtiq [ #albm0049#2; |HACIENDO nEtiq = nEtiq + 1;
          |XGRABA nHandle, aAlfa;
     |SIGUE;

     |XCIERRA nHandle;

     aAbre = aBase + "IMPRIME.BAT";
     |XABRE "CW", aAbre, nHandle;
     |SI FSalida < 0;  |ACABA;  |FINSI;

     aAlfa = "CD \" + &13 + &10;
     |XGRABA nHandle, aAlfa;

     aAlfa = "CD BARRAS" + &13 + &10;
     |XGRABA nHandle, aAlfa;

     aAlfa = aBase + "barcods0 " + "BARRAS.TXT";
     |XGRABA nHandle, aAlfa;

     |XCIERRA nHandle;

     aAlfa = "\BARRAS\IMPRIME.BAT";

     |RSYSTEM aAlfa;
     |RFBORRA aAbre;
|FPRC;

|PROCESO GuardaArt; |TIPO 7;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 2;
          aGuardaArt = #albm0046#0;
     |FINSI;
     |SI nModo = 1;      ||Alta ... permitir entrar Proveedor
          |C_M #albm0046#3, 1, "S";
     |SINO;
          |C_M #albm0046#3, 1, "N";
     |FINSI;
|FPRC;


|PROCESO GuardaPrecio; |TIPO 7;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 2;
          nPrecioAnt = #albm0046#38;
     |SINO;
          |SI nModo = 1;
               nPrecioAnt = 0;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Reclasifica; |TIPO 0;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 2;
          |SI #albm0046#0 ! aGuardaArt;
               aMensaje = "0000NReclasificar Articulo en la Partida?";
               |CONFI aMensaje;
               |SI FSalida = 0;
                    aParOri = #albm0046#45;
                    |QBF aParOri;
                    |SI aParOri = "";
                         #albm0046#45 = aGuardaArt;
                    |FINSI;

                    enAlberoDentro = 1;
                    |ABRE #dsm00024;
                    #dsm00024#0 = aGuardaArt;
                    #dsm00024#1 = #albm0046#1;
                    #dsm00024#2 = #albm0046#2;
                    |LEE 101#dsm00024.=;
                    |SI FSalida = 0;
                         |BORRA 020#dsm00024.a;
                    |FINSI;
                    |LIBERA #dsm00024;
                    enAlberoDentro = 0;
                    |PTEC 802;
                    |PTEC 802;
                    |PTEC 815;
               |SINO;
                    #albm0046#0 = aGuardaArt;
                    |PINTA #albm0046#0;
               |FINSI;
          |FINSI;
     |SINO;
          |SI nModo = 1;
               |ABRE #19;
               |DDEFECTO #19;
               #19#0 = #albm0046#0;
               |LEE 000#19=;
               |CIERRA #19;

               |PINTA 0833, #19#1;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO albm0047;
     |BORRA 020#albm0047.a;
|FPRC;

|DEFBUCLE albm0047;
 #albm0047#1;
 ;
 #albm0046#1, #albm0046#2, 001;
 #albm0046#1, #albm0046#2, 999;
 be;
 NULL, albm0047;
|FIN;

|PROCESO Baja; |TIPO 2;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 3;
          |BUCLE albm0047;
     |FINSI;
|FPRC;

|PROCESO Importe; |TIPO 0;
     #albm0046#46 = #albm0046#38 * #albm0046#31;
     |PINTA #albm0046#46;
|FPRC;

|PROCESO CargaPartidas;
     #1#0 = #referen@#0;
     |LEE 000#1=;
     aAlfa = #1#125; |QBF aAlfa;
     |SI FSalida = 0; |Y aAlfa = aParam;
          |SALVA_DATOS #referen@;
          |REPON_DATOS #0;
          |GRABA 020#0n;
     |FINSI;
|FPRC;

|DEFBUCLE CargaPartidas;
     #referen@#1002;
     ;
     00001, "               ";
     99999, "zzzzzzzzzzzzzzz";
     ;
     NULL, CargaPartidas;
|FIN;

|PROCESO Tip80_46; |TIPO 80;
     aParam = PARAMETRO % 101;
     |SI aParam = "C"; |O aParam = "V"; |O aParam = "M";
          |DDEF aAlfa; aAlfa = aAlfa + "albm0046";
          |CARGA_DEF aAlfa, referen@;
          |SI FSalida = 0;
               nCarga = 1;
               aAlfa = Usuario;
               |NOME_DAT #0 aAlfa; |DELINDEX #0;
               |ABRE #0;
               |ABRE #1;
               |BUCLE CargaPartidas;
               |CIERRA #1;
               |CIERRA #0;
               |DESCARGA_DEF #referen@;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Tip90_46; |TIPO 90;
     |SI nCarga = 1;
          |DELINDEX #0;
          |NOME_DAT #0 "albm0046"; || ¨hace falta?
     |FINSI;
|FPRC;

|PROGRAMA;
     |ABRE #agifa324;
     |DDEFECTO #agifa324;
     #agifa324#0 = "EUR";
     |LEE 000#agifa324.=;
     EURODB = #agifa324#9;
     |CIERRA #agifa324;



     aParametro = PARAMETRO;
     |SI aParametro = "";
          |MANTE #0;
     |SINO;
          aParam = aParametro % 101;
          |SI aParam = "C"; |O aParam = "V"; |O aParam = "M";
               |MANTE #0;
          |SINO;
               aAlmacen = aParametro % 104;
               nAlmacen = aAlmacen;
               aPartida = aParametro % 515;
               |ABRE #albm0046;
               |ABRE #albm0047;
               |DDEFECTO #albm0046;
               #albm0046#1 = nAlmacen;
               #albm0046#2 = aPartida;
               |LEE 000#albm0046.=;
               |PINPA #0#albm0046;
               |PINDA #0#albm0046;
               |PAUSA;
               |CIERRA #albm0047;
               |CIERRA #albm0046;
          |FINSI;
     |FINSI;
|FPRO;

|PROCESO MiniPeso64;
     #albm0064#0 = #albm0046#1;
     #albm0064#1 = #albm0046#2;
     #albm0064#2 = "     ";
     #albm0064#3 = 000000;
|FPRC;

|PROCESO MaxiPeso64;
     #albm0064#0 = #albm0046#1;
     #albm0064#1 = #albm0046#2;
     #albm0064#2 = "99999";
     #albm0064#3 = 999999;
|FPRC;

|PROCESO Pedidos;
     |ABRE #albm0064;
     |PUSHV 0741 2378;
     nRango = 4 + 8 + 16 + 128;
     |LINEAL_SIMPLE #1#64, nRango, 0721, 2378, MiniPeso64, MaxiPeso64, NULL;
     |POPV;
     |CIERRA #albm0064;
|FPRC;

|PROCESO Tipo12_46; |TIPO 12;
     |SI nModo = 2; |O nModo = 1;
          |SI nPrecioAnt ! #albm0046#38;
               enPrecioPar = #albm0046#38;
               eaParRecalculo = #albm0046#2;
               enAlmRecalculo = #albm0046#1;

               aEjecuta = "albz0030;AUTO";
               |SUB_EJECUTA aEjecuta;
          |FINSI;
     |FINSI;
|FPRC;
