|FICHEROS;
     dsz99978  #0;
     iber0002@ #2;
     iber0067@ #3;
     iber0136@ #4;
     iber0019@ #5;
     agifa142  #142;
     agifa077  #77;
     agifa080  #80;
     iber0016@  #16;
     agifa019  #19;
     agifa112  #112;
     dsm00241  #241;
     agifa505@;
     iber0041@  #41;
     dsz00042  #42;
     iber0213@  #213;
     dsm00121  #121;
|FIN;

|VARIABLES;
     aIP       = "";
     aEmp      = "";
     &enNumCaja= 0;
     nDcaja    = 0;
     nHcaja    = 0;
     &eaSerIber= "";
     &Tempo    = "";
     &eaProve  = "";
     &qSerie   = "";
     &nDeci_Base = 0;

     nFactor   = 0;
     nModo     = 0;
     nHay      = 0;
     aTempo0   = "";
     aAlfa     = "";
     aMensa    = "";
     aDef      = "";
     aDef1     = "";
     aDef2     = "";
     aDef3     = "";
     aDef4     = "";
     aCentro   = "";
     nCentro   = "";
     nPrecio   = 0;
     nPrecioCom = 0;
     fFecha    = @;
|FIN;

|PROCESO CargaIber16;
     |DDEF aDef;
     aDef = aDef + "iber0016";
     |CARGA_DEF aDef, iber0016@;
     |SI FSalida ! 0;
          aDef = "0000ERROR al cargar iber0016";
     |FINSI;
|FPRC;

|PROCESO CargaIber41;
     |DDEF aDef;
     aDef = aDef + "iber0041";
     |CARGA_DEF aDef, iber0041@;
     |SI FSalida ! 0;
          aDef = "0000ERROR al cargar iber0041";
     |FINSI;
|FPRC;

|PROCESO CargaIber213;
     |DDEF aDef;
     aDef = aDef + "iber0213";
     |CARGA_DEF aDef, iber0213@;
     |SI FSalida ! 0;
          aDef = "0000ERROR al cargar iber0213";
     |FINSI;
|FPRC;

|PROCESO DescargaDefIber21;
     |SI aDef1 ! ""; |DESCARGA_DEF #iber0002@; |FINSI;
     |SI aDef2 ! ""; |DESCARGA_DEF #iber0067@; |FINSI;
     |SI aDef3 ! ""; |DESCARGA_DEF #iber0136@; |FINSI;
     |SI aDef4 ! ""; |DESCARGA_DEF #iber0019@; |FINSI;
|FPRC;

|PROCESO CargaDefIber21;
     aMensa = "";
     aDef1 = "";
     aDef2 = "";
     aDef3 = "";
     aDef4 = "";
     |DDEF aDef;
     aDef1 = aDef + "iber0002";
     |CARGA_DEF aDef1, iber0002@;
     |SI FSalida ! 0;
          aMensa = "0000Error al cargar " + aDef1;
          |MENAV aMensa;
          |HAZ DescargaDefIber21;
          |ACABA;
     |FINSI;
     aDef2 = aDef + "iber0067";
     |CARGA_DEF aDef2, iber0067@;
     |SI FSalida ! 0;
          aMensa = "0000Error al cargar " + aDef2;
          |MENAV aMensa;
          |HAZ DescargaDefIber21;
          |ACABA;
     |FINSI;
     aDef3 = aDef + "iber0136";
     |CARGA_DEF aDef3, iber0136@;
     |SI FSalida ! 0;
          aMensa = "0000Error al cargar " + aDef3;
          |MENAV aMensa;
          |HAZ DescargaDefIber21;
          |ACABA;
     |FINSI;
     aDef4 = aDef + "iber0019";
     |CARGA_DEF aDef4, iber0019@;
     |SI FSalida ! 0;
          aMensa = "0000Error al cargar " + aDef4;
          |MENAV aMensa;
          |HAZ DescargaDefIber21;
          |ACABA;
     |FINSI;
|FPRC;

|PROCESO ActTarifas1;
     #3#6 = #0#7;
     #3#9 = #0#7;
     #3#10 = ~;
     |GRABA 020#3a;
     |LIBERA #3;
|FPRC;

|DEFBUCLE ActTarifas1;
     #3#1005;
     ;
     nCentro, 01, #0#8, #0#9, #0#1;
     nCentro, 99, #0#8, #0#9, #0#1;
     be;
     NULL, ActTarifas1;
|FIN;

|PROCESO ActTarifas;
     |BUCLE ActTarifas1;
|FPRC;

|DEFBUCLE ActTarifas;
     #0#1003;
     ;
     01, 01, "                    ";
     99, 99, "zzzzzzzzzzzzzzzzzzzz";
     ;
     NULL, ActTarifas;
|FIN;

|PROCESO BuscaPrecio;
     #4#0 = #19#3;
     |LEE 000#4=;
     |SI FSalida = 0;
          |SI #4#2 = "B";
               nPrecio = nPrecioCom + (nPrecioCom * #4#1 / 100);
          |SINO;
               nPrecio = nPrecioCom / ((100 - #4#1) / 100);
          |FINSI;
          nPrecio = nPrecio ! nDeci_Base;
          |SI nPrecio > #3#9;
               #0#0 = #3#1;
               #0#1 = #3#4;
               #0#2 = #3#5;
               #0#3 = #3#6;
               #0#4 = #3#7;
               #0#5 = #3#8;
               #0#6 = #3#9;
               #0#7 = nPrecio;
               #0#8 = #3#2;
               #0#9 = #3#3;
               |GRABA 020#0n;
               nHay = 1;
          |FINSI;
     |FINSI;

     |ERROR10;
|FPRC;

|DEFBUCLE BuscaPrecio;
     #3#2006;
     ;
     #5#0, #5#1, 01, #19#0, "M", fFecha;
     #5#0, #5#1, 99, #19#0, "M", "01.01.0000";
     ;
     NULL, BuscaPrecio;
|FIN;

|PROCESO CentroPuto;
     |BUCLE BuscaPrecio;
|FPRC;

|DEFBUCLE CentroPuto;
     #5#1002;
     ;
     nCentro, 01;
     nCentro, 99;
     ;
     NULL, CentroPuto;
|FIN;

|PROCESO Min;
     #0#0 = 1;
     #0#1 = " " * 20;
     #0#8 = 1;
|FPRC;

|PROCESO Max;
     #0#0 = 99;
     #0#1 = "z" * 20;
     #0#8 = 99;
|FPRC;

|PROCESO FactorUni;
     nFactor = 1;
     |ABRE #241; |SELECT #2#241;
     #241#0 = #19#5;
     #241#2 = #19#205
     |LEE 000#241=;
     |SI FSalida = 0;
          |SI #241#6 = "D";
               nFactor = 1 * #241#5;
          |SINO;
               nFactor = 1 / #241#5;
          |FINSI;
          #241#2 = #19#206;
          |LEE 000#241=;
          |SI FSalida = 0;
               |SI #241#6 = "D";
                    nFactor = nFactor * #241#5;
               |SINO;
                    nFactor = nFactor / #241#5;
               |FINSI;
          |FINSI;
     |FINSI;
     |CIERRA #241;
|FPRC;
컴컴컴컴컴컴컴컴컴컴컴컴컴 Llamadas Externas 컴컴컴컴컴컴컴컴컴컴컴컴컴
|PROCESO ProvIber21;
     |DFICE aAlfa; aCentro = aAlfa % -403; nCentro = aCentro;
     |SI aCentro = 0; |ACABA; |FINSI;

     |ABRE #142; |LEE 000#142p; |CIERRA #142;
     |SI #142#164 = "C"; |ACABA; |FINSI;

     |HAZ CargaDefIber21;
     |SI aMensa = "";
          |ABRE #2;
          |SELECT #2#2;
          #2#0 = aCentro;
          #2#2 = eaProve;
          |LEE 000#2=;
          |SI FSalida ! 0;
               eaProve = "";
               |MENAV "0000Proveedor no homologado...";
               |ERROR;
          |FINSI;
          |CIERRA #2;
          |HAZ DescargaDefIber21;
     |FINSI;
|FPRC;

|PROCESO agifa505;
     |SI eaSerIber = #agifa505@#103; |O eaSerIber = #agifa505@#106;
          eaSerIber = #16#27;
          nHay = 1;
          |ERROR10;
     |FINSI;
|FPRC;

|DEFBUCLE agifa505;
     #agifa505@#1001;
     ;
     nDcaja;
     nHcaja;
     ;
     NULL, agifa505;
|FIN;
컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
|PROCESO SerPrvAlba;
     qSerie = "";
     |ENCAMPO #3#77;
     |SI FSalida = 0;
          eaProve = #77#3;
          |DFICE aCentro;
          aCentro = aCentro % -403;
          |HAZ CargaIber16;
          |SI FSalida = 0;
               |ABRE #16;
               |ABRE #112;
               #112#0 = #77#3;
               |LEE 000#112=;
               |SI FSalida = 0;
                    #16#0 = aCentro;
                    |LEE 000#16=;
                    |SI FSalida = 0;
                         |SI #112#42 = #16#22; qSerie = #16#11; |FINSI;
                         |SI #112#42 = #16#23; qSerie = #16#12; |FINSI;
                         |SI #112#42 = #16#24; qSerie = #16#13; |FINSI;
                         |SI #112#42 = #16#25; qSerie = #16#21; |FINSI;
                    |FINSI;
               |FINSI;
               |CIERRA #112;
               |CIERRA #16;
               |DESCARGA_DEF #iber0016@;
          |FINSI;
     |FINSI;
|FPRC;
컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
|PROCESO MinimoIber;
     |DFICE aAlfa; aCentro = aAlfa % -403; nCentro = aCentro;
     |SI #142#164 ! "D"; |ACABA; |FINSI;

     |HAZ CargaDefIber21;
     |SI aMensa = "";
          |HAZ CreaTempo; aTempo0 = Tempo; |NOME_DAT #0 aTempo0; |DELINDEX #0;
          |ABRE #4;
          |ABRE #0;
          |ABRE #19;
          #19#0 = #80#2;
          |LEE 000#19=;
          |SI FSalida = 0;
               fFecha = ~;
               nHay = 0;
               |SI #19#204 = "S";
                    |HAZ FactorUni;
               |SINO;
                    nFactor = 1;
               |FINSI;
               nPrecioCom = (((#80#6 < #80#8) < #80#26) < #80#45) / nFactor;
               |BUCLE CentroPuto;
               |SI nHay ! 0;
                    |PUSHV 0501 2380;
                    |PINPA #0#0;
                    |ENTLINEAL #1#0, -1, 4, 20, 0, Min, Max;
                    |CONFI "0000NActualizar Calculado como Precio Final...";
                    |SI FSalida = 0;
                         |BUCLE ActTarifas;
                    |FINSI;
                    |POPV;
               |FINSI;
          |FINSI;
          |CIERRA #0;
          |CIERRA #4;
          |CIERRA #19;
          |HAZ DescargaDefIber21;

          |DELINDEX #0; Tempo = aTempo0; |HAZ BoraTempo;
     |FINSI;
|FPRC;

|PROCESO CambiaSerieIber;
     aCentro = eaSerIber % 103;
     |HAZ CargaIber16;
     |SI FSalida = 0;
          |ABRE #16;
          #16#0 = aCentro;
          |LEE 000#16=;
          |SI FSalida = 0;
               nHay = 0;
               aCentro = aCentro + "00";
               nDcaja = aCentro;
               nHcaja = nDcaja + 99;
               |DDEF aDef;
               aDef = aDef + "agifa505";
               |CARGA_DEF aDef, agifa505@;
               |SI FSalida = 0;
                    |BUCLE agifa505;
                    |DESCARGA_DEF #agifa505@;
               |SINO;
                    |MENAV "0000ERROR al cargar agifa505 en dsz99978";
               |FINSI;
               |SI nHay = 0; eaSerIber = ""; |FINSI;
          |FINSI;
          |CIERRA #16;
          |DESCARGA_DEF #iber0016@;
     |FINSI;
|FPRC;
컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
|PROCESO ClaveAlbIber;
     aAlfa = "";
     |PINTA 2426, " Clave:            ";
     E_sup = 10;
     E_inf = "";
     aAlfa = 2434 ? -1;
     |QBF aAlfa;
     |BLIN 24;

     FSalida = 0;
     |SI aAlfa ! "<MYPASWD>";
          |MENAV "0000Clave Incorrecta...";
          FSalida = 1;
     |FINSI;
|FPRC;
컴컴컴컴컴컴컴컴컴컴컴컴컴
|PROCESO CajaDefecto;
     aIP = "";
     |IP_REMOTO aIP;  |QBT aIP;
     |SI aIP = "";
          |IP_LOCAL aIP;
     |FINSI;

     |DFICE aEmp;
     aEmp = aEmp % -205;
     aEmp = aEmp % 103;

     |HAZ CargaIber41;
     |SI FSalida = 0;
          |ABRE #41;
          #41#0 = aEmp;
          #41#1 = "";
          |LEE 000#41];
          aAlfa = "";
          |SI FSalida = 0; |Y #41#0 = aEmp;
               |PARA; |SI FSalida = 0; |Y #41#0 = aEmp; |HACIENDO;
                    aAlfa = #41#3 +"."+ #41#4 +"."+ #41#5 +"."+ #41#6;
                    |SI aAlfa = aIP;
                         enNumCaja = #41#8;
                         |SAL;
                    |FINSI;
                    |LEE 000#41s;
               |SIGUE;
          |FINSI;
          |CIERRA #41;
          |DESCARGA_DEF #iber0041@;
     |FINSI;
|FPRC;
컴컴컴컴컴컴컴컴컴컴컴컴컴
|PROCESO iber0213;

     |DDEFECTO #42;
     #42#0 = #213#0;
     #42#1 = #213#1;
     |LEE 101#42=;
     |SI FSalida = 0;
          #42#5 = #213#4;
          #42#6 = #42#5 - #42#4;
          #42#7 = #213#5;
          |GRABA 020#42a;
          |LIBERA #42;
     |SINO;
          #42#2 = #213#2;
          #42#3 = #213#3;
          #42#4 = 0;
          #42#5 = #213#4;
          #42#6 = #42#5 - #42#4;
          #42#7 = #213#5;
          #42#5 = #213#4;
          |GRABA 020#42n;
     |FINSI;

     |DDEFECTO #121;
     #121#0 = #213#0;
     #121#1 = #213#1;
     |LEE 101#121=;
     |SI FSalida ! 0; |GRABA 020#121b; |FINSI;

     #121#2 = #213#2;
     #121#3 = #213#3;
     #121#4 = #42#4;     ||Uni STock
     #121#5 = #42#5;     ||Recu
     #121#6 = #42#6;     ||Dif
     #121#7 = #213#5;
     |GRABA 020#121a;
     |LIBERA #121;
|FPRC;

|DEFBUCLE iber0213;
     #213#1002;
     ;
     #42#0, "                    ";
     #42#0, "zzzzzzzzzzzzzzzzzzzz";
     ;
     NULL, iber0213;
|FIN;

|PROCESO Iber21Recuento;
     |HAZ CargaIber213;
     |SI FSalida = 0;
          |ABRE #121;
          |BUCLE iber0213;
          |CIERRA #121;
          |DESCARGA_DEF #iber0213@;
     |FINSI;
|FPRC;
