|FICHEROS;
     pcegz058 #0;
     pcegm004 #4;
     pcegm018 #18;
     pcegz064 #63,MANTE;  || Seleeccion
     agifa071 #71;
     agifa134 #134;
     agifa059 #59;
     :/dscarter/def/dscam003 #3;
     :/dscarter/def/dscam045 #45;
|FIN;

|VARIABLES;
     nEmpCarte = 0;
     aEmp = "";
     aAlfa = "";
     aBase = "";
     nEmpresa = 0;
     nHay = 0;
     aFax = "";
     aMail = "";
     aTlf = "";
     aSer = "";
     nPed = 0;
     nClave = 0;
     nAntes = 0;
|FIN;

|PROCESO Tipo66; |TIPO 66;
     |SI nAntes = 0;
          |CONSULTA_CLAVE #1#0;
     |SINO;
          |CONSULTA_CLAVE #2#0;
     |FINSI;
     |SI FSalida = 0;
          |ERROR;
          |LEE 000#0=;
          |PONCONTROL 23, "SI";
          |PTEC 809;
     |SINO;
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO Tipo11; |TIPO 11;
     |SI FSalida = 13;
          |ERROR;
          nClave = (nAntes + 1) $ 2;
          |PTEC 807;
     |FINSI;
|FPRC;

|PROCESO Tipo0; |TIPO 0;
     |SI nAntes = 0;
          |SI #0#0 ! Contenido; |ACABA; |FINSI;
     |SINO;
          |SI #0#3 ! Contenido; |ACABA; |FINSI;
     |FINSI;

  |ET Otra;
     |ENCAMPO #5#0;
     |SI FSalida = 0; |O FSalida = 3;
          |ENCAMPO #6#0;
          |SI FSalida = 2; |VETE Otra; |FINSI;
          |SI FSalida = 0; |O FSalida = 3;
               |GRABA 020#0a;
               #4#25 = #0#7;
               #4#0 = #0#8;
               |LEE 121#4=;
               |SI FSalida = 0;
                    #4#71 = #0#6;
                    #4#72 = #0#5;
                    |GRABA 020#4a;
                    |LIBERA #4;
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO LeePedido;
     aFax = "";
     aTlf = "";
     aMail = "";
     |ABRE #59;
     #59#14 = #3#0;
     #59#0 = #3#1;
     #59#1 = 1;
     |LEE 000#59];
     |SI FSalida = 0; |Y #59#14 = #3#0; |Y #59#0 = #3#1;
          |ABRE #71;
          #71#29 = #59#15;
          #71#0 = #59#2;
          #71#1 = 1;
          |LEE 000#71];
          |SI FSalida = 0; |Y #71#29 = #59#15; |Y #71#0 = #59#2;
               |ABRE #4;
               #4#25 = #71#31;
               #4#0 = #71#12;
               |LEE 000#4=;
               |SI FSalida = 0;
                    aFax = #4#72; |QBF aFax;
                    aTlf = #4#77; |QBF aTlf;
                    aMail = #4#71; |QBF aMail;
                    aSer = #4#25;
                    nPed = #4#0;
               |FINSI;
               |CIERRA #4;
          |FINSI;
          |CIERRA #71;
     |FINSI;
     |CIERRA #59;
|FPRC;

|PROCESO Recibos;
     |SI #3#14 = "C"; |O #3#14 = "P";
          |SI #3#12 = #18#4; |O #3#12 = #18#5; |O #3#12 = #18#6;
                    |O #3#12 = #18#13; |O #3#12 = #18#14; |O #3#12 = #18#15;
                    |O #3#12 = #18#17; |O #3#12 = #18#18; |O #3#12 = #18#19;
               |ACABA;
          |FINSI;
          |SI #3#75 = #18#7; |O #3#75 = #18#8; |O #3#75 = #18#9;
               |ACABA;
          |FINSI;

          |HAZ LeePedido;

          |SI aFax = ""; |Y aMail = "";
               #134#49 = #3#0;
               #134#0 = #3#1;
               |LEE 000#134=;
               |SI FSalida = 0;
                        |Y #134#2 ] #63#0; |Y #134#2 [ #63#1;
                        |Y #134#1 ] #63#2; |Y #134#1 [ #63#3;
                        |Y #134#49 ] #63#4; |Y #134#49 [ #63#5;
                        |Y #134#0 ] #63#6; |Y #134#0 [ #63#7;
                    |DDEFECTO #0;
                    #0#0 = #134#49;
                    #0#1 = #134#0;
                    #0#2 = #134#1;
                    #0#3 = #134#2;
                    #0#4 = #134#3;
                    #0#5 = aFax;
                    #0#6 = aMail;
                    #0#7 = aSer;
                    #0#8 = nPed;
                    |GRABA 000#0n;
                    nHay = 1;
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE Recibos;
     #3#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, Recibos;
|FIN;

|PROCESO dscam045;
     |SI nEmpCarte ! #45#6;
          nEmpCarte = #45#6;

          aEmp = ("00000" + #45#6) % -105;
          aAlfa = aBase + "dscarter/fich/" + aEmp + "/";
          |PATH_DAT #3#aAlfa#;
          |BUCLE Recibos;
     |FINSI;
|FPRC;

|DEFBUCLE dscam045;
     #45#1;
     ;
     nEmpresa, 001;
     nEmpresa, 999;
     ;
     NULL, NULL, NULL, NULL, NULL, dscam045;
     #45#6;
|FIN;

|PROCESO Min;
     #0#0 = "     ";
     #0#1 = 1;
     #0#3 = "      ";
|FPRC;

|PROCESO Max;
     #0#0 = "zzzzzz";
     #0#1 = 999999;
     #0#3 = "zzzzzz";
|FPRC;

|PROGRAMA;
     |CLS;
     |PINPA #0#63;
     |PINDA #0#63;
     |ENTLINEAL #1#0, 1, 7, 20, 0, Min, Max;
     |ENDATOS #1#63;
     |SI FSalida ! 0; |ACABA; |FINSI;

     |ABRE #18; |LEE 000#18p; |CIERRA #18;

     |DBASS aBase;
     aAlfa = aBase + "dscarter/fich/";
     |PATH_DAT #45 aAlfa;

     |DFICE aAlfa;
     aAlfa = aAlfa % -205;
     nEmpresa = aAlfa;
     nEmpCarte = 0;

     aAlfa = Usuario;
     |NOME_DAT #0 aAlfa; |DELINDEX #0;

     |ABRE #0;
     |ABRE #134;
     |BUCLE dscam045;
     |CIERRA #134;
     |CIERRA #0;

     |SI nHay = 0;
          |MENAV "0000No hay facturas...";
     |SINO;
          |ABRE #4;
          |PARA nClave = 0; |SI nClave ] 0; |HACIENDO;
               nAntes = nClave;
               nClave = -1;
               |SI nAntes = 0;
                    |C_M #0#0, 1, "S";
                    |ENTLINEAL #1#0, 1, 4, 20, 0, Min, Max;
               |SINO;
                    |C_M #0#0, 1, "N";
                    |ENTLINEAL #2#0, 1, 4, 20, 0, Min, Max;
               |FINSI;
          |SIGUE;
          |CIERRA #4;
     |FINSI;

     |DELINDEX #0;
|FPRO;
