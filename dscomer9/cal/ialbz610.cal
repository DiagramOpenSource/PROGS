     Partes

|FICHEROS;
     ialbz010 #0;
     ialbm017 #17;
     ialbm018 #18;

     refere2@;
     refere3@;
|FIN;

|VARIABLES;
     &enPestana = 0;
     &eaCliDoc = "";
     &enModo = 0;
     &enError = 0;
     &eaMatricula = "";
     &eaSerAviso = "";
     &enNumAviso = 0;
     v0 = "000000";
     v1 = "   ";
     v2 = 0;
     aPath = "";
     nPanta = 0;
     aAlfa = "";
     nRango = 0;
     nPos1 = 0;
     nPos2 = 0;
     nEvtFoco = 0;
     nBtnSalir6 = 0;
     nBtnModi6 = 0;
     nBtnCierra6 = 0;
     nBtnHisMaq6 = 0;
     nBtnAsgMat6 = 0;

     nGrid2 = 0;
     nGrid3 = 0;
|FIN;

|PROCESO EventoFoco6;
     enPestana = 6;
|FPRC;

|PROCESO BtnModi6;
     |LEE 000#refere2@.=;
     |SI FSalida ! 0; |ACABA; |FINSI;

     |SI #refere2@#3 = "C";
          |MENAV "0000Parte cerrado..."; |ACABA;
     |FINSI;

     |ABRE #17;
     #17#0 = #refere2@#0;
     #17#1 = #refere2@#1;
     |LEE 020#17=;
     |CIERRA #17;

     |SALVA_DATOS #refere2@;
     |REPON_DATOS #18;

     enModo = 2; |HAZ EditaParte;
     |SI enError = 0;
          #refere2@#1 = 0; |REFRESCACONTROL nGrid3;
          |SALVA_DATOS #18;
          |REPON_DATOS #refere2@;
          |LEE 000#refere2@.=;
          |REFRESCACONTROL nGrid2;
     |FINSI;
|FPRC;

|PROCESO BtnCierra6;
     |LEE 000#refere2@.=;
     |SI FSalida ! 0; |ACABA; |FINSI;
     |SI #refere2@#3 = "C";
          |MENAV "0000Parte ya cerrado..."; |ACABA;
     |FINSI;

     |ABRE #17;
     #17#0 = #refere2@#0;
     #17#1 = #refere2@#1;
     |LEE 020#17=;
     |CIERRA #17;
     |SALVA_DATOS #refere2@;
     |REPON_DATOS #18;

     |HAZ CierraParte;
     |SI enError = 0;
          #refere2@#1 = 0; |REFRESCACONTROL nGrid3;
          |SALVA_DATOS #18;
          |REPON_DATOS #refere2@;
          |LEE 000#refere2@.=;
          |REFRESCACONTROL nGrid2;
     |FINSI;
|FPRC;

|PROCESO BtnHisMaq6;
     |LEE 000#refere2@.=;
     |SI FSalida ! 0; |ACABA; |FINSI;

     eaMatricula = #refere2@#41;
     eaSerAviso = #refere2@#0;
     enNumAviso = #refere2@#1;
     |HAZ HistoMaquina;
|FPRC;

|PROCESO BtnAsgMat6;
     |LEE 000#refere2@.=;
     |SI FSalida ! 0; |ACABA; |FINSI;

     |ABRE #17;
     #17#0 = #refere2@#0;
     #17#1 = #refere2@#1;
     |LEE 020#17=;
     |CIERRA #17;
     |SALVA_DATOS #refere2@;
     |REPON_DATOS #18;
     |HAZ AsignaMatriPar;
     |SI enError = 0;
          |REFRESCACONTROL nGrid2;
     |FINSI;
|FPRC;

|PROCESO Evento62;
     |SI FSalida = 1; |O FSalida = 2;
          |LEE 000#refere2@.=;
          |SI FSalida = 0;
               eaCliDoc = #refere2@#39;
               #0#6 = #refere2@#12;
               #0#7 = #refere2@#13;
               #0#8 = #refere2@#14;
          |SINO;
               #0#6 = "";
               #0#7 = "";
               #0#8 = "";
          |FINSI;
          |PINPA #6#0;
          |PINTA 509, #0#6;
          |PINTA 609, #0#7;
          |PINTA 709, #0#8;
          |REFRESCACONTROL nGrid3;
     |FINSI;
|FPRC;

|PROCESO Evento63;
     |SI FSalida = 1; |O FSalida = 2;
          |LEE 000#refere3@.=;
     |FINSI;
|FPRC;

|PROCESO Min62;
     #refere2@#0 = "     ";
     #refere2@#1 = 000000;
     #refere2@#2 = 1;
     #refere2@#39 = #0#0;
     #refere2@#11 = #0#1;
     #refere2@#10 = #0#2;
     |SI #0#0 = "000000"; #refere2@#39# = "      "; |FINSI;
     |SI #0#1 = "000"; #refere2@#11# = "   "; |FINSI;
|FPRC;

|PROCESO Max62;
     #refere2@#0 = "zzzzz";
     #refere2@#1 = 999999;
     #refere2@#2 = 999;
     #refere2@#39 = #0#0;
     #refere2@#11 = #0#1;
     #refere2@#10 = #0#2;
     |SI #0#0 = "000000"; #refere2@#39# = "zzzzzz"; |FINSI;
     |SI #0#1 = "   "; #refere2@#11# = "zzz"; |FINSI;
     |SI #0#1 = "000"; #refere2@#11# = "zzz"; |FINSI;
     |SI #0#2 = 0; #refere2@#10# = 99999; |FINSI;
|FPRC;

|PROCESO Min63;
     #refere3@#0 = #refere2@#0;
     #refere3@#1 = #refere2@#1;
     #refere3@#2 = #refere2@#2;
     #refere3@#3 = 1;
|FPRC;

|PROCESO Max63;
     #refere3@#0 = #refere2@#0;
     #refere3@#1 = #refere2@#1;
     #refere3@#2 = #refere2@#2;
     #refere3@#3 = 999;
|FPRC;

|PROCESO Actualiza6;
     |DESTRUYECONTROL nGrid2;

     |PINPA #6#0; nPanta = FSalida;
     nRango = 4 + 8 + 16 + 32;
     nPos1 = 0902;
     nPos2 = 2498;

     |SI #0#0 ! v0;
          |SI #0#1 ! v1;
               |LINEAL_SIMPLE #2#refere2@, nRango, nPos1, nPos2, Min62, Max62, Evento62;
          |SINO;
               |LINEAL_SIMPLE #3#refere2@, nRango, nPos1, nPos2, Min62, Max62, Evento62;
          |FINSI;
     |SINO;
          |SI #0#1 ! v1;
               |SI #0#0 ! v0;
                    |LINEAL_SIMPLE #4#refere2@, nRango, nPos1, nPos2, Min62, Max62, Evento62;
               |SINO;
                    |LINEAL_SIMPLE #5#refere2@, nRango, nPos1, nPos2, Min62, Max62, Evento62;
               |FINSI;
          |SINO;
               |SI #0#0 ! v0;
                    |LINEAL_SIMPLE #6#refere2@, nRango, nPos1, nPos2, Min62, Max62, Evento62;
               |SINO;
                    |LINEAL_SIMPLE #7#refere2@, nRango, nPos1, nPos2, Min63, Max62, Evento62;
               |FINSI;
          |FINSI;
     |FINSI;
     nGrid2 = FSalida;
     #refere2@#1 = 0; |REFRESCACONTROL nGrid3;
     |REFRESCACONTROL nGrid2;
|FPRC;

|PROCESO PintaPan6;
     |DDEF aPath;
     aAlfa = aPath + "ialbm018"; |CARGA_DEF aAlfa, refere2@;
     aAlfa = aPath + "ialbm019"; |CARGA_DEF aAlfa, refere3@;
     |ABRET #refere2@;
     |ABRET #refere3@;

     |PINPA #6#0; nPanta = FSalida;
     nRango = 4 + 8 + 16 + 32;
     nPos1 = 2502;
     nPos2 = 3398;
     |LINEAL_SIMPLE #1#refere3@, nRango, nPos1, nPos2, Min63, Max63, Evento63;
     nGrid3 = FSalida;

     nRango = 4 + 8 + 16 + 32;
     nPos1 = 0902;
     nPos2 = 2498;
     |LINEAL_SIMPLE #1#refere2@, nRango, nPos1, nPos2, Max62, Min62, Evento62;
     nGrid2 = FSalida;

     |CONTROL_SIMPLE nPanta, "EVENTOPAN", 0000, 0000, EventoFoco6;
     nEvtFoco = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,Modificar", 0801, 0813, BtnModi6;
     nBtnModi6 = FSalida;
     |CONTROL_SIMPLE nPanta, "BOTON,Cerrar", 0814, 0826, BtnCierra6;
     nBtnCierra6 = FSalida;
     |CONTROL_SIMPLE nPanta, "BOTON,Histo. Maq.", 0827, 0839, BtnHisMaq6;
     nBtnHisMaq6 = FSalida;
     |CONTROL_SIMPLE nPanta, "BOTON,Asig. Mat.", 0840, 0852, BtnAsgMat6;
     nBtnAsgMat6 = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,{{207}} Salir", 3488, 3498, BtnSalir;
     nBtnSalir6 = FSalida;

     |PINTA 509, #0#6;
     |PINTA 609, #0#7;
     |PINTA 709, #0#8;
|FPRC;

|PROCESO DestruyePan6;
/*
     |DESTRUYECONTROL nGrid2;
     |DESTRUYECONTROL nGrid3;

     |FIN_CONTROL_WINDOWS nEvtFoco;
     |FIN_CONTROL_WINDOWS nBtnModi6;
     |FIN_CONTROL_WINDOWS nBtnCierra6;
     |FIN_CONTROL_WINDOWS nBtnHisMaq6;
     |FIN_CONTROL_WINDOWS nBtnAsgMat6;

     |FIN_CONTROL_WINDOWS nBtnSalir6;
*/

     |CIERRAT #refere3@;
     |CIERRAT #refere2@;
     |DESCARGA_DEF #refere3@;
     |DESCARGA_DEF #refere2@;
|FPRC;
