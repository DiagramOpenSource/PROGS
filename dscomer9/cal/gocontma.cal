|FICHEROS;
   gocontma #0;
   gotmpcon #10;
   gopartia #11;
   agifa722 #12;
   agifa724 #13;
   agifa007 #14;

   goparame #100;

   cacuent@ #1000;
   camandi@ #1001;
   caconce@ #1002;
   caconas@ #1003;
   canempr@ #1004;
   camasic@ #1005;
   caenlac@ #1006;

   goparti@ #5000;
|FIN;

|VARIABLES;
   &fich_alta = "";
   &enNoModif = 0;
   &PRMNT_enVer   = 0;

   fFechaIni = @;
   fFechaFin = @;

   nBusca    = 0;
   nTecla    = 0;
   nMaxDigit = 0;
   nCalc     = 0;
   nCalc1    = 0;
   nSwHay    = 0;
   nDiario   = 0;
   nDocum    = 0;
   nDocgpt   = 0;
   nLinea    = 0;
   nPerAnt   = 0;
   FEstado   = 0;

   fFecha    = @;

   aAlfa     = "";
   aAlfa1    = "";
   aSerie    = "";

   aAlfa1a   = "";
   aAlfa2a   = "";
   aAlfa3a   = "";
   aAlfa4a   = "";
   aAlfa1b   = "";
   aAlfa2b   = "";
   aAlfa3b   = "";
   aAlfa4b   = "";
|FIN;

|PROCESO MiraT66; |TIPO 66;
   nTecla = 10; |HAZ MiraCta;
|FPRC;

|PROCESO MiraCta;
   ||nTecla = FSalida;

   |ABRE #goparame;
   |DDEFECTO #goparame;
   |LEE 000#goparame.p;
   |SI FSalida ! 0;
      |MENAV "    No existe el control de parametros";
      |CIERRA #goparame;
      |ERROR; |ACABA;
   |FINSI;
   |CIERRA #goparame;

   aAlfa = #goparame#84; |QBF aAlfa;
   aAlfa1 = aAlfa + "def/cacuenta.mas";
   |CARGA_DEF aAlfa1, cacuent@;
   |SI FSalida ! 0;
      |MENAV "    No encuentro el fichero del plan contable";
      |ERROR; |ACABA;
   |SINO;
      aAlfa1 = aAlfa + "def/canempre.mas";
      |CARGA_DEF aAlfa1, canempr@;
      |SI FSalida ! 0;
         |DESCARGA_DEF #cacuent@;
         |MENAV "    No encuentro el fichero empresas";
         |ERROR; |ACABA;
      |FINSI;
    |FINSI;

    aAlfa1 = aAlfa + "fich/"; |PATH_DAT #canempr@#aAlfa1#;
    aAlfa1 = aAlfa + "fich/" + (("00000" + #goparame#85) % -105) + #goparame#86 + "/";
    |PATH_DAT #cacuent@#aAlfa1#;

    |ABRE #canempr@;
    #canempr@#2 = #goparame#85;
    #canempr@#3 = #goparame#86;
    |LEE 000#canempr@.=;
    |SI FSalida ! 0; |DDEFECTO #canempr@; |FINSI;
    |CIERRA #canempr@;

    nMaxDigit = 0;
    |SI #canempr@#14 ] nMaxDigit; nMaxDigit = #canempr@#14; |FINSI;
    |SI #canempr@#15 ] nMaxDigit; nMaxDigit = #canempr@#15; |FINSI;
    |SI #canempr@#16 ] nMaxDigit; nMaxDigit = #canempr@#16; |FINSI;
    |SI #canempr@#17 ] nMaxDigit; nMaxDigit = #canempr@#17; |FINSI;
    |SI #canempr@#18 ] nMaxDigit; nMaxDigit = #canempr@#18; |FINSI;
    |SI #canempr@#19 ] nMaxDigit; nMaxDigit = #canempr@#19; |FINSI;

    |ABRE #cacuent@;
    |SI nTecla = 10;
      #cacuent@#0 = #gocontma#Campo#;
      |LEE 000#cacuent@.];
      |CONSULTA_CLAVE #1#cacuent@;
      |SI FSalida = 0;
         #gocontma#Campo# = #cacuent@#0; |PINTA #gocontma#Campo#;
         #gocontma#7 = #cacuent@#2; |PINTA #gocontma#7;
      |FINSI;
    |FINSI;

    aAlfa = #gocontma#Campo#; |QBF aAlfa;
    aAlfa = aAlfa - ".";
    |PARA; |SI FSalida !0; |HACIENDO;
      aAlfa1 = aAlfa % 0; nCalc = aAlfa1;
      nCalc = nMaxDigit - nCalc;
      aAlfa1 = "0" * nCalc;
      nCalc = FSalida + 98; aAlfa1 = aAlfa1 + (aAlfa % nCalc);
      nCalc = ((FSalida / 100) ! 0) + 99;
      aAlfa1 = (aAlfa % nCalc) + aAlfa1; aAlfa = aAlfa1;
      aAlfa = aAlfa - ".";
    |SIGUE;
    #gocontma#Campo# = aAlfa; |PINTA #gocontma#Campo#;

    aAlfa = #gocontma#Campo#; |QBF aAlfa;
    aAlfa = aAlfa % 0; nCalc = aAlfa;
    |SI nCalc = #canempr@#14; #cacuent@#1 = 1; |FINSI;
    |SI nCalc = #canempr@#15; #cacuent@#1 = 2; |FINSI;
    |SI nCalc = #canempr@#16; #cacuent@#1 = 3; |FINSI;
    |SI nCalc = #canempr@#17; #cacuent@#1 = 4; |FINSI;
    |SI nCalc = #canempr@#18; #cacuent@#1 = 5; |FINSI;
    |SI nCalc = #canempr@#19; #cacuent@#1 = 6; |FINSI;
    #cacuent@#0 = #gocontma#Campo#;
    |LEE 000#cacuent@.=;
    |SI FSalida ! 0;
       |MENAV "    Cuenta inexistente";
       |ERROR; |ACABA;
    |SINO;
       |SI #cacuent@#3 = "N";
         aAlfa = "    NEsta seleccionando una cuenta sin pase directo. " + &191 + " Continuar ? ";
         |CONFI aAlfa;
         |SI FSalida ! 0; |ERROR; |FINSI;
       |SINO;
         #gocontma#7 = #cacuent@#2; |PINTA #gocontma#7;
       |FINSI;
    |FINSI;

    |CIERRA #canempr@; |DESCARGA_DEF #canempr@;
    |CIERRA #cacuent@; |DESCARGA_DEF #cacuent@;
|FPRC;

|PROCESO MiraDia;
   nTecla = FSalida;

   |ABRE #goparame;
   |DDEFECTO #goparame;
   |LEE 000#goparame.p;
   |SI FSalida ! 0;
      |MENAV "    No existe el control de parametros";
      |CIERRA #goparame;
      |ERROR; |ACABA;
   |FINSI;
   |CIERRA #goparame;

   aAlfa = #goparame#84; |QBF aAlfa;
   aAlfa1 = aAlfa + "def/camandia.mas";
   |CARGA_DEF aAlfa1, camandi@;
   |SI FSalida ! 0;
      |MENAV "    No encuentro el fichero de diarios";
      |ERROR; |ACABA;
   |FINSI;

   aAlfa1 = aAlfa + "fich/" + (("00000" + #goparame#85) % -105) + #goparame#86 + "/";
   |PATH_DAT #camandi@#aAlfa1#;

   |ABRE #camandi@;
   |SI nTecla = 10;
      #camandi@#0 = #gocontma#Campo#;
      |LEE 000#camandi@.];
      |CONSULTA_CLAVE #1#camandi@;
      |SI FSalida = 0;
         #gocontma#Campo# = #camandi@#0; |PINTA #gocontma#Campo#;
         #gocontma#8 = #camandi@#1; |PINTA #gocontma#8;
      |FINSI;
    |FINSI;

    #camandi@#0 = #gocontma#Campo#;
    |LEE 000#camandi@.=;
    |SI FSalida ! 0;
       |MENAV "    Diario inexistente";
       |ERROR; |ACABA;
    |SINO;
       #gocontma#8 = #camandi@#1; |PINTA #gocontma#8;
    |FINSI;

    |CIERRA #camandi@; |DESCARGA_DEF #camandi@;
|FPRC;

|PROCESO MiraCpt;
   nTecla = FSalida;

   |ABRE #goparame;
   |DDEFECTO #goparame;
   |LEE 000#goparame.p;
   |SI FSalida ! 0;
      |MENAV "    No existe el control de parametros";
      |CIERRA #goparame;
      |ERROR; |ACABA;
   |FINSI;
   |CIERRA #goparame;

   aAlfa = #goparame#84; |QBF aAlfa;
   aAlfa1 = aAlfa - "contagen/";
   |SI FSalida ! 0;
      aAlfa1 = aAlfa1 + "modelos/def/dsmom030.mas";
   |SINO;
      aAlfa1 = aAlfa + "def/caconcep.mas";
   |FINSI;

   |CARGA_DEF aAlfa1, caconce@;
   |SI FSalida ! 0;
      |MENAV "    No encuentro el fichero de conceptos automaticos";
      |ERROR; |ACABA;
   |FINSI;

   aAlfa = #goparame#84; |QBF aAlfa;
   aAlfa1 = aAlfa - "contagen/";
   |SI FSalida ! 0;
      aAlfa1 = aAlfa1 + "modelos/fich/";
   |SINO;
      aAlfa1 = aAlfa + "fich/" + (("00000" + #goparame#85) % -105) + #goparame#86 + "/";
   |FINSI;
   |PATH_DAT #camandi@#aAlfa1#;

   |ABRE #caconce@;
   |SI nTecla = 10;
      #caconce@#0 = #gocontma#Campo#;
      |LEE 000#caconce@.];
      |CONSULTA_CLAVE #1#caconce@;
      |SI FSalida = 0;
         #gocontma#Campo# = #caconce@#0; |PINTA #gocontma#Campo#;
         #gocontma#9 = #caconce@#1; |PINTA #gocontma#9;
      |FINSI;
    |FINSI;

    #caconce@#0 = #gocontma#Campo#;
    |LEE 000#caconce@.=;
    |SI FSalida ! 0;
       |MENAV "    Concepto inexistente";
       |ERROR; |ACABA;
    |SINO;
       #gocontma#9 = #caconce@#1; |PINTA #gocontma#9;
    |FINSI;

    |CIERRA #caconce@; |DESCARGA_DEF #caconce@;
|FPRC;

|PROCESO gopartia;
   |SI #gopartia#54 ! 0; |ACABA; |FINSI;

   |DDEFECTO #agifa722;
   #agifa722#0 = "MA";
   #agifa722#1 = #gopartia#4;
   #agifa722#2 = #gopartia#5;
   #agifa722#3 = #gopartia#6;
   |LEE 000#agifa722.];
   FEstado = FSalida;
   aAlfa1a = #agifa722#0; aAlfa2a = #agifa722#1; aAlfa3a = #agifa722#2; aAlfa4a = #agifa722#3;
   aAlfa1b = "MA";        aAlfa2b = #gopartia#4; aAlfa3b = #gopartia#5; aAlfa4b = #gopartia#6;
   |QBF aAlfa1a; |QBF aAlfa2a; |QBF aAlfa3a; |QBF aAlfa4a;
   |QBF aAlfa1b; |QBF aAlfa2b; |QBF aAlfa3b; |QBF aAlfa4b;
   |SI FEstado = 0; |Y aAlfa1a = aAlfa1b; |Y aAlfa2a = aAlfa2b;
    |Y aAlfa3a = aAlfa3b; |Y aAlfa4a = aAlfa4b;
       |ACABA;
   |FINSI;

   |DDEFECTO #gotmpcon;
   #gotmpcon#0 = #gopartia#4;
   #gotmpcon#1 = #gopartia#5;
   #gotmpcon#2 = #gopartia#6;
   #gotmpcon#3 = #gopartia#20;
   aAlfa = #gopartia#17; |QBF aAlfa;
   aAlfa = (("00000" + aAlfa) % -105);
   #gotmpcon#4 = aAlfa;
   #gotmpcon#5 = #gopartia#10;
   #gotmpcon#6 = #gopartia#25;
   #gotmpcon#7 = #gopartia#28;
   #gotmpcon#8 = #gopartia#2;
   |GRABA 020#gotmpcon.n;

   nSwHay = 1;
|FPRC;

|DEFBUCLE gopartia;
#gopartia#1;
28;
"A", #gocontma#0, #gocontma#1, 001, 001, #gocontma#10;
"A", #gocontma#2, #gocontma#3, 999, 999, #gocontma#11;
;
NULL,gopartia;
|FIN;

|PROCESO GrabaAsto;
   nDiario = #gocontma#5;
   fFecha  = #gotmpcon#7;
   |SI aSerie ! #gotmpcon#0; |O nDocgpt ! #gotmpcon#1;
      |LEE 101#caconas@.p;
      |SI FSalida ! 0;
         |DDEFECTO #caconas@;
         |GRABA 020#caconas@.n;
         |LEE 101#caconas@.p;
      |FINSI;

      #caconas@#1 = #caconas@#1 + 1;
      aAlfa = "|NC"; nCalc = #caconas@#aAlfa#;
      |SI nCalc > 2; #caconas@#2 = #caconas@#2 + 1; |FINSI;
      nDocum = #caconas@#1;
      |GRABA 020#caconas@.a;
      |LIBERA #caconas@;

      aSerie = #gotmpcon#0; nDocgpt = #gotmpcon#1;
      nLinea = 10;
   |FINSI;

   #camasic@#0 = nDiario;
   #camasic@#1 = fFecha;
   #camasic@#2 = nDocum;
   |LEE 000#camasic@.=;
   |SI FSalida = 0;
      #camasic@#0 = nDiario;
      #camasic@#1 = fFecha;
      #camasic@#2 = 999999;
      |LEE 000#camasic@.];
      |SI FSalida = 0;
         |SI #camasic@#0 = nDiario; |Y #camasic@#1 = fFecha;
            nDocum = 1;
         |SINO;
            |LEE 000#camasic@.a;
            |SI FSalida = 0;
               |SI #camasic@#0 = nDiario; |Y #camasic@#1 = fFecha;
                   nDocum = #camasic@#2 + 1;
               |SINO;
                  nDocum = 1;
               |FINSI;
            |SINO;
               nDocum = 1;
            |FINSI;
         |FINSI;
      |SINO;
         |LEE 000#camasic@.u;
         |SI FSalida = 0;
            |SI #camasic@#0 = nDiario; |Y #camasic@#1 = fFecha;
               nDocum = #camasic@#2 + 1;
            |SINO;
               nDocum = 1;
            |FINSI;
         |SINO;
            nDocum = 1;
         |FINSI;
      |FINSI;
   |FINSI;

   #agifa722#0 = "MA";
   #agifa722#1 = #gotmpcon#0;
   #agifa722#2 = #gotmpcon#1;
   #agifa722#3 = #gotmpcon#2;
   |LEE 000#agifa722.];
   FEstado = FSalida;
   aAlfa1a = #agifa722#0; aAlfa2a = #agifa722#1; aAlfa3a = #agifa722#2; aAlfa4a = #agifa722#3;
   aAlfa1b = "MA";        aAlfa2b = #gotmpcon#4; aAlfa3b = #gotmpcon#5; aAlfa4b = #gotmpcon#6;
   |QBF aAlfa1a; |QBF aAlfa2a; |QBF aAlfa3a; |QBF aAlfa4a;
   |QBF aAlfa1b; |QBF aAlfa2b; |QBF aAlfa3b; |QBF aAlfa4b;
   |SI FEstado ! 0; |O aAlfa1a ! aAlfa1b; |O aAlfa2a ! aAlfa2b;
    |O aAlfa3a ! aAlfa3b; |O aAlfa4a ! aAlfa4b;
      |DDEFECTO #agifa722;
      #agifa722#0 = "MA";
      #agifa722#1 = #gotmpcon#0;
      #agifa722#2 = #gotmpcon#1;
      #agifa722#3 = #gotmpcon#2;
      #agifa722#4 = nDiario;
      #agifa722#5 = fFecha;
      #agifa722#6 = nDocum;
      #agifa722#7 = 10;
      #agifa722#8 = #goparame#85;
      #agifa722#9 = #goparame#86;
      #agifa722#11 = 1;
      |GRABA 020#agifa722.n;
   |FINSI;

   nPerAnt = #goparame#86;
   #canempr@#2 = #goparame#85;
   #canempr@#3 = #goparame#86;
   |LEE 000#canempr@.=;
   |SI FSalida = 0;
      nBusca = 0;
      aAlfa = fFecha; |QBF aAlfa;
      aAlfa = aAlfa % -102; nCalc = aAlfa;

      |SI #canempr@#26 < 80;
         nCalc1 = 2000 + #canempr@#26;
      |SINO;
         nCalc1 = 1900 + #canempr@#26;
      |FINSI;
      aAlfa = "01." + (("00" + #canempr@#11) % -102) + "." + nCalc1; fFechaIni = aAlfa;
      aAlfa = "01." + (("00" + (#canempr@#12 + 1)) % -102) + "." + (nCalc1 + 1); fFechaFin = aAlfa;
      nCalc1 = fFechaFin; nCalc1 = nCalc1 - 1; fFechaFin = nCalc1;

      |SI nCalc ! #canempr@#26; |O fFecha < fFechaIni; |O fFecha > fFechaFin;
         |PARA; |SI FSalida = 0; |Y #canempr@#2 = #goparame#85; |HACIENDO;
            |LEE 000#canempr@.s;

            |SI #canempr@#26 < 80;
               nCalc1 = 2000 + #canempr@#26;
            |SINO;
               nCalc1 = 1900 + #canempr@#26;
            |FINSI;
            aAlfa = "01." + (("00" + #canempr@#11) % -102) + "." + nCalc1; fFechaIni = aAlfa;
            aAlfa = "01." + (("00" + (#canempr@#12 + 1)) % -102) + "." + (nCalc1 + 1); fFechaFin = aAlfa;
            nCalc1 = fFechaFin; nCalc1 = nCalc1 - 1; fFechaFin = nCalc1;
            |SI nCalc = #canempr@#26; |Y fFecha ] fFechaIni; |Y fFecha [ fFechaFin;
               nBusca = 1; |SAL;
            |FINSI;
         |SIGUE;
         |SI nBusca = 1;
            nPerAnt = #goparame#86;
            #goparame#85 = #canempr@#2;
            #goparame#86 = #canempr@#3;
         |FINSI;
      |FINSI;
   |FINSI;

   |DDEFECTO #caenlac@;
   #caenlac@#0 = nDiario;
   #caenlac@#1 = fFecha;
   #caenlac@#2 = nDocum;
   #caenlac@#3 = nLinea; nLinea = nLinea + 10;

   #agifa724#0 = #gotmpcon#3;
   |LEE 000#agifa724.=;
   |SI FSalida = 0;
      #caenlac@#4 = #agifa724#3;
   |SINO;
      #caenlac@#4 = #gocontma#4;
   |FINSI;
   #caenlac@#5 = 0;
   #caenlac@#6 = #gocontma#6;

   aAlfa = #gocontma#9; |QBF aAlfa;
   aAlfa = aAlfa + " " + #gotmpcon#0 + "/" + (("000000" + #gotmpcon#1) % -106);

   #caenlac@#7  = aAlfa;
   |SI #gotmpcon#6 ] 0;
      #caenlac@#8 = "D";
      #caenlac@#9 = #gotmpcon#6;
   |SINO;
      #caenlac@#8 = "H";
      #caenlac@#9 = -#gotmpcon#6;
   |FINSI;
   #caenlac@#37 = #goparame#85;
   #caenlac@#38 = #goparame#86;

   #agifa007#26 = #gotmpcon#8;
   |LEE 000#agifa007.=;
   |SI FSalida = 0;
      #caenlac@#39 = #agifa007#43;
   |SINO;
      #caenlac@#39 = #gotmpcon#4;
   |FINSI;

   |GRABA 020#caenlac@.n;

   #goparame#86 = nPerAnt;
   nSwHay = 1;
|FPRC;

|DEFBUCLE Asientos;
#gotmpcon#2;
;
INICIO;
FINAL;
;
NULL, GrabaAsto;
|FIN;

|PROCESO Asientos;
     aAlfa = "t1" + Usuario; |NOME_DAT #gotmpcon#aAlfa#;
     |DELINDEX #gotmpcon; |ABRE #gotmpcon;

     |ABRE #goparame;
     |LEE 000#goparame.p;
     |SI FSalida ! 0;
        |MENAV "    Control de defectos inexistente.";
        |CIERRA #goparame; |CIERRA #gotmpcon; |ACABA;
     |FINSI;

     |ABRE #agifa722; |ABRE #agifa724; |ABRE #agifa007;

     aAlfa = "SELECT * FROM gopartia INTO go" + Usuario + " WHERE 34 == 'A' AND ";
     || aAlfa = aAlfa + "4 BETWEEN '" + #gocontma#0 + "','" + #gocontma#2 + "' AND ";
     aAlfa = aAlfa + "5 BETWEEN " + #gocontma#1 + "," + #gocontma#3 + " AND ";
     aAlfa = aAlfa + "28 BETWEEN '" + #gocontma#10 + "','" + #gocontma#11 + "'";
     |SQL aAlfa;
     |SI FSalida = 0;
        aAlfa = "go" + Usuario; |NOME_DAT #gopartia#aAlfa#;
     |FINSI;

     nSwHay = 0;
     |BUCLE gopartia;

     |SI nSwHay = 0;
        |CIERRA #goparame; |CIERRA #gotmpcon; |CIERRA #agifa722; |ACABA;
     |FINSI;

     aAlfa = #goparame#84; |QBF aAlfa;
     aAlfa = aAlfa + "def/caenlace.mas";
     |CARGA_DEF aAlfa, caenlac@;
     |SI FSalida = 0;
        aAlfa = #goparame#84; |QBF aAlfa;
        aAlfa = aAlfa + "def/caconasi.mas";
        |CARGA_DEF aAlfa, caconas@;
        |SI FSalida = 0;
           aAlfa = #goparame#84; |QBF aAlfa;
           aAlfa = aAlfa + "def/camaasic.mas";
           |CARGA_DEF aAlfa, camasic@;
           |SI FSalida = 0;
              aAlfa = #goparame#84; |QBF aAlfa;
              aAlfa = aAlfa + "def/canempre.mas";
              |CARGA_DEF aAlfa, canempr@;
              |SI FSalida = 0;
                 aAlfa = #goparame#84; |QBF aAlfa;
                 aAlfa = aAlfa + "fich/";
                 |PATH_DAT #canempr@#aAlfa#;
                 aAlfa = #goparame#84; |QBF aAlfa;
                 aAlfa = aAlfa + "fich/" + (("00000" + #goparame#85) % -105) + #goparame#86 + "/";
                 |PATH_DAT #caenlac@#aAlfa#; |PATH_DAT #camasic@#aAlfa#;
                 |PATH_DAT #caconas@#aAlfa#;
                 aAlfa = "pu" + Usuario; |QBF aAlfa; |NOME_DAT #caenlac@#aAlfa#;
                 |DELINDEX #caenlac@; |ABRE #caenlac@;
                 |ABRE #caconas@; |ABRE #camasic@; |ABRE #canempr@;
                 nSwHay = 0;

                 |BUCLE Asientos;

                 |SI nSwHay = 1;
                    |HAZ CogeVersion;
                    aAlfa = #goparame#84; |QBF aAlfa;
                    aAlfa = aAlfa + "fich/" + (("00000" + #goparame#85) % -105) + #goparame#86 + "/";
                    fich_alta = aAlfa;
                    aAlfa1 = #goparame#84; |QBF aAlfa1;
                    aAlfa1 = aAlfa1 - "contagen";
                    |SI FSalida ! 0;
                       aAlfa1 = ":contagen/dsapunte.tab;";
                    |SINO;
                       aAlfa1 = ":agicont/dsapunte.tab;";
                    |FINSI;
                    enNoModif = 100;
                    aAlfa = aAlfa1 + aAlfa + ",pu" + Usuario;
                    |CIERRA #caenlac@;
                    |SUB_EJECUTA_NP aAlfa;
                    |ABRE #caenlac@; |LEE 000#caenlac@.c;
                 |FINSI;
              |FINSI;
              |CIERRA #canempr@; |DESCARGA_DEF #canempr@;
              |CIERRA #camasic@; |DESCARGA_DEF #camasic@;
              |CIERRA #caconas@; |DESCARGA_DEF #caconas@;
              |CIERRA #caenlac@; |DELINDEX #caenlac@; |DESCARGA_DEF #caenlac@;
           |FINSI;
        |FINSI;
     |SINO;
        |CIERRA #goparame; |CIERRA #gotmpcon; |CIERRA #agifa722; |ACABA;
     |FINSI;
     |CIERRA #agifa722; |CIERRA #agifa724; |CIERRA #agifa007;
     |CIERRA #goparame;

     |CIERRA #gotmpcon; |DELINDEX #gotmpcon;
|FPRC;

|PROGRAMA;
   |CLS;
   |CABEZA "Contabilizacion traspaso almacenes";
   |PINPA #0#0; |PINDA #0#0;
   |ENDATOS #1#0;
   |SI FSalida = 0;
      |HAZ Asientos;
   |FINSI;
|FPRO;
