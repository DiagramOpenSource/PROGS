|FICHEROS;
     vilam002 #0;
     vilam003 #1;
     vilam004 #4;
     vilam005 #5;
     agifa019 #19;
     agifa091 #91;
     agifa142 #142;
     agifa091 #91;
     vilam001 #100;
     agifa060 #60;
     vilam002@;
     vilam003@;
     agifa019@;
     agifa060@;
|FIN;

|VARIABLES;
     &Tempo = "";
     &eaCuenta = "";
     &enErrCta = 0;
     nOk = 0;
     nHay = 0;
     &enError = 0;
     nNume = 0;
     nModo = 0;
     nConta = 0;
     aSerie = "";
     aAlfa = "";
     aDef = "";
     nHay1Pago = "";
     nHayCobro = "";
     nForma = 0;
     nModoCab = 0;
 {-1}aMenu = "";
     aMenu1 = "2400";
     aMenu2 = "1";
     aMenu3 = "Opciones: ";
     aMenu4 = "F";
     aMenu6 = " Facturas ";

     i = 0;
     aCuenta = "";
     MaximoDigitos = 0;
     nNume1 = 0;
     nPos = 0;
     aAlfa2 = "";
     aAlfa3 = "";
     aAlfa4 = "";
     aEmpresa = "";
     aPeriodo = "";
     nHayArt = 0;
     nHayFan = 0;
     aTmp019 = "";
     aTmp060 = "";
     aPath = "";
     aFam = "";
|FIN;

|PROCESO CheqCobro; |TIPO 6;
     |SI #0Campo ! Contenido;
          |HAZ PreVtoCobrado;
          |SI nHayCobro = "S";
               |MENAV "0000Existen prevencimientos cobrados";
               |ERROR;
          |FINSI;
     |FINSI;
|FPRC

|PROCESO Pendiente; |TIPO 0;
     #0#22 = #0#21 - #0#20;
     |PINTA #0#22;
|FPRC;

|PROCESO CheqPripago; |TIPO 6;
     |SI #0Campo ! Contenido;
          |HAZ Fac1Pago;
          |SI nHay1Pago = "S";
               |MENAV "0000Existen facturas del 'Primer Pago'";
               |ERROR;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO PriPago; |TIPO 0;
     |SI #0#18 = "S";
          |C_M #0#19, 1, "S";
     |SINO;
          |C_M #0#19, 1, "N";
          #0#19 = 0; |PINTA #0#19;
     |FINSI;
|FPRC;

|PROCESO DefSer; |TIPO 7;
     nModo = (FEntrada / 100) ! 0;
     |ABRE #100; |LEE 000#100p; |CIERRA #100;
     |SI nModo = 1; |O nModo = 2;
          #0#0 = #100#1;
     |FINSI;
|FPRC;

|PROCESO Contador; |TIPO 7;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 1;
          aSerie = #0#0;
          #0#1 = 999999;
          |LEE 000#0];
          |SI FSalida = 0;
               |LEE 000#0a;
          |SINO;
               |LEE 000#0u;
          |FINSI;
          |SI FSalida = 0; |Y aSerie = #0#0;
               nConta = #0#1 + 1;
          |SINO;
               nConta = 1;
          |FINSI;
          |DDEFECTO #0;
          #0#0 = aSerie;
          #0#1 = nConta;

          #0#29 = #100#5; |PINTA #0#29;
          #0#30 = #100#6; |PINTA #0#30;
          #0#31 = #100#7; |PINTA #0#31;
     |FINSI;
|FPRC;

|PROCESO CheqCta; |TIPO 0;
     eaCuenta = #0#15;
     |HAZ CheqCta0;
     #0#15 = eaCuenta; |PINTA #0#15;
     |SI enErrCta ! 0; |ERROR; |FINSI;
|FPRC;

|PROCESO ConsuCta; |TIPO 66;
     eaCuenta = #0#15;
     |HAZ ConsuCta0;
     #0#15 = eaCuenta; |PINTA #0#15;
     |SI enErrCta ! 0; |ERROR; |FINSI;
|FPRC;

|PROCESO CheqCli; |TIPO 0;
     |DDEF aAlfa;
     aAlfa = aAlfa + "vilam003";
     |CARGA_DEF aAlfa, vilam003@;
     |SI FSalida = 0;
          |ABRE #vilam003@;
          |SELECT #2#vilam003@;
          #vilam003@#0 = #1#0;
          #vilam003@#1 = #1#1;
          #vilam003@#4 = #1#4;
          |LEE 000#vilam003@.=;
          |SI FSalida = 0; |Y #vilam003@#2 ! #1#2;
               aAlfa = "0000Cliente ya introducido en la linea: " + #vilam003@#2;
               |MENAV aAlfa;
               |ERROR;
          |FINSI;
          |CIERRA #vilam003@;
          |DESCARGA_DEF #vilam003@;
     |SINO;
          |MENAV "0000Error al cargar 'vilam003'";
     |FINSI;
|FPRC;

|PROCESO CheqParticipa; |TIPO 0;
     nNume = #0#25 + #1#3;
     |SI nNume > 100;
          aAlfa = "0000El porcentaje participación '" + nNume + "%' supera al 100%";
          |MENAV aAlfa;
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO Tipo11; |TIPO 11;
     |SI FSalida = 1; |O FSalida = 7;
          |SI #0#25 ! 100;
               aAlfa = "0000El porcentaje participación '" + #0#25 + "%' debe ser el 100%";
               |MENAV aAlfa;
               |ERROR;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO AltaCli; |TIPO 0;
     |HAZ Fac1Pago;
     |HAZ PreVtoCobrado;
     |SI nHay1Pago = "S"; |O nHayCobro = "S";
          |SI nHay1Pago = "S"; |MENAV "0000Existen facturas del 'Primer Pago'"; |FINSI;
          |SI nHayCobro = "S"; |MENAV "0000Existen prevencimientos cobrados"; |FINSI;
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO Tipo2Lin; |TIPO 2;
     |SI nModoCab = 3;
          |ACABA;
     |FINSI;

     nForma = 0 +. 1;
     |HAZ Fac1Pago;
     |HAZ PreVtoCobrado;
     |SI nHay1Pago = "S"; |O nHayCobro = "S";
          |SI nForma = -1;
               |SI nHay1Pago = "S"; |MENAV "0000Existen facturas del 'Primer Pago'"; |FINSI;
               |SI nHayCobro = "S"; |MENAV "0000Existen prevencimientos cobrados"; |FINSI;
          |FINSI;
          |ERROR; |ACABA;
     |FINSI;

     #0#16 = #0#16 +. 1;
     #0#25 = #0#25 +. #1#3;
|FPRC;

|PROCESO BorraPreVto0;
     |SI #4#11 ! 0; |Y #4#10 = 0;
          |BORRA 020#4a;
     |FINSI;
     |SI #4#11 ! 0; |Y #4#10 ! 0;
          #4#9 = #4#10;
          #4#11 = 0;
          |GRABA 020#4a;
     |FINSI;
     |LIBERA #4;
|FPRC;

|DEFBUCLE BorraPreVto0;
     #4#1;
     ;
     #0#0, #0#1, "      ", 01;
     #0#0, #0#1, "zzzzzz", 99;
     be;
     NULL, BorraPreVto0;
|FIN;

|PROCESO BorraPreVto1;
     |BORRA 020#4a;
|FPRC;

|DEFBUCLE BorraPreVto1;
     #4#1;
     ;
     #0#0, #0#1, "      ", 01;
     #0#0, #0#1, "zzzzzz", 99;
     be;
     NULL, BorraPreVto1;
|FIN;

|PROCESO Tipo2Cab; |TIPO 2;
     nModoCab = (FEntrada / 100) ! 0;
     nForma = 0 +. 1;

     |HAZ Fac1Pago;
     |HAZ PreVtoCobrado;
     |SI nModoCab = 3;
          |SI nHay1Pago = "S"; |O nHayCobro = "S";
               |SI nHay1Pago = "S"; |MENAV "0000Existen facturas del 'Primer Pago'"; |FINSI;
               |SI nHayCobro = "S"; |MENAV "0000Existen prevencimientos cobrados"; |FINSI;
               |ERROR; |ACABA;
          |FINSI;
          |BUCLE BorraPreVto1;
     |FINSI;
     |SI nModoCab = 2; |Y nForma = -1;
          |SI nHay1Pago = "N"; |Y nHayCobro = "N";
               |BUCLE BorraPreVto1;
               #0#23 = "N"; |PINTA #0#23;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Fac1Pago;
     |ABRE #5;
     #5#0 = #0#0;
     #5#1 = #0#1;
     #5#2 = "";
     #5#3 = 0;
     #5#4 = "";
     #5#5 = 0;
     |LEE 000#5];
     |SI FSalida = 0; |Y #5#0 = #0#0; |Y #5#1 = #0#1; |Y #5#3 = 0;
          nHay1Pago = "S";
     |SINO;
          nHay1Pago = "N";
     |FINSI;
     |CIERRA #5;
|FPRC;

|PROCESO CreaPrimePago; |TIPO 3;
     nHay = 0;
     |PARA i = 3; |SI i [ 8; |HACIENDO i = i + 1;
          aAlfa = #0i; |QBF aAlfa;
          |SI aAlfa ! ""; nHay = 1; |FINSI;
     |SIGUE;
     |SI nHay = 0;
          |MENAV "0000ERROR en precontrato, debe rellenar algún campo de 'Descripcion factura'";
          |ACABA;
     |FINSI;
     nHay = 0;
     |PARA i = 9; |SI i [ 14; |HACIENDO i = i + 1;
          aAlfa = #0i; |QBF aAlfa;
          |SI aAlfa ! ""; nHay = 1; |FINSI;
     |SIGUE;
     |SI nHay = 0;
          |MENAV "0000ERROR en precontrato, debe rellenar algun campo de 'Descripción en factura final'";
          |ACABA;
     |FINSI;

     |HAZ Fac1Pago;
     |SI nHay1Pago = "N"; |Y #0#18 = "S";
          |CONFI "0000N¿Generar facturas 1º pago?";
          |SI FSalida = 0; |HAZ Factu1Pago; |FINSI;
     |FINSI;

     |SI #0#23 = "N";
          |HAZ PreVto;
     |FINSI;

     |PINTA #0#16;
     |PINTA #0#23;
|FPRC;

|PROCESO Min5;
     #5#0 = #0#0;
     #5#1 = #0#1;
     #5#2 = "      ";
     #5#3 = 0;
     #5#4 = "     ";
     #5#5 = 000001;
|FPRC;

|PROCESO Max5;
     #5#0 = #0#0;
     #5#1 = #0#1;
     #5#2 = "zzzzzz";
     #5#3 = 99;
     #5#4 = "zzzzz";
     #5#5 = 999999;
|FPRC;

|PROCESO Opciones; |TIPO 20;
     |SI FSalida = "OPCION";
          FSalida = "Facturas";
          |ACABA;
     |FINSI;
     |SELECT #1#0;
     |LEE 101#0=;
     |SI FSalida ! 0; |ACABA; |FINSI;

     |SI #0#18 = "S";
          |HAZ Fac1Pago;
          |SI nHay1Pago = "N";
               |CONFI "0000N¿Generar facturas 1º pago?";
               |SI FSalida = 0;
                    |HAZ Factu1Pago;
               |FINSI;
          |FINSI;
          |ABRE #5;
          |CONSULTA_F_CLAVE #1#5, Min5, Max5;
          |CIERRA #5;
     |FINSI;
     |LIBERA #0;
|FPRC;

|PROCESO PreVtoCob;
     |SI #4#10 ! 0;
          nHayCobro = "S";
          |ERROR10;
     |FINSI;
|FPRC;

|DEFBUCLE PreVtoCob;
     #4#1;
     ;
     #0#0, #0#1, "      ", 01;
     #0#0, #0#1, "zzzzzz", 99;
     ;
     NULL, PreVtoCob;
|FIN;

|PROCESO PreVtoCobrado;
     nHayCobro = "N";
     |BUCLE PreVtoCob;
|FPRC;

|PROCESO Art0; |TIPO 0;
     |ABRE #100; |LEE 000#100p; |CIERRA #100;

     |ABRE #19;
     #19#0 = #0#27;
     |LEE 020#19=;
     |SI #19#125 ! #100#8;
          |MENAV "0000El articulo no es de 'Linea' promoción";
          |ERROR; |ACABA;
     |FINSI;
     |CIERRA #19;

     |DDEF aAlfa;
     aAlfa = aAlfa + "vilam002";
     |CARGA_DEF aAlfa, vilam002@;
     |SI FSalida = 0;
          |ABRE #vilam002@;
          |SELECT #3#vilam002@;
          #vilam002@#27 = #0#27;
          |LEE 000#vilam002@.];
          |PARA; |SI FSalida = 0; |HACIENDO;
               |SI #vilam002@#27 ! #0#27; |SAL; |FINSI;
               |SI #vilam002@#28 = "N";
                    |SI #vilam002@#0 ! #0#0; |O #vilam002@#1 ! #0#1;
                         aAlfa = "0000Artículo existente en precontrato:" + &13;
                         aAlfa = aAlfa + #vilam002@#0 + "/" + (("000000" + #vilam002@#1) % -106);
                         |MENAV aAlfa;
                         |ERROR;
                    |FINSI;
               |FINSI;
               |LEE 000#vilam002@.s;
          |SIGUE;
          |CIERRA #vilam002@;
          |DESCARGA_DEF #vilam002@;
     |FINSI;
|FPRC;

|PROCESO Comprueba;
     nOk = 0;
     #vilam002@#27 = #19#0;
     |LEE 000#vilam002@.=;
     |SI FSalida ! 0; |ACABA; |FINSI;

     nOk = 0;
     |LEE 000#vilam002@.];
     |PARA; |SI; |HACIENDO;
          |SI #vilam002@#27 ! #19#0; |O FSalida ! 0; |SAL; |FINSI;
          |SI #vilam002@#28 = "N";
               nOk = 1;
          |FINSI;
          |LEE 000#vilam002@.s;
     |SIGUE;
|FPRC;

|PROCESO agifa019_2;
     |HAZ Comprueba;
     |SI nOk = 0;
          |SALVA_DATOS #19;
          |REPON_DATOS #agifa019@;
          |GRABA 020#agifa019@.n;
          nHay = 1;
     |FINSI;
|FPRC;

|PROCESO agifa019_1;
     |HAZ Comprueba;
     |SI nOk = 0;
          |DDEFECTO #60;
          #60 = #19#2;
          |LEE 000#60=;

          |SALVA_DATOS #60;
          |REPON_DATOS #agifa060@;
          |GRABA 000#agifa060@.n;
          nHay = 1;
     |FINSI;
|FPRC;

|DEFBUCLE agifa019_1;
     #19#1;
     125;
     "                    ", #100#8;
     "zzzzzzzzzzzzzzzzzzzz", #100#8;
     ;
     NULL, agifa019_1;
|FIN;

|DEFBUCLE agifa019_2;
     #19#7;
     125;
     aFam, "                    ", #100#8;
     aFam, "zzzzzzzzzzzzzzzzzzzz", #100#8;
     ;
     NULL, agifa019_2;
|FIN;

|PROCESO GrabaEsteFam;
     #19#0 = #0#27;
     |LEE 000#19=;
     |SI FSalida = 0;
          |DDEFECTO #60;
          #60 = #19#2;
          |LEE 000#60=;

          |SALVA_DATOS #60;
          |REPON_DATOS #agifa060@;
          |GRABA 000#agifa060@.n;
          nHay = 1;
     |FINSI;
|FPRC;

|PROCESO GrabaEsteArt;
     #19#0 = #0#27;
     |LEE 000#19=;
     |SI FSalida = 0;
          |SALVA_DATOS #19;
          |REPON_DATOS #agifa019@;
          |GRABA 020#agifa019@.n;
          nHay = 1;
     |FINSI;
|FPRC;

|PROCESO Art66; |TIPO 66;
     nModo = (FEntrada / 100) ! 0;
     |ABRE #100; |LEE 000#100p; |CIERRA #100;
     |ABRE #19;
     #19#0 = #0#27;
     |LEE 000#19=;
     |CIERRA #19;
     aFam = #19#2;

     |DDEF aPath;
     aAlfa = aPath + "vilam002";
     |CARGA_DEF aAlfa, vilam002@;
     |SI FSalida ! 0; |ACABA; |FINSI;

     aAlfa = aPath + "agifa019";
     |CARGA_DEF aAlfa, agifa019@;
     |SI FSalida = 0;
          aAlfa = aPath + "agifa060"
          |CARGA_DEF aAlfa, agifa060@;
          |SI FSalida = 0;
               |HAZ CreaTempo; |NOME_DAT #agifa019@#Tempo#; aTmp019 = Tempo;
               |HAZ CreaTempo; |NOME_DAT #agifa060@#Tempo#; aTmp060 = Tempo;
               |DELINDEX #agifa019@; |DELINDEX #agifa060@;
               |ABRE #vilam002@; |SELECT #3#vilam002@;
               |ABRE #agifa019@;
               |ABRE #agifa060@;
               |ABRE #60;
               nHay = 0;
               |SI nModo = 2; |HAZ GrabaEsteFam; |FINSI;
               |BUCLE agifa019_1;
               |SI nHay = 0;
                    |MENAV "0000No hay artículos de promoción...";
                    |ERROR;
               |SINO;
                    #agifa060@#0 = aFam;
                    |LEE 000#agifa060@.];
                    |CONSULTA_CLAVE #1#agifa060@;
                    |SI FSalida = 0;
                         aFam = #agifa060@#0;
                         nHay = 0;
                         |SI nModo = 2; |HAZ GrabaEsteArt; |FINSI;
                         |BUCLE agifa019_2;
                         |SI nHay = 0;
                              |MENAV "0000No hay artículos de promoción...";
                         |SINO;
                              #agifa019@#0 = #19#0;
                              |LEE 000#agifa060@.];
                              |CONSULTA_CLAVE #1#agifa019@;
                              |SI FSalida = 0;
                                   #0#27 = #agifa019@#0; |PINTA #0#27;
                              |SINO;
                                   |ERROR;
                              |FINSI;
                         |FINSI;
                    |SINO;
                         |ERROR;
                    |FINSI;
               |FINSI;
               |CIERRA #60;
               |CIERRA #agifa060@;
               |CIERRA #agifa019@;
               |CIERRA #vilam002@;
               |DELINDEX #agifa019@; |DELINDEX #agifa060@;
               Tempo = aTmp019; |HAZ BoraTempo;
               Tempo = aTmp060; |HAZ BoraTempo;
               |DESCARGA_DEF #agifa060@;
          |FINSI;
          |DESCARGA_DEF #agifa019@;
     |FINSI;

     |DESCARGA_DEF #vilam002@;
|FPRC;

|PROCESO CheqSubroga; |TIPO 0;
     |SI #0#32 > #0#22;
          |MENAV "0000El importe no puede superar al pendiente de escritura...";
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO Anula; |TIPO 0;
     |SI #0Campo ! Contenido; |Y #0Campo = "S";
          |CONFI "0000NSe darán de baja lo prevencimientos con importe pendiente. ¿Continuar?";
          |SI FSalida = 0;
               |BUCLE BorraPreVto0;
          |SINO;
               |ERROR;
          |FINSI;
     |FINSI;
|FPRC;
