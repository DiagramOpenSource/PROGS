|FICHEROS;
     agifa081 #0;
     agifa112 #1;
     agifa019 #2;
     agifa067 #3;
     agifa017 #17;
     agifa024 #24;
     agifa007 #40;
     agifa142 #41;  || Parametros generales.
     agifa048 #98;  || Escandallos(C).
     agifa049 #99;  || Escandallos(L).
     agifa321 #45;  || Parametros de Divisas
     agifa322 #46;  || Lineas de Proveedores/Divisas
     agifa323 #47;  || Proveedores/Divisas
     agifa324 #42;  || Divisas
     agif0041 #11;  || datos empresa
     dsm00115 #115;
     dsw00040@ #239;||Tmp Promocion
     dsw00040 #240; ||Tmp Promocion
     dsm00241 #241; ||Medida
     dsm00244 #244; ||Promo C
     dsm00245 #245; ||Promo L
     dsm00087@;
     agifa073 #73;
     referen@;
|FIN;

|INCLUYE i_varart;

|VARIABLES;
     &enViseImpPdf = 0;
     nBtnVise = 0;
     nSwConstru = 0;
     &enModo81 = 0;
     &eaPCEAsigna = "";
     aSalida = "";
     nSwEtiPCEG = 0;
     nSwPCEGroup = 0;
     aSerie = "";
     nNumero = 0;
     aUsu = "";
     nContaLog = 0;
     aSer = "";
     aNum = "";
     aTipo = "";
     nTipo = 0;
     &enNumero = 0;
     &enEsCto = 0;
     nSwTecatel = 0;
     aFicSpoo = "";
     &eaDefImpre = "";
     &eaImpresora = "";
 {-1}Taglo = "";
     Taglo1 = "2400";
     Taglo2 = "1";
     Taglo3 = "";
     Taglo4 = "CIA";
     Taglo5 = " Correo/Impresion ";
     Taglo6 = " Solo Impresion ";
     Taglo7 = " Salir ";

     nSalida = 0;
     fFecEntraAnt    = @;
     nPendiente2    = 0;
     nPendiente     = 0;
     nNume7          = 0;
     nNume11         = 0;
     &enProg213      = 0;
     &Tempo          = "";
     aTempo240       = "";
     nSal            = 0;
     aAlfa           = "";
     nTecla          = 0;
     aDatos          = "";
     aDirEmp         = "";
     nCodEmp         = 0;

     nSwVersion     = 0;
     nImpo          = 0;
     nForma         = 0;
     nModo          = 0;
     &enSw          = 0;
     &ser_ped       = "";
     &num_ped       = 0;

     &nDeci_Base     = 0;   || Decimales en Totales de la moneda base
     &nDeci_PreBase  = 0;   || Decimales en el Precio en moneda base
     &nDeci_BaseMxP  = 0;   || Decimales en Totales de la moneda base (Maxima precision)
     &nDeci_PreBasMxP = 0;   || Decimales en el Precio en moneda base (Maxima precision)
     &nDeci_DivP      = 0;   || Decimales en Totales de la divisa
     &nDeci_PreDivP   = 0;  || Decimales en el Precio en divisa
     &nDeci_PreDiv    = 0;  || Decimales en el Precio en divisa
     &nDeci_TotVisP   = 0;  || Decimales en Totales visibles (el de la moneda de trabajo)
     &nDeci_TotNoVisP = 0;  || Decimales en Totales no visibles (el de la otra moneda)
     &nDeci_PreVisP   = 0;  || Decimales en el Precio visual. (lineas)
     &nDeci_ImpVisP   = 0;  || Decimales en el Importe visual. (lineas)
     nfecha    = 0;
     &ac_divisa = "N";   || Vale "S" cuando las divisas en compras estan
                         || activadas y la serie es de divisas
     &pintado = 0;       || Sirve para controlar los pintados de las lineas
     &decim_divisa = 0;  || Decimales de la divisa
     &precio_divisa = 0; || Decimales del precio en divisa
     &decim_base = 0;    || Decimales de la moneda base
     &precio_base = 0;   || Decimales del precio en moneda base
     div_act = 0;        || Divisa actualizada. Me sirve en un PARA para que
                         || no salga hasta que este actualizada (div_act=1)
     &prov_divisa = "";  || Codigo de Proveedor. Se pasa a PROVEED/DIVISAS
     &divisa = "";       || Codigo de Divisa. Se pasa a ACTUAL. de DIVISAS
     nModo081 = 0;
     contador   = 0;
     cont       = "";
     modo       = 0;
     alfa       = "";
     alf        = "";
     fmes1      = "  ";
     mes        = 0;
     control    = 0;
     mensaje10  = "0000El Pedido ya esta Recibido en parte o totalmente";
     mensaje11  = "0423Actualizando linea:";
     informe    = "";
     informe1   = "agifa081";
     &tolin     = 0;
     &cant      = 0;
     &iva1      = 0;
     &iva2      = 0;
     &iva3      = 0;
     &iva4      = 0;
     &iva5      = 0;
     &re1       = 0;
     &re2       = 0;
     &re3       = 0;
     &re4       = 0;
     &re5       = 0;
     &n_dec     = 0;
     &n_pre     = 0;
     &bl        = "                              ";
     &def_alm   = 0;
     puente     = "";
     &recoge    = "";     || Lo utilizo en agifa067
     nEntrada   = 0;
     &codiBarra = "";

     &eaCodigo     = "";
     &eaTipoCodigo = "PR";

 {-1}menu      = "";
     menu1     = "2400";
     menu2     = "1";
     menu3     = "Contador:  ";
     menu4     = "AM";
     menu5     = " Automatico ";
     menu6     = " Manual ";
     caltot    = 0;
     nSwPromo  = 0;
     nTotUni   = 0;
     nUni      = 0;
     nResto    = 0;
     nRegalo   = 0;
     nPrecio   = 0;
     nCamPre   = 0;
     nSwPinta  = 0;
     nDec      = 0;
     nFalta    = 0;
     nPromo    = 0;
     aDef      = "";
     nPPort1   = 0;
     nPPort2   = 0;
     nImpDiv   = 0;
     nPend     = 0;
 {-1}aMenuPCEG = "";
     aMenuPCEG1 = "2400";
     aMenuPCEG2 = "1";
     aMenuPCEG3 = "Imprimir:";
     aMenuPCEG4 = "PE";
     aMenuPCEG5 = " Pedido ";
     aMenuPCEG6 = " Etiqueta ";
 {-1}aMenuConstru = "";
     aMenuConstru1 = "2400";
     aMenuConstru2 = "1";
     aMenuConstru3 = "Opciones:";
     aMenuConstru4 = "IC";
     aMenuConstru5 = " Imprimir ";
     aMenuConstru6 = " Copia a Empresa ";
     nSwOk5050 = 0;
 {-1}aMenuOk5050 = "";
     aMenuOk50501 = "2400";
     aMenuOk50502 = "1";
     aMenuOk50503 = "Opciones:";
     aMenuOk50504 = "IE";
     aMenuOk50505 = " Imprimir ";
     aMenuOk50506 = " Enviar ";
|FIN;
컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
|PROCESO Entra081; |TIPO 7;
     |HAZ ControlUsuPC;
     |SI enErr ! 0; |PTEC 807; |ACABA; |FINSI;
     |HAZ DefSerCom;

     |SI nBtnVise ! 0;
          |ESTADO_CONTROL nBtnVise, "DISABLE";
     |FINSI;
|FPRC;

|CALCULO cogepc;|TIPO 0;
 control = 0;
|FCAL;

|CALCULO conpc;|TIPO 6;
     nModo = (FEntrada / 100) ! 0;

     |SI nModo = 2; |Y #0Campo ! Contenido;
          |SI #0#24 > 0;
               #0Campo = Contenido;
               |PINTA #0Campo;
               |MENAV mensaje10;
               |ERROR;
          |SINO;
               control = 1;
          |FINSI;
     |FINSI;
|FCAL;

|PROCESO Pago81_0; |TIPO 0;
     nModo = (FEntrada / 100) ! 0;
     |HAZ EsAlbero;
     |SI FSalida = 0; |Y nModo = 1;
          |HAZ AlbePedFec;
          |HAZ AlbePedPinta;
     |FINSI;
|FPRC;

|PROCESO mira_lin;
     #73#28 = #3#30;
     #73#0 = #3#29;
     #73#1 = #3#31;
     |LEE 000#73=;
     |SI FSalida = 0;
          aSerie = #3#30;
          nNumero = #3#29;
          |ERROR10;
     |FINSI;
|FPRC;

|DEFBUCLE mira_lin;
     #3#1;
     ;
     #0#25,#0#0,001;
     #0#25,#0#0,999;
     ;
     NULL, mira_lin;
|FIN;

|PROCESO ActProv;
     #2#0 = #3#2;
     |LEE 020#2=;
     |SI FSalida ! 0; |DDEFECTO #2; |FINSI;

     fmes1 = #0#1 % A402;
     mes = fmes1 + 42;
     |SI #2#196 = 2;
          nImpo = (((((#3#41 * #3#6) < #3#8) < #3#27) < #3#57) < #0#5) < #0#17;
     |SINO;
          nImpo = (((((#3#5 * #3#6) < #3#8) < #3#27) < #3#57) < #0#5) < #0#17;
     |FINSI;
     |SI #41#114 = "S";
          nImpo = nImpo < #0#6;
     |FINSI;
     #1mes = #1mes + (nImpo * nForma);
     #1#55 = #1#55 + (nImpo * nForma);

     enImpPrvPed = nImpo * nForma;
     efFecha = #0#1;
     eaProve = #0#4;
     |HAZ AcumulaPrv;
|FPRC;

|CALCULO pcprove;|TIPO 2;
     |HAZ ControlUsuPC;
     |SI enErr ! 0; |ERROR; |ACABA; |FINSI;

     |ABRE #41; |LEE 000#41p; |CIERRA #41;

     nModo = (FEntrada / 100) ! 0;
     enModo81 = nModo;
     nForma = 0 +. 1;

     |SI #0#28 = 0; || chequea el cambio
          |ABRE #agifa324;
          #agifa324#0 = #0#27;
          |LEE 020#agifa324.=;
          |SI FSalida = 0;
               #0#28 = #agifa324#3;
          |FINSI;
          |CIERRA #agifa324;
     |FINSI;

     |SI nModo = 2;
          |SI nForma = 1; |O #0#24 = 0;
               |C_M #0#1, 1, "S";
               |C_M #0#4, 1, "S";
               |C_M #0#9, 1, "S";
               |C_M #0#29, 1, "S";
               |C_M #0#27, 1, "S";
               |C_M #0#28, 1, "S";
               |C_M #0#5, 1, "S";
               |C_M #0#17, 1, "S";
               |C_M #0#6, 1, "S";
          |SINO;
               |C_M #0#1, 1, "N";
               |C_M #0#4, 1, "N";
               |C_M #0#9, 1, "N";
               |C_M #0#29, 1, "N";
               |C_M #0#27, 1, "N";
               |C_M #0#28, 1, "N";
               |C_M #0#5, 1, "N";
               |C_M #0#17, 1, "N";
               |C_M #0#6, 1, "N";
          |FINSI;
     |FINSI;

     |SI nModo = 3;
          aSerie = ""; nNumero = 0;
          |ABRE #73;
          |BUCLE mira_lin;
          |CIERRA #73;
          |SI nNumero ! 0;
               aAlfa = "0000Pedido procedente del pedido de ventas '" + aSerie + "/" + (("000000" + nNumero) % -106) + "'";
               |MENAV aAlfa;
               |ERROR; |ACABA;
          |FINSI;

          |SI #0#24 > 0;
               |MENAV mensaje10;
               |ERROR; |ACABA;
          |FINSI;
     |FINSI;

     |DDEFECTO #1;
     |ABRE #1;
     |ABRE #2;
     #1#0 = #0#4;
     |LEE 101#1=;
     |SI FSalida ! 0; |GRABA 020#1b; |FINSI;
     |BUCLELIN 2 ActProv #0;
     |GRABA 020#1a;
     |CIERRA #1;
     |CIERRA #2;

     |SI control = 1;
          control = 0;
          |HAZ actpc;
     |FINSI;

     |SI nModo = 2; |Y nForma = -1;
          |HAZ RestauraPromo;
     |FINSI;

     ||컴컴컴컴컴컴Automatizacion Empresas
     |ABRE #dsm00115; |LEE 000#dsm00115.p; nSal = FSalida; |CIERRA #dsm00115;
     |SI nSal = 0;
          eaProve = #0#4;
          |SI nForma = 1;
               aAlfa = "dsz99982;PEDALTA" + #0#25 + (("000000" + #0#0) % -106);
               |SUB_EJECUTA aAlfa;
          |SINO;
               aAlfa = "dsz99982;PEDBAJA" + #0#25 + (("000000" + #0#0) % -106);
               |SUB_EJECUTA aAlfa;
          |FINSI;
     |FINSI;

     |SI nModo = 3;
          |HAZ EsAlbero;
          |SI FSalida = 0;
               |HAZ AlbePedBorra;
          |FINSI;
          |HAZ EsTagloma;
          |SI FSalida = 0;
               |HAZ TagPedBorra;
          |FINSI;
          |HAZ EsPCEGroup;
          |SI FSalida = 0;
               eaSerie = #0#25;
               enDocu = #0#0;
               eaTipo = "PC"
               eaPregunta = "N";
               |SUB_EJECUTA_NP "pcegz019;BORRA";
          |FINSI;
          |HAZ EsDesanch;
          |SI FSalida = 0;
               aAlfa = "desan016;PB" + #0#25 + (("000000" + #0#0) % -106);
               |SUB_EJECUTA aAlfa;
          |FINSI;
     |FINSI;

     |HAZ EsOk5050;   || Para que entre en el tipo16/17 del arti/alma
     |SI FSalida = 0; || tambien esta en el tipo2 de la linea
          |ABRE #17;
          |BUCLELIN 2 LPedido #0;
          |CIERRA #17;
          |SI nModo = 3;
               eaSerie = #0#25;
               enCodigo = #0#0;
               |SUB_EJECUTA_NP "okz00002;C";
          |FINSI;
     |FINSI;
|FCAL;

|PROCESO LPedido;
     #17#0 = #3#2;
     #17#1 = #3#3;
     |LEE 020#17=;
     |GRABA 020#17a;
|FPRC;

|CALCULO actlinpc;|TIPO 0;
     #3#20 = #0#4;
     #3#23 = #0#5;
     #3#24 = #0#6;
     #3#25 = #0#17;
     #3#26 = #0#9;
     |SI caltot = 0;
          |LEE 101#3=;
          |PINTA 445,#3#1;
     |FINSI;
     |HAZ TotalLin67;

     #0#11 = #0#11 + #3#9;
     #0#12 = #0#12 + #3#11;
     #0#13 = #0#11 - #0#12;

     |SI #0#29 = "D";                    || Si la moneda de trabajo es la divisa ...
          nPPort2 = #3#50;
     |SINO;
          nPPort2 = #3#50 / #0#33 * #0#28;
     |FINSI;

     |SI #2#196 = 2;
         nPend = (#3#41 - #3#38) * #3#33;
     |SINO;
         nPend = (#3#5 - #3#38) * #3#33;
     |FINSI;
     nPend = nPend ! nDeci_BaseMxP;

     nPend = ((nPend < #3#8) < #3#27) < #3#57;
     nPend = nPend ! nDeci_BaseMxP;

     |SI #2#196 = 2;
         nPend = nPend + ((#3#41 - #3#38) * nPPort2)
     |SINO;
         nPend = nPend + ((#3#5  - #3#38) * nPPort2);
     |FINSI;
     nPend = nPend ! nDeci_BaseMxP;
     nPend = (nPend < #0#5) ! nDeci_BaseMxP;
     nPend = (nPend < #0#17) ! nDeci_BaseMxP;
     nPend = (nPend < #0#6) ! nDeci_BaseMxP;

     #0#34 = #0#34 + #3#34;
     #0#35 = #0#35 + nPend;
     #0#36 = #0#34 - #0#35;

     |GRABA 020#3a;
     |LIBERA #3;
|FCAL;

|CALCULO actpc;|TIPO 0;
 |MENSA mensaje11;
 #agifa081#11 = 0;
 #agifa081#12 = 0;
 #agifa081#34 = 0;
 #agifa081#35 = 0;
 |BUCLELIN 2actlinpc#0;
 |BLIN 4;
|FCAL;

|CALCULO siprnpc;|TIPO 3;
     |HAZ CalCabAgifa081;
     ||컴컴컴컴컴컴컴컴컴컴Modulo 000323
     |HAZ EsTecatel;
     |SI FSalida = 0;
          eaSer = #0#25;
          enDocu = #0#0;
          eaCodigo = #0#4;
          eaTipo = "PI";
          |HAZ TecaTrans;
     |FINSI;
     ||컴컴컴컴컴컴컴컴컴컴컴
     |HAZ EsAlbero;
     |SI FSalida = 0;
          |HAZ AlbeFinPedi;
     |FINSI;
     ||컴컴컴컴컴컴컴컴컴컴컴
     |SI enProg213 = 0;
          |HAZ Promociones;
          |CONFI "2400NDesea imprimir este pedido? ";
          |SI FSalida = 0;
               |HAZ EsTagloma;
               |SI FSalida = 0;
                    |HAZ TagloImpre;
               |SINO;
                    |HAZ prnpc;    || Proceso Estandar
               |FINSI;
          |FINSI;

          |HAZ EsPCEGroup;
          |SI FSalida = 0;
               |HAZ EtiquetaPCEG;
          |FINSI;
     |FINSI;
|FCAL;

|CALCULO prnpc;
     modo = (FEntrada / 100) ! 0;
     |SI modo = 1; |O modo = 2;
          |GRABA 021#0a;
          |LIBERA #0;
     |FINSI;
     ser_ped = #0#25;
     num_ped = #0#0;
     |CIERRAT #0;

     |SUB_EJECUTA_NP "agifa100";

     |ABRET #0;
     #0#25 = ser_ped;
     #0#0 = num_ped;
     |SI modo = 4;
          |LEE 001#0=;
     |SINO;
          |LEE 101#0=;
     |FINSI;
|FCAL;

|| Carga sobre el campo referencia por defecto la serie + el numero
|PROCESO PonRef;
   nEntrada = (FEntrada $ 100);
   |SI nEntrada = 1;   || Solo en alta
       #0#26 = #0#25 + "/"+ (("000000" + #0#0) % -106);
       |PINTA #0#26;
   |FINSI;
|FPRC;

|PROCESO mira_de;
     |ABRE #41; |LEE 000#41p; |CIERRA #41;

     n_dec     = #41#8;
     n_pre     = #41#9;
     recoge    = #41#39;   || Recoger pedidos <F4>
     codiBarra = #41#57;

     |ABRE #40;
     #40#26 = #0#25;
     |LEE 001#40=;
     |SI FSalida ! 0; |DDEFECTO #40; |FINSI;
     |CIERRA #40;
     def_alm = #40#31;
     |HAZ LeeMonBase;
     |HAZ LeeParamDiv;

     |HAZ PonRef;
|FPRC;

|PROCESO prov_obs; |TIPO 0;
     eaProve = #0#4;

     |HAZ EsIber21;
     |SI FSalida = 0;
          |HAZ ProvIber21;
          |SI eaProve = ""; |ERROR; |ACABA; |FINSI;
     |FINSI;

     nModo081 = (FEntrada / 100) ! 0;
     |SI nTecla = 11; |O #41#102 = "S"; |O #41#102 = "X";
          eaCodigo = #0#4;
          eaFuerza = "N";
          |SI nTecla = 11; eaFuerza = "S"; |FINSI;
          |SUB_EJECUTA_NP "dsz99994";
          |SI #41#102 = "N"; |Y nModo081 ! 2; |ERROR;  |FINSI;
     |FINSI;

     |SI #41#15 = "S";
          puente = #1#26;     |QBF puente;
          |SI puente ! "";
               |PUSHV 1008 1370;
               |SI #41#20 = "S"; |AVISO; |FINSI;
               |BLANCO 1008 1370;
               |CUADRO 1008 1370;
               |ATRI R;
               |PINTA 1110 , #1#26;
               |ATRI 0;
               |PAUSA;
               |POPV;
          |FINSI;
     |FINSI;
|FPRC;

__________________________/ BUSCA UN CODIGO DE PROVEEDOR DE FORMA AUTOMATICA.
|PROCESO altap; |TIPO 6;
     nTecla = FSalida;

     modo = (FEntrada / 100) ! 0;
     alfa = #0#4;
     |QBF alfa;
     |SI modo = 1; |Y alfa ! "";
          |ABRE #1;
          #1#0 = #0#4;
          |LEE 000#1=;
          |SI FSalida ! 0;
               |ABRE #41;
               |LEE 000#41p;
               |SI #41#3 = "S";
                    |HAZ Menus;
               |FINSI;
               |CIERRA #41;
          |FINSI;
          |CIERRA #1;
     |FINSI;
|FPRC;

|PROCESO Menus;
     |MENU menu;
     |SI FSalida = 1;    ||Automatico
          |ABRE #42;
          #42#0 = "proveedo";
          |LEE 101#42=;
          |SI FSalida = 0;
               #42#1 = #42#1 + 1;
               contador = #42#1;
               |GRABA 021#42a;
          |SINO;
               contador = 1;
               #42#1 = 1;
               |GRABA 021#42n;
          |FINSI;
          cont = contador;
          cont = ("000000" + cont) % -104;
          #0Campo = cont;
          |PINTA #0Campo;
          |LIBERA #42;
          |CIERRA #42;
     |FINSI;
|FPRC;

|PROCESO LeeMonBase; ||Lee los parametros de la moneda base
|ABRE #agifa321;
|ABRE #agifa324;
|LEE 010#agifa321.p;   ||Leo parametros de divisa
#agifa081#32 = #agifa321#9;  ||Cojo la Moneda Base de parametros
#agifa324#0 = #agifa321#9;
|LEE 010#agifa324.=;         ||Leo la ficha de la moneda base en Divisas
#agifa081#33 = #agifa324#3;  ||Asigno el cambio de la moneda base con el euro
decim_base = #agifa324#7;    ||Asigno los decimales de la moneda base
precio_base = #agifa324#9;   ||Asigno los decimales del precio en la mon. base
|CIERRA #agifa324;
|CIERRA #agifa321;
|FPRC;

|PROCESO Param_Divisas; |TIPO 0;
     pintado = 0;  ||Controla pintados de las lineas

     |ABRE #agifa324;
     #agifa324#0 = #agifa081#27;
     |LEE 001#agifa324.=;   || Leo la divisa
     |CIERRA #agifa324;

     decim_divisa = #agifa324#7;  || Decimales de la divisa
     precio_divisa = #agifa324#9;  || Decimales en el precio de la divisa
     nDeci_DivP = decim_divisa;   || Decimales de la divisa
     nDeci_PreDivP = precio_divisa;  || Decimales en el precio de la divisa
     nDeci_PreDiv  = precio_divisa;  || Decimales en el precio de la divisa
     |SI #0#29 = "B";   ||Si trabajo con la moneda base ...
          nDeci_PreVisP  = precio_base;   || precio_divisa;
          nDeci_ImpVisP  = precio_base;   ||decim_divisa;
          nDeci_Base     = decim_base;
          nDeci_PreBase  = precio_base;
          nDeci_BaseMxP  = decim_base;
          nDeci_PreBasMxP= precio_base;
          nDeci_TotVisP  = decim_base;
          nDeci_TotNoVisP = decim_divisa;
     |SINO;     ||Si trabajo con la divisa ...
          nDeci_PreVisP  = precio_divisa; || precio_base;
          nDeci_ImpVisP  = precio_divisa; ||decim_base;
          nDeci_Base     = decim_base;    || Max. precision en los decimales de la moneda base
          nDeci_PreBase  = precio_base;   || Max. precision en los dec. del precio de la moneda base
         |SI #0#28 = 1;
              nDeci_BaseMxP   = decim_base;    || sin triangulacion
              nDeci_PreBasMxP = precio_base;
         |SINO;
              nDeci_BaseMxP   = #agifa321#10;  || con triangulacion
              nDeci_PreBasMxP = #agifa321#11;
         |FINSI;
          nDeci_TotVisP   = decim_divisa;
          nDeci_TotNoVisP = decim_base;
     |FINSI;
     |HAZ Deci_IVAC;
|FPRC;

|PROCESO LeeParamDiv; |TIPO 0;
     |ABRE #agifa321;
     |LEE 001#agifa321.p;    || Leo parametros de divisas
     |ABRE #agifa007;
     #agifa007#26 = #agifa081#25;
     |LEE 001#agifa007.=;        || Leo la serie
     |SI #0#29 = "D";      || Si la mon. de trabajo es la divisa...
       || ...escondo los campos de la moneda base :
       |CAMPO_POSICION #3#6 , 1 , 1009;
       |CAMPO_POSICION #3#9 , 1 ,1026;
       |CAMPO_VISUAL #3#6 , 1 , "N";
       |CAMPO_VISUAL #3#9 , 1 , "N";
       |CAMPO_LINEAL #3#6 , 1 , "N";
       |CAMPO_LINEAL #3#9 , 1 , "N";
       |CAMPO_MODIFICA #3#6 , 1 , "N";
       || ...y pongo en pantalla los de divisa
       |CAMPO_POSICION #3#33 , 1 , 1545;
       |CAMPO_POSICION #3#34 , 1 ,1563;
       |CAMPO_VISUAL #3#33 , 1 , "S";
       |CAMPO_VISUAL #3#34 , 1 , "S";
       |CAMPO_MODIFICA #3#33 , 1 , "S";
       |CAMPO_LINEAL #3#33 , 1 , "S";
       |CAMPO_LINEAL #3#34 , 1 , "S";
       |SI #agifa007#39 = "S"; |Y #agifa321#2 = "S"; ||Si la serie es de ...
          ac_divisa = "S";  || ... divisas, y en parametros estan activadas ...
          || ... pongo como modificables los campos relacionados con divisas
          |CAMPO_MODIFICA #0#27 , 1 , "S";
          |CAMPO_MODIFICA #0#28 , 1 , "S";
          |CAMPO_MODIFICA #0#29 , 1 , "S";
          |CAMPO_MODIFICA #0#30 , 1 , "S";
          |CAMPO_MODIFICA #0#31 , 1 , "S";
          |SI #agifa321#4 = "S";  || Si la visualizacion de parametros esta activada
            || ... pongo los campos visual. de lineas como visualizables
            |CAMPO_VISUAL #3#35, 1 , "S";
            |CAMPO_VISUAL #3#36 , 1 , "S";
          |SINO;
            || ... los pongo como no visualizables
            |CAMPO_VISUAL #3#35 , 1 , "N";
            |CAMPO_VISUAL #3#36 , 1 , "N";
          |FINSI;
       |SINO; || Si la serie no es de divisas, o no estan activados parametros
          ac_divisa = "N";
          #0#29 = "B";  || La moneda de trabajo es por fuerza la moneda base
          |PINTA #0#29;
          || Pongo como no modificables los campos relacionados con divisas
          |CAMPO_MODIFICA #0#29 , 1 , "N";
          |CAMPO_MODIFICA #0#30 , 1 , "N";
          |CAMPO_MODIFICA #0#31 , 1 , "N";
          |CAMPO_MODIFICA #0#27 , 1 , "N";
          |CAMPO_MODIFICA #0#28 , 1 , "N";
          || Pongo como no visualizables los campos visual. de lineas
          |CAMPO_VISUAL #3#35, 1 , "N";
          |CAMPO_VISUAL #3#36, 1 , "N";
       |FINSI;
     |SINO;  || Si la moneda de trabajo es la moneda base
       || ...escondo los campos en divisa :
       |CAMPO_POSICION #3#33 , 1 , 1009;
       |CAMPO_POSICION #3#34 , 1 ,1026;
       |CAMPO_VISUAL #3#33 , 1 , "N";
       |CAMPO_VISUAL #3#34 , 1 , "N";
       |CAMPO_MODIFICA #3#33 , 1 , "N";
       |CAMPO_MODIFICA #3#34 , 1 , "N";
       |CAMPO_LINEAL #3#33 , 1 , "N";

       || ...coloco los campos en moneda base:
       |CAMPO_POSICION #3#6 , 1 , 1545;
       |CAMPO_POSICION #3#9 , 1 ,1563;
       |CAMPO_VISUAL #3#6 , 1 , "S";
       |CAMPO_VISUAL #3#9 , 1 , "S";
       |CAMPO_MODIFICA #3#6 , 1 , "S";
       |CAMPO_LINEAL #3#6 , 1 , "S";
       |CAMPO_LINEAL #3#9 , 1 , "S";
       |SI #agifa007#39 = "S"; |Y #agifa321#2 = "S"; || Si la serie es de divisas...
          ac_divisa = "S"; || ... y divisas estan activadas en parametros
          || Pongo como modificables los campos relacionados con divisas
          |CAMPO_MODIFICA #0#27 , 1 , "S";
          |CAMPO_MODIFICA #0#28 , 1 , "S";
          |CAMPO_MODIFICA #0#29 , 1 , "S";
          |CAMPO_MODIFICA #0#30 , 1 , "S";
          |CAMPO_MODIFICA #0#31 , 1 , "S";
          |SI #agifa321#4 = "S";  || Si la visualizacion esta activada en parametros ...
            || Pongo como visualizables los campos visual. de las lineas
            |CAMPO_VISUAL #3#35, 1 , "S";
            |CAMPO_VISUAL #3#36 , 1 , "S";
          |SINO; || Los pongo como no visualizables
            |CAMPO_VISUAL #3#35 , 1 , "N";
            |CAMPO_VISUAL #3#36 , 1 , "N";
          |FINSI;
       |SINO;  || Si la serie no es de divisas o no estan activados los parametros
          ac_divisa = "N";
          #0#29 = "B";  || La moneda de trabajo es por fuerza la moneda base
          || Pongo como no modificables los campos relacionados con divisas
          |CAMPO_MODIFICA #0#29 , 1 , "N";
          |CAMPO_MODIFICA #0#30 , 1 , "N";
          |CAMPO_MODIFICA #0#31 , 1 , "N";
          |CAMPO_MODIFICA #0#27 , 1 , "N";
          |CAMPO_MODIFICA #0#28 , 1 , "N";
          || Pongo como no visualizables los campos visual. de lineas
          |CAMPO_VISUAL #3#35, 1 , "N";
          |CAMPO_VISUAL #3#36, 1 , "N";
       |FINSI;
     |FINSI;
     |CIERRA #agifa007;
|CIERRA #agifa321;
|HAZ Param_Divisas;
|FPRC;

|PROCESO LinPedido;
     #3#13 = #0#2;
     |GRABA 020#3a;
     |LIBERA #3;
|FPRC;

|PROCESO Proveedor0; |TIPO 0;
     nModo = (FEntrada / 100) ! 0;
/*
     |SI #0Campo ! Contenido; |Y nModo ! 1;
          |CONFI "0000NModificar F.Entrega de lineas";
          |SI FSalida = 0;
               |BUCLELIN 0LinPedido#0;
          |FINSI;
     |FINSI;
*/
     |HAZ CambioDivisa;
     |HAZ prov_obs;
     |HAZ mira_de;
     |ABRE #1;
     #1#0 = #0#4;
     |LEE 000#1=;
     |CIERRA #1;
     ||컴컴컴컴컴컴컴컴컴컴
     |SI nModo = 1;
          #0#2 = #0#1 + #1#91; |PINTA #0#2;
     |FINSI;
|FPRC;

|PROCESO CambioDivisa; |TIPO 0;
     nModo081 = (FEntrada / 100) ! 0;
     |SI #0Campo ! Contenido; |O nModo081 = 1;   ||Si cambia el valor o es alta
          |SI Campo = 4; || Si llamo al proceso desde el cod. de proveedor ...
               |LEE 001#agifa112.=;
               #0#27 = #agifa112#81;  || Asigno la divisa por defecto del proveedor
               #0#29 = #agifa112#82; || Asigno la moneda de trabajo del proveedor
               |PINTA #0#29;
          |FINSI;
          #agifa007#26 = #agifa081#25;
          |LEE 001#agifa007.=;  || Leo la serie
          |SI #agifa007#39 = "S"; |Y #agifa321#2 = "S";
               |SI #agifa112#80 = "S"; || Si el proveedor es de divisa pactada
                    |ABRE #agifa323;
                    |ABRE #agifa322;
                    #agifa322#0 = #agifa081#4;
                    #agifa322#2 = #agifa081#27;
                    #agifa322#7 = "N";
                    |SELECT #2#agifa322;  || Con esta clave ...
                    |LEE 011#agifa322.=;  || ... busco periodos no finalizados de esa divisa
                    |SI FSalida = 0;  || Si existe un periodo no finalizado
                         |PARA div_act = 0; |SI div_act = 0; |HACIENDO;
                              #agifa081#27 = #agifa322#2;        || Cargo la divisa ...
                              #agifa081#28 = #agifa322#3;        || ...y el cambio
                              |LEE 001#agifa322.=;   || Leo las lineas de Proveed/Divisas
                              nfecha = #agifa081#1 - #agifa322#6;  || Resto la fecha de fin de periodo a la actual
                              |SI nfecha > 0;  || Si la fecha actual supera a la del periodo asignado ...
                                   |MENAV "0000ATENCION deberia Actualizar primero la divisa en Proveedores/Divisas";
                                   prov_divisa = #agifa081#4;  || Le envio el cod. de proveedor a "agifa323"
                                   |SUB_EJECUTA_NP "agifa323.tab";  || Ejecuto Proveedores/Divisas
                                   |ERROR;
                              |SINO;
                                   div_act = 1;  || Para salir del PARA
                              |FINSI;
                         |SIGUE;
                    |SINO;  || Si no existe un periodo no finalizado para esa divisa
                         |MENAV "0000No existe un periodo activo para la divisa en Proveedores / Divisas";
                         prov_divisa = #agifa081#4;  || Le paso a "agifa323" el proveedor
                         |SUB_EJECUTA_NP "agifa323.tab";  || Ejecuto Proveedores / Divisas
                         |ERROR;
                         ||#agifa081#27 = #agifa081#32;
                         ||#agifa081#28 = #agifa081#33;
                    |FINSI;
                    |SELECT #1#agifa322;
                    |CIERRA #agifa322;
                    |CIERRA #agifa323;
               |SINO; || Si el Proveedor no es de Divisa pactada ...
                    |ABRE #agifa324; || ...miro directamente en divisas
                    #agifa324#0 = #agifa081#27;
                    |LEE 001#agifa324.=;
                    |SI FSalida = 0; ||Si existe la divisa...
                         #agifa081#28 = #agifa324#3; || la recojo
                         |SI #agifa324#6 ! -1; || Si los dias de margen de actualizacion no
                                       || son = -1 (indefinidos)
                              |PARA div_act = 0; |SI div_act = 0; |HACIENDO;
                                   #agifa081#27 = #agifa324#0;        ||Cargo el valor de la divisa
                                   #agifa081#28 = #agifa324#3;        ||Cargo el valor del cambio
                                   |LEE 001#agifa324.=;  || Leo la divisa
                                   nfecha = #agifa081#1 - #agifa324#4;
                                   |SI nfecha > #agifa324#6; |Y #agifa324#6 ! -1;  || Si la fecha actual
                                        |MENAV "0000ATENCION deberia Actualizar primero la divisa";
                                        divisa = #agifa081#27;
                                        |SUB_EJECUTA_NP "agifa326.tab";
                                        |ERROR;
                                   |SINO;
                                        div_act = 1;
                                   |FINSI;
                              |SIGUE;
                         |FINSI;
                    |FINSI;
                    |CIERRA #agifa324;
               |FINSI;
          |SINO;
               #agifa081#27 = #agifa081#32;
               #agifa081#28 = #agifa081#33;
          |FINSI;
          |PINTA #agifa081#27;
          |PINTA #agifa081#28;
          |SI Campo = 4; || Si llamo al proceso desde el cod. de proveedor ...
               |HAZ LeeParamDiv;
          |SINO;
               |HAZ Param_Divisas;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO LeePed; |TIPO 13;  || Sirve para cargar los valores cada vez que
                            || cambio de ficha
     pintado = 0;   || controla pintado de las lineas
     |HAZ LeeMonBase;
     |HAZ LeeParamDiv;

     |ABRE #41; |LEE 000#41p; |CIERRA #41;
     |SI #41#148 = "N";
          |C_V #3#50, 1, "N";
     |SINO;
          |C_V #3#50, 1, "S";
     |FINSI;
     |HAZ EsAlbero;
     |SI FSalida = 0;
          |HAZ AlbePedPinta;
     |FINSI;
     |HAZ EsTagloma;
     |SI FSalida = 0;
          |HAZ TagPedPinta;
     |FINSI;
     |HAZ EsCercama;
     |SI FSalida = 0;
          |HAZ CercamaPinta;
     |FINSI;
     |HAZ EsDesanch;
     |SI FSalida = 0;
          |HAZ PinDesanch81;
     |FINSI;
|FPRC;

|CALCULO cambfecha;
|SI #0#30 ! "O";
  #0#31 = "        ";
  |PINTA #0#31;
  |C_M #0#31,1,"N";
  |C_V #0#31,1,"N";
|SINO;
  |C_M #0#31,1,"S";
  |C_V #0#31,1,"S";
|FINSI;
|FCAL;

|CALCULO modomod; |TIPO 6;
   |SI nModo081 = 2; |Y #0Campo ! Contenido;
       |MENAV "0000No se puede modificar el campo.";
       #0Campo = Contenido;
       |PINTA #0Campo;
       |ERROR;
   |FINSI;
   |SI #0Campo ! Contenido; |Y Campo = 29;
     |HAZ LeeParamDiv;
   |FINSI;
|FCAL;

|PROCESO caltotal1;
     caltot = 1;
     #agifa081#11 = 0;
     #agifa081#12 = 0;
     #agifa081#34 = 0;
     #agifa081#35 = 0;
     |BUCLELIN 2actlinpc#0;
     |SI #0#29 = "D"; || Si la moneda de trabajo es la divisa
          #0#37 = #0#34;
          #0#38 = #0#35;
          #0#39 = #0#36;
          #0#40 = #0#11;
          #0#41 = #0#12;
          #0#42 = #0#13;
     |SINO;
          #0#37 = #0#11;
          #0#38 = #0#12;
          #0#39 = #0#13;
          #0#40 = #0#34;
          #0#41 = #0#35;
          #0#42 = #0#36;
     |FINSI;
     |SI nSwPinta = 0;
          |PINTA #0#37;
          |PINTA #0#38;
          |PINTA #0#39;
     |FINSI;
     |GRABA 021#0a;  ||Grabo actual porque he rectificado las lineas y si
                     ||sale con Ctrl-c no coincidira el Dto de cabecera con las lineas
     caltot = 0;
|FPRC;

|PROCESO caltotal; |TIPO 0; ||Proceso especial para casos determinados como
                            ||modificar los campos de cabecera en dtos, cambio divisas etc
                            ||Recalcula total pedido y pendiente...
                            ||En el caso de cambiar el cambio de divisas recalcula tambien precio... de lineas
     |SI #0Campo ! Contenido;||Solo si cambia
          |HAZ caltotal1;
     |FINSI;
|FPRC;

|PROCESO DatosEmpresa; |TIPO 7;
     nModo = (FEntrada / 100) ! 0;

     |SI nModo ! 1; |ACABA;   |FINSI;

     |DFICE aDirEmp;            || tomo el directorio de la empresa
     |DFICB aDatos;             || tomo el directorio de los datos
     aDirEmp = aDirEmp % -205;  || me quedo con los 5 digitos de la empresa
     nCodEmp = aDirEmp;         || y los paso a una variable numerica
     |PATH_DAT #11 aDatos;      || pongo el path al fichero agifa041
     |ABRE #11;                 || abre el fichero agifa041
     #11#0 = nCodEmp;
     |LEE 000#11=;              || y busco la empresa en la que estoy
     |SI FSalida = 0;
          #0#19 = #11#1;        || nombre de la empresa
          #0#18 = #11#2;        || direccion de la empresa
          #0#20 = #11#3;        || poblacion de la empresa
          #0#21 = #11#4;        || provincia de la empresa
          |PINTA #0#18;
          |PINTA #0#19;
          |PINTA #0#20;
          |PINTA #0#21;
     |FINSI;
     |CIERRA #11;               || cierro el fichero agifa041

     ||컴컴컴컴MODULO 컴
     |HAZ EsConstru;
     |SI FSalida = 0;
          |HAZ DirEnvObra;
          |PINTA #0#18;
          |PINTA #0#19;
          |PINTA #0#20;
          |PINTA #0#21;
     |FINSI;
|FPRC;
컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
|PROCESO RestauraPrecio;
     |SI #3#55 ! 0;
          nSwPromo = 1;
          |SI #0#29 = "D";
               #3#33 = #3#55;
          |SINO;
               #3#6 = #3#55;
          |FINSI;
          #3#55 = 0;
          |GRABA 020#3a;
          |LIBERA #3;
     |FINSI;
|FPRC;

|PROCESO RestauraPromo;
     nSwPromo = 0;
     |BUCLELIN 0 RestauraPrecio #0;
     |SI nSwPromo ! 0;
          |HAZ caltotal1;
     |FINSI;
|FPRC;
컴컴컴컴컴컴컴컴컴컴컴컴컴
|PROCESO ModiLinPedi;
     nResto = nTotUni $ (#239#4 + #239#5);
     nFalta = (#239#4 + #239#5) - nResto;
     nRegalo = (nTotUni-nResto) / (#239#4 + #239#5);
     nRegalo = nRegalo * #239#5;
     #3#28 = #0#25;
     #3#0 = #0#0;
     #3#1 = #239#0;
     |LEE 121#3=;
     |SI FSalida = 0;
          #3#55 = #3nCamPre;             || Guardo Original
          #3nCamPre = (#3nCamPre - ((nPrecio * nRegalo) / nTotUni)) ! nDec;
          |GRABA 020#3a;
          |LIBERA #3;
     |FINSI;
|FPRC;

|DEFBUCLE ModiLinPedi;
     #239#2002;
     ;
     nPromo, nPrecio;
     nPromo, nPrecio;
     ;
     NULL, ModiLinPedi;
|FIN;

|PROCESO dsw00040;
     |SI nPromo ! #240#1;
          |SI nPromo ! 0;
               |BUCLE ModiLinPedi;
          |FINSI;
          nPromo = #240#1;
          nPrecio = #240#2;
          nTotUni = 0;
     |FINSI;
     |SI nPrecio ! #240#2;
          aAlfa = "0000Precios distintos en Promocion:" + #240#1;
          |MENAV aAlfa;
          |BUCLE ModiLinPedi;
          nPrecio = #240#2;
          nTotUni = 0;
     |FINSI;
     nTotUni = nTotUni + #240#3;
|FPRC;

|DEFBUCLE dsw00040;
     #240#2;
     ;
     INICIO;
     FINAL;
     ;
     NULL, dsw00040;
|FIN;

|PROCESO LineaPedi;
     #245#0 = #0#4;
     #245#2 = #3#2;
     |LEE 000#245=;
     |SI FSalida = 0;
          #244#0 = #245#0;
          #244#1 = #245#1;
          |LEE 000#244=;
          |SI FSalida = 0; |Y #3#54 = #244#2;
               #240#0 = #3#1;
               #240#1 = #244#1;
               #240#2 = #3nCamPre;
               #240#3 = #3#5;
               #240#4 = #244#3;
               #240#5 = #244#4;
               |GRABA 020#240n;
               nSwPromo = 1;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Promociones;
     |DDEF aDef;
     aDef = aDef + "dsw00040";
     |CARGA_DEF aDef, dsw00040@;
     |SI FSalida ! 0;
          aDef = "0000Error al cargar " + aDef;
          |MENAV aDef; |ACABA;
     |FINSI;

     |HAZ CreaTempo; aTempo240 = Tempo;
     |NOME_DAT #240 aTempo240; |DELINDEX #240;
     |NOME_DAT #239 aTempo240;

     |SI #0#29 = "D";
          nCamPre = 33;
          nDec = nDeci_PreDivP;
     |SINO;
          nCamPre = 6;
          nDec = nDeci_PreBasMxP;
     |FINSI;
     nSwPromo = 0;
     |ABRE #240;
     |ABRE #244;
     |ABRE #245; |SELECT #2#245;
     |BUCLELIN 2LineaPedi#0;
     |CIERRA #244;
     |CIERRA #245;
     |CIERRA #240;
     |SI nSwPromo ! 0;
          |ABRE #239;
          |ABRE #3;
          nPromo = 0;
          |BUCLE dsw00040;
          |SI nPromo ! 0;
               |BUCLE ModiLinPedi;
          |FINSI;
          |CIERRA #3;
          |CIERRA #239;
          nSwPinta = 1;
          |HAZ caltotal1;
          nSwPinta = 0;
     |FINSI;
     |DELINDEX #240; Tempo = aTempo240; |HAZ BoraTempo;
     |DESCARGA_DEF #dsw00040@;
|FPRC;

|PROCESO ModiFec81;
     nPendiente = #3#5 - #3#38;
     nPendiente2 = #3#41 - #3#46;
     |SI nPendiente > 0; |O nPendiente2 > 0;
          #3#13 = #0#2;
          |GRABA 020#3a;
          |LIBERA #3;
          |SI nSwTecatel = 0;
               |HAZ TecaFecPed;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Fecha81; |TIPO 0;
     nModo = (FEntrada / 100) ! 0;

     |SI FSalida = 13;   || F5
          |HAZ EsAlbero;
          |SI FSalida = 0;
               ||HAZ AlbeConsuCab;
               |HAZ AlbeEntCab;
          |FINSI;
     |SINO;
          |SI nModo = 2; |Y fFecEntraAnt ! #0#2;
               |CONFI "0000NModificar fecha entrega de lineas ";
               |SI FSalida = 0;
                    |HAZ EsTecatel;
                    nSwTecatel = FSalida;
                    |HAZ EsCoimasa;
                    |SI FSalida = 0;
                         |HAZ CoimaModiFec;
                    |FINSI;
                    |BUCLELIN 0ModiFec81 #0;
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Fecha81_7; |TIPO 7;
     fFecEntraAnt = #0#2;
     |HAZ EsAlbero;
     |SI FSalida = 0;
          |HAZ AlbePedEntra;
          |MENSA "0000<F5> Consulta.";
     |FINSI;
|FPRC;

|PROCESO Tipo081_8; |TIPO 8;
     |HAZ EsAlbero;
     |SI FSalida = 0;
          |HAZ AlbePedCom8;
     |FINSI;
     |HAZ EsVisever;
     |SI FSalida = 0;
          |CONTROL_SIMPLE 0, "BOTON,{{157}}Enviar pedido", 0565, 0578, ViseEnviaPed;
          nBtnVise = FSalida;
     |FINSI;
|FPRC;

|PROCESO Tipo081_9; |TIPO 9;
     |HAZ EsAlbero;
     |SI FSalida = 0;
          |HAZ AlbePedCom9;
     |FINSI;
     |HAZ EsVisever;
     |SI FSalida = 0;
          |FIN_CONTROL_WINDOWS nBtnVise;
     |FINSI;
|FPRC;

|PROCESO Cambio81; |TIPO 0;
     nSalida = FSalida;
     |HAZ caltotal;
     FSalida = nSalida;

     ||SI nSalida = 0;
          |HAZ EsTagloma;
          |SI FSalida = 0;
               |HAZ TagPedEntra;
          |FINSI;
     ||FINSI;

     |SI nSalida = 0;
          |HAZ EsDesanch;
          |SI FSalida = 0;
               aAlfa = "desan016;PA" + #0#25 + (("000000" + #0#0) % -106);
               |SUB_EJECUTA aAlfa;
               |HAZ PinDesanch81;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO TagloImpre;
     |MENU Taglo;
     Taglo2 = FSalida;
     |SI Taglo2 = 1;
          ||---------envio por correo (pdf)
          |DBASS aAlfa;
          aFicSpoo = aAlfa + "spool/" + #0#25 + (("000000"+#0#0)%-106)+".pdf";
          ||SISCO: poner las barras de Windows: cambiar / por \
          eaFichero = "c:\ds\crystal\" + #0#25 + (("000000"+#0#0)%-106)+".pdf";
          |QBT eaFichero;
          |QBT aFicSpoo;
          ||eaDefImpre = "CrystalReport;" + eaFichero;
          eaImpresora = "CrystalReport;" + eaFichero;

          |HAZ prnpc;     || esto crea el fichero en el cliente
          |RECIBE_FICHERO eaFichero, aFicSpoo;|| lo copio al servidor
          eaFichero = aFicSpoo;
          |HAZ TagEnvCorreo;
          |FBORRA eaFichero;
          |HAZ TagCorreoPed;  ||Marca
          ||-------imprime normal
          eaImpresora = "";
          eaDefImpre = "Crystal Reports";
          |HAZ prnpc;
          |HAZ TagImpPed;     ||Marca
     |FINSI;
     |SI Taglo2 = 2;
          eaDefImpre = "Crystal Reports";
          |HAZ prnpc;
          |HAZ TagImpPed;     ||Marca
     |FINSI;
     |HAZ TagPedPinta;
|FPRC;

|PROCESO Tipo81_20; |TIPO 20;
     |SI FSalida ! "OPCION";
          |SELECT #1#0;
          |LEE 000#0=;
          |SI FSalida ! 0;
               |MENAV "0000Por favor, consulte el registro correcto antes de imprimir.";
               |ACABA;
          |SINO;
               |PINDA #0#0;
          |FINSI;
     |FINSI;
     |HAZ T81_20;
|FPRC;

|PROCESO T81_20;
     aSalida = FSalida;
     |HAZ EsPCEGroup; nSwPCEGroup = FSalida;
     |HAZ EsConstru; nSwConstru = FSalida;
     |HAZ EsOk5050; nSwOk5050 = FSalida;

     |SI aSalida = "OPCION";
          |SI nBtnVise ! 0; |ESTADO_CONTROL nBtnVise, "ENABLE"; |FINSI;
          FSalida = "Imprimir";
          |SI nSwPCEGroup = 0; FSalida = "Opciones"; |FINSI;
          |SI nSwConstru = 0; FSalida = "Opciones"; |FINSI;
          |SI nSwOk5050 = 0; FSalida = "Opciones"; |FINSI;
          |ACABA;
     |FINSI;
     |SELECT #1#0;
     |LEE 000#0=;
     |SI FSalida ! 0; |ACABA; |FINSI;

     |SI nSwPCEGroup = 0;
          |HAZ OpcionPCEG;
          |ACABA;
     |FINSI;
     |SI nSwConstru = 0;
          |HAZ OpcionConstru;
          |ACABA;
     |FINSI;

     |HAZ EsTagloma;
     |SI FSalida = 0;
          |HAZ TagloImpre;
          |ACABA;
     |FINSI;

     |SI nSwOk5050 = 0;
          |HAZ OpcionOk5050;
          |ACABA;
     |FINSI;

     ||------------El proceso estandar

     |HAZ prnpc;
|FPRC;

|PROCESO T081_15; |TIPO 15;
     nTipo = 15; |HAZ Log081;
|FPRC;

|PROCESO T081_16; |TIPO 16;
     nTipo = 16; |HAZ Log081;
|FPRC;

|PROCESO T081_17; |TIPO 17;
     nTipo = 17; |HAZ Log081;
|FPRC;

|PROCESO Log081;
     |DDEF aAlfa;
     aAlfa = aAlfa + "dsm00087";
     |CARGA_DEF aAlfa, dsm00087@;
     |SI FSalida ! 0; |ACABA; |FINSI;

     aSer = #0#25;
     aNum = ("000000" + #0#0) % -106;
     aTipo = "PC";

     |ABRE #dsm00087@;
     |DDEFECTO #dsm00087@;
     |SELECT #2#dsm00087@;
     #dsm00087@#1 = aSer;
     #dsm00087@#2 = aNum;
     #dsm00087@#3 = aTipo;
     |LEE 101#dsm00087@.=;
     |SI FSalida ! 0;
          |SELECT #1#dsm00087@;
          |LEE 000#dsm00087@.u;
          |SI FSalida = 0;
               nContaLog = #dsm00087@#0 + 1;
          |SINO;
               nContaLog = 1;
          |FINSI;
          |DDEFECTO #dsm00087@;
          #dsm00087@#0 = nContaLog;
          #dsm00087@#1 = aSer;
          #dsm00087@#2 = aNum;
          #dsm00087@#3 = aTipo;
          |GRABA 020#dsm00087@.b;
     |FINSI;
     |SI FSalida = 0;
          |HORA aAlfa;
          aUsu = Usuario % 810;
          |SI nTipo = 15;
               #dsm00087@#4 = aUsu;
               #dsm00087@#5 = ~;
               #dsm00087@#6 = aAlfa;
               #dsm00087@#7 = "";
               #dsm00087@#8 = "01.01.0000";
               #dsm00087@#9 = ""
               #dsm00087@#10 = "";
               #dsm00087@#11 = "01.01.0000";
               #dsm00087@#12 = ""
          |FINSI;
          |SI nTipo = 16;
               #dsm00087@#7 = aUsu;
               #dsm00087@#8 = ~;
               #dsm00087@#9 = aAlfa;
          |FINSI;
          |SI nTipo = 17;
               #dsm00087@#10 = aUsu;
               #dsm00087@#11 = ~;
               #dsm00087@#12 = aAlfa;
          |FINSI;
          |GRABA 020#dsm00087@.a;
     |FINSI;
     |CIERRA #dsm00087@;
     |DESCARGA_DEF #dsm00087@;
|FPRC;

|PROCESO EtiquetaPCEG;
     modo = (FEntrada / 100) ! 0;
     |SI modo = 1; |O modo = 2;
          |GRABA 021#0a;
          |LIBERA #0;
     |FINSI;

     |DDEF aAlfa;
     aAlfa = aAlfa + "pcegm023";
     |CARGA_DEF aAlfa, referen@;
     |SI FSalida ! 0;
          |MENAV "0000ERROR al cargar 'pcegm023'";
     |SINO;
          |ABRE #referen@;
          #referen@#0 = "PC";
          #referen@#1 = #0#25;
          #referen@#2 = #0#0;
          |LEE 000#referen@.=;
          |SI FSalida ! 0; |O nSwEtiPCEG = 1;
               eaSerie = #0#25;
               enDocu = #0#0;
               eaTipo = "PC"
               eaPregunta = "S";
               |CIERRAT #0;
               |SUB_EJECUTA_NP "pcegz019";
               |ABRET #0;
               #0#25 = eaSerie;
               #0#0 = enDocu;
               |SI modo = 4;
                    |LEE 001#0=;
               |SINO;
                    |LEE 101#0=;
               |FINSI;
          |FINSI;
          |CIERRA #referen@;
          |DESCARGA_DEF #referen@;
     |FINSI;
|FPRC;

|PROCESO OpcionPCEG;
     |MENU aMenuPCEG;
     |SI FSalida [ 0; |ACABA; |FINSI;
     |SI FSalida = 1;
          |HAZ prnpc;
     |FINSI;
     |SI FSalida = 2;
          nSwEtiPCEG = 1;
          |HAZ EtiquetaPCEG;
          nSwEtiPCEG = 0;
     |FINSI;
|FPRC;

|PROCESO CoimaHisFec;
     #referen@#10 = #0#2;
     |GRABA 020#referen@.a;
     |LIBERA #referen@;
|FPRC;

|DEFBUCLE CoimaHisFec;
     #referen@#1006;
     6;
     "C", #0#25, #0#0, 001, 000000, 000, 0;
     "C", #0#25, #0#0, 999, 999999, 999, 0;
     be;
     NULL, CoimaHisFec;
|FIN;

|PROCESO CoimaModiFec;
     |DDEF aAlfa;
     aAlfa = aAlfa + "coima003";
     |CARGA_DEF aAlfa, referen@;
     |SI FSalida = 0;
          |BUCLE CoimaHisFec;
          |DESCARGA_DEF #referen@;
     |FINSI;
|FPRC;

|PROCESO CercamaPinta;
     |C_V #0#27, 1, "N";
     |C_V #0#28, 1, "N";
     |C_V #0#29, 1, "N";
     |PINTA 1203, " Cliente: @2____  @3____________________________  Pedido Vta: @_   /@5____";
     |PINTA 1302, "쳐컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컫컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴";
     |DDEF aDef;
     aDef = aDef + "cerca001";
     |CARGA_DEF aDef, referen@;
     |SI FSalida = 0;
          |DDEFECTO #referen@;
          |ABRE #referen@;
          #referen@#0 = #0#25;
          #referen@#1 = #0#0;
          |LEE 000#referen@.=;
          |CIERRA #referen@;
          |PINTA #referen@#2;
          |PINTA #referen@#3;
          |PINTA #referen@#4;
          |PINTA #referen@#5;
          |DESCARGA_DEF #referen@;
     |FINSI;
|FPRC;

|PROCESO PinDesanch81;
     |DDEF aAlfa;
     aAlfa = aAlfa + "desan016";
     |CARGA_DEF aAlfa, referen@;
     |SI FSalida = 0;
          |DDEFECTO #referen@;
          |ABRE #referen@;
          #referen@#0 = "P";
          #referen@#1 = #0#25;
          #referen@#2 = #0#0;
          |LEE 000#referen@.=;
          |CIERRA #referen@;
          |PINTA 1258, "%Dto2:   %Dto3:  ";
          |PINTA 1358, "컴컴컴컴컴컴컴컴";
          |ATRI I;
          |PINTA 1265, #referen@#3;
          |PINTA 1274, #referen@#4;
          |ATRI 0;
          |DESCARGA_DEF #referen@;
     |FINSI;
|FPRC;

|PROCESO OpcionConstru;
     |MENU aMenuConstru;
     |SI FSalida [ 0; |ACABA; |FINSI;
     |SI FSalida = 1;
          |HAZ prnpc;
     |FINSI;
     |SI FSalida = 2;
          eaSerie = #0#25;
          enDocu = #0#0;
          |DFICE aAlfa;
          |SUB_EJECUTA_NP  "gomaz003";
     |FINSI;
|FPRC;

|PROCESO ViseEnviaPed;
     ser_ped = #0#25;
     num_ped = #0#0;
     |SUB_EJECUTA_NP "visez012";
|FPRC;

|PROCESO OpcionOk5050;
     |MENU aMenuOk5050;
     |SI FSalida > 0;
          |SI FSalida = 1; |HAZ prnpc; |FINSI;
          |SI FSalida = 2;
               eaSerie = #0#25;
               enDocu = #0#0;
               |SUB_EJECUTA_NP "okm00002";
          |FINSI;
     |FINSI;
|FPRC;
