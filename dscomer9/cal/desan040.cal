|FICHEROS;
     desan040 #0;
     desan041 #1;
     agifa017 #17;
     agifa019 #19;
     desan027 #27;
     desan034 #34,MANTE; ||Tmp
     dsm00043 #43;
     dsm00044 #44;
     dsw00047 #47,MANTE;
     desan041a@;
     desan041b@;
     desan034a@;
     desan034b@;
|FIN;

|VARIABLES;
     &enForma = 0;
     nForma = 0;
     nLin = 0;
     &Tempo = "";
     i = 0;
     nHay = 0;
     nNum = 0;
     aAlfa = "";
     nModo = 0;
     nGaran = 0;
     aDef = "";
     nSw34 = 0;
     {-1}aMenu = "";
     aMenu1 = "2400";
     aMenu2 = "1";
     aMenu3 = "Opciones:  ";
     aMenu4 = "RAC";
     aMenu5 = " Repasar ";
     aMenu6 = " Aceptar ";
     aMenu7 = " Cancelar ";
     aTmp34 = "";
     aTmp34b = "";
|FIN;

|PROCESO Contador; |TIPO 7;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 1;
          aAlfa = #0#0;
          #0#1 = 999999;
          |LEE 000#0];
          |SI FSalida = 0;
               |LEE 000#0a;
          |SINO;
               |LEE 000#0u;
          |FINSI;
          |SI FSalida = 0; |Y #0#0 = aAlfa;
               nNum = #0#1 + 1;
          |SINO;
               nNum = 1;
          |FINSI;
          |DDEFECTO #0;
          #0#0 = aAlfa;
          #0#1 = nNum;
     |FINSI;
|FPRC;

|PROCESO Garan; |TIPO 0;
     |ABRE #27;
     #27#0 = #1#3;
     |LEE 020#27=;
     |SI FSalida = 0;
          |SI #27#28 = "E";
               |MENAV "0000Garantia enviada al proveedor...";
               |ERROR;
          |FINSI;
     |FINSI;
     |CIERRA #27;
|FPRC;

|PROCESO Tecla11; |TIPO 11;
     |SI FSalida = 13;
          nGaran = #34#0;
          |HAZ Garantia;
          |ERROR;
     |FINSI;
     |SI FSalida = 14;
          nSw34 = 1;
          |HAZ Busca34;
          nSw34 = 0;
     |FINSI;
|FPRC;

|PROCESO Tecla0; |TIPO 0;
     |SI FSalida = 0; |Y #34Campo = Contenido;
          |SI #34#31 = "*";
               #34#31 = "";
          |SINO;
               #34#31 = "*";
          |FINSI;
          |GRABA 020#34a;
     |FINSI;
|FPRC;

|PROCESO Carga;
     |PARA i = 0; |SI i [ 30; |HACIENDO i = i + 1;
          #34i = #27i;
     |SIGUE;
     #34#31 = "";
     |GRABA 020#34n;
     nHay = 1;
|FPRC;

|DEFBUCLE Carga;
     #27#2;
     28;
     #0#3, "A";
     #0#3, "A";
     ;
     NULL, Carga;
|FIN;

|PROCESO Tmp;
     #27#0 = #34#0;
     |LEE 121#27=;
     |SI FSalida = 0;
          #27#28 = "E";
          |GRABA 020#27a;
          |LIBERA #27;

          nLin = nLin + 1;
          |DDEFECTO #1;
          #1#0 = #0#0;
          #1#1 = #0#1;
          #1#2 = nLin;
          #1#3 = #34#0;
          #1#4 = #34#1;
          #1#5 = #34#5;
          #1#6 = #34#6;
          |GRABA 020#1n;
          |HAZ CreaRegu;
     |FINSI;
|FPRC;

|DEFBUCLE Tmp;
     #34#1;
     31;
     000000, "*";
     999999, "*";
     ;
     NULL, Tmp;
|FIN;

|PROCESO Min34;
     #34#0 = 0;
|FPRC;

|PROCESO Max34;
     #34#0 = 999999;
|FPRC;

|PROCESO Tipo5; |TIPO 5;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo ! 1 ; |ACABA; |FINSI;

     |HAZ CreaTempo; |NOME_DAT #34 Tempo; |DELINDEX #34; aTmp34 = Tempo;
     nHay = 0;
     |ABRE #34;
     |BUCLE Carga;
     |CIERRA #34;
     |SI nHay = 0;
          |MENAV "0000No hay garantias pendientes...";
     |SINO;
          |PUSHV 0501 2380;
          aMenu2 = "1";
          |PINPA #0#34;
          |PARA; |SI; |HACIENDO;
               |ENTLINEAL #1#34, 1, 4, 22, 0, Min34, Max34;
               |MENU aMenu;
               aMenu2 = FSalida;
               |SI aMenu2 ! 1; |SAL; |FINSI;
          |SIGUE;
          |POPV;
          |SI aMenu2 = "2";
               nLin = 0;
               |ABRE #27;
               |BUCLE Tmp;
               |CIERRA #27;
          |FINSI;
     |FINSI;
     Tempo = aTmp34; |HAZ BoraTempo; |DELINDEX #34;
|FPRC;

|PROCESO Tipo2; |TIPO 2;
     nForma = 0 +. 1;

     |ABRE #27;
     #27#0 = #1#3;
     |LEE 121#27=;
     |SI FSalida = 0;
          |SI nForma = 1;
               #27#28 = "E";
               |HAZ CreaRegu;
          |SINO;
               #27#28 = "A";
               |HAZ BorraRegu;
          |FINSI;
     |FINSI;
     |GRABA 020#27a;
     |CIERRA #27;
|FPRC;

|PROCESO CreaRegu;
     |ABRE #43;
     |ABRE #44;

     #0#5 = #0#0;   || Serie
     |SI #0#6 = 0;
          #43#4 = #0#5;
          #43#0 = 999999;
          |LEE 000#43];
          |SI FSalida = 0;
               |LEE 000#43a;
          |SINO;
               |LEE 000#43u;
          |FINSI;
          |SI FSalida = 0; |Y #43#4 = #0#5;
               nNum = #43#0 + 1;
          |SINO;
               nNum = 1;
          |FINSI;
          #0#6 = nNum;
          |DDEFECTO #43;
          #43#4 = #0#5;
          #43#0 = nNum;
          #43#2 = "GARANTIA PROVEEDOR";
          |GRABA 020#43b;
     |FINSI;

     |DDEFECTO #44;
     #44#21 = #0#5;
     #44#18 = #0#6;
     #44#8 = #1#2;

     #44#0 = #27#5;
     #44#1 = 1;
     #44#2 = #27#6;

     |ABRE #19;
     #19#0 = #44#0;
     |LEE 000#19=;
     |CIERRA #19;
     |ABRE #17;
     #17#0 = #44#0;
     #17#1 = #44#1;
     |LEE 000#17=;
     |CIERRA #17;

     #44#3 = #17#39;
     #44#4 = 1;
     #44#5 = #43#1;
     #44#6 = #19#6;
     #44#7 = "S";
     #44#19 = #17#5;
     #44#20 = #19#104;
     #44#26 = #19#133;
     |GRABA 020#44n;
     enForma = 1;
     |HAZ AcumulaES_SS;
     |CIERRA #43;
     |CIERRA #44;
|FPRC;

|PROCESO BorraRegu;
     |ABRE #44;
     |ABRE #43;
     #44#21 = #0#5;
     #44#18 = #0#6;
     #44#8 = #1#2;
     |LEE 101#44=;
     |SI FSalida = 0;
          enForma = -1;
          |HAZ AcumulaES_SS;
          |BORRA 020#44a;
          #44#21 = #0#5;
          #44#18 = #0#6;
          #44#8 = 0;
          |LEE 000#44];
          |SI FSalida ! 0; |O #44#21 ! #0#5; |O #44#18 ! #0#6;
               #43#4 = #0#5;
               #43#0 = #0#6;
               |BORRA 020#43c;
          |FINSI;
     |FINSI;
     |CIERRA #43;
     |CIERRA #44;
|FPRC;

|PROCESO Tecla41; |TIPO 11;
     |SI FSalida = 13;
          nGaran = #1#1;
          |HAZ Garantia;
          |ERROR;
     |FINSI;
     |SI FSalida = 14;
          |HAZ Busca;
     |FINSI;
|FPRC;

|PROCESO Garantia;
     |ABRE #27;
     #27#0 = nGaran;
     |LEE 101#27=;
     |SI FSalida = 0;
          |PUSHV 0501 2380;
          |PINPA #0#27;
          |PINDA #0#27;
          |ENDATOS #2#27;
          |SI FSalida = 0;
               |GRABA 020#27a;
          |FINSI;
          |POPV;
     |FINSI;
     |CIERRA #27;
|FPRC;

|PROCESO Tipo12; |TIPO 12;
|FPRC;

|PROCESO Min34a;
     #desan034a@#5 = "                    ";
     #desan034a@#0 = 000000;
|FPRC;

|PROCESO Max34a;
     #desan034a@#5 = "zzzzzzzzzzzzzzzzzzzz";
     #desan034a@#0 = 999999;
|FPRC;

|PROCESO Min41;
     #desan041a@#5 = "                    ";
     #desan041a@#0 = #0#0;
     #desan041a@#1 = #0#1;
|FPRC;

|PROCESO Max41;
     #desan041a@#5 = "zzzzzzzzzzzzzzzzzzzz";
     #desan041a@#0 = #0#0;
     #desan041a@#1 = #0#1;
     #desan041a@#2 = 999;
|FPRC;

|PROCESO Tipo66; |TIPO 66;
     |SI nSw34 = 0;
          |DDEF aDef;
          aAlfa = aDef + "desan041";
          |CARGA_DEF aAlfa, desan041a@;
          |SI FSalida = 0;
               |ABRE #desan041a@;
               |CONSULTA_F_CLAVE #2#desan041a@, Min41, Max41;
               |SI FSalida = 0;
                    #47#0 = #desan041a@#5;
               |SINO;
                    |ERROR;
               |FINSI;
               |CIERRA #desan041a@;
               |DESCARGA_DEF #desan041a@;
          |FINSI;
     |SINO;
          |DDEF aDef;
          aAlfa = aDef + "desan034";
          |CARGA_DEF aAlfa, desan034a@;
          |SI FSalida = 0;
               |NOME_DAT #desan034a@#aTmp34#;
               |ABRE #desan034a@;
               |CONSULTA_F_CLAVE #2#desan034a@, Min34a, Max34a;
               |SI FSalida = 0;
                    #47#0 = #desan034a@#5;
               |SINO;
                    |ERROR;
               |FINSI;
               |CIERRA #desan034a@;
               |DESCARGA_DEF #desan034a@;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO CargaTmp;
     |SALVA_DATOS #desan041b@;
     |REPON_DATOS #desan041a@;
     |GRABA 020#desan041a@.n;
|FPRC;

|DEFBUCLE CargaTmp;
     #desan041b@#2003;
     ;
     #47#0, #0#0, #0#1;
     #47#0, #0#0, #0#1;
     ;
     NULL, CargaTmp;
|FIN;

|PROCESO Busca;
     |PUSHV 0501 2380;
     |BLANCO 1026 1655;
     #47#0 = #1#5;
     |PINPA #0#47;
     |PINDA #0#47;
     |ENDATOS #1#47;
     |SI FSalida = 0;
          |DDEF aDef;
          aAlfa = aDef + "desan041";
          |CARGA_DEF aAlfa, desan041a@;
          |SI FSalida = 0;
               aAlfa = aDef + "desan041";
               |CARGA_DEF aAlfa, desan041b@;
               |SI FSalida = 0;
                    |HAZ CreaTempo; |NOME_DAT #desan041a@#Tempo#; |DELINDEX #desan041a@;
                    |ABRE #desan041a@;
                    |BUCLE CargaTmp;
                    |LEE 000#desan041a@.p;
                    |SI FSalida = 0
                         |CONSULTA_CLAVE #1#desan041a@;
                         |SI FSalida = 0;
                              #1#2 = #desan041a@#2;
                              |PTEC 802;
                         |FINSI;
                    |SINO;
                         |MENAV "0000No se encuentra el articulo...";
                    |FINSI;
                    |CIERRA #desan041a@;
                    |HAZ BoraTempo; |DELINDEX #desan041a@;
                    |DESCARGA_DEF #desan041b@;
               |FINSI;
               |DESCARGA_DEF #desan041a@;
               |ERROR;
          |FINSI;
     |FINSI;
     |POPV;
|FPRC;

|PROCESO CargaTmp34;
     |SALVA_DATOS #desan034b@;
     |REPON_DATOS #desan034a@;
     |GRABA 020#desan034a@.n;
|FPRC;

|DEFBUCLE CargaTmp34;
     #desan034b@#2002;
     ;
     #47#0, 000000;
     #47#0, 999999;
     ;
     NULL, CargaTmp34;
|FIN;

|PROCESO Busca34;
     |PUSHV 0501 2380;
     |BLANCO 1026 1655;
     #47#0 = #34#5;
     |PINPA #0#47;
     |PINDA #0#47;
     |ENDATOS #1#47;
     |SI FSalida = 0;
          |DDEF aDef;
          aAlfa = aDef + "desan034";
          |CARGA_DEF aAlfa, desan034a@;
          |SI FSalida = 0;
               aAlfa = aDef + "desan034";
               |CARGA_DEF aAlfa, desan034b@;
               |SI FSalida = 0;
                    |HAZ CreaTempo; |NOME_DAT #desan034a@#Tempo#; |DELINDEX #desan034a@;
                    aTmp34b = Tempo;
                    |NOME_DAT #desan034b@#aTmp34#;
                    |ABRE #desan034a@;
                    |BUCLE CargaTmp34;
                    |LEE 000#desan034a@.p;
                    |SI FSalida = 0
                         |CONSULTA_CLAVE #1#desan034a@;
                         |SI FSalida = 0;
                              #34#0 = #desan034a@#0;
                              |PTEC 802;
                         |FINSI;
                    |SINO;
                         |MENAV "0000No se encuentra el articulo...";
                    |FINSI;
                    |CIERRA #desan034a@;
                    Tempo = aTmp34b; |HAZ BoraTempo; |DELINDEX #desan034a@;
                    |DESCARGA_DEF #desan034b@;
               |FINSI;
               |DESCARGA_DEF #desan034a@;
               |ERROR;
          |FINSI;
     |FINSI;
     |POPV;
|FPRC;

|PROCESO Tipo3; |TIPO 3;
     |CONFI "0000NImprimir documento:";
     |SI FSalida = 0;
          |HAZ Tipo40_4;
     |FINSI;
|FPRC;

|PROCESO Impre;
     |PRINT;
|FPRC;

|PROCESO Tipo40_4; |TIPO 4;
     |IMPRE 0;
     |SI FSalida = 0;
          |INFOR "desan040";
          |SI FSalida = 0;
               |BUCLELIN 2 Impre #0;
               |PIEINF;
               |FININF;
          |SINO;
               |MENAV "0000Error en informe 'desan040'";
          |FINSI;
          |FINIMP;
     |FINSI;
|FPRC;
