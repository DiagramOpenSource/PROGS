NuevoParte, ModiParte, ModiLin, BtnCarac

|FICHEROS;
     extz0009 #0;
     extm0002 #2;
     extm0003 #3;
     extm0004 #4;
     extm0011 #11;
     extm0012 #12;
     extm0013 #13;
     extm0014 #14;
     extm0015 #15;
     extm0016 #16;
     extm0017 #17;
     agifa019 #19;
     agifa024 #24;
     extm0026 #26,MANTE;  || ambos tienen el mismo
     extm0027 #27,MANTE;  || fichero de datos
     dsm00003 #30;
     extm0001 #100;
     extz0004 #104,MANTE;     || Pan. Articulo Extras a Fact
     extz0005 #105,MANTE;     || Pan. Articulos Dejados Cli
     extz0006 #106,MANTE;     || Pan. Lin
     extz0010 #110,MANTE;     || Pan. Revi Cab
     extm0024 #124;
     extz0030 #300,MANTE;     || Pan. Agrupado

     extm0014@;
     referen@;
|FIN;

|VARIABLES;
     &eaPro = "";
     &enCod = "";
     &eaVal = "";
     &enSwAgrupa = 0;
     &eaCliInci = "";
     &eaSerParte = "";
     &enNumParte = 0;
     &enForma = 0;
     &enTipo = 0;
     &eaNumSerie = "";
     &eaNumNuevo = 0;
     &enModo = 0;
     &eaProg = "";
     &enCtrFin = 0;
     &enError = 0;
     &Tempo = "";
     nPanParte = 0;
     nPanLin = 0;
     nGrid1 = 0;
     nBtnAlta = -1;
     nBtnModi = -1;
     nBtnBaja = -1;
     nBtnCons = -1;
     nBtnDejado = 0;
     nBtnExtra = 0;
     nBtnAltaD = 0;
     nBtnModiD = 0;
     nBtnBajaD = 0;
     nBtnConsD = 0;
     nBtnAltaE = 0;
     nBtnModiE = 0;
     nBtnBajaE = 0;
     nBtnConsE = 0;
     nBtnActual = 0;
     nBtnAuto = 0;
     nBtnCarac = 0;
     nGridArt = 0;
     nModoArt = 0;
     nBloquea = 0;
     nBloqueaLin = 0;
     nModo = 0;
     nConta = 0;
     aConte = "";
     aAlfa = "";
     nSw = 0;
     i = 0;
     nRango = 0;
     nModoLin = 0;
     aEstado = "";
     aTmp = "";
     nLin = 0;
     aCliFa = "";
     aComer = "";
     {50}aModi = "";
     aCampos = "";
     nNume = 0;
     nModoReg = 0;
     aSer = "";
     nNum = 0;
     aPro = "";
     aMesAno1 = "";
     aMesAno2 = "";
|FIN;

|PROCESO BorraCara;
     |BORRA 020#26a;
|FPRC;

|DEFBUCLE BorraCara;
     #26#1;
     ;
     aSer, nNum, aPro, 00000;
     aSer, nNum, aPro, 99999;
     be;
     NULL, BorraCara;
|FIN;

|PROCESO BorraCara0;
     aSer = #14#0;
     nNum = #14#1;
     aPro = #14#2;
     |BUCLE BorraCara;
|FPRC;

|PROCESO CreaCara;
     |DDEFECTO #26;
     #26#0 = #106#0;
     #26#1 = #106#1;
     #26#2 = #106#2;
     #26#3 = #124#0;
     |LEE 101#26=;
     #26#4 = #124#1;
     #26#5 = #124#3;
     |SI FSalida ! 0;
          #26#6 = #124#7;
          |GRABA 020#26b;
     |FINSI;
     |GRABA 020#26a;
     |LIBERA #26;
|FPRC;

|DEFBUCLE CreaCara;
     #124#2;
     ;
     #100#10;
     #100#10;
     ;
     NULL, CreaCara;
|FIN;

|PROCESO QuitaCara;
     #124#0 = #26#3;
     |LEE 000#124=;
     |SI FSalida ! 0;
          |BORRA 020#26a;
     |FINSI;
|FPRC;

|DEFBUCLE QuitaCara;
     #26#1;
     ;
     #106#0, #106#1, #106#2, 00000;
     #106#0, #106#1, #106#2, 99999;
     be;
     NULL, QuitaCara;
|FIN;

|PROCESO CreaCara0;
     |ABRE #100;
     #100#0 = #106#5;
     |LEE 000#100=;
     |SI FSalida = 0;
          |BUCLE CreaCara;
          |ABRE #124;
          |BUCLE QuitaCara;
          |CIERRA #124;
     |FINSI;
     |CIERRA #100;
|FPRC;

|PROCESO NoModiReg2; |TIPO 2;
     nModoReg = (FEntrada / 100) ! 0;
     |SI nModoReg = 3; |AVISO; |ERROR; |FINSI;
|FPRC;

|PROCESO NoModiReg0; |TIPO 0;
     nModoReg = (FEntrada / 100) ! 0;
     |SI nModoReg = 1; |AVISO; |ERROR; |FINSI;
|FPRC;

|PROCESO ValorR;
     eaPro = #26#2;
     enCod = #26#3;
     eaVal = #26#6;
     |HAZ ChequeaValor;
     |SI enError = 0;
          #26#6 = eaVal; |PINTA #27#6;
     |SINO;
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO ValorT;
     eaPro = #27#2;
     enCod = #27#3;
     eaVal = #27#6;
     |HAZ ChequeaValor;
     |SI enError = 0;
          #27#6 = eaVal; |PINTA #27#6;
     |SINO;
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO EvtT26;
     |SI FSalida = 1; |O FSalida = 2;
          |SELECT #2#27;
          |LEE 000#27=;
     |FINSI;
|FPRC;

|PROCESO EvtR26;
     |SI FSalida = 1; |O FSalida = 2;
          |SELECT #2#26;
          |LEE 000#26=;
     |FINSI;
|FPRC;

|PROCESO MinT;
     #27#0 = #106#0;
     #27#1 = #106#1;
     #27#2 = #106#2;
     #27#5 = "T";
     #27#3 = 0000;
|FPRC;

|PROCESO MaxT;
     #27#0 = #106#0;
     #27#1 = #106#1;
     #27#2 = #106#2;
     #27#5 = "T";
     #27#3 = 9999;
|FPRC;

|PROCESO MinR;
     #26#0 = #106#0;
     #26#1 = #106#1;
     #26#2 = #106#2;
     #26#5 = "R";
     #26#3 = 0000;
|FPRC;

|PROCESO MaxR;
     #26#0 = #106#0;
     #26#1 = #106#1;
     #26#2 = #106#2;
     #26#5 = "R";
     #26#3 = 9999;
|FPRC;

|PROCESO BtnCarac;
     |VENTANA 1401, 3399, -1, 17, "Caracteristicas";
     |HAZ PushVenModal;
     |SI nModoLin = 4;
          nRango = 4 + 8 + 16 + 32;
     |SINO;
          nRango = 1 + 4 + 8 + 16 + 32;
     |FINSI;
     |ABRE #26;
     |ABRE #27;
     |HAZ CreaCara0;
     #26#0 = #106#0;
     #26#1 = #106#1;
     #26#2 = #106#2;
     #26#5 = "R";
     #26#3 = 0000;
     |LEE 000#26];
     #27#0 = #106#0;
     #27#1 = #106#1;
     #27#2 = #106#2;
     #27#5 = "T";
     #27#3 = 0000;
     |LEE 000#27];
     |CONTROL_SIMPLE 0, "LABEL,{{003}}Caracteristicas de revisi¢n", 1402, 1498, NULL;
     |LINEAL_SIMPLE #2#26, nRango, 1502, 2298, MinR, MaxR, EvtR26;
     |CONTROL_SIMPLE 0, "LABEL,{{003}}Caracteristicas de trabajo", 2302, 2398, NULL;
     |LINEAL_SIMPLE #2#27, nRango, 2402, 3198, MinT, MaxT, EvtT26;
     |PAUSA;
     |CIERRA #27;
     |CIERRA #26;
     |HAZ PopVenModal;
|FPRC;

|PROCESO FecReal; |TIPO 0;
     |SI enTipo = 6;
          |ABRE #11;
          #11#0 = #106#2;
          |LEE 020#11=;
          |CIERRA #11;

          |SI #11#25 = "A"; nNume = 1/1000000; |FINSI;
          |SI #11#25 = "S"; nNume = 1/1000 * 6; |FINSI;
          |SI #11#25 = "T"; nNume = 1/1000 * 3; |FINSI;
          |SI #11#25 = "M"; nNume = 1/1000 * 1; |FINSI;
          #106#14 = #106#25; |PINTA #106#14;
          #106#10 = #106#14 + nNume; |PINTA #106#10;
     |FINSI;
|FPRC;

|PROCESO BtnAuto;
     |ABRE #11;
     #11#0 = #106#2;
     |LEE 020#11=;
     |CIERRA #11;

     |SI #11#25 = "A"; nNume = 1/1000000; |FINSI;
     |SI #11#25 = "S"; nNume = 1/1000 * 6; |FINSI;
     |SI #11#25 = "T"; nNume = 1/1000 * 3; |FINSI;
     |SI #11#25 = "M"; nNume = 1/1000 * 1; |FINSI;
     #106#10 = #106#8 + nNume; |PINTA #106#10;
|FPRC;

|PROCESO BtnActual;
     #106#10 = #106#8; |PINTA #106#10;
|FPRC;

|PROCESO ModiLin;
     |ABRE #13;
     #13#0 = #14#0;
     #13#1 = #14#1;
     |LEE 020#13=;
     |CIERRA #13;

     aAlfa = "|NC"; aCampos = #14aAlfa;
     IaModi = aModi1<-;
     |PARA i = 0; |SI i < aCampos; |HACIENDO i = i + 1;
          #106i = #14i;
          |C_M #106i, 0, aAlfa;
          aModi = aAlfa;
          IaModi = IaModi + 1;
          |C_M #106i, 1, "N";
     |SIGUE;

     |SI enTipo = 1;
          |C_M #106#14, 1, "S";
          |VENTANA 1401, 3399, -1, 17, "Recogida cliente";
     |FINSI;
     |SI enTipo = 2;
          |C_M #106#6, 1, "S";
          |C_M #106#19, 1, "S";
          |C_M #106#16, 1, "S";
          |C_M #106#17, 1, "S";
          |VENTANA 1401, 3399, -1, 17, "Observaciones";
     |FINSI;
     |SI enTipo = 3;
          |C_M #106#10, 1, "S";
          |C_M #106#19, 1, "S";
          |C_M #106#13, 1, "S";
          |VENTANA 1401, 3399, -1, 17, "No se hace nada";
     |FINSI;
     |SI enTipo = 4;
          |C_M #106#10, 1, "S";
          |C_M #106#13, 1, "S";
          |C_M #106#19, 1, "S";
          |C_M #106#16, 1, "S";
          |C_M #106#6, 1, "S";
          |C_M #106#17, 1, "S";
          |VENTANA 1401, 3399, -1, 17, "Art¡culo no apto";
     |FINSI;
     |SI enTipo = 5;               ||retimbrado de revision in situ
          |C_M #106#13, 1, "S";
          |C_M #106#16, 1, "S";
          |C_M #106#6, 1, "S";
          |C_M #106#17, 1, "S";
          |C_M #106#19, 1, "S";
          |VENTANA 1401, 3399, -1, 17, "Retimbrado";
     |FINSI;
     |SI enTipo = 6;
          |C_M #106#25, 1, "S";
          |C_M #106#10, 1, "S";
          |C_M #106#16, 1, "S";
          |C_M #106#6, 1, "S";
          |C_M #106#17, 1, "S";
          |C_M #106#19, 1, "S";
          |VENTANA 1401, 3399, -1, 17, "Revisi¢n in situ";
     |FINSI;
     |SI enTipo = 7;
          |C_M #106#16, 1, "S";
          |C_M #106#6, 1, "S";
          |C_M #106#17, 1, "S";
          |C_M #106#10, 1, "S";
          |C_M #106#19, 1, "S";
          |VENTANA 1401, 3399, -1, 17, "Recarga";
     |FINSI;
     |SI enTipo = 8;
          |C_M #106#10, 1, "S";
          |C_M #106#14, 1, "S";
          |C_M #106#16, 1, "S";
          |C_M #106#6, 1, "S";
          |C_M #106#17, 1, "S";
          |C_M #106#19, 1, "S";
          |VENTANA 1401, 3399, -1, 17, "Retimbrado";
     |FINSI;
     |HAZ PushVenModal;

     |PINPA #0#106; nPanLin = FSalida;
     |SI enTipo = 6; |O enTipo = 7; |O enTipo = 8; |O enTipo = 4;
               |O enTipo = 5;
          |CONTROL_SIMPLE nPanLin, "BOTON,{{137}}Caracteristicas", 0680, 0698, BtnCarac;
          nBtnCarac = FSalida;
     |FINSI;
     |SI enTipo = 3;
          |CONTROL_SIMPLE nPanLin, "BOTON,Actual", 1289, 1299, BtnActual;
          nBtnActual = FSalida;
          |CONTROL_SIMPLE nPanLin, "BOTON,Autom tico", 1389, 1399, BtnAuto;
          nBtnAuto = FSalida;
     |FINSI;
     nSw = 1;
     |HAZ SisteLin;
     |HAZ ArtiLin;
     |HAZ CliLin;
     |HAZ ArtFac;
     nSw = 0;
     |PINDA #0#106;
     nBloqueaLin = 1;
     nModoLin = 2;
     |ENDATOS #2#106;
     |SI FSalida = 0;
          |PARA i = 0; |SI i < aCampos; |HACIENDO i = i + 1;
               #14i = #106i;
          |SIGUE;
          |GRABA 020#14a;
     |FINSI;
     |LIBERA #14;

     IaModi = aModi1<-;
     |PARA i = 0; |SI i < aCampos; |HACIENDO i = i + 1;
          aAlfa = aModi;
          IaModi = IaModi + 1;
          |C_M #106i, 1, aAlfa;
     |SIGUE;

     |SI enTipo = 6; |O enTipo = 7; |O enTipo = 8; |O enTipo = 4;
               |O enTipo = 5;
          |FIN_CONTROL_WINDOWS nBtnCarac;
     |FINSI;
     |SI enTipo = 3;
          |FIN_CONTROL_WINDOWS nBtnActual;
          |FIN_CONTROL_WINDOWS nBtnAuto;
     |FINSI;

     |HAZ PopVenModal;
|FPRC;

|PROCESO CliComerFac;
     aCliFa = #11#39;
     aComer = #11#52;
     |ERROR10;
|FPRC;

|DEFBUCLE CliComerFac;
     #11#2;
     ;
     #110#4, 0001, "                    ", "                    ", "                    ";
     #110#4, 9999, "zzzzzzzzzzzzzzzzzzzz", "zzzzzzzzzzzzzzzzzzzz", "zzzzzzzzzzzzzzzzzzzz";
     ;
     NULL, CliComerFac;
|FIN;

|PROCESO Ficticio; |TIPO 6;
     |SI FSalida = 13;
          |ABRE #12;
          |LEE 101#12p;
          |SI FSalida ! 0; |GRABA 020#12b; |FINSI;
          nConta = #12#1;
          #12#1 = #12#1 + 1;
          |GRABA 020#12a;
          |CIERRA #12;

          aAlfa = "DESC" + #110#4 + (("0000000000" + nConta) % -110);
          #106#2 = aAlfa; |PINTA #106#2;

          aCliFa = "";
          aComer = "";
          |BUCLE CliComerFac;
          |DDEFECTO #11;
          |ABRE #11;
          #11#0 = #106#2;
          #11#1 = #110#4;
          #11#2 = #110#6;
          #11#3 = #110#7;
          #11#4 = #106#5;
          #11#5 = #106#4;
          #11#39 = aCliFa;
          #11#52 = aComer;
          |GRABA 020#11n;
          |CIERRA #11;
          eaNumSerie = #11#0;
          eaNumNuevo = #11#0;
          enModo = 2;
          eaProg = "extz0003"; |HAZ EjecutaProg;
     |FINSI;
|FPRC;

|PROCESO Chequea; |TIPO 0;
     |SI nModoLin = 1; |Y nBloqueaLin = 0;
          |ABRE #11;
          #11#0 = #106#2;
          |LEE 000#11=;
          |SI FSalida ! 0;
               |MENAV "0000No existe N§ serie art¡culo a revisar...";
               |ERROR;
          |SINO;
               aAlfa = "";
               |SI #110#4 ! #11#1; aAlfa = "0000Cliente incorrecto..."; |FINSI;
               |SI #110#6 ! #11#2; aAlfa = "0000Instalaci¢n incorrecta..."; |FINSI;
               |SI #110#7 ! #11#3; aAlfa = "0000Edificio incorrecto..."; |FINSI;
               |SI #110#8 ! #11#5; aAlfa = "0000Sistema incorrecto..."; |FINSI;
               |SI #11#46 ! 0; aAlfa = "0000N§ serie en parte:" + #11#45 + "/" + (("000000" + #11#46) % -106); |FINSI;
               |SI aAlfa ! "";
                    |MENAV aAlfa;
                    |ERROR;
               |SINO;
                    #extm0014@#0 = #106#0;
                    #extm0014@#1 = #106#1;
                    #extm0014@#2 = #106#2;
                    |GRABA 020#extm0014@.b;
                    |SI FSalida ! 0;
                         |MENAV "0000Registro existente...";
                         |ERROR;
                    |SINO;
                         #106#3 = #11#7; |PINTA #106#3;
                         #106#4 = #11#5; |PINTA #106#4;
                         #106#5 = #11#4; |PINTA #106#5;
                         #106#6 = #11#6; |PINTA #106#6;
                         #106#7 = #11#38; |PINTA #106#7;
                         #106#8 = #11#24; |PINTA #106#8;
                         #106#9 = #11#23; |PINTA #106#9;
                         #106#10 = #11#27; |PINTA #106#10;
                         #106#11 = #11#34; |PINTA #106#11;
                         #106#12 = #11#33; |PINTA #106#12;
                         #106#13 = #11#37; |PINTA #106#13;
                         nSw = 1;
                         |HAZ SisteLin;
                         |HAZ ArtiLin;
                         |HAZ CliLin;
                         |HAZ ArtFac;
                         nSw = 0;

                         |C_M #106#2, 1, "N"; |PINTA #106#2;
                         |C_M #106#4, 1, "N"; |PINTA #106#4;
                         |C_M #106#5, 1, "N"; |PINTA #106#5;
                         nBloqueaLin = 1;
                         |ESTADO_CONTROL nBtnCarac, "ENABLE";
                    |FINSI;
               |FINSI;
          |FINSI;
          |CIERRA #11;
     |FINSI;
|FPRC;

|PROCESO Min15;
     #15#0 = #110#0;
     #15#1 = #110#1;
     #15#2 = 1;
|FPRC;

|PROCESO Max15;
     #15#0 = #110#0;
     #15#1 = #110#1;
     #15#2 = 999;
|FPRC;

|PROCESO Evt15;
     |SI FSalida = 1; |O FSalida = 2;
          |LEE 000#15=;
     |FINSI;
     |SI FSalida = 4;
          |HAZ BtnModiE;
     |FINSI;
|FPRC;

|PROCESO BtnExtra0;
     #110#0 = #13#0;
     #110#1 = #13#1;
     |HAZ BtnExtra;
|FPRC;

|PROCESO BtnExtra;
     |VENTANA 1201, 3399, -1, 17, "Art¡culos a facturar extra";
     |HAZ PushVenModal;

     |CONTROL_SIMPLE 0, "BOTON,{{193}}Alta", 1302, 1312, BtnAltaE;
     nBtnAltaE = FSalida;
     |CONTROL_SIMPLE 0, "BOTON,{{197}}Modificar", 1314, 1324, BtnModiE;
     nBtnModiE = FSalida;
     |CONTROL_SIMPLE 0, "BOTON,{{199}}Baja", 1326, 1336, BtnBajaE;
     nBtnBajaE = FSalida;
     |CONTROL_SIMPLE 0, "BOTON,{{155}}Consulta", 1338, 1348, BtnConsE;
     nBtnConsE = FSalida;

     |ABRE #15;
     nRango = 4 + 8 + 16 + 32;
     |LINEAL_SIMPLE #1#15, nRango, 1502, 3198, Min15, Max15, Evt15;
     nGridArt = FSalida;
     enCtrFin = nGridArt; |HAZ EsperaFin;
     |CIERRA #15;

     |HAZ PopVenModal;
|FPRC;

|PROCESO Min16;
     #16#0 = #110#0;
     #16#1 = #110#1;
     #16#2 = 1;
|FPRC;

|PROCESO Max16;
     #16#0 = #110#0;
     #16#1 = #110#1;
     #16#2 = 999;
|FPRC;

|PROCESO Evt16;
     |SI FSalida = 1; |O FSalida = 2;
          |LEE 000#16=;
     |FINSI;
     |SI FSalida = 4;
          |HAZ BtnModiD;
     |FINSI;
|FPRC;

|PROCESO BtnDejado0;
     #110#0 = #13#0;
     #110#1 = #13#1;
     |HAZ BtnDejado;
|FPRC;

|PROCESO BtnDejado;
     |VENTANA 1201, 3399, -1, 17, "Art¡culos dejados en el cliente";
     |HAZ PushVenModal;

     |CONTROL_SIMPLE 0, "BOTON,{{193}}Alta", 1302, 1312, BtnAltaD;
     nBtnAltaD = FSalida;
     |CONTROL_SIMPLE 0, "BOTON,{{197}}Modificar", 1314, 1324, BtnModiD;
     nBtnModiD = FSalida;
     |CONTROL_SIMPLE 0, "BOTON,{{199}}Baja", 1326, 1336, BtnBajaD;
     nBtnBajaD = FSalida;
     |CONTROL_SIMPLE 0, "BOTON,{{155}}Consulta", 1338, 1348, BtnConsD;
     nBtnConsD = FSalida;

     |ABRE #16;
     nRango = 4 + 8 + 16 + 32;
     |LINEAL_SIMPLE #1#16, nRango, 1502, 3198, Min16, Max16, Evt16;
     nGridArt = FSalida;
     enCtrFin = nGridArt; |HAZ EsperaFin;
     |CIERRA #16;

     |HAZ PopVenModal;
|FPRC;

|PROCESO BtnAltaE;
     nModoArt = 1;
     |HAZ EditaE;
|FPRC;

|PROCESO BtnModiE;
     |LEE 101#15=;
     |SI FSalida = 0; |Y #15#0 = #110#0; |Y #15#1 = #110#1;
          nModoArt = 2;
          |HAZ EditaE;
     |FINSI;
     |LIBERA #15;
|FPRC;

|PROCESO BtnBajaE;
     |LEE 101#15=;
     |SI FSalida = 0; |Y #15#0 = #110#0; |Y #15#1 = #110#1;
          aAlfa = "0000N¨Borrar linea:" + #15#2;
          |QBF aAlfa;
          aAlfa = aAlfa + "?";
          |CONFI aAlfa;
          |SI FSalida = 0;
               |BORRA 020#15a;
               |REFRESCACONTROL nGridArt;
          |FINSI;
     |FINSI;
     |LIBERA #15;
|FPRC;

|PROCESO BtnConsE;
     |LEE 000#15=;
     |SI FSalida = 0; |Y #15#0 = #110#0; |Y #15#1 = #110#1;
          nModoArt = 4;
          |HAZ EditaE;
     |FINSI;
|FPRC;

|PROCESO BtnAltaD;
     nModoArt = 1;
     |HAZ EditaD;
|FPRC;

|PROCESO BtnModiD;
     |LEE 101#16=;
     |SI FSalida = 0; |Y #16#0 = #110#0; |Y #16#1 = #110#1;
          nModoArt = 2;
          |HAZ EditaD;
     |FINSI;
     |LIBERA #16;
|FPRC;

|PROCESO BtnBajaD;
     |LEE 101#16=;
     |SI FSalida = 0; |Y #16#0 = #110#0; |Y #16#1 = #110#1;
          aAlfa = "0000N¨Borrar linea:" + #16#2;
          |QBF aAlfa;
          aAlfa = aAlfa + "?";
          |CONFI aAlfa;
          |SI FSalida = 0;
               |BORRA 020#16a;
               enForma = 0; |HAZ TraspasoEnt;
               |REFRESCACONTROL nGridArt;
          |FINSI;
     |FINSI;
     |LIBERA #16;
|FPRC;

|PROCESO BtnConsD;
     |LEE 000#16=;
     |SI FSalida = 0; |Y #16#0 = #110#0; |Y #16#1 = #110#1;
          nModoArt = 4;
          |HAZ EditaD;
     |FINSI;
|FPRC;

|PROCESO Impo4; |TIPO 0;
     #104#6 = (#104#4 * #104#5) < #104#11; |PINTA #104#6;
|FPRC;

|PROCESO Art4; |TIPO 0;
     |ABRE #19;
     #19#0 = #104#3;
     |LEE 000#19=;
     |SI FSalida = 0;
          #104#12 = #19#1; |PINTA #104#12;
     |SINO;
          |SI nSw = 0;
               |MENAV "0000No existe c¢digo de art¡culo...";
               |ERROR;
          |FINSI;
     |FINSI;
     |CIERRA #19;
|FPRC;

|PROCESO EditaE;
     |SI nModoArt = 1; |VENTANA 2801, 3399, -1, 17, "Nueva l¡nea art¡culo extra"; |FINSI;
     |SI nModoArt = 2; |VENTANA 2801, 3399, -1, 17, "Modifica l¡nea art¡culo extra"; |FINSI;
     |SI nModoArt = 4; |VENTANA 2801, 3399, -1, 17, "Consulta l¡nea art¡culo extra"; |FINSI;
     |HAZ PushVenModal;

     |PINPA #0#104;
     |SI nModoArt = 1;
          #15#0 = #110#0;
          #15#1 = #110#1;
          #15#2 = 999;
          |LEE 000#15];
          |SI FSalida = 0;
               |LEE 000#15a;
          |SINO;
               |LEE 000#15u;
          |FINSI;
          |SI FSalida = 0; |Y #15#0 = #110#0; |Y #15#1 = #110#1;
               nLin = #15#2 + 1;
          |SINO;
               nLin = 1;
          |FINSI;
          #15#0 = #110#0;
          #15#1 = #110#1;
          #15#2 = nLin;
          |GRABA 020#15b;

          |DDEFECTO #104;
          #104#0 = #110#0;
          #104#1 = #110#1;
          #104#2 = nLin;
          #104#7 = #110#17;
     |SINO;
          |PARA i = 0; |SI i [ 11; |HACIENDO i = i + 1;
               #104i = #15i;
          |SIGUE;
     |FINSI;
     nSw = 1;
     |HAZ Art4;
     nSw = 0;
     |PINDA #0#104;
     |SI nModoArt = 1; |ENDATOS #1#104; |FINSI;
     |SI nModoArt = 2; |ENDATOS #2#104; |FINSI;
     |SI nModoArt = 4; |PAUSA; |FINSI;
     |SI FSalida = 0; |Y nModoArt ! 4;
          |PARA i = 0; |SI i [ 11; |HACIENDO i = i + 1;
               #15i = #104i;
          |SIGUE;
          |GRABA 020#15a;
          |REFRESCACONTROL nGridArt;
     |SINO;
          |SI nModoArt = 1;
               |BORRA 020#15c;
          |FINSI;
     |FINSI;
     |LIBERA #15;

     |HAZ PopVenModal;
|FPRC;

|PROCESO Art5; |TIPO 0;
     |ABRE #19;
     #19#0 = #105#3;
     |LEE 000#19=;
     |SI FSalida = 0;
          #105#8 = #19#1; |PINTA #105#8;
     |SINO;
          |SI nSw = 0;
               |MENAV "0000No existe c¢digo de art¡culo...";
               |ERROR;
          |FINSI;
     |FINSI;
     |CIERRA #19;
|FPRC;

|PROCESO EditaD;
     |SI nModoArt = 1; |VENTANA 2801, 3399, -1, 17, "Nueva l¡nea art¡culo dejado al cliente"; |FINSI;
     |SI nModoArt = 2; |VENTANA 2801, 3399, -1, 17, "Modifica l¡nea art¡culo dejado al cliente"; |FINSI;
     |SI nModoArt = 4; |VENTANA 2801, 3399, -1, 17, "Consulta l¡nea art¡culo dejado al cliente"; |FINSI;
     |HAZ PushVenModal;

     |PINPA #0#105;
     |SI nModoArt = 1;
          #16#0 = #110#0;
          #16#1 = #110#1;
          #16#2 = 999;
          |LEE 000#16];
          |SI FSalida = 0;
               |LEE 000#16a;
          |SINO;
               |LEE 000#16u;
          |FINSI;
          |SI FSalida = 0; |Y #16#0 = #110#0; |Y #16#1 = #110#1;
               nLin = #16#2 + 1;
          |SINO;
               nLin = 1;
          |FINSI;
          #16#0 = #110#0;
          #16#1 = #110#1;
          #16#2 = nLin;
          |GRABA 020#16b;

          |DDEFECTO #105;
          #105#0 = #110#0;
          #105#1 = #110#1;
          #105#2 = nLin;
     |SINO;
          |PARA i = 0; |SI i [ 7; |HACIENDO i = i + 1;
               #105i = #16i;
          |SIGUE;
     |FINSI;
     nSw = 1;
     |HAZ Art5;
     nSw = 0;
     |PINDA #0#105;
     |SI nModoArt = 1; |ENDATOS #1#105; |FINSI;
     |SI nModoArt = 2; |ENDATOS #2#105; |FINSI;
     |SI nModoArt = 4; |PAUSA; |FINSI;
     |SI FSalida = 0; |Y nModoArt ! 4;
          |SI nModoArt = 2;
               enForma = -1; |HAZ TraspasoEnt;
          |FINSI;
          |PARA i = 0; |SI i [ 7; |HACIENDO i = i + 1;
               #16i = #105i;
          |SIGUE;
          enForma = 1; |HAZ TraspasoEnt;
          |GRABA 020#16a;
          |REFRESCACONTROL nGridArt;
     |SINO;
          |SI nModoArt = 1;
               |BORRA 020#16c;
          |FINSI;
     |FINSI;
     |LIBERA #16;

     |HAZ PopVenModal;
|FPRC;

|PROCESO SisteLin; |TIPO 0;
     |ABRE #3;
     #3#0 = #106#4;
     |LEE 000#3=;
     |SI FSalida = 0;
          #106#27 = #3#1; |PINTA #106#27;
     |SINO;
          |SI nSw = 0;
               |MENAV "0000No existe c¢digo sistema...";
               |ERROR;
          |FINSI;
     |FINSI;
     |CIERRA #3;
|FPRC;

|PROCESO ArtiLin; |TIPO 0;
     |ABRE #19;
     #19#0 = #106#5;
     |LEE 000#19=;
     |SI FSalida = 0;
          #106#28 = #19#1; |PINTA #106#28;
     |SINO;
          |SI nSw = 0;
               |MENAV "0000No existe c¢digo art¡culo...";
               |ERROR;
          |FINSI;
     |FINSI;
     |CIERRA #19;
|FPRC;

|PROCESO CliLin; |TIPO 0;
     |ABRE #24;
     #24#0 = #106#7;
     |LEE 000#24=;
     |SI FSalida = 0;
          #106#30 = #24#1; |PINTA #106#30;
     |SINO;
          |SI nSw = 0;
               |MENAV "0000No existe c¢digo cliente...";
               |ERROR;
          |FINSI;
     |FINSI;
     |CIERRA #24;
|FPRC;

|PROCESO ArtFac; |TIPO 0;
     |ABRE #19;
     #19#0 = #106#16;
     |LEE 000#19=;
     |SI FSalida = 0;
          #106#29 = #19#1; |PINTA #106#29;
     |SINO;
          |SI nSw = 0;
               |MENAV "0000No existe c¢digo art¡culo...";
               |ERROR;
          |FINSI;
     |FINSI;
     |CIERRA #19;
|FPRC;

|PROCESO Articulo;
     |SALVA_DATOS #11;
     |REPON_DATOS #referen@;
     |GRABA 020#referen@.n;
|FPRC;

|DEFBUCLE Articulo;
     #11#2;
     46;
     #110#4, #110#6, #110#7, #110#8, "                    ", 0;
     #110#4, #110#6, #110#7, #110#8, "zzzzzzzzzzzzzzzzzzzz", 0;
     ;
     NULL, Articulo;
|FIN;

|PROCESO NumSer66; |TIPO 66;
     |DDEF aAlfa;
     aAlfa = aAlfa + "extm0011";
     |CARGA_DEF aAlfa, referen@;
     |SI FSalida = 0;
          |HAZ CreaTempo; |NOME_DAT #referen@#Tempo#; |DELINDEX #referen@;
          |ABRE #referen@;
          |BUCLE Articulo;
          |LEE 000#referen@.p;
          |SI FSalida = 0;
               |CONSULTA_CLAVE #1#referen@;
               |SI FSalida = 0;
                    #106#2 = #referen@#0; |PINTA #106#2;
               |SINO;
                    |ERROR;
               |FINSI;
          |SINO;
               |MENAV "0000No hay art¡culos para revisar...";
               |ERROR;
          |FINSI;
          |CIERRA #referen@;
          |HAZ BoraTempo; |DELINDEX #referen@;
          |DESCARGA_DEF #referen@;
     |FINSI;
|FPRC;

|PROCESO Edita;||Lineas
     |SI nModoLin = 1; |VENTANA 1401, 3399, -1, 17, "Nueva l¡nea revisi¢n"; |FINSI;
     |SI nModoLin = 2; |VENTANA 1401, 3399, -1, 17, "Modifica l¡nea revisi¢n"; |FINSI;
     |SI nModoLin = 4; |VENTANA 1501, 3399, -1, 17, "Consulta l¡nea revisi¢n"; |FINSI;
     |HAZ PushVenModal;

     |ABRE #14;
     |PINPA #0#106; nPanLin = FSalida;
     |CONTROL_SIMPLE nPanLin, "BOTON,{{137}}Caracteristicas", 0680, 0698, BtnCarac;
     nBtnCarac = FSalida;
     |SI nModoLin = 1;
          |C_M #106#2, 1, "S";
          |C_M #106#4, 1, "N";
          |C_M #106#5, 1, "S";
          |DDEFECTO #106;
          #106#0 = #110#0;
          #106#1 = #110#1;
          #106#4 = #110#8;
          |ESTADO_CONTROL nBtnCarac, "DISABLE";
     |SINO;
          |C_M #106#2, 1, "N";
          |C_M #106#4, 1, "N";
          |C_M #106#5, 1, "N";
          |PARA i = 0; |SI i [ 25; |HACIENDO i = i + 1;
               #106i = #extm0014@#i#;
          |SIGUE;
     |FINSI;
     nSw = 1;
     |HAZ SisteLin;
     |HAZ ArtiLin;
     |HAZ CliLin;
     |HAZ ArtFac;
     nSw = 0;
     |PINDA #0#106;
     nBloqueaLin = 0;
     |SI nModoLin = 1; |ENDATOS #1#106; |FINSI;
     |SI nModoLin = 2; |ENDATOS #2#106; |FINSI;
     |SI nModoLin = 4; |PAUSA; |FINSI;
     |SI FSalida = 0; |Y nModoLin ! 4;
          |PARA i = 0; |SI i [ 25; |HACIENDO i = i + 1;
               #extm0014@#i# = #106i;
          |SIGUE;
          |GRABA 020#extm0014@.a;
          |HAZ CreaCara0;
          |REFRESCACONTROL nGrid1;
     |SINO;
          |SI nBloqueaLin = 1;
               |BORRA 020#extm0014@.c;
               aSer = #extm0014@#0;
               nNum = #extm0014@#1;
               aPro = #extm0014@#2;
               |BUCLE BorraCara;
          |FINSI;
     |FINSI;
     |LIBERA #extm0014@;
     |CIERRA #14;

     |HAZ PopVenModal;
|FPRC;

|PROCESO BtnAlta;
     nModoLin = 1;
     |HAZ Edita;
|FPRC;

|PROCESO BtnModi;
     |LEE 101#extm0014@.=;
     |SI FSalida = 0;
          nModoLin = 2;
          |HAZ Edita;
          |LIBERA #extm0014@;
     |FINSI;
|FPRC;


|PROCESO BtnBaja;
     |LEE 101#extm0014@.=;
     |SI FSalida = 0;
          aAlfa = "0000N¨Borrar art¡culo con n§ serie:" + #extm0014@#2;
          |QBF aAlfa;
          aAlfa = aAlfa + "?";
          |CONFI aAlfa;
          |SI FSalida = 0;
               aSer = #extm0014@#0;
               nNum = #extm0014@#1;
               aPro = #extm0014@#2;
               |BUCLE BorraCara;
               |BORRA 020#extm0014@.a;
               |REFRESCACONTROL nGrid1;
          |FINSI;
          |LIBERA #extm0014@;
     |FINSI;
|FPRC;

|PROCESO BtnCons;
     |LEE 000#extm0014@.=;
     |SI FSalida = 0;
          nModoLin = 4;
          |HAZ Edita;
     |FINSI;
|FPRC;

|PROCESO Conta10; |TIPO 7;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 1;
          #13#0 = #110#0;
          #13#1 = 999999;
          |LEE 000#13];
          |SI FSalida = 0;
               |LEE 000#13a;
          |SINO;
               |LEE 000#13u;
          |FINSI;
          |SI FSalida = 0; |Y #13#0 = #110#0;
               nConta = #13#1 + 1;
          |SINO;
               nConta = 1;
          |FINSI;
          #110#1 = nConta;
     |FINSI;
|FPRC;

|PROCESO Cliente10; TIPO 0;
     |ABRE #24;
     #24#0 = #110#4;
     |LEE 000#24=;
     |SI FSalida = 0;
          #110#5 = #24#2; |PINTA #110#5;
          |SI #24#111 = "S";
               |MENAV "0000Client bloqueado...";
               |ERROR;
          |FINSI;
     |SINO;
          |MENAV "0000No existe c¢digo cliente...";
          |ERROR;
     |FINSI;
     |CIERRA #24;
|FPRC;

|PROCESO Sistema10; |TIPO 0;
     |ABRE #3;
     #3#0 = #110#8;
     |LEE 000#3=;
     |SI FSalida = 0;
          #110#28 = #3#1; |PINTA #110#28;
     |SINO;
          |MENAV "0000No existe c¢digo sistema...";
          |ERROR;
     |FINSI;
     |CIERRA #3;
|FPRC;

|PROCESO Tecnico10; |TIPO 0;
     |ABRE #17;
     #17#0 = #110#10;
     |LEE 000#17=;
     |SI FSalida = 0;
          #110#29 = #17#1; |PINTA #110#29;
     |SINO;
          |MENAV "0000No existe c¢digo t‚cnico...";
          |ERROR;
     |FINSI;
     |CIERRA #17;
|FPRC;

|PROCESO Admin10; |TIPO 0;
     |ABRE #24;
     #24#0 = #110#11;
     |LEE 000#24=;
     |SI FSalida = 0;
          #110#30 = #24#2; |PINTA #110#30;
     |SINO;
          |SI nSw = 0;
               |MENAV "0000No existe c¢digo administrador...";
               |ERROR;
          |FINSI;
     |FINSI;
     |CIERRA #24;
|FPRC;

|PROCESO Conte10; |TIPO 7;
     aConte = #110Campo;
|FPRC;

|PROCESO Dir10_0; |TIPO 0;
     nModo = (FEntrada / 100) ! 0;
     |ABRE #30;
     #30#0 = #110#4;
     #30#1 = #110#6;
     |LEE 000#30=;
     |SI FSalida = 0;
          |SI nModo = 1; |O aConte ! #110Campo;
               |ABRE #2;
               #2#0 = #30#0;
               #2#1 = #30#1;
               |LEE 000#2=;
               |SI FSalida ! 0; |DDEFECTO #2; |FINSI;
               |CIERRA #2;
               #110#11 = #2#2; |PINTA #110#11;
               nSw = 1;
               |HAZ Admin10;
               nSw = 0;
               #110#18 = #2#4; |PINTA #110#18;
               #110#19 = #2#5;
               #110#20 = #2#6;
               #110#21 = #2#7;
               #110#22 = #2#8;
          |FINSI;
     |SINO;
          |MENAV "0000No existe direcci¢n de entrega...";
          |ERROR;
     |FINSI;
     |CIERRA #30;
|FPRC;

|PROCESO Min_30;
     #30#0 = #110#4;
     #30#1 = 1;
|FPRC;

|PROCESO Max_30;
     #30#0 = #110#4;
     #30#1 = 9999;
|FPRC;

|PROCESO Dir10_66; |TIPO 66;
     |ABRE #30;
     #30#0 = #110#4;
     #30#1 = #110#6;
     |LEE 000#30];
     |CONSULTA_F_CLAVE #1#30, Min_30, Max_30;
     |SI FSalida = 0;
          #110#6 = #30#1; |PINTA #110#6;
     |SINO;
          |ERROR;
     |FINSI;
     |CIERRA #30;
|FPRC;

|PROCESO Min_4;
     #4#0 = #110#4;
     #4#1 = #110#6;
     #4#2 = "                    ";
|FPRC;

|PROCESO Max_4;
     #4#0 = #110#4;
     #4#1 = #110#6;
     #4#2 = "zzzzzzzzzzzzzzzzzzzz";
|FPRC;

|PROCESO Edificio10_66; |TIPO 66;
     |ABRE #4;
     #4#0 = #110#4;
     #4#1 = #110#6;
     #4#2 = #110#7;
     |LEE 000#4];
     |CONSULTA_F_CLAVE #1#4, Min_4, Max_4;
     |SI FSalida = 0;
          #110#7 = #4#2; |PINTA #110#7;
     |SINO;
          |ERROR;
     |FINSI;
     |CIERRA #4;
|FPRC;

|PROCESO Chequea10; |TIPO 0;
     |SI nBloquea = 0;
          #13#0 = #110#0;
          #13#1 = #110#1;
          |GRABA 000#13b;
          |SI FSalida = 0;
               nBloquea = 1;
               |C_M #110#0, 1, "N"; |PINTA #110#0;
               |C_M #110#1, 1, "N"; |PINTA #110#1;
               aEstado = "ENABLE"; |HAZ EstadoBtn;
          |SINO;
               |MENAV "0000Registro existente....";
               |ERROR;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO EstadoBtn;
     |ESTADO_CONTROL nBtnAlta, aEstado;
     |ESTADO_CONTROL nBtnModi, aEstado;
     |ESTADO_CONTROL nBtnBaja, aEstado;
     |ESTADO_CONTROL nBtnCons, aEstado;

     |ESTADO_CONTROL nBtnExtra, aEstado;
     |ESTADO_CONTROL nBtnDejado, aEstado;
|FPRC;

|PROCESO Borra15;
     |BORRA 020#15a;
|FPRC;

|DEFBUCLE Borra15;
     #15#1;
     ;
     #13#0, #13#1, 001;
     #13#0, #13#1, 999;
     be;
     NULL, Borra15;
|FIN;

|PROCESO Borra16;
     |BORRA 020#16a;
     enForma = 0; |HAZ TraspasoEnt;
|FPRC;

|DEFBUCLE Borra16;
     #16#1;
     ;
     #13#0, #13#1, 001;
     #13#0, #13#1, 999;
     be;
     NULL, Borra16;
|FIN;

|PROCESO BorraLin;
     |BORRA 020#14a;
     |HAZ BorraCara0;
|FPRC;

|DEFBUCLE BorraLin;
     #14#1;
     ;
     #13#0, #13#1, "                    ";
     #13#0, #13#1, "zzzzzzzzzzzzzzzzzzzz";
     be;
     NULL, BorraLin;
|FIN;

|PROCESO BorraLin0;
     |BUCLE BorraLin;
     |BUCLE Borra15;
     |BUCLE Borra16;
|FPRC;

|PROCESO CopiaLin;
     |SALVA_DATOS #extm0014@;
     |REPON_DATOS #14;
     |GRABA 020#14n;

     #11#0 = #14#2;
     |LEE 121#11=;
     |SI FSalida = 0;
          #11#45 = #13#0;
          #11#46 = #13#1;
          |GRABA 020#11a;
          |LIBERA #11;
     |FINSI;
|FPRC;

|DEFBUCLE CopiaLin;
     #extm0014@#1003;
     ;
     "     ", 000001, "                    ";
     "zzzzz", 999999, "zzzzzzzzzzzzzzzzzzzz";
     ;
     NULL, CopiaLin;
|FIN;

|PROCESO BtnAgrupa;
     |VENTANA 1312, 1887, -1, 17, "Art¡culo agrupaci¢n l¡neas";
     |HAZ PushVenModal;

     |SI enSwAgrupa = 1;
          #300#0 = #13#41;
          #300#1 = #13#42;
     |SINO;
          #300#0 = #110#41;
          #300#1 = #110#42;
     |FINSI;
     |PINPA #0#300;
     |PINDA #0#300;
     |C_M #300#1, 1, "S";
     |ENDATOS #1#300;
     |SI FSalida = 0;
          aAlfa = #300#0; |QBF aAlfa;
          |SI enSwAgrupa = 1;
               |SI aAlfa = "";
                    #13#40 = "N";
                    #13#41 = "";
                    #13#42 = "";
               |SINO;
                    #13#40 = "S";
                    #13#41 = #300#0;
                    #13#42 = #300#1;
               |FINSI;
               |GRABA 020#13a;
          |SINO;
               |SI aAlfa = "";
                    #110#40 = "N"; |PINTA #110#40;
                    #110#41 = ""; |PINTA #110#41;
                    #110#42 = ""; |PINTA #110#42;
               |SINO;
                    #110#40 = "S"; |PINTA #110#40;
                    #110#41 = #300#0; |PINTA #110#41;
                    #110#42 = #300#1; |PINTA #110#42;
               |FINSI;
          |FINSI;
     |FINSI;

     |HAZ PopVenModal;
|FPRC;

|PROCESO NuevoParte;
     |VENTANA 0501, 3399, -1, 17, "Nuevo parte revisi¢n";
     |HAZ PushVenModal;

     |DDEF aAlfa;
     aAlfa = aAlfa + "extm0014";
     |CARGA_DEF aAlfa, extm0014@;
     |HAZ CreaTempo; |NOME_DAT #extm0014@#Tempo#; aTmp = Tempo;
     |DELINDEX #extm0014@; |ABRE #extm0014@;

     |ABRE #3;
     #3#0 = #0#2;
     |LEE 000#3=;
     |SI FSalida ! 0; |DDEFECTO #3; |FINSI;
     |CIERRA #3;

     |ABRE #13;
     |DDEFECTO #110;
     |SI eaCliInci = "";
          #110#4 = #0#0;
     |SINO;
          #110#4 = eaCliInci;
     |FINSI;
     |ABRE #24;
     #24#0 = #110#4;
     |LEE 000#24=;
     |SI FSalida ! 0; |DDEFECTO #24; |FINSI;
     |CIERRA #24;
     #110#5 = #24#2;
     |SI #0#1 = 0;
          #110#6 = 1;
     |SINO;
          #110#6 = #0#1;
     |FINSI;
     #110#8 = #0#2;
     #110#28 = #3#1;
     |PINPA #0#110; nPanParte = FSalida;
     |PINDA #0#110;

     |CONTROL_SIMPLE nPanParte, "BOTON,...", 1757, 1760, BtnAgrupa;
     |CONTROL_SIMPLE nPanParte, "BOTON,{{193}}Alta", 2502, 2512, BtnAlta;
     nBtnAlta = FSalida;
     |CONTROL_SIMPLE nPanParte, "BOTON,{{197}}Modificar", 2514, 2524, BtnModi;
     nBtnModi = FSalida;
     |CONTROL_SIMPLE nPanParte, "BOTON,{{199}}Baja", 2526, 2536, BtnBaja;
     nBtnBaja = FSalida;
     |CONTROL_SIMPLE nPanParte, "BOTON,{{155}}Consulta", 2538, 2548, BtnCons;
     nBtnCons = FSalida;

     |CONTROL_SIMPLE nPanParte, "BOTON,{{137}}Material Dejado", 0656, 0676, BtnDejado;
     nBtnExtra = FSalida;
     |CONTROL_SIMPLE nPanParte, "BOTON,{{137}}Material Extra", 0678, 0698, BtnExtra;
     nBtnDejado = FSalida;

     aEstado = "DISABLE"; |HAZ EstadoBtn;

     nRango = 4 + 8 + 16 + 32;
     |LINEAL_SIMPLE #1#extm0014@, nRango, 2602, 3298, NULL, NULL, NULL;
     nGrid1 = FSalida;

     aAlfa = "|NC"; aCampos = #13aAlfa;

     nBloquea = 0;
     |C_M #110#0, 1, "S";
     |C_M #110#1, 1, "S";
     |ENDATOS #1#110;
     |SI FSalida = 0;
          |PARA i = 0; |SI i < aCampos; |HACIENDO i = i + 1;
               #13i = #110i;
          |SIGUE;
          |GRABA 020#13a;
          |BUCLE BorraLin;
          |ABRE #14;
          |ABRE #11;
          |BUCLE CopiaLin;
          |CIERRA #11;
          |CIERRA #14;
          enError = 0;
          eaSerParte = #13#0;
          enNumParte = #13#1;
     |SINO;
          enError = 1;
          |SI nBloquea = 1;
               |BORRA 020#13c;
          |FINSI;
          |BUCLE Borra15;
          |BUCLE Borra16;
     |FINSI;
     |CIERRA #13;


     |DESTRUYECONTROL nGrid1;
     |FIN_CONTROL_WINDOWS nBtnAlta;
     |FIN_CONTROL_WINDOWS nBtnModi;
     |FIN_CONTROL_WINDOWS nBtnBaja;
     |FIN_CONTROL_WINDOWS nBtnCons;
     |FIN_CONTROL_WINDOWS nBtnDejado;
     |FIN_CONTROL_WINDOWS nBtnExtra;

     |HAZ PopVenModal;

     |CIERRA #extm0014@; Tempo = aTmp; |HAZ BoraTempo; |DELINDEX #extm0014@;
     |DESCARGA_DEF #extm0014@;
|FPRC;

|PROCESO Min14;
     #extm0014@#0 = #13#0;
     #extm0014@#1 = #13#1;
     #extm0014@#2 = "                    ";
|FPRC;

|PROCESO Max14;
     #extm0014@#0 = #13#0;
     #extm0014@#1 = #13#1;
     #extm0014@#2 = "zzzzzzzzzzzzzzzzzzzz";
|FPRC;

|PROCESO ModiParte;
     |VENTANA 0501, 3399, -1, 17, "Modificaci¢n parte revisi¢n";
     |HAZ PushVenModal;

     |DDEF aAlfa;
     aAlfa = aAlfa + "extm0014";
     |CARGA_DEF aAlfa, extm0014@;
     |ABRE #extm0014@;

     aAlfa = "|NC"; aCampos = #13aAlfa;

     IaModi = aModi1<-;
     |PARA i = 0; |SI i < aCampos; |HACIENDO i = i + 1;
          #110i = #13i;

          |C_M #110i, 0, aAlfa;
          aModi = aAlfa;
          IaModi = IaModi + 1;

          |C_M #110i, 1, "N";
     |SIGUE;
     |C_M #110#3, 1, "S";
     |C_M #110#10, 1, "S";
     |C_M #110#12, 1, "S";
     |C_M #110#11, 1, "S";
     |C_M #110#23, 1, "S";
     |C_M #110#33, 1, "S";
     |C_M #110#34, 1, "S";
     |C_M #110#38, 1, "S";
     |C_M #110#39, 1, "S";

     |PINPA #0#110; nPanParte = FSalida;
     |PINDA #0#110;

     |CONTROL_SIMPLE nPanParte, "BOTON,...", 1757, 1760, BtnAgrupa;
     |CONTROL_SIMPLE nPanParte, "BOTON,{{193}}Alta", 2502, 2512, BtnAlta;
     nBtnAlta = FSalida;
     |CONTROL_SIMPLE nPanParte, "BOTON,{{197}}Modificar", 2514, 2524, BtnModi;
     nBtnModi = FSalida;
     |CONTROL_SIMPLE nPanParte, "BOTON,{{199}}Baja", 2526, 2536, BtnBaja;
     nBtnBaja = FSalida;
     |CONTROL_SIMPLE nPanParte, "BOTON,{{155}}Consulta", 2538, 2548, BtnCons;
     nBtnCons = FSalida;

     |CONTROL_SIMPLE nPanParte, "BOTON,{{137}}Articulos Extras", 0656, 0676, BtnExtra;
     nBtnDejado = FSalida;
     |CONTROL_SIMPLE nPanParte, "BOTON,{{137}}Articulos Dejados", 0678, 0698, BtnDejado;
     nBtnExtra = FSalida;

     |ESTADO_CONTROL nBtnModi, "DISABLE";
     |ESTADO_CONTROL nBtnBaja, "DISABLE";

     nRango = 4 + 8 + 16 + 32;
     |LINEAL_SIMPLE #1#extm0014@, nRango, 2602, 3298, Min14, Max14, NULL;
     nGrid1 = FSalida;

     nBloquea = 1;
     |ENDATOS #2#110;
     |SI FSalida = 0;
          |PARA i = 0; |SI i < aCampos; |HACIENDO i = i + 1;
               #13i = #110i;
          |SIGUE;
          |GRABA 020#13a;
          enError = 0;
     |SINO;
          enError = 1;
     |FINSI;

     IaModi = aModi1<-;
     |PARA i = 0; |SI i < aCampos; |HACIENDO i = i + 1;
          aAlfa = aModi;
          IaModi = IaModi + 1;
          |C_M #110i, 1, aAlfa;
     |SIGUE;

     |DESTRUYECONTROL nGrid1;
     |FIN_CONTROL_WINDOWS nBtnAlta;
     |FIN_CONTROL_WINDOWS nBtnModi;
     |FIN_CONTROL_WINDOWS nBtnBaja;
     |FIN_CONTROL_WINDOWS nBtnCons;
     |FIN_CONTROL_WINDOWS nBtnDejado;
     |FIN_CONTROL_WINDOWS nBtnExtra;

     |HAZ PopVenModal;

     |CIERRA #extm0014@;
     |DESCARGA_DEF #extm0014@;
|FPRC;
