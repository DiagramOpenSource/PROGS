|FICHEROS;
     abyz0002 #0;
     abym0001 #1,MANTE;
     abym0002 #2,MANTE;
     abym0003 #3;
     abyw0002 #10,MANTE; || Tmp
     gotrabaj #11;
     agifa024 #24;
|FIN;

|VARIABLES;
     &Tempo = "";
     aAlfa = "";
     nCampos = 0;
     i = 0;
     nContaClave = 0;
     nClave = 0;
     nHay = 0;
     nModo = 0;
     nForma = 0;
     aTmp10 = "";
     nSwClave = 0;
|FIN;

|PROCESO Planing;
     |DDEFECTO #24;
     #24#0 = #2#4;
     |LEE 000#24=;

     |DDEFECTO #11;
     #11#0 = #2#12;
     |LEE 000#11=;

     #10#0 = #2#0;
     #10#1 = #2#1;
     #10#2 = #2#2;
     #10#3 = #2#3;
     #10#4 = #2#4;
     #10#5 = #2#19;
     #10#6 = #2#6;
     #10#7 = #2#7;
     #10#8 = #2#18;
     #10#9 = #24#1;
     #10#10 = #11#0;
     #10#11 = #11#1;
     |GRABA 020#10n;
     nHay = 1;
|FPRC;

|DEFBUCLE Planing;
     #2#3;
     2, 18, 0, 1;
     #0#0, #0#2, "R", "P", #0#4, #0#6;
     #0#1, #0#3, "R", "P", #0#5, #0#7;
     ;
     NULL, Planing;
|FIN;

|PROCESO Min10;
     #10#0 = "     ";
     #10#1 = 1;
     #10#2 = " ";
     #10#3 = 1;

     #10#5 = "01.01.0000";
     #10#10 = 00001;
|FPRC;

|PROCESO Max10;
     #10#0 = "zzzzz";
     #10#1 = 999999;
     #10#2 = "z";
     #10#3 = 9999;

     #10#5 = "31.12.9999";
     #10#10 = 99999;
|FPRC;

|PROCESO Min3;
     #3#0 = #2#0;
     #3#1 = #2#1;
     #3#2 = #2#2;
     #3#3 = #2#3;
     #3#4 = 1;
|FPRC;

|PROCESO Max3;
     #3#0 = #2#0;
     #3#1 = #2#1;
     #3#2 = #2#2;
     #3#3 = #2#3;
     #3#4 = 999;
|FPRC;

|PROCESO PinParte;
     |PUSHV 0501 2380;
     |PINPA #0#2;
     |PINDA #0#2;
     |ENTLINEAL #1#3, 3, 4, 22, 0, Min3, Max3;
     |POPV;
|FPRC;

|PROCESO Modifica; |TIPO 0;
     |C_M #10Campo, 0, aAlfa;
     |SI #10Campo = Contenido; |Y FSalida = 0; |Y aAlfa = "S";
          |ABRE #2;
          #2#0 = #10#0;
          #2#1 = #10#1;
          #2#2 = #10#2;
          #2#3 = #10#3;
          |LEE 121#2=;
          |SI FSalida = 0;
               |SI #2#18 = "P";
                    |PUSHV 0501 2380;
                    |PINPA #0#2;
                    |PINDA #0#2;
                    |ENDATOS #2#2;
                    |SI FSalida = 0;
                         |GRABA 020#2a;

                         || para refrescar el lineal
                         |SI #2#19 ! #10#5; |O #2#12 ! #10#10;
                              |SI nContaClave ! 0;
                                   |PTEC 807;
                                   nSwClave = 1;
                                   nContaClave = nContaClave - 1;
                              |FINSI;
                         |FINSI;

                         |ABRE #24;
                         #24#0 = #2#4;
                         |LEE 000#24=;
                         |CIERRA #24;
                         |ABRE #11;
                         #11#0 = #2#12;
                         |LEE 000#11=;
                         |CIERRA #11;

                         #10#4 = #2#4;
                         #10#5 = #2#19;
                         #10#6 = #2#6;
                         #10#7 = #2#7;
                         #10#8 = #2#18;
                         #10#9 = #24#1;
                         #10#10 = #11#0;
                         #10#11 = #11#1;

                         |GRABA 020#10a;
                    |FINSI;
                    |POPV;
               |SINO;
                    aAlfa = "";
                    |SI #2#18 = "R"; aAlfa = "Revisado"; |FINSI;
                    |SI #2#18 = "V"; aAlfa = "Visado"; |FINSI;
                    |SI #2#18 = "F"; aAlfa = "Faturado"; |FINSI;
                    aAlfa = "0000Parte en estado ("+#2#18+") " + aAlfa;
                    |MENAV aAlfa;
                    |HAZ PinParte;
                    |ERROR; |ACABA;
               |FINSI;
          |FINSI;
          |CIERRA #2;
     |FINSI;
|FPRC;

|PROCESO CamClave; |TIPO 11;
     |SI FSalida = 13;   ||F5
          |ERROR;
          |PTEC 807;
          nSwClave = 1;
     |FINSI;
     |SI FSalida = 14;   ||F6
          |PUSHV 0501 2380;
          |ABRE #1;
          #1#0 = #10#0;
          #1#1 = #10#1;
          |LEE 020#1=;
          |SI FSalida = 0;
               |PINPA #0#1;
               |PINDA #0#1;
               |PAUSA;
          |FINSI;
          |POPV;
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO CampMod;
     aAlfa = "|NC";
     nCampos = (A\(#2#aAlfa#));
     |PARA i = 0; |SI i < nCampos; |HACIENDO i =  i + 1;
          |C_M #2i, 1, "N";
     |SIGUE;
     |C_M #2#19, 1, "S";
     |C_M #2#12, 1, "S";
|FPRC;

|PROCESO Tipo3; |TIPO 3;
     |HAZ CreaTempo; aTmp10 = Tempo;
     |NOME_DAT #10 aTmp10; |DELINDEX #10;

     |ABRE #10;
     |ABRE #24;
     |ABRE #11;
     |BUCLE Planing;
     |CIERRA #11;
     |CIERRA #24;
     |CIERRA #10;
     |SI nHay = 0;
          |MENAV "0000Seleccion Vacia...";
     |SINO;
          |HAZ CampMod;
          nContaClave = 0;
          |PARA nSwClave = 1; |SI nSwClave = 1; |HACIENDO;
               nSwClave = 0;
               |PINPA #0#10;
               |SI nContaClave = 0;
                    |ATRI S; |PINTA 702, "Serie³Numero"; |ATRI 0;
                    |C_M #10#0, 1, "S";
                    |ENTLINEAL #1#10, 1, 4, 22, 0, Min10, Max10;
                    |C_M #10#0, 1, "N";
               |SINO;
                    |SI nContaClave = 1;
                         |ATRI S; |PINTA 758, "  Visita  "; |ATRI 0;
                         |C_M #10#5, 1, "S";
                         |ENTLINEAL #2#10, 1, 4, 22, 0, Min10, Max10;
                         |C_M #10#5, 1, "N";
                    |SINO;
                         |ATRI S; |PINTA 722, "Trabajador                         "; |ATRI 0;
                         |C_M #10#10, 1, "S";
                         |ENTLINEAL #3#10, 1, 4, 22, 0, Min10, Max10;
                         |C_M #10#10, 1, "N";
                    |FINSI;
               |FINSI;
               nContaClave = (nContaClave + 1) $ 3;
          |SIGUE;
     |FINSI;

     |DELINDEX #10; Tempo = aTmp10; |HAZ BoraTempo;

|FPRC;
