|FICHEROS;
     tagz0013 #0;
     tagw0029 #29,MANTE;
|FIN;

|VARIABLES;
     aRuta = "";
     nBoton1 = 0;
     aFic = "";
     aOrg = "";
     aDes = "";
     aAlfa = "";
     aPath = "";
     aNomFic = "";
     aLin = "";
     i = 0;
     nPos = 0;
     aLon = "";
     aMarca = "";
     aCpFtp = "";
     nPrime = 0;
|FIN;

|PROCESO Copia;
     |DBASE aAlfa;
     aCpFtp = aAlfa + "000654/cpftp_mecafusta.sh"

     |DFICE aDes;
     aDes = aDes + aNomFic;
     |RECIBE_FICHERO aOrg, aDes;
     |SI FSalida = 0;
          aAlfa = aCpFtp + " " + aDes;
          |SYSTEM aAlfa;
          |FBORRA aDes;

          ||aAlfa = "0000" + aNomFic; |MENAV aAlfa;
     |SINO;
          aAlfa = "0000Error al copiar:" + aNomFic;
          |MENAV aAlfa;
     |FINSI;
|FPRC;

|PROCESO Exporta;
     |SI nPrime = 0;
          nPrime = 1;
          |CONFI "0000SExportar imagenes?";
          |SI FSalida ! 0;
               |ERROR10;
               |ACABA;
          |FINSI;
     |FINSI;
     aNomFic = #29#0; |QBF aNomFic;
     aOrg = aNomFic;
     aOrg = aPath + aOrg;
     |HAZ Copia
|FPRC;

|DEFBUCLE Exporta;
     #29#2;
     ;
     "*";
     "*";
     ;
     NULL, Exporta;
|FIN;

|PROCESO SacaFic;
     aNomFic = "";
     aLon = aFic % A0;
     |PARA i = aLon; |SI i > 0; |HACIENDO i = i - 1;
          nPos = i * 100 + 1;
          aAlfa = aFic % nPos;
          |SI aAlfa = "/"; |O aAlfa = "\"; |SAL; |FINSI;
          aNomFic = aAlfa + aNomFic;
     |SIGUE;
|FPRC;

|PROCESO MarcaTodo;
     #29#1 = aMarca;
     |GRABA 020#29a;
     |LIBERA #29;
|FPRC;

|DEFBUCLE MarcaTodo;
     #29#1;
     ;
     INICIO;
     FINAL;
     be;
     NULL, MarcaTodo;
|FIN;

|PROCESO Marca0; |TIPO 0;
     |SI FSalida = 0; |Y #29Campo = Contenido;
          |SI #29#1 = " ";
               #29#1 = "*";
          |SINO;
               #29#1 = " ";
          |FINSI;
          |GRABA 020#29a;
          |PINTA #29#1;
     |FINSI;
|FPRC;

|PROCESO Marca11; |TIPO 11;
     |SI FSalida = 13;
          aMarca = "*";
          |PTEC 807;
     |FINSI;
     |SI FSalida = 14;
          aMarca = " ";
          |PTEC 807;
     |FINSI;
|FPRC;

|PROCESO Min;
     #29#0 = " " * 40;
|FPRC;

|PROCESO Max;
     #29#0 = "z" * 40;
|FPRC;

|PROCESO Tipo3; |TIPO 3;
     aPath = #0#1; |QBF aPath;
     aAlfa = aPath % -101;

     |SI aAlfa ! "/"; |Y aAlfa ! "\";
          aPath = aPath + "\";
     |FINSI;

     aAlfa = Usuario;
     |NOME_DAT #29 aAlfa; |DELINDEX #29;


     |ABRE #29;
     aFic = aPath + "*.*";
     |R_PDIR aFic;
     |PARA; |SI FSalida = 0; |HACIENDO;
          |HAZ SacaFic;
          #29#0 = aNomFic;
          |GRABA 020#29n;
          |R_SDIR aFic;
     |SIGUE;
     |LEE 000#29p;
     |SI FSalida = 0;
          |PINPA #0#29;
          |PARA aMarca = " ";; |SI aMarca ! ""; |HACIENDO;
               aMarca = "";
               |ENTLINEAL #1#29, 1, 4, 22, 0, Min, Max;
               |SI aMarca ! "";
                    |BUCLE MarcaTodo;
               |FINSI;
          |SIGUE;
          |BUCLE Exporta;
     |SINO;
          |MENAV "0000No hay ficheros en el directorio...";
     |FINSI;
     |CIERRA #29;
     |DELINDEX #29;
|FPRC;

|PROCESO Browser; |TIPO 0;
     |SI FSalida ! 10; |ACABA; |FINSI;

     aRuta = "D*.*";

     |BROWSE aRuta;

     aRuta = aRuta % 2; |QBF aRuta;
     |SI aRuta = "D"; aRuta = ""; |FINSI;

     |QBF aRuta;
     |SI aRuta = "";
          |ERROR;
     |SINO;
          #0#1 = aRuta;
          |PINTA #0#1;
     |FINSI;
|FPRC;

|PROCESO Boton1;
     |PTEC 824;
|FPRC;

|PROGRAMA;
     |CLS;
     |CONTROL_SIMPLE 0, "BOTON,  Carpeta ", 819, 827, Boton1;
     nBoton1 = FSalida;
     |ABRE #0;
     |LEE 101#0p;
     |SI FSalida ! 0; |GRABA 020#0b; |FINSI;
     |PINPA #0#0;
     |PINDA #0#0;
     |ENDATOS #1#0;
     |SI FSalida = 0;
          |GRABA 020#0a;
     |FINSI;
     |CIERRA #0;
     |FIN_CONTROL_WINDOWS nBoton1;
     |PULSATECLA;
|FPRO;
