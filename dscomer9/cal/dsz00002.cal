
             CUALQUIER COSA QUE TOQUES DE LOS CUATRO PRC QUE TIENE
             ESTE TAB PUEDE REPERCUTIR EN TODA LA APLICACION DEBES
             TENER MUY CLARO COMO FUNCIONA ESTE PROGRAM A.


  eaTipos :

             "PV" = Pedidos de Ventas             Pide Unidades y Precio

             "DP" = Depositos                      "      "     "    "

             "AV" = Albaranes Ventas               "      "     "    "

             "FC" = Facturas Contados              "      "     "    "

             "FP" = Facturas Proformas             "      "     "    "

             "PC" = Pedidos de Compras             "      "     "    "

             "AC" = Albaranes Compras              "      "     "    "

             "W1" = Entrada Valorada de Stock      "      "     "    "
                                                   y no actualiza mientras
                                                   introduces unidades.

             "W2" = Entrada Pre_pedidos Compras    "      "     "    "

             "W3" = Asig. Alb. Compras por Articulo      "     "

             "T1" = Devoluciones Depositos        Solo Pide Unidades

             "T2" = Entrada inicial de Articulos.  "     "     "

             "T3" = Regularizacion Exitencias.     "     "     "

             "T4" = Movimientos entra almacenes    "     "     "

             "T5/T55" = Ordenes Fabricacion        "     "     "

             "T6/T66" = Entrada Fabricacion            "     "     "

  aParametro :
             "RESE"  =  Pedido de Ventas en Reserva

             "BAJA"  =  Hace un Bucle al fichero y da de baja todas las
                        fichas seleccionadas.

             "PVC"   =  Actualiza en los Articulos Componentes los Pedidos
                        Ventas.

             "DPC"   =  Actualiza en los Articulos Componentes los Depositos

             "AVC"   =  Actualiza en los Articulos Componentes los Albaranes
                        Ventas

             "FCC"   =  Actualiza en los Articulos Componentes las Facturas
                        Contado

             "PCC"   =  Actualiza en los Articulos Componentes los Pedidos
                        Compras.

             "ACC"   =  Actualiza en los Articulos Componentes los Albaranes
                        Compras

             "EVS"   =  Actualiza en los Articulos Componentes las Entradas
                        Valoradas de Stocks

             "ERE"   =  Actualiza en los Articulos Componentes las Regularizaciones
                        de Existencias

             "CONSU"   = Consulta entrada

|FICHEROS;
     dsz00002 #0;
     agifa007 #7;
     agifa074 #74;
     dsw00103 #103;
     agifa142 #142;
     dsm00005 #905;
     dsm00010 #910;
     referen@;
|FIN;

|VARIABLES;
     &eaCtrStock = "";
     &eaEstado = "";       || Usado en Modulo 001133, defelz05
     &eaFicVertical = "";  || Usado en Entrada producto terminado agifa189
     &Tempo = "";
     nSwAgrupa = 0;
     nDefAlma   = 0;
     nAlmaVen   = 0;
     nAlmaCom   = 0;
     nNume      = 0;
     aAlfa      = "";
     aParametro = "";
     nLinV = 0;
     nSw = 0;

     &nDeci_Base = 0;
     &nDeci_PreBase = 0;
     &nDeci_PreDivP = 0 ;
     &nDeci_Div = 0;
     &nDeci_DivP = 0;
     &nDeci_DivD = 0;
     &nDeci_PreDivD = 0 ;
     &nDeci_PreDiv = 0 ;

     &nDeci_TBas = 0;
     &nDeci_TDiv = 0;
     &nDeci_PBas = 0;
     &nDeci_PDiv = 0;
     &nDeci_VTot = 0;
     &enComision = 0;
|FIN;

|INCLUYE i_varart;

|| ************************************************************************
                      PROCESOS ENTRADA DE LAS COLUMNAS
|| ************************************************************************
|PROCESO TallaFabEnt;
     |DDEFECTO #103;
     #103#0 = #905#4;
     |LEE 000#103=;

     |DDEFECTO #referen@;
     #referen@#0 = "T6";
     #referen@#1 = #905#1;
     #referen@#2 = #905#2;
     #referen@#3 = enLinea;
     #referen@#4 = #905#4;
     |LEE 020#referen@.=;
     |SI FSalida ! 0; |GRABA 020#referen@.b; |FINSI;
     #referen@#11 = #103#1;
     #referen@#12 = #103#2;
     #referen@#6 = #referen@#11 - #referen@#12;
     |GRABA 020#referen@.a;
     |LIBERA #referen@;
|FPRC;

|DEFBUCLE TallaFabEnt;
     #905#1;
     16;
     "T6", #74#9, #74#0, 00000, "      ", #0#3;
     "T6", #74#9, #74#0, 99999, "zzzzzz", #0#3;
     ;
     NULL, TallaFabEnt;
|FIN;

|PROCESO TallaAFab;
     |DDEFECTO #103;
     #103#0 = #referen@#4;
     |LEE 101#103=;
     |SI FSalida ! 0; |GRABA 020#103b; |FINSI;
     #103#1 = #103#1 + #referen@#6;
     #103#2 = #103#2 + #referen@#12;
     |GRABA 020#103a;
     |LIBERA #103;

     |DDEFECTO #905;
     #905#0 = "T6";
     #905#1 = #referen@#1;
     #905#2 = #referen@#2;
     #905#3 = enLinea;
     #905#4 = #referen@#4;
     |LEE 000#905=;
     |SI FSalida ! 0; |GRABA 020#905b; |FINSI;
     #905#5 = #referen@#5;
     ||#905#6 =
     #905#7 = #referen@#7;
     #905#8 = #referen@#8;
     #905#9 = #referen@#9;
     #905#10 = #referen@#10;
     #905#11 = 0;
     #905#12 = 0;
     #905#13 = #referen@#13;
     #905#14 = #referen@#14;
     #905#15 = #referen@#15;
     #905#16 = #referen@#16;
     |GRABA 020#905a;
     |LIBERA #905;
|FPRC;

|DEFBUCLE TallaAFab;
     #referen@#1005;
     16;
     "T5", #74#9, #74#0, nLinV, "      ", #0#3;
     "T5", #74#9, #74#0, nLinV, "zzzzzz", #0#3;
     ;
     NULL, TallaAFab;
|FIN;

|PROCESO agifa074;
     nLinV = #74#1;
     |SI nSw = 1;
          |BUCLE TallaAFab;
     |SINO;
          |BUCLE TallaFabEnt;
     |FINSI;
|FPRC;

|DEFBUCLE agifa074;
     #74#1;
     2;
     eaSerie, enCodigo, 000, #0#3;
     eaSerie, enCodigo, 999, #0#3;
     ;
     NULL, agifa074;
|FIN;

|PROCESO TallaFab0;
     |SALVA_DATOS #referen@;
     |REPON_DATOS #905;
     #905#0 = "T6";
     #905#11 = #905#6;
     #905#6 = #905#11 - #905#12;
     |GRABA 000#905n;
|FPRC;

|DEFBUCLE TallaFab0;
     #referen@#1005;
     ;
     "T5", eaSerie, enCodigo, enLinea, "      ";
     "T5", eaSerie, enCodigo, enLinea, "zzzzzz";
     ;
     NULL, TallaFab0;
|FIN;

|PROCESO TallaFab;
     |DDEF aAlfa;
     aAlfa = aAlfa + "dsm00005";
     |CARGA_DEF aAlfa, referen@;
     |SI FSalida = 0;
          |ABRE #905;
          |SI nSwAgrupa = 1;
               |HAZ CreaTempo; |NOME_DAT #103 Tempo; |DELINDEX #103;
               |ABRE #103;
               nSw = 1; |BUCLE agifa074;
               |NOME_DAT #referen@#eaFicVertical#;
               |ABRE #referen@;
               nSw = 2; |BUCLE agifa074;
               |CIERRA #referen@;
               |CIERRA #103;
               |HAZ BoraTempo; |DELINDEX #103;
          |SINO;
               |BUCLE TallaFab0;
          |FINSI;
          |CIERRA #905;
          |DESCARGA_DEF #referen@;
     |FINSI;
|FPRC;

|RUTINA ClaveBaja;
     #905#0 = eaTipo;
     #905#1 = #0#0;
     #905#2 = #0#1;
     #905#3 = #0#2;
     #905#4 = "      ";
|FRUT;

|RUTINA ClaveAlta;
     #905#0 = eaTipo;
     #905#1 = #0#0;
     #905#2 = #0#1;
     #905#3 = #0#2;
     #905#4 = "zzzzzz";
|FRUT;

|PROCESO Tipo7;  |TIPO 7;
     |SI #0Campo = 0;
         #0Campo = 1;
         |PINTA #0Campo;
     |FINSI;
|FPRC;

|PROCESO BajaCeros;
     |BORRA 020#905a;
     |LIBERA #905;
|FPRC;

|DEFBUCLE dsm00005Ceros;
     #905#1;
     6;
     eaTipo, #0#0, #0#1, #0#2, "      ", 0;
     eaTipo, #0#0, #0#1, #0#2, "zzzzzz", 0;
     be;
     NULL, BajaCeros;
|FIN;

|PROCESO ContaUni;
     #0#8 = #0#8 + #905#6;
     |SI eaMoneda = "B";
          #0#9 = #0#9 + (#905#6 * #905#7);
          #0#10 = #0#10 + (#905#6 * #905#7);
     |SINO;
          #0#9 = #0#9 + (#905#6 * #905#13);
          #0#10 = #0#10 + (#905#6 * #905#13);
     |FINSI;
|FPRC;

|DEFBUCLE dsm00005Conta;
     #905#1;
     ;
     eaTipo, #0#0, #0#1, #0#2, "      ";
     eaTipo, #0#0, #0#1, #0#2, "zzzzzz";
     be;
     NULL, ContaUni;
|FIN;

|PROCESO Columna;
     |ABRE #910;
     |LEE 040#910p;
     |SI FSalida ! 0;  |DDEFECTO #910;  |FINSI;
     |CIERRA #910;

     nNume         = #910#21;
     aAlfa         = "\" + #910#23 + "+#";
     |CAMPO_ANCHO  #905#4, 1, nNume;
     |CAMPO_MAXIMO #905#4, 1, aAlfa;

     aAlfa = eaTipo % 101;
     |SI aAlfa = "T";
         |C_V #0#9, 1, "N";  |C_M #0#9, 1, "N";

         |BLANCO 2102 2279;
         |ATRI R;
         |BLANCO 1543 1555;
         |BLANCO 1568 1579;
         |ATRI I;
     |FINSI;

     |SI eaTipo ! "PV";  |Y eaTipo ! "PC";
         |ATRI R;  |BLANCO 1543 1555;  |ATRI I;
     |FINSI;
     |SI eaTipo = "W3";
          |PINTA 2301, "º Pendientes .                                   ";
          |PINTA 2350, "                              º";
          |C_V #905#12, 1, "S";
          |C_L #905#12, 1, "S";
     |FINSI;
     |SI eaTipo = "T6";
          |PINTA 2103, "Fabricadas..";
          |PINTA 2203, "A Fabricar..";
          |C_P #905#12, 1, 2119; |C_V #905#12, 1, "S";
          |C_P #905#11, 1, 2219; |C_V #905#11, 1, "S";
     |FINSI;
     |SI eaTipo = "T55";
          |PINTA 2003, "A Fabricar..";
          |PINTA 2103, "Fabricadas..";
          |C_P #905#12, 1, 2119; |C_V #905#12, 1, "S";
          |SI enModo ! 7; enModo = 4; |FINSI;
     |FINSI;
     |SI eaTipo = "T3";
          aAlfa = "Stock Total.  Articulo.             Almacen.            " + #910#20;
          |QBF aAlfa; aAlfa = aAlfa + ".......";
          |PINTA 2103, aAlfa;
          aAlfa = "Stock Dispo.  Articulo.             Almacen.            " + #910#20;
          |QBF aAlfa; aAlfa = aAlfa + ".......";
          |PINTA 2203, aAlfa;
     |FINSI;

     |ENTCOLUMNA #1#905, 5, enModo, 5, 0, ClaveBaja, ClaveAlta;

     #0#8 = 0;
     #0#9 = 0;
     #0#10 = 0;
     |SI #910#45 = "S"; |Y eaTipo ! "T6";

          |HAZ EsEvaristo;
          |SI FSalida = 0;    || Modulo
                |SI eaTipo ! "PV";
                    |BUCLE dsm00005Ceros;
                |FINSI;
          |SINO;              || Estandar
               |BUCLE dsm00005Ceros;
          |FINSI;
     |FINSI;

     |BUCLE dsm00005Conta;
|FPRC;

|PROCESO Tipo12z2;  |TIPO 12;
     |SI aParametro = "CONSU";
          enModo = 4;  |HAZ Columna;
     |SINO;
          enModo = 2;  |HAZ Columna;
     |FINSI;
|FPRC;

|| ********************************************************************
||                PROCESO DE BAJAS DE LINEAS DESDE SUB_EJECUTAS
|| ********************************************************************

|PROCESO BajaVertical;
     |SI #905#0 = "PV"; |O #905#0 = "PC";
         enUnidades = (#905#6 - #905#12) * -1;
         |SI eaAbono = "S";
             enUnidades = #905#6 * -1;
         |FINSI;
     |SINO;
         enUnidades = #905#6 * -1;
     |FINSI;

     |HAZ ActComponente;

     |BORRA 020#905a;
     |LIBERA #905;
|FPRC;

|DEFBUCLE dsm00005B;
     #905#1;
     ;
     eaTipo, #0#0, #0#1, #0#2, "      ";
     eaTipo, #0#0, #0#1, #0#2, "zzzzzz";
     be;
     NULL, BajaVertical;
|FIN;

|PROCESO PedVentaRese;
     |HAZ ActComponente;
|FPRC;

|DEFBUCLE dsm00005R;
     #905#1;
     ;
     eaTipo, #0#0, #0#1, #0#2, "      ";
     eaTipo, #0#0, #0#1, #0#2, "zzzzzz";
     be;
     NULL, PedVentaRese;
|FIN;

|PROGRAMA;
     |ABRE #910; |LEE 000#910p; |CIERRA #910;
     |ABRE #142; |LEE 000#142p; |CIERRA #142;

     eaCtrStock = "N";
     |SI #910#52 = "S";
          |SI eaTipo = "PV"; eaCtrStock = #142#35; |FINSI;
          |SI eaTipo = "DP"; eaCtrStock = #142#5; |FINSI;
          |SI eaTipo = "AV"; eaCtrStock = #142#5; |FINSI;
          |SI eaTipo = "FC"; eaCtrStock = #142#230; |FINSI;
          |SI eaTipo = "FP"; eaCtrStock = #142#234; |FINSI;
     |FINSI;

     |SI eaTipo = "FP";
          nDeci_TBas = nDeci_Base;
          nDeci_TDiv = nDeci_Div;
          nDeci_PBas = nDeci_PreBase;
          nDeci_PDiv = nDeci_PreDiv;
     |FINSI;
     |SI eaTipo = "W1";
          nDeci_TBas = nDeci_PreBase;
          nDeci_TDiv = nDeci_PreDiv;
          nDeci_PBas = nDeci_PreBase;
          nDeci_PDiv = nDeci_PreDiv;
     |FINSI;
     |SI eaTipo = "W2";
          nDeci_TBas = nDeci_Div;
          nDeci_TDiv = nDeci_Div;
          nDeci_PBas = nDeci_PreDiv;
          nDeci_PDiv = nDeci_PreDiv;
     |FINSI;
     |SI eaTipo = "FC";
          nDefAlma = nAlmaVen;
          nDeci_TBas = nDeci_Base;
          nDeci_TDiv = nDeci_Div;
          nDeci_PBas = nDeci_PreBase;
          nDeci_PDiv = nDeci_PreDiv;
     |FINSI;

     |SI eaTipo = "PV";  |O eaTipo = "PC";
          |SI eaTipo = "PV"; nDefAlma = nAlmaVen; |SINO; nDefAlma = nAlmaCom; |FINSI;
          nDeci_TBas = nDeci_Base;
          nDeci_TDiv = nDeci_DivP;
          nDeci_PBas = nDeci_PreBase;
          nDeci_PDiv = nDeci_PreDivP;
     |FINSI;

     |SI eaTipo = "DP";  |O eaTipo = "T1";
          |SI eaTipo = "DP"; nDefAlma = nAlmaVen; |FINSI;
          nDeci_TBas = nDeci_Base;
          nDeci_TDiv = nDeci_DivD;
          nDeci_PBas = nDeci_PreBase;
          nDeci_PDiv = nDeci_PreDivD;
     |FINSI;

     |SI eaTipo = "AV";  |O eaTipo = "AC";
          |SI eaTipo = "AV"; nDefAlma = nAlmaVen; |SINO; nDefAlma = nAlmaCom; |FINSI;
          nDeci_TBas = nDeci_Base;
          nDeci_TDiv = nDeci_Div;
          nDeci_PBas = nDeci_PreBase;
          nDeci_PDiv = nDeci_PreDiv;
     |FINSI;

     |DDEFECTO #0;
     #0#0 = eaSerie;
     #0#1 = enCodigo;
     #0#2 = enLinea;
     #0#3 = eaArticulo;
     #0#4 = enAlmacen;
     #0#8 = enUnidades;
     #0#11 = eaProve;

     |SI eaTipo = "T66"; eaTipo = "T6"; nSwAgrupa = 1; |FINSI;
     |SI eaTipo = "T6";
          |NOME_DAT #905 eaFicVertical;
          |HAZ TallaFab;
     |FINSI;

     |SI eaMoneda = "B";
          nDeci_VTot = nDeci_TBas;
          #0#9 = enTBruto;
     |SINO;
          nDeci_VTot = nDeci_TDiv;
          #0#9 = #0#9 / enCamDiv * enCamBas;
     |FINSI;

     |ABRE #7;
     #7#26 = eaSerie;
     |LEE 000#7=;
     |CIERRA #7;
     nAlmaCom = #7#31;
     nAlmaVen = #7#29;

     aParametro = PARAMETRO;

     |SI aParametro = "RESE";
          |BUCLE dsm00005R;
          |ACABA;
     |FINSI;

     |SI aParametro = "BAJA";
         enForma = -1;
         |BUCLE dsm00005B;
         |ACABA;
     |FINSI;

     |SI aParametro = "PVC";
         nDefAlma = nAlmaVen;
         #905#0 = "PV";
         #905#4 = eaComponente;
         |HAZ ActComponente;

         |ACABA;
     |FINSI;

     |SI aParametro = "DPC";
         nDefAlma = nAlmaVen;
         #905#0 = "DP";
         #905#4 = eaComponente;
         |HAZ ActComponente;

         |ACABA;
     |FINSI;

     |SI aParametro = "AVC";           || Desde Albaranres Normales
         nDefAlma = nAlmaVen;
         enForma = 1;

         |ABRE #905;

         #905#0 = "AV";
         #905#1 = #0#0;
         #905#2 = #0#1;
         #905#3 = #0#2;
         #905#4 = eaComponente;
         |LEE 101#905=;
         |SI FSalida ! 0;  |CIERRA #905;  |ACABA;  |FINSI;

         |HAZ ActComponente;

         |LIBERA #905;
         |CIERRA #905;

         |ACABA;
     |FINSI;

     |SI aParametro = "FCC";           || Desde Facturas Contado
         nDefAlma = nAlmaVen;
         enForma = 1;

         |ABRE #905;

         #905#0 = "FC";
         #905#1 = #0#0;
         #905#2 = #0#1;
         #905#3 = #0#2;
         #905#4 = eaComponente;
         |LEE 101#905=;
         |SI FSalida ! 0;  |CIERRA #905;  |ACABA;  |FINSI;

         |HAZ ActComponente;

         |LIBERA #905;
         |CIERRA #905;

         |ACABA;
     |FINSI;

     |SI aParametro = "EVS";           || Desde Entrada Valorada de Stock

         |ABRE #905;

         #905#0 = "W1";
         #905#1 = #0#0;
         #905#2 = #0#1;
         #905#3 = #0#2;
         #905#4 = eaComponente;
         |LEE 101#905=;
         |SI FSalida ! 0;  |CIERRA #905;  |ACABA;  |FINSI;

         |HAZ ActComponente;

         |LIBERA #905;
         |CIERRA #905;

         |ACABA;
     |FINSI;

     |SI aParametro = "ERE";           || Desde Regularizaciones Existencias

         |ABRE #905;

         #905#0 = "T3";
         #905#1 = #0#0;
         #905#2 = #0#1;
         #905#3 = #0#2;
         #905#4 = eaComponente;
         |LEE 101#905=;
         |SI FSalida ! 0;  |CIERRA #905;  |ACABA;  |FINSI;

         |HAZ ActComponente;

         |LIBERA #905;
         |CIERRA #905;

         |ACABA;
     |FINSI;

     |SI aParametro = "MEA";           || Desde Movimientos Almacenes
         |ABRE #905;

         #905#0 = "T4";
         #905#1 = #0#0;
         #905#2 = #0#1;
         #905#3 = #0#2;
         #905#4 = eaComponente;
         |LEE 101#905=;
         |SI FSalida ! 0;  |CIERRA #905;  |ACABA;  |FINSI;

         |HAZ ActComponente;

         |LIBERA #905;
         |CIERRA #905;

         |ACABA;
     |FINSI;

     |SI aParametro = "PCC";          || Desde Pedidos Compras
         nDefAlma = nAlmaCom;
         #905#0 = "PC";
         #905#4 = eaComponente;
         |HAZ ActComponente;

         |ACABA;
     |FINSI;

     |SI aParametro = "ACC";           || Desde Albaranes Compras
         nDefAlma = nAlmaCom;
         enForma = 1;

         |ABRE #905;

         #905#0 = "AC";
         #905#1 = #0#0;
         #905#2 = #0#1;
         #905#3 = #0#2;
         #905#4 = eaComponente;
         |LEE 101#905=;
         |SI FSalida ! 0;  |CIERRA #905;  |ACABA;  |FINSI;

         |HAZ ActComponente;

         |LIBERA #905;
         |CIERRA #905;

         |ACABA;
     |FINSI;

     |CABEZA "Entrada Vertical";

     eaModifi = "";
     enModo   = 7;  |HAZ Columna;

     |SI #0#3 ! "                    ";
         |C_M #0#3, 1, "N";
     |FINSI;


     |SI #0#4 ! 0;
         |C_M #0#4, 1, "N";
     |SINO;
          #0#4 = nDefAlma; |PINTA #0#4;
     |FINSI;

     |SI #142#99 = "S";
         #0#4 = 1;
         |C_M #0#4, 1, "N";
     |FINSI;

     |PINPA #0#0;
     |PINDA #0#0;

     |ATRI S;
     |SI aParametro = "CONSU";
          aAlfa = "CONSULTA " + #910#20;
          |SI eaEstado ! "";
               aAlfa = aAlfa + " " + eaEstado;
          |FINSI;
     |SINO;
          aAlfa = "INTRODUCCION " + #910#20;
     |FINSI;
     |PINTA 1029, aAlfa;
     |ATRI s;

     |SI #910#50 = "S"; ||Pedir pvp
          |SI eaTipo = "PC"; |O eaTipo = "AC"
               |C_V #905#15, 1, "S";
               |BLIN 23;
               |PINTA 2301, "º PVP.........";
               |PINTA 2380, "º";
          |FINSI;
     |FINSI;

     |SI aParametro = "CONSU";
          |ENDATOS #4#0;
     |SINO;
          |ENDATOS #1#0;
          enAlmacen  = #0#4;
          enUnidades = #0#8;
          || enTBruto   = #0#9;
          enTBruto   = #0#10; || Cogemos el que tiene '*' decimales para que no
                              || descuadre el precio al dividir por las unidades
     |FINSI;

     |SI eaTipo = "PC"; |O eaTipo = "AC";
          |SUB_EJECUTA_NP "dsm00076;grapre";
     |FINSI;
|FPRO;
