|FICHEROS;
     agifa100 #0;
     agifa081 #1;
     agifa067 #2;
     agifa112 #3;
     agi00011 #11;
     agitp001 #10;
     agifa029 #29;
     agifa007 #40;
     dsm00083 #83;
     dsm00010 #910;
     agifa142 #142;
     :/agitool/def/dsfax001 #100;

     referen@;
     agifa067@;
|FIN;

|INCLUYE i_varart;

|VARIABLES;
     nSwSanchez  = 0;
     nSalida     = 0;
     nCampos     = 0;
     i           = 0;

     &enImpo = 0;
     &eaImpElige = "";
     &PRMNT_Imp = "";
     ||&enSwDA i_varart  || Para Des. Ampli
     &enSwi       = 0;   ||   ''
     &desa        = 0;   || Para Textos

     &descrip = "";
     &eaTxtCali1 = "";
     &eaTxtCali2 = "";
     nSwPCEGroup = 0;
     nHandle = 0;
     nVeces = 0;
     aMas = "";
     aPathEnvios = "";
     aFrom       = "";
     aTo         = "";
     aAsunto     = "";
     aMensaje    = "";
     aVersion1   = "";
     aVersion2   = "";
     aBase       = "";
     aBaseLocal  = "";
     aPara       = "";
     aAdjunto    = "";
     {1}aContiene    = "";
     {1}aLocal       = "";
     aOrigen = "";
     aSalida = "";
     aEmail = "";

     aTextoMSG = "";
     aParametro = "";
     aIPRemoto = "";
     aDestino = "";
     aNomFic = "";
     aFic = "";
     nPrime = 0;
     aImpre = "";
     aImpreFax = "";
     aNumFax = "";
     &enAlbeAlma = 0;
     &eaHoraPed = "";
     &eaImpresora = "";
     &eaDefImpre = "";

     &eaArtPrv = "";
     &eaArtPLB = "";
     &eaPrvPLB = "";
     &enTintado = 0;

     &eaDoc = "";
     nSwPackList = 0;

     aAlfa        = "";

     &aDivisa     = "";
     &aMoneda     = "";
     &aParam      = "";
     &enNumero = 0;
     &enCodig     = 0;
     &eaNArticulo = "";

     &enPantalla  = 0;
     &ser_ped     = "";
     &num_ped     = 0;

     &informe1 = "agifa081";
     informe = "";
     &tolin = 0;
     &cant = 0;
     &iva1 = 0;
     &iva2 = 0;
     &iva3 = 0;
     &iva4 = 0;
     &iva5 = 0;
     &re1 = 0;
     &re2 = 0;
     &re3 = 0;
     &re4 = 0;
     &re5 = 0;
     &bl  = "                              ";
|FIN;

컴컴컴컴컴컴컴컴컴컴컴컴Modulo 004159컴컴컴컴컴컴컴컴컴컴컴컴
|PROCESO Agrupador;
     nSalida = 1;
     |LEE 000#2p;
     |PARA; |SI FSalida = 0; |HACIENDO;
          |SI #2#2 = #agifa067@#2; |Y #2#47 = #agifa067@#47; |Y #agifa067@#47!0;
               nSalida = 0; |SAL;
          |FINSI;
          |LEE 000#2s;
     |SIGUE;
     |SI nSalida = 0;
          #2#6 = #2#6 + #agifa067@#6;   ||Precio
          #2#7 = #2#7 + #agifa067@#7;   ||Impo
          #2#9 = #2#9 + #agifa067@#9;   ||Total
          #2#33 = #2#33 + #agifa067@#33;||Precio Div
          #2#34 = #2#34 + #agifa067@#34;||Total Div
          |GRABA 020#2a;
     |SINO;
          aAlfa = "|NC"; aAlfa = #2#aAlfa#;
          nCampos = aAlfa; nCampos = nCampos - 1;
          |PARA i = 0; |SI i [ nCampos; |HACIENDO i = i + 1;
               #2i = #agifa067@#i#;
          |SIGUE;
          |GRABA 020#2n;
     |FINSI;
|FPRC;

|DEFBUCLE Agrupador;
     #agifa067@#1003;
     ;
     #1#25, #1#0, 001;
     #1#25, #1#0, 999;
     ;
     NULL, Agrupador;
|FIN;

|PROCESO AgruArtiIni;
     |DDEF aAlfa;
     aAlfa = aAlfa + "agifa067";
     |CARGA_DEF aAlfa, agifa067@;
     nSwSanchez = FSalida;
     |SI FSalida ! 0; |ACABA; |FINSI;

     aAlfa = Usuario;
     |NOME_DAT #2 aAlfa;
     |DELINDEX #2;
     |ABRE #2;
     |BUCLE Agrupador;
     |CIERRA #2;
|FPRC;

|PROCESO AgruArtiFin;
     |DESCARGA_DEF #agifa067@;
     |DELINDEX #2;
|FPRC;
컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴

|PROCESO PCEGTxtCali;
     |DDEF aAlfa;
     aAlfa = aAlfa + "pcegm005";
     |CARGA_DEF aAlfa, referen@;
     |SI FSalida = 0;
          eaTxtCali1 = "";
          eaTxtCali2 = "";
          |ABRE #referen@;
          |SELECT #4#referen@;
          #referen@#31 = #1#25;
          #referen@#30 = #1#0;
          #referen@#32 = 0;
          |LEE 000#referen@.];
          |SI FSalida = 0; |Y #referen@#31 = #1#25; |Y #referen@#30 = #1#0;
               eaTxtCali1 = #referen@#61;
               eaTxtCali2 = #referen@#62;
          |FINSI;
          |CIERRA #referen@;

          |DESCARGA_DEF #referen@;
     |FINSI;
|FPRC;

|PROCESO CargaFax;
     |SI nPrime = 0; |ACABA; |FINSI;
     |FINIMP;

     aDestino = "c:\dswinfax\" + aNomFic;

/*
     aIPRemoto = "";
     |IP_REMOTO aIPRemoto;  |QBT aIPRemoto;
     aDestino = "C:/dswinfax/";
     |RMKDIR aDestino;
     aDestino = aDestino + aNomFic;
     |SI aIPRemoto ! "";
         |ENVIA_FICHERO aFic, aDestino;
     |SINO;
         |COPIA_FICHERO aFic, aDestino;
     |FINSI;
     |FBORRA aFic;
*/

     |SI aParametro = "FAX";
          |DBASS aAlfa; |QBF aAlfa;
          aAlfa = aAlfa + "/agitool/fich/";
          |PATH_DAT #100 aAlfa;
          |ABRE #100;
          |LEE 101#100p;
          |SI FSalida ! 0; |ACABA; |FINSI;
          #100#3 = #3#2;          || destinatario
          #100#4 = aNumFax;       || nro. de fax destino
          #100#7 = aNomFic;       || doc
          #100#10 = #100#10 + 1;  || contador
          #100#11 = aDestino;     || fichero
          |GRABA 020#100a;
          |CIERRA #100;
          |SUB_EJECUTA ":agitool/dsfax001;AUTO";
     |SINO;         ||EMAIL
          ||TODO CCC es un DSCORREO de la propia maquina
          |HAZ MandaCorreo;
     |FINSI;
|FPRC;

|PROCESO MandaCorreo;
||Paso el adjunto (PDF) al servidor.
     |DBASE aBase; |QBF aBase;
     aPathEnvios = aBase + "envios/";
     |MKDIR aPathEnvios;
     aOrigen = aDestino;
     aDestino = aPathEnvios + aNomFic;
     aIPRemoto = "";
     |IP_REMOTO aIPRemoto;  |QBT aIPRemoto;
     |SI aIPRemoto ! "";
          |RECIBE_FICHERO aOrigen, aDestino;
     |SINO;
          |COPIA_FICHERO aOrigen, aDestino;
     |FINSI;
     aAdjunto = aDestino;

     |ESPECIFICA "RBASS", aLocal;
     aBaseLocal = aLocal;

     aDestino   = aBaseLocal + "bin/dscorreo.exe";
     aContiene1 = aDestino;
     |ESPECIFICA "REMOTOMD5", aContiene;
     aVersion1  = FSalida;  |QBF aVersion1;

     |DBASE aBase;  |QBF aBase;
     aOrigen    = aBase + "plugins/dscorreo.exe";
     aContiene1 = aOrigen;
     |ESPECIFICA "MD5", aContiene;
     aVersion2  = FSalida;  |QBF aVersion2;

     |SI aVersion1 ! aVersion2;
         aIPRemoto = "";
         |IP_REMOTO aIPRemoto;  |QBT aIPRemoto;

         |SI aIPRemoto ! "";
             |ENVIA_FICHERO aOrigen, aDestino;
         |SINO;
             |COPIA_FICHERO aOrigen, aDestino;
         |FINSI;
     |FINSI;

     aAlfa  = "start " + aDestino;
     |RSYSTEM aAlfa;
     |MENSA "         ";
     |PULSATECLA;
     |SLEEP 1;

     aIPRemoto = "";
     |IP_REMOTO aIPRemoto;  |QBT aIPRemoto;

     aSalida  = "MAPI";
     aFrom    = "MAPI";
     aTo      = aEmail;  |QBF aTo;
     aAsunto  = "Pedido de Compra " + aNomFic;
     aAsunto = aAsunto - ".pdf";
     |QBF aAsunto;
     aMensaje = aAsunto;

     |DSCORREO_ENVIA aIPRemoto, aSalida, aFrom, aTo, aAsunto, aMensaje, aAdjunto;
     |SI FSalida < 0;
         |MENAV "0000 Ha habido un error durante el envio.";
     |SINO;
         |MENAV "0000 El correo se encuentra en la Bandeja de Salida de su Gestor de Correos. Envielo desde alli.";
     |FINSI;

     |FBORRA aAdjunto;
|FPRC;

|PROCESO MiraArtPrv;
     eaArtPrv = "";
     eaArtPLB = #agifa067#2;
     eaPrvPLB = #agifa081#4;
     |HAZ AlbeArtPrv;
     |QBF eaArtPrv;
     |SI eaArtPrv ! "";
          enSwi = 2;
          |PRINT;
          enSwi = 0;
     |FINSI;
|FPRC;

|PROCESO DesAmpli;
     enSwDA = 1;
     enSwi = 1;
     descrip = #83#5;
     |PRINT;
     enSwDA = 0;
     enSwi = 0;
|FPRC;

|DEFBUCLE DesAmpli;
     #83#1;
     ;
     "P", #2#28, #2#0, #2#1, 001;
     "P", #2#28, #2#0, #2#1, 999;
     ;
     NULL, DesAmpli;
|FIN;

|PROCESO IVAS;
     eaPrv = #1#4;
     efFiva = #1#1;
     enTiva = 1; |HAZ IVAPrv; iva1 = enIva; re1 = enRec;
     enTiva = 2; |HAZ IVAPrv; iva1 = enIva; re1 = enRec;
     enTiva = 3; |HAZ IVAPrv; iva1 = enIva; re1 = enRec;
     enTiva = 4; |HAZ IVAPrv; iva1 = enIva; re1 = enRec;
     enTiva = 5; |HAZ IVAPrv; iva1 = enIva; re1 = enRec;
|FPRC;

|PROCESO prnpc1;
     |SI nSwSanchez = 0; |HAZ AgruArtiIni; |FINSI;

     |SI nSwPCEGroup = 0;
          |HAZ PCEGTxtCali;
     |FINSI;

     eaSerLog = #1#25;
     eaNumLog = (A\("000000" + #1#0) % -106);
     eaTipLog = "PC";
     |HAZ ImprimeLog;

     |DDEFECTO #3;
     #3#0 = #1#4;
     |LEE 000#3=;
     aNumFax = #3#7; |QBF aNumFax;
     aEmail = #3#84; |QBF aEmail;

||     |SI aImpre = aImpreFax;
     |SI aParametro = "FAX"; |O aParametro = "EMAIL";
          |SI aNumFax = ""; |Y aParametro = "FAX";
               aTextoMSG = "0000No se ha indicado Numero de Fax en la ficha del Proveedor";
               |MENAV aTextoMSG;
               |ERROR; |ACABA;
          |SINO;
               |SI aEmail = ""; |Y aParametro = "EMAIL";
                    aTextoMSG = "0000No se ha indicado direccion Email en la ficha del Proveedor";
                    |MENAV aTextoMSG;
                    |ERROR; |ACABA;
               |SINO;
                    |HAZ CargaFax;      ||Sirve tanto para FAX como para EMAIL
                    aNomFic = #1#25 +(("000000"+#1#0)%-106)+".pdf";|QBT aNomFic;
                    |DBASS aFic;
                    aFic = "c:\dswinfax\" + aNomFic;
                    Impresora = "CrystalReport;" + aFic;
                    |IMPRE -1;
                    |SI FSalida ! 0; |ERROR; |ACABA; |FINSI;
                    nPrime = 1;
               |FINSI;
          |FINSI;
     |FINSI;

     |SI nSwPackList = 0;
          eaDoc = "PC" + #1#25 + #1#0;
          |HAZ PackListVar;
     |FINSI;

     |HAZ IVAS;
     aDivisa = #agifa081#27;
     aMoneda = #agifa081#29;
     |HAZ Divisas081;

     |SI #3#0 ! #1#4;
         #3#0 = #1#4;
         |LEE 021#3=;
         |SI FSalida ! 0;  |DDEFECTO #3;  |FINSI;
     |FINSI;

     #40#26 = #1#25;
     |LEE 001#40=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;
     informe = #40#1; |QBF informe;
     informe1 = (informe % 0104) + "5" + (informe % 0603); |QBF informe1;

     |SI #0#8 ! "        ";
          informe = #0#8;
          |QBT informe;
     |FINSI;

     |INFOR informe;
     |SI FSalida ! 0;
          aAlfa = "0000No existe informe [" + informe + "]";
          |MENAV aAlfa;
          |SI eaFicImpre ! "";
               |INFOR "agifa081";
               |SI FSalida ! 0;
                    |MENAV "0000ERROR FALTA INFORME ESTANDAR...";
                    |ERROR;
               |FINSI;
          |SINO;
               |ERROR;
          |FINSI;
     |FINSI;

     |SI #910#19 = "S";  |HAZ AbreTemporales;  |FINSI;
|FPRC;

|PROCESO agi00011;
     desa = 1;
     |PRINT;
     desa = 0;
|FPRC;

|DEFBUCLE agi00011;
     #11#1;
     ;
     #2#28,#2#0,#2#1,001;
     #2#28,#2#0,#2#1,999;
     ;
     NULL,agi00011;
|FIN;

|PROCESO CodProv;
     |ABRE #10;
     |DDEFECTO #10;
     #10#0 = #1#4;
     #10#1 = #2#2;
     |LEE 000#10=;
     aAlfa = #10#2; |QBF aAlfa;
     |SI aAlfa = "";
          #10#2 = #2#2;  || Le asignamos el del articulo para la impresion
     |FINSI;
     |CIERRA #10;
|FPRC;

|PROCESO prnpcl;|TIPO 0;
     enArtImp = #2#2; |QBF enArtImp;

     tolin = #2#5 * #2#6;
     tolin = tolin < #2#8;
     cant  = cant + tolin;
     descrip = #2#4;

     |HAZ EsAparecida;
     |SI FSalida = 0;
          eaArticulo = #2#2;
          |HAZ ImpAparecida;
     |FINSI;

     |SI #910#19 = "N";
          ||컴컴컴컴컴컴컴컴컴컴컴Modulo 002743
          |HAZ EsGuerola;
          |SI FSalida = 0;
               |HAZ GuerImpPed;
          |FINSI;
          ||컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
          |HAZ CodProv;
          |PRINT;
          ||컴컴컴컴컴컴컴컴컴컴컴Modulo 003026
          |HAZ EsAlbero;
          |SI FSalida = 0;
               |HAZ MiraArtPrv;
          |FINSI;
          ||컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
          |BUCLE DesAmpli;
          ||컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
          |BUCLE agi00011;
          |ACABA;
     |FINSI;
     eaTipo      = "PC";
     eaSerie     = #2#28;
     enCodig     = #2#0;
     enLinea     = #2#1;
     eaArticulo  = #2#2;
     eaNArticulo = #2#4;
     enUnidades  = #2#5;
     enPrecio    = #2#6;
     enImpo = #2#34;
     enDto1      = #2#8;
     enDto2      = #2#27;
     enDto3      = #2#57;
     |HAZ TrataCompo;
|FPRC;

|PROCESO prnpc2;|TIPO 0;
     |SI #910#19 = "S";  |HAZ ElRemate;  |FINSI;
     ||컴컴컴컴컴컴컴
     |HAZ EsConstru;
     |SI FSalida = 0;
          |HAZ ObrImpPed;
     |FINSI;
     ||컴컴컴컴컴컴
     |PIEINF;
     |FININF;
     tolin = 0;
     cant  = 0;

     |SI nSwSanchez = 0; |HAZ AgruArtiFin; |FINSI;
|FPRC;

|DEFBUCLE agifa081;
     #1#2;
     25, 0, 1;
     #0#0, #0#6 , #0#2 , #0#4;
     #0#1, #0#7 , #0#3 , #0#5;
     ;
     NULL, prnpc1, prnpcl, prnpc2;
|FIN;

|PROCESO PideImpresora;
     |QBF eaImpresora;        || No pide
     |QBF eaFicImpre;         || a un fichero
     |QBF eaDefImpre;         || la por defecto

     |SI eaFicImpre ! "";
          Impresora = eaFicImpre;
          |IMPRE -77;
     |SINO;
          |SI eaImpresora = "";
               |SI eaDefImpre = "";
                    |IMPRE 1;
               |SINO;
                    Impresora = eaDefImpre;
                    |IMPRE 0;
               |FINSI;
          |SINO;
               Impresora = eaImpresora;
               |IMPRE -1;
          |FINSI;
     |FINSI;
     eaImpElige = Impresora;
|FPRC;

|PROCESO ImpCompra;  |TIPO 3;
     |SI aParametro = "";
          |HAZ PideImpresora;
          |SI FSalida ! 0;  |ACABA;  |FINSI;
          aImpre = Impresora;
     |FINSI;

     |ABRE #3;
     |ABRE #40;
     |BUCLE agifa081;
     |CIERRA #40;
     |CIERRA #3;

     |SI aParametro = "";
          |FINIMP;
     |SINO;
          |HAZ CargaFax;
     |FINSI;

     |SI aParametro = "FAX";
          |HAZ EsAlbero;
          |SI FSalida = 0;
               |DBASE aAlfa; |QBF aAlfa;
               aMas = aAlfa + "def/albm0013.mas";
               |CARGA_DEF aMas, referen@;
               |SI FSalida = 0;
                    aParametro = "";
                    |ABRE #referen@;
                    |DDEFECTO #referen@;
                    |LEE 000#referen@.p;
                    |CIERRA #referen@;
                    |SI #referen@#10 ! 0;
                         eaImpresora = #referen@#6;
                         |HAZ PideImpresora;
                         |PARA nVeces = 1; |SI nVeces [ #referen@#10; |HACIENDO nVeces = nVeces + 1;
                              |ABRE #3;
                              |ABRE #40;
                              |BUCLE agifa081;
                              |CIERRA #40;
                              |CIERRA #3;
                         |SIGUE;
                         |FINIMP;
                    |FINSI;
                    |DESCARGA_DEF #referen@;
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|PROGRAMA;
     |SI PRMNT_Imp ! "";
          ser_ped = PRMNT_Imp % 105; aAlfa = PRMNT_Imp % 606;
          num_ped = aAlfa;
     |FINSI;

     |HAZ EsSanchez;
     nSwSanchez = FSalida;

     |HAZ MiraLogos; || Copia Logos para el Crystal

     |ABRE #142; |LEE 000#142p; |CIERRA #142;

/*
     aImpreFax = #142#226; |QBF aImpreFax;
     |SI aImpreFax = ""; aImpreFax = " "; |FINSI;
*/

     aParametro = PARAMETRO;
     |QBF aParametro;
     |SI aParametro = "";
          aImpreFax = " ";
     |SINO;
          aImpreFax = aParametro;
     |FINSI;

     |HAZ EsPackList;
     nSwPackList = FSalida;

     |HAZ EsAlbero;
     |SI FSalida = 0; ||enAlbeAlma=Almacen salida orden tintado
          |ABRE #29; #29#0 = enAlbeAlma; |LEE 000#29=; |CIERRA #29;
     |FINSI;

     |HAZ EsPCEGroup;
     nSwPCEGroup = FSalida;

     aParam = PARAMETRO; |QBF aParam;
     enPantalla  = 0;

     |ABRE #910;
     |LEE 040#910p;
     |SI FSalida ! 0;  |DDEFECTO #910;  |FINSI;
     |CIERRA #910;

     |SI ser_ped = ""; |Y num_ped = 0;
         |CLS;
         |CABEZA "Imp.Pedidos";
         |PINPA #0#0;
         |PINDA #0#0;
         |ENDATOS #1#0;
         |SI FSalida = 0;  |HAZ ImpCompra;  |FINSI;
     |SINO;
          #0#6 = ser_ped;
          #0#7 = ser_ped;
          #0#2 = num_ped;
          #0#3 = num_ped;
          |HAZ ImpCompra;
     |FINSI;
|FPRO;
