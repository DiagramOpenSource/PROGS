|FICHEROS;
     vipez001 #0;
     vipem001 #1;
     vipem002 #2;
     vipem003 #3;
|FIN;

|VARIABLES;
     aDes = "";
 {-1}Menu = "";
     Menu1 = "1021";
     Menu2 = "1";
     Menu3 = "18";
     Menu4 = " *** SALIR ***";
     Menu5 = "Albaran";
     Menu6 = "Articulos";
     Menu7 = "CompoArticulo";
     Menu8 = "DatosProveedor";
     Menu9 = "DescripcionAlbaran";
     Menu10 = "DespArticulos";
     Menu11 = "DespMaterias";
     Menu12 = "FamMateriasPrimas";
     Menu13 = "MateriasPrimas";
     Menu14 = "MatPrimStock";
     Menu15 = "StockFinal";
     Menu16 = "Cancelaciones";
     Menu17 = "DescripcionPedido";
     Menu18 = "FormasDePago";
     Menu19 = "Pedidos"
     Menu20 = "ConsumoTeorico";
     Menu21 = "StockPeriodo";

     aPathMaestro = "";
     aPathTiquet = "";
     aPathInventario = "";
     aPathTxt = "";
     aAlfa = "";
     {1}aLocal = "";
     {1}aContiene = "";
     nHay = 0;
     aVersion1 = "";
     aVersion2 = "";
     aOrigen = "";
     aDestino = "";
     aBase = "";
     aIPRemoto = "";
     aExe = "";
     aTipo = "";
     aFic = "";
     aHora = "";
     nHandle = 0;
     nConta = 0;
     nLinea = 0;
     aTabla = "";
     aFecha1 = "";
     aFecha2 = "";
|FIN;

|PROCESO Min;
     #3#2 = aTabla;
     #3#0 = #2#0;
|FPRC;

|PROCESO Max;
     #3#2 = aTabla;
     #3#0 = #2#0;
|FPRC;

|PROCESO MuestraTabla;
     |SI Menu2 = 2; aTabla = "Albaran"; |FINSI;
     |SI Menu2 = 3; aTabla = "Articulos"; |FINSI;
     |SI Menu2 = 4; aTabla = "CompoArticulo"; |FINSI;
     |SI Menu2 = 5; aTabla = "DatosProveedor"; |FINSI;
     |SI Menu2 = 6; aTabla = "DescripcionAlbaran"; |FINSI;
     |SI Menu2 = 7; aTabla = "DespArticulos"; |FINSI;
     |SI Menu2 = 8; aTabla = "DespMaterias"; |FINSI;
     |SI Menu2 = 9; aTabla = "FamMateriasPrimas"; |FINSI;
     |SI Menu2 = 10; aTabla = "MateriasPrimas"; |FINSI;
     |SI Menu2 = 11; aTabla = "MatPrimStock"; |FINSI;
     |SI Menu2 = 12; aTabla = "StockFinal"; |FINSI;
     |SI Menu2 = 13; aTabla = "Cancelaciones"; |FINSI;
     |SI Menu2 = 14; aTabla = "DescripcionPedido"; |FINSI;
     |SI Menu2 = 15; aTabla = "FormasDePago"; |FINSI;
     |SI Menu2 = 16; aTabla = "Pedidos"; |FINSI;
     |SI Menu2 = 17; aTabla = "ConsumoTeorico"; |FINSI;
     |SI Menu2 = 18; aTabla = "StockPeriodo"; |FINSI;
     aTabla = aTabla < "";
     |ABRE #3;
     |LEE 000#3p;
     |CONSULTA_F_CLAVE #2#3, Min, Max;
     |CIERRA #3;
|FPRC;

|PROCESO ImportaFic;
     aAlfa = aPathTxt + aFic;

     |SI aTipo ! "";     || fichero en el cliente
          |DFICE aDes; aDes = aDes + aFic;
          |RECIBE_FICHERO aAlfa, aDes;
          aAlfa = aDes;
     |FINSI;

     |XABRE "", aAlfa, nHandle;
     |SI FSalida ] 0;
          aAlfa = "0000Importando " + aFic;
          |MENSA aAlfa;
          |SI aFic = "stockperiodo.txt";
               || El primero trae el nombre de los campos
               || la 1 linea es > de 250 caracteres
               |XLEE nHandle, 250, aAlfa;
               |XLEE nHandle, 250, aAlfa;
          |SINO;
               || El primero trae el nombre de los campos
               |XLEE nHandle, 250, aAlfa;
          |FINSI;
          |PARA; |SI; |HACIENDO;
               |XLEE nHandle, 250, aAlfa;
               |SI FSalida [ 0; |SAL; |FINSI;
               nLinea = nLinea + 1;
               #3#0 = #2#0;
               #3#1 = nLinea;
               #3#3 = aAlfa;
               aAlfa = aFic - ".txt";
               #3#2 = aAlfa;
               |SELECT #3#3;
               |LEE 000#3=;
               |SI FSalida ! 0;
                    nHay = 1;
                    |GRABA 020#3n;
               |SINO;
                    nLinea = nLinea - 1;
               |FINSI;
               aAlfa = aAlfa + " " + nLinea;
               |PINTA 2302, aAlfa;
          |SIGUE;
          |XCIERRA nHandle;
          |BLIN 23;
     |SINO;
          aAlfa = "0000No existe " + aFic;
          |MENAV aAlfa;
     |FINSI;

     aAlfa = aPathTxt + aFic;
     |SI aIPRemoto = "";
          |FBORRA aAlfa;
     |SINO;
          |RFBORRA aAlfa;
     |FINSI;
|FPRC;

|PROCESO CopiaExe;
     |DBASE aBase;
     aOrigen = aBase + "plugins/mdbatxt.exe";
     aContiene1 = aOrigen;
     |ESPECIFICA "MD5", aContiene;
     aVersion2 = FSalida;  |QBF aVersion2;

     |ESPECIFICA "RBASS", aLocal;
     aBase = aLocal;
     aDestino = aBase + "bin/mdbatxt.exe";
     aContiene1 = aDestino;
     |ESPECIFICA "REMOTOMD5", aContiene;
     aVersion1 = FSalida;  |QBF aVersion1;

     |SI aVersion1 ! aVersion2;
          |ENVIA_FICHERO aOrigen, aDestino;
     |FINSI;
|FPRC;

|PROCESO Tipo0; |TIPO 0;
     |SI #0#1 < "01.01.2000";
          |MENAV "0000La fecha no puede ser inferior al año 2000";
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO Tipo3; |TIPO 3;
     |ABRE #1; |LEE 000#1p; |CIERRA #1;
     aPathMaestro = #1#1; |QBF aPathMaestro;
     aPathTiquet = #1#2; |QBF aPathTiquet;
     aPathInventario = #1#3; |QBF aPathInventario;
     |SI aPathMaestro = ""; |O aPathTiquet = ""; |O aPathInventario = "";
          |MENAV "0000No esta configurado los parametros de importacion";
          |ACABA;
     |FINSI;

     |IP_REMOTO aIPRemoto;  |QBT aIPRemoto;

     |SI aIPRemoto = "";
          |DBASE aBase;
          aExe = aBase + "plugins/mdbatxt.exe";
          aPathTxt = aBase;
     |SINO;
          |HAZ CopiaExe;
          aExe = aDestino;
          aPathTxt = aBase;
     |FINSI;

     aFecha1 = (#0#1 % 102) + "-" + (#0#1 % 402) + "-" + (#0#1 % -102);
     aFecha2 = (#0#2 % 102) + "-" + (#0#2 % 402) + "-" + (#0#2 % -102);
     aAlfa = aExe + " " + aPathMaestro + " " + aPathTiquet + " " + aPathInventario + " " + aPathTxt + " " + aFecha1 + " " + aFecha2 + " sinmensajes";
     |SI aIPRemoto = "";
          |SYSTEM aAlfa;
          aTipo = "";

          aAlfa = aExe + ".err";
          |XABRE "E", aAlfa, 0;
     |SINO;
          |RSYSTEM aAlfa;
          aTipo = "C";

          aAlfa = aExe + ".err";
          |XABRE "CE", aAlfa, 0;
     |FINSI;
     |SI FSalida ] 0;
          |MENAV "0000CANCELADO. Se ha producido algun ERROR al importar los MDB";
          |ACABA;
     |FINSI;

     |HORA aHora;

     |ABRE #2;
     |LEE 000#2u;
     |SI FSalida = 0; nConta = #2#0 + 1; |SINO nConta = 1; |FINSI;
     |DDEFECTO #2;
     #2#0 = nConta;
     #2#1 = #0#1;
     #2#2 = ~;
     #2#3 = aHora;
     #2#4 = Usuario % 810;
     |GRABA 020#2b;

     |ABRE #3;
||     aFic = "datosproveedor.txt"; |HAZ ImportaFic;
     aFic = "articulos.txt"; |HAZ ImportaFic;
     aFic = "fammateriasprimas.txt"; |HAZ ImportaFic;
     aFic = "materiasprimas.txt"; |HAZ ImportaFic;
||     aFic = "compoarticulo.txt"; |HAZ ImportaFic;
||     aFic = "desparticulos.txt"; |HAZ ImportaFic;
||     aFic = "despmaterias.txt"; |HAZ ImportaFic;
     aFic = "albaran.txt"; |HAZ ImportaFic;
     aFic = "descripcionalbaran.txt"; |HAZ ImportaFic;
||     aFic = "matprimstock.txt"; |HAZ ImportaFic;
||     aFic = "stockfinal.txt"; |HAZ ImportaFic;
     aFic = "pedidos.txt"; |HAZ ImportaFic;
     aFic = "descripcionpedido.txt"; |HAZ ImportaFic;
     aFic = "formasdepago.txt"; |HAZ ImportaFic;
||     aFic = "cancelaciones.txt"; |HAZ ImportaFic;
||     aFic = "consumoteorico.txt"; |HAZ ImportaFic;
     aFic = "stockperiodo.txt"; |HAZ ImportaFic;
     |CIERRA #3;
     |SI nHay = 0;
          |BORRA 000#2c;
          |MENAV "0000No hay registros...";
     |SINO;
          |GRABA 020#2a;
          |PARA; |SI; |HACIENDO;
               |MENUG Menu;
               |SI FSalida [ 1; |ACABA; |FINSI;
               Menu2 = FSalida;
               |HAZ MuestraTabla;
          |SIGUE;
     |FINSI;
     |CIERRA #2;
|FPRC;

|PROGRAMA;
     |CLS;
     |ABRE #0;
     |LEE 101#0p;
     |SI FSalida ! 0; |GRABA 020#0b; |FINSI;
     #0#0 = ~;
     |PINPA #0#0;
     |PINDA #0#0;
     |ENDATOS #1#0;
     |SI FSalida = 0;
          |GRABA 020#0a;
     |FINSI;
     |CIERRA #0;
|FPRO;
