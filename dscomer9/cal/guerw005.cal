|FICHEROS;
     guerw005 #0;
     guerm006 #6;
     guerm007 #7;
     guerm008 #8;
     guerm033 #33,MANTE;       ||Vehiculos
|FIN;

|VARIABLES;
     nBoton    = 0;
     aRuta     = "";
     aDato     = "";
     nHandle   = 0;
     aAlfa     = "";
     aAlf1     = "";
     aAlf2     = "";
     aAlf3     = "";
     nConta    = 0;
|FIN;

|PROCESO Procesa;
     ||Fecha
     aAlfa = aDato % 108;
     aAlf1 = aAlfa % 104; aAlf2 = aAlfa % 502; aAlf3 = aAlfa % 702;
     aAlfa = aAlf3 + "." + aAlf2 + "." + aAlf1;
     #8#1 = aAlfa;

     ||Hora
     aAlfa = aDato % 908;
     #8#2 = aAlfa;

     ||Surtidor
     aAlfa = aDato % 1702;
     #8#3 = aAlfa;

     ||Litros
     aAlfa = aDato % 1910; aAlf1 = aAlfa % 106; aAlf2 = aAlfa % -104;
     #8#10 = aAlfa;

     ||Precio
     aAlfa = aDato % 2910; aAlf1 = aAlfa % 107; aAlf2 = aAlfa % -103;
     #8#11 = aAlfa;

     ||Total
     aAlfa = aDato % 3910; aAlf1 = aAlfa % 108; aAlf2 = aAlfa % -102;
     #8#12 = aAlfa;

     ||Matricula Asig
     aAlfa = aDato % 4910;
     |QBT aAlfa;
     #8#7 = aAlfa;

     ||Tarjeta
     aAlfa = aDato % 5920;
     #8#6 = aAlfa;

     ||Km
     aAlfa = aDato % 7906;
     #8#13 = aAlfa;

     ||Matricula Repo
     aAlfa = aDato % 8508;
     |QBT aAlfa;
     |HAZ MiraMatri;
     #8#9 = aAlfa;

     ||Contador
     aAlfa = aDato % 9306;
     #8#8 = aAlfa;

     #8#14 = "N";
     ||컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
     |SI #8#3 = #0#0; #8#3 = #0#3; |FINSI;
     |SI #8#3 = #0#1; #8#3 = #0#4; |FINSI;
     |SI #8#3 = #0#2; #8#3 = #0#5; |FINSI;

     |DDEFECTO #6;  || Surtidor
     #6#0 = #8#3;
     |LEE 000#6=;
     #8#4 = #6#1;

     |DDEFECTO #7;  || Tarjeta
     |SELECT #2#7;
     #7#1 = #8#6;
     |LEE 000#7=;
     |SI FSalida ! 0;
          |SELECT #1#7;
          aAlfa = #8#6 % -106;
          #7#0 = aAlfa;
          #7#2 = #8#7;
          |GRABA 020#7n;
     |FINSI;
     #8#5 = #7#0;
     ||컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
     |SALVA_DATOS #8;
     |SELECT #2#8;
     |LEE 000#8=;
     |SI FSalida = 0;
          |REPON_DATOS #8;
          aAlfa = "0000REGISTRO EXISTENTE:" + &13 + "Surtidor:" + #8#3 + " Contador:" + #8#8+ &13 + "Fecha: " + #8#1 + #8#2;
          |MENAV aAlfa;
          |ACABA;
     |FINSI;
     |SELECT #1#8;
     |LEE 000#8u;
     nConta = #8#0 + 1;
     |REPON_DATOS #8;
     #8#0 = nConta;
     |GRABA 020#8n;
|FPRC;

|PROCESO Inicio;
     |ABRE #6;
     |ABRE #7;
     |ABRE #8;
     aRuta = #0#7; |QBF aRuta;
     |XABRE "CU", aRuta, nHandle;
     |SI FSalida = 0;
          |ABRE #guerm033;
          |XLEE nHandle, 250, aDato;
          |PARA; |SI FSalida ] 0; |HACIENDO;
               |HAZ Procesa;
               |XLEE nHandle, 250, aDato;
          |SIGUE;
          |XCIERRA nHandle;
          |CIERRA #guerm033;
     |SINO;
          |MENAV "0000No encuentro el fichero...";
     |FINSI;
     |CIERRA #6;
     |CIERRA #7;
     |CIERRA #8;
|FPRC;

|PROCESO Desactiva; |TIPO 0;
     |ESTADO_CONTROL nBoton, "DISABLE";
|FPRC;

|PROCESO Activa; |TIPO 7;
     |ESTADO_CONTROL nBoton, "ENABLE";
|FPRC;

|PROCESO Boton;
     |PTEC 824; ||F2
|FPRC;

|PROCESO Browse; |TIPO 0;
     |SI FSalida ! 10; |ACABA; |FINSI;

     |ESTADO_CONTROL nBoton, "DISABLE";
     |PUSHV 0501 2380;
     aRuta = #0#7;  |QBF aRuta;
     |SI aRuta = ""; aRuta = "\*.*"; |FINSI;
     aRuta = "F" + aRuta;
     |BROWSE aRuta; |QBF aRuta;
     |POPV;
     |ESTADO_CONTROL nBoton, "ENABLE";
     #0#7 = aRuta % 280; |PINTA #0#7;
     aRuta = #0#7; |QBF aRuta;
     |SI aRuta = ""; |ERROR; |FINSI;
|FPRC;

|PROCESO Tipo9;
     |FIN_CONTROL_WINDOWS nBoton;
     |PULSATECLA;
|FPRC;

|PROCESO Tipo8;
     |CONTROL_SIMPLE 0, "BOTON, Fichero", 1021, 1028, Boton;
     nBoton = FSalida;
     |ESTADO_CONTROL nBoton, "DISABLE";
     |PULSATECLA;
|FPRC;

|PROGRAMA;
     |ABRE #0;
     |LEE 000#0p;
     |SI FSalida ! 0; |GRABA 020#0b; |FINSI;

     |HAZ Tipo8;

     |CLS;
     |PINPA #0#0;
     |PINDA #0#0;
     |ENDATOS #2#0;
     |SI FSalida = 0;
          |HAZ Inicio;
          |GRABA 020#0a;
     |FINSI;
     |CIERRA #0;
     |HAZ Tipo9;
|FPRO;

|PROCESO MiraMatri;
     #guerm033#0 = aAlfa;
     |LEE 101#guerm033.=;
     |SI FSalida ! 0;
          |HAZ Tipo9;
          |PUSHV 0501 2380;
          |CLS;
          |DDEFECTO #33;
          |PINPA #0#33;
          |PINDA #0#33;
          #33#0 = aAlfa;
          |PINTA #33#0;
          |ENDATOS #1#33;
          |SI FSalida = 0;
              |GRABA 020#33n;
          |FINSI;
          |POPV;
          |HAZ Tipo8;
     |FINSI;
     |LIBERA #33;
|FPRC;
