     || OJO es el mismo calculo para el agifa502 y agifa504


|FICHEROS;
     agifa501 #0;             ||Entrada tiquets (C)
     agifa502 #1;             ||Entrada tiquets (L)
     agifa019 #2;             ||Articulos
     agifa017 #3;             ||Almacenes
     agifa505 #4;             ||Parametros Caja
     agifa024 #14;            ||Clientes
     agifa517 #30;            ||Parametros T.P.V
     agifa017 #34;

     agifa571  #27;
     agica001  #28;           ||Codigos alternativos;

     agifa142  #32;
     agifa017  #17;
     agifa007  #69;
     dsl00001  #66;
     dsm00279  #279;
     dsm00004  #904;
     dsm00010  #910;
     dsm00014  #914;
     dsm00016  #916;
     dsm00040  #940;
     agifa502@;
|FIN;

|CAMPOS;
     #2#15  art_pser; #34#42  alm_pser;
     #2#16  art_prec; #34#43  alm_prec;
     #2#17  art_pval; #34#4   alm_pval;
     #2#11  art_nece;
     #2#12  art_rese; #34#8   alm_rese;
     #2#14  art_depo; #34#10  alm_depo;
     #2#13  art_fabr; #34#9   alm_fabr;
     #2#10  art_vsto; #34#3   alm_vsto;
     #2#7   art_mini; #34#6   alm_mini;
     #2#8   art_maxi; #34#7   alm_maxi;
     #2#6   art_tota; #34#5   alm_tota;
     #2#104 art_disp; #34#39  alm_disp;
     #2#9   art_pcm;
     #30#18 DUNI;
     #30#19 DPRE;
|FIN;

|VARIABLES;
     &enSwSematel = 0;
     &enFormaIMEI = 0;
     &enModoIMEI = 0;
     &eaSerIMEI = "";
     &enDocIMEI = 0;
     &enLinIMEI = 0;
     &eaArtIMEI = "";

     nPreDtoAnt = 0;
     &eaCodBar = "";
     &enPtoVerde = 0;
     alfa1 = "";
     alfa2 = "";
     alfa3 = "";
     alfa4 = "";
     alfa5 = "";
     &enTariCli = 0;
     &eaEsTPV = 0;
     nSwMulti = 0;
     &enNumAparca = 0;
     alfa = "";
     nRec           = 0;
     nNeto          = 0;
     nTotal         = 0;
     nBase          = 0;
     nIva           = 0;
     &EURODB        = 0;
     &nDeci_PreBase = 0;
     nCompru        = 0;
     aDef                     = "";
     nHayLin                  = 0;
     &aDef11                  = "";
     &aDef22                  = "";
     &nHayFOTO                = 999;
     nTecla   = 0;
     {-1}menu                 = "";
     menu1                    = "2400";
     menu2                    = "1";
     menu3                    = "Tipo DOCUMENTO: ";
     menu4                    = "TAF";
     menu5                    = " Tiquet ";
     menu6                    = " Albaran ";
     menu7                    = " Factura ";
     nUniNega= 0;
     &aClave   = "";
     nModo     = 0;
     aMensa    = "";
     aProg     = "";
     nSwBarra = 0;
     aAvPag = "";
     aRePag = "";
     aTecFin = "";
     aTecla = "";
     &enPreTPV  = 0; || PRECIO TPV
     &eaIVAsn   = "";|| IVA incluido S/N
     &xSerie = "";
     &xNumero = 0;
     Unidades= 0;
     UniEnv  = 0;
     Envase  = "";
     &aParam    = "";
     &enMensaje = 0;
     &enPromoTPV1 = 0;
     &enPromoTPV2 = 0;
     &uni_dt = 0;
     &uni_dt2 = 0;
     &fed_dt = @;   || Fecha....
     &art_dt = "";  || Articulo.
     &cli_dt = "";  || Cliente..
     &fam_dt = "";  || Familia.
     &iva_dt = 0;   || Tipo Iva.
     &dtos_2 = 0;   || Descuento 2.
     &dto_pt = 0;   || Descuento Ptas.
     &dtos_1 = 0;   || Descuento 1.
     &pvp_ve = 0;   || Precio Venta.
     &gru_dt = "";  || Grupo Cliente.
     &tarifa = 0;   || Tarifa Cliente.
     &presel = 0;   || tarifa Seleccion
     &dtosel = 0;   || dto    Seleccion
     &arti = "";
     &diferido = "";
     asto = 0;
     alf1 = "";
     factor = 1;
     &TVisor = 0;
     vacio = "                    ";
     swdevol = 0;
     puente_a                 = "";
     &modo_c                  = 0;
     &almacen                 = 0;
     &n_linea                 = 0;
     &salmenu                 = 0;
     &sw_carga                = 0;
     nerror                   = 0;
     imneto                   = 0;

     modo                     = 0;
     puente_n                 = 0;
     puente_s                 = 0;
     tipo                      = 0;
     &salida                  = 0;
     campo                    = 0;
     iva                      = 0;
     precio                   = 0;
     &c_alter = "";
     || ************************************** *(1)
     i                        = 0;
     &credito = "";
     credito1= "0000 CLIENTE SIN CREDITOS ";
     credito2= "0000 CAJERO SIN CREDITO";
     mensaje                  = "";
     aprograma                = "";
     &linea                   = 0;
     || Variables de repintado (poncontr)
     fiac                     = "  ";
     guarda                   = "  ";
     xrelleno                 = "                              ";
     relleno                  = "                                       ";
     aponer                   = "  ";
     || *************************************
     &exporta                 ="";    || Viene de agifa501
     ncampo                   = 0;
     ind                      = 0;
     numlin                   = 0;
     mensobre = "0000No Existe Sobre !!";
     menfactu = "0000El Sobre ya esta facturado!!";
     valida = "2361SCorrecto S/N: ";
     &aparcar = 0;
     rec = 0;

     &VSobre = 0;
     Valfa1 = "";
     &Vchivato = 0;
     aSob     = "";
     &HeRecogidoSob = 0;

     nGuarda   = 0;
     aBarra    = "";
     aLon      = "";
     nLon      = 0;
     nLon1     = 0;
     aCodBarra = "";
     nNume     = 0;
     aDCodF1   = "";
     aDCodF2   = "";
     aDCodF3   = "";
     aDCodF4   = "";
     aHCodF1   = "";
     aHCodF2   = "";
     aHCodF3   = "";
     aHCodF4   = "";
     pcs       = 0;
     aAlfa     = "";
     aAlf1     = "";
     aAlf2     = "";
     nModiUni  = 0;
     nAuxCal = 0;
     nSal = 0;
|FIN;

|INCLUYE i_varart;
ÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ
|PROCESO T13; |TIPO 13;
     nNume = #910#21;
     alfa = "\" + #910#23 + "+#";

     |CAMPO_ANCHO  #1#27, 1, nNume;
     |CAMPO_MAXIMO #1#27, 1, alfa;
     |CAMPO_ANCHO  #914#0, 1, nNume;
     |CAMPO_MAXIMO #914#0, 1, aAlfa;
     |HAZ PintaDes502;
|FPRC;

||___________________________________/ PROCESOS LLAMADOS DESDE CAMPOS TIPO 7
|PROCESO entra; |TIPO 7;
     |ABRE #279; |LEE 000#279p; |CIERRA #279;
     nPreDtoAnt = 0;
     nSwMulti = 0;
     modo = (FEntrada / 100) ! 0;
     |SI modo_c = 1; |O modo_c = 2;
          |HAZ lee_505;                 ||Cargar parametros CAJA
          |HAZ lee_clien;
          |LIBERA #agifa024;
          |CIERRA #agifa024;
     |FINSI;
     |SI modo = 1;
          aBarra = "";
          |HAZ no_modif;
          |HAZ carga_defec;
     |FINSI;
     |DEFECTO #agifa502#agifa501;
     HeRecogidoSob = 0;
|FPRC;

||___________________________________/ PROCESOS LLAMADOS DESDE CAMPOS TIPO 6
|PROCESO cArtiInf;
     #2#0 = " " * 20;
|FPRC;

|PROCESO cArtiSup;
     #2#0 = "z" * 20;
|FPRC;

|PROCESO tipo66; |TIPO 66;
     nGuarda = FEntrada;
     |PUSHV 0501 2380;
     |CLS;
     |ABRE #2;
     #2#0 = #1#2;
     |LEE 000#2=;
     |SI #4#118 = "R";
          |CONSULTA_CLAVE  #1#2;
          |SI FSalida = 0;
               |PINPA #0#2;
               |PINDA #0#2;
               |PAUSA;
               #1#2 = #2#0;
          |FINSI;
     |SINO;
          |PINPA #0#2;
          |PINDA #0#2;
          |CONSULTA #0#2;
          |SI FSalida = 0;
               #1#2 = #2#0;
          |FINSI;
     |FINSI;
     |CIERRA #2;
     |POPV;
     |ERROR;
     FEntrada = nGuarda;
|FPRC;

|PROCESO tip7; |TIPO 7;
     aAlfa = #1#22; |QBF aAlfa;
     |SI enSwSematel = 0; |Y aAlfa ! ""; |C_M #1#2, 1, "N"; |ACABA;  |FINSI;

     |SI #910#2 ] 3; |Y aAlfa = "";
          eAlfa = #1#2;
          |SUB_EJECUTA_NP "dsz99990"; ||Pide articulo por pantalla
          |SI eAlfa = "-2"; |O eAlfa = "-1";
               |PTEC 807;
          |SINO;
               eaArticulo = eAlfa;      ||  700000-4215

               #1#2 = eAlfa;
               |PTEC 802;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO sal; |TIPO 6;
     |SI Vchivato = 1; |ACABA; |FINSI;

     modo = (FEntrada / 100) ! 0;
     salida = FSalida;
     campo = Campo;

     |C_M #1#6, 0, aAlfa;
     |SI campo = 6; |Y aAlfa = "N"; |Y #1#6 = 0;
          |C_M #1#6, 1, "S";  || Si Precio = 0 y no modificable
          |ENCAMPO #6#1;      || lo pido
          |C_M #1#6, 1, "N";
     |FINSI;

     |SI salida = 23;   ||CTRL-F5 Tarifa Cliente;
          |HAZ SelecTariCli;
          |ERROR; |ACABA;
     |FINSI;

     |SI salida = 25;         || pide unidades (CTRL-F7)
          nModiUni = 1;       || para que lo pida aunque sea modificable
          |C_M #1#5, 0, aAlfa;
          |C_M #1#5, 1, "S";
          |ENCAMPO #5#1;
          |C_M #1#5, 1, aAlfa;
          nModiUni = 0;
          |ERROR;
          |ACABA;
     |FINSI;
     |SI salida = 11;
          |SI #4#140 = "S";
               |SI campo = 22; |O Campo = 2; ||Convierte unidades a negativo
                    |PUSHV 05012380;
                    |BLANCO 1323 1857;
                    |CUADRO 1323 1857;
                    |ATRI S;
                    |SI nUniNega = 0;
                         |PINTA 1525, "ACTIVADO UNIDADES NEGATIVAS";
                         nUniNega = -1;
                    |SINO;
                         |PINTA 1525, "DESACTIVADO UNIDADES NEGATIVAS";
                         nUniNega = 0;
                    |FINSI;
                    |ATRI s;
                    |PAUSA;
                    |POPV;
                    |ERROR; |ACABA;
               |FINSI;
          |SINO;
               |MENAV "0000Funcion no Activa...";
          |FINSI;
     |FINSI;

     |SI salida = 1;                || Escape en campo Unidades
          |SI #4#123 ! "S";
               |PTEC 824;                || Simula F2
               |ERROR; |ACABA;
          |SINO;
               |ERROR; |ACABA;
          |FINSI;
     |FINSI;
     |SI salida = 17;
          salida = -1;
          xSerie = #0#36;
          xNumero = #0#0;
          |SUB_EJECUTA_NP "tmp00501";
          |ERROR; |ACABA;
     |FINSI;

     |SI salida = 9;
          |HAZ pinta_ayudal;
          |ERROR;
     |FINSI;
     |SI salida = 13; |Y #1#1 = 1;||Salir TPV;
          |SI Campo = 5; |O Campo = 29;
               |ERROR;
               |PTEC 807;
               |PTEC 807;
          |FINSI;
     |FINSI;
     |SI salida = 10; |O salida = 18; |O salida = 20; || F2, F10, Ctrl+F2
          |SI salida = 18;    || F10
               #0#57 = #4#89; || Serie defecto
          |SINO;
               #0#57 = #4#105; || Serie oficial
          |FINSI;
          |SI salida = 20;    || Se salta el cobro
               |PTEC 802;
               |PTEC 802;
               |PTEC 802;
               |PTEC 802;
               |SI #4#19 = "S"; |PTEC 802; |FINSI;
          |FINSI;
          |SI campo = 5; |O campo = 2; |O campo = 22; |O campo = 29;
               |ERROR;
               |PTEC 807;
               |PTEC 807;
          |FINSI;
     |FINSI;
     |SI salida = 11;         ||Si es F3     abro el cajon
          |SI #4#116 = "N"; |Y #0#9 > 0;
               |ERROR; |ACABA;
          |FINSI;
          |HAZ AbreCajon;
          |ERROR;
     |FINSI;
     |SI salida = 12; |Y #4#32 = "N";      ||Tiquet aparcado
          |SI modo = 1;
               |HAZ MiraLineas2;
          |SINO;
               nHayLin = 1;
          |FINSI;
          |SI nHayLin = 0;
               |HAZ RecogeAparca;
               |ERROR;
               |SI enNumAparca ! 0;
                    |PTEC 807;
                    |PTEC 807;
               |FINSI;
          |SINO;
               |AVISO;
               |CONFI "2400NEsta seguro de aparcar el Tiquet : ";
               |SI FSalida = 0;
                    #0#35 = "N";
                    #0#57 = "";
                    #0#58 = "";
                    salmenu = 998;
                    |PTEC 807;
                    |PTEC 807;
               |FINSI;
               |ERROR;
          |FINSI;
     |FINSI;
                         ||Pulsar F6 en impresion directa, anulacion de linea
     |SI salida = 14;
          |SI #4#32 ! "S";
               |MENAV "0000Opcion solo activa en impresion directa";
               |ERROR; |ACABA;
          |FINSI;
          |MENSA "2420Que linea desea ANULAR : ";
          puente_a = "";
          E_sup = 3;
          E_inf = "";
          puente_a = 2445 ? 1;
          |HAZ anula_lin;
          |BLIN 24;
     |FINSI;
     |SI salida = 15;    || F7 ... CAMBIA EL CLIENTE.
          |SI #4#115 = "N"; |Y #0#9 > 0;
               |ERROR; |ACABA;
          |FINSI;
          |C_M #0#1, 1, "S";
          |ENCAMPO #1#0; || CODIGO CLIENTE.
          #1#4 = #0#1;
          alfa = #4#35;
          |C_M #0#1, 1, alfa;
          |SALVA_DATOS #1;
          |PARA ind = #1#1; |SI ind ] 1; |HACIENDO ind = ind -1;
               #1#1 = ind;
               |LEE 141#1=;
               |SI FSalida = 0;
                    #1#4 = #0#1;
                    |GRABA 021#1a;
               |FINSI;
          |SIGUE;
          |REPON_DATOS #1;
          |LEE 141#1=;
          |ERROR;
     |FINSI;
     |SI campo = 5; |O campo = 29;
          |SI salida = 2;
               |ERROR;             ||No dejo salir de las unidades con
          |FINSI;                  ||flecha arriba
     |FINSI;
     |SI salida = 16;
           |SI credito = "S";
               |SUB_EJECUTA_NP "agifa571";
               |ERROR;
           |FINSI;
     |FINSI;
     |SI salida = 21;        ||Ctrl+F3 Recalcula
          |SI Campo ! 1;
               |ERROR;
               |PTEC 835; ||Ctr-F3
               |PTEC 807; ||Ctrl+C
          |FINSI;
     |FINSI;

|FPRC;

|PROCESO barra66; |TIPO 66;
     nGuarda = FEntrada;
     modo = (FEntrada / 100) ! 0;
     |PUSHV 0501 2380;
     |CLS;
     |ABRE #2;
     |SI modo ! 1;
          |HAZ HallaBarra;
          |SELECT #4#2;
          #2#128 = aBarra;
          |LEE 000#2=;
     |SINO;
          |LEE 000#2p;
     |FINSI;
     |SI #4#118 = "R";
          |CONSULTA_CLAVE  #1#2;
          salida = FSalida;
          |SI FSalida = 0;
               |PINPA #0#2;
               |PINDA #0#2;
               |PAUSA;
               #1#2 = #2#0;
               #1#22 = #2#128;
          |FINSI;
     |SINO;
          |PINPA #0#2;
          |PINDA #0#2;
          |CONSULTA #0#2;
          salida = FSalida;
          |SI FSalida = 0;
               #1#2 = #2#0;
               #1#22 = #2#128;
          |FINSI;
     |FINSI;
     |POPV;
     |CIERRA #2;
     |SI salida = 0;
          |PINTA #1#2;
          |SI #2#176 = "S";
               |PTEC 816;          || Tecla Fin
               |ERROR;
          |FINSI;
     |SINO;
          |ERROR;
     |FINSI;
     FEntrada = nGuarda;
|FPRC;

|PROCESO no_cambio;
    modo=((FEntrada/100)!0);
     |SI modo=2; ||modificacion
     |Y #agifa502#25 ! "             "; ||Es una linea de sobres
          |MENAV "0000 No se permiten modificar lineas de sobres";
     |FINSI;
|FPRC;

|PROCESO CB_Compuesto;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo ! 1; |ACABA; |FINSI;

     aBarra = #1#22;

     aAlf2 = #4#162; |QBF aAlf2;
     |SI aAlf2 ! "";
          aLon = aAlf2 % A0;
          nLon = aLon;
          nLon = 100 + nLon;
          aAlfa = #1#22 % nLon;
          |SI aAlfa ! aAlf2; |ACABA; |FINSI;
          |SI #4#164 = "N";
               nLon = nLon - 100;
               aAlfa = #1#22; |QBF aAlfa;
               aLon = aAlfa % A0;
               nLon1 = aLon;
               nLon = (100 * (nLon+1)) + (nLon1 - nLon);
               aAlf2 = #1#22 % nLon;
          |SINO;
               aAlf2 = #1#22;      || prefijo incluido en el c.b.
          |FINSI;
     |SINO;
          aAlf2 = #1#22;
     |FINSI;

     alfa1 = (("00" + #4#149) % -102) + (("00" + #4#150) % -102);
     alfa2 = (("00" + #4#151) % -102) + (("00" + #4#152) % -102);
     alfa3 = (("00" + #4#169) % -102) + (("00" + #4#170) % -102);
     alfa4 = (("00" + #4#158) % -102) + (("00" + #4#159) % -102);
     alfa5 = (("00" + #4#160) % -102) + (("00" + #4#161) % -102);
     alfa1 = aAlf2 % alfa1;
     alfa2 = aAlf2 % alfa2;
     alfa3 = "." + aAlf2 % alfa3;
     alfa4 = aAlf2 % alfa4;
     alfa5 = "." + aAlf2 % alfa5;
     |SI #4#150 ! 0;
          #1#2 = alfa1;
     |FINSI;
     |SI #4#152 = 0; |Y #4#170 = 0;
          #1#5 = 1;
     |SINO;
          |SI #4#152 = 0; alfa2 = ""; |FINSI;
          |SI #4#170 = 0; alfa3 = ""; |FINSI;
          #1#5 = alfa2 + alfa3;
     |FINSI;
     |SI #4#159 ! 0; |O #4#161 ! 0;
          |SI #4#159 = 0; alfa4 = ""; |FINSI;
          |SI #4#161 = 0; alfa5 = ""; |FINSI;
          #1#6 = alfa4 + alfa5;
     |FINSI;
     #1#10 = 0;     || Usado de SW para que no calcule el iva 2 veces

     |PINTA #1#2;
     |PINTA #1#5;
     |PINTA #1#6;

     |ABRE #2;
     #2#0 = #1#2;
     |LEE 000#2=;
     |CIERRA #2;
     #1#22 = #2#128; |PINTA #1#22;
     aBarra = #1#22;
|FPRC;

|PROCESO CB_Elemento;
     |ABRE #940;
     |SELECT #2#940;
     #940#2 = #1#22;
     |LEE 000#940=;
     |SI FSalida = 0;
          #1#2 = #940#0;  |PINTA #1#2;
          #1#27 = #940#1; |PINTA #1#27;
          |ABRE #2;
          #2#0 = #1#2;
          |LEE 000#2=;
          |CIERRA #2;
          #1#22 = #2#128; |PINTA #1#22;
          aBarra = #1#22;
     |SINO;
          aLon = aCodBarra % A0;
          nLon = aLon;
          nLon = (100 + nLon - #910#21);
          aBarra = aCodBarra % nLon;

          nLon = -(100 + #910#21);
          alfa = aCodBarra % nLon;
          #1#27 = alfa;
          |SI aCodBarra ! "";
               |ABRE #2;
               |SELECT #4#2;
               #2#128 = #1#22;
               |LEE 000#2=;
               |SI FSalida = 0;
                    #1#27 = "";
                    aBarra = #1#22;
                    |ABRE #66; |SELECT #2#66;
                    #66#3 = #1#22;
                    |LEE 000#66=;
                    |SI FSalida = 0; |Y #66#6 ! 0; |Y nSwMulti = 0;
                         nSwMulti = 1;
                         #1#5 = #1#5 * #66#6;
                         |PINTA #1#5;
                    |FINSI;
                    |CIERRA #66;
               |FINSI;
               |CIERRA #2;
          |FINSI;
          |QBF aBarra;
          |SI aBarra = "";        || Si es blanco pongo basura
               aBarra = "*kk_kk*";|| (no creo que exista un CB como este !!!)
          |FINSI;
     |FINSI;
     |CIERRA #940;
|FPRC;

|PROCESO HallaBarra;
     enSwBarra = 0;
     aBarra = "";
     aCodBarra = #1#22; |QBF aCodBarra;
     |MODULO aProg;
     |SI aProg = "agifa501";   || EL NORMAL (el agifa503 es para doble stock)
          |SI #4#148 = "S";
               |HAZ CB_Compuesto; || El CB trae el articulo y las unidades
          |SINO;
               |HAZ CB_Elemento;  || El CB trae el Elemento (color)
          |FINSI;
     |SINO;                    || DOBLE STOCK (no hay 5 Elemento)
          |SI aCodBarra ! "";
               aBarra = #1#22;
          |SINO;
               aBarra = "*kk_kk*";|| (no creo que exista un CB como este !!!)
               enSwBarra = 1;     || Para saber en el dsz99998 si introduce
          |FINSI;                 || por codigo de barras o articulo
     |FINSI;
|FPRC;

|PROCESO ControlTecla;
     |AVISO;
     |PUSHV 0501 2380;
     |BLANCO 1322 2160;
     |CUADRO 1322 2160;
     |ATRI S; |PINTA 1425, "       CODIGO INEXISTENTE      "; |ATRI 0;
     aAvPag = &255 + 814;
     aRePag = &255 + 815;
     aTecFin = &255 + 816;
     |SI nSwBarra = 1;
          |SI #4#122 = "X";
               |ATRI I; |PINTA 1625, " Pulse Fin para Alta Cod.Barras"; |ATRI 0;
               |ATRI I; |PINTA 1825, " Pulse Re.Pag para Cod.Barras  "; |ATRI 0;
               |ATRI I; |PINTA 2025, " Pulse Av.Pag para Cod.Articulo"; |ATRI 0;
          |SINO;
               |ATRI I; |PINTA 1725, " Pulse Re.Pag para Cod.Barras  "; |ATRI 0;
               |ATRI I; |PINTA 1925, " Pulse Av.Pag para Cod.Articulo"; |ATRI 0;
          |FINSI;
     |SINO;
          |ATRI I; |PINTA 1925, "   Pulse Av.Pag para Continuar "; |ATRI 0;
          aAvPag = aRePag;
     |FINSI;
     |PARA; |SI; |HACIENDO;
          |LEETECLA aTecla;
          |SI aTecla = aAvPag; |O aTecla = aRePag; |O aTecla = aTecFin;
               |SAL;
          |FINSI;
     |SIGUE;
     |POPV;
     |SI aTecla = aAvPag; |O aTecla = aTecFin;
          |SI aTecla = aTecFin;
               eaCodBar = "";
               |SUB_EJECUTA_NP "dsz00068";
               #1#22 = eaCodBar; |PINTA #1#22;
          |FINSI;
          |ERROR;
     |SINO;
          |C_M #1#2, 1, "S";
          #1#22 = ""; |PINTA #1#22;
     |FINSI;
|FPRC;

|PROCESO barras; |TIPO 0;
     |SI #4#25 = "N"; |ACABA; |FINSI;

     |HAZ HallaBarra;
     modo = (FEntrada / 100) ! 0;

     |HAZ EsCoverin;
     |SI FSalida = 0;
          |SI HeRecogidoSob = 0;
               |HAZ VSobres;
               |SI VSobre ! 0;     |ERROR;    |ACABA;     |FINSI;
          |FINSI;
     |FINSI;

     |SI c_alter = "N";
          |ABRE #2;
          |SELECT #4#2;              ||Selecciono clave Codigo  Barras
          #2#128 = aBarra;
          |LEE 000#2=;
          |SI FSalida ! 0;
               #2#128 = #1#22;
               |LEE 000#2=;
          |FINSI;
          |SI FSalida = 0;|Y #1#22 ! "                    ";
               |HAZ carga_art;
               |ABRE #66;
               |SELECT #2#66;
               #66#3 = #1#22;
               |LEE 000#66=;
               |SI FSalida = 0; |Y #66#6 ! 0; |Y nSwMulti = 0;
                    nSwMulti = 1;
                    #1#5 = #1#5 * #66#6; |PINTA #1#5;
               |FINSI;
               |CIERRA #66;
          |SINO;
               |ABRE #66;
               |SELECT #2#66;
               #66#3 = aBarra;
               |LEE 001#66=;
               |SI FSalida ! 0;
                    #66#3 = #1#22;
                    |LEE 001#66=;
               |FINSI;
               |SI FSalida = 0;|Y #1#22 ! "                    ";
                     |SI #66#6 ! 0; |Y nSwMulti = 0;
                         nSwMulti = 1;
                         #1#5 = #1#5 * #66#6; |PINTA #1#5;
                     |FINSI;
                     |SELECT #1#2;
                     #2#0 = #66#1;
                     |LEE 001#2=;
                     |SI FSalida = 0;
                         |HAZ carga_art;
                     |FINSI;
               |SINO;
                    |SI #4#25 = "A";
                         |SI #4#122 = "S"; |O #4#122 = "X";
                              nSwBarra = 1;
                              |HAZ ControlTecla;
                         |SINO;
                              #1#22 = ""; |PINTA #1#22;
                              |C_M #1#2, 1, "S";
                         |FINSI;
                    |SINO;
                         |SI #4#122 = "S"; |O #4#122 = "X";
                              nSwBarra = 0;
                              |HAZ ControlTecla;
                         |SINO;
                              |AVISO;
                              |MENAV "0000El Codigo no EXISTE...";
                              |ERROR;
                         |FINSI;
                    |FINSI;
               |FINSI;
          |FINSI;
          |CIERRA #2;
          |CIERRA #66;
    |SINO; ||-------------------------- CODIGOS ALTERNATIVOS;
          |ABRE #28;
          #28#0 = aBarra;
          |LEE 000#28=;
          |SI FSalida ! 0;
               #28#0 = #1#22;
               |LEE 001#28=;
          |FINSI;
          |SI FSalida = 0;
               factor = #28#4;
               #1#5 = #1#5 * factor;
               |PINTA #1#5;
               |ABRE #2;
               #2#0 = #28#2;
               |LEE 001#2=;
               |HAZ carga_art;
               |CIERRA #2;
          |SINO;
               |ABRE #2;
               |SELECT #4#2;                 ||Selecciono clave Codigo  Barras
               #2#128 = aBarra;
               |LEE 001#2=;
               |SI FSalida ! 0;
                    #2#128 = #1#22;
                    |LEE 001#2=;
               |FINSI;
               |SI FSalida = 0;
                    |HAZ carga_art;
               |SINO;
                    |SI #4#25 = "A";
                         |SI #4#122 = "S"; |O #4#122 = "X";
                              nSwBarra = 1;
                              |HAZ ControlTecla;
                         |SINO;
                              #1#22 = ""; |PINTA #1#22;
                              |C_M #1#2, 1, "S";
                         |FINSI;
                    |SINO;
                         |SI #4#122 = "S"; |O #4#122 = "X";
                              nSwBarra = 0;
                              |HAZ ControlTecla;
                         |SINO;
                              |AVISO;
                              |MENAV "0000El Codigo no EXISTE...";
                              |ERROR;
                         |FINSI;
                    |FINSI;
               |FINSI;
               |CIERRA #2;
          |FINSI;
          |CIERRA #28;
    |FINSI;
|FPRC;

|PROCESO ErrChequea;
     |MENAV "     Elemento no configurado en el Fichero de Defectos.";
     |ERROR;
|FPRC;

|PROCESO Chequea;
     eAlfa = #1#2;  |SUB_EJECUTA_NP "dsz99990;V";
     aDCodF1 = " " * 20;
     aDCodF2 = " " * 20;
     aDCodF3 = " " * 20;
     aDCodF4 = " " * 20;
     |SI #910#26 = 1;
         aDCodF1 = eAlfa1;
     |FINSI;
     |SI #910#26 = 2;
         aDCodF1 = eAlfa1;
         aDCodF2 = eAlfa2;
     |FINSI;
     |SI #910#26 = 3;
         aDCodF1 = eAlfa1;
         aDCodF2 = eAlfa2;
         aDCodF3 = eAlfa3;
     |FINSI;
     |SI #910#26 = 4;
         aDCodF1 = eAlfa1;
         aDCodF2 = eAlfa2;
         aDCodF3 = eAlfa3;
         aDCodF4 = eAlfa4;
     |FINSI;

     |SI #910#44 = "T";
         eAlfa1  = #2#177;  |QBF eAlfa1;
         eAlfa1  = eAlfa1 + ((" " * 20) % 120);

         aDCodF1 = eAlfa1;     aHCodF1 = eAlfa1;
         aDCodF2 = " " * 20;   aHCodF2 = " " * 20;
         aDCodF3 = " " * 20;   aHCodF3 = " " * 20;
         aDCodF4 = " " * 20;   aHCodF4 = " " * 20;
     |FINSI;

     |ABRE #916;
     #916#0  = aDCodF1;
     #916#1  = aDCodF2;
     #916#2  = aDCodF3;
     #916#15 = aDCodF4;
     #916#3 = #1#27;
     |LEE 040#916=;
     |SI FSalida ! 0;
          |SI #910#25 = "F"; |HAZ ErrChequea; |FINSI;
          |SI #910#25 = "S"; |Y #910#27 = "S"; |HAZ ErrChequea; |FINSI;
     |FINSI;
     |CIERRA #916;
|FPRC;

|PROCESO articulo; |TIPO 6;
     eaEsTPV = 1;
     eaArticulo = #1Campo;
     |HAZ LlenaArticulo;
     |SI FSalida = 0;
          #1Campo = eaArticulo;
          |PINTA #1Campo;
     |SINO;
          |ERROR; |ACABA;
     |FINSI;

     |ABRE #2;
     #2#0 = #1#2;
     |LEE 000#2=;
     |SI FSalida ! 0;
          |SI #4#122 = "S"; |O #4#122 = "X";
               nSwBarra = 0;
               |HAZ ControlTecla;
               |ACABA;
          |SINO;
               |MENAV "0000Codigo inexistente...";
               |ERROR; |ACABA;
          |FINSI;
     |FINSI;
     |CIERRA #2;

     |C_M #1#2, 0, alfa;
     |SI #2#176 = "S"; |Y alfa = "S";
          |C_M #1#27, 1, "S";
          |ENCAMPO #27#1;
          |SI FSalida = 9; |O FSalida = 1;
               |C_M #1#27, 1, "N";
               |PTEC 807; |ACABA;
          |FINSI;
          |C_M #1#27, 1, "N";

          alfa = #910#23 * #910#21;
          alf1 = #1#27; |QBF alf1;
          nNume = -(#910#21 + 100)
          alfa = ( alfa + alf1 ) % nNume;

          |HAZ Chequea;

     |FINSI;
     |HAZ carga_art;
     |HAZ SacaKit;
|FPRC;

|PROCESO PrecioElemento;
     |SI #14#39 = 1;  pvp_ve = #916#5; pcs = 130; |FINSI;
     |SI #14#39 = 2;  pvp_ve = #916#6; pcs = 131; |FINSI;
     |SI #14#39 = 3;  pvp_ve = #916#7; pcs = 132; |FINSI;
     |SI #14#39 = 4;  pvp_ve = #916#8; pcs = 153; |FINSI;
     |SI #14#39 = 5;  pvp_ve = #916#9; pcs = 154; |FINSI;
     |SI #14#39 = 6;  pvp_ve = #916#10; pcs = 155;|FINSI;
     |SI #14#39 = 0;  pvp_ve = #916#11; pcs = ""; |FINSI;
     enPreTPV = pvp_ve;
     eaIVAsn  = #2pcs;
|FPRC;

|PROCESO carga_art;
     modo = (FEntrada / 100) ! 0;
     |SI modo = 2;|Y #1Campo = Contenido;
          |ACABA;
     |FINSI;
     |DEFECTO #1#2;
     #1#8 = #2#1;              ||descripcion
     #1#2 = #2#0;              ||codigo
     aCodBarra = #2#128;
     |QBF aCodBarra;
     aCodBarra = aCodBarra + #1#27;
     |QBF aCodBarra;
     #1#22 = aCodBarra;        ||Barras
     #1#11 = #2#2;             ||Familia
     #1#12 = #2#3;             ||Subfamilia
     #1#13 = #2#30;            ||Tipos de iva

     eaArticulo = #1#2;
     eaCliente = #0#1;
     |HAZ DefectoMediV;
     #1#32 = enMedida1;
     #1#33 = enMedida2;
     #1#34 = enMedida3;

     |SI swdevol = 0;
          uni_dt = #1#5;   || Unidades
          uni_dt2 = #1#29;   || Unidades
          fed_dt = #0#14;  || Fecha....
          art_dt = #1#2;   || Articulo.
          cli_dt = #1#4;   || Cliente..
          fam_dt = #1#11;  || Familia.
          iva_dt = #1#13;  || Tipo Iva.
          dtos_2 = 0;      || Descuento 2.
          dto_pt = 0;      || Descuento Ptas.
          dtos_1 = 0;      || Descuento 1.
          pvp_ve = 0;      || Precio Venta.
          ||tarifa = #14#39; || Tarifa Cliente....
          tarifa = enTariCli;
          gru_dt = #14#158;|| Grupo Cliente.
          presel = 0;      || Precio seleccion.
          dtosel = 0;      || Dto      '' ''
          uni_dt = #1#5;
          uni_dt2 = #1#29;
          enMensaje = 0;
          aParam = "";
          eaSerie = #1#20;
          enDirEnt = 0;
          |SI #2#176 = "S";  enMensaje = 1;  |FINSI;
          |SI #4#146 = "S";  enPromoTPV1 = 1; |FINSI;
          |SI #4#147 = "S";  enPromoTPV2 = 1; |FINSI;
          enAlmacen = #0#3;
          |HAZ calcdtos;
          |SI #910#24 = "S"; |Y #2#176 = "S";
               |HAZ Chequea;
               |HAZ PrecioElemento;
          |FINSI;
          #1#14  = dtos_1; |PINTA #1#14;            || 1¦ Dto.

          |SI exporta = "S"; #1#13 = 0; |FINSI;
          eaCli = #0#1; enTiva = #1#13; efFiva = #0#14; |HAZ IVACli;

          || CODIGO BARRAS COMPUESTO
          |SI #4#159 ! 0; |Y #4#148 = "S"; |Y #1#6 ! 0;
               enPreTPV = #1#6;
               |SI #1#10 = 0;    ||Para que no lo haga 2 veces el iva
                    |SI eaIVAsn = "N";
                         #1#10 = enPreTPV;
                         #1#6 = enPreTPV + (enPreTPV * enIva / 100);
                    |SINO;
                         #1#6 = enPreTPV;
                         #1#10 = enPreTPV / (1 + (enIva / 100));
                    |FINSI;
               |FINSI;
          |SINO;
               |SI eaIVAsn = "N";
                    #1#10 = enPreTPV ! DPRE;
                    #1#6 = enPreTPV + (enPreTPV * enIva / 100);
               |SINO;
                    #1#6 = enPreTPV ! DPRE;
                    #1#10 = enPreTPV / (1 + (enIva / 100));
               |FINSI;
          |FINSI;
     |FINSI;

     |HAZ totli;
     |PINTA #1#2;
     |PINTA #1#8;
     |PINTA #1#22;
     |PINTA #1#27;

     |SI #4#45 = #2#125;
          |HAZ si_modif;
     |FINSI;
|FPRC;

|PROCESO Precio; |TIPO 6;
     nSal = FSalida;
     |SI #4#109 = "S";   || Lo vemos con IVA
          |SI eaIVAsn = "N";
               |SI Campo = 6;
                    #1#10 = #1#6 / (1 + (enIva / 100));
                    #1#10 = #1#10 ! DPRE;
               |FINSI;
          |FINSI;
     |SINO;              || Lo vemos sin IVA
          |SI eaIVAsn = "S";
               |SI Campo = 10;
                    #1#6 = #1#10 + (#1#10 * enIva / 100);
                    #1#6 = #1#6 ! DPRE;
               |FINSI;
          |FINSI;
     |FINSI;
     |SI #279#3 = "S";
          eaArticulo = #1#2;
          enPrecio = #1#10;
          enDto1 = #1#14;
          enDto2 = 0;
          nNume =  (enPrecio < enDto1) < enDto2;
          |SI nNume ! nPreDtoAnt;
               |SUB_EJECUTA_NP "dsz00100";
               |SI enError ! 0; |ERROR; |ACABA; |FINSI;
               nPreDtoAnt = nNume;
          |FINSI;
     |FINSI;
     |SI nSal = 13;
          |HAZ EsAparecida;
          |SI FSalida = 0;
               enPrecio = #1Campo;
               eaArticulo = #1#2;
               eaCliente = #0#1;
               |SUB_EJECUTA_NP "aparm010";
               #1Campo = enPrecio; |PINTA #1Campo;
               |HAZ totli;
               |ERROR;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Dto0_502; |TIPO 0;
     |SI #279#3 = "S";
          eaArticulo = #1#2;
          enPrecio = #1#10;
          enDto1 = #1#14;
          enDto2 = 0;
          nNume =  (enPrecio < enDto1) < enDto2;
          |SI nNume ! nPreDtoAnt;
               |SUB_EJECUTA_NP "dsz00100";
               |SI enError ! 0; |ERROR; |ACABA; |FINSI;
               nPreDtoAnt = nNume;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO totli; |TIPO 0;      ||desde unidades,precio,.....
     |ABRE #2;
     #2#0 = #1#2;
     |LEE 020#2=;
     |CIERRA #2;

     #1#11 = #2#2;             ||Familia
     #1#12 = #2#3;             ||Subfamilia
     #1#13 = #2#30;            ||Tipos de iva

     |SI #1#5 < 0; |Y #4#95 = "S"; |Y Campo = 5;
          |HAZ Clave;
          alf1 = #4#96; |QBF alf1;
          |SI alf1 ! aClave;
               |MENAV "0000Clave incorrecta...";
               #1#5 = 0;
               |ERROR; |ACABA;
          |FINSI;
     |FINSI;

     eaCli = #0#1; enTiva = #1#13; efFiva = #0#14; |HAZ IVACli;

     |SI #0#29 = "S"; rec = enRec; |SINO; rec = 0; |FINSI; || Recargo


     |SI eaIVAsn = "S";
          #1#10 = #1#6 / (1 + (enIva / 100));
          nTotal = ((#1#6 * #1#5) < #1#14) ! EURODB;
          nBase = (nTotal / (1 + (enIva / 100))) ! EURODB;
          nIva = nTotal - nBase;
          #1#7 = nTotal;
          #1#21 = nBase;
          #1#19 = nIva;
     |SINO;
          nBase = ((#1#10 * #1#5) < #1#14);
          #1#7 = (nBase + (nBase * enIva / 100)) ! EURODB;
          #1#21 = nBase ! EURODB;
          #1#19 = #1#7 - #1#21;
          #1#6 = #1#10 + (#1#10 * enIva / 100);
     |FINSI;

     |SI #4#109 = "S";
          #1#6 = #1#6 ! DPRE;
          |PINTA #1#7;
          |PINTA #1#6;
     |SINO;
          #1#10 = #1#10 ! DPRE;
          |PINTA #1#21;
          |PINTA #1#10;
     |FINSI;
     |PINTA  #1#5;
|FPRC;


||_______________________________________/ PROCESOS DE ACTUALIZACIONES
|PROCESO ActLin;
     |SI #1#21 > 0;
         nerror = 1;
     |SINO;
         nerror = -1;
         #1#21 =  -#1#21;
     |FINSI;
     imneto = ((((#1#21 < #0#17) < #0#19) < #0#18)) * nerror;
     #1#21 = #1#21 * nerror;

     |SI diferido = "S"; |ACABA; |FINSI;

     ||-------------------Actualiza Historico, Articulo y Almacen
     ||enForma   =   (la cargo en el tipo2)
     |HAZ AcumulaTPV;
     #1#23 = enCoste;
     ||--------------------
     #0#8 = #0#8 + (imneto - enCoste);
     ||--------------------------------- El 5 Elemento
     |ABRE #2;
     #2#0 = #1#2;
     |LEE 000#2=;
     |CIERRA #2;
     |SI #2#176 = "S";
          |ABRE #904;
          |DDEFECTO #904;
          #904#0 = #1#2;
          #904#1 = #1#3;
          #904#2 = #1#27;
          |LEE 101#904=;
          |SI FSalida ! 0; |GRABA 020#904b; |FINSI;
          #904#3 = #1#28;
          #904#12 = #904#12 - (#1#5 * enForma);
          #904#13 = #904#13 - (#1#5 * enForma);
          |GRABA 020#904a;
          |CIERRA #904;
     |FINSI;
|FPRC;

|PROCESO t2_502; |TIPO 2;
     tipo = 0 +. 1;
     enForma = 0 +. 1;
     modo = (FEntrada / 100) ! 0;

     aSob=#1#25; |QBF aSob;
     |SI aSob ! "";      |ACABA;   |FINSI;

     |SI modo = 1; |Y enForma = 1; |Y nUniNega = -1;
          #1#5 = -#1#5;                 ||Convierte unidades a negativo
          #1#29 = -#1#29;               || <F3> en campo C.B. o Arti
          ||HAZ totli;
          nUniNega = 0;
     |FINSI;

    |HAZ totli;

     #0#76 = #0#76 +. #1#5;
     |PINTA #0#76;

     |SI modo = 1;
          |HAZ actua_cabe;
     |FINSI;
     |SI modo = 2; |O modo = 3;
          |HAZ no_modif;
          |SI #4#32 = "S";
               |SI tipo = -1;
                    |MENAV "0000ATENCION registro no modificable en Impresion Directa";
               |FINSI;
               |ERROR;
               |ACABA;
          |FINSI;
          |HAZ actua_cabe;
     |FINSI;
     |SI n_linea = 0; |Y #4#32 = "S";      ||Es un tiquet directo
          #0#57 = #4#105;  ||Serie Oficial
          |HAZ lee_serie;
          |HAZ abre_impre;
     |FINSI;
     |SI #4#32 = "S";
        |HAZ imprelineas;
     |FINSI;
     |SI #1#1 > n_linea;
          n_linea = #1#1;        ||Antes era un contador sin el SI-FINSI
     |FINSI;

     |HAZ ActLin;

     |SI #4#109 = "N";   || Presenta Lineas sin IVA
          enTiva = #agifa502#13;
          enPtoVerde = #agifa502#42;
          |HAZ Recal501_Sin;
     |FINSI;

     |SI enSwSematel = 0;
          enFormaIMEI = tipo;
          enModoIMEI = modo;
          eaSerIMEI = #1#20;
          enDocIMEI = #1#0;
          enLinIMEI = #1#1;
          |SUB_EJECUTA_NP "tlfw0003";
     |FINSI;

     |SI modo = 3;
          |HAZ BorraKit;
     |FINSI;
     |HAZ PintaVisor;
|FPRC;

|PROCESO actua_cabe;
     |SI tipo = 1;
          |C_M #1#1 , 1 , "S";
     |FINSI;

     |ABRE #2;
     #2#0 = #1#2;
     |LEE 020#2=;
     |CIERRA #2;

     eaCli = #0#1; enTiva = #1#13; efFiva = #0#14; |HAZ IVACli;

     ||**** OJO SI SE MODIFICA REPAZAR CALCULO 'recal501' ********
     rec = enRec;
     |SI #0#29 ! "S"; rec = 0; |FINSI;

     #1#42 = #1#5 * #2#208; ||P.Verde
     #0#77 = #0#77 +. #1#42;

     #1#3 = almacen;
     #1#23 = #2#9 * #1#5;         ||Coste linea

     #0#3 = #0#3 +. #1#21;                   ||Total bruto
     nRec = 0;

     |SI #4#109 = "N";
          nNeto = ((#1#21 < #0#17) < #0#19) < #0#18;
          nNeto = nNeto ! EURODB;
          nIva = (nNeto % enIva) ! EURODB;
          nRec = (nNeto % rec) ! EURODB;
          #0#28 = #0#28 +. (#1#21 - nNeto);        ||Total descuentos
          #0#9 = #0#9 +. (nNeto + nIva + nRec);    ||Total
     |SINO;
          ||TODO CCC
          |SI rec = 0;
               nAuxCal = #1#7;
          |SINO;
               nRec = #1#21 % rec;
               nAuxCal = #1#7 + nRec;
          |FINSI;

          nNeto = (((nAuxCal < #0#17) < #0#19) < #0#18) ! EURODB;
          nBase = (nNeto / (1 + ((enIva + rec) / 100) )) ! EURODB;
          |SI rec = 0;
               nIva = (nNeto - nBase) ! EURODB;
          |SINO;
               nIva = (nBase % enIva) ! EURODB;
               nRec = nNeto - nBase - nIva;
          |FINSI;
          #0#28 = #0#28 +. (#1#21 - nBase);        ||Total descuentos
          #0#9 = #0#9 +. (nBase + nIva + nRec);    ||Total
          nNeto = nBase;
     |FINSI;

     |SI #1#13 = 0;
          #0#30 = #0#30 +. nNeto;
     |FINSI;
     |SI #1#13 = 1;
           #0#20 = #0#20 +. nNeto;
           #0#21 = #0#21 +. nIva;
           #0#22 = #0#22 +. nRec;
     |FINSI;
     |SI #1#13 = 2;
          #0#23 = #0#23 +. nNeto;
          #0#24 = #0#24 +. nIva;
          #0#25 = #0#25 +. nRec;
     |FINSI;
     |SI #1#13 = 3;
          #0#26 = #0#26 +. nNeto;
          #0#27 = #0#27 +. nIva;
          #0#47 = #0#47 +. nRec;
     |FINSI;
     |SI #1#13 = 4;
          #0#48 = #0#48 +. nNeto;
          #0#49 = #0#49 +. nIva;
          #0#50 = #0#50 +. nRec;
     |FINSI;
     |SI #1#13 = 5;
          #0#51 = #0#51 +. nNeto;
          #0#52 = #0#52 +. nIva;
          #0#53 = #0#53 +. nRec;
     |FINSI;
     #0#4=#0#21+#0#22+#0#24+#0#25+#0#27+#0#47+#0#49+#0#50+#0#52+#0#53;
     |HAZ PintaVisor;
|FPRC;


||___________________________________/ PROCESOS VARIOS

|PROCESO pinta_ayudal;
     |PUSHV 0501 2480;
     |ATRI I;
     |BLANCO 0652 2279;
     |CUADRO 0652 2279;
     |PINTA 0753, "      A  Y  U  D  A     ";
     |PINTA 0953, "F1 - Ayuda";
     |PINTA 1053, "F2 - Cobrar Tiquet";
     |PINTA 1153, "F3 - Abrir Cajon";
     |PINTA 1253, "F4 - Aparcar/Recupera";
     |PINTA 1353, "F5 - Salir";
     |PINTA 1453, "F6 - Anula Linea N§";
     |PINTA 1553, "F7 - Cambia Cliente";
     |PINTA 1653, "F8 - Creditos";
     |PINTA 1753, "F9 - Re/Impresion";
     |PINTA 1853, "F10- Cobrar No Imprime";
     |PINTA 1953, "Ctrl+F2 Salta Cobro   ";
     |PINTA 2053, "Ctrl+F3 Recalcula  ";
     |PINTA 2153, "Ctrl+F5 Tarifa Cliente";
     |PAUSA;
     |ATRI 0;
     |POPV;
|FPRC;

|PROCESO anula_lin;                ||Anulacion de una linea
     puente_n = #1#1;
     #1#20 = #0#36;
     #1#0 = #0#0;
     #1#1 = puente_a;
     |LEE 001#1=;
     |SI FSalida = 0;
          #1#5 = - #1#5;
          |HAZ totli;
          |PINDA #0#1;
          |SI campo = 1;
               #1#1 = n_linea + 1;
               |GRABA 021#1b;
          |SINO;
               #1#1 = puente_n;
          |FINSI;
          |PINTA #1#1;
          |PTEC 815;
     |FINSI;
|FPRC;

|PROCESO carga_defec;         ||Cargo los valores defecto del fichero
                              ||de parametrizaciones
     #1#5 = #4#11;   ||Unidades
     |C_V #1#5, 0, aAlfa;
     |SI aAlfa = "S";|PINTA #1#5; |FINSI;

     #1#29 = #4#127; || Unidades
     |C_V #1#29, 0, aAlfa;
     |SI aAlfa = "S";|PINTA #1#29; |FINSI;

     #1#30 = #4#126; || Partida

     #1#2 = #4#13;    ||Articulo
     |PINTA #1#2;

|FPRC;


|PROCESO no_modif;            ||Pongo los campos como no modificables
     |C_M #1#27, 1, "N";

     puente_a = #4#9;
     |C_M #1#1 , 1 , puente_a;       ||Linea Tiquet


     |SI nModiUni = 0;
          puente_a = #4#10;
          |C_M #1#5 , 1 , puente_a;       ||Unidades
     |SINO;
          |C_M #1#5 , 1 , "S";            ||Unidades con F4, siempre modific.
     |FINSI;

     puente_a = #4#25;
     |SI puente_a = "S";
          |C_M #1#22 , 1 , "S";      ||Codigo de Barras
          |C_M #1#2 , 1 ,  "N";      ||Codigo Articulo
     |FINSI;
     |SI puente_a = "A";
          |C_M #1#22 , 1 , "S";      ||Codigo de Barras
          |C_M #1#2 , 1 ,  "S";      ||Codigo Articulo
     |FINSI;
     |SI puente_a = "N";
          |C_M #1#22 , 1 , "N";      ||Codigo de Barras
          |C_M #1#2 , 1 ,  "S";      ||Codigo Articulo
     |FINSI;

     |SI puente_a = "N";
          |C_M #1#2 , 1 , "S";       ||Articulo
     |SINO;
          |C_M #1#2 , 1 , "N";       ||Articulo
     |FINSI;


     puente_a = #4#14;
     |C_M #1#8 , 1 , puente_a;       ||Descripcion

     puente_a = #4#15;
     |SI #4#109 = "S";
        |C_M #1#6 , 1 , puente_a;       ||P.V.P
     |SINO;
        |C_M #1#10 , 1 , puente_a;       ||P.V.P
     |FINSI;

     puente_a = #4#16;
     |C_M #1#14 , 1 , puente_a;      ||Dto

     puente_a = #4#17;
     |SI #4#109 = "S";
          |C_M #1#7 , 1 , puente_a;       ||Importe
     |SINO;
          |C_M #1#21 , 1 , puente_a;       ||Importe
     |FINSI;
|FPRC;

|PROCESO si_modif;          ||Pongo los campos como si modificables
     |C_M #1#1,1,"S";       ||Linea Tiquet
     |C_M #1#5,1,"S";       ||Unidades

     |C_M #1#2,1,"S";       ||Articulo
     |C_M #1#8,1,"S";       ||Descripcion
     |C_M #1#6,1,"S";       ||P.V.P
     |C_M #1#14,1,"S";      ||Dto
     |C_M #1#7,1,"S";       ||Importe

||     |C_M #1#5, 1, "N";
|FPRC;

|PROCESO lee_505;             ||cargo las parametrizaciones, una sola vez
     |SI sw_carga = 0;
          |ABRE #agifa505;
          #4#0 = #agifa501#12;
          |LEE 001#4=;
          |SI FSalida ! 0; |DDEFECTO #agifa505; |FINSI;
          |CIERRA #agifa505;
          eaPathBascula = #4#130; |QBF eaPathBascula; || Para Doble Stock
          eaPesoBascula = #4#131;                    ||   ""
          eaPesBascula = #4#136;
          eaExeBascula = #4#135;
          eaPreBascula = #4#137;
          eaModiUni2   = #4#138;
          enDefeUni2   = #4#127;
     |FINSI;
     sw_carga = sw_carga + 1;      ||aprovecho como contador
                                   ||para saber el numero de lineas
|FPRC;

|PROCESO MiraLineas2;          || Con referencia no se va el puntero!!!
     nHayLin = 0;
     |DDEF aDef;
     aDef = aDef + "agifa502";
     |CARGA_DEF aDef, agifa502@;
     |SI FSalida = 0;
          |ABRE #agifa502@;
          #agifa502@#20 = #0#36;
          #agifa502@#0 = #0#0;
          #agifa502@#1 = 1;
          |LEE 000#agifa502@.];
          |PARA; |SI FSalida = 0; |Y #agifa502@#20 = #0#36; |Y #agifa502@#0 = #0#0; |HACIENDO;
               |SI #agifa502@#1 = #1#1;
                    |LEE 000#agifa502@.s;
               |SINO;
                    nHayLin = 1;
                    |SAL;
               |FINSI;
          |SIGUE;
          |CIERRA #agifa502@;
          |DESCARGA_DEF #agifa502@;
     |FINSI;
|FPRC;

|PROCESO MiraLineas;          || Con referencia no se va el puntero!!!
     nHayLin = 1;
     |DDEF aDef;
     aDef = aDef + "agifa502";
     |CARGA_DEF aDef, agifa502@;
     |SI FSalida = 0;
          |ABRE #agifa502@;
          #agifa502@#20 = #0#36;
          #agifa502@#0 = #0#0;
          #agifa502@#1 = 1;
          |LEE 000#agifa502@.];
          |SI FSalida ! 0; |O #agifa502@#20 ! #0#36; |O #agifa502@#0 ! #0#0;
               nHayLin = 0;
          |FINSI;
          |CIERRA #agifa502@;
          |DESCARGA_DEF #agifa502@;
     |FINSI;
|FPRC;

|PROCESO externo; |TIPO 11;
     nTecla = FSalida;
     swdevol = 0;
     nModo = (FEntrada / 100) ! 0;


     |SI nTecla = 13;
          eaArticulo = #1#2;
          |HAZ EsKit;
          |SI FSalida = 0;
               |HAZ VerKit;
          |FINSI;
     |FINSI;
     |SI nTecla = 13; |Y #32#117 = "S"; |Y nModo ! 1; || F5 (Ver Doble Stock)
          |HAZ VerDobleUniV;
          |ERROR; |ACABA;
     |FINSI;
     |SI nTecla = 13; |Y nModo ! 1; |Y enSwSematel = 0; || F5 (Ver IMEI)
          enFormaIMEI = 0;
          enModoIMEI = 4;
          eaSerIMEI = #1#20;
          enDocIMEI = #1#0;
          enLinIMEI = #1#1;
          |SUB_EJECUTA_NP "tlfw0003";
          |ERROR; |ACABA;
     |FINSI;
     |SI nModo = 1; |O nModo = 2;
          |SI nTecla = 1; |O nTecla = 7;
               |HAZ MiraLineas;
               |SI salmenu ! 998; |Y nHayLin ! 0;
                    |HAZ FinTiquet;
                    |SI salmenu = -1;
                         |ERROR;
                         |SI #4#9 = "N"; |PTEC 802; |FINSI;
                    |FINSI;
               |FINSI;
          |FINSI;
     |FINSI;

     |SI nTecla = 21;  || Ctrl+F3
          |HAZ Recal501;
          |ERROR;
          |PTEC 815; ||Av.Pag.
          |PTEC 814; ||Re.Pag
          |HAZ PintaVisor;
    |FINSI;
|FPRC;

|PROCESO control1;
   aponer= Asino;
   |HAZ poncontr;
|FPRC;

|PROCESO control2;
   aponer= Anosi;
   |HAZ poncontr;
|FPRC;

|PROCESO poncontr;
         fiac = Control % A109;
         fiac = fiac + relleno;
         guarda = fiac % A109;
         fiac = Control % A1001;
         Control = Control + fiac;
         Control = Control + relleno;
         Control = Control % A120;
         Control = Control + aponer;
|FPRC;

|PROCESO DobUniTPV_504; |TIPO 7;
     |HAZ DobleUniTPV;
     |SI eaSw2Stock = "N";
          puente_a = #4#10;
          |C_M #1#5 , 1 , puente_a;       ||Unidades
     |FINSI;
|FPRC;

|PROCESO DobUniTPV_502; |TIPO 0;
     |HAZ DobleUniTPV;
|FPRC;

|PROCESO EntraIMEI; |TIPO 7;
     |SI enSwSematel = 0;
          enFormaIMEI = 0;
          enModoIMEI = (FEntrada / 100) ! 0;
          eaSerIMEI = #1#20;
          enDocIMEI = #1#0;
          enLinIMEI = #1#1;
          enUnidades = #1#5;
          eaArtIMEI = "";
          |SUB_EJECUTA_NP "tlfw0003";
          |ABRE #2;
          #2#0 = eaArtIMEI;
          |LEE 000#2=;
          |CIERRA #2;

          #1#2 = #2#0;  |PINTA #1#2;     ||Articulo
          #1#22 = #2#128; |PINTA #1#22;   ||CB
          |PTEC 802;
     |FINSI;
|FPRC;

|PROCESO MensaPrecio; |TIPO 7;
     |HAZ EsAparecida;
     |SI FSalida = 0;
          |MENSA "0000<F5> Selección de tarifa";
     |FINSI;
|FPRC;

|PROCESO Des502; |TIPO 0;
     nGuarda = FSalida;
     |HAZ PintaDes502;
     FSalida = nGuarda;
|FPRC;

|PROCESO PintaDes502;
     nGuarda = FSalida;
     |HAZ EsAparecida;
     |SI FSalida = 0;
          |C_A #1#8, 0, nNume;
          |C_A #1#8, 1, 50;
          |PINTA 2315, "³Descripcion: ";
          |PINTA 2328, #1#8;
          |C_A #1#8, 1, nNume;
     |FINSI;
     FSalida = nGuarda;
|FPRC;
