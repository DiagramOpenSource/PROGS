|FICHEROS;
     vigm0017 #0;
     vigm0016 #1; || = agifa077
     agifa019 #2;    || Articulos
     agifa112 #6;
     agifa017 #8;
     agifa007 #11;   || Fichero de series;
     agitp001 #12;   || Tarifas Actualizadas Proveedor / Articulo.
     agifa142 #142;  || Param. Gen.
     agifa321 #15;    || DIV - Parametros

     dsm00014 #914;
     dsm00010 #910;
     dsm00004 #904;
     dsm00005 #905;
     dsm00006 #906;
     dsw00045 #100;
     iber0113@ #113;
     agifa561 #561;
     referen@ #555;
     agifa029 #29;
     agifa080 #80;
|FIN;

|VARIABLES;
     &enModoCab = 0;
     nPeso = 0;
     nHandle = 0;
     &enAlmaExpe = 0;
     nSal = 0;
     &eaModuloCon = "";
     &enDto = 0;
     nalmacen = 0;
     &eArtChampi = "";
     aPtec = "";
     nPtec = 0;
     nLin = 0;
     ||Variables Mod.Obras .....(Reysor)
     &prefot    = 0;
     &numfot    = 0;
     &modfot    = 0;
     &serfot    = "";
     &linfot    = 0;
     &canfot    = 0;
     &impfot    = 0;
     &artfot    = "";
     ||ÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ
     &eaModulo = "";

     &Tempo    = "";
     aAlfa     = "";
     &enSwIber = 0;
     &Envase    = "";
     &Unidades1 = 0;
     &Unidades2 = 0;
     &VArticulo = "";
     nNume      = 0;
     nModo      = 0;
     nPrecio         = 0;
     nForma          = 0;

     &nDeci_Base     = 0;   || Decimales en Totales de la moneda base
     &nDeci_PreBase  = 0;   || Decimales en el Precio en moneda base
     &nDeci_BaseMx   = 0;   || Decimales en Totales de la moneda base (Maxima precision)
     &nDeci_PreBaseMx= 0;   || Decimales en el Precio en moneda base (Maxima precision)
     &nDeci_Div      = 0;   || Decimales en Totales de la divisa
     &nDeci_PreDiv   = 0;   || Decimales en el Precio en divisa
     &nDeci_TotVis   = 0;   || Decimales en Totales visibles (el de la moneda de trabajo)
     &nDeci_TotNoVis = 0;   || Decimales en Totales no visibles (el de la otra moneda)
     &nDeci_PreVis   = 0;   || Decimales en el Precio visual. (lineas)
     &nDeci_ImpVis   = 0;   || Decimales en el Importe visual. (lineas)
     tecla      = "";
     &ac_divisa = ""; ||
     mensa      = ""; || Linea de visualizacion
     &pintado   = 0;|| Controla el pintado de mensa para que solo lo haga 1 vez
     titdiv = "";   || Encabezado de precio e imp. con la divisa "Imp.Tot(USD)"
     precio = 0;
     forma = 0;
     dto1 = 0;
     dto2 = 0;
     alfa = "";
     Unidades = 0;
     asto = 0;
     salida    = 0;
     descu = 0;
     imneto = 0;
     tf = 0;
     modo = 0;
     articulo = " ";
     fmes = "  ";
     mes = 0;
     mensaje3 = "";
     a = 0;
     sto = 0;
     vacio = "                    ";
     previo = 0;
     de = 0;
     pm = 0;
     pe = 0;
     m1 = 0;
     m2 = 0;
     m3 = 0;
     canti2 = 0;
     mes1 = 0;
     modo1 = 0;
     &n_dec = 0;
     &n_pre = 0;
     neto = 0;
     &def_alm = 0;
     &recoge   = "";     || Viene de agifa077
     campo = 0;
     mensaje5 = "0000 Albaran para exportacion Tipo iva = 0 !!";
     expor = "";

     serie     = "";
     serie_o   = "";
     albaran   = 0;
     albaran_o = 0;

     alfa1     = "";
     alfa2     = "";
     fiac      = "  ";
     guarda    = "  ";
     relleno   = "                                       ";
     aponer    = "  ";
     nerror    = 0;
     &LineaD  = 0;   || Lo utilizo en solis008
     nlinea    = 0;
     i         = 0;
     aprograma = "";
     impret = 0;
     &c_prove   = "";
     &c_artic   = "";
     &c_serie   = "";
     &entrada   = 0;
     inkey      = 0;
     &codiBarra = "";
     &r_arti    = "";
     &aRecoge      = "N";
     &aModalidad   = "C";  || Compras
     &aProceso     = "agifa080";
     nHayPrecio    = 0;
     aCentro       = "";
     aModi6        = "";
     aModi30       = "";
     aDef          = "";
     nSwPreIber    = 0;
|FIN;

|INCLUYE i_varart;
|INCLUYE i_cdbarr;

|PROCESO Tipo80_3; |TIPO 3;
     |SI #agifa142#207 = "S";
          aTipoDA = "A"; |HAZ AltaDAC;
     |FINSI;
|FPRC;

|PROCESO iber0113;
     nHayPrecio = 1;
     nPrecio = #113#4;
     |ERROR10;
|FPRC;

|DEFBUCLE iber0113;
     #113#1004;
     ;
     aCentro, #1#3, #0#2, #0#43;
     aCentro, #1#3, #0#2, #0#43;
     ;
     NULL, iber0113;
|FIN;

|PROCESO NoEntra; |TIPO 7;
     |SI #142#164 = "D";
     |FINSI;
|FPRC;

|PROCESO PrecioIber;
|FPRC;

|PROCESO civaalb;
     inkey = FSalida;
     |SI inkey = 14;
           c_prove = #1#3;      || Corregido Por VIC esta mal. antes #0#2
           c_artic = #0#2;      || Corregido Por VIC esta mal. antes #1#3
           c_serie = #1#50;
           entrada = 1;
          |SUB_EJECUTA_NP "agifa383.run";
          |ERROR;
          inkey = 0;
     |FINSI;
     |SI inkey = 15;
          eaPrv = "";
          enTiva = #0#11;
          efFiva = #1#1;
          |HAZ IVAPrv;
          #0#6 = #0#6 / (1 + enIva/100);
          |HAZ visual_precio;
     |FINSI;
|FPRC;

|PROCESO liinfos;|TIPO 0;
mensaje3 = "0410Stock Disponible Almacen : " + #8#39+"     Stock Minimo : "+#8#6;
|MENSA mensaje3;
|FPRC;

|PROCESO licogeas;|TIPO 0;
     articulo = #0#2;
     nalmacen = #0#3;
     |SI aModi6 = "";            || Solo al entrar
          |C_M #0#6, 0, aModi6;
          |C_M #0#30, 0, aModi30;
     |FINSI;
|FPRC;

|PROCESO defeclis;|TIPO 0;
     #0#15 = #1#3;

     |SI #0#2 ! #2#0;
          |ABRE #2;
          #2#0 = #0#2;
          |LEE 000#2=;
          |CIERRA #2;
     |FINSI;
     #0#13 = #2#2;

     |SI #0#15 ! #6#0;
          |ABRE #6;
          #6#0 = #0#15;
          |LEE 000#6=;
          |CIERRA #6;
     |FINSI;

     |HAZ mirespor;

     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 1; |O articulo ! #0#2; |O nalmacen ! #0#3;
          articulo = #0#2;
          nalmacen = #0#3;
          ||ÄÄÄÄÄÄÄÄÄÄÄÄ Doble/MultiÄ
          eaDocu = "agifa080";
          |HAZ LeePreDtoCom;
          #0#6 = enPrecio;
          #0#30 = enPreDiv;
          #0#8 = enDto1;
          #0#26 = enDto2;
          ||ÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ
          |PINTA #0#8;
          |PINTA #0#26;
          |HAZ upc_div;
     |FINSI;
|FPRC;

|PROCESO totlis;|TIPO 0;
     |HAZ TotalLinVig80;
     |HAZ visual_importe;
|FPRC;

|PROCESO Envase80;
     Envase    = #0#34;
     Unidades1 = #0#35;
     Unidades2 = #0#5;
     VArticulo = #0#2;
     enAlmacen = #0#3;
     eaTipo = "C";
     eaProgEnv = "albaran";
     |HAZ EntraEnvase;
     #0#34 = Envase;
     #0#35 = Unidades1;
     #0#5  = Unidades2;  |PINTA #0#5;
|FPRC;

|PROCESO Conversion;  |TIPO 7;
     ||ÄÄÄÄÄÄÄÄÄÄÄÄ Doble/MultiÄ
     eaDocu = "agifa080";
     |HAZ DobleUni;
     |SI eaSw2Stock = "S";
          |C_M #0#5, 1, "N";
     |FINSI;
     ||ÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ
     |SI #agifa142#72 = "S";
          |HAZ Envase80;
          |C_M #0#5, 1, "S";
          |SI enEntraEnvase ! 0;  |C_M #0#5, 1, "N";  |FINSI;
     |FINSI;
|FPRC;

|PROCESO GrabaElemento;
     |ABRE #914;
     #914#0 = eaEleme;
     |LEE 000#914=;
     |CIERRA #914;

     |ABRE #905;
     |DDEFECTO #905;
     #905#0 = "AC";
     #905#1 = #0#27;
     #905#2 = #0#0;
     #905#3 = #0#1;
     #905#4 = eaEleme;
     #905#5 = #914#1;
     #905#6 = #0#5;
     #905#7 = #0#32;
     #905#8 = #905#6 * #905#7;
     #905#11 = #0#5;
     |GRABA 020#905n;

     |DDEFECTO #904;
     |ABRE #904;
     #904#0 = #0#2;
     #904#1 = #0#3;
     #904#2 = eaEleme;
     |LEE 101#904=;
     |SI FSalida ! 0; |GRABA 020#904b; |FINSI;
     #904#3 = #914#1;
     |SI #1#42 = "N";
          #904#12 = #904#12 + #0#5;
          #904#13 = #904#13 + #0#5;
     |SINO;
          #904#12 = #904#12 + #0#5;
          #904#13 = #904#13 + #0#5;
     |FINSI;
     nNume = #904#12 - #904#6;
     |SI nNume > 0;
          #904#24 = nNume * #2#9;
     |SINO;
          #904#24 = 0;
     |FINSI;
     |GRABA 020#904a;
     |ABRE #906;
     #906#0 = #0#2;
     #906#1 = #0#3;
     #906#2 = #1#1;
     #906#3 = "AC";
     #906#4 = #0#27;
     #906#5 = #0#0;
     #906#6 = #0#1;
     #906#7 = eaEleme;
     #906#8 = #914#1;
     #906#9 = #0#5;
     #906#10= #0#30;
     |GRABA 020#906n;
     |CIERRA #906;
|FPRC;

|PROCESO liacartiC;|TIPO 2;
     |HAZ CopiaAl80;

     |MODULO aprograma;
     |SI aprograma ! "vigm0016";
         FEntrada = 101;
     |FINSI;
     modo   = (FEntrada / 100) !0;
     nForma = 0 +. 1;
     |SI modo = 2;|O modo = 3;
          efFec = #1#1;
          enAlma = #0#3;
          |HAZ MiraHisL;
          |SI enErrHis ! 0;
               |ERROR;
               |ACABA;
          |FINSI;
     |FINSI;
     |SI #0#19 = "S";  |O #1#38 = "S";
         |SI nForma < 0;
             |SI #0#19 = AS;
                 |MENAV "0000Esta linea proviene de un Pedido (use la opcion adecuada)";
             |FINSI;
             |SI #1#38 = AS;
                 |MENAV "0000Este albaran esta FACTURADO";
             |FINSI;
         |FINSI;
         |ERROR;
        |ACABA;
     |FINSI;

     enForma = 0 +. 1;

     imneto = #0#9 < #1#5;
     imneto = imneto < #1#32;
     imneto = imneto < #1#7;

     ||ÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ
     |SI codiBarra = "S"; |Y nModo = 1; |Y enForma = 1; |Y #2#176 = "S";
          |HAZ GrabaElemento;
     |FINSI;
     ||ÄÄÄÄÄÄÄÄÄÄÄÄ Doble/MultiÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ
     eaDocu = "agifa080";
     |HAZ StockEntra;
     enTipEnvase = 2;
     |HAZ Envase80;

     |HAZ CalCabAgifa077;

     |SI modo = 3;
          eaTipo      = "AC";
          eaSerie     = #0#27;
          enCodigo    = #0#0;
          enLinea     = #0#1;
          efFecha     = #1#1;
          eaAbono     = #1#42;
          enTarifa    = 0;
          eaArticulo   = #0#2;
          enAlmacen    = #0#3;
          |SUB_EJECUTA_NP "dsz00002;BAJA";

          |HAZ BorraKit;

          |SI #agifa142#207 = "S";
               aTipoDA = "A"; |HAZ BajaDAC;
          |FINSI;
     |FINSI;
     |HAZ pantcomp;

     |SI nForma = 1; |HAZ Obras; |FINSI;
|FPRC;

|PROCESO Obras;
|FPRC;

|PROCESO agitp001;
     |PARA i = 0; |SI i [ 9; |HACIENDO i = i + 1;
          #100i = #12i;
     |SIGUE;
     |GRABA 020#100n;
|FPRC;

|DEFBUCLE agitp001;
     #12#1;
     ;
     #1#3, "                    ";
     #1#3, "zzzzzzzzzzzzzzzzzzzz";
     ;
     NULL, agitp001;
|FIN;

|PROCESO art6alc;
     modo = (FEntrada / 100)  ! 0;
     r_arti = "";
     inkey = salida;
     |SI codiBarra = "S"; |Y modo = 1;
          eaArti = #0#2;

          |HAZ HallaArti;
          |SI eaArti = ""; |Y #910#19 = "S"; |ERROR; |ACABA; |FINSI;
          |SI #2#176 = "S"; |Y eaEleme = "";
               |MENAV "0000Codigo incompleto...";
               |ERROR; |ACABA;
          |FINSI;
          |SI eaArti ! ""; #0#2 = eaArti; |FINSI;
     |FINSI;
     |SI inkey = 10;
          |SI #142#74 = "T";
               |ABRE #2;
               |SELECT #6#2;
               #2#134 = #0#2;
               |LEE 000#2];
               |CONSULTA_CLAVE #6#2;
               |SI FSalida = 0;
                    #0#2 = #2#0;
                    #0#4 = #2#1;
                    |PINTA #0#2;
                    |PINTA #0#4;
               |SINO;
                    |ERROR;
               |FINSI;
               |CIERRA #2;
          |SINO;
               |HAZ CreaTempo; |NOME_DAT #100 Tempo; |DELINDEX #100;
               |ABRE #100;
               |BUCLE agitp001;
               #100#0 = #1#3;
               #100#1 = #0#2;
               |LEE 000#100];
               |CONSULTA_CLAVE #1#100;
               |SI FSalida = 0;
                    #0#2 = #100#1;
                    #0#4 = #100#7;
                    |PINTA #0#2;
                    |PINTA #0#4;
               |SINO;
                    |ERROR;
               |FINSI;
               |CIERRA #100;
               |DELINDEX #100; |HAZ BoraTempo;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO nomodif5; |TIPO 0;
     |QBF r_arti;
     |SI r_arti ! "";
          #0#2 = r_arti;
          |PINTA #0#2;
     |FINSI;
     modo = (FEntrada / 100) ! 0;
     |SI modo = 1;
          #0#3 = def_alm;
          |PINTA #0#3;
     |FINSI;
     |SI #0#2 = vacio;
          |CAMPO_MODIFICA #0#3,  1, "N";
          |CAMPO_MODIFICA #0#5,  1, "N";
          |CAMPO_MODIFICA #0#45,  1, "N";
          |CAMPO_MODIFICA #0#46,  1, "N";
          |SI #1#58 = "B"; || Si la moneda de trabajo es la moneda base
            |CAMPO_MODIFICA #0#6,  1, "N";
          |SINO;
            |CAMPO_MODIFICA #0#30,  1, "N";
          |FINSI;
          |CAMPO_MODIFICA #0#8,  1, "N";
          |CAMPO_MODIFICA #0#26,  1, "N";
          |CAMPO_MODIFICA #0#10, 1, "N";
          |CAMPO_MODIFICA #0#11, 1, "N";
     |SINO;
          |CAMPO_MODIFICA #0#3,  1, "S";
          |CAMPO_MODIFICA #0#5,  1, "S";
          |CAMPO_MODIFICA #0#45,  1, "S";
          |CAMPO_MODIFICA #0#46,  1, "S";
          |SI #1#58 = "B";
            |CAMPO_MODIFICA #0#6,  1, "S";
          |SINO;
            |CAMPO_MODIFICA #0#30,  1, "S";
          |FINSI;
          |CAMPO_MODIFICA #0#8,  1, "S";
          |CAMPO_MODIFICA #0#26,  1, "S";
          |CAMPO_MODIFICA #0#10, 1, "S";
          |CAMPO_MODIFICA #0#11, 1, "S";
     |FINSI;
|FPRC;

|PROCESO decim; |TIPO 6;
     |SI FSalida = 13;
          enTipEnvase = 1;
          |HAZ Envase80;
     |FINSI;
     |SI FSalida = 17; |Y Campo = 5;    || F9
          |HAZ LeePeso;
     |FINSI;
     #0#5 = #0#5 ! n_dec;
     |PINTA #0#5;
|FPRC;

|PROCESO decim1;
     |SI #2#176 = "S";
          |SI codiBarra = "N"; |O modo ! 1;
               |SI #1#58 = "D";  || Si la moneda de trabajo es la divisa ...
                    #0#30 = nPrecio;
               |SINO;
                    #0#6  = nPrecio;
               |FINSI;
          |FINSI;
     |FINSI;

     |SI #1#58 = "D";  || Si la moneda de trabajo es la divisa ...
         #0#6 = #0#30 / #1#57 * #1#62;  || ... recalculo en funcion de la divisa
     |SINO;
         #0#30 = #0#6 / #1#62 * #1#57; || .. recalculo en funcion de la moneda base
     |FINSI;

     |HAZ visual_precio; || Visualiza el precio
     ||ÄÄÄÄÄÄÄÄÄÄÄÄ Doble/MultiÄ
     eaDocu = "agifa080";
     |HAZ PintaUniMediC;
|FPRC;

|| *********************** *(1)
|PROCESO leeotroC; |TIPO 11;
     inkey = FSalida;
     modo = (FEntrada/100) !0;

     |SI inkey = 10;     ||F2
          |HAZ MenuTrack80;
          |SI FSalida = 0; |ERROR; |ACABA; |FINSI;
          inkey = FSalida;
     |FINSI;
     |SI inkey = 9;
          |PUSHV 0101 2380;
          |CLS;
          |PINPA #0#1;
          |PINDA #0#1;
          |PAUSA;
          |POPV;
          |ERROR;
     |FINSI;

     |SI inkey = 14;
          |SI modo = 1;
              c_artic = "";
          |SINO;
              c_artic = #0#2;
          |FINSI;
          c_prove = #1#3;
          c_serie = #1#50;
          entrada = 1;
          |SUB_EJECUTA_NP "agifa383.run";
          |ERROR;
          inkey = 0;
     |FINSI;

     |SI inkey = 13;          || F5
          |HAZ VerKit;
          ||ÄÄÄÄÄÄÄÄÄÄÄÄ Doble/MultiÄ
          eaDocu = "agifa080";
          |HAZ VerDobleUni;
          |ERROR; |ACABA;
          ||ÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ
     |FINSI;

     |SI inkey = 12; |O inkey = 17;     || F4 o F9
          |SI inkey = 12; |Y recoge ="N"; ||F4
               |PUSHV 0400 0479;
               |MENAV "0000Opcion no activada en Utilidades Usuario de Parametros Generales";
               |POPV;
               |ERROR;
               |ACABA;
          |FINSI;
           || compruebo que este en la ultima linea y en modo alta
          LineaD = #vigm0017#1;    || Guardo puntero
          LineaD = LineaD +1;     || Porque si estoy en alta la linea en la que
                                     || estoy todavia no esta grabada
          |LEE 001#vigm0017.];

          |SI FSalida ! 0;|O #vigm0017#0 ! #vigm0016#0;
                          |O #vigm0017#27 ! #vigm0016#50;|Y modo = 1;

               LineaD = LineaD -1;

               || Recupero el puntero
               #vigm0017#0  = #vigm0016#0;    || Albaran
               #vigm0017#27 = #vigm0016#50;   || Serie
               #vigm0017#1  = LineaD;        || Numero de linea
               |LEE 001#vigm0017.=;

               nerror = 0;
               |SI inkey = 12; ||F4
                    |HAZ entradatosC;
               |FINSI;
               |SI nerror = 0 ;
                    |SI inkey = 12;
                         |HAZ leelineasC;
                    |FINSI;
                    |HAZ control2C;
                      || Le meto 3 ptec's para cuando cambia de pantalla
                    |PTEC 809;    || Flecha abajo
                    |PTEC 809;    || Flecha abajo
                    |PTEC 809;    || Flecha abajo
               |SINO;
                    |ERROR;
               |FINSI;
          |SINO;
                   || Recupero el puntero
               LineaD = LineaD -1;
               #vigm0017#0  = #vigm0016#0;    || Albaran
               #vigm0017#27 = #vigm0016#50;   || Serie
               #vigm0017#1  = LineaD;        || Numero de linea
               |LEE 001#vigm0017.=;

               |MENAV "0000Para recoger un albaran debe estar en la ultima linea  y en Alta";
               |ERROR;
          |FINSI;    || Si #vigm0017#1 = 1 ...
     |FINSI;      || Si FSalida ...

     |SI inkey = 16; |Y #agifa142#207 = "S";
          aTipoDA = "A"; |HAZ AltaDAC;
     |FINSI;
     |SI inkey = 25; || Ctrl-F7
          |HAZ CreaTempo;
          eaFichero = Tempo;
          |SUB_EJECUTA_NP "psion066";
          |HAZ BoraTempo;
          |SI enError = 0; |HAZ ImportaPDA80; |FINSI;
          |ERROR; |ACABA;
     |FINSI;
|FPRC;

|| Recojo la serie y numero de albaran
|PROCESO entradatosC;
         |PINTA 2420 , " Recogida de datos del albaran :      /    ";
         alfa1 = "";
         E_sup = 5;
         E_inf = "";
         alfa1 = 2453 ? 1;
         serie_o = alfa1;
         alfa2 = "";
         E_sup = 6;
         E_inf = "";
         alfa2 = 2459 ? 1;
         albaran_o = alfa2;
         |BLIN 24;

         || No puedo leer en el que estoy sino se embuclaria
         |SI  serie_o = #vigm0016#50;|Y albaran_o = #vigm0016#0;
             |MENAV "0000ATENCION !!! Esta en este albaran";
             nerror = 1;
         |FINSI;
|FPRC;

|PROCESO control1C;
   aponer= Asino;
   |HAZ poncontrC;
|FPRC;

|PROCESO control2C;
   aponer= Anosi;
   |HAZ poncontrC;
|FPRC;

|PROCESO poncontrC;
         fiac = Control % A109;
         fiac = fiac + relleno;
         guarda = fiac % A109;
         fiac = Control % A1001;
         Control = Control + fiac;
         Control = Control + relleno;
         Control = Control % A120;
         Control = Control + aponer;
|FPRC;

|PROCESO CreaComponente;
     #905#0 = "AC";
     #905#1 = #0#27;
     #905#2 = #0#0;
     #905#3 = #0#1;
     #905#9 = #905#6;
     |GRABA 020#905n;
     |LIBERA #905;

     enUnidades   = #905#6;
     eaTipo       = "AC";
     eaSerie      = #0#27;
     enCodigo     = #0#0;
     enLinea      = #0#1;
     efFecha      = #1#1;
     eaArticulo   = #0#2;
     enAlmacen    = #0#3;
     eaComponente = #905#4;
     efFecha      = #1#1;
     enCamBas     = #1#57;
     eaMoneda     = #1#58;
     enCamDiv     = #1#62;

     |SUB_EJECUTA_NP "dsz00002;ACC";
|FPRC;

|DEFBUCLE dsm00005;
     #905#1;
     ;
     "AC", serie, albaran, nlinea, "      ";
     "AC", serie, albaran, nlinea, "zzzzzz";
     be;
     NULL, CreaComponente;
|FIN;

|PROCESO crealineasC;
    || Clave primaria
    #vigm0017#27= #vigm0016#50;    || Serie
    #vigm0017#0 = #vigm0016#0;     || Numero de Albaran
    #vigm0017#1 = LineaD;         || Numero de linea


    |DEFECTO #vigm0017#vigm0016;   || Cargo los valores de defecto de cab.albaran

    || Ahora cambio lo que es particular de este albaran y que por tanto
    || no puede ver como copia de otro
    #vigm0017#12 = "";                  || Familia
    || !!!! Descargar el coste !!! (pdte)
    |PARA i = 16;|SI i [ 26; |HACIENDO i = i+1;
          #vigm0017#i# = "";
    |SIGUE;
    #vigm0017#28 = ""; || Serie Factura
    #vigm0017#29 = ""; || Serie Pedido

    |HAZ totlis;       || Calculo total linea
    |HAZ liacartiC;    || Lo envio al t ipo 2 para que calcule lo mismo que si fuera
                       || una linea introducida por el operador
    |GRABA 021#vigm0017.b;
    |LIBERA #vigm0017;

    |BUCLE dsm00005;

    LineaD =LineaD + 1;  || Aumento contador de lineas
|FPRC;


|PROCESO leelineasC;
      #vigm0017#27= serie_o;
      #vigm0017#0 = albaran_o;
      #vigm0017#1 = 1;
      |LEE 001#vigm0017.];
      |SI FSalida =0;
           |PARA;|SI FSalida =0;|Y #vigm0017#27=serie_o;|Y #vigm0017#0=albaran_o;
                                                     |HACIENDO;
                  || Me guardo el puntero
                  serie   = #vigm0017#27;   || Serie y albaran actual
                  albaran = #vigm0017#0;
                  nlinea  = #vigm0017#1;

                  |HAZ crealineasC;

                  || Recupero puntero
                  #vigm0017#27 = serie;
                  #vigm0017#0  = albaran;
                  #vigm0017#1  = nlinea;
                  |LEE 001#vigm0017.=;

                  |LEE 001#vigm0017.s;
           |SIGUE;
      |SINO;
           |MENAV "0000Atencion !!! No se ha encontrado el albaran indicado";
           |ERROR;
      |FINSI;

      || Devuelvo el puntero de la linea
      #vigm0017#27= #vigm0016#50; || Serie
      #vigm0017#0 = #vigm0016#0;  || Albaran
      #vigm0017#1 = LineaD;      || Linea
      |LEE 001#vigm0017.=;
|FPRC;


|PROCESO param;|TIPO 7;
     nModo = (FEntrada / 100) ! 0;

     |ABRE #11;
     |DDEFECTO #11;
     #11#26 = #0#27;
     |LEE 001#11=;
     expor = #11#30;
     |CIERRA #11;

     |SI nModo = 1;
          #0#1 = #0#1 + #agifa142#236 - 1;
     |FINSI;
|FPRC;

|PROCESO mirespor;|TIPO 0
   |SI #0#11 ! 0; |Y expor = "S";
                 |MENSA mensaje5;
                       #0#11 = 0;
                    |PINTA #0#11;
   |FINSI;
|FPRC;

 REGISTRO DE ACTUALIZACIONES

*(1) ... Mediante la pulsacion de F4 podremos recoger las lineas de otro
         albaran.

*(2)...  Utilizamos este proceso entero para la creacion de albaranes desde
         otros procesos, por tanto forzamos esos valores, si no estamos en
         ese momento en el mantenimiento propiamente dicho (p.e. solis008)
         (Lorca 14/07/96).

|| _____________________________ VER CAMBIO DE PRECIOS. PROVEEDOR / ARTICULO.
|PROCESO Mensaje;
     |PUSHV 0501 2380;
     |AVISO;
     |COLOR 0, 0; |ATRI 0;
     |BLANCO 0815 1265;
     |COLOR 4, 0; |ATRI 0;
     |D_CUADRO 0815,1265;
     |PINTA 1017, alfa;
     |ATRI 0;
     |PAUSA;
     |POPV;
|FPRC;

|PROCESO cambipre; |TIPO 0;
     |SI #142#75 ! "S"; |ACABA; |FINSI;
     ||ÄÄÄÄÄÄÄÄÄÄÄÄ Doble/MultiÄ
     eaDocu = "agifa080";
     |HAZ LeePreDtoCom;
     ||ÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ
     |SI #1#58 = "B";
          |SI enPrecio ! #0#6;
               alfa = "­ ATENCION ! POSIBLE CAMBIO DE PRECIO COSTE...";
               |HAZ Mensaje;
          |FINSI;
     |SINO;
          |SI enPreDiv ! #0#30;
               alfa = "­ ATENCION ! POSIBLE CAMBIO DE PRECIO COSTE...";
               |HAZ Mensaje;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO cambidto;
     |SI #142#75 ! "S"; |ACABA; |FINSI;
     ||ÄÄÄÄÄÄÄÄÄÄÄÄ Doble/MultiÄ
     eaDocu = "agifa080";
     |HAZ LeePreDtoCom;
     ||ÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ
     |SI Campo = 8;
          |SI enDto1 ! #0#8;
               alfa = "­ ATENCION ! POSIBLE CAMBIO DE DESCUENTO 1 ...";
               |HAZ Mensaje;
          |FINSI;
     |SINO;
          |SI enDto2 ! #0#26;
               alfa = "­ ATENCION ! POSIBLE CAMBIO DE DESCUENTO 2 ...";
               |HAZ Mensaje;
          |FINSI;
     |FINSI;
|FPRC;
------------------ ACTUALIZA ARTICULOS Y ALMACEN -------------------------
|PROCESO pantcomp; |TIPO 13;
     |SI enModoCab = 3; |ACABA; |FINSI;
     |HAZ visual_totales; || Visualiza los totales
     |SI #142#148 = "S";
          |PINTA 1327, "Portes:"
     |FINSI;
|FPRC;


|PROCESO LinDiv;  |TIPO 7;
     ||ÄÄÄÄÄÄÄÄÄÄÄÄ Doble/MultiÄ
     eaDocu = "agifa080";
     |HAZ PintaUniMediC;
     ||ÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ
     |ABRE #agifa321;
     |LEE 001#agifa321.p;  || Leo parametros de divisa
     |SI ac_divisa = "S"; |Y pintado = 0; || Si divisas estan activadas y no he pintado ...
       |SI #1#58 = "D";   || Si la moneda de trabajo es la divisa la pinto en
         |ATRI R;         || el encabezado del precio y el importe
         titdiv = "Precio" + "(" + #1#56 + ")";
         |PINTA 1444,titdiv;
         titdiv = "I.Tot." + "(" + #1#56 + ")";
         |PINTA 1468,titdiv;
         |ATRI r;
       |FINSI;
       |SI #agifa321#4 = "S";  || Si la visualizacion esta activada ...
         mensa = "Precio:            Imp.:              T.Bruto:             T.ALB:             ";
         |PINTA 1102,mensa;  || ... pinto la linea de visualizacion
         |PINTA #0#32; || Pinto precio visual
         |PINTA #0#33; || Pinto importe visual
         |SI #1#58 = "D"; || Si la moneda de trabajo es la divisa
           |PINTA 1149,#1#67;
           |PINTA 1168,#1#68;
         |SINO;
           |PINTA 1149,#1#63;
           |PINTA 1168,#1#64;
         |FINSI;
         pintado = 1;  || Ya hemos pintado
       |FINSI;
     |FINSI;
     |CIERRA #agifa321;
|FPRC;

|PROCESO upc_div;
     |SI eaDivisa = #1#56;
          #0#6 = #0#30 * #1#62 / #1#57;
     |SINO;
          #0#30 = #0#6 / #1#62 * #1#57;
     |FINSI;
     |HAZ visual_precio;
|FPRC;

|PROCESO visual_precio; || Pinta el precio visual.
|SI #1#58 = "D";  || Si la moneda de trabajo es la divisa ...
  #0#32 = #0#6;   || ... el campo visualiz. es el precio en moneda base
  |PINTA #0#30;
|SINO;            || Si la moneda de trabajo es la moneda base ...
  #0#32 = #0#30;  || ... el campo visual. es el precio en divisa
  |PINTA #0#6;
|FINSI;
|SI ac_divisa = "S"; |Y #agifa321#4= "S";
  |PINTA #0#32; || Si las divisas y la visualizacion estan activadas, pinto
|FINSI;
|FPRC;


|PROCESO visual_importe;  || Pinta el importe visual
|SI #1#58 = "D";  || Si la moneda de trabajo es la divisa ...
  #0#33 = #0#9;   || el campo visualiz. es el importe en moneda base
  |PINTA #0#31;
|SINO;            || Si la moneda de trabajo es la moneda base ...
  #0#33 = #0#31;  || el campo visualiz. es el importe en divisa
  |PINTA #0#9;
|FINSI;
|SI ac_divisa = "S"; |Y #agifa321#4= "S";
  |PINTA #0#33; || Si las divisas y la visualizacion estan activadas, pinto
|FINSI;
|FPRC;

|PROCESO visual_totales;
     |ABRE #agifa321;
     |LEE 001#agifa321.p;
     |SI #1#58 = "D";
          |SI ac_divisa = "S"; |Y #agifa321#4= "S";
               |PINTA 1149,#1#67;
               |PINTA 1168,"        ";
               |PINTA 1168,#1#68;
          |FINSI;
          |CIERRA #agifa321;
        ||*  |PINTA 1349,#1#63;
          |PINTA 1366,#1#64;
     |SINO;
          |SI ac_divisa = "S"; |Y #agifa321#4= "S";
               |PINTA 1149,#1#63;
               |PINTA 1168,"        ";
               |PINTA 1168,#1#64;
         |FINSI;
         |CIERRA #agifa321;
         ||* |PINTA 1349,#1#15;
         |PINTA 1366,#1#55;
     |FINSI;
|FPRC;

|PROCESO CogeValores80;  |TIPO 7;
     |SI Campo = 3;
         |C_M #0Campo, 1, "S";
         |SI #142#99 = "S";
             |C_M #0Campo, 1, "N";
             #0Campo = 1;  |PINTA #0Campo;
         |FINSI;
     |FINSI;

     |SI #2#176 = "N";  |ACABA;  |FINSI;

     |SI Campo = 3;
         |SI enAlmacen ! 0;
             #0#3 = enAlmacen;  |PINTA #0#3;
         |FINSI;
     |FINSI;

     |SI Campo = 5;
         |SI enUnidades ! 0;
             |SI #0#5 ! enUnidades;  Contenido = #0#5;  |FINSI;
             #0#5 = enUnidades;  |PINTA #0#5;
         |FINSI;
     |FINSI;

     |SI Campo = 6;
         |SI nPrecio ! 0;
             #0#6 = nPrecio;  |PINTA #0#6;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO PideArt80;  |TIPO 7;
     nModo = (FEntrada/100) ! 0;
     |SI #142#140 = "S"; |Y nModo = 1; #0#2 = eaArtAnt; |FINSI;

     |C_M #0#2, 1, "S";
     |C_M #0#3, 1, "N";

     |SI #0#2 ! "                    ";
         |ABRE #2;
         #2#0 = #0#2;
         |LEE 000#2=;
         |SI FSalida ! 0;  |DDEFECTO #2;  |FINSI;
         |CIERRA #2;
         |SI #2#176 = "S";
             |C_M #0#2, 1, "N";
             |C_M #0#3, 1, "N";
             |ACABA;
         |FINSI;
     |FINSI;

     |ABRE #910;
     |LEE 040#910p;
     |SI FSalida ! 0;  |DDEFECTO #910;  |FINSI;
     |CIERRA #910;

     |SI #910#2 [ 2;    |ACABA;  |FINSI;

     eAlfa = #0Campo;
     |SUB_EJECUTA_NP "dsz99990";                 || Por Pantalla

     |SI eAlfa = "-1";  |PTEC 807;  |ACABA;  |FINSI;
     |SI eAlfa = "-2";              |ACABA;  |FINSI;

     eaArticulo = eAlfa;      ||  700000-4215

     #0Campo = eAlfa;
     |PINTA #0Campo;
     |PTEC 802;
|FPRC;

|PROCESO Calc680;  |TIPO 6;
     eaArtAnt = #0#2;
     salida = FSalida;

     ||ÄÄÄÄÄÄÄÄÄÄÄÄ Doble/MultiÄ
     eaDocu = "agifa080";
     |HAZ UniPartiCB;
     ||ÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ
     |SI #0Campo ! Contenido; |Y Campo = 2;
          eaArticulo = #0#2;
          |HAZ DefectoMediC;
          #0#39 = enMedida1;
          #0#40 = enMedida2;
          #0#41 = enMedida3;
     |FINSI;

     enAlta = 1;
     eaArticulo = #0Campo;
     |HAZ LlenaArticulo;
     |SI FSalida = 0;
          #0Campo = eaArticulo;
          |PINTA #0Campo;
     |SINO;
          |ERROR; |ACABA;
     |FINSI;

     |HAZ art6alc;

     |SI codiBarra = "S"; |Y #910#19 = "S"; |Y modo = 1;
          |ACABA;
     |FINSI;

     |ABRE #2;
     #2#0 = #0#2;
     |LEE 101#2=;
     |SI #142#209 = "S"; |Y #2#27 ! #1#3;
          aAlfa = "0000NModificar Proveedor en Articulo:"+&13+"Actual:["+#2#27+"]";
          |CONFI aAlfa;
          |SI FSalida = 0; #2#27 = #1#3; |GRABA 020#2a; |FINSI;
     |FINSI;
     |CIERRA #2;

     |SI #2#176 = "S";
         eaTipo      = "AC";
         eaSerie     = #0#27;
         enCodigo    = #0#0;
         enLinea     = #0#1;
         efFecha     = #1#1;
         eaArticulo  = #0#2;
         enAlmacen   = #0#3;
         enUnidades  = #0#5;
         enTBruto    = #0#7;
         eaAbono     = #1#42;
         enTarifa    = 0;
         eaProve     = #1#3;
         enCamBas     = #1#57;
         eaMoneda     = #1#58;
         enCamDiv     = #1#62;

         |SUB_EJECUTA_NP "dsz00002";

         #0#3    = enAlmacen;
         nPrecio = enTBruto / enUnidades;

         |PTEC 802;
         |PTEC 802;
         |PTEC 802;
         |PTEC 802;
     |FINSI;
     |HAZ SacaKit;
|FPRC;

|PROCESO Tipo080; |TIPO 0;
     |HAZ nomodif5;
     ||ÄÄÄÄÄÄÄÄÄÄÄÄ Doble/MultiÄ
     eaDocu = "agifa080";
     |HAZ PintaUniMediC;
     ||ÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ
     |SI #agifa142#207 = "S";
          aTipoDA = "A"; |HAZ DescriDAC;
     |FINSI;
     |HAZ Portes;

     ||Modulo 003273
     |SI #0Campo ! Contenido; |Y Campo = 2; eArtChampi = ""; |FINSI;
|FPRC;

|PROCESO Multi80; |TIPO 66;
     ||ÄÄÄÄÄÄÄÄÄÄÄÄ Doble/MultiÄ
     eaDocu = "agifa080";
     |HAZ CambiaUniMulti;
     |HAZ LeePreDtoCom;
     #0#6 = enPrecio;
     #0#30= enPreDiv;
     ||ÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ
     |HAZ upc_div;
|FPRC;

|PROCESO Tipo11080;  |TIPO 11;
     |SI FSalida = 7;  |Y #2#176 = "S";
         |MENAV "      Imposible abortar;  Termine de introducir la linea y efectue una Baja.";
         |ERROR;
     |FINSI;
|FPRC;

|PROCESO cierre; |TIPO 0;
     |SI FSalida = 12;   ||F4
          aAlfa = FSalida;
          |ABRE #29;
          #29#0 = #0#3;
          |LEE 000#29];
          |CONSULTA_CLAVE #1#29;
          |SI FSalida = 0;
               #0#3 = #29#0;
               |PINTA #0#3;
          |SINO;
               |ERROR; |ACABA;
          |FINSI;
          |CIERRA #29;
          FSalida = aAlfa;
     |FINSI;

     efFec = #1#1;
     enAlma = #0#3;
     |HAZ MiraHisL;
     |SI enErrHis ! 0;
          |ERROR;
          |ACABA;
     |FINSI;
|FPRC;

|PROCESO Tipo5_080;  |TIPO 5;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo ! 4;
          |HAZ actalc;
     |FINSI;
|FPRC;

|PROCESO CtaArt1;
   eaModulo = "agifa104"; |HAZ CtaArt;
|FPRC;


|PROCESO Portes;
|FPRC;

|PROCESO MinC080;
     #referen@#27 = #1#50;
     #referen@#0 = #1#0;
     #referen@#1 = 001;
|FPRC;

|PROCESO MaxC080;
     #referen@#27 = #1#50;
     #referen@#0 = #1#0;
     #referen@#1 = 999;
|FPRC;

|PROCESO Consulta080; |TIPO 66;
     nLin = 0;
     |DDEF aAlfa;
     aAlfa = aAlfa + "agifa080";
     |CARGA_DEF aAlfa, referen@;
     |SI FSalida = 0;
          |ABRE #referen@;
          |CONSULTA_F_CLAVE #1#referen@, MinC080, MaxC080;
          |SI FSalida = 0; nLin = #referen@#1; |FINSI;
          |CIERRA #referen@;
          |DESCARGA_DEF #referen@;
     |FINSI;
     |SI nLin ! 0;
          |PTEC 802;
          aAlfa = ("000" + nLin) % -103;
          aPtec = aAlfa % 301; nPtec = &aPtec;
          |PTEC nPtec;
          aPtec = aAlfa % 201; nPtec = &aPtec;
          |PTEC nPtec;
          aPtec = aAlfa % 101; nPtec = &aPtec;
          |PTEC nPtec;
     |FINSI;
|FPRC;

|PROCESO TotalImp7_80; |TIPO 7;
|FPRC;

|PROCESO ImportaPDA80;
     |DDEF aAlfa; aAlfa = aAlfa + "agifa080";
     |CARGA_DEF aAlfa, referen@;
     |SI FSalida ! 0; |ACABA; |FINSI;
     |ABRE #referen@;
     #referen@#27 = #1#50;
     #referen@#0 = #1#0;
     #referen@#1 = 999;
     |LEE 000#referen@.];
     |SI FSalida = 0;
          |LEE 000#referen@.a;
     |SINO;
          |LEE 000#referen@.u;
     |FINSI;
     |SI FSalida = 0; |Y #referen@#27 = #1#50; |Y #referen@#0 = #1#0;
          nLin = #referen@#1 + 1;
     |SINO;
          nLin = 1;
     |FINSI;
     |CIERRA #referen@;
     |DESCARGA_DEF #referen@;

     |DDEF aAlfa; aAlfa = aAlfa + "psion067";
     |CARGA_DEF aAlfa, referen@;
     |SI FSalida ! 0; |ACABA; |FINSI;
     |NOME_DAT #referen@#eaFichero#;
     |ABRE #referen@;
     |LEE 000#referen@.p;
     |PARA; |SI FSalida = 0; |HACIENDO;
          #0#27 = #1#50;
          #0#0 = #1#0;
          #0#1 = nLin;
          |GRABA 020#0b;
          #0#2 = #referen@#0; ||Arti
          #0#4 = #referen@#1; ||Des
          #0#3 = #referen@#2; ||Alma
          #0#5 = #referen@#3; ||Uni
          #0#37 = #referen@#9; ||Parti
          FEntrada = 100;     ||simula alta
          |HAZ defeclis;
          |HAZ TotalLinVig80;
          |HAZ liacartiC;
          |GRABA 020#0a;
          |LIBERA #0;
          nLin = nLin + 1;
          |LEE 000#referen@.s;
     |SIGUE;
     |CIERRA #referen@;
     |DELINDEX #referen@;
     |DESCARGA_DEF #referen@;
     |PONCONTROL 23, "SI"
     |PTEC 809;
     |PTEC 808;
|FPRC;

|PROCESO MenuTrack80;
     |VARIABLES; {20} aPopup = ""; |FINSI;
     aPopup1 = "-1";  || Si fuera -1 en la posicion
     aPopup2 = 5;
     aPopup3 = "<F4> Recoger Lineas";
     aPopup4 = "<F5> Doble Stock/Kit";
     aPopup5 = "<F6> Precios Proveedor";
     aPopup6 = "<F12> Consulta Cabecera";
     aPopup7 = "<Ctrl+F7> Captura PDA";
     IaPopup = aPopup1  <-;
     |ESPECIFICA "TRACKPOPUP", aPopup;
     nSal = FSalida;
     |SI FSalida = 1; nSal = 12; |FINSI;
     |SI FSalida = 2; nSal = 13; |FINSI;
     |SI FSalida = 3; nSal = 14; |FINSI;
     |SI FSalida = 4; nSal = 9; |FINSI;
     |SI FSalida = 5; nSal = 25; |FINSI;
     FSalida = nSal;
|FPRC;

|PROCESO CalcuUni; |TIPO 0;
     |SI FSalida = 17; || F9
          |HAZ LeePeso;
     |FINSI;
     |SI #0Campo ! Contenido;
          #0#5 = #0#45 - #0#46;
          |PINTA #0#5;
     |FINSI;
|FPRC;

|PROCESO LeePeso; |TIPO 0;
     nPeso = 0;
     aAlfa = "/ds/bin/leepeso";
     |RSYSTEM aAlfa;
     |SLEEP 1;
     aAlfa = "/ds/peso.txt";
     |XABRE "CU", aAlfa, nHandle;
     |SI FSalida ! 0 ;
          aAlfa = "0000no puede abrir " + aAlfa;
          |MENAV aAlfa;
     |SINO;
          |XLEE nHandle ,250 , aAlfa;
          nPeso = aAlfa;
          |XCIERRA nHandle;
     |FINSI;
     #0Campo = nPeso;
     |PINTA #0Campo;
|FPRC;

|PROCESO CopiaAl80;
     |PARA i = 0; |SI i [ 45; |HACIENDO i = i + 1;
          #80i = #i;
     |SIGUE;
|FPRC;

|PROCESO IVA017_66; |TIPO 66;
     enTiva = #0Campo;
     efFiva = #1#1;
     |HAZ ConsuIVA;
     |SI eaDiva = "";
          |ERROR;
     |SINO;
          #0Campo = enTiva; |PINTA #0Campo;
     |FINSI;
|FPRC;

|PROCESO IVA017_0; |TIPO 0;
     enTiva = #0Campo;
     efFiva = #1#1;
     |HAZ SacaIVA;
     |SI eaDiva = "";
          |MENAV "0000No existe tipo iva para esta fecha...";
          |ERROR; |ACABA;
     |FINSI;
|FPRC;
