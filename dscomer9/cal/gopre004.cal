NOTA:
  Definir los proceso tanto en el gopre004 como en el gopre026 porque
  son los mismos ficheros que se cargan como referencia en el &gopre004@


|FICHEROS;
     gopre001 #0;
     gopre002 #1;
     gopre003 #2;
||     gopre004 #3;
     &gopre004@ #3;
     goartive #4;
     goartdes #6;
     agifa321 #7;             ||Parametros Divisas.
     agifa324 #8;             ||Divisas.
     goparame #9;
     agifa019 #10;
     agifa553 #11;
     agifa060 #12;
     agifa120 #13;
     agifa048 #14;
     agifa049 #15;
     godeivec #18;                                ||Desc. Ampliada (CAB)
     godeivel #19;                                ||Desc. Ampliada (LIN)
||     goparam1 #30,MANTE;                          ||Seleccion Parametrico (1)
||     goparam2 #31,MANTE;                          ||Seleccion Parametrico (2)
||     goparam3 #32,MANTE;                          ||Seleccion Parametrico (3)
||     goparam4 #33,MANTE;                          ||Seleccion Parametrico (4)
     goprecos #40;
     agifa024 #41;

     gomedive #50;
     goaux004 #51;

     gosecive #100;
     gofamive #101;
     gosubive #102;
     goapaive #103;
     agi00063 #110;
     goartbc3 #111,MANTE;
     gopreaux #999,MANTE;
     agifa048 #480;
     dsm00238 #238;
|FIN;
------------------------------------------------------------------------------
|VARIABLES;
     nCambioArt = 0;
     &enUniFac = 0;
     aPide2Uni = "";
     nBotEscan = 0;
     nBotEscanModi = 0;
     &eaSw2Stock = "";
     &enModoVta = 0;
     &eaPartida = "";
     &eaArticulo = "";
     &eaArtiAnt = "";
     &enUnidad1 = 0;
     &enUnidad2 = 0;
     &enMedida1 = 0;
     &enMedida2 = 0;
     &enMedida3 = 0;

 {-1}aMenu = "";
     aMenu1 = "2400";
     aMenu2 = "1";
     aMenu3 = "";
     aMenu4 = "AB";
     aMenu5 = " Articulos ";
     aMenu6 = " Articulos Importados BC3";

     &enDirEnt      = 0;
     &nDeci_PreBase = 0;
     &cCodMonBase   = "";
     &cCodMonTrab   = "";
     &cCodMonDe     = "";
     &cCodMonA      = "";
     &cCodMonConv   = "";
     &cCodMonActu   = "";
     &cDesMonBase   = "";
     &cDesMonTrab   = "";

     &cCodigo       = "";
     &eaSerie       = "";
     &cEstado       = "";
     &eaCodigo      = "";
     &cDesc         = "";
     &cTitulo1      = "";
     &cTitulo2      = "";
     &cTitulo3      = "";
     &cTitulo4      = "";
     &cCombina      = "";

     &aCodAlta      = "";

     &nDeci_Medi    = 0;
     &nDeci_Incr    = 0;
     &nDeci_PBase   = 0;
     &nDeci_TBase   = 0;
     &nDeci_PTrab   = 0;
     &nDeci_TTrab   = 0;
     &nDeci_TotVis  = 0;
     &nEurComTrab   = 0;
     &nEurComBase   = 0;
     &nEurVenTrab   = 0;
     &nEurVenBase   = 0;
     &nPreComTrab   = 0;
     &nPreComBase   = 0;
     &nPreVenTrab   = 0;
     &nPreVenBase   = 0;
     &nPrecioComp   = 0;
     &nPrecioVent   = 0;

     &eaNumero      = 0;

     &fed_dt        = @;   || Fecha....
     &art_dt        = "";  || Articulo.
     &cli_dt        = "";  || Cliente..
     &fam_dt        = "";  || Familia.
     &iva_dt        = 0;   || Tipo Iva.
     &dtos_2        = 0;   || Descuento 2.
     &dto_pt        = 0;   || Descuento Ptas.
     dtopts         = 0;
     &dtos_1        = 0;   || Descuento 1.
     &pvp_ve        = 0;   || Precio Venta.
     &tarifa        = 0;   || Tarifa Cliente.
     &gru_dt        = "";  || Grupo Cliente.
     &uni_dt        = 0;
     &enMensaje     = 0;
     &aParam        = "";
     qAlta          = "";
     aSerie         = "";
     aCapitu        = "";
     aPartid        = "";
     cArti          = "";
     cUnid          = "";
     cFamilia       = "";
     cSubFamilia    = "";
     cCapitulo      = "";
     cDescr         = "";
     cParam         = "";
     cFich1         = "";
     cFich2         = "";
     cFich3         = "";
     cFich4         = "";
     cCadena        = "";
     {500}fiebdc    = "";

     Calc           = 0;
     Calc1          = 0;
     nTotCoste      = 0;
     nTCosteAnt     = 0;
     nTotDes        = 0;
     nPresup        = 0;
     nIncrem        = 0;
     nBanco         = 0;
     nPrecio        = 0;
     nDeciActual    = 0;
     nDesc          = 0;
     nLinea         = 0;
     nPreAct        = 0;
     nEuros         = 0;
     nCambio        = 0;
     nModo          = 0;
     nCarac         = 0;
     nBotAjust      = 0;
     nBotAmpli      = 0;
     nBotIncre      = 0;
     nBotBanco      = 0;
     nSalida        = 0;
     i              = 0;
     j              = 0;
     k              = 0;
     r              = 0;
     nErrorPar      = 0;
     nMedi          = 0;
     nIncr          = 0;
     nSal           = 0;
     nCampo         = 0;
     aAlfa          = "";
     nForma         = 0;
|FIN;

|PROCESO CogeUniFac4;
     enUniFac = #3#8;

     |ABRE #10;
     #10#0 = #3#5;
     |LEE 000#10=;
     |SI FSalida = 0; |Y #10#179 = "S"; ||Doble Stock
          |ABRE #238;
          #238#0 = #10#201;
          |LEE 000#238=;
          |SI FSalida = 0; |Y #238#6 = 2;    || Unidad Factura Ventas
               enUniFac = #3#17;
          |FINSI;
          |CIERRA #238;
     |FINSI;
     |CIERRA #10;
|FPRC;

------------------------------------------------------------------------------
|PROCESO DPar; |TIPO 7;
     |ATRI I;
     |PINTA 1002, #2#3;                           ||Linea.
     |PINTA 1006, #2#4;                           ||Capitulo.
     |PINTA 1016, #2#5;                           ||Descripcion.
     |PINTA 1028, #2#6;                           ||U. Medida.
     |PINTA 1032, #2#7;                           ||Cantidad.

     /*comprobamos si los precios son a venta o a coste.*/

     |SI cEstado = "Venta";
          |PINTA 1042, #2#8;                      ||Precio Venta.
          |PINTA 1055, #2#12;                     ||Incremento.
          |PINTA 1067, #2#10;                     ||Importe Venta.
          |SI #9#120 = "N";
               |ATRI R;
               |SI #9#120 = "S";
                    |PINTA 1167, "Venta";
               |SINO;
                    |PINTA 1175, "Venta";
               |FINSI;
               |ATRI 0;
          |FINSI;
     |SINO;
          |PINTA 1042, #2#9;                      ||Precio Venta.
          |PINTA 1067, #2#11;                     ||Importe Venta.
          |ATRI R;
          |SI #9#120 = "S";
               |PINTA 1167, "Coste";
          |SINO;
               |PINTA 1175, "Coste";
          |FINSI;
          |ATRI 0;
     |FINSI;

     |ATRI 0;
     cDescr = #3#6;
     |ATRI P;
     |PINTA 2202, cDescr;
     |ATRI 0;

     #0#22 = #2#8;
     #0#23 = #2#9;
     |PINTA 2267, #0#22;                          ||Total Venta Pres.
     |PINTA 2367, #0#23;                          ||Total Compra Pres.

|FPRC;
------------------------------------------------------------------------------
------------------------------------------------------------------------------
|PROCESO goaux004;
     |SI #51#14 = "S"; |FINSI;
|FPRC;

|DEFBUCLE goaux004;
     #51#1;
     ;
     #2#0,#2#1,#2#2,#2#4,  1;
     #2#0,#2#1,#2#2,#2#4,999;
     ;
     NULL,goaux004;
|FIN;

|PROCESO EsPorcen; |TIPO 0;
     |SI #3Campo ! Contenido;
          #3#21 = #3#8 * #2#7;
          |SI #9#120 = "S"; |PINTA #3#21; |FINSI;
     |FINSI;

     |ABRE #50;
     #50#0  = #3#7;
     |LEE 000#50=;
     |SI #50#6 = "S";
          |BUCLE goaux004;
     |FINSI;
     |CIERRA #50;
|FPRC;

|PROCESO Calporcen; |TIPO 0;
     |SI #3#11 ! Contenido;
          |HAZ CogeUniFac4;
          |SI #0#42 = "+";
               #3#13 = ((#3#11-(#3#9*enUniFac))/(#3#9*enUniFac))*100;
          |SINO;
               #3#13 = (1 - ((#3#9*enUniFac)/#3#11))*100;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO CalLinDes; |TIPO 0;
     nCampo = Campo;
     |SI nCampo = 12;
          |C_M #3#12 , 0 , aAlfa;
          |SI aAlfa = "N"; |ACABA; |FINSI;
     |FINSI;

     |HAZ CogeUniFac4;

     #3#10 = #3#9;
     |SI #0#42 = "+";
          #3#11 = (enUniFac*#3#9) > #3#13;           ||Importe Venta.
     |SINO;
          #3#11 = (enUniFac*#3#9) / (1 - (#3#13 / 100));
     |FINSI;
     #3#12 = enUniFac*#3#10;                          ||Importe Compra.
     |PINTA #3#11;
     |PINTA #3#12;
|FPRC;
------------------------------------------------------------------------------
|PROCESO CalTotPar; |TIPO 2;
     nModo = (FEntrada /100)!0;

     nCambioArt = 0;
     nForma = 0 +. 1;
     |SI nForma = -1; |Y nModo = 3;
          |CONFI "2400NDesea dar de baja la linea";
          |SI FSalida ! 0;
               |ERROR; |ACABA;
          |FINSI;
     |FINSI;

     |SI nModo = 3; |Y #2#22 > 0;
          |MENAV "0000LA PARTIDA ESTA SUBCONTRATADA";
          |ERROR;
          |ACABA;
     |FINSI;

     #2#8  = #2#8 +. #3#11;                      ||Precio Venta Partida.
     #2#9  = #2#9 +. #3#12;                      ||Precio Coste Partida.
     #2#10 = (#2#8 * #2#7);                       ||Importe Venta Partida.
     #2#11 = (#2#9 * #2#7);                       ||Importe Coste Partida.
     #2#12 = (((#2#8-#2#9)/#2#9)*100);            ||Incremento de partida.
     #0#22 = #2#8;
     #0#23 = #2#9;

     |PINTA 2267, #0#22;                          ||Total Venta Pres.
     |PINTA 2367, #0#23;                          ||Total Compra Pres.

     |ABRE #9;
     #9#0 = "A";
     |LEE 000#9=;

     |SI FSalida = 0;
          |SI #9#5 = #3#14;
               #2#16 = #2#16 +. #3#8;             ||Unidades Mano de Obra.
          |FINSI;
     |FINSI;

     |CIERRA #9;
     |HAZ DPar;
|FPRC;
------------------------------------------------------------------------------
|PROCESO MinSel;
     #4#13  = cCapitulo;
     #4#4   = "N";
     #100#0 = " ";
     #101#2 = cFamilia;
     #102#2 = cSubFamilia;
     #103#2 = cCapitulo;
|FPRC;
------------------------------------------------------------------------------
|PROCESO MaxSel;
     #4#13  = cCapitulo;
     #4#4   = "N";
     #100#0 = "z";
     #101#2 = cFamilia;
     #102#2 = cSubFamilia;
     #103#2 = cCapitulo;
|FPRC;
------------------------------------------------------------------------------
|PROCESO MiraArti; |TIPO 0;
     cCodigo = #3#5;
     nModo = (FEntrada/100)!0;

     |SI nModo = 1; |O #3Campo ! Contenido;
          nCambio = 1;
     |SINO;
          nCambio = 0;
     |FINSI;
     |SI #3Campo ! Contenido;
          nCambioArt = 1;
     |FINSI;

     |SI FSalida = 10;
          ||#3#5 = "";
          |HAZ AltaArt;
          |ERROR; |ACABA;
     |FINSI;
     |SI FSalida = 11;
          |HAZ AltaBC3;
          |ERROR; |ACABA;
     |FINSI;

     |ABRE #10;
     #10#0 = #3#5;
     |LEE 000#10=;
     |SI FSalida = 0;
          |SI nCambio = 1;
               /* Control de dtos. Ofertas. */
               |ABRE #41;
               #41#0 = #0#2;
               |SI FSalida = 0;
                    fed_dt = #0#4;                || Fecha....
                    art_dt = #3#5;                || Articulo.
                    cli_dt = #0#2;                || Cliente..
                    fam_dt = #10#2;              || Familia.
                    iva_dt = #10#30;             || Tipo Iva.
                    dtos_2 = 0;                   || Descuento 2.
                    dto_pt = 0;                   || Descuento Ptas.
                    dtos_1 = 0;                   || Descuento 1.
                    pvp_ve = 0;                   || Precio Venta.
                    tarifa = #41#39;              || Tarifa Cliente....
                    gru_dt = #41#158;             || Grupo Cliente.
                    uni_dt = #3#8;
                    enMensaje = 0;
                    aParam = "";
                    eaSerie = #0#0;
                    |SI #10#176 = "S";
                         enMensaje = 1;
                    |FINSI;
                    enMensaje = 1;
                    enDirEnt = 0;
                    |HAZ calcdtos;                || Calculo Dtos.....
                    dtopts = dto_pt;              || Dto.en ptas.
                    pvp_ve = pvp_ve < dtos_1;
                    pvp_ve = pvp_ve < dtos_2;     ||Precio Neto Venta.
               |FINSI;
               |CIERRA #41;
               #3#6 = #10#1;                          ||Decsripcion Articulo.
               #3#7 = #10#5;                          ||Unidad Medida.
               #3#8 = 1;                               ||Cantidad.
               cCodMonDe = cCodMonBase;
               cCodMonA  = #0#7;
               nPrecioComp = #10#133;                 ||Ultimo Precio Compra

               |HAZ EsForestales;
               |SI FSalida = 0; |Y #10#152 ! 0;
                    nPrecioComp = #10#149 - (#10#149 * #10#152 / 100);
               |FINSI;

               nPrecioVent = pvp_ve;

               |HAZ DemonAmon;
               #3#9  = nPrecioComp;                    ||Precio Compra.
               #3#10 = nPrecioVent;                    ||Precio Venta.
               |SI #3#10 ! 0;
                    #3#13 = ((#3#10-#3#9)/#3#9)*100;   ||Incremento de Articulo.
               |FINSI;
               #3#11 = 0;                              ||Importe Compra.
               #3#12 = 0;                              ||Importe Venta.
               #3#14 = #10#126;                       ||Tipo = Material.
               |ABRE #110;
               |DDEFECTO #110;
               #110#0 = #10#126;
               |LEE 000#110=;
               #3#15 = #110#1;                          ||Descripcion Tipo.
               |CIERRA #110;
               |PINTA #3#5;|PINTA #3#6;|PINTA #3#7;
               |PINTA #3#8;|PINTA #3#9;|PINTA #3#15;
               |PINTA #3#10; |PINTA #3#13;

               |HAZ Escandallo;
          |FINSI;
     |SINO;
          |ABRE #111;
          #111#0 = #3#5;
          #111#208 = #3#0;
          #111#209 = #3#1;
          |LEE 000#111=;
          |SI FSalida = 0;
               |SI nCambio = 1;
                    /* Control de dtos. Ofertas. */
                    |ABRE #41;
                    #41#0 = #0#2;
                    |SI FSalida = 0;
                         fed_dt = #0#4;                || Fecha....
                         art_dt = #3#5;                || Articulo.
                         cli_dt = #0#2;                || Cliente..
                         fam_dt = #111#2;              || Familia.
                         iva_dt = #111#30;             || Tipo Iva.
                         dtos_2 = 0;                   || Descuento 2.
                         dto_pt = 0;                   || Descuento Ptas.
                         dtos_1 = 0;                   || Descuento 1.
                         pvp_ve = 0;                   || Precio Venta.
                         tarifa = #41#39;              || Tarifa Cliente....
                         gru_dt = #41#158;             || Grupo Cliente.
                         uni_dt = #3#8;
                         enMensaje = 0;
                         aParam = "";
                         eaSerie = #0#0;
                         |SI #111#176 = "S";
                              enMensaje = 1;
                         |FINSI;
                         enMensaje = 1;
                         enDirEnt = 0;
                         |HAZ calcdtos;                || Calculo Dtos.....
                         dtopts = dto_pt;              || Dto.en ptas.
                         pvp_ve = pvp_ve < dtos_1;
                         pvp_ve = pvp_ve < dtos_2;     ||Precio Neto Venta.
                    |FINSI;
                    |CIERRA #41;
                    #3#6 = #111#1;                          ||Decsripcion Articulo.
                    #3#7 = #111#5;                          ||Unidad Medida.
                    #3#8 = 1;                               ||Cantidad.
                    cCodMonDe = cCodMonBase;
                    cCodMonA  = #0#7;
                    nPrecioComp = #111#28;                  ||Ultimo Precio Compra
                    nPrecioVent = pvp_ve;

                    |HAZ DemonAmon;
                    #3#9  = nPrecioComp;                    ||Precio Compra.
                    #3#10 = nPrecioVent;                    ||Precio Venta.
                    |SI #3#10 ! 0;
                         #3#13 = ((#3#10-#3#9)/#3#9)*100;   ||Incremento de Articulo.
                    |FINSI;
                    #3#11 = 0;                              ||Importe Compra.
                    #3#12 = 0;                              ||Importe Venta.
                    #3#14 = #111#126;                       ||Tipo = Material.
                    |ABRE #110;
                    |DDEFECTO #110;
                    #110#0 = #111#126;
                    |LEE 000#110=;
                    #3#15 = #110#1;                          ||Descripcion Tipo.
                    |CIERRA #110;
                    |PINTA #3#5;|PINTA #3#6;|PINTA #3#7;
                    |PINTA #3#8;|PINTA #3#9;|PINTA #3#15;
                    |PINTA #3#10; |PINTA #3#13;
               |FINSI;
          |SINO;
               |MENAV "0000No existe articulo...";
               |ERROR;
          |FINSI;
          |CIERRA #111;
     |FINSI;
     |CIERRA #10;
|FPRC;
------------------------------------------------------------------------------
|PROCESO UltLin;
     nLinea = #6#1 + 1;
|FPRC;
-------------------------------------------------------------------------------
|DEFBUCLE UltLin;
     #6#1;
     ;                                          ||Campos Extras.
     #2#4,   1;
     #2#4, 999;
     ;                                          ||b/e Bolqueo-Ordenacion.
     NULL, UltLin;
|FIN;
-------------------------------------------------------------------------------
|PROCESO MinSecPre;
     ||#5#0 = " ";
|FPRC;
-----------------------------------------------------------------------------
|PROCESO MaxSecPre;
     ||#5#0 = "z";
|FPRC;
-----------------------------------------------------------------------------
|PROCESO LinDes;
     nLinea = #6#1;
|FPRC;
-------------------------------------------------------------------------------
|DEFBUCLE LinDes;
     #6#1;
     ;                                          ||Campos Extras.
     #2#4, 1;
     #2#4, 999;
     ;                                          ||b/e Bolqueo-Ordenacion.
     NULL, LinDes;
|FIN;
-------------------------------------------------------------------------------
|PROCESO APrePar; |TIPO 3;

     |ABRE #9;
     #9#0 = "A";
     |LEE 000#9=;

     |SI FSalida = 0;

          |SI #9#27 = "N";
               |ACABA;
          |FINSI;

     |FINSI;

     |CIERRA #9;

     |SI nCambio = 1;
          |CONFI "0000N¿Desa actualizar el banco de datos?";
          |SI FSalida ! 0;
               |ACABA;
          |FINSI;
     |FINSI;

     |ABRE #4;
     #4#0 = #2#4;
     |LEE 000#4=;

     |SI FSalida = 0;
          cCodMonDe = #0#7;                  ||Moneda Gopre001
          cCodMonA  = #4#5;                  ||Moneda Goartive
          nPrecioComp = #3#10;               ||Ultimo Precio Compra
          |HAZ DemonAmon;

          |SI #4#4 = "S";
|| Subtiquet nro. 00152.0002 Cliente: 003774
               |DDEFECTO #6;
               |ABRE #6;
               #6#0 = #2#4;
               #6#1 = #3#4;
               |LEE 101#6=;
               |SI FSalida ! 0; |GRABA 020#6b; |FINSI;
               #6#2 = #3#5;
/*
               |SELECT #3#6;
               #6#0 = #2#4;
               #6#2 = #3#5;
               |LEE 010#6=;
               |SI FSalida ! 0;
                    nLinea = 0;
                    |BUCLE LinDes;
                    |DDEFECTO #6;
                    #6#0 = #2#4;
                    #6#1 = nLinea + 1;
                    #6#2 = #3#5;
                    |GRABA 020#6c;
               |FINSI;
*/
               nPreAct = #6#7;
               #6#3 = #3#6;                       ||Descripcion.
               #6#4 = #3#7;                       ||Unidad Medida.
               #6#5 = #3#8;                       ||Cantidad.
               #6#6 = #3#10; ||nPrecioComp;                ||Precio Compra.
               #6#7 = #6#5 * #6#6;                ||Importe.
               #6#8 = #3#14;                      ||Tipo Elemento.
               #6#9 = #3#15;                      ||Descripcion Elemento.
               #6#10 = #3#17;
               #6#11 = #3#18;
               #6#12 = #3#19;
               #6#13 = #3#20;
               |GRABA 020#6a;
               |CIERRA #6;
               #4#15 = #4#15+(#6#7-nPreAct);
               |GRABA 020#4a;
          |SINO;
               #4#15 = nPrecioComp;
               |GRABA 020#4a;
          |FINSI;
     |FINSI;
     |CIERRA #4;
|FPRC;
-----------------------------------------------------------------------------
|PROCESO CalLinPar4; |TIPO 0;
     #gopre003#10 = #gopre003#7 * #gopre003#8;                         ||Importe Venta
     #gopre003#11 = #gopre003#7 * #gopre003#9;                         ||Importe Coste
     #gopre003#12 = ((#gopre003#8-#gopre003#9)/#gopre003#9)*100;              ||Incremento de partida.

     |SI #gopre003#21 = "S";

          |SI #gopre003#20 > #gopre003#10;
               #gopre003#17 = 100;                       ||Porcentaje total a certificar
               #gopre003#19 = 100;                       ||Porcentaje pendiente de certificar
               #gopre003#20 = #gopre003#10;                     ||Importe pendiente a certificar
          |FINSI;

     |SINO;
          #gopre003#17 = 100;                            ||Porcentaje total a certificar
          #gopre003#19 = 100;                            ||Porcentaje pendiente de certificar
          #gopre003#20 = #gopre003#10;                          ||Importe pendiente a certificar
     |FINSI;

     |PINTA #gopre003#10;
     |PINTA #gopre003#12;
|FPRC;
-----------------------------------------------------------------------------
|PROCESO ActPrecios4;
     |HAZ CogeUniFac4;

     #gopre003#8  = #gopre003#8 - #3#11;                 ||Precio Venta Partida.
     #gopre003#9  = #gopre003#9 - #3#12;                 ||Precio Coste Partida.
     #3#13 = nIncrem;
     #3#10 = #3#9;                                ||Precio Compra.
     |SI #gopre001#42 = "+";
          #3#11 = (#3#9*enUniFac) > #3#13;   ||Importe Venta.
     |SINO;
          #3#11 = (#3#9*enUniFac) / (1 - (#3#13 / 100));
     |FINSI;
     #3#12 = #3#10*enUniFac;                   ||Importe Coste.
     #gopre003#8  = #gopre003#8 + #3#11;                 ||Precio Venta Partida.
     #gopre003#9  = #gopre003#9 + #3#12;                 ||Precio Coste Partida.
     |GRABA 020#3a;

     /* actualizamos los precios de la descomposcion del banco de datos */

     |SI nBanco = 1;                              ||Hemos solicitado cambio en banco de datos.
          |ABRE #goartive;
          #goartive#0 = #gopre003#4;
          |LEE 000#goartive.=;

          |SI FSalida = 0;
               |ABRE #goartdes;                          ||Actualizo los componentes de la partida seleccionada.
               |SELECT #3#goartdes;
               #goartdes#0 = #gopre003#4;
               #goartdes#2 = #3#5;
               |LEE 010#goartdes.=;

               |SI FSalida = 0;
                    nPreAct = #goartdes#7;
                    cCodMonDe = #gopre001#7;             ||Moneda Gopre001
                    cCodMonA  = #goartive#5;             ||Moneda Goartive
                    nPrecioComp = #3#10;          ||Ultimo Precio Compra
                    |HAZ DemonAmon;
                    #goartdes#6 = nPrecioComp;           ||Precio Compra Articulo.
                    #goartdes#7 = #goartdes#5 * #goartdes#6; ||Importe compra Articulo.
                    |GRABA 020#goartdes.a;
                    #goartive#15 = #goartive#15+(#goartdes#7-nPreAct);
                    |GRABA 020#goartive.a;
               |FINSI;

               |CIERRA #goartdes;
          |FINSI;

          |CIERRA #goartive;
     |FINSI;
|FPRC;
-----------------------------------------------------------------------------
|PROCESO IncreGlobal;
     |SI #gopreaux#3 = "V";
          |SI #3#14 = #gopreaux#0;
             #3#13 = #gopreaux#2;
             nTotDes = #3#9;
             nIncrem = #3#13;
             |HAZ ActPrecios4;
             #gopre003#12 = nIncrem;
             |HAZ CalLinPar4;
          |FINSI;
     |FINSI;
     |SI #gopreaux#3 = "C";
          #3#10 = #3#10 > #gopreaux#2;
          #3#9 = #3#10;
          nIncrem = #3#13;
          |HAZ ActPrecios4;
          #gopre003#12 = nIncrem;
          |HAZ CalLinPar4;
     |FINSI;
     |SI #gopreaux#3 = "U";
          #3#8 = #3#8 > #gopreaux#2;
          |HAZ ActPrecios4;
          #gopre003#12 = nIncrem;
          |HAZ CalLinPar4;
     |FINSI;
|FPRC;
-----------------------------------------------------------------------------
|DEFBUCLE IncreGlobal;
#3#1005;
;
aSerie,nPresup,aCapitu,aPartid,001;
aSerie,nPresup,aCapitu,aPartid,999;
;
NULL,IncreGlobal;
|FIN;
-----------------------------------------------------------------------------
|PROCESO NuevoTotal;
   Calc = #3#8 * #3#10;   || Precio de coste anterior
                                        || Prorrateo el precio de coste en
                                        || funcion al nuevo total indicado


   |SI #40#1 = "P";
        Calc = ((#40#0 * Calc) / nTCosteAnt) / #3#8;
        #3#10 = Calc; #3#9 = Calc;
        nTotDes = #3#9;
        nIncrem = #3#13;
    |SINO;
        Calc = ((#40#0 * Calc) / nTCosteAnt);
        #3#8 = Calc / #3#10; #3#9 = #3#10;
        nTotDes = #3#9;
        nIncrem = #3#13;
     |FINSI;

     #gopre003#12 = nIncrem;
     |HAZ ActPrecios4;
     |HAZ CalLinPar4;
|FPRC;
-----------------------------------------------------------------------------
|DEFBUCLE NuevoTotal;
     #3#1005;
     ;
     aSerie,nPresup,aCapitu,aPartid,001;
     aSerie,nPresup,aCapitu,aPartid,999;
     ;
     NULL,NuevoTotal;
|FIN;

-----------------------------------------------------------------------------
|PROCESO Des; |TIPO 11;
     |HAZ BH004;
     |SI FSalida = 15;
          |PUSHV 0501 2380;
          |ABRE #40;
          |DDEFECTO #40;
          |LEE 000#40p;
          #40#0 = #2#9;
          |PINPA #0#40;
          |PINDA #0#40;
          |ENDATOS #1#40;
          nSal = FSalida;
          |POPV;
          |SI nSal = 0;
                nTCosteAnt = #gopre003#9;
                aSerie  = #3#0; nPresup = #3#1;
                aCapitu = #3#2; aPartid = #3#3;
                |SALVA_FICHA #3;
                |BUCLE NuevoTotal;
                |PONCONTROL 23,"si";
                |REPON_FICHA #3;
                |PTEC 808;
          |FINSI;
          #0#22 = #2#8; |PINTA 2267, #0#22;               ||Total Venta Pres.
          #0#23 = #2#9; |PINTA 2367, #0#23;               ||Total Compra Pres.
          |HAZ BS004;
          |ERROR;

     |FINSI;

     |SI FSalida = 16;
          eaCodigo = #3#5;
          cDesc    = #3#6;
          eaSerie  = #1#0;
          eaNumero = #1#1;
          |HAZ Ampliada;
          |HAZ BS004;
          |ERROR;
     |FINSI;

     |SI FSalida = 17;
        |PUSHV 01012380;
        |PINPA #0#gopreaux; |PINDA #0#gopreaux;
        |ENDATOS #1#gopreaux;

        |SI FSalida = 0;
           aSerie  = #3#0; nPresup = #3#1;
           aCapitu = #3#2; aPartid = #3#3;
           |SALVA_FICHA #3;
           |BUCLE IncreGlobal;
           |PONCONTROL 23,"si";
           |REPON_FICHA #3;
           |HAZ BS004;
        |SINO;
           |HAZ BS004;
           |ERROR;
        |FINSI;

        |POPV;
     |FINSI;

     |SI FSalida = 12;
          aAlfa = "gopre024;" + #3#0 + (("000000" + #3#1) % -106) + #3#2 + #3#3 + (("000" + #3#4) % -103) + #3#5 + "C";
          |SUB_EJECUTA_NP aAlfa;
          |HAZ BS004;
          |ERROR;
     |FINSI;
     |SI FSalida = 13;
          aAlfa = "gopre024;" + #3#0 + (("000000" + #3#1) % -106) + #3#2 + #3#3 + (("000" + #3#4) % -103) + #3#5 + "M";
          |SUB_EJECUTA_NP aAlfa;
          |HAZ BS004;
          |ERROR;
     |FINSI;
|FPRC;
-------------------------------------------------------------------------------
|PROCESO BI004; |TIPO 80;
     |CLS;
     |CONTROL_SIMPLE 0, "BOTON,F4 - Consulta Escandallo", 2305, 2325, F4004;
     nBotEscan = FSalida;
     |CONTROL_SIMPLE 0, "BOTON,F5 - Modificación Escandallo", 2330, 2350, F5004;
     nBotEscanModi = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,F7 - Ajuste total compra ", 2405, 2425, F7004;
     nBotAjust = FSalida;
     |CONTROL_SIMPLE 0, "BOTON,F8 - Descripción Ampliada", 2430, 2450, F8004;
     nBotAmpli = FSalida;
     |CONTROL_SIMPLE 0, "BOTON,F9 - Incr.Por Naturaleza", 2455, 2475, F9004;
     nBotIncre = FSalida;
     |ESTADO_CONTROL nBotAjust, "HIDE";
     |ESTADO_CONTROL nBotAmpli, "HIDE";
     |ESTADO_CONTROL nBotIncre, "HIDE";
     |ESTADO_CONTROL nBotEscan, "HIDE";
     |ESTADO_CONTROL nBotEscanModi, "HIDE";
|FPRC;
------------------------------------------------------------------------------
|PROCESO BF004; |TIPO 90;
     |FIN_CONTROL_WINDOWS nBotAjust;
     |FIN_CONTROL_WINDOWS nBotAmpli;
     |FIN_CONTROL_WINDOWS nBotIncre;
     |FIN_CONTROL_WINDOWS nBotBanco;
     |FIN_CONTROL_WINDOWS nBotEscan;
     |FIN_CONTROL_WINDOWS nBotEscanModi;
     |PULSATECLA;
|FPRC;
------------------------------------------------------------------------------
|PROCESO BS004; |TIPO 7;
     nModo = (FEntrada / 100) ! 0;
     |SI Campo = 4;
          |SI #3#5 ! "                    ";
               |ESTADO_CONTROL nBotAjust, "SHOW";
               |ESTADO_CONTROL nBotAmpli, "SHOW";
               |ESTADO_CONTROL nBotIncre, "SHOW";
               |ABRE #480;
               #480#0 = #3#5;
               |LEE 000#480=;
               |SI FSalida = 0; |Y #9#106 = "S";
                    |ESTADO_CONTROL nBotEscan, "SHOW";
                    |ESTADO_CONTROL nBotEscanModi, "SHOW";
                    |SI nModo = 4;
                         |ESTADO_CONTROL nBotEscanModi, "DISABLE";
                    |SINO;
                         |ESTADO_CONTROL nBotEscanModi, "ENABLE";
                    |FINSI;
               |FINSI;
               |CIERRA #480;
          |FINSI;

     |FINSI;

|FPRC;
-------------------------------------------------------------------------------
|PROCESO BH004; |TIPO 0;
     |ESTADO_CONTROL nBotAjust, "HIDE";
     |ESTADO_CONTROL nBotAmpli, "HIDE";
     |ESTADO_CONTROL nBotIncre, "HIDE";
     |ESTADO_CONTROL nBotEscan, "HIDE";
     |ESTADO_CONTROL nBotEscanModi, "HIDE";
|FPRC;
-------------------------------------------------------------------------------
|PROCESO F4004;
     |PTEC 826;
|FPRC;

|PROCESO F5004;
     |PTEC 827;
|FPRC;

|PROCESO F7004;
     |PTEC 829;
|FPRC;
-------------------------------------------------------------------------------
|PROCESO F8004;
     |PTEC 830;
|FPRC;
-------------------------------------------------------------------------------
|PROCESO F9004;
     |PTEC 831;
|FPRC;
-------------------------------------------------------------------------------
|PROCESO MirCam; |TIPO 0;
     |SI #3Campo ! Contenido;
          nCambio = 1;
     |FINSI;
|FPRC;
-------------------------------------------------------------------------------
/*
|PROCESO DescParam33;
     cCombina = "";
     cTitulo1 = "";cTitulo2 = "";cTitulo3 = "";cTitulo4 = "";
     cParam = #3#5;
     |QBF cParam;
     Ifiebdc = fiebdc1<-;
     fiebdc1 = "PARAMETROS";
     fiebdc2 = 400;
     fiebdc3 = cParam;
     |ESPECIFICA "FIEBDC",fiebdc;

     |SI FSalida < 0;
          ||MENAV "0000Error o no es parametrico!";
     |SINO;
          cFich1 = Usuario % 108;
          cFich1 = "A"+cFich1;
          cFich2 = Usuario % 108;
          cFich2 = "B"+cFich1;
          cFich3 = Usuario % 108;
          cFich3 = "C"+cFich1;
          cFich4 = Usuario % 108;
          cFich4 = "D"+cFich1;
          |QBF cFich1;|QBF cFich2;|QBF cFich3;|QBF cFich4
          |NOME_DAT #30 cFich1;|NOME_DAT #31 cFich2;
          |NOME_DAT #32 cFich3;|NOME_DAT #33 cFich4;
          |CIERRA #30;|DELINDEX #30;
          |CIERRA #31;|DELINDEX #31;
          |CIERRA #32;|DELINDEX #32;
          |CIERRA #33;|DELINDEX #33;
          |ABRE #30;|ABRE #31;|ABRE #32;|ABRE #33;
          Ifiebdc = fiebdc5<-;

          |PARA i = 0;|SI i < fiebdc4;|HACIENDO i = i + 1;
               k = fiebdc;
               Ifiebdc = Ifiebdc  + 1;

               |PARA j = 0;|SI j < k;|HACIENDO j = j + 1;
                    nCarac = 96+j;

                    |SI j = 0;
                         |SI i = 0;cTitulo1 = fiebdc;|FINSI;
                         |SI i = 1;cTitulo2 = fiebdc;|FINSI;
                         |SI i = 2;cTitulo3 = fiebdc;|FINSI;
                         |SI i = 3;cTitulo4 = fiebdc;|FINSI;
                    |SINO;
                         Ifiebdc = Ifiebdc + 1;

                         |SI i = 0;
                              #30#0 = " ";#30#1 = &nCarac;
                              #30#2 = fiebdc;|GRABA 020#30c;
                         |FINSI;

                         |SI i = 1;
                              #31#0 = " ";#31#1 = &nCarac;
                              #31#2 = fiebdc;|GRABA 020#31c;
                         |FINSI;

                         |SI i = 2;
                              #32#0 = " ";#32#1 = &nCarac;
                              #32#2 = fiebdc;|GRABA 020#32c;
                         |FINSI;

                         |SI i = 3;
                              #33#0 = " ";#33#1 = &nCarac;
                              #33#2 = fiebdc;|GRABA 020#33c;
                         |FINSI;

                    |FINSI;

               |SIGUE;

               Ifiebdc = Ifiebdc + 1;

          |SIGUE;

          |PUSHV 0101 2380;
          |ENTLINEAL #1#30, 1, 4, 14, 1, MinFicDes, MaxFicDes;
          |POPV;
          |CIERRA #30;|DELINDEX #30;|CIERRA #31;|DELINDEX #31;
          |CIERRA #32;|DELINDEX #32;|CIERRA #33;|DELINDEX #33;
          cParam = cParam - "$";
          |QBF cParam;
          |QBF cCombina;
          cParam = cParam + cCombina;
          Ifiebdc = fiebdc1<-;
          fiebdc1 = "PARAMETRICO";
          fiebdc2 = 400;
          fiebdc3 = cParam;
          |ESPECIFICA "FIEBDC",fiebdc;

          |SI FSalida < 0;
               aAlfa = "0000Error en parametrico! ("+fiebdc4+")"+fiebdc5;
               |MENAV aAlfa;
               nSalida = -1;
               nErrorPar = 1;
          |SINO;
               |HAZ DescAmplDes;
               #3#5  = fiebdc3;
               #3#6  = fiebdc27;             ||Descripcion.
               #3#10 = fiebdc28;             ||Precio Compra.
               |PINTA #3#5; |PINTA #3#6; |PINTA #3#10;
          |FINSI;

     |FINSI;
|FPRC;
--------------------------------------------------------------------------------
|PROCESO MinFicDes;
     #30#1 = " ";
|FPRC;
--------------------------------------------------------------------------------
|PROCESO MaxFicDes;
     #30#1 = "z";
|FPRC;
*/
--------------------------------------------------------------------------------
|PROCESO DescAmplDes;
     |ABRE #18;
     #18#0 = fiebdc3;
     #18#1 = fiebdc27;
     |LEE 000#18=;

     |SI FSalida ! 0;
          nLinea = 0;
          |ABRE #19;
          Ifiebdc = fiebdc6<-;

          |PARA r = 1; |SI r [ 20; |HACIENDO r = r + 1;
               |DDEFECTO #19;
               cCadena = fiebdc;
               |QBF cCadena;

               |SI cCadena = "";
                    |SAL;
               |FINSI;

               nLinea = nLinea + 1;
               #19#0 = #18#0;
               #19#1 = nLinea;
               #19#2 = cCadena;
               |GRABA 020#19c;
               Ifiebdc = Ifiebdc + 1;

          |SIGUE;

          |CIERRA #19;
          |GRABA 020#18c;
     |FINSI;

     |GRABA 020#18a;
     |CIERRA #18;
|FPRC;
--------------------------------------------------------------------------------
|PROCESO TipoElem; |TIPO 7;
/*
     |SI #3#14 = " ";
          |C_M #3#14, 1, "S";
     |SINO;
          |C_M #3#14, 1, "N";
     |FINSI;
*/
|FPRC;
-------------------------------------------------------------------------------
|PROCESO AltaBC3;
     |CONFI "0000SDESEA CREAR/MODIFICAR EL ARTICULO INTRODUCIDO";
     |SI FSalida = 0;
          |PUSHV 0501 2380;
          |DDEFECTO #111;
          |ABRE #111;
          #111#0 = #3#5;
          #111#208 = #0#0;
          #111#209 = #0#1;
          |LEE 101#111=;
          |SI FSalida = 0;
               |PINPA #0#111;
               |PINDA #0#111;
               |ENDATOS #2#111;
               |SI FSalida = 0; |GRABA 020#111a; |FINSI;
          |SINO;
               |PINPA #0#111;
               |PINDA #0#111;
               |ENDATOS #1#111;
               |SI FSalida = 0; |GRABA 020#111n; |FINSI;
          |FINSI;
          |CIERRA #111;
          |POPV;
     |FINSI;
|FPRC;

|PROCESO AltaArt;
     |CONFI "0000SDESEA CREAR EL ARTICULO INTRODUCIDO";
     |SI FSalida = 0;
          |PUSHV 01012380;
          |CLS;
          |SUB_EJECUTA "goaltaar";
          #3#5 = aCodAlta;
          |POPV;
     |FINSI;
|FPRC;

|PROCESO Min;
     #111#0 = " " * 20;
     #111#208 = #0#0;
     #111#209 = #0#1;
|FPRC;

|PROCESO Max;
     #111#0 = "z" * 20;
     #111#208 = #0#0;
     #111#209 = #0#1;
|FPRC;

|PROCESO BuscaArt; |TIPO 66;
     |ABRE #111;
     |SELECT #2#111;
     #111#208 = #0#0;
     #111#209 = #0#1;
     #111#0 = "";
     |LEE 000#111];
     |SI FSalida = 0; |Y #111#208 = #0#0; |Y #111#209 = #0#1;
          nSal = 0;
     |SINO;
          nSal = 1;
     |FINSI;
     |CIERRA #111;

     |SI nSal = 0;
          |MENU aMenu;
          aMenu2 = FSalida;
          |SI aMenu2 [ 0; |ACABA; |FINSI;
     |SINO;
          aMenu2 = 1;
     |FINSI;

     |SI aMenu2 = 1;
          |ABRE #10;
          #10#0 = #3#5;
          |LEE 000#10];
          |CONSULTA_CLAVE #1#10;
          |SI FSalida ! 0;
               |ERROR;
          |SINO;
               #3#5 = #10#0;
               #3#6 = #10#1;
               |PINTA #3#5;
               |PINTA #3#6;
          |FINSI;
          |CIERRA #10;
     |SINO;
          |ABRE #111;
          #111#0 = #3#5;
          #111#208 = #0#0;
          #111#209 = #0#1;
          |LEE 000#111];
          |CONSULTA_F_CLAVE #2#111, Min, Max;
          |SI FSalida ! 0;
               |ERROR;
          |SINO;
               #3#5 = #111#0;
               #3#6 = #111#1;
               |PINTA #3#5;
               |PINTA #3#6;
          |FINSI;
          |CIERRA #111;
     |FINSI;
|FPRC;

|PROCESO DefArti; |TIPO 0;
     eaArtiAnt = #3#5;
|FPRC;

|PROCESO DobleUnidad; |TIPO 7;
     aPide2Uni = "N";
     |C_M #3#8, 1, "S";

     |ABRE #4;
     #4#0 = #3#3;
     |LEE 000#4=;
     |SI #4#4 = "N";          || Sin descomposicion
          |ABRE #50;
          #50#0 = #3#7;
          |LEE 000#50=;
          |SI FSalida = 0; |Y #50#12 = "S";
               aPide2Uni = "S";
          |FINSI;
          |CIERRA #4;
     |SINO;
          |ABRE #10;
          #10#0 = #3#5;
          |LEE 000#10=;
          |SI FSalida = 0; |Y #10#179 = "S";
               aPide2Uni = "S";
          |FINSI;
          |CIERRA #10;
     |FINSI;
     |CIERRA #50;

     |SI aPide2Uni = "S";
          ||eaArtiAnt  (se carga en el tipo 0 de la linea)
          eaArticulo = #3#5;
          enUnidad1 = #3#8;
          enUnidad2 = #3#17;
          enMedida1 = #3#18;
          enMedida2 = #3#19;
          enMedida3 = #3#20;
          eaPartida = "";
          enModoVta = (FEntrada / 100) ! 0;
          |SUB_EJECUTA_NP "dsz99907";
          |SI eaSw2Stock = "S";
               |C_M #3#8, 1, "N";
               #3#8 = enUnidad1; |PINTA #3#8;
               #3#17 = enUnidad2;
               #3#18 = enMedida1;
               #3#19 = enMedida2;
               #3#20 = enMedida3;
          |SINO;
               #3#17 = 0;
               #3#18 = 0;
               #3#19 = 0;
               #3#20 = 0;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO TotUni; |TIPO 0;
     |SI #3Campo ! Contenido; |O nCambioArt = 1;
          #3#8 = #3#21 / #2#7;
          |HAZ CalLinDes;
          |PINTA #3#8;
     |FINSI;
|FPRC;
