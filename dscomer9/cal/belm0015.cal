|FICHEROS;
     belm0015 #0;
     belm0011 #1;
     belm0012 #2;
     belm0013 #3;
     belm0014 #4;
     belm0016 #6;||TMP
     belm0017 #7;
     belm0018 #8;
     belm0019 #9;

     agifa010 #10;
     agifa071 #71;
     agifa077 #77;
     agifa080 #80;
     agifa019 #19;
|FIN;

|VARIABLES;
     &enTotaC = 0;
     &enTotaV = 0;

     nDeciPeso = 3;  ||Cambiar un decimal supondria cambiar el def dsint006
     nDeciUni  = 3;
     nDeciImpo = 2;

     &aDivisa  = "";
     &aMoneda  = "";
     &Tempo    = "";
     aParti    = "";
     nBoton1   = 0;
     i         = 0;
     nDeci     = 0;
     nHandle   = 0;
     nNume     = 0;
     aFic      = "";
     aAlfa     = "";
     aReg      = "";
     aRuta     = "";
     fFecha    = @;
     aHora     = "";
     aAlf1     = "";
     aAlf2     = "";
     aAlf3     = "";
     aAlf4     = "";
     aAntes    = "";
     dFecha    = @;
     hFecha    = @;
     iFecha    = @;
     aFam      = "";
     nTotaPesoC= 0;
     nTotaUniC = 0;
     nTotaImpoC= 0;
     nTotaPesoV= 0;
     nTotaUniV = 0;
     nTotaImpoV= 0;

     &nUni     = 0;
     &nPeso    = 0;
     &nImpo    = 0;
     &aTipo    = "";

     nMensajes = 0;
     nLinea    = 0;
     nSegmento = 0;

     nTotaPeso = 0;
     nTotaUni  = 0;
     nTotaImpo = 0;
     nImpre    = 1;
     nInfo     = 1;

     aLon = "";
     aNum = "";
     aCar = "";
     nPos = 0;
     j = 0;
     k = 0;
     aCompraVenta = "";
     nTotaEsta = 0;
     nTotaEstaC = 0;
     nTotaEstaV = 0;
|FIN;
컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
|PROCESO Punto2Coma;
     aAlfa = aAlf1 - ".";
     |SI aAlf1 = aAlfa;
         aAlf1 = "0" * nDeci;
         aAlf1 = aAlfa + "."  + aAlf1;
     |FINSI;
     aLon = aAlf1 % A0;
     aNum = "";
     |PARA j = 1; |SI j [ aLon; |HACIENDO j = j + 1;
          nPos = j * 100 + 1;
          aCar = aAlf1 % nPos;
          |SI aCar = "."; k = j; aCar = ","; |FINSI;
          aNum = aNum + aCar;
     |SIGUE;
     k = j - k;
     |PARA j = k; |SI j [ nDeci; |HACIENDO j = j + 1;
          aNum = aNum + "0";
     |SIGUE;
     aAlf1 = aNum;
|FPRC;

컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
|PROCESO Imprime;
     |PRINT;
|FPRC;

|DEFBUCLE Imprime;
     #6#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, Imprime;
|FIN;
컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
|PROCESO UNB;
     aReg = "UNB+UNOC:3+";

     aAlf1 = #1#3; |QBF aAlf1;          ||Ident. Emisor
     aReg = aReg + aAlf1 + "+DA::DA+";

     aAlf1 = fFecha % 102;
     aAlf2 = fFecha % 402;
     aAlf3 = fFecha % -104;
     aAlf1 = aAlf3 + aAlf2 + aAlf1;
     aAlf2 = aHora % 102;
     aAlf3 = aHora % 402;
     aAlf2 = aAlf2 + aAlf3;
     aReg = aReg + aAlf1 + ":" + aAlf2 + "+"; ||Fecha/Hora

     aAlf1 = #0#5; |QBF aAlf1;
     aReg = aReg + aAlf1 + "+";         ||Referencia

     aReg = aReg + "+++";               || no usados

     aAlf1 = #1#7; |QBF aAlf1;
     aReg = aReg + aAlf1 + "'";         ||Peticion acuse recepcion

     |HAZ Graba;
|FPRC;

|PROCESO UNZ;
     aReg = "UNZ+";

     aAlf1 = nMensajes; |QBF aAlf1;
     aReg = aReg + aAlf1 + "+";    ||N Mensajes (veces UNH)

     aAlf1 = #0#5; |QBF aAlf1;     ||Referencia
     aReg = aReg + aAlf1 + "'";

     |HAZ Graba;
|FPRC;

|PROCESO UNH;
     nSegmento = nSegmento + 1;

     aReg = "UNH+";
     aAlf1 = #0#5; |QBF aAlf1;     ||Referencia
     aReg = aReg + aAlf1 + "+";

     aReg = aReg + "CUSDEC:1:921:UN:148401'";
     |HAZ Graba;
|FPRC;

|PROCESO BGM;
     nSegmento = nSegmento + 1;

     aReg = "BGM+";
     aReg = aReg + "896+";

     aAlf1 = #1#8; |QBF aAlf1;
     aAlf2 = ("00" + #0#3) % -102;
     aAlf3 = ("00" + #0#4) % -102;
     aAlf4 = #1#9; |QBF aAlf2;

     aReg = aReg + aAlf1 + aAlf2 + aAlf3 + #6#0 + "AA" + aAlf4 + "'";
     |HAZ Graba;

     #1#9 = #1#9 + 1;    || N Declaracion
     |GRABA 020#1a;
|FPRC;

|PROCESO CST;
     nSegmento = nSegmento + 1;

     aReg = "CST++" + #6#0 + ":176'";
     |HAZ Graba;
|FPRC;

|PROCESO DTM;
     nSegmento = nSegmento + 1;
     aReg = "DTM+";
     aAlf1 = ("00" + #0#3) % -102;
     aAlf2 = ("00" + #0#4) % -102;
     aReg = aReg + "320:" + aAlf1 + aAlf2 + "'";
     |HAZ Graba;

     nSegmento = nSegmento + 1;
     aReg = "DTM+";
     aAlf1 = fFecha % 102;
     aAlf2 = fFecha % 402;
     aAlf3 = fFecha % -102;
     aAlf1 = aAlf3 + aAlf2 + aAlf1;
     aReg = aReg + "137:" + aAlf1 + "'";
     |HAZ Graba;
|FPRC;

|PROCESO RFF;
     nSegmento = nSegmento + 1;
     aReg = "RFF+ACD:Intrast::3.20c'";
     |HAZ Graba;
|FPRC;

|PROCESO GIS;
     nSegmento = nSegmento + 1;
     aReg = "GIS+E:105'";
     |HAZ Graba;
|FPRC;

|PROCESO NAD;
     nSegmento = nSegmento + 1;
     aReg = "NAD+DT+";
     aAlf1 = #1#8; |QBF aAlf1;
     aAlf2 = #1#10; |QBF aAlf2;
     aReg = aReg + aAlf1 + "+" + aAlf2 + "'";
     |HAZ Graba;

     aAlf1 = #1#11; |QBF aAlf1;
     |SI aAlf1 ! "";  || Modificado segun  00460.0007 Cliente: 003308 PCEGroup
          nSegmento = nSegmento + 1;
          aReg = "NAD+AE+";
          aReg = aReg + aAlf1 + "'";
          |HAZ Graba;
     |FINSI;

     nSegmento = nSegmento + 1;
     aReg = "NAD+DO+DA'";
     |HAZ Graba;
|FPRC;

|PROCESO MOA;
     nSegmento = nSegmento + 1;
     aReg = "MOA+ZZZ:EUR'";
     |HAZ Graba;

     nSegmento = nSegmento + 1;
     aAlf1 = nTotaImpo;
     nDeci = nDeciImpo;
     |HAZ Punto2Coma;
     aReg = "MOA+39:" + aAlf1 + ":EUR'";
     |HAZ Graba;


     nSegmento = nSegmento + 1;
     aAlf1 =  nTotaEsta;
     nDeci = nDeciImpo;
     |HAZ Punto2Coma;
     aReg = "MOA+128:" + aAlf1 + ":EUR'";
     |HAZ Graba;
|FPRC;

|PROCESO UNS;
     nSegmento = nSegmento + 1;
     aReg = "UNS+D'";
     |HAZ Graba;
|FPRC;

|PROCESO CST_D;
     nSegmento = nSegmento + 1;
     aReg = "CST+";
     aReg = aReg + nLinea + "+";

     aAlf1 = #6#1; |QBF aAlf1;

     aReg = aReg + aAlf1 + ":122++11:112+1:177'";
     |HAZ Graba;
|FPRC;

|PROCESO LOC;
     nSegmento = nSegmento + 1;
     aReg = "LOC+";
     |SI #6#0 = "A";          || Introdu.
          aReg = aReg + "35+";
     |SINO;                   || Exped.
          aReg = aReg + "36+";
     |FINSI;

     aAlf1 = #6#3; |QBF aAlf1;

     aReg = aReg + aAlf1 + "'";

     |HAZ Graba;

     nSegmento = nSegmento + 1;
     aReg = "LOC+";
     |SI #6#0 = "A";          || Introdu.
          aReg = aReg + "47+";
     |SINO;                   || Exped.
          aReg = aReg + "106+";
     |FINSI;

     aAlf1 = #6#4; |QBF aAlf1;

     aReg = aReg + aAlf1 + "'";

     |HAZ Graba;
|FPRC;

|PROCESO MEA;
     nSegmento = nSegmento + 1;
     aReg = "MEA+WT++KGM:";
     aAlf1 = #6#5; |QBF aAlf1;
     nDeci = nDeciPeso;
     |HAZ Punto2Coma;
     aReg = aReg + aAlf1 + "'";
     |HAZ Graba;
|FPRC;

|PROCESO TDT;
     nSegmento = nSegmento + 1;
     aReg = "TDT+2++3'";
     |HAZ Graba;
|FPRC;

|PROCESO MOA_E;     || Partida Estadistica
     nSegmento = nSegmento + 1;
     aReg = "MOA+123:";
     aAlf1 = #6#9; |QBF aAlf1;
     nDeci = nDeciImpo;
     |HAZ Punto2Coma;
     aReg = aReg + aAlf1 + "'";
     |HAZ Graba;
|FPRC;

|PROCESO MOA_D;
     nSegmento = nSegmento + 1;
     aReg = "MOA+38:";
     aAlf1 = #6#10; |QBF aAlf1;
     nDeci = nDeciImpo;
     |HAZ Punto2Coma;
     aReg = aReg + aAlf1 + "'";
     |HAZ Graba;
|FPRC;

|PROCESO TOD;
     nSegmento = nSegmento + 1;
     aReg = "TOD+++";
     aAlf1 = #6#8; |QBF aAlf1;
     aReg = aReg + aAlf1 + "'";
     |HAZ Graba;
|FPRC;

|PROCESO UNS_D;
     nSegmento = nSegmento + 1;
     aReg = "UNS+S'";
     |HAZ Graba;
|FPRC;

|PROCESO CNT;
     nSegmento = nSegmento + 1;
     aAlf1 = nLinea; |QBF aAlf1;
     aReg = "CNT+2:" + aAlf1 + "'";
     |HAZ Graba;

     nSegmento = nSegmento + 1;
     aAlf1 = nTotaPeso; |QBF aAlf1;
     nDeci = nDeciPeso; |HAZ Punto2Coma;
     aReg = "CNT+18:" + aAlf1 + "'";
     |HAZ Graba;

     nSegmento = nSegmento + 1;
     aAlf1 = nTotaUni; |QBF aAlf1;
     nDeci = nDeciUni; |HAZ Punto2Coma;
     aReg = "CNT+19:" + aAlf1 + "'";
     |HAZ Graba;

     nSegmento = nSegmento + 1;
     aAlf1 = nTotaImpo; |QBF aAlf1;
     nDeci = nDeciImpo; |HAZ Punto2Coma;
     aReg = "CNT+20:" + aAlf1 + "'";
     |HAZ Graba;

     nSegmento = nSegmento + 1;
     aAlf1 = nTotaImpo; |QBF aAlf1;
     nDeci = nDeciImpo; |HAZ Punto2Coma;
     aReg = "CNT+21:" + aAlf1 + "'";
     |HAZ Graba;
|FPRC;

|PROCESO AUT;
     nSegmento = nSegmento + 1;
     aReg = "AUT+TARIC'";
     |HAZ Graba;
|FPRC;

|PROCESO DTM_D;
     nSegmento = nSegmento + 1;

     aAlf1 = fFecha % 102;
     aAlf2 = fFecha % 402;
     aAlf3 = fFecha % -102;
     aAlf1 = aAlf3 + aAlf2 + aAlf1;
     aReg = "DTM+187:" + aAlf1 + "'";
     |HAZ Graba;
|FPRC;

|PROCESO UNT;
     nSegmento = nSegmento + 1;

     aAlf1 = nSegmento; |QBF aAlf1;
     aAlf2 = #0#5; |QBF aAlf2;
     aReg = "UNT+" + aAlf1 + "+" + aAlf2 + "'";
     |HAZ Graba;
|FPRC;
컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
|PROCESO Inicio;
     |ABRE #1;
     |LEE 101#1p;
     |SI FSalida ! 0; |GRABA 020#1b; |FINSI;
     |HAZ UNB;
|FPRC;

|PROCESO Fin;
     |HAZ FinDetalle;
     |HAZ UNZ;
     |CIERRA #1;
|FPRC;

|PROCESO FinDetalle;
     |HAZ UNS_D;
     |HAZ CNT;
     |HAZ AUT;
     |HAZ DTM_D;
     |HAZ UNT;
     nLinea = 0;
     nSegmento = 0;
|FPRC;

|PROCESO Detalle;
     |SI #6#7 [ 0;
          |ACABA;
     |FINSI;

     |SI #6#0 = "A";
          nTotaPeso = nTotaPesoC;
          nTotaUni  = nTotaUniC;
          nTotaImpo = nTotaImpoC;
          nTotaEsta = nTotaEstaC;
     |SINO;
          nTotaPeso = nTotaPesoV;
          nTotaUni  = nTotaUniV;
          nTotaImpo = nTotaImpoV;
          nTotaEsta = nTotaEstaV;
     |FINSI;

     |SI aAntes ! #6#0;
          |SI aAntes ! "";
               |HAZ FinDetalle;
          |FINSI;
          nMensajes = nMensajes + 1;
          |HAZ UNH;
          |HAZ BGM;
          |HAZ CST;
          |HAZ DTM;
          |HAZ GIS;
          |HAZ RFF;
          |HAZ NAD;
          |HAZ MOA;
          |HAZ UNS;
          aAntes = #6#0;
     |FINSI;

     nLinea = nLinea + 1;
     |HAZ CST_D;
     |HAZ LOC;
     |HAZ MEA;
     |HAZ TDT;
     |HAZ MOA_D;
     |HAZ MOA_E;
     |HAZ TOD;
|FPRC;

|DEFBUCLE Temporal;
     #6#1;
     ;
     INICIO;
     FINAL;
     ;
     Inicio, Detalle, NULL, NULL, Fin;
|FIN;

|PROCESO Graba;
     |XGRABA nHandle, aReg;
     aReg = "";
|FPRC;

|PROCESO Crea;
     aFic = #0#6; |QBF aFic;

     |XABRE "CWB", aFic, nHandle;
     |SI FSalida ] 0;
          |BUCLE Temporal;
          |XCIERRA nHandle;
     |SINO;
          |MENAV "0000No puedo crear el fichero...";
     |FINSI;
|FPRC;
컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
|PROCESO ValorEstadico;
     |DDEFECTO #6;
     #6#0 = aCompraVenta;
     #6#1 = #9#4;
     #6#2 = #2#2;
     #6#8 = #2#3;
     #6#3 = #2#4;            || Pais
     #6#4 = #2#5;            || Prov
     |LEE 101#6=;
     |SI FSalida ! 0; |GRABA 020#6b; |FINSI;

     #6#9 = #6#9 + #9#6;
     #6#10 = #6#10 + #9#5;
     |GRABA 020#6a;
     |LIBERA #6;

     |SI #6#0 = "A";
          nTotaImpoC = nTotaImpoC + #9#5;
          nTotaEstaC = nTotaEstaC + #9#6;
     |SINO;
          nTotaImpoV = nTotaImpoV + #9#5;
          nTotaEstaV = nTotaEstaV + #9#6;
     |FINSI;
|FPRC;

|PROCESO CabCom;
     aDivisa = #agifa077#56;
     aMoneda = #agifa077#58;
     |HAZ Divisas077;

     #2#0 = "I";
     #2#8 = "A";
     #2#6 = #77#50;
     #2#7 = #77#0;
     |LEE 000#2=;
     |SI FSalida ! 0;
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO FinCompras;
     |SI #1#12 = "S";
          |BUCLELIN 0 ValorEstadico #2;
     |FINSI;
|FPRC;

|PROCESO Compras;
     |DDEFECTO #19;
     #19#0 = #80#2;
     |LEE 000#19=;

     aFam = #19#2;

     #4#1 = #19#0;
     |LEE 000#4=;
     |SI FSalida = 0;
          aFam = #4#0;
     |FINSI;

     #3#0 = aFam;
     |LEE 000#3=;
     |SI FSalida ! 0; |ACABA; |FINSI;

     aParti = #3#2;

     nPeso = (#80#5 * #19#102) ! nDeciPeso;
     nUni  = (#80#5) ! nDeciUni;
     ||nImpo = ((#80#5 * #80#6) * 100) ! nDeciImpo;
     nImpo = (#80#5 * #80#6) ! nDeciImpo;    || modificado para pcegroup

     aCompraVenta = "A";
     |DDEFECTO #6;
     #6#0 = "A";
     #6#1 = aParti;
     #6#2 = #2#2;
     #6#8 = #2#3;
     #6#3 = #2#4;            || Pais
     #6#4 = #2#5;            || Prov
     |LEE 101#6=;
     |SI FSalida ! 0; |GRABA 020#6b; |FINSI;

     #6#5 = #6#5 + nPeso;
     #6#6 = #6#6 + nUni;
     #6#7 = #6#7 + nImpo;
     |GRABA 020#6a;
     |LIBERA #6;
     nTotaPesoC = nTotaPesoC + nPeso;
     nTotaUniC  = nTotaUniC + nUni;
||     nTotaImpoC = nTotaImpoC + nImpo;

     aTipo = "C";
     |SI nInfo = 0;
          #9#0 = #2#0;
          #9#1 = #2#8;
          #9#2 = #2#6;
          #9#3 = #2#7;
          #9#4 = aParti;
          |LEE 000#9=;
          |SI FSalida ! 0; |DDEFECTO #9; |FINSI;
          |PRINT;
     |FINSI;
|FPRC;

|PROCESO CabVta;
     aDivisa = #10#68;
     aMoneda = #10#70;
     |HAZ Divisas010;

     #2#0 = "E";
     #2#8 = "A";
     #2#6 = #10#52;
     #2#7 = #10#0;
     |LEE 000#2=;
     |SI FSalida ! 0;
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO FinVentas;
     |SI #1#12 = "S";
          |BUCLELIN 0 ValorEstadico #2;
     |FINSI;
|FPRC;

|PROCESO Ventas;
     |DDEFECTO #19;
     #19#0 = #71#2;
     |LEE 000#19=;

     aFam = #19#2;

     #4#1 = #19#0;
     |LEE 000#4=;
     |SI FSalida = 0;
          aFam = #4#0;
     |FINSI;

     #3#0 = aFam;
     |LEE 000#3=;
     |SI FSalida ! 0; |ACABA; |FINSI;

     aParti = #3#2;

     nPeso = (#71#5 * #19#102) ! nDeciPeso;
     nUni  = (#71#5) ! nDeciUni;
     ||nImpo = ((#71#5 * #71#6) * 100) ! nDeciImpo;
     nImpo = (#71#5 * #71#6) ! nDeciImpo;    || modificado para pcegroup

     aCompraVenta = "D";
     |DDEFECTO #6;
     #6#0 = "D";
     #6#1 = aParti;
     #6#2 = #2#2;
     #6#8 = #2#3;
     #6#3 = #2#4;              || Pais
     #6#4 = #2#5;              || Prov
     |LEE 101#6=;
     |SI FSalida ! 0; |GRABA 020#6b; |FINSI;

     #6#5 = #6#5 + nPeso;
     #6#6 = #6#6 + nUni;
     #6#7 = #6#7 + nImpo;
     |GRABA 020#6a;
     |LIBERA #6;
     nTotaPesoV = nTotaPesoV + nPeso;
     nTotaUniV  = nTotaUniV + nUni;
||     nTotaImpoV = nTotaImpoV + nImpo;

     aTipo = "V";
     |SI nInfo = 0;
          #9#0 = #2#0;
          #9#1 = #2#8;
          #9#2 = #2#6;
          #9#3 = #2#7;
          #9#4 = aParti;
          |LEE 000#9=;
          |SI FSalida ! 0; |DDEFECTO #9; |FINSI;
          |PRINT;
     |FINSI;
|FPRC;

|DEFBUCLE Compras;
     #77#3;
     ;
     "      ", dFecha, #0#1, 000001;
     "zzzzzz", hFecha, #0#1, 999999;
     ;
     NULL, CabCom, Compras, FinCompras;
|FIN;

|DEFBUCLE Ventas;
     #10#4;
     ;
     #0#1, dFecha, 000001;
     #0#1, hFecha, 999999;
     ;
     NULL, CabVta, Ventas, FinVentas;
|FIN;
컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
|PROCESO Boton1;
     |PTEC 824;
|FPRC;

|PROCESO Browser;
     |SI FSalida ! 10; |ACABA; |FINSI;

     aRuta = #0#6;  |QBF aRuta;
     |SI aRuta = "";
         aRuta = "\*.*";
     |FINSI;
     aRuta = "F" + aRuta;

     |BROWSE aRuta;

     aRuta = aRuta % 299;
     #0#6 = aRuta;
     |PINTA #0#6;
|FPRC;

|PROCESO Quita; |TIPO 0;
     |ESTADO_CONTROL nBoton1, "DISABLE";
|FPRC;

|PROCESO Pone; |TIPO 7;
     |ESTADO_CONTROL nBoton1, "ENABLE";
|FPRC;

|PROGRAMA;
     |ABRE #1; |LEE 000#1p; |CIERRA #1;

     |CONTROL_SIMPLE 0, "BOTON, Fichero", 919, 926, Boton1;
     nBoton1 = FSalida;
     |HAZ Quita;

     |HORA aHora;
     fFecha = ~;
     #0#4 = (A\(fFecha % 402));
     #0#3 = (A\(fFecha % -104));

     |ABRE #0;
     |LEE 101#0=;
     |SI FSalida ! 0; |GRABA 020#0b; |FINSI;

     |CLS;
     |PINPA #0#0;
     |PINDA #0#0;
     |ENDATOS #1#0;
     |SI FSalida = 0;
          |GRABA 020#0a;
          aAlf1 = ("00" + #0#4) % -102;
          aAlf2 = ("0000" + #0#3) % -104;
          aAlf1 = "01." + aAlf1 + "." + aAlf2;
          dFecha = aAlf1;
          |PARA iFecha = dFecha; |SI; |HACIENDO iFecha = iFecha + 1;
               aAlf1 = iFecha % 402;
               |SI aAlf1 ! #0#4; |SAL; |FINSI;
               hFecha = iFecha;
          |SIGUE;

          |HAZ CreaTempo; |NOME_DAT #6 Tempo; |DELINDEX #6;

          |ABRE #2;
          |ABRE #3;
          |ABRE #4;
          |ABRE #6;
          |ABRE #9;
          |ABRE #19;

          |SI #0#8 = "S";
               |IMPRE 0;
               nImpre = FSalida;
               |SI FSalida = 0;
                    |INFOR "belm0a15";
                    nInfo = FSalida;
               |FINSI;
          |FINSI;

          |SI #0#2 = "I"; |O #0#2 = "A";
               |BUCLE Compras;
          |FINSI;
          |SI #0#2 = "E"; |O #0#2 = "A";
               |BUCLE Ventas;
          |FINSI;

          enTotaV = nTotaEstaV;
          enTotaC = nTotaEstaC;
          |SI nImpre = 0;
               |SI nInfo = 0;
                    |PIEINF;
                    |FININF;
               |FINSI;
               |FINIMP;
          |FINSI;

          |CIERRA #19;
          |CIERRA #9;
          |CIERRA #6;
          |CIERRA #4;
          |CIERRA #3;
          |CIERRA #2;

          |HAZ Crea;

          |SI #0#7 = "S";
               |IMPRE 0;
               |SI FSalida = 0;
                    |INFOR "belm0015";
                    |SI FSalida = 0;
                         |BUCLE Imprime;
                         |PIEINF;
                         |FININF;
                    |FINSI;
                    |FINIMP;
               |FINSI;
          |FINSI;

          |DELINDEX #6; |HAZ BoraTempo;
     |FINSI;
     |CIERRA #0;

     |FIN_CONTROL_WINDOWS nBoton1;
     |PULSATECLA;
|FPRO;
