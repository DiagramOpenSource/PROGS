|FICHEROS;
     malpe162 #0;
     malpe154 #154; || Tabajadores
     malpe156 #156; || Partes
     malpe158 #158; || Fabricas
     malpe164 #164; || Tmp
|FIN;

|VARIABLES;
     &Tempo = "";
     i = 0;
     nDiasMes = 0;
     fFecha = @;
     aDFecha = "";
     aHFecha = "";
     aMes = "";
     aA¤o = "";
     aAlfa = "";
     nDia = 0;
     nCampo = 0;
     aDia = "";
     f = @;
|FIN;

|PROCESO PonDias;
     nCampo = 67;
     |PARA f = aDFecha; |SI f [ aHFecha; |HACIENDO f = f + 1;
          aDia = f % 102;
          #164nCampo = aDia;
          nCampo = nCampo + 1;
     |SIGUE;
     |GRABA 020#164a;
|FPRC;

|DEFBUCLE PonDias;
     #164#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, PonDias;
|FIN;

|PROCESO Partes;
     |DDEFECTO #154;
     #154#0 = #156#0;
     |LEE 000#154=;
     |SI #0#5 = 2;
          |SI #154#12 ! #0#6; |ACABA; |FINSI;
     |FINSI;
     |SI #0#5 = 3;
          |SI #154#12 ! #0#7; |Y #154#12 ! #0#8; |Y #154#12 ! #0#9; |Y #154#12 ! #0#10;
               |ACABA;
          |FINSI;
          |SI #154#5 ! #0#11; |Y #154#5 ! #0#12; |Y #154#5 ! #0#13; |Y #154#5 ! #0#14; |Y #154#5 ! #0#15;
                    |Y #154#5 ! #0#16; |Y #154#5 ! #0#17; |Y #154#5 ! #0#18; |Y #154#5 ! #0#19; |Y #154#5 ! #0#20;
               |ACABA;
          |FINSI;
     |FINSI;

     |DDEFECTO #164;
     #164#0 = #156#0;
     #164#1 = #156#2;
     #164#2 = #156#3;
     |LEE 101#164=;
     |SI FSalida ! 0; |GRABA 020#164b; |FINSI;
     #164#3 = nDiasMes;
     aDia = #156#1 % 102;    || Dia
     nDia = aDia;
     |SI #0#4 = 2;
          nDia = nDia - 15;
     |FINSI;
     nCampo = nDia + 3;
     #164nCampo = #164nCampo + #156#4;
     nCampo = nCampo + 32;
     #164nCampo = #156#5;
     nCampo = nCampo + 31;
     #164nCampo = aDia;

     #164#35 = 0;
     |PARA i = 4; |SI i [ 34; |HACIENDO i = i + 1;
          #164#35 = #164#35 + #164i;
     |SIGUE;
     |GRABA 020#164a;
     |LIBERA #164;
|FPRC;

|DEFBUCLE Partes;
     #156#1;
     ;
     #0#0, aDFecha, 00, " ";
     #0#1, aHFecha, 99, "z";
     ;
     NULL, Partes;
|FIN;

|PROCESO Defecto; |TIPO 7;
     |SI #0#3 = 0;
          fFecha = ~;
          aAlfa = fFecha % 402;
          #0#3 = aAlfa;
          aAlfa = fFecha % 704;
          #0#2 = aAlfa;
          |PINTA #0#2;
          |PINTA #0#3;
     |FINSI;
|FPRC;

|PROCESO Temporal;
     |DDEFECTO #154;
     #154#0 = #164#0;
     |LEE 000#154=;

     |DDEFECTO #158;
     #158#0 = #164#1;
     |LEE 000#158=;

     |PRINT;
|FPRC;

|DEFBUCLE Temporal;
     #164#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, Temporal;
|FIN;

|PROCESO SacaMes;
     aMes = aAlfa % 402;
     aA¤o = aAlfa % 704;
     aDFecha = "01." + aMes + "." + aA¤o;
     aHFecha = "27." + aMes + "." + aA¤o;
     |PARA; |SI; |HACIENDO;
          fFecha = aHFecha;
          fFecha = fFecha + 1;
          aAlfa = fFecha % 402;
          |SI aMes ! aAlfa; |SAL; |FINSI;
          aHFecha = fFecha;
     |SIGUE;
|FPRC;

|PROCESO Fabri; |TIPO 0;
     |SI #0#5 = 2;
          |ABRE #158;
          #158#0 = #0#6;
          |LEE 000#158=;
          |SI FSalida ! 0;
               |MENAV "0000No existe fabrica...";
               |ERROR;
          |FINSI;
          |CIERRA #158;
     |FINSI;
|FPRC;

|PROCESO Modi; |TIPO 0;
     |PARA i = 6; |SI i [ 20; |HACIENDO i = i + 1;
          |C_M #0i, 1, "N";
     |SIGUE;
     |SI #0#5 = 2;
          |C_M #0#6, 1, "S";
     |FINSI;
     |SI #0#5 = 3;
          |PARA i = 7; |SI i [ 20; |HACIENDO i = i + 1;
               |C_M #0i, 1, "S";
          |SIGUE;
     |FINSI;
|FPRC;

|PROCESO Tipo3; |TIPO 3;
     aAlfa = "01." + (("00" + #0#3) % -102) + "." + (("0000" + #0#2) % -104);
     |HAZ SacaMes;
     |PARA fFecha = aDFecha; |SI fFecha [ aHFecha; |HACIENDO fFecha = fFecha + 1;
          nDiasMes = nDiasMes + 1;
     |SIGUE;
     |SI #0#4 = 1;
          aHFecha = "15." + aMes + "." + aA¤o;
     |SINO;
          aDFecha = "16." + aMes + "." + aA¤o;
     |FINSI;

     |HAZ CreaTempo;
     |NOME_DAT #164#Tempo#;
     |DELINDEX #164;

     Impresora = "Crystal Reports";
     |IMPRE 0;
     |SI FSalida = 0;
          |INFOR "malpe162";
          |SI FSalida ! 0;
               |MENAV "0000No existe informe 'malpe162'";
          |SINO;
               |ABRE #154;
               |ABRE #164;
               |BUCLE Partes;
               |CIERRA #164;
               |ABRE #158;
               |BUCLE PonDias;
               |BUCLE Temporal;
               |CIERRA #158;
               |CIERRA #154;
               |PIEINF;
               |FININF;
          |FINSI;
          |FINIMP;
     |FINSI;

     |DELINDEX #164;
     |HAZ BoraTempo;
|FPRC;
