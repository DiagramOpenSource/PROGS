|FICHEROS;
con00671 #1,MANTE;
con00672 #2;
con00673 #3,MANTE;
con00653 #4;
con00670 #5;
con00674 #6,MANTE;
con00675 #7;
con00676 #8;
con00652 #10;
con00625 #20;
con00626 #21;
contmp01 #50;
agifa024 #24;
agifa112 #112;
|FIN;


|VARIABLES;
     &Tempo = "";
     aAlfa = "";
     &EURODB = 0;
&sw_inf = 0;
&texto1 = "";
&texto11 = "";
&texto2 = "";
&texto22= "";
&texto3 =  "------------------------------------------------------------";
&texto33 = "------------------------------------------------------------";

hay_nuevos = "N";
aAlfa1 = "";
quitar_base = 0;
a_quitar = 0;
base = 0;
iva = 0;
basti = "";
basti1 = "";
modo = 0;
opcion = 0;
fondo      = "°";
sw = 0;
x = 0;
posici = 0;
horizo = "";
 {-1} menu      = "";
    menu1       = "2400";
    menu2       = "1";
    menu3       = "";
    menu4       = "ECIUOTR"
    menu5       = "Economicos ";
    menu6       = "Cliente";
    menu7       = "Incidencia";
    menu8       = "Usados";
    menu9       = "Ordenes R. ";
    menu10      = "Totales";
    menu11      = "Resumen";
|FIN;

|PROCESO imprime;
|PRINT;
|FPRC;

|DEFBUCLE lee_economicos;
#3#1;
;
#5#0 , #5#22 , 001;
#5#0 , #5#22 , 999;
;
NULL , imprime;
|FIN;

|PROCESO lee_clientes;
|ABRE #1;
#1#0 = #5#0;
#1#3 = #5#22;
|LEE 000#1=;
|SI FSalida = 0;
     |PRINT;
|FINSI;
|FPRC;

|DEFBUCLE lee_inci;
#2#1;
;
#5#0 , #5#22 , 001;
#5#0 , #5#22 , 999;
;
NULL , imprime;
|FIN;

|DEFBUCLE lee_usados;
#8#1;
;
#5#0 , #5#22 , 001;
#5#0 , #5#22 , 999;
;
NULL , imprime;
|FIN;

|DEFBUCLE lee_lineas_or;
#21#1;
;
#6#2 , #6#3 , 0001;
#6#2 , #6#3 , 9999;
;
NULL , imprime;
|FIN;

|PROCESO saca_lineas_or;
|BUCLE lee_lineas_or;
|FPRC;

|DEFBUCLE lee_or;
#6#1;
;
#5#0 , #5#22 , 001;
#5#0 , #5#22 , 999;
;
NULL , saca_lineas_or;
|FIN;

|PROCESO resumen;
|IMPRE 0 ;
|SI FSalida = 0;
     |INFOR "con06701";
     |SI FSalida = 0;
          sw_inf = 0;
          |ABRE #7;
          #7#0 = #5#0;
          #7#27 = #5#22;
          |LEE 001#7=;
          |SI FSalida ! 0 ; |DDEFECTO #7; |FINSI;
          |LIBERA #7;
          |CIERRA #7;
          texto1 = "";
          texto11 = "";
          texto2 = "Descripcion                    Precio Venta       Coste  %IV";
          texto22= "A       Beneficio Factura";
          |PRINT;
          sw_inf = 1;
          |BUCLE lee_economicos;
          sw_inf = 11;
          |PRINT;
          sw_inf = 0;
          texto2 = "                                  Datos de Clientes";
          texto22 = "";
          |PRINT;
          sw_inf = 2;
          |HAZ lee_clientes;
          sw_inf = 0;
          texto2 = "                                  Comentarios e Incidencias";
          texto22 = " ";
          |PRINT;
          sw_inf = 5;
          |BUCLE lee_inci;
          sw_inf = 0;
          texto1 = "                       VEHICULOS USADOS ";
          texto11 = "";
          texto2 = "Matricula   Valor pasado     Valor Real   Exceso Valor    Fe";
          texto22= "cha Entrada";
          |PRINT;
          sw_inf = 7;
          |BUCLE lee_usados;
          sw_inf = 0;
          texto1 = "                       ORDENES DE REPARACION";
          texto11 = "";
          texto2 = "Orden  Referencia Articulo  Descripcion                    ";
          texto22 = "Cantidad      Precio %Dto.  Imp.Dto.  Imp. Total";
          |PRINT;
          sw_inf = 9;
          |BUCLE lee_or;
          |PIEINF;
     |FINSI;
     |FININF;
|FINSI;
|FINIMP;
|FPRC;

|PROCESO graba_tmp;
#50#0 = #5#0;
#50#1 = #5#34;
#50#2 = #5#1;
#50#3 = #5#2;
#50#4 = #5#3;
#50#5 = #5#4;
#50#6 = #5#22;
|GRABA 000#50n;
|LIBERA #50;
hay_nuevos = "S";
|FPRC;

|DEFBUCLE crea_tmp;
#5#3;
;
"  " , 000000 ;
"  " , 000000 ;
;
NULL , graba_tmp;
|FIN;


|PROCESO efe11; |TIPO 66;
  |CONFI "0000SSolo Pendientes de Facturar";
  |SI FSalida = 0;
       |HAZ CreaTempo;
       |NOME_DAT #50 Tempo; |DELINDEX #50;
       |ABRE #50;hay_nuevos = "N";
       |BUCLE crea_tmp;
       |SI hay_nuevos = "S";
            |CONSULTA_CLAVE #1#50;
            |SI FSalida = 0;
               #5#0 = #50#0;
               #5#22 = #50#6;
               |LEE 000#5=;
               |PINDA #0#5;
            |SINO;
               |ERROR;
            |FINSI;
       |SINO;
          |MENAV "0000No existen vehiculos pendientes de facturar";
       |FINSI;
       |CIERRA #50;
       |HAZ BoraTempo; |DELINDEX #50;
  |SINO;
       |CONSULTA_F_CLAVE #1#con00670 , min_vehi2 , max_vehi2;
  |FINSI;
|FPRC;

|PROCESO min_vehi2;
#5#0 = "";
#5#22 = "01.01.0000";
|FPRC;

|PROCESO max_vehi2;
#5#0 = "zzzzzzzzzz";
#5#22 = "01.01.2999";
|FPRC;

|PROCESO borra_del;
|BORRA 001#3a;
|LIBERA #3;
|FPRC;

|DEFBUCLE borra_de;
#3#1;
;
#5#0 , #5#22 , 001;
#5#0 , #5#22 , 999;
;
NULL , borra_del;
|FIN;

|PROCESO borra_orl;
     aAlfa = "con00802;" + #6#2 + #6#3;
     |SUB_EJECUTA_NP aAlfa;

     |BORRA 001#6a;
     |LIBERA #6;
|FPRC;

|DEFBUCLE borra_or;
#6#1;
;
#5#0 , #5#22 , 001;
#5#0 , #5#22 , 999;
;
NULL , borra_orl;
|FIN;

|PROCESO borra_incil;
|BORRA 001#2a;
|LIBERA #2;
|FPRC;

|DEFBUCLE borra_inci;
#2#1;
;
#5#0 , #5#22 , 001;
#5#0 , #5#22 , 999;
;
NULL , borra_incil;
|FIN;

|PROCESO borra_vul;
|BORRA 001#8a;
|LIBERA #8;
|FPRC;

|DEFBUCLE borra_vu;
#8#1;
;
#5#0 , #5#22 , 001;
#5#0 , #5#22 , 999;
;
NULL , borra_incil;
|FIN;

|PROCESO borra_clil;
|BORRA 001#1a;
|LIBERA #1;
|FPRC;

|DEFBUCLE borra_cli;
#1#1;
;
#5#0 ,#5#22;
#5#0 ,#5#22;
;
NULL , borra_clil;
|FIN;

|PROCESO borra_total;
|BORRA 001#7a;
|LIBERA #7;
|FPRC;

|DEFBUCLE borra_totales;
#7#1;
;
#5#0 ,#5#22;
#5#0 ,#5#22;
;
NULL , borra_total;
|FIN;

|PROCESO tipo22; |TIPO 2;
modo = (FEntrada / 100) ! 0;
|SI modo = 3;
     |BUCLE borra_de;
     |BUCLE borra_cli;
     |BUCLE borra_inci;
     |BUCLE borra_vu;
     |BUCLE borra_or;
     |BUCLE borra_totales;
|FINSI;
|FPRC;


|PROCESO acceso; |TIPO 3;
modo = (FEntrada / 100) ! 0;
|SI modo = 1;
     |HAZ pantalla;
|FINSI;
|FPRC;


|PROCESO inciden;
|PUSHV 0501 2380;
|ENTLINEAL #1#2,3,1,22,1,min,max;
|POPV;
|FPRC;

|PROCESO usados;
|PUSHV 0501 2380;
|ENTLINEAL #1#8,3,1,22,1,min5,max5;
|POPV;
|FPRC;

|PROCESO min5;
#8#0 = #5#0;
#8#1 = 1;
#8#7 = #5#22;
|FPRC;

|PROCESO max5;
#8#0 = #5#0;
#8#1 = 999;
#8#7 = #5#22;
|FPRC;

|PROCESO min;
#2#0 = #5#0;
#2#3 = 1;
#2#4 = #5#22;
|FPRC;

|PROCESO max;
#2#0 = #5#0;
#2#3 = 999;
#2#4 = #5#22;
|FPRC;

|PROCESO alta_precio;
|DDEFECTO #3;
#3#0 = #5#0;
#3#1 = 1;
#3#3 = "DATOS VEHICULO";
#3#4 = 0;
#3#5 = #5#23;
#3#7 = 0;
#3#9 = 0;
#3#11 = #5#22;
|GRABA 021#3n;
|LIBERA #3;
|FPRC;

|PROCESO datosec;
|PUSHV 0501 2380;
|ABRE #3;
#3#0 = #5#0;
#3#1 = 1;
#3#11 = #5#22;
|LEE 001#3];
basti = #3#0;
basti1 = #5#0;
|QBF basti;
|QBF basti1;
|SI FSalida ! 0 ; |O basti ! basti1; |O #5#22 ! #3#11;
     |HAZ alta_precio;
|FINSI;
|LIBERA #3;
|CIERRA #3;
|ENTLINEAL #1#3,3,1,20,1,min2,max2;
|POPV;
|FPRC;

|PROCESO min2;
#3#0 = #5#0;
#3#1 = 1;
#3#11 = #5#22;
|FPRC;

|PROCESO max2;
#3#0 = #5#0;
#3#1 = 999;
#3#11 = #5#22;
|FPRC;

|PROCESO cliente;
|PUSHV 0501 2380;
|ABRE #1;
#1#0 = #5#0;
#1#3 = #5#22;
|LEE 101#1=;
|SI FSalida ! 0;
     |DDEFECTO #1;
     #1#0 = #5#0;
     #1#3 = #5#22;
     #1#4 = #5#33;
     |GRABA 021#1c;
|FINSI;
|PINPA #0#1;
|PINDA #0#1;
|ENDATOS #1#1;
|SI FSalida = 0;
     |GRABA 021#1a;
|FINSI;
|LIBERA #1;
|CIERRA #1;
|POPV;
|FPRC;

|PROCESO orden_r;
|PUSHV 0501 2380;
|ENTLINEAL #1#6,3,1,22,1,min3,max3;
|POPV;
|FPRC;

|PROCESO min3;
#6#0 = #5#0;
#6#1 = 1;
#6#5 = #5#22;
|FPRC;

|PROCESO max3;
#6#0 = #5#0;
#6#1 = 999;
#6#5 = #5#22;
|FPRC;

|PROCESO suma_ord;
#7#2 = #7#2 + #6#4;
|FPRC;

|DEFBUCLE suma_or;
#6#1;
;
#5#0 , #5#22 , 001;
#5#0 , #5#22 , 999;
;
NULL , suma_ord;
|FIN;

|PROCESO suma_dec;
     #7#1 = #7#1 + #3#4;
     #7#2 = #7#2 + #3#5;
     |SI #3#10 = "S";
          |SI #3#7 ! 0;
              base = #3#6 + 3;
              iva = base + 3;
              #7base = #7base + #3#4;
              #7iva = #3#7;
          |SINO;
              #7#13 = #7#13 + #3#4;
          |FINSI;
     |FINSI;
     |SI #3#10 = "A";
          |SI #3#7 ! 0;
               base = #3#6 + 15;
               iva = base + 3;
               #7base = #7base + #3#4;
               #7iva = #3#7;
          |SINO;
               #7#25 = #7#25 + #3#4;
          |FINSI;
     |FINSI;
     #7#15 = #7#15 + ((#3#4 % #3#9)!EURODB);

|FPRC;

|DEFBUCLE suma_de;
#3#1;
;
#5#0 , #5#22 ,001;
#5#0 , #5#22 ,999;
;
NULL , suma_dec;
|FIN;

|PROCESO suma_usa;
#7#2 = #7#2 + #8#5;
|FPRC;

|DEFBUCLE suma_usados;
#8#1;
;
#5#0 , #5#22 ,  1;
#5#0 , #5#22 ,999;
;
NULL , suma_usa;
|FIN;

|PROCESO suma_totales;
     |BUCLE suma_or;
     |BUCLE suma_de;
     |BUCLE suma_usados;
     #7#3 = #7#1 - #7#2;
     |ABRE #1;
      #1#0 = #5#0;
      #1#3 = #5#22;
      |LEE 001#1=;
      |SI FSalida ! 0 ; |DDEFECTO #1; |FINSI;
      |LIBERA #1;
      |CIERRA #1;
     #7#22 = (#7#16 % #7#19) ! EURODB;
     #7#23 = (#7#17 % #7#20) ! EURODB;
     #7#24 = (#7#18 % #7#21) ! EURODB;
     |SI #1#4 = "U";
          quitar_base = #5#23;
          |SI #7#4 ! 0;
                 a_quitar = #7#4 - quitar_base;
                 |SI a_quitar ] 0;
                        #7#4 = #7#4 - quitar_base;
                        #7#13 = #7#13 + quitar_base;
                        quitar_base = 0
                 |SINO;
                        quitar_base = quitar_base - #7#4;
                        #7#13 = #7#13 + #7#4;
                        #7#4 = 0;
                 |FINSI;
          |FINSI;
          |SI #7#5 ! 0; |Y quitar_base > 0;
                 a_quitar = #7#5 - quitar_base;
                 |SI a_quitar ] 0;
                        #7#5 = #7#5 - quitar_base;
                        #7#13 = #7#13 + quitar_base;
                        quitar_base = 0;
                 |SINO;
                        quitar_base = quitar_base - #7#5;
                        #7#13 = #7#13 + #7#5;
                        #7#5 = 0;
                 |FINSI;
          |FINSI;
          |SI #7#6 ! 0; |Y quitar_base > 0;
                 a_quitar = #7#6 - quitar_base;
                 |SI a_quitar ] 0;
                        #7#6 = #7#6 - quitar_base;
                        #7#13 = #7#13 + quitar_base;
                        quitar_base = 0;
                 |SINO;
                        quitar_base = quitar_base - #7#6;
                        #7#13 = #7#13 + #7#6;
                        #7#6 = 0;
                 |FINSI;
          |FINSI;
     |FINSI;
     |SI #1#4 = "S";
          #7#13 = #7#13 + #7#4 + #7#5 + #7#6;
          #7#4 = 0;
          #7#5 = 0;
          #7#6 = 0;
          #7#7 = 0;
          #7#8 = 0;
          #7#9 = 0;
     |FINSI;

     #7#10 = (#7#4 % #7#7) ! EURODB;
     #7#11 = (#7#5 % #7#8) ! EURODB;
     #7#12 = (#7#6 % #7#9) ! EURODB;
     #7#22 = (#7#16 % #7#19) ! EURODB;
     #7#23 = (#7#17 % #7#20) ! EURODB;
     #7#24 = (#7#18 % #7#21) ! EURODB;
     #7#14 = #7#4 + #7#5 + #7#6 + #7#10 + #7#11 + #7#12 + #7#13;
     #7#26 = #7#16 + #7#17 + #7#18 + #7#22 + #7#23 + #7#24 + #7#25;

|FPRC;



|PROCESO total;
|PUSHV 0501 2380;
|ABRE #7;
#7#0 = #5#0;
#7#27 = #5#22;
|LEE 001#7=;
|SI FSalida = 0; |BORRA 001#7a; |FINSI;
|LIBERA #7;
|DDEFECTO #7;
#7#0 = #5#0;
#7#27 = #5#22;
|GRABA 001#7c;
|LIBERA #7;
|HAZ suma_totales;
|GRABA 001#7a;
|LIBERA #7;
|PINPA #0#7;
|PINDA #0#7;
|ENDATOS #4#7;
|CIERRA #7;
|POPV;

|FPRC;

|PROCESO pantalla; |TIPO 20;
     aAlfa1 = FSalida;

     |SI aAlfa1 = "OPCION";
         FSalida = "Opciones";
         |ACABA;
     |FINSI;

|PUSHV 0501 2380;
opcion = 1;
sw = 0;
|PARA; |SI sw = 0; |HACIENDO;
   menu2  = opcion;

   |MENU menu;
   opcion = FSalida;
   |SI opcion ] 1; |Y opcion [ 7;
       |SI opcion = 1; |HAZ datosec; |FINSI;
       |SI opcion = 2; |HAZ cliente; |FINSI;
       |SI opcion = 3; |HAZ inciden; |FINSI;
       |SI opcion = 4; |HAZ usados;  |FINSI;
       |SI opcion = 5; |HAZ orden_r; |FINSI;
       |SI opcion = 6; |HAZ total;   |FINSI;
       |SI opcion = 7; |HAZ resumen; |FINSI;
   |SINO;
       sw = 1;
   |FINSI;
|SIGUE;
|POPV;
|FPRC;

|PROCESO pinta_pan;
|SI modo = 4; |ACABA ; |FINSI;
|PINPA #0#5;
|PINDA #0#5;
|FPRC;

|PROCESO TipoVeh;
     |SI #con00670#36 = "U";
          |C_M #con00670#37, 1, "N";
          |C_M #con00670#38, 1, "S";
          |C_M #con00670#39, 1, "S";
     |SINO;
          |C_M #con00670#37, 1, "S";
          |C_M #con00670#38, 1, "N";
          |C_M #con00670#39, 1, "N";
     |FINSI;
|FPRC;

|PROCESO CliPrv0; |TIPO 0;
     |SI FSalida = 2; |ACABA; |FINSI;
     |SI #5#40 = "P";
          |ABRE #112;
          #112#0 = #5#11;
          |LEE 000#112=;
          |SI FSalida ! 0;
               |MENAV "0000No existe el proveedor...";
               |ERROR;
          |SINO;
               #5#19 = #112#1; |PINTA #5#19;
          |FINSI;
          |CIERRA #112;
     |SINO;
          |ABRE #24;
          #24#0 = #5#11;
          |LEE 000#24=;
          |SI FSalida ! 0;
               |MENAV "0000No existe el cliente...";
               |ERROR;
          |SINO;
               #5#19 = #24#1; |PINTA #5#19;
          |FINSI;
          |CIERRA #24;
     |FINSI;
|FPRC;

|PROCESO CliPrv66; |TIPO 66;
     |SI #5#40 = "P";
          |ABRE #112;
          #112#0 = #5#11;
          |LEE 000#112];
          |CONSULTA_CLAVE #5#112;
          |SI FSalida = 0;
               #5#11 = #112#0; |PINTA #5#11;
          |SINO;
               |ERROR;
          |FINSI;
          |CIERRA #112;
     |SINO;
          |ABRE #24;
          #24#0 = #5#11;
          |LEE 000#24];
          |CONSULTA_CLAVE #5#24;
          |SI FSalida = 0;
               #5#11 = #24#0; |PINTA #5#11;
          |SINO;
               |ERROR;
          |FINSI;
          |CIERRA #24;
     |FINSI;
|FPRC;
