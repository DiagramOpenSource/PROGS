     TraeLogoPan, PintaLogoPan
     TX80, TZ80
     TX12
     EsperaFin
     EjecutaProg
     PushVenModal, PopVenModal
     QuitaRarosImp, QuitaRarosExp
     IniMensaje, FinMensaje
     PideClave
     Ansi2Utf8
|FICHEROS;
|FIN;

|VARIABLES;
     aAlfa = "";
     &eaCtrTxt = "";
     &enCtrFin = 0;
     {2}nNivel = 0;
     aEsc1 = "";
     aEsc2 = "";
     aRetu = "";
     aTecla = "";

     &eaTitulo = "";
     &eaProg = "";
     &enIniVen = 0;
     &enFinVen = 0;
     nVentana = 0;
 {-1}aVent = "";
     aVent1 = 0;
     aVent2 = 0;
     aVent3 = 0;
     aVent4 = 0;
     aVent5 = "";

     &eaMensaje = "";
     nVenMensa = 0;
     nLabelMen = 0;

     &enContaVentana = 0;
 {20}&_Ventana = 0;
     _nVen = 0;
     _aSalida = "";

     &eaAlfa = "";
     aCar = "";
     aDato = "";
     aLon = "";
     i = 0;
     nPos = 0;
     nCar = 0;

     &eaNomLogo = "";
     &enPantaLogo = 0;
     &enCtrlLogo = 0;
     aNomLogo = "";
     aBase = "";
     aFotoOrigen = "";
     aFotoDestino = "";
     aDirLocal = "";
     {1}aContiene = "";
     aIPRemoto = "";
     aDocRemoto = "";
     aDocServidor = "";
     aFoto = "";

     nBtnClaveAcepta = 0;
     nBtnClaveCancela = 0;
     nLabelClave = 0;
     &eaClave = "";
|FIN;

|PROCESO TZ80; |TIPO 80;
     FSalida = 3099;
|FPRC

|PROCESO TX80; |TIPO 80;
     FSalida = 3399;
|FPRC

|PROCESO TX12; |TIPO 12;
|FPRC;

Variable entrada:  enCtrFin, eaCtrTxt
          salida:
|PROCESO EsperaFin;
     aEsc1 = &255 + "806";
     aEsc2 = &255 + "807";
     aRetu = &255 + "802";
     |SI eaCtrTxt ! "";
          aAlfa = "::" + eaCtrTxt + ",";
     |SINO;
          aAlfa = "::{{001}}Salir,";
     |FINSI;
     |PARA; |SI;  |HACIENDO;
          FSalida = aAlfa + enCtrFin;
          |LEETECLA aTecla;
          |SI aTecla = aEsc1; |SAL; |FINSI;
          |SI aTecla = aEsc2; |SAL; |FINSI;
          |SI aTecla = aRetu; |SAL; |FINSI;
     |SIGUE;
     eaCtrTxt = "";
|FPRC;

Variable entrada: eaTitulo, eaProg, enIniVen, enFinVen
|PROCESO EjecutaProg;
     |SI enIniVen = 0; enIniVen = 0501; |FINSI;
     |SI enFinVen = 0; enFinVen = 3399; |FINSI;
     |VENTANA enIniVen, enFinVen, -1, 17, eaTitulo;
     |HAZ PushVenModal;

     |SI eaProg = "";
          |MENAV "0000En desarrollo...";
     |SINO;
          |SUB_EJECUTA_NP eaProg;
     |FINSI;

     |HAZ PopVenModal;
|FPRC;

Variable entrada,salida: FSalida
|PROCESO PushVenModal;
     _nVen = FSalida;
     |SI enContaVentana > 20;
          |MENAV "0000Superado el contador de ventanas ( > 20)...";
     |SINO;
          I_Ventana = _Ventana1<-;
          I_Ventana = I_Ventana + enContaVentana;
          _Ventana = _nVen;
          enContaVentana = enContaVentana + 1;

          aVent1 = -1;
          aVent2 = -1;
          aVent3 = 0;
          aVent4 = _nVen;
          aVent5 = "MODAL";
          |ESPECIFICA "ESTADO_VENTANA", aVent;
     |FINSI;
     FSalida = _nVen;
|FPRC;

Variable entrada/salida: ninguna
|PROCESO PopVenModal;
     _aSalida = FSalida;
     enContaVentana = enContaVentana - 1;
     I_Ventana = _Ventana1<-;
     I_Ventana = I_Ventana + enContaVentana;

     _nVen = _Ventana;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = _nVen;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;
     |FINVENTANA _nVen;

     _Ventana = 0;
     |SI enContaVentana > 0;
          I_Ventana = I_Ventana - 1;
          _nVen = _Ventana;

          aVent1 = -1;
          aVent2 = -1;
          aVent3 = 0;
          aVent4 = _nVen;
          aVent5 = "MODAL";
          |ESPECIFICA "ESTADO_VENTANA", aVent;
     |FINSI;
     |SI enContaVentana < 0;
          |MENAV "0000Error en el contador de ventanas ( < 0)...";
          enContaVentana = 0;
     |FINSI;
     |PULSATECLA;
     FSalida = _aSalida;
|FPRC;

Variable entrada/salida: eaAlfa
|PROCESO QuitaRarosImp;
     |QBF eaAlfa;
     aAlfa = eaAlfa;
     aLon = aAlfa % A0;
     aDato = "";
     |PARA i = 0; |SI i [ aLon; |HACIENDO i = i + 1;
          nPos = (i + 1) * 100 + 1;
          aCar = aAlfa % nPos;
          nCar = &aCar;
          |SI nCar = 241; aCar = "¤"; |FINSI;
          |SI nCar = 209; aCar = "¥"; |FINSI;
          |SI nCar = 208; aCar = "¥"; |FINSI; ||no se porque
          |SI nCar = 225; aCar = " "; |FINSI;
          |SI nCar = 233; aCar = "‚"; |FINSI;
          |SI nCar = 237; aCar = "¡"; |FINSI;
          |SI nCar = 243; aCar = "¢"; |FINSI;
          |SI nCar = 250; aCar = "£"; |FINSI;
          |SI nCar = 193; aCar = "A"; |FINSI;
          |SI nCar = 201; aCar = "E"; |FINSI;
          |SI nCar = 205; aCar = "I"; |FINSI;
          |SI nCar = 211; aCar = "O"; |FINSI;
          |SI nCar = 218; aCar = "U"; |FINSI;
          |SI nCar = 186; aCar = "§"; |FINSI;
          |SI nCar = 170; aCar = "¦"; |FINSI;
          |SI nCar = 176; aCar = "°"; |FINSI;     ||Euro
          aDato = aDato + aCar;
     |SIGUE;
     eaAlfa = aDato;
|FPRC;

Variable entrada/salida: eaAlfa
|PROCESO QuitaRarosExp;
     |QBF eaAlfa;
     aAlfa = eaAlfa;
     aLon = aAlfa % A0;
     aDato = "";
     |PARA i = 0; |SI i [ aLon; |HACIENDO i = i + 1;
          nPos = (i + 1) * 100 + 1;
          aCar = aAlfa % nPos;
          |SI aCar = "¥"; aCar = &209; |FINSI;
          |SI aCar = "¤"; aCar = &241; |FINSI;
          |SI aCar = " "; aCar = &225; |FINSI;
          |SI aCar = "‚"; aCar = &233; |FINSI;
          |SI aCar = "¡"; aCar = &237; |FINSI;
          |SI aCar = "¢"; aCar = &243; |FINSI;
          |SI aCar = "£"; aCar = &250; |FINSI;
          |SI aCar = "§"; aCar = &176; |FINSI;
          |SI aCar = "¦"; aCar = &170; |FINSI;
          |SI aCar = "°"; aCar = &176; |FINSI;  ||Euro
          aDato = aDato + aCar;
     |SIGUE;
     eaAlfa = aDato;
|FPRC;

|PROCESO FinMensaje;
     |FIN_CONTROL_WINDOWS nLabelMen;
     |HAZ PopVenModal;
|FPRC;

Variable entrada: eaMensaje
|PROCESO IniMensaje;
     |VENTANA 1527, 1776, -1, 17, "Mensaje";
     |HAZ PushVenModal;
     aAlfa = "LABEL,{{004}}" + eaMensaje;
     |CONTROL_SIMPLE 0, aAlfa, 1630, 1773, NULL;
     nLabelMen = FSalida;
     |PTEC 802;
     |PAUSA;
     |PULSATECLA;
|FPRC;

|PROCESO TraeLogoPan;
     |QBF eaNomLogo;
     aNomLogo = eaNomLogo + ".bmp";
     |DBASE aBase;
     |QBF aBase;
     aFotoOrigen = aBase + "documentos/logos/" + aNomLogo;
     |XABRE "E", aFotoOrigen, 0;
     |SI FSalida < 0;  |ACABA;  |FINSI;

     aDirLocal = "C:\DOCUMENTOS";             |RMKDIR aDirLocal;
     aDirLocal = "C:\DOCUMENTOS\logos\";   |RMKDIR aDirLocal;
     aFotoDestino = aDirLocal + "\" + aNomLogo;

     aContiene1 = aFotoDestino;
     |ESPECIFICA "REMOTOMD5", aContiene;
     aDocRemoto = FSalida;  |QBF aDocRemoto;

     aContiene1 = aFotoOrigen;
     |ESPECIFICA "MD5", aContiene;
     aDocServidor = FSalida;  |QBF aDocServidor;

     |SI aDocRemoto ! aDocServidor;  |O aDocRemoto = "";
         aIPRemoto = "";
         |IP_REMOTO aIPRemoto;  |QBT aIPRemoto;

         |SI aIPRemoto ! "";
             |ENVIA_FICHERO aFotoOrigen, aFotoDestino;
         |SINO;
             |COPIA_FICHERO aFotoOrigen, aFotoDestino;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO PintaLogoPan;
     aNomLogo = eaNomLogo + ".bmp";
     aFotoDestino = "C:\DOCUMENTOS\logos\" + aNomLogo;
     aFoto = "IMAGEN,"  + aFotoDestino;
     |CONTROL_SIMPLE enPantaLogo, aFoto, 0501, 0825, NULL;
     enCtrlLogo = FSalida;
     |PULSATECLA;
|FPRC;

|PROCESO BtnClaveAcepta;
     |PTEC 802;
|FPRC;

|PROCESO BtnClaveCancela;
     |PTEC 807;
|FPRC;

Variable entrada: eaTitulo
Variable salida: eaClave
|PROCESO PideClave;
     |VENTANA 1233, 1864, -1, 17, eaTitulo;
     |HAZ PushVenModal;

     |CONTROL_SIMPLE 0, "BOTON,{{206}}Aceptar", 1740, 1750, BtnClaveAcepta;
     nBtnClaveAcepta = FSalida;
     |CONTROL_SIMPLE 0, "BOTON,{{207}}Cancelar", 1752, 1762, BtnClaveCancela;
     nBtnClaveCancela = FSalida;
     |CONTROL_SIMPLE 0, "LABEL,{{001}}Introduzca la clave:", 1433, 1450, NULL;
     nLabelClave = FSalida;
     eaClave = "";
     E_sup = 10;
     E_inf = "";
     eaClave = 1452 ? -1;
     |SI FSalida ! 0; eaClave = ""; |FINSI;

     |FIN_CONTROL_WINDOWS nBtnClaveAcepta;
     |FIN_CONTROL_WINDOWS nBtnClaveCancela;
     |FIN_CONTROL_WINDOWS nLabelClave;
     |HAZ PopVenModal;
|FPRC;

|PROCESO Ansi2Utf8;
     aAlfa = eaAlfa;
     aLon = aAlfa % A0;
     aDato = "";
     |PARA i = 0; |SI i [ aLon; |HACIENDO i = i + 1;
          nPos = (i + 1) * 100 + 1;
          aCar = aAlfa % nPos;
          |SI aCar = "¥"; aCar = &195 + &145; |FINSI;
          |SI aCar = "¤"; aCar = &195 + &177; |FINSI;
          |SI aCar = " "; aCar = &195 + &161; |FINSI;
          |SI aCar = "‚"; aCar = &195 + &169; |FINSI;
          |SI aCar = "¡"; aCar = &195 + &173; |FINSI;
          |SI aCar = "¢"; aCar = &195 + &179; |FINSI;
          |SI aCar = "£"; aCar = &195 + &186; |FINSI;
          |SI aCar = "§"; aCar = &194 + &186; |FINSI;
          |SI aCar = "¦"; aCar = &194 + &170; |FINSI;
          |SI aCar = "°"; aCar = &226 + &130 + &172; |FINSI;  ||Euro
          aDato = aDato + aCar;
     |SIGUE;
     eaAlfa = aDato;
|FPRC;
