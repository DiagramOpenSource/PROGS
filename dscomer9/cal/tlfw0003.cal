|FICHEROS;
     tlfm0019 #19;  || Histo
     tlfw0003 #300;
     tlfm0001 #301;
     tlfm0002 #302;
     tlfm0003 #303;
     agifa501 #501;
     agifa502 #502;
     agifa024 #24;
|FIN;

|VARIABLES;
     i = 0;
     nForma = 0;
     nModo = 0;
     nSalida = 0;
     aAlfa = "";
     aAlf1 = "";
     aAlf2 = "";
     &enFormaIMEI = 0;
     &enModoIMEI= 0;
     &eaSerIMEI= "";
     &enDocIMEI= 0;
     &enLinIMEI= 0;
     &eaArtIMEI= "";
     &enUnidades = 0;
|FIN;

|PROCESO IMEI12; |TIPO 12;
     nModo = (FEntrada / 100) ! 0;
     nSalida = FSalida;
     aAlfa = #300#0 + #300#1 + #300#2; |QBF aAlfa;
     |SI aAlfa = ""; nSalida = 1; |FINSI;
     |SI nSalida = 0;
          |DDEFECTO #303;
          |ABRE #303;
          #303#0 = eaSerIMEI;
          #303#1 = enDocIMEI;
          #303#2 = enLinIMEI;
          |LEE 101#303=;
          |SI FSalida ! 0; |GRABA 020#303b; |FINSI;
          #303#3 = #300#0; ||IMEI
          #303#4 = #300#1; ||ABO
          #303#5 = #300#2; ||ICC
          |GRABA 020#303a;
          |CIERRA #303;
     |SINO;
          |SI nModo = 1;
               |HAZ BorraIMEI;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Escape; |TIPO 0;
     |SI FSalida = 1;
          |ERROR; |PTEC 807;
     |FINSI;
|FPRC;

|PROCESO IMEI0; |TIPO 0;
     aAlfa = #300#0; |QBF aAlfa;
     |SI aAlfa ! "";
          |ABRE #301;
          #301#0 = #300#0;
          |LEE 000#301=;
          |SI FSalida ! 0;
               |MENAV "0000IMEI inexistente...";
               |ERROR;
          |SINO;
               |SI #301#27 ! 0; |O #301#31 ! 0; |O #301#35 ! 0;
                    |SI enUnidades > 0; |Y #301#57 = "N";
                         |MENAV "0000IMEI Vendido....";
                         |ERROR;
                    |SINO;
                         #300#1 = #301#3; |PINTA #300#1;
                         #300#2 = #301#4; |PINTA #300#2;
                         #300#3 = #301#1;
                    |FINSI;
               |SINO;
                    #300#1 = #301#3; |PINTA #300#1;
                    #300#2 = #301#4; |PINTA #300#2;
                    #300#3 = #301#1;
               |FINSI;
          |FINSI;
          |CIERRA #301;
     |FINSI;
|FPRC;

|PROCESO Abo0; |TIPO 0;
     aAlfa = #300#1; |QBF aAlfa;
     |SI aAlfa ! "";
          |ABRE #302;
          #302#3 = #300#1;
          |LEE 000#302=;
          |SI FSalida ! 0;
               |MENAV "0000Abono inexistente...";
               |ERROR;
          |SINO;
               |SI #302#27 ! 0; |O #302#31 ! 0; |O #302#35 ! 0;
                    |SI enUnidades > 0; |Y #302#50 = "N";
                         |MENAV "0000Abono Vendido....";
                         |ERROR;
                    |SINO;
                         #300#0 = #302#0; |PINTA #300#0;
                         #300#2 = #302#4; |PINTA #300#2;
                         #300#3 = #302#1;
                    |FINSI;
               |SINO;
                    #300#0 = #302#0; |PINTA #300#0;
                    #300#2 = #302#4; |PINTA #300#2;
                    #300#3 = #302#1;
               |FINSI;
          |FINSI;
          |CIERRA #302;
     |FINSI;
|FPRC;

|PROCESO LeeIMEI;
     |ABRE #303;
     #303#0 = eaSerIMEI;
     #303#1 = enDocIMEI;
     #303#2 = enLinIMEI;
     |LEE 000#303=;
     #300#0 = #303#3;
     #300#1 = #303#4;
     #300#2 = #303#5;
     nSalida = FSalida;
     |CIERRA #303;
|FPRC;

---------------------------------------------------------------------------
|PROCESO Limpia;
     |PARA i = 12; |SI i [ 22; |HACIENDO i = i + 1;
          #301i = "";
          #302i = "";
     |SIGUE;
     |PARA i = 25; |SI i [ 36; |HACIENDO i = i + 1;
          aAlf1 = "|C"+ (("000"+i)%-103) + "TIPO";
          aAlf2 = #301#aAlf1#;
          |SI aAlf2 = "F"; #301i = "01.01.0000"; |SINO; #301i = ""; |FINSI;
          aAlf2 = #302#aAlf1#;
          |SI aAlf2 = "F"; #302i = "01.01.0000"; |SINO; #302i = ""; |FINSI;
     |SIGUE;
     #301#39 = "01.01.0000";
|FPRC;

|PROCESO ActIMEI;
     nForma = enFormaIMEI;
     |ABRE #303;
     #303#0 = eaSerIMEI;
     #303#1 = enDocIMEI;
     #303#2 = enLinIMEI;
     |LEE 000#303=;
     nSalida = FSalida;
     |CIERRA #303;
     |SI nSalida ! 0; |ACABA; |FINSI;

     aAlfa = #303#3; |QBF aAlfa;
     |SI aAlfa ! "";
          |ABRE #301;
          #301#0 = #303#3;
          |LEE 101#301=;
          |SI FSalida = 0;  || Lo marco con sesion/numero
               |SI nForma = 1;
                    #301#29 = eaSerIMEI;
                    #301#30 = enDocIMEI;
                    #301#31 = enLinIMEI;
                    #301#57 = "N";
               |SINO;
                    |HAZ Limpia;
               |FINSI;
               |GRABA 020#301a;
          |FINSI;
          |CIERRA #301;
     |FINSI;
     aAlfa = #303#4; |QBF aAlfa;
     |SI aAlfa ! "";
          |ABRE #302;
          #302#3 = #303#4;
          |LEE 121#302=;
          |SI FSalida = 0;  || Lo marco con sesion/numero
               |SI nForma = 1;
                    #302#29 = eaSerIMEI;
                    #302#30 = enDocIMEI;
                    #302#31 = enLinIMEI;
                    #302#50 = "N";
               |SINO;
                    |HAZ Limpia;
               |FINSI;
               |GRABA 020#302a;
          |FINSI;
          |CIERRA #302;
     |FINSI;
|FPRC;
---------------------------------------------------------------------------
|PROCESO BorraIMEI;
     |ABRE #303;
     #303#0 = eaSerIMEI;
     #303#1 = enDocIMEI;
     #303#2 = enLinIMEI;
     |BORRA 000#303c;
     |CIERRA #303;

     |ABRE #301;
     |SELECT #9#301;
     #301#33 = eaSerIMEI;
     #301#34 = enDocIMEI;
     #301#35 = enLinIMEI;
     |LEE 000#301=;
     |SI FSalida = 0;
          |SELECT #1#301;
          |LEE 121#301=;
          |SI FSalida = 0;
               |HAZ Limpia;
               |GRABA 020#301a;
          |FINSI;
     |FINSI;
     |CIERRA #301;

     |ABRE #302;
     |SELECT #7#302;
     #302#33 = eaSerIMEI;
     #302#34 = enDocIMEI;
     #302#35 = enLinIMEI;
     |LEE 000#302=;
     |SI FSalida = 0;
          |SELECT #1#302;
          |LEE 121#302=;
          |SI FSalida = 0;
               |HAZ Limpia;
               |GRABA 020#302a;
          |FINSI;
     |FINSI;
     |CIERRA #302;
|FPRC;
---------------------------------------------------------------------------
|PROCESO VerIMEI;
     |DDEFECTO #300;
     |ABRE #303;
     |HAZ LeeIMEI;
     |SI FSalida ! 0; |ACABA; |FINSI;
     |CIERRA #303;
     |PUSHV 0501 2380;
     |BLANCO 1423 2055;
     |PINPA #0#300;
     |PINDA #0#300;
     |PAUSA;
     |POPV;
|FPRC;
---------------------------------------------------------------------------
|PROCESO DocIMEI;
     #501#36 = #502#20;
     #501#0 = #502#0;
     |LEE 000#501=;
     |SI FSalida ! 0; |ACABA; |FINSI;

     |DDEFECTO #24;
     #24#0 = #501#1;
     |LEE 000#24=;

     #303#0 = #502#20;
     #303#1 = #502#0;
     #303#2 = #502#1;
     |LEE 101#303=;
     |SI FSalida = 0;
          #303#6 = #501#35;
          #303#7 = #501#36;
          #303#8 = #501#0;
          |GRABA 020#303a;
          |LIBERA #303;

          aAlfa = #303#3; |QBF aAlfa;
          |SI aAlfa ! "";
               #301#0 = #303#3;
               |LEE 101#301=;
               |SI FSalida = 0;  || Marca definitiva
                    |PARA i = 25; |SI i [ 36; |HACIENDO i = i + 1;
                         #301i = "";
                    |SIGUE;
                    |SI #501#35 = "A";
                         #301#25 = #501#57;
                         #301#26 = #501#58;
                         #301#27 = #502#1;
                         #301#28 = #501#14;
                    |FINSI;
                    |SI #501#35 = "F";
                         #301#33 = #501#57;
                         #301#34 = #501#58;
                         #301#35 = #502#1;
                         #301#36 = #501#14; #301#39 = #301#36;
                    |FINSI;
                    |SI #501#35 ! "F"; |Y #501#35 ! "A";
                         #301#29 = #501#57;
                         #301#30 = #501#58;
                         #301#31 = #502#1;
                         #301#32 = #501#14;
                    |FINSI;
                    #301#12 = #501#1;
                    |DEFECTO #301#24;
                    |GRABA 020#301a;
               |FINSI;
               |HAZ Historico;
          |FINSI;

          aAlfa = #303#4; |QBF aAlfa;
          |SI aAlfa ! "";
               #302#3 = #303#4;
               |LEE 101#302=;
               |SI FSalida = 0;  || Marca Definitiva
                    |PARA i = 25; |SI i [ 36; |HACIENDO i = i + 1;
                         #302i = "";
                    |SIGUE;
                    |SI #501#35 = "A";
                         #302#25 = #501#57;
                         #302#26 = #501#58;
                         #302#27 = #502#1;
                         #302#28 = #501#14;
                    |FINSI;
                    |SI #501#35 = "F";
                         #302#33 = #501#57;
                         #302#34 = #501#58;
                         #302#35 = #502#1;
                         #302#36 = #501#14;
                    |FINSI;
                    |SI #501#35 ! "F"; |Y #501#35 ! "A";
                         #302#29 = #501#57;
                         #302#30 = #501#58;
                         #302#31 = #502#1;
                         #302#32 = #501#14;
                    |FINSI;
                    #302#12 = #501#1;
                    |DEFECTO #302#24;
                    |GRABA 020#302a;
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Historico;
     #19#0 = #303#3;
     #19#1 = #502#2;
     #19#2 = #501#14;
     |SI #501#35 = "A";
          #19#3 = "AV";
     |SINO;
          #19#3 = "TI";
     |FINSI;
     #19#4 = #303#7;
     #19#5 = #303#8;
     #19#6 = #502#1;
     #19#7 = 0;
     #19#8 = #502#3;
     #19#9 = #501#1;
     #19#10 = #502#10;
     |GRABA 020#19n;
|FPRC;

|DEFBUCLE DocIMEI;
     #502#1;
     ;
     eaSerIMEI, enDocIMEI, 001;
     eaSerIMEI, enDocIMEI, 999;
     be;
     NULL, DocIMEI;
|FIN;
---------------------------------------------------------------------------
|PROGRAMA;
     |SI PARAMETRO = "ACTUALIZA";  || Llama la cabecera
          |ABRE #301;
          |ABRE #302;
          |ABRE #303;
          |ABRE #501;
          |ABRE #24;
          |ABRE #19;
          |BUCLE DocIMEI;
          |CIERRA #19;
          |CIERRA #24;
          |CIERRA #501;
          |CIERRA #301;
          |CIERRA #302;
          |CIERRA #303;
          |ACABA;
     |FINSI;

     |SI enFormaIMEI ! 0;     || Actualizacion
          |HAZ ActIMEI;
          |ACABA;
     |FINSI;
     |SI enModoIMEI = 3;      || Baja
          |HAZ BorraIMEI;
          |ACABA;
     |FINSI;
     |SI enModoIMEI = 4;      || Consulta
          |HAZ VerIMEI;
          |ACABA;
     |FINSI;

     |DDEFECTO #300;
     |SI enModoIMEI = 1;
          |ABRE #303;
          #303#0 = eaSerIMEI;
          #303#1 = enDocIMEI;
          #303#2 = enLinIMEI;
          |BORRA 000#303c;
          |CIERRA #303;
     |SINO;
          |HAZ LeeIMEI;
          |SI nSalida ! 0; |ACABA; |FINSI;
     |FINSI;
     |PUSHV 0501 2380;
     |BLANCO 1423 2055;
     |PINPA #0#300;
     |PINDA #0#300;
     |ENDATOS #2#300;
     |SI nSalida = 0;
          eaArtIMEI = #300#3;
          |PAUSA;
     |FINSI;
     |POPV;
|FPRO;
