|FICHEROS;
     rodw0005 #0;
     rodm0001 #1;
     rodm0004 #4; || Historico
     rodm0005 #5;
     rodw0007 #7;

     agifa010 #10;
     agifa017 #17;
     agifa019 #19;
     agifa024 #24;
     agifa071 #71;
     dsm00043 #43;
     dsm00044 #44;
     agifa065 #65;

     agifa041 #41;
     agifa200 #200;
     agifa208 #208;
     origen@;
     referen0@;
     referen1@;
     referen@;
|FIN;

|CAMPOS;
     #19#15  art_pser;  #17#42  alm_pser;
     #19#182 art_pser2; #17#44  alm_pser2;
     #19#16  art_prec;  #17#43  alm_prec;
     #19#183 art_prec2; #17#45  alm_prec2;
     #19#17  art_pval;  #17#4   alm_pval;
     #19#11  art_nece;  #17#50  alm_nece;
     #19#12  art_rese;  #17#8   alm_rese;
     #19#14  art_depo;  #17#10  alm_depo;
     #19#184 art_depo2; #17#46  alm_depo2;
     #19#13  art_fabr;  #17#9   alm_fabr;
     #19#10  art_vsto;  #17#3   alm_vsto;
     #19#7   art_mini;  #17#6   alm_mini;
     #19#8   art_maxi;  #17#7   alm_maxi;
     #19#6   art_tota;  #17#5   alm_tota;
     #19#185 art_tota2; #17#47  alm_tota2;
     #19#104 art_disp;  #17#39  alm_disp;
     #19#193 art_disp2; #17#48 alm_disp2;
     #19#9   art_pcm;   #17#69 alm_pcm;
|FIN;

|VARIABLES;
     &enSwNoCopia = 0;  || Para desactivar la actualizacion
     &enEstaDentro = 0; ||  !!
     nPasa = 0;
     nModoCam = 0;
     nCampo = 0;
     aSwLocal = "";
     nHay = 0;
     aEmp = "";
     aDir = "";
     a1 = "";
     a2 = "";
     aDirectorio = "";
     direc = "";
     aAlfa1 = "";
     i = 0;
     aCar = "";
     aLon = "";
     nPos = 0;
     aAlfa = "";
     aPath = "";
     aHora = "";
     fFecha = @;
     aUsu = "";
     aNom = "";
     aFic = "";
     aDef = "";
     aDestino = "";
     aPathTras = "";
     aPathEmp = "";
     nUni = 0;
     nUni0 = 0;
     nUni2 = 0;
     &enForma = 0;
     nNume = 0;
     nAbo = 0;
     nImpo = 0;
     &enImpCliPen = 0;
     &eaCliente = "";
     &efFecha = @;
     aCod1 = "";
     aCod2 = "";
     aCod3 = "";
     aFic0 = "";
     aFic1 = "";
     nCamAlta = 0;
     nCamModi = 0;
     nCamBaja = 0;
     nModo = 0;
     nError = 0;
|FIN;

|PROCESO GrabaFic0;
     |SI aFic0 = ""; |ACABA; |FINSI;

     |PATH_DAT #referen0@#aPath#;
     |ABRE #referen0@;

     #referen0@#0 = #referen1@#0;
     |SI aFic1 = "dsm00123";
          #referen0@#1 = #referen1@#1;
     |FINSI;

     |LEE 000#referen0@.=;
     |SI FSalida ! 0;
          |GRABA 020#referen0@.n;
     |FINSI;
     |CIERRA #referen0@;
|FPRC;

|PROCESO LeeReg;
     #referen1@#0 = #origen@#0;
     |SI aFic1 = "dsm00003"; |O aFic1 = "agifa023"; |O aFic1 = "agifa095";
               |O aFic1 = "agifa039"; |O aFic1 = "dsm00210";
               |O aFic1 = "dsm00212";
          #referen1@#1 = #origen@#1;
     |FINSI;

     |SI aFic1 = "dsm00123"; |O aFic1 = "agifa310";
          #referen1@#0 = #origen@#0;
          #referen1@#1 = #origen@#1;
          #referen1@#2 = #origen@#2;
     |FINSI;
     |SI aFic1 = "agifa290";
          #referen1@#0 = #origen@#0;
          #referen1@#41 = #origen@#41;
     |FINSI;
     |LEE 101#referen1@.=;
|FPRC;

|PROCESO CamCopia;
     nPasa = 0;
     |SI nModoCam = 1; |Y #208#3 = "S"; nPasa = 1; |FINSI;
     |SI nModoCam = 2; |Y #208#4 = "S"; nPasa = 1; |FINSI;
     |SI nPasa = 0; |ACABA; |FINSI;
     nCampo = #208#1;
     #referen1@#nCampo# = #origen@#nCampo#;
|FPRC;

|DEFBUCLE CamCopiaModi;
     #208#3;
     ;
     aFic1, 001, "S";
     aFic1, 999, "S";
     ;
     NULL, CamCopia;
|FIN;

|DEFBUCLE CamCopiaAlta;
     #208#2;
     ;
     aFic1, 001, "S";
     aFic1, 999, "S";
     ;
     NULL, CamCopia;
|FIN;

|PROCESO EmCopia;
     |DFICB aAlfa; |QBF aAlfa;
     |PATH_DAT #41 aAlfa;

     |ABRE #41;
     |SALVA_FICHA #41;
     #41#0 = #200#0;
     |LEE 000#41=;
     |SI FSalida = 0;
          |DFICB aAlfa; |QBF aAlfa;
          aPath = aAlfa + (("00000" + #200#0) % -105) + "/";
          |PATH_DAT #referen1@#aPath#;
          |ABRE #referen1@;
          |DDEFECTO #referen1@;
          |HAZ LeeReg;
          |SI FSalida = 0;
               |SI nModo = 3; |Y #200nCamBaja = "S";
                    |BORRA 000#referen1@.a;
               |FINSI;
               |SI nModo ! 3; |Y #200nCamModi = "S";
                    nModoCam = 2;
                    |BUCLE CamCopiaModi;
                    |GRABA 020#referen1@.a;
                    |HAZ GrabaFic0;
               |FINSI;
          |SINO;
               |SI #200nCamAlta = "S";
                    nModoCam = 1;
                    |BUCLE CamCopiaAlta;
                    |HAZ GrabaFic0;
                    |GRABA 020#referen1@.n;
               |FINSI;
          |FINSI;
          |CIERRA #referen1@;
     |FINSI;
     |REPON_FICHA #41;
|FPRC;

|DEFBUCLE EmCopia;
     #200#1001;
     ;
     00001;
     99999;
     ;
     NULL, EmCopia;
|FIN;

|PROCESO SacaClave;
     aCod1 = "";
     aCod2 = "";
     aCod3 = "";
     aAlfa = #5#1; |QBF aAlfa;
     aLon = aAlfa % A0;
     |PARA i = 1; |SI i [ aLon; |HACIENDO i = i + 1;
          nPos = i * 100 + 1;
          aCar = aAlfa % nPos;
          |SI aCar = "|"; |SAL; |FINSI;
          aCod1 = aCod1 + aCar;
     |SIGUE;
     i = i + 1;
     |PARA; |SI i [ aLon; |HACIENDO i = i + 1;
          nPos = i * 100 + 1;
          aCar = aAlfa % nPos;
          |SI aCar = "|"; |SAL; |FINSI;
          aCod2 = aCod2 + aCar;
     |SIGUE;
     i = i + 1;
     |PARA; |SI i [ aLon; |HACIENDO i = i + 1;
          nPos = i * 100 + 1;
          aCar = aAlfa % nPos;
          aCod3 = aCod3 + aCar;
     |SIGUE;
|FPRC;

|PROCESO rodm0005;
     |HAZ SacaClave;

     aFic1 = #5#2; |QBF aFic1;
     aFic0 = "";

     aAlfa = aDef + aFic1;
     |CARGA_DEF aAlfa, origen@;
     |SI FSalida ! 0; |ACABA; |FINSI;

     |PATH_DAT #origen@#aPathTras#;

     |ABRE #origen@;
     |SI aFic1 = "agifa024";
          #origen@#0 = aCod1;
          nCamAlta = 1; nCamModi = 2; nCamBaja = 3;
     |FINSI;
     |SI aFic1 = "agifa019";
          #origen@#0 = aCod1;
          nCamAlta = 4; nCamModi = 5; nCamBaja = 6;
     |FINSI;
     |SI aFic1 = "agifa112";
          #origen@#0 = aCod1;
          nCamAlta = 7; nCamModi = 8; nCamBaja = 9;
     |FINSI;
     |SI aFic1 = "dsm00003";
          #origen@#0 = aCod1;
          #origen@#1 = aCod2;
          nCamAlta = 10; nCamModi = 11; nCamBaja = 12;
     |FINSI;
     |SI aFic1 = "agifa023";
         #origen@#0 = aCod1;
         #origen@#1 = aCod2;
         nCamAlta = 13; nCamModi = 14; nCamBaja = 15;
         aFic0 = "agifa020";
     |FINSI;
     |SI aFic1 = "dsm00123";
         #origen@#0 = aCod1;
         #origen@#1 = aCod2;
         #origen@#2 = aCod3;
         nCamAlta = 16; nCamModi = 17; nCamBaja = 18;
         aFic0 = "dsm00122";
     |FINSI;
     |SI aFic1 = "agifa095";
         #origen@#0 = aCod1;
         #origen@#1 = aCod2;
         nCamAlta = 19; nCamModi = 20; nCamBaja = 21;
         aFic0 = "agifa030";
     |FINSI;
     |SI aFic1 = "agifa039";
         #origen@#0 = aCod1;
         #origen@#1 = aCod2;
         nCamAlta = 28; nCamModi = 29; nCamBaja = 30;
     |FINSI;
     |SI aFic1 = "agifa310";
         #origen@#0 = aCod1;
         #origen@#1 = aCod2;
         #origen@#2 = aCod3;
         nCamAlta = 31; nCamModi = 32; nCamBaja = 33;
     |FINSI;
     |SI aFic1 = "agifa290";
         #origen@#0 = aCod1;
         #origen@#41 = aCod2;
         nCamAlta = 34; nCamModi = 35; nCamBaja = 36;
     |FINSI;
     |SI aFic1 = "dsm00117";
         #origen@#0 = aCod1;
         nCamAlta = 37; nCamModi = 38; nCamBaja = 39;
     |FINSI;
     |SI aFic1 = "dsm00209";
          #origen@#0 = aCod1;
          nCamAlta = 40; nCamModi = 41; nCamBaja = 42;
     |FINSI;
     |SI aFic1 = "dsm00210";
          #origen@#0 = aCod1;
          #origen@#1 = aCod2;
          nCamAlta = 22; nCamModi = 23; nCamBaja = 24;
     |FINSI;
     |SI aFic1 = "dsm00211";
          #origen@#0 = aCod1;
          nCamAlta = 43; nCamModi = 44; nCamBaja = 45;
     |FINSI;
     |SI aFic1 = "dsm00212";
          #origen@#0 = aCod1;
          #origen@#1 = aCod2;
          nCamAlta = 25; nCamModi = 26; nCamBaja = 27;
     |FINSI;
     |SI aFic0 ! "";
          aAlfa = aDef + aFic0;
          |CARGA_DEF aAlfa, referen0@;
          |SI FSalida ! 0; aFic0 = ""; |FINSI;
     |FINSI;
     aAlfa = aDef + aFic1;
     |CARGA_DEF aAlfa, referen1@;
     |LEE 000#origen@.=;
     |SI #5#3 = "A"; |Y FSalida = 0; nModo = 1; |BUCLE EmCopia; |FINSI;
     |SI #5#3 = "M"; |Y FSalida = 0; nModo = 2; |BUCLE EmCopia; |FINSI;
     |SI #5#3 = "B"; |Y FSalida ! 0; nModo = 3; |BUCLE EmCopia; |FINSI;
     |CIERRA #origen@;
     |DESCARGA_DEF #referen1@;
     |SI aFic0 ! "";
          |DESCARGA_DEF #referen0@;
     |FINSI;
     |DESCARGA_DEF #origen@;
|FPRC;

|DEFBUCLE rodm0005;
     #5#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, rodm0005;
|FIN;

|PROCESO CopiMaestros;
     nHay = 1;
     |DDEF aDef;
     |PATH_DAT #200#aPathTras#;
     |PATH_DAT #208#aPathTras#;
     |PATH_DAT #5#aPathTras#;
     |BUCLE rodm0005;
|FPRC;

|PROCESO Stock_ESSS;
     |ABRE #19;
     |DDEFECTO #19;
     #19#0 = #44#0;
     |LEE 101#19=;
     |SI FSalida ! 0; |GRABA 020#19b; |FINSI;
     |ABRE #17;
     |DDEFECTO #17;
     #17#0 = #44#0;
     #17#1 = #44#1;
     |LEE 101#17=;
     |SI FSalida ! 0; |GRABA 020#17b; |FINSI;

     |SI #44#7 = "E"; nUni = #44#4;   |SINO; nUni  = -#44#4;  |FINSI;
     |SI #44#7 = "E"; nUni2 = #44#12; |SINO; nUni2 = -#44#12; |FINSI;
     nUni = nUni * enForma;
     nUni2 = nUni2 * enForma;
     |SI #19#196 = 1; nUni0 = nUni; |SINO; nUni0 = nUni2; |FINSI;

     art_tota = art_tota + nUni;
     art_tota2 = art_tota2 + nUni2;
     art_disp = art_disp + nUni;
     art_disp2 = art_disp2 + nUni2;

     alm_tota = alm_tota + nUni;
     alm_tota2 = alm_tota2 + nUni2;
     alm_disp = alm_disp + nUni;
     alm_disp2 = alm_disp2 + nUni2;

     aAlfa = #44#5 % 402;
     nNume = aAlfa;
     nNume = (nNume - 1) * 2;
     |SI #44#7 = "E";
          |SI #19#196 = 1; nUni0 = nUni; |SINO; nUni0 = nUni2; |FINSI;
          nNume = nNume + 12;
          #17#36 = #17#36 + nUni0;
     |SINO;
          |SI #19#194 = 1; nUni0 = -nUni; |SINO; nUni0 = -nUni2; |FINSI;
          nNume = nNume + 11;
          #17#35 = #17#35 + nUni0;
     |FINSI;
     #17nNume = #17nNume + nUni0;

     |HAZ ValoraStock;
     |GRABA 020#19a;
     |GRABA 020#17a;
     |CIERRA #19;
     |CIERRA #17;
|FPRC;

|PROCESO Histo_ESSS;
     |ABRE #65;
     |DDEFECTO #65;
     #65#0 = #44#0;
     #65#1 = #44#5;
     |SI #44#7 = "E";
          #65#2 = "ES";
          #65#6 = #44#4;
          #65#17 = #44#12;
     |SINO;
          #65#2 = "SS";
          #65#6 = -#44#4;
          #65#17 = -#44#12;
     |FINSI;
     #65#3 = #44#18;
     #65#4 = #44#8;
     #65#5 = #44#1;
     #65#7 = #19#6;
     #65#8 = #19#9;
     #65#11 = #44#21;
     #65#18 = #44#13;
     #65#19 = #44#14;
     |SI enForma = 1;
          |GRABA 020#65n;
     |SINO;
          |BORRA 020#65c;
     |FINSI;
     |CIERRA #65;
|FPRC;

|PROCESO AcumuCli;
     |SI #10#15 ] 0;
          nAbo = 1;
     |SINO;
          nAbo = -1;
          #10#15 = -#10#15;
     |FINSI;
     nImpo = #10#15 < #10#5;
     nImpo = nImpo < #10#32;
     nImpo = nImpo < #10#7;
     nImpo = nImpo * nAbo * enForma;

     |ABRE #24;
     #24#0 = #10#3;
     |LEE 101#24=;
     |SI FSalida ! 0; |GRABA 020#24b; |FINSI;
     |SI #10#43 = AN;
          #24#50 = #24#50 + nImpo;    || es albaran
     |SINO;
          #24#50 = #24#50 - nImpo;    || es abono
     |FINSI;
     |GRABA 020#24a;
     |CIERRA #24;

     |SI #10#43 = "N";
          enImpCliPen = nImpo;
     |SINO;
          enImpCliPen =  -nImpo;
     |FINSI;
     eaCliente = #10#3;
     efFecha = #10#1;
     |HAZ AcumulaCli;
|FPRC;

|PROCESO BorraLinMovi;
     enForma = -1;
     |HAZ Histo_ESSS;
     |HAZ Stock_ESSS;
     |BORRA 020#44a;
|FPRC;

|PROCESO CopiaDatos;
     |PATH_DAT #17#aPathEmp#;
     |PATH_DAT #19#aPathEmp#;
     |PATH_DAT #24#aPathEmp#;
     |PATH_DAT #65#aPathEmp#;

     nHay = 1;
     |DDEF aDef;

     aAlfa = aDef + "agifa010";
     |CARGA_DEF aAlfa, referen@;
     |SI FSalida = 0;
          |PATH_DAT #referen@#aPathTras#;
          |PATH_DAT #10#aPathEmp#;
          |ABRE #10;
          |ABRE #referen@;
          |LEE 000#referen@.p;
          |PARA; |SI FSalida = 0; |HACIENDO;
               |SALVA_DATOS #referen@;
               |REPON_DATOS #10;
               |GRABA 040#10n;
               |SI FSalida ! 0;
                    aAlfa = "0000Cabecera Albaran existente:" + #10#52 + "/" + #10#0;
                    |MENAV aAlfa;
               |SINO;
                    enForma = 1; |HAZ AcumuCli;
               |FINSI;
               |LEE 000#referen@.s;
          |SIGUE;
          |CIERRA #referen@;
          |CIERRA #10;
          |DELINDEX #referen@;
          |DESCARGA_DEF #referen@;
     |FINSI;

     aAlfa = aDef + "rodw0007";
     |CARGA_DEF aAlfa, referen@;
     |SI FSalida = 0;
          |PATH_DAT #referen@#aPathTras#;
          |PATH_DAT #43#aPathEmp#;
          |PATH_DAT #44#aPathEmp#;
          |ABRE #43;
          |ABRE #referen@;
          |LEE 000#referen@.p;
          |PARA; |SI FSalida = 0; |HACIENDO;
               #43#4 = #referen@#1;
               #43#0 = #referen@#2;
               |LEE 141#43=;
               |SI FSalida = 0;
                    |BORRA 020#43a;
                    |BUCLELIN 0 BorraLinMovi #43;
               |FINSI;
               |LEE 000#referen@.s;
          |SIGUE;
          |CIERRA #referen@;
          |CIERRA #43;
          |DELINDEX #referen@;
          |DESCARGA_DEF #referen@;
     |FINSI;

     aAlfa = aDef + "agifa071";
     |CARGA_DEF aAlfa, referen@;
     |SI FSalida = 0;
          |PATH_DAT #referen@#aPathTras#;
          |PATH_DAT #71#aPathEmp#;
          |ABRE #71;
          |ABRE #referen@;
          |LEE 000#referen@.p;
          |PARA; |SI FSalida = 0; |HACIENDO;
               |SALVA_DATOS #referen@;
               |REPON_DATOS #71;
               |GRABA 040#71n;
               |SI FSalida ! 0;
                    aAlfa = "0000Linea Albaran existente:" + #71#29 + "/" + #71#0 + "-" + #71#1;
                    |MENAV aAlfa;
               |SINO;
                     enForma = 1; |HAZ AcumulaAV;
               |FINSI;
               |LEE 000#referen@.s;
          |SIGUE;
          |CIERRA #referen@;
          |CIERRA #71;
          |DELINDEX #referen@;
          |DESCARGA_DEF #referen@;
     |FINSI;

     aAlfa = aDef + "dsm00043";
     |CARGA_DEF aAlfa, referen@;
     |SI FSalida = 0;
          |PATH_DAT #referen@#aPathTras#;
          |PATH_DAT #43#aPathEmp#;
          |ABRE #43;
          |ABRE #referen@;
          |LEE 000#referen@.p;
          |PARA; |SI FSalida = 0; |HACIENDO;
               |SALVA_DATOS #referen@;
               |REPON_DATOS #43;
               |GRABA 040#43n;
               |SI FSalida ! 0;
                    aAlfa = "0000Cabecera Regularizacion existente:" + #43#4 + "/" + #43#0;
                    |MENAV aAlfa;
               |FINSI;
               |LEE 000#referen@.s;
          |SIGUE;
          |CIERRA #referen@;
          |CIERRA #43;
          |DELINDEX #referen@;
          |DESCARGA_DEF #referen@;
     |FINSI;

     aAlfa = aDef + "dsm00044";
     |CARGA_DEF aAlfa, referen@;
     |SI FSalida = 0;
          |PATH_DAT #referen@#aPathTras#;
          |PATH_DAT #44#aPathEmp#;
          |ABRE #44;
          |ABRE #referen@;
          |LEE 000#referen@.p;
          |PARA; |SI FSalida = 0; |HACIENDO;
               |SALVA_DATOS #referen@;
               |REPON_DATOS #44;
               |GRABA 040#44n;
               |SI FSalida ! 0;
                    aAlfa = "0000Cabecera Regularizacion existente:" + #44#21 + "/" + #44#18 + "-" + #44#8;
                    |MENAV aAlfa;
               |SINO;
                    |HAZ Stock_ESSS;
                    |HAZ Histo_ESSS;
               |FINSI;
               |LEE 000#referen@.s;
          |SIGUE;
          |CIERRA #referen@;
          |CIERRA #44;
          |DELINDEX #referen@;
          |DESCARGA_DEF #referen@;
     |FINSI;
|FPRC;

|PROCESO BorraDir;          ||Borra todos los archivos de aDirectorio
     |DFICE aDirectorio;
     aDirectorio = aDirectorio + "T/";

     direc = ""; |DIRECTORIO direc; |QBF direc;
     |SI aDirectorio ! "/";
          aAlfa1 = direc + "bin/agirm -r " + aDirectorio;
          |SYSTEM aAlfa1;
     |FINSI;
|FPRC;

|PROCESO ProcesaFic;
     #4#0 = aFic;
     |LEE 000#4=;
     |SI FSalida = 0; |ACABA; |FINSI;

     |HAZ BorraDir;

     |DFICE aPath;
     aDestino = aPath + aFic;
     |SI aSwLocal = "S";
          |COPIA_FICHERO aNom, aDestino;
     |SINO;
          |RECIBE_FICHERO aNom, aDestino;
     |FINSI;

     nError = 0;

     a1 = aDestino;
     a2 = aPath;
     |DETAR a1, a2;

     aDir = aPath + "T/";
     |_PDIR aDir;
     |PARA; |SI FSalida = 0; |HACIENDO;
          aLon = aDir % A0;
          aEmp = "";
          |PARA i = 1; |SI i [ aLon; |HACIENDO i = i + 1;
               nPos = -(i * 100 + 1);
               aCar = aDir % nPos;
               |SI aCar = "/"; |O aCar = "\"; |SAL; |FINSI;
               aEmp = aCar + aEmp;
          |SIGUE;

          a1 = aDir + "/" + aFic - ".tar" + ".gz";
          |XABRE "E", a1, 0;
          |SI FSalida < 0;
               ||el .tar a importar debe de tener el mismo nombre que el .gz
               aAlfa = "0000ERROR. El fichero " + aFic + " no es correcto, esta renombrado?..";
               |MENAV aAlfa;
               nError = 1; |SAL;
          |SINO;
               |DESCOMPRIME a1;
               |SI FSalida = 0;
                    a1 = a1 - ".gz";
                    a2 = aDir + "/";
                    |DETAR a1, a2;
                    aPathTras = aDir + "/";
                    |DBASE aPathEmp;
                    aPathEmp = aPathEmp + "fich/" + aEmp + "/";
                    |SI aEmp = "00000"; || Los maestros estan en la empresa 0
                         |HAZ CopiMaestros;
                    |SINO;
                         |HAZ CopiaDatos;
                    |FINSI;
               |SINO;
                    aAlfa = aFic - ".tar" + ".gz";
                    aAlfa = "0000ERROR al descompimir: " + aAlfa;
                    |MENAV aAlfa;
                    nError = 1; |SAL;
               |FINSI;
          |FINSI;
          |_SDIR aDir;
     |SIGUE;

     |HAZ BorraDir;
     |FBORRA aDestino;

     |SI nError = 0;
          #4#1 = fFecha;
          #4#2 = aHora;
          #4#3 = aUsu;
          |GRABA 020#4n;
     |FINSI;
|FPRC;

|PROCESO Tipo3; |TIPO 3;
     |SI #0#1 ! "SI"; |ACABA; |FINSI;
     |ABRE #1; |LEE 000#1p; |CIERRA #1;

     aUsu = Usuario % 810;
     fFecha = ~;
     |HORA aHora;

     |ABRE #4;
     aPath = #1#5; |QBF aPath;
     aNom = aPath;
     |SI aSwLocal = "S";
          |_PDIR aNom;
     |SINO;
          |R_PDIR aNom;
     |FINSI;
     |PARA; |SI FSalida = 0; |HACIENDO;
          aLon = aNom % A0;
          aFic = "";
          |PARA i = 1; |SI i [ aLon; |HACIENDO i = i + 1;
               nPos = -(i * 100 + 1);
               aCar = aNom % nPos;
               |SI aCar = "/"; |O aCar = "\"; |SAL; |FINSI;
               aFic = aCar + aFic;
          |SIGUE;
          aLon = aFic % A0;
          |SI aLon = 18;
               aAlfa = aFic % -104;
               |SI aAlfa = ".tar";
                    |HAZ ProcesaFic;
               |FINSI;
          |FINSI;
          |SI aSwLocal = "S";
               |_SDIR aNom;
          |SINO;
               |R_SDIR aNom;
          |FINSI;
     |SIGUE;
     |CIERRA #4;
     |SI nHay = 0;
          |MENAV "0000No hay ficheros a procesar...";
     |FINSI;
|FPRC;

|PROGRAMA;
     enSwNoCopia = 1;
     enEstaDentro = 1;

     |IP_REMOTO aAlfa;
     |SI aAlfa ! ""; aSwLocal = "N"; |SINO; aSwLocal = "S"; |FINSI;

     |DBASE aAlfa;
     aAlfa = aAlfa + "fich/";
     |PATH_DAT #4 aAlfa;

     |ABRE #0;
     |LEE 101#0p;
     |SI FSalida ! 0; |GRABA 020#0b; |FINSI;
     |CLS;
     |PINPA #0#0;
     |PINDA #0#0;
     |ENDATOS #1#0;
     |CIERRA #0;
     enSwNoCopia = 0;
|FPRO;
