|FICHEROS;
     legiz011 #0;
     legim005 #5;
     agifa007 #7;
     dsz00089 #89,MANTE;
     agifa041 #41;
|FIN;

|VARIABLES;
     nError = 0;
     aEmp = "";
     nBoton = 0;
     &Tempo = "";
     aTmp0 = "";
     {-1}aMenu = "";
     aMenu1 = "2400";
     aMenu2 = "1";
     aMenu3 = "Opciones: ";
     aMenu4 = "RAC";
     aMenu5 = " Revisar ";
     aMenu6 = " Aceptar ";
     aMenu7 = " Cancelar ";
     aAlfa = "";
     nSwEmp = 0;
     nModo = 0;
|FIN;

|PROCESO NoAlta; |TIPO 0;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 1;
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO CreaSerie;
     aAlfa = #0#2; |QBF aAlfa;
     |SI aAlfa = ""; |ACABA; |FINSI;

     #5#0 = #0#0;
     |LEE 000#5=;
     |SI FSalida = 0;
          #5#0 = #0#2;
          |GRABA 040#5n; || Sin errores
     |FINSI;

     #7#26 = #0#0;
     |LEE 000#7=;
     |SI FSalida = 0;
          #7#26 = #0#2;
          #7#27 = #0#3;
          |GRABA 040#7n; || Sin errores
     |FINSI;
|FPRC;

|DEFBUCLE CreaSerie;
     #0#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, CreaSerie;
|FIN;

|PROCESO Empresas2;
     aEmp = ("00000" + #89#0) % -105;
     |DBASE aAlfa;
     aAlfa = aAlfa + "fich/" + aEmp + "/";
     |PATH_DAT #5 aAlfa;
     |PATH_DAT #7 aAlfa;

     |ABRE #5;
     |ABRE #7;
     |BUCLE CreaSerie;
     |CIERRA #7;
     |CIERRA #5;
|FPRC;

|PROCESO EmpresaAct2;
     |DFICE aAlfa;
     aEmp = aAlfa % -205;
     |DBASE aAlfa;
     aAlfa = aAlfa + "fich/" + aEmp + "/";
     |PATH_DAT #5 aAlfa;
     |PATH_DAT #7 aAlfa;

     |ABRE #5;
     |ABRE #7;
     |BUCLE CreaSerie;
     |CIERRA #7;
     |CIERRA #5;
|FPRC;

|DEFBUCLE Empresas2;
     #89#1;
     ;
     INICIO;
     FINAL;
     ;
     EmpresaAct2, Empresas2;
|FIN;

|PROCESO Empresas1;
     aEmp = ("00000" + #89#0) % -105;
     |DBASE aAlfa;
     aAlfa = aAlfa + "fich/" + aEmp + "/";
     |PATH_DAT #5 aAlfa;

     |ABRE #5;
     #5#0 = #0#2;
     |LEE 000#5=;
     |SI FSalida = 0;
          nError = 1;
          |ERROR10;
     |FINSI;
     |CIERRA #5;
|FPRC;

|PROCESO EmpresaAct1;
     |DFICE aAlfa;
     aEmp = aAlfa % -205;
     |DBASE aAlfa;
     aAlfa = aAlfa + "fich/" + aEmp + "/";
     |PATH_DAT #5 aAlfa;

     |ABRE #5;
     #5#0 = #0#2;
     |LEE 000#5=;
     |SI FSalida = 0;
          nError = 1;
          |ERROR10;
     |FINSI;
     |CIERRA #5;
|FPRC;

|DEFBUCLE Empresas1;
     #89#1;
     ;
     INICIO;
     FINAL;
     ;
     EmpresaAct1, Empresas1;
|FIN;

|PROCESO CheqSerie;
     aAlfa = #0#2; |QBF aAlfa;
     |SI aAlfa = ""; |ACABA; |FINSI;

     nError = 0;
     |BUCLE Empresas1;
     |SI nError ! 0;
          |MENAV "0000Codigo serie existente...";
          |ERROR;
     |FINSI;
     |CIERRA #7;
|FPRC;

|PROCESO Serie;
     |DDEFECTO #7;
     #7#26 = #5#0;
     |LEE 000#7=;

     #0#0 = #7#26;
     #0#1 = #7#27;
     #0#2 = "";
     #0#3 = #7#27;

     aAlfa = #0#0; |QBF aAlfa;
     |SI aAlfa ! "";
          |GRABA 040#0n; || Sin errores
     |FINSI;
|FPRC;

|DEFBUCLE Serie;
     #5#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, Serie;
|FIN;

|PROCESO Empresas;
     aEmp = ("00000" + #89#0) % -105;
     |DBASE aAlfa;
     aAlfa = aAlfa + "fich/" + aEmp + "/";
     |PATH_DAT #5 aAlfa;
     |PATH_DAT #7 aAlfa;
     |ABRE #7;
     |BUCLE Serie;
     |CIERRA #7;
|FPRC;

|PROCESO EmpresaAct;
     |DFICE aAlfa;
     aEmp = aAlfa % -205;
     |DBASE aAlfa;
     aAlfa = aAlfa + "fich/" + aEmp + "/";
     |PATH_DAT #5 aAlfa;
     |PATH_DAT #7 aAlfa;
     |ABRE #7;
     |BUCLE Serie;
     |CIERRA #7;
|FPRC;

|DEFBUCLE Empresas;
     #89#1;
     ;
     INICIO;
     FINAL;
     ;
     EmpresaAct, Empresas;
|FIN;

|PROCESO CheqEmp; |TIPO 0;
     |DFICE aAlfa;
     aAlfa = aAlfa % -205;
     |SI #89#0 = aAlfa;
          |MENAV "0000Esta es la empresa actual...";
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO Min;
     #0#0 = "     ";
     #89#0 = 1;
|FPRC;

|PROCESO Max;
     #0#0 = "zzzzz";
     #89#0 = 99999;
|FPRC;

|PROCESO Esc;
     |PTEC 807;
     nSwEmp = 1;
|FPRC;

|PROGRAMA;
     |DBASE aAlfa; aAlfa = aAlfa + "fich/";
     |PATH_DAT #41 aAlfa;
     |SALVA_FICHA #41;

     |CLS;
     |CONTROL_SIMPLE 0, "BOTON, Otras Empresas", 0537, 0550, Esc;
     nBoton = FSalida;

     |HAZ CreaTempo; |NOME_DAT #0#Tempo#; |DELINDEX #0; aTmp0 = Tempo;
     |ABRE #0;
     |BUCLE Empresas;
     |CIERRA #0;
     |PINPA #0#0;
     |PARA; |SI aMenu2 = 1; |HACIENDO;
          |PARA; |SI; |HACIENDO;
               |ENTLINEAL #1#0, 1, 2, 21, 0, Min, Max;
               |SI nSwEmp = 1;
                    nSwEmp = 0;
                    |PUSHV 0501 2380;
                    |ENTLINEAL #1#89, 1, 1, 22, 1, Min, Max;
                    |POPV;
                    |ABRE #0;
                    |BUCLE Empresas;
                    |CIERRA #0;
               |SINO;
                    |SAL;
               |FINSI;
          |SIGUE;
          |MENU aMenu;
          aMenu2 = FSalida;
     |SIGUE;
     |SI aMenu2 = 2;
          |BUCLE Empresas2;
          |ABRE #7;
          |BUCLE CreaSerie;
          |CIERRA #7;
          |MENAV "0000Creacion de series finalizada";
     |FINSI;
     Tempo = aTmp0; |HAZ BoraTempo; |DELINDEX #0;


     |REPON_FICHA #41;
     |FIN_CONTROL_WINDOWS nBoton;
     |PULSATECLA;
|FPRO;
