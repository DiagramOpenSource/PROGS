     CALCULO PEDIDOS VENTA PMalpeLin73 PCalCabMalpe087

||si el campo de lote reservado esta rellenado (y este no es modificable),
||el campo de lote entregado solo puede ser igual que el reservado

|FICHEROS;
     malpe087 #0,MANTE;   ||Cab Ped. Vta
     malpe088 #3;   ||Lin Ped. Vta
     agifa019 #19;
|FIN;

|VARIABLES;
     nNume11   = 0;
     nNume7    = 0;
     &nDeci_DivP = 0;
     &nDeci_BaseMxP = 0;
     nImpo     = 0;
     nImpDiv   = 0;
     nMult     = 0;
|FIN;


|CALCULO PMalpeLineasPedido;
     #3#16 = #0#7;
     #3#20 = #0#4;
     #3#23 = #0#5;
     #3#24 = #0#6;
     #3#25 = #0#17;
     #3#26 = #0#9;

     |HAZ PMalpeLin73;

     #0#11 = #0#11 + #3#9;
     #0#42 = #0#42 + #3#39;

     |SI #19#194 = 2;
          #0#13 = #0#13 +(((((#3#49 *#3#6)))<#0#5)<#0#17)<#0#6;
          #0#44 = #0#44 +(((((#3#49*#3#38)))<#0#5)<#0#17)<#0#6;
     |SINO;
          #0#13 = #0#13 +(((((#3#43 *#3#6)))<#0#5)<#0#17)<#0#6;
          #0#44 = #0#44 +(((((#3#43*#3#38)))<#0#5)<#0#17)<#0#6;
     |FINSI;

     #0#12 = #0#11 - #0#13;
     #0#43 = #0#42 - #0#44;

     |SI #0#37 = "D";
          #0#45 = #0#42;
          #0#46 = #0#43;
          #0#47 = #0#44;
          #0#48 = #0#11;
          #0#49 = #0#12;
          #0#50 = #0#13;
     |SINO;
          #0#45 = #0#11;
          #0#46 = #0#12;
          #0#47 = #0#13;
          #0#48 = #0#42;
          #0#49 = #0#43;
          #0#50 = #0#44;
     |FINSI;
     |GRABA 020#3a;
     |LIBERA #3;
|FCAL;

|PROCESO PCalCabMalpe087;
     |ABRE #19;
     #0#11 = 0;
     #0#12 = 0;
     #0#13 = 0;
     #0#42 = 0;
     #0#43 = 0;
     #0#44 = 0;
     |BUCLELIN 0 PMalpeLineasPedido #0;
     |GRABA 021#0a;
     |LIBERA #0;
     |CIERRA #19;
|FPRC;

|PROCESO PMalpeLin73;
     |ABRE #19;
     #19#0 = #3#2;
     |LEE 000#19=;
     |CIERRA #19;

     |SI #0#37 = "D";                    || Si la moneda de trabajo es la divisa ...
          #3#6  = #3#38 / #0#36 * #0#41;  || ... recalculo precio en funcion de la divisa
          #3#40 = #3#6;                  || ... el campo visualiz. es el precio en moneda base
     |SINO;
          #3#38 = #3#6 / #0#41 * #0#36;  || .. recalculo precio en funcion de la moneda base
          #3#40 = #3#38;                 || ... el campo visual. es el precio en divisa
     |FINSI;

     |SI #19#194 = 2;                     || Unidades de facturacion
          nImpo = #3#44 * #3#6;
     |SINO;
          nImpo = #3#5 * #3#6;
     |FINSI;

     || nMult corrige el error del redondeo para negativos
     |SI nImpo < 0; nMult = -1; |SINO; nMult = 1; |FINSI;

     |SI #19#194 = 2;                     || Unidades de facturacion
          nNume7  = ((#3#44 * #3#6) ! nDeci_BaseMxP)  * nMult;
          nImpDiv = ((#3#44 * #3#38) ! nDeci_DivP) * nMult;
          nNume11 = (#3#44 - #3#49) * #3#6 * nMult;
     |SINO;
          nNume7  = ((#3#5 * #3#6) ! nDeci_BaseMxP) * nMult;
          nImpDiv = ((#3#5 * #3#38) ! nDeci_DivP) * nMult;
          nNume11 = (#3#5 - #3#43) * #3#6 * nMult;
     |FINSI;
     nNume11 = nNume11 ! nDeci_DivP;

     #3#9  = ((((nNume7 ) ) < #0#5) < #0#17) < #0#6;
     #3#39 = ((((nImpDiv ) ) < #0#5) < #0#17) < #0#6;

     nNume11 = ((((nNume11 ) ) < #0#5) < #0#17) < #0#6;
     nNume11 = nNume11 ! nDeci_DivP;

     #3#7  = nNume7  * nMult;
     #3#11 = nNume11 * nMult;
     #3#9  = #3#9  * nMult;
     #3#39 = #3#39 * nMult;

     |SI #0#37 = "D";
          #3#41 = #3#9;
     |SINO;
          #3#41 = #3#39;
     |FINSI;
|FPRC;
