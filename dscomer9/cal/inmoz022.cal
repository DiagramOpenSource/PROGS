|FICHEROS;
     inmoz022 #0;
     inmom001 #1;
|FIN;

|VARIABLES;
     aAux1 = "";
     aAux2 = "";
     aAux3 = "";

     fFecha = @;
     nSal = 0;
     nBoton = 0;
     aRuta = "";
     aAlfa     = "";
     aDato     = "";
     nHandle   = 0;
     nCampo    = 0;
     aLon      = "";
     i         = 0;
     nPos      = 0;
     aLetra    = "";
     aReg      = "";
     aTab      = "";
 {99}Valor     = "";
 {99}CB        = "";
     aFic = "";
     aHora = "";
|FIN;

|PROCESO CB;
     nCampo = 1;
     ICB = CB1<-;
     aLon = aDato % A0;
     aReg = "";
     |PARA i = 1; |SI i [ aLon; |HACIENDO i = i + 1;
          nPos = i * 100 + 1;
          aLetra = aDato % nPos;
          |SI aLetra = "/";
               CB = aReg;
               aReg = "";
               nCampo = nCampo + 1;
               ICB = ICB + 1;
          |SINO;
               aReg = aReg + aLetra;
          |FINSI;
          |SI nCampo > 90; |SAL; |FINSI;     || Por si.... :)
     |SIGUE;
     |QBF aReg;
     CB = aReg;
|FPRC;

|PROCESO Graba;
     || 1                  2       3        4      5         6       7       8    9
     || codigo de barras, obra, seguidor, serie, orden, paralelo, fecha, hora y usuario
     |DDEFECTO #1;
     #1#0 = Valor1; || CB
     |LEE 101#1=;
     |SI FSalida = 0;
          aAux1 = #1#9;
          |QBF aAux1;
          aAux2 = #1#10;
          |QBF aAux2;
          aAux3 = #1#11;
          |QBF aAux3;
          |SI aAux1 = ""; |Y aAux2 = ""; |Y aAux3 = "";
               #1#9 = Valor2; || Obra
               #1#10 = Valor3; || Seguidor
               #1#11 = Valor4; || Serie
               #1#12 = Valor5; || Orden
               #1#40 = Valor6; || Paralelo
               #1#16 = Valor9; || Usu
               #1#23 = Valor7; || Fecha
               #1#24 = aFic;
               |GRABA 020#1a;
               |LIBERA #1;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Dato;
     nCampo = 1;
     IValor = Valor1<-;
     aLon = aDato % A0;
     aReg = "";
     |PARA i = 1; |SI i [ aLon; |HACIENDO i = i + 1;
          nPos = i * 100 + 1;
          aLetra = aDato % nPos;
          |SI aLetra = aTab;
               Valor = aReg;
               aReg = "";
               nCampo = nCampo + 1;
               IValor = IValor + 1;
          |SINO;
               aReg = aReg + aLetra;
          |FINSI;
          |SI nCampo > 90; |SAL; |FINSI;     || Por si.... :)
     |SIGUE;
     |QBF aReg;
     Valor = aReg;
|FPRC;

|PROCESO SacaFic;
     aFic = "";
     aLon = aRuta % A0;
     |PARA i = aLon; |SI i > 0; |HACIENDO i = i - 1;
          nPos = i * 100 + 1;
          aAlfa = aRuta % nPos;
          |SI aAlfa = "/"; |O aAlfa = "\"; |SAL; |FINSI;
          aFic = aAlfa + aFic;
     |SIGUE;
|FPRC;

|PROCESO Tipo3; |TIPO 3;
     |SI #0#2 ! "SI"; |ACABA; |FINSI;

     |ESTADO_CONTROL nBoton, "HIDE";

     aRuta = #0#1; |QBF aAlfa;
     |HAZ SacaFic;

     |HORA aHora;
     fFecha = ~;

     aTab = &9;

     |ABRE #1;
     |XABRE "C", aRuta, nHandle;
     |SI FSalida ] 0
          |XLEE nHandle, 250, aDato;
          |PARA; |SI FSalida > 0; |HACIENDO;
               |HAZ Dato;
               |HAZ Graba;
               |SI nSal = 1; |SAL; |FINSI;
               |XLEE nHandle, 250, aDato;
          |SIGUE;
          |XCIERRA nHandle;
     |SINO;
          |MENAV "0000No puedo abrir el fichero...";
     |FINSI;
     |CIERRA #1;
|FPRC;

|PROCESO Tipo0; |TIPO 0;
     |SI FSalida = 13;
          |ESTADO_CONTROL nBoton, "DISABLE";

          aRuta = #0#1;  |QBF aRuta;
          |SI aRuta = "";
              aRuta = "D*.*";
          |FINSI;
          aRuta = "F" + aRuta;

          |BROWSE aRuta;

          aRuta = aRuta % 2;
          |SI aRuta ! "F";
               #0#1 = aRuta;
          |FINSI;
          |PINTA #0#1;

          |ESTADO_CONTROL nBoton, "ENABLE";
          |ERROR;
     |FINSI;

     aRuta = #0#1;
     |HAZ SacaFic;

     aAlfa = aFic % 101;
     aAlfa = aAlfa > "";
     |SI aAlfa ! "D";
          |MENAV "0000Nombre fichero incorrecto debe de comezar por: 'D'";
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO Boton;
     |PTEC 827;
|FPRC;

|PROGRAMA;
     |ABRE #0;
     |LEE 101#0p;
     |SI FSalida ! 0; |GRABA 020#0b; |FINSI;
     |CLS;
     #0#2 = "NO";
     |PINPA #0#0;
     |PINDA #0#0;
     |CONTROL_SIMPLE 0, "BOTON, Fichero", 1010, 1018, Boton;
     nBoton = FSalida;
     |ENDATOS #1#0;
     |SI FSalida = 0;
          |GRABA 020#0a;
     |FINSI;
     |CIERRA #0;
     |FIN_CONTROL_WINDOWS nBoton;
|FPRO;
