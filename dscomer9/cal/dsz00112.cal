|FICHEROS;
     agifa007 #7;
     agifa010 #10;
     agifa024 #24;
     agifa071 #71;
     agifa142 #142;
     agifa143 #143;   ||  Temporal pase Facturas Proforma.
     agifa146 #146;
     agifa147 #147;
     agifa166 #166;   ||  Pantalla de pase.
     dsm00053 #53;
     dsm00005 #905;
     dsm00047 #947;
     dsm00050 #950;
|FIN;

|VARIABLES;
     &enSwMenu = 0;
     &eaTag = "";
     aFichero = "";
     aAlfa = "";
     i = 0;
     importe = 0;
     cant = 0;
|FIN;

|INCLUYE i_varart;

|PROCESO GrabaCompoN;
     |SALVA_DATOS #905;

     #905#0 = "FC";
     #905#1 = #71#29;
     #905#2 = #71#0;
     #905#3 = #71#1;
     |GRABA 020#905n;
     |LIBERA #905;

     eaTipo        = "AV";
     eaSerie       = #71#29;
     enCodigo      = #71#0;
     enLinea       = #71#1;
     efFecha       = #10#1;
     eaArticulo    = #71#2;
     enAlmacen     = #71#3;
     eaComponente  = #905#4;
     enUnidades    = #71#5;
     enTBruto      = #71#7;
     eaAbono       = "N";
     enTarifa      = #24#39;
     enCamBas      = #10#69;
     eaMoneda      = #10#70;
     enCamDiv      = #10#68;

     |SUB_EJECUTA_NP "dsz00002;FCC";

     |REPON_DATOS #905;
     |LEE 040#905=;
|FPRC;

|DEFBUCLE dsm00005N;
     #905#1;
     ;
     "FP", #147#20, #147#0, #147#1, "      ";
     "FP", #147#20, #147#0, #147#1, "zzzzzz";
     ;
     NULL, GrabaCompoN;
|FIN;

|PROCESO LineasKit;
     #947#0 = #71#29;          || serie factura
     #947#1 = #71#0;           || nro. factura
     #947#2 = #71#1;           || linea factura
     #947#6 = #950#6;         || linea kit
     #947#3 = #950#3;         || articulo
     #947#4 = #950#4;         || almacen
     #947#5 = #950#5;         || unidades
     #947#7 = #950#7;         || observaciones
     #947#8 = #950#8;         || precio coste medio
     #947#9 = #950#9;
     #947#10 = #950#10;
     #947#11 = #950#11;
     #947#12 = #950#12;
     |GRABA 020#947n;
|FPRC;

|DEFBUCLE LineasKit;
     #950#1;
     ;
     #147#20, #147#0, #147#1, 001;
     #147#20, #147#0, #147#1, 999;
     ;
     NULL, LineasKit;
|FIN;

|PROCESO dsm00053;
     #53#0 = "F";
     #53#1 = #71#29;
     #53#2 = #71#0;
     #53#3 = #71#1;
     |GRABA 020#53n;
|FPRC;

|DEFBUCLE dsm0053;
     #53#1;
     ;
     "X", #147#20, #147#0, #147#1, 0001;
     "X", #147#20, #147#0, #147#1, 9999;
     ;
     NULL, dsm00053;
|FIN;

|PROCESO Lineas;
     |DDEFECTO #71;
     #71#29 = #10#52;
     #71#0 = #10#0;
     #71#1 = #147#1;
     |PARA i = 2; |SI i [17; |HACIENDO i = i + 1;
          #71i = #147i;
     |SIGUE;
     #71#33 = #147#21;
     #71#34 = #147#22;
     #71#35 = #147#23;
     #71#36 = #147#24;
     #71#48 = #147#28;
     #71#49 = #147#29;
     #71#50 = #147#30;
     #71#51 = #147#31;
     #71#52 = #147#32;
     #71#53 = #147#33;
     |GRABA 020#71n;
     |BUCLE dsm0053;
     |BUCLE LineasKit;
     |BUCLE dsm00005N;
     enForma = 1;
     |HAZ AcumulaAV;
|FPRC;

|PROCESO Cabecera;
     |DDEFECTO #24;
     |ABRE #24;
     #24#0 = #146#3;
     |LEE 000#24=;
     |CIERRA #24;

     |DDEFECTO #7;
     |ABRE #7;
     #7#26 = #146#48;
     |LEE 000#7=;
     |CIERRA #7;

     |PARA i = 1; |SI i [ 37; |HACIENDO i = i + 1;
          #10i = #146i;
     |SIGUE;
     #10#1 = #143#5;
     #10#40 = #24#41;
     #10#50 = #24#40;
     #10#51 = #7#28;
     #10#62 = #146#81;
     #10#63 = #146#86;
     #10#68 = #146#65;
     #10#69 = #146#66;
     #10#70 = #146#67;
     #10#73 = #146#68;
     #10#74 = #146#69;
     #10#84 = #146#80;
     #10#88 = #146#78;
     aAlfa = "PROFORMA:" + #146#48 + "/" + (("000000" + #146#0) % -106);
     #10#97 = aAlfa;
|FPRC;

|PROCESO Actualiza;
     |SI #10#15 ] 0;
          importe = 1;
     |SINO;
          importe = -1;
          #10#15 = -#10#15;
     |FINSI;
     cant = #10#15 < #10#5;
     cant = cant < #10#32;
     cant = cant < #10#7;
     cant = cant * importe;
     |ABRE #24;
     #24#0 = #10#3;
     |LEE 101#24=;
     |SI FSalida = 0;
        |SI #10#43 = AN;
           #24#50 = #24#50 + cant;    || es albaran
        |SINO;
           #24#50 = #24#50 - cant;    || es abono
        |FINSI;
        |GRABA 020#24a;
        |LIBERA #24;
     |FINSI;
     |CIERRA #24;

     |SI #10#43 = "N";
          enImpCliPen = cant;
     |SINO;
          enImpCliPen =  -cant;
     |FINSI;
     eaCliente = #10#3;
     efFecha = #10#1;
     |HAZ AcumulaCli;

     eaTag = "AV";  |HAZ Riesgos;
|FPRC;

|PROCESO RecalLin;
     |HAZ TotalLin71;
     |HAZ CalCabAgifa010_2;
     |GRABA 020#71a;
|FPRC;

|PROCESO Temporal;
     |DDEFECTO #166;
     #166#0 = #143#2;
     #166#1 = #143#3;
     |PINDA #0#166;

     #146#48 = #143#2;
     #146#0 = #143#3;
     |LEE 121#146=;
     |SI FSalida = 0;
          #10#52 = #143#7;
          enSwMenu = 1;
          |HAZ ContaAV7;
          |GRABA 000#10b;
          |SI FSalida = 0;
               #166#2 = #10#52;
               #166#3 = #10#0;
               |PINDA #0#166;
               |HAZ Cabecera;
               |BUCLELIN 0 Lineas #146;

               |HAZ LimCabAgifa010;
               |BUCLELIN 0 RecalLin #10;
               |GRABA 020#10a;
               |LIBERA #10;

               |HAZ Actualiza;

               #146#60 = #10#52;  ||  Serie.
               #146#61 = ("000000" + #10#0) % -106;   ||  Factura.
               #146#62 = "A";    ||  Tipo de documento asignado
               |GRABA 020#146a;
          |SINO;
               aAlfa = "0000ERROR. Albaran existente:" + #10#52 + "/" + (("000000" + #10#0)%-106);
               |MENAV aAlfa;
          |FINSI;
     |FINSI;
     |LIBERA #146;
|FPRC;

|DEFBUCLE Temporal;
     #143#1;
     8;
     001, "*";
     999, "*";
     ;
     NULL, Temporal;
|FIN;

|PROGRAMA;
     |ABRE #142; |LEE 000#142p; |CIERRA #142;
     aFichero = PARAMETRO; |QBF aFichero;
     |NOME_DAT #143 aFichero;

     |PINPA #0#166;
     |PINTA 1446,"Albaranes";

     |ABRE #146;
     |ABRET #10;
     |ABRET #71;
     |BUCLE Temporal;
     |CIERRAT #10;
     |CIERRAT #71;
     |CIERRA #146;

     |DELINDEX #143;
|FPRO;
