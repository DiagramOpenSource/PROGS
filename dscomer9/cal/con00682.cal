|FICHEROS;
con00682 #0;
con00680 #1;
con00625 #3;
agifa024 #2;
con00683 #683;
|FIN;

|VARIABLES;
     aClave = "";
     nClave = 0;
     nSec = 0;
     aAlfa = "";
     fFecha = @;
     aOpera = "";
horas = "";
tecla = 0;
hora_ini = "";
hora_ini_num = 0;
hora_fin = "";
hora_fin_num = 0;
minut_ini = "";
minut_ini_num = 0;
minut_fin = "";
minut_fin_num = 0;
hora_totali = 0;
horatotalf = 0;
total_horas = 0;
|FIN;

|PROGRAMA;
|CLS;
|CABEZA "Trabajos Operarios";
|ENTLINEAL #1#0 , 3 , 7 , 19 , 1 , min , max;
#0#1 = ~;
|ENCAMPO #0#0;
|ENCAMPO #1#0;
|CAMPO_MODIFICA #0#0 , 1 , "N";
|CAMPO_MODIFICA #0#1 , 1 , "N";
|SI FSalida = 0;
     nClave = 1;
     aOpera = #0#0;
     fFecha = #0#1;
     |ENTLINEAL #1#0 , 3 , 1 , 19 , 1 , min , max;
|FINSI;
|CAMPO_MODIFICA #0#0 , 1 , "S";
|CAMPO_MODIFICA #0#1 , 1 , "S";
|FPRO;

|PROCESO min;
     #0#0 = aOpera;
     #0#1 = fFecha;
     #0#2 = 1;
|FPRC;

|PROCESO max;
     #0#0 = aOpera;
     #0#1 = fFecha;
     #0#2 = 1;
     #0#2 = 999;
|FPRC;

|PROCESO vertipo;
  |SI #0#3 = 2;
    |CAMPO_MODIFICA #0#4, 1, "N";
    |CAMPO_MODIFICA #0#5, 1, "N";
    |CAMPO_MODIFICA #0#13, 1, "S";
  |SINO;
    |CAMPO_MODIFICA #0#4, 1, "S";
    |CAMPO_MODIFICA #0#5, 1, "S";
    |CAMPO_MODIFICA #0#13, 1, "N";
  |FINSI;
|FPRC;

|PROCESO pintadat;|TIPO 7;
|PINTA #0#0;
|PINTA #0#1;
|PINTA 0741 , #1#1;
|FPRC;


|PROCESO hora;
tecla = FSalida;
|SI #0Campo ! Contenido;
     |HAZ ClaveH;
     |SI FSalida ! 0; |ERROR; |ACABA; |FINSI;
|FINSI;

|SI tecla = 17;
|SI #0Campo ! "     ";
|MENAV "0000Ya tiene hora";
|ERROR;
|ACABA;
|FINSI;
|HORA horas;
#0Campo = horas;
|PINTA #0Campo;
|FINSI;
|SI #0#9 ! "     ";

     hora_ini = #0#8 % A102;
     hora_ini_num = hora_ini;
     minut_ini = #0#8 % -102;
     minut_ini_num = minut_ini;

     hora_fin = #0#9 % A102;
     hora_fin_num = hora_fin;
     minut_fin = #0#9 % -102;
     minut_fin_num = minut_fin;

     |SI hora_fin_num < hora_ini_num;
          hora_fin_num = hora_fin_num + 24;
     |FINSI;

     hora_totali = hora_ini_num + (minut_ini_num / 60);
     horatotalf = hora_fin_num + (minut_fin_num / 60);
     total_horas = horatotalf - hora_totali;
     #0#6 = total_horas;
     |SI #0#10 = 0;
        #0#10 = #0#6;
     |FINSI;
     |PINTA #0#6;
     |PINTA #0#10;
|FINSI;

|FPRC;

|PROCESO lee_cliente;
|SI #0#3 = 1;
     |ABRE #3;
     #3#0 = #0#4;
     #3#1 = #0#5;
     |LEE 000#3=;
     |SI FSalida ! 0 ;
       |SI #0#3 = 1;
          |MENAV "0000LA ORDEN DE REPARACION NO EXISTE";
          |ERROR;
       |FINSI;
     |FINSI;
     |LIBERA #3;
     |CIERRA #3;
     |ABRE #2;
     #2#0 = #3#11;
     |LEE 000#2=;
     |SI FSalida ! 0 ; |DDEFECTO #2; |FINSI;
     #0#7 = #2#1;
     |PINTA #0#7;
     |LIBERA #2;
     |CIERRA #2;
     |SI #3#16 ! 0;
          |MENAV "0000Orden de Reparacion Cerrada o Aparcada.";
          |ERROR;
     |FINSI;
|FINSI;
|FPRC;

|PROCESO MiraSiOR; |TIPO 7;
   |SI #con00682#11 = "S";
      |MENAV "    El trabajo ya ha pasado a O.R.";
      |PTEC 807;
   |FINSI;
|FPRC;


|PROCESO Clave;
     |SI nClave = 0; |ACABA; |FINSI;
     |AVISO;
     |BLIN 24;
     |ATRI I;
     |PINTA 2420, "Entre la Clave...:";
     |ATRI 0;
     E_sup = 10;
     E_inf = "";
     aAlfa = "";
     aAlfa = 2439 ? -1;
     |BLIN 24;
     aAlfa = aAlfa > ""; |QBF aAlfa;
     FSalida = 0;
     |SI aAlfa ! "NUEVASEC";
          |MENAV "0000ERROR. Clave incorrecta";
          FSalida = 1;
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO ClaveSec; |TIPO 0;
     aAlfa = #0#5; |QBF aAlfa;
     |SI #0Campo ! nSec; |Y Campo = 14; |Y aAlfa ! "";
          |HAZ Clave;
          |SI FSalida ! 0; #0Campo = nSec; |FINSI;
     |FINSI;
|FPRC;

|PROCESO CogeSec; |TIPO 7;
     nSec = #0Campo;
|FPRC;

|PROCESO ClaveH;
     |ABRE #683; |LEE 000#683p; |CIERRA #683;
     aClave = #683#10; |QBF aClave;
     aAlfa = "";
     |BLIN 24;
     |ATRI I;
     |PINTA 2420, "Entre la clave...:";
     |ATRI 0;
     E_sup = 10;
     E_inf = "";
     aAlfa = 2439 ? -1; |QBF aAlfa;
     |BLIN 24;
     |SI aAlfa = aClave;
          FSalida = 0;
     |SINO;
          FSalida = 1;
          |MENAV "0000Clave incorrecta...";
     |FINSI;
|FPRC;
