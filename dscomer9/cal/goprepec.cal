     EL DEF goprepec/goprepel ya no se usan
     estan quitado del menu, ahora estan el goprep2c y goprep2l


|FICHEROS;
     goprepec #0;        || Pre-Pedidos(C).

     goprepel #2;        || Pre-Pedidos(L).
     agifa081 #3;        || Pedidos de Compras(C).
     agifa067 #4;        || Pedidos de Compras(L).
     agi00030 #5,MANTE;  || Pantalla desde/hasta para creacion de pedidos.
     agifa112 #6;        || Proveedores.
     agifa019 #7;        || Articulos.
     agifa017 #8;        || Almacenes Stock
     agifa029 #9;        || Almacenes
     agifa112 #10;       || Proveedores
     ||agifa041 #11;       || Datos empresa;
     agifa142 #12;       || Parametros Generales
     gopreped #13;
     go000002 #14;
     gomantho #15;
     gocodpos #16;
     go000001 #17;
     agifa321 #35;       || Parametros de Divisas
     agifa322 #36;       || Clientes/Divisas
     agifa323 #37;       || Lineas de Clientes/Divisas
     agifa324 #38;       || Divisas
     gopregtm #99,MANTE; || Temporal

     dsm00064 #964;
     dsm00065 #965;
     goparame #100;
     goparam5 #105;
     guerm025 #25;
     gopre001 #1000;
     dsm00083 #83;
     gotrabaj #101;

     agifa019@ #19;
     dserpm10@;
|FIN;

|VARIABLES;
     aEmp = "";
     &eaErOb1 = ""; &eaErOb2 = ""; &eaErOb3 = ""; &eaErOb4 = "";
     &eaErOb5 = ""; &eaErOb6 = ""; &eaErOb7 = ""; &eaErOb8 = "";
     &eaErOb9 = ""; &eaErOb10 = ""; &eaErOb11 = ""; &eaErOb12 = "";
     &eaErOb13 = ""; &eaErOb14 = ""; &eaErOb15 = "";
     nAlm = 0;
     nUni = 0;
     nUni2 = 0;
     aPrepedido = "";
     nForma = 0;
     aCadena = "";
     {100}Matriz = "";
     aModi = "";
     nPosi = 0;
     aVisu = "";
     aLin = "";
     aCampos = "";
     nPrimera = 0;
     &enCambiArti = 0;
     &ser_ped = "";
     &num_ped = 0;
     aProveAnt = "";
     aTraba = "";
     nSwImprePrv=0;
     nSwImpreDA= 0;
     aEnt      = "";
     aDir      = "";
     aPrv      = "";
     aPob      = "";
     i         = 0;
     aDef19    = "";
     nCampo    = 0;
     &eaJefe   = "";
     &enSwi    = 0;
     &enEvento = 1;
     &eaAsunto = "";
     &eaOrigen = "";
     &eaTipoD  = "";
     nCalc     = 0;
     aAlfa           = "";
     fec_ant         = @;
     &eaHora         = "";
     &enStComp       = 0;
     &enStDep        = 0;
     swim            = 0;
     swVic           = 0;
     aFic            = "";
     &enSwMenu       = 0;
     nModo           = 0;
     nConta          = 0;
     nLinea          = 0;
     nImpo           = 0;
     aDatos          = "";
     aDirEmp         = "";
     nCodEmp         = 0;
     &nDeci_Base     = 0;   || Decimales en Totales de la moneda base
     &nDeci_PreBase  = 0;   || Decimales en el Precio en moneda base
     &nDeci_BaseMxP  = 0;   || Decimales en Totales de la moneda base (Maxima precision)
     &nDeci_PreBasMxP = 0;   || Decimales en el Precio en moneda base (Maxima precision)
     &nDeci_DivP      = 0;   || Decimales en Totales de la divisa
     &nDeci_PreDivP   = 0;  || Decimales en el Precio en divisa
     &nDeci_TotVisP   = 0;  || Decimales en Totales visibles (el de la moneda de trabajo)
     &nDeci_TotNoVisP = 0;  || Decimales en Totales no visibles (el de la otra moneda)
     &nDeci_PreVisP   = 0;  || Decimales en el Precio visual. (lineas)
     &nDeci_ImpVisP   = 0;  || Decimales en el Importe visual. (lineas)
     nfecha    = 0;
     &ac_divisa = "N";   || Vale "S" cuando las divisas en compras estan
                         || activadas y la serie es de divisas
||     &pintado = 0;       || Sirve para controlar los pintados de las lineas
     &decim_divisa = 0;  || Decimales de la divisa
     &precio_divisa = 0; || Decimales del precio en divisa
     &decim_base = 0;    || Decimales de la moneda base
     &precio_base = 0;   || Decimales del precio en moneda base
     div_act = 0;        || Divisa actualizada. Me sirve en un PARA para que
                         || no salga hasta que este actualizada (div_act=1)
     &prov_divisa = "";  || Codigo de Proveedor. Se pasa a PROVEED/DIVISAS
     ||&divisa = "";       || Codigo de Divisa. Se pasa a ACTUAL. de DIVISAS

     fechasis  = ~;
     Unidades  = 0;
     asto      = 0;
     tempo     = " ";
     pro_ant   = "";
     pedido    = "";
     linea     = 0;
     numero    = 0;
     div_ant   = "";
     monba     = "";
     serie     = "";
     montra    = "";
     divisa    = "";
     cambiomb  = 0;
     cambio    = 0;
     mes       = 0;
     fmes1     = "  ";
     art       = "";
     pre       = 0;
     dto       = 0;
     nImpDiv   = 0;
     nPend     = 0;
     {-1}aUtil  = "";
         aUtil1 = "2400";
         aUtil2 = "1";
         aUtil3 = " ";
         aUtil4 = "GLED";
         aUtil5 = " Generar ";
         aUtil6 = " Listar  ";
         aUtil7 = " Envio   ";
         aUtil8 = " Duplicar ";
     {-1}aDato  = "";
         aDato1 = "2400";
         aDato2 = "1";
         aDato3 = "Generacion : ";
         aDato4 = "PT";
         aDato5 = "  Parcial ";
         aDato6 = "  Total   ";
  {-1}aEnv = "";
     aEnv1 = "1010";
     aEnv2 = "1";
     aEnv3 = "3";
     aEnv4 = "Prepedido pdte revision precios";
     aEnv5 = "Incidencias en precios o fechas previstas";
     aEnv6 = "Orden de generacion de pedido";
|FIN;

|INCLUYE i_varart;

|PROCESO ObsDerp;
     |DFICE aEmp;
     aEmp = aEmp % -205;
     |DBASS aAlfa;
     aAlfa = aAlfa + "dserp/def/dserpm10";
     |CARGA_DEF aAlfa, dserpm10@;
     |SI FSalida = 0;
          |DBASS aAlfa;
          aAlfa = aAlfa + "dserp/fich/";
          |PATH_DAT #dserpm10@#aAlfa#;

          aAlfa = #0#0;
          |DDEFECTO #dserpm10@;
          |ABRE #dserpm10@;
          #dserpm10@#19 = "dscomer9";
          #dserpm10@#18 = aEmp;
          #dserpm10@#0 = "goprep2c";
          #dserpm10@#1 = aAlfa;
          |LEE 000#dserpm10@.=;
          || #dserpm10@#2 esta vacio
          eaErOb1 = #dserpm10@#3;
          eaErOb2 = #dserpm10@#4;
          eaErOb3 = #dserpm10@#5;
          eaErOb4 = #dserpm10@#6;
          eaErOb5 = #dserpm10@#7;
          eaErOb6 = #dserpm10@#8;
          eaErOb7 = #dserpm10@#9;
          eaErOb8 = #dserpm10@#10;
          eaErOb9 = #dserpm10@#11;
          eaErOb10 = #dserpm10@#12;
          eaErOb11 = #dserpm10@#13;
          eaErOb12 = #dserpm10@#14;
          eaErOb13 = #dserpm10@#15;
          eaErOb14 = #dserpm10@#16;
          eaErOb15 = #dserpm10@#17;
          |CIERRA #dserpm10@;
          |DESCARGA_DEF #dserpm10@;
     |FINSI;
|FPRC;

|PROCESO MiraAct;
     |SI #2#10 = "S";
          aAlfa = "S";
          |ERROR10;
     |FINSI;
|FPRC;

|PROCESO CheqBorLin;
     |SI #2#23 ! 0;
          aAlfa = "0000Linea: " + #2#1 + " con stock reservado...";
          |MENAV aAlfa;
          enError = 1;
     |FINSI;
     |SI #2#30 ! #2#32;
          aAlfa = "0000Linea: " + #2#1 + " con unidades pdte distintas al expediente...";
          |MENAV aAlfa;
          enError = 1;
     |FINSI;
     |SI #2#31 ! 0;
          aAlfa = "0000Linea: " + #2#1 + " con unidades a pedir...";
          |MENAV aAlfa;
          enError = 1;
     |FINSI;
|FPRC;

|PROCESO Tipo2; |TIPO 2;
     nModo = (FEntrada / 100) ! 0;
     nForma = 0 +. 1;

     |MODULO aPrepedido;
     |SI aPrepedido = "goprepec"; aPrepedido = "N"; |SINO; aPrepedido = "S"; |FINSI;
     |SI aPrepedido ! #0#15; |Y nModo = 2;
          |SI #0#15 = "S";
               aAlfa = "0000Es un pre-pedido parcial, modifiquelo por su mantenimiento";
          |SINO;
               aAlfa = "0000No es un pre-pedido parcial, modifiquelo por su mantenimiento";
          |FINSI;
          |SI nForma = -1; |MENAV aAlfa; |FINSI;
          |ERROR;
     |FINSI;

     |SI nModo = 3;
          aAlfa = "";
          |BUCLELIN 2 MiraAct #0;
          |SI aAlfa = "S";
               |MENAV "0000Existen lineas actualizadas...";
               |ERROR; |ACABA;
          |FINSI;
          enError = 0;
          |BUCLELIN 2 CheqBorLin #0;
          |SI enError ! 0; |ERROR; |ACABA; |FINSI;
     |FINSI;
|FPRC;

|CALCULO Contador; |TIPO 7;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 1;
          |LEE 000#0u;
          |SI FSalida = 0;
               nConta = #0#0 + 1;
          |SINO;
               nConta = 1;
          |FINSI;
          |DDEFECTO #0;
          #0#0 = nConta;

          |MODULO aPrepedido;
          |SI aPrepedido = "goprepec"; aPrepedido = "N"; |SINO; aPrepedido = "S"; |FINSI;
          #0#15 = aPrepedido; |PINTA #0#15;
     |FINSI;
|FCAL;

|PROCESO miramonba;
     |ABRE #agifa321;
     |ABRE #agifa324;
     |LEE 010#agifa321.p;   ||Leo parametros de divisa
     monba = #agifa321#9;  ||Cojo la Moneda Base de parametros
     #agifa324#0 = #agifa321#9;
     |LEE 010#agifa324.=;         ||Leo la ficha de la moneda base en Divisas
     cambiomb    = #agifa324#3;  ||Asigno el cambio de la moneda base con el euro
     decim_base  = #agifa324#7;    ||Asigno los decimales de la moneda base
     precio_base = #agifa324#9;   ||Asigno los decimales del precio en la mon. base
     |CIERRA #agifa324;
     |CIERRA #agifa321;
|FPRC;

|PROCESO Param_Divisas; |TIPO 0;
     |ABRE #agifa324;
     #agifa324#0 = divisa;
     |LEE 001#agifa324.=;
     |CIERRA #agifa324;

     decim_divisa = #agifa324#7;
     precio_divisa = #agifa324#9;
     nDeci_DivP = decim_divisa;
     nDeci_PreDivP = precio_divisa;
     cambio = #agifa324#3; ||Cambio divisa
     |SI montra = "B";
         nDeci_PreVisP   = precio_divisa;
         nDeci_ImpVisP   = decim_divisa;
         nDeci_Base      = decim_base;
         nDeci_PreBase   = precio_base;
         nDeci_BaseMxP   = decim_base;
         nDeci_PreBasMxP = precio_base;
         nDeci_TotVisP   = decim_base;
         nDeci_TotNoVisP = decim_divisa;
     |SINO;
         nDeci_PreVisP   = precio_base;
         nDeci_ImpVisP   = decim_base;
         nDeci_Base      = decim_base;
         nDeci_PreBase   = precio_base;
         |SI cambio = 1;
              nDeci_BaseMxP   = decim_base;    || sin triangulacion
              nDeci_PreBasMxP = precio_base;
         |SINO;
              nDeci_BaseMxP   = #agifa321#10;  || con triangulacion
              nDeci_PreBasMxP = #agifa321#11;
         |FINSI;
         nDeci_TotVisP   = decim_divisa;
         nDeci_TotNoVisP = decim_base;
     |FINSI;
|FPRC;

|PROCESO CambioDivisa; |TIPO 0;
      |SI #agifa112#80 = "S"; || Si el proveedor es de divisa pactada
         |ABRE #agifa323;
         |ABRE #agifa322;
         #agifa322#0 = #agifa081#4;
         #agifa322#2 = #agifa081#27;
         #agifa322#7 = "N";
         |SELECT #2#agifa322;  || Con esta clave ...
         |LEE 011#agifa322.=;  || ... busco periodos no finalizados de esa divisa
         |SI FSalida = 0;  || Si existe un periodo no finalizado
           |PARA div_act = 0; |SI div_act = 0; |HACIENDO;
             #agifa081#28 = #agifa322#3;        || ...y el cambio
             |LEE 001#agifa322.=;   || Leo las lineas de Proveed/Divisas
             nfecha = fechasis - #agifa322#6;  || Resto la fecha de fin de periodo a la actual
             |SI nfecha > 0;  || Si la fecha actual supera a la del periodo asignado ...
                |MENAV "0000ATENCION deberia Actualizar primero la divisa en Proveedores/Divisas";
                prov_divisa = #agifa081#4;  || Le envio el cod. de proveedor a "agifa323"
                |SUB_EJECUTA_NP "agifa323.tab";  || Ejecuto Proveedores/Divisas
                |ERROR;
             |SINO;
                div_act = 1;  || Para salir del PARA
             |FINSI;
           |SIGUE;
         |SINO;  || Si no existe un periodo no finalizado para esa divisa
           |MENAV "0000No existe un periodo activo para la divisa en Proveedores / Divisas";
           prov_divisa = #agifa081#4;  || Le paso a "agifa323" el proveedor
           |SUB_EJECUTA_NP "agifa323.tab";  || Ejecuto Proveedores / Divisas
           |ERROR;
         |FINSI;
         |SELECT #1#agifa322;
         |CIERRA #agifa322;
         |CIERRA #agifa323;
      |SINO; || Si el Proveedor no es de Divisa pactada ...
         |ABRE #agifa324; || ...miro directamente en divisas
         #agifa324#0 = #agifa081#27;
         |LEE 001#agifa324.=;
         |SI FSalida = 0; ||Si existe la divisa...
            #agifa081#28 = #agifa324#3; || la recojo
            |SI #agifa324#6 ! -1; || Si los dias de margen de actualizacion no
                                  || son = -1 (indefinidos)
              |PARA div_act = 0; |SI div_act = 0; |HACIENDO;
                 #agifa081#28 = #agifa324#3;        ||Cargo el valor del cambio
                 |LEE 001#agifa324.=;  || Leo la divisa
                 nfecha = fechasis - #agifa324#4;
                 |SI nfecha > #agifa324#6; |Y #agifa324#6 ! -1;  || Si la fecha actual
                    |MENAV "0000ATENCION deberia Actualizar primero la divisa";
                    divisa = #agifa081#27;
                    |SUB_EJECUTA_NP "agifa326.tab";
                    |ERROR;
                 |SINO;
                    div_act = 1;
                 |FINSI;
              |SIGUE;
            |FINSI;
         |FINSI;
         |CIERRA #agifa324;
      |FINSI;
|FPRC;

|PROCESO crea_cabecera;
     |SI monba = #2#11; ||Si Moneda base coincide con la del pre-pedido
         #6#0 = #2#4;
         |LEE 001#6=;
         |SI #6#81 ! monba; ||El prov tiene una divisa distinta a monbase
             montra = "B"; ||Moneda de trabajo Base
             divisa = #6#81;
         |SINO;
             montra = "B";
             divisa = #2#11;
         |FINSI;
     |SINO;
         montra = "D";
         divisa = #2#11;
     |FINSI;

     #6#0  = #2#4;
     |LEE 020#6=;

     enSwMenu = 1;
     #3#25 = #0#3; |HAZ ContaPC7;
     pedido = #3#0;
     serie = #0#3;

     |DDEFECTO #3;
     #3#0  = pedido;          || N.PEDIDO.
     #3#4  = #2#4;            || PROVEEDOR.
     #3#8  = #6#2;            || NOMBRE.
     #3#9  = #6#17;           || FORMA DE PAGO.
     #3#25 = serie;           || SERIE PEDIDO
     aAlfa = ("000000" + #3#0) % -106;
     #3#26 = #3#25 + "/" + aAlfa;   || REFERENCIA INTERNA.
     #3#29 = montra;          || MONEDA DE TRABAJO
     #3#27 = divisa;          || DIVISA
     #3#32 = monba;           || MONEDA BASE
     #3#33 = cambiomb;        || CAMBIO MON. BASE A EURO
     #3#5  = #6#24;           || DTO1 del proveedor
     #3#6  = #6#25;           || DTO P.P. del proveedor
     #3#17 = #6#41;           || DTO Acumulado del proveedor
     #3#43 = #0#13;           ||Curso

     #3#19 = aEnt;
     #3#18 = aDir;
     #3#20 = aPob;
     #3#21 = aPrv;

     |HAZ Param_Divisas;
     |HAZ CambioDivisa;
     |GRABA 021#3b;
|FPRC;

|PROCESO TotalLinea;
     #7#0 = #2#2;
     |LEE 020#7=;

     |SI montra = "D";                     || Si la moneda de trabajo es la divisa ...
         #4#33 = #2#6;                     || ... el precio en divisa es el precio de prepedidos
         #4#6  = #2#6 / cambio * cambiomb; || Precio en mon base
         #4#35 = #4#6;                     || Precio visualizable
     |SINO;                                || Si la moneda de trabajo es la moneda base ...
         #4#6  = #2#6;                      || ... el precio en base es el precio de prepedidos
         #4#33 = #2#6 / cambiomb * cambio; || Precio en divisa
         #4#35 = #4#33;                    || Precio visualizable
     |FINSI;

     |SI #7#196 = 2;
          #4#7    = #4#41 * #4#6;     || IMPORTE EN BASE.
          nImpDiv = (#4#41 * #4#33) ! nDeci_DivP;
     |SINO;
          #4#7    = #4#5 * #4#6;      || IMPORTE EN BASE.
          nImpDiv = (#4#5 * #4#33) ! nDeci_DivP;
     |FINSI;

     #4#8  = #2#7;             || DTO de la linea de prepedido
     #4#9  = (((#4#7 < #4#8) < #3#5) < #3#17) < #3#6;
     #4#34 = (((nImpDiv < #4#8) < #3#5) < #3#17) < #3#6;

     |SI montra = "D";  || Si la moneda de trabajo es la divisa ...
          #4#36 = #4#9;                      ||Importe Total visualizable
     |SINO;            || Si la moneda de trabajo es la moneda base ...
          #4#36 = #4#34;                     || I. Total visualizable
     |FINSI;
|FPRC;

|PROCESO DesGraba;
     #83#0 = "P";
     #83#1 = serie;
     #83#2 = pedido;
     #83#3 = linea;
     #83#4 = #13#2;
     #83#5 = #13#3;
     |GRABA 020#83n;
|FPRC;

|DEFBUCLE DesGraba;
     #13#1;
     ;
     #2#0, #2#1, 001;
     #2#0, #2#1, 999;
     ;
     NULL, DesGraba;
|FIN;

|PROCESO crea_linea;      || CREA LAS LINEAS DEL PEDIDO DE COMPRAS...................
     |ABRE #83;
     |BUCLE DesGraba;
     |CIERRA #83;

     |DDEFECTO #4;
     #4#0 = pedido;          || N.PEDIDO.
     #4#1 = linea;           || N.LINEA.
     #4#2 = #2#2;            || ARTICULO.
     |SI #2#33 = 0;
          |HAZ ConstruDefAlma;
     |SINO;
          enAlmacen = #2#33;
     |FINSI;
     #4#3 = enAlmacen;||#15#11 ||#0#3;              || ALMACEN.
     |SI #100#105 = "S";
          #105#0 = enAlmacen
          |LEE 000#105=;
          |SI FSalida = 0;
               #4#3 = #105#2
          |FINSI;
     |FINSI;
     #4#4 = #2#3;            || DESCRIPCION.
     |SI #0#15 = "S";
          #4#5 = #2#31;
          #4#41 = #2#35;    ||uni2
     |SINO;
          #4#5 = #2#5;            || UNIDADES.
          #4#41 = #2#14;    ||uni2
     |FINSI;
     #4#11 = #4#9;            || IMPORTE PENDIENTE.
     #4#14 = #7#30;            || TIPO DE IVA.
     #4#20 = #2#4;            || PROVEEDOR.
     #4#26 = #3#9;            || FORMA DE PAGO.
     #4#28 = serie;           || SERIE PEDIDO.
     #4#39 = #2#12;
     #4#40 = #2#13;
     #4#37 = #4#5;
     #4#42 = #2#15;    ||par
     #4#43 = #2#16;    ||merma
     #4#45 = #4#41;

     #4#47 = #2#17;
     #4#48 = #2#18;
     #4#49 = #2#19;
     #4#54 = #2#20;

     |SI #2#21 = "01.01.0000";
          #4#13 = #0#1;
     |SINO;
          #4#13 = #2#21;
     |FINSI;
     |HAZ TotalLinea;
     |GRABA 021#4n;
     || ACTUALIZA LA CABECERA DEL PRE-PEDIDO.................................
     #0#0 = #2#0;
     |LEE 141#0=;
     |SI FSalida = 0;
          #0#2 = "S";
          |GRABA 020#0a;
          |LIBERA #0;
     |FINSI;

     || ACTUALIZA LA LINEA DEL PRE-PEDIDO....................................
     #2#10 = "S";
     #2#28 = #3#25;
     #2#29 = #3#0;
     |SI #0#15 = "S";
          #2#5 = #2#5 + #2#31;
     |FINSI;
     #2#31 = 0;
     |GRABA 020#2a;
     |LIBERA #2;
|FPRC;

|PROCESO AcumulaLinea;
     #7#0 = #2#2;
     |LEE 020#7=;

     |DDEFECTO #4;
     #4#28 = serie;           || SERIE PEDIDO.
     #4#0  = pedido;          || N.PEDIDO.
     #4#1  = linea;           || N.LINEA.
     |LEE 101#4=;
     #4#5  = #4#5 + #2#5;     || UNIDADES.
     #4#40 = #4#40 + #2#13;
     ||#4#41 = #4#41 + #2#14;    ||uni2
     |HAZ TotalLinea;
     #4#11 = #4#9;            || IMPORTE PENDIENTE.
     |GRABA 021#4a;
     |LIBERA #4;

     #2#10 = "S";
     |GRABA 021#2a;
     |LIBERA #2;
|FPRC;

|PROCESO Actualiza;
     |ABRE #12;    |LEE 020#12p; |CIERRA #12;

     |ABRE #9;
     #9#0 = #4#3;
     |LEE 000#9=;
     |SI FSalida ! 0; |DDEFECTO #9; |FINSI;
     |CIERRA #9;
     |ABRE #7;
     #7#0 = #4#2;
     |LEE 000#7=;
     |SI FSalida ! 0; |DDEFECTO #7; |FINSI;
     |CIERRA #7;
     |ABRE #8;
     #8#0 = #4#2;
     #8#1 = #4#3;
     #8#40 = #9#1;
     #8#41 = #7#1;
     |LEE 000#8=;
     |SI FSalida ! 0; |GRABA 020#8n; |FINSI;
     |CIERRA #8;

     |ABRE #10;
     #10#0 = #2#4;
     |LEE 121#10=;
     |SI 0 = FSalida;
          fmes1 = #0#1 % A402;
          mes = fmes1 + 42;

          #7#0 = #4#2;
          |LEE 020#7=;

          |SI #7#196 = 2;
               nImpo = (((((#4#41 * #4#6) < #4#8) < #4#27) < #4#57) < #3#5) < #3#17;
          |SINO;
               nImpo = (((((#4#5 * #4#6) < #4#8) < #4#27) < #4#57) < #3#5) < #3#17;
          |FINSI;
          |SI #12#114 = "S";          || si DTO PP a PCM
               nImpo = nImpo < #3#6;
          |FINSI;
          #10mes = #10mes + nImpo;
          #10#55 = #10#55 + nImpo;
          |GRABA 020#10a;
          |LIBERA #10;
     |FINSI;
     |CIERRA #10;

     enForma = 1;
     eaDocu = "agifa067";
     |HAZ StockEntra;
|FPRC;

|PROCESO Crealos;
     #99#0 = #2#2;
     #99#9 = #2#1;
     |LEE 000#99=;
     |SI FSalida ! 0; |O #99#5 ! "S"; |ACABA; |FINSI;

     |SI pro_ant ! #2#4; |O div_ant ! #2#11; |O fec_ant ! #2#21;
          pro_ant = #2#4;
          div_ant = #2#11;
          fec_ant = #2#21;
          |SI art ! "";
               |HAZ Actualiza;
               |HAZ CalCabAgifa081;
               |HAZ EventoPedido;
          |FINSI;
          |LIBERA #3;
          |HAZ crea_cabecera;
          linea    = 0;
          art = "";
          pre = 0;
          dto = 0;
     |FINSI;

||     |SI art ! #2#2; |O pre ! #2#6; |O dto ! #2#7; |O nAlm ! #2#33;
          art = #2#2;
          pre = #2#6;
          dto = #2#7;
          nAlm = #2#33;
          linea = linea + 1;
          |HAZ crea_linea;
||     |SINO;
||          |HAZ AcumulaLinea;
||     |FINSI;

     |SI #2#23 ! 0;
          |ABRE #25;
          |SELECT #2#25;
          #25#10 = #2#0;
          #25#11 = #2#1;
          |LEE 101#25=;
          |SI FSalida = 0;
               #25#12 = "S";
               |GRABA 020#25a;
          |FINSI;
          |CIERRA #25;
     |FINSI;
|FPRC;

|DEFBUCLE Crealos;
     #2#1;
     10;
     #0#0, 001, "N";
     #0#0, 999, "N";
     be;
     NULL, NULL, NULL, NULL, NULL, Crealos;
     #2#4, #2#11, #2#21, #2#2, #2#6, #2#7, #2#33;
   ||Prv    Div   F.Nec  Art   Pre    Dto   Alm
|FIN;

|PROCESO crea_ped; |TIPO 20;
     |SI FSalida ! "OPCION";
          |SELECT #1#0;
          |LEE 000#0=;
          |SI FSalida ! 0;
               |MENAV "0000Por favor, consulte el registro correcto.";
               |ACABA;
          |SINO;
               |PINDA #0#0;
          |FINSI;
     |FINSI;
     |HAZ T20;
|FPRC;

|PROCESO T20;
     |SI FSalida = "OPCION";
          FSalida = "Opciones";
          |ACABA;
     |FINSI;

     |HAZ ControlUsuPC;
     |SI enErr ! 0; |ACABA; |FINSI;

     |SI #0#0 = 0; |ACABA; |FINSI;

     |ABRE #100; |LEE 000#100p; |CIERRA #100;

     |SI #100#141 ! "O"; aUtil4 = "GLE"; |FINSI;

     |PARA;  |SI;  |HACIENDO;
          |MENU aUtil;
          aUtil2 = FSalida;
          |SI aUtil2 = "1";  |HAZ crear; |SAL; |FINSI;
          |SI aUtil2 = "2";  |HAZ lista; |SAL; |FINSI;
          |SI aUtil2 = "3";  |HAZ Envio; |SAL; |FINSI;
          |SI aUtil2 = "4";  |HAZ Duplica; |SAL; |FINSI;
          |SI aUtil2 < "1"; |O aUtil2 > "4";  |SAL;  |FINSI;
     |SIGUE;
|FPRC;

|PROCESO Duplica;
     aAlfa = "goprepe0;" + #0#0;
     |SUB_EJECUTA_NP aAlfa;
|FPRC;

|PROCESO gopregtm;
     |SI #0#15 = "N";
          nUni = #2#5;
          nUni2 = #2#14;
     |SINO;
          nUni = #2#31;
          nUni2 = #2#35;
     |FINSI;
     |SI nUni = 0; |ACABA; |FINSI;

     |DDEFECTO #99;
     #99#0 = #2#2;
     #99#1 = #2#3;
     #99#2 = nUni;
     #99#3 = #2#6;
     #99#4 = "";
     |SI swVic = 1;
          #99#5 = "N";
     |SINO;
          #99#5 = "S";
     |FINSI;
     #99#9 = #2#1;
     #99#12 = nUni2;
     #99#13 = #2#17;
     #99#14 = #2#18;
     #99#15 = #2#19;
     |GRABA 020#99n;
     |LIBERA #99;
|FPRC;

|DEFBUCLE gopregtm;
     #2#1;
     10;
     #0#0, 001, "N";
     #0#0, 999, "N";
     ;
     NULL,gopregtm;
|FIN;

|PROCESO ColorLin; |TIPO 5;
     #99#5 = "S";
     |LINEAS_BI_ATRI #99#5, S, D, -1;
|FPRC;

|PROCESO crear;
     |PARA;  |SI;  |HACIENDO;
          |MENU aDato;
          aDato2 = FSalida;
          |SI aDato2 = "1";  swVic = 1; |SAL; |FINSI;
          |SI aDato2 = "2";  swVic = 2; |SAL; |FINSI;
          |SI aDato2 < "1"; |O aDato2 > "2";  |SAL;  |FINSI;
     |SIGUE;

     |SI swVic = 0; |ACABA; |FINSI;

     aFic = Usuario;
     |NOME_DAT #99 aFic;
     |DELINDEX #99;
     |ABRE #99;
     |BUCLE gopregtm;
     |CIERRA #99;

     |SI swVic = 1;
          |PUSHV 0501 2380;
          |ENTLINEAL #1#99 , -4 , 4 , 23 , 1 , inf , sup;
          |POPV;
          |CONFI "0000NEs correcto";
          |SI FSalida ! 0;
               |DELINDEX #99;
               |ACABA;
          |FINSI;
     |FINSI;

     |ABRE #15;
     #15#0 = #0#3;
     #15#1 = #0#7;
     |LEE 000#15=;
     |CIERRA #15;

     |ABRE #14;
     #14#0 = #0#14;
     |LEE 000#14=;
     |SI FSalida = 0; |Y #0#14 ! 0;
          aEnt = #14#1;
          aDir = #14#2;
          aPob = #14#3;
          aPrv = #14#4;
     |SINO;
          |ABRE #16;
          #16#0 = #15#4;
          |LEE 000#16=;
          |CIERRA #16;
          aEnt = #15#29
          aDir = #15#3;
          aPob = #16#1;
          aPrv = #16#3;
     |FINSI;
     |CIERRA #14;

     |ABRE #105;
     |ABRE #0;
     |ABRE #3;
     |ABRE #4;
     |ABRE #6;
     |ABRET #7;
     |ABRET #8;
     |ABRE #99;
     |HAZ miramonba;
     pro_ant = "";
     div_ant = "";
     fec_ant = "";
     art = "";
     |BUCLE Crealos;
     |SI art ! "";
          |HAZ CalCabAgifa081;
          |HAZ Actualiza;
          |HAZ EventoPedido;
     |FINSI;
     |CIERRA #0;
     |CIERRA #3;
     |CIERRA #4;
     |CIERRA #6;
     |CIERRAT #7;
     |CIERRAT #8;
     |CIERRA #99;
     |CIERRA #105;

     |DELINDEX #99;
|FPRC;

|PROCESO CreaFicImp;
     |DBASS aAlfa;
     eaFicImpre= aAlfa + "spool/";
     aAlfa = Usuario;
     eaFicImpre = eaFicImpre + aAlfa;
     ser_ped = #3#25;
     num_ped = #3#0;
     |GRABA 000#3a;
     |CIERRA #3;
     |SUB_EJECUTA_NP "agifa100";
     |ABRE #3;
     #3#25 = ser_ped;
     #3#0  = num_ped;
     |LEE 101#3=;
|FPRC;

|PROCESO EventoPedido;
     enEvento = 2; eaTipo = "02";
     eaAsunto = "Generacion Pedido: "+ serie + "/"+ (("000000" + pedido) % -106);
     eaAsunto = eaAsunto + " del Prepedio " + (("000000" + #0#0) % -106);
     eaOrigen = Usuario % 0810;
     eaJefe = #0#8;

     |DBASS aAlfa;
     eaFicImpre= aAlfa + "spool/";
     aAlfa = Usuario;
     eaFicImpre = eaFicImpre + aAlfa;
     ser_ped = serie;
     num_ped = pedido;
     |SUB_EJECUTA_NP "agifa100";

     eaPrograma = "goprepec" + serie + pedido;
     |HAZ EnviaEvento;
|FPRC;

|RUTINA inf;
     #99#0 = "                    ";
     #99#9 = 1;
|FRUT;

|RUTINA sup;
     #99#0 = "zzzzzzzzzzzzzzzzzzzz";
     #99#9 = 999;
|FRUT;

|PROCESO marca; |TIPO 0;
     |SI FSalida = 0;
          |SI #99#5 = "N";
               #99#5 = "S";
          |SINO;
               #99#5 = "N";
          |FINSI;
          |PINTA #99#5;
          |GRABA 020#99a;
     |FINSI;
|FPRC;

|PROCESO DesAmpli2;
     enSwi = 2;
     |PRINT;
     enSwi = 0;
|FPRC;

|DEFBUCLE DesAmpli2;
     #13#1;
     ;
     #2#0, #2#1, 001;
     #2#0, #2#1, 999;
     ;
     NULL, DesAmpli2;
|FIN;

|PROCESO DesAmpli;
     enSwi = 1;
     |PRINT;
     enSwi = 0;
|FPRC;

|DEFBUCLE DesAmpli;
     #83#1;
     ;
     "C", "     ", #2#0, #2#1, 001;
     "C", "     ", #2#0, #2#1, 999;
     ;
     NULL, DesAmpli;
|FIN;

|PROCESO goprepec;
     #8#0 = #2#2;
     #8#1 = #100#22;
     |LEE 000#8=;
     |SI FSalida ! 0; |DDEFECTO #8; |FINSI;
     enStComp = #8#5;

     #8#1 = #100#51;
     |LEE 000#8=;
     |SI FSalida ! 0; |DDEFECTO #8; |FINSI;
     enStDep = #8#5;

     swim = 1;
     |PRINT;

     |BUCLE DesAmpli;
     |SI nSwImpreDA = 0;
          |BUCLE DesAmpli2;
     |FINSI;
|FPRC;

|DEFBUCLE goprepec;
     #2#1;
     ;
     #0#0,001;
     #0#0,999;
     ;
     NULL,goprepec;
|FIN;

|PROCESO go000001;
     |SI enSwi = 0;
          enSwi = 3;
          #10#0 = #2#4;
          |LEE 020#10=;
          |PRINT;
     |SINO;
          aTraba = #17#2;
          |QBT aTraba;
          enSwi = 4;
          |SI aTraba ! "";
              |PRINT;
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE go000001;
     #17#1;
     ;
     #2#4, #2#0, 001;
     #2#4, #2#0, 999;
     ;
     NULL, go000001;
|FIN;

|PROCESO goprepec_Ord;
     enSwi = 0;
     |SI aProveAnt = ""; |O aProveAnt ! #2#4;
          |BUCLE go000001;
          aProveAnt = #2#4;
     |FINSI;
     enSwi = 0;
     |HAZ goprepec;
|FPRC;

|DEFBUCLE goprepec_Ord;
     #2#1;
     ;
     #0#0,001;
     #0#0,999;
     ;
     NULL, NULL, NULL, NULL, NULL, goprepec_Ord;
     #2#4;
|FIN;

|PROCESO lista;
     |HAZ ObsDerp;

     |HORA eaHora;

     |CONFI "0000NImprimir Descripcion Ampliada";
     nSwImpreDA = FSalida;
     |CONFI "0000NImprimir Descripcion Proveedor";
     nSwImprePrv = FSalida;

     |IMPRE 0;
     |SI FSalida ! 0; |ACABA; |FINSI;
     |INFOR "goprepec";
     |SI FSalida ! 0; |FINIMP; |ACABA; |FINSI;
     |ABRE #8;
     |ABRE #10;
     |SI nSwImprePrv = 0;
          aProveAnt = "";
          |BUCLE goprepec_Ord;
     |SINO;
          |BUCLE goprepec;
     |FINSI;
     |CIERRA #8;
     |CIERRA #10;
     |SI swim = 1;
          |PIEINF;
     |FINSI;
     |FININF;
     |FINIMP;
     swim = 0;
|FPRC;
컴컴컴컴컴컴컴컴컴컴컴컴컴

|PROCESO Expedte;
   nCalc = #gopre001#1;
   |SI #goparame#52 = "S"; |Y #goparame#55 = "S";
      nCalc = nCalc - #goparame#54;
   |FINSI;
   enModo = 1; eaTipoD = "C";
   |MENSA "2400DUPLICANDO PRESUPUESTO...";
   aAlfa = "gopredup" + &59 + #gopre001#0 + (("000000" + nCalc) % -106);
   enCambiArti = 1;      || Cambia articulos por los de compras
   |SUB_EJECUTA_NP aAlfa;
   |BLIN 24;
|FPRC;

|DEFBUCLE Expedte;
#gopre001#4;
;
#goprepec#3, #goprepec#7;
#goprepec#3, #goprepec#7;
;
NULL,Expedte;
|FIN;

|PROCESO Envio;
     aTraba = #0#3;
     |QBT aTraba;
     |SI #0#7 = 0; |Y aTraba = "";
         |ACABA;
     |FINSI;
     aTraba = #0#10;
     |QBT aTraba;
     |SI aTraba = "";
         |ACABA;
     |FINSI;
     |PUSHV 0501 2380;
     |BLANCO 06061562;
     |ATRI I;
     |PINTA 0721, " NOTIFICACION POR CORREO ";
     |ATRI 0;
     |CUADRO 06061562;

     aEnv1 = "1015";
     aEnv2 = "1";
     aEnv3 = "3";
     |MENUG aEnv;
     aEnv2 = FSalida;
     |SI aEnv2 > 0;
          |SI aEnv2 = 1;
               |SI #goparame#52 = "S";
                  |SI #goparame#57 = "S"; |BUCLE Expedte; |FINSI;
               |FINSI;
               enEvento = 9; eaTipo = "09";
               eaAsunto = "Prepedido N:" + (("000000" + #0#0)%-106) + " del expediente "
               eaAsunto = eaAsunto +  #0#3 + "/" + (("000000" + #0#7) % -106);
               eaAsunto = eaAsunto + " Pdte Revision Precios";
               eaOrigen = #0#10;
               #0#11 = "S";
          |FINSI;
          |SI aEnv2 = 2;
               enEvento = 10; eaTipo = "10";
               eaAsunto = "Incidencias en Prepedido N:" + #0#0 + " del expediente "
               eaAsunto = eaAsunto +  #0#3 + "/" + (("000000" + #0#7) % -106);
               eaOrigen = #0#10;
          |FINSI;
          |SI aEnv2 = 3;
               enEvento = 11; eaTipo = "11";
               eaAsunto = "Prepedido N:" + #0#0 + " del expediente "
               eaAsunto = eaAsunto +  #0#3 + "/" + (("000000" + #0#7) % -106);
               eaAsunto = eaAsunto +  " revisado y conforme";
               eaOrigen = #0#10;
          |FINSI;
          #0#12 = enEvento;
          |GRABA 020#0a;
          eaJefe = #0#8;
          eaPrograma = "goprepec" + "xxxxx" + #0#0;
          |HAZ FicEnvio;
          |HAZ EnviaEvento;
     |FINSI;
     |POPV;
     |PINTA #0#12;
|FPRC;


|PROCESO EnvioCorreos; |TIPO 3;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 2;
        enEvento = 5; eaTipo = "05";
        eaAsunto = "Modificacion Pre-pedido nro. " + (("000000"+#0#0)%-106);
        eaOrigen = Usuario % 0810;
        eaJefe = #0#8;
        eaPrograma = "goprepec" + "xxxxx" + #0#0;
        |HAZ FicEnvio;
        |HAZ EnviaEvento;
     |FINSI;
|FPRC;

|PROCESO EventoResAlta;
     enEvento = 12; eaTipo = "12";
     eaAsunto = "Alta Deposito Expediente: "+ #0#3 + "/"+ (("000000" + #0#7) % -106);
     eaAsunto = eaAsunto + " Articulo: " + #2#2 + " " + #2#3;
     eaAsunto = eaAsunto + " Cantidad: " + #2#23;
     eaOrigen = Usuario % 0810;
     eaJefe = #0#8;
     eaPrograma = "goprepec" + "xxxxx" + #0#0;
     |HAZ FicEnvio;
     |HAZ EnviaEvento;
|FPRC;

|PROCESO EventoResBaja;
     enEvento = 13; eaTipo = "13";
     eaAsunto = "Baja Deposito Expediente: "+ #0#3 + "/"+ (("000000" + #0#7) % -106);
     eaAsunto = eaAsunto + " Articulo: " + #2#2 + " " + #2#3;
     eaAsunto = eaAsunto + " Cantidad: " + #2#23;
     eaOrigen = Usuario % 0810;
     eaJefe = #0#8;
     eaPrograma = "goprepec" + "xxxxx" + #0#0;
     |HAZ FicEnvio;
     |HAZ EnviaEvento;
|FPRC;

|PROCESO CargaUsu; |TIPO 0;
     |ABRE #101;
     #101#0 = #0#8;
     |LEE 000#101=;
     |CIERRA #101;
     #0#10 = #101#27;
     |PINTA #0#10;
|FPRC;
컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
|PROCESO Tipo8; |TIPO 8;
     |DDEF aDef19;
     aDef19 = aDef19 + "agifa019";
     |CARGA_DEF aDef19, agifa019@;
     |SI FSalida ! 0;
          aDef19 = "";
     |SINO;
          aAlfa = Usuario;
          |QBF aAlfa;
          aAlfa = "_" + aAlfa;
          |NOME_DAT #19 aAlfa;
          |DELINDEX #19;
     |FINSI;
     |HAZ PideAlmaCompra;
|FPRC;

|PROCESO Tipo9; |TIPO 9;
     |SI aDef19 ! "";
          |DELINDEX #19;
          |DESCARGA_DEF #agifa019@;
     |FINSI;
|FPRC;

|PROCESO agifa019;
     |SI #100#6 = #7#126; |O #100#7 = #7#126;
          |PARA i = 0; |SI i [ nCampo; |HACIENDO i = i + 1;
               #19i = #7i;
          |SIGUE;
          |GRABA 020#19n;
     |FINSI;
|FPRC;

|DEFBUCLE agifa019;
     #7#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, agifa019;
|FIN;

|PROCESO Tipo66; |TIPO 66;
     |ABRE #7;
     #7#0 = #2#2;
     |LEE 000#7];
     |CONSULTA_CLAVE #1#7;
     |SI FSalida = 0;
          #2#2 = #7#0;
     |FINSI;
     |CIERRA #7;
     |ERROR;
|FPRC;

|PROCESO Consulta;
     |SI nCampo = 0;
          |PUSHV 0501 2380;
          |BLANCO 1529 2051;
          |CUADRO 1529 2051;
          |CUADRO 1630 1950;
          |ATRI P; |PINTA 1733, "  CARGANDO..."; |ATRI 0;
          |ABRE #100; |LEE 000#100p; |CIERRA #100;
          aAlfa = "|NC";
          aAlfa = #7#aAlfa#;
          nCampo = aAlfa;
          nCampo = nCampo - 1;
          |ABRE #19;
          |BUCLE agifa019;
          |CIERRA #19;
          |POPV;
     |FINSI;

     |SI aDef19 ! "";
          |ABRE #19;
          #19#0 = #2#2;
          |LEE 000#19];
          |CONSULTA_CLAVE #1#19;
          |SI FSalida = 0;
               #2#2 = #19#0;
               |PINTA #2#2;
          |SINO;
               |ERROR;
          |FINSI;
          |CIERRA #19;
     |SINO;
          |ABRE #7;
          #7#0 = #2#2;
          |LEE 000#7];
          |CONSULTA_CLAVE #1#7;
          |SI FSalida = 0;
               #2#2 = #7#0;
               |PINTA #2#2;
          |SINO;
               |ERROR;
          |FINSI;
          |CIERRA #7;
     |FINSI;
|FPRC;

|PROCESO Tipo13; |TIPO 13;
     |ABRE #100; |LEE 000#100p; |CIERRA #100;
     aAlfa = #100#67; |QBF aAlfa;
     |SI aAlfa ! "";
          |C_V #0#13, 1, "S";
          |PINTA 1003, #100#67;
     |FINSI;

     |MODULO aAlfa;
     |SI aAlfa = "goprepec"; aAlfa = "N"; |SINO; aAlfa = "S"; |FINSI;
     |SI aAlfa = "S";
          |ATRI S; |PINTA 566,  "MANTE. PARCIAL"; |ATRI 0;
     |SINO;
          |ATRI S; |PINTA 566,  "MANTE. NORMAL "; |ATRI 0;
     |FINSI;
|FPRC;

|PROCESO goprepecImp;
     |PRINT;
|FPRC;

|PROCESO FicEnvio;
     |DBASS aAlfa;
     eaFicImpre= aAlfa + "spool/";
     aAlfa = Usuario;
     eaFicImpre = eaFicImpre + aAlfa;
     Impresora = eaFicImpre;
     |IMPRE -77;
     |SI FSalida = 0;
          |INFOR "goprepec";   ||*Correo
          |SI FSalida = 0;
               enSwi = 0;
               |BUCLELIN 2 goprepecImp #0;
               |PIEINF;
               |FININF;
          |FINSI;
          |FINIMP;
     |FINSI;
|FPRC;
