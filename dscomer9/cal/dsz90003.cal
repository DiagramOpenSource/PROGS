|VARIABLES;
     &enSwBorraCabDet = 0;
     &eaImpMotorCrystal = "";
     {3}aPuntero = "";
     aRDirBass = "";
     nSwBorra1 = 0;
     nSwBorra2 = 0;
     aBorra = "";
     nSwMotorCrystal = 0;
     aAux = "";
|FIN;

|PROCESO BorraCabDetCrystal;
     ||Primero que nada comprobamos si la impresora utiliza el motor Crystal

     nSwMotorCrystal = 0;
     |QBF eaImpMotorCrystal;

     aAux = eaImpMotorCrystal % 104;
     |SI aAux = "Fax;"; |O aAux = "PDF;"
          nSwMotorCrystal = 1;
     |FINSI;
     aAux = eaImpMotorCrystal % 107;
     |SI aAux = "Correo;";
          nSwMotorCrystal = 1;
     |FINSI;
     aAux = eaImpMotorCrystal % 113;
     |SI aAux = "CrystalReport";
          nSwMotorCrystal = 1;
     |FINSI;

     |SI nSwMotorCrystal = 0;
          ||Si no usamos el motor Crystal, no necesitamos borrar los ficheros
          ||es como si ya lo hubieramos borrado.
          enSwBorraCabDet = 1;
          |ACABA;
     |FINSI;

     nSwBorra1 = 0;
     nSwBorra2 = 0;

     |ESPECIFICA "RBASS", aPuntero;
     aRDirBass = aPuntero;
     |QBF aRDirBass;

     ||detalle.dbf
     aBorra = aRDirBass + "crystal/detalle.dbf";
     |XABRE "CE", aBorra, 0;
     |SI FSalida ] 0;
          |RFBORRA aBorra;
          |XABRE "CE", aBorra, 0;
          |SI FSalida ] 0;
               nSwBorra1 = 0;
          |SINO;
               nSwBorra1 = 1;
          |FINSI;
     |SINO;
          nSwBorra1 = 1;
     |FINSI;

     |SI nSwBorra1 = 1;
          ||cabecera.dbf
          aBorra = aRDirBass + "crystal/cabecera.dbf";
          |XABRE "CE", aBorra, 0;
          |SI FSalida ] 0;
               |RFBORRA aBorra;
               |XABRE "CE", aBorra, 0;
               |SI FSalida ] 0;
                    nSwBorra2 = 0;
               |SINO;
                    nSwBorra2 = 1;
               |FINSI;
          |SINO;
               nSwBorra2 = 1;
          |FINSI;

          |SI nSwBorra2 = 1;
               enSwBorraCabDet = 1;
          |SINO;
               enSwBorraCabDet = 0;
          |FINSI;
     |SINO;
          enSwBorraCabDet = 0;
     |FINSI;
|FPRC;
