||Da exceso de ficheros en un proceso... dsz99961:(

|FICHEROS;
     agifa065  #65;

     albm0040@  #40;     ||Rel. Orden/Pedido
     albm0060@  #60;     ||Rel. Orden/Mezclada
     albm0061@  #61;     ||Mezclad.PorConfirmar
     albm0037@  #37;     ||Orden CAB
     albm0038@  #38;     ||Orden LIN
     albm0031@  #31;     ||Mezcladas CAB
     albm0032@  #32;     ||Mezcladas LIN

     albw0044@  #44;     ||Aux. Modif.Pedidos

     agifa053 #400;  || Usado como temporal de comprobacion
     agifa049 #401;  || Escandallo lineas
     agifa142 #2000;
     refere48@;

     albm0045@ #945;     ||Contadores Partidas

     agifa048 #148;   ||Escandallos Cabs.
     albm0029@ #29;  ||Formulas Cabs. MEZCLADAS
     albm0030@ #30;  ||Formulas Lins. MEZCLADAS

     dsm00054 #54;  ||Mtos Almacenes (C)
     dsm00055 #55;  ||Mtos Almacenes (L)
|FIN;

|VARIABLES;
     &eaArt61 = "";
     &enAlm61 = 0;
     &eaPar61 = "";
     &eaSer61 = "";
     &enNum61 = 0;
     &enLin61 = 0;

     nForma = 0;

     &enAlbeAlm = 0;
     &eaTipoMov = "";
     &eaAlbePar = "";

     nValor = 0;
     nCantidad = 0;
     aArticulo = "";
     aAlmacen = "";

     nSwEsMezcla = 0;
     nSwEsEscan = 0;

     aTempo44 = "";
     &Tempo = "";

     aAlfa = "";
     aMas = "";
     aMensaje = "";

     &enSwHayOrdMez = 0;
     &enSwHayFinaliz = 0;
     &eaArtMod = "";
     &enCanMod = 0;
     &enAlmMod = 0;
     &eaSerPedido = "";
     &enNumPedido = 0;

     &enPreHis = 0;
     &enPrecio = 0;
     &eaProvAlbero = "";
     &eaArtAlbero = "";
     nPrecioBase = 0;

     nPrimero = 0;
     aFecUlt = "";
     nPreUlt = 0;
     nPreAnt = 0;
     nPrecio = 0;

     nSwVenta = 0;  ||Desglose de ventas
     nOk = 0;
     aTempo400 = "";
     ||------------------------Variables de Comunicacion
     nMaxNivel      = 100;  || Nivel maximo que desglosa
     aEscan         = "";   || Escandallo
     nCantiEscan    = 0;    || Cantidad
     nAlmaEscan     = 0;    || Almacen
     ||--------------------------------------------

 {100}E             = "";
 {100}L             = 0;
 {100}C             = 0;
 {100}U             = 0;
     nNivelEscan    = 0;
     nLinEscan      = 0;
     aFicheroTMP    = "";
     aAlfaEscan     = "";
|FIN;

|PROCESO agifa065_miraPLB;
     |SI #agifa065#14 ! nPrecioBase;
          |SI #agifa065#14 ! nPreAnt;
               nPreAnt = #agifa065#14;
               nPrecio = #agifa065#14;
          |FINSI;
     |SINO;
          |SI nPrimero = 1;
               nPreUlt = #agifa065#14;
               nPrimero = 0;
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE agifa065_miraPLB;
     #agifa065#3;
     ;
     eaProvAlbero, "AC", eaArtAlbero, "00.00.0000";
     eaProvAlbero, "AC", eaArtAlbero, "31.12.9999";
     ;
     NULL,agifa065_miraPLB;
|FIN;

|PROCESO RecorreHistorico;
     nPrecioBase = enPrecio;
     nPrimero = 1;
     nPreUlt = enPrecio;
     nPrecio = 0;
     nPreAnt = 0;
     |BUCLE agifa065_miraPLB;
     |SI nPrecio ! 0;
          enPreHis = nPrecio;
     |SINO;
          enPreHis = enPrecio;
     |FINSI;
|FPRC;

_____________________________________________________________________
|PROCESO albm0060_plb;
     #albm0031@#0 = #albm0060@#1;
     |LEE 000#albm0031@.=;
     |SI FSalida = 0; |Y #albm0031@#16 = "S";
          enSwHayFinaliz = 1;
          |ERROR10;
     |FINSI;
|FPRC;

|DEFBUCLE albm0060_plb;
 #albm0060@#1004;
 ;
 #albm0040@#4, #albm0040@#0, "     ", 000000;
 #albm0040@#4, #albm0040@#0, "zzzzz", 999999;
 ;
 NULL, albm0060_plb;
|FIN;

|PROCESO albm0040;
     enSwHayOrdMez = 1;

     #albm0037@#16 = #albm0040@#4;
     #albm0037@#0 = #albm0040@#0;
     |LEE 000#albm0037@.=;
     |SI FSalida = 0; |Y #albm0037@#15 = "S";
          enSwHayFinaliz = 1;
     |FINSI;

     |SI enSwHayFinaliz = 0;
          |BUCLE albm0060_plb;
     |SINO;
          |ERROR10;
     |FINSI;
|FPRC;

|DEFBUCLE albm0040;
 #albm0040@#1005;
 ;
 "     ", 000000, eaSerPedido, enNumPedido, 001;
 "zzzzz", 999999, eaSerPedido, enNumPedido, 999;
 ;
 NULL, albm0040;
|FIN;

|PROCESO MiraOrdenMezAsociadas;
     enSwHayOrdMez = 0;
     enSwHayFinaliz = 0;

     |DBASE aAlfa; |QBF aAlfa;

     aMas = aAlfa + "def/albm0040.mas";
     |CARGA_DEF aMas, albm0040@;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     aMas = aAlfa + "def/albm0060.mas";
     |CARGA_DEF aMas, albm0060@;
     |SI FSalida ! 0;
         |DESCARGA_DEF #albm0040@;
         |ACABA;
     |FINSI;

     aMas = aAlfa + "def/albm0037.mas";
     |CARGA_DEF aMas, albm0037@;
     |SI FSalida ! 0;
         |DESCARGA_DEF #albm0060@;
         |DESCARGA_DEF #albm0040@;
         |ACABA;
     |FINSI;

     aMas = aAlfa + "def/albm0031.mas";
     |CARGA_DEF aMas, albm0031@;
     |SI FSalida ! 0;
         |DESCARGA_DEF #albm0037@;
         |DESCARGA_DEF #albm0060@;
         |DESCARGA_DEF #albm0040@;
         |ACABA;
     |FINSI;

     |ABRE #albm0037@;
     |ABRE #albm0031@;

     |BUCLE albm0040;

     |CIERRA #albm0031@;
     |CIERRA #albm0037@;

     |DESCARGA_DEF #albm0031@;
     |DESCARGA_DEF #albm0037@;
     |DESCARGA_DEF #albm0060@;
     |DESCARGA_DEF #albm0040@;
|FPRC;

|PROCESO TempoEscan;
     ||Mezcladas por Confirmar.
     |SELECT #4#albm0061@;
     #albm0061@#3 = #albw0044@#1;
     #albm0061@#11 = #albw0044@#2;
     |LEE 101#albm0061@.=;
     |SI FSalida = 0;
          #albm0061@#13 = #albm0061@#13 + #albw0044@#3;
          |GRABA 020#albm0061@.a;
          |LIBERA #albm0061@;
     |FINSI;
|FPRC;

|DEFBUCLE TempoEscan;
 #albw0044@#1003;
 4;
 "M", "                    ", 00001, "N";
 "M", "zzzzzzzzzzzzzzzzzzzz", 99999, "N";
 ;
 NULL, TempoEscan;
|FIN;

|PROCESO albm0038_inc;
     nCantidad = 0;
     aArticulo = #albm0038@#2;
     aAlmacen = #albm0038@#4;
     |HAZ DameCantidad;
     #albm0038@#8 = #albm0038@#8 + nCantidad;
     |GRABA 020#albm0038@.a;
|FPRC;

|DEFBUCLE albm0038_inc;
 #albm0038@#1003;
 ;
 #albm0037@#16, #albm0037@#0, 01;
 #albm0037@#16, #albm0037@#0, 99;
 be;
 NULL, albm0038_inc;
|FIN;

|PROCESO albm0032;
     |SELECT #2#albm0030@;
     #albm0030@#0 = #albm0031@#3;
     #albm0030@#2 = #albm0032@#2;
     |LEE 000#albm0030@.=;
     |SI FSalida = 0;
          nValor = nCantidad % #albm0030@#4;
          #albm0032@#7 = #albm0032@#7 + nValor;
          |GRABA 020#albm0032@.a;
     |FINSI;
|FPRC;

|DEFBUCLE albm0032;
 #albm0032@#1002;
 ;
 #albm0031@#0, 01;
 #albm0031@#0, 99;
 be;
 NULL, albm0032;
|FIN;

|PROCESO albm0060_inc;
     #albm0031@#0 = #albm0060@#1;
     |LEE 101#albm0031@.=;
     |SI FSalida = 0;
          |ABRE #44;
          #44#0 = "M";
          #44#1 = #albm0031@#3;
          #44#2 = #albm0031@#11;
          |LEE 101#44=;
          |SI FSalida = 0;
               #albm0031@#13 = #albm0031@#13 + #44#3;
               #44#4 = "S";
               |GRABA 020#44a;
               |LIBERA #44;
               nCantidad = #44#3;
               |BUCLE albm0032;
          |FINSI;
          |CIERRA #44;
     |FINSI;
|FPRC;

|DEFBUCLE albm0060_inc;
 #albm0060@#1004;
 ;
 #albm0040@#4, #albm0040@#0, "     ", 000000;
 #albm0040@#4, #albm0040@#0, "zzzzz", 999999;
 ;
 NULL, albm0060_inc;
|FIN;

|PROCESO albm0040_inc;

     #albm0037@#16 = #albm0040@#4;
     #albm0037@#0 = #albm0040@#0;
     |LEE 101#albm0037@.=;
     |SI FSalida = 0;
          |BUCLE albm0038_inc;
          nCantidad = 0;
          aArticulo = #albm0037@#2;
          aAlmacen = #albm0037@#10;
          |HAZ DameCantidad;
          #albm0037@#12 = #albm0037@#12 + nCantidad;
          |GRABA 020#albm0037@.a;
          |LIBERA #albm0037@;
     |FINSI;

     ||ARA
     |BUCLE albm0060_inc;
|FPRC;

|DEFBUCLE albm0040_inc;
 #albm0040@#1005;
 ;
 "     ", 000000, eaSerPedido, enNumPedido, 001;
 "zzzzz", 999999, eaSerPedido, enNumPedido, 999;
 ;
 NULL, albm0040_inc;
|FIN;

|PROCESO ModOrdenMezcladasAlb;
     aMensaje = "0000Escandallo para " + eaArtMod + " con Dif. de unidades " + enCanMod;
     ||MENAV aMensaje;

     |DBASE aAlfa; |QBF aAlfa;
     aMas = aAlfa + "def/albw0044.mas";
     |CARGA_DEF aMas, albw0044@;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     aMas = aAlfa + "def/albm0029.mas";
     |CARGA_DEF aMas, albm0029@;
     |SI FSalida ! 0;
          |DESCARGA_DEF #albw0044@;
          |ACABA;
     |FINSI;

     aMas = aAlfa + "def/albm0040.mas";
     |CARGA_DEF aMas, albm0040@;
     |SI FSalida ! 0;
          |DESCARGA_DEF #albm0029@;
          |DESCARGA_DEF #albw0044@;
          |ACABA;
     |FINSI;

     aMas = aAlfa + "def/albm0060.mas";
     |CARGA_DEF aMas, albm0060@;
     |SI FSalida ! 0;
          |DESCARGA_DEF #albm0040@;
          |DESCARGA_DEF #albm0029@;
          |DESCARGA_DEF #albw0044@;
          |ACABA;
     |FINSI;

     aMas = aAlfa + "def/albm0037.mas";
     |CARGA_DEF aMas, albm0037@;
     |SI FSalida ! 0;
          |DESCARGA_DEF #albm0060@;
          |DESCARGA_DEF #albm0040@;
          |DESCARGA_DEF #albm0029@;
          |DESCARGA_DEF #albw0044@;
          |ACABA;
     |FINSI;

     aMas = aAlfa + "def/albm0038.mas";
     |CARGA_DEF aMas, albm0038@;
     |SI FSalida ! 0;
          |DESCARGA_DEF #albm0037@;
          |DESCARGA_DEF #albm0060@;
          |DESCARGA_DEF #albm0040@;
          |DESCARGA_DEF #albm0029@;
          |DESCARGA_DEF #albw0044@;
          |ACABA;
     |FINSI;

     aMas = aAlfa + "def/albm0031.mas";
     |CARGA_DEF aMas, albm0031@;
     |SI FSalida ! 0;
          |DESCARGA_DEF #albm0038@;
          |DESCARGA_DEF #albm0037@;
          |DESCARGA_DEF #albm0060@;
          |DESCARGA_DEF #albm0040@;
          |DESCARGA_DEF #albm0029@;
          |DESCARGA_DEF #albw0044@;
          |ACABA;
     |FINSI;

     aMas = aAlfa + "def/albm0032.mas";
     |CARGA_DEF aMas, albm0032@;
     |SI FSalida ! 0;
          |DESCARGA_DEF #albm0031@;
          |DESCARGA_DEF #albm0038@;
          |DESCARGA_DEF #albm0037@;
          |DESCARGA_DEF #albm0060@;
          |DESCARGA_DEF #albm0040@;
          |DESCARGA_DEF #albm0029@;
          |DESCARGA_DEF #albw0044@;
          |ACABA;
     |FINSI;

     aMas = aAlfa + "def/albm0030.mas";
     |CARGA_DEF aMas, albm0030@;
     |SI FSalida ! 0;
          |DESCARGA_DEF #albm0032@;
          |DESCARGA_DEF #albm0031@;
          |DESCARGA_DEF #albm0038@;
          |DESCARGA_DEF #albm0037@;
          |DESCARGA_DEF #albm0060@;
          |DESCARGA_DEF #albm0040@;
          |DESCARGA_DEF #albm0029@;
          |DESCARGA_DEF #albw0044@;
          |ACABA;
     |FINSI;

     aMas = aAlfa + "def/albm0061.mas";
     |CARGA_DEF aMas, albm0061@;
     |SI FSalida ! 0;
          |DESCARGA_DEF #albm0030@;
          |DESCARGA_DEF #albm0032@;
          |DESCARGA_DEF #albm0031@;
          |DESCARGA_DEF #albm0038@;
          |DESCARGA_DEF #albm0037@;
          |DESCARGA_DEF #albm0060@;
          |DESCARGA_DEF #albm0040@;
          |DESCARGA_DEF #albm0029@;
          |DESCARGA_DEF #albw0044@;
          |ACABA;
     |FINSI;


     |HAZ CreaTempo; aTempo44 = Tempo;
     |NOME_DAT #44 aTempo44; |DELINDEX #44;


     |ABRE #44;

     |DDEFECTO #44;
     #44#0 = "O";
     #44#1 = eaArtMod;
     #44#2 = enAlmMod;
     |LEE 101#44=;
     |SI FSalida ! 0; |GRABA 020#44b; |FINSI;
     #44#3 = enCanMod;
     |GRABA 020#44a;
     |LIBERA #44;

     aEscan = eaArtMod;
     nCantiEscan = enCanMod;

     |HAZ RestoEscandallo;

     |ABRE #albm0037@;
     |ABRE #albm0031@;
     |ABRE #albm0030@;
     |BUCLE albm0040_inc;
     |CIERRA #albm0030@;
     |CIERRA #albm0031@;
     |CIERRA #albm0037@;

     |ABRE #albm0061@;
     |BUCLE TempoEscan;            ||Faltan las mezcladas por Confirmar
     |CIERRA #albm0061@;

     ||ARA
     |CIERRA #44;
     |DELINDEX #44; Tempo = aTempo44; |HAZ BoraTempo;

     |DESCARGA_DEF #albm0061@;
     |DESCARGA_DEF #albm0030@;
     |DESCARGA_DEF #albm0032@;
     |DESCARGA_DEF #albm0031@;
     |DESCARGA_DEF #albm0038@;
     |DESCARGA_DEF #albm0037@;
     |DESCARGA_DEF #albm0060@;
     |DESCARGA_DEF #albm0040@;
     |DESCARGA_DEF #albm0029@;
     |DESCARGA_DEF #albw0044@;
|FPRC;

|PROCESO LineaEscandallo;
     nSwEsEscan = 0;
     |ABRE #agifa048;
     #agifa048#0 = aEscan;
     |LEE 000#agifa048.=;
     |SI FSalida = 0;
          nSwEsEscan = 1;
     |FINSI;
     |CIERRA #agifa048;

     nSwEsMezcla = 0;
     |ABRE #albm0029@;
     #albm0029@#0 = aEscan;
     |LEE 000#albm0029@.=;
     |SI FSalida = 0;
          nSwEsMezcla = 1;
     |FINSI;
     |CIERRA #albm0029@;

     |DDEFECTO #44;
     |SI nSwEsMezcla = 1;
          #44#0 = "M";
     |SINO;
          #44#0 = "O";
     |FINSI;

     #44#1 = aEscan;
     #44#2 = nAlmaEscan;
     |LEE 101#44=;
     |SI FSalida ! 0; |GRABA 020#44b; |FINSI;
     #44#3 = nCantiEscan;
     |GRABA 020#44a;
     |LIBERA #44;
|FPRC;


/*
||ESCANDALLO
||Copiado directamente del i_escand
    (!OJO! este proceso ABRE y CIERRA el 'agifa049')

Proceso 'DesglosaEscan'   (proceso de llamada)
     aEscan = Escandallo a desglosar
     nCantiEscan = Unidades
     nMaxNivel = Nivel maximo de desglose (no asignar para desglose completo)

Proceso 'LinEscan'        (proceso llamado por cada articulo del escandallo)
     aEscan = Articulo o escandallo
     nCantiEscan = Unidades
     nNivelEscan = Nivel del escandallo
     (debe estar definido en el calculo que llama a 'DesglosaEscan'

-----------------------------------------------------------------
*/


|PROCESO RestoEscandallo;          ||DesglosaEscan
     |DDEF aAlfaEscan;
     aAlfaEscan = aAlfaEscan + "agifa048";
     |CARGA_DEF aAlfaEscan, refere48@;
     |SI FSalida ! 0;
          |MENAV "0000ERROR al cargar 'agifa048' en desglose escandallo";
          |ACABA;
     |FINSI;
     |ABRE #refere48@;

     |ABRE #2000; |LEE 000#2000p; |CIERRA #2000;

     |HAZ CreaTempo; aTempo400 = Tempo;
     |NOME_DAT #400 aTempo400; |DELINDEX #400;
     |ABRE #400;
     |ABRE #401;

     #400#0 = aEscan;
     |GRABA 020#400n;
     IE = E1<-;
     IL = L1<-;
     IC = C1<-;
     IU = U1<-;
     nNivelEscan = 0;
     nLinEscan = 1;
 |ET OtroEscan;
     #401#0 = aEscan;
     #401#1 = nLinEscan;
     |LEE 000#401];

     |SI FSalida = 0; #agifa142#12 = "X"; |Y nSwVenta = 1;
          #refere48@#0 = #401#0;
          |LEE 000#refere48@.=;
          |SI #refere48@#33 = "N";
               nOk = 1;
          |SINO;
               nOk = 0;
          |FINSI;
          FSalida = 0;
     |SINO;
          nOk = 0;
     |FINSI;

     |SI FSalida = 0; |Y #401#0 = aEscan; |Y nNivelEscan < nMaxNivel; |Y nOk = 0;
          E = #401#0;
          L = #401#1;
          C = #401#4;
          U = nCantiEscan;
          |SI #2000#165 = "S";
               |SI #401#16 = "D";
                    C = #401#4 > #401#13;
               |SINO;
                    C = #401#4 / ((100 - #401#13) / 100) ;
               |FINSI;
          |FINSI;
          nCantiEscan = nCantiEscan * C;
          nLinEscan = 1;
          aEscan = #401#2;
          nAlmaEscan = #401#9;
          nNivelEscan = nNivelEscan + 1;
          IE = IE + 1;
          IL = IL + 1;
          IC = IC + 1;
          IU = IU + 1;
          ||---------------------------------
          |HAZ LineaEscandallo;
          ||---------------------------------
          #400#0 = #401#2;
          |GRABA 000#400n;
          |SI FSalida = 100;
               aAlfaEscan = "0000ESCANDALLO INCORRECTO...";
               aAlfaEscan = aAlfaEscan + &13 + "Escandallo:" + #401#0;
               aAlfaEscan = aAlfaEscan + &13 + "Componente:" + #401#2;
               |MENAV aAlfaEscan;
          |SINO;
               |VETE OtroEscan;
          |FINSI;
     |SINO;
          #400#0 = aEscan;
          |BORRA 020#400c;
          |SI nNivelEscan > 0;
               IE = IE - 1;
               IL = IL - 1;
               IC = IC - 1;
               IU = IU - 1;
               |SI C = 0;
                    nCantiEscan = U;
               |SINO;
                    nCantiEscan = nCantiEscan / C;
               |FINSI;

               nNivelEscan = nNivelEscan - 1;
               nLinEscan = L + 1;                || Siguiente Linea
               aEscan = E;
               |VETE OtroEscan;
          |FINSI;
     |FINSI;
     |CIERRA #401;
||--------------------------- Borra Tmp;
     |CIERRA #400;
     |DELINDEX #400; Tempo = aTempo400; |HAZ BoraTempo;

     |CIERRA #refere48@;
     |DESCARGA_DEF #refere48@;
|FPRC;


|PROCESO DameCantidad;
     |ABRE #44;
     |DDEFECTO #44;

     #44#0 = "O";
     #44#1 = aArticulo;
     #44#2 = aAlmacen;
     |LEE 000#44=;
     |SI FSalida = 0;
          nCantidad = #44#3;
     |SINO;
          #44#0 = "M";
          #44#1 = aArticulo;
          #44#2 = aAlmacen;
          |LEE 000#44=;
          |SI FSalida = 0;
               nCantidad = #44#3;
          |FINSI;
     |FINSI;
     |CIERRA #44;
|FPRC;

|PROCESO AlbeQuitaPartida;
/*
     |DBASE aAlfa; |QBF aAlfa;
     aAlfa = aAlfa + "def/albm0045.mas";
     |CARGA_DEF aAlfa, albm0045@;
     |SI FSalida = 0;
          |ABRE #albm0045@;
          #albm0045@#0 = enAlbeAlm;
          #albm0045@#1 = eaTipoMov;
          |LEE 101#albm0045@.=;
          |SI FSalida = 0; |Y #albm0045@#2 = eaAlbePar;
               |SI #albm0045@#2 = #albm0045@#2 - 1;
                    |GRABA 020#albm0045@;
               |FINSI;
               |LIBERA #albm0045@;
          |FINSI;
          |CIERRA #albm0045@;
          |DESCARGA_DEF #albm0045@;
     |FINSI;
*/
|FPRC;


|PROCESO AlberoActPartidasMS61;
     eaArt61 = #dsm00055#0;
     enAlm61 = #dsm00055#1;
     eaPar61 = #dsm00055#16;
     eaSer61 = #dsm00055#28;
     enNum61 = #dsm00055#27;
     enLin61 = #dsm00055#11;

     |HAZ AlberoActPartidasMS;
|FPRC;

|PROCESO AlberoActPartidasME61;
     eaArt61 = #dsm00055#0;
     enAlm61 = #dsm00055#5;
     eaPar61 = #dsm00055#17;
     eaSer61 = #dsm00055#28;
     enNum61 = #dsm00055#27;
     enLin61 = #dsm00055#11;

     |HAZ AlberoActPartidasME;
|FPRC;
