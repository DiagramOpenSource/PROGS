  ImportaImos

|FICHEROS;
     tagm0092 #0;
     tagm0093 #1,MANTE;
     agifa019 #19;
     agifa024 #24;
     tagw0045 #45;  || Tmp Articulos
     tagw0046 #46;  || Tmp csv
     tagm0094 #94;
     dsm00238 #238;
     agifa142 #142;
|FIN;

|VARIABLES;
     aDsDestino = "";
     &Tempo = "";
     {1}aLocal = "";
     {1}aContiene = "";
     aAlfa = "";
     nHandle = 0;
     aIP = "";
     aLon = "";
     nPos = 0;
     aCar = "";
     aPath = "";
     aBat = "";
     aDat = "";
     aFin = "";
     i = 0;
     aOrg = "";
     aDes = "";
     aMd5Org = "";
     aMd5Des = "";
     nCampo = 0;
     aReg = "";
     aDato = "";
     aLetra = "";
     aTab = "";
 {99}Valor = "";
     nLin = 0;
     nPrecio = 0;
     aUsu = "";
     aCli = "";
     aFecha = "";
     aDia = "";
     aMes = "";
     aA¤o = "";
     aTmp45 = "";
     aTmp46 = "";
     nImpo = 0;
|FIN;

|PROCESO BorraLineas1;
     |BORRA 020#1a;
|FPRC;

|DEFBUCLE BorraLineas1;
     #1#1;
     ;
     #0#0, 001;
     #0#0, 999;
     be;
     NULL, BorraLineas1;
|FIN;

|PROCESO UltimaLin1;
     #1#0 = #0#0;
     #1#1 = 999;
     |LEE 000#1];
     |SI FSalida = 0;
          |LEE 000#1a;
     |SINO;
          |LEE 000#1u;
     |FINSI;
     |SI FSalida = 0; |Y #1#0 = #0#0;
          nLin = #1#1 + 1;
     |SINO;
          nLin = 1;
     |FINSI;
     nLin = nLin + #142#236 - 1;
     nPrecio = 0;
|FPRC;

|PROCESO Precio1;
     |SI #24#39 = 1; nPrecio = #19#18; |FINSI;
     |SI #24#39 = 2; nPrecio = #19#19; |FINSI;
     |SI #24#39 = 3; nPrecio = #19#20; |FINSI;
     |SI #24#39 = 4; nPrecio = #19#147; |FINSI;
     |SI #24#39 = 5; nPrecio = #19#148; |FINSI;
     |SI #24#39 = 6; nPrecio = #19#149; |FINSI;
|FPRC;

|PROCESO TmpArti;
     |HAZ UltimaLin1;
     |DDEFECTO #1;
     #1#0 = #0#0;
     #1#1 = nLin;
     #1#2 = #45#0;

     #19#0 = #45#0;
     |LEE 000#19=;
     |SI FSalida = 0;
          #1#3 = #19#1;
          |HAZ Precio1;
     |FINSI;

     #1#4 = #45#1;
     #1#5 = nPrecio;
     #1#7 = 1;
     #1#8 = #24#204;

     #238#0 = #19#201;
     |LEE 000#238=;
     |SI FSalida ! 0; |DDEFECTO #238; |FINSI;
     #1#10 = #238#9;
     #1#11 = #238#10;
     #1#12 = #238#11;

     |SI #238#8 = 1; #1#13 = #1#4 * #1#10; |FINSI;
     |SI #238#8 = 2; #1#13 = #1#4 * #1#10 * #1#11; |FINSI;
     |SI #238#8 = 3; #1#13 = #1#4 * #1#10 * #1#11 * #1#12; |FINSI;

     |SI #238#6 = 1;
          nImpo = #1#4 * #1#5;
     |SINO;
          nImpo = #1#4 * #1#13;
     |FINSI;
     nImpo = nImpo - (nImpo * #1#8 / 100);
     nImpo = nImpo - (nImpo * #1#9 / 100);
     #1#6 = nImpo;
     |GRABA 020#1n;
|FPRC;

|DEFBUCLE TmpArti;
     #45#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, TmpArti;
|FIN;

1  ORDERID
2  DateCreate
3  CUSTOMER
4  EMPLOYEE
5  TYP
6  BOM_FLAG
7  PURCH_ID
8  NAME
9  NAME2
10 CLENGTH
11 CWIDTH
12 CNC_BARCODE1
13 CNC_BARCODE2
14 INFO1
15 INFO2
16 INFO3
17 INFO4
18 MATID
19 PARENTID
20 EDGE_ID
21 ID
22 PROJECT
23 CNT

|PROCESO ProcesaDato;
     |SI Valor1 = "ORDERID";
          |ACABA;
     |FINSI;

     |SI Valor5 = 1;
          aFecha = Valor2;
          aCli = Valor3;
          aUsu = Valor4;

          aDia = aFecha % 102; |QBT aDia;
          aMes = aFecha % 402; |QBT aMes;
          aA¤o = aFecha % 704; |QBT aA¤o;
          aFecha = (("00"+aDia)%-102)+"."+(("00"+aMes)%-102)+"."+(("0000"+aA¤o)%-104);

          |QBT aCli;
          aCli = ("00000" + aCli) % -106;

          #24#0 = aCli;
          |LEE 000#24=;
          |SI FSalida ! 0; |DDEFECTO #24; |FINSI;

          #0#1 = aFecha;
          #0#2 = aCli;
          #0#3 = #24#25;
          #0#4 = #24#20;
          #0#5 = #24#37;
          #0#6 = #24#38;
          #0#7 = #24#156;
          #0#8 = #24#202;
          #0#13 = "S"; ||Pdte Opti
     |FINSI;

     |SI Valor5 = 8; |Y Valor6 = 1;
          |DDEFECTO #45;
          #45#0 = Valor7;
          |LEE 101#45=;
          |SI FSalida ! 0; |GRABA 020#45b; |FINSI;
          #45#1 = #45#1 + Valor23;
          |GRABA 020#45a;
          |LIBERA #45;
     |FINSI;

     || el csv
     |SI Valor5 = 3;
          |DDEFECTO #46;
          #46#0 = Valor21;   || ID
          #46#22 = Valor1;   || ORDERID
          |LEE 101#46=;
          |SI FSalida ! 0; |GRABA 020#46b; |FINSI;
          #46#1 = Valor8;
          #46#2 = Valor9;
          #46#3 = Valor10;
          #46#4 = Valor11;
          #46#5 = Valor12;
          #46#6 = Valor13;
          #46#7 = Valor14;
          #46#8 = Valor15;
          #46#9 = Valor16;
          #46#10 = Valor17;
          #46#11 = Valor18;
          #46#20 = Valor22;
          #46#21 = Valor23;
          #46#23 = Valor3;
          |GRABA 020#46a;
          |LIBERA #46;
     |FINSI;
     |SI Valor5 = 7; |Y Valor8 ! "";
          |DDEFECTO #46;
          #46#0 = Valor19;    || PARENTID
          #46#22 = Valor1;    || ORDERID
          |LEE 101#46=;
          |SI FSalida = 0;
               |SI Valor20 = 1; #46#12 = Valor8; |FINSI;
               |SI Valor20 = 2; #46#13 = Valor8; |FINSI;
               |SI Valor20 = 3; #46#14 = Valor8; |FINSI;
               |SI Valor20 = 4; #46#15 = Valor8; |FINSI;
               |SI Valor20 = 5; #46#16 = Valor8; |FINSI;
               |SI Valor20 = 6; #46#17 = Valor8; |FINSI;
               |SI Valor20 = 7; #46#18 = Valor8; |FINSI;
               |SI Valor20 = 8; #46#19 = Valor8; |FINSI;
               |GRABA 020#46a;
               |LIBERA #46;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Dato;
     nCampo = 1;
     IValor = Valor1<-;
     aLon = aDato % A0;
     aReg = "";
     |PARA i = 1; |SI i [ aLon; |HACIENDO i = i + 1;
          nPos = i * 100 + 1;
          aLetra = aDato % nPos;
          |SI aLetra = aTab;
               Valor = aReg;
               aReg = "";
               nCampo = nCampo + 1;
               IValor = IValor + 1;
          |SINO;
               aReg = aReg + aLetra;
          |FINSI;
          |SI nCampo > 90; |SAL; |FINSI;     || Por si.... :)
     |SIGUE;
     Valor = aReg;
|FPRC;

|PROCESO CopiaExe;
     |DBASE aAlfa;
     aOrg = aAlfa + "plugins/sqlserver.exe";
     aDes = aPath + aDsDestino + "/sqlserver.exe";
     aContiene = aOrg;
     |ESPECIFICA "MD5", aContiene;
     aMd5Org = FSalida;
     aContiene = aDes;
     |SI aIP = "";
          |ESPECIFICA "MD5", aContiene;
     |SINO;
          |ESPECIFICA "REMOTOMD5", aContiene;
     |FINSI;
     aMd5Des = FSalida;
     |SI aMd5Org ! aMd5Des;
          |SI aIP = "";
               |COPIA_FICHERO aOrg, aDes;
          |SINO;
               |ENVIA_FICHERO aOrg, aDes;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO CheqDat;
     |XABRE "C", aDat, nHandle;
     |SI FSalida ] 0;
          |XLEE nHandle, 250, aAlfa;
          |XCIERRA nHandle;
     |FINSI;
     aAlfa = aAlfa % 107;
     |SI aAlfa = "ORDERID";
          FSalida = "0";
     |SINO;
          FSalida = "1";
     |FINSI;
|FPRC;

|PROCESO CambiaBarra;
     aAlfa = "";
     aLon = aPath % A0;
     |PARA i = 1; |SI i [ aLon; |HACIENDO i = i + 1;
          nPos = i * 100 + 1;
          aCar = aPath % nPos;
          |SI aCar = "/"; aCar = "\"; |FINSI;
          aAlfa = aAlfa + aCar;
     |SIGUE;
     aPath = aAlfa;
|FPRC;

|PROCESO CreaBat;
     |XABRE "CWB", aBat, nHandle;
     aAlfa = "@echo off" + aFin;
     |XGRABA nHandle, aAlfa;

     aAlfa = "sqlserver " + #94#2; |QBF aAlfa;
     |XGRABA nHandle, aAlfa;

     aAlfa = " " + #94#5; |QBF aAlfa;
     |XGRABA nHandle, aAlfa;

     aAlfa = " " + #94#6; |QBF aAlfa;
     |XGRABA nHandle, aAlfa;

     aAlfa = " " + #94#3; |QBF aAlfa;
     |XGRABA nHandle, aAlfa;

     aAlfa = " " + &34 + "SELECT ORDERID, DateCreate, CUSTOMER, EMPLOYEE, TYP, BOM_FLAG, PURCH_ID,";
     |XGRABA nHandle, aAlfa;
     aAlfa = " NAME, NAME2, CLENGTH, CWIDTH, CNC_BARCODE1, CNC_BARCODE2, INFO1, INFO2, INFO3, INFO4, MATID, PARENTID";
     aAlfa = aAlfa + ", EDGE_ID, ID, PROJECT, CNT";
     |XGRABA nHandle, aAlfa;

     aAlfa = " FROM " + #94#4; |QBF aAlfa;
     |XGRABA nHandle, aAlfa;

     aAlfa = " WHERE PROJECT='" + #0#0; |QBF aAlfa;
     aAlfa = aAlfa + "' ORDER BY TYP" + &34;
     |XGRABA nHandle, aAlfa;

     aAlfa = " >" + aDat + aFin;
     |XGRABA nHandle, aAlfa;

     aAlfa = "exit";
     |XGRABA nHandle, aAlfa;

     |XCIERRA nHandle;
|FPRC;

|PROCESO ImportaImos;
     |HAZ DecimalesBase;
     |ABRE #94; |LEE 000#94p; |CIERRA #94;
     |ABRE #142; |LEE 000#142p; |CIERRA #142;
     aTab = &9;
     aFin = "" + &13 + &10;

     |QBF aIP;
     |IP_REMOTO aIP;
     |SI aIP = "";
          |DBASS aPath;
     |SINO;
          |ESPECIFICA "RBASS", aLocal;
          aPath = aLocal;
     |FINSI;

     aDsDestino = "tmp"; |HAZ CopiaExe;
     aDsDestino = "bin"; |HAZ CopiaExe;

     |HAZ CambiaBarra;
     aBat = aPath + "tmp\sql" + Usuario + ".bat";
     aDat = aPath + "tmp\dat" + Usuario + ".txt";

     |HAZ CreaBat;

     aBat  = "cmd " + &34 + "/c START /MIN /WAIT " + aBat + &34;
     |SI aIP = "";
          |SYSTEM aBat;
     |SINO;
          |RSYSTEM aBat;
     |FINSI;

     |HAZ CheqDat;
     |SI FSalida ! 0;
          |MENAV "0000Error en la comunicac¢n con el servidor...";
          FSalida = "1";
     |SINO;
          |HAZ CreaTempo; |NOME_DAT #45 Tempo; |DELINDEX #45; aTmp45 = Tempo;
          |HAZ CreaTempo; |NOME_DAT #46 Tempo; |DELINDEX #46; aTmp46 = Tempo;
          |ABRE #46;
          |ABRE #45;
          |ABRE #1;
          |ABRE #19;
          |ABRE #24;
          |ABRE #24;
          |ABRE #238;
          |XABRE "C", aDat, nHandle;
          |SI FSalida ] 0;
               |XLEE nHandle, 250, aDato;
               |PARA; |SI FSalida > 0; |HACIENDO;
                    |HAZ Dato;
                    |HAZ ProcesaDato;
                    |XLEE nHandle, 250, aDato;
               |SIGUE;
               |XCIERRA nHandle;
          |FINSI;

          |BUCLE BorraLineas1;
          nLin = 0;
          |BUCLE TmpArti;
          |CIERRA #238;
          |CIERRA #24;
          |CIERRA #19;
          |CIERRA #1;
          |CIERRA #45;
          |CIERRA #46;

          |HAZ ExportaCSV;

          Tempo = aTmp45; |DELINDEX #45; |HAZ BoraTempo;
          Tempo = aTmp46; |DELINDEX #46; |HAZ BoraTempo;

          |PTEC 815;
          FSalida = 0;
     |FINSI;
|FPRC;
