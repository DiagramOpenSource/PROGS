!|FICHEROS;
     crdom002 #0;             || C
     crdom003 #1,MANTE;       || L
     agifa029 #29;
     agifa024 #24;
     referen@;
     crdoz001 #10,MANTE;
|FIN;

|VARIABLES;
     nModoEntra = 0;
     &enCtrFin = 0;
     nId = 0;
     nPanta = 0;

     aAlfa = "";
     nGrid1 = -1;
     nRango = 0;
     nModo = 0;
     nHay = 0;
     nForma = 0;
     i = 0;
     nLin = 0;
     aSocio = "";
     nAlma = 0;
|FIN;

|PROCESO Evento;
/*

     |SI FSalida = 1; |O FSalida = 2;
          |SELECT #1#1;
          |LEE 000#1=;
     |FINSI;
*/
|FPRC;

|PROCESO Min;
     #1#0 = #0#0;
     #1#1 = #0#9;
     #1#2 = 001;
|FPRC;

|PROCESO Max;
     #1#0 = #0#0;
     #1#1 = #0#9;
     #1#2 = 999;
|FPRC;

|PROCESO T14; |TIPO 14;    ||(*)
     |SI nGrid1 = -1;
          nRango = 1 + 4 + 8 + 16 + 32;
          |LINEAL_SIMPLE #1#1, nRango, 2102, 2998, Min, Max, Evento;
          nGrid1 = FSalida;
     |SINO;
          |REFRESCACONTROL nGrid1;
     |FINSI;
|FPRC;

|PROCESO T8; |TIPO 8; ||(*)
     |ABRET #1;
|FPRC;

|PROCESO T9; |TIPO 9; ||(*)
     |CIERRAT #1;
|FPRC;

|PROCESO BorraLin;
     |BORRA 020#1a;
     |LIBERA #1;
|FPRC;

|DEFBUCLE BorraLin;
     #1#1;
     ;
     #0#0, #0#9, 001;
     #0#0, #0#9, 999;
     be;
     NULL, BorraLin;
|FIN;

|PROCESO T2; |TIPO 2; ||(*)
     nModo = (FEntrada / 100) ! 0;
     nForma = 0 +. 1;
     |SI nModo = 3;
          |BUCLE BorraLin;
     |FINSI;
|FPRC;

|PROCESO T0; |TIPO 0;     ||(*)
     |REFRESCACONTROL nGrid1;
|FPRC

|PROCESO T12; |TIPO 12;    ||(*)
     |SI nModoEntra = 1; |Y FSalida = 7;
          |BUCLE BorraLin;
     |FINSI;
|FPRC;

|PROCESO T30; |TIPO 30;    ||(*)
     nModo = (FEntrada / 100) ! 0;
     |ABRE #1;
     #1#0 = #0#0;
     #1#1 = #0#9;
     #1#2 = 1;
     |LEE 000#1];
     |SI FSalida ! 0; |O #1#0 ! #0#0; |O #1#1 ! #0#9;
          |SI nModo = 1;
               |MENAV "0000Se anulan los datos entrados.";
          |SINO;
               |MENAV "0000No existen lineas, se borra la cabecera.";
          |FINSI;
          |BORRA 020#0a;
          |ERROR;
     |FINSI;
     |CIERRA #1;
|FPRC;

|PROCESO T13; |TIPO 13;
     |PINPA #0#0; nPanta = FSalida;

     |ABRE #29;
     #29#0 = #0#9;
     |LEE 000#29=;
     |SI FSalida ! 0; |DDEFECTO #29; |FINSI;
     |CIERRA #29;
     |PINTA 1521, #29#1;
|FPRC;

|PROCESO T1_7; |TIPO 7;
     nModoEntra = (FEntrada / 100) ! 0;
     |SI nModoEntra = 1;
          |BUCLE BorraLin;
          |REFRESCACONTROL nGrid1;
          aAlfa  = "|X000"; aAlfa = #0aAlfa; nId = aAlfa;
          |ESTADO_CONTROL nId, "DISABLE";
     |FINSI;
|FPRC;

|PROCESO DefeLin; |TIPO 7;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 1;
          #1#0 = #0#0;
          #1#1 = #0#9;
          #1#2 = 999;
          |LEE 000#1];
          |SI FSalida = 0;
               |LEE 000#1a;
          |SINO;
               |LEE 000#1u;
          |FINSI;
          |SI FSalida = 0; |Y #1#0 = #0#0; |Y #1#1 = #0#9;
               nLin = #1#2 + 1;
          |SINO;
               nLin = 1;
          |FINSI;
          |DDEFECTO #1;
          #1#0 = #0#0;
          #1#1 = #0#9;
          #1#2 = nLin;
     |FINSI;
|FPRC;

|PROCESO Lin; |TIPO 0;
     aAlfa  = "|X000"; aAlfa = #0aAlfa; nId = aAlfa;
     |ESTADO_CONTROL nId, "ENABLE";
|FPRC;

|PROCESO Socio; |TIPO 6;
     nModo = (FEntrada / 100) ! 0;
     |SI FSalida = 13;
          |ABRE #24;
          #24#0 = #0#0;
          |LEE 000#24];
          |CONSULTA_CLAVE #1#24;
          |SI FSalida = 0;
               #0#0 = #24#0; |PINTA #0#0;
               |SI nModo = 1;
                    #0#1 = #24#1; |PINTA #0#1;
                    #0#2 = #24#5; |PINTA #0#2;
                    #0#3 = #24#6; |PINTA #0#3;
                    #0#4 = #24#178; |PINTA #0#4;
                    #0#5 = #24#179; |PINTA #0#5;
                    #0#6 = #24#180;
                    #0#7 = #24#17; |PINTA #0#7;
                    #0#8 = #24#18; |PINTA #0#8;
               |FINSI;
          |SINO;
               |ERROR;
          |FINSI;
          |CIERRA #24;
     |SINO;
          |SI nModo = 1;
               |ABRE #24;
               #24#0 = #0#0;
               |LEE 000#24=;
               |SI FSalida = 0;
                    #0#1 = #24#1; |PINTA #0#1;
               |SINO;
                    aAlfa = "*dszx0001;C" + #0#0;
                    |SUB_EJECUTA_NP aAlfa;
                    #24#0 = #0#0;
                    |LEE 000#24=;
                    |SI FSalida = 0;
                         #0#1 = #24#1; |PINTA #0#1;
                         #0#2 = #24#5; |PINTA #0#2;
                         #0#3 = #24#6; |PINTA #0#3;
                         #0#4 = #24#178; |PINTA #0#4;
                         #0#5 = #24#179; |PINTA #0#5;
                         #0#6 = #24#180;
                         #0#7 = #24#17; |PINTA #0#7;
                         #0#8 = #24#18; |PINTA #0#8;
                    |SINO;
                         |ERROR;
                    |FINSI;
               |FINSI;
               |CIERRA #24;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Almacen6; |TIPO 6;
     nModo = (FEntrada / 100) ! 0;
     |SI FSalida = 13;
          |ABRE #29;
          #29#0 = #0#9;
          |LEE 000#29];
          |CONSULTA_CLAVE #1#29;
          |SI FSalida = 0;
               |PINTA 1521, #29#1;
          |SINO;
               |ERROR;
          |FINSI;
          |CIERRA #29;
     |SINO;
          |SI nModo = 1;
               |ABRE #29;
               #29#0 = #0#9;
               |LEE 000#29=;
               |SI FSalida = 0;
                    |PINTA 1521, #29#1;
               |SINO;
                    aAlfa = "*dszx0010;A" + #0#9;
                    |SUB_EJECUTA_NP aAlfa;
                    #29#0 = #0#9;
                    |LEE 000#29=;
                    |SI FSalida = 0;
                         |PINTA 1521, #29#1;
                    |SINO;
                         |ERROR;
                    |FINSI;
               |FINSI;
               |CIERRA #29;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Almacen7; |TIPO 7;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 1;
          |SALVA_DATOS #0;
          aSocio = #0#0;
          #0#9 = 99999;
          |LEE 000#0];
          |SI FSalida = 0;
               |LEE 000#0a;
          |SINO;
               |LEE 000#0u;
          |FINSI;
          |SI FSalida = 0; |Y #0#0 = aSocio;
               nAlma = #0#9 + 1;
          |SINO;
               nAlma = 1;
          |FINSI;
          |REPON_DATOS #0;
          #0#9 = nAlma; |PINTA #0#9;
     |FINSI;
|FPRC;

|PROCESO CPostal; |TIPO 0;
     aAlfa = (("000" + #0#4) % -103) + (("00" + #0#5) % -102);
     #0#6 = aAlfa;
|FPRC;

|PROCESO T4; |TIPO 4;
     |VENTANA 1130, 1867, -1, 17, "Impresi¢n Almacenes";
     |HAZ PushVenModal;
     #10#0 = #0#0;
     #10#1 = #0#0;
     |PINPA #0#10;
     |PINDA #0#10;
     |ENDATOS #1#10;
     aAlfa = FSalida;
     |HAZ PopVenModal;
     |SI aAlfa = 0;
          |SALVA_DATOS #0;
          |HAZ Imprime;
          |REPON_DATOS #0;
          |LEE 0000#0=;
     |FINSI;
|FPRC;

|PROCESO Imprelin;
     |PRINT;
|FPRC;

|DEFBUCLE Imprelin;
     #1#1;
     ;
     #0#0, #0#9, 000;
     #0#0, #0#9, 999;
     ;
     NULL, Imprelin;
|FIN;

|PROCESO Impresion;
     |BUCLE Imprelin;
     |PIEINF;
|FPRC;

|DEFBUCLE Impresion;
     #0#1;
     ;
     #10#0, 00000;
     #10#1, 99999;
     ;
     NULL, Impresion;
|FIN;

|PROCESO Imprime;
     |IMPRE 0;
     |SI FSalida = 0;
          |INFOR "crdom002";
          |SI FSalida = 0;
               |BUCLE Impresion;
               |FININF;
          |FINSI;
          |FINIMP;
     |FINSI;
|FPRC;

|PROCESO T3; |TIPO 3;
     |CONFI "0000N¨Imprimir Resgistro Almacenes?";
     |SI FSalida = 0;
          |IMPRE 0;
          |SI FSalida = 0;
               |INFOR "crdom002";
               |SI FSalida = 0;
                    |HAZ Impresion;
                    |PIEINF;
                    |FININF;
               |FINSI;
               |FINIMP;
          |FINSI;
     |FINSI;
|FPRC;
