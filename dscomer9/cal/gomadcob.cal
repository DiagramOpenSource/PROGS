|FICHEROS;
     gomadcob #0;
     gomantca #1;        ||Expedientes (CAB)
     gomantli #2;        ||Expedientes (LIN)
     gotmpmac #3,MANTE;  ||Temporal Desviaciones MO.Capitulos
     gopartia #4;        ||Adjudicacion Partidas
     goparame #5;        ||Parametros Generales Obras
     gopre001 #6;        ||Presupuestos
     gopre002 #7;        ||Presupuestos
     gopre003 #8;        ||Presupuestos
     gopre004 #9;        ||Presupuestos
     gosubive #10;       ||Subfamilia
|FIN;
-----------------------------------------------------------------------------
|VARIABLES;
     &Tempo = "";
     aTmp3 = "";
     &cCodMonBase   = "";
     &cDesMonBase   = "";
     &cCodMonTrab   = "";
     &cDesMonTrab   = "";
     &nDeci_PBase   = 0;
     &nDeci_TBase   = 0;
     &nDeci_PTrab   = 0;
     &nDeci_TTrab   = 0;
     &nDeci_TotVis  = 0;
     &nEurComTrab   = 0;
     &nEurComBase   = 0;
     &nEurVenTrab   = 0;
     &nEurVenBase   = 0;
     nError         = 0;
     nHay           = 0;
     nLinea         = 0;
     cTipo          = "";
     nTotCosPre     = 0;
     nTotVenPre     = 0;
     nTotCosRea     = 0;
     nTotVenRea     = 0;
|FIN;
-----------------------------------------------------------------------------
|PROCESO GrabaTmp;
     |SI #4#29 = "M";
          nTotCosRea = nTotCosRea + #4#14;  ||Importe Coste Real
          nTotVenRea = nTotVenRea + #4#13;  ||Importe Venta Real
          nHay = 1;
     |FINSI;
|FPRC;
-----------------------------------------------------------------------------
|DEFBUCLE AdjPar;
     #4#3;
     ;
     #2#0, #2#1, #2#3, #2#4, #8#2, #8#4;
     #2#0, #2#1, #2#3, #2#4, #8#2, #8#4;
     ;
     NULL,GrabaTmp;
|FIN;
-----------------------------------------------------------------------------
|PROCESO MiraMO;
     |SI cTipo = #9#14;
          nTotCosPre = nTotCosPre + (#9#12 * #8#7); ||Mano de Obra Coste
          nTotVenPre = nTotVenPre + (#9#11 * #8#7); ||Mano de Obra Venta
          nHay = 1;
     |FINSI;
|FPRC;
------------------------------------------------------------------------------
|DEFBUCLE MiraMOPar;
     #9#1;
     ;
     #2#3,#2#4,#7#12,#8#4,1;
     #2#3,#2#4,#7#12,#8#4,999;
     ;
     NULL, MiraMO;
|FIN;
------------------------------------------------------------------------------
|PROCESO MiraMOPar;
     |BUCLE MiraMOPar;
     |BUCLE AdjPar;
|FPRC;
------------------------------------------------------------------------------
|DEFBUCLE MiraMOCap;
     #8#1;
     ;
     #2#3,#2#4,#7#12,1;
     #2#3,#2#4,#7#12,999;
     ;
     NULL, MiraMOPar;
|FIN;
------------------------------------------------------------------------------
|PROCESO MiraMOCap;
     |BUCLE MiraMOCap;
     |SI nHay = 1;
          nLinea = nLinea + 1;
          #3#0 = #2#0;             ||Serie Expediente
          #3#1 = #2#1;             ||Numero Expediente
          #3#2 = #2#3;             ||Serie Presupuesto
          #3#3 = #2#4;             ||Numero Presupuesto
          #3#4 = #7#12;            ||Capitulo
          #3#5 = nLinea;           ||Linea
          |LEE 010#3=;
          |SI FSalida ! 0;
               #3#14 = #7#13;           ||Descripcion del Capitulo
               #3#6  = nTotCosPre;      ||Mano de Obra Coste Presupuestado
               #3#7  = nTotVenPre;      ||Mano de Obra Venta Presupuestado
               #3#8  = nTotCosRea;      ||Mano de Obra Coste Real
               #3#9  = nTotVenRea;      ||Mano de Obra Venta Real
               #3#10 = #3#6 - #3#8;     ||Desviaciones Coste
               #3#11 = #3#7 - #3#9;     ||Desviaciones Venta
               #3#12 = (#3#10 / #3#6) * 100; ||% Desviacion Coste
               #3#13 = (#3#11 / #3#7) * 100; ||% Desviacion Venta
               #0#7  = #0#7 + #3#6;     ||Total Coste M.O. Presupuestado
               #0#8  = #0#8 + #3#7;     ||Total Venta M.O. Presupuestado
               #0#9  = #0#9 + #3#8;     ||Total Coste M.O. Real
               #0#10 = #0#10 + #3#9;    ||Total Venta M.O. Real
               #0#11 = #0#7 - #0#9;     ||Total Desviaciones Coste
               #0#12 = #0#8 - #0#10;    ||Total Desviaciones Venta
               |GRABA 010#3n;
               nTotCosPre = 0;
               nTotVenPre = 0;
               nTotCosRea = 0;
               nTotVenRea = 0;
               nHay       = 0;
          |FINSI;
     |FINSI;
|FPRC;
-----------------------------------------------------------------------------
|DEFBUCLE MiraMOPre;
     #7#6;
     ;
     #2#3,#2#4,#0#5;
     #2#3,#2#4,#0#6;
     ;
     NULL, MiraMOCap;
|FIN;
-----------------------------------------------------------------------------
|PROCESO Presu;
     |BUCLE MiraMOPre;
|FPRC;
-----------------------------------------------------------------------------
|PROCESO MiraMOPre;
     |HAZ MonTraTmp;
     |BUCLELIN 2 Presu #1;
|FPRC;
-----------------------------------------------------------------------------
|DEFBUCLE Busca;
     #1#1;
     ;
     #0#0,#0#2;
     #0#1,#0#3;
     ;
     NULL,MiraMOPre;
|FIN;
-----------------------------------------------------------------------------
|PROCESO BuscaCap; |TIPO 3;
     nHay = 0;
     nLinea = 0;
     |HAZ CreaTempo; aTmp3 = Tempo;
     |NOME_DAT #3 aTmp3; |DELINDEX #3;
     |ABRE #1;
     |ABRE #3;
     |ABRE #4;
     |HAZ MiraTipo;
     |BUCLE Busca;
     |SI nError = 1; |ACABA; |FINSI;
     |SI #0#4 = "P";
          |ENTLINEAL #1#3,6,4,16,1,min,max;
     |SINO;
          |HAZ Impresion;
     |FINSI;
     |CIERRA #1;
     |CIERRA #3;
     |CIERRA #4;
     Tempo = aTmp3; |HAZ BoraTempo; |DELINDEX #3;
|FPRC;
-----------------------------------------------------------------------------
|PROCESO MiraTipo;
     |ABRE #5;
     |LEE 010#5p;
     |SI FSalida = 0;
          cTipo = #5#6;
     |FINSI;
     |CIERRA #5;
|FPRC;
-----------------------------------------------------------------------------
|PROCESO MonTraTmp;
     |QBF cCodMonTrab;
     |SI cCodMonTrab ! ""; |Y #1#6 ! cCodMonTrab;
          |MENAV "0000HAY EXPEDIENTES CON DISTINTA MONEDA";
          |ERROR10;
          nError = 1;
          |ACABA;
     |FINSI;

     cCodMonTrab = #1#6;
     |HAZ MonBas;
|FPRC;
-----------------------------------------------------------------------------
|PROCESO min;
     #3#0 = "     ";
     #3#1 = 1;
     #3#2 = "     ";
     #3#3 = 1;
     #3#4 = " "*20;
     #3#5 = 1;
|FPRC;
-----------------------------------------------------------------------------
|PROCESO max;
     #3#0 = "zzzzz";
     #3#1 = 999999;
     #3#2 = "zzzzz";
     #3#3 = 999999;
     #3#4 = "z"*20;
     #3#5 = 999;
|FPRC;
-----------------------------------------------------------------------------
|PROCESO ImpTmp;
     |PRINT;
|FPRC;
-----------------------------------------------------------------------------
|DEFBUCLE ImpTmp;
     #3#1;
     ;
     "     ",     1,"     ",     1," "*20,  1;
     "zzzzz",999999,"zzzzz",999999,"z"*20,999;
     ;
     NULL, ImpTmp;
|FIN;
-----------------------------------------------------------------------------
|PROCESO Impresion;
     |SUB_EJECUTA_NP "goinform;IMPgomadcob";
     |INFOR "gomadcob";
     |BUCLE ImpTmp;
     |PIEINF;
     |FININF;
     |SUB_EJECUTA_NP "goinform;ENVgomadcob";
     |SUB_EJECUTA_NP "goinform;RUNMATER. CAP.";
|FPRC;
-----------------------------------------------------------------------------
|PROCESO Posicion; |TIPO 7;
     |ABRE #1;
     |LEE 000#1p;

     |SI FSalida = 0;
          #0#0 = #1#0;
          #0#2 = #1#1;
     |FINSI;

     |LEE 000#1u;

     |SI FSalida = 0;
          #0#1 = #1#0;
          #0#3 = #1#1;
     |FINSI;

     |CIERRA #1;
     |ABRE #10;
     |LEE 000#10p;

     |SI FSalida = 0;
          #0#5 = #10#0;
          #0#13 = #10#1;
     |FINSI;

     |LEE 000#10u;

     |SI FSalida = 0;
          #0#6 = #10#0;
          #0#14 = #10#1;
     |FINSI;

     |CIERRA #10;
     |PINDA #0#0;
|FPRC;
-----------------------------------------------------------------------------
|PROCESO Tipo13; |TIPO 13;
     |ABRE #5; |LEE 000#5p; |CIERRA #5;
     |SI #5#141 = "O";
          |ATRI R; |PINTA 1205, " O.T.      "; |ATRI 0;
     |FINSI;
|FPRC;
