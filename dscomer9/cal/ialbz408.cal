     FACTURAS

|FICHEROS;
     ialbz008 #0;
     agifa059 #59;
     agifa134 #134;
     agifa142 #142;
     referec@;
     referel@;
|FIN;

|VARIABLES;
     &aDivisa = "";
     &aMoneda = "";
     &eaEstadoBtn = "";
     &eaTmp = "";
     &eaPestana = "";
     &eaSwCarga4 = "";
     &eaBtnAcota4 = "";
     &eaSer4 = "";
     &eaNum4 = "";
     &eaLin4 = "";
     &eaCli4 = "";
     &eaDir4 = "";

     aProg = "";
     aTmp = "";
     aAlfa = "";
     nRango = 0;
     nPos1 = 0;
     nPos2 = 0;
     nPestana = 0;
     aPath = "";
     nPos = 0;
     nDDir = 0;
     nHDir = 0;
     aDFac = "";
     aHFac = "";

     nBtnVerFac4 = 0;
     nBtnVerAlb4 = 0;
     nBtnVerRec4 = 0;
     nBtnVerAsi4 = 0;
     nBtnImpre4 = 0;
     nBtnSalir4 = 0;
     nGridC = -1;
     nGridL = -1;
     nPanta = 0;
     nEvtFoco = 0;
|FIN;

//----------------//
|PROCESO Existe4;
     |ABRE #134;
     #134#49 = #referec@#49;
     #134#0 = #referec@#0;
     |LEE 000#134=;
     aAlfa = FSalida;
     |CIERRA #134;
     FSalida = aAlfa;
|FPRC;

|PROCESO CargaDatos4;
     |SI eaSwCarga4 ! "S";
          |HAZ DestruyePan4;
          |HAZ Descarga4;
          |HAZ PintaPanta4;
     |FINSI;
|FPRC;

|PROCESO CogePosicion4;
     eaCli4 = #referec@#2;
     eaDir4 = ("0000" + #referec@#115) % -104;
     eaSer4 = #referec@#49
     eaNum4 = ("000000" + #referec@#0) % -106;
     eaLin4 = ("000" + #referel@#1) % -103;
|FPRC;

|PROCESO Posiciona4;
     #referel@#14 = eaSer4;
     #referel@#0 = eaNum4;
     #referel@#1 = eaLin4;
     |LEE 000#referel@.=;

     #referec@#2 = eaCli4;
     #referec@#115 = eaDir4;
     #referec@#49 = eaSer4;
     #referec@#0 = eaNum4;
     |LEE 000#referec@.=;
     |REFRESCACONTROL nGridC;

     |SI eaPestana = "4"; |FOCOTECLADO nGridC; |FINSI;
|FPRC;

|PROCESO EstadoBtn4;
|FPRC;

|PROCESO BtnVerFac4;
     |HAZ Existe4;
     |SI FSalida ! 0; |ACABA; |FINSI;

     aProg = "*dszx0005;"+ "V" + #referec@#49 + (("000000"+#referec@#0)%-106);
     |SUB_EJECUTA aProg;
|FPRC;

|PROCESO BtnVerAlb4;
     |HAZ Existe4;
     |SI FSalida ! 0; |ACABA; |FINSI;

     aProg = "*dszx0005;"+ "A" + #referec@#49 + (("000000"+#referec@#0)%-106);
     |SUB_EJECUTA aProg;
|FPRC;

|PROCESO BtnVerRec4;
     |HAZ Existe4;
     |SI FSalida ! 0; |ACABA; |FINSI;
     |SI #referec@#45 = "N";
          |MENAV "0000Factura no impresa...";
          |ACABA;
     |FINSI;

     aProg = "*dszx0005;"+ "R" + #referec@#49 + (("000000"+#referec@#0)%-106);
     |SUB_EJECUTA aProg;
|FPRC;

|PROCESO nBtnVerAsi4;
     |HAZ Existe4;
     |SI FSalida ! 0; |ACABA; |FINSI;

     |SI #134#48 = "N";
          |MENAV "0000Factura no contabilizada...";
          |ACABA;
     |FINSI;

     aProg = "*dszx0005;"+ "C" + #referec@#49 + (("000000"+#referec@#0)%-106);
     |SUB_EJECUTA aProg;
|FPRC;

|PROCESO BtnImpre4;
     |HAZ Existe4;
     |SI FSalida ! 0; |ACABA; |FINSI;

     aProg = "*dszx0005;"+ "I" + #referec@#49 + (("000000"+#referec@#0)%-106);
     |SUB_EJECUTA aProg;
|FPRC;

|PROCESO EventoC4;
     |SI FSalida = 1; |O FSalida = 2;
          |LEE 000#referec@.=;
          |SI FSalida = 0;
               aDivisa = #referec@#58;
               aMoneda = #referec@#60;
               |HAZ Divisas134;
          |FINSI;
          |REFRESCACONTROL nGridL;
     |FINSI;
|FPRC;

|PROCESO EventoL4;
     |SI FSalida = 1; |O FSalida = 2;
          |LEE 000#referel@.=;
     |FINSI;
|FPRC;

|PROCESO EventoFoco4;
     |HAZ CargaDatos4;
     eaPestana = "4";
|FPRC;

|PROCESO NomeDat4;
     aTmp = "agifa134";
     |NOME_DAT #referec@#aTmp#;
|FPRC;

|PROCESO MinL4;
     #referel@#14 = #referec@#49
     #referel@#0 = #referec@#0;
     #referel@#1 = 1;
|FPRC;

|PROCESO MaxL4;
     #referel@#14 = #referec@#49
     #referel@#0 = #referec@#0;
     #referel@#1 = 999;
|FPRC;

|PROCESO MinC4;
     #referec@#2 = #0#0;
     #referec@#115 = nDDir;
     #referec@#49 = "     ";
     #referec@#0 = 0;
|FPRC;

|PROCESO MaxC4;
     #referec@#2 = #0#0;
     #referec@#115 = nHDir;
     #referec@#49 = "zzzzz";
     #referec@#0 = 999999;
|FPRC;

|PROCESO EstadoIniBtnPan4;
     |ESTADO_CONTROL nBtnVerFac4, eaEstadoBtn;
     |ESTADO_CONTROL nBtnVerAlb4, eaEstadoBtn;
     |ESTADO_CONTROL nBtnVerRec4, eaEstadoBtn;
     |ESTADO_CONTROL nBtnVerAsi4, eaEstadoBtn;
     |ESTADO_CONTROL nBtnImpre4, eaEstadoBtn;
     |ESTADO_CONTROL nBtnSalir4, eaEstadoBtn;
|FPRC;

|PROCESO DestruyePan4;
     |DESTRUYECONTROL nGridL;
     |DESTRUYECONTROL nGridC;

     |FIN_CONTROL_WINDOWS nEvtFoco;

     |FIN_CONTROL_WINDOWS nBtnVerFac4;
     |FIN_CONTROL_WINDOWS nBtnVerAlb4;
     |FIN_CONTROL_WINDOWS nBtnVerRec4;
     |FIN_CONTROL_WINDOWS nBtnVerAsi4;
     |FIN_CONTROL_WINDOWS nBtnImpre4;
     |FIN_CONTROL_WINDOWS nBtnSalir4;
|FPRC;

|PROCESO Descarga4;
     |CIERRAT #referec@;
     |CIERRAT #referel@;
     |DESCARGA_DEF #referel@;
     |DESCARGA_DEF #referec@;
|FPRC;

|PROCESO PintaPanta4;
     |DDEF aPath;
     aAlfa = aPath + "agifa134";
     |CARGA_DEF aAlfa, referec@;
     aAlfa = aPath + "agifa059";
     |CARGA_DEF aAlfa, referel@;

     |HAZ NomeDat4;
     #referec@#0 = 0;    || Para que no salga nada si esta vacio

     |ABRET #referec@;;
     |ABRET #referel@;
     |SI #0#1 = 0;
          nDDir = 0; nHDir = 9999;
     |SINO;
          nDDir = #0#1; nHDir = #0#1;
     |FINSI;
     eaSwCarga4 = "S";

     |PINPA #4#0;  nPanta = FSalida;

     nRango = 4 + 8 + 16 + 32;
     nPos1 = 2402;
     nPos2 = 3398;
     |SI nGridL ! -1; |DESTRUYECONTROL nGridL; |FINSI;
     |LINEAL_SIMPLE #1#referel@, nRango, nPos1, nPos2, MinL4, MaxL4, EventoL4;
     nGridL = FSalida;

     nRango = 4 + 8 + 16 + 32;
     nPos1 = 0602;
     nPos2 = 2298;
     |SI nGridC ! -1; |DESTRUYECONTROL nGridC; |FINSI;
     |SI #0#0 = "000000";
          |LINEAL_SIMPLE #1#referec@, nRango, nPos1, nPos2, MinC4, MaxC4, EventoC4;
     |SINO;
          |LINEAL_SIMPLE #6#referec@, nRango, nPos1, nPos2, MinC4, MaxC4, EventoC4;
     |FINSI;
     nGridC = FSalida;

     |CONTROL_SIMPLE nPanta, "EVENTOPAN", 0000, 0000, EventoFoco4;
     nEvtFoco = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,Ver Factura", 2301, 2313, BtnVerFac4;
     nBtnVerFac4 = FSalida;
     |CONTROL_SIMPLE nPanta, "BOTON,Ver Albaran", 2314, 2325, BtnVerAlb4;
     nBtnVerAlb4 = FSalida;
     |CONTROL_SIMPLE nPanta, "BOTON,Ver Recibo", 2326, 2338, BtnVerRec4;
     nBtnVerRec4 = FSalida;
     |CONTROL_SIMPLE nPanta, "BOTON,Ver Asiento", 2339, 2351, nBtnVerAsi4;
     nBtnVerAsi4 = FSalida;
     |CONTROL_SIMPLE nPanta, "BOTON,{{205}}Imprimir", 0579, 0591, BtnImpre4;
     nBtnImpre4 = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,{{207}} Salir", 3488, 3498, BtnSalir;
     nBtnSalir4 = FSalida;

     |HAZ EstadoBtn4;
|FPRC;
