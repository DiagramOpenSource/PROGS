|FICHEROS;
     psion066 #0;
     psion067 #67,MANTE;
     agifa019 #19;
|FIN;

|VARIABLES;
     &eaAlfa = "";
     nPosi = 0;
     aLen = "";
     aCadena = "";
     aCaracter = "";
     &eaFichero = "";
     &enError = 0;
     nConta = 0;
     aTipoDato = "";
 {-1}aMenu = "";
     aMenu1 = "2400";
     aMenu2 = "1";
     aMenu3 = "Opciones:  ";
     aMenu4 = "ECa";
     aMenu5 = " Editar ";
     aMenu6 = " Cargar ";
     aMenu7 = " Cancelar ";

 {90}Valor = "";
     aReg = "";
     aLon = "";
     aDato = "";
     nPos = 0;
     aLetra = "";
     aTab = "";
     i = 0;
     nCampo = 0;
     aFic = "";
     nHandle = 0;
|FIN;

|PROCESO Dato;
     IValor = Valor1<-;
     |PARA i = 1; |SI i [ 90; |HACIENDO i = i + 1;
          Valor = "";
          IValor = IValor + 1;
     |SIGUE;
     nCampo = 1;
     IValor = Valor1<-;
     aLon = aDato % A0;
     aReg = "";
     aTab = &9;
     |PARA i = 1; |SI i [ aLon; |HACIENDO i = i + 1;
          nPos = i * 100 + 1;
          aLetra = aDato % nPos;
          |SI aLetra = aTab;
               Valor = aReg;
               aReg = "";
               nCampo = nCampo + 1;
               IValor = IValor + 1;
          |SINO;
               aReg = aReg + aLetra;
          |FINSI;
          |SI nCampo > 90; |SAL; |FINSI;     || Por si.... :)
     |SIGUE;
     Valor = aReg;
|FPRC;

|PROCESO GraArticulo;
     |DDEFECTO #19;
     #19#0 = #67#0;
     |LEE 000#19=;
     |SI FSalida ! 0;
          #19#1 = #67#1;
          |GRABA 020#19n;
     |FINSI;
|FPRC;

|DEFBUCLE GraArticulo;
     #67#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, GraArticulo;
|FIN;

|PROCESO Movimiento;
     |SI Valor5 ! aTipoDato; |Y aTipoDato ! "";
          |ACABA;
     |FINSI;

     nConta = nConta + 1;
     |DDEFECTO #67;
     #67#0 = Valor1;
     #67#1 = Valor2;
     #67#2 = Valor3;
     #67#3 = Valor4;
     #67#4 = Valor5;
     #67#5 = ~;
     #67#6 = nConta;
     #67#7 = Valor6;
     #67#8 = Valor7;
     #67#9 = Valor8;
     |GRABA 121#67n;
     enError = 0;
|FPRC;

|PROCESO Min67;
     #67#6 = 1;
|FPRC;

|PROCESO Max67;
     #67#6 = 999;
|FPRC;

|PROCESO Tipo3; |TIPO 3;
     enError = 1;
     |NOME_DAT #67#eaFichero#;
     |DELINDEX #67;
     |ABRE #67;
     aFic = #0#2; |QBF aFic;
     aFic = aFic + "movimie.asc";
     |XABRE "C", aFic, nHandle;
     |SI FSalida ] 0;
          |XLEE nHandle, 254, aDato;
          |PARA; |SI FSalida > 0; |HACIENDO;
               |PINTA 1719, aDato;
               |HAZ Dato;
               |HAZ Movimiento;
               |XLEE nHandle, 254, aDato;
          |SIGUE;
          |XCIERRA nHandle;
          |SI enError ! 0;
               |MENAV "0000Fichero sin registros !!!";
          |SINO;
               |ENTLINEAL #1#67, 1, 7, 22, 1, Min67, Max67;
               |PARA; |SI aMenu2 = 1; |HACIENDO;
                    |MENU aMenu;
                    aMenu2 = FSalida;
                    |SI aMenu2 = 1;
                         |ENTLINEAL #1#67, 1, 2, 22, 0, Min67, Max67;
                    |FINSI;
               |SIGUE;
               enError = 1;
               |SI aMenu2 = 2; enError = 0; |FINSI;
          |FINSI;
     |SINO;
          |MENAV "0000No se encuentra fichero a cargar.";
     |FINSI;
     |CIERRA #67;
     |SI enError = 0;
          |ABRE #19;
          |BUCLE GraArticulo;
          |CIERRA #19;
     |FINSI;
|FPRC;

|PROGRAMA;
     enError = 1;
     aTipoDato = PARAMETRO;
     enError = 1;
     |HAZ EsPsion;
     |SI FSalida ! 0;
          |MENAV "0000Módulo 00psion no instalado.";
     |SINO;
          |CLS;
          |PINPA #0#0;
          |PINDA #0#0;
          |ENDATOS #1#0;
     |FINSI;
|FPRO;
