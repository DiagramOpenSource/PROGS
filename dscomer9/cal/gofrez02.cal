|FICHEROS;
     gofrez02 #0;
     gofrew01 #1;   ||Tmp
     gofrem02 #2;
     gofrem04 #4;
     agifa010 #10;
     agifa071 #71;
     gofrem01 #100;
|FIN;

|VARIABLES;
     aIP = "";
     aOrg = "";
     aDes = "";
     aFic = "";
     nConta = 0;
     {1}aBaseLocal = "";
     &enEti = 0;
     &Tempo = "";
     nTipo = 0;
     aFormato = "";
     aImpresora = "";
     aEtiqueta = "";
     aAlfa = "";
     nEti = 0;
     nTipoAnt = -1;
     aInfAnt = "";
     aNomImp = "";
     aInf = "";
     aBat = "";
     aLon = "";
     nPosi = 0;
     i = 0;
     aCar = "";
|FIN;

|PROCESO CambiaBarra;
     aLon = aDes % A0;
     aAlfa = "";
     |PARA i = 1; |SI i [ aLon; |HACIENDO i = i + 1;
          nPosi = i * 100 + 1;
          aCar = aDes % nPosi;
          |SI aCar = "/"; aCar = "\"; |FINSI;
          aAlfa = aAlfa + aCar;
     |SIGUE;
     aDes = aAlfa;
|FPRC;

|PROCESO Bat;
     |SI nTipoAnt = -1; |ACABA; |FINSI;

     |FINIMP;
     nConta = nConta + 1;
     aDes = aBaseLocal1 + "spool/" + Usuario + nConta;

     aIP = "";
     |IP_REMOTO aIP;
     |SI aIP = "";
          |COPIA_FICHERO aOrg, aDes;
     |SINO;
          |ENVIA_FICHERO aOrg, aDes;
     |FINSI;
     |HAZ CambiaBarra;

     aBat = "";
     |SI nTipoAnt = 1; aBat = "pgofre1"; |FINSI;
     |SI nTipoAnt = 2; aBat = "pgofre2"; |FINSI;
     |SI nTipoAnt = 3; aBat = "pgofre3"; |FINSI;

     aBat = aBat + " " + aNomImp + " " + aFormato + " " + aDes;

     aAlfa  = "cmd " + &34 + "/c START /MIN /WAIT " + aBat + &34;
     |SI aIP = "";
          |SYSTEM aAlfa;
     |SINO;
          |RSYSTEM aAlfa;
     |FINSI;

     Impresora = aOrg;
     |IMPRE -77;
|FPRC;

|PROCESO LineasEnv;
     #4#0 = #71#29;
     #4#1 = #71#0;
     #4#2 = #71#1;
     |LEE 000#4=;
     |SI FSalida ! 0; |DDEFECTO #4; |FINSI;

     |PARA enEti = 1; |SI enEti [ #4#6; |HACIENDO enEti = enEti + 1;
          |PRINT;
          |PIEINF;
     |SIGUE;
|FPRC;

|PROCESO Lineas;
     |PRINT;
|FPRC;

|PROCESO Tmp;
     #10#52 = #1#0;
     #10#0 = #1#1;
     |LEE 020#10=;

     aInf = #1#3; |QBF aInf;
     |SI #1#2 ! nTipoAnt; |O aInf ! aInfAnt;
          |HAZ Bat;
          nTipoAnt = #1#2;
          aInfAnt = aInf;
          aNomImp = #1#5; |QBF aNomImp;
          aFormato = #1#4; |QBF aFormato;
     |FINSI;

     |SI #1#2 = 1;
          nEti = #10#2;
          |INFOR aInf;
          |SI FSalida = 0;
               |PARA enEti = 1; |SI enEti [ nEti; |HACIENDO enEti = enEti + 1;
                    |BUCLELIN 2Lineas #10;
                    |PIEINF;
               |SIGUE;
               |FININF;
          |SINO;
               aAlfa = "0000Error en informe:'" + aInf + "'";
               |MENAV aAlfa;
          |FINSI;
     |FINSI;
     |SI #1#2 = 2;
          |INFOR aInf;
          |SI FSalida = 0;
               |BUCLELIN 2Lineas #10;
               |PIEINF;
               |FININF;
          |SINO;
               aAlfa = "0000Error en informe:'" + aInf + "'";
               |MENAV aAlfa;
          |FINSI;
     |FINSI;
     |SI #1#2 = 3;
          |INFOR aInf;
          |SI FSalida = 0;
               |BUCLELIN 2LineasEnv #10;
               |FININF;
          |SINO;
               aAlfa = "0000Error en informe:'" + aInf + "'";
               |MENAV aAlfa;
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE Tmp;
     #1#2;
     ;
     INICIO;
     FINAL;
     ;
     NULL, Tmp;
|FIN;

|PROCESO GraTmp;
     #1#0 = #10#52;
     #1#1 = #10#0;
     #1#2 = nTipo;
     #1#3 = aFormato;
     #1#4 = aImpresora;
     #1#5 = aEtiqueta;
     |GRABA 020#1n;
|FPRC;

|PROCESO agifa010;
     #2#0 = #10#3;
     |LEE 000#2=;
     |SI FSalida ! 0;
          #2#0 = #100#2;
          |LEE 000#2=;
     |FINSI;
     |SI FSalida ! 0; |DDEFECTO #2; |FINSI;

     aAlfa = #2#3; |QBF aAlfa;
     |SI aAlfa ! ""; |Y #0#8 = #2#4;
          nTipo = 1;
          aFormato = #2#1;
          aImpresora = #2#2;
          aEtiqueta = #2#3;
          |HAZ GraTmp;
     |SINO;
          aAlfa = #2#7; |QBF aAlfa;
          |SI aAlfa ! ""; |Y #0#8 = #2#8;
               nTipo = 2;
               aFormato = #2#5;
               aImpresora = #2#6;
               aEtiqueta = #2#7;
               |HAZ GraTmp;
          |SINO;
               aAlfa = #2#11; |QBF aAlfa;
               |SI aAlfa ! ""; |Y #0#8 = #2#12;
                    aFormato = #2#9;
                    aImpresora = #2#10;
                    aEtiqueta = #2#11;
                    nTipo = 3;
                    |HAZ GraTmp;
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE agifa010;
     #10#1;
     1, 3;
     #0#0, #0#2, #0#4, #0#6;
     #0#1, #0#3, #0#5, #0#7;
     ;
     NULL, agifa010;
|FIN;

|PROCESO T3; |TIPO 3;
     |ABRE #100; |LEE 000#100p; |CIERRA #100;
     |HAZ CreaTempo; |NOME_DAT #1 Tempo; |DELINDEX #1; |ABRE #1;

     |ABRE #2;
     |BUCLE agifa010;
     |CIERRA #2;

     |ESPECIFICA "RBASS", aBaseLocal;
     |DFICE aOrg;
     aOrg = aOrg  + Usuario;

     Impresora = aOrg;
     |IMPRE -77;
     |SI FSalida = 0;
          nTipoAnt = -1;
          |ABRE #4;
          |ABRE #10;
          |BUCLE Tmp;
          |CIERRA #10;
          |CIERRA #4;

          |HAZ Bat;
          |FINIMP;
          |FBORRA aOrg;
     |FINSI;

     |CIERRA #1; |DELINDEX #1; |HAZ BoraTempo;
|FPRC;
