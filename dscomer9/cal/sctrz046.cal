|FICHEROS;
     sctrz046 #0;
     sctrm008 #8;
     sctrw011 #11; ||Tmp Poblaciones
     sctrm023 #23;
     sctrm030 #30;
     sctrm031 #31;
     sctrm052 #52;
     sctrm057 #57;
     sctrm001 #100;
     referen@;
|FIN;

|VARIABLES;
     &enTipo = 0;   ||para el informe

     &eaMensaje = "";
     &Tempo = "";
     &eaNomLogo = "";
     &enPantaLogo = 0;
     nPanta = 0;
     aInforme = "";
     fFecha = @;
     aTemporada = "";
     aDTempo = "";
     aHTempo = "";
     nRango = 0;
     nGrid = 0;
     aAlfa = "";
     nCampo = 0;
     nHay = 0;
     nSwPrv = 0;
     nLinea = 0;
     nServi = 0;
     nNume = 0;
     aEstado = "";
     aPobla = "";
|FIN;

|PROCESO Imprime;
     |SI fFecha < #0#1; |O fFecha > #0#2; |ACABA; |FINSI;
     |SI nLinea < #0#3; |O nLinea > #0#4; |ACABA; |FINSI;
     |SI nServi < #0#6; |O nServi > #0#7; |ACABA; |FINSI;
     |SI #0#8 = "N"; |Y aEstado = "L";  |ACABA; |FINSI;
     |SI aEstado = "A"; |ACABA; |FINSI;

     |SI nSwPrv = 0;
          #11#0 = aPobla;
          |LEE 000#11=;
          |SI FSalida = 0; |Y #11#2 = "*";
               |PRINT;
          |FINSI;
     |SINO;
          |PRINT;
     |FINSI;
|FPRC;

|PROCESO Reserva;
     |SI #0#0 = "O";     ||Salida
          nNume = #52#20 - #52#99 + 1;
          fFecha = nNume;
          aPobla = #52#8;
     |SINO;              ||Llegada
          fFecha = #52#23;
          aPobla = #52#12;
     |FINSI;
     nLinea = #52#75;
     nServi = #52#22;
     aEstado = #52#52;
     enTipo = 1;
     |HAZ Imprime;

     |SI #0#0 = "O";     ||Salida
          nNume = #52#26 - #52#100 + 1;
          fFecha = nNume;
          aPobla = #52#12;
     |SINO;              ||Llegada
          fFecha = #52#29;
          aPobla = #52#8;
     |FINSI;
     nLinea = #52#80;
     nServi = #52#28;
     aEstado = #52#53;
     enTipo = 2;
     |SI #52#17 = 2; |Y #52#23 ! "01.01.0000";
          |HAZ Imprime;
     |FINSI;
     |SI #52#17 = 3;
          |HAZ Imprime;
     |FINSI;
|FPRC;

|DEFBUCLE Reserva;
     #52#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, Reserva;
|FIN;

|PROCESO T3; |TIPO 3;
     aInforme = #57#13; |QBF aInforme;
     Impresora = "CrystalReport";
     |IMPRE -1;
     |SI FSalida = 0;
          |INFOR aInforme;
          |SI FSalida = 0;
               |ABRE #11;
               |LEE 000#11p;
               nSwPrv = FSalida;
               |BUCLE Reserva;
               |CIERRA #11;
               |PIEINF;
               |FININF;
          |SINO;
               aAlfa = "0000Error en informe '" + aInforme + "'...";
               |MENAV aAlfa;
          |FINSI;
          |FINIMP;
     |FINSI;
|FPRC;

|PROCESO BuscaTempo;
     |SI fFecha ] #8#2; |Y fFecha [ #8#3;
          aTemporada = #8#0;
          |ERROR10;
     |FINSI;
|FPRC;

|DEFBUCLE BuscaTempo;
     #8#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, BuscaTempo;
|FIN;

|PROCESO sctrm023;
     |SALVA_DATOS #23;
     |REPON_DATOS #referen@;
     |GRABA 020#referen@.n;
|FPRC;

|DEFBUCLE sctrm023;
     #23#1;
     ;
     aTemporada, #0#3, #0#5, 000000;
     aTemporada, #0#4, #0#5, 999999;
     ;
     NULL, sctrm023;
|FIN;

|PROCESO Servicio; |TIPO 66;
     |SI Campo = 6;
          fFecha = #0#1;
     |SINO;
          fFecha = #0#2;
     |FINSI;
     |BUCLE BuscaTempo;
     |DDEF aAlfa;
     aAlfa = aAlfa + "sctrm023";
     |CARGA_DEF aAlfa, referen@;
     |SI FSalida = 0;
          |HAZ CreaTempo; |NOME_DAT #referen@#Tempo#; |DELINDEX #referen@;
          |ABRE #referen@;
          |BUCLE sctrm023;
          |LEE 000#referen@.p;
          |CONSULTA_CLAVE #1#referen@;
          |SI FSalida = 0;
               #0Campo = #referen@#5; |PINTA #0Campo;
          |SINO;
               |ERROR;
          |FINSI;
          |CIERRA #referen@;
          |HAZ BoraTempo; |DELINDEX #referen@;
          |DESCARGA_DEF #referen@;
     |FINSI;
|FPRC;

|PROCESO sctrm031;
     nCampo = #30#5 + 11;
     |SI nCampo ] 12; |Y nCampo [ 21;
          |SI #31nCampo ! "     ";
               #11#0 = #31#6;
               #11#1 = #31#7;
               #11#2 = "*";
               |GRABA 000#11n;
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE sctrm031
     #31#1;
     9;
     #30#0, #30#1, #30#2, #30#3, #30#4, 000, #0#0;
     #30#0, #30#1, #30#2, #30#3, #30#4, 999, #0#0;
     ;
     NULL, sctrm031;
|FIN;

|PROCESO sctrm030;
     |BUCLE sctrm031;
|FPRC;

|DEFBUCLE sctrm030;
     #30#1;
     6;
     aDTempo, #0#3, #0#5, 000, 00, 000, #0#6;
     aHTempo, #0#4, #0#5, 999, 99, 999, #0#7;
     ;
     NULL, sctrm030;
|FIN;

|PROCESO BtnPobla;
     eaMensaje = "Calculando Poblaciones...";
     |HAZ IniMensaje;

     |CIERRAT #11; |DELINDEX #11; |ABRET #11;
     fFecha = #0#1; |BUCLE BuscaTempo; aDTempo = aTemporada;
     fFecha = #0#2; |BUCLE BuscaTempo; aHTempo = aTemporada;
     |BUCLE sctrm030;
     |LEE 000#11p;

     |HAZ FinMensaje;
     |REFRESCACONTROL nGrid;
|FPRC;

|PROCESO Evt;
     |SI FSalida = 1; |O FSalida = 2;
          |SELECT #1#11;
          |LEE 000#11=;
     |FINSI;
     |SI FSalida = 4;
          |SI #11#2 = "*";
               #11#2 = "";
          |SINO;
               #11#2 = "*";
          |FINSI;
          |PINTA #11#2;
          |GRABA 020#11a;
     |FINSI;
|FPRC;

|PROCESO LogoPanta;
     eaNomLogo = #100#1;
     |HAZ TraeLogoPan;
     enPantaLogo = nPanta;
     |HAZ PintaLogoPan;
|FPRC;

|PROGRAMA;
     |ABRE #100; |LEE 000#100p; |CIERRA #100;
     |ABRE #57; |LEE 000#57p; |CIERRA #57;

     |PINPA #0#0; nPanta = FSalida;
     |PINDA #0#0;
     |HAZ LogoPanta;

     |HAZ CreaTempo; |NOME_DAT #11#Tempo#; |DELINDEX #11; |ABRET #11;

     |CONTROL_SIMPLE nPanta, "BOTON,{{137}}C lculo Poblaciones", 1371, 1498, BtnPobla;

     nRango = 4 + 8 + 16 + 32;
     |LINEAL_SIMPLE #1#11, nRango, 2003, 2999, NULL, NULL, Evt;
     nGrid = FSalida;
     |ENDATOS #1#0;

     |CIERRAT #11; |DELINDEX #11; |HAZ BoraTempo;
|FPRO;
