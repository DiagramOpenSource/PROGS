|FICHEROS;
     maesm060@;
     autfm002@;
     autfm003@;
     pycm0072@;

     agifa007;
     agifa077;
     agifa080;
     agifa019;
     agifa324;
     agifa112;
     agifa321;
     agifa079;
     agifa091;
     agifa177;
     dsz99984;
     agifa716;
     agifa704;
     agifa029;
     agifa722;
     agifa730;

     dsm00083;
|FIN;

|VARIABLES;
     nCalc            = 0;
     nCont            = 0;
     Primero          = 1;
     NumB             = 0;
     NumZ             = 0;
     nLinea           = 0;
     nPPort1          = 0;
     nPPort2          = 0;
     nImpDiv          = 0;
     nSwLin           = 0;
     nAntDocum        = 0;
     j                = 0;
     i                = 0;
     nCamImp          = 0;
     nCamFec          = 0;
     nNumRecibos      = 0;
     nHandleCorreo    = 0;
     nNumFac          = 0;
     vic              = 0;
     vic1             = 0;

     aAlfa            = "";
     aAlfa1           = "";
     aSerieFac        = "";
     aBase            = "";
     aPathCorreo      = "";
     aFichero         = "";
     aTo              = "";
     aIP              = "";
     aSalida          = "";
     aFrom            = "";
     aAsunto          = "";
     aMensaje         = "";
     aA¤o             = "";
     aAntUsuario      = "";
     aAntSExp         = "";
     SerB             = "";
     SerZ             = "";
     Bl               = "";
     ZZ               = "";
     aDefDspreven     = "";
     aFicDspreven     = "";

     fAntFecha        = @;

     &enContaFac      = 0;
     &Tempo           = "";
     &aTmp1           = "";
     &aTmp2           = "";
     &enModiFac       = 0;
     &enNoModif       = 0;
     &efFecha         = @;
     &cCodMonTrab     = "";
     &qSufac          = "";
     &eaDocu          = "";
     &eaUsuario       = "";
     &eaFecha         = "";
     &qSerie          = "";
     &qSerieF         = "";
     &qAlbar          = 0;
     &qFecha          = @;
     &qAbono          = "";
     &enForma         = 0;
     &enDDocum        = 0;
     &enHDocum        = 0;
     &enSwMenu        = 0;
     &eaNombre        = "";
     &eaCif           = "";
     &eaToIncidencia  = "";

     &decim_divisa    = 0; || Decimales de la divisa
     &precio_divisa   = 0; || Decimales del precio en divisa
     &nDeci_Div       = 0; || Decimales en Totales de la divisa
     &nDeci_PreVis    = 0; || Decimales en el Precio visual. (lineas)
     &nDeci_ImpVis    = 0; || Decimales en el Importe visual. (lineas)
     &nDeci_Base      = 0; || Decimales en Totales de la moneda base
     &decim_base      = 0;  || Decimales de la moneda base
     &precio_base     = 0; || Decimales del precio en moneda base
     &nDeci_PreDiv    = 0;
     &nDeci_PreBase   = 0;
     &nDeci_BaseMx    = 0;
     &nDeci_PreBaseMx = 0;
     &nDeci_TotVis    = 0;
     &nDeci_TotNoVis  = 0;
     &aDivisa         = "";

     &eaSerieCompra   = "";
     &eaEmpDspreven   = "";
     &eaCodProveedor  = "";
     &eaCodEnlace     = "";
     &eaZona          = "";
     &eaCifProveedor  = "";
     &enAnyo          = 0;
     &enMesFac        = 0;
     &eaSer           = "";
     &enNum           = 0;
     &eaOrden         = "";
     &eaReimp         = "";
     &eaImprime       = "";
|FIN;

|PROCESO Param_Divisas; |TIPO 0;
     |ABRE #agifa321;
     |LEE 000#agifa321.p;
     |SI FSalida ! 0; |DDEFECTO #agifa321; |FINSI;
     |CIERRA #agifa321;

     |ABRE #agifa324;
     #agifa324#0 = #agifa321#9;
     |LEE 001#agifa324.=;   || Leo la moneda base

     decim_base   = #agifa324#7;    ||Asigno los decimales de la moneda base
     precio_base  = #agifa324#9;   ||Asigno los decimales del precio en la mon. base
     #agifa077#61 = #agifa321#9;
     #agifa077#62 = #agifa324#3;

     #agifa324#0 = #agifa077#56;
     |LEE 001#agifa324.=;   || Leo la divisa
     |CIERRA #agifa324;

     decim_divisa  = #agifa324#7;  || Decimales de la divisa
     precio_divisa = #agifa324#9;  || Decimales en el precio de la divisa
     nDeci_Div     = decim_divisa;   || Decimales de la divisa
     nDeci_PreDiv  = precio_divisa;  || Decimales en el precio de la divisa

     |SI #agifa077#58 = "B";   ||Si trabajo con la moneda base ...
          nDeci_PreVis    = precio_divisa;
          nDeci_ImpVis    = decim_divisa;
          nDeci_Base      = decim_base;
          nDeci_PreBase   = precio_base;
          nDeci_BaseMx    = decim_base;
          nDeci_PreBaseMx = precio_base;
          nDeci_TotVis    = decim_base;
          nDeci_TotNoVis  = decim_divisa;
     |SINO;
          nDeci_PreVis    = precio_base;
          nDeci_ImpVis    = decim_base;
          nDeci_Base      = decim_base;
          nDeci_PreBase   = precio_base;

          |SI #agifa077#57 = 1;
              nDeci_BaseMx    = decim_base;    || sin triangulacion
              nDeci_PreBaseMx = precio_base;
         |SINO;
              nDeci_BaseMx    = #agifa321#10;  || con triangulacion
              nDeci_PreBaseMx = #agifa321#11;
         |FINSI;
         nDeci_TotVis     = decim_divisa;
         nDeci_TotNoVis   = decim_base;
     |FINSI;
|FPRC;

|PROCESO CreaCabAlbaran;
      #agifa077#50 = qSerieF;
      enSwMenu = 1; |HAZ ContaAC7;

      #agifa112#0 = eaCodProveedor;
      |LEE 000#agifa112.=;

      #agifa077#56 = #agifa112#81;
      #agifa077#58 = #agifa112#82;
      |HAZ Param_Divisas;

      #agifa077#3  = #agifa112#0;
      #agifa077#4  = #agifa112#1;
      #agifa077#5  = #agifa112#24;
      #agifa077#7  = #agifa112#25;
      #agifa077#8  = #agifa112#17;
      #agifa077#29 = #48#1;
      #agifa077#30 = #48#2;
      #agifa077#31 = #48#3;
      #agifa077#32 = #agifa112#41;
      #agifa077#33 = #48#4;
      #agifa077#34 = "AS";
      #agifa077#36 = #agifa112#58;
      #agifa077#52 = #agifa112#78;
      #agifa077#57 = #agifa324#3;
      #agifa077#72 = #agifa112#87;
      |GRABA 020#agifa077.b;
|FPRC;

|PROCESO CierraCabAlbaran;
     qSerie = #agifa077#50;
     qAlbar = #agifa077#0;
     qFecha = #agifa077#1;
     qAbono = #agifa077#42;

     |GRABA 020#agifa077.a;
     |LIBERA #agifa077;

     #agifa730#0 = #agifa077#50;
     #agifa730#1 = #agifa077#0;
     |GRABA 020#agifa730.n;

     #agifa112#0 = #agifa077#3;
     |LEE 101#agifa112.=;
     |SI FSalida = 0;
         aAlfa = #agifa077#1; |QBF aAlfa;
         aAlfa = aAlfa % 0402; nCalc = aAlfa;
         nCalc = nCalc + 26;
         #agifa112#nCalc# = #agifa112#nCalc# + #agifa077#65;
         |GRABA 020#agifa112.a;
         |LIBERA #agifa112;
     |FINSI;
|FPRC;

|PROCESO autfm003;
     |SI nLinea = 999;
         |HAZ CierraCabAlbaran;

         nLinea = 0;
     |FINSI;

     |SI nLinea = 0;
         |HAZ CreaCabAlbaran;
     |FINSI;

     |ABRE #agifa019;
     #agifa019#0 = #autfm003@#5;
     |LEE 101#agifa019.=;
     |SI FSalida ! 0;
         |DDEFECTO #agifa019;
         #agifa019#0 = #autfm003@#5;
         #agifa019#1 = #autfm003@#6;

         |ABRE #maesm060@;
         #maesm060@#0 = #agifa019#0;
         |LEE 000#maesm060@.=;
         |SI FSalida ! 0;  |DDEFECTO #maesm060@;  |FINSI;
         |CIERRA #maesm060@;

         #agifa019#2  = #maesm060@#3;

         |GRABA 020#agifa019.b;
     |FINSI;

     #agifa019#30 = #maesm060@#8;

     |GRABA 020#agifa019.a;
     |LIBERA #agifa019;
     |CIERRA #agifa019;

     nLinea = nLinea + 1;

     |DDEFECTO #agifa080;
     #agifa080#27 = #agifa077#50;
     #agifa080#0  = #agifa077#0;
     #agifa080#1  = nLinea;
     #agifa080#2  = #autfm003@#5;
     #agifa080#3  = 1;
     #agifa080#4  = #autfm003@#6;
     #agifa080#5  = 1;
     #agifa080#6  = #autfm003@#14;
     #agifa080#7  = #autfm003@#14;
     #agifa080#11 = #agifa019#30;
     #agifa080#15 = #agifa077#3;
     |GRABA 020#agifa080.b;

     |SI #agifa077#58 = "D";
         #agifa080#6  = #agifa080#30 / #agifa077#57 * #agifa077#62; || Recalculo precio en mon. base
         #agifa080#32 = #agifa080#6;

         nPPort1 = #agifa080#42 / #agifa077#57 * #agifa077#62;
         nPPort2 = #agifa080#42;
     |SINO;
         #agifa080#30 = #agifa080#6 / #agifa077#62 * #agifa077#57; || Recalculo precio en divisa
         #agifa077#32 = #agifa077#30;

         nPPort1 = #agifa080#42;
         nPPort2 = #agifa080#42 / #agifa077#62 * #agifa077#57;
     |FINSI;

     |SI #agifa019#196 = 2;
          #agifa080#7 = #agifa080#36 * #agifa080#6;
          nImpDiv = (#agifa080#36 * #agifa080#30) ! nDeci_PreDiv;
     |SINO;
          #agifa080#7 = #agifa080#5 * #agifa080#6;
          nImpDiv = (#agifa080#5  * #agifa080#30) ! nDeci_PreDiv;
     |FINSI;

     #agifa080#9  = ((#agifa080#7    < #agifa080#8) < #agifa080#26) < #agifa080#45;
     #agifa080#31 = ((nImpDiv < #agifa080#8) < #agifa080#26) < #agifa080#45;

     |SI #agifa019#196 = 2;
         #agifa080#9  = #agifa080#9  + (#agifa080#36 * nPPort1);  || Portes Linea
         #agifa080#31 = #agifa080#31 + (#agifa080#36 * nPPort2);  || Portes Linea
     |SINO;
         #agifa080#9  = #agifa080#9  + (#agifa080#5  * nPPort1);  || Portes Linea
         #agifa080#31 = #agifa080#31 + (#agifa080#5  * nPPort2);  || Portes Linea
     |FINSI;

     |SI #agifa077#58 = "D";     || Si mon. de trabajo es divisa ...
         #agifa080#33 = #agifa080#9;   || Importe visual, en mon. base
     |SINO;               || Si mon. de trabajo es mon. base ...
         #agifa080#33 = #agifa080#31;  || Importe visual, en divisa
     |FINSI;

     |GRABA 020#agifa080.a; |LIBERA #agifa080;

     #dsm00083#0 = "A";
     #dsm00083#1 = #agifa080#27;
     #dsm00083#2 = #agifa080#0;
     #dsm00083#3 = #agifa080#1;
     #dsm00083#4 = 1;
     #dsm00083#5 = #autfm003@#13;
     |GRABA 020#dsm00083.n;
     |LIBERA #dsm00083;

     #dsm00083#0 = "A";
     #dsm00083#1 = #agifa080#27;
     #dsm00083#2 = #agifa080#0;
     #dsm00083#3 = #agifa080#1;
     #dsm00083#4 = 2;
     #dsm00083#5 = #autfm003@#12;
     |GRABA 020#dsm00083.n;
     |LIBERA #dsm00083;

     eaDocu = "agifa080"; enForma = 1; |HAZ StockEntra;
     eaDocu = "agifa077"; enForma = 1; |HAZ StockEntra;

     |HAZ CalCabAgifa077;
|FPRC;

|DEFBUCLE autfm003;
     #autfm003@#1005;
     ;
     enAnyo, enMesFac, eaZona, eaCifProveedor, 0001;
     enAnyo, enMesFac, eaZona, eaCifProveedor, 9999;
     ;
     NULL, autfm003;
|FIN;

|PROCESO AdjFactura;
     qSufac     = #agifa077#54;
     aAlfa      = "agifa229;otpz0006" + Usuario + ",NoVtos";
     |SUB_EJECUTA_NP aAlfa;       ||Facturar Albaranes
     |CIERRA #agifa730;

     aAlfa = "ta" + Usuario; |NOME_DAT #agifa730#aAlfa#;
     |DELINDEX #agifa730; |ABRE #agifa730;

     #agifa077#50 = qSerie;
     #agifa077#0  = qAlbar;
     |LEE 000#agifa077.=;

|FPRC;

|PROCESO CreaRecibos;
     #agifa079#66 = #agifa077#51;
     #agifa079#0  = #agifa077#39;
     |LEE 101#agifa079.=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     #agifa112#0 = #agifa079#2;
     |LEE 000#agifa112.=;
     |SI FSalida = 0;
         #agifa079#12 = #agifa112#10;
     |FINSI;

     |PARA j = 33; |SI j [ 44; |HACIENDO j=j+1;   #agifa079#j# = 0; |SIGUE;
     |PARA j = 68; |SI j [ 73; |HACIENDO j=j+1;   #agifa079#j# = "01.01.0000"; |SIGUE;
     |PARA j = 129; |SI j [ 134; |HACIENDO j=j+1; #agifa079#j# = "01.01.0000"; |SIGUE;

     |ABRE #agifa091;
     #agifa091#0 = #agifa079#11;
     |LEE 000#agifa091.=;
     |CIERRA #agifa091;

     |HAZ VenciComp;

     |ABRE #pycm0072@;
     #pycm0072@#0 = eaCifProveedor;
     #pycm0072@#1 = eaZona;
     |LEE 000#pycm0072@.=;
     |SI FSalida ! 0;  |DDEFECTO #pycm0072@;  |FINSI;
     |CIERRA #pycm0072@;

     #agifa079#12 = #pycm0072@#4;
     aAlfa = #pycm0072@#4;  |QBF aAlfa;
     |SI aAlfa = "";
         #agifa079#12 = #pycm0072@#9;
     |FINSI;

     |GRABA 020#agifa079.a;
     |LIBERA #agifa079;

     aSerieFac = #agifa079#66;
     nNumFac   = #agifa079#0;

     |DDEFECTO #dsz99984;
     #dsz99984#0  = #agifa079#0;
     #dsz99984#17 = #agifa079#66;
     |PARA i = 1;|SI i [ 12; |HACIENDO i = i + 1;
           nCamImp = i + 32;
          |SI #agifa079#nCamImp# ! 0; nNumRecibos = i; |FINSI;
     |SIGUE;

     |HAZ AbreRecCompra;
     |PARA i = 1;|SI i [ 12; |HACIENDO i = i + 1;
          nCamImp = i + 32;
          |SI #agifa079#nCamImp# = 0; |O i > 12; |SAL; |FINSI;

          |SI i [ 6;
               nCamFec = i + 67;
          |SINO;
               nCamFec = i + 121;
          |FINSI;

          #dsz99984#7 = #agifa079#1;
          #dsz99984#8 = #agifa079#nCamFec#;
          #dsz99984#9 = #agifa079#nCamImp#;
          #dsz99984#1 = i;
          #dsz99984#2 = #agifa079#2;
          #dsz99984#3 = #agifa079#3;
          #dsz99984#4 = #agifa079#9;
          #dsz99984#12 = #agifa079#12;
          #dsz99984#18 = #agifa091#69;      ||Tipo de Recibo
          #dsz99984#21 = #agifa091#69;
          #dsz99984#23 = nNumRecibos;
          #dsz99984#24 = #agifa091#0;
          #dsz99984#25 = #agifa091#1;

          |ABRE #agifa177;
          #agifa177#0 = #dsz99984#18;
          |LEE 000#agifa177.=;
          |CIERRA #agifa177;

          #dsz99984#15 = #agifa177#3;      ||Cuenta
          |HAZ RecCompra;
      |SIGUE;

      |HAZ CierraRecCompra;
|FPRC;

|PROCESO EnlaceCont;
     |ABRE #agifa704;
     #agifa704#0 = eaCodEnlace;
     |LEE 000#agifa704.=;
     |SI FSalida ! 0;  |DDEFECTO #agifa704;  |FINSI;
     |CIERRA #agifa704;

     nSwLin = 0;
     |SI #agifa704#8 = "S";
         nSwLin = 1;
     |FINSI;

     aAlfa = "ac" + Usuario; |NOME_DAT #agifa716#aAlfa#;
     |DELINDEX #agifa716; |ABRE #agifa716;

     |DDEFECTO #agifa716;
     #agifa716#15 = eaCodEnlace;
     #agifa716#0  = 1;
     #agifa716#11 = "A";
     #agifa716#12 = 5;

     |SI nSwLin = 0;
         #agifa716#13 = "agifa079";
         #agifa716#14 = 66;
     |SINO;
         #agifa716#13 = "agifa080";
         #agifa716#14 = 28;
     |FINSI;

     #agifa716#3 = #agifa079#66;
     #agifa716#4 = #agifa079#66;
     |GRABA 020#agifa716.n;
     |LIBERA #agifa716;

     |DDEFECTO #agifa716;
     #agifa716#15 = eaCodEnlace;
     #agifa716#0  = 2;
     #agifa716#11 = "N";
     #agifa716#12 = 6;
     |SI nSwLin = 0;
         #agifa716#13 = "agifa079";
         #agifa716#14 = 0;
     |SINO;
         #agifa716#13 = "agifa080";
         #agifa716#14 = 25;
     |FINSI;
     #agifa716#5 = #agifa079#0;
     #agifa716#6 = #agifa079#0;
     |GRABA 020#agifa716.n;
     |LIBERA #agifa716;
     |CIERRA #agifa716;

     eaNombre = #agifa112#1;
     eaCif    = #agifa112#9;

     aAlfa = eaCodEnlace; |QBF aAlfa;
     aAlfa = aAlfa + ("*" + enAnyo);
     aAlfa = "agifa711.tab;" + aAlfa + ",CAJA";
     |SUB_EJECUTA_NP aAlfa;

     eaNombre = "";
     eaCif    = "";
|FPRC;

|PROCESO Emision;
     eaSer     = #agifa079#66;
     enNum     = #agifa079#0;
     eaOrden   = "S";
     eaReimp   = #agifa079#45;
     eaImprime = "CrystalReport";
     |SUB_EJECUTA_NP "agifa056";
|FPRC;

|PROGRAMA;
     aAlfa1 = PARAMETRO - "EMISION";
     |SI FSalida ! 0;
         aAlfa = aAlfa1 % 105;  aSerieFac = aAlfa;
         aAlfa = aAlfa1 % 606;  nNumFac   = aAlfa;

         |ABRE #agifa079;
         #agifa079#66 = aSerieFac;
         #agifa079#0  = nNumFac;
         |LEE 101#agifa079.=;
         |SI FSalida = 0;
             |HAZ Emision;
         |FINSI;
         |CIERRA #agifa079;

         |ACABA;
     |FINSI;

     eaEmpDspreven  = PARAMETRO % 105;
     eaSerieCompra  = PARAMETRO % 605;
     eaCodEnlace    = PARAMETRO % 1104;
     eaCodProveedor = PARAMETRO % 1506;
     eaZona         = PARAMETRO % 2105;
     aAlfa          = PARAMETRO % 2604;  enAnyo   = aAlfa;
     aAlfa          = PARAMETRO % 3002;  enMesFac = aAlfa;
     eaCifProveedor = PARAMETRO % 3215;

     |ABRE #agifa007;
     #agifa007#26 = eaSerieCompra;
     |LEE 000#agifa007.=;
     |SI FSalida ! 0;
         |MENAV "0000No existe la serie de compra.";
         |CIERRA #agifa007;
         |ACABA;
     |FINSI;
     |CIERRA #agifa007;

     |ABRE #agifa704;
     #agifa704#0 = eaCodEnlace;
     |LEE 000#agifa704.=;
     |SI FSalida ! 0;
         |MENAV "0000No existe Definici¢n enlace contable.";
         |CIERRA #agifa704;
         |ACABA;
     |FINSI;
     |CIERRA #agifa704;

     |DBASS aBase;  |QBF aBase;
     aDefDspreven = aBase + "dspreven/def/";
     aFicDspreven = aBase + "dspreven/fich/";

     aAlfa = aDefDspreven + "maesm060.mas";
     |CARGA_DEF aAlfa, maesm060@;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     aAlfa = aDefDspreven + "pycm0072.mas";
     |CARGA_DEF aAlfa, pycm0072@;
     |SI FSalida ! 0;  |ACABA;  |FINSI;


     |PATH_DAT #maesm060@#aFicDspreven#;
     |PATH_DAT #pycm0072@#aFicDspreven#;

     aFicDspreven = aBase + "dspreven/fich/" + eaEmpDspreven + "/";

     aAlfa = aDefDspreven + "autfm002.mas";
     |CARGA_DEF aAlfa, autfm002@;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     aAlfa = aDefDspreven + "autfm003.mas";
     |CARGA_DEF aAlfa, autfm003@;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |PATH_DAT #autfm002@#aFicDspreven#;
     |PATH_DAT #autfm003@#aFicDspreven#;

     qSerieF = eaSerieCompra;

     aAlfa = "otpz0006" + Usuario;
     |NOME_DAT #agifa730#aAlfa#;
     |DELINDEX #agifa730;

     |ABRE #agifa730;
     |ABRE #agifa077;
     |ABRE #agifa080;
     |ABRE #agifa112;
     |ABRE #agifa079;
     |ABRE #agifa079;
     |ABRE #dsm00083;

     nLinea = 0;
     |BUCLE autfm003;

     |HAZ CierraCabAlbaran;

     |HAZ AdjFactura;
     |HAZ CreaRecibos;
     |HAZ EnlaceCont;

     #agifa079#66 = aSerieFac;
     #agifa079#0  = nNumFac;
     |LEE 101#agifa079.=;
     |SI FSalida = 0;
         aAlfa = #agifa079#1; |QBF aAlfa;
         aAlfa = aAlfa % 0704;
         |ABRE #agifa722;
         #agifa722#0 = "FC";
         #agifa722#1 = aSerieFac;
         #agifa722#2 = nNumFac;
         #agifa722#3 = aAlfa;
         |LEE 000#agifa722.];
         |SI FSalida = 0; |Y #agifa722#0 = "FC"; |Y #agifa722#1 = aSerieFac;
          |Y #agifa722#2 = nNumFac; |Y #agifa722#3 = aAlfa;
             #agifa079#48 = "S";
             |GRABA 020#agifa079.a;
         |FINSI;
         |CIERRA #agifa722;
         |LIBERA #agifa079;
     |FINSI;

     |ABRE #autfm002@;
     #autfm002@#0 = enAnyo;
     #autfm002@#1 = enMesFac;
     #autfm002@#2 = eaZona;
     #autfm002@#3 = eaCifProveedor;
     |LEE 101#autfm002@.=;
     |SI FSalida = 0;
         #autfm002@#8  = #agifa079#66;
         #autfm002@#9  = #agifa079#0;
         #autfm002@#10 = "0,185,0;255,255,255";
         |GRABA 020#autfm002@.a;
         |LIBERA #autfm002@;
     |FINSI;
     |CIERRA #autfm002@;

     |CIERRA #agifa730;  |DELINDEX #agifa730;
     |CIERRA #agifa112;
     |CIERRA #agifa079;
     |CIERRA #agifa077;
     |CIERRA #agifa080;
     |CIERRA #dsm00083;

     |DESCARGA_DEF #autfm003@;
     |DESCARGA_DEF #autfm002@;
     |DESCARGA_DEF #pycm0072@;
     |DESCARGA_DEF #maesm060@;

     |HAZ Emision;
|FPRO;
