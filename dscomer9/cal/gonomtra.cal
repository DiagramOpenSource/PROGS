|FICHEROS;
     gonomtra #0;
     goptrbli #2;
     aomentra@ #1;
     aomobras@ #3;
     gotrarel;      || lineas relacion codigos trab gestion-nominas
|FIN;

|VARIABLES;
     &efFecha  = @;
     aDef      = "";
     aPath     = "";
     aPatf     = "";
     aEmp      = "";
     fFechaIni = @;
     fFechaFin = @;
     fFechaParte = @;
     nFechaIni = 0;
     nFechaFin = 0;
     nFechaParte = 0;

     nEmpr = 0;
     nTrab = 0;
|FIN;

|PROCESO BuscaCodigo;
     fFechaIni = #gotrarel#4;
     fFechaFin = #gotrarel#5;
     fFechaParte = #2#2;
     nFechaIni = fFechaIni;
     nFechaFin = fFechaFin;
     |SI nFechaFin = -1;
          fFechaFin = "31.12.2099";
          nFechaFin = fFechaFin;
     |FINSI;
     nFechaParte = fFechaParte;

     |SI nFechaParte ] nFechaIni; |Y nFechaParte [ nFechaFin;
          nEmpr = #gotrarel#1;
          nTrab = #gotrarel#2;
     |FINSI;
|FPRC;

|DEFBUCLE BuscaCodigo;
     #gotrarel;
     ;
     #2#4, "          ";
     #2#4, "zzzzzzzzzz";
     ;
     NULL, BuscaCodigo;
|FIN;

|PROCESO goptrbli;
     #3#2 = #2#0
     |LEE 000#3=;
     |SI FSalida ! 0;
          |MENAV "0000No existe Localizador...";
     |SINO;
          nEmpr = 0;
          nTrab = 0;
          |BUCLE BuscaCodigo;
          |DDEFECTO #1;
          #1#0 = #2#2;   ||Fecha
          #1#1 = #3#0;   ||Obra

          || #1#2 = aEmp;   ||Emp
          || #1#3 = #2#4;   ||Tra

          #1#2 = nEmpr;   ||Emp
          #1#3 = nTrab;   ||Tra

          #1#4 = #2#26;  ||T.h
          #1#7 =  1;     ||Subobra
          |LEE 101#1=;
          |SI FSalida ! 0; |GRABA 020#1b; |FINSI;
          #1#5 = #1#5 + #2#11;   ||Uni
          #1#6 =  0;             ||Otros
          |GRABA 020#1a;
          |LIBERA #1;
     |FINSI;
|FPRC;

|DEFBUCLE goptrbli;
     #2#2;
     ;
     "     ", 000000, #0#0, 0001, #0#2;
     "zzzzz", 999999, #0#1, 0001, #0#3;
     ;
     NULL, goptrbli;
|FIN;

|PROCESO Tipo3; |TIPO 3;
     |DFICE aEmp;
     aEmp = aEmp % -205;

     |DBASS aPath;
     |QBF aPath;
     aPath = aPath + "aginom/";
     aPatf = aPath + "fich/";

     aDef = aPath + "def/" + "aomentra";
     |CARGA_DEF aDef, aomentra@;
     |SI FSalida = 0;
          aDef = aPath + "def/" + "aomobras";
          |CARGA_DEF aDef, aomobras@;
          |SI FSalida = 0;
               |PATH_DAT #1 aPatf;
               |PATH_DAT #3 aPatf;
               |ABRE #1;
               |ABRE #3; |SELECT #3#3;
               |BUCLE goptrbli;
               |CIERRA #3;
               |CIERRA #1;
               |DESCARGA_DEF #aomobras@;
          |SINO;
               aDef = "0000Error al cargar " + aDef;
               |MENAV aDef;
          |FINSI;
          |DESCARGA_DEF #aomentra@;
     |SINO;
          aDef = "0000Error al cargar " + aDef;
          |MENAV aDef;
     |FINSI;
|FPRC;
