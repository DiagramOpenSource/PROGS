     PEDIDOS

|FICHEROS;
     ialbz008 #0;
     agifa019 #19;
     agifa073 #73;
     agifa104 #104;
     agifa142 #142;
     referec@;
     referel@;
|FIN;

|VARIABLES;
     &aDivisa = "";
     &aMoneda = "";
     &eaAlfa = "";
     &eaRegNuevo = "";
     &eaProg = "";
     &eaEstadoBtn = "";
     &eaTmp = "";
     &eaPestana = "";
     &eaSwCarga2 = "";
     &eaBtnAcota2 = "";
     &eaSer2 = "";
     &eaNum2 = "";
     &eaLin2 = "";

     aProg = "";
     aTmp = "";
     aAlfa = "";
     nRango = 0;
     nPos1 = 0;
     nPos2 = 0;
     nPestana = 0;
     aPath = "";
     nPos = 0;
     nDDir = 0;
     nHDir = 0;
     aSwPdte = "";
     aSwServi = "";
     nOk = 0;
     nNume = 0;
     nLin = 0;
     aSer = "";
     aNum = "";
     nSw = 0;

     nBtnPdtSer2 = 0;
     nBtnServidos2 = 0;
     nBtnTodos2 = 0;
     nBtnNuevo2 = 0;
     nBtnImpre2 = 0;
     nBtnSalir2 = 0;
     nGridC = -1;
     nGridL = -1;
     nPanta = 0;
     nEvtFoco = 0;
     nBtnModifi12 = -1;
     nBtnAnula12 = -1;
     nBtnServir12 = -1;
     nBtnVerSer12 = -1;
     nBtnVerSer22 = -1;
     nBtnIrPed32 = -1;
     aTipo = "";
     nSwRefre = 0;
|FIN;

|PROCESO ActLineal2;
     |SI #0#0 ! "000000";
          |ABRE #104;
          #104#25 = aSer;
          #104#0 = aNum;
          |LEE 000#104=;
          |SI FSalida = 0;
               aSwPdte = "N";
               |ABRE #19;
               |BUCLELIN 2 LinPed #104;
               |CIERRA #19;
               nSw = 0;
               |SI eaBtnAcota2 = "1"; |Y aSwPdte = "S"; nSw = 1; |FINSI;
               |SI eaBtnAcota2 = "2"; |Y aSwPdte = "N"; nSw = 1; |FINSI;
               |SI eaBtnAcota2 = "3"; nSw = 1; |FINSI;
               #referec@#25 = #104#25;
               #referec@#0 = #104#0;
               |LEE 000#referec@.=;
               |SI nSw = 1;   || si debe de estar
                    |SI FSalida = 0;
                         |BORRA 020#referec@.c;
                    |FINSI;
                    |SALVA_DATOS #104;
                    |REPON_DATOS #referec@;
                    |GRABA 000#referec@.n;
                    |LEE 000#referec@.=;
               |SINO;         || no debe de estar
                    |SI FSalida = 0
                         |BORRA 020#referec@.c;
                    |FINSI;
               |FINSI;
          |FINSI;
          |CIERRA #104;
     |FINSI;
     |REFRESCACONTROL nGridC;
|FPRC;
//----------------//
|PROCESO Existe2;
     |ABRE #104;
     #104#25 = #referec@#25;
     #104#0 = #referec@#0;
     |LEE 000#104=;
     aAlfa = FSalida;
     |CIERRA #104;
     FSalida = aAlfa;
|FPRC;

|PROCESO MiraPdte;
     |HAZ LinPed;
|FPRC;

|DEFBUCLE MiraPdte;
     #73#1;
     ;
     eaSer2, nNume, nLin;
     eaSer2, nNume, nLin;
     ;
     NULL, MiraPdte;
|FIN;

|PROCESO BtnIrPed32;
     |HAZ Existe2;
     |SI FSalida ! 0; |ACABA; |FINSI;

     |HAZ CogePosicion2;
     nNume = eaNum2; nLin = eaLin2;
     aSwPdte = "N";
     |ABRE #19;
     |BUCLE MiraPdte;
     |CIERRA #19;
     |SI aSwPdte = "N";
          |HAZ BtnServidos2;
     |SINO;
          |HAZ BtnPdtSer2;
     |FINSI;
     |PULSATECLA;        || sin esto no se posiciona
     |HAZ Posiciona2;
|FPRC;

|PROCESO BtnVerSer22;
     |HAZ BtnVerSer12;
|FPRC;

|PROCESO BtnModifi12;
     |HAZ Existe2;
     |SI FSalida ! 0; |ACABA; |FINSI;

     aProg = "*dszx0003;"+ "M" + #referec@#25 + (("000000" + #referec@#0)%-106);
     |SUB_EJECUTA aProg;
     aSer = #referec@#25;
     aNum = #referec@#0;
     aTipo = "M"; |HAZ ActLineal2;
|FPRC;

|PROCESO MiraSer;
     |SI #73#43 ! 0; |O #73#49 ! 0;
          aSwServi = "S";
          |ERROR10;
     |FINSI;
|FPRC;

|PROCESO BtnAnula12;
     |HAZ Existe2;
     |SI FSalida ! 0; |ACABA; |FINSI;

     |ABRE #104;
     #104#25 = #referec@#25;
     #104#0 = #referec@#0;
     |LEE 000#104=;
     |SI FSalida = 0;
         aSwServi = "N";
         |ABRE #19;
         |BUCLELIN 2 MiraSer #104;
         |CIERRA #19;
     |FINSI;
     |CIERRA #104;
     |SI aSwServi = "S";
          |MENAV "0000Pedido con unidades servidas...";
          |ACABA;
     |FINSI;

     aAlfa = "0000NAnular Pedido:" + #referec@#25 +"/"+ (("000000" + #referec@#0)%-106);
     |CONFI aAlfa;
     |SI FSalida = 0;
          aProg = "*dszx0003;"+ "B" + #referec@#25 + (("000000" + #referec@#0)%-106);
          |SUB_EJECUTA aProg;
          aSer = #referec@#25;
          aNum = #referec@#0;
          aTipo = "B"; |HAZ ActLineal2;
     |FINSI;
|FPRC;

|PROCESO BtnServir12;
     |HAZ Existe2;
     |SI FSalida ! 0; |ACABA; |FINSI;

     aProg = "*dszx0003;"+ "A" + #referec@#25 + (("000000" + #referec@#0)%-106);
     |SUB_EJECUTA aProg;
     aSer = #referec@#25;
     aNum = #referec@#0;
     aTipo = "A"; |HAZ ActLineal2;
|FPRC;

|PROCESO BtnVerSer12;
     |HAZ Existe2;
     |SI FSalida ! 0; |ACABA; |FINSI;

     aProg = "*dszx0003;"+ "S" + #referec@#25 + (("000000" + #referec@#0)%-106);
     |SUB_EJECUTA aProg;
|FPRC;
//----------------//
|PROCESO CargaDatos2;
     |SI eaSwCarga2 ! "S";
          |HAZ DestruyePan2;
          |HAZ Descarga2;
          |SI #0#0 = "000000";
               |REFERENCIA_DEF referec@, #104;
          |FINSI;
          |REFERENCIA_DEF referel@, #73;
          |HAZ PintaPanta2;
     |FINSI;
|FPRC;

|PROCESO CogePosicion2;
     eaSer2 = #referec@#25;
     eaNum2 = ("000000" + #referec@#0) % -106;
     eaLin2 = ("000" + #referel@#1) % -103;
|FPRC;

|PROCESO Posiciona2;
     #referel@#28 = eaSer2;
     #referel@#0 = eaNum2;
     #referel@#1 = eaLin2;
     |LEE 000#referel@.=;

     #referec@#25 = eaSer2;
     #referec@#0 = eaNum2;
     |LEE 000#referec@.=;
     |REFRESCACONTROL nGridC;

     |SI eaPestana = "2"; |FOCOTECLADO nGridC; |FINSI;
|FPRC;

|PROCESO EstadoBtn2;
     |SI #0#0 = "000000";
          |ESTADO_CONTROL nBtnPdtSer2, "DISABLE";
          |ESTADO_CONTROL nBtnServidos2, "DISABLE";
          |ESTADO_CONTROL nBtnTodos2, "DISABLE";
     |SINO;
          |SI eaBtnAcota2 = "1";
               |ESTADO_CONTROL nBtnPdtSer2, "DISABLE";
               |ESTADO_CONTROL nBtnServidos2, "ENABLE";
               |ESTADO_CONTROL nBtnTodos2, "ENABLE";
          |FINSI;
          |SI eaBtnAcota2 = "2";
               |ESTADO_CONTROL nBtnPdtSer2, "ENABLE";
               |ESTADO_CONTROL nBtnServidos2, "DISABLE";
               |ESTADO_CONTROL nBtnTodos2, "ENABLE";
          |FINSI;
          |SI eaBtnAcota2 = "3";
               |ESTADO_CONTROL nBtnPdtSer2, "ENABLE";
               |ESTADO_CONTROL nBtnServidos2, "ENABLE";
               |ESTADO_CONTROL nBtnTodos2, "DISABLE";
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO BtnPdtSer2;
     eaBtnAcota2 = "1";
     eaSwCarga2 = "N";
     |HAZ EventoFoco2;
|FPRC;

|PROCESO BtnServidos2;
     eaBtnAcota2 = "2";
     eaSwCarga2 = "N";
     |HAZ EventoFoco2;
|FPRC;

|PROCESO BtnTodos2;
     eaBtnAcota2 = "3";
     eaSwCarga2 = "N";
     |HAZ EventoFoco2;
|FPRC;

|PROCESO BtnNuevo2;
     eaProg = "*dszx0003;"+ "N" + "     000000" + #0#0 + (("0000" + #0#1) % -104);
     |HAZ EjecutaProg;
     aSer = eaAlfa % 105;
     aNum = eaAlfa % 606;
     aTipo = "N"; |HAZ ActLineal2;
|FPRC;

|PROCESO BtnImpre2;
     |HAZ Existe2;
     |SI FSalida ! 0; |ACABA; |FINSI;

     aProg = "*dszx0003;"+ "I" + #referec@#25 + (("000000"+#referec@#0)%-106);
     |SUB_EJECUTA aProg;
|FPRC;

|PROCESO EventoC2;
     |SI FSalida = 1; |O FSalida = 2;
          |LEE 000#referec@.=;
          |SI FSalida = 0;
               aDivisa = #referec@#35;
               aMoneda = #referec@#37;
               |HAZ Divisas104;
          |FINSI;
          |REFRESCACONTROL nGridL;
     |FINSI;
|FPRC;

|PROCESO EventoL2;
     |SI FSalida = 1; |O FSalida = 2;
          |LEE 000#referel@.=;
     |FINSI;
|FPRC;

|PROCESO EventoFoco2;
     |HAZ CargaDatos2;
     eaPestana = "2";
|FPRC;

|PROCESO CargaTmp2;
     nOk = 0;
     |SI aSwPdte = "S"; |Y eaBtnAcota2 = "1"; nOk = 1; |FINSI;
     |SI aSwPdte = "N"; |Y eaBtnAcota2 = "2"; nOk = 1; |FINSI;
     |SI nOk = 1; |O eaBtnAcota2 = "3";
          |SALVA_DATOS #104;
          |REPON_DATOS #referec@;
          |GRABA 020#referec@.n;
     |FINSI;
|FPRC;

|PROCESO Pedido;
     aSwPdte = "N";
|FPRC;

|PROCESO LinPed;
     #19#0 = #73#2;
     |LEE 000#19=;
     |SI #73#2 ! "                    "; |Y FSalida = 0;
          |SI #142#224 = "B";
               |SI #19#179 = "S";
                    |SI #73#43 = #73#5; |Y #73#49 = #73#44; |ACABA; |FINSI;
               |SINO;
                    |SI #73#43 = #73#5;  |ACABA; |FINSI;
               |FINSI;
          |SINO;
               |SI #73#43 = #73#5;  |ACABA; |FINSI;
               |SI #19#179 = "S";
                    |SI #73#49 = #73#44; |ACABA; |FINSI;
               |FINSI;
          |FINSI;
     |FINSI;
     aSwPdte = "S";
|FPRC;

|DEFBUCLE CargaTmp2;
     #104#6;
     ;
     #0#0, nDDir;
     #0#0, nHDir;
     ;
     NULL, Pedido, LinPed, CargaTmp2;
|FIN;

|PROCESO NomeDat2;
     |SI #0#0 = "000000";
          aTmp = "agifa104";
          |NOME_DAT #referec@#aTmp#;
     |SINO;
          nPos = ((8 * 1 + 1) * 100 + 8);
          aTmp = eaTmp % nPos;
          |NOME_DAT #referec@#aTmp#;
          |DELINDEX #referec@;
     |FINSI;
|FPRC;

|PROCESO MinL2;
     #referel@#28 = #referec@#25;
     #referel@#0 = #referec@#0;
     #referel@#1 = 1;
|FPRC;

|PROCESO MaxL2;
     #referel@#28 = #referec@#25;
     #referel@#0 = #referec@#0;
     #referel@#1 = 999;
|FPRC;

|PROCESO MinC2;
     #referec@#25 = "     ";
     #referec@#0 = 0;
|FPRC;

|PROCESO MaxC2;
     #referec@#25 = "zzzzz";
     #referec@#0 = 999999;
|FPRC;

|PROCESO EstadoIniBtnPan2;
     |ESTADO_CONTROL nBtnPdtSer2, eaEstadoBtn;
     |ESTADO_CONTROL nBtnServidos2, eaEstadoBtn;
     |ESTADO_CONTROL nBtnTodos2, eaEstadoBtn;
     |ESTADO_CONTROL nBtnNuevo2, eaEstadoBtn;
     |ESTADO_CONTROL nBtnImpre2, eaEstadoBtn;
     |ESTADO_CONTROL nBtnSalir2, eaEstadoBtn;
|FPRC;

|PROCESO DestruyePan2;
     |DESTRUYECONTROL nGridL;
     |DESTRUYECONTROL nGridC;

     |FIN_CONTROL_WINDOWS nEvtFoco;

     |FIN_CONTROL_WINDOWS nBtnPdtSer2;
     |FIN_CONTROL_WINDOWS nBtnServidos2;
     |FIN_CONTROL_WINDOWS nBtnTodos2;
     |FIN_CONTROL_WINDOWS nBtnNuevo2;
     |FIN_CONTROL_WINDOWS nBtnImpre2;
     |FIN_CONTROL_WINDOWS nBtnSalir2;

     |SI nBtnModifi12 ! -1; |FIN_CONTROL_WINDOWS nBtnModifi12; |FINSI;
     |SI nBtnAnula12 ! -1; |FIN_CONTROL_WINDOWS nBtnAnula12; |FINSI;
     |SI nBtnServir12 ! -1; |FIN_CONTROL_WINDOWS nBtnServir12; |FINSI;
     |SI nBtnVerSer12 ! -1; |FIN_CONTROL_WINDOWS nBtnVerSer12; |FINSI;
     nBtnModifi12 = -1;
     nBtnAnula12 = -1;
     nBtnServir12 = -1;
     nBtnVerSer12 = -1;
     |SI nBtnVerSer22 ! -1; |FIN_CONTROL_WINDOWS nBtnVerSer22; |FINSI;
     nBtnVerSer22 = -1;
     |SI nBtnIrPed32 ! -1; |FIN_CONTROL_WINDOWS nBtnIrPed32; |FINSI;
     nBtnIrPed32 = -1;
|FPRC;

|PROCESO Descarga2;
     |CIERRAT #referec@;
     |CIERRAT #referel@;
     |DESCARGA_DEF #referel@;
     |DESCARGA_DEF #referec@;
|FPRC;

|PROCESO PintaPanta2;
     |DDEF aPath;
     aAlfa = aPath + "agifa104";
     |CARGA_DEF aAlfa, referec@;
     aAlfa = aPath + "agifa073";
     |CARGA_DEF aAlfa, referel@;

     |HAZ NomeDat2;
     #referec@#0 = 0;    || Para que no salga nada si esta vacio

     |ABRET #referec@;;
     |ABRET #referel@;
     |SI #0#0 ! "000000";
          |SI #0#1 = 0;
               nDDir = 0; nHDir = 9999;
          |SINO;
               nDDir = #0#1; nHDir = #0#1;
          |FINSI;
          |ABRE #19;
          |BUCLE CargaTmp2;
          |CIERRA #19;
          |LEE 000#referec@.p;
     |FINSI;
     eaSwCarga2 = "S";

     |PINPA #2#0;  nPanta = FSalida;

     nRango = 4 + 8 + 16 + 32;
     nPos1 = 2402;
     nPos2 = 3398;
     |SI nGridL ! -1; |DESTRUYECONTROL nGridL; |FINSI;
     |LINEAL_SIMPLE #1#referel@, nRango, nPos1, nPos2, MinL2, MaxL2, EventoL2;
     nGridL = FSalida;

     nRango = 4 + 8 + 16 + 32;
     nPos1 = 0602;
     nPos2 = 2298;
     |SI nGridC ! -1; |DESTRUYECONTROL nGridC; |FINSI;
     |LINEAL_SIMPLE #1#referec@, nRango, nPos1, nPos2, MinC2, MaxC2, EventoC2;
     nGridC = FSalida;

     |CONTROL_SIMPLE nPanta, "EVENTOPAN", 0000, 0000, EventoFoco2;
     nEvtFoco = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,Ptes. Servir", 0501, 0513, BtnPdtSer2;
     nBtnPdtSer2 = FSalida;
     |CONTROL_SIMPLE nPanta, "BOTON,Servidos", 0514, 0526, BtnServidos2;
     nBtnServidos2 = FSalida;
     |CONTROL_SIMPLE nPanta, "BOTON,Todos", 0527, 0539, BtnTodos2;
     nBtnTodos2 = FSalida;
     |CONTROL_SIMPLE nPanta, "BOTON,{{193}}Nuevo", 0566, 0578, BtnNuevo2;
     nBtnNuevo2 = FSalida;
     |CONTROL_SIMPLE nPanta, "BOTON,{{205}}Imprimir", 0579, 0591, BtnImpre2;
     nBtnImpre2 = FSalida;

     |SI #0#0 ! "000000";
          |SI eaBtnAcota2 = "1";
               |CONTROL_SIMPLE nPanta, "BOTON,Modificar", 2301, 2313, BtnModifi12;
               nBtnModifi12 = FSalida;
               |CONTROL_SIMPLE nPanta, "BOTON,Anular", 2314, 2326, BtnAnula12;
               nBtnAnula12 = FSalida;
               |CONTROL_SIMPLE nPanta, "BOTON,Servir", 2327, 2339, BtnServir12;
               nBtnServir12 = FSalida;
               |CONTROL_SIMPLE nPanta, "BOTON,Ver Serv.", 2340, 2352, BtnVerSer12;
               nBtnVerSer12 = FSalida;
          |FINSI;
          |SI eaBtnAcota2 = "2";
               |CONTROL_SIMPLE nPanta, "BOTON,Ver Serv.", 2301, 2313, BtnVerSer22;
               nBtnVerSer22 = FSalida;
          |FINSI;
          |SI eaBtnAcota2 = "3";
               |CONTROL_SIMPLE nPanta, "BOTON,Ir a Pedido", 2301, 2313, BtnIrPed32;
               nBtnIrPed32 = FSalida;
          |FINSI;
     |FINSI;

     |CONTROL_SIMPLE nPanta, "BOTON,{{207}} Salir", 3488, 3498, BtnSalir;
     nBtnSalir2 = FSalida;

     |HAZ EstadoBtn2;
|FPRC;
