|FICHEROS;
     pcegz076 #0;
     dsm00003 #3;
     agifa010 #10;
     pcegw019 #19;
     agifa024 #24;
     agifa025 #25;
     pcegm051 #51;
     agifa059 #59;
     agifa066 #66;
     agifa071 #71;
     agifm001 #101;
     agifm002 #102;
     agifa134 #134;
     agifa019 #109;
     agifa142 #142;
     dsm00225 #225;
     agifa321 #321;
     agifa324 #324;
     agifa010@;
     agifa071@;
|FIN;

|INCLUYE i_varart;

|VARIABLES;
     DEBUG = 0;               ||Para mis pruebas
     &eaFic = "";
     &enDescuento1 = 0;
     &enDescuento2 = 0;
     &eaTComision = "";
     &enComision = 0;
     &Tempo = "";
     aTmp71 = "";
     aTmp10 = "";
     aTmp19 = "";
     fHoy = @;
     aAno = "";
     nTipoIVA = 0;
     aSer = "";
     aNum = "";
     nLin = 0;
     aFP = "";
     nSwArti = "";
     nSwLin = 0;
     aGuid = "";
     nSwFPago = 0;
     nDtoCab = 0;
     nSwTerm = 0;
     nGasto = 0;
     nSwTasa = 0;
     nSwImp = 0;
     nSwCli = 0;
     aRecargo = "";
     aCp = "";
     aCp1 = "";
     aCp2 = "";
     nSwNoCli = 0;
     aConTerms = "N";
     nNum = 0;
     aEsAbono = "";
     nPrime = 0;
     aSerPrime = "";
     nNumPrime = 0;
     nNumUltimo = 0;

     ||...
     RETORNO = "";
     nReg = 0;
     aSoloUno = "";
     aFicheros = "";
     nError = 0;
     nPos = 0;
     nRem = 0;
     aAlfa = "";
     aPath = "";
     aCar = "";
     nHandle = 0;
     aFic = "";
     aVal = "";
     nNivel = 0;
     aTmp = "";
     {100}Matriz = "";
     aLon = "";
     aTag = "";
     aIniTag = "N";
     aFinTag = "N";
     aIniPro = "N";
     aFinPro = "N";
     aFinTagVacio = "N";
     aIniAtri = "N";
     aTagAtri = "";
     aResul = "";
     aLetra = "";
     i = 0;
     aDato = "";
     nPosD = 0;
     nPosI = 0;
     aIniComi = "";
     aComilla = "";
     nTotalPortes = 0;
     nTipoIVAPortes = 0;
|FIN;

|PROCESO NoImportadas;
     |SALVA_DATOS #agifa010@;
     |REPON_DATOS #10;

     #24#0 = #10#3;
     |LEE 000#24=;
     |SI FSalida ! 0; |DDEFECTO #24; |FINSI;

     |PRINT;
|FPRC;

|DEFBUCLE NoImportadas;
     #agifa010@#1002;
     ;
     "     ", 000001;
     "zzzzz", 999999;
     ;
     NULL, NoImportadas;
|FIN;

|PROCESO Comi;
     enDescuento1 = #71#8;
     enDescuento2 = #71#33;
     eaTComision  = #71#16;
     eaCliente    = #10#3;
     eaArticulo   = #71#2;
     eaRepre      = #10#6;
     efFecha      = #10#1;
     enDirEnt = #10#84;
     enAlmacen = #71#3;
     |HAZ Comisiones;
     #71#10 = enComision;
|FPRC;

|PROCESO GraLinAlba;
     |ABRE #109;
     |DDEFECTO #71;
     #71#29 = #agifa071@#29;
     #71#0 = #agifa071@#0;
     #71#1 = #agifa071@#1;
     #71#2 = #agifa071@#2;

     |DDEFECTO #109;
     #109#0 = #71#2;
     |LEE 000#109=;
     |SI FSalida ! 0; |GRABA 020#109b; |FINSI;

     #71#3 = 1;
     #71#4 = #109#1;
     #71#5 = #agifa071@#5;
     #71#6 = #agifa071@#6;
     #71#8 = #agifa071@#8;
     #71#11 = #agifa071@#11;
     #71#13 = #109#2;
     #71#15 = #10#3;
     #71#16 = #10#34;
     #71#17 = #10#35;
     #71#30 = #10#53;
     #71#36 = #71#6;
     #71#38 = #71#6;
     |HAZ Comi;
     |HAZ TotalLin71;
     |HAZ CalCabAgifa010_2;
     |HAZ AcumulaAV;
     #71#26 = #agifa010@#0;  ||ser fac
     #71#30 = #agifa010@#53;   ||num fac
     |GRABA 020#71n;
     |CIERRA #109;
|FPRC;

|DEFBUCLE GraLinAlba;
     #agifa071@#1003;
     ;
     #10#52, #10#0, 001;
     #10#52, #10#0, 999;
     ;
     NULL, GraLinAlba;
|FIN;

|PROCESO GraAlbaFac;
     |SI #agifa010@#58 = "X";   || ha dado error en el chequeo
          |ACABA;
     |FINSI;

     |ABRE #24;
     #24#0 = #agifa010@#3;
     |LEE 000#24=;
     |SI FSalida = 0;
          |ABRE #3;
          #3#0 = #24#0;
          #3#1 = 1;
          |LEE 000#3=;

          aAlfa = "Creando factura:" + #agifa010@#53 + "/" + #agifa010@#0;
          |PINTA 1206, aAlfa;

          |DDEFECTO #10;
          #10#52 = #agifa010@#52;
          #10#0 = #agifa010@#0;
          |GRABA 020#10b;
          #10#1 = #agifa010@#1;
          #10#3 = #agifa010@#3;
          #10#4 = #24#2;
          #10#8 = #agifa010@#8;
          #10#10 = "S";
          #10#11 = #agifa010@#11;
          #10#12 = #agifa010@#12;

          #10#29 = #3#10;
          #10#30 = #3#2;
          #10#31 = #3#3;
          #10#33 = #3#7;
          #10#62 = #3#6;
          #10#84 = #3#1;
          |SI #3#14 = "  ";
              #10#6 = #24#25;
          |SINO;
              #10#6 = #3#14;
          |FINSI;
          #25#0 = #10#6;
          |LEE 000#25=;
          |SI FSalida ! 0; |DDEFECTO #25; |FINSI;

          #10#34 = #25#7;
          #10#35 = #24#4;
          #10#38 = "S";
          #10#40 = #24#41;
          #10#50 = #24#40;
          #10#51 = "S";
          #10#60 = #10#1;
          #10#63 = #24#15;
          #10#64 = "S";
          #10#68 = #24#192;

          #324#0 = #10#68;;
          |LEE 000#324=;
          #10#69 = #324#2;

          #10#70 = #24#193;
          #10#73 = #321#9;

          #324#0 = #10#73;
          |LEE 000#324=;
          #10#74 = #324#2;

          #10#84 = 1;

          |HAZ LimCabAgifa010;
          |BUCLE GraLinAlba;
          |SI #agifa010@#13 ! 0;   ||este importe es el %dto
               #10#5 = #agifa010@#13 * 100 / #10#15; || calcula el %dto
               #agifa010@#13 = 0;
               |HAZ LimCabAgifa010;
               |BUCLELIN 2 CalCabAgifa010_2 #10;
          |FINSI;
          #10#53 = #agifa010@#53;  ||ser fac
          #10#39 = #agifa010@#0;   ||num fac

          |GRABA 020#10a;
          |LIBERA #10;

          |DDEFECTO #134;
          #134#49 = #agifa010@#53;
          #134#0 = #agifa010@#0;
          |GRABA 020#134b;
          #134#1 = #10#1;
          #134#2 = #agifa010@#3;
          #134#3 = #24#1;
          #134#4 = #10#5;
          #134#5 = #10#32;
          #134#6 = #10#7;
          #134#7 = #10#6;
          #134#9 = #24#29;
          #134#10 = #24#30;
          #134#11 = #agifa010@#8;
          #134#12 = #24#16;
          #134#13 = #24#47;
          #134#14 = #24#22;
          #134#15 = #24#23;
          #134#16 = #24#24;
          #134#45 = "S";
          #134#58 = #10#68;
          #134#59 = #10#69;
          #134#60 = #10#70;
          #134#61 = #10#73;
          #134#62 = #10#74;
          #134#63 = 1;
          #134#115 = #10#84;
          #134#124 = #10#63;

          |DDEFECTO #59;
          #59#14 = #134#49;
          #59#0 = #134#0;
          #59#1 = 1;
          #59#15 = #10#52;
          #59#2 = #10#0;
          #59#18 = #10#68;
          #59#19 = #10#69;
          #59#20 = #10#70;
          |GRABA 020#59n;

          |HAZ CalCabAgifa134;
          |GRABA 020#134a;
          |LIBERA #134;

          |HAZ VenciVta;
          |HAZ hazreci;

          |CIERRA #3;

          |BORRA 020#agifa010@.a;

          |SI nPrime = 1;
               aSerPrime = #agifa010@#53;
               nNumPrime = #agifa010@#0;
               nNumUltimo = #agifa010@#0;
          |SINO;
               nNumUltimo = #agifa010@#0;
          |FINSI;
     |FINSI;
     |CIERRA #24;
|FPRC;

|DEFBUCLE GraAlbaFac;
     #agifa010@#1002;
     ;
     "     ", 000001;
     "zzzzz", 999999;
     ;
     NULL, GraAlbaFac;
|FIN;

|PROCESO CheckExisteLin;
     #109#0 = #agifa071@#2;
     |LEE 000#109=;

     |SI DEBUG = 1;
          |SI FSalida ! 0; |GRABA 020#109n; |FINSI;
     |FINSI;

     |SI FSalida ! 0;
          |PARA; |SI; |HACIENDO;
               aAlfa = "dsz99895;" + #109#0;
               |SUB_EJECUTA_NP aAlfa;
               #109#0 = #agifa071@#2;
               |LEE 000#109=;
               |SI FSalida ! 0;
                    aAlfa = "0000NArtículo no creado." + &13 +"¿Desea avandonar la importación?";
                    |CONFI aAlfa;
                    |SI FSalida = 0;
                         nError = 2; |ERROR10; |SAL;
                    |FINSI;
               |SINO;
                    |SAL;
               |FINSI;
          |SIGUE;
     |FINSI;
|FPRC;

|DEFBUCLE CheckExisteLin;
     #agifa071@#1003;
     ;
     #10#52, #10#0, 001;
     #10#52, #10#0, 999;
     ;
     NULL, CheckExisteLin;;
|FIN;

|PROCESO CheckExiste;
     #10#52 = #agifa010@#52;
     #10#0 = #agifa010@#0;
     |LEE 000#10=;
     |SI FSalida = 0;
          aAlfa = "0000Albaran existente:" + #10#52 + "/" + (("000000" + #10#0)%-106);
          nError = 1;
          |MENAV aAlfa;
     |FINSI;

     #134#49 = #agifa010@#53;
     #134#0 = #agifa010@#0;
     |LEE 000#134=;
     |SI FSalida = 0;
          aAlfa = "0000Factura existente:" + #134#49 + "/" + (("000000" + #134#0)%-106);
          nError = 1;
          |MENAV aAlfa;
     |FINSI;

     #24#0 = #agifa010@#3;
     |LEE 000#24=;

     |SI DEBUG = 1;
          |SI FSalida ! 0; |GRABA 020#24n; |FINSI;
     |FINSI;

     || Esto ya no pasa nunca, el cliente se graba sino existe
     |SI FSalida ! 0;
          aAlfa = "dszx0001;C" + #24#0;
          |SUB_EJECUTA_NP aAlfa;
     |FINSI;
     |BUCLE CheckExisteLin;
     |SI nError = 2; |ERROR10; |FINSI;

     |SI nError ! 0;               || Marco con X el campo exportado
          #agifa010@#58 = "X";     || para despues no pasar este albaran
          |GRABA 020#agifa010@.a;
          nError = 0;
     |FINSI;
|FPRC;

|DEFBUCLE CheckExiste;
     #agifa010@#1002;
     ;
     "     ", 000001;
     "zzzzz", 999999;
     ;
     NULL, CheckExiste;
|FIN;

|PROCESO GrabaFac;
     |PINTA 1206, "Chequeando...";
     |ABRE #134;
     |ABRE #10;
     |ABRE #24;
     |ABRE #109;
     |BUCLE CheckExiste;
     |CIERRA #109;
     |CIERRA #24;

     ||ABRE  #agifa010@;|CONSULTA_CLAVE #1#agifa010@;

     |SI nError = 0;
          enForma = 1;
          |ABRE #59
          |ABRE #71;
          |BUCLE GraAlbaFac;
          |CIERRA #71;
          |CIERRA #59;
     |FINSI;

     |SI nPrime ! 0;
          nError = 0;
          |PARA i = nNumPrime; |SI i [ nNumUltimo; |HACIENDO i = i + 1;
               #134#49 = aSerPrime;
               #134#0 = i;
               |LEE 000#134=;
               |SI FSalida ! 0;
                    nError = 1;
               |FINSI;
          |SIGUE;
          |SI nError ! 0;
               aAlfa = "0000Existen saltos de la numeracion. (" + nNumPrime + ", " + nNumUltimo + ")";
               |MENAV aAlfa;
          |FINSI;
     |FINSI;

     |CIERRA #10;
     |CIERRA #134;
|FPRC;

|PROCESO PonTipoIVA;
     |SELECT #2#19;
     #19#1 = #agifa071@#1;
     |LEE 020#19=;

     #agifa071@#11 = #19#2;
     |GRABA 020#agifa071@.a;
|FPRC;

|DEFBUCLE PonTipoIVA;
     #agifa071@#1003;
     ;
     #agifa010@#52, nNum, 001;
     #agifa010@#52, nNum, 999;
     ;
     NULL, PonTipoIVA;
|FIN;

|PROCESO BusTipoIva;
     nTipoIVA = 0;
     |ABRE #66;
     |LEE 000#66p;
     |PARA; |SI FSalida = 0; |HACIENDO;
          |SI #66#4 < fHoy;
               |SI #66#2 = aVal;
                    nTipoIVA = #66#0;
               |FINSI;
          |FINSI;
          |LEE 000#66s;
     |SIGUE;
     |CIERRA #66;
|FPRC;

|PROCESO BuscaFP;
     |ABRE #51;
     #51#0 = aVal;
     |LEE 000#51=;
     |SI FSalida = 0;
          aFP = #51#1;
     |SINO;
          aAlfa = "0000F.pago inexistente: " + aVal;
          nSwFPago = 1;
          |MENAV aAlfa;
          |GRABA 020#51n;
     |FINSI;
     |CIERRA #51;
|FPRC;

|PROCESO GrabaCli;
     |LEE 101#24=;
     |SI FSalida = 0;
          #24#47 = aRecargo;
          |GRABA 020#24a;
     |SINO;
          |ABRE #225;
          #225#0 = #24#205;
          |LEE 000#225=;
          |SI FSalida ! 0; |DDEFECTO #225; |FINSI;
          |CIERRA #225;
          #24#14 = #225#1;

          #24#2 = #24#1;
          #24#16 = "430" + #24#0;
          #24#47 = aRecargo;
          |GRABA 020#24n;

          |ABRE #101;
          |ABRE #102;
          |ABRE #25;
          |ABRE #3;
          #101#0 = #24#3;
          |LEE 000#101=; || Zona
          |SI FSalida ! 0; |DDEFECTO #101; |FINSI;

          #102#0 = #24#189;
          |LEE 000#102=; ||Ruta
          |SI FSalida ! 0; |DDEFECTO #102; |FINSI;

          #25#0 = #24#25;
          |LEE 000#25=;
          |SI FSalida ! 0; |DDEFECTO #25; |FINSI;

          |DDEFECTO #3;
          #3#0 = #24#0;
          #3#1 = 1;
          |LEE 101#3=;
          |SI FSalida ! 0; |GRABA 021#3b; |FINSI;
          #3#2 = #24#8;
          #3#3 = #24#9;
          #3#4 = #24#181;
          #3#5 = #24#182;
          #3#6 = #24#183;
          #3#7 = #24#10;
          #3#8 = #24#14;
          #3#10 = #24#2;
          #3#11 = #24#3;
          #3#12 = #101#1;
          #3#13 = #24#17;
          #3#14 = #24#25;
          #3#15 = #25#1;
          #3#16 = #24#189;
          #3#17 = #102#1;
          #3#19 = #24#205;
          #3#20 = #24#196;
          #3#21 = #24#18;
          #3#22 = #24#36;
          #3#23 = #24#211;
          #3#24 = #24#197;
          |GRABA 021#3a;
          |CIERRA #3;
          |CIERRA #25;
          |CIERRA #102;
          |CIERRA #101;
     |FINSI;
|FPRC;

|PROCESO ProcesaTagPop;
     ||aAlfa = "0000Pop: " + aTag + " => " + aVal; |MENAV aAlfa;

     |SI aTag = "Invoice"; |O aTag = "Credit";
          |SI aEsAbono = "S"
               #agifa010@#11 = -#agifa010@#11;
               #agifa010@#13 = -#agifa010@#13;
          |FINSI;

          #agifa010@#8 = aFP;
          |GRABA 020#agifa010@.n;

          nNum = #agifa010@#0;
          |BUCLE PonTipoIVA;

          |DDEFECTO #agifa010@;
          |DDEFECTO #agifa071@;
          |CIERRA #19; |DELINDEX #19; |ABRE #19;
          aEsAbono = "";
          nLin = 0;
          nSwArti = 0;
          nSwLin = 0;
          aGuid = "";
          nDtoCab = 0;
          nGasto = 0;
          aConTerms = "N";
     |FINSI;
     |SI aTag = "InvoiceNo"; |O aTag = "CreditNo";
          |SI aTag = "CreditNo"; aEsAbono = "S"; |FINSI;
          aSer = aVal % 104;
          #agifa010@#53 = aSer;
          aNum = aVal - aSer; aNum = ("000000" + aNum) % -106;
          #agifa010@#0 = aNum;
     |FINSI;
     |SI aTag = "TaxDefinition";
          aAlfa = "";
          |SI aVal = "Drittland"; aAlfa = "EX"; |FINSI;
          |SI aVal = "Inland"; aAlfa = "ES"; |FINSI;
          |SI aVal = "EU"; aAlfa = "CE"; |FINSI;
          |SI aVal = "EU-ohneUstID"; aAlfa = "CE"; |FINSI;
          aAlfa = aAlfa + aAno;
          #agifa010@#52 = aAlfa;
     |FINSI;
     |SI aTag = "InvoiceDate";
          aAlfa = (aVal % 902) +"."+ (aVal % 602) +"."+ (aVal % 104);
          #agifa010@#1 = aAlfa;
     |FINSI;
     |SI aTag = "CreditDate";
          aAlfa = (aVal % 902) +"."+ (aVal % 602) +"."+ (aVal % 104);
          #agifa010@#1 = aAlfa;
     |FINSI;
     |SI aTag = "PaymentTermUID";
          |HAZ BuscaFP;
     |FINSI;

     |SI nSwCli = 1;
          |SI aTag = "CustomerID";
               aAlfa = ("000000" + aVal) % -106;
               #agifa010@#3 = aAlfa;

               #24#0 = aAlfa;
          |FINSI;
          |SI aTag = "Name1";
               eaAlfa = aVal; |HAZ QuitaRarosImp; aVal = eaAlfa;
               #24#1 = aVal;
           |FINSI;
          |SI aTag = "VAT-ID";
               aVal = aVal % 3; #24#15 = aVal;
          |FINSI;
          |SI aTag = "Country"; #24#205 = aVal; |FINSI;
          |SI aTag = "TaxDefinition";
               |SI aVal = "Inland+Zusatz";
                    aRecargo = "S";
               |SINO;
                    aRecargo = "N";
               |FINSI;
          |FINSI;
     |FINSI;
     |SI nSwCli = 2;
          |SI aTag = "Street1";
               eaAlfa = aVal; |HAZ QuitaRarosImp; aVal = eaAlfa;
               #24#5 = aVal; #24#8 = aVal; #24#11 = aVal;
          |FINSI;
          |SI aTag = "Zip";
               aCp1 = aVal % 102;
               aCp2 = aVal % 303;
               aCp = aCp1 + aCp2;
               #24#178 = aCp1; #24#181 = aCp1; #24#184 = aCp1;
               #24#179 = aCp2; #24#182 = aCp1; #24#185 = aCp1;
               #24#180 = aCp; #24#183 = aCp; #24#186 = aCp;
          |FINSI;
          |SI aTag = "City";
               eaAlfa = aVal; |HAZ QuitaRarosImp; aVal = eaAlfa;
               #24#6 = aVal; #24#9 = aVal; #24#12 = aVal;
          |FINSI;
          |SI aTag = "State";
               eaAlfa = aVal; |HAZ QuitaRarosImp; aVal = eaAlfa;
               #24#7 = aVal; #24#10 = aVal; #24#13 = aVal;
          |FINSI;
     |FINSI;

     |SI nSwArti = 1;
          |SI aTag = "Guid"; |O aTag = "ItemGuid"; #19#0 = aVal; |FINSI;
          |SI aTag = "Aid"; #agifa071@#2 = aVal; |FINSI;
          |SI aTag = "Percent"; #agifa071@#8  = aVal; |FINSI;
          |SI aTag = "Quantity"; #agifa071@#5 = aVal; |FINSI;
          |SI aTag = "Price"; #agifa071@#6 = aVal; |FINSI;
          |SI aTag = "InvoiceItem"; |O aTag = "CreditItem";
               nLin = nLin + 1;
               #agifa071@#29 = #agifa010@#52;
               #agifa071@#0 = #agifa010@#0;
               #agifa071@#1 = nLin;

               |SI aEsAbono = "S";
                    #agifa071@#5 = -#agifa071@#5;
                    aAlfa = #agifa071@#2; |QBF aAlfa;
                    |SI aAlfa = "";
                         #agifa071@#2 = "ABONO";
                    |FINSI;
               |FINSI;

               |GRABA 020#agifa071@.n;
               |DDEFECTO #agifa071@;

               #19#1 = nLin;
               |GRABA 020#19n;
          |FINSI;
     |FINSI;

     |SI nSwLin = 2;
          |SI aTag = "Amount"; |Y nSwImp = 1;
                nTotalPortes = aVal;
          |FINSI;
          |SI aTag = "TaxRate"; |Y nSwTasa = 1;
               |HAZ BusTipoIva;
                nTipoIVAPortes = nTipoIVA;
          |FINSI;
     |FINSI;
     |SI nSwLin = 3;
          |SI aTag = "TaxRate"; |Y nSwTasa = 1;
               |HAZ BusTipoIva;
          |FINSI;
          |SI aTag = "Guid"; |O aTag = "ItemGuid"; aGuid = aVal; |FINSI;
     |FINSI;

     |SI aTag = "CrossEntry";
          |SI nSwLin = 2;
               |SI aConTerms = "N";
                    #agifa010@#11 = nTotalPortes;
               |SINO;
                    #agifa010@#11 = nGasto;
                    #agifa010@#13 = nDtoCab;||el importe gasto,temporalmente
               |FINSI;                      ||lo guardo en el campo varios
               #agifa010@#12 = nTipoIVAPortes;;

               nTotalPortes = 0;
               nTipoIVAPortes = 0;
          |FINSI;
          |SI nSwLin = 3;
               #19#0 = aGuid;
               |LEE 101#19=;
               |SI FSalida ! 0;
                    aAlfa = "0000Guid incorrecto:" + aGuid;
                    |MENAV aAlfa;
                    nError = 1;
               |SINO;
                    #19#2 = nTipoIVA;
                    |GRABA 020#19a;
                    |LIBERA #19;
               |FINSI;
               aGuid = "";
               nSwLin = 0;
          |FINSI;
     |FINSI;

     |SI aTag = "Amount"; |Y nSwTerm = 2;
          nDtoCab = aVal;
     |FINSI;
     |SI aTag = "Amount"; |Y nSwTerm = 3;
          nGasto = aVal;
     |FINSI;

     |SI aTag = "OrderCustomer"; nSwNoCli = 0; |FINSI;
     |SI aTag = "Customer"; nSwCli = 0; |FINSI;
     |SI aTag = "Address"; |Y nSwNoCli = 0;
          nSwCli = 0;
          |HAZ GrabaCli;
          |CIERRA #24;
     |FINSI;
     |SI aTag = "Terms"; nSwTerm = 0; |FINSI;
     |SI aTag = "Term"; nSwTerm = 1; |FINSI;
     |SI aTag = "InvoiceItems"; |O aTag = "CreditItems"; nSwArti = 0; |FINSI;
     |SI aTag = "CrossEntries"; nSwLin = 0; |FINSI;
     |SI aTag = "CrossEntry"; nSwLin = 1; |FINSI;
     |SI aTag = "RevenueAccount"; nSwImp = 0; |FINSI;
     |SI aTag = "TaxAccount"; nSwTasa = 0; |FINSI;
|FPRC;

|PROCESO ProcesaTagPush;
     ||aAlfa = "0000Push: " + aTag + " => " + aTagAtri; |MENAV aAlfa;

     |SI aTag = "OrderCustomer";
          nSwNoCli = 1;
     |FINSI;
     |SI aTag = "Customer";
          nSwCli = 1;
          |ABRE #24; |DDEFECTO #24;
     |FINSI;
     |SI aTag = "Address";
          nSwCli = 2;
     |FINSI;

     |SI aTag = "Terms"; |Y nSwArti = 0;
          nSwTerm = 1;
          aConTerms = "S";
     |FINSI;

     |SI aTag = "Term"; |Y nSwTerm = 1;
          aAlfa = "type"; |HAZ GetAtri;
          |SI aAlfa = "rebate";
               nSwTerm = 2;
          |FINSI;
          |SI aAlfa = "surcharge";
               nSwTerm = 3;
          |FINSI;
     |FINSI;

     |SI aTag = "InvoiceItems"; |O aTag = "CreditItems";
          nSwArti = 1;
     |FINSI;
     |SI aTag = "CrossEntries";
          nSwLin = 1;
     |FINSI;
     |SI aTag = "CrossEntry"; |Y nSwLin = 1;
          aAlfa = aTagAtri - "DefaultTaxKey";
          |SI FSalida > 0;
               nSwLin = 3;    ||linea
          |SINO;
               nSwLin = 2;    ||portes
          |FINSI;
     |FINSI;
     |SI aTag = "RevenueAccount"; nSwImp = 1; |FINSI;
     |SI aTag = "TaxAccount"; nSwTasa = 1; |FINSI;
|FPRC;

|PROCESO ProcesaTagVacio;
|FPRC;

|PROCESO ProcesaPrologo;
|FPRC;

|PROCESO ProcesaRem;
|FPRC;

|PROCESO GetAtri;
     aResul = "";
     aAlfa = aTagAtri - aAlfa;
     |SI FSalida > 0;
          aResul = ("00000" + FSalida) % -105;
          nPosI = (A\(aResul % 103));
          nPosD = (A\(aResul % 402));
          nPos = nPosI + nPosD;
          aDato = aTagAtri;

          || si nPos > 100, FALLA: aTagAtri % nPos;
          |PARA; |SI nPos > 0; |HACIENDO;
               nPos = nPos - 90;
               |SI nPos < 0;
                    nPos = nPos + 90;
                    aDato = aDato % nPos;
                    |SAL;
               |SINO;
                    aDato = aDato % 91;
               |FINSI;
          |SIGUE;
          || busca la parte entre ""
          aComilla = &34;
          aIniComi = "N";
          aResul = "";
          aLon = aDato % A0;
          |PARA i = 1; |SI i [ aLon; |HACIENDO i = i + 1;
               nPos = i * 100 + 1;
               aLetra = aDato % nPos;
               |SI aLetra = aComilla;
                    |SI aIniComi = "S";
                         |SAL;
                    |SINO;
                         aIniComi = "S";
                    |FINSI;
               |SINO;
                    |SI aIniComi = "S";
                         aResul = aResul + aLetra;
                    |FINSI;
               |FINSI;
          |SIGUE;
     |FINSI;
    aAlfa = aResul;
|FPRC;

|PROCESO QuitaBlancos;   || a izquierda y derecha
     aResul = "";
     aLon = aAlfa % A0;
     |PARA i = 1; |SI i [ aLon; |HACIENDO i = i + 1;
          nPos = i * 100 + 1;
          aLetra = aAlfa % nPos;
          |SI aLetra ! " "; |O aResul ! "";
               aResul = aResul + aLetra;
          |FINSI;
     |SIGUE;
     aAlfa = aResul;
     |QBF aAlfa;
|FPRC;

|PROCESO PopTag;
     nNivel = nNivel - 1;
     IMatriz = Matriz1<-;
     IMatriz = IMatriz + nNivel;
     FSalida = Matriz;
|FPRC;

|PROCESO PushTag;
     IMatriz = Matriz1<-;
     IMatriz = IMatriz + nNivel;
     Matriz = aTag;
     nNivel = nNivel + 1;
|FPRC;

|PROCESO PinLinea;
     nReg = nReg + 1;
     aAlfa = "Linea: " + nReg;
     |PINTA 1206, aAlfa;
|FPRC;

|PROCESO Caracter;
     |SI aCar = RETORNO; |HAZ PinLinea; |FINSI;

     |SI aCar < " "; |O nError ! 0; |ACABA; |FINSI;

     |SI nRem > 0;
          |SI nRem < 3;
               |SI aCar = "-";
                    nRem = nRem + 1;
               |SINO;
                    aAlfa = "0000ERROR en XML: " + aTag;
                    |MENAV aAlfa;
                    nError = 1;
               |FINSI;
          |SINO;
               |SI aCar = ">"; || Un comentario no puede tener el caracter '>'
                    aAlfa = aTag % -102;
                    |SI aAlfa = "--";
                         aLon = aTag % A0;
                         nPos = (aLon - 2) + 100;
                         aTag = aTag % nPos;
                         |HAZ ProcesaRem;
                    |SINO;
                         aAlfa = "0000ERROR en XML: " + aTag;
                         |MENAV aAlfa;
                         nError = 1;
                    |FINSI;
                    aTag = ""; nRem = 0;
               |SINO;
                    aLon = aTag % A0;
                    |SI aLon < 250; aTag = aTag + aCar; |FINSI;
               |FINSI;
          |FINSI;
          |ACABA;
     |FINSI;

     |SI aCar = "<";
          aIniTag = "S"; aTag = "";
     |SINO;
          |SI aIniTag = "S";
               |SI aCar = "/"; |Y aIniAtri = "N";
                    |SI aTag = "";
                         aFinTag = "S";
                    |SINO;
                         aFinTagVacio = "S";
                    |FINSI;
               |SINO;
                    |SI aCar = "!"; |Y aIniAtri = "N";
                         |SI aTag = "";
                              nRem = 1;
                         |SINO;
                              aAlfa = "0000ERROR en XML: " + aTag;
                              |MENAV aAlfa;
                              nError = 1;
                         |FINSI;
                         |ACABA;
                    |FINSI;
                    |SI aCar = "?"; |Y aIniAtri = "N";
                         |SI aIniPro = "S";
                              aFinPro = "S";
                              aIniPro = "N";
                         |SINO;
                              |SI aFinPro = "S";
                                   aAlfa = "0000ERROR en XML: " + aTag;
                                   |MENAV aAlfa;
                                   nError = 1;
                              |SINO;
                                   aIniPro = "S"; aTag = "";
                              |FINSI;
                         |FINSI;
                    |SINO;
                         |SI aCar = ">";
                              aAlfa = aTag; |HAZ QuitaBlancos; aTag = aAlfa;
                              aAlfa = aTagAtri; |HAZ QuitaBlancos; aTagAtri = aAlfa;
                              |SI aFinPro = "S";
                                   |HAZ ProcesaPrologo;
                                   aFinPro = "N";
                              |SINO;
                                   |SI aFinTag = "S";
                                        |HAZ PopTag;
                                        |SI aTag ! FSalida;
                                             aAlfa = "0000ERROR en XML: <" + FSalida + "> " + aVal;
                                             |MENAV aAlfa;
                                             nError = 1;
                                        |SINO;
                                             |HAZ ProcesaTagPop;
                                        |FINSI;
                                   |SINO;
                                        |SI aFinTagVacio = "S";
                                             |HAZ ProcesaTagVacio;
                                             aFinTagVacio = "N";
                                        |SINO;
                                             |HAZ PushTag;
                                             |HAZ ProcesaTagPush;
                                        |FINSI;
                                   |FINSI;
                              |FINSI;
                              aIniTag = "N"; aFinTag = "N"; aTag = ""; aVal = "";
                              aIniAtri = "N"; aTagAtri = "";
                         |SINO;
                              aLon = aTag % A0;
                              |SI aLon < 250;
                                   |SI aCar = " "; |Y aIniAtri = "N";
                                        aIniAtri = "S";
                                   |SINO;
                                        |SI aIniAtri = "S";
                                             aTagAtri = aTagAtri + aCar;
                                        |SINO;
                                             aTag = aTag + aCar;
                                        |FINSI;
                                   |FINSI;
                              |FINSI;
                         |FINSI;
                    |FINSI;
               |FINSI;
          |SINO;
               aLon = aVal % A0;
               |SI aLon < 250; aVal = aVal + aCar; |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO XML;
     nReg = 0;
     nRem = 0;
     nNivel = 0;
     aVal = "";
     aTmp = "";
     aTag = "";
     aIniTag = "N";
     aFinTag = "N";
     aIniPro = "N";
     aFinPro = "N";
     aFinTagVacio = "N";
     aIniAtri = "N";
     aTagAtri = "";

     |XLEE nHandle, 250, aTmp;
     |HAZ PinLinea;
     |PARA; |SI FSalida > 0; |HACIENDO;
          |PARA; |SI aTmp ! ""; |HACIENDO;
               aCar = aTmp % 101;
               |SI aTmp = aCar;
                    aTmp = ""; ||con un caracter falla: "aTmp = aTmp % 2"
               |SINO;
                    aTmp = aTmp % 2;
               |FINSI;
               |HAZ Caracter;
               |SI nError ! 0; |SAL; |FINSI;
          |SIGUE;
          |XLEE nHandle, 250, aTmp;
          |SI nError ! 0; |SAL; |FINSI;
     |SIGUE;
|FPRC;

|PROCESO T3; |TIPO 3;
     RETORNO = &10;
     aSoloUno = "S";
     aAlfa = #0#0; |QBF aAlfa;

     |SI aSoloUno = "S";           || #0#0 es fichero
          |XABRE "EC", aAlfa, 0;
          |SI FSalida ] 0;
               eaFic = aAlfa; |HAZ Utf82Ansi; aAlfa = eaFic;

               |DFICE aFic;
               aFic = aFic + Usuario;
               |RECIBE_FICHERO aAlfa, aFic;
               |SI FSalida ! 0;
                    |MENAV "0000Error al copiar fichero al servidor...";
               |SINO;
                    |XABRE "B", aFic, nHandle;
                    |SI FSalida ] 0;
                         |HAZ XML;
                         |XCIERRA nHandle;
                         |FBORRA aFic;
                    |FINSI;
               |FINSI;

               |RFBORRA eaFic;
          |SINO;
               |MENAV "0000No existe fichero...";
          |FINSI;
     |SINO;                   || #0#0 es directorio
          aFicheros = aAlfa;
          |R_PDIR aFicheros;
          |PARA; |SI FSalida = 0; |HACIENDO;
               aAlfa  = aFicheros;
               |PINTA 1201, aAlfa;

               |DFICE aFic;
               aFic = aFic + Usuario;
               |RECIBE_FICHERO aAlfa, aFic;
               |SI FSalida ! 0;
                    |MENAV "0000Error al copiar fichero al servidor...";
               |SINO;
                    |XABRE "B", aFic, nHandle;
                    |SI FSalida ] 0;
                         |HAZ XML;
                         |XCIERRA nHandle;
                         |FBORRA aFic;
                    |FINSI;
               |FINSI;

               |SI nError ! 0; |SAL; |FINSI;
               |R_SDIR aFicheros;
          |SIGUE;
     |FINSI;

     |SI  nSwFPago = 1;
          |MENAV "0000Existen Formas de pago sin definir...";
          nError = 1;
     |FINSI;

     |LEE 000#agifa010@.p;
     |SI FSalida = 0; |Y nError = 0;
          |HAZ GrabaFac;
          |LEE 000#agifa010@.p;
          |SI FSalida = 0;
               |CONFI "0000S¿Imprimir facturas no importadas?";
               |SI FSalida = 0;
                    |IMPRE 0;
                    |SI FSalida = 0;
                         |INFOR "pcegz076";
                         |SI FSalida = 0;
                              |ABRE #24;
                              |BUCLE NoImportadas;
                              |CIERRA #24;
                         |SINO;
                              |PIEINF;
                              |FININF;
                         |FINSI;
                         |FINIMP;
                    |FINSI;
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Fichero; |TIPO 0;
     |SI FSalida = 10;
          aAlfa = "F*.xml";
          |BROWSE aAlfa;

          aAlfa = aAlfa % 2; |QBF aAlfa;
          |SI aAlfa = "F"; aAlfa = ""; |FINSI;

          |QBF aAlfa;
          |SI aAlfa = "";
               |ERROR;
          |SINO;
               #0#0 = aAlfa; |PINTA #0#0;
               |ERROR;
          |FINSI;
     |FINSI;
|FPRC;

|PROGRAMA;
     |ABRE #142; |LEE 000#142p; |CIERRA #142;
     |ABRE #321; |LEE 000#321p; |CIERRA #321;

     |HAZ CreaTempo; |NOME_DAT #19#Tempo#; aTmp19 = Tempo;
     |DELINDEX #19; |ABRE #19;

     |DDEF aPath;
     aAlfa = aPath + "agifa071"; |CARGA_DEF aAlfa, agifa071@;
     aAlfa = aPath + "agifa010"; |CARGA_DEF aAlfa, agifa010@;
     |HAZ CreaTempo; |NOME_DAT #agifa010@#Tempo#; aTmp10 = Tempo;
     |HAZ CreaTempo; |NOME_DAT #agifa071@#Tempo#; aTmp71 = Tempo;
     |DELINDEX #agifa010@; |ABRE #agifa010@;
     |DELINDEX #agifa071@; |ABRE #agifa071@;

     |HAZ DecimalesBase;
     fHoy = ~;
     aAno = fHoy % -102;

     |ABRE #0;
     |LEE 101#0p;
     |SI FSalida ! 0; |GRABA 020#0b; |FINSI;
     |PINPA #0#0;
     |PINDA #0#0;
     |ENDATOS #1#0;
     |SI FSalida = 0; |GRABA 020#0a; |FINSI;
     |CIERRA #0;

     Tempo = aTmp10; |HAZ BoraTempo; |CIERRA #agifa010@; |DELINDEX #agifa010@;
     Tempo = aTmp71; |HAZ BoraTempo; |CIERRA #agifa071@; |DELINDEX #agifa071@;
     |DESCARGA_DEF #agifa010@;
     |DESCARGA_DEF #agifa071@;

     Tempo = aTmp19; |HAZ BoraTempo; |CIERRA #19; |DELINDEX #19;
|FPRO;
