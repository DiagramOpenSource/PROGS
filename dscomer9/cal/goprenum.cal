|FICHEROS;
     gopre001 #0;
     gopre002 #1;
     gopre003 #2;
     gopre004 #3;
     goprenum #5;
     goapaive #6;
     gosubive #7;
|FIN;
----------------------------------------------------------------------
|VARIABLES;
     nNumCampos = 15;
     aAlfa = "";
     &cSerie   = "";

     &nNumero  = 0;

     BAutoma   = 0;
     nLinea    = 0;
     nModo     = 0;
     nCampo    = 0;

     cUsuario  = "";
     cFichero  = "";
|FIN;
----------------------------------------------------------------------
|PROCESO GrabaPresu;
     |PARA nCampo = 0; |SI nCampo [ nNumCampos; |HACIENDO nCampo = nCampo + 1;
          #5nCampo = #1nCampo;
     |SIGUE;
     #5#16 = #1#2;
     #5#17 = #1#16;
     |GRABA 020#5c;
|FPRC;
-------------------------------------------------------------------------------
|DEFBUCLE TomaPresu;
     #1#1;
     ;                                          ||Campos Extras.
     cSerie, nNumero,   1;
     cSerie, nNumero, 999;
     ;                                          ||b/e Bolqueo-Ordenacion.
     NULL, GrabaPresu;
|FIN;
-------------------------------------------------------------------------------
|PROGRAMA;
     aAlfa = "|NC";
     aAlfa = #1#aAlfa#;
     |SI aAlfa ! 17;
          |MENAV "0000ERROR, la estructura del gopre002 ha cambiado, consulte con su distribuidor";
          |ACABA;
     |FINSI;

     cUsuario = Usuario % 808;
     cFichero = "TMP01" + cUsuario;
     cFichero = cFichero % 108;
     |NOME_DAT #5 cFichero;
     |DELINDEX #5;
     |ABRE #5;
     |BUCLE TomaPresu;
     |CONTROL_SIMPLE 0, "BOTON, Renumeacion Automatica", 2425, 2460, BAutoma;
     BAutoma = FSalida;
     |ESTADO_CONTROL BAutoma, "SHOW";
     nModo = 23;
     |PARA; |SI nModo ] 23; |HACIENDO;
          |HAZ TipoSalida;
     |SIGUE;
     |HAZ GrabCamb;
     |CIERRA #5;
     |DELINDEX #5;
|FPROG;
-------------------------------------------------------------------------------
|PROCESO FinPrograma; |TIPO 90;
     |PULSATECLA;
     |FIN_CONTROL_WINDOWS BAutoma;
     |PULSATECLA;
|FPRC;
-------------------------------------------------------------------------------
|PROCESO Renum;
     nLinea =  nLinea + 1;
     aAlfa = ("000" + nLinea) % -103;
     #5#16 = aAlfa;
     |GRABA 020#5a;
|FPRC;
-------------------------------------------------------------------------------
|DEFBUCLE Renum;
     #5#1;
     ;                                          ||Campos Extras.
     1;
     999;
     ;                                          ||b/e Bolqueo-Ordenacion.
     NULL, Renum;
|FIN;
-------------------------------------------------------------------------------
|PROCESO BAutoma;
     FSalida = 10;
     nLinea = 0;
     |BUCLE Renum;
     |PTEC 806;
     nModo = 23;
|FPRC;
-------------------------------------------------------------------------------
|PROCESO MinPres;
     #5#2 = 1;
|FPRC;
-------------------------------------------------------------------------------
|PROCESO MaxPres;
     #5#2 = 999;
|FPRC;
-------------------------------------------------------------------------------
|PROCESO TipoSalida;
     nModo = 0;
     |PINPA #0#5;
     |ENTLINEAL #1#5, -1, 2, 21, 0, MinPres, MaxPres;
|FPRC;
-------------------------------------------------------------------------------
|PROCESO ActRen;
     |PARA nCampo = 0; |SI nCampo [ nNumCampos; |HACIENDO nCampo = nCampo + 1;
          |SI nCampo = 2;
               #1nCampo = #5#16;
          |SINO;
               #1nCampo = #5nCampo;
          |FINSI;
     |SIGUE;
     #1#16 = #5#17;
     |GRABA 020#1c;
|FPRC;
-------------------------------------------------------------------------------
|DEFBUCLE ActRen;
     #5#2;
     ;                                          ||Campos Extras.
     INICIO;
     FINAL;
     ;                                          ||b/e Bolqueo-Ordenacion.
     NULL, ActRen;
|FIN;

|PROCESO GrabCamb;
     |CONFI "0000S¿DESEA ACTUALIZAR LA RENUMERACION?";
     |SI FSalida = 0;
          |ABRE #0;
          #0#0 = cSerie;
          #0#1 = nNumero;
          |LEE 000#0=;
          |SI FSalida = 0;
               |ABRE #1;
               |BUCLELIN 1 NULL #0;
               |BUCLE ActRen;
               |CIERRA #1;
          |FINSI;
          |CIERRA #0;
     |FINSI;
|FPRC;
-------------------------------------------------------------------------------
|PROCESO Intro; |TIPO 0;
     |PTEC 802;
|FPRC;
-------------------------------------------------------------------------------
|PROCESO BSnum; |TIPO 7;
     |ESTADO_CONTROL BAutoma, "SHOW";
|FPRC;
-------------------------------------------------------------------------------
|PROCESO BHnum; |TIPO 7;
     |ESTADO_CONTROL BAutoma, "HIDE";
|FPRC;
-------------------------------------------------------------------------------
