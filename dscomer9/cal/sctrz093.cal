|FICHEROS;
     sctrz093 #0;
     sctrw027 #27;  || Tmp servicios
     sctrw028 #28,MANTE;  || Tmp resultado

     sctrm079 #79;  || Historicos
     sctrm080 #80;  ||  ''
     sctrm081 #81;  ||  ''

     sctrm003 #3;
     sctrm036 #36;
     sctrm072 #72;
     sctrm076 #76;
     sctrm077 #77;
|FIN;

|VARIABLES;
     &enError = 0;
     &Tempo = "";
     aTmp27 = "";
     aTmp28 = "";
     aParam = "";
     nNume = 0;
     aDes = "";
     aPath = "";
     nHandle = 0;
     i = 0;
     aDato = "";
     aLon = "";
     nPos = 0;
     aLetra = "";
     aReg = "";
     aTab = "";
 {99}Valor = "";
     nCampo = 0;
     aNomFic = "";
     aAlfa = "";
     aOrgDes = "";
     nReg = 0;
     nHay = 0;
     aEnlaces = "S";
     aDivisa = "";
     aAgencia = "";
|FIN;

|PROCESO sctrm077;
     |SI nHay = 0;
          nHay = 1;

          |DFICE aPath;
          aDes = aPath + aNomFic + ".sal";
          |XABRE "W", aDes, nHandle;
          |SI FSalida ] 0;
               aDato = "origen_destino" + aTab;
               aDato = aDato + "enlace" + aTab;
               aDato = aDato + "id_enlace" + aTab;
               aDato = aDato + "origen" + aTab;
               aDato = aDato + "id_origen" + aTab;
               aDato = aDato + "destino" + aTab;
               aDato = aDato + "id_destino" + aTab;
               aDato = aDato + "minimo" + aTab;
               aDato = aDato + "maximo"+ aTab;
               aDato = aDato + "id";
               |XGRABA nHandle, aDato;
          |FINSI;
     |FINSI;

     #72#0 = "999999";   || Agencia
     #72#1 = #77#2;
     |LEE 000#72=;
     |SI FSalida ! 0; |DDEFECTO #72; |FINSI;
     nNume = #72#4;
     aDato = aOrgDes + aTab + #77#2 + aTab + nNume;

     #72#0 = "999999";   || Agencia
     #72#1 = #0#7;
     |LEE 000#72=;
     |SI FSalida ! 0; |DDEFECTO #72; |FINSI;
     nNume = #72#4;
     aDato = aDato + aTab + #0#7 + aTab + nNume;

     #72#0 = "999999";   || Agencia
     #72#1 = #0#8;
     |LEE 000#72=;
     |SI FSalida ! 0; |DDEFECTO #72; |FINSI;
     nNume = #72#4;
     aDato = aDato + aTab + #0#8 + aTab + nNume;

     aDato = aDato + aTab + #76#4 + aTab + #76#2 + aTab + #79#18;

     |XGRABA nHandle, aDato;
|FPRC;

|DEFBUCLE sctrm077;
     #77#1;
     ;
     #3#0, 001;
     #3#0, 999;
     ;
     NULL, sctrm077;
|FIN;

|PROCESO Enlaces;
     |ABRE #76; |SELECT #2#76;
     #76#1 = "999999";
     |LEE 000#76=;
     |CIERRA #76;

     |ABRE #72;
     |ABRE #3;
     #3#0 = #0#7;
     |LEE 000#3=;
     |SI FSalida = 0; |Y #3#15 = "N";
          aOrgDes = "O";
          |BUCLE sctrm077;
     |FINSI;
     #3#0 = #0#8;
     |LEE 000#3=;
     |SI FSalida = 0; |Y #3#15 = "N";
          aOrgDes = "D";
          |BUCLE sctrm077;
     |FINSI;
     |CIERRA #3;
     |CIERRA #72;
|FPRC;

|PROCESO GrabaEnlaces;
     nHay = 0;
     |HAZ Enlaces;
     |SI nHay = 0;
          |DFICE aPath;
          aDes = aPath + aNomFic + ".sal";
          |XABRE "W", aDes, nHandle;
          |SI FSalida ] 0;
               aDato = "solo_id";
               |XGRABA nHandle, aDato;
               aDato = #79#18;
               |XGRABA nHandle, aDato;
               |XCIERRA nHandle;
          |FINSI;
     |SINO;
           |XCIERRA nHandle;
     |FINSI;
|FPRC;

|PROCESO Dato;
     nCampo = 1;
     IValor = Valor1<-;
     aLon = aDato % A0;
     aReg = "";
     |PARA i = 1; |SI i [ aLon; |HACIENDO i = i + 1;
          nPos = i * 100 + 1;
          aLetra = aDato % nPos;
          |SI aLetra = aTab;
               |SI aReg = "None"; aReg = ""; |FINSI;
               Valor = aReg;
               aReg = "";
               nCampo = nCampo + 1;
               IValor = IValor + 1;
          |SINO;
               aReg = aReg + aLetra;
          |FINSI;
          |SI nCampo > 90; |SAL; |FINSI;     || Por si.... :)
     |SIGUE;
     |SI aReg = "None"; aReg = ""; |FINSI;
     Valor = aReg;
|FPRC;

|PROCESO GrabaHisto79;
     aDivisa = "EUR";
     aAgencia = #0#4; |QBF aAgencia;
     |SI aAgencia ! "";
          |ABRE #36;
          #36#1 = #0#4;
          |LEE 000#36=;
          |CIERRA #36;
          aDivisa = #36#46;
     |FINSI;

     |LEE 000#79u;
     |SI FSalida = 0; nReg = #79#0 + 1; |SINO; nReg = 1; |FINSI;
     |DDEFECTO #79;
     #79#0 = nReg;
     |PARA i = 1; |SI i [ 16; |HACIENDO i = i + 1;
          #79i = #0i;
     |SIGUE;
     #79#18 = aParam;
     #79#21 = #0#17;
     #79#22 = aDivisa;
     |GRABA 020#79n;
|FPRC;

|PROCESO Inicio; |TIPO 3;
     |HORA aAlfa;
     #0#15 = ~;
     #0#16 = aAlfa;

     |ABRE #79;
     |ABRE #80;
     |ABRE #81;
     |HAZ GrabaHisto79;

     |HAZ CompruOpen;
     |SI enError = 0;
          |HAZ CompruAgencia;
     |FINSI;
     |SI enError = 0;
          |HAZ FecSalida;
          |SI #0#1 ] 2;
               |HAZ FecVuelta;
          |FINSI;
          nReg = 0;
          |HAZ BuscaTarifa;
          |HAZ TmpResultado;
     |FINSI;

     |CIERRA #81;
     |CIERRA #80;
     |CIERRA #79;
|FPRC;

|PROCESO TipoViaje; |TIPO 0;
     |SI #0#1 = 1;
          |C_M #0#3, 1, "N";
          #0#3 = "01.01.0000"; |PINTA #0#3;
     |SINO;
          |C_M #0#3, 1, "S";
     |FINSI;
|FPRC;

|PROCESO Min28;
     #28#0 = 001;
|FPRC;

|PROCESO Max28;
     #28#0 = 999;
|FPRC;

|PROCESO LeePeticion;
     |DFICE aPath;
     aDes = aPath + aNomFic + ".ent";

     |XABRE "", aDes, nHandle;
     |SI FSalida ] 0;
          |XLEE nHandle, 250, aDato;    || los nombres
          |XLEE nHandle, 250, aDato;
          |HAZ Dato;
          |XCIERRA nHandle;

          Valor2 = (Valor2%902)+"."+(Valor2%602)+"."+(Valor2%104);
          |SI Valor3 = "";
               Valor3 = "01.01.0000";
          |SINO;
               Valor3 = (Valor3%902)+"."+(Valor3%602)+"."+(Valor3%104);
          |FINSI;

          #0#1 = Valor1;
          #0#2 = Valor2;
          #0#3 = Valor3;
          #0#4 = Valor4;
          #0#5 = Valor5;
          #0#6 = Valor6;
          #0#7 = Valor7;
          #0#8 = Valor8;
          #0#9 = Valor9;
          #0#10 = Valor10;
          #0#11 = Valor11;
          #0#12 = Valor12;
          #0#13 = Valor13;
          #0#14 = Valor14;
          #0#17 = Valor15;
     |FINSI;
|FPRC;

|PROCESO GrabaResultado;
     |DFICE aPath;
     aDes = aPath + aNomFic + ".sal";
     |XABRE "W", aDes, nHandle;
     |SI FSalida ] 0;
          aDato = "error" + aTab;
          aDato = aDato + "ida_vuelta" + aTab;
          aDato = aDato + "bloqueado" + aTab;
          aDato = aDato + "pax_disponible" + aTab;
          aDato = aDato + "linea" + aTab;
          aDato = aDato + "linea_nombre" + aTab;
          aDato = aDato + "servicio" + aTab;
          aDato = aDato + "servicio_nombre" + aTab;
          aDato = aDato + "fecha_salida" + aTab;
          aDato = aDato + "fecha_llegada" + aTab;
          aDato = aDato + "horas_viaje" + aTab;
          aDato = aDato + "importe" + aTab;
          aDato = aDato + "hora_salida" + aTab;
          aDato = aDato + "hora_llegada" + aTab;
          aDato = aDato + "id" + aTab;
          aDato = aDato + "id_servicio" + aTab;
          aDato = aDato + "cerror" + aTab;
          aDato = aDato + "ciudad_llegada" + aTab;
          aDato = aDato + "plataforma" + aTab;
          |XGRABA nHandle, aDato;
          |LEE 000#28p;
          |PARA; |SI FSalida = 0; |HACIENDO;
               aDato = "";
               |PARA i = 1; |SI i [ 17; |HACIENDO i = i + 1;
                    aAlfa = #28i; |QBF aAlfa;
                    |SI i = 9; |O i = 10;
                         aAlfa = (aAlfa%704)+"-"+(aAlfa%402)+"-"+(aAlfa%102);
                    |FINSI;
                    aDato = aDato + aAlfa + aTab;
               |SIGUE;
               aDato = aDato + #0#8 + aTab + "diagram" + aTab;
               |XGRABA nHandle, aDato;
               |LEE 000#28s;
          |SIGUE;
          |XCIERRA nHandle;
     |FINSI;
|FPRC;

|PROCESO Min81;
     #81#0 = #79#0;
     #81#1 = #28#0;
     #81#3 = "       ";
|FPRC;

|PROCESO Max81;
     #81#0 = #79#0;
     #81#1 = #28#0;
     #81#3 = "zzzzzzz";
|FPRC;

|PROCESO ConsultaTari; |TIPO 11;
     |SI FSalida = 13;
          |ABRE #81;
          |CONSULTA_F_CLAVE #1#81, Min81, Max81;
          |CIERRA #81;
          |ERROR;
     |FINSI;
|FPRC;

|PROGRAMA;
     aParam = PARAMETRO;

     aTab = &9;

     |HAZ CreaTempo; |NOME_DAT #27 Tempo; |DELINDEX #27; aTmp27 = Tempo; |ABRE #27;
     |HAZ CreaTempo; |NOME_DAT #28 Tempo; |DELINDEX #28; aTmp28 = Tempo; |ABRE #28;

     |SI aParam = "menu";
          |PINPA #0#0;
          |PARA; |SI; |HACIENDO;
               |PINDA #0#0;
               |ENTLINEAL #1#28, 1, 7, 20, 0, Min28, Max28;
               |ENDATOS #1#0;
               |SI FSalida ! 0; |SAL; |FINSI;
               |LEE 000#28p;
               |SI FSalida = 0;
                    |ENTLINEAL #1#28, 1, 4, 20, 0, Min28, Max28;
                    aNomFic = "resultado";
                    |HAZ GrabaResultado;
                    |LEE 000#27p;
                    |PARA; |SI FSalida = 0; |HACIENDO;
                         |BORRA 020#27a;
                         |LEE 000#27s;
                    |SIGUE;
                    |LEE 000#28p;
                    |PARA; |SI FSalida = 0; |HACIENDO;
                         |BORRA 020#28a;
                         |LEE 000#28s;
                    |SIGUE;
               |SINO;
                    aNomFic = "resultado";
                    |HAZ GrabaEnlaces;
                    |MENAV "0000No hay servicios...";
               |FINSI;
          |SIGUE;
     |SINO;
          aNomFic = aParam;
          |HAZ LeePeticion;
          |HAZ Inicio;
          |LEE 000#28p;
          |SI FSalida = 0;
               |HAZ GrabaResultado;
          |SINO;
               |HAZ GrabaEnlaces;
          |FINSI;
     |FINSI;
     |CIERRA #28; |DELINDEX #28; Tempo = aTmp28; |HAZ BoraTempo;
     |CIERRA #27; |DELINDEX #27; Tempo = aTmp27; |HAZ BoraTempo;
|FPRO;
