|FICHEROS;
     extm0024 #0;
     extm0025 #25;
|FIN;

|VARIABLES;
     nModo = 0;
     nConta = 0;
     aAlfa = "";
     aAlfa1 = "";
     aPosible = "";
     aLon = "";
     aCar = "";
     aDesde = "";
     aHasta = "";
     nPos = 0;
     aLonNum = "";
     i = 0;
     j = 0;
     k = 0;
|FIN;

|PROCESO T24_13; |TIPO 13;
     |ABRE #25;
     #25#0 = #0#2;
     |LEE 000#25=;
     |SI FSalida ! 0; |DDEFECTO #25; |FINSI;
     |CIERRA #25;
     |PINTA 1533, #25#1;
|FPRC;

|PROCESO TipoArti; |TIPO 0;
     |ABRE #25;
     #25#0 = #0#2;
     |LEE 000#25=;
     |SI FSalida = 0;
          |PINTA 1533, #25#1;
     |SINO;
          |MENAV "0000No existe c¢digo sistema...";
          |ERROR;
     |FINSI;
     |CIERRA #25;
|FPRC;

|PROCESO Conta; |TIPO 7;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 1;
          |LEE 000#0u;
          |SI FSalida = 0;
               nConta = #0#0 + 1;
          |SINO;
               nConta = 1;
          |FINSI;
          |DDEFECTO #0;
          #0#0 = nConta;
     |FINSI;
|FPRC;

|PROCESO EsNumerico;
     |QBF aAlfa;
     aLonNum = aAlfa % A0;
     |PARA k = 1; |SI k [ aLonNum; |HACIENDO k = k + 1;
          nPos = k * 100 + 1;
          aCar = aAlfa % nPos;
          aCar = "0123456789" - aCar;
          |SI FSalida = 0; |SAL; |FINSI;
     |SIGUE;
     |SI k > aLonNum;
          FSalida = 0;
     |SINO;
          FSalida = 1;
     |FINSI;
|FPRC;

|PROCESO Defecto; |TIPO 0;
     |SI #0#4 = "N";
          aAlfa = #0#7;
          |HAZ EsNumerico;
          |SI FSalida ! 0;
               |MENAV "0000El valor debe ser num‚rico...";
               |ERROR; |ACABA;
          |FINSI;
     |FINSI;
     |SI #0#5 ! 0;
          aAlfa = #0#7; |QBF aAlfa;
          aLon = aAlfa % A0;
          |SI aLon > #0#5;
               |MENAV "0000La longitud supera a la permitida...";
               |ERROR;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Posible; |TIPO 0;
     aPosible = #0#6; |QBF aPosible;
     aAlfa1 = aPosible - "/";
     |SI FSalida > 0;    || Desde - Hasta
          aAlfa = aAlfa1 - "/";
          |SI aAlfa ! aAlfa1;
               |MENAV "0000Valor erroneo (solo puede contener un '/') ...";
               |ERROR; |ACABA;
          |FINSI;
          aAlfa = aAlfa1 - ",";
          |SI aAlfa ! aAlfa1;
               |MENAV "0000Valor erroneo (no puede contener ',' y "/") ...";
               |ERROR; |ACABA;
          |FINSI;
          aAlfa = aPosible - "/";
          aAlfa = FSalida % 101;
          nPos = (aAlfa - 1) + 100;
          aDesde = aPosible % nPos;
          aAlfa = aDesde + "/";
          aHasta = aPosible - aAlfa;
          |SI #0#5 ! 0;
               aLon = aDesde % A0;
               |SI aLon > #0#5;
                    |MENAV "0000Valor erroneo (el desde supera la longitud) ...";
                    |ERROR; |ACABA;
               |FINSI;
               aLon = aHasta % A0;
               |SI aLon > #0#5;
                    |MENAV "0000Valor erroneo (el hasta supera la longitud) ...";
                    |ERROR; |ACABA;
               |FINSI;
               |SI #0#4 = "N";
                    aAlfa = aDesde;
                    |HAZ EsNumerico;
                    |SI FSalida ! 0;
                         |MENAV "0000Valor erroneo (el desde debe ser num‚rico) ...";
                         |ERROR; |ACABA;
                    |FINSI;
                    aAlfa = aHasta;
                    |HAZ EsNumerico;
                    |SI FSalida ! 0;
                         |MENAV "0000Valor erroneo (el hasta debe ser num‚rico) ...";
                         |ERROR; |ACABA;
                    |FINSI;
               |FINSI;
               |SI aDesde > aHasta;
                    |MENAV "0000Valor erroneo (el hasta es mayor al desde) ...";
                    |ERROR; |ACABA;
               |FINSI;
          |FINSI;
     |SINO;         || valores concretos
          aLon = aPosible % A0;
          aAlfa = "";
          j = aLon;
          |PARA i = 1; |SI i [ j; |HACIENDO i = i + 1;
               nPos = i * 100 + 1;
               aCar = aPosible % nPos;
               |SI aCar = "/";
                    |MENAV "0000Valor erroneo (no puede contener un '/') ...";
                    aAlfa = "";
                    |ERROR; |SAL;
               |FINSI;
               |SI aCar = ",";
                    |SI #0#4 = "N";
                         |HAZ EsNumerico;
                         |SI FSalida ! 0;
                              aAlfa = "0000Valor erroneo:" + aAlfa + " (debe ser num‚rico) ...";
                              |MENAV aAlfa;
                              aAlfa = "";
                              |ERROR; |SAL;
                         |FINSI;
                    |FINSI;
                    |SI #0#5 ! 0;
                         aLon = aAlfa % A0;
                         |SI aLon > #0#5;
                              aAlfa = "0000Valor erroneo:" + aAlfa + " (supera la longitud) ...";
                              |MENAV aAlfa;
                              aAlfa = "";
                              |ERROR; |SAL;
                         |FINSI;
                    |FINSI;
                    aAlfa = "";
               |SINO;
                    aAlfa = aAlfa + aCar;
               |FINSI;
          |SIGUE;
          |SI aAlfa ! "";
               |SI #0#4 = "N";
                    |HAZ EsNumerico;
                    |SI FSalida ! 0;
                         aAlfa = "0000Valor erroneo:" + aAlfa + " (debe ser num‚rico) ...";
                         |MENAV aAlfa;
                         |ERROR; |ACABA;
                    |FINSI;
               |FINSI;
               |SI #0#5 ! 0;
                    aLon = aAlfa % A0;
                    |SI aLon > #0#5;
                         aAlfa = "0000Valor erroneo:" + aAlfa + " (supera la longitud) ...";
                         |MENAV aAlfa;
                         |ERROR;
                    |FINSI;
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;
