|FICHEROS;
   vigz0102 #0;
   vigm0044 #10;
   vigw0031 #11;
   vigw0032 #12;

   cacuenta@ #1000;
   caclipro@ #1001;
   camaniva@ #1002;
   camandia@ #1003;
   calibros@ #1004;
   canempre@ #1005;
   cacuebal@ #1006;
   caivalin@ #1007;
   camaasic@ #1008;
   camaasil@ #1009;
   caconasi@ #1010;
   caivaasi@ #1011;
   cacuiv2c@ #1012;
   cacuiv2l@ #1013;
   caivases@ #1014;
|FIN;

|VARIABLES;
   aAlfa    = "";
   aAlfa1   = "";
   aAlfa2   = "";
   aAlfa3   = "";
   aReg     = "";
   aDirBase = "";
   aDirCont = "";
   aCuenta  = "";
   aCuentaBase = "";

   nNumOrden  = 0;
   nHandle    = 0;
   nTecla     = 0;
   nCalc      = 0;
   nCont      = 0;
   nSwDefecto = 0;
   nBloque    = 0;
   nTipo      = 0;
   nAgrup     = 0;
   nEpigra    = 0;
   nPartid    = 0;
   nNivel     = 0;
   nLinea     = 0;
   nAntAsto   = 0;
   nNumDocum  = 0;

   nNumBase    = 0;
   nImporte    = 0;
   nBaseExenta = 0;
   nBase       = 0;
   nBase1      = 0;
   nBase2      = 0;
   nBase3      = 0;
   nCuotaI     = 0;
   nCuotaI1    = 0;
   nCuotaI2    = 0;
   nCuotaI3    = 0;
   nCuotaR     = 0;
   nCuotaR1    = 0;
   nCuotaR2    = 0;
   nCuotaR3    = 0;
   nTipoI      = 0;
   nTipoI1     = 0;
   nTipoI2     = 0;
   nTipoI3     = 0;
   nTipoR      = 0;
   nTipoR1     = 0;
   nTipoR2     = 0;
   nTipoR3     = 0;
   nPorcI      = 0;
   nPorcR      = 0;
   nPIva1      = 0;
   nPIva2      = 0;
   nPIva3      = 0;
   aCifDni     = "";
   aCta        = 0;
   aCta1       = 0;
   aCta2       = 0;
   aCta3       = 0;

   aCR         = "";
   aLF         = "";
|FIN;

|PROCESO MiraEmpCont;
   |SI FSalida = 2; |ACABA; |FINSI;

   |SI FSalida = 10;
      #canempre@#2 = #vigz0102#7;
      #canempre@#3 = #vigz0102#8;
      |LEE 000#canempre@.];
      |CONSULTA_CLAVE #1#canempre@;

      #vigz0102#7 = #canempre@#2; |PINTA #vigz0102#7;
      #vigz0102#8 = #canempre@#3; |PINTA #vigz0102#8;
      #vigz0102#9 = #canempre@#1; |PINTA #vigz0102#9;
   |FINSI;

   |SI Campo = 7; |ACABA; |FINSI;

   #canempre@#2 = #vigz0102#7;
   #canempre@#3 = #vigz0102#8;
   |LEE 000#canempre@.=;
   |SI FSalida ! 0;
      |MENAV "    Empresa inexistente";
      |ERROR;
   |SINO;
      #vigz0102#9 = #canempre@#1; |PINTA #vigz0102#9;
   |FINSI;
|FPRC;

|PROCESO MiraDiario;
   nTecla = FSalida;
   |SI nTecla = 2; |ACABA; |FINSI;

   aAlfa = aDirCont + "fich/" + (("00000" + #vigz0102#7) % -105) + #vigz0102#8 + "/";
   |PATH_DAT #camandia@#aAlfa#;
   |ABRE #camandia@;

   |SI nTecla = 10;
      #camandia@#0 = #vigz0102#10;
      |LEE 000#camandia@.];
      |CONSULTA_CLAVE #1#camandia@;

      #vigz0102#10 = #camandia@#0; |PINTA #vigz0102#10;
      #vigz0102#11 = #camandia@#1; |PINTA #vigz0102#11;
   |FINSI;

   #camandia@#0 = #vigz0102#10;
   |LEE 000#camandia@.=;
   |SI FSalida ! 0;
      |MENAV "    Diario inexistente";
      |ERROR;
   |SINO;
      #vigz0102#11 = #camandia@#1; |PINTA #vigz0102#11;
   |FINSI;
   |CIERRA #camandia@;
|FPRC;

|PROCESO MiraLibro;
   nTecla = FSalida;
   |SI nTecla = 2; |ACABA; |FINSI;

   aAlfa = aDirCont + "fich/" + (("00000" + #vigz0102#7) % -105) + #vigz0102#8 + "/";
   |PATH_DAT #calibros@#aAlfa#;
   |ABRE #calibros@;

   |SI nTecla = 10;
      #calibros@#0 = #vigz0102#Campo#;
      |LEE 000#calibros@.];
      |CONSULTA_CLAVE #1#calibros@;

      nCalc = Campo + 1;
      #vigz0102#Campo# = #calibros@#0; |PINTA #vigz0102#Campo#;
      #vigz0102#nCalc# = #calibros@#1; |PINTA #vigz0102#nCalc#;
   |FINSI;

   #calibros@#0 = #vigz0102#Campo#;
   |LEE 000#calibros@.=;
   |SI FSalida ! 0;
      |MENAV "    Libro inexistente"; |ERROR;
   |SINO;
      nCalc = Campo + 1;
      |SI Campo = 12;
         |SI #calibros@#2 ! "R";
            |MENAV "    El libro no es de iva repercutido"; |ERROR;
         |SINO;
            #vigz0102#nCalc# = #calibros@#1; |PINTA #vigz0102#nCalc#;
         |FINSI;
      |SINO;
         |SI #calibros@#2 ! "S";
            |MENAV "    El libro no es de iva soportado"; |ERROR;
         |SINO;
            #vigz0102#nCalc# = #calibros@#1; |PINTA #vigz0102#nCalc#;
         |FINSI;
      |FINSI;
   |FINSI;
   |CIERRA #calibros@;
|FPRC;

|PROCESO CompruebaFichero;
  |SI FSalida = 2; |ACABA; |FINSI;

  aAlfa = #vigz0102#Campo#; |QBF aAlfa;
  |SI aAlfa = ""; |ACABA; |FINSI;

  |XABRE "CE", aAlfa, nHandle;
  |SI FSalida < 0;
     |MENAV "    Fichero inexistente";
     |ERROR;
  |FINSI;
|FPRC;

|PROCESO Cuentas;
  aAlfa = #vigz0102#1; |QBF aAlfa; |SI aAlfa = ""; |ACABA; |FINSI;

  aAlfa = aDirCont + "fich/" + (("00000" + #vigz0102#7) % -105) + #vigz0102#8 + "/";
  |PATH_DAT #cacuenta@#aAlfa#; |PATH_DAT #cacuebal@#aAlfa#;
  |ABRE #cacuenta@; |ABRE #cacuebal@;

  aAlfa = #vigz0102#1; |QBF aAlfa;
  |XABRE "CB", aAlfa, nHandle;
  |SI FSalida ] 0;
     FSalida = 1;
     |PARA; |SI; |HACIENDO;
        |XLEE nHandle, 12, aAlfa;  || Cuenta contable
        |SI FSalida = 0; |SAL; |FINSI;

        |QBF aAlfa;
        #cacuenta@#0 = aAlfa;
        |LEE 101#cacuenta@.=;
        |SI FSalida ! 0;
           nSwDefecto = 0;
           |PARA nCont = 19; |SI nCont ] 14; |HACIENDO nCont = nCont - 1;
              |SI #canempre@#nCont# ! 0;
                 nCalc = #canempre@#nCont#; nCalc = nCalc + 100;
                 aAlfa1 = aAlfa % nCalc;
                 #cacuebal@#0 = aAlfa1;
                 |LEE 000#cacuebal@.=;
                 |SI FSalida = 0;
                    nSwDefecto = 1;
                    nBloque = #cacuebal@#1;
                    nTipo   = #cacuebal@#2;
                    nAgrup  = #cacuebal@#3;
                    nEpigra = #cacuebal@#4;
                    nPartid = #cacuebal@#5;
                    |SAL;
                 |SINO;
                    |SI aAlfa1 ! aAlfa;
                       #cacuenta@#0 = aAlfa1;
                       |LEE 000#cacuenta@.=;
                       |SI FSalida = 0;
                          nSwDefecto = 1;
                          nBloque = #cacuebal@#1;
                          nTipo   = #cacuebal@#2;
                          nAgrup  = #cacuebal@#3;
                          nEpigra = #cacuebal@#4;
                          nPartid = #cacuebal@#5;
                          |SAL;
                       |FINSI;
                    |FINSI;
                 |FINSI;
              |FINSI;
           |SIGUE;
           #cacuenta@#0 = aAlfa;
           #cacuenta@#3 = "S";
           |SI nSwDefecto = 1;
              #cacuenta@#4 = nBloque;
              #cacuenta@#5 = nTipo;
              #cacuenta@#6 = nAgrup;
              #cacuenta@#7 = nEpigra;
              #cacuenta@#8 = nPartid;
           |FINSI;
           |GRABA 020#cacuenta@.b;
        |FINSI;

        |XLEE nHandle,  30, aAlfa; || Nombre cuenta contable
        #cacuenta@#2 = aAlfa;
        |GRABA 020#cacuenta@.a; |LIBERA #cacuenta@;

        |XLEE nHandle,  28, aAlfa; || Varios campos que no valen
        |SI FSalida = 0; |SAL; |FINSI;
        |XLEE nHandle, 180, aAlfa;
        |SI FSalida = 0; |SAL; |FINSI;
        |XLEE nHandle, 180, aAlfa;
        |SI FSalida = 0; |SAL; |FINSI;
        |XLEE nHandle, 180, aAlfa;
        |SI FSalida = 0; |SAL; |FINSI;
        |XLEE nHandle, 180, aAlfa;
        |SI FSalida = 0; |SAL; |FINSI;
        |XLEE nHandle, 180, aAlfa;
        |SI FSalida = 0; |SAL; |FINSI;
        |XLEE nHandle, 132, aAlfa;
        |SI FSalida = 0; |SAL; |FINSI;
        |XLEE nHandle,   1, aAlfa; || A (Alta), B (Baja), C (Cambio)
        |SI FSalida = 0; |SAL; |FINSI;

        |PARA; |SI; |HACIENDO;  || Busco el CR + LF
           |XLEE nHandle,   1, aAlfa;
           |SI FSalida = 0;
              |SAL;
           |SINO;
              |SI aAlfa = aCR;
                 |XLEE nHandle,   1, aAlfa;
                 |SI FSalida = 0;
                    |SAL;
                 |SINO;
                    |SI aAlfa = aLF; |SAL; |FINSI;
                 |FINSI;
              |FINSI;
           |FINSI;
        |SIGUE;
        |SI FSalida = 0; |SAL; |FINSI;
     |SIGUE;
     |XCIERRA nHandle;
  |FINSI;
  |CIERRA #cacuebal@; |CIERRA #cacuenta@;
|FPRC;

|PROCESO CliPro;
  aAlfa = #vigz0102#2; |QBF aAlfa; |SI aAlfa = ""; |ACABA; |FINSI;

  aAlfa = aDirCont + "fich/" + (("00000" + #vigz0102#7) % -105) + #vigz0102#8 + "/";
  |PATH_DAT #caclipro@#aAlfa#; |ABRE #caclipro@;

  aAlfa = #vigz0102#2; |QBF aAlfa;
  |XABRE "CB", aAlfa, nHandle;
  |SI FSalida ] 0;
     FSalida = 1;
     |PARA; |SI; |HACIENDO;
        |XLEE nHandle, 12, aAlfa; || Cuenta cli/pro
        |SI FSalida = 0; |SAL; |FINSI;
        aAlfa1 = aAlfa % 0103;
        |SI aAlfa1 = "430";
           #caclipro@#23 = "C";
        |SINO;
           #caclipro@#23 = "P";
        |FINSI;
        #caclipro@#10 = aAlfa;
        |LEE 101#caclipro@.=;
        |SI FSalida ! 0;
           |DDEFECTO #caclipro@;
           |SI aAlfa1 = "430";
              #caclipro@#23 = "C";
           |SINO;
              #caclipro@#23 = "P";
           |FINSI;
           #caclipro@#10 = aAlfa;
           |GRABA 020#caclipro@.b;
        |FINSI;

        |XLEE nHandle, 14, aAlfa; || NIF
        #caclipro@#9 = aAlfa;
        |XLEE nHandle, 30, aAlfa; || Direccion
        #caclipro@#2 = aAlfa;
        |XLEE nHandle, 20, aAlfa; || Poblacion
        #caclipro@#6 = aAlfa;
        |XLEE nHandle, 20, aAlfa; || Provincia
        #caclipro@#5 = aAlfa;
        |XLEE nHandle,  2, aAlfa; || C.P.1
        #caclipro@#3 = aAlfa;
        |XLEE nHandle,  3, aAlfa; || C.P.2
        #caclipro@#4 = aAlfa;
        |XLEE nHandle, 15, aAlfa; || Telefono
        #caclipro@#7 = aAlfa;
        |XLEE nHandle, 15, aAlfa; || Fax
        #caclipro@#8 = aAlfa;
        |GRABA 020#caclipro@.a; |LIBERA #caclipro@;

        |XLEE nHandle, 1, aAlfa; || A (Alta), B (Baja), C (Cambio)
        |SI FSalida = 0; |SAL; |FINSI;

        |PARA; |SI; |HACIENDO;  || Busco el CR + LF
           |XLEE nHandle,   1, aAlfa;
           |SI FSalida = 0;
              |SAL;
           |SINO;
              |SI aAlfa = aCR;
                 |XLEE nHandle,   1, aAlfa;
                 |SI FSalida = 0;
                    |SAL;
                 |SINO;
                    |SI aAlfa = aLF; |SAL; |FINSI;
                 |FINSI;
              |FINSI;
           |FINSI;
        |SIGUE;
        |SI FSalida = 0; |SAL; |FINSI;
     |SIGUE;
     |XCIERRA nHandle;
  |FINSI;
  |CIERRA #caclipro@;
|FPRC;

|PROCESO NivelCont;
   aAlfa2 = aCuenta; |QBF aAlfa2;
   aAlfa2 = aAlfa2 % 0; nCalc = aAlfa2;
   |PARA nCont = 14; |SI nCont [ 19; |HACIENDO nCont = nCont + 1;
      |SI nCalc = #canempre@#nCont#; nNivel = nCont - 13; |FINSI;
   |SIGUE;
|FPRC;

|PROCESO Diario;
  aAlfa = #vigz0102#3; |QBF aAlfa; |SI aAlfa = ""; |ACABA; |FINSI;

  aAlfa = aDirCont + "fich/" + (("00000" + #vigz0102#7) % -105) + #vigz0102#8 + "/";
  |PATH_DAT #camaasic@#aAlfa#; |PATH_DAT #camaasil@#aAlfa#;
  |PATH_DAT #caconasi@#aAlfa#; |PATH_DAT #caclipro@#aAlfa#;
  |ABRE #camaasic@; |ABRE #camaasil@; |ABRE #caconasi@;
  |ABRE #vigm0044;

  aAlfa = #vigz0102#3; |QBF aAlfa;
  |XABRE "CB", aAlfa, nHandle;
  |SI FSalida ] 0;
     FSalida = 1;
     |PARA; |SI; |HACIENDO;
        |XLEE nHandle, 6, aAlfa; || Numero de asiento
        |SI FSalida = 0; |SAL; |FINSI;
        nNumOrden = aAlfa; nCalc = nNumOrden;

        |SI nAntAsto ! nCalc;
           |LEE 101#caconasi@.p;
           |SI FSalida = 0;
              nNumDocum = #caconasi@#1;
              #caconasi@#1 = #caconasi@#1 + 1;
              |GRABA 020#caconasi@.a;
           |FINSI;
           nAntAsto = nCalc; nLinea = 1;

           |DDEFECTO #camaasic@;
           #camaasic@#0 = #vigz0102#10;

           |XLEE nHandle, 8, aAlfa1; || Fecha del asiento (AAAAMMDD)
           |SI FSalida = 0; |SAL; |FINSI;

           aAlfa1 = (aAlfa1 % 0702) + "." + (aAlfa1 % 0502) + "." + (aAlfa1 % 0104);
           #camaasic@#1 = aAlfa1;
           #camaasic@#2 = nNumDocum;
           #camaasic@#3 = nNumDocum;
           #camaasic@#13 = 000;
           |GRABA 000#camaasic@.b;      || si ya existe no se tiene en cuenta
        |SINO;
           |XLEE nHandle, 8, aAlfa; || Fecha del asiento (AAAAMMDD)
           |SI FSalida = 0; |SAL; |FINSI;
        |FINSI;

        #vigw0032#0 = nNumOrden;
        |LEE 000#vigw0032.=;
        |SI FSalida ! 0;
           |DDEFECTO #vigw0032;
           #vigw0032#0 = nNumOrden;
           #vigw0032#1 = #camaasic@#0;
           #vigw0032#2 = #camaasic@#1;
           #vigw0032#3 = #camaasic@#2;
           |GRABA 020#vigw0032.c;
        |FINSI;

        |DDEFECTO #camaasil@;
        #camaasil@#0 = #camaasic@#0;
        #camaasil@#1 = #camaasic@#1;
        #camaasil@#2 = #camaasic@#2;
        #camaasil@#3 = nLinea;       nLinea = nLinea + 1;

        |XLEE nHandle, 12, aAlfa; || Cuenta contable
        #camaasil@#4 = aAlfa;  aCuenta = aAlfa; nNivel = 0; |HAZ NivelCont;

        #camaasil@#5 = nNivel;
        |XLEE nHandle, 2,  aAlfa;  || Concepto contable
        |SI aAlfa ! " 0";
           #vigm0044#0 = aAlfa;
           |LEE 000#vigm0044.=;
           |SI FSalida = 0;
              #camaasil@#6 = #vigm0044#2;
           |SINO;
              #camaasil@#6 = 1;
           |FINSI;
        |SINO;
           #camaasil@#6 = 1;
        |FINSI;
        |XLEE nHandle, 40, aAlfa; || Descripcion concepto contable
        #camaasil@#7 = aAlfa;
        |XLEE nHandle, 1, aAlfa; || Signo contable
        #camaasil@#8 = aAlfa;
        |XLEE nHandle, 13, aAlfa; || Importe
        #camaasil@#9 = aAlfa;
        |XLEE nHandle, 13, aAlfa; || Importe en pesetas
        |XLEE nHandle, 1,  aAlfa; || Punteado
        |SI aAlfa = "F"
           #camaasil@#18 = " ";
        |SINO;
           #camaasil@#18 = "û";
        |FINSI;
        |SI #camaasil@#8 = "D";
           #camaasil@#16 = #camaasil@#4;
           #camaasil@#17 = #camaasil@#5;
        |SINO;
           #camaasil@#10 = #camaasil@#4;
           #camaasil@#11 = #camaasil@#5;
        |FINSI;
        |GRABA 000#camaasil@.c;

        #vigw0031#0 = #camaasil@#1;
        #vigw0031#1 = #camaasil@#2;
        #vigw0031#2 = #camaasil@#4;
        |LEE 000#vigw0031.=;
        |SI FSalida ! 0;
           |DDEFECTO #vigw0031;
           #vigw0031#0 = #camaasil@#1;
           #vigw0031#1 = #camaasil@#2;
           #vigw0031#2 = #camaasil@#4;
           |GRABA 020#vigw0031.c;
        |FINSI;
        #vigw0031#3 = #vigw0031#3 + #camaasil@#9;
        |GRABA 020#vigw0031.a;

        |SI #camaasil@#8 = "D";
           aAlfa = #camaasic@#8; |QBF aAlfa;
           |SI aAlfa = "";
              #camaasic@#8 = #camaasil@#4;
              #camaasic@#9 = #camaasil@#5;
           |FINSI;
           #camaasic@#4 = #camaasic@#4 + #camaasil@#9;
        |SINO;
           aAlfa = #camaasic@#10; |QBF aAlfa;
           |SI aAlfa = "";
              #camaasic@#10 = #camaasil@#4;
              #camaasic@#11 = #camaasil@#5;
           |FINSI;
           #camaasic@#5 = #camaasic@#5 + #camaasil@#9;
        |FINSI;
        #camaasic@#6 = #camaasic@#4 - #camaasic@#5;
        |GRABA 000#camaasic@.a;
        |LIBERA #camaasic@;

        |PARA; |SI; |HACIENDO;  || Busco el CR + LF
           |XLEE nHandle,   1, aAlfa;
           |SI FSalida = 0;
              |SAL;
           |SINO;
              |SI aAlfa = aCR;
                 |XLEE nHandle,   1, aAlfa;
                 |SI FSalida = 0;
                    |SAL;
                 |SINO;
                    |SI aAlfa = aLF; |SAL; |FINSI;
                 |FINSI;
              |FINSI;
           |FINSI;
        |SIGUE;
        |SI FSalida = 0; |SAL; |FINSI;
     |SIGUE;
     |XCIERRA nHandle;
  |FINSI;
  |CIERRA #camaasic@; |CIERRA #camaasil@; |CIERRA #caconasi@;
  |CIERRA #vigm0044;

  nNumDocum = 0;
|FPRC;

|PROCESO BuscaContrapartida;
   aCuenta = ""; aCuentaBase = "";
   #vigw0031#0 = #camaniva@#4;
   #vigw0031#1 = #camaniva@#9;
   #vigw0031#2 = "";
   |LEE 000#vigw0031.];
   |PARA; |SI FSalida = 0; |Y #vigw0031#0 = #camaniva@#4; |Y #vigw0031#1 = #camaniva@#9; |HACIENDO;
      aAlfa = #vigw0031#2; |QBF aAlfa; aAlfa = aAlfa % 0101;
      |SI aAlfa = "6"; |O aAlfa = "7";
         |QBF aCuentaBase;
         |SI aCuentaBase = "";
            aCuentaBase = #vigw0031#2;
         |SINO;
            aCuentaBase = "";
         |FINSI;
      |FINSI;

      |SI #vigw0031#3 = nImporte;
         aCuenta = #vigw0031#2; |SAL;
      |FINSI;
      |LEE 000#vigw0031.s;
   |SIGUE;

   |QBF aCuenta; |QBF aCuentaBase;
   |SI aCuenta = ""; |Y aCuentaBase ! ""; aCuenta = aCuentaBase; |FINSI;
|FPRC;

|PROCESO GrabaBase;
   |DDEFECTO #caivalin@;
   #caivalin@#0 = #camaniva@#0;
   #caivalin@#1 = #camaniva@#1;
   #caivalin@#2 = nNumBase;
   #caivalin@#5 = #caivaasi@#71;
   |SI nNumBase ! 99; || Base exenta
      |SI nNumBase = 1;
         nBase   = nBase1; nTipoI  = nTipoI1;
         nPorcI  = nPIva1;
         nPorcR  = 0.5;     nCuotaI = nCuotaI1;
      |FINSI;
      |SI nNumBase = 2;
         nBase   = nBase2; nTipoI  = nTipoI2;
         nPorcI  = nPIva2;
         nPorcR  = 1;       nCuotaI = nCuotaI2;
      |FINSI;
      |SI nNumBase = 3;
         nBase   = nBase3; nTipoI  = nTipoI3;
         nPorcI  = nPIva3;
         nPorcR  = 4;      nCuotaI = nCuotaI3;
      |FINSI;

      |SI nBase = 0; |ACABA; |FINSI;

      #caivalin@#6  = nBase;
      #caivalin@#7  = nPorcI;
      #caivalin@#8  = nCuotaI;
      |SI nCuotaR ! 0; #caivalin@#9  = nPorcR; |FINSI;
      #caivalin@#10 = nCuotaR;
      nImporte = nBase;   |HAZ BuscaContrapartida; #caivalin@#12 = aCuenta;
      nImporte = nCuotaI; |HAZ BuscaContrapartida; #caivalin@#13 = aCuenta;
      nImporte = nCuotaR; |HAZ BuscaContrapartida; #caivalin@#14 = aCuenta;
      |SI nTipoI ! 0; #caivalin@#16 = nTipoI; |SINO; #caivalin@#16 = nTipoR; |FINSI;
   |SINO;
      |SI nBaseExenta = 0; |ACABA; |FINSI;

      #caivalin@#6 = nBaseExenta;
      nImporte = nBaseExenta; |HAZ BuscaContrapartida; #caivalin@#12 = aCuenta;
   |FINSI;

   #caivalin@#3 = #camaniva@#29;
   #caivalin@#4 = #camaniva@#30;

   #caivalin@#30 = #camaniva@#10;
   #caivalin@#31 = #camaniva@#11;
   |GRABA 020#caivalin@.c;

   #camaniva@#19 = #camaniva@#19 + #caivalin@#6;
   #camaniva@#15 = #camaniva@#15 + #caivalin@#6;
   #camaniva@#17 = #camaniva@#15 %  #camaniva@#16;
   #camaniva@#20 = #camaniva@#20 + #caivalin@#22;
   #camaniva@#23 = #camaniva@#23 + (#caivalin@#8 + #caivalin@#10);
   #camaniva@#26 = #camaniva@#26 + #caivalin@#11;
   #camaniva@#21 = #camaniva@#19 + #camaniva@#23 - #camaniva@#26 - #camaniva@#20;

   |GRABA 020#camaniva@.a;
|FPRC;

|PROCESO Reperc;
  aAlfa = #vigz0102#4; |QBF aAlfa; |SI aAlfa = ""; |ACABA; |FINSI;
  |XABRE "CB", aAlfa, nHandle;
  |SI FSalida ] 0;
     FSalida = 1;
     |PARA; |SI; |HACIENDO;
        nNumDocum = 0;
        #camaniva@#0 = #vigz0102#12;
        #camaniva@#1 = 9999999999;
        |LEE 000#camaniva@.];
        |SI FSalida = 0;
           |SI #camaniva@#0 ! #vigz0102#12;
              |LEE 000#camaniva@.a;
              |SI FSalida = 0;
                 |SI #camaniva@#0 = #vigz0102#12;
                    nNumDocum = #camaniva@#1 + 1;
                 |SINO;
                    nNumDocum = 1;
                 |FINSI;
              |SINO;
                 nNumDocum = 1;
              |FINSI;
           |FINSI;
        |SINO;
           |LEE 000#camaniva@.u;
           |SI FSalida = 0;
              |SI #camaniva@#0 = #vigz0102#12;
                 nNumDocum = #camaniva@#1 + 1;
              |SINO;
                 nNumDocum = 1;
              |FINSI;
           |SINO;
              nNumDocum = 1;
           |FINSI;
        |FINSI;

        |DDEFECTO #camaniva@;
        #camaniva@#0 = #vigz0102#12;
        #camaniva@#1 = nNumDocum;

        |XLEE nHandle, 6, aAlfa; || Numero de orden
        |SI FSalida = 0; |SAL; |FINSI;

        #camaniva@#3 = aAlfa;
        #camaniva@#36 = "R";
        |GRABA 020#camaniva@.b;

        |XLEE nHandle, 6, aAlfa; || Numero de asiento
        #vigw0032#0 = aAlfa;
        |LEE 000#vigw0032.=;
        |SI FSalida ! 0; |DDEFECTO #vigw0032; |FINSI;

        |XLEE nHandle, 8, aAlfa; || Fecha asiento
        aAlfa = (aAlfa % 0702) + "." + (aAlfa % 0502) + "." + (aAlfa % 0104);
        #camaniva@#4 = aAlfa;
        #camaniva@#7 = #vigw0032#1;
        #camaniva@#8 = #vigw0032#2;
        #camaniva@#9 = #vigw0032#3;

        |XLEE nHandle, 1, aAlfa; || Punteado F/T
        |XLEE nHandle, 5, aAlfa; || Serie
        #camaniva@#2 = aAlfa;
        |XLEE nHandle, 8, aAlfa; || N§ Factura
        #camaniva@#3 = aAlfa;

        #caivases@#0 = #camaniva@#2;
        #caivases@#1 = "R";
        |LEE 101#caivases@.=;
        |SI FSalida ! 0;
           |DDEFECTO #caivases@;
           #caivases@#0 = #camaniva@#2;
           #caivases@#1 = "R";
           |GRABA 020#caivases@.b;
        |FINSI;
        |SI #camaniva@#3 = 0;
           #camaniva@#3 = #caivases@#3;
           #caivases@#3 = #caivases@#3 + #caivases@#4;
        |SINO;
           |SI #camaniva@#3 > #caivases@#3;
              #caivases@#3 = #camaniva@#3 + #caivases@#4;
           |FINSI;
        |FINSI;
        |GRABA 020#caivases@.a; |LIBERA #caivases@;

        aAlfa = #camaniva@#4; |QBF aAlfa;
        aAlfa = aAlfa % 0402; nCalc = aAlfa;
        |SI #caivaasi@#46 = "T";
           |SI nCalc ] 1;  |Y nCalc [ 3;  #camaniva@#5 = 1; |FINSI;
           |SI nCalc ] 4;  |Y nCalc [ 6;  #camaniva@#5 = 2; |FINSI;
           |SI nCalc ] 7;  |Y nCalc [ 9;  #camaniva@#5 = 3; |FINSI;
           |SI nCalc ] 10; |Y nCalc [ 12; #camaniva@#5 = 4; |FINSI;
        |SINO;
           #camaniva@#5 = nCalc;
        |FINSI;
        #camaniva@#10 = #caivaasi@#19;
        #camaniva@#11 = #caivaasi@#67;
        |XLEE nHandle, 12, aAlfa; || Cuenta contable
        #camaniva@#12 = aAlfa;

        #camaniva@#27 = #caivaasi@#5;
        #camaniva@#28 = #caivaasi@#7;
        #camaniva@#29 = #caivaasi@#13;
        #camaniva@#30 = #caivaasi@#15;

        nBaseExenta = 0; nBase1      = 0; nBase2      = 0; nBase3      = 0;
        nCuotaI1    = 0; nCuotaI2    = 0; nCuotaI3    = 0;
        nCuotaR1    = 0; nCuotaR2    = 0; nCuotaR3    = 0;
        nTipoI1     = 0; nTipoI2     = 0; nTipoI3     = 0;
        nTipoR1     = 0; nTipoR2     = 0; nTipoR3     = 0;
        nPIva1      = 0; nPIva2      = 0; nPIva3      = 0;

        |DDEFECTO #cacuiv2c@;
        aAlfa = #camaniva@#4; |QBF aAlfa; aAlfa = aAlfa % -104;
        #cacuiv2c@#0  = "R";
        #cacuiv2c@#20 = aAlfa; aAlfa = #cacuiv2c@#20;
        |LEE 000#cacuiv2c@.];
        |PARA; |SI FSalida = 0; |Y #cacuiv2c@#0  = "R"; |Y #cacuiv2c@#20 = aAlfa; |HACIENDO;
           |SI #cacuiv2c@#21 [ #camaniva@#4;
              |DDEFECTO #cacuiv2l@;
              #cacuiv2l@#0  = #cacuiv2c@#0;
              #cacuiv2l@#12 = #cacuiv2c@#20;
              #cacuiv2l@#13 = #cacuiv2c@#21;
              #cacuiv2l@#1  = 01;
              |LEE 000#cacuiv2l@.];
              |PARA; |SI FSalida = 0; |Y #cacuiv2l@#0  = #cacuiv2c@#0; |Y #cacuiv2l@#12 = #cacuiv2c@#20; |HACIENDO;
                 |SI #cacuiv2l@#2 = 4;
                    |SI #cacuiv2l@#4 = 0;
                       nTipoI1 = #cacuiv2l@#1;
                       nPIva1  = #cacuiv2l@#2;
                    |SINO;
                       nTipoR1 = #cacuiv2l@#1;
                    |FINSI;
                 |FINSI;
                 |SI #cacuiv2l@#2 = 7; |O #cacuiv2l@#2 = 8;
                    |SI #cacuiv2l@#4 = 0;
                       nTipoI2 = #cacuiv2l@#1;
                       nPIva2  = #cacuiv2l@#2;
                    |SINO;
                       nTipoR2 = #cacuiv2l@#1;
                    |FINSI;
                 |FINSI;
                 |SI #cacuiv2l@#2 = 16; |O #cacuiv2l@#2 = 18;
                    |SI #cacuiv2l@#4 = 0;
                       nTipoI3 = #cacuiv2l@#1;
                       nPIva3  = #cacuiv2l@#2;
                    |SINO;
                       nTipoR3 = #cacuiv2l@#1;
                    |FINSI;
                 |FINSI;
                 |LEE 000#cacuiv2l@.s;
              |SIGUE;
           |FINSI;
           |LEE 000#cacuiv2c@.s;
        |SIGUE;

        |XLEE nHandle, 30, aAlfa; || Comentario
        |XLEE nHandle, 11, aAlfa; || Base exenta
        nBaseExenta = aAlfa;
        |XLEE nHandle, 11, aAlfa; || Base exenta PTAS
        |XLEE nHandle, 11, aAlfa; || Base Reducida
        nBase2 = aAlfa;
        |XLEE nHandle, 11, aAlfa; || Base Reducida PTAS
        |XLEE nHandle, 11, aAlfa; || Base Rec.Equ.Reducido
        |XLEE nHandle, 11, aAlfa; || Base Rec.Equ.Reducido PTAS
        |XLEE nHandle, 11, aAlfa; || Base General
        nBase3 = aAlfa;
        |XLEE nHandle, 11, aAlfa; || Base General PTAS
        |XLEE nHandle, 11, aAlfa; || Base Rec.Equ.General
        |XLEE nHandle, 11, aAlfa; || Base Rec.Equ.General PTAS
        |XLEE nHandle, 11, aAlfa; || Base Super reducida
        nBase1 = aAlfa;
        |XLEE nHandle, 11, aAlfa; || Base Super reducida PTAS
        |XLEE nHandle, 10, aAlfa; || IVA Exento
        |XLEE nHandle, 10, aAlfa; || IVA Exento PTAS
        |XLEE nHandle, 10, aAlfa; || IVA Reducido
        nCuotaI2 = aAlfa;
        |XLEE nHandle, 10, aAlfa; || IVA Reducido PTAS
        |XLEE nHandle, 10, aAlfa; || NADA
        |XLEE nHandle, 10, aAlfa; || NADA
        |XLEE nHandle, 10, aAlfa; || IVA General
        nCuotaI3 = aAlfa;
        |XLEE nHandle, 10, aAlfa; || IVA General PTAS
        |XLEE nHandle, 10, aAlfa; || REC General
        nCuotaR3 = aAlfa;
        |XLEE nHandle, 10, aAlfa; || REC General PTAS
        |XLEE nHandle, 10, aAlfa; || IVA Super reducido
        nCuotaI1 = aAlfa;
        |XLEE nHandle, 10, aAlfa; || IVA Super reducido PTAS
        |XLEE nHandle, 11, aAlfa; || Total Factura
        |XLEE nHandle, 11, aAlfa; || Total Factura PTAS
        |XLEE nHandle, 11, aAlfa; || Base Rec.Equ.Super reducido
        |XLEE nHandle, 11, aAlfa; || Base Rec.Equ.Super reducido PTAS
        |XLEE nHandle, 10, aAlfa; || REC Super reducido
        nCuotaR1 = aAlfa;
        |XLEE nHandle, 10, aAlfa; || REC Super reducido PTAS

        nNumBase = 1;  |HAZ GrabaBase;
        nNumBase = 2;  |HAZ GrabaBase;
        nNumBase = 3;  |HAZ GrabaBase;
        nNumBase = 99; |HAZ GrabaBase;

        |XLEE nHandle, 14, aAlfa; || CIF/DNI
        aCifDni = aAlfa;

        |DDEFECTO #caclipro@;
        #caclipro@#23 = "C";
        #caclipro@#10 = #camaniva@#12;
        |LEE 000#caclipro@.=;
        |SI FSalida = 0;
             #camaniva@#13 = #caclipro@#1; || Nombre cliente
             #camaniva@#14 = #caclipro@#9; || CIF cliente
        |SINO;
             #camaniva@#13 = "";           || Nombre cliente
             #camaniva@#14 = aCifDni;      || CIF cliente
        |FINSI;

        |GRABA 020#camaniva@.a;
        |LIBERA #camaniva@;

        |PARA; |SI; |HACIENDO;  || Busco el CR + LF
           |XLEE nHandle,   1, aAlfa;
           |SI FSalida = 0;
              |SAL;
           |SINO;
              |SI aAlfa = aCR;
                 |XLEE nHandle,   1, aAlfa;
                 |SI FSalida = 0;
                    |SAL;
                 |SINO;
                    |SI aAlfa = aLF; |SAL; |FINSI;
                 |FINSI;
              |FINSI;
           |FINSI;
        |SIGUE;
        |SI FSalida = 0; |SAL; |FINSI;
     |SIGUE;
     |XCIERRA nHandle;
  |FINSI;
|FPRC;

|PROCESO Soport;
  aAlfa = #vigz0102#5; |QBF aAlfa; |SI aAlfa = ""; |ACABA; |FINSI;
  |XABRE "CB", aAlfa, nHandle;
  |SI FSalida ] 0;
     FSalida = 1;
     |PARA; |SI; |HACIENDO;
        nNumDocum = 0;
        #camaniva@#0 = #vigz0102#14;
        #camaniva@#1 = 9999999999;
        |LEE 000#camaniva@.];
        |SI FSalida = 0;
           |SI #camaniva@#0 ! #vigz0102#14;
              |LEE 000#camaniva@.a;
              |SI FSalida = 0;
                 |SI #camaniva@#0 = #vigz0102#14;
                    nNumDocum = #camaniva@#1 + 1;
                 |SINO;
                    nNumDocum = 1;
                 |FINSI;
              |SINO;
                 nNumDocum = 1;
              |FINSI;
           |FINSI;
        |SINO;
           |LEE 000#camaniva@.u;
           |SI FSalida = 0;
              |SI #camaniva@#0 = #vigz0102#14;
                 nNumDocum = #camaniva@#1 + 1;
              |SINO;
                 nNumDocum = 1;
              |FINSI;
           |SINO;
              nNumDocum = 1;
           |FINSI;
        |FINSI;

        |DDEFECTO #camaniva@;
        #camaniva@#0 = #vigz0102#14;
        #camaniva@#1 = nNumDocum;

        |XLEE nHandle, 6, aAlfa; || Numero de orden
        |SI FSalida = 0; |SAL; |FINSI;

        #camaniva@#3 = aAlfa;
        #camaniva@#36 = "S";
        |GRABA 020#camaniva@.b;

        |XLEE nHandle, 6, aAlfa; || Numero de asiento
        #vigw0032#0 = aAlfa;
        |LEE 000#vigw0032.=;
        |SI FSalida ! 0; |DDEFECTO #vigw0032; |FINSI;

        |XLEE nHandle, 8, aAlfa; || Fecha asiento
        aAlfa = (aAlfa % 0702) + "." + (aAlfa % 0502) + "." + (aAlfa % 0104);
        #camaniva@#4 = aAlfa;
        #camaniva@#7 = #vigw0032#1;
        #camaniva@#8 = #vigw0032#2;
        #camaniva@#9 = #vigw0032#3;

        |XLEE nHandle, 1, aAlfa; || Punteado F/T
        #camaniva@#2 = "";
        |XLEE nHandle, 8, aAlfa; || N§ Factura
        #camaniva@#3 = aAlfa;

        #caivases@#0 = "";
        #caivases@#1 = "S";
        |LEE 101#caivases@.=;
        |SI FSalida ! 0;
           |DDEFECTO #caivases@;
           #caivases@#0 = "";
           #caivases@#1 = "S";
           |GRABA 020#caivases@.b;
        |FINSI;
        |SI #camaniva@#3 = 0;
           #camaniva@#3 = #caivases@#3;
           #caivases@#3 = #caivases@#3 + #caivases@#4;
        |SINO;
           |SI #camaniva@#3 > #caivases@#3;
              #caivases@#3 = #camaniva@#3 + #caivases@#4;
           |FINSI;
        |FINSI;
        |GRABA 020#caivases@.a; |LIBERA #caivases@;

        aAlfa = #camaniva@#4; |QBF aAlfa;
        aAlfa = aAlfa % 0402; nCalc = aAlfa;
        |SI #caivaasi@#46 = "T";
           |SI nCalc ] 1;  |Y nCalc [ 3;  #camaniva@#5 = 1; |FINSI;
           |SI nCalc ] 4;  |Y nCalc [ 6;  #camaniva@#5 = 2; |FINSI;
           |SI nCalc ] 7;  |Y nCalc [ 9;  #camaniva@#5 = 3; |FINSI;
           |SI nCalc ] 10; |Y nCalc [ 12; #camaniva@#5 = 4; |FINSI;
        |SINO;
           #camaniva@#5 = nCalc;
        |FINSI;
        #camaniva@#10 = #caivaasi@#19;
        #camaniva@#11 = #caivaasi@#67;
        |XLEE nHandle, 12, aAlfa; || Cuenta contable
        #camaniva@#12 = aAlfa;

        #caclipro@#23 = "P";
        #caclipro@#10 = aAlfa;
        |LEE 000#caclipro@.=;
        |SI FSalida ! 0; |DDEFECTO #caclipro@; |FINSI;

        #camaniva@#13 = #caclipro@#1; || Nombre cliente/proveedor
        #camaniva@#14 = #caclipro@#9; || CIF cliente/proveedor

        #camaniva@#27 = #caivaasi@#31;
        #camaniva@#28 = #caivaasi@#33;
        #camaniva@#29 = #caivaasi@#57;
        #camaniva@#30 = #caivaasi@#59;

        nBaseExenta = 0; nBase1      = 0; nBase2      = 0; nBase3      = 0;
        nCuotaI1    = 0; nCuotaI2    = 0; nCuotaI3    = 0;
        nCuotaR1    = 0; nCuotaR2    = 0; nCuotaR3    = 0;
        nTipoI1     = 0; nTipoI2     = 0; nTipoI3     = 0;
        nTipoR1     = 0; nTipoR2     = 0; nTipoR3     = 0;

        |DDEFECTO #cacuiv2c@;
        aAlfa = #camaniva@#4; |QBF aAlfa; aAlfa = aAlfa % -104;
        #cacuiv2c@#0  = "S";
        #cacuiv2c@#20 = aAlfa; aAlfa = #cacuiv2c@#20;
        |LEE 000#cacuiv2c@.];
        |PARA; |SI FSalida = 0; |Y #cacuiv2c@#0  = "S"; |Y #cacuiv2c@#20 = aAlfa; |HACIENDO;
           |SI #cacuiv2c@#21 [ #camaniva@#4;
              |DDEFECTO #cacuiv2l@;
              #cacuiv2l@#0  = #cacuiv2c@#0;
              #cacuiv2l@#12 = #cacuiv2c@#20;
              #cacuiv2l@#13 = #cacuiv2c@#21;
              #cacuiv2l@#1  = 01;
              |LEE 000#cacuiv2l@.];
              |PARA; |SI FSalida = 0; |Y #cacuiv2l@#0  = #cacuiv2c@#0; |Y #cacuiv2l@#12 = #cacuiv2c@#20; |HACIENDO;
                 |SI #cacuiv2l@#2 = 4;
                    |SI #cacuiv2l@#4 = 0;
                       nTipoI1 = #cacuiv2l@#1;
                       nPIva1  = #cacuiv2l@#2;
                    |SINO;
                       nTipoR1 = #cacuiv2l@#1;
                    |FINSI;
                 |FINSI;
                 |SI #cacuiv2l@#2 = 7; |O #cacuiv2l@#2 = 8;
                    |SI #cacuiv2l@#4 = 0;
                       nTipoI2 = #cacuiv2l@#1;
                       nPIva2  = #cacuiv2l@#2;
                    |SINO;
                       nTipoR2 = #cacuiv2l@#1;
                    |FINSI;
                 |FINSI;
                 |SI #cacuiv2l@#2 = 16; |O #cacuiv2l@#2 = 18;
                    |SI #cacuiv2l@#4 = 0;
                       nTipoI3 = #cacuiv2l@#1;
                       nPIva3  = #cacuiv2l@#2;
                    |SINO;
                       nTipoR3 = #cacuiv2l@#1;
                    |FINSI;
                 |FINSI;
                 |LEE 000#cacuiv2l@.s;
              |SIGUE;
           |FINSI;
           |LEE 000#cacuiv2c@.s;
        |SIGUE;

        |XLEE nHandle, 30, aAlfa; || Comentario
        |XLEE nHandle, 1,  aAlfa; || ¨?¨?¨?
        |XLEE nHandle, 11, aAlfa; || Base exenta
        nBaseExenta = aAlfa;
        |XLEE nHandle, 11, aAlfa; || Base exenta PTAS
        |XLEE nHandle, 11, aAlfa; || Base Reducida
        nBase2 = aAlfa;
        |XLEE nHandle, 11, aAlfa; || Base Reducida PTAS
        |XLEE nHandle, 11, aAlfa; || Base Rec.Equ.Reducido
        |XLEE nHandle, 11, aAlfa; || Base Rec.Equ.Reducido PTAS
        |XLEE nHandle, 11, aAlfa; || Base General
        nBase3 = aAlfa;
        |XLEE nHandle, 11, aAlfa; || Base General PTAS
        |XLEE nHandle, 11, aAlfa; || Base Rec.Equ.General
        |XLEE nHandle, 11, aAlfa; || Base Rec.Equ.General PTAS
        |XLEE nHandle, 11, aAlfa; || Base Super reducida
        nBase1 = aAlfa;
        |XLEE nHandle, 11, aAlfa; || Base Super reducida PTAS
        |XLEE nHandle, 10, aAlfa; || IVA Exento
        |XLEE nHandle, 10, aAlfa; || IVA Exento PTAS
        |XLEE nHandle, 10, aAlfa; || IVA Reducido
        nCuotaI2 = aAlfa;
        |XLEE nHandle, 10, aAlfa; || IVA Reducido PTAS
        |XLEE nHandle, 10, aAlfa; || NADA
        |XLEE nHandle, 10, aAlfa; || NADA
        |XLEE nHandle, 10, aAlfa; || IVA General
        nCuotaI3 = aAlfa;
        |XLEE nHandle, 10, aAlfa; || IVA General PTAS
        |XLEE nHandle, 10, aAlfa; || REC General
        nCuotaR3 = aAlfa;
        |XLEE nHandle, 10, aAlfa; || REC General PTAS
        |XLEE nHandle, 10, aAlfa; || IVA Super reducido
        nCuotaI1 = aAlfa;
        |XLEE nHandle, 10, aAlfa; || IVA Super reducido PTAS
        |XLEE nHandle, 11, aAlfa; || Total Factura
        |XLEE nHandle, 11, aAlfa; || Total Factura PTAS
        |XLEE nHandle, 11, aAlfa; || Base Rec.Equ.Super reducido
        |XLEE nHandle, 11, aAlfa; || Base Rec.Equ.Super reducido PTAS
        |XLEE nHandle, 10, aAlfa; || REC Super reducido
        nCuotaR1 = aAlfa;
        |XLEE nHandle, 10, aAlfa; || REC Super reducido PTAS

        nNumBase = 1;  |HAZ GrabaBase;
        nNumBase = 2;  |HAZ GrabaBase;
        nNumBase = 3;  |HAZ GrabaBase;
        nNumBase = 99; |HAZ GrabaBase;

        |GRABA 020#camaniva@.a;
        |LIBERA #camaniva@;

        |PARA; |SI; |HACIENDO;  || Busco el CR + LF
           |XLEE nHandle,   1, aAlfa;
           |SI FSalida = 0;
              |SAL;
           |SINO;
              |SI aAlfa = aCR;
                 |XLEE nHandle,   1, aAlfa;
                 |SI FSalida = 0;
                    |SAL;
                 |SINO;
                    |SI aAlfa = aLF; |SAL; |FINSI;
                 |FINSI;
              |FINSI;
           |FINSI;
        |SIGUE;
        |SI FSalida = 0; |SAL; |FINSI;
     |SIGUE;
     |XCIERRA nHandle;
  |FINSI;
|FPRC;

|PROCESO PaseDatos;
   |HAZ Cuentas;
   |HAZ CliPro;

   aAlfa = "ti" + Usuario; |NOME_DAT #vigw0031#aAlfa#;
   |DELINDEX #vigw0031; |ABRE #vigw0031;
   |HAZ Diario;

   aAlfa  = aDirCont + "fich/" + (("00000" + #vigz0102#7) % -105) + #vigz0102#8 + "/";
   aAlfa3 = aDirCont + "fich/000000/";
   |PATH_DAT #camaniva@#aAlfa#; |PATH_DAT #caivalin@#aAlfa#;
   |PATH_DAT #caivaasi@#aAlfa#; |PATH_DAT #caclipro@#aAlfa#;
   |PATH_DAT #cacuiv2c@#aAlfa3#; |PATH_DAT #cacuiv2l@#aAlfa3#;
   |PATH_DAT #caivases@#aAlfa#;
   |ABRE #camaniva@; |ABRE #caivalin@; |ABRE #caivaasi@;
   |ABRE #caclipro@; |ABRE #cacuiv2c@; |ABRE #cacuiv2l@;
   |ABRE #vigm0044;  |ABRE #caivases@;
   |LEE 000#caivaasi@.p;
   |SI FSalida ! 0; |DDEFECTO #caivaasi@; |FINSI;

   |HAZ Reperc;
   |HAZ Soport;

   |CIERRA #vigw0031;  |DELINDEX #vigw0031;
   |CIERRA #vigm0044;  |CIERRA #caivases@;
   |CIERRA #cacuiv2c@; |CIERRA #cacuiv2l@; |CIERRA #caclipro@;
   |CIERRA #caivaasi@; |CIERRA #caivalin@; |CIERRA #camaniva@;

   || Lanzo el recalculo de saldos
   aAlfa = ":contagen/carecupe.tab canempr1 " + (("00000" + #vigz0102#7) % -105) + #vigz0102#8 + ";F";
   |SUB_EJECUTA aAlfa;
|FPRC;

|PROGRAMA;
   aCR = &13; aLF = &10;

   |DBASS aDirBase; |QBF aDirBase;
   aDirCont = aDirBase + "contagen/";
   aAlfa1 = aDirCont + "def/cacuenta.mas";
   |CARGA_DEF aAlfa1, cacuenta@;
   |SI FSalida ! 0;
      |ACABA;
   |FINSI;

   aAlfa1 = aDirCont + "def/caclipro.mas";
   |CARGA_DEF aAlfa1, caclipro@;
   |SI FSalida ! 0;
      |DESCARGA_DEF #cacuenta@;
      |ACABA;
   |FINSI;

   aAlfa1 = aDirCont + "def/camaniva.mas";
   |CARGA_DEF aAlfa1, camaniva@;
   |SI FSalida ! 0;
      |DESCARGA_DEF #caclipro@;
      |DESCARGA_DEF #cacuenta@;
      |ACABA;
   |FINSI;

   aAlfa1 = aDirCont + "def/camandia.mas";
   |CARGA_DEF aAlfa1, camandia@;
   |SI FSalida ! 0;
      |DESCARGA_DEF #camaniva@;
      |DESCARGA_DEF #caclipro@;
      |DESCARGA_DEF #cacuenta@;
      |ACABA;
   |FINSI;

   aAlfa1 = aDirCont + "def/calibros.mas";
   |CARGA_DEF aAlfa1, calibros@;
   |SI FSalida ! 0;
      |DESCARGA_DEF #camandia@;
      |DESCARGA_DEF #camaniva@;
      |DESCARGA_DEF #caclipro@;
      |DESCARGA_DEF #cacuenta@;
      |ACABA;
   |FINSI;

   aAlfa1 = aDirCont + "def/canempre.mas";
   |CARGA_DEF aAlfa1, canempre@;
   |SI FSalida ! 0;
      |DESCARGA_DEF #calibros@;
      |DESCARGA_DEF #camandia@;
      |DESCARGA_DEF #camaniva@;
      |DESCARGA_DEF #caclipro@;
      |DESCARGA_DEF #cacuenta@;
      |ACABA;
   |FINSI;

   aAlfa1 = aDirCont + "def/cacuebal.mas";
   |CARGA_DEF aAlfa1, cacuebal@;
   |SI FSalida ! 0;
      |DESCARGA_DEF #canempre@;
      |DESCARGA_DEF #calibros@;
      |DESCARGA_DEF #camandia@;
      |DESCARGA_DEF #camaniva@;
      |DESCARGA_DEF #caclipro@;
      |DESCARGA_DEF #cacuenta@;
      |ACABA;
   |FINSI;

   aAlfa1 = aDirCont + "def/caivalin.mas";
   |CARGA_DEF aAlfa1, caivalin@;
   |SI FSalida ! 0;
      |DESCARGA_DEF #cacuebal@;
      |DESCARGA_DEF #canempre@;
      |DESCARGA_DEF #calibros@;
      |DESCARGA_DEF #camandia@;
      |DESCARGA_DEF #camaniva@;
      |DESCARGA_DEF #caclipro@;
      |DESCARGA_DEF #cacuenta@;
      |ACABA;
   |FINSI;

   aAlfa1 = aDirCont + "def/camaasic.mas";
   |CARGA_DEF aAlfa1, camaasic@;
   |SI FSalida ! 0;
      |DESCARGA_DEF #caivalin@;
      |DESCARGA_DEF #cacuebal@;
      |DESCARGA_DEF #canempre@;
      |DESCARGA_DEF #calibros@;
      |DESCARGA_DEF #camandia@;
      |DESCARGA_DEF #camaniva@;
      |DESCARGA_DEF #caclipro@;
      |DESCARGA_DEF #cacuenta@;
      |ACABA;
   |FINSI;

   aAlfa1 = aDirCont + "def/camaasil.mas";
   |CARGA_DEF aAlfa1, camaasil@;
   |SI FSalida ! 0;
      |DESCARGA_DEF #camaasic@;
      |DESCARGA_DEF #caivalin@;
      |DESCARGA_DEF #cacuebal@;
      |DESCARGA_DEF #canempre@;
      |DESCARGA_DEF #calibros@;
      |DESCARGA_DEF #camandia@;
      |DESCARGA_DEF #camaniva@;
      |DESCARGA_DEF #caclipro@;
      |DESCARGA_DEF #cacuenta@;
      |ACABA;
   |FINSI;

   aAlfa1 = aDirCont + "def/caconasi.mas";
   |CARGA_DEF aAlfa1, caconasi@;
   |SI FSalida ! 0;
      |DESCARGA_DEF #camaasil@;
      |DESCARGA_DEF #camaasic@;
      |DESCARGA_DEF #caivalin@;
      |DESCARGA_DEF #cacuebal@;
      |DESCARGA_DEF #canempre@;
      |DESCARGA_DEF #calibros@;
      |DESCARGA_DEF #camandia@;
      |DESCARGA_DEF #camaniva@;
      |DESCARGA_DEF #caclipro@;
      |DESCARGA_DEF #cacuenta@;
      |ACABA;
   |FINSI;

   aAlfa1 = aDirCont + "def/caivaasi.mas";
   |CARGA_DEF aAlfa1, caivaasi@;
   |SI FSalida ! 0;
      |DESCARGA_DEF #caconasi@;
      |DESCARGA_DEF #camaasil@;
      |DESCARGA_DEF #camaasic@;
      |DESCARGA_DEF #caivalin@;
      |DESCARGA_DEF #cacuebal@;
      |DESCARGA_DEF #canempre@;
      |DESCARGA_DEF #calibros@;
      |DESCARGA_DEF #camandia@;
      |DESCARGA_DEF #camaniva@;
      |DESCARGA_DEF #caclipro@;
      |DESCARGA_DEF #cacuenta@;
      |ACABA;
   |FINSI;

   aAlfa1 = aDirCont + "def/cacuiv2c.mas";
   |CARGA_DEF aAlfa1, cacuiv2c@;
   |SI FSalida ! 0;
      |DESCARGA_DEF #caivaasi@;
      |DESCARGA_DEF #caconasi@;
      |DESCARGA_DEF #camaasil@;
      |DESCARGA_DEF #camaasic@;
      |DESCARGA_DEF #caivalin@;
      |DESCARGA_DEF #cacuebal@;
      |DESCARGA_DEF #canempre@;
      |DESCARGA_DEF #calibros@;
      |DESCARGA_DEF #camandia@;
      |DESCARGA_DEF #camaniva@;
      |DESCARGA_DEF #caclipro@;
      |DESCARGA_DEF #cacuenta@;
      |ACABA;
   |FINSI;

   aAlfa1 = aDirCont + "def/cacuiv2l.mas";
   |CARGA_DEF aAlfa1, cacuiv2l@;
   |SI FSalida ! 0;
      |DESCARGA_DEF #cacuiv2c@;
      |DESCARGA_DEF #caivaasi@;
      |DESCARGA_DEF #caconasi@;
      |DESCARGA_DEF #camaasil@;
      |DESCARGA_DEF #camaasic@;
      |DESCARGA_DEF #caivalin@;
      |DESCARGA_DEF #cacuebal@;
      |DESCARGA_DEF #canempre@;
      |DESCARGA_DEF #calibros@;
      |DESCARGA_DEF #camandia@;
      |DESCARGA_DEF #camaniva@;
      |DESCARGA_DEF #caclipro@;
      |DESCARGA_DEF #cacuenta@;
      |ACABA;
   |FINSI;

   aAlfa1 = aDirCont + "def/caivases.mas";
   |CARGA_DEF aAlfa1, caivases@;
   |SI FSalida ! 0;
      |DESCARGA_DEF #cacuiv2l@;
      |DESCARGA_DEF #cacuiv2c@;
      |DESCARGA_DEF #caivaasi@;
      |DESCARGA_DEF #caconasi@;
      |DESCARGA_DEF #camaasil@;
      |DESCARGA_DEF #camaasic@;
      |DESCARGA_DEF #caivalin@;
      |DESCARGA_DEF #cacuebal@;
      |DESCARGA_DEF #canempre@;
      |DESCARGA_DEF #calibros@;
      |DESCARGA_DEF #camandia@;
      |DESCARGA_DEF #camaniva@;
      |DESCARGA_DEF #caclipro@;
      |DESCARGA_DEF #cacuenta@;
      |ACABA;
   |FINSI;

   aAlfa1 = aDirCont + "fich/"; |PATH_DAT #canempre@#aAlfa1#;
   |ABRE #canempre@;
   aAlfa1 = "pp" + Usuario;     |NOME_DAT #vigw0032#aAlfa1#;
   |DELINDEX #vigw0032; |ABRE #vigw0032;

   |ABRE #vigz0102;
   |LEE 000#vigz0102.p;
   |SI FSalida ! 0;
      |DDEFECTO #vigz0102;
      |GRABA 020#vigz0102.c;
   |FINSI;

   |CLS;
   |CABEZA "Proceso importacion datos contabilidad";
   |PINPA #0#0; |PINDA #0#0;
   |ENDATOS #1#0;
   |SI FSalida = 0; |Y #vigz0102#6 = "S";
      #vigz0102#6 = "N"; |GRABA 020#vigz0102.a;
      |HAZ PaseDatos;
   |FINSI;

   |CIERRA #vigw0032; |DELINDEX #vigw0032;
   |CIERRA #vigz0102;

   |DESCARGA_DEF #caivases@;
   |DESCARGA_DEF #cacuiv2l@; |DESCARGA_DEF #cacuiv2c@;
   |DESCARGA_DEF #caivaasi@; |DESCARGA_DEF #caconasi@;
   |DESCARGA_DEF #camaasil@; |DESCARGA_DEF #camaasic@;
   |DESCARGA_DEF #caivalin@; |DESCARGA_DEF #cacuebal@;
   |CIERRA #canempre@; |DESCARGA_DEF #canempre@;
   |DESCARGA_DEF #calibros@; |DESCARGA_DEF #camandia@;
   |DESCARGA_DEF #camaniva@; |DESCARGA_DEF #caclipro@; |DESCARGA_DEF #cacuenta@;
|FPRO;
