|FICHEROS;
     fergam04 #0;
     fergam05 #1,MANTE;
     fergam02 #2;
     fergaz03 #3,MANTE;
     fergam01 #10;
     referen@;
|FIN;

|VARIABLES;
     nPanta = 0;
     nModo = 0;
     nModoEntra = 0;
     aAlfa = "";
     nGrid1 = -1;
     nRango = 0;
     nForma = 0;
     nId = 0;
     nLin = 0;
     nBtnConsulta = -1;
     nGrid = 0;
     nNum = 0;
     nMaquina = 0;
     aHora = "";
     aLon = "";
     aAlf1 = "";
     aAlf2 = "";
     aAlf3 = "";
|FIN;

|PROCESO Evento;
     |SI FSalida = 1; |O FSalida = 2;
          |SELECT #1#1;
          |LEE 000#1=;
     |FINSI;
     |SI FSalida = 4;
     |FINSI;
|FPRC;

|PROCESO Min;
     #1#0 = #0#0;
     #1#1 = #0#1;
     #1#2 = 001;
     #1#3 = 001;
|FPRC;

|PROCESO Max;
     #1#0 = #0#0;
     #1#1 = #0#1;
     #1#2 = 999;
     #1#3 = 999;
|FPRC;

|PROCESO T3_14; |TIPO 14;    ||(*)
     |SI nGrid1 = -1;
          nRango = 1 + 4 + 8 + 16 + 32;
          |LINEAL_SIMPLE #1#1, nRango, 1302, 2998, Min, Max, Evento;
          nGrid1 = FSalida;
     |FINSI;
|FPRC;

|PROCESO T3_8; |TIPO 8; ||(*)
     |ABRET #1;
|FPRC;

|PROCESO T3_9; |TIPO 9; ||(*)
     |CIERRAT #1;
|FPRC;

|PROCESO BorraLin;
     |BORRA 020#1a;
     |LIBERA #1;
|FPRC;

|DEFBUCLE BorraLin;
     #1#1;
     ;
     #0#0, #0#1, 001, 001;
     #0#0, #0#1, 999, 999;
     be;
     NULL, BorraLin;
|FIN;

|PROCESO T3_2; |TIPO 2; ||(*)
     nModo = (FEntrada / 100) ! 0;
     nForma = 0 +. 1;
     |SI nModo = 3;
          |BUCLE BorraLin;
     |FINSI;
|FPRC;

|PROCESO T3_0; |TIPO 0;     ||(*)
     aAlfa  = "|X000"; aAlfa = #0aAlfa; nId = aAlfa;
     |ESTADO_CONTROL nId, "ENABLE";

     |REFRESCACONTROL nGrid1;
|FPRC

|PROCESO T3_12; |TIPO 12;    ||(*)
     |SI nModoEntra = 1; |Y FSalida = 7;
          |BUCLE BorraLin;
     |FINSI;
|FPRC;

|PROCESO T3_30; |TIPO 30;    ||(*)
     nModo = (FEntrada / 100) ! 0;
     #1#0 = #0#0;
     #1#1 = #0#1;
     #1#2 = 1;
     #1#3 = 1;
     |LEE 000#1];
     |SI FSalida ! 0; |O #1#0 ! #0#0; |O #1#1 ! #0#1;
          |SI nModo = 1;
               |MENAV "0000Se anulan los datos entrados.";
          |SINO;
               |MENAV "0000No existen lineas, se borra la cabecera.";
          |FINSI;
          |BORRA 020#0a;
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO T3_13; |TIPO 13;
     |PINPA #0#0; nPanta = FSalida;
|FPRC;

|PROCESO T3_20; |TIPO 20;
     nModo = (FEntrada / 100) ! 0;
     |SI FSalida = "OPCION";
          FSalida = "Duplicar";
     |SINO;
          |DDEF aAlfa;
          aAlfa = aAlfa + "fergam04";
          |CARGA_DEF aAlfa, referen@;
          |SI FSalida = 0;
               |ABRE #referen@;

               |VENTANA 1025, 1776, -1, 17, "Duplicar turno";
               |HAZ PushVenModal;
               |DDEFECTO #3;
               #3#0 = #0#0;
               #3#1 = #0#1;
               |PINPA #0#3;
               |PINDA #0#3;
               |ENDATOS #1#3;
               |SI FSalida = 0;
                    |HAZ Duplica;
               |FINSI;
               |HAZ PopVenModal;

               |CIERRA #referen@;
               |DESCARGA_DEF #referen@;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO T3_7; |TIPO 7;
     nModoEntra = (FEntrada / 100) ! 0;
     |SI nModoEntra = 1;
          aAlfa  = "|X000"; aAlfa = #0aAlfa; nId = aAlfa;
          |ESTADO_CONTROL nId, "DISABLE";
     |FINSI;
|FPRC;

|PROCESO T3_1_7; |TIPO 7;
     nModoEntra = (FEntrada / 100) ! 0;
     |SI nModoEntra = 1;
          |BUCLE BorraLin;
          |REFRESCACONTROL nGrid1;
     |FINSI;
     |ESTADO_CONTROL nBtnConsulta, "DISABLE";
|FPRC;

|PROCESO DefeLin; |TIPO 7;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 1;
          nMaquina = #1#2;
          #1#0 = #0#0;
          #1#1 = #0#1;
          #1#2 = nMaquina;
          #1#3 = 999;
          |LEE 000#1];
          |SI FSalida = 0;
               |LEE 000#1a;
          |SINO;
               |LEE 000#1u;
          |FINSI;
          |SI FSalida = 0; |Y #1#0 = #0#0; |Y #1#1 = #0#1; |Y #1#2 = nMaquina;
               nLin = #1#3 + 1;
          |SINO;
               nLin = 1;
          |FINSI;
          |DDEFECTO #1;
          #1#0 = #0#0;
          #1#1 = #0#1;
          #1#2 = nMaquina;
          #1#3 = nLin; |PINTA #1#3;
          aAlfa  = "|X000"; aAlfa = #0aAlfa; nId = aAlfa;
          |ESTADO_CONTROL nId, "ENABLE";
     |FINSI;
|FPRC;

|PROCESO Maquina0; |TIPO 0;
     |ABRE #2;
     #2#0 = #1#2;
     |LEE 000#2=;
     |SI FSalida ! 0;
          |MENAV "0000No existe m quina...";
          |ERROR;
     |FINSI;
     |CIERRA #2;
|FPRC;

|PROCESO Maquina66; |TIPO 66;
     |ABRE #2;
     #2#0 = #1#2;
     |LEE 000#2];
     |CONSULTA_CLAVE #1#2;
     |SI FSalida = 0;
          #1#2 = #2#0; |PINTA #1#2;
     |SINO;
          |ERROR;
     |FINSI;
     |CIERRA #2;
|FPRC;

|PROCESO Operario0; |TIPO 0;
     |ABRE #10;
     #10#0 = #1#4;
     |LEE 000#10=;
     |SI FSalida = 0;
          #1#5 = #10#1; |PINTA #1#5;

          |DDEF aAlfa;
          aAlfa = aAlfa + "fergam05";
          |CARGA_DEF aAlfa, referen@;
          |SI FSalida = 0;
               |ABRE #referen@;
               |SELECT #2#referen@;
               #referen@#0 = #1#0;
               #referen@#1 = #1#1;
               #referen@#4 = #1#4;
               |LEE 000#referen@.];
               |PARA; |SI FSalida = 0; |Y #referen@#0 = #1#0; |Y #referen@#1 = #1#1; |Y #referen@#4 = #1#4; |HACIENDO;
                    |SI #referen@#0 ! #1#0; |O #referen@#1 ! #1#1; |O #referen@#2 ! #1#2; |O #referen@#3 ! #1#3;
                         |SI #referen@#4 = #1#4;
                              aAlfa = "0000El operario tiene turno en la maquina:" + #referen@#2 + " linea:" + #referen@#3;
                              |MENAV aAlfa; |ERROR;
                         |FINSI;
                    |FINSI;
                    |LEE 000#referen@.s;
               |SIGUE;
               |CIERRA #referen@;
               |DESCARGA_DEF #referen@;
          |FINSI;
     |SINO;
          |MENAV "0000No existe operario...";
          |ERROR;
     |FINSI;
     |CIERRA #2;
|FPRC;

|PROCESO Operario66; |TIPO 66;
     |ABRE #10;
     #10#0 = #1#4;
     |LEE 000#10];
     |CONSULTA_CLAVE #1#10;
     |SI FSalida = 0;
          #1#4 = #10#0; |PINTA #1#4;
     |SINO;
          |ERROR;
     |FINSI;
     |CIERRA #10;
|FPRC;

|PROCESO FormaHora;
     aLon = aHora % A0;
     |SI aLon = 5;
          aAlf1 = aHora % 102;
          aAlf2 = aHora % 402;
          nNum = aAlf1 $ 24; aAlf1 = nNum;
          nNum = aAlf2 $ 60; aAlf2 = nNum;
          aHora = (("00" + aAlf1)%-102)+":"+(("00" + aAlf2)%-102);
     |SINO;
          aAlf1 = aHora % 102;
          aAlf2 = aHora % 402;
          aAlf3 = aHora % 702;
          nNum = aAlf1 $ 24; aAlf1 = nNum;
          nNum = aAlf2 $ 60; aAlf2 = nNum;
          nNum = aAlf3 $ 60; aAlf3 = nNum;
          aHora = (("00" + aAlf1)%-102)+":"+(("00" + aAlf2)%-102)+":"+(("00" + aAlf3)%-102);
     |FINSI;
|FPRC;

|PROCESO Hora; |TIPO 0;
     aHora = #0Campo;
     |HAZ FormaHora;
     #0Campo = aHora; |PINTA #0Campo;
|FPRC;

|PROCESO DupTurno0; |TIPO 0;
     |SI FSalida = 2; |ACABA; |FINSI;
     |SI Campo = 1;
          #referen@#0 = #3#0;
          #referen@#1 = #3#1;
          |LEE 000#referen@.=;
          |SI FSalida ! 0;
               |MENAV "0000No existe turno...";
               |ERROR;
          |FINSI;
     |FINSI;
     |SI Campo = 3;
          #referen@#0 = #3#2;
          #referen@#1 = #3#3;
          |LEE 000#referen@.=;
          |SI FSalida = 0;
               |MENAV "0000El turno ya existe...";
               |ERROR;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO DupTurno66; |TIPO 66;
     |SI Campo [ 1;
          #referen@#0 = #3#0;
          #referen@#1 = #3#1;
     |SINO;
          #referen@#0 = #3#2;
          #referen@#1 = #3#3;
     |FINSI;
     |LEE 000#referen@.=;
     |CONSULTA_CLAVE #1#referen@;
     |SI FSalida = 0;
          |SI Campo [ 1;
               #3#0 = #referen@#0; |PINTA #3#0;
               #3#1 = #referen@#1; |PINTA #3#1;
          |SINO;
               #3#2 = #referen@#0; |PINTA #3#2;
               #3#3 = #referen@#1; |PINTA #3#3;
          |FINSI;
     |SINO;
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO DuplicaLin;
     #1#0 = #3#2;
     #1#1 = #3#3;
     |GRABA 020#1n;
|FPRC;

|DEFBUCLE DuplicaLin;
     #1#1;
     ;
     #3#0, #3#1, 001, 001;
     #3#0, #3#1, 999, 999;
     ;
     NULL, DuplicaLin;
|FIN;

|PROCESO Duplica;
     #0#0 = #3#2;
     #0#1 = #3#3;
     |LEE 000#0=;
     |SI FSalida ! 0;
          #0#0 = #3#0;
          #0#1 = #3#1;
          |LEE 020#0=;
          |SI FSalida = 0;
               |BUCLE DuplicaLin;
               #0#0 = #3#2;
               #0#1 = #3#3;
               |GRABA 020#0n;
               |LEE 020#0=;
               |PINDA #0#0;
          |FINSI;
     |FINSI;
|FPRC;
