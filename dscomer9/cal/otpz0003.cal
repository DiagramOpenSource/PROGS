|FICHEROS;
     otpz0003 #0;
     otpw0003 #3; || Tmp Cab
     otpw0004 #4; || Tmp Lin
     otpw0005 #5; || Tmp Cli
     otpw0006 #6; || Tmp Alumno
     agifa007 #7;
     agifa019 #19;
     agifa024 #24;
     agifa025 #25;
     otpm0003 #30; ||Aux.Fac
     agifa040 #40;
     dsm00003 #49;
     agifa066 #66; ||*
     agifa069 #69;
     agifa084 #84;
     agifa091 #91;
     agifm001 #101;
     agifm002 #102;
     agifa142 #142;
     agifa321 #321;
     agifa324 #324;
     caclipro@;
|FIN;

|INCLUYE i_varart;

|VARIABLES;
     &aDivisa = "";
     &aMoneda = "";
     &enSwMenu = 0;
     &Tempo = "";
     &eaTag = "";
     aTmp3 = "";
     aTmp4 = "";
     aTmp5 = "";
     aTmp6 = "";
     aRuta = "";
     nBoton1 = 0;
     nSw = 0;
     nLin = 0;
     nAlumno = 0;
     nIVA = 0;
     fmes = "";
     mes = 0;
     nImpo = 0;
     nDto = 0;
     imneto = 0;
     retencion = 0;
     nForma = 0;
     aSer = "";
     aNume = "";
     nMargen = 0;
     nNume = 0;
     nLinFac = 0;

     nPos = 0;
     nRem = 0;
     aAlfa = "";
     aPath = "";
     aCar = "";
     nHandle = 0;
     nPrime = 0;
     nFinCod = 0;
     aDes = "";
     aFic = "";
     aVal = "";
     nNivel = 0;
     aTmp = "";
     aIniCab = "";
     aIniLin = "";
     nCampo = 0;
     {100}Matriz = "";
     nReg = 0;
     aLon = "";
     aTag = "";
     aIniTag = "N";
     aFinTag = "N";
     aIniPro = "N";
     aFinPro = "N";
     aFinTagVacio = "N";
     nError = 0;
     aDomi = "";
     aCp1 = "";
     aCp2 = "";
     aCta = "";
     aDat = "";
     aEmp = "";
     aMas = "";
     nPre1 = 0;
     nPre2 = 0;
     aNonC = "";
     aExpC = "";
     aIniC = "";
     aFinC = "";
     aHoTo = "";
     aHoPr = "";
     aHoTu = "";
     aHoTe = "";
     aModC = "";
     aCodFP = "";
     aSpa = "";
     aAlf1 = "";
     aAlf2 = "";
     nCta = 0;
     i = 0;
     nDig = 0;
     aSerAnt = "";
     nHay = 0;

     aAlfa1 = "";
     aAlfa2 = "";
     aAlfa3 = "";
     aAlfa4 = "";
     aAlfa5 = "";

     &eaObs = "";
|FIN;

|PROCESO BuscaCta;
     aCta = "";
     aAlfa = #142#151; |QBF aAlfa;
     aEmp = ("000000" + #142#152) % -106;
     aDat = aAlfa + "fich/" + aEmp + "/";
     aMas = aAlfa + "def/caclipro";
     |CARGA_DEF aMas, caclipro@;
     |SI FSalida = 0;
          |PATH_DAT #caclipro@#aDat#;
          |ABRE #caclipro@;
          |SELECT #4#caclipro@;
          #caclipro@#9 = #24#15;
          |LEE 000#caclipro@.=;
          |SI FSalida = 0;
               aCta = #caclipro@#10;
          |FINSI;
          |SI aCta = "";
               nCta = 0;
               nDig = 5;
               |SELECT #1#caclipro@;
               |LEE 000#caclipro@.p;
               |PARA; |SI FSalida = 0; |HACIENDO;
                    |SI nCta < #caclipro@#10; |Y #caclipro@#23 = "C";
                         aAlfa = #caclipro@#10;
                         aAlfa = aAlfa % 102;
                         |SI aAlfa = "43";
                              nCta = #caclipro@#10;
                              nDig = #caclipro@#11;
                         |FINSI;
                    |FINSI;
                    |LEE 000#caclipro@.s;
               |SIGUE;
               nCta = nCta + 1;
               aCta = nCta;

               |DDEFECTO #caclipro@;
               #caclipro@#23 = "C";
               #caclipro@#10 = aCta;
               #caclipro@#11 = nDig;
               |LEE 101#caclipro@.=;
               |SI FSalida ! 0; |GRABA 020#caclipro@.b; |FINSI;
               #caclipro@#1 = #24#1;
               #caclipro@#2 = #24#8;
               #caclipro@#3 = #24#181;
               #caclipro@#4 = #24#182;
               #caclipro@#5 = #24#10;
               #caclipro@#6 = #24#9;
               #caclipro@#7 = #24#17;
               #caclipro@#8 = #24#18;
               #caclipro@#9 = #24#15;
               #caclipro@#24 = #24#205;
               |GRABA 020#caclipro@.a;
          |FINSI;
          |CIERRA #caclipro@;
          |DESCARGA_DEF #caclipro@;
     |FINSI;
     |SI aCta = "";
          aAlfa = "0000No existe cuenta para el cliente:" + #24#0 + ", con dni:" + #24#15;
          |MENAV aAlfa;
     |FINSI;
|FPRC;

|PROCESO DirEntrega;
     |ABRE #101;
     #101#0 = #24#3;
     |LEE 000#101=; || Zona
     |CIERRA #101;
     |ABRE #102;
     #102#0 = #24#189;
     |LEE 000#102=; ||Ruta
     |CIERRA #102;
     |ABRE #25;
     #25#0 = #24#25;
     |LEE 000#25=;
     |CIERRA #25;

     |DDEFECTO #49;
     #49#0 = #24#0;
     #49#1 = 1;
     |LEE 101#49=;
     |SI FSalida ! 0; |GRABA 021#49b; |FINSI;
     #49#2 = #24#5;
     #49#3 = #24#6;
     #49#4 = #24#178;
     #49#5 = #24#179;
     #49#6 = #24#180;
     #49#7 = #24#7;
     #49#8 = #24#14;
     #49#10 = #24#2;
     #49#11 = #24#3;
     #49#12 = #101#1;
     #49#13 = #24#17;
     #49#14 = #24#25;
     #49#15 = #25#1;
     #49#16 = #24#189;
     #49#17 = #102#1;
     #49#19 = #24#205;
     #49#20 = #24#196;
     #49#21 = #24#18;
     #49#22 = #24#36;
     #49#23 = #24#211;
     #49#24 = #24#197;
     |GRABA 020#49a;
     |LIBERA #49;
|FPRC;

|PROCESO CreaCliente;
     |PARA i = 2; |SI i [ 26; |HACIENDO i = i + 1;
          aAlfa = #5i; |QBF aAlfa; aAlfa = aAlfa < "";
          |SI aAlfa = "null"; #5i = ""; |FINSI;
     |SIGUE;

     |SELECT #1#24;
     |DDEFECTO #24;
     aAlfa = #5#0; |QBF aAlfa;
     aAlfa = ("000000" + aAlfa) % -106;
     #24#0 = aAlfa;
     |LEE 000#24=;
     |SI FSalida = 0;
          aAlfa = "0000El cliente:" + aAlfa + " ya existe en gestion con el NIF:" + #24#15;
          |MENAV aAlfa;
          aAlfa = "0000El NIF del cliente:" + #5#0 + " del xml es:" + #5#1;
          |MENAV aAlfa;
     |SINO;
          #24#15 = #5#1;
          #24#1 = #5#2;

          aDomi = #5#4; |QBF aDomi;
          aAlfa = #5#5; |QBF aAlfa;
          |SI aAlfa ! ""; aDomi = aDomi + " N:" + aAlfa; |FINSI;
          aAlfa = #5#6; |QBF aAlfa;
          |SI aAlfa ! ""; aDomi = aDomi + " Bis:" + aAlfa; |FINSI;
          aAlfa = #5#7; |QBF aAlfa;
          |SI aAlfa ! ""; aDomi = aDomi + " Esc:" + aAlfa; |FINSI;
          aAlfa = #5#8; |QBF aAlfa;
          |SI aAlfa ! ""; aDomi = aDomi + " Piso:" + aAlfa; |FINSI;
          aAlfa = #5#9; |QBF aAlfa;
          |SI aAlfa ! ""; aDomi = aDomi + " Letra:" + aAlfa; |FINSI;
          #24#8 = aDomi; #24#11 = aDomi;

          aCp1 = #5#10 % 102; aCp2 = #5#10 % 303;
          #24#181 = aCp1; #24#182 = aCp2;

          #24#184 = aCp1; #24#185 = aCp2;
          #24#180 = #5#10; #24#186 = #5#10;

          #24#9 = #5#11; #24#12 = #5#11;
          #24#10 = #5#26; #24#13 = #5#26;
          #24#17 = #5#12;
          #24#18 = #5#13;
          #24#197 = #5#14;

          aAlfa = #5#15; |QBF aAlfa;
          |SI aAlfa = "";
               #24#5 = #24#8;
          |SINO;
               #24#5 = #5#15;
          |FINSI;

          aAlfa = #5#16; |QBF aAlfa;
          |SI aAlfa = "";
               #24#178 = #24#181; #24#179 = #24#182;
               #24#183 = #24#180; #24#211 = #24#180;
          |SINO;
               aCp1 = #5#16 % 102; aCp2 = #5#16 % 303;
               #24#178 = aCp1; #24#179 = aCp2;
               #24#183 = #5#16; #24#211 = #5#16;
          |FINSI;

          #24#6 = #5#18;

          aAlfa = #24#6; |QBF aAlfa;
          |SI aAlfa = ""; #24#6 = #24#9; |FINSI;

          aAlfa = #24#7; |QBF aAlfa;
          |SI aAlfa = "";  #24#7 = #24#10; |FINSI;

          #24#22  = #5#19;
          #24#23  = #5#20;
          #24#24  = #5#21;

          #24#32  = #5#22;
          #24#33  = #5#23;
          #24#31  = #5#24;
          #24#30  = #5#25;

          aAlfa1  = #5#27;  |QBF aAlfa1;
          aAlfa2  = #5#22;  |QBF aAlfa2;
          aAlfa3  = #5#23;  |QBF aAlfa3;
          aAlfa4  = #5#24;  |QBF aAlfa4;
          aAlfa5  = #5#25;  |QBF aAlfa5;

          #24#215 = aAlfa1 + aAlfa2 + aAlfa3 + aAlfa4 + aAlfa5;
          #24#217 = #5#28;

          |HAZ BuscaCta;
          #24#16 = aCta;
          |GRABA 020#24n
          |HAZ DirEntrega;
     |FINSI;
|FPRC;

|PROCESO ActArti;
     fmes = #84#1 % A402;

     mes = fmes - 1;
     mes = mes * 5;
     mes = mes + 35;
     #19#0 = #69#2;
     |LEE 101#19=;
     |SI FSalida = 0;
          |SI #19#194 = 2;
               nImpo = ((((#69#30 * #69#6) < #69#8) < #69#21) < #84#5) < #84#32;
          |SINO;
               nImpo = ((((#69#5 * #69#6) < #69#8) < #69#21) < #84#5) < #84#32;
          |FINSI;
          |SI #142#115 = "S";
               nImpo = nImpo < #84#7;
          |FINSI;
          nMargen = nImpo - #69#14;
          #19mes = #19mes + (nImpo * nForma);
          #19#95 = #19#95 + (nImpo * nForma);
          mes = mes + 1;
          #19mes = #19mes + (nMargen * nForma);
          #19#96 = #19#96 + (nMargen * nForma);
          |GRABA 020#19a;
     |FINSI;
     |LIBERA #19;

     efFecha = #84#1;
     eaArticulo = #69#2;
     enImpVta = (nImpo * nForma);
     enImpMar = (nMargen * nForma);
     |HAZ AcumulaArt;
|FPRC;

|CALCULO fcaccli;
     nForma = 1;

     |ABRE #24;
     #24#0 = #84#3;
     |LEE 101#24=;
     |SI FSalida = 0;
          fmes = #84#1 % A402;
          mes = fmes - 1;
          mes = mes * 4;
          mes = mes + 54;
          imneto = #84#15 - #84#25;
          |SI #142#115 = "N";
               nImpo = (imneto * 100) / (100 - #84#7);
               nDto = #84#25 + (imneto - nImpo);
          |SINO;
               nImpo = imneto;
               nDto = #84#25;
          |FINSI;
          #24mes = #24mes + nImpo; || imneto;
          mes = mes + 1;
          #24mes = #24mes + nDto;  || #84#25;
          mes = mes + 1;
          #24mes = #24mes + #84#37;
          #24#102 = #24#102 + nImpo; || imneto;
          #24#103 = #24#103 + nDto;  || #84#25;
          #24#104 = #24#104 + #84#37;
          mes = FEntrada / 100;
          mes = mes ! 0;
          |SI 1 = mes;
               #24#45 = #84#1;
               #24#46 = imneto;
          |FINSI;
          #24#110 = #24#110 + imneto;
          imneto = imneto + #84#24;

          aAlfa1 = #24#32;                 |QBF aAlfa1;
          aAlfa1 = aAlfa1 + " " + #24#33;  |QBF aAlfa1;
          aAlfa1 = aAlfa1 + " " + #24#31;  |QBF aAlfa1;
          aAlfa1 = aAlfa1 + " " + #24#30;  |QBF aAlfa1;

          aAlfa2 = #5#22;                  |QBF aAlfa2;
          aAlfa2 = aAlfa2 + " " + #5#23;   |QBF aAlfa2;
          aAlfa2 = aAlfa2 + " " + #5#24;   |QBF aAlfa2;
          aAlfa2 = aAlfa2 + " " + #5#25;   |QBF aAlfa2;

          |SI aAlfa2 ! "";
              |SI aAlfa1 ! aAlfa2;
                  aAlfa = "Cambiada cuenta bancaria en el cliente. Antes - ";
                  aAlfa = aAlfa + aAlfa1 + " Ahora -  ";
                  aAlfa = aAlfa + aAlfa2;

                  #otpw0003#10 = aAlfa;

                  #24#32  = #5#22;
                  #24#33  = #5#23;
                  #24#31  = #5#24;
                  #24#30  = #5#25;

                  aAlfa1  = #5#27;  |QBF aAlfa1;
                  aAlfa2  = #5#22;  |QBF aAlfa2;
                  aAlfa3  = #5#23;  |QBF aAlfa3;
                  aAlfa4  = #5#24;  |QBF aAlfa4;
                  aAlfa5  = #5#25;  |QBF aAlfa5;

                  #24#215 = aAlfa1 + aAlfa2 + aAlfa3 + aAlfa4 + aAlfa5;
                  #24#217 = #5#28;
              |FINSI;
          |FINSI;

          |GRABA 020#24a;
          |LIBERA #24;

          enImpCliCom = nImpo * nForma;
          enImpCliDto = nDto * nForma;
          enImpCliMar = #84#37 * nForma;
          enImpCliFac = imneto * nForma;
          eaCliente = #84#3;
          efFecha = #84#1;
          |HAZ AcumulaCli;
     |FINSI;
     |CIERRA #24;

     |ABRE #25;
     #25#0 = #84#6;
     |LEE 121#25=;
     |SI 0 = FSalida;
           imneto = #84#15 - #84#25;
           fmes = #84#1 % A402;
           mes = fmes - 1;
           mes = mes * 2;
           mes = mes + 11;
           #25mes = #25mes + #84#26;
           retencion = #25mes % #25#39;
           mes = mes + 1;
           #25mes = #25mes + imneto;
           #25#35 = #25#35 + #84#26;
           #25#36 = #25#36 + imneto;
           mes = fmes - 1;
           mes = mes + 40;
           #25mes = retencion;
           retencion = #25#35 % #25#39;
           #25#52 = retencion;
           |GRABA 020#25a;
           |LIBERA #25;

           enImpRepComi = #84#26 * nForma;
           enImpRepVta = imneto * nForma;
           enImpRepRete = retencion * nForma;
           eaRepre = #84#6;
           efFecha = #84#1;
           |HAZ AcumulaRep;
     |FINSI;
     |CIERRA #25;

     |ABRE #19;
     |BUCLELIN 2 ActArti #0;
     |CIERRA #19;
     |SI #142#47 = "S";  ||컴컴컴컴컴 Acumula en riesgos Si Crea Recibo
          eaTag = "FC";  |HAZ Riesgos;
     |FINSI;
|FCAL;

|PROCESO BuscaIVA;
     nIVA = -1;
     |ABRE #66;
     |LEE 000#66p;
     |PARA; |SI FSalida = 0; |HACIENDO;
          nNume = #66#2 / 100;
          |SI #4#5 = nNume;
               nIVA = #66#0; |SAL;
          |FINSI;
          |LEE 000#66s;
     |SIGUE;
     |CIERRA #66;
     |SI nIVA = -1;
          aAlfa = #4#0; |QBF aAlfa;
          aAlfa = "0000No se encuentra el iva para la factura:" + aAlfa + "/" + #4#1;
          |MENAV aAlfa;
          nIVA = 2;
     |FINSI;
|FPRC;

|PROCESO BuscaCli;
     |ABRE #24;
     #5#0 = #3#2;
     |LEE 000#5=;
     |SI FSalida ! 0;
          aAlfa = "0000Falta cliente:" + #3#2 + " de factura:" + #3#0 + " en el XML"
          |MENAV aAlfa;
          #24#0 = "000000";
     |SINO;
          |SELECT #4#24;
          #24#15 = #5#1;
          |LEE 000#24=;
          |SI FSalida ! 0;
               |HAZ CreaCliente;
          |FINSI;
     |FINSI;
     |SELECT #1#24;
     |LEE 000#24=;
     |CIERRA #24;

     |ABRE #25;
     #25#0 = #24#25;
     |LEE 000#25=;
     |CIERRA #25;
|FPRC;

|PROCESO Tmp6;
     |DDEFECTO #19;
     |ABRE #19;
     #19#0 = #0#3;
     |LEE 000#19=;
     |CIERRA #19;

     nLinFac = nLinFac + 1;
     |DDEFECTO #69;
     #69#20 = #84#48;
     #69#0 = #84#0;
     #69#1 = nLinFac;
     #69#2 = #19#0;
     #69#3 = 1;
     aAlfa = #6#4; |QBF aAlfa;
     aAlfa = aAlfa + " " + #6#3; |QBF aAlfa;
     aAlfa = aAlfa + ", NIF:" + #6#2;
     #69#4 = aAlfa;
     #69#13 = #19#2;
     #69#15 = #84#3;
     #69#16 = #84#34;
     #69#17 = #84#35;
     #69#31 = #6#5;        ; ||Partida = FianlizaAbandona
     |GRABA 020#69n;
|FPRC;

|DEFBUCLE Tmp6;
     #6#1;
     ;
     #3#0, 001;
     #3#0, 999;
     ;
     NULL, Tmp6;
|FIN;

|PROCESO Tmp4;
     |ABRE #19;
     |SI nLinFac = 1;         || Cada factura trae 2 lineas
          #19#0 = #0#2;
     |SINO;
          #19#0 = #0#4;
     |FINSI;
     |LEE 020#19=;
     |CIERRA #19;

     nLinFac = nLinFac + 1;
     |DDEFECTO #69;
     #69#20 = #84#48;
     #69#0 = #84#0;
     #69#1 = nLinFac;
     #69#2 = #19#0;
     #69#3 = 1;
     #69#4 = #4#3;
     #69#5 = 1;
     #69#6 = #4#4; #69#26 = #4#4;
     |HAZ BuscaIVA;
     #69#11 = nIVA;
     |HAZ TotalLin69;
     enForma = 1;
     |HAZ AcumulaFC;
     #69#14 = enCoste;
     #69#13 = #19#2;
     #69#15 = #84#3;
     #69#16 = #84#34;
     #69#17 = #84#35;
     |GRABA 020#69n;
     |HAZ CalCabAgifa084_2;
|FPRC;

|DEFBUCLE Tmp4;
     #4#1;
     ;
     #3#0, 001;
     #3#0, 999;
     ;
     NULL, Tmp4;
|FIN;

|PROCESO Linea1;
     |ABRE #19;
     #19#0 = "";
     |LEE 020#19=;
     |CIERRA #19;

     nLinFac = nLinFac + 1;
     |DDEFECTO #69;
     #69#20 = #84#48;
     #69#0 = #84#0;
     #69#1 = nLinFac;
     #69#2 = #19#0;
     #69#3 = 1;
     #69#4 = "Expediente:" + #3#3;
     #69#13 = #19#2;
     #69#15 = #84#3;
     #69#16 = #84#34;
     #69#17 = #84#35;
     |GRABA 020#69n;
|FPRC;

|PROCESO DatosCli;
     |SI #142#229 = "E";
          || Deja las del dsm00003
     |FINSI;

     |SI #142#229 = "F";
          #84#30 = #24#8;
          #84#31 = #24#9;
          #84#64 = #24#183;
          #84#33 = #24#10;
     |FINSI;
     |SI #142#229 = "C";
          #84#30 = #24#11;
          #84#31 = #24#12;
          #84#64 = #24#186;
          #84#33 = #24#13;
     |FINSI;
|FPRC;

|PROCESO BusFPago;
     aCodFP = "";
     |ABRE #91;
     |LEE 000#91p;
     |PARA; |SI FSalida = 0; |HACIENDO;
          aAlf1 = #91#1; |QBT aAlf1; aAlf1 = aAlf1 > "";
          aAlf2 = #3#4; |QBT aAlf2; aAlf2 = aAlf2 > "";
          |SI aAlf1 = aAlf2;
               aCodFP = #91#0;
          |FINSI;
          |LEE 000#91s;
     |SIGUE;
     |CIERRA #91;
|FPRC;

|PROCESO Tmp3;
     aAlfa = #3#0 - "/";
     nPos = FSalida;
     nPos = 99 + ((nPos - 1) / 100);
     aSer = #3#0 % nPos;
     aAlfa = aSer + "/";
     aNume = #3#0 - aAlfa;

     #7#26 = aSer;
     |LEE 000#7=;
     |SI FSalida ! 0;
          |SI aSerAnt ! aSer;
               aSerAnt = aSer;
               aAlfa = "0000La Serie '" + aSer + "' no existe...";
               |MENAV aAlfa;
          |FINSI;
          |ACABA;
     |FINSI;
     nHay = 1;

     |DDEFECTO #84;
     #84#48 = aSer;
     #84#0 = aNume;
     |LEE 000#84=;
     |SI FSalida = 0;
          aAlfa = #3#0; |QBF aAlfa;
          aAlfa = "0000Factura:" + aAlfa + " ya existe en gestion, no se importa";
          |MENAV aAlfa; |ACABA;
     |FINSI;
     |GRABA 020#84b;

     aAlfa = (#3#1 % 102) + "." + (#3#1 % 402) + "." + (#3#1 % 704);
     #84#1 = aAlfa;
     #84#93 = #3#5;

     #84#94 = "ALUMNOS:" + #3#7;
     #84#95 = "NFRAS:" + #3#0;
     |HAZ BuscaCli;
     |HAZ LimCabAgifa084;

     #84#3 = #24#0;
     #84#4 = #24#1;
     #84#6 = #24#25;

     |HAZ BusFPago;
     #84#8 = aCodFP; ||#24#20;
     #84#91 = 1;

     #49#0 = #84#3;
     #49#1 = #84#91;
     |LEE 020#49=;
     |SI FSalida = 0;
          #84#91 = #49#1;
          #84#29 = #49#10;
          #84#30 = #49#2;
          #84#31 = #49#3;
          #84#33 = #49#7;
          #84#64 = #49#6;
          |SI #49#14 = "  ";
              #84#6 = #24#25;
          |SINO;
              #84#6 = #49#14;
          |FINSI;
          |HAZ DatosCli;
     |FINSI;
     #84#34 = #25#7;
     #84#35 = #24#4;
     #84#36 = #24#47;
     #84#40 = #24#16;
     #84#60 = #24#15;
     #84#65 = #24#192;

     #324#0 = #84#65;
     |LEE 000#324=;
     #84#66 = #324#2;

     #84#67 = #24#193;
     #84#68 = #321#9;

     #324#0 = #84#68;
     |LEE 000#324=;
     #84#69 = #324#2;

     #84#88 = #24#22;
     #84#89 = #24#23;
     #84#70 = #24#24;

     aDivisa = #84#65;
     aMoneda = #84#67;
     |HAZ Divisas084;

     nLinFac = 0;
     |HAZ Linea1;
     |BUCLE Tmp4;
     |BUCLE Tmp6;

     |GRABA 020#84a;
     |LIBERA #84;

     |HAZ fcaccli;

     #3#8 = #84#48;
     #3#9 = #84#0;
     |GRABA 020#3a;

     #30#0 = #84#48;
     #30#1 = #84#0;
     |LEE 000#30=;
     |SI FSalida ! 0;
         |DDEFECTO #30;
         #30#0 = #84#48;
         #30#1 = #84#0;
         |GRABA 020#30b;
     |FINSI;

     #30#2 = aNonC;
     #30#3 = aExpC;
     #30#4 = aIniC;
     #30#5 = aFinC;
     #30#6 = aHoTo;
     #30#7 = aHoPr;
     #30#8 = aHoTu;
     #30#9 = aHoTe;
     #30#10 = aModC;
     #30#11 = aSpa;

     |GRABA 020#30a;
     |LIBERA #30;
|FPRC;

|DEFBUCLE Tmp3;
     #3#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, Tmp3;
|FIN;

|PROCESO ProcesaTagPop;
  ||   aAlfa = "S0000" + aTag + ":" + aVal;
  ||   |CONFI aAlfa; |SI FSalida ! 0; nError = 1; |FINSI;

     |SI aVal = "null"; aVal = ""; |FINSI;

     |SI nSw = 1;
          |SI aTag = "NFRAS"; #3#0 = aVal; |FINSI;
          |SI aTag = "FechaFactura"; #3#1 = aVal; |FINSI;
          |SI aTag = "Cliente"; #3#2 = aVal; |FINSI;
          |SI aTag = "expediente"; #3#3 = aVal; |FINSI;
          |SI aTag = "FormadePago"; #3#4 = aVal; |FINSI;
          |SI aTag = "CCC"; #3#5 = aVal; |FINSI;
          |SI aTag = "TipoFactura"; #3#6 = aVal; |FINSI;
          |SI aTag = "AlumnosFinalizados";
               #3#7  = aVal;
               #3#10 = "";
               |GRABA 000#3n;
               |SI FSalida ! 0;
                    nError = 1;
                    aAlfa = "0000Factura " + #3#0 + " duplicada en el xml...";
                    |MENAV aAlfa;
               |FINSI;
               nLin = 1;
               nAlumno = 1;
          |FINSI;
     |FINSI;
     |SI nSw = 2;
          |SI aTag = "Cliente";           #5#0 = aVal; |FINSI;
          |SI aTag = "CIF";               #5#1 = aVal; |FINSI;
          |SI aTag = "RazonSocial";       #5#2 = aVal; |FINSI;
          |SI aTag = "TipoVia";           #5#3 = aVal; |FINSI;
          |SI aTag = "Domicilio";         #5#4 = aVal; |FINSI;
          |SI aTag = "Numero";            #5#5 = aVal; |FINSI;
          |SI aTag = "Bis";               #5#6 = aVal; |FINSI;
          |SI aTag = "Escalera";          #5#7  = aVal; |FINSI;
          |SI aTag = "Piso";              #5#8  = aVal; |FINSI;
          |SI aTag = "Letra";             #5#9  = aVal; |FINSI;
          |SI aTag = "CodigoPostal";      #5#10 = aVal; |FINSI;
          |SI aTag = "Poblacion";         #5#11 = aVal; |FINSI;
          |SI aTag = "provincia";         #5#26 = aVal; |FINSI;
          |SI aTag = "Telefono";          #5#12 = aVal; |FINSI;
          |SI aTag = "fax";               #5#13 = aVal; |FINSI;
          |SI aTag = "correoelectronico"; #5#14 = aVal; |FINSI;
          |SI aTag = "DireccionF";        #5#15 = aVal; |FINSI;
          |SI aTag = "CPF";               #5#16 = aVal; |FINSI;
          |SI aTag = "ProvinciaF";        #5#17 = aVal; |FINSI;
          |SI aTag = "PoblacionF";        #5#18 = aVal; |FINSI;
          |SI aTag = "diapago1";          #5#19 = aVal; |FINSI;
          |SI aTag = "diapago2";          #5#20 = aVal; |FINSI;
          |SI aTag = "diapago3";          #5#21 = aVal; |FINSI;
          |SI aTag = "CCIBAN";            #5#27 = aVal; |FINSI;
          |SI aTag = "CCBco";             #5#22 = aVal; |FINSI;
          |SI aTag = "CCSucursal";        #5#23 = aVal; |FINSI;
          |SI aTag = "CCDC";              #5#24 = aVal; |FINSI;
          |SI aTag = "CCCTA";             #5#25 = aVal; |FINSI;
          |SI aTag = "FechaFirma";
              |QBF aVal;
              |SI aVal ! "";
                  #5#28 = (aVal % 102) + "." + (aVal % 402) + "." + (aVal % -104);
              |FINSI;
          |FINSI;

          |SI aTag = "FormadePago";
              |GRABA 000#5n;
          |FINSI;
     |FINSI;
     |SI nSw = 3;
          |SI aTag = "NFRAS"; #4#0 = aVal; |FINSI;
          |SI aTag = "expediente"; #4#2 = aVal; |FINSI;
          |SI aTag = "Descripcion"; #4#3 = aVal; |FINSI;
          |SI aTag = "Base"; #4#4 = aVal; |FINSI;
          |SI aTag = "TipoIVA";
               #4#5 = aVal;
               #4#1 = nLin;
               |GRABA 020#4n;
               nLin = nLin + 1;
          |FINSI;
     |FINSI;
     |SI nSw = 4;
          |SI aTag = "NFRAS"; #6#0 = aVal; |FINSI;
          |SI aTag = "NIF"; #6#2 = aVal; |FINSI;
          |SI aTag = "Apellidos"; #6#3 = aVal; |FINSI;
          |SI aTag = "Nombre";  #6#4 = aVal; |FINSI;
          |SI aTag = "FinalizaAbandona";
               #6#1 = nAlumno;
               #6#5 = aVal;
               |GRABA 020#6n;
               nAlumno = nAlumno + 1;
          |FINSI;
     |FINSI;
     |SI nSw = 5;
          |SI aTag = "nombre"; aNonC = aVal; |FINSI;
          |SI aTag = "expediente"; aExpC = aVal; |FINSI;
          |SI aTag = "inicio"; aIniC = aVal; |FINSI;
          |SI aTag = "fin"; aFinC = aVal; |FINSI;
          |SI aTag = "HorasTotales"; aHoTo = aVal; |FINSI;
          |SI aTag = "HorasPresenciales"; aHoPr = aVal; |FINSI;
          |SI aTag = "HorasTutorias"; aHoTu = aVal; |FINSI;
          |SI aTag = "HorasTeleformacion"; aHoTe = aVal; |FINSI;
          |SI aTag = "modalidad"; aModC = aVal; |FINSI;
          |SI aTag = "SPA"; aSpa = aVal; |FINSI;
     |FINSI;
|FPRC;

|PROCESO ProcesaTagPush;
     |SI aTag = "Facturas"; nSw = 1; |FINSI;
     |SI aTag = "Clientes"; nSw = 2; |FINSI;
     |SI aTag = "Lineas"; nSw = 3; |FINSI;
     |SI aTag = "Alumnos"; nSw = 4; |FINSI;
     |SI aTag = "Curso"; nSw = 5; |FINSI;
|FPRC;

|PROCESO ProcesaTagVacio;
|FPRC;

|PROCESO ProcesaPrologo;
|FPRC;

|PROCESO ProcesaRem;
|FPRC;

|PROCESO PopTag;
     nNivel = nNivel - 1;
     IMatriz = Matriz1<-;
     IMatriz = IMatriz + nNivel;
     FSalida = Matriz;
|FPRC;

|PROCESO PushTag;
     IMatriz = Matriz1<-;
     IMatriz = IMatriz + nNivel;
     Matriz = aTag;
     nNivel = nNivel + 1;
|FPRC;

|PROCESO Caracter;
     |SI aCar < " "; |O nError ! 0; |ACABA; |FINSI;

     |SI nRem > 0;
          |SI nRem < 3;
               |SI aCar = "-";
                    nRem = nRem + 1;
               |SINO;
                    aAlfa = "0000ERROR en XML: " + aTag;
                    |MENAV aAlfa;
                    nError = 1;
               |FINSI;
          |SINO;
               |SI aCar = ">"; || Un comentario no puede tener el caracter '>'
                    aAlfa = aTag % -102;
                    |SI aAlfa = "--";
                         aLon = aTag % A0;
                         nPos = (aLon - 2) + 100;
                         aTag = aTag % nPos;
                         |HAZ ProcesaRem;
                    |SINO;
                         aAlfa = "0000ERROR en XML: " + aTag;
                         |MENAV aAlfa;
                         nError = 1;
                    |FINSI;
                    aTag = ""; nRem = 0;
               |SINO;
                    aLon = aTag % A0;
                    |SI aLon < 250; aTag = aTag + aCar; |FINSI;
               |FINSI;
          |FINSI;
          |ACABA;
     |FINSI;

     |SI aCar = "<";
          aIniTag = "S"; aTag = "";
     |SINO;
          |SI aIniTag = "S";
               |SI aCar = "/";
                    |SI aTag = "";
                         aFinTag = "S";
                    |SINO;
                         aFinTagVacio = "S";
                    |FINSI;
               |SINO;
                    |SI aCar = "!";
                         |SI aTag = "";
                              nRem = 1;
                         |SINO;
                              aAlfa = "0000ERROR en XML: " + aTag;
                              |MENAV aAlfa;
                              nError = 1;
                         |FINSI;
                         |ACABA;
                    |FINSI;
                    |SI aCar = "?";
                         |SI aIniPro = "S";
                              aFinPro = "S";
                              aIniPro = "N";
                         |SINO;
                              |SI aFinPro = "S";
                                   aAlfa = "0000ERROR en XML: " + aTag;
                                   |MENAV aAlfa;
                                   nError = 1;
                              |SINO;
                                   aIniPro = "S"; aTag = "";
                              |FINSI;
                         |FINSI;
                    |SINO;
                         |SI aCar = ">";
                              |QBT aTag; ||*** solo deberia quitarse a izq y der.
                              |SI aFinPro = "S";
                                   |HAZ ProcesaPrologo;
                                   aFinPro = "N";
                              |SINO;
                                   |SI aFinTag = "S";
                                        |HAZ PopTag;
                                        |SI aTag ! FSalida; ||*** fallaria si en el tag
                                                            ||    se definen 'atributos'
                                             aAlfa = "0000ERROR en XML: <" + FSalida + "> " + aVal;
                                             |MENAV aAlfa;
                                             nError = 1;
                                        |SINO;
                                             |HAZ ProcesaTagPop;
                                        |FINSI;
                                   |SINO;
                                        |SI aFinTagVacio = "S";
                                             |HAZ ProcesaTagVacio;
                                             aFinTagVacio = "N";
                                        |SINO;
                                             |HAZ PushTag;
                                             |HAZ ProcesaTagPush;
                                        |FINSI;
                                   |FINSI;
                              |FINSI;
                              aIniTag = "N"; aFinTag = "N"; aTag = ""; aVal = "";
                         |SINO;
                              aLon = aTag % A0;
                              |SI aLon < 250; aTag = aTag + aCar; |FINSI;
                         |FINSI;
                    |FINSI;
               |FINSI;
          |SINO;
               aLon = aVal % A0;
               |SI aLon < 250; aVal = aVal + aCar; |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO ProcesaXml;
     nError = 0;
     |XABRE "BC", aFic, nHandle;
     |SI FSalida ] 0;
          |XLEE nHandle, 250, aTmp;
          |PARA; |SI FSalida > 0; |HACIENDO;
               |PARA; |SI aTmp ! ""; |HACIENDO;
                    aCar = aTmp % 101;
                    |SI aTmp = aCar;
                         aTmp = ""; ||con un caracter falla: "aTmp = aTmp % 2"
                    |SINO;
                         aTmp = aTmp % 2;
                    |FINSI;
                    |HAZ Caracter;
                    |SI nError ! 0; |SAL; |FINSI;
               |SIGUE;
               |XLEE nHandle, 250, aTmp;
               |SI nError ! 0; |SAL; |FINSI;
          |SIGUE;
          |XCIERRA nHandle;
     |SINO;
          aAlfa = "0000ERROR al abrir fichero";
          |MENAV aAlfa;
     |FINSI;
|FPRC;

|PROCESO Imprime;
     |DDEFECTO #84;
     #84#48 = #3#8;
     #84#0 = #3#9;
     |LEE 000#84=;

     eaObs = #otpw0003#10;  |QBF eaObs;

     |PRINT;
|FPRC;

|DEFBUCLE Imprime;
     #3#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, Imprime;
|FIN;

|PROCESO Tipo3; |TIPO 3;
     |ABRE #142; |LEE 000#142p; |CIERRA #142;
     |ABRE #321; |LEE 000#321p; |CIERRA #321;
     |HAZ CreaTempo; aTmp3 = Tempo; |NOME_DAT #3 Tempo; |DELINDEX #3;
     |HAZ CreaTempo; aTmp4 = Tempo; |NOME_DAT #4 Tempo; |DELINDEX #4;
     |HAZ CreaTempo; aTmp5 = Tempo; |NOME_DAT #5 Tempo; |DELINDEX #5;
     |HAZ CreaTempo; aTmp6 = Tempo; |NOME_DAT #6 Tempo; |DELINDEX #6;
     |ABRE #3;
     |ABRE #4;
     |ABRE #5;
     |ABRE #6;
     aFic = #0#1; |QBF aFic;
     |HAZ ProcesaXml;
     |SI nError ! 0;
          |MENAV "0000Se ha producido un error, importacion anulada...";
     |SINO;
          |ABRE #84;
          |ABRE #324;
          |ABRE #69;
          |ABRE #49;
          |ABRE #5;
          |ABRE #30;
          |ABRE #7;
          |BUCLE Tmp3;
          |CIERRA #7;
          |CIERRA #30;
          |CIERRA #5;
          |CIERRA #49;
          |CIERRA #69;
          |CIERRA #324;

          |SI nHay ! 0;
               |IMPRE 0;
               |SI FSalida = 0;
                    |INFOR "otpz0003";
                    |SI FSalida = 0;
                         |BUCLE Imprime;
                         |PIEINF;
                         |FININF;
                    |SINO;
                         |MENAV "0000Error en informe 'otpz0003'";
                    |FINSI;
                    |FINIMP;
               |FINSI;
          |FINSI;
          |CIERRA #84;
     |FINSI;
     |CIERRA #6;
     |CIERRA #5;
     |CIERRA #4;
     |CIERRA #3;
     |DELINDEX #3;
     |DELINDEX #4;
     |DELINDEX #5;
     |DELINDEX #6;
|FPRC;

|PROCESO Browser; |TIPO 0;
     |SI FSalida ! 10; |ACABA; |FINSI;

     aRuta = "F*.xml";

     |BROWSE aRuta;

     aRuta = aRuta % 2; |QBF aRuta;
     |SI aRuta = "F"; aRuta = ""; |FINSI;

     |QBF aRuta;
     |SI aRuta = "";
          |ERROR;
     |SINO;
          #0#1 = aRuta;
          |PINTA #0#1;
     |FINSI;
|FPRC;

|PROCESO Boton1;
     |PTEC 824;
|FPRC;

|PROGRAMA;
     |CLS;
     |CONTROL_SIMPLE 0, "BOTON,  Fichero ", 1119, 1127, Boton1;
     nBoton1 = FSalida;
     |ABRE #0;
     |LEE 101#0p;
     |SI FSalida ! 0; |GRABA 020#0b; |FINSI;
     |PINPA #0#0;
     |PINDA #0#0;
     |ENDATOS #1#0;
     |SI FSalida = 0;
          |GRABA 020#0a;
     |FINSI;
     |CIERRA #0;
     |FIN_CONTROL_WINDOWS nBoton1;
     |PULSATECLA;
|FPRO;
