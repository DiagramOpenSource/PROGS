|FICHEROS;
     gocoy001 #0;

     gomantho #10;  ||Expedientes CAB
     gomantl1 #11;  ||Expedientes LIN
     gopre001 #12;  ||Presupuestos CAB
     gopre003 #13;  ||Presup. Partidas
     gocercab #14;  ||Certificaciones.
     gocodpos #40;  ||Codigos Postales
     gofase01 #1;
     gofase02 #2;
     gopartia #15;
     gocerlin #24;
     goparame #20;
|FIN;

|VARIABLES;
     nSw0 = 0;
     nMax  = 0;
     &enCer     = 0;
     nHayPre     = 0;
     nNumPre    = 0;
     nExiste    = 0;
     aMensaje   = "";
     aInforme   = "";
     nSwPie     = 0;
     &enImpCert = 0;
     &enDif     = 0;
     &entDif    = 0;
     &enFecIni  = @;
     &enFecFin  = @;
     &enDifCom  = 0;
     &enDifPrg  = 0;
     &enEje     = 0;
     fFecha     = @;
|FIN;

|PROCESO gocerlin;
     |SI #24#15 ! 0;
          enCer = enCer + #24#15;
     |SINO;
          enCer = enCer + #24#6;
     |FINSI;
     nHayPre = 1;
|FPRC;

|PROCESO ImporCerti;
     nHayPre = 0;
     enCer = 0;
     |DDEFECTO #gocercab;
     #gocercab#0 = #gomantl1#3;
     #gocercab#1 = #gomantl1#4;
     |LEE 000#gocercab.=;
     |SI FSalida = 0;
          |BUCLELIN 2 gocerlin #14;
     |FINSI;

     |SI nHayPre ! 0; |ACABA; |FINSI;     ||Lo encontro
     |SI #20#60 = "N"; |ACABA; |FINSI;   ||Cert distinto Exp
     |SI #0#19 ! "N"; |Y #0#19 ! "E"; |ACABA; |FINSI;
     nMax = #20#59 + #20#54;
     nNumPre = 0;
     |SI #gomantl1#4 ] #20#58; |Y #gomantl1#4 [ #20#59; || El Real
          nNumPre = #gomantl1#4 + #20#54;
     |FINSI;
     |SI #gomantl1#4 > #20#59; |Y #gomantl1#4 [ nMax; || Copia
          nNumPre = #gomantl1#4 - #20#54;
     |FINSI;
     |SI nNumPre = 0; |ACABA; |FINSI;

     #gocercab#0 = #gomantl1#3;
     #gocercab#1 = nNumPre;
     |LEE 000#gocercab.=;
     |SI FSalida = 0;
          |BUCLELIN 2 gocerlin #14;
     |FINSI;
|FPRC;

|PROCESO gopartia;
     ||enEje = enEje + (#15#23 * #15#10);
     enEje = enEje + #15#14;
|FPRC;

|DEFBUCLE gopartia;
     #15#3;
     ;
     #10#0, #10#1, #11#3, #11#4, "                    ", "                    ";
     #10#0, #10#1, #11#3, #11#4, "zzzzzzzzzzzzzzzzzzzz", "zzzzzzzzzzzzzzzzzzzz";
     ;
     NULL, gopartia;
|FIN;

|PROCESO FechaFase;
     |SI enFecIni > #2#11;
          |SI #2#11 ! "01.01.0000"; |Y #2#11 ! "00.00.0000";
               enFecIni = #2#11;
          |FINSI;
     |FINSI;
     |SI enFecFin < #2#12;
          enFecFin = #2#12;
     |FINSI;
|FPRC;

|DEFBUCLE FechaFase;
     #2#1;
     ;
     #11#3, #11#4, 001;
     #11#3, #11#4, 999;
     ;
     NULL, FechaFase;
|FIN;

|PROGRAMA;
     |ABRE #20; |LEE 000#20p; |CIERRA #20;
     |CLS;
     |ABRE #0;
     |DDEFECTO #0;
     |LEE 101#0p;
     nExiste = FSalida;
     |PINPA #0#0;
     |PINDA #0#0;
     |ENDATOS #1#0;
     |SI FSalida = 0;
          |SI nExiste = 0;
               |GRABA 020#0a;
          |SINO;
               |GRABA 020#0n;
          |FINSI;
          |HAZ Principal;
     |FINSI;
     |LIBERA #0;
     |CIERRA #0;
|FPRO;

|PROCESO gopre003;
     enImpCert = enImpCert + #gopre003#13;
|FPRC;

|DEFBUCLE gopre003;
 #gopre003#1;
 ;
 #gomantl1#0, #gomantl1#1, "                    ",   1;
 #gomantl1#0, #gomantl1#1, "zzzzzzzzzzzzzzzzzzzz", 999;
 ;
 NULL, gopre003;
|FIN;


|PROCESO gomantl1;
     |SI #gomantl1#3 ] #gocoy001#5; |Y #gomantl1#3 [ #gocoy001#6;
          |SI #gomantl1#4 ] #gocoy001#7; |Y #gomantl1#4 [ #gocoy001#8;

               |DDEFECTO #gocodpos;
               #gocodpos#0 = #gomantho#4;
               |LEE 000#gocodpos.=;
               enImpCert = 0;
               |BUCLE gopre003;
               |DDEFECTO #gopre001;
               #gopre001#0 = #gomantl1#3;
               #gopre001#1 = #gomantl1#4;
               |LEE 000#gopre001.=;

               |HAZ ImporCerti;

               enDif  = (#gomantl1#9 - #gocercab#10);
               |SI #gocercab#10 > #gomantl1#9;
                    enDif = 0;
               |FINSI;

               enFecIni = "31.12.9999";
               enFecFin = "01.01.0000";

               |BUCLE FechaFase;
               enEje = 0;
               |BUCLE gopartia;

               |DDEFECTO #1;
               |ABRE #1;
               #1#0 = #11#3;
               #1#1 = #11#4;
               |LEE 000#1=;
               |CIERRA #1;

               fFecha = ~;
               enDifCom = fFecha - #11#8;
               enDifPrg = fFecha - #1#7;

               |PRINT;
               nSwPie = 1;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO gomantho;
     nSw0 = 0;
     |SI #0#15 = "S"; |Y #10#9 = "C"; nSw0 = 1; |FINSI;
     |SI #0#16 = "S"; |Y #10#9 = "P"; nSw0 = 1; |FINSI;
     |SI #0#17 = "S"; |Y #10#9 = "T"; nSw0 = 1; |FINSI;
     |SI #0#18 = "S"; |Y #10#9 = "E"; nSw0 = 1; |FINSI;
     |SI nSw0 = 0;
          |ERROR;
     |FINSI;

     enCer = 0;
|FPRC;

|DEFBUCLE gomantho;
 #gomantho#1;
 2,4,30,37;
 #gocoy001#1, #gocoy001#3, #gocoy001#9 , #gocoy001#11, #gocoy001#13,#0#20;
 #gocoy001#2, #gocoy001#4, #gocoy001#10, #gocoy001#12, #gocoy001#14,#0#21;
 ;
 NULL, gomantho, gomantl1;
|FIN;

|PROCESO Principal;
     |IMPRE 0;
     |SI FSalida = 0;
          |SI #gocoy001#19 = "N"; aInforme = "gocoy001"; |FINSI;
          |SI #gocoy001#19 = "F"; aInforme = "gocoy101"; |FINSI;
          |SI #gocoy001#19 = "E"; aInforme = "gocoy201"; |FINSI;
          |SI #gocoy001#19 = "C"; aInforme = "gocoy301"; |FINSI;
          |INFOR aInforme;
          |SI FSalida = 0;
               |ABRE #gocodpos;
               |ABRE #gopre001;
               |ABRE #gocercab;
               |BUCLE gomantho;
               |CIERRA #gocodpos;
               |CIERRA #gopre001;
               |CIERRA #gocercab;
               |SI nSwPie = 1;
                    |PIEINF;
               |FINSI;
               |FININF;
          |SINO;
               aMensaje = "0000Informe " + aInforme + " inaccesible.";
               |MENAV aMensaje;
          |FINSI;
          |FINIMP;
     |SINO;
          |MENAV "0000Proceso cancelado.";
     |FINSI;
|FPRC;

|PROCESO Tipo13; |TIPO 13;
     |ABRE #20; |LEE 000#20p; |CIERRA #20;
     |SI #20#141 = "O";
          |PINTA 1104, "Serie  O.E.      ";
          |PINTA 1204, "Numero O.E.      ";
          |PINTA 1304, "Serie  O.T.      ";
          |PINTA 1404, "Numero O.T.      ";
          |PINTA 1504, "F.Alta O.T.      ";
     |FINSI;
|FPRC;
