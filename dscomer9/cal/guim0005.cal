|FICHEROS;
     guim0005 #0;
     guim0006 #1;
     dsm00003 #3,MANTE;
     dsm00020 #20; || Agencia
     dsm00003 #30; || Dir Ent
     agifa024 #24,MANTE;
     agifa027 #27;
     agifa025 #25;
     agifa019 #19,MANTE;
     agifa024 #24;
     agifa040 #40;
     dsm00053 #53;
     agifa073 #73;
     agifa091 #91;
     infom000 #100;
     agifm001 #101;
     agifm002 #102;
     agifa104 #104;
     agifa142 #142;
     agifa321 #321;
     agifa324 #324;
     guim0003 #300;
     guim0004 #400;
     infom003@;
     infom004@;
     dscam038@;
     dscam023@;
     dscam024@;
|FIN;

|VARIABLES;
     nLineasPed = 0;
     nLineasGes = 0;
     aWget = "";
     aUrl = "";
     &enForma = 0;
     aAlfa = "";
     nLin = 0;
     nUltLin = 0;
     fmes = "";
     mes = 0;
     nImpo = 0;
     aArt = "";
     aDes = "";
     nUni = 0;
     nPre = 0;
     {99}Valor = "";
     nCampo = 0;
     aLon = "";
     aDato = "";
     aReg = "";
     aTab = "";
     i = 0;
     nPos = 0;
     aLetra = "";
     nNum = 0;
     nCod = 0;
     aCod = "";
     aCP = "";
     aCta = "";
     nError = 0;
     aPath = "";
     nEmp = 0;
     nHay = 0;
     aCadena = "";
     nLetra = 0;
     aOrg = "";
     nSwCuenta = 0;
|FIN;

|PROCESO CuentaLinPed;
     |SI nSwCuenta = 0;
          nUltLin = nUltLin + 1;
     |SINO;
          |SI #400#1 = nUltLin;
               aAlfa = #400#9; |QBF aAlfa;
               |SI aAlfa = "PAGADOS"; |ACABA; |FINSI;

               nLineasPed = nLineasPed + 1;
               |ACABA;
          |FINSI;

          nLineasPed = nLineasPed + 1;

          |SI #400#8 ! 0;
               nLineasPed = nLineasPed + 1;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO CuentaLinGes;
     nLineasGes = nLineasGes + 1;
|FPRC;

|DEFBUCLE CuentaLinGes;
     #73#1;
     ;
     "     ", #0#0, 001;
     "     ", #0#0, 999;
     ;
     NULL, CuentaLinGes;
|FIN;

|DEFBUCLE CuentaLinPed;
     #400#1;
     ;
     #0#0, 001;
     #0#0, 999;
     ;
     NULL, CuentaLinPed;
|FIN;

|PROCESO QuitaRaros;
     aCadena = "";
     |QBT aAlfa;
     aLon = aAlfa % A0;
     |PARA i = 1; |SI i [ aLon; |HACIENDO i = i + 1;
          nPos = i * 100 + 1;
          aLetra = aAlfa % nPos;
          nLetra = &aLetra;
          |SI nLetra < 48; |O nLetra > 122; aLetra = ""; |FINSI;
          |SI nLetra ] 58; |Y nLetra [ 64; aLetra = ""; |FINSI;
          |SI nLetra ] 91; |Y nLetra [ 96; aLetra = ""; |FINSI;
          aCadena = aCadena + aLetra;
     |SIGUE;
     aAlfa = aCadena;
|FPRC;

|PROCESO Dato;
     nCampo = 1;
     IValor = Valor1<-;
     aLon = aDato % A0;
     aReg = "";
     aTab = "#";
     |PARA i = 1; |SI i [ aLon; |HACIENDO i = i + 1;
          nPos = i * 100 + 1;
          aLetra = aDato % nPos;
          |SI aLetra = aTab;
               Valor = aReg;
               aReg = "";
               nCampo = nCampo + 1;
               IValor = IValor + 1;
          |SINO;
               aReg = aReg + aLetra;
          |FINSI;
          |SI nCampo > 90; |SAL; |FINSI;     || Por si.... :)
     |SIGUE;
     Valor = aReg;
|FPRC;

|PROCESO NueDirEntre;
     aAlfa = #0#5;
     aAlfa = aAlfa - "#";
     |SI FSalida ! 0;
          aDato = "#########"; |HAZ Dato;
          aDato = #0#5; |QBF aAlfa; |HAZ Dato;
          #104#19 = Valor1; ||Nom
          #104#14 = Valor2; ||Dir
          #104#59 = Valor3; ||CP
          #104#20 = Valor4; ||Pobla
          #104#21 = Valor5; ||Prv

          #104#15 = Valor6;   || Per.Contacto
          #104#51 = Valor8;   || Movil
          aAlfa = Valor8; |QBF aAlfa;
          |SI aAlfa = "";
               #104#51 = Valor7; ||Tlf
          |FINSI;
     |SINO;
          |DDEFECTO #30;
          |ABRE #30;
          #30#0 = #104#4;
          #30#1 = #0#8;
          |LEE 000#30=;
          |CIERRA #30;
          #104#57 = #30#1;
          #104#19 = #30#10;
          #104#14 = #30#2;
          #104#20 = #30#3;
          #104#21 = #30#7;
          #104#59 = #30#6;
          |SI #30#14 = "  ";
              #104#7 = #24#25;
          |SINO;
              #104#7 = #30#14;
          |FINSI;
          aAlfa = #0#5; |QBT aAlfa; aAlfa = aAlfa > "";
          |SI aAlfa = "RECOGERENAGENCIA";
               #104#14 = #0#5;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO ActCli;
     #19#0 = #73#2;
     |LEE 020#19=;

     #24#0 = #104#4;
     |LEE 101#24=;
     |SI FSalida = 0;
          |SI #19#194 = 2;
               nImpo = ((((#73#44 * #73#6) < #73#8) < #73#29) < #104#5) < #104#17;
          |SINO;
               nImpo = ((((#73#5 * #73#6) < #73#8) < #73#29) < #104#5) < #104#17;
          |FINSI;
          |SI #142#115 = "S";
               nImpo = nImpo < #104#6;
          |FINSI;
          fmes = #104#1 % A402;
          mes = fmes - 1;
          mes = mes * 3;
          mes = mes + 114;
          #24mes = #24mes   + nImpo;     || VENTA.
          #24#150 = #24#150 + nImpo;
          |GRABA 020#24a;
     |FINSI;
|FPRC;

|PROCESO CreaLinPed0;
     |DDEFECTO #19;
     |ABRE #19;
     #19#0 = aArt;
     |LEE 000#19=;
     |CIERRA #19;
     |SI aDes = ""; aDes = #19#1; |FINSI;

     nLin = nLin + 1;
     |DDEFECTO #73;
     #73#28 = #104#25;
     #73#0 = #104#0;
     #73#1 = nLin;
     #73#2 = #19#0;
     #73#3 = 1;     ||Almacen
     #73#4 = aDes;
     #73#5 = nUni;
     #73#42 = nUni;
     #73#6 = nPre;
     #73#13 = #104#2;
     |SI #24#28 = "S";
          #73#14 = 0;
     |SINO;
          #73#14 = #19#30;
     |FINSI;
     #73#16 = #104#7;
     #73#18 = #19#2;
     #73#19 = #104#18;
     #73#20 = #104#4;
     #73#23 = #104#5;
     #73#24 = #104#6;
     #73#25 = #104#17;
     #73#26 = #104#9;
     #73#38 = nPre;
     #73#45 = #1#8;
     |GRABA 020#73n;
     |SI FSalida ! 0;
          aAlfa = "0000Pedido Existente: " + #73#28 + "/" + #73#0 + "/" + #73#1;;
          |MENAV aAlfa;
     |SINO;
          enForma = 1;
          |HAZ AcumulaPV;

          #53#0 = "P";
          #53#1 = #73#28;
          #53#2 = #73#0;
          #53#3 = #73#1;
          #53#4 = 1;
          #53#5 = #1#6;
          |GRABA 040#53n;
     |FINSI;
|FPRC;

|PROCESO CreaLinPed;
     aArt = #1#2;
     aDes = #1#3;
     nUni = #1#4;
     nPre = #1#5;
     |HAZ CreaLinPed0;
|FPRC;

|PROCESO CreaCabPed;
     nLin = 0;
     |ABRE #25;
     #25#0 = #0#4;
     |LEE 000#25=;
     |CIERRA #25;

     |ABRE #20;
     #20#0 = #0#6;
     |LEE 000#20=;
     |CIERRA #20;

     |DDEFECTO #24;
     #24#0 = #0#2;
     |LEE 000#24=;

     |DDEFECTO #104;
     #104#25 = "";
     #104#0 = #0#0;
     #104#1 = #0#1;
     #104#2 = #0#1;
     #104#4 = #24#0;
     #104#5 = #24#37;
     #104#6 = #24#38;
     #104#7 = #25#0;
     #104#8 = #24#2;
     |SI #0#9 = 0;
          #104#9 = #24#20;
     |SINO;
          #104#9 = #100#20;
     |FINSI;
     #104#14 = #0#5;
     #104#16 = #25#7;
     #104#17 = #24#156;
     #104#18 = #24#4;
     #104#19 = #24#2;
     #104#20 = #24#6;
     #104#21 = #24#7;
     #104#22 = #24#47;
     #104#23 = #24#41;
     #104#35 = #24#192;     || Divisa

     #324#0 = #104#35;
     |LEE 000#324=;

     #104#36 = #324#2;     || Cambio
     #104#37 = #24#193;    || Moneda
     #104#40 = #321#9;     || Base

     #324#0 = #104#40;
     |LEE 000#324=;

     #104#41 = #324#2;     || Cambio
     #104#60 = #0#6;
     #104#61 = #24#208;
     #104#64 = #24#15;

     |HAZ NueDirEntre;

     |GRABA 000#104b;
     |SI FSalida ! 0;
          aAlfa = "0000Pedido Existente: " + #104#25 + "/" + (("000000"+#104#0)%-106);
          |MENAV aAlfa;
     |FINSI;
|FPRC;

|PROCESO Ti20; |TIPO 20;
     |SI FSalida = "OPCION";
          FSalida = "Crear";
     |SINO;
          |LEE 000#0=;
          |SI FSalida ! 0; |ACABA; |FINSI;

          |SI #0#2 = "000000"; |O #0#2 = "      ";
               |CONFI "0000NCrear cliente en dscomer?";
               |SI FSalida ! 0; |ACABA; |FINSI;
               |HAZ CreaCli;
               |SI nError = 0;
                    |LEE 121#0=;
                    |SI FSalida = 0;
                         #0#2 = #24#0; |PINTA #0#2;
                         |GRABA 020#0a;
                    |FINSI;
                    |LIBERA #0;
               |SINO;
                    |ACABA;
               |FINSI;
          |FINSI;

          nError = 0;
          |BUCLELIN 2 CheqArt #0;
          |SI nError ! 0; |ACABA; |FINSI;

          |ABRE #142; |LEE 000#142p; |CIERRA #142;
          |ABRE #321; |LEE 000#321p; |CIERRA #321;
          |ABRE #100; |LEE 000#100p; |CIERRA #100;

          |ABRE #24;
          |ABRE #53;
          |ABRE #104;
          |ABRE #73;
          |HAZ CreaCabPed;
          |SI FSalida = 0;
               |BUCLELIN 2CreaLinPed#0;
               |HAZ CalCabAgifa104;
               |ABRE #19;
               |BUCLELIN 2ActCli#104;
               |CIERRA #19;

               |ABRE #300;
               #300#0 = #0#0;
               |LEE 101#300=;
               |SI FSalida = 0;
                    nUltLin = 0;
                    nSwCuenta = 0;
                    |BUCLE CuentaLinPed;
                    nSwCuenta = 1;
                    |BUCLE CuentaLinPed;
                    |BUCLE CuentaLinGes;
                    |SI nLineasPed = nLineasGes;
                         #300#13 = "G";
                    |SINO;
                         #300#13 = "N";
                    |FINSI;
                    |GRABA 020#300a;
               |FINSI;
               |CIERRA #300;

               |BUCLELIN 1NULL#0;
               |BORRA 020#0a;
               |MENAV "0000Pedido Creado...";
          |FINSI;
          |CIERRA #104;
          |CIERRA #73;
          |CIERRA #53;
          |CIERRA #24;
     |FINSI;
|FPRC;

|PROCESO Cliente0; |TIPO 0;
     |SI FSalida = 13;
          |SI #0#2 = "000000"; |O #0#2 = "      ";
               |HAZ CreaCli;
               |SI nError = 0;
                    #0#2 = #24#0; |PINTA #0#2;
               |FINSI;
          |SINO;
               |MENAV "0000El cliente debe se '000000'";
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO BuscaAge;
     |HAZ QuitaRaros; aOrg = aAlfa;
     nCod = 0;
     |ABRE #20;
     |LEE 000#20p;
     |PARA; |SI FSalida = 0; |HACIENDO;
          aAlfa = #20#1; |HAZ QuitaRaros; aDes = aAlfa;
          |SI aDes = aOrg;
               nCod = #20#0; |SAL;
          |FINSI;
          |LEE 000#20s;
     |SIGUE;
     |CIERRA #20;
|FPRC;

|PROCESO BuscaRepre;
     |HAZ QuitaRaros; aOrg = aAlfa;
     aCod = "";
     |ABRE #25;
     |LEE 000#25p;
     |PARA; |SI FSalida = 0; |HACIENDO;
          aAlfa = #25#1; |HAZ QuitaRaros; aDes = aAlfa;
          |SI aDes = aOrg;
               aCod = #25#0; |SAL;
          |FINSI;
          |LEE 000#25s;
     |SIGUE;
     |CIERRA #25;
     |DDEFECTO #25;
|FPRC;

|PROCESO BuscaFP;
     |HAZ QuitaRaros; aOrg = aAlfa;
     aCod = "";
     |ABRE #91;
     |LEE 000#91p;
     |PARA; |SI FSalida = 0; |HACIENDO;
          aAlfa = #91#1; |HAZ QuitaRaros; aDes = aAlfa;
          |SI aDes = aOrg;
               aCod = #91#0; |SAL;
          |FINSI;
          |LEE 000#91s;
     |SIGUE;
     |CIERRA #91;
     |DDEFECTO #91;
|FPRC;

|PROCESO DirEnt;
     |DDEFECTO #30;
     |ABRE #30;
     #30#0 = #infom003@#2;
     #30#1 = #infom004@#2;
     |LEE 101#30=;
     |SI FSalida ! 0; |GRABA 020#3b; |FINSI;
     #30#10 = #infom004@#3;
     #30#2 = #infom004@#4;

     aCP = #infom004@#5; |QBT aCP;
     aCP = ("00000" + aCP) % -105;
     aAlfa = aCP % 102; #30#4 = aAlfa;
     aAlfa = aCP % 303; #30#5 = aAlfa;
     #30#6 =  aCP;
     #30#23 = aCP;

     #30#3 = #infom004@#6;

     |DDEFECTO #40;
     |ABRE #40;
     #40#0 = #30#4;
     |LEE 000#40=;
     |CIERRA #40;
     #30#7 = #40#1;

     #30#13 = #infom004@#8;
     #30#21 = #infom004@#9;
     #30#20 = #infom004@#10;
     |GRABA 020#30a;
     |CIERRA #30;
|FPRC;

|DEFBUCLE ActDirEnt;
     #infom004@#2001;
     ;
     nCod;
     nCod;
     ;
     NULL, DirEnt;
|FIN;

|PROCESO CreaCli;
     nError = 1;
     |DDEF aPath;
     aAlfa = aPath + "infom003";
     |CARGA_DEF aAlfa, infom003@;
     |SI FSalida = 0;
          aAlfa = aPath + "infom004";
          |CARGA_DEF aAlfa, infom004@;
          |SI FSalida = 0;
               |ABRE #infom003@;
               #infom003@#0 = #0#7;
               |LEE 000#infom003@.=;
               |SI FSalida = 0;
                    |PUSHV 0501 2380;
                    |ABRE #27;
                    |ABRE #24;
                    |PARA FSalida = 1; |SI FSalida ! 0; |HACIENDO;
                         #27#0 = "clientes";
                         |LEE 101#27=;
                         |SI FSalida = 0;
                              #27#1 = #27#1 + 1;
                              nNum = #27#1;
                              |GRABA 021#27a;
                         |SINO;
                              #27#1 = 1;
                              nNum = #27#1;
                              |GRABA 021#27n;
                         |FINSI;
                         |DDEFECTO #24;
                         aAlfa = ("000000" + nNum) % -106;
                         #24#0 = aAlfa;
                         |GRABA 000#24b;
                    |SIGUE;

                    || Esto mismo esta en el guiz0002
                    |ABRE #142; |LEE 000#142p; |CIERRA #142;
                    #24#3 = #infom003@#1;
                    #24#15 = #infom003@#3;
                    #24#1 = #infom003@#6;
                    #24#2 = #infom003@#7;
                    #24#36 = #infom003@#8;
                    #24#8 = #infom003@#10;
                    #24#9 = #infom003@#11;

                    aCP = #infom003@#12; |QBT aCP;
                    aCP = ("00000" + aCP) % -105;
                    aAlfa = aCP % 102; #24#181 = aAlfa;
                    aAlfa = aCP % 303; #24#182 = aAlfa;
                    #24#183 = aCP;
                    #24#211 = aCP;

                    |DDEFECTO #40;
                    |ABRE #40;
                    #40#0 = #24#181;
                    |LEE 000#40=;
                    |CIERRA #40;
                    #24#10 = #40#1;

                    #24#5 = #24#8;
                    #24#6 = #24#9;
                    #24#178 = #24#181;
                    #24#179 = #24#182;
                    #24#180 = #24#183;
                    #24#7 = #24#10;

                    #24#11 = #24#8;
                    #24#12 = #24#9;
                    #24#184 = #24#181;
                    #24#185 = #24#182;
                    #24#186 = #24#183;
                    #24#13 = #24#10;

                    aCta = #infom003@#13;
                    aAlfa = aCta % 104; #24#32 = aAlfa;
                    aAlfa = aCta % 604; #24#33 = aAlfa;
                    aAlfa = aCta % 1102; #24#31 = aAlfa;
                    aAlfa = aCta % 1410; #24#30 = aAlfa;

                    aAlfa = #infom003@#14;
                    |HAZ BuscaFP;
                    #24#20 = aCod;

                    aAlfa = #infom003@#16;
                    |HAZ BuscaAge;
                    #24#35 = aAlfa;
                    #24#202 = nCod;

                    |SI #infom003@#19 ! 0; #24#17 = #infom003@#19; |FINSI;
                    |SI #infom003@#20 ! 0; #24#18 = #infom003@#20; |FINSI;
                    #24#197 = #infom003@#21;
                    #24#53 = #infom003@#22;
                    #24#159 = #infom003@#53;

                    nCod = #24#0;
                    aAlfa = "000000000000" + nCod;
                    |QBT aAlfa;
                    nPos = -(100 + (#142#11 - 2));
                    aAlfa = aAlfa % nPos;
                    aAlfa = "43" + aAlfa;
                    #24#16 = aAlfa;

                    #24#40 = "S";  ||Valorado
                    #24#44 = 1;    ||Copias

                    aAlfa = #infom003@#17;
                    |HAZ BuscaRepre;
                    #24#25 = aCod; ||Repre1

                    aAlfa = #infom003@#18;
                    |HAZ BuscaRepre;
                    #24#26 = aCod; ||Repre2

                    #24#47 = #infom003@#62; ||R.Equiv.

                    #24#8 = #infom003@#63;
                    #24#9 = #infom003@#64;
                    aCP = #infom003@#65; |QBT aCP;
                    aCP = ("00000" + aCP) % -105;
                    aAlfa = aCP % 102; #24#181 = aAlfa;
                    aAlfa = aCP % 303; #24#182 = aAlfa;
                    #24#183 =  aCP;
                    #24#211 = aCP;
                    |DDEFECTO #40;
                    |ABRE #40;
                    #40#0 = #24#181;
                    |LEE 000#40=;
                    |CIERRA #40;
                    #24#10 = #40#1;

                    #24#208 = "P";

                    #24#22 = #infom003@#68;
                    #24#23 = #infom003@#69;
                    #24#24 = #infom003@#70;
               ||     aAlfa = ("00" + #infom003@#37) % -102;
                    #24#158 = #infom003@#37
                    |GRABA 020#24a;
                    |PINPA #0#24;
                    |PINDA #0#24;
                    |ENDATOS #2#24;
                    nError = FSalida;
                    |SI FSalida = 0;
                         |GRABA 020#24a;
                         #infom003@#2 = #24#0;
                         |GRABA 020#infom003@.a;
                         nCod = #infom003@#0;
                         |BUCLE ActDirEnt;
                         |HAZ Crea1Dir;
                         |HAZ ActUrl;
                         |HAZ CreaCliCarte;
                    |SINO;
                         |BORRA 020#24c;
                    |FINSI;
                    |CIERRA #24;
                    |CIERRA #27;
                    |POPV;
               |SINO;
                    aAlfa = ("0000000000000000000" + #0#7) % -119;
                    aAlfa = "0000No existe Cliente idCli:" + aAlfa;
                    |MENAV aAlfa;
               |FINSI;
               |CIERRA #infom003@;
               |DESCARGA_DEF #infom004@;
          |SINO;
               |MENAV "0000Error al cargar 'infom004'";
          |FINSI;
          |DESCARGA_DEF #infom003@;
     |SINO;
          |MENAV "0000Error al cargar 'infom003'";
     |FINSI;
|FPRC;

|PROCESO Crea1Dir;
     |ABRE #101;
     #101#0 = #24#3;
     |LEE 000#101=; || Zona
     |CIERRA #101;
     |ABRE #102;
     #102#0 = #24#189;
     |LEE 000#102=; ||Ruta
     |CIERRA #102;
     |ABRE #25;
     #25#0 = #24#25;
     |LEE 000#25=;
     |CIERRA #25;

     |ABRE #3;
     |DDEFECTO #3;
     #3#0 = #24#0;
     #3#1 = 1;
     |LEE 101#3=;
     |SI FSalida ! 0; |GRABA 021#3b; |FINSI;
     #3#2 = #24#5;
     #3#3 = #24#6;
     #3#4 = #24#178;
     #3#5 = #24#179;
     #3#6 = #24#180;
     #3#7 = #24#7;
     #3#8 = #24#14;
     #3#10 = #24#2;
     #3#11 = #24#3;
     #3#12 = #101#1;
     #3#13 = #24#17;
     #3#14 = #24#25;
     #3#15 = #25#1;
     #3#16 = #24#189;
     #3#17 = #102#1;
     #3#19 = #24#205;
     #3#20 = #24#196;
     #3#21 = #24#18;
     #3#22 = #24#36;
     #3#23 = #24#211;
     #3#24 = #24#197;
     |GRABA 021#3a;
     |CIERRA #3;
|FPRC;

|PROCESO dscam024;
     nHay = 1;
     #dscam024@#9 = #dscam023@#19 - #dscam023@#20;
     |GRABA 020#dscam024@.a;
     |LIBERA #dscam024@;
|FPRC;

|DEFBUCLE dscam024;
     #dscam024@#1002;
     ;
     #dscam023@#0, 01;
     #dscam023@#0, 99;
     be;
     NULL, dscam024;
|FIN;

|PROCESO dscam038;
     |DBASS aAlfa;
     aAlfa = aAlfa + "dscarter/fich/" + (("00000" + #dscam038@#5) % -105) + "/";
     |PATH_DAT #dscam023@#aAlfa#;
     |PATH_DAT #dscam024@#aAlfa#;
     |DDEFECTO #dscam023@;
     |ABRE #dscam023@;
     #dscam023@#0 = #24#0;
     |LEE 101#dscam023@.=;
     |SI FSalida ! 0; |GRABA 020#dscam023@.b; |FINSI;
     #dscam023@#1 = #24#1;
     #dscam023@#16 = ~;
     #dscam023@#15 = #infom003@#50;
     #dscam023@#19 = #dscam023@#13 + #dscam023@#15 + #dscam023@#17;
     |GRABA 020#dscam023@.a;
     |CIERRA #dscam023@;
     nHay = 0;
     |BUCLE dscam024;
     |SI nHay = 0;
          |DDEFECTO #dscam024@;
          |ABRE #dscam024@;
          #dscam024@#0 = #dscam023@#0;
          #dscam024@#1 = 1;
          |GRABA 020#dscam024@.n;
          |CIERRA #dscam024@;
          |BUCLE dscam024;
     |FINSI;
|FPRC;

|DEFBUCLE dscam038;
     #dscam038@#2004;
     ;
     00001, nEmp, " ", 001;
     99999, nEmp, "z", 999;
     ;
     NULL, dscam038;
|FIN;

|PROCESO CreaCliCarte;
     |DBASS aPath;
     aAlfa = aPath + "dscarter/def/" + "dscam038";
     |CARGA_DEF aAlfa, dscam038@;
     |SI FSalida ! 0; |ACABA; |FINSI;
     aAlfa = aPath + "dscarter/def/" + "dscam023";
     |CARGA_DEF aAlfa, dscam023@;
     |SI FSalida ! 0; |ACABA; |FINSI;
     aAlfa = aPath + "dscarter/def/" + "dscam024";
     |CARGA_DEF aAlfa, dscam024@;
     |SI FSalida ! 0; |ACABA; |FINSI;
     aAlfa = aPath + "dscarter/fich/";
     |PATH_DAT #dscam038@#aAlfa#;

     |DFICE aAlfa; aAlfa = aAlfa % -205; nEmp = aAlfa;
     |BUCLE dscam038;

     |DESCARGA_DEF #dscam024@;
     |DESCARGA_DEF #dscam023@;
     |DESCARGA_DEF #dscam038@;
|FPRC;

|PROCESO CheqArt; |TIPO 6;
     |PUSHV 0501 2380;

     |DDEFECTO #19;
     |ABRE #19;
     #19#0 = #1#2;
     |LEE 000#19=;
     |SI FSalida ! 0;
          #19#1 = #1#3;
          |GRABA 020#19b;
          |PINPA #0#19;
          |PINDA #0#19;
          |CONFI "0000SCrear articulo S/N..:";
          |SI FSalida = 0;
               |ENDATOS #1#19;
               |SI FSalida = 0;
                    |GRABA 020#19a;
               |SINO;
                    |BORRA 020#19c;
                    |ERROR10;
                    nError = 1;
               |FINSI;
          |SINO;
               |BORRA 020#19c;
               |ERROR10;
                nError = 1;
          |FINSI;
     |FINSI;
     |CIERRA #19;

     |POPV;
|FPRC;

|PROCESO ActUrl;
     |ABRE #100; |LEE 000#100p; |CIERRA #100;

     aWget = #100#17; |QBF aWget;
     aUrl = #100#19; |QBF aUrl;

     aAlfa = aWget + "wget -o /tmp/001980.out " + &34 + aUrl + "?idcliente=" + #infom003@#0;
     |QBF aAlfa;
     aAlfa = aAlfa + "&codcliente=" + #infom003@#2 + &34;
     aAlfa = aAlfa + " >> /tmp/001980.log 2>> /tmp/001980.err";
     |SYSTEM aAlfa;
|FPRC;
