|FICHEROS;
     ibery014 #0;
     temp0023 #1;   ||Tmp
     iberw010 #10;  ||Tmp Dias
     agifa501 #501;
     agifa502 #502;
     agifa510 #510;
|FIN;

|VARIABLES;
     nBase     = 0;
     &Tempo    = "";
     aTempo1   = "";
     aTempo10  = "";
     aInforme1 = "ibery014";
     aInforme2 = "ibery114";
     aInforme  = "";
     aAlfa     = "";
     nHora     = 0;
     nHay      = 0;
     nPeriodo  = 0;
     i         = 0;
     j         = 0;
     k         = 0;
     aCaja     = "";
|FIN;

|PROCESO Parrilla; |TIPO 0;
     |SI #0#70 = "L";
          |C_M #0#2, 1, "S";
          |C_M #0#3, 1, "S";
     |SINO;
          |C_M #0#2, 1, "N";
          |C_M #0#3, 1, "N";
          |ENTLINEAL #1#10, -1, 2, 20, 0, Min, Max;
          |ABRE #10;
          |LEE 000#10p;
          #0#2 = #10#0;
          |LEE 000#10u;
          #0#3 = #10#0;
          |CIERRA #10;
          |PINTA #0#2;
          |PINTA #0#3;
     |FINSI;
|FPRC;

|PROCESO &PonCero;
     |PARA i = 4; |SI i [ 34; |HACIENDO i = i + 1;
          #0i = 0;
     |SIGUE;
|FPRC;

|PROCESO Temporal;
     aAlfa = #1#1 % 102;
     #1#35 = aAlfa;

     #1#32 = #1#2 + #1#3 + #1#4 + #1#5 + #1#6 + #1#7 + #1#8 + #1#9 + #1#10 + #1#11;
     #1#33 = #1#12 + #1#13 + #1#14 + #1#15 + #1#16 + #1#17 + #1#18 + #1#19 + #1#20 + #1#21;
     #1#34 = #1#22 + #1#23 + #1#24 + #1#25 + #1#26 + #1#27 + #1#28 + #1#29 + #1#30 + #1#31;
     nHay = 1;
     |PRINT;

     |PARA i = 2; |SI i [ 31; |HACIENDO i = i + 1;
          j = i + 2;
          #0j = #0j + #1i;
          k = i + 33;
          #0k = #0k + #1i;
     |SIGUE;
|FPRC;

|DEFBUCLE Temporal;
     #1#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, Temporal;
|FIN;

|PROCESO agifa501;
     |SI #501#31 = "R"; |Y #0#66 = "N"; |ERROR; |ACABA; |FINSI;
     |SI #501#31 = "A"; |Y #0#67 = "N"; |ERROR; |ACABA; |FINSI;
     |SI #501#31 = "L"; |Y #0#68 = "N"; |ERROR; |ACABA; |FINSI;

     |SI #0#70 = "P";
          #10#0 = #501#14;
          |LEE 000#10=;
          |SI FSalida ! 0; |ACABA; |FINSI;
     |FINSI;

     aAlfa = (#501#34 % 102) + (#501#34 % 402);
     nHora = aAlfa;

     nPeriodo = 0;
     |PARA i = 1; |SI i [ 19; |HACIENDO i = i + 2;
          j = i + 1;
          nPeriodo = nPeriodo + 1;
          |SI #510#i# > #510#j#;
               |SI nHora ] #510#i#; |Y nHora [ 2400;
                    |SAL;
               |FINSI;
               |SI nHora ] 0; |Y nHora [ #510#j#;
                    |SAL;
               |FINSI;
          |SINO;
               |SI nHora ] #510#i#; |Y nHora [ #510#j#;
                    |SAL;
               |FINSI;
          |FINSI;
     |SIGUE;
     |SI nPeriodo > 10; nPeriodo = 10; |FINSI;

     aCaja = #501#12;
     |SI #0#69 = "S";
          aCaja = (aCaja % 103) + "00";
     |FINSI;

     |DDEFECTO #1;
     #1#0 = aCaja;
     #1#1 = #501#14;
     |LEE 101#1=;
     |SI FSalida ! 0; |GRABA 020#1b; |FINSI;

     nPeriodo = nPeriodo + 1;
     |SI #0#71 = "S";
          #1nPeriodo = #1nPeriodo + #501#9;    || Imp
     |SINO;
          nBase = #501#30 + #501#20 + #501#23 + #501#26;
          nBase = nBase + #501#48 + #501#51;
          #1nPeriodo = #1nPeriodo + nBase;    || Imp
     |FINSI;

     nPeriodo = nPeriodo + 10;
     #1nPeriodo = #1nPeriodo + 1;         || Tiquets

     nPeriodo = nPeriodo + 10;
     #1nPeriodo = #1nPeriodo + #501#55;   || Comen

     |GRABA 020#1a;
     |LIBERA #1;
|FPRC;

|DEFBUCLE agifa501;
     #501#3;
     12;
     #0#2, "      ", " ", "     ", 000000, #0#0;
     #0#3, "zzzzzz", "z", "zzzzz", 999999, #0#1;
     ;
     NULL, agifa501;
|FIN;

|PROCESO Tipo3; |TIPO 3;
     |SI #0#70 = "P";
          aInforme = aInforme2;
     |SINO;
          aInforme = aInforme1;
     |FINSI;

     |HAZ CreaTempo; aTempo1 = Tempo; |NOME_DAT #1 aTempo1; |DELINDEX #1;
     |ABRE #1;
     |IMPRE 0;
     |SI FSalida = 0;
          |INFOR aInforme;
          |SI FSalida = 0;
               |ABRE #510;
               |DDEFECTO #510;
               #510#0 = #0#65;
               |LEE 000#510=;
               |CIERRA #510;

               |ABRE #10;
               |BUCLE agifa501;
               |BUCLE Temporal;
               |CIERRA #10;
               |SI nHay ! 0; |PIEINF; |FINSI;
               |FININF;
          |SINO;
               aAlfa = "0000No existe: " + aInforme;
               |MENAV aAlfa;
          |FINSI;
          |FINIMP;
     |FINSI;
     |CIERRA #1;
     |DELINDEX #1; Tempo = aTempo1; |HAZ BoraTempo;
|FPRC;

|PROCESO Min;
     #10#0 = "01.01.2000";
|FPRC;

|PROCESO Max;
     #10#0 = "31.12.9999";
|FPRC;

|PROGRAMA;
     |HAZ CreaTempo; aTempo10 = Tempo; |NOME_DAT #10 aTempo10; |DELINDEX #10;

     |CLS;
     |PINPA #0#0;
     |PINDA #0#0;
     |ENTLINEAL #1#10, 1, 7, 20, 1, Min, Max;
     |ENDATOS #1#0;

     |DELINDEX #10; Tempo = aTempo10; |HAZ BoraTempo;
|FPRO;
