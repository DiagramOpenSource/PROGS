|FICHEROS;
     artex001 #0;
     dsm00003 #3;
     agifa019 #19;
     agifa024 #24;
     agifa025 #25;
     agifa073 #73;
     agifa104 #104;
     dsm00113 #113;
     agifa142 #142;
     agifa321 #321;
     agifa324 #324;
|FIN;

|VARIABLES;
     nContaPed = 0;
     &eaTag = "";
     &enTiva = 0;
     &efFiva = @;
     &enIva = 0;
     &enRec = 0;
     &enForma = 1;
     &enDescuento1 = 0;
     &enDescuento2 = 0;
     &eaTComision  = "";
     &eaCliente    = "";
     &eaRepre      = "";
     &eaArticulo   = "";
     &efFecha      = @;
     &eaSerie      = "";
     &enDirEnt = 0;
     &enAlmacen = 0;
     &enComision = 0;
     &enImpCliPed = 0;

     nLin = 1;
     nHandle = 0;
     nBoton = 0;
     aAlfa = "";
     aRuta = "";
     aDato = "";
     fPedido = "";
     fEntrega = "";
     aSuPed = "";
     aNumPed = "";
     aCodCli = "";
     aCodArt = "";
     nCanti = 0;
     aDesArt = "";
     aPrecio = "";
     aRefere = "";
     nError = 0;
     fmes = "";
     mes = 0;
     nInpo = 0;
     nForma = 1;
     nCalc = 0;
     &nImpo = 0;
     aFic = "";
     aPath = "";
     aDes = "";
     aExe = "";

     &eaAlfa = "";
     aCar = "";
     aLon = "";
     nCar = 0;
     nPos = 0;
     i = 0;
|FIN;

|PROCESO QuitaRarosImp;
     |QBF eaAlfa;
     aAlfa = eaAlfa;
     aLon = aAlfa % A0;
     aDato = "";
     |PARA i = 0; |SI i [ aLon; |HACIENDO i = i + 1;
          nPos = (i + 1) * 100 + 1;
          aCar = aAlfa % nPos;
          nCar = &aCar;
          |SI nCar = 241; aCar = "¤"; |FINSI;
          |SI nCar = 209; aCar = "¥"; |FINSI;
          |SI nCar = 208; aCar = "¥"; |FINSI; ||no se porque
          |SI nCar = 225; aCar = " "; |FINSI;
          |SI nCar = 233; aCar = "‚"; |FINSI;
          |SI nCar = 237; aCar = "¡"; |FINSI;
          |SI nCar = 243; aCar = "¢"; |FINSI;
          |SI nCar = 250; aCar = "£"; |FINSI;
          |SI nCar = 193; aCar = "A"; |FINSI;
          |SI nCar = 201; aCar = "E"; |FINSI;
          |SI nCar = 205; aCar = "I"; |FINSI;
          |SI nCar = 211; aCar = "O"; |FINSI;
          |SI nCar = 218; aCar = "U"; |FINSI;
          |SI nCar = 186; aCar = "§"; |FINSI;
          |SI nCar = 170; aCar = "¦"; |FINSI;
          |SI nCar = 176; aCar = "°"; |FINSI;     ||Euro
          aDato = aDato + aCar;
     |SIGUE;
     eaAlfa = aDato;
|FPRC;

|PROCESO CalcConIva;
     |ABRE #dsm00113;
     #dsm00113#0 = #agifa104#4;
     #dsm00113#1 = #agifa073#14;
     |LEE 000#dsm00113.=;
     |SI FSalida = 0;
        nCalc = #agifa073#11 % #dsm00113#3;
        |SI #agifa024#47 = "S";
           nCalc = nCalc + (#agifa073#11 % #dsm00113#4);
        |FINSI;
     |SINO;
        enTiva = #73#14;
        efFiva = #104#1;
        |HAZ SacaIVA;

        nCalc = #agifa073#11 % enIva;
        |SI #agifa024#47 = "S";
             nCalc = nCalc + (#agifa073#11 % enRec);
        |FINSI;
     |FINSI;
     |CIERRA #dsm00113;

     nImpo = nImpo + (#agifa073#11 + nCalc);
|FPRC;

|DEFBUCLE CalcConIva;
     #agifa073#1;
     ;
     #agifa104#25, #agifa104#0,001;
     #agifa104#25, #agifa104#0,999;
     ;
     NULL,CalcConIva;
|FIN;

|PROCESO ActCli;
     #19#0 = #73#2;
     |LEE 020#19=;

     #24#0 = #104#4;
     |LEE 121#24=;
     |SI FSalida = 0;
          |SI #19#194 = 2;
               nImpo = ((((#73#44 * #73#6) < #73#8) < #73#29) < #104#5) < #104#17;
          |SINO;
               nImpo = ((((#73#5 * #73#6) < #73#8) < #73#29) < #104#5) < #104#17;
          |FINSI;
          |SI #142#115 = "S";
               nImpo = nImpo < #104#6;
          |FINSI;
          nImpo = nImpo * nForma;
          fmes = #104#1 % A402;
          mes = fmes - 1;
          mes = mes * 3;
          mes = mes + 114;
          #24mes = #24mes   + nImpo;     || VENTA.
          #24#150 = #24#150 + nImpo;
          |GRABA 020#24a;

          eaCliente = #agifa104#4;
          efFecha = #agifa104#1;
          enImpCliPed = nImpo;
          |HAZ AcumulaCli;
     |FINSI;
|FPRC;

|PROCESO LinPed;
     #73#0 = #104#0;
     #73#1 = nLin;
     #73#2 = #19#0;
     #73#3 = 1;
     #73#4 = aDesArt;
     #73#5 = nCanti;
     #73#6 = aPrecio;

     enDescuento1 = #73#8;
     enDescuento2 = #73#29;
     eaTComision  = #73#17;
     eaCliente    = #104#4;
     eaRepre      = #104#7;
     eaArticulo   = #73#2;
     efFecha      = #104#1;
     eaSerie      = #73#28;
     enDirEnt = #104#57;
     enAlmacen = #73#3;
     |HAZ Comisiones;
     #73#10 = enComision;

     #73#14 = #19#30;
     #73#16 = #104#7;
     #73#17 = #104#16;
     #73#18 = #19#2;
     #73#19 = #104#18;
     #73#20 = #104#4;
     #73#23 = #104#5;
     #73#24 = #104#6;
     #73#25 = #104#17;
     #73#26 = #104#9;
     #73#28 = #104#25;
     #73#38 = aPrecio;
     #73#40 = aPrecio;
     #73#42 = #73#5;

     enForma = 1;
     |HAZ AcumulaPV;

     |GRABA 020#73n;
     nLin = nLin + #142#236;
|FPRC;

|PROCESO CabPed;
     #104#1 = fPedido;
     #104#2 = fEntrega;
     #104#3 = aSuPed;
     #104#4 = #24#0;
     #104#5 = #24#37;
     #104#6 = #24#38;
     #104#8 = #24#2;
     #104#9 = #24#20;
     #104#16 = #25#7;
     #104#17 = #24#156;
     #104#18 = #24#4;
     #104#22 = #24#47;
     #104#23 = #24#41;
     #104#35 = #24#192;

     |DDEFECTO #324;
     #324#0 = #104#35;
     |LEE 000#324=;
     #104#36 = #324#2;

     #104#37 = #24#193;
     #104#40 = #321#9;

     |DDEFECTO #324;
     #324#0 = #104#40;
     |LEE 000#324=;
     #104#41 = #324#2;

     #104#57 = #3#1;
     #104#60 = #24#202;
     #104#61 = #24#208;
     #104#64 = #24#15;

     #104#57 = #3#1;
     #104#19 = #3#10;
     #104#14 = #3#2;
     #104#20 = #3#3;
     #104#21 = #3#7;
     #104#59 = #3#6;
     |SI #3#14 = "  ";
         #104#7 = #24#25;
     |SINO;
         #104#7 = #3#14;
     |FINSI;

     |GRABA 020#104b;
|FPRC;

|PROCESO Procesa;
     fPedido = aDato % 110;
     fEntrega = aDato % 1110;
     aSuPed = aDato % 2110; |QBT aSuPed;
     aNumPed = aDato % 3106;
     aCodCli = aDato % 3805; |QBT aCodCli; aCodCli = ("000000" + aCodCli) % -106;
     aCodArt = aDato % 4306;
     aAlfa = aDato % 4910; nCanti = aAlfa; nCanti = nCanti / 1000;
     aDesArt = aDato % 5962;
     aPrecio = aDato % 12109;
     ||aRefere = aDato % 13109;    lunes, 16 de julio de 2018, 11:22:26 CEST
     aRefere = aDato % 13010;

     eaAlfa = aDesArt; |HAZ QuitaRarosImp; aDesArt = eaAlfa;

     |SI nLin = 1;
          |DDEFECTO #104;
          #104#25 = "";
          #104#0 = aNumPed;
          |LEE 000#104=;
          |SI FSalida = 0;
               aAlfa = "0000Pedido existente:" + aNumPed;
               |MENAV aAlfa;
               nError = 1; |ACABA;
          |FINSI;

          |DDEFECTO #24;
          |ABRE #24;
          #24#0 = aCodCli;
          |LEE 000#24=;
          |CIERRA #24;

          |DDEFECTO #3;
          |ABRE #3;
          #3#0 = #24#0;
          #3#1 = 1;
          |LEE 000#3=;
          |CIERRA #3;

          |DDEFECTO #25;
          |ABRE #25;
          |SI #3#14 = "  ";
               #25#0 = #24#25;
          |SINO;
               #25#0 = #3#14;
          |FINSI;
          |LEE 000#25=;
          |CIERRA #25;

          |HAZ CabPed;
     |FINSI;

     |DDEFECTO #19;
     |ABRE #19;
     #19#0 = aCodArt;
     |LEE 101#19=;
     |SI FSalida ! 0;
          #19#1 = aDesArt;
          #19#18 = aPrecio;
          #19#134 = aRefere;
          |GRABA 020#19n;

          |PUSHV 0501 2380;
          aAlfa = "dsz99895;" + #19#0;
          |SUB_EJECUTA_NP aAlfa;
          |POPV;

          |LEE 020#19=;
     |SINO;
          #19#1 = aDesArt;
          #19#134 = aRefere;
          |GRABA 020#19a;
     |FINSI;
     |CIERRA #19;

     |HAZ LinPed;
|FPRC;

|PROCESO Fichero;
     |DFICE aDes;
     aDes = aDes + Usuario;

     |DBASE aExe;
     aExe = aExe + "plugins/ansitoutf8.exe";

     aAlfa = aExe + " 1 " + aFic + " " + aDes;
     aAlfa = "cmd " + &34 + "/c START /MIN /WAIT " + aAlfa + &34;
     |SYSTEM aAlfa;

     |SI #0#2 = "S";
          |FBORRA aFic;
     |FINSI;
     aFic = aDes;

     nLin = 1;
     nError = 0;

     |ABRE #104;
     |ABRE #73;
     |ABRE #324;

     |XABRE "", aFic, nHandle;
     |SI FSalida ] 0;
          |XLEE nHandle, 250, aDato;
          |PARA; |SI FSalida ] 0; |HACIENDO;
               |HAZ Procesa;
               |SI nError ! 0; |SAL; |FINSI;
               |XLEE nHandle, 250, aDato;
          |SIGUE;
          |XCIERRA nHandle;
     |FINSI;
     |FBORRA aDes;

     |SI nLin > 1;
          |HAZ CalCabAgifa104;
          |ABRE #24;
          |ABRE #19;
          |BUCLELIN 2 ActCli #104;
          |CIERRA #24;
          |CIERRA #19;

          nImpo = 0; |BUCLE CalcConIva;
          eaTag = "PE";  |HAZ Riesgos;
          |GRABA 020#104a;

          ||aAlfa = "0000Pedido creado:" + aNumPed;
          ||MENAV aAlfa;
          nContaPed = nContaPed + 1;
     |FINSI;

     |CIERRA #324;
     |CIERRA #73;
     |CIERRA #104;
|FPRC;

|PROCESO Tipo3; |TIPO 3;
     |ABRE #142; |LEE 000#142p; |CIERRA #142;
     |ABRE #321; |LEE 000#321p; |CIERRA #321;

     aPath = #0#1; |QBF aPath;
     aAlfa = aPath % -101;
     |SI aAlfa ! "/"; |Y aAlfa ! "\"; aPath = aPath + "/"; |FINSI;
     aPath = aPath + "*.*";
     |_PDIR aPath;
     |PARA; |SI FSalida = 0; |HACIENDO;
          aFic = aPath;
          |HAZ Fichero;
          |_SDIR aPath;
     |SIGUE;
     |SI nContaPed > 0;
          |SI nContaPed = 1;
               aAlfa = "0000Importado 1 pedido.";
          |SINO;
               aAlfa = "0000Importados " + nContaPed + " pedidos.";
          |FINSI;
          |MENAV aAlfa;
     |FINSI;
|FPRC;

|PROGRAMA;
     |CLS;
     |ABRE #0;
     |LEE 101#0p;
     |SI FSalida ! 0; |GRABA 020#0b; |FINSI;
     #0#2 = "S";
     |PINPA #0#0;
     |PINDA #0#0;
     |ENDATOS #1#0;
     |SI FSalida = 0; |GRABA 020#0a; |FINSI;
     |CIERRA #0;
|FPRO;
