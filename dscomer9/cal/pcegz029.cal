|FICHEROS;
     pcegz029 #0;
     pcegw007 #1;
     pcegw008 #2;
     agifa019 #19;
     pcegm004 #4;   || Pedidos Cab
     pcegm005 #5;   || Pedidos Lin
     pcegm006 #6;   || Obs.
     pcegw009 #9;  || Tmp pedidos reservados
     pcegm011 #11;
     pcegz015 #15,MANTE;  || Ventana reserva uni
     gestempo;
     agifa324;
     agifa321;
     agifa017 #17;
     agifa019 #19;
     agifa142;
     pcegm012;
     pcegm005@;
|FIN;

|VARIABLES;
     &Tempo = "";
     nNume = 0;
     nForma = 0;
     {-1}aVent = "";
         aVent1 = 0;
         aVent2 = 0;
         aVent3 = 0;
         aVent4 = 0;
         aVent5 = "";
     nTecla = 0;
     nOk = 0;
     aEsCalibra = "";
     nBtnReservar = 0;
     nBtnVerPedido = 0;
     nPanta0 = 0;
     nPos1 = 0;
     nPos2 = 0;
     nRango = 0;
     nGridC = 0;
     nGridL = -1;
     nVentana0 = -1;
     nVenEspera = -1;
     aEsc1 = "";
     aEsc2 = "";
     aRetu = "";
     aTecla = "";
     aFich = "";
     aFichero = "";
     nHandle = 0;
     aAuto = "";
     aEmp = "";
     aArti = "";
     aProg = "";
     aClave = "";
     aPath = "";
     aAlfa = "";
     aTmp1 = "";
     aTmp2 = "";
     aTmp9 = "";
     nPendiente = 0;
     i = 0;
     nDepar = 0;
|FIN;

|PROCESO FinVentanaEspera;
     |SI nVenEspera = -1; |ACABA; |FINSI;
     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVenEspera;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;
     |FINVENTANA nVenEspera;
|FPRC;

|PROCESO IniVentanaEspera;
     |VENTANA 1512, 2291, -1, 17, "Mensaje";
     nVenEspera = FSalida;
     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVenEspera;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;
     |PINPA #0#6;
     |PTEC 802;
     |PAUSA;
|FPRC;

|PROCESO AcumulaRese;
     |DDEFECTO #19;
     |DDEFECTO #17;
     |ABRE #19;
     |ABRE #17;
     #19#0 = #5#2;
     |LEE 101#19=
     |SI FSalida ! 0; |GRABA 020#19a; |FINSI;
     #17#0 = #5#2;
     #17#1 = #5#3;
     |LEE 101#17=;
     |SI FSalida ! 0; |GRABA 020#17b; |FINSI;

     #19#12 = #19#12 + (#5#60 * nForma);
     #19#104 = #19#104 - (#5#60 * nForma);
     #17#8 = #17#8 + (#5#60 * nForma);
     #17#39 = #17#39 - (#5#60 * nForma);

     |HAZ ValoraStock;
     |GRABA 020#19a;
     |GRABA 020#17a;
     |CIERRA #17;
     |CIERRA #19;
|FPRC;

|PROCESO BtnVerPedido;
     |LEE 000#2=;
     |SI FSalida ! 0; |ACABA; |FINSI;

     |HAZ IniVentanaEspera;
     aAuto = "1";
                              ||Programa + Modo + Clave
     aProg = "pcegz008" + "4" + #2#28 + (("000000" + #2#0) % -106);
     |PTEC 807;
|FPRC;

|PROCESO PideCalibra;
     aAlfa = #pcegm005@#2; |QBF aAlfa;
     aArti = #5#2; |QBF aArti;
     aAlfa = aAlfa - aArti;
     |SI aAlfa ! "CAL"; |ACABA; |FINSI;

     |SI #pcegm005@#30 ! 0; |ACABA; |FINSI; || ya esta pedido

     aEsCalibra = "S";
     aAlfa = "0000SPedir calibracion al proveedor, articulo: [" + #pcegm005@#2 + "]";
     |CONFI aAlfa;
     |SI FSalida = 0;
          aAuto = "1";
            ||Programa + Articulo + Unidades + Serie + Pedido + Linea
          aProg = "pcegz016" + #pcegm005@#2 + (("0000000000000000" + #pcegm005@#5) % -116);
          aProg = aProg + #pcegm005@#28 + (("000000" + #pcegm005@#0)%-106) + (("000" + #pcegm005@#1)%-103);
     |FINSI;

     |ERROR10;
|FPRC;

|DEFBUCLE PideCalibra;
     #pcegm005@#1003;
     ;
     #5#28, #5#0, 001;
     #5#28, #5#0, 999;
     ;
     NULL, PideCalibra;
|FIN;

|PROCESO BtnReservar;
     |LEE 000#2=;

     |ABRE #4;
     |ABRE #5;
     #4#25 = #2#28;
     #4#0 = #2#0;
     |LEE 020#4=;
     |SI FSalida ! 0; |ACABA; |FINSI;

     #5#28 = #2#28;
     #5#0 = #2#0;
     #5#1 = #2#1;
     |LEE 020#5=;
     |SI FSalida ! 0; |ACABA; |FINSI;

     nPendiente = #5#5 - #5#43;

     |SI #5#5 < 0;
          |MENAV "0000Unidades negativas...";
          |ACABA;
     |FINSI;

     |SI nPendiente [ 0;
          |MENAV "0000La linea esta completamente servida...";
          |ACABA;
     |FINSI;

     aAlfa = #5#2;
     aAlfa = aAlfa - "CAL";
     |SI aAlfa ! #5#2;
          |MENAV "0000El articulo es de calibracion...";
          |ACABA;
     |FINSI;

     |VENTANA 1302, 2197, -1, 17, "Reservar unidades";
     nVentana0 = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVentana0;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |LEE 121#4=;
     |LEE 121#5=;

     |DDEFECTO #15;
     #15#0 = #5#28;
     #15#1 = #5#0;
     #15#2 = #5#1;
     #15#3 = #5#2;
     #15#4 = #5#4;
     #15#5 = #5#5;
     #15#6 = #5#60;

     nForma = -1;
     |HAZ AcumulaRese;

     |PINPA #0#15;
     |PINDA #0#15;
  |ET Reserva;
     |ENDATOS #2#15;
     |SI FSalida = 0; |Y nTecla = 0;
          |SI #15#6 > nPendiente;
               |MENAV "0000No se pueden reservar mas unidades de las pendientes...";
               |VETE Reserva;
          |FINSI;

          aEsCalibra = "N";
          |DDEF aAlfa;
          aAlfa = aAlfa + "pcegm005";
          |CARGA_DEF aAlfa, pcegm005@;
          |SI FSalida = 0;
               |DFICE aAlfa; aAlfa = aAlfa + aEmp + "/";
               |PATH_DAT #pcegm005@#aAlfa#;
               |BUCLE PideCalibra;
               |DESCARGA_DEF #pcegm005@;
          |FINSI;
          |SI aEsCalibra = "N";
               nOk = 0;
          |SINO;
               |SI aAuto = "";
                    nOk = 1;
                    |MENAV "0000No se reservan las unidades...";
               |SINO;
                    nOk = 0;
               |FINSI;
          |FINSI;
          |SI nOk = 0;
               #5#60 = #15#6;
               |GRABA 020#5a;

               #2#60 = #5#60;
               |GRABA 020#2a;

               |ABRE #9;
               #9#0 = #5#28;
               #9#1 = #5#0;
               |GRABA 000#9n;
               |CIERRA #9;

               |SALVA_FICHA #5;
               |HAZ RecalTipoPed;
               |GRABA 020#4a;
               |REPON_FICHA #5;
               |REFRESCACONTROL nGridL;
          |FINSI;
     |FINSI;

     nForma = 1;
     |HAZ AcumulaRese;

     |LIBERA #4;
     |LIBERA #5;
     |CIERRA #4;
     |CIERRA #5;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVentana0;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA nVentana0;
     |PULSATECLA;

     |SI aAuto ! "";  || Pide CAL al proveedor
          |HAZ IniVentanaEspera;
          |PTEC 807;
     |FINSI;
|FPRC;

|PROCESO LineasPed;
     |PARA i = 0; |SI i [ 60; |HACIENDO i = i + 1;
          #2i = #5i;
     |SIGUE;
     |GRABA 020#2n;
|FPRC;

|PROCESO EventoC;
     |SI FSalida = 1;  |O FSalida = 2;
          |LEE 000#1=;

          |ABRE #4;
          #4#25 = #1#28;
          #4#0 = #1#0;
          |LEE 020#4=;
          |SI FSalida = 0;
               #0#5 = #4#4; |PINTA #0#5;
               #0#6 = #4#8; |PINTA #0#6;
               #0#7 = #4#1; |PINTA #0#7;
               #0#8 = #4#62; |PINTA #0#8;

               |CIERRAT #2;
               |DELINDEX #2;
               |ABRET #2;

               |BUCLELIN 2 LineasPed #4;
               |LEE 000#2p;

               #0#2 = #4#53; |PINTA #0#2;
               #0#3 = #4#54; |PINTA #0#3;
               #0#4 = #4#55; |PINTA #0#4;
               |HAZ Grid2;
          |FINSI;
          |CIERRA #4;
     |FINSI;
|FPRC;

|PROCESO MinC;
     #1#28 = "     ";
     #1#0 = 1;
     #1#1 = 1;
|FPRC;

|PROCESO MaxC;
     #1#28 = "zzzzz";
     #1#0 = 999999;
     #1#1 = 999;
|FPRC;

|PROCESO EventoL;
     |SI FSalida = 1;  |O FSalida = 2;
          |LEE 000#2=;
     |FINSI;
|FPRC;

|PROCESO MinL;
     #2#28 = #1#28;
     #2#0 = #1#0;
     #2#1 = 1;
|FPRC;

|PROCESO MaxL;
     #2#28 = #1#28;
     #2#0 = #1#0;
     #2#1 = 999;
|FPRC;

|PROCESO Pedidos;
     #4#25 = #5#28;
     #4#0 = #5#0;
     |LEE 000#4=;
     |SI FSalida ! 0; |O #4#70 ! "C"; |ACABA; |FINSI;

     nPendiente = #5#5 - #5#43;
     |SI nPendiente [ 0; |ACABA; |FINSI;

     |PARA i = 0; |SI i [ 60; |HACIENDO i = i + 1;
          #1i = #5i;
     |SIGUE;
     |GRABA 020#1n;
|FPRC;

|DEFBUCLE Pedidos;
     #5#3;
     ;
     aArti, "     ", 000001, "      ";
     aArti, "zzzzz", 999999, "zzzzzz";
     ;
     NULL, Pedidos;
|FIN;

|PROCESO LeeTxt;
     |DBASE aFich;
     aFich = aFich + "fich/";
     aFichero = aFich + "A" + (Usuario % 810);
     |XABRE "", aFichero, nHandle;

     |XLEE nHandle, 250, aEmp;
     |XLEE nHandle, 250, aArti;
     |XLEE nHandle, 250, aAuto;
     |XLEE nHandle, 250, aClave;

     |XCIERRA nHandle;
|FPRC;

|PROCESO GrabaTxt;
     |DBASE aFich;
     aFich = aFich + "fich/";
     aFichero = aFich + "A" + (Usuario % 810);
     |XABRE "W", aFichero, nHandle;

     |XGRABA nHandle, aEmp;
     |XGRABA nHandle, aArti;
     |XGRABA nHandle, aAuto;
     aClave = #1#28 + (("000000" + #1#0) % -106) + (("000" + #1#1) % -103) ;
     aClave = aClave + (("000" + #2#1) % -103) ;
     |XGRABA nHandle, aClave;
     |XGRABA nHandle, aProg;
     |XGRABA nHandle, aTmp9;

     |XCIERRA nHandle;
|FPRC;

|PROCESO Grid2;
     |SI nGridL ! -1; |DESTRUYECONTROL nGridL; |FINSI;

     nRango = 4 + 8 + 16 + 32;
     nPos1 = 2802;
     nPos2 = 3498;
     |LINEAL_SIMPLE #1#2, nRango, nPos1, nPos2, MinL, MaxL, EventoL;
     nGridL = FSalida;
|FPRC;

|PROCESO Posiciona; || (sin el PULSATECLA no funcionaba!!!)
     |PULSATECLA;
     aAlfa = aClave % 105; #1#28 = aAlfa;
     aAlfa = aClave % 606; #1#0 = aAlfa;
     aAlfa = aClave % 1203; #1#1 = aAlfa;
     |LEE 000#1=;
     |REFRESCACONTROL nGridC;

     |PULSATECLA;
     aAlfa = aClave % 105; #2#28 = aAlfa;
     aAlfa = aClave % 606; #2#0 = aAlfa;
     aAlfa = aClave % 1503; #2#1 = aAlfa;
     |LEE 000#2=;
     |REFRESCACONTROL nGridL;
|FPRC;

|PROCESO Confirma; |TIPO 12;
     |SI FSalida ! 7;
          |CONFI "0000N¿Desea continuar?";
          nTecla = FSalida;
     |SINO;
          nTecla = 1;
     |FINSI;
|FPRC;

|PROCESO Tipo80; |TIPO 80;
     FSalida = 3599;
|FPRC;

|PROGRAMA;
     |CLS;

     |PINPA #0#0;  nPanta0 = FSalida;
     |PINDA #0#0;

     |HAZ LeeTxt;

     |ABRE #11;
     #11#0 = Usuario % 810;
     |LEE 000#11=;
     |SI FSalida = 0;
          nDepar = #11#3;
     |FINSI;
     |CIERRA #11;

     |HAZ DecimalesBase;

     |ABRE #19;
     #19#0 = aArti;
     |LEE 000#19=;
     |CIERRA #19;

     #0#0 = #19#0; |PINTA #0#0;
     #0#1 = #19#1; |PINTA #0#1;

     |HAZ CreaTempo; |NOME_DAT #1#Tempo#; aTmp1 = Tempo;
     |HAZ CreaTempo; |NOME_DAT #2#Tempo#; aTmp2 = Tempo;
     |HAZ CreaTempo; |NOME_DAT #9#Tempo#; aTmp9 = Tempo;
     |ABRE #1;
     |ABRE #4;
     |BUCLE Pedidos;
     |CIERRA #4;
     |CIERRA #1;

     |ABRET #1;
     |ABRET #2;
     |LEE 000#1p;

     |HAZ Grid2;

     nRango = 4 + 8 + 16 + 32;
     nPos1 = 1002;
     nPos2 = 2198;
     |LINEAL_SIMPLE #1#1, nRango, nPos1, nPos2, MinC, MaxC, EventoC;
     nGridC = FSalida;

     |CONTROL_SIMPLE nPanta0, "BOTON, Reservar", 3502, 3516, BtnReservar;
     nBtnReservar = FSalida;

     |CONTROL_SIMPLE nPanta0, "BOTON, Ver Pedido", 3518, 3532, BtnVerPedido;
     nBtnVerPedido = FSalida;

     |SI nDepar = 1;
          |ESTADO_CONTROL nBtnReservar, "HIDE";
     |FINSI;

     |SI aAuto = "1";
          |HAZ Posiciona;
     |FINSI;

     aAuto = ""; || salida normal del programa

     aEsc1 = &255 + "806";
     aEsc2 = &255 + "807";
     aRetu = &255 + "802";
     |PARA; |SI; |HACIENDO;
         FSalida = "::{{001}}Salir," + nGridC;
         |LEETECLA aTecla;
         |SI aTecla = aEsc1; |SAL;  |FINSI;
         |SI aTecla = aEsc2; |SAL;  |FINSI;
         |SI aTecla = aRetu; |SAL;  |FINSI;
     |SIGUE;

     |HAZ GrabaTxt;

     |CIERRAT #1;
     |CIERRAT #2;

     |DELINDEX #1; Tempo = aTmp1; |HAZ BoraTempo;
     |DELINDEX #2; Tempo = aTmp2; |HAZ BoraTempo;
     || (esto lo hace el pcegz005)
     ||DELINDEX #9; Tempo = aTmp9; |HAZ BoraTempo;

     |DESTRUYECONTROL nGridC;
     |DESTRUYECONTROL nGridL;
     |FIN_CONTROL_WINDOWS nBtnReservar;
     |FIN_CONTROL_WINDOWS nBtnVerPedido;

     |HAZ FinVentanaEspera;
     |PULSATECLA;
|FPRO;
