     || Si se modifica modificar el tagz0015

|FICHEROS;
     tagz0015 #0;
     agifa010 #10;
     agifa019 #19;
     tagm0021 #21;
     tagm0036 #36;
     agifa081 #81;
     agifa067 #67;
     agifa071 #71;
     agifa073 #73;
     agifa104 #104;
     tagm0021@;     || Usado como temporal para chequear si se ha modificado
     tagw0035@;     || Usado como temporal para saber cuales se han tratando
     noecontr@;     || temporal de los no encontrados en ped.vta
     nombrcli@;     || temporal que guarda el nombre
     nomodifi@;     || temporal para los que no se deben de modificar
|FIN;

|VARIABLES;
     &Tempo = "";
     &enSw = 0;
     nSalida = 0;
     nUni = 0;
     nAbo = 0;
     aAlfa = "";
     aParam = "";
     aFic = "";
     aCli = "";
     aEsAsociaCli = "";
     aLon = "";
     aSer = "";
     nLin = 0;
     aImpreso = "";
     aTmp21 = "";
     aTmp35 = "";
     aTmp = "";
     aTm0 = "";
     aTmn = "";
     aPath = "";
     aEncontrado = "";
     nEsta = 0;

     aIP = "";
     aServidor = "";
     aFrom = "";
     aTo = "";
     aSubject = "";
     aMensaje = "";
     aDjunto = "";
     nHandle = 0;
     aEmp = "";
     aDirDestino = "";

     {1}aBaseLocal = "";
|FIN;

|PROCESO EnviaMail;
     aDirDestino = #0#7; |QBF aDirDestino;
     aDjunto = aFic;

     |DFICE aEmp;
     aEmp = aEmp %- 205;
     aIP = "xdscorreo.dv.diagram.net";
     aServidor = "asmtp.diagram.es";
     aFrom = aDirDestino;
     aTo = aDirDestino;
     |SI aImpreso ! "S";
          aSubject = "Recalculo articulos varios empresa: " + aEmp + " (correcto)";
          aMensaje = "Sin diferencias.";
     |SINO;
          aSubject = "Recalculo articulos varios empresa: " + aEmp + " (con diferencias)";
          aMensaje = "Adjunta diferencias encontradas.";
     |FINSI;
     |DSCORREO_ENVIA aIP, aServidor, aFrom, aTo, aSubject, aMensaje, aDjunto;
|FPRC;

|PROCESO QuitaHisto;
     #36#2 = #tagw0035@#0;
     #36#1 = #tagw0035@#3;
     |LEE 000#36=;
     |SI FSalida = 0;
          #21#0 = #36#1;
          #21#2 = #36#2;
          |LEE 121#21=;
          |SI FSalida = 0;
               #21#12 = #21#12 - #36#4;
               |SI #21#12 < 0; #21#12 = 0; |FINSI;
               |GRABA 020#21a;
               |LIBERA #21;

          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE QuitaHisto;
     #tagw0035@#1002;
     ;
     "                    ", "      ";
     "zzzzzzzzzzzzzzzzzzzz", "zzzzzz";
     ;
     NULL, QuitaHisto;
|FIN;

|PROCESO AlbVenta;
     |SI #10#43 = "S";
          nAbo = -1;
     |SINO;
          nAbo = 1;
     |FINSI;

     nUni = #71#5 * nAbo;
     |SI nUni < 0; |ACABA; |FINSI; || los abonos no se tratan

     #noecontr@#0 = #71#2;
     |LEE 000#noecontr@.=;
     |SI FSalida = 0;
          #21#0 = #noecontr@#3;
          #21#2 = #noecontr@#0;
          |LEE 121#21=;
          #21#12 = #21#12 - nUni;
          |SI #21#11 < 0; #21#11 = 0; |FINSI;
          |SI #21#12 < 0; #21#12 = 0; |FINSI;

          #21#13 = #10#4;
          |GRABA 020#21a;
          |LIBERA #21;

     |FINSI;
|FPRC;

|DEFBUCLE AlbVenta;
     #10#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, NULL, AlbVenta;
|FIN;

|PROCESO BusPedVta;
     nEsta = 0;
     #73#2 = #tagw0035@#0;
     #73#28 = "";
     #73#0 = 0;
     #73#20 = "";
     |LEE 000#73];
     |PARA; |SI FSalida = 0; |Y #73#2 = #tagw0035@#0; |HACIENDO;
          nEsta = 1;
          |DDEFECTO #104;
          #104#25 = #73#28;
          #104#0 = #73#0;
          |LEE 000#104=;

          #21#0 = #tagw0035@#3;
          #21#2 = #tagw0035@#0;
          |LEE 121#21=;
          #21#12 = #21#12 - #73#43;
          #21#13 = #104#8;
          |SI #21#11 < 0; #21#11 = 0; |FINSI;
          |SI #21#12 < 0; #21#12 = 0; |FINSI;
          |GRABA 020#21a;
          |LIBERA #21;


          |LEE 000#73s;
     |SIGUE;

     |SI nEsta = 0;
          aEncontrado = "N";

          #noecontr@#0 = #tagw0035@#0;
          #noecontr@#3 = #tagw0035@#3;
          |GRABA 020#noecontr@.n;
     |FINSI;
|FPRC;

|DEFBUCLE BusPedVta;
     #tagw0035@#1002;
     ;
     "                    ", "      ";
     "zzzzzzzzzzzzzzzzzzzz", "zzzzzz";
     ;
     NULL, BusPedVta;
|FIN;

|PROCESO CheqModi;
     #21#0 = #tagm0021@#0;
     #21#2 = #tagm0021@#2;
     |LEE 000#21=;
     |SI FSalida ! 0; |ACABA; |FINSI;

     enSw = 0;
     |SI #21#3 ! #tagm0021@#3; enSw = 2; |FINSI;
     |SI #21#4 ! #tagm0021@#4; enSw = 2; |FINSI;
     |SI #21#5 ! #tagm0021@#5; enSw = 2; |FINSI;
     |SI #21#6 ! #tagm0021@#6; enSw = 2; |FINSI;
     |SI #21#7 ! #tagm0021@#7; enSw = 2; |FINSI;
     |SI #21#8 ! #tagm0021@#8; enSw = 2; |FINSI;
     |SI #21#9 ! #tagm0021@#9; enSw = 2; |FINSI;
     |SI #21#10 ! #tagm0021@#10; enSw = 2; |FINSI;
     |SI #21#11 ! #tagm0021@#11; enSw = 2; |FINSI;
     |SI #21#12 ! #tagm0021@#12; enSw = 2; |FINSI;
     |SI #21#13 ! #tagm0021@#13; enSw = 2; |FINSI;
     |SI enSw ! 0;
          #0#9 = #tagm0021@#0;
          #0#10 = #tagm0021@#2;
          #0#11 = #tagm0021@#3;
          #0#12 = #tagm0021@#4;
          #0#13 = #tagm0021@#5;
          #0#14 = #tagm0021@#6;
          #0#15 = #tagm0021@#7;
          #0#16 = #tagm0021@#8;
          #0#17 = #tagm0021@#9;
          #0#18 = #tagm0021@#10;
          #0#19 = #tagm0021@#11;
          #0#20 = #tagm0021@#12;
          #0#21 = #tagm0021@#13;

          |PRINT;
          aImpreso = "S";
     |FINSI;
|FPRC;

|DEFBUCLE CheqModi;
     #tagm0021@#1002;
     ;
     "      ", 00001;
     "zzzzzz", 99999;
     ;
     NULL, CheqModi;
|FIN;

|PROCESO EsAsociaCli;
     aAlfa = #67#2; |QBF aAlfa;
     aLon = aAlfa % A0;
     |SI aLon ! 14;
          aEsAsociaCli = "S";
     |SINO;
          aSer = #67#2 % 102;
          aAlfa = #67#28; |QBF aAlfa;
          |SI aAlfa = aSer;
               aEsAsociaCli = "N";
          |SINO;
               aEsAsociaCli = "S";
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Alta;
     |SELECT #1#21;
     #21#0 = aCli;
     #21#1 = 99999;
     |LEE 000#21];
     |SI FSalida = 0;
          |LEE 000#21a;
     |SINO;
          |LEE 000#21u;
     |FINSI;
     |SI FSalida = 0; |Y #21#0 = aCli;
          nLin = #21#1 + 1;
     |SINO;
          nLin = 1;
     |FINSI;

     |DDEFECTO #21;
     #21#0 = aCli;
     #21#1 = nLin;
     #21#2 = #67#2;
     #21#3 = #67#4;
     #21#4 = #67#47;
     #21#5 = #67#48;
     #21#6 = #67#49;
     #21#7 = #67#28;
     #21#8 = #67#0;
     #21#9 = #67#1;
     #21#10 = aEsAsociaCli;
     #21#11 = #67#5 - #67#38;|| Pdte Rec
     #21#12 = #67#38;    || Reser Vta
     |SI #21#11 < 0; #21#11 = 0; |FINSI;
     |SI #21#12 < 0; #21#12 = 0; |FINSI;
     #21#13 = "";   || Nombre
     |GRABA 020#21n;


     #tagw0035@#0 = #21#2;
     #tagw0035@#3 = #21#0;
     |GRABA 040#tagw0035@.n;
|FPRC;

|PROCESO Modi;
     #21#3 = #67#4;
     #21#4 = #67#47;
     #21#5 = #67#48;
     #21#6 = #67#49;
     #21#7 = #67#28;
     #21#8 = #67#0;
     #21#9 = #67#1;
     #21#10 = aEsAsociaCli;
     #21#11 = #67#5 - #67#38;|| Pdte Rec
     #21#12 = #67#38;    || Reser Vta
     |SI #21#11 < 0; #21#11 = 0; |FINSI;
     |SI #21#12 < 0; #21#12 = 0; |FINSI;
     ||#21#13 = "";   || Nombre
     |GRABA 020#21a;


     #tagw0035@#0 = #21#2;
     #tagw0035@#3 = #21#0;
     |GRABA 040#tagw0035@.n;
|FPRC;

|PROCESO PedCompra;
     |SI #67#2 < #0#3; |O #67#2 > #0#4;
          |ACABA;
     |FINSI;
     #19#0 = #67#2;
     |LEE 000#19=;
     |SI FSalida ! 0; |O #19#126 ! "VAR";
          |ACABA;
     |FINSI;

     |HAZ EsAsociaCli;

     aAlfa = #67#2; |QBF aAlfa;
     aCli = aAlfa % -106;

     |SELECT #2#21;
     #21#0 = aCli;
     #21#2 = #67#2;
     |LEE 101#21=;
     |SI FSalida ! 0;
          |HAZ Alta;
          |SI #0#5 = "N";
               enSw = 1;
               |PRINT;
               aImpreso = "S";
          |FINSI;
     |SINO;
          |HAZ Modi;
     |FINSI;
     |LIBERA #21;
|FPRC;

|DEFBUCLE PedCompra;
     #81#1;
     1;
     "     ", 000001, #0#1;
     "zzzzz", 999999, #0#2;
     ;
     NULL, NULL, PedCompra;
|FIN;

|PROCESO Copialo;
     |SALVA_DATOS #21;
     |REPON_DATOS #tagm0021@;
     |GRABA 020#tagm0021@.n;

     |SI #21#14 = "S";
          |SALVA_DATOS #21;
          |REPON_DATOS #nomodifi@;
          |GRABA 020#nomodifi@.n;
     |FINSI;
|FPRC;

|DEFBUCLE Copialo;
     #21#3;
     ;
     #0#3;
     #0#4;
     ;
     NULL, Copialo;
|FIN;

|PROCESO LlenaNom;
     #nombrcli@#0 = #21#2; ||Art
     #nombrcli@#3 = #21#0; ||Cli
     #nombrcli@#4 = #21#13; ||Nom
     |GRABA 020#nombrcli@.n;
|FPRC;

|DEFBUCLE LlenaNom;
     #21#3;
     ;
     #0#3;
     #0#4;
     ;
     NULL, LlenaNom;
|FIN;

|PROCESO AsignaNom;
     #nombrcli@#0 = #21#2;  ||Art
     #nombrcli@#3 = #21#0;  ||Cli
     |LEE 000#nombrcli@.=;
     |SI FSalida = 0;
          #21#13 = #nombrcli@#4;
          |GRABA 020#21a;
     |FINSI;
     |LIBERA #21;
|FPRC;

|DEFBUCLE AsignaNom;
     #21#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, AsignaNom;
|FIN;

|PROCESO CopiaNoModi;
     |SALVA_DATOS #nomodifi@;
     #21#0 = #nomodifi@#0;
     #21#1 = #nomodifi@#1;
     |LEE 101#21=;
     nSalida = FSalida;
     |REPON_DATOS #21;

     |SI nSalida = 0;
          |GRABA 020#21a;
     |SINO;
          |GRABA 020#21n;
     |FINSI;
     |LIBERA #21;

|FPRC;

|DEFBUCLE CopiaNoModi;
     #nomodifi@#1002;
     ;
     "      ", 00001;
     "zzzzzz", 99999;
     ;
     NULL, CopiaNoModi;
|FIN;

|PROCESO Recalculo;
     |DDEF aPath;
     aAlfa = aPath + "tagm0021";
     |CARGA_DEF aAlfa, tagm0021@;
     |SI FSalida ! 0; |ACABA; |FINSI;

     |CARGA_DEF aAlfa, nomodifi@;
     |SI FSalida ! 0; |ACABA; |FINSI;

     aAlfa = aPath + "tagw0035";
     |CARGA_DEF aAlfa, tagw0035@;
     |SI FSalida = 0;
          aAlfa = aPath + "tagw0035";
          |CARGA_DEF aAlfa, noecontr@;
          |SI FSalida = 0;
               aAlfa = aPath + "tagw0035";
               |CARGA_DEF aAlfa, nombrcli@;
               |SI FSalida = 0;
                    |HAZ CreaTempo; aTmp35 = Tempo; |NOME_DAT #tagw0035@#Tempo#; |DELINDEX #tagw0035@;
                    |HAZ CreaTempo; aTmp21 = Tempo; |NOME_DAT #tagm0021@#Tempo#; |DELINDEX #tagm0021@;
                    |HAZ CreaTempo; aTmp = Tempo; |NOME_DAT #noecontr@#Tempo#; |DELINDEX #noecontr@;
                    |HAZ CreaTempo; aTm0 = Tempo; |NOME_DAT #nombrcli@#Tempo#; |DELINDEX #nombrcli@;
                    |HAZ CreaTempo; aTmn = Tempo; |NOME_DAT #nomodifi@#Tempo#; |DELINDEX #nomodifi@;

                    |ABRE #tagm0021@;
                    |ABRE #nomodifi@;
                    |BUCLE Copialo;
                    |CIERRA #nomodifi@;
                    |CIERRA #tagm0021@;

                    |SI #0#5 = "S";
                         |ABRE #nombrcli@;
                         |BUCLE LlenaNom;
                         |CIERRA #nombrcli@;
                         |DELINDEX #21;
                    |FINSI;

                    |ABRE #tagw0035@;
                    |ABRE #tagm0021@;
                    |ABRE #noecontr@;

                    |ABRE #19;
                    |ABRE #21;
                    |BUCLE PedCompra;
                    |CIERRA #21;
                    |CIERRA #19;

                    |ABRE #21; |SELECT #2#21;
                    |ABRE #104;
                    |ABRE #73; |SELECT #3#73;
                    |BUCLE BusPedVta;

                    |CIERRA #73;
                    |CIERRA #104;
                    |SI aEncontrado = "N";
                         |SELECT #2#noecontr@;
                          |BUCLE AlbVenta;
                    |FINSI;

                    |ABRE #36; |SELECT #2#36;
                    |BUCLE QuitaHisto;
                    |CIERRA #36;

                    |CIERRA #21;

                    |SI #0#5 = "S";
                         |ABRE #nombrcli@;
                         |BUCLE AsignaNom;
                         |CIERRA #nombrcli@;
                    |FINSI;

                    |CIERRA #noecontr@;
                    |CIERRA #tagm0021@;
                    |CIERRA #tagw0035@;

                    |ABRE #21;
                    |BUCLE CopiaNoModi;
                    |CIERRA #21;

                    |ABRE #21; |SELECT #2#21;
                    |BUCLE CheqModi;
                    |CIERRA #21;
                    |SI aImpreso ! "S";
                         enSw = 0; || sin errores
                         |PRINT;
                    |FINSI;

                    Tempo = aTmn; |HAZ BoraTempo; |DELINDEX #nomodifi@;
                    Tempo = aTm0; |HAZ BoraTempo; |DELINDEX #nombrcli@;
                    Tempo = aTmp; |HAZ BoraTempo; |DELINDEX #noecontr@;
                    Tempo = aTmp21; |HAZ BoraTempo; |DELINDEX #tagm0021@;
                    Tempo = aTmp35; |HAZ BoraTempo; |DELINDEX #tagw0035@;

                    |DESCARGA_DEF #nombrcli@;
               |FINSI;
               |DESCARGA_DEF #noecontr@;
          |FINSI;
          |DESCARGA_DEF #tagw0035@;
     |FINSI;
     |DESCARGA_DEF #nomodifi@;
     |DESCARGA_DEF #tagm0021@;
|FPRC;

|PROCESO Inicio;
     |SI #0#6 = "S";
          |DFICE aFic;
          aFic = aFic + "recalvar.txt";
          Impresora = aFic;
          |IMPRE -77;
          |SI FSalida = 0;
               |INFOR "tagz0015";
               |SI FSalida = 0;
                    |HAZ Recalculo;
                    |PIEINF;
                    |FININF;
               |FINSI;
               |FINIMP;
          |FINSI;
          |HAZ EnviaMail;
          |FBORRA aFic;
     |SINO;
          |IMPRE 0;
          |SI FSalida = 0;
               |INFOR "tagz0015";
               |SI FSalida = 0;
                    |HAZ Recalculo;
                    |PIEINF;
                    |FININF;
               |FINSI;
               |FINIMP;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Mail; |TIPO 0;
     aAlfa = #0#6;
     |C_M #0#7, 1, aAlfa;
|FPRC;

|PROGRAMA;
     aParam = PARAMETRO;
     |ABRE #0;
     |SI aParam = "menu";
          |CLS;
          |LEE 101#0=;
          |SI FSalida ! 0; |GRABA 020#0b; |FINSI;
          |PINPA #0#0;
          |PINDA #0#0;
          |ENDATOS #1#0;
          |SI FSalida = 0;
               |HAZ Inicio;
               |GRABA 020#0a;
          |FINSI;
     |SINO;
          |LEE 101#0=;
          |HAZ Inicio;
     |FINSI;
     |CIERRA #0;
|FPRO;
