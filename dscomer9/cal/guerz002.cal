|FICHEROS;
     guerz002 #0;
     guerw003 #1;
     guerw004 #2;
     agif0041 #3;

     agifa010@ #100;
     agifa071@ #200;

     agifa010 #10;
     agifa071 #71;
     agifa065 #65;
     agifa017 #17;
     agifa019 #19;
     agifa024 #24;
     agifa027 #27;
|FIN;

|CAMPOS;
     #71#2 a1;
     #71#3 a2;
     #71#6 a3;
     #71#8 a4;
     #71#10 a5;
     #71#11 a6;
     #71#27 a7;
     #71#33 a8;

     #10#43 b1;
     #10#1 b2;
     #10#3 b3;
     #10#6 b4;
     #10#8 b5;
     #10#70 b6;
     #10#68 b7;
     #10#5 b8;
     #10#7 b9;
     #10#32 b10;
     #10#12 b11;
     #10#14 b12;
|FIN;

|INCLUYE i_varart;

|VARIABLES;
     &Tempo    = "";
     &enSwMenu = 0;
     aTempo1   = "";
     aTempo2   = "";
     aAlfa24   = "";
     aAlfa19   = "";
     aAlfa17   = "";
     aAlfa65   = "";
     aAlfa71   = "";
     aAlfa10   = "";
     aDef      = "";
     aPath     = "";
     c43       = "";
     c1        = @;
     c3        = "";
     c6        = "";
     c8        = "";
     c70       = "";
     c68       = "";
     c5        = 0;
     c17       = 0;
     c32       = 0;
     c12       = 0;
     c14       = 0;
     l2        = "";
     l3        = 0;
     l6        = 0;
     l8        = 0;
     l10       = 0;
     l11       = 0;
     l27       = "";
     l33       = 0;
     nLin      = 0;
     aSerie    = "";
     nNume     = 0;
     aAlfa     = "";
     i         = 0;
     nCampo    = 0;
     nMult     = 0;
     nImpDiv   = 0;
     nCanti    = 0;
     nAbo      = 0;
     nOrdenCab = 0;
     nOrdenLin = 0;
     nNueva    = 0;
|FIN;

|PROCESO Contador;
     #10#52 = #100#52;
     enSwMenu = 1;
     |HAZ ContaAV7;
     nNume = #10#0;
|FPRC;

|PROCESO Cliente;
     |ABRE #24;
     #24#0 = #10#3;
     |LEE 101#24=;
     |SI FSalida ! 0; |GRABA 020#24b; |FINSI;
     |SI #10#15 ] 0;
          nAbo = 1;
     |SINO;
          nAbo = -1;
          #10#15 = -#10#15;
     |FINSI;
     nCanti = #10#15 < #10#5;
     nCanti = nCanti < #10#32;
     nCanti = nCanti < #10#7;
     nCanti = nCanti * nAbo;
     #10#15 = #10#15 * nAbo;
     |SI #10#43 = AN;
          #24#50 = #24#50 +. nCanti;    || es albaran
     |SINO;
          #24#50 = #24#50 -. nCanti;    || es abono
     |FINSI;
     |GRABA 020#24a;
     |CIERRA #24;

     |SI #10#43 = "N";
          enImpCliPen = nCanti;
     |SINO;
          enImpCliPen = -nCanti;
     |FINSI;
     eaCliente = #10#3;
     efFecha = #10#1;
     |HAZ AcumulaCli;
|FPRC;

|PROCESO Lineas;
     |HAZ CalCabAgifa010;
     enForma = 1;
     eaProgVta = "agifa010";
     |HAZ AcumulaAV;
|FPRC;

|DEFBUCLE Lineas;
     #71#1;
     ;
     aSerie, nNume, 001;
     aSerie, nNume, 999;
     ;
     NULL, Lineas;
|FIN;

|PROCESO FinCab;
     |HAZ LimCabAgifa010;
     |BUCLE Lineas;
     |GRABA 020#10a;
     |LIBERA #10;
     |HAZ Cliente;
|FPRC;


|PROCESO Crea_agifa071;
     |SI l2 ! #200#2; |O l3 ! #200#3; |O l6 ! #200#6;
               |O l8 ! #200#8; |O l10 ! #200#10;
               |O l11 ! #200#11; |O l27 ! #200#27;
               |O l33 ! #200#33; |O nLin = 0; |O #0#7 = "N";
          nLin = nLin + 1;
          aAlfa = "|NC";
          aAlfa = #200#aAlfa#;
          nCampo = aAlfa;
          nCampo = nCampo - 1;

          |PARA i = 0; |SI i [ nCampo; |HACIENDO i = i + 1;
               #71i = #200#i#;
          |SIGUE;
          |HAZ TotalLin71;
          #71#28 = aSerie;
          #71#0 = nNume;
          #71#1 = nLin;
          |GRABA 020#71b;
     |SINO;
          #71#28 = aSerie;
          #71#0 = nNume;
          #71#1 = nLin;
          |LEE 101#71=;
          |SI FSalida = 0;
               #71#5 = #71#5 + #200#5;
               |GRABA 020#71a;
               |LIBERA #71;
          |FINSI;
          |HAZ TotalLin71;
     |FINSI;
     l2 = #200#2;
     l3 = #200#3;
     l6 = #200#6;
     l8 = #200#8;
     l10 = #200#10;
     l11 = #200#11;
     l27 = #200#27;
     l33 = #200#33;
|FPRC;

|PROCESO Crea_agifa010;
     |SI c43 ! #100#43; |O c1 ! #100#1; |O c3 ! #100#3;
               |O c6 ! #100#6; |O c8 ! #100#8;
               |O c70 ! #100#70; |O c68 ! #100#68;
               |O c5 ! #100#5; |O c17 ! #100#7;
               |O c32 ! #100#32; |O c12 ! #100#12;
               |O c14 ! #100#14; |O #0#6 = "N";

          |SI c43 ! "";
               |HAZ FinCab;
          |FINSI;

          aSerie = #100#52;
          nLin = 0;
          |HAZ Contador;

          aAlfa = "|NC";
          aAlfa = #100#aAlfa#;
          nCampo = aAlfa;
          nCampo = nCampo - 1;

          |PARA i = 0; |SI i [ nCampo; |HACIENDO i = i + 1;
               #10i = #100i;
          |SIGUE;
          #10#52 = aSerie;
          #10#0 = nNume;
          |GRABA 020#10b;
     |FINSI;

     c43 = #100#43;
     c1 = #100#1;
     c3 = #100#3;
     c6 = #100#6;
     c8 = #100#8;
     c70 = #100#70;
     c68 = #100#68;
     c5 = #100#5;
     c17 = #100#7;
     c32 = #100#32;
     c12 = #100#12;
     c14 = #100#14;
|FPRC;

|PROCESO TmpLin;
     #200#29 = #2#2;
     #200#0 = #2#3;
     #200#1 = #2#4;
     |LEE 000#200.=;
     |SI FSalida = 0;
          |HAZ Crea_agifa071;
     |FINSI;
|FPRC;

|DEFBUCLE TmpLin;
     #2#1;
     ;
     #1#0, 1;
     #1#0, 999999999;
     ;
     NULL, TmpLin;
|FIN;

|PROCESO TmpCab;
     #100#52 = #1#1;
     #100#0 = #1#2;
     |LEE 101#100.=;
     |SI FSalida = 0;
          |HAZ Crea_agifa010;
          #100#58 = "S";   || EXPORTADO
          #100#59 = ~;
          |GRABA 020#100.a;
     |FINSI;
     |BUCLE TmpLin;
|FPRC;

|DEFBUCLE Tmp;
     #1#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, TmpCab;
|FIN;
컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
|PROCESO GrabaTmp;
     |SI nNueva = 1;
          nNueva = 0;
          nOrdenCab = nOrdenCab + 1;
          nOrdenLin = 0;
          #1#0 = nOrdenCab;
          #1#1 = #71#29;
          #1#2 = #71#0;
          |GRABA 020#1n;
     |FINSI;
     nOrdenLin = nOrdenLin + 1;
     #2#0 = nOrdenCab;
     #2#1 = nOrdenLin;
     #2#2 = #71#29;
     #2#3 = #71#0;
     #2#4 = #71#1;
     |GRABA 020#2n;
|FPRC;

|PROCESO GrabaTmp0;
     |SI c43 ! #10#43; |O c1 ! #10#1; |O c3 ! #10#3;
               |O c6 ! #10#6; |O c8 ! #10#8;
               |O c70 ! #10#70; |O c68 ! #10#68;
               |O c5 ! #10#5; |O c17 ! #10#7;
               |O c32 ! #10#32; |O c12 ! #10#12;
               |O c14 ! #10#14;
          nNueva = 1;
     |FINSI;
     |HAZ GrabaTmp;
     c43 = #10#43;
     c1 = #10#1;
     c3 = #10#3;
     c6 = #10#6;
     c8 = #10#8;
     c70 = #10#70;
     c68 = #10#68;
     c5 = #10#5;
     c17 = #10#7;
     c32 = #10#32;
     c12 = #10#12;
     c14 = #10#14;
|FPRC;

|DEFBUCLE agifa071;
     #71#1;
     ;
     #10#52, #10#0, 001;
     #10#52, #10#0, 999;
     ;
     NULL, NULL, NULL, NULL, NULL, GrabaTmp;
     a1,a2,a3,a4,a5,a6,a7,a8;
|FIN;

|PROCESO Agifa010;
     nNueva = 1;
     |BUCLE agifa071;
|FPRC;

|DEFBUCLE Agifa010;
     #10#3;
     58;
     "      ", #0#4, #0#0, #0#2, "N";
     "zzzzzz", #0#5, #0#1, #0#3, "N";
     be;
     NULL, NULL, NULL, NULL, NULL, Agifa010;
     b1,b2,b3,b4,b5,b6,b7,b8,b9,b10,b11,b12;
|FIN;

|PROCESO LeeCab;
     #10#52 = #71#29;
     #10#0 = #71#0;
     |LEE 000#10=;
     |SI #10#1 < #0#4; |O #10#1 > #0#5; |O FSalida ! 0;
          |ERROR;
     |FINSI;
     |SI #10#58 = "S";   ||Exportado
          |ERROR;
     |FINSI;
|FPRC;

|DEFBUCLE agifa071_2;
     #71#1;
     ;
     #0#0, #0#2, 001;
     #0#1, #0#3, 999;
     ;
     NULL, LeeCab, NULL, NULL, NULL, GrabaTmp0;
     a1,a2,a3,a4,a5,a6,a7,a8, b1,b2,b3,b4,b5,b6,b7,b8,b9,b10,b11,b12;
|FIN;
컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
|PROCESO DescargaDef;
     |SI FSalida ! 0;
          |MENAV "0000Error al cargar Def...";
     |FINSI;
     |SI aAlfa71 ! ""; |DESCARGA_DEF #agifa071@; |FINSI;
     |SI aAlfa10 ! ""; |DESCARGA_DEF #agifa010@; |FINSI;
|FPRC;

|PROCESO Tipo3; |TIPO 3;
     |DDEF aDef;
     aAlfa10 = aDef + "agifa010";
     |CARGA_DEF aAlfa10, agifa010@;
     |SI FSalida ! 0; aAlfa10 = ""; |HAZ DescargaDef; |ACABA; |FINSI;

     aAlfa71 = aDef + "agifa071";
     |CARGA_DEF aAlfa71, agifa071@;
     |SI FSalida ! 0; aAlfa71 = ""; |HAZ DescargaDef; |ACABA; |FINSI;

     |DBASE aPath;
     aPath = aPath + "fich/";
     |PATH_DAT #3 aPath;

     |DFICE aAlfa;
     aAlfa = aAlfa % -205;

     |ABRE #3;
     #3#0 = #0#8;
     |LEE 000#3=;
     |SI FSalida = 0; |Y aAlfa ! #0#8;
          |HAZ CreaTempo; aTempo1 = Tempo; |NOME_DAT #1 aTempo1;|DELINDEX #1;
          |HAZ CreaTempo; aTempo2 = Tempo; |NOME_DAT #2 aTempo2;|DELINDEX #2;
          |ABRE #1;
          |ABRE #2;
          |SI #0#6 = "S"; |Y #0#7 = "S";
               |ABRE #10;
               |BUCLE agifa071_2;
               |CIERRA #10;
          |SINO;
               |BUCLE Agifa010;
          |FINSI;
          |CIERRA #2;
          |CIERRA #1;
          c43 = "";
          l2 = "";
          aPath = aPath + (("00000" + #0#8) % -105) + "/";
          |PATH_DAT #10 aPath;
          |PATH_DAT #71 aPath;
          |PATH_DAT #65 aPath;
          |PATH_DAT #17 aPath;
          |PATH_DAT #19 aPath;
          |PATH_DAT #24 aPath;
          |PATH_DAT #27 aPath;

          |ABRE #agifa010@;
          |ABRE #agifa071@;
          |ABRE #10;
          |ABRE #71;

          |BUCLE Tmp;
          |SI c43 ! ""; |HAZ FinCab; |FINSI;

          |CIERRA #agifa071@;
          |CIERRA #agifa010@;
          |CIERRA #71;
          |CIERRA #10;

          |DELINDEX #1; Tempo = aTempo1; |HAZ BoraTempo;
          |DELINDEX #2; Tempo = aTempo2; |HAZ BoraTempo;
     |SINO;
          |SI FSalida = 0;
               |MENAV "0000Esta empresa es la actual...";
          |SINO;
               |MENAV "0000La empresa indicada NO EXISTE...";
          |FINSI;
     |FINSI;
     |CIERRA #3;
     |HAZ DescargaDef;
|FPRC;
