|FICHEROS;
     diefa306 #0;   || este def
     agifa010 #1;   || albaranes venta (C)
     diefaz03 #3;   || Auxiliar
|FIN;

|VARIABLES;
     nModo = 0;
     nCuan = 0;
     fFech = @;
     nRep  = 0;
     aAlfa = "";
|FIN;

|PROGRAMA;
     |CLS;
     |CABEZA "Adj. Albaranes-Agencia Trans.";

     |ABRE #3;
     #3#0 = "";
     #3#1 = 0;
     |LEE 000#3=;
     |SI FSalida = 0;
          aAlfa = "0000Proceso ocupado por el usuario:[" + #3#2 + "]";
          |MENAV aAlfa;
          |ACABA;
     |FINSI;
     |CIERRA #3;

     |CONFI "2400NDesea limpiar la informacion almacenada (S/N)";
     |SI FSalida = 0;
          |DELINDEX #0;
          |DELINDEX #3;
     |FINSI;

     |ABRE #3;
     #3#0 = "";
     #3#1 = 0;
     #3#2 = Usuario % 810;
     |GRABA 020#3n;
     |CIERRA #3;

     fFech = ~;
     |ENTLINEAL #1#0 , 1 , 1 , 20 , 1 , pro1 , pro2;

     |ABRE #3;
     #3#0 = "";
     #3#1 = 0;
     |BORRA 020#3c;
     |CIERRA #3;
|FPRO;

|PROCESO pro1;
     #0#0 = 1;
|FPRC;

|PROCESO pro2;
     #0#0 = 99;
|FPRC;

|PROCESO Albaran;   |TIPO 0;
     |ABRE #3;
     #3#0 = #0#1;
     #3#1 = #0#2;
     |LEE 000#3=;
     |SI FSalida ! 0;
          nModo = (FEntrada / 100) ! 0;
          |SI nModo = 1;
               |ABRE #1;
               |DDEFECTO #1;
               #1#52 = #0#1;
               #1#0  = #0#2;
               |LEE 000#1=;
               |SI FSalida = 0;
                    #0#3 = #1#1;   || fecha
                    #0#6 = #1#3;   || cliente
                    #0#7 = #1#78;  || importe
                    |SI #1#88 ! 0;
                         #0#8 = #1#88;  || cod agencia
                         #0#5 = #1#9;   || nombre agencia
                         |PINTA #0#5;
                         |PINTA #0#8;
                    |FINSI;
                    |PINTA #0#3;
                    |PINTA #0#6;
                    |PINTA #0#7;
               |FINSI;
               |LIBERA #1;
               |CIERRA #1;
          |FINSI;
     |SINO;
          |MENAV "0000Albaran repetido en adjudicacion...";
          |ERROR;
     |FINSI;
     |CIERRA #3;
|FPRC;

|PROCESO Actualiza; |TIPO 2;
     nModo = (FEntrada / 100) ! 0;
     nCuan = 0 +. 1;
     |SI nModo = 1;
          |ABRE #1;
          |DDEFECTO #1;
          #1#52 = #0#1;
          #1#0  = #0#2;
          |LEE 101#1=;
          |SI FSalida = 0;
               #1#88 = #0#8;
               #1#9  = #0#5;
               aAlfa = #1#57; |QBF aAlfa;
               |SI aAlfa ! ""; |Y aAlfa ! "01.01.1000";
                    aAlfa = "0000NSe Modifica Albaran con fecha de reparto " + aAlfa + " S/N:";
                    |CONFI aAlfa;
                    |SI FSalida ! 0;
                         |ERROR; |ACABA;
                    |FINSI;
               |FINSI;
               #1#57 = #0#4;
               |GRABA 020#1a;
          |FINSI;
          |LIBERA #1;
          |CIERRA #1;
          |ABRE #3;
          #3#0 = #1#52;
          #3#1 = #1#0;
          |GRABA 020#3n;
          |CIERRA #3;
     |FINSI;
     |SI nModo = 2; |Y nCuan = -1;
          |ABRE #1;
          |DDEFECTO #1;
          #1#52 = #0#1;
          #1#0  = #0#2;
          |LEE 101#1=;
          |SI FSalida = 0;
               #1#88 = 0;
               #1#9  = "";
               #1#57 = "01.01.1000";
               |GRABA 020#1a;
          |FINSI;
          |LIBERA #1;
          |CIERRA #1;
          |ABRE #3;
          #3#0 = #1#52;
          #3#1 = #1#0;
          |BORRA 020#3c;
          |CIERRA #3;
     |FINSI;
     |SI nModo = 2; |Y nCuan = 1;
          |ABRE #1;
          |DDEFECTO #1;
          #1#52 = #0#1;
          #1#0  = #0#2;
          |LEE 101#1=;
          |SI FSalida = 0;
               #1#88 = #0#8;
               #1#9  = #0#5;
               #1#57 = #0#4;
               |GRABA 020#1a;
          |FINSI;
          |LIBERA #1;
          |CIERRA #1;
          |ABRE #3;
          #3#0 = #1#52;
          #3#1 = #1#0;
          |GRABA 020#3n;
          |CIERRA #3;
     |FINSI;
     |SI nModo = 3;
          |ABRE #1;
          |DDEFECTO #1;
          #1#52 = #0#1;
          #1#0  = #0#2;
          |LEE 101#1=;
          |SI FSalida = 0;
               #1#88 = 0;
               #1#9  = "";
               #1#57 = "01.01.1000";
               |GRABA 020#1a;
          |FINSI;
          |LIBERA #1;
          |CIERRA #1;
          |ABRE #3;
          #3#0 = #1#52;
          #3#1 = #1#0;
          |BORRA 020#3c;
          |CIERRA #3;
     |FINSI;
|FPRC;

|PROCESO Gef; |TIPO 0;
     fFech = #0#4;
|FPRC;

|PROCESO Ref; |TIPO 7;
     |SI nModo = 1;
          #0#4 = fFech;
          |PINTA #0#4;
     |FINSI;
|FPRC;
|PROCESO Gre; |TIPO 0;
     nRep = #0#8;
|FPRC;

|PROCESO Rre; |TIPO 7;
     |SI nModo = 1;
          #0#8 = nRep;
          |PINTA #0#8;
     |FINSI;
|FPRC;
