|FICHEROS;
     malpe710 #710;
     malpe711 #711;
     malpe712 #712;
|FIN;

|VARIABLES;
     aCadena = "";
     aAlfa = "";
     nBoton1_1 = 0;
     nBoton1_2 = 0;
     nVali = 0;
     nCancel = 0;
     nEncuentra = 0;

     fFecha = @;

     nVentana = 0;
     nPanta = 0;

     nNumDef = 0;
     nCampo = 0;
     nCampoD = 0;
     nCampoH = 0;
     nCampoDesde = 0;
     nCampoHasta = 0;

     &enModo = 0;
     &enDefinicion = 0;
     &enLinea = 0;
|FIN;

|PROCESO Tipo0w1;
     #712#0 = #711#1;
     |LEE 000#712=;
     |SI FSalida = 0;
          #711#8 = #712#1;
     |FINSI;
|FPRC;

|PROCESO malpe710w7;
     |SI #710#0 ! enDefinicion;
          nEncuentra = 1;
     |FINSI;
|FPRC;

|DEFBUCLE malpe710w7;
     #710#2;
     ;
     #711#7, 00001;
     #711#7, 99999;
     ;
     NULL, malpe710w7;
|FIN;

|PROCESO Tipo0w7;
     nEncuentra = 0;
     |BUCLE malpe710w7;
     |SI nEncuentra = 1;
          |MENAV "    Fichero existente. Elija otro nombre."
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO malpe711w3;
     |SI #711#3 = "T";
          |ESTADO_CONTROL nBoton1_1, "DISABLE";
     |SINO;
          |ESTADO_CONTROL nBoton1_1, "ENABLE";
     |FINSI;
|FPRC;

|PROCESO DameDefLibre;
     |PARA nCampo = 1; |SI nCampo [ 99999; |HACIENDO nCampo = nCampo + 1;
           #710#0 = nCampo;
           |LEE 000#710=;
           |SI FSalida ! 0;
               nNumDef = nCampo;
               |SAL;
           |FINSI;
     |SIGUE;
|FPRC;

|PROCESO GrabaDefinicion;
     #710#0 = #711#0;
     |LEE 101#710=;
     |SI FSalida ! 0;
         |DDEFECTO #710;
         #710#0 = #711#0;
         |GRABA 020#710b;
     |FINSI;

     |PARA nCampoD = 1;  |SI nCampoD [ nCampoHasta;  |HACIENDO nCampoD = nCampoD + 1;
           nCampoH   = nCampoD;
           #710nCampoD = #711nCampoH;
     |SIGUE;
     |GRABA 020#710a;
     |LIBERA #710;
|FPRC;

|PROCESO Boton1_1;
     aAlfa = "|X000";
     aAlfa = #711#aAlfa#;
     nVali = aAlfa;
     |ESTADO_CONTROL nVali, "HIDE";

     aAlfa = "|X001";
     aAlfa = #711#aAlfa#;
     nCancel = aAlfa;
     |ESTADO_CONTROL nCancel, "HIDE";

     |SI #711#3 ! "G"; |ACABA; |FINSI;

     enLinea = #711#0;
     |SUB_EJECUTA_NP "malpe713";

     |ESTADO_CONTROL nCancel, "SHOW";
     |ESTADO_CONTROL nVali, "SHOW";
|FPRC;

|PROCESO Boton1_2;
     aAlfa = "|X000";
     aAlfa = #711#aAlfa#;
     nVali = aAlfa;
     |ESTADO_CONTROL nVali, "HIDE";

     aAlfa = "|X001";
     aAlfa = #711#aAlfa#;
     nCancel = aAlfa;
     |ESTADO_CONTROL nCancel, "HIDE";

     enLinea = #711#0;
     |SUB_EJECUTA_NP "malpe714";

     |ESTADO_CONTROL nCancel, "SHOW";
     |ESTADO_CONTROL nVali, "SHOW";
|FPRC;

|PROGRAMA;
     |VENTANA 0501, 2799, -1, 1, "";
     nVentana = FSalida;

     aCadena     ="|NC";
     aAlfa       = #710#aCadena#;
     nCampoHasta = aAlfa;
     nCampoHasta = nCampoHasta - 1;

     |SI enModo = 1;
          nNumDef = 1;
          |HAZ DameDefLibre;
          #711#0 = nNumDef;
          fFecha = ~;
          aAlfa = fFecha;
          aAlfa = aAlfa % -104;
          #711#4 = aAlfa;
          #711#5 = aAlfa;
     |FINSI;

     |SI enModo = 2;
          #710#0 = enDefinicion;
          |LEE 101#710=;
          |SI FSalida ! 0;
               |DDEFECTO #710;
               #710#0 = enDefinicion;
          |FINSI;

          |PARA nCampoD = 0;  |SI nCampoD [ nCampoHasta;   |HACIENDO nCampoD = nCampoD + 1;
               nCampoH = nCampoD;
               #711nCampoH = #710nCampoD;
          |SIGUE;

          |C_M #711#0, 1, "N";
     |FINSI;

     |PINPA #0#711; nPanta = FSalida;
     |PINDA #0#711;

     |CONTROL_SIMPLE nPanta, "BOTON,{{197}} Ver... ", 1326, 1335, Boton1_1;
     nBoton1_1 = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,{{197}} Ver... ", 1556, 1565, Boton1_2;
     nBoton1_2 = FSalida;


     |SI #711#3 = "T";
          |ESTADO_CONTROL nBoton1_1, "DISABLE";
     |SINO;
          |ESTADO_CONTROL nBoton1_1, "ENABLE";
     |FINSI;

     |ENDATOS #1#711;

     |SI FSalida = 0;
          |HAZ GrabaDefinicion;
     |FINSI;

     |FINVENTANA nVentana;

     |PULSATECLA;
|FPRO;

|PROCESO Tipo80Fmalpe711; |TIPO 80;
     |ABRE #710;
     |ABRE #711;
     |ABRE #712;

     FSalida = 2799;
|FPRC;

|PROCESO Tipo90Fmalpe711; |TIPO 90;
     |CIERRA #712;
     |CIERRA #711;
     |CIERRA #710;
|FPRC;
