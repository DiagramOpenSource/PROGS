|FICHEROS;
     primz003 #0;
     prim0001 #1;
     prim0002 #2;
     primw003 #3;
     primw004 #4;
     primw005 #5;
     agifa019 #19;
     :dscarter/dscam023 #23;
     agifa024 #24;
     agifa025 #25;
     :dscarter/dscam038 #38;
     agifa073 #73;
     agifa104 #104;
     agifa128 #128;
     agifa142 #142;
     agifa321 #321;
     agifa324 #324;

     dsm00113 #113;
|FIN;

|VARIABLES;
     &enTiva = 0;
     &efFiva = @;
     &eaDiva = "";
     &enIva = 0;
     &enRec = 0;
     &nImpo = 0;
     &eaTag = "";
     &Tempo = "";
     &aDivisa = "";
     &aMoneda = "";
     &enForma = 0;
     nNume = 0;
     nSal = 0;
     aIP = "";
     aAlfa = "";
     aPath = "";
     nBoton1 = 0;
     aDes1 = "";
     aDes2 = "";
     aOrg = "";
     nHandle1 = 0;
     nHandle2 = 0;
     aTmp3 = "";
     aTmp4 = "";
     aTmp5 = "";
     aTmp = "";
     aCar = "";
     aFin = "";
     nCar = 0;
     aAlfa1 = "";
     aAlfa2 = "";
     aAlfa3 = "";
     aAlfa4 = "";
     aTipo = "";
     aRetorno = "";
     aDto = "";
     aEmpre = "";
     aPathFic = "";
     aBloqueado = "";
     nEmpresa = 0;
     aArti = "";
     nError = 0;
     nEan = 0;
     nCalc = 0;

     &fed_dt = @;   || Fecha....
     &art_dt = "";  || Articulo.
     &cli_dt = "";  || Cliente..
     &fam_dt = "";  || Familia.
     &iva_dt = 0;   || Tipo Iva.
     &dtos_2 = 0;   || Descuento 2.
     &dto_pt = 0;   || Descuento Ptas.
     &dtos_1 = 0;   || Descuento 1.
     &pvp_ve = 0;   || Precio Venta.
     &tarifa = 0;   || Tarifa Cliente.
     &quedto = 0;   || 1=cli./art, 2=cli./familia, 3=cli./subfam.
     &gru_dt = "";  || Grupo Cliente.
     &uni_dt = 0;
     &enMensaje = 0;
     &aParam    = "";
     &nPromo = 0;
     &eaSerie = "";
     &enDirEnt = 0;
     &enAlmacen = 0;
     &enInterno = 0;

     &enDescuento1 = 0;
     &enDescuento2 = 0;
     &eaTComision = "";
     &eaCliente = "";
     &eaRepre = "";
     &eaArticulo = "";
     &efFecha = @;
     &enComision = 0;
||     &eaCliente = "";
||     &efFecha = @;
     &enImpCliPed = 0;
     fmes = "";
     mes = 0;
|FIN;

|PROCESO ActCli;
     #24#0 = #104#4;
     |LEE 101#24=;
     |SI FSalida = 0;
          |SI #19#194 = 2;
               nImpo = ((((#73#44 * #73#6) < #73#8) < #73#29) < #104#5) < #104#17;
          |SINO;
               nImpo = ((((#73#5 * #73#6) < #73#8) < #73#29) < #104#5) < #104#17;
          |FINSI;
          |SI #142#115 = "S";
               nImpo = nImpo < #104#6;
          |FINSI;
          ||nImpo = nImpo * nForma;
          fmes = #104#1 % A402;
          mes = fmes - 1;
          mes = mes * 3;
          mes = mes + 114;
          #24mes = #24mes   + nImpo;     || VENTA.
          #24#150 = #24#150 + nImpo;
          |GRABA 020#24a;

          eaCliente = #agifa104#4;
          efFecha = #agifa104#1;
          enImpCliPed = nImpo;
          |HAZ AcumulaCli;
     |FINSI;
|FPRC;

|PROCESO BuscaComi;
     enDescuento1 = #73#8;
     enDescuento2 = #73#29;
     eaTComision  = #73#17;
     eaCliente    = #104#4;
     eaRepre      = #104#7;
     eaArticulo   = #73#2;
     efFecha      = #104#1;
     eaSerie      = #73#28;
     enDirEnt = #104#57;
     enAlmacen = #73#3;
     |HAZ Comisiones;
|FPRC;

|PROCESO BuscaPrecio;
     #73#20 = #104#4;
     fed_dt = #104#1;   || Fecha....
     art_dt = #73#2;   || Articulo.
     cli_dt = #73#20;  || Cliente..
     fam_dt = #73#18;  || Familia.
     iva_dt = #19#30;  || Tipo Iva.
     dtos_2 = 0;      || Descuento 2.
     dto_pt = 0;      || Descuento Ptas.
     dtos_1 = 0;      || Descuento 1.
     pvp_ve = 0;      || Precio Venta.
     tarifa = #24#39;  || Tarifa Cliente....
     gru_dt = #24#158; || Grupo Cliente.
     uni_dt = #73#5;

     nPromo = #104#56;
     enMensaje = 0;
     aParam = "";
     eaSerie = #73#28;
     enDirEnt = #104#57;
     enAlmacen = #73#3;

     enInterno = tarifa;
     |HAZ calcdtos;    || Calculo Dtos.....

     #4#5 = pvp_ve - dto_pt;  || Precio Venta. sin iva.
     #4#6 = dtos_1;  || 1¦ Dto.
     #4#7 = dtos_2;  || 2§ Dto.
|FPRC;

|PROCESO Cartera;
     |SI #104#25 ] #38#4; |Y #104#25 [ #38#5;
          |DBASS aAlfa;
          aEmpre = ("00000" + #38#5) % -105;
          aPathFic = aAlfa + "dscarter/fich/" + aEmpre + "/";
          |PATH_DAT #23#aPathFic#;
          |ABRE #23;
          #23#0 = #104#4;
          |LEE 000#23=;
          |SI FSalida = 0;
               aBloqueado = #23#9;
          |FINSI;
          |CIERRA #23;
     |FINSI;
|FPRC;

|DEFBUCLE Cartera;
     #38#1;
     2;
     nEmpresa, 001, "P";
     nEmpresa, 999, "P";
     ;
     NULL, Cartera;
|FIN;

|PROCESO MiraRiesgo;
     aBloqueado = "N";

     |DBASS aAlfa;
     aPathFic = aAlfa + "dscarter/fich/";
     |PATH_DAT #38 aPathFic;

     |DFICE aAlfa;
     aAlfa = aAlfa % -205;
     nEmpresa = aAlfa;
     |BUCLE Cartera;
|FPRC;

|PROCESO Min;
     #5#1 = #4#2;
|FPRC;

|PROCESO Max;
     #5#1 = #4#2;
|FPRC;

|PROCESO BusEAN;
     nEan = nEan + 1;
     aArti = #5#0;
|FPRC;

|DEFBUCLE BusEAN;
     #5#2;
     ;
     #4#2;
     #4#2;
     ;
     NULL, BusEAN;
|FIN;

|PROCESO CreaLinPed;
     |SI #4#8 = "01.01.0000";
          |SI #3#1 = "01.01.0000";
               #4#8 = #3#2;
          |SINO;
               #4#8 = #3#1;
          |FINSI;
     |FINSI;

     nEan = 0;
     |BUCLE BusEAN;

     |SI nEan > 0;
          |SI nEan > 1;
               aAlfa = "0000Hay mas de un articulo con el EAN:" + #4#2;
               |MENAV aAlfa;
               |ABRE #5;
               |CONSULTA_F_CLAVE #2#5, Min, Max;
               |SI FSalida = 0;
                    aArti = #5#0;
               |FINSI;
               |CIERRA #5;
          |FINSI;
     |SINO;
          aAlfa = "0000No se encuentra el EAN:" + #4#2;
          |MENAV aAlfa;
          |ABRE #19;
       |ET OtraMas;
          |CONSULTA_CLAVE #1#19;
          |SI FSalida = 0;
               aArti = #19#0;
          |SINO;
               |MENAV "0000Debe seleccionar un articulo";
               |VETE OtraMas;
          |FINSI;
          |CIERRA #19;
     |FINSI;

     |ABRE #19;
     #19#0 = aArti;
     |LEE 121#19=;
     |SI FSalida = 0;
          #19#110 = #4#2;
          |GRABA 020#19a;

          |ABRE #5;
          #5#0 = #19#0;
          |LEE 121#5=;
          |SI FSalida = 0;
               #5#1 = #19#110;
               |GRABA 020#5a;
          |FINSI;
          |CIERRA #5;
     |FINSI;
     |CIERRA #19;

     |DDEFECTO #73;
     #73#28 = #104#25;
     #73#0 = #104#0;
     #73#1 = #4#1;
     #73#2 = #19#0;
     #73#3 = 1;
     #73#4 = #19#195;   ||#19#1; || #4#3;
     #73#5 = #4#4;
     #73#6 = #4#5;
     #73#8 = #4#6;

     |DDEFECTO #128;
     #128#0 = #104#18;
     |LEE 000#128=;
     #73#10 = #128#1;

     #73#13 = #4#8;
     #73#14 = #19#30;
     #73#16 = #24#25;
     #73#17 = #104#16;
     #73#18 = #19#2;
     #73#19 = #104#18;
     #73#20 = #104#4;
     #73#23 = #104#5;
     #73#24 = #104#17;
     #73#25 = #104#6;
     #73#26 = #104#9;
     #73#29 = #4#7;
     #73#38 = #73#6;
     #73#40 = #73#6;

     |SI #4#5 = 0;
          |HAZ BuscaPrecio;

          #73#6 = #4#5;
          #73#38 = #73#6;
          #73#40 = #73#6;
          #73#8 = #4#6;
          #73#29 = #4#7;

          |HAZ BuscaComi;
          #73#8 = enDescuento1;
          #73#10 = enComision;
     |FINSI;

     |GRABA 020#73n;
     enForma = 1;
     |HAZ AcumulaPV;

     |HAZ ActCli;
|FPRC;

|DEFBUCLE CreaLinPed;
     #4#1;
     ;
     #3#0, 001;
     #3#0, 999;
     ;
     NULL, CreaLinPed;
|FIN;

|PROCESO CreaCabPed;
     |HAZ MiraRiesgo;
     #104#1 = #3#1;
     #104#2 = #3#2;
     #104#3 = #3#10;
     #104#4 = #24#0;
     #104#5 = #3#4;
     #104#6 = #3#5;
     #104#7 = #24#25;
     #104#8 = #24#2;
     #104#9 = #24#20;
     #104#10 = aBloqueado;
     #104#14 = #24#5;
     |DDEFECTO #25;
     #25#0 = #104#7;
     |LEE 000#25=;
     #104#16 = #25#7;
     #104#18 = #24#4;
     #104#19 = #24#2;
     #104#20 = #24#6;
     #104#21 = #24#7;
     #104#22 = #24#47;
     #104#23 = #24#41;
     #104#35 = #24#192; || #3#3;
     |DDEFECTO #324;
     #324#0 = #104#35;
     |LEE 000#324=;
     #104#36 = #324#2;
     #104#37 = #24#193;
     #104#40 = #321#9;
     |DDEFECTO #324;
     #324#0 = #104#40;
     |LEE 000#324=;
     #104#41 = #324#2;
     #104#53 = #3#12;
     #104#54 = #3#10;
     #104#59 = #24#180;
     #104#60 = #24#35;
     #104#61 = #24#208;
     #104#64 = #24#15;
|FPRC;

|PROCESO AltaCli;
     nError = 0;

     |PUSHV 0501 2380;
     |SELECT #1#2;
     #2#9 = #3#11;
     |PINPA #0#2;
     |PINDA #0#2;
  |ET Otra;
     |ENCAMPO #0#2;
     |SI FSalida ! 0;
          aAlfa = "0000NCancelar el pedido: " + #3#0;
          |CONFI aAlfa;
          |SI FSalida ! 0; |VETE Otra; |FINSI;
          nError = 1;
     |SINO;
          |LEE 101#2=;
          |PINDA #0#2;
          nSal = FSalida;
          |ENDATOS #1#2;
          |SI FSalida = 0;
               |SI nSal = 0;
                    |GRABA 020#2a;
               |SINO;
                    |GRABA 020#2n;
               |FINSI;
          |SINO;
               aAlfa = "0000NCancelar el pedido: " + #3#0;
               |CONFI aAlfa;
               |SI FSalida ! 0; |VETE Otra; |FINSI;
               nError = 1;
          |FINSI;
          |LIBERA #2;
     |FINSI;
     |POPV;
|FPRC;

|PROCESO CalcConIva;
     |ABRE #dsm00113;
     #dsm00113#0 = #agifa104#4;
     #dsm00113#1 = #agifa073#14;
     |LEE 000#dsm00113.=;
     |SI FSalida = 0;
        nCalc = #agifa073#11 % #dsm00113#3;
        |SI #agifa024#47 = "S";
           nCalc = nCalc + (#agifa073#11 % #dsm00113#4);
        |FINSI;
     |SINO;
        enTiva = #agifa073#14;
        efFiva = #agifa104#1;
        nCalc = #agifa073#11 % enIva;
        |SI #agifa024#47 = "S";
             nCalc = nCalc + (#agifa073#11 % enRec);
        |FINSI;
     |FINSI;
     |CIERRA #dsm00113;

     nImpo = nImpo + (#agifa073#11 + nCalc);
|FPRC;

|DEFBUCLE CalcConIva;
     #agifa073#1;
     ;
     #agifa104#25, #agifa104#0,001;
     #agifa104#25, #agifa104#0,999;
     ;
     NULL,CalcConIva;
|FIN;

|PROCESO CreaPedido;
     |SELECT #2#2;
     #2#7 = #3#7;
     |LEE 000#2=;
     |SI FSalida ! 0;
          |HAZ AltaCli;
          |SI nError ! 0; |ACABA; |FINSI;
     |FINSI;

     |DDEFECTO #24;
     #24#0 = #2#0;
     |LEE 000#24=;

     |DDEFECTO #104;
     #104#25 = #1#16;
     #104#0 = #3#0;
     |GRABA 000#104b;
     |SI FSalida = 0;
          |HAZ CreaCabPed;
          |BUCLE CreaLinPed;
          aDivisa = #104#35;
          aMoneda = #104#37;
          |HAZ CalCabAgifa104;
          |GRABA 020#104a;
          |LIBERA #104;

          nImpo = 0; |BUCLE CalcConIva;
          eaTag = "PE";  |HAZ Riesgos;

     |SINO;
          aAlfa = "0000Ya existe el pedido: " + #104#25 +"/" + (("000000" + #104#0) % -106);
          |MENAV aAlfa;
     |FINSI;
|FPRC;

|DEFBUCLE CreaPedido;
     #3#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, CreaPedido;
|FIN;

|PROCESO Fecha;
     |QBF aAlfa;
     |SI aAlfa = "";
          aAlfa = "01.01.0000";
     |SINO;
          aAlfa = (aAlfa % 702) +"."+ (aAlfa % 502) +"."+ (aAlfa % 104);
     |FINSI;
|FPRC;

|PROCESO LineaLin;
     aAlfa = aAlfa1 % 108;
     #4#0 = aAlfa; ||Pedido

     aAlfa = aAlfa1 % 905;
     #4#1 = aAlfa; ||Linea

     aAlfa = aAlfa1 % 1417;
     #4#2 = aAlfa; ||EAN

     aAlfa = aAlfa1 % 15235;
     #4#3 = aAlfa; ||Des.

     aAlfa = aAlfa2 % 715;    ||615
     nNume = aAlfa;
     nNume = nNume / 1000;
     #4#4 = nNume; ||Canti

     aAlfa = aAlfa2 % 9615;
     nNume = aAlfa;
     nNume = nNume / 1000;
     #4#5 = nNume; ||Precio

     aAlfa = aAlfa2 % 14908;
     nNume = aAlfa;
     nNume = nNume / 1000;
     #4#6 = nNume; ||Dto1

     aAlfa = aAlfa2 % 18008;
     nNume = aAlfa;
     nNume = nNume / 1000;
     #4#7 = nNume; ||Dto2

     aAlfa = aAlfa4;
     |HAZ Fecha;
     #4#8 = aAlfa; ||F.Entrega
     |GRABA 020#4n;
     |SI FSalida ! 0;
          |LEE 000#4=
          |SI FSalida = 0;
               aAlfa = "0000Linea repetida en linped.txt, clave:" + #4#0 +"/"+ #4#1;
          |SINO;
               aAlfa = "0000Error al grabar TMP cabped.txt";
          |FINSI;
          |MENAV aAlfa;
     |FINSI;
|FPRC;

|PROCESO CaracterLin;
     |SI aCar = aFin;
          |HAZ LineaLin;
          nCar = 1;
          aAlfa1 = "";
          aAlfa2 = "";
          aAlfa3 = "";
          aAlfa4 = "";
     |SINO;
          |SI aCar ! aRetorno;
               |SI nCar [ 250;
                    aAlfa1 = aAlfa1 + aCar;
               |SINO;
                    |SI nCar [ 500;
                         aAlfa2 = aAlfa2 + aCar;
                    |SINO;
                         |SI nCar [ 750;
                              aAlfa3 = aAlfa3 + aCar;
                         |FINSI;
                    |FINSI;
               |FINSI;
               |SI nCar ] 990; |Y aCar [ 1001;
                    aAlfa4 = aAlfa4 + aCar;
               |FINSI;
               nCar = nCar + 1;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO ImportaLin;
     aAlfa1 = "";
     aAlfa2 = "";
     aAlfa3 = "";
     aAlfa4 = "";
     nCar = 1;
     |XLEE nHandle2, 200, aTmp;
     |PARA; |SI FSalida > 0; |HACIENDO;
          |PARA; |SI aTmp ! ""; |HACIENDO;
               aCar = aTmp % 101;
               |SI aTmp = aCar;
                    aTmp = ""; ||con un caracter falla: "aTmp = aTmp % 2"
               |SINO;
                    aTmp = aTmp % 2;
               |FINSI;
               |HAZ CaracterLin;
          |SIGUE;
          |XLEE nHandle2, 250, aTmp;
     |SIGUE;
|FPRC;

|PROCESO DtoCab;
     aAlfa = "";
     aTipo = aAlfa2 % 11703;
     |SI aTipo = aDto; aAlfa = aAlfa2 % 12008; |ACABA; |FINSI;
     aTipo = aAlfa2 % 14803;
     |SI aTipo = aDto; aAlfa = aAlfa2 % 15108; |ACABA; |FINSI;
     aTipo = aAlfa2 % 17903;
     |SI aTipo = aDto; aAlfa = aAlfa2 % 18208; |ACABA; |FINSI;
     aTipo = aAlfa2 % 21003;
     |SI aTipo = aDto; aAlfa = aAlfa2 % 21308; |ACABA; |FINSI;
     aTipo = aAlfa2 % 24103;
     |SI aTipo = aDto; aAlfa = (aAlfa2 % -107)+(aAlfa3 % 101); |ACABA; |FINSI;
     nNume = aAlfa;
     nNume = aAlfa % 1000;
     aAlfa = nNume;
|FPRC;

|PROCESO LineaCab;
     aAlfa = aAlfa1 % 0108;
     #3#0 = aAlfa;  ||Pedido

     aAlfa = aAlfa1 % 3012;
     |HAZ Fecha;
     #3#1 = aAlfa;  ||Fecha

     aAlfa = aAlfa1 % 5412;
     |HAZ Fecha;
     #3#2 = aAlfa;  ||F.Entrega

     aAlfa = aAlfa2 % 10903;
     #3#3 = aAlfa;  ||Moneda

     aDto = "TD"; |HAZ DtoCab;
     #3#4 = aAlfa;  ||Dto

     aDto = "EAB"; |HAZ DtoCab;
     #3#5 = aAlfa;  ||DtoPP

     #3#6 = 0;  ||DtoAcu

     aAlfa = aAlfa1 % 16417;
     #3#7 = aAlfa;  ||Cliente

     aAlfa = aAlfa1 % 1515;
     #3#8 = aAlfa;  ||Obs

     aAlfa = aAlfa4;
     #3#9 = aAlfa;  ||Obs

     aAlfa = aAlfa1 % 1515;
     #3#10 = aAlfa;  ||Su ped.

     aAlfa = (aAlfa1 % -102) + (aAlfa2 % 115);
     #3#11 = aAlfa; ||Vendedor

     #3#12 = aAlfa4;
     |GRABA 020#3n;
     |SI FSalida ! 0;
          |LEE 000#3=
          |SI FSalida = 0;
               aAlfa = "0000Linea repetida en cabped.txt, clave:" + #3#0;
          |SINO;
               aAlfa = "0000Error al grabar TMP cabped.txt";
          |FINSI;
          |MENAV aAlfa;
     |FINSI;
|FPRC;

|PROCESO CaracterCab;
     |SI aCar = aFin;
          |HAZ LineaCab;
          nCar = 1;
          aAlfa1 = "";
          aAlfa2 = "";
          aAlfa3 = "";
          aAlfa4 = "";
     |SINO;
          |SI aCar ! aRetorno;
               |SI nCar [ 250;
                    aAlfa1 = aAlfa1 + aCar;
               |SINO;
                    |SI nCar [ 500;
                         aAlfa2 = aAlfa2 + aCar;
                    |SINO;
                         |SI nCar [ 750;
                              aAlfa3 = aAlfa3 + aCar;
                         |FINSI;
                    |FINSI;
               |FINSI;
               |SI nCar ] 1012; |Y aCar [ 1028;
                    aAlfa4 = aAlfa4 + aCar;
               |FINSI;
               nCar = nCar + 1;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO ImportaCab;
     aAlfa1 = "";
     aAlfa2 = "";
     aAlfa3 = "";
     aAlfa4 = "";
     nCar = 1;
     |XLEE nHandle1, 200, aTmp;
     |PARA; |SI FSalida > 0; |HACIENDO;
          |PARA; |SI aTmp ! ""; |HACIENDO;
               aCar = aTmp % 101;
               |SI aTmp = aCar;
                    aTmp = ""; ||con un caracter falla: "aTmp = aTmp % 2"
               |SINO;
                    aTmp = aTmp % 2;
               |FINSI;
               |HAZ CaracterCab;
          |SIGUE;
          |XLEE nHandle1, 250, aTmp;
     |SIGUE;
|FPRC;

|PROCESO Llena5;
     #5#0 = #19#0;
     #5#1 = #19#110;
     #5#2 = #19#1;
     |GRABA 020#5n;
|FPRC;

|DEFBUCLE Llena5;
     #19#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, Llena5;
|FIN;

|PROCESO Tipo3; |TIPO 3;
     aFin = &10;  aRetorno = &13;

     aPath = #0#1; |QBF aPath;
     aAlfa = aPath % -101;
     |SI aAlfa ! "\"; |Y aAlfa ! "/";
          aPath = aPath + "\";
     |FINSI;
     #0#1 = aPath;
     aIP = "";
     |IP_REMOTO aIP;
     |SI aIP = "";
          aDes1 = aPath + "cabped.txt";
          aDes2 = aPath + "linped.txt";
     |SINO;
          |DFICE aAlfa;
          aOrg = aPath + "cabped.txt";
          aDes1 = aAlfa + "_cabped.txt";
          |RECIBE_FICHERO aOrg, aDes1;
          aOrg = aPath + "linped.txt";
          aDes2 = aAlfa + "_linped.txt";
          |RECIBE_FICHERO aOrg, aDes2;
     |FINSI;

     |XABRE "B", aDes1, nHandle1;
     |SI FSalida ] 0;
          |XABRE "B", aDes2, nHandle2;
          |SI FSalida ] 0;
               |HAZ CreaTempo; |NOME_DAT #3#Tempo#; |DELINDEX #3; aTmp3 = Tempo;
               |HAZ CreaTempo; |NOME_DAT #4#Tempo#; |DELINDEX #4; aTmp4 = Tempo;
               |HAZ CreaTempo; |NOME_DAT #5#Tempo#; |DELINDEX #5; aTmp5 = Tempo;
               |ABRE #3;
               |ABRE #4;
               |ABRE #1; |LEE 000#1p; |CIERRA #1;
               |ABRE #321; |LEE 000#321p; |CIERRA #321;
               |ABRE #5;
               |BUCLE Llena5;
               |CIERRA #5;
               |HAZ ImportaCab;
               |HAZ ImportaLin;
               |CIERRA #4;
               |CIERRA #3;

               |ABRE #104;
               |ABRE #73;
               |ABRE #24;
               |ABRE #2;
               |ABRE #128;
               |ABRE #25;
               |ABRE #324;
               |BUCLE CreaPedido;
               |CIERRA #324;
               |CIERRA #25;
               |CIERRA #128;
               |CIERRA #2;
               |CIERRA #24;
               |CIERRA #73;
               |CIERRA #104;

               |DELINDEX #3; Tempo = aTmp3; |HAZ BoraTempo;
               |DELINDEX #4; Tempo = aTmp4; |HAZ BoraTempo;
               |DELINDEX #5; Tempo = aTmp5; |HAZ BoraTempo;
               |XCIERRA nHandle2;
          |SINO;
               |MENAV "0000Error al abrir 'linped.txt'";
          |FINSI;
          |XCIERRA nHandle1;
     |SINO;
          |MENAV "0000Error al abrir 'cabped.txt'";
     |FINSI;
     |FBORRA aDes1;
     |FBORRA aDes2;
     |SI aIP ! "";
          aAlfa =  aPath + "cabped.txt"; |RFBORRA aAlfa;
          aAlfa =  aPath + "linped.txt"; |RFBORRA aAlfa;
     |FINSI;
|FPRC;

|PROCESO Browser; |TIPO 0;
     |SI FSalida ! 10; |ACABA; |FINSI;

     aPath = "D*.*";

     |BROWSE aPath;

     aPath = aPath % 2; |QBF aPath;
     |SI aPath = "D"; aPath = ""; |FINSI;

     |QBF aPath;
     |SI aPath = "";
          |ERROR;
     |SINO;
          #0#1 = aPath;
          |PINTA #0#1;
     |FINSI;
|FPRC;

|PROCESO Boton1;
     |PTEC 824;
|FPRC;

|PROGRAMA;
     |ABRE #142; |LEE 000#142p; |CIERRA #142;

     |CLS;
     |CONTROL_SIMPLE 0, "BOTON,Carpeta", 1007, 1014, Boton1;
     nBoton1 = FSalida;
     |ABRE #0;
     |LEE 101#0p;
     |SI FSalida ! 0; |GRABA 020#0b; |FINSI;
     |PINPA #0#0;
     |PINDA #0#0;
     |ENDATOS #1#0;
     |SI FSalida = 0;
          |GRABA 020#0a;
     |FINSI;
     |CIERRA #0;
     |FIN_CONTROL_WINDOWS nBoton1;
     |PULSATECLA;
|FPRO;
