|FICHEROS;
     goresmat #0;
     gomantca #1;
     agifa019 #2;
     agifa076 #3;
     agifa080 #4;
     goresmal #5,MANTE;
     goresmbl #6,MANTE;
     goresmtl #7;
     goresmcl #8,MANTE;
     gomatljn #9;
     gomatcab #10;
     gopartia #11;
     goalbpa1 #12;
     gopre001 #13;
     goparame #100;
|FIN;
------------------------------------------------------------------------------
|VARIABLES;
     dVacio         = "                   ";
     hVacio         = "zzzzzzzzzzzzzzzzzzz";

     &cCodMonBase   = "";
     &cDesMonBase   = "";
     &cCodMonTrab   = "";
     &cDesMonTrab   = "";

     &nDeci_PBase   = 0;
     &nDeci_TBase   = 0;
     &nDeci_PTrab   = 0;
     &nDeci_TTrab   = 0;
     &nDeci_TotVis  = 0;
     &nEurComTrab   = 0;
     &nEurComBase   = 0;
     &nEurVenTrab   = 0;
     &nEurVenBase   = 0;

     &cSerie        = "";
     cFichero1      = "";
     cFichero2      = "";
     cFichero3      = "";
     cFichero4      = "";
     cParam         = "";
     cMoneda        = "";

     &nExped        = 0;

     nLinea         = 0;
     nLinAr         = 0;
     nLinAl         = 0;
     nHay           = 0;
     nGraba         = 0;
     nPreVen        = 0;

     {-1}menu       = "";
     menu1          = "2400";
     menu2          = "1";
     menu3          = "Impreso: ";
     menu4          = "RA";
     menu5          = " Resumen Articulos ";
     menu6          = " Resumen Albaranes ";

|FIN;
------------------------------------------------------------------------------
|PROCESO Entrada; |TIPO 7;
     |ABRE #1;
     |LEE 000#1p;
     |SI FSalida = 0;
          #0#0 = #1#0;        ||Expediente.
          #0#2 = #1#1;        ||No. Expe.
     |FINSI;
     |LEE 000#1u;
     |SI FSalida = 0;
          #0#1 = #1#0;        ||Expediente.
          #0#3 = #1#1;        ||No. Expe.
     |FINSI;
     |CIERRA #1;
     |ABRE #2;
     |LEE 000#2p;
     |SI FSalida = 0;
          #0#4  = #2#0;        ||Articulo.
          #0#10 = #2#1;        ||Descripcion Articulo.
     |FINSI;
     |LEE 000#2u;
     |SI FSalida = 0;
          #0#5  = #2#0;        ||Trabajador.
          #0#11 = #2#1;        ||Nombre Trabajador.
     |FINSI;
     |CIERRA #2;
     |PINDA #0#0;
|FPRC;
------------------------------------------------------------------------------
|PROCESO Moneda;
     |SI #1#6 ! cMoneda;
          nHay = 1;
     |FINSI;
|FPRC;
------------------------------------------------------------------------------
|DEFBUCLE Moneda;
     #1#1;
     ;
     #0#0, #0#2;
     #0#1, #0#3;
     ;
     NULL, Moneda;
|FIN;
------------------------------------------------------------------------------
|PROCESO MiraMoneda;
     nHay = 0;
     |ABRE #1;
     #1#0 = #0#0;
     #1#1 = #0#2;
     |LEE 000#1=;

     |SI FSalida = 0;
          cMoneda = #1#6;
          cCodMonTrab = #1#6;
          |HAZ MonBas;
          |QBF cMoneda;
          |BUCLE Moneda;
     |FINSI;

     |CIERRA #1;
|FPRC;
------------------------------------------------------------------------------
|PROCESO Grabate;

     cCodMonTrab = #11#21;
     |HAZ MonBas;
     nHay = 1;

     |DDEFECTO #6;
     |SELECT #2#6;
     #6#0 = #11#4;                                ||Serie Albaran.
     #6#1 = #11#5;                                ||Numero Albaran.
     #6#2 = #11#20;                               ||Codigo Articulo.
     |LEE 000#6=;

     |SI FSalida ! 0; |O #6#8 ! #11#11; |O #6#9 ! #11#12;
                         || Si Precio Coste o Precio Venta distinto
                         || han de salir en distintas lineas
          nLinAl = nLinAl + 1;
          #6#3  = nLinAl;                         ||Linea.
          |GRABA 020#6c;
          #6#5 = 0;      || inicializo cantidad, que no me acumule
     |FINSI;

     |ABRE #12;
     #12#0 = #11#4;
     #12#1 = #11#5;
     |LEE 000#12=;

     |SI FSalida = 0;
          #6#4  = #12#7;                          ||Su Albaran.
     |FINSI;

     |CIERRA #12;
     #6#5  = #6#5 + #11#10;                       ||Cantidad.
     #6#6  = #11#2;                               ||Serie Exp.
     #6#7  = #11#3;                               ||N§. Exped.

     #6#8  = #11#11;          || Precio Venta
     #6#9  = #11#12;          || Precio Coste
     |GRABA 020#6a;

     |ABRE #13;
     #13#0 = #11#0;                               ||Serie Pres.
     #13#1 = #11#1;                               ||Numero Pres.
     |LEE 000#13=;
     |CIERRA #13;

     |SI #13#18 = "P";
          #7#0 = #7#0 + #11#14;                   ||Importe Coste.
          #7#1 = #7#1 + #11#13;                   ||Importe Venta.
     |FINSI;

     |SI #13#18 = "A";
          #7#2 = #7#2 + #11#14;
          #7#3 = #7#3 + #11#13;
     |FINSI;

     |SI #13#18 = "E";
          #7#2 = #7#4 + #11#14;
          #7#3 = #7#5 + #11#13;
     |FINSI;

     #7#6 = #7#0+#7#2+#7#4;
     #7#7 = #7#1+#7#3+#7#5;
     |GRABA 020#7a;

     |DDEFECTO #5
     |SELECT #2#5;
     #5#0 = #11#2;                                ||Serie expediente.
     #5#1 = #11#3;                                ||Numero Expediente.
     |LEE 000#5=;

     |SI FSalida ! 0;
          nLinea = nLinea + 1;
          #5#2  = nLinea;                         ||Linea.
          |GRABA 020#5c;
     |FINSI;

     #5#3  = #5#3 + #11#14;                       ||Costo Mon. Trabaj.
     #5#4  = #5#4 + #11#13;                       ||Venta Mon. Trabaj.
     #5#5  = #13#2;                               ||Cliente.
     |GRABA 020#5a;

     |DDEFECTO #8;
     |SELECT #2#8;
     #8#2 = #11#20;                               ||Codigo Articulo.
     |LEE 000#8=;

     |SI FSalida ! 0;
          nLinAr = nLinAr + 1;
          #8#0  = nLinAr;                         ||Linea.
          |GRABA 020#8c;
     |FINSI;

     #8#3  = #11#30;                              ||Descripcion Art.
     #8#4  = #8#4 + #11#14;                       ||Imp. Costo Trab.
     #8#5  = #8#5 + #11#13;                       ||Imp. Venta Trab.
     #8#1  = #8#1 + #11#10;                       ||Cantidad.

     |SI #13#18 = "P";
          #8#6 = #8#6 + #11#14;
          #8#9 = #8#9 + #11#13;                   ||Precio Venta.
     |FINSI;

     |SI #13#18 = "A";
          #8#7 = #8#7 + #11#14;
          #8#10= #8#10+ #11#13;
     |FINSI;

     |SI #13#18 = "E";
          #8#8 = #8#8 + #11#14;
          #8#11= #8#11+ #11#13;
     |FINSI;

     |GRABA 020#8a;

|FPRC;
------------------------------------------------------------------------------
|DEFBUCLE Material;
     #11#3;
     20,29;
     #0#0, #0#2, "     ",      1, dVacio, dVacio, #0#4, "M";
     #0#1, #0#3, "zzzzz", 999999, hVacio, hVacio, #0#5, "M";
     ;
     NULL, Grabate;
|FIN;
------------------------------------------------------------------------------
|PROCESO Ejecuta; |TIPO 3;

     |SI nHay = 1;
          |MENAV "0000HAY EXPEDIENTES CON DISTINTA MONEDA DE TRABAJO";
          |ACABA;
     |FINSI;

     nLinea = 0;
     nLinAl = 0;
     nLinAr = 0;
     |TEMPO cFichero1;
     |TEMPO cFichero2;
     |TEMPO cFichero3;
     |TEMPO cFichero4;
     |QBF cFichero1;
     |QBF cFichero2;
     |QBF cFichero3;
     |QBF cFichero4;
     cFichero1 = cFichero1 % -108;
     cFichero2 = cFichero2 % -108;
     cFichero3 = cFichero3 % -108;
     cFichero4 = cFichero4 % -108;
     |NOME_DAT #5 cFichero1;
     |NOME_DAT #6 cFichero2;
     |NOME_DAT #7 cFichero3;
     |NOME_DAT #8 cFichero4;
     |ABRE #5;
     |ABRE #6;
     |ABRE #7;
     |ABRE #8;
     |ABRE #10;
     |LEE 000#7p;
     |DDEFECTO #7;
     |GRABA 020#7c;
     |BUCLE Material;

     |SI #0#8 = "P";
          |PINPA #0#6;
          |PINDA #0#6;
          |ATRI R;
          |PINTA 2219, cMoneda;
          |ATRI 0;
          |PINTA 1207, #7#0;
          |ENTLINEAL #1#5, 3, 4, 10, 1, MinResCat, MaxResCat;
     |SINO;
          |HAZ ImpMat;
     |FINSI;

     |CIERRA #5;
     |CIERRA #6;
     |CIERRA #7;
     |CIERRA #8;
     |DELINDEX #5;
     |DELINDEX #6;
     |DELINDEX #7;
     |DELINDEX #8;

|FPRC;
------------------------------------------------------------------------------
|PROCESO MinResCat;
     #5#0 = "     ";
     #5#1 = 1;
     #5#2 = 1;
|FPRC;
------------------------------------------------------------------------------
|PROCESO MaxResCat;
     #5#0 = "zzzzz";
     #5#1 = 999999;
     #5#2 = 999;
|FPRC;
------------------------------------------------------------------------------
|PROCESO Imprimete;
     |PRINT;
|FPRC;
------------------------------------------------------------------------------
|DEFBUCLE ImpExp;
     #5#1;
     ;
     "     ", 1, 1;
     "zzzzz",999999, 999;
     ;
     NULL, Imprimete;
|FIN;
------------------------------------------------------------------------------
|DEFBUCLE ImpArt;
     #8#1;
     ;
        1, " "*20;
     9999, "z"*20;
     ;
     NULL, Imprimete;
|FIN;
------------------------------------------------------------------------------
|DEFBUCLE ImpAlb;
     #6#2;
     ;
     "     ",      1, " "*20;
     "zzzzz", 999999, "z"*20;
     ;
     NULL, Imprimete;
|FIN;
------------------------------------------------------------------------------
|PROCESO ImpMat;
     |SUB_EJECUTA_NP "goinform;IMPgoresmat";
     |INFOR "goresmat";
     |BUCLE ImpExp;
     |PIEINF;
     |FININF;
     |SUB_EJECUTA_NP "goinform;ENVgoresmat";

     |SUB_EJECUTA_NP "goinform;IMPgoresmar";
     |INFOR "goresmar";
     |BUCLE ImpArt;
     |PIEINF;
     |FININF;
     |SUB_EJECUTA_NP "goinform;ENVgoresmar";

     |SUB_EJECUTA_NP "goinform;IMPgoresmal";
     |INFOR "goresmal";
     |BUCLE ImpAlb;
     |PIEINF;
     |FININF;
     |SUB_EJECUTA_NP "goinform;ENVgoresmal";
     |SUB_EJECUTA_NP "goinform;RUNMATERIALES";
|FPRC;

|PROCESO Tipo13; |TIPO 13;
     |ABRE #100; |LEE 000#100p; |CIERRA #100;
     |SI #100#141 = "O";
          |ATRI R; |PINTA 1005, " O.T.       "; |ATRI 0;
     |FINSI;
|FPRC;
