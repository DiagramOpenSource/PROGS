|FICHEROS;
     vigz0001 #0;
     vigm0004 #1; ||  dsm00001 #1;
     vigm0005 #2; ||  dsm00002 #2;
     agifa019 #19;
     agifa065 #65;
     agifa142 #142;
|FIN;

|VARIABLES;
     aTipo = "CU";
     aDes = "";
     nLin = 0;
     nPrecio = 0;
     nConta = 0;
     aOrden = "";
     aArti = "";
     nLon = 0;

     aRuta = "";
     nBoton1 = 0;
     nHandle = 0;
     aFic = "";
     aAlfa = "";

     aDato     = "";
     nCampo    = 0;
     aLon      = "";
     i         = 0;
     nPos      = 0;
     aLetra    = "";
     aReg      = "";
     aSepara   = "|";
 {99}Valor     = "";
|FIN;

|PROCESO BuscaPrecio;
     |DDEFECTO #65;
     |ABRE #65;
     #65#0 = #19#0;
     #65#1 = "31.12.9999";
     |LEE 000#65];
     |SI FSalida = 0;
          |LEE 000#65a;
     |SINO;
          |LEE 000#65u;
     |FINSI;
     |PARA; |SI FSalida = 0; |HACIENDO;
          |SI #65#0 = #19#0; |Y #65#8 ! 0;
               nPrecio = #65#8;
               |SAL;
          |FINSI;
          |LEE 000#65a;
     |SIGUE;
     |CIERRA #65;
|FPRC;

|PROCESO RecalCab;
     #1#6 = nPrecio / #1#3;
     |GRABA 020#1a;
     |LIBERA #1;
     nPrecio = 0;
|FPRC;

|PROCESO RecalLin;
     #1#3 = #1#3 + #2#4;
     #1#8 = #1#8 + (#2#4 * #2#5);
     nPrecio = nPrecio + (#2#4 * #2#5);
|FPRC;

|DEFBUCLE Recalcula;
     #1#1;
     ;
     nConta;
     nConta;
     be;
     NULL, NULL, RecalLin, RecalCab;
|FIN;

|PROCESO SacaFecha;
     aAlfa = (aAlfa % 102) + "." + (aAlfa % 402) + "." + (aAlfa % 704);
|FPRC;

|PROCESO QuitaComa;
     aLon = aAlfa % A0;
     nLon = aLon;
     nLon = nLon - 2;
     nLon = 200 + nLon;
     aAlfa = aAlfa % nLon;
|FPRC;

|PROCESO BusArt;
     aAlfa = #19#127; |QBF aAlfa;
     |SI aAlfa = aOrden;
          aArti = #19#0;
          aDes = #19#1;
          |ERROR10;
     |FINSI;
|FPRC;

|DEFBUCLE BusArt;
     #19#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, BusArt;
|FIN;

|PROCESO Graba;
     |SI Valor1 = 1;     || Cabecera
          |BUCLE Recalcula;
          |ABRE #1;
          |LEE 000#1u;
          |SI FSalida = 0;
               nConta = #1#0 + 1;
          |SINO;
               nConta = 1;
          |FINSI;
          |DDEFECTO #1;
          #1#0 = nConta;
          aAlfa = Valor3; |HAZ QuitaComa;
          aOrden = aAlfa;
          aArti = "???";
          aDes = "";
          |BUCLE BusArt;
          #1#1 = aArti;
          #1#2 = aDes;
          aAlfa = Valor6; |HAZ SacaFecha;
          #1#4 = aAlfa;
          #1#5 = #0#3;
          |GRABA 020#1n;
          nLin = 0;
          |CIERRA #1;
          |PINTA 1618, #1#0;
     |FINSI;
     |SI Valor1 = 2;
          |ABRE #2;
          nLin = nLin + 1;
          |DDEFECTO #2;
          #2#0 = #1#0;
          #2#1 = nLin;
          aAlfa = Valor3; |HAZ QuitaComa;
          |DDEFECTO #19;
          |ABRE #19;
          #19#0 = aAlfa;
          |LEE 000#19=;
          |CIERRA #19;
          #2#2 = aAlfa;
          #2#3 = #19#1;
          #2#4 = Valor6;
          nPrecio = #19#9;
          ||HAZ BuscaPrecio;  segun tiquet 2534/739
          #2#5 = nPrecio;
          #2#6 = #0#2;
          |GRABA 020#2n;
          |CIERRA #2;
     |FINSI;
|FPRC;

|PROCESO Dato;
     nCampo = 1;
     IValor = Valor1<-;
     aLon = aDato % A0;
     aReg = "";
     |PARA i = 1; |SI i [ aLon; |HACIENDO i = i + 1;
          nPos = i * 100 + 1;
          aLetra = aDato % nPos;
          |SI aLetra = aSepara;
               Valor = aReg;
               aReg = "";
               nCampo = nCampo + 1;
               IValor = IValor + 1;
          |SINO;
               aReg = aReg + aLetra;
          |FINSI;
          |SI nCampo > 90; |SAL; |FINSI;     || Por si.... :)
     |SIGUE;
     Valor = aReg;
|FPRC;

|PROCESO Tipo3; |TIPO 3;
     |ABRE #142; |LEE 000#142p; |CIERRA #142;
     |ESTADO_CONTROL nBoton1, "HIDE";

     aAlfa = #0#1; |QBF aAlfa;
     |XABRE aTipo, aAlfa, nHandle;
     |SI FSalida ] 0;
          |XLEE nHandle, 250, aDato;
          |PARA; |SI FSalida > 0; |HACIENDO;
               |HAZ Dato;
               |HAZ Graba;
               |XLEE nHandle, 250, aDato;
          |SIGUE;
          |XCIERRA nHandle;
          |BUCLE Recalcula;
     |SINO;
          |MENAV "0000No puedo abrir el fichero";
     |FINSI;
|FPRC;
컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
|PROCESO Boton1;
     |PTEC 824;
|FPRC;

|PROCESO Browser;
     |SI FSalida ! 10; |ACABA; |FINSI;

     aRuta = #0#1;  |QBF aRuta;
     |SI aRuta = "";
         aRuta = "\*.*";
     |FINSI;
     aRuta = "F" + aRuta;

     |BROWSE aRuta;

     aRuta = aRuta % 299;
     #0#1 = aRuta;
     |PINTA #0#1;
     |QBF aRuta;
     |SI aRuta = ""; |ERROR; |FINSI;
|FPRC;

|PROCESO Quita; |TIPO 0;
     |ESTADO_CONTROL nBoton1, "DISABLE";
|FPRC;

|PROCESO Pone; |TIPO 7;
     |ESTADO_CONTROL nBoton1, "ENABLE";
|FPRC;

|PROGRAMA;
     |CONTROL_SIMPLE 0, "BOTON, Fichero", 919, 926, Boton1;
     nBoton1 = FSalida;
     |HAZ Quita;

     |ABRE #0;
     |LEE 101#0=;
     |SI FSalida ! 0; |GRABA 020#0b; |FINSI;

     |CLS;
     |PINPA #0#0;
     |PINDA #0#0;
     |ENDATOS #1#0;
     |SI FSalida = 0;
          |GRABA 020#0a;
     |FINSI;
     |CIERRA #0;

     |FIN_CONTROL_WINDOWS nBoton1;
     |PULSATECLA;
|FPRO;
