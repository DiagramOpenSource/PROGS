Procesos: ValoraStock, ConsultaPartida

|FICHEROS;
     agifa017 #17;
     agifa019 #19;
     dsm00024 #24;
     agifa142 #142;
     dsm00238 #238;
     agifa321;
     agifa324;
|FIN;

|VARIABLES;
     &nDeci_PreBase = 0;
     &nDeci_Base = 0;
     nEntero   = 0;
     nMts      = 0;
     nMulti    = 0;
     nTari     = 0;
     nValor    = 0;
|FIN;

|CAMPOS;
     #19#15  art_pser;  #17#42  alm_pser;
     #19#182 art_pser2; #17#44  alm_pser2;
     #19#16  art_prec;  #17#43  alm_prec;
     #19#183 art_prec2; #17#45  alm_prec2;
     #19#17  art_pval;  #17#4   alm_pval;
     #19#11  art_nece;  #17#50  alm_nece;
     #19#12  art_rese;  #17#8   alm_rese;
     #19#14  art_depo;  #17#10  alm_depo;
     #19#184 art_depo2; #17#46  alm_depo2;
     #19#13  art_fabr;  #17#9   alm_fabr;
     #19#10  art_vsto;  #17#3   alm_vsto;
     #19#7   art_mini;  #17#6   alm_mini;
     #19#8   art_maxi;  #17#7   alm_maxi;
     #19#6   art_tota;  #17#5   alm_tota;
     #19#185 art_tota2; #17#47  alm_tota2;
     #19#104 art_disp;  #17#39  alm_disp;
     #19#193 art_disp2; #17#48  alm_disp2;
     #19#9   art_pcm;   #17#69  alm_pcm;
     #24#9   par_ereal; #24#10  par_ereal2;
     #24#11  par_sreal; #24#12  par_sreal2;
     #24#18  par_prec2; #24#19  par_prec;
     #24#16  par_pser;  #24#17  par_pser2;
     #24#20  par_depo;  #24#21  par_depo2;
     #24#22  par_tota;  #24#23  par_tota2;
     #24#31  par_disp;  #24#32  par_disp2;
     #24#14  par_merma;
|FIN;

|PROCESO TruncaDeci; || Quita la parte decimal
     |SI nMts < 0; nMulti = -1; |SINO nMulti = 1; |FINSI;
     nMts = nMts * nMulti;

     nMts = (nMts / #19#192);
     nEntero = nMts ! 0;
     |SI nEntero > nMts;
          nMts = nEntero - 1;
     |SINO;
          nMts = nEntero;
     |FINSI;

     nMts = nMts * nMulti;
|FPRC;

|PROCESO Divide;
     |SI #142#117 = "N"; |ACABA; |FINSI;

     |DDEFECTO #238;
     |ABRE #238;
     #238#0 = #19#201;
     |LEE 000#238=;
     |CIERRA #238;

     |SI #238#8 = 0; |O #238#13 = 1;  |ACABA; |FINSI;

     nMts = art_pser2; |HAZ TruncaDeci; art_pser = nMts;
     nMts = art_prec2; |HAZ TruncaDeci; art_prec = nMts;
     nMts = art_depo2; |HAZ TruncaDeci; art_depo = nMts;
     nMts = art_tota2; |HAZ TruncaDeci; art_tota = nMts;
     nMts = art_disp2; |HAZ TruncaDeci; art_disp = nMts;

     nMts = alm_pser2; |HAZ TruncaDeci; alm_pser = nMts;
     nMts = alm_prec2; |HAZ TruncaDeci; alm_prec = nMts;
     nMts = alm_depo2; |HAZ TruncaDeci; alm_depo = nMts;
     nMts = alm_tota2; |HAZ TruncaDeci; alm_tota = nMts;
     nMts = alm_disp2; |HAZ TruncaDeci; alm_disp = nMts;

     nMts = par_ereal2; |HAZ TruncaDeci; par_ereal = nMts;
     nMts = par_sreal2; |HAZ TruncaDeci; par_sreal = nMts;
     nMts = par_pser2; |HAZ TruncaDeci; par_pser = nMts;
     nMts = par_prec2; |HAZ TruncaDeci; par_prec = nMts;
     nMts = par_depo2; |HAZ TruncaDeci; par_depo = nMts;
     nMts = par_tota2; |HAZ TruncaDeci; par_tota = nMts;
|FPRC;

|PROCESO Valora_Arti;
     art_nece = art_pser + art_mini - art_prec - art_disp;
     art_nece = art_nece - art_pval - art_fabr - art_rese;
     |SI art_nece < 0; art_nece = 0; |FINSI;

     |SI #19#196 = 1;
          nValor = art_tota - art_pval;
          |SI nValor > 0;
               art_vsto = nValor * art_pcm;
          |SINO;
               art_vsto = 0;
          |FINSI;
     |SINO;
          nValor = art_tota2;
          |SI nValor > 0;
               art_vsto = nValor * art_pcm;
          |SINO;
               art_vsto = 0;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Valora_Alma;
     alm_nece = alm_pser + alm_mini - alm_prec - alm_disp;
     alm_nece = alm_nece - alm_pval - alm_fabr - alm_rese;
     |SI alm_nece < 0; alm_nece = 0; |FINSI;

     alm_vsto = alm_tota - alm_pval;
     |SI alm_vsto > 0;
          |SI #agifa142#9 = "S";
               alm_vsto = alm_vsto * alm_pcm;
          |SINO;
               alm_vsto = alm_vsto * art_pcm;
          |FINSI;
     |SINO;
          alm_vsto = 0;
     |FINSI;
|FPRC;

|PROCESO Valora_Parti;
     par_tota = par_ereal - par_sreal;
     par_tota2 = par_ereal2 - par_sreal2;

     #24#13 = #24#23 / #24#22;                          ||Factor
     #24#15 = ((#24#10 - #24#14) * 100) / #24#10;       ||%Rend. Real
     |SI #142#9 = "S";
          #24#33 = #24#22 * alm_pcm;
     |SINO;
          #24#33 = #24#22 * art_pcm;
     |FINSI;
     #24#31 = #24#22;
     #24#32 = #24#23;
|FPRC;

|PROCESO Decima;
     |ABRE #agifa324;
     |ABRE #agifa321;
     |LEE 001#agifa321.p;
     #agifa324#0 = #agifa321#9; ||Igualo a moneda base
     |LEE 001#agifa324.=;
     nDeci_Base = #agifa324#7;  ||Decimales moneda base
     nDeci_PreBase = #agifa324#9;  ||Decimales precio de moneda base
     |CIERRA #agifa321;
     |CIERRA #agifa324;
|FPRC;

|PROCESO ValoraStock;
     |HAZ Decima;
     |HAZ Divide;
     |HAZ Valora_Arti;
     |HAZ Valora_Alma;
     |HAZ Valora_Parti;
|FPRC;
========================================================
