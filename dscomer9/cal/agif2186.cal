     VtoFactuVta, VtoFactuDir

|FICHEROS;
     agifa010 #10;
     agifa024 #24;
     agifa059 #59;
     agifa084 #84;
     agifa091 #91;
     dsm00130 #130;
     agifa133 #133;
     agifa134 #134;
     agifa142 #142;
     dscam045@ #45;
     dscam003@ #3;
     dscam004@ #4;
     dsw00090 #90;
|FIN;

|VARIABLES;
     fUltDiaMes = @;
     f = @;
     aMes1 = "";
     nDiasMes = 0;
     nNumFec = 0;
     nNumAlb = 0;
     nSwDir = 0;
     &enSwVipei = 0;
     &efFecAnt = @;
     &eaSerAnt = "";
     &enNumAnt = 0;
     aAlfa = "";
     aDirectorio = "";
     fFec = @;
     aSer = "";
     nNum = "";
     nVto = 0;
     nCamFPago = 0;
     fFecVto = @;
     fFechaFac = @;
     nMult = 0;
     nDia1 = 0;
     nDia2 = 0;
     nDia3 = 0;
     aCli = "";
     aPago = "";
     nMesNoPago = 0;
     aAno = "";
     aDFecha = "";
     aHFecha = "";
     fDFecha = @;
     fHFecha = @;
     fFecha = @;
     fFecha1 = @;
     aDia = "";
     aMes = "";
     nDiaPago = 0;
     nMesPago = 0;
     fAntes = @;
     nMesAnt = 0;
     nNume = 0;
     nDias = 0;
     nEmp = 0;
     aEmp = "";
|FIN;

|PROCESO VipeGraRec;
     |ABRE #90;
     #90#0 = aSer;
     #90#1 = nNum;
     #90#2 = #dscam003@#2;
     #90#3 = eaSerAnt;
     #90#4 = enNumAnt;
     #90#5 = #dscam003@#14;
     #90#6 = #dscam003@#15;
     #90#7 = fFec;
     #90#8 = fFecVto;
     #90#9 = #dscam003@#26;
     |GRABA 020#90n;
     |CIERRA #90;
|FPRC;

|PROCESO CtrlEmp;
     aAlfa = aDirectorio + "fich/" + (("00000" + #dscam045@#0) % -105) + "/";
     |PATH_DAT #dscam003@#aAlfa#;
     |PATH_DAT #dscam004@#aAlfa#;

     |ABRE #dscam003@;
     |ABRE #dscam004@;
     |SELECT #12#dscam003@;
     |DDEFECTO #dscam003@;
     #dscam003@#0 = eaSerAnt;
     #dscam003@#1 = enNumAnt;
     #dscam003@#2 = #133#1;
     |LEE 101#dscam003@.];
     |PARA; |SI FSalida = 0; |Y #dscam003@#0 = eaSerAnt;
               |Y #dscam003@#1 = enNumAnt; |Y #dscam003@#2 = #133#1; |HACIENDO;
          |SI #dscam003@#4 = efFecAnt;
               |SI enSwVipei = 0; |HAZ VipeGraRec; |FINSI;
               #dscam003@#0 = aSer;
               #dscam003@#1 = nNum;
               #dscam003@#3 = fFecVto;
               #dscam003@#4 = fFec;
               |GRABA 020#dscam003@.a;

               #dscam004@#17 = #dscam003@#40;
               #dscam004@#3 = 0;
               |LEE 101#dscam004@.];
               |PARA; |SI FSalida = 0; |Y #dscam004@#17 = #dscam003@#40; |HACIENDO;
                    #dscam004@#0 = #dscam003@#0;
                    #dscam004@#1 = #dscam003@#1;
                    #dscam004@#2 = #dscam003@#2;
                    |GRABA 020#dscam004@.a;
                    |LIBERA #dscam004@;
                    |LEE 101#dscam004@.s;
               |SIGUE;

               |SAL;
          |FINSI;
          |LIBERA #dscam003@;
          |LEE 101#dscam003@.s;
     |SIGUE;
     |CIERRA #dscam004@;
     |CIERRA #dscam003@;
|FPRC;

|DEFBUCLE CtrlEmp;
     #dscam045@#1002;
     2;
     nEmp, 001, "V";
     nEmp, 999, "V";
     ;
     NULL, CtrlEmp;
|FIN;

|PROCESO Recibo;
     |DBASS aDirectorio; |QBF aDirectorio;
     aDirectorio = aDirectorio + "dscarter/";
     aAlfa = aDirectorio + "def/dscam045.mas";
     |CARGA_DEF aAlfa, dscam045@;
     |SI FSalida = 0;
          aAlfa = aDirectorio + "fich/";
          |PATH_DAT #dscam045@#aAlfa#;
          aAlfa = aDirectorio + "def/dscam003.mas"; |CARGA_DEF aAlfa, dscam003@;
          aAlfa = aDirectorio + "def/dscam004.mas"; |CARGA_DEF aAlfa, dscam004@;
          |DFICE aAlfa;
          aEmp = aAlfa % -205;
          nEmp = aEmp;
          |BUCLE CtrlEmp;
          |DESCARGA_DEF #dscam004@;
          |DESCARGA_DEF #dscam003@;
          |DESCARGA_DEF #dscam045@;
     |FINSI;
|FPRC;

|PROCESO _dsm00130;
     aAno = fFecVto; aAno = aAno % -104;
     aDFecha = (("00" + #130#2) % -102) +"."+(("00" + #130#3) % -102) +"."+aAno;
     aHFecha = (("00" + #130#4) % -102) +"."+(("00" + #130#5) % -102) +"."+aAno;
     fDFecha = aDFecha;
     fHFecha = aHFecha;
     |SI fFecVto ] fDFecha; |Y fFecVto [ fHFecha;
          fFecha = fHFecha + 1;
          |HAZ DiasFijo;
          fFecVto = fFecha;
     |FINSI;
|FPRC;

|DEFBUCLE _dsm00130;
     #130#1002;
     ;
     aCli, 01;
     aCli, 99;
     ;
     NULL, _dsm00130;
|FIN;

|PROCESO Cambios;
     fFecha1 = fFecha;
     aDia = fFecha1 % 102;
     aMes = fFecha1 % 402;
     nDiaPago = aDia;
     nMesPago = aMes;
|FPRC;

|PROCESO DiasFijo;
     |SI nDia1 = 0;
          |VETE FinFijos;
     |FINSI;
     |HAZ Cambios;
     |SI nDiaPago > nDia1;
          |VETE  Fijos2;
     |FINSI;

  |ET Fijos1;
     |SI nDiaPago = nDia1;
          |VETE FinFijos;
     |FINSI;

     |HAZ Cambios;
     fAntes = fFecha;
     nMesAnt = nMesPago;

     fFecha = fFecha + 1;
     |HAZ Cambios;

     || SI el dia fijo=30 y el vto es en Febrero y la fp a 30 dias
     nNume = nDias $ 30;
     |SI nDia1 = 30; |Y nMesAnt = 2; |Y nMesPago = 3; |Y nNume = 0;
          fFecha = fAntes;
          |VETE FinFijos;
     |FINSI;

     |VETE Fijos1;

  |ET Fijos2;
     |SI nDia2 = 0; |O nDiaPago [ nDia1;
          |VETE Fijos1;
     |FINSI;
     |SI nDiaPago > nDia2;
          |VETE Fijos3;
     |FINSI;
     |SI nDiaPago = nDia2;
          |VETE FinFijos;
     |FINSI;

     |HAZ Cambios;
     fAntes = fFecha;
     nMesAnt = nMesPago;

     fFecha = fFecha + 1;
     |HAZ Cambios;

     || SI el dia fijo=30 y el vto es en Febrero y la fp a 30 dias
     nNume = nDias $ 30;
     |SI nDia2 = 30; |Y nMesAnt = 2; |Y nMesPago = 3; |Y nNume = 0;
          fFecha = fAntes;
          |VETE FinFijos;
     |FINSI;

     |VETE Fijos2;

  |ET Fijos3;
     |SI nDia3 = 0;|O nDiaPago > nDia3; |O nDiaPago [ nDia1; |O nDiaPago [ nDia2;
          |VETE Fijos1;
     |FINSI;
     |SI nDiaPago = nDia3;
         |VETE FinFijos;
     |FINSI;

     |HAZ Cambios;
     fAntes = fFecha;
     nMesAnt = nMesPago;

     fFecha = fFecha + 1;
     |HAZ Cambios;

     || SI el dia fijo=30 y el vto es en Febrero y la fp a 30 dias
     nNume = nDias $ 30;
     |SI nDia3 = 30; |Y nMesAnt = 2; |Y nMesPago = 3; |Y nNume = 0;
          fFecha = fAntes;
          |VETE FinFijos;
     |FINSI;

     |VETE Fijos3;

  |ET FinFijos;
     fFecha1 = fFecha;
|FPRC;

|PROCESO Vencimientos;
     |SI #91#5 = "N";
          nMult = 1;
     |SINO;
          nMult = 1000;
     |FINSI;
     nDias = #91nCamFPago / nMult;
     |SI nDias ! 0; |O nDia1 ! 0; |O nDia2 ! 0; |O nDia3 ! 0;
          fFecha = fFechaFac + nDias;
          |HAZ EsInforpor;
          |SI FSalida = 0;
               |HAZ FijosInforpor;
          |SINO;
               |HAZ DiasFijo; ||Estandar
          |FINSI;
          aMes = fFecha % 402;
          |SI aMes = nMesNoPago;          ||Si es mes de no pago...
               fFecha = fFecha + .001;
               fFechaFac = fFechaFac + .001;
               fFecha1 = fFecha;
          |FINSI;
          fFecVto = fFecha1;
     |SINO;
          aMes = fFechaFac % 402;
          |SI aMes = nMesNoPago;          ||Si es mes de no pago...
               fFechaFac = fFechaFac + .001;
          |FINSI;
          fFecVto = fFechaFac;
     |FINSI;

     |BUCLE _dsm00130;
|FPRC;

|PROCESO LeeAlbFactu;
     #10#52 = #59#15;
     #10#0 = #59#2;
     |LEE 000#10=;
     nNumFec = nNumFec + #10#1;
     nNumAlb = nNumAlb + 1;
|FPRC;

|PROCESO CambiaVto;
     nVto = 0;
     |ABRE #133;
     #133#4 = eaSerAnt;
     #133#0 = enNumAnt;
     #133#1 = 0;
     |LEE 101#133];
     |PARA; |SI FSalida = 0; |Y #133#0 = enNumAnt; |Y #133#4 = eaSerAnt; |HACIENDO;
          |BORRA 021#133a;
          |SALVA_DATOS #133;
          #133#4 = aSer;
          #133#0 = nNum;
          |SI fFec ! efFecAnt;
               nVto = nVto + 1;
               nCamFPago = nVto + 5;

               |SI #10#74 = "F"; |O nSwDir = 0;
                    fFechaFac = fFec;
               |SINO;
                    nNumFec = 0;
                    nNumAlb = 0;
                    |ABRE #10;
                    |BUCLELIN 2 LeeAlbFactu #134;
                    |CIERRA #10;
                    nNumFec = nNumFec / nNumAlb;
                    fFechaFac = nNumFec;
                    |SI nNumAlb = 0; fFechaFac = fFec; |FINSI;
               |FINSI;

               |HAZ Vencimientos;
               #133#3 = fFecVto;
               |SI nVto = 1; |Y fFecVto < fFec;
                    |MENAV "0000Algún vencimiento calculado es anterior a la fecha factura, revise las fechas de factura o albaranes.";
               |FINSI;
          |FINSI;
          |HAZ Recibo;
          |BORRA 000#133c;    || asegura que no exista
          |GRABA 020#133n;
          |REPON_DATOS #133;
          |LEE 101#133s;
     |SIGUE;
     |CIERRA #133;
|FPRC;

|PROCESO LeeCli;
     |DDEFECTO #24;
     |ABRE #24;
     #24#0 = aCli;
     |LEE 000#24=;
     |CIERRA #24;
|FPRC;

|PROCESO LeePago;
     |DDEFECTO #91;
     |ABRE #91;
     #91#0 = aPago;
     |LEE 000#91=;
     |CIERRA #91;
|FPRC;

|PROCESO VtoFactuDir;
     |SI #142#47 = "N";
          |ACABA;
     |FINSI;
     aSer = #84#48;
     nNum = #84#0;
     fFec = #84#1;

     nDia1 = #84#88;
     nDia2 = #84#89;
     nDia3 = #84#90;
     aCli = #84#3;
     |HAZ LeeCli;
     nMesNoPago = #24#21;
     aPago = #84#8;
     |HAZ LeePago;

     nSwDir = 0;
     |HAZ CambiaVto;
|FPRC;

|PROCESO VtoFactuVta;
     aSer = #134#49;
     nNum = #134#0;
     fFec = #134#1;

     nDia1 = #134#14;
     nDia2 = #134#15;
     nDia3 = #134#16;
     fFechaFac = #134#1;
     aCli = #134#2;
     |HAZ LeeCli;
     nMesNoPago = #24#21;
     aPago = #134#11;
     |HAZ LeePago;

     nSwDir = 1;
     |HAZ CambiaVto;
|FPRC;



|PROCESO DesgloFec;
     aDia = f % 102;
     aMes = f % 402;
     aAno = f % 704;
     nDiaPago = aDia;
     nMesPago = aMes;
|FPRC;

|PROCESO UlDiaMes;
     aAlfa = "01." + aMes + "." + aAno;
     aMes1 = aMes;
     nDiasMes = 0;
     |PARA f = aAlfa; |SI; |HACIENDO f = f + 1;
          |HAZ DesgloFec;
          |SI aMes1 ! aMes; |SAL; |FINSI;
          fUltDiaMes = f;
          nDiasMes = nDiasMes + 1;
     |SIGUE;
|FPRC;

/*
  Si el vto es el 30 febrero, 31 abril ... (dias imposibles)
  se pasa al 1 marzo (2 si bisiesto), 1 mayo...
*/
|PROCESO FijosInforpor;
     f = fFecha; |HAZ DesgloFec;
     |HAZ UlDiaMes;

     |SI nDia1 ! 0; |Y nDia1 > nDiasMes;
          nDias = nDia1 - nDiasMes;
          fFecha1 = fUltDiaMes + nDias;
     |SINO;
          |SI nDia2 ! 0; |Y nDia2 > nDiasMes;
               nDias = nDia2 - nDiasMes;
               fFecha1 = fUltDiaMes + nDias;
          |SINO;
               |SI nDia3 ! 0; |Y nDia3 > nDiasMes;
                    nDias = nDia3 - nDiasMes;
                    fFecha1 = fUltDiaMes + nDias;
               |SINO;
                    |HAZ DiasFijo;
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;
