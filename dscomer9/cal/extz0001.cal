|FICHEROS;
     extz0001 #0;
     dsm00003 #3;
     extm0004 #4;
     extm0005 #5;
     extm0006 #6;
     extm0011 #11;
     extm0013 #13;
     extm0014 #14;
     agifa024 #24;
     extw0003 #30;            ||Tmp Histo
     extz0032 #132,MANTE;     ||Pan Cambio Cli
     extz0033 #133,MANTE;     ||Pan Cambio Cli Fac
     extz0002 #102,MANTE;     ||Pan.Edificios
     extm0011@ #1;               ||Grid
|FIN;

|VARIABLES;
     &eaMensaje = "";
     &eaNumNuevo = "";
     &eaNumSerie = "";
     &eaCliente = "";
     &enDirEnt = 0;
     &eaCodEdi = "";
     &enModo = 0;
     &eaProg = "";
     &eaTitulo = "";
     &Tempo = "";
     VACIO = "____________________";
     aTmp11 = "";
     aAlfa = "";
     nPanta = 0;
     nBtnActualizar = 0;
     nBtnSalir = 0;
     nBtnAltaEdi = 0;
     nBtnCamCli = 0;
     nBtnCliFac = 0;
     nBtnBajaEdi = 0;
     nBtnAltaArt = 0;
     nBtnModiArt = 0;
     nBtnBajaArt = 0;
     nBtnConsArt = 0;
     nBtnHisto = 0;
     nBtnImpEti = 0;
     nGrid = -1;
     nGridHis = -1;
     nTree = -1;
 {-1}aGet = 0;
     aGet1 = 0;
     aGet2 = 0;
 {-1}sTree = 0;
     sT_nID = 0;
     sT_nOper = 0;
     sT_aData = "";
     aOrg = "";
     aFicTxt = "";
     nHandle = 0;
     nRango = 0;
     aIP = "";
     aDes = "";
     aFicTgz = "";
     aFicTar = "";
     aBass = "";
     nDir = 0;
     nNivel1 = 0;
     nNivel2 = 0;
  {1}aLocal = "";
     aCod = "";
     aEdi = "";
     aDato = "";
     nDIns = 0;
     nHIns = 0;
     aDEdi = "";
     aHEdi = "";
     aCodEdi = "";
     nCodDir = 0;
     nCampo = 0;
     i = 0;
     ii = 0;
     nLin = 0;
     aLon = "";
|FIN;

|PROCESO PrinHisto;
     |PRINT;
|FPRC;

|DEFBUCLE PrinHisto;
     #30#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, PrinHisto;
|FIN;

|PROCESO BtnImpHis;
     Impresora = "Crystal Reports";
     |IMPRE 0;
     |SI FSalida = 0;
          |INFOR "extzhis1";
          |SI FSalida = 0;
               |SALVA_FICHA #30;
               |BUCLE PrinHisto;
               |REPON_FICHA #30;
               |PIEINF;
               |FININF;
          |SINO;
               |MENAV "0000No existe informe 'extzhis1'...";
          |FINSI;
          |FINIMP;
     |FINSI;
|FPRC;

|PROCESO VerHis;
     |VENTANA 1101, 3199, -1, 17, "Historico";
     |HAZ PushVenModal;

     |CONTROL_SIMPLE 0, "BOTON,{{205}}Imprimir", 1202, 1212, BtnImpHis;

     nRango = 4 + 8 + 16 + 32;
     |LINEAL_SIMPLE #1#30, nRango, 1402, 2998, NULL, NULL, NULL;
     |PAUSA;
     nGridHis = FSalida;
     |DESTRUYECONTROL nGridHis;

     |HAZ PopVenModal;
|FPRC;

|PROCESO LineasRev;
     #13#0 = #14#0;
     #13#1 = #14#1;
     |LEE 020#13=;
     |SI FSalida ! 0; |ACABA; |FINSI;

     |PARA i = 0; |SI i [ 38; |HACIENDO i = i + 1;
          #30i = #13i;
     |SIGUE;
     |PARA i = 2; |SI i [ 26; |HACIENDO i = i + 1;
          ii = i +  39 - 2;
          #30ii = #14i;
     |SIGUE;

     |GRABA 020#30n;
|FPRC;

|DEFBUCLE LineasRev;
     #14#2;
     ;
     #extm0011@#0;
     #extm0011@#0;
     ;
     NULL, LineasRev;
|FIN;

|PROCESO BtnHisto;
     |LEE 000#extm0011@.=;
     |SI FSalida = 0;
          |HAZ CreaTempo; |NOME_DAT #30 Tempo; |DELINDEX #30;
          |ABRE #30;
          nLin = 0;
          |ABRE #13;
          |BUCLE LineasRev;
          |CIERRA #13;
          |LEE 000#30p;
          |SI FSalida = 0;
               |HAZ VerHis;
          |SINO;
               |MENAV "0000No existen registros...";
          |FINSI;
          |CIERRA #30;
          |HAZ BoraTempo; |DELINDEX #30;
     |FINSI;
|FPRC;

|PROCESO BtnImpEti;
     |ABRE #11;
     #11#0 = #extm0011@#0;
     |LEE 000#11=;
     |SI FSalida = 0;
          |IMPRE 0;
          |SI FSalida = 0;
               |INFOR "extz0001";
               |SI FSalida = 0;
                    |PRINT;
                    |PIEINF;
                    |FININF;
               |SINO;
                    |MENAV "0000Error en informe 'extz0001'...";
               |FINSI;
               |FINIMP;
          |FINSI;
     |FINSI;
     |CIERRA #11;
|FPRC;

|PROCESO EvtGrid;
     |SI FSalida = 1; |O FSalida = 2;
          |LEE 000#extm0011@.=;
     |FINSI;
     |SI FSalida = 4;
          |HAZ BtnModiArt;
     |FINSI;
|FPRC;

|PROCESO EvtTree;
     aAlfa = "0000El tree "+ FSalida  + " indica que se ha efectuado la operacion " + FEntrada + " y dice: " + Contenido;
     ||MENAV aAlfa;
     |SI FEntrada = 1; |ACABA; |FINSI;   || doble click

     aLon = Contenido % A0;
     |SI aLon = 40;      || Edificio
          aDato = Contenido % -134;
          aAlfa = aDato % 104; nCodDir = aAlfa;
          aAlfa = aDato % 520; aCodEdi = aAlfa;
     |SINO;
          |SI aLon = 30; || Dir. Entrega
               aDato = Contenido % -124;
               aAlfa = aDato % 104; nCodDir = aAlfa;
               aAlfa = aDato % 520; aCodEdi = aAlfa;
          |SINO; || = 6 || Instalaciones
               aCodEdi = VACIO;
               nCodDir = 0;
          |FINSI;
     |FINSI;

     |SI aCodEdi = VACIO; |Y nCodDir ! 0;
          nDIns = nCodDir;
          nHIns = nCodDir;
          aDEdi = "                    ";
          aHEdi = "zzzzzzzzzzzzzzzzzzzz";
          aCodEdi = "";
     |SINO;
          |ABRE #4;
          #4#0 = #0#0;
          #4#1 = nCodDir;
          #4#2 = aCodEdi;
          |LEE 000#4=;
          |SI FSalida = 0;
               nDIns = #4#1;
               nHIns = #4#1;
               aDEdi = #4#2;
               aHEdi = #4#2;
          |SINO;
               nDIns = 0001;
               nHIns = 9999;
               aDEdi = "                    ";
               aHEdi = "zzzzzzzzzzzzzzzzzzzz";
               nCodDir = 0;
          |FINSI;
          |CIERRA #4;
     |FINSI;

     |PULSATECLA;
     |REFRESCACONTROL nGrid;
|FPRC;

|PROCESO BtnAltaArt;
     eaNumSerie = "";
     eaCliente = #0#0;
     enDirEnt = nCodDir;
     |SI aCodEdi = VACIO;
          eaCodEdi = "";
     |SINO;
          eaCodEdi = aCodEdi;
     |FINSI;
     enModo = 1;
     eaTitulo = "Alta art¡culo a revisar";
     eaProg = "extz0003";
     |HAZ EjecutaProg;

     |SI eaNumNuevo ! "";
          |ABRE #11;
          #11#0 = eaNumNuevo;
          |LEE 000#11=;
          |SI FSalida = 0; |Y #11#1 = #0#0;
               |ESTADO_CONTROL nBtnModiArt, "ENABLE";
               |ESTADO_CONTROL nBtnBajaArt, "ENABLE";
               |ESTADO_CONTROL nBtnConsArt, "ENABLE";
               |ESTADO_CONTROL nBtnHisto, "ENABLE";
               |ESTADO_CONTROL nBtnImpEti, "ENABLE";
               |SALVA_DATOS #11;
               |REPON_DATOS #extm0011@;
               |GRABA 020#extm0011@.n;
               |LEE 000#extm0011@.=;
          |FINSI;
          |CIERRA #11;
          |REFRESCACONTROL nGrid;
     |FINSI;
|FPRC;

|PROCESO BtnModiArt;
     |LEE 000#extm0011@.=;
     |SI FSalida = 0;
          eaNumSerie = #extm0011@#0;
          eaNumNuevo = #extm0011@#0;
          enModo = 2;
          eaTitulo = "Modificaci¢n art¡culo a revisar";
          eaProg = "extz0003"; |HAZ EjecutaProg;

          |SELECT #1#extm0011@;
          |ABRE #11;
          #11#0 = eaNumNuevo;
          |LEE 000#11=;
          |SI FSalida = 0; |Y #11#1 = #0#0;
               |SALVA_DATOS #11;
               |REPON_DATOS #extm0011@;
               |BORRA 000#extm0011@.c;
               |GRABA 020#extm0011@.n;
          |FINSI;
          |SI eaNumSerie ! eaNumNuevo;
               #extm0011@#0 = eaNumSerie;
               |BORRA 000#extm0011@.c;
               #11#0 = eaNumSerie;
               |BORRA 000#11.c;

               #extm0011@#0 = eaNumNuevo;
               |LEE 000#extm0011@.=;
          |FINSI;
          |CIERRA #11;

          |SELECT #2#extm0011@;
          |LEE 000#extm0011@.=;
          |REFRESCACONTROL nGrid;
     |FINSI;
|FPRC;

|PROCESO BtnBajaArt;
     |LEE 000#extm0011@.=;
     |SI FSalida = 0;
          aAlfa = "0000N¨Borrar art¡culo con n§ serie:" + #extm0011@#0;
          |QBF aAlfa;
          aAlfa = aAlfa + "?";
          |CONFI aAlfa;
          |SI FSalida = 0;
               |BORRA 020#extm0011@.a;

               |ABRE #11;
               #11#0 = #extm0011@#0;
               |BORRA 020#11c;
               |CIERRA #11;

               |REFRESCACONTROL nGrid;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO BtnConsArt;
     |LEE 000#extm0011@.=;
     |SI FSalida = 0;
          eaNumSerie = #extm0011@#0;
          enModo = 4;
          eaTitulo = "Consulta art¡culo a revisar";
          eaProg = "extz0003"; |HAZ EjecutaProg;
     |FINSI;
|FPRC;

|PROCESO BorArtRevisar;
     |BORRA 020#11a;
|FPRC;

|DEFBUCLE BorArtRevisar;
     #11#2;
     ;
     #4#0, #4#1, #4#2, "    ", "                    ";
     #4#0, #4#1, #4#2, "zzzz", "zzzzzzzzzzzzzzzzzzzz";
     be;
     NULL, BorArtRevisar;
|FIN;

|PROCESO BorEdificio;
     |BUCLE BorArtRevisar;
     |BORRA 020#4a;
|FPRC;

|DEFBUCLE BorEdificio;
     #4#1;
     ;
     #0#0, nDIns, aDEdi;
     #0#0, nHIns, aHEdi;
     be;
     NULL, BorEdificio;
|FIN;

|PROCESO BtnBajaEdi;
     |SI nCodDir = 0;
          aAlfa = "0000N¨Borrar los art¡culos/edificios/instalaciones a revisar?";
     |SINO;
          |ABRE #3;
          #3#0 = #0#0;
          #3#1 = nCodDir;
          |LEE 000#3=;
          |SI FSalida ! 0; |DDEFECTO #3; |FINSI;
          |CIERRA #3;
          |SI aCodEdi = "";
               aAlfa = "0000N¨Borrar los art¡culos/edificios de la instalaci¢n:" + #3#25;
               |QBF aAlfa;
               aAlfa = aAlfa + "?";
          |SINO;
              aAlfa = "0000N¨Borrar todas los art¡culos y edificio:" + aCodEdi;
              |QBF aAlfa;
              aAlfa = aAlfa + " de la instalaci¢n:" + #3#25;
              |QBF aAlfa;
              aAlfa = aAlfa + "?";
          |FINSI;
     |FINSI;
     |CONFI aAlfa;
     |SI FSalida = 0;
          |BUCLE BorEdificio;
          |HAZ CreaArbol;
     |FINSI;
|FPRC;

|PROCESO Min3;
     #3#0 = #102#0;
     #3#1 = 1;
|FPRC;

|PROCESO Max3;
     #3#0 = #102#0;
     #3#1 = 9999;
|FPRC;

|PROCESO Dir2_66; |TIPO 66;
     |ABRE #3;
     #3#0 = #102#0;
     #3#1 = #102#1;
     |LEE 000#3];
     |CONSULTA_F_CLAVE #1#3, Min3, Max3;
     |SI FSalida = 0;
          #102#1 = #3#1; |PINTA #102#1;
     |SINO;
          |MENAV "0000No existe direcci¢n entrega...";
          |ERROR;
     |FINSI;
     |CIERRA #3;
|FPRC;

|PROCESO Dir2_0; |TIPO 0;
     |ABRE #3;
     #3#0 = #102#0;
     #3#1 = #102#1;
     |LEE 000#3=;
     |SI FSalida = 0;
          #102#4 = #3#25; |PINTA #102#4;
     |SINO;
          |MENAV "0000No existe direcci¢n entrega...";
          |ERROR;
     |FINSI;
     |CIERRA #3;
|FPRC;

|PROCESO BtnAltaEdi;
     |VENTANA 1206, 1994, -1, 17, "Alta edificios por cliente";
     |HAZ PushVenModal;

     |DDEFECTO #102;
     #102#0 = #0#0;
     #102#3 = #0#1;
     |PINPA #0#102;
     |PINDA #0#102;
     |ENDATOS #1#102;
     aAlfa = FSalida;

     |HAZ PopVenModal;

     |SI aAlfa = 0;
          |ABRE #4;
          #4#0 = #102#0;
          #4#1 = #102#1;
          #4#2 = #102#2;
          |GRABA 020#4n;
          |CIERRA #4;

          |ABRE #5;
          #5#0 = #4#0;
          #5#1 = #4#1;
          #5#2 = #4#2;
          #5#3 = "";
          |GRABA 000#5n;
          |CIERRA #5;

          |ABRE #6;
          #6#0 = #4#0;
          #6#1 = #4#1;
          #6#2 = #4#2;
          #6#3 = "";
          #6#4 = "";
          |GRABA 000#6n;
          |CIERRA #6;

          |HAZ CreaArbol;
     |FINSI;
|FPRC;

|PROCESO CamDirOrg0; |TIPO 0;
     |ABRE #3;
     #3#0 = #0#0;
     #3#1 = #132#0;
     |LEE 000#3=;
     |SI FSalida = 0;
          #132#5 = #3#10; |PINTA #132#5;
     |SINO;
          |MENAV "0000No existe instalaci¢n...";
          |ERROR;
     |FINSI;
     |CIERRA #3;
|FPRC;

|PROCESO MinOrg3;
     #3#0 = #0#0;
     #3#1 = 1;
|FPRC;

|PROCESO MaxOrg3;
     #3#0 = #0#0;
     #3#1 = 999;
|FPRC;

|PROCESO CamDirOrg66; |TIPO 66;
     |ABRE #3;
     #3#0 = #0#0;
     #3#1 = #132#0;
     |LEE 000#3];
     |CONSULTA_F_CLAVE #1#3, MinOrg3, MaxOrg3;
     |SI FSalida = 0;
          #132#0 = #3#1; |PINTA #132#0;
     |SINO;
          |ERROR;
     |FINSI;
     |CIERRA #3;
|FPRC;

|PROCESO CamEdiOrg0; |TIPO 0;
     |ABRE #4;
     #4#0 = #0#0;
     #4#1 = #132#0;
     #4#2 = #132#1;
     |LEE 000#4=;
     |SI FSalida ! 0;
          |MENAV "0000No existe edificio...";
          |ERROR;
     |FINSI;
     |CIERRA #4;
|FPRC;

|PROCESO MinOrg4;
     #4#0 = #0#0;
     #4#1 = #132#0;
     #4#2 = "                             ";
|FPRC;

|PROCESO MaxOrg4;
     #4#0 = #0#0;
     #4#1 = #132#0;
     #4#2 = "zzzzzzzzzzzzzzzzzzzzzzzzzzzzz";
|FPRC;

|PROCESO CamEdiOrg66; |TIPO 66;
     |ABRE #4;
     #4#0 = #0#0;
     #4#1 = #132#0;
     #4#2 = #132#1;
     |LEE 000#4];
     |CONSULTA_F_CLAVE #1#4, MinOrg4, MaxOrg4;
     |SI FSalida = 0;
          #132#1 = #4#2; |PINTA #132#1;
     |SINO;
          |ERROR;
     |FINSI;
     |CIERRA #4;
|FPRC;

|PROCESO CamCliDes0; |TIPO 0;
     |ABRE #24;
     #24#0 = #132#2;
     |LEE 000#24=;
     |SI FSalida = 0;
          #132#6 = #24#1; |PINTA #132#6;
     |SINO;
          |MENAV "0000No existe c¢digo cliente...";
     |FINSI;
     |CIERRA #24;
|FPRC;

|PROCESO CamDirDes0; |TIPO 0;
     |ABRE #3;
     #3#0 = #132#2;
     #3#1 = #132#3;
     |LEE 000#3=;
     |SI FSalida = 0;
          #132#7 = #3#10; |PINTA #132#7;
     |SINO;
          |MENAV "0000No existe instalaci¢n...";
          |ERROR;
     |FINSI;
     |CIERRA #3;
|FPRC;

|PROCESO MinDes3;
     #3#0 = #132#2;
     #3#1 = 1;
|FPRC;

|PROCESO MaxDes3;
     #3#0 = #132#2;
     #3#1 = 999;
|FPRC;

|PROCESO CamDirDes66; |TIPO 66;
     |ABRE #3;
     #3#0 = #132#2;
     #3#1 = #132#3;
     |LEE 000#3];
     |CONSULTA_F_CLAVE #1#3, MinDes3, MaxDes3;
     |SI FSalida = 0;
          #132#3 = #3#1; |PINTA #132#3;
     |SINO;
          |ERROR;
     |FINSI;
     |CIERRA #3;
|FPRC;

|PROCESO CamEdiDes0; |TIPO 0;
     |ABRE #4;
     #4#0 = #132#2;
     #4#1 = #132#3;
     #4#2 = #132#4;
     |LEE 000#4=;
     |SI FSalida ! 0;
          |MENAV "0000No existe edificio...";
          |ERROR;
     |FINSI;
     |CIERRA #4;
|FPRC;

|PROCESO MinDes4;
     #4#0 = #132#2;
     #4#1 = #132#3;
     #4#2 = "                             ";
|FPRC;

|PROCESO MaxDes4;
     #4#0 = #132#2;
     #4#1 = #132#3;
     #4#2 = "zzzzzzzzzzzzzzzzzzzzzzzzzzzzz";
|FPRC;

|PROCESO CamEdiDes66; |TIPO 66;
     |ABRE #4;
     #4#0 = #132#2;
     #4#1 = #132#3;
     #4#2 = #132#4;
     |LEE 000#4];
     |CONSULTA_F_CLAVE #1#4, MinDes4, MaxDes4;
     |SI FSalida = 0;
          #132#4 = #4#2; |PINTA #132#4;
     |SINO;
          |ERROR;
     |FINSI;
     |CIERRA #4;
|FPRC;

|PROCESO CamCliFac0; |TIPO 0;
     |ABRE #24;
     #24#0 = #133#1;
     |LEE 000#24=;
     |SI FSalida = 0;
          #133#3 = #24#1; |PINTA #133#3;
     |SINO;
          |MENAV "0000No existe c¢digo cliente...";
     |FINSI;
     |CIERRA #24;
|FPRC;

|PROCESO CamCli;
     #11#1 = #132#2;
     #11#2 = #132#3;
     #11#3 = #132#4;
     #11#38 = #11#1;
     #11#39 = #11#1;
     |GRABA 020#11a;
|FPRC;

|DEFBUCLE CamCli;
     #11#1;
     1, 2, 3;
     "                    ", #0#0, #132#0, #132#1;
     "zzzzzzzzzzzzzzzzzzzz", #0#0, #132#0, #132#1;
     be;
     NULL, CamCli;
|FIN;

|PROCESO BtnCamCli;
     |VENTANA 1206, 2294, -1, 17, "Cambio de cliente";
     |HAZ PushVenModal;
     |DDEFECTO #132;
     |PINPA #0#132;
     |PINDA #0#132;
     |ENDATOS #1#132;
     aAlfa = FSalida;
     |HAZ PopVenModal;
     |SI aAlfa = 0;
          eaMensaje = "Procesando cambio cliente";
          |HAZ IniMensaje;
          |BUCLE CamCli;
          |HAZ FinMensaje;
          |HAZ CreaArbol;
     |FINSI;
|FPRC;

|PROCESO CamDirOrgA0; |TIPO 0;
     |ABRE #3;
     #3#0 = #0#0;
     #3#1 = #133#0;
     |LEE 000#3=;
     |SI FSalida = 0;
          #133#2 = #3#10; |PINTA #133#2;
     |SINO;
          |MENAV "0000No existe instalaci¢n...";
          |ERROR;
     |FINSI;
     |CIERRA #3;
|FPRC;

|PROCESO CamDirOrgA66; |TIPO 66;
     |ABRE #3;
     #3#0 = #0#0;
     #3#1 = #133#0;
     |LEE 000#3];
     |CONSULTA_F_CLAVE #1#3, MinOrg3, MaxOrg3;
     |SI FSalida = 0;
          #133#0 = #3#1; |PINTA #133#0;
     |SINO;
          |ERROR;
     |FINSI;
     |CIERRA #3;
|FPRC;

|PROCESO CamCliFac;
     #11#39 = #133#1;
     |GRABA 020#11a;
|FPRC;

|DEFBUCLE CamCliFac;
     #11#2;
     ;
     #0#0, #133#0, "                              ", "    ", "                    ";
     #0#0, #133#0, "zzzzzzzzzzzzzzzzzzzzzzzzzzzzzz", "zzzz", "zzzzzzzzzzzzzzzzzzzz";
     be;
     NULL, CamCliFac;
|FIN;

|PROCESO BtnCliFac;
     |VENTANA 1206, 1994, -1, 17, "Cambio cliente factura";
     |HAZ PushVenModal;
     |DDEFECTO #133;
     |PINPA #0#133;
     |PINDA #0#133;
     |ENDATOS #1#133;
     aAlfa = FSalida;
     |HAZ PopVenModal;
     |SI aAlfa = 0;
          eaMensaje = "Procesando cambio cliente factura";
          |HAZ IniMensaje;
          |BUCLE CamCliFac;
          |HAZ FinMensaje;
          |HAZ CreaArbol;
     |FINSI;
|FPRC;

|PROCESO EnviaFic;
     aIP = "";
     |IP_REMOTO aIP;
     |SI aIP ! "";
          aFicTar = aOrg + Usuario + ".tar";

          aAlfa = aFicTar + " " + aFicTxt;
          |ATAR aAlfa, aOrg;
          aAlfa = aOrg + aFicTxt;
          |FBORRA aAlfa;

          |COMPRIME aFicTar;
          |FBORRA aFicTar;

          |ESPECIFICA "RBASS", aLocal;
          aBass = aLocal;
          aDes = aBass + Usuario + ".tgz";

          aOrg = aOrg + Usuario + ".tgz";
          |ENVIA_FICHERO aOrg, aDes;
          |FBORRA aOrg;

          |RDESCOMPRIME aDes;
          aAlfa = aBass + Usuario + ".tgz";
          |RFBORRA aAlfa;

          aAlfa = aBass + Usuario + ".tar";
          |RDETAR aAlfa, aBass;
          |RFBORRA aAlfa;

          aFicTxt = aBass + Usuario + ".txt";
     |SINO;
          aFicTxt = aOrg + aFicTxt;
     |FINSI;
|FPRC;

|PROCESO GrabaTxt;
     |XGRABA nHandle, aAlfa;
|FPRC;

|PROCESO ArtRevisar;
     |SALVA_DATOS #11;
     |REPON_DATOS #extm0011@;
     |GRABA 020#extm0011@.n;
|FPRC;

|DEFBUCLE ArtRevisar;
     #11#2;
     ;
     #4#0, #4#1, #4#2, "    ", "          ";
     #4#0, #4#1, #4#2, "zzzz", "zzzzzzzzzz";
     ;
     NULL, ArtRevisar;
|FIN;

|PROCESO Edificios;
     #3#0 = #4#0;
     #3#1 = #4#1;
     |LEE 000#3=;
     |SI FSalida ! 0; |DDEFECTO #3; |FINSI;
     |SI nDir ! #4#1;
          nNivel2 = 0;
          nNivel1 = nNivel1 + 1;
          aCod = (("0000" + #4#1) % -104) + VACIO;
          aDes = #3#25; |QBF aDes;
          aAlfa = nNivel1 + ":" + aDes +  "{{" + aCod + "}}";
          |HAZ GrabaTxt;
     |FINSI;
     |SI aEdi ! #4#2;
          nNivel2 = nNivel2 + 1;
          aCod = (("0000" + #4#1) % -104) + #4#2;
          aDes = #4#2; |QBF aDes;
          aAlfa = nNivel1 + "," + nNivel2 + ":" + aDes +  "{{" + aCod + "}}";
          |HAZ GrabaTxt;
     |FINSI;
     nDir = #4#1;
     aEdi = #4#2;
     |BUCLE ArtRevisar;
|FPRC;

|DEFBUCLE Edificios;
     #4#1;
     ;
     #0#0, 0001, "                    ";
     #0#0, 9999, "zzzzzzzzzzzzzzzzzzzz";
     ;
     NULL, Edificios;
|FIN;

|PROCESO Min;
     #extm0011@#1 = #0#0;
     #extm0011@#2 = nDIns;
     #extm0011@#3 = aDEdi;
     #extm0011@#5 = "    ";
     #extm0011@#0 = "                    ";
|FPRC;

|PROCESO Max;
     #extm0011@#1 = #0#0;
     #extm0011@#2 = nHIns;
     #extm0011@#3 = aHEdi;
     #extm0011@#5 = "zzzz";
     #extm0011@#0 = "zzzzzzzzzzzzzzzzzzzz";
|FPRC;

|PROCESO CreaArbol;
     |SI nTree ! -1; |FIN_CONTROL_WINDOWS nTree; |FINSI;
     |SI nGrid ! -1; |DESTRUYECONTROL nGrid; |FINSI;
     |PULSATECLA;   || si esto se cuelga!!!

     |CIERRAT #extm0011@; |DELINDEX #extm0011@; |ABRET #extm0011@;

     nDir = 0; aEdi = "";
     |DFICE aOrg;
     aFicTxt = Usuario + ".txt";
     aAlfa = aOrg + aFicTxt;
     |XABRE "W", aAlfa, nHandle;
     |SI FSalida ] 0;
          nNivel1 = 0; nNivel2 = 0;
          |ABRE #3;
          |BUCLE Edificios;
          |CIERRA #3;
          |SI nDir = 0;
               aAlfa = "1:Sin datos {{0000                    }}";
               |HAZ GrabaTxt;
          |FINSI;
          |XCIERRA nHandle;
          |HAZ EnviaFic
     |FINSI;
/*
     |SI nDir = 0;
          |ESTADO_CONTROL nBtnBajaEdi, "DISABLE";
     |SINO;
          |ESTADO_CONTROL nBtnBajaEdi, "ENABLE";
     |FINSI;
*/
     aAlfa = "TREECTRL,Instalaciones {{" + aFicTxt + "}}";
     |CONTROL_SIMPLE nPanta, aAlfa, 1202, 3124, EvtTree;
     nTree = FSalida;

     |SELECT #2#extm0011@;
     |LEE 000#extm0011@.p;
     aAlfa = FSalida;
     |SI nDir = 0;
          |ESTADO_CONTROL nBtnAltaArt, "DISABLE";
          |ESTADO_CONTROL nBtnModiArt, "DISABLE";
          |ESTADO_CONTROL nBtnBajaArt, "DISABLE";
          |ESTADO_CONTROL nBtnConsArt, "DISABLE";
          |ESTADO_CONTROL nBtnHisto, "DISABLE";
          |ESTADO_CONTROL nBtnImpEti, "DISABLE";
     |SINO;
          |ESTADO_CONTROL nBtnAltaArt, "ENABLE";
          |SI aAlfa = 0;
               |ESTADO_CONTROL nBtnModiArt, "ENABLE";
               |ESTADO_CONTROL nBtnBajaArt, "ENABLE";
               |ESTADO_CONTROL nBtnConsArt, "ENABLE";
               |ESTADO_CONTROL nBtnHisto, "ENABLE";
               |ESTADO_CONTROL nBtnImpEti, "ENABLE";
          |FINSI;
     |FINSI;

     nRango = 4 + 8 + 16 + 32;
     |LINEAL_SIMPLE #2#extm0011@, nRango, 1225, 3198, Min, Max, EvtGrid;
     nGrid = FSalida;
/*
     sT_nID = nTree;
     sT_nOper = 2;       || Selecciona
     sT_aData = "1:";
     |ESPECIFICA "COMMANDCONTROL", sTree;
*/
     || Selecciona el tree
     |SI nDir > 0; |FOCOTECLADO nTree; |FINSI;
|FPRC;

|PROCESO BtnActualizar;
     |REFERENCIA_DEF aGet1, #0;
     aGet2  = 0;    || numero de campo
     IaGet = aGet1<-;
     |ESPECIFICA "GETDATACONTROL", aGet;
     aAlfa = FSalida; |QBT aAlfa;
     aAlfa = ("000000" + aAlfa) % -106;
     #0#0 = aAlfa; |PINTA #0#0;

     |ESTADO_CONTROL nBtnAltaEdi, "ENABLE";
     |ESTADO_CONTROL nBtnCamCli, "ENABLE";
     |ESTADO_CONTROL nBtnCliFac, "ENABLE";
     |HAZ CreaArbol;
|FPRC;

|PROCESO BtnSalir;
     |PTEC 807;
|FPRC;

|PROCESO T12; |TIPO 12;
|FPRC;

|PROCESO T66; |TIPO 66;
     |ABRE #24;
     #24#0 = #0#0;
     |LEE 000#24];
     |CONSULTA_CLAVE #1#24;
     |SI FSalida = 0;
          #0#0 = #24#0; |PINTA #0#0;
     |SINO;
          |ERROR;
     |FINSI;
     |CIERRA #24;
|FPRC;

|PROCESO T0; |TIPO 0;
     |ABRE #24;
     #24#0 = #0#0;
     |LEE 000#24=;
     |SI FSalida = 0;
          #0#1 = #24#1; |PINTA #0#1;
     |SINO;
          |MENAV "0000No existe el cliente...";
          |ERROR;
     |FINSI;
     |CIERRA #24;
|FPRC;

|PROGRAMA;
     |DDEF aAlfa; aAlfa = aAlfa + "extm0011";
     |CARGA_DEF aAlfa, extm0011@;
     |HAZ CreaTempo;
     |NOME_DAT #extm0011@#Tempo#; |DELINDEX #extm0011@; aTmp11 = Tempo;
     |ABRET #extm0011@;

     |PINPA #0#0; nPanta = FSalida;
     |PINDA #0#0;
     |CONTROL_SIMPLE nPanta, "BOTON,{{137}}Actualizar", 0902, 0914, BtnActualizar;
     nBtnActualizar = FSalida;
     |CONTROL_SIMPLE nPanta, "BOTON,{{197}}Cambio Cliente", 0885, 0899, BtnCamCli;
     nBtnCamCli = FSalida;
     |CONTROL_SIMPLE nPanta, "BOTON,{{197}}Cliente factura", 0985, 0999, BtnCliFac;
     nBtnCliFac = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,{{001}}Salir", 3389, 3398, BtnSalir;
     nBtnSalir = FSalida;
     |CONTROL_SIMPLE nPanta, "BOTON,{{193}}Alta", 1102, 1112, BtnAltaEdi;
     nBtnAltaEdi = FSalida;
|| anulado pero funciona
||     |CONTROL_SIMPLE nPanta, "BOTON,{{199}}Baja", 1114, 1124, BtnBajaEdi;
     nBtnBajaEdi = FSalida;
     |ESTADO_CONTROL nBtnAltaEdi, "DISABLE";
     |ESTADO_CONTROL nBtnCamCli, "DISABLE";
     |ESTADO_CONTROL nBtnCliFac, "DISABLE";
||     |ESTADO_CONTROL nBtnBajaEdi, "DISABLE";
     |HAZ CreaArbol;
     |CONTROL_SIMPLE nPanta, "BOTON,{{193}}Alta", 1128, 1138, BtnAltaArt;
     nBtnAltaArt = FSalida;
     |CONTROL_SIMPLE nPanta, "BOTON,{{197}}Modificar", 1140, 1150, BtnModiArt
     nBtnModiArt = FSalida;
     |CONTROL_SIMPLE nPanta, "BOTON,{{199}}Baja", 1152, 1162, BtnBajaArt;
     nBtnBajaArt = FSalida;
     |CONTROL_SIMPLE nPanta, "BOTON,{{155}}Consulta", 1164, 1174, BtnConsArt;
     nBtnConsArt = FSalida;
     |CONTROL_SIMPLE nPanta, "BOTON,{{155}}Hist¢rico", 1176, 1186, BtnHisto;
     nBtnHisto = FSalida;
     |CONTROL_SIMPLE nPanta, "BOTON,{{205}}Etiqueta", 1188, 1198, BtnImpEti;
     nBtnImpEti = FSalida;

     |ESTADO_CONTROL nBtnAltaArt, "DISABLE";
     |ESTADO_CONTROL nBtnModiArt, "DISABLE";
     |ESTADO_CONTROL nBtnBajaArt, "DISABLE";
     |ESTADO_CONTROL nBtnConsArt, "DISABLE";
     |ESTADO_CONTROL nBtnHisto, "DISABLE";
     |ESTADO_CONTROL nBtnImpEti, "DISABLE";
     |PARA FSalida = 0; |SI FSalida = 0; |HACIENDO;
          FSalida = "::ENDATOS::SINBOTONES::";
          |ENDATOS #1#0;
     |SIGUE;
/*
     |DESTRUYECONTROL nGrid;
     |FIN_CONTROL_WINDOWS nTree;
     |FIN_CONTROL_WINDOWS nBtnActualizar;
     |FIN_CONTROL_WINDOWS nBtnSalir;
     |FIN_CONTROL_WINDOWS nBtnAltaEdi;
     |FIN_CONTROL_WINDOWS nBtnCamCli;
     |FIN_CONTROL_WINDOWS nBtnBajaEdi;
     |FIN_CONTROL_WINDOWS nBtnAltaArt;
     |FIN_CONTROL_WINDOWS nBtnModiArt;
     |FIN_CONTROL_WINDOWS nBtnBajaArt;
     |FIN_CONTROL_WINDOWS nBtnConsArt;
     |FIN_CONTROL_WINDOWS nBtnHisto;
     |FIN_CONTROL_WINDOWS nBtnImpEti;
*/
     |PULSATECLA;

     |CIERRAT #extm0011@;
     Tempo = aTmp11; |HAZ BoraTempo; |DELINDEX #extm0011@;
     |DESCARGA_DEF #extm0011@;
|FPRO;
