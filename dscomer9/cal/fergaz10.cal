|FICHEROS;
     fergaz10 #0;
     fergam14 #14;
     agifa104 #104;
     agifa024 #24,MANTE;
     agifa142 #41;
     dsm00003 #39,MANTE;

     referen@;
|FIN;

|VARIABLES;
     aAlfa = "";
     aSinError = "";
     aSql      = "";
     aDef      = "";

     nSal      = 0;
     nModo     = 0;
     nTecla    = 0;

     &enModo = 0;
     &eaSerPedido = "";
     &enNumPedido = 0;
     &eaCliente = "";
     &eaDirEntrega = "";
     &efFecha = @;
     &enProg211 = 0;
|FIN;

|PROCESO AsignaDir;
     #fergaz10#4 = #39#1;

     |PINDA #0#fergaz10;
|FPRC;

|PROCESO T66C4;  |TIPO 66;
     nModo = (FEntrada / 100) ! 0;
     |ABRE #39;
     aSql = "SELECT * FROM dsm00003 INTO sq" + Usuario + ".def WHERE ";
     aSql = aSql + "0 == " + #0#0 + " AND 18 == S";
     |SQL aSql;
     |SI FSalida = 0;
          |DDEF aDef; |QBF aDef;
          aDef = aDef + "sq" + Usuario + ".mas";
          |CARGA_DEF aDef, referen@;
          |SI FSalida = 0;
               |ABRE #referen@;
               #referen@#0 = #0#0;
               #referen@#1 = #0#4;
               |LEE 000#referen@.]
               |CONSULTA_CLAVE #1#referen@;
               nSal = FSalida;
               |SI FSalida = 0;
                    |SI nModo ! 2; |O #referen@#0 ! #0#0; |O #referen@#1 ! #0#4;
                         #39#0 = #referen@#0;
                         #39#1 = #referen@#1;
                         |LEE 020#39=;
                         |HAZ AsignaDir;
                    |FINSI;
               |SINO;
                    |SI aSinError ! "S";
                         |ERROR;
                    |FINSI;
               |FINSI;
               |CIERRA #referen@;
               |DELINDEX #referen@;
               |DESCARGA_DEF #referen@;
               |FBORRA aDef;
          |FINSI;
     |FINSI;
     |CIERRA #39;
|FPRC;

|PROCESO T7C4;  |TIPO 7;
     |ABRE #41; |LEE 001#41p; |CIERRA #41;

     |SI #41#71 = "S"; |Y enProg211 ! 211;
          |ABRE #39;
          #39#0 = #0#0;
          #39#1 = 1;
          |LEE 000#39];
          |SI FSalida = 0; |Y #39#0 = #0#0;
               |LEE 000#39s;
               |SI FSalida = 0; |Y #39#0 = #0#0;
                    aSinError = "S";
                    |HAZ T66C4;
                    aSinError = "N";
                    |SI nSal = 0; |ERROR; |FINSI;
               |FINSI;
          |FINSI;
          |CIERRA #39;
     |FINSI;
|FPRC;

|PROCESO MinDir;
     #39#0 = #0#0;
     #39#1 = 1;
|FPRC;

|PROCESO MaxDir;
     #39#0 = #0#0;
     #39#1 = 9999;
|FPRC;

|PROCESO T0C4;  |TIPO 0;
     |SI FSalida = 2; |ACABA; |FINSI;

     nTecla = FSalida;
     |SI nTecla = 12;    || F4 Alta direccion
          |PUSHV 0501 2380;
          |ENTLINEAL #1#39, -2, 1, 22, 1, MinDir, MaxDir;
          |POPV;
          nTecla = 18;
     |FINSI;

     |ABRE #39;
     |SI nTecla = 18;   || F10  Todas las direcciones
          #39#0 = #0#0;
          #39#1 = #0#4;
          |LEE 000#39];
          |CONSULTA_F_CLAVE #1#39, MinDir, MaxDir;
          |SI FSalida = 0;
               |HAZ AsignaDir;
          |SINO;
               |ERROR; |ACABA;
          |FINSI;
     |FINSI;

     #39#0 = #0#0;
     #39#1 = #0#4;
     |LEE 000#39=;
     |SI FSalida = 0;
          |SI #39#18 = "N";
               |CONFI "0000NDireccion de entrega desactiva. ¿Continuar?";
               |SI FSalida ! 0; |ERROR; |ACABA; |FINSI;
          |FINSI;
     |SINO;
          |MENAV "0000No existe direccion de entrega...";
          |ERROR; |ACABA;
     |FINSI;
     |CIERRA #39;

     |SI #0Campo ! Contenido;
          |HAZ AsignaDir;
     |FINSI;
|FPRC;

|PROCESO Cliente; |TIPO 6;
     |SI FSalida ! 13; |ACABA; |FINSI;

     |PUSHV 0501 2380;
     |ABRE #24;
     #24#0 = #0#0;
     |LEE 101#24=;
     |SI FSalida = 0;
          |PINPA #0#24;
          |PINDA #0#24;
          |ENDATOS #2#24;
          |SI FSalida = 0;
               |GRABA 020#24a;
          |SINO;
               |ERROR;
          |FINSI;
     |SINO;
          |GRABA 020#24b;
          |PINPA #0#24;
          |PINDA #0#24;
          |ENDATOS #1#24;
          |SI FSalida = 0;
               |GRABA 020#24a;
          |SINO;
               |BORRA 020#24c;
               |ERROR;
          |FINSI;
     |FINSI;
     |CIERRA #24;
     |POPV;
|FPRC;

|PROCESO T12; |TIPO 12;
|FPRC;

|PROCESO Min;
     #104#4 = #0#0;
     #104#25 = "      ";
     #104#0 = 0;
|FPRC;

|PROCESO Max;
     #104#4 = #0#0;
     #104#25 = "zzzzz";
     #104#0 = 999999;
|FPRC;

|PROCESO Pedido66; |TIPO 66;
     |SELECT #2#104;
     #104#4 = #0#0;
     #104#25 = #0#3;
     #104#0 = #0#1;
     |LEE 000#104];
     |CONSULTA_F_CLAVE #2#104, Min, Max;
     |SI FSalida = 0;
          #0#1 = #104#0; |PINTA #0#1;
          #0#2 = #104#1; |PINTA #0#2;
          #0#3 = #104#25;
     |SINO;
          |ERROR;
     |FINSI;
     |SELECT #1#104;
|FPRC;

|PROCESO Pedido0; |TIPO 0;
     |SI FSalida = 2; |ACABA; |FINSI;

     |SI enModo = 1;
          |DDEFECTO #104;
          #104#25 = #0#3;
          #104#0 = #0#1;
          |LEE 000#104=
          |SI FSalida = 0;
               |MENAV "0000Pedido existente...";
               |ERROR;
          |FINSI;
     |FINSI;
     |SI enModo = 4;
          |DDEFECTO #104;
          #104#25 = #0#3;
          #104#0 = #0#1;
          |LEE 000#104=
          |SI FSalida ! 0;
               |MENAV "0000El Pedido no existe...";
               |ERROR;
          |FINSI;
     |FINSI;
|FPRC;

|PROGRAMA;
     |ABRET #104;
     |SI enModo = 1;
          aAlfa = Usuario % 810;
          |ABRE #14;
          #14#0 = aAlfa;
          |LEE 000#14=;
          |SI FSalida = 0;
               #0#0 = #14#1;
          |FINSI;
          |CIERRA #14;

          |C_M #0#2, 1, "S";
          |ENDATOS #1#0;
          |SI FSalida = 0;
               eaCliente    = #0#0;
               enNumPedido  = #0#1;
               efFecha      = #0#2;
               eaSerPedido  = #0#3;
               eaDirEntrega = #0#4;
          |SINO;
               enNumPedido = 0;
          |FINSI;
     |SINO;
          |C_M #0#2, 1, "N";
          |SI enModo = 4;
               #0#0 = eaCliente;
               #0#1 = enNumPedido;
               #0#2 = efFecha;
               #0#3 = eaSerPedido;
               #0#4 = eaDirEntrega;
               |PINDA #0#0;
               |ENDATOS #1#0;
               |SI FSalida = 0;
                    enNumPedido  = #0#1;
                    eaSerPedido  = #0#3;
                    eaDirEntrega = #0#4;
               |SINO;
                    enNumPedido = 0;
               |FINSI;
          |SINO;
          |FINSI;
     |FINSI;
     |CIERRAT #104;
|FPRO;
