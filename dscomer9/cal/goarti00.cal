|FICHEROS;
     agifa019 #0;        || Este def.
     agifa017 #1;        || Stock por almacenes.
     agifa163 #3;        || Campos opcionales de articulos.
     agifa142 #4;        || Parametros generales.
     dsw00034 #6;        || Auxiliar Art/Prov.
     agifa065 #7;        || Historico Mov.
     agifa112 #8;        || Proveedores.
     agifa060 #9;
     agifa120 #10;
     agi00016 #80,MANTE; || Texto Familias.
     agi00014 #99;       || Defectos de articulos, por proveedor.
     agipan00 #11;       || Pantalla tallas
     agifa324 #12;       || Divisas
     agifa321 #13;       || Parametros divisas
     agitp001 #14;       || Tarifas Proveerdor
     dsl00001 #50;       || Codigos de barras multiples
     agica001 #60;       || Alternativos
     agifa219 #219;
     agifa220 #220;
     dsm00015 #915;
     dsm00010 #910;
     dsm00019 #919;
     dsm00004 #904;
     marhup08@ #30;
     dsm00238 #238;
     dsm00239 #239;
     dsm00240 #240;
     dsm00241 #241;
     dsw00044 #100,MANTE;
     dsm00081 #81;
     dsm00082 #82;
     goartmer;
     goparame #110;
     goartih6 #116;
     goapaive #117;
     referen@;
|FIN;

|CAMPOS;
     #0#18 pvp11;
     #0#19 pvp22;
     #0#20 pvp33;
     #0#147 pvp44;
     #0#148 pvp55;
     #0#149 pvp66;
     #0#109 redo;
|FIN;

|VARIABLES;
     nSwPin = 0;
     &enSwAltaArt = 0; || Para la replica segun el control de empresas
     &enAlta = 0;
     &eaArticulo = "";
     alfa = "";
     &eaParCod = "";
     &eaParSec = "";
     &eaParFam = "";
     &eaParSub = "";
     &eaParCap = "";

     aDesAnt   = "";
     nGuarda   = 0;
     nBoton    = 0;
     aDef      = "";
     &eaArti   = "";
     &eaConte1 = "";
     &eaConte2 = "";
     &eaDocu   = "";
     &enSwCtrlUsu  = 0;
     &Tempo    = "";
     aTempo6   = "";
     aPro      = "";
     aSer      = "";
     nDoc      = 0;
     nLdo      = 0;
     aArt      = "";
     fFec      = @;
     nCan      = 0;
     nPre      = 0;
     nSw       = 0;
     aFich     = "";
     aAlfa1    = "";
     nSal      = 0;
     swCar1    = 0;
     aAlfa33   = "";
     swImp     = 0;
     &eaPrv    = "";
     &enTiva   = 0;
     &enIva    = 0;
     &enRec    = 0;
     &efFiva = @;

     aTx1       = "";
     aTx2       = "";
     aTx3       = "";
     aTx4       = "";
     aTx5       = "";
     aTx6       = "";
     aTx7       = "";
     aTx8       = "";
     aTx9       = "";
     aTx0       = "";

     aProg      = "";
     nModo      = 0;
     &eAlfa     = "";
     &eAlfaC    = "";
     &eAlfa1    = "";
     &eAlfa2    = "";
     &eAlfa3    = "";
     &eAlfa4    = "";
     &enModoArt = 0;

     &nDeci_PreBaseMx = 0;
     &nDeci_PreDiv  = 0;
     &nDeci_Base = 0;
     &nDeci_PreBase = 0;
     &precio_base = 0;
     aAlfa = "";
     aprograma      = "";
     precio    = 0;
     &Marca    = "";
     aConfir   = "";
     codant    = "";
     codart    = 0;
     &pasa     = "";
     salida    = "";
     &orden    = "";
     mensaje1  = "0000Valor minimo 0.01    Valor maximo 999999";
     &mes      = 0;
     i         = 0;
     tot       = 0;
     informe   = "agifa019";
     informe2  = "agifal19";
     m1        = 0;
     m2        = 0;
     m3        = 0;
     m4        = 0;
     m5        = 0;
     m6        = 0;
     &modo     = 0;
     &codigo   = "";
     desde     = "                    ";
     hasta     = "zzzzzzzzzzzzzzzzzzzz";
     codigo1   = "";
     cam_pos   = 0;
     cam_vis   = "";
     cam_mod   = "";
     vis       = 0;
     &sw1      = 0;
     &registro = "";
     i1        = "";
     i2        = "";
     i3        = "";
     i4        = "";
     i5        = "";
     i6        = "";
     p1        = "";
     p2        = "";
     p3        = "";
     p4        = "";
     p5        = "";
     p6        = "";
     calcodb   = "";
     sumai     = 0;
     sumap     = 0;
     base      = 0;
     resto     = 0;
     total     = 0;
     digito    = 0;
     xx        = "";
     ind       = 0;
     nombrep   = "";

     &Cascada    = "";
     &CodEnvase1 = "";
     &CodEnvase2 = "";
     &CodEnvase3 = "";
     &CodEnvase4 = "";
     &FacEnvase1 = 0;
     &FacEnvase2 = 0;
     &FacEnvase3 = 0;
     &FacEnvase4 = 0;
     calpre = 0;
     aTallas = "";
     cambio_div = 0;
     cambio_base = 0;
     factordec = 0;
     tpcontro  = 0;  ||Controla si se han modificado los campos que afectan a tarifas-proveedor
     nContador = 0;
     aCodigo = "";
     &pvp1       = 0;
     &pvp2       = 0;
     &pvp3       = 0;
     &pvp4       = 0;
     &pvp5       = 0;
     &pvp6       = 0;
 {-1}aDato  = "";
     aDato1 = "2400";
     aDato2 = "1";
     aDato3 = "Impresion  de : ";
     aDato4 = "FR";
     aDato5 = "    Ficha     ";
     aDato6 = "   Relacion   ";
 {-1}aUtil  = "";
     aUtil1 = "2400";
     aUtil2 = "1";
     aUtil3 = "";
     aUtil4 = "IPAVCGB";           ||| OJO que se modifica el valor
     aUtil5 = "Imprimir";
     aUtil6 = "Proveedores";
     aUtil7 = "Alter.(tpv)";
     aUtil8 = "DA.Vta";
     aUtil9 = "DA.Com";
     aUtil10 = "Grafico";
     aUtil11 = "Bultos";
|FIN;

|INCLUYE buscaart;

|PROCESO dsl00001Ver;
     #100#0 = #50#3;
     #100#1 = #50#6;
     |GRABA 020#100n;
|FPRC;

|DEFBUCLE dsl00001Ver;
     #50#3;
     ;
     #0#0, "             ";
     #0#0, "zzzzzzzzzzzzz";
     ;
     NULL, dsl00001Ver;
|FIN;

|PROCESO Min100;
     #100#0 = " " * 13;
|FPRC;

|PROCESO Max100;
     #100#0 = "z" * 13;
|FPRC;

|PROCESO ConsultaCB; |TIPO 6;
     |SI FSalida ! 13; |ACABA; |FINSI;

     |HAZ CreaTempo; |NOME_DAT #100 Tempo; |DELINDEX #100;
     |DELINDEX #100; |ABRE #100;
     |BUCLE dsl00001Ver;
     |PUSHV 0501 2380;
     |BLANCO 814 2343;
     |ENTLINEAL #1#100, -1, 2, 21, 1, Min100, Max100;
     |POPV;
     |CIERRA #100; |DELINDEX #100; |HAZ BoraTempo;
     |ERROR;
|FPRC;

|PROCESO DefMedida; |TIPO 0;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 1;
          #0#205 = #0#5; |PINTA #0#205;
          #0#206 = #0#5; |PINTA #0#206;
          #0#207 = #0#5; |PINTA #0#207;
     |FINSI;
     aAlfa = #0#5; |QBF aAlfa;
     |SI aAlfa = ""; |Y #110#90 = "S"; |Y #110#141 = "O";
          |MENAV "0000Atencion Unidad Medida en Blanco...";
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO Marca; |TIPO 0;
     aAlfa = #0#126; |QBF aAlfa;
     |SI aAlfa = ""; |Y #110#90 = "S"; |Y #110#141 = "O";
          |MENAV "0000Atencion Naturaleza en Blanco...";
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO DecDesglose; |TIPO 7;
     |SI #0#178 = "P";
          |C_M #0#187, 1, "N";
          #0#187 = 0; |PINTA #0#187;
     |SINO;
          |C_M #0#187, 1, "S";
     |FINSI;
|FPRC;

|PROCESO entra194; |TIPO 7;
     |SI Campo = 194;
          aAlfa = "0000Facturar para ventas:  <1> " + #238#3 + "  <2> " + #238#2;
          |MENSA aAlfa;
     |FINSI;
     |SI Campo = 196;
          aAlfa = "0000Facturar para compras:  <1> " + #238#3 + "  <2> " + #238#2;
          |MENSA aAlfa;
     |FINSI;
|FPRC;

|PROCESO sal194; |TIPO 0;
     |MENSA "0000 ";
|FPRC;

|PROCESO DivStock; |TIPO 7;
     |SI #0#179 = "S"; |Y #0#188 > 0;
          |C_M #0#192, 1, "S";
     |SINO;
          |C_M #0#192, 1, "N"; #0#192 = 1; |PINTA #0#192;
     |FINSI;
|FPRC;

|PROCESO PartiArti7; |TIPO 7;
     aAlfa = #4#118;
     |C_M #0#180, 1, aAlfa;
|FPRC;

|PROCESO DobleStockArti7; |TIPO 7;
     aAlfa = #4#117;
     |C_M #0#179, 1, aAlfa;
|FPRC;

|PROCESO DobleStockArti; |TIPO 0;
     aAlfa = #0#179;
     |C_M #0#201, 1, aAlfa;
     |SI #0#179 = "S";
          #0#204 = "N"; |PINTA #0#204;
     |FINSI;
|FPRC;

|PROCESO upc;
     #0#133 = #0#28 < #0#29;
     #0#133 = #0#133 < #0#129;
     #0#164 = #0#163 < #0#29; ||upc div
     #0#164 = #0#164 < #0#129; ||upc div
     #0#164 = #0#164 < #0#209; ||upc div
     |PINTA #0#133;
     |PINTA #0#164;
|FPRC;

|PROCESO DesApliArt;
     |PUSHV 0101 2380;
     |CLS;
     ||BLANCO 0511 2370; |CUADRO 0511 2370;
     |ABRE #219;
     #219#0 = #0#0;
     |LEE 000#219=;
     |CIERRA #219;
     #219#1 = #0#1;
     |PINPA #0#219;
     |PINDA #0#219;
     |ENTLINEAL #1#220, -2, 2, 21, 0, Min220, Max220;
     |POPV;
     |HAZ CompruDesAmpli;
|FPRC;

|PROCESO DesApliArtCom;
     |PUSHV 0101 2380;
     |CLS;
     ||BLANCO 0511 2370; |CUADRO 0511 2370;
     |ABRE #81;
     #81#0 = #0#0;
     |LEE 000#81=;
     |CIERRA #81;
     #81#1 = #0#1;
     |PINPA #0#81;
     |PINDA #0#81;
     |ENTLINEAL #1#82, -2, 2, 21, 0, Min82, Max82;
     |POPV;
     |HAZ CompruDesAmpliCom;
|FPRC;

|PROCESO impc; |TIPO 20;
     |ESTADO_CONTROL nBoton, "DISABLE";

     aAlfa1 = FSalida;
     |SI aAlfa1 = "OPCION";
         FSalida = "Opciones";
         |ACABA;
     |FINSI;
     |ABRE #4; |LEE 000#4p; |CIERRA #4;
     aUtil4 = "IPAVCGB";
     |SI #4#72 = "N"; aUtil4 = "IPAVCG";|FINSI;
     |MENU aUtil;
     aUtil2 = FSalida;
     |SI aUtil2 = "1"; |HAZ imparticu; |FINSI;
     |SI aUtil2 = "2"; |HAZ verlospro; |FINSI;
     |SI aUtil2 = "3";
          |ABRE #60;
          |SELECT #2#60;
          #60#2 = #0#0;
          |LEE 000#60=;
          |SI FSalida = 0;
               |CONSULTA_F_CLAVE #2#60, inf60, sup60;
          |SINO;
               |MENAV "0000No tiene codigo alternativo...";
          |FINSI;
          |CIERRA #60;
     |FINSI;
     |SI aUtil2 = "4";      || Des. Ampliada
          |HAZ DesApliArt;
          |ERROR;
     |FINSI;
     |SI aUtil2 = "5";      || Des. Ampliada
          |HAZ DesApliArtCom;
          |ERROR;
     |FINSI;
     |SI aUtil2 = "6";    || BMP
          orden = #0#0;
          pasa = "A";
          |SUB_EJECUTA_NP "agi00009.tab";
          orden = "";
     |FINSI;
     |SI aUtil2 = "7"; |HAZ Envase; |FINSI;

     |ESTADO_CONTROL nBoton, "ENABLE";
|FPRC;

|PROCESO imparticu; || ---------- |TIPO 4;
     |PARA;  |SI;  |HACIENDO;
          |MENU aDato;
          aDato2 = FSalida;
          |SI aDato2 = "1";  |HAZ imprefic; |FINSI;
          |SI aDato2 = "2";  |HAZ imprerel; |FINSI;
          |SI aDato2 < "1"; |O aDato2 > "2";  |SAL;  |FINSI;
     |SIGUE;
|FPRC;

|PROCESO imprefic;
     |IMPRE 1;
     |SI 0 = FSalida;
          |INFOR informe;
          |SI 0 = FSalida;
               |ABRE #1;
               #1#0 = #0#0;
               #1#1 = 0;
               |LEE 001#1];
               |PARA ; |SI FSalida = 0; |Y #1#0 = #0#0; |HACIENDO ;
                    |PRINT;
                    |LEE 001#1s;
               |SIGUE;
               |CIERRA #1;
               |PIEINF; |FININF;
          |FINSI;
          |FINIMP;
     |FINSI;
|FPRC;

|PROCESO impreart;
     swImp = 1;
     |PRINT;
|FPRC;

|DEFBUCLE impreart;
     #0#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL,impreart;
|FIN;

|PROCESO imprerel;
     swImp = 0;
     |IMPRE 1;
     |SI 0 = FSalida;
          |INFOR informe2;
          |SI 0 = FSalida;
               |BUCLE impreart;
               |SI swImp = 1;
                    |PIEINF;
               |FINSI;
               |FININF;
          |FINSI;
          |FINIMP;
     |FINSI;
|FPRC;

|PROCESO totalart; |TIPO 0;
     mes = Campo - 34;
     i = mes $ 5;
     |SI i = 0; #0Campo = #0Campo ! #4#8; |FINSI;
     mes = Campo - 32;
     i = mes $ 5;
     |SI i = 0; #0Campo = #0Campo ! #4#8; |FINSI;
     tot = 92 + i;
     #0tot = #0tot - Contenido + #0Campo;
     |PINTA #0tot;
|FPRC;

|PROCESO PvpCeroNo;
     pvp1 = #0#18;
     pvp2 = #0#19;
     pvp3 = #0#20;
     pvp4 = #0#147;
     pvp5 = #0#148;
     pvp6 = #0#149;
     |SI #0#108 = "S";
          |SI #0#105 ! 0; pvp1 = precio > #0#105; |FINSI;
          |SI #0#106 ! 0; pvp2 = precio > #0#106; |FINSI;
          |SI #0#107 ! 0; pvp3 = precio > #0#107; |FINSI;
          |SI #0#156 ! 0; pvp4 = precio > #0#156; |FINSI;
          |SI #0#157 ! 0; pvp5 = precio > #0#157; |FINSI;
          |SI #0#158 ! 0; pvp6 = precio > #0#158; |FINSI;
     |SINO;
          |SI #0#105 ! 0; pvp1 = (precio * 100) / (100 - #0#105); |FINSI;
          |SI #0#106 ! 0; pvp2 = (precio * 100) / (100 - #0#106); |FINSI;
          |SI #0#107 ! 0; pvp3 = (precio * 100) / (100 - #0#107); |FINSI;
          |SI #0#156 ! 0; pvp4 = (precio * 100) / (100 - #0#156); |FINSI;
          |SI #0#157 ! 0; pvp5 = (precio * 100) / (100 - #0#157); |FINSI;
          |SI #0#158 ! 0; pvp6 = (precio * 100) / (100 - #0#158); |FINSI;
     |FINSI;

     enTiva = #0#30;
     eaPrv = "";
     efFiva = ~;
     |HAZ IVAPrv;

     |SI #0#130 = "S"; |Y #0#105 ! 0; pvp1 = pvp1 > enIva; |FINSI;
     |SI #0#131 = "S"; |Y #0#106 ! 0; pvp2 = pvp2 > enIva; |FINSI;
     |SI #0#132 = "S"; |Y #0#107 ! 0; pvp3 = pvp3 > enIva; |FINSI;
     |SI #0#153 = "S"; |Y #0#156 ! 0; pvp4 = pvp4 > enIva; |FINSI;
     |SI #0#154 = "S"; |Y #0#157 ! 0; pvp5 = pvp5 > enIva; |FINSI;
     |SI #0#155 = "S"; |Y #0#158 ! 0; pvp6 = pvp6 > enIva; |FINSI;
|FPRC;

|PROCESO PvpCeroSi;
     |SI #0#108 = "S";
          pvp1 = precio > #0#105;
          pvp2 = precio > #0#106;
          pvp3 = precio > #0#107;
          pvp4 = precio > #0#156;
          pvp5 = precio > #0#157;
          pvp6 = precio > #0#158;
     |SINO;
          pvp1 = (precio * 100) / (100 - #0#105);
          pvp2 = (precio * 100) / (100 - #0#106);
          pvp3 = (precio * 100) / (100 - #0#107);
          pvp4 = (precio * 100) / (100 - #0#156);
          pvp5 = (precio * 100) / (100 - #0#157);
          pvp6 = (precio * 100) / (100 - #0#158);
     |FINSI;

     enTiva = #0#30;
     eaPrv = "";
     |HAZ IVAPrv;

     |SI #0#130 = "S"; pvp1 = pvp1 > enIva; |FINSI;
     |SI #0#131 = "S"; pvp2 = pvp2 > enIva; |FINSI;
     |SI #0#132 = "S"; pvp3 = pvp3 > enIva; |FINSI;
     |SI #0#153 = "S"; pvp4 = pvp4 > enIva; |FINSI;
     |SI #0#154 = "S"; pvp5 = pvp5 > enIva; |FINSI;
     |SI #0#155 = "S"; pvp6 = pvp6 > enIva; |FINSI;
|FPRC;

|PROCESO pvps1;|TIPO 0;
     |SI #0#108 =  "S";|O #0#108 = "G";
          |HAZ CalPre019;
     |FINSI;
|FPRC;

|PROCESO pre;|TIPO 0;
     |SI #0Campo ! Contenido;
          |HAZ pvps1;
     |FINSI;
|FPRC;

|PROCESO altmacen; |TIPO 3;
     |MODULO nombrep;
     |HAZ verlinea;
     |HAZ verfamili;
     |HAZ versubfam;
     aAlfa = #0#5; |QBF aAlfa;
     |SI aAlfa = ""; |Y #110#90 = "S"; |Y #110#141 = "O";
          |MENAV "0000Atencion Unidad Medida en Blanco...";
     |FINSI;
     aAlfa = #0#126; |QBF aAlfa;
     |SI aAlfa = ""; |Y #110#90 = "S"; |Y #110#141 = "O";
          |MENAV "0000Atencion Naturaleza en Blanco...";
     |FINSI;
     modo = (FEntrada / 100) ! 0;
     |SI modo = 2;
          |ABRE #219;                 || abre descip. ampliada
          #219#0 = #0#0;
          |LEE 110#219=;              || busca el articulo
          |SI FSalida = 0;
               |SI #219 ! #0#1;       || si ha cambiado la descripcion
                    #219#1 = #0#1;
                    |GRABA 020#219a;  || la actualiza
               |FINSI;
          |FINSI;
          |LIBERA #219;
          |CIERRA #219;
     |FINSI;
     |SI modo = 1; |Y nombrep = "goarticu";
          |ABRE #1;
          #1#0  = #0#0;
          #1#1  = 1;
          |LEE 010#1=;
          |SI FSalida ! 0;
               |DDEFECTO #1;
               #1#0  = #0#0;
               #1#1  = 1;
               #1#40 = #0#1;
               |GRABA 020#1n;
          |FINSI;
          |CIERRA #1;
     |FINSI;
     #0#123 = "N";            ||Como es tipo 3 es en modificacion
     #0#124 = "00.00.0000";

     |MODULO aprograma;
     |SI tpcontro = 1;|Y aprograma ! "agitp001";
          |SI #agifa142#210 = "S";
               |ABRE #14;
               #14#0 = #0#27;
               #14#1 = #0#0;
               |LEE 001#14=;
               |SI FSalida = 0;
                  #14#3 = #0#163; ||Upc en divisa
                  #14#8 = #0#165; ||Divisa de upc
                  #14#9 = #0#28;  ||Upc en base
                  #14#4 = #0#29;  ||Dto 1
                  #14#5 = #0#129; ||Dto 2
                  #14#10 = #0#209; ||Dto 3
                  |GRABA 001#14a;
               |SINO;
                  #14#3 = #0#163; ||Upc en divisa
                  #14#8 = #0#165; ||Divisa de upc
                  #14#9 = #0#28;  ||Upc en base
                  #14#4 = #0#29;  ||Dto 1
                  #14#5 = #0#129; ||Dto 2
                  #14#10 = #0#209; ||Dto 3
                  #14#7 = #0#1;   ||Descripcion
                  |GRABA 001#14n;
               |FINSI;
               |CIERRA #14;
          |FINSI;
     |FINSI;

     |SI enModoArt = 1;  |Y #910#2 = 4;
         |ABRE #919;
         #919#0 = eAlfaC;
         #919#1 = #0#0;
         #919#2 = #0#1;
         |GRABA 020#919n;
         |CIERRA #919;
     |FINSI;
|FPRC;

|PROCESO bajmacen; |TIPO 1;
     modo = (FEntrada / 100) ! 0;
     |SI modo = 3;
          |ABRE #1; |ABRE #904;
          #1#0 = #0#0;
          #1#1 = 0;
          |LEE 101#1];
          |PARA ; |SI FSalida = 0;|Y #1#0 = #0#0; |HACIENDO ;
               #904#0 = #1#0;
               #904#1 = #1#1;
               #904#2 = "";
               |LEE 101#904];
               |PARA; |SI FSalida = 0; |Y #904#0 = #1#0; |Y #904#1 = #1#1; |HACIENDO;
                    |BORRA 020#904a;
                    |LEE 101#904s;
               |SIGUE;
               |BORRA 020#1a;
               |LEE 101#1s;
          |SIGUE;
          |CIERRA #1; |CIERRA #904;
     |FINSI;
|FPRC;

|PROCESO defec4; |TIPO 0;
     |ABRE #4; |LEE 000#4p; |CIERRA #4;
     modo = (FEntrada / 100) ! 0;

     |SI #4#30 ! "N";
          |CAMPO_MODIFICA #0#9 , 1 , "S";
     |SINO;
          |CAMPO_MODIFICA #0#9 , 1 , "N";
     |FINSI;

     |SI modo = 1; |Y Campo = 0;
         |ABRE #agifa321;
         |LEE 001#agifa321.p;
         |CIERRA #agifa321;
         #agifa019#165 = #agifa321#9; ||Primera vez moneda div = moneda base
     |FINSI;


     ||------------Pone Ult. Proveedor
     aConfir = #4#76;
     calpre  = #4#60;

     |HAZ UltProv;
|FPRC;

|PROCESO tex1; |TIPO 14;
     #0#122 = &178 * 4;
     |CAMPO_MODIFICA #0#122 , 1 , "S";
     |CAMPO_VISUAL #0#122 , 1 , "S";
|FPRC;

|PROCESO nomodi;
     |CAMPO_MODIFICA #0#110 , 1 , "N"; |CAMPO_VISUAL #0#110 , 1 , "N";
|FPRC;

|PROCESO decs; |TIPO 0;
     #0Campo = #0Campo ! #4#8;
     |PINTA #0Campo;
|FPRC;

|PROCESO ValoraAlma;
     #1#3 = #1#5 - #1#4;
     |SI #1#3 > 0;
          #1#3 = #1#3 * #0#9;
     |SINO;
          #1#3 = 0;
     |FINSI;
     #1#40 = #0#1;       || Descripcion
     #1#50 = #1#42 + #1#6 - #1#43 - #1#39; || necesidades
     #1#50 = #1#50 - #1#4 - #1#9 - #1#8;
     |GRABA 020#1a;
|FPRC;

|DEFBUCLE ValoraAlma;
     #1#1;
     ;
     #0#0, 00001;
     #0#0, 99999;
     be;
     NULL, ValoraAlma;
|FIN;

|PROCESO dsl00001_1;
     |BORRA 020#dsl00001.a;
     |LIBERA #dsl00001;
|FPRC;

|DEFBUCLE dsl00001;
     #dsl00001#3;
     ;
     #agifa019#0, "             ";
     #agifa019#0, "zzzzzzzzzzzzz";
     be;
     NULL, dsl00001_1;
|FIN;

|PROCESO copia2; |TIPO 2;
     enSwAltaArt = (FEntrada/100)!0; || Para el control de empresas

     eaDocu = "agifa019";
     |HAZ ControlUsu;
     |SI enSwCtrlUsu ! 0;
          |ERROR; |ACABA;
     |FINSI;

     aAlfa = #0#195; |QBF aAlfa;
     |SI aAlfa = ""; #0#195 = aAlfa; |FINSI;

     |SI enModoArt = 3;|Y #910#2 = 4;
         |ABRE #919;
         #919#0 = eAlfaC;
         |BORRA 020#919c;
         |CIERRA #919;
     |FINSI;

     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 3;
         |BUCLE dsl00001;
     |FINSI;

     #0#10 = #0#6 - #0#17;              || Valor Stock
     |SI #0#10 > 0;
          #0#10 = #0#10 * #0#9;
     |SINO;
          #0#10 = 0;
     |FINSI;

     |MODULO aProg;
     |SI aProg = "goarticu";
          |BUCLE ValoraAlma;
     |FINSI;

     |HAZ defec4;

     |SI #4#28 = "S";          ||컴컴컴횮opia Empresa
          modo = (FEntrada / 100) ! 0;
          mes = 0 +. 1;
          |SI mes = 1; |O modo = 3;
               |SI mes = 1;
                    |GRABA 000#0a;
                    |SI FSalida = 112; |GRABA 020#0b; |FINSI;
                    |LIBERA #0;
               |FINSI;
               registro = #0#0;
               |CIERRAT #0;
               |SUB_EJECUTA_NP "agifa201.run";
               |ABRET #0;
               #0#0 = registro;
               |SI mes = 1;
                    |LEE 101#0=;
               |SINO;
                    |LEE 001#0=;
               |FINSI;
          |FINSI;
     |FINSI;
     |SI nModo = 1; |Y swCar1 = 1;
          |PUSHV 0501 2380;
          |ABRE #30;
          #30#0 = #0#0;
          |LEE 101#30=;
          nSal = FSalida;
        |ET eVic;
          |PINPA #0#30;
          #30#0 = #0#0;
          #30#2 = #0#1;
          |PINDA #0#30;
          |ENDATOS #1#30;
          |SI FSalida ! 0;
               |VETE eVic;
          |SINO;
               |SI nSal = 0; |GRABA 020#30a; |FINSI;
               |SI nSal ! 0; |GRABA 020#30n; |FINSI;
          |FINSI;
          |LIBERA #30;
          |CIERRA #30;
          |POPV;
     |FINSI;
|FPRC;

|PROCESO salta_exa; |TIPO 7;
     modo = (FEntrada / 100) ! 0;
     |SI modo = 1; |O modo = 2;
          |CAMPO_MODIFICA #0#123 , 1 , "N";
          |CAMPO_MODIFICA #0#124 , 1 , "N";
     |FINSI;
|FPRC;

|PROCESO salta_exa1; |TIPO 0;
     |SI modo = 1; |O modo = 2;
          |CAMPO_MODIFICA #0#123 , 1 , "S";
          |CAMPO_MODIFICA #0#124 , 1 , "S";
     |FINSI;
|FPRC;


|PROCESO UltProv;
     |SI #4#43 = "S"; |Y #4#44 > 0;
          ||DESGLOSA EL CODIGO DEL ARTICULO, Y SACA EL CODIGO DEL PROVEEDOR.
          i = 100 + #4#44;
          xx = (#0#0 % i);
          xx = "000000" + xx; |QBF xx;
          xx = (xx % -106);
          #0#27 = xx;
          || R.AGUERA 28-07-98
          || LEE LA FICHA DE DEFECTOS POR PROVEEDOR, Y RELLENA LOS DEFECTOS.
          modo = (FEntrada / 100) ! 0;
          |SI modo = 1;
               |ABRE #99;
               #99#0 = #0#27;
               |LEE 000#99=;
               |SI FSalida = 0;
                    #0#109 = #99#2;     || PTS.REDONDEO.
                    #0#108 = #99#3;     || CONFIRMA AUMENTOS.
                    #0#105 = #99#4;     || % INCREMENTO 1.
                    #0#106 = #99#5;     || % INCREMENTO 2.
                    #0#107 = #99#6;     || % INCREMENTO 3;
                    #0#156 = #99#7;     || % INCREMENTO 4.
                    #0#157 = #99#8;     || % INCREMENTO 5.
                    #0#158 = #99#9;     || % INCREMENTO 6;

               |FINSI;
               |CIERRA #99;
          |FINSI;
     |FINSI;
|FPRC;

|RUTINA inf60;
     #60#2 = #0#0;
|FRUT;

|RUTINA sup60;
     #60#2 = #0#0;
|FRUT;

|RUTINA Min220;
     #220#0 = #0#0;
     #220#1 = 1;
|FRUT;

|RUTINA Max220;
     #220#0 = #0#0;
     #220#1 = 999;
|FRUT;

|RUTINA Min82;
     #82#0 = #0#0;
     #82#1 = 1;
|FRUT;

|RUTINA Max82;
     #82#0 = #0#0;
     #82#1 = 999;
|FRUT;

|PROCESO CompruDesAmpli;
     |ABRE #219;
     |ABRE #220;
     #220#0 = #0#0;
     #220#1 = 1;
     |LEE 000#220];
     |SI FSalida = 0; |Y #220#0 = #0#0;
          #219#0 = #0#0;
          |LEE 000#219=;
          |SI FSalida ! 0;
               #219#1 = #0#1;
               #219#2 = "S";
               |GRABA 020#219n;
          |FINSI;
     |SINO;
          #219#0 = #0#0;
          |BORRA 000#219c;
     |FINSI;
     |CIERRA #220;
     |CIERRA #219;
|FPRC;

|PROCESO CompruDesAmpliCom;
     |ABRE #81;
     |ABRE #82;
     #82#0 = #0#0;
     #82#1 = 1;
     |LEE 000#82];
     |SI FSalida = 0; |Y #82#0 = #0#0;
          #81#0 = #0#0;
          |LEE 000#81=;
          |SI FSalida ! 0;
               #81#1 = #0#1;
               #81#2 = "S";
               |GRABA 020#81n;
          |FINSI;
     |SINO;
          #81#0 = #0#0;
          |BORRA 000#81c;
     |FINSI;
     |CIERRA #82;
     |CIERRA #81;
|FPRC;

|PROCESO ModiDesComp; |TIPO 0;
     |SI #0#195 ! Contenido; eaConte2 = Contenido; |FINSI;
|FPRC;

|PROCESO sal6; |TIPO 6;
     |SI #0#1 ! Contenido; eaConte1 = Contenido; |FINSI;
     nModo = (FEntrada / 100) ! 0;
     salida = FSalida;
     |MODULO aProg;

     |SI nModo = 1; |Y aProg ! "agica001";
          |ABRE #60;
          #60#0 = #0#0;
          |LEE 000#60=;
          |SI FSalida = 0;
               |MENAV "0000Codigo existente en Codigos Alternativos...";
               |ERROR; |ACABA;
          |FINSI;
          |CIERRA #60;
     |FINSI;

     |HAZ Calc6;

     |SI aDesAnt ! #0#1; |Y Campo = 1;
          |SI nModo = 1;
               FSalida = 0;
          |SINO;
               |CONFI "0000SCambiar descripcion proveedor?";
          |FINSI;
          |SI FSalida = 0; #0#195 = #0#1; |FINSI;
          |PINTA #0#195;
          aDesAnt = #0#1;
     |FINSI;

     FSalida = salida;
|FPRC;

________________________/ TEXTO LINEAS.......................................
|RUTINA mini_f;
     #80#0 = #0#2;
     #80#1 = 001;
|FRUT;

|RUTINA maxi_f;
     #80#0 = #0#2;
     #80#1 = 200;
|FRUT;

|PROCESO texto_fam; TIPO 0;
     |SI FSalida = 10;
          |ABRET #80;
          |PUSHV 0501 2380;
          |PINPA #0#80;
          |PARA ind = 1; |SI ind [ 200; |HACIENDO ind = ind + 1;
               #80#0 = #0#2;
               #80#1 = ind;
               |LEE 141#80=;
               |SI FSalida ! 0;
                    #80#2 = "";
                    |GRABA 021#80n;
               |FINSI;
               |LIBERA #80;
          |SIGUE;
          |MENSA "0000< ESC > SALIR.";
          |ENTLINEAL #1#80,1,4,21,1,mini_f,maxi_f;
          |PARA ind = 200; |SI ind ] 1; |HACIENDO ind = ind - 1;
               #80#0 = #0#2;
               #80#1 = ind;
               |LEE 141#80=;
               |SI FSalida = 0;
                    alfa = #80#2;
                    |QBF alfa;
                    |SI alfa ! ""; |SAL; |FINSI;
                    |BORRA 021#80a;
               |FINSI;
               |LIBERA #80;
          |SIGUE;
          |POPV;
          |CIERRAT #80;
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO verfamili; |TIPO 0;
     |SI #4#156 = "N";
          aAlfa33 = #0#2;
          |QBF aAlfa33;
          |SI aAlfa33 = "";
               |MENAV "0000Atencion Cod.Familia en Blanco";
               |ERROR;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO versubfam; TIPO 0;
     |SI #4#158 = "N";
          aAlfa33 = #0#3;
          |QBF aAlfa33;
          |SI aAlfa33 = "";
               |MENAV "0000Atencion Cod.SubFamilia en Blanco";
               |ERROR;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO verlinea; TIPO 0;
     |SI #4#157 = "N";
          aAlfa33 = #0#125;
          |QBF aAlfa33;
          |SI aAlfa33 = "";
               |MENAV "0000Atencion Cod.Linea en Blanco";
               |ERROR;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Envase;
     |SI #4#72 = "N";
          |MENAV "0000Bultos no activo en parametros generales";
          |ACABA;
     |FINSI;

     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 1;
          FSalida = 0;
     |SINO;
          |LEE 101#0=;
     |FINSI;

     |LEE 101#0=;
     |SI FSalida = 0;
          Cascada    = #0#138;
          CodEnvase1 = #0#139;
          FacEnvase1 = #0#4;
          CodEnvase2 = #0#141;
          FacEnvase2 = #0#142;
          CodEnvase3 = #0#143;
          FacEnvase3 = #0#144;
          CodEnvase4 = #0#145;
          FacEnvase4 = #0#146;
          |SUB_EJECUTA_NP "agiw0003";
          #0#4   = FacEnvase1;
          #0#138 = Cascada;
          #0#139 = CodEnvase1;
          #0#140 = FacEnvase1;
          #0#141 = CodEnvase2;
          #0#142 = FacEnvase2;
          #0#143 = CodEnvase3;
          #0#144 = FacEnvase3;
          #0#145 = CodEnvase4;
          #0#146 = FacEnvase4;
          |GRABA 020#0a;
     |FINSI;
|FPRC;

|PROCESO decdiv; |TIPO 0;
  |ABRE #agifa324;
  #agifa324#0 = #agifa019#165;
  |LEE 001#agifa324.=;
  nDeci_PreDiv = #agifa324#9;
  cambio_div = #agifa324#3;
  |ABRE #agifa321;
  |LEE 001#agifa321.p;
  #agifa324#0 = #agifa321#9; ||Igualo a moneda base
  |LEE 001#agifa324.=;
  nDeci_Base = #agifa324#7;  ||Decimales moneda base
  nDeci_PreBase = #agifa324#9;  ||Decimales precio de moneda base
  cambio_base = #agifa324#3;
  |CIERRA #agifa321;
  |CIERRA #agifa324;
  |SI #0Campo ! Contenido; ||Aqui porque debe leer al entrar en articulo
    |SI Campo = 165;|O Campo = 28;  ||Estoy en campo divisa de UPC o UPC Base
      #0#163 = #0#28 / cambio_base * cambio_div; ||U.P.C Div
      #0#164 = #0#133 / cambio_base * cambio_div; ||U.P.C Div con Dto
    |FINSI;
    |SI Campo = 163; ||Estoy en UPC Divisa
      #0#28 = #0#163 / cambio_div * cambio_base;  ||U.P.C
      #0#164 = #0#163 < #agifa019#29;
      #0#164 = #0#164 < #agifa019#129;
      #0#133 = #0#164 / cambio_div * cambio_base; ||U.P.C con Dto
    |FINSI;
    |SI Campo = 164; ||Estoy en UPC Div con Dto
      #0#133 = #0#164 / cambio_div * cambio_base;  ||U.P.C con Dto
    |FINSI;
    |SI Campo = 133; ||Estoy en UPC con Dto
      #0#164 = #0#133 / cambio_div * cambio_base; ||U.P.C Div con Dto
    |FINSI;
    |PINTA #0#163;
    |PINTA #0#164;
    |PINTA #0#28;
    |PINTA #0#133;
  |FINSI;
|FPRC;

|PROCESO div13; |TIPO 13; ||Miro parametros de divisas y decimales divisas
     modo = (FEntrada / 100) ! 0;
     |MODULO aprograma;
     |SI aprograma = "goarticu"; |HAZ decdiv; |FINSI;
     |SI #4#138 = "S";
          |C_V #0#9, 1, "N";
          |C_V #0#28, 1, "N";
          |C_V #0#133, 1, "N";
          |C_V #0#163, 1, "N";
          |C_V #0#164, 1, "N";
     |FINSI;
|FPRC;

|PROCESO ControlPro; |TIPO 7;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 4;
          |C_M #0#27, 1 , "N";
     |SINO;
          |C_M #0#27, 1 , "S";
     |FINSI;
|FPRC;

|PROCESO tpcont;|TIPO 0;
  |SI #0Campo ! Contenido;
     tpcontro = 1;
  |FINSI;
|FPRC;

|PROCESO Tabla7; |TIPO7;
     |C_M #0#177, 1, "S";
     |SI #0#176 = "N";
         |C_M #0#177, 1, "N";
         #0#177 = "";  |PINTA #0#177;
         |ACABA;
     |FINSI;

     |ABRE #910;
     |LEE 040#910p;
     |SI FSalida ! 0;  |DDEFECTO #910;  |FINSI;
     |CIERRA #910;

     |C_M #0#177, 1, "N";
     |SI #910#44 = "T";  |C_M #0#177, 1, "S";  |FINSI;
|FPRC;

|PROCESO Tabla0; |TIPO 0;
     |C_M #0#177, 0, aAlfa;
     |SI aAlfa = "N";  |ACABA;  |FINSI;

     |ABRE #915;
     #915#0 = #0#177;
     #915#1 = "";
     #915#2 = "";
     #915#5 = "";
     |LEE 040#915=;
     |SI FSalida ! 0;
         |MENAV "       Tabla inexistente";
         |ERROR;
     |FINSI;
     |CIERRA #915;
|FPRC;

|PROCESO CodBarra;  |TIPO 7;
     |ABRE #910;
     |LEE 040#910p;
     |SI FSalida ! 0;  |DDEFECTO #910;  |FINSI;
     |CIERRA #910;

     |C_M #0#175, 1, "S";
     |C_M #0#176, 1, "S";

     |SI #910#1  = "N";  |C_M #0#175, 1, "N";  |FINSI;
     |SI #910#19 = "N";  |C_M #0#176, 1, "N";  |FINSI;

     |SI #910#1  ! "S";  |ACABA;  |FINSI;

     |SI #0#128 = "                    ";
         |SI #910#33 = "S";
             eAlfa = #0#0;
             |SUB_EJECUTA_NP "dsz99990;B";
             #0#128 = eAlfa;
         |SINO;
             #0#128 = #0#0;
         |FINSI;
         |PINTA #0#128;
     |FINSI;
|FPRC;

|PROCESO Calc6;  |TIPO 6;
     salida = FSalida;

     |ABRE #910;
     |LEE 040#910p;
     |SI FSalida ! 0;  |DDEFECTO #910;  |FINSI;
     |CIERRA #910;

     |C_M #0#175, 1, "S";
     |C_M #0#176, 1, "S";

     |SI #910#1  = "N";  |C_M #0#175, 1, "N";  |FINSI;
     |SI #910#19 = "N";  |C_M #0#176, 1, "N";  |FINSI;

     enAlta = 1;
     eaArticulo = #0Campo;
     |HAZ LlenaArticulo;
     |SI FSalida = 0;
          #0Campo = eaArticulo;
          |PINTA #0Campo;
     |SINO;
          |ERROR; |ACABA;
     |FINSI;
|FPRC;

|PROCESO PideArt;  |TIPO 7;
|ACABA;
/*****************************************
     nModo = (FEntrada / 100) ! 0;
          |C_M #0#2,   1, "S";
          |C_M #0#3,   1, "S";
          |C_M #0#125, 1, "S";
          |C_M #0#127, 1, "S";
          |SI nModo = 1;
               eaParCod = " ";          ||OJO UN ESPACIO si retorna el espacio
               |SUB_EJECUTA "goartic0"; ||es que no esta activo en 'goparame'
               #0#0 = eaParCod;
               #0#125 = eaParSec;
               #0#2 = eaParFam;
               #0#3 = eaParSub;
               #0#127 = eaParCap;
               |SI eaParCod = ""; |O eaParCod = " ";
                    |SI eaParCod = ""; |ERROR; |FINSI;
                    |||| SI ES UN ESPACIO LO DEJAMOS CONTINUAR
               |SINO;
                    |C_M #0#2,   1, "N";
                    |C_M #0#3,   1, "N";
                    |C_M #0#125, 1, "N";
                    |C_M #0#127, 1, "N";
                    |PTEC 802;
                    |PINTA #0#0;
                    |PINTA #0#125;
                    |PINTA #0#2;
                    |PINTA #0#3;
                    |PINTA #0#127;
               |FINSI;
          |FINSI;
*/
|FPRC;

|PROCESO PintaFam;
     |ABRE #9;
     #9#0 = #0#2;
     |LEE 000#9=;
     |SI FSalida ! 0; |DDEFECTO #9; |FINSI;
     |CIERRA #9;
     |ABRE #10;
     #10#0 = #0#3;
     |LEE 000#10=;
     |SI FSalida ! 0; |DDEFECTO #10; |FINSI;
     |CIERRA #10;
     |PINTA 2145, #9#1;
     |PINTA 2245, #10#1;
|FPRC;

|PROCESO PintaT; |TIPO 14;
     |SI #0#179 = "S";
          |ABRE #238;
          #238#0 = #0#201;
          |LEE 000#238=;
          |CIERRA #238;
     |FINSI;

     |ATRI I;
     |SI #0#179 = "S"; |Y #238#8 > 0; |Y #4#117 = "S";
          |PINTA 2203, "Medidas:         x         x         ";
          |PINTA 2212, #238#9;
          |PINTA 2222, #238#10;
          |PINTA 2232, #238#11;
     |SINO;
          |PINTA 2203, "                                     ";
     |FINSI;
     |ATRI 0;

     ||컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
     |ATRI N;
     |SI #0#179 = "S";
          aTx1 = "P.Servir  " + #238#5 + ":";
          aTx2 = "P.Servir  " + #238#4 + ":";
          aTx3 = "P.Recibir " + #238#5 + ":";
          aTx4 = "P.Recibir " + #238#4 + ":";
          aTx5 = "S.Depo " + #238#5 + ":";
          aTx6 = "S.Depo " + #238#4 + ":";
          aTx7 = "Total " + #238#5 + ":";
          aTx8 = "Total " + #238#4 + ":";
          aTx9 = "Dispo " + #238#5 + ":";
          aTx0 = "Dispo " + #238#4 + ":";
          |C_V #0#182, 1, "S";
          |C_V #0#183, 1, "S";
          |C_V #0#184, 1, "S";
          |C_V #0#185, 1, "S";
          |C_V #0#193, 1, "S";
     |SINO;
          aAlfa = " " * 10;
          aTx1 = "P.Servir.....:";
          aTx2 = "              " + aAlfa;
          aTx3 = "P.Recibir....:";
          aTx4 = "              " + aAlfa;
          aTx5 = "S.Depo....:";
          aTx6 = "           " + aAlfa;
          aTx7 = "Total....:";
          aTx8 = "          " + aAlfa;
          aTx9 = "Dispo....:";
          aTx0 = "          " + aAlfa;
          |C_V #0#182, 1, "N";
          |C_V #0#183, 1, "N";
          |C_V #0#184, 1, "N";
          |C_V #0#185, 1, "N";
          |C_V #0#193, 1, "N";
     |FINSI;

     |PINTA 1503, aTx1;
     |PINTA 1603, aTx2;
     |PINTA 1703, aTx3;
     |PINTA 1803, aTx4;
     |PINTA 1630, aTx5;
     |PINTA 1730, aTx6;
     |PINTA 1754, aTx7;
     |PINTA 1854, aTx8;
     |PINTA 1954, aTx9;
     |PINTA 2054, aTx0;
     |ATRI I;

     |ABRE #910;
     |LEE 040#910p;
     |SI FSalida ! 0;  |DDEFECTO #910;  |FINSI;
     |CIERRA #910;

     |SI #910#33 = "S";
         eAlfa = #0#0;
         |SUB_EJECUTA_NP "dsz99990;V";
     |SINO;
         eAlfa1 = "";
         eAlfa2 = "";
         eAlfa3 = "";
         eAlfa4 = "";
         eAlfa  = "";
     |FINSI;

     |SI #910#2 = 4;
         |ABRE #919;
         |SELECT #3#919;
         #919#1 = #0#0;
         |LEE 040#919=;
         |SI FSalida ! 0;  |DDEFECTO #919;  |FINSI;
         |CIERRA #919;

         |PINPA #0#agipan00;
         |PINTA 2202, #919#0;
         |ACABA;
     |SINO;
          |HAZ PintaFam;
     |FINSI;

     |SI #910#8 = 0;  |Y #910#12 = 0;  |Y #910#16 = 0;
          |HAZ PintaFam;
          |ACABA;
     |FINSI;

     |PINPA #0#agipan00;

     eAlfa4 = eAlfa4 %  115;

     |SI #0#175 = "S";
         |ATRI I;
         |PINTA 2202, eAlfa1;
         |PINTA 2223, eAlfa2;
         |PINTA 2244, eAlfa3;
         |PINTA 2265, eAlfa4;
         |ATRI i;

         |ATRI R;
         |PINTA 2102, #910#3;
         |PINTA 2123, #910#7;
         |PINTA 2144, #910#11;
         |PINTA 2165, #910#15;
         |ATRI r;
     |FINSI;

     |ATRI I;
|FPRC;

CODIGO DE BARRAS ANTIGUO

|PROCESO calcodb;|TIPO 0;
     |SI #910#33 = "S";  |ACABA;  |FINSI;

     |SI FSalida = 9;    ||F1
          i1 = #0#128 % 101;
          i2 = #0#128 % 301;
          i3 = #0#128 % 501;
          i4 = #0#128 % 701;
          i5 = #0#128 % 901;
          i6 = #0#128 % 1101;
          p1 = #0#128 % 201;
          p2 = #0#128 % 401;
          p3 = #0#128 % 601;
          p4 = #0#128 % 801;
          p5 = #0#128 % 1001;
          p6 = #0#128 % 1201;
          sumai = i1 + i2 + i3 + i4 + i5 + i6;
          sumap = p1 + p2 + p3 + p4 + p5 + p6;
          sumap = sumap * 3;
          total = sumai + sumap;
          base = total / 10;
          resto = total $ 10;
          |SI resto ! 0;
               resto = resto / 10;
               base = (base - resto) + 1;
               base = base * 10;
          |SINO;
               base = base * 10;
          |FINSI;
          digito = base - total;
          calcodb = #0#128;
          |QBF calcodb;
          calcodb = calcodb + "000000000000000";
          calcodb = calcodb % 0112;
          calcodb = calcodb + digito;
          |QBF calcodb;
          #0#128 = calcodb;
          |PINTA #0#128;
     |FINSI;
|FPRC;

|PROCESO MasCodB; |TIPO 0;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 4; |ACABA; |FINSI;

     |SI #910#33 = "S";  |ACABA;  |FINSI;

     |SI Contenido ! #agifa019#128;
          |ABRE #dsl00001;
          |SELECT #2#dsl00001;

          #dsl00001#3 = Contenido;      || borra el anterior (si existe)
          |LEE 101#dsl00001.=;
          |SI FSalida = 0;
               |BORRA 020#dsl00001.a;
          |FINSI;
          |LIBERA #dsl00001;

          #dsl00001#3 = #agifa019#128;  || graba o actualiza el nuevo
          |LEE 000#dsl00001.=;
          |SI FSalida ! 0;
               |SELECT #1#dsl00001;
               |LEE 000#dsl00001.u;
               |SI FSalida = 0;
                    nContador = #dsl00001#0 + 1;
               |SINO;
                    nContador = 1;
               |FINSI;
               |DDEFECTO #dsl00001;
               #dsl00001#0 = nContador;
               #dsl00001#1 = #agifa019#0;
               #dsl00001#2 = #agifa019#1;
               #dsl00001#3 = #agifa019#128;
               |GRABA 020#dsl00001.n;        || graba si no existe
          |SINO;
               |SI #dsl00001#1 ! #agifa019#0;
                    aAlfa = "0000Codigo de Barras " + &13 + &10 + "asignado a [" + #dsl00001#1 + "].";
                    |MENAV aAlfa;
                    |ERROR;
               |FINSI;
          |FINSI;
          |CIERRA #dsl00001;
     |FINSI;
|FPRC;

|PROCESO Pulsab; |TIPO 6;
     |SI FSalida = 14;
          |HAZ impc;
          |ERROR;
          FSalida = 14;
     |FINSI;
|FPRC;

|PROCESO PulsaF6;
     |PTEC 828;
|FPRC;

|PROCESO T88; |TIPO 88;
|FPRC;

|PROCESO t81; |TIPO 8;
     |MODULO aAlfa;
     |SI aAlfa = "goarticu";
          |HAZ PideAlmaCompra;
     |FINSI;

     |CONTROL_SIMPLE 0, "BOTON,F6.- Opciones", 0561, 0573, PulsaF6;
     nBoton = FSalida;
     |ESTADO_CONTROL nBoton, "DISABLE";
|FPRC;

|PROCESO t91; |TIPO 9;
     |FIN_CONTROL_WINDOWS nBoton;
     |PULSATECLA;
|FPRC;

|PROCESO GrabaLin2;
     |DDEFECTO #8;
     #8#0 = aPro;
     |LEE 000#8=;
     |DDEFECTO #6;
     #6#0 = aPro;
     |LEE 000#6=;
     nSal = FSalida;
     #6#1 = #8#1;
     #6#2 = fFec;
     #6#3 = nCan;
     #6#4 = nPre;
     #6#5 = aSer;
     #6#6 = nDoc;
     #6#7 = nLdo;
     |SI nSal ! 0; |GRABA 020#6n; |FINSI;
     |SI nSal = 0; |GRABA 020#6a; |FINSI;
|FPRC;

|PROCESO aagifa065;
     |SI nSw = 0;
          aPro = #7#10;
          nSw  = 1;
     |FINSI;
     |SI aPro ! #7#10;
          |HAZ GrabaLin2;
          aPro = #7#10;
     |FINSI;
     fFec = #7#1;
     nCan = #7#6;
     nPre = #7#14;
     aSer = #7#11;
     nDoc = #7#3;
     nLdo = #7#4;
|FPRC;

|DEFBUCLE aagifa065;
     #7#1;
     ;
     #0#0,"00.00.0000","AC",000000,00000,"     ";
     #0#0,"31.12.9999","AC",999999,99999,"zzzzz";
     ;
     NULL,aagifa065;
|FIN;

|PROCESO verlospro;
     |HAZ CreaTempo; aTempo6 = Tempo;
     |NOME_DAT #6 aTempo6; |DELINDEX #6;
     |ABRE #6;
     |ABRE #8;
     nSw = 0;
     nDeci_PreBaseMx = nDeci_PreBase;
     |BUCLE aagifa065;
     |SI nSw = 1;
          |HAZ GrabaLin2;
     |FINSI;
     |CIERRA #8;
     |CIERRA #6;
     |PUSHV 0501 2380;
     |ENTLINEAL #1#6 , -1 , 4 , 22 , 1 , inf322 , sup322;
     |POPV;
     |DELINDEX #6; Tempo = aTempo6; |HAZ BoraTempo;
|FPRC;

|RUTINA inf322;
     #6#0 = "      ";
|FRUT;

|RUTINA sup322;
     #6#0 = "zzzzzz";
|FRUT;

|PROCESO CmpMulti; |TIPO 0;
     |SI FSalida = 10;
          |HAZ MultiF2;
          |ACABA;
     |FINSI;
     |SI FSalida = 2; |ACABA; |FINSI;
     |SI #0#204 = "S"; |Y #0#5 ! #0Campo;
          |ABRE #241;
          |SELECT #2#241;
          #241#0 = #0#5;
          #241#2 = #0Campo;
          |LEE 000#241=;
          |SI FSalida ! 0;
               |MENAV "0000Codigo no definido...";
               |ERROR;
          |FINSI;
          |CIERRA #241;
     |FINSI;
|FPRC;

|PROCESO UniMulti_0; |TIPO 0;
     aAlfa = #0#204;
     |C_M #0#205, 1, aAlfa;
     |C_M #0#206, 1, aAlfa;
     |C_M #0#207, 1, aAlfa;
     |SI #0#204 = "S";
          #0#179 = "N"; |PINTA #0#179;
     |SINO;
          #0#205 = #0#5; |PINTA #0#205;
          #0#206 = #0#5; |PINTA #0#206;
          #0#207 = #0#5; |PINTA #0#207;
     |FINSI;
|FPRC;

|PROCESO UniMulti_7; |TIPO 7;
     aAlfa = #4#163;
     |C_M #0#204, 1, aAlfa;
|FPRC;

|PROCESO MinMulti;
     #241#0 = #0#5;
     #241#1 = 1;
|FPRC;

|PROCESO MaxMulti;
     #241#0 = #0#5;
     #241#1 = 999;
|FPRC;

|PROCESO MultiF2; |TIPO 0;
     |SI FSalida ! 10; |ACABA; |FINSI;
     |ABRE #241;
     |ABRE #240;
     #240#0 = #0#5;
     |LEE 000#240=;
     |SI FSalida ! 0;
          |ABRE #239;
          #239#0 = #0#5;
          |LEE 000#239=;
          |CIERRA #239;

          #240#1 = #239#1;
          |GRABA 020#240n;

          #241#0 = #240#0;
          #241#1 = 1;
          #241#2 = #241#0;
          #241#3 = #239#2;
          #241#4 = #239#1;
          #241#5 = 1;
          #241#6 = "D";
          |GRABA 020#241n;
     |FINSI;
     |CIERRA #240;
     |CIERRA #241;
     |PUSHV 0101 2380;
     |PINPA #0#240;
     |PINDA #0#241;
     |PINDA #0#240;
     |ENDATOS #2#240;
     |POPV;
     |ERROR;
|FPRC;

|PROCESO MultiF11; |TIPO 66;
     |ABRE #241;
     |CONSULTA_F_CLAVE #1#241, MinMulti, MaxMulti;
     |SI FSalida = 0;
          #0Campo = #241#2; |PINTA #0Campo
     |SINO;
          |ERROR;
     |FINSI;
     |CIERRA #241;
|FPRC;

|PROCESO T30; |TIPO 30;
     |MODULO aAlfa;
     |SI aAlfa ! "agifa017";
          |HAZ ObraAltaAlm;
     |FINSI;
|FPRC;

|PROCESO T66; |TIPO 66;
     |CONSULTA_CLAVE #1#agifa019;
|FPRC;

|PROCESO TecaPrecio; |TIPO 14;
|FPRC;

|PROCESO PintaBoton; |TIPO 7;
     aDesAnt = #0#1;
     |ESTADO_CONTROL nBoton, "ENABLE";
|FPRC;

|PROCESO TipoArt19_0; |TIPO 0;
|FPRC;

|PROCESO VaStock; |TIPO 0;
|FPRC;

|PROCESO llena;
|FPRC;

|PROCESO PreMercado14; |TIPO 14; || se llama de los campos 25 y 31
     |DDEFECTO #goartmer;
     |ABRE #goartmer;
     #goartmer#0 = #0#0;
     |LEE 000#goartmer.=;
     |SI FSalida ! 0; |GRABA 020#goartmer.n; |FINSI;
     |CIERRA #goartmer;
     |PINTA 2153, #goartmer#1;
     nSwPin = 0;
|FPRC;

|PROCESO PreMercado0; |TIPO 0; || se llama desde el campo 25
     nSwPin = nSwPin + 1;
     |SI nSwPin = 1; |ACABA; |FINSI;
     |DDEFECTO #goartmer;
     |ABRE #goartmer;
     #goartmer#0 = #0#0;
     |LEE 101#goartmer.=;
     |SI FSalida ! 0; |GRABA 020#goartmer.b; |FINSI;
     |ENCAMPO #1#goartmer;
     |SI FSalida ! 7;
          |GRABA 020#goartmer.a;
     |FINSI;
     |CIERRA #goartmer;
|FPRC;

|PROCESO Capitulo66; |TIPO 66;
     |ABRE #110; |LEE 000#110p; |CIERRA #110;
     |SI #110#32 = "H";
          |ABRE #116;
          #116#0 = #0#127;
          |LEE 000#116];
          |CONSULTA_CLAVE #1#116;
          |SI FSalida = 0;
               #0#127 = #116#0; |PINTA #0#127;
          |SINO;
          |FINSI;
          |CIERRA #116;
     |SINO;
          |ABRE #117;
          #117#0 = #0#127;
          |LEE 000#117];
          |CONSULTA_CLAVE #1#117;
          |SI FSalida = 0;
               #0#127 = #117#0; |PINTA #0#127;
          |SINO;
          |FINSI;
          |CIERRA #117;
     |FINSI;
|FPRC;

|PROCESO AlmaAuto66; |TIPO 66;
     |DDEF aDef;
     aDef = aDef + "agifa029";
     |CARGA_DEF aDef, referen@;
     |SI FSalida = 0;
          |ABRE #referen@;
          #referen@#0 = #0#161;
          |LEE 000#referen@.];
          |CONSULTA_CLAVE #1#referen@;
          |SI FSalida = 0;
               #0#161 = #referen@#0;
               |PINTA #0#161;
          |SINO;
               |ERROR;
          |FINSI;
          |CIERRA #referen@;
     |FINSI;
|FPRC;

|PROCESO AlmaAuto0; |TIPO 0;
     |SI #0#161 = 0; |ACABA; |FINSI;
     |DDEF aDef;
     aDef = aDef + "agifa029";
     |CARGA_DEF aDef, referen@;
     |SI FSalida = 0;
          |ABRE #referen@;
          #referen@#0 = #0#161;
          |LEE 000#referen@.=;
          |SI FSalida ! 0;
               |MENAV "0000No existe Almacen...";
               |ERROR;
          |FINSI;
          |CIERRA #referen@;
     |FINSI;
|FPRC;

          |SI #110#90 = "S"; |Y #110#141 = "O";
          |FINSI;
