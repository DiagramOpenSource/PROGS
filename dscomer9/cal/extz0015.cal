|FICHEROS;
     extz0015 #0;   || Temporal   (ojo, no se renombra ni se hace delindex)
     extm0001 #1;   || Aux. Arti
     extm0019 #19;  || Articulo+N§ Serie
     referen@;
|FIN;

|VARIABLES;
     &enUnidades = 0;
     &eaArticulo = "";
     &efFecha = @;
     &eaSerie = "";
     &enCodigo = 0;
     &enLinea = 0;
     aParam = "";

     aAlfa = "";
     nLin = 0;
     nGrid = 0;
     nRango = 0;
     nModo = 0;
|FIN;

|PROCESO DefeLin; |TIPO 7;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 1;
          #0#0 = eaSerie;
          #0#1 = enCodigo;
          #0#2 = enLinea;
          #0#3 = 999;
          |LEE 000#0];
          |SI FSalida = 0;
               |LEE 000#0a;
          |SINO;
               |LEE 000#0u;
          |FINSI;
          |SI FSalida = 0; |Y #0#0 = eaSerie; |Y #0#1 = enCodigo; |Y #0#2 = enLinea;
               nLin = #0#3 + 1;
          |SINO;
               nLin = 1;
          |FINSI;
          |DDEFECTO #0;
          #0#0 = eaSerie;
          #0#1 = enCodigo;
          #0#2 = enLinea;
          #0#3 = nLin;
          ||PTEC 802;
     |FINSI;
|FPRC;

|PROCESO CheqNum; |TIPO 0;
     |SI FSalida = 1; |O FSalida = 7; |ACABA; |FINSI;

     aAlfa = #0#4; |QBF aAlfa;
     |SI aAlfa = ""; |AVISO; |ERROR; |ACABA; |FINSI;

     nLin = 0;
     |DDEF aAlfa;
     aAlfa = aAlfa + "extz0015";
     |CARGA_DEF aAlfa, referen@;
     |SI FSalida = 0;
          |ABRE #referen@; |SELECT #2#referen@;
          #referen@#0 = eaSerie;
          #referen@#1 = enCodigo;
          #referen@#2 = enLinea;
          #referen@#4 = #0#4;
          |LEE 000#referen@.=;
          |SI FSalida = 0; |Y #0#3 ! #referen@#3;
               nLin = #referen@#3;
          |FINSI;
          |CIERRA #referen@;
          |DESCARGA_DEF #referen@;
     |FINSI;
     |SI nLin ! 0;
          aAlfa = "0000Nº serie indicado en la línea: " + nLin;
          |MENAV aAlfa;
          |ERROR; |ACABA;
     |FINSI;

     |ABRE #19;
     #19#0 = eaArticulo;
     #19#1 = #0#4;
     |LEE 000#19=;
     |SI FSalida = 0;
          aAlfa = "0000Número serie intoducido en el albarán:" + #19#3 + "/" + (("000000"+#19#4)%-106) + "/" + (("000"+#19#5)%-103);
          |MENAV aAlfa;
          |ERROR;
     |FINSI;
     |CIERRA #19;
|FPRC;

|PROCESO BtnSalir;
     |PTEC 807;
|FPRC;

|PROCESO Min;
     #0#0 = eaSerie;
     #0#1 = enCodigo;
     #0#2 = enLinea;
     #0#3 = 001;
|FPRC;

|PROCESO Max;
     #0#0 = eaSerie;
     #0#1 = enCodigo;
     #0#2 = enLinea;
     #0#3 = 999;
|FPRC;

|PROCESO Edita;
     |BLANCO 1226 2357;
     |ENTLINEAL #1#0, 4, 2, 22, 1, Min, Max;
|FPRC;

|PROCESO Cuenta;
     enUnidades = enUnidades + 1;
|FPRC;

|DEFBUCLE Cuenta;
     #0#1;
     ;
     eaSerie, enCodigo, enLinea, 001;
     eaSerie, enCodigo, enLinea, 999;
     ;
     NULL, Cuenta;
|FIN;

|PROCESO Carga;
     nLin = nLin + 1;
     #0#0 = eaSerie;
     #0#1 = enCodigo;
     #0#2 = enLinea;
     #0#3 = nLin;
     #0#4 = #19#1;
     |GRABA 020#0n;
     |BORRA 020#19a;
|FPRC;

|DEFBUCLE Carga;
     #19#2;
     ;
     eaSerie, enCodigo, enLinea;
     eaSerie, enCodigo, enLinea;
     be;
     NULL, Carga;
|FIN;

|PROCESO Baja;
     |BORRA 020#19a;
|FPRC;

|DEFBUCLE Baja;
     #19#2;
     ;
     eaSerie, enCodigo, enLinea;
     eaSerie, enCodigo, enLinea;
     be;
     NULL, Baja;
|FIN;

|PROCESO Crea;
     enUnidades = enUnidades + 1;

     #19#0 = eaArticulo;
     #19#1 = #0#4;
     #19#2 = efFecha;
     #19#3 = eaSerie;
     #19#4 = enCodigo;
     #19#5 = enLinea;
     |GRABA 020#19n;

     |BORRA 020#0a;
|FPRC;

|DEFBUCLE Crea;
     #0#1;
     ;
     eaSerie, enCodigo, enLinea, 001;
     eaSerie, enCodigo, enLinea, 999;
     be;
     NULL, Crea;
|FIN;

|PROCESO BorraTmp;
     |BORRA 020#0a;
|FPRC;

|DEFBUCLE BorraTmp;
     #0#1;
     ;
     eaSerie, enCodigo, enLinea, 001;
     eaSerie, enCodigo, enLinea, 999;
     be;
     NULL, BorraTmp;
|FIN;

|PROGRAMA;
     aParam = PARAMETRO;

     |ABRE #1;
     #1#0 = eaArticulo;
     |LEE 000#1=;
     |CIERRA #1;

     |SI aParam = "ALTA"; |Y #1#6 = "S";
          |BUCLE BorraTmp;

          |ABRE #0;
          |BUCLE Carga;
          |LEE 000#0p;
          |HAZ Edita;
          |CIERRA #0;

          enUnidades = 0;
          |BUCLE Cuenta;
     |FINSI;
     |SI aParam = "CREA"; |Y #1#6 = "S";
          |ABRE #19;
          |BUCLE Crea;
          |CIERRA #19;
     |FINSI;
     |SI aParam = "ADJ"; |Y #1#6 = "S";
          |BUCLE BorraTmp;

          |ABRE #0;
          |BUCLE Carga;
          |LEE 000#0p;
          |HAZ Edita;
          |CIERRA #0;

          enUnidades = 0;
          |ABRE #19;
          |BUCLE Crea;
          |CIERRA #19;
     |FINSI;
     |SI aParam = "BAJA";
          |BUCLE Baja;
          |BUCLE BorraTmp;
     |FINSI;
|FPRO;
