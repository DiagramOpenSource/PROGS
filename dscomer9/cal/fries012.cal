|FICHEROS;
     agifa080 #0;
     agifa077 #1;
     agifa019 #2;    || Articulos
     agifa112 #6;
     agifa017 #8;
     agifa065 #9;
     agifa561 #10;   || Tarifas de Proveedor
     agifa007 #11;   || Fichero de series;
     agitp001 #12;   || Tarifas Actualizadas Proveedor / Articulo.
     agifa142 #142;  || Param. Gen.
     agifa321 #15;    || DIV - Parametros
     dsl00001 #36; ||Codigos de barras multiples

     dsm00010 #910;
     dsm00014 #914;
     dsm00005 #905;
     dsm00004 #904;
     dsm00006 #906;

     desan001@ #33;      ||Modulo Desanch.

     fries013 #60,MANTE;   || Piezas / Numero..
     fries004 #44;       || Kilos / Piezas..

|FIN;


|VARIABLES;
     ||Variables Mod.Desanch....
     kil    = 0;
     nSal   = 0;
     mensaki        = "0000 ­ Atencion ! Supera los Kilos Pieza.......";
     aAlfa         = "";
     swCar         = 0;
     swCar1        = 0;
     nAlmaor       = 0;
     swSiai        = 0;
     nCon          = 0;
     nCan          = 0;
     nGuauni       = 0;
     ||-------------------------
     aDivisa    = "";
     nPreDiv    = 0;
     nNume      = 0;
     &enHayIMEI = 0;
     &enUniIMEI = 0;
     &Envase    = "";
     &Unidades1 = 0;
     &Unidades2 = 0;
     &VArticulo = "";
     nPrecio         = 0;
     nForma          = 0;

     &nDeci_Base     = 0;   || Decimales en Totales de la moneda base
     &nDeci_PreBase  = 0;   || Decimales en el Precio en moneda base
     &nDeci_BaseMx   = 0;   || Decimales en Totales de la moneda base (Maxima precision)
     &nDeci_PreBaseMx= 0;   || Decimales en el Precio en moneda base (Maxima precision)
     &nDeci_Div      = 0;   || Decimales en Totales de la divisa
     &nDeci_PreDiv   = 0;   || Decimales en el Precio en divisa
     &nDeci_TotVis   = 0;   || Decimales en Totales visibles (el de la moneda de trabajo)
     &nDeci_TotNoVis = 0;   || Decimales en Totales no visibles (el de la otra moneda)
     &nDeci_PreVis   = 0;   || Decimales en el Precio visual. (lineas)
     &nDeci_ImpVis   = 0;   || Decimales en el Importe visual. (lineas)
     tecla      = "";
     nModo      = 0;
     &ac_divisa = ""; ||
     mensa      = ""; || Linea de visualizacion
     &pintado   = 0;|| Controla el pintado de mensa para que solo lo haga 1 vez
     titdiv = "";   || Encabezado de precio e imp. con la divisa "Imp.Tot(USD)"
     precio = 0;
     forma = 0;
     dto1 = 0;
     dto2 = 0;
     alfa = "";
     Unidades = 0;
     asto = 0;
     salida    = 0;
     descu = 0;
     imneto = 0;
     tf = 0;
     modo = 0;
     articulo = " ";
     fmes = "  ";
     mes = 0;
     mensaje3 = "";
     a = 0;
     sto = 0;
     vacio = "                    ";
     previo = 0;
     de = 0;
     pm = 0;
     pe = 0;
     m1 = 0;
     m2 = 0;
     m3 = 0;
     canti2 = 0;
     mes1 = 0;
     modo1 = 0;
     &n_dec = 0;
     &n_pre = 0;
     neto = 0;
     &def_alm = 0;
     &recoge   = "";     || Viene de agifa077
     campo = 0;
     mensaje5 = "0000 Albaran para exportacion Tipo iva = 0 !!";
     expor = "";

     serie     = "";
     serie_o   = "";
     albaran   = 0;
     albaran_o = 0;

     alfa1     = "";
     alfa2     = "";
     fiac      = "  ";
     guarda    = "  ";
     relleno   = "                                       ";
     aponer    = "  ";
     nerror    = 0;
     &LineaD  = 0;   || Lo utilizo en solis008
     nlinea    = 0;
     i         = 0;
     aprograma = "";
     impret = 0;
     &c_prove   = "";
     &c_artic   = "";
     &c_serie   = "";
     &entrada   = 0;
     inkey      = 0;
     &codiBarra = "";
     &r_arti    = "";
     &aRecoge      = "N";
     &aModalidad   = "C";  || Compras
     &aProceso     = "agifa080";
|FIN;

|INCLUYE i_varart;
|INCLUYE i_cdbarr;

|PROCESO civaalb;
     inkey = FSalida;
     |SI inkey = 14;
           c_prove = #1#3;      || Corregido Por VIC esta mal. antes #0#2
           c_artic = #0#2;      || Corregido Por VIC esta mal. antes #1#3
           c_serie = #1#50;
           entrada = 1;
          |SUB_EJECUTA_NP "agifa383.run";
          |ERROR;
          inkey = 0;
     |FINSI;
     |SI inkey = 15;
          enTiva = #0#11;
          efFiva = #1#1;
          |HAZ SacaIVA;
          #0#6 = #0#6 / (1 + enIva/100);
          |HAZ visual_precio;
     |FINSI;
|FPRC;

|PROCESO liinfos;|TIPO 0;
mensaje3 = "0410Stock Disponible Almacen : " + #8#39+"     Stock Minimo : "+#8#6;
|MENSA mensaje3;
|FPRC;

|PROCESO licogeas;|TIPO 0;
articulo = #0#2;
|FPRC;

|PROCESO defeclis;|TIPO 0;
     #0#15 = #1#3;

     |SI #0#2 ! #2#0;
          |ABRE #2;
          #2#0 = #0#2;
          |LEE 000#2=;
          |CIERRA #2;
     |FINSI;
     #0#13 = #2#2;

     |SI #0#15 ! #6#0;
          |ABRE #6;
          #6#0 = #0#15;
          |LEE 000#6=;
          |CIERRA #6;
     |FINSI;

     |HAZ mirespor;

     descu = FEntrada $ 100;
     |SI 1 = descu;|O articulo ! #0#2;
          articulo = #0#2;
          |HAZ LeePreDto;
          #0#6 = precio;
          #0#30 = nPreDiv;
          #0#8 = dto1;
          #0#26 = dto2;
          |PINTA #0#8;
          |PINTA #0#26;
          |HAZ upc_div;
     |FINSI;
|FPRC;

|PROCESO totlis;|TIPO 0;
     |HAZ TotalLin80;
     |HAZ visual_importe;
|FPRC;

|PROCESO Conversion;  |TIPO 7;
     |HAZ DobleUni;
     |SI eaSw2Stock = "S"; |C_M #0#5, 1, "N"; |ACABA; |FINSI;
/*
     |HAZ EntraIMEI;
     |SI enHayIMEI = 0; |C_M #0#5, 1, "N"; |ACABA; |FINSI;
*/
     |ABRE #2;
     #2#0 = #0#2;
     |LEE 020#2=;
     |SI FSalida = 0;
         |SI #2#4 > 0;
             Envase    = #0#34;
             Unidades1 = #0#35;
             Unidades2 = #0#5;
             VArticulo = #0#2;
             |PUSHV 0101 2480;
             |SUB_EJECUTA_NP "agiw0004.tab;ESTO ES UNA KK....";
             |POPV;
             #0#34 = Envase;
             #0#35 = Unidades1;
             #0#5  = Unidades2;  |PINTA #0#5;
             |C_M #0#5, 1, "S";
             |SI Unidades1 ! 0;  |C_M #0#5, 1, "N";  |FINSI;
         |FINSI;
     |FINSI;
     |CIERRA #2;
|FPRC;

|PROCESO GrabaElemento;
     |ABRE #914;
     #914#0 = eaEleme;
     |LEE 000#914=;
     |CIERRA #914;

     |ABRE #905;
     |DDEFECTO #905;
     #905#0 = "AC";
     #905#1 = #0#27;
     #905#2 = #0#0;
     #905#3 = #0#1;
     #905#4 = eaEleme;
     #905#5 = #914#1;
     #905#6 = #0#5;
     #905#7 = #0#32;
     #905#8 = #905#6 * #905#7;
     #905#11 = #0#5;
     |GRABA 020#905n;

     |DDEFECTO #904;
     |ABRE #904;
     #904#0 = #0#2;
     #904#1 = #0#3;
     #904#2 = eaEleme;
     |LEE 101#904=;
     |SI FSalida ! 0; |GRABA 020#904b; |FINSI;
     #904#3 = #914#1;
     |SI #1#42 = "N";
          #904#12 = #904#12 + #0#5;
          #904#13 = #904#13 + #0#5;
     |SINO;
          #904#12 = #904#12 + #0#5;
          #904#13 = #904#13 + #0#5;
     |FINSI;
     nNume = #904#12 - #904#6;
     |SI nNume > 0;
          #904#24 = nNume * #2#9;
     |SINO;
          #904#24 = 0;
     |FINSI;
     |GRABA 020#904a;
     |ABRE #906;
     #906#0 = #0#2;
     #906#1 = #0#3;
     #906#2 = #1#1;
     #906#3 = "AC";
     #906#4 = #0#27;
     #906#5 = #0#0;
     #906#6 = #0#1;
     #906#7 = eaEleme;
     #906#8 = #914#1;
     #906#9 = #0#5;
     #906#10= #0#30;
     |GRABA 020#906n;
     |CIERRA #906;
|FPRC;

|PROCESO Envase80;
     Envase    = #0#34;
     Unidades1 = #0#35;
     Unidades2 = #0#5;
     VArticulo = #0#2;
     enAlmacen = #0#3;
     eaTipo = "C";
     eaProgEnv = "albaran";
     |HAZ EntraEnvase;
     #0#34 = Envase;
     #0#35 = Unidades1;
     #0#5  = Unidades2;  |PINTA #0#5;
|FPRC;

|PROCESO liacartiC;|TIPO 2;
     |MODULO aprograma;
     |SI aprograma ! "agifa077";
         FEntrada = 101;
     |FINSI;
     modo   = (FEntrada / 100) !0;
     nForma = 0 +. 1;
     |SI modo = 2;|O modo = 3;
          efFec = #1#1;
          enAlma = #0#3;
          |HAZ MiraHisL;
          |SI enErrHis ! 0;
               |ERROR;
               |ACABA;
          |FINSI;
     |FINSI;
     |SI #0#19 = "S";  |O #1#38 = "S";
         |SI nForma < 0;
             |SI #0#19 = AS;
                 |MENAV "0000Esta linea proviene de un Pedido (use la opcion adecuada)";
             |FINSI;
             |SI #1#38 = AS;
                 |MENAV "0000Este albaran esta FACTURADO";
             |FINSI;
         |FINSI;
         |ERROR;
        |ACABA;
     |FINSI;

     imneto = #0#9 < #1#5;
     imneto = imneto < #1#32;
     imneto = imneto < #1#7;

     enForma = 0 +. 1;
     ||ÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ
     |SI codiBarra = "S"; |Y nModo = 1; |Y enForma = 1; |Y #2#176 = "S";
          |HAZ GrabaElemento;
     |FINSI;
     ||ÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ
     eaDocu = "agifa080";
     |HAZ StockEntra;
     enTipEnvase = 2;
     |HAZ Envase80;
     ||ÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ

     |HAZ CalCabAgifa077;

     |SI modo = 3;
          eaTipo      = "AC";
          eaSerie     = #0#27;
          enCodigo    = #0#0;
          enLinea     = #0#1;
          efFecha     = #1#1;
          eaAbono     = #1#42;
          enTarifa    = 0;

          eaArticulo   = #0#2;
          enAlmacen    = #0#3;
          |SUB_EJECUTA_NP "dsz00002;BAJA";

          |HAZ BorraKit;

          |ABRE #60;
          #60#0 = #0#27;
          #60#1 = #0#0;
          #60#2 = #0#1;
          |BORRA 000#60c;
          |CIERRA #60;
     |FINSI;

|| _____________________________ ACTUALIZAR PRECIO PROVEEDOR / ARTICULO.
                                 || VIC 17/07/98
     forma = 0 +.1;
     |SI forma = 1;
         |ABRE #12;
         |DDEFECTO #12;
         #12#0 = #0#15;            || Proveedor.
         #12#1 = #0#2;             || Articulo.
         |LEE 101#12=;
         |SI FSalida ! 0;
             #12#4 = #0#8;           || ultimo descuento de compra
             #12#5 = #0#26;          || ultimo descuento de compra
             #12#6 = #1#1;           || ultima fecha de compra
             #12#7 = #0#4;           || Descripcion Articulo.
             |SI #1#58 = "D";        || Moneda de Trabajo divisa
                 #12#8 = #1#56;       || Divisa
                 #12#9 = #0#6;        || ultimo precio de compra
                 #12#3 = #0#30;       || upc en divisa
             |SINO;
                 #12#8 = #1#61;       || Divisa = Base
                 #12#9 = #0#6;
                 #12#3 = #0#6;
             |FINSI;
             |GRABA 020#12n;
         |SINO;
             #12#4 = #0#8;           || ultimo descuento de compra
             #12#5 = #0#26;          || ultimo descuento de compra
             #12#6 = #1#1;           || ultima fecha de compra
             #12#7 = #0#4;           || Descripcion Articulo.
             |SI #1#58 = "D";        || Moneda de Trabajo divisa
                 #12#8 = #1#56;       || Divisa
                 #12#9 = #0#6;        || ultimo precio de compra
                 #12#3 = #0#30;       || upc en divisa
             |SINO;
                 #12#8 = #1#61;       || Divisa = Base
                 #12#9 = #0#6;
                 #12#3 = #0#6;
             |FINSI;
             |GRABA 020#12a;
         |FINSI;
         |LIBERA #12;
         |CIERRA #12;
     |FINSI;
     |HAZ pantcomp;
|FPRC;

|PROCESO Min12;
     #12#0 = #1#3;
     #12#2 = " " * 13;
|FPRC;

|PROCESO Max12;
     #12#0 = #1#3;
     #12#2 = "z" * 13;
|FPRC;

|PROCESO art6alc; |TIPO 6;
     r_arti = "";
     inkey = salida;
     |SI codiBarra = "S"; |Y modo = 1;
          eaArti = #0#2;
          |CIERRA #36;
          |HAZ HallaArti;
          |SI eaArti = ""; |Y #910#19 = "S"; |ERROR; |ACABA; |FINSI;
          |SI #2#176 = "S"; |Y eaEleme = "";
               |MENAV "0000Codigo incompleto...";
               |ERROR; |ACABA;
          |FINSI;
          |SI eaArti ! ""; #0#2 = eaArti; |FINSI;
     |FINSI;
     |SI inkey = 10;
          |SI #142#74 = "T";
               |ABRE #2;
               #2#0 = #0#2;
               |LEE 000#2=;
               |CONSULTA_CLAVE #6#2;
               |SI FSalida = 0;
                    #0#2 = #2#0;
               |SINO;
                    |ERROR;
               |FINSI;
               |CIERRA #2;
          |SINO;
               |ABRE #12;
               #12#0 = #1#3;
               #12#1 = #0#2;
               |LEE 000#12];
               |CONSULTA_F_CLAVE #3#12, Min12, Max12;
               |SI FSalida = 0;
                    #0#2 = #12#1;
               |SINO;
                    |ERROR;
               |FINSI;
               |CIERRA #12;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO nomodif5; |TIPO 0;
     |QBF r_arti;
     |SI r_arti ! "";
          #0#2 = r_arti;
          |PINTA #0#2;
     |FINSI;
     modo = (FEntrada / 100) ! 0;
     |SI modo = 1; #0#3 = def_alm; |PINTA #0#3; |FINSI;
     |SI #0#2 = vacio;
          |CAMPO_MODIFICA #0#3,  1, "N";
          |CAMPO_MODIFICA #0#5,  1, "N";
          |SI #1#58 = "B"; || Si la moneda de trabajo es la moneda base
            |CAMPO_MODIFICA #0#6,  1, "N";
          |SINO;
            |CAMPO_MODIFICA #0#30,  1, "N";
          |FINSI;
          |CAMPO_MODIFICA #0#8,  1, "N";
          |CAMPO_MODIFICA #0#26,  1, "N";
          |CAMPO_MODIFICA #0#10, 1, "N";
          |CAMPO_MODIFICA #0#11, 1, "N";
     |SINO;
          |CAMPO_MODIFICA #0#3,  1, "S";
          |CAMPO_MODIFICA #0#5,  1, "S";
          |SI #1#58 = "B";
            |CAMPO_MODIFICA #0#6,  1, "S";
          |SINO;
            |CAMPO_MODIFICA #0#30,  1, "S";
          |FINSI;
          |CAMPO_MODIFICA #0#8,  1, "S";
          |CAMPO_MODIFICA #0#26,  1, "S";
          |CAMPO_MODIFICA #0#10, 1, "S";
          |CAMPO_MODIFICA #0#11, 1, "S";
     |FINSI;
|FPRC;

|PROCESO decim;
     #0#5 = #0#5 ! n_dec;
     |PINTA #0#5;
|FPRC;

|PROCESO decim1;
     |SI #2#176 = "S";
          |SI codiBarra = "N"; |O modo ! 1;
               |SI #1#58 = "D";  || Si la moneda de trabajo es la divisa ...
                    #0#30 = nPrecio;
               |SINO;
                    #0#6  = nPrecio;
               |FINSI;
          |FINSI;
     |FINSI;

     |SI #1#58 = "D";  || Si la moneda de trabajo es la divisa ...
         #0#6 = #0#30 / #1#57 * #1#62;  || ... recalculo en funcion de la divisa
     |SINO;
         #0#30 = #0#6 / #1#62 * #1#57; || .. recalculo en funcion de la moneda base
     |FINSI;

     |HAZ visual_precio; || Visualiza el precio
|FPRC;

|PROCESO Tipo11080;  |TIPO 11;
     inkey = FSalida;
     modo = (FEntrada/100) !0;
/*
     |SI inkey = 7; |Y Campo ] 4;
         |HAZ ModuloIMEI;
         |SI #2#176 = "S"; |O enHayIMEI = 0;
               |MENAV "      Imposible abortar;  Termine de introducir la linea y efectue una Baja.";
               |ERROR; |ACABA;
          |FINSI;
     |FINSI;
*/
     |SI Campo ! 1; |ACABA; |FINSI;

     |SI inkey = 9;
          |PUSHV 501 2380;
          |PINPA #0#1;
          |PINDA #0#1;
          |PAUSA;
          |POPV;
          |ERROR;
          Control = "agifa080";
     |FINSI;

     |SI inkey = 14;
          |SI modo = 1;
              c_artic = "";
          |SINO;
              c_artic = #0#2;
          |FINSI;
          c_prove = #1#3;
          c_serie = #1#50;
          entrada = 1;
          |SUB_EJECUTA_NP "agifa383.run";
          |ERROR;
          inkey = 0;
     |FINSI;

     |SI inkey = 13;
          |HAZ VerKit;
          |HAZ VerDobleUni;
       ||   |HAZ VerIMEI;
          |ERROR; |ACABA;
     |FINSI;

     |SI inkey = 12; |O inkey = 17;     || F4 o F9
          |SI inkey = 12; |Y recoge ="N"; ||F4
               |PUSHV 0400 0479;
               |MENAV "0000Opcion no activada en Utilidades Usuario de Parametros Generales";
               |POPV;
               |ERROR;
               |ACABA;
          |FINSI;
           || compruebo que este en la ultima linea y en modo alta
          LineaD = #agifa080#1;    || Guardo puntero
          LineaD = LineaD +1;     || Porque si estoy en alta la linea en la que
                                     || estoy todavia no esta grabada
          |LEE 001#agifa080.];

          |SI FSalida ! 0;|O #agifa080#0 ! #agifa077#0;
                          |O #agifa080#27 ! #agifa077#50;|Y modo = 1;

               LineaD = LineaD -1;

               || Recupero el puntero
               #agifa080#0  = #agifa077#0;    || Albaran
               #agifa080#27 = #agifa077#50;   || Serie
               #agifa080#1  = LineaD;        || Numero de linea
               |LEE 001#agifa080.=;

               nerror = 0;
               |SI inkey = 12; ||F4
                    |HAZ entradatosC;
               |FINSI;
               |SI nerror = 0 ;
                    |SI inkey = 12;
                         |HAZ leelineasC;
                    |FINSI;
                    |HAZ control2C;
                      || Le meto 3 ptec's para cuando cambia de pantalla
                    |PTEC 809;    || Flecha abajo
                    |PTEC 809;    || Flecha abajo
                    |PTEC 809;    || Flecha abajo
               |SINO;
                    |ERROR;
               |FINSI;
          |SINO;
                   || Recupero el puntero
               LineaD = LineaD -1;
               #agifa080#0  = #agifa077#0;    || Albaran
               #agifa080#27 = #agifa077#50;   || Serie
               #agifa080#1  = LineaD;        || Numero de linea
               |LEE 001#agifa080.=;

               |MENAV "0000Para recoger un albaran debe estar en la ultima linea  y en Alta";
               |ERROR;
          |FINSI;    || Si #agifa080#1 = 1 ...
     |FINSI;      || Si FSalida ...
|FPRC;

|| Recojo la serie y numero de albaran
|PROCESO entradatosC;
         |PINTA 2420 , " Recogida de datos del albaran :   /    ";
         alfa1 = "";
         E_sup = 2;
         E_inf = "";
         alfa1 = 2453 ? 1;
         serie_o = alfa1;
         alfa2 = "";
         E_sup = 6;
         E_inf = "";
         alfa2 = 2456 ? 1;
         albaran_o = alfa2;
         |BLIN 24;

         || No puedo leer en el que estoy sino se embuclaria
         |SI  serie_o = #agifa077#50;|Y albaran_o = #agifa077#0;
             |MENAV "0000ATENCION !!! Esta en este albaran";
             nerror = 1;
         |FINSI;
|FPRC;

|PROCESO control1C;
   aponer= Asino;
   |HAZ poncontrC;
|FPRC;

|PROCESO control2C;
   aponer= Anosi;
   |HAZ poncontrC;
|FPRC;

|PROCESO poncontrC;
         fiac = Control % A109;
         fiac = fiac + relleno;
         guarda = fiac % A109;
         fiac = Control % A1001;
         Control = Control + fiac;
         Control = Control + relleno;
         Control = Control % A120;
         Control = Control + aponer;
|FPRC;

|PROCESO CreaComponente;
     #905#0 = "AC";
     #905#1 = #0#27;
     #905#2 = #0#0;
     #905#3 = #0#1;
     #905#9 = #905#6;
     |GRABA 020#905n;
     |LIBERA #905;

     enUnidades   = #905#6;
     eaTipo       = "AC";
     eaSerie      = #0#27;
     enCodigo     = #0#0;
     enLinea      = #0#1;
     efFecha      = #1#1;
     eaArticulo   = #0#2;
     enAlmacen    = #0#3;
     eaComponente = #905#4;
     efFecha      = #1#1;
     enCamBas     = #1#57;
     eaMoneda     = #1#58;
     enCamDiv     = #1#62;

     |SUB_EJECUTA_NP "dsz00002;ACC";
|FPRC;

|DEFBUCLE dsm00005;
     #905#1;
     ;
     "AC", serie, albaran, nlinea, "      ";
     "AC", serie, albaran, nlinea, "zzzzzz";
     be;
     NULL, CreaComponente;
|FIN;

|PROCESO crealineasC;
    || Clave primaria
    #agifa080#27= #agifa077#50;    || Serie
    #agifa080#0 = #agifa077#0;     || Numero de Albaran
    #agifa080#1 = LineaD;         || Numero de linea


    |DEFECTO #agifa080#agifa077;   || Cargo los valores de defecto de cab.albaran

    || Ahora cambio lo que es particular de este albaran y que por tanto
    || no puede ver como copia de otro
    #agifa080#12 = "";                  || Familia
    || !!!! Descargar el coste !!! (pdte)
    |PARA i = 16;|SI i [ 26; |HACIENDO i = i+1;
          #agifa080#i# = "";
    |SIGUE;
    #agifa080#28 = ""; || Serie Factura
    #agifa080#29 = ""; || Serie Pedido

    |HAZ totlis;       || Calculo total linea
    |HAZ liacartiC;    || Lo envio al tipo 2 para que calcule lo mismo que si fuera
                       || una linea introducida por el operador
    |GRABA 021#agifa080.b;
    |LIBERA #agifa080;

    |BUCLE dsm00005;

    LineaD =LineaD + 1;  || Aumento contador de lineas
|FPRC;


|PROCESO leelineasC;
      #agifa080#27= serie_o;
      #agifa080#0 = albaran_o;
      #agifa080#1 = 1;
      |LEE 001#agifa080.];
      |SI FSalida =0;
           |PARA;|SI FSalida =0;|Y #agifa080#27=serie_o;|Y #agifa080#0=albaran_o;
                                                     |HACIENDO;
                  || Me guardo el puntero
                  serie   = #agifa080#27;   || Serie y albaran actual
                  albaran = #agifa080#0;
                  nlinea  = #agifa080#1;

                  |HAZ crealineasC;

                  || Recupero puntero
                  #agifa080#27 = serie;
                  #agifa080#0  = albaran;
                  #agifa080#1  = nlinea;
                  |LEE 001#agifa080.=;

                  |LEE 001#agifa080.s;
           |SIGUE;
      |SINO;
           |MENAV "0000Atencion !!! No se ha encontrado el albaran indicado";
           |ERROR;
      |FINSI;

      || Devuelvo el puntero de la linea
      #agifa080#27= #agifa077#50; || Serie
      #agifa080#0 = #agifa077#0;  || Albaran
      #agifa080#1 = LineaD;      || Linea
      |LEE 001#agifa080.=;
|FPRC;


|PROCESO param;|TIPO 7;
|ABRE #11;
#11#0 = #0#27;
|LEE 001#11=;
|SI FSalida = 0;
   expor = #11#30;
|SINO;
  |DDEFECTO #11;
|FINSI;
|CIERRA #11;
|FPRC;

|PROCESO mirespor;|TIPO 0
   |SI #0#11 ! 0; |Y expor = "S";
                 |MENSA mensaje5;
                       #0#11 = 0;
                    |PINTA #0#11;
   |FINSI;
|FPRC;

 REGISTRO DE ACTUALIZACIONES

*(1) ... Mediante la pulsacion de F4 podremos recoger las lineas de otro
         albaran.

*(2)...  Utilizamos este proceso entero para la creacion de albaranes desde
         otros procesos, por tanto forzamos esos valores, si no estamos en
         ese momento en el mantenimiento propiamente dicho (p.e. solis008)
         (Lorca 14/07/96).

|| _____________________________ VER CAMBIO DE PRECIOS. PROVEEDOR / ARTICULO.
|PROCESO Mensaje;
     |PUSHV 0501 2380;
     |AVISO;
     |COLOR 0, 0; |ATRI 0;
     |BLANCO 0815 1265;
     |COLOR 4, 0; |ATRI 0;
     |D_CUADRO 0815,1265;
     |PINTA 1017, alfa;
     |ATRI 0;
     |PAUSA;
     |POPV;
|FPRC;

|PROCESO LeePreDto;
     |ABRE #142; |LEE 000#142p; |CIERRA #142;
     |SI #142#74 = "T";
          |ABRE #10;
          |ABRE #2;
          #2#0 = #0#2;
          |LEE 000#2=;
          #10#0 = #0#2;    ||Articulo
          #10#1 = #0#15;   ||Proveedor
          |LEE 000#10=;
          |SI FSalida = 0;
               precio = #10#26;
               nPreDiv = precio;
               dto1 = #10#3;
               dto2 = #10#4;
               aDivisa = #agifa321#9;
          |SINO;
               precio = #2#28;
               nPreDiv = #2#163;
               dto1 = #2#29;
               dto2 = #2#129;
               aDivisa = #2#165;
          |FINSI;
          |CIERRA #2;
          |CIERRA #10;
     |SINO;
          |ABRE #12;
          #12#0 = #0#15;            || Proveedor.
          #12#1 = #0#2;             || Articulo.
          |LEE 101#12=;
          |SI FSalida ! 0;  |DDEFECTO #12; |FINSI;
          |CIERRA #12;
          |SI #12#9 = 0;
               #12#9 = #12#3; ||Necesario para datos de versiones antiguas
          |FINSI;
          precio = #12#9; ||Precio en moneda base de tarifas proveedor
          nPreDiv = #12#3;
          dto1 = #12#4;
          dto2 = #12#5;
          aDivisa = #12#8;
     |FINSI;
|FPRC;

|PROCESO cambipre; |TIPO 0;
     |HAZ LeePreDto;
     |SI #142#75 ! "S"; |ACABA; |FINSI;
     |SI #1#58 = "B";
          |SI precio ! #0#6;
               alfa = "­ ATENCION ! POSIBLE CAMBIO DE PRECIO COSTE...";
               |HAZ Mensaje;
          |FINSI;
     |SINO;
          |SI nPreDiv ! #0#30;
               alfa = "­ ATENCION ! POSIBLE CAMBIO DE PRECIO COSTE...";
               |HAZ Mensaje;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO cambidto;
     |HAZ LeePreDto;
     |SI #142#75 ! "S"; |ACABA; |FINSI;
     |SI Campo = 8;
          |SI dto1 ! #0#8;
              alfa = "­ ATENCION ! POSIBLE CAMBIO DE DESCUENTO 1 ...";
               |HAZ Mensaje;
          |FINSI;
     |SINO;
          |SI dto2 ! #0#26;
               alfa = "­ ATENCION ! POSIBLE CAMBIO DE DESCUENTO 2 ...";
               |HAZ Mensaje;
          |FINSI;
     |FINSI;
|FPRC;
------------------ ACTUALIZA ARTICULOS Y ALMACEN -------------------------

|PROCESO pantcomp;|TIPO 13;
     |HAZ visual_totales; || Visualiza los totales
|FPRC;


|PROCESO LinDiv;  |TIPO 7;
     |HAZ PintaMedidaC;
     |ABRE #agifa321;
     |LEE 001#agifa321.p;  || Leo parametros de divisa
     |SI ac_divisa = "S"; |Y pintado = 0; || Si divisas estan activadas y no he pintado ...
       |SI #1#58 = "D";   || Si la moneda de trabajo es la divisa la pinto en
         |ATRI R;         || el encabezado del precio y el importe
         titdiv = "Precio" + "(" + #1#56 + ")";
         |PINTA 1444,titdiv;
         titdiv = "I.Tot." + "(" + #1#56 + ")";
         |PINTA 1468,titdiv;
         |ATRI r;
       |FINSI;
       |SI #agifa321#4 = "S";  || Si la visualizacion esta activada ...
         mensa = "Precio:            Imp.:              T.Bruto:             T.ALB:             ";
         |PINTA 1102,mensa;  || ... pinto la linea de visualizacion
         |PINTA #0#32; || Pinto precio visual
         |PINTA #0#33; || Pinto importe visual
         |SI #1#58 = "D"; || Si la moneda de trabajo es la divisa
           |PINTA 1149,#1#67;
           |PINTA 1168,#1#68;
         |SINO;
           |PINTA 1149,#1#63;
           |PINTA 1168,#1#64;
         |FINSI;
         pintado = 1;  || Ya hemos pintado
       |FINSI;
     |FINSI;
     |CIERRA #agifa321;
|FPRC;

|PROCESO upc_div;
     |SI #1#56 = aDivisa;  || Si la moneda de trabajo es la divisa ...
          #0#6 = #0#30 * #1#62 / #1#57; || ... recalculo en funcion de la divisa
     |SINO;
          #0#30 = #0#6 / #1#62 * #1#57; || .. recalculo en funcion de la moneda base
     |FINSI;
     |HAZ visual_precio;
|FPRC;

|PROCESO visual_precio; || Pinta el precio visual.
|SI #1#58 = "D";  || Si la moneda de trabajo es la divisa ...
  #0#32 = #0#6;   || ... el campo visualiz. es el precio en moneda base
  |PINTA #0#30;
|SINO;            || Si la moneda de trabajo es la moneda base ...
  #0#32 = #0#30;  || ... el campo visual. es el precio en divisa
  |PINTA #0#6;
|FINSI;
|SI ac_divisa = "S"; |Y #agifa321#4= "S";
  |PINTA #0#32; || Si las divisas y la visualizacion estan activadas, pinto
|FINSI;
|FPRC;


|PROCESO visual_importe;  || Pinta el importe visual
|SI #1#58 = "D";  || Si la moneda de trabajo es la divisa ...
  #0#33 = #0#9;   || el campo visualiz. es el importe en moneda base
  |PINTA #0#31;
|SINO;            || Si la moneda de trabajo es la moneda base ...
  #0#33 = #0#31;  || el campo visualiz. es el importe en divisa
  |PINTA #0#9;
|FINSI;
|SI ac_divisa = "S"; |Y #agifa321#4= "S";
  |PINTA #0#33; || Si las divisas y la visualizacion estan activadas, pinto
|FINSI;
|FPRC;

|PROCESO visual_totales;
||   |SI Control = "agifa080";
          |ABRE #agifa321;
          |LEE 001#agifa321.p;
          |SI #1#58 = "D";
               |SI ac_divisa = "S"; |Y #agifa321#4= "S";
                    |PINTA 1149,#1#67;
                    |PINTA 1168,"        ";
                    |PINTA 1168,#1#68;
               |FINSI;
               |CIERRA #agifa321;
               |PINTA 1349,#1#63;
               |PINTA 1366,#1#64;
          |SINO;
               |SI ac_divisa = "S"; |Y #agifa321#4= "S";
                    |PINTA 1149,#1#63;
                    |PINTA 1168,"        ";
                    |PINTA 1168,#1#64;
              |FINSI;
              |CIERRA #agifa321;
              |PINTA 1349,#1#15;
              |PINTA 1366,#1#55;
          |FINSI;
||   |FINSI;
|FPRC;

|PROCESO CogeValores80;  |TIPO 7;
     |ABRE #142;
     |LEE 001#142p;
     |SI FSalida ! 0; |DDEFECTO #142; |FINSI;
     |CIERRA #142;

     |SI Campo = 3;
         |C_M #0Campo, 1, "S";
         |SI #142#99 = "S";
             |C_M #0Campo, 1, "N";
             #0Campo = 1;  |PINTA #0Campo;
         |FINSI;
     |FINSI;

     |SI #2#176 = "N";  |ACABA;  |FINSI;

     |SI Campo = 3;
         |SI enAlmacen ! 0;
             #0#3 = enAlmacen;  |PINTA #0#3;
         |FINSI;
     |FINSI;

     |SI Campo = 5;
         |SI enUnidades ! 0;
             |SI #0#5 ! enUnidades;  Contenido = #0#5;  |FINSI;
             #0#5 = enUnidades;  |PINTA #0#5;
         |FINSI;
     |FINSI;

     |SI Campo = 6;
         |SI nPrecio ! 0;
             #0#6 = nPrecio;  |PINTA #0#6;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO PideArt80;  |TIPO 7;
     nModo = (FEntrada/100) ! 0;
     |SI #142#140 = "S"; |Y nModo = 1; #0#2 = eaArtAnt; |FINSI;

     |C_M #0#5, 1, "S";
     |C_M #0#2, 1, "S";
     |C_M #0#3, 1, "N";

     |SI #0#2 ! "                    ";
         |ABRE #2;
         #2#0 = #0#2;
         |LEE 000#2=;
         |SI FSalida ! 0;  |DDEFECTO #2;  |FINSI;
         |CIERRA #2;
         |SI #2#176 = "S";
             |C_M #0#2, 1, "N";
             |C_M #0#3, 1, "N";
             |ACABA;
         |FINSI;
     |FINSI;

     |ABRE #910;
     |LEE 040#910p;
     |SI FSalida ! 0;  |DDEFECTO #910;  |FINSI;
     |CIERRA #910;

     |SI #910#2 [ 2;    |ACABA;  |FINSI;

     eAlfa = #0Campo;
     |SUB_EJECUTA_NP "dsz99990";                 || Por Pantalla

     |SI eAlfa = "-1";  |PTEC 807;  |ACABA;  |FINSI;
     |SI eAlfa = "-2";              |ACABA;  |FINSI;

     eaArticulo = eAlfa;      ||  700000-4215

     #0Campo = eAlfa;
     |PINTA #0Campo;
     |PTEC 802;
|FPRC;

|PROCESO Calc680;  |TIPO 6;
     eaArtAnt = #0#2;
     salida = FSalida;

     ||ÄÄÄÄÄÄÄÄÄÄÄÄ Doble/MultiÄ
     eaDocu = "agifa080";
     |HAZ UniPartiCB;
     ||ÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ
     |SI #0Campo ! Contenido; |Y Campo = 2;
          eaArticulo = #0#2;
          |HAZ DefectoMediC;
          #0#39 = enMedida1;
          #0#40 = enMedida2;
          #0#41 = enMedida3;
     |FINSI;

     enAlta = 1;
     eaArticulo = #0Campo;
     |HAZ LlenaArticulo;
     |SI FSalida = 0;
          #0Campo = eaArticulo;
          |PINTA #0Campo;
     |SINO;
          |ERROR; |ACABA;
     |FINSI;

     |HAZ art6alc;

     |SI codiBarra = "S"; |Y #910#19 = "S"; |Y modo = 1;
          |ACABA;
     |FINSI;

     |ABRE #2;
     #2#0 = #0#2;
     |LEE 101#2=;
     |SI #142#209 = "S"; |Y #2#27 ! #1#3;
          aAlfa = "0000NModificar Proveedor en Articulo:"+&13+"Actual:["+#2#27+"]";
          |CONFI aAlfa;
          |SI FSalida = 0; #2#27 = #1#3; |GRABA 020#2a; |FINSI;
     |FINSI;
     |CIERRA #2;

     |SI #2#176 = "S";
         eaTipo      = "AC";
         eaSerie     = #0#27;
         enCodigo    = #0#0;
         enLinea     = #0#1;
         efFecha     = #1#1;
         eaArticulo  = #0#2;
         enAlmacen   = #0#3;
         enUnidades  = #0#5;
         enTBruto    = #0#7;
         eaAbono     = #1#42;
         enTarifa    = 0;
         eaProve     = #1#3;
         enCamBas     = #1#57;
         eaMoneda     = #1#58;
         enCamDiv     = #1#62;

         |SUB_EJECUTA_NP "dsz00002";

         #0#3    = enAlmacen;
         nPrecio = enTBruto / enUnidades;

         |PTEC 802;
         |PTEC 802;
         |PTEC 802;
         |PTEC 802;
     |FINSI;
     |HAZ SacaKit;
|FPRC;

|PROCESO Tipo080;
     |HAZ nomodif5;
|FPRC;

|PROCESO cierre; |TIPO 0;
     efFec = #1#1;
     enAlma = #0#3;
     |HAZ MiraHisL;
     |SI enErrHis ! 0;
          |ERROR;
          |ACABA;
     |FINSI;
|FPRC;

||---------------------- MODULO DESANCH ---------------------------------
|PROCESO CarDesanch; ||Modulo Desanch......................
     |DBASE aAlfa;
     aAlfa = aAlfa + "/def/desan001.mas";
     |XABRE "E", aAlfa, 0;
     |SI FSalida = 0;
          |DBASE aAlfa;
          aAlfa = aAlfa + "/def/desan001.mas";
          |CARGA_DEF aAlfa, desan001@;
          |SI FSalida = 0;
               swCar1 = 1;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO DesDesanch;
     |SI swCar1 = 1; |DESCARGA_DEF #desan001@; |FINSI;
|FPRC;

|PROCESO desan001;
     |SI #33#2 ! "S"; |ACABA; |FINSI;
     nCan = nGuauni / #33#4;
     nCon = ((nCan - 0.49) ! 0) * #33#5;
     |SI nCon [ 0; |ACABA; |FINSI;

     nlinea = nlinea + 1; || Dejo un intervalo por si hubiera que intercalar

     || Cargo valores de albaran actual
     #0#1 = nlinea;

     #0#2 = #33#1;   || Codigo

     |HAZ defeclis;

     #0#4 = #2#1;
     #0#3 = nAlmaor;
     #0#5 = nCon;
     #0#11 = #2#30;
     |HAZ totlis;       || Calculo total linea
     |HAZ liacartiC;
     |GRABA 020#0n;
|FPRC;

|DEFBUCLE desan001;
     #33#1002;
     ;
     #0#2,"                    ";
     #0#2,"zzzzzzzzzzzzzzzzzzzz";
     ;
     NULL,desan001;
|FIN;

|PROCESO DescargaCascos;
     nlinea  = #0#1;
     nGuauni = #0#5;
     nAlmaor = #0#3;
     |BUCLE desan001;
|FPRC;

|PROCESO CompruebCascos;
     |SI #33#2 ! "S"; |ACABA; |FINSI;
     nCan = nGuauni / #33#4;
     nCon = ((nCan - 0.49) ! 0) * #33#5;
     |SI nCon [ 0; |ACABA; |FINSI;
     swSiai = 1;
     |ERROR10;
|FPRC;

|DEFBUCLE CompruebCascos;
     #33#1002;
     ;
     #0#2,"                    ";
     #0#2,"zzzzzzzzzzzzzzzzzzzz";
     ;
     NULL,CompruebCascos;
|FIN;

|PROCESO Tipo3_077;|TIPO 3;
     modo = (FEntrada / 100) ! 0;
/*************
     swCar = 0; swCar1 = 0;
     |HAZ CarDesanch;
     |SI modo = 1; |Y swCar1 = 1;
          swSiai = 0;
          nGuauni = #0#5;
          |BUCLE CompruebCascos;
          |SI swSiai = 1;
               |SALVA_FICHA #0;
               |HAZ control1C;
               |HAZ DescargaCascos;
               |HAZ control2C;
               |PTEC 806;
               |REPON_FICHA #0;
          |FINSI;
     |FINSI;
     |HAZ DesDesanch;
*/
|FPRC;

|PROCESO pidepi; |TIPO 0;
     |ABRE #60;
     #60#0 = #0#27;
     #60#1 = #0#0;
     #60#2 = #0#1;
     |LEE 101#60=;
     nSal = FSalida;
     |SI FSalida ! 0;
          |DDEFECTO #60;
     |FINSI;
     #60#0 = #0#27;
     #60#1 = #0#0;
     #60#2 = #0#1;
     ||PINPA #0#60;
     |PINDA #0#60;
     |ENDATOS #1#60;
     |SI nSal = 0; |GRABA 020#60a; |FINSI;
     |SI nSal ! 0; |GRABA 020#60n; |FINSI;
     |LIBERA #60;
     |CIERRA #60;
     #0#36 = #60#3; || Campo usado para poner las piezas
     #0#37 = #60#4; || Campo usado para poner el numero
|FPRC;

|PROCESO sacapi; |TIPO 7;
     |DDEFECTO #60;
     |ABRE #60;
     #60#0 = #0#27;
     #60#1 = #0#0;
     #60#2 = #0#1;
     |LEE 000#60=;
     |PINDA #0#60;
     |CIERRA #60;
|FPRC;

|PROCESO conkil; |TIPO 0;
     kil = #0#5 / #60#3;
     |ABRE #44;
     #44#0 = #0#2;
     |LEE 000#44=;
     |SI FSalida = 0;
          |SI kil < #44#1; |O kil > #44#2;
               |MENAV mensaki;
          |FINSI;
     |FINSI;
     |CIERRA #44;
|FPRC;

|PROCESO petec; |TIPO 0;
 |SI FSalida ! 2;
     FSalida = 0;
     |PTEC 802;
 |FINSI;
|FPRC;

|PROCESO Tipo5_080;  |TIPO 5;
     |HAZ actalc;
|FPRC;

|PROCESO IVA012_66; |TIPO 66;
     enTiva = #0Campo;
     efFiva = #1#1;
     |HAZ ConsuIVA;
     |SI eaDiva = "";
          |ERROR;
     |SINO;
          #0Campo = enTiva; |PINTA #0Campo;
     |FINSI;
|FPRC;

|PROCESO IVA012_0; |TIPO 0;
     enTiva = #0Campo;
     efFiva = #1#1;
     |HAZ SacaIVA;
     |SI eaDiva = "";
          |MENAV "0000No existe tipo iva para esta fecha...";
          |ERROR; |ACABA;
     |FINSI;
|FPRC;
