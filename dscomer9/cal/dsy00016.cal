|FICHEROS;
     dsy00016 #0;
     agifm002 #2;
     agifa010 #10;
     agifa024 #24;
     agifa069 #69;
     agifa071 #71;
     agifa084 #84;
     dsw00105 #105;
     psioa032@;
     psioa033@;
|FIN;

|VARIABLES;
     &eaArticulo = "";
     &Tempo = "";
     aVacio = "_articulovacio_"; || en la impresion
     fDFecha = @;
     fHFecha = @;
     nNume = 0;
     nSwGesineed = 0;
     aRuta = "";
     aAlfa = "";
     nHayAlb = 0;
     nHayFac = 0;
     nHay = 0;
     aCli = "";
     aDesRuta = "";
     aPath = "";
|FIN;

|PROCESO Tmp;
     |SI #0#15 = "R";
          |PRINT;
     |SINO;
          aAlfa = #105#1; |QBF aAlfa;
          |SI aAlfa = aVacio;
               nHay = 0;
               |SALVA_FICHA #105;
               aCli = #105#0;
               #105#1 = "";
               |LEE 000#105];
               |PARA; |SI FSalida = 0; |Y aCli = #105#0; |HACIENDO;
                    aAlfa = #105#1; |QBF aAlfa;
                    |SI aAlfa ! aVacio;
                         nHay = 1; |SAL;
                    |FINSI;
                    |LEE 000#105s;
               |SIGUE;
               |REPON_FICHA #105;
               |SI nHay = 0;  || si no hay ninguno es que no se ha vendido
                    |PRINT;   || y si hay alguno el vacio no se imprime
               |FINSI;
          |SINO;
               |PRINT;
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE Tmp;
     #105#2;
     ;
     #0#10, "      ", "                    ";
     #0#11, "zzzzzz", "zzzzzzzzzzzzzzzzzzzz";
     ;
     NULL, Tmp;
|FIN;

|PROCESO CargaFac;
     #105#0 = #24#0;
     #105#1 = #69#2;
     |LEE 101#105=;
     |SI FSalida ! 0;
          #105#0 = #24#0;
          #105#1 = aVacio;
          |LEE 121#105=;
          #105#1 = #69#2;
          |GRABA 020#105b;
     |FINSI;
     #105#3 = #84#1;
     #105#4 = #84#15;
     #105#5 = #69#4;
     #105#6 = #69#5;
     #105#7 = #69#6;
     #105#8 = #69#8;
     #105#9 = #69#21;
     #105#10 = #69#9;
     |GRABA 020#105a;
     |LIBERA #105;
|FPRC;

|PROCESO CargaAlb;
     #105#0 = #24#0;
     #105#1 = #71#2;
     |LEE 101#105=;
     |SI FSalida ! 0;
          #105#0 = #24#0;
          #105#1 = aVacio;
          |LEE 121#105=;
          #105#1 = #71#2;
          |GRABA 020#105b;
     |FINSI;
     #105#3 = #10#1;
     #105#4 = #10#15;
     #105#5 = #71#4;
     #105#6 = #71#5;
     #105#7 = #71#6;
     #105#8 = #71#8;
     #105#9 = #71#33;
     #105#10 = #71#9;
     |GRABA 020#105a;
     |LIBERA #105;
|FPRC;

|PROCESO UltConsumo;
     nHayAlb = 0;
     #10#3 = #24#0;
     #10#1 = fDFecha;
     #10#52 = "";
     #10#0 = 0;
     |LEE 000#10];
     |SI FSalida = 0;
          |LEE 000#10a;
     |SINO;
          |LEE 000#10u;
     |FINSI;
     |SI FSalida = 0; |Y #10#3 = #24#0; |Y #10#1 [ fDFecha;
          nHayAlb = 1;
     |FINSI;

     nHayFac = 0;
     #84#3 = #24#0;
     #84#1 = fDFecha;
     #84#48 = "";
     #84#0 = 0;
     |LEE 000#84];
     |SI FSalida = 0;
          |LEE 000#84a;
     |SINO;
          |LEE 000#84u;
     |FINSI;
     |SI FSalida = 0; |Y #84#3 = #24#0; |Y #84#1 [ fDFecha;
          nHayFac = 1;
     |FINSI;

     |SI #0#15 = "R";
          #105#0 = #24#0;
          #105#1 = aVacio;
          |LEE 121#105=;
          |SI nHayFac = 1; |Y nHayAlb = 1;
               |SI #10#1 ] #84#1;
                    #105#3 = #10#1;
                    #105#4 = #10#15;
               |SINO;
                    #105#3 = #84#1;
                    #105#4 = #84#15;
               |FINSI;
          |SINO;
               |SI nHayAlb = 1;
                    #105#3 = #10#1;
                    #105#4 = #10#15;
               |FINSI;
               |SI nHayFac = 1;
                    #105#3 = #84#1;
                    #105#4 = #84#15;
               |FINSI;
          |FINSI;
          |GRABA 020#105a;
          |LIBERA #105;
     |SINO;
          |SI nHayFac = 1; |Y nHayAlb = 1;
               |SI #10#1 ] #84#1;
                    |BUCLELIN 2 CargaAlb #10;
               |SINO;
                    |BUCLELIN 2 CargaFac #10;
               |FINSI;
          |SINO;
               |SI nHayAlb = 1;
                    |BUCLELIN 2 CargaAlb #10;
               |FINSI;
               |SI nHayFac = 1;
                    |BUCLELIN 2 CargaFac #84;
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE UltConsumo;
     #24#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, UltConsumo;
|FIN;

|PROCESO CopiaCli;
     #24#0 = #105#0;
     |GRABA 020#24n;
|FPRC;

|DEFBUCLE CopiaCli;
     #105#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, CopiaCli;
|FIN;

|PROCESO Facturas;
     |SI #69#2 ] #0#6; |Y #69#2 [ #0#7;
          |SI #69#13 ] #0#8; |Y #69#13 [ #0#9;
               #105#0 = #84#3;
               |BORRA 000#105c;
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE Facturas;
     #84#3;
     6;
     #0#0, fDFecha, #0#2, #0#4, #0#19;
     #0#1, fHFecha, #0#3, #0#5, #0#20;
     ;
     NULL, NULL, Facturas;
|FIN;

|PROCESO Albaranes;
     |SI #71#2 ] #0#6; |Y #71#2 [ #0#7;
          |SI #71#13 ] #0#8; |Y #71#13 [ #0#9;
               #105#0 = #10#3;
               |BORRA 000#105c;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO CabAlb;
     |SI #0#13 = "F"; |Y #10#38 = "N"; |ERROR; |FINSI;
     |SI #0#13 = "S"; |Y #10#38 = "S"; |ERROR; |FINSI;
|FPRC;

|DEFBUCLE Albaranes;
     #10#3;
     6;
     #0#0, fDFecha, #0#2, #0#4, #0#19;
     #0#1, fHFecha, #0#3, #0#5, #0#20;
     ;
     NULL, CabAlb, Albaranes;
|FIN;

|PROCESO psioa033;
     aRuta = #psioa033@#0;
     |ERROR10;
|FPRC;

|DEFBUCLE psioa033;
     #psioa033@#2003;
     ;
     #24#0, 0001, "      ";
     #24#0, 9999, "zzzzzz";
     ;
     NULL, psioa033;
|FIN;

|PROCESO Clientes;
     |SI #24#45 = "01.01.0000";
||          |ACABA;
     |FINSI;
     |SI nSwGesineed = 0;
          aRuta = "";
          |BUCLE psioa033;
          |SI aRuta = ""; |ACABA; |FINSI;

          |SI aRuta < #0#10; |O aRuta > #0#11;
               |ACABA;
          |FINSI;

          #psioa032@#0 = aRuta;
          |LEE 000#psioa032@.=;
          |SI FSalida ! 0; |DDEFECTO #psioa032@; |FINSI;
          aDesRuta = #psioa032@#1;
     |SINO;
          |SI #24#189 < #0#10; |O #24#189 > #0#11;
               |ACABA;
          |FINSI;
          aRuta = #24#189;
          #2#0 = aRuta;
          |LEE 000#2=;
          |SI FSalida ! 0; |DDEFECTO #2; |FINSI;
          aDesRuta = #2#1;
     |FINSI;
     #105#0 = #24#0;
     #105#1 = aVacio;
     #105#2 = #24#2;
     #105#11 = aRuta;
     #105#12 = aDesRuta;
     |GRABA 020#105n;
|FPRC;

|DEFBUCLE Clientes;
     #24#1;
     ;
     #0#0;
     #0#1;
     ;
     NULL, Clientes;
|FIN;

|PROCESO Tipo3; |TIPO 3;
     |HAZ EsGesineed; nSwGesineed = FSalida;

     fHFecha = ~;
     nNume = fHFecha; nNume = nNume - #0#12;
     fDFecha = nNume;

     |IMPRE 0;
     |SI FSalida = 0;
          |SI #0#15 = "R";
               |INFOR "dsyr0016";
          |SINO;
               |INFOR "dsye0016";
          |FINSI;
          |SI FSalida = 0;
               |SI nSwGesineed = 0;
                    |DDEF aPath;
                    aAlfa = aPath + "psioa032"; |CARGA_DEF aAlfa, psioa032@; |ABRE #psioa032@;
                    aAlfa = aPath + "psioa033"; |CARGA_DEF aAlfa, psioa033@; |ABRE #psioa033@;
               |FINSI;

               |DELINDEX #105;
               |ABRE #105;
               |ABRE #24;
               |ABRE #2;
               |BUCLE Clientes;
               |BUCLE Albaranes;
               |SI #0#14 = "S";
                    |BUCLE Facturas;
               |FINSI;
               |CIERRA #2;
               |CIERRA #24;
               |CIERRA #105;

               |SI nSwGesineed = 0;
                    |CIERRA #psioa033@; |DESCARGA_DEF #psioa033@;
                    |CIERRA #psioa032@; |DESCARGA_DEF #psioa032@;
               |FINSI;

               |HAZ CreaTempo; |NOME_DAT #24 Tempo; |DELINDEX #24;
               |ABRE #24;
               |BUCLE CopiaCli;    ||usa fic cliente como temporal
               |ABRE #10; |SELECT #3#10;
               |ABRE #84; |SELECT #3#84;
               |ABRE #105;
               nNume = fDFecha; nNume = nNume - 1;
               fDFecha = nNume;
               |BUCLE UltConsumo;
               |CIERRA #105;
               |CIERRA #84;
               |CIERRA #10;
               |CIERRA #24; |DELINDEX #24; |HAZ BoraTempo;
               |NOME_DAT #24 "agifa024";

               |BUCLE Tmp;
               |PIEINF;
               |FININF;
               |DELINDEX #105;

          |SINO;
               |SI #0#15 = "R";
                    |MENAV "0000Error informe 'dsyr0016'...";
               |SINO;
                    |MENAV "0000Error informe 'dsye0016'...";
               |FINSI;
          |FINSI;
          |FINIMP;
     |FINSI;
|FPRC;

|PROCESO Llena001; |TIPO 6;
     eaArticulo = #0Campo;
     |HAZ LlenaArticulo;
     |SI FSalida = 0;
          #0Campo = eaArticulo;
          |PINTA #0Campo;
     |SINO;
          |ERROR; |ACABA;
     |FINSI;
|FPRC;

|PROGRAMA;
     |CLS;
     |CABEZA "0000Informe clientes sin consumo";
     |ABRE #0;
     |LEE 101#0=;
     |SI FSalida ! 0; |GRABA 020#0b; |FINSI;
     |PINPA #0#0;
     |PINDA #0#0;
     |ENDATOS #1#0;
     |SI FSalida = 0; |GRABA 020#0a; |FINSI;
     |CIERRA #0;
|FPRO;
