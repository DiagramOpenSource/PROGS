
          PROCESO COMUN PARA 'guerw032' Y 'goalbpa3'


|FICHEROS;
     goalbpa1 #0;
     goalbpa2 #1;
     goalbpa3 #2;  ||= gopartia
     gomatcab #10;
     gomatlin #11;
     gomatljn #12;
     gomantca #20;
     gomantli #21;
     goalquil #90;
     goparame #100;
     agifa077 #101;
     agifa080 #102;
     agifa017 #103;
     agifa019 #104;
     agifa024 #105;
|FIN;
--------------------------------------------------------------------------------
|VARIABLES;
     &Flag = 0;

     &cSerieAlba    = "";
     &cSuAlbaran    = "";
     &cProveAlba    = "";
     &cNombrAlba    = "";

     &fFechaAlba    = @;

     &nNumerAlba    = 0;
     &nTotComTra    = 0;
     &nTotVenTra    = 0;
     &nTotComBas    = 0;
     &nTotVenBas    = 0;

     &cCodMonBase   = "";
     &cCodMonTrab   = "";
     &cCodMonConv   = "";
     &cCodMonActu   = "";
     &cDesMonBase   = "";
     &cDesMonTrab   = "";

     &nNumerPres    = 0;
     &nDeci_PBase   = 0;
     &nDeci_TBase   = 0;
     &nDeci_PTrab   = 0;
     &nDeci_TTrab   = 0;
     &nDeci_TotVis  = 0;
     &nEurComTrab   = 0;
     &nEurComBase   = 0;
     &nEurVenTrab   = 0;
     &nEurVenBase   = 0;
     &nPreComTrab   = 0;
     &nPreComBase   = 0;
     &nPreVenTrab   = 0;
     &nPreVenBase   = 0;
     &nPrecioComp   = 0;
     &nPrecioVent   = 0;
     &nAbono        = 0;

     &xDocTras      = 0;
     &xFecDocu      = @;
     &xLinDocu      = 0;
     &xCodArti      = "";
     &xDesArti      = "";
     &xCanArti      = 0;
     &xAlmEntr      = 0;
     &xAlmSali      = 0;

     &cParam        = "";

     nLinea         = 0;
     nImpComTrab    = 0;
     nImpComBase    = 0;
     nImpVenTrab    = 0;
     nImpVenBase    = 0;
     nSwAct         = 0;
     entrad         = 0;
     nModo          = 0;
     nConta         = 0;
|FIN;
--------------------------------------------------------------------------------
|INCLUYE i_varart;
--------------------------------------------------------------------------------
|PROCESO ModiMate;
     nImpComTrab = #2#14;
     nImpComBase = #2#25;
     nImpVenTrab = #2#13;
     nImpVenBase = #2#24;

     |SI cParam = "MATERIAL";
          nTotComTra = nTotComTra +. nImpComTrab; ||Imp. Comp. Trabajo.
          nTotVenTra = nTotVenTra +. nImpVenTrab; ||Imp. Vent. Trabajo.
          nTotComBas = nTotComBas +. nImpComBase; ||Imp. Comp. Base.
          nTotVenBas = nTotVenBas +. nImpVenBase; ||Imp. Vent. Base.
          |ACABA;
     |FINSI;

     |ABRE #10;
     |DDEFECTO #10;
     #10#0 = #2#2;                                ||Serie Expediente.
     #10#1 = #2#3;                                ||Nº Expediente.
     |LEE 101#10=;

     |SI FSalida ! 0;
          |HAZ AltaExpMatCab;
     |FINSI;

     |ABRE #11;
     |SELECT #2#11;
     #11#3 = #2#0;                                ||Serie Presu.
     #11#4 = #2#1;                                ||Nº Presu.
     |LEE 101#11=;

     |SI FSalida = 0;
          |ABRE #12;
          |SELECT #5#12;
          |DDEFECTO #12;
          #12#0 = #2#0;                           ||Serie Presu.
          #12#1 = #2#1;                           ||Nº Presu.
          #12#5 = #2#4;                           ||Serie Albaran.
          #12#6 = #2#5;                           ||Numero Albaran.
          |LEE 101#12=;

          |SI FSalida ! 0;
               |HAZ AltaExpMatAlb;
          |FINSI;
          #12#4 = #0#7;                           ||Su Albaran.
          |GRABA 020#11a;
          |HAZ CalTotMatAlb;
          |HAZ CalTotMatPre;
          |HAZ CalTotMatExp;
     |FINSI;

     |CIERRA #12;|CIERRA #11;|CIERRA #10;
|FPRC;
--------------------------------------------------------------------------------
|PROCESO AltaExpMatCab;
     |ABRE #20;
     #20#0 = #10#0;
     #20#1 = #10#1;
     |LEE 000#20=;

     |SI FSalida = 0;
          |ABRE #11;
          nLinea = 0;
          |BUCLELIN 2 AltaExpMatLin #20;
          #10#18 = #20#2;                         ||Codigo Cliente.
          #10#19 = #20#8;                         ||Nombre Cliente.
          #10#20 = #20#6;                         ||Moneda Trabajo.
          #10#21 = cCodMonBase;                   ||Moneda Base.
          cCodMonActu = #20#6;
          cCodMonConv = "";
          |HAZ MonBas;
          #10#22 = nEurComBase;                   ||Valor Euro Comp. Trab.
          #10#23 = nEurComBase;                   ||Valor Euro Comp. Base.
          |GRABA 020#10c;
          |CIERRA #11;
     |FINSI;

     |CIERRA #20;
|FPRC;
--------------------------------------------------------------------------------
|PROCESO AltaExpMatLin;
     |DDEFECTO #11;
     #11#0 = #10#0;                               ||Serie Expediente.
     #11#1 = #10#1;                               ||Nº Expediente.
     nLinea = nLinea + 1;
     #11#2 = nLinea;
     #11#3 = #21#3;                               ||Serie Presupuesto.
     #11#4 = #21#4;                               ||Nº Presupuesto.
     #11#9 = #21#5;                               ||Tipo Doc.
     |GRABA 020#11c;
|FPRC;
--------------------------------------------------------------------------------
|PROCESO MiraLin;
     nLinea = #12#2;
|FPRC;
--------------------------------------------------------------------------------
|DEFBUCLE UltLin;
     #12#1;
     ;
     #11#3, #11#4,    1;
     #11#3, #11#4, 9999;
     ;
     NULL, MiraLin;
|FIN;
--------------------------------------------------------------------------------
|PROCESO AltaExpMatAlb;
     nLinea = 0;
     |BUCLE UltLin;
     |DDEFECTO #12;
     #12#0  = #11#3;                              ||Serie Presu.
     #12#1  = #11#4;                              ||Nº Presu.
     nLinea = nLinea + 1;
     #12#2  = nLinea;
     #12#3  = #0#2;                               ||Fecha Albar.
     #12#4  = #0#7;                               ||Su Numero Albar.
     #12#5  = #0#0;                               ||Serie Albar.
     #12#6  = #0#1;                               ||Nº Albar.
     #12#7  = #0#3;                               ||Cod. Prove.
     #12#8  = #0#4;                               ||Nombre Prove.
     #12#13 = #11#9;                              ||Tipo Doc.
     #12#14 = #11#0;                              ||Serie Exp.
     #12#15 = #11#1;                              ||Nº Exp.
     |GRABA 020#12c;
|FPRC;
--------------------------------------------------------------------------------
|PROCESO BajaMate;
     nImpComTrab = #2#14;
     nImpComBase = #2#25;
     nImpVenTrab = #2#13;
     nImpVenBase = #2#24;

     |SI cParam = "MATERIAL";
          nTotComTra = nTotComTra - nImpComTrab; ||Imp. Comp. Trabajo.
          nTotVenTra = nTotVenTra - nImpVenTrab; ||Imp. Vent. Trabajo.
          nTotComBas = nTotComBas - nImpComBase; ||Imp. Comp. Base.
          nTotVenBas = nTotVenBas - nImpVenBase; ||Imp. Vent. Base.
          |ACABA;
     |FINSI;

     nImpComTrab = (#2#14)*nAbono;
     nImpComBase = (#2#25)*nAbono;
     nImpVenTrab = (#2#13)*nAbono;
     nImpVenBase = (#2#24)*nAbono;
     |ABRE #10;
     #10#0 = #2#2;                                ||Serie Expediente.
     #10#1 = #2#3;                                ||Nº Expediente.
     |LEE 101#10=;

     |SI FSalida = 0;
          |ABRE #11;
          |SELECT #2#11;
          #11#3 = #2#0;                           ||Serie Presu.
          #11#4 = #2#1;                           ||Nº Presu.
          |LEE 101#11=;

          |SI FSalida = 0;
               |ABRE #12;
               |SELECT #5#12;
               |DDEFECTO #12;
               #12#0 = #2#0;                      ||Serie Presu.
               #12#1 = #2#1;                      ||Nº Presu.
               #12#5 = #2#4;                      ||Serie Albaran.
               #12#6 = #2#5;                      ||Numero Albaran.
               |LEE 101#12=;

               |SI FSalida = 0;
                    |HAZ CalTotMatAlb;
                    |HAZ CalTotMatPre;
                    |HAZ CalTotMatExp;

                    |SI #12#9 = 0; |Y #12#11 = 0;
                         |BORRA 020#12a;
                    |FINSI;

               |FINSI;

               |LIBERA #12;
               |CIERRA #12;
          |FINSI;

          |LIBERA #11;
          |CIERRA #11;
     |FINSI;
     |LIBERA #10;
     |CIERRA #10;
|FPRC;
--------------------------------------------------------------------------------
|PROCESO CalTotMatAlb;
     |SI Flag < 0;
        #12#9  = #12#9  - nImpComTrab;              ||Imp. Comp. Trabajo.
        #12#10 = #12#10 - nImpComBase;              ||Imp. Comp. Base.
        #12#11 = #12#11 - nImpVenTrab;              ||Imp. Vent. Trabajo
        #12#12 = #12#12 - nImpVenBase;              ||Imp. Vent. Base.
     |SINO;
        #12#9  = #12#9  + nImpComTrab;              ||Imp. Comp. Trabajo.
        #12#10 = #12#10 + nImpComBase;              ||Imp. Comp. Base.
        #12#11 = #12#11 + nImpVenTrab;              ||Imp. Vent. Trabajo
        #12#12 = #12#12 + nImpVenBase;              ||Imp. Vent. Base.
     |FINSI;

     |GRABA 020#12a;
|FPRC;
--------------------------------------------------------------------------------
|PROCESO CalTotMatPre;
     |SI Flag < 0;
        #11#5  = #11#5  - nImpComTrab;              ||Imp. Comp. Trabajo.
        #11#6  = #11#6  - nImpComBase;              ||Imp. Comp. Base.
        #11#7  = #11#7  - nImpVenTrab;              ||Imp. Vent. Trabajo.
        #11#8  = #11#8  - nImpVenBase;              ||Imp. Vent. Base.
     |SINO;
        #11#5  = #11#5  + nImpComTrab;              ||Imp. Comp. Trabajo.
        #11#6  = #11#6  + nImpComBase;              ||Imp. Comp. Base.
        #11#7  = #11#7  + nImpVenTrab;              ||Imp. Vent. Trabajo.
        #11#8  = #11#8  + nImpVenBase;              ||Imp. Vent. Base.
     |FINSI;
     |GRABA 020#11a;
|FPRC;
--------------------------------------------------------------------------------
|PROCESO CalTotMatExp;
     |SI Flag < 0;
        |SI #11#9 = "P";                             ||PRESUPUESTOS.
           #10#2  = #10#2  - nImpComTrab;         ||Imp. Comp. Trabajo.
           #10#3  = #10#3  - nImpComBase;         ||Imp. Comp. Base.
           #10#4  = #10#4  - nImpVenTrab;         ||Imp. Vent. Trabajo.
           #10#5  = #10#5  - nImpVenBase;         ||Imp. Vent. Base.
        |FINSI;
        |SI #11#9 = "A";                             ||ADMINISTRACION.
           #10#6  = #10#6  - nImpComTrab;         ||Imp. Comp. Trabajo.
           #10#7  = #10#7  - nImpComBase;         ||Imp. Comp. Base.
           #10#8  = #10#8  - nImpVenTrab;         ||Imp. Vent. Trabajo.
           #10#9  = #10#9  - nImpVenBase;         ||Imp. Vent. Base.
        |FINSI;
        |SI #11#9 = "E";                             ||EXTRAS.
           #10#10 = #10#10 - nImpComTrab;         ||Imp. Comp. Trabajo.
           #10#11 = #10#11 - nImpComBase;         ||Imp. Comp. Base.
           #10#12 = #10#12 - nImpVenTrab;         ||Imp. Vent. Trabajo.
           #10#13 = #10#13 - nImpVenBase;         ||Imp. Vent. Base.
        |FINSI;
        #10#14 = #10#14 - nImpVenTrab;              ||Total Venta Trabajo.
        #10#15 = #10#15 - nImpVenBase;              ||Total Venta Base.
        #10#16 = #10#16 - nImpComTrab;              ||Total Comp. Trabajo.
        #10#17 = #10#17 - nImpVenBase;              ||Total Comp. Base.
     |SINO;
        |SI #11#9 = "P";                             ||PRESUPUESTOS.
           #10#2  = #10#2  + nImpComTrab;         ||Imp. Comp. Trabajo.
           #10#3  = #10#3  + nImpComBase;         ||Imp. Comp. Base.
           #10#4  = #10#4  + nImpVenTrab;         ||Imp. Vent. Trabajo.
           #10#5  = #10#5  + nImpVenBase;         ||Imp. Vent. Base.
        |FINSI;
        |SI #11#9 = "A";                             ||ADMINISTRACION.
           #10#6  = #10#6  + nImpComTrab;         ||Imp. Comp. Trabajo.
           #10#7  = #10#7  + nImpComBase;         ||Imp. Comp. Base.
           #10#8  = #10#8  + nImpVenTrab;         ||Imp. Vent. Trabajo.
           #10#9  = #10#9  + nImpVenBase;         ||Imp. Vent. Base.
        |FINSI;
        |SI #11#9 = "E";                             ||EXTRAS.
           #10#10 = #10#10 + nImpComTrab;         ||Imp. Comp. Trabajo.
           #10#11 = #10#11 + nImpComBase;         ||Imp. Comp. Base.
           #10#12 = #10#12 + nImpVenTrab;         ||Imp. Vent. Trabajo.
           #10#13 = #10#13 + nImpVenBase;         ||Imp. Vent. Base.
        |FINSI;
        #10#14 = #10#14 + nImpVenTrab;              ||Total Venta Trabajo.
        #10#15 = #10#15 + nImpVenBase;              ||Total Venta Base.
        #10#16 = #10#16 + nImpComTrab;              ||Total Comp. Trabajo.
        #10#17 = #10#17 + nImpVenBase;              ||Total Comp. Base.
     |FINSI;
     |GRABA 020#10a;
|FPRC;
