|FICHEROS;
     albm0054 #54;
     albm0053 #53;
     albm0052 #52;
     albm0002 #2;
     albm0035 #35;
     albm0046 #46;

     agifa081 #81;
     agifa077 #77;
     agifa019 #119;
     agifa112 #112;
     agifa065 #65;

     dsw00034  #934;
     albm0002@ #902;
|FIN;

|VARIABLES;
     aFechaDesde = "";
     aParametro = "";
     aCodigoPro = "";
     aCodigoArt = "";
     aDescri    = "";
     aAlfa      = "";
     aFichero   = "";
     aArti      = "";
     aMas       = "";

     nCampo     = 0;
     nCampo1    = 0;
     nPuntos    = 0;
     nPedidos   = 0;
     nPartidas  = 0;
     nDias      = 0;
     nKilos     = 0;
     nPosicion  = 0;
     nBoton1    = 0;
     nBoton2    = 0;
     nPanta     = 0;
|FIN;

|PROCESO Fechas;
                     nDias = #81#2 - #77#1;
     |SI nDias < 0;  nDias = 0;  |FINSI;

     nPuntos = 0;
     |PARA nCampo = 1;  |SI nCampo [ 5;  |HACIENDO nCampo = nCampo + 1;
           nCampo1 = nCampo + 5;
           |SI nDias ] #52nCampo;
               nPuntos = #52nCampo1;
           |FINSI;
     |SIGUE;

     #54#8 = #54#8 + nPuntos;
|FPRC;

|PROCESO Kilos;
     nKilos = #2#6 - #2#15;
     |SI nKilos < 0;  nKilos = -nKilos;  |FINSI;

     nKilos  = (100 * nKilos) / #2#6;
     nPuntos = 0;

     |PARA nCampo = 11;  |SI nCampo [ 15;  |HACIENDO nCampo = nCampo + 1;
           nCampo1 = nCampo + 5;
           |SI nKilos ] #52nCampo;
               nPuntos = #52nCampo1;
           |FINSI;
     |SIGUE;

     #54#6 = #54#6 + nPuntos;
|FPRC;

|PROCESO agifa065;
     #934#0 = #65#10;
     |LEE 101#934=;
     |SI FSalida ! 0;
         |DDEFECTO #934;
         #934#0 = #65#10;
         |GRABA 020#934b;
     |FINSI;

     #934#2 = #65#1;
     #934#3 = #65#6;
     #934#4 = #65#14;
     #934#5 = #65#11;
     #934#6 = #65#3;
     #934#7 = #65#4;
     |GRABA 020#934a;
     |LIBERA #934;
|FPRC;

|DEFBUCLE agifa065;
     #65#1;
     ;
     #2#4, aFechaDesde, "AC", 000000, 00000, "     ";
     #2#4, "31.12.9999", "AC", 999999, 99999, "zzzzz";
     ;
     NULL, agifa065;
|FIN;

|PROCESO dsw00034;
     nPosicion = nPosicion + 1;

     |SI #934#0 = #2#7;
         |ERROR10;
     |FINSI;
|FPRC;

|DEFBUCLE dsw00034;
     #934#2;
     ;
     9999999, "      ";
     -999999, "zzzzzz";
     ;
     NULL, dsw00034;
|FIN;

|PROCESO Precios;
     aFichero = ("34" + Usuario + "zzzzzzzz") % 108;
     |ABRE #934;  |CIERRA #934;  |DELINDEX #934;

     |ABRE #934;
     |BUCLE agifa065;
     |CIERRA #934;

     nPosicion = 0;
     |BUCLE dsw00034;

     |ABRE #934;  |CIERRA #934;  |DELINDEX #934;

     nPuntos = 0;
     |PARA nCampo = 21;  |SI nCampo [ 25;  |HACIENDO nCampo = nCampo + 1;
           nCampo1 = nCampo + 5;
           |SI nPosicion ] #52nCampo;
               nPuntos = #52nCampo1;
           |FINSI;
     |SIGUE;

     #54#7 = #54#7 + nPuntos;
|FPRC;

|PROCESO albm0002;
     #81#25 = #2#0;
     #81#0  = #2#1;
     |LEE 000#81=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     #77#50 = #2#11;
     #77#0  = #2#12;
     |LEE 000#77=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     nPedidos = nPedidos + 1;

     |HAZ Fechas;
     |HAZ Kilos;
     |HAZ Precios;
|FPRC;

|DEFBUCLE albm0002;
     #2#3;
     2;
     #54#0, #54#1, aFechaDesde;
     #54#0, #54#1, "31.12.9999";
     ;
     NULL, albm0002;
|FIN;

|PROCESO albm0046;
     nPuntos = #52#31;

     #35#0 = #46#0;
     #35#2 = #46#1;
     #35#4 = #46#2;
     |LEE 000#35=;
     |SI FSalida = 0;
         |SI #35#39 = "S";  nPuntos = #52#34;  |FINSI;
         |SI #35#40 = "S";  nPuntos = #52#33;  |FINSI;
         |SI #35#41 = "S";  nPuntos = #52#32;  |FINSI;
     |FINSI;

     nPartidas = nPartidas + 1;
     #54#5 = #54#5 + nPuntos;
|FPRC;

|DEFBUCLE albm0046;
     #46#4;
     4;
     #54#0, #54#1, aFechaDesde;
     #54#0, #54#1, "31.12.9999";
     ;
     NULL, albm0046;
|FIN;

|PROCESO Calculo;
     nPedidos  = 0;
     nPartidas = 0;
     #54#5     = 0;
     #54#6     = 0;
     #54#7     = 0;
     #54#8     = 0;

     |ABRE #81;
     |ABRE #77;
     |ABRE #35;
     |BUCLE albm0002;
     |BUCLE albm0046;
     |CIERRA #35;
     |CIERRA #77;
     |CIERRA #81;

     #54#5 = #54#5 / nPartidas;
     #54#6 = #54#6 / nPedidos;
     #54#7 = #54#7 / nPedidos;
     #54#8 = #54#8 / nPedidos;

     |SI #54#6 = 0;  #54#6 = 7;  |FINSI;
     |SI #54#7 = 0;  #54#7 = 7;  |FINSI;
     |SI #54#8 = 0;  #54#8 = 7;  |FINSI;

     #54#4  = "CALCULO REALIZADO SOBRE " + nPedidos + " PEDIDOS Y SOBRE " + nPartidas + " PARTIDAS.";
     #54#21 = nPedidos;
     #54#22 = nPartidas;
|FPRC;

|PROCESO PintaCampos;
     |PINTA #54#4;
     |PINTA #54#5;
     |PINTA #54#6;
     |PINTA #54#7;
     |PINTA #54#8;
     |PINTA #54#9;
     |PINTA #54#10;
     |PINTA #54#11;
     |PINTA #54#12;
     |PINTA #54#13;
     |PINTA #54#14;
     |PINTA #54#15;
     |PINTA #54#16;
     |PINTA #54#17;
     |PINTA #54#18;
     |PINTA #54#19;
     |PINTA #54#20;
|FPRC;

|PROCESO CogeLiteral;
     |PARA nCampo = 35;  |SI nCampo [ 39;  |HACIENDO nCampo = nCampo + 1;
           nCampo1 = nCampo + 5;
           |SI nPuntos ] #52nCampo;
               aDescri = #52nCampo1;
           |FINSI;
     |SIGUE;
|FPRC;

|PROCESO Valoracion;
     |SI #54#5  > 10;  #54#5  = 10;  |FINSI;
     |SI #54#6  > 10;  #54#6  = 10;  |FINSI;
     |SI #54#7  > 10;  #54#7  = 10;  |FINSI;
     |SI #54#8  > 10;  #54#8  = 10;  |FINSI;
     |SI #54#9  > 10;  #54#9  = 10;  |FINSI;
     |SI #54#10 > 10;  #54#10 = 10;  |FINSI;
     |SI #54#11 > 10;  #54#11 = 10;  |FINSI;

     #54#12 = #54#5 + #54#6 + #54#7 + #54#8 + #54#9 + #54#10 + #54#11;

     nPuntos = #54#5;   aDescri = "";   |HAZ CogeLiteral;  #54#13 = aDescri;
     nPuntos = #54#6;   aDescri = "";   |HAZ CogeLiteral;  #54#14 = aDescri;
     nPuntos = #54#7;   aDescri = "";   |HAZ CogeLiteral;  #54#15 = aDescri;
     nPuntos = #54#8;   aDescri = "";   |HAZ CogeLiteral;  #54#16 = aDescri;
     nPuntos = #54#9;   aDescri = "";   |HAZ CogeLiteral;  #54#17 = aDescri;
     nPuntos = #54#10;  aDescri = "";   |HAZ CogeLiteral;  #54#18 = aDescri;
     nPuntos = #54#11;  aDescri = "";   |HAZ CogeLiteral;  #54#19 = aDescri;

     |PARA nCampo = 45;  |SI nCampo [ 49;  |HACIENDO nCampo = nCampo + 1;
           nCampo1 = nCampo + 5;
           |SI #54#12 ] #52nCampo;  |Y #52nCampo1 ! "               ";
               #54#20 = #52nCampo1;
           |FINSI;
     |SIGUE;
|FPRC;

|PROCESO Tipo12;  |TIPO 12;
|FPRC;

|PROCESO Tipo80;  |TIPO 80;
     |ABRE #52;
     |LEE 000#52p;
     |SI FSalida ! 0;  |DDEFECTO #52;  |FINSI;
     |CIERRA #52;

     aFechaDesde = #52#56;

     FSalida = 2799;
|FPRC;

|PROCESO Boton1;
     |ESTADO_CONTROL nBoton1, "DISABLE";
     |ESTADO_CONTROL nBoton2, "DISABLE";

     |HAZ Calculo;
     |HAZ Valoracion;
     |PINDA #0#54;

     |ESTADO_CONTROL nBoton1, "ENABLE";
     |ESTADO_CONTROL nBoton2, "ENABLE";
|FPRC;

|PROCESO albm0002R;
     |SI aArti ! #902#4;
         #54#0 = #112#0;
         #54#1 = #902#4;
         |LEE 101#54=;
         |SI FSalida ! 0;
             |DDEFECTO #54;
             #54#0 = #112#0;
             #54#1 = #902#4;
             |GRABA 020#54b;
         |FINSI;

         #54#2 = #112#1;

         #119#0 = #54#1;
         |LEE 000#119=;
         |SI FSalida ! 0;  |DDEFECTO #119;  |FINSI;

         #54#3 = #119#1;

         |HAZ Calculo;
         |HAZ Valoracion;
         |GRABA 020#54a;
         |LIBERA #54;

         aArti = #902#4;
     |FINSI;
|FPRC;

|DEFBUCLE albm0002R;
     #902#3002;
     2;
     #112#0, "                    ", aFechaDesde;
     #112#0, "zzzzzzzzzzzzzzzzzzzz", "31.12.9999";
     ;
     NULL, albm0002R;
|FIN;

|PROCESO agifa112;
     aArti = "";
     |BUCLE albm0002R;
|FPRC;

|DEFBUCLE agifa112;
     #112#1;
     ;
     "      ";
     "zzzzzz";
     ;
     NULL, agifa112;
|FIN;

|PROCESO Boton2;
     |CONFI "0000NSeguro de Calcular a Todos los Proveedores-Articulos.";
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |DBASE aMas;  |QBF aMas;
     aMas = aMas + "def/albm0002.mas";
     |CARGA_DEF aMas, albm0002@;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |ESTADO_CONTROL nBoton1, "DISABLE";
     |ESTADO_CONTROL nBoton2, "DISABLE";

     |ABRE #119;
     |BUCLE agifa112;
     |CIERRA #119;

     #54#0 = aCodigoPro;
     #54#1 = aCodigoArt;
     |LEE 101#54=;

     |PINDA #0#54;

     |ESTADO_CONTROL nBoton1, "ENABLE";
     |ESTADO_CONTROL nBoton2, "ENABLE";

     |DESCARGA_DEF #albm0002@;
|FPRC;

|PROCESO CogeProveedor;
     |ABRE #53;
     #53#0 = #54#0;
     |LEE 000#53=;
     |SI FSalida ! 0;  |DDEFECTO #53;  |FINSI;
     |CIERRA #53;

     #54#9  = #53#7;
     #54#10 = #53#8;
     #54#11 = #53#9;
|FPRC;

|PROGRAMA;
     aParametro = PARAMETRO;  |QBF aParametro;
     aCodigoPro = aParametro % 706;
     aCodigoArt = aParametro % 1320;
     aParametro = aParametro % 106;

     |SI aParametro = "CALCUL";
         |ABRE #54;
         #54#0 = aCodigoPro;
         #54#1 = aCodigoArt;
         |LEE 101#54=;
         |SI FSalida ! 0;
             |DDEFECTO #54;
             #54#0 = aCodigoPro;
             #54#1 = aCodigoArt;
             |GRABA 020#54b;
         |FINSI;

         |ABRE #112;
         #112#0 = #54#0;
         |LEE 000#112=;
         |SI FSalida ! 0;  |DDEFECTO #112;  |FINSI;
         |CIERRA #112;

         #54#2 = #112#1;

         |ABRE #119;
         #119#0 = #54#1;
         |LEE 000#119=;
         |SI FSalida ! 0;  |DDEFECTO #119;  |FINSI;
         |CIERRA #119;

         #54#3 = #119#1;

         |HAZ CogeProveedor;
         |HAZ Calculo;
         |HAZ Valoracion;
         |GRABA 020#54a;
         |LIBERA #54;
         |CIERRA #54;

         |ACABA;
     |FINSI;

     |ABRE #54;

     #54#0 = aCodigoPro;
     #54#1 = aCodigoArt;
     |LEE 101#54=;
     |SI FSalida ! 0;
         |DDEFECTO #54;
         #54#0 = aCodigoPro;
         #54#1 = aCodigoArt;
         |GRABA 020#54b;
     |FINSI;

     |ABRE #112;
     #112#0 = #54#0;
     |LEE 000#112=;
     |SI FSalida ! 0;  |DDEFECTO #112;  |FINSI;
     |CIERRA #112;

     #54#2 = #112#1;

     |ABRE #119;
     #119#0 = #54#1;
     |LEE 000#119=;
     |SI FSalida ! 0;  |DDEFECTO #119;  |FINSI;
     |CIERRA #119;

     #54#3 = #119#1;

     |PINPA #0#54;
     nPanta = FSalida;

     |HAZ CogeProveedor;
     |HAZ Valoracion;

     |PINDA #0#54;

     |CONTROL_SIMPLE nPanta,"BOTON,{{217}} Refrescar Proveedor-Articulo",  0560, 0598, Boton1;
     nBoton1  = FSalida;

     |CONTROL_SIMPLE nPanta,"BOTON,{{218}} Refrescar Todos los Proveedores-Articulos",  0660, 0698, Boton2;
     nBoton2  = FSalida;

     |ENDATOS #1#54;
     |SI FSalida = 0;
         |GRABA 020#54a;
     |FINSI;
     |LIBERA #54;
     |CIERRA #54;

     |DESTRUYECONTROL nBoton1;
     |DESTRUYECONTROL nBoton2;

     |PULSATECLA;
|FPRO;
