********************** APERTURA DEL CAJON ***************************
Rutina: AbreCajon -> al llamar el agifa505 debe estar leido!!!


|FICHEROS;
     agifa505 #505;
|FIN;

|VARIABLES;
     &enClaveCajon = 0;
     aUsu = "";
     aAlfa = "";
     nHandle = 0;
     aFic = "";
     aPath = "";
     aNom = "";
     aClave = "";
     aClavePide = "";
     aTipo = "";
     nNum = 0;

     alfa1 = "";
     alfa2 = "";
     nume1 = 0;
     para1 = 0;
     camuf = 0;
     camufz = "";
     clave = "";

     aProg = "";
     aImpre= "";
     aSecu = "";

     ascii = "";
     secu  = "";
     traba = "";
     trab1 = "";
     trab2 = "";
     trab3 = "";
     nume = 0;
     num1 = 0;
     num2 = 0;
     x = 0;
     y = 0;
|FIN;

|PROCESO PasaaNume;
     secu = "";
     trab1 = traba % 0;
     nume = trab1;
     |PARA x = 1; |SI x [ nume;  |HACIENDO x = x + 1;
          num1 = (100 *  x) + 1;
          trab2 = num1;
          trab1 = traba % trab2;
          |SI trab1 = "\";
               |HAZ SecEscape;
               trab2 = trab1;
          |SINO;
               num1 = &trab1;
               trab2 = num1;
          |FINSI;

          ascii = "000" + trab2;  ascii = ascii % -103;
          |SI ascii = "000"; |O ascii = "027";
               secu = secu + &27 + ascii;
          |SINO;
               secu = secu + &trab2;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO SecEscape;
     trab3 = "";
     x = x + 1;
     num1 = (100 * x) + 1;
     trab2 = num1;
     trab1 = traba % trab2;
     |SI trab1 = "\";
          trab1 = "92";
          |ACABA;
     |FINSI;
     trab3 = trab3 + trab1;
     x = x + 1;
     num1 = (100 * x) + 1;
     trab2 = num1;
     trab1 = traba % trab2;
     |SI trab1 = "\";
          trab1 = trab3;
          |ACABA;
     |FINSI;
     trab3 = trab3 + trab1;
     x = x + 1;
     num1 = (100 * x) + 1;
     trab2 =  num1;
     trab1 = traba % trab2;
     |SI trab1 = "\";
          trab1 = trab3;
          |ACABA;
     |FINSI;
     trab3 = trab3 + trab1;
     x = x + 1;
     trab1 = trab3;
|FPRC;

|PROCESO Desencripta;
     clave = aClave;
     camufz = "";
     |PARA para1 = 1; |SI para1 [ 20; |HACIENDO para1 = para1 + 2;
          nume1 = (para1 * 100) + 1;           alfa1 = clave % nume1;
          nume1 = ((para1 + 1) * 100) + 1;     alfa2 = clave % nume1;

          nume1 = &alfa2;
          nume1 = nume1 - 65;

          camuf = &alfa1;
          camuf = camuf - 65;
          camuf = ((camuf * 26) + nume1) / 2;

          camufz = camufz + &camuf;
     |SIGUE;
     aClave = camufz;
|FPRC;

|PROCESO PideClave;
     |PUSHV 2401 2480;
     |ATRI I;
     |PINTA 2425, "  Apertura Cajon:            ";
     |ATRI 0;
     E_sup = 10;
     E_inf = "";
     aClavePide = "";
     aClavePide = 2443 ? -1;
     |SI FSalida ! 0; aClavePide = ""; |FINSI;
     |POPV;
|FPRC;

|PROCESO LeeUsu;
     aUsu = Usuario % 810; |QBF aUsu;
     |DIRECTORIO aPath;
     aFic = aPath + "dev/usr/ds_sys.psw"
     |XABRE "U", aFic, nHandle;
     |SI FSalida ] 0;
          |XLEE nHandle, 250, aAlfa;
          |PARA; |SI FSalida ] 0; |HACIENDO;
               aNom = aAlfa % 410; |QBF aNom;
               aClave = aAlfa % 1420;
               aTipo = aAlfa % 3401;
               nNum = (A\aAlfa % 103);
               |SI aNom = aUsu;
                    |HAZ Desencripta;
                    |SAL;
               |FINSI;
               |XLEE nHandle, 250, aAlfa;
          |SIGUE;
          |XCIERRA nHandle;
     |FINSI;
|FPRC;

|PROCESO AbreCajon;
     |SI #505#145 = "S"; |Y enClaveCajon = 0;
          |HAZ LeeUsu;
          |HAZ PideClave;
          |SI aClave ! aClavePide;
               |MENAV "0000Clave incorrecta...";
               |ACABA;
          |FINSI;
     |FINSI;

     |SI #505#142 ! "P";
          traba = #505#28; |QBF traba;
          |HAZ PasaaNume;
          aSecu = secu;
     |FINSI;
     aImpre = #505#66;  |QBF aImpre;

     |SI #505#142 = "P";
          aProg = #505#28; |QBF aProg;
          |SI aProg ! "";
               |RSYSTEM aProg;
          |FINSI;
     |SINO;
          |SI aImpre ! ""; |Y aSecu ! "";
               Impresora = aImpre;
               |IMPRE -1;
               |IMPRIMESIN aSecu;
               |FINIMP;
          |FINSI;
     |FINSI;
|FPRC;
