|FICHEROS;
     ortsy008 #0;
     agifa019 #19;
     ortsm020 #20;
     ortsm036 #36;
     agifa049 #49;
     agifa048 #480;
     ortsw008 #8; || Tmp volumen

     ortsm019 #190; || Aux. Escan Cab
|FIN;

|VARIABLES;
     &Tempo = "";
     aEti = "";
     nLin = 0;
     aAlfa = "";
     nVol = 0;
     nTVol = 0;
     nNume = 0;
     aObserva = "";
|FIN;

|PROCESO Borra49_20;
     #20#0 = #49#0;
     #20#1 = #49#1;
     |BORRA 000#20c;
     |BORRA 020#49a;
|FPRC;

|DEFBUCLE Borra49_20;
     #49#1;
     10;
     #480#0, 001, 4;
     #480#0, 999, 4;
     ;
     NULL, Borra49_20;
|FIN;

|PROCESO LinEscandallo;
     #8#0 = #49#0;
     #8#1 = #49#1;
     |LEE 020#8=;

     |DDEFECTO #20;
     #20#0 = #49#0;
     #20#1 = #49#1;
     #20#2 = "N";
     #20#20 = #8#2;
     |GRABA 020#20n;
|FPRC;

|DEFBUCLE LinEscandallo;
     #49#1;
     10;
     #480#0, 001, 4;
     #480#0, 999, 4;
     ;
     NULL, LinEscandallo;
|FIN;

|PROCESO Graba49;
     #49#0 = #480#0;
     #49#1 = 999;
     |LEE 000#49];
     |SI FSalida = 0;
          |LEE 000#49a;
     |SINO;
          |LEE 000#49u;
     |FINSI;
     |SI FSalida = 0; |Y #49#0 = #480#0;
          nLin = #49#1 + 1;
     |SINO;
          nLin = 1;
     |FINSI;
     |DDEFECTO #49;
     #49#0 = #480#0;
     #49#1 = nLin;
     #49#2 = aEti;
     aAlfa = "Bulto nr.: " + aEti;
     #49#3 = aAlfa;
     #49#4 = 1;
     #49#10 = 4;
     #49#7 = aObserva;
     |GRABA 020#49n;

     #8#0 = #49#0;
     #8#1 = #49#1;
     #8#2 = nVol;
     |GRABA 020#8n;

     nTVol = nTVol + nVol;
     nVol = 0;
|FPRC;

|PROCESO AuxEscanLin;
     #49#0 = #20#0;
     #49#1 = #20#1;
     |LEE 000#49=;

     aAlfa = #20#3; |QBF aAlfa;
     |SI aAlfa = ""; |ACABA; |FINSI;

     |SI aEti = ""; aEti = #20#3; aObserva = #20#16; |FINSI;

     |SI aEti ! #20#3;
          |HAZ Graba49;
          aEti = #20#3;
          aObserva = #20#16;
     |FINSI;
     |SI #36#25 = "S";
          nNume = #20#20 + (#20#20 * #36#26 / 100);
          nVol = nVol + nNume;
     |SINO;
          nVol = nVol + #20#20;
     |FINSI;
|FPRC;

|DEFBUCLE AuxEscanLin;
     #20#2;
     ;
     #480#0, "                    ";
     #480#0, "zzzzzzzzzzzzzzzzzzzz";
     ;
     NULL, AuxEscanLin;
|FIN;

|PROCESO Escandallo;
     #190#0 = #480#0;
     |LEE 000#190=;
     |SI FSalida ! 0; |DDEFECTO #190; |FINSI;

     |SI #190#2 = "S";
          |ABRE #20;
          |BUCLE Borra49_20;
          |CIERRA #20;

          nVol = 0;
          nTVol = 0;
          aEti = "";
          |ABRE #49;
          |BUCLE AuxEscanLin;
          |SI aEti ! ""; |HAZ Graba49; |FINSI;
          |CIERRA #49;

          |SI #36#28 = "S";
               #19#0 = #480#0;
               |LEE 121#19=;
               |SI FSalida = 0;
                    #19#114 = nTVol;
                    |GRABA 020#19a;
                    |LIBERA #19;
               |FINSI;
          |FINSI;

          |ABRE #20;
          |BUCLE LinEscandallo;
          |CIERRA #20;
     |FINSI;
|FPRC;

|DEFBUCLE Escandallo;
     #480#1;
     ;
     #0#0;
     #0#1;
     ;
     NULL, Escandallo;
|FIN;

|PROCESO T3; |TIPO 3;
     |HAZ CreaTempo; |NOME_DAT #8 Tempo; |DELINDEX #8; |ABRE #8;

     |ABRE #36; |LEE 000#36p; |CIERRA #36;

     |ABRE #19;
     |ABRE #190;
     |BUCLE Escandallo;
     |CIERRA #190;
     |CIERRA #19;

     |CIERRA #8; |HAZ BoraTempo; |DELINDEX #8;
|FPRC;
