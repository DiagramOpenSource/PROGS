|| El pedido se crea en el por00001, en CrearPedidoAlSalir


|FICHEROS;
     porm0001 #0;
     porm0002 #2,MANTE;  || temporal clientes
     porm0003 #3,MANTE;  || datos nuevo cliente
     porm0004 #4,MANTE;  || temporal cliente nuevo
     porm0005 #5,MANTE;  || temporal para modificacion de datos de cliente
     porm0006 #6,MANTE;  || consulta datos cliente, al pulsar 'mas datos'
                         || en cliente antiguo
     porm0007 #7,MANTE;  || fichero de facturas de un cliente

     agifa024 #24; || clientes
     agifa134 #134; || cabecera de facturas
     agifa091 #91;  || formas de pago, para leer la descripcion

     poro0003 #103,MANTE;  ||Direcciones de entrega
|FIN;

|VARIABLES;
     &Tempo = "";
     aTmp2 = "";
     aTmp7 = "";
     aCodigoAux="";
     aNombreAux="";
     aPoblacionAux="";
     nCP1Aux=0;
     nCP2Aux=0;
     aClienteSuperAux="";

     nNume1=0;
     nClientesTotal=0;
     nClienteActual=0;

     nTecla=0;
     i=0;
     aTecla="";

     Titulo1="";
     Titulo2="";

     aEstado="";

     aMensaje="0000Tienes que Seleccionar un Cliente!";
     &nEsPedido=0;
     nCero=0;
     totalc=0;
     nContador=0;
     &aClienteSuper="";

     nUltimo=0;
     nSeguir=0;

     aCadena1="";
     aCadena2="";
     aCadena3="";
     aCadena4="";
     aCadena5="";
     aCadena6="";

     aPoblacion="";
     aProvincia="";

     &eaTag="";   || para llamar al proceso de obtener el riesgo
     &nRieTotal  = 0;
     &nRieUtili  = 0;
     aParametro="";

     nBotonx1=0;
     nBotonx2=0;
     nBotonx3=0;
     nBotonx4=0;

     nBotonx21=0;
     nBotonx22=0;
     nBotonx23=0;
     nBotonx24=0;
     nBotonx25=0;
     nBotonx26=0;
     nBotonx27=0;

     nBotonx31=0;
     nBotonx32=0;
     nBotonx33=0;
     nBotonx34=0;
     nBotonx35=0;
     nBotonx36=0;
     nBotonx37=0;

     nBotonx888=0;

|| variables para llenar de ceros
     aCadena="";
     r_alfa = "";
     r_long = "";
     r_num  = 0;
     r_masc = "00000";

     &nFiltro = 0;        ||Indica si hemos filtrado (1) o no (o) los clientes.
     aMensaget = "";

     puente = "";
     puente1 = "";
     aAlfa = "";
|FIN;

|PROCESO Tipo12; |TIPO 12;

|FPRC;

|PROCESO llena_ceros;
          |QBF aCadena;
          r_alfa= aCadena;
          |QBF r_alfa;
          r_long = aCadena % A0;
          r_alfa = r_masc + r_alfa;
          r_num = -105;
          aCadena = r_alfa % r_num;
|FPRC;


|PROCESO PulsaRegActualCli;
|FPRC;

|PROCESO PintaRegistroActual;
     |SI nBotonx888!0;
          |FIN_CONTROL_WINDOWS nBotonx888;
     |FINSI;

     aCadena1="BOTON,Cliente "+nClienteActual+"/"+nClientesTotal;
     |CONTROL_SIMPLE 0, aCadena1, 2414, 2433, PulsaRegActualCli;
     nBotonx888 = FSalida;
|FPRC;

|PROCESO QuitaRegistroActual;
     |FIN_CONTROL_WINDOWS nBotonx888;
     nBotonx888=0;
|FPRC;

|PROCESO GenerarPedidoPresupuestoA;
     aCadena1=#0#2;
     |QBF aCadena1;
     |SI aCadena1="";
          |MENAV aMensaje;
     |SINO;
          aClienteSuper=#0#2;
          |SUB_EJECUTA "por00001;porm0001a";
     |FINSI;
|FPRC;

|PROCESO GenerarPedidoPresupuestoN;
     aCadena1=#4#0;
     |QBF aCadena1;
     |SI aCadena1="";
          |MENAV aMensaje;
     |SINO;
          aClienteSuper=#4#0;
          |SUB_EJECUTA "por00001;porm0001n";
     |FINSI;
|FPRC;


|PROCESO min;
     #7#0="  ";
     #7#1=000001;
|FPRC;

|PROCESO max;
     #7#0="zz";
     #7#1=999999;
|FPRC;

|PROCESO RellenaMasDatos; || abre el fichero de clientes y leemos sus datos
     |ABRE #24;
     #24#0=#0#2;
     |LEE 000#24=;
     |SI FSalida!0;
          |MENAV aMensaje;
          |ACABA;
     |FINSI;
     |CIERRA #24;

     #6#0=#24#0;
     #6#1=#24#1;
     #6#2=#24#5;
     #6#3=#24#178;
     #6#4=#24#179;
     #6#5=#24#6;
     #6#6=#24#7;
     #6#7=#24#17;
     #6#8=#24#15;
     #6#9=#24#189;
     #6#10=#24#47;
     #6#11=#24#29;
     #6#12=#24#32;
     #6#13=#24#33;
     #6#14=#24#31;
     #6#15=#24#30;

     |ABRE #91;
     #91#0=#24#20;
     |LEE 000#91=;
     |CIERRA #91;
     #6#16=#91#1;   || forma de pago

     #6#17=#24#8;
     #6#18=#24#181;
     #6#19=#24#182;
     #6#20=#24#9;
     #6#21=#24#10;
     #6#22=#24#11;
     #6#23=#24#184;
     #6#24=#24#185;
     #6#25=#24#12;
     #6#26=#24#13;
     #6#27=#24#37;
     #6#28=#24#38;
     #6#29=#24#156;
     #6#30=#24#106;
     #6#31=#24#154;
     #6#32=#24#102;
     #6#33=#24#151;

     nRieTotal  = 0;
     nRieUtili  = 0;
     eaTag="PORTATIL" + #6#0 + #6#1 ;
     |HAZ SacaRiesgo;
     #6#34=nRieTotal;     || puede ser riesgo total, y riesgo utilizado
|FPRC;

|PROCESO RellenaMasDatosBis; || abre el fichero de clientes y leemos sus datos
     #6#0=#24#0;
     #6#1=#24#1;
     #6#2=#24#5;
     #6#3=#24#178;
     #6#4=#24#179;
     #6#5=#24#6;
     #6#6=#24#7;
     #6#7=#24#17;
     #6#8=#24#15;
     #6#9=#24#189;
     #6#10=#24#47;
     #6#11=#24#29;
     #6#12=#24#32;
     #6#13=#24#33;
     #6#14=#24#31;
     #6#15=#24#30;

     |ABRE #91;
     #91#0=#24#20;
     |LEE 000#91=;
     |CIERRA #91;
     #6#16=#91#1;   || forma de pago

     #6#17=#24#8;
     #6#18=#24#181;
     #6#19=#24#182;
     #6#20=#24#9;
     #6#21=#24#10;
     #6#22=#24#11;
     #6#23=#24#184;
     #6#24=#24#185;
     #6#25=#24#12;
     #6#26=#24#13;
     #6#27=#24#37;
     #6#28=#24#38;
     #6#29=#24#156;
     #6#30=#24#106;
     #6#31=#24#154;
     #6#32=#24#102;
     #6#33=#24#151;

     nRieTotal  = 0;
     nRieUtili  = 0;
     eaTag="PORTATIL" + #6#0 + #6#1 ;
     |HAZ SacaRiesgo;
     #6#34=nRieTotal;     || puede ser riesgo total, y riesgo utilizado

     |PINDA #0#6;
|FPRC;


|PROCESO PBuscaFacturasCliente;
     nContador=nContador+1;
     #7#0=#134#49;
     #7#1=#134#0;
     #7#2=#134#1;
     #7#3=#134#101;
     #7#4=#0#3;

     |GRABA 020#7n;
|FPRC;

|DEFBUCLE BBuscaFacturasCliente;
     #134#1;
     2;
     "  ",000001,#0#2;  || buscamos las facturas del cliente #0#2
     "zz",999999,#0#2;
     ;
     NULL,PBuscaFacturasCliente;
|FIN;

|PROCESO ClienteAnterior;
     |SI nFiltro = 1;
          |ABRE #porm0002;
          #porm0002#0=#6#0;
          |LEE 000#2=;
          |LEE 000#2a;
          |SI FSalida = 0;
               |ABRE #24;
               #24#0=#2#0;
               |LEE 000#24=;
               |SI FSalida=0;
                    |HAZ PonDatosClienteAntiguo;  || acutalizar pantalla 1
                    |HAZ RellenaMasDatosBis;
                    nClienteActual=nClienteActual-1;
               |SINO;
                    |MENAV "0000Primera Ficha";
               |FINSI;
          |SINO;
               |MENAV "0000Primera Ficha";
          |FINSI;
          |CIERRA #24;
          |CIERRA #2;
     |SINO;
          |ABRE #24;
          #24#0=#6#0;
          |LEE 000#24=;
          |LEE 000#24a;
          |SI FSalida=0;
               |HAZ PonDatosClienteAntiguo;  || acutalizar pantalla 1
               |HAZ RellenaMasDatosBis;
               nClienteActual=nClienteActual-1;
          |SINO;
               |MENAV "0000Primera Ficha";
          |FINSI;
          |CIERRA #24;
     |FINSI;
|FPRC;

|PROCESO ClienteSiguiente;
     ||CCC
     |SI nFiltro = 1;
          aMensaget = "0000Clientes Filtrados";
          ||MENAV aMensaget;
          |ABRE #porm0002;
          #porm0002#0=#6#0;
          |LEE 000#2=;
          |LEE 000#2s;
          |SI FSalida = 0;
               |ABRE #24;
               #24#0=#2#0;
               |LEE 000#24=;
               |SI FSalida=0;
                    |HAZ PonDatosClienteAntiguo;  || acutalizar pantalla 1
                    |HAZ RellenaMasDatosBis;
                    nClienteActual=nClienteActual+1;
               |SINO;
                    |MENAV "0000Ultima Ficha";
               |FINSI;
          |SINO;
               |MENAV "0000Ultima Ficha";
          |FINSI;
          |CIERRA #24;
          |CIERRA #2;
     |SINO;
          |ABRE #24;
          #24#0=#6#0;
          |LEE 000#24=;
          |LEE 000#24s;
          |SI FSalida=0;
               |HAZ PonDatosClienteAntiguo;  || acutalizar pantalla 1
               |HAZ RellenaMasDatosBis;
               nClienteActual=nClienteActual+1;
          |SINO;
               |MENAV "0000Ultima Ficha";
          |FINSI;
          |CIERRA #24;
     |FINSI;
|FPRC;

|PROCESO ClientePrimero;
     |SI nFiltro = 1;
          |ABRE #porm0002;
          #porm0002#0=#6#0;
          |LEE 000#2=;
          |LEE 000#2p;
          |SI FSalida = 0;
               |ABRE #24;
               #24#0=#2#0;
               |LEE 000#24=;
               |SI FSalida=0;
                    |HAZ PonDatosClienteAntiguo;  || acutalizar pantalla 1
                    |HAZ RellenaMasDatosBis;
                    nClienteActual=1;
               |FINSI;
          |FINSI;
          |CIERRA #24;
          |CIERRA #2;
     |SINO;
          |ABRE #24;
          #24#0=#6#0;
          |LEE 000#24=;
          |LEE 000#24p;
          |SI FSalida=0;
               |HAZ PonDatosClienteAntiguo;  || acutalizar pantalla 1
               |HAZ RellenaMasDatosBis;
               nClienteActual=1;
          |FINSI;
          |CIERRA #24;
      |FINSI;
|FPRC;

|PROCESO ClienteUltimo;
     |SI nFiltro = 1;
          |ABRE #porm0002;
          #porm0002#0=#6#0;
          |LEE 000#2=;
          |LEE 000#2u;
          |SI FSalida = 0;
               |ABRE #24;
               #24#0=#2#0;
               |LEE 000#24=;
               |SI FSalida=0;
                    |HAZ PonDatosClienteAntiguo;  || acutalizar pantalla 1
                    |HAZ RellenaMasDatosBis;
                    nClienteActual=nClientesTotal;
               |FINSI;
          |FINSI;
          |CIERRA #24;
          |CIERRA #2;
     |SINO;
          |ABRE #24;
          #24#0=#6#0;
          |LEE 000#24=;

          |LEE 000#24u;
          |SI FSalida=0;
               |HAZ PonDatosClienteAntiguo;  || acutalizar pantalla 1
               |HAZ RellenaMasDatosBis;
               nClienteActual=nClientesTotal;
          |FINSI;
          |CIERRA #24;
      |FINSI;
|FPRC;

|PROCESO PonEstadoNavegador;
     |ESTADO_CONTROL nBotonx31,"ENABLE";
     |ESTADO_CONTROL nBotonx32,"ENABLE";
     |ESTADO_CONTROL nBotonx33,"ENABLE";
     |ESTADO_CONTROL nBotonx34,"ENABLE";

     |SI nClienteActual=1;
          |ESTADO_CONTROL nBotonx31,"DISABLE";
          |ESTADO_CONTROL nBotonx32,"DISABLE";
          |SI nClientesTotal=1;
               |ESTADO_CONTROL nBotonx33,"DISABLE";
               |ESTADO_CONTROL nBotonx34,"DISABLE";
          |FINSI;
     |FINSI;

     |SI nClienteActual=nClientesTotal;
          |ESTADO_CONTROL nBotonx33,"DISABLE";
          |ESTADO_CONTROL nBotonx34,"DISABLE";
          |SI nClienteActual=1;
               |ESTADO_CONTROL nBotonx31,"DISABLE";
               |ESTADO_CONTROL nBotonx32,"DISABLE";
          |FINSI;
     |FINSI;
|FPRC;


|PROCESO PulsaPrimeroMasDCA;
     |PTEC 824;
|FPRC;

|PROCESO PulsaAnteriorMasDCA;
     |PTEC 825;
|FPRC;

|PROCESO PulsaSiguienteMasDCA;
     |PTEC 826;
|FPRC;

|PROCESO PulsaUltimoMasDCA;
     |PTEC 827;
|FPRC;

|PROCESO PulsaFacturasMasDCA;
     |PTEC 828;
|FPRC;

|PROCESO PulsaDirEnt;    ||F7 Dir. Entrega.
     |PTEC 829;
|FPRC;

|PROCESO PulsaVolverMasDCA;   ||F8 Volver
     |PTEC 823;
|FPRC;

|PROCESO PonBotonesMasDCA;
     |CONTROL_SIMPLE 0, "BOTON,|<< F2", 2402, 2407, PulsaPrimeroMasDCA;
     nBotonx31 = FSalida;
     |CONTROL_SIMPLE 0, "BOTON,< F3", 2408, 2413, PulsaAnteriorMasDCA;
     nBotonx32 = FSalida;
     |CONTROL_SIMPLE 0, "BOTON,F4 >", 2434, 2439, PulsaSiguienteMasDCA;
     nBotonx33 = FSalida;
     |CONTROL_SIMPLE 0, "BOTON,F5 >>|", 2440, 2445, PulsaUltimoMasDCA;
     nBotonx34 = FSalida;
     |CONTROL_SIMPLE 0, "BOTON,F6 Facturas", 2446, 2456, PulsaFacturasMasDCA;
     nBotonx35 = FSalida;
     |CONTROL_SIMPLE 0, "BOTON,F7 Dir. Entrega", 2457, 2467, PulsaDirEnt;
     nBotonx37 = FSalida;
     |CONTROL_SIMPLE 0, "BOTON,F12 SALIR", 2468, 2478, PulsaVolverMasDCA;
     nBotonx36 = FSalida;
|FPRC;

|PROCESO QuitaBotonesMasDCA;
     |FIN_CONTROL_WINDOWS nBotonx31;
     |FIN_CONTROL_WINDOWS nBotonx32;
     |FIN_CONTROL_WINDOWS nBotonx33;
     |FIN_CONTROL_WINDOWS nBotonx34;
     |FIN_CONTROL_WINDOWS nBotonx35;
     |FIN_CONTROL_WINDOWS nBotonx36;
     |FIN_CONTROL_WINDOWS nBotonx37;
     |PULSATECLA;
|FPRC;

|PROCESO EstadoBotonesMasDCA;
     |ESTADO_CONTROL nBotonx31,aEstado;
     |ESTADO_CONTROL nBotonx32,aEstado;
     |ESTADO_CONTROL nBotonx33,aEstado;
     |ESTADO_CONTROL nBotonx34,aEstado;
     |ESTADO_CONTROL nBotonx35,aEstado;
     |ESTADO_CONTROL nBotonx36,aEstado;
     |ESTADO_CONTROL nBotonx37,aEstado;
     |ESTADO_CONTROL nBotonx888,aEstado;
|FPRC;

|PROCESO PulsaVentasFacturas;
     |PTEC 802;
|FPRC;

|PROCESO PulsaVolverFacturas;
     |PTEC 807;
|FPRC;

|PROCESO PulsaSubirFacturas;
     |PTEC 808;
|FPRC;

|PROCESO PulsaBajarFacturas;
     |PTEC 809;
|FPRC;

|PROCESO PonBotonesFacturas;
     |CONTROL_SIMPLE 0, "BOTON,Ver Ventas", 1155, 1275, PulsaVentasFacturas;
     nBotonx1 = FSalida;
     |CONTROL_SIMPLE 0, "BOTON,Volver", 2263, 2375, PulsaVolverFacturas;
     nBotonx2 = FSalida;
     |CONTROL_SIMPLE 0, "BOTON,Subir", 1008, 1015, PulsaSubirFacturas;
     nBotonx3 = FSalida;
     |CONTROL_SIMPLE 0, "BOTON,Bajar", 1208, 1215, PulsaBajarFacturas;
     nBotonx4 = FSalida;
|FPRC;

|PROCESO QuitaBotonesFacturas;
     |FIN_CONTROL_WINDOWS nBotonx1;
     |FIN_CONTROL_WINDOWS nBotonx2;
     |FIN_CONTROL_WINDOWS nBotonx3;
     |FIN_CONTROL_WINDOWS nBotonx4;
     |PULSATECLA;
|FPRC;


|PROCESO VerFacturas;
               |DELINDEX #7;              || crear fichero de faturas del cliente
               |ABRE #7;
               nContador=0;
               |BUCLE BBuscaFacturasCliente;
               |CIERRA #7;

               |SI nContador>0;
                    |PUSHV 0501 2380;

                    |HAZ PonBotonesFacturas;
                    |ENTLINEAL #1#7,1,4,22,1,min,max;
                    |HAZ QuitaBotonesFacturas;

                    |POPV;
               |SINO;
                    |MENAV "0000No Existen Facturas para este cliente!";
               |FINSI;
|FPRC;

|PROCESO ConsultaDatosClienteAntiguo;
     aCadena1=#0#2;
     |QBF aCadena1;
     |SI aCadena1="";
          |MENAV aMensaje;
     |SINO;
          |PUSHV 0501 2380;
          aEstado="SHOW";

          aEstado="HIDE"; |HAZ PonEstadoNavegador; |HAZ PonBotonesMasDCA;

          |CLS;
          |HAZ RellenaMasDatos;
          |PINPA #0#6;
          |PINDA #0#6;

          |PARA; |SI; |HACIENDO;
               |HAZ PintaRegistroActual;
               |LEETECLA aTecla;
               aTecla = aTecla % 203;
               |SI aTecla = 806;|O aTecla=807; |O aTecla=823;
                    |HAZ QuitaRegistroActual;
                    |SAL;
               |FINSI;
               nTecla = aTecla; nTecla = (nTecla - 824) + 1
               |SI nTecla ] 1; |Y  nTecla [ 6;
                    aEstado="HIDE"; |HAZ PonEstadoNavegador; |HAZ QuitaBotonesMasDCA;
                    |SI nTecla = 1;
                         |SI nClienteActual!1;
                              |HAZ ClientePrimero;
                         |FINSI;
                    |FINSI;
                    |SI nTecla = 2;
                         |SI nClienteActual!1;
                              |HAZ ClienteAnterior;
                         |FINSI;
                    |FINSI;
                    |SI nTecla = 3;
                         |SI nClienteActual!nClientesTotal;
                              |HAZ ClienteSiguiente;
                         |FINSI;
                    |FINSI;
                    |SI nTecla = 4;
                         |SI nClienteActual!nClientesTotal;
                              |HAZ ClienteUltimo;
                         |FINSI;
                    |FINSI;
                    |SI nTecla = 5;
                         |HAZ VerFacturas;
                    |FINSI;
                    |SI nTecla = 6;
                         |PUSHV 0501 2380;
                         |ENTLINEAL #1#103, -2, 4, 17, 1, InfDir, SupDir;
                         |POPV;
                    |FINSI;
                    aEstado="SHOW"; |HAZ PonEstadoNavegador; |HAZ PonBotonesMasDCA;
               |FINSI;
          |SIGUE;

          aEstado="HIDE"; |HAZ PonEstadoNavegador; |HAZ QuitaBotonesMasDCA;

          |POPV;
          |PINDA #0#0; || se debe a que hay que pintar la pantalla #0
                       || por un cambio posible de del cliente

     |FINSI;
|FPRC;

|PROCESO ModificarClienteNuevo;
     |PUSHV 0501 2380;

     aCadena1=#4#0;
     |QBF aCadena1;
     |SI aCadena1="";
          |MENAV aMensaje;
     |SINO;
          |ABRE #3;
          #3#0=#4#0;
          |LEE 101#3=;

          |HAZ PonBotonesCliNuevo;

          |PINPA #0#3;
          |PINDA #0#3;
          |ENDATOS #2#3;

          |SI FSalida=0;
               |GRABA 020#3a;

               aPoblacion=#3#6;
               aProvincia=#3#7;
               |QBF aPoblacion;
               |QBF aProvincia;

               |SI aProvincia="";
                    #4#2=aPoblacion;
               |SINO;
                    #4#2=aPoblacion+ " ("+aProvincia+")";    || poblacion
               |FINSI;
               #4#3=#3#4;  || CP1
          |FINSI;
          |LIBERA #3;
          |CIERRA #3;

          |HAZ QuitaBotonesCliNuevo;

     |FINSI;


     |POPV;
|FPRC;

|PROCESO EliminaCliente;
     aCadena1=#4#0;
     |QBF aCadena1;
     |SI aCadena1="";
          |MENAV aMensaje;
     |SINO;
          |ABRE #3;
          #3#0=#4#0;
          |LEE 000#3=;
          |SI FSalida=0;
               |CONFI "00000Seguro que quieres eliminar este Cliente?";
               |SI FSalida=0;
                    |BORRA 020#3a;
                    |DDEFECTO #4;
                    |PINDA #0#4;

                    aClienteSuper="";
                    |MENAV "0000Cliente Eliminado";
               |FINSI;
          |SINO;
               |MENAV "0000El Cliente no Existe!";
          |FINSI;
          |CIERRA #3;
     |FINSI;
|FPRC;


|PROCESO PulsaOkModCA;
     |PTEC 806;
|FPRC;

|PROCESO PulsaCancelarModCA;
     |PTEC 807;
|FPRC;

|PROCESO PonBotonesModCA;
     |CONTROL_SIMPLE 0, "BOTON,OK", 2128, 2138, PulsaOkModCA;
     nBotonx1 = FSalida;
     |CONTROL_SIMPLE 0, "BOTON,Cancelar", 2143, 2153, PulsaCancelarModCA;
     nBotonx2 = FSalida;
|FPRC;

|PROCESO QuitaBotonesModCA;
     |FIN_CONTROL_WINDOWS nBotonx1;
     |FIN_CONTROL_WINDOWS nBotonx2;
     |PULSATECLA;
|FPRC;

|PROCESO ModificarDatosClienteAntiguo;
     |PUSHV 0501 2380;

     aCadena1=#0#2;
     |QBF aCadena1;
     |SI aCadena1="";
          |MENAV aMensaje;
     |SINO;
          |ABRE #24;
          #24#0=#0#2;
          |LEE 101#24=;
          |SI FSalida=0;
               |ABRE #5;
               |DDEFECTO #5;

               #5#0=#24#0;
               #5#1=#24#1;
               #5#2=#24#5;
               #5#3=#24#178;
               #5#4=#24#179;
               #5#5=#24#6;
               #5#6=#24#7;
               #5#7=#24#17;
               #5#8=#24#32;
               #5#9=#24#33;
               #5#10=#24#31;
               #5#11=#24#30;
               #5#12=#24#18;
               #5#13=#24#36;
               |PINPA #0#5;
               |PINDA #0#5;

               |HAZ PonBotonesModCA;
               |C_M #5#1 , 1 , "N";
               |C_M #5#2 , 1 , "N";
               |C_M #5#3 , 1 , "N";
               |C_M #5#4 , 1 , "N";
               |C_M #5#5 , 1 , "N";
               |C_M #5#6 , 1 , "N";
               |C_M #5#8 , 1 , "N";
               |C_M #5#9 , 1 , "N";
               |C_M #5#10 , 1 , "N";
               |C_M #5#11 , 1 , "N";
               |ENDATOS #1#5;
               |SI FSalida=0;
                    #24#0=#5#0;
                    #24#1=#5#1;
                    #24#5=#5#2;
                    #24#178=#5#3;
                    #24#179=#5#4;
                    #24#6=#5#5;
                    #24#7=#5#6;
                    #24#17=#5#7;
                    #24#32=#5#8;
                    #24#33=#5#9;
                    #24#31=#5#10;
                    #24#30=#5#11;
                    #24#18=#5#12;
                    #24#36=#5#13;
                    |GRABA 020#24a;
               |FINSI;
               |C_M #5#1 , 1 , "S";
               |C_M #5#2 , 1 , "S";
               |C_M #5#3 , 1 , "S";
               |C_M #5#4 , 1 , "S";
               |C_M #5#5 , 1 , "S";
               |C_M #5#6 , 1 , "S";
               |C_M #5#8 , 1 , "S";
               |C_M #5#9 , 1 , "S";
               |C_M #5#10 , 1 , "S";
               |C_M #5#11 , 1 , "S";
               |CIERRA #5;

               |HAZ QuitaBotonesModCA;

               |POPV;

               #0#3=#24#1;    || nombre
               aPoblacion=#24#6;
               aProvincia=#24#7;
               |QBF aPoblacion;
               |QBF aProvincia;

               |SI aProvincia="";
                    #0#4=aPoblacion;
               |SINO;
                    #0#4=aPoblacion+ " ("+aProvincia+")";    || poblacion
               |FINSI;

               #0#5=#24#178;  || CP1
               #0#6=#24#179;  || CP2

               |PINTA #0#2;
               |PINTA #0#3;
               |PINTA #0#4;
               |PINTA #0#5;
               |PINTA #0#6;
          |SINO;
               |MENAV "0000Cliente no Encontrado!";
          |FINSI;

          |LIBERA #24;
          |CIERRA #24;
     |FINSI;
|FPRC;



|PROCESO PBuscaClientesAntiguos;
     aCadena1=#24#1;
     |QBF aCadena1;
     aCadena1=aCadena1 > "";
     aCadena2=#0#0;
     |QBF aCadena2;
     aCadena2=aCadena2 > "";
     aCadena3=aCadena1 - aCadena2;
     |QBF aCadena3;

     aCadena4=#24#6;
     |QBF aCadena4;
     aCadena4=aCadena4 > "";
     aCadena5=#0#1;
     |QBF aCadena5;
     aCadena5=aCadena5 > "";
     aCadena6=aCadena4 - aCadena5;
     |QBF aCadena6;

     |SI aCadena2=""; aCadena3="x"; |FINSI;
     |SI aCadena5=""; aCadena6="x"; |FINSI;

     |SI aCadena1!aCadena3;|Y aCadena4!aCadena6;
          |DDEFECTO #2;

          #2#0=#24#0;    || codigo
          #2#1=#24#1;    || nombre

          aPoblacion=#24#6;
          aProvincia=#24#7;
          |QBF aPoblacion;
          |QBF aProvincia;

          |SI aProvincia="";
               #2#2=aPoblacion;
          |SINO;
               #2#2=aPoblacion+ " ("+aProvincia+")";    || poblacion
          |FINSI;

          #2#3=#24#178;  || CP1
          #2#4=#24#179;  || CP2

          |GRABA 020#2n;
     |FINSI;
|FPRC;

|DEFBUCLE BBuscaClientesAntP;
     #24#6;
     ;
     INICIO;
     FINAL;
     f;
     NULL,PBuscaClientesAntiguos;
|FIN;

|DEFBUCLE BBuscaClientesAntN;
     #24#2;
     ;
     INICIO;
     FINAL;
     f;
     NULL,PBuscaClientesAntiguos;
|FIN;


|PROCESO ConsultaClientesAntiguos;
     aCodigoAux=#0#2;
     aNombreAux=#0#3;
     aPoblacionAux=#0#4;
     nCP1Aux=#0#5;
     nCP2Aux=#0#6;
     aClienteSuperAux=aClienteSuper;


     aCadena2=#0#0;
     |QBF aCadena2;
     aCadena5=#0#1;
     |QBF aCadena5;
     |SI aCadena5 = "";|Y aCadena2 = "";
          nFiltro = 0;
          |ABRE #24;
          |CONSULTA_CLAVE #2#24;
          nSeguir=FSalida;
          |SI nSeguir=0;
               |DDEFECTO #2;

               #2#0=#24#0;    || codigo
               #2#1=#24#1;    || nombre

               aPoblacion=#24#6;
               aProvincia=#24#7;
               |QBF aPoblacion;
               |QBF aProvincia;

               |SI aProvincia="";
                    #2#2=aPoblacion;
               |SINO;
                    #2#2=aPoblacion+ " ("+aProvincia+")";    || poblacion
               |FINSI;

               #2#3=#24#178;  || CP1
               #2#4=#24#179;  || CP2

               nSeguir=0;
          |FINSI;
     |SINO;
          nFiltro = 1;
          |DELINDEX #2;
          |ABRE #2;
          |SI aCadena2 ! "";
               FSalida = aCadena2;
               |BUCLE BBuscaClientesAntN;    || buscamos en todos los clientes
          |SINO;
               FSalida = aCadena5;
               |BUCLE BBuscaClientesAntP;    || buscamos en todos los clientes
          |FINSI;

          |LEE 000#2p;
          |SI FSalida!0;
               |MENAV "0000No Existen Clientes con esas Caracteristicas!";
               nSeguir=-1;
               nFiltro = 0;
          |SINO;
               |CONSULTA_CLAVE #1#2;
               nSeguir=FSalida;
               nFiltro = 1;
          |FINSI;

          |CIERRA #2;
     |FINSI;

     |SI nSeguir=0;
          #0#2=#2#0;
          #0#3=#2#1;
          #0#4=#2#2;
          #0#5=#2#3;
          #0#6=#2#4;

          |PINDA #0#0;

          |CONFI "00000Es Pedido (Si), Es Presupuesto (No)?";
          |SI FSalida=0;
               nEsPedido=1;
          |SINO;
               nEsPedido=nCero;
          |FINSI;

          ||CCC.
          |SI nFiltro = 1;
               |DAREGN #2,nNume1;
               nClienteActual = FSalida+1;
          |SINO;
               |DAREGN #24,nNume1;
               nClienteActual = FSalida+1;
          |FINSI;

          aCadena1=#2#0;
          |QBF aCadena1;
          aCadena2=#2#1;
          |QBF aCadena2;

          aClienteSuper=#2#0;
          aCadena3="00000Desea Mostrar el Catalogo al cliente "+aCadena1+" "+aCadena2+" ?";
          |CONFI aCadena3;
          |SI FSalida=0;
               |SUB_EJECUTA "por00001;porm0001a";
          |FINSI;
     |SINO;
          #0#2=aCodigoAux;
          #0#3=aNombreAux;
          #0#4=aPoblacionAux;
          #0#5=nCP1Aux;
          #0#6=nCP2Aux;
          aClienteSuper=aClienteSuperAux;

          |PINDA #0#0;
     |FINSI;
|FPRC;

|PROCESO PulsaOkCliNuevo;
     |PTEC 815;
|FPRC;

|PROCESO PulsaCancelarCliNuevo;
     |PTEC 807;
|FPRC;

|PROCESO PonBotonesCliNuevo;
     |CONTROL_SIMPLE 0, "BOTON,Ok", 2328, 2338, PulsaOkCliNuevo;       nBotonx1 = FSalida;
     |CONTROL_SIMPLE 0, "BOTON,Cancelar", 2342, 2352, PulsaCancelarCliNuevo; nBotonx2 = FSalida;
|FPRC;

|PROCESO QuitaBotonesCliNuevo;
     |FIN_CONTROL_WINDOWS nBotonx1;
     |FIN_CONTROL_WINDOWS nBotonx2;
     |PULSATECLA;
|FPRC;

|PROCESO NuevoCliente;
     |PUSHV 0501 2380;
     |CLS;

     |ABRE #3;
     nUltimo=0;
     |LEE 000#3u;
     aCadena=#3#0 % 205;
     nUltimo=aCadena+1;

     aCadena=nUltimo;
     |HAZ llena_ceros; || en aCadena esta el resultado
     aCadena="N"+aCadena;

     |HAZ PonBotonesCliNuevo;

     |DDEFECTO #3;
     #3#0=aCadena;
     |PINPA #0#3;
     |PINDA #0#3;
     |ENDATOS #1#3;

     |SI FSalida=0;
          |GRABA 020#3n;
          #4#0=#3#0;    || codigo
          #4#1=#3#2;    || nombre

          aPoblacion=#3#6;
          aProvincia=#3#7;
          |QBF aPoblacion;
          |QBF aProvincia;

          |SI aProvincia="";
               #4#2=aPoblacion;
          |SINO;
               #4#2=aPoblacion+ " ("+aProvincia+")";    || poblacion
          |FINSI;
          #4#3=#3#4;  || CP1
          #4#4=#3#5;  || CP2
          |MENAV "0000Cliente dado de alta";
          aClienteSuper=#3#0;
          |SUB_EJECUTA "porm0009;porm0001n";
     |FINSI;
     |CIERRA #3;

     |HAZ QuitaBotonesCliNuevo;

     |POPV;
     |PINDA #0#4;
|FPRC;


|PROCESO ConsultaClientesNuevos;
     |ABRE #3;
     |CONSULTA_CLAVE #1#3;
     |SI FSalida=0;
          #4#0=#3#0;    || codigo
          #4#1=#3#2;    || nombre

          aPoblacion=#3#6;
          aProvincia=#3#7;
          |QBF aPoblacion;
          |QBF aProvincia;

          |SI aProvincia="";
               #4#2=aPoblacion;
          |SINO;
               #4#2=aPoblacion+ " ("+aProvincia+")";    || poblacion
          |FINSI;
          #4#3=#3#4;  || CP1
          #4#4=#3#5;  || CP2

          |PINDA #0#4;

          |CONFI "00000Es Pedido (Si), Es Presupuesto (No)?";
          |SI FSalida=0;
               nEsPedido=1;
          |SINO;
               nEsPedido=nCero;
          |FINSI;

          aCadena1=#3#0;
          |QBF aCadena1;
          aCadena2=#3#2;
          |QBF aCadena2;

          aClienteSuper=#3#0;
          aCadena3="00000Desea Mostrar el Catalogo al cliente "+aCadena1+" "+aCadena2+" ?";
          |CONFI aCadena3;
          |SI FSalida=0;
               |SUB_EJECUTA "por00001;porm0001n";
          |FINSI;
     |SINO;
          #4#0="";
          #4#1="";
          #4#2="";
          #4#3=0;
          #4#4=0;

          |PINDA #0#4;
          aClienteSuper="";
     |FINSI;
     |CIERRA #3;
|FPRC;

|FICHEROS;
     pfondo01 #0;
|FIN;




|PROCESO PonDatosClienteNuevo;
     aCadena1=aClienteSuper % 101;
     |SI aCadena1!"N";
          |ACABA;
     |FINSI;

     |ABRE #3;
     #3#0=aClienteSuper;
     |LEE 101#3=;
     |SI FSalida=0;
          #4#0=#3#0;    || codigo
          #4#1=#3#2;    || nombre

          aPoblacion=#3#6;
          aProvincia=#3#7;
          |QBF aPoblacion;
          |QBF aProvincia;

          |SI aProvincia="";
               #4#2=aPoblacion;
          |SINO;
               #4#2=aPoblacion+ " ("+aProvincia+")";    || poblacion
          |FINSI;
          #4#3=#3#4;  || CP1
          #4#4=#3#5;  || CP2
     |FINSI;
     |CIERRA #3;
|FPRC;

|PROCESO PonDatosClienteAntiguo;
          #0#2=#24#0;    || codigo
          #0#3=#24#1;    || nombre

          aPoblacion=#24#6;
          aProvincia=#24#7;
          |QBF aPoblacion;
          |QBF aProvincia;

          |SI aProvincia="";
               #0#4=aPoblacion;
          |SINO;
               #0#4=aPoblacion+ " ("+aProvincia+")";    || poblacion
          |FINSI;

          #0#5=#24#178;  || CP1
          #0#6=#24#179;  || CP2

          aClienteSuper=#0#2;
|FPRC;

|PROCESO PresentaClienteAntiguo;
     aCadena1=aClienteSuper % 101;
     |SI aCadena1="N";
          |ACABA;
     |FINSI;

     |ABRE #24;
     #24#0=aClienteSuper;
     |LEE 101#24=;
     |SI FSalida=0;
          #0#2=#24#0;    || codigo
          #0#3=#24#1;    || nombre

          aPoblacion=#24#6;
          aProvincia=#24#7;
          |QBF aPoblacion;
          |QBF aProvincia;

          |SI aProvincia="";
               #0#4=aPoblacion;
          |SINO;
               #0#4=aPoblacion+ " ("+aProvincia+")";    || poblacion
          |FINSI;

          #0#5=#24#178;  || CP1
          #0#6=#24#179;  || CP2
          |PINDA #0#0;
     |FINSI;
     |CIERRA #24;

     ||CCC
     |SI nFiltro = 1;
          |ABRE #2;
          #2#0 = aClienteSuper;
          |LEE 000#2=;
          |SI FSalida = 0;
               |DAREGN #2,nNume1;
               nClienteActual = FSalida+1;
          |FINSI;
          |CIERRA #2;
     |SINO;
          |DAREGN #24,nNume1;
          nClienteActual = FSalida+1;
     |FINSI;
|FPRC;

|PROCESO VerDescuentosClienteA;
     aCadena1=#0#2;
     |QBF aCadena1;
     |SI aCadena1="";
          |MENAV aMensaje;
     |SINO;
          aClienteSuper=#0#2;
          |SUB_EJECUTA "porm0009;porm0001a";
     |FINSI;
|FPRC;

|PROCESO VerDescuentosClienteN;
     aCadena1=#4#1;
     |QBF aCadena1;
     |SI aCadena1="";
          |MENAV aMensaje;
     |SINO;
          aClienteSuper=#4#0;
          |SUB_EJECUTA "porm0009;porm0001n";
     |FINSI;
|FPRC;

|PROCESO PulsaOkBuscarCA;
     |PTEC 806;
|FPRC;

|PROCESO PulsaCancelarBuscarCA;
     |PTEC 807;
|FPRC;

|PROCESO PonBotonesBuscarCA;
     |CONTROL_SIMPLE 0, "BOTON,OK", 0665, 0675, PulsaOkBuscarCA;
     nBotonx1 = FSalida;
     |CONTROL_SIMPLE 0, "BOTON,Cancelar", 0765, 0775, PulsaCancelarBuscarCA;
     nBotonx2 = FSalida;
|FPRC;

|PROCESO QuitaBotonesBuscarCA;
     |FIN_CONTROL_WINDOWS nBotonx1;
     |FIN_CONTROL_WINDOWS nBotonx2;
     |PULSATECLA;
|FPRC;

|PROCESO BuscarClienteAntiguo;
     |HAZ PonBotonesBuscarCA;

     |ENDATOS #1#0;
     |SI FSalida=0;
          |HAZ ConsultaClientesAntiguos;
          |SI nSeguir!0;
               |PINTA #0#0;
               |PINTA #0#1;
          |FINSI;
     |FINSI;

     |HAZ QuitaBotonesBuscarCA;
|FPRC;

|PROCESO PulsaMasDatosCA;
     |PTEC 824;
|FPRC;

|PROCESO PulsaModificarCA;
     |PTEC 825;
|FPRC;

|PROCESO PulsaBuscarCA;
     |PTEC 826;
|FPRC;

|PROCESO PulsaPedidoCA;
     |PTEC 827;
|FPRC;

|PROCESO PulsaDescuentosCA;
     |PTEC 828;
|FPRC;

|PROCESO PulsaVolverCA;
     |PTEC 823;
|FPRC;

|PROCESO PonBotonesCA;
     |CONTROL_SIMPLE 0, "BOTON,F2 Mas Datos", 2407, 2417, PulsaMasDatosCA;
     nBotonx21 = FSalida;
     |CONTROL_SIMPLE 0, "BOTON,F3 Modificar", 2418, 2428, PulsaModificarCA;
     nBotonx22 = FSalida;
     |CONTROL_SIMPLE 0, "BOTON,F4 Buscar", 2429, 2439, PulsaBuscarCA;
     nBotonx23 = FSalida;
     |CONTROL_SIMPLE 0, "BOTON,F5 Pedido", 2440, 2450, PulsaPedidoCA;
     nBotonx24 = FSalida;
     |CONTROL_SIMPLE 0, "BOTON,F6 Descuentos", 2451, 2461, PulsaDescuentosCA;
     nBotonx25 = FSalida;
     |CONTROL_SIMPLE 0, "BOTON,F12 SALIR ", 2462, 2472, PulsaVolverCA;
     nBotonx26 = FSalida;
|FPRC;

|PROCESO QuitaBotonesCA;
     |FIN_CONTROL_WINDOWS nBotonx21;
     |FIN_CONTROL_WINDOWS nBotonx22;
     |FIN_CONTROL_WINDOWS nBotonx23;
     |FIN_CONTROL_WINDOWS nBotonx24;
     |FIN_CONTROL_WINDOWS nBotonx25;
     |FIN_CONTROL_WINDOWS nBotonx26;
     |PULSATECLA;
|FPRC;

|PROCESO EstadoBotonesCA;
     |ESTADO_CONTROL nBotonx21,aEstado;
     |ESTADO_CONTROL nBotonx22,aEstado;
     |ESTADO_CONTROL nBotonx23,aEstado;
     |ESTADO_CONTROL nBotonx24,aEstado;
     |ESTADO_CONTROL nBotonx25,aEstado;
     |ESTADO_CONTROL nBotonx26,aEstado;
|FPRC;

|PROCESO MenuClientesAntiguos;
          |CLS;
          |DDEFECTO #0;
          |PINPA #0#0;
          |PINDA #0#0;

          aCadena=aClienteSuper;
          |QBF aCadena;
          |SI aCadena="";
               |HAZ BuscarClienteAntiguo;
          |SINO;
               |HAZ PresentaClienteAntiguo; || ya que podemos haber seleccionado uno antes
          |FINSI;

          aEstado="SHOW"; |HAZ EstadoBotonesCA; |HAZ PonBotonesCA;
          |PARA; |SI; |HACIENDO;
               |LEETECLA aTecla;
               aTecla = aTecla % 203;
               |SI aTecla = 806;|O aTecla=807; |O aTecla=823; |SAL; |FINSI;
               nTecla = aTecla; nTecla = (nTecla - 824) + 1
               |SI nTecla ] 1; |Y  nTecla [ 6;
                    aEstado="HIDE"; |HAZ EstadoBotonesCA; |HAZ QuitaBotonesCA;
                    |SI nTecla = 1;
                         |HAZ ConsultaDatosClienteAntiguo;
                    |FINSI;
                    |SI nTecla = 2;
                         |HAZ ModificarDatosClienteAntiguo;
                    |FINSI;
                    |SI nTecla = 3;
                         |HAZ BuscarClienteAntiguo;
                    |FINSI;
                    |SI nTecla = 4;
                         |HAZ GenerarPedidoPresupuestoA;
                    |FINSI;
                    |SI nTecla = 5;
                         |HAZ VerDescuentosClienteA;
                    |FINSI;
                    aEstado="SHOW"; |HAZ EstadoBotonesCA; |HAZ PonBotonesCA;
               |FINSI;
          |SIGUE;
          aEstado="HIDE"; |HAZ EstadoBotonesCA; |HAZ QuitaBotonesCA;
|FPRC;

|PROCESO PulsaListadoCN;
     |PTEC 826;
|FPRC;

|PROCESO PulsaNuevoCN;
     |PTEC 825;
|FPRC;

|PROCESO PulsaBorrarCN;
     |PTEC 829;
|FPRC;

|PROCESO PulsaMasDatosCN;
     |PTEC 824;
|FPRC;

|PROCESO PulsaPedidoCN;
     |PTEC 827;
|FPRC;

|PROCESO PulsaDescuentosCN;
     |PTEC 828;
|FPRC;

|PROCESO PulsaVolverCN;
     |PTEC 823;
|FPRC;

|PROCESO PonBotonesCN;
     |CONTROL_SIMPLE 0, "BOTON,F2 Mas Datos", 2403, 2413, PulsaMasDatosCN;   nBotonx21 = FSalida;
     |CONTROL_SIMPLE 0, "BOTON,F3 Nuevo", 2414, 2424, PulsaNuevoCN;      nBotonx22 = FSalida;
     |CONTROL_SIMPLE 0, "BOTON,F4 Buscar", 2425, 2435, PulsaListadoCN;    nBotonx23 = FSalida;
     |CONTROL_SIMPLE 0, "BOTON,F5 Pedido", 2436, 2446, PulsaPedidoCN;     nBotonx24 = FSalida;
     |CONTROL_SIMPLE 0, "BOTON,F6 Descuentos", 2447, 2457, PulsaDescuentosCN; nBotonx25 = FSalida;
     |CONTROL_SIMPLE 0, "BOTON,F7 Borrar", 2458, 2468, PulsaBorrarCN;     nBotonx26 = FSalida;
     |CONTROL_SIMPLE 0, "BOTON,F12 SALIR ", 2469, 2479, PulsaVolverCN;     nBotonx27 = FSalida;
|FPRC;

|PROCESO QuitaBotonesCN;
     |FIN_CONTROL_WINDOWS nBotonx21;
     |FIN_CONTROL_WINDOWS nBotonx22;
     |FIN_CONTROL_WINDOWS nBotonx23;
     |FIN_CONTROL_WINDOWS nBotonx24;
     |FIN_CONTROL_WINDOWS nBotonx25;
     |FIN_CONTROL_WINDOWS nBotonx26;
     |FIN_CONTROL_WINDOWS nBotonx27;
     |PULSATECLA;
|FPRC;

|PROCESO EstadoBotonesCN;
     |ESTADO_CONTROL nBotonx21,aEstado;
     |ESTADO_CONTROL nBotonx22,aEstado;
     |ESTADO_CONTROL nBotonx23,aEstado;
     |ESTADO_CONTROL nBotonx24,aEstado;
     |ESTADO_CONTROL nBotonx25,aEstado;
     |ESTADO_CONTROL nBotonx26,aEstado;
     |ESTADO_CONTROL nBotonx27,aEstado;
|FPRC;

|PROCESO MenuClientesNuevos;
          |CLS;
          |PINPA #0#4;
          |HAZ PonDatosClienteNuevo; || ya que podemos haber seleccionado uno antes
          |PINDA #0#4;

          aEstado="SHOW"; |HAZ EstadoBotonesCN; |HAZ PonBotonesCN;
          |PARA; |SI; |HACIENDO;
               |LEETECLA aTecla;
               aTecla = aTecla % 203;
               |SI aTecla = 806;|O aTecla=807; |O aTecla=823; |SAL; |FINSI;
               nTecla = aTecla; nTecla = (nTecla - 824) + 1
               |SI nTecla ] 1; |Y  nTecla [ 7;
                    aEstado="HIDE"; |HAZ EstadoBotonesCN; |HAZ QuitaBotonesCN;
                    |SI nTecla = 1;
                         |HAZ ModificarClienteNuevo;
                    |FINSI;
                    |SI nTecla = 2;
                         |HAZ NuevoCliente;
                    |FINSI;
                    |SI nTecla = 3;
                         |HAZ ConsultaClientesNuevos;
                    |FINSI;
                    |SI nTecla = 4;
                         |HAZ GenerarPedidoPresupuestoN;
                    |FINSI;
                    |SI nTecla = 5;
                         |HAZ VerDescuentosClienteN;
                    |FINSI;
                    |SI nTecla = 6;
                         |HAZ EliminaCliente;
                    |FINSI;
                    aEstado="SHOW"; |HAZ EstadoBotonesCN; |HAZ PonBotonesCN;
               |FINSI;
          |SIGUE;
          aEstado="HIDE"; |HAZ EstadoBotonesCN; |HAZ QuitaBotonesCN;
|FPRC;

|PROCESO OcultaCamposCliente;
     totalc = #24#"|NC"#;

     |PARA i = 0;|SI i < totalc;|HACIENDO i = i + 1;
          |C_V #24#i#,1,"N";
          |C_M #24#i#,1,"N";
     |SIGUE;

     |C_V #24#0,1,"S";
     |C_V #24#1,1,"S";
     |C_V #24#6,1,"S";
     |C_V #24#2,1,"S";
     |C_V #24#178,1,"S";
     |C_V #24#179,1,"S";
     |C_V #24#7,1,"S";
     |C_V #24#8,1,"S";
     |C_V #24#9,1,"S";
     |C_V #24#181,1,"S";
     |C_V #24#182,1,"S";
     |C_V #24#10,1,"S";
     |C_V #24#15,1,"S";
     |C_V #24#17,1,"S";
     |C_M #24#0,1,"S";
     |C_M #24#1,1,"S";
     |C_M #24#6,1,"S";
     |C_M #24#2,1,"S";
     |C_M #24#178,1,"S";
     |C_M #24#179,1,"S";
     |C_M #24#7,1,"S";
     |C_M #24#8,1,"S";
     |C_M #24#9,1,"S";
     |C_M #24#181,1,"S";
     |C_M #24#182,1,"S";
     |C_M #24#10,1,"S";
     |C_M #24#15,1,"S";
     |C_M #24#17,1,"S";
|FPRC;


|PROCESO TotalClientes;
     nClientesTotal=nCero;
     |SI nFiltro = 1;
          aCadena1="porm0002";
     |SINO;
          aCadena1="agifa024";
     |FINSI;
     |LEEDEDEF aCadena1,"";
     aCadena1=FSalida;
     aCadena1 =aCadena1 % 1108;
     nClientesTotal=aCadena1;
     nClienteActual=1;
|FPRC;


|PROGRAMA;
     |HAZ CreaTempo; aTmp7 = Tempo; |NOME_DAT #7 Tempo; |DELINDEX #7;
     |HAZ CreaTempo; aTmp2 = Tempo; |NOME_DAT #2 Tempo; |DELINDEX #2;


     |HAZ OcultaCamposCliente;

     |HAZ TotalClientes;

     aParametro=PARAMETRO;

     |SI aParametro="DesdeMenuAntiguos";
          |HAZ MenuClientesAntiguos;
     |FINSI;

     |SI aParametro="DesdeMenuNuevos";
          |HAZ MenuClientesNuevos;
     |FINSI;


     Tempo = aTmp2; |HAZ BoraTempo; |DELINDEX #2;
     Tempo = aTmp7; |HAZ BoraTempo; |DELINDEX #7;
|FPRO;

|RUTINA InfDir;
     #103#0 = #porm0006#0;
     #103#1 = 1;
|FRUT;

|RUTINA SupDir;
     #103#0 = #porm0006#0;
     #103#1 = 999;
|FRUT;
