|FICHEROS;
     tlfw0004 #0;
     tlfm0001 #1;
     tlfm0002 #2;
     agifa024 #24;
     agifa025 #25;
     agifa112 #112;
|FIN;

|VARIABLES;
     nSw = 0;
     &enForma = 0;
     &eaArtiAntes = "";
     &eaArticulo = "";
     &enModo = 0;
     &eaHayIMEI = "";
     &eaSerie = "";
     &enCodigo = 0;
     &enLinea = 0;
     &efFecha = @;
     &eaCliente = "";
     &eaNomCli = "";
     &eaRepre = "";
     &eaAbono = "";
     &enAlmacen = 0;
     aAlfa = "";
     aAlf1 = "";
     aAlf2 = "";
     aAlf3 = "";
     aArti = "";
     i = 0;
     nGuarda = 0;
     aImeiAnt = "";
     aAbonoAnt = "";
|FIN;

|PROCESO IMEI7; |TIPO 7;
     aImeiAnt = #0#0;
|FPRC;

|PROCESO Abono7; |TIPO 7;
     aAbonoAnt = #0#1;
|FPRC;

|PROCESO CheqIMEI0; |TIPO 0;
     |SI FSalida = 1;           ||Escape
          |SI enModo = 2;
               |ERROR; |PTEC 802; |PTEC 802; |ACABA;
          |SINO;
               |ERROR; |PTEC 807; |ACABA;
          |FINSI;
     |FINSI;

     |SI FSalida = 2; |ACABA; |FINSI;

     aAlfa = #0#0; |QBF aAlfa;
     |SI aAlfa ! ""; |Y Campo = 0;
          |ABRE #1;
          #1#0 = #0#0;
          |LEE 000#1=;
          |SI FSalida ! 0;
               |MENAV "0000IMEI Inexistente...";
               |ERROR; |ACABA;
          |SINO;
               |SI #1#1 ! #0#3;
                    aAlfa = "0000El IMEI corresponde al articulo:" + #1#1;
                    |MENAV aAlfa;
                    |ERROR; |ACABA;
               |FINSI;
               |SI #1#11 ! enAlmacen;
                    aAlfa = "0000IMEI existente en Almacen: " + #1#11;
                    |MENAV aAlfa;
                    |ERROR; |ACABA;
               |FINSI;
               |SI #1#58 ! eaSerie; |O #1#59 ! enCodigo; |O #1#60 ! enLinea;
                    |SI #1#27 ! 0; |O #1#31 ! 0; |O #1#35 ! 0; |O #1#60 ! 0;
                         |SI #1#60 ! 0;
                              aAlfa = "0000IMEI reservado al cliente:" + #1#12;
                              |MENAV aAlfa;
                              |ERROR; |ACABA;
                         |SINO;
                              |SI #1#57 = "N";
                                   |MENAV "0000IMEI Vendido...";
                                   |ERROR; |ACABA;
                              |FINSI;
                         |FINSI;
                    |FINSI;
               |FINSI;
          |FINSI;
          |CIERRA #1;
          |SI enModo = 1; |O #0#0 ! aImeiAnt;
               #0#1 = #1#3; |PINTA #0#1;
               #0#2 = #1#4; |PINTA #0#2;
          |FINSI;
     |FINSI;
     aAlfa = #0#1; |QBF aAlfa;
     |SI aAlfa ! ""; |Y Campo = 1;
          |ABRE #2;
          #2#3 = #0#1;
          |LEE 000#2=;
          |SI FSalida ! 0;
               |MENAV "0000Abono Inexistente...";
               |ERROR; |ACABA;
          |SINO;
               |SI #2#1 ! #0#3;
                    aAlfa = "0000El Abono corresponde al articulo:" + #2#1;
                    |MENAV aAlfa;
                    |ERROR; |ACABA;
               |FINSI;
               |SI #2#11 ! enAlmacen;
                    aAlfa = "0000IMEI existente en Almacen: " + #2#11;
                    |MENAV aAlfa;
                    |ERROR; |ACABA;
               |FINSI;
               |SI #2#51 ! eaSerie; |O #2#52 ! enCodigo; |O #2#53 ! enLinea;
                   |SI #2#27 ! 0; |O #2#31 ! 0; |O #2#35 ! 0; |O #2#53 ! 0;
                        |SI #2#50 = "N";
                             |MENAV "0000Abono Vendido...";
                             |ERROR; |ACABA;
                        |FINSI;
                   |FINSI;
               |FINSI;
          |FINSI;
          |CIERRA #2;
          |SI enModo = 1; |O #0#1 ! aAbonoAnt;
               #0#0 = #2#0; |PINTA #0#0;
               #0#2 = #2#4; |PINTA #0#2;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO CargaIMEIPed;
     |DDEFECTO #0;

     |ABRE #1;
     |SELECT #10#1;
     #1#58 = eaSerie;
     #1#59 = enCodigo;
     #1#60 = enLinea;
     |LEE 000#1=;
     |SI FSalida = 0;
          #0#0 = #1#0;
     |FINSI;
     |CIERRA #1;

     |ABRE #2;
     |SELECT #8#2;
     #2#51 = eaSerie;
     #2#52 = enCodigo;
     #2#53 = enLinea;
     |LEE 000#2=;
     |SI FSalida = 0;
          #0#1 = #2#3;
          #0#2 = #2#4;
     |FINSI;
     |CIERRA #2;
     #0#3 = aArti;
|FPRC;

|PROCESO Limpia;
     |PARA i = 12; |SI i [ 22; |HACIENDO i = i + 1;
          #1i = "";
          #2i = "";
     |SIGUE;
     |PARA i = 25; |SI i [ 36; |HACIENDO i = i + 1;
          aAlf1 = "|C"+ (("000"+i)%-103) + "TIPO";
          aAlf2 = #1#aAlf1#;
          |SI aAlf2 = "F"; #1i = "01.01.0000"; |SINO; #1i = ""; |FINSI;
          aAlf2 = #2#aAlf1#;
          |SI aAlf2 = "F"; #2i = "01.01.0000"; |SINO; #2i = ""; |FINSI;
     |SIGUE;
     #1#39 = "01.01.0000";

     #1#58 = ""; #1#59 = 0; #1#60 = 0; #1#61 = "01.01.0000";
     #2#51 = ""; #2#52 = 0; #2#53 = 0; #2#54 = "01.01.0000";
|FPRC;

|PROCESO BorraPed;
     |ABRE #1;
     |SELECT #10#1;
     #1#58 = eaSerie;
     #1#59 = enCodigo;
     #1#60 = enLinea;
     |LEE 000#1=;
     |SI FSalida = 0;
          |SELECT #1#1;
          |LEE 121#1=;
          |SI FSalida = 0;
               |HAZ Limpia;
               |GRABA 020#1a;
          |FINSI;
     |FINSI;
     |CIERRA #1;

     |ABRE #2;
     |SELECT #8#2;
     #2#51 = eaSerie;
     #2#52 = enCodigo;
     #2#53 = enLinea;
     |LEE 000#2=;
     |SI FSalida = 0;
          |SELECT #1#2;
          |LEE 121#2=;
          |SI FSalida = 0;
               |HAZ Limpia;
               |GRABA 020#2a;
          |FINSI;
     |FINSI;
     |CIERRA #2;
|FPRC;

|PROCESO EntIMEI12; |TIPO 12;
     |SI FSalida ! 0;
          eaHayIMEI = "N";
          |ACABA;
     |FINSI;
     eaHayIMEI = "S";

     aAlfa = #0#0; |QBF aAlfa;
     |SI aAlfa ! "";
          |ABRE #1;
          #1#0 = #0#0;
          |LEE 121#1=;
          |SI FSalida = 0;
               #1#58 = eaSerie;
               #1#59 = enCodigo;
               #1#60 = enLinea;
               #1#61 = efFecha;
               #1#12 = eaCliente;
               #1#21 = eaRepre;
               #1#13 = eaNomCli;

               |DDEFECTO #24;
               |ABRE #24;
               #24#0 = #1#12;
               |LEE 000#24=;
               |CIERRA #24;

               |DDEFECTO #112;
               |ABRE #112;
               #112#0 = #1#5;
               |LEE 000#112=;
               |CIERRA #112;

               |DDEFECTO #25;
               |ABRE #25;
               #25#0 = #1#21;
               |LEE 000#25=;
               |CIERRA #25;

               #1#6 = #112#1;
               ||#1#13 = #24#2;
               #1#14 = #24#32;
               #1#15 = #24#33;
               #1#16 = #24#31;
               #1#17 = #24#29;
               #1#18 = #24#162;
               #1#19 = #24#34;
               #1#20 = #24#30;
               #1#22 = #25#1;
               |GRABA 020#1a;
          |FINSI;
          |CIERRA #1;
     |FINSI;
     aAlfa = #0#1; |QBF aAlfa;
     |SI aAlfa ! "";
          |ABRE #2;
          #2#3 = #0#1;
          |LEE 121#2=;
          |SI FSalida = 0;
               #2#51 = eaSerie;
               #2#52 = enCodigo;
               #2#53 = enLinea;
               #2#54 = efFecha;
               #2#12 = eaCliente;
               #2#13 = eaNomCli;
               |GRABA 020#2a;
          |FINSI;
          |CIERRA #2;
     |FINSI;
|FPRC;

|PROCESO MiraArtIMEI;
     |ABRE #1;
     |ABRE #2;
     |SELECT #6#1;
     |SELECT #6#2;
     #1#1 = aArti;
     |LEE 000#1=;
     |SI FSalida ! 0;
          #2#1 = aArti;
          |LEE 000#2=;
          |SI FSalida ! 0;
               eaHayIMEI = "N"; || NO HAY...
          |FINSI;
     |FINSI;
     |CIERRA #1;
     |CIERRA #2;
|FPRC;
----------------------------------------------------------------
|PROGRAMA;
     eaHayIMEI = "S";
     aArti = eaArticulo;
     |HAZ MiraArtIMEI;

     |SI enModo = 2;
          |SI eaArtiAntes ! eaArticulo;
               |HAZ BorraPed;
               enModo = 1;
          |FINSI;
     |FINSI;
     |SI eaHayIMEI = "N"; |ACABA; |FINSI;

     |SI enModo = 3;
          |HAZ BorraPed;
          |ACABA;
     |FINSI;

     nSw = 0;
     |SI enModo = 2;
          |SI eaArtiAntes = eaArticulo;
               nSw = 1;
               |HAZ CargaIMEIPed;
               |HAZ BorraPed;
          |FINSI;
     |FINSI;

     |SI nSw = 0;
          |HAZ CargaIMEIPed;
     |FINSI;
     aAlfa = #0#0 + #0#1; |QBF aAlfa;
     |SI enModo = 2; |Y aAlfa = "";
          eaHayIMEI = "N";                 || NO HAY....
     |SINO;
          |PUSHV 0501 2380;
          |BLANCO 1423 2055;
          |PINPA #0#0;
          |PINDA #0#0;
          nGuarda = FEntrada;

          |SI enModo = 4;
               |PAUSA;
          |SINO;
               |ENDATOS #1#0;
          |FINSI;

          FEntrada = nGuarda;
          |POPV;
     |FINSI;
|FPRO;
