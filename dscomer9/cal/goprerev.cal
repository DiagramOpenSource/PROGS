|FICHEROS;
     goprerev #0;
     gopre001 #1;
     gopre002 #2;
     gopre003 #3;
     gopre004 #4;
     agifa027 #8;
     agifa324 #9;
     gomedici #10;
     gopredu1 #11;
     gopredu2 #12;
     gopredu3 #13;
     gopredu4 #14;
     godupmed #15;
     goduprec #16;
     goduprel #17;
     godeprec #18;
     godeprel #19;
     goparame #100;
|FIN;
-----------------------------------------------------------------------------
|VARIABLES;
     &Tempo = "";
     aTmp11 = "";
     aTmp12 = "";
     aTmp13 = "";
     aTmp14 = "";
     aTmp16 = "";
     aTmp17 = "";
     &cCodMonBase   = "";
     &cCodMonTrab   = "";
     &cCodMonDe     = "";
     &cCodMonA      = "";
     &cCodMonConv   = "";
     &cCodMonActu   = "";
     &cDesMonBase   = "";
     &cDesMonTrab   = "";

     &nDeci_PBase   = 0;
     &nDeci_TBase   = 0;
     &nDeci_PTrab   = 0;
     &nDeci_TTrab   = 0;
     &nDeci_TotVis  = 0;
     &nEurComTrab   = 0;
     &nEurComBase   = 0;
     &nEurVenTrab   = 0;
     &nEurVenBase   = 0;
     &nPreComTrab   = 0;
     &nPreComBase   = 0;
     &nPreVenTrab   = 0;
     &nPreVenBase   = 0;
     &nPrecioComp   = 0;
     &nPrecioVent   = 0;
     &nDeci_Medi    = 0;
     &nDeci_Meda    = 0;
     &nDeci_Incr    = 0;


     nEuroExis      = 0;
     nEuroNuev      = 0;
     nPrecio        = 0;
     nImporte       = 0;
     nConta         = 0;

     cConta         = "";
|FIN;
-----------------------------------------------------------------------------
|PROCESO ConDup; |TIPO 7;
     |ABRE #8;
     #8#0 = "vfacprof";
     #8#2 = #0#3;
     |LEE 101#8=;

     |SI FSalida = 0;
          #8#1 = #8#1 + 1;
          nConta = #8#1;
          |GRABA 021#8a;
     |SINO;
          nConta = 1;
          #8#1 = 1;
          |GRABA 021#8n;
     |FINSI;

     cConta = nConta;
     cConta = ("000000" + cConta) % -106;
     #0#4 = cConta;
     |PINTA #0#4;
     |LIBERA #8;
     |CIERRA #8;

     cConta = #0#2;
     |QBF cConta;
     cConta = "000000" + cConta;
     cConta = cConta % -106;
     cConta = "REVISION PRESUPUESTO " + #0#1 + "\" + cConta + " Rev. No.: "
     #0#14 = cConta;
     |PINTA #0#14;
|FPRC;
------------------------------------------------------------------------------
|PROCESO GrabDesc;
     #14#0 = #0#3;
     #14#1 = #0#4;

     |PARA nConta = 2; |SI nConta [ 15; |HACIENDO nConta= nConta+1;
          #14nConta = #4nConta;
     |SIGUE;

     |GRABA 020#14c;
     #0#9 = "COPIANDO DESCOMPOSICION";
     |PINTA #0#9;
|FPRC;
------------------------------------------------------------------------------
|PROCESO GrabDupDesc;
     nPrecio = #14#9 / nEuroExis;
     nPrecio = nPrecio * nEuroNuev;
     #14#9 = nPrecio;                   ||PRECIO VENTA.
     nPrecio = #14#10 / nEuroExis;
     nPrecio = nPrecio * nEuroNuev;
     #14#10 = nPrecio;                  ||PRECIO COSTE.
     nPrecio = #14#11 / nEuroExis;
     nPrecio = nPrecio * nEuroNuev;
     #14#11 = nPrecio;                  ||IMPORTE VENTA.
     nPrecio = #14#12 / nEuroExis;
     nPrecio = nPrecio * nEuroNuev;
     #14#12 = nPrecio;                  ||IMPORTE COSTE.

     |PARA nConta = 0; |SI nConta [ 15; |HACIENDO nConta= nConta+1;
          #4nConta = #14nConta;
     |SIGUE;
     |GRABA 020#4c;
     #0#9 = "GRABANDO DESCOMPOSICION";
     |PINTA #0#9;
|FPRC;
------------------------------------------------------------------------------
|DEFBUCLE EjecDesc;
     #4#1;
     ;
     #3#0, #3#1, #3#2, #3#4,   1;
     #3#0, #3#1, #3#2, #3#4, 999;
     ;
     NULL, GrabDesc;
|FIN;
------------------------------------------------------------------------------
|DEFBUCLE EjecDupDesc;
     #14#1;
     ;
     #13#0, #13#1, #13#2, #13#4,   1;
     #13#0, #13#1, #13#2, #13#4, 999;
     ;
     NULL, GrabDupDesc;
|FIN;
------------------------------------------------------------------------------
|PROCESO GrabPart;
     |BUCLE EjecDesc;
     #13#0 = #0#3;
     #13#1 = #0#4;
     |PARA nConta = 2; |SI nConta [ 16; |HACIENDO nConta= nConta+1;
          #13nConta = #3nConta;
     |SIGUE;
     |GRABA 020#13c;
     #0#9 = "COPIANDO PARTIDA";
     |PINTA #0#9;
|FPRC;
------------------------------------------------------------------------------
|PROCESO DupliMedi;
     |DDEFECTO #15;
     #15#0 = #0#3;            || Serie presupuesto nuevo
     #15#1 = #0#4;            || Numero presupuesto nuevo
     #15#2 = #10#2;           || Capitulo
     #15#3 = #10#3;           || Partida
     #15#4 = #10#4;           || Linea
     |LEE 101#15=;

     |SI FSalida ! 0;
          #15#5 = #10#5;      || Unidades
          #15#6 = #10#6;      || Largo
          #15#7 = #10#7;      || Ancho
          #15#8 = #10#8;      || Alto
          #15#9 = #10#9;      || Parcial
          |GRABA 020#15n;
          |PARA nConta = 0; |SI nConta [ 9; |HACIENDO nConta = nConta + 1;
               #10nConta = #15nConta;
          |SIGUE;

          |GRABA 020#10n;
          |LIBERA #10;
     |FINSI;
     |LIBERA #15;
|FPRC;
------------------------------------------------------------------------------
|DEFBUCLE Mediciones;
     #10#1;
     ;
     #0#1,#0#2,#3#2,#3#4,1;
     #0#1,#0#2,#3#2,#3#4,999;
     be;
     NULL,DupliMedi;
|FIN;
------------------------------------------------------------------------------
|PROCESO GrabDupPart;
     |BUCLE EjecDupDesc;
     nPrecio = #13#8 / nEuroExis;
     nPrecio = nPrecio * nEuroNuev;
     #13#8 = nPrecio;                   ||PRECIO VENTA.
     nPrecio = #13#9  / nEuroExis;
     nPrecio = nPrecio * nEuroNuev;
     #13#9  = nPrecio;                  ||PRECIO COSTE.
     nPrecio = #13#10 / nEuroExis;
     nPrecio = nPrecio * nEuroNuev;
     #13#10 = nPrecio;                  ||IMPORTE VENTA.
     nPrecio = #13#11 / nEuroExis;
     nPrecio = nPrecio * nEuroNuev;
     #13#11 = nPrecio;                  ||IMPORTE COSTE.
     |PARA nConta = 0; |SI nConta [ 16; |HACIENDO nConta= nConta+1;
          #3nConta = #13nConta;
     |SIGUE;
     |GRABA 020#3c;
     #0#9 = "GRABANDO PARTIDA";
     |PINTA #0#9;
     |BUCLE Mediciones;
|FPRC;
------------------------------------------------------------------------------
|DEFBUCLE EjecPart;
     #3#1;
     ;
     #2#0, #2#1, #2#12,   1;
     #2#0, #2#1, #2#12, 999;
     ;
     NULL, GrabPart;
|FIN;
------------------------------------------------------------------------------
|DEFBUCLE EjecDupPart;
     #13#1;
     ;
     #12#0, #12#1, #12#12,   1;
     #12#0, #12#1, #12#12, 999;
     ;
     NULL, GrabDupPart;
|FIN;
------------------------------------------------------------------------------
|PROCESO GrabCapi;
     |BUCLE EjecPart;
     #12#0 = #0#3;
     #12#1 = #0#4;
     |PARA nConta = 2; |SI nConta [ 13; |HACIENDO nConta= nConta+1;
          #12nConta = #2nConta;
     |SIGUE;
     |GRABA 020#12c;
     #0#9 = "COPIANDO CAPITULO";
     |PINTA #0#9;
|FPRC;
------------------------------------------------------------------------------
|PROCESO GrabDupCapi;
     |BUCLE EjecDupPart;
     nPrecio = #12#7 / nEuroExis;
     nPrecio = nPrecio * nEuroNuev;
     #12#7 = nPrecio;                   ||PRECIO VENTA.
     nPrecio = #12#8  / nEuroExis;
     nPrecio = nPrecio * nEuroNuev;
     #12#8  = nPrecio;                  ||PRECIO COSTE.
     nPrecio = #12#9  / nEuroExis;
     nPrecio = nPrecio * nEuroNuev;
     #12#9  = nPrecio;                  ||IMPORTE VENTA.
     nPrecio = #12#10 / nEuroExis;
     nPrecio = nPrecio * nEuroNuev;
     #12#10 = nPrecio;                  ||IMPORTE COSTE.
     |PARA nConta = 0; |SI nConta [ 13; |HACIENDO nConta= nConta+1;
          #2nConta = #12nConta;
     |SIGUE;
     |GRABA 020#2c;
     #0#9 = "GRABANDO CAPITULO";
     |PINTA #0#9;
|FPRC;
------------------------------------------------------------------------------
|DEFBUCLE EjecDupCapi;
     #12#1;
     ;
     #11#0, #11#1,   1;
     #11#0, #11#1, 999;
     ;
     NULL, GrabDupCapi;
|FIN;
------------------------------------------------------------------------------
|PROCESO GrabDupPres;
     |BUCLE EjecDupCapi;
     |DDEFECTO #1;
     nPrecio = #11#5 / nEuroExis;
     nPrecio = nPrecio * nEuroNuev;
     #11#5 = nPrecio;                   ||PRECIO VENTA.
     nPrecio = #11#6  / nEuroExis;
     nPrecio = nPrecio * nEuroNuev;
     #11#6  = nPrecio;                  ||PRECIO COSTE.

     |PARA nConta = 0; |SI nConta [ 39; |HACIENDO nConta= nConta+1;
          #1nConta = #11nConta;
          |SI nConta = 4;
               #1nConta = #0#13;
          |FINSI;
          |SI nConta = 7;
               #1nConta = #0#11;
          |FINSI;
          |SI nConta = 8;
               #1nConta = #0#14;
          |FINSI;
          |SI nConta = 15; |O nConta = 17;
               #1nConta = "       "
          |FINSI;
          |SI nConta = 16;
               #1nConta = 0
          |FINSI;
     |SIGUE;
     |GRABA 020#1c;
     #0#9 = "GRABANDO PRESUPUESTO";
     |PINTA #0#9;
|FPRC;
------------------------------------------------------------------------------
|DEFBUCLE EjecDupPres;
     #11#1;
     ;
     #0#3, #0#4;
     #0#3, #0#4;
     ;
     NULL, GrabDupPres;
|FIN;
------------------------------------------------------------------------------
|PROCESO DesAmpLin;
     #19#1 = #0#3;
     #19#2 = #0#4;
     |GRABA 020#19n;
|FPRC;
-------------------------------------------------------------------------------
|DEFBUCLE DesAmpLin;
     #19#1;
     ;                                          ||Campos Extras.
     #18#0, #18#1, #18#2, #18#4, 1;
     #18#0, #18#1, #18#2, #18#4, 999;
     ;                                          ||b/e Bolqueo-Ordenacion.
     NULL, DesAmpLin;
|FIN;
-------------------------------------------------------------------------------
|PROCESO DescAmpCab;
     |BUCLE DesAmpLin;
     #18#1 = #0#3;
     #18#2 = #0#4;
     |GRABA 020#18n;
|FPRC;
-------------------------------------------------------------------------------
|DEFBUCLE DescAmpCab;
     #18#1;
     ;                                          ||Campos Extras.
     "                    ", #0#1, #0#2, "                    ";
     "zzzzzzzzzzzzzzzzzzzz", #0#1, #0#2, "zzzzzzzzzzzzzzzzzzzz";
     ;                                          ||b/e Bolqueo-Ordenacion.
     NULL, DescAmpCab;
|FIN;
-------------------------------------------------------------------------------
|PROCESO Duplica; |TIPO 3;
     |ABRE #100; |LEE 000#100p; |CIERRA #100;
     nDeci_Meda = #100#115;

     |HAZ CreaTempo; aTmp11 = Tempo;
     |HAZ CreaTempo; aTmp12 = Tempo;
     |HAZ CreaTempo; aTmp13 = Tempo;
     |HAZ CreaTempo; aTmp14 = Tempo;
     |HAZ CreaTempo; aTmp16 = Tempo;
     |HAZ CreaTempo; aTmp17 = Tempo;

     |NOME_DAT #11 aTmp11; |DELINDEX #11;
     |NOME_DAT #12 aTmp12; |DELINDEX #12;
     |NOME_DAT #13 aTmp13; |DELINDEX #13;
     |NOME_DAT #14 aTmp14; |DELINDEX #14;
     |NOME_DAT #16 aTmp16; |DELINDEX #16;
     |NOME_DAT #17 aTmp17; |DELINDEX #17;

     |ABRE #1;
     |ABRE #2;
     |ABRE #3;
     |ABRE #4;
     |ABRE #10;
     |ABRE #11;
     |ABRE #12;
     |ABRE #13;
     |ABRE #14;
     |ABRE #15;
     |ABRE #16;
     |ABRE #17;
     #1#0 = #0#1;        ||Serie.
     #1#1 = #0#2;        ||Numero.
     |LEE 000#1=;

     |SI FSalida = 0;
          nDeci_Medi = #1#24;
          nDeci_Incr = #1#25;
          |BUCLELIN 0 GrabCapi #1;
          #11#0 = #0#3;
          #11#1 = #0#4;
          #11#2 = #0#5;
          #11#3 = #0#6;

          |PARA nConta = 4; |SI nConta [ 39; |HACIENDO nConta= nConta+1;
               #11nConta = #1nConta;
          |SIGUE;

          |GRABA 020#11c;
     |FINSI;

     |BUCLE EjecDupPres;
     |BUCLE DescAmpCab;
     |CIERRA #1;
     |CIERRA #2;
     |CIERRA #3;
     |CIERRA #4;
     |CIERRA #11;
     |CIERRA #12;
     |CIERRA #13;
     |CIERRA #14;
     |CIERRA #15;
     |CIERRA #16;
     |CIERRA #17;
     |DELINDEX #11;
     |DELINDEX #12;
     |DELINDEX #13;
     |DELINDEX #14;
     |DELINDEX #16;
     |DELINDEX #17;
|FPRC;
------------------------------------------------------------------------------
|PROCESO MonedaS; |TIPO 0;
     |ABRE #9;
     #9#0 = #0#10;
     |LEE 000#9=;

     |SI FSalida = 0;
          nEuroExis = #9#2;        ||Valor Euro Existente.
     |FINSI;

     #9#0 = #0#11;
     |LEE 000#9=;

     |SI FSalida = 0;
          nEuroNuev = #9#2;        ||Valor Euro Nuevo.
          cCodMonTrab = #0#11;
          |HAZ MonBas;
     |FINSI;

     |SI #0#10 ! #0#11;
          |CONFI "0000SLA MONEDA DE TRABAJO ES DISTINTA.¨REALIZAR CONVERSION?";

          |SI FSalida = -1;
               |PTEC 807;
          |FINSI;

     |FINSI;

|FPRC;
------------------------------------------------------------------------------
|PROCESO Tipo13; |TIPO 13;
     |ABRE #100; |LEE 000#100p; |CIERRA #100;
     |SI #100#141 = "O";
          |ATRI R; |PINTA 518, "ORDEN ENTRADA "; |ATRI 0;
          |PINTA 709, "O.E............:";
     |FINSI;
|FPRC;
