|FICHEROS;
     dsm00120 #0;
     dsm00121 #1,MANTE;
     agifa019 #19;
     agifa065 #65;
     dsw00088 #88; || Tmp impresion
     agifa142 #142;
     dsm00144 #144; || Log
     dsm00238 #238;
     agifa501 #501;
     agifa502 #502;

     dsm00154 #154;
     dsm00010 #100;
     dsz00137 #137; || Tmp Impresion tallas
     dsm00015 #15;
     dsm00016 #16;
|FIN;

|VARIABLES;
     nMax = 16;
     i = 0;
     aTabla = "";
     nCamU = 0;
     nCamE = 0;
     nCamR = 0;
     nError = 0;
     aEsMoviInv = "";
     fFecActual = @;
     aFormato = "";
     nAlma = 0;
     &Tempo = "";
     aEncontrado = "";
     fFechaIni = @;
     nConta = 0;
     aTipo = "";
     &eaArticulo = "";
     nModo     = 0;
     nForma    = 0;
     aInforme1 = "invencom";
     aInforme2 = "invenrec";
     aInforme  = "";
     aAlfa     = "";

 {-1}aMenu = "";
     aMenu1 = "2400";
     aMenu2 = "1";
     aMenu3 = "Opciones:  ";
     aMenu4 = "IVE";
     aMenu5 = " Imprimir ";
     aMenu6 = " Imp. Valorado ";
     aMenu7 = " Emisiones Valoradas";

 {-1}aMenuV = "";
     aMenuV1 = "2400";
     aMenuV2 = "1";
     aMenuV3 = "Tipo informe:  ";
     aMenuV4 = "UPD";
     aMenuV5 = " Por Ultimo PC ";
     aMenuV6 = " Por PCM ";
     aMenuV7 = " Por PDTO ";

 {-1}aMenuI = "";
     aMenuI1 = "2400";
     aMenuI2 = "1";
     aMenuI3 = "Tipo informe:  ";
     aMenuI4 = "CS";
     aMenuI5 = " Completa ";
     aMenuI6 = " Solo Recuento ";
|FIN;

|PROCESO Tipo0; |TIPO 0;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 1;
          |MENAV "0000Debe de realizar una carga de stock...";
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO Tipo12; |TIPO 12;
|FPRC;

|PROCESO Imprime;
     |PRINT;
|FPRC;

|DEFBUCLE Imprime;
     #1#1;
     ;
     #0#0, "                    ", 00001, "               ";
     #0#0, "zzzzzzzzzzzzzzzzzzzz", 99999, "zzzzzzzzzzzzzzz";
     ;
     NULL, Imprime;
|FIN;


|| La impresion vertical agrupa por "Tabla" del articulo e imprime todos lo
|| colores de esta en la cabecera, con un maximo de 16 elementos (nMax)
|| el 17 se usa para los restos y el 18 para el total
|PROCESO Elementos;
     |SI #154#5 ! 0;
          |PARA i = 0; |SI i < nMax; |HACIENDO i = i + 1;
               |SI #137i = #154#3;
                    nCamU = i + 20;
                    #137nCamU = #154#5;
                    |SAL;
               |FINSI;
          |SIGUE;
          |SI i ] nMax;  || no existe, lo metemos en la 17
               #137#16 = "  ??? ";
               #137#36 = #137#36 + #154#5;
               #137#56 = "ÄÄÄÄÄÄ";
          |FINSI;
     |FINSI;
     #137#37 = #137#37 + #154#5;  || la 18 el total
|FPRC;

|DEFBUCLE Elementos;
     #154#1;
     ;
     #1#0, #1#1, #1#3, "      ";
     #1#0, #1#1, #1#3, "zzzzzz";
     ;
     NULL, Elementos;
|FIN;

|PROCESO ImprimeV;
     |SI aTabla ! #19#177;
          |SI aTabla ! ""; |PIEINF; |FINSI;
          aTabla = #19#177;

          |ABRE #15;
          #15#0 = #19#177;
          #15#1 = "";
          #15#2 = "";
          #15#5 = "";
          |LEE 000#15=;
          |SI FSalida ! 0; |DDEFECTO #15; |FINSI;
          |CIERRA #15;

          nCamE = 0;
          nCamR = 40;
          |ABRE #16;
          #16#0 = #15#0;
          #16#1 = #15#1;
          #16#2 = #15#2;
          #16#15 = #15#5;
          #16#3 = "";
          |LEE 000#16];
          |PARA i = 1; |SI i [ nMax; |HACIENDO i = i + 1;
               |SI FSalida = 0;
                         |Y #16#0 = #15#0;
                         |Y #16#1 = #15#1;
                         |Y #16#2 = #15#2;
                         |Y #16#15 = #15#5;
                    #137nCamE = #16#3;
                    #137nCamR = "ÄÄÄÄÄÄ";
                    |LEE 000#16s;
               |SINO;
                    #137nCamE = "";
                    #137nCamR = "";
               |FINSI;
               nCamE = nCamE + 1;
               nCamR = nCamR + 1;
          |SIGUE;

          #137#16 = "";  || la 17
          #137#36 = 0;
          #137#56 = "";

          #137#17 = " TOTAL";  || la 10
          #137#37 = 0;
          #137#57 = "ÄÄÄÄÄÄ";

          |CIERRA #15;
     |FINSI;

     |PARA i = 20; |SI i [ 39; |HACIENDO i = i + 1;
          #137i = 0;
     |SIGUE;
     |BUCLE Elementos;
     |PRINT;
|FPRC;

|PROCESO Ordena;
     #19#0 = #1#1;
     |LEE 000#19=;
     |SI FSalida ! 0; |DDEFECTO #19; |FINSI;
|FPRC;

|DEFBUCLE ImprimeV;
     #1#1;
     ;
     #0#0, "                    ", 00001, "               ";
     #0#0, "zzzzzzzzzzzzzzzzzzzz", 99999, "zzzzzzzzzzzzzzz";
     ;
     NULL, Ordena, NULL, NULL, NULL, ImprimeV;
     #19#177;
|FIN;

|PROCESO TipoImp;
     |HAZ MiraLogos; || Copia Logos para el Crystal

     |MENU aMenuI;
     |SI FSalida [ 0; |ACABA; |FINSI;

     ||aAlfa = "0000NImpresion Completa (S)," + &13 + "Solo Recuento (N)";
     ||CONFI aAlfa;

     |SI FSalida = 1;
          aInforme = aInforme1;
     |SINO;
          aInforme = aInforme2;
     |FINSI;
     |IMPRE 0;
     |SI FSalida = 0;
          |INFOR aInforme;
          |SI FSalida = 0;
               |SI #100#19 = "S";
                    aTabla = "";
                    |ABRE #19;
                    |BUCLE ImprimeV;
                    |CIERRA #19;
               |SINO;
                    |BUCLE Imprime;
               |FINSI;
               |PIEINF;
               |FININF;
          |SINO;
               aInforme = "0000No existe informe " + aInforme;
               |MENAV aInforme;
          |FINSI;
          |FINIMP;
     |FINSI;

|FPRC;

|PROCESO Min;
     #1#0 = #0#0;
     #1#1 = "                    ";
     #1#3 = 00001;
     #1#11 = "               ";
|FPRC;

|PROCESO Max;
     #1#0 = #0#0;
     #1#1 = "zzzzzzzzzzzzzzzzzzzz";
     #1#3 = 99999;
     #1#11 = "zzzzzzzzzzzzzzz";
|FPRC;

|PROCESO Tipo13; |TIPO 13;
     |ABRE #142; |LEE 000#142p; |CIERRA #142;
     |SI #142#117 = "N";
          |PINTA 2246, "ÁÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÁÄÙ";
          |C_V #1#12, 1, "N";
          |C_V #1#13, 1, "N";
          |C_V #1#14, 1, "N";
     |SINO;
          |ATRI I;
          |PINTA 2347, #1#12;
          |PINTA 2357, #1#13;
          |PINTA 2367, #1#14;
     |FINSI;
     |ENTLINEAL #1#1, -2, 7, 21, 0, Min, Max;
|FPRC;

|PROCESO Borralos;
     |BORRA 020#1a;
|FPRC;

|DEFBUCLE Borralos;
     #1#1;
     ;
     #0#0, "                    ", 00001, "               ";
     #0#0, "zzzzzzzzzzzzzzzzzzzz", 99999, "zzzzzzzzzzzzzzz";
     be;
     NULL, Borralos;
|FIN;

|PROCESO BorraV;
     |BORRA 020#154a;
|FPRC;

|DEFBUCLE BorraV;
     #154#1;
     ;
     #0#0, "                    ", 00001, "      ";
     #0#0, "zzzzzzzzzzzzzzzzzzzz", 99999, "zzzzzz";
     be;
     NULL, BorraV;
|FIN;

|PROCESO Tipo2; |TIPO 2;
     nModo = (FEntrada / 100) ! 0;
     nForma = 0 +. 1;
     |SI nModo ] 2; |Y #0#5 > 0;
          |SI nForma = -1;
               |MENAV "0000Se han efectuado actualizaciones...";
          |FINSI;
          |ERROR; |ACABA;
     |FINSI;
     |SI nModo = 2; |Y nForma = 1;
          |ENTLINEAL #1#1, -2, 2, 21, 0, Min, Max;
     |FINSI;
     |SI nModo = 3;
          |BUCLE Borralos;
          |BUCLE BorraV;
     |FINSI;
|FPRC;

|PROCESO Llena121; |TIPO 6;
     eaArticulo = #1Campo;
     |HAZ LlenaArticulo;
     |SI FSalida = 0;
          #1Campo = eaArticulo;
          |PINTA #1Campo;
     |SINO;
          |ERROR; |ACABA;
     |FINSI;
|FPRC;

|PROCESO Tipo20; |TIPO 20;
     |SI FSalida = "OPCION";
          FSalida = "Opciones";
          |ACABA;
     |FINSI;

     |SELECT #1#0;
     |LEE 101#0=;
     |SI FSalida ! 0; |ACABA; |FINSI;
     |SALVA_FICHA #0;

     |ABRE #100; |LEE 000#100p; |CIERRA #100;

     |PARA aMenu2 = "1"; |SI aMenu2 > 0; |HACIENDO;
          |MENU aMenu;
          aMenu2 = FSalida;
          |SI FSalida = 1; |HAZ TipoImp; |FINSI;
          |SI FSalida = 2; |HAZ ImpValor; |FINSI;
          |SI FSalida = 3; |HAZ Emisiones; |FINSI;
     |SIGUE;
     |SELECT #1#0;
     |REPON_FICHA #0;
     |LIBERA #0;
|FPRC;

|PROCESO Min144;
     #144#0 = #0#0;
     #144#1 = 1;
|FPRC;

|PROCESO Max144;
     #144#0 = #0#0;
     #144#1 = 99999;
|FPRC;

|PROCESO Emisiones;
     |ABRE #144;
     #144#0 = #0#0;
     #144#1 = 1;
     |LEE 000#144];
     |CONSULTA_F_CLAVE #1#144, Min144, Max144;
     |CIERRA #144;
|FPRC;

|PROCESO GrabaLog;
     |ABRE #144;
     #144#0 = #0#0;
     #144#1 = 99999;
     |LEE 000#144];
     |SI FSalida = 0;
          |LEE 000#144a;
     |SINO;
          |LEE 000#144u;
     |FINSI;
     |SI FSalida = 0; |Y #144#0 = #0#0;
          nConta = #144#1 + 1;
     |SINO;
          nConta = 1;
     |FINSI;

     |HORA aAlfa;
     |DDEFECTO #144;
     #144#0 = #0#0;
     #144#1 = nConta;
     #144#2 = Usuario % 810;
     #144#3 = ~;
     #144#4 = aAlfa;
     #144#5 = aFormato;
     #144#6 = #0#5;
     |GRABA 020#144n;
     |CIERRA #144;
|FPRC;

|PROCESO Arti0; |TIPO 0;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 1;
          |ABRE #19;
          |DDEFECTO #19;
          #19#0 = #1#1;
          |LEE 000#19=;
          |CIERRA #19;
          |SI #19#179 = "S";
               |ABRE #238;
               |DDEFECTO #238;
               #238#0 = #19#201;
               |LEE 000#238=;
               |CIERRA #238;

               #1#18 = #238#9;
               #1#19 = #238#10;
               #1#20 = #238#11;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO ImpVal;
     |DDEFECTO #19;
     #19#0 = #1#1;
     |LEE 000#19=;

     |DDEFECTO #238;
     #238#0 = #19#201;
     |LEE 000#238=;

     |DDEFECTO #88;
     #88#0 = #1#1;
     #88#1 = #1#3;
     #88#2 = #1#11;
     |LEE 000#88=;

     |PRINT;
|FPRC;

|DEFBUCLE ImpVal;
     #1#1;
     ;
     #0#0, "                    ", 00000, "               ";
     #0#0, "zzzzzzzzzzzzzzzzzzzz", 99999, "zzzzzzzzzzzzzzz";
     ;
     NULL, ImpVal;
|FIN;

|PROCESO LlenaInicial;
     |DDEFECTO #88;
     #88#0 = #1#1;
     #88#1 = #1#3;
     #88#2 = #1#11;

     |SI #0#3 = "S"; |O #0#3 = "N";
          #88#1 = #0#4; || El almacen del doc
          #88#2 = "";
     |FINSI;
     |SI #0#3 = "T"; #88#2 = ""; |FINSI;

     |LEE 101#88=;
     |SI FSalida ! 0; |GRABA 020#88b; |FINSI;

     #88#3 = #88#3 + #1#5;

     |GRABA 020#88a;
     |LIBERA #88;
|FPRC;

|DEFBUCLE LlenaInicial;
     #1#1;
     ;
     #0#0, "                    ", 00000, "               ";
     #0#0, "zzzzzzzzzzzzzzzzzzzz", 99999, "zzzzzzzzzzzzzzz";
     ;
     NULL, LlenaInicial;
|FIN;

|PROCESO Inicial;
     |SALVA_FICHA #0;

     aTipo = #0#3;
     nAlma = #0#4;
     fFechaIni = "01.01.0000";
     |LEE 000#0a;
     |SI FSalida = 0;
          aEncontrado = "N";
          |SI aTipo = "S"; |O aTipo = "N";
               |PARA; |SI FSalida = 0; |HACIENDO;
                    |SI #0#3 = "S"; |O #0#3 = "N";
                         |SI nAlma = #0#4;
                              aEncontrado = "S";
                              |SAL;
                         |FINSI;
                    |FINSI;
                    |LEE 000#0a;
               |SIGUE;
          |FINSI;
          |SI aTipo = "T";
               |PARA; |SI FSalida = 0; |HACIENDO;
                    |SI #0#3 = aTipo;
                         aEncontrado = "S";
                         |SAL;
                    |FINSI;
                    |LEE 000#0a;
               |SIGUE;
          |FINSI;
          |SI aTipo = "P";
               |PARA; |SI FSalida = 0; |HACIENDO;
                    |SI #0#3 = aTipo;
                         aEncontrado = "S";
                         |SAL;
                    |FINSI;
                    |LEE 000#0a;
               |SIGUE;
          |FINSI;

          |SI aEncontrado = "S";
               fFechaIni = #0#0;
               |BUCLE LlenaInicial;
          |FINSI;
     |FINSI;

     |REPON_FICHA #0;
|FPRC;

|PROCESO EsMoviInv;
     aEsMoviInv = "N";

     |SELECT #2#0
     #0#6 = #65#11;
     #0#7 = #65#3;
     |LEE 000#0=;
     |SI FSalida = 0; |Y #0#0 ! fFecActual;
          |SI #65#2 = "EV";
               |SI #0#8 = "V"; aEsMoviInv = "S"; |FINSI;
          |SINO;
               |SI #0#8 = "R"; aEsMoviInv = "S"; |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO EntraSal;
     |SI #0#3 = "N";
          |SI #65#5 ! #0#4; |ACABA; |FINSI;
     |FINSI;


     |SI #65#2 = "SS"; |O #65#2 = "ES"; |O #65#2 = "EV";
          |HAZ EsMoviInv;
          |SI aEsMoviInv = "S"; |ACABA; |FINSI;
     |FINSI;


     |DDEFECTO #88;
     #88#0 = #65#0;
     #88#1 = #65#5;
     #88#2 = #65#18;

     |SI #0#3 = "S"; |O #0#3 = "N";
          #88#1 = #0#4; || El almacen del doc
          #88#2 = "";
     |FINSI;
     |SI #0#3 = "T"; #88#2 = ""; |FINSI;

     |LEE 101#88=;
     |SI FSalida ! 0; |GRABA 020#88b; |FINSI;

     |SI #65#2 = "AC"; |O #65#2 = "BV"; |O #65#2 = "ES"; |O #65#2 = "EV";
               |O #65#2 = "ME"; |O #65#2 = "FB";
          #88#4 = #88#4 + #65#6;
          #88#7 = #88#7 + #65#17;
     |FINSI;
     |SI #65#2 = "AV"; |O #65#2 = "BC"; |O #65#2 = "FC"; |O #65#2 = "SS";
               |O #65#2 = "TI"; |O #65#2 = "MS"; |O #65#2 = "SF";
               |O #65#2 = "EV";
          #88#5 = #88#5 + #65#6;
          #88#8 = #88#8 + #65#17;
     |FINSI;

     |SI #65#2 = "AV"; |O #65#2 = "FC";
          #88#11 = #88#11 + #65#6;
          #88#12 = #88#12 + #65#17;
     |FINSI;
     |SI #65#2 = "TI";
          |SELECT #2#501;
          #501#36 = "TI";
          #501#57 = #65#11;
          #501#58 = #65#3;
          |LEE 000#501=;
          |SI #501#31 ! "R";
               #88#11 = #88#11 + #65#6;
               #88#12 = #88#12 + #65#17;
          |FINSI;
     |FINSI;

     |GRABA 020#88a;
     |LIBERA #88;
|FPRC;

|DEFBUCLE EntraSal;
     #65#1;
     ;
     "                    ", fFechaIni, "  ", 000000, 00000, "     ";
     "zzzzzzzzzzzzzzzzzzzz", #0#0     , "zz", 999999, 99999, "zzzzz";
     ;
     NULL, EntraSal;
|FIN;

|PROCESO MermasLin;
     |SI #0#3 = "N";
          |SI #502#3 ! #0#4; |ACABA; |FINSI;
     |FINSI;

     |DDEFECTO #88;
     #88#0 = #502#2;
     #88#1 = #502#3;
     #88#2 = #502#30;

     |SI #0#3 = "S"; |O #0#3 = "N";
          #88#1 = #0#4; || El almacen del doc
          #88#2 = "";
     |FINSI;

     |SI #0#3 = "T"; #88#2 = ""; |FINSI;

     |LEE 101#88=;
     |SI FSalida ! 0; |GRABA 020#88b; |FINSI;

     #88#9 = #88#9 + #502#5;
     #88#10 = #88#10 + #502#29;

     |GRABA 020#88a;
     |LIBERA #88;
|FPRC;

|DEFBUCLE MermasLin;
     #502#1;
     ;
     #501#36, #501#0, 001;
     #501#36, #501#0, 999;
     ;
     NULL, MermasLin;
|FIN;

|PROCESO Mermas;
     |BUCLE MermasLin;
|FPRC;

|DEFBUCLE Mermas;
     #501#3;
     31;
     fFechaIni, "      ", " ", "     ", 000001, "R";
     #0#0     , "zzzzzz", "z", "zzzzz", 999999, "R";
     ;
     NULL, Mermas;
|FIN;

|PROCESO ImpValor;
     |MENU aMenuV;
     |SI FSalida [ 0; |ACABA; |FINSI;

     |SI FSalida = 1; aFormato = "PC"; aInforme = "hisinve1"; |FINSI;
     |SI FSalida = 2; aFormato = "PCM"; aInforme = "hisinve2"; |FINSI;
     |SI FSalida = 3; aFormato = "PDTO"; aInforme = "hisinve3"; |FINSI;

     fFecActual = #0#0;

     |HAZ MiraLogos; || Copia Logos para el Crystal

     Impresora = "Crystal Reports";
     |IMPRE 0;
     |SI FSalida = 0;
          |INFOR aInforme;
          |SI FSalida = 0;
               |HAZ CreaTempo;
               |NOME_DAT #88 Tempo; |DELINDEX #88;
               |ABRE #88;

               |HAZ Inicial;
               |ABRE #501;

               fFechaIni = fFechaIni + 1;
               |BUCLE EntraSal;
               |CIERRA #501;

               |BUCLE Mermas;
||CONSULTA_CLAVE #1#88;
               |ABRE #19;
               |ABRE #238;
               |BUCLE ImpVal;
               |CIERRA #19;
               |CIERRA #238;

               |PIEINF;
               |FININF;

               |HAZ GrabaLog;

               |CIERRA #88;
               |DELINDEX #88; |HAZ BoraTempo;
          |SINO;
               aAlfa = "0000Error en informe '" + aInforme + "'";
               |MENAV aAlfa;
          |FINSI;
          |FINIMP;
     |FINSI;
|FPRC;

|PROCESO BorraLinV;
     |BORRA 020#154a;
|FPRC;

|DEFBUCLE BorraLinV;
     #154#1;
     ;
     #1#0, #1#1, #1#3, "      ";
     #1#0, #1#1, #1#3, "zzzzzz";
     be;
     NULL, BorraLinV;
|FIN;

|PROCESO Minc;
     #154#0 = #1#0;
     #154#1 = #1#1;
     #154#2 = #1#3;
     #154#3 = "      ";
|FPRC;

|PROCESO Maxc;
     #154#0 = #1#0;
     #154#1 = #1#1;
     #154#2 = #1#3;
     #154#3 = "zzzzzz";
|FPRC;

|PROCESO Tipo2Lin; |TIPO 2;
     nModo = (FEntrada / 100);
     nForma = 0 +. 1;
     |SI nModo = 3;
          |BUCLE BorraLinV;
     |FINSI;

     |SI nForma = 1;
          |ABRE #19;
          #19#0 = #1#1;
          |LEE 000#19=;
          |SI FSalida ! 0; |DDEFECTO #19; |FINSI;
          |CIERRA #19;
          |SI #19#176 = "S";
               |PUSHV 0501 2380;
               |ENTCOLUMNA #1#154, 4, 4, 5, 1, Minc, Maxc;
               |POPV;
          |FINSI;
          |CIERRA #19;
     |FINSI;
|FPRC;
