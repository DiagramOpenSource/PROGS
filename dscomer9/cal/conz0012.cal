|FICHEROS;
     conz0012 #0;
     conz0013 #1,MANTE;
     agifa007 #5;   ||Series
     agifa019 #7;   ||Articulos
     agifa065 #8;   ||Historico
     con00009 #9;   ||Aux. His
     agifa010 #15;  ||albaran
     agifa071 #16;  ||lineas albaran
     agifa084 #17;  ||Contado
     agifa069 #18;  ||lineas contado
     agifa036 #19;  ||Depositos
     agifa070 #20;  ||lineas depositos
     agifa077 #21;  ||Albaran Compras
     agifa080 #22;  ||lineas albaran compras
     agifa501 #23;  ||Tickets
     agifa502 #24;  ||lineas Tickets
     agifa024 #25;  ||Clientes
     agifa112 #26;  ||Proveedor
     agifa090 #27;  ||Ordenes de Fabricacion
     dsm00006 #906;
     dsm00010 #910;
     agifa142 #142;
     agifa204 #204;
     dsm00056 #56;
     dsm00057 #57;
     dsm00054 #54;
     dsm00043 #43;
     dsm00044 #44;
     agifa041 #41;
     dsw00082 #82,MANTE;
     con00610 #610;
     con00625 #625,MANTE;
|FIN;

|VARIABLES;
     aParam = "";
     nClave = 0;
     nSal = 0;
     &PRMNT_aHisto = "";
     aProg = "";
     aPath = "";
     aEmp = "";
     &enAlta = 0;
     &eaArticulo = "";
     &Tempo     = "";
     aTempo1    = "";
     nMulti     = 0;
     nMts       = 0;
     nTecla     = 0;
     nNume      = 0;
     aAlfa      = "";

     nom_tmp    = "";

     &fuera     = 0;
     &nom       = "";
     &operacion = "";
     &docum     = "";
     &linea     = 0;
     &fecha     = @;
     &program   = "agifa145";
     &operz     = "";
     &factu     = "";
     &eAlfa     = "";
     nomodi     = 0;

     nStock     = 0;
     nStock2    = 0;
     c          = 0;
     m          = 0;
     mensaje    = "";
     pintar     = "";
     i          = 0;
     paso       = 0;
     codigo     = "";
     sw         = 0;

     &aObs1     = "";
     &aObs2     = "";
     &aObs3     = "";

 {-1}menu = "";
     menu1 = "2400";
     menu2 = "1";
     menu3 = "Elija : ";
     menu4 = "PVSO";
     menu5 = " Pantalla ";
     menu6 = " I. Valorado ";
     menu7 = " I. Sin Valorar ";
     menu8 = " I. Observaciones ";
     salmenu = 0;
     informe   = "";
     &ser_alb = "";
     &num_alb = 0;
|FIN;

|CAMPOS;
     #0#21 Entero;
|FIN;

|PROCESO Min;
     #82#0 = 1;
|FPRC;

|PROCESO Max;
     #82#0 = 99999;
|FPRC;

|PROCESO OtrasEmp; |TIPO 0;
     |SI #0#24 = "N"; |ACABA; |FINSI;
     |PUSHV 0501 2380;
     |ENTLINEAL #1#82, 1, 1, 22, 1, Min, Max;
     |POPV;
|FPRC;

|PROCESO CheqEmp; |TIPO 0;
     |DFICE aAlfa;
     aAlfa = aAlfa % -205;
     |SI #82#0 = aAlfa;
          |MENAV "0000Esta es la empresa actual...";
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO mayus; |TIPO 0;
     #0Campo = #0Campo > " ";
     |PINTA #0Campo;
|FPRC;

|PROCESO alta; |TIPO 0;
     #0#20 = #0#0;  |PINTA #0#20;
|FPRC;

|| **************************************************************************
|PROCESO Trunca; || Quita la parte decimal de un numero
     |SI nMts < 0; nMulti = -1; |SINO nMulti = 1; |FINSI;
     nMts = nMts * nMulti;
     nMts = nMts / #7#192;
     Entero = nMts;
     |SI nMulti = 1;
          nMts = Entero;
     |SINO;
          |SI Entero > nMts;
               nMts = Entero - 1;
         |SINO;
               nMts = Entero;
         |FINSI;
     |FINSI;
     nMts = nMts * nMulti;
|FPRC;

|PROGRAMA;
     aParam = PARAMETRO;

     |ABRE #142; |LEE 000#142p; |CIERRA #142;
     aAlfa = #142#118;    || partidas
     |C_V #0#22, 1, aAlfa;
     |C_V #0#23, 1, aAlfa;

     |ABRE #204; |LEE 000#204p; |CIERRA #204;

     |DBASE aAlfa; aAlfa = aAlfa + "fich/";
     |PATH_DAT #41 aAlfa;
     |SALVA_FICHA #41;

     |CLS;
     |CABEZA "Consulta Historico";

     |HAZ CreaTempo; aTempo1 = Tempo;
     |NOME_DAT #1 aTempo1; |DELINDEX #1;

     |DDEFECTO #0;
     |SI aParam = "auto";
          |PINPA #0#0;
          |SI #142#118 = "N"; |PINTA 903, "          "; |FINSI;
          |PINDA #0#0;

          #0#0 = eaArticulo;
          #0#20 = eaArticulo;
          |C_M #0#0, 1, "N";
          |C_M #0#20, 1, "N";

          |ENDATOS #2#0;      || Modi para que no de de alta el alma 99999
          |SI FSalida = 0;
               salmenu = 1;
               |HAZ impresion;
          |FINSI;
     |SINO;
          || Bucle menu
          FSalida = 1;
          |PARA; |SI 0 < FSalida; |HACIENDO;
                 |PINPA #0#0;    || Pinta la pantalla del informe
                 |SI #142#118 = "N"; |PINTA 903, "          "; |FINSI;
                 |PINDA #0#0;    || Pinta los datos por defecto del informe
                 |MENU menu;
                 |SI 0 < FSalida;          || Se ha elegido opcion
                     menu2 = FSalida;      || Defecto de la ultima opcion elegida
                     |C_M #0#20,1,"S";
                     |SI menu2 = 1; |C_M #0#20,1,"N"; |FINSI; || Pantalla, uno solo

                     |ENDATOS #2#0;     || Modi para que no de de alta el alma 99999
                     |SI 0 = FSalida;
                         salmenu = menu2;
                         |HAZ impresion;
                      |FINSI;
                      FSalida = 1;
                |FINSI;
          |SIGUE;
     |FINSI;
     |DELINDEX #1; Tempo = aTempo1; |HAZ BoraTempo;
     |REPON_FICHA #41;
|FPRO;

|| **************************************************************************
|| .................... Temporal

|PROCESO grabar;
     |SI #8#2 = "DP";
          |ACABA;
     |FINSI;

     #9#0 = #8#0;
     #9#1 = #8#1;
     #9#2 = #8#2;
     #9#3 = #8#3;
     #9#4 = #8#4;
     #9#5 = #8#11;
     |LEE 000#9=;
     |SI FSalida ! 0; |DDEFECTO #9; |FINSI;

     || Seleccion de las Operaciones
     |SI #0#14 = "S";
         paso = 0;
         |PARA i = 4; |SI i [ 13; |HACIENDO i = i + 1;
               |SI #8#2 = #0i; paso = 1;  |FINSI;
         |SIGUE;
     |SINO;
         paso = 1;
         |PARA i = 4; |SI i [ 13; |HACIENDO i = i + 1;
               |SI #8#2 = #0i; paso = 0; |FINSI;
         |SIGUE;
     |FINSI;
     |SI paso = 1; |ACABA; |FINSI;

     |PINTA 1359 , #8#1;      || ...  Pinta Fecha

     |SI sw = 0;
         codigo = #8#0;
         |HAZ leearticul;
         nStock = 0;
         nStock2 = 0;
         sw = 1;
     |FINSI;

     |SI codigo ! #8#0;
         |SI salmenu = 1;
             |ERROR10;
         |SINO;
             |PIEINF;
         |FINSI;
         codigo = #8#0;
         |HAZ leearticul;
         nStock = 0;
         nStock2 = 0;
     |FINSI;

     |DDEFECTO #1;
     c = c + 1;

     nStock = nStock + #8#6;
     nStock2 = nStock2 + #8#17;

     || Doble Stock   Y Factor
     |SI #7#179 = "S"; |Y #7#192 ! 1;
          nMts = nStock2;
          |HAZ Trunca;
     |SINO;
          nMts = nStock;
     |FINSI;

     #1#0  = c;
     #1#1  = #8#0;
     #1#2  = #8#1;
     #1#3  = #8#2;
     #1#4  = #8#3;
     #1#5  = #8#4;
     #1#6  = #8#5;
     #1#7  = #8#6;
     #1#8  = nMts;
     #1#9  = #8#8;
     #1#10 = #8#9;
     #1#11 = #8#10;
     #1#12 = #8#11;
     #1#13 = #1#8 * #1#9;
     #1#14 = #8#14;
     #1#15 = #8#17;
     #1#16 = nStock2;
     #1#18 = #8#18;
     #1#51 = aEmp;
     #1#53 = #9#6;
     #1#54 = #9#7;
     #1#55 = #9#8;
     #1#56 = #9#10;
     #1#57 = #9#11;
     #1#58 = #9#12;

     |SI salmenu = 1;
         |GRABA 021#1n;
     |SINO;
         |HAZ calbles;
         |PRINT;
     |FINSI;
|FPRC;

|PROCESO leearticul;
 |DDEFECTO #7;
 #7#0 = #8#0;
 |LEE 001#7=;
 |SI FSalida ! 0;
     #7#1 = "NO EXISTE ARTICULO EN MAESTRO";
 |FINSI;
|FPRC;

|PROCESO RegulaExi;
     |DDEFECTO #44;
     |ABRE #44;
     #44#21 = #8#11;
     #44#18 = #8#3;
     #44#8 = #8#4;
     |LEE 000#44=;
     |CIERRA #44;
     aObs1 = #44#9;
     aObs2 = #44#10;
     aObs3 = #44#11;
|FPRC;

|PROCESO EntraValor;
     |DDEFECTO #57;
     |ABRE #57;
     #57#28 = #8#11;
     #57#27 = #8#3;
     #57#14 = #8#4;
     |LEE 000#57=;
     |CIERRA #57;
     aObs1 = #57#15;
     aObs2 = #57#16;
     aObs3 = #57#17;
|FPRC;

|PROCESO calbles;
 operz = #1#3;
 |HAZ leeserie;

 nom = "";
 |SI operz = "AC"; |O operz = "BC"; |O operz = "BF";
     #26#0 = #1#11;
     |LEE 001#26=;
     |SI FSalida ! 0; |DDEFECTO #26; |FINSI;
      nom = #26#1;
 |FINSI;
 |SI operz = "AV"; |O operz = "BV"; |O operz = "FC"; |O operz = "DP";
                   |O operz = "DV"; |O operz = "SF";
     #25#0 = #1#11;
     |LEE 001#25=;
     |SI FSalida ! 0; |DDEFECTO #25; |FINSI;
     nom = #25#1;
 |FINSI;

 factu  = "";
 nomodi = 1;
 |SI operz = "AV";|O operz = "BV";    ||Albaran
     |SI #1#58 = "S";
         |HAZ albataller;
     |SINO;
         |HAZ albaval;
     |FINSI;
 |FINSI;
 |SI operz = "FC";    ||Factura Contado
     factu = #1#4;
 |FINSI;
 |SI operz = "AC"; |O operz =  "BC";    ||Albaran Compras
     |HAZ albacom;
 |FINSI;
 |SI operz = "ES"; |O operz = "SS";
     |HAZ RegulaExi;
 |FINSI;
 |SI operz = "EV";
     |HAZ EntraValor;
 |FINSI;
|FPRC;

|DEFBUCLE pasa;
     #8#1;
     5, 18;
     #0#0  , #0#1  , "  " ,       0 ,     0 , #0#15 , #0#17, #0#22;
     #0#20 , #0#19 , "zz" ,  999999 , 99999 , #0#16 , #0#18, #0#23;
     e;
     NULL, grabar;
|FIN;
|| ***************************************************************************

|PROCESO PonPath;
     |PATH_DAT #7 aPath;
     |PATH_DAT #25 aPath;
     |PATH_DAT #26 aPath;
     |PATH_DAT #8 aPath;
     |PATH_DAT #9 aPath;
     |PATH_DAT #5 aPath;
     |PATH_DAT #16 aPath;
     |PATH_DAT #15 aPath;
     |PATH_DAT #21 aPath;
     |PATH_DAT #22 aPath;
|FPRC;

|PROCESO Empresas;
     |SI #0#24 = "S";
          aEmp = ("00000" + #82#0) % -105;
          |DBASE aAlfa;
          aPath = aAlfa + "fich/" + aEmp + "/";
          |HAZ PonPath;
          |ABRE #7;
          |ABRE #9;
          |ABRE #25;
          |ABRE #26;
          |BUCLE pasa;
          |CIERRA #25;
          |CIERRA #26;
          |CIERRA #9;
          |CIERRA #7;
     |FINSI;
|FPRC;

|PROCESO EmpresaActual;
     |DFICE aAlfa;
     aEmp = aAlfa % -205;
     |DBASE aAlfa;
     aPath = aAlfa + "fich/" + aEmp + "/";
     |HAZ PonPath;
     |ABRE #7;
     |ABRE #9;
     |ABRE #25;
     |ABRE #26;
     |BUCLE pasa;
     |CIERRA #25;
     |CIERRA #26;
     |CIERRA #9;
     |CIERRA #7;
|FPRC;

|DEFBUCLE Empresas;
     #82#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, Empresas;
|FIN;

|PROCESO impresion;
     nStock = 0;
     nStock2 = 0;
     c      = 0;
     sw     = 0;
     |SI salmenu ! 1;      || ............ Informes por Impresora
         |IMPRE 0; |SI FSalida ! 0; |ACABA; |FINSI;
         |SI salmenu = 2; informe = "conz0a12"; |FINSI;
         |SI salmenu = 3; informe = "conz0b12"; |FINSI;
         |SI salmenu = 4; informe = "conz0c12"; |FINSI;
         |INFOR informe;
         |SI FSalida ! 0;
             aAlfa = "    No existe Informe '" + informe + "'";
             |MENAV aAlfa;
             |FINIMP;
             |ACABA;
         |FINSI;
     |FINSI;

     |DELINDEX #1;

     |SI salmenu = 1;
          |ABRE #1;
     |FINSI;

     |PINTA 1339 , "Procesando Fecha : ";
     |BUCLE Empresas;
     |HAZ EmpresaActual;
     |SI salmenu ! 1;
         |SI sw = 1;  |PIEINF; |FINSI;
         |FININF;
         |FINIMP;
     |SINO;
         |CIERRA #1;
         |PUSHV 0501 2380;
         |PINPA #0#1;

         |SI #142#117 = "S"; ||Doble Stock
              |C_V #1#15, 1, "S";
              |C_V #1#16, 1, "S";
         |FINSI;

         |SI #142#118 = "N";
              |C_V #1#18, 1, "N";
         |SINO;
              |ATRI R; |PINTA 1502, " PARTIDA.....:"; |ATRI r;
         |FINSI;
         |PARA nSal = 1; |SI nSal ! 0; |HACIENDO;
               nSal = 0;
               |SI nClave = 0;
                     |ENTLINEAL #1#1 , -1 , 4 , 22 , 0 , pro1 , pro2;
               |SINO;
                     |ENTLINEAL #6#1 , -4 , 4 , 22 , 0 , pro1 , pro2;
               |FINSI;
         |SIGUE;
         |DELINDEX #1;
         |POPV;
     |FINSI;
|FPRC;

|RUTINA pro1;
     |SI FSalida = "POSICION"; |Y c > 8;
          #1#0 = c - 7;
     |SINO;
          #1#0 = 1;
     |FINSI;
     #1#51 = 0;
     #1#2 = "01.01.0000";
     #1#1 = " " * 20;
|FRUT;

|RUTINA pro2;
     #1#0 = 999999;
     #1#51 = 99999;
     #1#2 = "31.12.9999";
     #1#1 = "z" * 20;
|FRUT;

|RUTINA ClaveBaja;
     #906#0 = #1#1;
     #906#1 = #1#6;
     #906#2 = #1#2;
     #906#3 = #1#3;
     #906#4 = #1#12;
     #906#5 = #1#4;
     #906#6 = #1#5;
     #906#7 = "      ";
|FRUT;

|RUTINA ClaveAlta;
     #906#0 = #1#1;
     #906#1 = #1#6;
     #906#2 = #1#2;
     #906#3 = #1#3;
     #906#4 = #1#12;
     #906#5 = #1#4;
     #906#6 = #1#5;
     #906#7 = "zzzzzz";
|FRUT;

|PROCESO PonPath2;
     |DBASE aAlfa;
     aPath = aAlfa + "fich/" + aEmp + "/";
     |PATH_DAT #910 aPath;
     |PATH_DAT #906 aPath;
     |PATH_DAT #16 aPath;
     |PATH_DAT #15 aPath;
     |PATH_DAT #17 aPath;
     |PATH_DAT #18 aPath;
     |PATH_DAT #19 aPath;
     |PATH_DAT #20 aPath;
     |PATH_DAT #21 aPath;
     |PATH_DAT #22 aPath;
     |PATH_DAT #23 aPath;
     |PATH_DAT #24 aPath;
     |PATH_DAT #27 aPath;
     |PATH_DAT #56 aPath;
     |PATH_DAT #54 aPath;
     |PATH_DAT #43 aPath;
|FPRC;

|PROCESO PinEmp; |TIPO 7;
     |ABRE #41;
     #41#0 = #1#51;
     |LEE 000#41=;
     |CIERRA #41;

     #1#52 = #41#1; |PINTA #1#52;

     |||ATRI S; |PINTA 1215, #41#1; |ATRI 0;
|FPRC;

|PROCESO sal; |TIPO 11;
     nTecla = FSalida;

     |SI nTecla = 12;
          nClave = nClave + 1;
          nClave = nClave $ 2;
          nSal = 1; |PTEC 807; |ACABA;
     |FINSI;

     aEmp = ("00000" + #1#51) % -105;
     |HAZ PonPath2;

     |SI nTecla  = 10;  |HAZ imprime;  |FINSI;

     |SI nTecla = 11;
         |ABRE #910;
         |LEE 040#910p;
         |SI FSalida ! 0;  |DDEFECTO #910;  |FINSI;
         |CIERRA #910;

         nNume         = #910#21;
         aAlfa         = "\" + #910#23 + "+#";
         |CAMPO_ANCHO  #906#7, 1, nNume;
         |CAMPO_MAXIMO #906#7, 1, aAlfa;

         |PUSHV 0501 2380;
         |ENTCOLUMNA #1#906, 8, 4, 5, 1, ClaveBaja, ClaveAlta;
         |POPV;
     |FINSI;

     |DFICE aAlfa;
     aEmp = aAlfa % -205;
     |HAZ PonPath2;
|FPRC;

|| ::::::::::::::::: Ha pulsado F2 (Ver Documento)
|PROCESO imprime;

     operz = #1#3;
     |HAZ leeserie;
     nomodi = 0;

     |PUSHV 0501 2380;
     |CLS;
     |SI operz = "AV";|O operz = "BV";    ||Albaran
         |SI #1#58 = "S";
             |HAZ albataller;
         |SINO;
             |HAZ albaval;
         |FINSI;
     |FINSI;
     |SI operz = "FC";    ||Factura Contado
         |HAZ faconta;
     |FINSI;
     |SI operz = "DP";    ||Deposito
         |HAZ depo;
     |FINSI;
     |SI operz = "AC"; |O operz =  "BC";    ||Albaran Compras
         |HAZ albacom;
     |FINSI;
     |SI operz = "TI";
         |HAZ tiquet;
     |FINSI;
     |SI operz = "SF"; |O operz = "FB";
         |HAZ fabrica;
     |FINSI;
     |SI operz = "EV";
           |HAZ Valorada;
     |FINSI;
     |SI operz = "ME"; |O operz = "MS";
           |HAZ Movimiento;
     |FINSI;
     |SI operz = "ES"; |O operz = "SS";
           |HAZ Regulariza;
     |FINSI;
     |SI operz = "OR";
          |HAZ OR;
     |FINSI;
     |POPV;
|FPRC;

|PROCESO albaval;
 |ABRE #16;
 |ABRE #15;
 aObs1 = "";
 aObs2 = "";
 aObs3 = "";
 #15#52 = #1#12;
 #15#0 = #1#4;
 |LEE 001#15=;
 |SI FSalida = 0;
      |SI nomodi = 0;
          |PINPA #0#15;
          |PINDA #0#15;
          ser_alb = #15#52;
          num_alb = #15#0;
          |PTEC 802;
          PRMNT_aHisto = ser_alb + num_alb;
          aProg = ":dscomer9/agifa010.tab agif0041 " + aEmp;
          |SUB_EJECUTA_NP aProg;
          PRMNT_aHisto = "";
      |SINO;
          factu = #15#39;
          aObs1 = #15#55;
          aObs2 = #15#56;
          aObs3 = #15#57;
      |FINSI;
 |SINO;
      |SI nomodi = 0;
          mensaje = "0000No Encuentro el Albaran " + #1#12 + "\" + #1#4;
          |MENAV mensaje;
      |FINSI;
 |FINSI;
 |CIERRA #15;
 |CIERRA #16;
|FPRC;

|PROCESO albataller;
 |ABRE #610;
 aObs1 = "";
 aObs2 = "";
 aObs3 = "";
 #610#52 = #1#12;
 #610#0 = #1#4;
 |LEE 001#610=;
 |SI FSalida = 0;
      |SI nomodi = 0;
          |PINPA #0#610;
          |PINDA #0#610;
          ser_alb = #610#52;
          num_alb = #610#0;
          |PTEC 802;
          PRMNT_aHisto = ser_alb + num_alb;
          aProg = ":dscomer9/con00610.tab agif0041 " + aEmp;
          |SUB_EJECUTA_NP aProg;
          PRMNT_aHisto = "";
      |SINO;
          factu = #610#39;
          aObs1 = #610#55;
          aObs2 = #610#56;
          aObs3 = #610#57;
      |FINSI;
 |SINO;
      |SI nomodi = 0;
          mensaje = "0000No Encuentro el Albaran " + #1#12 + "\" + #1#4;
          |MENAV mensaje;
      |FINSI;
 |FINSI;
 |CIERRA #610;
|FPRC;

|PROCESO faconta;
 |ABRE #17;
 |ABRE #18;
 #17#48 = #1#12;
 #17#0 = #1#4;
 |LEE 001#17=;
 |SI FSalida = 0;
      |PINPA #0#17;
      |PINDA #0#17;
      ser_alb = #17#48;
      num_alb = #17#0;
      |PTEC 802;
      PRMNT_aHisto = ser_alb + num_alb;
      aProg = ":dscomer9/agifa084.tab agif0041 " + aEmp;
      |SUB_EJECUTA_NP aProg;
      PRMNT_aHisto = "";
 |SINO;
      mensaje = "0000No Encuentro la Factura " + #1#12 + "\" + #1#4;
      |MENAV mensaje;
 |FINSI;
 |CIERRA #17;
 |CIERRA #18;
|FPRC;

|PROCESO depo;
 |ABRE #20;
 |ABRE #19;
 #19#46 = #1#12;
 #19#0 = #1#4;
 |LEE 001#19=;
 |SI FSalida = 0;
      |PINPA #0#19;
      |PINDA #0#19;
      ser_alb = #19#46;
      num_alb = #19#0;
      |PTEC 802;
      PRMNT_aHisto = ser_alb + num_alb;
      aProg = ":dscomer9/agifa036.tab agif0041 " + aEmp;
      |SUB_EJECUTA_NP aProg;
      PRMNT_aHisto = "";
 |SINO;
      mensaje = "0000No Encuentro el Deposito " + #1#12 + "\" + #1#4;
      |MENAV mensaje;
 |FINSI;
 |CIERRA #19;
 |CIERRA #20;
|FPRC;

|PROCESO albacom;
 |ABRE #21;
 |ABRE #22;
 #21#0 = #1#4;
 #21#50 = #1#12;
 |LEE 001#21=;
 |SI FSalida = 0;
     |SI nomodi = 0;
         |PINPA #0#21;
         |PINDA #0#21;
         ser_alb = #21#50;
         num_alb = #21#0;
         |PTEC 802;
         PRMNT_aHisto = ser_alb + num_alb;
         aProg = ":dscomer9/agifa077.tab agif0041 " + aEmp;
         |SUB_EJECUTA_NP aProg;
         PRMNT_aHisto = "";
     |SINO;
         factu = #21#39;
     |FINSI;
 |SINO;
     |SI nomodi = 0;
         mensaje = "0000No Encuentro el Albaran " + #1#4;
         |MENAV mensaje;
     |FINSI;
 |FINSI;
 |CIERRA #21;
 |CIERRA #22;
|FPRC;

|PROCESO tiquet;
 |ABRE #23;
 |ABRE #24;
 |SELECT  #2#23;
 #23#35 = "T";
 #23#57 = #1#12;
 #23#58 = #1#4;

 |LEE 001#23=;
 |SI FSalida = 0;
      |PINPA #0#23;
      |PINDA #0#23;
      |ENDATOS #4#23;
 |SINO;
      mensaje = "0000No Encuentro el Tiquet " + #1#12 + "\" + #1#4;
      |MENAV mensaje;
 |FINSI;
 |CIERRA #23;
 |CIERRA #24;
|FPRC;

|PROCESO fabrica;
 |ABRE #27;
 #27#0 = #1#4;
 |LEE 001#27=;
 |SI FSalida = 0;
      |PINPA #0#27;
      |PINDA #0#27;
      |ENDATOS #4#27;
 |SINO;
      mensaje = "0000No Encuentro la Orden de Fabricacion " + #1#4;
      |MENAV mensaje;
 |FINSI;
 |CIERRA #27;
|FPRC;

|PROCESO Valorada;
     |ABRE #56;
     #56#2 = #1#12;
     #56#0 = #1#4;
     |LEE 001#56=;
     |SI FSalida = 0;
          |PINPA #0#56;
          |PINDA #0#56;
          |ENDATOS #4#56;
     |SINO;
          mensaje = "0000No Encuentro la Entrada Valorada " + #1#12 + "/" + #1#4;
          |MENAV mensaje;
     |FINSI;
     |CIERRA #56;
|FPRC;

|PROCESO Movimiento;
     |ABRE #54;
     #54#2 = #1#12;
     #54#0 = #1#4;
     |LEE 001#54=;
     |SI FSalida = 0;
          |PINPA #0#54;
          |PINDA #0#54;
          |ENDATOS #4#54;
     |SINO;
          mensaje = "0000No Encuentro la Entrada Valorada " + #1#12 + "/" + #1#4;
          |MENAV mensaje;
     |FINSI;
     |CIERRA #54;
|FPRC;

|PROCESO Regulariza;
     |ABRE #43;
     #43#4 = #1#12;
     #43#0 = #1#4;
     |LEE 000#43=;
     |SI FSalida = 0;
          |PINPA #0#43;
          |PINDA #0#43;
          |ENDATOS #4#43;
     |SINO;
          mensaje = "0000No Encuentro la Entrada Valorada " + #1#12 + "/" + #1#4;
          |MENAV mensaje;
     |FINSI;
     |CIERRA #43;
|FPRC;

|PROCESO OR;
     aAlfa = ("000000" + #1#4) % -106;
     |ABRE #625;
     #625#0 = #1#12;
     #625#1 = aAlfa;
     |LEE 000#625=;
     |SI FSalida = 0;
          |PINPA #0#625;
          |PINDA #0#625;
          |PAUSA;
     |SINO;
          mensaje = "0000No Encuentro la OR " + #1#12 + "/" + aAlfa;
          |MENAV mensaje;
     |FINSI;
     |CIERRA #625;
|FPRC;

|PROCESO leeserie; || ....... Lee la Serie por si es Interno
 |ABRE #5;
 |DDEFECTO #5;
 #5#26 = #1#12; ||Serie
 |LEE 001#5=;
 |SI #5#37 = "S";
     |SI operz = "AC"; operz = "AV"; |FINSI;
     |SI operz = "BC"; operz = "BV"; |FINSI;
 |FINSI;
 |CIERRA #5;
|FPRC;

|PROCESO Llena083; |TIPO 6;
     eaArticulo = #0Campo;
     |HAZ LlenaArticulo;
     |SI FSalida = 0;
          #0Campo = eaArticulo;
          |PINTA #0Campo;
     |SINO;
          |ERROR; |ACABA;
     |FINSI;
|FPRC;

|PROCESO Calc683;  |TIPO 6;
     |HAZ Llena083;
|FPRC;

|PROCESO PideArt83;  |TIPO 7;
     |ABRE #910;
     |LEE 040#910p;
     |SI FSalida ! 0;  |DDEFECTO #910;  |FINSI;
     |CIERRA #910;

     |SI #910#2 [ 2;    |ACABA;  |FINSI;

     eAlfa = #0Campo;
     |SUB_EJECUTA_NP "dsz99990";           || Por Pantalla
     |SI eAlfa = "-1";  |PTEC 807;  |ACABA;  |FINSI;
     |SI eAlfa = "-2";              |ACABA;  |FINSI;

     eaArticulo = eAlfa;      ||  700000-4215

     #0Campo = eAlfa;
     |PINTA #0Campo;
     |PTEC 802;
|FPRC;

|PROCESO EntraPar; |TIPO 7;
     |C_M #0#22, 1, "N";
     |C_M #0#23, 1, "N";
     |SI menu2 = 1;
          |DDEFECTO #7;
          |ABRE #7;
          #7#0 = #0#0;
          |LEE 000#7=;
          |CIERRA #7;
          |SI #7#180 = "S"; |Y #142#118 = "S";
               |C_M #0#22, 1, "S";
               |C_M #0#23, 1, "S";
          |FINSI;
     |SINO;
          |SI #142#118 = "S";
               |C_M #0#22, 1, "S";
               |C_M #0#23, 1, "S";
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Partida0; |TIPO 6;
     |C_M #0Campo, 0, aAlfa;
     |SI #142#122 = "S"; |Y aAlfa = "S";
          aAlfa = #0Campo; |QBF aAlfa;
          aAlfa = (#142#121 * #142#120) + aAlfa;
          nNume = -(#142#120 + 100);
          #0Campo = aAlfa % nNume;
          |PINTA #0Campo;
     |FINSI;
|FPRC;
