|FICHEROS;
     diam0001 #0;
     diam0002 #1;
     diam0009 #9;
     agifa019 #19;
     agifa024 #24;
     agifa142 #142;
     diam0004;
     dsparm05@ #100;
     dsparm01@ #101;
|FIN;

|VARIABLES;
     &enDirEnt = 0;
     aDef1     = "";
     aDef2     = "";
     aDat      = "";
     aIP       = "";
     aDjunto   = "";
     aMensaje  = "";
     aFrom     = "";
     aServidor = "";
     aSubject  = "";
     aTo       = "";
     aFic      = "";
     aBase     = "";

     nModo     = 0;
     aAlfa     = "";
     aArtAnt   = "";

     &enPromo     = 0;
     &efFecha     = @;
     &nPromo = 0;
     &uni_dt = 0;   || Unidades
     &fed_dt = @;   || Fecha....
     &art_dt = "";  || Articulo.
     &cli_dt = "";  || Cliente..
     &fam_dt = "";  || Familia.
     &iva_dt = 0;   || Tipo Iva.
     &dtos_2 = 0;   || Descuento 2.
     &dto_pt = 0;   || Descuento Ptas.
     &dtos_1 = 0;   || Descuento 1.
     &pvp_ve = 0;   || Precio Venta.
     &gru_dt = "";  || Grupo Cliente.
     &tarifa = 0;   || Tarifa Cliente.
     &quedto = 0;   || 1=cli./art, 2=cli./familia, 3=cli./subfam.
     &enMensaje = 0;
     &aParam  = "";
     aModifica = "";
     nPara1 = 0;
     nOper = 0;
     nCampo1 = 0;
     nCampo2 = 0;
     aTecla  = "";
|FIN;

|PROCESO Alta; |TIPO 7;
     nModo = (FEntrada / 100) ! 0
     |SI nModo = 1;
          |HORA aAlfa;
          #0#12 = ~;
          #0#13 = aAlfa;
     |FINSI;
|FPRC;

|PROCESO Tecnico1;
     |C_M #0#30, 1, "S";
     |SI #0#18 = "  ";
         |C_M #0#30, 1, "N";
         #0#30 = 0;
         |PINTA #0#30;
     |FINSI;
|FPRC;

|PROCESO Tecnico2;
     |C_M #0#31, 1, "S";
     |SI #0#29 = "  ";
         |C_M #0#31, 1, "N";
         #0#31 = 0;
         |PINTA #0#31;
     |FINSI;
|FPRC;

|PROCESO AltaPorce;
     |PUSHV 0501 2380;

     aAlfa = #0#1 % -104;

     #9#0 = #0nCampo1;
     #9#5 = aAlfa;
     |LEE 101#9=;
     |SI FSalida ! 0;
         |DDEFECTO #9;
         #9#0 = #0nCampo1;
         #9#5 = aAlfa;
         |GRABA 020#9b;
     |FINSI;

     |PINPA #0#9;
     |PINDA #0#9;
     |PINTA 0802, #9#5;
     |ENDATOS #1#9;
     |SI FSalida = 0;
         |GRABA 020#9a;
     |FINSI;

     |POPV;
|FPRC;

|PROCESO TipoPorce;
     |SI Campo = 30;  nCampo1 = 18;  nCampo2 = 30;  |FINSI;
     |SI Campo = 31;  nCampo1 = 29;  nCampo2 = 31;  |FINSI;

     |SI FSalida = 10;
         |PUSHV 0501 2380;
         aAlfa = #0#1 % -104;

         |ABRE #9;
         #9#0 = #0nCampo1;
         #9#5 = aAlfa;
         |LEE 040#9=;
         |SI FSalida ! 0;  |DDEFECTO #9;  |FINSI;

         |PINPA #0#9;
         |PINDA #0#9;
         |PINTA 0802, #9#5;
         |LEETECLA aTecla;

         |CIERRA #9;
         |POPV;

         |ERROR;
         |ACABA;
     |FINSI;

     |SI #0nCampo2 = 0;  |Y #0nCampo1 ! "  ";
         |ERROR;
         |ACABA;
     |FINSI;

     |SI #0nCampo2 = 0;  |Y #0nCampo1 = "  ";
         |ACABA;
     |FINSI;

     aAlfa = #0#1 % -104;

     |ABRE #9;
     #9#0 = #0nCampo1;
     #9#5 = aAlfa;
     |LEE 101#9=;
     |SI FSalida ! 0;
         |HAZ AltaPorce;
     |FINSI;

     |SI #0nCampo2 = 1;  |Y #9#1 = 0;
         |MENAV "     No hay asignado porcentaje en el tipo introducido";
         |HAZ AltaPorce;
         |ERROR;
     |FINSI;

     |SI #0nCampo2 = 2;  |Y #9#2 = 0;
         |MENAV "     No hay asignado porcentaje en el tipo introducido";
         |HAZ AltaPorce;
         |ERROR;
     |FINSI;

     |SI #0nCampo2 = 3;  |Y #9#2 = 0;
         |MENAV "     No hay asignado porcentaje en el tipo introducido";
         |HAZ AltaPorce;
         |ERROR;
     |FINSI;

     |CIERRA #9;
|FPRC;


|PROCESO Arti; |TIPO 0;
     nModo = (FEntrada / 100) ! 0

     |SI nModo ! 1;
          |SI Contenido = #1#4;
               |ACABA;
          |FINSI;
     |FINSI;

     |ABRE #142; |LEE 000#142p; |CIERRA #142;

     |ABRE #19;
     #19#0 = #1#4;
     |LEE 000#19=;
     |CIERRA #19;

     |ABRE #24;
     #24#0 = #0#0;
     |LEE 000#24=;
     |CIERRA #24;

     |SI #142#60 = 1; #1#10 = #19#9; |FINSI;
     |SI #142#60 = 1; #1#10 = #19#28; |FINSI;
     |SI #142#60 = 1; #1#10 = #19#133; |FINSI;
     |PINTA #1#10;

     fed_dt = #0#1;   || Fecha....
     art_dt = #1#4;   || Articulo.
     cli_dt = #1#0;   || Cliente..
     fam_dt = #19#2;  || Familia.
     iva_dt = #19#30; || Tipo Iva.
     dtos_2 = 0;      || Descuento 2.
     dto_pt = 0;      || Descuento Ptas.
     dtos_1 = 0;      || Descuento 1.
     pvp_ve = 0;      || Precio Venta.
     tarifa = #24#39; || Tarifa Cliente....
     gru_dt = #24#158;|| Grupo Cliente.
     uni_dt = #0#5;
     enMensaje = 0;
     enDirEnt = 0;
     aParam = "";
     |SI #19#176 = "S";  enMensaje = 1;  |FINSI;

     |HAZ calcdtos;

     #1#8 = pvp_ve - dto_pt;   || Precio Venta. sin iva.
     |PINTA #1#8;

     aArtAnt = #1#4;
|FPRC;

|PROCESO Tota; |TIPO 0;
     #1#9 = #1#6 * #1#8; |PINTA #1#9;
     #1#11 = #1#6 * #1#10; |PINTA #1#11;
|FPRC;

|PROCESO Modificable;
     |PARA nPara1 =  2; |SI nPara1 [ 11; |HACIENDO nPara1 = nPara1 + 1;
          |C_M #diam0001#nPara1#, 1, aModifica;
     |SIGUE;
     |PARA nPara1 = 18; |SI nPara1 [ 18; |HACIENDO nPara1 = nPara1 + 1;
          |C_M #diam0001#nPara1#, 1, aModifica;
     |SIGUE;
     |PARA nPara1 =  3; |SI nPara1 [  6; |HACIENDO nPara1 = nPara1 + 1;
          |C_M #diam0002#nPara1#, 1, aModifica;
     |SIGUE;
     |PARA nPara1 =  8; |SI nPara1 [  8; |HACIENDO nPara1 = nPara1 + 1;
          |C_M #diam0002#nPara1#, 1, aModifica;
     |SIGUE;
|FPRC;

|PROCESO NoModi; |TIPO 2;
     nModo = (FEntrada / 100) ! 0
     nOper = 0 +. 1;
     |SI nModo = 2; |Y #0#15 ! 0; |Y nOper = -1;
          |MENAV "0000Solo se puede modificar el precio de coste ...";
          aModifica = "N";    |HAZ Modificable;
     |FINSI;
     |SI nModo = 2; |Y #0#15 ! 0; |Y nOper = 1;
          aModifica = "S";    |HAZ Modificable;
     |FINSI;
     |SI nModo = 3; |Y #0#15 ! 0;
          |MENAV "0000No se puede borrar ...";
          |ERROR;
     |FINSI;
     |SI nOper = 1;
          |HAZ EnviaCorreo;
     |FINSI;
|FPRC;

|PROCESO LasDivisas; |TIPO 13;
     |HAZ DecimalesBase;
|FPRC;

|PROCESO Descrip7; |TIPO 7;
     aAlfa = #1#5; |QBF aAlfa;
     |SI aAlfa = "";
          |ABRE #19;
          #19#0 = #1#4;
          |LEE 000#19=;
          |CIERRA #19;
          #1#5 = #19#1; |PINTA #1#5;
     |FINSI;
|FPRC;

|PROCESO Descrip0; |TIPO 0;
     |SI FSalida = 10;
          #1#5 = "";
          |HAZ Descrip7;
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO Tipo2Lin; |TIPO 2;
     nModo = (FEntrada / 100) ! 0
     |SI nModo = 3; |Y #0#15 ! 0;
          |ERROR;
     |FINSI;

     |ABRE #diam0004;
     |LEE 000#diam0004.p;
     |CIERRA #diam0004;
     |SI #diam0002#4 ] #diam0004#1;|Y #diam0002#4 [ #diam0004#2;
          #0#20 = #0#20 +. #1#11;
          #0#21 = #0#21 +. #1#9;
          |SI #0#20 ! 0;|O #0#21 ! 0;
               #0#22 = ((#0#21 - #0#20) / #0#21) * 100;
          |SINO;
               #0#22 = 0;
          |FINSI;
     |FINSI;

     #0#16 = #0#16 +. #1#11;
     #0#17 = #0#17 +. #1#9;
     |SI #0#16 ! 0;|O #0#17 ! 0;
          #0#19 = ((#0#17 - #0#16) / #0#17) * 100;
     |SINO;
          #0#19 = 0;
     |FINSI;

     |PINTA #0#16;
     |PINTA #0#17;
     |PINTA #0#19;
     |PINTA #0#20;
     |PINTA #0#21;
     |PINTA #0#22;
|FPRC;

|PROCESO Diam20; |TIPO 20;
     |SI FSalida = "OPCION";
          FSalida = "Observ.";
          |ACABA;
     |FINSI;
     |HAZ Observa;
|FPRC;

|PROCESO Tecla; |TIPO 6;
     |SI FSalida = 13;
          |HAZ Observa;
          |ERROR;
     |SINO;
          |MENSA "0000<F5> Observaciones";
     |FINSI;
|FPRC;

|PROCESO Observa;
     |PUSHV 0501 2380;
     |BLANCO 717 1571;
     |CUADRO 717 1571;
     |CUADRO 818 1470;
     |ATRI I; |PINTA 820, " O B S E R V A C I O N E S "; |ATRI 0;

     |C_V #0#23, 1, "S"; |PINTA #0#23;
     |C_V #0#24, 1, "S"; |PINTA #0#24;
     |C_V #0#25, 1, "S"; |PINTA #0#25;
     |C_V #0#26, 1, "S"; |PINTA #0#26;
     |C_V #0#27, 1, "S"; |PINTA #0#27;
     |SI #0#15 = 0;
          |LEE 101#0=;
          |SI FSalida ! 0; |VETE Final; |FINSI;
       |ET E0;
          |ENCAMPO #23#0;
          |SI FSalida = 1; |VETE Fin; |FINSI;
          |SI FSalida = 2; |VETE Fin; |FINSI;
       |ET E1;
          |ENCAMPO #24#0;
          |SI FSalida = 1; |VETE Fin; |FINSI;
          |SI FSalida = 2; |VETE E0; |FINSI;
       |ET E2;
          |ENCAMPO #25#0;
          |SI FSalida = 1; |VETE Fin; |FINSI;
          |SI FSalida = 2; |VETE E1; |FINSI;
       |ET E3;
          |ENCAMPO #26#0;
          |SI FSalida = 1; |VETE Fin; |FINSI;
          |SI FSalida = 2; |VETE E2; |FINSI;
       |ET E4;
          |ENCAMPO #27#0;
          |SI FSalida = 1; |VETE Fin; |FINSI;
          |SI FSalida = 2; |VETE E3; |FINSI;
       |ET Fin;
          |GRABA 020#0a;
       |ET Final;
     |SINO;
          |PAUSA;
     |FINSI;
     |C_V #0#23, 1, "N"; |PINTA #0#23;
     |C_V #0#24, 1, "N"; |PINTA #0#24;
     |C_V #0#25, 1, "N"; |PINTA #0#25;
     |C_V #0#26, 1, "N"; |PINTA #0#26;
     |C_V #0#27, 1, "N"; |PINTA #0#27;
     |POPV;
|FPRC;

|PROCESO EnviaCorreo;
     |CARGA_DEF aDef1, dsparm05@;
     |SI FSalida = 0;
          |PATH_DAT #100 aDat;
          |ABRE #100;
          |CARGA_DEF aDef2, dsparm01@;
          |SI FSalida = 0;
               |PATH_DAT #101 aDat;
               |ABRE #101; |LEE 000#101p; |CIERRA #101;

               aAlfa = #0#0 + "/" + #0#1 + "/" + #0#13;

               #100#0 = #101#12;
               |LEE 000#100=;
               |SI FSalida = 0;
                    aTo = #100#3;       |QBF aTo;
                    |SI nModo = 1; aSubject = "Alta Prepedido:" + aAlfa; |FINSI;
                    |SI nModo = 2; aSubject = "Modificacion Prepedido:" + aAlfa; |FINSI;
                    |SI nModo = 3; aSubject = "Baja Prepedido:" + aAlfa; |FINSI;

                    aAlfa = Usuario % 810;
                    |SELECT #3#100;
                    #100#2 = aAlfa;
                    |LEE 000#100=;

                    aFic      = "vacio.txt";
                    aFrom     = #100#3;     |QBF aFrom;
                    aServidor = #101#7; |QBF aServidor;
                    aIP       = #101#8;       |QBF aIP;
                    aDjunto   = aFic;
                    aMensaje  = aSubject;

                    |DSCORREO_ENVIA aIP, aServidor, aFrom, aTo, aSubject, aMensaje, aDjunto;
                    |SI FSalida ! 0;
                         |MENAV "0000Ha habido un error durante el envio.";
                    |FINSI;
               |FINSI;
               |DESCARGA_DEF #dsparm01@;
          |SINO;
               |MENAV "      No puedo cargar el Def dsparm01";
          |FINSI;
          |CIERRA #100;
          |DESCARGA_DEF #dsparm05@;
     |SINO;
          |MENAV "      No puedo cargar el Def dsparm05";
     |FINSI;
|FPRC;

|PROCESO Tipo80;  |TIPO 80;
     |DBASS aBase; |QBF aBase;

     aDef1 = aBase + "dspartes/def/dsparm05";
     aDef2 = aBase + "dspartes/def/dsparm01";
     aDat  = aBase + "dspartes/fich/";
|FPRC;

|PROCESO Fentrega; |TIPO 0;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 1;
          #0#28 = #0#1 + 15;
          |PINTA #0#28;
     |FINSI;
|FPRC;
