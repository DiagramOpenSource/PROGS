|FICHEROS;
     congz005 #0;
     congw001 #1,MANTE;   || Tmp
     cong0008 #8;
     cong0010 #10;         || Aux Pedido
     agifa019 #19;
     dsm00024 #24;
     agifa073 #73;
     agifa104 #104;
     agifa142 #142;
|FIN;

|VARIABLES;
     &eaAlfa = "";
     &Tempo = "";
     aSepara = ";";

     nAbre = 0;
     aRep = "";
     nPos = 0;
     aIP = "";
     aFecha = "";
     aTmp = "";
     nHandle = 0;
     aPath = "";
     aAlfa = "";
     aDes = "";
     aFich = "";
     aArti = "";
     aDato = "";
     aPartida = "";
     fCaduca = @;
     nClave = 0;
     nSwFin = 0;
     nNume = 0;
     nBtnOrden = 0;
     aMarca = "";
     fFecha = @;
     aHora = "";
     aNomFic = "";
     {-1}aMenu = "";
     aMenu1 = "2400";
     aMenu2 = "1";
     aMenu3 = "Opciones:  ";
     aMenu4 = "RAC";
     aMenu5 = " Repasar ";
     aMenu6 = " Aceptar ";
     aMenu7 = " Cancelar ";
     nPendientes = 0;
|FIN;

|PROCESO Partidas;
     |SI #24#22 > 0;
          |SI aPartida = ""; |O #24#27 < fCaduca;
               aPartida = #24#2;
               fCaduca = #24#27;
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE Partidas;
     #24#1;
     ;
     #73#2, #73#3, "                              ";
     #73#2, #73#3, "zzzzzzzzzzzzzzzzzzzzzzzzzzzzzz";
     ;
     NULL, Partidas;
|FIN;

|PROCESO CierraFic;
     |SI aRep ! ""; |Y nAbre ] 0;
          |XCIERRA nHandle;

          |SI aIP = "";
               |COPIA_FICHERO aTmp, aDes;
          |SINO;
               |ENVIA_FICHERO aTmp, aDes;
          |FINSI;
          |SI FSalida ! 0;
               aAlfa = "0000Error al copiar " + aNomFic;
               |MENAV aAlfa;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO AbreFic;
     aNomFic = "Entrada/";
     aNomFic = aNomFic + (fFecha%704)+(fFecha%402)+(fFecha%102);
     aNomFic = aNomFic + (aHora%102) + (aHora%402);
     ||aNomFic = aNomFic + "_" + (Usuario % 810); |QBF aNomFic;
     aNomFic = aNomFic + "_" + #1#14; |QBF aNomFic;
     aNomFic = aNomFic + "_" + (fFecha%902)+(fFecha%402)+(fFecha%102);

     aDes = aPath + aNomFic + ".CSV";

     |DFICE aFich;
     aTmp = aFich + Usuario;
     |XABRE "W", aTmp, nHandle;
     nAbre = FSalida;
     |SI FSalida ] 0;
          aDato = "ID_LINEA" + aSepara + "NUM_PEDIDO" + aSepara;
          aDato = aDato + "COD_PRODUCTO"+ aSepara + "DES_PRODUCTO" + aSepara;
          aDato = aDato + "COD_CLIENTE"+ aSepara + "DES_CLIENTE" + aSepara;
          aDato = aDato + "PARTIDA"+ aSepara + "FECHA" + aSepara;
          aDato = aDato + "PRECIO"+ aSepara + "TARA" + aSepara;
          aDato = aDato + "MIN_PESO"+ aSepara + "MAX_PESO" + aSepara;
          aDato = aDato + "TIPO_PESO" + aSepara + "CANTIDAD";
          |XGRABA nHandle, aDato;
     |SINO;
          aAlfa = "0000Error al crear fichero representante: " + #1#14;
          |MENAV aAlfa;
     |FINSI;
|FPRC;

|PROCESO TmpLineas;
     |SI aRep ! #1#14;
          |HAZ CierraFic;
          |HAZ AbreFic;
          aRep = #1#14;
     |FINSI;

     #104#25 = #1#0;
     #104#0 = #1#1;
     |LEE 020#104=;

     #73#28 = #1#0;
     #73#0 = #1#1;
     #73#1 = #1#2;
     |LEE 020#73=;

     |DDEFECTO #10;
     #10#0 = #1#0;
     #10#1 = #1#1;
     #10#2 = #1#2;
     |LEE 101#10=;
     |SI FSalida ! 0; |GRABA 020#10b; |FINSI;
     #10#3 = 1;
     |GRABA 020#10a;

     aFecha = (#104#1%102)+"/"+(#104#1%402)+"/"+(#104#1%704);
     aPartida = "";
     |BUCLE Partidas;

     nPos = 100 + #142#155;
     aAlfa = #1#0 % nPos;
     aDato = aAlfa + (("000000"+#1#1)%-106) + (("000"+#1#2)%-103); || ID
     aDato = aDato + aSepara + #104#0;                   || Pedido
     aDato = aDato + aSepara + #73#2; |QBF aDato;       || Articulo
     aDato = aDato + aSepara + #73#4; |QBF aDato;       || Descripcion
     aDato = aDato + aSepara + #104#4; |QBF aDato;     || Cliente
     aDato = aDato + aSepara + #104#8; |QBF aDato;     || Nombre
     aDato = aDato + aSepara + aPartida;               || Partida
     aDato = aDato + aSepara + aFecha;                 || Fecha
     aDato = aDato + aSepara + #73#6;                  || Precio
     aDato = aDato + aSepara + "0";               ||Tara
     aDato = aDato + aSepara + "0";               ||Min peso
     aDato = aDato + aSepara + "0";               ||Max peso
     aDato = aDato + aSepara + "1";               ||Tipo peso
     aDato = aDato + aSepara + #73#5;                  || Cantidad

     eaAlfa = aDato; |HAZ QuitaRarosExp; aDato = eaAlfa;

     |XGRABA nHandle, aDato;
|FPRC;

|DEFBUCLE TmpLineas;
     #1#4;
     13;
     "  ", "     ", 000001, 001, "*";
     "zz", "zzzzz", 999999, 999, "*";
     ;
     NULL, TmpLineas;
|FINSI;

|PROCESO EnviaLineas;
     fFecha = ~;
     |HORA aHora;
     aRep = "";
     |ABRE #104;
     |ABRE #73;
     |ABRE #24;
     |ABRE #19;
     |ABRE #10;
     |BUCLE TmpLineas;
     |CIERRA #10;
     |CIERRA #19;
     |CIERRA #24;
     |CIERRA #73;
     |CIERRA #104;
     |HAZ CierraFic;
|FPRC;

|PROCESO TmpProductos;
     |SI aArti ! #1#3;
          aArti = #1#3;

          aDato = #1#3; |QBF aDato;
          aDato = aDato + aSepara + #1#4; |QBF aDato;

          |XGRABA nHandle, aDato;
     |FINSI;
|FPRC;

|DEFBUCLE TmpProductos;
     #1#2;
     13;
     "                    ", "     ", 000001, 001, "*";
     "zzzzzzzzzzzzzzzzzzzz", "zzzzz", 999999, 999, "*";
     ;
     NULL, TmpProductos;
|FINSI;

|PROCESO EnviaProdu;
     aDes = aPath + "Maestro/PRODUCTOS.CSV";

     |DFICE aFich;
     aTmp = aFich + Usuario;
     |XABRE "W", aTmp, nHandle;
     |SI FSalida ] 0;
          aDato = "Co_Articulo" + aSepara + "Des_Articulo";
          |XGRABA nHandle, aDato;

          aArti = "";
          |BUCLE TmpProductos;
          |XCIERRA nHandle;

          |SI aIP = "";
               |COPIA_FICHERO aTmp, aDes;
          |SINO;
               |ENVIA_FICHERO aTmp, aDes;
          |FINSI;
          |SI FSalida ! 0;
               |MENAV "0000Error al copiar Maestro/PRODUCTOS";
          |FINSI;
     |SINO;
          |MENAV "0000Error al crear productos...";
     |FINSI;
|FPRC;

|PROCESO Envia;
     aPath = #8#1; |QBF aPath;
     aAlfa = aPath % -101;
     |SI aAlfa ! "\"; |Y aAlfa ! "/"; aPath = aPath + "/"; |FINSI;
     |HAZ EnviaProdu;
     |HAZ EnviaLineas;
|FPRC;

|PROCESO Todos;
     #1#13 = aMarca;
     |GRABA 020#1a;
|FPRC;

|DEFBUCLE Todos;
     #1#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, Todos;
|FIN;

|PROCESO LinPedido;
     |SI #73#2 < #0#12; |O #73#2 > #0#13;    || Arti
          |ACABA;
     |FINSI;
     |SI #73#18 < #0#14; |O #73#18 > #0#15;  || Fam
          |ACABA;
     |FINSI;

     |DDEFECTO #10;
     #10#0 = #104#25;
     #10#1 = #104#0;
     #10#2 = #73#1;
     |LEE 000#10=;
     |SI FSalida ! 0; |DDEFECTO #10; |FINSI;

     |SI #0#16 ! "T";
          |SI #0#16 = "S";
               |SI #10#3 = 0; |ACABA; |FINSI;
          |SINO;
               |SI #10#3 ! 0; |ACABA; |FINSI;
          |FINSI;
     |FINSI;

     #19#0 = #73#2;
     |LEE 000#19=;
     |SI FSalida ! 0; |DDEFECTO #19; |FINSI;
     aAlfa = #19#113 > ""; |QBF aAlfa;
     |SI aAlfa = "S";     || articulos que se pesan
          nPendientes = (#73#5 - #73#43) ! #142#8;
          |SI nPendientes > 0;
               #1#0 = #104#25;
               #1#1 = #104#0;
               #1#2 = #73#1;
               #1#3 = #73#2;
               #1#4 = #73#4;
               #1#5 = #104#1;
               #1#6 = #104#2;
               #1#7 = #104#4;
               #1#8 = #104#8;
               #1#9 = #73#5;
               #1#10 = #73#36;
               #1#11 = #19#104; || Stock Disponible
               #1#12 = #10#3;
               #1#13 = "";
               #1#14 = #104#7;
               #1#15 = nPendientes;
               |GRABA 020#1n;
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE Pedido;
     #104#1;
     4, 7, 1, 2;
     #0#2, #0#6, #0#0, #0#4, #0#8, #0#10;
     #0#3, #0#7, #0#1, #0#5, #0#9, #0#11;
     ;
     NULL, NULL, LinPedido;
|FIN;

|PROCESO Min;
     #1#0 = "     ";
     #1#1 = 000000;
     #1#2 = 000;
     #1#3 = " " * 20;
     #1#8 = " " * 60;
|FPRC;

|PROCESO Max;
     #1#0 = "zzzzz";
     #1#1 = 999999;
     #1#2 = 999;
     #1#3 = "z" * 20;
     #1#8 = "z" * 60;
|FPRC;

|PROCESO BtnOrden;
     |PTEC 807;
     nSwFin = 1;
     nClave = nClave + 1;
|FPRC;

|PROCESO Tecla0; |TIPO 0;
     |SI FSalida = 0;
          |SI #1#13 = "*";
               #1#13 = ""; |PINTA #1#13;
          |SINO;
               #1#13 = "*"; |PINTA #1#13;
          |FINSI;
          |GRABA 020#1a;
     |FINSI;
|FPRC;

|PROCESO Tecla11; |TIPO 11;
     |SI FSalida = 10;
          |PTEC 807;
          nSwFin = 1;
          |SI #1#13 = " ";
               aMarca = "*";
          |SINO;
               aMarca = " ";
          |FINSI;
     |FINSI;
     |SI FSalida = 13;
          |PTEC 807;
          nSwFin = 1;
          nClave = nClave + 1;
     |FINSI;
|FPRC;

|PROCESO T3; |TIPO 3;
     |CONTROL_SIMPLE 0, "BOTON,{{137}}F5.- Cambio Orden", 0569, 0580, BtnOrden;
     nBtnOrden = FSalida;

     |ABRE #8; |LEE 000#8p; |CIERRA #8;
     |ABRE #142; |LEE 000#142p; |CIERRA #142;

     aIP = ""; |IP_REMOTO aIP;

     |HAZ CreaTempo;
     |NOME_DAT #1Tempo; |DELINDEX #1;
     |ABRE #1;

     |ABRE #10;
     |ABRE #19;
     |BUCLE Pedido;
     |CIERRA #19;
     |CIERRA #10;

     |LEE 000#1p;
     |SI FSalida = 0;
          |PARA; |SI; |HACIENDO;
               |ENTLINEAL #2#1, 1, 7, 19, 1, Min, Max;
               |PARA nSwFin = 1; |SI nSwFin = 1; |HACIENDO;
                    nSwFin = 0;
                    nNume = nClave $ 2;
                    |SI nNume = 0;
                         |ENTLINEAL #2#1, 1, 4, 19, 0, Min, Max;
                    |SINO;
                         |ENTLINEAL #3#1, 1, 4, 19, 0, Min, Max;
                    |FINSI;
                    |SI aMarca ! "";
                         |BUCLE Todos;
                         aMarca = "";
                    |FINSI;
               |SIGUE;
               |MENU aMenu;
               |SI FSalida = 2; |HAZ Envia; |SAL; |FINSI;
               |SI FSalida = 3; |SAL; |FINSI;
          |SIGUE;
     |SINO;
          |MENAV "0000No hay registros...";
     |FINSI;
     |CIERRA #1; |DELINDEX #1;

     |FIN_CONTROL_WINDOWS nBtnOrden;
|FPRC;
