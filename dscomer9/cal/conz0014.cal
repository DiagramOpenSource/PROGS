|FICHEROS;
     conz0014 #0;
     agifa019 #19;
|FIN;

|VARIABLES;
     nBtn = 0;
     aAlfa = "";
     nHandle = 0;
     aReg = "";
     aDes = "";
     aPvp = "";
     aDto = "";
     aOpt = "";
     aKey = "";
     aDato = "";
     nNume = 0;
     aIP = "";
     aFic = "";
|FIN;

|PROCESO Dato;
     aReg = aDato % 0115;
     aDes = aDato % 1616;
     aPvp = aDato % 3208;
     aDto = aDato % 4002;
     aOpt = aDato % 5602;
     aKey = aDato % 6601;

     #19#0 = aReg;
     |LEE 101#19=;
     |||SI FSalida ! 0; |GRABA 020#19b; |FINSI;
     |SI FSalida = 0;
          |PINTA 1306, #19#0;
          nNume = aPvp; nNume = nNume / 100;
          aAlfa = aDto + aOpt + aKey; |QBF aAlfa;
          |SI aAlfa ! "";
               |QBF aDes;
               aDes = aDes + "-" + aAlfa;
          |FINSI;
          #19#1 = aDes;
          #19#18 = nNume;
          #19#110 = aDto;
          #19#111 = aOpt;
          #19#112 = aKey;
          |GRABA 020#19a;
          |LIBERA #19;
     |FINSI;
|FPRC;

|PROCESO Tipo3; |TIPO 3;
     aFic = #0#0; |QBF aFic;
     |SI aFic ! "";
          |IP_REMOTO aIP;
          |SI aIP ! "";
               |DFICE aAlfa;
               aAlfa = aAlfa + Usuario;
               |RECIBE_FICHERO aFic, aAlfa;
               aFic = aAlfa;
          |FINSI;

          |ABRE #19;
          |XABRE "", aFic, nHandle;
          |SI FSalida ] 0;
               |XLEE nHandle, 250, aDato;
               |PARA; |SI FSalida ] 0; |HACIENDO;
                    |HAZ Dato;
                    |XLEE nHandle, 250, aDato;
               |SIGUE;
               |XCIERRA nHandle;
          |SINO;
               |MENAV "0000Error al abrir fichero...";
          |FINSI;
          |CIERRA #19;

          |SI aIP ! "";
               |FBORRA aFic;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Browse; |TIPO 0;
     |SI FSalida ! 10; |ACABA; |FINSI;

     aAlfa = "F*.txt";

     |BROWSE aAlfa;

     aAlfa = aAlfa % 2; |QBF aAlfa;
     |SI aAlfa = "F"; aAlfa = ""; |FINSI;

     |QBF aAlfa;
     |SI aAlfa = "";
          |ERROR;
     |SINO;
          #0#0 = aAlfa;
          |PINTA #0#0;
     |FINSI;
|FPRC;

|PROCESO Btn;
     |PTEC 824;
|FPRC;

|PROGRAMA;
     |CONTROL_SIMPLE 0, "BOTON,{{137}}Fichero", 1006, 1013, Btn;
     nBtn = FSalida;
     |PINPA #0#0;
     |PINDA #0#0;
     |ENDATOS #1#0;
     |FIN_CONTROL_WINDOWS nBtn;
|FPRO;
