|FICHEROS;
     agifa321;
     agifa324;
     malpe090;
|FIN;

|VARIABLES;
     &nDeci_IVADiv   = 0;
     &nDeci_Base     = 0;   || Decimales en Totales de la moneda base
     &nDeci_PreBase  = 0;   || Decimales en el Precio en moneda base
     &nDeci_BaseMx   = 0;   || Decimales en Totales de la moneda base (Maxima precision)
     &nDeci_PreBaseMx= 0;   || Decimales en el Precio en moneda base (Maxima precision)
     &nDeci_Div      = 0;   || Decimales en Totales de la divisa
     &nDeci_PreDiv   = 0;   || Decimales en el Precio en divisa
     &nDeci_TotVis   = 0;   || Decimales en Totales visibles (el de la moneda de trabajo)
     &nDeci_TotNoVis = 0;   || Decimales en Totales no visibles (el de la otra moneda)
     &nDeci_PreVis   = 0;   || Decimales en el Precio visual. (lineas)
     &nDeci_ImpVis   = 0;   || Decimales en el Importe visual. (lineas)
     nfecha    = 0;
     &ac_divisa = "N";   || Vale "S" cuando las divisas en ventas estan
                         || activadas y la serie es de divisas
     &pintado = 0;       || Sirve para controlar los pintados de las lineas
     &decim_divisa = 0;  || Decimales de la divisa
     &precio_divisa = 0; || Decimales del precio en divisa
     &decim_base = 0;    || Decimales de la moneda base
     &precio_base = 0;   || Decimales del precio en moneda base
|FIN;

|PROCESO Param_Divisas;
     |ABRE #agifa324;
     #agifa324#0 = #malpe090#68;
     |LEE 001#agifa324.=;   || Leo la divisa
     |CIERRA #agifa324;

     decim_divisa = #agifa324#7;  || Decimales de la divisa
     precio_divisa = #agifa324#9;  || Decimales en el precio de la divisa
     nDeci_Div = decim_divisa;   || Decimales de la divisa
     nDeci_PreDiv = precio_divisa;  || Decimales en el precio de la divisa
     |SI #malpe090#70 = "B";   || Si trabajo con la moneda base ...
          nDeci_PreVis   = precio_divisa;
          nDeci_ImpVis   = decim_divisa;
          nDeci_Base     = decim_base;
          nDeci_PreBase  = precio_base;
          nDeci_BaseMx  = decim_base;
          nDeci_PreBaseMx = precio_base;
          nDeci_TotVis   = decim_base;
          nDeci_TotNoVis = decim_divisa;
     |SINO;     || Si trabajo con la divisa ...
          nDeci_PreVis   = precio_base;
          nDeci_ImpVis   = decim_base;
          nDeci_Base     = decim_base; || Max. precision en los decimales de la moneda base
          nDeci_PreBase  = precio_base;  || Max. precision en los dec. del precio de la moneda base
          |SI #malpe090#69 = 1;
               nDeci_BaseMx    = decim_base;    || sin triangulacion
               nDeci_PreBaseMx = precio_base;
          |SINO;
               nDeci_BaseMx    = #agifa321#10;  || con triangulacion
               nDeci_PreBaseMx = #agifa321#11;
          |FINSI;
          nDeci_TotVis   = decim_divisa;
          nDeci_TotNoVis = decim_base;
     |FINSI;
     |HAZ Deci_IVA;
|FPRC;

|PROCESO LeeMonBase; ||Lee los parametros de la moneda base
     |ABRE #agifa321;
     |ABRE #agifa324;
     |LEE 010#agifa321.p;   ||Leo parametros de divisa
     #malpe090#73 = #agifa321#9;  ||Cojo la Moneda Base de parametros
     #agifa324#0 = #agifa321#9;
     |LEE 010#agifa324.=;         ||Leo la ficha de la moneda base en Divisas
     #malpe090#74 = #agifa324#2;  ||Asigno el cambio de la moneda base con el euro
     decim_base = #agifa324#7;    ||Asigno los decimales de la moneda base
     precio_base = #agifa324#9;   ||Asigno los decimales del precio en la mon. base
     |CIERRA #agifa324;
     |CIERRA #agifa321;
|FPRC;

|PROCESO DivisaAlb;
     |HAZ LeeMonBase;
     |HAZ Param_Divisas;
|FPRC;
