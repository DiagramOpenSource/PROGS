|FICHEROS;
     diay0003 #0;

     agifa025 #25;
     diam0010 #10;
     diam0011 #11;
     diam0012 #12;

     :/dspartes/def/dsparm01 #101;
     :/dspartes/def/dsparm05 #105;
|FIN;

|VARIABLES;
     nCampo       = 0;
     nCampo1      = 0;
     nAnyo        = 0;
     nMes         = 0;
     nLinea       = 0;
     nSwHay       = 0;
     nSwEsta      = 0;
     nDAnyo       = 0;
     nHAnyo       = 0;
     nError       = 0;

     aTecnico     = "";
     aBase        = "";
     aPathInforme = "";
     aPathPdf     = "";
     aHora        = "";
     aNombreDoc   = "";
     aImpresora   = "";
     aPdf         = "";
     aSystem      = "";
     aBaseBin     = "";
     aOrigen      = "";
     aDestino     = "";
     aRuta        = "";
     aAlfa        = "";
     aUsuario     = "";
     aTo          = "";
     aFrom        = "";
     aSubject     = "";
     aServidor    = "";
     aIP          = "";
     aMensaje     = "";
     aAdjunto     = "";

     fFecha       = @;
|FIN;

|| __________________________ Calculos de las Pantallas _____________________

|PROCESO Browse;  |TIPO 0;
     |SI FSalida = 10;
         aRuta = #0#18;  |QBF aRuta;
         aRuta = "D" + aRuta;

         |BROWSE aRuta;

         aRuta = aRuta % 299;  |QBF aRuta;
         #0#18 = aRuta + "\";
     |FINSI;

     aRuta = #0#18;
     |QBT aRuta;
     |SI aRuta = "";
         |MENAV "0000 Necesito una ubicacion.";
         |ERROR;
     |FINSI;

     |PINTA #0#18;
|FPRC;

|PROCESO SacaBrowse;  |TIPO 7;
     aRuta = #0#18;  |QBF aRuta;
     |SI aRuta = "";
         FSalida = 10;
         |HAZ Browse;
     |FINSI;
|FPRC;

|PROCESO Representante;
     |C_M #0#01, 1, "S";
     |C_M #0#02, 1, "S";
     |C_M #0#03, 1, "S";
     |C_M #0#04, 1, "S";
     |C_M #0#05, 1, "S";
     |C_M #0#06, 1, "S";
     |C_M #0#07, 1, "S";
     |C_M #0#08, 1, "S";
     |C_M #0#09, 1, "S";
     |C_M #0#10, 1, "S";
     |C_M #0#11, 1, "S";
     |C_M #0#12, 1, "S";

     |SI #0#0  = "T";
         #0#01 = "!!";  |PINTA #0#01;  |C_M #0#01, 1, "N";
         #0#02 = "!!";  |PINTA #0#02;  |C_M #0#02, 1, "N";
         #0#03 = "!!";  |PINTA #0#03;  |C_M #0#03, 1, "N";
         #0#04 = "!!";  |PINTA #0#04;  |C_M #0#04, 1, "N";
         #0#05 = "!!";  |PINTA #0#05;  |C_M #0#05, 1, "N";
         #0#06 = "!!";  |PINTA #0#06;  |C_M #0#06, 1, "N";
         #0#07 = "!!";  |PINTA #0#07;  |C_M #0#07, 1, "N";
         #0#08 = "!!";  |PINTA #0#08;  |C_M #0#08, 1, "N";
         #0#09 = "!!";  |PINTA #0#09;  |C_M #0#09, 1, "N";
         #0#10 = "!!";  |PINTA #0#10;  |C_M #0#10, 1, "N";
         #0#11 = "  ";  |PINTA #0#11;  |C_M #0#11, 1, "N";
         #0#12 = "zz";  |PINTA #0#12;  |C_M #0#12, 1, "N";
     |FINSI;

     |SI #0#0  = "A";
         #0#11 = "  ";  |PINTA #0#11;  |C_M #0#11, 1, "N";
         #0#12 = "zz";  |PINTA #0#12;  |C_M #0#12, 1, "N";
     |FINSI;

     |SI #0#0  = "D";
         #0#01 = "!!";  |PINTA #0#01;  |C_M #0#01, 1, "N";
         #0#02 = "!!";  |PINTA #0#02;  |C_M #0#02, 1, "N";
         #0#03 = "!!";  |PINTA #0#03;  |C_M #0#03, 1, "N";
         #0#04 = "!!";  |PINTA #0#04;  |C_M #0#04, 1, "N";
         #0#05 = "!!";  |PINTA #0#05;  |C_M #0#05, 1, "N";
         #0#06 = "!!";  |PINTA #0#06;  |C_M #0#06, 1, "N";
         #0#07 = "!!";  |PINTA #0#07;  |C_M #0#07, 1, "N";
         #0#08 = "!!";  |PINTA #0#08;  |C_M #0#08, 1, "N";
         #0#09 = "!!";  |PINTA #0#09;  |C_M #0#09, 1, "N";
         #0#10 = "!!";  |PINTA #0#10;  |C_M #0#10, 1, "N";
     |FINSI;
|FPRC;

|PROCESO ConsuRepresentante;  |TIPO 66;
     |ABRE #25;

     #25#0 = #0Campo;
     |LEE 040#25=;
     |SI FSalida ! 0;  |LEE 040#25p; |FINSI;

     |CONSULTA_CLAVE #1#25;
     |SI FSalida = 0;
         #0Campo = #25#0;  |PINTA #0Campo;
     |FINSI;
     |CIERRA #25;
|FPRC;

|PROCESO Mes;
     nCampo1 = Campo + 1;

     |SI #0Campo = 0;   #0nCampo1 = "TODOS     ";  |FINSI;
     |SI #0Campo = 1;   #0nCampo1 = "ENERO     ";  |FINSI;
     |SI #0Campo = 2;   #0nCampo1 = "FEBRERO   ";  |FINSI;
     |SI #0Campo = 3;   #0nCampo1 = "MARZO     ";  |FINSI;
     |SI #0Campo = 4;   #0nCampo1 = "ABRIL     ";  |FINSI;
     |SI #0Campo = 5;   #0nCampo1 = "MAYO      ";  |FINSI;
     |SI #0Campo = 6;   #0nCampo1 = "JUNIO     ";  |FINSI;
     |SI #0Campo = 7;   #0nCampo1 = "JULIO     ";  |FINSI;
     |SI #0Campo = 8;   #0nCampo1 = "AGOSTO    ";  |FINSI;
     |SI #0Campo = 9;   #0nCampo1 = "SEPTIEMBRE";  |FINSI;
     |SI #0Campo = 10;  #0nCampo1 = "OCTUBRE   ";  |FINSI;
     |SI #0Campo = 11;  #0nCampo1 = "NOVIEMBRE ";  |FINSI;
     |SI #0Campo = 12;  #0nCampo1 = "DICIEMBRE ";  |FINSI;

     |PINTA #0nCampo1;
|FPRC;

|| __________________________ Calculos del Informe  __________________________

|PROCESO AbreImpresora;
     |DBASE aBase;   |QBF aBase;
     aPathInforme = aBase + "000577/pdf";
     |MKDIR aPathInforme;

     |HORA aHora
     fFecha = ~;

     aNombreDoc = #25#0 + (fFecha % -104) + (fFecha % 402) + (fFecha % 102) + ".pdf";
     aImpresora = aPathInforme + "/comision.txt";
     aPdf       = aPathInforme + "/" + aNombreDoc;

     nError    = 0;
     Impresora = aImpresora;
     |IMPRE -77;
     |SI FSalida ! 0;  nError = 1;  |FINSI;
|FPRC;

|PROCESO diam0011;
     |PRINT;
|FPRC;

|DEFBUCLE diam0011;
     #11#1;
     ;
     #10#0, #10#1, #10#2, " ", "     ", 000000;
     #10#0, #10#1, #10#2, "z", "zzzzz", 999999;
     ;
     NULL, diam0011;
|FIN;

|PROCESO diam0010;
     |SI #10#0 = #0#13;
         |SI #10#1 < #0#14;  |ACABA;  |FINSI;
     |FINSI;

     |SI #0#16 ! 0;
         |SI #10#1 > #0#16;  |ACABA;  |FINSI;
     |FINSI;

     |BUCLE diam0011;

     nSwHay = 1;
|FPRC;

|DEFBUCLE diam0010;
     #10#2;
     ;
     #25#0, nDAnyo, 01;
     #25#0, nHAnyo, 99;
     ;
     NULL, diam0010;
|FIN;

|PROCESO EnviaCorreo;
     |ABRE #101; |LEE 000#101p; |CIERRA #101;

     |ABRE #105;

     aUsuario = Usuario % 810;
     |SELECT #3#105;
     #105#2 = aUsuario;
     |LEE 040#105=;
     |SI FSalida ! 0;
         |CIERRA #105;
         aAlfa = "0000 No puede enviar Correo al Comisionista, el Usuario del Informe esta mal configurado.";
         |MENAV aAlfa;
         |ACABA;
     |FINSI;

     aTo = #105#3;       |QBF aTo;

     |SELECT #4#105;
     #105#6 = #25#0;
     |LEE 040#105=;
     |SI FSalida ! 0;
         |CIERRA #105;
         aAlfa = "0000 No puede enviar Correo al Comisionista " + #25#0 + " no existe en TECNICOS.";
         |MENAV aAlfa;
         |ACABA;
     |FINSI;
     |CIERRA #105;

     aFrom = #105#3;  |QBF aFrom;

     |SI aTo = "";
         aAlfa = "0000 No puede enviar Correo, la Cuenta DE no esta configurada";
         |MENAV aAlfa;
         |ACABA;
     |FINSI;

     |SI aFrom = "";
         aAlfa = "0000 No puede enviar Correo, la Cuenta PARA no esta configurada";
         |MENAV aAlfa;
         |ACABA;
     |FINSI;

     aSubject = "Informe de Comisiones";
     aServidor = #101#7; |QBF aServidor;
     aIP       = #101#8;       |QBF aIP;
     aMensaje  = "Leer fichero adjunto.";
     aAdjunto  = aOrigen;

     |DSCORREO_ENVIA aIP, aServidor, aFrom, aTo, aSubject, aMensaje, aAdjunto;
     |SI FSalida ! 0;
         aAlfa = "0000Ha habido un error durante el envio del Comisionista " + #25#0;
         |MENAV aAlfa;
     |FINSI;
|FPRC;

|PROCESO agifa025;
     #12#0 = #25#0;
     |LEE 040#12=;
     |SI FSalida ! 0;  |DDEFECTO #12;  |FINSI;

     nSwHay = 0;

     |SI #0#16 = 0;
         nDAnyo = #0#13;
         nHAnyo = 2999;
     |SINO;
         nDAnyo = #0#13;
         nHAnyo = #0#13;
     |FINSI;

     |HAZ AbreImpresora;
     |SI nError = 1;  |ERROR10;  |ACABA;  |FINSI;

     |INFOR "diay0003";
     |SI FSalida ! 0;  |FINIMP;  |ACABA;  |FINSI;

     |BUCLE diam0010;

     |SI nSwHay = 1;
         |PIEINF;
         |FININF;
         |FINIMP;

         aSystem = aBaseBin + aImpresora + " Horizontal";

         |QUE_SISTEMA aAlfa;
         |SI aAlfa = "ESDOS";
              aSystem = "d:\perl\bin\perl.exe d:\perl\bin\dsbin\ascii2pdf.pl -l -p6 " + aImpresora  + " >> d:\dsx\bin\log.log 2>> d:\dsx\bin\log.err";
         |FINSI;

         |SYSTEM aSystem;

         aOrigen  = aImpresora - ".txt" + ".pdf";
         aDestino = aPdf;
         |RENOMBRA_FICHERO aOrigen, aDestino;

         |FBORRA aImpresora;

         aOrigen  = aDestino;
         aDestino = aPathPdf + aNombreDoc;
         |ENVIA_FICHERO aOrigen, aDestino;

         |SI #0#20 = "S";  |HAZ EnviaCorreo;  |FINSI;

         |FBORRA aOrigen;
     |SINO;
         |FININF;
         |FINIMP;
     |FINSI;
|FPRC;

|DEFBUCLE agifa025;
     #25#1;
     ;
     #0#11;
     #0#12;
     ;
     NULL, agifa025;
|FIN;

|PROCESO Tipo3;  |TIPO 3;
     aRuta = #0#18;
     |QBT aRuta;
     |SI aRuta = "";
         |MENAV "0000 Necesito una ubicacion.";
         |ACABA;
     |FINSI;

     |DBASS aBaseBin;  |QBF aBaseBin;
     aBaseBin = aBaseBin + "bin/topdf.sh ";

     aPathPdf = #0#18;  |QBF aPathPdf;

     |ABRE #12;
     |SI #0#0 = "A";
         |ABRE #25;
         |PARA nCampo = 1;  |SI nCampo [ 10;  |HACIENDO nCampo = nCampo + 1;
               |SI #0nCampo ! "!!";
                   #25#0 = #0nCampo;
                   |LEE 000#25=;
                   |SI FSalida = 0;
                       |HAZ agifa025;
                   |FINSI;
               |FINSI;
         |SIGUE;
         |CIERRA #25;
     |SINO;
         |BUCLE agifa025;
     |FINSI;
     |CIERRA #12;
|FPRC;

|PROGRAMA;
    |CLS;
    |CABEZA "Informe Comisiones";

    |ABRE #0;
    |DDEFECTO #0;
    #0#19 = Usuario % 810;
    |LEE 101#0=;
    |SI FSalida ! 0;
        |GRABA 020#0b;
    |FINSI;

    |PDEFECTO #0, 1, 18;
    |PINPA #0#0;
    |PINDA #0#0;
    |ENDATOS #1#0;
    |SI FSalida = 0;
        |HAZ Tipo3;
        |GRABA 020#0a;
    |FINSI;
    |LIBERA #0;
    |CIERRA #0;
|FPRO;
