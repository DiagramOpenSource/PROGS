|FICHEROS;
     agifa017 #17;
     agifa019 #19;
     agifa142 #142;
|FIN;

|CAMPOS;
     #19#15  art_pser; #17#42  alm_pser;
     #19#16  art_prec; #17#43  alm_prec;
     #19#17  art_pval; #17#4   alm_pval;
     #19#11  art_nece; #17#50  alm_nece;
     #19#12  art_rese; #17#8   alm_rese;
     #19#14  art_depo; #17#10  alm_depo;
     #19#13  art_fabr; #17#9   alm_fabr;
     #19#10  art_vsto; #17#3   alm_vsto;
     #19#7   art_mini; #17#6   alm_mini;
     #19#8   art_maxi; #17#7   alm_maxi;
     #19#6   art_tota; #17#5   alm_tota;
     #19#104 art_disp; #17#39  alm_disp;
     #19#9   art_pcm;
|FIN;

|VARIABLES;
     nNume         = 0;
     nCampo        = 0;
     nForma        = 0;
     nNume1        = 0;
     nNume2        = 0;
     nNume3        = 0;
     nNume4        = 0;
     nNume5        = 0;
     nNume6        = 0;
     nPvp1         = 0;
     nPvp2         = 0;
     nPvp3         = 0;
     nPvp4         = 0;
     nPvp5         = 0;
     nPvp6         = 0;
     nPrecio       = 0;
     nPcm          = 0;
     nPm           = 0;

     &nDeci_PreBase = 0;
     aAlfa         = "";
|FIN;

|INCLUYE i_varart;

|PROCESO AbreFicheros;
     |ABRE #142;
     |LEE 040#142p;
     |SI FSalida ! 0;  |DDEFECTO #142;  |FINSI;
     |CIERRA #142;

     |ABRE #17;
     |ABRE #19;
|FPRC;

|PROCESO CierraFicheros;
     |CIERRA #17;
     |CIERRA #19;
|FPRC;

|PROCESO LeArticulo;
     #19#0 = eaArticulo;
     |LEE 101#19=;
     |SI FSalida ! 0;
         |DDEFECTO #19;
         #19#0 = eaArticulo;
         |GRABA 020#19b;
     |FINSI;
|FPRC;

|PROCESO LeeAlmacen;
      #17#0 = eaArticulo;
      #17#1 = enAlmacen;
      |LEE 101#17=;
      |SI FSalida ! 0;
          |DDEFECTO #17;
          #17#0 = eaArticulo;
          #17#1 = enAlmacen;
          |GRABA 020#17b;
      |FINSI;
|FPRC;

|PROCESO GrabaArticulo;
      |GRABA 020#19a;
      |LIBERA #19;
|FPRC;

|PROCESO GrabaAlmacen;
      |GRABA 020#17a;
      |LIBERA #17;
|FPRC;

 ---------------------------------------------------------------------------
                        PEDIDOS VENTAS
 ---------------------------------------------------------------------------

|PROCESO PedidoVenta;
     art_pser = art_pser + enUnidades;
     alm_pser = alm_pser + enUnidades;
     |SI eaReservado = "S";
          art_rese = art_rese + enUnidades;
          art_disp = art_disp - enUnidades;
          alm_rese = alm_rese + enUnidades;
          alm_disp = alm_disp - enUnidades;
     |FINSI;

     |HAZ ValoraStock;
|FPRC;

 ---------------------------------------------------------------------------
                        DEPOSITOS
 ---------------------------------------------------------------------------

|PROCESO Deposito;
     art_disp = art_disp - enUnidades;
     art_depo = art_depo + enUnidades;
     alm_disp = alm_disp - enUnidades;
     alm_depo = alm_depo + enUnidades;

     |HAZ ValoraStock;
|FPRC;

 ---------------------------------------------------------------------------
                        ALBARANES VENTAS
 ---------------------------------------------------------------------------

|PROCESO AlbaranVenta;
     |SI eaDeposito = "S";
         art_depo = art_depo - enUnidades;
         alm_depo = alm_depo - enUnidades;
         |SI eaAbono ! "S";               || caso albaran
              art_tota = art_tota - enUnidades;
              alm_tota = alm_tota - enUnidades;
         |SINO;                        || caso abono
              art_tota = art_tota + enUnidades;
              alm_tota = alm_tota + enUnidades;
         |FINSI;
     |SINO;
         |SI eaAbono ! "S";
              art_disp = art_disp - enUnidades;
              art_tota = art_tota - enUnidades;
              alm_disp = alm_disp - enUnidades;
              alm_tota = alm_tota - enUnidades;
         |SINO;
              art_disp = art_disp + enUnidades;
              art_tota = art_tota + enUnidades;
              alm_disp = alm_disp + enUnidades;
              alm_tota = alm_tota + enUnidades;
         |FINSI;
     |FINSI;

     |HAZ ValoraStock;

     aAlfa = efFecha % 402;
     nNume = aAlfa;
     nNume = ((nNume - 1) * 5) + 34;

     enCoste = enUnidades * art_pcm;

     |SI eaAbono ! "S";                    || caso albaran
          #19nNume = #19nNume + enUnidades;
          #19#94   = #19#94   + enUnidades;
          nNume    = aAlfa;
          nNume    = ((nNume - 1) * 2) + 11;
          #17nNume = #17nNume + enUnidades;
          #17#35   = #17#35   + enUnidades;
          enUniVta = enUnidades;
          enUniSal = enUnidades;
     |SINO;                             || caso abono
          #19nNume = #19nNume - enUnidades;
          #19#94   = #19#94 - enUnidades;
          nNume    = aAlfa;
          nNume    = ((nNume  - 1) * 2) + 11;
          #17nNume = #17nNume - enUnidades;
          #17#35   = #17#35   - enUnidades;
          enUniVta = -enUnidades;
          enUniSal = -enUnidades;
     |FINSI;

     |HAZ GrabaArticulo; |HAZ GrabaAlmacen;
     ||efFecha = ;
     eaArticulo = #17#0;
     enAlmacen = #17#1;
     |HAZ AcumulaArt;
     |HAZ AcumulaAlm;
|FPRC;

 ---------------------------------------------------------------------------
                        ADJUDICACION DE ALBARANES
 ---------------------------------------------------------------------------

|PROCESO AlbaranPedidoV;
     art_pser = art_pser - enUnidPend;
     alm_pser = alm_pser - enUnidPend;
     |SI eaReservado = "S";
         art_disp = art_disp + enUnidPend;
         art_rese = art_rese - enUnidPend;

         alm_disp = alm_disp + enUnidPend;
         alm_rese = alm_rese - enUnidPend;
     |FINSI;

     art_disp = art_disp - enUnidades;
     alm_disp = alm_disp - enUnidades;
     art_tota = art_tota - enUnidades;
     alm_tota = alm_tota - enUnidades;

     |HAZ ValoraStock;

     aAlfa = efFecha % 402;
     nNume = aAlfa;
     nNume = ((nNume - 1) * 5) + 34;

     enCoste = enUnidades * art_pcm;

     |SI eaAbono ! "S";                    || caso albaran
          #19nNume = #19nNume + enUnidades;
          #19#94   = #19#94   + enUnidades;
          nNume    = aAlfa;
          nNume    = ((nNume - 1) * 2) + 11;
          #17nNume = #17nNume + enUnidades;
          #17#35   = #17#35   + enUnidades;
          enUniVta = enUnidades;
          enUniSal = enUnidades;
     |SINO;                             || caso abono
          #19nNume = #19nNume - enUnidades;
          #19#94   = #19#94   - enUnidades;
          nNume    = aAlfa;
          nNume    = ((nNume  - 1) * 2) + 11;
          #17nNume = #17nNume - enUnidades;
          #17#35   = #17#35   - enUnidades;
          enUniVta = -enUnidades;
          enUniSal = -enUnidades;
     |FINSI;
     |HAZ GrabaArticulo; |HAZ GrabaAlmacen;
     eaArticulo = #17#0;
     enAlmacen = #17#1;
     ||efFecha =
     |HAZ AcumulaArt;
     |HAZ AcumulaAlm;
|FPRC;
 ---------------------------------------------------------------------------
                        TPV
 ---------------------------------------------------------------------------

|PROCESO Tpv;
     art_disp = art_disp - enUnidades;
     art_tota = art_tota - enUnidades;
     alm_disp = alm_disp - enUnidades;
     alm_tota = alm_tota - enUnidades;

     |HAZ ValoraStock;

     enCoste = enUnidades * art_pcm;

     aAlfa = efFecha % 402;
     nNume = aAlfa;
     nNume = ((nNume - 1) * 5) + 34;

      #19nNume = #19nNume + enUnidades;
      #19#94   = #19#94   + enUnidades;
      nNume    = aAlfa;
      nNume    = ((nNume - 1) * 2) + 11;
      #17nNume = #17nNume + enUnidades;
      #17#35   = #17#35   + enUnidades;

      #19#24   = efFecha;
      #19#25   = #19#25 + enUnidades;

     |HAZ GrabaArticulo; |HAZ GrabaAlmacen;
     enUniVta = enUnidades;
     enUniSal = enUnidades;
     eaArticulo = #17#0;
     enAlmacen = #17#1;
     ||efFecha =
     |HAZ AcumulaArt;
     |HAZ AcumulaAlm;
|FPRC;

 ---------------------------------------------------------------------------
                        FACTURAS CONTADO
 ---------------------------------------------------------------------------

|PROCESO FacturasContado;
     art_disp = art_disp - enUnidades;
     art_tota = art_tota - enUnidades;
     alm_disp = alm_disp - enUnidades;
     alm_tota = alm_tota - enUnidades;

     |HAZ ValoraStock;

     aAlfa = efFecha % 402;
     nNume = aAlfa;
     nNume = ((nNume - 1) * 5) + 34;

     enCoste = enUnidades * art_pcm;

      #19nNume = #19nNume + enUnidades;
      #19#94   = #19#94   + enUnidades;
      nNume    = aAlfa;
      nNume    = ((nNume - 1) * 2) + 11;
      #17nNume = #17nNume + enUnidades;
      #17#35   = #17#35   + enUnidades;

      #19#24   = efFecha;
      #19#25   = #19#25 + enUnidades;

     enUniVta = enUnidades;
     enUniSal = enUnidades;
     |HAZ GrabaArticulo; |HAZ GrabaAlmacen;
     eaArticulo = #17#0;
     enAlmacen = #17#1;
     ||efFecha =
     |HAZ AcumulaArt;
     |HAZ AcumulaAlm;
|FPRC;

 ---------------------------------------------------------------------------
                        PEDIDOS COMPRAS
 ---------------------------------------------------------------------------

|PROCESO PedidoCompra;
     art_prec = art_prec + enUnidades;
     alm_prec = alm_prec + enUnidades;

     |HAZ ValoraStock;
|FPRC;

 ---------------------------------------------------------------------------
                        ALBARAN COMPRA
 ---------------------------------------------------------------------------
|PROCESO AlbaCompraCab;
     #19#27 = eaProve;                  || ultimo proveedor
     #19#29 = enDto1;                 || ultimo descuento de compra
     #19#129 = enDto2;                 || ultimo descuento de compra
     #19#209 = enDto3;
     |SI enPrecio ! 0;
          #19#28  = enPrecio;          || ultimo precio de compra
          #19#163 = enPrecioD;         || U.P.C. en Divisa;
          #19#165 = eaDivisa;          || Divisa de U.P.C;
          #19#133 = (((((#19#28 < #19#29) < #19#129) < #19#209) < enDtoEs) < enDtoAc);
          #19#164 = ((((#19#163 < #19#29) < #19#129) < #19#209) < enDtoEs) < enDtoAc);
          |SI #142#114 = "S";
               #19#133 = #19#133 < enDtoPP;
               #19#164 = #19#164 < enDtoPP;
          |FINSI;
     |FINSI;
     #19#26   = efFecha;                || ultima fecha de compra
     aAlfa = efFecha % 402;
     nNume = aAlfa;
     nNume = ((nNume - 1) * 5) + 33;
     #19nNume = #19nNume + (enNeto * nForma);
     #19#93   = #19#93   + (enNeto * nForma);


     |HAZ GrabaArticulo;|HAZ GrabaAlmacen;
     enImpCom = enNeto * nForma;
     eaArticulo = #17#0;
     enAlmacen = #17#1;
     ||efFecha =
     |HAZ AcumulaArt;
     |HAZ AcumulaAlm;
|FPRC;

|PROCESO AlbaranCompra;
     |SI eaAbono = "S";
          art_prec = art_prec -. enUnidades;
     |FINSI;

     aAlfa = efFecha % 402;
     nNume = aAlfa;
     nNume = ((nNume - 1) * 5) + 32;

     |SI eaAbono = "N";
          #19nNume = #19nNume + enUnidades;   || unidades compradas
          #19#92   = #19#92   + enUnidades;    || total unidades

          aAlfa    = efFecha % 402;
          nNume    = aAlfa;
          nNume    = ((nNume - 1) * 2) + 12;
          #17nNume = #17nNume + enUnidades;
          #17#36   = #17#36   + enUnidades;

          art_disp = art_disp + enUnidades;
          alm_disp = alm_disp + enUnidades;
     |SINO;
          #19nNume = #19nNume - enUnidades;
          #19#92   = #19#92   - enUnidades;

          aAlfa    = efFecha % 402;
          nNume    = aAlfa;
          nNume    = ((nNume - 1) * 2) + 12;
          #17nNume = #17nNume - enUnidades;
          #17#36   = #17#36   - enUnidades;

          art_disp = art_disp - enUnidades;
          alm_disp = alm_disp - enUnidades;
     |FINSI;

     |SI eaAbono = "N";
          enUniCom = enUnidades;
          enUniEnt = enUnidades;
     |SINO;
          enUniCom = -enUnidades;
          enUniEnt = -enUnidades;
     |FINSI;

     |HAZ GrabaArticulo; |HAZ GrabaAlmacen;
     eaArticulo = #17#0;
     enAlmacen = #17#1;
     ||efFecha =
     |HAZ AcumulaArt;
     |HAZ AcumulaAlm;

     |HAZ ValoraStock;
|FPRC;

 ---------------------------------------------------------------------------
                        ADJUDICACION DE ALBARANES COMPRAS
 ---------------------------------------------------------------------------

|PROCESO AlbaranPedidoC;
     art_prec = art_prec - enUnidPend;
     alm_prec = alm_prec - enUnidPend;

     aAlfa = efFecha % 402;
     nNume = aAlfa;
     nNume = ((nNume - 1) * 5) + 32;

     #19nNume = #19nNume + enUnidades;   || unidades compradas

     #19#92   = #19#92   + enUnidades;    || total unidades

     aAlfa    = efFecha % 402;
     nNume    = aAlfa;
     nNume    = ((nNume - 1) * 2) + 12;
     #17nNume = #17nNume + enUnidades;
     #17#36   = #17#36   + enUnidades;

     art_disp = art_disp + enUnidades;
     alm_disp = alm_disp + enUnidades;

     |HAZ GrabaArticulo; |HAZ GrabaAlmacen;
     enUniCom = enUnidades;
     enUniEnt = enUnidades;
     eaArticulo = #17#0;
     enAlmacen = #17#1;
     |HAZ AcumulaArt;
     |HAZ AcumulaAlm;

     |HAZ ValoraStock;
|FPRC;

 ---------------------------------------------------------------------------
                        ENTRADA STOCK
 ---------------------------------------------------------------------------
|PROCESO EntradaStock;
     aAlfa = efFecha % 402;
     nNume = aAlfa;
     nNume = ((nNume - 1) * 2) + 12;
     art_disp  = art_disp  + enUnidades;
     art_tota  = art_tota  + enUnidades;
     alm_disp  = alm_disp  + enUnidades;
     alm_tota  = alm_tota  + enUnidades;

     #17#36 = #17#36 + enUnidades;
     #17nNume = #17nNume + enUnidades;

     |HAZ ValoraStock;

|FPRC;

 ---------------------------------------------------------------------------
                        PRECIO MEDIO COSTE
 ---------------------------------------------------------------------------
|PROCESO pvps2;
     |SI enUnidades [ 0; |ACABA; |FINSI;
     |SI #19#108 =  "N"; |ACABA;  |FINSI;

     |HAZ CalPre077;
|FPRC;

|PROCESO PrecioMedioCoste;
     enOrgPcm = #19#9;
     enOrgPcom = #19#28;
     enOrgPdto = #19#133;

     nForma = 1;
     |SI eaAbono = "S";  nForma = -1;  |FINSI;
     |HAZ ValoraStock;
     nForma   = nForma * enForma;

     nPm = 0;
     |SI #142#104 = "S"; nPm = (enPortes + enVarios) / enBruto; |FINSI;
     |SI nPm ! 0; nPm = (enNeto * nPm) + enNeto; |SINO; nPm = enNeto; |FINSI;

     || nPm = Coste de la linea
     nPcm = art_pcm;

     |SI enUnidades > 0; |Y nPm > 0;
         nPm = nPm / enUnidades;
         nNume = art_tota - art_pval;
         |SI nNume > 0;
             nPm  = nPm * enUnidades;
             nNume1  = art_vsto + (nPm * enForma);
             nPcm = nNume1 / (nNume + enUnidades * nForma);
         |SINO;
             nPcm = nPm;
         |FINSI;
     |FINSI;

     |SI nPcm ! 0;  |Y #142#30 ! "X";
         art_pcm = nPcm;
     |FINSI;

     art_tota = art_tota  + (enUnidades * nForma);
     alm_tota = alm_tota  + (enUnidades * nForma);

     |HAZ ValoraStock;

     |SI enForma = 1;  |HAZ pvps2;  |FINSI;
|FPRC;
