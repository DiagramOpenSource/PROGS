|FICHEROS;
     psioa071 #0;
     agifa007 #7;
     psioa001 #1;
     psioa018 #18;
     agifa041 #41;
     :/dsoporte/def/dsprem01 #301;
     :/dsoporte/def/dsprem02 #302;
     :/dsoporte/def/dsprem03 #303;
     :/dsoporte/def/dsprem04 #304;
     :/dsoporte/def/dsprem07 #307;
     :/dsoporte/def/dsprem08 #308;
     :/dsoporte/def/dsorm001 #309;

     referen@  #900;
|FIN;

|VARIABLES;
     &eaEmpresa = "";
     aPerfil = "";
     aClave = "";
     aBase = "";
     aAlfa = "";
     aParam = "";
     aLon = "";
     nPos = 0;
     aUsu = "";
     aEmp = "";
     nSw = 0;
     i = 0;
     aPathDSoporte = "";
     aPathDSoporteDef = "";
     aAlfa1 = "";
     aAlfa2 = "";
     aAlfa3 = "";
     aMas = "";
|FIN;

|PROCESO dsprem07;
     #303#0 = #302#0;
     #303#1 = #302#1;
     #303#2 = #302#2;
     #303#3 = #307#0;
     #303#4 = #307#1;
     |LEE 101#303=;
     |SI FSalida ! 0;
         |DDEFECTO #303;
         #303#0 = #302#0;
         #303#1 = #302#1;
         #303#2 = #302#2;
         #303#3 = #307#0;
         #303#4 = #307#1;
         |GRABA 020#303b;
     |FINSI;

     |SI #303#11 = 0;
         aMas = aPathDSoporteDef + "dsprem03.mas";
         |CARGA_DEF aMas, referen@;
         |SI FSalida = 0;
             |PATH_DAT #900#aPathDSoporte#;
             |ABRE #900;
             |SELECT #2#900;
             |LEE 040#900u;
             |SI FSalida ! 0;  |DDEFECTO #900;  |FINSI;
             |CIERRA #900;

             #303#11 = #900#11 + 1;
             |SI #303#11 = 8888;  #303#11 = #303#11 + 1;  |FINSI;

             |DESCARGA_DEF #referen@;
         |FINSI;
     |FINSI;

     #303#5  = "DEMO";
     #303#6  = #308#2;
     #303#7  = #302#3;
     #303#8  = #307#2;
     #303#9  = #307#3;
     aAlfa = #303#8; |QBF aAlfa;
     aAlfa = aAlfa + "-" + #303#9;
     #303#10 = #302#0 + (("00" + #302#1) % -102) + (("000" + #302#2) % -102) + " " + aAlfa;
     |GRABA 020#303a;
     |LIBERA #303;

     #309#0 = #303#11;
     |LEE 101#309=;
     |SI FSalida ! 0;
         |DDEFECTO #309;
         #309#0 = #303#11;
         |GRABA 020#309b;
     |FINSI;
     #309#1 = #303#10;
     |GRABA 020#309a;
     |LIBERA #309;
|FPRC;

|DEFBUCLE dsprem07;
     #307#1;
     ;
     00, 00;
     99, 99;
     ;
     NULL, dsprem07;
|FIN;

|PROCESO Usuario;
     |DBASS aBase;  |QBF aBase;
     aPathDSoporte = aBase + "dsoporte/fich/";
     aPathDSoporteDef = aBase + "dsoporte/def/";
     |PATH_DAT #301#aPathDSoporte#;
     |PATH_DAT #302#aPathDSoporte#;
     |PATH_DAT #303#aPathDSoporte#;
     |PATH_DAT #304#aPathDSoporte#;
     |PATH_DAT #307#aPathDSoporte#;
     |PATH_DAT #308#aPathDSoporte#;
     |PATH_DAT #309#aPathDSoporte#;

     aEmp = ("00000" + #0#1) % -105;
     aAlfa1 = aEmp % 101;
     aAlfa2 = aEmp % 202;
     aAlfa3 = aEmp % 402;

     |ABRE #301;
     #301#0 = aAlfa1;
     |LEE 000#301=;
     |SI FSalida ! 0;
          #301#1 = "DEMO";
          |GRABA 020#301n;
     |FINSI;
     |CIERRA #301;

     |ABRE #302;
     #302#0 = aAlfa1;
     #302#1 = aAlfa2;
     #302#2 = aAlfa3;
     |LEE 000#302=;
     |SI FSalida ! 0;
          #302#3 = "DEMO Ineed";
          #302#5 = "S";
          #302#12 = aEmp;
          #302#13 = "01";
          #302#14 = "001";
          #302#15 = aEmp;
          #302#16 = aEmp;
          #302#17 = aEmp;
          #302#18 = aEmp;
          #302#19 = aEmp;
          |GRABA 020#302n;
     |FINSI;
     |CIERRA #302;

     |ABRE #308;
     #308#0 = aAlfa1;
     #308#1 = aAlfa2;
     |LEE 000#308=;
     |SI FSalida ! 0;
          #308#2 = "DEMO Ineed";
          |GRABA 020#308n;
     |FINSI;
     |CIERRA #308;

     |ABRE #304;
     #304#0 = aAlfa1;
     #304#1 = aAlfa2;
     #304#2 = aAlfa3;
     #304#3 = 97;
     #304#4 = 01;
     #304#5 = #0#0;
     |LEE 000#304=;
     |SI FSalida ! 0;
          #304#7 = #0#3;
          |GRABA 020#304n;

          |ABRE #303;
          |ABRE #309;
          |BUCLE dsprem07;
          |CIERRA #303;
          |CIERRA #309;

          |ABRE #303;
          #303#0 = #304#0;
          #303#1 = #304#1;
          #303#2 = #304#2;
          #303#3 = #304#3;
          #303#4 = #304#4;
          |LEE 000#303=;
          |SI FSalida = 0;
               aPerfil = #303#11;
               aAlfa = "*:dsoporte/dsprez02;AUTOMATICO" + (("0000" + aPerfil) % -104);
               |SUB_EJECUTA aAlfa;
          |FINSI;
          |CIERRA #303;
     |SINO;
          |SI aParam = ""; |MENAV "0000Usuario existente..."; |FINSI;
     |FINSI;
     |CIERRA #304;
|FPRC;

|PROCESO Tipo3; |TIPO 3;
     |SI #0#2 = "SI";
          |DBASE aBase;
          aAlfa = aBase + "fich/";
          |PATH_DAT #41 aAlfa;

          eaEmpresa = ("00000" + #0#1) % -105;

          |ABRE #41;
          #41#0 = #0#1;
          |LEE 000#41=;
          |SI FSalida ! 0;
               #41#1 = #0#0;
               #41#8 = "          ";
               |GRABA 020#41n;

               aAlfa = aBase + "fich/" + eaEmpresa;
               |MKDIR aAlfa;
               |SUB_EJECUTA_NP "agifa041;algo";
          |SINO;
               |SI aParam = ""; |MENAV "0000Empresa existente..."; |FINSI;
          |FINSI;
          |CIERRA #41;

          aAlfa = aBase + "fich/" + eaEmpresa + "/";
          |PATH_DAT #1 aAlfa;
          |PATH_DAT #7 aAlfa;
          |PATH_DAT #18 aAlfa;
          |ABRE #1;
          #1#0 = #0#0;
          |LEE 000#1=;
          |SI FSalida ! 0;
               aAlfa = aBase + "ineed/exportaciones/" + #0#0; |QBF aAlfa;
               aAlfa = aAlfa + "/";
               |MKDIR aAlfa;
               #1#29 = aAlfa;
               #1#74 = "S";
               #1#58 = 30;
               #1#59 = 3;
               aAlfa = aBase + "ineed/importaciones/" + #0#0; |QBF aAlfa;
               aAlfa = aAlfa + "/";
               |MKDIR aAlfa;
               #1#75 = aAlfa;
               #1#76 = "S";
               #1#77 = #0#0;
               #1#78 = "/usr/dsbin/sync_ineed.sh";
               #1#79 = "S";
               #1#80 = "/usr/dsbin/sync_ineed_impo.sh";
               #1#81 = "S";
               |GRABA 020#1n;

               |ABRE #18;
               #18#0 = "A";
               |GRABA 020#18n;
               |CIERRA #18;

               |ABRE #7;
               #7#26 = "A";
               |GRABA 020#7n;
               |CIERRA #7;
          |SINO;
               |SI aParam = ""; |MENAV "0000Codigo exportaci¢n/importaci¢n existente..."; |FINSI;
          |FINSI;
          |CIERRA #1;

          |HAZ Usuario;
     |FINSI;
|FPRC;

|PROGRAMA;
     aParam = PARAMETRO; ||= Usuario,Empresa,Clave
     |SI aParam = "";
          |MANTE #0;
     |SINO;
          aLon = aParam % A0;
          |PARA i = 1; |SI i [ aLon; |HACIENDO i = i + 1;
               nPos = (i * 100) + 1;
               aAlfa = aParam % nPos;
               |SI aAlfa = ",";
                    nSw = nSw + 1;
               |SINO;
                    |SI nSw = 0; aUsu = aUsu + aAlfa; |FINSI;
                    |SI nSw = 1; aEmp = aEmp + aAlfa; |FINSI;
                    |SI nSw = 2; aClave = aClave + aAlfa; |FINSI;
               |FINSI;
          |SIGUE;
          #0#0 = aUsu;
          #0#1 = aEmp;
          #0#2 = "SI";
          #0#3 = aClave;
          |HAZ Tipo3;
     |FINSI;
|FPRO;
