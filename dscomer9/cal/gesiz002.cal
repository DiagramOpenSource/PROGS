|FICHEROS;
     gesiz002 #0;
     gesim002 #2;
     gesim008 #8;
     gesim009 #9;
     gesim018 #18;
     agifa019 #19;
     agifa024 #24;
     agifa025 #25;
     gesiz003 #103,MANTE;  ||Pan Cab
     gesiz004 #104,MANTE;  ||Pan Lin
     gesim008@;
     gesim009@;
|FIN;

|VARIABLES;
     &Tempo = "";
     &enCtrFin = 0;
     nRango = 0;
     aSer = "";
     nNum = 0;
     aAlfa = "";
     nPanta = 0;
     nPantaLin = 0;
     nModo = 0;
     nModoLin = 0;
     nGridCab = 0;
     nGridLin = 0;
     nBtnAltaLin = 0;
     nBtnModiLin = 0;
     nBtnBajaLin = 0;
     nBtnConsLin = 0;
     nBloquea = 0;
     aTmp9 = "";
     aPath = "";
     aConte = "";
     nSw = 0;
     nConta = 0;
     i = 0;
|FIN;

|PROCESO Conte; |TIPO 0;
     aConte = #103Campo;
|FPRC;

|PROCESO ConteLin; |TIPO 0;
     aConte = #104Campo;
|FPRC;

|PROCESO Importe; |TIPO 0;
     #104#8 = #104#6 * #104#7; |PINTA #104#8;
|FPRC;

|PROCESO Min2;
     #2#6 = #103#3;
|FPRC;

|PROCESO Max2
     #2#6 = #103#3;
|FPRC;

|PROCESO Matri66; |TIPO 66;
     |ABRE #2;
     #2#0 = #104#5;
     |LEE 000#2];
     |CONSULTA_F_CLAVE #4#2, Min2, Max2;
     |SI FSalida = 0;
          #104#5 = #2#0; |PINTA #104#5;
     |SINO;
          |ERROR;
     |FINSI;
     |CIERRA #2;
|FPRC;

|PROCESO Matri; |TIPO 0;
     |ABRE #2;
     #2#0 = #104#5;
     |LEE 000#2=;
     |SI FSalida = 0;
          |SI #2#6 ! #103#3;
               aAlfa = "0000La m quina pertenece al cliente:" + #2#6;
               |MENAV aAlfa;
               |ERROR;
          |SINO;
               |PINTA 747, #2#1;
          |FINSI;
     |SINO;
          |DDEFECTO #2;
          |PINTA 747, #2#1;
          |SI nSw = 0;
               |MENAV "0000No existe c¢digo de m quina...";
               |ERROR;
          |FINSI;
     |FINSI;
     |CIERRA #2;
|FPRC;

|PROCESO Arti; |TIPO 0;
     |ABRE #24;
     #24#0 = #103#3;
     |LEE 020#24=;
     |CIERRA #24;

     |ABRE #19;
     #19#0 = #104#3;
     |LEE 000#19=;
     |SI FSalida = 0;
          |SI nModoLin = 1; |O aConte ! #9Campo;
               #104#4 = #19#1; |PINTA #104#4;
               |SI #24#39 = 1; #104#7 = #19#18; |FINSI;
               |SI #24#39 = 2; #104#7 = #19#19; |FINSI;
               |SI #24#39 = 3; #104#7 = #19#20; |FINSI;
               |SI #24#39 = 4; #104#7 = #19#147; |FINSI;
               |SI #24#39 = 5; #104#7 = #19#148; |FINSI;
               |SI #24#39 = 6; #104#7 = #19#149; |FINSI;
               |HAZ Importe;
          |FINSI;
     |SINO;
          |SI nSw = 0;
               |MENAV "0000No existe c¢digo de art¡culo...";
               |ERROR;
          |FINSI;
     |FINSI;
     |CIERRA #19;
|FPRC;

|PROCESO EditaLin;
     |SI nModoLin = 1; |VENTANA 2301, 3198, -1, 17, "Nueva linea propuesta"; |FINSI;
     |SI nModoLin = 2; |VENTANA 2301, 3198, -1, 17, "Modifica linea propuesta"; |FINSI;
     |SI nModoLin = 4; |VENTANA 2301, 3198, -1, 17, "Consulta linea propuesta"; |FINSI;
     |HAZ PushVenModal;

     |SI nModoLin = 1;
          #gesim009@#0 = #103#0;
          #gesim009@#1 = #103#1;
          #gesim009@#2 = 999;
          |LEE 000#gesim009@.];
          |SI FSalida = 0;
               |LEE 000#gesim009@.a;
          |SINO;
               |LEE 000#gesim009@.u;
          |FINSI;
          |SI FSalida = 0; |Y #gesim009@#0 = #103#0; |Y #gesim009@#1 = #103#1;
               nConta = #gesim009@#2 + 1;
          |SINO;
               nConta = 1;
          |FINSI;
          |DDEFECTO #gesim009@;
          #gesim009@#0 = #103#0;
          #gesim009@#1 = #103#1;
          #gesim009@#2 = nConta;
          |GRABA 020#gesim009@.b;
     |SINO;
          #gesim009@#0 = #gesim009@#0;
          #gesim009@#1 = #gesim009@#1;
          #gesim009@#2 = #gesim009@#2;
          |SI nModoLin = 2;
               |LEE 121#gesim009@.=;
          |SINO;
               |LEE 020#gesim009@.=;
          |FINSI;
     |FINSI;
     |PARA i = 0; |SI i [ 8; |HACIENDO i = i + 1;
          #104i = #gesim009@#i#;
     |SIGUE;

     |PINPA #0#104;
     |PINDA #0#104;
     nSw = 1;
     |HAZ Arti;
     |HAZ Matri;
     nSw = 0;
     |SI nModoLin = 1; |ENDATOS #1#104; |FINSI;
     |SI nModoLin = 2; |ENDATOS #2#104; |FINSI;
     |SI nModoLin = 4; |PAUSA; |FINSI;
     |SI FSalida = 0; |Y nModoLin ! 4;
          #gesim009@#0 = #104#0;
          #gesim009@#1 = #104#1;
          #gesim009@#2 = #104#2;
          |LEE 121#gesim009@.=;
          |PARA i = 0; |SI i [ 8; |HACIENDO i = i + 1;
               #gesim009@#i# = #104i;
          |SIGUE;
          |GRABA 020#gesim009@.a;
          |REFRESCACONTROL nGridLin;
          |ESTADO_CONTROL nBtnModiLin, "ENABLE";
          |ESTADO_CONTROL nBtnBajaLin, "ENABLE";
          |ESTADO_CONTROL nBtnConsLin, "ENABLE";
     |SINO;
          |SI nBloquea = 1;
               #gesim009@#0 = #104#0;
               #gesim009@#1 = #104#1;
               #gesim009@#1 = #104#2;
               |BORRA 020#gesim009@.c;
          |FINSI;
     |FINSI;

     |HAZ PopVenModal;
|FPRC;

|PROCESO Repre; |TIPO 0;
     |ABRE #25;
     #25#0 = #103#4;
     |LEE 000#25=;
     |SI FSalida = 0;
          |C_A #25#1, 1, 60; |PINTA 822, #25#1;
     |SINO;
          |DDEFECTO #25;
          |PINTA 822, #25#1;
          |SI nSw = 0;
               |MENAV "0000No existe c¢digo de representante...";
               |ERROR;
          |FINSI;
     |FINSI;
     |CIERRA #25;
|FPRC;

|PROCESO Cliente; |TIPO 0;
     |ABRE #24;
     #24#0 = #103#3;
     |LEE 000#24=;
     |SI FSalida = 0;
          |C_A #24#1, 1, 60; |PINTA 726, #24#1;
          |SI nModo = 1; |O aConte ! Contenido;
               #103#4 = #24#25; |PINTA #103#4;
          |FINSI;
     |SINO;
          |DDEFECTO #24;
          |PINTA 726, #24#1;
          |SI nSw = 0;
               |MENAV "0000No existe c¢digo de cliente...";
               |ERROR;
          |FINSI;
     |FINSI;
     |CIERRA #24;
|FPRC;

|PROCESO Conta0; |TIPO 0;
     |SI nModo = 1; |Y nBloquea = 0;
          |DDEFECTO #8;
          #8#0 = #103#0;
          #8#1 = #103#1;
          |GRABA 000#8b;
          |SI FSalida = 0;
               nBloquea = 1;
               |C_M #103#0, 1, "N";
               |C_M #103#1, 1, "N";
               |ESTADO_CONTROL nBtnAltaLin, "ENABLE";
          |SINO;
               |MENAV "0000Exite el registro...";
               |ERROR;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Conta7; |TIPO 7;
     |SI nModo = 1; |Y nBloquea = 0;
          #8#0 = #103#0;
          #8#1 = 999999;
          |LEE 000#8];
          |SI FSalida = 0;
               |LEE 000#8a;
          |SINO;
               |LEE 000#8u;
          |FINSI;
          |SI FSalida = 0; |Y #8#0 = #103#0;
               nConta = #8#1 + 1;
          |SINO;
               nConta = 1;
          |FINSI;
          #103#1 = nConta;
     |FINSI;
|FPRC;

|PROCESO BtnAltaLin;
     nModoLin = 1;
     |HAZ EditaLin;
|FPRC;

|PROCESO BtnModiLin;
     |SELECT #1#gesim009@;
     |LEE 000#gesim009@.=;
     |SI FSalida ! 0; |ACABA; |FINSI;

     nModoLin = 2;
     |HAZ EditaLin;
|FPRC;

|PROCESO BtnConsLin;
     |SELECT #1#gesim009@;
     |LEE 000#gesim009.=;
     |SI FSalida ! 0; |ACABA; |FINSI;

     nModoLin = 4;
     |HAZ EditaLin;
|FPRC;

|PROCESO BtnBajaLin;
     |SELECT #1#gesim009@;
     |LEE 000#gesim009@.=;
     |SI FSalida ! 0; |ACABA; |FINSI;

     aAlfa = "0000N¿Borrar linea propuesta: " + #gesim009@#2 + "?";
     |CONFI aAlfa;
     |SI FSalida = 0;
          |BORRA 020#gesim009@.a;
          |REFRESCACONTROL nGridLin;
     |FINSI;
|FPRC;

|PROCESO EvtLin;
     |SI FSalida = 1; |O FSalida = 2;
          |SELECT #1#gesim009@;
          |LEE 000#gesim009@.=;
     |FINSI;
     |SI FSalida = 4;
          |SI nModo = 4;
               |HAZ BtnConsLin;
          |SINO;
               |HAZ BtnModiLin;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO BorraLin;
     |BORRA 020#9a;
|FPRC;

|DEFBUCLE BorraLin;
     #9#1;
     ;
     aSer, nNum, 001;
     aSer, nNum, 999;
     be;
     NULL, BorraLin;
|FIN;

|PROCESO LlenaLin;
     |SALVA_DATOS #9;
     |REPON_DATOS #gesim009@;
     |GRABA 020#gesim009@.n;
|FPRC;

|DEFBUCLE LlenaLin;
     #9#1;
     ;
     #8#0, #8#1, 001;
     #8#0, #8#1, 999;
     ;
     NULL, LlenaLin;
|FIN;

|PROCESO GrabaLin;
     |SALVA_DATOS #gesim009@;
     |REPON_DATOS #9;
     |GRABA 020#9n;
|FPRC;

|DEFBUCLE GrabaLin;
     #gesim009@#1003;
     ;
     #8#0, #8#1, 001;
     #8#0, #8#1, 999;
     ;
     NULL, GrabaLin;
|FIN;

|PROCESO Edita;
     |SI nModo = 1; |VENTANA 901, 3198, -1, 17, "Nueva propuesta"; |FINSI;
     |SI nModo = 2; |VENTANA 901, 3198, -1, 17, "Modifica propuesta"; |FINSI;
     |SI nModo = 4; |VENTANA 901, 3198, -1, 17, "Consulta propuesta"; |FINSI;
     |HAZ PushVenModal;

     |DELINDEX #gesim009@; |ABRE #gesim009@;

     |SI nModo = 1;
          |DDEFECTO #103;
          |C_M #103#0, 1, "S";
          |C_M #103#1, 1, "S";
     |SINO;
          #8#0 = #gesim008@#0;
          #8#1 = #gesim008@#1;
          |SI nModo = 2;
               |LEE 121#8=;
          |SINO;
               |LEE 020#8=;
          |FINSI;
          |PARA i = 0; |SI i [ 8; |HACIENDO i = i + 1;
               #103i = #8i;
          |SIGUE;
          |C_M #103#0, 1, "N";
          |C_M #103#1, 1, "N";
          |BUCLE LlenaLin;
          |LEE 000#gesim009@.p;
     |FINSI;

     |PINPA #0#103; nPantaLin = FSalida;
     |PINDA #0#103;
     nRango = 4 + 8 + 16 + 32;
     |LINEAL_SIMPLE #1#gesim009@, nRango, 1402, 2798 , NULL, NULL, EvtLin;
     nGridLin = FSalida;
     |CONTROL_SIMPLE nPantaLin, "BOTON,{{193}}Alta", 1302, 1312, BtnAltaLin;
     nBtnAltaLin = FSalida;
     |CONTROL_SIMPLE nPantaLin, "BOTON,{{197}}Modificar", 1314, 1324, BtnModiLin;
     nBtnModiLin = FSalida;
     |CONTROL_SIMPLE nPantaLin, "BOTON,{{199}}Baja", 1326, 1336, BtnBajaLin;
     nBtnBajaLin = FSalida;
     |CONTROL_SIMPLE nPantaLin, "BOTON,{{155}}Consulta", 1338, 1348, BtnConsLin;
     nBtnConsLin = FSalida;

     |SI nModo = 1; |O nModo = 4;
          |ESTADO_CONTROL nBtnAltaLin, "DISABLE";
          |ESTADO_CONTROL nBtnModiLin, "DISABLE";
          |ESTADO_CONTROL nBtnBajaLin, "DISABLE";
          |SI nModo = 1;
               |ESTADO_CONTROL nBtnConsLin, "DISABLE";
          |FINSI;
     |FINSI;

     nBloquea = 0;
     nSw = 1;
     |HAZ Cliente;
     |HAZ Repre;
     nSw = 0;
     |SI nModo = 1; |ENDATOS #1#103; |FINSI;
     |SI nModo = 2; |ENDATOS #2#103; |FINSI;
     |SI nModo = 4; |PAUSA; |FINSI;
     |SI FSalida = 0; |Y nModo ! 4;
          #8#0 = #103#0;
          #8#1 = #103#1;
          |LEE 121#8=;
          |PARA i = 0; |SI i [ 8; |HACIENDO i = i + 1;
               #8i = #103i;
          |SIGUE;
          |GRABA 020#8a;
          aSer = #8#0;
          nNum = #8#1;
          |BUCLE BorraLin;
          |BUCLE GrabaLin;
          |REFRESCACONTROL nGridCab;
     |SINO;
          |SI nBloquea = 1;
               #8#0 = #103#0;
               #8#1 = #103#1;
               |BORRA 020#8c;
               aSer = #8#0;
               nNum = #8#1;
               |BUCLE BorraLin;
          |FINSI;
     |FINSI;

     |CIERRA #gesim009@; |DELINDEX #gesim009@;

     |HAZ PopVenModal;
|FPRC;

|PROCESO BtnAltaCab;
     nModo = 1;
     |HAZ Edita;
|FPRC;

|PROCESO BtnModiCab;
     |SELECT #1#gesim008@;
     |LEE 100#gesim008@.=;  ||bloquea y sigue
     |SI FSalida ! 0; |ACABA; |FINSI;

     nModo = 2;
     |HAZ Edita;
     |LIBERA #gesim008@;
|FPRC;

|PROCESO BtnConsCab;
     |SELECT #1#gesim008@;
     |LEE 000#gesim008@.=;
     |SI FSalida ! 0; |ACABA; |FINSI;

     nModo = 4;
     |HAZ Edita;
|FPRC;

|PROCESO BtnBajaCab;
     |SELECT #1#gesim008@;
     |LEE 100#gesim008@.=;  ||bloquea y sigue
     |SI FSalida ! 0; |ACABA; |FINSI;

     aAlfa = "0000N¿Borrar propuesta: " + #gesim008@#0 + "/" + #gesim008@#1 + "?";
     |CONFI aAlfa;
     |SI FSalida = 0;
          |BORRA 020#gesim008@.a;
          aSer = #gesim008@#0;
          nNum = #gesim008@#1;
          |BUCLE BorraLin;
          |REFRESCACONTROL nGridCab;
     |FINSI;
|FPRC;

|PROCESO Imprime;
     |PRINT;
|FPRC;

|DEFBUCLE Imprime;
     #9#1;
     ;
     #8#0, #8#1, 001;
     #8#0, #8#1, 999;
     ;
     NULL, Imprime;
|FIN;

|PROCESO BtnImpr;
     |SELECT #1#gesim008@;
     |LEE 000#gesim008@.=;
     |SI FSalida ! 0; |ACABA; |FINSI;

     #8#0 = #gesim008@#0;
     #8#1 = #gesim008@#1;
     |LEE 021#8=;

     Impresora = "CrystalReport";
     |IMPRE -1;
     |SI FSalida = 0;
          |ABRE #18;
          #18#0 = #8#0;
          |LEE 000#18=;
          |SI FSalida ! 0; |DDEFECTO #18; |FINSI;
          |CIERRA #18;
          aAlfa = #18#3; |QBF aAlfa;
          |INFOR aAlfa;
          |SI FSalida = 0;
               |BUCLE Imprime;
               |PIEINF;
               |FININF;
          |SINO;
               aAlfa = "0000No existe informe:" + aAlfa;
               |MENAV aAlfa;
          |FINSI;
          |FINIMP;
     |FINSI;
|FPRC;

|PROCESO EvtCab;
     |SI FSalida = 1; |O FSalida = 2;
          |SELECT #1#gesim008@;
          |LEE 000#gesim008@.=;
     |FINSI;
     |SI FSalida = 4;
          |HAZ BtnModiCab;
     |FINSI;
|FPRC;

|PROGRAMA;
     |CLS;
     |PINPA #0#0; nPanta = FSalida;
     |DDEF aPath;
     aAlfa = aPath + "gesim008"; |CARGA_DEF aAlfa, gesim008@;
     aAlfa = aPath + "gesim009"; |CARGA_DEF aAlfa, gesim009@;
     |ABRET #8;
     |ABRET #9;
     |ABRET #gesim008@;
     |HAZ CreaTempo; aTmp9 = Tempo; |NOME_DAT #gesim009@#Tempo#;

     |CONTROL_SIMPLE nPanta, "BOTON,{{193}}Alta", 1002, 1012, BtnAltaCab;
     |CONTROL_SIMPLE nPanta, "BOTON,{{197}}Modificar", 1014, 1024, BtnModiCab;
     |CONTROL_SIMPLE nPanta, "BOTON,{{199}}Baja", 1026, 1036, BtnBajaCab;
     |CONTROL_SIMPLE nPanta, "BOTON,{{155}}Consulta", 1038, 1048, BtnConsCab;
     |CONTROL_SIMPLE nPanta, "BOTON,{{205}}Imprime", 1050, 1060, BtnImpr;

     nRango = 4 + 8 + 16 + 32;
     |LINEAL_SIMPLE #1#gesim008@, nRango, 1102, 3298 , NULL, NULL, EvtCab;
     nGridCab = FSalida;
     enCtrFin = nGridCab;
     |HAZ EsperaFin;

     Tempo = aTmp9; |HAZ BoraTempo;
     |CIERRAT #gesim009@;
     |CIERRAT #gesim008@;
     |DESCARGA_DEF #gesim009@;
     |DESCARGA_DEF #gesim008@;
     |CIERRAT #9;
     |CIERRAT #8;
     |PULSATECLA;
|FPRO;
