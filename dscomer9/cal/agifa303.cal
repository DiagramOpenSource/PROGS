|FICHEROS;
     agifa303 #0;

     agifa010 #1;   ||-Albaranes
     expor010@ #3;

     agifa071 #2;   ||-Lineas de Albaran
     expor071@ #4;

     agifa084 #5;   ||-Facturas contado
     expor084@ #7;

     agifa069 #6;   ||-Lineas de facturas de contado
     expor069@ #8;

     agifa025 #9;   ||-Comisionistas
     expor025@ #24;

     agifa024 #10;  ||-Clientes
     expor024@ #22;

     agifa019 #11;  ||-Articulos
     expor019@ #23;

     agifa017 #12;       ||Almacenes
     agifa065 #13;       ||Historico

     agifa134 #14;  ||-Facturas Ventas
     expor134@ #16;

     agifa059 #15;  ||-Lineas Facturas Ventas
     expor059@ #17;

     agifa133 #18;  ||-Vencimientos Facturas
     expor133@ #19;

     agifa112 #20;  ||-Proveedores
     expor112@ #21;

     agifa142 #30;

     agifa039 #32;  ||-Dtos Familia / Clientes
     expor039@ #33;

     agifa023 #34;  ||-Precios Clientes/Articulos
     expor023@ #35;

     dsm00117 #36;  ||-Descuentos Promocionales (C).
     expor117@ #38;

     dsw00011 #999; || Usado de temporal para el directorio
     dsm00202 #100; ||Historico E/I Delegacion.

|FIN;

|INCLUYE i_varart;

|VARIABLES;
     &eaNomDef = "";
     &enCamposDef = 0;
     aParam = "";
     n010 = 0; n071 = 0; n084 = 0; n069 = 0; n134 = 0; n059 = 0;
     n133 = 0; n039 = 0; n023 = 0; n305 = 0; n306 = 0; n024 = 0;
     n019 = 0; n112 = 0; n025 = 0; n146 = 0; n147 = 0; n104 = 0;
     n073 = 0;
     ddx = "";
     fich = "";
     org = "";
     num = 0;
     bufer = 0;
     err = 0;
     fdes = "";
     des = "";
     fdef = "";
     def = "";
     exp = 0;
     fmes      = "";
     operacion = "";
     cli       = "";
     nomcli    = "";
     arti      = "";
     nomart    = "";
     seri      = "";
     docum     = 0;
     rep       = "";
     &path     = "";
     horat     = "";
     empresa   = "";
     blanco1   = "                    ";
     mensaje   = "";
     param     = "";
     tipo_exp  = "";

     alma      = 0;
     i         = 0;
     imneto    = 0;
     abono     = 0;
     mes       = 0;
     canti     = 0;
     retencion = 0;
     margen    = 0;
     sw_que    = 0;
     dto       = 0;
     bto       = 0;
     tfac      = 0;
     mfac      = 0;
     dto1      = 0;
     dto2      = 0;
     dto3      = 0;
     imptot    = 0;
     uni       = 0;
     lindoc    = 0;
     preci     = 0;
     sw_testigo= 0;
     sw_pasada = 0;

     fecha     = @;
     fechat    = @;
     fecalb    = @;

     swErr    = 0;
     aMensa   = "";
     aPath    = "";
     aDef     = "";
     aDef1    = "";
     aDef2    = "";
     aDef3    = "";
     aDef4    = "";
     aDef5    = "";
     aDef6    = "";
     aDef7    = "";
     aDef8    = "";
     aDef9    = "";
     aDef10   = "";
     aDef11   = "";
     aDef12   = "";
     aDef13   = "";
     aDef14   = "";
     aDef15   = "";
     aDef16   = "";
     aDef17   = "";
     aDef18   = "";
     aDef19   = "";
     aDef20   = "";
     aDef21   = "";
     aDef22   = "";


     aFic     = "";
     aAlfa    = "";
     fFecha   = @;
     nLin     = 0;
     aHora    = "";
     aAlf1    = "";
     nLon     = 0;
     aOrg     = "";
     aExt     = "";
     aGz      = "";
     nHandle  = 0;
     nHay     = 0;
     nSal     = 0;
|FIN;

|PROCESO linalba;
     |SI #3#58 = "E";
          canti = #4#5 * abono;
          |PARA i = 0; |SI i [ n071; |HACIENDO i = i + 1;
               #2i = #4i;
          |SIGUE;
          |GRABA 021#2c;
          |HAZ act_arti;
          arti = #4#2;
          alma = #4#3;
          nomart = #4#4;
          lindoc = #4#1;
          preci = #4#6;
          |HAZ act_alma;
          |HAZ act_hist;
          |LIBERA #2;
     |FINSI;
|FPRC;

|DEFBUCLE lin071;
     #4#1003;
     ;
     #3#52,#3#0, 001;
     #3#52,#3#0, 999;
     e;
     NULL,linalba;
|FIN;

|PROCESO cabalba;
     sw_pasada = 1;
     #0#2 = #3#52;  |PINTA #0#2;
     #0#3 = #3#0;   |PINTA #0#3;
     #1#0 = #3#0;
     #1#52 = #3#52;
     |LEE 001#agifa010.=;
     |SI FSalida ! 0;
          fecha = #3#1;
          fmes = #3#1 % A402;
          seri = #3#52;
          fecalb = #3#1;
          docum = #3#0;
          cli = #3#3;
          |SI #3#43 = "S";
               abono = -1;
               operacion = "BV";
          |SINO;
               abono = 1;
               operacion = "AV";
          |FINSI;
          |SI #3#58 = "E";
               |PARA i = 0; |SI i [ n010; |HACIENDO i = i + 1;
                    #1i = #3i;
               |SIGUE;
               #1#58 = "R";        ||lo pongo como recibido
               #1#39 = 999999;     ||Si esta facturado no me lo pueden tocar
               |GRABA 021#1c;
               |HAZ act_cli;  ||Actualizo el cliente
          |FINSI;
          |SI #3#58 = "R";
               |HAZ factura;
          |FINSI;
     |SINO;
          |SI #agifa010#39 = 999999; |Y #3#58 = "R";
               |HAZ factura;
          |SINO;
               mensaje = "0000El albaran: " + #1#52 + "/" + #1#0 + " ya EXISTE";
               |MENAV mensaje;
               |ERROR;
          |FINSI;
     |FINSI;

     |BUCLE lin071;  || *(1)
|FPRC;


||______________________________________/ CREACION DE FACTURAS

|PROCESO albalin;
     |LEE 101#2=;
     #2#30 = #16#49;
     #2#26 = #16#0;
     |GRABA 021#2a;
     |LIBERA #2;
     imptot = #2#9;
     uni = #2#5;
     arti = #2#2;
     nomart = #2#4;
     |HAZ act_art2;
|FPRC;

|DEFBUCLE albali;
     #agifa071#1;
     ;
     #agifa010#52,#agifa010#0,000;
     #agifa010#52,#agifa010#0,999;
     e;
     NULL,albalin;
|FIN;

|PROCESO linfactu;
     |PARA i = 0; |SI i [ n059; |HACIENDO i = i + 1;
          #15i = #17i;
     |SIGUE;
     |GRABA 021#15c;
     #1#52 = #17#15;
     #1#0 = #17#2;
     |LEE 101#1=;
     |SI FSalida = 0;
          #1#53 = #16#49;
          #1#39 = #16#0;
          #1#38 = "S";
          #1#58 = "S";
          dto1 = #1#5;
          dto2 = #1#7;
          dto3 = #1#32;

          |BUCLE albali;
          |GRABA 021#1a;
     |FINSI;
     |LIBERA #1;
|FPRC;

|DEFBUCLE linfact;
     #17#1003;
     ;
     #16#49,#16#0, 001;
     #16#49,#16#0, 999;
     e;
     NULL,linfactu;
|FIN;

|PROCESO factura;
     #16#49 = #3#53;
     #16#0 = #3#39;
     |LEE 001#16=;
     |SI FSalida = 0;
          |DDEFECTO #14;
          #14#49 = #3#53;
          #14#0 = #3#39;
          |LEE 001#14=;
          |SI FSalida ! 0;         ||Si la factura no existe
               |PARA i = 0; |SI i [ 55; |HACIENDO i = i + 1;
                    #14i = #16i;
               |SIGUE;
               |GRABA 021#14c;
               |HAZ crea_venci;

               |BUCLE linfact;
               cli = #16#2;
               rep = #16#7;
               nomcli = #16#3;
               fecha = #16#1;
               dto = #16#17;
               bto = #16#18;
               tfac = #16#31;
               fmes = fecha % 402;
               mfac = #16#46;
               |HAZ act_cli2;
               |HAZ act_rep2;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO crea_venci;
     |ABRE #18;
     #19#4 = #16#49;
     #19#0 = #16#0;
     #19#1 = 0;
     |LEE 001#19];
     |PARA ; |SI FSalida = 0; |Y #19#4 = #16#49; |Y #19#0 = #16#0; |HACIENDO ;
          #18#0 = #19#0;
          #18#1 = #19#1;
          #18#2 = #19#2;
          #18#3 = #19#3;
          #18#4 = #19#4;
          |GRABA 021#18n;
          |LEE 001#19s;
     |SIGUE;
     |CIERRA #18;
|FPRC;



||______________________________________/ ACTUALIZACION DEL CLIENTE

|PROCESO act_cli;             ||Llamado desde el albaran
     |ABRE #10;
     #10#0 = #3#3;
     |LEE 101#10=;
     |SI FSalida ! 0;
          |DDEFECTO #10;
          #10#0 = #3#3;
          #10#1 = #3#4;
          |GRABA 021#10b;
     |FINSI;
     imneto = #3#15 < #3#5;
     imneto = imneto < #3#7;
     imneto = imneto < #3#32;
     imneto = imneto ! 0;
     #10#50 = #10#50 + imneto;      ||Pendiente de Facturar
     |GRABA 021#10a;
     |LIBERA #10;
     |CIERRA #10;

     enImpCliPen = imneto;
     eaCliente = #3#3;
     efFecha = #3#1;
     |HAZ AcumulaCli;
|FPRC;

|PROCESO act_cli2;       ||Llamado desde la factura
     |ABRE #10;
     #10#0 = cli;
     |LEE 101#10=;
     |SI FSalida ! 0;
          |DDEFECTO #10;
          #10#0 = cli;
          #10#1 = nomcli;
          |GRABA 021#10b;
     |FINSI;
     imneto = bto - dto;
     |SI sw_que = 0;          ||Solamente en los albaranes
          #10#50 = #10#50 - imneto;     ||Pendiente de Facturar
          #10#49 = #10#49 + tfac;     ||Riesgo Actual

          enImpCliPen = imneto;
          eaCliente = cli;
          efFecha = fecha;
          |HAZ AcumulaCli;
     |FINSI;
     #10#45 = fecha;               ||Fecha Ult. Compra
     #10#46 = imneto;              ||Importe ultima Compra
     mes = ((fmes -1) * 4) + 54;
     #10mes = #10mes + imneto;          ||Compras meses
     mes = mes + 1;
     #10mes = #10mes + dto;          ||Importe Dto meses
     mes = mes + 1;
     #10mes = #10mes + mfac;          ||Margen factura
     #10#102 = #10#102 + imneto;        ||Total compras
     #10#103 = #10#103 + dto;        ||Total Dtos
     #10#104 = #10#104 + mfac;        ||Total Margen
     #10#110 = #10#110 + imneto;        ||Total Facturado

     enImpCliCom = imneto;
     enImpCliDto = dto;
     enImpCliMar = mfac;
     enImpCliFac = imneto;
     eaCliente = cli;
     efFecha = fecha;
     |HAZ AcumulaCli;

     |GRABA 021#10a;
     |LIBERA #10;
     |CIERRA #10;
|FPRC;

||______________________________________/ ACTUALIZACION DEL REPRESENTANTE

|PROCESO act_rep2;       ||Llamado desde la factura
     |ABRE #9;
     #9#0 = rep;
     |LEE 101#9=;
     |SI FSalida ! 0;
          |DDEFECTO #9;
          #9#0 = rep;
          |GRABA 021#9b;
     |FINSI;
     mes = ((fmes - 1) * 2) + 11;
     #9mes = #9mes +. #16#8;
     retencion = #16#8 % #9#39;
     mes = mes + 1;
     #9mes = #9mes +. imneto;
     #9#35 = #9#35 +. #16#8;
     #9#36 = #9#36 +. imneto;
     mes = (fmes - 1) + 40;
     |SI #16#55 ! 0;
          #9mes = #9mes +. retencion;
          #9#52 = #9#52 +. retencion;
     |FINSI;
     |GRABA 020#9a;
     |CIERRA #9;

     enImpRepComi = #16#8;
     enImpRepVta = imneto;
     |SI #16#55 ! 0;
          enImpRepRete = retencion;
     |FINSI;
     eaRepre = #16#7;
     efFecha = #16#1;
     |HAZ AcumulaRep;
|FPRC;

||______________________________________/ ACTUALIZACION DEL ARTICULO

|PROCESO act_arti;
     |ABRE #11;
     #11#0 = #4#2;
     |LEE 101#11=;
     |SI FSalida ! 0;
          |DDEFECTO #11;
          #11#0 = #4#2;
          #11#1 = #4#4;
          |GRABA 021#11b;
     |FINSI;
     |HAZ stock;
     |GRABA 021#11a;
     |LIBERA #11;
     |CIERRA #11;
|FPRC;

|PROCESO stock;
     #11#6 = #11#6 - canti;         ||Stock total
     #11#104 = #11#104 - canti;     ||Stock disponible
     #11#11 = #11#11 + canti;       ||necesidades
     mes = ((fmes - 1) * 5) + 34;
     #11mes = #11mes + canti;
     #11#94 = #11#94 + canti;
     |SI #11#6 > 0;
          #11#10 = #11#6 * #11#9;
     |SINO;
          #11#10 = 0;
     |FINSI;

     enUniVta = canti;
     eaArticulo = arti;
     efFecha = fecha;
     |HAZ AcumulaArt;
|FPRC;


|PROCESO act_art2;       ||Llamado desde las facturas
     |ABRE #11;
     #11#0 = arti;
     |LEE 101#11=;
     |SI FSalida ! 0;
          |DDEFECTO #11;
          #11#0 = arti;
          #11#1 = nomart;
          |GRABA 021#11b;
     |FINSI;
     |SI sw_que = 1; |HAZ stock; |FINSI;
     imneto = imptot < dto1;
     imneto = imneto < dto2;
     imneto = imneto < dto3;
     mes = ((fmes - 1) * 5) + 35;
     margen = uni * #11#9;
     margen = imneto - margen;
     #11mes = #11mes + imneto;
     mes = mes + 1;
     |SI sw_que = 0;
          #1#37 = #1#37 + margen;
     |FINSI;
     #11mes = #11mes + margen;
     #11#95 = #11#95 + imneto;
     #11#96 = #11#96 + margen;
     #11#24 = fecha;
     #11#25 = uni;
     |GRABA 020#11a;
     |LIBERA #11;
     |CIERRA #11;

     efFecha = fecha;
     eaArticulo = arti;
     enImpVta = imneto;
     enImpMar = margen;
     |HAZ AcumulaArt;
|FPRC;

||_______________________________________________/ ACTUALIZACION DEL ALMACEN

|PROCESO act_alma;
     #12#0 = arti;
     #12#1 = alma;
     |LEE 101#12=;
     |SI FSalida ! 0;
          |DDEFECTO #12;
          #12#0 = arti;
          #12#1 = alma;
          #12#40 = nomart;
          |GRABA 021#12b;
     |FINSI;

     #12#39 = #12#39 - canti;
     #12#5 = #12#5 - canti;
     mes = ((fmes - 1) * 2) + 11;
     #12mes = #12mes + canti;
     #12#35 = #12#35 + canti;
     |SI #12#5 > 0;
          #12#3 = #12#5 * #11#9;
     |SINO;
          #12#3 = 0;
     |FINSI;
     #12#50 = #12#42 + #12#6 - #12#43 - #12#39; || necesidades
     #12#50 = #12#50 - #12#4 - #12#9 - #12#8;
     |GRABA 021#12a;
     |LIBERA #12;

     efFecha = fecha;
     eaArticulo = arti;
     enAlmacen = alma;
     enUniSal = canti;
     |HAZ AcumulaAlm;
|FPRC;

|PROCESO act_hist;
     |ABRE #13;
     |DDEFECTO #13;
     #13#0 = arti;
     #13#11= seri;
     #13#1 = fecalb;
     #13#2 = operacion;
     #13#3 = docum;
     #13#4 = lindoc;
     #13#5 = alma;        || almacen
     #13#6 = -canti;   || stock
     #13#7 = #11#6;
     #13#8 = #11#9;
     #13#9 = preci;
     #13#10 = cli;
     |GRABA 121#13n;
     |LIBERA #13;
     |CIERRA #13;
|FPRC;

|DEFBUCLE leealba;
#3#1002;
;
"     " ,       0;
"zzzzz" ,  999999;
e;
 NULL,cabalba;
|FIN;

||__________________________________________/ IMPORTACION FACTURAS CONTADO
|PROCESO lincont;
     |PARA i = 0; |SI i [ n069; |HACIENDO i = i + 1;
          #6i = #8i;
     |SIGUE;
     |GRABA 021#6c;
     canti = #6#5;
     imptot = #6#9;
     uni = #6#5;
     arti = #6#2;
     alma = #6#3;
     nomart = #6#4;
     |HAZ act_art2;
     operacion = "FC";
     lindoc = #6#1;
     preci = #6#6;
     |HAZ act_alma;
     |HAZ act_hist;
|FPRC;

|DEFBUCLE lin069;
     #8#1003;
     ;
     #7#48,#7#0, 001;
     #7#48,#7#0, 999;
     e;
     NULL,lincont;
|FIN;

|PROCESO cabcont;
     sw_pasada = 1;
     #0#2 = #7#48;  |PINTA #0#2;
     #0#3 = #7#0;   |PINTA #0#3;
     |PARA i = 0; |SI i [ n084; |HACIENDO i = i + 1;
          #5i = #7i;
     |SIGUE;
     #5#61 = "S";
     |GRABA 021#5c;
     cli = #5#3;
     rep = #5#6;
     nomcli = #5#4;
     fecha = #5#1;
     fmes = fecha % 402;
     dto = #5#25;
     bto = #5#15;
     tfac = #5#41;
     mfac = #5#37;
     dto1 = #5#5;
     dto2 = #5#7;
     dto3 = #5#32;
     seri = #5#48;
     fecalb = #5#1;
     docum = #5#0;
     |HAZ act_cli2;
     |HAZ act_rep2;

     |BUCLE lin069;
|FPRC;

|DEFBUCLE leecont;
#7#1002;
;
"     " ,       0;
"zzzzz" ,  999999;
e;
NULL , cabcont ;
|FIN;

||_________________________________________/ IMPORTACION CLIENTES

|PROCESO pasa_cli;
     sw_pasada = 1;
     |PINTA 2163 , blanco1;
     |PINTA 2163 , #agifa024#0;
     #agifa024#0 = #22#0;
     |LEE 101#agifa024.=;
     |SI FSalida ! 0;
          |DDEFECTO #agifa024;
          #agifa024#0 = #22#0;
          |GRABA 021#agifa024.b;
     |FINSI;
     |PARA i = 1; |SI i [ 48; |HACIENDO i = i + 1;
          #10i = #22i;
     |SIGUE;
     #agifa024#53 = #22#53;        ||Observaciones 1
     |PARA i = 156; |SI i [ 174; |HACIENDO i = i + 1;
          #10i = #22i;
     |SIGUE;
     |PARA i = 178; |SI i [ n024; |HACIENDO i = i + 1;
          #10i = #22i;
     |SIGUE;
     #agifa024#175 = "S";
     #agifa024#176 = fechat;
     |GRABA 021#agifa024.a;
     |LIBERA #agifa024;
|FPRC;

|DEFBUCLE lee_cli;
#22#1001;
;
"      ";
"zzzzzz";
e;
NULL , pasa_cli;
|FIN;

||_________________________________________/ IMPORTACION ARTICULOS

|PROCESO pasa_art;
     sw_pasada = 1;
     |PINTA 2163 , blanco1;
     |PINTA 2163 , #agifa019#0;
     #agifa019#0 = #23#0;
     |LEE 101#agifa019.=;
     |SI FSalida ! 0;
          |DDEFECTO #agifa019;
          #agifa019#0 = #23#0;
          |GRABA 021#agifa019.b;
     |FINSI;
     |PARA i = 1; |SI i [ 5; |HACIENDO i = i + 1;
          #agifa019#i# = #23#i#;
     |SIGUE;
     #agifa019#9 = #23#9;
     #agifa019#10 = #agifa019#10 * #agifa019#6;
     |SI #agifa019#10 < 0; #agifa019#10 = 0; |FINSI;
     |PARA i = 18; |SI i [ 23; |HACIENDO i = i + 1;
          #agifa019#i# = #23#i#;
     |SIGUE;
     |PARA i = 26; |SI i [ 31; |HACIENDO i = i + 1;
          #agifa019#i# = #23#i#;
     |SIGUE;
     #agifa019#102 = #23#102;
     #agifa019#103 = #23#103;
     |PARA i = 105; |SI i [ n019; |HACIENDO i = i + 1;
          #agifa019#i# = #23#i#;
     |SIGUE;
     #agifa019#123 = "S";
     #agifa019#124 = fechat;
     |GRABA 021#agifa019.a;
     |LIBERA #agifa019;
     #agifa017#0 = #23#0;
     #agifa017#1 = 1;
     |LEE 101#agifa017.=;
     |SI FSalida ! 0;
          |DDEFECTO #agifa017;
          #agifa017#0 = #23#0;
          #agifa017#1 = 1;
          |GRABA 021#agifa017.b;
     |FINSI;
     #agifa017#40 = #23#1;
     |GRABA 021#agifa017.a;
     |LIBERA #agifa017;
|FPRC;

|DEFBUCLE lee_art;
#23#1001;
;
"                    ";
"zzzzzzzzzzzzzzzzzzzz";
e;
NULL , pasa_art;
|FIN;

||_________________________________________/ IMPORTACION PROVEEDORES

|PROCESO pasa_pro;
     sw_pasada = 1;
     |PINTA 2163 , blanco1;
     |PINTA 2163 , #agifa112#0;
     #agifa112#0 = #21#0;
     |LEE 101#agifa112.=;
     |SI FSalida ! 0;
          |DDEFECTO #agifa112;
          #agifa112#0 = #21#0;
          |GRABA 021#agifa112.b;
     |FINSI;
     |PARA i = 1; |SI i [ 26; |HACIENDO i = i + 1;
          #20i = #21i;
     |SIGUE;
     #agifa112#41 = #21#41;
     #agifa112#42 = #21#42;
     |PARA i = 57; |SI i [ n112; |HACIENDO i = i + 1;
          #20i = #21i;
     |SIGUE;
     #agifa112#71 = "S";
     #agifa112#72 = fechat;
     |GRABA 021#agifa112.a;
     |LIBERA #agifa112;
|FPRC;

|DEFBUCLE lee_pro;
#21#1001;
;
"      ";
"zzzzzz";
e;
NULL , pasa_pro;
|FIN;

||_________________________________________/ IMPORTACION REPRESENTANTES

|PROCESO pasa_rep;
     sw_pasada = 1;
     |PINTA 2163 , blanco1;
     |PINTA 2163 , #agifa025#0;
     #agifa025#0 = #24#0;
     |LEE 101#agifa025.=;
     |SI FSalida ! 0;
          |DDEFECTO #agifa025;
          #agifa025#0 = #24#0;
          |GRABA 021#agifa025.b;
     |FINSI;
     |PARA i = 1; |SI i [ 025; |HACIENDO i = i + 1;
          #9i = #24i;
     |SIGUE;
     #agifa025#39 = #24#39;

     #agifa025#54 = "S";
     #agifa025#55 = fechat;

     #agifa025#56 = #24#56;
     #agifa025#57 = #24#57;
     #agifa025#58 = #24#58;
     #agifa025#59 = #24#59;
     #agifa025#60 = #24#60;
     |GRABA 021#agifa025.a;
     |LIBERA #agifa025;
|FPRC;

|DEFBUCLE lee_rep;
#24#1001;
;
"  ";
"zz";
e;
NULL , pasa_rep;
|FIN;

||_________________________________________/ IMPORTACION DTO CLI./FAM.

|PROCESO pasa_039;
     sw_pasada = 1;
     |PINTA 2163 , blanco1;
     |PINTA 2163 , #agifa039#0;
     #agifa039#0 = #33#0;
     #agifa039#1 = #33#1;
     |LEE 101#agifa039.=;
     |SI FSalida ! 0;
          |DDEFECTO #agifa039;
          #agifa039#0 = #33#0;
          #agifa039#1 = #33#1;
          |GRABA 021#agifa039.b;
     |FINSI;

     #agifa039#2 = #33#2;
     #agifa039#3 = #33#3;
     #agifa039#4 = #33#4;
     #agifa039#5 = "S";
     #agifa039#6 = fechat;

     |GRABA 021#agifa039.a;
     |LIBERA #agifa039;
|FPRC;

|DEFBUCLE lee_039;
#33#1002;
;
"      " , "   ";
"zzzzzz" , "zzz";
e;
NULL , pasa_039;
|FIN;

||_________________________________________/ IMPORTACION DTO CLI./FAM.

|PROCESO pasa_023;
     sw_pasada = 1;
     |PINTA 2163 , blanco1;
     |PINTA 2163 , #agifa023#0;
     #agifa023#0 = #35#0;
     #agifa023#1 = #35#1;
     |LEE 101#agifa023.=;
     |SI FSalida ! 0;
          |DDEFECTO #agifa023;
          #agifa023#0 = #35#0;
          #agifa023#1 = #35#1;
          |GRABA 021#agifa023.b;
     |FINSI;
     |PARA i = 2; |SI i [ n023; |HACIENDO i = i + 1;
          #34i = #35i;
     |SIGUE;
          #34#32 = #35#32;
     #agifa023#30 = "S";
     #agifa023#31 = fechat;

     |GRABA 021#agifa023.a;
     |LIBERA #agifa023;
|FPRC;

|DEFBUCLE lee_023;
#35#1002;
;
"      " , "                    ";
"zzzzzz" , "zzzzzzzzzzzzzzzzzzzz";
e;
NULL , pasa_023;
|FIN;

||_________________________________________/ IMPORTACION DTOS PROMOCIONALES
|PROCESO pasa_305;
     sw_pasada = 1;
     |PINTA 2163 , blanco1;
     |PINTA 2163 , #38#0;
     |PARA i = 0; |SI i [ n305; |HACIENDO i = i + 1;
          #36i = #38i;
     |SIGUE;
     |LEE 101#36=;
     |PARA i = 0; |SI i [ n305; |HACIENDO i = i + 1;
          #36i = #38i;
     |SIGUE;
     |SI FSalida = 0;
          |GRABA 020#36a;
     |SINO;
          |GRABA 020#36n;
     |FINSI;
     |LIBERA #36;
|FPRC;


|DEFBUCLE lee_305;
     #38#1001;
     ;
     00001
     99999;
     ;
     NULL , pasa_305;
|FIN;
-------------------------------------------------------------------------
|PROCESO Vencimiento;
     #19#4 = #16#49;
     #19#0 = #16#0;
     #19#1 = 0;
     |LEE 000#19];
     |PARA; |SI FSalida = 0; |Y #19#4 = #16#49; |Y #19#0 = #16#0; |HACIENDO;
          |PARA i = 0; |SI i [ n133; |HACIENDO i = i + 1;
               #18i = #19i;
          |SIGUE;
          |BORRA 000#18c;     || Por si existe
          |GRABA 021#18n;
          |LEE 000#19s;
     |SIGUE;
|FPRC;

|PROCESO LinFacVenta;
     #17#14 = #16#49;
     #17#0 = #16#0;
     #17#1 = 0;
     |LEE 000#17];
     |PARA; |SI FSalida = 0; |Y #17#14 = #16#49; |Y #17#0 = #16#0; |HACIENDO;
          |PARA i = 0; |SI i [ n059; |HACIENDO i = i + 1;
               #15i = #17i;
          |SIGUE;
          |BORRA 000#15c;     || Por si existe
          |GRABA 021#15n;
          |LEE 000#17s;
     |SIGUE;
|FPRC;

|PROCESO FacVenta;
     |PARA i = 0; |SI i [ n134; |HACIENDO i = i + 1;
          #14i = #16i;
     |SIGUE;
     |LEE 000#14=;
     |SI FSalida ! 0;
          |GRABA 021#14n;
          |HAZ Vencimiento;
          |HAZ LinFacVenta;
          cli = #16#2;
          rep = #16#7;
          nomcli = #16#3;
          fecha = #16#1;
          dto = #16#17;
          bto = #16#18;
          tfac = #16#31;
          fmes = fecha % 402;
          mfac = #16#46;
          |HAZ act_cli2;
          |HAZ act_rep2;
     |FINSI;
|FPRC;

|DEFBUCLE FacVenta;
     #16#1002;
     ;
     "     ",       0;
     "zzzzz",  999999;
     ;
     NULL, FacVenta;
|FIN;

-------------------------------------------------------------------------
|PROCESO tipo3;
     fechat = ~;
     fFecha = ~;
     |HAZ BusCampos;
     |SI err ! 0;
          |MENAV "0000Proceso anulado...";
          |ACABA;
     |FINSI;
     |ABRE #30;
     |LEE 001#30p;
     |SI FSalida ! 0;
          |CIERRA #30;
          |MENAV "0000No se encuentra el Control Path";
          |ERROR;
          |ACABA;
     |FINSI;
     |CIERRA #30;
     path = #30#149;  |QBF path;
     |SI path = "";
          |MENAV "0000Path no configurado en Parametros generales.";
          |ACABA;
     |SINO;
          |MKDIR path;
     |FINSI;

     |HAZ  CargaDef;

     |SI swErr ! 0;
          aMensa = "0000Error al cargar:" + aDef;
          |MENAV aMensa;
          |HAZ DesCargaDef;
          |ACABA;
     |FINSI;
     |SI #0#15 = "S";
          |SI tipo_exp = "delega";
                |CONFI "0000SRecoger Ficheros de central ahora: ";
                |SI FSalida = 0;
                    |MENAV "0000Atencion!! Debe estar conectado a internet";
                    |SUB_EJECUTA "dsm00206.tab";
                |FINSI;
          |SINO;
                |CONFI "0000SRecibir datos desde delegaciones ahora: ";
                |SI FSalida = 0;
                    |MENAV "0000Atencion!! Debe estar conectado a internet";
                    |SUB_EJECUTA "dsm00207.tab";
                |FINSI;
          |FINSI;
     |FINSI;
     |HAZ CargaTempo;

     |HAZ DesCargaDef;
|FPRC;


|PROCESO Importacion;
     |SI #0#4 = "S";     ||Clientes
          |PATH_DAT #22 path;
          |PINTA 2124 , "Clientes        ";
          |ABRE #10;
          |BUCLE lee_cli;
          |CIERRA #10;
     |FINSI;
     |SI #0#7 = "S";     ||Articulos
          |PATH_DAT #23 path;
          |PINTA 2124 , "Articulos       ";
          |ABRE #11;
          |ABRE #12;
          |BUCLE lee_art;
          |CIERRA #11;
          |CIERRA #12;
     |FINSI;
     |SI #0#5 = "S";     ||Proveedores
          |PATH_DAT #21 path;
          |PINTA 2124 , "Proveedores     ";
          |ABRE #20;
          |BUCLE lee_pro;
          |CIERRA #20;
     |FINSI;
     |SI #0#6 = "S";     ||Representantes
          |PATH_DAT #24 path;
          |PINTA 2124 , "Representantes  ";
          |ABRE #9;
          |BUCLE lee_rep;
          |CIERRA #9;
     |FINSI;
     |SI #0#9 = "S"; |Y tipo_exp = "delega";      ||Dto Familia Cliente
          |PINTA 2124 , "Dto  Fam./Cli.  ";
          |PATH_DAT #33 path;
          |ABRE #32;
          |BUCLE lee_039;
          |CIERRA #32;
     |FINSI;
     |SI #0#10 = "S"; |Y tipo_exp = "delega";     ||Precio Clientes/Articulos
          |PINTA 2124 , "Precio Cli./Art.";
          |PATH_DAT #35 path;
          |ABRE #34;
          |BUCLE lee_023;
          |CIERRA #34;
     |FINSI;
     |SI #0#11 = "S"; |Y tipo_exp = "delega";     ||Precio Clientes/Articulos
          |PINTA 2124 , "Dtos Promocion. ";
          |PATH_DAT #38 path;
          |ABRE #36;
          |BUCLE lee_305;
          |CIERRA #36;
     |FINSI;
     |PINTA 2163 , blanco1;
     |SI #0#0 = "S";
          sw_que = 0;
          |ABRE #1;
          |ABRET #2;
          |ABRE #12;
          |ABRE #14;
          |ABRE #15;
          |PINTA 2124 , "Albaranes";
          |PATH_DAT #3 path;
          |PATH_DAT #4 path;
          |PATH_DAT #16 path;
          |PATH_DAT #17 path;
          |PATH_DAT #19 path;
          |ABRE #16;
          |ABRE #19;
          |BUCLE leealba;
          |CIERRA #1;
          |CIERRA #2;
          |CIERRAT #12;
          |CIERRA #14;
          |CIERRA #15;
          |CIERRA #16;
          |CIERRA #19;
     |FINSI;
     |SI #0#1 = "S";
          sw_que = 1;
          |PINTA 2124 , "Facturas Contado";
          |ABRE #5;
          |ABRE #6;
          |ABRE #9;
          |ABRE #10;
          |ABRE #11;
          |ABRE #12;
          |ABRE #13;
          |PATH_DAT #7 path;
          |PATH_DAT #8 path;
          |BUCLE leecont;
          |CIERRA #5;
          |CIERRA #6;
          |CIERRA #9;
          |CIERRA #11;
          |CIERRA #12;
          |CIERRA #13;
     |FINSI;
     |SI #0#12 = "S";
          sw_que = 1;
          |PATH_DAT #16 path;
          |PATH_DAT #17 path;
          |PATH_DAT #19 path;
          |PINTA 2124 , "Facturas Venta  ";
          |ABRE #14; |ABRE #15; |ABRE #18; |ABRE #17; |ABRE #19;
          |ABRE #10;
          |BUCLE FacVenta;
          |CIERRA #10;
          |CIERRA #14; |CIERRA #15; |CIERRA #18; |CIERRA #17; |CIERRA #19;
     |FINSI;
     |HAZ Seleccion;
|FPRC;

|PROCESO tipo7; |TIPO 7;
     |ATRI R;
     |SI tipo_exp = "central";
          |PINTA 0532, "CENTRAL ";
     |SINO;
          |PINTA 0532, "DELEGACION ";
     |FINSI;
     |ATRI 0;
|FPRC;

|PROCESO mayus;
 #0Campo = #0Campo > " ";
 |PINTA #0Campo;
|FPRC;
=======================================================================
|PROCESO BusCampos;
     |DFICE org; |QBF org;
     |DBASE des; |QBF des;
     |DDEF  def; |QBF def;
      eaNomDef = "agifa010"; |HAZ CamposDef; n010 = enCamposDef;
      eaNomDef = "agifa071"; |HAZ CamposDef; n071 = enCamposDef;
      eaNomDef = "agifa084"; |HAZ CamposDef; n084 = enCamposDef;
      eaNomDef = "agifa084"; |HAZ CamposDef; n084 = enCamposDef;
      eaNomDef = "agifa069"; |HAZ CamposDef; n069 = enCamposDef;
      eaNomDef = "agifa025"; |HAZ CamposDef; n025 = enCamposDef;
      eaNomDef = "agifa024"; |HAZ CamposDef; n024 = enCamposDef;
      eaNomDef = "agifa019"; |HAZ CamposDef; n019 = enCamposDef;
      eaNomDef = "agifa134"; |HAZ CamposDef; n134 = enCamposDef;
      eaNomDef = "agifa059"; |HAZ CamposDef; n059 = enCamposDef;
      eaNomDef = "agifa133"; |HAZ CamposDef; n133 = enCamposDef;
      eaNomDef = "agifa112"; |HAZ CamposDef; n112 = enCamposDef;
      eaNomDef = "agifa039"; |HAZ CamposDef; n039 = enCamposDef;
      eaNomDef = "agifa023"; |HAZ CamposDef; n023 = enCamposDef;
      eaNomDef = "dsm00117"; |HAZ CamposDef; n305 = enCamposDef;
|FPRC;

|PROCESO CargaDef;
     |DBASE aPath;

     aDef1 = aPath + "def/" + "agifa010";
     aDef  = aDef1;
     |CARGA_DEF aDef, expor010@;
     |SI FSalida ! 0;
          swErr = 1;
          aDef1 = "";
          |ACABA;
     |FINSI;

     aDef2 = aPath + "def/" + "agifa071";
     aDef  = aDef2;
     |CARGA_DEF aDef, expor071@;
     |SI FSalida ! 0;
          swErr = 1;
          aDef2 = "";
          |ACABA;
     |FINSI;

     aDef3 = aPath + "def/" + "agifa084";
     aDef  = aDef3;
     |CARGA_DEF aDef, expor084@;
     |SI FSalida ! 0;
          swErr = 1;
          aDef3 = "";
          |ACABA;
     |FINSI;

     aDef4 = aPath + "def/" + "agifa069";
     aDef  = aDef4;
     |CARGA_DEF aDef, expor069@;
     |SI FSalida ! 0;
          swErr = 1;
          aDef4 = "";
          |ACABA;
     |FINSI;

     aDef5 = aPath + "def/" + "agifa134";
     aDef  = aDef5;
     |CARGA_DEF aDef, expor134@;
     |SI FSalida ! 0;
          swErr = 1;
          aDef5 = "";
          |ACABA;
     |FINSI;

     aDef7 = aPath + "def/" + "agifa059";
     aDef  = aDef7;
     |CARGA_DEF aDef, expor059@;
     |SI FSalida ! 0;
          swErr = 1;
          aDef7 = "";
          |ACABA;
     |FINSI;

     aDef8 = aPath + "def/" + "agifa133";
     aDef  = aDef8;
     |CARGA_DEF aDef, expor133@;
     |SI FSalida ! 0;
          swErr = 1;
          aDef8 = "";
          |ACABA;
     |FINSI;

     aDef9 = aPath + "def/" + "agifa039";
     aDef  = aDef9;
     |CARGA_DEF aDef, expor039@;
     |SI FSalida ! 0;
          swErr = 1;
          aDef9 = "";
          |ACABA;
     |FINSI;

     aDef10= aPath + "def/" + "agifa023";
     aDef  = aDef10;
     |CARGA_DEF aDef, expor023@;
     |SI FSalida ! 0;
          swErr = 1;
          aDef10 = "";
          |ACABA;
     |FINSI;

     aDef11= aPath + "def/" + "dsm00117";
     aDef  = aDef11;
     |CARGA_DEF aDef, expor117@;
     |SI FSalida ! 0;
          swErr = 1;
          aDef11 = "";
          |ACABA;
     |FINSI;

     aDef13= aPath + "def/" + "agifa024";
     aDef  = aDef13;
     |CARGA_DEF aDef, expor024@;
     |SI FSalida ! 0;
          swErr = 1;
          aDef13 = "";
          |ACABA;
     |FINSI;

     aDef14= aPath + "def/" + "agifa019";
     aDef  = aDef14;
     |CARGA_DEF aDef, expor019@;
     |SI FSalida ! 0;
          swErr = 1;
          aDef14 = "";
          |ACABA;
     |FINSI;

     aDef15= aPath + "def/" + "agifa112";
     aDef  = aDef15;
     |CARGA_DEF aDef, expor112@;
     |SI FSalida ! 0;
          swErr = 1;
          aDef15 = "";
          |ACABA;
     |FINSI;

     aDef16= aPath + "def/" + "agifa025";
     aDef  = aDef16;
     |CARGA_DEF aDef, expor025@;
     |SI FSalida ! 0;
          swErr = 1;
          aDef16 = "";
          |ACABA;
     |FINSI;

     |HAZ NomeDat;

|FPRC;

|PROCESO DesCargaDef;
     |SI aDef16 ! ""; |DESCARGA_DEF #expor025@; |FINSI;
     |SI aDef15 ! ""; |DESCARGA_DEF #expor112@; |FINSI;
     |SI aDef14 ! ""; |DESCARGA_DEF #expor019@; |FINSI;
     |SI aDef13 ! ""; |DESCARGA_DEF #expor024@; |FINSI;
     |SI aDef11 ! ""; |DESCARGA_DEF #expor117@; |FINSI;
     |SI aDef10 ! ""; |DESCARGA_DEF #expor023@; |FINSI;
     |SI aDef9  ! ""; |DESCARGA_DEF #expor039@; |FINSI;
     |SI aDef8  ! ""; |DESCARGA_DEF #expor133@; |FINSI;
     |SI aDef7  ! ""; |DESCARGA_DEF #expor059@; |FINSI;
     |SI aDef5  ! ""; |DESCARGA_DEF #expor134@; |FINSI;
     |SI aDef4  ! ""; |DESCARGA_DEF #expor069@; |FINSI;
     |SI aDef3  ! ""; |DESCARGA_DEF #expor084@; |FINSI;
     |SI aDef2  ! ""; |DESCARGA_DEF #expor071@; |FINSI;
     |SI aDef1  ! ""; |DESCARGA_DEF #expor010@; |FINSI;
|FPRC;

|PROCESO NomeDat;
     |NOME_DAT #24 "expor025";
     |NOME_DAT #21 "expor112";
     |NOME_DAT #23 "expor019";
     |NOME_DAT #22 "expor024";
     |NOME_DAT #38 "dsm00117";
     |NOME_DAT #35 "expor023";
     |NOME_DAT #33 "expor039";
     |NOME_DAT #19 "expor133";
     |NOME_DAT #17 "expor059";
     |NOME_DAT #16 "expor134";
     |NOME_DAT #8  "expor069";
     |NOME_DAT #7  "expor084";
     |NOME_DAT #4  "expor071";
     |NOME_DAT #3  "expor010";
|FPRC;
컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
|PROCESO BorraExpor;
     aFic = path;
     |_PDIR aFic;
     |PARA; |SI FSalida = 0; |HACIENDO;
          aAlfa = aFic % -103;
          |SI aAlfa = "ddx"; |O aAlfa = "ixx"; |O aAlfa = "dat";
               aAlfa = aFic % -1102;
               |SI aAlfa = "ex"; |FBORRA aFic; |FINSI;
               aAlfa = aFic % -0507;
               |SI aAlfa = "testigo"; |FBORRA aFic; |FINSI;
          |FINSI;
          |_SDIR aFic;
     |SIGUE;
|FPRC;


|PROCESO CargaTempo;
     |SI path ! "";
          |DELINDEX #999;
          |ABRE #999;
          |HAZ BuscaFichero;
          |CIERRA #999;
     |FINSI;
|FPRC;

|PROCESO BuscaFichero;
     err = 1;
     |ABRE #100;
     aFic = path;
     |_PDIR aFic;
     |PARA; |SI FSalida = 0; |HACIENDO;
          aAlfa = aFic % -115;
          |SELECT #2#100;
          #100#0 = "I";
          #100#5 = aAlfa;
          |LEE 000#100=;
          |SI FSalida ! 0;
               #999#4 = aAlfa;
               #999#1 = aAlfa;
               |GRABA 020#999n;
          |FINSI;
          |_SDIR aFic;
     |SIGUE;
     |CIERRA #100;
     ||컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
     |LEE 000#999p;
     |PARA; |SI FSalida = 0; |HACIENDO;
          aFic = path + #999#4;
          |HAZ DesComprime;
          |SI err = 0;
               |HAZ Importacion;
               |HAZ ActHisto;
               |HAZ BorraExpor;
          |FINSI;
          |LEE 000#999s;
     |SIGUE;
|FPRC;

|PROCESO ActHisto;
     |ABRE #100;
     |SELECT #1#100;
     #100#0 = "I";
     #100#1 = fFecha;
     #100#2 = 999;
     |LEE 000#100];
     |SI FSalida = 0;
          |LEE 000#100a;
     |SINO;
          |LEE 000#100u;
     |FINSI;
     |SI FSalida = 0; |Y #100#0 = "I"; |Y #100#1 = fFecha;
          nLin = #100#2 + 1;
     |SINO;
          nLin = 1;
     |FINSI;

     |HORA aHora;

     |DDEFECTO #100;
     #100#0 = "I";
     #100#1 = fFecha;
     #100#2 = nLin;
     #100#3 = aHora;
     #100#4 = Usuario % 810;
     #100#5 = aFic % -115;
     |GRABA 020#100n;
     |CIERRA #100;
|FPRC;

|PROCESO DesComprime;
     ||---------Chequeo del fichero (debe se todo con numeros)
     ||         DETAR se cuelga si el fichero no es un tar
     err = 0;
     |QBF aFic;
     aAlfa = aFic % -115;
     |PARA i = 101; |SI i [ 1500; |HACIENDO i = i + 100;
          aAlf1 = (aAlfa % i);
          |SI aAlf1 < "0"; |O aAlf1 > "9";
               |SI i = 1201; |Y aAlf1 = ".";
               |SINO;
                    err = 1;
                    |SAL;
               |FINSI;
          |FINSI;
     |SIGUE;
     ||----------
     |SI err = 0;
          aAlfa = aFic % A0;
          nLon = aAlfa;
          nLon = nLon + 100 - 4;
          aOrg = aFic % nLon;
          aExt = aFic % -104;
          aGz  = aOrg + ".gz";

          |RENOMBRA_FICHERO aFic, aGz;
          |DESCOMPRIME aGz;
          |RENOMBRA_FICHERO aOrg, aFic;

          |XABRE "B", aFic, nHandle;
          |XLEE nHandle, 1, aAlfa;
          nHay = FSalida;
          |XCIERRA nHandle;

          |SI nHay > 0;
               |DETAR aFic, path;
               nSal = FSalida;
               |FBORRA aFic;
               |RENOMBRA_FICHERO aGz, aFic;
          |FINSI;

          |SI nSal = 0;
               err = 0;
          |SINO;
               err = 1;
               aAlfa = "0000Error en... [" + aFic + "]";
               |MENAV aAlfa;
          |FINSI;
     |FINSI;
|FPRC;

|PROGRAMA;
     aParam = PARAMETRO;
     |QBF aParam;
     param = aParam;
     |SI aParam = "centralaut";
          param = "central";
     |FINSI;
     |SI aParam = "delegaaut";
          param = "delega";
     |FINSI;
     |ABRE #0;
     #0#16 = 1;
     |LEE 101#0=;
     |SI FSalida ! 0;
          |DDEFECTO #0;
          |GRABA 020#0b;
     |FINSI;
     |SI aParam = "centralaut";
          |HAZ tipo3;
     |SINO;
          |PINPA #0#0;
          |PINDA #0#0;
          |SI param = "delega";
               tipo_exp = "delega";
          |SINO;
               tipo_exp = "central";
               |CAMPO_MODIFICA #0#9 , 1 , "N";
               |CAMPO_MODIFICA #0#10 , 1 , "N";
               |CAMPO_MODIFICA #0#11 , 1 , "N";
          |FINSI;
          |ENDATOS #1#0;
          |SI FSalida = 0;
               |GRABA 020#0a;
               |HAZ tipo3;
          |FINSI;
     |FINSI;
     |CIERRA #0;
|FPRO;
