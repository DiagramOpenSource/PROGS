|FICHEROS;
     belm0012 #0;
     belm0019 #1;
     belz0002 #2,MANTE;
     belm0011 #11;
     belm0013 #13;
     belm0014 #14;
     belm0018 #18;
     agifa019 #19;
     agifa024 #24;
     agifa040 #40;
     agifa112 #112;
     agifa146 #146; || prof
     agifa147 #147;
     agifa104 #104; || pedv
     agifa073 #73;
     agifa010 #10;  || albv
     agifa071 #71;
     agifa077 #77;  || albc
     agifa080 #80;
     agifa081 #81;  || pedc
     agifa067 #67;

     dsm00225 #225; ||paises
|FIN;

|VARIABLES;
     nDto = 0;
     nDtoPP = 0;
     nDtoEsp = 0;

     nPorteResta = 0;
     aAlfa = "";
     nGrabadas = 0;
     nGrabadasTotal = 0;
     aArti = "";
     aFam = "";
     aSerie = "";
     nModo = 0;
     nEsta = 0;
     nPortes = 0;
     nTotal = 0;
     aPartida = "";
     aCodigo = "";
     aPais = "";
     nError = 0;
     aTipo = "";
     aDocu = "";
     aSer = "";
     nNum = 0;
     nImpoLin = 0;
     aCEE = "";
|FIN;

|PROCESO Consulta; |TIPO 66;
     |SI #0#0 = "I";     || Compras
          |ABRE #112;
          #112#0 = #0#1;
          |LEE 000#112=;
          |CONSULTA_CLAVE #1#112;
          |SI FSalida = 0;
               #0#1 = #112#0;
               |PINTA #0#1;
          |SINO;
               |ERROR;
          |FINSI;
          |CIERRA #112;
     |SINO;              || Ventas
          |ABRE #24;
          #24#0 = #0#1;
          |LEE 000#24=;
          |CONSULTA_CLAVE #1#24;
          |SI FSalida = 0;
               #0#1 = #24#0;
               |PINTA #0#1;
          |SINO;
               |ERROR;
          |FINSI;
          |CIERRA #24;
     |FINSI;
|FPRC;

|PROCESO Borra1;
     |BORRA 020#1a;
|FPRC;

|DEFBUCLE Borra1;
     #1#1;
     ;
     #0#0, #0#8, #0#6, #0#7, "                  ";
     #0#0, #0#8, #0#6, #0#7, "zzzzzzzzzzzzzzzzzz";
     be;
     NULL, Borra1;
|FIN;

|PROCESO Documento; |TIPO 0;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo ! 1; |ACABA; |FINSI;
     |SI FSalida = 2; |ACABA; |FINSI;

     |ABRE #11; |LEE 000#11p; |CIERRA #11;

     |ABRE #18;
     #18#0 = #11#14;
     |LEE 000#18=;
     |SI FSalida ! 0; |DDEFECTO #18; |FINSI;
     |CIERRA #18;

     |ABRE #40;
     #40#0 = #18#4;
     |LEE 000#40=;
     |SI FSalida ! 0; |DDEFECTO #40; |FINSI;
     |CIERRA #40;

     |ABRET #1;
     |ABRE #19;
     |ABRE #13;
     |ABRE #14;
     |ABRE #40;
     |ABRE #10;
     |ABRE #24;
     |ABRE #77;
     |ABRE #81;
     |ABRE #104;
     |ABRE #112;
     |ABRE #146;
     |ABRE #225;
     aCEE = "T";
     nError = 0;
     aTipo = #0#0;
     aDocu = #0#8;
     aSer = #0#6;
     nNum = #0#7;
     |HAZ CargaDocu;
     |PINDA #0#0;
     |CIERRAT #1;
     |CIERRA #19;
     |CIERRA #13;
     |CIERRA #14;
     |CIERRA #40;
     |CIERRA #10;
     |CIERRA #24;
     |CIERRA #77;
     |CIERRA #81;
     |CIERRA #104;
     |CIERRA #112;
     |CIERRA #146;
     |CIERRA #225;
     |SI nError = 0;
          |PTEC 806;
     |FINSI;
     |SI nError = 1;
          |MENAV "0000Doumento no permitido...";
          |ERROR; |ACABA;
     |FINSI;
     |SI nError = 2;
          |MENAV "0000No existe documento...";
          |ERROR; |ACABA;
     |FINSI;
|FPRC;

|PROCESO CargaDocu;
     |SI aTipo = "I";     || Compras
          |SI aDocu = "P";
               #81#25 = aSer;
               #81#0 = nNum;
               |LEE 000#81=;
               |SI FSalida = 0;
                    |HAZ CargaValores;
               |SINO;
                    nError = 2;
               |FINSI;
          |FINSI;
          |SI aDocu = "A";
               #77#50 = aSer;
               #77#0 = nNum;
               |LEE 000#77=;
               |SI FSalida = 0;
                    |HAZ CargaValores;
               |SINO;
                    nError = 2;
               |FINSI;
          |FINSI;
          |SI aDocu = "R";
               nError = 1;
          |FINSI;
     |SINO;
          |SI aDocu = "P";
               #104#25 = aSer;
               #104#0 = nNum;
               |LEE 000#104=;
               |SI FSalida = 0;
                    |HAZ CargaValores;
               |SINO;
                    nError = 2;
               |FINSI;
          |FINSI;
          |SI aDocu = "A";
               #10#52 = aSer;
               #10#0 = nNum;
               |LEE 000#10=;
               |SI FSalida = 0;
                    |HAZ CargaValores;
               |SINO;
                    nError = 2;
               |FINSI;
          |FINSI;
          |SI aDocu = "R";
               #146#48 = aSer;
               #146#0 = nNum;
               |LEE 000#146=;
               |SI FSalida = 0;
                    |HAZ CargaValores;
               |SINO;
                    nError = 2;
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO agifa067;
     aPartida = #67#42;
     nImpoLin = #67#9;
     aArti = #67#2;

     |HAZ GraLinea;
|FPRC;

|PROCESO agifa080;
     aPartida = #80#37;
     nImpoLin = #80#9;
     aArti = #80#2;

     |HAZ GraLinea;
|FPRC;

|PROCESO agifa073;
     aPartida = #73#45;
     nImpoLin = #73#9;
     aArti = #73#2;

     |HAZ GraLinea;
|FPRC;

|PROCESO agifa071;
     aPartida = #71#49;
     nImpoLin = #71#9;
     aArti = #71#2;

     |HAZ GraLinea;
|FPRC;

|PROCESO agifa147;
     aPartida = #147#29;
     nImpoLin = #147#9;
     aArti = #147#2;

     |HAZ GraLinea;
|FPRC;

|PROCESO GraLinea;
     nImpoLin = nImpoLin - (nImpoLin * nDto / 100)
     nImpoLin = nImpoLin ! 2
     nImpoLin = nImpoLin - (nImpoLin * nDtoEsp / 100)
     nImpoLin = nImpoLin ! 2
     nImpoLin = nImpoLin - (nImpoLin * nDtoPP / 100)
     nImpoLin = nImpoLin ! 2

     #19#0 = aArti;
     |LEE 000#19=;
     |SI FSalida ! 0; |DDEFECTO #19; |FINSI;
     aFam = #19#2;

     #14#1 = #19#0;
     |LEE 000#14=;
     |SI FSalida = 0;
          aFam = #14#0;
     |FINSI;

     #13#0 = aFam;
     |LEE 000#13=;
     |SI FSalida ! 0; |ACABA; |FINSI;

     aPartida = #13#2;

     |SI #18#3 = "S";
          nPorteResta = nImpoLin * nPortes / nTotal;
     |SINO;
          nPorteResta = 0;
     |FINSI;
     nImpoLin = nImpoLin - nPorteResta

     nEsta = nImpoLin + (nImpoLin * #18#2 / 100);


     |DDEFECTO #1;
     #1#0 = #0#0;
     #1#1 = #0#8;
     #1#2 = #0#6;
     #1#3 = #0#7;
     #1#4 = aPartida;
     |LEE 101#1=;
     |SI FSalida ! 0;
          |GRABA 020#1b;
          nGrabadas = nGrabadas + 1;
          nGrabadasTotal = nGrabadasTotal + 1;
     |FINSI;
     #1#5 = #1#5 + nImpoLin + nPorteResta;
     #1#6 = #1#6 + nEsta;
     |GRABA 020#1a;
     |LIBERA #1;

     #0#10 = #0#10 + nEsta;
|FPRC;

|PROCESO CargaValores;
     nEsta = 0;

     |SI aTipo = "I";     || Compras
          |SI aDocu = "P";
               nTotal = #81#11;
               nPortes = 0;
               aCodigo = #81#4;
               nDto = #81#5;
               nDtoPP = #81#6;
               nDtoEsp = #81#17;
          |FINSI;
          |SI aDocu = "A";
               nTotal = #77#15 - #77#25;
               nPortes = #77#11;
               aCodigo = #77#3;
               nDto = #77#5;
               nDtoPP = #77#7;
               nDtoEsp = #77#32;
          |FINSI;
          |SI aDocu = "R";
               nError = 1;
          |FINSI;
          #112#0 = aCodigo;
          |LEE 000#112=;
          |SI FSalida = 0; aPais = #112#89; |SINO; aPais = ""; |FINSI;
     |SINO;
          |SI aDocu = "P";
               nTotal = #104#11;
               nPortes = 0;
               aCodigo = #104#4;
               nDto = #104#5;
               nDtoPP = #104#6;
               nDtoEsp = #104#17;
          |FINSI;
          |SI aDocu = "A";
               nTotal = #10#15 - #10#25;
               nPortes = #10#11;
               aCodigo = #10#3;
               nDto = #10#5;
               nDtoPP = #10#7;
               nDtoEsp = #10#32;
          |FINSI;
          |SI aDocu = "R";
               nTotal = #146#15 - #146#25;
               nPortes = #146#11;
               aCodigo = #146#3;
               nDto = #146#5;
               nDtoPP = #146#7;
               nDtoEsp = #146#32;
          |FINSI;
          #24#0 = aCodigo;
          |LEE 000#24=;
          |SI FSalida = 0; aPais = #24#205; |SINO; aPais = ""; |FINSI;
     |FINSI;
     #225#0 = aPais;
     |LEE 000#225=;
     |SI FSalida ! 0; |DDEFECTO #225; |FINSI;

     |SI #2#5 = "S";
          |SI #225#2 ! "S"; |ACABA; |FINSI;
     |FINSI;

     #0#1 = aCodigo;
     #0#2 = #11#13;
     #0#3 = #11#14;
     #0#4 = #225#1;
     #0#5 = #40#1;
     #0#9 = nTotal;
     #0#12 = nPortes;
     #0#10 = 0; ||V.estad.

     |BUCLE Borra1;

     |SI aTipo = "I";     || Compras
          |SI aDocu = "P";
               |BUCLELIN 0 agifa067 #81;
          |FINSI;
          |SI aDocu = "A";
               |BUCLELIN 0 agifa080 #77;
          |FINSI;
          |SI aDocu = "R";
          |FINSI;
     |SINO;
          |SI aDocu = "P";
               |BUCLELIN 0 agifa073 #104;
          |FINSI;
          |SI aDocu = "A";
               |BUCLELIN 0 agifa071 #10;
          |FINSI;
          |SI aDocu = "R";
               |BUCLELIN 0 agifa147 #146;
          |FINSI;
     |FINSI;

     |PINTA #0#10;
|FPRC;

|PROCESO PedCompras;
     |DDEFECTO #0;
     #0#0 = aTipo;
     #0#8 = aDocu;
     #0#6 = aSerie;
     #0#7 = #81#0;
     |LEE 000#0=;
     |SI FSalida ! 0;
          nGrabadas = 0;
          |HAZ CargaValores;
          |SI nGrabadas ! 0;
               |GRABA 020#0n;
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE PedCompras;
     #81#1;
     ;
     aSerie, #2#3;
     aSerie, #2#4;
     ;
     NULL, PedCompras;
|FIN;

|PROCESO AlbCompras;
     |DDEFECTO #0;
     #0#0 = aTipo;
     #0#8 = aDocu;
     #0#6 = aSerie;
     #0#7 = #77#0;
     |LEE 000#0=;
     |SI FSalida ! 0;
          nGrabadas = 0;
          |HAZ CargaValores;
          |SI nGrabadas ! 0;
               |GRABA 020#0n;
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE AlbCompras;
     #77#1;
     ;
     aSerie, #2#3;
     aSerie, #2#4;
     ;
     NULL, AlbCompras;
|FIN;

|PROCESO PedVentas;
     |DDEFECTO #0;
     #0#0 = aTipo;
     #0#8 = aDocu;
     #0#6 = aSerie;
     #0#7 = #104#0;
     |LEE 000#0=;
     |SI FSalida ! 0;
          nGrabadas = 0;
          |HAZ CargaValores;
          |SI nGrabadas ! 0;
               |GRABA 020#0n;
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE PedVentas;
     #104#1;
     ;
     aSerie, #2#3;
     aSerie, #2#4;
     ;
     NULL, PedVentas;
|FIN;

|PROCESO AlbVentas;
     |DDEFECTO #0;
     #0#0 = aTipo;
     #0#8 = aDocu;
     #0#6 = aSerie;
     #0#7 = #10#0;
     |LEE 000#0=;
     |SI FSalida ! 0;
          nGrabadas = 0;
          |HAZ CargaValores;
          |SI nGrabadas ! 0;
               |GRABA 020#0n;
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE AlbVentas;
     #10#1;
     ;
     aSerie, #2#3;
     aSerie, #2#4;
     ;
     NULL, AlbVentas;
|FIN;

|PROCESO Proforma;
     |DDEFECTO #0;
     #0#0 = aTipo;
     #0#8 = aDocu;
     #0#6 = aSerie;
     #0#7 = #146#0;
     |LEE 000#0=;
     |SI FSalida ! 0;
          nGrabadas = 0;
          |HAZ CargaValores;
          |SI nGrabadas ! 0;
               |GRABA 020#0n;
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE Proforma;
     #10#1;
     ;
     aSerie, #2#3;
     aSerie, #2#4;
     ;
     NULL, Proforma;
|FIN;

|PROCESO T20; |TIPO 20;
     |SI FSalida = "OPCION";
          FSalida = "Carga Masiva"
     |SINO;
          |PUSHV 0501 2380;
          |BLANCO 925 1961;
          |PINPA #0#2;
          |PINDA #0#2;
          |ENDATOS #1#2;
          |SI FSalida = 0;
               aCEE = #2#5;
               aTipo = #2#0;
               aDocu = #2#1;
               aSerie = #2#2;

               |ABRE #11; |LEE 000#11p; |CIERRA #11;

               |ABRE #18;
               #18#0 = #11#14;
               |LEE 000#18=;
               |SI FSalida ! 0; |DDEFECTO #18; |FINSI;
               |CIERRA #18;

               |ABRE #40;
               #40#0 = #18#4;
               |LEE 000#40=;
               |SI FSalida ! 0; |DDEFECTO #40; |FINSI;
               |CIERRA #40;

               |ABRET #1;
               |ABRE #19;
               |ABRE #13;
               |ABRE #14;
               |ABRE #40;
               |ABRE #10;
               |ABRE #24;
               |ABRE #77;
               |ABRE #81;
               |ABRE #104;
               |ABRE #112;
               |ABRE #146;
               |ABRE #225;
               nGrabadasTotal = 0;
               |SI aTipo = "I";     || Compras
                    |SI aDocu = "P";
                         |BUCLE PedCompras;
                    |FINSI;
                    |SI aDocu = "A";
                         |BUCLE AlbCompras
                    |FINSI;
                    |SI aDocu = "R";
                    |FINSI;
               |SINO;
                    |SI aDocu = "P";
                         |BUCLE PedVentas;
                    |FINSI;
                    |SI aDocu = "A";
                         |BUCLE AlbVentas;
                    |FINSI;
                    |SI aDocu = "R";
                         |BUCLE Proforma;
                    |FINSI;
               |FINSI;
               |CIERRAT #1;
               |CIERRA #19;
               |CIERRA #13;
               |CIERRA #14;
               |CIERRA #40;
               |CIERRA #10;
               |CIERRA #24;
               |CIERRA #77;
               |CIERRA #81;
               |CIERRA #104;
               |CIERRA #112;
               |CIERRA #146;
               |CIERRA #225;
               aAlfa = "0000Grabadas : " + nGrabadasTotal + " partidas.";
               |MENAV aAlfa;
          |FINSI;
          |POPV;
     |FINSI;
|FPRC;

|PROCESO T2Linea; |TIPO 2;
     #0#9 = #0#9 +. #1#5; |PINTA #0#9;
     #0#10 = #0#10 +. #1#6; |PINTA #0#10;
|FPRC;
