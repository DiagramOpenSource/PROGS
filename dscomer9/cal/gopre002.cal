     || Recalculo Totales Capitulo (copiado del gopre010)
     || El gopreiv2 es una copia de este

|FICHEROS;
     gopre001 #1;
     gopre002 #2;
     gopre003 #3,MANTE;
     gopre004 #4,MANTE;
     goparame #9;
     agifa019 #19;
     dsm00238 #238;
|FIN;

|VARIABLES;
     &enUniFac = 0;
     nSwAbre = 0;
|FIN;

|PROCESO CogeUniFac;
     enUniFac = #4#8;

     #19#0 = #4#5;
     |LEE 000#19=;
     |SI FSalida = 102;
          nSwAbre = 1;
          |ABRE #19;
          |LEE 000#19=;
     |FINSI;
     |SI FSalida = 0; |Y #19#179 = "S"; ||Doble Stock
          |ABRE #238;
          #238#0 = #19#201;
          |LEE 000#238=;
          |SI FSalida = 0; |Y #238#6 = 2;    || Unidad Factura Ventas
               enUniFac = #4#17;
          |FINSI;
          |CIERRA #238;
     |FINSI;
     |SI nSwAbre = 1; |CIERRA #19; |FINSI;
|FPRC;

|PROCESO Rgopre004;
     |HAZ CogeUniFac;
     |SI #1#42 = "+";
          #4#11 = (enUniFac * #4#9) > #4#13;                 ||Importe Venta.
     |SINO;
          #4#11 = (enUniFac * #4#9) / (1 - (#4#13 / 100));
     |FINSI;
     #4#12 = enUniFac * #4#10;

     #3#8  = #3#8  + #4#11;
     #3#9  = #3#9  + #4#12;
     #3#10 = (#3#8 * #3#7);
     #3#11 = (#3#9 * #3#7);
     |SI #1#42 = "+";
          #3#12 = (((#3#8-#3#9)/#3#9)*100);
     |SINO;
          #3#12 = (1 - (#3#9/#3#8))*100;
     |FINSI;

     #1#22 = #3#8;
     #1#23 = #3#9;

     |SI #9#5 = #4#14;
          #3#16 = #3#16 + #4#8;             ||Unidades Mano de Obra.
     |FINSI;

     |GRABA 020#4a;
     |LIBERA #4;
|FPRC;

|DEFBUCLE Rgopre004;
     #4#1;
     ;
     #1#0, #1#1, #2#12, #3#4,      1;
     #1#0, #1#1, #2#12, #3#4, 999999;
     be;
     NULL, Rgopre004;
|FIN;

|PROCESO Rgopre003;

     #3#8 = 0;
     #3#9 = 0;
     #3#16 = 0;

     |BUCLE Rgopre004;
     #1#22 = #3#8;
     #1#23 = #3#9;


     |GRABA 020#3a;
     |LIBERA #3;

     #2#7  = #2#7 + #3#10;                       ||Precio Venta Cap.
     #2#8  = #2#8 + #3#11;                       ||Precio Coste Cap.
     #2#9  = #2#6 * #2#7;                         ||Importe Venta Cap.
     #2#10 = #2#6 * #2#8;                         ||Importe Coste Cap.
     |SI #1#42 = "+";
          #2#11 = ((#2#7-#2#8)/#2#8)*100;         ||Incremento en Capitulo.
     |SINO;
          #2#11 = (1 - (#2#8/#2#7))*100;
     |FINSI;
|FPRC;

|DEFBUCLE Rgopre003;
     #3#1;
     ;
     #1#0, #1#1, #2#12,      1;
     #1#0, #1#1, #2#12, 999999;
     be;
     NULL, Rgopre003;
|FIN;

|PROCESO TotalCapi;
     #2#7 = 0;
     #2#8 = 0;
     |BUCLE Rgopre003;
|FPRC;
