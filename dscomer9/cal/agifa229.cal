|FICHEROS;
     agifa229 #0;
     agifa077 #1;
     agifa079 #2;
     agifa072 #3;
     agifa112 #5;
     agifa080 #7;

     agifa730 #10;
     dsz99984 #11;
     agifa091 #12;
     agifa177 #13;

     agifa321 #45;  || Parametros de Divisas
     agifa322 #46;  || Lineas de Proveedores/Divisas
     agifa323 #47;  || Proveedores/Divisas
     agifa324 #42;  || Divisas
     agifa325 #30;  || Historico de Actualizaciones
     agifa142 #142; || Parametros generales
     dsm00279 #279;
     referen@;
|FIN;

|INCLUYE i_varart;

|VARIABLES;
   nSwTagloma = 0;
   aNume = "";
   aEmp = "";
   aDef = "";
   aDat = "";
   nSwPalmHotel = 0;
   &enNumPal = 0; || variable del modulo PalmHotel
   nSwSematel = 0;
   &enContaFac = 0;
   &eaSERFAC = "";
   &enNUMFAC = 0;

   &enSwNoAcotaFecha = 0;
   &qSerieF     = "";
   &qSerie      = "";
   &qAlbar      = 0;
   &qFecha      = @;
   &qAbono      = "";
   &qFactu      = 0;
   &qSufac      = "";
   nBase        = 0;
   nExe         = 0;

   nSwVtos = 0;

   nBrutoAlb=0;nNeto=0;nIVA=0;nTotLin=0;
   nBaseAlb0=0;nBaseAlb1=0;nBaseAlb2=0;nBaseAlb3=0;nBaseAlb4=0;nBaseAlb5=0;
   nIvaAlb1=0;nIvaAlb2=0;nIvaAlb3=0;nIvaAlb4=0;nIvaAlb5=0;
   nRecAlb1=0;nRecAlb2=0;nRecAlb3=0;nRecAlb4=0;nRecAlb5=0;nReteAlb=0;
   tPortes          = 0;
   tVarios          = 0;
   abo              = 0;
   &enSwMenu        = 0;
   &nDeci_Base      = 0;   || Decimales en Totales de la moneda base
   &nDeci_PreBase   = 0;   || Decimales en el Precio en moneda base
   &nDeci_BaseMx    = 0;   || Decimales en Totales de la moneda base
   &nDeci_PreBaseMx = 0;   || Decimales en el Precio en moneda base
   &nDeci_DivF      = 0;   || Decimales en Totales de la divisa
   &nDeci_BaseMxF   = 0;   || Decimales en Totales de la moneda base
   &nDeci_PreBasMxF = 0;   || Decimales en el Precio en moneda base
   &nDeci_PreDivF   = 0;   || Decimales en el Precio en divisa
   &nDeci_TotVisF   = 0;   || Decimales en Totales visibles (el de la moneda de trabajo)
   &nDeci_TotNoVisF = 0;   || Decimales en Totales no visibles (el de la otra moneda)
   &nDeci_PreVisF   = 0;   || Decimales en el Precio visual. (lineas)
   &nDeci_ImpVisF   = 0;   || Decimales en el Importe visual. (lineas)
   nfecha    = 0;
                         || activadas y la serie es de divisas
   &decim_divisa = 0;  || Decimales de la divisa
   &precio_divisa = 0; || Decimales del precio en divisa
   &decim_base = 0;    || Decimales de la moneda base
   &precio_base = 0;   || Decimales del precio en moneda base
   div_act = 0;        || Divisa actualizada. Me sirve en un PARA para que
                      || no salga hasta que este actualizada (div_act=1)
  &prov_divisa = "";  || Codigo de Proveedor. Se pasa a PROVEED/DIVISAS
  &divisa = "";       || Codigo de Divisa. Se pasa a ACTUAL. de DIVISAS
  factor = 0;         || factor de diferencia de cambio de albaran /factura
  div    = "";        || Variable de pintado
  sali = 0;           || Para condicio de salida
  camb = 0;           || Var. aux. para guardar el cambio
  aAlfa = "";
  fecha1 = 0;
  fecha2 = 0;
  fecha3 = 0;
  fecha4 = 0;
  fecha5 = 0;
  fecha6 = 0;
  mascara = "000000";
  tempo = " ";
  filtro = "-106";
  provee = "";
  contador = 0;
  linea = 0;
  sw = 0;
  base1 = 0;
  base2 = 0;
  base3 = 0;
  base4 = 0;
  base5 = 0;
  base = 0;
  porte = 0;
  vario = 0;
  totiv = 0;
  total = 0;
  descu = 0;
  base0 = 0;
  cuot1 = 0;
  cuot2 = 0;
  cuot3 = 0;
  cuot4 = 0;
  cuot5 = 0;
  cuor1 = 0;
  cuor2 = 0;
  cuor3 = 0;
  cuor4 = 0;
  cuor5 = 0;
  bruto = 0;
  reten = 0;
  nFactura = 0;

  i = 0;
  nCamImp = 0;
  nCamFec = 0;
  nNumRecibos = 0;
|FIN;

|PROCESO PriNumeSer;
     aNume = "0";
     |PARA i = 101; |SI i [ 501; |HACIENDO i = i + 100;
          aAlfa = #2#66 % i;
          aAlfa = "0123456789" - aAlfa;
          |SI FSalida > 0; aNume = #2#66 % i; |SAL; |FINSI;
     |SIGUE;
|FPRC;

|PROCESO PalmHotelCta;
     |HAZ PriNumeSer;
     aAlfa = "40000" + aNume + #2#2;
     #2#12 = aAlfa;
/*
     aAlfa = #2#66 % 101;
     |SI aAlfa = "Z";
          aAlfa = #2#2;
          aAlfa = aAlfa % -105;
          aAlfa = "40000" + (#2#66 % 202) + aAlfa;
          #2#12 = aAlfa;
     |SINO;
          |DFICE aAlfa;
          aAlfa = aAlfa % -205;
          aAlfa = "40000" + (#2#66 % 102) + aAlfa;
          #2#12 = aAlfa;
     |FINSI;
*/
|FPRC;

|PROCESO Tipo8; |TIPO 8;
     |HAZ ControlUsuFC;
     |SI enErr ! 0; |PTEC 807; |ACABA; |FINSI;
|FPRC;

||---------------------------------------------------------------------------
|| Este proceso mira en PG si en la adj. las series de alb. y fac. tienen que
|| ser iguales (parametro 128). Si es asi los campos desde serie, hasta serie
|| y serie factura son iguales y no modificables, de este modo no se pueden
|| facturar albaranes de otras series. Alejandro 27/03/2000
||----------------------------------------------------------------------------
|PROCESO MiraParam; |TIPO 0;
     |SI #142#128 = "S";
          #0#8 = #0#7;
          #0#9 = #0#7;
          |C_M #0#8, 1, "N";
          |C_M #0#9, 1, "N";
          |PINTA #0#8;
          |PINTA #0#9;
     |SINO;
          |C_M #0#8, 1, "S";
          |C_M #0#9, 1, "S";
     |FINSI;
|FPRC;

|PROCESO fecha_pact_lin;
|SI #agifa325#4 > #1#60; |Y sali = 0; || Si la fecha en el hist. > fecha pactada ...
  #3#19 = camb;||Significa que me he pasado, el cambio valido es el anterior
  sali = 1; || Para que no vuelva a entrar en la condicion
  |SI camb = 0; || Si vale 0, significa que no hay actualizaciones anteriores, por tanto...
    |MENAV "0000Se utiliza como fecha de pacto la de la primera actualizacion";
    #3#19 = #agifa325#3; || Asigno el cambio de la 1a. actualizacion
  |FINSI;
|FINSI;
camb = #agifa325#3; ||Me guardo el cambio para la siguiente linea
|FPRC;

|DEFBUCLE fecha_pact; || Recorre el Historico de actualizaciones
#agifa325#2;          || 2a. Clave: divisa,fecha,guia
;
#3#18,"00.00.0000",0;
#3#18,"31.12.9999",99999999;
be;
NULL,fecha_pact_lin;
|FIN;

|PROCESO LeeMonBase2; ||Lee los parametros de la moneda base
     |ABRE #agifa321;
     |ABRE #agifa324;
     |LEE 010#agifa321.p;   ||Leo parametros de divisa
     #2#78 = #agifa321#9;  ||Cojo la Moneda Base de parametros
     #agifa324#0 = #agifa321#9;
     |LEE 010#agifa324.=;         ||Leo la ficha de la moneda base en Divisas
     #2#79 = #agifa324#3;  ||Asigno el cambio de la moneda base con el euro
     decim_base = #agifa324#7;    ||Asigno los decimales de la moneda base
     precio_base = #agifa324#9;   ||Asigno los decimales del precio en la mon. base
     |CIERRA #agifa324;
     |CIERRA #agifa321;
|FPRC;


|PROCESO Param_Divisas3; |TIPO 0;
     |ABRE #agifa324;
     #agifa324#0 = #2#75;
     |LEE 001#agifa324.=;   || Leo la divisa
     |CIERRA #agifa324;

     decim_divisa = #agifa324#7;  || Decimales de la divisa
     precio_divisa = #agifa324#9;  || Decimales en el precio de la divisa
     nDeci_DivF = decim_divisa;   || Decimales de la divisa
     nDeci_PreDivF = precio_divisa;  || Decimales en el precio de la divisa
     |SI #2#77 = "B";   ||Si trabajo con la moneda base ...
          nDeci_PreVisF   = precio_divisa;
          nDeci_ImpVisF   = decim_divisa;
          nDeci_Base     = decim_base;
          nDeci_PreBase  = precio_base;
          nDeci_BaseMx   = decim_base;
          nDeci_PreBaseMx= precio_base;
          nDeci_BaseMxF   = decim_base;
          nDeci_PreBasMxF = precio_base;
          nDeci_TotVisF   = decim_base;
          nDeci_TotNoVisF = decim_divisa;
     |SINO;     ||Si trabajo con la divisa ...
          nDeci_PreVisF   = precio_base;
          nDeci_ImpVisF   = decim_base;
         |SI #2#76 = 1;
              nDeci_BaseMx    = decim_base;    || sin triangulacion
              nDeci_PreBaseMx = precio_base;
              nDeci_BaseMxF   = decim_base;    || sin triangulacion
              nDeci_PreBasMxF = precio_base;
         |SINO;
              nDeci_BaseMx    = #agifa321#10;  || con triangulacion
              nDeci_PreBaseMx = #agifa321#11;
              nDeci_BaseMxF   = #agifa321#10;  || con triangulacion
              nDeci_PreBasMxF = #agifa321#11;
         |FINSI;
          nDeci_Base     = decim_base;
          nDeci_PreBase  = precio_base;
          nDeci_TotVisF   = decim_divisa;
          nDeci_TotNoVisF = decim_base;
     |FINSI;
     |HAZ Deci_IVAC;
|FPRC;

|PROCESO CambioDivisa3; |TIPO 0;
      |SI #agifa112#80 = "S"; || Si el proveedor es de divisa pactada
         |ABRE #agifa323;
         |ABRE #agifa322;
         #agifa322#0 = #2#2;
         #agifa322#2 = #3#18;
         #agifa322#7 = "N";
         |SELECT #2#agifa322;  || Con esta clave ...
         |LEE 011#agifa322.=;  || ... busco periodos no finalizados de esa divisa
         |SI FSalida = 0;  || Si existe un periodo no finalizado
           |PARA div_act = 0; |SI div_act = 0; |HACIENDO;
             #3#18 = #agifa322#2;        || Cargo la divisa ...
             #3#19 = #agifa322#3;        || ...y el cambio
             |LEE 001#agifa322.=;   || Leo las lineas de Proveed/Divisas
             nfecha = #2#1 - #agifa322#6;  || Resto la fecha de fin de periodo a la actual
             |SI nfecha > 0;  || Si la fecha actual supera a la del periodo asignado ...
                |MENAV "0000ATENCION deberia Actualizar primero la divisa en Proveedores/Divisas";
                prov_divisa = #2#2;  || Le envio el cod. de proveedor a "agifa323"
                |SUB_EJECUTA_NP "agifa323.tab";  || Ejecuto Proveedores/Divisas
                |ERROR;
             |SINO;
                div_act = 1;  || Para salir del PARA
             |FINSI;
           |SIGUE;
         |SINO;  || Si no existe un periodo no finalizado para esa divisa
           |MENAV "0000No existe un periodo activo para la divisa en Proveedores / Divisas";
           prov_divisa = #2#2;  || Le paso a "agifa323" el proveedor
           |SUB_EJECUTA_NP "agifa323.tab";  || Ejecuto Proveedores / Divisas
           |ERROR;
           ||#3#18 = #2#78;
           ||#3#19 = #2#79;
         |FINSI;
         |SELECT #1#agifa322;
         |CIERRA #agifa322;
         |CIERRA #agifa323;
      |SINO; || Si el Proveedor no es de Divisa pactada ...
         |ABRE #agifa324; || ...miro directamente en divisas
         #agifa324#0 = #3#18;
         |LEE 001#agifa324.=;
         |SI FSalida = 0; ||Si existe la divisa...
            #3#19 = #agifa324#3; || la recojo
            |SI #agifa324#6 ! -1; || Si los dias de margen de actualizacion no
                                  || son = -1 (indefinidos)
              |PARA div_act = 0; |SI div_act = 0; |HACIENDO;
                 #3#18 = #agifa324#0;        ||Cargo el valor de la divisa
                 #3#19 = #agifa324#3;        ||Cargo el valor del cambio
                 |LEE 001#agifa324.=;  || Leo la divisa
                 nfecha = #2#1 - #agifa324#4;
                 |SI nfecha > #agifa324#6; |Y #agifa324#6 ! -1;  || Si la fecha actual
                    |MENAV "0000ATENCION deberia Actualizar primero la divisa";
                    divisa = #3#18;
                    |SUB_EJECUTA_NP "agifa326.tab";
                    |ERROR;
                 |SINO;
                    div_act = 1;
                 |FINSI;
              |SIGUE;
            |FINSI;
         |FINSI;
         |CIERRA #agifa324;
      |FINSI;
      |HAZ Param_Divisas3;
|FPRC;

|PROCESO agifa080;       || graba serie/factura en las lineas del albaran
     #7#28 = #1#51;      || serie factura
     #7#25 = #1#39;      || serie albaran
     |GRABA 020#7a;
|FPRC;

|CALCULO contfaco;
     |ABRE #2;
     enSwMenu = 1;
     #2#66 = #0#9;
     |SI enContaFac = 0;
         |HAZ ContaFC7;
     |SINO;
         #2#0 = enContaFac;
         enContaFac = 0;
     |FINSI;

     #3#0     = #2#0;
     nFactura = #2#0;
     |CIERRA #2;
|FCAL;

|PROCESO Divisas;
     #2#75 = #1#56; || Divisa (cabecera)
     #2#77 = #1#58; || Moneda de trabajo (cabecera)
     #3#18 = #1#56; || Divisa (lineas)
     #3#19 = #1#57; || Cambio (lineas;
     #3#20 = #1#58; || Moneda de trabajo (lineas)
     |SI #1#59 = "A"; || Si en el alb., el cambio esta pactado al del albaran
       |HAZ Param_Divisas3; || Lee Parametros
     |FINSI;
     |SI #1#59 = "F"; || Si en el alb., el cambio esta pact. a la fecha de la factura
       |HAZ CambioDivisa3; || Coge el valor actual de la divisa
     |FINSI;
     |SI #1#59 = "O"; || Si en el alb. el cambio esta pactado a Otra fecha
       |ABRE #agifa325; || Abre Hist. de actualizaciones
       sali = 0;  || Inicializo
       camb = 0;
       |BUCLE fecha_pact; || Busca en el historico de actualizaciones
       |SI sali = 0;
         |MENAV "0000Se utiliza como fecha de pacto la de la ultima actualizacion";
         #3#19 = camb;
       |FINSI;
       |CIERRA #agifa325;
       |HAZ Param_Divisas3; || Lee Parametros
     |FINSI;

     |HAZ CalLinAgifa079;
|FPRC;

|PROCESO Vtos;
   |HAZ VenciComp;

   |ABRE #agifa091;
   #agifa091#0 = #agifa079#11;
   |LEE 121#agifa091.=;
   |SI 0 ! FSalida;
      |DDEFECTO #agifa091;
   |FINSI;
   |CIERRA #agifa091;

   |SI nSwVtos = 1;
      |DDEFECTO #dsz99984;
      #dsz99984#0 = #agifa079#0;
      #dsz99984#17 = #agifa079#66;
      |PARA i = 1;|SI i [ 12; |HACIENDO i = i + 1;
         nCamImp = i + 32;
         |SI #agifa079#nCamImp# ! 0; nNumRecibos = i; |FINSI;
      |SIGUE;
      |HAZ AbreRecCompra;
      |PARA i = 1;|SI i [ 12; |HACIENDO i = i + 1;
         nCamImp = i + 32;
         |SI #agifa079#nCamImp# = 0; |O i > 12; |SAL; |FINSI;
         |SI i [ 6;
             nCamFec = i + 67;
         |SINO;
             nCamFec = i + 121;
         |FINSI;
         #dsz99984#7 = #agifa079#1;
         #dsz99984#8 = #agifa079#nCamFec#;
         #dsz99984#9 = #agifa079#nCamImp#;
         #dsz99984#1 = i;
         #dsz99984#2 = #agifa079#2;
         #dsz99984#3 = #agifa079#3;
         #dsz99984#4 = #agifa079#9;
         #dsz99984#12 = #agifa079#12;
         #dsz99984#18 = #agifa091#69;      ||Tipo de Recibo
         #dsz99984#21 = #agifa091#69;
         #dsz99984#23 = nNumRecibos;
         #dsz99984#24 = #agifa091#0;
         #dsz99984#25 = #agifa091#1;
         |ABRE #agifa177;
         #agifa177#0 = #dsz99984#18;
         |LEE 000#agifa177.=;
         |CIERRA #agifa177;
         #dsz99984#15 = #agifa177#3;      ||Cuenta
         |HAZ RecCompra;
      |SIGUE;
      |HAZ CierraRecCompra;
   |FINSI;
|FPRC;

|CALCULO adjudica;
     |SI #279#11 = "N";
          |SI #1#1 < "01.07.2010"; |Y #0#6 ] "01.07.2010";
               aAlfa = "0000El % de iva no coincide con el de la factura. Albaran:";
               aAlfa = aAlfa + #1#50 + "/" + (("000000"+ #1#0)%-106);
               |MENAV aAlfa; |ACABA;
          |FINSI;
          |SI #1#1 ] "01.07.2010"; |Y #0#6 < "01.07.2010";
               aAlfa = "0000El % de iva no coincide con el de la factura. Albaran:";
               aAlfa = aAlfa + #1#50 + "/" + (("000000"+ #1#0)%-106);
               |MENAV aAlfa; |ACABA;
          |FINSI;
     |FINSI;

     ||Grabacion De La Linea De Factura;
     |ABRE #3;
     sw = 0;
     |HAZ LeeMonBase2;

     |SI provee ! #1#3; |O #1#56 ! #2#75; |O #1#58 ! #2#77;
          |O #0#14 = "N";
          ||Pasa Por Aqui Cuando Cambia De Proveedor o de Divisa / Moneda de trabajo
          ||Genera Nuevo Numero De Factura De Compras;

          || INICIALIZO LOS TOTALES EN DIVISA
          #2#80 = 0;

          reten = 0;
          linea = 0;
          sw    = 1;
          provee = #1#3;

          |HAZ contfaco;
     |FINSI;

     |ABRE #5;
     #5#0 = #1#3;
     |LEE 111#5=;
     |LIBERA #5;
     |CIERRA #5;
     linea = linea + 1;

     #3#1  = linea;
     #3#2  = #1#0;
     #3#17 = #1#50;
     #3#0  = nFactura;
     #3#16 = #0#9;
     |PINTA 2405, "Generando Factura:";
     |PINTA 2425, #3#16;
     |PINTA 2428, #3#0;
     |PINTA 2436, "Linea:";
     |PINTA 2443, #3#1;

     |HAZ Divisas;

     |PINTA 555,#3#0;
     |PINTA 555,#3#1;

     |GRABA 021#3c;
     |LIBERA #3;


     ||Grabacion De La Cabecera De Factura;
     |DDEFECTO #2;
     |ABRE #2;
     #2#0 = #3#0;
     #2#1 = #0#6;
     #2#2 = #1#3;
     #2#3 = #1#4;
     #2#4 = #1#5;
     #2#66 = #agifa229#9;
     |LEE 101#2=;
     |SI FSalida ! 0; |GRABA 021#2b; |FINSI;
     #2#5 = #1#32;
     #2#6 = #1#7;
     #2#9 = #5#11;
     #2#10 = #5#12;
     #2#11 = #1#8;
     #2#12 = #5#10;
     |SI nSwPalmHotel = 0;
          |HAZ PalmHotelCta;
     |FINSI;
     #2#14 = #5#19;
     #2#15 = #5#20;
     #2#16 = #5#21;
     #2#67 = #1#53;
     #2#103 = #1#53;
     #2#126 = #1#53;
     #2#144 = #1#52;
     #2#74 = qSufac;
     #2#78 = #1#61;
     #2#79 = #1#62;
     |SI PARAMETRO ! ""; #2#45 = "S"; |FINSI;

     eaSERFAC = #2#66;
     enNUMFAC = #2#0;

     ||Grabacion De Numero De Factura Y Facturado S;
     ||En Cabecera De Albaran Compra;
     #1#38 = "S";
     #1#39 = #2#0;
     #1#51 = #2#66;
     #1#77 = #2#1;
     |GRABA 020#1a;
     |BUCLELIN 0agifa080#1;
     |LIBERA #1;

     #2#80 = #2#80 + 1;     || Numero de lineas existentes

     |SI #2#80 = 1;                 || Si hay solo una linea ...
          #2#75 = #3#18;     || ... cojo la divisa ..
          #2#76 = #1#57;
          #2#77 = #3#20;     || ... y la moneda base.
     |FINSI;

     |HAZ CalCabAgifa079;
     |SELECT #3#1;

     |HAZ Vtos;

     |GRABA 020#2a;
     |LIBERA #2;
     |CIERRA #2;

     |SI nSwSematel = 0;
          eaSerie = #2#66;
          enCodigo = #2#0;
          efFecha = #2#1;
          enForma = 1;
          |SUB_EJECUTA_NP "tlfw0001;FACTURA";
     |FINSI;
     |SI nSwTagloma = 0;
          eaSerie = #2#66;
          enCodigo = #2#0;
          |SUB_EJECUTA_NP "tagz0027;FAC";
     |FINSI;
|FCAL;

|DEFBUCLE adjudica;
     #1#3;
     38,42,8;
     #0#0, #0#4, #0#7, #0#2, "N",#0#13,#0#10;
     #0#1, #0#5, #0#8, #0#3, "N",#0#13,#0#11;
     e;
     NULL,adjudica;
|FIN;

|CALCULO adalcom;|TIPO 3;
     |SI #0#12 = "SI";
          |BUCLE adjudica;
     |FINSI;
|FCAL;

|PROCESO Temporal;
   |SELECT #1#agifa077;
   #agifa077#50 = #agifa730#0;
   #agifa077#00 = #agifa730#1;
   |LEE 000#agifa077.=;
   |SI FSalida = 0; |HAZ adjudica; |FINSI;
|FPRC;

|DEFBUCLE Temporal;
#agifa730#2;
;
INICIO;
FINAL;
;
NULL,Temporal;
|FIN;

|PROCESO DefSerie; |TIPO 7;
     |SI #agifa142#185 = "S"; #0#9 = #agifa142#194; |FINSI;
     |PINTA #0#9;
|FPRC;

|PROGRAMA;
     |HAZ EsSematel; nSwSematel = FSalida;
     |HAZ EsPalmHotel; nSwPalmHotel = FSalida;
     |HAZ EsTagloma; nSwTagloma = FSalida;

     |ABRE #142; |LEE 000#142p; |CIERRA #142;
     |ABRE #279; |LEE 000#279p; |CIERRA #279;

     |SI PARAMETRO = "";
          |SI qSerie = ""; |Y qAlbar = 0;
               |CLS;
               |CABEZA "Facturar Albaranes Compra";
               |PINPA #0#0;
               |PINDA #0#0;
               |ENDATOS #1#0;
          |SINO;
               |DDEFECTO #0;
               #0#13 = qAbono;
               #0#7  = qSerie;
               #0#8  = qSerie;
               #0#2  = qAlbar;
               #0#3  = qAlbar;
               |SI enSwNoAcotaFecha = 1;
                    #0#4 = "01.01.0000";
                    #0#5 = "31.12.2999";
               |SINO;
                    #0#4  = qFecha;
                    #0#5  = qFecha;
               |FINSI;
               #0#6  = qFecha;

               |SI qSerieF = "";
                   #0#9  = qSerie;
               |SINO;
                   #0#9  = qSerieF;
               |FINSI;

               #0#12 = "SI";
               |HAZ adalcom;
          |FINSI;
     |SINO;         || Trae un parametro
          nSwVtos = 1;
          aAlfa = PARAMETRO; |QBF aAlfa;
          aAlfa = aAlfa - ",NoVtos";
          |SI FSalida ! 0;
               nSwVtos = 0;
          |FINSI;
          #0#13 = qAbono;
          #0#7  = qSerie;
          #0#8  = qSerie;
          #0#2  = qAlbar;
          #0#3  = qAlbar;
          #0#4  = qFecha;
          #0#5  = qFecha;
          #0#6  = qFecha;
          |SI qSerieF = "";
               #0#9  = qSerie;
          |SINO;
               #0#9  = qSerieF;
          |FINSI;
          #0#12 = "SI";
          aAlfa = PARAMETRO; |QBF aAlfa;
          aAlfa = aAlfa - ",NoVtos";
          |NOME_DAT #10 aAlfa;
          |ABRE #agifa077;
          |BUCLE Temporal;
          |CIERRA #agifa077;
     |FINSI;
|FPRO;

|PROCESO CheqSerie; |TIPO 0;
     |HAZ EsMalpesa;
     |SI FSalida = 0;
          |DDEF aAlfa;
          aAlfa = aAlfa + "malpe081";
          |CARGA_DEF aAlfa, referen@;
          |SI FSalida = 0;
               |ABRE #referen@; |LEE 000#referen@.p; |CIERRA #referen@;
               aEmp = (("00000" + #referen@#23) % -105) + #referen@#24;
               |DESCARGA_DEF #referen@;
          |SINO;
               |MENAV "0000ERROR al cargar 'malpe081";
          |FINSI;
          |DBASS aAlfa;
          aDef = aAlfa + "contagen/def/caivases";
          aDat = aAlfa + "contagen/fich/" + aEmp + "/";
          |CARGA_DEF aDef, referen@;
          |SI FSalida = 0;
               |PATH_DAT #referen@#aDat#;
               |ABRE #referen@;
               #referen@#0 = #0#9;
               #referen@#1 = "S";
               |LEE 000#referen@.=;
               |SI FSalida ! 0;
                    |MENAV "0000No existe serie en contabilidad...";
                    |ERROR;
               |FINSI;
               |CIERRA #referen@;
               |DESCARGA_DEF #referen@;
          |SINO;
               |MENAV "0000ERROR al cargar 'caivases'";
          |FINSI;
     |FINSI;
|FPRC;
