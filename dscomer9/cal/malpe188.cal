|FICHEROS;
     malpe188 #0;
     malpe189 #1,MANTE;
     malpe004 #4;
     malpe006 #6;   || Transportista
     agifa019 #19;
     malpe085 #85;  || Aux Arti/Alma
     malpe090 #90;  || Alba
     malpe091 #91;
     malpe109 #109; || Suplementos
     malpe187 #187; || Tarifas
|FIN;

|VARIABLES;
     aDAge = "";
     aHAge = "";
     aDTra = "";
     aHTra = "";
     aAlfa = "";
     nHay = 0;
 {-1}aMenu = "";
     aMenu1 = "2400";
     aMenu2 = "1";
     aMenu3 = "";
     aMenu4 = "RAC";
     aMenu5 = " Revisar ";
     aMenu6 = " Aceptar ";
     aMenu7 = " Cancelar ";
     i = 0;
     aTipo = "";
     nPrecio = 0;
     nPeso = 0;
     nConta = 0;
     nTotaPeso = 0;
     nClavePeso = 0;
|FIN;

|PROCESO Suplemento;
     |DDEFECTO #6;
     |ABRE #6;
     #6#0 = #4#27;
     |LEE 000#6=;
     |CIERRA #6;

     |ABRE #109;
     |LEE 000#109u;
     |SI FSalida = 0;
          nConta = #109#0 + 1;
     |SINO;
          nConta = 1;
     |FINSI;
     |DDEFECTO #109;
     #109#0 = nConta;
     #109#9 = #1#29;
     #109#14 = nPrecio;
     #109#12 = #4#9;
     #109#13 = 1;        || Almacen
     #109#10 = "Suplemento orden de carga:" + #4#0 + "/" + (("000000" + #4#1) % -106);
     #109#1 = #6#0;
     #109#2 = #6#1;
     #109#3 = #6#2;
     #109#4 = #6#3;
     #109#5 = #6#4;
     #109#6 = #6#5;
     #109#7 = #6#6;
     #109#8 = #6#7;
     |GRABA 020#109n;
     |CIERRA #109;
|FPRC;

|PROCESO Precio;
     #187#0 = #1#27;
     #187#1 = nPeso;
     |LEE 000#187];
     |SI FSalida = 0; |Y #187#0 = #1#27;
          |SI                    #1#31 [ #187#2; nPrecio = #187#3; aTipo = #187#4; |FINSI;
          |SI #1#31 > #187#2; |Y #1#31 [ #187#5; nPrecio = #187#6; aTipo = #187#7; |FINSI;
          |SI #1#31 > #187#5; |Y #1#31 [ #187#8; nPrecio = #187#9; aTipo = #187#10; |FINSI;
          |SI #1#31 > #187#8; |Y #1#31 [ #187#11; nPrecio = #187#12; aTipo = #187#13; |FINSI;
          |SI #1#31 > #187#11; |Y #1#31 [ #187#14; nPrecio = #187#15; aTipo = #187#16; |FINSI;
          |SI #1#31 > #187#14; |Y #1#31 [ #187#17; nPrecio = #187#18; aTipo = #187#19; |FINSI;
          |SI #1#31 > #187#17; |Y #1#31 [ #187#20; nPrecio = #187#21; aTipo = #187#22; |FINSI;
          |SI #1#31 > #187#20; |Y #1#31 [ #187#23; nPrecio = #187#24; aTipo = #187#25; |FINSI;
          |SI #1#31 > #187#23; |Y #1#31 [ #187#26; nPrecio = #187#27; aTipo = #187#28; |FINSI;
     |SINO;
          nPrecio = 0;
          aTipo = "";
     |FINSI;
|FPRC;

|PROCESO CalcuPeso;
     |DDEFECTO #85;
     #85#0 = #91#2;
     #85#1 = #91#3;
     #85#2 = #91#49;
     |LEE 000#85=;
     nPeso = #85#3;
     |SI FSalida ! 0;
          |DDEFECTO #19;
          #19#0 = #91#2;
          |LEE 000#19=;
          nPeso = #19#102;
     |FINSI;
     nTotaPeso = nTotaPeso + nPeso
|FPRC;

|PROCESO Tmp;
     #90#52 = #1#0;
     #90#0 = #1#17;
     |LEE 121#90=;
     |SI FSalida = 0; |Y #90#51 = "N";
          #4#0 = #1#0;
          #4#1 = #1#1;
          |LEE 121#4=;
          |SI FSalida = 0;
               nTotaPeso = 0;
               |BUCLELIN 0 CalcuPeso #90;
               #90#51 = "S";       ||Facturable
               |GRABA 020#90a;
               |HAZ Precio;
               |SI aTipo = "P";
                    nPrecio = nPrecio * nTotaPeso;
               |FINSI;
               |HAZ Suplemento;
               #4#29 = #1#29;      || Fec
               #4#30 = #1#30;      || Matri
               #4#32 = #109#0;
               |GRABA 020#4a;
          |FINSI;
          |LIBERA #90;
     |FINSI;
|FPRC;

|DEFBUCLE Tmp;
     #1#1;
     33;
     "     ", 000001, "*";
     "zzzzz", 999999, "*";
     ;
     NULL, Tmp;
|FIN;

|PROCESO Acepta;
     |ABRE #187;
     |ABRE #90;
     |ABRE #85;
     |ABRE #19;
     |ABRE #4;
     |BUCLE Tmp;
     |CIERRA #4;
     |CIERRA #19;
     |CIERRA #85;
     |CIERRA #90;
     |CIERRA #187;
|FPRC;

|PROCESO Tecla; |TIPO 0;
     |SI FSalida = 0; |Y #1Campo = Contenido;
          |SI #1#33 = "*";
               #1#33 = "";
          |SINO;
               #1#33 = "*"; |PINTA #1#33;
             |ET E29;
               |SI #1#29 = "01.01.0000"; #1#29 = ~; |FINSI;
               |ENCAMPO #29#1;
               |SI FSalida = 0;
                 |ET E30;
                    |ENCAMPO #30#1;
                    |SI FSalida = 2; |VETE E29; |FINSI;
               |FINSI;
               |SI FSalida = 0;
                    |ENCAMPO #31#1;
                    |SI FSalida = 2; |VETE E30; |FINSI;
               |FINSI;
          |FINSI;
          |GRABA 020#1a;
          |PINTA #1#33;
     |FINSI;
|FPRC;

|PROCESO PonNom; |TIPO 0;
     |SI #0Campo = "      ";
          |SI Campo = 0;
               #0#2 = "<Todas las agencias>";
               |PINTA #0#2;
          |SINO;
               #0#3 = "<Todos los transportistas>";
               |PINTA #0#3;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Ordenes;
     #90#52 = #4#0;
     #90#0 = #4#17;
     |LEE 000#90=;
     |SI FSalida ! 0; |O #90#51 = "S"; |ACABA; |FINSI;

     |PARA i = 0; |SI i [ 32; |HACIENDO i = i + 1;
          #1i = #4i;
     |SIGUE;
     |GRABA 020#1n;
     nHay = 1;
|FPRC;

|DEFBUCLE Ordenes;
     #4#1;
     28, 27, 32, 15;
     "     ", 000000, aDAge, aDTra, 0, "S";
     "zzzzz", 999999, aHAge, aHTra, 0, "S";
     ;
     NULL, Ordenes;
|FIN;

|PROCESO Min;
     #1#0 = "     ";
     #1#1 = 0;
|FPRC;

|PROCESO Max;
     #1#0 = "zzzzz";
     #1#1 = 999999;
|FPRC;

|PROCESO Tipo3; |TIPO 3;
     aAlfa = Usuario;
     |NOME_DAT #1 aAlfa;
     |DELINDEX #1;

     |SI #0#0 = "      ";
          aDAge = "      ";
          aHAge = "zzzzzz";
     |SINO;
          aDAge = #0#0;
          aHAge = #0#0;
     |FINSI;
     |SI #0#1 = "      ";
          aDTra = "      ";
          aHTra = "zzzzzz";
     |SINO;
          aDTra = #0#1;
          aHTra = #0#1
     |FINSI;

     |ABRE #1;
     |ABRE #90;
     |BUCLE Ordenes;
     |CIERRA #1;
     |CIERRA #90;
     |SI nHay = 0;
          |MENAV "0000No hay ordenes de carga...";
     |SINO;
          |PINPA #0#1;
          |PARA; |SI; |HACIENDO;
               |ENTLINEAL #1#1, 1, 4, 22, 0, Min, Max;
               |MENU aMenu;
               |SI FSalida [ 0; |O FSalida = 3; |SAL; |FINSI;
               |SI FSalida = 2; |HAZ Acepta; |SAL; |FINSI;
          |SIGUE;
     |FINSI;
     |DELINDEX #1;
|FPRC;
