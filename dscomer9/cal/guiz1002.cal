|FICHEROS;
     infom000 #1;
     dsm00003 #3;
     dsm00020 #20;
     agifa024 #24;
     agifa025 #25;
     agifa040 #40;
     agifa091 #91;
     agifm001 #101;
     agifm002 #102;
     agifa142 #142;
     infom003@;
     infom004@;
     dscam038@;
     dscam023@;
     dscam024@;
|FIN;

|VARIABLES;
     &eaIBAN = "";
     &eaCta = "";
     &eaPais = "";
     &eaFic = "";
     &eaAlfa = "";
     &eaAsunto = "";
     nExiste = 0;
     aAlfa = "";
     nSal = 0;
     nModo = 0;
     aPath = "";
     i = 0;
     j = 0;
     {90}Valor = "";
     aTab = "";
     nConta = 0;
     aCar = "";
     aFic = "";
     aTmp = "";
     nHandle = 0;
     aCP = "";
     aCta = "";
     nCod = 0;
     aCod = "";
     aDes = "";
     nPos = 0;
     nEmp = 0;
     nHay = 0;
     aLon = "";
     aNuevo = "";
     nCar = 0;
|FIN;

|PROCESO QuitaRarosImp;
     aAlfa = eaAlfa;
     aLon = aAlfa % A0;
     aNuevo = "";
     |PARA i = 0; |SI i [ aLon; |HACIENDO i = i + 1;
          nPos = (i + 1) * 100 + 1;
          aCar = aAlfa % nPos;
          nCar = &aCar;
          |SI nCar = 241; aCar = "¤"; |FINSI;
          |SI nCar = 209; aCar = "¥"; |FINSI;
          |SI nCar = 208; aCar = "¥"; |FINSI; ||no se porque
          |SI nCar = 225; aCar = " "; |FINSI;
          |SI nCar = 233; aCar = "‚"; |FINSI;
          |SI nCar = 237; aCar = "¡"; |FINSI;
          |SI nCar = 243; aCar = "¢"; |FINSI;
          |SI nCar = 250; aCar = "£"; |FINSI;
          |SI nCar = 193; aCar = "A"; |FINSI;
          |SI nCar = 201; aCar = "E"; |FINSI;
          |SI nCar = 205; aCar = "I"; |FINSI;
          |SI nCar = 211; aCar = "O"; |FINSI;
          |SI nCar = 218; aCar = "U"; |FINSI;
          |SI nCar = 186; aCar = "§"; |FINSI;
          |SI nCar = 170; aCar = "¦"; |FINSI;
          aNuevo = aNuevo + aCar;
     |SIGUE;
     eaAlfa = aNuevo;
|FPRC;

|PROCESO Letra;
     aAlfa = &13;
     |SI aCar = aAlfa; |ACABA; |FINSI;

     aTab = &10;
     |SI aCar = aTab;
          nConta = nConta + 1;
          |SI nConta < 90;         || --- por si :)
               IValor = IValor + 1;
          |FINSI;
     |SINO;
          Valor = Valor + aCar;
     |FINSI;
|FPRC;

|PROCESO LeeDatos;
     |SI #1#24 = "S";
          aFic = #1#12; |QBF aFic;
          aFic = aFic + "_cuerpo_.txt"; || Nombre del fichero con el asunto
     |SINO;
          aFic = eaFic;
     |FINSI;

     |XABRE "B", aFic, nHandle;
     |SI FSalida ] 0;
          |XLEE nHandle, 250, aTmp;
          |PARA; |SI FSalida > 0; |HACIENDO;
               |PARA; |SI aTmp ! ""; |HACIENDO;
                    aCar = aTmp % 101;
                    |SI aTmp = aCar;
                         aTmp = ""; ||con un caracter falla: "aTmp = aTmp % 2"
                    |SINO;
                         aTmp = aTmp % 2;
                    |FINSI;
                    |HAZ Letra;
               |SIGUE;
               |XLEE nHandle, 250, aTmp;
          |SIGUE;
          |XCIERRA nHandle;
          |SI #1#24 = "S"; |FBORRA aFic; |FINSI;
     |FINSI;
     |HAZ Letra;

     IValor = Valor1<-;
     |PARA j = 1; |SI j < 90; |HACIENDO j = j + 1;
          eaAlfa = Valor;
          |HAZ QuitaRarosImp;
          Valor = eaAlfa;
          IValor = IValor + 1;
     |SIGUE;
|FPRC;

|PROCESO BuscaAge;
     |QBF aAlfa;
     nCod = 0;
     |ABRE #20;
     |LEE 000#20p;
     |PARA; |SI FSalida = 0; |HACIENDO;
          aDes = #20#1; |QBF aDes;
          |SI aDes = aAlfa;
               nCod = #20#0; |SAL;
          |FINSI;
          |LEE 000#20s;
     |SIGUE;
     |CIERRA #20;
|FPRC;

|PROCESO BuscaRepre;
     |QBF aAlfa;
     aCod = "";
     |ABRE #25;
     |LEE 000#25p;
     |PARA; |SI FSalida = 0; |HACIENDO;
          aDes = #25#1; |QBF aDes;
          |SI aDes = aAlfa;
               aCod = #25#0; |SAL;
          |FINSI;
          |LEE 000#25s;
     |SIGUE;
     |CIERRA #25;
|FPRC;

|PROCESO BuscaFP;
     |QBF aAlfa;
     aCod = "";
     |ABRE #91;
     |LEE 000#91p;
     |PARA; |SI FSalida = 0; |HACIENDO;
          aDes = #91#1; |QBF aDes;
          |SI aDes = aAlfa;
               aCod = #91#0; |SAL;
          |FINSI;
          |LEE 000#91s;
     |SIGUE;
     |CIERRA #91;
|FPRC;

|PROCESO dscam024;
     nHay = 1;
     #dscam024@#9 = #dscam023@#19 - #dscam023@#20;
     |GRABA 020#dscam024@.a;
     |LIBERA #dscam024@;
|FPRC;

|DEFBUCLE dscam024;
     #dscam024@#1002;
     ;
     #dscam023@#0, 01;
     #dscam023@#0, 99;
     be;
     NULL, dscam024;
|FIN;

|PROCESO dscam038;
     |DBASS aAlfa;
     aAlfa = aAlfa + "dscarter/fich/" + (("00000" + #dscam038@#5) % -105) + "/";
     |PATH_DAT #dscam023@#aAlfa#;
     |PATH_DAT #dscam024@#aAlfa#;
     |DDEFECTO #dscam023@;
     |ABRE #dscam023@;
     #dscam023@#0 = #24#0;
     |LEE 101#dscam023@.=;
     |SI FSalida ! 0; |GRABA 020#dscam023@.b; |FINSI;
     #dscam023@#1 = #24#1;
     #dscam023@#16 = ~;
     #dscam023@#15 = #infom003@#50;
     #dscam023@#19 = #dscam023@#13 + #dscam023@#15 + #dscam023@#17;
     |GRABA 020#dscam023@.a;
     |CIERRA #dscam023@;
     nHay = 0;
     |BUCLE dscam024;
     |SI nHay = 0;
          |DDEFECTO #dscam024@;
          |ABRE #dscam024@;
          #dscam024@#0 = #dscam023@#0;
          #dscam024@#1 = 1;
          |GRABA 020#dscam024@.n;
          |CIERRA #dscam024@;
          |BUCLE dscam024;
     |FINSI;
|FPRC;

|DEFBUCLE dscam038;
     #dscam038@#2004;
     ;
     00001, nEmp, " ", 001;
     99999, nEmp, "z", 999;
     ;
     NULL, dscam038;
|FIN;

|PROCESO CreaCliCarte;
     |DBASS aPath;
     aAlfa = aPath + "dscarter/def/" + "dscam038";
     |CARGA_DEF aAlfa, dscam038@;
     |SI FSalida ! 0; |ACABA; |FINSI;
     aAlfa = aPath + "dscarter/def/" + "dscam023";
     |CARGA_DEF aAlfa, dscam023@;
     |SI FSalida ! 0; |ACABA; |FINSI;
     aAlfa = aPath + "dscarter/def/" + "dscam024";
     |CARGA_DEF aAlfa, dscam024@;
     |SI FSalida ! 0; |ACABA; |FINSI;
     aAlfa = aPath + "dscarter/fich/";
     |PATH_DAT #dscam038@#aAlfa#;

     |DFICE aAlfa; aAlfa = aAlfa % -205; nEmp = aAlfa;
     |BUCLE dscam038;

     |DESCARGA_DEF #dscam024@;
     |DESCARGA_DEF #dscam023@;
     |DESCARGA_DEF #dscam038@;
|FPRC;

|PROCESO Crea1Dir;
     |ABRE #101;
     #101#0 = #24#3;
     |LEE 000#101=; || Zona
     |CIERRA #101;
     |ABRE #102;
     #102#0 = #24#189;
     |LEE 000#102=; ||Ruta
     |CIERRA #102;
     |ABRE #25;
     #25#0 = #24#25;
     |LEE 000#25=;
     |CIERRA #25;

     |ABRE #3;
     |DDEFECTO #3;
     #3#0 = #24#0;
     #3#1 = 1;
     |LEE 101#3=;
     |SI FSalida ! 0; |GRABA 021#3b; |FINSI;
     #3#2 = #24#5;
     #3#3 = #24#6;
     #3#4 = #24#178;
     #3#5 = #24#179;
     #3#6 = #24#180;
     #3#7 = #24#7;
     #3#8 = #24#14;
     #3#10 = #24#2;
     #3#11 = #24#3;
     #3#12 = #101#1;
     #3#13 = #24#17;
     #3#14 = #24#25;
     #3#15 = #25#1;
     #3#16 = #24#189;
     #3#17 = #102#1;
     #3#19 = #24#205;
     #3#20 = #24#196;
     #3#21 = #24#18;
     #3#22 = #24#36;
     #3#23 = #24#211;
     #3#24 = #24#197;
     |GRABA 021#3a;
     |CIERRA #3;
|FPRC;

|PROCESO ModiCli;
     |DDEFECTO #24;
     |ABRE #24;
     #24#0 = #infom003@#2;
     |LEE 101#24=;
     nExiste = FSalida;
     |SI FSalida ! 0; |GRABA 020#24b; |FINSI;

     || esto mismo esta en el guim0005
     |ABRE #142; |LEE 000#142p; |CIERRA #142;
     #24#3 = #infom003@#1;
     #24#15 = #infom003@#3;
     #24#1 = #infom003@#6;
     #24#2 = #infom003@#7;
     #24#36 = #infom003@#8;
     #24#8 = #infom003@#10;
     #24#9 = #infom003@#11;

     aCP = #infom003@#12; |QBT aCP;
     aCP = ("00000" + aCP) % -105;
     aAlfa = aCP % 102; #24#181 = aAlfa;
     aAlfa = aCP % 303; #24#182 = aAlfa;
     #24#183 =  aCP;
     #24#211 = aCP;

     |DDEFECTO #40;
     |ABRE #40;
     #40#0 = #24#181;
     |LEE 000#40=;
     |CIERRA #40;
     #24#10 = #40#1;

     #24#5 = #24#8;
     #24#6 = #24#9;
     #24#178 = #24#181;
     #24#179 = #24#182;
     #24#180 = #24#183;
     #24#7 = #24#10;

     #24#11 = #24#8;
     #24#12 = #24#9;
     #24#184 = #24#181;
     #24#185 = #24#182;
     #24#186 = #24#183;
     #24#13 = #24#10;

     aCta = #infom003@#13; |QBT aCta;
     aAlfa = aCta % 104; #24#32 = aAlfa;
     aAlfa = aCta % 604; #24#33 = aAlfa;
     aAlfa = aCta % 1102; #24#31 = aAlfa;
     aAlfa = aCta % 1410; #24#30 = aAlfa;

     aAlfa = #infom003@#14;
     |HAZ BuscaFP;
     #24#20 = aCod;

     aAlfa = #infom003@#16;
     |HAZ BuscaAge;
     #24#35 = aAlfa;
     #24#202 = nCod;

     |SI #infom003@#19 ! 0; #24#17 = #infom003@#19; |FINSI;
     |SI #infom003@#20 ! 0; #24#18 = #infom003@#20; |FINSI;
     #24#197 = #infom003@#21;
     #24#53 = #infom003@#22;
     #24#159 = #infom003@#53;

     nCod = #24#0;
     aAlfa = "000000000000" + nCod;
     |QBT aAlfa;
     nPos = -(100 + (#142#11 - 2));
     aAlfa = aAlfa % nPos;
     aAlfa = "43" + aAlfa;
     #24#16 = aAlfa;

     |SI nExiste ! 0;
          #24#40 = "S";  ||Valorado
          #24#44 = 1;    ||Copias
     |FINSI;

     aAlfa = #infom003@#17;
     |HAZ BuscaRepre;
     #24#25 = aCod;||Repre1

     aAlfa = #infom003@#18;
     |HAZ BuscaRepre;
     #24#26 = aCod; ||Repre2

     #24#47 = #infom003@#62; ||R.Equiv.

     #24#8 = #infom003@#63;
     #24#9 = #infom003@#64;
     aCP = #infom003@#65; |QBT aCP;
     aCP = ("00000" + aCP) % -105;
     aAlfa = aCP % 102; #24#181 = aAlfa;
     aAlfa = aCP % 303; #24#182 = aAlfa;
     #24#183 =  aCP;
     #24#211 = aCP;
     |DDEFECTO #40;
     |ABRE #40;
     #40#0 = #24#181;
     |LEE 000#40=;
     |CIERRA #40;
     #24#10 = #40#1;

     #24#208 = "P";

     #24#22 = #infom003@#68;
     #24#23 = #infom003@#69;
     #24#24 = #infom003@#70;

     ||aAlfa = ("00" + #infom003@#37) % -102;
     #24#158 = #infom003@#37;

     aAlfa = #24#205; |QBF aAlfa;
     |SI aAlfa = "";
          #24#205 = "ES";
     |FINSI;

     eaCta = (#24#32 % 104) + (#24#33 % 104) + #24#31 + (#24#30 % 110);
     eaPais = #24#205
     |HAZ CalcuIBAN;
     #24#215 = eaIBAN;

     |GRABA 020#24a;
     |CIERRA #24;
|FPRC;

|PROCESO ModiDirEnt;
     |DDEFECTO #3;
     |ABRE #3;
     #3#0 = #infom003@#2;
     #3#1 = #infom004@#2;
     |LEE 101#3=;
     |SI FSalida ! 0; |GRABA 020#3b; |FINSI;
     #3#10 = #infom004@#3;
     #3#2 = #infom004@#4;

     aCP = #infom004@#5; |QBT aCP;
     aCP = ("00000" + aCP) % -105;
     aAlfa = aCP % 102; #3#4 = aAlfa;
     aAlfa = aCP % 303; #3#5 = aAlfa;
     #3#6 =  aCP;
     #3#23 = aCP;

     #3#3 = #infom004@#6;

     |DDEFECTO #40;
     |ABRE #40;
     #40#0 = #3#4;
     |LEE 000#40=;
     |CIERRA #40;
     #3#7 = #40#1;

     #3#13 = #infom004@#8;
     #3#21 = #infom004@#9;
     #3#20 = #infom004@#10;
     |GRABA 020#3a;
     |CIERRA #3;
|FPRC;

|DEFBUCLE ActDirEnt;
     #infom004@#2001;
     ;
     nCod;
     nCod;
     ;
     NULL, ModiDirEnt;
|FIN;

|PROCESO DirEnt;
     IValor = Valor1<-;
     |PARA i = 1; |SI i < 90; |HACIENDO i = i + 1;
          Valor = "";
          IValor = IValor + 1;
     |SIGUE;

     |DDEF aPath;
     aAlfa = aPath + "infom003";
     |CARGA_DEF aAlfa, infom003@;
     |SI FSalida = 0;
          aAlfa = aPath + "infom004";
          |CARGA_DEF aAlfa, infom004@;
          |SI FSalida = 0;
               IValor = Valor1<-;
               |HAZ LeeDatos;
               |DDEFECTO #infom004@;
               |ABRE #infom004@;
               |ABRE #infom003@;
               #infom004@#0 = Valor1;
               |LEE 101#infom004@.=;
               |SI FSalida ! 0; |GRABA 020#infom004@.b; |FINSI;
               IValor = Valor1<-;
               IValor = IValor + 1;
               |PARA i = 1; |SI i [ 10; |HACIENDO i = i + 1;
                    #infom004@#i# = Valor;
                    IValor = IValor + 1;
               |SIGUE;
               aAlfa = ("00000" + Valor6) % -105; || CP
               #infom004@#5 = aAlfa; || CP
               |GRABA 020#infom004@.a;

               #infom003@#0 = #infom004@#1;
               |LEE 000#infom003@.=;
               |SI FSalida = 0; |Y #infom003@#2 ! "      ";
                    |HAZ ModiCli;
                    nCod = #infom003@#0;;
                    |BUCLE ActDirEnt;
                    |HAZ Crea1Dir;
                    |HAZ CreaCliCarte;
               |FINSI;

               |CIERRA #infom003@;
               |CIERRA #infom004@;
               |DESCARGA_DEF #infom004@;
          |SINO;
               |MENAV "0000Error al cargar 'infom004'";
          |FINSI;
          |DESCARGA_DEF #infom003@;
     |SINO;
          |MENAV "0000Error al cargar 'infom003'";
     |FINSI;
|FPRC;

|PROCESO Cliente;
     IValor = Valor1<-;
     |PARA i = 1; |SI i < 90; |HACIENDO i = i + 1;
          Valor = "";
          IValor = IValor + 1;
     |SIGUE;

     |DDEF aPath;
     aAlfa = aPath + "infom003";
     |CARGA_DEF aAlfa, infom003@;
     |SI FSalida = 0;
          aAlfa = aPath + "infom004";
          |CARGA_DEF aAlfa, infom004@;
          |SI FSalida = 0;
               IValor = Valor1<-;
               |HAZ LeeDatos;
               |DDEFECTO #infom003@;
               |ABRE #infom003@;
               #infom003@#0 = Valor1;
               |LEE 101#infom003@.=;
               |SI FSalida ! 0; |GRABA 020#infom003@.b; |FINSI;
               IValor = Valor1<-;
               IValor = IValor + 1;
               |PARA i = 1; |SI i [ 72; |HACIENDO i = i + 1;
                    |SI i = 2;
                         aAlfa = Valor;
                         |QBF aAlfa;
                         aAlfa = ("000000" + aAlfa) % -106;
                         #infom003@#i# = aAlfa;
                    |SINO;
                         #infom003@#i# = Valor;
                    |FINSI;
                    IValor = IValor + 1;
               |SIGUE;
               aAlfa = ("00000" + Valor13) % -105;
               #infom003@#12 = aAlfa; || CP
               |GRABA 020#infom003@.a;
               |SI nModo = 2; |Y #infom003@#2 ! "      ";
                    |HAZ ModiCli;
                    nCod = #infom003@#0;;
                    |BUCLE ActDirEnt;
                    |HAZ Crea1Dir;
                    |HAZ CreaCliCarte;
               |FINSI;
               |CIERRA #infom003@;
               |DESCARGA_DEF #infom004@;
          |SINO;
               |MENAV "0000Error al cargar 'infom004'";
          |FINSI;
          |DESCARGA_DEF #infom003@;
     |SINO;
          |MENAV "0000Error al cargar 'infom003'";
     |FINSI;
|FPRC;

|PROCESO InforprCliDir;
     nConta = 0;
     nSal = 1;
     aAlfa = eaAsunto - "Alta cliente";
     |SI FSalida > 0;
          nSal = 0;
          nModo = 1;
          |HAZ Cliente;
     |FINSI;
     aAlfa = eaAsunto - "Modificacion cliente";
     |SI FSalida > 0;
          nSal = 0;
          nModo = 2;
          |HAZ Cliente;
     |FINSI;
     aAlfa = eaAsunto - "Alta direccion entrega";
     |SI FSalida > 0;
          nSal = 0;
          nModo = 1;
          |HAZ DirEnt;
     |FINSI;
     aAlfa = eaAsunto - "Modificacion direccion entrega";
     |SI FSalida > 0;
          nSal = 0;
          nModo = 2;
          |HAZ DirEnt;
     |FINSI;
     FSalida = nSal;
|FPRC;
