|FICHEROS;
     vallcm03 #0;
     vallcm02 #2;
     dsm00003 #3;
     agifa007 #7;
     agifa010 #10;
     agifa024 #24;
     agifa025 #25;
|FIN;

|VARIABLES;
     &enImpVallcorba = 0;
     &ser_alb = "";
     &num_alb = 0;
     &abono = "";
     &eaMenuImpre = "";
     &eaImpresora = "";
     &eaFicImpre = "";
     aIP = "";
     aAlfa = "";
     aDjunto = "";
     aServidor = "";
     aFrom = "";
     aSubject = "";
     aMensaje = "";
     aTo = "";
     aEmail1 = "";
     aEmail2 = "";
     aDRei = "";
     aHRei = "";
     {1}aBaseLocal;
     aInf = "";
|FIN;

|PROCESO VallcorbaMailAlbT;
     |DFICE aAlfa;
     aDjunto = aAlfa + #10#52 + "_" + (("000000" + #10#0)%-106)+ ".txt";
     |QBT aDjunto;
     eaFicImpre = aDjunto;
     ser_alb = #10#52;
     num_alb = #10#0;
     abono = #10#43;
     eaMenuImpre = "N";
     |SUB_EJECUTA_NP "agifa014";

     aMensaje = "Le adjuntamos  TXT con Albarán:" +  #10#52 + "/" + (("000000" + #10#0)%-106);
     |HAZ VallcorbaMailAlb;
|FPRC;

|PROCESO VallcorbaMailAlbP;
     aIP = ""; |IP_REMOTO aIP;
     |SI aIP = "";
          |DFICE aAlfa;
          aDjunto = aAlfa + #10#52 + "_" + (("000000" + #10#0)%-106)+ ".pdf";
          |QBT aDjunto;
          eaImpresora = "CrystalReport;" + aDjunto;
          ser_alb = #10#52;
          num_alb = #10#0;
          abono = #10#43;
          eaMenuImpre = "N";
          |SUB_EJECUTA_NP "agifa014";
     |SINO;
          |DFICE aAlfa;
          aDjunto = aAlfa + #10#52 + "_" + (("000000" + #10#0)%-106)+ ".pdf";
          |QBT aDjunto;
          |ESPECIFICA "RBASS", aBaseLocal;
          aAlfa = aBaseLocal1 + #10#52 + "_" + (("000000" + #10#0)%-106)+ ".pdf";
          |QBT aAlfa;
          eaImpresora = "CrystalReport;" + aAlfa;
          ser_alb = #10#52;
          num_alb = #10#0;
          abono = #10#43;
          eaMenuImpre = "N";
          |SUB_EJECUTA_NP "agifa014";
          |RECIBE_FICHERO aAlfa, aDjunto;
          |RFBORRA aAlfa;
     |FINSI;
     aMensaje = "Le adjuntamos  PDF con Albarán:" +  #10#52 + "/" + (("000000" + #10#0)%-106);
     |HAZ VallcorbaMailAlb;
|FPRC;

|PROCESO VallcorbaMailAlb;
     eaMenuImpre = "";
     eaImpresora = "";
     eaFicImpre = "";

     aIP = "";
     |SI #2#2 = "L";
          |IP_REMOTO aIP;
     |SINO;
          |SI #2#2 = "S";
               |IP_LOCAL aIP;
          |SINO;
               aIP = #2#3; |QBF aIP;
          |FINSI;
     |FINSI;
     aServidor = #2#4; |QBF aServidor;
     aFrom = #2#5; |QBF aFrom;
     aSubject = "Albarán:" +  #10#52 + "/" + (("000000" + #10#0)%-106);

     |XABRE "E", aDjunto, 0;
     |SI FSalida ] 0;
          aTo = aEmail1;
          |SI aTo ! "";
               |DSCORREO_ENVIA aIP, aServidor, aFrom, aTo, aSubject, aMensaje, aDjunto;
          |FINSI;
          aTo = aEmail2;
          |SI aTo ! "";
               |DSCORREO_ENVIA aIP, aServidor, aFrom, aTo, aSubject, aMensaje, aDjunto;
          |FINSI;
          |FBORRA aDjunto;
     |FINSI;
|FPRC;

|PROCESO PorAlbaran;
     |ABRE #24;
     #24#0 = #10#3;
     |LEE 000#24=;
     |CIERRA #24;

     |ABRE #3;
     #3#0 = #10#3;
     #3#1 = #10#84;
     |LEE 020#3=;
     |CIERRA #3;

     |ABRE #25;
     #25#0 = #10#6;
     |LEE 000#25=;
     |CIERRA #25;

     |SI #10#84 = 1;
          aEmail1 = #24#163; |QBF aEmail1;
          |SI aEmail1 = ""; aEmail1 = #24#197; |QBF aEmail1; |FINSI;
     |SINO;
          aEmail1 = #3#24; |QBF aEmail1;
          |SI aEmail1 = ""; aEmail1 = #24#163; |QBF aEmail1; |FINSI;
          |SI aEmail1 = ""; aEmail1 = #24#197; |QBF aEmail1; |FINSI;
     |FINSI;
     aEmail2 = #25#64; |QBF aEmail2;
     |SI aEmail2 = ""; aEmail2 = #25#62; |QBF aEmail2; |FINSI;

     |SI aEmail1 ! ""; |O aEmail2 ! "";
          |ABRE #7;
          #7#26 = #10#52;
          |LEE 000#7=;
          |SI FSalida ! 0; |DDEFECTO #7; |FINSI;
          |SI #10#43 = "N";
               |SI #10#50 = "S";
                    aInf = #7#3; |QBF aInf;
               |SINO;
                    aInf = #7#4; |QBF aInf;
               |FINSI;
          |SINO;
               aInf = #7#5; |QBF aInf;
          |FINSI;
          aInf = aInf + ".rpt";
          |DDEF aAlfa;
          aAlfa = aAlfa + aInf;
          |XABRE "E", aAlfa, 0;
          |SI FSalida ] 0;
               |HAZ VallcorbaMailAlbP; ||pdf
          |SINO;
               |HAZ VallcorbaMailAlbT; ||txt
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE PorAlbaran;
     #10#1;
     1, 3, 43, 88;
     #0#9,  #0#2, #0#4, #0#0, #0#8, #0#21;
     #0#10, #0#3, #0#5, #0#1, #0#8, #0#22;
     ;
     NULL, PorAlbaran;
|FIN;

|PROCESO Tipo3; |TIPO 3;
     |GRABA 020#0a;

     enImpVallcorba = 1;

     |SI #0#24 = "S"; |O #0#24 = "N";
          ser_alb = "";
          num_alb = 0;
          |SUB_EJECUTA_NP "agifa014";
     |FINSI;

     |SI #0#24 = "S"; |O #0#24 = "X";
          |ABRE #2;
          |LEE 000#2p;
          |SI FSalida ! 0;
               |MENAV "0000Parámetros mail no configurado...";
          |SINO;
               |BUCLE PorAlbaran;
          |FINSI;
          |CIERRA #2;
     |FINSI;

|FPRC;

|PROGRAMA;
     |ABRE #0;
     |LEE 101#0p;
     |SI FSalida ! 0; |GRABA 020#0b; |FINSI;
     |PINPA #0#0;
     |PINDA #0#0;
     |ENDATOS #1#0;
     |CIERRA #0;
|FPRO;
