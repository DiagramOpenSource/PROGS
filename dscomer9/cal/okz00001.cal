|FICHEROS;
     okz00001 #0;
     okw00001 #1;   ||Tmp
     okm00009 #9;
     okm00010 #10,MANTE;
     okm00001 #100;
     agifa142 #142;
     referen@;
|FIN;

|VARIABLES;
     &enCtrFin = 0;
     aAlfa = "";
     nPanta = 0;
     nRango = 0;
     nGrid = -1;
     nBtnModi = 0;
     nBtnCons = 0;
     nBtnPdte = 0;
     nBtnValida = 0;
     nBtnTodo = 0;
     nBtnPrepe = 0;
     aTipoC = "";
     aTipoV = "";
     aValida = "";
     nError = 0;
     nTiene = 0;
     nLin = 0;
     aSistema = "";
     aEjecuta = "";
     nHandle = 0;
     aPaginaWeb = "";
     aCadena = "";
     aLon = "";
     nPos = 0;
     i = 0;
     aCar = "";
     aNomRes = "";
     aEspera = "";
     &Tempo = "";
     fHoy = @;
     aHora = "";
|FIN;

|PROCESO CuentaLin;
     nLin = nLin + 1;
     |SI #10#10 = "P"; aTipoV = "S"; |FINSI;
     |SI #10#10 = "C"; aTipoC = "S"; |FINSI;
|FPRC;

|DEFBUCLE CuentaLin;
     #10#1;
     ;
     #9#2, 0000000001;
     #9#2, 9999999999;
     ;
     NULL, CuentaLin;
|FIN;

|PROCESO Min10;
     #10#2 = #9#2;
     #10#3 = 0000000001;
|FPRC;

|PROCESO Max10;
     #10#2 = #9#2;
     #10#3 = 9999999999;
|FPRC;

|PROCESO BtnCons;
     |LEE 000#9=;
     |SI FSalida ! 0; |ACABA; |FINSI;

     aAlfa = "Consulta L¡neas Prepedido: " + #9#2;
     |VENTANA 0901, 2898, -1, 17, aAlfa;
     |HAZ PushVenModal;

     |ABRET #10;
     nRango = 4 + 8 + 16 + 32;
     |LINEAL_SIMPLE #1#10, nRango, 1002, 2698, Min10, Max10, NULL;
     |PAUSA;
     |CIERRAT #10;

     |HAZ PopVenModal;
|FPRC;

|PROCESO BtnModi;
     |LEE 100#9=;
     |SI FSalida ! 0; |ACABA; |FINSI;

     |SALVA_DATOS #9;
     |SELECT #1#9;

     |SI #9#13 = "S";
          |MENAV "0000Pedido validado...";
     |SINO;
          aAlfa = "Modificaci¢n L¡neas Prepedido: " + #9#2;
          |VENTANA 0901, 2898, -1, 17, aAlfa;
          |HAZ PushVenModal;

          |ABRET #10;
          nRango = 1 + 4 + 8 + 16 + 32;
          |LINEAL_SIMPLE #1#10, nRango, 1002, 2698, Min10, Max10, NULL;
          |PAUSA;
          |CIERRAT #10;

          nLin = 0;
          |BUCLE CuentaLin;
          |SI nLin = 0;
               |MENAV "0000Se borra el prepedido y no se actualiza el Status del pedido Web...";
               |BORRA 020#9c;
          |FINSI;
          |HAZ PopVenModal;
     |FINSI;

     |REPON_DATOS #9;
     |SI aValida = ""; |SELECT #1#9; |SINO; |SELECT #2#9; |FINSI;
     |LEE 000#9=;
     |REFRESCACONTROL nGrid;
|FPRC;

|PROCESO Evt;
     |SI FSalida = 1; |O FSalida = 2;
          |SI aValida = ""; |SELECT #1#9; |SINO; |SELECT #2#9; |FINSI;
          |LEE 000#9=;
          |SI #9#13 = "S";
               |ESTADO_CONTROL nBtnModi, "DISABLE";
          |SINO;
               |ESTADO_CONTROL nBtnModi, "ENABLE";
          |FINSI;
     |FINSI;
     |SI FSalida = 4;
          |SI #9#13 = "S";
               aAlfa = "0000El pedido: " + #9#2 + ", ya esta validado...";
               |MENAV aAlfa;
          |SINO;
               aTipoV = ""; aTipoC = "";
               |BUCLE CuentaLin;
               aAlfa = "0000S¨Validar prepedido: " + #9#2 + "?";
               |CONFI aAlfa;
               |SI FSalida = 0;
                    |LEE 121#9=;
                    |SI aTipoV = "S";
                         |HAZ PedVenta;
                    |FINSI;
                    |SI aTipoC = "S";
                         |HAZ PedVentaC;
                         |HAZ PedCompra;
                    |FINSI;
                    |LIBERA #9;
                    |HAZ MarcaPrepedidos;
                    |HAZ TieneLineas;
                    |REFRESCACONTROL nGrid;
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO TieneLineas;
     |DDEF aAlfa;
     aAlfa = aAlfa + "okm00009";
     |CARGA_DEF aAlfa, referen@;
     |SI FSalida = 0;
          |ABRE #referen@;
          |SI aValida = "";
               |LEE 000#referen@.p;
          |SINO;
               |SELECT #2#referen@;
               #referen@#13 = aValida;
               #referen@#0 = " ";
               #referen@#1 = "     ";
               #referen@#2 = 000000;
               |LEE 000#referen@.];
               |SI FSalida ! 0; |O #referen@#13 ! aValida;
                    FSalida = "1";
               |FINSI;
          |FINSI;
          |SI FSalida = 0;
               |ESTADO_CONTROL nBtnModi, "ENABLE";
               |ESTADO_CONTROL nBtnCons, "ENABLE";
          |SINO;
               |ESTADO_CONTROL nBtnModi, "DISABLE";
               |ESTADO_CONTROL nBtnCons, "DISABLE";
          |FINSI;
          |CIERRA #referen@;
          |DESCARGA_DEF #referen@;
     |FINSI;
|FPRC;

|PROCESO Min9;
     #9#13 = aValida;
     #9#2 = "          ";
|FPRC;

|PROCESO Max9;
     #9#13 = aValida;
     #9#2 = "zzzzzzzzzz";
|FPRC;

|PROCESO Refresca;
     |PULSATECLA;
     |SI nGrid ] 0; |DESTRUYECONTROL nGrid; |FINSI;

     nRango = 4 + 8 + 16 + 32;
     |HAZ Min9;
     |SI aValida = "";
          |SELECT #1#9;
          |LEE 000#9];
          |LINEAL_SIMPLE #1#9, nRango, 1002, 2999, NULL, NULL, Evt;
     |SINO;
          |SELECT #2#9;
          |LEE 000#9];
          |LINEAL_SIMPLE #2#9, nRango, 1002, 2999, Min9, Max9, Evt;
     |FINSI;
     nGrid = FSalida;
     |HAZ TieneLineas;
|FPRC;

|PROCESO BtnPdte
     |ESTADO_CONTROL nBtnPdte, "DISABLE";
     |ESTADO_CONTROL nBtnValida, "ENABLE";
     |ESTADO_CONTROL nBtnTodo, "ENABLE";
     aValida = "N";
     |HAZ Refresca;
|FPRC;

|PROCESO BtnValida;
     |ESTADO_CONTROL nBtnPdte, "ENABLE";
     |ESTADO_CONTROL nBtnValida, "DISABLE";
     |ESTADO_CONTROL nBtnTodo, "ENABLE";
     aValida = "S";
     |HAZ Refresca;
|FPRC;

|PROCESO BtnTodo;
     |ESTADO_CONTROL nBtnPdte, "ENABLE";
     |ESTADO_CONTROL nBtnValida, "ENABLE";
     |ESTADO_CONTROL nBtnTodo, "DISABLE";
     aValida = "";
     |HAZ Refresca;
|FPRC;

|PROCESO Cambia2Dos;
     aCadena = aAlfa;
     aAlfa = "";
     aLon = aCadena % A0;
     |PARA i = 1; |SI i [ aLon; |HACIENDO i = i + 1;
          nPos = i * 100 + 1;
          aCar = aCadena % nPos;
          |SI aCar = "/"; aCar = "\"; |FINSI;
          aAlfa = aAlfa + aCar;
     |SIGUE;
|FPRC;

|PROCESO CreaBat;
     |XABRE "W", aEjecuta, nHandle;
     aAlfa = "@echo off";
     |XGRABA nHandle, aAlfa;

     |DBASS aAlfa;
     aAlfa = aAlfa + "agitool\plugins\ds_eje_php "
     |HAZ Cambia2Dos;
     aAlfa = aAlfa + &34 + aPaginaWeb + &34;
     |XGRABA nHandle, aAlfa;
     |XCIERRA nHandle;
|FPRC;

|PROCESO CreaShell;
     |XABRE "WB", aEjecuta, nHandle;
     aAlfa = "#!/bin/sh" + &10;
     |XGRABA nHandle, aAlfa;

     |SI aNomRes = "";
          aAlfa = "wget -o /tmp/wget.log -O /tmp/wget.res --no-check-certificate " + &34 + aPaginaWeb + &34 + &10;
          |XGRABA nHandle, aAlfa;
     |SINO;
          aAlfa = "wget -o /tmp/wget.log -O " + aNomRes + " --no-check-certificate " + &34 + aPaginaWeb + &34 + &10;
          |XGRABA nHandle, aAlfa;
          aAlfa = "echo ok > " + aEspera;
          |XGRABA nHandle, aAlfa;
     |FINSI;
     |XCIERRA nHandle;
     aAlfa = "chmod +x " + aEjecuta;

     |SYSTEM aAlfa;
|FPRC;

|PROCESO MuestraResul;
     |PARA; |SI; |HACIENDO;
          |SLEEP 1;
          |XABRE "E", aEspera, 0;
          |SI FSalida ] 0; |SAL; |FINSI;
     |SIGUE;
     nLin = 0;
     |HAZ CreaTempo; |NOME_DAT #1Tempo; |DELINDEX #1; |ABRE #1;
     |XABRE "", aNomRes, nHandle;
     |XLEE nHandle, 250, aAlfa;
     |PARA; |SI FSalida ] 0; |HACIENDO;
          nLin = nLin + 1;
          #1#0 = nLin;
          #1#1 = aAlfa;
          |GRABA 020#1n;
          |XLEE nHandle, 250, aAlfa;
     |SIGUE;
     |XCIERRA nHandle;
     |LEE 000#1p;
     |VENTANA 1001, 2899, -1, 17, "Resultado recogida";
     |HAZ PushVenModal;
     nRango = 4 + 8 + 16 + 32;
     |LINEAL_SIMPLE #1#1, nRango, 1002, 2698, NULL, NULL, NULL;
     |PAUSA;
     |HAZ PopVenModal;
     |CIERRA #1; |HAZ BoraTempo; |DELINDEX #1;
|FPRC;

|PROCESO RecogePrepedidos;
     aNomRes = #100#15; |QBF aNomRes;
     aAlfa = aNomRes % -101;
     |SI aAlfa ! "/"; |Y aAlfa ! "\"; aNomRes = aNomRes + "/"; |FINSI;
     fHoy = ~; |HORA aHora;
     aNomRes = aNomRes + (fHoy % 704) + (fHoy % 402) + (fHoy % 102);
     aNomRes = aNomRes + (aHora % 102) + (aHora % 402) + (aHora % 702) + ".txt";
     aEspera = "/tmp/" + Usuario;
     |FBORRA aEspera;
     aPaginaWeb = #100#13; |QBF aPaginaWeb;
     |DFICE aEjecuta;
     aEjecuta = aEjecuta + "/";
     |SI aSistema = "ESDOS";
          aEjecuta = aEjecuta + "comunica.bat";
     |SINO;
          aEjecuta = aEjecuta + "comunica.sh";
     |FINSI;
     |SI aSistema = "ESDOS";
          |HAZ CreaBat;
          |SYSTEM aEjecuta;
          |FBORRA aEjecuta;
     |SINO;
          |HAZ CreaShell;
          |SYSTEM aEjecuta;
          |HAZ MuestraResul;
          |FBORRA aEspera;
     |FINSI;
|FPRC;

|PROCESO MarcaPrepedidos;
     aNomRes = "";
     aPaginaWeb = #100#14; |QBF aPaginaWeb;
     aPaginaWeb = aPaginaWeb + "?prepedido=" + #9#2;
     |SI #9#17 ! 0;
          aPaginaWeb = aPaginaWeb + "&pedido=" + #9#17;
     |SINO;
          aPaginaWeb = aPaginaWeb + "&pedido=" + #9#20;
     |FINSI;
     |DFICE aEjecuta;
     aEjecuta = aEjecuta + "/";
     |SI aSistema = "ESDOS";
          aEjecuta = aEjecuta + "comunica.bat";
     |SINO;
          aEjecuta = aEjecuta + "comunica.sh";
     |FINSI;
     |SI aSistema = "ESDOS";
          |HAZ CreaBat;
          |SYSTEM aEjecuta;
          |FBORRA aEjecuta;
     |SINO;
          |HAZ CreaShell;
          |SYSTEM aEjecuta;
     |FINSI;
|FPRC;

|PROCESO BtnPrepe;
     |HAZ RecogePrepedidos;
     |HAZ Refresca;
|FPRC;

|PROGRAMA;
     |QUE_SISTEMA aSistema;
     |ABRE #100; |LEE 000#100p; |CIERRA #100;
     |ABRE #142; |LEE 000#142p; |CIERRA #142;
     |PINPA #0#0; nPanta = FSalida;
     |PINDA #0#0;
     |CONTROL_SIMPLE nPanta, "BOTON,{{165}}Pendientes", 0560, 0570, BtnPdte;
     nBtnPdte = FSalida;
     |CONTROL_SIMPLE nPanta, "BOTON,{{165}}Validados", 0660, 0670, BtnValida;
     nBtnValida = FSalida;
     |CONTROL_SIMPLE nPanta, "BOTON,{{165}}Todos", 0760, 0770, BtnTodo;
     nBtnTodo = FSalida;
     |CONTROL_SIMPLE nPanta, "BOTON,{{137}}Capturar Prepedidos", 0855, 0875, BtnPrepe;
     nBtnPrepe = FSalida;
     |CONTROL_SIMPLE nPanta, "BOTON,{{155}}Consultar Prepedido", 0577, 0698, BtnCons;
     nBtnCons = FSalida;
     |CONTROL_SIMPLE nPanta, "BOTON,{{197}}Modificar Prepedido", 0777, 0898, BtnModi;
     nBtnModi = FSalida;

     |CONFI "0000S¨Recoger prepedidos web?";
     |SI FSalida = 0;
          |HAZ RecogePrepedidos;
     |FINSI;

     |ABRET #9;
     |HAZ BtnPdte;
     |PAUSA;
     |CIERRAT #9;
     |PULSATECLA;
|FPRO;
