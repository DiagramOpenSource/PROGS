|FICHEROS;
     malpe107 #0;        || Este def.
     malpe106 #56;
     agifa019 #1;        || Articulos.
     agifa017 #2;        || Stock por almacenes.
     agifa112 #3;        || Proveedores.
     agifa029 #10;       || Almacenes.
     agifa142 #42;       || Parametros generales.
     agifa321 #11;       || Parametros de divisas
     agifa324 #112;       || Divisas
     agifa322 #212;       || Divisas
     agifa323 #312;       || Divisas
     dsl00001 #6; ||Codigos de barras multiples
     agitp001 #12;

     dsm00005 #905;
     dsm00010 #910;
     dsm00014 #914;
     dsm00241 #241;
|FIN;

|VARIABLES;
     nForma         = 0;
     &Cie_pasa      = 0;
     &Cie_fech      = @;
     &Cie_alma      = 0;
     &nDeci_PreBase = 0; ||Para fichero articulos
     &nDeci_Base    = 0;
     &nDeci_PreDiv  = 0; ||Externa para articulos
     &docum     = "";
     &articulo  = "";

     nPrecio    = 0;
     nDLinea    = 0;
     nHLinea    = 0;
     nModo      = 0;

     s          = 0;
     pelas      = 0;
     mes        = 0;
     clave      = "entraval";
     &linea     = 0;
     &operacion = "EV";
     codigo    = 0;
     orden      = 1;
     forden     = "";
     sto        = 0;
     sto1       = 0;
     fmes       = "";
     &fecha     = @;
     mm1        = "";
     m1         = 0;
     m2         = 0;
     m3         = 0;
     m4         = 0;
     m5         = 0;
     m6         = 0;
     aprograma  = "";
     nom_tmp    = "";
     &c_prove   = "";
     &c_artic   = "";
     &c_serie   = "";
     &entrada   = 0;
     modo       = 0;
     inkey      = 0;
     &calpre    = 0;
     &aConfir   = "";
     precio     = 0;
     codart     = 0;
     ind        = 0;
     alfa       = "";
     art_ant    = "";
     alm        = 0;
     pro        = "";
     ent        = 0;
     pvp        = 0;
     dto        = 0;
     dto1       = 0;
     &r_arti    = "";
     pvp1       = 0;
     pvp2       = 0;
     pvp3       = 0;
     pvp4       = 0;
     pvp5       = 0;
     pvp6       = 0;
     jsum       = "";
     salida     = 0;
     cambio_base = 0;
     cambio_div  = 0;
     factordec   = 0;
     i          = 0;
     nfecha    = 0;
     div_act = 0;        || Divisa actualizada. Me sirve en un PARA para que
     &divisa = "";       || Codigo de Divisa. Se pasa a ACTUAL. de DIVISAS
     &prov_divisa = ""   || La paso a agifa322
     vcontrol = "";
     nSwBarra = 1;
     &registro = "";
|FIN;

|CAMPOS;
     #1#11 nec;
     #1#15 sps;
     #1#16 spr;
     #1#7 stmia;
     #1#104 stdia;
     #1#9 pcm;
     #1#12 stres;
     #1#17 pdva;
     #1#13 stfa;
     #1#18 pvp11;
     #1#19 pvp22;
     #1#20 pvp33;
     #1#147 pvp44;
     #1#148 pvp55;
     #1#149 pvp66;
     #1#109 redo;
|FIN;

|INCLUYE i_varart;
|INCLUYE i_cdbarr;

|| ********************************************************************
|PROCESO acarpr;
     |DDEFECTO #12;
     #12#0 = #0#5;            || Proveedor.
     #12#1 = #0#0;            || Articulo.
     |LEE 101#12=;
     |SI FSalida ! 0;
          #12#4 = #0#7;           || ultimo descuento de compra
          #12#5 = #0#20;          || ultimo descuento de compra
          #12#6 = #0#12;          || ultima fecha de compra
          #12#7 = #1#1;           || Descripcion Articulo.
          #12#8 = #0#18;          || Divisa
          #12#9 = #0#6;           || ultimo precio de compra
          #12#3 = #0#19;          || upc en divisa
          |GRABA 020#12n;
      |SINO;
          #12#4 = #0#7;           || ultimo descuento de compra
          #12#5 = #0#20;          || ultimo descuento de compra
          #12#6 = #0#12;          || ultima fecha de compra
          #12#7 = #1#1;           || Descripcion Articulo.
          #12#8 = #0#18;          || Divisa
          #12#9 = #0#6;           || ultimo precio de compra
          #12#3 = #0#19;          || upc en divisa
          |GRABA 020#12a;
     |FINSI;
     |LIBERA #12;
|FPRC;

|PROCESO actualmas;|TIPO 0;
     #2#0 = #1#0;
     FSalida = 0;
     |PARA #2#1 = 0;|SI FSalida = 0; |HACIENDO ;
          |LEE 101#2>;
          |SI #2#0 = #1#0; |Y FSalida = 0;
               sto = #2#5 - #2#4;
               #2#3 = sto * #1#9;
               #2#50 = #2#42 + #2#6 - #2#43 - #2#39; || necesidades
               #2#50 = #2#50 - #2#4 - #2#9 - #2#8;
               |GRABA 020#2a;
               |LIBERA #2;
          |SINO;
               FSalida = 2 ;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO pvps;
     |SI #1#108 =  "S"; |O #1#108 = "G";
          |HAZ CalPre077;
     |FINSI;
|FPRC;

|PROCESO ActuaCompo;
     enForma      = nForma;
     eaTipo       = "W1";
     eaSerie      = #0#28;
     enCodigo     = #0#27;
     enLinea      = #0#14;
     eaArticulo   = #0#0;
     enAlmacen    = #0#1;
     enUnidades   = #905#6 * enForma;
     eaComponente = #905#4;
     efFecha      = #0#12;
     enCamBas    = cambio_div;
     enCamDiv    = cambio_base;
     |SI #0#18 = #11#9;
          eaMoneda = "B";
     |SINO;
          eaMoneda = "D";
     |FINSI;
     |SUB_EJECUTA_NP "dsz00002;EVS";
|FPRC;

|DEFBUCLE dsm00005A;
     #905#1;
     ;
     "W1", #0#28, #0#27, #0#14, "      ";
     "W1", #0#28, #0#27, #0#14, "zzzzzz";
     ;
     NULL, ActuaCompo;
|FIN;

|PROCESO salida;
     #1#0 = #0#0;
     |LEE 120#1=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     #2#0 = #0#0;
     #2#1 = #0#1;
     |LEE 120#2=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     #3#0 = #0#5;
     |LEE 120#3=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     eaProve = #3#0;
     enCodigo = #0#27;
     enForma = nForma;
     |HAZ AcumulaEV;

     #0#3 = #2#39;
     |SI #0#12 ] #1#26; |Y nForma = 1;
         #1#26 = #0#12;         || Ultima Fecha de Compra
         #1#27 = #0#5;          || Ultimo Proveedor
         |SI #0#6 ! 0;
             #1#28  = #0#6;     || Ultimo Precio de Compra
             #1#165 = #0#18;    || Divisa UPC
             #1#163 = #0#19;    || UPC Divisa
             |HAZ acarpr;
         |FINSI;
         #1#29  = #0#7;                      || Ultimo Descuento Compra 1
         #1#129 = #0#20;                     || Ultimo Descuento Compra 2
         #1#209 = 0;
         #1#133 = ((#1#28 < #1#29) < #1#129) < #1#209;  || Precio Ult.Compra Descuento
         #1#164 = ((#1#163 < #1#29) < #1#129) < #1#209; || UPC Divida con Dto.
     |FINSI;

     |SI #0#4 = 0;
          pelas = 0;
     |SINO;
          |SI #1#196 = 1;
               pelas = #0#4 * #0#11;
          |SINO;
               pelas = #0#21 * #0#11;
          |FINSI;
     |FINSI;

     fecha = #0#12;
     fmes  = fecha % A402;
     mes   = fmes - 1;
     mes   = mes + 27;
     #3mes = #3mes +. pelas;
     #3#39 = #3#39 +. pelas;
     |SI nForma = 1;
          |HAZ pvps;
     |FINSI;

     #1#123 = "N";
     #1#124 = "00.00.0000";
     |GRABA 020#1a;  |LIBERA #1;
     |GRABA 020#2a;  |LIBERA #2;

     |HAZ actualmas;
     |GRABA 020#3a;

     |LIBERA #1;
     |LIBERA #2;
     |LIBERA #3;

     enImpPrvCom = pelas;
     efFecha = #0#12;
     eaProve = #0#5;
     |HAZ AcumulaPrv;

     |BUCLE dsm00005A;
|FPRC;

|PROCESO BorraCompo;
     |BORRA 020#905a;
     |LIBERA #905;
|FPRC;

|DEFBUCLE dsm00005;
     #905#1;
     ;
     "W1", #0#28, #0#27, #0#14, "      ";
     "W1", #0#28, #0#27, #0#14, "zzzzzz";
     be;
     NULL, BorraCompo;
|FIN;

|PROCESO Conversion;
   |ACABA;

   |SI #1#204 = "S";
      |ABRE #241; |SELECT #2#241;
      #241#0 = #1#5;
      #241#2 = #1#205;
      |LEE 000#241=;
      |CIERRA #241;

      |SI #241#6 = "D";
          #malpe107#21 = #malpe107#4 * #241#5;
      |SINO;
          #malpe107#21 = #malpe107#4 / #241#5;
      |FINSI;
      #malpe107#29 = #1#205;
   |FINSI;
|FPRC;

|PROCESO Tipo2; |TIPO 2;
     nModo = (FEntrada / 100) ! 0;
     nForma = 0 +. 1;
     |SI nSwBarra = 0; |Y nModo = 1; |Y nForma = 1; |Y #1#176 = "S";
          |HAZ GrabaElemento;
     |FINSI;

     |ABRET #1;
     |ABRET #2;
     |ABRET #3;
     |ABRE #12;
     |HAZ salida;
     |CIERRAT #1;
     |CIERRAT #2;
     |CIERRAT #3;
     |CIERRA #12;

     |SI nModo = 3;
          |BUCLE dsm00005;
     |FINSI;
|FPRC;

|PROCESO CambioDivisa; |TIPO 0;
      |SI #agifa112#80 = "S"; || Si el proveedor es de divisa pactada
         |ABRE #agifa323;
         |ABRE #agifa322;
         #agifa322#0 = #0#5;
         #agifa322#2 = #0#18;
         #agifa322#7 = "N";
         |SELECT #2#agifa322;  || Con esta clave ...
         |LEE 011#agifa322.=;  || ... busco periodos no finalizados de esa divisa
         |SI FSalida = 0;  || Si existe un periodo no finalizado
           |PARA div_act = 0; |SI div_act = 0; |HACIENDO;
             cambio_div = #agifa322#3;        || ...y el cambio
             |LEE 001#agifa322.=;   || Leo las lineas de Proveed/Divisas
             nfecha = #0#12 - #agifa322#6;  || Resto la fecha de fin de periodo a la actual
             |SI nfecha > 0;  || Si la fecha actual supera a la del periodo asignado ...
                |MENAV "0000ATENCION deberia Actualizar primero la divisa en Proveedores/Divisas";
                prov_divisa = #0#5;  || Le envio el cod. de proveedor a "agifa323"
                |SUB_EJECUTA_NP "agifa323.tab";  || Ejecuto Proveedores/Divisas
                |ERROR;
             |SINO;
                div_act = 1;  || Para salir del PARA
             |FINSI;
           |SIGUE;
         |SINO;  || Si no existe un periodo no finalizado para esa divisa
           |MENAV "0000No existe un periodo activo para la divisa en Proveedores / Divisas";
           prov_divisa = #0#5;  || Le paso a "agifa323" el proveedor
           |SUB_EJECUTA_NP "agifa323.tab";  || Ejecuto Proveedores / Divisas
           |ERROR;
         |FINSI;
         |SELECT #1#agifa322;
         |CIERRA #agifa322;
         |CIERRA #agifa323;
      |SINO; || Si el Proveedor no es de Divisa pactada ...
         |ABRE #agifa324; || ...miro directamente en divisas
         #agifa324#0 = #0#18;
         |LEE 001#agifa324.=;
         |SI FSalida = 0; ||Si existe la divisa...
            cambio_div = #agifa324#3; || la recojo
            |SI #agifa324#6 ! -1; || Si los dias de margen de actualizacion no
                                  || son = -1 (indefinidos)
              |PARA div_act = 0; |SI div_act = 0; |HACIENDO;
                 cambio_div = #agifa324#3;        ||Cargo el valor del cambio
                 |LEE 001#agifa324.=;  || Leo la divisa
                 nfecha = #0#12 - #agifa324#4;
                 |SI nfecha > #agifa324#6; |Y #agifa324#6 ! -1;  || Si la fecha actual
                    |MENAV "0000ATENCION deberia Actualizar primero la divisa";
                    divisa = #0#18;
                    |SUB_EJECUTA_NP "agifa326.tab";
                    |ERROR;
                 |SINO;
                    div_act = 1;
                 |FINSI;
              |SIGUE;
            |FINSI;
         |FINSI;
         |CIERRA #agifa324;
      |FINSI;
|FPRC;

|PROCESO decdivev;|TIPO 0;
     |ABRE #agifa321;
     |LEE 001#agifa321.p;
     |SI #0#18 = "   ";
         #0#18 = #agifa321#9; ||Pongo por defecto Divisa Base
         |SI #agifa321#2 = "N";
           |CAMPO_MODIFICA #0#18,1,"N";
         |FINSI;
         |PINTA #0#18;
     |FINSI;
     |ABRE #agifa324;
     #agifa324#0 = #0#18;
     |LEE 001#agifa324.=;
     nDeci_PreDiv = #agifa324#9;
     |HAZ CambioDivisa;

     #agifa324#0 = #agifa321#9; ||Igualo a moneda base
     |LEE 001#agifa324.=;
     nDeci_PreBase = #agifa324#9;  ||Decimales precio de moneda base (Para fichero articulos)
     nDeci_Base    = #agifa324#7;  ||Decimales precio de moneda base (Para fichero articulos)
     cambio_base = #agifa324#3;
     |CIERRA #agifa321;
     |CIERRA #agifa324;
|FPRC;

|PROCESO precio_divisa;
   #0#19 = #0#6 / cambio_base * cambio_div; || Precio (en Div)
   #0#19 = #0#19 ! nDeci_PreDiv;
   |PINTA #0#19;
|FPRC;

|PROCESO cta_art; |TIPO 7;
     alfa = art_ant;  |QBF alfa;

     |SI alfa = "";  |ACABA;  |FINSI;
     |SI #0#0 ! "                    ";  |ACABA;  |FINSI;

     |SI #42#64 ! "S";  |ACABA;  |FINSI;

     codart = alfa;                     || PARA EL COD.ART. A NUMERO.
     codart = codart + 1;
     alfa   = "0000000000000" + codart;
     codart = -100 - #42#62;            || LONGITUD TOTAL DEL COD.ART.
     alfa   = alfa % codart;
     #0#0 = alfa;
     |PINTA #0#0;

     |ABRE #1;
     #1#0 = #0#0;
     |LEE 000#1=;
     |SI FSalida ! 0;
         |CONFI "2400SArticulo inexistente. Dar de alta? ";
         |SI FSalida = 0;
             #1#0 = art_ant;
             |LEE 000#1=;
             |SI FSalida = 0;
                 #1#0   = #0#0;
                 #1#6   = 0;
                 |PDEFECTO #1, 10, 18;
                 |PDEFECTO #1, 24, 27;
                 |PDEFECTO #1, 32, 102;
                 #1#104 = 0;
                 #1#123 = "N";
                 #1#124 = "00.00.0000";
                 #1#128 = #1#0;
                 |GRABA 021#1n;
                 |SI FSalida = 0;|Y #42#28 = "S";
                      registro = #0#0;
                      |CIERRA #0;
                      |SUB_EJECUTA_NP "agifa201.run";
                      |ABRE #0;
                 |FINSI;
             |FINSI;
         |FINSI;
     |FINSI;
     |CIERRA #1;
|FPRC;

|PROCESO LeeMonBase;|TIPO 7; ||Antes de todo por si consulta articulos
     |ABRE #agifa321;
     |LEE 001#agifa321.p;
     |ABRE #agifa324;
     #agifa324#0 = #agifa321#9; ||Igualo a moneda base
     |LEE 001#agifa324.=;
     nDeci_PreBase = #agifa324#9;  ||Decimales precio de moneda base (Para fichero articulos)
     nDeci_Base    = #agifa324#7;  ||Decimales precio de moneda base (Para fichero articulos)
     |CIERRA #agifa321;
     |CIERRA #agifa324;

     |HAZ cta_art;
|FPRC;

|PROCESO Tip6; |TIPO 6;
     vcontrol = Control;  |QBF vcontrol;
     |ABRE #6;
     |SELECT #2#6;
     #6#3=#0#0;
     |LEE 010#6=;
     |SI FSalida=0; |Y #0#0 ! #6#1;
         #0#0 = #6#1;
         |ERROR;
         |PINTA #0#0;
     |FINSI;
     |CIERRA #6;
     r_arti = "";
|FPRC;

|PROCESO divev;|TIPO 0;
     |HAZ decdivev;
     |SI #0Campo ! Contenido; ||Aqui porque debe leer lo anterior
       |SI Campo = 18;  ||Estoy en campo divisa
           |HAZ precio_divisa;
       |FINSI;
       |SI Campo = 19; ||Estoy en Precio (Divisa)
         #0#6 = #0#19 / cambio_div * cambio_base;  || Precio en Mon. Base
         |HAZ precio;  ||Calcula neto base
       |FINSI;
     |FINSI;
|FPRC;

|PROCESO pintam;|TIPO 0;
     mm1="0401"+#0#2+"    Stock: "+#0#3+"    Neto: "+#0#11;
     |MENSA mm1;
|FPRC;

|PROCESO altaalm; |TIPO 6;
     |ABRE #2;
     |ABRE #10;
     #2#0 = #0#0;
     #2#1 = #0#1;
     |LEE 000#2=;
     |SI FSalida ! 0;
          |DDEFECTO #2;
          #2#0 = #0#0;
          #2#1 = #0#1;
          #2#40 = #0#2;
          #10#0 = #0#1;
          |LEE 000#10=;
          |SI FSalida ! 0; |DDEFECTO #10; |FINSI;
          #2#41 = #10#1;
          |GRABA 021#2n;
     |FINSI;
     |CIERRA #2;
     |CIERRA #10;
|FPRC;

|PROCESO decim;
     #0#4 = #0#4 ! #42#8;
     |PINTA #0#4;
|FPRC;

|PROCESO piva; |TIPO 0;
     inkey = FSalida;
     |SI inkey = 14;
          c_prove = #0#5;
          c_artic = #0#0;
          c_serie = "  ";
          entrada = 1;
          |SUB_EJECUTA_NP "agifa383.run";
          |ERROR;
          inkey = 0;
     |FINSI;

     |SI inkey = 15;
          eaPrv = #1#5;
          enTiva = #1#30
          efFiva = #1#12;
          |HAZ IVAPrv;
          #0#6 = #0#6 / (1 + enIva/100);
          |HAZ precio_divisa;
     |FINSI;
|FPRC;

|PROCESO precio;|TIPO 0;
     #0#11 = ((((#0#6 < #0#7) < #0#20) < #0#8) < #0#9) < #0#10;
|FPRC;

|PROCESO guarda_to; |TIPO 0;
     |SI #42#64 ! "S";  |ACABA;  |FINSI;

     alm = #0#1;
     pro = #0#5;
     ent = #0#4;
     pvp = #0#6;
     dto = #0#7;
     dto1 = #0#20;
|FPRC;

|PROCESO tipo11; |TIPO 11;
     modo = (FEntrada / 100) ! 0;
     |SI FSalida = 14; |Y modo ! 1;
          c_prove = #0#5;
          c_artic = #0#0;
          c_serie = "     ";
          entrada = 1;
          |SUB_EJECUTA_NP "agifa383.run";
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO CogeValores046;  |TIPO 7;
     |ABRE #42;
     |LEE 001#42p;
     |SI FSalida ! 0; |DDEFECTO #42; |FINSI;
     |CIERRA #42;

     |SI Campo = 1;
         |C_M #0Campo, 1, "S";
         |SI #42#99 = "S";
             |C_M #0Campo, 1, "N";
             #0Campo = 1;  |PINTA #0Campo;
         |FINSI;
     |FINSI;

     |SI Campo = 4;
          |HAZ DobleUni;
          |SI eaSw2Stock = "S";
               |C_M #0#4, 1, "N";
               |ACABA;
          |FINSI;
     |FINSI;

     |SI #1#176 = "N";  |ACABA;  |FINSI;

     |SI Campo = 1;
         |SI enAlmacen ! 0;
             #0#1 = enAlmacen;  |PINTA #0#1;
         |FINSI;
     |FINSI;

     |SI Campo = 4; |O Campo = 1;
         |SI enUnidades ! 0;
             |SI #0#4 ! enUnidades;  Contenido = #0#4;  |FINSI;
             #0#4 = enUnidades;  |PINTA #0#4;
         |FINSI;
     |FINSI;

     |SI Campo = 19; |O Campo = 1;
         |SI nPrecio ! 0;
             #0#19 = nPrecio;  |PINTA #0#19;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO PideArt046;  |TIPO 7;
     |C_M #0#0, 1, "S";
     |C_M #0#1, 1, "S";

     nModo = (FEntrada / 100) ! 0;
     |SI nSwBarra = 0; |Y nModo = 1; |ACABA; |FINSI;

     |SI #0#0 ! "                    ";
         |ABRE #1;
         #1#0 = #0#0;
         |LEE 000#1=;
         |SI FSalida ! 0;  |DDEFECTO #1;  |FINSI;
         |CIERRA #1;
         |SI #1#176 = "S";
             |C_M #0#0, 1, "N";
             |C_M #0#1, 1, "N";
             |ACABA;
         |FINSI;
     |FINSI;

     |HAZ LeeMonBase;

     |ABRE #910;
     |LEE 040#910p;
     |SI FSalida ! 0;  |DDEFECTO #910;  |FINSI;
     |CIERRA #910;

     |SI #910#2 [ 2;    |ACABA;  |FINSI;

     eAlfa = #0Campo;
     |SUB_EJECUTA_NP "dsz99990";                 || Por Pantalla

     |SI eAlfa = "-1";  |PTEC 807;  |ACABA;  |FINSI;
     |SI eAlfa = "-2";              |ACABA;  |FINSI;

     eaArticulo = eAlfa;      ||  700000-4215

     #0Campo = eAlfa;
     |PINTA #0Campo;
     |PTEC 802;
|FPRC;

|PROCESO Calc6046;  |TIPO 6;
     nModo = (FEntrada / 100) ! 0;
     |SI nSwBarra = 0; |Y nModo = 1;
          |C_M #0#1,  1, "S";
          |C_M #0#4,  1, "S";
          |C_M #0#19, 1, "S";
          eaArti = #0#0;
          |HAZ HallaArti;
          |SI eaArti = ""; |ERROR; |ACABA; |FINSI;
          |SI #1#176 = "S"; |Y eaEleme = "";
               |MENAV "0000Codigo incompleto...";
               |ERROR; |ACABA;
          |FINSI;
          #0#0 = eaArti; |PINTA #0#0;
          |ACABA;
     |FINSI;

     enAlta = 1;
     eaArticulo = #0Campo;
     |HAZ LlenaArticulo;
     |SI FSalida = 0;
          #0Campo = eaArticulo;
          |PINTA #0Campo;
     |SINO;
          |ERROR; |ACABA;
     |FINSI;

     |HAZ Tip6;
|FPRC;

|PROCESO ElElemento; |TIPO 0;
     |ABRE #1;
     #1#0 = #0#0;
     |LEE 000#1=;
     |SI FSalida ! 0;  |DDEFECTO #1;  |FINSI;
     |CIERRA #1;

     |SI #1#176 = "S";
          eaTipo      = "W1";
          eaSerie     = #0#28;
          enCodigo    = #0#27;
          enLinea     = #0#14;
          eaArticulo  = #0#0;
          enAlmacen   = #0#1;
          enTarifa    = 1;
          enUnidades  = #0#4;
          enTBruto    = #0#4 * #0#19;
          eaCliente   = "";
          efFecha     = #0#12;
          enCamBas    = cambio_div;
          enCamDiv    = cambio_base;
          |SI #0#18 = #11#9;
               eaMoneda = "B";
          |SINO;
               eaMoneda = "D";
          |FINSI;
          |SUB_EJECUTA_NP "dsz00002";
          #0#1    = enAlmacen;
          nPrecio = enTBruto / enUnidades;
          |C_M #0#1,  1, "N";
          |C_M #0#4,  1, "N";
          |C_M #0#19, 1, "N";
     |FINSI;
|FPRC;

|PROCESO Tipo0046;
     nModo = (FEntrada / 100) ! 0;

     |SI nSwBarra ! 0; |Y nModo = 1;
          |QBF r_arti;
          |SI r_arti ! "";
               #0#0 = r_arti;
               |PINTA #0#0;
          |FINSI;
          art_ant = #0#0;
     |FINSI;

     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 1;
          |QBF pro;
          |SI #42#64 = "S"; |Y pro ! "";
               #0#1 = alm;
               #0#5 = pro;
               #0#4 = ent;
               #0#6 = pvp;
               #0#7 = dto;
               #0#20 = dto1;
          |SINO;
               |ABRE #1;
               #1#0 = #0#0;
               |LEE 000#1=;
               #0#1 = 1;
               #0#5 = #1#27;
               #0#4 = 0;
               #0#6 = #1#28;
               #0#7 = #1#29;
               #0#20 = #1#129;
               |CIERRA #1;
          |FINSI;
          |SI #42#64 = "S"; |Y #0#4 = 0; #0#4 = 1; |FINSI;

          |ABRE #3;
          #3#0 = #0#5;
          |LEE 001#3=;
          |SI #3#82 = "D";
               #0#18 = #3#81; ||Divisa la del proveedor si en su moneda de trab.
               |PINTA #0#18;
          |FINSI;
          |CIERRA #3;
          |HAZ decdivev;

          |PINTA #0#1;
          |PINTA #0#5;
          |PINTA #0#4;

          |HAZ precio_divisa;
          |PINTA #0#7;
          |PINTA #0#20;
     |FINSI;
|FPRC;

|PROCESO Tipo11046;  |TIPO 11;
     nModo = (FEntrada / 100) ! 0;
     |SI FSalida = 7;  |Y #1#176 = "S";
         |MENAV "      Imposible abortar;  Termine de introducir la linea y efectue una Baja.";
         |ERROR;
     |FINSI;
|FPRC;

|PROCESO cierre; |TIPO 0;
     efFec = #0#12;
     enAlma = #0#1;
     |HAZ MiraHisL;
     |SI enErrHis = 0; |HAZ MiraStockIni; |FINSI;
     |SI enErrHis ! 0;
          |ERROR;
          |ACABA;
     |FINSI;
|FPRC;

|PROCESO Tipo5; |TIPO 5;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo [ 2; |Y nSwBarra = 1;
          |CONFI "0000NIntroduccion por Codigo de Barras";
          nSwBarra = FSalida;
          |ATRI S;
          |SI FSalida = 0;
               |PINTA 0533, "Introduccion por codigo de barras  ";
          |SINO;
               |PINTA 0533, "Introduccion por codigo de articulo";
          |FINSI;
          |ATRI 0;
     |FINSI;
     |HAZ SinBarra;
|FPRC;

|PROCESO GrabaElemento;
     |ABRE #914;
     #914#0 = eaEleme;
     |LEE 000#914=;
     |CIERRA #914;

     |ABRE #905;
     |DDEFECTO #905;
     #905#0 = "W1";
     #905#1 = #0#28;
     #905#2 = #0#27;
     #905#3 = #0#14;
     #905#4 = eaEleme;
     #905#5 = #914#1;
     #905#6 = #0#4;
     #905#7 = #0#19;
     #905#8 = #905#6 * #905#7;
     |GRABA 020#905n;
|FPRC;
