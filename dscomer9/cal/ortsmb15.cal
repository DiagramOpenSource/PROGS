     Si se modifica algo, hacerlo tambien en el ortsza15

|FICHEROS;
     ortsm007 #7;
     ortsm015 #15;
     ortsm016 #16;
     ortsm017 #17,MANTE;
     ortsm018 #18,MANTE;
     ortsm019 #19;
     ortsm029 #29,MANTE;
     ortsm030 #30,MANTE;
     ortsm031 #31,MANTE;  || Consumo
     ortsm034 #34;
     ortsm036 #36;
     agifa019 #119;
|FIN;

|VARIABLES;
     aArt = "";
     aDes = "";
     nUni = 0;
     aTirador = "";
|FIN;

|PROCESO BorraConsumo;
     |BORRA 020#31a;
|FPRC;

|DEFBUCLE BorraConsumo;
     #31#1;
     ;
     #16#28, #16#0, #16#1, "                    ";
     #16#28, #16#0, #16#1, "zzzzzzzzzzzzzzzzzzzz";
     be;
     NULL, BorraConsumo;
|FIN;

|PROCESO GrabaConsumo;
     #119#0 = aArt;
     |LEE 000#119=;
     |SI FSalida ! 0; |DDEFECTO #119; |FINSI;

     |DDEFECTO #31;
     #31#0 = #16#28;
     #31#1 = #16#0;
     #31#2 = #16#1;
     #31#3 = aArt;
     |LEE 101#31=;
     |SI FSalida ! 0; |GRABA 020#31b; |FINSI;
     #31#4 = aDes;
     #31#5 = #31#5 + nUni;
     #31#6 = #119#9;
     #31#7 = #31#5 * #31#6;
     |GRABA 020#31a;
     |LIBERA #31;

     #17#25 = #17#25 + #31#7;
|FPRC;

|PROCESO CombiUniCanteado;
     #34#0 = #30#12;
     #34#1 = #18#5;
     |LEE 000#34=;
     |SI FSalida = 0;
          aArt = #34#3;
          aDes = #34#4;
          nUni = (#30#9 / #36#8 * #36#9) * (#30#10 / #36#8 * #36#9) * #16#5;
          |HAZ GrabaConsumo;
     |FINSI;
|FPRC;

|DEFBUCLE CombiUniCanteado;
     #18#1;
     ;
     #17#0, #17#1, #17#2, #17#3, 001;
     #17#0, #17#1, #17#2, #17#3, 999;
     ;
     NULL, CombiUniCanteado;
|FIN;

|PROCESO CompoCanteado;
     #34#0 = #30#12;
     #34#1 = #17#6;
     |LEE 000#34=;
     |SI FSalida = 0;
          |SI #17#8 = "S"; |Y #17#7 > 1;    || Desglose unidades
               |BUCLE CombiUniCanteado;
          |SINO;
               aArt = #34#3;
               aDes = #34#4;
               nUni = (#30#9 / #36#8 * #36#9)  * (#30#10 / #36#8 * #36#9) * #17#7 * #16#5;
               |HAZ GrabaConsumo;
          |FINSI;
     |SINO;
          aArt = #30#12;
          aDes = #30#8;
          nUni = (#30#9 / #36#8 * #36#9)  * (#30#10 / #36#8 * #36#9) * #17#7 * #16#5;
          |HAZ GrabaConsumo;
     |FINSI;
|FPRC;

|DEFBUCLE CompoCanteado;
     #30#1;
     ;
     #17#0, #17#1, #17#2, #17#3, 0, " ", 00;
     #17#0, #17#1, #17#2, #17#3, 9, "z", 99;
     ;
     NULL, CompoCanteado;
|FIN;

|PROCESO CompoHerrajes;
     #119#0 = #29#4;
     |LEE 000#119=;
     |SI FSalida ! 0; |DDEFECTO #119; |FINSI;

     |SI #119#2 = #36#15; |ACABA; |FINSI;

     aArt = #29#4;
     aDes = #29#5;
     |SI #29#7 = 0;
          nUni = #29#6 * #17#7 * #16#5;
     |SINO;
          nUni = #29#6 * (#29#7 / #36#8 * #36#9) * #17#7 * #16#5;
     |FINSI;
     |HAZ GrabaConsumo;
|FPRC;

|DEFBUCLE CompoHerrajes;
     #29#1;
     8;
     #17#0, #17#1, #17#2, #17#3, "                    ", "N";
     #17#0, #17#1, #17#2, #17#3, "zzzzzzzzzzzzzzzzzzzz", "N";
     ;
     NULL, CompoHerrajes;
|FIN;

|PROCESO TiraUni;
     #34#0 = #17#4;
     #34#1 = #18#6;
     |LEE 000#34=;
     |SI FSalida = 0;
          aArt = #34#3;
          aDes = #34#4;
          nUni = #16#5;
          |HAZ GrabaConsumo;
     |FINSI;
|FPRC;

|DEFBUCLE TiraUni;
     #18#1;
     ;
     #17#0, #17#1, #17#2, #17#3, 001;
     #17#0, #17#1, #17#2, #17#3, 999;
     ;
     NULL, TiraUni;
|FIN;

|PROCESO CombiUni;
     #34#0 = #17#4;
     #34#1 = #18#5;
     |LEE 000#34=;
     |SI FSalida = 0;
          aArt = #34#3;
          aDes = #34#4;
          nUni = (#17#16 / #36#8 * #36#9) * (#17#17 / #36#8 * #36#9) * #16#5;
          |HAZ GrabaConsumo;
     |FINSI;
|FPRC;

|DEFBUCLE CombiUni;
     #18#1;
     ;
     #17#0, #17#1, #17#2, #17#3, 001;
     #17#0, #17#1, #17#2, #17#3, 999;
     ;
     NULL, CombiUni;
|FIN;

|PROCESO CompoPedido;
     #17#25 = 0;
     aTirador = #17#11; |QBF aTirador;

     |SI aTirador = "";
          #34#0 = #17#4;
          #34#1 = #17#6;
          |LEE 000#34=;
          |SI FSalida = 0;
               |SI #17#8 = "S"; |Y #17#7 > 1;    || Desglose unidades
                    |BUCLE CombiUni;
               |SINO;
                    aArt = #34#3;
                    aDes = #34#4;
                    nUni = (#17#16 / #36#8 * #36#9) * (#17#17 / #36#8 * #36#9) * #17#7 * #16#5;
                    |HAZ GrabaConsumo;
               |FINSI;
          |SINO;
               aArt = #17#4;
               aDes = #17#5;
               nUni = #17#7 * #16#5;
               |HAZ GrabaConsumo;
          |FINSI;
     |SINO;
          #7#0 = #17#11;
          |LEE 000#7=;
          |SI FSalida = 0;
               |SI #17#8 = "S"; |Y #17#7 > 1;    || Desglose unidades
                    |BUCLE TiraUni;
               |SINO;
                    aArt = #7#2;
                    aDes = #7#3;
                    nUni = #17#7 * #16#5;
                    |HAZ GrabaConsumo;
               |FINSI;
          |FINSI;
     |FINSI;

     |BUCLE CompoHerrajes;
     |BUCLE CompoCanteado;

     |GRABA 020#17a;

     #16#62 = #16#62 + #17#25;
|FPRC;

|DEFBUCLE CompoPedido;
     #17#1;
     ;
     #16#28, #16#0, #16#1, 001;
     #16#28, #16#0, #16#1, 999;
     be;
     NULL, CompoPedido;
|FIN;

|PROCESO CalConsumoLin;
     #16#62 = 0;

     |ABRE #36; |LEE 000#36p; |CIERRA #36;

     |BUCLE BorraConsumo;

     |ABRE #119;
     |ABRE #34;
     |ABRE #31;
     |ABRE #19;
     |ABRE #7;

     #19#0 = #16#2;
     |LEE 000#19=;
     |SI FSalida = 0; |Y #19#2 = "S";
          |BUCLE CompoPedido;
     |FINSI;
     |CIERRA #7;
     |CIERRA #19;
     |CIERRA #31;
     |CIERRA #34;
     |CIERRA #119;
|FPRC;
