|FICHEROS;
     tescoz03 #0;
     tescow01 #1;
     tescow02 #2;
     agifa071 #71;
     agifa010 #10;
     agifa019 #19;
     agifa060 #60;
|FIN;

|VARIABLES;
     i = 0;
     aAlfa = "";
     nHay = 0;
     nImpo = 0;
     &enVenta = 0;
     &enRenta = 0;
     aRepre = "";
|FIN;

|PROCESO Tmp;
     #2#0 = #1#1;
     |LEE 020#2=;

     |DDEFECTO #60;
     #60#0 = #1#1;
     |LEE 000#60=;

     |SI aRepre ! #1#0; |Y aRepre ! "";
          |PIEINF;
     |FINSI;

     #1#2 = #60#1;
     #1#4 = 100 * #1#3 / #2#1; ||%Venta
     #1#6 = 100 * #1#5 / #1#3; ||%Rentabilidad

     |PRINT;
     aRepre = #1#0;
|FPRC;

|DEFBUCLE Tmp;
     #1#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, Tmp;
|FIN;

|PROCESO agifa071;
     |SI #71#13 < #0#4; |O #71#13 > #0#5;
          |ACABA;
     |FINSI;

     |DDEFECTO #2;
     #2#0 = #71#13;
     |LEE 101#2=;
     |SI FSalida ! 0; |GRABA 020#2b; |FINSI;
     #2#1 = #2#1 + #71#37;
     |GRABA 020#2a;
     |LIBERA #2;

     |SI #0#8 = "P";
          |PARA i = 9; |SI i [ 16; |HACIENDO i = i + 1;
               |SI #0i = #10#6; |SAL; |FINSI;
          |SIGUE;
          |SI i > 16; |ACABA; |FINSI;
     |SINO;
          |SI #10#6 < #0#6; |O #10#6 > #0#7;
               |ACABA;
          |FINSI;
     |FINSI;

     |DDEFECTO #19;
     #19#0 = #71#2;
     |LEE 000#19=;

     |DDEFECTO #1;
     #1#0 = #10#6;
     #1#1 = #71#13;
     |LEE 101#1=;
     |SI FSalida ! 0; |GRABA 020#1b; |FINSI;

     #1#2 = "";
     #1#3 = #1#3 + #71#37;
     #1#4 = 0;
     nImpo = ((#71#37 < #10#5) < #10#32) < #10#7;
     nImpo = nImpo - (#71#5 * #19#9);
     #1#5 = #1#5 + nImpo;
     #1#6 = 0;
     |GRABA 020#1a;
     |LIBERA #1;

     nHay = 1;
|FPRC;

|DEFBUCLE agifa010;
     #10#2;
     38;
     "      ", #0#2, #0#0, 000001, "S";
     "zzzzzz", #0#3, #0#1, 999999, "S";
     ;
     NULL, NULL, agifa071;
|FIN;

|PROCESO Parrilla; |TIPO 0;
     |C_M #0#6, 1, "N";
     |C_M #0#7, 1, "N";
     |PARA i = 9; |SI i [ 16; |HACIENDO i = i +1;
          |C_M #0i, 1, "N";
     |SIGUE;
     |SI #0#8 = "S";
          |PARA i = 9; |SI i [ 16; |HACIENDO i = i +1;
               |C_M #0i, 1, "S";
          |SIGUE;
     |SINO;
          |C_M #0#6, 1, "S";
          |C_M #0#7, 1, "S";
     |FINSI;
|FPRC;

|PROCESO Tipo3; |TIPO 3;
     aAlfa = Usuario; aAlfa = "1" + aAlfa;
     |NOME_DAT #1 aAlfa;
     |DELINDEX #1;

     aAlfa = Usuario; aAlfa = "2" + aAlfa;
     |NOME_DAT #2 aAlfa;
     |DELINDEX #2;

     |IMPRE 0;
     |SI FSalida = 0;
          |INFOR "tescoz03";
          |SI FSalida = 0;
               |ABRE #2;
               |ABRE #1;
               |ABRE #19;
               |BUCLE agifa010;
               |CIERRA #19;
               |CIERRA #1
               |ABRE #60;
               |BUCLE Tmp;
               |CIERRA #60;
               |CIERRA #2;
               |SI aRepre ! 0; |PIEINF; |FINSI;
               |FININF;
          |SINO;
               |MENAV "0000No existe informe 'tescoz03'";
          |FINSI;
          |FINIMP;
     |FINSI;

     |DELINDEX #1;
     |DELINDEX #2;
|FPRC;
