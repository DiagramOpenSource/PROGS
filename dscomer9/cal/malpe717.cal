|FICHEROS;
     malpe710  #710;
     malpe716  #716;
     malpe712  #712;
     malpe713  #713;
     malpe714  #714;
     malpe717  #717;
     malpe721  #721;
     agifa025  #25;
|FIN;

|VARIABLES;
     &enLinea = 0;
     &eaTitulo = "";
     &eaRepre = "";

     aAlfa = "";
     aParam = "";

     aDirRecoge = "";
     aPathPlugins = "";
     aAbre = "";
     aReturnDos = "";
     aUsuario = "";
     aRuta = "";
     aHost = "";
     aMensaje = "";
     aPass = "";

     nVentana = 0;
     aImpre = "";
     aIPRemoto = "";
     aInfor = "";
     aInforDef = "";
     aInforOri = "";
     aInforOriRPT = "";
     aOrigen = "";
     aDestino = "";
     aDestinoCpta = "";
     aDestinoCpta1 = "";
     aDestinoCptaTXT = "";
     nHandle = 0;
     aNombre = "";

     {1]}aBaseLocal = "";
|FIN;

|PROCESO malpe716;
     eaRepre = "";
     #25#0 = #716#2
     |LEE 000#25=;
     |SI FSalida = 0;
          eaRepre = #25#1;
     |FINSI;

     |PRINT;
|FPRC;

||Bucle a todos
|DEFBUCLE malpe716;
     #716#1;
     ;
     #710#4,1,"", 00000;
     #710#5,12,"zz", 99999;
     ;
     NULL,malpe716;
|FIN;

|| Bucle por comerciales
|DEFBUCLE malpe716wCom;
     #716#1;
     ;
     #710#4,1 ,#713#1, 00000;
     #710#5,12,#713#1, 99999;
     ;
     NULL,malpe716;
|FIN;

|PROCESO malpe713;
     |BUCLE malpe716wCom;
|FPRC;

|DEFBUCLE malpe713;
     #713#2;
     ;
     #710#0,"","*";
     #710#0,"zz","*";
     ;
     NULL, malpe713;
|FIN;

|PROCESO SubeFTP;
     aReturnDos = &13 + &10;       ||Return en maquina Local.
     || aDestinoCpta1         PDF
     || aDestinoCptaTXT       TXT
     || aDestinoCpta          CPTA
     aDirRecoge = aDestinoCpta;
     |DBASE aPathPlugins;     |QBF aPathPlugins;
     aPathPlugins = aPathPlugins + "plugins/";
     aAbre =  aDestinoCpta + "dsftp.bat";
     |XABRE "WB", aAbre, nHandle;
     |SI FSalida ] 0;
          aAlfa = "@ECHO OFF"                                         + aReturnDos;  |XGRABA nHandle, aAlfa;
          aAlfa = ":: Comprobar si hay contrasenya"                   + aReturnDos;  |XGRABA nHandle, aAlfa;
          aAlfa = "IF " + &34 + "%1" + &34 + "==" + &34 + &34 + " GOTO Ayuda"  + aReturnDos;  |XGRABA nHandle, aAlfa;
          aAlfa = ":: crear un archivo temporal llamado script.ftp"   + aReturnDos;  |XGRABA nHandle, aAlfa;
          aAlfa = ":: el signo > y >> es para canalizar el texto."    + aReturnDos;  |XGRABA nHandle, aAlfa;
          aAlfa = "> " + aDirRecoge + "script.ftp ECHO " + aUsuario   + aReturnDos;  |XGRABA nHandle, aAlfa;
          aAlfa = ">>" + aDirRecoge + "script.ftp ECHO %1"            + aReturnDos;  |XGRABA nHandle, aAlfa;
          aAlfa = ">>" + aDirRecoge + "script.ftp ECHO cd " + aRuta + aReturnDos;  |XGRABA nHandle, aAlfa;
          aAlfa = ">>" + aDirRecoge + "script.ftp ECHO lcd " + aDestinoCpta + aReturnDos;  |XGRABA nHandle, aAlfa;
          aAlfa = ">>" + aDirRecoge + "script.ftp ECHO bin"           + aReturnDos;  |XGRABA nHandle, aAlfa;
          aAlfa = ">>" + aDirRecoge + "script.ftp ECHO put " + aDestinoCpta1   + aReturnDos;  |XGRABA nHandle, aAlfa;
          aAlfa = ">>" + aDirRecoge + "script.ftp ECHO put " + aDestinoCptaTXT   + aReturnDos;  |XGRABA nHandle, aAlfa;
          aAlfa = ">>" + aDirRecoge + "script.ftp ECHO bye"           + aReturnDos;  |XGRABA nHandle, aAlfa;
          aAlfa = ":: Usamos el archivo recien creado:"               + aReturnDos;  |XGRABA nHandle, aAlfa;
          aAlfa = aPathPlugins + "dsftp.exe " + aDirRecoge + "script.ftp " + aHost + aReturnDos; |XGRABA nHandle, aAlfa;
          aAlfa = ":: sobreescribimos el fichero temporal y lo borramos" + aReturnDos;  |XGRABA nHandle, aAlfa;
          aAlfa = "TYPE NUL > " + aDirRecoge + "script.ftp"           + aReturnDos;  |XGRABA nHandle, aAlfa;
          aAlfa = "DEL " + aDirRecoge + "script.ftp"                  + aReturnDos;  |XGRABA nHandle, aAlfa;
          aAlfa = "GOTO End"                                          + aReturnDos;  |XGRABA nHandle, aAlfa;
          aAlfa = ":Ayuda"                                            + aReturnDos;  |XGRABA nHandle, aAlfa;
          aAlfa = "ECHO Uso: %0 password"                             + aReturnDos;  |XGRABA nHandle, aAlfa;
          aAlfa = ":End"                                              + aReturnDos;  |XGRABA nHandle, aAlfa;

          |XCIERRA nHandle;
     |SINO;
          aMensaje = "0000Problemas al crear dsftp.bat para el traspaso a e-CRM.";
          |MENAV aMensaje;
          |XCIERRA nHandle;
          |ACABA;
     |FINSI;

     aAlfa = "cmd " + &34 + "/c " + aDirRecoge + "dsftp.bat " + aPass + &34;
     |SYSTEM aAlfa;

     |SLEEP 1;
     aAbre = aDirRecoge + "dsftp.bat";
     |RFBORRA aAbre;
|FPRC;


||RENOMBRADO DE FICHERO SEGUN CARPETA
|PROCESO malpe714;

     aAlfa = #714#1; |QBF aAlfa;
     aDestinoCpta1 = aDestinoCpta + aAlfa + aNombre + ".pdf";
     aDestinoCptaTXT = aDestinoCpta + aAlfa + aNombre + ".txt";

     |COPIA_FICHERO aDestino, aDestinoCpta1;

     |XABRE "W", aDestinoCptaTXT, nHandle;
     |SI FSalida < 0;
          aAlfa = "0000No puedo crear:" + aDestinoCptaTXT;
          |MENAV aAlfa;  |ACABA;
     |FINSI;

     ||LINEA 1: Titulo del Informe
     aAlfa = #710#8;     |QBF aAlfa;
     |XGRABA nHandle, aAlfa;

     ||LINEA 2: Desripcion del Informe
     aAlfa = #710#2;     |QBF aAlfa;
     |XGRABA nHandle, aAlfa;

     ||LINEA 3: Texto figo "graf"
     aAlfa = "graf";
     |XGRABA nHandle, aAlfa;

     ||LINEA 4: Texto figo "General"
     aAlfa = "General";
     |XGRABA nHandle, aAlfa;

     ||LINEA 5:Nivel de acceso "0"
     aAlfa = "0";
     |XGRABA nHandle, aAlfa;

     |XCIERRA nHandle;

     ||COPIAR SEGUN MANTE EL TXT y PDF
     || aDestinoCpta1         PDF
     || aDestinoCptaTXT       TXT
     || aDestinoCpta          CPTA
     |HAZ SubeFTP;
|FPRC;

|DEFBUCLE malpe714;
     #714#2;
     ;
     #710#0,"","*";
     #710#0,"zzz","*";
     ;
     NULL, malpe714;
|FIN;


|PROCESO MachacaRPT;
     |DDEF aAlfa;
     aInforDef = aAlfa + aInfor;
     aInforOriRPT = aAlfa + aInforOri + ".rpt";;
     |COPIA_FICHERO aInforDef, aInforOriRPT;
|FPRC;

|PROCESO ReponRPT;
     |DDEF aAlfa;
     aInforDef = aAlfa + "malpe717.rpt";
     aInforOriRPT = aAlfa + aInforOri + ".rpt";;
     |COPIA_FICHERO aInforDef, aInforOriRPT;
|FPRC;

|PROCESO CopiaInfo;
     aNombre = #710#7;  |QBF aNombre;
     aIPRemoto = "";
     |IP_REMOTO aIPRemoto;  |QBT aIPRemoto;

     |DBASE aAlfa;
     aDestino = aAlfa + "pdfcrm/";
     |MKDIR aDestino;

     aDestino = aDestino + aNombre + ".pdf";

     |SI aIPRemoto ! "";
          aBaseLocal1 = "";
          |ESPECIFICA "RBASS", aBaseLocal;
          aOrigen = aBaseLocal1 + "crystal/"+ aNombre + ".pdf";

          |RECIBE_FICHERO aOrigen, aDestino;
          |RFBORRA aOrigen;
     |SINO;
          |DBASS aAlfa;
          aOrigen = aAlfa + "crystal/" + aNombre + ".pdf";
          |COPIA_FICHERO aOrigen, aDestino;
          |FBORRA aOrigen;
     |FINSI;

||UNA VEZ COPIADO AL DIRECTORIO HAY QUE RENOMBRARLO AL NOMBRE INDICADO SEGUN CARPETA
     ||SISCO: A¤adir creacion de txt
     |DBASE aAlfa;
     aDestinoCpta = aAlfa + "pdfcrm/";

     |ABRE #721;
     |LEE 000#721p;
     |SI FSalida = 0;
          aHost = #721#1;     |QBF aHost;
          aUsuario = #721#2;  |QBF aUsuario;
          aPass = #721#3;     |QBF aPass;
          aRuta = #721#4;     |QBF aRuta;
     |SINO;
         |MENAV "    No se puede actualizar la ruta al servidor";
     |FINSI;
     |CIERRA #721;
     |BUCLE malpe714;
     |FBORRA aDestino;
|FPRC;


|PROGRAMA;
     aParam = PARAMETRO;

     |ABRE #710;
     |ABRE #712;
     |ABRE #713;

     |SI aParam ! "";
          enLinea = aParam;
     |SINO;
          |VENTANA 0501, 2799, -1, 1, "";
          nVentana = FSalida;

          |PINPA #0#717;
          |PTEC 802; |PAUSA;

     |FINSI;

     #710#0 = enLinea;
     |LEE 000#710=;
     |SI FSalida ! 0;
          |MENAV "    Error. En la lectura de la definicion."
          |ACABA;
     |FINSI;
     eaTitulo = #710#2;

     #712#0 = #710#1;
     |LEE 000#712=;
     |SI FSalida ! 0;
          |MENAV "    Error. En la lectura del Informe."
          |ACABA;
     |FINSI;
     aAlfa = #710#7;  |QBF aAlfa;
     aInfor = #712#2; |QBF aInfor;
     |SI aParam ! "";
          aImpre = "CrystalReport;C:\ds\crystal\" + aAlfa + ".pdf";
     |SINO;
          aImpre = "CrystalReport";
     |FINSI;
     Impresora = aImpre;

     |IMPRE -1;
     |SI FSalida = 0;
          aInforOri = "malpe716"
          |INFOR aInforOri;
          |SI FSalida = 0;

               |ABRE #25;

               |SI #710#3 = "T";
                    |BUCLE malpe716;
               |SINO;
                    |BUCLE malpe713;
               |FINSI;
               |PIEINF;
               |HAZ MachacaRPT;
               |FININF;
               |FINIMP;

               |CIERRA #25;

               |HAZ ReponRPT;
               |SI aParam ! "";
                    |HAZ CopiaInfo;
               |FINSI;
          |FINSI;
     |FINSI;

     |CIERRA #713;
     |CIERRA #712;
     |CIERRA #710;

     |SI aParam = "";
         |FINVENTANA nVentana;
     |FINSI;

     |PULSATECLA;
|FPRO;

|PROCESO Tipo80Fm717;  |TIPO 80;
     FSalida = 2799;
|FPRC;
