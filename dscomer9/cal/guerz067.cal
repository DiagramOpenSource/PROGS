|FICHEROS;
     guerz067 #0;
     gopre001 #1;
     gopre002 #2;
     gopre003 #3;
     gopre004 #4;
     gomantho #5;
     gomantl1 #6;
     goarticu #19;
     guerw059 #59,MANTE; || Tmp selec arti
     guerw061 #61,MANTE; || Tmp oferta C
     guerw062 #62,MANTE; || Tmp oferta L
     guerw063 #63,MANTE; || Tmp proveedores
     goparame #100;
     guerm119 #119;
     guerm120 #120;
     guerm122 #122; || Histo Ofertas
|FIN;

|VARIABLES;
     &cCodMonTrab = "";
     &nDeci_Medi = 0;
     &nDeci_Meda = 0;
     &Tempo = "";
     aTmp61 = "";
     aTmp62 = "";
     aTmp63 = "";
     nOferta = 0;
     nLin = 0;
     nLinPrv = 0;
     nSwPasa = 0;
     nSw = 0;
  {-1}aMenu = "";
     aMenu1 = "2400";
     aMenu2 = "1";
     aMenu3 = " ";
     aMenu4 = "RGAC";
     aMenu5 = " Repasar ";
     aMenu6 = " Generar ";
     aMenu7 = " Generar Ag. Prv. ";
     aMenu8 = " Cancelar ";
     nLinOferta = 0;
     aPrv = "";
|FIN;
--------------------------------------------------------------
|PROCESO CreaCab;
     |LEE 000#119u;
     |SI FSalida = 0;
          nOferta = #119#0 + 1;
     |SINO;
          nOferta = 1;
     |FINSI;
     |DDEFECTO #119;
     #119#0 = nOferta;
     #119#1 = #61#1;
     #119#2 = #61#2;
     #119#3 = #61#12;
     #119#4 = #61#3;
     #119#5 = #61#4;
     #119#6 = #61#6;
     #119#7 = #61#7;
     #119#8 = #61#11;
     #119#9 = #0#4;
     #119#10 = #63#3;
     #119#11 = #63#4;
     #119#12 = #61#13;
     #119#13 = cCodMonTrab;
     |GRABA 020#119n;
|FPRC;

|PROCESO Historico;
/*
     |DDEFECTO #122;
     #122#0 = #62#2;
     #122#1 = #63#3;
     |LEE 101#122=;
     |SI FSalida ! 0; |GRABA 020#122b; |FINSI;
     #122#2 = #62#3;
     #122#3 = #63#4;
     #122#4 = #61#1;
     #122#5 = #63#6;
     #122#6 = #63#7;
     |GRABA 020#122a;
     |LIBERA #122;
*/
|FPRC;

|PROCESO CreaOferta;
     #62#0 = #63#0;
     #62#1 = #63#1;
     |LEE 020#62=;

     |SI #63#8 = "N"; |ACABA; |FINSI;
     |SI #62#6 = "N"; |ACABA; |FINSI;

     |SI aPrv ! #63#3; |O aMenu2 = 2;
          nLinOferta = 0;
          |HAZ CreaCab;
          aPrv = #63#3;
     |FINSI;

     nLinOferta = nLinOferta + 1;
     |DDEFECTO #120;
     #120#0 = #119#0;
     #120#1 = nLinOferta;
     #120#2 = #62#2;
     #120#3 = #62#3;
     #120#4 = #63#6;
     #120#5 = #63#7;
     #120#6 = #120#4 * #120#5;
     |GRABA 020#120n;
     |HAZ Historico;
|FPRC;

|DEFBUCLE CreaOferta;
     #63#2;
     ;
     INICIO;
     FINAL;
     ;
     NULL, CreaOferta;
|FIN;
--------------------------------------------------------------
|PROCESO Tipo63_2; |TIPO 2;
     #63#9 = #62#2;
|FPRC;

|PROCESO FechaFin7; |TIPO 7;
     |SI #61#12 = "01.01.0000";
          #61#12 = #61#2; |PINTA #61#12;
     |FINSI;
     |SI #0#4 = "P";
          |C_M #61#12, 1, "S";
          |C_M #61#13, 1, "S";
     |SINO;
          |C_M #61#12, 1, "N";
          |C_M #61#13, 1, "N";
     |FINSI;
|FPRC;

|PROCESO Min63;
     #63#0 = #62#0;
     #63#1 = #62#1;
     #63#2 = 1;
|FPRC;

|PROCESO Max63;
     #63#0 = #62#0;
     #63#1 = #62#1;
     #63#2 = 999;
|FPRC;


|PROCESO Articu66; |TIPO 66;
     |ABRE #19;
     #19#0 = #62#2;
     |LEE 000#19];
     |CONSULTA_CLAVE #1#19;
     |SI FSalida = 0;
          #62#2 = #19#0; |PINTA #62#2;
          #62#3 = #19#1; |PINTA #62#3;
     |SINO;
          |ERROR;
     |FINSI;
     |CIERRA #19;
|FPRC;
--------------------------------------------------------------
|PROCESO GraPrv;
     nLinPrv = nLinPrv + 1;

     |DDEFECTO #19;
     #19#0 = #122#0;
     |LEE 000#19=;

     #63#0 = #62#0;
     #63#1 = #62#1;
     #63#2 = nLinPrv;
     #63#3 = #122#1;
     #63#4 = #122#3;
     #63#5 = #19#26;
     #63#6 = 0;
     #63#7 = #122#6;
     #63#9 = #62#2;
     |GRABA 020#63n;
|FPRC;

|DEFBUCLE GraPrv;
     #122#1;
     ;
     #62#2, "      ";
     #62#2, "zzzzzz";
     ;
     NULL, GraPrv;
|FIN;

|PROCESO gopre004;
     |DDEFECTO #19;
     #19#0 = #4#5;
     |LEE 000#19=;
     nSw = 0;
     |SI #19#126 = #59#0; nSw = 1; |SI #59#8 = "N"; |ACABA; |FINSI; |FINSI;
     |SI #19#126 = #59#1; nSw = 1; |SI #59#9 = "N"; |ACABA; |FINSI; |FINSI;
     |SI #19#126 = #59#2; nSw = 1; |SI #59#10 = "N"; |ACABA; |FINSI; |FINSI;
     |SI #19#126 = #59#3; nSw = 1; |SI #59#11 = "N"; |ACABA; |FINSI; |FINSI;
     |SI #59#8 = "S"; |Y #59#9 = "S"; |Y #59#10 = "S"; |Y #59#11 = "S";
          nSw = 1;
     |FINSI;
     |SI nSw = 0; |ACABA; |FINSI;

     nLin = nLin + 1;
     |DDEFECTO #62;
     #62#0 = #61#0;
     #62#1 = nLin;
     #62#2 = #4#5;
     #62#3 = #4#6;
     #62#4 = #4#8;
     #62#5 = #4#10;
     |GRABA 020#62n;
     nLinPrv = 0;
     |BUCLE GraPrv;
|FPRC;

|DEFBUCLE gopre004;
     #4#1;
     ;
     #3#0, #3#1, #3#2, #3#4, 001;
     #3#0, #3#1, #3#2, #3#4, 999;
     ;
     NULL, gopre004;
|FIN;

|PROCESO gopre003;
     |SI #0#4 = "A";
          |BUCLE gopre004;
          |ACABA;
     |FINSI;
     nLin = nLin + 1;
     |DDEFECTO #62;
     #62#0 = #61#0;
     #62#1 = nLin;
     #62#2 = #3#4;
     #62#3 = #3#5;
     #62#4 = #3#7;
     #62#5 = #3#9;
     |GRABA 020#62n;
     nLinPrv = 0;
     |BUCLE GraPrv;
|FPRC;

|DEFBUCLE gopre003;
     #3#1;
     ;
     #2#0, #2#1, #2#12, 001;
     #2#0, #2#1, #2#12, 999;
     ;
     NULL, gopre003;
|FIN;

|PROCESO gopre002;
     |BUCLE gopre003;
|FPRC;

|PROCESO Tipo12; |TIPO 12;
|FPRC;

|PROCESO ArtiParti; |TIPO 0;
     nSwPasa = 1;
     |ABRE #100; |LEE 000#100p; |CIERRA #100;
     |SI #0#4 = "A";
          #59#0 = #100#5;
          #59#1 = #100#6;
          #59#2 = #100#7;
          #59#3 = #100#8;
          #59#4 = #100#9;
          #59#5 = #100#10;
          #59#6 = #100#11;
          #59#7 = #100#12;
          |PINPA #0#59;
          |PINDA #0#59;
          |ENDATOS #1#59;
     |FINSI;
|FPRC;

|PROCESO Tipo3; |TIPO 3;
     |ABRE #100; |LEE 000#100p; |CIERRA #100;
     nDeci_Meda = #100#115;

     |SI nSwPasa = 0; |HAZ ArtiParti; |FINSI;

     |ABRE #1;
     #1#0 = #0#0;
     #1#1 = #0#1;
     |LEE 101#1=;
     |SI FSalida ! 0;
          |MENAV "0000No existe Presupuesto...";
     |SINO;
          cCodMonTrab = #1#7;
          |HAZ MonBas;
          nDeci_Medi = #1#24;

          |HAZ CreaTempo; aTmp62 = Tempo; |NOME_DAT #62#Tempo#; |DELINDEX #62;
          |HAZ CreaTempo; aTmp63 = Tempo; |NOME_DAT #63#Tempo#; |DELINDEX #63;

          |ABRE #6; |SELECT #3#6;
          #6#3 = #0#0;
          #6#4 = #0#1;
          |LEE 000#6=;
          |CIERRA #6;

          |ABRE #5;
          #5#0 = #6#0;
          #5#1 = #6#1;
          |LEE 000#5=;
          |CIERRA #5;

          |ABRE #62;
          |ABRE #63;
          |DDEFECTO #61;
          #61#0 = 1;
          #61#1 = #0#2;
          #61#2 = #0#2;
          #61#3 = #0#0;
          #61#4 = #0#1;
          #61#5 = #1#8;
          #61#6 = #5#0;
          #61#7 = #5#1;
          #61#8 = #5#29;
          #61#9 = #5#30;
          #61#10 = #5#31;
          #61#11 = Usuario % 810;

          |ABRE #19;
          |BUCLELIN 2 gopre002 #1;
          |CIERRA #19;

          |PINPA #0#61;
          |PINDA #0#61;
       |ET Otra;
          |ENDATOS #2#61;
          |SI FSalida = 0;
               |MENU aMenu;
               aMenu2 = FSalida;
               |SI FSalida = 1; |VETE Otra; |FINSI;
               |ABRE #119;
               |ABRE #120;
               |ABRE #122;
               |SI aMenu2 = 2; |BUCLE CreaOferta; |FINSI;
               |SI aMenu2 = 3; |BUCLE CreaOferta; |FINSI;
               |CIERRA #122;
               |CIERRA #120;
               |CIERRA #119;
          |FINSI;

          |CIERRA #62;
          |CIERRA #63;

          Tempo = aTmp62; |HAZ BoraTempo; |DELINDEX #62;
          Tempo = aTmp63; |HAZ BoraTempo; |DELINDEX #63;
     |FINSI;
|FPRC;

|PROCESO Articu0; |TIPO 0;
     |ABRE #19;
     #19#0 = #62#2;
     |LEE 000#19=;
     |SI FSalida = 0;
          #62#3 = #19#1; |PINTA #62#3;
     |SINO;
          |MENAV "0000No existe articulo...";
          |ERROR;
     |FINSI;
     |CIERRA #19;

     |ABRE #63;
     #63#0 = #62#0;
     #63#1 = #62#1;
     #63#2 = 0;
     |LEE 000#63];
     |SI FSalida ! 0; |O #63#0 ! #62#0; |O #63#1 ! #62#1;
          |ABRE #19;
          nLinPrv = 0;
          |BUCLE GraPrv;
          |CIERRA #19;
     |FINSI;
     |CIERRA #63;

     |PUSHV 0501 2380;
     |ENTLINEAL #1#63, 3, 2, 22, 1, Min63, Max63;
     |POPV;
|FPRC;
