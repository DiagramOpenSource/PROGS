/*


||primy004     Informe Margen Representante/Articulo.

||TODO Falta ver la ultima columna como se calcula, esta Paco en ello.

|FICHEROS;
     primy004 #0;   ||este def

     agifa010 #1;   ||Albaranes Vtas. CAB
     agifa071 #2;   ||Albaranes Vtas. LIN
     agifa048 #3;   ||Escandallos.
     agifa019 #4;   ||Articulos.

     primw001 #10;  ||Temporal para almacenar los valores.
|FIN;

|VARIABLES;
     aMensaje = "";
     aInforme = "";
     nSwPie = 0;
     aCodArtAnt = "";    ||Articulo anterior

     ||variables para enviar al informe.
     &eaCodArt = "";  ||Codigo articulo
     &enUdVta = 0;    ||Unidades de Vta
     nValorEs = 0;    ||Coste escandallo
     &enCoste = 0;    ||Coste. Unidades * Coste escandallo
     &enImpVta = 0;   ||Importe Venta
     &enImpMar = 0;   ||Importe Margen
     &enPorMar = 0;   ||% Margen
     nDifer = 0;
     &enPorVta = 0;   ||% Venta
     &enPorDif = 0;   ||% Diferencia
     &enPvp2 = 0;     ||Sirve para calcular enPorDif.
     &enProd = 0;     ||Producto de enUdVta * enPvp2

     nClave = 0;     ||Contador/Clave para tmp. primw001
     aFichero = "";
|FIN;

|PROGRAMA;
     nClave = 1;
     aCodArtAnt = "//////////\\\\\\\\\\";
     |CLS;
     |CABEZA "Informe Margen Representante/Articulo";
     |ABRE #primy004;
     |ABRE #agifa048;
     |ABRE #agifa019;
     #primy004#0 = "1";
     |LEE 101#primy004.=;
     |SI FSalida!0; |GRABA 020#primy004.b;|FINSI;
     |PINPA #0#0;
     |PINDA #0#0;
     |ENDATOS #1#0;
     |SI FSalida = 0;
          |HAZ Inicio;
          |HAZ Impresion;
          |GRABA 020#primy004.a;
          |HAZ Final;
     |FINSI;
     |CIERRA #primy004;
     |CIERRA #agifa048;
     |CIERRA #agifa019;
|FPRO;
______________________________________________________________________
|PROCESO agifa071;
     eaCodArt = #agifa071#2;
     |QBF eaCodArt;
     #agifa048#0 = eaCodArt;
     |LEE 000#agifa048.=;
     |SI FSalida = 0;
          nValorEs = #agifa048#9;
     |SINO;
          nValorEs = 0;
     |FINSI;
     enUdVta = #agifa071#5;
     enCoste = enUdVta * nValorEs;
     enImpVta = #agifa071#37;
     enImpMar = enImpVta - enCoste;
     enPorMar = (enImpVta / enCoste) - 1;

     #agifa019#0 = eaCodArt;
     |LEE 000#agifa019.=;
     |SI FSalida = 0;
          enPvp2 = #agifa019#19;
     |SINO;
          enPvp2 = 0;
     |FINSI;
     enProd = enUdVta * enPvp2;
     enPorVta = ((enProd) / enCoste) - 1;
     enPorDif = enPorMar - enPorVta;

     |DDEFECTO #primw001;
     #primw001#7 = nClave;
     |LEE 101#primw001.=;
     |SI FSalida ! 0; |GRABA 020#primw001.b; |FINSI;
     #primw001#0 = eaCodArt;
     #primw001#1 = enUdVta;
     #primw001#2 = enCoste;
     #primw001#3 = enImpVta;
     #primw001#4 = enImpMar;
     #primw001#5 = enPorMar;
     #primw001#6 = enPorDif;
     #primw001#8 = enPorVta;
     #primw001#9 = enPvp2;
     #primw001#10 = enProd;
     |GRABA 020#primw001.a;
     |LIBERA #primw001;
     nClave = nClave + 1;
|FPRC;

|DEFBUCLE agifa071;
     #agifa071#1;
     2;
     #agifa010#52, #agifa010#0, 001, #primy004#3;
     #agifa010#52, #agifa010#0, 999, #primy004#4;
     ;
     NULL,agifa071;
|FIN;
______________________________________________________________________
|PROCESO agifa010;
     ||Comprobar los articulos que entren en la seleccion.
     |BUCLE agifa071;
|FPRC;

|DEFBUCLE agifa010;      ||Recorro las lineas del albaran VTAS.
     #agifa010#4;
     6;
     #primy004#7,#primy004#5,000000,#primy004#1;
     #primy004#8,#primy004#6,999999,#primy004#2;
     ;
     NULL,agifa010;
|FIN;
______________________________________________________________________
|PROCESO primw001;
     |SI #primy004#9 = "D";
          eaCodArt = #primw001#0;
          enUdVta = #primw001#1;
          enCoste = #primw001#2;
          enImpVta = #primw001#3;
          enImpMar = #primw001#4;
          enPorMar = #primw001#5;
          enPorDif = #primw001#6;
          enPorVta = #primw001#8;
          enPvp2 = #primw001#9;
          enProd = #primw001#10;
          |PRINT;
          nSwPie = 1;
     |SINO;    ||Quiere decir resumido
          |SI #primw001#0 ! aCodArtAnt;
               |SI aCodArtAnt ! "//////////\\\\\\\\\\";
                    |PRINT;
                    nSwPie = 1;
               |FINSI;
               nSwPie = 1;
               aCodArtAnt = #primw001#0;
               eaCodArt = #primw001#0;
               enUdVta = #primw001#1;
               enCoste = #primw001#2;
               enImpVta = #primw001#3;
               enImpMar = #primw001#4;
               enPorMar = (enImpVta / enCoste) - 1;
               enPvp2 = #primw001#9;
               enProd = #primw001#10;
               enPorVta = ((enProd) / enCoste ) - 1;
               enPorDif = enPorMar - enPorVta;
          |SINO;
               eaCodArt = #primw001#0;
               enUdVta = enUdVta + #primw001#1;
               enCoste = enCoste + #primw001#2;
               enImpVta = enImpVta + #primw001#3;
               enImpMar = enImpMar + #primw001#4;
               enPorMar = (enImpVta / enCoste) - 1;
               enPvp2 = enPvp2 + #primw001#9;
               enProd = enProd + #primw001#10;
               enPorVta = ((enProd) / enCoste ) - 1;
               enPorDif = enPorMar - enPorVta;
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE primw001;      ||Recorro la temporal
     #primw001#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL,NULL,NULL,NULL,NULL,primw001;
     #primw001#0;
|FIN;
______________________________________________________________________

|PROCESO Inicio;
     ||Inicializa la temporal.
     aFichero = Usuario;
     |QBF aFichero;
     |NOME_DAT #10 aFichero;
     |DELINDEX #primw001;
     |ABRE #primw001;
|FPRC;

|PROCESO Final;
     |CIERRA #primw001;
     |DELINDEX #primw001;
|FPRC;
______________________________________________________________________

|PROCESO Impresion;
     |IMPRE 0;
     |SI FSalida = 0;
          aInforme = "primy004";
          |INFOR aInforme;
          |SI FSalida = 0;
               |BUCLE agifa010;    ||Relleno temporal
               |BUCLE primw001;    ||Recorro la temporal
               |SI nSwPie = 1;
                    |SI #primy004#9 ! "D";
                         |PRINT;
                    |FINSI;
                    |PIEINF;
               |FINSI;
               |FININF;
          |SINO;
               aMensaje = "0000Informe " + aInforme + " inaccesible.";
               |MENAV aMensaje;
          |FINSI;
          |FINIMP;
     |SINO;
          |MENAV "0000Proceso cancelado.";
     |FINSI;
|FPRC;


*/
