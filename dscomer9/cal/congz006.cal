  El csv trae la 1§ l¡nea como resumen y las siguientes las pesadas
  la 1 pesada actualiza la linea del pedido, las siguientes crean
  lineas nuevas

|FICHEROS;
     congz006 #0;
     congw002 #2;
     cong0008 #8;
     cong0009 #9;
     agifa019 #19;
     agifa073 #73;
     agifa104 #104;
     agifa142 #142;
|FIN;

|VARIABLES;
     aArti = "";
     nPesadas = 0;
     nConta = 0;
     &Tempo = "";
     aTab = ";";
     aLetra = "";
     aAlfa = "";
     aPath = "";
     aIP = "";
     aLon = "";
     i = 0;
     nPos = 0;
     aCar = "";
     aPathSal = "";
     aPathDes = "";
     aNom = "";
     aFic = "";
     nError = 0;
     nHandle = 0;
     aDes = "";
     nCampo = 0;
     aDato = "";
     aReg = "";
     {99}Valor = "";
     aSer = "";
     aNum = "";
     aLin = "";
     nReg = 0;
     nNum = 0;
     aEsNueva = "";
     nLin = 0;
     aHora = "";
     fFecha = @;
|FIN;

|PROCESO Recalcula;
     |SI aSer ! #2#1; |O nNum ! #2#2;
          aSer = #2#1;

          #104#25 = #2#1;
          #104#0 = #2#2;
          |LEE 121#104=;
          |SI FSalida = 0;
               |HAZ CalCabAgifa104;
               |LIBERA #104;
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE Recalcula;
     #2#2;
     ;
     "     ", 000001, 001, 0000000;
     "zzzzz", 999999, 999, 9999999;
     ;
     NULL, Recalcula;
|FIN;

|PROCESO UltimaLinea;
     #73#28 = #2#1;
     #73#0 = #2#2;
     #73#1 = 999;
     |LEE 000#73];
     |SI FSalida = 0;
          |LEE 000#73a;
     |SINO;
          |LEE 000#73u;
     |FINSI;
     |SI FSalida = 0; |Y #73#28 = #2#1; |Y #73#0 = #2#2;
          nLin = #73#1 + 1;
     |SINO;
          nLin = 1;
     |FINSI;
|FPRC;

|PROCESO Pesadas;
     #73#28 = #2#1;
     #73#0 = #2#2;
     #73#1 = #2#3;
     |LEE 121#73=;

     |SI #2#8 = "S";     || Nueva Linea
          |LIBERA #73;

          |SALVA_DATOS #73;
          |HAZ UltimaLinea;
          |REPON_DATOS #73;
          #73#1 = nLin;
          |GRABA 020#73b
     |FINSI;

     #73#5 = #2#7;
     |SI #73#2 ! #2#5;
          #19#0 = #2#5;
          |LEE 000#19=;
          |SI FSalida ! 0; |DDEFECTO #19; |FINSI;

          #73#2 = #2#5;
          #73#4 = #19#1;
     |FINSI;
     |GRABA 020#73a;
     |LIBERA #73;

     |LEE 000#9u;
     |SI FSalida = 0; nReg = #9#0 + 1; |SINO; nReg = 1; |FINSI;
     |DDEFECTO #9;
     #9#0 = nReg;
     #9#1 = #73#28;
     #9#2 = #73#0;
     #9#3 = #2#3;
     |SI #2#8 = "S";
          #9#4 = nLin;
     |FINSI;
     #9#5 = #73#2;
     #9#6 = fFecha;
     #9#7 = aHora;
     #9#8 = Usuario % 810;
     #9#9 = aNom;
     |GRABA 020#9n;
|FPRC;

|DEFBUCLE Pesadas;
     #2#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, Pesadas;
|FIN;

|PROCESO Graba;
     nPos = 100 + #142#155;
     aSer = Valor1 % nPos;

     nPos = (#142#155 + 1) * 100 + 6;
     aNum = Valor1 % nPos;

     nPos = (#142#155 + 1 + 6 + 1) * 100 + 2;
     aLin = Valor1 % nPos;

     #104#25 = aSer;
     #104#0 = aNum;
     |LEE 000#104=;
     |SI FSalida ! 0; nError = 1; |FINSI;

     #73#28 = aSer;
     #73#0 = aNum;
     #73#1 = aLin;
     |LEE 000#73=;
     |SI FSalida ! 0; nError = 1; |FINSI;

     |SI Valor2 ! "";         || la 1§ linea de resumen trae un 1, las demas nada
          nPesadas = Valor4;
          aArti = Valor3;
          nConta = 0;
          |ACABA;
     |SINO;
          nConta = nConta + 1;
     |FINSI;

     |SI nConta = 1;
          aEsNueva = "N"
     |SINO;
          aEsNueva = "S"
     |FINSI;

     nReg = nReg + 1;
     #2#0 = nReg;
     #2#1 = #73#28;
     #2#2 = #73#0;
     #2#3 = #73#1;
     #2#4 = Valor2;
     #2#5 = aArti;
     #2#6 = Valor4;
     #2#7 = Valor5;
     #2#8 = aEsNueva;
     |GRABA 020#2n;
|FPRC;

|PROCESO Dato;
     nCampo = 1;
     IValor = Valor1<-;
     aLon = aDato % A0;
     aReg = "";
     |PARA i = 1; |SI i [ aLon; |HACIENDO i = i + 1;
          nPos = i * 100 + 1;
          aLetra = aDato % nPos;
          |SI aLetra = aTab;
               Valor = aReg;
               aReg = "";
               nCampo = nCampo + 1;
               IValor = IValor + 1;
          |SINO;
               aReg = aReg + aLetra;
          |FINSI;
          |SI nCampo > 90; |SAL; |FINSI;     || Por si.... :)
     |SIGUE;
     Valor = aReg;
|FPRC;

|PROCESO Procesa;
     |SELECT #2#9;
     #9#9 = aNom;
     |LEE 000#9=;
     |SI FSalida = 0;    || Ya se ha procesado el fichero
          nError = 1;
          |ACABA;
     |FINSI;
     |SELECT #1#9;

     |BLIN 22;
     |PINTA 2201, aNom;
     |SI aIP ! "";
          |DFICE aFic; aFic = aFic + Usuario;
          |RECIBE_FICHERO aPathSal, aFic;
     |SINO;
          aFic = aPathSal;
     |FINSI;

     nError = 0;
     |XABRE "", aFic, nHandle;
     |SI FSalida ] 0;
          |XLEE nHandle, 250, aDato;
          |XLEE nHandle, 250, aDato;
          |PARA; |SI FSalida > 0; |Y nError = 0; |HACIENDO;
               |HAZ Dato;
               |HAZ Graba;
               |XLEE nHandle, 250, aDato;
          |SIGUE;
          |XCIERRA nHandle;
     |SINO;
          aAlfa = "0000Error al procesar " + aNom;
          |MENAV aAlfa;
          nError = 1;
     |FINSI;
|FPRC;

|PROCESO Historico;
     |SI nError = 1;
          aPathDes = aPath + "Error/";
     |SINO;
          aPathDes = aPath + "Historico/";
     |FINSI;
     aDes = aPathDes + aNom;
     |SI aIP = "";
          |COPIA_FICHERO aPathSal, aDes;
          |FBORRA aPathSal;
     |SINO;
          |RCOPIA_FICHERO aPathSal, aDes;
          |RFBORRA aPathSal;
     |FINSI;
|FPRC;

|PROCESO SacaNom;
     aLon = aPathSal % A0;
     aNom = "";
     |PARA i = 1; |SI i [ aLon; |HACIENDO i = i + 1;
          nPos = -(i * 100 + 1)
          aCar = aPathSal % nPos;
          |SI aCar = "\"; |O aCar = "/"; |SAL; |FINSI;
          aNom = aCar + aNom;
     |SIGUE;
     aNom = aNom > "";
|FPRC;

|PROCESO T3; |TIPO 3;
     |SI #0#0 ! "SI"; |ACABA; |FINSI;

     |HORA aHora;
     fFecha = ~;

     |ABRE #8; |LEE 000#8p; |CIERRA #8;
     |ABRE #142; |LEE 000#142p; |CIERRA #142;

     aPath = #8#2; |QBF aPath;
     aAlfa = aPath % -101;
     |SI aAlfa ! "\"; |Y aAlfa ! "/"; aPath = aPath + "/"; |FINSI;

     |HAZ CreaTempo; |NOME_DAT #2Tempo; |DELINDEX #2;

     |ABRET #104;
     |ABRET #73;
     |ABRE #19;
     |ABRE #2;
     |ABRE #9;
     aPathSal = aPath + "Salida/*.*";
     aIP = ""; |IP_REMOTO aIP;
     |SI aIP = "";
          |_PDIR aPathSal;
          |PARA; |SI FSalida = 0; |HACIENDO;
               |HAZ SacaNom;
               aAlfa = aNom % -104;
               |SI aAlfa = ".CSV";
                    |HAZ Procesa;
                    |SI nError = 0;
                         |BUCLE Pesadas;
                         aSer = "";
                         |BUCLE Recalcula;
                    |FINSI;
                    |HAZ Historico;
               |FINSI;
               |_SDIR aPathSal;
          |SIGUE;
     |SINO;
          |R_PDIR aPathSal;
          |PARA; |SI FSalida = 0; |HACIENDO;
               |HAZ SacaNom;
               aAlfa = aNom % -104;
               |SI aAlfa = ".CSV";
                    |HAZ Procesa;
                    |SI nError = 0;
||ABRE #2;|CONSULTA_CLAVE #1#2;
                         |BUCLE Pesadas;
                         aSer = "";
                         |BUCLE Recalcula;
                    |FINSI;
                    |HAZ Historico;
               |FINSI;
               |R_SDIR aPathSal;
          |SIGUE;
     |FINSI;
     |CIERRA #9;
     |CIERRA #2;
     |CIERRA #19;
     |CIERRAT #73;
     |CIERRAT #104;

     |CIERRA #2; |DELINDEX #2;
|FPRC;
