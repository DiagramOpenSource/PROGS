|FICHEROS;
     sctrz033 #0;
     sctrm036 #36;
     sctrm040 #40;
     sctrm041 #41,MANTE;
     sctrm052 #52;
     sctrm057 #57;
     sctrm001 #100;
     sctrz034 #134,MANTE;
     referen@;
|FIN;

|VARIABLES;
     aInf = "";
     &eaModo = "";
     &enReserva = 0;
     &eaCliente = "";
     &eaProg = "";
     &eaNomLogo = "";
     &enPantaLogo = 0;
     &enCtrFin = 0;
     &enFinVen = 0;
     aAlfa = "";
     nModoCab = 0;
     nModoLin = 0;
     nPanta = 0;
     nPantaLin = 0;
     nRango = 0;
     nGrid = 0;
     nGridLin = 0;
     nBtnAlta = 0;
     nBtnModi = 0;
     nBtnBaja = 0;
     nBtnCons = 0;
     nBtnImpr = 0;
     nBtnTodos = 0;
     aReg0 = "";
     nReg1 = 0;
     nBloquea = 0;
     i = 0;
     nId = 0;
     aConte = "";
     nLin = 0;
     nSwFin = 0;
     nHay = 0;
|FIN;

|PROCESO BtnReserva;
     |SI #41#3 ! 0;
          eaModo = "CONSULTA";
          enReserva = #41#3;
          enFinVen = 3099;
          eaProg = "sctrz056";
          |HAZ EjecutaProg;
     |FINSI;
|FPRC;

|PROCESO Reserva0; |TIPO 0;
     |ABRE #52;
     #52#0 = #41#3;
     |LEE 000#52=;
     |SI FSalida ! 0;
          |MENAV "0000No existe c¢digo reserva...";
          |ERROR;
     |FINSI;
     |CIERRA #52;
|FPRC;

|PROCESO Reserva66; |TIPO 66;
     |ABRE #52;
     #52#0 = #41#3;
     |LEE 000#52];
     |CONSULTA_CLAVE #1#52;
     |SI FSalida = 0;
          #41#3 = #52#0; |PINTA #41#3;
     |SINO;
          |ERROR;
     |FINSI;
     |CIERRA #52;
|FPRC;

|PROCESO CheqBono; |TIPO 0;
     |SI nModoCab = 1; |O #134Campo ! aConte;
          |SI #41#2 < #134#3; |O #41#2 > #134#4;
               |MENAV "0000El N§ Bono no esta comprendido entre los l¡mites...";
               |ERROR;
          |SINO;
               |DDEF aAlfa;
               aAlfa = aAlfa + "sctrm041";
               |CARGA_DEF aAlfa, referen@;
               |SI FSalida = 0;
                    |ABRE #referen@;
                    |SELECT #2#referen@;
                    #referen@#2 = #41#2;
                    |LEE 000#referen@.=;
                    |PARA; |SI FSalida = 0; |HACIENDO;
                         |SI #referen@#0 ! #41#0; |Y #referen@#1 ! #41#1;
                              aAlfa = "0000El bono existe en cliente/l¡nea:" + #referen@#0 + "/" + #referen@#1 + " ...";
                              |MENAV aAlfa;
                              |ERROR;
                              |SAL;
                         |FINSI;
                         |LEE 000#referen@.s;
                    |SIGUE;
                    |CIERRA #referen@;
                    |DESCARGA_DEF #referen@;
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO DefeBono; |TIPO 7;
     nModoLin = (FEntrada / 100) ! 0;
     |SI nModoLin = 1;
          #41#0 = #40#0;
          #41#1 = #40#1;
          #41#2 = 9999999999;
          |LEE 000#41];
          |SI FSalida = 0;
               |LEE 000#41a;
          |SINO;
               |LEE 000#41u;
          |FINSI;
          |SI FSalida = 0; |Y #41#0 = #40#0; |Y #41#1 = #40#1;
               nLin = #41#2 + 1;
               |SI nLin < #134#3;
                    nLin = #134#3;
               |FINSI;
          |SINO;
               nLin = #134#3;
          |FINSI;
          |DDEFECTO #41;
          #41#0 = #40#0;
          #41#1 = #40#1;
          #41#2 = nLin; |PINTA #41#2;
     |FINSI;
|FPRC;

|PROCESO Chequea; |TIPO 0;
     |SI nModoCab = 1; |Y nBloquea = 0;
          |DDEFECTO #40;
          #40#0 = #134#0;
          #40#1 = #134#1;
          |GRABA 000#40b;
          |SI FSalida ! 0;
               |MENAV "0000Registro existente...";
               |ERROR;
          |SINO;
               |C_M #134#0, 1, "N"; |PINTA #134#0;
               |C_M #134#1, 1, "N"; |PINTA #134#1;
               nBloquea = 1;

               aAlfa = "|X000"; aAlfa = #134aAlfa; nId = aAlfa;
               |ESTADO_CONTROL nId, "ENABLE";
               |REFRESCACONTROL nGridLin;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Lin7; |TIPO 7;
     |SI nModoCab = 1; |Y nBloquea = 0;
          #40#0 = #134#0;
          #40#1 = 999;
          |LEE 000#40];
          |SI FSalida = 0;
               |LEE 000#40a;
          |SINO;
               |LEE 000#40u;
          |FINSI;
          |SI FSalida = 0; |Y #40#0 = #134#0;
               nLin = #40#1 + 1;
          |SINO;
               nLin = 1;
          |FINSI;
          #134#1 = nLin; |PINTA #134#1;
     |FINSI;
|FPRC;

|PROCESO Cliente; |TIPO 0;
     |ABRE #36;
     #36#1 = #134#0;
     |LEE 000#36=;
     |SI FSalida = 0;
          #134#2 = #36#2; |PINTA #134#2;
          |SI #36#50 = "N";
               |MENAV "0000El cliente no tiene activo 'Control bonos viaje'...";
               |ERROR;
          |FINSI;
     |SINO;
          |MENAV "0000No existe c¢digo de cliente...";
          |ERROR;
     |FINSI;
     |CIERRA #36;
|FPRC;

|PROCESO CheqBonos;
     |SI #40#0 ! #134#0; |Y #40#1 ! #134#1;
          |SI #referen@#4 ] #134#3; |Y #referen@#3 [ #134#4;
               aAlfa = "0000Bonos incluidos en el cliente/l¡nea:" + #134#0 + "/" + #134#1 + "...";
               |MENAV aAlfa;
               |ERROR10;
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE CheqBonos;
     #referen@#1002;
     ;
     "      ", 000;
     "zzzzzz", 999;
     ;
     NULL, CheqBonos;
|FIN;

|PROCESO HastaBono; |TIPO 0;
     |SI #134#3 > #134#4;
          |MENAV "0000Bono incorrecto.... ";
          |ERROR;
     |SINO;
          |DDEF aAlfa;
          aAlfa = aAlfa + "sctrm040";
          |CARGA_DEF aAlfa, referen@;
          |SI FSalida = 0;
               aAlfa = "";
               |BUCLE CheqBonos;
               |SI aAlfa ! ""; |ERROR; |FINSI;
               |DESCARGA_DEF #referen@;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Conte; |TIPO 7;
     aConte = #134Campo;
|FPRC;

|PROCESO Valida; |TIPO 7;
     |SI nModoCab = 1;
          aAlfa = "|X000"; aAlfa = #134aAlfa; nId = aAlfa;
          |ESTADO_CONTROL nId, "DISABLE";
     |FINSI;
|FPRC;

|PROCESO BorraLin;
     |BORRA 020#41a;
|FPRC;

|DEFBUCLE BorraLin;
     #41#1;
     ;
     #40#0, #40#1, 0000000000;
     #40#0, #40#1, 9999999999;
     be;
     NULL, BorraLin;
|FIN;

|PROCESO Min41;
     #41#0 = #134#0;
     #41#1 = #134#1;
     #41#2 = 0000000000;
|FPRC;

|PROCESO Max41;
     #41#0 = #134#0;
     #41#1 = #134#1;
     #41#2 = 9999999999;
|FPRC;

|PROCESO Edita;
     aReg0 = #40#0;
     nReg1 = #40#1;

     |SI nModoCab = 1; |VENTANA 901, 2898, -1, 17, "Nuevo Bono Viaje"; |FINSI;
     |SI nModoCab = 2; |VENTANA 901, 2898, -1, 17, "Modifica Bono Viaje"; |FINSI;
     |SI nModoCab = 4; |VENTANA 901, 2898, -1, 17, "Consulta Bono Viaje"; |FINSI;
     |HAZ PushVenModal;

     |SI nModoCab = 1;
          |DDEFECTO #134;
          |SI eaCliente ! "";
               |C_M #134#0, 1, "N";
               #134#0 = eaCliente;
               |HAZ Cliente;
          |SINO;
               |C_M #134#0, 1, "S";
          |FINSI;
          |C_M #134#1, 1, "S";
     |SINO;
          |PARA i = 0; |SI i [ 4; |HACIENDO i = i + 1;
               #134i = #40i;
          |SIGUE;
          |C_M #134#0, 1, "N";
          |C_M #134#1, 1, "N";
     |FINSI;

     |PINPA #0#134; nPantaLin = FSalida;
     |PINDA #0#134;

     nRango = 1 + 4 + 8 + 16 + 32;
     |LINEAL_SIMPLE #1#41, nRango, 1102, 2498, Min41, Max41, NULL;
     nGridLin = FSalida;
     |CONTROL_SIMPLE nPantaLin, "BOTON,{{155}}Reserva", 984, 994, BtnReserva;

     nBloquea = 0;
     |SI nModoCab = 1; |ENDATOS #1#134; |FINSI;
     |SI nModoCab = 2; |ENDATOS #2#134; |FINSI;
     |SI nModoCab = 4; |PAUSA; |FINSI;
     |SI FSalida = 0; |Y nModoCab ! 4;
          #40#0 = #134#0;
          #40#1 = #134#1;
          |LEE 121#40=;
          |PARA i = 0; |SI i [ 4; |HACIENDO i = i + 1;
               #40i = #134i;
          |SIGUE;
          |GRABA 020#40a;
          aReg0 = #40#0;
          nReg1 = #40#1;
     |SINO;
          |SI nBloquea = 1;
               #40#0 = #134#0;
               #40#1 = #134#1;
               |BORRA 020#40c;
               |BUCLE BorraLin;
          |FINSI;
     |FINSI;
     |LIBERA #40;

     |HAZ PopVenModal;

     #40#0 = aReg0;
     #40#1 = nReg1;
     |LEE 000#40=;
     |REFRESCACONTROL nGrid;
|FPRC;

|PROCESO BtnAlta;
     nModoCab = 1; |HAZ Edita;
     |HAZ TieneLineas;
|FPRC;

|PROCESO BtnModi;
     |LEE 100#40=;
     |SI FSalida ! 0; |ACABA; |FINSI;
     nModoCab = 2; |HAZ Edita;
|FPRC;

|PROCESO BtnBaja;
     |LEE 100#40=;
     |SI FSalida ! 0; |ACABA; |FINSI;
     aAlfa = "0000N¨Borrar bono viajes " + #40#0 + "/" + #40#1;
     |QBF aAlfa;
     aAlfa = aAlfa + "?";
     |CONFI aAlfa;
     |SI FSalida = 0;
          |BORRA 020#40a;
          |BUCLE BorraLin;
          |HAZ TieneLineas;
          |REFRESCACONTROL nGrid;
     |FINSI;
     |LIBERA #40;
|FPRC;

|PROCESO BtnCons;
     |LEE 000#40=;
     |SI FSalida ! 0; |ACABA; |FINSI;
     nModoCab = 4; |HAZ Edita;
|FPRC;

|PROCESO Lineas;
     |PRINT;
|FPRC;

|DEFBUCLE Lineas;
     #41#1;
     ;
     #40#0, #40#1, 0000000000;
     #40#0, #40#1, 9999999999;
     ;
     NULL, Lineas;
|FIN;

|PROCESO Imprime;
     Impresora = "CrystalReport";
     |IMPRE -1;
     |SI FSalida = 0;
          aInf = #57#7; |QBF aInf;
          |INFOR aInf;
          |SI FSalida = 0;
               nHay = 0;
               |BUCLE Lineas;
               |SI nHay = 0; |DDEFECTO #41; |PRINT; |FINSI;
               |PIEINF;
               |FININF;
          |SINO;
               aInf = "0000Error en in forme:" + aInf;
               |MENAV aInf;
          |FINSI;
          |FINIMP;
     |FINSI;
|FPRC;

|PROCESO BtnImpr;
     |LEE 000#40=;
     |SI FSalida ! 0; |ACABA; |FINSI;
     |HAZ Imprime;
|FPRC;

|PROCESO Evento;
     |SI FSalida = 1; |O FSalida = 2;
          |SELECT #1#40;
          |LEE 000#40=;
          |HAZ TieneLineas;
     |FINSI;
     |SI FSalida = 4;
          |HAZ BtnCons;
     |FINSI;
|FPRC;

|PROCESO TieneLineas;
     |DDEF aAlfa;
     aAlfa = aAlfa + "sctrm040";
     |CARGA_DEF aAlfa, referen@;
     |SI FSalida = 0;
          |ABRE #referen@;
          |SI eaCliente = "";
               |LEE 000#referen@.p;
          |SINO;
               |DDEFECTO #referen@;
               #referen@#0 = eaCliente;
               |LEE 000#referen@.];
               |SI FSalida ! 0; |O #referen@#0 ! eaCliente;
                    FSalida = "1";
               |FINSI;
          |FINSI;
          |SI FSalida = 0;
               |ESTADO_CONTROL nBtnModi, "ENABLE";
               |ESTADO_CONTROL nBtnBaja, "ENABLE";
               |ESTADO_CONTROL nBtnCons, "ENABLE";
               |ESTADO_CONTROL nBtnImpr, "ENABLE";
               |ESTADO_CONTROL nBtnImpr, "ENABLE";
          |SINO;
               |ESTADO_CONTROL nBtnModi, "DISABLE";
               |ESTADO_CONTROL nBtnBaja, "DISABLE";
               |ESTADO_CONTROL nBtnCons, "DISABLE";
               |ESTADO_CONTROL nBtnImpr, "DISABLE";
          |FINSI;
          |CIERRA #referen@;
          |DESCARGA_DEF #referen@;
     |FINSI;
|FPRC;

|PROCESO LogoPanta;
     eaNomLogo = #100#1;
     |HAZ TraeLogoPan;
     enPantaLogo = nPanta;
     |HAZ PintaLogoPan;
|FPRC;

|PROCESO BtnTodos;
     nSwFin = 1;
     eaCliente = "";
     |PTEC 807;
|FPRC;

|PROCESO Min;
     #40#0 = eaCliente;
     #40#1 = 000;
|FPRC;

|PROCESO Max;
     #40#0 = eaCliente;
     #40#1 = 999;
|FPRC;

|PROGRAMA;
     |ABRE #100; |LEE 000#100p; |CIERRA #100;
     |ABRE #57; |LEE 000#57p; |CIERRA #57;

     |PINPA #0#0; nPanta = FSalida;
     |PINDA #0#0;
     |HAZ LogoPanta;

     |CONTROL_SIMPLE nPanta, "BOTON,{{193}}Alta", 1002, 1012, BtnAlta;
     nBtnAlta = FSalida;
     |CONTROL_SIMPLE nPanta, "BOTON,{{197}}Modificar", 1014, 1024, BtnModi;
     nBtnModi = FSalida;
     |CONTROL_SIMPLE nPanta, "BOTON,{{199}}Baja", 1026, 1037, BtnBaja;
     nBtnBaja = FSalida;
     |CONTROL_SIMPLE nPanta, "BOTON,{{155}}Consulta", 1040, 1048, BtnCons;
     nBtnCons = FSalida;
     |CONTROL_SIMPLE nPanta, "BOTON,{{205}}Impresi¢n", 1050, 1060, BtnImpr;
     nBtnImpr = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,{{230}}Todos", 1088, 1098, BtnTodos;
     nBtnTodos = FSalida;

     |ABRET #40;
     |ABRET #41;
     |PARA nSwFin = 1; |SI nSwFin = 1; |HACIENDO;
          |HAZ TieneLineas;
          nSwFin = 0;
          nRango = 4 + 8 + 16 + 32;
          |SI eaCliente = "";
               |ESTADO_CONTROL nBtnTodos, "DISABLE";
               |LINEAL_SIMPLE #1#40, nRango, 1102, 2999, NULL, NULL, Evento;
          |SINO;
               |LINEAL_SIMPLE #1#40, nRango, 1102, 2999, Min, Max, Evento;
          |FINSI;
          nGrid = FSalida;
          enCtrFin = nGrid;
          |HAZ EsperaFin;
          |PULSATECLA;
          |DESTRUYECONTROL nGrid;
     |SIGUE;
     |CIERRAT #41;
     |CIERRAT #40;
|FPRO;
