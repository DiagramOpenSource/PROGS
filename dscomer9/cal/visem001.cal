|FICHEROS;
     visem001 #0;
     visem002 #1;
     visem003 #2,MANTE;
     visem004 #3;
     visem005 #5;        || Orden C
     visem006 #6;        || Orden L
     agifa080 #13;       || Alba Compras L
     agifa077 #14;       || Alba Compras C
     agifa017 #17;
     agifa019 #19;
     agi00029 #29,MANTE;
     agifa041 #41;
     agifa067 #67;
     agifa081 #81;
     dsm00082 #82;
     dsm00083 #83;       || DA pedido
     gopartia #99;
     goparame #100;
     go000002 #102;
     agitp001 #104;
     goprepec #105;
     goprepel #106;
     agifa112 #112;
     agifa142 #142;
     agifa321 #321;
     agifa324 #324;
     agifa019@;     ||Se usa de Temporal
     visem003@;
     referen@;
|FIN;

|INCLUYE i_varart;

|VARIABLES;
     &enSwMenu = 0;
     &enSwImp = 0;
     &nDeci_PreDiv = 0;
     &nDeci_Base = 0;
     &nDeci_PreBase = 0;
     &aDivisa = "";
     &aMoneda = "";
     &enPrepe = 0;
     &enPreli = 0;
     &eaCodigo = "";
     aArtiAnt = "";
     aPro = "";
     nPre = 0;
     nDto = 0;
     linea = 0;
     &Tempo = "";
     aTmp19 = "";
     aTmp29 = "";
     aAlfa = "";
     nConta = 0;
     nModo = 0;
     nForma = 0;
     nLin = 0;
     nTecla = 0;
     i = 0;
     nCampo = 0;
     nUni = 0;
     nUniAnt = 0;
     nHayPed = 0;
     nHayOrd = 0;
 {-1}aMenu  = "";
     aMenu1 = "2400";
     aMenu2 = "1";
     aMenu3 = " ";
     aMenu4 = "IPOT";
     aMenu5 = " Imprimir ";
     aMenu6 = " Generar Pedido ";
     aMenu7 = " Generar Orden ";
     aMenu8 = " Generar Todo ";
     aSql = "";
     aDef = "";
     aDirEmp = "";
     aDatos = "";
     aProve = "";
     fmes1 = "";
     mes = 0;
     nImpo = 0;
     nPrepe = 0;
     nLinPre = 0;
     aSerie = "";
     nContaPed = 0;
     nContaOrd = 0;
     nOrden = 0;
     nLinOrd = 0;
     nSwAct = 0;
|FIN;

|PROCESO CreaCabOrden;
     #5#0 = #2#28;
     #5#1 = 999999;
     |LEE 000#5];
     |SI FSalida = 0;
          |LEE 000#5a;
     |SINO;
          |LEE 000#5u;
     |FINSI;
     |SI FSalida = 0; |Y #5#0 = #2#28;
          nOrden = #5#1 + 1;
     |SINO;
          nOrden = 1;
     |FINSI;
     |DDEFECTO #5;
     #5#0 = #2#28;
     #5#1 = nOrden;
     #5#2 = ~;
     |GRABA 020#5n;
|FPRC;

|PROCESO CreaOrden;
     |SI aSerie ! #2#28;
          nContaOrd = nContaOrd + 1;
          |HAZ CreaCabOrden;
          nLinOrd = 0;
          aSerie = #2#28;
     |FINSI;
     |ABRE #19;
     #19#0 = #2#3;
     |LEE 000#19=;
     |CIERRA #19;

     nLinOrd = nLinOrd + 1;
     |DDEFECTO #6;
     #6#0 = #5#0;
     #6#1 = #5#1;
     #6#2 = nLinOrd;
     #6#3 = #2#3;
     #6#4 = #19#1;
     #6#5 = #2#6;
     #6#7 = #0#3;
     #6#8 = #0#7;
     #6#9 = #0#4;
     #6#10 = #2#24;
     |GRABA 020#6n;

     || Act Stock
     |DDEFECTO #19;
     #19#0 = #6#3;
     |LEE 101#19=;
     |SI FSalida ! 0; |GRABA 020#19b; |FINSI;
     |DDEFECTO #17;
     #17#0 = #6#3;
     #17#1 = #6#10;
     |LEE 101#17=;
     |SI FSalida ! 0; |GRABA 020#17b; |FINSI;
     #19#13 = #19#13 + #6#5;
     #17#9 = #17#9 + #6#5;
     |GRABA 020#17a;
     |GRABA 020#19a;
     |LIBERA #17;
     |LIBERA #19;

     || Act. Linea
     #2#28 = #5#0;
     #2#29 = #5#1;
     |GRABA 020#2a;
     |LIBERA #2;

     #1#0 = #2#0;
     #1#1 = #2#1;
     |LEE 101#1=;
     #1#22 = #1#22 - #2#6;
     |GRABA 020#1a;
|FPRC;

|DEFBUCLE CreaOrden;
     #2#1;
     29, 11;
     #0#0, 001, 001, 0, "O";
     #0#0, 999, 999, 0, "O";
     be;
     NULL, NULL, NULL, NULL, NULL, CreaOrden;
     #2#28, #2#3, #2#24;
|FIN;

|PROCESO CreaOrden0;
     nContaOrd = 0;
     aSerie = "";
     |ABRE #6;
     |ABRE #5;
     |ABRE #1;
     |ABRE #17;
     |ABRE #19;
     |BUCLE CreaOrden;
     |CIERRA #19;
     |CIERRA #17;
     |CIERRA #1;
     |CIERRA #5;
     |CIERRA #6;
     aAlfa = "0000Creados " + nContaOrd + " ordenes.";
     |MENAV aAlfa;
|FPRC;

|PROCESO ActProv;
     fmes1 = #81#1 % A402;
     mes = fmes1 + 42;
     |SI #19#196 = 2;
          nImpo = (((((#67#41 * #67#6) < #67#8) < #67#27) < #67#57) < #81#5) < #81#17;
     |SINO;
          nImpo = (((((#67#5 * #67#6) < #67#8) < #67#27) < #67#57) < #81#5) < #81#17;
     |FINSI;
     |SI #142#114 = "S";
          nImpo = nImpo < #81#6;
     |FINSI;
     #112mes = #112mes + (nImpo * nForma);
     #112#55 = #112#55 + (nImpo * nForma);

     enImpPrvPed = nImpo * nForma;
     efFecha = #81#1;
     eaProve = #81#4;
     |HAZ AcumulaPrv;
|FPRC;

|PROCESO CreaCabPedi;
     |LIBERA #112;
     |DDEFECTO #112;
     #112#0 = #2#5;
     |LEE 101#112=;

     #81#25 = #2#25;
     enSwMenu = 1;
     |HAZ ContaPC7;
     #81#1 = ~;
     #81#2 = #2#22;
     #81#4 = #112#0;
     #81#5 = #112#24;
     #81#6 = #112#25;
     #81#8 = #112#1;
     #81#9 = #112#17;
/*
     #81#18 = #41#2;        || direccion de la empresa
     #81#19 = #41#1;        || nombre de la empresa
     #81#20 = #41#3;        || poblacion de la empresa
     #81#21 = #41#4;        || provincia de la empresa
*/
     #81#18 = #102#2;
     #81#19 = #102#1;
     #81#20 = #102#3;
     #81#21 = #102#4;

     #81#27 = #2#12;

     #324#0 = #81#27;
     |LEE 000#324=;
     #81#28 = #324#3;
     #81#29 = #112#81;
     #81#32 = #321#9;
     #324#0 = #81#32;
     |LEE 000#324=;
     #81#33 = #324#3;
     |GRABA 020#81b;
|FPRC;

|PROCESO CreaCabPrepe;
     |LEE 000#105u;
     |SI FSalida = 0;
          nPrepe = #105#0 + 1;
     |SINO;
          nPrepe = 1;
     |FINSI;
     |DDEFECTO #105;
     #105#0 = nPrepe;
     #105#1 = #0#1;
     #105#2 = "S";
     #105#3 = #0#3;
     #105#4 = #0#4;
     #105#7 = #0#7;
     #105#8 = #0#8;
     #105#9 = #0#9;
     #105#10 = #0#10;
     #105#14 = #0#14;
     #105#15 = "N";
     |GRABA 020#105n;
|FPRC;

|PROCESO DAPedido;
     #83#0 = "P";
     #83#1 = #67#28;
     #83#2 = #67#0;
     #83#3 = #67#1;
     #83#4 = #3#4;
     #83#5 = #3#3;
     |GRABA 020#83n;
|FPRC;

|DEFBUCLE DAPedido;
     #3#1;
     ;
     #2#0, #2#1, #2#2, 001;
     #2#0, #2#1, #2#2, 999;
     ;
     NULL, DAPedido;
|FIN;

|PROCESO CreaPedido;
     |SI aProve ! #2#5; |O aSerie ! #2#25;
          |SI aProve = "";
               nContaPed = nContaPed + 1;
               |HAZ CreaCabPrepe;
               nLinPre = 0;
          |FINSI;
          |SI aProve ! "";
               |BUCLELIN 2 ActProv #81;
          |FINSI;
          nLin = 0;
          |HAZ CreaCabPedi;
          aProve = #2#5;
          aSerie = #2#25;
     |FINSI;
     |ABRE #19;
     #19#0 = #2#3;
     |LEE 000#19=;
     |CIERRA #19;

     nLin = nLin + 1;
     |DDEFECTO #67;
     #67#28 = #81#25;
     #67#0 = #81#0;
     #67#1 = nLin;
     #67#2 = #2#3;
     #67#3 = #2#24;
     #67#4 = #19#195;
     #67#5 = #2#6;
     #67#6 = #2#7;
     #67#8 = #2#8;
     #67#13 = #2#22;
     #67#14 = #19#30;
     #67#18 = #19#2;
     #67#20 = #112#2;
     #67#23 = #81#5;
     #67#24 = #81#6;
     #67#25 = #81#17;
     #67#26 = #81#9;
     #67#33 = #67#6;
     #67#39 = #2#13;
     #67#40 = #2#14;
     #67#41 = #2#15;
     #67#42 = #2#16;
     #67#43 = #2#17;
     #67#45 = #67#5;
     #67#46 = #2#15;
     #67#47 = #2#18;
     #67#48 = #2#19;
     #67#49 = #2#20;
     #67#54 = #2#21;
     |GRABA 020#67n;

     |BUCLE DAPedido;
     enForma = 1;
     eaDocu = "agifa067";
     |HAZ StockEntra;

     nLinPre = nLinPre + 1;
     |DDEFECTO #106;
     #106#0 = #105#0;
     #106#1 = nLinPre;
     #106#2 = #2#3;
     #106#3 = #2#4;
     #106#4 = #2#5;
     #106#5 = #2#6;
     #106#6 = #2#7;
     #106#7 = #2#8;
     #106#8 = #2#9;
     #106#9 = #2#10;
     #106#10 = "S";
     #106#11 = #2#12;
     #106#12 = #2#13;
     #106#13 = #2#14;
     #106#14 = #2#15;
     #106#15 = #2#16;
     #106#16 = #2#17;
     #106#17 = #2#18;
     #106#18 = #2#19;
     #106#19 = #2#20;
     #106#20 = #2#21;
     #106#21 = #2#22;
     #106#33 = #2#24;
     #106#28 = #67#28;
     #106#29 = #67#0;
     |GRABA 020#106n;

     || Act. Linea
     #2#25 = #67#28;
     #2#26 = #67#0;
     #2#27 = #67#1;
     #2#30 = #106#0;
     #2#31 = #106#1;
     |GRABA 020#2a;
     |LIBERA #2;

     #1#0 = #2#0;
     #1#1 = #2#1;
     |LEE 101#1=;
     #1#22 = #1#22 - #2#6;
     |GRABA 020#1a;
|FPRC;

|DEFBUCLE CreaPedido;
     #2#1;
     26, 11;
     #0#0, 001, 001, 0, "R";
     #0#0, 999, 999, 0, "R";
     be;
     NULL, NULL, NULL, NULL, NULL, CreaPedido;
     #2#25, #2#5, #2#3, #2#24;
|FIN;

|PROCESO CreaPedido0;
     aProve = "";
     aSerie = "";
     nContaPed = 0;
     |ABRE #321; |LEE 000#321p; |CIERRA #321;
     |ABRE #142; |LEE 000#142p; |CIERRA #142;
     |ABRE #102;
     #102#0 = #0#14;
     |LEE 000#102=;
     |CIERRA #102;

     |DFICE aDirEmp;
     |DFICB aDatos;
     aDirEmp = aDirEmp % -205;
     |PATH_DAT #41 aDatos;
     |ABRE #41;
     #41#0 = aDirEmp;
     |LEE 000#41=;
     |CIERRA #41;

     |ABRE #81;
     |ABRE #67;
     |ABRE #112;
     |ABRE #105;
     |ABRE #106;
     |ABRE #1;
     |ABRE #83;
     |BUCLE CreaPedido;
     |SI aProve ! ""; |HAZ CalCabAgifa081; |BUCLELIN 2 ActProv #81; |FINSI;
     |CIERRA #83;
     |CIERRA #1;
     |CIERRA #106;
     |CIERRA #105;
     |CIERRA #112;
     |CIERRA #81;
     |CIERRA #67;
     aAlfa = "0000Creados " + nContaPed + " pedidos.";
     |MENAV aAlfa;
|FPRC;

|PROCESO Print;
     |PRINT;
|FPRC;

|DEFBUCLE ImpreDA;
     #3#1;
     ;
     #2#0, #2#1, #2#2, 001;
     #2#0, #2#1, #2#2, 999;
     ;
     NULL, Print;
|FIN;

|PROCESO ImpreSubLin;
     enSwImp = 2;
     |BUCLE ImpreDA;
     enSwImp = 1;
     |PRINT;
|FPRC;

|DEFBUCLE ImpreSubLin;
     #2#1;
     ;
     #1#0, #1#1, 001;
     #1#0, #1#1, 999;
     ;
     NULL, Print;
|FIN;

|PROCESO ImpreLin;
     enSwImp = 0;
     |PRINT;
     |BUCLE ImpreSubLin;
|FPRC;

|PROCESO Imprimir;
     |IMPRE 0;
     |SI FSalida = 0;
          |INFOR "vise0001";
          |SI FSalida ! 0;
               |MENAV "0000No existe informe 'vise0001'";
          |SINO;
               |BUCLELIN 2 ImpreLin #0;
               |PIEINF;
               |FININF;
          |FINSI;
          |FINIMP;
     |FINSI;
|FPRC;

--CABECERA-----------------------------------------------------------
|PROCESO CheqPedOrd;
     |SI #2#26 ! 0; |O #2#29 ! 0;
          nSwAct = 1;
     |FINSI;
|FPRC;

|DEFBUCLE CheqPedOrd0;
     #2#1;
     ;
     #0#0, 001, 001;
     #0#0, 999, 999;
     ;
     NULL, CheqPedOrd;
|FIN;

|PROCESO Tipo2Cab; |TIPO 2;
     nForma = 0 +. 1;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 3;
          nSwAct = 0;
          |BUCLE CheqPedOrd0;
          |SI nSwAct ! 0;
               |SI nForma = -1; |MENAV "0000Hay lineas actualizadas..."; |FINSI;
               |ERROR; |ACABA;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Prepe20; |TIPO 20;
     |SI FSalida = "OPCION";
          FSalida = "Opciones";
          |ACABA;
     |FINSI;
     |LEE 000#0=;
     |SI FSalida ! 0; |ACABA; |FINSI;
     |MENU aMenu;
     aMenu2 = FSalida;
     |SI aMenu2 = 1; |HAZ Imprimir; |FINSI;
     |SI aMenu2 = 2; |O aMenu2 = 4; |HAZ CreaPedido0; |FINSI;
     |SI aMenu2 = 3; |O aMenu2 = 4; |HAZ CreaOrden0; |FINSI;
|FPRC;

|PROCESO HayPedOrd;
     |SI #2#11 = "R"; |Y #2#26 = 0;
          nHayPed = 0;
     |FINSI;
     |SI #2#11 = "O"; |Y #2#29 = 0;
          nHayOrd = 0;
     |FINSI;
     |SI nHayPed = 0; |Y nHayOrd = 0;
          |ERROR10;
     |FINSI;
|FPRC;

|DEFBUCLE HayPedOrd;
     #2#1;
     ;
     #0#0, 001, 001;
     #0#0, 999, 999;
     ;
     NULL, HayPedOrd;
|FIN;

|PROCESO Final; |TIPO 3;
     nHayPed = 1;
     nHayOrd = 1;
     |BUCLE HayPedOrd;
     |SI nHayPed = 0;
          |CONFI "0000SCrear Pedidos pendientes";
          nHayPed = FSalida;
     |FINSI;
     |SI nHayOrd = 0;
          |CONFI "0000SCrear Ordenes de fabricacion";
          nHayOrd = FSalida;
     |FINSI;
     |SI nHayPed = 0; |HAZ CreaPedido0; |FINSI;
     |SI nHayOrd = 0; |HAZ CreaOrden0; |FINSI;
|FPRC;

|PROCESO CargaUsu; |TIPO 7;
     aAlfa = #0#10; |QBF aAlfa;
     |SI aAlfa = "";
          #0#10 = Usuario % 810;
          |PINTA #0#10;
     |FINSI;
|FPRC;

|PROCESO ContaPrepe; |TIPO 7;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 1;
          |LEE 000#0u;
          |SI FSalida = 0;
               nConta = #0#0 + 1;
          |SINO;
               nConta = 1;
          |FINSI;
          |DDEFECTO #0;
          #0#0 = nConta;
     |FINSI;
|FPRC;
-LINEAS--------------------------------------------------------------
|PROCESO Tipo8; |TIPO 8;
     |ABRE #100; |LEE 000#100p; |CIERRA #100;
     |ABRE #142; |LEE 000#142p; |CIERRA #142;

     |DDEF aAlfa;
     aAlfa = aAlfa + "agifa019";
     |CARGA_DEF aAlfa, agifa019@;
     |SI FSalida ! 0;
          aAlfa = "";
     |SINO;
          |HAZ CreaTempo; aTmp19 = Tempo;
          |NOME_DAT #agifa019@#aTmp19#; |DELINDEX #agifa019@;
          |ABRE #agifa019@;
     |FINSI;
|FPRC;

|PROCESO Tipo9; |TIPO 9;
     |SI aTmp19 ! "";
          |CIERRA #agifa019@;
          |DELINDEX #agifa019@; Tempo = aTmp19; |HAZ BoraTempo;
          |DESCARGA_DEF #agifa019@;
     |FINSI;
|FPRC;

|PROCESO agifa019;
     |SI #100#6 = #19#126; |O #100#7 = #19#126;
          |SALVA_DATOS #19;
          |REPON_DATOS #agifa019@;
          |GRABA 020#agifa019@.n;
     |FINSI;
|FPRC;

|DEFBUCLE agifa019;
     #19#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, agifa019;
|FIN;

|PROCESO ArtiLin0; |TIPO 0;
     |SI FSalida = 10; || F2
          |SI aTmp19 = "";
               |ABRE #19;
               #19#0 = #1#2;
               |LEE 000#19];
               |CONSULTA_CLAVE #1#19;
               |SI FSalida = 0;
                    #1#2 = #19#0; |PINTA #1#2;
               |SINO;
                    |ERROR; |ACABA;
               |FINSI;
               |CIERRA #19;
          |SINO;
               |LEE 000#agifa019@.p;
               |SI FSalida ! 0;
                    |PUSHV 0501 2380;
                    |BLANCO 1529 2051;
                    |CUADRO 1529 2051;
                    |CUADRO 1630 1950;
                    |ATRI P; |PINTA 1733, "  CARGANDO..."; |ATRI 0;
                    |BUCLE agifa019;
                    |POPV;
               |FINSI;
               #agifa019@#0 = #1#2;
               |LEE 000#agifa019@.];
               |CONSULTA_CLAVE #1#agifa019@;
               |SI FSalida = 0;
                    #1#2 = #agifa019@#0; |PINTA #1#2;
               |SINO;
                    |ERROR; |ACABA;
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Min3;
     #3#0 = #2#0;
     #3#1 = #2#1;
     #3#2 = #2#2;
     #3#4 = 1;
|FPRC;

|PROCESO Max3;
     #3#0 = #2#0;
     #3#1 = #2#1;
     #3#2 = #2#2;
     #3#4 = 999;
|FPRC;

|PROCESO Min2;
     #2#0 = #1#0;
     #2#1 = #1#1;
     #2#2 = 1;
|FPRC;

|PROCESO Max2;
     #2#0 = #1#0;
     #2#1 = #1#1;
     #2#2 = 999;
|FPRC;

|PROCESO dsm00082;
     nLin = nLin + 1;
     #3#0 = #2#0;
     #3#1 = #2#1;
     #3#2 = #2#2;
     #3#3 = #82#2;
     #3#4 = nLin;
     |GRABA 020#3n;
|FPRC;

|DEFBUCLE dsm00082;
     #82#1;
     ;
     #1#2, 001;
     #1#2, 999;
     ;
     NULL, dsm00082;
|FIN;

|PROCESO GraDesAmpli;
     |SI #142#206 = "N"; |ACABA; |FINSI;
     |ABRE #3;
     #3#0 = #2#0;
     #3#1 = #2#1;
     #3#2 = #2#2;
     #3#4 = 1;
     |LEE 000#3];
     |SI FSalida = 0; |Y #3#0 = #2#0; |Y #3#1 = #2#1; |Y #3#2 = #2#2;
     |SINO;
          nLin = 0;
          |BUCLE dsm00082;
     |FINSI;
     |CIERRA #3;
|FPRC;

|PROCESO CuentaUni;
     #1#5 = #1#5 + #2#6;
     |SI #2#26 = 0; |Y #2#29 = 0;
          #1#22 = #1#22 + #2#6;
     |FINSI;
|FPRC;

|DEFBUCLE CuentaUni;
     #2#1;
     ;
     #1#0, #1#1, 001;
     #1#0, #1#1, 999;
     ;
     NULL, CuentaUni;
|FIN;

|PROCESO TeclaLin; |TIPO 11;
     nModo = (FEntrada / 100) ! 0;
     nTecla = FSalida;
     |SI nTecla = 13; ||F5
          |PUSHV 0501 2380;
          |ENTLINEAL #1#2, 3, nModo, 22, 1, Min2, Max2;
          |POPV;
          #1#5 = 0; #1#22 = 0;
          |BUCLE CuentaUni;
          |PINTA #1#4; |PINTA #1#22;
          |GRABA 020#1a;
          |ERROR;
     |FINSI;
     |SI nTecla = 15;   ||F7
          |HAZ ConEnviada;
          |ERROR; |ACABA;
     |FINSI;
|FPRC;

|PROCESO DesAmpli; |TIPO 0;
     |SI FSalida = 16; ||F8
          |HAZ GraDesAmpli;
          |PUSHV 0501 2380;
          |ENTLINEAL #1#3, 4, 2, 22, 1, Min3, Max3;
          |POPV;
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO DefAlma; |TIPO 7;
     |SI #1#23 = 0;
          #1#23 = 1;
     |FINSI;
|FPRC;

|PROCESO FechaPrev7; |TIPO 7;
     |SI #1#21 = "01.01.0000";
          #1#21 = #0#1;
     |FINSI;
|FPRC;

|PROCESO DeciPrv;
     |ABRE #agifa324;
     #agifa324#0 = #1#11;
     |LEE 001#agifa324.=;
     |CIERRA #agifa324;
     nDeci_PreDiv = #agifa324#9;

     |ABRE #agifa321;
     |LEE 001#agifa321.p;
     #agifa324#0 = #agifa321#9; ||Igualo a moneda base
     |LEE 001#agifa324.=;
     |CIERRA #agifa321;
     nDeci_Base = #agifa324#7;  ||Decimales moneda base
     nDeci_PreBase = #agifa324#9;  ||Decimales precio de moneda base
|FPRC;

|PROCESO GrabaAlba;
     linea = linea + 1;
     #29#0 = #112#0;                  || PROVEEDOR.
     |LEE 101#29=;
     |SI FSalida ! 0;
          #29#1 = #112#1;              || NOMBRE.
          #29#2 = #13#6;             || PRECIO.
          #29#3 = #13#5;             || CANTIDAD.
          #29#4 = #14#1;             || FECHA
          #29#5 = #13#8;             || % DTO.
          #29#6 = #29#2 < #29#5;       || PRECIO NETO.
          |GRABA 020#29n;
     |SINO;
          |SI #13#8 > #29#5;
               #29#1 = #3#1;              || NOMBRE.
               #29#2 = #13#6;             || PRECIO.
               #29#3 = #13#5;             || CANTIDAD.
               #29#4 = #14#1;             || FECHA
               #29#5 = #13#8;             || % DTO.
               #29#6 = #29#2 < #29#5;       || PRECIO NETO.
               |GRABA 020#29a;
          |FINSI;
     |FINSI;
     |LIBERA #29;
|FPRC;

|PROCESO GrabaAlbaFec;
     linea = linea + 1;
     #29#0 = #112#0;                  || PROVEEDOR.
     |LEE 101#29=;
     |SI FSalida ! 0;
          #29#1 = #3#1;              || NOMBRE.
          #29#2 = #13#6;             || PRECIO.
          #29#3 = #13#5;             || CANTIDAD.
          #29#4 = #14#1;             || FECHA
          #29#5 = #13#8;             || % DTO.
          #29#6 = #29#2 < #29#5;       || PRECIO NETO.
          |GRABA 020#29n;
     |SINO;
          |SI #14#1 ] #29#4;
               #29#1 = #3#1;              || NOMBRE.
               #29#2 = #13#6;             || PRECIO.
               #29#3 = #13#5;             || CANTIDAD.
               #29#4 = #14#1;             || FECHA
               #29#5 = #13#8;             || % DTO.
               #29#6 = #29#2 < #29#5;       || PRECIO NETO.
               |GRABA 020#29a;
          |FINSI;
     |FINSI;
     |LIBERA #29;
|FPRC;

|PROCESO BuscaAlba;
     |MENSA "2403Buscando....";
     |LEE 000#13p;
     |PARA; |SI FSalida = 0; |HACIENDO;
          |SI #1#2 = #13#2;
               #14#50 = #13#27;
               #14#0 = #13#0;
               |LEE 000#14=;
               |SI FSalida = 0;
                    #112#0 = #14#3;
                    |LEE 000#112=;
                    |SI FSalida ! 0; |DDEFECTO #112; |FINSI;
                    aDivisa = #agifa077#56;
                    aMoneda = #agifa077#58;
                    |HAZ Divisas077;
                    |SI nTecla = 18;
                         |HAZ GrabaAlbaFec;
                    |SINO;
                         |HAZ GrabaAlba;
                    |FINSI;
               |FINSI;
          |FINSI;
          |LEE 000#13s;
     |SIGUE;
     |BLIN 24;
|FPRC;

|PROCESO mini;
     #29#0 = "      ";
     #29#6 = 0;
|FPRC;

|PROCESO maxi;
     #29#0 = "zzzzzz";
     #29#6 = 99999999;
|FPRC;

|PROCESO TeclaEnter; |TIPO 0;
     |SI #29Campo = Contenido; |Y FSalida = 0;
          aPro = #29#0;
          nPre = #29#2;
          nDto = #29#5;
          |ERROR;
          |PTEC 806;
     |FINSI;
|FPRC;

|PROCESO Proveedor0; |TIPO 0;
     nTecla = FSalida;
     |SI #1Campo ! Contenido; |O aArtiAnt ! #1#2;
          aArtiAnt = #1#2;
          |HAZ DeciPrv;
          |ABRE #104;
          #104#0 = #1#4;
          #104#1 = #1#2;
          |LEE 000#104=;
          |SI FSalida = 0;
               #1#6 = #104#3; |PINTA #1#6;
               #1#7 = #104#4; |PINTA #1#7;
          |FINSI;
          |CIERRA #104;
     |FINSI;
     |SI nTecla = 9; |O nTecla = 18;
          |HAZ CreaTempo; aTmp29 = Tempo;
          |NOME_DAT #29 aTmp29; |DELINDEX #29;
          |ABRE #29; |ABRE #112; |ABRE #13; |ABRE #14;
          linea = 1;
          |HAZ BuscaAlba;
          |SI linea ! 1;
               aPro = "";
               |PUSHV 0501 2380;
               ||ENTLINEAL #1#29,1,4,22,1,mini,maxi;     || por proveedor
               |ENTLINEAL #2#29,1,4,22,1,mini,maxi;     || por precio neto
               |POPV;
               |SI aPro ! "";
                    #1#4 = aPro;
                    #1#6 = nPre;             || PRECIO.
                    #1#7 = nDto;             || % DTO.
               |PINTA #1#4;
               |PINTA #1#6;
               |PINTA #1#7;
               |FINSI;
          |SINO;
               |MENAV "0000No se encontraron compras anteriores.";
          |FINSI;
          |CIERRA #29; |CIERRA #112; |CIERRA #13; |CIERRA #14;
          |DELINDEX #29;
          Tempo = aTmp29; |HAZ BoraTempo;
          |ERROR;
          |ACABA;
     |FINSI;

     |SI nTecla = 11; |O #142#102 = "S"; |O #142#102 = "X";
          enPrepe = #1#0;
          enPreli = #1#1;
          eaCodigo = #1#4;
          eaFuerza = "N";
          |SI FSalida = 11; eaFuerza = "S"; |FINSI;
          |SUB_EJECUTA_NP "go000001";
          |SI #142#102 = "N";  |ERROR;  |FINSI;
     |FINSI;
|FPRC;

|PROCESO SubProve0; |TIPO 0;
     nTecla = FSalida;
     |SI #2Campo ! Contenido; |O aArtiAnt ! #1#2;
          aArtiAnt = #1#2;
          |HAZ DeciPrv;
          |ABRE #104;
          #104#0 = #2#4;
          #104#1 = #1#2;
          |LEE 000#104=;
          |SI FSalida = 0;
               #2#6 = #104#3; |PINTA #2#6;
               #2#7 = #104#4; |PINTA #2#7;
          |FINSI;
          |CIERRA #104;
     |FINSI;
     |SI nTecla = 9; |O nTecla = 18;
          |HAZ CreaTempo; aTmp29 = Tempo;
          |NOME_DAT #29 aTmp29; |DELINDEX #29;
          |ABRE #29; |ABRE #112; |ABRE #13; |ABRE #14;
          linea = 1;
          |HAZ BuscaAlba;
          |SI linea ! 1;
               aPro = "";
               |PUSHV 0501 2380;
               ||ENTLINEAL #1#29,1,4,22,1,mini,maxi;     || por proveedor
               |ENTLINEAL #2#29,1,4,22,1,mini,maxi;     || por precio neto
               |POPV;
               |SI aPro ! "";
                    #2#4 = aPro;
                    #2#6 = nPre;             || PRECIO.
                    #2#7 = nDto;             || % DTO.
               |PINTA #2#4;
               |PINTA #2#6;
               |PINTA #2#7;
               |FINSI;
          |SINO;
               |MENAV "0000No se encontraron compras anteriores.";
          |FINSI;
          |CIERRA #29; |CIERRA #112; |CIERRA #13; |CIERRA #14;
          |DELINDEX #29; Tempo = aTmp29; |HAZ BoraTempo;
          |ERROR;
          |ACABA;
     |FINSI;

     |SI nTecla = 11; |O #142#102 = "S"; |O #142#102 = "X";
          enPrepe = #2#0;
          enPreli = #2#1;
          eaCodigo = #2#4;
          eaFuerza = "N";
          |SI FSalida = 11; eaFuerza = "S"; |FINSI;
          |SUB_EJECUTA_NP "go000001";
          |SI #142#102 = "N";  |ERROR;  |FINSI;
     |FINSI;
|FPRC;

|PROCESO BorraDA;
     |BORRA 020#3a;
|FPRC;

|DEFBUCLE BorraDA;
     #3#1;
     ;
     #2#0, #2#1, #2#2, 001;
     #2#0, #2#1, #2#2, 999;
     be;
     NULL, BorraDA;
|FIN;

|PROCESO BorraSubLin;
     |BUCLE BorraDA;
     |BORRA 020#2a;
|FPRC;

|DEFBUCLE BorraSubLin;
     #2#1;
     ;
     #1#0, #1#1, 001;
     #1#0, #1#1, 999;
     be;
     NULL, BorraSubLin;
|FIN;

|PROCESO dsm00082Nuevo;
     nLin = nLin + 1;
     #3#0 = #visem003@#0;
     #3#1 = #visem003@#1;
     #3#2 = #visem003@#2;
     #3#3 = #82#2;
     #3#4 = nLin;
     |GRABA 020#3n;
|FPRC;

|DEFBUCLE dsm00082Nuevo;
     #82#1;
     ;
     #1#2, 001;
     #1#2, 999;
     ;
     NULL, dsm00082Nuevo;
|FIN;

|PROCESO CreaNuevaLin;
     |DDEF aAlfa;
     aAlfa = aAlfa + "visem003.mas";
     |CARGA_DEF aAlfa, visem003@;
     |SI FSalida ! 0; |ACABA; |FINSI;

     |ABRE #visem003@;
     #visem003@#0 = #1#0;
     #visem003@#1 = #1#1;
     #visem003@#2 = 999;
     |LEE 000#visem003@.];
     |SI FSalida = 0;
          |LEE 000#visem003@.a;
     |SINO;
          |LEE 000#visem003@.u;
     |FINSI;
     |SI FSalida = 0; |Y #visem003@#0 = #1#0; |Y #visem003@#1 = #1#1;
          nLin = #visem003@#2 + 1;
     |SINO;
          nLin = 1;
     |FINSI;
     |DDEFECTO #visem003@;
     #visem003@#0 = #1#0;
     #visem003@#1 = #1#1;
     #visem003@#2 = nLin;
     |PARA i = 2; |SI i [ 23; |HACIENDO i = i + 1;
          nCampo = i + 1;
          #visem003@#nCampo# = #1i;
     |SIGUE;
     #visem003@#6 = nUni;
     |GRABA 020#visem003@.n;

     |ABRE #3;
     #3#0 = #visem003@#0;
     #3#1 = #visem003#1;
     #3#2 = #visem003@#2;
     #3#4 = 1;
     |LEE 000#3];
     |SI FSalida = 0; |Y #3#0 = #visem003@#0; |Y #3#1 = #visem003@#1; |Y #3#2 = #visem003@#2;
     |SINO;
          |SI #142#206 = "S";
               nLin = 0;
               |BUCLE dsm00082Nuevo;
          |FINSI;
     |FINSI;
     |CIERRA #3;
     |DESCARGA_DEF #visem003@;
|FPRC;

|DEFBUCLE CheqPedOrd;
     #2#1;
     ;
     #1#0, #1#1, 001;
     #1#0, #1#1, 999;
     ;
     NULL, CheqPedOrd;
|FIN;

|PROCESO Tipo2Lin; |TIPO 2;
     nModo  = (FEntrada / 100) ! 0;
     nForma = 0 +.1;
     |SI nModo = 3;
          nSwAct = 0;
          |BUCLE CheqPedOrd;
          |SI nSwAct ! 0;
               |SI nForma = -1; |MENAV "0000Hay lineas actualizadas..."; |FINSI;
               |ERROR; |ACABA;
          |FINSI;
     |FINSI;
     |SI nForma = 1;
          |SI nModo = 1; || Crea la 1§ linea
               nUni = #1#5;
               |HAZ CreaNuevaLin;
          |FINSI;
          |PUSHV 0501 2380;
          |ENTLINEAL #1#2, 3, 2, 22, 1, Min2, Max2;
          |POPV;
          #1#5 = 0; #1#22 = 0;
          |BUCLE CuentaUni;
          |PINTA #1#4; |PINTA #1#22;
     |FINSI;
     |SI nModo = 3;
          |BUCLE BorraSubLin;
     |FINSI;
|FPRC;

-SUBLINEAS-----------------------------------------------------------
|PROCESO ConEnviada;
     ||       Ser Expe     Num Expe             Articulo
     aAlfa = "2 == " + #0#3 + " AND 3 == " + #0#7 + " AND 20 == " + #1#2;
     aSql = "SELECT * FROM gopartia INTO sq" + Usuario + ".def WHERE ";
     aSql = aSql + aAlfa;
     |SQL aSql;
     |SI FSalida = 0;
          |DDEF aDef; |QBF aDef;
          aDef = aDef + "sq" + Usuario + ".mas";
          |CARGA_DEF aDef, referen@;
          |SI FSalida = 0;
               |ABRE #referen@;
               |CONSULTA_CLAVE #1#referen@;
               |CIERRA #referen@;
               |DELINDEX #referen@;
               |DESCARGA_DEF #referen@;
               |FBORRA aDef;
               |ERROR;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO SubLin0; |TIPO 0;
     nUniAnt = #2#6;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 1;
          |PARA i = 2; |SI i [ 23; |HACIENDO i = i + 1;
               nCampo = i + 1;
               #2#nCampo# = #1i;
          |SIGUE;
          |PINTA #2#6;
          |PINTA #2#24;
          |PINTA #2#5;
          |PINTA #2#22;
          |PINTA #2#7;
          |PINTA #2#8;
     |FINSI;
|FPRC;

|PROCESO SubLinMarca; |TIPO 11;
     |SI FSalida = 13;   ||F5
          |SI #2#26 ! 0;
               |MENAV "0000La linea ya esta pedida...";
               |ERROR; |ACABA;
          |FINSI;
          |SI #2#29 ! 0;
               |MENAV "0000La linea ya esta realizada...";
               |ERROR; |ACABA;
          |FINSI;
          |SI #2#25 = "     ";
               #2#25 = #0#3; |PINTA #2#25;
               |||MENAV "0000Debe de indicar la serie del pedido";
               |||ENCAMPO #25#2;
               |||SI #2#25 = "     ";
               ||     |ERROR; |ACABA;
               |||FINSI;
          |FINSI;
          |SI #2#11 = "P"; |O #2#11 = "O";
               #2#11 = "R";
               #2#28 = ""; |PINTA #2#28;
          |SINO;
               #2#11 = "P";
               #2#25 = ""; |PINTA #2#25;
               #2#28 = ""; |PINTA #2#28;
          |FINSI;
          |PINTA #2#11;
          |GRABA 020#2a;
          |ERROR; |ACABA;
     |FINSI;
     |SI FSalida = 14;   ||F6
          |SI #2#26 ! 0;
               |MENAV "0000La linea ya esta pedida...";
               |ERROR; |ACABA;
          |FINSI;
          |SI #2#29 ! 0;
               |MENAV "0000La linea ya esta realizada...";
               |ERROR; |ACABA;
          |FINSI;
          |SI #2#28 = "     ";
               aAlfa = ("00000" + #2#24) % -105;
               #2#28 = aAlfa; |PINTA #2#28;
               ||MENAV "0000Debe de indicar la serie de la orden";
               ||ENCAMPO #28#2;
               ||SI #2#28 = "     ";
               ||     |ERROR; |ACABA;
               ||FINSI;
          |FINSI;
          |SI #2#11 = "P"; |O #2#11 = "R";
               #2#11 = "O";
               #2#25 = ""; |PINTA #2#25;
          |SINO;
               #2#11 = "P";
               #2#11 = "P";
               #2#25 = ""; |PINTA #2#25;
               #2#28 = ""; |PINTA #2#28;
          |FINSI;
          |PINTA #2#11;
          |GRABA 020#2a;
          |ERROR; |ACABA;
     |FINSI;
     |SI FSalida = 15;   ||F7
          |HAZ ConEnviada;
          |ERROR; |ACABA;
     |FINSI;
     |SI FSalida = 16; ||F8
          |HAZ DesAmpli;
     |FINSI;
|FPRC;

|PROCESO SubTipo2; |TIPO 2;
     #2#3 = #1#2;
     #2#4 = #1#3;
     nModo = (FEntrada / 100) ! 0;
     nForma = 0 +. 1;
     |SI nModo ! 1;
          |SI #2#26 ! 0; |O #2#29 ! 0;
               |SI #2#26 ! 0; |Y nForma = -1;
                    |MENAV "0000Pedido realizado...";
               |FINSI;
               |SI #2#29 ! 0; |Y nForma = -1;
                    |MENAV "0000Orden realizada...";
               |FINSI;
               |ERROR; |ACABA;
          |FINSI;
     |FINSI;

     |SI nUniAnt > #2#6;
          |CONFI "0000SSe ha cambiado las unidades, crear linea con la diferencia?";
          |SI FSalida = 0;
               |SALVA_DATOS #2;
               nUni = nUniAnt - #2#6;
               |HAZ CreaNuevaLin;
               |REPON_DATOS #2;
          |FINSI;
     |FINSI;
     |SI nModo = 1;
          |HAZ GraDesAmpli;
     |FINSI;
     |SI nModo = 3;
          |BUCLE BorraDA;
     |FINSI;
|FPRC;
