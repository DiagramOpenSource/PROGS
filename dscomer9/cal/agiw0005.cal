|FICHEROS;
     agifv001 #0; || Pantalla Seleccion.
     agifa077 #3; || Cabecera Albaranes Compra.
     agifa080 #4; || Lineas      ''       ''
     agifa007 #6; || Series.
     agifa112 #7; || Proveedores.
     agifa017 #8; || Almacenes.
     agifa019 #9; || Articulos.
     agifa142 #10;|| Parametros Generales.
     agifa065 #11;|| Historico.
     agifv017 #33;|| Articulo Familia Fantasma para cargar la familia si no existe.
     agifa060 #34;|| Familias.
|FIN;

|CAMPOS;
     #9#15  art_pser; #8#42  alm_pser;
     #9#16  art_prec; #8#43  alm_prec;
     #9#17  art_pval; #8#4   alm_pval;
     #9#11  art_nece; #8#50  alm_nece;
     #9#12  art_rese; #8#8   alm_rese;
     #9#14  art_depo; #8#10  alm_depo;
     #9#13  art_fabr; #8#9   alm_fabr;
     #9#10  art_vsto; #8#3   alm_vsto;
     #9#7   art_mini; #8#6   alm_mini;
     #9#8   art_maxi; #8#7   alm_maxi;
     #9#6   art_tota; #8#5   alm_tota;
     #9#104 art_disp; #8#39  alm_disp;
     #9#9   art_pcm;
     #9#185 art_tota2; #8#47  alm_tota2;
     #9#193 art_disp2;  #8#48 alm_disp2;
|FIN;

|INCLUYE i_varart;

|VARIABLES;
     &err      = 0;
     aProv     = "";
     param     = "";
     serie     = "";
     numero    = "";
     emp       = "";
     dir       = "";
     su_alba   = "";
     neto      = 0;
     fmes      = "";
     mes       = 0;
     mes1      = 0;
     asto      = 0;
|FIN;

|PROCESO CambiaDir;
     |DFICB dir; |QBF dir;
     dir = dir + emp + "/";
     |PATH_DAT #3 dir;
     |PATH_DAT #4 dir;
     |PATH_DAT #6 dir;
     |PATH_DAT #7 dir;
     |PATH_DAT #8 dir;
     |PATH_DAT #9 dir;
     |PATH_DAT #10 dir;
     |PATH_DAT #11 dir;
     |PATH_DAT #34 dir;
|FPRC;

|PROCESO Actualiza;
     |ABRE #9; |ABRE #8;
     #9#0 = #4#2;
     |LEE 000#9=;
     |SI FSalida ! 0; |GRABA 021#9n; |FINSI;
     #8#0 = #4#2;
     #8#1 = #4#3;
     |LEE 101#8=;
     |SI FSalida ! 0; |GRABA 021#8b; |FINSI;
     |SI #4#19 > AS;
          art_prec = art_prec + #4#5;
     |FINSI;
     fmes = #3#1 % A402;
     mes = fmes - 1;
     mes = mes * 5;
     mes = mes + 32;
     neto = #4#9 < #3#5;
     neto = neto < #3#32;
     neto = neto < #3#7;
     |SI #3#42 = AN;                || caso albaran
          #9mes = #9mes - #4#5;     || unidades compradas
          mes1 = mes + 1;
          #9mes1 = #9mes1 - neto;
          #9#92 = #9#92 - #4#5;     || total unidades
          #9#93 = #9#93 - neto;
          mes = fmes - 1;
          mes = mes * 2;
          mes = mes + 12;
          #8mes = #8mes - #4#5;
          #8#36 = #8#36 - #4#5;
          art_disp = art_disp - #4#5;
          art_tota = art_tota - #4#5;
          alm_disp = alm_disp - #4#5;
          alm_tota = alm_tota - #4#5;
     |SINO;
          #9mes = #9mes + #4#5;
          mes1 = mes + 1;
          #9mes1 = #9mes1 + neto;
          #9#92 = #9#92 + #4#5;    || total unidades
          #9#93 = #9#93 + neto;
          mes = fmes - 1;
          mes = mes * 2;
          mes = mes + 12;
          #8mes = #8mes + #4#5;
          #8#36 = #8#36 + #4#5;
          art_disp = art_disp + #4#5;
          art_tota = art_tota + #4#5;
          alm_disp = alm_disp + #4#5;
          alm_tota = alm_tota + #4#5;
     |FINSI;
     art_nece = art_pser + art_mini - art_prec - art_disp;
     art_nece = art_nece - art_pval - art_fabr - art_rese;
     |SI art_nece < 0; art_nece = 0; |FINSI;
     alm_nece = alm_pser + alm_mini - alm_prec - alm_disp;
     alm_nece = alm_nece - alm_pval - alm_fabr - alm_rese;
     |SI alm_nece < 0; alm_nece = 0; |FINSI;
     |SI #9#196 = 1;
          asto = art_tota - art_pval;
          |SI asto > 0;
               art_vsto = asto * art_pcm;
          |SINO;
               art_vsto = 0;
          |FINSI;
          asto = alm_tota - alm_pval;
          |SI asto > 0;
               alm_vsto = asto * art_pcm;
          |SINO;
               alm_vsto = 0;
          |FINSI;
     |SINO;
          asto = art_tota2;
          |SI asto > 0;
               art_vsto = asto * art_pcm;
          |SINO;
               art_vsto = 0;
          |FINSI;
          asto = alm_tota2;
          |SI asto > 0;
               alm_vsto = asto * art_pcm;
          |SINO;
               alm_vsto = 0;
          |FINSI;
     |FINSI;
     |GRABA 021#8a;
     |GRABA 021#9a;
     |CIERRA #9; |CIERRA #8;

     |SI #3#42 = AN;                || caso albaran
          enUniCom = -#4#5;
          enImpCom = -neto;
          enUniEnt = -#4#5;
     |SINO;
          enUniCom = #4#5;
          enImpCom = neto;
          enUniEnt = #4#5;
     |FINSI;
     eaArticulo = #4#2;
     enAlmacen = #4#3;
     efFecha = #3#1;
     |HAZ AcumulaArt;
     |HAZ AcumulaAlm;

     |ABRE #7;
     #7#0 = #4#15;
     |LEE 101#7=;
     |SI FSalida = 0;
          fmes = #3#1 % A402;
          mes1 = fmes - 1;
          mes1 = mes1 + 27;
          |SI #3#42 = "N";
               #7mes1 = #7mes1 - neto;
               #7#39 = #7#39 - neto;
          |SINO;
               #7mes1 = #7mes1 + neto;
               #7#39 = #7#39 + neto;
          |FINSI;
          |GRABA 021#7a;
          |LIBERA #7;
     |FINSI;
     |CIERRA #7;

     |SI #3#42 = "N";
          enImpPrvCom = -neto;
     |SINO;
          enImpPrvCom = neto;
     |FINSI;
     efFecha = #3#1;
     eaProve = #3#3;
     |HAZ AcumulaPrv;
|FPRC;

|PROCESO BorraLineas;
     |HAZ Actualiza;
     |BORRA 020#4a;
|FPRC;

|PROGRAMA;
     |MENSA "2403Borrando Albaran Compras...";
     param = PARAMETRO + "      ";
     serie = param % 102;
     numero = param % 306;

     emp = "";
     |ABRE #0;
     |LEE 000#0p;
     |PARA; |SI FSalida = 0; |HACIENDO;
          |SI #0#0 = serie;
               emp = ("00000" + #0#1) % -105;
               aProv = #0#27;
               |SAL;
          |FINSI;
          |LEE 000#0s;
     |SIGUE;
     |CIERRA #0;
     |SI emp = "";
          err = 1; |ACABA;
          |MENAV "0000No encuetro la serie en el TRASPASO DE ALABARANES...";
     |FINSI;

     |HAZ CambiaDir;
     |DFICE emp; |QBF emp;
     emp = emp % -205;
     su_alba = emp + serie + numero;

     |ABRE #3;
     |SELECT #4#3;
     #3#3 = aProv;
     #3#54# = su_alba;
     |LEE 101#3=;
     |SI FSalida = 0;
          |BUCLELIN 0 BorraLineas #3;
          |BORRA 021#3a;
     |SINO;
          |MENAV "0000No encuentro Albaran de Compras...";
          err = 2;
     |FINSI;
     |CIERRA #3;
     |HAZ CambiaDir;
     |BLIN 24;
|FPRO;
