|FICHEROS;
     ibery016 #0;
     agifa007 #7;
     agifa019 #19;
     agifa060 #60;
     agifa077 #77;
     agifa080 #80;
     agifa120 #120;
     dsm00241 #241;
     iberw019 #919;
|FIN;

|VARIABLES;
     nFactor     = 0;
     nMes        = 0;
     nCampo      = 0;
     nSwHay      = 0;

     aAlfa       = "";
     aFichero    = "";
|FIN;

|PROCESO FactorUni;
     nFactor = 1;

     |ABRE #241;
     |SELECT #2#241;
     #241#0 = #19#5;
     #241#2 = #80#43;
     |LEE 000#241=;
     |SI FSalida = 0;
          |SI #241#6 = "D";
               nFactor = 1 * #241#5;
          |SINO;
               nFactor = 1 / #241#5;
          |FINSI;
     |FINSI;
     |CIERRA #241;
|FPRC;

|PROCESO LeeLineas;
     #19#0 = #80#2;
     |LEE 040#19=;
     |SI FSalida ! 0;  |DDEFECTO #19;  |FINSI;


     |SI #19#2 < #0#6;  |O #19#2 > #0#7;  |ACABA;  |FINSI;
     |SI #19#3 < #0#8;  |O #19#3 > #0#9;  |ACABA;  |FINSI;

     #7#26 = #77#50;
     |LEE 040#7=;
     |SI FSalida ! 0;  |DDEFECTO #7;   |FINSI;

     #60#0 = #19#2;
     |LEE 040#60=;
     |SI FSalida ! 0;  |DDEFECTO #60;   |FINSI;

     #120#0 = #19#3;
     |LEE 040#120=;
     |SI FSalida ! 0;  |DDEFECTO #120;   |FINSI;

     #919#0  = #77#50;
     #919#1  = "";
     |SI #0#12 = "I";  #919#1  = #77#3;  |FINSI;
     #919#2  = #19#2;
     #919#3  = #19#3;
     #919#4  = #80#2;
     |LEE 101#919=;
     |SI FSalida ! 0;
         |DDEFECTO #919;
         #919#0  = #77#50;
         #919#1  = "";
         |SI #0#12 = "I";  #919#1  = #77#3;  |FINSI;
         #919#2  = #19#2;
         #919#3  = #19#3;
         #919#4  = #80#2;
         #919#5  = #7#27;
         #919#6  = #77#4;
         #919#7  = #60#1;
         #919#8  = #120#1;
         #919#9  = #19#1;
         #919#10 = #19#5;
         |GRABA 020#919b;
     |FINSI;

     |HAZ FactorUni;

     #919#11 = #919#11 + (#80#5 * nFactor);
     #919#13 = #919#13 + #80#31;
     #919#12 = #919#13 / #919#11;

     aAlfa  = #77#1 % 402;
     nMes   = aAlfa;
     nCampo = 13 + nMes;
     #919nCampo = #919nCampo + (#80#5 * nFactor);
     #919#26    = #919#26    + (#80#5 * nFactor);

     |GRABA 020#919a;
     |LIBERA #919;
|FPRC;

|DEFBUCLE agifa080;
     #80#4;
     ;
     #77#50, #77#0, #0#10;
     #77#50, #77#0, #0#11;
     ;
     NULL, LeeLineas;
|FIN;

|PROCESO LeeCabecera;
     |BUCLE agifa080;
|FPRC;

|DEFBUCLE agifa077;
     #77#3;
     ;
     #0#2, #0#0, #0#4, 000000;
     #0#3, #0#1, #0#5, 999999;
     ;
     NULL, LeeCabecera;
|FIN;

|PROCESO Imprime;
     |SI #919#11 = 0;  |Y #919#12 = 0;  |Y #919#13 = 0;
         |ACABA;
     |FINSI;

     |PRINT;
     nSwHay = 1;
|FPRC;

|DEFBUCLE iberw019;
     #919#1;
     ;
     "     ", "      ", "   ", "     ", "                    ";
     "zzzzz", "zzzzzz", "zzz", "zzzzz", "zzzzzzzzzzzzzzzzzzzz";
     ;
     NULL, Imprime;
|FIN;

|PROCESO Tipo3;  |TIPO 3;
     |IMPRE 0;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |SI #0#12 = "I";
         |INFOR "ibery116";
     |SINO;
         |INFOR "ibery216";
     |FINSI;
     |SI FSalida ! 0;  |FINIMP;  |ACABA;  |FINSI;

     aFichero = ("w19" + Usuario + "zzzzzzzz") % 108;
     |NOME_DAT #919 aFichero;
     |ABRE #919;  |CIERRA #919;  |DELINDEX #919;

     |ABRE #7;
     |ABRE #19;
     |ABRE #60;
     |ABRE #120;
     |ABRE #919;
     |BUCLE agifa077;
     |CIERRA #7;
     |CIERRA #19;
     |CIERRA #60;
     |CIERRA #120;
     |CIERRA #919;

     nSwHay = 0;
     |BUCLE iberw019;

     |SI nSwHay ! 0;
         |PIEINF;
     |SINO;
         |MENAV "    Seleccion Vacia";
     |FINSI;

     |FININF;
     |FINIMP;

     |ABRE #919;  |CIERRA #919;  |DELINDEX #919;
|FPRC;
