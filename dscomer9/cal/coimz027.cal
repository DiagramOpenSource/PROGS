|FICHEROS;
     tmp00000 #0;
     dsm00003 #3;
     coima035 #22;
     coima023 #23;
     agifa024 #24;
     agifa025 #25;
     agifa019 #19;
     agifa040 #40;
     dsm00046 #46;
     agifa049 #49;
     coima053 #53;
     agifa073 #73;
     agifa104 #104;
     coima049 #124;
     agifa142 #142;
     dsm00053 #153;
     agifa321 #321;
     agifa324 #324;
     agifa048 #480;
     dsm00113;

     refere1@;
     refere2@;
     refere3@;
|FIN;

|INCLUYE i_varart;

|VARIABLES;
     aPob = "";
     aFormato2 = "N";
     nSwChampiEmar = 0;
     aDef = "";
     nDeci = 0;
     aArti = "";
     aCli = "";
     nPedido = 0;
     aTmp0 = "";
     &Tempo = "";
     &enModoCoimasa = 0;
     &enFormaCoimasa = 0;
     &eaImpresora = "";
     {1}aBaseLocal = "";
     &num_ped = 0;
     &ser_ped = "";
     aIP = "";
     aServidor = "";
     aFrom = "";
     aTo = "";
     aSubject = "";
     aMensaje = "";
     aDjunto = "";
     aPath = "";
     aAlfa = "";
     &aMoneda = "";
     &aDivisa = "";
     &fed_dt = @;   || Fecha....
     &art_dt = "";  || Articulo.
     &cli_dt = "";  || Cliente..
     &fam_dt = "";  || Familia.
     &iva_dt = 0;   || Tipo Iva.
     &dtos_2 = 0;   || Descuento 2.
     &dto_pt = 0;   || Descuento Ptas.
     &dtos_1 = 0;   || Descuento 1.
     &pvp_ve = 0;   || Precio Venta.
     &tarifa = 0;   || Tarifa Cliente.
     &quedto = 0;   || 1=cli./art, 2=cli./familia, 3=cli./subfam.
     &gru_dt = "";  || Grupo Cliente.
     &uni_dt = 0;
     &enMensaje = 0;
     &aParam    = "";
     &nPromo    = 0;

     &enSwMenu = 0;
     &enDescuento1 = 0;
     &enDescuento2 = 0;
     &eaTComision  = "";
     &enComision   = 0;

     nLin = 0;
     nLinKit = 0;
     nImpo = 0;
     nForma = 1;
     fmes = "";
     mes = 0;
     &eaTag = "";
     nCalc = 0;
     aEmail = "";
     aMailRep1 = "";
     aMailRep2 = "";
|FIN;

|PROCESO ChampiEmarEnvase;
     #refere3@#0 = #124#19;
     |LEE 000#refere3@.=;
     |SI FSalida ! 0; |DDEFECTO #refere3@; |FINSI;

     |DDEFECTO #refere1@;
     #refere1@#0 = #73#28;
     #refere1@#1 = #73#0;
     #refere1@#2 = #73#1;
     |LEE 101#refere1@.=;
     |SI FSalida ! 0; |GRABA 020#refere1@.b; |FINSI;

     #refere1@#3 = #124#20;                  ||bultos
     #refere1@#4 = #124#21;                  ||bruto
     #refere1@#5 = #124#20 * #refere3@#2;    ||tara
     #refere1@#6 = #124#19;                  ||envase
     |GRABA 020#refere1@.a;
     |LIBERA #refere1@;

     #73#5 = (#refere1@#4 - #refere1@#5) ! nDeci;
|FPRC;

|PROCESO RecalPed;
     #104#25 = #0#0;
     #104#0 = #0#1;
     |LEE 121#104=;
     |SI FSalida = 0;
          |HAZ CalCabAgifa104;
          |LIBERA #104;
     |FINSI;
|FPRC;

|DEFBUCLE RecalPed;
     #0#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, RecalPed;
|FIN;

|PROCESO CalcConIva;
     |ABRE #dsm00113;
     #dsm00113#0 = #agifa104#4;
     #dsm00113#1 = #agifa073#14;
     |LEE 000#dsm00113.=;
     |SI FSalida = 0;
        nCalc = #agifa073#11 % #dsm00113#3;
        |SI #agifa024#47 = "S";
           nCalc = nCalc + (#agifa073#11 % #dsm00113#4);
        |FINSI;
     |SINO;

        enTiva = #agifa073#14;
        efFiva = #agifa104#1;
        |HAZ SacaIVA;

         nCalc = #agifa073#11 % enIva;
        |SI #agifa024#47 = "S";
              nCalc = nCalc + (#agifa073#11 % enRec);
        |FINSI;
     |FINSI;
     |CIERRA #dsm00113;

     nImpo = nImpo + (#agifa073#11 + nCalc);
|FPRC;

|DEFBUCLE CalcConIva;
#agifa073#1;
;
#agifa104#25, #agifa104#0,001;
#agifa104#25, #agifa104#0,999;
;
NULL,CalcConIva;
|FIN;

|PROCESO ActCli;
     #19#0 = #73#2;
     |LEE 020#19=;

     #24#0 = #104#4;
     |LEE 121#24=;
     |SI FSalida = 0;
          |SI #19#194 = 2;
               nImpo = ((((#73#44 * #73#6) < #73#8) < #73#29) < #104#5) < #104#17;
          |SINO;
               nImpo = ((((#73#5 * #73#6) < #73#8) < #73#29) < #104#5) < #104#17;
          |FINSI;
          |SI #142#115 = "S";
               nImpo = nImpo < #104#6;
          |FINSI;
          nImpo = nImpo * nForma;
          fmes = #104#1 % A402;
          mes = fmes - 1;
          mes = mes * 3;
          mes = mes + 114;
          #24mes = #24mes   + nImpo;     || VENTA.
          #24#150 = #24#150 + nImpo;
          |GRABA 020#24a;

          eaCliente = #agifa104#4;
          efFecha = #agifa104#1;
          enImpCliPed = nImpo;
          |HAZ AcumulaCli;
     |FINSI;
|FPRC;

|PROCESO Final;
     |SI aCli = ""; |ACABA; |FINSI;

     |HAZ CalCabAgifa104;

     |ABRE #24;
     |ABRE #19;
     |BUCLELIN 2 ActCli #104;
     |CIERRA #24;
     |CIERRA #19;

     nImpo = 0; |BUCLE CalcConIva;
     eaTag = "PE";  |HAZ Riesgos;

     |SI #22#16 = "N"; |ACABA; |FINSI;

     aIP = ""; |IP_REMOTO aIP;
     |SI aIP = "";
          |DFICE aAlfa;
          aDjunto = aAlfa + #104#25 + "_" + (("000000" + #104#0)%-106)+ ".pdf";
          |QBT aDjunto;
          eaImpresora = "CrystalReport;" + aDjunto;
          ser_ped = #104#25;
          num_ped = #104#0;
          |SUB_EJECUTA_NP "agifa105";
     |SINO;
          |DFICE aAlfa;
          aDjunto = aAlfa + #104#25 + "_" + (("000000" + #104#0)%-106)+ ".pdf";
          |QBT aDjunto;
          |ESPECIFICA "RBASS", aBaseLocal;
          aAlfa = aBaseLocal1 + #104#25 + "_" + (("000000" + #104#0)%-106)+ ".pdf";
          |QBT aAlfa;
          eaImpresora = "CrystalReport;" + aAlfa;
          ser_ped = #104#25;
          num_ped = #104#0;
          |SUB_EJECUTA_NP "agifa105";
          |RECIBE_FICHERO aAlfa, aDjunto;
          |RFBORRA aAlfa;
     |FINSI;

     aIP = "";
     |SI #22#8 = "L";
          |IP_REMOTO aIP;
     |SINO;
          |SI #22#8 = "S";
               |IP_LOCAL aIP;
          |SINO;
               aIP = #22#7; |QBF aIP;
          |FINSI;
     |FINSI;
     aServidor = #22#14; |QBF aServidor;
     aFrom = #22#15; |QBF aFrom;
     aTo = aEmail;
     aSubject = "Confirmación pedido web.";
     aMensaje = "Adjunto pedido:" + #104#25 + "/" + (("000000" + #104#0)%-106);

     aMensaje = "Le adjuntamos PDF con la confirmacion de su pedido realizado a traves de nuestra web.";
     aMensaje = aMensaje + &13 + "Reciba un cordial saludo.";
     aMensaje = aMensaje + &13 + #22#18; || "Coimasa.";
     |QBF aMensaje;

     |SI aTo ! "";
          |DSCORREO_ENVIA aIP, aServidor, aFrom, aTo, aSubject, aMensaje, aDjunto;
     |FINSI;

     aTo = aMailRep1; |QBF aTo;
     |SI aTo ! "";
          |DSCORREO_ENVIA aIP, aServidor, aFrom, aTo, aSubject, aMensaje, aDjunto;
     |FINSI;
     aTo = aMailRep2; |QBF aTo;
     |SI aTo ! "";
          |DSCORREO_ENVIA aIP, aServidor, aFrom, aTo, aSubject, aMensaje, aDjunto;
     |FINSI;

     aTo = #22#19; |QBF aTo;
     |SI aTo ! "";
          aSubject = "Copia de pedido web.";
          aMensaje = "Adjunto pedido:" + #104#25 + "/" + (("000000" + #104#0)%-106);

          aMensaje = "Adjuntamos PDF con la copia del pedido realizado a traves de la web.";
          aMensaje = aMensaje + &13 + "Reciba un cordial saludo.";
          aMensaje = aMensaje + &13 + #22#18; || "Coimasa.";

          |DSCORREO_ENVIA aIP, aServidor, aFrom, aTo, aSubject, aMensaje, aDjunto;
     |FINSI;
|FPRC;

|PROCESO Comi;
     enDescuento1 = #73#8;
     enDescuento2 = #73#29;
     eaTComision  = #73#17;
     eaCliente    = #104#4;
     eaRepre      = #104#7;
     eaArticulo   = #73#2;
     efFecha      = #104#1;
     eaSerie      = #73#28;
     enDirEnt = #104#57;
     enAlmacen = #73#3;
     |HAZ Comisiones;
|FPRC;

|PROCESO Precio;
     fed_dt = #104#1;   || Fecha....
     art_dt = #73#2;   || Articulo.
     cli_dt = #104#4;  || Cliente..
     fam_dt = #73#18;  || Familia.
     iva_dt = #19#30;  || Tipo Iva.
     dtos_2 = 0;      || Descuento 2.
     dto_pt = 0;      || Descuento Ptas.
     dtos_1 = 0;      || Descuento 1.
     pvp_ve = 0;      || Precio Venta.
     tarifa = #24#39;  || Tarifa Cliente....
     gru_dt = #24#158; || Grupo Cliente.
     |SI #73#36 ! 0;
           uni_dt = #73#36;
     |SINO;
           uni_dt = #73#5;
     |FINSI;
     nPromo = #104#56;
     enMensaje = 0;
     aParam = "";
     eaSerie = #73#28;
     enDirEnt = #104#57;
     |SI #19#176 = "S";  enMensaje = 1;  |FINSI;
     enAlmacen = #73#3;
     |HAZ calcdtos;
|FPRC;

|PROCESO Cabecera;
     |DDEFECTO #3;
     |ABRE #3;
     #3#0 = #124#2;
     #3#1 = #124#3;
     |LEE 000#3=;
     |CIERRA #3;

     |LIBERA #104;
     |DDEFECTO #104;
     #104#25 = #22#10;
     enSwMenu = 1;
     |HAZ ContaPV7;
     #104#1 = ~;    || #23#2;
     #104#2 = #124#16;
     #104#3 = #124#15;
     #104#4 = #24#0;
     #104#5 = #24#37;
     #104#6 = #24#38;
     #104#7 = #25#0;
     #104#8 = #24#2;
     #104#9 = #24#20;

     #104#57 = #3#1;
     #104#19 = #3#10;
     #104#14 = #3#2;
     #104#20 = #3#3;
     #104#21 = #3#7;
     #104#59 = #3#6;

     #104#16 = #25#7;
     #104#17 = #24#156;
     #104#18 = #24#4;
     #104#22 = #24#47;
     #104#23 = #24#41;
     #104#26 = "WEB";
     #104#35 = #24#192;
     |ABRE #324;
     #324#0 = #104#35;
     |LEE 000#324=;
     #104#36 = #324#2;
     #104#37 = #24#193;
     #104#40 = #321#9;
     #324#0 = #104#40;
     |LEE 000#324=;
     #104#41 = #324#2;
     #104#60 = #24#202;
     #104#64 = #24#15;
     |CIERRA #324;
     #104#52 = #24#167;

     #104#53 = #124#12;
     #104#54 = #124#13;
     #104#55 = #124#14;

     |SI aFormato2 = "S";
          #104#5 = #124#22;    ||Dto Espe
        ||         #124#24;    ||Varios
          #104#19 = #124#25;   ||Entregar
          #104#14 = #124#26;   ||Dir
          #104#20 = #124#27;   ||Pob
          #104#59 = #124#28;   ||CP
          #104#64 = #124#29;   ||NIF

          |ABRE #40;
          aPob = #104#59 % 102;
          #40#0 = aPob;
          |LEE 000#40=;
          |SI FSalida = 0;
               #104#21 =  #40#1;
          |FINSI;
          |CIERRA #40;
     |FINSI;


     |GRABA 020#104b;

     #0#0 = #104#25;
     #0#1 = #104#0;
     |GRABA 000#0n;

     |SI nSwChampiEmar = 0;
          |DDEFECTO #refere2@;
          #refere2@#0 = #104#25;
          #refere2@#1 = #104#0;
          |LEE 101#refere2@.=;
          |SI FSalida ! 0; |GRABA 020#refere2@.b; |FINSI;
          #refere2@#2 = #124#18;
          |GRABA 020#refere2@.a;
          |LIBERA #refere2@;
     |FINSI;

     aDivisa = #104#35;
     aMoneda = #104#37;
     |HAZ Divisas104;

     aAlfa = #104#53 + #104#54 + #104#55; |QBT aAlfa;
     |SI aAlfa ! "";
          |HAZ EnviaMail;
     |FINSI;
|FPRC;

|PROCESO Kit;
     nLinKit = nLinKit + 1;
     |DDEFECTO #46;
     #46#0 = #73#28;
     #46#1 = #73#0;
     #46#2 = #73#1;
     #46#3 = #49#2;
     #46#4 = #49#9;
     #46#5 = #49#4;
     #46#6 = nLinKit;
     #46#8 = #19#9;
     |GRABA 020#46n;
|FPRC;

|PROCESO precio_divisa;
     #73#38 = #73#6 / #104#41 * #104#36;
     |SI #104#37 = "D";
          #73#6 = #73#38 / #104#36 * #104#41;
     |SINO;
          #73#38 = #73#6 / #104#41 * #104#36
     |FINSI;
     |SI #104#37 = "D";
          #73#40 = #73#6;
     |SINO;
          #73#40 = #73#38;
     |FINSI;
|FPRC;

|PROCESO Lineas;
     #53#2 = #124#5;
     |LEE 000#53=;
     |SI FSalida = 0;
          aArti = #53#0;
     |SINO;
          aArti = #124#5;
     |FINSI;


     |DDEFECTO #19;
     |ABRE #19;
     #19#0 = aArti;
     |LEE 000#19=;
     |CIERRA #19;

     nLin = nLin + 1;
     |DDEFECTO #73;
     #73#28 = #104#25;
     #73#0 = #104#0;
     #73#1 = nLin;
     #73#2 = #19#0;
     #73#3 = #22#11;
     #73#4 = #19#1;

     |SI nSwChampiEmar = 0;
          |HAZ ChampiEmarEnvase;
     |SINO;
          #73#5 = #124#6
     |FINSI;

     |HAZ Precio;
     #73#6 = pvp_ve - dto_pt;       || Precio
     |SI aFormato2 = "S";
          #73#8 = #124#23;
          #73#29 = 0;
     |SINO;
          #73#8 = dtos_1;
          #73#29 = dtos_2;
     |FINSI;
     |HAZ precio_divisa;
     |HAZ Comi;
     #73#10 = enComision;      || Comi
     #73#13 = #104#2;
     |SI #24#28 = "S";   || Exportacion
          #73#14 = 0;
     |SINO;
          #73#14 = #19#30;
     |FINSI;
     #73#16 = #104#7;
     #73#17 = #25#7;
     #73#18 = #19#2;
     #73#19 = #24#4;
     #73#20 = #104#4;
     #73#22 = #22#9;
     #73#23 = #104#5;
     #73#24 = #104#6;
     #73#25 = #104#17;
     #73#26 = #104#9;
     #73#42 = #73#5;
     #73#45 = #124#17;
     |GRABA 020#73n;

     #480#0 = #73#2;
     |LEE 000#480=;
     |SI FSalida = 0; |Y #480#33 = "K";      || solo para kits
          |ABRE #46;
          nLinKit = 0;
          |BUCLELIN 2 Kit #480;
          |CIERRA #46;
     |FINSI;

     enForma = 1;
     |HAZ AcumulaPV;

     |SI nSwChampiEmar = 0;
          aAlfa = "-LOTE: " + #124#32;
          |ABRE #153;
          #153#0 = "P";
          #153#1 = #73#28;
          #153#2 = #73#0;
          #153#3 = #73#1;
          #153#4 = 1;
          #153#5 = aAlfa;
          |GRABA 020#153n;
          |CIERRA #153;
     |FINSI;
|FPRC;

|PROCESO coima049;
     #23#0 = #124#0;
     |LEE 000#23=;

     |ABRE #24;
     #24#0 = #124#2;
     |LEE 000#24=;
     |SI FSalida ! 0; |DDEFECTO #24; |FINSI;
     |CIERRA #24;

     |DDEFECTO #25;
     |ABRE #25;
     #25#0 = #124#4;
     |LEE 000#25=;
     |SI #25#57 = "S";
          |DDEFECTO #25;
          #25#0 = #24#25;
          |LEE 000#25=;
     |FINSI;
     |CIERRA #25;


     |SI nPedido ! #124#0;

          |HAZ Final;

          aMailRep1 = #25#62;
          aMailRep2 = #25#64;

          |ABRE #24;
          #24#0 = #124#2;
          |LEE 000#24=;
          |SI FSalida ! 0; |DDEFECTO #24; |FINSI;
          |CIERRA #24;
          |HAZ Cabecera;

          nPedido = #124#0;
          aEmail = #124#11; |QBF aEmail
          aCli = #124#2;
          nLin = 0;
     |FINSI;

     |HAZ Lineas;

     #124#9 = "S";
     #124#33 = "P";
     #124#34 = #104#25;
     #124#35 = #104#0;
     |GRABA 020#124a;
     |LIBERA #124;
|FPRC;

|DEFBUCLE coima049;
     #124#2;
     ;
     00000001, 000001, "N";
     99999999, 999999, "N";
     be;
     NULL, coima049;
|FIN;

|PROGRAMA;
     |SI PARAMETRO = "formato2";
          aFormato2 = "S";
     |FINSI;

     |HAZ EsChampiEmar; nSwChampiEmar = FSalida;

     |SI nSwChampiEmar = 0;
          |DDEF aDef;
          aAlfa = aDef + "champi10"; |CARGA_DEF aAlfa, refere1@; |ABRE #refere1@;
          aAlfa = aDef + "champi14"; |CARGA_DEF aAlfa, refere2@; |ABRE #refere2@;
          aAlfa = aDef + "champi06"; |CARGA_DEF aAlfa, refere3@; |ABRE #refere3@;
     |FINSI;


     |ABRE #142; |LEE 000#142p; |CIERRA #142;
     |ABRE #321; |LEE 000#321p; |CIERRA #321;
     |ABRE #22; |LEE 000#22p; |CIERRA #22;

     nDeci = #142#8;

     |HAZ CreaTempo; |NOME_DAT #0 Tempo; aTmp0 = Tempo; |DELINDEX #0;
     |ABRE #0;

     |ABRET #104;
     |ABRET #73;
     |ABRE #23;
     |ABRE #480;
     |ABRE #46;
     |ABRE #53; |SELECT #2#53;
     |BUCLE coima049;
     |HAZ Final;
     |CIERRA #53;
     |CIERRA #46;
     |CIERRA #480;
     |CIERRA #23;
     |CIERRAT #73;
     |CIERRAT #104;

     |ABRE #104;         || No deberia hacer falta, pero dicen
     |BUCLE RecalPed;    || que algunos pedidos no estan calculados !!!
     |CIERRA #104;

     |CIERRA #0; |DELINDEX #0;
     Tempo = aTmp0; |HAZ BoraTempo;

     |SI nSwChampiEmar = 0;
          |CIERRA #refere3@; |DESCARGA_DEF #refere3@;
          |CIERRA #refere2@; |DESCARGA_DEF #refere2@;
          |CIERRA #refere1@; |DESCARGA_DEF #refere1@;
     |FINSI;
|FPRO;
