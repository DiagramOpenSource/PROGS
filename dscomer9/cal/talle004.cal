|FICHEROS;
     talle004 #6; || Observa
|FIN;
------------------------------------------------------------------------------
|VARIABLES;
     nHay = 0;
     aAlfa1 = "";
     aAlfa2 = "";
     nModo = 0;
     aDirectorio    = "";
     aNFichero      = "";
     aVariableCarga = "";
     aFichero       = "";
     aAlfa          = "";

     nVariable      = 0;
     nBaffer        = 0;
     nTotalLinea    = 0;
     nTope          = 0;
     nVari1         = 0;
     nVari2         = 0;

     cDesc         = "";
     eaTipoCodigo  = "";
     eaNombreList  = "";

     enCuadroIni   = 0;
     enCuadroFin   = 0;
     enCampoAncho  = 0;
     enModoEdita   = 0;
     enTotalVaria  = 0;

{100}epPuntEdita   = "";

     cMensa         = "";
     cNumero        = "";

     &eaArticulo = "";
|FIN;
------------------------------------------------------------------------------
|PROCESO GrabaSecuencial;
     IepPuntEdita  = epPuntEdita1 <-;
     |PARA nTotalLinea = 0;  |SI nTotalLinea [ enTotalVaria;  |HACIENDO nTotalLinea = nTotalLinea + 1;
          |AL_BUF nBaffer, nTotalLinea, epPuntEdita;
          IepPuntEdita = IepPuntEdita + 1;
     |SIGUE;
|FPRC;
------------------------------------------------------------------------------
|PROCESO Edicion;
     nTope  = nTotalLinea - 1;
     nVari1 = 0;
     nVari2 = 0;
     enModoEdita = enModoEdita + 100;
     |EDITA nBaffer, 0, nTope, enCuadroIni, enCuadroFin, enModoEdita, eaNombreList, enCampoAncho, nVari1, nVari2;
     enModoEdita = enModoEdita $ 100;
|FPRC;
------------------------------------------------------------------------------
|PROCESO CogePuntero;
     IepPuntEdita  = epPuntEdita1 <-;
     |PARA nTotalLinea = 0;  |SI nTotalLinea [ enTotalVaria;  |HACIENDO nTotalLinea = nTotalLinea + 1;
          |DEL_BUF nBaffer, nTotalLinea, epPuntEdita;
          IepPuntEdita = IepPuntEdita + 1;
     |SIGUE;
|FPRC;
------------------------------------------------------------------------------
|PROCESO Edita;
     |DIMENSIONA  200, nBaffer, 0;

     |HAZ GrabaSecuencial;
     |HAZ Edicion;
     |HAZ CogePuntero;

     |FINDIM nBaffer;
|FPRC;
------------------------------------------------------------------------------
|PROGRAMA;
     |SI PARAMETRO = "EXISTE";
          nHay = 0;
          |ABRE #6;
          #6#0 = eaArticulo;
          #6#1 = 1;
          |LEE 000#6];
          |SI FSalida = 0; |Y #6#0 = eaArticulo;
               nHay = 1;
          |FINSI;
          |CIERRA #6;
          |SI nHay = 0; |ACABA; |FINSI;
     |FINSI;

     enTotalVaria  = 100;
     eaNombreList  = "";
     enCuadroIni   = 0711;
     enCuadroFin   = 2070;
     enCampoAncho  = 60;
     enModoEdita   = 0;

     |ATRI R; |PINTA 0611, "  Observaciones  "; |ATRI 0;
     |PINTA 0628, "                                           "; |ATRI 0;

     |ABRE #6;
     |PARA nVariable = 1;  |SI nVariable [ 100;  |HACIENDO nVariable = nVariable + 1;
           IepPuntEdita  = epPuntEdita1 <-;
           IepPuntEdita = IepPuntEdita + (nVariable - 1);

           #6#0 = eaArticulo;
           #6#1 = nVariable;
           |LEE 101#6=;
           |SI FSalida ! 0;
               |SI nVariable = 1;
                    epPuntEdita  = ".";
               |SINO;
                    epPuntEdita  = "";
               |FINSI;
           |SINO;
               epPuntEdita  = #6#2;
           |FINSI;
           |LIBERA #6;
     |SIGUE;
     |ATRI 0;

     |HAZ Edita;

     nHay = 0;
     |PARA nVariable = 1;  |SI nVariable [ 100;  |HACIENDO nVariable = nVariable + 1;
           IepPuntEdita = epPuntEdita1 <-;
           IepPuntEdita = IepPuntEdita + (nVariable - 1);

           |DDEFECTO #6;
           #6#0 = eaArticulo;
           #6#1 = nVariable;
           |LEE 101#6=;
           |SI FSalida ! 0;  |GRABA 020#6b;  |FINSI;

           aAlfa1 = #6#2; |QBT aAlfa1;
           aAlfa2 = epPuntEdita; |QBT aAlfa2;

           |SI aAlfa1 ! aAlfa2;
               #6#2 = epPuntEdita;
               |GRABA 020#6a;
           |FINSI;
           aAlfa = #6#2; |QBF aAlfa;
           |SI aAlfa ! ""; nHay = 1; |FINSI;

           |LIBERA #6;
     |SIGUE;

     |SI nHay = 0;
          #6#0 = eaArticulo;
          #6#1 = 1;
          |LEE 101#6];
          |PARA; |SI FSalida = 0; |Y #6#0 = eaArticulo; |HACIENDO;
               |BORRA 020#6a;
               |LIBERA #6;
               |LEE 101#6s;
          |SIGUE;
     |FINSI;

     |CIERRA #6;
|FPRO;
