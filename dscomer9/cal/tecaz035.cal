          || en el malpe148 hay otro calculo mejor para importar XML  xml

|FICHEROS;
     tecaz035 #0;
     tecam040 #40; || Seguimiento
     referen@;
|FIN;

|VARIABLES;
     fFec = @;
     aFin = "";
     aDato = "";
     aDato1 = "";
     aTexto = "";
     aServidor = "";
     aDirOri = "";
     aDirDes = "";
     aTitulo = "";
     aFic = "";
     aAsunto = "";
     aError = "";
     nConta = 0;
     nSwConta = 0;
     nCampo1 = 0;
     nCampo2 = 0;
     nCampo3 = 0;
     nCampo4 = 0;
     nCampo5 = 0;
     nCampo6 = 0;
     nCampo7 = 0;
     nCampo8 = 0;
     nCampo9 = 0;
     nCampo10 = 0;
     nCampo11 = 0;
     nCampo12 = 0;
     nCampo13 = 0;
     nCampo14 = 0;
     nCampo15 = 0;
     nCampo16 = 0;
     aValor1 = "";
     aValor2 = "";
     aValor3 = "";
     aValor4 = "";
     aValor5 = "";
     aValor6 = "";
     aValor7 = "";
     aValor8 = "";
     aValor9 = "";
     aValor10 = "";
     aValor11 = "";
     aValor12 = "";
     aValor13 = "";
     aValor14 = "";
     aValor15 = "";
     aValor16 = "";
     aCampos = "";
     aNumCam = "";
     nUltimo = 0;
     nCompo = 0;
     nPosi = 0;
     {16}K = 0;
     {16}V = "";
     aValorMaximo = "";

     aParam = "";
     {255}C = "";
     {100}Matriz = "";
     aTmp = "";
     i = 0;
     aGraba = "";
     aFicEmpresa = "";
     nEmpresa = 0;
     aDatEmp = "";
     aDef = "";
     aBas = "";
     nSal = 0;
     aDatFic = "";
     a_aplicacion = "";
     a_empresa = "";
     a_operacion = "";
     a_reemplazar = "";
     aCod = "";
     nPrime = 0;
     aCar = "";
     nFinCod = 0;
     aFichero = "";
     nHandle = 0;
     aRuta = "";
     nBoton1 = 0;
     aAlfa = "";
     nNivel = 0;
     aNomDef = "";
     aCampo = "";
     nError = 0;
     nCampo = 0;
     aVal = "";
     aClave = "";
     aLon = "";
     aDir = "";
     aDSCorreo = "";
     aUsu = "";
     aPwd = "";
     aPop3 = "";
     aNombre = "";
     aMensaje = "";
     nCorreos = 0;
     {1}aLocal = "";
     aTipoFic = "";
     aAlfa1 = "";
     aCadena = "";
     j = 0;
     aEnter = "";
     aReturn = "";
|FIN;

|PROCESO Modi; |TIPO 0;
     |C_M #0#1, 1, "N";
     |C_M #0#3, 1, "N";
     |C_M #0#4, 1, "N";
     |C_M #0#5, 1, "N";
     |C_M #0#6, 1, "N";
     |C_M #0#7, 1, "N";
     |C_M #0#8, 1, "N";
     |SI #0#3 = "S";
          |C_M #0#3, 1, "S";
          |C_M #0#4, 1, "S";
          |C_M #0#5, 1, "S";
          |C_M #0#6, 1, "S";
          |C_M #0#7, 1, "S";
          |C_M #0#8, 1, "S";
     |SINO;
          |C_M #0#1, 1, "S";
     |FINSI;
|FPRC;

|PROCESO Clave0; |TIPO 0;
     |SI #0Campo = Contenido; |ACABA; |FINSI;
     aClave = #0#8; |QBF aClave;
     aLon = aClave % A0;
     aAlfa = "*" * (N\aLon);
     #0#8 = aAlfa;
     |PINTA #0#8;
|FPRC;

|PROCESO SoloFija; |TIPO 7;
     |SI #0#4 = "F";
          aAlfa = #0#5; |QBF aAlfa;
          |SI aAlfa = "Automatico";
               #0#5 = "";
               |PINTA #0#5;
          |FINSI;
          |SI #0#3 = "S";
               |C_M #0#5, 1, "S";
          |FINSI;
     |SINO;
          |C_M #0#5, 1, "N";
          #0#5 = "Automatico";
          |PINTA #0#5;
     |FINSI;
|FPRC;

|PROCESO DSCorreo;
     |QUE_SISTEMA aAlfa;
     |SI aAlfa = "ESBSD";|Y #0#4 = "S";
          |MENAV "    Configuracion no permitida, servidor UNIX. Recomendado: [F]";
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO Ultimo1;
     |LEE 000#referen@.u;
     |SI FSalida = 0;
          nConta = #referen@#nCampo# + 1;
     |SINO;
          nConta = 1;
     |FINSI;
|FPRC;

|PROCESO QuitaVDSCorreo;
     aAlfa = aCadena % 0301;
     |SI aAlfa = "[";
          aAlfa = aCadena % 1101;
          |SI aAlfa = "]"; aCadena = (aCadena % 0102) + (aCadena % 1299); |FINSI;
     |SINO;
          aAlfa = aCadena % 0401;
          |SI aAlfa = "[";
               aAlfa = aCadena % 1201;
               |SI aAlfa = "]"; aCadena = (aCadena % 0103) + (aCadena % 1399); |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Ultimo16;
     aValorMaximo = "|C" + (("000" + nCampo) % -103) + "MAXIMO";
     aValorMaximo = #referen@#aValorMaximo#;
     nCampo1 = K1;
     nCampo2 = K2;
     nCampo3 = K3;
     nCampo4 = K4;
     nCampo5 = K5;
     nCampo6 = K6;
     nCampo7 = K7;
     nCampo8 = K8;
     nCampo9 = K9;
     nCampo10 = K10;
     nCampo11 = K11;
     nCampo12 = K12;
     nCampo13 = K13;
     nCampo14 = K14;
     nCampo15 = K15;
     nCampo16 = K16;
     aValor1 = V1;
     aValor2 = V2;
     aValor3 = V3;
     aValor4 = V4;
     aValor5 = V5;
     aValor6 = V6;
     aValor7 = V7;
     aValor8 = V8;
     aValor9 = V9;
     aValor10 = V10;
     aValor11 = V11;
     aValor12 = V12;
     aValor13 = V13;
     aValor14 = V14;
     aValor15 = V15;
     aValor16 = V16;
     #referen@#nCampo# = aValorMaximo;
     |LEE 000#referen@.];
     |SI FSalida = 0;
          |LEE 000#referen@.a;
     |SINO;
          |LEE 000#referen@.u;
     |FINSI;
     |SI FSalida = 0;
               |Y #referen@#nCampo1# = aValor1;
               |Y #referen@#nCampo2# = aValor2;
               |Y #referen@#nCampo3# = aValor3;
               |Y #referen@#nCampo4# = aValor4;
               |Y #referen@#nCampo5# = aValor5;
               |Y #referen@#nCampo6# = aValor6;
               |Y #referen@#nCampo7# = aValor7;
               |Y #referen@#nCampo8# = aValor8;
               |Y #referen@#nCampo9# = aValor9;
               |Y #referen@#nCampo10# = aValor10;
               |Y #referen@#nCampo11# = aValor11;
               |Y #referen@#nCampo12# = aValor12;
               |Y #referen@#nCampo13# = aValor13;
               |Y #referen@#nCampo14# = aValor14;
               |Y #referen@#nCampo15# = aValor15;
               |Y #referen@#nCampo16# = aValor16;
          nConta = #referen@#nCampo# + 1;
     |SINO;
          nConta = 1;
     |FINSI;
|FPRC;

|PROCESO Contador;
     aAlfa = "|K000";
     aCampos = #referen@#aAlfa#;
     aNumCam = aCampos % 103;
     IK = K1<-; || Se guardan los campos clave
     IV = V1<-; || Se guardan los valores de los campos clave
     |PARA nCompo = 1; |SI nCompo < aNumCam; |HACIENDO  nCompo = nCompo + 1;
          nPosi = nCompo * 300 + 103;
          aAlfa = aCampos % nPosi;
          nCampo = aAlfa;
          K = nCampo;
          V = #referen@#nCampo#;
          IK = IK + 1;
          IV = IV + 1;
     |SIGUE;
     |PARA; |SI nCompo [ 16; |HACIENDO nCompo = nCompo + 1;
          K = nCampo;
          V = #referen@#nCampo#;
          IK = IK + 1;
          IV = IV + 1;
     |SIGUE;

     nCompo = aNumCam;
     nPosi = nCompo * 300 + 103;
     aAlfa = aCampos % nPosi;
     nCampo = aAlfa;

     |SI aNumCam = 1;
          |HAZ Ultimo1;
     |SINO;
          |HAZ Ultimo16;
     |FINSI;
     |DDEFECTO #referen@;
     #referen@#nCampo# = nConta;
|FPRC;

|PROCESO ActuaEmp;
     |SI aFicEmpresa ! "";  || Si no es '' se chequea que exista la empresa
          nSal = 1;
          aAlfa = aDef + aFicEmpresa;
          |CARGA_DEF aAlfa, referen@;
          |SI FSalida = 0;
               |PATH_DAT #referen@#aDatEmp#;
               |ABRE #referen@;
               #referen@#0 = a_empresa;
               |LEE 000#referen@.=;
               nSal = FSalida;
               |CIERRA #referen@;
               |DESCARGA_DEF #referen@;
          |FINSI;
          |SI nSal ! 0;
               aAlfa = "0000No existe empresa '" + a_empresa + "' en '" + a_aplicacion + "' (" + aFicEmpresa + ")";
               aError = "No existe empresa '" + a_empresa + "' en '" + a_aplicacion + "' (" + aFicEmpresa + ")";
               nError = 1;
               |MENAV aAlfa;
               |ACABA;
          |FINSI;
     |FINSI;

     || Y ahora actualiza el fichero
     aAlfa = aDef + aNomDef;
     |CARGA_DEF aAlfa, referen@;
     |SI FSalida = 0;
          |PATH_DAT #referen@#aDatFic#;
          |DDEFECTO #referen@;
          |ABRE #referen@;

          aAlfa = "|K000";
          aCampos = #referen@#aAlfa#;
          aNumCam = aCampos % 103;

          nUltimo = (N\aNumCam) * 300 + 103;
          aNumCam = aCampos % nUltimo;
          nUltimo = aNumCam;            || Ultimo campo de la clave

          nSwConta = 0;
          |PARA IC = C1<-; |SI C ! ""; |HACIENDO;
               aAlfa = C % 103;
               nCampo = aAlfa;
               aAlfa = C % 4;
               #referen@#nCampo# = aAlfa;
               |SI nCampo = nUltimo;
                    |QBT aAlfa;
                    |SI aAlfa = "#CONTADOR#"; nSwConta = 1; |FINSI;
               |FINSI;
               IC = IC + 1;
          |SIGUE;

          |SI nSwConta = 1;  || Busca ultimo+1 y copia otra vez los campos
               |HAZ Contador;
               |PARA IC = C1<-; |SI C ! ""; |HACIENDO;
                    aAlfa = C % 103;
                    nCampo = aAlfa;
                    aAlfa = C % 4;
                    |SI nCampo ! nUltimo;
                         #referen@#nCampo# = aAlfa;
                    |FINSI;
                    IC = IC + 1;
               |SIGUE;
          |FINSI;

          |SI a_operacion = "GRABAR";
               |LEE 101#referen@.=;
               aGraba = "N";
               |SI a_reemplazar = "S"
                     |SI FSalida ! 0; |GRABA 020#referen@.b; |FINSI;
                     aGraba = "S";
               |SINO;
                    |SI FSalida ! 0;
                         |GRABA 020#referen@.b;
                         aGraba = "S";
                    |FINSI;
               |FINSI;
               |SI aGraba = "S";
                    |PARA IC = C1<-; |SI C ! ""; |HACIENDO;
                         aAlfa = C % 103;
                         nCampo = aAlfa;
                         aAlfa = C % 4;
                         |SI nSwConta = 0;
                              #referen@#nCampo# = aAlfa;
                         |SINO;
                              |SI nCampo ! nUltimo;
                                   #referen@#nCampo# = aAlfa;
                              |FINSI;
                         |FINSI;
                         IC = IC + 1;
                    |SIGUE;
                    |GRABA 020#referen@.a;
               |FINSI;
               |LIBERA #referen@;
          |FINSI;

          |SI a_operacion = "BORRAR";
               |BORRA 000#referen@.c;
          |FINSI;

          |CIERRA #referen@;
          |DESCARGA_DEF #referen@;
     |SINO;
          aAlfa = "0000ERROR, carga:" + aAlfa;
          aError = "ERROR, carga:" + aAlfa;
          nError = 1;
          |MENAV aAlfa;
     |FINSI;
|FPRC;

|PROCESO Actualiza;
/*
     |BLIN 14; |PINTA 1401, a_aplicacion;
     |BLIN 15; |PINTA 1501, a_empresa;
     |BLIN 16; |PINTA 1601, a_operacion;
     |BLIN 17; |PINTA 1701, a_reemplazar;
     |BLIN 18; |PINTA 1801, aNomDef;
*/
     |SI a_operacion ! "GRABAR"; |Y a_operacion ! "BORRAR";
          aAlfa = "0000Operacion no permitida '" + a_operacion + "'";
          aError = "Operacion no permitida '" + a_operacion + "'";
          nError = 1;
          |MENAV aAlfa; |ACABA;
     |FINSI;

     /* Aqui se definen los distintos ficheros de empresas y se formatea */
     aFicEmpresa = "";
     nEmpresa = a_empresa;

     |SI a_aplicacion = "dscomer9";
          aFicEmpresa = "agifa041";                || Fichero empresa
          a_empresa = ("00000" + nEmpresa) % -105; || Formateo
     |FINSI;
     /* -- */

     |DBASS aBas;
     aDatEmp = aBas + a_aplicacion + "/fich/";
     aDatFic = aBas + a_aplicacion + "/fich/" + a_empresa + "/";
     aDef = aBas + a_aplicacion + "/def/";

     aAlfa = aDef + aNomDef + ".mas";
     |XABRE "E", aAlfa, 0;
     |SI FSalida = 0;
          |HAZ ActuaEmp;
     |SINO;
          aAlfa = "0000No existe def '" + aNomDef + "' en aplicacion '" + a_aplicacion + "'";
          aError = "No existe def '" + aNomDef + "' en aplicacion '" + a_aplicacion + "'";
          nError = 1;
          |MENAV aAlfa;
     |FINSI;
|FPRC;

|PROCESO Pop;
     nNivel = nNivel - 1;
     IMatriz = Matriz1<-;
     IMatriz = IMatriz + nNivel;

     |SI aNomDef ! "";
          aCampo = Matriz - "C";
          |SI aCampo = aNomDef;
               |HAZ Actualiza;
               IC = C1<-;
               C = "";
               aNomDef = "";
          |SINO;
               |SI aCampo = Matriz;
                    |MENAV "0000Estructura xml incorrecta (campo)";
                    aError = "Estructura xml incorrecta (campo)";
                    nError = 1;
               |FINSI;
               nCampo = aCampo;
               aCampo = ("000" + nCampo) % -103;
               C = aCampo + aVal;
               IC = IC + 1;
               C = "";
          |FINSI;
     |FINSI;
     |SI Matriz = "aplicacion"; a_aplicacion = aVal; |FINSI;
     |SI Matriz = "empresa"; a_empresa = aVal; |FINSI;
     |SI Matriz = "operacion"; a_operacion = aVal; |FINSI;
     |SI Matriz = "reemplazar"; a_reemplazar = aVal; |FINSI;
     FSalida = Matriz;
|FPRC;

|PROCESO Push;
     IMatriz = Matriz1<-;
     IMatriz = IMatriz + nNivel;
     Matriz = aCod;
     nNivel = nNivel + 1;

     |SI a_aplicacion ! ""; |Y a_empresa ! ""; |Y a_operacion ! ""; |Y a_reemplazar ! "";
          |SI aNomDef = ""; aNomDef = Matriz; |FINSI;
     |FINSI;
     |SI Matriz = "aplicacion"; a_aplicacion = ""; aNomDef = ""; |FINSI;
     |SI Matriz = "empresa"; a_empresa = ""; aNomDef = ""; |FINSI;
     |SI Matriz = "operacion"; a_operacion = ""; aNomDef = ""; |FINSI;
     |SI Matriz = "reemplazar"; a_reemplazar = ""; aNomDef = ""; |FINSI;
|FPRC;

|PROCESO Codigo;
     |BLIN 23; |PINTA 2301, aCod;
     aAlfa = aCod % 101;
     |SI aAlfa = "/";
          |HAZ Pop;
          aAlfa = "/" + FSalida;
          |SI aAlfa ! aCod;
               |MENAV "0000Estructura xml incorrecta";
               aError = "Estructura xml incorrecta";
               nError = 1;
          |FINSI;
     |SINO;
          |HAZ Push;
     |FINSI;
     aCod = "";
     aVal = "";
|FPRC;

|PROCESO Caracter;
     |SI nError = 1; |ACABA; |FINSI;
     |SI nPrime = 1;
          |SI aCar = ">";
               |HAZ Codigo;
               nFinCod = 1;
          |SINO;
               |SI aCar = "<";
                    nFinCod = 0;
               |SINO;
                    |SI nFinCod = 0;
                         aCod = aCod + aCar;
                    |SINO;
                         aVal = aVal + aCar;
                    |FINSI;
               |FINSI;
          |FINSI;
     |SINO;
          |SI aCar = "<"; nPrime = 1; |FINSI; || El primer caracter valido
          nNivel = 0;
          nFinCod = 0;
          aCod = "";
          aVal = "";
     |FINSI;
|FPRC;

|PROCESO ImportaFic;
     |QBF aFichero;
     |XABRE aTipoFic, aFichero, nHandle;
     |SI FSalida ] 0;
          |XLEE nHandle, 250, aTmp;
          |PARA; |SI FSalida > 0; |HACIENDO;
               |PARA; |SI aTmp ! ""; |HACIENDO;
                    aCar = aTmp % 101;
                    |SI aTmp = aCar;
                         aTmp = ""; ||con un caracter falla: "aTmp = aTmp % 2"
                    |SINO;
                         aTmp = aTmp % 2;
                    |FINSI;
                    |HAZ Caracter;
               |SIGUE;
               |XLEE nHandle, 250, aTmp;
          |SIGUE;
          |XCIERRA nHandle;
     |SINO;
          |MENAV "0000No existe fichero...";
          aError = "No exsite fichero";
          nError = 1;
     |FINSI;
|FPRC;

|PROCESO Recoge;
     |DFICE aDir;
     |SI #0#4 = "F";
          aDSCorreo = #0#5;  |QBF aDSCorreo;
     |SINO;
          |SI #0#4 = "L";   |IP_REMOTO aDSCorreo;    |FINSI;
          |SI #0#4 = "S";   |IP_LOCAL aDSCorreo;     |FINSI;
     |FINSI;
     aUsu = #0#7; |QBF aUsu;
     aPwd = aClave; |QBF aPwd;
     aPop3 = #0#6; |QBF aPop3;
     aNombre = "";

     aAlfa = "0000Recibiendo [" + aDSCorreo + "]";
     |MENSA aAlfa;
     |DSCORREO_RECIBE aDSCorreo, aPop3, aUsu, aPwd, aDir, aNombre;
     nError = FSalida;

     aAsunto = "";
     |SI nError ] 0;
          aCadena = FSalida; |HAZ QuitaVDSCorreo; aAlfa = aCadena; |QBF aAlfa;
          aAlfa = aAlfa % 0299; aAsunto = aAlfa;
          aLon = aAlfa % A0;
          |PARA j = 0; |SI j [ aLon; |HACIENDO j = j + 1;
               aAlfa1 = aAsunto % 0101;
               |SI aAlfa1 = " ";
                    aAsunto = aAsunto % 0299;
                |SINO;
                    |SAL;
               |FINSI;
          |SIGUE;
     |FINSI;

     |HORA aAlfa;
     |DDEFECTO #40;
     #40#0 = ~;
     #40#1 = aAlfa;
     #40#2 = aNombre;
     #40#3 = Usuario % 810;
     #40#9 = aAsunto;

     |BLIN 4;
     |SI nError < 0;
          aMensaje = "    Problemas al recibir correo ..." + nError;
          |MENAV aMensaje;

          #40#4 = "Problemas al recibir correo";
          |GRABA 020#40n;
          |HAZ EnviaCorreo;
     |FINSI;
     |SI nError = 0;
          aMensaje = "    Correo recibido con exito [" + nError + "][" + aNombre + "]";
          |MENSA aMensaje;
     |FINSI;
     |SI nError = 1;
          |SI nCorreos = 1;
               |MENAV "    No hay correo ...";
          |SINO;
               i = nCorreos - 1;
               aAlfa = "0000" + i + " Correos recibidos...";
               |MENAV aAlfa;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO RecogeCorreo;
     nCorreos = 0;
     |PARA; |SI; |HACIENDO;
          nError = 0;
          nCorreos = nCorreos + 1;
          |HAZ Recoge;
          |SI nError ! 0; |SAL; |FINSI;

          |MENSA "2403Procesando...";
          aAlfa = aNombre % -103;
          aAlfa = aAlfa > "";
          |SI aAlfa = "XML";
               aFichero = aNombre;
               aTipoFic = "B";
               |HAZ ImportaFic;

               |SI nError = 0;
                    |HORA aAlfa;
                    #40#5 = ~;
                    #40#6 = aAlfa;
                    #40#8 = "S";
               |SINO;
                    #40#7 = aError;
                    #40#8 = "N";
               |FINSI;
               |GRABA 020#40n;
          |SINO;
               |MENAV "0000El adjunto debe traer la extension 'xml'";
               #40#4 = "El adjunto no es XML";
               |GRABA 020#40n;
          |FINSI;
          |FBORRA aNombre;
          |SLEEP 2;

          |HAZ EnviaCorreo;
     |SIGUE;
|FPRC;

|PROCESO Tipo3; |TIPO 3;
     |SI #0#2 ! "SI"; |ACABA; |FINSI;
     |ABRE #40;
     |SI #0#3 = "N";
          aFichero = #0#1;
          aTipoFic = "CB";
          |HAZ ImportaFic;
     |SINO;
          |HAZ RecogeCorreo;
     |FINSI;
     |CIERRA #40;
|FPRC;

|PROCESO Boton1;
     |PTEC 824;
|FPRC;

|PROCESO Browser;
     |SI FSalida ! 10; |ACABA; |FINSI;

     aRuta = #0#1;  |QBF aRuta;
     |SI aRuta = "";
         aRuta = "*.xml";
     |FINSI;
     aRuta = "F" + aRuta;

     |BROWSE aRuta;

     aRuta = aRuta % 299;
     #0#1 = aRuta;
     |PINTA #0#1;
     |QBF aRuta;
     |SI aRuta = ""; |ERROR; |FINSI;
|FPRC;

|PROCESO Quita; |TIPO 0;
     |ESTADO_CONTROL nBoton1, "DISABLE";
|FPRC;

|PROCESO Pone; |TIPO 7;
     |ESTADO_CONTROL nBoton1, "ENABLE";
|FPRC;

|PROGRAMA;
     aParam = PARAMETRO; ||desde el menu se llama con parametro
     |SI aParam ! "";
          |CONTROL_SIMPLE 0, "BOTON,  Fichero ", 1616, 926, Boton1;
          nBoton1 = FSalida;
          |HAZ Quita;
     |FINSI;

     |ABRE #0;
     |LEE 101#0=;
     |SI FSalida ! 0; |GRABA 020#0b; |FINSI;
     |SI aParam ! "";
          aClave = #0#8; |QBF aClave;
          aLon = aClave % A0;
          aAlfa = "*" * (N\aLon);
          #0#8 = aAlfa;
          |CLS;
          |PINPA #0#0;
          |PINDA #0#0;
          |ENDATOS #1#0;
          #0#2 = "NO";
          #0#8 = aClave;
          |GRABA 020#0a;
          |FIN_CONTROL_WINDOWS nBoton1;
          |PULSATECLA;
     |SINO;
          aClave = #0#8; |QBF aClave;
          aLon = aClave % A0;
          aAlfa = "*" * (N\aLon);
          #0#8 = aAlfa;
          #0#2 = "SI";
          |HAZ Tipo3;
     |FINSI;
     |CIERRA #0;
|FPRO;

|PROCESO EnviaCorreo;
     aEnter = &13;
     aReturn = &10;
     aFin = aEnter + aReturn;

     aAlfa = "|NC";
     aCampo = #40#aAlfa#;
     aDato = "";
     aDato1 = aFin + aFin + ("-"*20) + aFin;
     |PARA i = 0; |SI i < aCampo; |HACIENDO i = i + 1;
          aAlfa = #40i; |QBF aAlfa;
          aDato = aDato + aAlfa + "#";
          aDato1 = aDato1 + aAlfa + aFin;
     |SIGUE;
     aDato1 = aDato1 + ("-"*20);

     fFec = ~;
     aAlfa1 = (fFec % 704) + (fFec % 402) + (fFec % 102);
     |HORA aAlfa;
     aAlfa = (aAlfa % 102) + (aAlfa % 402) + (aAlfa % 702);
     aFic = aAlfa1 + aAlfa + ".txt";
     |XABRE "W", aFic, nHandle;
     |SI FSalida ] 0;
          |XGRABA nHandle, aDato;
          |XCIERRA nHandle;
     |FINSI;

     aDSCorreo = "xdscorreo.dv.diagram.net";
     aServidor = "smtp.diagram.es";
     aDirOri = "jorge.diaz@diagram.es";
     aDirDes = aDirOri;
     aTitulo = "XML TECATEL";
     aTexto = "Adjunta fichero." + aDato1;
     |DSCORREO_ENVIA aDSCorreo, aServidor, aDirOri, aDirDes, aTitulo, aTexto, aFic;
     |SI FSalida ! 0;
          ||MENAV "0000Problemas al enviar correo. Revise la conexion.";
     |SINO;
          ||MENAV "0000Envio correo completado.";
     |FINSI;
     |RFBORRA aFic;
|FPRC;
