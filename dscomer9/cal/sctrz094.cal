|FICHEROS;
     sctrz090 #0;
     sctrm079 #79;
     sctrm082 #82;
     sctrm083 #83;
     sctrm084 #84;
|FIN;

|VARIABLES;
     &efFechaViaje = @;

     &eaError = "";
     &enError = 0;
     &Tempo = "";
     aTmp14 = "";
     aParam = "";
     nNume = 0;
     aPath = "";
     aDes = "";
     aDato = "";
     nHandle = 0;
     i = 0;
     aLon = "";
     nPos = 0;
     aLetra = "";
     aReg = "";
     aTab = "";
 {99}Valor = "";
 {99}Viaje = "";
     nCampo = 0;
     aNomFic = "";
     aAlfa = "";
     nReg = 0;
     aCar = "";
     aReturn = "";
     aEnter = "";
     aTmp = "";
     aViaje = "";
     aTipoViaje = "";
     aFecha = "";
     aHora = "";
     aOrg = "";
|FIN;

|PROCESO Inicio; |TIPO 3;
     |HAZ Chequea;
     |SI enError = 0;
          |HAZ CreaReserva;
     |FINSI;
|FPRC;

|PROCESO Borra83;
     |BORRA 020#83a;
|FPRC;

|DEFBUCLE Borra83;
     #83#1;
     ;
     #79#0, " ", 1;
     #79#0, "z", 9;
     ;
     NULL, Borra83;
|FIN;

|PROCESO Borra82;
     |BORRA 020#82a;
|FPRC;

|DEFBUCLE Borra82;
     #82#1;
     ;
     #79#0, 001;
     #79#0, 999;
     ;
     NULL, Borra82;
|FIN;

|PROCESO GraViajes;
     IViaje = Viaje1<-;
     aDato = "";
     aLon = aViaje % A0;
     |PARA i = 1; |SI i [ aLon; |HACIENDO i = i + 1;
          nPos = i * 100 + 1;
          aCar = aViaje % nPos;
          |SI aCar = ",";
               |SI aDato = "None"; aDato = ""; |FINSI;
               Viaje = aDato;
               aDato = "";
               IViaje = IViaje + 1;
          |SINO;
               aDato = aDato + aCar;
          |FINSI;
     |SIGUE;
     |SI aDato = "None"; aDato = ""; |FINSI;
     Viaje = aDato;

     aAlfa = Viaje1; |QBF aAlfa;
     |SI aAlfa = ""; |ACABA; |FINSI;

     |DDEFECTO #83;
     #83#0 = #79#0;
     #83#1 = aTipoViaje;
     #83#2 = 1;
     #83#3 = Viaje1;
     #83#4 = Viaje2;
     #83#5 = Viaje3;

     aFecha = Viaje4 % 110; aHora = Viaje4 % 12;
     #83#6 = aFecha;
     #83#7 = aHora;

     aFecha = Viaje5 % 110; aHora = Viaje5 % 12;
     #83#8 = aFecha;
     #83#9 = aHora;
     #83#10 = Viaje6;
     #83#11 = Viaje7;
     |GRABA 020#83n;

     aAlfa = Viaje8; |QBF aAlfa;
     |SI aAlfa = ""; |ACABA; |FINSI;

     |DDEFECTO #83;
     #83#0 = #79#0;
     #83#1 = aTipoViaje;
     #83#2 = 2;
     #83#3 = Viaje8;
     #83#4 = Viaje9;
     #83#5 = Viaje10;

     aFecha = Viaje11 % 110; aHora = Viaje11 % 12;
     #83#6 = aFecha;
     #83#7 = aHora;

     aFecha = Viaje12 % 110; aHora = Viaje12 % 12;
     #83#8 = aFecha;
     #83#9 = aHora;
     #83#10 = Viaje13;
     #83#11 = Viaje14;
     |GRABA 020#83n;

     aAlfa = Viaje15; |QBF aAlfa;
     |SI aAlfa = ""; |ACABA; |FINSI;

     |DDEFECTO #83;
     #83#0 = #79#0;
     #83#1 = aTipoViaje;
     #83#2 = 3;
     #83#3 = Viaje15;
     #83#4 = Viaje16;
     #83#5 = Viaje17;

     aFecha = Viaje18 % 110; aHora = Viaje18 % 12;
     #83#6 = aFecha;
     #83#7 = aHora;

     aFecha = Viaje19 % 110; aHora = Viaje19 % 12;
     #83#8 = aFecha;
     #83#9 = aHora;
     #83#10 = Viaje20;
     #83#11 = Viaje21;
     |GRABA 020#83n;
|FPRC;

|PROCESO Peticion;
     |SI nReg = 1;
          #79#18 = Valor1;
          |LEE 101#79=;
          |SI FSalida ! 0;
               eaError = "Id no encontrada.";
               enError = 5; |ACABA;
          |FINSI;

          #84#0 = #79#0;
          #84#1 = 1;
          |LEE 000#84];
          |SI FSalida = 0; |Y #84#0 = #79#0;
               eaError = "Reserva ya procesada.";
               enError = 6; |ACABA;
          |FINSI;

          #79#19 = Valor9;
          #79#20 = Valor10;
          |GRABA 020#79a;
          |LIBERA #79;

          |BUCLE Borra82;

          |BUCLE Borra83;
          aViaje = Valor11; aTipoViaje = "I"; |HAZ GraViajes;
          aViaje = Valor12; aTipoViaje = "V"; |HAZ GraViajes;
     |FINSI;
     |DDEFECTO #82;
     #82#0 = #79#0;
     #82#1 = nReg;
     #82#2 = Valor2;
     #82#3 = Valor3;
     #82#4 = Valor4;
     #82#5 = Valor5;
     #82#6 = Valor6;
     #82#7 = Valor7;
     #82#8 = Valor8;
     #82#11 = Valor13;
     |SI Valor14 ! ""; #82#12 = Valor14; |FINSI;
     |GRABA 020#82n;
|FPRC;

|PROCESO Caracter;
     |SI aCar = aEnter; |ACABA; |FINSI;
     |SI aCar = aReturn;
          |SI aReg = "None"; aReg = ""; |FINSI;
          Valor = aReg;
          aReg = "";
          |SI nReg ] 1;
               |HAZ Peticion;
          |FINSI;
          IValor = Valor1<-;
          nReg = nReg + 1;
     |SINO;
          |SI aCar = aTab;
               |SI aReg = "None"; aReg = ""; |FINSI;
               Valor = aReg;
               aReg = "";
               IValor = IValor + 1;
          |SINO;
               aReg = aReg + aCar;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO LeePeticion;
     nReg = 0;
     IValor = Valor1<-;

     |ABRE #84;
     |ABRET #82;
     |ABRET #83;
     |DFICE aPath;
     aDes = aPath + aNomFic + ".ent";
     |XABRE "B", aDes, nHandle;
     |SI FSalida ] 0;
          |XLEE nHandle, 250, aTmp;
          |PARA; |SI FSalida > 0; |HACIENDO;
               |PARA; |SI aTmp ! ""; |HACIENDO;
                    aCar = aTmp % 101;
                    |SI aTmp = aCar;
                         aTmp = ""; ||con un caracter falla: "aTmp = aTmp % 2"
                    |SINO;
                         aTmp = aTmp % 2;
                    |FINSI;
                    |HAZ Caracter;
                    |SI enError ! 0; |SAL; |FINSI;
               |SIGUE;
               |SI enError ! 0; |SAL; |FINSI;
               |XLEE nHandle, 250, aTmp;
          |SIGUE;
          |XCIERRA nHandle;
     |FINSI;
     |CIERRAT #83;
     |CIERRAT #82;
     |CIERRA #84;
|FPRC;

|PROCESO GrabaReservas;
     aOrg = #84#6; |QBF aOrg;
     aDes = #84#7; |QBF aDes;
     aDato = #84#5 + aTab + aOrg + aTab + aDes + aTab;
     aDato = aDato + #84#1 + aTab + #84#2 + aTab + #79#22 + aTab + "" + aTab;
     aDato = aDato + 0 + aTab + #84#8;
     |XGRABA nHandle, aDato;
|FPRC;

|DEFBUCLE GrabaReservas;
     #84#1;
     ;
     #79#0, 0000000;
     #79#0, 9999999;
     ;
     NULL, GrabaReservas;
|FIN;

|PROCESO GrabaResultado;
     |DFICE aPath;
     aDes = aPath + aNomFic + ".sal";
     |XABRE "W", aDes, nHandle;
     |SI FSalida = 0;
          aDato = "plataforma" + aTab;
          aDato = aDato + "origen" + aTab;
          aDato = aDato + "destino" + aTab;
          aDato = aDato + "reserva" + aTab;
          aDato = aDato + "localizador" + aTab;
          aDato = aDato + "divisa" + aTab;
          aDato = aDato + "error" + aTab;
          aDato = aDato + "cerror" + aTab;
          aDato = aDato + "tipo_pax" + aTab;
          |XGRABA nHandle, aDato;
          |SI eaError ! "";
               aAlfa = aTab + aTab + aTab + aTab + aTab + aTab + eaError + aTab + enError + aTab + aTab;
               |XGRABA nHandle, aAlfa;
          |SINO;
               |BUCLE GrabaReservas;
          |FINSI;
          |XCIERRA nHandle;
     |FINSI;
|FPRC;

|PROGRAMA;
     aParam = PARAMETRO;

     aTab = &9;
     aEnter = &13;
     aReturn = &10;

     |SI aParam = "menu";
          aParam = "valor a debugar";
          |DEBUG;
     |FINSI;

     enError = 0; eaError = "";

     aNomFic = aParam;

     |ABRE #79; |SELECT #2#79;

     |HAZ LeePeticion;
     |SI enError = 0;
          |HAZ Inicio;
     |FINSI;
     |HAZ GrabaResultado;

     |CIERRA #79;
|FPRO;
