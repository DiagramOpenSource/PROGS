|FICHEROS;
     agifa010 #0,MANTE;
     agifa007 #7;
     agifa071 #71;
     agifa142 #142;
|FINSI;

|VARIABLES;
     &enSwMenu = 0;
     nErrorConta = 0;
     nConta = 0;
     nNume = 0;
     &eaParam = "";
     &eaSerie = "";
     &enCodigo = 0;
     nExiste = 0;
     aBono = "";
     aAlfa = "";
     aSer = "";
     aNum = "";
     &enSalida = 0;
     &ser_alb = "";
     &num_alb = 0;
     &abono = "";
     &enSwRecalAlb = 0;
|FINS;

|PROCESO _CompruConta;
     |SI #142#136 ! "S";
          aAlfa = ("000000" + nConta ) % -106;
          |SI #142#136 = "N";
               |SI nNume > nConta;
                    aAlfa = "0000Numero incorrecto. No puede ser mayor de " + aAlfa;
                    |MENAV aAlfa;
                    nErrorConta = 1;
               |FINSI;
          |SINO;
               |SI nNume > nConta;
                    aAlfa = "0000Numero superior a " + aAlfa;
                    |MENAV aAlfa;
                    |CONFI "0000N¿Esta Seguro?";
                    |SI FSalida ! 0;
                         nErrorConta = 1;
                    |FINSI;
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO RecalLin;
     |HAZ TotalLin71;
     |HAZ CalCabAgifa010;
     |GRABA 020#71a;
     |LIBERA #71;
|FPRC;

|PROGRAMA;               || lo llama el 'agifa059'
     aAlfa = PARAMETRO;
     |QBF aAlfa;

     |ABRE #142; |LEE 000#142p; |CIERRA #142;

     |HAZ EsMalpesa;
     |SI FSalida = 0;
          eaParam = aAlfa;
          |CORRE "malpe035";
     |FINSI;

     aBono = aAlfa % 101;
     aSer = aAlfa % 205;
     aNum = aAlfa % 706;
     |SI enSwRecalAlb = 1;    || lo llama el 'agifa024'
          |ABRE #0;
          #0#52 = aSer;
          #0#0 = aNum;
          |LEE 121#0=;
          |SI FSalida = 0;
               |HAZ LimCabAgifa010;
               |BUCLELIN 0RecalLin#0;
               |GRABA 020#0a;
          |FINSI;
          |CIERRA #0;
          |ACABA;
     |FINSI;

     |C_M #0#43, 1, "N";
     #0#43 = aBono;

     |PUSHV 0101 2380;
     |CLS;

     |ABRE #0;
     #0#52 = aSer;
     #0#0 = aNum;
     |LEE 000#0=;
     |SI #0#41 ! 0; |Y FSalida = 0;
          |CIERRA #0;
          |CORRE "dsz99914";  ||la adj.
     |FINSI;

     |LEE 101#0=;
     nExiste = FSalida;
     |PINPA #0#0;
     |PINDA #0#0;
     |SI nExiste ! 0;
          |SI #142#137 ! "F";
               enSwMenu = 1;
               |HAZ ContaAV7;
               nConta = #0#0;
               nNume = aNum;
               |HAZ _CompruConta;
          |FINSI;
          |SI nErrorConta = 0;
               #0#52 = aSer;
               #0#0 = aNum;
               |GRABA 020#0b;
               |CONFI "0000SConfirma el alta (S/N)";
          |SINO;
               FSalida = 1;
          |FINSI;
     |SINO;
          FSalida = 0;
     |FINSI;
     enSalida = FSalida;
     |SI FSalida ! 0;
          |SI nErrorConta = 0;
               |SI nExiste ! 0; |BORRA 020#0c; |FINSI;
          |FINSI;
     |SINO;
          |ABRE #7;
          #7#26 = aSer;
          |LEE 020#7=;
          |CIERRA #7;
          |ATRI I;
          |PINTA 0908, #7#27;
          |ATRI 0;
          |SI nExiste = 0;
               |ENDATOS #2#0;
          |SINO;
               |ENDATOS #1#0;
          |FINSI;
          enSalida = FSalida;
          |SI FSalida ! 0;
               |SI nExiste ! 0; |BORRA 020#0c; |FINSI;
          |SINO;
               |GRABA 020#0a;
               enSalida = FSalida;
               |HAZ EsAlbadis;
               |SI FSalida = 0;
                    eaSerie = #0#52;
                    enCodigo = #0#0;
                    |HAZ AlbaRecAlb;
               |FINSI;
               |CONFI "0000NImprimir Albaran";
               |SI FSalida = 0;
                    ser_alb = #0#52;
                    num_alb = #0#0;
                    abono = #0#43;
                    |CIERRA #0;
                    |SUB_EJECUTA_NP "agifa014";    || Impresion de albaranes
               |FINSI;
          |FINSI;
     |FINSI;
     |CIERRA #0;
     |POPV;
|FPRO;
