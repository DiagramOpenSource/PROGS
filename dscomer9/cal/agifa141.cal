************************ Alta de cuentas AGICON Ver 2. **********************
|FICHEROS;
     agifa141 #0;
     agifa060 #6;
     agifa024 #7;
     agifa112 #8;
     agifa025 #11;      ||Comisionistas
     agifa142 #142;

     canempr@ #1;
     cacuent@ #2;
     cacueba@ #5;
     caclipr@ #9;
     carepre@ #50;

     Referen@  #100;
     Referen1@ #101;
     dsm00225 #225;
|FIN;

|VARIABLES;
     nHandle = 0;
     &enSwRegraba = 0;

     aPais = "";
     aLong = "";
     nLong = 0;
     nPais = 0;

     nGraba = 0;
     aCli = "";
     nCeros = 0;
     aZona     = "";
     aEntidad  = "";
     aSucursal = "";
     aDC       = "";
     aCuenta   = "";
     aAlfa = "";
     aPathBase = "";
     Handle1 = 0;
     nCodigo = 0;
     nCalc   = 0;

     aEmp      = "";
     aMensaje  = "";
     aMensa    = "";
     aPath     = "";
     aDef1     = "";
     aDef2     = "";
     aDef5     = "";
     aDef9     = "";
     aDef50    = "";
     &swvta = 0;
     &swcom = 0;
     &empre = 0;
     &periodo = 0;
     &cliente = "";
     &repre   = "";
     &provee  = "";
     clave = " ";
     long = "  ";
     errant = 0;

     path0 = "";
     path  = "";
     patpan = "";
     &empresa = "";
     &dig1 = 0;
     &dig3 = 0;
     &dig4 = 0;
     &dig5 = 0;
     &dig6 = 0;
     &dig7 = 0;
     &dig8 = 0;
     &dig9 = 0;
     cuini = "            ";
     cufin = "999999999998";
     diini = 0;
     difin = 9;
     fecini = "01.01.0000";
     fecfin = "31.12.8999";
     fini = @;
     ffin = @;
     cuenta = " ";
     tifin = "H";
     diain = 0;
     salida = 0;
     diafi = 99;
     prim = 0;
     cuant = "";
     lon = 0;
     aa = 0;
     ini = 0;
     fecalf = "";
     mesfec = 0;
     nomcue = " ";
     a = 0;
     b = 0;
     c = 0;
     mensa = "";
     digito1 = 0;
     traba = "";
     errcuent = "aaaaaaaaaaaa";
     mensaje1 = "0000Cuenta fuera de nivel !!";
     mensa2   = "0000No esta definida esta cuenta en Cuentas de Balance";
     uno = "";
     dos = "";
     mascara = "";
     nivel = 0;
     dir_fich = " ";
     anyo = " ";
     litmes = " ";
     mes = 0;
     dmes = " ";
     hmes = " ";
     swerror = 0;
     estado = 0;
     estadc = 0;
     alfa1 = "";
     alfa2 = "";
     alfa3 = "";
     aAlfa1 = "";
     aAlfa2 = "";

     &eaClPr      = "";
     &eaCodigoNum = "";
     nAvisar      = 0;
     nError       = "";
     aLosDefs     = "";
     aElProgr     = "";
     nCanal       = 0;
     nConta       = 0;
|FIN;

-------------------------------------------------------
|PROCESO MalpesaCli;
     || la cuenta de malpesa son 10 digitos :  prefijo + 5 del cliente

     aCli = #7#0 % -105;

     cuenta =  "43100" + aCli;
     nomcue = #7#1;
     |HAZ montacu;

     cuenta = "43120" + aCli;
     nomcue = #7#1;
     |HAZ montacu;

     cuenta = "43110" + aCli;
     nomcue = #7#1;
     |HAZ montacu;

     cuenta = "43200" + aCli;
     nomcue = #7#1;
     |HAZ montacu;

     cuenta = "43201" + aCli;
     nomcue = #7#1;
     |HAZ montacu;
|FPRC;

|DEFBUCLE MalpesaCli;
     #7#1;
     ;
     #0#1;
     #0#2;
     ;
     NULL, MalpesaCli;
|FIN;
-------------------------------------------------------

|PROCESO Pinta;
     |PINTA 2005, #1#1;

     |SI #0#12 = "A";
     |SINO;
     |FINSI;
|FPRC;

|PROCESO DesCargaDef; |TIPO 7;
     |SI aDef1 ! "";  |DESCARGA_DEF #canempr@; aDef1  = ""; |FINSI;
     |SI aDef2 ! "";  |DESCARGA_DEF #cacuent@; aDef2  = ""; |FINSI;
     |SI aDef5 ! "";  |DESCARGA_DEF #cacueba@; aDef5  = ""; |FINSI;
     |SI aDef9 ! "";  |DESCARGA_DEF #caclipr@; aDef9  = ""; |FINSI;
     |SI aDef50 ! ""; |DESCARGA_DEF #carepre@; aDef50 = ""; |FINSI;
|FPRC;

|PROCESO CargaDef; |TIPO 0;
     aMensa = "";
     nError = 0;
     aPath = #agifa141#11; |QBF aPath;

     aLosDefs = aPath + "def/";
     aElProgr = aLosDefs + "canempre.mas";
     |XABRE "E", aElProgr, nCanal;
     |SI FSalida < 0;
         nError = 1;
         aMensa = "0000Error, Contabilidad no instalada";
         |SI nAvisar = 0;
             |MENAV aMensa;
         |FINSI;
         |ACABA;
     |FINSI;
     |XCIERRA nCanal;

     |SI aDef1 = "";
          aDef1 = aPath + "def/canempre";
          |CARGA_DEF aDef1, canempr@;
          |SI FSalida ! 0;
               nError = 1;
               aMensa = "0000Error al cargar:" + aDef1;
               |SI nAvisar = 0;
                   |MENAV aMensa;
               |FINSI;
               aDef1 = "";
          |FINSI;
     |FINSI;

     dir_fich = aPath + "fich/";
     |PATH_DAT #1 dir_fich;

     |SI aDef2 = "";
          aDef2 = aPath + "def/cacuenta";
          |CARGA_DEF aDef2, cacuent@;
          |SI FSalida ! 0;
               aMensa = "0000Error al cargar:" + aDef2;
               nError = 1;
               |SI nAvisar = 0;
                   |MENAV aMensa;
               |FINSI;
               aDef2 = "";
          |FINSI;
     |FINSI;

     |SI aDef5 = "";
          aDef5 = aPath + "def/cacuebal";
          |CARGA_DEF aDef5, cacueba@;
          |SI FSalida ! 0;
               aMensa = "0000Error al cargar:" + aDef5;
               nError = 1;
               |SI nAvisar = 0;
                   |MENAV aMensa;
               |FINSI;
               aDef5 = "";
          |FINSI;
     |FINSI;

     |SI aDef9 = "";
          aDef9 = aPath + "def/caclipro";
          |CARGA_DEF aDef9, caclipr@;
          |SI FSalida ! 0;
               nError = 1;
               aMensa = "0000Error al cargar:" + aDef9;
               |SI nAvisar = 0;
                   |MENAV aMensa;
               |FINSI;
               aDef9 = "";
          |FINSI;
     |FINSI;

     |SI aDef50 = "";
          aDef50 = aPath + "def/carepres";
          |CARGA_DEF aDef50, carepre@;
          |SI FSalida ! 0;
               nError = 1;
               aMensa = "0000Error al cargar:" + aDef50;
               |SI nAvisar = 0;
                   |MENAV aMensa;
               |FINSI;
               aDef50 = "";
          |FINSI;
     |FINSI;

     |SI aMensa ! "";
          |SI nAvisar = 0;
              |MENAV "0000Verifique el path.";
              |ERROR;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO montacu;
 |QBF cuenta
 |SI cuenta = ""; |ACABA; |FINSI;  || No hay Cuenta

 |PINTA 2249, "              ";
 |PINTA 2249,cuenta;
 |QBF cuenta;
 long = cuenta % 0;
 digito1 = 0;
 |SI long = dig4; digito1 = 1; |FINSI;
 |SI long = dig5; digito1 = 2; |FINSI;
 |SI long = dig6; digito1 = 3; |FINSI;
 |SI long = dig7; digito1 = 4; |FINSI;
 |SI long = dig8; digito1 = 5; |FINSI;
 |SI long = dig9; digito1 = 6; |FINSI;

 |SI digito1 = 0;
     mensaje1 = "0000Cuenta [" + cuenta + "] fuera de nivel.";
     |SI eaClPr = ""; |MENAV mensaje1; |FINSI;
     |ACABA;
 |FINSI;

 |DDEFECTO #2;

 #2#0  = cuenta; ||Cod.Cuenta;
 #2#1  = digito1;||Nivel de la cuenta;
 |LEE 101#2=;
 estado = FSalida;
 |SI estado = 0; |Y #0#7 = "N";
     |LIBERA #2;
     |ACABA;
 |FINSI;

 #2#2  = nomcue; ||Descripcion cuenta;
 #2#3  = "S";    ||Pase directo;
 |SI #agifa141#12 = "C";                     || (SOLO EN CONTAGEN!)
      |SI aZona ! ""; #2#61 = aZona; |FINSI;
 |FINSI;

 |SI estado ! 0;
     |HAZ defect;
     |GRABA 020#2n;
 |SINO;
     |GRABA 020#2a;
 |FINSI;
 |LIBERA #2;
|FPRC;

|PROCESO defect;
  |ABRE #5;
  uno = cuenta;   ||cod.cuenta
  |QBF uno;
  dos = uno + "000000000000";
  uno = dos % A101;
  #5#0 = uno;
  |LEE 040#5=;
  |SI FSalida = 0;
      |HAZ saca;
      |VETE findefe;
  |FINSI;
  uno = dos % A102;
  #5#0 = uno;
  |LEE 040#5=;
  |SI FSalida = 0;
      |HAZ saca;
      |VETE findefe;
  |FINSI;
  uno = dos % A103;
  #5#0 = uno;
  |LEE 040#5=;
  |SI FSalida = 0;
      |HAZ saca;
      |VETE findefe;
  |FINSI;
  uno = dos % A104;
  #5#0 = uno;
  |LEE 040#5=;
  |SI FSalida = 0;
      |HAZ saca;
      |VETE findefe;
  |SINO;
      |SI eaClPr = ""; |MENAV mensa2; |FINSI;
      |DDEFECTO #5;
      |HAZ saca;
  |FINSI;
|ET findefe;
  #2#56 = dmes;   ||Desde ejercicio;
  #2#57 = hmes;   ||Hasta ejercicio;
  clave = cuenta;
  |QBF clave;
  clave = clave + "zzzzzzzzzzzzzzzzzzzz";
  #2#54 = clave % 112;
  #2#55 = digito1;
  |CIERRA #5;
|FPRC;

|PROCESO saca;
 #2#4  = #5#1;   ||bloque - fichero cacuebal;
 #2#5  = #5#2;   ||tipo;
 #2#6  = #5#3;   ||agrupacion;
 #2#7  = #5#4;   ||epigrafe;
 #2#8  = #5#5;   ||partida;
|FPRC;

|| =======================================================================

|PROCESO clien;
     cuenta = #7#16;
     nomcue = #7#1;
     aZona = #7#3; |HAZ montacu; aZona = "";
     |SI #1#21 = "S";|O #0#12 = "C";
         |HAZ enlazacli;
     |FINSI;
|FPRC;

|PROCESO prove;
 cuenta = #8#10;
 nomcue = #8#1;
 aZona = #8#92; |HAZ montacu; aZona = "";
 |SI #1#22 = "S"; |O #0#12 = "C";
     |HAZ enlazapro;
 |FINSI;
|FPRC;

|PROCESO comis;
 cuenta = #11#56;
 nomcue = #11#1;
 |HAZ montacu;
 |HAZ EnlazaRep;
|FPRC;

|PROCESO famil;
 cuenta = #6#2;
 nomcue = #6#1;
 |HAZ montacu;
 cuenta = #6#3;
 nomcue = #6#1;
 |HAZ montacu;
|FPRC;

|DEFBUCLE familias;
 #6#1;
 ;
 #0#5;
 #0#6;
 e;
 NULL,famil;
|FIN;

|DEFBUCLE clientes;
 #7#1;
 ;
 #0#1;
 #0#2;
 e;
 NULL,clien;
|FIN;

|DEFBUCLE proveedor;
 #8#1;
 ;
 #0#3;
 #0#4;
 e;
 NULL,prove;
|FIN;

|DEFBUCLE comision;
 #11#1;
 ;
 #0#9;
 #0#10;
 e;
 NULL , comis;
|FIN;
|| -----------------------------------------------------------------

|PROCESO GrabaBasica;
|ACABA;   || Subtiquet nro. 00461.0017 Cliente: 003127


     || La intyegral/basica lo graba el dsz00097

   aAlfa = #agifa141#11; |QBF aAlfa;
   aAlfa = aAlfa - "contagen/";
   |SI FSalida = 0;
      |DBASS aPathBase;
   |SINO;
      aPathBase = aAlfa;
   |FINSI;
   |QBF aPathBase;

   aAlfa = aPathBase + "basica/fich/agim0003.dat";
   |XABRE "E", aAlfa, Handle1;
   |SI FSalida ] 0;
      aAlfa = aPathBase + "basica/def/agim0003.mas";
      |CARGA_DEF aAlfa, Referen@;
      |SI FSalida = 0;
         aAlfa = aPathBase + "basica/def/agim0016.mas"; |CARGA_DEF aAlfa, Referen1@;
         aAlfa = aPathBase + "basica/fich/";
         |PATH_DAT #100 aAlfa;
         |PATH_DAT #101 aAlfa;
         aAlfa = aPathBase + "basica/pan/"; |PATH_PAN #101 aAlfa;
         |ABRE #Referen@; |ABRE #Referen1@;
         #Referen@#0 = #9#9;
         |LEE 000#Referen@.=;
         |SI FSalida ! 0;
            |DDEFECTO #Referen@;
            #Referen@#0  = #9#9;
            #Referen@#1  = #9#1;
            #Referen@#2  = #9#2;
            #Referen@#4  = #9#3;
            #Referen@#5  = #9#4;
            #Referen@#6  = #9#6;
            #Referen@#7  = #9#5;
            #Referen@#8  = #9#7;
            #Referen@#12 = #9#24;
            #Referen@#17 = #9#8;

            |DDEFECTO #Referen1@;
            #Referen1@#0 = #9#24;
            |LEE 000#Referen1@.=;
            |SI FSalida ! 0;
                 |MENAV "0000No existe el pais en el modulo integrador";
                 |DDEFECTO #225;
                 |ABRE #225;
                 #225#0 = #Referen1@#0;
                 |LEE 000#225=;
                 |CIERRA #225;

                 #Referen1@#1 = #225#1;
                 #Referen1@#5 = #225#6;
                 |PUSHV 0501 2380;
                 |PINPA #0#Referen1@;
                 |PINDA #0#Referen1@;
                 |ENDATOS #1#Referen1@;
                 |SI FSalida = 0;
                      |GRABA 020#Referen1@.n;
                 |FINSI;
                 |POPV;
            |FINSI;

            #Referen@#13 = #Referen1@#1;
            |GRABA 020#Referen@.c;
         |SINO;
            |SI enSwRegraba = 1;
                 #Referen@#1  = #9#1;
                 #Referen@#2  = #9#2;
                 #Referen@#4  = #9#3;
                 #Referen@#5  = #9#4;
                 #Referen@#6  = #9#6;
                 #Referen@#7  = #9#5;
                 #Referen@#8  = #9#7;
                 #Referen@#12 = #9#24;
                 #Referen@#17 = #9#8;

                 |DDEFECTO #Referen1@;
                 #Referen1@#0 = #9#24;
                 |LEE 000#Referen1@.=;
                 |SI FSalida ! 0;
                      |MENAV "0000No existe el pais en el modulo integrador";
                      |DDEFECTO #225;
                      |ABRE #225;
                      #225#0 = #Referen1@#0;
                      |LEE 000#225=;
                      |CIERRA #225;

                      #Referen1@#1 = #225#1;
                      #Referen1@#5 = #225#6;
                      |PUSHV 0501 2380;
                      |PINPA #0#Referen1@;
                      |PINDA #0#Referen1@;
                      |ENDATOS #1#Referen1@;
                      |SI FSalida = 0;
                           |GRABA 020#Referen1@.n;
                      |FINSI;
                      |POPV;
                 |FINSI;

                 #Referen@#13 = #Referen1@#1;
                 |GRABA 020#Referen@.a;
            |FINSI;
         |FINSI;
         |CIERRA #Referen1@; |DESCARGA_DEF #Referen1@;
         |CIERRA #Referen@;  |DESCARGA_DEF #Referen@;
      |FINSI;
      |ACABA;
   |FINSI;

   aAlfa = aPathBase + "integral/fich/dsam0103.dat";
   |XABRE "E", aAlfa, Handle1;
   |SI FSalida ] 0;
      aAlfa = aPathBase + "integral/def/dsam0103.mas";
      |CARGA_DEF aAlfa, Referen@;
      |SI FSalida = 0;
         aAlfa = aPathBase + "integral/def/dsam0104.mas"; |CARGA_DEF aAlfa, Referen1@;
         aAlfa = aPathBase + "integral/fich/";
         |PATH_DAT #100 aAlfa; |PATH_DAT #101 aAlfa;
         |ABRE #Referen@; |ABRE #Referen1@;
         #Referen@#0 = #9#9;
         |LEE 000#Referen@.=;
         |SI FSalida ! 0;
            |DDEFECTO #Referen@;
            #Referen@#0  = #9#9;
            #Referen@#1  = #9#1;
            #Referen@#2  = #9#2;
            #Referen@#4  = #9#3;
            #Referen@#5  = #9#4;
            #Referen@#6  = #9#6;
            #Referen@#7  = #9#5;
            #Referen@#8  = #9#7;
            #Referen@#12 = #9#24;
            #Referen@#17 = #9#8;

            #Referen1@#0 = #9#24;
            |LEE 000#Referen1@.=;
            |SI FSalida ! 0; |DDEFECTO #Referen1@; |FINSI;

            #Referen@#13 = #Referen1@#1;
            |GRABA 020#Referen@.c;
         |SINO;
            |SI enSwRegraba = 1;
                 #Referen@#1  = #9#1;
                 #Referen@#2  = #9#2;
                 #Referen@#4  = #9#3;
                 #Referen@#5  = #9#4;
                 #Referen@#6  = #9#6;
                 #Referen@#7  = #9#5;
                 #Referen@#8  = #9#7;
                 #Referen@#12 = #9#24;
                 #Referen@#17 = #9#8;

                 #Referen1@#0 = #9#24;
                 |LEE 000#Referen1@.=;
                 |SI FSalida ! 0; |DDEFECTO #Referen1@; |FINSI;

                 #Referen@#13 = #Referen1@#1;
                 |GRABA 020#Referen@.a;
            |FINSI;
         |FINSI;
         |CIERRA #Referen1@; |DESCARGA_DEF #Referen1@;
         |CIERRA #Referen@;  |DESCARGA_DEF #Referen@;
      |FINSI;
   |FINSI;
|FPRC;

|PROCESO enlazacli;
 #9#23 = "C";
 #9#10 = #7#16;
 #9#11 = digito1;
 |LEE 101#9=;
 estadc = FSalida;

 |SI estadc = 0; |Y #0#7 = "N";
     |LIBERA #9;
     |ACABA;
 |FINSI;

     #9#0 = #7#0;
     #9#23 = "C";
     #9#1 = #7#1;
     #9#2 = #7#8;
     #9#3 = #7#181;   ||CP 1
     #9#4 = #7#182;   ||CP 2
     #9#25= #7#183;   ||Codigo alfa

     aPais = #7#205;
     aLong = aPais % A0;
     nLong = aLong;
     |SI nLong = 4;
          nPais = #7#205;
          |SI nPais ! 0;
               aPais = ("000" + nPais) % -103;
          |FINSI;
     |FINSI;
     #9#24= aPais;   || pais (en DSCOMER9 es de 4 y en CONTAGEN es de 3)!!!

     #9#5 = #7#10;
     #9#6 = #7#9;
     #9#7 = #7#17;
     #9#8 = #7#18;
     #9#9 = #7#15;
     #9#10 = #7#16;
     #9#11= digito1;||digito control;
  || #9#12= grupo iva;
     #9#13= #7#29;||nombre del banco;
     #9#14= #7#34;||direccion del banco;
  || #9#15= cod1 banco;
  || #9#16= cod2 banco;
     #9#17= #7#30;||cta corriente;
     alfa3 = #7#32 + #7#33 + #7#31; |QBT alfa3;
     #9#18= alfa3;  ||c.c.c;
     #9#19= #7#162;  ||poblacion banco;
     #9#20= #7#20;   ||Forma de Pago..
     #9#28= #7#203;  ||Banco remesas
     |SI #agifa141#12 = "C";
          #9#33= #7#3;    ||Zona.. (SOLO EN CONTAGEN!)
     |FINSI;

     aAlfa = "|NC"; nCalc = #9#aAlfa#;

     |SI #agifa141#12 = "C";  ||Contagen
          |SI nCalc > 29;
             #9#29= #7#32;   ||Entidad
             #9#30= #7#33;   ||Oficina
             #9#31= #7#31;   ||DC
             #9#32= #7#30;   ||Nº Cuenta
          |FINSI;
          |SI nCalc > 33;
             #9#34 = #7#22; ||Dia fijo 1
             #9#35 = #7#23; ||Dia fijo 2
             #9#36 = #7#24; ||Dia fijo 3
          |FINSI;
          |SI nCalc > 43;
             #9#44= #7#11;
             #9#45= #7#184;
             #9#46= #7#185;
             #9#47= #7#13;
             #9#48= #7#12;
             #9#49= #7#27;
             #9#50= #7#186;
          |FINSI;

          |SI nCalc ] 38;
               ||#9#38 = ||blz
               ||#9#39 = ||swift
               #9#40 = #7#215; ||iban
               #9#41 = #7#216; ||bic
          |FINSI;
          |SI nCalc ] 53;
               #9#53 = #7#211; ||Cp extranjero
          |FINSI;
     |SINO;
          |SI nCalc ] 29;
               #9#29 = #7#216; ||bic
               #9#30 = #7#215; ||iban
               #9#37 = #7#217; ||f.sepa
          |FINSI;
     |FINSI;

     aEntidad  = #7#32;
     aSucursal = #7#33;
     aDC       = #7#31;
     aCuenta   = #7#30;

     |SI estadc ! 0;
         |GRABA 020#9n;
     |SINO;
         |GRABA 020#9a;
     |FINSI;
     |LIBERA #9;
     |HAZ GrabaBasica;
|FPRC;

|PROCESO enlazapro;
  #9#23 = "P";
  #9#10 = #8#10;||CONTABILIDAD;
  #9#11 = digito1;
  |LEE 101#9=;
  estadc = FSalida;

  |SI estadc = 0; |Y #0#7 = "N";
      |LIBERA #9;
      |ACABA;
  |FINSI;

  #9#0 = #8#0;
  #9#23 = "P";
  #9#1  = #8#1;||RAZ SOCIAL,
  #9#2  = #8#3;||DIRECC;
  #9#3  = #8#74;  ||CP 1
  #9#4  = #8#75;  ||CP 2
  #9#25 = #8#76;   ||Codigo alfa

  aPais = #8#89;
  aLong = aPais % A0;
  nLong = aLong;
  |SI nLong = 4;
     nPais = #8#89;
     |SI nPais ! 0;
          aPais = ("000" + nPais) % -103;
     |FINSI;
  |FINSI;
  #9#24= aPais;   || pais (en DSCOMER9 es de 4 y en CONTAGEN es de 3)!!!

  #9#5  = #8#5;||PROVINCIA;
  #9#6  = #8#4;||POBLACION;
  #9#7  = #8#6;||TELF;
  #9#8  = #8#7;||FAX;
  #9#9  = #8#9;||CIF;
  #9#10 = #8#10;||CONTABILIDAD;
  #9#11 = digito1;
  #9#13 = #8#11;||nombre del banco;
  #9#14 = #8#16;||direccion;
  #9#17 = #8#12;||cuenta;
  alfa3 = #8#14 + #8#15 + #8#13; |QBT alfa3;
  #9#18 = alfa3; ||ccc;
  #9#19 = #8#57;||poblacion banco;
  #9#20 = #8#17;||Forma de pago...
  #9#28 = #8#98;||Banco asociado
  |SI #agifa141#12 = "C";
       #9#33 = #8#92;||Zona.. (SOLO EN CONTAGEN!)
  |FINSI;

     aAlfa = "|NC"; nCalc = #9#aAlfa#;

     |SI #agifa141#12 = "C";  ||Contagen
          |SI nCalc > 29;
             #9#29= #8#14;   ||Entidad
             #9#30= #8#15;   ||Oficina
             #9#31= #8#13;   ||DC
             #9#32= #8#12;   ||Nº Cuenta
             #9#34 = #8#19; ||Dia fijo 1
             #9#35 = #8#20; ||Dia fijo 2
             #9#36 = #8#21; ||Dia fijo 3
             #9#37 = #8#99;
             #9#38 = #8#94;
             #9#39 = #8#95;
             |SI nCalc > 40;
                  #9#40 = #8#96; ||IBAN
                  #9#41 = #8#97; ||BIC
             |FINSI;
          |FINSI;
          |SI nCalc ] 43;
              aAlfa1 = #8#100; |QBF aAlfa1;
              |SI aAlfa1 ! ""; #9#43 = #8#100; |FINSI;
              |SI nCalc > 43;
                  #9#44= #8#101;
                  #9#45= #8#102;
                  #9#46= #8#103;
                  #9#47= #8#104;
                  #9#48= #8#105;
                  #9#49= #8#106;
                  #9#50= #8#107;
              |FINSI;
          |FINSI;

          |SI nCalc ] 38;
               #9#38 = #8#94; ||blz
               #9#39 = #8#95; ||swift
               #9#40 = #8#96; ||iban
               #9#41 = #8#97; ||bic
          |FINSI;
          |SI nCalc ] 53;
               #9#53 = #8#108; ||Cp extranjero
          |FINSI;
     |SINO;
          |SI nCalc ] 29;
               #9#29 = #8#97; ||bic
               #9#30 = #8#96; ||iban
               ||#9#37 =        ||f.sepa
          |FINSI;
     |FINSI;

     aEntidad  = #8#14;
     aSucursal = #8#15;
     aDC       = #8#13;
     aCuenta   = #8#12;
     |SI estadc ! 0;
         |GRABA 020#9n;
     |SINO;
         |GRABA 020#9a;
     |FINSI;
     |LIBERA #9;
     |HAZ GrabaBasica;
|FPRC;

|PROCESO EnlazaRep;

  #50#0 = #11#0;
  |LEE 101#50=;
  estadc = FSalida;

  |SI estadc = 0; |Y #0#7 = "N";
      |LIBERA #50;
      |ACABA;
  |FINSI;

  #50#0  = #11#0;      || Codigo
  #50#1  = #11#1;      || Nombre
  #50#2  = #11#2;      || Direccion
  #50#3  = #11#58;     || CP 1
  #50#4  = #11#59;     || CP 2
  #50#5  = #11#4;      || Poblacion
  #50#6  = #11#3;      || Provincia
  #50#7  = #11#5;      || DNI o CIF
  #50#8  = #11#8;      || Telefono
  #50#9  = #11#10;     || Fax
  #50#10 = #11#62;     || E-Mail
  #50#11 = #11#61;     || Movil

  |SI estadc ! 0;
      |GRABA 020#50n;
  |SINO;
      |GRABA 020#50a;
  |FINSI;
  |LIBERA #50;
|FPRC;

|| -----------------------------------------------
|PROCESO leempresa; |TIPO 0;
 |HAZ lectura;
 |SI swerror = 1;
     |SI eaClPr = ""; |MENAV "      No existe Empresa Contable !!!"; |FINSI;
     |ERROR;
 |FINSI;
|FPRC;

|PROCESO lectura;
 swerror = 0;
 |ABRE #1;
 #1#2 = #0#0;
 #1#3 = #0#8;
 |LEE 040#1=;
 |SI FSalida ! 0;
     swerror = 1;
 |FINSI;
 |CIERRA #1;
|FPRC;

|PROCESO acceder;|TIPO 0;
 |HAZ lectura;
 |SI swerror = 1;
     |MENAV "    NO EXISTE EMPRESA CONTABLE. PROCESO ABORTADO ... ";
     |ACABA;
 |FINSI;

 empresa = #0#0;
 empresa = "00000" + empresa;
 empresa = empresa % -105;
 anyo = #0#8;
 anyo = "0" + anyo;
 anyo = anyo % -101;
 empresa = empresa + anyo;

 path = dir_fich + empresa +  "/";

   dig1 = #1#11;
   dig3 = #1#13;
   dig4 = #1#14;
   dig5 = #1#15;
   dig6 = #1#16;
   dig7 = #1#17;
   dig8 = #1#18;
   dig9 = #1#19;
   mes = #1#11;
   |HAZ mes;
   dmes = litmes;
   mes = #1#12;
   |HAZ mes;
   hmes = litmes;

 |PATH_DAT #02 path;
 |PATH_DAT #05 path;
 |PATH_DAT #09 path;
 |PATH_DAT #50 path;

 aAlfa = path + Usuario;
 |XABRE "W", aAlfa, nHandle;
 |SI FSalida ] 0;
     |XCIERRA nHandle;
     |FBORRA aAlfa;
 |SINO;
     swerror = 1;
 |FINSI;
|FPRC;

|PROCESO Defecto; |TIPO 7;
     |DBASS alfa1; |QBF alfa1;
     |SI #agifa141#12 = "A";  alfa1 = alfa1 + "agicont/";   |FINSI;
     |SI #agifa141#12 = "C";  alfa1 = alfa1 + "contagen/";  |FINSI;
     #agifa141#11 = alfa1;
     |PINTA #agifa141#11;
|FPRC;

|PROCESO AntesVersion; |TIPO 7;

     nConta = 0;
     |DBASS alfa1; |QBF alfa1;

     aPath = alfa1 + "agicont/";
     aLosDefs = aPath + "def/";
     aElProgr = aLosDefs + "canempre.mas";
     |XABRE "E", aElProgr, nCanal;
     |SI FSalida ] 0;
         nConta = nConta + 1;
         #agifa141#12 = "A";
     |FINSI;
     |XCIERRA nCanal;

     aPath = alfa1 + "contagen/";
     aLosDefs = aPath + "def/";
     aElProgr = aLosDefs + "canempre.mas";
     |XABRE "E", aElProgr, nCanal;
     |SI FSalida ] 0;
         nConta = nConta + 1;
         #agifa141#12 = "C";
     |FINSI;
     |XCIERRA nCanal;

     |SI nConta = 0; |O nConta = 2;
         |C_M #agifa141#11,1,"S";
         |C_M #agifa141#12,1,"S";
     |SINO;
         |C_M #agifa141#11,1,"N";
         |C_M #agifa141#12,1,"N";
     |FINSI;
     |PINTA #agifa141#12;
|FPRC;
|| **************************************************************************
|PROGRAMA;
     |ABRE #142; |LEE 000#142p; |CIERRA #142;

     |ABRE #0;
     |SI PARAMETRO = "";
        |LEE 101#0p;
        |SI FSalida ! 0;
            |DBASS alfa1; |QBF alfa1;
            |SI #agifa141#12 = "A";  alfa1 = alfa1 + "agicont/";   |FINSI;
            |SI #agifa141#12 = "C";  alfa1 = alfa1 + "contagen/";  |FINSI;
            #agifa141#11 = alfa1;
            |GRABA 020#0b;
        |FINSI;
     |FINSI;

     |SI #142#182 = "N";
       |DFICE aEmp;
       aEmp = aEmp % -205;
       |C_M #0#0, 1, "N";
       #0#0 = aEmp;
     |FINSI;

     |SI PARAMETRO = "";
        |SI eaClPr = "";
            |CLS;
            |CABEZA "Exportacion Cuentas";
            |PINPA #0#0;
            |PINDA #0#0;
            |ENDATOS #1#0;
        |SINO;
            nAvisar = 1;
            #0#1 = ""; #0#2 = "";
            #0#3 = ""; #0#4 = "";
            #0#5 = ""; #0#6 = "";
            #0#9 = ""; #0#10 = "";
            |SI eaClPr = "P"; #0#3 = eaCodigoNum; #0#4 = eaCodigoNum; |FINSI;
            |SI eaClPr = "C"; #0#1 = eaCodigoNum; #0#2 = eaCodigoNum; |FINSI;

            || pone el path+empresa de parametros
            aAlfa = #142#151;
            aAlfa = aAlfa - "agicont";
            |SI FSalida > 0; #0#12 = "A"; |SINO; #0#12 = "C"; |FINSI;
            aAlfa = ("000000" + #142#152) % -205; #0#0 = aAlfa;
            aAlfa = ("000000" + #142#152) % -101; #0#8 = aAlfa;
            #0#11 = #142#151;
            FSalida = 0;
            nGraba = 1;
        |FINSI;
     |SINO;
        nGraba = 1;
        |SI PARAMETRO = "vipei";         || Lo llama dsz00071
               |O PARAMETRO = "cepsaxx"; ||  ""      dsz00134
             nAvisar = 1;
             |LEE 020#0p; ||Lee la ficha
        |SINO;
             |DBASS aAlfa; |QBF aAlfa; aAlfa = aAlfa + "contagen/";
             #0#11 = aAlfa;
             aAlfa = PARAMETRO; |QBF aAlfa;
             aAlfa = aAlfa - ",";
             |SI FSalida ! 0;
                nCalc = 99 + ((FSalida / 100) ! 0);
                aAlfa = aAlfa % nCalc; #0#0 = aAlfa;
                nCalc = FSalida + 198;
                aAlfa = PARAMETRO; |QBF aAlfa;
                aAlfa = aAlfa % nCalc; #0#8 = aAlfa;
                FSalida = 0;
             |SINO;
                FSalida = 1;
             |FINSI;
        |FINSI;
     |FINSI;

     |SI FSalida = 0;
       |SI nGraba = 0; |GRABA 020#0a; |FINSI;

       |HAZ PonBarra;      || con esto aseguramos las referencias
       |HAZ CargaDef;      ||/si se le daba ESC en el primer campo
                           ||/no se cargaban (R.PRIEGO - 20.10.2004)

       |SI nError = 1; |HAZ DesCargaDef; |ACABA; |FINSI;

       |HAZ acceder;
       |SI swerror = 1; |HAZ DesCargaDef; |ACABA; |FINSI;

       |SI PARAMETRO ! ""; |Y PARAMETRO ! "cepsaxx";
           #0#7 = "S";
       |FINSI;


       |ABRE #2;
       |ABRE #9;
       |ABRE #50;
       |SI eaClPr ! "";
           |BUCLE clientes;
           |BUCLE proveedor;
       |SINO;
           |BUCLE familias;
           |BUCLE clientes;
           |BUCLE proveedor;
           |BUCLE comision;
       |FINSI;

       |HAZ EsMalpesa;
       |SI FSalida = 0; |BUCLE MalpesaCli; |FINSI;

       |CIERRA #2;
       |CIERRA #9;
       |CIERRA #50;

       |LIBERA #0;
     |FINSI;
     |HAZ DesCargaDef;

     |CIERRA #0;
|FPRO;

|PROCESO mes;
 |SI mes = 01;litmes = "Enero";     |FINSI;
 |SI mes = 02;litmes = "Febrero";   |FINSI;
 |SI mes = 03;litmes = "Marzo";     |FINSI;
 |SI mes = 04;litmes = "Abril";     |FINSI;
 |SI mes = 05;litmes = "Mayo";      |FINSI;
 |SI mes = 06;litmes = "Junio";     |FINSI;
 |SI mes = 07;litmes = "Julio";     |FINSI;
 |SI mes = 08;litmes = "Agosto";    |FINSI;
 |SI mes = 09;litmes = "Septiembre";|FINSI;
 |SI mes = 10;litmes = "Octubre";   |FINSI;
 |SI mes = 11;litmes = "Noviembre"; |FINSI;
 |SI mes = 12;litmes = "Diciembre"; |FINSI;
|FPRC;

|PROCESO PonBarra; |TIPO 0;
     aAlfa1 = #0Campo;
     |QBF aAlfa1;
     aAlfa2 = aAlfa1 % -101;
     |SI aAlfa2 ! "/";|Y aAlfa2 ! "\";
          #0Campo = aAlfa1 + "/";
          |PINTA #0Campo;
     |FINSI;
|FPRC;
