|FICHEROS;
     pcegz075 #0;
     dsm00003 #3;
     agifa024 #24;
     agifa025 #25;
     agifm001 #101;
     agifm002 #102;
     dsm00225 #225;
|FIN;

|VARIABLES;
     &eaFic = "";
     &eaEntidad = "";
     &eaSucursal = "";
     &eaCuenta = "";
     &eaDC = "";
     aCp1 = "";
     aCp2 = "";
     aCp = "";
     nPostalAddress = 0;
     nBanco = 0;
     nEmail = 0;
     nTasa = 0;
     nHay = 0;

     ||...
     &eaAlfa = "";
     RETORNO = "";
     nReg = 0;
     aSoloUno = "";
     aFicheros = "";
     nError = 0;
     nPos = 0;
     nRem = 0;
     aAlfa = "";
     aPath = "";
     aCar = "";
     nHandle = 0;
     aFic = "";
     aVal = "";
     nNivel = 0;
     aTmp = "";
     {100}Matriz = "";
     aLon = "";
     aTag = "";
     aIniTag = "N";
     aFinTag = "N";
     aIniPro = "N";
     aFinPro = "N";
     aFinTagVacio = "N";
     aIniAtri = "N";
     aTagAtri = "";
     aResul = "";
     aLetra = "";
     i = 0;
     aDato = "";
     nPosD = 0;
     nPosI = 0;
     aIniComi = "";
     aComilla = "";
|FIN;

|PROCESO GrabaCli;
     #225#0 = #24#205;
     |LEE 000#225=;
     |SI FSalida ! 0; |DDEFECTO #225; |FINSI;
     #24#14 = #225#1;
     #24#2 = #24#1;
     #24#16 = "430" + #24#0;
     |GRABA 000#24n;     || sin errores, si existe no se graba
     |SI FSalida = 0;
          #101#0 = #24#3;
          |LEE 000#101=; || Zona
          |SI FSalida ! 0; |DDEFECTO #101; |FINSI;

          #102#0 = #24#189;
          |LEE 000#102=; ||Ruta
          |SI FSalida ! 0; |DDEFECTO #102; |FINSI;

          #25#0 = #24#25;
          |LEE 000#25=;
          |SI FSalida ! 0; |DDEFECTO #25; |FINSI;

          |DDEFECTO #3;
          #3#0 = #24#0;
          #3#1 = 1;
          |LEE 101#3=;
          |SI FSalida ! 0; |GRABA 021#3b; |FINSI;
          #3#2 = #24#8;
          #3#3 = #24#9;
          #3#4 = #24#181;
          #3#5 = #24#182;
          #3#6 = #24#183;
          #3#7 = #24#10;
          #3#8 = #24#14;
          #3#10 = #24#2;
          #3#11 = #24#3;
          #3#12 = #101#1;
          #3#13 = #24#17;
          #3#14 = #24#25;
          #3#15 = #25#1;
          #3#16 = #24#189;
          #3#17 = #102#1;
          #3#19 = #24#205;
          #3#20 = #24#196;
          #3#21 = #24#18;
          #3#22 = #24#36;
          #3#23 = #24#211;
          #3#24 = #24#197;
          |GRABA 021#3a;
          |LIBERA #3;
     |FINSI;
|FPRC;

|PROCESO ProcesaTagPop;
     ||aAlfa = "0000Push: " + aTag + " => " + aVal; |MENAV aAlfa;

     |SI aTag = "Id"; #24#0 = aVal; |FINSI;
     |SI aTag = "Name1";
          eaAlfa = aVal; |HAZ QuitaRarosImp; aVal = eaAlfa;
          #24#1 = aVal;
     |FINSI;
     |SI aTag = "Country"; #24#205 = aVal; |FINSI;
     |SI aTag = "VATRegNo"; aVal = aVal % 3; #24#15 = aVal; |FINSI;
     |SI nPostalAddress = 1;
          |SI aTag = "Street";
               eaAlfa = aVal; |HAZ QuitaRarosImp; aVal = eaAlfa;
               #24#5 = aVal; #24#8 = aVal; #24#11 = aVal;
          |FINSI;
          |SI aTag = "Zip";
               aCp1 = aVal % 102;
               aCp2 = aVal % 303;
               aCp = aCp1 + aCp2;
               #24#178 = aCp1; #24#181 = aCp1; #24#184 = aCp1;
               #24#179 = aCp2; #24#182 = aCp1; #24#185 = aCp1;
               #24#180 = aCp; #24#183 = aCp; #24#186 = aCp;
          |FINSI;
          |SI aTag = "City";
               eaAlfa = aVal; |HAZ QuitaRarosImp; aVal = eaAlfa;
               #24#6 = aVal; #24#9 = aVal; #24#12 = aVal;
          |FINSI;
          |SI aTag = "State";
               eaAlfa = aVal; |HAZ QuitaRarosImp; aVal = eaAlfa;
               #24#7 = aVal; #24#10 = aVal; #24#13 = aVal;
          |FINSI;
     |FINSI;

     |SI nBanco = 1;
          |SI aTag = "Name";
               eaAlfa = aVal; |HAZ QuitaRarosImp; aVal = eaAlfa;
               #24#29 = aVal;
          |FINSI;
          |SI aTag = "IBAN";
               #24#215 = aVal;

               #24#32 = aVal % 504;
               #24#33 = aVal % 904;
               #24#30 = aVal % 1510;

               eaEntidad = #24#32; |QBF eaEntidad; eaEntidad = eaEntidad % 104;
               eaSucursal = #24#33; |QBF eaSucursal; eaSucursal = eaSucursal % 104;
               eaCuenta = #24#30; |QBF eaCuenta; eaCuenta = eaCuenta % 110;
               eaDC = "";
               |HAZ CalculoDC;
               #24#31 = eaDC;
          |FINSI;
     |FINSI;
     |SI aTag = "Banks"; nBanco = 0; |FINSI;

     |SI nTasa = 1;
          |SI aTag = "Name";
               |SI aVal = "Inland+Zusatz";
                    #24#47 = "S";
               |SINO;
                    #24#47 = "N";
               |FINSI;
          |FINSI;
     |FINSI;

     |SI aTag = "TaxDefinition"; nTasa = 0; |FINSI;

     |SI aTag = "Customer";
          |HAZ GrabaCli;
     |FINSI;
|FPRC;

|PROCESO ProcesaTagPush;
     ||aAlfa = "0000Push: " + aTag + " => " + aTagAtri; |MENAV aAlfa;

     |SI aTag = "Customer";
          nEmail = 0;
          nPostalAddress = 0;
          nBanco = 0;
          nHay = 1;
          |DDEFECTO #24;
     |FINSI;
     |SI aTag = "Email"; |Y nEmail = 0;
          nEmail = nEmail + 1;
          aAlfa = "address"; |HAZ GetAtri; #24#197 = aAlfa;
     |FINSI;
     |SI aTag = "PostalAddress";
          nPostalAddress = nPostalAddress + 1;
     |FINSI;
     |SI aTag = "Banks"; nBanco = 1; |FINSI;
     |SI aTag = "TaxDefinition"; nTasa = 1; |FINSI;
|FPRC;

|PROCESO ProcesaTagVacio;
|FPRC;

|PROCESO ProcesaPrologo;
|FPRC;

|PROCESO ProcesaRem;
|FPRC;

|PROCESO GetAtri;
     aResul = "";
     aAlfa = aTagAtri - aAlfa;
     |SI FSalida > 0;
          aResul = ("00000" + FSalida) % -105;
          nPosI = (A\(aResul % 103));
          nPosD = (A\(aResul % 402));
          nPos = nPosI + nPosD;
          aDato = aTagAtri;

          || si nPos > 100, FALLA: aTagAtri % nPos;
          |PARA; |SI nPos > 0; |HACIENDO;
               nPos = nPos - 90;
               |SI nPos < 0;
                    nPos = nPos + 90;
                    aDato = aDato % nPos;
                    |SAL;
               |SINO;
                    aDato = aDato % 91;
               |FINSI;
          |SIGUE;
          || busca la parte entre ""
          aComilla = &34;
          aIniComi = "N";
          aResul = "";
          aLon = aDato % A0;
          |PARA i = 1; |SI i [ aLon; |HACIENDO i = i + 1;
               nPos = i * 100 + 1;
               aLetra = aDato % nPos;
               |SI aLetra = aComilla;
                    |SI aIniComi = "S";
                         |SAL;
                    |SINO;
                         aIniComi = "S";
                    |FINSI;
               |SINO;
                    |SI aIniComi = "S";
                         aResul = aResul + aLetra;
                    |FINSI;
               |FINSI;
          |SIGUE;
     |FINSI;
    aAlfa = aResul;
|FPRC;

|PROCESO QuitaBlancos;   || a izquierda y derecha
     aResul = "";
     aLon = aAlfa % A0;
     |PARA i = 1; |SI i [ aLon; |HACIENDO i = i + 1;
          nPos = i * 100 + 1;
          aLetra = aAlfa % nPos;
          |SI aLetra ! " "; |O aResul ! "";
               aResul = aResul + aLetra;
          |FINSI;
     |SIGUE;
     aAlfa = aResul;
     |QBF aAlfa;
|FPRC;

|PROCESO PopTag;
     nNivel = nNivel - 1;
     IMatriz = Matriz1<-;
     IMatriz = IMatriz + nNivel;
     FSalida = Matriz;
|FPRC;

|PROCESO PushTag;
     IMatriz = Matriz1<-;
     IMatriz = IMatriz + nNivel;
     Matriz = aTag;
     nNivel = nNivel + 1;
|FPRC;

|PROCESO PinLinea;
     nReg = nReg + 1;
     aAlfa = "Linea: " + nReg;
     |PINTA 1206, aAlfa;
|FPRC;

|PROCESO Caracter;
     |SI aCar = RETORNO; |HAZ PinLinea; |FINSI;

     |SI aCar < " "; |O nError ! 0; |ACABA; |FINSI;

     |SI nRem > 0;
          |SI nRem < 3;
               |SI aCar = "-";
                    nRem = nRem + 1;
               |SINO;
                    aAlfa = "0000ERROR en XML: " + aTag;
                    |MENAV aAlfa;
                    nError = 1;
               |FINSI;
          |SINO;
               |SI aCar = ">"; || Un comentario no puede tener el caracter '>'
                    aAlfa = aTag % -102;
                    |SI aAlfa = "--";
                         aLon = aTag % A0;
                         nPos = (aLon - 2) + 100;
                         aTag = aTag % nPos;
                         |HAZ ProcesaRem;
                    |SINO;
                         aAlfa = "0000ERROR en XML: " + aTag;
                         |MENAV aAlfa;
                         nError = 1;
                    |FINSI;
                    aTag = ""; nRem = 0;
               |SINO;
                    aLon = aTag % A0;
                    |SI aLon < 250; aTag = aTag + aCar; |FINSI;
               |FINSI;
          |FINSI;
          |ACABA;
     |FINSI;

     |SI aCar = "<";
          aIniTag = "S"; aTag = "";
     |SINO;
          |SI aIniTag = "S";
               |SI aCar = "/"; |Y aIniAtri = "N";
                    |SI aTag = "";
                         aFinTag = "S";
                    |SINO;
                         aFinTagVacio = "S";
                    |FINSI;
               |SINO;
                    |SI aCar = "!"; |Y aIniAtri = "N";
                         |SI aTag = "";
                              nRem = 1;
                         |SINO;
                              aAlfa = "0000ERROR en XML: " + aTag;
                              |MENAV aAlfa;
                              nError = 1;
                         |FINSI;
                         |ACABA;
                    |FINSI;
                    |SI aCar = "?"; |Y aIniAtri = "N";
                         |SI aIniPro = "S";
                              aFinPro = "S";
                              aIniPro = "N";
                         |SINO;
                              |SI aFinPro = "S";
                                   aAlfa = "0000ERROR en XML: " + aTag;
                                   |MENAV aAlfa;
                                   nError = 1;
                              |SINO;
                                   aIniPro = "S"; aTag = "";
                              |FINSI;
                         |FINSI;
                    |SINO;
                         |SI aCar = ">";
                              aAlfa = aTag; |HAZ QuitaBlancos; aTag = aAlfa;
                              aAlfa = aTagAtri; |HAZ QuitaBlancos; aTagAtri = aAlfa;
                              |SI aFinPro = "S";
                                   |HAZ ProcesaPrologo;
                                   aFinPro = "N";
                              |SINO;
                                   |SI aFinTag = "S";
                                        |HAZ PopTag;
                                        |SI aTag ! FSalida;
                                             aAlfa = "0000ERROR en XML: <" + FSalida + "> " + aVal;
                                             |MENAV aAlfa;
                                             nError = 1;
                                        |SINO;
                                             |HAZ ProcesaTagPop;
                                        |FINSI;
                                   |SINO;
                                        |SI aFinTagVacio = "S";
                                             |HAZ ProcesaTagVacio;
                                             aFinTagVacio = "N";
                                        |SINO;
                                             |HAZ PushTag;
                                             |HAZ ProcesaTagPush;
                                        |FINSI;
                                   |FINSI;
                              |FINSI;
                              aIniTag = "N"; aFinTag = "N"; aTag = ""; aVal = "";
                              aIniAtri = "N"; aTagAtri = "";
                         |SINO;
                              aLon = aTag % A0;
                              |SI aLon < 250;
                                   |SI aCar = " "; |Y aIniAtri = "N";
                                        aIniAtri = "S";
                                   |SINO;
                                        |SI aIniAtri = "S";
                                             aTagAtri = aTagAtri + aCar;
                                        |SINO;
                                             aTag = aTag + aCar;
                                        |FINSI;
                                   |FINSI;
                              |FINSI;
                         |FINSI;
                    |FINSI;
               |FINSI;
          |SINO;
               aLon = aVal % A0;
               |SI aLon < 250; aVal = aVal + aCar; |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO XML;
     RETORNO = &10;
     nReg = 0;
     nRem = 0;
     nNivel = 0;
     aVal = "";
     aTmp = "";
     aTag = "";
     aIniTag = "N";
     aFinTag = "N";
     aIniPro = "N";
     aFinPro = "N";
     aFinTagVacio = "N";
     aIniAtri = "N";
     aTagAtri = "";

     |XLEE nHandle, 250, aTmp;
     |HAZ PinLinea;
     |PARA; |SI FSalida > 0; |HACIENDO;
          |PARA; |SI aTmp ! ""; |HACIENDO;
               aCar = aTmp % 101;
               |SI aTmp = aCar;
                    aTmp = ""; ||con un caracter falla: "aTmp = aTmp % 2"
               |SINO;
                    aTmp = aTmp % 2;
               |FINSI;
               |HAZ Caracter;
               |SI nError ! 0; |SAL; |FINSI;
          |SIGUE;
          |XLEE nHandle, 250, aTmp;
          |SI nError ! 0; |SAL; |FINSI;
     |SIGUE;

     |SI nHay = 1;  |HAZ GrabaCli; |FINSI;
|FPRC;

|PROCESO T3; |TIPO 3;
     |ABRE #225;
     |ABRE #101;
     |ABRE #102;
     |ABRE #25;
     |ABRE #24;
     |ABRE #3;

     RETORNO = &10;
     aSoloUno = "S";
     aAlfa = #0#0; |QBF aAlfa;

     |SI aSoloUno = "S";           || #0#0 es fichero
          |XABRE "EC", aAlfa, 0;
          |SI FSalida ] 0;
               eaFic = aAlfa; |HAZ Utf82Ansi; aAlfa = eaFic;

               |DFICE aFic;
               aFic = aFic + Usuario;
               |RECIBE_FICHERO aAlfa, aFic;
               |SI FSalida ! 0;
                    |MENAV "0000Error al copiar fichero al servidor...";
               |SINO;
                    |XABRE "B", aFic, nHandle;
                    |SI FSalida ] 0;
                         |HAZ XML;
                         |XCIERRA nHandle;
                         |FBORRA aFic;
                    |FINSI;
               |FINSI;

               |RFBORRA eaFic;
          |SINO;
               |MENAV "0000No existe fichero...";
          |FINSI;
     |SINO;                   || #0#0 es directorio
          aCar = #0#0 % -101;
          |SI aCar ! "/"; |Y aCar ! "\";
               aAlfa = aAlfa + "/";
          |FINSI;
          aAlfa = aAlfa + "*.xml";
          aFicheros = aAlfa;
          |R_PDIR aFicheros;
          |PARA; |SI FSalida = 0; |HACIENDO;
               aAlfa  = aFicheros;
               |PINTA 1201, aAlfa;

               |DFICE aFic;
               aFic = aFic + Usuario;
               |RECIBE_FICHERO aAlfa, aFic;
               |SI FSalida ! 0;
                    |MENAV "0000Error al copiar fichero al servidor...";
               |SINO;
                    |XABRE "B", aFic, nHandle;
                    |SI FSalida ] 0;
                         |HAZ XML;
                         |XCIERRA nHandle;
                         |FBORRA aFic;
                    |FINSI;
               |FINSI;

               |SI nError ! 0; |SAL; |FINSI;
               |R_SDIR aFicheros;
          |SIGUE;
     |FINSI;

     |CIERRA #3;
     |CIERRA #24;
     |CIERRA #25;
     |CIERRA #101;
     |CIERRA #102;
     |CIERRA #225;
|FPRC;

|PROCESO Fichero; |TIPO 0;
     |SI FSalida = 10;
          aAlfa = "F*.xml";
          |BROWSE aAlfa;

          aAlfa = aAlfa % 2; |QBF aAlfa;
          |SI aAlfa = "F"; aAlfa = ""; |FINSI;

          |QBF aAlfa;
          |SI aAlfa = "";
               |ERROR;
          |SINO;
               #0#0 = aAlfa; |PINTA #0#0;
               |ERROR;
          |FINSI;
     |FINSI;
|FPRC;

|PROGRAMA;
     |ABRE #0;
     |LEE 101#0p;
     |SI FSalida ! 0; |GRABA 020#0b; |FINSI;
     |PINPA #0#0;
     |PINDA #0#0;
     |ENDATOS #1#0;
     |SI FSalida = 0; |GRABA 020#0a; |FINSI;
     |CIERRA #0;
|FPRO;
