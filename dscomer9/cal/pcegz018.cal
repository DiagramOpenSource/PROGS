|FICHEROS;
     pcegz018 #0;
     pcegw001 #1; ||Tmp reservadas
     pcegw002 #2; ||Informe reservado
     pcegm004 #4;
     pcegm005 #5;
     pcegw006 #6; ||Tmp pedidos tratados
     pcegw009 #9; ||Tmp pedidos tratados  por pcegz029
     agifa010 #10;
     pcegm012 #12;  || Aux. Articulo
     pcegm014 #14; ||Log
     pcegm016 #16; ||Relacion albaranes
     agifa017 #17;
     agifa019 #19;
     dsm00020 #20;
     agifa024 #24;
     agifa025 #25;
     pcegm042 #42;
     agifa059 #59;
     agifa071 #71;
     agifa134 #134;
     agifa142 #142;
     pcegm005@;
     agifa071@;
     pcegw006@;
|FIN;

|INCLUYE i_varart;

|VARIABLES;
     &enSwRep = 0;  || Boton Cerrar Reparacion (Ver Datos)
     &enSwAdj = 0;  || Boton Adj. Stock/Servir (Ver Datos)
     &aDivisa = "";
     &aMoneda = "";
     &Tempo = "";
     &enPedido = 0;
     &enAlba = 0;
     &eaConPanta = "";
     &enSwMenu = 0;
     &eaImpresora = "";
     &eaImprime = "";
     &ser_alb = "";
     &num_alb = 0;
     &ser_fac = "";
     &num_fac = 0;
     &reimp = "";
     &ser_ped = "";
     &num_ped = 0;
     &eaTag = "";

     aAlfa = "";
     aArti = "";
     nPendiente = 0;
     nSwReserva = 0;
     aModifica = "";
     aCompleto = "";
     aHayPdte = "";
     aTmp1 = "";
     aTmp2 = "";
     aTmp6 = "";
     aServir = "";
     nPrime = 0;
     nSw = 0;
     aHayReserva = "";
     nUni = 0;
     aPedido = "";
     nNume = 0;
     nReserva = 0;
     fFecha = @;
     aSerSal = "";
     nNumSal = 0;
     aCali = "N";
     aRese = "N";
     aPend = "N";
     aPath = "";
     fHoy = @;
     nLon = 0;
     aLon = "";
     nPos = 0;
     aA¤o = "";
     nAbo = 0;
     nPedidas = 0;
     nServidas = 0;
     nReservadas = 0;
     &nImpo = 0;
     margen = 0;
     nForma = 0;
     fmes = "";
     mes = 0;
     imneto = 0;
     nDto = 0;
     aEsCalibra = "";
     nHay = 0;
     &nDeci_Div = 0;
     &nDeci_BaseMx = 0;
|FIN;

|PROCESO ActuaFa;
     |ABRE #24;
     #24#0 = #134#2;
     |LEE 121#24=;
     |SI 0 = FSalida;
          fmes = #134#1 % A402;
          mes = ((fmes - 1) * 4) + 54;
          imneto = #134#18 - #134#17;
          |SI #142#115 = "N";
               nImpo = (imneto * 100) / (100 - #134#6);
               nDto = #134#17 + (imneto - nImpo);
          |SINO;
               nImpo = imneto;
               nDto = #134#17;
          |FINSI;
          #24#50 = #24#50 -. imneto;        || pendiente de facturar
          #24mes = #24mes +. nImpo;
          mes = mes + 1;
          #24mes = #24mes +. nDto;
          mes = mes + 1;
          #24mes = #24mes +. #134#46;
          #24#102 = #24#102 +. nImpo;
          #24#103 = #24#103 +. nDto;
          #24#104 = #24#104 +. #134#46;
          #24#45 = #134#1;
          #24#46 = imneto;
          #24#110 = #24#110 +. imneto;
          #24#175 = "N";
          |GRABA 020#24a;

          enImpCliPen = -imneto;
          enImpCliCom = nImpo;
          enImpCliDto = nDto;
          enImpCliMar = #134#46;
          enImpCliFac = imneto;
          eaCliente = #134#2;
          efFecha = #134#1;
          |HAZ AcumulaCli;
     |FINSI;
     |CIERRA #24;

     |ABRE #25;
     #25#0 = #134#7;
     |LIBERA #25;
     |LEE 121#25=;
     |SI 0 = FSalida;
          imneto = #134#18 - #134#17;
          fmes = #134#1 % A402;
          mes = fmes - 1;
          mes = mes * 2;
          mes = mes + 11;
          #25mes = #25mes +. #134#8;

          mes = mes + 1;
          #25mes = #25mes +. imneto;
          #25#35 = #25#35 +. #134#8;
          #25#36 = #25#36 +. imneto;
          mes = fmes - 1;
          mes = mes + 40;
          #25mes = #25mes + #134#55;
          #25#52 = #25#52 + #134#55;

          #25#54 = "N";
          |GRABA 020#25a;

          enImpRepComi = #134#8;
          enImpRepVta = imneto;
          enImpRepRete = #134#55;
          eaRepre = #134#7;
          efFecha = #134#1;
          |HAZ AcumulaRep;

     |FINSI;
     |CIERRA #25;
     ||************* CALCULA EL RIESGO EN FACTURAS;
     eaTag = "FAC";  |HAZ Riesgos;
|FPRC;

|PROCESO ActFac;
     nForma = 1;
     #19#0 = #71#2;
     |LEE 121#19=;
     |SI FSalida = 0;
          #71#14 = #71#5 * #19#9;  ||Coste
          |GRABA 020#71a;

          |SI #19#194 = 2;
               nImpo = ((((#71#48 * #71#6) < #71#8) < #71#33) < #10#5) < #10#32;
          |SINO;
               nImpo = ((((#71#5 * #71#6) < #71#8) < #71#33) < #10#5) < #10#32;
          |FINSI;
          |SI #142#115 = "S";
               nImpo = nImpo < #10#7;
          |FINSI;
          margen = #71#14;             ||Coste albaran !!
          |SI #134#57 = "S";
               nImpo = -nImpo;
               margen = nImpo + margen;
          |SINO;
               margen = nImpo - margen;
          |FINSI;
          #10#37 = #10#37 + (margen * nForma);

          fmes = #134#1 % A402;
          mes = ((fmes - 1) * 5) + 35;

          #19mes = #19mes + (nImpo * nForma);      ||       imneto;
          mes = mes + 1;
          #19mes = #19mes + (margen * nForma);
          #19#95 = #19#95 + (nImpo * nForma);       ||    imneto;
          #19#96 = #19#96 + (margen * nForma);
          mes = (FEntrada / 100) ! 0;
          |SI 1 = mes;
               #19#24 = #134#1;
               #19#25 = #71#5;
          |FINSI;
          |GRABA 020#19a;

          efFecha = #134#1;
          eaArticulo = #71#2;
          enImpVta = (nImpo * nForma);
          enImpMar = (margen * nForma);
          |HAZ AcumulaArt;

          enForma = nForma;
          enImpKit = nImpo;
          enMrgKit = margen;
          |HAZ ImpArtKit;    || Acumulados Articulos con Kit
     |FINSI;
     |LIBERA #71;
|FPRC;

|PROCESO ReserPedido;
     |SI #5#5 > 0;
          |DDEFECTO #12;
          |ABRE #12;
          #12#0 = #71#2;
          |LEE 101#12=;
          |SI FSalida ! 0; |GRABA 020#12b; |FINSI;
          #12#1 = #12#1 - #71#5;
          |GRABA 020#12a;
          |CIERRA #12;
     |FINSI;

     |ABRE #17;
     |ABRE #19;
     |DDEFECTO #17;
     |DDEFECTO #19;
     #19#0 = #71#2;
     |LEE 101#19=;
     |SI FSalida ! 0; |GRABA 020#19b; |FINSI;
     #17#0 = #71#2;
     #17#1 = #71#3;
     |LEE 101#17=;
     |SI FSalida ! 0; |GRABA 020#17b; |FINSI;

     |SI #5#5 > 0;
          #19#15 = #19#15 - #71#5;
          #17#42 = #17#42 - #71#5;

          aAlfa = #19#0 - "CAL";
          |SI FSalida > 0; aEsCalibra = "S"; |SINO; aEsCalibra = "N"; |FINSI;

          #19#12 = #19#12 - #71#5;
          #17#8 = #17#8 - #71#5;

          || incremento el disponilbe porque AcumulAV resta otra vez de este
          #19#104 = #19#104 + #71#5;
          #17#39 = #17#39 + #71#5;
     |FINSI;

     |HAZ ValoraStock;
     |GRABA 020#19a;
     |GRABA 020#17a;

     |CIERRA #19;
     |CIERRA #17;
|FPRC;

|PROCESO Tratados;
     #4#25 = #6#0;
     #4#0 = #6#1;
     |LEE 121#4=;
     |SI FSalida = 0;
          |HAZ RecalTipoPed;
          |GRABA 020#4a;
          |LIBERA #4;
     |FINSI;
|FPRC;

|DEFBUCLE Tratados;
     #6#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, Tratados;
|FIN;

|PROCESO ImpFactura;
     |LIBERA #134;
     |SI #134#0 ! 0;
          ser_fac = #134#49;
          num_fac = #134#0;
          reimp = "N";
          eaImprime = "CrystalReport";
          |SUB_EJECUTA_NP "agifa136;"
     |FINSI;
|FPRC;

|PROCESO ImpAlbaran;
     |LIBERA #10;
     |SI #10#0 ! 0;
          eaImpresora = "CrystalReport";
          ser_alb = #10#52;
          num_alb = #10#0;
          |SUB_EJECUTA_NP "agifa014";
     |FINSI;
|FPRC;

|PROCESO Etiqueta;
     |LIBERA #10;
     |SI #10#0 ! 0;
          |SI enSwRep = 0;
               eaSerie = #10#52;
               enDocu = #10#0;
               eaTipo = "AV";
          |SINO;
               eaSerie = #4#95;
               enDocu = #4#96;
               eaTipo = "RE";
          |FINSI;
          |SUB_EJECUTA_NP "pcegz019";
     |FINSI;
|FPRC;

|PROCESO Reservados;
     #5#28 = #2#0;
     #5#0 = #2#1;
     #5#1 = #2#2;
     |LEE 020#5=;
     |PRINT;
|FPRC;

|DEFBUCLE Reservados;
     #2#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, Reservados;
|FIN;

|PROCESO InfoReserva;
|ACABA;

|| Eleminado: Subtiquet nro. 00255.0008 Cliente: 003308

     Impresora = "CrystalReport";
     |IMPRE -1;
     |SI FSalida = 0;
          |INFOR "pcegz018";
          |SI FSalida = 0;
               |ABRE #5;
               |BUCLE Reservados;
               |CIERRA #5;
               |PIEINF;
               |FININF;
               |FINIMP;
          |SINO;
               |MENAV "0000Error en informe 'pcegz018'";
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Log;
     |LEE 000#14u;
     |SI FSalida = 0;
          #14#0 = #14#0 + 1;
     |SINO;
          #14#0 = 1;
     |FINSI;
     #14#1 = aAlfa;
     |GRABA 020#14n;
|FPRC;

|PROCESO GrabaReserva;
     |SI #1#3 = 0; |ACABA; |FINSI;
     #1#0 = #5#28;
     #1#1 = #5#0;
     #1#2 = #5#1;
     |LEE 000#1=;
     |SI FSalida ! 0; |ACABA; |FINSI;

     #2#0 = #1#0;
     #2#1 = #1#1;
     #2#2 = #1#2;
     #2#3 = #1#3;
     #2#4 = #5#2;
     |GRABA 020#2n;
     aHayReserva = "S";
|FPRC;

|PROCESO CabFac;
     #134#1 = ~;
     #134#2 = #10#3;
     #134#3 = #10#4;
     #134#4 = #10#5;
     #134#5 = #10#32;
     #134#6 = #10#7;
     #134#7 = #10#6;
     #134#9 = #24#29;
     #134#10 = #24#30;
     #134#11 = #10#8;
     #134#12 = #24#16;
     #134#13 = #10#36;
     #134#14 = #24#22;
     #134#15 = #24#23;
     #134#16 = #24#24;
     #134#58 = #10#68;
     #134#59 = #10#69;
     #134#60 = #10#70;
     #134#61 = #10#73;
     #134#62 = #10#74;
     #134#63 = 1;
     #134#115 = #10#84;
     #134#124 = #10#63;
|FPRC;

|PROCESO LinFac;
     |DDEFECTO #59;
     #59#14 = #134#49;
     #59#0 = #134#0;
     #59#1 = 1;
     #59#2 = #10#0;
     #59#15 = #10#52;
     #59#18 = #10#68;
     #59#19 = #10#69;
     #59#20 = #10#70;
     |GRABA 020#59n;

     |ABRE #19;
     |BUCLELIN 0 ActFac #10;
     |CIERRA #19;
|FPRC;

|PROCESO CreaFactura;
     aAlfa = #4#25;
     |HAZ CamA¤oSerie;

     |ABRE #134;
     |ABRE #59;
     #134#49 = aAlfa;

     |HAZ ContaHuecoFV;
/*
     enSwMenu = 1;
     |HAZ ContaFV7;
     |GRABA 000#134b;
     |PARA; |SI FSalida ! 0; |HACIENDO;
          #134#0 = #134#0 + 1;
          |GRABA 020#134b;
     |SIGUE;
*/
     #10#53 = #134#49;
     #10#39 = #134#0;
     |GRABA 020#10a;

     |HAZ CabFac;
     |HAZ LinFac;

     aDivisa = #134#58;
     aMoneda = #134#60;
     |HAZ Divisas134;
     |HAZ CalCabAgifa134;
     |HAZ VenciVta;
     |GRABA 020#134a;
     |HAZ ActuaFa;
     |CIERRA #59;
     |CIERRA #134;
|FPRC;

|PROCESO CamA¤oSerie;
     |QBF aAlfa;
     aLon = aAlfa % A0;
     nLon = aLon;
     nPos = nLon - 2;
     fHoy = ~;
     aA¤o = fHoy % -102;
     |SI nPos [ 0;
          aAlfa = aA¤o;
     |SINO;
          nPos = nPos + 100;
          aAlfa = aAlfa % nPos;
          aAlfa = aAlfa + aA¤o;
     |FINSI;
|FPRC;

|PROCESO CabAlba;
     aAlfa = #4#25;
     |HAZ CamA¤oSerie;

     |SI nSw = 1;   || Alb.Sal
          #10#52 = aAlfa;
     |SINO;         || Alb.Vta
          #10#52 = "Z" + aAlfa;
     |FINSI;
     enSwMenu = 1;
     |HAZ ContaAV7;
     |GRABA 000#10b;
     |PARA; |SI FSalida ! 0; |HACIENDO;
          #10#0 = #10#0+ 1;
          |GRABA 020#10b;
     |SIGUE;

     |DDEFECTO #20;
     |ABRE #20;
     #20#0 = #4#60;
     |LEE 000#20=;
     |CIERRA #20;
     |ABRE #24;
     #24#0 = #4#4;
     |LEE 000#24=;
     |CIERRA #24;

     #10#68 = #4#35;
     #10#70 = #4#37;
     aDivisa = #10#68;
     aMoneda = #10#70;
     |HAZ Divisas010;

     #10#1 = ~;
     #10#3 = #4#4;
     #10#4 = #4#8;
     #10#5 = #4#5;
     #10#6 = #4#7;
     #10#7 = #4#6;
     #10#8 = #4#9;
     #10#9 = #20#1;

     |SI nSw = 2;                  || = Albaran Z
          |SI #4#37 = "D";
               |SI #4#98 ! #4#100;
                    #10#81 = #4#98;
                    #10#11 = #4#67;
                    #4#100 = #4#98;
                    #4#97 = #4#67;
               |FINSI;
          |SINO;
               |SI #4#67 ! #4#97;
                    #10#81 = #4#98;
                    #10#11 = #4#67;
                    #4#100 = #4#98;
                    #4#97 = #4#67;
               |FINSI;
          |FINSI;
     |SINO;
          |SI #4#37 = "D";
               |SI #4#98 ! #4#99;
                    #10#81 = #4#98;
                    #10#11 = #4#67;
                    #4#99 = #4#98;
                    #4#68 = #4#67;
               |FINSI;
          |SINO;
               |SI #4#67 ! #4#68;
                    #10#81 = #4#98;
                    #10#11 = #4#67;
                    #4#99 = #4#98;
                    #4#68 = #4#67;
               |FINSI;
          |FINSI;
     |FINSI;

     #10#12 = #4#82;
     #10#29 = #4#19;
     #10#30 = #4#14;
     #10#31 = #4#20;
     #10#32 = #4#17;
     #10#33 = #4#21;
     #10#34 = #4#16;
     #10#35 = #4#18;
     #10#36 = #24#47;
     #10#37 = #24#41;
     #10#55 = #4#53;
     #10#56 = #4#54;
     #10#57 = #4#55;
     #10#62 = #4#59;
     #10#63 = #4#64;
     #10#64 = #4#61;
     #10#69 = #4#36;
     #10#73 = #4#40;
     #10#74 = #4#41;
     #10#83 = #4#51;
     #10#84 = #4#57;
     #10#88 = #20#0;


     |HAZ LimCabAgifa010;

     |GRABA 020#4a;
     |GRABA 020#10a;
|FPRC;

|PROCESO CreaAlbaran;
     |SI nSw = 1;   || Alb.Sal
          |DDEFECTO #1;
          #1#0 = #5#28;
          #1#1 = #5#0;
          #1#2 = #5#1;
          |LEE 000#1=;
          nUni = #1#3;                  || reservadas
          |SI aCompleto = "S";
               nUni = #5#5 - #5#43;     || las pendientes
          |FINSI;
     |SINO;         || Alb.Vta
          nUni = #5#5 - #5#59;          || pedidas - facturadas
          |SI #5#5 < 0;
               |SI nUni ] 0; |ACABA; |FINSI;
          |SINO;
               |SI enSwRep = 0;
                    |SI nUni [ 0; |ACABA; |FINSI;
               |FINSI;
          |FINSI;

          #agifa071@#29 = aSerSal;
          #agifa071@#0 = nNumSal;
          #agifa071@#1 = #5#1;
          |LEE 000#agifa071@.=;
          |SI FSalida ! 0; |ACABA; |FINSI;
          |SI nUni > #agifa071@#5;
               nUni = #agifa071@#5;     || Factura las unidades del albaran
          |FINSI;
     |FINSI;

     |SI #5#5 < 0;
          |SI nUni ] 0; |ACABA; |FINSI;
     |SINO;
          |SI enSwRep = 0;
               |SI nUni [ 0; |ACABA; |FINSI;
          |FINSI;
     |FINSI;

     |SI nPrime = 0;
          |HAZ CabAlba;
          nPrime = 1;
     |FINSI;

     |DDEFECTO #71;
     #71#29 = #10#52;
     #71#0 = #10#0;
     #71#1 = #5#1;
     #71#2 = #5#2;
     #71#3 = #5#3;
     #71#4 = #5#4;
     #71#5 = nUni;
     #71#6 = #5#6;
     #71#8 = #5#8;
     #71#10 = #5#10;
     #71#11 = #5#14;
     #71#12 = #5#0;
     #71#13 = #5#18;
     #71#15 = #10#3;
     #71#16 = #5#17;
     #71#17 = #5#19;
     #71#21 = #5#1;
     #71#27 = #5#27;
     #71#31 = #5#28;
     #71#33 = #5#29;
     #71#34 = #5#35;
     #71#35 = #5#36;
     #71#36 = #5#38;
     #71#49 =  #5#45;

     |HAZ TotalLin71;
     |HAZ CalCabAgifa010;
     |GRABA 020#71n;
     #10#41 = #10#41 + 1;
     |GRABA 020#10a;

     |SI nSw = 1;        || Alb.Sal
          enForma = 1;
          |HAZ AcumulaAV;
          #5#43 = #5#43 + #71#5;

          #5#60 = #5#60 - #71#5;

          |HAZ ReserPedido;
     |SINO;              || Alb.Vta
          #5#59 = #5#59 + #71#5;
     |FINSI;
     |GRABA 020#5a;
|FPRC;

|PROCESO Reserva;
     nSwReserva = 1;  || No reserva

     aAlfa = #5#2;
     aAlfa = aAlfa - "CAL";
     |SI FSalida > 0;
          |ACABA;
     |FINSI;

     |ABRE #19;
     |ABRE #17;
     #19#0 = #5#2;
     |LEE 101#19=;
     |SI FSalida = 0;
          #17#0 = #5#2;
          #17#1 = #5#3;
          |LEE 101#17=;
          |SI FSalida = 0;
               nNume = nPendiente - nReservadas;
               |SI #17#39 ] nNume; || Se reserva todo lo pendiente
                    nReserva = nNume;
                    nSwReserva = 0;
               |SINO;
                    |SI #17#39 > 0;     || Se reserva lo que se puede
                         nReserva = #17#39;
                         nSwReserva = 0;
                    |FINSI;
               |FINSI;
/*
               |SI #17#39 ] nPendiente; || Se reserva todo lo pendiente
                    nReserva = nPendiente;
                    nSwReserva = 0;
               |SINO;
                    |SI #17#39 > 0;     || Se reserva lo que se puede
                         nReserva = #17#39;
                         nSwReserva = 0;
                    |FINSI;
               |FINSI;
*/
               |SI nSwReserva = 0;
                    #17#8 = #17#9 + nReserva;
                    #17#39 = #17#39 - nReserva;
                    #19#12 = #19#12 + nReserva;
                    #19#104 = #19#104 - nReserva;
                    |HAZ ValoraStock;
                    |GRABA 020#19a;
                    |GRABA 020#17a;
               |FINSI;
          |FINSI;
     |FINSI;
     |CIERRA #17;
     |CIERRA #19;
     FSalida = nSwReserva;
|FPRC;

|PROCESO CheqCalibra;
     aAlfa = #5#2;
     aAlfa = aAlfa - "CAL";
     |SI FSalida > 0;
          nNume = #5#5 - #5#43 - #5#60;
          |SI nNume > 0;
               aServir = "N";
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO FinPedido;
     aPedido = #4#25 + "/" + (("000000" + #4#0) % -106);

     |SI aHayPdte = "N";      || Estaba todo reservado
          aAlfa = aPedido + " estaba todo reservado, no se modifica";
          |HAZ Log;
          |ACABA;
     |FINSI;

     |SI enSwAdj = 0;
          aServir = "S";
          |BUCLELIN 2 CheqCalibra #4;
          |SI aServir = "N";
               aAlfa = aPedido + " pedido con articulos CAL pendiente";
               |HAZ Log;
               |ACABA;
          |FINSI;
     |FINSI;

     #42#0 = #4#95;
     #42#1 = #4#96;
     |LEE 000#42=;
     |SI FSalida = 0; |Y enSwRep = 0;   || Es de reparacion
          |ACABA;                       || y no se esta cerrando desde reparaciones
     |FINSI;

     |ABRE #10;
     |ABRE #71;

     |SI aCompleto = "S";
          aAlfa = aPedido + " esta completo";
          |HAZ Log;

          nPrime = 0;
          nSw = 1; |BUCLELIN 2 CreaAlbaran #4; || Alb. Sal
          |SI nPrime = 0; |ACABA; |FINSI;
          aAlfa = #10#52 + "/" + (("000000" + #10#0) % -106);
          aAlfa = "Crea albaran salida " + aAlfa;
          |HAZ Log;
          aSerSal = #10#52; nNumSal = #10#0;

          |SALVA_DATOS #10;
          nPrime = 0;
          nSw = 2; |BUCLELIN 2 CreaAlbaran #4; || Alb. Vta
          |SI nPrime = 0;
               aAlfa = "";
               |DDEFECTO #134;
          |SINO;
               #16#0 = #10#52;
               #16#1 = #10#0;
               #16#2 = aSerSal;
               #16#3 = nNumSal;
               |GRABA 020#16n;

               aAlfa = #10#52 + "/" + (("000000" + #10#0) % -106);
               aAlfa = "Crea albaran venta " + aAlfa;
               |HAZ Log;

               |HAZ CreaFactura;

               #42#0 = #4#95;
               #42#1 = #4#96;
               |LEE 101#42=;
               |SI FSalida = 0; |Y enSwRep = 1;   || Es de reparacion
                    #42#23 = #134#49;
                    #42#24 = #134#0;
                    |GRABA 020#42a;
               |FINSI;
               |LIBERA #42;

               aAlfa = #134#49 + "/" + (("000000" + #134#0) % -106);
               aAlfa = "Crea factura " + aAlfa;
               |HAZ Log;

               aAlfa = #10#52 + "/" + (("000000" + #10#0) % -106);
          |FINSI;
          |REPON_DATOS #10;
          |LEE 121#10=;
          |SI FSalida = 0; |Y aAlfa ! "";
               #10#55 = aAlfa;
               |GRABA 020#10a;
          |FINSI;

          |HAZ ImpAlbaran;
          |HAZ ImpFactura;
          |HAZ Etiqueta;
     |SINO;
          |SI #4#84 = 0;
               nPrime = 0;
               nSw = 1; |BUCLELIN 2 CreaAlbaran #4; || Alb. Sal
               |SI nPrime = 0; |ACABA; |FINSI;
               aAlfa = #10#52 + "/" + (("000000" + #10#0) % -106);
               aAlfa = "Crea albaran salida " + aAlfa;
               |HAZ Log;
               |HAZ ImpAlbaran;
          |FINSI;
          |SI #4#84 = 1; |Y aModifica = "S"; || si algo nuevo
               nPrime = 0;
               nSw = 1; |BUCLELIN 2 CreaAlbaran #4; || Alb. Sal
               |SI nPrime = 0; |ACABA; |FINSI;
               aAlfa = #10#52 + "/" + (("000000" + #10#0) % -106);
               aAlfa = "Crea albaran salida " + aAlfa;
               |HAZ Log;
               |HAZ ImpAlbaran;
          |FINSI;
          |SI #4#84 = 2;
               |HAZ GrabaReserva;
          |FINSI;
     |FINSI;

     |CIERRA #10;
     |CIERRA #71;
|FPRC;

|PROCESO LinPedido;
     aPedido = #5#28+"/"+(("000000"+#5#0)%-106)+"/"+(("000"+#5#1)%-103);
     aPedido = "Linea pedido " + aPedido + " art:" + #5#2;
     |QBF aPedido;
     aPedido = aPedido + ", ";

     nAbo = 1;

     |SI #5#5 < 0; nAbo = -1; |FINSI;
     nPedidas = #5#5 * nAbo;
     nServidas = #5#43 * nAbo;
     nReservadas = #5#60 * nAbo;

     nPendiente = nPedidas - nServidas;

     |SI nPendiente [ 0; || Esta servido
          aAlfa =  aPedido + "servida no se procesa";
          |HAZ Log;
          |ACABA;
     |FINSI;

     aHayPdte = "S";
     |SI nReservadas < nPendiente;
          |SI nAbo = 1;
               |HAZ Reserva;
          |SINO;
               nReserva = nPendiente * nAbo;
               FSalida = 0;
          |FINSI;
          |SI FSalida = 0;
               #5#60 = #5#60 + nReserva;
               |GRABA 020#5a;

               aAlfa = aPedido + "se reservan " + nReserva + " de " + nPendiente + " pendientes";
               |HAZ Log;
               enLinea = 0;

               aModifica = "S";
               #1#0 = #5#28;
               #1#1 = #5#0;
               #1#2 = #5#1;
               #1#3 = nReserva;
               |GRABA 020#1n;
          |SINO;
               aAlfa = aPedido + "no se pueden reservar " + nPendiente + " pendientes";
               |HAZ Log;
          |FINSI;
     |SINO;
          |SI #5#60 ! 0;
               #1#0 = #5#28;
               #1#1 = #5#0;
               #1#2 = #5#1;
               #1#3 = #5#60 * nAbo;
               |GRABA 020#1n;
          |FINSI;
     |FINSI;

     nPendiente = nPendiente * nAbo;
     |SI nPendiente ! #5#60;
          aCompleto = "N";
     |FINSI;
|FPRC;

|PROCESO Pedido;
     aCompleto = "S";
     aModifica = "N";
     aHayPdte = "N";

     aPedido = #4#25 + "/" + (("000000" + #4#0) % -106);
     aAlfa = "Procesando pedido:" + aPedido;

     |HAZ Log;

     || Tratados
     #6#0 = #4#25;
     #6#1 = #4#0;
     |GRABA 020#6n;

     aDivisa = #4#35;
     aMoneda = #4#37;
     |HAZ Divisas104;
|FPRC;

|DEFBUCLE Pedidos;
     #4#9;
     ;
     "C", 1, #0#4, #0#0, #0#2;
     "C", 3, #0#5, #0#1, #0#3;
     be;
     NULL, Pedido, LinPedido, FinPedido;
|FIN;

|PROCESO TmpFic;
     #4#25 = #9#0;
     #4#0 = #9#1;
     |LEE 121#4=;
     |SI FSalida = 0;
          |HAZ Pedido;
          |BUCLELIN 0 LinPedido #4;
          |HAZ FinPedido;
          |LIBERA #4;
     |FINSI;
|FPRC;

|DEFBUCLE TmpFic;
     #9#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, TmpFic;
|FIN;

|PROCESO Tipo3; |TIPO 3;
     |DDEF aPath;
     aAlfa = aPath + "agifa071";
     |CARGA_DEF aAlfa, agifa071@;
     |SI FSalida ! 0;
          |MENAV "0000Error al cargar 'agifa071'";
          |ACABA;
     |FINSI;
     |ABRE #agifa071@;

     |HAZ CreaTempo; |NOME_DAT #1 Tempo; |DELINDEX #1; aTmp1 = Tempo;
     |HAZ CreaTempo; |NOME_DAT #2 Tempo; |DELINDEX #2; aTmp2 = Tempo;
     |HAZ CreaTempo; |NOME_DAT #6 Tempo; |DELINDEX #6; aTmp6 = Tempo;
     |ABRE #1;
     |ABRE #2;
     |ABRE #6;
     |ABRE #42;

     |DDEF aPath;
     aAlfa = aPath + "pcegm005";
     |CARGA_DEF aAlfa, pcegm005@;
     |SI FSalida = 0;
          |ABRE #14;
          |ABRE #16;
          fFecha = ~;
          |HORA aAlfa;
          aAlfa = "-.Ejecucion el " + fFecha + " a las " + aAlfa + " por usuario " + (Usuario % 810);
          |HAZ Log;

          |SI enSwRep = 1;
               |ABRE #4;
               #4#25 = eaSerie;
               #4#0 = enPedido;
               |LEE 121#4=;
               |SI FSalida = 0;
                    |HAZ Pedido;
                    aCompleto = "S";
                    aHayPdte = "S";
                    |HAZ FinPedido;
                    |LIBERA #4;
               |FINSI;
               |CIERRA #4;
          |SINO;
               |SI eaFichero = "";
                    |BUCLE Pedidos;
               |SINO;
                    |ABRE #4;
                    |BUCLE TmpFic;
                    |CIERRA #4;
               |FINSI;
          |FINSI;
          |CIERRA #16;
          |CIERRA #14;
          |SI aHayReserva = "S";
               |HAZ InfoReserva;
          |FINSI;

          |DESCARGA_DEF #pcegm005@;
     |FINSI;

     |CIERRA #42;
     |CIERRA #6;
     |CIERRA #2;
     |CIERRA #1;

     |ABRE #4;
     |BUCLE Tratados;
     |CIERRA #4;

     |DELINDEX #1; Tempo = aTmp1; |HAZ BoraTempo;
     |DELINDEX #2; Tempo = aTmp2; |HAZ BoraTempo;
     |DELINDEX #6; Tempo = aTmp6; |HAZ BoraTempo;

     |CIERRA #agifa071@;
     |DESCARGA_DEF #agifa071@;
|FPRC;

|PROGRAMA;
     |ABRE #142; |LEE 000#142p; |CIERRA #142;
     |SI eaFichero ! "";
          |NOME_DAT #9 eaFichero;
          |HAZ Tipo3;
          |ACABA;
     |FINSI;
     |SI enPedido ! 0;
          #0#0 = eaSerie;
          #0#1 = eaSerie;
          #0#2 = enPedido;
          #0#3 = enPedido;
          #0#4 = efFecha;
          #0#5 = efFecha;
          |HAZ Tipo3;
     |SINO;
          |MANTE #0;
     |FINSI;
|FPRO;
