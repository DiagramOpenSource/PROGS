|FICHEROS;
     iber0217 #9;   ||Regularizacion

     dsm00023 #2;   ||Configuracion

     agifa017 #17;  ||Arti/Alma
     agifa019 #19;  ||Arti
     dsm00024 #24;  ||Arti/Alma/Partida
     agifa065 #65;  ||Historico
     agifa142 #142; ||Param
     dsm00238 #238; ||Unidades

     agifa321 #321; || Parametros divisas
     agifa324 #322; || Divisas
|FIN;

|CAMPOS;
     #19#15  art_pser;  #17#42  alm_pser;
     #19#182 art_pser2; #17#44  alm_pser2;
     #19#16  art_prec;  #17#43  alm_prec;
     #19#183 art_prec2; #17#45  alm_prec2;
     #19#17  art_pval;  #17#4   alm_pval;
     #19#11  art_nece;  #17#50  alm_nece;
     #19#12  art_rese;  #17#8   alm_rese;
     #19#14  art_depo;  #17#10  alm_depo;
     #19#184 art_depo2; #17#46  alm_depo2;
     #19#13  art_fabr;  #17#9   alm_fabr;
     #19#10  art_vsto;  #17#3   alm_vsto;
     #19#7   art_mini;  #17#6   alm_mini;
     #19#8   art_maxi;  #17#7   alm_maxi;
     #19#6   art_tota;  #17#5   alm_tota;
     #19#185 art_tota2; #17#47  alm_tota2;
     #19#104 art_disp;  #17#39  alm_disp;
     #19#193 art_disp2;  #17#48 alm_disp2;
     #19#9   art_pcm;
     #24#9   par_ereal2; #24#10  par_ereal;
     #24#11  par_sreal2; #24#12  par_sreal;
     #24#16  par_pser2;  #24#17  par_pser;
     #24#18  par_prec2;  #24#19  par_prec;
     #24#20  par_depo2;  #24#21  par_depo;
     #24#22  par_tota2;  #24#23  par_tota;
     #24#14  par_merma;
|FIN;

|VARIABLES;
     &nDeci_Base = 0;
     &nDeci_PreBase = 0;
     nValor    = 0;
     aEsSalida = "";

     aArti     = "";
     nAlma     = 0;
     aParti    = "";
     nUni0     = 0;
     nUni      = 0;
     nUni2     = 0;
     nCoste    = 0;
     aAlfa     = "";
     nNume     = 0;
     fUltFecha = @;
|FIN;

|INCLUYE i_varart;

|PROCESO Deci_iber;
     |ABRE #agifa324;
     |ABRE #agifa321;
     |LEE 001#agifa321.p;
     #agifa324#0 = #agifa321#9; ||Igualo a moneda base
     |LEE 001#agifa324.=;
     nDeci_Base = #agifa324#7;  ||Decimales moneda base
     nDeci_PreBase = #agifa324#9;  ||Decimales precio de moneda base
     |CIERRA #agifa321;
     |CIERRA #agifa324;
|FPRC;

|PROCESO ValorStock_iber;
     |HAZ Deci_iber;
     art_nece = art_pser + art_mini - art_prec - art_disp;
     art_nece = art_nece - art_pval - art_fabr - art_rese;
     |SI art_nece < 0; art_nece = 0; |FINSI;

     alm_nece = alm_pser + alm_mini - alm_prec - alm_disp;
     alm_nece = alm_nece - alm_pval - alm_fabr - alm_rese;
     |SI alm_nece < 0; alm_nece = 0; |FINSI;

     |SI #19#196 = 1;
          nValor = art_tota - art_pval;
          |SI nValor > 0;
               art_vsto = nValor * art_pcm;
          |SINO;
               art_vsto = 0;
          |FINSI;

          nValor = alm_tota - alm_pval;
          |SI nValor > 0;
               alm_vsto = nValor * art_pcm;
          |SINO;
               alm_vsto = 0;
          |FINSI;
     |SINO;
          nValor = art_tota2;
          |SI nValor > 0;
               art_vsto = nValor * art_pcm;
          |SINO;
               art_vsto = 0;
          |FINSI;

          nValor = alm_tota2;
          |SI nValor > 0;
               alm_vsto = nValor * art_pcm;
          |SINO;
               alm_vsto = 0;
          |FINSI;
     |FINSI;
     par_tota = par_ereal - par_sreal;
     par_tota2 = par_ereal2 - par_sreal2;
     |SI aEsSalida = "S"; #24#5 = fUltFecha; |FINSI;
     #24#13 = #24#23 / #24#22;                          ||Factor
     #24#15 = ((#24#10 - #24#14) * 100) / #24#10;       ||%Rend. Real
     |SI #24#25 = 1; nNume = #19#18;  |FINSI;
     |SI #24#25 = 2; nNume = #19#19;  |FINSI;
     |SI #24#25 = 3; nNume = #19#20;  |FINSI;
     |SI #24#25 = 4; nNume = #19#147; |FINSI;
     |SI #24#25 = 5; nNume = #19#148; |FINSI;
     |SI #24#25 = 6; nNume = #19#149; |FINSI;
     #24#26 = (#24#10 * nNume) - #24#24;
|FPRC;

|PROCESO LeeArticulo_iber;
     |ABRE #142; |LEE 000#142p; |CIERRA #142;

     |ABRE #19;
     |ABRE #17;
     |DDEFECTO #19;
     |DDEFECTO #17;

     #19#0 = aArti;
     |LEE 101#19=;
     |SI FSalida ! 0; |GRABA 020#19b; |FINSI;

     #17#0 = aArti;
     #17#1 = nAlma;
     |LEE 101#17=;
     |SI FSalida ! 0; |GRABA 020#17b; |FINSI;

     |SI #19#180 = "S";       || Si hay partidas
          |ABRE #24;
          |DDEFECTO #24;
          #24#0 = aArti;
          #24#1 = nAlma;
          #24#2 = aParti;
          |LEE 101#24=;
          |SI FSalida ! 0; |GRABA 020#24b; |FINSI;
     |FINSI;
|FPRC;

|PROCESO GrabaArti_iber;
     #19#9 = #9#22;      ||PCM

     |GRABA 020#17a;
     |GRABA 020#19a;
     |CIERRA #19;
     |CIERRA #17;
     |SI #19#180 = "S";
          |GRABA 020#24a;     || Si hay partidas
          |CIERRA #24;
     |FINSI;
|FPRC;

|PROCESO StockES_SS_iber;
     |SI #9#7 = "E"; nUni = #9#4;   |SINO; nUni  = -#9#4;  |FINSI;
     |SI #9#7 = "E"; nUni2 = #9#12; |SINO; nUni2 = -#9#12; |FINSI;
     nUni = nUni * enForma;
     nUni2 = nUni2 * enForma;
     |SI #19#196 = 1; nUni0 = nUni; |SINO; nUni0 = nUni2; |FINSI;

     art_tota = art_tota + nUni;
     art_tota2 = art_tota2 + nUni2;
     art_disp = art_disp + nUni;
     art_disp2 = art_disp2 + nUni2;

     alm_tota = alm_tota + nUni;
     alm_tota2 = alm_tota2 + nUni2;
     alm_disp = alm_disp + nUni;
     alm_disp2 = alm_disp2 + nUni2;

     |SI #9#7 = "E";
          par_ereal = par_ereal + #9#4;
          par_ereal2 = par_ereal2 + #9#12;
          par_merma = par_merma + #9#14;
          |SI #24#4 = "01.01.0000";
               #24#3 = #19#27;
               #24#4 = #9#5;
          |FINSI;
          |SI #24#9 = 0; |Y #24#10 = 0; |Y #24#24 = 0;
               nCoste = nUni0 * art_pcm;
          |SINO;
               nCoste = nUni0 * #24#24 / #24#10;
          |FINSI;
          #24#24 = #24#24 + nCoste;
     |SINO;
          par_sreal = par_sreal + #9#4;
          par_sreal2 = par_sreal2 + #9#12;
          par_merma = par_merma - #9#14;
     |FINSI;
     aAlfa = #9#5 % 402;
     nNume = aAlfa;
     nNume = (nNume - 1) * 2;
     |SI #9#7 = "E";
          nNume = nNume + 12;
          #17#36 = #17#36 + nUni;
          enUniEnt = nUni;
     |SINO;
          nUni = nUni * -1;
          nNume = nNume + 11;
          #17#35 = #17#35 + nUni;
          enUniSal = nUni;
     |FINSI;
     #17nNume = #17nNume + nUni;
     |SI #9#7 ! "E";
          nUni = -nUni;
     |FINSI;

     |HAZ ValorStock_iber;

     efFecha = #9#5;
     eaArticulo = #17#0;
     enAlmacen = #17#1;
     |HAZ AcumulaAlm;
|FPRC;

|PROCESO HistoricoES_SS_iber;
     |ABRE #65;
     |DDEFECTO #65;
     #65#0 = #9#0;
     #65#1 = #9#5;
     |SI #9#7 = "E";
          #65#2 = "ES";
          #65#6 = #9#4;
     |SINO;
          #65#2 = "SS";
          #65#6 = -#9#4;
     |FINSI;
     #65#3 = #9#18;
     #65#4 = #9#8;
     #65#5 = #9#1;
     #65#7 = #19#6;
     #65#8 = #19#9;
     #65#11 = #9#21;
     #65#17 = #9#12;
     #65#18 = #9#13;
     #65#19 = #9#14;
     |SI enForma = 1;
          |GRABA 020#65n;
     |SINO;
          |BORRA 020#65c;
     |FINSI;
     |CIERRA #65;
|FPRC;

|PROCESO AcumulaES_SS_iber;
     aArti = #9#0;
     nAlma = #9#1;
     aParti = #9#13;
     |HAZ LeeArticulo_iber;
     |HAZ StockES_SS_iber;
     |HAZ GrabaArti_iber;
     |HAZ HistoricoES_SS_iber;
|FPRC;
