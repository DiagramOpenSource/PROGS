|FICHEROS;
     agifa073 #0;   || Lineas de pedido
     agifa104 #1;   || Pedido
     agifa019 #2;   || Articulos
     agifa024 #6;   || Clientes
     agifa017 #8;   || Stocks almacenes
     agifa025 #12;  || Representantes
     agifa048 #13;  || Escandallos
     agifa049 #14;  || Lineas de escandallos
     alba0034 #98;  || Cabecera revision.
     alba0035 #99;  || Lineas revision.
     agifa142 #42;  || Parametros Generales
     agifa321 #44;  || Divisas-Parametros.
     agifa324 #45;  || Divisas-Divisas.
     alba0007 #47,MANTE;
     dsm00024 #24;
|FIN;

|CAMPOS;
     #2#11 nec;  #2#15 sps;    #2#16 spr;
     #2#7 stmia; #2#104 stdia; #2#12 stres;
     #2#17 pdva; #2#13 stfa;
|FIN;

|INCLUYE i_varart;

|VARIABLES;
     nImpo = 0;
     &enDescuento1 = 0;
     &enDescuento2 = 0;
     &eaTComision  = "";
     &enComision   = 0;

     vacio = "00000000000000000000";
     descu = 0;
     tf = 0;
     comi1 = 0;
     comi2 = 0;
     comi3 = 0;
     menor = 0;
     pc1 = 0;
     cantidad = 0;
     articulo = " ";
     mensaje3 = "0410 Stock Disponible Almacen :             Stock Minimo : ";
     mensaje  = "0000 Pedido para Exportacion.Tipo de IVA = 0  !!";
     pc = 0;
     &n_dec = 0;
     &n_pre = 0;
     codigo = "";
     &ext1 = "";
     modo = 0;
     mes = 0;
     sw_pend = 0;
     numero = "";
     pendiente = 0;
     primera = 0;
     &aMoneda = "";
     &aDivisa = "";
|FIN;

|PROCESO Unidadess;
     |ABRE #47;
     #47#0 = "PV";
     #47#1 = #0#28;
     #47#2 = #0#0;
     #47#3 = #0#1;
     |LEE 101#47=;
     |SI FSalida ! 0; |GRABA 020#47b; |FINSI;
     #47#4 = #99#12;
     #47#5 = #99#13;
     #47#6 = #99#6;
     |GRABA 020#47a;
     |CIERRA #47;
     #0#5 = #47#6;  ||Uni1 Stock
     #0#44 = #47#4; ||Uni2 Factu

     eaArticulo = #0#2;
     |HAZ BseFactu2;
|FPRC;

|CALCULO lpdefec;|TIPO 0;
     |ABRE #12;
     #12#0 = #98#10;
     |LEE 000#12=;
     |LIBERA #12;
     |CIERRA #12;
     #0#16 = #98#10;|| #lin.pedidos#representante=#pedido#representante
     #0#17 = #12#7; || #lin.pedidos#tipo de comision=#pedidos#tipo de comision
     #0#20 = #1#4;  || #lin.pedidos#cliente=#pedidos#cliente
     || Esta linea la pongo aqui porque con un D003agifa104. Pasa de todo
     || Lorca 22/04/94
     #0#13= #1#2;   || #lin.pedidos#fecha entrega=#pedidos#fecha 1 entrega
     |SI #0#2 ! #2#0;
         |ABRE #2;
         #2#0 = #0#2;
         |LEE 000#2=;
         |LIBERA #2;
         |CIERRA #2;
     |FINSI;
     |SI #0#20 ! #6#0;
         |ABRE #6;
         #6#0 = #0#20;
         |LEE 000#6=;
         |LIBERA #6;
         |CIERRA #6;
     |FINSI;
     #0#18 = #2#2;
     #0#19 = #6#4;
|FCAL;

|CALCULO lpsubcom;|TIPO 0;
     enDescuento1 = #0#8;
     enDescuento2 = #0#29;
     eaTComision  = #0#17;
     eaCliente    = #1#4;
     eaArticulo   = #0#2;
     eaRepre      = #1#7;
     |HAZ Comisiones;
     #0#10 = enComision;
|FCAL;

|CALCULO totallip;|TIPO 0;
     #0#7 = #0#44 * #0#6;
     #0#9 = #0#7 < #0#8;
     #0#9 = #0#9 < #0#29;
     #0#9 = #0#9 < #1#5;
     #0#9 = #0#9 < #1#17;
     #0#9 = #0#9 < #1#6;
     #0#9 = #0#9 ! #45#7;
     #0#11 = #0#9;
|FCAL;

|PROCESO ActPartida;
   |ABRE #24;
   #24#0 = #0#2;
   #24#1 = #0#3;
   #24#2 = #0#45;
   |LEE 101#24=;
   |SI FSalida = 0;
        #24#16 = #24#16 + #0#5;
        |GRABA 020#24a;
   |FINSI;
   |CIERRA #24;
|FPRC;

|CALCULO lpacarti;
     mes = 0 + 1;
     codigo = #0#2; |QBF codigo;
     |SI #0#42 = 0; |Y codigo ! ""; |Y modo = 2;
         ||sw_pend controla si se entra por primera o segunda vez
         |SI mes = -1; |O sw_pend = 1;
             sw_pend = 1;
             |ERROR;
             |ACABA;
         |FINSI;
     |SINO;
         sw_pend = 0;
     |FINSI;
     |SI Campo < 3;
         |HAZ lpdefec;
     |FINSI;
     |ABRE #2;
     #2#0 = #0#2;
     |LEE 121#2=;
     |LIBERA #2;
     |SI 0 = FSalida;
         #2#15 = #2#15 + (#0#5 - #0#43);
         |SI #0#22 = AS;
             stres = stres + (#0#5 - #0#43);
             stdia = stdia - (#0#5 - #0#43);
         |FINSI;
         nec = sps + stmia; nec = nec - spr; nec = nec - stdia;
         nec = nec - pdva;  nec = nec - stfa;nec = nec - stres;
         |GRABA 020#2a;
         |LIBERA #2;
     |FINSI;
     |ABRE #8;
     #8#0 = #0#2;
     #8#1 = #0#3;
     |LEE 121#8=;
     |LIBERA #8;
     |SI 0 = FSalida;
         |HAZ para_ger;
         |SI #42#12 = "S";
             |HAZ actualiz;
         |FINSI;
         |SI #0#22 = AS;
             #8#8  = #8#8  + (#0#5 - #0#43);
             #8#39 = #8#39 - (#0#5 - #0#43);
             |GRABA 020#8a;
             |LIBERA #8;
         |FINSI;
         |LIBERA #2;
         |LIBERA #8;
     |FINSI;
     |LIBERA #2;
     |CIERRA #2;
     |LIBERA #8;
     |CIERRA #8;
     |HAZ ActPartida;
     #1#11 = #1#11 + #0#9;
     #1#11 = #1#11 ! #45#7;
     #1#12 = #1#11;
     #1#12 = #1#12 ! #45#7;
     #1#13 = #1#11 - #1#12;
     #1#13 = #1#13 ! #45#7;
|FCAL;

||***************************** CALCULO ESCANDALLOS *******************;
|CALCULO linescan;
   #2#0 = #14#2;
   |LEE 040#2=;
   |LIBERA #2;
   |SI FSalida = 0;
       cantidad = #0#5 * #14#4;
       #2#15 = #2#15 + cantidad;
       |LEE 110#2=;
       |GRABA 040#2a;
       |LIBERA #2;

       |DDEFECTO #8;
       #8#0 = #14#2; ||Articulo;
       #8#1 = #14#9; ||Almacen;
       |LEE 110#8=;
       |LIBERA #8;
       |SI FSalida = 0;
           cantidad = #0#5 * #14#4;
           #8#4 = #8#4 + cantidad;
           |GRABA 040#8a;
           |LIBERA #8;
       |FINSI;
   |FINSI;
|FCAL;

|CALCULO actualiz;
     |ABRE #13;
     #13#0 = #0#2;
     |LEE 040#13=;
     |SI FSalida = 0;
         #2#0 = #13#0;
         |LEE 040#2=;
         |LIBERA #2;
         |BUCLELIN 0linescan#13;
     |FINSI;
     |LIBERA #13;
     |CIERRA #13;
|FCAL;

|PROCESO pasa;
     |HAZ LeeDivisa;
     numero = "000000" + #99#1;
     numero = numero % -106;
     |DDEFECTO #0;
     #0#0 = numero;                || Numero Pedido.
     #0#28 = #99#2;                || Serie Pedido.
     #1#0 = #0#0;                  || Numero Pedido Cabecera.
     #1#25 = #0#28;                || Serie Pedido Cab.
     |LEE 121#1=;                  || Lee cabecera de Pedidos.
     |LIBERA #1;
     #0#1 = #99#3;                 || Linea Pedidos.
     |ABRE #98;
     #98#0 = #99#0;
     #98#1 = #99#1;
     #98#2 = #99#2;
     |LEE 000#98=;
     |LIBERA #98;
     |CIERRA #98;
     #0#2 = #99#4;                 || Cod. Articulo.
     |ABRE #2;
     #2#0 = #99#4;
     |LEE 000#2=;
     |LIBERA #2;
     |CIERRA #2;
     #0#3 = #99#11;                || Nro. Almacen.
     #0#4 = #2#1;                  || Descrip. Articulo.
     #0#6 = #99#7;                 || Precio.
     #0#7 = #99#10;                || Importe.
     #0#8 = #99#8;                 || Dto.
     #0#9 = #0#7 < #0#8;           || Importe Total.
     #0#11 = #0#9;                 || Importe Pendiente de servir.
     #0#14 = #99#9;                || Tipo Iva.
     #0#18 = #2#2;                 || Familia.
     #0#20 = #98#4;                || Cliente.
     #0#45 = #99#17; ||partida
     |ABRE #6;
     #6#0 = #98#4;
     |LEE 000#6=;
     |LIBERA #6;
     |CIERRA #6;
     #0#19 = #6#4;                 || Tipo cliente.
     |HAZ Unidadess;
     |HAZ totallip;
     #0#38 = #0#6;
     #0#39 = #0#9;
     #0#40 = #0#6;
     #0#41 = #0#9;
     #0#42 = #0#5;
     #0#54 = #99#15 * #99#6;
     |HAZ lpacarti;
     |HAZ lpdefec;
     |HAZ lpsubcom;
     |GRABA 121#0n;
     |LIBERA #0;

     |HAZ ActuaCli;
|FPRC;

|PROCESO ActuaCli;
     |ABRE #2;
     #2#0 = #0#2;
     |LEE 000#2=;
     |CIERRA #2;

     |SI #2#194 = 2;
          nImpo = ((((#0#44 * #0#6) < #0#8) < #0#29) < #1#5) < #1#17;
     |SINO;
          nImpo = ((((#0#5 * #0#6) < #0#8) < #0#29) < #1#5) < #1#17;
     |FINSI;
     |SI #agifa142#115 = "S"; nImpo = nImpo < #1#6; |FINSI;

     eaCliente = #agifa104#4;
     efFecha = #agifa104#1;
     enImpCliPed = nImpo;
     |HAZ AcumulaCli;              |||hay que quitar lo del psion011
|FPRC;

|DEFBUCLE lee_lineas;
     #99#1;
     ;
     "P","     ",000000,001;
     "P","zzzzz",999999,999;
     be;
     NULL,pasa;
|FIN;

|PROGRAMA;
     aMoneda = "B";
     aDivisa = "EUR";
     |HAZ Divisas104;

     modo = 1;
     |ABRE #0;
     |ABRE #1;
     |BUCLE lee_lineas;
     |LIBERA #0;
     |CIERRA #0;
     |LIBERA #1;
     |CIERRA #1;
|FPRO;

|PROCESO para_ger;
     |ABRE #42;
     #42#0 = "A";
     |LEE 001#42=;
     |SI FSalida ! 0; |DDEFECTO #42; |FINSI;
     |LIBERA #42;
     |CIERRA #42;
|FPRC;

|PROCESO LeeDivisa;
     |ABRE #44;
     |ABRE #45;
     #44#0 = "A";
     |LEE 001#44=;
     |SI FSalida ! 0; |DDEFECTO #44; |FINSI;
     #45#0 = #44#9;
     |LEE 001#45=;
     |SI FSalida ! 0; |DDEFECTO #45; |FINSI;
     |LIBERA #44;
     |LIBERA #45;
     |CIERRA #44;
     |CIERRA #45;
|FPRC;
