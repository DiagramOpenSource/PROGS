              Procesos grabalineas y grabacabeza (iguales al psion003)

              Comun para psion061 y psion028


|FICHEROS;
     psion004 #1;   || Cabecera.
     psion005 #2;   || Lineas.
     agifa024 #3;   || Clientes.
     agifa019 #4;   || Articulos.
     agifa324 #15;  || Divisas-Divisas.
|FIN;

|VARIABLES;
     &enTiva = 0;
     &efFiva = @;
     &eaDiva = "";
     &enIva = 0;
     &enRec = 0;
     nBruto = 0;
     nError = 0;
     nCamBase = 0;
     nCamIva = 0;
     nCamRec = 0;
     i = 0;
     represen = "";
     nRepresen = 0;
     total = 0;
     bruto = 0;
     impuesto = 0;
|FIN;

|PROCESO Suma;
     enTiva = #2#9;
     efFiva = #1#3;
     |HAZ SacaIVA;

||T.Linea
    #2#10 = ((#2#6 * #2#7) < #2#8) ! #15#7;

||Bruto
     #1#8 = (#1#8 +. #2#10) ! #15#7;
     nBruto = (((#2#10 < #1#11) < #1#13) < #1#12);

     nError = 1;
     |SI #1#8 < 0; nError = -1; |FINSI;
     #1#8 = #1#8 * nError;

||Dto
     #1#30 = (((#1#8 < #1#11) < #1#13) < #1#12) ! #15#7;
     #1#30 = #1#30 * nError;
     #1#8 = #1#8 * nError;
     #1#30 = #1#8 - #1#30;

||Ivas
     |SI #2#9 = 0;
          #1#14 = (#1#14 +. nBruto) ! #15#7;
     |SINO;
          nCamBase = #2#9 - 1 + 15;
          nCamIva = #2#9 - 1 + 20;
          nCamRec = #2#9 - 1 + 25;
          #1nCamBase = (#1nCamBase +. nBruto) ! #15#7;
          #1nCamIva = (#1nCamBase %  enIva) ! #15#7;
          |SI #3#47 = "S";
               #1nCamRec = (#1nCamBase % enRec) ! #15#7;
          |FINSI;
     |FINSI;

||T.IVA
     #1#31 = #1#20+#1#21+#1#22+#1#23+#1#24+#1#25+#1#26+#1#27+#1#28+#1#29;
     #1#31 = #1#31 ! #15#7;

||Total
     #1#9 = (#1#8 - #1#30 + #1#31) ! #15#7;
|FPRC;

|DEFBUCLE totalalb;
     #2#1;
     ;
     #1#0,#1#2,#1#1,000;
     #1#0,#1#2,#1#1,999;
     be;
     NULL, Suma;
|FIN;


|PROCESO grabacabeza;
     #1#8 = 0;
     #1#9 = 0;
     |PARA i = 14; |SI i [ 31; |HACIENDO i = i + 1;
          #1i = 0;
     |SIGUE;

     |ABRE #3;
     #3#0 = #1#4;
     |LEE 000#3=;
     |CIERRA #3;

     |BUCLE totalalb;

     |ABRE #3;
     #3#0 = #1#4;
     |LEE 000#3=;
     #1#5 = #3#1;
     |SI #1#10 = "  "; |O #1#10 = "00";
          #1#10 = #3#25;
     |FINSI;
     |LIBERA #3;
     |CIERRA #3;

     |SI #1#7 [ 0;
         #1#6 = "N";
     |FINSI;
     |SI #1#7 ] #1#9;
         #1#6 = "S";
     |FINSI;
     |SI #1#7 < #1#9; |Y #1#7 > 0;
         #1#6 = "P";
     |FINSI;

     total = #1#9 - #1#7;
     total = total ! #15#7;
     |SI total < 0; total = -total;     |FINSI;   || valor absoluto
     |SI total [ 0.05;
         #1#6 = "S";
         #1#7 = #1#9;
     |FINSI;
|FPRC;

|PROCESO grabalineas;
     bruto    = 0;
     impuesto = 0;
     |ABRE #4;           || Articulos
     #4#0 = #2#4;
     |LEE 000#4=;
     |CIERRA #4;

     enTiva = #2#9;
     efFiva = #1#3;
     |HAZ SacaIVA;
     impuesto = enIva;

     |ABRE #3;
     #3#0 = #1#4;
     |LEE 000#3=;
     |CIERRA #3;

     |SI #3#47 = "S"; impuesto = enIva + enRec; |FINSI;

     bruto = #2#6 * #2#7;          || base.
     bruto = bruto < #2#8;         || dto.
     bruto = bruto ! #15#7;
     #2#10 = bruto;                || bruto.
|FPRC;
