|FICHEROS;
     pcegz024 #0;
     pcegz025 #1;
     pcegm004 #4;
     agifa210 #10,MANTE;
     agifa071 #71;
     pcegm023 #23;
|FIN;

|VARIABLES;
     &eaImpresora = "";
     &ser_alb = "";
     &num_alb = 0;
     &enDocu = 0;
     &eaTipo = "";
     &Tempo = "";
     &eaSerie = "";
     &enPedido = 0;
     &aDivisa = "";
     &aMoneda = "";
     aTmp0 = "";
     aTmp1 = "";
     aAlfa = "";
|FIN;

|PROCESO Tipo7; |TIPO 7;
     |ENTLINEAL #1#1, 1, 7, 22, 0, Min1, Max1;
|FPRC;

|PROCESO Tipo_11; |TIPO 11;
     |SI FSalida = 10; ||F2
          |ENTLINEAL #1#1, 1, 4, 22, 0, Min1, Max1;
     |FINSI;
     |SI FSalida = 11; ||F3
          |PUSHV 0101 2380;
          |CLS;
          #10#52 = #0#0;
          #10#0 = #0#1;
          |LEE 000#10=;
          |SI FSalida = 0;
               |PINPA #0#10;
               |PINDA #0#10;
               |ENDATOS #4#10;
          |FINSI;
          |POPV;
     |FINSI;
     |SI FSalida = 12; ||F4
          |ABRE #4;
          #4#25 = eaSerie;
          #4#0 = enPedido;
          |LEE 020#4=;
          |CIERRA #4;

          |ABRE #23;
          |SI #4#96 ! 0; || Es de reparacion
               #23#0 = "RE";
               #23#1 = #4#95;
               #23#2 = #4#96;
          |SINO;
               #23#0 = "AV";
               #23#1 = #0#0;
               #23#2 = #0#1;
          |FINSI;
          |LEE 000#23=;
          |SI FSalida = 0;
               |PUSHV 0101 2380;
               |CLS;
               |PINPA #0#23;
               |PINDA #0#23;
               |ENDATOS #4#23;
               |POPV;
          |SINO;
               |MENAV "0000El servicio no tiene etiquetas de agencia.";
          |FINSI;
          |CIERRA #23;
     |FINSI;
     |SI FSalida = 13; ||F5
          #10#52 = #0#0;
          #10#0 = #0#1;
          |LEE 000#10=;
          |SI FSalida = 0;
               aAlfa = "0000SImprimir etiquetas albaran:" + #0#0 + "/" + (("000000" + #0#1) % -106);
               |CONFI aAlfa;
               |SI FSalida = 0;
                    eaSerie = #0#0;
                    enDocu = #0#1;
                    eaTipo = "AV";
                    |SUB_EJECUTA_NP "pcegz019";
               |FINSI;
          |FINSI;
     |FINSI;
     |SI FSalida = 14; ||F6
          eaImpresora = "CrystalReport";
          ser_alb = #0#0;
          num_alb = #0#1;
          |SUB_EJECUTA_NP "agifa014";
     |FINSI;
|FPRC;

|PROCESO agifa071;
     |SI #71#29 > "XXXXX";
          |ACABA;
     |FINSI;

     #10#52 = #71#29;
     #10#0 = #71#0;
     |LEE 020#10=;

     aDivisa = #10#68;
     aMoneda = #10#70;
     |HAZ Divisas010;

     #0#0 = #10#52;
     #0#1 = #10#0;
     #0#2 = #10#1;
     #0#3 = #10#3;
     #0#4 = #10#4;
     #0#5 = #10#78;
     |LEE 000#0=;
     |SI FSalida ! 0; |GRABA 020#0n; |FINSI;

     #1#0 = #71#29;
     #1#1 = #71#0;
     #1#2 = #71#1;
     #1#3 = #71#2;
     #1#4 = #71#4;
     #1#5 = #71#5;
     |GRABA 020#1n;
|FPRC;

|DEFBUCLE agifa071;
     #71#4;
     ;
     eaSerie, enPedido, 001;
     eaSerie, enPedido, 999;
     ;
     NULL, agifa071;
|FIN;

|PROCESO Min0;
     #0#0 = "     ";
     #0#1 = 1;
|FPRC;

|PROCESO Max0;
     #0#0 = "zzzzz";
     #0#1 = 999999;
|FPRC;

|PROCESO Min1;
     #1#0 = #0#0;
     #1#1 = #0#1;
     #1#2 = 1;
|FPRC;

|PROCESO Max1;
     #1#0 = #0#0;
     #1#1 = #0#1;
     #1#2 = 999;
|FPRC;

|PROGRAMA;
     aAlfa = PARAMETRO % 103;
     |SI aAlfa = "REP";
          eaSerie = PARAMETRO % 405;
          aAlfa = PARAMETRO % 906;
          enPedido = aAlfa;
     |FINSI;

     |CLS;
     |PINPA #0#0;
     #0#0 = eaSerie;
     #0#1 = enPedido;
     |PINTA 525, #0#0;
     |PINTA 531, #0#1;
     |ENTLINEAL #1#0, 1, 7, 14, 0, Min0, Max0;
     |ENTLINEAL #1#1, 1, 7, 22, 0, Min1, Max1;

     |HAZ CreaTempo; |NOME_DAT #0 Tempo; |DELINDEX #0; aTmp0 = Tempo;
     |HAZ CreaTempo; |NOME_DAT #1 Tempo; |DELINDEX #1; aTmp1 = Tempo;

     |ABRE #0;
     |ABRE #1;
     |ABRE #10;
     |BUCLE agifa071;
     |CIERRA #10;
     |CIERRA #1;
     |CIERRA #0;

     |ABRE #10;
     |ENTLINEAL #1#1, 1, 7, 22, 0, Min1, Max1;
     |ENTLINEAL #1#0, 1, 4, 14, 0, Min0, Max0;
     |CIERRA #10;

     |DELINDEX #0; Tempo = aTmp0; |HAZ BoraTempo;
     |DELINDEX #1; Tempo = aTmp1; |HAZ BoraTempo;
|FPRO;
