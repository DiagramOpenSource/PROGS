     || Importacion XML

|FICHEROS;
     malpe148 #0;
     agifa019 #19;
     malpe147 #147; |Control
     malpe149 #149;
     malpe150 #150;
|FIN;

|VARIABLES;
     aIP = "";
     aDir = "";
     aDSCorreo = "";
     aUsu = "";
     aPwd = "";
     aPop3 = "";
     aNombre = "";
     aMensaje = "";
     nCorreos = 0;
     i = 0;

     nPos = 0;
     nRem = 0;
     aAlfa = "";
     aPath = "";
     aCar = "";
     nHandle = 0;
     nPrime = 0;
     nFinCod = 0;
     aDes = "";
     aFic = "";
     aVal = "";
     nNivel = 0;
     aTmp = "";
     aIniCab = "";
     aIniLin = "";
     nCampo = 0;
     {100}Matriz = "";
     nReg = 0;

     aLon = "";
     aTag = "";
     aIniTag = "N";
     aFinTag = "N";
     aIniPro = "N";
     aFinPro = "N";
     aFinTagVacio = "N";
     nError = 0;
     nSwLin = 0;
     nSwCab = 0;
     nLinea = 0;
     aDia = "";
     aMes = "";
     aA¤o = "";
     nNume = 0;
     aNom = "";
     j = 0;
|FIN;

|PROCESO Fecha;
     aDia = aVal % 102;
     aMes = aVal % 402;
     aA¤o = aVal % 704;
     |SI aDia = 0; aDia = "01"; |FINSI;
     |SI aMes = 0; aMes = "01"; |FINSI;
     aVal = aDia + "." + aMes + "." + aA¤o;
|FPRC;

|PROCESO DivPrecio;
     #19#0 = #150#2;
     |LEE 000#19=;
     |SI FSalida = 0; |Y #19#181 = "S";
          ||mortero
     |SINO;
          nNume = aVal;
          nNume = nNume / 1000;
          aVal = nNume;
     |FINSI;
|FPRC;

|PROCESO CamposLin;
     nCampo = -1;
     |SI aTag = "CODIGO"; nCampo = 2; |FINSI;
     |SI aTag = "LOTE"; nCampo = 3; |FINSI;
     |SI aTag = "CODESTANDAR"; nCampo = 4; |FINSI;
     |SI aTag = "DESCRIPCION"; nCampo = 5; |FINSI;
     |SI aTag = "CANTIDAD"; nCampo = 6; |FINSI;
     |SI aTag = "VOLUMEN"; nCampo = 7; |FINSI;
     |SI aTag = "PRECIO"; |HAZ DivPrecio; nCampo = 8; |FINSI;
     |SI aTag = "DESCUENTO"; nCampo = 9; |FINSI;
     |SI aTag = "TRANSPORTE"; nCampo = 10; |FINSI;
     |SI aTag = "PRECIOTRANSPORTE";|HAZ DivPrecio; nCampo = 11; |FINSI;

     |SI nCampo ] 0; #150#nCampo# = aVal; |FINSI;
|FPRC;

|PROCESO CamposCab;
     nCampo = -1;
     |SI aTag = "CODIGO"; nCampo = 0; |FINSI;
     |SI aTag = "EMPRESACRM"; nCampo = 1; |FINSI;
     |SI aTag = "OFICINACRM"; nCampo = 2; |FINSI;
     |SI aTag = "COMERCIALCRM"; nCampo = 3; |FINSI;
     |SI aTag = "CODPROMOTOR"; nCampo = 4; |FINSI;
     |SI aTag = "PROMOTOR"; nCampo = 5; |FINSI;
     |SI aTag = "CODCONSTRUCTOR"; nCampo = 6; |FINSI;
     |SI aTag = "CONSTRUCTOR"; nCampo = 7; |FINSI;
     |SI aTag = "CODARQUITECTO"; nCampo = 8; |FINSI;
     |SI aTag = "ARQUITECTO"; nCampo = 9; |FINSI;
     |SI aTag = "FECHAOFERTA"; |HAZ Fecha; nCampo = 10; |FINSI;
     |SI aTag = "CODFORMAPAGO"; nCampo = 11; |FINSI;
     |SI aTag = "FORMAPAGO"; nCampo = 12; |FINSI;
     |SI aTag = "CUENTABANCARIA"; nCampo = 13; |FINSI;
     |SI aTag = "INCLUIRCUENTAB"; nCampo = 14; |FINSI;
     |SI aTag = "AGENCIATRANS"; nCampo = 15; |FINSI;
     |SI aTag = "PORTES"; nCampo = 16;
          |SI aVal = "S"; aVal = "P"; |FINSI;
          |SI aVal = "N"; aVal = "D"; |FINSI;
     |FINSI;
     |SI aTag = "DIASPAGO"; nCampo = 17; |FINSI;
     |SI aTag = "FECHASERVICIO"; |HAZ Fecha; nCampo = 18; |FINSI;
     |SI aTag = "VALIDEZ"; nCampo = 19; |FINSI;
     |SI aTag = "PLAZO"; nCampo = 20; |FINSI;
     |SI aTag = "PRECIOAPIE"; nCampo = 21; |FINSI;
     |SI aTag = "SITUACION"; nCampo = 22; |FINSI;
     |SI aTag = "MOTIVO"; nCampo = 23; |FINSI;
|| aunque esten en el xml, siempre son vacios
|| y el campo 24/25 se usa como pedido de gestion
||     |SI aTag = "SERIEPEDIDO"; nCampo = 24; |FINSI;
||     |SI aTag = "NUMPEDIDO"; nCampo = 25; |FINSI;
     |SI aTag = "PROVINCIAOFERTA"; nCampo = 26; |FINSI;
     |SI aTag = "CODCONTACTO"; nCampo = 27; |FINSI;
     |SI aTag = "CONTACTO"; nCampo = 28; |FINSI;
     |SI aTag = "TELEFONO"; nCampo = 29; |FINSI;
     |SI aTag = "FAX"; nCampo = 30; |FINSI;
     |SI aTag = "EMAIL"; nCampo = 31; |FINSI;
     |SI aTag = "DOMICILIO"; nCampo = 32; |FINSI;
     |SI aTag = "APARTADO"; nCampo = 33; |FINSI;
     |SI aTag = "CODPOSTAL"; nCampo = 34; |FINSI;
     |SI aTag = "POBLACION"; nCampo = 35; |FINSI;
     |SI aTag = "PROVINCIA"; nCampo = 36; |FINSI;
     |SI aTag = "PAIS"; nCampo = 37; |FINSI;
     |SI aTag = "DESCRIPCION"; nCampo = 38; |FINSI;
     |SI aTag = "NOTAS"; nCampo = 39; |FINSI;
     |SI aTag = "OBSERVACIONES1"; nCampo = 40; |FINSI;
     |SI aTag = "OBSERVACIONES2"; nCampo = 41; |FINSI;
     |SI aTag = "OBSERVACIONES3"; nCampo = 42; |FINSI;
     |SI aTag = "OBSERVACIONES4"; nCampo = 43; |FINSI;
     |SI aTag = "OBSERVACIONESFABRICA"; nCampo = 44; |FINSI;
     |SI aTag = "CODRESPONSABLEOBRA"; nCampo = 45; |FINSI;
     |SI aTag = "RESPONSABLEOBRA"; nCampo = 46; |FINSI;
     |SI aTag = "TELEFONOOBRA"; nCampo = 47; |FINSI;
     |SI aTag = "MOVILOBRA"; nCampo = 48; |FINSI;
     |SI aTag = "FAXOBRA"; nCampo = 49; |FINSI;
     |SI aTag = "DOMICILIOOBRA"; nCampo = 50; |FINSI;
     |SI aTag = "CODPOSTALOBRA"; nCampo = 51; |FINSI;
     |SI aTag = "POBLACIONOBRA"; nCampo = 52; |FINSI;
     |SI aTag = "PROVINCIAOBRA"; nCampo = 53; |FINSI;
     |SI aTag = "REFERENCIAOBRA"; nCampo = 77; |FINSI;
     |SI aTag = "DESCRIPCIONOBRA"; nCampo = 54; |FINSI;
     |SI aTag = "DOMICILIOFACTURA"; nCampo = 55; |FINSI;
     |SI aTag = "CODPOSTALFACTURA"; nCampo = 56; |FINSI;
     |SI aTag = "POBLACIONFACTURA"; nCampo = 57; |FINSI;
     |SI aTag = "PROVINCIAFACTURA"; nCampo = 58; |FINSI;
     |SI aTag = "CODCONTACTOFACTURA"; nCampo = 59; |FINSI;
     |SI aTag = "CONTACTOFACTURA"; nCampo = 60; |FINSI;
     |SI aTag = "TELEFCONTACTOFACTURA"; nCampo = 61; |FINSI;
     |SI aTag = "MOVILCONTACTOFACTURA"; nCampo = 62; |FINSI;
     |SI aTag = "FAXCONTACTOFACTURA"; nCampo = 63; |FINSI;
     |SI aTag = "NOMBREEMPRESA"; nCampo = 64; |FINSI;
     |SI aTag = "NIFEMPRESA"; nCampo = 65; |FINSI;
     |SI aTag = "DOMICILIOENVIO"; nCampo = 66; |FINSI;
     |SI aTag = "CODPOSTALENVIO"; nCampo = 67; |FINSI;
     |SI aTag = "POBLACIONENVIO"; nCampo = 68; |FINSI;
     |SI aTag = "PROVINCIAENVIO"; nCampo = 69; |FINSI;
     |SI aTag = "DOMICILIOFISCAL"; nCampo = 70; |FINSI;
     |SI aTag = "CODPOSTALFISCAL"; nCampo = 71; |FINSI;
     |SI aTag = "POBLACIONFISCAL"; nCampo = 72; |FINSI;
     |SI aTag = "PROVINCIAFISCAL"; nCampo = 73; |FINSI;
     |SI aTag = "ENCARGADO"; nCampo = 78; |FINSI;
     |SI aTag = "TELEFONOENCARGADO"; nCampo = 79; |FINSI;

     |SI nCampo ] 0; #149#nCampo# = aVal; |FINSI;
|FPRC;

|PROCESO ProcesaTagPop;
     nCampo = -1;
     |SI nSwCab = 1;
          |HAZ CamposCab;
          |SI aTag = "OFERTA";
               |GRABA 000#149n;
               |SI FSalida ! 0;
                    aAlfa = "0000Oferta " + #149#0 + " ya existe...";
                    |MENAV aAlfa;
               |FINSI;
               nError = FSalida;
               nSwCab = 0;
               nLinea = 0;
          |FINSI;
     |FINSI;
     |SI nSwLin = 1;
          |HAZ CamposLin;
          |SI aTag = "PRODUCTOOFERTA"; |O aTag = "LOTEOFERTA";
               nLinea = nLinea + 1;
               #150#0 = #149#0;
               #150#1 = nLinea;
               |GRABA 020#150n;
               nSwLin = 0;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO ProcesaTagPush;
     |SI aTag = "OFERTA";
          nSwCab = 1;
          |DDEFECTO #149;
     |FINSI;
     |SI aTag = "PRODUCTOOFERTA"; |O aTag = "LOTEOFERTA";
          nSwLin = 1;
          |DDEFECTO #150;
     |FINSI;
|FPRC;

|PROCESO ProcesaTagVacio;
|FPRC;

|PROCESO ProcesaPrologo;
|FPRC;

|PROCESO ProcesaRem;
|FPRC;

|PROCESO PopTag;
     nNivel = nNivel - 1;
     IMatriz = Matriz1<-;
     IMatriz = IMatriz + nNivel;
     FSalida = Matriz;
|FPRC;

|PROCESO PushTag;
     IMatriz = Matriz1<-;
     IMatriz = IMatriz + nNivel;
     Matriz = aTag;
     nNivel = nNivel + 1;
|FPRC;

|PROCESO Caracter;
     |SI aCar < " "; |O nError ! 0; |ACABA; |FINSI;

     |SI nRem > 0;
          |SI nRem < 3;
               |SI aCar = "-";
                    nRem = nRem + 1;
               |SINO;
                    aAlfa = "0000ERROR en XML: " + aTag;
                    |MENAV aAlfa;
                    nError = 1;
               |FINSI;
          |SINO;
               |SI aCar = ">"; || Un comentario no puede tener el caracter '>'
                    aAlfa = aTag % -102;
                    |SI aAlfa = "--";
                         aLon = aTag % A0;
                         nPos = (aLon - 2) + 100;
                         aTag = aTag % nPos;
                         |HAZ ProcesaRem;
                    |SINO;
                         aAlfa = "0000ERROR en XML: " + aTag;
                         |MENAV aAlfa;
                         nError = 1;
                    |FINSI;
                    aTag = ""; nRem = 0;
               |SINO;
                    aLon = aTag % A0;
                    |SI aLon < 250; aTag = aTag + aCar; |FINSI;
               |FINSI;
          |FINSI;
          |ACABA;
     |FINSI;

     |SI aCar = "<";
          aIniTag = "S"; aTag = "";
     |SINO;
          |SI aIniTag = "S";
               |SI aCar = "/";
                    |SI aTag = "";
                         aFinTag = "S";
                    |SINO;
                         aFinTagVacio = "S";
                    |FINSI;
               |SINO;
                    |SI aCar = "!";
                         |SI aTag = "";
                              nRem = 1;
                         |SINO;
                              aAlfa = "0000ERROR en XML: " + aTag;
                              |MENAV aAlfa;
                              nError = 1;
                         |FINSI;
                         |ACABA;
                    |FINSI;
                    |SI aCar = "?";
                         |SI aIniPro = "S";
                              aFinPro = "S";
                              aIniPro = "N";
                         |SINO;
                              |SI aFinPro = "S";
                                   aAlfa = "0000ERROR en XML: " + aTag;
                                   |MENAV aAlfa;
                                   nError = 1;
                              |SINO;
                                   aIniPro = "S"; aTag = "";
                              |FINSI;
                         |FINSI;
                    |SINO;
                         |SI aCar = ">";
                              |QBT aTag; ||*** solo deberia quitarse a izq y der.
                              |SI aFinPro = "S";
                                   |HAZ ProcesaPrologo;
                                   aFinPro = "N";
                              |SINO;
                                   |SI aFinTag = "S";
                                        |HAZ PopTag;
                                        |SI aTag ! FSalida; ||*** fallaria si en el tag
                                                            ||    se definen 'atributos'
                                             aAlfa = "0000ERROR en XML: <" + FSalida + "> " + aVal;
                                             |MENAV aAlfa;
                                             nError = 1;
                                        |SINO;
                                             |HAZ ProcesaTagPop;
                                        |FINSI;
                                   |SINO;
                                        |SI aFinTagVacio = "S";
                                             |HAZ ProcesaTagVacio;
                                             aFinTagVacio = "N";
                                        |SINO;
                                             |HAZ PushTag;
                                             |HAZ ProcesaTagPush;
                                        |FINSI;
                                   |FINSI;
                              |FINSI;
                              aIniTag = "N"; aFinTag = "N"; aTag = ""; aVal = "";
                         |SINO;
                              aLon = aTag % A0;
                              |SI aLon < 250; aTag = aTag + aCar; |FINSI;
                         |FINSI;
                    |FINSI;
               |FINSI;
          |SINO;
               aLon = aVal % A0;
               |SI aLon < 250; aVal = aVal + aCar; |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO ProcesaXml;
     nError = 0;
     |ABRE #19;
     |ABRE #149;
     |ABRE #150;
     |XABRE "BC", aFic, nHandle;
     |SI FSalida ] 0;
          |XLEE nHandle, 250, aTmp;
          |PARA; |SI FSalida > 0; |HACIENDO;
               |PARA; |SI aTmp ! ""; |HACIENDO;
                    aCar = aTmp % 101;
                    |SI aTmp = aCar;
                         aTmp = ""; ||con un caracter falla: "aTmp = aTmp % 2"
                    |SINO;
                         aTmp = aTmp % 2;
                    |FINSI;
                    |HAZ Caracter;
                    |SI nError ! 0; |SAL; |FINSI;
               |SIGUE;
               |XLEE nHandle, 250, aTmp;
               |SI nError ! 0; |SAL; |FINSI;
          |SIGUE;
          |XCIERRA nHandle;
          |SI nError = 0;
               aDes = aFic - ".dat" + ".imp";
               |RRENOMBRA_FICHERO aFic, aDes;
               nReg = nReg + 1;
          |FINSI;
     |SINO;
          aAlfa = "0000ERROR al abrir" + aFic;
          |MENAV aAlfa;
     |FINSI;
     |CIERRA #150;
     |CIERRA #149;
     |CIERRA #19;
|FPRC;

|PROCESO Recoge;
     aDir = #147#1; |QBF aDir;
     |SI #147#2 = "F";
          aDSCorreo = #147#3;  |QBF aDSCorreo;
     |SINO;
          |SI #147#2 = "L";   |IP_REMOTO aDSCorreo;    |FINSI;
          |SI #147#2 = "S";   |IP_LOCAL aDSCorreo;     |FINSI;
     |FINSI;
     aUsu = #147#5; |QBF aUsu;
     aPwd = #147#6; |QBF aPwd;
     aPop3 = #147#4; |QBF aPop3;
     aNombre = "";

     aAlfa = "0000Recibiendo [" + aDSCorreo + "]";
     |MENSA aAlfa;

     aIP = "";
     |IP_REMOTO aIP;
     |SI aIP ! "";
          aDes = aDir;
          |DFICE aDir;
     |FINSI;

     || El dscorreo deja el adj en el servidor
     |DSCORREO_RECIBE aDSCorreo, aPop3, aUsu, aPwd, aDir, aNombre;

     nError = FSalida;

     |BLIN 4;
     |SI nError < 0;
          aMensaje = "    Problemas al recibir correo ..." + nError;
          |MENAV aMensaje;
     |FINSI;
     |SI nError = 0;
          || le quito los posbles ";"
          |PARA FSalida = 1; |SI FSalida ! 0; |HACIENDO;
               aNombre = aNombre - ";"
          |SIGUE;

          aMensaje = "    Correo recibido con exito [" + nError + "][" + aNombre + "]";
          |MENSA aMensaje;
          |SI aIP ! "";
               || y lo copiamos al local
               |HAZ SacaNombre;
               aDes = aDes + aNom;
               |ENVIA_FICHERO aNombre, aDes;
               || y lo borro del servidor
               |FBORRA aDir;
          |FINSI;
     |FINSI;
     |SI nError = 1;
          |SI nCorreos = 1;
               |MENAV "    No hay correo ...";
          |SINO;
               i = nCorreos - 1;
               aAlfa = "0000" + i + " Correos recibidos...";
               |MENAV aAlfa;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO RecibeCorreo;
     nCorreos = 0;
     |PARA; |SI; |HACIENDO;
          nError = 0;
          nCorreos = nCorreos + 1;
          |HAZ Recoge;
          |SI nError ! 0; |SAL; |FINSI;
          |SLEEP 2;
     |SIGUE;
|FPRC;

|PROCESO Tipo3; |TIPO 3;
     |SI #0#1 ! "SI"; |ACABA; |FINSI;
     |ABRE #147; |LEE 000#147p; |CIERRA #147;

     |HAZ RecibeCorreo;

     aPath = #147#1; |QBF aPath;
     aPath = aPath + "*.dat";

     |R_PDIR aPath;
     |PARA; |SI FSalida = 0; |HACIENDO;
          aFic = aPath;
          |BLIN 23; |PINTA 2301, aFic;
          |HAZ ProcesaXml;
          |R_SDIR aPath;
     |SIGUE;

     aAlfa = "0000Importados " + nReg + " ficheros.";
     |MENAV aAlfa;
|FPRC;

|PROGRAMA;
     |CLS;
     |ABRE #0;
     |LEE 101#0=;
     |SI FSalida ! 0; |GRABA 020#0b; |FINSI;
     #0#1 = "NO";
     |PINPA #0#0;
     |PINDA #0#0;
     |ENDATOS #1#0;
     |SI FSalida = 0;
          |GRABA 020#0a;
     |FINSI;
     |CIERRA #0;
|FPRO;

|PROCESO SacaNombre;
     aNom = "";
     aLon = aNombre % A0;
     |PARA j = 1; |SI j [ aLon; |HACIENDO j = j + 1;
          nPos = -(j * 100 + 1);
          aAlfa = aNombre % nPos;
          |SI aAlfa = "/"; |O aAlfa = "\"; |SAL; |FINSI;
          aNom = aAlfa + aNom;
     |SIGUE;
|FPRC;
