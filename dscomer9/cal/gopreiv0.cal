     PonDesglose, Chequea

|FICHEROS;
     gopre002 #2;
     gopre003 #3;
     gopre004 #4;
     gomedici #5;
     agifa019 #19;
     agi00063 #63;
|FIN;

|VARIABLES;
     aAlfa = "";
     &enErr = 0;
     &eaErr = "";
     &enPrecio = 0;

     &aIveCodigoSerie     = "XXXX";
     &aIveCodigoPresupuesto = "XXXX";
     &eaIndIncr = "";

     nHayDes = 0;
     nHayPar = 0;
     nPrime = 0;
     nInfor = 1;
     nImpre = 1;
     nNume = 0;
     nAjusta = 0;
     nHayMed = 0;
|FIN;

|PROCESO Print;
     |SI nPrime = 0;
          nPrime = 1;
          |CONFI "0000SImprimir Errores...";
          |SI FSalida = 0;
               |IMPRE 0;
               nImpre = FSalida;
               |SI FSalida = 0;
                    |INFOR "gopreivi";
                    nInfor = FSalida;
               |FINSI;
          |FINSI;
     |FINSI;
     |PRINT;
|FPRC;

|PROCESO gomedici;
     nHayMed = 1;
|FPRC;

|DEFBUCLE gomedici;
     #5#1;
     ;
     #3#0, #3#1, #3#2, #3#4, 001;
     #3#0, #3#1, #3#2, #3#4, 999;
     ;
     NULL, gomedici;
|FIN;

|PROCESO gopre004;
     nHayDes = 1;
|FPRC;

|DEFBUCLE gopre004;
     #4#1;
     ;
     #3#0, #3#1, #3#2, #3#4, 001;
     #3#0, #3#1, #3#2, #3#4, 999;
     ;
     NULL, gopre004;
|FIN;

|PROCESO gopre003;
     nHayDes = 0;
     nHayPar = 1;
     |BUCLE gopre004;
     |SI nHayDes = 0;
          enErr = 1; eaErr = "SIN DESGLOSE"; |HAZ Print;
     |FINSI;

     |SI #3#9 = 0;
          enErr = 4; eaErr = "CON PRECIO 0"; |HAZ Print;
     |FINSI;

     nHayMed = 0;
     |BUCLE gomedici;
     |SI nHayMed = 0;
          enErr = 5; eaErr = "CON MEDIDA 0"; |HAZ Print;
     |FINSI;

     #2#7 = #2#7 + #3#10;
|FPRC;

|DEFBUCLE gopre003;
     #3#1;
     ;
     #2#0, #2#1, #2#12, 001;
     #2#0, #2#1, #2#12, 999;
     ;
     NULL, gopre003;
|FIN;

|PROCESO gopre002;
     nHayPar = 0;
     enPrecio = #2#7;
     #2#7 = 0;
     |BUCLE gopre003;
     |SI nHayPar = 0;
          enErr = 2; eaErr = "SIN PARTIDAS"; |HAZ Print;
     |SINO;
          |SI #2#7 ! enPrecio;
               enErr = 3; eaErr = "PRECIO DIFERENTE"; |HAZ Print;
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE gopre002;
     #2#1;
     ;
     aIveCodigoSerie, nNume, 001;
     aIveCodigoSerie, nNume, 999;
     ;
     NULL, gopre002;
|FIN;

|PROCESO Chequea;
     nNume = aIveCodigoPresupuesto;

     |BUCLE gopre002;
     |SI nPrime ! 0;
          |SI nInfor = 0; |PIEINF;  |FININF; |FINSI;
          |SI nImpre = 0; |FINIMP; |FINSI;
     |FINSI;
|FPRC;
====================================================================
|PROCESO Desglose;
     nHayDes = 1;
|FPRC;

|DEFBUCLE Desglose;
     #4#1;
     ;
     #3#0, #3#1, #3#2, #3#4, 001;
     #3#0, #3#1, #3#2, #3#4, 999;
     ;
     NULL, Desglose;
|FIN;

|PROCESO Partidas;
     nHayDes = 0;
     |BUCLE Desglose;
     |SI nHayDes = 0;
          |SELECT #6#2;
          #2#0 = #3#0;
          #2#1 = #3#1;
          #2#12 = #3#2;
          |LEE 000#2=;

          |ABRE #4;
          #4#0 = #3#0;
          #4#1 = #3#1;
          #4#2 = #3#2;
          #4#3 = #3#4;
          #4#4 = 1;
          #4#5 = #3#4;
          #4#6 = #3#5;
          #4#7 = #3#6;

          || Corrige importes partida
          |SI #3#7 = 0; #3#7 = 1; |FINSI;
          |SI #3#8 = 0; #3#8 = #2#7; |FINSI;
          |SI #3#9 = 0; #3#9 = #2#8; |FINSI;
          #3#10 = #3#8 * #3#7;
          #3#11 = #3#9 * #3#7;

          #4#8 = 1;            || Uni
          #4#9 = #3#8;         || Pre Vta
          #4#10 = #3#9;        || Pre Cos
          #4#11 = #3#8 * #4#8; || Imp. Vta
          #4#12 = #3#9 * #4#8; || Imp .Cos
          #4#13 = 0;           || Incr.

          |DDEFECTO #19;
          #19#0 = #4#5;
          |LEE 000#19=;
          #4#14 = #19#126;

          |DDEFECTO #63;
          #63#0 = #19#126;
          |LEE 000#63=;
          #4#15 = #63#1;

          |GRABA 020#4n;
          |CIERRA #4;
     |FINSI;
     |GRABA 020#3a;
     |LIBERA #3;
|FPRC;

|DEFBUCLE Partidas;
     #3#1;
     ;
     aIveCodigoSerie, nNume, "                    ", 001;
     aIveCodigoSerie, nNume, "zzzzzzzzzzzzzzzzzzzz", 999;
     be;
     NULL, Partidas;
|FIN;

|PROCESO PonDesglose; || A¤ade desglose de componentes, si no existe
     nNume = aIveCodigoPresupuesto;
     |ABRE #19;
     |ABRE #63;
     |ABRE #2;
     |BUCLE Partidas;
     |CIERRA #2;
     |CIERRA #63;
     |CIERRA #19;
|FPRC;
