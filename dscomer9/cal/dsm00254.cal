|FICHEROS;
     dsm00254 #0;
|FIN;

|VARIABLES;
     aCarpeta = "unidades";
     aAlfa = "";
     nModo = 0;
     nBoton = 0;
     aRuta = "";
     aDes = "";
     aOrg = "";
     aIP = "";
     nBotonImg = -1;
     {1}aLocal = "";
|FIN;

|PROCESO Tipo8; |TIPO 8;
     |CONTROL_SIMPLE 0, "BOTON,F5.- Asignar Imagen ", 0649, 0663, F5;
     nBoton = FSalida;
|FPRC;

|PROCESO Tipo9; |TIPO 9;
     |FIN_CONTROL_WINDOWS nBoton;
     |FIN_CONTROL_WINDOWS nBotonImg;
     |PULSATECLA;
     |ESPECIFICA "RBASS", aLocal;
     aDes = aLocal;
     aDes = aDes + Usuario + ".bmp";
     |IP_REMOTO aIP;
     |SI aIP ! "";
          |RFBORRA aDes;
     |SINO;
          |FBORRA aDes;
     |FINSI;
|FPRC;

|PROCESO F5;
     |PTEC 827;
|FPRC;

|PROCESO Tecla; |TIPO 0;
     |SI FSalida ! 13; |ACABA; |FINSI;  || F5

     |LEE 000#0=;
     |SI FSalida ! 0; |ACABA; |FINSI;

     aRuta = "F*.bmp";
     |BROWSE aRuta;
     aRuta = aRuta % 2;
     |QBF aRuta;
     |SI aRuta = ""; |ACABA; |FINSI;
     aAlfa = aRuta % -104; aAlfa = aAlfa < "";
     |SI aAlfa ! ".bmp";
          |MENAV "0000El formato debe ser BMP";
          |ERROR; |ACABA;
     |FINSI;

     aOrg = aRuta;
     |DBASE aDes;
     aDes = aDes + "imagen/";
     |MKDIR aDes;
     aDes = aDes + aCarpeta + "/";
     |MKDIR aDes;
     aDes = aDes + #0#0; |QBF aDes;

     aIP = "";
     |IP_REMOTO aIP;
     |SI aIP ! "";
          |RECIBE_FICHERO aOrg, aDes;
     |SINO;
          |COPIA_FICHERO aOrg, aDes;
     |FINSI;
     |HAZ Tipo13;
|FPRC;

|PROCESO Tipo13; |TIPO 13;
     |SI nBotonImg ! -1;
          |FIN_CONTROL_WINDOWS nBotonImg;
     |FINSI;
     |DBASE aOrg;
     aOrg = aOrg + "imagen/" + aCarpeta + "/" + #0#0; |QBF aOrg;
     |ESPECIFICA "RBASS", aLocal;
     aDes = aLocal;
     aDes = aDes + Usuario + ".bmp";
     aIP = "";
     |IP_REMOTO aIP;
     |SI aIP ! "";
          |RFBORRA aDes;
          |ENVIA_FICHERO aOrg, aDes;
     |SINO;
          |FBORRA aDes;
          |COPIA_FICHERO aOrg, aDes;
     |FINSI;
     aDes = "IMAGEN," + aDes;
     |CONTROL_SIMPLE 0, aDes , 1327, 2249, NULL;
     nBotonImg = FSalida;
|FPRC;

|PROCESO Tipo7; |TIPO 7;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 1;
          |SI nBotonImg ! -1;
               |FIN_CONTROL_WINDOWS nBotonImg;
          |FINSI;
          aDes = "IMAGEN," + "";
          |CONTROL_SIMPLE 0, aDes , 1327, 2249, NULL;
     |FINSI;
|FPRC;
