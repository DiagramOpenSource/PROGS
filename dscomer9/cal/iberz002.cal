|FICHEROS;
     iberz002 #0;
     iberca06 #3;
     iber0013 #4;
     iberw004 #1,MANTE;   ||| BORRAR COMPLETO EN CASO DE BLOQUEO
     agifa019 #2,MANTE;
     iber0031 #31,MANTE;
     iber0032 #32,MANTE;
     iber0033 #33,MANTE;
     agifa065 #45;
     iber0209 #209;
     iber0067 #67;
|FIN;

|VARIABLES;
     &eaArticulo = "";
     &nBajaArt   = 0;
     wT          = 0;
     bAlfa       = "";
     cAlfa       = "";
     nContador   = 0;
     nModo       = 0;
     nEstado     = 0;
     nForma      = 0;

     aFichero    = "";
     aDArticulo  = "";
     aHArticulo  = "";
     aAlfa       = "";
     aSubFamilia = "";
     nSwCarga1   = 0;
     aLon        = "";
|FIN;

|| Procesos del iber0033

|PROCESO Tipo7; |TIPO 7;
     |ENTLINEAL #1#1, -1, 7, 22, 0, ClaveBaja1, ClaveAlta1;
|FPRC;

|PROCESO MiraFam; |TIPO 0;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo ! 1; |ACABA; |FINSI;

     #2#0 = #33#2;
     |LEE 000#2=;
     |SI #2#3 ! #33#1;
          |MENAV "0000Familia Incorrecta...";
          |ERROR;
     |FINSI;
|FPRC;

|| Procesos del iber0032

|PROCESO Borra33;
     |BORRA 020#33a;
|FPRC;

|DEFBUCLE Borra33;
     #33#1;
     ;
     #32#0, #32#1, "                    ";
     #32#0, #32#1, "zzzzzzzzzzzzzzzzzzzz";
     be;
     NULL, Borra33;
|FIN;

|RUTINA ClaveBaja33;
     #33#0 = #32#0;
     #33#1 = #32#1;
     #33#2 = "                    ";
|FRUT;

|RUTINA ClaveAlta33;
     #33#0 = #32#0;
     #33#1 = #32#1;
     #33#2 = "zzzzzzzzzzzzzzzzzzzz";
|FRUT;

|PROCESO Carga; |TIPO 11;
     |SI FSalida = 13;
          nSwCarga1 = 1;
          |PTEC 807;
     |FINSI;
|FPRC;

|PROCESO CargaSub;
     #33#0 = #32#0;
     #33#1 = #32#1;
     #33#2 = #2#0;
     #33#3 = #2#1
     |GRABA 000#33n;
|FPRC;

|DEFBUCLE CargaSub;
     #2#1;
     3;
     "                    ", #32#1;
     "zzzzzzzzzzzzzzzzzzzz", #32#1;
     ;
     NULL, CargaSub;
|FIN;

|PROCESO TeclaSub; |TIPO 11;
     |SI FSalida = 12;
           |PUSHV 0501 2380;
           |ENTLINEAL #1#33 , -1 , 4 , 22 , 1 , ClaveBaja33 , ClaveAlta33;
           |POPV;
           |ERROR;
     |FINSI;
|FPRC;

|PROCESO Configu; |TIPO 2;
     nForma = 0 +. 1;
     nModo = (FEntrada / 100)  ! 0;
     |SI nForma = 1;
          |PUSHV 0501 2380;
          |PARA nSwCarga1 = 1; |SI nSwCarga1 = 1; |HACIENDO;
               nSwCarga1 = 0;
               |ENTLINEAL #1#33 , -1 , 2 , 22 , 1 , ClaveBaja33 , ClaveAlta33;
               |SI nSwCarga1 = 1;
                    |ABRE #33;
                    |BUCLE CargaSub;
                    |CIERRA #33;
               |FINSI;
          |SIGUE;
          |POPV;
     |FINSI;
     |SI nModo = 3;
          |BUCLE Borra33;
     |FINSI;
|FPRC;

|| Procesos del iberw004

|PROCESO Borra31;
     |BORRA 020#31a;
|FPRC;

|DEFBUCLE Borra31;
     #31#1;
     ;
     #2#0, "                    ";
     #2#0, "zzzzzzzzzzzzzzzzzzzz";
     be;
     NULL, Borra31;
|FIN;

|PROCESO Borra32;
     |BUCLE Borra33;
     |BORRA 020#32a;
|FPRC;

|DEFBUCLE Borra32;
     #32#1;
     ;
     #2#0, "   ";
     #2#0, "zzz";
     be;
     NULL, Borra32;
|FIN;

|RUTINA ClaveBaja32;
     #32#0 = #2#0;
     #32#1 = "   ";
|FRUT;

|RUTINA ClaveAlta32;
     #32#0 = #2#0;
     #32#1 = "zzz";
|FRUT;

|RUTINA ClaveBaja31;
     #31#0 = #2#0;
     #31#1 = "                    ";
|FRUT;

|RUTINA ClaveAlta31;
     #31#0 = #2#0;
     #31#1 = "zzzzzzzzzzzzzzzzzzzz";
|FRUT;

|PROCESO Producto;
     aAlfa = aSubFamilia + (("000" + #1#1) % -103);
     #2#0  = aAlfa;
     |LEE 040#2=;
     |SI FSalida ! 0;  |DDEFECTO #2;  |FINSI;

     |SI #1#5 ! Contenido;
||          |BUCLE Borra31;
||          |BUCLE Borra32;
     |FINSI;

     |SI #1#5 = "F";
          |PUSHV 0501 2380;
     ||     |ENTLINEAL #1#31 , -1 , 2 , 22 , 1 , ClaveBaja31 , ClaveAlta31;
          |ENTLINEAL #1#32 , -1 , 2 , 22 , 1 , ClaveBaja32 , ClaveAlta32;
          |POPV;
     |FINSI;

     |SI #1#5 = "P";
          |PUSHV 0501 2380;
          |ENTLINEAL #1#32 , -1 , 2 , 22 , 1 , ClaveBaja32 , ClaveAlta32;
          |POPV;
     |FINSI;
|FPRC;

|PROCESO agifa065b;
     wT = 1;
     |ERROR10;
|FPRC;

|DEFBUCLE agifa065b;
     #45#1;
     ;
     bAlfa,"01.01.0000","  ",000000,00000,"      ";
     bAlfa,"31.12.2300","zz",999999,99999,"zzzzzz";
     ;
     NULL,agifa065b;
|FIN;

|PROCESO iber0209;
     #209#3 = #2#1;
     |GRABA 020#209a;
     |LIBERA #209;
|FPRC;

|DEFBUCLE iber0209;
     #209#1;
     ;
     "     ", "01.01.0000", #2#0;
     "zzzzz", "31.12.9999", #2#0;
     be;
     NULL, iber0209;
|FIN;

|PROCESO iber0067;
     #67#5 = #2#1;
     |GRABA 020#67a;
     |LIBERA #67;
|FPRC;

|DEFBUCLE iber0067;
     #67#1;
     ;
     001, 01, 01, "01.01.0000", #2#0, " ";
     999, 99, 99, "31.12.9999", #2#0, "z";
     be;
     NULL, iber0067;
|FIN;

|PROCESO Tipo2;  |TIPO 2;
     nForma = 0 +. 1;
     nModo = (FEntrada / 100) ! 0;

     aAlfa = aSubFamilia + (("000" + #1#1) % -103);
     #2#0 = aAlfa;
     |LEE 040#2=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |SI nForma = 1;
          |LEE 121#2=;
          #2#1 = #1#2;
          #2#110 = #1#8;
          |GRABA 020#2a;
          |LIBERA #2;
     |FINSI;

     |SI nModo = 3;
         bAlfa = #1#1; |QBF bAlfa;
         bAlfa = ("000" + bAlfa) % -103;
         cAlfa = #1#0; |QBF cAlfa;
         bAlfa = cAlfa + bAlfa;
         wT = 0;
         |BUCLE agifa065b;
         |SI wT  = 1;
              |MENAV "     Este Articulo Tiene Movimientos";
         |FINSI;
         |CONFI "0000NEsta Seguro que quiere dar de BAJA el Articulo";
         |SI FSalida ! 0;
              |ERROR;
              |ACABA;
         |SINO;
              nBajaArt = 1;
              bAlfa = #1#1; |QBF bAlfa;
              bAlfa = ("000" + bAlfa) % -103;
              cAlfa = #1#0; |QBF cAlfa;
              bAlfa = cAlfa + bAlfa;
              aAlfa = "iber0093" + &59 + bAlfa;
              |SUB_EJECUTA_NP aAlfa;
         |FINSI;
     |FINSI;

     |ABRE #3;
     #3#0 = #0#0;
     |LEE 040#3=;
     |SI FSalida ! 0;  |DDEFECTO #3;  |FINSI;
     |CIERRA #3;

     |SI nForma = 1;
         #4#0 = #2#0;
         |LEE 101#4=;
         |SI FSalida ! 0;
             |DDEFECTO #4;
             #4#0 = #2#0;
             |GRABA 020#4b;
         |FINSI;
         #4#1 = #1#3;
         #4#2 = #1#4;
         #4#2 = #1#4;
         #4#4 = #1#2;
         #4#5 = #0#0;
         #4#6 = #0#1;
         #4#7 = #1#5;
         #4#8 = #1#6;
         #4#9 = #1#7;
         #4#10 = #3#14;
         |SI #2#204 = "S";
              #4#12 = #2#205;
         |SINO;
              #4#12 = #2#5;
         |FINSI;
         |GRABA 020#4a;
         |LIBERA #4;

          |BUCLE iber0209;
          |BUCLE iber0067;
     |FINSI;
|FPRC;

|| Procesos del iberz002

|PROCESO GrabaTempo;
     aAlfa     = #2#0;  |QBF aAlfa;
     aAlfa     = aAlfa - aSubFamilia;
     nContador = aAlfa;
     |SI nContador = 0;    |ACABA;  |FINSI;
     |SI nContador > 999;  |ACABA;  |FINSI;

     #4#0 = #2#0;
     |LEE 040#4=;
     |SI FSalida ! 0;  |DDEFECTO #4;  |FINSI;

     #1#0 = #0#0;
     #1#1 = nContador;
     #1#2 = #2#1;
     #1#3 = #4#1;
     #1#4 = #4#2;
     #1#5 = #4#7;
     #1#6 = #4#8;
     #1#7 = #4#9;
     #1#8 = #2#110;
     |GRABA 020#1n;
|FPRC;

|DEFBUCLE agifa019;
     #2#1;
     ;
     aDArticulo;
     aHArticulo;
     ;
     NULL, GrabaTempo;
|FIN;

|PROCESO BorraTempo;
     |BORRA 020#1a;
     |LIBERA #1;
|FPRC;

|DEFBUCLE iberw004;
     #1#1;
     ;
     #0#0,   1;
     #0#0, 999;
     be;
     NULL, BorraTempo;
|FIN;

|RUTINA ClaveBaja1;
    #1#0 = #0#0;
    #1#1 = 1;
|FRUT;

|RUTINA ClaveAlta1;
    #1#0 = #0#0;
    #1#1 = 999;
|FRUT;

|PROCESO AltaArticulo;
     |ABRE #3;
     #3#0 = #0#0;
     |LEE 040#3=;
     |SI FSalida ! 0;  |DDEFECTO #3;  |FINSI;
     |CIERRA #3;

     aAlfa = aSubFamilia + (("000" + #1#1) % -103);
     #2#0 = aAlfa;
     |LEE 101#2=;
     nEstado = FSalida;
     |SI FSalida ! 0;
         |DDEFECTO #2;
         #2#0   = aAlfa;
         #2#1   = #1#2;
         #2#2   = #3#10;
         #2#3   = #3#0;
         #2#125 = #3#8;
         #2#200 = #3#12;
         #2#130 = #3#2;
         #2#131 = #3#3;
         #2#132 = #3#4;
         #2#153 = #3#5;
         #2#154 = #3#6;
         #2#155 = #3#7;
         #2#110 = #1#8;
         |GRABA 020#2b;
     |FINSI;
     #2#1 = #1#2;

     |PUSHV 0501 2380;
     |PINPA #0#2;
     |PINDA #0#2;
     |SI nEstado = 0;
          |ENDATOS #2#2;
     |SINO;
          |ENDATOS #1#2;
     |FINSI;
     |SI FSalida = 0;
         #2#123 = "N";             || exportado NO
         #2#124 = "00.00.0000";    || fecha exportado
         |GRABA 020#2a;
         aAlfa = "iber0093" + &59 + #2#0;
         |SUB_EJECUTA_NP aAlfa; ||Descripciones
     |SINO;
         |SI nEstado ! 0;
             |BORRA 020#2a;
             |PTEC 807;
         |FINSI;
     |FINSI;
     |LIBERA #2;
     |POPV;
|FPRC;

|PROCESO Tipo12;  |TIPO 12;
     |SI FSalida ! 0; |ACABA; |FINSI;
     |ABRET #2;
     |ABRE #1;
     #1#0 = #0#0;
     #1#1 = 1;
     |LEE 040#1];
     |SI FSalida = 0;  |Y #1#0 = #0#0;
         |CIERRA #1;
         |MENAV "     Introduccion bloqueada (iberw004) ...";
         |ERROR;
         |ACABA;
     |FINSI;

     |ABRE #4;
     aSubFamilia = #0#0;  |QBF aSubFamilia;
     aDArticulo  = (aSubFamilia + (" " * 20)) % 120;
     aHArticulo  = (aSubFamilia + ("z" * 20)) % 120;
     |BUCLE agifa019;
     |CIERRA #1;

     |ENTLINEAL #1#1, -2, 1, 22, 0, ClaveBaja1, ClaveAlta1;
     |CIERRA #4;

     |BUCLE iberw004;
     |CIERRAT #2;
|FPRC;

|PROCESO vernatu; |TIPO 0;
     |SI #1#3 = "MP";
          |C_M #1#4 , 1 , "N";
          |C_M #1#5 , 1 , "N";
          #1#4 = "N";
          #1#5 = "N";
     |SINO;
          |C_M #1#4 , 1 , "S";
          |C_M #1#5 , 1 , "S";
     |FINSI;
     |PINTA #1#4;
     |PINTA #1#5;
|FPRC;

|PROCESO Sale; |TIPO 6;
     |SI FSalida = 1;
          |ERROR;
          |PTEC 807;
     |FINSI;
|FPRC;

|PROCESO Longi002; |TIPO 0;
     aLon = #0#0; |QBF aLon;
     aLon = aLon % A0;
     |SI aLon ! 4;
          |MENAV "0000El Codigo debe ser de 4 digitos...";
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO CreaArti; |TIPO 11;
     |SI FSalida = 13;
          eaArticulo = aSubFamilia + (("000" + #1#1) % -103);
          |SUB_EJECUTA_NP "iber0207";
          |ERROR;
     |FINSI;
|FPRC ;
