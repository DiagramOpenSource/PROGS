|FICHEROS;
     fergaz12 #0;
     fergam02 #2;
     fergam03 #3;
     fergam06 #6;
     fergam07 #7;
     fergam08 #8;
     fergam10 #10;

     fergaw07 #70;   ||Tmp
|FIN;

|VARIABLES;
     &Tempo = "";
     aParam = "";
     nTraba = 0;
     nNumTraba = 0;
     nLin = 0;
     fHoy = @;
     aHora = "";
     nHay = 0;
     nNumTra = 0;
     aAlfa = "";
     nTiempo = 0;
     nHora = 0;
     nHora1 = 0;
     nHora2 = 0;
     nMinu = 0;
|FIN;

|PROCESO Tiempo;
     nHora = (A\(#8#5 % 102));
     nMinu = (A\(#8#5 % 402));
     nHora1 = nHora * 60 + nMinu;

     nHora = (A\(#8#6 % 102));
     nMinu = (A\(#8#6 % 402));
     nHora2 = nHora * 60 + nMinu;

     |SI nHora2 < nHora1;
          nTiempo = ((24*60 - nHora1) + nHora2) / 60;
     |SINO;
          nTiempo = (nHora2 - nHora1) / 60;
     |FINSI;
|FPRC;

|PROCESO IniElaborada;
     #8#0 = #70#0;
     #8#1 = 999;
     #8#2 = 999;
     |LEE 000#8];
     |SI FSalida = 0;
          |LEE 000#8a;
     |SINO;
          |LEE 000#8u;
     |FINSI;
     |SI FSalida = 0; |Y #8#0 = #70#0; |Y #8#1 = 999;
          nLin = #8#2 + 1;
     |SINO;
          nLin = 1;
     |FINSI;
     |DDEFECTO #8;
     #8#0 = #70#0;
     #8#1 = 999;
     #8#2 = nLin;
     #8#3 = fHoy;
     #8#4 = #70#1;
     #8#5 = aHora;
     |GRABA 020#8n;

     |DDEFECTO #7;
     #7#0 = #70#0;
     #7#1 = 999;
     #7#3 = "ELABORACION";
     |GRABA 000#7n;
|FPRC;

|PROCESO IniciaMaqui;
     #6#0 = #7#0;
     |LEE 000#6=;
     |SI FSalida = 0; |Y #6#8 = "N";
          #8#0 = #7#0;
          #8#1 = #7#1;
          #8#2 = 999;
          |LEE 000#8];
          |SI FSalida = 0;
               |LEE 000#8a;
          |SINO;
               |LEE 000#8u;
          |FINSI;
          |SI FSalida = 0; |Y #8#0 = #7#0; |Y #8#1 = #7#1;
               nLin = #8#2 + 1;
          |SINO;
               nLin = 1;
          |FINSI;
          |DDEFECTO #8;
          #8#0 = #7#0;
          #8#1 = #7#1;
          #8#2 = nLin;
          #8#3 = fHoy;
          #8#4 = #70#1;
          #8#5 = aHora;
          |GRABA 020#8n;
          nHay = 1;
     |FINSI;
|FPRC;

|DEFBUCLE Componentes;
     #7#2;
     ;
     #70#0, #3#2;
     #70#0, #3#2;
     ;
     NULL, IniciaMaqui;
|FIN;

|PROCESO MaquinasLin;
     |BUCLE Componentes;
|FPRC;

|DEFBUCLE MaquinasLin;
     #3#1;
     ;
     #70#1, 001;
     #70#1, 999;
     ;
     NULL, MaquinasLin;
|FIN;

|PROCESO Tmp;
     #2#0 = #70#1;
     |LEE 000#2=;
     |SI FSalida ! 0; |DDEFECTO #2; |FINSI;

     |SI #2#3 = "S"; |O #2#4 = "S"; |O #2#5 = "S";
          |ABRE #8;
          |ABRE #7;
          |HAZ IniElaborada;
          |CIERRA #7;
          |CIERRA #8;
     |SINO;
          nHay = 0;
          |ABRE #6;
          |ABRE #8;
          |BUCLE MaquinasLin;
          |CIERRA #6;
          |CIERRA #8;
     |FINSI;
|FPRC;

|DEFBUCLE Tmp;
     #70#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, Tmp;
|FIN;

|PROCESO CuentaTraba;
     |SI nTraba ! #10#2;
          nTraba = #10#2;
          nNumTra = nNumTra + 1;
     |FINSI;
|FPRC;

|DEFBUCLE CuentaTraba;
     #10#1;
     ;
     #8#3, #8#4, 0001, "     ";
     #8#3, #8#4, 9999, "zzzzz";
     ;
     NULL, CuentaTraba;
|FIN;

|PROCESO Maquinas;
     |SI #8#10 = "S";
          |ACABA;        || Finalizada
     |FINSI;

     aAlfa = #8#6; |QBF aAlfa;
     |SI aParam = "P";
          |SI aAlfa ! ""; |ACABA; |FINSI; || Parada

          nNumTra = 0; nTraba = -1;
          |BUCLE CuentaTraba;
          #8#6 = aHora;
          |HAZ Tiempo;
          #8#7 = nTiempo;
          #8#8 = nNumTra;
          #8#9 = 0;
          |GRABA 020#8a;
     |SINO;
          |SI aAlfa = ""; |ACABA; |FINSI; || Iniciada

          #70#0 = #8#0;
          #70#1 = #8#4;
          |GRABA 000#70n;
     |FINSI;
|FPRC;

|DEFBUCLE Maquinas;
     #8#1;
     ;
     #6#0, 001, 001;
     #6#0, 999, 999;
     be;
     NULL, Maquinas;
|FIN;

|PROCESO Ordenes;
     |BUCLE Maquinas;
|FPRC;

|DEFBUCLE Ordenes;
     #6#2;
     ;
     "N", 000001;
     "N", 999999;
     ;
     NULL, Ordenes;
|FIN;

|PROGRAMA;
     aParam = PARAMETRO;

     |SI aParam = "";
          |PINPA #0#0;
          |PINDA #0#0;
          |ENDATOS #1#0;
          |SI FSalida ! 0; |ACABA; |FINSI;
          aParam = #0#0;
     |FINSI;

     |HAZ CreaTempo;
     |NOME_DAT #70 Tempo; |DELINDEX #70; |ABRE #70;

     fHoy = ~;
     |HORA aHora;

     |SI aParam = "P"; |O aParam = "I";
          |ABRE #2;
          |BUCLE Ordenes;
||CONSULTA_CLAVE #1#70;
          |BUCLE Tmp;
          |CIERRA #2;
     |SINO;
          aAlfa = "Parametro incorrecto [" + aParam + "], solo puede ser P/I...";
          |MENAV aAlfa;
     |FINSI;

     |HAZ BoraTempo; |DELINDEX #70;
|FPRO;
