|FICHEROS;
     diefa001 #0;
     agifa024 #3;
     agifa104 #8;
     agifa038 #13;
     agifa048 #15;
     agifa049 #16;
     agifa007 #40;
     agifa142 #41;
     agi00002 #43;  || Fichero de trabajos;
     agi00003 #44;
     dsm00010 #910;
     agifa010 #11;
     agifa071 #12;
     dsm00005 #1905;
     tmp00010 #1;
     tmp00071 #2;
     tmpdsm05 #905;
     dsm00053 #53;
     dsm00047 #47;
     agifa019 #19;
     agifa321 #321;
     agifa324 #324;
     agi00101 #100;

     &marhu355@ #200;
|FIN;

|VARIABLES;
     nLinV        = 0;
     dFactu       = "";
     hFactu       = "";
     aFecha       = "";

     &eaDSer      = "";
     &eaASer      = "";
     &enDNum      = 0;
     &enANum      = 0;

     &eaLote      = "";
     &swmarhu062  = 0;
     &Tempo       = "";
     aTempo1      = "";
     aTempo2      = "";
     aTempo3      = "";
     aDRei        = "";
     aHRei        = "";
     &enSwMarhu   = 0;
     &eaCli       = "";
     &enTiva      = 0;
     &enIva       = 0;
     &enRec       = 0;
     &efFiva = @;
     aAlfa        = "";
     aAlf1        = "";
     &aDivisa     = "";
     &aMoneda     = "";
     &enEscan    = 0;
     &aQuelis     = "";
     &enImpo      = 0;
     nSwPasa      = 0;
     &eaTipo      = "";
     &eaSerie     = "";
     &enCodig     = 0;
     &enLinea     = 0;
     &eaArticulo  = "";
     &eaNArticulo = "";
     &enUnidades  = 0;
     &enPrecio    = 0;
     &enDto1      = 0;
     &enDto2      = 0;
     &eaFichAlba  = "";
     &enPantalla  = 0;
     &enNPedido   = 0;

     &trabajo  = 0;
     &ser_alb  = "";
     &num_alb  = 0;
     &codigo   = "";
     &descrip  = "";
     &supedino = "";
     &bl       = "                              ";
     &abono    = "N";  || Viene de agifa010

     mes        = 0;
     margen     = 0;
     imneto     = 0;
     aAlfa1     = "";
     aAlfa2     = "";
     nCampo     = 0;
     nSw010     = 0;
     aFichero   = "";
     aParametro = "";

     nSwOpcion = 0;
     desde     = "                              ";
     hasta     = "zzzzzzzzzzzzzzzzzzzzzzzzzzzzzz";
     informe   = "";
     &informe1 = "";
     vacio     = "";
     digito    = "";

     &ascii    = 0;
     &impal    = 0;
     &impor    = 0;
     &iva1     = 0;
     &iva2     = 0;
     &iva3     = 0;
     &iva4     = 0;
     &iva5     = 0;
     &re1      = 0;
     &re2      = 0;
     &re3      = 0;
     &re4      = 0;
     &re5      = 0;
     &precio   = 0:
     &dto      = 0;
     &dto1     = 0;
     &unid     = 0;
     &imptot   = 0;
     &tiva     = 0;
     &linea    = 0;

     indtope   = 0;
     ind       = 0;
     n_copia   = 0;

     &dtrab    = "";
     &swt      = 0;

{-1} MenuImpre   = "";
     MenuImpre1  = "1021";
     MenuImpre2  = "1";
     MenuImpre3  = "7";
     MenuImpre4  = "Salir                    ";
     MenuImpre5  = "Albaran                  ";
     MenuImpre6  = "Albaran Agencia          ";
     MenuImpre7  = "Etiquetas Agencia        ";
     MenuImpre8  = "Pdte.Servir Parcialmente ";
     MenuImpre9  = "Pdte.Servir Completamente";
     MenuImpre10 = "Pdte.Servir Ambos.       ";

     &sobre = "";

     &nSwDefecto  = 0; || Para Descripcion Ampli

     nImpDiv      = 0;
     nMult        = 0;

     &nDeci_Base     = 0;   || Decimales en Totales de la moneda base
     &nDeci_PreBase  = 0;   || Decimales en el Precio en moneda base
     &nDeci_BaseMx   = 0;   || Decimales en Totales de la moneda base (Maxima precision)
     &nDeci_PreBaseMx= 0;   || Decimales en el Precio en moneda base (Maxima precision)
     &nDeci_Div      = 0;   || Decimales en Totales de la divisa
     &nDeci_PreDiv   = 0;   || Decimales en el Precio en divisa
     &nDeci_TotVis   = 0;   || Decimales en Totales visibles (el de la moneda de trabajo)
     &nDeci_TotNoVis = 0;   || Decimales en Totales no visibles (el de la otra moneda)
     &nDeci_PreVis   = 0;   || Decimales en el Precio visual. (lineas)
     &nDeci_ImpVis   = 0;   || Decimales en el Importe visual. (lineas)
     &decim_divisa = 0;  || Decimales de la divisa
     &precio_divisa = 0; || Decimales del precio en divisa
     &decim_base = 0;    || Decimales de la moneda base
     &precio_base = 0;   || Decimales del precio en moneda base
|FIN;

||INC-LUYE llena_c1;

|PROCESO Param_Divisas001; |TIPO 0;
     |ABRE #agifa321;
     |LEE 001#agifa321.p;   || Leo la divisa
     |CIERRA #agifa321;

     |ABRE #agifa324;
     #agifa324#0 = #tmp00010#68;
     |LEE 001#agifa324.=;   || Leo la divisa
     |CIERRA #agifa324;

     decim_divisa = #agifa324#7;  || Decimales de la divisa
     precio_divisa = #agifa324#9;  || Decimales en el precio de la divisa
     nDeci_Div = decim_divisa;   || Decimales de la divisa
     nDeci_PreDiv = precio_divisa;  || Decimales en el precio de la divisa
     |SI #tmp00010#70 = "B";   || Si trabajo con la moneda base ...
          nDeci_PreVis   = precio_divisa;
          nDeci_ImpVis   = decim_divisa;
          nDeci_Base     = decim_base;
          nDeci_PreBase  = precio_base;
          nDeci_BaseMx  = decim_base;
          nDeci_PreBaseMx = precio_base;
          nDeci_TotVis   = decim_base;
          nDeci_TotNoVis = decim_divisa;
     |SINO;     || Si trabajo con la divisa ...
          nDeci_PreVis   = precio_base;
          nDeci_ImpVis   = decim_base;
          nDeci_Base     = decim_base; || Max. precision en los decimales de la moneda base
          nDeci_PreBase  = precio_base;  || Max. precision en los dec. del precio de la moneda base
          |SI #tmp00010#69 = 1;
               nDeci_BaseMx    = decim_base;    || sin triangulacion
               nDeci_PreBaseMx = precio_base;
          |SINO;
               nDeci_BaseMx    = #agifa321#10;  || con triangulacion
               nDeci_PreBaseMx = #agifa321#11;
          |FINSI;
          nDeci_TotVis   = decim_divisa;
          nDeci_TotNoVis = decim_base;
     |FINSI;
     |HAZ Deci_IVA;
|FPRC;

|PROCESO &Defecto;       || Lo llama el informe
     |SI nSwDefecto = 1;
          |PDEFECTO #12, 1, 4;
          |PDEFECTO #12, 5, 60;
     |FINSI;
|FPRC;

|| PROCESO DE SEPARACION DEL ABARAN CREADO AHORA EN SU HONONIMO EN NEGRO

|PROCESO TotalLin071;|TIPO 0;
     |ABRE #19;
     #19#0 = #2#2;
     |LEE 020#19=;
     |CIERRA #19;

     |SI #1#70 = "D";                    || Si mon. de trabajo es divisa ...
          #2#6  = #2#36 / #1#69 * #1#74; || Recalculo precio en mon. base
          #2#38 = #2#6;                  || Precio visual, en mon. base
     |SINO;                              || Si mon. de trabajo es mon. base
          #2#36 = #2#6 / #1#74 * #1#69;  || Recalculo precio en divisa
          #2#38 = #2#36;                 || Precio visual, en divisa
     |FINSI;

     |SI #19#194 = 2;
         #2#7    = #2#48 * #2#6;
         nImpDiv = (#2#48 * #2#36) ! nDeci_PreDiv;
     |SINO;
          #2#7    = #2#5 * #2#6;
          nImpDiv = (#2#5 * #2#36) ! nDeci_PreDiv;
     |FINSI;

     || nMult corrige el error del redondeo para negativos
     |SI #2#7 < 0; nMult = -1; |SINO; nMult = 1; |FINSI;
     #2#7    = #2#7    * nMult;
     nImpDiv = nImpDiv * nMult;

     #2#9  = (((#2#7 < #2#8) < #2#33));
     #2#37 = (((nImpDiv < #2#8) < #2#33));

     #2#7  = #2#7 * nMult;
     #2#9  = #2#9 * nMult;
     #2#37 = #2#37 * nMult;
     ||컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴

     |SI #1#70 = "D";     || Si la moneda de trabajo es la divisa ...
          #2#39 = #2#9;   || el campo visualiz. es el importe en moneda base
     |SINO;               || Si la moneda de trabajo es la moneda base ...
          #2#39 = #2#37;  || el campo visualiz. es el importe en divisa
     |FINSI;

     |GRABA 001#2a; ||Grabo lineas
|FPRC;

|PROCESO acttl;|TIPO 0;
     |HAZ TotalLin071;
     |HAZ CalCabTmp00010;
|FPRC;

|DEFBUCLE tmp00071Act;
     #2#1;
     ;
     #1#52, #1#0, 001;
     #1#52, #1#0, 999;
     ;
     NULL, acttl;
|FIN;

|PROCESO ActualizaFicha;
     |HAZ LimCabTmp00010;
     |BUCLE tmp00071Act;
|FPRC;

|PROCESO dsm00005;
     |PARA nCampo = 0;  |SI nCampo [ 10;  |HACIENDO nCampo = nCampo + 1;
           #905nCampo = #1905nCampo;
     |SIGUE;
     |GRABA 020#905n;
     |LIBERA #905;
|FPRC;

|DEFBUCLE dsm00005;
     #1905#1;
     ;
     "AV", #2#29, #2#0, nLinV, "      ";
     "AV", #2#29, #2#0, nLinV, "zzzzzz";
     be;
     NULL, dsm00005;
|FIN;

|PROCESO agifa071;
     |PARA nCampo = 0;  |SI nCampo [ 47;  |HACIENDO nCampo = nCampo + 1;
           #2nCampo = #12nCampo;
     |SIGUE;
     |GRABA 020#2n;
     |LIBERA #2;
     nLinV = #2#1;
     |BUCLE dsm00005;
|FPRC;

|PROCESO Pasalos;
     #1#68 = #11#68;
     #1#69 = #11#69;
     #1#70 = #11#70;
     |HAZ Param_Divisas001;
     |PARA nCampo = 0;  |SI nCampo [ 87;  |HACIENDO nCampo = nCampo + 1;
           #1nCampo = #11nCampo;
     |SIGUE;
     |GRABA 020#1n;
     |LIBERA #1;

     |BUCLELIN 0 agifa071 #11;
|FPRC;

|DEFBUCLE agifa010;
     #11#3;
     43;
     #0#0, #0#4, #0#9, #0#2, #0#8;
     #0#1, #0#5, #0#10,#0#3, #0#8;
     ;
     NULL, Pasalos;
|FIN;


|PROCESO Temporales;
     |HAZ CreaTempo; aTempo1 = Tempo;
     |NOME_DAT #1 aTempo1;

     |HAZ CreaTempo; aTempo2 = Tempo;
     |NOME_DAT #2 aTempo2;

     |HAZ CreaTempo; aTempo3 = Tempo;
     |NOME_DAT #905 aTempo3;

     |DELINDEX #1;
     |DELINDEX #2;
     |DELINDEX #905;

     |ABRE #1;
     |ABRE #2;
     |ABRE #905;
     |BUCLE agifa010;
     |CIERRA #1;
     |CIERRA #2;
     |CIERRA #905;
|FPRC;

|PROCESO VuelveAlSitioC;
     |SI aAlfa1 = "U";
         #905#6 = #905#10;
     |SINO;
         #905#7 = #905#10;
     |FINSI;

     #905#8 = #905#6 * #905#7;

     |GRABA 020#905a;
     |LIBERA #905;
|FPRC;

|DEFBUCLE tmpdsm05A;
     #905#1;
     ;
     "AV", #2#29, #2#0, nLinV, "      ";
     "AV", #2#29, #2#0, nLinV, "zzzzzz";
     be;
     NULL, VuelveAlSitioC;
|FIN;

|PROCESO VuelveAlSitio;
     aAlfa1 = #2#47 % 101;
     aAlfa2 = #2#47 % 615;
     |SI aAlfa1 = "U";
         #2#5 = aAlfa2;
     |SINO;
         #2#6 = aAlfa2;
     |FINSI;
     nLinV = #2#1;
     |BUCLE tmpdsm05A;

     |GRABA 020#2a;
     |LIBERA #2;
|FPRC;

|DEFBUCLE tmp00071S;
     #2#1;
     ;
     #1#52, #1#0, 001;
     #1#52, #1#0, 999;
     ;
     NULL, VuelveAlSitio;
|FIN;

|PROCESO BorraEl905;
     |BORRA 020#905a;
     |LIBERA #905;
|FPRC;

|DEFBUCLE tmpdsm05B;
     #905#1;
     ;
     "AV", #2#29, #2#0, nLinV, "      ";
     "AV", #2#29, #2#0, nLinV, "zzzzzz";
     be;
     NULL, BorraEl905;
|FIN;

|PROCESO BorraEl2;
     nLinV = #2#1;
     |BUCLE tmpdsm05B;

     |BORRA 020#2a;
     |LIBERA #2;
|FPRC;

|DEFBUCLE tmp00071B;
     #2#1;
     ;
     #1#52, #1#0, 001;
     #1#52, #1#0, 999;
     be;
     NULL, BorraEl2;
|FIN;

|PROCESO Juntalos;
     |SALVA_FICHA #1;

     |SELECT #1#1;
     #1#52 = #1#86;
     #1#0  = #1#87;
     |LEE 101#1=;
     |SI FSalida = 0;
         |BUCLE tmp00071B;
         |BORRA 020#1a;
         |LIBERA #1;
     |FINSI;

     |REPON_FICHA #1;

     |SELECT #2#1;
     |LEE 101#1=;
     |SI FSalida = 0;
         |HAZ Param_Divisas001;
         |BUCLE tmp00071S;
         |HAZ ActualizaFicha;
         |GRABA 020#1a;
         |LIBERA #1;
     |FINSI;
|FPRC;

|PROCESO Junta;
     |SI #1#85 ! "E";  |ACABA;   |FINSI;
     |HAZ Juntalos;
|FPRC;

|DEFBUCLE tmp00010Junta;
     #1#1;
     ;
     #0#9,  #0#2;
     #0#10, #0#3;
     ;
     NULL, Junta;
|FIN;

|PROCESO alimcon;|TIPO 0;
          |ERROR;
|FPRC;

|PROCESO trabajo;
     dtrab = #44#2;      || DESCRIPCION TRABAJO.
     |PRINT;
|FPRC;

|PROCESO escanlinea;
    unid    = #16#4 * #2#5;
    descrip = #16#3;
    codigo  = "";
    dto     = 0;
    precio  = 0;
    imptot  = 0;
    tiva    = 0;
    linea   = 1;

    |PRINT;
|FPRC;

|PROCESO xx;|TIPO 0;
     codigo  = "";
     descrip = "";

     dto    = "";
     dto1   = "";
     precio = "";
     unid   = "";
     imptot = "";
     tiva   = "";
     indtope=0
     |PARA ind=6;|SI ind>0;|HACIENDO ind=ind-1;
          descrip = #13ind;  |QBF descrip;
          |SI descrip ! vacio;
              indtope = ind + 1;
              ind=0;
          |FINSI;
     |SIGUE;

     |PARA ind=1;|SI ind<indtope;|HACIENDO ind=ind+1;
           descrip=#13ind;
           linea=0;
           |PRINT;
     |SIGUE;
|FPRC;

|PROCESO impdesam;|TIPO 0;
     |ABRE #13;
     #13#0=#2#27;
     |LEE 101#13=;
     |SI FSalida = 0;  |HAZ xx;  |FINSI;
     |LIBERA #13;
     |CIERRA #13;
|FPRC;

|PROCESO DesAmpli;
     |DDEFECTO #12;

     #12#3 = 0;      || Almacen
     #12#4 = #53#5;
     unid    = 0;
     dto     = 0;
     precio  = 0;
     imptot  = 0;
     tiva    = 0;
     codigo = "";
     descrip = #12#4;
     nSwDefecto  = 1;
     |PRINT;
     nSwDefecto  = 0;
|FPRC;

|DEFBUCLE DesAmpli;
     #53#1;
     ;
     "A", #12#29, #12#0, #12#1, 0001;
     "A", #12#29, #12#0, #12#1, 9999;
     ;
     NULL, DesAmpli;
|FIN;

|PROCESO dsm00047;
     unid = #12#5 * #47#5;
     |PRINT;
|FPRC;

|DEFBUCLE dsm00047;
     #47#1;
     ;
     #12#29, #12#0, #12#1, 001;
     #12#29, #12#0, #12#1, 999;
     ;
     NULL, dsm00047;
|FIN;

|PROCESO IVAS;
     eaCli = #2#15;
     efFiva = #1#1;
     enTiva = 1; |HAZ IVACli; iva1 = enIva; re1 = enRec;
     enTiva = 2; |HAZ IVACli; iva2 = enIva; re2 = enRec;
     enTiva = 3; |HAZ IVACli; iva3 = enIva; re3 = enRec;
     enTiva = 4; |HAZ IVACli; iva4 = enIva; re4 = enRec;
     enTiva = 5; |HAZ IVACli; iva5 = enIva; re5 = enRec;
|FPRC;

|PROCESO LeeLote;
     eaLote = "";
     |ABRE #100;
     #100#0 = #2#29;
     #100#1 = #2#0;
     #100#8 = #2#1;
     #100#5 = "";
     |LEE 000#100];
     |SI #100#0 = #2#29; |Y #100#1 = #2#0; |Y #100#8 = #2#1;
          eaLote = #100#5;
     |FINSI;
     |CIERRA #100;
|FPRC;

|PROCESO ImpreNormal;
     #8#25 = #2#31;      ||serie pedido
     #8#0 = #2#12;       ||pedido
     |LEE 041#8=;
     |SI FSalida ! 0;  |DDEFECTO #8;  |FINSI;

     |HAZ LeeLote;

     codigo  = #2#2;
     descrip = #2#4;

     dto    = #2#8;
     dto1   = #2#33;
     precio = #2#6;
     unid   = #2#5;
     imptot = #2#9;
     tiva   = #2#11;
     linea  = 1;
     digito = #2#4 % "101";
     ascii  = &digito;
     sobre  = #2#47;

     |HAZ IVAS;
     ||컴컴컴컴컴컴컴컴컴컴 Modulo 000654
     |HAZ EsTagloma;
     |SI FSalida = 0;
          |HAZ ImpUniFami;
     |FINSI;
     ||컴컴컴컴컴컴컴컴컴컴
     |PRINT;

     |SALVA_DATOS #12;
     |BUCLE DesAmpli;
     |REPON_DATOS #12;

     |HAZ impdesam;

     |SI #41#16 = "S";
          |ABRE #15;
          #15#0 = #2#2;
          |LEE 040#15=;
          |SI FSalida = 0;
               |SI #41#12 = "K";
                    enEscan = 2;
                    |BUCLE dsm00047;
               |SINO;
                    enEscan = 1;
                    |BUCLELIN 0escanlinea#15;
               |FINSI;
                enEscan = 0;
          |FINSI;
          |CIERRA #15;
     |FINSI;
|FPRC;

|PROCESO ImpreHorizo;
     eaTipo      = "AV";
     eaSerie     = #2#29;
     enCodig     = #2#0;
     enLinea     = #2#1;
     eaArticulo  = #2#2;
     eaNArticulo = #2#4;
     enUnidades  = #2#5;
     enPrecio    = #2#6;
     enDto1      = #2#8;
     enDto2      = #2#33;
     enNPedido   = #2#12;
     enImpo      = #2#37;
     |HAZ TrataCompo;
|FPRC;

|PROCESO albimprl;|TIPO 0;
     ||컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
     ||No se imprimen las unidades desglosadas al 100%
     aAlfa = #2#47; |QBF aAlfa;    ||(Anna&Robert lo usa)
     |SI aAlfa ! "";
          aAlfa = #2#47 % 101;
          aAlf1 = #2#47 % 201;
          |SI aAlfa = "P";         ||Por Unidad
               |SI aAlf1 = "P";
                    aAlfa = #2#47 % 303;
                    |SI aAlfa = 100; |ACABA; |FINSI;
               |SINO;
                    aAlfa = #2#47 % 303;
                    |SI aAlfa = 0; |ACABA; |FINSI;
               |FINSI;
          |FINSI;
     |FINSI;
     ||컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
     |SI #0#13 = "N"; |Y #2#5 = 0; |ACABA; |FINSI;
     |SI #0#14 = "N"; |Y #2#6 = 0; |ACABA; |FINSI;

     nSwPasa = 1;
     |SI #910#19 = "N";  |HAZ ImpreNormal;  |FINSI;
     |SI #910#19 = "S";  |HAZ ImpreHorizo;  |FINSI;
|FPRC;

|DEFBUCLE tmp00071;
     #2#1;
     ;
     #1#52, #1#0, 001;
     #1#52, #1#0, 999;
     ;
     NULL, albimprl;
|FIN;

|PROCESO Marhuenda;
     #2#29 = #200#0;
     #2#0 = #200#1;
     #2#1 = #200#2;
     |LEE 020#2=;
     |HAZ albimprl;
|FPRC;

|DEFBUCLE Marhuenda;
     #200#2004;
     ;
     "          ", #1#52, #1#0, 001;
     "zzzzzzzzzz", #1#52, #1#0, 999;
     ;
     NULL, Marhuenda;
|FIN;

|PROCESO albimpr1;
     aDivisa = #1#68;
     aMoneda = #1#70;
     |HAZ Divisas010;

     |SI #3#0 ! #1#3;
         #3#0 = #1#3;
         |LEE 111#3=;
         |SI FSalida ! 0;  |DDEFECTO #3;  |FINSI;
         |LIBERA #3;
     |FINSI;

     |SI #3#189 < #0#16; |O #3#189 > #0#17;  ||Ruta
          |ACABA;
     |FINSI;
     aFecha = #1#57; |QBF aFecha;          ||Fecha = Obse 3
     |SI aFecha ! #0#20;
          |ACABA;
     |FINSI;

     #40#26 = #1#52;
     |LEE 001#40=;
     |SI FSalida ! 0;  |DDEFECTO #40;  |FINSI;

     |SI #0#8 = "N";     ||No imprimo abonos
         |SI #1#50 = "S";
             informe = #40#3; |QBF informe;
         |SINO;
             informe = #40#4; |QBF informe;
         |FINSI;
     |SINO;
         informe = #40#5; |QBF informe;
     |FINSI;
     informe1 = (informe % 0104) + "5" + (informe % 0603); |QBF informe1;

     |INFOR informe;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |HAZ OrdenaMarhu;
     |PARA n_copia = 0; |SI n_copia [ #3#187; |HACIENDO n_copia = n_copia + 1;
          trabajo = 0;
          nSwPasa = 0;
          |SI #910#19 = "S";  |HAZ AbreTemporales;  |FINSI;
          |SI enSwMarhu = 0;
               |ABRE #2;
               |BUCLE Marhuenda;
               |CIERRA #2;
          |SINO;
               |BUCLE tmp00071;
          |FINSI;

          |SI #910#19 = "S";  |HAZ ElRemate;  |FINSI;
          |SI nSwPasa = 0; |PIEINF; |FINSI;
          ||------------------ OPCION IMPRIMIR TRABAJOS --------------;
          |SI #41#132 = "S";
               |ABRE #43;
               |SELECT #4#43;
               #43#2 = #1#52;      || Serie;
               #43#3 = #1#0;       || Albaran;
               |LEE 001#43=;
               |SI FSalida = 0;
                   trabajo = 1;
                   swt     = 1;
                   |BUCLELIN 2 trabajo #43;
               |FINSI;
               |SELECT #1#43;
               |CIERRA #43;
          |FINSI;
          swt = 0;
          ||----------------------------------------------------------;
          |PIEINF;
          impal = 0;
          impor = 0;
     |SIGUE;
     |FININF;

     |LEE 121#1=;
     #1#90 = "S";
     |GRABA 020#1a;
     |LIBERA #1;
|FPRC;

|DEFBUCLE tmp00010p;
     #1#2;
     43, 90, 83, 38, 88;
     #0#0, #0#4, #0#9 , #0#2, #0#8, aDRei, #0#18, dFactu, #0#22;
     #0#1, #0#5, #0#10, #0#3, #0#8, aHRei, #0#19, hFactu, #0#23;
     ;
     NULL, albimpr1;
|FIN;

|DEFBUCLE tmp00010pa;
     #1#1;
     3, 1, 90, 83, 38, 88;
     #0#9  , #0#2 , #0#0 , #0#4,aDRei, #0#18, dFactu, #0#22;
     #0#10 , #0#3 , #0#1 , #0#5,aHRei, #0#18, hFactu, #0#23;
     ;
     NULL, albimpr1;
|FIN;

|DEFBUCLE tmp00010s;
     #1#1;
     90;
     #0#9,  #0#2, aDRei;
     #0#10, #0#3, aHRei;
     ;
     NULL, albimpr1;
|FIN;

|PROCESO Tipo3;
     |SI #0#15 = "S";
          aDRei = " ";
          aHRei = "z";
     |SINO;
          aDRei = "N";
          aHRei = "N";
     |FINSI;
     |SI #0#21 = "T";
          dFactu = " ";
          hFactu = "z";
     |SINO;
          dFactu = #0#21;
          hFactu = #0#21;
     |FINSI;

     enPantalla = 0;
     |SI swmarhu062 = 0;
          |IMPRE 1;
          |SI FSalida ! 0;  |ACABA;  |FINSI;
     |FINSI;

     |SI aParametro = "D";
         #0#11 = "NOTAS DE ENTREGA";
         |HAZ Temporales;
         |BUCLE tmp00010Junta;
     |FINSI;

     |ABRE #3;
     |ABRE #8;
     |ABRE #40;
     |SI ser_alb = ""; |Y num_alb = 0;
         |SI #0#12 = "C";
              |BUCLE tmp00010p; ||Por Cliente...
         |SINO;
              |BUCLE tmp00010pa;||Por Albaran...
         |FINSI;
     |SINO;
         #0#8 = abono;  || Le indico si quiero imprimir abonos
         |BUCLE tmp00010s;
     |FINSI;
     |CIERRA #40;
     |CIERRA #8;
     |CIERRA #3;

     |SI swmarhu062 = 0;
          |FINIMP;
     |FINSI;
     |SI aParametro = "D";
         |DELINDEX #1;   Tempo = aTempo1; |HAZ BoraTempo;
         |DELINDEX #2;   Tempo = aTempo2; |HAZ BoraTempo;
         |DELINDEX #905; Tempo = aTempo3; |HAZ BoraTempo;
     |FINSI;
|FPRC;

|PROCESO Agencia;
     |SUB_EJECUTA_NP "agifa011";
|FPRC;

|PROCESO Etiquetas;
     |SUB_EJECUTA_NP "agifa013";
|FPRC;

|PROCESO Resto;
     |SUB_EJECUTA_NP "dsy00002";
|FPRC;

|PROGRAMA;
     aParametro = PARAMETRO;
     |ABRE #41;  |LEE 000#41p; |CIERRA #41;
     |ABRE #910; |LEE 000#910p; |CIERRA #910;

     |HAZ IniMarhu;

     |SI ser_alb = ""; |Y num_alb = 0;
         |CLS;
         |CABEZA "Imp. Albaran";
         |DDEFECTO #0;
         |PINPA #0#0;
         |PINDA #0#0;
         |SI aParametro = "D";
               |ATRI R;
               |PINTA 0506, "    NOTAS  DE ENTREGA   ";
               |ATRI 0;
         |FINSI;
         |ENDATOS #1#0;
         |SI FSalida = 0;  |HAZ Tipo3;  |FINSI;
     |SINO;
         #0#9  = ser_alb;
         #0#10 = ser_alb;
         #0#2  = num_alb;
         #0#3  = num_alb;
         #0#15 = "S";
         FSalida = 0;
         |SI swmarhu062 ! 0;
             |HAZ Tipo3;
             |HAZ FinMarhu;
             |ACABA;
         |FINSI;
         |CONFI "2400NDesea Menu de Impresion : ";
         |SI FSalida ! 0;
             |HAZ Tipo3;
             |HAZ FinMarhu;
             |ACABA;
         |FINSI;

         nSwOpcion = 1;

         |PUSHV 0501 2380;
         |PARA; |SI nSwOpcion = 1; |HACIENDO;
                |BLANCO 0802 2062;
                |MENUG MenuImpre;
                MenuImpre2 = FSalida;
                |SI MenuImpre2 = "1";  nSwOpcion = 0;     |FINSI;
                |SI MenuImpre2 = "2";  |HAZ Tipo3;        |FINSI;
                |SI MenuImpre2 = "3";  |HAZ Agencia;      |FINSI;
                |SI MenuImpre2 = "4";  |HAZ Etiquetas;    |FINSI;
                |SI MenuImpre2 = "5";  aQuelis = "P"; |HAZ Resto;  |FINSI;
                |SI MenuImpre2 = "6";  aQuelis = "T"; |HAZ Resto;  |FINSI;
                |SI MenuImpre2 = "7";  aQuelis = "A"; |HAZ Resto;  |FINSI;
         |SIGUE;
         |POPV;
     |FINSI;

     |HAZ FinMarhu;
|FPRO;
