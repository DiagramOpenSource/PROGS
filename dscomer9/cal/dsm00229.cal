|FICHEROS;
     dsm00229 #0;

     agifa104 #1;   ||-Pedidos
     expor104@ #3;

     agifa073 #2;   ||-Lineas de Pedidos
     expor073@ #4;

     agifa024 #10;  ||-Clientes
     expor024@ #22;

     agifa019 #19;  ||-Articulos
     agifa142 #41;  ||-Parametros.
     agifa027 #32;  ||-Contadores.


     dsm00230 #30;

     dsw00011 #999; || Usado de temporal para el directorio
     dsm00217 #100; ||Historico E/I Delegacion.
     dsm00232 #200; ||Codigos Clientes Cambiados.

     agifa324;
|FIN;

|INCLUYE i_varart;

|VARIABLES;
     &eaNomDef = "";
     &enCamposDef = 0;
     &eaPedido= "";
     &aDivisa = "";
     &aMoneda = "";
     &enSw = 0;
     aInforme = "dsm00229";
     nImpre = 0;
     aRepre = "";
     aParam = "";
     n010 = 0; n071 = 0; n084 = 0; n069 = 0; n134 = 0; n059 = 0;
     n133 = 0; n039 = 0; n023 = 0; n305 = 0; n306 = 0; n024 = 0;
     n019 = 0; n112 = 0; n025 = 0; n146 = 0; n147 = 0; n104 = 0;
     n073 = 0;
     nImpo   = 0;
     nForma  = 0;
     aCod    = "";
     qCod    = "";
     contador = 0;
     ddx = "";
     fich = "";
     org = "";
     num = 0;
     bufer = 0;
     err = 0;
     fdes = "";
     des = "";
     fdef = "";
     def = "";
     exp = 0;
     fmes      = "";
     operacion = "";
     cli       = "";
     nomcli    = "";
     arti      = "";
     nomart    = "";
     seri      = "";
     docum     = 0;
     rep       = "";
     &path     = "";
     horat     = "";
     empresa   = "";
     blanco1   = "                    ";
     mensaje   = "";
     param     = "";
     tipo_exp  = "";

     alma      = 0;
     i         = 0;
     imneto    = 0;
     abono     = 0;
     mes       = 0;
     canti     = 0;
     retencion = 0;
     margen    = 0;
     sw_que    = 0;
     dto       = 0;
     bto       = 0;
     tfac      = 0;
     mfac      = 0;
     dto1      = 0;
     dto2      = 0;
     dto3      = 0;
     imptot    = 0;
     uni       = 0;
     lindoc    = 0;
     preci     = 0;
     sw_testigo= 0;
     sw_pasada = 0;

     fecha     = @;
     fechat    = @;
     fecalb    = @;

     swErr    = 0;
     aMensa   = "";
     aPath    = "";
     aDef     = "";
     aDef1    = "";
     aDef2    = "";
     aDef3    = "";
     aDef4    = "";
     aDef5    = "";
     aDef6    = "";
     aDef7    = "";
     aDef8    = "";
     aDef9    = "";
     aDef10   = "";
     aDef11   = "";
     aDef12   = "";
     aDef13   = "";
     aDef14   = "";
     aDef15   = "";
     aDef16   = "";
     aDef17   = "";
     aDef18   = "";
     aDef19   = "";
     aDef20   = "";
     aDef21   = "";
     aDef22   = "";


     aFic     = "";
     aAlfa    = "";
     fFecha   = @;
     nLin     = 0;
     aHora    = "";
     aAlf1    = "";
     nLon     = 0;
     aOrg     = "";
     aExt     = "";
     aGz      = "";
     nHandle  = 0;
     nHay     = 0;
     nSal     = 0;
|FIN;

|PROCESO agifa073;
     |PARA i = 0; |SI i [ n071; |HACIENDO i = i + 1;
          #2i = #4i;
     |SIGUE;
     |LEE 101#2=;
     |SI FSalida = 0;
          enForma = -1;
          |HAZ AcumulaPV;
          |PARA i = 0; |SI i [ n071; |HACIENDO i = i + 1;
               #2i = #4i;
          |SIGUE;
          |GRABA 021#2a;
     |SINO;
          |PARA i = 0; |SI i [ n071; |HACIENDO i = i + 1;
               #2i = #4i;
          |SIGUE;
          #2#20 = #1#4;
          |GRABA 021#2b;
     |FINSI;
     ||컴컴컴컴컴컴컴컴컴Pone precio en divisas
     |SI #1#37 = "D";
          #2#6 = #2#38;
     |SINO;
          #2#38 = #2#6;
     |FINSI;
     ||컴컴컴컴컴컴컴컴컴Deja pedido no servido....
     #2#42 = #2#5; #2#48 = #2#44;  ||pedidas
     #2#43 = 0;    #2#49 = 0;      ||servidas
     #2#12 = 0;    #2#47 = 0;      ||variacion

     |GRABA 020#2a;
     |LIBERA #2;

     enForma = 1;
     |HAZ AcumulaPV;

     #1#11 = #1#11 + #2#9;
     #1#12 = #1#12 + #2#9;
     #1#13 = #1#11 - #1#12;

||----Actualizar El Cliente.
     |DDEFECTO #19;
     #19#0 = #2#2;
     |LEE 000#19=;

     #10#0 = #1#4;
     |LEE 121#10=;
     |SI FSalida = 0;
          |SI #19#194 = 2;
               nImpo = ((((#2#44 * #2#6) < #2#8) < #2#29) < #1#5) < #1#17;
          |SINO;
               nImpo = ((((#2#5  * #2#6) < #2#8) < #2#29) < #1#5) < #1#17;
          |FINSI;
          |SI #41#115 = "S";
               nImpo = nImpo < #1#6;
          |FINSI;
          nImpo = nImpo * nForma;
          fmes = #1#1 % A402;
          mes = fmes - 1;
          mes = mes * 3;
          mes = mes + 114;
          #10mes = #10mes   + nImpo;     || VENTA.
          #10#150 = #10#150 + nImpo;
          |GRABA 020#10a;

          eaCliente = #agifa104#4;
          efFecha = #agifa104#1;
          enImpCliPed = nImpo;
          |HAZ AcumulaCli;
     |FINSI;
     |LIBERA #10;
|FPRC;

|DEFBUCLE agifa073;
     #4#1003;
     ;
     #3#25,#3#0, 001;
     #3#25,#3#0, 999;
     e;
     NULL,agifa073;
|FIN;

|PROCESO agifa104;
     sw_pasada = 1;
     #0#2 = #3#25;  |PINTA #0#2;
     #0#3 = #3#0;   |PINTA #0#3;

     |PARA i = 0; |SI i [ n010; |HACIENDO i = i + 1;
          #1i = #3i;
     |SIGUE;
     |LEE 000#1=;
     |SI FSalida  = 0;
          |SI #1#24 ! 0;
               eaPedido = "SERVIDO";
          |SINO;
               |BORRA 020#1c;
               eaPedido = "ACTUALIZADO";
               FSalida = 1;
          |FINSI;
     |SINO;
          eaPedido = "NUEVO";
     |FINSI;

     |SI FSalida ! 0;
          |PARA i = 0; |SI i [ n010; |HACIENDO i = i + 1;
               #1i = #3i;
          |SIGUE;
          ||컴컴컴컴컴컴컴컴컴 Cambio Moneda Base
          #1#36 = #agifa324#2;
          #1#41 = #agifa324#2;
          ||컴컴컴컴컴컴컴컴컴
          #200#0 = #1#4;
          |LEE 000#200=;
          |SI FSalida = 0;
               #1#4 = #200#1;
          |FINSI;

          |DDEFECTO #10;
          #10#0 = #1#4;
          |LEE 000#10=;
          #1#9 = #10#20;      || F.P.

          aDivisa = #1#35;
          aMoneda = #1#37;
          |HAZ Divisas104;

          |BUCLE agifa073;
          |GRABA 020#1b;

          |HAZ CalCabAgifa104;
          |GRABA 020#1a;
          |LIBERA #1;
     |FINSI;

     |SI nImpre = 0;
          enSw = 1;
          |PRINT;
     |FINSI;
     aRepre = #1#7;
|FPRC;

|DEFBUCLE agifa104;
     #3#1002;
     ;
     "     " ,       0;
     "zzzzz" ,  999999;
     e;
     NULL,agifa104;
|FIN;

||_________________________________________/ IMPORTACION CLIENTES
|PROCESO agifa024;

     aCod = #22#0 % 101;
     |SI aCod = "N";
          |HAZ codigonuevo;
     |SINO;
          qCod = #22#0;
     |FINSI;
     #200#0 = #22#0;
     |LEE 101#200=;
     |SI FSalida ! 0;
          #200#1 = qCod;
          |GRABA 020#200n;
     |FINSI;
     |LIBERA #200;
     sw_pasada = 1;
     |PINTA 2163 , blanco1;
     |PINTA 2163 , #agifa024#0;

     #10#0 = qCod;
     |LEE 101#10=;
     |SI FSalida ! 0;
          |DDEFECTO #10;
          #10#0 = qCod;
          |GRABA 101#10b;
     |FINSI;
     |PARA i = 1; |SI i [ 48; |HACIENDO i = i + 1;
          #10i = #22i;
     |SIGUE;
     #10#53 = #22#53;        ||Observaciones 1
     |PARA i = 156; |SI i [ 174; |HACIENDO i = i + 1;
          #10i = #22i;
     |SIGUE;
     |PARA i = 178; |SI i [ n024; |HACIENDO i = i + 1;
          #10i = #22i;
     |SIGUE;
     |SI qCod = #22#0;
          #10#175 = "S";
          #10#176 = fechat;
     |SINO;
          #10#175 = "N";
          #10#176 = "01.01.0000";
     |FINSI;
     |GRABA 021#10a;
     |LIBERA #10;

     |SI nImpre = 0;
          enSw = 2;
          |PRINT;
     |FINSI;
|FPRC;

|DEFBUCLE agifa024;
     #22#1001;
     ;
     "      ";
     "zzzzzz";
     e;
     NULL , agifa024;
|FIN;

-------------------------------------------------------------------------
|PROCESO tipo3;
     fechat = ~;
     fFecha = ~;

     |HAZ BusCampos;
     |SI err ! 0;
          |MENAV "0000Proceso anulado...";
          |ACABA;
     |FINSI;

     |ABRE #30;
     |LEE 000#30p;
     |SI FSalida ! 0;
          |CIERRA #30;
          |MENAV "0000No se encuentra el Control Path";
          |ERROR;
          |ACABA;
     |FINSI;
     |CIERRA #30;
     path = #30#5;  |QBF path;

     |HAZ CargaDef;

     |SI swErr ! 0;
          aMensa = "0000Error al cargar:" + aDef;
          |MENAV aMensa;
          |HAZ DesCargaDef;
          |ACABA;
     |FINSI;

     |SI #0#15 = "S";
           |CONFI "0000SRecibir datos desde portatil ahora: ";
           |SI FSalida = 0;
               |MENAV "0000Atencion!! Debe estar conectado a internet";
               |SUB_EJECUTA "dsm00231.tab";
           |FINSI;
     |FINSI;
     |HAZ BorraExpor;
     |HAZ CargaTempo;
     |HAZ BorraExpor;

     |HAZ DesCargaDef;
|FPRC;


|PROCESO Importacion;
     |ABRE #41; |LEE 001#41p; |CIERRA #41;
     |SI #0#4 = "S";     ||Clientes
          |PINTA 2124 , "Clientes        ";
          |PATH_DAT #22 path;
          |ABRE #10;
          |ABRE #200;
          |BUCLE agifa024;
          |CIERRA #10;
          |CIERRA #200;
     |FINSI;
     |SI #0#0 = "S";
          |PINTA 2124 , "Pedidos Venta";
          |ABRE #1;
          |ABRE #2;
          |ABRE #10;
          |ABRE #19;
          |ABRE #200;
          |PATH_DAT #3 path;
          |PATH_DAT #4 path;
          |BUCLE agifa104;
          |CIERRA #1;
          |CIERRA #2;
          |CIERRA #10;
          |CIERRA #200;
          |CIERRA #19;
     |FINSI;
     |DELINDEX #200;
     |HAZ Auxiliares;
|FPRC;

|PROCESO mayus;
 #0Campo = #0Campo > " ";
 |PINTA #0Campo;
|FPRC;

=======================================================================
|PROCESO BusCampos;
     |DFICE org; |QBF org;
     |DBASE des; |QBF des;
     |DDEF  def; |QBF def;
     eaNomDef = "agifa104"; |HAZ CamposDef; n010 = enCamposDef;
     eaNomDef = "agifa073"; |HAZ CamposDef; n071 = enCamposDef;
     eaNomDef = "agifa024"; |HAZ CamposDef; n024 = enCamposDef;
|FPRC;

|PROCESO CargaDef;
     |DBASE aPath;

     aDef1 = aPath + "def/" + "agifa104";
     aDef  = aDef1;
     |CARGA_DEF aDef, expor104@;
     |SI FSalida ! 0;
          swErr = 1;
          aDef1 = "";
          |ACABA;
     |FINSI;

     aDef2 = aPath + "def/" + "agifa073";
     aDef  = aDef2;
     |CARGA_DEF aDef, expor073@;
     |SI FSalida ! 0;
          swErr = 1;
          aDef2 = "";
          |ACABA;
     |FINSI;

     aDef13= aPath + "def/" + "agifa024";
     aDef  = aDef13;
     |CARGA_DEF aDef, expor024@;
     |SI FSalida ! 0;
          swErr = 1;
          aDef13 = "";
          |ACABA;
     |FINSI;


     |HAZ NomeDat;

|FPRC;

|PROCESO DesCargaDef;
     |SI aDef13 ! ""; |DESCARGA_DEF #expor024@; |FINSI;
     |SI aDef2  ! ""; |DESCARGA_DEF #expor073@; |FINSI;
     |SI aDef1  ! ""; |DESCARGA_DEF #expor104@; |FINSI;
|FPRC;

|PROCESO NomeDat;
     |NOME_DAT #22 "expor024";
     |NOME_DAT #4  "expor073";
     |NOME_DAT #3  "expor104";
|FPRC;
컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
|PROCESO BorraExpor;
     aFic = path;
     |_PDIR aFic;
     |PARA; |SI FSalida = 0; |HACIENDO;
          aAlfa = aFic % -103;
          |SI aAlfa = "ddx"; |O aAlfa = "ixx"; |O aAlfa = "dat";
               aAlfa = aFic % -1102;
               |SI aAlfa = "ex"; |FBORRA aFic; |FINSI;
               aAlfa = aFic % -0507;
               |SI aAlfa = "testigo"; |FBORRA aFic; |FINSI;
          |FINSI;
          |_SDIR aFic;
     |SIGUE;
|FPRC;


|PROCESO CargaTempo;
     |SI path ! "";
          |DELINDEX #999;
          |ABRE #999;
          |HAZ BuscaFichero;
          |CIERRA #999;
     |FINSI;
|FPRC;

|PROCESO BuscaFichero;
     err = 1;
     |ABRE #100;
     aFic = path;
     |_PDIR aFic;
     |PARA; |SI FSalida = 0; |HACIENDO;
          aAlfa = aFic % -115;
          ||SELECT #2#100;
          ||#100#0 = "I";
          ||#100#5 = aAlfa;
          ||LEE 000#100=;
          ||SI FSalida ! 0;        || siempre lo graba !!! (PRIEGO)
               #999#4 = aAlfa;     || los paquetes se pueden repetir
               #999#1 = aAlfa;
               |GRABA 020#999n;
          ||FINSI;
          |_SDIR aFic;
     |SIGUE;
     |CIERRA #100;
     ||컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
     |LEE 000#999p;
     |PARA; |SI FSalida = 0; |HACIENDO;
          aFic = path + #999#4;
          |HAZ DesComprime;
          |SI err = 0;
               |SI nImpre = 0;
                    |INFOR aInforme;
                    nImpre = FSalida;
                    |SI nImpre ! 0; |FINIMP; |FINSI;
               |FINSI;

               |HAZ Importacion;
               |HAZ ActHisto;
               |HAZ BorraExpor;

               |SI nImpre = 0;
                    |PIEINF;
                    |FININF;
               |FINSI;

          |FINSI;
          |LEE 000#999s;
     |SIGUE;
|FPRC;

|PROCESO ActHisto;
     |ABRE #100;
     |SELECT #1#100;
     #100#6 = aRepre;
     #100#0 = "I";
     #100#1 = fFecha;
     #100#2 = 999;
     |LEE 000#100];
     |SI FSalida = 0;
          |LEE 000#100a;
     |SINO;
          |LEE 000#100u;
     |FINSI;
     |SI FSalida = 0;|Y #100#6 = aRepre;|Y #100#0 = "I";|Y #100#1 = fFecha;
          nLin = #100#2 + 1;
     |SINO;
          nLin = 1;
     |FINSI;

     |HORA aHora;

     |DDEFECTO #100;
     #100#6 = aRepre;
     #100#0 = "I";
     #100#1 = fFecha;
     #100#2 = nLin;
     #100#3 = aHora;
     #100#4 = Usuario % 810;
     #100#5 = aFic % -115;
     |GRABA 020#100n;
     |CIERRA #100;
|FPRC;

|PROCESO DesComprime;
     ||---------Chequeo del fichero (debe se todo con numeros)
     ||         DETAR se cuelga si el fichero no es un tar
     err = 0;
     |QBF aFic;
     aAlfa = aFic % -115;
     |PARA i = 101; |SI i [ 1500; |HACIENDO i = i + 100;
          aAlf1 = (aAlfa % i);
          |SI aAlf1 < "0"; |O aAlf1 > "9";
               |SI i = 1201; |Y aAlf1 = ".";
               |SINO;
                    err = 1;
                    |SAL;
               |FINSI;
          |FINSI;
     |SIGUE;
     ||----------
     |SI err = 0;
          aAlfa = aFic % A0;
          nLon = aAlfa;
          nLon = nLon + 100 - 4;
          aOrg = aFic % nLon;
          aExt = aFic % -104;
          aGz  = aOrg + ".gz";

          |RENOMBRA_FICHERO aFic, aGz;
          |DESCOMPRIME aGz;
          |RENOMBRA_FICHERO aOrg, aFic;

          |XABRE "B", aFic, nHandle;
          |XLEE nHandle, 1, aAlfa;
          nHay = FSalida;
          |XCIERRA nHandle;

          |SI nHay > 0;
               |DETAR aFic, path;
               nSal = FSalida;
               |FBORRA aFic;
               |RENOMBRA_FICHERO aGz, aFic;
          |FINSI;

          |SI nSal = 0;
               err = 0;
          |SINO;
               err = 1;
               aAlfa = "0000Error en... [" + aFic + "]";
               |MENAV aAlfa;
          |FINSI;
     |FINSI;
|FPRC;

|PROGRAMA;
     |HAZ DecimalesBase;

     aParam = PARAMETRO;
     |QBF aParam;
     |ABRE #0;
     #0#16 = 1;
     |LEE 101#0=;
     |SI FSalida ! 0;
          |DDEFECTO #0;
          |GRABA 020#0b;
     |FINSI;
     |SI aParam = "centralaut";
          |HAZ tipo3;
     |SINO;

          |PINPA #0#0;
          |PINDA #0#0;
          |ENDATOS #1#0;
          |SI FSalida = 0;
               |IMPRE 0;
               nImpre = FSalida;

               |GRABA 020#0a;
               |HAZ tipo3;

               |SI nImpre = 0;
                    |FINIMP;
               |FINSI;
          |FINSI;
     |FINSI;
     |CIERRA #0;
|FPRO;

|PROCESO codigonuevo;
     |ABRE #32;
     #32#0 = "clientes";
     |LEE 101#32=;
     |SI FSalida = 0;
          #32#1    = #32#1 + 1;
          contador = #32#1;
          |GRABA 021#32a;
     |SINO;
          contador  = 1;
          #32#1     = 1;
          |GRABA 021#32n;
     |FINSI;
     qCod = contador;
     qCod = ("000000" + qCod) % -106;
     |LIBERA #2;
     |CIERRA #2;
|FPRC;
