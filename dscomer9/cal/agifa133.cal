|FICHEROS;
     agifa133 #133;
     agifa142 #1;
     agifa134 #2;
     agifa084 #3;
     agifa323 #323;
     agifa324 #324;
     agifa135 #0,MANTE;
|FIN;

|VARIABLES;
     aDef = "";
     aParam         = "";
     nModoEnt       = 0;
     &Tempo         = "";
     aTempo0        = "";
     &nFactor       = 0;
     &aDivisa       = "";
     &nDeci_Base    = 0;
     &nDeci_TotVisF = 0;
     &nDeci_TotVis  = 0;
     &aPrograma     = "";
     &ser_fac       = "";
     &num_fac       = 0;
     &enPres        = 0;

     mensaje        = "";

     importe_t      = 0;
     total          = 0;
     num_lin        = 0;
     sw_modo        = 0;
     nGuardaDiv     = 0;
     i              = 0;
     aFic           = "";
     nResto         = 0;
     nUltLin        = 0;
     nModi          = 0;
     nACta          = 0;
     aAlfa          = "";
|FIN;

|PROCESO DescargaVenci;
     nUltLin = nUltLin + 1;
     #133#4 = #0#4;
     #133#0 = #0#0;
     #133#1 = nUltLin;
     |LEE 101#133=;
     |SI FSalida ! 0; |GRABA 020#133b; |FINSI;
     #133#2 = (#0#2 / nFactor) ! nDeci_Base;
     #133#3 = #0#3;
     #133#5 = #0#5;
     #133#6 = #0#6;
     #133#7 = #0#7;
     #133#8 = #0#8;
     |GRABA 020#133a;
     |LIBERA #133;
|FPRC;

|DEFBUCLE DescargaVenci;
     #0#1;
     ;
     ser_fac , num_fac ,   0;
     ser_fac , num_fac , 999;
     e;
     NULL , DescargaVenci;
|FIN;

|PROCESO CargaVenci;
     nUltLin = nUltLin + 1;
     |PARA i = 0; |SI i [ 8; |HACIENDO i = i+1;
          #0i = #133i;
     |SIGUE;
     #0#1 = nUltLin;

     #0#2 = #0#2 * nFactor;
     |GRABA 020#0n;
     num_lin = num_lin + 1;
     importe_t = (importe_t + #0#2) ! nDeci_Base;
|FPRC;

|DEFBUCLE CargaVenci;
     #133#1003;
     ;
     ser_fac , num_fac ,   0;
     ser_fac , num_fac , 999;
     e;
     NULL , CargaVenci;
|FIN;

|PROCESO pinta;
     |SI enPres = 1;
         |ACABA;
     |FINSI;

     |SI aParam = "NOMODI";
          sw_modo = 4;
     |FINSI;

     |PUSHV 0653 1979;
     |ENTLINEAL #1#0 , -3 , sw_modo , 18 , 1 , pro133 , pro233;
     |POPV;
|FPRC;

|PROCESO pro133;
     #0#4 = ser_fac; ||serie
     #0#0 = num_fac;
     #0#1 = 1;
|FPRC;

|PROCESO pro233;
     #0#4 = ser_fac; ||serie
     #0#0 = num_fac;
     #0#1 = num_lin;
|FPRC;

|PROCESO sumar; |TIPO 2;
     nModi = 1;
     importe_t = importe_t +. #0#2;
     |SI #0#5 = "S";
          |MENAV "    No se puede modificar un recibo adelantado";
          |ERROR;
          nModi = 0;
     |FINSI;
|FPRC;

|PROCESO control; |TIPO 11;
     |SI FSalida ! 1; |Y FSalida ! 7; |ACABA; |FINSI;
     |SI sw_modo = 4; |ACABA; |FINSI;

     |SI aPrograma = "agifa084";
          importe_t = importe_t ! nDeci_TotVisF;
          total = total ! nDeci_TotVisF;
     |SINO;
          importe_t = importe_t ! nDeci_TotVis;
          total = total ! nDeci_TotVis;
     |FINSI;

     |SI importe_t ! total;
          |SI #agifa142#4 = "N";
               mensaje = "0000El importe de los vencimientos "+importe_t+ " no coincide con el de la Factura:"+ total;
               |MENAV mensaje;
               |ERROR;
          |SINO;
               |AVISO;
               mensaje = "2400NEl importe de Factura:"+total+" No Coincide con los Vencimientos "+importe_t+", Seguir:";
               |CONFI mensaje;
               |SI FSalida ! 0;
                    |ERROR;
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO lee_factucont;
     |ABRE #agifa084;
     |DDEFECTO #agifa084;
     #agifa084#48 = ser_fac;
     #agifa084#0 = num_fac;
     |LEE 001#agifa084.=;
     |CIERRA #agifa084;

     || ABDON (22/11/2004)
     ||   Esta linea estaba comentada, pero provocaba (junto el hecho de que
     ||   el proceso que calculaba el importe de los vencimientos no tomaba
     ||   en cuenta las entregas a cuenta) que cuando se hacia una factura
     ||   directa con una entrega a cuenta, no se reflejase correctamente en
     ||   el paso a DsCarter. La vuelvo a poner y comento la que habia.
     total = (#agifa084#73 - #agifa084#63) ! nDeci_TotVis;
     || total = #agifa084#73 ! nDeci_TotVis;

     aDivisa = #agifa084#65;
     |HAZ HallaFactor;
|FPRC;

|PROCESO lee_factu;
     |ABRE #agifa134;
     |DDEFECTO #agifa134;
     #agifa134#49 = ser_fac;
     #agifa134#0 = num_fac;
     |LEE 001#agifa134.=;
     |CIERRA #agifa134;

     || La linea siguiente estaba comentada, por lo que no tenia en cuenta las
     ||    entregas a cuenta para calcular el total.
     ||     total = (#agifa134#101 - #agifa134#32) ! nDeci_TotVisF;
     |SI #agifa134#32 ] 0;
        ||total = (#agifa134#101 - #agifa134#32) ! nDeci_TotVisF;
        total = #agifa134#101 ! nDeci_TotVisF;
     |SINO;
        total = (#agifa134#101 + #agifa134#32) ! nDeci_TotVisF;
     |FINSI;

||     total = #agifa134#101 ! nDeci_TotVisF;

     aDivisa = #agifa134#58;
     |HAZ HallaFactor;
|FPRC;

|PROGRAMA;
     aParam = PARAMETRO;  |QBF aParam;
     |SI aParam = "NOMODI";
          nModoEnt = 4;
     |SINO;
          nModoEnt = 2;
     |FINSI;

     |ABRE #agifa142; |LEE 001#agifa142.p; |CIERRA #agifa142;

     |HAZ CreaTempo; aTempo0 = Tempo;
     |NOME_DAT #0 aTempo0; |DELINDEX #0;
     |ABRE #0;

     nGuardaDiv = nDeci_Base;

     nUltLin = 0;
     |SI aPrograma = "agifa084";
          |HAZ lee_factucont;
          nDeci_Base = nDeci_TotVis;
          |SI #agifa142#2 = "S"; |Y #agifa084#10 = "N";
               sw_modo = 2;
               importe_t = 0;
               |SI #agifa142#10 = "S";
                    |BUCLE CargaVenci;
                    num_lin = 999;
               |SINO;
                    num_lin = 0;
                    |BUCLE CargaVenci;
               |FINSI;
          |SINO;
               |BUCLE CargaVenci;
               sw_modo = 4;
               num_lin = 999;
          |FINSI;
     |SINO;
          |HAZ lee_factu;
          nDeci_Base = nDeci_TotVisF;
          |SI #agifa142#2 = "S"; |Y #agifa134#45 = "N";
               sw_modo = 2;
               importe_t = 0;
               |SI #agifa142#10 = "S";
                    |BUCLE CargaVenci;
                    num_lin = 999;
               |SINO;
                    num_lin = 0;
                    |BUCLE CargaVenci;
               |FINSI;
          |SINO;
               |BUCLE CargaVenci;
               sw_modo = 4;
               num_lin = 999;
          |FINSI;
     |FINSI;

     |SI aParam ! "NOMODI"; ||컴컴컴컴 Ajuste en ultimo recibo
          nResto = importe_t - total;
          #0#4 = ser_fac;
          #0#0 = num_fac;
          #0#1 = nUltLin;
          |LEE 101#0=;
          |SI FSalida = 0;
               #0#2 = #0#2 - nResto;
               importe_t = importe_t - nResto;
               |GRABA 020#0a;
          |FINSI;
          |LIBERA #0;
     |FINSI;
     nModi = 0;

     |HAZ pinta;

     |SI nModi ! 0;     ||컴컴컴컴컴 Regrabar solo si modifican
          nUltLin = 0;
          |ABRE #133;
          |BUCLE DescargaVenci;
          ||컴컴컴컴컴컴컴컴컴컴컴컴 Borra por si anulan vtos
          #133#4 = ser_fac;
          #133#0 = num_fac;
          #133#1 = 0;
          |LEE 101#133];
          |PARA; |SI FSalida = 0; |Y #133#4 = ser_fac; |Y #133#0 = num_fac; |HACIENDO;
               |SI #133#1 > nUltLin;
                    |BORRA 020#133a;
               |FINSI;
               |LIBERA #133;
               |LEE 101#133s;
          |SIGUE;
          |CIERRA #133;
     |FINSI;

     nDeci_Base = nGuardaDiv;
     |CIERRA #0;

     |DELINDEX #0; Tempo = aTempo0; |HAZ BoraTempo;
|FPRO;
