|FICHEROS;
     ortsm023 #0;
     ortsm024 #1;
     ortsz011 #11,MANTE; ||Seleccion
     ortsm016 #16;
     ortsm017 #17;
     ortsm018 #18;
     agifa019 #19;
     ortsm020 #20;
     agifa024 #24;
     agifa073 #73;
     ortsm001 #100;
     agifa104 #104;

     ortsm006 #6;
     ortsm007 #7;
     ortsm013 #13;
     ortsm019 #119;
|FIN;

|VARIABLES;
     nBtnRefresca = 0;
     aSerie = "";
 {-1}aMenu  = "";
     aMenu1 = "2400";
     aMenu2 = "1";
     aMenu3 = "";
     aMenu4  = "EIOF";
     aMenu5  = "Exportar Fabri.";
     aMenu6  = "Importar Fabri.";
     aMenu7  = "Exportar Optimizador";
     aMenu8  = "Etiquetas Fabri.";

     nConta = 0;
     nModo = 0;
     nForma = 0;
     &enUni = 0;
     &eaCombi = "";
     &eaTira = "";
     &eaCodBar = "";
     aCodBarras = "";
     i = 0;
     j = 0;
     aAlfa = "";
     nUni = 0;
|FIN;

|PROCESO ObtenCodBarras;
     aCodBarras = #17#4;
     aCodBarras = aCodBarras - "-" - "-" - "-" - "-" - "-" - "-" - "-";
     aCodBarras = aCodBarras - "-" - "-" - "-" - "-" - "-" - "-" - "-";
     eaCodBar = aCodBarras;
|FPRC;

|PROCESO Ultimo;
     #1#13 = #0#3;
     #1#0 = #0#0;
     #1#1 = 9999;
     |LEE 000#1];
     |SI FSalida = 0;
          |LEE 000#1a;
     |SINO;
          |LEE 000#1u;
     |FINSI;
     |SI FSalida = 0; |Y #1#0 = #0#0; |Y #1#13 = #0#3;
          nConta = #1#1 + 1;
     |SINO;
          nConta = 1;
     |FINSI;
     |DDEFECTO #1;
     #1#13 = #0#3;
     #1#0 = #0#0;
     #1#1 = nConta;
|FPRC;

|PROCESO Resto;
     |SI #17#8 = "S"; nUni = #16#5; |SINO; nUni = #17#7 * #16#5; |FINSI;
     #1#14 = #19#114;
     #1#15 = nUni;
     #1#16 = #19#113;
     #1#17 = #19#110;
     #1#18 = #19#111;
     #1#19 = #19#112;
     #1#20 = #19#200;

     |SI #119#2 = "S";
          #1#9 = #17#35;
          #1#14 = #17#18;
          #1#16 = #17#22;
          #1#17  = #17#19;
          #1#18 = #17#31;
          #1#20 = #17#27;
     |FINSI;

     |SI #119#2 = "S"; |Y #20#21 = "S"; |Y #17#8 = "S";
          #1#11 = #18#15;
          #1#12 = #18#14;
          #1#14 = #18#16;
          #1#16 = #17#22;
          #1#17 = #18#11;
          #1#18 = #18#10;
          aAlfa = #18#17; |QBF aAlfa;
          |SI aAlfa ! ""; #1#20 = #18#17; |FINSI;
     |FINSI;
|FPRC;

|PROCESO ortsm018;
     |HAZ Ultimo;
     #1#2 = #18#0;
     #1#3 = #18#1;
     #1#4 = #18#2;
     #1#5 = #18#3;
     #1#6 = #18#4;  ||*

     #1#7 = #17#4;
     #1#8 = #18#5;  ||*
     #1#9 = #17#15;
     #1#10 = #16#61;
     #1#11 = #17#16; || #17#13;
     #1#12 = #17#17; || #17#14;
     |HAZ Resto;
     |GRABA 020#1n;
|FPRC;

|DEFBUCLE ortsm018;
     #18#1;
     ;
     #17#0, #17#1, #17#2, #17#3, 001;
     #17#0, #17#1, #17#2, #17#3, 999;
     ;
     NULL, ortsm018;
|FIN;

|PROCESO ortsm017;
     #19#0 = #17#4;
     |LEE 000#19=;
     |SI FSalida ! 0; |DDEFECTO #19; |FINSI;

     |SI #19#127 < #11#22; |O #19#127 > #11#23; |ACABA; |FINSI;
     |SI #19#2 < #11#24;   |O #19#2 > #11#25; |ACABA; |FINSI;
     |SI #19#3 < #11#26;   |O #19#3 > #11#27; |ACABA; |FINSI;
     |SI #19#125 < #11#28; |O #19#125 > #11#29; |ACABA; |FINSI;

     |SI #17#39 = "N";
          #20#0 = #16#2;
          #20#1 = #17#3;
     |SINO;
          #20#0 = #17#43;
          #20#1 = #17#44;
     |FINSI;
     |LEE 000#20=;
     |SI FSalida ! 0; |DDEFECTO #20; |FINSI;

     |SI #17#8 = "N";
          |HAZ Ultimo;
          #1#2 = #17#0;
          #1#3 = #17#1;
          #1#4 = #17#2;
          #1#5 = #17#3;

          #1#6 = 0;
          #1#7 = #17#4;
          #1#8 = #17#6;
          #1#9 = #17#15;
          #1#10 = #16#61;
          #1#11 = #17#16; ||#17#13;
          #1#12 = #17#17; ||#17#14;
          |HAZ Resto;
          |GRABA 020#1n;
     |SINO;
          |BUCLE ortsm018;
     |FINSI;
|FPRC;

|DEFBUCLE ortsm017;
     #17#1;
     4;
     #73#28, #73#0, #73#1, 000, #11#20;
     #73#28, #73#0, #73#1, 999, #11#21;
     ;
     NULL, ortsm017;
|FIN;

|PROCESO agifa073;
     |SI #73#2 < #11#12; |O #73#2 > #11#13;
          |ACABA;
     |FINSI;

     #19#0 = #73#2;
     |LEE 000#19=;
     |SI FSalida ! 0; |DDEFECTO #19; |FINSI;

     |SI #19#127 < #11#14; |O #19#127 > #11#15; |ACABA; |FINSI;
     |SI #19#2 < #11#16;   |O #19#2 > #11#17; |ACABA; |FINSI;
     |SI #19#125 < #11#18; |O #19#125 > #11#19; |ACABA; |FINSI;


     #16#28 = #73#28;
     #16#0 = #73#0;
     #16#1 = #73#1;
     |LEE 000#16=;
     |SI FSalida ! 0; |DDEFECTO #16; |FINSI;

     #119#0 = #16#2;
     |LEE 000#119=;
     |SI FSalida ! 0; |DDEFECTO #119; |FINSI;

     |BUCLE ortsm017;
|FPRC;

|DEFBUCLE agifa073;
     #73#1;
     ;
     #104#25, #104#0, 001;
     #104#25, #104#0, 999;
     ;
     NULL, agifa073;
|FIN;

|PROCESO agifa104;
     #24#0 = #104#4;
     |LEE 000#24=;
     |SI FSalida ! 0; |DDEFECTO #24; |FINSI;

     |SI #24#3 < #11#6; |O #24#3 > #11#7; |ACABA; |FINSI;

     |BUCLE agifa073;
|FPRC;

|DEFBUCLE agifa104;
     #104#1;
     4, 1, 2;
     #11#0, #11#2, #11#4, #11#8, #11#10;
     #11#1, #11#3, #11#5, #11#9, #11#11;
     ;
     NULL, agifa104;
|FIN;

|PROCESO Lote0; |TIPO 0;
|FPRC;

|PROCESO Lote7; |TIPO 7;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 1;
          aSerie = #0#3;
          #0#0 = 999999;
          |LEE 000#0];
          |SI FSalida = 0;
               |LEE 000#0a;
          |SINO;
               |LEE 000#0u;
          |FINSI;
          |SI FSalida = 0; |Y aSerie = #0#3;
               nConta = #0#0 + 1;
          |SINO;
               nConta = 1;
          |FINSI;
          |DDEFECTO #0;
          #0#3 = aSerie;
          #0#0 = nConta;
     |FINSI;
|FPRC;

|PROCESO BorraLin;
     |BORRA 020#1a;
|FPRC;

|DEFBUCLE BorraLin;
     #1#1;
     ;
     #0#3, #0#0, 0000;
     #0#3, #0#0, 9999;
     be;
     NULL, BorraLin;
|FIN;

|PROCESO Seleccion; |TIPO 0;
     nModo = (FEntrada / 100) ! 0;
     |SI #0#2 = 1;
          |CONFI "0000N¿Seguro que quiere recargar los datos, enviado al optimizador?";
          |SI FSalida ! 0; |ACABA; |FINSI;
     |FINSI;
     |SI nModo = 2;
          |CONFI "0000N¿Desea inicializar las lineas del lote?";
          |SI FSalida = 0;
               |BUCLE BorraLin;
          |FINSI;
     |FINSI;
     |PUSHV 0501 2380;
     |PINPA #0#11;
     |PINDA #0#11;
     |ENDATOS #1#11;
     |SI FSalida = 0;
          nConta = 0;
          |ABRE #1;
          |ABRE #16;
          |ABRE #19;
          |ABRE #24;
          |ABRE #119;
          |ABRE #20;
          |BUCLE agifa104;
          |CIERRA #20;
          |CIERRA #119;
          |CIERRA #24;
          |CIERRA #19;
          |CIERRA #16;
          |CIERRA #1;
          |PTEC 816;
          |PTEC 812;
     |FINSI;
     |POPV;
|FPRC;

|PROCESO T20; |TIPO 20;
     aAlfa = FSalida;
     |ESTADO_CONTROL nBtnRefresca, "DISABLE";
     FSalida = aAlfa;

     |SI FSalida = "OPCION";
          FSalida = "Opciones";
     |SINO;
          |LEE 000#0=;
          |SI FSalida ! 0; |ACABA; |FINSI;
          |MENU aMenu;
          |PUSHV 0501 2380;;
          |SI FSalida = 1; |HAZ ExportaFab; |FINSI;
          |SI FSalida = 2; |HAZ ImportaFab; |FINSI;
          |SI FSalida = 3; |HAZ ExportaOpt; |FINSI;
          |SI FSalida = 4; |HAZ Etiquetas; |FINSI;
          |POPV;
     |FINSI;
|FPRC;

|PROCESO LineasEti;
     |SI #1#9 = "X";
          |ACABA;
     |FINSI;

     #104#25 = #1#2;
     #104#0 = #1#3;
     |LEE 000#104=;
     |SI FSalida ! 0; |DDEFECTO #104; |FINSI;

     #73#28 = #1#2;
     #73#0 = #1#3;
     #73#1 = #1#4;
     |LEE 000#73=;
     |SI FSalida ! 0; |DDEFECTO #104; |FINSI;

     #16#28 = #1#2;
     #16#0 = #1#3;
     #16#1 = #1#4;
     |LEE 020#16=;
     |SI FSalida ! 0; |DDEFECTO #16; |FINSI;

     #17#0 = #1#2;
     #17#1 = #1#3;
     #17#2 = #1#4;
     #17#3 = #1#5;
     |LEE 020#17=;
     |SI FSalida ! 0; |DDEFECTO #17; |FINSI;

     #18#0 = #1#2;
     #18#1 = #1#3;
     #18#2 = #1#4;
     #18#3 = #1#5;
     #18#4 = #1#6;
     |LEE 000#18=;
     |SI FSalida ! 0; |DDEFECTO #18; |FINSI;

     #19#0 = #17#4;
     |LEE 020#19=;
     |SI FSalida ! 0; |DDEFECTO #19; |FINSI;


     |SI #17#8 = "S";
          #6#0 = #18#5;
          #7#0 = #18#6;
     |SINO;
          #6#0 = #17#6;
          #7#0 = #17#11;
     |FINSI;
     |LEE 000#6=;
     |SI FSalida ! 0; |DDEFECTO #6; |FINSI;
     |LEE 000#7=;
     |SI FSalida ! 0; |DDEFECTO #7; |FINSI;

     |SELECT #2#13;
     #13#1 = #104#25;
     #13#2 = #104#0;
     |LEE 000#13=;
     |SI FSalida ! 0; |DDEFECTO #13; |FINSI;

     |SI #17#8 = "S";
          enUni = #1#6; ||  #16#5;
          eaCombi = #18#5;
          eaTira = #18#6;
     |SINO;
          enUni = #17#7;
          eaCombi = #17#6;
          eaTira = #17#11;
     |FINSI;

     |HAZ ObtenCodBarras;
     |QBF eaCodBar;
     eaCodBar = eaCodBar + (eaTira % 102);
/*
     |SI #17#8 = "S";
          |PARA i = 1; |SI i [ #16#5; |HACIENDO i = i + 1;
               |PRINT;
          |SIGUE;
     |SINO;
          |PARA i = 1; |SI i [ #16#5; |HACIENDO i = i + 1;
               |PARA j = 1; |SI j [ #17#7; |HACIENDO j = j + 1;
                    enUni = j;
                    |PRINT;
               |SIGUE;
          |SIGUE;
     |FINSI;

*/
     |PARA i = 1; |SI i [ #1#15; |HACIENDO i = i + 1;
          |PRINT;
     |SIGUE;
|FPRC;

|DEFBUCLE LineasEti;
     #1#1;
     9;
     #0#3, #0#0, 0001, "N";
     #0#3, #0#0, 9999, "N";
     ;
     NULL, NULL, NULL, NULL, NULL, LineasEti;
     #1#7;
|FIN;

|PROCESO Etiquetas;
     |ABRE #100;
     #100#0 = "";
     |LEE 000#100=;
     |CIERRA #100;

     Impresora = #100#4; |QBF Impresora;
     |IMPRE -1;
     |SI FSalida = 0;
          aAlfa = "ortsm023"; |QBF aAlfa;
          |INFOR aAlfa;
          |SI FSalida = 0;
               |ABRE #104;
               |ABRE #73;
               |ABRE #16;
               |ABRE #17;
               |ABRE #18;
               |ABRE #19;
               |ABRE #6;
               |ABRE #7;
               |ABRE #13;
               |BUCLE LineasEti;
               |CIERRA #13;
               |CIERRA #7;
               |CIERRA #6;
               |CIERRA #19;
               |CIERRA #18;
               |CIERRA #17;
               |CIERRA #16;
               |CIERRA #73;
               |CIERRA #104;

               |PIEINF;
               |FININF;
          |SINO;
               aAlfa = "0000Error en informe:" + aAlfa;
               |MENAV aAlfa;
          |FINSI;
          |FINIMP;
     |FINSI;
|FPRC;

|PROCESO Lin7; |TIPO 7;
     |ESTADO_CONTROL nBtnRefresca, "ENABLE";
|FPRC;

|PROCESO Lin6; |TIPO 6;
     aAlfa = FSalida;
     |ESTADO_CONTROL nBtnRefresca, "DISABLE";
     FSalida = aAlfa;
|FPRC;

|PROCESO BtnRefresca;
     |PTEC 827;
|FPRC;

|PROCESO T9; |TIPO 9;
     |FIN_CONTROL_WINDOWS nBtnRefresca;
|FPRC;

|PROCESO T8; |TIPO 8;
     |CONTROL_SIMPLE 0, "BOTON,F5.- Refrescar", 0666, 0677, BtnRefresca;
     nBtnRefresca = FSalida;
|FPRC;

|PROCESO Lin11; |TIPO 11;
     |SI FSalida = 13;
          |SI #1#6 = 0;
               |ABRE #17;
               #17#0 = #1#2;
               #17#1 = #1#3;
               #17#2 = #1#4;
               #17#3 = #1#5;
               |LEE 000#17=;
               |SI FSalida = 0;
                    #1#8 = #17#6; |PINTA #1#8;
               |FINSI;
               |CIERRA #17;
          |SINO;
               |ABRE #18;
               #18#0 = #1#2;
               #18#1 = #1#3;
               #18#2 = #1#4;
               #18#3 = #1#5;
               #18#4 = #1#6;
               |LEE 000#18=;
               |SI FSalida = 0;
                    #1#8 = #18#5; |PINTA #1#8;
               |FINSI;
               |CIERRA #18;
          |FINSI;
          |GRABA 000#1a;
          |ERROR;
     |FINSI;
|FPRC;
