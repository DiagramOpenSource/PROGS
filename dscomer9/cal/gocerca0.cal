               CreaRecibos

|FICHEROS;
     agifa133 #17;
     agifa084 #1;
     agifa024 #3;
     dsz99984 #11;
     agifa091 #6;
     agifa177 #99;
     agifa142 #41;
|FIN;

|VARIABLES;
     nVto = 0;
     nRecibo = 0;
     &enDirEnt = 0;
     &eaProg = "";
     &aDivisa = "";
     &aMoneda = "";
|FIN;

|PROCESO CreaRecibos;
     |HAZ DecimalesBase;

     aDivisa = #agifa084#65;
     aMoneda = #agifa084#67;
     |HAZ Divisas084;

     |DDEFECTO #3;
     |ABRE #3;
     #3#0 = #1#3;
     |LEE 000#3=;
     |CIERRA #3;

     |ABRE #17;
     #17#0 = #1#0;
     #17#4 = #1#48;
     #17#1 = 0;
     |LEE 001#17];
     nVto = 0;
     |SI FSalida ! 0; |O #17#0 ! #1#0;  |O #17#4 ! #1#48;
         |HAZ VenciDir;
     |FINSI;

     |ABRE #99;
     nVto = 0;
     #17#0 = #1#0;
     #17#4 = #1#48;
     #17#1 = 0;
     |LEE 001#17];
     |PARA ; |SI FSalida = 0; |Y #17#0 = #1#0;|Y #17#4 = #1#48; |HACIENDO;
          nVto = nVto + 1;
          |LEE 001#17s;
     |SIGUE;

     |HAZ AbreRecVenta;

     nRecibo = 0;
     #17#0 = #1#0;
     #17#4 = #1#48;
     #17#1 = 0;
     |LEE 001#17];
     |PARA ; |SI FSalida = 0; |Y #17#0 = #1#0;|Y #17#4 = #1#48; |HACIENDO;
          |DDEFECTO #11;
          #11#17 = #1#48;
          #11#0  = #1#0;
          #11#1  = #17#1;           || Linea;
          #11#2  = #1#3;            || cliente
          #11#3  = #1#4;            || nombre del cliente
          #11#4  = #3#29;           || Banco
          #11#5  = "S";             || domiciliado
          #11#6  = "N";             || aceptado
          #11#7  = #1#1;            || fecha de emision
          #11#8  = #17#3;           || Fecha de vencimiento
          #11#9  = #17#2;           || Importe
          #11#10 = "N";            || Impreso
          #11#11 = "N";            || Contabilizado
          #11#12 = #1#40;

          |ABRE #6;
          |DDEFECTO #6;
          #6#0 = #1#8;
          |LEE 000#6=;
          |CIERRA #6;
          |SI #41#63 = "S";
               #99#0 = #6#69;
               |LEE 000#99=;
               |SI FSalida = 0;
                    #11#12 = #99#3;     || NUMERO CTA.CONTABLE.
               |SINO;
                    #11#12 = #1#40;     || NUMERO CTA.CONTABLE.
               |FINSI;
          |SINO;
               #11#12 = #1#40;          || NUMERO CTA.CONTABLE.
          |FINSI;
          #11#13 = "N";            || Cobrado
          #11#14 = "N";            || Impagado
          #11#15 = #3#161;         || A aceptar
          #11#16 = "01.01.0000";   || fecha de cobro
          #11#18 = #1#40;          || Cta
          #11#21 = #6#69;          || Tipo de Recibo (forma de pago)
          nRecibo = nRecibo + 1;
          |SI nRecibo = nVto; |Y #6#70 ! 0;
               #11#21 = #6#73;     || Tipo de Recibo (Fianza retenida)
          |FINSI;
          #11#22 = "Pdte. Cobro";
          #11#23 = nVto;
          #11#24 = #6#0;
          #11#25 = #6#1;

          #11#26 = "";
          #11#27 = "";
          #11#29 = "";
          #11#28 = "";
          ||HAZ MiraDspreven;

          eaProg = "agifa084";
          ||HAZ CreaRecVenta;
          |HAZ RecVenta;
          |LEE 001#17s;
     |SIGUE;
     |CIERRA #17;
     |CIERRA #99;
     enDirEnt = #agifa084#91; |HAZ CierraRecVenta;
|FPRC;
