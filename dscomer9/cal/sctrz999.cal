|FICHEROS;
     sctrz999;
     sctrw999;

     fictodos@ #1;
     ficnuevo@ #2;
     ficmodif@ #3;
     ficborra@ #4;
     ficrecup@ #0;
|FIN;

|VARIABLES;
     nOk        = 0;
     nNumCampos = 0;
     i          = 0;
     nLonCam    = 0;
     nCamClave  = 0;
     nPosCam    = 0;
     nNume      = 0;
     nCmp       = 0;
     nEstado    = 0;
     nIgual     = 0;
     nLonCmp    = 0;
     nPosi      = 0;
     nUltReg    = 0;
     nValClave  = 0;

     aValor = "";
     aCamClave = "";
     aValClave = "";
     aCampo = "";
     aBase = "";
     aPath = "";
     aDes = "";
     aFic = "";
     aAlfa = "";
     aOrg = "";
     aEmpAnt = "";
     aDefAnt = "";
     aEmp = "";
     aDef = "";
     aLon = "";
     aPathApli1 = "";
     aPathApli2 = "";
     aFicDef = "";
     nCarga = 0;
     aFichero = "";
     aQueQuiero = "";
     aTipoAnt   = "";
     aAbre     = "";
     aGraba     = "";
     aFicRec    = "";

     nHandle    = 0;
     aLong     = "";
     nLong     = 0;
     aTab      = "";
     aCadena   = "";
     nCampo    = 0;
     nPos      = 0;
     nCarac    = 0;
     aCaracter = "";
     nRegis    = 0;
|FIN;

|PROCESO ficnuevo;
     |PARA nCmp = 0;  |SI nCmp [ nNumCampos;  |HACIENDO nCmp = nCmp + 1;
           #ficrecup@#nCmp# = #ficnuevo@#nCmp#;
     |SIGUE;

     |GRABA 000#ficrecup@.n;
     |LIBERA #ficrecup@;

     |BORRA 000#ficnuevo@.a;
     |LIBERA #ficnuevo@;
|FPRC;

|PROCESO ficmodif_Cheq;
     |PARA nCmp = 0;  |SI nCmp [ nNumCampos;  |HACIENDO nCmp = nCmp + 1;
           #ficrecup@#nCmp# = #ficmodif@#nCmp#;
     |SIGUE;
     |LEE 000#ficrecup@.=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     nIgual = 0;
     |PARA nCmp = 1;  |SI nCmp [ nNumCampos;  |HACIENDO nCmp = nCmp + 1;
           |SI #ficrecup@#nCmp# ! #ficmodif@#nCmp#;
               nIgual = 1;
           |SINO;
               #ficmodif@#nCmp# = "";
           |FINSI;
     |SIGUE;

     |SI nIgual = 1;
         |GRABA 000#ficmodif@.a;
     |SINO;
         |BORRA 000#ficmodif@.a;
     |FINSI;
     |LIBERA #ficmodif@;
|FPRC;

|PROCESO ficmodif_Gra;
     |PARA nCmp = 0;  |SI nCmp [ nNumCampos;  |HACIENDO nCmp = nCmp + 1;
           #ficrecup@#nCmp# = #ficmodif@#nCmp#;
           #fictodos@#nCmp# = #ficmodif@#nCmp#;
     |SIGUE;
     |LEE 000#ficrecup@.=;
     |SI FSalida ! 0;
        |ACABA;
     |FINSI;

     |LEE 000#fictodos@.=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |PARA nCmp = 0;  |SI nCmp [ nNumCampos;  |HACIENDO nCmp = nCmp + 1;
           |SI #ficrecup@#nCmp# ! #fictodos@#nCmp#;
               #ficrecup@#nCmp# = #fictodos@#nCmp#;
           |FINSI;
     |SIGUE;

     |GRABA 000#ficrecup@.a;
     |LIBERA #ficrecup@;

     |BORRA 000#ficmodif@.a;
     |LIBERA #ficmodif@;
|FPRC;

|PROCESO ficborra_Cheq;
     |PARA nCmp = 0;  |SI nCmp [ nNumCampos;  |HACIENDO nCmp = nCmp + 1;
           #fictodos@#nCmp# = #ficborra@#nCmp#;
     |SIGUE;
     |LEE 000#fictodos@.=;
     |SI FSalida = 0;
         |BORRA 000#ficborra@.a;
     |FINSI;
     |LIBERA #ficborra@;
|FPRC;

|PROCESO ficborra_Bor;
     |PARA nCmp = 0;  |SI nCmp [ nNumCampos;  |HACIENDO nCmp = nCmp + 1;
           #ficrecup@#nCmp# = #ficborra@#nCmp#;
     |SIGUE;
     |LEE 000#ficrecup@.=;
     |SI FSalida ! 0;
         |ACABA;
     |FINSI;

     |BORRA 000#ficrecup@.a;
     |LIBERA #ficrecup@;

     |BORRA 000#ficborra@.a;
     |LIBERA #ficborra@;
|FPRC;

|PROCESO PonVal;
     nValClave = #sctrw999#7;
     |SI nValClave < 1000;
         nValClave = nUltReg + nValClave;
         aValClave = ("000000" + nValClave) % -106;
     |FINSI;
|FPRC;

|PROCESO LeeReg;
     aValClave = #sctrw999#7;
     |SI aFicRec = "pycm0002";
         |HAZ PonVal;
     |FINSI;

     aAlfa     = "|K000";
     aCamClave = #fictodos@#aAlfa#;
     aAlfa     = aCamClave % 103;
     nCamClave = aAlfa;
     nPos = 4;
     nPosCam = 1;
     |PARA i = 1; |SI i [ nCamClave; |HACIENDO i = i + 1;
          nNume   = nPos * 100 + 3;
          aCampo  = aCamClave % nNume;
          nCampo  = aCampo;

          aAlfa   = "|C" + aCampo + "LONGITUD";
          aAlfa   = #fictodos@#aAlfa#;
          nLonCam = aAlfa;
          nNume   = nPosCam * 100 + nLonCam;
          aValor  = aValClave % nNume;

          #fictodos@#nCampo# = aValor;

          nPos    = nPos + 3;
          nPosCam = nPosCam + nLonCam + 1;
     |SIGUE;

     |LEE 000#fictodos@.=;
     |SI FSalida ! 0;
         |PARA nCmp = 0;  |SI nCmp [ nNumCampos;  |HACIENDO nCmp = nCmp + 1;
                #ficrecup@#nCmp# = #fictodos@#nCmp#;
         |SIGUE;
         |LEE 000#ficrecup@.=;
         |SI FSalida = 0;
             |PARA nCmp = 0;  |SI nCmp [ nNumCampos;  |HACIENDO nCmp = nCmp + 1;
                   #fictodos@#nCmp# = #ficrecup@#nCmp#;
             |SIGUE;
         |FINSI;
         |GRABA 000#fictodos@.b;
     |FINSI;

     |PARA nCmp = 0;  |SI nCmp [ nNumCampos;  |HACIENDO nCmp = nCmp + 1;
           #ficrecup@#nCmp# = #fictodos@#nCmp#;
     |SIGUE;
     |LEE 000#ficrecup@.=;
     |SI FSalida = 0;  |ACABA;  |FINSI;

     aValClave = #sctrw999#7;
     |SI aFicRec = "pycm0002";
         |HAZ PonVal;
     |FINSI;

     aAlfa     = "|K000";
     aCamClave = #fictodos@#aAlfa#;
     aAlfa     = aCamClave % 103;
     nCamClave = aAlfa;
     nPos      = 4;
     nPosCam   = 1;
     |PARA i = 1; |SI i [ nCamClave; |HACIENDO i = i + 1;
           nNume  = nPos * 100 + 3;
           aCampo = aCamClave % nNume;
           nCampo = aCampo;

           aAlfa   = "|C" + aCampo + "LONGITUD";
           aAlfa   = #fictodos@#aAlfa#;
           nLonCam = aAlfa;
           nNume   = nPosCam * 100 + nLonCam;
           aValor  = aValClave % nNume;

           #fictodos@#nCampo# = aValor;

           nPos    = nPos + 3;
           nPosCam = nPosCam + nLonCam + 1;
     |SIGUE;

     aGraba = "Nueva ficha: Tab-" + #sctrw999#5 + " Clave-" + #sctrw999#7 + &13 + &10;
     |XGRABA nHandle, aGraba;

     |PARA nCmp = 0;  |SI nCmp [ nNumCampos;  |HACIENDO nCmp = nCmp + 1;
           #ficnuevo@#nCmp# = #fictodos@#nCmp#;
     |SIGUE;

     |LEE 000#ficnuevo@.=;
     nEstado = FSalida;

     |PARA nCmp = 0;  |SI nCmp [ nNumCampos;  |HACIENDO nCmp = nCmp + 1;
           #ficnuevo@#nCmp# = #fictodos@#nCmp#;
     |SIGUE;

     |SI nEstado ! 0;
         |GRABA 000#ficnuevo@.n;
     |SINO;
         |GRABA 000#ficnuevo@.a;
     |FINSI;
     |LIBERA #ficnuevo@;
|FPRC;

|PROCESO Log;
     |SI #sctrw999#9 ! "00003     ";  |ACABA;  |FINSI;
     |SI #sctrw999#0 = "DS1117    ";  |ACABA;  |FINSI;
     |SI #sctrw999#1 < #sctrz999#1;   |ACABA;  |FINSI;

     aFicDef = #sctrw999#5; |QBF aFicDef;

     nOk = 0;
     |SI #sctrw999#3 = "C";
         |SI aTipoAnt ! "R32";  nOk = 1; |FINSI;
     |FINSI;

     |SI #sctrw999#3 = "R";
         |SI #sctrw999#4 = "32";
             nOk = 1;
         |FINSI;
         aTipoAnt = #sctrw999#3 + #sctrw999#4;
     |FINSI;

     |DFICE aFic;  |QBF aFic;

     |SI nOk = 0; |ACABA; |FINSI;

     nOk = 0;
     |SI aFicDef = aFicRec;  nOk = 1;  |FINSI;

     |SI nOk = 0; |ACABA; |FINSI;

     |SI aFicDef = aDefAnt; |Y nCarga = 0;
         |ACABA;
     |FINSI;

     nCampo   = #sctrw999#6;
     |SI nCampo > nNumCampos;  |ACABA;  |FINSI;

     |DDEFECTO #fictodos@;

     |HAZ LeeReg;
     |SI #sctrw999#3 = "C";               || Modifica campo
          nCampo   = #sctrw999#6;
          #fictodos@#nCampo# = #sctrw999#12;
          |GRABA 000#fictodos@.a;

          |PARA nCmp = 0;  |SI nCmp [ nNumCampos;  |HACIENDO nCmp = nCmp + 1;
                #ficrecup@#nCmp# = #fictodos@#nCmp#;
          |SIGUE;
          |LEE 000#ficrecup@.=;
          |SI FSalida = 0;
              |PARA nCmp = 0;  |SI nCmp [ nNumCampos;  |HACIENDO nCmp = nCmp + 1;
                    #ficmodif@#nCmp# = #fictodos@#nCmp#;
              |SIGUE;

              |LEE 000#ficmodif@.=;
              nEstado = FSalida;

              |PARA nCmp = 0;  |SI nCmp [ nNumCampos;  |HACIENDO nCmp = nCmp + 1;
                    #ficmodif@#nCmp# = #fictodos@#nCmp#;
              |SIGUE;

              |SI nEstado ! 0;
                  |GRABA 000#ficmodif@.n;
              |SINO;
                  |GRABA 000#ficmodif@.a;
              |FINSI;
              |LIBERA #ficmodif@;

              aGraba = "Actualizacion: Tab-" + #sctrw999#5 + " Clave-" + #sctrw999#7 + " Campo-" + #sctrw999#6 + " Valor-" + #sctrw999#12 + &13 + &10;
              |XGRABA nHandle, aGraba;
         |FINSI;
     |FINSI;

     |SI #sctrw999#3 = "R"; |Y #sctrw999#4 = "32";
          |BORRA 000#fictodos@.a;

          |PARA nCmp = 0;  |SI nCmp [ nNumCampos;  |HACIENDO nCmp = nCmp + 1;
                #ficborra@#nCmp# = #fictodos@#nCmp#;
          |SIGUE;

          |LEE 000#ficborra@.=;
          nEstado = FSalida;

          |PARA nCmp = 0;  |SI nCmp [ nNumCampos;  |HACIENDO nCmp = nCmp + 1;
                #ficborra@#nCmp# = #fictodos@#nCmp#;
          |SIGUE;

          |SI nEstado ! 0;
              |GRABA 000#ficborra@.n;
          |SINO;
              |GRABA 000#ficborra@.a;
          |FINSI;
          |LIBERA #ficborra@;

          aGraba = "Borrado: Tab-" + #sctrw999#5 + " Clave-" + #sctrw999#7 + &13 + &10;
          |XGRABA nHandle, aGraba;
     |FINSI;
     |LIBERA #fictodos@;

     |PULSATECLA;
|FPRC;

|DEFBUCLE Log;
     #sctrw999#2;
     ;
     INICIO;
     FINAL;
     ;
     NULL, Log;
|FIN;

|PROCESO IndexaTxtLong;
     nRegis = 0;
     aAbre  = #sctrz999#0;  |QBF aAbre;
     |XABRE "", aAbre, nHandle;
     |SI FSalida < 0;  |ACABA;  |FINSI;

     |PARA; |SI; |HACIENDO;
          |XLEE nHandle, 250, aAlfa;
          |SI FSalida < 0;
              |SAL;
          |FINSI;
          |QBF aAlfa;

          |DDEFECTO #sctrw999;
          aLong   = aAlfa % 0;
          nLong   = aLong;
          aCadena = "";
          nCampo  = 0;
          nPosi   = 0;
          |PARA nCarac = 1;  |SI nCarac [ nLong;  |HACIENDO nCarac = nCarac + 1;
                nPos      = (nCarac * 100) + 1;

                |SI nCampo = 0;  nLonCmp = 10;  |FINSI;
                |SI nCampo = 1;  nLonCmp = 8;   |FINSI;
                |SI nCampo = 2;  nLonCmp = 8;   |FINSI;
                |SI nCampo = 3;  nLonCmp = 1;   |FINSI;
                |SI nCampo = 4;  nLonCmp = 2;   |FINSI;
                |SI nCampo = 5;  nLonCmp = 12;  |FINSI;
                |SI nCampo = 6;  nLonCmp = 3;   |FINSI;
                |SI nCampo = 7;  nLonCmp = 50;  |FINSI;
                |SI nCampo = 8;  nLonCmp = 17;  |FINSI;
                |SI nCampo = 9;  nLonCmp = 10;  |FINSI;
                |SI nCampo = 10; nLonCmp = 50;  |FINSI;
                |SI nCampo = 11; nLonCmp = 30;  |FINSI;
                |SI nCampo = 12; nLonCmp = 30;  |FINSI;

                nPosi     = nPosi + 1;
                aCaracter = aAlfa % nPos;
                aCadena   = aCadena + aCaracter;

                |SI nPosi = nLonCmp;
                    |SI nCampo = 1;
                        aCadena = (aCadena % 702) + "." + (aCadena % 502) + "." + (aCadena % 104);
                    |FINSI;

                    #sctrw999#nCampo# = aCadena;
                    nCampo    = nCampo + 1;
                    nPosi     = 0;
                    aCadena   = "";
               |FINSI;
          |SIGUE;

          #sctrw999#nCampo# = aCadena;

          |SI #sctrw999#9 = "00003     ";
              nRegis        = nRegis + 1;
              #sctrw999#13  = nRegis;

              |GRABA 000#sctrw999.n;
              |LIBERA #sctrw999;
          |FINSI;
     |SIGUE;

     |XCIERRA nHandle;
|FPRC;

|PROCESO Tipo3; |TIPO 3;
     aFicRec = #sctrz999#2 < "";

     |DBASE aPath;  |QBF aPath;
     aPath = aPath + "fich/";

     |PATH_DAT #sctrw999#aPath#;
     |NOME_DAT #sctrw999#"ficlog"#;

     aDefAnt = "";
     nCarga  = 0;

     |ABRE #sctrw999;  |CIERRA #sctrw999;  |DELINDEX #sctrw999;
     |ABRE #sctrw999;
     |HAZ IndexaTxtLong;
     |CIERRA #sctrw999;

     |ABRE #sctrw999;
     |CONSULTA_CLAVE #2#sctrw999;
     |CIERRA #sctrw999;

     |DBASE aBase;
     aAlfa = aBase + "def/" + aFicRec;
     |CARGA_DEF aAlfa, fictodos@;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |CARGA_DEF aAlfa, ficnuevo@;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |CARGA_DEF aAlfa, ficmodif@;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |CARGA_DEF aAlfa, ficborra@;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |CARGA_DEF aAlfa, ficrecup@;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     aQueQuiero = "|NC";
     aAlfa      = #fictodos@#aQueQuiero#;
     nNumCampos = aAlfa;
     nNumCampos = nNumCampos - 1;

     |PATH_DAT #fictodos@#aPath#;  |NOME_DAT #fictodos@#"fictodos"#
     |PATH_DAT #ficnuevo@#aPath#;  |NOME_DAT #ficnuevo@#"ficnuevo"#
     |PATH_DAT #ficmodif@#aPath#;  |NOME_DAT #ficmodif@#"ficmodif"#
     |PATH_DAT #ficborra@#aPath#;  |NOME_DAT #ficborra@#"ficborra"#

     |ABRE #fictodos@;  |CIERRA #fictodos@;  |DELINDEX #fictodos@;
     |ABRE #ficnuevo@;  |CIERRA #ficnuevo@;  |DELINDEX #ficnuevo@;
     |ABRE #ficmodif@;  |CIERRA #ficmodif@;  |DELINDEX #ficmodif@;
     |ABRE #ficborra@;  |CIERRA #ficborra@;  |DELINDEX #ficborra@;

     |ABRE #fictodos@;
     |ABRE #ficnuevo@;
     |ABRE #ficmodif@;
     |ABRE #ficborra@;
     |ABRE #ficrecup@;

     |LEE 000#ficrecup@.u;
     nUltReg = #ficrecup@#0;

     aAbre = aPath + "incidencias.txt";
     |XABRE "W", aAbre, nHandle;

     |BUCLE Log;
     |XCIERRA nHandle;

     |MENAV "0000Registros nuevos";
     |CONSULTA_CLAVE #1#ficnuevo@;

     |MENAV "0000Registros modificados";
     |CONSULTA_CLAVE #1#ficmodif@;

     |MENAV "0000Registros borrados";
     |CONSULTA_CLAVE #1#ficborra@;

     |LEE 000#ficnuevo@.p;
     |PARA;  |SI FSalida = 0;  |HACIENDO;
          |HAZ ficnuevo;

          |LEE 000#ficnuevo@.s;
     |SIGUE;

     |LEE 000#ficmodif@.p;
     |PARA;  |SI FSalida = 0;  |HACIENDO;
          |HAZ ficmodif_Cheq;

          |LEE 000#ficmodif@.s;
     |SIGUE;

     |LEE 000#ficmodif@.p;
     |PARA;  |SI FSalida = 0;  |HACIENDO;
          |HAZ ficmodif_Gra;

          |LEE 000#ficmodif@.s;
     |SIGUE;

     |LEE 000#ficborra@.p;
     |PARA;  |SI FSalida = 0;  |HACIENDO;
          |HAZ ficborra_Cheq;

          |LEE 000#ficborra@.s;
     |SIGUE;

     |LEE 000#ficborra@.p;
     |PARA;  |SI FSalida = 0;  |HACIENDO;
          |HAZ ficborra_Bor;

          |LEE 000#ficborra@.s;
     |SIGUE;

     |CIERRA #ficnuevo@;  |DELINDEX #ficnuevo@;
     |CIERRA #ficmodif@;  |DELINDEX #ficmodif@;
     |CIERRA #ficborra@;  |DELINDEX #ficborra@;
     |CIERRA #fictodos@;  |DELINDEX #fictodos@;
     |CIERRA #ficrecup@;

     |DESCARGA_DEF #ficrecup@;
     |DESCARGA_DEF #ficborra@;
     |DESCARGA_DEF #ficmodif@;
     |DESCARGA_DEF #ficnuevo@;
     |DESCARGA_DEF #fictodos@;
|FPRC;
