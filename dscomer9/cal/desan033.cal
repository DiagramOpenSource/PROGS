|FICHEROS;
     desan033 #0;
     dsm00003 #3;
     agifa007 #7;
     agifa010 #10;
     agifa017 #17;
     agifa019 #19;
     agifa024 #24;
     agifa025 #25;
     desan027 #27,MANTE;
     dsm00043 #43;
     dsm00044 #44;
     agifa065 #65;
     agifa071 #71;
     agifa112 #112;
     agifa321 #321;
     agifa324 #324;
|FIN;

|VARIABLES;
     &eaAbono = "";
     &eaFacturable = "S";
     &eaMovimi = "";
     &enSwMenu = 0;
     &enForma = 0;
     &eaSerie = "";
     &enCodigo = 0;
     &enLinea = 0;
     &eaAlfa = "";
     nNum = 0;
     nError = 0;
     aAlfa = "";
     aSerie = "";
     importe = 0;
     cant = 0;
     &enImpCliPen = 0;
     &eaCliente = "";
     &efFecha = @;
     &eaTag = "";
     &enCoste = 0;
     &aDivisa = "";
     &aMoneda = "";
     aArt = "";
     aCli = "";
     aPrv = "";
|FIN;

|PROCESO Imprime;
     |IMPRE 0;
     |SI FSalida = 0;
          |INFOR "desan027";
          |SI FSalida = 0;
               |PRINT;
               |PIEINF;
               |FININF;
          |FINSI;
          |FINIMP;
     |FINSI;
|FPRC;

|PROCESO actpf;
     |SI #10#15 ] 0;
          importe = 1;
     |SINO;
          importe = -1;
          #10#15 = -#10#15;
     |FINSI;
     cant = #10#15 < #10#5;
     cant = cant < #10#32;
     cant = cant < #10#7;
     cant = cant * importe;
     #10#15 = #10#15 * importe;
     |ABRE #24;
     #24#0 = #10#3;
     |LEE 121#24=;
     |SI FSalida = 0;
        |SI #10#43 = AN;
           #24#50 = #24#50 + cant;    || es albaran
        |SINO;
           #24#50 = #24#50 - cant;    || es abono
        |FINSI;
        |GRABA 020#24a;
        |LIBERA #24;
     |FINSI;
     |CIERRA #24;

     |SI #10#43 = "N";
          enImpCliPen = cant;
     |SINO;
          enImpCliPen =  -cant;
     |FINSI;
     eaCliente = #10#3;
     efFecha = #10#1;
     |HAZ AcumulaCli;

     eaTag = "AV";  |HAZ Riesgos;
|FPRC;

|PROCESO LinAlb;
     #71#29 = #27#25;
     #71#0 = #27#26;
     #71#1 = #27#27;
     |LEE 020#71=;  || Lee el alb. de la garantia

     #71#29 = #10#52;
     #71#0 = #10#0;
     #71#1 = 1;
     |HAZ TotalLin71;
     |HAZ CalCabAgifa010;
     |HAZ AcumulaAV;
     #71#14 = enCoste;
     |GRABA 020#71n;
|FPRC;

|PROCESO Abono;
     |ABRE #321; |LEE 000#321p; |CIERRA #321;
     |ABRE #7;
     #7#26 = aSerie;
     |LEE 000#7=;
     |CIERRA #7;
     |ABRE #24;
     #24#0 = #27#2;
     |LEE 000#24=;
     |CIERRA #24;
     |ABRE #3;
     #3#0 = #24#0;
     #3#1 = 1;
     |LEE 000#3=;
     |CIERRA #3;
     |ABRE #19;
     #19#0 = #27#5;
     |LEE 000#19=;
     |CIERRA #19;
     |ABRE #25;
     #25#0 = #24#25;
     |LEE 000#25=;
     |CIERRA #25;

     |ABRE #10;
     |ABRE #71;
     #10#52 = aSerie;
     enSwMenu = 1;
     |HAZ ContaAV7;
     |GRABA 020#10b;
     |SI FSalida = 0;
          #10#3 = #24#0;
          #10#4 = #24#2;
          #10#5 = #24#37;
          #10#6 = #25#0;
          #10#7 = #24#38;
          #10#8 = #24#20;
          #10#29 = #3#10;
          #10#30 = #3#2;
          #10#31 = #3#3;
          #10#33 = #3#7;
          #10#62 = #3#6;
          #10#84 = #3#1;
          |SI #3#14 = "  ";
              #10#6 = #24#25;
          |SINO;
              #10#6 = #3#14;
          |FINSI;
          #10#34 = #25#7;
          #10#35 = #24#4;
          #10#36 = #24#47;
          #10#40 = #24#41;
          #10#43 = "S";
          #10#50 = #24#40;
          #10#51 = #7#28;
          #10#63 = #24#15;
          #10#68 = #24#192;
          |ABRE #324;
          #324#0 = #10#68;
          |LEE 000#324=;
          #10#69 = #324#2;
          #10#70 = #24#193;
          #10#73 = #321#9;

          #324#0 = #10#73;
          |LEE 000#324=;
          #10#74 = #324#2;
          #10#84 = #3#1;
          #10#91 = #24#206;
          |CIERRA #324;
          aDivisa = #10#68;
          aMoneda = #10#70;
          |HAZ Divisas010;
          |HAZ LimCabAgifa010;

          |HAZ LinAlb;
          |GRABA 020#10a;
          |HAZ actpf;
     |FINSI;
     |CIERRA #71;
     |CIERRA #10;
|FPRC;

|PROCESO EntraAlma;
     |ABRE #43;
     |ABRE #44;
     #43#4 = aSerie;
     #43#0 = 999999;
     |LEE 000#43];
     |SI FSalida = 0;
          |LEE 000#43a;
     |SINO;
          |LEE 000#43u;
     |FINSI;
     |SI FSalida = 0; |Y #43#4 = aSerie;
          nNum = #43#0 + 1;
     |SINO;
          nNum = 1;
     |FINSI;
     |DDEFECTO #43;
     #43#0 = nNum;
     #43#2 = "GARANTIA CLIENTE";
     #43#4 = aSerie;
     |GRABA 020#43b;
     |DDEFECTO #44;
     #44#0 = #27#5;
     #44#1 = 1;
     #44#2 = #27#6;

     |ABRE #19;
     #19#0 = #44#0;
     |LEE 000#19=;
     |CIERRA #19;
     |ABRE #17;
     #17#0 = #44#0;
     #17#1 = #44#1;
     |LEE 000#17=;
     |CIERRA #17;

     #44#3 = #17#39;
     #44#4 = 1;
     #44#5 = #43#1;
     #44#6 = #19#6;
     #44#7 = "E";
     #44#8 = 1;
     #44#18 = #43#0;
     #44#19 = #17#5;
     #44#20 = #19#104;
     #44#21 = #43#4;
     #44#26 = #19#133;
     |GRABA 020#44n;
     eaMovimi = "ES_SS";
     eaSerie = #44#21;
     enCodigo = #44#18;
     enLinea = #44#8;
     enForma = 1;
     |SUB_EJECUTA_NP "dsz90001";
|FPRC;

|PROCESO BuscaPrv;
     |ABRE #65;
     |DDEFECTO #65;
     #65#0 = #19#0;
     #65#1 = "31.12.9999";
     |LEE 000#65];
     |SI FSalida = 0;
          |LEE 000#65a;
     |SINO;
          |LEE 000#65u;
     |FINSI;
     |PARA; |SI FSalida = 0; |Y #65#0 = #19#0; |HACIENDO;
          |SI #65#2 = "AC"; |Y #65#10 ! "999999"; |Y #65#10 ! "      ";
                aPrv = #65#10;
                |SAL;
          |FINSI;
          |LEE 000#65a;
     |SIGUE;
     |CIERRA #65;
|FPRC;

|PROCESO CreaGarantia;
     |ABRE #27;
     |LEE 000#27u;
     |SI FSalida = 0;
          nNum = #27#0 + 1;
     |SINO;
          nNum = 1;
     |FINSI;
     |DDEFECTO #27;
     #27#0 = nNum;
     |GRABA 020#27b;
     nError = 0;

     |C_M #27#0, 1, "N";
     |C_M #27#1, 1, "N";
     |C_M #27#2, 1, "N";
     |C_M #27#3, 1, "N";
     |C_M #27#4, 1, "N";
     |C_M #27#6, 1, "N";
     |C_M #27#6, 1, "N";
     |C_M #27#7, 1, "N";
     |C_M #27#8, 1, "N";
     |C_M #27#9, 1, "N";
     |C_M #27#10, 1, "N";
     |C_M #27#28, 1, "N";
     |C_M #27#29, 1, "N";

     || Cli + Nom  + Pobla + Arti + Des  + Fec
     aAlfa = eaAlfa % 106; #27#2 = aAlfa;
     aAlfa = eaAlfa % 760; #27#3 = aAlfa;
     aAlfa = eaAlfa % 6728; #27#4 = aAlfa;
     aAlfa = eaAlfa % 9520; #27#5 = aAlfa;
     aAlfa = eaAlfa % 11550; #27#6 = aAlfa;
     aAlfa = eaAlfa % 16510; #27#7 = aAlfa;
     |ABRE #19;
     #19#0 = #27#5;
     |LEE 020#19=;
     |CIERRA #19;

     aPrv = #19#213;
     |SI aPrv = "999999";
          |HAZ BuscaPrv;
     |FINSI;

     |ABRE #112;
     #112#0 = aPrv;
     |LEE 000#112=;
     |CIERRA #112;

     #27#8 = #112#0;
     #27#9 = #112#1;
     #27#10 = #27#1 + #19#114;
     #27#22 = #0#0;
     #27#23 = #0#1;
     #27#24 = #0#2;
     |PINPA #0#27;
     |PINDA #0#27;
     |ENDATOS #2#27;
     |SI FSalida = 0;
          |GRABA 020#27a;
          |CONFI "0000NImprimir garantia:";
          |SI FSalida = 0; |HAZ Imprime; |FINSI;
          nError = 0;
     |SINO;
          |BORRA 020#27c;
          nError = 1;
     |FINSI;

     |CIERRA #27;
|FPRC;

|PROCESO HistoVenta;
     nError = 0;
     |ERROR10;
|FPRC;

|DEFBUCLE HistoVenta;
     #65#1;
     10;
     aArt, "01.01.0000", "AV", 000000, 00000, "      ", aCli;
     aArt, "31.12.9999", "AV", 999999, 99999, "zzzzzz", aCli;
     ;
     NULL, HistoVenta;
|FIN;

|PROGRAMA;
     |ABRE #0;
     #0#0 = eaSerie;
     #0#1 = enCodigo;
     #0#2 = enLinea;
     |SI PARAMETRO = "BAJA";
          |BORRA 000#0c;
     |SINO;
          |LEE 101#0=;
          |SI FSalida ! 0; |GRABA 020#0b; |FINSI;
          aAlfa = #0#3;
          |C_P #0#3, 1, 1342;
          |ENCAMPO #3#0;
          |SI aAlfa ! #0#3; |Y #0#3 = "S";
               nError = 1;
               || Cli + Nom  + Pobla + Arti + Des  + Fec
               aAlfa = eaAlfa % 106; aCli = aAlfa;
               aAlfa = eaAlfa % 9520; aArt = aAlfa;
               |BUCLE HistoVenta;
               |SI nError = 1;
                    |MENAV "0000No hay ventas para el cliente/articulo";
               |SINO;
                    |SI eaFacturable = "S";
                         |CONFI "0000S¿Dejar albaran como facturable?:";
                    |SINO;
                         |CONFI "0000N¿Dejar albaran como facturable?:";
                    |FINSI;
                    |SI FSalida = 0;
                         eaFacturable = "S";
                    |SINO;
                         eaFacturable = "N";
                    |FINSI;
                    |HAZ CreaGarantia;
               |FINSI;
               |SI nError = 1;
                    #0#3 = "N";
               |SINO;
                    aSerie = "GAR  ";
                    |ABRE #27;
                    |LEE 121#27=;
                    |CONFI "0000NCrear abono al cliente S/N:";
                    |SI FSalida = 0;
                         #27#30 = "S";
                         eaAbono = "S";
                     |||    |HAZ Abono;
                    |SINO;
                         #27#30 = "N";
                         |HAZ EntraAlma;
                    |FINSI;
                    |GRABA 020#27a;
                    |CIERRA #27;
               |FINSI;
          |FINSI;
          |GRABA 020#0a;
     |FINSI;
     |CIERRA #0;
|FPRO;
