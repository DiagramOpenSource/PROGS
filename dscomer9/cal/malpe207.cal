|FICHEROS;
     malpe207 #0;
     malpe208 #1; || Inventario Especial
     agifa065 #65;
|FIN;

|VARIABLES;
     fFecha = @;
     aAlfa = "";
     nEjeAnt = 0;
     fIni = @;
     fFin = @;
     fIniEje = @;
|FIN;

|PROCESO CalcuFin;
     #1#6 = #1#3 + #1#4 + #1#5;
     |GRABA 020#1a;
     |LIBERA #1;
|FPRC;

|DEFBUCLE CalcuFin;
     #1#1;
     ;
     "                    ", #0#0;
     "zzzzzzzzzzzzzzzzzzzz", #0#0;
     be;
     NULL, CalcuFin;
|FIN;

|PROCESO Historico;
     |SI #65#2 = "ME"; |O #65#2 = "MS";
          |ACABA;
     |FINSI;
     |DDEFECTO #1;
     #1#0 = #65#0;
     #1#1 = #0#0;
     |LEE 101#1=;
     |SI FSalida ! 0; |GRABA 020#1b; |FINSI;

     |SI #65#1 < fIniEje;
          #1#3 = #1#3 + #65#6; || Inicial
          |SI #1#2 < #65#1; #1#2 = #65#1; |FINSI;
     |SINO;
          |SI #65#2 = "BV"; |O #65#2 = "AC"; |O #65#2 = "ES";
                    |O #65#2 = "FB"; |O #65#2 = "EV";
               #1#4 = #1#4 + #65#6;     || Entradas
               |SI #1#2 < #65#1; #1#2 = #65#1; |FINSI;
          |FINSI;
          |SI #65#2 = "AV"; |O #65#2 = "BC"; |O #65#2 = "FC";
                    |O #65#2 = "SS"; |O #65#2 = "SF"; |O #65#2 = "TI";
               #1#5 = #1#5 + #65#6;     || Salidas
               |SI #1#2 < #65#1; #1#2 = #65#1; |FINSI;
          |FINSI;
     |FINSI;
     |GRABA 020#1a;
     |LIBERA #1;
|FPRC;

|DEFBUCLE Historico;
     #65#1;
     ;
     "                    ", fIni, "  ", 000000, 00000, "     ";
     "zzzzzzzzzzzzzzzzzzzz", fFin, "zz", 999999, 99999, "zzzzz";
     ;
     NULL, Historico;
|FIN;

|PROCESO EjeAnterior;
     #1#1 = #0#0;
     #1#3 = #1#6;
     #1#4 = 0;
     #1#5 = 0;
     #1#6 = 0;
     |GRABA 020#1n;
|FPRC;

|DEFBUCLE EjeAnterior;
     #1#1;
     ;
     "                    ", nEjeAnt;
     "zzzzzzzzzzzzzzzzzzzz", nEjeAnt;
     ;
     NULL, EjeAnterior;
|FIN;

|PROCESO BorraEje;
     |BORRA 020#1a;
|FPRC;

|DEFBUCLE BorraEje;
     #1#1;
     ;
     "                    ", #0#0;
     "zzzzzzzzzzzzzzzzzzzz", #0#0;
     be;
     NULL, BorraEje;
|FIN;

|PROCESO Tipo3; |TIPO 3;
     |BUCLE BorraEje;
     aAlfa = "01.01." + #0#0;
     fIniEje = aAlfa;
     |SI #0#1 = "F";
          nEjeAnt = #0#0 - 1;
          |BUCLE EjeAnterior;
          aAlfa = "01.01." + #0#0; fIni = aAlfa;
          aAlfa = "31.12." + #0#0; fFin = aAlfa;
          |ABRE #1;
          |BUCLE Historico;
          |CIERRA #1;
     |SINO;
          fIni = "01.01.0000";
          aAlfa = "31.12." + #0#0; fFin = aAlfa;
          |ABRE #1;
          |BUCLE Historico;
          |CIERRA #1;
     |FINSI;
     |BUCLE CalcuFin;
|FPRC;

|PROCESO CheqEje; |TIPO 0;
     |ABRE #1;
     |SELECT #2#1;
     #1#1 = #0#0;
     #1#0 = "";
     |LEE 000#1];
     |SI FSalida = 0; |Y #1#1 = #0#0;
          |MENAV "0000Ya existen registros de este ejercicio";
     |FINSI;
     |CIERRA #1;
|FPRC;

|PROGRAMA;
     |CLS;
     |ABRE #0;
     |LEE 101#0p;
     |SI FSalida ! 0; |GRABA 020#0b; |FINSI;
     fFecha = ~;
     aAlfa = fFecha % -104;
     #0#0 = aAlfa;
     #0#1 = "F";
     |PINPA #0#0;
     |PINDA #0#0;
     |ENDATOS #1#0;
     |CIERRA #0;
|FPRO;
