|FICHEROS;
     agifa048 #0;   || escandallo cabs.
     agifa049 #1;   || escandallo lins.
     agifa019 #2,MANTE;   || articulos
     agifa017 #3;   || almacenes
     agifa142 #142;
     dsm00241 #241;
     dsm00238 #238;
     agifa048@ #480;
     referen@;
|FIN;

|VARIABLES;
     aArtiAnt = "";
     nLinea = 0;
     nCampo = 0;
     nImpo = 0;
     nImpo2 = 0;
     nUni = 0;
     nAlma = 0;
     nModo = 0;
     nUnidad = 0;
     &eaMate = "";
     &eaEscan = "";
     &enTecla = 0;
     &enTipo = 0;
     nForma = 0;
     aTipo = "";
     nNume1        = 0;
     nNume2        = 0;
     nNume         = 0;

     &nDeci_EsCab = 0;
     &nDeci_EsLin = 0;

     nMerma         = 0;
     swdes          = 0;
     &nModcab       = 0;
     &nDec          = 0;
     vacio          = " " * 20;
     impcompone     = 0 ;
     fiac           = " ";
     paac           =  0 ;
     &aLargo        = "N";
     &nUniLargo     = 0;
     &nLargo        = 0;
     &nAncho        = 0;
     &nGrosor       = 0;
     &nResultado    = 0;
     &nTipoLargo    = 0;
     &nExitAlterna  = 0;
     unifab         = 0;
     modo           = 0;
     resp           = 0;
     aAlfa          = "";
|FIN;

|INCLUYE i_varart;

|PROCESO DefeUniPre;
     |HAZ LeeConverUni;
     |HAZ EsTecatel;
     |SI #142#9 = "S"; |Y FSalida = 0;
          |SI #241#6 = "D";
               nNume = #3#69 * #241#5;
               #1#15 = nNume;
          |SINO;
               nNume = #3#69 / #241#5;
               #1#15 = nNume;
          |FINSI;
     |SINO;
          |SI Campo = 9; |ACABA; |FINSI;
          |SI #241#6 = "D";
               nNume = #2#9 * #241#5;
               #1#15 = nNume;
          |SINO;
               nNume = #2#9 / #241#5;
               #1#15 = nNume;
          |FINSI;
     |FINSI;
     |PINTA #1#15;
|FPRC;


|PROCESO tipo_1; |TIPO 1;
     swdes = 0;
|FPRC;

|PROCESO Tipo2_049; |TIPO 2;
     modo = (FEntrada / 100) ! 0;
     nForma = 0 +. 1;

     |SI nForma = -1; aArtiAnt = #1#2; |FINSI;

     |HAZ totliesc;

     |SI modo = 3;
          |HAZ EsMecanogra48;
          |SI FSalida = 0;
               eaArticulo = #1#0;
               enLinea = #1#1;
               |SUB_EJECUTA_NP "mecam004;BAJA";
          |FINSI;
          |HAZ EsCoimasa48;
          |SI FSalida = 0;
               eaArticulo = #1#0;
               enLinea = #1#1;
               |SUB_EJECUTA_NP "coima010;BAJA";
          |FINSI;
          |HAZ EsOrts;
          |SI FSalida = 0;
               eaArticulo = #1#0;
               enLinea = #1#1;
               ||tambien se borran:ortsm027,ortsm028,ortsm037
               |SUB_EJECUTA_NP "ortsm020;BAJA";
          |FINSI;
     |FINSI;

     |HAZ EsRodacal48;
     |SI FSalida = 0; |Y #1#10 = 1;
          eaEscan = #1#0;
          eaMate = #1#2;
          enLinea = #1#1;
          enUnid1 = #1#12;
          enUnid2 = #1#11;
          enForma = nForma;
          enModo = modo;
          |HAZ RodLinEscan
          #1#12 = enUnid1; |PINTA #1#12;
          aAlfa = ("          " + enUnid2) % -109;||Formatea, es alfa!!!!
          #1#11 = aAlfa;
     |FINSI;

     |HAZ ConverUni;

     impcompone = #1#6 / #0#6;
     |SI #1#10 = 1; #0#8 = #0#8 +. impcompone;    |FINSI;   ||Componentes
     |SI #1#10 = 2; #0#2 = #0#2 +. impcompone;    |FINSI;   ||Mano Obra
     |SI #1#10 = 3; #0#3 = #0#3 +. impcompone;    |FINSI;   ||Maquinaria
     |SI #1#10 = 4; #0#4 = #0#4 +. impcompone;    |FINSI;   ||Otros


     |SI #1#10 = 1;
         |SI #0#17 = "S";
             |SI #0#26 = "D";
                 #0#21 = #0#21 +. (#1#6 > #0#16);
             |SINO;
                 #0#21 = #0#21 +. (#1#6 / ((100 - #0#16) / 100));
             |FINSI;
         |SINO;
             #0#21 = #0#21 +. #1#6;
         |FINSI;
     |FINSI;

     |SI #1#10 = 2;
         |SI #0#19 = "S";
             |SI #0#28 = "D";
                 #0#21 = #0#21 +. (#1#6 > #0#24);
             |SINO;
                 #0#21 = #0#21 +. (#1#6 / ((100 - #0#24) / 100));
             |FINSI;
         |SINO;
             #0#21 = #0#21 +. #1#6;
         |FINSI;
     |FINSI;

     |SI #1#10 = 3;
         |SI #0#18 = "S";
             |SI #0#27 = "D";
                 #0#21 = #0#21 +. (#1#6 > #0#23);
             |SINO;
                 #0#21 = #0#21 +. (#1#6 / ((100 - #0#23) / 100));
             |FINSI;
         |SINO;
             #0#21 = #0#21 +. #1#6;
         |FINSI;
     |FINSI;

     |SI #1#10 = 4;
         |SI #0#20 = "S";
             |SI #0#29 = "D";
                 #0#21 = #0#21 +. (#1#6 > #0#25);
             |SINO;
                 #0#21 = #0#21 +. (#1#6 / ((100 - #0#25) / 100));
             |FINSI;
         |SINO;
             #0#21 = #0#21 +. #1#6;
         |FINSI;
     |FINSI;

     #0#5 = #0#2 + #0#3 + #0#4;         || total otros costes
     #0#11 = (#0#5 + #0#8) > #0#7;      || total serie
     #0#11 = #0#11 > #0#34;
     #0#9 = #0#11 / #0#12;              || total unitario
     |HAZ CosteModulo;

     |PINTA #0#2;
     |PINTA #0#3;
     |PINTA #0#4;
     |PINTA #0#5;
     |PINTA #0#8;
     |PINTA #0#9;
     |PINTA #0#11;
     |PINTA #0#22;
     |PINTA #0#21;

     |HAZ EsOrts;
     |SI FSalida = 0; |Y nForma = 1;
          |SI aArtiAnt ! #1#2;
               enUnidades = #1#12;
               aAlfa = "dsz99905;ESCAN" + aArtiAnt + #1#0 + #1#2 + #1#1;
               |SUB_EJECUTA_NP aAlfa;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO escartic; |TIPO 0;
     nModo = (FEntrada / 100) ! 0;
     |SI #1#2 = #1#0;
          |MENAV "0000No se puede descomponer el Articulo en Escandallo ...";
          |ERROR; |ACABA;
     |FINSI;
     |SI #142#12 = "X";
          aTipo = "";
          |DDEF aAlfa;
          aAlfa = aAlfa + "agifa048";
          |CARGA_DEF aAlfa, agifa048@;
          |SI FSalida = 0;
               |ABRE #480;
               #480#0 = #1#2;
               |LEE 000#480=;
               |SI FSalida = 0;
                    aTipo = #480#33;
               |FINSI;
               |CIERRA #480;
               |DESCARGA_DEF #agifa048@;
          |FINSI;
          |SI aTipo = "K";
               |MENAV "0000Error, el componente es un KIT";
               |ERROR; |ACABA;
          |FINSI;
     |FINSI;

     |SI nModo = 1; |O #1Campo ! Contenido;
          |ABRE #2;
          #2#0 = #1#2;
          |LEE 020#2=;
          |CIERRA #2;

          |HAZ Es48X;
          |SI FSalida = 0;
               #1#3 = #2#1; |PINTA #1#3;
               #1#14 = #2#207;
          |FINSI;

          |HAZ DefeUniPre;
          eaArticulo = #1#2;
          eaCliente = "";
          |||HAZ DefectoMediV;
          |SUB_EJECUTA_NP "dsz99919;D";
          #1#18 = enMedida1;
          #1#19 = enMedida2;
          #1#20 = enMedida3;
     |FINSI;
     |HAZ PintaUni49;
     |HAZ EscanLin13;
|FPRC;

|PROCESO AlmaEsca7; |TIPO 7;
     nAlma = #1#9;
|FPRC;

|PROCESO AlmaEsca0; |TIPO 0;
     |SI #1#9 = 0;
          |AVISO; |ERROR; |ACABA;
     |FINSI;

     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 1; |O nAlma ! #1#9;
          |HAZ DefeUniPre;
     |FINSI;
|FPRC;

|PROCESO nomodif6; |TIPO 0;
|SI #0#2 = vacio;
      |CAMPO_MODIFICA #0#3,  1, "N";
      |CAMPO_MODIFICA #0#5,  1, "N";
      |CAMPO_MODIFICA #0#6,  1, "N";
      |CAMPO_MODIFICA #0#8,  1, "N";
      |CAMPO_MODIFICA #0#11,  1, "N";
      |CAMPO_MODIFICA #0#9,  1, "N";
|SINO;
      |CAMPO_MODIFICA #0#3,  1, "S";
      |CAMPO_MODIFICA #0#5,  1, "S";
      |CAMPO_MODIFICA #0#6,  1, "S";
      |CAMPO_MODIFICA #0#8,  1, "S";
      |CAMPO_MODIFICA #0#11,  1, "S";
      |CAMPO_MODIFICA #0#9,  1, "S";
|FINSI;
|FPRC;

|PROCESO MinUni;
     #241#0 = #2#5;
     #241#1 = 1;
|FPRC;

|PROCESO MaxUni;
     #241#0 = #2#5;
     #241#1 = 999;
|FPRC;

|PROCESO UniF11; |TIPO 66;
     |ABRE #2;
     #2#0 = #1#2;
     |LEE 000#2=;
     |CIERRA #2;

     |ABRE #241;
     |CONSULTA_F_CLAVE #1#241, MinUni, MaxUni;
     |SI FSalida = 0;
          #1#14 = #241#2;
          |HAZ PintaUni49;
          |HAZ LeeConverUni;
          |SI #241#6 = "D";
               nNume = #2#9 * #241#5;
               #1#15 = nNume;
          |SINO;
               nNume = #2#9 / #241#5;
               #1#15 = nNume;
          |FINSI;
          |PINTA #1#15;
     |FINSI;
     |CIERRA #241;
     |ERROR;
|FPRC;

|PROCESO PintaUni49; |TIPO 7;
     |SI #142#163 = "S";
          |HAZ LeeConverUni;
          |SI #2#204 = "S";
               |PINTA 2307, #1#14;
               |PINTA 2313, #241#4;
          |SINO;
               aAlfa = " " * 21;
               |PINTA 2307, aAlfa;
          |FINSI;
     |FINSI;
     |HAZ EsOrts;
     |SI FSalida = 0;
          |HAZ PinOrts;
     |FINSI;
|FPRC;

|PROCESO Llena049; |TIPO 6;
     enAlta = 1;
     eaArticulo = #1Campo;
     |HAZ LlenaArticulo;
     |SI FSalida = 0;
          #1Campo = eaArticulo;
          |PINTA #1Campo;

          |HAZ CreaArti;
     |SINO;
          |ERROR; |ACABA;
     |FINSI;
|FPRC;

|PROCESO EntraLin7; |TIPO 7;
     nModo = (FEntrada / 100) ! 0;

     |HAZ EsRodacal48;
     |SI FSalida = 0;
          ||MENSA "0000Unidades Componentes: <F5> Modificacion  <F6> Alta ";
          |MENSA "0000<F5> Modificacion Unidades Componentes";
     |FINSI;
     |HAZ EsOrts;
     |SI FSalida = 0; |Y nModo = 4;
          |MENSA "0000<ENTER> Muestra todos los campos de la linea";
     |FINSI;

     |HAZ Es48X;
     |SI FSalida = 0; |Y nModo = 1;
          #1#0 = #0#0;
          #1#1 = 999;
          |LEE 000#1];
          |SI FSalida = 0;
               |LEE 000#1a;
          |SINO;
               |LEE 000#1u;
          |FINSI;
          |SI FSalida = 0; |Y #1#0 = #0#0;
               nLinea = #1#1 + 1;
          |SINO;
               nLinea = 1;
          |FINSI;
          |DDEFECTO #1;
          #1#0 = #0#0;
          #1#1 = nLinea;
     |FINSI;
|FPRC;

|PROCESO SalLin; |TIPO 0;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 1;
          #1#13 = #0#7; |PINTA #1#13;
     |FINSI;

     |SI nModo = 4;
          |HAZ EsOrts;
          |SI FSalida = 0;
               enModo = 4;
               |HAZ EntraLinOrts;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Escan11; |TIPO 11;
     |SI FSalida = 13;
          enTecla = FSalida;
          |HAZ EsRodacal48;
          |SI FSalida = 0;
               eaEscan = #1#0;
               eaMate = #1#2;
               enUnid2 = #1#11;
               |HAZ RodModEscan;
               |ERROR; |ACABA;
          |FINSI;
          FSalida = enTecla;
     |FINSI;
     |SI FSalida = 13;
          ||SI #0#33 ! "K"; |ERROR; |ACABA; |FINSI;
          eaArticulo = #1#2;
          enUnidad1 = #1#12;
          enUnidad2 = #1#17;
          enMedida1 = #1#18;
          enMedida2 = #1#19;
          enMedida3 = #1#20;

          ||HAZ VerDobleUniV;
          eaProgVta = "agifa048";
          |SUB_EJECUTA_NP "dsz99919;V";
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO EntraUni49; |TIPO 7;
     ||SI #0#33 ! "K"; |ACABA; |FINSI;

     enAlmacen = #1#9;
     eaCliente = "";
     efFecha = "01.01.000";
 ||***
     eaArticulo = #1#2;
     enUnidad1 = #1#12;
     enUnidad2 = #1#17;
     enMedida1 = #1#18;
     enMedida2 = #1#19;
     enMedida3 = #1#20;

     ||HAZ DobleUniV;
     eaProgVta = "agifa048";
     enModoVta = (FEntrada / 100) ! 0;
     |SUB_EJECUTA_NP "dsz99919";

     |C_M #1#12, 1, "S";
     |SI eaSw2Stock = "S";
          |C_M #1#12, 1, "N";
          #1#12 = enUnidad1; |PINTA #1#12;
          #1#17 = enUnidad2;
          #1#18 = enMedida1;
          #1#19 = enMedida2;
          #1#20 = enMedida3;
     |FINSI;
|FPRC;

|PROCESO AlmaCom6; |TIPO 6;
     |SI #1#9 ! 0;
          eaArticulo = #1#2;
          enAlmacen = #1#9;
          |HAZ CreaArtiAlma;
     |FINSI;
|FPRC;

|PROCESO EntraArti; |TIPO 7;
     |HAZ EsOrts;
     |SI FSalida = 0;
          enModo = (FEntrada / 100) ! 0;
          |HAZ EntraLinOrts;
          |SI FSalida = 0;
               |PTEC 806; ||Escape
          |SINO;
               |PTEC 807; ||Ctrl-C
          |FINSI;
     |FINSI;
|FPRC;
