|FICHEROS;
     malpe007 #0;   || Este
     malpt007 #1,MANTE;   || Lineas de Este (Orden de Carga L)
     malpe004 #2;   || Orden Carga (C)
||     malpe136 #3;   || Comisiones
     malpe090 #4;   || Albaranes (C)
     malpe091 #5;   ||           (L)
     agifa024 #6;   || Clientes
     malpe001 #7;   || Obras;
     agifa025 #8;   || Comisionistas;
     agifa019 #9;   || Articulos
     malpe006 #11;  || Trasnportista
     malpe002 #12;  || Vehiculos
     agifa017 #13;  || Stock Almacenes
     dsm00024 #14;  || Stock Lotes
     agifa065 #15;  || Historico
     malpe104 #16;  || Fechas de Servicios
     malpe087 #17;  || Pedidos (C)
     malpe088 #18;  ||         (L)
     malp0004 #19,MANTE;  || Pantalla Entlineal Carga
     agifa027 #27;
     agifa142 #41;  || Parametros Generales
     agifa007 #50; || fichero de series para ver si es de exportacion
     malpe081 #81;
     malpe083 #83;
     malpe085 #85;
     malpe119 #119;
     malpe137 #137;
     malpe203 #203;
     malpe209 #209;
     malpe210 #210;
     malpe211 #211;
|FIN;

|INCLUYE i_varart;

|VARIABLES;
     nPalets = 0;
     fFecha = @;
     nEncuentra = 0;
     nPortes = 0;
     nPesoMortero = 0;
     nRemanente = 0;
     nError = 0;
     aSerCheqAlb = "";
     nNumCheqAlb = 0;
     nNum = 0;
     aTecla = "";
     aFin = "";
     &eaTieneComi = "";
     &eaTag = "";
     &enComision1 = 0;
     &enComision2 = 0;
     ||&eaArticulo = "";
     &eaTipoCli = "";
     ||&efFecha = @;
     &eaRemanente = "";
     &enDto = 0;
     &eaTipoComi = "";

     nComi1 = 0;
     nComi2 = 0;
     menor = 0;
     pc = 0;
     pcr = 0;
     nPorce1 = 0;
     nPorce2 = 0;
     nImpo = 0;
     nForma = 0;

     nUni = 0;
     nLin = 0;
     aSerPed = "";
     nNumPed = 0;
     axLote = "";
     nServir = 0;
     nPend = 0;
     nUnidades = 0;
     aLote = "";
     aLoteRese = "";
     &enUniPedi = 0;
     &aMoneda = "";
     &aDivisa = "";
     &enSwMenu = 0;
     nConta = 0;
     aAlfa = "";
     nHay  = 0;
     &EURODB   = 0;
     base_com = 0;
     informe   = "";
     infdefe   = "malpe007";
     fichero   = "";
     i         = 0;
     &n_dec    = 0;
     &n_pre    = 0;
     lin       = 0;
     imneto    = 0;
     margen    = 0;
     mes       = 0;
     ymes      = "";
     xmes      = 0;
     seri      = "";
     nume      = "";
     &ser_alb  = "";
     &num_alb  = 0;
     uni       = 0;
     pend      = 0;
     xuni      = 0;
     xtipo     = 0;
     impo      = 0;
     asto      = 0;
     cant      = 0;
     correcto  = "N";
     cod_pale = "";
|FIN;

|PROCESO ModiStockLote;
     |DDEFECTO #14;
     #14#0 = #18#2;
     #14#1 = #18#3;
     #14#2 = axLote;
     |LEE 101#14=;
     |SI FSalida ! 0; |GRABA 020#14b; |FINSI;
     #14#16 = #14#16 + nServir;
     |GRABA 020#14a;
|FPRC;

|PROCESO CreaPendiente;
     |SI #16#5 ! #16#7; |Y #16#7 ! 0;
          nUni = #16#5 - #16#7;
          #16#5 = #16#7;
          |GRABA 020#16a;
          |SALVA_FICHA #16;
          #16#3 = nLin;
          #16#5 = nUni;
          #16#7 = 0;
          #16#10 = #1#31;
          |GRABA 020#16n;
          nLin = nLin + 1;
          |REPON_FICHA #16;
     |FINSI;
|FPRC;

|DEFBUCLE CreaPendiente;
     #16#1;
     ;
     #1#32, #1#33, #1#34, #1#35;
     #1#32, #1#33, #1#34, 999;
     be;
     NULL, CreaPendiente;
|FIN;

|PROCESO CreaLinPen
     |ABRE #16;
     #16#0 = #1#32;
     #16#1 = #1#33;
     #16#2 = #1#34;
     #16#3 = 999;
     |LEE 000#16];
     |SI FSalida = 0;
          |LEE 000#16a;
     |SINO;
          |LEE 000#16u;
     |FINSI;
     |SI FSalida = 0; |Y #16#0 = #1#32; |Y #16#1 = #1#33; |Y #16#2 = #1#34;
          nLin = #16#3 + 1;
     |SINO;
          nLin = 1;
     |FINSI;
     |CIERRA #16;

     |BUCLE CreaPendiente;
|FPRC;

|PROCESO ActLote;
     |ABRE #14;
     |ABRE #16;
     #16#0 = #1#32;
     #16#1 = #1#33;
     #16#2 = #1#34;
     #16#3 = #1#35;
     |LEE 101#16=;
     |SI FSalida = 0;
          nUnidades = #1#5;
          nPend = 0;
          |PARA; |SI FSalida = 0; |Y #16#0 = #1#32; |Y #16#1 = #1#33; |Y #16#2 = #1#34;
                    |Y nPend < nUnidades; |HACIENDO;
               nUnidades = nUnidades - #16#5;
               nPend = #16#5 - #16#7;

               axLote = #16#10;
               nServir = -nPend;
               |HAZ ModiStockLote;

               axLote = #1#31;
               nServir = nPend;
               |HAZ ModiStockLote;

               #16#10 = #1#31;
               |GRABA 020#16a;
               |LIBERA #16;
               |LEE 101#16s;
          |SIGUE;
     |FINSI;
     |CIERRA #16;
     |CIERRA #14;
|FPRC;

|PROCESO mirar_de;
     |ABRE #41;
     #41#0 = "A";
     |LEE 001#41=;
     |SI FSalida ! 0; |DDEFECTO #41; |FINSI;
     n_dec = #41#8;
     n_pre = #41#9;
     |CIERRA #41;
|FPRC;

|PROCESO infcarg;
     #19#9  = #0#0;
     #19#15 = "N";
     #19#3 = "      ";
|FPRC;

|PROCESO supcarg;
     #19#9  = #0#0;
     #19#15 = "N";
     #19#3  = "zzzzzz";
|FPRC;

|PROCESO infer;
     #1#28 = #0#1;
     #1#0 = #0#2;
     #1#1 = 001;
|FPRC;

|PROCESO super;
     #1#28= #0#1;
     #1#0 = #0#2;
     #1#1 = 999;
|FPRC;

|PROCESO CompruCarga;
     aSerPed = #1#32;
     nNumPed = #1#33;
     correcto = #1#36;
     |SI correcto = "N";
          |ERROR10;
     |FINSI;
|FPRC;

|DEFBUCLE CompruCarga;
     #1#1;
     ;
     #0#1, #0#2, 001;
     #0#1, #0#2, 999;
     ;
     NULL, CompruCarga;
|FIN;

|PROCESO Leer;
     |ABRE #17;
     #17#25 = aSerPed;
     #17#0 = nNumPed;
     |LEE 020#17=;
     |CIERRA #17;
     aDivisa = #17#35
     aMoneda = #17#37;
     |HAZ Divisas104;

     |ABRE #6;      || Clientes
     #6#0 = #2#3;
     |LEE 000#6=;
     |CIERRA #6;

     |ABRE #7;      || Obras
     #7#0 = #2#3;
     #7#1 = #2#4;
     |LEE 000#7=;
     |CIERRA #7;

     |ABRE #8;      || Representantes
     #8#0 = #6#25;
     |LEE 000#8=;
     |CIERRA #8;

     |ABRE #12;     || Vehiculos
     #12#0 = #0#0;
     |LEE 000#12=;
     |CIERRA #12;
|FPRC;

|PROCESO CreaLin;
     #4#41 = #4#41 + 1;

     #18#28 = #1#32;
     #18#0 = #1#33;
     #18#1 = #1#34;
     |LEE 121#18=;
     |SI FSalida = 0;
          #18#64 = #0#3;
          |GRABA 020#18a;
          |LIBERA #18;
     |FINSI;

     |DDEFECTO #9;       || Articulos
     |ABRE #9;
     #9#0 = #1#2;
     |LEE 101#9=;
     |CIERRA #9;


     |DDEFECTO #5;
     lin = lin + 1;
     #5#0 = #4#0;   || Numero  Albaran
     #5#1 = lin;
     #5#2 = #1#2;   || Codigo
     #5#3 = #1#3;   || Almacen
     #5#4 = #1#4;
     #5#5 = #1#5;   || Unidades

     #5#6 = #18#6;
     #5#36 = #18#38;
     ||#5#6 = #1#6;   || Precio
     ||#5#36 = #1#6;

     |SI #4#70 = "D";
          #5#36 = #1#6 / #4#74 * #4#69;  ||Precio DIV
          #5#6 = #1#6 * #4#74 / #4#69;
     |FINSI;
     #5#7 = #1#5 * #1#6;   || Impor
     #5#8 = #1#8;   || %Dto
     #5#10= #1#10;  || %Comi

     ||\\ tipo de iva leer si es exportacion y poner tipo 0
     |ABRE #50;
     #50#26 = #4#52;
     |LEE 000#50=;
     |SI FSalida ! 0 ; |DDEFECTO #50; |FINSI;
     |LIBERA #50;
     |CIERRA #50;

     |SI #50#30 = "S"; |O #6#28 = "S";
          #5#11= 0;  || T.IVA
     |SINO;
          #5#11= #1#14;  || T.IVA
     |FINSI;
     ||\\ ya he leido si es de exportacion

     #5#13= #1#18;
     #5#14= #9#9 * #1#5;   || Coste
     #5#15= #2#3;   || Cliente
     #5#16= #1#17;
     #5#17= #1#19;
     #5#27= #1#27;
     #5#29= #4#52;   || Serie Albaran
     #5#33= #1#29;   || %Dto2
     #5#62= #1#30;   || Palet redondeado al alza
     #5#49= #1#31;   || Lote
     #5#63= #1#0;    || Orden de Carga
     #5#64= #1#1;    || Linea Orden
     #5#65= #1#2;
     #5#66= " ";     || Rotura/Error
     #5#67= #18#58;
     #5#68= #18#59
     #5#69= #18#60;
     #5#70= "";         ||??Lote Reservado
     #5#71= #18#54;
     #5#72= #18#55;
     #5#73= #18#56;

     #5#39= #18#37;
     #5#40= #18#38;
     #5#42 = #18#39;
     #5#12 = #1#33;
     #5#21 = #1#34;
     #5#31 = #1#32;
     #5#41 = #1#38;
     #5#79 = #1#40;
     |HAZ TotalLinMp;
     |HAZ Remanente;
     |GRABA 020#5b;
     enForma = 1;
     enUniPedi = #5#5;
     |HAZ AcumulaAVMp;
     |HAZ CreaLinPen;
     |HAZ ActLote;
     |GRABA 021#5a;

     |SI #9#181 = "S";
          nPesoMortero = nPesoMortero + (#5#5 * #9#102);
     |FINSI;

     |ABRE #83;
     #83#0 = #9#0;
     |LEE 020#83=;
     |CIERRA #83;

     cod_pale = #83#1; |QBF cod_pale;
     |SI cod_pale ! "";
            |HAZ CreaPale;
     |FINSI;
|FPRC;

|PROCESO CreaPale;
     |DDEFECTO #9;       || Articulos
     |ABRE #9;
     #9#0 = cod_pale;
     |LEE 020#9=;
     |CIERRA #9;

     nPalets = (#1#30 + 0.49) ! 0;   || Palets redondeado al alza
     |SI #1#37 ! 0; nPalets = #1#37; |FINSI;
     |SI #81#21 = "S"; nPalets = #1#37; |FINSI;

     |SI nPalets = 0; |ACABA; |FINSI;

     |DDEFECTO #5;
     lin = lin + 1;
     #5#0 = #4#0;   || Numero  Albaran
     #5#1 = lin;
     #5#2 = cod_pale;   || Codigo
     #5#3 = #1#3;   || Almacen
     #5#4 = #9#1;   || Descripcion ARticulo Pale
     #5#5 = nPalets;   || Palets redondeado al alza
     #5#6 = #7#23;   || Precio
     #5#36 = #7#23;
     |SI #4#70 = "D";
          #5#36= #7#23 / #4#74 * #4#69;  ||Precio DIV
     |FINSI;
     #5#7 = #5#5 * #5#6;   || Impor
     #5#8 = 0;   || %Dto
     #5#9 = #5#7 ;   || Impo Total
     ||#5#10= #1#10;  || %Comi
     #5#10= 0;  || %Comi
     |SI #50#30 = "S";|O #6#28 = "S";
          #5#11= 0;  || T.IVA
     |SINO;
          #5#11= #1#14;  || T.IVA
     |FINSI;
     #5#13= #9#2;   || familia
     #5#14= #9#9 * ((#1#30 + 0.49) ! EURODB);   || Coste
     #5#15= #2#3;   || Cliente
     #5#16= #1#17;
     #5#17= #1#19;
     #5#27= #1#27;
     #5#29= #4#52;   || Serie Albaran
     #5#33= 0;   || %Dto2
     #5#79 = #1#40;
     |HAZ TotalLinMp;
     |HAZ AcumulaAVMp;
     |GRABA 021#5b;
     |GRABA 021#5a;
     |LIBERA #5;
|FPRC;

|DEFBUCLE CreaLin;
     #1#1;
     ;
     #0#1, #0#2, 001;
     #0#1, #0#2, 999;
     ;
     NULL, CreaLin;
|FIN;

|PROCESO Albaran;
     |HAZ Leer;

     |ABRET #27;              || Por problemas en el cliente
     #27#2 = #2#0;            || dejamos todo el tiempo bloqueado el contador
     #27#0 = "valbaran";      || hasta grabar la cabecera del albaran
     |LEE 101#27=;
     |SI FSalida ! 0; |GRABA 020#27b; |FINSI;

     |ABRE #4;
     |ABRE #5;

     |SELECT #6#4;
     #4#96 = #2#0;
     #4#97 = #2#1;
     |LEE 000#4=;
     |SI FSalida = 0;
          aAlfa = "0000Orden existente en el albaran:" + #4#52 + "/" + #4#0;
          |MENAV aAlfa;
          |MENAV "0000No se crea el albaran !!!";
          |CIERRA #4;
          |CIERRA #5;
          |CIERRAT #27;
          nError = 1;
          |ACABA;
     |FINSI;

     #4#52 = #2#0;
     enSwMenu = 1;
     |HAZ MalpeAV7;
     nConta = #4#0;

     |SELECT #1#4;
     |DDEFECTO #4;
     #4#0 = nConta;  || N.Alba
     #4#52 = #2#0;   || Serie Albaran
     |LEE 000#4=;
     |SI FSalida = 0;
          |PARA; |SI FSalida = 0; |HACIENDO nConta = nConta + 1;
               #4#0 = nConta;
               |LEE 000#4=;
          |SIGUE;
          #27#1 = nConta + 1;
          |GRABA 020#27a;
          nConta = #4#0;
     |FINSI;
     |CIERRAT #27;

     |ABRE #50;
     #50#26 = #4#52;
     |LEE 000#50=;
     |SI FSalida ! 0 ; |DDEFECTO #50; |FINSI;
     |CIERRA #50;
     ||-----------------Cabecera
     |DDEFECTO #4;
     #4#0 = nConta;
     #4#52 = #2#0;
     |GRABA 121#4b;
     |SI FSalida ! 0; |ACABA; |FINSI;
     aSerCheqAlb =  #4#52;
     nNumCheqAlb = #4#0;

     #4#1 = #0#3;  || Fecha
     #4#3 = #6#0;   || Cliente
     #4#4 = #6#2;   || Nombre
     #4#5 = #17#5;   || Dto
     #4#6 = #17#7;   || Repre
     #4#7 = #17#6;   || Dto PP
     #4#8 = #17#9;   || FP
     #4#9 = #6#35;   || Agencia
     #4#29 = #17#64;   || Entregar
     #4#30= #17#14;   || Dir Ent
     #4#31= #17#20;   || Pobla
     #4#32= #17#17;   || Dto Acumu
     #4#33= #17#21;   || Provin
     #4#34= #8#7;   || Tipo Comi
     #4#35= #6#4;   || Tipo Cli
     #4#36= #6#47;  || Reg. Equi
     #4#40= #6#41;  || Tipo Factura
     #4#43= "N";   || Abono
     |SI #81#22 = "O";
          #4#55= #7#16 % 122;   || Obs. 1
          #4#56= #7#16 % 2337;   || Obs. 2
          #4#57= "";   || Obs. 3
     |SINO;
          #4#55= #17#53;
          #4#56= #17#54;
          #4#57= #17#55;
     |FINSI;
     #4#62= #6#180;
     #4#63= #6#15;

     #4#50 = #6#40;
     |SI #2#26 = "S";
          #4#51 = "N";
     |SINO;
          #4#51 = #50#28;
     |FINSI;
/*
     || Portes P/D
     |SI #2#16 = "S";  ||Tarjeta
          #4#64= "P";
     |SINO;
          #4#64= "D";
     |FINSI;
*/
     #4#64 = #17#61;     || Porte=pedido

     #4#68= #17#35;  ||Divisa
     #4#69= #17#36;  ||Cambio
     #4#70= #17#37;  ||Mone Tra
     #4#73= #17#40;  ||Mone Base
     #4#74= #17#41;  ||Cambio a euros
     #4#88= #6#202;
     #4#91= #6#206;
     #4#84= #7#1;   || Obra
     #4#93= #17#64;   || Entregar_1
     #4#94= #17#65;   || Entregar_2
     #4#95= #17#66;   || Entregar_3
     #4#96= #2#0;   || S. Orden
     #4#97= #2#1;   || N. Orden

     #4#98= #0#0;   || Matricula

     aAlfa = #2#24; |QBF aAlfa;
     |SI aAlfa = "";
         #4#99 = #12#6;  || Conductor
     |SINO;
          #4#99 = #2#24;
     |FINSI;

     #4#100= #0#4;   || Transportista
     #4#101= #0#5;   || Nombre
     #4#102 = #2#16; ||Tarjeta
     #4#103 = #17#73;
     #4#104 = #2#23;     ||Agencia

     #4#105 = #2#25;

     lin = 0;
     nPesoMortero = 0;
     aDivisa = #4#68;
     aMoneda = #4#70;
     |HAZ Divisas010;
     |ABRE #18;
     |BUCLE CreaLin;
     |CIERRA #18;
     |SI #4#64 ! "D";
          |SI nPesoMortero ! 0; |HAZ CalcuPortesMortero; |FINSI;
     |FINSI;
     |GRABA 021#4a;
     |HAZ LinCabMalpe090;
     |BUCLELIN 0 CalCabMalpe090 #4;
     |GRABA 020#4a;

     |CIERRA #4;
     |CIERRA #5;


     |ABRE #6;      || Act. Clientes pend. Facturar
     #6#0 = #4#3;
     |LEE 101#6=;
     |SI FSalida = 0;
          cant = #4#15 < #4#5;
          cant = cant  < #4#32;
          cant = cant < #4#7;
          cant = cant ! EURODB;
          #6#50 = #6#50 + cant;
          |GRABA 021#6a;
     |FINSI;
     |CIERRA #6;
     eaTag = "AV";  |HAZ RiesgosMalpe;

     FSalida = 0;
|FPRC;

|PROCESO ActStock;
     |ABRE #9;
     |ABRE #13;
     |ABRE #14;
     #9#0 = #5#2;
     #13#0 = #5#2; #13#1 = #5#3;
     #14#0 = #5#2; #14#1 = #5#3; #14#2 = #5#35;
     |LEE 121#9=;
     |SI FSalida = 0;
          |LEE 121#13=;
          |SI FSalida = 0;
               |LEE 121#14=;
               |SI FSalida = 0;
                    |DDEFECTO #85;
                    |ABRE #85;
                    #85#0 = #5#2;
                    #85#1 = #5#3;
                    #85#2 = #5#35;
                    |LEE 101#85=;
                    |SI FSalida ! 0; |GRABA 020#85b; |FINSI;
                    aAlfa = #1#38; |QBF aAlfa;
                    |SI aAlfa = ""; |Y cod_pale = "";||Si no esta reservado
                       #9#104 = #9#104 - #5#5;       || Disponible
                    |FINSI;
                    |SI cod_pale ! ""; || cuando es pale siempre resto dispo.
                        #9#104 = #9#104 - #5#5;     || Disponible
                        #13#39 = #13#39 - #5#5;     || Disp.
                        #14#31 = #14#31 - #5#5;     || Disp.
                    |FINSI;
                    #9#6   = #9#6 - #5#5;       || Total
                    #9#94  = #9#94 + #5#5;      || T.Ventas
                    asto = #9#6 - #9#17;
                    |SI asto > 0;
                         #9#10 = asto * #9#9;
                    |SINO;
                         #9#10 = 0;
                    |FINSI;
                    aAlfa = #1#38; |QBF aAlfa;
                    |SI aAlfa = "";|Y cod_pale = ""; || Si no esta reservado
                        #13#39 = #13#39 - #5#5;     || Disp.
                        #14#31 = #14#31 - #5#5;     || Disp.
                    |FINSI;
                    #13#5  = #13#5  - #5#5;     || Tot.
                    #13#35 = #13#35 + #5#5;     || T.Ventas
                    #14#11  = #14#11  - #5#5;     || Tot.

                    ymes = #0#3 % 402;
                    xmes = ymes;
                    xmes = xmes - 1; ymes = xmes;
                    xmes = (xmes * 5) + 34
                    #9xmes = #9xmes + #5#5;

                    xmes = ymes;
                    xmes = (xmes * 2) + 11;
                    #13xmes = #13xmes + #5#5;
||\\ sio es reservado lo resto de reservado
                    |SI #1#38 ! "               ";|Y cod_pale = "";
                         #13#8 = #13#8 - #5#5;
                         #85#9 = #85#9 - #5#5;
                         #9#12 = #9#12 - #5#5;
                    |FINSI;
                    |HAZ ValoraStock;
                    |GRABA 020#85a;
                    |CIERRA #85;
                    |GRABA 021#9a;
                    |GRABA 021#13a;
                    |GRABA 021#14a;

                    enUniVta = #5#5;
                    enUniSal = #5#5;
                    efFecha = #0#3;
                    eaArticulo = #5#2;
                    enAlmacen = #5#3;
                    |HAZ AcumulaArt;
                    |HAZ AcumulaAlm;

               |FINSI;
          |FINSI;
     |FINSI;
     |CIERRA #9;
     |CIERRA #13;
     |CIERRA #14;
|FPRC;

*********************************IMPRESION

|PROCESO Impre
     |PRINT;
|FPRC;

|DEFBUCLE BImpre;
     #1#1;
     ;
     #0#1, #0#2, 001;
     #0#1, #0#2, 999;
     ;
     NULL, Impre;
|FIN;

|PROCESO Imprime;
     |CONFI "0000S Imprimir Albaran ? ";
     |SI FSalida = 0;
          ser_alb = #4#52;
          num_alb = #4#0;
          |SUB_EJECUTA "agifa014";
     |FINSI;
||\\\\ NO PREGUNTAR SI QUIERO EMITIR VALE DE PORTE
||     |SI #2#16 = "N"; |ACABA; |FINSI;
||     |CONFI "0000S Imprimir Tarjeta ? ";
||     |SI FSalida ! 0; |ACABA; |FINSI;
||
||     |ABRE #11;
||     #11#0 = #0#4;
||     |LEE 101#11=;
||     |SI FSalida = 0;
||          informe = #11#10;
||          |QBF informe;
||          |SI informe = ""; informe = infdefe; |FINSI;
||          |IMPRE 0;
||          |SI FSalida = 0;
||               |INFOR informe;
||               |SI FSalida = 0;
||                    |BUCLE BImpre;
||                    |PIEINF;
||                    |FININF;
||               |FINSI;
||               |FINIMP;
||          |FINSI;
||     |FINSI;
||     |CIERRA #11;
|FPRC;

|PROCESO EligeMatri; |TIPO 0;
     |SI #19Campo = Contenido; |Y FSalida = 0;
          |ERROR;
          |PTEC 807;
     |FINSI;
|FPRC;

|PROCESO CheaqLinOrd;
     |SI #1#30 ! #1#37;
          aAlfa = "0000Palets distintos a los reales. Linea:"+#1#1+&13;
          aAlfa = aAlfa + "Articulo:" + #1#2 + " Lote:" + #1#31;
          |MENAV aAlfa;
          aAlfa = "0000Palets:" + #1#30+&13+"Palets reales:"+#1#37;
          |MENAV aAlfa;
     |FINSI;
|FPRC;

|DEFBUCLE CheaqLinOrd;
     #1#1;
     ;
     #2#0, #2#1, 001;
     #2#0, #2#1, 999;
     ;
     NULL, CheaqLinOrd;
|FIN;

|PROGRAMA;
     |ABRE #81; |LEE 000#81p; |CIERRA #81;
     |CLS;
     |CABEZA "Carga";
     |PINPA #0#0;
     |HAZ mirar_de;
     |ABRE #2;
     |ABRE #119;
     |PARA; |SI; |HACIENDO;
          |DDEFECTO #0;
          |PINPA #0#0;
          |PINDA #0#0;
          |ENTLINEAL #1#1, 3, 7, 22, 0, infer, super;
          |ENCAMPO #0#0;
          aAlfa = #0#0; |QBF aAlfa;
          |SI aAlfa = ""; |ACABA; |FINSI;

          ||컴컴컴컴컴Control
          #119#0 = #0#0;
          aAlfa = Usuario % 810;
          #119#1 = aAlfa;
          |HORA aAlfa;
          #119#3 = aAlfa;
          |GRABA 000#119n;
          |SI FSalida ! 0;
               aAlfa = "0000Camion Bloqueado por ["+#119#1+"]"+&13+"Fecha:"+#119#2+" Hora:"+#119#3;
               |MENAV aAlfa;
               |CONFI "0000NContinuar la Carga...";
          |FINSI;
          |SI FSalida = 0;
               |ABRE #19; |SELECT #2#19;
               #19#9  = #0#0;
               #19#15 = "N";
               #19#3 = "";
               |LEE 000#19];
               |SI FSalida = 0; |Y #19#9 = #0#0; |Y #19#15 = "N";
                    nHay = 1;
               |SINO;
                    nHay = 0;
               |FINSI;
               |CIERRA #19;

               |SI nHay ! 0;
                    |PUSHV 0501 2380;
                    |ENTLINEAL #2#19, 1, 4, 22, 1, infcarg, supcarg;
                    |POPV;
                    #2#0 = #19#0;
                    #2#1 = #19#1;
                    |LEE 121#2=;
               |SINO;
                    |MENAV "0000No hay registros...";
                    FSalida = 1;
               |FINSI;
               |SI FSalida = 0;
                    |BUCLE CheaqLinOrd;
                    #0#1 = #2#0;   || Serie
                    #0#2 = #2#1;   || Orden
                    #0#3 = #2#2;   || Fecha
                    #0#4 = #2#10;   || Trans
                    #0#5 = #2#11;   || Nomb
                    #0#6 = #2#6;   || Obra1
                    #0#7 = #2#7;   || Obra2
                    #0#8 = #2#8;   || Obra3
                    #0#9 = #2#14;  || Importe Total
                    #0#10= #2#18;  || Peso Actual
                    #0#11= #2#13;  || Peso Maximo
                    #0#12= #2#20;  || Observaciones
                    |ABRE #203;
                    #203#0 = #2#23;
                    |LEE 000#203=;
                    |CIERRA #203;
                    #0#13 = #203#0;
                    #0#14 = #203#1;
                    |PINDA #0#0;
                    |ENTLINEAL #1#1, 3, 2, 22, 0, infer, super;
                    correcto = "N";
                    |BUCLE CompruCarga;
                    |SI correcto = "S";
                         |CONFI "0000S Crear Albaran ? ";
                         |SI FSalida = 0;
                              |MENSA "0000Creando Albaran...";
                              |HAZ Albaran;
                              |SI FSalida ! 0; |O nError ! 0;
                                   |SI nError ! 0;
                                        |MENSA "0000ERROR. Albaran bloqueado, reintente otra vez";
                                   |FINSI;
                              |SINO;
                                   |MENSA "0000 ";
                                   #2#15 = "S";   || Confirmada
                                   #2#17 = nConta; || N쬋lbaran
                                   |GRABA 021#2a;
                                   |HAZ Imprime;
                              |FINSI;
                         |FINSI;
                    |FINSI;
                    |GRABA 021#2a;
               |FINSI;
               |LIBERA #2;
               ||컴컴컴컴컴컴Control
               #119#0 = #0#0;
               |BORRA 000#119c;
          |FINSI;
          |SI aSerCheqAlb ! "";
               |HAZ ChequeaAlb;
               aSerCheqAlb = "";
          |FINSI;
     |SIGUE;
     |CIERRA #119;
     |CIERRA #2;
|FPRO;

|PROCESO ChequeaAlb;
     |ABRE #4;
     #4#52 = aSerCheqAlb
     #4#0 = nNumCheqAlb;
     |LEE 020#4=;
     |SI FSalida = 0;
          #2#0 = #4#96;
          #2#1 = #4#97;
          |LEE 020#2=;
          |SI FSalida = 0;
               |SI #4#3 ! #2#3; |O #4#84 ! #2#4;
                    |AVISO;
                    |PUSHV 0501 2380;
                    |BLANCO 0919 1667;
                    |CUADRO 0919 1667;
                    |CUADRO 1020 1566;
                    |COLOR 7,4; |ATRI 0; |BLANCO 1021 1465; |ATRI 0;
                    |COLOR 4,7; |ATRI 0; |PINTA 1230, "CLIENTE ALBARAN INCORRECTO "; |ATRI 0;
                    |COLOR 15,4; |ATRI 0; |PINTA 1440, "Pulse Fin para continuar."; |ATRI 0;
                    aFin = &255 + 816;
                    |PARA aTecla = ""; |SI aTecla ! aFin; |HACIENDO;
                         |LEETECLA aTecla;
                    |SIGUE;
                    |POPV;
                    aAlfa = "0000Albaran:" + #4#3 + "/" + (("0000"+#4#84)%-104) + &13;
                    aAlfa = aAlfa + "Carga:" + #2#3 + "/" + (("0000"+#2#4)%-104);
                    |MENAV aAlfa;
               |FINSI;
          |FINSI;
     |FINSI;
     |CIERRA #4;
|FPRC;
컴컴컴컴컴컴컴컴컴컴컴컴컴컴
/*
|PROCESO BusComi;
     nComi1 = 0;
     nComi2 = 0;
     aAlfa = #4#1; aAlfa = aAlfa % -104;
     |DDEFECTO #3;
     |ABRE #3;
     #3#0 = #5#16;
     #3#62 = aAlfa;
     #3#63 = #4#103;
     |LEE 000#3=;
     |SI FSalida = 0;
          menor = 100;
          |PARA pc = 1;|SI pc < 40;|HACIENDO pc = pc + 2;
               pcr = pc + 1;
               |SI #3pc = 0;|Y #3pcr = 0;
                    |SAL;
               |SINO;
                    |SI #3pc ] #5#8;|Y #3pc < menor;
                         menor = #3pc;
                         nComi1 = #3pcr;
                         pcr = (pcr / 2) - 1 + 42;
                         nComi2 = #3pcr;
                    |FINSI;
               |FINSI;
          |SIGUE;
     |FINSI;
     |CIERRA #3;
     nRemanente = #3#64;
|FPRC;
*/
|PROCESO Remanente;
     |SI #4#103 = "N";
          #5#74 = "N";
          #5#75  = 0;
          #5#76 = 0;
          |ACABA;
     |FINSI;

     |ABRE #81; |LEE 000#81p; |CIERRA #81;

     efFecha = #4#1;
     eaRemanente = #4#103;
     eaTieneComi = #5#72;
     eaArticulo = #5#2;
     eaTipoCli = #5#17;
     enDto = #5#8;
     eaTipoComi = #5#16;
     |HAZ BusComi;
     nComi1 = enComision1;
     nComi2 = enComision2;

     |DDEFECTO #137;
     |ABRE #137;
     #137#0 = #4#6;
     |LEE 101#137=;
     |SI FSalida ! 0; |GRABA 020#137b; |FINSI;

     nForma = 1;
     nPorce1 = (#5#5 * #5#68) * #5#8 / 100;
     nPorce2 = (#5#5 * #5#68) * nRemanente / 100;
     nImpo = nPorce2 - nPorce1;
     |SI #5#8 = nRemanente;
          #5#74 = "N";
          #5#75 = 0;
          #5#76 = nComi1;
     |FINSI;
     |SI #5#8 < nRemanente;
          #5#74 = "A";
          #5#75 = nImpo;
          #5#76 = nComi1;
          #137#5 = #137#5 + (nImpo * nForma);
     |FINSI;
     |SI #5#8 > nRemanente;
          #5#74 = "C";
          #5#75 = nImpo;
          #5#76 = nComi1;
          #137#4 = #137#4 + (nImpo * nForma);
     |FINSI;
     #137#7 = #137#1 + #137#3 + #137#2 - #137#6;
     #137#8 = #137#1 + #137#5 + #137#4 - #137#6;
     |GRABA 020#137a;
     |CIERRA #137;
|FPRC;

|PROCESO AsigPrePorte;
     #5#69 = nPortes;
     #5#6 = #5#67 + #5#69;
     |HAZ TotalLinMp;
     |GRABA 020#5a;
     |LIBERA #5;
|FPRC;

|DEFBUCLE AsigPrePorte;
     #5#1;
     ;
     #4#52, #4#0, 001;
     #4#52, #4#0, 999;
     be;
     NULL, AsigPrePorte;
|FIN;

|PROCESO TariMortero;
     |SI nPesoMortero ] #211#3; |Y nPesoMortero [ #211#4;
||          |SI #211#6 = "TO";
               nPortes = #211#5;
||          |SINO;
||               nPortes = nPesoMortero * #211#5;
||          |FINSI;

          nEncuentra = 1;
          |ERROR10;
     |FINSI;
|FPRC;

|PROCESO CalcuPortesMortero;
     nEncuentra = 0;
     |SI #9#181 = "S";
          fFecha = #17#1;
     |SINO;
          fFecha = #4#1;
     |FINSI;
     |ABRE #210;
     #210#0 = #7#22;
     #210#1 = fFecha;
     |LEE 000#210];
     |SI FSalida = 0;
          |SI #210#0 ! #7#22; |O #210#1 ! fFecha;
               |LEE 000#210a;
          |FINSI;
     |SINO;
          |LEE 000#210u;
     |FINSI;
     |SI FSalida = 0; |Y #210#0 = #7#22; |Y #210#1 [ fFecha;
          |BUCLELIN 2 TariMortero #210;
     |FINSI;
     |CIERRA #210;
     |SI nEncuentra ! 0;
          |BUCLE AsigPrePorte;
     |FINSI;
|FPRC;
