|FICHEROS;
     guiz0001 #0;
     agifa024 #3;
     infom005 #50;
     agifa104 #8;
     agifa038 #13;
     agifa048 #15;
     agifa049 #16;
     agifa007 #40;
     agifa142 #41;

     agi00002 #43;  || Fichero de trabajos;
     agi00003 #44;
     dsm00010 #910;

     agifa010 #11;
     agifa071 #12;
     dsm00005 #1905;

     tmp00010 #1;
     tmp00071 #2;
     tmpdsm05 #905;
     dsm00053 #53;
     dsm00047 #47;
     agifa324 #324;
     agifa019 #19;
     agi00101 #100;

     infom000 #5;
     guim0007 #200; || Bandeja Albaranes

     tmp00000 #300; || Temporal de albaranes que se imprimen
     dsm00073;
     referen@;
|FIN;

|VARIABLES;
     aParam = "";
     &enSwDA = 0;
     &eaDaAgr = "";
     aLon = "";
     aTmp300 = "";
     &enSwInfoz003 = 0;
     &Tempo = "";
     &eaImpresora = "";
     &eaMenuImpre = "";
     &enTiva = 0;
     &efFiva = @;
     &eaDiva = "";
     &enIva = 0;
     &enRec = 0;

     &enSwMail = 0;
     &enInforpConfi = -1;
     aEsUno = "";
     aIP = "";
     aDSCorreo = "";
     aProxy = "";
     aDirOri = "";
     aDirDes = "";
     aTitulo = "";
     aTexto = "";
     aNomFic = "";
     {1}aLocal = "";
     aBaseLocal = "";
     aMail = "";
     nHayMail = 0;
     nImpMail = 0;
     &enBulto = 0; || Modulo Orts
     &enCntrl = 0;

     &eaCli = "";
     nSalida = 0;
     nLinV        = 0;
     nCampos      = 0;
     i            = 0;
     &nSwDefecto  = 0; || Para Descripcion Ampli
     &eaLote      = "";
     aAlfa        = "";
     aAlf1        = "";
     &enHayIMEI   = 0;
     &eaIMEI      = "";
     &eaABO       = "";
     &eaICC       = "";
     &enSwIMEI    = 0;

     &aDivisa     = "";
     &aMoneda     = "";
     &enEscan     = 0;
     &aQuelis     = "";
     &enImpo      = 0;
     &enDto1      = 0;
     &enDto2      = 0;
     nSwPasa      = 0;
     &eaTipo      = "";
     &eaSerie     = "";
     &enCodig     = 0;
     &enLinea     = 0;
     &eaArticulo  = "";
     &eaNArticulo = "";
     &enUnidades  = 0;
     &enPrecio    = 0;
     &eaFichAlba  = "";
     &enPantalla  = 0;
     &enNPedido   = 0;

     &trabajo  = 0;
     &ser_alb  = "";
     &num_alb  = 0;
     &codigo   = "";
     &descrip  = "";
     &supedino = "";
     &bl       = "                              ";
     &abono    = "N";  || Viene de agifa010

     mes        = 0;
     margen     = 0;
     imneto     = 0;
     aAlfa1     = "";
     aAlfa2     = "";
     nCampo     = 0;
     nSw010     = 0;
     aFichero   = "";

     nSwOpcion = 0;
     desde     = "                              ";
     hasta     = "zzzzzzzzzzzzzzzzzzzzzzzzzzzzzz";
     informe   = "";
     vacio     = "";
     digito    = "";

     &ascii    = 0;
     &impal    = 0;
     &impor    = 0;
     &iva1     = 0;
     &iva2     = 0;
     &iva3     = 0;
     &iva4     = 0;
     &iva5     = 0;
     &re1      = 0;
     &re2      = 0;
     &re3      = 0;
     &re4      = 0;
     &re5      = 0;
     &precio   = 0;
     &dto      = 0;
     &dto1     = 0;
     &unid     = 0;
     &imptot   = 0;
     &tiva     = 0;
     &linea    = 0;

     indtope   = 0;
     ind       = 0;
     n_copia   = 0;

     &dtrab    = "";
     &swt      = 0;

{-1} MenuImpre   = "";
     MenuImpre1  = "1021";
     MenuImpre2  = "1";
     MenuImpre3  = "7";
     MenuImpre4  = "Salir                    ";
     MenuImpre5  = "Albaran                  ";
     MenuImpre6  = "Albaran Agencia          ";
     MenuImpre7  = "Etiquetas Agencia        ";
     MenuImpre8  = "Pdte.Servir Parcialmente ";
     MenuImpre9  = "Pdte.Servir Completamente";
     MenuImpre10 = "Pdte.Servir Ambos.       ";

{-1} MenuGuipu   = "";
     MenuGuipu1  = "1021";
     MenuGuipu2  = "1";
     MenuGuipu3  = "9";
     MenuGuipu4  = "Salir                    ";
     MenuGuipu5  = "Albaran                  ";
     MenuGuipu6  = "Albaran Agencia          ";
     MenuGuipu7  = "Etiquetas Agencia        ";
     MenuGuipu8  = "Pdte.Servir Parcialmente ";
     MenuGuipu9  = "Pdte.Servir Completamente";
     MenuGuipu10 = "Pdte.Servir Ambos.       ";
     MenuGuipu11 = "Etiquetas GUIPUZCOANA    ";
     MenuGuipu12 = "Etiquetadoras            ";

     &sobre = "";
     nImpDiv      = 0;
     nMult        = 0;

     &nDeci_Base     = 0;   || Decimales en Totales de la moneda base
     &nDeci_PreBase  = 0;   || Decimales en el Precio en moneda base
     &nDeci_BaseMx   = 0;   || Decimales en Totales de la moneda base (Maxima precision)
     &nDeci_PreBaseMx= 0;   || Decimales en el Precio en moneda base (Maxima precision)
     &nDeci_Div      = 0;   || Decimales en Totales de la divisa
     &nDeci_PreDiv   = 0;   || Decimales en el Precio en divisa
     &nDeci_TotVis   = 0;   || Decimales en Totales visibles (el de la moneda de trabajo)
     &nDeci_TotNoVis = 0;   || Decimales en Totales no visibles (el de la otra moneda)
     &nDeci_PreVis   = 0;   || Decimales en el Precio visual. (lineas)
     &nDeci_ImpVis   = 0;   || Decimales en el Importe visual. (lineas)
     &decim_divisa = 0;  || Decimales de la divisa
     &precio_divisa = 0; || Decimales del precio en divisa
     &decim_base = 0;    || Decimales de la moneda base
     &precio_base = 0;   || Decimales del precio en moneda base

     &enNumEje = 0;
     &enNumEnt = 0;
|FIN;

|| PROCESO DE SEPARACION DEL ABARAN CREADO AHORA EN SU HONONIMO EN NEGRO

|PROCESO &Defecto;       || Lo llama el informe
     |SI nSwDefecto = 1;
          |PDEFECTO #2, 1, 4;
          |PDEFECTO #12, 1, 4;
          |PDEFECTO #2, 5, 60;
          |PDEFECTO #12, 5, 60;
     |FINSI;
|FPRC;

|PROCESO Informe_Inforpor;
     || si la impresora es PDF, Correo PDF o Crytal
     || le cambia la primera letra por "a"
     aAlfa = Impresora % 104;
     |SI aAlfa = "PDF;"; informe = "a" + (informe % 2); |FINSI;
     aAlfa = Impresora % 107;
     |SI aAlfa = "Correo;"; informe = "a" + (informe % 2); |FINSI;
     aAlfa = Impresora % 113;
     |SI aAlfa = "CrystalReport"; informe = "a" + (informe % 2); |FINSI;
|FPRC;

|PROCESO trabajo;
     dtrab = #44#2;      || DESCRIPCION TRABAJO.
     |PRINT;
|FPRC;

|PROCESO escanlinea;
    unid    = #16#4 * #2#5;
    descrip = #16#3;
    codigo  = "";
    dto     = 0;
    precio  = 0;
    imptot  = 0;
    tiva    = 0;
    linea   = 1;
    |PRINT;
|FPRC;

|PROCESO xx;|TIPO 0;
     codigo  = "";
     descrip = "";

     dto    = "";
     dto1   = "";
     precio = "";
     unid   = "";
     imptot = "";
     tiva   = "";
     indtope=0
     |PARA ind=6;|SI ind>0;|HACIENDO ind=ind-1;
          descrip = #13ind;  |QBF descrip;
          |SI descrip ! vacio;
              indtope = ind + 1;
              ind=0;
          |FINSI;
     |SIGUE;

     |PARA ind=1;|SI ind<indtope;|HACIENDO ind=ind+1;
           descrip=#13ind;
           linea=0;
           |PRINT;
     |SIGUE;
|FPRC;

|PROCESO impdesam;|TIPO 0;
     |ABRE #13;
     #13#0=#2#27;
     |LEE 000#13=;
     |SI FSalida = 0;  |HAZ xx;  |FINSI;
     |LIBERA #13;
     |CIERRA #13;
|FPRC;

|PROCESO DesAmpli;
     aAlfa = eaDaAgr + " " + #53#5; |QBF aAlfa;
     aLon = aAlfa % A0;
     |SI aLon ] 50
          enSwDA = 1;
          |PRINT;
          enSwDA = 0;
          eaDaAgr = #53#5; |QBF eaDaAgr;
     |SINO;
          eaDaAgr = eaDaAgr + " " + #53#5; |QBF eaDaAgr;
     |FINSI;
|FPRC;

|DEFBUCLE DesAmpli;
     #53#1;
     ;
     "A", #2#29, #2#0, #2#1, 0001;
     "A", #2#29, #2#0, #2#1, 9999;
     ;
     NULL, DesAmpli;
|FIN;

|PROCESO dsm00047;
     unid = #12#5 * #47#5;
     |PRINT;
|FPRC;

|DEFBUCLE dsm00047;
     #47#1;
     ;
     #2#29, #2#0, #2#1, 001;
     #2#29, #2#0, #2#1, 999;
     ;
     NULL, dsm00047;
|FIN;

|PROCESO IVAS;
     enTiva = 1; efFiva = #11; |HAZ SacaIVA;
     iva1 = enIva;  re1 = enRec;
     enTiva = 2; efFiva = #11; |HAZ SacaIVA;
     iva2 = enIva;  re2 = enRec;
     enTiva = 3; efFiva = #11; |HAZ SacaIVA;
     iva3 = enIva;  re3 = enRec;
     enTiva = 4; efFiva = #11; |HAZ SacaIVA;
     iva4 = enIva;  re4 = enRec;
     enTiva = 5; efFiva = #11; |HAZ SacaIVA;
     iva5 = enIva;  re5 = enRec;
|FPRC;

|PROCESO LeeLote;
     eaLote = "";
     |ABRE #100;
     #100#0 = #2#29;
     #100#1 = #2#0;
     #100#8 = #2#1;
     #100#5 = "";
     |LEE 000#100];
     |SI #100#0 = #2#29; |Y #100#1 = #2#0; |Y #100#8 = #2#1;
          eaLote = #100#5;
     |FINSI;
     |CIERRA #100;
|FPRC;

|PROCESO ImpreNormal;
     #8#25 = #2#31;      ||serie pedido
     #8#0 = #2#12;       ||pedido
     |LEE 000#8=;
     |SI FSalida ! 0;  |DDEFECTO #8;  |FINSI;

     |HAZ LeeLote;

     codigo  = #2#2;
     descrip = #2#4;

     dto    = #2#8;
     dto1   = #2#33;
     precio = #2#6;
     unid   = #2#5;
     imptot = #2#9;
     tiva   = #2#11;
     linea  = 1;
     digito = #2#4 % "101";
     ascii  = &digito;
     sobre  = #2#47;

     |PRINT;

     |SALVA_DATOS #12;
     |BUCLE DesAmpli;
     |SI eaDaAgr ! "";
          enSwDA = 1;
          |PRINT;
          enSwDA = 0;
          eaDaAgr = "";
     |FINSI;
     |REPON_DATOS #12;

     |HAZ impdesam;

     |SI #41#16 = "S";
          |ABRE #15;
          #15#0 = #2#2;
          |LEE 000#15=;
          |SI FSalida = 0;
               |SI #41#12 = "K";
                    enEscan = 2;
                    |BUCLE dsm00047;
               |SINO;
                    enEscan = 1;
                    |BUCLELIN 0escanlinea#15;
               |FINSI;
                enEscan = 0;
          |FINSI;
          |CIERRA #15;
     |FINSI;
|FPRC;

|PROCESO ImpreHorizo;
     eaTipo      = "AV";
     eaSerie     = #2#29;
     enCodig     = #2#0;
     enLinea     = #2#1;
     eaArticulo  = #2#2;
     eaNArticulo = #2#4;
     enUnidades  = #2#5;
     enPrecio    = #2#6;
     enNPedido   = #2#12;
     enImpo      = #2#37;
     enDto1      = #2#8;
     enDto2      = #2#33;
     |HAZ TrataCompo;
|FPRC;

|PROCESO albimprl;|TIPO 0;
     ||컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
     ||No se imprimen las unidades desglosadas al 100%
     aAlfa = #2#47; |QBF aAlfa;    ||(Anna&Robert lo usa)
     |SI aAlfa ! "";
          aAlfa = #2#47 % 101;
          aAlf1 = #2#47 % 201;
          |SI aAlfa = "P";         ||Por Unidad
               |SI aAlf1 = "P";
                    aAlfa = #2#47 % 303;
                    |SI aAlfa = 100; |ACABA; |FINSI;
               |SINO;
                    aAlfa = #2#47 % 303;
                    |SI aAlfa = 0; |ACABA; |FINSI;
               |FINSI;
          |FINSI;
     |FINSI;
     ||컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴

     |SI #0#13 = "N"; |Y #2#5 = 0; |ACABA; |FINSI;
     |SI #0#14 = "N"; |Y #2#6 = 0; |ACABA; |FINSI;

     nSwPasa = 1;
     |SI #910#19 = "N";  |HAZ ImpreNormal;  |FINSI;
     |SI #910#19 = "S";  |HAZ ImpreHorizo;  |FINSI;
|FPRC;

|DEFBUCLE tmp00071;
     #2#1;
     ;
     #1#52, #1#0, 001;
     #1#52, #1#0, 999;
     ;
     NULL, albimprl;
|FIN;

|PROCESO albimpr1;|TIPO 0;
     |HAZ IVAS;

     aDivisa = #1#68;
     aMoneda = #1#70;
     |HAZ Divisas010;

     #3#0 = #1#3;
     |LEE 000#3=;
     |SI FSalida ! 0;  |DDEFECTO #3;  |FINSI;
     aMail = #3#197; |QBF aMail;
     |SI nImpMail = 0; |Y aMail ! ""; |Y aParam ! "NOGRABA";
          |ABRE #200;
          |DDEFECTO #200;
          #200#0 = #1#52;
          #200#1 = #1#0;
          #200#2 = ~;
          |HORA aAlfa;
          #200#3 = aAlfa;
          #200#4 = Usuario % 810;
          |GRABA 020#200n;
          |CIERRA #200;
     |FINSI;

     #40#26 = #1#52;
     |LEE 000#40=;
     |SI FSalida ! 0;  |DDEFECTO #40;  |FINSI;

     |SI #0#8 = "N";     ||No imprimo abonos
         |SI #1#50 = "S";
             informe = #40#3; |QBF informe;
         |SINO;
             informe = #40#4; |QBF informe;
         |FINSI;
     |SINO;
         informe = #40#5; |QBF informe;
     |FINSI;

     |HAZ Informe_Inforpor;
     |INFOR informe;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |PARA n_copia = 0; |SI n_copia [ #3#187; |HACIENDO n_copia = n_copia + 1;
           trabajo = 0;
           nSwPasa = 0;
           |SI #910#19 = "S";  |HAZ AbreTemporales;  |FINSI;

           |BUCLE tmp00071;

           |SI #910#19 = "S";  |HAZ ElRemate;  |FINSI;
           |SI nSwPasa = 0; |PIEINF; |FINSI;

           ||------------------ OPCION IMPRIMIR TRABAJOS --------------;
           |SI #41#132 = "S";
               |ABRE #43;
               |SELECT #4#43;
               #43#2 = #1#52;      || Serie;
               #43#3 = #1#0;       || Albaran;
               |LEE 000#43=;
               |SI FSalida = 0;
                   trabajo = 1;
                   swt     = 1;
                   |BUCLELIN 2 trabajo #43;
               |FINSI;
               |SELECT #1#43;
               |CIERRA #43;
           |FINSI;

           swt = 0;
           ||----------------------------------------------------------;
           |PIEINF;
           impal = 0;
           impor = 0;

           |SI nImpMail = 1; |SAL; |FINSI;
     |SIGUE;

     |FININF;

     #300#0 = #1#52;
     #300#1 = #1#0;
     #300#2 = #1#90;
     |GRABA 000#300n;    ||Temporal de albaranes que se imprimen

     |LEE 140#1=;
     |SI FSalida = 0;
          #1#90 = "S";
          |GRABA 020#1a;
          |LIBERA #1;
     |FINSI;

     |SI enSwInfoz003 = 1;
          |ABRE #50;
          #50#0 = "A";
          #50#1 = #1#52;
          #50#2 = #1#0;
          |GRABA 000#50n;
          |CIERRA #50;
     |FINSI;
|FPRC;

|DEFBUCLE tmp00010p;
     #1#2;
     1, 43;
     #0#0 , desde, #0#9  , #0#2 , #0#4 , #0#8;
     #0#1 , hasta, #0#10 , #0#3 , #0#5 , #0#8;
     ;
     NULL, albimpr1;
|FIN;

|DEFBUCLE tmp00010pa;
     #1#1;
     3,1;
     #0#9  , #0#2 , #0#0 , #0#4;
     #0#10 , #0#3 , #0#1 , #0#5;
     ;
     NULL, albimpr1;
|FIN;

|DEFBUCLE tmp00010s;
     #1#1;
     ;
     #0#9,  #0#2;
     #0#10, #0#3;
     ;
     NULL, albimpr1;
|FIN;

|PROCESO TmpMail;
     |ESPECIFICA "RBASS", aLocal;
     aBaseLocal = aLocal;

     aNomFic = ser_alb + (("000000" + num_alb) % -106) + ".pdf"; |QBT aNomFic;

     aFichero = aBaseLocal + "crystal/albaran.pdf";
     Impresora = "CrystalReport;" + aFichero;
     |IMPRE -1;
     |SI FSalida ! 0; |ACABA; |FINSI;

     #1#52 = ser_alb;
     #1#0 = num_alb;
     |LEE 000#1=;
     |SI FSalida = 0;
          |HAZ albimpr1;
     |FINSI;
     |LIBERA #1;
     |FINIMP;

     |ABRE #5; |LEE 000#5p; |CIERRA #5;
     |SI #5#6 = "F"; aDSCorreo = #5#5;             |FINSI;
     |SI #5#6 = "L"; aDSCorreo = ""; |IP_REMOTO aDSCorreo; |FINSI;
     |SI #5#6 = "S"; aDSCorreo = ""; |IP_LOCAL  aDSCorreo; |FINSI;
     |QBF aDSCorreo;
     aProxy = #5#7; |QBF aProxy;
     aDirOri = #5#8; |QBF aDirOri;
     aDirDes = #3#197;  |QBF aDirDes;
     aTitulo = "Albaran " + #1#52 + "/" + (("000000" + #1#0) % -106);
     aTexto = aTitulo;

     aAlfa = "2405Enviando [" + aDSCorreo + "] ...";
     |MENSA aAlfa;

     |DFICE aAlfa;
     aAlfa = aAlfa + aNomFic;

     aIP = ""; |IP_REMOTO aIP;
     |SI aIP ! "";
          |RECIBE_FICHERO aFichero, aAlfa;
     |SINO;
          |COPIA_FICHERO aFichero, aAlfa;
     |FINSI;
     |RFBORRA aFichero;
     aFichero = aAlfa;

     |DSCORREO_ENVIA aDSCorreo, aProxy, aDirOri, aDirDes, aTitulo, aTexto, aFichero;
     |SI FSalida ! 0;
          aAlfa = "0000Problemas al enviar correo, Albaran: " + ser_alb + "/" + (("000000" + num_alb) % -106);
          |MENAV aAlfa;
     |SINO;
          ||MENAV "0000Envio correo completado.";
     |FINSI;
     |FBORRA aFichero;
|FPRC;

|PROCESO EnviaMail;
     nImpMail = 1;
     |ABRE #3;
     |ABRE #8;
     |ABRE #40;
     |ABRE #1;
     |HAZ TmpMail;
     |CIERRA #1;
     |CIERRA #40;
     |CIERRA #8;
     |CIERRA #3;
|FPRC;

|PROCESO Tipo3;
     nImpMail = 0;
     enPantalla = 0;

     |SI aMail = "";
          |SI eaImpresora = "";
               |IMPRE 1;
          |SINO;
               Impresora = eaImpresora;
               |IMPRE -1;
          |FINSI;
          |SI FSalida ! 0;  |ACABA;  |FINSI;

          |ABRE #3;
          |ABRE #8;
          |ABRE #40;
          |SI ser_alb = ""; |Y num_alb = 0;
              |SI #0#12 = "C";
                   |BUCLE tmp00010p; ||Por Cliente...
              |SINO;
                   |BUCLE tmp00010pa;||Por Albaran...
              |FINSI;
          |SINO;
              #0#8 = abono;  || Le indico si quiero imprimir abonos
              |BUCLE tmp00010s;
          |FINSI;
          |CIERRA #40;
          |CIERRA #8;
          |CIERRA #3;

          |FINIMP;
     |FINSI;
|FPRC;

|PROCESO Agencia;
     |SUB_EJECUTA_NP "agifa011";
|FPRC;

|PROCESO Etiquetas;
     |SUB_EJECUTA_NP "agifa013";
|FPRC;

|PROCESO Resto;
     |SUB_EJECUTA_NP "dsy00002";
|FPRC;

|PROCESO Final;
     |CIERRA #300;

     |SI enSwInfoz003  = 0;
          aAlfa = "infoz003;" + aTmp300;
          |SUB_EJECUTA_NP aAlfa;
     |FINSI;

     Tempo = aTmp300; |HAZ BoraTempo; |DELINDEX #300;
|FPRC;

|PROGRAMA;
     aParam = PARAMETRO;
     |ABRE #41; |LEE 000#41p; |CIERRA #41;
     |ABRE #910; |LEE 000#910p; |CIERRA #910;

     |HAZ CreaTempo; |NOME_DAT #300 Tempo; |DELINDEX #300; aTmp300 = Tempo; |ABRE #300;

     |SI ser_alb = ""; |Y num_alb = 0;
          aEsUno = "N";
          |CLS;
          |CABEZA "Imp. Albaran";
          |PINPA #0#0;
          |PINDA #0#0;
          |ENDATOS #1#0;
          |SI FSalida = 0;  |HAZ Tipo3;  |FINSI;
     |SINO;
          aEsUno = "S";
          #0#9  = ser_alb;
          #0#10 = ser_alb;
          #0#2  = num_alb;
          #0#3  = num_alb;
          FSalida = 0;
          |SI enInforpConfi = 0; |O enInforpConfi = 2;
               |HAZ Tipo3;
               |HAZ GuipuzEti2;
               |HAZ Final; |ACABA;
          |FINSI;
          |SI enSwMail = 1;
               |HAZ EnviaMail;
               |HAZ Final; |ACABA;
          |FINSI;

          |SI eaMenuImpre = "N";
               |HAZ Tipo3;
               |HAZ Final; |ACABA;
          |FINSI;
          |CONFI "2400NDesea Menu de Impresion : ";
          |SI FSalida ! 0;
               |HAZ Tipo3;
               |HAZ Final; |ACABA;
          |FINSI;
          nSwOpcion = 1;

         |PUSHV 0501 2380;
         |PARA; |SI nSwOpcion = 1; |HACIENDO;
                |BLANCO 0802 2062;
                |MENUG MenuGuipu;
                MenuImpre2 = FSalida;
                |SI MenuImpre2 = "1";  nSwOpcion = 0;     |FINSI;
                |SI MenuImpre2 = "2";  |HAZ Tipo3;        |FINSI;
                |SI MenuImpre2 = "3";  |HAZ Agencia;      |FINSI;
                |SI MenuImpre2 = "4";  |HAZ Etiquetas;    |FINSI;
                |SI MenuImpre2 = "5";  aQuelis = "P"; |HAZ Resto;  |FINSI;
                |SI MenuImpre2 = "6";  aQuelis = "T"; |HAZ Resto;  |FINSI;
                |SI MenuImpre2 = "7";  aQuelis = "A"; |HAZ Resto;  |FINSI;
                |SI MenuImpre2 = "8";  |HAZ GuipuzEti;    |FINSI;
                |SI MenuImpre2 = "9";  |HAZ GuipuzEti2;|FINSI;
                aMail = "";
         |SIGUE;
         |POPV;
     |FINSI;
|FPRO;
