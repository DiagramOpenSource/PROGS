|FICHEROS;
     gofase01 #0;
     gofase02 #1;
     gofase03 #2,MANTE;
     gopre001 #3;
     gopre002 #4;
     gopre003 #5;
     gopre004 #6;
     goparame #7;
     gocallac #17;                                ||Calendario Laboral (cab).
     gocallal #18;                                ||Calendario Laboral (lin).
     agifa024 #19;
     gofase10 #20;

     gocerc01 #203;
     gocerc02 #204;
     agifa007 #205;
     guerw023 #23,MANTE;
     gofase11 #24;
     gofase03@ #100;
|FIN;
----------------------------------------------------------------------
|VARIABLES;
     aDef    = "";
     &eaTipo = "";
     &enNum  = 0;
     &eaSer  = "";

     &eaSerie       = "";
     &enNuPre       = 0;
     i              = 0;
     codPro         = "";
     nMod           = 0;
     nModo          = 0;
     nLinea         = 0;
     nOrdCap        = 0;
     nOrdPar        = 0;
     aAlfa          = "";
     cMano          = "";
     nAnoIni        = 0;
     nAnoFin        = 0;
     cAnos          = "";
     nCua           = 0;
     nDias          = 0;
     nSuma          = 0;
     nTotDias       = 0;
     nFecIni        = 0;
     nFecFin        = 0;
     nCampo         = 0;
     nHora          = 0;

     cAno           = "";
     cAnoIni        = "";
     cAnoFin        = "";
     cMonPre        = "";
     cConta         = "";
     cFecIni        = "";
     cFecFin        = "";
     cFecha         = "";
     cDia           = "";

     fFecha         = @;
     fDia           = @;
     fFecFin        = @;
     fFecIni        = @;
     fFecIniPar     = @;
     fFecFinPar     = @;
     fFecIniCap     = @;
     fFecFinCap     = @;
     fFecha5        = @;

     &nDeci_Medi    = 0;
     &nDeci_Meda    = 0;

     fFechaIni = @;
     fFechaFin = @;
     nImporte = 0;
     aAviso = "";
     aFechaIni = "";
     aFechaFin = "";
     aFecha = "";
     aMesIni = "";
     aAnyoIni = "";
     aMesFin = "";
     aAnyoFin = "";
     aMes = "";
     aAnyo = "";
     aClave = "";
     nClave = 0;
     num = 0;
     nNume = 0;
     nMaxDias = 0;
|FIN;
----------------------------------------------------------------------
|PROCESO PinFas; |TIPO 7;
     |HAZ FasIni;
|FPRC;
-------------------------------------------------------------------------------
|PROCESO FasIni; |TIPO 14;
     |ABRE #7; |LEE 000#7p; |CIERRA #7;
     nDeci_Medi = #0#10;
     nDeci_Meda = #7#115;
     |ENTLINEAL #1#2, -4, 7, 22, 0, cFasParMin, cFasParMax;
|FPRC;
-------------------------------------------------------------------------------
|PROCESO cFasParMin;
     #2#0 = #1#0;
     #2#1 = #1#1;
     #2#2 = #1#3;
     #2#3 = 1;
|FPRC;
-------------------------------------------------------------------------------
|PROCESO cFasParMax;
     #2#0 = #1#0;
     #2#1 = #1#1;
     #2#2 = #1#3;
     #2#3 = 999;
|FPRC;
-------------------------------------------------------------------------------
|PROCESO FasEnt; |TIPO 0;
     nModo = (FEntrada/100)!0;
     |ABRE #7;
     #7#0 = "A";
     |LEE 000#7=;

     |SI FSalida = 0;
          cMano = #7#5;                           ||Codigo Mano de Obra;
     |FINSI;

     |CIERRA #7;

     |SI nModo = 1;
          nLinea = 0;
          nOrdCap = 0;
          nOrdPar = 0;
          |ABRE #1;
          |ABRE #2;
          |ABRE #3;
          #3#0 = #0#0;
          #3#1 = #0#1;
          |LEE 000#3=;

          |SI FSalida = 0;
               |BUCLELIN 2 MiraFasCapi #3;
          |FINSI;

          |CIERRA #3;
          |CIERRA #2;
          |CIERRA #1;
     |FINSI;

     fFecha5 = #0#5;
     nHora = #0#6;

|FPRC;
-------------------------------------------------------------------------------
|PROCESO MiraFasDes;
     #2#8 = #2#8 + #6#8;
|FPRC;
-------------------------------------------------------------------------------
|DEFBUCLE MiraFasDes;
     #6#1;
     14;                                          ||Campos Extras.
     #5#0, #5#1, #5#2, #5#4,   1, cMano;
     #5#0, #5#1, #5#2, #5#4, 999, cMano;
     ;                                          ||b/e Bolqueo-Ordenacion.
     NULL, MiraFasDes;
|FIN;
-------------------------------------------------------------------------------
|PROCESO MiraFasPart;
     nOrdPar = nOrdPar + 1;
     |DDEFECTO #2;
     |BUCLE MiraFasDes;
     #2#0  = #5#0;
     #2#1  = #5#1;
     #2#2  = #5#2;
     #2#3  = #5#3;
     #2#4  = #5#4;
     #2#5  = #5#5;
     #2#6  = #5#6;
     #2#7  = #5#7;
     #2#8  = #2#8 * #2#7;
     #2#11 = nOrdPar;
     |GRABA 020#2c;
     #1#7 = #1#7 + #2#8;
|FPRC;
-------------------------------------------------------------------------------
|DEFBUCLE MiraFasPart;
     #5#1;
     ;                                          ||Campos Extras.
     #4#0, #4#1, #4#12, 1;
     #4#0, #4#1, #4#12, 999;
     ;                                          ||b/e Bolqueo-Ordenacion.
     NULL, MiraFasPart;
|FIN;
-------------------------------------------------------------------------------
|PROCESO MiraFasCapi;
     nOrdCap = nOrdCap + 1;
     |DDEFECTO #1;
     |BUCLE MiraFasPart;
     #1#0 = #4#0;
     #1#1 = #4#1;
     #1#2 = #4#2;
     #1#3 = #4#12;
     #1#4 = #4#13;  ||antes #4#4 VIC
     #1#5 = #4#5;
     #1#6 = #4#6;
     #1#10 = nOrdCap;
     |GRABA 020#1c;
|FPRC;
-------------------------------------------------------------------------------
|PROCESO BajaFasPar;
     |BORRA 020#2a;
|FPRC;
-------------------------------------------------------------------------------
|DEFBUCLE BajaFasPar;
     #2#1;
     ;                                          ||Campos Extras.
     #1#0, #1#1, #1#3, 1;
     #1#0, #1#1, #1#3, 999;
     be;                                          ||b/e Bolqueo-Ordenacion.
     NULL, BajaFasPar;
|FIN;

|PROCESO BajaPerson;
     |BORRA 020#24a;
|FPRC;

|DEFBUCLE BajaPerson;
     #24#1;
     ;
     #1#0, #1#1, #1#3, "                    ", "                    ";
     #1#0, #1#1, #1#3, "zzzzzzzzzzzzzzzzzzzz", "zzzzzzzzzzzzzzzzzzzz";
     be;
     NULL, BajaPerson;
|FIN;

|PROCESO Fase02; |TIPO 2;
     nModo = (FEntrada/100)!0

     |SI nModo = 3;
          |BUCLE BajaFasPar;
          |BUCLE BajaPerson;
     |FINSI;
|FPRC;
-------------------------------------------------------------------------------
|PROCESO DiasFest;
     nSuma = 0;
     nTotDias = 0;
     cFecIni = fFecIni;
     cFecFin = fFecFin;
     cAnoIni = cFecIni % 704;                     ||Año de Inicio.
     cAnoFin = cFecFin % 704;                     ||Año de Fin.

     |ABRE #17;
     |ABRE #18;

     /*MIRAMOS SI EXISTE EL AÑO LABORAL DE LA FECHA FINAL.*/

     |ABRE #19;
     #19#0 = #0#2;
     |LEE 000#19=;
     |CIERRA #19;
     aAlfa = #19#181;
     |QBF aAlfa;
     aAlfa = ("00" + aAlfa) % -102;
     codPro = aAlfa;

     aAlfa = codPro + cAnoFin;

     #17#0 = aAlfa;
     |LEE 000#17=;

     |SI FSalida ! 0;
          |MENAV "0000NO EXISTE AÑO LABORAL DE LA FECHA FINAL";
          |ERROR;
          |ACABA;
     |FINSI;

     /*MIRAMOS SI EXISTE EL AÑO LABORAL DE LA FECHA INICIAÑ.*/

     aAlfa = codPro + cAnoIni;

     #17#0 = aAlfa;
     |LEE 000#17=;

     |SI FSalida ! 0;
          |MENAV "0000NO EXISTE AÑO LABORAL DE LA FECHA DE INICIO";
          |ERROR;
          |ACABA;
     |FINSI;


     |PARA fFecha = fFecIni; |SI fFecha [ fFecFin; |HACIENDO fFecha = fFecha + 1;
          cFecha = fFecha;
          fDia = fFecha % 1;
          cDia = fDia % 1115;
          |QBF cDia;
          cFecFin = fFecha;
          cAnoIni = cFecFin % 704;
          cAnos = codPro + cAnoIni;
          |HAZ MiraFech;

          |SI nSuma = 0;

               |SI cDia = "Lunes";
                     |SI #17#1 = "N";nSuma = nSuma + 1;|FINSI;
               |FINSI;

               |SI cDia = "Martes";
                     |SI #17#2 = "N";nSuma = nSuma + 1;|FINSI;
               |FINSI;

               |SI cDia = "Miercoles";
                     |SI #17#3 = "N";nSuma = nSuma + 1;|FINSI;
               |FINSI;

               |SI cDia = "Jueves";
                    |SI #17#4 = "N";nSuma = nSuma + 1;|FINSI;
               |FINSI;

               |SI cDia = "Viernes";
                    |SI #17#5 = "N";nSuma = nSuma + 1;|FINSI;
               |FINSI;

               |SI cDia = "Sabado";
                    |SI #17#6 = "N";nSuma = nSuma + 1;|FINSI;
               |FINSI;

               |SI cDia = "Domingo";
                    |SI #17#7 = "N";nSuma = nSuma + 1;|FINSI;
               |FINSI;

          |FINSI;

          nTotDias = nTotDias + nSuma;
          nSuma = 0;
     |SIGUE;

     |CIERRA #17;
     |CIERRA #18;

|FPRC;
-----------------------------------------------------------------------------
|PROCESO MiraFech;
     |SELECT #2#18;
     #18#0 = cAnos;
     #18#2 = fFecha;
     |LEE 000#18=;

     |SI FSalida = 0;
          nSuma = 1;                              ||Es un día festivo.
     |SINO;
          nSuma = 0;                              ||No es festivo.
     |FINSI;

|FPRC;
-----------------------------------------------------------------------------
|PROCESO FechasFasPar;

     |SI #2#8 > 0;
          nDias = (#2#8 / #0#6) ! 0;
     |SINO;
          #2#9  = "01.01.0000";
          #2#10 = "01.01.0000";
          |GRABA 020#2a;
          |ACABA;
     |FINSI;

     fFecIni = fFecIniPar;
     fFecFin = fFecIni + nDias;

     |SI nDias > 0;
          |HAZ DiasFest;
     |FINSI;

     fFecFinPar = fFecFin + nTotDias;

     #2#9  = fFecIniPar;
     #2#10 = fFecFinPar;
     |GRABA 020#2a;
     fFecIniPar = fFecFinPar;

|FPRC;
-------------------------------------------------------------------------------
|DEFBUCLE FechasFasPar;
     #2#1;
     ;                                          ||Campos Extras.
     #1#0, #1#1, #1#3, 1;
     #1#0, #1#1, #1#3, 999;
     ;                                          ||b/e Bolqueo-Ordenacion.
     NULL, FechasFasPar;
|FIN;
-------------------------------------------------------------------------------
|PROCESO FechaFas;
     fFecIniPar = fFecIniCap;
     |BUCLE FechasFasPar;
     |SI fFecFinPar ! "00.00.0000";
          #1#8 = fFecIniCap;
          #1#9 = fFecFinPar;
          fFecIniCap = #1#9;
     |SINO;
          #1#8 = "01.01.0000";
          #1#9 = #1#8;
     |FINSI;
     |GRABA 020#1a;
|FPRC;
-------------------------------------------------------------------------------
|PROCESO CalFecFas; |TIPO 0;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 1;
          fFecIniCap = #0#5;
          |BUCLELIN 2 FechaFas #0;
          #0#7 = #1#9;
          |PINTA #0#7;
     |FINSI;
|FPRC;
-------------------------------------------------------------------------------
|PROCESO Imprimir;
     |PRINT;
|FPRC;
-------------------------------------------------------------------------------
|DEFBUCLE ImpFasPar;
     #2#1;
     ;                                          ||Campos Extras.
     #0#0, #0#1, "                   ",   1;
     #0#0, #0#1, "zzzzzzzzzzzzzzzzzzz", 999;
     ;                                          ||b/e Bolqueo-Ordenacion.
     NULL, Imprimir;
|FIN;
-------------------------------------------------------------------------------
|DEFBUCLE gopre002;
     #4#1;
     ;                                          ||Campos Extras.
     #0#0, #0#1,   1;
     #0#0, #0#1, 999;
     ;                                          ||b/e Bolqueo-Ordenacion.
     NULL, Imprimir;
|FIN;
-------------------------------------------------------------------------------
|DEFBUCLE gofase10;
     #20#1;
     ;                                          ||Campos Extras.
     1;
     999999;
     ;                                          ||b/e Bolqueo-Ordenacion.
     NULL, Imprimir;
|FIN;
-------------------------------------------------------------------------------
|PROCESO Gantt;
|FPRC;
||   Eliminado el Wed Sep 27 09:10:11 CEST 2006
||   00315.0002 Cliente: 003066
/***********************************************
|PROCESO Gantt; |TIPO 20;

     nModo = (FEntrada/100)!0;

     |SI FSalida = "OPCION";
          |HAZ PinFas;
          FSalida = "G.Gantt ";
          |ACABA;
     |FINSI;

     |LEE 000#0=;
     |SI FSalida ! 0;
          |MENAV "0000TIENE QUE SELECCIONAR PRIMERO UNA FICHA";
          |ACABA;
     |FINSI;

     |SUB_EJECUTA_NP "goinform;IMPgofase01";
     |INFOR "gofase01";
     |PRINT;
     |PIEINF;
     |FININF;
     |SUB_EJECUTA_NP "goinform;ENVgofase01";

     |SUB_EJECUTA_NP "goinform;IMPgofase02";
     |INFOR "gofase02";
     |BUCLELIN 2 Imprimir #0;
     |PIEINF;
     |FININF;
     |SUB_EJECUTA_NP "goinform;ENVgofase02";

     |SUB_EJECUTA_NP "goinform;IMPgofase03";
     |INFOR "gofase03";
     |BUCLE ImpFasPar;
     |PIEINF;
     |FININF;
     |SUB_EJECUTA_NP "goinform;ENVgofase03";

     |SUB_EJECUTA_NP "goinform;IMPgopre002";
     |INFOR "gopre002";
     |BUCLE gopre002;
     |PIEINF;
     |FININF;
     |SUB_EJECUTA_NP "goinform;ENVgopre002";

     |SUB_EJECUTA_NP "goinform;IMPgofase10";

     |INFOR "gofase10";
     |HAZ TotalMeses;
     |BUCLE gofase10;
     |PIEINF;
     |FININF;
     |SUB_EJECUTA_NP "goinform;ENVgofase10";

     |SUB_EJECUTA_NP "goinform;RUNFASES";

|FPRC;
******************************************************************/
-------------------------------------------------------------------------------
|PROCESO EntPart; |TIPO 0;
     nMod = (FEntrada/100) ! 0;
     |SI nMod = 1; |ERROR; |ACABA; |FINSI;
     |ENTLINEAL #1#2, -4, 4, 22, 0, cFasParMin, cFasParMax;
     |PTEC 807;
|FPRC;
ÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ
|PROCESO ConsuPres1; |TIPO 0;
     |HAZ ConsuPres;
     |SI enNuPre ! 0;
          #0#0 = eaSerie; |PINTA #0#0;
          #0#1 = enNuPre; |PINTA #0#1;
     |FINSI;
|FPRC;
ÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ
|PROCESO ModiFec2; |TIPO 11;
     |SI FSalida = 13;    ||Modi fecha
          |C_M #1#8, 1, "S"; |C_M #1#9, 1, "S";
          |LEE 101#1=;
          |ENCAMPO #8#1;
          |SI FSalida = 0;
               nNume = #1#7 / #0#6;
               nDias = nNume ! 0;
               |SI nNume ! nDias; |Y nDias < nNume;
                    nDias = nDias + 1;
               |FINSI;
               #1#9 = #1#8 + nDias;
               |ENCAMPO #9#1;
               |SI FSalida = 0;
                    |GRABA 020#1a;
               |FINSI;
          |FINSI;
          |LIBERA #1;
          |LEE 000#1=;
          |PINTA #1#8;
          |PINTA #1#9;
          |C_M #1#8, 1, "N"; |C_M #1#9, 1, "N";
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO ModiFec3; |TIPO 11;
     |SI FSalida = 14;
          |PUSHV 0501 2380;
          |HAZ ModiPerson;
          |POPV;
          |PINTA #2#10; |PINTA #1#9;
          |ERROR;
          |HAZ UltFechaCapi;
     |FINSI;
     |SI FSalida = 13;
          |C_M #2#9, 1, "S"; |C_M #2#10, 1, "S";
          |LEE 101#2=;
          |ENCAMPO #9#2;
          |SI FSalida = 0;
               |ENCAMPO #10#2;
               |SI FSalida = 0;
                    |GRABA 020#2a;
                    |HAZ UltFechaCapi;
               |FINSI;
          |FINSI;
          |LIBERA #2;
          |LEE 000#2=;
          |PINTA #2#9;
          |PINTA #2#10;
          |C_M #2#9, 1, "N"; |C_M #2#10, 1, "N";
          |ERROR;
     |FINSI;
|FPRC;
ÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ
|PROCESO calcula;
     num = num + 1;
     fFechaIni = #1#8;
     fFechaFin = #1#9;
     nDias = fFechaFin - fFechaIni + 1;
     |SELECT #6#4;
     #4#0 = #1#0;
     #4#1 = #1#1;
     #4#12 = #1#3;
     |LEE 000#4=;
     |SI FSalida = 0;
          nImporte = #4#7 / nDias;
     |SINO;
          aAviso = "    Error en lectura de presupuesto.";
          |MENAV aAviso;
     |FINSI;
     |SELECT #1#4;

|PARA fFecha = fFechaIni; |SI fFecha [ fFechaFin; |HACIENDO fFecha = fFecha + 1;

     aFechaIni = fFechaIni;
     aFechaFin = fFechaFin;
     aFecha = fFecha;
     aMesIni = aFechaIni % 402;
     aAnyoIni = aFechaIni % -104;
     aMesFin = aFechaFin % 402;
     aAnyoFin = aFechaFin % -104;
     aMes = aFecha % 402;
     aAnyo = aFecha % -104;
     aClave = aAnyo + aMes;
     nClave = aClave;

     #gofase10#0 = nClave;
     |LEE 000#gofase10.=:
     |SI FSalida = 0;
          #gofase10#1 = #gofase10#1 + nImporte;
          |GRABA 020#gofase10.a;
     |SINO;
          #gofase10#0 = nClave;
          #gofase10#1 = nImporte;
          #gofase10#2 = #0#0;
          #gofase10#3 = #0#1;
          #gofase10#4 = num;
          |GRABA 020#gofase10.n;
     |FINSI;
|SIGUE;
|FPRC;

|DEFBUCLE gofase02_meses;
     #1#1;
     ;                                          ||Campos Extras.
     #0#0, #0#1,   1;
     #0#0, #0#1, 999;
     ;                                          ||b/e Bloqueo-Ordenacion.
     NULL, calcula;
|FIN;

|PROCESO TotalMeses;
     |DELINDEX #20;
     |ABRE #gofase10;
     |ABRE #4;           || gopre002
     |BUCLE gofase02_meses;
     |CIERRA #4;         || gopre002
     |CIERRA #gofase10;
|FPRC;

|PROCESO ModPresup; |TIPO 6;
   |ABRE #gopre001;
   eaTipo = "F"; eaSer = #gofase01#0; enNum = #gofase01#1; |HAZ ModifPres;
   |SI enNum = -1; |ERROR; |ACABA; |FINSI;
   #gofase01#1 = enNum; |LEE 000#gofase01.=; |PINTA #gofase01#1;
   |CIERRA #gopre001;
|FPRC;
ÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ
|PROCESO gopre004;
     #5#0 = #6#0;
     #5#1 = #6#1;
     #5#2 = #6#2;
     #5#4 = #6#3;
     |LEE 020#5=;

     |DDEFECTO #23;
     #23#0 = #6#5;
     |LEE 101#23=;
     |SI FSalida ! 0; |GRABA 020#23b; |FINSI;

     #23#1 = #6#6;
     #23#2 = #23#2 + (#6#8 * #5#7);        ||Horas
     #23#3 = 1;                            ||Personas
     |GRABA 020#23a;
|FPRC;

|DEFBUCLE gopre004;
     #6#1;
     14;
     #1#0, #1#1, #1#3, #2#4, 001, #7#5;
     #1#0, #1#1, #1#3, #2#4, 999, #7#5;
     ;
     NULL, gopre004;
|FIN;

|PROCESO guerw023Defe;
     |DDEFECTO #24;
     #24#0 = #0#0;
     #24#1 = #0#1;
     #24#2 = #1#3;
     #24#3 = #2#4;
     #24#4 = #23#0;
     |LEE 000#24=;
     |SI FSalida = 0;
          #23#3 = #24#5; ||Personas
          |GRABA 020#23a;
     |FINSI;
|FPRC;

|DEFBUCLE guerw023Defe;
     #23#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, guerw023Defe;
|FIN;

|PROCESO Personas; |TIPO 0;
     |SI FSalida = 0; |Y #23Campo = Contenido;
          |ENCAMPO #3#23;
          |SI FSalida = 0;
               |GRABA 020#23a;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Min23;
     #23#0 = "                    ";
|FPRC;

|PROCESO Max23;
      #23#0 = "zzzzzzzzzzzzzzzzzzzz";
|FPRC;

|PROCESO guerw023Gra;
     |DDEFECTO #24;
     #24#0 = #0#0;
     #24#1 = #0#1;
     #24#2 = #1#3;
     #24#3 = #2#4;
     #24#4 = #23#0;
     |LEE 101#24=;
     |SI FSalida ! 0; |GRABA 020#24b; |FINSI;
     #24#5 = #23#3;
     |GRABA 020#24a;
     |LIBERA #24;

     nDias = #23#2 / #0#6 / #23#3;

     |SI nMaxDias < nDias;
          nMaxDias = nDias;
     |FINSI;
|FPRC;

|DEFBUCLE guerw023Gra;
     #23#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, guerw023Gra;
|FIN;

|PROCESO ModiPerson;
     |ABRE #7; |LEE 000#7p; |CIERRA #7;

     aAlfa = Usuario; |QBF aAlfa;
     |NOME_DAT #23 aAlfa;
     |DELINDEX #23;

     |ABRE #23;
     |ABRE #5; |SELECT #2#5;
     |BUCLE gopre004;
     |CIERRA #5;
     |CIERRA #23;

     |ABRE #24;
     |BUCLE guerw023Defe;
     |CIERRA #24;

     |PINPA #0#23;
     |ENTLINEAL #1#23, -1, 4, 22, 0, Min23, Max23;
     |CONFI "0000NCorrecto";
     |SI FSalida = 0;
          nMaxDias = 0;
          |ABRE #24;
          |BUCLE guerw023Gra;
          |CIERRA #24;

          nNume = nMaxDias ! 0;
          |SI nMaxDias > nNume;
               nNume = nNume + 1;
          |FINSI;
          nNume = nNume - 1;

          #2#10 = #2#9 + nNume;
          |GRABA 020#2a;
          |SI #2#10 > #1#9;
               #1#9 = #2#10;
               |GRABA 020#1a;
          |FINSI;
     |FINSI;
     |DELINDEX #23;
|FPRC;
ÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ
|PROCESO gofase03Ref;
     |SI #1#9 < #100#10;
          #1#9 = #100#10;
     |FINSI;
|FPRC;

|DEFBUCLE gofase03Ref;
     #100#1004;
     ;
     #1#0, #1#1, #1#3, 001;
     #1#0, #1#1, #1#3, 999;
     ;
     NULL, gofase03Ref;
|FIN;

|PROCESO UltFechaCapi;
     |DDEF aDef;
     aDef = aDef + "gofase03";
     |CARGA_DEF aDef, gofase03@;
     |SI FSalida = 0;
          #1#9 = "01.01.0000";
          |BUCLE gofase03Ref;
          |DESCARGA_DEF #gofase03@;
          |GRABA 020#1a;
     |FINSI;
|FPRC;

|PROCESO Tipo13; |TIPO 13;
     |ABRE #7; |LEE 000#7p; |CIERRA #7;
     |SI #7#141 = "O";
          |ATRI I; |PINTA 502, "O.E........:"; |ATRI 0;
     |FINSI;
|FPRC;
