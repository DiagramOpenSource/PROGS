|FICHEROS;
     guerm030 #0;
     guerm031 #1;
     gosubcoc #2;
     gosubcap #3;
     gosubpar #4;
     gosubdes #5;
     gomantho #6;
     gopre001 #7;
     gopartia #10;
     gomantl1 #11;
     gotrabaj #12;
     gocatcon #13;
     agifa019 #19;
     agif0041 #41;
     agifa077 #77;
     agifa080 #80;
     gotmp009 #90;
     gosubaux #91;
     agifa112 #112;
     goartive #102;
     guerm030@ #100;
     guerm031@ #101;
     agifa321 #321;
     agifa324 #324;
     goparame #1001;
|FIN;

|VARIABLES;
     aDesArti = "";
     nExiste = 0;
     nModoCab = 0;
     nSw = 0;
     aDestajista = "";
     &enSwGra47 = 0;
     &nDeci_STrab = 0;
     nError = 0;
     nTotal = 0;
     nActa = 0;
     nCanti = 0;
     nGraba = 0;
     nCerti = 0;
     nImp = 0;
     aCapi = "";
     aParti = "";
     &nDeci_TTrab = 0;
     nImporte = 0;
     nHay      = 0;
     aAlfa     = "";
     aInf      = "";
     &efFecha  = @;
     nPreVta   = 0;
     nPreCom   = 0;
     &aDivisa  = "";
     &aMoneda  = "";
     &cCodMonTrab = "";
     &enForma  = 1;
     &eaDocu   = "";
     aDef      = "";
     nNoHayLin = 0;
     nModo     = 0;
     nForma    = 0;
     aDirEmp   = "";
     aDatos    = "";
     nLin      = 0;
     nPPort1   = 0;
     nPPort2   = 0;
     nImpDiv   = 0;
     &nDeci_PreDiv = 0;
|FIN;

|PROCESO MinCap;
     #3#0 = #0#2;
     #3#2 = "                    ";
|FPRC;

|PROCESO MaxCap;
     #3#0 = #0#2;
     #3#2 = "zzzzzzzzzzzzzzzzzzzz";
|FPRC;

|PROCESO MinPar;
     #4#0 = #0#2;
     #4#1 = #1#3
     #4#3 = "                    ";
|FPRC;

|PROCESO MaxPar;
     #4#0 = #0#2;
     #4#1 = #1#3;
     #4#3 = "zzzzzzzzzzzzzzzzzzzz";
|FPRC;

|PROCESO SacaCapi; |TIPO 7;
     |C_M #1#3, 1, "S";
     |ABRE #3;
     |SELECT #2#3;
     #3#0 = #0#2;
     #3#2 = #1#3;
     |LEE 000#3];
     |CONSULTA_F_CLAVE #2#3, MinCap, MaxCap;
     |SI FSalida = 0;
          #1#3 = #3#2;
          |PINTA #1#3;
     |FINSI;
     |CIERRA #3;
     |C_M #1#3, 1, "N";
|FPRC;

|PROCESO SacaParti; |TIPO 7;
     |C_M #1#4, 1, "S";
     |ABRE #4;
     #4#0 = #0#2;
     #4#1 = #1#3;
     #4#2 = #1#13;
     |LEE 000#4=;
     |SI FSalida ! 0;
          |SELECT #3#4;
          #4#0 = #0#2;
          #4#1 = #1#3;
          #4#3 = #1#4;
          |LEE 000#4];
     |FINSI;
     |CONSULTA_F_CLAVE #3#4, MinPar, MaxPar;
     |SI FSalida = 0;
          #1#4 = #4#3;
          #1#6 = #4#13;
          #1#8 = #4#17;
          #1#9 = #4#18;
          #1#13 = #4#2;
          |PINTA #1#4;
          |PINTA #1#6;
          |PINTA #1#8;
          |PINTA #1#9;
     |FINSI;
     |CIERRA #4;
     |C_M #1#4, 1, "N";
|FPRC;

|PROCESO LinExpe;
     |SI #11#3 = #2#4; |Y #11#4 = #2#5;
          nHay = 1;
     |FINSI;
|FPRC;

|PROCESO Pinta; |TIPO 0;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 1;
          |ABRE #2;
          #2#0 = #0#2;
          |LEE 000#2=;
          |CIERRA #2;
          #0#6 = #2#14;
          |PINTA #0#6;

          |ABRE #7;
          #7#0 = #2#4;
          #7#1 = #2#5;
          |LEE 000#7=;
          |SI FSalida ! 0;
               |MENAV "0000No existe Presupuesto...";
               |ERROR; |ACABA;
          |FINSI;

          |SI #7#15 ! #0#0;
               aAlfa = ("000000" + #7#16) % -106;
               aAlfa = #7#15 + "/" + aAlfa;
               aAlfa = "0000La serie no coincide con el Expediente: [" + aAlfa +"]";
               |MENAV aAlfa;
               |ERROR; |ACABA;
          |FINSI;

          |ABRE #6;
          #6#0 = #7#15;
          #6#1 = #7#16;
          |LEE 000#6=;
          |SI FSalida ! 0;
               |MENAV "0000No existe Expediente...";
               |ERROR; |ACABA;
          |FINSI;
          nHay = 0;
          |BUCLELIN 2 LinExpe #6;
          |SI nHay = 0;
               |MENAV "0000Subcontrata incorrecta...";
               |ERROR; |ACABA;
          |FINSI;
          |CIERRA #6;
          |CIERRA #7;
     |FINSI;

     |HAZ Tipo13;

     |ABRE #2;
     #2#0 = #0#2;
     |LEE 000#2=;
     |CIERRA #2;
     ||ÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄControl Cierre
     efFecha = #2#1;
     |HAZ EstaCerrado;
     |SI FSalida = 0;
          |ERROR; |ACABA;
     |FINSI;
     ||ÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ
|FPRC;

|PROCESO Tipo13; |TIPO 13;
     |HAZ Divisas;
     |ABRE #2;
     #2#0 = #0#2;
     |LEE 000#2=;
     |CIERRA #2;
     nDeci_STrab = #2#15;

     |DDEFECTO #2;
     |ABRE #2;
     #2#0 = #0#2;
     |LEE 000#2=;
     |CIERRA #2;

     |PINTA 1019, #2#2;
     |PINTA 1026, #2#3;
     |PINTA 1119, #2#4;
     |PINTA 1125, #2#5;
     |PINTA 1140, #2#1;
     |PINTA 1163, #2#9;
     |PINTA 1219, #2#10;

     |ABRE #1001; |LEE 000#1001p; |CIERRA #1001;
     |SI #1001#141 = "O";
          |PINTA 1102, "O.E............:"; |ATRI 0;
     |FINSI;
|FPRC;

|PROCESO Conta7; |TIPO 7;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 1;
          #77#50 = #0#0;
          |ABRE #77;
          |HAZ ContaAC7;
          |CIERRA #77;
          #0#1 = #77#0;
          |PINTA #0#1;
     |FINSI;
|FPRC;

|PROCESO Conta0; |TIPO 0;
     |SI nModo = 1;
          |HAZ ContaAC0;
          |ABRE #77;
          #77#50 = #0#0;
          #77#0 = #0#1;
          |LEE 000#77=;
          |SI FSalida = 0;
               |MENAV "0000Albaran compras existente...";
               |ERROR;
          |FINSI;
          |CIERRA #77;
     |FINSI;
|FPRC;

|PROCESO Bloquea; |TIPO 7;
     |ABRE #77;
     #77#50 = #0#0;
     #77#0 = #0#1;
     |GRABA 000#77b;
|FPRC;

|PROCESO Libera; |TIPO 12;
     |SI FSalida = 7;
          |SI nModo = 1;
               |BORRA 020#77a;
          |FINSI;
          |CIERRA #77;
     |FINSI;
|FPRC;

|PROCESO CtrlC; |TIPO 11;
     |SI FSalida = 13;   || F5
          |HAZ CargarPendientes;
     |FINSI;
     |SI FSalida = 14;   || F6
          |HAZ CargarCertifi;
     |FINSI;
     |SI FSalida = 15;   || F7
          |HAZ CargarDesta;
     |FINSI;
     |SI FSalida = 7; |O FSalida = 1;
          |DDEF aDef;
          aDef = aDef + "guerm031";
          |CARGA_DEF aDef, guerm031@;
          |SI FSalida = 0;
               |ABRE #101;
               #101#0 = #0#0;
               #101#1 = #0#1;
               #101#2 = 0;
               |LEE 000#101];
               |SI FSalida = 0;
                    nNoHayLin = 0;
                    |SI #101#0 ! #0#0; |O #101#1 ! #0#1;
                         nNoHayLin = 1;
                    |FINSI;
               |SINO;
                     nNoHayLin = 1;
               |FINSI;
               |SI nNoHayLin = 1;
                    |BORRA 020#77a;
                    |CIERRA #77;
               |FINSI;
               |CIERRA #101;
               |DESCARGA_DEF #guerm031@;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Tipo2Cab; |TIPO 2;
     nModo = (FEntrada / 100) ! 0;
     nModoCab = nModo;
     nForma = 0 +. 1;

     |ABRE #2;
     #2#0 = #0#2;
     |LEE 000#2=;
     |CIERRA #2;

     |SI nModo ! 1;
          |ABRE #77;
          #77#50 = #0#0;
          #77#0 = #0#1;
          |LEE 121#77=;
          |SI #77#38 = "S";
               |MENAV "0000Albaran Facturado...";
               |CIERRA #77;
               |ERROR; |ACABA;
          |FINSI;
     |FINSI;
     |SI nForma = 1;
          |HAZ CreaAlba;
          |HAZ LimCabAgifa077;
          |BUCLELIN 0 CalCabAgifa077 #77;
          |GRABA 020#77a
          |CIERRA #77;
     |SINO;
          |ABRE #77;
          |HAZ BorraAlba;
          |CIERRA #77;
     |FINSI;
|FPRC;
ÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ
|PROCESO LeeEmpre;
     |DFICE aDirEmp;
     |DFICB aDatos;
     aDirEmp = aDirEmp % -205;
     |PATH_DAT #41 aDatos;

     |DDEFECTO #41;
     |ABRE #41;
     #41#0 = aDirEmp;
     |LEE 020#41=;
     |CIERRA #41;
|FPRC;

|PROCESO Total80;
     |SI #77#58 = "D";                      || Si mon. de trabajo es divisa ...
          #80#6    = #80#30 / #77#57 * #77#62; || Recalculo precio en mon. base
          #80#32   = #80#6;                  || Precio visual, en mon. base
          nPPort1 = #80#42 / #77#57 * #77#62;
          nPPort2 = #80#42;
     |SINO;                               || Si mon. de trabajo es mon. base
          #80#30   = #80#6 / #77#62 * #77#57; || Recalculo precio en divisa
          #80#32   = #80#30;                || Precio visual, en divisa
          nPPort1 = #80#42;
          nPPort2 = #80#42 / #77#62 * #77#57;
     |FINSI;

     |SI #19#196 = 2;
          #80#7    = #80#36 * #80#6;
          nImpDiv = (#80#36 * #80#30) ! nDeci_PreDiv;
     |SINO;
          #80#7    = #80#5  * #80#6;
          nImpDiv = (#80#5  * #80#30) ! nDeci_PreDiv;
     |FINSI;

     #80#9  = ((#80#7    < #80#8) < #80#26) < #80#45;
     #80#31 = ((nImpDiv < #80#8) < #80#26) < #80#45;

     |SI #19#196 = 2;
          #80#9  = #80#9  + (#80#36 * nPPort1);  || Portes Linea
          #80#31 = #80#31 + (#80#36 * nPPort2);  || Portes Linea
     |SINO;
          #80#9  = #80#9  + (#80#5  * nPPort1);  || Portes Linea
          #80#31 = #80#31 + (#80#5  * nPPort2);  || Portes Linea
     |FINSI;

     |SI #77#58 = "D";     || Si mon. de trabajo es divisa ...
          #80#33 = #80#9;   || Importe visual, en mon. base
     |SINO;               || Si mon. de trabajo es mon. base ...
          #80#33 = #80#31;  || Importe visual, en divisa
     |FINSI;
|FPRC;

|PROCESO GrabaParti;
     |DDEFECTO #10;
     #10#34 = "S";
     #10#4 = #1#0;
     #10#5 = #1#1;
     #10#6 = #1#2;
     #10#7 = #80#1;

     #10#0 = #7#0;
     #10#1 = #7#1;
     #10#2 = #7#15;
     #10#3 = #7#16;
     #10#8 = #1#4;

     |ABRE #4;
     nExiste = 0;
     #4#0 = #0#2;
     #4#1 = #1#3;
     #4#2 = #1#13;
     |LEE 000#4=;
     |SI FSalida ! 0;
          |SELECT #3#4;
          #4#0 = #0#2;
          #4#1 = #1#3;
          #4#3 = #1#4;
          |LEE 000#4];
          |SI FSalida ! 0; |O #4#0 ! #0#2; |O #4#1 ! #1#3; |O #4#3 ! #1#4;
               nExiste = 1;
          |FINSI;
     |FINSI;
     |SI nExiste = 1;
          |DDEFECTO #19;
          |ABRE #19;
          #19#0 = #1#7;
          |LEE 000#19=;
          |CIERRA #19;
          aAlfa = #19#1; |HAZ DesArti;
     |FINSI;
     |SELECT #1#4;
     |DDEFECTO #4;
     #4#0 = #0#2;
     #4#1 = #1#3;
     #4#2 = #1#13;
     |LEE 000#4=;
     |CIERRA #4;

     #10#9 = #4#4;
     #10#10 = #80#5; ||Canti
     #10#15 = #6#2;  ||CLi
     #10#16 = #6#8;
     #10#17 = #6#11;
     #10#18 = "";
     #10#20 = #80#2;
     #10#21 = #0#3;
     #10#28 = #77#1;
     #10#29 = "S"; ||Tipo articulo = S Subcontrata
     #10#30 = #80#4;
     |SI nExiste = 1; #10#30 = aAlfa; |FINSI;
     #10#33 = #1#3;

     #10#37 = nPreVta;
     #10#38 = #1#8;
     #10#39 = #1#9;
     #10#40 = #0#6;
     #10#41 = nPreCom;
     #10#42 = #1#8;
     #10#43 = #1#9;
     #10#44 = #0#6;
     |HAZ CalcuGopartia;
     |GRABA 020#10n;
|FPRC;

|PROCESO gosubdes;
     #7#0 = #2#4;
     #7#1 = #2#5;
     |LEE 020#7=;   ||Presu

     #6#0 = #7#15;
     #6#1 = #7#16;
     |LEE 020#6=;   ||Expe

     |ABRE #19;
     #19#0 = #5#4;
     |LEE 020#19=;  ||Arti
     |CIERRA #19;

     nLin = nLin + 1;
     |DDEFECTO #80;
     #80#27 = #77#50;
     #80#0 = #77#0;
     #80#1 = nLin;
     #80#2 = #5#4;
     #80#3 = #6#11;         ||Almacen
     #80#4 = #5#5;
     #80#5 = #1#5 * #5#14;  ||Uni
     #80#6 = #5#12;         ||Pre
     #80#11 = #19#30;
     #80#13 = #19#2;
     #80#15 = #77#3;
     #80#43 = #19#5;
     #80#8 = #1#8;
     #80#26 = #1#9;
     #80#45 = 0;
     |HAZ Total80;
     |HAZ CalCabAgifa077;
     enForma = 1;
     eaDocu = "agifa080";
     enSwGra47 = 1;
     |HAZ StockEntra;
     |GRABA 020#80n;

     nPreVta = #5#12;
     nPreCom = #5#12;
     |HAZ GrabaParti;

     |SELECT #3#91;
     #91#14 = #1#0;
     #91#15 = #1#1;
     #91#16 = #1#2;
     |LEE 101#91=;
     |SI FSalida = 0;
          #91#10 = #80#27;
          #91#11 = #80#0;
          #91#12 = #80#1;
          |GRABA 020#91a;
          |LIBERA #91;
     |FINSI;
|FPRC;

|DEFBUCLE gosubdes;
     #5#1;
     ;
     #0#2, #1#3, #1#4, 001;
     #0#2, #1#3, #1#4, 999;
     ;
     NULL, gosubdes;
|FIN;

|PROCESO CreaLineas;
     |SI #0#4 = "A";
          |BUCLE gosubdes;
     |SINO;
          #7#0 = #2#4;
          #7#1 = #2#5;
          |LEE 020#7=;   ||Presu

          #6#0 = #7#15;
          #6#1 = #7#16;
          |LEE 020#6=;   ||Expe

          |ABRE #19;
          #19#0 = #1#7;
          |LEE 020#19=;  ||Arti
          |CIERRA #19;

          |DDEFECTO #4;
          |ABRE #4;
          #4#0 = #0#2;
          #4#1 = #1#3;
          #4#2 = #1#13;
          |LEE 000#4=;
          |CIERRA #4;

          nLin = nLin + 1;
          |DDEFECTO #80;
          #80#27 = #77#50;
          #80#0 = #77#0;
          #80#1 = nLin;
          #80#2 = #19#0;
          #80#3 = #6#11;        ||Almacen
          #80#4 = #4#4;
          #80#5 = #1#5;         ||Uni
          #80#6 = #1#6;         ||Pre
          #80#30 = #80#6;       ||Pre
          #80#11 = #19#30;
          #80#13 = #19#2;
          #80#15 = #77#3;
          #80#43 = #19#5;
          #80#37 = #1#4;
          #80#8 = #1#8;
          #80#26 = #1#9;
          #80#45 = 0;
          |HAZ Total80;
          |HAZ CalCabAgifa077;
          enForma = 1;
          eaDocu = "agifa080";
          enSwGra47 = 1;
          |HAZ StockEntra;
          |GRABA 020#80n;

          nPreVta = #1#6;
          nPreCom = #1#6;
          |HAZ GrabaParti;

          |SELECT #3#91;
          #91#14 = #1#0;
          #91#15 = #1#1;
          #91#16 = #1#2;
          |LEE 101#91=;
          |SI FSalida = 0;
               #91#10 = #80#27;
               #91#11 = #80#0;
               #91#12 = #80#1;
               |GRABA 020#91a;
               |LIBERA #91;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO CreaAlba;
     |HAZ LeeEmpre;
     |HAZ Divisas;
     |DDEFECTO #112;
     |ABRE #112;
     #112#0 = #2#2; ||Prv = Contratista
     |LEE 020#112=;
     |CIERRA #112;

     #77#1 = #0#5;
     #77#3 = #112#0;
     #77#4 = #112#1;
     #77#5 = #0#6;
||     #77#5 = #112#24;
     #77#6 = "XX";            || Marca, se usa en el gomaw002
     #77#7 = #112#25;
     #77#32 = 0;

     #77#8 = #112#17;

     #77#29 = #41#1;
     #77#30 = #41#2;
     #77#31 = #41#3;
     #77#33 = #41#4;

     #77#36 = #112#58;
     #77#52 = #112#78;
     #77#54 = ("000000" + #0#2) % -106;
     #77#40 = "S";
     #77#34 = "AS";
     |ABRE #321; |LEE 000#321p; |CIERRA #321;
     |ABRE #324;
     #77#56 = #0#3;            ||Divisa
     #324#0 = #0#3;
     |LEE 020#324=;
     #77#57 = #324#3;     ||Cambio
     #77#58 = "B";             ||Moneda Tra
     #324#0 = #321#9;
     |LEE 020#324=;
     #77#61 = #321#9;     ||Moneda Base
     #77#62 = #324#3;     ||Cambio a EUR
     #77#72 = #112#87;
     nLin = 0;
     |ABRE #80;
     |ABRE #6;
     |ABRE #7;
     |ABRE #10;
     |ABRE #91;
     |BUCLELIN 2 CreaLineas #0;
     |CIERRA #91;
     |CIERRA #10;
     |CIERRA #7;
     |CIERRA #6;
     |CIERRA #80;

     |HAZ EntregaActa;
     |GRABA 020#77a;
     |LIBERA #77;
     |CIERRA #324;
|FPRC;
ÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ
|PROCESO BorraParti;
     |BORRA 020#10a;
|FPRC;

|DEFBUCLE BorraParti;
     #10#1;
     ;
     "S", #1#0, #1#1, 001, #80#1;
     "S", #1#0, #1#1, 999, #80#1;
     be;
     NULL, BorraParti;
|FIN;

|PROCESO BorLin;
     #7#0 = #2#4;
     #7#1 = #2#5;
     |LEE 020#7=;   ||Presu

     |BUCLE BorraParti;

     enForma = -1;
     eaDocu = "agifa080";
     enSwGra47 = 1;
     |HAZ StockEntra;
     |BORRA 020#80a;
|FPRC;

|PROCESO BorraAlba;
     #77#50 = #0#0;
     #77#0 = #0#1;
     |LEE 020#77=;
     |SI FSalida = 0;
          |ABRE #7;
          |BUCLELIN 0 BorLin #77;
          |CIERRA #7;
          |BORRA 020#77a;
     |FINSI;
|FPRC;
ÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ
|PROCESO Divisas;
     cCodMonTrab = #0#3;      ||Moneda de Trabajo.
     |HAZ MonBas;

     aDivisa = #0#3;
     aMoneda = "B";
     |HAZ Divisas077;
|FPRC;

|PROCESO DefPasa; |TIPO 7;
     nModo = (FEntrada /100) ! 0;
     |SI nModo = 1;
          |C_M #0#4, 1, "S";
     |SINO;
          |C_M #0#4, 1, "N";
     |FINSI;
|FPRC;

|PROCESO RecalCab;
     |SI #1#14 = 0;
          |HAZ CalcuTot;
          |GRABA 020#1a;
          |LIBERA #1;
     |FINSI;
     #0#7 = #0#7 + #1#14;
|FPRC;

|PROCESO Tipo3; |TIPO 3;
     #0#7 = 0;
     |BUCLELIN 2 RecalCab #0;
     |PINTA #0#7;
|FPRC;

|PROCESO Tipo30; |TIPO 30;
     |HAZ ChequeaParti;
     |CONFI "0000NImprimir Certificacion";
     |SI FSalida = 0;
          |HAZ Tipo4;
     |FINSI;
|FPRC;

|PROCESO guerm031;
     |SELECT #2#3;
     #3#0 = #0#2;
     #3#2 = #1#3;
     |LEE 000#3=;

     #4#0 = #0#2;
     #4#1 = #1#3;
     #4#2 = #1#13;
     |LEE 000#4=;
     |SI FSalida ! 0;
          |DDEFECTO #19;
          #19#0 = #1#7;
          |LEE 000#19=;
          aAlfa = #19#1; |HAZ DesArti; #19#1 = aAlfa;
          #4#4 = #19#1;
     |FINSI;

     |PRINT;
|FPRC;

|PROCESO Tipo4; |TIPO 4;
     |ABRE #1001; |LEE 000#1001p; |CIERRA #1001;
     aInf = #1001#66; |QBF aInf;
     |IMPRE 0;
     |SI FSalida = 0;
          |INFOR aInf;
          |SI FSalida = 0;
               |ABRE #3;
               |ABRE #4;
               |ABRE #19;
               |BUCLELIN 2 guerm031 #0;
               |CIERRA #19;
               |CIERRA #4;
               |CIERRA #3;
               |PIEINF;
               |FININF;
          |SINO;
               aInf = "0000No existe informe " + aInf;
               |MENAV aInf;
          |FINSI;
          |FINIMP;
     |FINSI;
|FPRC;

|PROCESO Tipo2Linea; |TIPO 2;
     cCodMonTrab = #0#3;      ||Moneda de Trabajo.
     |HAZ MonBas;

     nModo = (FEntrada / 100) ! 0;
     nForma = 0 +. 1;
     |SI nModo = 2; |Y #1#15 = "S";
          |C_M #1#5, 1, "N";
          |C_M #1#6, 1, "N";
          |C_M #1#12, 1, "N";
          |C_M #1#14, 1, "N";
     |SINO;
          |C_M #1#5, 1, "S";
          |C_M #1#6, 1, "S";
          |C_M #1#12, 1, "S";
          |C_M #1#14, 1, "S";
     |FINSI;
     |SI nModo = 3; |Y #1#16 = -1; |Y nModoCab ! 3;
          |SI nForma = -1;
               aAlfa = "0000Linea Seguridad Social, borre la linea anterior";
               |MENAV aAlfa;
          |FINSI;
          |ERROR; |ACABA;
     |FINSI;

     #0#7 = #0#7 +. #1#14;
     |PINTA #0#7;

     |ABRE #4;
     #4#0 = #0#2;
     #4#1 = #1#3;
     #4#2 = #1#13;
     |LEE 101#4=;
     |SI FSalida = 0;
          #4#16 = #4#16 +. #1#5;
          #4#9 = #4#9 +. #1#14;
          |GRABA 020#4a;
     |FINSI;
     |CIERRA #4;

     |SI #1#15 = "S"; |Y nModo = 3;
          |ABRE #91; |SELECT #3#91;
          #91#14 = #1#0;
          #91#15 = #1#1;
          #91#16 = #1#2;
          |LEE 101#91=;
          |SI FSalida = 0;
               #91#6 = "N";
               #91#10 = ""; #91#11 = ""; #91#12 = "";
               #91#14 = ""; #91#15 = ""; #91#16 = "";
               |GRABA 020#91a;
          |FINSI;
          |CIERRA #91;

          |SI nModoCab ! 3;
               nSw = 0;
               |SALVA_FICHA #1;
               #1#2 = #1#16;
               |LEE 101#1=;
               |SI FSalida = 0;
                    #0#7 = #0#7 - #1#14;
                    |PINTA #0#7;
                    nSw = 1;
                    |BORRA 020#1a;
                    |LIBERA #1;
               |FINSI;
               |REPON_FICHA #1;
               |SI nSw = 1;
                    |PONCONTROL 23, "SI";
                    |PTEC 809; |PTEC 808;
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Tipo13Lin; |TIPO 13;
     cCodMonTrab = #0#3;      ||Moneda de Trabajo.
     |HAZ MonBas;

     nExiste = 0;
     |ABRE #4;
     #4#0 = #0#2;
     #4#1 = #1#3;
     #4#2 = #1#13;
     |LEE 000#4=;
     |SI FSalida ! 0;
          |SELECT #3#4;
          #4#0 = #0#2;
          #4#1 = #1#3;
          #4#3 = #1#4;
          |LEE 000#4];
          |SI FSalida ! 0; |O #4#0 ! #0#2; |O #4#1 ! #1#3; |O #4#3 ! #1#4;
               |DDEFECTO #4;
               nExiste = 1;
          |FINSI;
     |FINSI;
     |CIERRA #4;
     |SI nExiste = 0;
          |C_A #4#4, 1, 28;
          |PINTA 1526, #4#4;
     |SINO;
          |DDEFECTO #19;
          |ABRE #19;
          #19#0 = #1#7;
          |LEE 000#19=;
          |CIERRA #19;
          aAlfa = #19#1; |HAZ DesArti; #19#1 = aAlfa;
          |C_A #19#1, 1, 28;
          |PINTA 1526, #19#1;
     |FINSI;
|FPRC;

|PROCESO EntraLin; |TIPO 7;
     aCapi = #1#3;
     aParti = #1#4;
     |ABRE #2;
     #2#0 = #0#2;
     |LEE 020#2=;
     |CIERRA #2;
     |SI #2#21 = "S";
          aAlfa = "<F5>Cargar Pendientes  <F6>Cargar Certificadas  <F7>Carga Costes Destajista";
     |SINO;
          aAlfa = "<F5> Cargar Pendientes            <F6> Cargar Certificadas  ";
     |FINSI;
     |MENSA aAlfa;
     aDestajista = #2#21;
|FPRC;

|PROCESO PintaParti; |TIPO 0;
     nModo = (FEntrada / 100) ! 0;
     |HAZ Tipo13Lin;
     |SI nModo = 1; |O aCapi ! #1#3; |O aParti ! #1#4;
          aCapi = #1#3;
          aParti = #1#4;
          |DDEF aDef;
          aAlfa = aDef + "guerm030";
          |CARGA_DEF aAlfa, guerm030@;
          |SI FSalida = 0;
               aAlfa = aDef + "guerm031";
               |CARGA_DEF aAlfa, guerm031@;
               |SI FSalida = 0;
                    |HAZ DefectoLin;
                    |DESCARGA_DEF #guerm031@;
               |FINSI;
               |DESCARGA_DEF #guerm030@;
          |FINSI;
          |ABRE #1001; |LEE 000#1001p; |CIERRA #1001;
          #1#7 = #1001#89;
     |FINSI;
|FPRC;

|PROCESO BusCertiLin;
     #1#11 = #1#11 + #101#5;
|FPRC;

|DEFBUCLE BusCertiLin;
     #101#1003;
     3, 13;
     #100#0, #100#1, 001, #1#3, #1#13;
     #100#0, #100#1, 999, #1#3, #1#13;
     ;
     NULL, BusCertiLin;
|FIN;

|PROCESO BusCerti;
     |BUCLE BusCertiLin;
|FPRC;

|DEFBUCLE BusCerti;
     #100#2001;
     ;
     #0#2;
     #0#2;
     ;
     NULL, BusCerti;
|FIN;

|PROCESO BusSubcontra;
     #1#10 = #1#10 + #4#14;
|FPRC;

|DEFBUCLE BusSubcontra;
     #4#1;
     ;
     #0#2, #1#3, #1#13;
     #0#2, #1#3, #1#13;
     ;
     NULL, BusSubcontra;
|FIN;

|PROCESO DefectoLin;
     #1#10 = 0;
     #1#11 = 0;
     |BUCLE BusSubcontra;
     |BUCLE BusCerti;

     |PINTA #1#10;
     |PINTA #1#11;
|FPRC;

|PROCESO AOrigen; |TIPO 0;
     |SI Contenido ! #1Campo;
          |SI #1#12 < #1#11;
               |MENAV "0000Cantidad inferior a la certificada.";
          |FINSI;
          #1#5 = #1#12 - #1#11;
          |PINTA #1#5;
     |FINSI;
|FPRC;

|PROCESO ACertificar; |TIPO 0;
     |SI Contenido ! #1Campo;
          #1#12 = #1#5 + #1#11;
          |PINTA #1#12;
     |FINSI;
|FPRC;

|PROCESO CalcuTot; |TIPO 0;
     #1#14 = ((#1#5 * #1#6) < #1#8);
     #1#14 = (#1#14 < #1#9);
     |PINTA #1#14;
|FPRC;

|PROCESO TotalCer; TIPO 0;
     |SI #1Campo ! Contenido;
          |MENAV "0000El importe total no coincide con las unidades a certificar";
          |CONFI "0000NLa diferencia se aplicara sobre el descuento";
          |SI FSalida = 0;
               #1#8 = 100 - #1#14*100/(#1#6*#1#5);
          |SINO;
               nImp = #1#14 * 100 / (100 - #1#9);
               nImp = nImp * 100 / (100 - #1#8);
               #1#5 = nImp / #1#6;
          |FINSI;

          #1#12 = #1#5 + #1#11;
          |PINTA #1#5;
          |PINTA #1#8;
          |PINTA #1#12;
     |FINSI;
|FPRC;

|PROCESO Pendientes;
     nCanti = #4#14 - #4#16;
     |SI nCerti = 0;     || Cargar Todas
          #1#2 = nLin;
          #1#3 = #4#1;
          #1#4 = #4#3;
          #1#5 = nCanti; || A certificar
          #1#6 = #4#13;
          #1#7 = #1001#89;
          #1#8 = #4#17;
          #1#9 = #4#18;
          #1#10 = #4#14; || #4#5  segun tiquet 2743/2109
          #1#11 = #4#16;
          #1#12 = #1#5 + #1#11;
          #1#13 = #4#2
          #1#14 = ((#1#5 * #1#6) < #1#8);
          #1#14 = (#1#14 < #1#9);
          |GRABA 020#1n;
          nGraba = 1;
          nLin = nLin + 1;
          |HAZ Tipo2Linea;
     |SINO;
          |SI nCanti > 0;
               #1#2 = nLin;
               #1#3 = #4#1;
               #1#4 = #4#3;
               #1#5 = nCanti; || A certificar
               #1#6 = #4#13;
               #1#7 = #1001#89;
               #1#8 = #4#17;
               #1#9 = #4#18;
               #1#10 = #4#14; || #4#5  segun tiquet 2743/2109
               #1#11 = #4#16;
               #1#12 = #1#5 + #1#11;
               #1#13 = #4#2
               #1#14 = ((#1#5 * #1#6) < #1#8);
               #1#14 = (#1#14 < #1#9);
               |GRABA 020#1n;
               nGraba = 1;
               nLin = nLin + 1;
               |HAZ Tipo2Linea;
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE Pendientes;
     #4#1;
     ;
     #0#2, "                    ", 001;
     #0#2, "zzzzzzzzzzzzzzzzzzzz", 999;
     ;
     NULL, Pendientes;
|FIN;

|PROCESO CargarPendientes;
     |SI FSalida ! 13; |ACABA; |FINSI;
     |CONFI "0000NCargar tambien las certificadas";
     nCerti = FSalida;

     |HAZ UltLin;

     |DDEFECTO #1;
     #1#0 = #0#0;
     #1#1 = #0#1;
     nGraba = 0;
     |BUCLE Pendientes;
     |SI nGraba = 0;
          |MENAV "0000No hay pendientes";
     |SINO;
          |PONCONTROL 23, "SI";
     |FINSI;
|FPRC;

|PROCESO EntregaActa;
     nTotal = #77#55;
     nActa = 0;
     |SALVA_FICHA #77;
     |ABRE #2;
     #2#0 = #0#2;
     |LEE 101#2=;
     |SI FSalida = 0;
          #77#50 = #2#16;
          #77#0 = #2#17;
          |LEE 101#77=;
          |SI FSalida = 0; |Y #77#74 > 0;
               |SI #77#74 > nTotal;
                    nActa = nTotal;
                    #77#74 = #77#74 - nTotal;
               |SINO;
                    nActa = #77#74;
                    #77#74 = 0;
               |FINSI;
               |GRABA 020#77a;
               |LIBERA #77;
               #2#19 = #77#74;
               |GRABA 020#2a;
          |FINSI;
     |FINSI;
     |CIERRA #2;
     |REPON_FICHA #77;
     #77#74 = nActa;
|FPRC;

|PROCESO Certificadas;
     |SI #4#16 = 0;
          |ACABA;
     |FINSI;
     #1#0 = #0#0;
     #1#1 = #0#1;
     #1#13 = #4#2;
     |LEE 000#1=;
     |SI FSalida = 0; |ACABA; |FINSI;

     #1#2 = nLin;
     #1#3 = #4#1;
     #1#4 = #4#3;
     #1#5 = 0;       || A certificar
     #1#6 = #4#13;
     #1#7 = #1001#89;
     #1#8 = #4#17;
     #1#9 = #4#18;
     #1#10 = #4#14; || #4#5  segun tiquet 2743/2109
     #1#11 = #4#16;
     #1#12 = #1#5 + #1#11;
     #1#13 = #4#2
     #1#14 = ((#1#5 * #1#6) < #1#8);
     #1#14 = (#1#14 < #1#9);
     |GRABA 020#1n;
     nGraba = 1;
     nLin = nLin + 1;
     |HAZ Tipo2Linea;
|FPRC;

|DEFBUCLE Certificadas;
     #4#1;
     ;
     #0#2, "                    ", 001;
     #0#2, "zzzzzzzzzzzzzzzzzzzz", 999;
     ;
     NULL, Certificadas;
|FIN;

|PROCESO UltLin;
     |ABRE #1001; |LEE 000#1001p; |CIERRA #1001;
     #1#2 = 999;
     |LEE 000#1];
     |SI FSalida = 0;
          |LEE 000#1a;
     |SINO;
          |LEE 000#1u;
     |FINSI;
     |SI FSalida = 0; |Y #1#0 = #0#0; |Y #1#1 = #0#1;
          nLin = #1#2 + 1;
     |SINO;
          nLin = 1;
     |FINSI;
|FPRC;

|PROCESO CargarCertifi;
     |HAZ UltLin;

     |DDEFECTO #1;
     #1#0 = #0#0;
     #1#1 = #0#1;
     nGraba = 0;
     |SELECT #2#1;
     |BUCLE Certificadas;
     |SELECT #1#1;
     |SI nGraba = 0;
          |MENAV "0000Todas las partidas estan cargadas";
          |ERROR;
     |SINO;
          |PONCONTROL 23, "SI";
     |FINSI;
|FPRC;

|PROCESO Chequea;
     #1#0 = #0#0;
     #1#1 = #0#1;
     #1#13 = #4#2;
     |LEE 000#1=;
     |SI FSalida ! 0; |Y #4#16 ! 0;
          |MENAV "0000Quedan partidas certificadas sin cargar en la actual certificación";
          |ERROR10;
          nError = 1;
     |FINSI;
|FPRC;

|DEFBUCLE Chequea;
     #4#1;
     ;
     #0#2, "                    ", 001;
     #0#2, "zzzzzzzzzzzzzzzzzzzz", 999;
     ;
     NULL, Chequea;
|FIN;

|PROCESO Min1;
     #1#0 = #0#0;
     #1#1 = #0#1;
     #1#2 = 1;
|FPRC;

|PROCESO Max1;
     #1#0 = #0#0;
     #1#1 = #0#1;
     #1#2 = 999;
|FPRC;

|PROCESO ChequeaParti;
     nError = 0;
     |ABRE #1;
     |SELECT #2#1;
     |BUCLE Chequea;
     |SELECT #1#1;
     |CIERRA #1;
     |SI nError = 1;
          |CONFI "0000SDesea cargar las partidas certificadas no cargadas?";
          |SI FSalida = 0;
               |ABRE #1;
               |HAZ CargarCertifi;
               |CIERRA #1;
               |ENTLINEAL #1#1, 1, 7, 22, 0, Min1, Max1;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO gosubaux;
     |DDEFECTO #12;
     #12#0 = #91#17;
     |LEE 000#12=;
     |DDEFECTO #13;
     #13#0 = #12#3;
     #13#1 = #12#2;
     |LEE 000#13=;

     |DDEFECTO #10;
     #10#34 = #91#0;
     #10#4 = #91#1;
     #10#5 = #91#2;
     #10#6 = #91#3;
     #10#7 = #91#4;
     |LEE 020#10=;

     #1#2 = nLin;
     #1#3 = #10#33;
     #1#4 = #10#8;
     #1#5 = -#10#10; || A certificar
     #1#6 = #91#8;
/*
     |SI #91#13 ! 0;
          #1#6 = #91#8;
     |SINO;
          #1#6 = -#91#8;
     |FINSI;
*/
     #1#7 = #10#20;
     |SI #91#17 ! 0;
          |SI #91#13 = 1; #1#7 = "D.COMPLETA"; |FINSI;
          |SI #91#13 = 2; #1#7 = "D.MEDIA"; |FINSI;
     |FINSI;
     #1#11 = 0;
     #1#12 = #1#5 + #1#11;
     #1#13 = 0;
     #1#14 = ((#1#5 * #1#6) < #1#8);
     #1#14 = (#1#14 < #1#9);
     #1#15 = "S";
     |SI #91#13 = 0; |Y #91#9 ! 0;
          #1#16 = nLin + 1;
     |SINO;
          #1#16 = 0;
     |FINSI;
     |GRABA 020#1n;

     #91#6 = "S";
     #91#14 = #1#0;
     #91#15 = #1#1;
     #91#16 = #1#2;
     |GRABA 020#91a;
     |LIBERA #91;

     nGraba = 1;
     nLin = nLin + 1;
     |HAZ Tipo2Linea;

     |SI #91#13 = 0; |Y #91#9 ! 0;      || Tipo Hora y SS
          #1#2 = nLin;
          #1#3 = #10#33;
          #1#4 = #10#8;
          #1#5 = -1; || A certificar
          #1#6 = #91#9;
          #1#7 = "S.SOCIAL";
          #1#11 = 0;
          #1#12 = #1#5 + #1#11;
          #1#13 = 0;
          #1#14 = ((#1#5 * #1#6) < #1#8);
          #1#14 = (#1#14 < #1#9);
          #1#15 = "S";
          #1#16 = - 1;
          |GRABA 020#1n;
          nLin = nLin + 1;
          |HAZ Tipo2Linea;
     |FINSI;
|FPRC;

|DEFBUCLE gosubaux;
     #91#2;
     6;
     #90#0, #0#2, "N";
     #90#1, #0#2, "N";
     be;
     NULL, gosubaux;
|FIN;

|PROCESO CargarDesta;
     |SI aDestajista = "N"; |ACABA; |FINSI;
     |PUSHV 0501 2380;
     |BLANCO 1426 2055;
     |PINPA #0#90;
     |PINDA #0#90;
     |ENDATOS #1#90;
     |SI FSalida = 0;
          |HAZ UltLin;

          |DDEFECTO #1;
          #1#0 = #0#0;
          #1#1 = #0#1;
          nGraba = 0;
          |ABRE #10;
          |ABRE #12;
          |ABRE #13;
          |BUCLE gosubaux;
          |CIERRA #13;
          |CIERRA #12;
          |CIERRA #10;
          |SI nGraba = 0;
               |MENAV "0000No hay lineas destajista";
               |ERROR;
          |SINO;
               |PONCONTROL 23, "SI";
          |FINSI;
     |SINO;
          |ERROR;
     |FINSI;
     |POPV;
     |PINTA #0#7;
|FPRC;

|PROCESO DesArti;
     |SI #1#7 = "D.COMPLETA          ";
          aAlfa = "DIETA COMPLETA";
     |FINSI;
     |SI #1#7 = "D.MEDIA             ";
          aAlfa = "MEDIA DIETA";
     |FINSI;
     |SI #1#7 = "S.SOCIAL            ";
          aAlfa = "SEGURIDAD SOCIAL TRABAJADOR";
     |FINSI;
|FPRC;
