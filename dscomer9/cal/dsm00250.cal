|FICHEROS;
     dsm00250 #0;
     dsm00251 #1,MANTE;
     agifa019 #19;
     agifa023 #23; ||Cli/Art
     agifa039 #39; ||Cli/Fam
     agifa310 #310;||Cli/Fam/Sub
     agifa060 #60;
     agifa120 #120;
     dsw00062 #62,MANTE;
|FIN;

|VARIABLES;
     aAlfa = "";
     nModo = 0;
     nError = 0;
     nClave = 0;
     aCampo0 = 0;
     aCampo1 = "";
     aCampo2 = "";
     aCampo3 = "";
     aCampo4 = "";
     nSw = 0;
     nTari = 0;
     aCli = "";
     nCli = 0;
|FIN;
컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
|PROCESO DefDestino; |TIPO 7;
     |ABRE #0;
     |LEE 000#0u;
     |CIERRA #0;
     #62#2 = #0#0 + 1;
|FPRC;

|PROCESO CheqOrigen;
     |ABRE #0;
     #0#0 = #62#0;
     |LEE 000#0=;
     |SI FSalida ! 0;
          |MENAV "0000No existe tarifa...";
          |ERROR;
     |SINO;
          #62#1 = #0#1; |PINTA #62#1;
     |FINSI;
     |CIERRA #0;
|FPRC;

|PROCESO CheqDestino;
     |SI FSalida = 2; |ACABA; |FINSI;
     |ABRE #0;
     #0#0 = #62#2;
     |LEE 000#0=;
     |SI FSalida = 0;
          |MENAV "0000Tarifa destino existente...";
          |ERROR;
     |FINSI;
     |CIERRA #0;
|FPRC;
컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
|PROCESO Copia;
     #1#0 = #62#2;
     |GRABA 020#1n;
|FPRC;

|DEFBUCLE Copia;
     #1#1;
     ;
     #62#0, "   ", "     ", "                    ";
     #62#0, "zzz", "zzzzz", "zzzzzzzzzzzzzzzzzzzz";
     ;
     NULL, Copia;
|FIN;

|PROCESO Duplica; |TIPO 20;
     |SI FSalida = "OPCION";
          FSalida = "Duplicar";
     |SINO;
          |PUSHV 0501 2380;
          #62#0 = #0#0;
          #62#1 = #0#1;
          |PINPA #0#62;
          |PINDA #0#62;
          |ENDATOS #1#62;
          |SI FSalida = 0;
               |ABRE #0;
               |ABRE #1;
               #0#0 = #62#2;
               #0#1 = #62#3;
               |GRABA 020#0n;
               |SI FSalida = 0;
                    |BUCLE Copia;
               |FINSI;
               |CIERRA #0;
               |CIERRA #1;
          |FINSI;
          |POPV;
     |FINSI;
|FPRC;

|PROCESO ModiArt; |TIPO 7;
     aCampo1 = #1#1; |QBF aCampo1;
     aCampo2 = #1#2; |QBF aCampo2;
     |C_M #1#7, 1, "S";
     |C_M #1#9, 1, "S";
     |SI aCampo1 ! "";
          #1#3 = "";
          |C_M #1#7, 1, "N";
          |C_M #1#9, 1, "N";
          |PTEC 802;
     |FINSI;
|FPRC;

|PROCESO CheqArti; |TIPO 6;
     aCampo1 = #1#1; |QBF aCampo1;
     aCampo2 = #1#2; |QBF aCampo2;
     aCampo3 = #1#3; |QBF aCampo3;
     |SI aCampo1 = ""; |Y aCampo2 = ""; |Y aCampo3 = "";
          |MENAV "0000Campo obligatgorio...";
          |ERROR; |ACABA;
     |FINSI;

     |C_M #1#3, 0, aAlfa;
     |SI aAlfa = "N"; |ACABA; |FINSI;
/*
     |ABRE #19;
     #19#0 = #1#3;
     |LEE 000#19=;
     |CIERRA #19;
     |SI #1#1 ! #19#2;
          aAlfa = "0000Articulo con familia: " + #19#2;
          |MENAV aAlfa;
          |ERROR;
     |FINSI;
     |SI #1#2 ! #19#3;
          aAlfa = "0000Articulo con subfamilia: " + #19#3;
          |MENAV aAlfa;
          |ERROR;
     |FINSI;
*/
|FPRC;

|PROCESO agifa023;
     |DDEFECTO #19;
     #19#0 = #23#1;
     |LEE 000#19=;

     |DDEFECTO #1;
     #1#0 = #0#0;
     #1#1 = #19#2;   ||Fam
     #1#2 = #19#3;   ||Sub
     #1#3 = #23#1;   ||Art
     #1#4 = #23#32;  ||Dto
     #1#5 = #23#28;  ||D.Fecha
     #1#6 = #23#29;  ||H.Fecha
     #1#7 = #23#2;  ||Pre
     #1#8 = #23#34; ||Comi
     #1#9 = #23#33;||Refe
     |GRABA 020#1n;
|FPRC;

|PROCESO agifa039;
     |DDEFECTO #1;
     #1#0 = #0#0;
     #1#1 = #39#1; ||Fam
     #1#2 = "";    ||Sub
     #1#3 = "";    ||Art
     #1#4 = #39#2; ||Dto
     #1#5 = #39#3; ||D.Fecha
     #1#6 = #39#4; ||H.Fecha
     |GRABA 000#1n;
|FPRC;

|PROCESO agifa310;
     |DDEFECTO #1;
     #1#0 = #0#0;
     #1#1 = #310#1; ||Fam
     #1#2 = #310#2; ||Sub
     #1#3 = "";     ||Art
     #1#4 = #310#3; ||Dto
     #1#5 = #310#4; ||D.Fecha
     #1#6 = #310#5; ||H.Fecha
     |GRABA 000#1n;
|FPRC;

|DEFBUCLE agifa023;
     #23#1;
     ;
     aCli, "                    ";
     aCli, "zzzzzzzzzzzzzzzzzzzz";
     ;
     NULL, agifa023;
|FIN;

|DEFBUCLE agifa039;
     #39#1;
     ;
     aCli, "   ";
     aCli, "zzz";
     ;
     NULL, agifa039;
|FIN;

|DEFBUCLE agifa310;
     #310#1;
     ;
     aCli, "   ", "     ";
     aCli, "zzz", "zzzzz";
     ;
     NULL, agifa310;
|FIN;

|PROCESO CargaArti;
     aAlfa = #19#0; |QBF aAlfa;
     |SI aAlfa = ""; |ACABA; |FINSI;

     |DDEFECTO #1;
     #1#0 = #0#0;
     #1#1 = #19#2;
     #1#2 = #19#3;
     #1#3 = #19#0;
     |GRABA 000#1n;
|FPRC;

|DEFBUCLE CargaArti;
     #19#7;
     3;
     aCampo1, "                   ", aCampo2;
     aCampo1, "zzzzzzzzzzzzzzzzzzz", aCampo2;
     ;
     NULL, CargaArti;
|FIN;

|PROCESO CargaArtiTari;
     aAlfa = #19#0; |QBF aAlfa;
     |SI aAlfa = ""; |ACABA; |FINSI;
     |DDEFECTO #1;
     #1#0 = #0#0;
     #1#3 = #19#0;
     |SI nTari ! 0;
          |SI nTari = 1; #1#7 = #19#18; #1#4 = #19#21; |FINSI;
          |SI nTari = 2; #1#7 = #19#19; #1#4 = #19#22; |FINSI;
          |SI nTari = 3; #1#7 = #19#20; #1#4 = #19#23; |FINSI;
          |SI nTari = 4; #1#7 = #19#147; #1#4 = #19#150; |FINSI;
          |SI nTari = 5; #1#7 = #19#148; #1#4 = #19#151; |FINSI;
          |SI nTari = 6; #1#7 = #19#149; #1#4 = #19#152; |FINSI;
     |FINSI;
     |GRABA 000#1n;
|FPRC;

|DEFBUCLE CargaArtiTari;
     #19#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, CargaArtiTari;
|FIN;

|PROCESO Mensa; |TIPO 7;
     |SI nClave ! 0;
          |MENSA "0000<F5> Cambia clave (modificacion)";
     |SINO;
          |MENSA "0000<F2> Cargar Articulos";
     |FINSI;
|FPRC;

|PROCESO Fam7; |TIPO 7;
     |HAZ TipoLin13;
|FPRC;

|PROCESO Tipo11; |TIPO 11;
     |SI nSw ! 0; |ACABA ;|FINSI;
     |SI FSalida = 13;
          nSw = 1;
          nClave = (nClave + 1) $ 2;
          aCampo0 = #1#0;
          aCampo1 = #1#1;
          aCampo2 = #1#2;
          aCampo3 = #1#3;
          |PTEC 807;
     |FINSI;
     |SI Campo = 1; |Y FSalida = 10; |Y nClave = 0;
          |MENSA "0000Si tarifa = 0 recoge articulos sin precio";
          |PUSHV 0501 2380;
          |BLANCO 1832 2356;
          |CUADRO 1832 2456;
          |CUADRO 2033 2355;
          |ATRI R; |PINTA 1934, " TARIFA A CARGAR     "; |ATRI 0;
          |ATRI I; |PINTA 2136, "Tarifa (0-6):"; |ATRI 0;
          E_inf = 0;
          E_sup = 6;
          nTari = 2150 ? 1;
          |SI FSalida = 0;
               aCampo0 = #1#0;
               aCampo1 = #1#1;
               aCampo2 = #1#2;
               aCampo3 = #1#3;
               nSw = 3;
               |PTEC 807;
          |FINSI;
          |POPV;
          |ERROR;
     |FINSI;
     |SI Campo = 1; |Y FSalida = 11; |Y nClave = 0;
          |MENSA "0000Condiciones del Cliente";
          |PUSHV 0501 2380;
          |BLANCO 1832 2356;
          |CUADRO 1832 2456;
          |CUADRO 2033 2355;
          |ATRI R; |PINTA 1934, " CLIENTE A CARGAR    "; |ATRI 0;
          |ATRI I; |PINTA 2136, "Cliente:"; |ATRI 0;
          E_inf = "";
          E_sup = 6;
          aCli = 2145 ? 1;
          |SI FSalida = 0;
               nCli = aCli;
               aCli = ("000000" + nCli) % -106;
               aCampo0 = #1#0;
               aCampo1 = #1#1;
               aCampo2 = #1#2;
               aCampo3 = #1#3;
               nSw = 4;
               |PTEC 807;
          |FINSI;
          |POPV;
          |ERROR;
     |FINSI;
     |SI Campo = 3; |Y FSalida = 10; |Y nClave = 0;
          |CONFI "0000SCargar Articulos segun familia/subfamilia";
          |SI FSalida = 0;
               aCampo0 = #1#0;
               aCampo1 = #1#1;
               aCampo2 = #1#2;
               aCampo3 = #1#3;
               nSw = 2;
               |PTEC 807;
          |FINSI;
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO TipoLin13; |TIPO 13;
     |HAZ DecimalesBase;
     |ABRE #19;
     #19#0 = #1#3;
     |LEE 000#19=;
     |CIERRA #19;

     |ABRE #60;
     #60#0 = #1#1;
     |LEE 000#60=;
     |CIERRA #60;

     |ABRE #120;
     #120#0 = #1#2;
     |LEE 000#120=;
     |CIERRA #120;

     |PINTA 2024, #19#1;
     |PINTA 2124, #60#1;
     |PINTA 2224, #120#1;
|FPRC;

컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
CONTROL CABECERA/LINEAS
컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
|PROCESO BorraLin;
     |BORRA 020#1a;
|FPRC;

|DEFBUCLE BorraLin;
     #1#1;
     ;
     #0#0, "   ", "     ", "                    ";
     #0#0, "zzz", "zzzzz", "zzzzzzzzzzzzzzzzzzzz";
     ;
     NULL, BorraLin;
|FIN;

|PROCESO Tipo2; |TIPO 2;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 3;
          |BUCLE BorraLin;
     |FINSI;
     ||***|ERROR; nError = 1;   || Para anular la baja de la ficha
|FPRC;

|PROCESO Min;
     #1#0 = #0#0;
     #1#1 = " " * 3;
     #1#2 = " " * 5;
     #1#3 = " " * 20;
/*
     |SI FSalida = "POSICION";
          #1#0 = aCampo0;
          #1#1 = aCampo1;
          #1#2 = aCampo2;
          #1#3 = aCampo3;
     |FINSI;
*/
|FPRC;

|PROCESO Max;
     #1#0 = #0#0;
     #1#1 = "z" * 3;
     #1#2 = "z" * 5;
     #1#3 = "z" * 20;
|FPRC;

|PROCESO Tipo7; |TIPO 7;
     |ENTLINEAL #1#1, 2, 7, 16, 0, Max, Min;
|FPRC;

|PROCESO Tipo12; |TIPO 12;
|FPRC;

|PROCESO Tipo3; |TIPO 3;
     nModo = (FEntrada / 100) ! 0;
     |SI nError ! 0; |ACABA; |FINSI; || Del tipo2

     |HAZ DecimalesBase;
  |ET EntraLin;
     |PARA nSw = 1; |SI nSw ! 0; |HACIENDO;
          nSw = 0;

          |SI nClave = 0;
               |ENTLINEAL #1#1, 2, nModo, 16, 0, Min, Max;
          |SINO;
               |ENTLINEAL #2#1, 2, 4, 16, 0, Min, Max;
          |FINSI;
          |SI nSw = 2;
               |ABRE #1;
               |BUCLE CargaArti;
               |CIERRA #1;
          |FINSI;
          |SI nSw = 3;
               |ABRE #1;
               |BUCLE CargaArtiTari;
               |CIERRA #1;
          |FINSI;
          |SI nSw = 4;
               |ABRE #1;
               |ABRE #19;
               |BUCLE agifa039; ||Cli/Fam
               |BUCLE agifa310; ||Cli/Fam/Sub
               |BUCLE agifa023; ||Cli/Art
               |CIERRA #19;
               |CIERRA #1;
          |FINSI;
     |SIGUE;

     |DDEFECTO #1;
     |ABRE #1;
     #1#0 = #0#0;
     |LEE 101#1];
     |SI FSalida ! 0; |O #1#0 ! #0#0;
          |SI nModo = 1;
               |MENAV "0000Se anulan los datos entrados...";
               |ERROR;
          |FINSI;
     |FINSI;
     |CIERRA #1;
|FPRC;

|PROCESO Tipo13; |TIPO 13;
     |ENTLINEAL #1#1, 2, 7, 16, 0, Min, Max;
     |HAZ TipoLin13;
|FPRC;
컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
