|FICHEROS;
     malpe120 #0;
     malpe121 #1,MANTE;
     malpe122 #2;
     agifa029 #29;
     agifa019 #19;
     malpe130 #130;
|FIN;

|VARIABLES;
     DEBUG = 0;
     &Tempo = "";
     nBoton1 = 0;
     aAlfa = "";
     nForma = 0;
     nModo = 0;
     aTempo1 = "";
     aArt = "";
     aLote = "";
     aHora = "";
     fFecha = @;
     aFic = "";
     aAlma = "";
     aLector = "";
     nError = 0;
     nHandle = 0;
     aDestino = "";
     aDir = "";
    {1}pLocal ="";
|FIN;

|PROCESO Boton1;
     |PTEC 824;  ||F2
|FPRC;

|PROCESO LeeTxt;
     |ABRE #130; |LEE 000#130=; |CIERRA #130;
     aAlma = "";
     aLector = "";
     nError = 0;
     aFic = #130#1; |QBF aFic; aFic = "C:/" + aFic + ".txt";
     |XABRE "CU", aFic, nHandle;
     |SI FSalida ] 0;
          |XLEE nHandle, 250, aAlma;
          |SI FSalida > 0;
               |XLEE nHandle, 250, aLector;
               |SI FSalida [ 0; nError = 1; |FINSI;
          |SINO;
               nError = 1;
          |FINSI;
          |XCIERRA nHandle;
     |SINO;
          nError = 1;
     |FINSI;

     |SI nError ! 0;
          |MENAV "0000ERROR al leer el fichero de configuracion";
     |SINO;
          #0#0 = aAlma;
          #0#2 = aLector;
          |PINDA #0#0;
          |HAZ PintaAlma;
     |FINSI;
     FSalida = nError;
|FPRC;

|PROCESO Tipo2; |TIPO 2;
     nForma = 0 +. 1;
     |DDEFECTO #2;
     #2#0 = #0#0;
     #2#1 = #0#2;
     #2#2 = #1#5;
     #2#3 = #1#6;
     #2#4 = #1#4;
     #2#5 = 1;      || 1 -> Lector
     #2#6 = #1#3;
     #2#7 = "N";
     |SI nForma = 1;
          |GRABA 020#2n;
     |SINO;
          |BORRA 020#2c;
     |FINSI;
|FPRC;

|PROCESO Tipo12; |TIPO 12;
|FPRC;

|PROCESO CodBar11; |TIPO 0;
|FPRC;

|PROCESO CodBar0; |TIPO 0;
     aLote = #1#1 % 105;
     aArt = #1#1 % 608;

     #1#3 = aLote; |PINTA #1#3;
     #1#4 = aArt;

     |ABRE #19;
     #19#0 = #1#4;
     |LEE 000#19=;
     |SI FSalida ! 0; |DDEFECTO #19; |FINSI;
     |CIERRA #19;
     fFecha = ~;
     |HORA aHora;
     #1#2 = #19#1; |PINTA #19#1;
     #1#5 = fFecha;
     #1#6 = aHora;
|FPRC;

|PROCESO CodBar6; |TIPO 6;
     nModo = (FEntrada / 100) ! 0;

     |SI FSalida = 10; || F2
          |ESTADO_CONTROL nBoton1, "DISABLE";
          |ESPECIFICA "RBASS",pLocal;
          aDestino = pLocal1 + "bin/Apaga.bat";
          |SYSTEM aDestino;
          |SLEEP 1;
          |ESTADO_CONTROL nBoton1, "ENABLE";
          |ERROR; |ACABA;
     |FINSI;
     |SI FSalida ! 0; |Y nModo = 1;
          |ERROR;
          |PTEC 807;
     |FINSI;
     |SI FSalida = 0; |Y nModo = 1;
          |PTEC 802;
          aAlfa = #1#1; |QBT aAlfa;
          aAlfa = aAlfa > "";
          |SI aAlfa = "NOREAD";
                || aqui se hara el system para ejecutar el bat

                |ESPECIFICA "RBASS",pLocal;
                aDestino = pLocal1 + "bin/x10commEnciende.bat";
               |SYSTEM aDestino;
               /* |SLEEP 15;
                aDestino = pLocal1 + "bin/x10commApaga.bat";
                |SYSTEM aDestino;*/

               |PTEC 802;
          |FINSI;
     |FINSI;
     aAlfa = "FSalida = " + FSalida + "             ";
     ||PINTA 526, aAlfa;
|FPRC;

|PROCESO PintaAlma; |TIPO 0;
     |ABRE #29;
     #29#0 = #0#0;
     |LEE 000#29=;
     |CIERRA #29;
     #0#1 = #29#1; |PINTA #0#1;
|FPRC;

|PROCESO Min;
     #1#0 = 0001;
|FPRC;

|PROCESO Max;
     #1#0 = 9999;
|FPRC;

|PROGRAMA;
     |HAZ CreaTempo; aTempo1 = Tempo;
     |NOME_DAT #1 aTempo1;
     |DELINDEX #1;

     |CLS;
     |PINPA #0#0;
     |PINDA #0#0;
     |ENTLINEAL #1#1, 1, 7, 22, 0, Min, Max;
     |CONTROL_SIMPLE 0, "BOTON,F2. - Desconectar Alarma", 0555, 0577, Boton1;
     nBoton1 = FSalida;
     |SI DEBUG = 1;      || para pruebas :)
         |ENDATOS #1#0;
     |SINO;
          |HAZ LeeTxt;
     |FINSI;
     |SI FSalida = 0;
          |ESTADO_CONTROL nBoton1, "SHOW";
          |ABRE #2;
          |PTEC 802;
          |ENTLINEAL #1#1, 1, 1, 22, 0, Min, Max;
          |CIERRA #2;
     |FINSI;
     |DELINDEX #1; Tempo = aTempo1; |HAZ BoraTempo;

     |FIN_CONTROL_WINDOWS nBoton1;
     |PULSATECLA;
|FPRO;
