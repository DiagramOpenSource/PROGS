|FICHEROS;
     guillz04 #0;
     agifa010 #10;
     agifa019 #19;
     agifa024 #24;
     agifa071 #71;
|FIN;

|VARIABLES;
     nAbono = 0;
     nNu1 = 0;
     nNu2 = 0;
     aAlf1 = "";
     nPos = 0;
     aTipo = "";
     nLon = 0;
     nDec = 0;
     nNume = 0;
     aDato = "";
     aAlfa = "";
     nId = 0;
     nBtn = 0;
     nPanta = 0;
     aPath = "";
     aFic = "";
     aDFec = "";
     aHFec = "";
     aOrg = "";
     nHandle = 0;
     nHay = 0;
     aLon = "";
     i = 0;
     aCar = "";
     nPrecio = 0;
     nTotal = 0;
     aNuevo = "";
|FIN;

|INCLUYE i_varart;

|PROCESO QuitaRaros0;
     aNuevo = " ";
     |SI aCar = "¥"; aNuevo = &209; |FINSI;
     |SI aCar = "¤"; aNuevo = &241; |FINSI;
     |SI aCar = " "; aNuevo = &225; |FINSI;
     |SI aCar = "‚"; aNuevo = &233; |FINSI;
     |SI aCar = "¡"; aNuevo = &237; |FINSI;
     |SI aCar = "¢"; aNuevo = &243; |FINSI;
     |SI aCar = "£"; aNuevo = &250; |FINSI;
     |SI aCar = "§"; aNuevo = &176; |FINSI;
     |SI aCar = "¦"; aNuevo = &170; |FINSI;
     |SI aCar = "°"; aNuevo = &176; |FINSI;  ||Euro
     aCar = aNuevo;
|FPRC;

|PROCESO QuitaRaros;
     aDato = "";
     aLon = aAlfa % A0;
     |PARA i = 1; |SI i [ aLon; |HACIENDO i = i + 1;
          nPos = i * 100 + 1;
          aCar = aAlfa % nPos;
          |SI aCar ] "0"; |Y aCar [ "9";
               aDato = aDato + aCar;
          |SINO;
               |SI aCar ] "A"; |Y aCar [ "Z";
                    aDato = aDato + aCar;
               |SINO;
                    |SI aCar > "a"; |Y aCar < "z";
                         aDato = aDato + aCar;
                    |SINO;
                         |HAZ QuitaRaros0;
                         aDato = aDato + aCar;
                    |FINSI;
               |FINSI;
          |FINSI;
     |SIGUE;
     aAlfa = aDato;
|FPRC;

|PROCESO Linea;
     aTipo = "A"; nLon = 10; aAlfa = #10#52; |QBF aAlfa; aAlfa = aAlfa + (("000000"+#10#0)%-106);
     |HAZ Relleno;
     aDato = "<numeroAlbaran>" + aAlfa + "</numeroAlbaran>";
     |XGRABA nHandle, aDato;

     |SI #19#3 = "ENVAS"; aAlfa = "E"; |SINO; aAlfa = "V"; |FINSI;
     |HAZ Relleno;
     aDato = "<tipoLinea>" + aAlfa + "</tipoLinea>";
     |XGRABA nHandle, aDato;

     aTipo = "A"; nLon = 4; aAlfa = #19#111;
     |HAZ Relleno;
     aDato = "<codigoProductoHR>" + aAlfa + "</codigoProductoHR>";
     |XGRABA nHandle, aDato;

     aTipo = "A"; nLon = 20; aAlfa = #71#4;
     |HAZ QuitaRaros;
     |HAZ Relleno;
     aDato = "<nombreProductoHR>" + aAlfa + "</nombreProductoHR>";
     |XGRABA nHandle, aDato;

     aAlfa = "";
     aDato = "<codigoProductoHRCruzado>" + aAlfa + "</codigoProductoHRCruzado>";
     |XGRABA nHandle, aDato;

     |SI #19#3 = "ENVAS";
          nNume = -#71#5;
     |SINO;
          nNume = #71#5;
     |FINSI;
     aTipo = "N"; nLon = 8; nDec = 0; aAlfa = nNume;
     |HAZ Relleno;
     aDato = "<unidades>" + aAlfa + "</unidades>";
     |XGRABA nHandle, aDato;

     |SI #19#3 = "ENVAS";
          nNume = -1 * (#19#19 * #71#5);
     |SINO;
          nNume = nPrecio * #71#5;
     |FINSI;
     aTipo = "N"; nLon = 10; nDec = 2; aAlfa = nNume;
     |HAZ Relleno;
     aDato = "<importeBruto>" + aAlfa + "</importeBruto>";
     |XGRABA nHandle, aDato;

     |SI #19#3 = "ENVAS"; |O #71#62 > 2; || envase o con promocion
          nNume = 0;
     |SINO;
          |SI #71#62 < 0;     ||linea promocion
               nNume = nPrecio * #71#5;
          |SINO;
               nTotal = nPrecio * #71#5;
               nNume = nTotal - #71#9;
          |FINSI;
     |FINSI;
     aTipo = "N"; nLon = 10; nDec = 2; aAlfa = nNume;
     |HAZ Relleno;
     aDato = "<descuento>" + aAlfa + "</descuento>";
     |XGRABA nHandle, aDato;

     nNume = enIva;
     aTipo = "N"; nLon = 5; nDec = 2; aAlfa = nNume;
     |HAZ Relleno;
     aDato = "<iva>" + aAlfa + "</iva>";
     |XGRABA nHandle, aDato;

     |SI #24#47 = "S"; nNume = enRec; |SINO; nNume = 0; |FINSI;
     aTipo = "N"; nLon = 5; nDec = 2; aAlfa = nNume;
     |HAZ Relleno;
     aDato = "<recargoEquivalencia>" + aAlfa + "</recargoEquivalencia>";
     |XGRABA nHandle, aDato;
|FPRC;

|PROCESO LinXml;
     |SI #71#13 < #0#6; |O #71#13 > #0#7; |ACABA; |FINSI;

     #19#0 = #71#2;
     |LEE 000#19=;
     |SI FSalida ! 0; |DDEFECTO #19; |FINSI;

     |SI #24#39 = 1; nPrecio = #19#18; |FINSI;
     |SI #24#39 = 2; nPrecio = #19#19; |FINSI;
     |SI #24#39 = 3; nPrecio = #19#20; |FINSI;
     |SI #24#39 = 4; nPrecio = #19#147; |FINSI;
     |SI #24#39 = 5; nPrecio = #19#148; |FINSI;
     |SI #24#39 = 6; nPrecio = #19#149; |FINSI;

     enTiva = #71#11;
     efFiva = #10#1;
     |HAZ SacaIVA;

     aAlfa = "<detalle>";
     |XGRABA nHandle, aAlfa;

     |SI #19#3 = "ENVAS"; |Y #71#5 ] 0;
          || envases solo las negativas
     |SINO;
          |HAZ Linea;
     |FINSI;

     aAlfa = "</detalle>";
     |XGRABA nHandle, aAlfa;
|FPRC;


|PROCESO CabXml;
     #24#0 = #10#3;
     |LEE 000#24=;
     |SI FSalida ! 0; |DDEFECTO #24; |FINSI;

     aTipo = "A"; nLon = 10; aAlfa = #10#52; |QBF aAlfa; aAlfa = aAlfa + (("000000"+#10#0)%-106);
     |HAZ Relleno;
     aDato = "<numero>" + aAlfa + "</numero>";
     |XGRABA nHandle, aDato;

     aTipo = "F"; aAlfa = #10#1;
     |HAZ Relleno;
     aDato = "<fecha>" + aAlfa + "</fecha>";
     |XGRABA nHandle, aDato;

     aTipo = "N"; nLon = 12; nDec = 0; aAlfa = #0#1;
     |HAZ Relleno;
     aDato = "<distribuidor>" + aAlfa + "</distribuidor>";
     |XGRABA nHandle, aDato;

     aTipo = "A"; nLon = 12; aAlfa = #10#3;
     |HAZ Relleno;
     aDato = "<clienteExterno>" + aAlfa + "</clienteExterno>";
     |XGRABA nHandle, aDato;

     aTipo = "A"; nLon = 50; aAlfa = #10#29;
     |HAZ QuitaRaros;
     |HAZ Relleno;
     aDato = "<nombreClienteExterno>" + aAlfa + "</nombreClienteExterno>";
     |XGRABA nHandle, aDato;

     aTipo = "A"; nLon = 12; aAlfa = #24#15;
     |HAZ Relleno;
     aDato = "<cifClienteExterno>" + aAlfa + "</cifClienteExterno>";
     |XGRABA nHandle, aDato;

     aTipo = "A"; nLon = 40; aAlfa = #10#30;
     |HAZ QuitaRaros;
     |HAZ Relleno;
     aDato = "<direccionClienteExterno>" + aAlfa + "</direccionClienteExterno>";
     |XGRABA nHandle, aDato;

     aTipo = "A"; nLon = 20; aAlfa = #10#31;
     |HAZ QuitaRaros;
     |HAZ Relleno;
     aDato = "<poblacionClienteExterno>" + aAlfa + "</poblacionClienteExterno>";
     |XGRABA nHandle, aDato;

     aTipo = "A"; nLon = 20; aAlfa = #10#33;
     |HAZ QuitaRaros;
     |HAZ Relleno;
     aDato = "<provinciaClienteExterno>" + aAlfa + "</provinciaClienteExterno>";
     |XGRABA nHandle, aDato;

     aTipo = "A"; nLon = 5; aAlfa = #10#62;
     |HAZ Relleno;
     aDato = "<cpClienteExterno>" + aAlfa + "</cpClienteExterno>";
     |XGRABA nHandle, aDato;

     aTipo = "A"; nLon = 15; aAlfa = #24#14;
     |HAZ QuitaRaros;
     |HAZ Relleno;
     aDato = "<paisClienteExterno>" + aAlfa + "</paisClienteExterno>";
     |XGRABA nHandle, aDato;

     aTipo = "A"; nLon = 50; aAlfa = #24#1;
     |HAZ QuitaRaros;
     |HAZ Relleno;
     aDato = "<nombreFiscalClienteExterno>" + aAlfa + "</nombreFiscalClienteExterno>";
     |XGRABA nHandle, aDato;

     aTipo = "A"; nLon = 40; aAlfa = #24#8;
     |HAZ QuitaRaros;
     |HAZ Relleno;
     aDato = "<direccionFiscalClienteExterno>" + aAlfa + "</direccionFiscalClienteExterno>";
     |XGRABA nHandle, aDato;

     aTipo = "A"; nLon = 20; aAlfa = #24#9;
     |HAZ QuitaRaros;
     |HAZ Relleno;
     aDato = "<poblacionFiscalClienteExterno>" + aAlfa + "</poblacionFiscalClienteExterno>";
     |XGRABA nHandle, aDato;

     aTipo = "A"; nLon = 20; aAlfa = #24#10;
     |HAZ QuitaRaros;
     |HAZ Relleno;
     aDato = "<provinciaFiscalClienteExterno>" + aAlfa + "</provinciaFiscalClienteExterno>";
     |XGRABA nHandle, aDato;

     aTipo = "A"; nLon = 5; aAlfa = #24#183;
     |HAZ Relleno;
     aDato = "<cpFiscalClienteExterno>" + aAlfa + "</cpFiscalClienteExterno>";
     |XGRABA nHandle, aDato;

     aTipo = "A"; nLon = 15; aAlfa = #24#14;
     |HAZ QuitaRaros;
     |HAZ Relleno;
     aDato = "<paisFiscalClienteExterno>" + aAlfa + "</paisFiscalClienteExterno>";
     |XGRABA nHandle, aDato;

     aAlfa = "";
     aDato = "<contacto>" + aAlfa + "</contacto>";
     |XGRABA nHandle, aDato;

     aTipo = "A"; nLon = 10; aAlfa = #24#17;
     |HAZ Relleno;
     aDato = "<telefono>" + aAlfa + "</telefono>";
     |XGRABA nHandle, aDato;

     |SI #24#47 = "S"; aAlfa = 1; |SINO; aAlfa = 0; |FINSI;
     aDato = "<recargoEquivalencia>" + aAlfa + "</recargoEquivalencia>";
     |XGRABA nHandle, aDato;

     |SI #24#28 = "S"; aAlfa = 0; |SINO; aAlfa = 1; |FINSI;
     aDato = "<iva>" + aAlfa + "</iva>";
     |XGRABA nHandle, aDato;

     aAlfa = "";
     aDato = "<numeroPedido>" + aAlfa + "</numeroPedido>";
     |XGRABA nHandle, aDato;

     aAlfa = "";
     aDato = "<clienteExternoContado>" + aAlfa + "</clienteExternoContado>";
     |XGRABA nHandle, aDato;

     aAlfa = "";
     aDato = "<comentario>" + aAlfa + "</comentario>";
     |XGRABA nHandle, aDato;
|FPRC;

|PROCESO CheqLin;
     |SI #71#13 ] #0#6; |Y #71#13 [ #0#7;
          nHay = 1; |ERROR10;
     |FINSI;
|FPRC;

|PROCESO AlbaVentas;
     nHay = 0;
     |BUCLELIN 2CheqLin#10;
     |SI nHay = 0;
          |ERROR;
     |SINO;
          aAlfa = "<venta>";
          |XGRABA nHandle, aAlfa;
          aAlfa = "<cabecera>";
          |XGRABA nHandle, aAlfa;
          |HAZ CabXml;
          aAlfa = "</cabecera>";
          |XGRABA nHandle, aAlfa;

          |BUCLELIN 2LinXml#10;
          aAlfa = "</venta>";
          |XGRABA nHandle, aAlfa;
     |FINSI;
|FPRC;

|DEFBUCLE AlbaVentas;
     #10#7;
     ;
     #0#4, "     ", 000001;
     #0#5, "zzzzz", 999999;
     ;
     NULL, AlbaVentas;
|FIN;

|PROCESO T3; |TIPO 3;
     aPath = #0#3; |QBF aPath;
     aAlfa = aPath % -101;
     |SI aAlfa ! "/"; |Y aAlfa ! "\";
          aPath = aPath + "/";
     |FINSI;
     aDFec = (#0#4 % 704) + (#0#4 % 402) + (#0#4 % 102);
     aHFec = (#0#5 % 704) + (#0#5 % 402) + (#0#5 % 102);
     aFic = aPath + "ventas_" + (("000000000000" + #0#1)%-112) + "_" + aDFec + "_" + aHFec + "_" + (("000"+#0#2)%-103) + ".xml";
     |XABRE "CE", aFic, 0;
     |SI FSalida ] 0;
          |CONFI "0000NEl fichero existe. ¿Sobreescribir?";
          |SI FSalida ! 0; |ACABA; |FINSI;
     |FINSI;

     |DFICE aOrg;
     aOrg = aOrg + Usuario;
     |XABRE "W", aOrg, nHandle;
     |SI FSalida ] 0;
          aAlfa = "<?xml version='1.0' encoding='utf-8'?>";
          |XGRABA nHandle, aAlfa;
          aAlfa = "<listaVentas>";
          |XGRABA nHandle, aAlfa;

          |ABRE #24;
          |ABRE #19;
          |BUCLE AlbaVentas;
          |CIERRA #19;
          |CIERRA #24;

          aAlfa = "</listaVentas>";
          |XGRABA nHandle, aAlfa;
          |XCIERRA nHandle;

          |ENVIA_FICHERO aOrg, aFic;
     |FINSI;
|FPRC;

|PROCESO Relleno;
     |SI aTipo = "F";
          aAlfa = (aAlfa % 102) +"/"+ (aAlfa % 402) +"/"+ (aAlfa % 704);
     |FINSI;

     |SI aTipo = "A";
          eaAlfa = aAlfa; |HAZ Ansi2Utf8; aAlfa = eaAlfa;
          aAlf1 = " " * nLon;
          aAlfa = aAlfa + aAlf1;
          nPos = nLon + 100;
          aAlfa = aAlfa % nPos;
          |QBF aAlfa;
     |FINSI;

     |SI aTipo = "N";
          nNume = aAlfa;
          |SI nNume < 0; nAbono = -1; |SINO; nAbono = 1; |FINSI;
          |SI nNume < 0; nNume = nNume * - 1; |FINSI;
          nNu1 = nNume ! 0;
          |SI nNu1 > nNume; nNu1 = nNu1 - 1; |FINSI;
          nNu2 = nNume - nNu1;
          |SI nDec < 0;
               aAlfa = nNume;
          |SINO;
               |SI nDec = 0;
                    aAlfa = nNu1;
               |SINO;
                    aAlfa = nNu2;
                    aAlfa = aAlfa + "00000000000000000000000000";
                    nPos = nDec + 300;
                    aAlfa = aAlfa % nPos;
                    aAlfa = nNu1 + "," + aAlfa;  || con decimal
                    ||aAlfa = nNu1 + aAlfa;          || sin decimal
               |FINSI;
          |FINSI;
          aAlfa = "000000000000000000000000000000000000000000000" + aAlfa;
          ||aAlfa = "                                             " + aAlfa;
          |SI nAbono = 1;
               nPos = -1 * (nLon + 100);
               aAlfa = aAlfa % nPos;
          |SINO;
               nPos = -1 * (nLon + 99);
               aAlfa = "-" + aAlfa % nPos;
          |FINSI;
          nDec = 0;
          |QBT aAlfa;
     |FINSI;
|FPRC;

|PROCESO BtnCarpeta;
     aAlfa = "D" + #0#3; |QBF aAlfa;
     |BROWSE aAlfa;

     aAlfa = aAlfa % 2; |QBF aAlfa;
     |SI aAlfa = "D"; aAlfa = ""; |FINSI;

     |QBF aAlfa;
     |SI aAlfa = "";
          |ERROR;
     |SINO;
          #0#3 = aAlfa;
          |PINTA #0#3;

          aAlfa = "|C003WID";
          aAlfa = #0#aAlfa#;
          nId = aAlfa;
          |FOCOTECLADO nId;
     |FINSI;
|FPRC;

|PROCESO Path0; |TIPO 0;
     |SI FSalida = 13;
          |HAZ BtnCarpeta;
     |FINSI;
|FPRC;

|PROCESO Path6; |TIPO 6;
     |ESTADO_CONTROL nBtn, "DISABLE";
|FPRC;

|PROCESO Path7; |TIPO 7;
     |ESTADO_CONTROL nBtn, "ENABLE";
|FPRC;

|PROCESO Ptec; |TIPO 0;
     |SI FSalida = 0;
          |PTEC 806;
     |FINSI;
|FPRC;

|PROGRAMA;
     |ABRE #0;
     |LEE 101#0p;
     |SI FSalida ! 0; |GRABA 020#0b; |FINSI;
     |PINPA #0#0;
     |PINDA #0#0; nPanta = FSalida;
     |CONTROL_SIMPLE nPanta, "BOTON,...", 1384, 1387, BtnCarpeta;
     nBtn = FSalida;
     |ESTADO_CONTROL nBtn, "DISABLE";
     |ENDATOS #1#0;
     |SI FSalida = 0; |GRABA 020#0a; |FINSI;
     |CIERRA #0;
|FPRO;
