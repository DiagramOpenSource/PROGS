|FICHEROS;
     malpe500 #0;
     malpe090 #1;
     malpe091 #2;
     dsm00024 #3;
     malpe501 #4;
     malpe502 #5;
     malpe001 #10; || obras
     malpe003 #11; || importe por tonelada
     malpe004 #30; || ordenes de carga manuales
     malpe085 #85;
     malpe109 #109;
     malpe200 #200;
     malpe201 #201;
|FIN;

|VARIABLES;
     peso   = 0;
     precio = 0;
     nLin = 0;
|FIN;

|PROCESO cabecera;
#4#0 = #1#98;|| matricula
#4#1 = #1#100;|| transportista
#4#2 = #2#3;|| almacen
#4#3 = #0#0;||Desde Fecha
|LEE 001#4=;
|SI FSalida = 0;
     #4#6 = #4#6 + 1;
     |GRABA 001#4a;
|SINO;
     #4#4 = #0#1;
     #4#6 = 1;
     |GRABA 001#4n;
|FINSI;
|LIBERA #4;
|FPRC;


|PROCESO mira_peso;
     #3#0 = #2#2;
     #3#1 = #2#3;
     #3#2 = #2#49;
     |LEE 000#3=;
     |SI FSalida ! 0 ; |DDEFECTO #3; |FINSI;

     #85#0 = #3#0;
     #85#1 = #3#1;
     #85#2 = #3#2;
     |LEE 000#85=;
     |SI FSalida ! 0; |DDEFECTO #85; |FINSI;
     peso = #85#3;;
|FPRC;

|PROCESO mira_valor;
     #10#0 = #1#3;  ||Cli
     #10#1 = #1#84; ||Obra
     |LEE 000#10=;
     |SI FSalida ! 0 ; |DDEFECTO #10; |FINSI;
     #11#0 = #10#22; || Tipo Trans
     #11#3 = #1#1;   || Fecha Alba
     |LEE 001#11];
     |SI FSalida ! 0;
          |LEE 000#11u;
     |SINO;
          |LEE 000#11a;
     |FINSI;
     |SI FSalida ! 0 ; |O #11#0 ! #10#22;
          |DDEFECTO #11;
     |FINSI;
     precio = #11#2;
     |LIBERA #10;
     |LIBERA #11;
|FPRC;

|PROCESO acumula_a;
     |SI #2#66 ! "R"; ||SI es una rotura, ni suma ni resta
          |HAZ cabecera; || miro que linea tengo que guardar
          |HAZ mira_peso; || miro lo que pesa un ladrillo
          |HAZ mira_valor;|| miro el valor por tonelada
          |DDEFECTO #5;
          #5#0 = #4#0;
          #5#1 = #4#1;
          #5#2 = #4#2;
          #5#3 = #4#3;
          #5#4 = #4#6;
          #5#5 = #1#1;
          #5#6 = #1#52;
          #5#7 = #1#0;
          #5#8 = "A";
          #5#9 = #2#1;
          #5#10 = #2#2;
          #5#11 = #2#5;
          #5#13 = precio;
          #5#15 = #2#5 * peso;
          #5#12 = #5#15/1000;
          #5#14 = #5#12 * #5#13;
          |GRABA 021#5n;
          |LIBERA #5;
     |FINSI;
|FPRC;

|DEFBUCLE acum_pedi;
     #1#4;
     64;
     "     ", #0#0, 000001, "P";
     "zzzzz", #0#1, 999999, "P";
     ;
     NULL , NULL, acumula_a;
|FIN;

|PROCESO Precio200;
     #11#0 = #200#13; || Tipo Trans
     #11#3 = #200#7;   || Fecha
     |LEE 001#11];
     |SI FSalida ! 0;
          |LEE 000#11u;
     |SINO;
          |LEE 000#11a;
     |FINSI;
     |SI FSalida ! 0 ; |O #11#0 ! #200#13;
          |DDEFECTO #11;
     |FINSI;
     precio = #11#2;
|FPRC;

|PROCESO malpe201;
     #4#0 = #200#11; ||Matri
     #4#1 = #200#12; ||Trans
     #4#2 = #201#7;  ||Alma
     #4#3 = #0#0; ||Desde F.
     |LEE 101#4=;
     |SI FSalida = 0;
          #4#6 = #4#6 + 1;
     |SINO;
          #4#4 = #0#1;
          #4#6 = 1;
          |GRABA 020#4b;
     |FINSI;
     |GRABA 020#4a;
     |LIBERA #4;

     #5#0 = #4#0;
     #5#1 = #4#1;
     #5#2 = #4#2;
     #5#3 = #4#3;
     #5#4 = 9999;
     |LEE 000#5];
     |SI FSalida = 0;
          |LEE 000#5a;
     |SINO;
          |LEE 000#5u;
     |FINSI;
     |SI FSalida = 0; |Y #5#0 = #4#0; |Y #5#1 = #4#1; |Y #5#2 = #4#2;
               |Y #5#3 = #4#3;
          nLin = #5#4 + 1;
     |SINO;
          nLin = 1;
     |FINSI;
     |DDEFECTO #5;
     #5#0 = #4#0;
     #5#1 = #4#1;
     #5#2 = #4#2;
     #5#3 = #4#3;
     #5#4 = nLin;
     #5#5 = #200#7;
     #5#6 = "";
     #5#7 = #200#0;
     #5#8 = "T";
     #5#9 = #201#0;
     #5#10 = #201#2;

     |DDEFECTO #85;
     #85#0 = #201#2;
     #85#1 = #201#7;
     #85#2 = #201#9;
     |LEE 000#85=;
     peso = #85#3;
     |HAZ Precio200;

     #5#11 = #201#4;           ||Unidades
     #5#13 = precio;           ||Precio
     #5#15 = #5#11 * peso;     ||Peso Ki
     #5#12 = #5#15/1000; ||Peso Ton
     #5#14 = #5#12 * #5#13;    ||Valor
     |GRABA 020#5n;
|FPRC;

|DEFBUCLE malpe200;
     #200#3;
     ;
     #0#0;
     #0#1;
     ;
     NULL, NULL, malpe201;
|FIN;

|PROCESO malpe109;
     #4#0 = #109#12; ||Matri
     #4#1 = #109#1;  ||Trans
     #4#2 = #109#13; ||Alma
     #4#3 = #0#0;    ||Desde F.
     |LEE 101#4=;
     |SI FSalida = 0;
          #4#6 = #4#6 + 1;
     |SINO;
          #4#4 = #0#1;
          #4#6 = 1;
          |GRABA 020#4b;
     |FINSI;
     |GRABA 020#4a;
     |LIBERA #4;

     #5#0 = #4#0;
     #5#1 = #4#1;
     #5#2 = #4#2;
     #5#3 = #4#3;
     #5#4 = 9999;
     |LEE 000#5];
     |SI FSalida = 0;
          |LEE 000#5a;
     |SINO;
          |LEE 000#5u;
     |FINSI;
     |SI FSalida = 0; |Y #5#0 = #4#0; |Y #5#1 = #4#1; |Y #5#2 = #4#2;
               |Y #5#3 = #4#3;
          nLin = #5#4 + 1;
     |SINO;
          nLin = 1;
     |FINSI;
     |DDEFECTO #5;
     #5#0 = #4#0;
     #5#1 = #4#1;
     #5#2 = #4#2;
     #5#3 = #4#3;
     #5#4 = nLin;
     #5#5 = #109#9;
     #5#6 = "";
     #5#7 = #109#0;
     #5#8 = "S";
     #5#9 = 0;
     #5#10 = "";
/*
     #5#11 =       ;           ||Unidades
     #5#13 = precio;           ||Precio
     #5#15 = #5#11 * peso;     ||Peso Ki
     #5#12 = #5#15/1000; ||Peso Ton
     #5#14 = #5#12 * #5#13;    ||Valor
*/
     #5#14 = #109#14;
     |GRABA 020#5n;
|FPRC;

|DEFBUCLE malpe109;
     #109#2;
     ;
     #0#0;
     #0#1;
     ;
     NULL, malpe109;
|FIN;

|PROCESO BorraCab;
     |BORRA 020#4a;
|FPRC;

|PROCESO BorraLin;
     |BORRA 020#5a;
|FPRC;

|DEFBUCLE Borra;
     #4#1;
     ;
     "          ", "      ", 00000, #0#0;
     "zzzzzzzzzz", "zzzzzz", 99999, #0#0;
     be;
     NULL, BorraCab, BorraLin;
|FIN;

|PROCESO FinCab;
     |GRABA 020#4a;
     |LIBERA #4;
|FPRC;

|PROCESO Lin;
     #4#11 = #4#11 + #5#14;
|FPRC;

|PROCESO Cab;
     #4#11 = 0;
|FPRC;

|DEFBUCLE RecalImp;
     #4#1;
     ;
     "          ", "      ", 00000, #0#0;
     "zzzzzzzzzz", "zzzzzz", 99999, #0#0;
     be;
     NULL, Cab, Lin, FinCab;
|FIN;

|PROCESO tipo3; |TIPO 3;
     |SI #0#2 = "S";
          |BUCLE Borra;
          |ABRE #85;
          |ABRE #3;
          |ABRE #4;
          |ABRE #5;
          |ABRE #10;
          |ABRE #11;
          |BUCLE acum_pedi;
          |BUCLE malpe200; ||Traspaso Alma
          |BUCLE malpe109; ||Suplementos
          |CIERRA #3;
          |CIERRA #4;
          |CIERRA #5;
          |CIERRA #10;
          |CIERRA #11;
          |CIERRA #85;
          |BUCLE RecalImp;
     |FINSI;
|FPRC;
