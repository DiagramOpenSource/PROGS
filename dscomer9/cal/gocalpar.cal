|FICHEROS;
     gocalpar #0; ||Este Def.
     goartive #2; ||Cab.Partidas.
     goartdes #3; ||Descomposicion Partidas.
     goparame #10;
     goarticu #19; ||Articulos.
     agifa048 #480;
     referen@ #1;
|FIN;

|VARIABLES;
     nSwForestales = 0;
     &enPrecio = 0;
     &nDeci_PreBase = 0;
     aAlfa = "";
     aParam = "";
     nPrime = 0;
     nImpre = -1;
     nInfor = -1;
     nTotal = 0;
     nModi = 0;
     aDef = "";
     nSw = 0;
     nPrecio = 0;
     aPartida = "";
     nNume1 = 0;
     nNume2 = 0;

     aFicMail = "";
     aDirDestino = "";
     aIP = "";
     aServidor = "";
     aFrom = "";
     aTo = "";
     aSubject = "";
     aMensaje = "";
     aDjunto = "";
|FIN;

|PROCESO EnviaMail;
     aDirDestino = #0#19; |QBF aDirDestino;
     aIP = "xdscorreo.dv.diagram.net";
     aServidor = "asmtp.diagram.es";
     aFrom = aDirDestino;
     aTo = aDirDestino;
     aSubject = "Recalculo precios partida";
     aMensaje = "$$$$" + aFicMail;
     aDjunto = aFicMail;

     |SI aDirDestino ! "";
          |DSCORREO_ENVIA aIP, aServidor, aFrom, aTo, aSubject, aMensaje, aDjunto;
          |SI FSalida < 0;
               ||MENAV "0000Error al enviar correo";
          |FINSI;
     |FINSI;
     |FBORRA aFicMail;
|FPRC;

|PROCESO Descomposicion;
     nTotal = nTotal + #3#7;
|FPRC;

|DEFBUCLE Descomposicion;
     #3#1;
     ;
     #2#0, 001;
     #2#0, 999;
     ;
     NULL, Descomposicion;
|FIN;

|PROCESO RecalPartida;
     #2#0 = aPartida;
     |LEE 101#2=;
     |SI FSalida = 0;
          nTotal = 0;
          |BUCLE Descomposicion;
          #2#15 = nTotal;
          |GRABA 020#2a;
          |LIBERA #2;
     |FINSI;
|FPRC;

|PROCESO Recalcula;
     #referen@#6 = nPrecio;
     #referen@#5 = #referen@#5 ! #10#115;
     #referen@#7 = #referen@#5 * #referen@#6;
     |GRABA 020#referen@.a;
     |SI aPartida ! #referen@#0; |Y aPartida ! "";
          |HAZ RecalPartida;
     |FINSI;
     aPartida = #referen@#0;
|FPRC;

|PROCESO Impresion;
     |SI nPrime = 0;
          nPrime = 1;
          |SI aParam ! "";
               |IMPRE 0;
          |SINO;
               |DFICE aAlfa;
               aFicMail = aAlfa + "recalpar.txt";
               Impresora = aFicMail;
               |IMPRE -77;
          |FINSI;
          nImpre = FSalida;
          |SI FSalida = 0;
               |INFOR "gocalpar";
               nInfor = FSalida;
          |FINSI;
     |FINSI;
     |SI nInfor = 0;
          #3#0 = #referen@#0;
          #3#1 = #referen@#1;
          |LEE 000#3=;
          enPrecio = nPrecio;
          |PRINT;
     |FINSI;
|FPRC;

|PROCESO goartdes;
     #2#0 = #referen@#0;
     |LEE 000#2=;
     |SI FSalida = 0;
          |SI #2#7 ] #0#2; |Y #2#7 [ #0#3;
                    |Y #2#9 ] #0#4; |Y #2#9 [ #0#5;
                    |Y #2#11 ] #0#6; |Y #2#11 [ #0#7;
                    |Y #2#13 ] #0#8; |Y #2#13 [ #0#9;
               #19#0 = #referen@#2;
               |LEE 000#19=;
               |SI FSalida = 0;
                    |SI #19#126 = #0#10; |O #19#126 = #0#11; |O #19#126 = #0#12;
                              |O #19#126 = #0#13; |O #19#126 = #0#14; |O #19#126 = #0#15;
                              |O #19#126 = #0#16; |O #19#126 = #0#17; |O #19#126 = #0#18;
                         nModi = 0;
                         #480#0 = #referen@#2;
                         |LEE 000#480=;
                         |SI FSalida = 0;
                              nNume1 = #referen@#6 ! nDeci_PreBase;
                              nNume2 = #19#9 ! nDeci_PreBase;
                              |SI nNume1 ! nNume2;
                                   nPrecio = nNume2;
                                   nModi = 1;
                              |FINSI;
                         |SINO;
                              nNume1 = #referen@#6 ! nDeci_PreBase;
                              nNume2 = #19#133 ! nDeci_PreBase;
                              |SI nSwForestales = 0; |Y #19#152 ! 0;
                                   nNume2 = #19#149 - (#19#149 / 100 * #19#152);
                                   nNume2 = nNume2 ! nDeci_PreBase;
                              |FINSI;
                              |SI nNume1 ! nNume2;
                                   nPrecio = nNume2;
                                   nModi = 1;
                              |FINSI;
                         |FINSI;
                         |SI nModi = 1; |Y nPrecio ! 0;
                              |SI nSw = 0;
                                   |HAZ Impresion;
                              |SINO;
                                   |HAZ Recalcula;
                              |FINSI;
                         |FINSI;
                    |FINSI;
               |FINSI;
          |FINSI;
     |FINSI;
     |LIBERA #referen@;
|FPRC;

|DEFBUCLE goartdes;
     #referen@#3002;
     ;
     "                    ", #0#0;
     "zzzzzzzzzzzzzzzzzzzz", #0#1;
     be;
     NULL, goartdes;
|FIN;

|PROCESO Tipo3; |TIPO 3;
     |HAZ DecimalesBase;
     |DDEF aDef;
     aDef = aDef + "goartdes";
     |CARGA_DEF aDef, referen@;
     |SI FSalida = 0;
          |ABRE #19;
          |ABRE #480;
          |ABRE #2;
          |ABRE #3;
          nSw = 0;
          |BUCLE goartdes;
          |SI nPrime = 1;
               |SI nInfor ! 0;
                    |SI nImpre ! 0; |FINIMP; |FINSI;
                    |SI aParam ! "";
                         |MENAV "0000Error o impresión cancelada, no se modifica...";
                    |FINSI;
               |SINO;
                    |PIEINF;
                    |FININF;
                    |FINIMP;
                    |SI aParam ! "";
                         |CONFI "0000N¿Actualizar precios?";
                    |SINO;
                         |HAZ EnviaMail;
                         FSalida = 0;
                    |FINSI;
                    |SI FSalida = 0;
                         nSw = 1;
                         |BUCLE goartdes;
                         |SI aPartida ! "";
                              |HAZ RecalPartida;
                         |FINSI;
                    |FINSI;
               |FINSI;
          |SINO;
               |SI aParam ! "";
                    |MENAV "0000No hay articulos a modificar...";
               |FINSI;
          |FINSI;
          |CIERRA #3;
          |CIERRA #2;
          |CIERRA #480;
          |CIERRA #19;
          |DESCARGA_DEF #referen@;
     |FINSI;
|FPRC;

|PROGRAMA;
     aParam = PARAMETRO;

     |HAZ EsForestales;
     nSwForestales = FSalida;

     |ABRE #0;
     |LEE 101#0=;
     |SI aParam ! "";
          |SI FSalida ! 0; |GRABA 020#0b; |FINSI;
          |CLS;
          |PINPA #0#0;
          |PINDA #0#0;
          |ENDATOS #1#0;
          |SI FSalida = 0;
               |GRABA 020#0a;
          |FINSI;
     |SINO;
          |HAZ Tipo3;
     |FINSI;
     |CIERRA #0;
|FPRO;
