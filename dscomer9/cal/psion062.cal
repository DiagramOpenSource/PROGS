|FICHEROS;
     psion062 #0;
     psion004 #4;
     psion005 #5;
     psion063 #63;  || Historico
     crontcom@;
|FIN;

|VARIABLES;
     &enNumFic = 0;
     &enPedImpor = 0;
     nConta = 0;
     nUltima = 0;
     aDBass = "";
     aNumEmpre = "";
     aShell = "";
     nLinea = 0;
     nHandle = 0;
     aDirBase = "";
     aLong = "";
     nLong = 0;
     aAplicacion = "";
     aDefEmpre = "";
     aTAB = "";
     aParam = "";
     aAlfa2 = "";
     aDirBass = "";
     aFicCron = "";
     aMensaje = "";
     aDatos = "";

 {-1}menu = "";
     menu1 = "2400";
     menu2 = "1";
     menu3 = " ";
     menu4 = "CIoPH";
     menu5 = " Configuracion ";
     menu6 = " Importar ";
     menu7 = " Consulta ";
     menu8 = " Programacion ";
     menu9 = " Historico";
     nSal = 0;
     aRuta = "";
     aAlfa = "";
     aMsg = "";
     aLon = "";
     aCar = "";
     aPath = "";
     aBarra = "";
     nPos = 0;
     i = 0;
     aMin = "";
     aHora = "";
     aDia_mes = "";
     aMes = "";
     aDia_Sem = "";
     aComando = "";
     nCanal = 0;
     aAbre = "";
|FIN;

|PROCESO Browser;
     aRuta = "D" + #0#1;
     |QBF aRuta;
     |BROWSE aRuta;

     aRuta = aRuta % 299;
     |QBF aRuta;
     |SI aRuta ! "";
          #0#1 = aRuta;
          |PINTA #0#1;
     |FINSI;
|FPRC;

|PROCESO CreaDir; |TIPO 0;
     |SI FSalida = 10;
          |HAZ Browser;
          |ERROR; |ACABA;
     |FINSI;
     aAlfa = #0#1; |QBF aAlfa;
     aBarra = aAlfa % -101;
     |SI aBarra ! "/"; |Y aBarra ! "\";
          aAlfa = aAlfa + "\";
          #0#1 = aAlfa;
     |FINSI;
     aPath = aAlfa;
     aMsg = "";
     aAlfa = "";
     aLon = aPath % A0;
     |PARA i = 1; |SI aAlfa ! aPath; |HACIENDO i = i + 1;
          |PARA; |SI i [ aLon; |HACIENDO i = i + 1;
               nPos = i * 100 + 1;
               aCar = aPath % nPos;
               aAlfa = aAlfa + aCar;
               |SI aCar = "/"; |O aCar = "\";
                    |SI i > 1; |SAL; |FINSI;
               |FINSI;
          |SIGUE;
          |RMKDIR aAlfa;
          |SI FSalida = 0;
               |RRMDIR aAlfa;
               aMsg = "0000No existe directorio: [" + aAlfa + "]";
               |MENAV aMsg;
               aMsg = "0000SCrear directorio: [" + aAlfa + "]";
               |CONFI aMsg;
               |SI FSalida = 0;
                    |RMKDIR aAlfa;;
               |SINO;
                    |ERROR;|ACABA;
               |FINSI;
          |FINSI;
     |SIGUE;
     #0#1 = aAlfa;
     |PINTA #0#1;
     |SI aMsg ! ""; |ERROR; |FINSI;
|FPRC;

|PROCESO Importa;
     |SUB_EJECUTA_NP "psion061";
     aAlfa = "0000Ficheros Procesados: "+enNumFic+&13+" Pedidos Generados: " + enPedImpor;
     |MENAV aAlfa;
|FPRC;

|PROCESO Consulta;
     |SUB_EJECUTA "psion064";
|FPRC;

|PROCESO GrabSRV;
     aMin     = #crontcom@#2;    |QBF aMin;
     aHora    = #crontcom@#3;    |QBF aHora;
     aDia_mes = #crontcom@#4;    |QBF aDia_mes;
     aMes     = #crontcom@#5;    |QBF aMes;
     aDia_Sem = #crontcom@#6;    |QBF aDia_Sem;
     aComando = #crontcom@#7;    |QBF aComando;

     |SI #crontcom@#8 = "S";
          aAlfa = aMin + " " + aHora + " " + aDia_mes + " " + aMes + " " + aDia_Sem + " " + aComando + &13 + &10;
     |SINO;
          aAlfa = "#" + aMin + " " + aHora + " " + aDia_mes + " " + aMes + " " + aDia_Sem + " " + aComando + &13 + &10;
     |FINSI;
     |XGRABA nCanal, aAlfa;
|FPRC;

|DEFBUCLE GrabSRV;
     #crontcom@#1002;
     ;
     00, 000;
     00, 999;
     ;
     NULL, GrabSRV;
|FIN;

|PROCESO Graba_SRV;
     |DBASS aAbre;
     aAbre = aAbre + "bin/tareas.srv";
     |XABRE "WB", aAbre, nCanal;
     |DBASS aDBass; |QBF aDBass;
     aAlfa = aDBass + "cron";
     |MKDIR aAlfa;
     |BUCLE GrabSRV;
     |XCIERRA nCanal;
|FPRC;

|PROCESO Shell;
     |DBASS aDBass; |QBF aDBass;
     |DFICE aNumEmpre;
     aNumEmpre = aNumEmpre % -205;
     aShell = aDBass + "bin/cron" + aNumEmpre  + ".sh";
     |XABRE "WB", aShell, nHandle;
     aDirBase = aDBass;
     aLong = aDirBase % 0;
     nLong = aLong;
     aAlfa = aDirBase;
     nPos = 100 + nLong - 1;
     aDirBase = aAlfa % nPos;
     aAplicacion = "dscomer9";
     aNumEmpre = aNumEmpre;
     aDefEmpre = "agif0041";
     aTAB = "psion061";
     aParam = "";

     |SI #0#4 = "N";
          aAlfa2 = &10;
     |SINO;
          aAlfa = "echo Iniciando la ejecucion:";
          aAlfa2 = " >>" + aDBass + "bin/cron" + nLinea + ".log 2>>" + aDBass + "bin/cron" + nLinea + ".err" + &10;
          aAlfa = aAlfa + aAlfa2;
          |XGRABA nHandle, aAlfa;

          aAlfa = "date";
          aAlfa = aAlfa + aAlfa2;
          |XGRABA nHandle, aAlfa;
     |FINSI;

     aAlfa = aDBass + "bin/serverds @" + aDirBase + " " + &34 + "/!:" + aAplicacion +"/"+ aTAB;
     |SI aDefEmpre ! "";
          aAlfa = aAlfa + " " + aDefEmpre;
     |FINSI;
     |SI aNumEmpre ! "";
          aAlfa = aAlfa + " " + aNumEmpre;
     |FINSI;
     |SI aParam ! "";
          aAlfa = aAlfa + ";" + aParam;
     |FINSI;
     aAlfa = aAlfa + &34 + aAlfa2;
     |XGRABA nHandle, aAlfa;

     |SI #0#4 = "S";
          aAlfa = "echo Finalizada la ejecucion:";
          aAlfa = aAlfa + aAlfa2;
          |XGRABA nHandle, aAlfa;

          aAlfa = "date";
          aAlfa = aAlfa + aAlfa2;
          |XGRABA nHandle, aAlfa;
     |FINSI;

     |XCIERRA nHandle;
     aAlfa = "chmod 777 " + aShell;
     |SYSTEM aAlfa;
|FPRC;

|PROCESO Bat;
     |DBASS aDBass; |QBF aDBass;
     |DFICE aNumEmpre;
     aNumEmpre = aNumEmpre % -205;
     aShell = aDBass + "bin/cron" + aNumEmpre  + ".bat";
     |XABRE "WB", aShell, nHandle;
     aDirBase = aDBass;
     aLong = aDirBase % 0;
     nLong = aLong;
     aAlfa = aDirBase;
     nPos = 100 + nLong - 1;
     aDirBase = aAlfa % nPos;
     aAplicacion = "dscomer9";
     aDefEmpre = "agif0041";
     aNumEmpre = aNumEmpre;
     aTAB = "psion061";
     aParam = "";

     aAlfa = "@echo off" + &13 + &10;
     |XGRABA nHandle, aAlfa;

     aAlfa = "echo Iniciando la ejecucion:";
     aAlfa2 = " >>" + aDBass + "bin/cron" + nLinea + ".log 2>>" + aDBass + "bin/cron" + nLinea + ".err" + &13 + &10;
     aAlfa = aAlfa + aAlfa2;
     |XGRABA nHandle, aAlfa;

     aAlfa = "time /T";
     aAlfa = aAlfa + aAlfa2;
     |XGRABA nHandle, aAlfa;

     aAlfa = aDBass + "bin/diagramx.exe /DS:" + &34 + "@" + aDirBase + " $$$batch,batch# //(" + aAplicacion +")"+ aTAB;
     |SI aDefEmpre ! "";
          aAlfa = aAlfa + "&" + aDefEmpre;
     |FINSI;
     |SI aNumEmpre ! "";
          aAlfa = aAlfa + "&" + aNumEmpre;
     |FINSI;
     |SI aParam ! "";
          aAlfa = aAlfa + ";" + aParam;
     |FINSI;
     aAlfa = aAlfa + &34 + aAlfa2;
     |XGRABA nHandle, aAlfa;

     aAlfa = "echo Finalizada la ejecucion:";
     aAlfa = aAlfa + aAlfa2;
     |XGRABA nHandle, aAlfa;

     aAlfa = "time /T";
     aAlfa = aAlfa + aAlfa2;
     |XGRABA nHandle, aAlfa;

     |XCIERRA nHandle;
|FPRC;

|PROCESO crontcom;
     nUltima = #crontcom@#1;
     aAlfa = #crontcom@#22; |QBF aAlfa;
     aAlfa2 = "Traspasos Pedidos Automaticos PDA " + aNumEmpre;
     |SI aAlfa = aAlfa2;
          nLinea = #crontcom@#1;
     |FINSI;
|FPRC;

|DEFBUCLE crontcom;
     #crontcom@#1002;
     ;
     00, 000;
     00, 999;
     ;
     NULL, crontcom;
|FIN;

|PROCESO Programa;
     |C_V #0#1, 1, "N";
     |C_V #0#2, 1, "N";
     |C_V #0#5, 1, "N";
     |C_V #0#3, 1, "S";
     |C_V #0#4, 1, "S";
     |ENDATOS #1#0;
     |SI FSalida ! 0; |ACABA; |FINSI;
     |GRABA 020#0a;

     |DBASS aDirBass;
     |QBF aDirBass;
     aFicCron = aDirBass + "agitool/def/crontcom";
     |CARGA_DEF aFicCron, crontcom@;
     |SI FSalida ! 0;
          aMensaje = "0000Problemas con el CargaDef " + aFicCron;
          |MENAV aMensaje;
          |ACABA;
     |FINSI;

     aDatos = aDirBass + "agitool/fich/";
     |PATH_DAT #crontcom@#aDatos#;
     |SI #0#3 = 0;
          nLinea = 0;
          |BUCLE crontcom;
          |SI nLinea > 0;
               |ABRE #crontcom@;
               #crontcom@#0 = 0;
               #crontcom@#1 = nLinea;
               |BORRA 020#crontcom@.c;
               |CIERRA #crontcom@;
          |FINSI;
          |HAZ Graba_SRV;
     |SINO;
          nLinea = 0;
          |BUCLE crontcom;
          |ABRE #crontcom@;
          |SI nLinea = 0;
               #crontcom@#0 = 0;
               #crontcom@#1 = nUltima + 1;
               |GRABA 020#crontcom@.b;
          |SINO;
               #crontcom@#0 = 0;
               #crontcom@#1 = nLinea;
               |LEE 121#crontcom@.=;
          |FINSI;
          |SI FSalida = 0;
               nLinea = #crontcom@#1;
               |QUE_SISTEMA aAlfa;
               |SI aAlfa = "ESDOS";
                    |HAZ Bat;
               |SINO;
                    |HAZ Shell;
               |FINSI;
               aAlfa = "0-59/" + #0#3;
               #crontcom@#2 = aAlfa;
               #crontcom@#3 = "*";
               #crontcom@#4 = "*";
               #crontcom@#5 = "*";
               #crontcom@#6 = "*";
               #crontcom@#7 = aShell;
               #crontcom@#8 = "S";
               #crontcom@#9 = #crontcom@#2;
               #crontcom@#10 = #crontcom@#3;
               #crontcom@#11 = #crontcom@#4;
               #crontcom@#12 = #crontcom@#5;
               #crontcom@#13 = #crontcom@#6;
               #crontcom@#14 = #crontcom@#7;
               #crontcom@#15 = "N";
              aAlfa2 = "Traspasos Pedidos Automaticos PDA " + aNumEmpre;
               #crontcom@#22 = aAlfa2;
               #crontcom@#23 = "S";
               #crontcom@#24 = #0#4;
               #crontcom@#25 = #0#4;
               |GRABA 020#crontcom@.a;
               |HAZ Graba_SRV;
               |MENAV "0000Configuración del traspaso de pedidos automáticos realizada con éxito";
          |FINSI;
     |FINSI;
     |CIERRA #crontcom@;

     |DESCARGA_DEF #crontcom@;

|FPRC;

|PROCESO Configura;
     |C_V #0#1, 1, "S";
     |C_V #0#2, 1, "S";
     |C_V #0#5, 1, "S";
     |C_V #0#3, 1, "N";
     |C_V #0#4, 1, "N";
     |ENDATOS #1#0;
     |SI FSalida = 0; |GRABA 020#0a; |FINSI;
|FPRC;

|PROCESO Historico;
     |ABRE #63;
     |LEE 000#63p;
     |CONSULTA_CLAVE #1#63;
     |CIERRA #63;
|FPRC;

|PROCESO Tipo12; |TIPO 12;
|FPRC;

|PROGRAMA;
     |DBASS aDBass; |QBF aDBass;
     |DFICE aNumEmpre;
     aNumEmpre = aNumEmpre % -205;

     |CLS;
     |ABRE #0;
     |LEE 101#0p;
     |SI FSalida ! 0; |GRABA 020#0n; |FINSI;
     |PINPA #0#0;
     |PARA; |SI menu2 > 0; |HACIENDO;
          |LEE 101#0p;
          |PINDA #0#0;
          |MENU menu;
          menu2 = FSalida;
          |SI menu2 = 1; |HAZ Configura; |FINSI;
          |SI menu2 = 2; |HAZ Importa; |FINSI;
          |SI menu2 = 3; |HAZ Consulta; |FINSI;
          |SI menu2 = 4; |HAZ Programa; |FINSI;
          |SI menu2 = 5; |HAZ Historico; |FINSI;
          |LIBERA #0;
     |SIGUE;
     |CIERRA #0;
|FPRO;
