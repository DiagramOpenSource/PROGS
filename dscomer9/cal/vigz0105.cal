|FICHEROS;
     vigz0105 #0;
     vigm0049 #49;
     vigm0050 #50;
     vig00001@;
     dscam003@;
     dscam045@;
|FIN;

|VARIABLES;
     aBass = "";
     fHoy = @;
     aAlfa = "";
     nEmpresa = 0;
     aSer = "";
     nNum = 0;
     aRep = "";
     aLiq = "";
|FIN;

|PROCESO CtrlLiqui;
     #vig00001@#2 = #0#0;
     |GRABA 020#vig00001@.a;

     #50#3 = #vig00001@#0;
     #50#4 = #vig00001@#1;
     |LEE 101#50=;
     |SI FSalida ! 0; |GRABA 020#50b; |FINSI;
     #50#0 = #0#0;
     #50#1 = #0#1;
     #50#2 = #vig00001@#3;
     #50#5 = "-";
     |GRABA 020#50a;
     |LIBERA #50;
|FPRC;

|DEFBUCLE CtrlLiqui;
     #vig00001@#1002;
     2;
     "     ", 000001, 0;
     "zzzzz", 999999, 0;
     be;
     NULL, CtrlLiqui;
|FIN;

|PROCESO FinRec;
     |SI aLiq = "S";   || Todos los recibos de la factura liquidados
          #50#3 = aSer;
          #50#4 = nNum;
          |LEE 000#50=;
          |SI FSalida ! 0; |O #50#5 = "-";
               |SI FSalida ! 0; |GRABA 020#50b; |FINSI;
               #50#0 = #0#0;
               #50#1 = #0#1;
               #50#2 = aRep;
               #50#5 = "+";
               |GRABA 020#50a;
               |LIBERA #50;

               #vig00001@#0 = aSer;
               #vig00001@#1 = nNum;
               |LEE 101#vig00001@.=;
               |SI FSalida = 0; |Y #vig00001@#2 = 0;
                    |BORRA 020#vig00001@.a;
                    |LIBERA #vig00001@;
               |FINSI;
          |FINSI;
     |FINSI;
     aLiq = "S";
|FPRC;

|PROCESO Recibos;
     #49#0 = #dscam003@#32;
     |LEE 000#49=;
     |SI FSalida = 0; |ACABA; |FINSI;

     |SI aSer = ""; aSer = #dscam003@#0; nNum = #dscam003@#1; |FINSI;
     |SI aSer ! #dscam003@#0; |O nNum ! #dscam003@#1;
          |HAZ FinRec;
     |FINSI;
     |SI #dscam003@#14 ! "L"; aLiq = "N"; |FINSI;
     aSer = #dscam003@#0;
     nNum = #dscam003@#1;
     aRep = #dscam003@#33;
|FPRC;

|DEFBUCLE Recibos;
     #dscam003@#12006;
     ;
     "     ", 000001, 000, "01.01.0000", "            ", "      ";
     "zzzzz", 999999, 999, "31.12.9999", "zzzzzzzzzzzz", "zzzzzz";
     ;
     NULL, Recibos;
|FIN;

|PROCESO CtrlCartera;
     aAlfa = ("00000" + #dscam045@#6) % -105;
     aAlfa = aBass + "dscarter/fich/" + aAlfa + "/";
     |PATH_DAT #dscam003@#aAlfa#;
     |PATH_DAT #vig00001@#aAlfa#;
     aLiq = "S";
     |BUCLE Recibos;
     |SI aSer ! ""; |HAZ FinRec; |FINSI;
     |BUCLE CtrlLiqui;
|FPRC;

|DEFBUCLE CtrlCartera;
     #dscam045@#1002;
     2;
     nEmpresa, 001, "V";
     nEmpresa, 999, "V";
     ;
     NULL, CtrlCartera;
|FINI;

|PROCESO Defecto; |TIPO 7;
     |SI #0#1 = 0;
          fHoy = ~;
          aAlfa = fHoy % 402; #0#0 = aAlfa; |PINTA #0#0;
          aAlfa = fHoy % 704; #0#1 = aAlfa; |PINTA #0#1;
     |FINSI;
|FPRC;

|PROCESO Tipo3; |TIPO 3;
     |DBASS aBass;
     aAlfa = aBass + "dscarter/def/dscam045";
     |CARGA_DEF aAlfa, dscam045@;
     |SI FSalida = 0;
          aAlfa = aBass + "dscarter/def/dscam003";
          |CARGA_DEF aAlfa, dscam003@;
          |SI FSalida = 0;
               aAlfa = aBass + "dscarter/def/vig00001";
               |CARGA_DEF aAlfa, vig00001@;
               |SI FSalida = 0;
                    |DFICE aAlfa; aAlfa = aAlfa % -205;
                    nEmpresa = aAlfa;
                    aAlfa = aBass + "dscarter/fich/";
                    |PATH_DAT #dscam045@#aAlfa#;
                    |ABRE #49;
                    |ABRE #50;
                    |BUCLE CtrlCartera;
                    |CIERRA #50;
                    |CIERRA #49;
                    |DESCARGA_DEF #vig00001@;
               |FINSI;
               |DESCARGA_DEF #dscam003@;
          |FINSI;
          |DESCARGA_DEF #dscam045@;
     |FINSI;
|FPRC;
