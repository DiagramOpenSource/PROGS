|FICHEROS;
     agif0005 #0;
     agif0006 #1,MANTE;
     agifa019 #2;
     agifa017 #3;
     agifa029 #29;
|FIN;

|VARIABLES;
     &eaArticulo = "";
     &Tempo    = "";
     aTempo1   = "";
     fichero   = "";
     varalfa   = "";
|FIN;

|CAMPOS;
     #2#15  art_pser; #3#42  alm_pser;
     #2#16  art_prec; #3#43  alm_prec;
     #2#17  art_pval; #3#4   alm_pval;
     #2#11  art_nece; #3#50  alm_nece;
     #2#12  art_rese; #3#8   alm_rese;
     #2#14  art_depo; #3#10  alm_depo;
     #2#13  art_fabr; #3#9   alm_fabr;
     #2#10  art_vsto; #3#3   alm_vsto;
     #2#7   art_mini; #3#6   alm_mini;
     #2#8   art_maxi; #3#7   alm_maxi;
     #2#6   art_tota; #3#5   alm_tota;
     #2#104 art_disp; #3#39  alm_disp;
     #2#9   art_pcm;
     #2#185 art_tota2; #3#47  alm_tota2;
     #2#193 art_disp2;  #3#48 alm_disp2;
|FIN;

|PROCESO Pinta; |TIPO 7;
     |ENTLINEAL #1#1, -1, 7, 20, 0, inf, sup;
|FPRC;

|PROCESO Modifica;
     |SI #1#7 = " ";
          |ACABA;
     |FINSI;

     varalfa = "2401 Articulo: " + #1#0;
     |MENSA varalfa;

     |DDEFECTO #29;
     #29#0 = #1#6;
     |LEE 000#29=;

     |DDEFECTO #2;
     #2#0 = #1#0;
     |LEE 121#2=;

     #3#0 = #1#0;
     #3#1 = #1#6;
     |LEE 121#3=;
     |SI FSalida = 0;
          #3#6 = #1#4;   ||Min
          #3#7 = #1#3;   ||Max
          alm_nece = alm_pser + alm_mini - alm_prec - alm_disp;
          alm_nece = alm_nece - alm_pval - alm_fabr - alm_rese;
          |SI alm_nece < 0; alm_nece = 0; |FINSI;

          #3#40 = #2#1;
          #3#41 = #29#1;
          |GRABA 020#3a;
          |LIBERA #3;
     |FINSI;

     #2#0 = #1#0;
     |LEE 121#2=;
     |SI FSalida = 0;
          #2#7 = 0;   || Minimo
          #2#8 = 0;   || Maximo

          #3#0 = #1#0;
          #3#1 = 0;
          |LEE 000#3];
          |PARA; |SI FSalida = 0; |Y #3#0 = #1#0; |HACIENDO;
               #2#7 = #2#7 + #3#6;
               #2#8 = #2#8 + #3#7;
               |LEE 000#3s;
          |SIGUE;

          art_nece = art_pser + art_mini - art_prec - art_disp;
          art_nece = art_nece - art_pval - art_fabr - art_rese;
          |SI art_nece < 0; art_nece = 0; |FINSI;

          |GRABA 021#2a;
          |LIBERA #2;
     |FINSI;
|FPRC;

|DEFBUCLE Modifica;
     #1#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, Modifica;
|FIN;

|PROCESO agifa017;
     |DDEFECTO #2;
     #2#0 = #3#0;
     |LEE 020#2=;

     |SI #2#2 < #0#6; |O #2#2 > #0#7; |ACABA; |FINSI;

     |SI #0#0 ! 0;
          |SI #2#27 < #0#0; |O #2#27 > #0#0;
               |ACABA;
          |FINSI;
     |FINSI;
     #1#0 = #3#0;
     #1#6 = #3#1;
     #1#1 = #2#1;
     #1#2 = #3#35;
     #1#3 = #1#2 / #0#4;
     #1#4 = #1#2 / #0#5;
     #1#5 = #2#9;   || P.C.M.
     #1#8 = #3#7;
     #1#9 = #3#6;
     |GRABA 021#1n;
|FPRC;

|DEFBUCLE agifa017;
     #3#1;
     ;
     #0#2, #0#8;
     #0#3, #0#9;
     ;
     NULL, agifa017;
|FIN;

|PROCESO inf;
     #1#0 = "             ";
     #1#6 = 1;
|FPRC;

|PROCESO sup;
     #1#0 = "zzzzzzzzzzzzz";
     #1#6 = 99999;
|FPRC;

|PROCESO inicio; |TIPO 3;
     |HAZ CreaTempo; aTempo1 = Tempo;
     |NOME_DAT #1 aTempo1; |DELINDEX #1;

     |ABRE #1;
     |ABRE #2;
     |BUCLE agifa017;
     |CIERRA #2;
     |CIERRA #1;

     |ENTLINEAL #1#1, -1, 4, 20, 0, inf, sup;
     |CONFI "0000NModificar Maximo y Minimo de Articulos";
     |SI FSalida = 0;
          |ABRE #29;
          |ABRE #3;
          |ABRE #2;
          |BUCLE Modifica;
          |CIERRA #2;
          |CIERRA #3;
          |CIERRA #29;
     |FINSI;

     |DELINDEX #1; Tempo = aTempo1; |HAZ BoraTempo;
|FPRC;

|PROCESO Llena005; |TIPO 6;
     eaArticulo = #0Campo;
     |HAZ LlenaArticulo;
     |SI FSalida = 0;
          #0Campo = eaArticulo;
          |PINTA #0Campo;
     |SINO;
          |ERROR; |ACABA;
     |FINSI;
|FPRC;
