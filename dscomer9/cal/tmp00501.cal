     || IMPRESION/REIMPRESION DE TIQUETS
     || SI LA CLAVE NO ES OFICIAL LA CAMBIA A OFICIAL

|FICHEROS;
     tmp00501 #0;
     agifa502 #502;
     agifa505 #1;
     agifa027 #2;
     agifa084 #3;
     agifa010 #4;
     agifa142 #142;
     agifa508 #51;
     agifa027 #5;
     agifa065 #65;
|FIN;

|VARIABLES;
     aSerAnt   = "";
     nNumAnt   = 0;
     conta_reg = 0;
     aSer      = "";
     claveconta= "";
     &acaja    = 0;      || Del agifa501
     &afecha   = "";
     &xSerie   = "";
     &xNumero  = 0;

     &aser     = "";     || Para agifa578
     &aregis   = 0;
     &reimp    = "";

     &ser_alb  = "";     || Para agifa014
     &num_alb  = 0;

     &serie    = "";     || Para agifa086
     &fecha    = "";
     &numero   = 0;
     &reimpr   = "";

     salida    = 0;
     fFecha    = @;
     nFecha    = 0;
     nNume     = 0;
     aSerie = "";
|FIN;

|PROCESO SiguienteTiq;
     |SALVA_FICHA #0;
     |SELECT #2#0;
     aSer = #0#57;
     #0#35 = "T";
     #0#58 = 999999;
     |LEE 000#0];
     |SI FSalida = 0;
          |LEE 000#0a;
     |SINO;
          |LEE 000#0u;
     |FINSI;
     |SI FSalida = 0; |Y #0#57 = aSer; |Y #0#35 = "T";
          conta_reg = #0#58 + 1;
     |SINO;
          conta_reg = 1;
     |FINSI;
     |SELECT #1#0;
     |REPON_FICHA #0;
|FPRC;

|PROCESO contique; |TIPO 0;
     conta_reg = 0;
     |ABRE #51;
     |DDEFECTO #51;
     #51#0 = claveconta;
     claveconta = #51#0;        ||para que lo rellene de blancos
     #51#1 = #0#57;
     |LEE 000#51];
     |PARA; |SI FSalida = 0; |Y #51#0 = claveconta; |Y #51#1 = #0#57; |HACIENDO;
          claveconta = #51#0;
          |SI #51#3 = #0#14;
               conta_reg = #51#2;
               |LEE 121#51=;
               |BORRA 020#51a;
               |LIBERA #51;
               |SAL;
          |FINSI;
          |LEE 000#51s;
     |SIGUE;
     |CIERRA #51;

     |SI conta_reg = 0;
          |ABRE #5;
          #5#0 = claveconta;
          #5#2 = #0#57;
          |LEE 101#5=;
          |SI FSalida ! 0;
               #5#1 = 1;
               |GRABA 021#5b;
          |FINSI;
          conta_reg = #5#1;
          #5#1 = #5#1 + 1;
          |GRABA 021#5a;
          |CIERRA #5;
     |FINSI;
|FPRC;

|PROCESO ModiHisto;
     #65#0 = #502#2;  || Art
     #65#1 = #0#14;   || Fec
     #65#2 = "TI";    || Ope
     #65#3 = nNumAnt; || Doc
     #65#4 = #502#1;  || Lin
     #65#11= aSerAnt; || Ser
     |LEE 101#65=;
     |SI FSalida = 0;
          |BORRA 020#65a;
          #65#3 = #0#58;
          #65#11 = #0#57;
          |GRABA 020#65n;
     |FINSI;
|FPRC;

|DEFBUCLE ModiHisto;
     #502#1;
     ;
     #0#36, #0#0, 001;
     #0#36, #0#0, 999;
     ;
     NULL, ModiHisto;
|FIN;

|PROCESO Modifica;
     |ABRE #142; |LEE 000#142p; |CIERRA #142;
     |ABRE #1;
     #1#0 = acaja;
     |LEE 020#1=;
     |CIERRA #1;

     |SI #0#57 ! #1#105;      || Si la serie del Tiq. no es oficial
          |LEE 121#0=;
          aSerAnt = #0#57;
          nNumAnt = #0#58;
          #0#57 = #1#105;     || la pone oficial
          |SI #142#137 = "F";
               claveconta = "vtiquet";
               |HAZ contique;
          |SINO;
               |HAZ SiguienteTiq;
          |FINSI;
          #0#58 = conta_reg;
          |GRABA 021#0a;
          |LIBERA #0;

          |ABRE #65;
          |BUCLE ModiHisto;
          |CIERRA #65;
     |FINSI;
|FPRC;

|PROCESO Tecla; |TIPO 0;
     |SI FSalida = 0; |Y #0Campo = Contenido;
          |PTEC 806;
          nNume = #0#0;
          aSerie = #0#36;
     |FINSI;
|FPRC;

|PROCESO Max;
     #0#36 = xSerie;
     #0#0 = xNumero - 1;
|FPRC;

|PROCESO Min;
     #0#36 = xSerie;
     #0#0 = 1;
|FPRC;

|PROCESO ImpreDoc;
     |SI #0#35 = "A";
          |ABRE #4;
          #4#52 = #0#57;
          #4#0 = #0#58;
          |LEE 020#4=;
          salida = FSalida;
          |CIERRA #4;
          |SI salida = 0;
               ser_alb = #0#57;
               num_alb = #0#58;
               |SUB_EJECUTA_NP "agifa014";
          |FINSI;
     |FINSI;
     |SI #0#35 = "F";
          |ABRE #3;
          #3#48 = #0#57;
          #3#0 = #0#58;
          |LEE 020#3=;
          salida = FSalida;
          |CIERRA #3;
          |SI salida = 0;
               serie = #3#48;
               fecha = #3#1;
               numero = #3#0;
               reimpr = #3#10;
               |SUB_EJECUTA_NP "agifa086";
          |FINSI;
     |FINSI;
     |SI #0#35 = "T";
          |HAZ Modifica;
          aser = #0#57;
          aregis = #0#58;
          reimp = #0#13;
          |SUB_EJECUTA_NP "agifa578";
     |FINSI;
|FPRC;

|PROGRAMA;
     fFecha = afecha;
     nFecha = fFecha;
     nFecha = nFecha - 1;
     fFecha = nFecha;

     |PUSHV 0501 2380;
     |PTEC 816;
     |ENTLINEAL #1#0, 2, 4, 22, 1, Min, Max;
     |SI nNume ! 0;
          |ABRE #0;
          |SI xNumero = nNume; |Y xSerie = aSerie;
               |MENAV "0000ESTE ES EL TIQUET ACTUAL...";
          |SINO;
               #0#36 = xSerie;
               #0#0 = nNume;
               |SELECT #1#0;
               |LEE 020#0=;
               |SI FSalida = 0;
                    |SI #0#35 = "N";
                         |MENAV "0000ESTE TIQUET ESTA APARCADO...";
                    |SINO;
                         |SI #0#35 = " ";
                              |MENAV "0000Este tiquet lo esta creando otro usuario...";
                         |SINO;
                              |LEE 000#0=;
                              |SI FSalida = 0;
                                   |HAZ ImpreDoc;
                              |SINO;
                                   |MENAV "0000Este tiquet lo esta modificando otro usuario...";
                              |FINSI;
                         |FINSI;
                    |FINSI;
               |FINSI;
          |FINSI;
          |CIERRA #0;
     |FINSI;
     |POPV;
|FPRO;
