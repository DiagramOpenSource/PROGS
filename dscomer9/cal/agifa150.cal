|FICHEROS;
     agifa150 #0;
     agifa146 #1;
     agifa147 #2;
     agifa024 #3;
     agifa038 #13;
     agifa048 #15;
     agifa049 #16;
     agifa019 #19;
     agifa007 #40;
     agifa142 #42;
     dsm00010 #910;
     dsm00053 #53;
     dsm00050 #50;
     tagm0056@;
     mecam018@;
     tmp00000 #300; || Tmp Prof. que se imprimen
     dsm00225 #225;
     dsm00156 #156;
|FIN;

|INCLUYE i_varart;
|INCLUYE i_semana;

|VARIABLES;
     aArti = "";
     &eaImpresora = "";
     aTmp300 = "";
     nSwTagloma = 0;
     &enGraba300 = 0;
     &eaArtIdioma = "";
     &eaCodArtIdioma = "";
     &Tempo = "";
     &enSemana = 0;
     nPrecio = 0;
     nPrecioD = 0;
     aAlfa = "";
     nTotal = 0;
     nTotalD = 0;
     aEsPostFormaso = "N";
     &eaDoc = "";
     nSwPackList = 0;

     &aMoneda     = "";
     &aDivisa     = "";
     &enEscan    = 0;
     &enCodig     = 0;
     &eaNArticulo = "";
     &enPantalla  = 0;
     &n5Elem      = 0;
     &enImpo = 0;

     indtope = 0;
     ind = 0;
     informe  = "";
     &informe1 = "";

     &numero = 0;
     &serie  = "";
     &reimpr = "";
     &iva1 = 0;
     &iva2 = 0;
     &iva3 = 0;
     &iva4 = 0;
     &iva5 = 0;
     &re1 = 0;
     &re2 = 0;
     &re3 = 0;
     &re4 = 0;
     &re5 = 0;
     &codigo="";
     &descrip="";
     &precio=0;
     &dto=0;
     &unid=0;
     &imptot=0;
     &tiva=0;
     &linea=0;
     vacio="";
     &bl  = "                              ";
|FIN;

|PROCESO IdiomaArticulo;
     aAlfa = "";
     |ABRE #225;
     #225#0 = #3#205;
     |LEE 000#225=;
     |SI FSalida = 0;
          |ABRE #156;
          #156#0 = aArti;
          #156#1 = #225#5;
          |LEE 000#156=;
          |SI FSalida = 0;
               aAlfa = #156#2; |QBF aAlfa;
          |FINSI;
          |CIERRA #156;
     |FINSI;
     |CIERRA #225;
|FPRC;

|PROCESO TagPostFormado;
     |ABRE #19;
     #19#0 = #2#2;
     |LEE 000#19=;
     |CIERRA #19;

     aEsPostFormaso = "N";
     |DDEF aAlfa;
     aAlfa = aAlfa + "tagm0056";
     |CARGA_DEF aAlfa, tagm0056@;
     |SI FSalida = 0;
          |ABRE #tagm0056@;
          #tagm0056@#0 = "PR";
          #tagm0056@#1 = #2#20;
          #tagm0056@#2 = #2#0;
          #tagm0056@#3 = #2#1;
          |LEE 000#tagm0056@.=;
          |SI FSalida = 0;
               descrip = #2#4;
               nTotal = #2#9;
               nTotalD = #2#25;
               |SALVA_FICHA #2;
               #2#1 = #tagm0056@#5;
               |LEE 000#2=;
               |SI FSalida = 0;
                    nTotal = nTotal + #2#9;
                    nTotalD = nTotalD + #2#25;
                    nPrecio = #2#6;
                    nPrecioD = #2#24;
               |FINSI;
               |REPON_FICHA #2;
               |SI #19#194 = 2;
                    nPrecio = nPrecio + (#2#6 * #2#28 / #2#5);
                    nPrecioD = nPrecioD + (#2#24 * #2#28 / #2#5);
               |SINO;
                    nPrecio = nPrecio + #2#6;
                    nPrecioD = nPrecioD + #2#24;
               |FINSI;
               #2#6 = nPrecio;
               #2#24 = nPrecioD;
               #2#9 = nTotal;
               #2#25 = nTotalD;
          |SINO;
               |SELECT #2#tagm0056@;
               #tagm0056@#5 = #2#1;
               |LEE 000#tagm0056@.=;
               |SI FSalida = 0;
                    aEsPostFormaso = "S";
               |FINSI;
          |FINSI;
          |CIERRA #tagm0056@;
          |DESCARGA_DEF #tagm0056@;
     |FINSI;
|FPRC;

|PROCESO Meca1Fecha;
     |DDEF aAlfa;
     aAlfa = aAlfa + "mecam018";
     |CARGA_DEF aAlfa, mecam018@;
     |SI FSalida = 0;
          |ABRE #mecam018@;
          #mecam018@#0 = #1#48;
          #mecam018@#1 = #1#0;
          #mecam018@#2 = 1;
          |LEE 000#mecam018@.];
          |CIERRA #mecam018@;
          fFecha = #mecam018@#7;
          |DESCARGA_DEF #mecam018@;
     |FINSI;
|FPRC;

|PROCESO impdesam;|TIPO 0;
     #13#0 = #2#18;
     |ABRE #13;
     |LEE 101#13=;
     |SI 0=FSalida
          |HAZ xx;
     |FINSI;
     |LIBERA #13;
     |CIERRA #13;
|FPRC;

|PROCESO xx;
     indtope=0;
     |PARA ind=6;|SI ind>0;|HACIENDO ind=ind-1;
          descrip=#13ind;
          |QBF descrip;
          |SI descrip ! vacio;
               indtope=ind+1;
               ind=0;
          |FINSI;
     |SIGUE;

     |PARA ind=1;|SI ind < indtope;|HACIENDO ind=ind+1;
          descrip=#13ind;
          linea=0;
          |PRINT;
     |SIGUE;
|FPRC;


|PROCESO escanlinea;
    unid = #16#4 * #2#5;
    descrip = #16#3;
    codigo = "";
    dto = 0;
    precio = 0;
    imptot = 0;
    tiva = 0;
    linea = 1;

     aArti = #16#2;
     |HAZ IdiomaArticulo;
     |SI aAlfa ! "";
          #16#3 = aAlfa;
          descrip = aAlfa;
     |FINSI;

     |PRINT;
|FPRC;

|PROCESO DesAmpli;
     |DDEFECTO #2;
     #2#3 = 0;      || Almacen
     #2#4 = #53#5;
     descrip = #2#4;
     codigo = "";
     dto = "";
     precio = "";
     unid = "";
     imptot = "";
     tiva = "";
     enSwDA = 1;
     |PRINT;
     enSwDA = 0;
|FPRC;

|DEFBUCLE DesAmpli;
     #53#1;
     ;
     "X", #2#20, #2#0, #2#1, 0001;
     "X", #2#20, #2#0, #2#1, 9999;
     ;
     NULL, DesAmpli;
|FIN;

|PROCESO dsm00050;
     unid = #2#5 * #50#5;
     |PRINT;
|FPRC;

|DEFBUCLE dsm00050;
     #50#1;
     ;
     #2#20, #2#0, #2#1, 001;
     #2#20, #2#0, #2#1, 999;
     ;
     NULL, dsm00050;
|FIN;

|PROCESO ImpreNormal;
     |SI nSwTagloma = 0;
          |HAZ ImpUniFami;
          |HAZ TagPostFormado;
          |SI aEsPostFormaso = "S"; |ACABA; |FINSI;
     |FINSI;
     enArtImp = #2#2; |QBF enArtImp;
     codigo=#2#2;
     descrip=#2#4;
     dto=#2#8;
     precio = #2#6;
     unid=#2#5;
     imptot=#2#9;
     tiva=#2#11;
     linea=1;

     |HAZ EsCoimasa;
     |SI FSalida = 0;
          eaCodArtIdioma = #2#2;
          eaArtIdioma = "";
          |HAZ IdiomaPedidoCoimasa;
          |SI eaArtIdioma ! "";
               descrip = eaArtIdioma;
          |FINSI;
     |FINSI;

     aArti = #2#2;
     |HAZ IdiomaArticulo;
     |SI aAlfa ! "";
          #2#4 = aAlfa;
          descrip = aAlfa;
     |FINSI;

     |PRINT;

     |SALVA_DATOS #2;
     |BUCLE DesAmpli;
     |REPON_DATOS #2;

     |HAZ impdesam;

     |ABRE #42;
     |LEE 000#42p;
     |CIERRA #42;

     |SI #42#16 = "S";
          eaArticulo = #2#2;
          |HAZ EsEscandallo;
          |SI FSalida = 0;
               |HAZ EsKit;
               |SI FSalida = 0;
                    enEscan = 2;
                    |BUCLE dsm00050;
               |SINO;
                    enEscan = 1;
                    |BUCLELIN 0escanlinea#15;
               |FINSI;
               enEscan = 0;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO ImpreHorizo;
     enArtImp = #2#2; |QBF enArtImp;
     eaTipo      = "FP";
     eaSerie     = #2#20;
     enCodig     = #2#0;
     enLinea     = #2#1;
     eaArticulo  = #2#2;
     eaNArticulo = #2#4;
     enUnidades  = #2#5;
     enPrecio    = #2#6;
     enImpo = #2#25;
     enDto1      = #2#8;
     enDto2      = #2#21;
     |HAZ TrataCompo;
|FPRC;

|PROCESO conimprl;
     eaSerLog = #2#20;
     eaNumLog = (A\("000000" + #2#0) % -106);
     eaTipLog = "PF";
     |HAZ ImprimeLog;
     |SI #910#19 = "N";  |HAZ ImpreNormal;  |FINSI;
     |SI #910#19 = "S";  |HAZ ImpreHorizo;  |FINSI;
|FPRC;

|PROCESO conimpr2;|TIPO 0;
     |SI #910#19 = "S";  |HAZ ElRemate; |FINSI;

     |PIEINF;
     |FININF;

     |SI #0#6 = "N";
          #1#10 = AS;
          |BORRA 020#1a;
          |GRABA 020#1n;
     |FINSI;
     |SI nSwTagloma = 0; |Y enGraba300 = 0;
          #300#0 = #1#48;
          #300#1 = #1#0;
          |GRABA 020#300n;
     |FINSI;
|FPRC;

|PROCESO conimpr1;|TIPO 0;
     |HAZ EsMecanogra;
     |SI FSalida = 0; || Modulo Master Panel: 003679
          |HAZ Meca1Fecha; |HAZ CalculaSemana; enSemana = nSemana;
     |FINSI;

     |SI nSwPackList = 0;
          eaDoc = "PR" + #1#48 + #1#0;
          |HAZ PackListVar;
     |FINSI;

     aDivisa = #agifa146#65;
     aMoneda = #agifa146#67;
     |HAZ Divisas146;

     |SI #1#73 < #0#7; |O #1#73 > #0#8;
          |ERROR;
          |ACABA;
     |FINSI;

     |SI #3#0 ! #1#3;
          #3#0 = #1#3;|LEE 021#3=;
          |SI 0 ! FSalida;|DDEFECTO #3;|FINSI;
     |FINSI;

     |ABRE #40;
     #40#26 = #1#48;
     |LEE 001#40=;
     |SI FSalida ! 0;  |DDEFECTO #40;  |FINSI;
     |CIERRA #40;

     informe = #40#20; |QBF informe;
     informe1 = (informe % 0104) + "5" + (informe % 0603); |QBF informe1;

     iva1 = #1#50; re1 = #1#51;
     iva2 = #1#52; re2 = #1#53;
     iva3 = #1#54; re3 = #1#55;
     iva4 = #1#56; re4 = #1#57;
     iva5 = #1#58; re5 = #1#59;

     |INFOR informe;
     |SI FSalida ! 0; |ERROR10; |ACABA;  |FINSI;

     |SI #910#19 = "S";  |HAZ AbreTemporales;  |FINSI;
|FPRC;

|DEFBUCLE agifa4950;
    #1#2;
    10;
    #0#0, #0#4, #0#9 , #0#2, #0#6;
    #0#1, #0#5, #0#10, #0#3, #0#6;
    be;
    NULL,conimpr1,conimprl,conimpr2;
|FIN;

|PROCESO conim;|TIPO 3;
     |ABRE #3;
     |SI eaImpresora ! "";
          Impresora = eaImpresora;
          |IMPRE -1;
     |SINO;
          |IMPRE 2;
     |FINSI;
     |SI 0 = FSalida;
          |BUCLE agifa4950;
          |FINIMP;
     |FINSI;
     |CIERRA #3;
|FPRC;

|PROGRAMA;
     |HAZ SacaIVA;
     |HAZ MiraLogos; || Copia Logos para el Crystal

     |HAZ EsPackList; nSwPackList = FSalida;
     |HAZ EsTagloma; nSwTagloma = FSalida;

     enPantalla  = 0;

     |ABRE #910; |LEE 000#910p; |CIERRA #910;
     |ABRE #42; |LEE 000#42p; |CIERRA #42;

     |SI nSwTagloma = 0; |Y enGraba300 = 0;
          |HAZ CreaTempo; |NOME_DAT #300 Tempo; aTmp300 = Tempo;
          |DELINDEX #300; |ABRE #300;
     |FINSI;

     |SI numero ! 0;
          #0#9  = serie;
          #0#10 = serie;
          #0#2 = numero;
          #0#3 = numero;
          #0#6 = reimpr;
          |HAZ conim;
     |SINO;
          |MANTE #0;
     |FINSI;

     |SI nSwTagloma = 0; |Y enGraba300 = 0;
          aAlfa = "tagz0028;" + aTmp300;
          |SUB_EJECUTA_NP aAlfa;
          |CIERRA #300; Tempo = aTmp300; |DELINDEX #300;
     |FINSI;

|FPRO;
