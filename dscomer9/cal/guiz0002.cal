|FICHEROS;
     guiz0002 #0;
     infom000 #1;
     guim0003 #3;
     guim0004 #4;
|FIN;

|VARIABLES;
     &eaFic = "";
     &eaAlfa = "";
     nSwPrueba = 0;
     aFicPrueba = "";
     &eaAsunto = "";
     &enPedido = 0;
     aParam = "";
     nLinAnt = 0;
     nErrorPed = 0;
     i = 0;
     nPos = 0;
     aLon = "";
     aFic = "";
     nHandle = 0;
     aValor = "";
     nTipoLin = 0;
     aDato = "";
     nLin = 0;
     aCar = "";
     nSwDato = 0;
     aFinLinea = "";
     aRetLinea = "";
     aTmp = "";

     aDSCorreo = "";
     aUsu = "";
     aPwd = "";
     aDir = "";
     aNombre = "";
     aAlfa = "";
     aAlfa1 = "";
     aBase = "";
     aIP = "";
     aServidor = "";
     aFrom = "";
     aTo = "";
     aAdjunto = "";
     aAsunto = "";
     nError = 0;
     aMensaje = "";
     nCorreos = 0;
     aPath = "";
     aPath1 = "";
     nIniLin = 0;
     nNumLinea = 0;
     nOk = 0;
     aNuevo = "";
     nCar = 0;
     aDjunto = "";
     aSubject = "";
     aIpCorreo = "";
     aTipo = "";
     aNom = "";
     aOrg = "";
     aDes = "";
|FIN;

|PROCESO ErrorLineas;
     aTo = #1#23; |QBF aTo;
     |SI aTo = ""; |ACABA; |FINSI;

     aDjunto = "vacio.txt";
     aSubject = "Error, lineas pedidos diferentes. Pedido:" + #3#0;
     aMensaje = aSubject;

     aIpCorreo = "";
     |SI #1#6 = "L";
          |IP_REMOTO aIpCorreo;
     |SINO;
          |SI #1#6 = "S";
               |IP_LOCAL aIpCorreo;
          |SINO;
               aIpCorreo = #1#5; |QBF aIpCorreo;
          |FINSI;
     |FINSI;
     aServidor = #1#7; |QBF aServidor;
     aFrom = aTo;

     |DSCORREO_ENVIA aIpCorreo, aServidor, aFrom, aTo, aSubject, aMensaje, aDjunto;
     |SI FSalida ! 0; |Y aParam = "";
          |MENAV "0000Error al enviar correo....";
     |FINSI;
|FPRC;

|DEFBUCLE ErrorLineas;
     #3#2;
     ;
     "N", 000001;
     "N", 999999;
     ;
     NULL, ErrorLineas;
|FIN;

|PROCESO QuitaBlancoIzq;
     aAlfa1 = "";
     aLon = aAlfa % A0;
     |PARA i = 1; |SI i [ aLon; |HACIENDO i = i + 1;
          nPos = i * 100 + 1;
          aCar = aAlfa % nPos;
          |SI aCar = " "; |Y aAlfa1 = "";
               || quita 1§ blanco a la izq
          |SINO;
               aAlfa1 = aAlfa1 + aCar;
          |FINSI;
     |SIGUE;
     aAlfa = aAlfa1;
|FPRC;

|PROCESO QuitaBr;
     |PARA; |SI; |HACIENDO;
          aAlfa1 = aAlfa;
          aAlfa = aAlfa - "<br>";
          aAlfa = aAlfa - "<BR>";
          aAlfa = aAlfa - "<Br>";
          aAlfa = aAlfa - "<bR>";
          |SI aAlfa1 = aAlfa; |SAL; |FINSI;
     |SIGUE;
|FPRC;

|PROCESO QuitaRaros;
     |QBF aValor;
     eaAlfa = aValor;
     |HAZ QuitaRarosImp;
     aValor = eaAlfa;
|FPRC;

|PROCESO Campo;
     |QBF aValor;
     |SI aValor = ""; |ACABA; |FINSI;

     aAlfa = aValor; |HAZ QuitaBlancoIzq; aValor = aAlfa;
     aAlfa = aDato; |HAZ QuitaBlancoIzq; aDato = aAlfa;

     aAlfa = aValor; |HAZ QuitaBr; aValor = aAlfa;
     aAlfa = aDato; |HAZ QuitaBr; aDato = aAlfa;

     |HAZ QuitaRarosImp;

     |SI nTipoLin = 1;
          |SI aDato = "CODCLI"; |QBT aValor; #3#1 = ("000000" + aValor) % -106; |FINSI;
          |SI aDato = "CIF"; |QBT aValor; #3#2 = aValor; |FINSI;
          |SI aDato = "RS"; #3#3 = aValor; |FINSI;
          |SI aDato = "IDCLIENTE"; |QBT aValor; #3#10 = aValor; |FINSI;
          |SI aDato = "CODENTREGA"; |QBT aValor; #3#11 = aValor; |FINSI;
          |SI aDato = "FECHA"; |QBT aValor; #3#4 = aValor; #3#5 = aValor % -108; |FINSI;
          |SI aDato = "NPED"; |QBT aValor; #3#0 = aValor; |FINSI;
          |SI aDato = "COM"; #3#6 = aValor; |FINSI;
          |SI aDato = "DIR"; #3#7 = aValor; |FINSI;
          |SI aDato = "AGENCIA"; |QBT aValor; #3#8 = aValor; |FINSI;
          |SI aDato = "NOTAS"; #3#9 = aValor; |FINSI;
          |SI aDato = "IMPORTECOBRADO";  #3#12 = aValor; |FINSI;

          ||aAlfa = "0000" + aDato + "=" + aValor; |MENAV aAlfa;
     |SINO;
          #4#0 = #3#0;
          #4#1 = nLin;
          |SI aDato = "CODPROD"; |QBT aValor; #4#2 = aValor; |FINSI;
          |SI aDato = "REF"; |QBT aValor; #4#3 = aValor; |FINSI;
          |SI aDato = "DESC"; #4#4 = aValor; |FINSI;
          |SI aDato = "LPI"; |QBT aValor; #4#8 = aValor; |FINSI;
          |SI aDato = "CANT"; |QBT aValor; #4#5 = aValor; |FINSI;
          |SI aDato = "PRECIOUNI"; |QBT aValor; #4#6 = aValor; |FINSI;
          |SI aDato = "NAME"; |QBT aValor; #4#7 = aValor; |FINSI;
          |SI aDato = "PORTES"; |QBT aValor; #4#9 = aValor; |FINSI;
          |SI aDato = "PROMO"; |QBT aValor; #4#10 = aValor; |FINSI;

          ||aAlfa = "0000" + aDato + "=" + aValor; |MENAV aAlfa;
     |FINSI;
|FPRC;

|PROCESO Caracter;
     |SI aCar = aRetLinea; |ACABA; |FINSI;
     |SI aCar = aFinLinea; |ACABA; |FINSI;

     |SI aCar = "=";
          nSwDato = 1; aValor = ""; |ACABA;
     |FINSI;
     |SI nSwDato = 1;
          |SI aCar = ";"; |||O aCar = aFinLinea; -> ahora siempre trae un ";"
               |HAZ Campo;
               nSwDato = 0; aDato = ""; aValor = ""; |ACABA;
          |FINSI;
     |FINSI;
     |SI aCar = aFinLinea; |ACABA; |FINSI;

     |SI nSwDato = 0;
          aDato = aDato + aCar;
          |QBT aDato;
     |SINO;
          aValor = aValor + aCar;
     |FINSI;


     aAlfa = aDato; |HAZ QuitaBr; aDato = aAlfa;

     |SI aDato = "CABECERA:";
          nLin = 1;
          nTipoLin = 1; aDato = ""; aValor = "";
     |FINSI;

     |SI aDato = "LPED";
          nIniLin = 1;
     |FINSI;

     |SI nNumLinea > 0;
          nLinAnt = nNumLinea;
     |FINSI;
     nNumLinea = 0;
     |SI nIniLin = 1; |Y aCar = ":";
          aAlfa = aDato - "LPED";
          aAlfa = aAlfa - ":";
          nNumLinea = aAlfa;
          nIniLin = 0;
     |FINSI;

     |SI nNumLinea > 0;
          |SI nLin = 1;
               #3#13 = "N";
               |GRABA 040#3n;
               |SI FSalida ! 0;
                    aAlfa = "0000Pedido Existente:" + #3#0;
                    |SI aParam = "";
                         |MENAV aAlfa;
                    |SINO;
                         |MENSA aAlfa;
                    |FINSI;
                    nErrorPed = 1;
               |FINSI;
          |SINO;
               #4#1 = nLinAnt;
               |GRABA 040#4n;
               |SI FSalida ! 0;
                    aAlfa = "0000Linea Pedido Existente:" + #4#0 + "/" + #4#1;
                    |SI aParam = "";
                         |MENAV aAlfa;
                    |SINO;
                         |MENSA aAlfa;
                    |FINSI;
                    nErrorPed = 1;
               |FINSI;
               |DDEFECTO #4;
          |FINSI;
          nTipoLin = 2; aDato = ""; aValor = "";
          nLin = nLin + 1;
     |FINSI;
|FPRC;

|PROCESO ProcesarCorreo;
     aRetLinea = &13;
     aFinLinea = &10;

     eaAsunto = aAsunto;
     |HAZ InforprCliDir;
     |SI FSalida = 0; |ACABA; |FINSI;

     nLin = 0;
     nErrorPed = 0;
     nTipoLin = 0;
     aDato = "";
     aValor = "";
     |ABRE #3;
     |ABRE #4;
     |DDEFECTO #3;
     |DDEFECTO #4;

     |SI #1#24 = "S";
          aFic = #1#12; |QBF aFic;
          aFic = aFic + "_cuerpo_.txt"; || Nombre del fichero con el asunto
     |FINSI;

     |SI nSwPrueba = 1; aFic = aFicPrueba; |FINSI;

     |XABRE "B", aFic, nHandle;
     |SI FSalida ] 0;
          |XLEE nHandle, 250, aTmp;
          |PARA; |SI FSalida > 0; |HACIENDO;
               |PARA; |SI aTmp ! ""; |HACIENDO;
                    aCar = aTmp % 101;
                    |SI aTmp = aCar;
                         aTmp = ""; ||con un caracter falla: "aTmp = aTmp % 2"
                    |SINO;
                         aTmp = aTmp % 2;
                    |FINSI;
                    |HAZ Caracter;
               |SIGUE;
               |XLEE nHandle, 250, aTmp;
          |SIGUE;
          |XCIERRA nHandle;

          |SI #1#24 = "S";
               aAlfa = #1#12; |QBF aAlfa;
               aAlfa = aAlfa + "correo" + nCorreos + ".txt";
               ||  |COPIA_FICHERO aFic, aAlfa;
               |SI nSwPrueba ! 1; |FBORRA aFic; |FINSI;
          |FINSI;
     |FINSI;
     |SI nSwDato = 1; |HAZ Campo; |FINSI;

     |SI nTipoLin > 0;
          #4#1 = nLinAnt;
          |GRABA 040#4n;
          |SI FSalida ! 0;
               aAlfa = "0000Linea Pedido Existente:" + #3#0 + "/" + #3#1;
               |SI aParam = "";
                    |MENAV aAlfa;
               |SINO;
                    |MENSA aAlfa;
               |FINSI;
               nErrorPed = 1;
          |FINSI;
     |FINSI;
     |CIERRA #3;
     |CIERRA #4;

     |SI nSwPrueba = 1; |ACABA; |FINSI;

     |SI nErrorPed = 0;
          enPedido = #3#0;
          aAlfa = "guiz0003;" + aParam;
          |SUB_EJECUTA_NP aAlfa;
          |BUCLE ErrorLineas;
     |FINSI;
|FPRC;

|PROCESO SacaNombre;
     aLon = aFic % A0;
     aNom = "";
     |PARA i = 1; |SI i [ aLon; |HACIENDO i = i + 1;
          nPos = -(i * 100 + 1)
          aCar = aFic % nPos;
          |SI aCar = "\"; |O aCar = "/"; |SAL; |FINSI;
          aNom = aCar + aNom;
     |SIGUE;
|FPRC;

|PROCESO ProcesaFic;
     |HAZ SacaNombre;
     aAsunto = "";
     aNom = aNom < "";

     aAlfa = aNom - "alta_cliente.txt";
     |SI FSalida > 0; aAsunto = "Alta cliente"; |FINSI;

     aAlfa = aNom - "modificacion_cliente.txt";
     |SI FSalida > 0; aAsunto = "Modificacion cliente"; |FINSI;

     aAlfa = aNom - "alta_direccion_entrega.txt";
     |SI FSalida > 0; aAsunto = "Alta direccion entrega"; |FINSI;

     aAlfa = aNom - "modificacion_direccion_entrega.txt";
     |SI FSalida > 0; aAsunto = "Modificacion direccion entrega"; |FINSI;

     aAlfa = aNom - "pedido";
     |SI FSalida > 0; aAsunto = "Pedido"; |FINSI;

     |SI aAsunto ! "";
          eaFic = aFic;
          |HAZ ProcesarCorreo;
          aOrg = aFic;

          aPath1 = #1#27; |QBF aPath1;
          aAlfa = aPath1 % -101;
          |SI aAlfa ! "/"; |Y aAlfa ! "\"; aPath1 = aPath1 + "/"; |FINSI;
          |MKDIR aPath1;

          aDes = aPath1 + aNom;
          |SI #1#25 = "S";
               |RENOMBRA_FICHERO aOrg, aDes;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Recoge;
     aDir = #1#12; |QBF aDir;
     |MKDIR aDir;

     |SI #1#6 = "F";
          aDSCorreo = #1#5;  |QBF aDSCorreo;
     |SINO;
          |SI #1#6 = "L"; |IP_REMOTO aDSCorreo; |FINSI;
          |SI #1#6 = "S"; |IP_LOCAL aDSCorreo; |FINSI;
     |FINSI;
     aUsu = #1#10; |QBF aUsu;
     aPwd = #1#11; |QBF aPwd;
     aServidor = #1#9; |QBF aServidor;
     aNombre = "";

     aAlfa = "0000Recibiendo [" + aDSCorreo + "]";
     |MENSA aAlfa;
     |DSCORREO_RECIBE aDSCorreo, aServidor, aUsu, aPwd, aDir, aNombre;
     nError = FSalida;
     aAlfa = FSalida;
     aAsunto = "";
     nOk = 0;
     |PARA i = 1; |SI i [ 50; |HACIENDO i = i + 1;
          nPos = i * 100 + 1;
          aCar = aAlfa % nPos;
          |SI nOk = 0;
               |SI aCar = "]"; nOk = 1; |FINSI;
          |SINO;
               aAsunto = aAsunto + aCar;
          |FINSI;
     |SIGUE;

     |BLIN 4;
     |SI nError < 0;
          aMensaje = "    Problemas al recibir correo ..." + nError;
          |SI aParam = "";
               |MENAV aMensaje;
          |SINO;
               |MENSA aMensaje;
          |FINSI;
     |FINSI;
     |SI nError = 0;
          aMensaje = "    Correo recibido con exito [" + nError + "][" + aNombre + "]";
          |MENSA aMensaje;
     |FINSI;
     |SI nError = 1;
          |SI nCorreos = 1;
               |SI aParam = "";
                    |MENAV "    No hay correo ...";
               |SINO;
                    |MENSA "    No hay correo ...";
               |FINSI;
          |SINO;
               nCorreos = nCorreos - 1;
               aAlfa = "0000" + nCorreos + " Correos recibidos...";
               |SI aParam = "";
                    |MENAV aAlfa;
               |SINO;
                    |MENSA aAlfa;
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Principal;
     |SI #1#24 = "S";
          nCorreos = 0;
          |PARA; |SI; |HACIENDO;
               nError = 0;
               nCorreos = nCorreos + 1;
               |HAZ Recoge;
               |SI nError ! 0;  |SAL; |FINSI;
               |HAZ ProcesarCorreo;
               |SLEEP 2;
          |SIGUE;
     |SINO;
          ||DBASE aBase; aPath = aBase + "001980/correosweb/";

          aPath = #1#26; |QBF aPath;
          aAlfa = aPath % -101;
          |SI aAlfa ! "/"; |Y aAlfa ! "\"; aPath = aPath + "/"; |FINSI;

          |_PDIR aPath;
          |PARA; |SI FSalida = 0; |HACIENDO;
               aFic = aPath;
               |HAZ ProcesaFic;
               |_SDIR aPath;
          |SIGUE;
     |FINSI;
|FPRC;

|PROCESO Tecla; |TIPO 0;
     |SI FSalida = 13;
          aFic = #1#12; |QBF aFic;
          aFic = aFic + "_cuerpo_.txt";
          aAlfa = "0000NCapturar:" + aFic;
          |CONFI  aAlfa;
          |SI FSalida = 0;
               |XABRE "E", aFic, 0;
               |SI FSalida = 0;
                    |HAZ ProcesarCorreo;
               |SINO;
                    |SI aParam = "";
                         |MENAV "0000No existe fichero...";
                    |SINO;
                         |MENSA "0000No existe fichero...";
                    |FINSI;
               |FINSI;
          |FINSI;
          |ERROR;
     |FINSI;
|FPRC;

|PROGRAMA;
/********************************
     aFicPrueba = "/tmp/correo2.txt";
     nSwPrueba = 1;
     |HAZ ProcesarCorreo;
     |ACABA;

     #1#12 = "/tmp/";
     eaAsunto = "Alta cliente";
     |HAZ InforprCliDir;
     |ACABA;
********************************/

     aParam = PARAMETRO;
     aParam = aParam > "";

     |ABRE #1; |LEE 000#1p; |CIERRA #1;

     |SI aParam = "AUTO";
          |HAZ Principal;
     |SINO;
          aParam = "";
          |CLS;
          |DDEFECTO #0;
          |PINPA #0#0;
          |PINDA #0#0;
          |ENDATOS #1#0;
          |SI FSalida = 0;|Y #0#0 = "SI";
               |HAZ Principal;
          |FINSI;
     |FINSI;
|FPRO;

/* EJEMPLO DE CORREO

CABECERA: CODCLI= 0; CIF= SARA; RS= Sara Martínez;
FECHA= 05/01/2007 16:20:13; NPED= 69114; COM= RAMON MARTI - SARA;
DIR= AVDA. Reyes Católicos nº 7 Of.5;
AGENCIA= DHL;
NOTAS= . 2 cintas del Gr 8 2 cintas citizen DP-600;
IDCLIENTE=6562;
CODENTREGA=1;

LPED1: CODPROD= 7254; REF= C7115X; DESC= HP Laserjet 1000W/1005/1200/1220/ 3300/3320/ 3330/3380 Toner Alta Capacidad, 3.500 Páginas; LPI= 0; CANT= 1; PRECIOUNI= 49,98; NAME= .
LPED2: CODPROD= 5547; REF= TN-8000; DESC= Brother MFC-9070/9160/9180, Fax 8070P Toner, 2.200 páginas; LPI= 0; CANT= 1; PRECIOUNI= 24,04; NAME= .
LPED3: CODPROD= 60041; REF= 09218377; DESC= Ampliacion de memoria OKI de 256MB para impresoras laser C3100/C3200/C5100/C5200/C5510MFP; LPI= 0; CANT= 1; PRECIOUNI= 77,57; NAME= .
LPED4: CODPROD= 60001; REF= 42158512; DESC= Ban deja Adicional OKI para impresoras Laser Serie C5000; LPI= 0; CANT= 1; PRECIOUNI= 377,86; NAME= .
LPED5: CODPROD= 40002; REF= 01161601; DESC= Impresora OKI Laser Color C5250N; LPI= 0; CANT= 1; PRECIOUNI= 275,08; NAME= .
LPED6: CODPROD= 0000; REF= NS; DESC= N SERIE; LPI= 0; CANT= 1; PRECIOUNI= 0; NAME= .
LPED7: CODPROD= 7526; REF= KX-PDP1; DESC= Revelador PANASONIC 4450/4451/4455 ; LPI= ; CANT= 1; PRECIOUNI= 102,27; NAME= .
LPED8: CODPROD= 4422; REF= PFA300; DESC= TRANSFER PHILIPS MAGIC 1 PFA-300/301; LPI= 0; CANT= 1; PRECIOUNI= 25,03; NAME= .
LPED9: CODPROD= 5686; REF= CLI-8C; DESC= Canon Pixma IP4200/5200/5200R/6600D, MP-500/800 Cartucho Cián; LPI= 0; CANT= 1; PRECIOUNI= 9; NAME= .
LPED10: CODPROD= 24360; REF= 43244; DESC= CD-R VERBATIM 700Mb 32x DataLifePlus Ultra Speed(Pack 10); LPI= 2,1; CANT= 1; PRECIOUNI= 10,85; NAME= .
LPED11: CODPROD= 24212; REF= 22-22360-0; DESC= IMATION USB Flash "Pivot" 2GB; LPI= 0; CANT= 1; PRECIOUNI= 39,48; NAME= .
LPED12: CODPROD= 7303; REF= TK-20H; DESC= Toner KYOCERA-MITA FS-1700/FS-1750/FS-3700/FS-3750 ; LPI= 0; CANT= 1; PRECIOUNI= 79,69; NAME= .
LPED13: CODPROD= 4087; REF= 1-46156-3; DESC= Cartucho de Datos IMATION DC 6525 525Mb -SLR-2-.; LPI= 0; CANT= 1; PRECIOUNI= 25,68; NAME= .
LPED14: CODPROD= 24967; REF= 405533; DESC= RICOH GX3000 GX3000 Tinta gel Type GC-21C Cian; LPI= 0; CANT= 1; PRECIOUNI= 31,06; NAME= .
LPED15: CODPROD= 3859; REF= 12A7415*; DESC= Toner LEXMARK T-420 Prebate, Unidad de Impresión Allto Rendimiento; LPI= 0; CANT= 1; PRECIOUNI= 129,19; NAME= .
LPED16: CODPROD= 20751; REF= 12A7468; DESC= Toner LEXMARK T-630/T-632/T-634 Alto Rendimiento Retornable ETIQUETAS ; LPI= 0; CANT= 1; PRECIOUNI= 223,87; NAME= .
LPED17:PORTES=PAGADOS
*/
