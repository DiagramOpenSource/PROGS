|FICHEROS;
     dsz99972 #0;   || este def
     diefam01@ #1;   || Comisiones / tarifas
     agifa019 #19;
     agifa023 #23;

     agifa071 #71;
     agifa073 #73;
     agifa069 #69;
     agifa070 #70;
     agifa147 #147;
     diefa003@ #3;   ||Promociones
     agifa010 #10;
     diefaw01@ #20;

     diefa007@ #7;
     diefaw06 #6,MANTE;
     agifa024 #24;
     agifa112 #112;
     agifa081 #81;
     agifa067 #67;
     diefaw15@ #15;       ||Seleccion Lin Regalo
     agifa104 #104;
     agifa142 #142;
|FIN;

|VARIABLES;
     &eaTipo = "";
     nServi = 0;
     nUnidades = 0;
     nSwHayLin = 0;
     aTexto49 = "";
     aArticulo = "";
     aMensaje = "";
     &enDesprecia = 0;
     {-1}aMenu = "";
     aMenu1    = "2400";
     aMenu2    = "1";
     aMenu3    = "Despreciar:  ";
     aMenu4    = "NDIF";
     aMenu5    = "  NO  ";
     aMenu6    = " Despreciar ";
     aMenu7    = " Incidencia ";
     aMenu8    = " F. Entrega ";

     nCargaDef   = 0;
     nImpDiv     = 0;
     nMult       = 0;
     &nDeci_IVADiv = 0;
     &enCoste    = 0;
     nNume       = 0;
     nRegalo     = 0;
     &enForma    = 0;
     &enIva      = 0;
     &pvp_ve     = 0;
     &iva_dt     = 0;
     &eaPrv      = "";
     &enTiva     = 0;
     &efFiva = @;
     &tarifa     = 0;
     &enComision = 0;
     &eaRepre    = "";
     &art_dt     = "";
     &cli_dt     = "";
     &fed_dt     = @;
     nHaLeido    = 1;
     i           = 0;
     nTarifa     = 0;
     nModo       = 0;
     aProg       = "";
     pcs         = 0;
     pcv         = 0;
     pcd         = 0;
     nTariAnt    = 0;
     aTempo20    = "";
     aTempo15    = "";
     nLin        = 0;
     nSwPromo    = 0;
     aAlfa       = "";

     nLinea      = 0;
     aTempo99972 = "";
     &Tempo      = "";

     aDirD       = "";
     aDirE       = "";
     &eaCliente = "";
     nNumPed = 0;
     aSerPed = "";
     nLinPed = 0;
     nEsAdjPed = 0;
|FIN;
컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
|PROCESO ComisionDiexpa;
     |SI nCargaDef = 0; |HAZ CargaDefs1172; |FINSI;
     |ABRE #1; |DDEFECTO #1;
     #1#0 = eaRepre;
     |LEE 000#1=;
     |SI FSalida = 0;
          |PARA i=2; |SI i < 8; |HACIENDO i = i + 1;
               nTarifa = i - 1;
               |SI tarifa = nTarifa;    || si es la de la tarifa introducida
                    enComision = #1i;   || la devuelve en esta variable
                    |SAL;
               |FINSI;
          |SIGUE;
          |HAZ ClienArti;
          |SI nHaLeido = 1;
               enComision = #1#8;
          |FINSI;
     |FINSI;
     |CIERRA #1;
     |SI nCargaDef = 0; |HAZ DescargaDefs1172; |FINSI;
|FPRC;
컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
|PROCESO SacaTarifa;              || USA EL CAMPO 'INTERNO'
     |MODULO aProg;
     |SI nModo = 0; |ACABA; |FINSI;
     |SI aProg = "agifa010"; tarifa = #71#61; |FINSI;
     |SI aProg = "agifa104"; tarifa = #73#53; |FINSI;
     |SI aProg = "agifa036"; tarifa = #70#36; |FINSI;
     |SI aProg = "agifa084"; tarifa = #69#43; |FINSI;
     |SI aProg = "agifa146"; tarifa = #147#34; |FINSI;
     nTariAnt = tarifa;
     #0#0 = tarifa;
|FPRC;

|PROCESO PonTarifa;               || USA EL CAMPO INTERNO
     |SI aProg = "agifa010"; #71#61 = tarifa; |FINSI;
     |SI aProg = "agifa104"; #73#53 = tarifa; |FINSI;
     |SI aProg = "agifa036"; #70#36 = tarifa; |FINSI;
     |SI aProg = "agifa084"; #69#43 = tarifa; |FINSI;
     |SI aProg = "agifa146"; #147#34 = tarifa;|FINSI;
|FPRC;

|PROCESO ClienArti;
     nHaLeido = 0;
     |ABRE #23;
     #23#0 = cli_dt;     || cliente
     #23#1 = art_dt;     || articulo
     |LEE 000#23=;
     |SI FSalida = 0; |Y fed_dt ] #23#28; |Y fed_dt [ #23#29;
          nHaLeido = 1;
     |FINSI;
     |CIERRA #23;
|FPRC;

|PROCESO TarifaDiexpa;
     |PUSHV 0501 2380;
     |BLANCO 931 1144;
     |HAZ ClienArti;
     |PINPA #0#0;
     |SI nHaLeido = 1;
          #0#0 = 0;
     |SINO;
          #0#0 = tarifa;
     |FINSI;
     |HAZ SacaTarifa;
     |PINDA #0#0;
     |ENCAMPO #0#0;
     |SI FSalida = 0;
          tarifa = #0#0;
     |FINSI;
     |POPV;
     |HAZ PonTarifa;
     nModo = 0;
|FPRC;

|PROCESO ModiTariDiexpa;
     nModo = 1;
     |HAZ TarifaDiexpa;

     |SI nTariAnt = tarifa; |ACABA; |FINSI; || No se modifico!!

     |ABRE #19;
     #19#0 = art_dt;
     |LEE 000#19=;
     |CIERRA #19;

     |SI tarifa = 1; pcs = 130; pcv = 18;  pcd = 21;  |FINSI;
     |SI tarifa = 2; pcs = 131; pcv = 19;  pcd = 22;  |FINSI;
     |SI tarifa = 3; pcs = 132; pcv = 20;  pcd = 23;  |FINSI;
     |SI tarifa = 4; pcs = 153; pcv = 147; pcd = 150; |FINSI;
     |SI tarifa = 5; pcs = 154; pcv = 148; pcd = 151; |FINSI;
     |SI tarifa = 6; pcs = 155; pcv = 149; pcd = 152; |FINSI;
     pvp_ve = #19pcv;
     |SI #19pcs = "S";
          eaPrv = "";
          enTiva = iva_dt;
          efFiva = ~;
          |HAZ IVAPrv;
          pvp_ve = pvp_ve / ( 1 + (enIva/100));  ||quito el iva
     |FINSI;

|FPRC;
컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
|PROCESO CreaLinAlb;
     |ABRE #19;
     #19#0 = #20#0;
     |LEE 020#19=;
     |CIERRA #19;

     nLin = nLin + 1;
     |DDEFECTO #71;
     #71#29 = #10#52;
     #71#0 = #10#0;
     #71#1 = nLin;
     #71#2 = #19#0;
     #71#3 = 1;          ||Almacen
     #71#4 = #19#1;
     #71#5 = #20#4;      ||Uni
     #71#13 = #19#2;
     #71#14 = #19#9 * #71#5;
     #71#15 = #10#3;
     #71#16 = #10#34;
     #71#17 = #10#35;
     #71#49 = "REGALO";  || ID en Campo Partida
     |GRABA 020#71n;

     enForma = 1;
     |HAZ AcumulaAV;
|FPRC;

|DEFBUCLE CreaLinAlb;
     #20#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, CreaLinAlb;
|FIN;

|PROCESO LineasAlba;
     nLin = #71#1;

     #3#0 = #10#3;
     #3#1 = #71#2;
     |LEE 101#3=;
     |SI FSalida = 0; |Y #10#1 ] #3#4; |Y #10#1 [ #3#5;
          #3#8 = #3#8 + #71#5;
          #3#9 = #3#9 + #71#5;
          |SI #3#8 ] #3#6; |Y #3#6 > 0;
               nRegalo = 0;
               |PARA; |SI #3#8 ] #3#6; |HACIENDO;
                    nRegalo = nRegalo + #3#7;
                    #3#8 = #3#8 - #3#6;
               |SIGUE;
               #3#10 = #3#10 + nRegalo;

               nSwPromo = 1;

               |DDEFECTO #20;
               #20#0 = #71#2;
               |LEE 101#20=;
               |SI FSalida ! 0; |GRABA 020#20b; |FINSI;
               #20#4 = #20#4 + nRegalo;
               |GRABA 020#20a;
               |LIBERA #20;
          |FINSI;
          |GRABA 020#3a;
     |FINSI;
     |LIBERA #3;
|FPRC;

|PROCESO LineasAlba2;
     #3#0 = #10#3;
     #3#1 = #71#2;
     |LEE 000#3=;
     |SI FSalida = 0; |Y #10#1 ] #3#4; |Y #10#1 [ #3#5;
          |DDEFECTO #20;
          #20#0 = #71#2;
          |LEE 101#20=;
          |SI FSalida ! 0;
               nRegalo = 0;
               |SI #3#8 ] #3#6; |Y #3#6 > 0;
                    nUnidades = #3#8;
                    |PARA; |SI nUnidades ] #3#6; |HACIENDO;
                         nRegalo = nRegalo + #3#7;
                         nUnidades = nUnidades - #3#6;
                    |SIGUE;
                    nSwPromo = 1;
               |FINSI;
               #20#4 = #20#4 + nRegalo;
               |GRABA 020#20n;
          |FINSI;
          |LIBERA #20;
     |FINSI;
|FPRC;


컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
|PROCESO BorPromoDiexpa;
     |HAZ CargaDefs1172;
     |ABRE #3;
     #3#0 = #10#3;
     #3#1 = #71#2;
     |LEE 101#3=;
     |SI FSalida = 0; |Y #10#1 ] #3#4; |Y #10#1 [ #3#5;
          aAlfa = #71#49; |QBF aAlfa;
          |SI aAlfa = "REGALO";
               #3#10 = #3#10 - #71#5;
          |SINO;
               #3#8 = #3#8 - (#3#9 - (#3#10/#3#7*#3#6));
               #3#9 = #3#9 - #71#5;
          |FINSI;
          |GRABA 020#3a;
     |FINSI;
     |CIERRA #3;
     |HAZ DescargaDefs1172;
|FPRC;

|PROCESO AcuPromoDiexpa;
     |HAZ CargaDefs1172;
     |HAZ CreaTempo; aTempo20 = Tempo;
     |NOME_DAT #20 aTempo20; |DELINDEX #20;

     nSwPromo = 0;
     |ABRE #20;
     |ABRE #3;
     |BUCLELIN 0 LineasAlba #10;
     |CIERRA #3;
     |CIERRA #20;

     |SI nSwPromo ! 0;
          |ABRE #71;
          |BUCLE CreaLinAlb;
          |CIERRA #71;
     |FINSI;

     |DELINDEX #20; Tempo = aTempo20; |HAZ BoraTempo;
     |HAZ DescargaDefs1172;
|FPRC;
컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
|PROCESO AdjFalta;
     |SI #6#7 = "N";
          |ACABA;
     |FINSI;
     #7#15 = #6#11;
     #7#0 = #6#8;
     #7#1 = #6#9;
     #7#2 = #6#10;
     |LEE 121#7=;
     |SI FSalida = 0;
          #7#13 = #6#7;
          |GRABA 020#7a;
          |LIBERA #7;
     |FINSI;
     |SI #6#7 = "D"; |ACABA; |FINSI;

     |DDEFECTO #19;
     |ABRE #19;
     #19#0 = #6#1;
     |LEE 020#19=;
     |CIERRA #19;

     |ABRE #24;
     #24#0 = #10#3;
     |LEE 020#24=;
     |CIERRA #24;

     nLinea = nLinea + 1;
     |DDEFECTO #71;
     #71#29 = #10#52;
     #71#0 = #10#0;
     #71#1 = nLinea;
     #71#2 = #19#0;
     #71#3 = 1;
     #71#4 = #19#1;
     #71#5 = #7#8;
     #71#6 = #7#9;
     #71#7 = #71#5 * #71#6;
     #71#8 = #7#10;
     tarifa = #7#12;
     enComision = #19#31;
     eaRepre = #10#6;
     cli_dt = #10#3;
     art_dt = #71#2;
     fed_dt = #10#1;
     nCargaDef = 1;
     |HAZ ComisionDiexpa;
     nCargaDef = 0;
     #71#10 = enComision;
     #71#11 = #19#30;
     #71#13 = #19#2;
     #71#14 = #71#5 * #19#9;
     #71#15 = #10#3;
     #71#16 = #10#34;
     #71#17 = #24#4;
     #71#33 = #7#11;
     |SI #19#140 = 1; #71#34 = #19#139; |FINSI;
     |SI #19#142 = 1; #71#34 = #19#141; |FINSI;
     |SI #19#144 = 1; #71#34 = #19#143; |FINSI;
     |SI #19#146 = 1; #71#34 = #19#145; |FINSI;
     #71#35 = #71#5;
     #71#36 = #71#6;
     #71#61 = tarifa;
     |SI eaTipo = "A";        || De albaran o adjudicacion
          enForma = 1;
          |HAZ AcumulaAV;
          #71#14 = enCoste;
          |HAZ TotalLin71;
          |SI nEsAdjPed = 1;
               nServi = #71#5;
               |HAZ CreaLinPe;
               #71#31 = #73#28;
               #71#12 = #73#0;
               #71#21 = #73#1;
               #10#41 = #10#41 + 1;
          |FINSI;
          |HAZ CalCabAgifa010;
          |GRABA 020#71n;
     |SINO;                   || De pedido
          aSerPed = #104#25;
          nNumPed = #104#0;
          nServi = 0;
          |HAZ CreaLinPe;
          enForma = 1;
          |HAZ AcumulaPV;
          |HAZ CalCabAgifa104;
     |FINSI;
|FPRC;

|DEFBUCLE AdjFalta;
     #6#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, AdjFalta;
|FIN;

|PROCESO diefa007;
     nLinea = nLinea + 1;

     |DDEFECTO #19;
     #19#0 = #7#4;
     |LEE 020#19=;

     #6#0 = nLinea;
     #6#1 = #7#4;
     #6#2 = #19#1;
     #6#3 = #7#3;
     #6#4 = #7#6;
     #6#5 = #7#7;
     #6#6 = #7#8;
     #6#7 = "N";
     #6#8 = #7#0;
     #6#9 = #7#1;
     #6#10 = #7#2;
     #6#11 = #7#15;
     |GRABA 020#6n;
|FPRC;

|DEFBUCLE diefa007;
     #7#2002;
     ;
     eaCliente, "N";
     eaCliente, "N";
     ;
     NULL, diefa007;
|FIN;

|PROCESO UltLinPeDie;
     #73#28 = #104#25;
     #73#0 = #104#0;
     #73#1 = 999;
     |LEE 000#73];
     |SI FSalida = 0;
          |LEE 000#73a;
     |SINO;
          |LEE 000#73u;
     |FINSI;
     |SI FSalida = 0; |Y #73#28 = #104#25; |Y #73#0 = #104#0;
          nLin = #73#1;
     |SINO;
          nLin = 0;
     |FINSI;
|FPRC;

|PROCESO AdjDiexpa;
     |HAZ CreaTempo; aTempo99972 = Tempo;
     |NOME_DAT #6 aTempo99972; |DELINDEX #6;

     nLinea = 0;
     |HAZ CargaDefs1172;
     |ABRE #6;
     |ABRE #19;
     |BUCLE diefa007;
     |CIERRA #6;
     |CIERRA #19;
     |HAZ DescargaDefs1172;
     |SI nLinea ! 0;
          |PUSHV 0501 2380;
          |BLANCO 907 2380;
          |ENTLINEAL #1#6, -1, 4, 20, 1, Min6, Max6;
          |POPV;
          |SI eaTipo = "A";
               |HAZ UltimaLineaDie;
          |SINO;
               |HAZ UltLinPeDie;
          |FINSI;
          nLinea = nLin;
          |HAZ CargaDefs1172;
          |ABRE #7;
          nEsAdjPed = 0;
          |BUCLE AdjFalta;
          |CIERRA #7;
          |HAZ DescargaDefs1172;
     |FINSI;

     |DELINDEX #6; Tempo = aTempo99972; |HAZ BoraTempo;
|FPRC;

|PROCESO Min6;
     #6#0 = 1;
|FPRC;

|PROCESO Max6;
     #6#0 = 999;
|FPRC;

|PROCESO Diefa6_0; |TIPO 0;
     nNume = Contenido;
     |SI FSalida = 0; |Y nNume = #6#0;
          |SI #6#7 ! "N";
               #6#7 = "N";
          |SINO;
               #6#7 = "S";
          |FINSI;
          |GRABA 020#6a;
          |PINTA #6#7;
     |FINSI;
|FPRC;

|PROCESO Diefa6_11; |TIPO 11;
     |SI FSalida = 10;
          #6#7 = "D";
          |GRABA 020#6a;
          |PINTA #6#7;
     |FINSI;
|FPRC;


|PROCESO infw15;
     #diefaw15@#0 = "                    ";
|FPRC;

|PROCESO supw15;
     #diefaw15@#0 = "zzzzzzzzzzzzzzzzzzzz";
|FPRC;

|PROCESO LineasRegalo;
     |SI #agifa010#38 = "N";
          |HAZ CargaDefs1172;
          |ABRE #diefa003@;
          |HAZ CreaTempo; aTempo20 = Tempo;
          |NOME_DAT #20 aTempo20; |DELINDEX #20;
          |HAZ CreaTempo; aTempo15 = Tempo;
          |NOME_DAT #15 aTempo15; |DELINDEX #15;

          ||TODO CCC
          nSwHayLin = 0;
          |HAZ CalLinReg;                 ||Calcula lineas de Regalo
          |SI nSwHayLin = 1;
               |PUSHV 0501 2380;
               |PINPA #0#diefaw15@;
               |ENTLINEAL #1#15, -1, 2, 11, 1, infw15, supw15;
               |POPV;
               |HAZ MeteLinReg;                ||Mete las lineas de Regalo
          |FINSI;
          |DELINDEX #20; Tempo = aTempo20; |HAZ BoraTempo;
          |DELINDEX #15; Tempo = aTempo15; |HAZ BoraTempo;
          |CIERRA #diefa003@;
          |HAZ DescargaDefs1172;
     |FINSI;
|FPRC;

|PROCESO diefaw15;
     aArticulo = #diefaw15@#0;
     |QBF aArticulo;
     |SI aArticulo ! ""; |Y #diefaw15@#4 = "S";
          #3#0 = #agifa010#3;
          #3#1 = #diefaw15@#0;
          |LEE 101#3=;
          |SI FSalida = 0; |Y #10#1 ] #3#4; |Y #10#1 [ #3#5;
               nRegalo = #diefaw15@#2;
               #3#8 = #3#8 - (nRegalo * (#3#6 / #3#7));     ||Acum. Pendiente
               #3#10 = #3#10 + nRegalo;           ||Acum. Regalo.
               |GRABA 020#3a;
          |FINSI;
          |LIBERA #3;

          |ABRE #19;
          #19#0 = #diefaw15@#0;
          |LEE 020#19=;
          |CIERRA #19;

          nLin = nLin + 1;

          |DDEFECTO #71;
          #71#29 = #10#52;
          #71#0 = #10#0;
          #71#1 = nLin;
          #71#2 = #19#0;
          #71#3 = 1;          ||Almacen
          #71#4 = #19#1;
          #71#5 = #20#4;      ||Uni
          #71#13 = #19#2;
          #71#14 = #19#9 * #71#5;
          #71#15 = #10#3;
          #71#16 = #10#34;
          #71#17 = #10#35;
          #71#49 = "REGALO";  || ID en Campo Partida
          |GRABA 020#71n;

          enForma = 1;
          |HAZ AcumulaAV;
     |FINSI;
|FPRC;

|DEFBUCLE diefaw15;
 #diefaw15@#1001;
 ;
 "                    ";
 "zzzzzzzzzzzzzzzzzzzz";
 ;
 NULL, diefaw15;
|FIN;

|PROCESO MeteLinReg;
     |ABRE #agifa071;
     |HAZ UltimaLineaDie;
     |BUCLE diefaw15;
     |CIERRA #agifa071;
|FPRC;


|PROCESO UltimaLineaDie;         ||Devuelve en nLin el numero de la ultima linea
     #agifa071#29 = #agifa010#52;
     #agifa071#0 = #agifa010#0;
     #agifa071#1 = 999;
     |LEE 000#agifa071.];
     |SI FSalida = 0;
          |LEE 000#agifa071.a;
     |SINO;
          |LEE 000#agifa071.u;
     |FINSI;
     |SI FSalida = 0; |Y #agifa071#29 = #agifa010#52; |Y #agifa071#0 = #agifa010#0;
          nLin = #agifa071#1;
     |SINO;
          nLin = 0;
     |FINSI;
|FPRC;

|PROCESO CreaLinRegalo;
     |ABRE #19;
     #19#0 = #20#0;
     |LEE 020#19=;
     |CIERRA #19;
     ||ARA

     |DDEFECTO #15;
     #15#0 = #20#0;
     #15#1 = #19#1;
     #15#2 = #20#4;
     #15#3 = 0;
     |GRABA 020#15n;
|FPRC;

|DEFBUCLE CreaLinRegalo;
     #20#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, CreaLinRegalo;
|FIN;

|PROCESO CalLinReg;
     nSwPromo = 0;
     |ABRE #20;
     |ABRE #3;
     |BUCLELIN 0 LineasAlba2 #10;
     |CIERRA #3;
     |CIERRA #20;

     |SI nSwPromo ! 0;
          nSwHayLin = 1;
          |ABRE #15;
          |BUCLE CreaLinRegalo;
          |CIERRA #15;
     |FINSI;
|FPRC;

|PROCESO agifa071Die;
     aTexto49 = #agifa071#49;
     |QBF aTexto49;
     |SI aTexto49 = "REGALO";
          enForma = -1;
          |HAZ AcumulaAV;
          #3#0 = #agifa010#3;
          #3#1 = #agifa071#2;
          |LEE 101#3=;
          |SI FSalida = 0; |Y #10#1 ] #3#4; |Y #10#1 [ #3#5;
               nRegalo = #agifa071#5;
               #3#8 = #3#8 + (nRegalo * (#3#6 / #3#7));     ||Acum. Pendiente
               #3#10 = #3#10 - nRegalo;           ||Acum. Regalo.
               |GRABA 020#3a;
          |FINSI;
          |LIBERA #3;

          |BORRA 020#agifa071.a;
     |FINSI;
|FPRC;

|DEFBUCLE agifa071Die;
 #agifa071#1;
 ;
 #agifa010#52, #agifa010#0,   1;
 #agifa010#52, #agifa010#0, 999;
 be;
 NULL, agifa071Die;
|FIN;

|PROCESO BorraLinReg;
     |SI #agifa010#38 = "N";  ||Solo si no esta ya facturado
          |HAZ CargaDefs1172;
          |ABRE #diefa003@;
          |BUCLE agifa071Die;
          |CIERRA #diefa003@;
          |HAZ DescargaDefs1172;
     |FINSI;
     ||TODO CCC
|FPRC;

|PROCESO ActPromoDiexpa;
     |HAZ CargaDefs1172;
     |ABRE #3;
     aTexto49 = #agifa071#49;
     |QBF aTexto49;
     |SI aTexto49 ! "REGALO";
          #3#0 = #10#3;
          #3#1 = #71#2;
          |LEE 101#3=;
          |SI FSalida = 0; |Y #10#1 ] #3#4; |Y #10#1 [ #3#5;
               #3#8 = #3#8 +. #71#5;
               #3#9 = #3#9 +. #71#5;
               |GRABA 020#3a;
          |FINSI;
     |SINO;
          #3#0 = #10#3;
          #3#1 = #71#2;
          |LEE 101#3=;
          |SI FSalida = 0; |Y #10#1 ] #3#4; |Y #10#1 [ #3#5;
               #3#10 = #3#10 +. #71#5;
               #3#8 = #3#8 -. (#71#5 * (#3#6/#3#7));
               |GRABA 020#3a;
          |FINSI;
     |FINSI;
     |LIBERA #3;
     |CIERRA #3;
     |HAZ DescargaDefs1172;
|FPRC;

|PROCESO CargaDefs1172;
     |DFICE aDirE; |QBF aDirE;
     |DDEF aDirD;  |QBF aDirD;

     aAlfa = aDirD + "diefa007.mas";
     |CARGA_DEF aAlfa, diefa007@;
     |SI FSalida = 0; |DFICE aAlfa; |PATH_DAT #7 aAlfa; |FINSI;

     aAlfa = aDirD + "diefa003.mas";
     |CARGA_DEF aAlfa, diefa003@;
     |SI FSalida = 0; |DFICE aAlfa; |PATH_DAT #3 aAlfa; |FINSI;

     aAlfa = aDirD + "diefam01.mas";
     |CARGA_DEF aAlfa, diefam01@;
     |SI FSalida = 0; |DFICE aAlfa; |PATH_DAT #1 aAlfa; |FINSI;

     aAlfa = aDirD + "diefaw01.mas";
     |CARGA_DEF aAlfa, diefaw01@;
     |SI FSalida = 0; |DFICE aAlfa; |PATH_DAT #20 aAlfa; |FINSI;

     aAlfa = aDirD + "diefaw15.mas";
     |CARGA_DEF aAlfa, diefaw15@;
     |SI FSalida = 0; |DFICE aAlfa; |PATH_DAT #15 aAlfa; |FINSI;

|FPRC;

|PROCESO DescargaDefs1172;
     |DESCARGA_DEF #15;
     |DESCARGA_DEF #20; |DESCARGA_DEF #1;
     |DESCARGA_DEF #3;  |DESCARGA_DEF #7;
|FPRC;

|PROCESO Mi104;
     #104#4 = eaCliente;
     #104#25 = "     ";
     #104#0 = 1;
|FPRC;

|PROCESO Ma104;
     #104#4 = eaCliente;
     #104#25 = "zzzzz";
     #104#0 = 999999;
|FPRC;

|PROCESO AdjDiexpa2;
     nNumPed = 0;
     |ABRE #104;
     |CONSULTA_F_CLAVE #2#104, Mi104, Ma104;
     |SI FSalida = 0;
          aSerPed = #104#25;
          nNumPed = #104#0;
     |FINSI;
     |CIERRA #104;
     |SI nNumPed = 0; |ERROR; |ACABA; |FINSI;

     |HAZ CreaTempo; aTempo99972 = Tempo;
     |NOME_DAT #6 aTempo99972; |DELINDEX #6;

     nLinea = 0;
     |HAZ CargaDefs1172;
     |ABRE #6;
     |ABRE #19;
     |BUCLE diefa007;
     |CIERRA #6;
     |CIERRA #19;
     |HAZ DescargaDefs1172;
     |SI nLinea ! 0;
          |PUSHV 0501 2380;
          |BLANCO 907 2380;
          |ENTLINEAL #1#6, -1, 4, 20, 1, Min6, Max6;
          |POPV;
          |HAZ UltimaLineaDie;
          nLinea = nLin;
          |HAZ CargaDefs1172;
          |ABRE #7;
          |ABRE #104;
          |ABRE #73;
          #104#25 = aSerPed;
          #104#0 = nNumPed;
          |LEE 121#104=;
          |SI FSalida = 0;
               nEsAdjPed = 1;
               |BUCLE AdjFalta;
               |HAZ CalCabAgifa104;
          |FINSI;
          |CIERRA #73;
          |CIERRA #104;
          |CIERRA #7;
          |HAZ DescargaDefs1172;
     |FINSI;

     |DELINDEX #6; Tempo = aTempo99972; |HAZ BoraTempo;
|FPRC;

|PROCESO CreaLinPe;
     #73#28 = aSerPed;
     #73#0 = nNumPed;
     #73#1 = 999;
     |LEE 000#73];
     |SI FSalida = 0;
          |LEE 000#73a;
     |SINO;
          |LEE 000#73u;
     |FINSI;
     |SI FSalida = 0; |Y #73#28 = aSerPed; |Y #73#0 = nNumPed;
          nLinPed = #73#1 + 1;
     |SINO;
          nLinPed = 1;
     |FINSI;
     |SI eaTipo = "A"; || Linea 500 solo en Adj.
          |SI nLinPed < #142#123; nLinPed = #142#123; |FINSI;
     |FINSI;
     |DDEFECTO #73;
     #73#28 = aSerPed;
     #73#0 = nNumPed;
     #73#1 = nLinPed;
     #73#2 = #71#2;
     #73#3 = #71#3;
     #73#4 = #71#4;
     #73#5 = #71#5;
     #73#6 = #71#6;
     #73#8 = #71#8;
     #73#9 = #71#10;
     #73#13 = #104#2;
     #73#14 = #71#11;
     #73#16 = #104#7;
     #73#17 = #71#16;
     #73#18 = #71#13;
     #73#19 = #71#17;
     #73#20 = #104#4;
     #73#23 = #104#5;
     #73#24 = #104#6;
     #73#25 = #104#17;
     #73#26 = #104#9;
     #73#29 = #71#33;
     #73#35 = #71#34;
     #73#36 = #71#35;
     #73#42 = #73#5;
     #73#43 = nServi;
     |HAZ TotalLin73;
     |GRABA 020#73n;
|FPRC;
