|FICHEROS;
     pcegz028 #0;
     pcegm004 #4;
     pcegm005 #5;
     agifa007 #7;
     agifa010 #10;
     agifa024 #24;
     pcegw005 #50; ||Tmp unidades
     agifa071 #71;
     agifa142 #142;
     pcegm016 #16;
|FIN;

|INCLUYE i_varart;

|VARIABLES;
     &eaSerAlbPCEG = "";
     &enNumAlbPCEG = 0;
     &Tempo = "";
     &ser_alb = "";
     &num_alb = 0;
     &eaImpresora = "";        || No pide
   ||  &eaFicImpre = "";         || a un fichero
     &eaDefImpre = "";         || la por defecto

     &iva1     = 0;
     &iva2     = 0;
     &iva3     = 0;
     &iva4     = 0;
     &iva5     = 0;
     &re1      = 0;
     &re2      = 0;
     &re3      = 0;
     &re4      = 0;
     &re5      = 0;

     aTmp50 = "";
     fFecha = @;
     aSerPed = "";
     nNumPed = 0;
     aDRei = "";
     aHRei = "";
     aInforme = "";
     aAlfa = "";
     nHay = 0;
|FIN;

|PROCESO AlbaZ;
     eaSerAlbPCEG = "";
     enNumAlbPCEG = 0;
     |ABRE #16;
     #16#0 = #71#29;
     #16#1 = #71#0;
     |LEE 000#16=;
     |SI FSalida = 0;
          eaSerAlbPCEG = #16#2;
          enNumAlbPCEG = #16#3;
     |FINSI;
     |CIERRA #16;
|FPRC;

|PROCESO ImpLinAlb;
     |DDEFECTO #4;
     |DDEFECTO #5;
     |HAZ AlbaZ;
     |PRINT;
|FPRC;

|PROCESO Temporal;
     |DDEFECTO #4;
     #4#25 = #50#0;
     #4#0 = #50#1;
     |LEE 020#4=;

     |DDEFECTO #5;
     #5#28 = #50#0;
     #5#0 = #50#1;
     #5#1 = #50#2;
     |LEE 020#5=;

     |DDEFECTO #71;
     #71#29 = #50#3;
     #71#0 = #50#4;
     #71#1 = #50#2;
     |LEE 000#71=;

     |HAZ AlbaZ;
     |PRINT;
     nHay = 1;
|FPRC;

|DEFBUCLE Temporal;
     #50#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, Temporal;
|FIN;

|PROCESO Servidos;
     #10#52 = #71#29;
     #10#0 = #71#0;
     |LEE 000#10=;
     |SI #10#1 > fFecha; |ACABA; |FINSI;
     |SI #10#52 ] "YYYYY"; |ACABA; |FINSI;

     #50#2 = #71#1;
     |LEE 121#50=;
     |SI FSalida = 0;
          #50#7 = #50#7 + #71#5;
          |GRABA 020#50a;
     |FINSI;
|FPRC;

|DEFBUCLE Servidos;
     #71#4;
     ;
     aSerPed, nNumPed, 001;
     aSerPed, nNumPed, 999;
     ;
     NULL, Servidos;
|FIN;

|PROCESO LineasAlb;
     #50#2 = #71#1;
     |LEE 121#50=;
     |SI FSalida = 0;
          #50#6 = #71#5;
          |GRABA 020#50a;
     |FINSI;
|FPRC;

|PROCESO LineasPed;
     |DDEFECTO #50;
     #50#0 = #5#28;
     #50#1 = #5#0;
     #50#2 = #5#1;
     #50#3 = #10#52;
     #50#4 = #10#0;
     #50#5 = #5#5;
     #50#8 = #5#5 - #5#43;
     |GRABA 020#50n;
|FPRC;

|PROCESO IVAS;
     eaCli = #10#3;
     efFiva = #10#1;
     enTiva = 1; |HAZ IVACli; iva1 = enIva; re1 = enRec;
     enTiva = 2; |HAZ IVACli; iva2 = enIva; re2 = enRec;
     enTiva = 3; |HAZ IVACli; iva3 = enIva; re3 = enRec;
     enTiva = 4; |HAZ IVACli; iva4 = enIva; re4 = enRec;
     enTiva = 5; |HAZ IVACli; iva5 = enIva; re5 = enRec;
|FPRC;

|PROCESO Albaran;
     |DDEFECTO #7;
     #7#26 = #10#52;
     |LEE 000#7=;
     aInforme = #7#4;
     |SI #10#50 = "S"; aInforme = #7#3; |FINSI;
     |SI #10#43 = "S"; aInforme = #7#5; |FINSI;
     |QBF aInforme;

     |DDEFECTO #24;
     #24#0 = #10#3;
     |LEE 020#24=;

     |HAZ IVAS;

     #71#29 = #10#52;
     #71#0 = #10#0;
     #71#1 = 0;
     |LEE 000#71];
     aSerPed = #71#31;
     nNumPed = #71#12;
     fFecha = #10#1;

     |DELINDEX #50;
     |ABRE #50;

     |ABRE #4;
     #4#25 = aSerPed;
     #4#0 = nNumPed;
     |LEE 000#4=
     |SI FSalida = 0;
          |BUCLELIN 2 LineasPed #4;

          |SALVA_FICHA #10;
          |BUCLELIN 2 LineasAlb #10;
          |REPON_FICHA #10;
     |FINSI;
     |CIERRA #4;


     |BUCLE Servidos;

     |CIERRA #50;

     |HAZ PideImpresora;
     |SI FSalida = 0;
          |INFOR aInforme;
          |SI FSalida = 0;
               nHay = 0;
               |BUCLE Temporal;
               |SI nHay = 0;
                    |SALVA_FICHA #10;
                    |BUCLELIN 2 ImpLinAlb #10;
                    |REPON_FICHA #10;
               |FINSI;
               |PIEINF;
               |FININF;

               #10#90 = "S";
               |GRABA 020#10a;
          |SINO;
               aAlfa = "0000Error en informe '" + aInforme + "'";
               |MENAV aAlfa;
          |FINSI;
          |FINIMP;
     |FINSI;

     |DELINDEX #50;

     |LIBERA #10;
|FPRC;

|DEFBUCLE PorAlbaran;
     #10#1;
     1, 3, 90;
     #0#6, #0#2, #0#4, #0#0, aDRei;
     #0#7, #0#3, #0#5, #0#1, aHRei;
     be;
     NULL, Albaran;
|FIN;

|DEFBUCLE SoloUno;
     #10#1;
     90;
     #0#6, #0#2, aDRei;
     #0#7, #0#3, aHRei;
     be;
     NULL, Albaran;
|FIN;

|PROCESO PideImpresora;
     |QBF eaImpresora;        || No pide
     |QBF eaFicImpre;         || a un fichero
     |QBF eaDefImpre;         || la por defecto

     |SI eaFicImpre ! "";
          Impresora = eaFicImpre;
          |IMPRE -77;
     |SINO;
          |SI eaImpresora = "";
               |SI eaDefImpre = "";
                    |IMPRE 1;
               |SINO;
                    Impresora = eaDefImpre;
                    |IMPRE 1;
               |FINSI;
          |SINO;
               Impresora = eaImpresora;
               |IMPRE -1;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Tipo3;
     |SI #0#8 = "S";
          aDRei = " ";
          aHRei = "z";
     |SINO;
          aDRei = "N";
          aHRei = "N";
     |FINSI;

     |HAZ CreaTempo; |NOME_DAT #50 Tempo; aTmp50 = Tempo;

     |ABRE #5;
     |ABRE #4;
     |ABRE #24;
     |ABRE #71;
     |ABRE #7;
     |SI num_alb = 0;
          |BUCLE PorAlbaran;
     |SINO;
          |BUCLE SoloUno;
     |FINSI;
     |CIERRA #7;
     |CIERRA #71;
     |CIERRA #24;
     |CIERRA #4;
     |CIERRA #5;

     Tempo = aTmp50; |HAZ BoraTempo;
|FPRC;

|PROGRAMA;
     |ABRE #142; |LEE 000#142p; |CIERRA #142;

     |HAZ MiraLogos; || Copia Logos para el Crystal

     |SI ser_alb = ""; |Y num_alb = 0;
         |ABRE #0;
         |LEE 101#0p;
         |SI FSalida ! 0; |GRABA 020#0b; |FINSI;
         |CLS;
         |CABEZA "Imp. Albaran";
         |PINPA #0#0;
         |PINDA #0#0;
         |ENDATOS #1#0;
         |SI FSalida = 0;
               |GRABA 020#0a;
               |LIBERA #0;
               |HAZ Tipo3;
         |FINSI;
         |CIERRA #0;
     |SINO;
         #0#6  = ser_alb;
         #0#7 = ser_alb;
         #0#2  = num_alb;
         #0#3  = num_alb;
         #0#8  = "S";
         |HAZ Tipo3;
     |FINSI;
|FPRO;
