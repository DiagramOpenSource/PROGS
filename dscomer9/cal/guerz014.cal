     ||Creada clave 3 en el godiacal=godiaca1

|FICHEROS;
     guerz014 #0;
     gopartia #1;
     gomantho #2;
     gomantl1 #3;
     gopre001 #4;
     gopre002 #5;
     gopre003 #6;
     goalbpa1 #7;
     goalbpa2 #8;
     gopartca #9;
     gopartll #10;
     gopartli #11;
     goptrbca #12;
     goptrbli@ #13;
     agifa029 #29;
     agifa065 #65;
     agifa077 #77;
     agifa080 #80;
     gomatljn #90;
     goparame #100;
     gopartia@;
     godiacal #20;   ||
     goptrbli #21;
     goptrbca #22;
     agifa324 #324;
     gotmp011 #111;
|FIN;

|VARIABLES;
     &nDeci_PBase = 0;
     &nDeci_TBase = 0;
     &nDeci_PTrab = 0;
     &nDeci_TTrab = 0;
     &aDivisa = "";
     &aMoneda = "";
     &eaTipo = "";
     &enTipo = 0;
     &Tempo = "";
     nExpe = 0;
     nPre = 0;
     aSerPre = "";
     aParti = "";
     aDesParti = "";
     aCapi = "";
     nConta = 0;
     nLin = 0;
     aSerAlba = "";
     nNumAlba = 0;
     aAlfa = "";
     fHoy = @;
     aIP = "";
     aServidor = "";
     aFrom = "";
     aMail = "";
     aTo = "";
     aSubject = "";
     aDjunto = "";
     aFicMail = "";
     aMensaje = "";
     nPrePar = 0;
     nPreAlb = 0;
     nAlma = 0;
     nSwPrecio = 0;
     nDto = 0;
     nTotaAlb = 0;
     aTmp111 = "";
     aTmp0 = "";
|FIN;

|PROCESO TotalGopartia;
     #90#9 = #90#9 + #1#14;
     #90#10 = #90#10 + #1#25;
     #90#11 = #90#11 + #1#13;
     #90#12 = #90#12 + #1#24;
|FPRC;

|DEFBUCLE TotalGopartia;
     #1#1;
     ;
     "M", aSerAlba, nNumAlba, 001, 001;
     "M", aSerAlba, nNumAlba, 999, 999;
     ;
     NULL, TotalGopartia;
|FIN;

|PROCESO Crea_gomatljn;
     |SELECT #4#90;
     #90#5 = #gopartia@#4;
     #90#6 = #gopartia@#5;
     |LEE 000#90=;
     |SI FSalida = 0; |ACABA; |FINSI;

     |SELECT #1#90;
     #90#0 = #gopartia@#0;
     #90#1 = #gopartia@#1;
     #90#2 = 9999;
     |LEE 000#90];
     |SI FSalida = 0;
          |LEE 000#90a;
     |SINO;
          |LEE 000#90u;
     |FINSI;
     |SI FSalida = 0; |Y #90#0 = #gopartia@#0; |Y #90#1 = #gopartia@#1;
          nLin = #90#2 + 1;
     |SINO;
          nLin = 1;
     |FINSI;

     #77#50 = #gopartia@#4;
     #77#0 = #gopartia@#5;
     |LEE 020#77=;

     |DDEFECTO #90;
     #90#0 = #gopartia@#0;
     #90#1 = #gopartia@#1;
     #90#2 = nLin;
     #90#3 = #77#1;
     #90#4 = #77#54;
     #90#5 = #77#50;
     #90#6 = #77#0;
     #90#7 = #77#3;
     #90#8 = #77#4;

     aSerAlba = #77#50;
     nNumAlba = #77#0;
     |BUCLE TotalGopartia;

     #90#14 = #gopartia@#2;
     #90#15 = #gopartia@#3;
     |GRABA 020#90n;

     eaTipo = "Crea gomatljn";
     enTipo = 1; |PRINT;
|FPRC;

|DEFBUCLE Crea_gomatljn;
     #gopartia@#1005;
     ;
     " ", "     ", 000000, 000, 000;
     "z", "zzzzz", 999999, 999, 999;
     ;
     NULL, Crea_gomatljn;;
|FIN;

|PROCESO Almacen;
     |DDEFECTO #29;
     #29#0 = #1#17;
     |LEE 000#29=;
|FPRC;

|PROCESO Presupuesto;
     #4#0 = aSerPre;
     #4#1 = nPre;
     |LEE 000#4=;
     |SI FSalida = 0;
          #5#0 = #4#0;
          #5#1 = #4#1;
          #5#2 = 0;
          |LEE 000#5];
          |SI FSalida = 0; |Y #5#0 = #4#0; |Y #5#1 = #4#1;
               aCapi = #5#12;

               #6#0 = #5#0;
               #6#1 = #5#1;
               #6#2 = #5#12;
               #6#3 = 0;
               |LEE 000#6];
               |SI FSalida = 0; |Y #6#0 = #5#0; |Y #6#1 = #5#1; |Y #6#2 = #5#12;
                    aParti = #6#4;
                    aDesParti = #6#5;
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Expediente;
     nExpe = 0;
     #2#0 = #1#2;
     #2#1 = 0;
     |LEE 000#2];
     |SI #2#0 = #1#2;
          nExpe = #2#1;

          #3#0 = #2#0;
          #3#1 = #2#1;
          #3#2 = 0;
          |LEE 000#3];
          |SI FSalida = 0; |Y #3#0 = #2#0; |Y #3#1 = #2#1;
               aSerPre = #3#3;
               nPre = #3#4;
          |FINSI;
     |SINO;
          |DDEFECTO #2;
     |FINSI;
|FPRC;

|PROCESO CreaGopartia;
     #1#28 = #77#1;
     #1#21 = "EUR";
     #1#31 = "N";
     #1#2 = #80#27;
     |HAZ Expediente;
     |SI nExpe = 0; |ACABA; |FINSI;

     |HAZ Presupuesto;
     #1#3 = nExpe;
     #1#0 = aSerPre;
     #1#1 = nPre;
     #1#15 = #2#2; ||Cli
     #1#16 = #2#8; ||Nom
     #1#33 = aCapi;
     #1#8 = aParti;
     #1#9 = aDesParti;
     #1#17 = #80#27; ||Almacen
     |HAZ Almacen;
     #1#18 = #29#1; ||Des Alm
     #1#20 = #80#2;
     #1#30 = #80#4;
     #1#26 = "AUT";
     #1#27 = "31.01.2003";
     #1#10 = #80#5; ||Canti
     #1#26 = #80#5; ||Canti2
     #1#41 = #80#6; ||Precio
     #1#49 = #80#6;
     #1#42 = #80#8; ||Dto
     #1#50 = #80#8;
     #1#43 = #80#26; ||Dto2
     #1#51 = #80#26;
     #1#44 = #77#5 + #77#32 + #77#7; ||DtoC
     #1#52 = #77#5 + #77#32 + #77#7;
     #1#12 = ((#1#41 < #1#42) < #1#43) < #1#44;
     #1#23 = ((#1#41 < #1#42) < #1#43) < #1#44;
     #1#14 = #80#9;
     #1#25 = #80#9;
     |GRABA 020#1n;

     eaTipo = "Crea gopartia Alb";
     enTipo = 2; |PRINT;
|FPRC;

|PROCESO AlbCompra;
     aDivisa = #77#56;
     aMoneda = #77#58;
     |HAZ Divisas077;
|FPRC;

|PROCESO AlbCompraLin;
     |DDEFECTO #1;
     #1#34 = "M";
     #1#4 = #80#27;
     #1#5 = #80#0;
     #1#6 = #80#1;
     #1#7 = 1;
     |LEE 000#1=;
     |SI FSalida ! 0;
          |SELECT #3#20;
          #20#19 = #1#4;
          #20#20 = #1#5;
          #20#21 = #1#6;
          |LEE 000#20=;
          |SI FSalida ! 0;
               |HAZ CreaGopartia;
               |SI nExpe ! 0;
                    nConta = nConta + 1;

                    |SALVA_DATOS #1;
                    |REPON_DATOS #gopartia@;
                    |GRABA 020#gopartia@.n;
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE AlbCompra;
     #77#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, AlbCompra, AlbCompraLin;
|FIN;

|PROCESO GraGopartia80;
     #1#10 = #80#5;

     #1#41 = #80#6;
     #1#49 = #80#6;

     #1#42 = #80#8;
     #1#43 = #80#26;
     #1#50 = #80#8;
     #1#51 = #80#26;

     #1#12 = nPreAlb;
     #1#23 = nPreAlb;
     #1#14 = #80#9;
     #1#25 = #80#9;

     #1#17 = nAlma;
     |HAZ Almacen;
     #1#18 = #29#1; ||Des Alm

     #1#44 = #77#5;
     |GRABA 020#1a;

     eaTipo = "Actualiza gopartia Alb";
     enTipo = 3; |PRINT;
|FPRC;

|PROCESO GraGoptrbli;
     #21#11 = #1#10;
     |GRABA 020#21a;

     eaTipo = "Actualiza goptrbli";
     enTipo = 4; |PRINT;
|FPRC;

|PROCESO PreParAlb;
     nSwPrecio = 0;
     nDto = #77#5 + #77#32 + #77#7;
     nPreAlb = ((#80#6 < #80#8) < #80#26) < nDto;

     nPreAlb = nPreAlb ! nDeci_PBase;
     nTotaAlb = #80#9 ! nDeci_TBase;

     |SI nPreAlb ! #1#12;
          nSwPrecio = 1;
     |FINSI;
     |SI nPreAlb ! #1#23;
          nSwPrecio = 1;
     |FINSI;

     |SI nTotaAlb ! #1#14;
          nSwPrecio = 1;
     |FINSI;
     |SI nTotaAlb ! #1#25;
         nSwPrecio = 1;
     |FINSI;
|FPRC;

|PROCESO ActLinAlb;
     #80#3 = nAlma;
     |GRABA 020#80a;

     #65#0 = #80#2;
     #65#1 = #77#1;
     #65#2 = "AC";
     #65#3 = #80#0;
     #65#4 = #80#1;
     #65#11 = #80#27;
     |LEE 101#65=;
     |SI FSalida = 0;
          #65#5 = nAlma;
          |GRABA 020#65a;
          |LIBERA #65;

          eaTipo = "Actualiza historico";
          enTipo = 5; |PRINT;
     |FINSI;
|FPRC;

|PROCESO Bgomatljn;
     eaTipo = "Borra gomatljn";
     enTipo = 11; |PRINT;
     |BORRA 020#90a;
|FPRC;

|DEFBUCLE Bgomatljn;
     #90#3;
     ;
     #1#4, #1#5;
     #1#4, #1#5;
     be;
     NULL, Bgomatljn;
|FIN;

|PROCESO Bgoalbpa1;
     eaTipo = "Borra goalbpa1";
     enTipo = 12; |PRINT;
     |BORRA 020#7a;
|FPRC;

|DEFBUCLE Bgoalbpa1;
     #7#1;
     ;
     #1#4, #1#5;
     #1#4, #1#5;
     be;
     NULL, Bgoalbpa1;
|FIN;

|PROCESO Bgoalbpa2;
     eaTipo = "Borra goalbpa2";
     enTipo = 13; |PRINT;
     |BORRA 020#8a;
|FPRC;

|DEFBUCLE Bgoalbpa2;
     #8#1;
     ;
     #1#4, #1#5, 000;
     #1#4, #1#5, 999;
     be;
     NULL, Bgoalbpa2;
|FIN;

|PROCESO Duplicado;
     #111#0 = #1#4;
     #111#1 = #1#5;
     #111#2 = #1#6;
     #111#3 = #1#7;
     |LEE 000#111=;
     |SI FSalida = 0;
          eaTipo = "Registro Duplicado";
          enTipo = 19; |PRINT;
     |SINO;
          |GRABA 020#111n;
     |FINSI;
|FPRC;

|PROCESO Gopartia;
     #2#0 = #1#2;
     #2#1 = #1#3;
     |LEE 000#2=;
     |SI FSalida ! 0; |BORRA 020#1a; |ACABA; |FINSI;
     nAlma = #2#11;

     |SI #1#34 = "M"; |O #1#34 = "C";
          |HAZ Duplicado;
          #77#50 = #1#4;
          #77#0 = #1#5;
          |LEE 000#77=;
          |SI FSalida ! 0;
               #80#27 = #1#4;
               #80#0 = #1#5;
               #80#1 = #1#6;
               |BORRA 000#80c;
          |FINSI;

          #80#27 = #1#4;
          #80#0 = #1#5;
          #80#1 = #1#6;
          |LEE 101#80=;
          |SI FSalida ! 0;
               eaTipo = "Borra gopartia sin Alb";
               enTipo = 6; |PRINT;
               |BORRA 020#1a;
               |BUCLE Bgomatljn;
               |BUCLE Bgoalbpa1;
               |BUCLE Bgoalbpa2;
          |SINO;
               |SI #1#28 = "01.01.0000";
                    #1#28 = #77#1;
                    |GRABA 020#1a;
                    enTipo = "Actualiza fecha gopartia alb";
                    enTipo = 18; |PRINT;
               |FINSI;
               |SI #1#34 = "M";
                    #77#50 = #1#4;
                    #77#0 = #1#5;
                    |LEE 000#77=;
                    |SI FSalida ! 0; |DDEFECTO #77; |FINSI;
                    aDivisa = #77#56;
                    aMoneda = #77#58;
                    |HAZ Divisas077;

                    |HAZ PreParAlb;
                    nPre = #80#6 ! nDeci_PBase;
                    |SI #1#10 ! #80#5;
                              |O #1#41 ! nPre; |O #1#49 ! nPre;

                              |O #1#42 ! #80#8; |O #1#43 ! #80#26;
                              |O #1#50 ! #80#8; |O #1#51 ! #80#26;

                              |O nSwPrecio ! 0;

                              |O nAlma ! #1#17;

                              |O #1#44 ! #77#5;
                         |HAZ GraGopartia80;
                    |FINSI;

                    |SI nAlma ! #80#3;
                         |HAZ ActLinAlb;
                    |FINSI;
               |FINSI;
          |FINSI;
          |LIBERA #80;
     |FINSI;
     |SI #1#34 = "T";
          |SELECT #4#21;
          #21#23 = #1#4; || Serie Parte
          #21#24 = #1#5; || N§ Parte
          #21#3 = #1#6; || Linea
          |LEE 101#21=;
          |SI FSalida ! 0;
               eaTipo = "Borra gopartia sin goptrbli";
               enTipo = 7; |PRINT;
               |BORRA 020#1a;
          |SINO;
               |SI #1#10 ! #21#11;
                    |HAZ GraGoptrbli;
               |FINSI;
               |SI #1#41 ! #21#13;
                         |O #1#12 ! #21#13;
                         |O #1#23 ! #21#13;

                         |O #1#14 ! #21#17;
                         |O #1#25 ! #21#17;
                    |HAZ GraGopartia21;
               |FINSI;
          |FINSI;
          |LIBERA #21;
     |FINSI;
     |SI #1#34 = "T"; |Y  #1#28 = "01.01.0000";
          |SELECT #4#21;
          #21#23 = #1#4; || Serie Parte
          #21#24 = #1#5; || N§ Parte
          #21#3 = #1#6; || Linea
          |LEE 000#21=;
          |SI FSalida = 0; |Y #21#2 ! "01.01.0000";
               #1#28 = #21#1;
               |GRABA 020#1a;
               eaTipo = "Actualiza gopartia sin fecha";
               enTipo = 16; |PRINT;
          |SINO;
               eaTipo = "Borra gopartia sin fecha";
               enTipo = 17; |PRINT;
               |BORRA 020#1a;

               #22#0 = #21#0;     ||SExpe
               #22#1 = #21#1;     ||NExpe
               #22#2 = #21#2;       ||Fecha
               #22#16 = #21#20;      ||SPres
               #22#17 = #21#21;      ||NPres
               |LEE 101#22=;
               |SI FSalida = 0;
                    |BORRA 020#22a;
                    |LIBERA #22;
               |FINSI;

               |SELECT #2#11;
               #11#0 = #21#20;
               #11#1 = #21#21;
               #11#3 = #21#2;
               |LEE 101#11=;
               |SI FSalida = 0;
                    |BORRA 020#11a;
                    |LIBERA #11;
               |FINSI;
               |SELECT #1#11;
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE Gopartia;
     #1#1;
     ;
     " ", "     ", 000001, 001, 001;
     "z", "zzzzz", 999999, 999, 999;
     be;
     NULL, Gopartia;
|FIN;

|PROCESO CreaGopartia21;
     #1#34 = "T";
     #1#4 = #22#15;                                ||Serie Part.
     #1#5 = #22#3;                                 ||No.  Parte.
     #1#6 = #21#3;                                 ||Linea Docu.
     #1#7 = 1;
     |LEE 101#1=;
     |SI FSalida ! 0; |GRABA 020#1b; |FINSI;
     #1#0 = #22#16;
     #1#1 = #22#17;
     #1#10 = #21#11;

     #1#15 = #22#7;                                ||Codigo de Cliente.
     #1#16 = #22#8;                                ||Nombre del Cliente.
     |HAZ Almacen;
     #1#18 = #29#1;
     #1#2  = #22#0;                                ||Serie Expediente.
     #1#3  = #22#1;                                ||No. Expediente.
     #1#20 = #21#22;                               ||Codigo M.O.
     #1#21 = #22#14;                               ||Moneda de Trabajo.
     #1#28 = #21#2;                                ||Fecha Documento.
     #1#29 = "T";                                 ||Tipo Documeto(Trabajos).
     ||Ä
     #1#37 = #21#12;
     #1#38 = 0;
     #1#39 = 0;
     #1#40 = 0;
     #1#41 = #21#13;
     #1#42 = 0;
     #1#43 = 0;
     #1#44 = 0;
     |HAZ CalcuGopartia;
     |GRABA 020#1a;
     |LIBERA #1;

     eaTipo = "Crea gopartia goptrbli";
     enTipo = 8; |PRINT;
|FPRC;

|PROCESO GraGopartia21;
     #1#41 = #21#13;
     #1#12 = #21#13;
     #1#23 = #21#13;
     #1#14 = #21#17;
     #1#25 = #21#17;
     |GRABA 020#1a;

     eaTipo = "Graba gopartia goptrbli";
     enTipo = 9; |PRINT;
|FPRC;

|PROCESO goptrbli;
     |SI #12#20 = "P";#13#8  = #13#11;|FINSI;
     |SI #12#20 = "A";#13#9  = #13#11;|FINSI;
     |SI #12#20 = "E";#13#10 = #13#11;|FINSI;
     #13#16 = #13#11*#13#12;           ||Total Trabajador Venta Mon Trab.
     #13#17 = #13#11*#13#13;           ||Total Trabajador Costo Mon Trab.
     #13#18 = #13#11*#13#14;           ||Total Trabajador Venta Mon Base.
     #13#19 = #13#11*#13#15;           ||Total Trabajador Costo Mon Base.
     |GRABA 020#13a;

     #12#4 = #12#4 + #13#8;
     #12#5 = #12#5 + #13#9;
     #12#6 = #12#6 + #13#10;
     #12#9 = #12#4 + #12#5 + #12#6;
     #12#10 = #12#10 + #13#17; ||CT
     #12#11 = #12#11 + #13#16; ||VT
     #12#12 = #12#12 + #13#19; ||CB
     #12#13 = #12#13 + #13#18; ||VB

     #11#6 = #11#6 + #13#8;
     #11#7 = #11#7 + #13#9;
     #11#8 = #11#8 + #13#10;
     #11#9 = #11#9 + #13#17;   ||CT
     #11#10 = #11#10 + #13#16; ||VT
     #11#11 = #11#11 + #13#19; ||CB
     #11#12 = #11#12 + #13#18; ||VB
     #11#13 = #11#6 + #11#7 + #11#8;

     #10#6 = #10#6 + #13#8;
     #10#7 = #10#7 + #13#9;
     #10#8 = #10#8 + #13#10;
     #10#9 = #10#9 + #13#17;
     #10#10 = #10#10 + #13#16;
     #10#11 = #10#11 + #13#19;
     #10#12 = #10#12 + #13#18;
     #10#13 = #10#6 + #10#7 + #10#8;

     #9#2 = #9#2 + #13#8;
     #9#3 = #9#3 + #13#9;
     #9#4 = #9#4 + #13#10;
     #9#5 = #9#2 + #9#3 + #9#4;
     #9#6 = #9#6 + #13#17;
     #9#7 = #9#7 + #13#16;
     #9#8 = #9#8 + #13#19;
     #9#9 = #9#9 + #13#18;
|FPRC;

|DEFBUCLE goptrbli;
     #13#1004;
     ;
     #12#16, #12#17, #12#2, 0001;
     #12#16, #12#17, #12#2, 9999;
     be;
     NULL,  goptrbli;
|FIN;

|PROCESO gopartli;
     #11#6 = 0;
     #11#7 = 0;
     #11#8 = 0;
     #11#9 = 0;
     #11#10 = 0;
     #11#11 = 0;
     #11#12 = 0;
     #11#13 = 0;

     #11#14 = #9#0;
     #11#15 = #9#1;

     |DDEFECTO #12;
     |ABRE #12;
     #12#0 = #11#14;
     #12#1 = #11#15;
     #12#2 = #11#3;
     #12#16 = #11#0;
     #12#17 = #11#1;
     |LEE 101#12=;
     |SI FSalida ! 0;
          #12#3 = #11#5;
          #12#7 = #9#10;
          #12#8 = #9#12;
          #12#14 = #9#11;
          #12#15 = #11#4;
          #12#20 = #11#16;
          |GRABA 020#12b;
     |FINSI;
     |SI FSalida = 0;
          #12#4 = 0;
          #12#5 = 0;
          #12#6 = 0;
          #12#9 = 0;
          #12#10 = 0;
          #12#11 = 0;
          #12#12 = 0;
          #12#13 = 0;
          |BUCLE goptrbli;
          |GRABA 020#12a;
     |FINSI;
     |CIERRA #12;

     |GRABA 020#11a;
     |LIBERA #11;
|FPRC;

|DEFBUCLE gopartli;
     #11#1;
     ;
     #10#4, #10#5, 0001;
     #10#4, #10#5, 9999;
     ;
     NULL, gopartli;
|FIN;

|PROCESO gopartll;
     #10#6 = 0;
     #10#7 = 0;
     #10#8 = 0;
     #10#9 = 0;
     #10#10 = 0;
     #10#11 = 0;
     #10#12 = 0;
     #10#13 = 0;
     |BUCLE gopartli;
     |GRABA 020#10a;
     |LIBERA #10;
|FPRC;

|DEFBUCLE gopartll;
     #10#1;
     ;
     #9#0, #9#1, 0001;
     #9#0, #9#1, 9999;
     be;
     NULL, gopartll;
|FIN;

|PROCESO Goptrbli;
     |SI #21#2 = "01.01.0000";
          #22#0 = #21#0;     ||SExpe
          #22#1 = #21#1;     ||NExpe
          #22#2 = #21#2;       ||Fecha
          #22#16 = #21#20;      ||SPres
          #22#17 = #21#21;      ||NPres
          |LEE 101#22=;
          |SI FSalida = 0;
               |BORRA 020#22a;
               |LIBERA #22;

               |SELECT #2#11;
               #11#0 = #21#20;
               #11#1 = #21#21;
               #11#3 = #21#2;
               |LEE 101#11=;
               |SI FSalida = 0;
                    |BORRA 020#11a;
                    |LIBERA #11;
               |FINSI;

               #1#34 = "T";
               #1#4 = #22#15;
               #1#5 = #22#3;
               #1#6 = #21#3;
               #1#7 = 1;
               |LEE 101#1=;
               |SI FSalida= 0;
                    |BORRA 020#1a;
                    |LIBERA #1;
               |FINSI;
          |FINSI;
          eaTipo = "Borra goptrbli sin fecha";
          enTipo = 10; |PRINT;
          |BORRA 020#21a;
          |ACABA;
     |FINSI;

     #22#0 = #21#0;     ||SExpe
     #22#1 = #21#1;     ||NExpe
     #22#2 = #21#2;       ||Fecha
     #22#16 = #21#20;      ||SPres
     #22#17 = #21#21;      ||NPres
     |LEE 000#22=;
     |SI FSalida ! 0;
          |BORRA 020#21a;

          eaTipo = "Borra goptrbli";
          enTipo = 15; |PRINT;
          |ACABA;
     |FINSI;

     #1#34 = "T";
     #1#4 = #22#15;
     #1#5 = #22#3;
     #1#6 = #21#3;
     #1#7 = 1;
     |LEE 000#1=;
     |SI FSalida ! 0;
          |HAZ CreaGopartia21;
     |FINSI;

     #4#0 = #21#20;
     #4#1 = #21#21;
     |LEE 000#4=;
     |SI FSalida = 0; |Y #4#18 = "P";
          #1#34 = "T";
          #1#4 = #22#15;
          #1#5 = #22#3;
          #1#6 = #21#3;
          #1#7 = 1;
          |LEE 020#1=;
          |SI FSalida = 0;
               |SI #21#11 = #1#10; |Y #21#8 ! #1#10;
                    #21#8 = #1#10;
                    #21#16 = #21#11 * #21#12;
                    #21#17 = #21#11 * #21#13;
                    #21#18 = #21#11 * #21#14;
                    #21#19 = #21#11 * #21#15;
                    |GRABA 020#21a;
                    eaTipo = "Act. Horas Pres. goptrbli";
                    enTipo = 14; |PRINT;

                    |DDEF aAlfa;
                    aAlfa = aAlfa + "goptrbli";
                    |CARGA_DEF aAlfa, goptrbli@;
                    |SI FSalida = 0;
                         #9#0 = #21#0;
                         #9#1 = #21#1;
                         |LEE 101#9=;
                         |SI FSalida = 0;
                              #9#2 = 0;
                              #9#3 = 0;
                              #9#4 = 0;
                              #9#5 = 0;
                              #9#6 = 0;
                              #9#7 = 0;
                              #9#8 = 0;
                              #9#9 = 0;
                              |BUCLE gopartll;
                              |GRABA 020#9a;
                              |LIBERA #9;
                         |FINSI;
                         |DESCARGA_DEF #goptrbli@;
                    |FINSI;
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE Goptrbli;
     #21#1;
     ;
     "     ", 000000, "01.01.0000", 0001;
     "zzzzz", 999999, "31.12.9999", 9999;
     be;
     NULL, Goptrbli;
|FIN;

|PROCESO Tipo3; |TIPO 3;
     |ABRE #100; |LEE 000#100p; |CIERRA #100;
     |ABRE #324; |LEE 000#324p; |CIERRA #324;
     nDeci_PBase = #324#9;
     nDeci_PTrab = #324#9;
     nDeci_TBase = #324#7;
     nDeci_TTrab = #324#7;

     |DFICE aAlfa;
     aFicMail = aAlfa + Usuario + ".txt";

     Impresora = aFicMail;
     |IMPRE -77;
     |SI FSalida = 0;
          |INFOR "guerz014";
          |SI FSalida = 0;
               |HAZ CreaTempo; |NOME_DAT #111 Tempo; aTmp111 = Tempo;
               |DELINDEX #111; |ABRE #111;

               |DDEF aAlfa;
               aAlfa = aAlfa + "gopartia";
               |CARGA_DEF aAlfa, gopartia@;

               |HAZ CreaTempo; aTmp0 = Tempo;
               |NOME_DAT #gopartia@#Tempo#; |DELINDEX #gopartia@;
               |ABRET #gopartia@;

               |ABRE #20;
               |ABRE #29;
               |ABRE #6;
               |ABRE #5;
               |ABRE #4;
               |ABRE #3;
               |ABRE #2;
               |ABRE #1;
               |BUCLE AlbCompra;
               |CIERRA #1;
               |CIERRA #2;
               |CIERRA #3;
               |CIERRA #4;
               |CIERRA #5;
               |CIERRA #6;
               |CIERRA #29;
               |CIERRA #20;
               |SI nConta > 0;
                    |ABRE #77;
                    |ABRE #90;
                    |BUCLE Crea_gomatljn;
                    |CIERRA #90;
                    |CIERRA #77;
               |FINSI;

               |CIERRAT #gopartia@;
               Tempo = aTmp0; |HAZ BoraTempo; |DELINDEX #gopartia@;
               |DESCARGA_DEF #gopartia@;

               ||*******
               |ABRE #80;
               |ABRE #77;
               |ABRE #29;
               |ABRE #11;
               |ABRE #21;
               |ABRE #22;
               |ABRE #2;
               |ABRE #65;
               |BUCLE Gopartia;
               |CIERRA #65;
               |CIERRA #2;
               |CIERRA #22;
               |CIERRA #21;
               |CIERRA #11;
               |CIERRA #29;
               |CIERRA #77;
               |CIERRA #80;

               |ABRE #29;
               |ABRE #22;
               |ABRE #11;
               |ABRE #1;
               |ABRE #4;
               |ABRE #9;
               |BUCLE Goptrbli;
               |CIERRA #9;
               |CIERRA #4;
               |CIERRA #1;
               |CIERRA #11;
               |CIERRA #22;
               |CIERRA #29;
               |CIERRA #111; |DELINDEX #111; Tempo = aTmp111; |HAZ BoraTempo;

               |PIEINF;
               |FININF;
          |SINO;
               |MENAV "0000No existe informe 'guerz014'";
          |FINSI;
          |FINIMP;
     |FINSI;

     aMail = #0#1; |QBF aMail;
     |SI aMail ! "";
          fHoy = ~;
          aIP = "xdscorreo.dv.diagram.net";
          aServidor = "asmtp.diagram.es";
          aFrom = aMail;
          aTo = aMail;
          aSubject = "Chequeo gopartia a fecha:" + fHoy;
          |SI enTipo = 0;
               aMensaje = "Sin errores...";
          |SINO;
               aMensaje = "$$$$" + aFicMail;
          |FINSI;
          aDjunto = aFicMail;
          |DSCORREO_ENVIA aIP, aServidor, aFrom, aTo, aSubject, aMensaje, aDjunto;
     |FINSI;
     |FBORRA aFicMail;
|FPRC;

|PROGRAMA;
     |SI PARAMETRO ! "menu";
          |ABRE #0; |LEE 000#0p; |CIERRA #0;
          |HAZ Tipo3;
     |SINO;
          |CLS;
          |ABRE #0;
          |LEE 101#0p;
          |SI FSalida ! 0; |GRABA 020#0b; |FINSI;
          |PINPA #0#0;
          |PINDA #0#0;
          |ENDATOS #1#0;
          |SI FSalida = 0; |GRABA 020#0a; |FINSI;
          |CIERRA #0;
     |FINSI;
|FPRO;
