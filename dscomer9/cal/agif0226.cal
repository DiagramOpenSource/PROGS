||  ---------- Generacion Pedidos De Compra  -----------

|FICHEROS;
     tempo226 #0,MANTE;
     agifa081 #3;
     agifa067 #4;
     agifa027 #5;
     agifa112 #6;

     agifa019 #10;
     agif0041 #11;
     agifa226 #12;
     agifa007 #13;
     agifa321 #35;  || Parametros de Divisas
     agifa322 #36;  || Clientes/Divisas
     agifa323 #37;  || Lineas de Clientes/Divisas
     agifa324 #38;  || Divisas
     dsm00082 #82;
     dsm00083 #83;
     agifa142 #142;
|FIN;

|INCLUYE i_varart;

|VARIABLES;
     aAlfa = "";
     nLinDa = 0;
     aDirEmp = "";
     aDatos = "";
     nCodEmp = "";

     &enSwMenu = 0;
     provee = "";
     imneto = 0;
     fmes = "  ";
     mes = 0;
     pm = 0;
     pe = 0;
     m1 = 0;
     m2 = 0;
     m3 = 0;
     canti2 = 0;
     mes1 = 0;
     modo1 = 0;
     st = 0;
     sto = 0;
     neto = 0;
     lin = 0;
     menos = -1;
     codigo = 0;
     bruto = 0;
     total = 0;
     &nDeci_Base     = 0;   || Decimales en Totales de la moneda base
     &nDeci_PreBase  = 0;   || Decimales en el Precio en moneda base
     &nDeci_BaseMxP  = 0;   || Decimales en Totales de la moneda base (Maxima precision)
     &nDeci_PreBasMxP = 0;   || Decimales en el Precio en moneda base (Maxima precision)
     &nDeci_DivP      = 0;   || Decimales en Totales de la divisa
     &nDeci_PreDivP   = 0;  || Decimales en el Precio en divisa
     &nDeci_TotVisP   = 0;  || Decimales en Totales visibles (el de la moneda de trabajo)
     &nDeci_TotNoVisP = 0;  || Decimales en Totales no visibles (el de la otra moneda)
     &nDeci_PreVisP   = 0;  || Decimales en el Precio visual. (lineas)
     &nDeci_ImpVisP   = 0;  || Decimales en el Importe visual. (lineas)
     nfecha    = 0;
     &ac_divisa = "N";   || Vale "S" cuando las divisas en compras estan
                         || activadas y la serie es de divisas
||   &pintado = 0;       || Sirve para controlar los pintados de las lineas
     &decim_divisa = 0;  || Decimales de la divisa
     &precio_divisa = 0; || Decimales del precio en divisa
     &decim_base = 0;    || Decimales de la moneda base
     &precio_base = 0;   || Decimales del precio en moneda base
     div_act = 0;        || Divisa actualizada. Me sirve en un PARA para que
                         || no salga hasta que este actualizada (div_act=1)
     &prov_divisa = "";  || Codigo de Proveedor. Se pasa a PROVEED/DIVISAS
     ||&divisa = "";       || Codigo de Divisa. Se pasa a ACTUAL. de DIVISAS
     div_ant   = "";
     monba     = "";
     serie     = "";
     montra    = "";
     &divisa   = "";
     cambiomb  = 0;
     cambio    = 0;
     fechasis  = ~;
     totdiv    = 0;
|FIN;

|CALCULO leelin;
     lin = lin + 1;
     #10#0 = #0#0;
     |LEE 000#10=;
     |SI FSalida = 0;
          |DDEFECTO #4;
          #4#0 = codigo;
          #4#28= #12#8;
          #4#1 = lin;
          #4#2 = #0#0;
          #4#3 = #0#9;
          #4#4 = #0#1;
          #4#5 = #0#3;
          #4#37 = #4#5;
          |HAZ precios_div;
          #4#7 = #4#5 * #4#6;
          #4#8 = #0#6;
          #4#27 = #0#7;
          #4#57 = #0#11;
          #4#9 = ((#4#7 < #4#8) < #4#27) < #4#57;
          #4#9 = #4#9 < #6#24;
          #4#9 = #4#9 < #6#25;
          #4#9 = #4#9 < #6#41;
          |HAZ importe_div;
          totdiv = totdiv + #4#34;
          #4#11 = #4#9;
          #4#12 = 0;
          #4#13 = #12#9;
          #4#14 = #10#30;
          |SI #13#30 = "S"; |O #6#90 = "S";
               #4#14 = 0;
          |FINSI;
          #4#15 = "N";
          bruto = bruto + #4#9;
          #4#18 = #10#2;
          #4#20 = #0#4;
          #4#22 = "N";
          |HAZ lisart;
          #4#56 = #4#5 * #10#208;
          |GRABA 020#4n;
          |LIBERA #4;
          |HAZ AltaDa;
     |FINSI;
|FCAL;


|CALCULO genera;
#6#0 = #0#4;
|LEE 101#6=;
|SI FSalida = 0;
     |HAZ CambioDivisa;
     |HAZ Param_Divisas;
     |SI provee ! #0#4;
          |SI provee ! "";
               |HAZ CalCabAgifa081;
               aAlfa = "0000Pedido creado:" + #3#25 + "/" + (("000000" + #3#0)%-106);
               |MENAV aAlfa;
          |FINSI;
          provee = #0#4;
          #3#25 = #12#8;
          enSwMenu = 1;
          |HAZ ContaPC7;
          |GRABA 020#3b;
          codigo = #3#0;
          ||컴컴컴컴컴컴컴컴컴컴
          total = 0;
          bruto = 0;
          totdiv = 0;
          lin = 0;
     |FINSI;
||  ----------------- Generacion Lineas Pedido Compra -------------
     |HAZ leelin;
||  ----------------- Generacion Cabecera Pedido Compra -------------
     |DDEFECTO #3;
     #3#0 = codigo;
     #3#4 = #0#4;
     #3#25 = #12#8;
     #3#1 = #12#9;
     #3#2 = #12#9;
     #3#5 = #6#24;
     #3#6 = #6#25;
     #3#17 = #6#41;
     #3#8 = #6#2;
     #3#9 = #6#17;
     bruto = bruto ! 0;
     #3#11 = bruto;
     #3#12 = bruto;
     #3#34 = totdiv;
     #3#35 = #3#34;
     |HAZ visual_totales;
     |HAZ DatoEnvio;
     #3#25 = #12#8;
     #3#26 = #3#25 + "/" + #3#0;   ||Referencia Interna;

     #3#29 = montra;
     #3#27 = divisa;
     #3#32 = monba;
     #3#33 = cambio;
     #3#28 = cambiomb;
     #3#44 = #3#44 + #4#56;
|| ------ Acumulacion En Proveedor -----------;
     fmes = #3#1 % A402;
     mes = fmes + 42;
     #6mes = #6mes +. #4#9;
     #6#55 = #6#55 +. #4#9;
     |GRABA 020#6a;
     |LIBERA #6;
     |GRABA 000#3a;
     |LIBERA #3;

     enImpPrvPed = #4#9;
     efFecha = #3#1;
     eaProve = #3#4;
     |HAZ AcumulaPrv;

|FINSI;
|FCAL;

|DEFBUCLE proceso;
#0#2;
3;
"    " , 1;
"zzzz" , 999999999;
;
NULL,genera;
|FIN;

|CALCULO temp226;
     |ABRE #142; |LEE 000#142p; |CIERRA #142;

     |ABRE #13;
     #13#26 = #12#8;
     |LEE 020#13=;
     |CIERRA #13;

     |HAZ LeeMonBase;
     |ABRE #3;
     |ABRE #4;
     |ABRE #5;
     |ABRE #6;
     |ABRE #10;
     |BUCLE proceso;
     |SI provee ! "";
          |HAZ CalCabAgifa081;
          aAlfa = "0000Pedido creado:" + #3#25 + "/" + (("000000" + #3#0)%-106);
          |MENAV aAlfa;
     |FINSI;
     |CIERRA #3;
     |CIERRA #4;
     |CIERRA #5;
     |CIERRA #6;
     |CIERRA #10;
|FCAL;

|CALCULO lisart;
     |CIERRA #10;
     enForma = 1;
     eaDocu = "agifa067";
     |HAZ StockEntra;
     |ABRE #10;
|FCAL;
========================================================================
|PROCESO precios_div; || Pone precios (Base,divisa y visualizable)
|SI montra = "D";  || Si la moneda de trabajo es la divisa ...
  #4#33 = #0#5;   || ... el precio en divisa es el precio de prepedidos
  #4#6 = #0#5 / cambio * cambiomb; || Precio en mon base
  #4#35 = #4#6;   || Precio visualizable
|SINO;            || Si la moneda de trabajo es la moneda base ...
  #4#6 = #0#5;  || ... el precio en base es el precio de prepedidos
  #4#33 = #0#5 / cambiomb * cambio; || Precio en divisa
  #4#35 = #4#33; || Precio visualizable
|FINSI;
|FPRC;

|PROCESO importe_div;  || Pone el importe total visualizable y divisa.
|SI montra = "D";  || Si la moneda de trabajo es la divisa ...
  #4#36 = #4#9;        ||Importe Total visualizable
  #4#34 = #4#5 * #0#5; || Importe Total en divisa
  #4#34 = #4#34 < #4#8;
|SINO;            || Si la moneda de trabajo es la moneda base ...
  #4#34 = #4#9 / cambiomb * cambio;  ||I.Total en divisa
  #4#36 = #4#34;        || I. Total visualizable
|FINSI;
|FPRC;

|PROCESO visual_totales;
   |SI montra = "D";
     #3#37 = #3#34;
     #3#38 = #3#35;
||     #3#39 = #3#36;
     #3#40 = #3#11;
     #3#41 = #3#12;
||     #3#42 = #3#13;
   |SINO;
     #3#37 = #3#11;
     #3#38 = #3#12;
||     #3#39 = #3#13;
     #3#40 = #3#34;
     #3#41 = #3#35;
||     #3#42 = #3#36;
   |FINSI;
|FPRC;

|PROCESO Param_Divisas; |TIPO 0;
     |ABRE #agifa324;
     #agifa324#0 = divisa;
     |LEE 001#agifa324.=;   || Leo la divisa
     |CIERRA #agifa324;

     decim_divisa = #agifa324#7;  || Decimales de la divisa
     precio_divisa = #agifa324#9;  || Decimales en el precio de la divisa
     nDeci_DivP = decim_divisa;   || Decimales de la divisa
     nDeci_PreDivP = precio_divisa;  || Decimales en el precio de la divisa
     cambiomb = #agifa324#3; ||Cambio divisa
     |SI montra = "B";   ||Si trabajo con la moneda base ...
          nDeci_PreVisP   = precio_divisa;
          nDeci_ImpVisP   = decim_divisa;
          nDeci_Base     = decim_base;
          nDeci_PreBase  = precio_base;
          nDeci_BaseMxP  = decim_base;
          nDeci_PreBasMxP= precio_base;
          nDeci_TotVisP  = decim_base;
          nDeci_TotNoVisP = decim_divisa;
     |SINO;     ||Si trabajo con la divisa ...
          nDeci_PreVisP   = precio_base;
          nDeci_ImpVisP   = decim_base;
          nDeci_Base     = decim_base; || Max. precision en los decimales de la moneda base
          nDeci_PreBase  = precio_base;  || Max. precision en los dec. del precio de la moneda base
          |SI cambiomb = 1;
               nDeci_BaseMxP   = decim_base;    || sin triangulacion
               nDeci_PreBasMxP = precio_base;
          |SINO;
               nDeci_BaseMxP   = #agifa321#10;  || con triangulacion
               nDeci_PreBasMxP = #agifa321#11;
          |FINSI;
          nDeci_TotVisP   = decim_divisa;
          nDeci_TotNoVisP = decim_base;
     |FINSI;
     |HAZ Deci_IVAC;
|FPRC;

|PROCESO CambioDivisa;
     divisa = #agifa112#81; || Asigno la divisa por defecto del proveedor
     montra = #agifa112#82; || Asigno la moneda de trabajo del proveedor
     |ABRE #agifa007;
     #agifa007#26 = #0#8;  || Serie Pantalla.
     |LEE 001#agifa007.=;  || Leo la serie
     |SI #agifa007#39 = "S"; |Y #agifa321#2 = "S";
          |SI #agifa112#80 = "S"; || Si el proveedor es de divisa pactada
               |ABRE #agifa323;
               |ABRE #agifa322;
               #agifa322#0 = #0#4;
               #agifa322#2 = divisa;
               #agifa322#7 = "N";
               |SELECT #2#agifa322;  || Con esta clave ...
               |LEE 011#agifa322.=;  || ... busco periodos no finalizados de esa divisa
               |SI FSalida = 0;  || Si existe un periodo no finalizado
                    |PARA div_act = 0; |SI div_act = 0; |HACIENDO;
                         divisa   = #agifa322#2;        || Cargo la divisa ...
                         cambiomb = #agifa322#3;        || ...y el cambio
                         |LEE 001#agifa322.=;   || Leo las lineas de Proveed/Divisas
                         nfecha = #12#9 - #agifa322#6;  || Resto la fecha de fin de periodo a la actual
                         |SI nfecha > 0;  || Si la fecha actual supera a la del periodo asignado ...
                              |MENAV "0000ATENCION deberia Actualizar primero la divisa en Proveedores/Divisas";
                               prov_divisa = #0#4;  || Le envio el cod. de proveedor a "agifa323"
                              |SUB_EJECUTA_NP "agifa323.tab";  || Ejecuto Proveedores/Divisas
                              |ERROR;
                         |SINO;
                               div_act = 1;  || Para salir del PARA
                         |FINSI;
                    |SIGUE;
               |SINO;  || Si no existe un periodo no finalizado para esa divisa
                    |MENAV "0000No existe un periodo activo para la divisa en Proveedores / Divisas";
                    prov_divisa = #0#4;  || Le paso a "agifa323" el proveedor
                    |SUB_EJECUTA_NP "agifa323.tab";  || Ejecuto Proveedores / Divisas
                    |ERROR;
                    ||#agifa081#27 = #agifa081#32;
                    ||#agifa081#28 = #agifa081#33;
               |FINSI;
               |SELECT #1#agifa322;
               |CIERRA #agifa322;
               |CIERRA #agifa323;
          |SINO; || Si el Proveedor no es de Divisa pactada ...
               |ABRE #agifa324; || ...miro directamente en divisas
               #agifa324#0 = divisa;
               |LEE 001#agifa324.=;
               |SI FSalida = 0; ||Si existe la divisa...
                    cambiomb = #agifa324#3; || la recojo
                    |SI #agifa324#6 ! -1; || Si los dias de margen de actualizacion no
                                  || son = -1 (indefinidos)
                         |PARA div_act = 0; |SI div_act = 0; |HACIENDO;
                              divisa   = #agifa324#0;        ||Cargo el valor de la divisa
                              cambiomb = #agifa324#3;        ||Cargo el valor del cambio
                              |LEE 001#agifa324.=;  || Leo la divisa
                              nfecha = #12#9 - #agifa324#4;
                              |SI nfecha > #agifa324#6; |Y #agifa324#6 ! -1;  || Si la fecha actual
                                   |MENAV "0000ATENCION deberia Actualizar primero la divisa";
                                   || divisa = #agifa081#27;
                                   |SUB_EJECUTA_NP "agifa326.tab";
                                   |ERROR;
                              |SINO;
                                   div_act = 1;
                              |FINSI;
                         |SIGUE;
                    |FINSI;
               |FINSI;
               |CIERRA #agifa324;
          |FINSI;
     |SINO;
          cambiomb = #agifa081#33;
     |FINSI;
|FPRC;

|PROCESO LeeMonBase; ||Lee los parametros de la moneda base
|ABRE #agifa321;
|ABRE #agifa324;
|LEE 010#agifa321.p;   ||Leo parametros de divisa
monba = #agifa321#9;  ||Cojo la Moneda Base de parametros
#agifa324#0 = #agifa321#9;
|LEE 010#agifa324.=;         ||Leo la ficha de la moneda base en Divisas
cambio      = #agifa324#3;   ||Asigno el cambio de la moneda base con el euro
decim_base  = #agifa324#7;   ||Asigno los decimales de la moneda base
precio_base = #agifa324#9;   ||Asigno los decimales del precio en la mon. base
|CIERRA #agifa324;
|CIERRA #agifa321;
|FPRC;

|PROCESO DatoEnvio;
     |DFICE aDirEmp;            || tomo el directorio de la empresa
     |DFICB aDatos;             || tomo el directorio de los datos
     aDirEmp = aDirEmp % -205;  || me quedo con los 5 digitos de la empresa
     nCodEmp = aDirEmp;         || y los paso a una variable numerica
     |PATH_DAT #11 aDatos;      || pongo el path al fichero agifa041
     |ABRE #11;                 || abre el fichero agifa041
     #11#0 = nCodEmp;
     |LEE 000#11=;              || y busco la empresa en la que estoy
     |SI FSalida = 0;
          #3#19 = #11#1;        || nombre de la empresa
          #3#18 = #11#2;        || direccion de la empresa
          #3#20 = #11#3;        || poblacion de la empresa
          #3#21 = #11#4;        || provincia de la empresa
     |FINSI;
     |CIERRA #11;               || cierro el fichero agifa041
|FPRC;

|PROCESO dsm00082;
     nLinDa = nLinDa + 1;
     #83#0 = "P";
     #83#1 = #4#28;
     #83#2 = #4#0;
     #83#3 = #4#1;
     #83#4 = nLinDa;
     #83#5 = #82#2;
     |GRABA 020#83n;
|FPRC;

|DEFBUCLE dsm00082;
     #82#1;
     ;
     #4#2, 001;
     #4#2, 999;
     ;
     NULL, dsm00082;
|FIN;

|PROCESO AltaDa;
     |SI #142#206 ! "S"; |ACABA; |FINSI;
     nLinDa = 0;
     |ABRE #83;
     |BUCLE dsm00082;
     |CIERRA #83;
|FPRC;
