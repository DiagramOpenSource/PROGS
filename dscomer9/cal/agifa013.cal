|FICHEROS;
     agifa013 #0;
     agifa010 #1;
     dsm00020 #20;
     agifa007 #40;
     dsm00053 #53;
     agifa071 #71;
|FIN;

|VARIABLES;
     aSwImpreLin = "";
     aParametro = "";
     nSwCoimasa = 0;
     &enSwDA = 0;
     &eaChampiEmarInf = "";
     nSwChampiEmar = 0;
     aAlfa = "";
     nBulto = 0;
     sw = 0;
     desde = "                               ";
     hasta = "zzzzzzzzzzzzzzzzzzzzzzzzzzzzzzz";
     informe = "";
     informe1 = "agifa013";
     etiq = 1;
     nombre = "";
     &num_eti = 0;
     &nombre1 = "";
     &direc1 = "";
     &pobla1 = "";
     &provi1 = "";
     &etiqueta1 = 0;
     &bultos1 = 0;
     &serie1 = "";
     &factura1 = 0;
     &fecha1 = @;
     &nombre2 = "";
     &direc2 = "";
     &pobla2 = "";
     &provi2 = "";
     &etiqueta2 = 0;
     &bultos2 = 0;
     &serie2 = "";
     &factura2 = 0;
     &fecha2 = @;

     &ser_alb     = "";
     &num_alb     = 0;
     &eaImpreEti = "";
     &enLinea = 0;
|FIN;

|PROCESO DesAmpli;
     aAlfa = #53#5; |QBF aAlfa;
     |SI aAlfa ! "";
          enSwDA = 1;
          |PRINT;
     |FINSI;
|FPRC;

|DEFBUCLE DesAmpli;
     #53#1;
     ;
     "A", #71#29, #71#0, #71#1, 0001;
     "A", #71#29, #71#0, #71#1, 9999;
     ;
     NULL, DesAmpli;
|FIN;

|PROCESO ChampEmarAlb;
     informe = eaChampiEmarInf;
     |INFOR informe;
     |SI FSalida = 0;
          etiq = 1;
          |HAZ carga1;
          enSwDA = 0;
          |PRINT;
          |BUCLE DesAmpli;
          |PIEINF;
          |FININF;
          |HAZ descarga1;
     |SINO;
          aAlfa = "0000Error en informe '" + informe + "' ...";
          |MENAV aAlfa;
          |ERROR10;
     |FINSI;
|FPRC;

|DEFBUCLE ChampEmarAlb;
     #1#3;
     43;
     #0#0 , #0#4 , #0#6 , #0#2, "N";
     #0#1 , #0#5 , #0#7 , #0#3, "N";
     ;
     NULL, NULL, ChampEmarAlb;
|FIN;

|PROCESO ImpLineas;
     |PRINT;
|FPRC;

|PROCESO Impresion;
     |HAZ mirainf;

     |SI nSwCoimasa = 0; |Y #20#10 = "S";
          aAlfa = "coimz024;" + #1#52 + #1#0;
          |SUB_EJECUTA_NP aAlfa;
     |FINSI;

     |SI eaChampiEmarInf ! ""; informe = eaChampiEmarInf; |FINSI;

     |INFOR informe;
     |SI FSalida = 0;
          |SI aSwImpreLin = "S";
               |BUCLELIN 0 ImpLineas #1;
               |PIEINF;
          |SINO;
               |PARA etiq = 1;|SI etiq [ nBulto;|HACIENDO etiq = etiq + 1;
                    |HAZ carga1;
                    |PRINT;
                    |PIEINF;
                    |HAZ descarga1;
               |SIGUE;
          |FINSI;
          |FININF;
     |SINO;
          aAlfa = "0000Error en informe '" + informe + "' ...";
          |MENAV aAlfa;
     |FINSI;
|FPRC;

|CALCULO albenpr1;|TIPO 0;
     |SI #1#2 = 0; |Y aSwImpreLin = "N";
          |ERROR;
     |SINO;
          nBulto = #1#2;
          |HAZ Impresion;
     |FINSI;
|FCAL;

|DEFBUCLE calalben0;
     #1#3;
     43;
     #0#0 , #0#4 , #0#6 , #0#2, "N";
     #0#1 , #0#5 , #0#7 , #0#3, "N";
     ;
     NULL,albenpr1;
|FIN;

|CALCULO carga1;
nombre = #1#29;
|QBF nombre;
|SI nombre = "";
   nombre1 = #1#4;
|SINO;
   nombre1 = #1#29;
|FINSI;
direc1 = #1#30;
pobla1 = #1#31;
provi1 = #1#33;
etiqueta1 = etiq;
bultos1 = #1#2;
factura1 = #1#0;
fecha1 = #1#1;
|FCAL;

|CALCULO descarga1;
nombre1 = "";
direc1 = "";
pobla1 = "";
provi1 = "";
etiqueta1 = 1;
bultos1 = 0;
serie1 = "";
factura1 = 0;
|FCAL;

|PROCESO mirainf;
     |ABRE #40;
     #40#26 = #1#52;
     |LEE 001#40=;
     |SI FSalida = 0;
          informe = #40#13; |QBF informe;
     |SINO;
          informe = informe1;
     |FINSI;
     |CIERRA #40;

     |ABRE #20;
     #20#0 = #1#88;
     |LEE 000#20=;
     |SI FSalida = 0; |Y #20#9 ! "        ";
          informe = #20#9; |QBF informe;
     |FINSI;
     |CIERRA #20;
|FPRC;

|PROCESO ImpLin;
     |ABRE #1;
     #1#52 = ser_alb;
     #1#0 = num_alb;
     |LEE 020#1=;
     |SI FSalida = 0;
          |ABRE #71;
          #71#29 = #1#52;
          #71#0 = #1#0;
          #71#1 = enLinea;
          |LEE 020#71=;
          |SI FSalida = 0;
               nBulto = #1#2;
               |SI nBulto = 0; nBulto = 1; |FINSI;
               |HAZ Impresion;
          |FINSI;
          |CIERRA #71;
     |FINSI;
     |CIERRA #1;
|FPRC;

|CALCULO Tipo3;  |TIPO 3;
     |SI eaImpreEti = "";
          |IMPRE 1;
     |SINO;
          Impresora = eaImpreEti;
          |IMPRE -1;
     |FINSI;
     |SI FSalida = 0;
          |SI nSwChampiEmar = 0;
               |BUCLE ChampEmarAlb;

          |SINO;    || la estandar
               |SI aParametro = "LIN";
                    |HAZ ImpLin;
               |SINO;
                    |BUCLE calalben0;
               |FINSI;
          |FINSI;
          |FINIMP;
     |FINSI;
|FCAL;

|PROGRAMA;
     |HAZ EsDHL;
     |SI FSalida = 0; |CORRE "tecaz033"; |FINSI;

     |HAZ EsInforAlba;
     |SI FSalida = 0; |CORRE "ialbz023"; |FINSI;


     |HAZ SacaIVA;
     |HAZ MiraLogos; || Copia Logos para el Crystal

     |HAZ EsChampiEmar; nSwChampiEmar = FSalida;
     |HAZ EsCoimasa; nSwCoimasa = FSalida;

     |HAZ EsGofre;
     |SI FSalida = 0;
          aSwImpreLin = "S";
     |FINSI;

     |SI nSwChampiEmar = 0;
          |SUB_EJECUTA_NP "champi12;SELE";
          |SI eaChampiEmarInf = "*NINGUNO*"; |ACABA; |FINSI;
     |FINSI;

     aParametro = PARAMETRO;
     |SI ser_alb = ""; |Y num_alb = 0;
         |CLS;
         |CABEZA "Emitir Etiquetas Envio";
         |PINPA #0#0;
         |PINDA #0#0;
         |ENDATOS #1#0;
         |SI FSalida = 0;  |HAZ Tipo3;  |FINSI;
     |SINO;
         |DDEFECTO #0;
         #0#6  = ser_alb;
         #0#7  = ser_alb;
         #0#2  = num_alb;
         #0#3  = num_alb;
         #0#4 = "01.01.0000";
         #0#5 = "31.12.9999";
         |HAZ Tipo3;
     |FINSI;
|FPRO;
