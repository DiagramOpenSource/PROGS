|FICHEROS;
     malpe144 #0;
     malpe141 #1;
     referen2@ #2;
     referen3@ #3;
|FIN;

|VARIABLES;
     &eaBusca = "";
     &enBusCampo = -1;
     &enError = 0;
     &Tempo = "";
     aBusca = "";
     aFicSql = "";
     nHandle = 0;
     aFich = "";
     aOrigen = "";
     aDestino = "";
     aTmp = "";
     aTmp2 = "";
     aTmp3 = "";
     nBoton1 = 0;
     nBoton2 = 0;
     nBoton3 = 0;
     nBoton4 = 0;
     nBoton5 = 0;
     nBoton6 = 0;
     nGrid2 = 0;
     nGrid3 = 0;
     aAlfa = "";
     aEsc1 = "";
     aEsc2 = "";
     aRetu = "";
     aDef = "";
     nPanta = 0;
     nRango = 0;
     aTecla = "";
     aTipo = "";
     i = 0;
|FIN;

|PROCESO Prueba;         ||| Para hacer pruebas
     eaBusca = "7";
     aTmp = Usuario;
     enBusCampo = 0;
   ||  |HAZ CreaSql;

     aBusca = "'" + eaBusca + "'";
     aAlfa = "SELECT * FROM malpe141 INTO " + aTmp + " WHERE 0 LIKE " + aBusca;

     aAlfa = aAlfa + " OR 10 > 0"; || Le he a¤adido esto y ahora funciona !!!
     |SQL aAlfa;
     aAlfa = "00" + FSalida;
     |MENAV aAlfa;
|FPRC;

|PROCESO CreaSql;
     |DFICE aAlfa;
     aFicSql = aAlfa + "sql" + Usuario;
     |XABRE "WB", aFicSql, nHandle;
     aBusca = "'" + eaBusca + "'";
     aAlfa = "* FROM malpe141" + " INTO " + aTmp + " WHERE ";
     |XGRABA nHandle, aAlfa;

     |SI enBusCampo ] 0;
          aAlfa = "0 LIKE " + aBusca;             ||Al poner solo un campo no funciona,
          aAlfa = aAlfa + " OR 0 LIKE " + aBusca; ||le pongo el mismo 2 veces y parece ok
          |XGRABA nHandle, aAlfa;                 ||Tue Sep  5 19:04:08 CEST 2006
     |SINO;
          |PARA i = 0; |SI i [ 43; |HACIENDO i = i + 1;
               aTipo = "|C" + (("000" + i) % -103) + "TIPO";
               aTipo = #1aTipo;
               |SI aTipo = "A";    || Solo en los campos Alfa
                    |SI i = 0;
                         aAlfa = i + " LIKE " + aBusca;
                    |SINO;
                         aAlfa = " OR " + i + " LIKE " + aBusca;
                    |FINSI;
                    |XGRABA nHandle, aAlfa;
               |FINSI;
          |SIGUE;
     |FINSI;
     |XCIERRA nHandle;

     aAlfa = "SELECT @" + aFicSql;
     |SQL aAlfa;

     ||aAlfa = "00" + FSalida; |MENAV aAlfa;

     |FBORRA aFicSql;
|FPRC;

|PROCESO Busca;
     |SUB_EJECUTA_NP "malpe146";
     |SI enError = 0;
          |CIERRA #1;
          |CIERRA #2;

          |NOME_DAT #1#aTmp2#;
          |HAZ CreaSql;
          aAlfa = "malpe141";|NOME_DAT #1#aAlfa#;

          aOrigen = aFich + aTmp + ".ixx";
          aDestino = aFich + aTmp2 + ".ixx";
          |COPIA_FICHERO aOrigen, aDestino;
          aOrigen = aFich + aTmp + ".dat";
          aDestino = aFich + aTmp2 + ".dat";
          |COPIA_FICHERO aOrigen, aDestino;

          |NOME_DAT #2#aTmp#; |DELINDEX #2; || borra el tmp creado por SQL
          |NOME_DAT #2#aTmp2#;

          |ABRE #1;
          |ABRE #2;
          |REFRESCACONTROL nGrid2;

          |ESTADO_CONTROL nBoton6, "ENABLE";
     |FINSI;
|FPRC;

|PROCESO BorraBusca;
     |ESTADO_CONTROL nBoton6, "DISABLE";

     |CIERRA #2;
     aOrigen  = aFich + "malpe141.dat";
     aDestino = aFich + aTmp2 + ".dat";
     |COPIA_FICHERO aOrigen, aDestino;
     aOrigen  = aFich + "malpe141.ixx";
     aDestino = aFich + aTmp2 + ".ixx";
     |COPIA_FICHERO aOrigen, aDestino;
     |NOME_DAT #2#aTmp2#;
     |ABRE #2;
     |REFRESCACONTROL nGrid2;
|FPRC;

|PROCESO PasaArriba;
     #2#0 = #3#0;
     #2#1 = #3#1;
     #2#44 = #3#44;
     |LEE 121#2=;
     |SI FSalida = 0;
          #2#44 = "N";
          |GRABA 020#2a;
          |LIBERA #2;

          #3#44 = "N";
          |GRABA 020#3a;
          |LIBERA #3;

          #1#0 = #3#0;
          #1#1 = #3#1;
          |LEE 121#1=;
          |SI FSalida = 0;
               #1#44 = "N";
               |GRABA 020#1a
               |LIBERA #1;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO PasaAbajo;
     #3#0 = #2#0;
     #3#1 = #2#1;
     #3#44 = #2#44;
     |LEE 121#3=;
     |SI FSalida = 0;
          #3#44 = "P";
          |GRABA 020#3a;
          |LIBERA #3;

          #2#44 = "P";
          |GRABA 020#2a;
          |LIBERA #2;

          #1#0 = #2#0;
          #1#1 = #2#1;
          |LEE 121#1=;
          |SI FSalida = 0;
               #1#44 = "P";
               |GRABA 020#1a
               |LIBERA #1;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO UnoArriba;
     |HAZ PasaArriba;
     |REFRESCACONTROL nGrid2;
     |REFRESCACONTROL nGrid3;
|FPRC;

|PROCESO UnoAbajo;
     |HAZ PasaAbajo;
     |REFRESCACONTROL nGrid2;
     |REFRESCACONTROL nGrid3;
|FPRC;

|PROCESO TodosArriba;
     |SELECT #1#3;
     |LEE 101#3p;
     |PARA; |SI FSalida = 0; |HACIENDO;
          |SI #3#44 = "P";
               |HAZ PasaArriba;
          |FINSI;
          |LEE 101#3s;
     |SIGUE;
     |SELECT #2#3;
     |REFRESCACONTROL nGrid2;
     |REFRESCACONTROL nGrid3;
|FPRC;

|PROCESO TodosAbajo;
     |SELECT #1#2;
     |LEE 101#2p;
     |PARA; |SI FSalida = 0; |HACIENDO;
          |SI #2#44 = "N";
               |HAZ PasaAbajo;
          |FINSI;
          |LEE 101#2s;
     |SIGUE;
     |SELECT #2#2;
     |REFRESCACONTROL nGrid2;
     |REFRESCACONTROL nGrid3;
|FPRC;

|PROCESO Evento2;
     |SI FSalida = 1;  |O FSalida = 2;
         |LEE 000#2=;
     |SINO;
          |SI FSalida = 4;
               |HAZ UnoAbajo;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Alta2;
     #2#44 = "N";
     #2#0 = "zzzzz";
     #2#1 = "zzzzzzzz";
|FPRC;

|PROCESO Baja2;
     #2#44 = "N";
     #2#0 = "     ";
     #2#1 = "        ";
|FPRC;

|PROCESO Evento3;
     |SI FSalida = 1;  |O FSalida = 2;
         |LEE 000#3=;
     |SINO;
          |SI FSalida = 4;
               |HAZ UnoArriba;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Alta3;
     #3#44 = "P";
     #3#0 = "zzzzz";
     #3#1 = "zzzzzzzz";
|FPRC;

|PROCESO Baja3;
     #3#44 = "P";
     #3#0 = "     ";
     #3#1 = "        ";
|FPRC;

|PROCESO Tipo80; |TIPO 80;
     FSalida = 3299;
|FPRC;

|PROGRAMA;
||HAZ Prueba;
     |CLS;
     |CABEZA "Seleccion";
     |ABRE #0;
     |LEE 101#0=;
     |SI FSalida ! 0; |GRABA 020#0b; |FINSI;

     |PINPA #0#0;  nPanta = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,\/", 1844, 1847, TodosAbajo;
     nBoton1 = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,/\", 1855, 1858, TodosArriba;
     nBoton2 = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,\/", 1869, 1872, UnoAbajo;
     nBoton3 = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,/\", 1882, 1885, UnoArriba;
     nBoton4 = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,{{155}} Buscar", 1802, 1812, Busca;
     nBoton5 = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,{{207}} Borrar Busqueda", 1814, 1830, BorraBusca;
     nBoton6 = FSalida;

     |ESTADO_CONTROL nBoton6, "DISABLE";

     |DDEF aAlfa;
     aDef = aAlfa + "malpe141";
     |CARGA_DEF aDef, referen2@;
     |CARGA_DEF aDef, referen3@;

     |HAZ CreaTempo; aTmp = Tempo; |NOME_DAT #2 aTmp; |DELINDEX #2;
     |HAZ CreaTempo; aTmp2 = Tempo; |NOME_DAT #2 aTmp2; |DELINDEX #2;
     |HAZ CreaTempo; aTmp3 = Tempo; |NOME_DAT #3 aTmp3; |DELINDEX #3;

     |DFICE aFich;  |QBF aFich;
     aOrigen  = aFich + "malpe141.dat";
     aDestino = aFich + aTmp2 + ".dat";
     |COPIA_FICHERO aOrigen, aDestino;
     aDestino = aFich + aTmp3 + ".dat";
     |COPIA_FICHERO aOrigen, aDestino;

     aOrigen  = aFich + "malpe141.ixx";
     aDestino = aFich + aTmp2 + ".ixx";
     |COPIA_FICHERO aOrigen, aDestino;
     aDestino = aFich + aTmp3 + ".ixx";
     |COPIA_FICHERO aOrigen, aDestino;

     |ABRE #1;
     |ABRE #2;
     |ABRE #3;

     nRango = 4 + 8 + 16 + 32 + 128;

     |LINEAL_SIMPLE #2#2, nRango, 0702, 1698, Baja2, Alta2, Evento2;
     nGrid2 = FSalida;

     |LINEAL_SIMPLE #2#3, nRango, 2102, 3198, Baja3, Alta3, Evento3;
     nGrid3 = FSalida;

     |FOCOTECLADO nGrid2;

     aEsc1  = &255 + "806";
     aEsc2  = &255 + "807";
     aRetu  = &255 + "802";
     |PARA; |SI;  |HACIENDO;
         FSalida = "::{{001}}Salir," + nGrid2;
         |LEETECLA aTecla;
         |SI aTecla = aEsc1; |SAL;  |FINSI;
         |SI aTecla = aEsc2; |SAL;  |FINSI;
         |SI aTecla = aRetu; |SAL;  |FINSI;
     |SIGUE;

     |CIERRA #1;
     |CIERRA #2;
     |CIERRA #3;

     Tempo = aTmp2; |HAZ BoraTempo; |DELINDEX #2;
     Tempo = aTmp3; |HAZ BoraTempo; |DELINDEX #3;
     |DESCARGA_DEF #referen2@;
     |DESCARGA_DEF #referen3@;

     |FIN_CONTROL_WINDOWS nBoton1;
     |FIN_CONTROL_WINDOWS nBoton2;
     |FIN_CONTROL_WINDOWS nBoton3;
     |FIN_CONTROL_WINDOWS nBoton4;
     |FIN_CONTROL_WINDOWS nBoton5;
     |FIN_CONTROL_WINDOWS nBoton6;
     |PULSATECLA;

     |CIERRA #0;
|FPRO;
