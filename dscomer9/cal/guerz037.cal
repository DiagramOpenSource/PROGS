||guer1037.inf
|FICHEROS;
     guerz037 #0;
     gotrabaj #1;
     aomentra@ #2;
     labtraba@ #3;
     goptrbli #5;
     gopartia #10;
     gopartca #20;
     gopartll #21;
     gopartli #22;
     goptrbca #23;
     guerw034 #34;  ||Tmp
|FIN;

|VARIABLES;
     nDObra = 0;
     nHObra = 0;
     nLinea = 0;
     aPath = "";
     aAlfa = "";
     aDni = "";
|FIN;

|PROCESO Tmp;
     |PRINT;
|FPRC;

|DEFBUCLE Tmp;
     #34#2;
     ;
     INICIO;
     FINAL;
     ;
     NULL, Tmp;
|FIN;

|PROCESO aomentra;
     |DDEFECTO #3;
     #3#0 = #2#2; ||Empresa
     #3#1 = #2#3; ||Traba
     |LEE 000#3=;
     aDni = #3#7;
     |SI FSalida ! 0;
          aAlfa = "0000No existe Empresa/Trabajador" + #2#2 + "-" + #2#3;
          ||MENAV aAlfa;
          aDni = "*" + #2#2 + "-" + #2#3;
     |FINSI;

     |DDEFECTO #34;
     aAlfa = ("00000" + #2#1) % -105;
     #34#0 = #2#1; ||Obra
     #34#1 = aDni;
     #34#2 = #2#0; || Fecha
     #34#10 = #2#4;
     |LEE 101#34=;
     |SI FSalida ! 0; |GRABA 020#34b; |FINSI;
     #34#5 = #34#5 + #2#6; ||Imp Noninas
     #34#6 = #34#6 + #2#5; ||Horas
     #34#7 = #3#2;
     |SI #34#8 = "C"; |O #34#8 = "X";
          #34#8 = "X";
     |SINO;
          #34#8 = "N";
     |FINSI;
     |GRABA 020#34a;
     |LIBERA #34;
|FPRC;

|DEFBUCLE aomentra;
     #2#1006;
     ;
     #0#0, 00001, 00001, nDObra, 000000, 01;
     #0#1, 99999, 99999, nHObra, 999999, 99;
     ;
     NULL, aomentra;
|FIN;

|PROCESO gopartia;
     #34#3 = #34#3 + #10#25;
|FPRC;

|DEFBUCLE gopartia;
     #10#1;
     ;
     "T", #23#15, #23#3, nLinea, 1;
     "T", #23#15, #23#3, nLinea, 999;
     ;
     NULL, gopartia;
|FIN;

|PROCESO goptrbli;
     |DDEFECTO #1;
     #1#0 = #5#4;
     |LEE 000#1=;
     aDni = #1#12;
     |SI FSalida ! 0;
          aAlfa = "0000No existe Trabajador:" + #5#4;
          ||MENAV aAlfa;
          aDni = "*" + #5#4;
     |FINSI;

     |DDEFECTO #34;
     #34#0 = #5#0;  ||Obra
     #34#1 = aDni;
     #34#2 = #5#2; ||Fecha
     #34#10 = #5#26;
     |LEE 101#34=;
     |SI FSalida ! 0; |GRABA 020#34b; |FINSI;

     nLinea = #5#3;
     |BUCLE gopartia;
     #34#4 = #34#4 + #5#11;  ||Horas
     #34#7 = #1#1;
     #34#8 = "C";
     #34#9 = #1#0;
     |GRABA 020#34a;
     |LIBERA #34;
|FPRC;

|DEFBUCLE goptrbca;
     #23#1;
     ;
     #22#14, #22#15, #22#3, #22#0, #22#1;
     #22#14, #22#15, #22#3, #22#0, #22#1;
     ;
     NULL, NULL, goptrbli;
|FIN;

|PROCESO gopartli;
     |BUCLE goptrbca;
|FPRC;

|DEFBUCLE gopartli;
     #22#2;
     14;
     "     ", 000001, #0#0, #0#2;
     "zzzzz", 999999, #0#1, #0#3;
     ;
     NULL, gopartli;
|FIN;

|PROCESO Tipo3; |TIPO 3;
     nDObra = #0#2;
     nHObra = #0#3;

     aAlfa = Usuario;
     |NOME_DAT #34 aAlfa; |DELINDEX #34;

     |IMPRE 0;
     |SI FSalida = 0;
          |INFOR "guerz037";
          |SI FSalida ! 0;
               |MENAV "0000No existe informe guerz037";
          |SINO;
               |ABRE #34;

               |ABRE #1;
               |BUCLE gopartli;
               |CIERRA #1;

               |DBASS aPath;
               aAlfa = aPath + "aginom/def/aomentra";
               |CARGA_DEF aAlfa, aomentra@;
               |SI FSalida = 0;
                    aAlfa = aPath + "aginom/def/labtraba";
                    |CARGA_DEF aAlfa, labtraba@;
                    |SI FSalida = 0;
                         aPath = aPath + "aginom/fich/";
                         |PATH_DAT #3 aPath;
                         |PATH_DAT #2 aPath;
                         |ABRE #3;
|LEE 000#3p;
                         |BUCLE aomentra;
                         |CIERRA #3;
                         |DESCARGA_DEF #labtraba@;
                    |FINSI;
                    |DESCARGA_DEF #aomentra@;
               |FINSI;

               |CIERRA #34;

               |BUCLE Tmp;

               |PIEINF;
               |FININF;
          |FINSI;
          |FINIMP;
     |FINSI;

     |DELINDEX #34;
|FPRC;
