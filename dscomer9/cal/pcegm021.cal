|FICHEROS;
     pcegm021 #0;
     pcegm022 #22;
     pcegm020 #20;
|FIN;

|VARIABLES;
     aCP = "";
     aPlaza = "";
     aCodPlaza = "";
     aAlfa = "";
     aDato = "";
     nHandle = 0;
     aPais = "";
     i         = 0;
     nPos      = 0;
     aLetra    = "";
     aReg      = "";
     aTab      = "";
     aLon      = "";
 {99}Valor     = "";
     nCampo    = 0;
|FIN;

|PROCESO Dato;
     nCampo = 1;
     IValor = Valor1<-;
     aLon = aDato % A0;
     aReg = "";
     |PARA i = 1; |SI i [ aLon; |HACIENDO i = i + 1;
          nPos = i * 100 + 1;
          aLetra = aDato % nPos;
          |SI aLetra = aTab;
               Valor = aReg;
               aReg = "";
               nCampo = nCampo + 1;
               IValor = IValor + 1;
          |SINO;
               aReg = aReg + aLetra;
          |FINSI;
          |SI nCampo > 90; |SAL; |FINSI;     || Por si.... :)
     |SIGUE;
     Valor = aReg;
|FPRC;

|PROCESO Importa;
     |CONFI "0000SImportar fichero de codigos postales?";
     |SI FSalida ! 0; |ACABA; |FINSI;

     |ABRE #20; |LEE 000#20p; |CIERRA #20;
     aAlfa = #20#3; |QBF aAlfa;
     |SI aAlfa = "";
          |DBASE aAlfa;
          aAlfa = aAlfa + "plugins/mdispost.txt";
     |FINSI;

     aTab = &9;
     |XABRE "", aAlfa, nHandle;
     |SI FSalida < 0;
          aAlfa = "0000Error al cargar: " + aAlfa;
          |MENAV aAlfa;
     |SINO;
          |DELINDEX #0;
          |ABRE #0;
          |XLEE nHandle, 250, aDato;
          |PARA; |SI FSalida > 0; |HACIENDO;
               |HAZ Dato;
               aPais = Valor1;
               aCP = Valor2;
               aPlaza = Valor3;
               aCodPlaza = Valor4;

               #0#0 = aPais;
               #0#1 = aCP;
               #0#2 = aPlaza;
               #0#3 = aCodPlaza;
               |GRABA 020#0n;

               |XLEE nHandle, 250, aDato;
          |SIGUE;
          |XCIERRA nHandle;
          |CIERRA #0;
     |FINSI;

     |ABRE #22;

     #22#0 = "ES";
     |LEE 101#22=;
     |SI FSalida ! 0; |GRABA 020#22b; |FINSI;
     #22#1 = "ESPA¥A";
     |GRABA 020#22a;


     #22#0 = "PT";
     |LEE 101#22=;
     |SI FSalida ! 0; |GRABA 020#22b; |FINSI;
     #22#1 = "PORTUGAL";
     |GRABA 020#22a;

     |CIERRA #22;
|FPRC;


|PROCESO Tipo8; |TIPO 8;
     |LEE 000#0p;
     |SI FSalida ! 0;
          |CIERRAT #0;
          |HAZ Importa;
          |ABRET #0;
     |FINSI;
|FPRC;

|PROCESO Tipo20; |TIPO 20;
     |SI FSalida = "OPCION";
          FSalida = "Importar";
          |ACABA;
     |FINSI;
     |CIERRAT #0;
     |HAZ Importa;
     |ABRET #0;
|FPRC;
