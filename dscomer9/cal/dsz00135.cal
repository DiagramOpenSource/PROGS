|FICHEROS;
     dsz00135;
     agifa024;
     agifa134;
     dsm00282;
     dsw00107;

     dscam003@ #3;
     dscam012@;
     dscam044@;
     dscam045@;
|FIN;

|VARIABLES;
     nEmpresa     = 0;
     nHay         = 0;
     nCampo       = 0;
     nLong        = 0;
     nPos         = 0;
     nHandle      = 0;
     nObserva     = 0;
     nTot1        = 0;
     nTot2        = 0;

     aEmp         = "";
     aLong        = "";
     aBase        = "";
     aAlfa        = "";
     aAlfa1       = "";
     aAlfa2       = "";
     aPathLocal   = "";
     aPathServ    = "";
     aFicCSV      = "";
     aHora        = "";
     aCtaCble     = "";
     aOrigen      = "";
     aDestino     = "";
     aAbre        = "";

     fFecha       = @;

     {1}aContiene = "";

     &Tempo       = "";
     &eaCta       = "";
     &nDeci_Base  = 0;
     &enTipo      = 0;
     &eswCta      = 0;
|FIN;

|PROCESO Tipo0Excl;   |TIPO 0;
     |C_M #dsz00135#37, 1, "S";
     |SI #dsz00135#35 = "N";  |Y #dsz00135#36 = "N";
         |C_M #dsz00135#37, 1, "N";  #dsz00135#37 = "N";
     |FINSI;
     |PINTA #dsz00135#37;
|FPRC;

|PROCESO Tipo0Cuenta;   |TIPO 0;
     eaCta = #dsz00135#Campo#;
     |HAZ MFiltraPtos;
     #dsz00135#Campo# = eaCta;
     |PINTA #dsz00135#Campo#;

     |HAZ MCtaContable;
     |SI eswCta = 1;
         |MENAV "    Error. Longitud de Cuenta fuera de Nivel";
         |ERROR;
     |FINSI;
|FPRC;

|PROCESO Tipo0C7;   |TIPO 0;
     |PARA nCampo = 8;  |SI nCampo [ 24;  |HACIENDO nCampo = nCampo + 1;
           |C_M #dsz00135#nCampo#,  1, "S";
     |SIGUE;

     |SI #dsz00135#7 = "N";
         |PARA nCampo = 10;  |SI nCampo [ 24;  |HACIENDO nCampo = nCampo + 1;
               #dsz00135#nCampo# = "";  |C_M #dsz00135#nCampo#,  1, "N";
         |SIGUE;
     |SINO;
         #dsz00135#8  = "";       |C_M #dsz00135#8,  1, "N";
         #dsz00135#9  = "zzzzz";  |C_M #dsz00135#9,  1, "N";
     |FINSI;

     |PARA nCampo = 8;  |SI nCampo [ 24;  |HACIENDO nCampo = nCampo + 1;
           |PINTA #dsz00135#nCampo#;
     |SIGUE;
|FPRC;

|PROCESO QuitoSerie;
     nHay = 1;

     |PARA nCampo = 27;  |SI nCampo [ 31;  |HACIENDO nCampo = nCampo + 1;
           aAlfa = #dsz00135#nCampo#; |QBF aAlfa;
           aAlfa1 = aAlfa - "*";
           |SI FSalida ! 0;
               aLong  = aAlfa1 % 0;
               nLong  = aLong;
               nPos   = 100 + nLong;
               aAlfa2 = #dscam003@#0 % nPos;
               |SI aAlfa1 = aAlfa2;
                   nHay = 0;
                   |SAL;
               |FINSI;
           |SINO;
               |SI #dscam003@#0 = #dsz00135#nCampo#;
                   nHay = 0;
                   |SAL;
               |FINSI;
           |FINSI;
     |SIGUE;
|FPRC;

|PROCESO dscam003;
     |SI #dscam003@#46 ! nEmpresa;
         |SI nEmpresa = 2;
             |SI #dscam003@#46 ! 99999;
                 |ACABA;
             |FINSI;
         |SINO;
             |ACABA;
         |FINSI;
     |FINSI;



     |SI #dsz00135#35 = "N";
         |SI #dscam003@#75 = "CO";  |ACABA;  |FINSI;
     |FINSI;

     |SI #dsz00135#36 = "N";
         |SI #dscam003@#75 = "SU";  |ACABA;  |FINSI;
     |FINSI;

     |SI #dscam003@#75 ! "CO";  |Y #dscam003@#75 ! "SU";
         |SI #dsz00135#37 = "S";
             |ACABA;
         |FINSI;
     |FINSI;

     #agifa024#0 = #dscam003@#32;
     |LEE 000#agifa024.=;
     |SI FSalida ! 0; |DDEFECTO #agifa024; |FINSI;

     |SI #dsz00135#34 = "C";
         |SI #agifa024#49 ! 1;  |ACABA;  |FINSI;
     |FINSI;

     |SI #dsz00135#34 = "B";
         |SI #agifa024#49 ! 2;  |ACABA;  |FINSI;
     |FINSI;

     |SI #dsz00135#6 = "I";
         |SI #dscam003@#14 ! "I";  |ACABA;  |FINSI;
     |SINO;
         |SI #dscam003@#14 ! "I";  |Y #dscam003@#14 ! "C";  |Y #dscam003@#14 ! "P";
             |ACABA;
         |FINSI;
     |FINSI;

     |SI #dsz00135#7 = "N";
         |SI #dscam003@#0 < #dsz00135#8;  |ACABA;  |FINSI;
         |SI #dscam003@#0 > #dsz00135#9;  |ACABA;  |FINSI;
     |SINO;
         nHay = 0;
         |PARA nCampo = 10;  |SI nCampo [ 24;  |HACIENDO nCampo = nCampo + 1;
               |SI #dscam003@#0 = #dsz00135#nCampo#;
                   nHay = 1;  |SAL;
               |FINSI;
         |SIGUE;

         |SI nHay = 0;  |ACABA;  |FINSI;
     |FINSI;

     |HAZ QuitoSerie;
     |SI nHay = 0;  |ACABA;  |FINSI;

     #agifa134#49 = #dscam003@#0;
     #agifa134#0  = #dscam003@#1;
     |LEE 000#agifa134.=;
     |SI FSalida ! 0;
         |DDEFECTO #agifa134;
         #agifa134#1 = #dscam003@#99;
     |FINSI;

     #dscam012@#0 = #dscam003@#17;
     |LEE 000#dscam012@.=;
     |SI FSalida ! 0;  |DDEFECTO #dscam012@;  |FINSI;

     aAlfa = #dscam012@#3;  |QBF aAlfa;
     |SI aAlfa = "";
         aAlfa = #dscam003@#52;  |QBF aAlfa;
     |FINSI;

     #dsw00107#0  = #dscam003@#5;
     #dsw00107#1  = #dscam003@#0;
     #dsw00107#2  = #dscam003@#1;
     #dsw00107#3  = #dscam003@#2;
     #dsw00107#4  = #dscam003@#3;
     #dsw00107#5  = #dscam003@#26;
     #dsw00107#6  = #dscam003@#64;
     #dsw00107#7  = #dscam003@#65;
     #dsw00107#8  = #dscam003@#36;
     #dsw00107#9  = aAlfa;
     #dsw00107#10 = #dscam003@#39;
     #dsw00107#11 = #dscam003@#15;
     #dsw00107#12 = #dscam003@#6;
     #dsw00107#13 = #dscam003@#32;
     #dsw00107#14 = #dscam003@#40;
     #dsw00107#15 = #agifa134#1;

     |GRABA 000#dsw00107.n;
     |LIBERA #dsw00107;
|FPRC;

|DEFBUCLE dscam003;
     #dscam003@#2002;
     32, 5;
     #dsz00135#4, 000000001, #dsz00135#0, #dsz00135#25;
     #dsz00135#5, 999999999, #dsz00135#1, #dsz00135#26;
     ;
     NULL, dscam003;
|FIN;

|PROCESO dscam045;
     aEmp = ("00000" + #dscam045@#6) % -105;
     aAlfa = aBase + "dscarter/fich/" + aEmp + "/";
     |PATH_DAT #dscam003@#aAlfa#;
     |PATH_DAT #dscam012@#aAlfa#;

     |ABRE #dscam012@;
     |ABRE #agifa134;
     |BUCLE dscam003;
     |CIERRA #dscam012@;
     |CIERRA #agifa134;
|FPRC;

|DEFBUCLE dscam045;
     #dscam045@#1002;
     2;
     nEmpresa, 001, "V";
     nEmpresa, 999, "V";
     ;
     NULL, dscam045;
|FIN;

|PROCESO Pendientes;
     |DBASS aBase;
     aAlfa = aBase + "dscarter/def/dscam045";
     |CARGA_DEF aAlfa, dscam045@;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     aAlfa = aBase + "dscarter/def/dscam044";
     |CARGA_DEF aAlfa, dscam044@;
     |SI FSalida ! 0;
         |DESCARGA_DEF #dscam045@;
         |ACABA;
     |FINSI;

     aAlfa = aBase + "dscarter/def/dscam003";
     |CARGA_DEF aAlfa, dscam003@;
     |SI FSalida ! 0;
         |DESCARGA_DEF #dscam044@;
         |DESCARGA_DEF #dscam045@;
         |ACABA;
     |FINSI;

     aAlfa = aBase + "dscarter/def/dscam012";
     |CARGA_DEF aAlfa, dscam012@;
     |SI FSalida ! 0;
         |DESCARGA_DEF #dscam003@;
         |DESCARGA_DEF #dscam044@;
         |DESCARGA_DEF #dscam045@;
         |ACABA;
     |FINSI;

     |DFICE aAlfa;
     aAlfa    = aAlfa % -205;
     nEmpresa = aAlfa;
     aAlfa = aBase + "dscarter/fich/";
     |PATH_DAT #dscam045@#aAlfa#;

     |BUCLE dscam045;

     |DESCARGA_DEF #dscam012@;
     |DESCARGA_DEF #dscam003@;
     |DESCARGA_DEF #dscam044@;
     |DESCARGA_DEF #dscam045@;
|FPRC;

|PROCESO dsw00107;
     #agifa024#0 = #dsw00107#13;
     |LEE 000#agifa024.=;
     |SI FSalida ! 0; |DDEFECTO #agifa024; |FINSI;

     |SI #dsz00135#34 = "C";
         |SI #agifa024#49 ! 1;  |ACABA;  |FINSI;
     |FINSI;

     |SI #dsz00135#34 = "B";
         |SI #agifa024#49 ! 2;  |ACABA;  |FINSI;
     |FINSI;

     enTipo = 2;  |PRINT;
|FPRC;

|DEFBUCLE dsw00107;
     #dsw00107#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, dsw00107;
|FIN;

|PROCESO CrystalObs;
     #agifa024#0 = #dsm00282#0;
     |LEE 000#agifa024.=;
     |SI FSalida ! 0; |DDEFECTO #agifa024; |FINSI;

     |SI #dsz00135#34 = "C";
         |SI #agifa024#49 ! 1;  |ACABA;  |FINSI;
     |FINSI;

     |SI #dsz00135#34 = "B";
         |SI #agifa024#49 ! 2;  |ACABA;  |FINSI;
     |FINSI;

     #dsw00107#13 = #dsm00282#0;
     #dsw00107#1  = "";
     #dsw00107#2  = 0;
     #dsw00107#3  = 0;
     |LEE 000#dsw00107.];
     |SI FSalida = 0;  |Y  #dsw00107#13 = #dsm00282#0;
         enTipo = 1;  |PRINT;
     |FINSI;
|FPRC;

|PROCESO ImpreObserva;
     #dsm00282#0 = #dsz00135#0;
     #dsm00282#1 = "31.12.9999";
     |LEE 000#dsm00282.];
     |PARA;  |SI FSalida = 0;  |HACIENDO;
          |SI #dsm00282#0 < #dsz00135#0;  |O #dsm00282#1 > #dsz00135#3;
              |SAL;
          |FINSI;

          |SI #dsm00282#1 ] #dsz00135#2;  |Y #dsm00282#1 [ #dsz00135#3;
              |SI #dsz00135#33 ! "T";
                  |SI #dsm00282#6 = #dsz00135#33;
                      |HAZ CrystalObs;
                  |FINSI;
              |SINO;
                  |HAZ CrystalObs;
              |FINSI;
          |FINSI;

          |LEE 000#dsm00282.s;
     |SIGUE;
|FPRC;

|PROCESO PorCrystal;
     Impresora = "CrystalReport";
     |IMPRE -1;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |INFOR "dsz00135";
     |SI FSalida ! 0;
         |FINIMP;
         |ACABA;
     |FINSI;

     |ABRE #dsw00107;
     |HAZ Pendientes;
     |CIERRA #dsw00107;

     |BUCLE dsw00107;

     |ABRE #dsw00107;  |SELECT #2#dsw00107;
     |ABRE #dsm00282;
     |HAZ ImpreObserva;
     |CIERRA #dsm00282;
     |CIERRA #dsw00107;

     |PIEINF;
     |FININF;
     |FINIMP;
|FPRC;

|PROCESO GrabaCSV;
     |XGRABA nHandle, aAlfa;
|FPRC;

|PROCESO Encabezado;
     |SI aCtaCble ! "";
         |SI nTot1 ! 0;  |O nTot2 ! 0;
             aAlfa = ";;;;;;;;";              |HAZ GrabaCSV;
             aAlfa = nTot1 + ";";             |HAZ GrabaCSV;
             aAlfa = nTot2 + ";";             |HAZ GrabaCSV;
             aAlfa = ";" + &13 + &10;         |HAZ GrabaCSV;
         |FINSI;

         aAlfa = ";" + &13 + &10;             |HAZ GrabaCSV;
     |FINSI;

     #agifa024#0 = #dsw00107#13;
     |LEE 000#agifa024.=;
     |SI FSalida ! 0; |DDEFECTO #agifa024; |FINSI;

     |SI #dsz00135#34 = "C";
         |SI #agifa024#49 ! 1;  |ACABA;  |FINSI;
     |FINSI;

     |SI #dsz00135#34 = "B";
         |SI #agifa024#49 ! 2;  |ACABA;  |FINSI;
     |FINSI;

     aAlfa = #dsw00107#0 + ";";         |HAZ GrabaCSV;
     aAlfa = #agifa024#1 + ";";         |HAZ GrabaCSV;
     aAlfa = ";;;;;;;;;;";              |HAZ GrabaCSV;

     aAlfa = "";
     |SI #agifa024#49 = 1;  aAlfa = "CORTADO";    |FINSI;
     |SI #agifa024#49 = 2;  aAlfa = "BLOQUEADO";  |FINSI;

     aAlfa = aAlfa + &13 + &10;   |HAZ GrabaCSV;
|FPRC;

|PROCESO CSVObserva;
     |SI nObserva = 0;
         aAlfa = "Observaciones" + &13 + &10; |HAZ GrabaCSV;

         nObserva = 1;
     |FINSI;

     aAlfa = #dsm00282#0 + ";";                  |HAZ GrabaCSV;
     aAlfa = (#dsm00282#1 % 102) + "/";
     aAlfa = aAlfa + (#dsm00282#1 % 402) + "/";
     aAlfa = aAlfa + (#dsm00282#1 % 704) + ";";  |HAZ GrabaCSV;
     aAlfa = #dsm00282#2;           |QBF aAlfa;
     aAlfa = aAlfa + " " + #dsm00282#3;   |QBF aAlfa;
     aAlfa = aAlfa + " " + #dsm00282#4;   |QBF aAlfa;
     aAlfa = aAlfa + " " + #dsm00282#5;   |QBF aAlfa;
     aAlfa = aAlfa + &13 + &10;                  |HAZ GrabaCSV;
|FPRC;

|PROCESO agifa024OBS;
     |SI #dsz00135#34 = "C";
         |SI #agifa024#49 ! 1;  |ACABA;  |FINSI;
     |FINSI;

     |SI #dsz00135#34 = "B";
         |SI #agifa024#49 ! 2;  |ACABA;  |FINSI;
     |FINSI;

     #dsm00282#0 = #agifa024#0;
     #dsm00282#1 = "31.12.9999";
     |LEE 000#dsm00282.];
     |PARA;  |SI FSalida = 0;  |HACIENDO;
          |SI #dsm00282#0 ! #agifa024#0;
              |SAL;
          |FINSI;

          |SI #dsm00282#1 ] #dsz00135#2;  |Y #dsm00282#1 [ #dsz00135#3;
              |SI #dsz00135#33 ! "T";
                  |SI #dsm00282#6 = #dsz00135#33;
                      |HAZ CSVObserva;
                  |FINSI;
              |SINO;
                  |HAZ CSVObserva;
              |FINSI;
          |FINSI;

          |LEE 000#dsm00282.s;
     |SIGUE;
|FPRC;

|DEFBUCLE agifa024OBS;
     #agifa024#1;
     16;
     #dsz00135#0, #dsw00107#0;
     #dsz00135#1, #dsw00107#0;
     ;
     NULL, agifa024OBS;
|FIN;

|PROCESO Observaciones;
     nObserva = 0;
     |ABRE #dsm00282;
     |BUCLE agifa024OBS;
     |CIERRA #dsm00282;
|FPRC;

|PROCESO dsw00107CSV;
     #agifa024#0 = #dsw00107#13;
     |LEE 000#agifa024.=;
     |SI FSalida ! 0; |DDEFECTO #agifa024; |FINSI;

     |SI #dsz00135#34 = "C";
         |SI #agifa024#49 ! 1;  |ACABA;  |FINSI;
     |FINSI;

     |SI #dsz00135#34 = "B";
         |SI #agifa024#49 ! 2;  |ACABA;  |FINSI;
     |FINSI;

     |SI aCtaCble ! #dsw00107#0;
         |HAZ Encabezado;
         |HAZ Observaciones;

         nTot1 = 0;
         nTot2 = 0;

         aAlfa = "Pedidos pendientes" + &13 + &10; |HAZ GrabaCSV;
         aAlfa = "Cliente;";         |HAZ GrabaCSV;
         aAlfa = "Numero documento;";|HAZ GrabaCSV;
         aAlfa = "Serie;";           |HAZ GrabaCSV;
         aAlfa = "Factura;";         |HAZ GrabaCSV;
         aAlfa = "Recibo;";          |HAZ GrabaCSV;
         aAlfa = "Fecha factura;";   |HAZ GrabaCSV;
         aAlfa = "Fecha vto.;";      |HAZ GrabaCSV;
         aAlfa = "Fecha cobro;";     |HAZ GrabaCSV;
         aAlfa = "Total recibo;";    |HAZ GrabaCSV;
         aAlfa = "Total pendiente;"; |HAZ GrabaCSV;
         aAlfa = "Gastos;";          |HAZ GrabaCSV;
         aAlfa = "Banco;";           |HAZ GrabaCSV;
         aAlfa = "Zona;";            |HAZ GrabaCSV;
         aAlfa = "Estado";           |HAZ GrabaCSV;
         aAlfa = &13 + &10;          |HAZ GrabaCSV;
     |FINSI;

     aAlfa = #dsw00107#13 + ";";         |HAZ GrabaCSV;
     aAlfa = #dsw00107#14 + ";";         |HAZ GrabaCSV;
     aAlfa = #dsw00107#1  + ";";         |HAZ GrabaCSV;
     aAlfa = #dsw00107#2  + ";";         |HAZ GrabaCSV;
     aAlfa = #dsw00107#3  + ";";         |HAZ GrabaCSV;
     aAlfa = (#dsw00107#15 % 102) + "/";
     aAlfa = aAlfa + (#dsw00107#15 % 402) + "/";
     aAlfa = aAlfa + (#dsw00107#15 % 704) + ";";  |HAZ GrabaCSV;
     aAlfa = (#dsw00107#4 % 102) + "/";
     aAlfa = aAlfa + (#dsw00107#4 % 402) + "/";
     aAlfa = aAlfa + (#dsw00107#4 % 704) + ";";  |HAZ GrabaCSV;
     |SI #dsw00107#5 > "01.01.2000";
         aAlfa = (#dsw00107#5 % 102) + "/";
         aAlfa = aAlfa + (#dsw00107#5 % 402) + "/";
         aAlfa = aAlfa + (#dsw00107#5 % 704) + ";";  |HAZ GrabaCSV;
     |SINO;
         aAlfa = ";";  |HAZ GrabaCSV;
     |FINSI;
     aAlfa = #dsw00107#6  + ";";          |HAZ GrabaCSV;
     aAlfa = #dsw00107#7  + ";";          |HAZ GrabaCSV;
     aAlfa = #dsw00107#8  + ";";          |HAZ GrabaCSV;
     aAlfa = #dsw00107#9  + ";";          |HAZ GrabaCSV;
     aAlfa = #dsw00107#10 + ";";          |HAZ GrabaCSV;
     aAlfa = #dsw00107#11 + ";";          |HAZ GrabaCSV;
     aAlfa = &13 + &10;                   |HAZ GrabaCSV;

     nTot1 = nTot1 + #dsw00107#6;
     nTot2 = nTot2 + #dsw00107#7;

     aCtaCble = #dsw00107#0;
|FPRC;

|DEFBUCLE dsw00107CSV;
     #dsw00107#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, dsw00107CSV;
|FIN;

|PROCESO PorFichero;
     |ABRE #dsw00107;
     |HAZ Pendientes;
     |CIERRA #dsw00107;

     |ABRE #dsw00107;
     |LEE 000#dsw00107.p;
     |CIERRA #dsw00107;

     aPathLocal = "C:/documentos";               |RMKDIR aPathLocal;
     aPathLocal = aPathLocal + "/RecibosPdtes";  |RMKDIR aPathLocal;
     aPathLocal = aPathLocal + "/";

     |DBASE aPathServ;
     aPathServ  = aPathServ + "tmp";  |MKDIR aPathServ;
     aPathServ  = aPathServ + "/";

     fFecha = ~;
     |HORA aHora;

     aFicCSV = (fFecha % -104);
     aFicCSV = aFicCSV + (fFecha % 402);
     aFicCSV = aFicCSV + (fFecha % 102);
     aFicCSV = aFicCSV + (aHora % 102);
     aFicCSV = aFicCSV + (aHora % 402);
     aFicCSV = aFicCSV + ".csv";

     aAbre = aPathServ + aFicCSV;

     |XABRE "BW", aAbre, nHandle;

     |BUCLE dsw00107CSV;

     |SI nTot1 ! 0;  |O nTot2 ! 0;
         aAlfa = ";;;;;;;;";              |HAZ GrabaCSV;
         aAlfa = nTot1 + ";";              |HAZ GrabaCSV;
         aAlfa = nTot2 + ";";              |HAZ GrabaCSV;
     |FINSI;

     |XCIERRA nHandle;

     aOrigen = aPathServ + aFicCSV;
     |XABRE "E", aOrigen, 0;
     |SI FSalida < 0;
         |MENAV "0000Selecci¢n vac¡a.";
         |ACABA;
     |FINSI;

     aDestino = aPathLocal + aFicCSV;
     |ENVIA_FICHERO aOrigen, aDestino;

     |FBORRA aOrigen;

     aAlfa = "0000El CSV obtenido se encuentra en " + aPathLocal + aFicCSV;
     |MENAV aAlfa;
|FPRC;

|PROCESO T3; |TIPO 3;
     |HAZ DecimalesBase;

     |HAZ CreaTempo; |NOME_DAT #dsw00107#Tempo#; |DELINDEX #dsw00107;

     |ABRET #agifa024;
     |SI #dsz00135#32 = "C";
         |HAZ PorCrystal;
     |FINSI;

     |SI #dsz00135#32 = "F";
         |HAZ PorFichero;
     |FINSI;
     |CIERRAT #agifa024;

     |HAZ BoraTempo; |DELINDEX #dsw00107;
|FPRC;
