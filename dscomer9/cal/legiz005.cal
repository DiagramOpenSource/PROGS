|FICHEROS;
     legiz005 #0;

     agifa024 #24;
     agifa025 #25;
     agifa010 #10;
     agifa071 #71;

     agifa019 #19;
     agifa060 #60;
     agifa219 #219;

     legim003 #3;

     legim005 #5;

     dsm00003 #7,MANTE;

     legim015 #15;
     legim016 #16;
|FIN;

|VARIABLES;
     aTextoCelda = "";
     nNumMax = 0;
     nLinInci = 0;
     nLinMedi = 0;
     nVoy = 0;

     aAuxMarca = "";
     aLiteralMarca = "";
     aLiteralMarca2 = "";

     nTabla = 0;
     nFila = 0;
     nFilaBase = 0;
     nTotalLineas = 0;

     nHora = 0;
     nMinutos = 0;

     aTiempoSer = "";
     nDiferenciaMin = 0;
     nMinutosIni = 0;
     nMinutosFin = 0;


     aCP = "";
     nSw003YaLeido = 0;            ||Sirve para indicar si tengo que leer o no
                                   ||el legim003, para sacar datos

     aAlfa = "";
     nNV = 0;
     aTipoXML = "";
     aVarXML = "";
     aValorXML = "";

     aUsuario = "";
     aFicheroTxt = "";
     aTxt = "";
     aExe = "";
     nHandle = 0;
     aBase = "";
     aPathCli = "";
     aIPRemoto = "";

     &eaQuitaRaro = "";
     &eaOrigenPLB = "";
     &eaDestinoPLB = "";



     aInforme = "";

     aMensaje = "";
     aParametro = "";


     &eaSerAlbaCer = "";
     &enNumAlbaCer = 0;
|FIN;


|PROCESO agifa071xml;
     |DDEFECTO #agifa019;
     #agifa019#0 = #agifa071#2;
     |LEE 000#agifa019.=;

     aAuxMarca = #agifa019#126;
     |QBF aAuxMarca;
     |SI aAuxMarca = aLiteralMarca; |O aAuxMarca = aLiteralMarca2;
          |ACABA;
     |FINSI;

     |DDEFECTO #agifa060;
     #agifa060#0 = #agifa019#2;
     |LEE 000#agifa060.=;

     |DDEFECTO #agifa219;
     #agifa219#0 = #agifa071#2;
     |LEE 000#agifa219.=;

     ||La fila 1 es el encabezado
     ||Empieza por la fila 2.

     nNV = nNV + 1;
     nFila = nNV + 1;

     |SI nNV = nTotalLineas;
          ||Es la ultima fila.  Solo tengo que poner los datos
     |SINO;
          ||Copio la fila base al final del documento y luego relleno la anterior fila base

          aTipoXML = "T";
          aVarXML = "}" + nTabla + "}" + nFila + "}1}+G";
          aValorXML = "";
          |HAZ MeteTxt5;

          nFilaBase = nFila + 1;

          aTipoXML = "T";
          aVarXML = "}" + nTabla + "}" + nFilaBase + "}1}+P";
          aValorXML = "";
          |HAZ MeteTxt5;
     |FINSI;

     aTipoXML = "T";
     aVarXML = "}" + nTabla + "}" + nFila + "}" + 1;
     aValorXML = #agifa060#1;
     |HAZ MeteTxt5;

     aTipoXML = "T";
     aVarXML = "}" + nTabla + "}" + nFila + "}" + 2;
     aValorXML = #agifa219#1;
     |HAZ MeteTxt5;

     aTipoXML = "T";
     aVarXML = "}" + nTabla + "}" + nFila + "}" + 3;
     aValorXML = #agifa071#4;
     |HAZ MeteTxt5;

     aTipoXML = "T";
     aVarXML = "}" + nTabla + "}" + nFila + "}" + 4;
     aValorXML = #agifa019#110;
     |HAZ MeteTxt5;

     aTipoXML = "T";
     aVarXML = "}" + nTabla + "}" + nFila + "}" + 5;
     aValorXML = #agifa019#114;
     |HAZ MeteTxt5;
|FPRC;

|DEFBUCLE agifa071xml;
 #agifa071#1;
 ;
 #agifa010#52, #agifa010#0, 001;
 #agifa010#52, #agifa010#0, 99;
 ;
 NULL, agifa071xml;
|FIN;

|PROCESO agifa071Cuenta;
     |DDEFECTO #agifa019;
     #agifa019#0 = #agifa071#2;
     |LEE 000#agifa019.=;

     aAuxMarca = #agifa019#126;
     |QBF aAuxMarca;
     |SI aAuxMarca = aLiteralMarca; |O aAuxMarca = aLiteralMarca2;
          |ACABA;
     |FINSI;

     nTotalLineas = nTotalLineas + 1;
|FPRC;

|DEFBUCLE agifa071Cuenta;
 #agifa071#1;
 ;
 #agifa010#52, #agifa010#0, 001;
 #agifa010#52, #agifa010#0, 99;
 ;
 NULL, agifa071Cuenta;
|FIN;


|PROCESO MeteTxt5;
     aTxt = aTipoXML;    |QBF aTxt;
     |XGRABA nHandle, aTxt;

     aTxt = aVarXML;    |QBF aTxt;
     |XGRABA nHandle, aTxt;

     eaQuitaRaro = aValorXML;
     |HAZ QuitaRaros003871;
     aTxt = eaQuitaRaro;    |QBF aTxt;
     |XGRABA nHandle, aTxt;
|FPRC;

|PROCESO legim016Cuenta;
     nLinMedi = nLinMedi + 1;
|FPRC;

|DEFBUCLE legim016Cuenta;
 #legim016#1;
 ;
 #legim003#0, #legim003#1, 001;
 #legim003#0, #legim003#1, 999;
 ;
 NULL, legim016Cuenta;
|FIN;

|PROCESO legim015Cuenta;
     nLinInci = nLinInci + 1;
|FPRC;

|DEFBUCLE legim015Cuenta;
 #legim015#1;
 ;
 #legim003#0, #legim003#1, 001;
 #legim003#0, #legim003#1, 999;
 ;
 NULL, legim015Cuenta;
|FIN;

|PROCESO MeteLineaDoble;
     ||La fila 1 es el encabezado
     ||Empieza por la fila 2.

     nNV = nNV + 1;
     nFila = nNV + 1;

     |SI nNV = nNumMax;
          ||Es la ultima fila.  Solo tengo que poner los datos
     |SINO;
          ||Copio la fila base al final del documento y luego relleno la anterior fila base

          aTipoXML = "T";
          aVarXML = "}" + nTabla + "}" + nFila + "}1}+G";
          aValorXML = "";
          |HAZ MeteTxt5;

          nFilaBase = nFila + 1;

          aTipoXML = "T";
          aVarXML = "}" + nTabla + "}" + nFilaBase + "}1}+P";
          aValorXML = "";
          |HAZ MeteTxt5;
     |FINSI;

     ||ARA.  En la 1 meto la indicencia (15)
     ||Y en la 2, meto la medida correctora (16)

     |DDEFECTO #legim015;
     #legim015#0 = #legim003#0;
     #legim015#1 = #legim003#1;
     #legim015#2 = nVoy;
     |LEE 000#legim015.=;
     |SI FSalida ! 0;
          aTextoCelda = "";
     |SINO;
          aTextoCelda = #legim015#3;
     |FINSI;

     aTipoXML = "T";
     aVarXML = "}" + nTabla + "}" + nFila + "}" + 1;
     aValorXML = aTextoCelda;
     |HAZ MeteTxt5;

     |DDEFECTO #legim016;
     #legim016#0 = #legim003#0;
     #legim016#1 = #legim003#1;
     #legim016#2 = nVoy;
     |LEE 000#legim016.=;
     |SI FSalida ! 0;
          aTextoCelda = "";
     |SINO;
          aTextoCelda = #legim016#3;
     |FINSI;
     aTipoXML = "T";
     aVarXML = "}" + nTabla + "}" + nFila + "}" + 2;
     aValorXML = aTextoCelda;
     |HAZ MeteTxt5;
|FPRC;

|PROCESO CertifAlbaran;
     ||ARA39

     ||Impresion del certificado usando dsxml.exe

     ||Primero averiguo el informe. (aInforme)

     |ABRE #legim005;
     |DDEFECTO #legim005;
     #legim005#0 = #agifa010#52;
     |LEE 000#legim005.=;
     aInforme = #legim005#4;
     |QBF aInforme;
     |CIERRA #legim005;

     |SI aInforme = "";
          aMensaje = "0000Error: No existe formato para los certificados. Serie: [" + #agifa010#52 + "]";
          |MENAV aMensaje;
          |ACABA;
     |FINSI;

     |SI nSw003YaLeido = 0;
          |DDEFECTO #legim003;
          |SELECT #3#legim003;

          #legim003#17 = #agifa010#52;
          #legim003#18 = #agifa010#0;
          |LEE 000#legim003.=;
          |SI FSalida ! 0;
               aMensaje = "0000El albaran [" + #agifa010#52 + "/" + #agifa010#0 + "]" + &13 + "no proviene de un parte";
               |MENAV aMensaje;
               |ACABA;
          |FINSI;
     |FINSI;

     |DDEFECTO #agifa024;
     #agifa024#0 = #agifa010#3;
     |LEE 000#agifa024.=;

     |DDEFECTO #agifa025;
     #agifa025#0 = #legim003#13;
     |LEE 000#agifa025.=;

     |DBASE aBase;
     |QBF aBase;
     eaOrigenPLB = aBase + "word/" + aInforme + ".doc";
     |XABRE "E", eaOrigenPLB, nHandle;
     |SI FSalida < 0;
          aMensaje = "0000No existe el documento word: " + aInforme + ".doc";
          |MENAV aMensaje;
          |ACABA;
     |FINSI;

     aPathCli = "c:\documentos\";
     |RMKDIR aPathCli;
     aPathCli = "c:\documentos\dscomer9\";
     |RMKDIR aPathCli;

     aIPRemoto = "";
     |IP_REMOTO aIPRemoto;

     eaDestinoPLB = aPathCli + aInforme + ".doc";
     |SI aIPRemoto = "";
          |HAZ TraeFicheroPLB;
     |SINO;
          |HAZ RTraeFicheroPLB;
     |FINSI;

     eaOrigenPLB = aBase + "plugins/dsxml.exe";
     eaDestinoPLB = aPathCli + "dsxml.exe";
     |SI aIPRemoto = "";
          |HAZ TraeFicheroPLB;
     |SINO;
          |HAZ RTraeFicheroPLB;
     |FINSI;

     aUsuario = Usuario;
     |QBF aUsuario;
     aFicheroTxt = aPathCli + "certificado" + aUsuario + ".txt";

     |XABRE "CW", aFicheroTxt, nHandle;
     |SI FSalida < 0;
          aMensaje = "0000Problemas al crear fichero " + aFicheroTxt;
          |MENAV aMensaje;
     |FINSI;

     aTxt = "[FICHERO]#" + aPathCli + aInforme + ".doc#" + aPathCli + "1" + aInforme + ".doc";
     |XGRABA nHandle, aTxt;

     aTipoXML = "V"; aVarXML = "#CODCLI#"; aValorXML = #agifa024#0; |HAZ MeteTxt5;
     aTipoXML = "V"; aVarXML = "#NOMBRECLIENTE#";  aValorXML = #agifa010#4; |HAZ MeteTxt5;
     aTipoXML = "V"; aVarXML = "#CIFCLIENTE#";  aValorXML = #agifa024#15; |HAZ MeteTxt5;
     aTipoXML = "V"; aVarXML = "#DIRCLIENTE#";  aValorXML = #agifa024#5; |HAZ MeteTxt5;
     aTipoXML = "V"; aVarXML = "#POBCLIENTE#";  aValorXML = #agifa024#6; |HAZ MeteTxt5;

     aCP = (("00" + #agifa024#178) % -102) + (("000" + #agifa024#179) % -103);
     aTipoXML = "V"; aVarXML = "#CPCLI#"; aValorXML = aCP; |HAZ MeteTxt5;

     aTipoXML = "V"; aVarXML = "#PROVCLI#";  aValorXML = #agifa024#7; |HAZ MeteTxt5;
     aTipoXML = "V"; aVarXML = "#TELEFONOCLI#";  aValorXML = #agifa024#17; |HAZ MeteTxt5;
     aTipoXML = "V"; aVarXML = "#FAXCLI#";  aValorXML = #agifa024#18; |HAZ MeteTxt5;

     aTipoXML = "V"; aVarXML = "#FECHA#"; aValorXML = #legim003#7; |HAZ MeteTxt5;

     aTipoXML = "V"; aVarXML = "#HORAINICIO#";
     aValorXML = (("00" + #legim003#8) % -102) + ":" + (("00" + #legim003#9) % -102) ;
     |HAZ MeteTxt5;
     aTipoXML = "V"; aVarXML = "#HORAFIN#";
     aValorXML = (("00" + #legim003#19) % -102) + ":" + (("00" + #legim003#20) % -102) ;
     |HAZ MeteTxt5;

     nDiferenciaMin = 0;

     nMinutosIni = (#legim003#8 * 60) + #legim003#9;
     nMinutosFin = (#legim003#19 * 60) + #legim003#20;
     |SI nMinutosIni [ nMinutosFin;
          nDiferenciaMin = nMinutosFin - nMinutosIni;
     |SINO;
          nMinutosFin = nMinutosFin + 1440;
          nDiferenciaMin = nMinutosFin - nMinutosIni;
     |FINSI;


     nMinutos = nDiferenciaMin $ 60;
     nDiferenciaMin = nDiferenciaMin - nMinutos;
     nHora = nDiferenciaMin / 60;

     ||ARA
     aTiempoSer = (("00" + nHora) % -102) + ":" + (("00" + nMinutos) % -102);
     aTipoXML = "V"; aVarXML = "#TIEMPOSER#"; aValorXML = aTiempoSer; |HAZ MeteTxt5;

     aTipoXML = "V"; aVarXML = "#APLICADOR#"; aValorXML = #agifa025#1; |HAZ MeteTxt5;
     aTipoXML = "V"; aVarXML = "#NUMEROCARNET#"; aValorXML = #agifa025#9; |HAZ MeteTxt5;

     aTipoXML = "V"; aVarXML = "#SERIEALBARAN#"; aValorXML = #agifa010#52; |HAZ MeteTxt5;
     aTipoXML = "V"; aVarXML = "#NUMEROALBARAN#"; aValorXML = #agifa010#0; |HAZ MeteTxt5;
     aTipoXML = "V"; aVarXML = "#SERIEPARTE#"; aValorXML = #legim003#0; |HAZ MeteTxt5;
     aTipoXML = "V"; aVarXML = "#NUMEROPARTE#"; aValorXML = #legim003#1; |HAZ MeteTxt5;


     aTipoXML = "V"; aVarXML = "#KILOMETROS#"; aValorXML = #legim003#10; |HAZ MeteTxt5;
     aTipoXML = "V"; aVarXML = "#GASTOS#"; aValorXML = #legim003#11; |HAZ MeteTxt5;

     aTipoXML = "V"; aVarXML = "#FECHAALBA#"; aValorXML = #agifa010#1; |HAZ MeteTxt5;

     nTabla = 1;

     nTotalLineas = 0;
     |BUCLE agifa071Cuenta;

     nNV = 0;
     |BUCLE agifa071xml;

     nNV = 0;
     nTabla = 2;
     nNumMax = 0;
     nLinInci = 0;
     nLinMedi = 0;
     |BUCLE legim015Cuenta;
     |BUCLE legim016Cuenta;
     |SI nLinInci > nLinMedi;
          nNumMax = nLinInci;
     |SINO;
          nNumMax = nLinMedi;
     |FINSI;

     |ABRE #legim015;
     |ABRE #legim016;
     |SI nNumMax ! 0;
          |PARA nVoy = 1; |SI nVoy [ nNumMax; |HACIENDO nVoy = nVoy + 1;
               |HAZ MeteLineaDoble;
          |SIGUE;
     |FINSI;
     |CIERRA #legim015;
     |CIERRA #legim016;

     ||aTxt = "[IMPRESORA]#I#1";
     aTxt = "[IMPRESORA]#P";
     |XGRABA nHandle, aTxt;

     |XCIERRA nHandle;

     ||Creo el certificadoUsuario.txt
     ||Y ejecuto el dsxml.exe

     aExe = aPathCli + "dsxml.exe " + aFicheroTxt;
     |RSYSTEM aExe;

     ||RFBORRA aFicheroTxt;
     aAlfa = aPathCli + "certificado" + aUsuario + ".LCK";
     |RFBORRA aAlfa;
|FPRC;

|PROCESO legim003;
     ||ARA39
     ||Leo el albaran , y si entra en el resto de acotaciones.
     ||llamo a la impresion del certificado del albaran en cuestion

     |HAZ CertifAlbaran;
|FPRC;

|DEFBUCLE legim003;
 #legim003#1;
 ;
 #legiz005#8, #legiz005#10;
 #legiz005#9, #legiz005#11;
 ;
 NULL, legim003;
|FIN;

|PROCESO agifa010xml;
     ||ARA39
     ||Miro el resto de acotaciones
     ||Si es todo correcto

     |HAZ CertifAlbaran;
|FPRC;

|DEFBUCLE agifa010xml;
 #agifa010#1;
 ;
 #legiz005#0, #legiz005#2;
 #legiz005#1, #legiz005#3;
 ;
 NULL, agifa010xml;
|FIN;


|PROCESO Principal;
     ||Tengo que distinguir si busco en Albaranes o en los Partes.
     ||Aunque al final siempre tengo que ir a parar a la busqueda en albaranes

     |SI #legiz005#8 = "     "; |Y #legiz005#9 = "zzzzz"; |Y #legiz005#10 = 0; |Y #legiz005#11 = 999999;
          |ABRE #legim003;
          |BUCLE agifa010xml;
          |CIERRA #legim003;
     |SINO;
          |ABRE #agifa010;
          nSw003YaLeido = 1;
          |BUCLE legim003;
          |CIERRA #agifa010;
     |FINSI;
|FPRC;

|PROGRAMA;
     aLiteralMarca = "NO";
     aLiteralMarca2 = "no";

     |ABRE #agifa019;
     |ABRE #agifa219;
     |ABRE #agifa024;
     |ABRE #agifa025;
     |ABRE #agifa060;

     nSw003YaLeido = 0;
     aParametro = PARAMETRO;
     |SI aParametro = "";
          |CLS;
          |DDEFECTO #0;
          |PINPA #0#0;
          |PINDA #0#0;
          |ENDATOS #1#0;
          |SI FSalida = 0;
               |HAZ Principal;
          |FINSI;
     |SINO;
          ||Imprimire directamente el certificado a partir del albaran
          |ABRE #agifa010;
          |DDEFECTO #agifa010;
          #agifa010#52 = eaSerAlbaCer;
          #agifa010#0 = enNumAlbaCer;
          |LEE 000#agifa010.=;
          |SI FSalida = 0;
               |ABRE #legim003;
               |HAZ CertifAlbaran;
               |CIERRA #legim003;
          |FINSI;
     |FINSI;

     |CIERRA #agifa019;
     |CIERRA #agifa219;
     |CIERRA #agifa060;
     |CIERRA #agifa024;
     |CIERRA #agifa025;
|FPRO;
