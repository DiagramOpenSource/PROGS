|FICHEROS;
     agitp003 #0;
     agitp001 #1;

     agifa142 #42;
     agifa007 #5;   ||Series
     agifa019 #7;   ||Articulos
     agifa065 #8;   ||Historico
     agifa112 #112;
     agifa321 #321;
     agifa324 #324;
     dsm00241 #241;
     dsm00006 #906;
     dsm00076 #976;
|FIN;

|VARIABLES;
     &eaArticulo = "";
     nFactor = 0;
     &nDeci_PreDiv   = 0;
     &nDeci_PreBase  = 0;
     nBase = 0;
     nDivisa = 0;
     sw = 0;
     operz     = "";
     c_arti = "";
     c_prov = "";
     fecMayor = @;
     ultprec  = 0;
     ultdto1  = 0;
     ultdto2  = 0;
     ultdto3  = 0;

     swbueno  = 0;
     swarti   = 0;
     swno = 0;
     proveed = "";
     serie   = "";
     alfa1 = "";
     nume1 = 0;
     nAlm = 0;
     aOpe = "";
     aSer = "";
     nDoc = 0;
     nLin = 0;
|FIN;

|PROCESO leearticul;
     swarti = 1;
     #7#0 = #8#0;
     |LEE 001#7=;
     |SI FSalida ! 0;
         |DDEFECTO #7;
         swarti = 0;
     |FINSI;
     c_arti   = #8#0;
     c_prov   = "";
     fecMayor = "01.01.1900";
     ultprec  = 0;
     ultdto1  = 0;
     ultdto2  = 0;
     ultdto3  = 0;
     swbueno  = 0;

     |SI #7#204 = "N";
          nFactor = 1;
     |SINO;
          #241#0 = #7#5;
          #241#2 = #8#20;
          |LEE 000#241=;
          |SI FSalida ! 0; |DDEFECTO #241; |FINSI;
          |SI #241#6 = "D";
              nFactor = 1 * #241#5;
          |SINO;
              nFactor = 1 / #241#5;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO leeserie; || ....... Lee la Serie por si es Interno
     |ABRE #5;
     |DDEFECTO #5;
     #5#26 = serie; ||Serie
     |LEE 001#5=;
     |SI #5#37 = "S";
         |SI operz = "AC"; operz = "AV"; |FINSI;
         |SI operz = "BC"; operz = "BV"; |FINSI;
     |FINSI;
     |CIERRA #5;
|FPRC;

|PROCESO Divisa003;
     #112#0 = c_prov;
     |LEE 000#112=;

     #324#0 = #321#9;
     |LEE 000#324=;
     nDeci_PreBase = #324#9;
     nBase = #324#2;

     #324#0 = #112#81;
     |LEE 000#324=;
     nDeci_PreDiv = #324#9;
     nDivisa = #324#2;
|FPRC;

|PROCESO HistoVertical;
     #976#0 = c_prov;
     #976#1 = c_arti;
     #976#2 = #906#7;
     |LEE 101#976=;
     |SI FSalida  = 0;
          |SI #0#6 = "S";
               #976#3 = #906#8;
               #976#4 = #906#10;
               |GRABA 020#976a;
          |FINSI;
          |LIBERA #976;
     |SINO;
          #976#3 = #906#8;
          #976#4 = #906#10;
          |GRABA 020#976n;
     |FINSI;
|FPRC;

|DEFBUCLE HistoVertical;
     #906#1;
     ;
     c_arti, nAlm, fecMayor, aOpe, aSer, nDoc, nLin, "      ";
     c_arti, nAlm, fecMayor, aOpe, aSer, nDoc, nLin, "zzzzzz";
     ;
     NULL, HistoVertical;
|FIN;

|PROCESO grabalinea;
     |SI swbueno = 0; |ACABA; |FINSI;
     |HAZ Divisa003;

     ultprec = ultprec / nFactor;

     #1#0 = c_prov;
     #1#1 = c_arti;
     |LEE 101#1=;
     |SI FSalida = 0;
         |SI #0#6 = "S";
             #1#3 = ultprec / nBase * nDivisa;
             #1#4 = ultdto1;
             #1#5 = ultdto2;
             #1#10 = ultdto3;
             #1#6 = fecMayor;
             #1#8 = #112#81;
             #1#9 = ultprec;
             |GRABA 021#1a;
         |FINSI;
     |SINO;
         |DDEFECTO #1;
         #1#0 = c_prov;
         #1#1 = c_arti;
         #1#3 = ultprec / nBase * nDivisa;
         #1#4 = ultdto1;
         #1#5 = ultdto2;
         #1#10 = ultdto3;
         #1#6 = fecMayor;
         #1#7 = #7#1;
         #1#8 = #112#81;
         #1#9 = ultprec;
         |GRABA 021#1n;
     |FINSI;
     |BUCLE HistoVertical;
|FPRC;

|PROCESO calbles;
     operz = #8#2;
     serie = #8#11; || Serie
     |HAZ leeserie;
     |SI operz = "AV"; |O operz = "BV"; |O operz = "FC"; |O operz = "DP";
                       |O operz = "DV"; |O operz = "SF"; |O operz = "TI";
         swno = 1;                                        || Es un Cliente
     |FINSI;
|FPRC;

|PROCESO grabar;
     || Seleccion de las Operaciones
     swno = 0;
     |HAZ calbles;
     |SI swno = 1; |ACABA; |FINSI;

     proveed = #8#10;
     |SI proveed < #0#0; |O proveed > #0#1; |ACABA; |FINSI;

     |PINTA 2224, #8#0;      || ...  Pinta Fecha
     |PINTA 2239, #8#10;     || ...  Pinta Proveedor

     |SI sw = 0;
         |HAZ leearticul;
         |SI swarti = 0; |ACABA; |FINSI;   || No existe Articulo
         sw = 1;
         c_prov   = #8#10;
     |FINSI;

     |SI c_arti ! #8#0;
         |HAZ grabalinea;
         |HAZ leearticul;
         |SI swarti = 0; |ACABA; |FINSI;   || No existe Articulo
         c_arti   = #8#0;
     |FINSI;
     |SI c_prov ! #8#10;
         |HAZ grabalinea;
         c_prov   = #8#10;
     |FINSI;

     fecMayor = #8#1;
     ultprec  = #8#9;
     ultdto1  = #8#12;
     ultdto2  = #8#13;
     ultdto3 = #8#21;
     swbueno  = 1;

     nAlm = #8#5;
     aOpe = #8#2;
     aSer = #8#11;
     nDoc = #8#3;
     nLin = #8#4;
|FPRC;

|DEFBUCLE pasa;
     #8#1;
     ;
     #0#2, "01.01.1900", "  " ,        0 ,     0 , "     ";
     #0#3, "31.12.2090", "zz" ,   999999 , 99999 , "zzzzz";
     e;
     NULL, NULL, NULL, NULL, NULL, grabar;
     #8#0,#8#10,#8#1;
|FIN;

|PROCESO cargar; |TIPO 3;
     |SI #0#7 = "NO"; |ACABA; |FINSI;

     |ABRE #42; |LEE 001#42p; |CIERRA #42;

     sw    = 0;

     |D_CUADRO 2109, 2353;
     |PINTA 2210 , " Procesando :                              ";

     |ABRE #976;
     |ABRE #241; |SELECT #2#241;
     |ABRE #1;
     |ABRE #7;
     |ABRE #321; |LEE 000#321p; |CIERRA #321;
     |ABRE #324;
     |ABRE #112;
     |BUCLE pasa;
     |HAZ grabalinea;
     |CIERRA #112;
     |CIERRA #324;
     |CIERRA #7;
     |CIERRA #1;
     |CIERRA #241;
     |CIERRA #976;
|FPRC;

|PROCESO Llena003; |TIPO 6;
     eaArticulo = #0Campo;
     |HAZ LlenaArticulo;
     |SI FSalida = 0;
          #0Campo = eaArticulo;
          |PINTA #0Campo;
     |SINO;
          |ERROR; |ACABA;
     |FINSI;
|FPRC;
