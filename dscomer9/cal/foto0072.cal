|FICHEROS;
     foto0072 #0;

     expor024 #1;        ||Cliente a exportar
     agifa024 #11;

     expor019 #2;        ||Articulo a exportar
     agifa019 #12;

     expor112 #3;        ||Proveedor a importar
     agifa112 #13;

     expor025 #4;        ||Comisionista a importar
     agifa025 #14;

     expor010 #5;        || Albaran a exportar (Cab y lineas)
     agifa010 #15;
     expor071 #6;
     agifa071 #16;

     expor084 #7;        ||Factura contado
     agifa084 #17;
     expor069 #8;        ||Linea factura contado
     agifa069 #18;

     expor501 #9;
     agifa501 #19;       || Cabecera de tiquet
     expor502 #10;
     agifa502 #20;       || Linea de tiquet

     expor513 #21;
     agifa513 #31;       ||Cobros pase

     expor512 #22;          ||Pagos pase
     agifa512 #32;

     expor509 #53;  ||Resumen Parcial pase
     agifa509 #54;  ||Resumen Parcial

     expor001 #55;  ||Cod. Barras
     dsl00001 #56;

     foto0076 #50;       || Fichero CONTROL CAMINO EXPORT/IMPORT

     testigo  #51;  ||testigo de exportacion
     agif0041 #52;  ||Empresas
     expor065 #23;
     agifa065 #33;

     foto0x18 #118;      || Cabecera F
     foto0x19 #128;      || Cabecera R
     foto0015 #138;      || Cabecera Sobres

     foto0z18 #119;      || Lineas F
     foto0z19 #129;      || Lineas R
     foto0016 #139;      || Lineas Sobres
     agift600 #600;
     agifa517 #517; ||Parametros tpv

|FIN;

|VARIABLES;
     &eaNomDef = "";
     &enCamposDef = 0;
     n024 = 0; n019 = 0; n112 = 0; n025 = 0; n010 = 0; n071 = 0;
     n084 = 0; n069 = 0; n501 = 0; n502 = 0; n513 = 0; n512 = 0;
     n509 = 0; n065 = 0; n001 = 0;
     n015 = 0; n016 = 0;
     ddx = "";
     fich = "";
     org = "";
     num = 0;
     bufer = 0;
     err = 0;
     fdes = "";
     des = "";
     fdef = "";
     def = "";
     exp = 0;

     desdec="      ";
     hastac="zzzzzz";
     desdea="                    ";
     hastaa="zzzzzzzzzzzzzzzzzzzz";
     desdep="    ";
     hastap="zzzz";
     desdecom="  ";
     hastacom="zz";
     desdeser="  ";
     hastaser="zz";
     desdealb=0;
     hastaalb=999999;
     desdefa="      ";
     hastafa="zzzzzz";
     desfecha= "00.00.0000";
     hasfecha= "31.12.9999";
     i=0;
     sist="";
     dirfich="";
     mandato="";
     mandato1="";
     mensaje1= "0000ATENCION No puedo abrir fichero de control EXPORT/IMPORT";
     mensaje2= "0000ATENCION Debe informar del CONTROL CAMINO EXPORT/IMPORT";
     path="";
     no="N";
     fecha="00.00.0000";
     copia="";
     p="";
     p1="";
     campos=0;
     cam="";
     si="S";
     blanco="                         ";
     blanco1="                    ";
     tipo = "";
     pintar = "";
     longfich = "";
     longfich1 = 0;
     longfich2 = "";
     empresa = "";
     hora = "";
     horat = "";

     aexpor = "";
     aexpor2 = "";
     asystem = "";
     atgz = "";
     acopias = "";
     errorya = 0;
     &errorenv = 0;

     aborrar = "";
     copiasdemas = 0;
     numcopias = 0;

     acentral = "";

|FIN;

||**********NO SE INCLUYEN ACUMULADOS

|PROCESO CodBarra;
     |PINTA 2163 , blanco1;
     |PINTA 2163 , #56#1;
     |PARA i = 0; |SI i [ n001; |HACIENDO i = i + 1;
          #55i = #56i;
     |SIGUE;
     |GRABA 011#55n;
     #56#4 = "S";
     #56#5 = #0#4;
     |GRABA 011#56a;
     |LIBERA #56;
|FPRC;

|PROCESO miracli;
     |PINTA 2163 , blanco1;
     |PINTA 2163 , #11#0;
     |PARA i = 0; |SI i [ n024; |HACIENDO i = i + 1;
          #1i = #11i;
     |SIGUE;
     |GRABA 011#1n; ||grabo el nuevo
     #11#175 = "S";
     #11#176 = #0#4;
     |GRABA 011#11a;     ||Graba el original
     |LIBERA #11;
|FPRC;

|PROCESO miraarti;
     |PINTA 2163 , blanco1;
     |PINTA 2163 , #12#0;
     |PARA i = 0; |SI i [ n019; |HACIENDO i = i + 1;
          #2i = #12i;
     |SIGUE;
     |GRABA 011#2n;
     #12#123 = "S";
     #12#124 = #0#4;
     |GRABA 011#12a;     ||Graba el original
     |LIBERA #12;
|FPRC;

|PROCESO miraprove;
     |PINTA 2163 , blanco1;
     |PINTA 2163 , #13#0;
     |PARA i = 0; |SI i [ n112; |HACIENDO i = i + 1;
          #3i = #13i;
     |SIGUE;
     |GRABA 011#3n;
     #13#71 = "S";
     #13#72 = #0#4;
     |GRABA 011#13a;     ||Graba el original
     |LIBERA #13;
|FPRC;

|PROCESO miracomis;
     |PINTA 2163 , blanco1;
     |PINTA 2163 , #14#0;
     |PARA i = 0; |SI i [ n025; |HACIENDO i = i + 1;
          #4i = #14i;
     |SIGUE;
     |GRABA 011#4n;
     #14#54 = "S";
     #14#55 = #0#4;
     |GRABA 011#14a;     ||Graba el original
     |LIBERA #14;
|FPRC;

|PROCESO mirahis;
|SI #33#2 = "ES"; |O #33#2 = "SS"; |O #33#2 = "EV"; |O #33#2 = "ME"; |O #33#2 = "MS";
     |PINTA 1763 , blanco1;
     |PINTA 1763 , #33#0;
     |PARA i = 0; |SI i [ n065; |HACIENDO i = i + 1;
          #23i = #33i;
     |SIGUE;
     |GRABA 011#23n;
     |LIBERA #23;
     #33#15 = "S";
     #33#16 = #0#4;
     |GRABA 011#33a;     ||Graba el original
     |LIBERA #33;
|FINSI;
|FPRC;

|PROCESO miralin;        ||Lineas de albaran
||Actualizo los campos
     |PARA i = 0; |SI i [ n071; |HACIENDO i = i + 1;
          #6i = #16i;
     |SIGUE;
     |GRABA 011#6n;      ||Grabo en el fichero destino
|FPRC;

|DEFBUCLE miralin;
     #16#1;
     ;
     #15#52, #15#0, 001;
     #15#52, #15#0, 999;
     ;
     NULL, miralin;
|FIN;

|PROCESO miraalba;       ||albaran
     |PINTA 2163 , blanco1;
     pintar = #15#44 + " / " + #15#0;
     |PINTA 2163 , pintar;

||Actualizo los campos

     |PARA i = 0; |SI i [ n010; |HACIENDO i = i + 1;
          #5i = #15i;
     |SIGUE;
     #5#61 = #15#61;     || *(1)  A cuenta

     |GRABA 011#5n;      ||Grabo en el fichero destino
     #15#58 = "S";
     #15#59 = #0#4;

     |GRABA 011#15a;     ||Grabo en el origen que esta exportado
     |LIBERA #15;
     |BUCLE miralin;     || Por cada una de las lineas del albaran
|FPRC;

|PROCESO miralinfac;        ||Lineas de factura contado
||Actualizo los campos
     |PARA i = 0; |SI i [ n069; |HACIENDO i = i + 1;
          #8i = #18i;
     |SIGUE;
     |GRABA 011#8n;      ||Grabo en el fichero destino
|FPRC;

|DEFBUCLE miralinfac;
     #18#1;
     ;
     #17#48, #17#0, 001;
     #17#48, #17#0, 999;
     ;
     NULL, miralinfac;
|FIN;

|PROCESO mirafac;       ||Facturas contado
     |PINTA 2163 , blanco1;
     pintar = #17#48 + " / " + #17#0;
     |PINTA 2163 , pintar;
||Actualizo los campos
     |PARA i = 0; |SI i [ n084; |HACIENDO i = i + 1;
          #7i = #17i;
     |SIGUE;
     #7#63= #17#63;      || *(1) A cuenta

     |GRABA 011#7n;      ||Grabo en el fichero destino
     #17#61 = "S";
     #17#62 = #0#4;
     |GRABA 011#17a;     ||Grabo en el origen que esta exportado y la fecha
     |LIBERA #17;
     |BUCLE miralinfac;      || Creo todas las lineas de esta factura
|FPRC;

|PROCESO miralintpv;        ||Lineas de tpv
||Actualizo los campos
     || *(1) Actualizo a cuenta
     |PARA i = 0; |SI i [ n502; |HACIENDO i = i + 1;
          #10i = #20i;
     |SIGUE;
     |GRABA 011#10n;      ||Grabo en el fichero destino
|FPRC;

|DEFBUCLE miralintpv;
     #20#1;
     ;
     #19#36, #19#0, 001;
     #19#36, #19#0, 999;
     ;
     NULL, miralintpv;
|FIN;

|PROCESO miratpv;          || Tiquets
     |PINTA 2163 , blanco1;
     pintar = #19#36 + " / " + #19#0;
     |PINTA 2163 , pintar;

||Actualizo los campos
     || *(1) Se incluye a cuenta
     |PARA i = 0; |SI i [ n501; |HACIENDO i = i + 1;
          #9i = #19i;
     |SIGUE;
     |GRABA 011#9n;      ||Grabo en el fichero destino
     #19#44 = "S";
     #19#45 = #0#4;
     |GRABA 011#19a;     ||Grabo en el origen que esta exportado y la fecha
     |LIBERA #19;
     |BUCLE miralintpv;      || Creo todas las lineas de este tiquet
|FPRC;

|PROCESO miracobro;      ||Cobros
     |PINTA 2163 , blanco1;
     pintar = #31#14 + " / " + #31#0;
     |PINTA 2163 , pintar;
     |PARA i = 0; |SI i [ n513; |HACIENDO i = i + 1;
          #21i=#31i;
     |SIGUE;
     |GRABA 011#21n;
     #31#9 = "S";          ||Exportado "S"
     #31#10 = #0#4;        ||Fecha export
     |GRABA 011#31a;
     |LIBERA #31;
|FPRC;

|PROCESO mirapago;       ||pagos
     |PINTA 2163 , blanco1;
     pintar = #32#12 + " / " + #32#0;
     |PINTA 2163 , pintar;
     |PARA i = 0; |SI i [ n512; |HACIENDO i = i + 1;
          #22i = #32i;
     |SIGUE;
     |GRABA 011#22n;
     #32#9="S";          ||Exportado "S"
     #32#10=#0#4;        ||Fecha export
     |GRABA 011#32a;
     |LIBERA #32;
|FPRC;

||davith.--------------------------------------------------------------------
|PROCESO LineasF;
     |PARA i = 0; |SI i [ n016; |HACIENDO i = i + 1;
          #119i = #139i;
     |SIGUE;
     |GRABA 011#119n;
     |LIBERA #119;
|FPRC;

|DEFBUCLE LineasF;
  #139#1;
  ;
  #138#0,1;
  #138#0,999;
  be;
  NULL,LineasF;
|FIN;

|PROCESO LineasR;
     |PARA i = 0; |SI i [ n016; |HACIENDO i = i + 1;
          #129i = #139i;
     |SIGUE;
     |GRABA 011#129n;
     |LIBERA #129;
|FPRC;

|DEFBUCLE LineasR;
  #139#1;
  ;
  #138#0,1;
  #138#0,999;
  be;
  NULL,LineasR;
|FIN;

|PROCESO SobresF;
     |PINTA 2163 , blanco1;
     |PINTA 2163 , #138#0;
     |PARA i = 0; |SI i [ n015; |HACIENDO i = i + 1;
          #118i = #138i;
     |SIGUE;
     |GRABA 011#118n;
     |ABRE #119;
     |BUCLE LineasF;
     |CIERRA #119;
     #138#17 = "S";
     |GRABA 011#138a;     ||Graba el original
     |LIBERA #138;
|FPRC;

|PROCESO SobresR;
     |PINTA 2163 , blanco1;
     |PINTA 2163 , #138#0;
     |PARA i = 0; |SI i [ n015; |HACIENDO i = i + 1;
          #128i = #138i;
     |SIGUE;
     |GRABA 011#128n;
     |ABRE #129;
     |BUCLE LineasR;
     |CIERRA #129;
     #138#17 = "S";
     |GRABA 011#138a;     ||Graba el original
     |LIBERA #138;
|FPRC;
||---------------------------------------------------------------------------

|DEFBUCLE 0;        ||CLientes #1
#11#1;
175;
"      " , no;
"zzzzzz" , no;
be;
NULL,miracli;
|FIN;

|DEFBUCLE 1;        ||Articulos #2
#12#1;
123;
"                    " , no;
"zzzzzzzzzzzzzzzzzzzz" , no;
be;
NULL,miraarti;
|FIN;

|DEFBUCLE 2;        ||Proveedor #3
#13#1;
71;
"    " , no;
"zzzz" , no;
be;
NULL,miraprove;
|FIN;

|DEFBUCLE 3;        ||Comisionista #4
#14#1;
54;
"  " , no;
"zz" , no;
be;
NULL,miracomis;
|FIN;

|DEFBUCLE 33;        ||Historico
#33#1;
15;
"                    ", "01.01.1900", "  ", 000000, 00000, "  ", "N";
"zzzzzzzzzzzzzzzzzzzz", "31.12.9999", "zz", 999999, 99999, "zz", "N";
be;
NULL,mirahis;
|FIN;

|DEFBUCLE 4;        ||albaran#4
#15#1;
 58;
" " ,       0 , no;
"z" ,  999999 , no;
be;
NULL,miraalba;
|FIN;


|DEFBUCLE 5;        ||Facturas contado
#17#1;
 61;
" " ,       0 , no;
"z" ,  999999 , no;
be;
NULL,mirafac;
|FIN;


|| Por este defbucle se pasara dos veces una para cuando los tiquets sean tipo
|| de documento "T" y otra para cuando sean " ";
|DEFBUCLE 6;
  #19#2;
  44;
  tipo , "  ",       0, no;
  tipo , "zz",  999999, no;
  be;
  NULL,miratpv;
|FIN;


|DEFBUCLE 7;        ||Defbucle de cobros
#31#1;
9;
"  " ,       0 , no;
"zz" ,  999999 , no;
be;
NULL,miracobro;
|FIN;

|DEFBUCLE 8;        ||Defbucle de pagos
#32#1;
9;
"  " ,       0 , no;
"zz" ,  999999 , no;
be;
NULL,mirapago;
|FIN;

|DEFBUCLE CodBarra;
     #56#1;
     4;
     0       , no;
     9999999 , no;
     be;
     NULL, CodBarra;
|FIN;

||davith,--------------------------------------------------------------------
|DEFBUCLE SobresF;
  #138#3;
  ;
  #0#24,#0#22,"F",#0#20,"N";
  #0#25,#0#23,"F",#0#21,"N";
  be;
  NULL,SobresF;
|FIN;

|DEFBUCLE SobresR;
  #138#3;
  ;
  #0#31,#0#29,"R",#0#27,"N";
  #0#32,#0#30,"R",#0#28,"N";
  be;
  NULL,SobresR;
|FIN;
||---------------------------------------------------------------------------

==================================================================
|PROCESO inicio;|TIPO 3;

     |HAZ BusCampos;
     |SI err ! 0;
          |MENAV "0000Proceso Abortado...";
          |ACABA;
     |FINSI;

     ||SUB_EJECUTA "agifa551.run";
     |DFICE dirfich; |QBF dirfich;      ||Directorio de la empresa
/*
     |QUE_SISTEMA sist;  |QBF sist;
     |SI sist="ESDOS";
          copia="copy";
          |QBF copia;
          copia=copia+" ";
     |FINSI;
     |SI sist="ESUNIX";|O sist="ESXENIX";
          copia="cp";
          |QBF copia;
          copia=copia+" ";
     |FINSI;
*/

     || Averiguo el path que tengo indicado en el fichero CONTROL CAMINO EXPORT/IMPORT
     |ABRE #50;
     |SI FSalida ! 0;        || Por si hubiera un desastre en el disco duro
         |MENAV mensaje1;
         |ERROR;
         |ACABA;
     |FINSI;

     |LEE 001#50=;
     path=#50#1;
     |QBF path;
     |SI path = "";   || Si no habia nada informado, debo indicar que lo ponga bien
          |MENAV mensaje2;
          |ERROR;
          |ACABA;
     |FINSI;

     |CIERRA #50;

     || Testeo existencia directorio definido en el agifa313 (Con clientes mismo)
     |PATH_DAT #1 path;
     |ABRE #1;
     |SI FSalida ! 0;
        |MENAV "0000Error: No existe el path definido en el directorio de exportacion.";
        |ERROR; |ACABA;
     |SINO;
        |CIERRA #1;
     |FINSI;

     |HAZ controlyaexp;
     |SI errorya = 1;|ERROR;|ACABA;|FINSI;

  |SI errorya = 0; ||No exportada ni enviada
     ||Clientes
     |SI #0#5="S";|O #0#5="s";    || Si he decidido pasar clientes
          |PINTA 2124,blanco;
          |PINTA 2124,"Clientes";

          |PATH_DAT #1 path;   || Informo del camino del fichero de clientes

          |DELINDEX #1;        || Borro los del anterior pase y creo de nuevo
          |INDEXA #1;          || el fichero para añadir informacion

          |ABRE #11;
          |SI FSalida=0;      ||Clientes
               |ABRE #1;
               |BUCLE 0;
               |LIBERA #1;
               |CIERRA #1;

          |FINSI;
          |LIBERA #11;
          |CIERRA #11;
     |FINSI;


     ||Articulo
     |SI #0#6="S";|O #0#6="s";     ||  Si he decidido pasar articulos
          |PINTA 2124,blanco;
          |PINTA 2124,"Articulos";

          |PATH_DAT #2 path;   || Informo del camino del fichero de articulos
          |DELINDEX #2;
          |INDEXA #2;
          |ABRE #12;
          |SI FSalida=0;
               |ABRE #2;
               |BUCLE 1;
               |LIBERA #2;
               |CIERRA #2;
          |FINSI;
          |LIBERA #12;
          |CIERRA #12;
     |FINSI;

     ||Proveedores
     |SI #0#7="S";|O #0#7="s";         || Si he decidido pasar proveedores
          |PINTA 2124,blanco;
          |PINTA 2124,"Proveedores";

          |PATH_DAT #3 path;   || Informo del camino del fichero de proveedores
          |DELINDEX #3;
          |INDEXA #3;
          |ABRE #13;
          |SI FSalida=0;
               |ABRE #3;
               |BUCLE 2;
               |LIBERA #3;
               |CIERRA #3;
          |FINSI;
          |LIBERA #13;
          |CIERRA #13;
     |FINSI;


     ||Comisionistas
     |SI #0#8="S";|O #0#8="s";      || Si he decidido pasar comisionistas
          |PINTA 2124,blanco;
          |PINTA 2124,"Comisionistas";

          |PATH_DAT #4 path;   || Informo del camino del fichero de comisionistas
          |DELINDEX #4;
          |INDEXA #4;
          |ABRE #14;
          |SI FSalida=0;
               |ABRE #4;
               |BUCLE 3;
               |LIBERA #4;
               |CIERRA #4;
          |FINSI;
          |LIBERA #14;
          |CIERRA #14;
     |FINSI;

     ||Historico
     |SI #0#17="S";|O #0#17="s"      || Si he decidido pasar Historico
          |PINTA 2124,blanco;
          |PINTA 2124,"Historico    ";

          |PATH_DAT #23 path;   || Informo del camino del fichero de historico
          |ABRE #23; |CIERRA #23; |DELINDEX #23;
               |ABRE #23;
               |BUCLE 33;
               |LIBERA #23;
               |CIERRA #23;
     |FINSI;

     ||Albaranes
     |SI #0#9="S";|O #0#9="s";        || Si he decidido pasar los albaranes
          |PINTA 2124,blanco;
          |PINTA 2124,"Albaranes";

          |PATH_DAT #5 path;   || Informo del camino del fichero de albaranes
          |PATH_DAT #6 path;   || Informo del camino del fichero de lineas albaranes
          |DELINDEX #5;
          |DELINDEX #6;
          |INDEXA #6;
          |INDEXA #5;
          |ABRE #16;
          |ABRE #15;
          |SI FSalida=0;
               |ABRE #5;
               |ABRE #6;
               |BUCLE 4;
               |LIBERA #6;
               |CIERRA #6;
               |LIBERA #5;
               |CIERRA #5;
          |FINSI;
          |LIBERA #16;
          |CIERRA #16;
          |LIBERA #15;
          |CIERRA #15;
     |FINSI;


     ||Facturas de Contado
     |SI #0#10="S";|O #0#10="s";
          |PINTA 2124,blanco;
          |PINTA 2124,"Facturas Contado";

          |PATH_DAT #7 path;   || Informo del camino del fichero de facturas contado
          |PATH_DAT #8 path;   || Informo del camino del fichero de LINEAS
          |DELINDEX #7;
          |DELINDEX #8;
          |INDEXA #7;
          |INDEXA #8;
          |ABRE #17;
          |ABRE #18;
          |SI FSalida=0;
               |ABRE #7;
               |ABRE #8;
               |BUCLE 5;
               |LIBERA #8;
               |CIERRA #8;
               |LIBERA #7;
               |CIERRA #7;
          |FINSI;
          |LIBERA #18;
          |CIERRA #18;
          |LIBERA #17;
          |CIERRA #17;
     |FINSI;


     || Tiquets
     |SI #0#11="S";|O #0#11="s";       || Si he decidido pasar tiquets
          |PINTA 2124,blanco;
          |PINTA 2124,"Tiquets          ";

          |PATH_DAT #9  path;   || Informo del camino del fichero tpv cabeceras
          |PATH_DAT #10 path;   || Informo del camino del fichero de LINEAS
          |DELINDEX #9;
          |DELINDEX #10;
          |INDEXA #9;
          |INDEXA #10;
          |ABRE #19;          || Tiquet (cab.)
          |ABRE #20;          || tiquet (lin.)
          |SI FSalida=0;
               |ABRE #9;      || Abro tiquet cab. pase
               |ABRE #10;     || Abro tiquet lin. pase
               tipo= "T";     || Tipo de documento (tiquet)
               |BUCLE 6;      || Paso por el bucle con tipo de documento "T"
               |LIBERA #10;
               tipo =" ";     || Tipo de documento (blanco)
               |BUCLE 6;      || Paso por el bucle con tipo de documento "T"
               |LIBERA #10;

               |CIERRA #10;
               |LIBERA #9;
               |CIERRA #9;


          |FINSI;
          |LIBERA #20;
          |CIERRA #20;
          |LIBERA #19;
          |CIERRA #19;
     |FINSI;

     || Cobros
     |SI #0#12="S";|O #0#12="s";       || Si he decidido pasar Cobros
          |PINTA 2124,blanco;
          |PINTA 2124,"Cobros           ";

          |PATH_DAT #21  path;   || Informo del camino del fichero tpv cabeceras
          |DELINDEX #21;
          |INDEXA #21;
          |ABRE #31;          || Tiquet (cab.)
          |SI FSalida=0;
               |ABRE #21;      || AbrO COBRO pase
               |BUCLE 7;
               |LIBERA #21;
               |CIERRA #21;
          |FINSI;
          |LIBERA #31;
          |CIERRA #31;
     |FINSI;

     || Pagos
     |SI #0#13="S";|O #0#13="s";       || Si he decidido pasar pAGOS
          |PINTA 2124,blanco;
          |PINTA 2124,"Pagos            ";

          |PATH_DAT #22  path;   || Informo del camino de pagos
          |DELINDEX #22;
          |INDEXA #22;
          |ABRE #32;          || Pagos original
          |SI FSalida=0;
               |ABRE #22;      || AbrO pago pase
               |BUCLE 8;
               |LIBERA #22;
               |CIERRA #22;
          |FINSI;
          |LIBERA #32;
          |CIERRA #32;
     |FINSI;

     ||Cod.Barras
     |SI #0#18="S";|O #0#18="s";
          |PINTA 2124,blanco;
          |PINTA 2124,"Cod.Barras";
          |PATH_DAT #55 path;
          |DELINDEX #55;
          |ABRE #55;
          |BUCLE CodBarra;
          |CIERRA #55;
     |FINSI;

     |PATH_DAT #51 path;
     |ABRE #51;
     |CIERRA #51;
     |DELINDEX #51;
     |ABRE #51;
     |DDEFECTO #51;
     |QBF dirfich;
     longfich = dirfich % 0;
     longfich1 = longfich;
     longfich1 = longfich1 - 2;
     longfich2 = longfich1 + "02"; ||Averiguo el numero de la empresa
     empresa = dirfich % longfich2;
     #51#0 = empresa;
     longfich1 = longfich;
     longfich1 =longfich1 - 3;
     longfich2 = "01" + longfich1;
     longfich2 = dirfich % longfich2;
     |PATH_DAT #52 longfich2;
     |ABRE #52;
     #52#0 = #51#0;
     |LEE 001#52=;
     |CIERRA #52;
     #51#1 = #52#1;      ||Nombre de la empresa
     |HORA hora;
     horat = hora % 102 + hora % 402;
     #51#3 = horat;
     #51#4 = "N";
     #51#5 = "00.00.0000";
     |GRABA 021#51n;
     |LIBERA #51;
     |CIERRA #51;

     |HAZ resumen1;

     ||SobresF. davith,------------------------------------------------------
     |SI #0#19 = "S";|O #0#19 = "s";
       |PINTA 2124, blanco;
       |PINTA 2124, "Sob. fact.";
       |PATH_DAT #118 path;|PATH_DAT #119 path;
       |ABRE #118;|CIERRA #118;|DELINDEX #118; ||Cabecera F
       |ABRE #119;|CIERRA #119;|DELINDEX #119; ||Lineas F
       |ABRE #118;
       |BUCLE SobresF;
       |LIBERA #118;
       |CIERRA #118;
     |FINSI;

     ||SobresR
     |SI #0#26 = "S";|O #0#26 = "s";
       |PINTA 2124, blanco;
       |PINTA 2124, "Sob. recog.";
       |PATH_DAT #128 path;|PATH_DAT #129 path;
       |ABRE #128;|CIERRA #128;|DELINDEX #128; ||Cabecera R
       |ABRE #129;|CIERRA #129;|DELINDEX #129; ||Lineas R
       |ABRE #128;
       |BUCLE SobresR;
       |LIBERA #128;
       |CIERRA #128;
     |FINSI;
     ||----------------------------------------------------------------------

        |HAZ compresion;

  |FINSI; ||Error ya exportado y enviado

     |SI errorya = 2;
         |CONFI "0000SEnviar Exportacion pendiente de envio ahora: ";
     |SINO;
         |CONFI "0000SEnviar Exportacion ahora: ";
     |FINSI;
     |SI FSalida = 0;
         |MENAV "0000Atencion!! Debe estar conectado a internet";
         |SUB_EJECUTA "foto0102.tab";
         |SI errorenv = 0; ||Envio correcto
             |ABRE #600;
             #600#0 = "ex" + #0#4 + ".tgz";
             |LEE 001#600=;
             |SI FSalida = 0;
                 #600#2 = "S"; ||Enviado
                 #600#3 = #0#4; ||F. Envio
                 |GRABA 001#600a;
             |FINSI;
             |CIERRA #600;
         |FINSI;
     |FINSI;

|FPRC;

|PROCESO pasares;
     |DDEFECTO #53;
     |PARA i = 0; |SI i [ n509; |HACIENDO i = i + 1;
          #53i = #54i;
     |SIGUE;
     |GRABA 021#53n;
     |LIBERA #53;
     #54#60 = "S";
     #54#61 = #0#4;
     |GRABA 021#54a;
     |LIBERA #54;
|FPRC;

|DEFBUCLE resum;
#54#2;
56;  ||No paso las que la caja este abierta
0  ,  0 , "00.00.0000" , " " , "          " , " ";
99 , 99 , "31.12.9999" , "N" , "31.12.9999" , "N";     ||Las fechas se desactualizan
be;
NULL ,pasares;
|FIN;

|PROCESO resumen1;
|PATH_DAT #53 path;
|ABRE #53;
|CIERRA #53;
|DELINDEX #53;
|ABRE #53;
|BUCLE resum;
|CIERRA #53;
|FPRC;
=======================================================================
|PROCESO LeeDDX;
     fdef = def + fich; fdes = org + fich;
     |LEEDEDEF fdef, fdes;
     |SI exp = 1; |Y FSalida = -1;
          fich = fdes + ".dat"; |FBORRA fich;
          fich = fdes + ".ixx"; |FBORRA fich;
          fich = fdes + ".ddx"; |FBORRA fich;
          |LEEDEDEF fdef, fdes;
          |MENAV "0000Fichero exportacion reparado...";
     |FINSI;
     eaNomDef = fich;
     |HAZ CamposDef;
     num = enCamposDef;
|FPRC;

|PROCESO BusCampos;
     |DFICE org; |QBF org;
     |DBASE des; |QBF des;
     |DDEF  def; |QBF def;
     err = 0;
     exp = 0; fich = "agifa024"; |HAZ LeeDDX; n024 = num;
     exp = 1; fich = "expor024"; |HAZ LeeDDX;
     |SI n024 ! num;
          |MENAV "0000Fichero enlace Clientes incorrecto..."; err = 1;
     |FINSI;

     exp = 0; fich = "agifa019"; |HAZ LeeDDX; n019 = num;
     exp = 1; fich = "expor019"; |HAZ LeeDDX;
     |SI n019 ! num;
          |MENAV "0000Fichero enlace Articulos incorrecto..."; err = 1;
     |FINSI;

     exp = 0; fich = "agifa112"; |HAZ LeeDDX; n112 = num;
     exp = 1; fich = "expor112"; |HAZ LeeDDX;
     |SI n112 ! num;
          |MENAV "0000Fichero enlace Proveedores incorrecto..."; err = 1;
     |FINSI;

     exp = 0; fich = "agifa025"; |HAZ LeeDDX; n025 = num;
     exp = 1; fich = "expor025"; |HAZ LeeDDX;
     |SI n025 ! num;
          |MENAV "0000Fichero enlace Comisionistas incorrecto..."; err = 1;
     |FINSI;

     exp = 0; fich = "agifa010"; |HAZ LeeDDX; n010 = num;
     exp = 1; fich = "expor010"; |HAZ LeeDDX;
     |SI n010 ! num;
          |MENAV "0000Fichero enlace Albaran (C) incorrecto..."; err = 1;
     |FINSI;

     exp = 0; fich = "agifa071"; |HAZ LeeDDX; n071 = num;
     exp = 1; fich = "expor071"; |HAZ LeeDDX;
     |SI n071 ! num;
          |MENAV "0000Fichero enlace Albaran (L) incorrecto..."; err = 1;
     |FINSI;

     exp = 0; fich = "agifa084"; |HAZ LeeDDX; n084 = num;
     exp = 1; fich = "expor084"; |HAZ LeeDDX;
     |SI n084 ! num;
          |MENAV "0000Fichero enlace Facturas (C) incorrecto..."; err = 1;
     |FINSI;

     exp = 0; fich = "agifa069"; |HAZ LeeDDX; n069 = num;
     exp = 1; fich = "expor069"; |HAZ LeeDDX;
     |SI n069 ! num;
          |MENAV "0000Fichero enlace Facturas (L) incorrecto..."; err = 1;
     |FINSI;

     exp = 0; fich = "agifa501"; |HAZ LeeDDX; n501 = num;
     exp = 1; fich = "expor501"; |HAZ LeeDDX;
     |SI n501 ! num;
          |MENAV "0000Fichero enlace TPV (C) incorrecto..."; err = 1;
     |FINSI;

     exp = 0; fich = "agifa502"; |HAZ LeeDDX; n502 = num;
     exp = 1; fich = "expor502"; |HAZ LeeDDX;
     |SI n502 ! num;
          |MENAV "0000Fichero enlace TPV (L) incorrecto..."; err = 1;
     |FINSI;

     exp = 0; fich = "agifa513"; |HAZ LeeDDX; n513 = num;
     exp = 1; fich = "expor513"; |HAZ LeeDDX;
     |SI n513 ! num;
          |MENAV "0000Fichero enlace Cobros incorrecto..."; err = 1;
     |FINSI;

     exp = 0; fich = "agifa512"; |HAZ LeeDDX; n512 = num;
     exp = 1; fich = "expor512"; |HAZ LeeDDX;
     |SI n512 ! num;
          |MENAV "0000Fichero enlace Pagos incorrecto..."; err = 1;
     |FINSI;

     exp = 0; fich = "agifa509"; |HAZ LeeDDX; n509 = num;
     exp = 1; fich = "expor509"; |HAZ LeeDDX;
     |SI n509 ! num;
          |MENAV "0000Fichero enlace Resumen incorrecto..."; err = 1;
     |FINSI;

     exp = 0; fich = "agifa065"; |HAZ LeeDDX; n065 = num;
     exp = 1; fich = "expor065"; |HAZ LeeDDX;
     |SI n065 ! num;
          |MENAV "0000Fichero enlace Historico incorrecto..."; err = 1;
     |FINSI;

     exp = 0; fich = "dsl00001"; |HAZ LeeDDX; n001 = num;
     exp = 1; fich = "expor001"; |HAZ LeeDDX;
     |SI n001 ! num;
          |MENAV "0000Fichero enlace Cod.Barras incorrecto..."; err = 1;
     |FINSI;

     ||davith. Comprueba la coincidencia de los campos-----------------------
     exp = 0; fich = "foto0015"; |HAZ LeeDDX; n015 = num;
     exp = 1; fich = "foto0x18"; |HAZ LeeDDX;
     |SI n015 ! num;
          |MENAV "0000Fichero enlace Cab. Sobres incorrecto..."; err = 1;
     |FINSI;

     exp = 1; fich = "foto0x19"; |HAZ LeeDDX;
     |SI n015 ! num;
          |MENAV "0000Fichero enlace Cab. Sobres incorrecto..."; err = 1;
     |FINSI;

     exp = 0; fich = "foto0016"; |HAZ LeeDDX; n016 = num;
     exp = 1; fich = "foto0z18"; |HAZ LeeDDX;
     |SI n016 ! num;
          |MENAV "0000Fichero enlace Lin. Sobres incorrecto..."; err = 1;
     |FINSI;
     exp = 1; fich = "foto0z19"; |HAZ LeeDDX;
     |SI n016 ! num;
          |MENAV "0000Fichero enlace Lin. Sobres incorrecto..."; err = 1;
     |FINSI;
   ||------------------------------------------------------------------------
|FPRC;

|PROCESO DefectosF; ||davith.
|SI #0#19 = "N"; |O #0#19 = "n";
  |C_M #0#20, 1 , "N";
  |C_M #0#21, 1 , "N";
  |C_M #0#22, 1 , "N";
  |C_M #0#23, 1 , "N";
  |C_M #0#24, 1 , "N";
  |C_M #0#25, 1 , "N";
|SINO;
  |C_M #0#20, 1 , "S";
  |C_M #0#21, 1 , "S";
  |C_M #0#22, 1 , "S";
  |C_M #0#23, 1 , "S";
  |C_M #0#24, 1 , "S";
  |C_M #0#25, 1 , "S";
|FINSI;
|FPRC;

|PROCESO DefectosR; ||davith.
|SI #0#26 = "N"; |O #0#26 = "n";
  |C_M #0#27, 1 , "N";
  |C_M #0#28, 1 , "N";
  |C_M #0#29, 1 , "N";
  |C_M #0#30, 1 , "N";
  |C_M #0#31, 1 , "N";
  |C_M #0#32, 1 , "N";
|SINO;
  |C_M #0#27, 1 , "S";
  |C_M #0#28, 1 , "S";
  |C_M #0#29, 1 , "S";
  |C_M #0#30, 1 , "S";
  |C_M #0#31, 1 , "S";
  |C_M #0#32, 1 , "S";
|FINSI;
|FPRC;

|PROCESO compresion;
     |MENSA "0000Comprimiendo los datos...";
     aexpor = path + "expor";
     |QBF aexpor;
     aexpor = aexpor + #52#0;
     |QBF aexpor;
     atgz = aexpor + ".tgz";

     aexpor = aexpor + ".tar";
     |QBF aexpor;
     aexpor2 = aexpor + " *.*";

     acentral = path + "exporce.tgz";
     |FBORRA acentral; ||Por si acaso ha quedado colgado uno de impor central

     |FBORRA atgz; ||Borro el anterior tgz
     |ATAR aexpor2,path;
     |COMPRIME aexpor;
     |SI FSalida = 0;
        |QUE_SISTEMA asystem;
        acopias = path + "copiasex";
        |MKDIR acopias;
        acopias = acopias + "/ex" + #0#4 + ".tgz";
        |FBORRA acopias; ||Borro si hay copia ant misma fecha

        |COPIA_FICHERO atgz,acopias;
        |SI asystem = "ESBSD";
            asystem = "chmod 777 " + path + "copiasex";
            |SYSTEM asystem;
            asystem = "chmod 777 " + atgz;
            |SYSTEM asystem;
            asystem = "chmod 777 " + acopias;
            |SYSTEM asystem;
        |FINSI;
        |FBORRA aexpor;  ||Borro el tar
        aexpor = path + "testigo.dat";
        |FBORRA aexpor;
        aexpor = path + "testigo.ddx";
        |FBORRA aexpor;
        aexpor = path + "testigo.ixx";
        |FBORRA aexpor;
        |PARA i=1;|SI i < 100;|HACIENDO i = i +1;
            aexpor = path + "expor00" + i;
            aexpor2 = aexpor + ".dat";
            |FBORRA aexpor2;
            aexpor2 = aexpor + ".ixx";
            |FBORRA aexpor2;
            aexpor2 = aexpor + ".ddx";
            |FBORRA aexpor2;
        |SIGUE;
        |PARA i=10;|SI i < 100;|HACIENDO i = i +1;
            aexpor = path + "expor0" + i;
            aexpor2 = aexpor + ".dat";
            |FBORRA aexpor2;
            aexpor2 = aexpor + ".ixx";
            |FBORRA aexpor2;
            aexpor2 = aexpor + ".ddx";
            |FBORRA aexpor2;
        |SIGUE;
        |PARA i=100;|SI i < 1000;|HACIENDO i = i +1;
            aexpor = path + "expor" + i;
            aexpor2 = aexpor + ".dat";
            |FBORRA aexpor2;
            aexpor2 = aexpor + ".ixx";
            |FBORRA aexpor2;
            aexpor2 = aexpor + ".ddx";
            |FBORRA aexpor2;
        |SIGUE;
        aexpor= path + "foto0x18";
        aexpor2 = aexpor + ".dat";
        |FBORRA aexpor2;
        aexpor2 = aexpor + ".ixx";
       |FBORRA aexpor2;
        aexpor2 = aexpor + ".ddx";
        |FBORRA aexpor2;
        aexpor= path + "foto0z18";
        aexpor2 = aexpor + ".dat";
        |FBORRA aexpor2;
        aexpor2 = aexpor + ".ixx";
        |FBORRA aexpor2;
        aexpor2 = aexpor + ".ddx";
        |FBORRA aexpor2;
        aexpor= path + "foto0x19";
        aexpor2 = aexpor + ".dat";
        |FBORRA aexpor2;
        aexpor2 = aexpor + ".ixx";
        |FBORRA aexpor2;
        aexpor2 = aexpor + ".ddx";
        |FBORRA aexpor2;
        aexpor= path + "foto0z19";
        aexpor2 = aexpor + ".dat";
        |FBORRA aexpor2;
        aexpor2 = aexpor + ".ixx";
        |FBORRA aexpor2;
        aexpor2 = aexpor + ".ddx";
        |FBORRA aexpor2;
        |HAZ controlexp;
     |SINO;
        |MENAV "0000Error comprimiendo los datos";
     |FINSI;
|FPRC;

|PROCESO controlyaexp;
     errorya = 0;
     |ABRE #600;
     #600#0 = "ex" + #0#4 + ".tgz";
     |LEE 001#600=;
     |SI FSalida = 0;|Y #600#2 = "N";
         |MENAV "0000Atencion: Ya ha realizado una exportacion hoy y no esta enviada";
         |MENAV "0000Envie primero la pendiente y asegurese de que la central la importe";
         errorya = 2;
     |FINSI;
     |SI FSalida = 0;|Y #600#2 = "S";
         |MENAV "0000Atencion: Ya ha enviado una exportacion hoy";
         |CONFI "0000NExportar de todos modos ";
         |SI FSalida ! 0;errorya = 1;|FINSI;
     |FINSI;
     |CIERRA #600;
|FPRC;

|PROCESO bc;
   |SI copiasdemas > 0;|Y #600#5 = "S";
      aborrar = path + "copiasex/" + #600#0;
      |QBF aborrar;
      |FBORRA aborrar; ||Borro ficheros copias
      |SI FSalida = 0;
        #600#5 = "N"; ||hay copia
        |GRABA 001#600a;
      |FINSI;
      copiasdemas = copiasdemas - 1;
   |FINSI;
|FPRC;

|DEFBUCLE borracopias;
#600#2;
;
INICIO;
FINAL;
e;
NULL,bc;
|FIN;

|PROCESO ast;
   |SI #600#5 = "S";
      numcopias = numcopias + 1;
   |FINSI;
   #600#4 = " ";
   |GRABA 001#600a;
|FPRC;

|DEFBUCLE limpiaast;
#600#1;
;
INICIO;
FINAL;
e;
NULL,ast;
|FIN;

|PROCESO controlexp;
     numcopias = 0;
     |BUCLE limpiaast;
     |ABRE #600;
     #600#0 = "ex" + #0#4 + ".tgz";
     |LEE 001#600=;
     |SI FSalida = 0;
         #600#2 = "N";
         #600#3 = "00.00.0000";
         #600#4 = "*";
         #600#5 = "S";
         |GRABA 001#600a;
     |SINO;
         #600#1 = #0#4;
         #600#2 = "N";
         #600#3 = "00.00.0000";
         #600#4 = "*";
         #600#5 = "S";
         |GRABA 001#600n;
         numcopias = numcopias + 1;
     |FINSI;
     |CIERRA #600;
     |ABRE #517;
     |LEE 001#517p;
     |SI FSalida = 0;
        copiasdemas = numcopias - #517#23;
        |SI copiasdemas > 0;
           |BUCLE borracopias; ||Borra las copias de mas
        |FINSI;
     |FINSI;
     |CIERRA #517;
|FPRC;
