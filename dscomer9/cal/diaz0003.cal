|FICHEROS;
     diaz0003 #0;
     diam0003 #3;
     diam0005 #5;
     diam0006 #6;
     agifa024 #24;
     diaw0005 #50,MANTE;
     agifa104 #104;

     agifa104@ #1004;
     dsparm01@ #100;
     dstelw01@ #111;
     dstelm02@ #200;
     dsparm05@ #500;
     dstelm03@ #300;
|FIN;

|VARIABLES;
     &eaSer    = "";
     &enNum    = 0;
     &enTec1   = 0;
     &enTec2   = 0;
     aDat      = "";
     aDef5     = "";
     aDef2     = "";
     aDef3     = "";
     aDef1     = "";
     aDef10    = "";
     aBase     = "";
     nu1       = 0;
     nu2       = 0;
     i         = 0;
     alfa      = "";
     alf1      = "";
     nTiquet   = 0;
     nLin      = 0;
     nErr      = 0;
     nHay      = 0;
     aAlfa     = "";
     aHora     = "";
     aDirOri   = "";
     aDirDes   = "";
     aTitulo   = "";
     aTexto    = "";
     aFic      = "";
     aDSCorreo = "";
     aProxy    = "";

     clave = "";
     nume1 = 0;
     camuf = 0;
     camufz = "";
     para1 = 0;
     para2 = 0;
     alfa1 = "";
     alfa2 = "";
|FIN;
컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
|PROCESO LaClave;
     |BLIN 24;
     |PINTA 2418, "Entre la clave...:";

     clave = "";
     E_sup = 10;
     E_inf = "";
     clave = 2439 ? -1;
     |BLIN 24;
     |HAZ Encripta;
     |SI camufz ! #500#5;
          |MENAV "0000Clave incorrecta...";
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO Desencripta;
     camufz = "";
     |PARA para1 = 1; |SI para1 [ 20; |HACIENDO para1 = para1 + 2;
          nume1 = (para1 * 100) + 1;
          alfa1 = clave % nume1;
          nume1 = ((para1 + 1) * 100) + 1;
          alfa2 = clave % nume1;

          nume1 = &alfa2;
          nume1 = nume1 - 65;

          camuf = &alfa1;
          camuf = camuf - 65;
          camuf = ((camuf * 26) + nume1) / 2;

          camufz = camufz + &camuf;
     |SIGUE;
|FPRC;

|PROCESO Encripta;
     camufz = "";
     |PARA para1 = 1; |SI para1 [ 10; |HACIENDO para1 = para1 + 1;
          nume1 = (para1 * 100) + 1;
          alfa1 = clave % nume1;

          camuf = &alfa1;
          camuf = camuf * 2;

          nume1 = ((camuf - (camuf $ 26)) / 26 ) + 65;
          camufz = camufz + &nume1;

          nume1 = (camuf $ 26) + 65;
          camufz = camufz + &nume1;
     |SIGUE;
|FPRC;
컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
|PROCESO Correo;
     |DDEFECTO #500;
     #500#0 = #0#0;
     |LEE 020#500=;
     aDirOri = #500#4; |QBF aDirOri;
     |SI aDirOri = "";
          aDirOri = #500#3;
     |SINO;
         aDirOri = aDirOri + ";" + #500#3;
     |FINSI;
     |QBF aDirOri;

     |DDEFECTO #500;
     #500#0 = #5#3
     |LEE 020#500=;
     aDirDes = #500#4; |QBF aDirDes;
     |SI aDirDes = "";
          aDirDes = #500#3;
     |SINO;
          aDirDes = aDirDes + ";" + #500#3; |QBF aDirDes;
     |FINSI;
     |QBF aDirDes;

     aDSCorreo = #100#8; |QBF aDSCorreo;
     aProxy    = #100#7; |QBF aProxy;

     aTitulo = "Tiquet nro. ";
     aAlfa = ("00000" + #300#0) % -105; aTitulo = aTitulo + aAlfa;
     aAlfa = ("00000" + #300#1) % -104; aTitulo = aTitulo + "." + aAlfa;
     aTitulo = aTitulo + " Cliente: " + #104#4;
     aTexto = aTitulo;
     aFic = "vacio";

     aAlfa = "2405Enviando [" + aDSCorreo + "] ..."; |MENSA aAlfa;
     |DSCORREO_ENVIA aDSCorreo, aProxy, aDirOri, aDirDes, aTitulo, aTexto, aFic;
     |BLIN 24;
     |SI FSalida < 0;
          |MENAV "    Problemas al enviar correo. Revisar conexion.";
     |SINO;
          |MENAV "    Envio correo completado.";
     |FINSI;
|FPRC;

|PROCESO CargaDef;
     |SI nErr = 1; |ACABA; |FINSI;

     |CARGA_DEF aDef1, dstelw01@;
     |SI FSalida ! 0;
          aAlfa = "0000Error al cargar " + aDef1;
          |MENAV aAlfa; nErr = 1;
     |FINSI;

     |CARGA_DEF aDef2, dstelm02@;
     |SI FSalida ! 0;
          aAlfa = "0000Error al cargar " + aDef2;
          |MENAV aAlfa; nErr = 1;
     |FINSI;

     |CARGA_DEF aDef3, dstelm03@;
     |SI FSalida ! 0;
          aAlfa = "0000Error al cargar " + aDef3;
          |MENAV aAlfa; nErr = 1;
     |FINSI;

     |CARGA_DEF aDef10, dsparm01@;
     |SI FSalida ! 0;
          aAlfa = "0000Error al cargar " + aDef10;
          |MENAV aAlfa; nErr = 1;
     |FINSI;
     |SI nErr = 0;
          |PATH_DAT #100 aDat;
          |PATH_DAT #200 aDat;
          |PATH_DAT #300 aDat;
          |PATH_DAT #111 aDat;

          |ABRE #100; |LEE 020#100p; |CIERRA #100;
     |FINSI;
|FPRC;

|PROCESO DescargaDef;
     |DESCARGA_DEF #dstelw01@;
     |DESCARGA_DEF #dstelm02@;
     |DESCARGA_DEF #dstelm03@;
     |DESCARGA_DEF #dsparm01@;
|FPRC;

|PROCESO diam0006;
     nLin = nLin + 1;
     |SI nLin = 14; nLin = 19; |FINSI;
     #200nLin = #6#5;

     #111#5 = #104#4;
     #111#0 = nTiquet;
     #111#1 = "M";
     #111#4 = 1;
     #111#2 =  #6#4-1;

     #111#3 = #6#5;
     |GRABA 020#111n;
|FPRC;

|DEFBUCLE diam0006;
     #6#1;
     ;
     #5#0, #5#1, #5#2, #5#3, 1;
     #5#0, #5#1, #5#2, #5#3, 10;
     ;
     NULL, diam0006;
|FIN;

|PROCESO CreaTiquet;
     |HAZ CargaDef;
     |SI nErr = 1; |ACABA; |FINSI;

     |ABRE #104;
     #104#25 = #5#0;
     #104#0 = #5#1;
     |LEE 020#104=;
     |CIERRA #104;

     |ABRE #24;
     #24#0 = #104#4;
     |LEE 000#24=;
     |CIERRA #24;

     |ABRE #200;
     #200#5 = #104#4;
     #200#0 = 99999;
     |LEE 000#200];
     |SI FSalida = 0;
          |LEE 000#200a;
     |SINO;
          |LEE 000#200u;
     |FINSI;
     |SI FSalida = 0; |Y #200#5 = #104#4;
          nTiquet = #200#0 + 1;
     |SINO;
          nTiquet = 1;
     |FINSI;
     |HORA aHora;
     aHora = aHora % 105;

     |DDEFECTO #200;
     #200#5 = #104#4;
     #200#0 = nTiquet;
     #200#1 = ~;
     #200#2 = aHora;
     #200#3 = #0#0;
     #200#5 = #24#0;
     #200#6 = #24#1;
     #200#38 = "N";

     nLin = 10;
     |ABRE #111;
     |BUCLE diam0006;
     |CIERRA #111;
     |GRABA 020#200n;

     |DDEFECTO #300;
     |ABRE #300;
     #300#8 = #200#5;
     #300#0 = #200#0;
     #300#1 = 1;
     #300#2 = ~;

     |HORA aAlfa; aAlfa = aAlfa % 105;

     #300#3 = aAlfa;
     #300#4 = #0#0;
     #300#5 = #5#3;
     #300#6 = #0#1;

     |DDEFECTO #500;
     #500#0 = #5#3;
     |LEE 000#500=;

     #300#7 = #500#1;
     #300#8 = #104#4;
     #300#9 = "A";
     |GRABA 020#300n;
     |CIERRA #300;

     |HAZ Correo;

     |HAZ DescargaDef;
|FPRC;

|PROCESO Tecla; |TIPO 11;
     |SI FSalida = 13; |Y #50#1 ! 0;        ||Obs

          eaSer = #50#0;
          enNum = #50#1;
          enTec1 = #50#2;
          enTec2 = #50#3;
          |SUB_EJECUTA_NP "diam0006";
          |ERROR; |ACABA;
     |FINSI;
     |SI FSalida = 14; |Y #50#1 ! 0;       ||Tiquet
          |SI #50#11 = "N";
               |MENAV "0000Pedido no visado...";
               |ERROR; |ACABA;
          |FINSI;
          |SI #50#9 ! 0;
               |MENAV "0000Tiquet YA creado...";
               |ERROR; |ACABA;
          |FINSI;
          |ABRE #5;
          #5#0 = #50#0;
          #5#1 = #50#1;
          #5#2 = #50#2;
          #5#3 = #50#3;
          |LEE 121#5=;
          |SI FSalida = 0;
               |HAZ CreaTiquet;
               #5#9 = nTiquet;
               |GRABA 020#5a;
               #50#9 = nTiquet; |PINTA #50#9;
               |GRABA 020#50a;
          |FINSI;
          |CIERRA #5;
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO Tipo5; |TIPO 5;
     #50#11 = "N";
     |LINEAS_BI_ATRI #50#11, S , D, -1;
|FPRC;

컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
|PROCESO Sale; |TIPO 6;
     |SI FSalida = 1;
          |ERROR;
          |PTEC 807;
     |FINSI;
|FPRC;

|PROCESO DiagTecnico; |TIPO 66;
     |HAZ NomTecnico;
     |CONSULTA_CLAVE #1#500;
     |SI FSalida = 0;
          #0#0 = #500#0; |PINTA #0#0;
          #0#1 = #500#1; |PINTA #0#1;
     |SINO;
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO NomTecnico; |TIPO 0;
     |DDEFECTO #500;
     #500#0 = #0#0;
     |LEE 000#500=;
     |SI FSalida ! 0;
          |MENAV "0000No existe...";
          |ERROR;
     |SINO;
          #0#1 = #500#1;
          |PINTA #0#1;
          |HAZ LaClave;
     |FINSI;
|FPRC;

|PROCESO DefTecnico; |TIPO 7;
     |SELECT #3#500;
     aAlfa = Usuario % 810;
     #500#2 = aAlfa;
     |LEE 000#500=;
     |SI FSalida = 0;
          #0#0 = #500#0;
          |PINTA #0#0;
     |FINSI;
     |SELECT #1#500;
|FPRC;
컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
|PROCESO Min;
     #50#0 = "     ";
     #50#1 = 000001;
     #50#3 = 0001;
|FPRC;

|PROCESO Max;
     #50#0 = "zzzzz";
     #50#1 = 999999;
     #50#3 = 9999;
|FPRC;

|PROCESO diam0003;
     nHay = 1;
     |SI #3#4 ! "S";
          nHay = 0;
          |ERROR10;
     |FINSI;
|FPRC;

|DEFBUCLE diam0003;
     #3#1;
     ;
     #50#0, #50#1, 0001;
     #50#0, #50#1, 9999;
     ;
     NULL, diam0003;
|FIN;

|PROCESO diam0005;
     |PARA i = 0; |SI i [ 10; |HACIENDO i = i + 1;
          #50i = #5i;
     |SIGUE;
     #50#11 = "N";
     nHay = 0;
     |BUCLE diam0003;
     |SI nHay = 1;
          #50#11 = "S";
     |FINSI;
     |GRABA 020#50n;
|FPRC;

|DEFBUCLE diam0005;
     #5#1;
     9;
     "     ", 000001, #0#0, 0001, 0;
     "zzzzz", 999999, #0#0, 9999, 0;
     ;
     NULL, diam0005;
|FIN;

|PROCESO Tipo12; |TIPO 12;
     |SI FSalida = 1;|O FSalida = 7;
          |ACABA;
     |FINSI;

     aAlfa = Usuario; |QBF aAlfa;
     |NOME_DAT #50 aAlfa; |DELINDEX #50;
     |ABRE #50;
     |BUCLE diam0005;
     |CIERRA #50;
     |ENTLINEAL #1#50, -1, 4, 22, 0, Min, Max;
     |DELINDEX #50;
|FPRC;

|PROGRAMA;
     |DBASS aBase; |QBF aBase;

     aDef1  = aBase + "dspartes/def/dstelw01";
     aDef2  = aBase + "dspartes/def/dstelm02";
     aDef3  = aBase + "dspartes/def/dstelm03";
     aDef5  = aBase + "dspartes/def/dsparm05";
     aDef10 = aBase + "dspartes/def/dsparm01";
     aDat   = aBase + "dspartes/fich/";

     |CLS;
     |PINPA #0#0
     |PINDA #0#0;
     |ENTLINEAL #1#50, -1, 7, 22, 0, Min, Max;

     |CARGA_DEF aDef5, dsparm05@;
     |SI FSalida = 0;
          |PATH_DAT #500 aDat;
          |ABRET #500;
          |ENDATOS #1#0;
          |CIERRAT #500;
          |DESCARGA_DEF #dsparm05@;
     |SINO;
          aDef5 = "0000Error al cargar: " + aDef5;
          |MENAV aDef5;
     |FINSI;
|FPRO;
