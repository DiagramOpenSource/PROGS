|FICHEROS;
     iber0029 #0;
     referen@ #1;
     refere2@ #2;
     refere3@ #3;
     refere4@ #4;

     ib__data #20;
     dsm00200 #200; ||Parametros Correo Central
     agifa142 #142;
     iber0023 #100; ||Historico
     dsw00011 #999; || Usado de temporal para el directorio
     agifa112 #112;
     agifa024 #24;
     agifa025 #25;
     agifa019 #19;
     agifa017 #17;
     dsm00024 #240;
     agifa065 #65;

||컴컴 A Importar 컴컴컴컴컴컴컴컴컴컴컴컴컴컴
|| SI SE A쩇DE ALGUNO A쩇DIRLO A LA MATRIZ !!!!
||컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
     agifa512 #512; ||Pagos
     agifa513 #513; ||Cobros
     iber0003 #103; ||Ped. Repo
     iber0004 #104; || ' '
     agifa077 #77;  ||Alb. Comp
     agifa080 #80;  || ' '
     agifa010 #10;  ||Alb Vta
     agifa071 #71;  || ' '
     dsm00047 #47;  || ' '
     agifa084 #84;  ||Fact Directa
     agifa069 #69;  || ' '
     dsm00048 #480; || ' '
     agifa501 #501; ||Tiquets
     agifa502 #502; || ' '
     dsm00051 #503; || ' '
     dsm00056 #56;  ||Entrada Valorada
     dsm00057 #57;  || ' '
     dsm00043 #43;  ||Regu.Existencias
     dsm00044 #44;  || ' '
     dsm00054 #54;  ||Movi.Almacenes
     dsm00055 #55;  || ' '
     agifa509 #509; || Resumen
     agifa090 #90;  || Programaciones
     agifa074 #74;  || ' '
     dsm00052 #52;  || ' '
     dsm00067 #67;  || ' '
|FIN;

|INCLUYE i_varart;

|VARIABLES;
     &eaNomDef = "";
     &enCamposDef = 0;
     &aDivisa = "";
     &aMoneda  = "";

     &qFichero = "";
     &eaTag    = "";

     &enFicOrg = "";
     &enFicImp = "";

     &eaImporIber = "";

     fFecha    = @;
     aCentro   = "";
     aParam    = "";
     aFic2     = "";
     aFic3     = "";
     aFic4     = "";
     aFic      = "";
     aAlfa     = "";
     aAlf1     = "";
     aPath     = "";
     &path     = "";
     aOrg      = "";
     aExt      = "";
     aGz       = "";
     aDef      = "";
     aDef1     = "";
     aDef2     = "";
     aDef3     = "";
     aDef4     = "";
     aMes      = "";
     aSer      = "";
     nNume     = 0;
     nNum      = 0;
     nForma    = 0;
     nImpo     = 0;
     nMes      = 0;
     nHandle   = 0;
     i         = 0;
     nSwErr    = 0;
     nLon      = 0;
     nHay      = 0;
     nSal      = 0;
     nLin      = 0;
     nCampo1   = 0;
     nCampo2   = 0;
     nCampo3   = 0;
     nCampo4   = 0;
     nSw       = 0;

     fmes      = "";
     mes       = 0;
     nMargen   = 0;
     imneto    = 0;
     nDto      = 0;
     retencion = 0;
     beta      = 0;
     pelas     = 0;
     fecha     = @;

     aFicDdx   = "";
     aFdef     = "";
     aFdes     = "";
     aDdx      = "";
     nBufer    = 0;
     nCamOrg   = 0;
     nCampos   = 0;

 {-1}Fichero   = "";
     Fichero1  = "agifa512";
     Fichero2  = "agifa513";
     Fichero3  = "iber0003";
     Fichero4  = "iber0004";
     Fichero5  = "agifa077";
     Fichero6  = "agifa080";
     Fichero7  = "agifa010";
     Fichero8  = "agifa071";
     Fichero9  = "dsm00047";
     Fichero10 = "agifa084";
     Fichero11 = "agifa069";
     Fichero12 = "dsm00048";
     Fichero13 = "agifa501";
     Fichero14 = "agifa502";
     Fichero15 = "dsm00051";
     Fichero16 = "dsm00056";
     Fichero17 = "dsm00057";
     Fichero18 = "dsm00043";
     Fichero19 = "dsm00044";
     Fichero20 = "dsm00054";
     Fichero21 = "dsm00055";
     Fichero22 = "agifa509";
     Fichero23 = "agifa090";
     Fichero24 = "agifa074";
     Fichero25 = "dsm00052";
     Fichero26 = "dsm00067";
     Fichero27 = "FIN";

     aAux = "";
     aFichero = "";
     aFicTxt = "";
     nTipo = 0;
     aFicEco = "";
     aFecEco = "";
     aHoraEco = "";
     aMensaje = "";
     nConta = 0;
     aDirEco = "";
     aPathEco = "";
     &eaAlfaDir = "";
     &eaDirecOk = "";
     aMenLog = "";
     nErrLog = 0;
     aDBase = "";
     aDMenu = "";
     aDNMen = "002472.men";
     aDAlfa = "";
     nDHand = 0;
     nCampo = 0;
     aConta = "";
     aOrigen = "";
     aDVers = "";
     aDirOri = "";
     aTipo = "";
     aNombre = "";
     aFecha = "";
     aHora = "";
     aVarHora = "";
     aPrograma = "";
     &eaFicEnvio = "";
     &eaDestino = "";
     &enSwCentral = 0;
     aNomTxt = "";
     aFicTar = "";
     nHandle1  = 0;

|FIN;
컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
|PROCESO LeeDDX;
     eaNomDef = aFicDdx;
     |HAZ CamposDef;
     nCampos = enCamposDef;
|FPRC;

|PROCESO ChequeaDDX;
     IFichero = Fichero1<-;
     |PARA; |SI Fichero ! "FIN"; |HACIENDO;
          aFicDdx = Fichero;
          |DDEF aDef;  |QBF aDef;
          |DFICE aOrg; |QBF aOrg;
          aFdef = aDef + aFicDdx;     || Crea el .ddx si no existe
          aFdes = aOrg + aFicDdx;
          |LEEDEDEF aFdef, aFdes;

          |HAZ LeeDDX;
          nCamOrg = nCampos;
          enFicOrg = aDdx;

          aFicDdx = "exp" + (aFicDdx % -105);
          aOrg = aPath;
          |HAZ LeeDDX;
          enFicImp = aDdx;
          |SI nCampos > nCamOrg;
               aAlfa = "0000Error estructura de " + aFicDdx;
               |MENAV aAlfa;
               nSwErr = 1;
          |SINO;
               ||enFicOrg = "";
               ||enFicImp = "";
               |HAZ CheqFicImp;
               |SI enFicOrg = ""; |O enFicImp = "";
                    aAlfa = "0000Error estructura de " + aFicDdx;
                    |MENAV aAlfa;
                    nSwErr = 1;
               |FINSI;
          |FINSI;
          IFichero = IFichero + 1;
     |SIGUE;
|FPRC;
컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
|PROCESO Acu_dsm00067;
     |ABRE #19;
     |ABRE #17;

     |DDEFECTO #19;
     #19#0 = #74#2;
     |LEE 101#19=;
     |SI FSalida ! 0; |GRABA 020#19b; |FINSI;

     |DDEFECTO #17;
     #17#0 = #74#2;
     #17#1 = #74#7;
     |LEE 101#17=;
     |SI FSalida ! 0; |GRABA 020#17b; |FINSI;

     #19#6 =  #19#6 + #67#4;        ||Tota
     #19#104 = #19#104 + #67#4;     ||Dis

     #17#5 =  #17#5 + #67#4;        ||Tota
     #17#39 = #17#39 + #67#4;       ||Dis

     |HAZ ValoraStock;
     |GRABA 020#19a;
     |GRABA 020#17a;
     |CIERRA #17;
     |CIERRA #19;

     |ABRE #65;
     |DDEFECTO #65;
     #65#0 = #19#0;  || Art
     #65#1 = #90#1;  || Fec
     #65#2 = "FB";
     #65#3 = #74#0;  || Doc
     #65#4 = #74#1;  || Lin
     #65#11 = #74#9; || Ser
     |LEE 101#65=;
     |SI FSalida ! 0; |GRABA 020#65b; |FINSI;

     #65#5 = #17#1;  || Alma
     #65#6 = #67#4;  || Uni
     #65#7 = #19#6;  || Stock Re
     #65#8 = #19#9;  || PCM
     |GRABA 020#65a;
     |CIERRA #65;
|FPRC;

|PROCESO Acu_dsm00052;
     |ABRE #19;
     |ABRE #17;
     |ABRE #240;

     |DDEFECTO #19;
     #19#0 = #52#3;
     |LEE 101#19=;
     |SI FSalida ! 0; |GRABA 020#19b; |FINSI;

     |DDEFECTO #17;
     #17#0 = #52#3;
     #17#1 = #52#5;
     |LEE 101#17=;
     |SI FSalida ! 0; |GRABA 020#17b; |FINSI;

     |SI #19#180 = "S";
          |DDEFECTO #24;
          #240#0 = #74#2;
          #240#1 = #74#7;
          #240#2 = #67#3;
          |LEE 101#240=;
          |SI FSalida ! 0; |GRABA 020#240b; |FINSI;
     |FINSI;

     #19#6 = #19#6 - #52#7;         ||Tota
     #19#185 = #19#185 - #52#10;
     #19#104 = #19#104 - #52#7;     ||Dis
     #19#193 = #19#193 - #67#5;

     #17#5 = #17#5 - #52#7;         ||Tota
     #17#47 = #17#47 - #52#10;
     #17#39 = #17#39 - #52#7;       ||Dis
     #17#48 = #17#48 - #52#10;

     #240#10 = #24#10 + #52#7;
     #240#9 = #24#9 + #52#10;

     |HAZ ValoraStock;
     |GRABA 020#17a;
     |GRABA 020#19a;
     |SI #19#180 = "S";
          |GRABA 020#240a;
     |FINSI;
     |CIERRA #17;
     |CIERRA #19;
     |CIERRA #240;

     |ABRE #65;
     |DDEFECTO #65;
     #65#0 = #19#0;  || Art
     #65#1 = #90#1;  || Fec
     #65#2 = "SF";
     #65#3 = #74#0;  || Doc
     #65#4 = #74#1;  || Lin
     #65#11 = #74#9; || Ser
     |LEE 101#65=;
     |SI FSalida ! 0; |GRABA 020#65b; |FINSI;

     #65#5 = #17#1;  || Alma
     #65#6 = -#52#7; || Uni
     #65#7 = #19#6;  || Stock Re
     #65#8 = #19#9;  || PCM
     #65#17 = #52#10;
     #65#18 = #52#8;
     |GRABA 020#65a;
     |CIERRA #65;
|FPRC;

|PROCESO Acu_agifa077;
     nForma = 1;
     |ABRE #112;
     nImpo = (#80#9 < #77#5) < #77#32;
     |SI #142#114 = "S";
          nImpo = nImpo < #77#7;
     |FINSI;
     #112#0 = #77#3;
     |LEE 101#112=;
     |SI FSalida = 0;
         aMes = #77#1 % A402;
         nMes = aMes + 26;
         |SI #77#42 = "N";
             #112nMes = #112nMes + (nImpo * nForma);
             #112#39 = #112#39 + (nImpo * nForma);
         |SINO;
             #112nMes = #112nMes - (nImpo * nForma);
             #112#39 = #112#39 - (nImpo * nForma);
         |FINSI;
         |GRABA 020#112a;
         |LIBERA #112;
     |FINSI;
     |CIERRA #112;

     |SI #77#42 = "N";
          enImpPrvCom = nImpo * nForma;
     |SINO;
          enImpPrvCom = -nImpo * nForma;
     |FINSI;
     efFecha = #77#1;
     eaProve = #77#3;
     |HAZ AcumulaPrv;
|FPRC;

|PROCESO Acu_agifa010;
     |SI #10#15 ] 0;
          nNume = 1;
     |SINO;
          nNume = -1;
          #10#15 = -#10#15;
     |FINSI;
     nImpo = ((#10#15 < #10#5) < #10#32) < #10#7;
     nImpo = nImpo * nNume;
     #10#15 = #10#15 * nNume;

     |DDEFECTO #24;
     |ABRE #24;
     #24#0 = #10#3;
     |LEE 101#24=;
     |SI FSalida ! 0;    |GRABA 020#24b;     |FINSI;
     |SI #10#43 = AN;
          #24#50 = #24#50 +. nImpo;    || es albaran
     |SINO;
          #24#50 = #24#50 -. nImpo;    || es abono
     |FINSI;
     |GRABA 020#24a;
     |LIBERA #24;
     |CIERRA #24;
     eaTag = "AV"; |HAZ Riesgos;

     |SI #10#43 = "N";
          enImpCliPen = nImpo;
     |SINO;
          enImpCliPen =  -nImpo;
     |FINSI;
     eaCliente = #10#3;
     efFecha = #10#1;
     |HAZ AcumulaCli;
|FPRC;

|PROCESO ActArti;
     nForma = 1;
     fmes = #84#1 % A402;
     mes = fmes - 1;
     mes = mes * 5;
     mes = mes + 35;
     #19#0 = #69#2;
     |LEE 101#19=;
     |SI FSalida = 0;
          |SI #19#194 = 2;
               nImpo = ((((#69#30 * #69#6) < #69#8) < #69#21) < #84#5) < #84#32;
          |SINO;
               nImpo = ((((#69#5 * #69#6) < #69#8) < #69#21) < #84#5) < #84#32;
          |FINSI;
          |SI #142#115 = "S";
               nImpo = nImpo < #84#7;
          |FINSI;
          nMargen = nImpo - #69#14;
          #19mes = #19mes + (nImpo * nForma);
          #19#95 = #19#95 + (nImpo * nForma);
          mes = mes + 1;
          #19mes = #19mes + (nMargen * nForma);
          #19#96 = #19#96 + (nMargen * nForma);
          |GRABA 020#19a;
          |LIBERA #19;

          efFecha = #84#1;
          eaArticulo = #69#2;
          enImpVta = (nImpo * nForma);
          enImpMar = (nMargen * nForma);
          |HAZ AcumulaArt;
     |FINSI;
|FPRC;

|PROCESO Acu_agifa084;
     |DDEFECTO #24;
     |ABRE #24;
     #24#0 = #84#3;
     |LEE 101#24=;
     |SI FSalida ! 0;    |GRABA 020#24b;     |FINSI;
     fmes = #84#1 % A402;
     mes = fmes - 1;
     mes = mes * 4;
     mes = mes + 54;
     imneto = #84#15 - #84#25;
     |SI #142#115 = "N";
          nImpo = (imneto * 100) / (100 - #84#7);
          nDto = #84#25 + (imneto - nImpo);
     |SINO;
          nImpo = imneto;
          nDto = #84#25;
     |FINSI;
     #24mes = #24mes +. nImpo;
     mes = mes + 1;
     #24mes = #24mes +. nDto;
     mes = mes + 1;
     #24mes = #24mes +. #84#37;
     #24#102 = #24#102 +. nImpo; || imneto;
     #24#103 = #24#103 +. nDto;
     #24#104 = #24#104 +. #84#37;
     mes = FEntrada / 100;
     mes = mes ! 0;
     |SI 1 = mes;
          #24#45 = #84#1;
          #24#46 = imneto;
     |FINSI;
     #24#110 = #24#110 +. imneto;
     imneto = imneto + #84#24;
     |GRABA 020#24a;
     |LIBERA #24;
     |CIERRA #24;

     enImpCliCom = nImpo;
     enImpCliDto = nDto;
     enImpCliFac = imneto;
     enImpCliMar = #84#37;
     eaCliente = #84#3;
     efFecha = #84#1;
     |HAZ AcumulaCli;

     |DDEFECTO #25;
     |ABRE #25;
     #25#0 = #84#6;
     |LEE 101#25=;
     |SI FSalida ! 0;    |GRABA 020#25b;     |FINSI;
     imneto = #84#15 - #84#25;
     fmes = #84#1 % A402;
     mes = fmes - 1;
     mes = mes * 2;
     mes = mes + 11;
     #25mes = #25mes +. #84#26;
     retencion = #25mes % #25#39;
     mes = mes + 1;
     #25mes = #25mes +. imneto;
     #25#35 = #25#35 +. #84#26;
     #25#36 = #25#36 +. imneto;
     mes = fmes - 1;
     mes = mes + 40;
     #25mes = retencion;
     retencion = #25#35 % #25#39;
     #25#52 = retencion;
     |GRABA 020#25a;
     |LIBERA #25;
     |CIERRA #25;

     enImpRepComi = #84#26;
     enImpRepVta = imneto;
     enImpRepRete = retencion;
     eaRepre = #84#6;
     efFecha = #84#1;
     |HAZ AcumulaRep;

     |ABRE #19;
     |BUCLELIN 2 ActArti #84;
     |CIERRA #19;
     |SI #142#47 = "S";  ||컴컴컴컴컴 Acumula en riesgos Si Crea Recibo
          eaTag = "FC";  |HAZ Riesgos;
     |FINSI;
|FPRC;

|PROCESO ActImpoArti;
     |ABRE #19;
     fmes = #501#14 % A402;
     mes = ((fmes - 1) * 5) + 35;
     #19#0 = #502#2;
     |LEE 101#19=;
     |SI FSalida = 0;
          |SI #19#194 = 2;
               nImpo = ((((#502#29 * #502#10) < #502#14) < #501#17) < #501#19);
          |SINO;
               nImpo = ((((#502#5 * #502#10) < #502#14) < #501#17) < #501#19);
          |FINSI;
          |SI #142#115 = "S";
               nImpo = nImpo < #501#18;
          |FINSI;
          nMargen = nImpo - #502#23;
          #19mes = #19mes + (nImpo * nForma);
          #19#95 = #19#95 + (nImpo * nForma);
          mes = mes + 1;
          #19mes = #19mes + (nMargen * nForma);
          #19#96 = #19#96 + (nMargen * nForma);
          |GRABA 020#19a;
          |LIBERA #19;

          efFecha = #501#14;
          eaArticulo = #502#2;
          enImpVta = (nImpo * nForma);
          enImpMar = (nMargen * nForma);
          |HAZ AcumulaArt;
     |FINSI;
     |CIERRA #19;
|FPRC;

|PROCESO Acu_agifa501;
     |ABRE #24;
     |DDEFECTO #24;
     #24#0 = #501#1;
     |LEE 101#24=;
     |SI FSalida ! 0; |GRABA 020#24b; |FINSI;
     fmes = #501#14 % A402;
     mes = ((fmes - 1) * 4) + 54;
     imneto = #501#3 - nDto;
     |SI #501#35 = "A";
          #24#50 = #24#50 +. imneto;
          enImpCliPen = imneto;
     |SINO;
          |SI #142#115 = "N";
               nImpo = (imneto * 100) / (100 - #501#18);
               nDto = nDto + (imneto - nImpo);
          |SINO;
               nImpo = imneto;
          |FINSI;
          #24mes = #24mes +. nImpo;
          mes = mes + 1;
          #24mes = #24mes +. nDto;
          mes = mes + 1;
          #24mes = #24mes +. #501#8;
          #24#102 = #24#102 +. nImpo;
          #24#103 = #24#103 +. nDto;
          #24#104 = #24#104 +. #501#8;
          beta = (FEntrada / 100) ! 0;
          |SI beta = 1;
               #24#45 = #501#14;
          |FINSI;
          #24#46 = imneto;
          #24#110 = #24#110 +. imneto;

          enImpCliCom = nImpo;
          enImpCliDto = nDto;
          enImpCliFac = imneto;
          enImpCliMar = #501#8;
     |FINSI;
     eaCliente = #501#1;
     efFecha = #501#14;
     |HAZ AcumulaCli;

     |GRABA 020#24a;
     |CIERRA #24;
|FPRC;

|PROCESO Acu_dsm00057;
     |DDEFECTO #112;
     |ABRE #112;
     #112#0 = #57#5;
     |LEE 101#112=;
     |SI FSalida ! 0; |GRABA 020#112b; |FINSI;

     |SI #57#4 = 0;
          pelas = 0;
     |SINO;
          |SI #19#196 = 1;
               pelas = #57#4 * #57#11;
          |SINO;
               pelas = #57#21 * #57#11;
          |FINSI;
     |FINSI;

     fecha = #57#12;
     fmes  = fecha % A402;
     mes   = fmes - 1;
     mes   = mes + 27;
     #112mes = #112mes +. pelas;
     #112#39 = #112#39 +. pelas;

     |GRABA 020#112a;
     |CIERRA #112;

     enImpPrvCom = pelas;
     efFecha = #57#12;
     eaProve = #57#5;
     |HAZ AcumulaPrv;
|FPRC;
컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
|PROCESO CargaDef;
     |DDEF aDef;
     aDef = aDef + aFic;
     |CARGA_DEF aDef, referen@;
     |SI FSalida ! 0;
          aAlfa = "0000 Error al cargar " + aDef;
          |MENAV aAlfa;
     |SINO;
          aAlfa = "0000 Procesando " + aFic;
          ||MENAV aAlfa;
     |FINSI;
|FPRC;

|PROCESO CargaDef2;
     |DDEF aDef;
     aDef = aDef + aFic2;
     |CARGA_DEF aDef, refere2@;
     |SI FSalida ! 0;
          aAlfa = "0000 Error al cargar " + aDef;
          |MENAV aAlfa;
     |SINO;
          aAlfa = "0000 Procesando " + aFic;
          ||MENAV aAlfa;
     |FINSI;
|FPRC;

|PROCESO CargaDef3;
     |DDEF aDef;
     aDef = aDef + aFic3;
     |CARGA_DEF aDef, refere3@;
     |SI FSalida ! 0;
          aAlfa = "0000 Error al cargar " + aDef;
          |MENAV aAlfa;
     |SINO;
          aAlfa = "0000 Procesando " + aFic;
          ||MENAV aAlfa;
     |FINSI;
|FPRC;

|PROCESO CargaDef4;
     |DDEF aDef;
     aDef = aDef + aFic4;
     |CARGA_DEF aDef, refere4@;
     |SI FSalida ! 0;
          aAlfa = "0000 Error al cargar " + aDef;
          |MENAV aAlfa;
     |SINO;
          aAlfa = "0000 Procesando " + aFic;
          ||MENAV aAlfa;
     |FINSI;
|FPRC;

|PROCESO NomeDat;
     |PATH_DAT #1 aPath;
     aAlfa = "exp" + (aFic % -105);
     |NOME_DAT #1 aAlfa;
     aAlfa = "|NC";
     aAlfa = #referen@#aAlfa#;
     nCampo1 = aAlfa; nCampo1 = nCampo1 - 1;
     |ABRE #referen@;
|FPRC;

|PROCESO NomeDat2;
     |PATH_DAT #2 aPath;
     aAlfa = "exp" + (aFic2 % -105);
     |NOME_DAT #2 aAlfa;
     aAlfa = "|NC";
     aAlfa = #refere2@#aAlfa#;
     nCampo2 = aAlfa; nCampo2 = nCampo2 - 1;
     |ABRE #refere2@;
|FPRC;

|PROCESO NomeDat3;
     |PATH_DAT #3 aPath;
     aAlfa = "exp" + (aFic3 % -105);
     |NOME_DAT #3 aAlfa;
     aAlfa = "|NC";
     aAlfa = #refere3@#aAlfa#;
     nCampo3 = aAlfa; nCampo3 = nCampo3 - 1;
     |ABRE #refere3@;
|FPRC;

|PROCESO NomeDat4;
     |PATH_DAT #4 aPath;
     aAlfa = "exp" + (aFic4 % -105);
     |NOME_DAT #4 aAlfa;
     aAlfa = "|NC";
     aAlfa = #refere4@#aAlfa#;
     nCampo4 = aAlfa; nCampo4 = nCampo4 - 1;
     |ABRE #refere4@;
|FPRC;

|PROCESO DescargaDef;
     |CIERRA #referen@;
     |DESCARGA_DEF #referen@;
|FPRC;

|PROCESO DescargaDef2;
     |CIERRA #refere2@;
     |DESCARGA_DEF #refere2@;
|FPRC;

|PROCESO DescargaDef3;
     |CIERRA #refere3@;
     |DESCARGA_DEF #refere3@;
|FPRC;

|PROCESO DescargaDef4;
     |CIERRA #refere4@;
     |DESCARGA_DEF #refere4@;
|FPRC;
컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
|PROCESO Bagifa512;
     |PARA i = 0; |SI i [ nCampo1; |HACIENDO i = i + 1;
          #512i = #1i;
     |SIGUE;
     |LEE 000#512=;
     |SI FSalida ! 0;
          |GRABA 020#512n;
     |FINSI;
|FPRC;

|DEFBUCLE Bagifa512;
     #1#1002;
     ;
     "     ", 000001;
     "zzzzz", 999999;
     ;
     NULL, Bagifa512;
|FIN;

|PROCESO Bagifa513;
     |PARA i = 0; |SI i [ nCampo1; |HACIENDO i = i + 1;
          #513i = #1i;
     |SIGUE;
     |LEE 000#513=;
     |SI FSalida ! 0;
          |GRABA 020#513n;
     |FINSI;
|FPRC;

|DEFBUCLE Bagifa513;
     #1#1002;
     ;
     "     ", 000001;
     "zzzzz", 999999;
     ;
     NULL, Bagifa513;
|FIN;

|PROCESO Biber0004;
     |PARA i = 0; |SI i [ nCampo2; |HACIENDO i = i + 1;
          #104i = #2i;
     |SIGUE;
     |LEE 000#104=;
     |SI FSalida ! 0;
          |GRABA 020#104n;
     |FINSI;
|FPRC;

|DEFBUCLE Biber0004;
     #2#1003;
     ;
     #1#0, #1#1, 001
     #1#0, #1#1, 999;
     ;
     NULL, Biber0004;
|FIN;

|PROCESO Biber0003;
     |PARA i = 0; |SI i [ nCampo1; |HACIENDO i = i + 1;
          #103i = #1i;
     |SIGUE;
     |LEE 000#103=;
     |SI FSalida ! 0;
          |GRABA 020#103n;
          |BUCLE Biber0004;
     |FINSI;
|FPRC;

|DEFBUCLE Biber0003;
     #1#1002;
     ;
     00001, "01.01.0000";
     99999, "31.12.9999";
     ;
     NULL, Biber0003;
|FIN;

|PROCESO Bagifa080;
     |PARA i = 0; |SI i [ nCampo2; |HACIENDO i = i + 1;
          #80i = #2i;
     |SIGUE;
     |LEE 000#80=;
     |SI FSalida ! 0;
          |GRABA 020#80n;
          eaSer = #80#27;
          enNum = #80#0;
          enLin = #80#1;
          |SUB_EJECUTA_NP "iber1029;AC"; ||Acumula Stock
     |FINSI;
|FPRC;

|DEFBUCLE Bagifa080;
     #2#1003;
     ;
     #1#50, #1#0, 001
     #1#50, #1#0, 999;
     ;
     NULL, Bagifa080;
|FIN;

|PROCESO Bagifa077;
     aDivisa = #1#56;
     aMoneda = #1#58;
     |HAZ Divisas077;

     #77#50 = #1#50;
     #77#0 = #1#0;
     |LEE 000#77=;
     |SI FSalida = 0;
          eaSer = #77#50;
          enNum = #77#0;
          |SUB_EJECUTA_NP "iber1029;ACBAJA"; ||Acumula Stock
     |FINSI;

     |PARA i = 0; |SI i [ nCampo1; |HACIENDO i = i + 1;
          #77i = #1i;
     |SIGUE;
     |LEE 000#77=;
     |SI FSalida ! 0;
          |GRABA 020#77n;
          |BUCLE Bagifa080;
          |HAZ Acu_agifa077;
          eaSer = #77#50;
          enNum = #77#0;
          enLin = 0;
          |SUB_EJECUTA_NP "iber1029;AC"; ||Acumula Stock
     |FINSI;
|FPRC;

|DEFBUCLE Bagifa077;
     #1#1002;
     ;
     "     ", 000001;
     "zzzzz", 999999;
     ;
     NULL, Bagifa077;
|FIN;

|PROCESO Bdsm00047;
     |PARA i = 0; |SI i [ nCampo3; |HACIENDO i = i + 1;
          #47i = #3i;
     |SIGUE;
     |LEE 000#47=;
     |SI FSalida ! 0;
          |GRABA 020#47n;
     |FINSI;
|FPRC;

|DEFBUCLE Bdsm00047;
     #3#1004;
     ;
     #71#29, #71#0, #71#1, 001;
     #71#29, #71#0, #71#1, 999;
     ;
     NULL, Bdsm00047;
|FIN;

|PROCESO Bagifa071;
     |PARA i = 0; |SI i [ nCampo2; |HACIENDO i = i + 1;
          #71i = #2i;
     |SIGUE;
     |LEE 000#71=;
     |SI FSalida ! 0;
          |BUCLE Bdsm00047;
          eaImporIber = "S";
          eaTipo550 = "AV";
          enForma = 1; |HAZ AcumulaAV;
          |GRABA 020#71n;
     |FINSI;
|FPRC;

|DEFBUCLE Bagifa071;
     #2#1003;
     ;
     #1#52, #1#0, 001
     #1#52, #1#0, 999;
     ;
     NULL, Bagifa071;
|FIN;

|PROCESO Bagifa010;
     aDivisa = #1#68;
     aMoneda = #1#70;
     |HAZ Divisas010;

     |PARA i = 0; |SI i [ nCampo1; |HACIENDO i = i + 1;
          #10i = #1i;
     |SIGUE;
     |LEE 000#10=;
     |SI FSalida ! 0;
          |GRABA 020#10n;
          |BUCLE Bagifa071;
          eaSer = #10#52;
          enNum = #10#0;
          |SUB_EJECUTA_NP "iberz010;10";
          |HAZ Acu_agifa010;
     |FINSI;
|FPRC;

|DEFBUCLE Bagifa010;
     #1#1002;
     ;
     "     ", 000001;
     "zzzzz", 999999;
     ;
     NULL, Bagifa010;
|FIN;

|PROCESO Bdsm00048;
     |PARA i = 0; |SI i [ nCampo3; |HACIENDO i = i + 1;
          #480i = #3i;
     |SIGUE;
     |LEE 000#480=;
     |SI FSalida ! 0;
          |GRABA 020#480n;
     |FINSI;
|FPRC;

|DEFBUCLE Bdsm00048;
     #3#1004;
     ;
     #69#20, #69#0, #69#1, 001;
     #69#20, #69#0, #69#1, 999;
     ;
     NULL, Bdsm00048;
|FIN;

|PROCESO Bagifa069;
     |PARA i = 0; |SI i [ nCampo2; |HACIENDO i = i + 1;
          #69i = #2i;
     |SIGUE;
     |LEE 000#69=;
     |SI FSalida ! 0;
          |BUCLE Bdsm00048;
          |GRABA 020#69n;
          enForma = 1; |HAZ AcumulaFC;
     |FINSI;
|FPRC;

|DEFBUCLE Bagifa069;
     #2#1003;
     ;
     #1#48, #1#0, 001
     #1#48, #1#0, 999;
     ;
     NULL, Bagifa069;
|FIN;

|PROCESO Bagifa084;
     aDivisa = #1#65;
     aMoneda = #1#67;
     |HAZ Divisas084;

     |PARA i = 0; |SI i [ nCampo1; |HACIENDO i = i + 1;
          #84i = #1i;
     |SIGUE;
     |LEE 000#84=;
     |SI FSalida ! 0;
          |GRABA 020#84n;
          |BUCLE Bagifa069;
          |LEE 000#84=;
          eaSer = #84#48;
          enNum = #84#0;
          |SUB_EJECUTA_NP "iberz010;84";
          |HAZ Acu_agifa084;
     |FINSI;
|FPRC;

|DEFBUCLE Bagifa084;
     #1#1002;
     ;
     "     ", 000001;
     "zzzzz", 999999;
     ;
     NULL, Bagifa084;
|FIN;

|PROCESO Bdsm00051;
     |PARA i = 0; |SI i [ nCampo3; |HACIENDO i = i + 1;
          #503i = #3i;
     |SIGUE;
     |LEE 000#503=;
     |SI FSalida ! 0;
          |GRABA 020#503n;
     |FINSI;
|FPRC;

|DEFBUCLE Bdsm00051;
     #3#1004;
     ;
     #2#20, #2#0, #2#1, 001;
     #2#20, #2#0, #2#1, 999;
     ;
     NULL, Bdsm00051;
|FIN;

|PROCESO Bagifa502;
     |PARA i = 0; |SI i [ nCampo2; |HACIENDO i = i + 1;
          #502i = #2i;
     |SIGUE;
     |LEE 000#502=;
     |SI FSalida ! 0;
          |GRABA 020#502n;
          |BUCLE Bdsm00051;
          |SI #501#35 = "T";
               eaImporIber = "S";
               eaTipo550 = "TI";
               enForma = 1; |HAZ AcumulaTPVR;
               |HAZ ActImpoArti;
          |FINSI;
          |SI #501#35 = "F";
               |HAZ ActImpoArti;
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE Bagifa502;
     #2#1003;
     ;
     #1#36, #1#0, 001
     #1#36, #1#0, 999;
     ;
     NULL, Bagifa502;
|FIN;

|PROCESO Bagifa501;
     |PARA i = 0; |SI i [ nCampo1; |HACIENDO i = i + 1;
          #501i = #1i;
     |SIGUE;
     |LEE 000#501=;
     |SI FSalida ! 0;
          |GRABA 020#501n;
          |BUCLE Bagifa502;
          eaSer = #501#36;
          enNum = #501#0;
          |SUB_EJECUTA_NP "iberz010;501";
          |HAZ Acu_agifa501;
     |FINSI;
|FPRC;

|DEFBUCLE Bagifa501;
     #1#1002;
     ;
     "     ", 000001;
     "zzzzz", 999999;
     ;
     NULL, Bagifa501;
|FIN;

|PROCESO Bdsm00057;
     |PARA i = 0; |SI i [ nCampo2; |HACIENDO i = i + 1;
          #57i = #2i;
     |SIGUE;
     |LEE 000#57=;
     |SI FSalida ! 0;
          |GRABA 020#57n;
          |HAZ Acu_dsm00057;
          eaSer = #57#28;
          enNum = #57#27;
          enLin = #57#14;
          |SUB_EJECUTA_NP "iber1029;EV";
     |FINSI;
|FPRC;

|DEFBUCLE Bdsm00057;
     #2#1003;
     ;
     #1#2, #1#0, 001
     #1#2, #1#0, 999;
     ;
     NULL, Bdsm00057;
|FIN;

|PROCESO Bdsm00056;
     |PARA i = 0; |SI i [ nCampo1; |HACIENDO i = i + 1;
          #56i = #1i;
     |SIGUE;
     |LEE 000#56=;
     |SI FSalida ! 0;
          |GRABA 020#56n;
          |BUCLE Bdsm00057;
     |FINSI;
|FPRC;

|DEFBUCLE Bdsm00056;
     #1#1002;
     ;
     "     ", 000001;
     "zzzzz", 999999;
     ;
     NULL, Bdsm00056;
|FIN;

|PROCESO Bdsm00044;
     |PARA i = 0; |SI i [ nCampo2; |HACIENDO i = i + 1;
          #44i = #2i;
     |SIGUE;
     |LEE 000#44=;
     |SI FSalida ! 0;
          |GRABA 020#44n;
          eaSer = #44#21;
          enNum = #44#18;
          enLin = #44#8;
          |SUB_EJECUTA_NP "iber1029;ES";
     |FINSI;
|FPRC;

|DEFBUCLE Bdsm00044;
     #2#1003;
     ;
     #1#4, #1#0, 001
     #1#4, #1#0, 999;
     ;
     NULL, Bdsm00044;
|FIN;

|PROCESO Bdsm00043;
     |PARA i = 0; |SI i [ nCampo1; |HACIENDO i = i + 1;
          #43i = #1i;
     |SIGUE;
     |LEE 000#43=;
     |SI FSalida ! 0;
          |GRABA 020#43n;
          |BUCLE Bdsm00044;
     |FINSI;
|FPRC;

|DEFBUCLE Bdsm00043;
     #1#1002;
     ;
     "     ", 000001;
     "zzzzz", 999999;
     ;
     NULL, Bdsm00043;
|FIN;

|PROCESO Bdsm00055;
     |PARA i = 0; |SI i [ nCampo2; |HACIENDO i = i + 1;
          #55i = #2i;
     |SIGUE;
     |LEE 000#55=;
     |SI FSalida ! 0;
          |GRABA 020#55n;
          eaSer = #55#28;
          enNum = #55#27;
          enLin = #55#11;
          |SUB_EJECUTA_NP "iber1029;ME";
     |FINSI;
|FPRC;

|DEFBUCLE Bdsm00055;
     #2#1003;
     ;
     #1#2, #1#0, 001
     #1#2, #1#0, 999;
     ;
     NULL, Bdsm00055;
|FIN;

|PROCESO Bdsm00054;
     |PARA i = 0; |SI i [ nCampo1; |HACIENDO i = i + 1;
          #54i = #1i;
     |SIGUE;
     |LEE 000#54=;
     |SI FSalida ! 0;
          |GRABA 020#54n;
          |BUCLE Bdsm00055;
     |FINSI;
|FPRC;

|DEFBUCLE Bdsm00054;
     #1#1002;
     ;
     "     ", 000001;
     "zzzzz", 999999;
     ;
     NULL, Bdsm00054;
|FIN;

|PROCESO Bagifa509;
     |PARA i = 0; |SI i [ nCampo1; |HACIENDO i = i + 1;
          #509i = #1i;
     |SIGUE;
     |LEE 000#509=;
     |PARA i = 0; |SI i [ nCampo1; |HACIENDO i = i + 1;
          #509i = #1i;
     |SIGUE;
     |SI FSalida = 0;
          |GRABA 020#509a;
     |SINO;
          |GRABA 020#509n;
     |FINSI;
|FPRC;

|DEFBUCLE Bagifa509;
     #1#1003;
     ;
     00001, 00001, "01.01.0000";
     99999, 99999, "31.12.9999";
     ;
     NULL, Bagifa509;
|FIN;

|PROCESO Bdsm00067;
     |PARA i = 0; |SI i [ nCampo4; |HACIENDO i = i + 1;
          #67i = #4i;
     |SIGUE;
     |LEE 000#67=;
     |SI FSalida ! 0;
          |GRABA 020#67n;
          |HAZ Acu_dsm00067;
     |FINSI;
|FPRC;

|DEFBUCLE Bdsm00067;
     #4#1004;
     ;
     #74#9, #74#0, #74#1, "               ";
     #74#9, #74#0, #74#1, "zzzzzzzzzzzzzzz";
     ;
     NULL, Bdsm00067;
|FIN;

|PROCESO Bdsm00052;
     |PARA i = 0; |SI i [ nCampo3; |HACIENDO i = i + 1;
          #52i = #3i;
     |SIGUE;
     |LEE 000#52=;
     |SI FSalida ! 0;
          |GRABA 020#52n;
          |HAZ Acu_dsm00052;
     |FINSI;
|FPRC;

|DEFBUCLE Bdsm00052;
     #3#1004;
     ;
     #74#9, #74#0, #74#1, 001;
     #74#9, #74#0, #74#1, 999;
     ;
     NULL, Bdsm00052;
|FIN;

|PROCESO Bagifa074
     |PARA i = 0; |SI i [ nCampo2; |HACIENDO i = i + 1;
          #74i = #2i;
     |SIGUE;
     |LEE 000#74=;
     |SI FSalida ! 0;
          |GRABA 020#74n;
          |BUCLE Bdsm00052;
          |BUCLE Bdsm00067;
     |FINSI;
|FPRC;

|DEFBUCLE Bagifa074;
     #2#1003;
     ;
     #90#20, #90#0, 001;
     #90#20, #90#0, 999;
     ;
     NULL, Bagifa074;
|FIN;

|PROCESO Bagifa090
     |PARA i = 0; |SI i [ nCampo1; |HACIENDO i = i + 1;
          #90i = #1i;
     |SIGUE;
     |LEE 000#90=;
     |SI FSalida ! 0;
          |GRABA 020#90n;
          |BUCLE Bagifa074;
     |FINSI;
|FPRC;

|DEFBUCLE Bagifa090;
     #1#1002;
     ;
     "      ", 000001;
     "zzzzzz", 999999;
     ;
     NULL, Bagifa090;
|FIN;

|PROCESO Bib__data;
     |PARA i = 0; |SI i [ nCampo1; |HACIENDO i = i + 1;
          #20i = #1i;
     |SIGUE;
     |LEE 000#20=;
     |SI FSalida ! 0;
          |GRABA 020#20n;
     |FINSI;
|FPRC;

|DEFBUCLE Bib__data;
     #1#1004;
     ;
     001, 01, 000001, 000001;
     999, 99, 999999, 999999;
     ;
     NULL, Bib__data;
|FIN;
컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
|PROCESO Importa;
     aFic = "ib__data";
     |HAZ CargaDef;
     |SI FSalida = 0;
          |HAZ NomeDat;
          |ABRE #20;
          |BUCLE Bib__data;
          |CIERRA #20;
          |HAZ DescargaDef;
     |FINSI;
     ||컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
     aFic = "agifa512";
     |HAZ CargaDef;
     |SI FSalida = 0;
          |HAZ NomeDat;
          |ABRE #512;
          |BUCLE Bagifa512;
          |CIERRA #512;
          |HAZ DescargaDef;
     |FINSI;
     ||컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
     aFic = "agifa513";
     |HAZ CargaDef;
     |SI FSalida = 0;
          |HAZ NomeDat;
          |ABRE #513;
          |BUCLE Bagifa513;
          |CIERRA #513;
          |HAZ DescargaDef;
     |FINSI;
     ||컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
     aFic = "iber0003";
     |HAZ CargaDef;
     |SI FSalida = 0;
          aFic2 = "iber0004";
          |HAZ CargaDef2;
          |SI FSalida = 0;
               |HAZ NomeDat;
               |HAZ NomeDat2;
               |ABRE #103;
               |ABRE #104;
               |BUCLE Biber0003;
               |CIERRA #104;
               |CIERRA #103;
               |HAZ DescargaDef2;
          |FINSI;
          |HAZ DescargaDef;
     |FINSI;
     ||컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
     aFic = "agifa077";
     |HAZ CargaDef;
     |SI FSalida = 0;
          aFic2 = "agifa080";
          |HAZ CargaDef2;
          |SI FSalida = 0;
               |HAZ NomeDat;
               |HAZ NomeDat2;
               |ABRE #77;
               |ABRE #80;
               |BUCLE Bagifa077;
               |CIERRA #80;
               |CIERRA #77;
               |HAZ DescargaDef2;
          |FINSI;
          |HAZ DescargaDef;
     |FINSI;
     ||컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
     aFic = "agifa010";
     |HAZ CargaDef;
     |SI FSalida = 0;
          aFic2 = "agifa071";
          |HAZ CargaDef2;
          |SI FSalida = 0;
               aFic3 = "dsm00047";
               |HAZ CargaDef3;
               |SI FSalida = 0;
                    |HAZ NomeDat;
                    |HAZ NomeDat2;
                    |HAZ NomeDat3;
                    |ABRE #47;
                    |ABRE #10;
                    |ABRE #71;
                    |BUCLE Bagifa010;
                    |CIERRA #71;
                    |CIERRA #10;
                    |CIERRA #47;
                    |HAZ DescargaDef3;
               |FINSI;
               |HAZ DescargaDef2;
          |FINSI;
          |HAZ DescargaDef;
     |FINSI;
     ||컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
     aFic = "agifa084";
     |HAZ CargaDef;
     |SI FSalida = 0;
          aFic2 = "agifa069";
          |HAZ CargaDef2;
          |SI FSalida = 0;
               aFic3 = "dsm00047";
               |HAZ CargaDef3;
               |SI FSalida = 0;
                    |HAZ NomeDat;
                    |HAZ NomeDat2;
                    |HAZ NomeDat3;
                    |ABRE #480;
                    |ABRE #84;
                    |ABRE #69;
                    |BUCLE Bagifa084;
                    |CIERRA #69;
                    |CIERRA #84;
                    |CIERRA #480;
                    |HAZ DescargaDef3;
               |FINSI;
               |HAZ DescargaDef2;
          |FINSI;
          |HAZ DescargaDef;
     |FINSI;
     ||컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
     aFic = "agifa501";
     |HAZ CargaDef;
     |SI FSalida = 0;
          aFic2 = "agifa502";
          |HAZ CargaDef2;
          |SI FSalida = 0;
               aFic3 = "dsm00051";
               |HAZ CargaDef3;
               |SI FSalida = 0;
                    |HAZ NomeDat;
                    |HAZ NomeDat2;
                    |HAZ NomeDat3;
                    |ABRE #501;
                    |ABRE #502;
                    |ABRE #503;
                    |BUCLE Bagifa501;
                    |CIERRA #501;
                    |CIERRA #502;
                    |CIERRA #503;
                    |HAZ DescargaDef3;
               |FINSI;
               |HAZ DescargaDef2;
          |FINSI;
          |HAZ DescargaDef;
     |FINSI;
     ||컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
     aFic = "dsm00056";
     |HAZ CargaDef;
     |SI FSalida = 0;
          aFic2 = "dsm00057";
          |HAZ CargaDef2;
          |SI FSalida = 0;
               |HAZ NomeDat;
               |HAZ NomeDat2;
               |ABRE #56;
               |ABRE #57;
               |BUCLE Bdsm00056;
               |CIERRA #57;
               |CIERRA #56;
               |HAZ DescargaDef2;
          |FINSI;
          |HAZ DescargaDef;
     |FINSI;
     ||컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
     aFic = "dsm00043";
     |HAZ CargaDef;
     |SI FSalida = 0;
          aFic2 = "dsm00044";
          |HAZ CargaDef2;
          |SI FSalida = 0;
               |HAZ NomeDat;
               |HAZ NomeDat2;
               |ABRE #43;
               |ABRE #44;
               |BUCLE Bdsm00043;
               |CIERRA #44;
               |CIERRA #43;
               |HAZ DescargaDef2;
          |FINSI;
          |HAZ DescargaDef;
     |FINSI;
     ||컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
     aFic = "dsm00054";
     |HAZ CargaDef;
     |SI FSalida = 0;
          aFic2 = "dsm00055";
          |HAZ CargaDef2;
          |SI FSalida = 0;
               |HAZ NomeDat;
               |HAZ NomeDat2;
               |ABRE #54;
               |ABRE #55;
               |BUCLE Bdsm00054;
               |CIERRA #55;
               |CIERRA #54;
               |HAZ DescargaDef2;
          |FINSI;
          |HAZ DescargaDef;
     |FINSI;
     ||컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
     aFic = "agifa509";
     |HAZ CargaDef;
     |SI FSalida = 0;
          |HAZ NomeDat;
          |ABRE #509;
          |BUCLE Bagifa509;
          |CIERRA #509;
          |HAZ DescargaDef;
     |FINSI;
     ||컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
     aFic = "agifa090";
     |HAZ CargaDef;
     |SI FSalida = 0;
          aFic2 = "agifa074";
          |HAZ CargaDef2;
          |SI FSalida = 0;
               aFic3 = "dsm00052";
               |HAZ CargaDef3;
               |SI FSalida = 0;
                    aFic4 = "dsm00067";
                    |HAZ CargaDef4;
                    |SI FSalida = 0;
                         |HAZ NomeDat;
                         |HAZ NomeDat2;
                         |HAZ NomeDat3;
                         |HAZ NomeDat4;
                         |ABRE #90;
                         |ABRE #74;
                         |ABRE #52;
                         |ABRE #67;
                         |BUCLE Bagifa090;
                         |CIERRA #90;
                         |CIERRA #74;
                         |CIERRA #52;
                         |CIERRA #67;
                         |HAZ DescargaDef4;
                    |FINSI;
                    |HAZ DescargaDef3;
               |FINSI;
               |HAZ DescargaDef2;
          |FINSI;
          |HAZ DescargaDef;
     |FINSI;
     ||컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
|FPRC;
컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
|PROCESO ActHisto;
     |DFICE aCentro;
     aCentro = aFic % -703;

     |ABRE #100;
     |SELECT #1#100;
     #100#0 = "I";
     #100#6 = aCentro;
     #100#1 = fFecha;
     #100#2 = 999;
     |LEE 000#100];
     |SI FSalida = 0;
          |LEE 000#100a;
     |SINO;
          |LEE 000#100u;
     |FINSI;
     |SI FSalida = 0; |Y #100#0 = "I"; |Y #100#6 = aCentro; |Y #100#1 = fFecha;
          nLin = #100#2 + 1;
     |SINO;
          nLin = 1;
     |FINSI;
     |HORA aAlfa;
     |DDEFECTO #100;
     #100#0 = "I";
     #100#1 = fFecha;
     #100#2 = nLin;
     #100#3 = aAlfa;
     #100#4 = Usuario % 810;
     #100#5 = aFic % -115;
     #100#6 = aCentro;
     |GRABA 020#100n;
     |CIERRA #100;

     |HAZ EnviaEco;
|FPRC;

|PROCESO BorraExpor;
     aFic = aPath;
     |_PDIR aFic;
     |PARA; |SI FSalida = 0; |HACIENDO;
          aAlfa = aFic % -103;
          |SI aAlfa = "ddx"; |O aAlfa = "ixx"; |O aAlfa = "dat";
               aAlfa = aFic % -1003;
               |SI aAlfa = "exp"; |FBORRA aFic; |FINSI;
          |FINSI;
          |_SDIR aFic;
     |SIGUE;
|FPRC;

|PROCESO DesComprime;
     ||---------Chequeo del fichero (debe se todo con numeros)
     ||         DETAR se cuelga si el fichero no es un tar
     nSwErr = 0;
     |QBF aFic;
     aAlfa = aFic % -115;
     |PARA i = 101; |SI i [ 1500; |HACIENDO i = i + 100;
          aAlf1 = (aAlfa % i);
          |SI aAlf1 < "0"; |O aAlf1 > "9";
               |SI i = 1201; |Y aAlf1 = ".";
               |SINO;
                    nSwErr = 1;
                    |SAL;
               |FINSI;
          |FINSI;
     |SIGUE;

     |SI nSwErr = 0;
          aAlfa = aFic % A0;
          nLon = aAlfa;
          nLon = nLon + 100 - 4;
          aOrg = aFic % nLon;
          aExt = aFic % -104;
          aGz  = aOrg + ".gz";

          |RENOMBRA_FICHERO aFic, aGz;
          |DESCOMPRIME aGz;
          |RENOMBRA_FICHERO aOrg, aFic;

          |XABRE "B", aFic, nHandle;
          |XLEE nHandle, 1, aAlfa;
          nHay = FSalida;
          |XCIERRA nHandle;

          |SI nHay > 0;
               |DETAR aFic, aPath;
               nSal = FSalida;
               |FBORRA aFic;
               |RENOMBRA_FICHERO aGz, aFic;
          |SINO;
               nSal = 1;
          |FINSI;

          |SI nSal = 0;
               nSwErr = 0;
          |SINO;
               nSwErr = 1;
               aAlfa = "0000Error en... [" + aFic + "]";
               |MENAV aAlfa;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO BuscaFichero;
     |ABRE #100;
     |SELECT #2#100;
     aFic = aPath;
     |_PDIR aFic;
     |PARA; |SI FSalida = 0; |HACIENDO;

          ||Ahoro los paquetes son de 15 + 4 (.tar)
          ||Tendre que descomprimirlos para sacar el txt
          aAux = aFic % -104;
          |SI aAux = ".tar";
               |DETAR aFic, aPath;
               |SI FSalida = 0;
                    aFichero = aFic - ".tar";
                    aFicTxt = aFichero + ".txt";
                    nTipo = 0;
                    aFicEco = "";
                    aFecEco = "";
                    aHoraEco = "";
                    aDirEco = "";
                    |HAZ LeeTxt;
                    |SI nTipo = 60;
                         ||Paquete = q hasta ahora.
                         aAlfa = aFichero % -115;
                         |SELECT #2#100;
                         #100#0 = "I";
                         #100#5 = aAlfa;
                         |LEE 000#100=;
                         |SI FSalida ! 0;
                              #999#1 = aAlfa;
                              #999#5 = aDirEco;
                              |GRABA 020#999n;
                         |FINSI;
                    |FINSI;
                    |SI nTipo = 61;
                         ||Eco, Actualizar Historico
                         aAlfa = aFicEco;
                         |SELECT #2#100;
                         #100#0 = "E";
                         #100#5 = aAlfa;
                         |LEE 101#100=;
                         |SI FSalida = 0;
                              #100#7 = aFecEco;
                              #100#8 = aHoraEco;
                              |GRABA 020#100.a;
                              |LIBERA #100;
                         |FINSI;
                    |FINSI;
               |FINSI;
          |FINSI;
          |_SDIR aFic;
     |SIGUE;
     |CIERRA #100;
|FPRC;

|PROCESO Tipo_3; |TIPO 3;
     |SI #0#0 = "NO"; |ACABA; |FINSI;

     fFecha = ~;
     |ABRE #142; |LEE 000#142p; |CIERRA #142;
     aPath = #142#149; |QBF aPath;
     |SI aPath = "";
          |MENAV "0000Directorio no indicado...";
          |ACABA;
     |FINSI;
     aPath = aPath + "/";

     |CONFI "0000SRecibir datos desde TPV ahora: ";
     |SI FSalida = 0;
          path = aPath;
          |MENAV "0000Atencion!! Debe estar conectado a internet";
          |SUB_EJECUTA "dsm00207";
     |FINSI;

     aAlfa = Usuario; |QBF aAlfa;
     |NOME_DAT #999 aAlfa;
     |DELINDEX #999;
     |ABRE #999;
     |HAZ BuscaFichero;

     |LEE 000#999p;
     |PARA; |SI FSalida = 0; |HACIENDO;
          |HAZ BorraExpor;
          aFic = aPath + #999#1;
          |HAZ DesComprime;
          |SI nSwErr = 0;
               |HAZ ChequeaDDX;
               |SI nSwErr = 0;|O aParam = "DEFCON";
                    aDirEco = #999#5;
                    |HAZ ActHisto;
                    |HAZ Importa;
               |SINO;
                    aAlfa = "0000Anulada por diferencias de version" + &13 + aFic;
                    |MENAV aAlfa;
                    |MENAV "0000Actualice la version y ejecute Control de Indices";
               |FINSI;
          |FINSI;
          |HAZ BorraExpor;
          |LEE 000#999s;
     |SIGUE;
     |CIERRA #999;
     |DELINDEX #999;
|FPRC;

|PROGRAMA;
     |HAZ DecimalesBase;

     aParam = PARAMETRO;

     |SI aParam ! "";|Y aParam ! "DEFCON";
          #0#0 = "SI";
          |HAZ Tipo_3;
     |SINO;
          |MANTE #0;
     |FINSI;
|FPRO;

|PROCESO LeeTxt;
     |XABRE "U", aFicTxt, nHandle1;
     |SI FSalida < 0;
          aMensaje = "0000Error al leer el fichero .txt: " + aFicTxt;
          |MENAV aMensaje;
     |FINSI;

     |PARA nConta = 1; |SI nConta [ 22; |HACIENDO nConta = nConta + 1;
          |XLEE nHandle1, 250, aAlfa;
          |QBF aAlfa;
          |SI nConta = 1;     nTipo = aAlfa ;       |FINSI;
          |SI nConta = 2;     aFecEco = aAlfa ;     |FINSI;
          |SI nConta = 3;     aHoraEco = aAlfa ;    |FINSI;
          |SI nConta = 17;    aFicEco = aAlfa ;     |FINSI;
          |SI nConta = 20;
               eaAlfaDir = aAlfa;
               |HAZ ArreglaDir;
               aDirEco = eaDirecOk ;
          |FINSI;
     |SIGUE;
     |XCIERRA nHandle1;
|FPRC;


|PROCESO EnviaEco;
     aTipo = "51";
     aFecha = fFecha;
     |HORA aHora;
     aVarHora = aHora % 105;
     |ABRE #dsm00200;
     |LEE 000#dsm00200.p;
     |SI FSalida ! 0;
          |ACABA;
     |FINSI;
     |CIERRA #dsm00200;
     |HAZ Txt;
|FPRC;

|PROCESO Txt;
     aOrigen = #dsm00200#6;
     |QBF aOrigen;
     aDirOri = aDirEco;
     |QBF aDirOri;
     aNombre = #iber0023#5;
     |QBF aNombre;

     aNomTxt = aNombre + ".txt";

     aPathEco = aPath + "eco/";
     |MKDIR aPathEco;

     |HAZ SacaVersion;
     aFicTxt = aPathEco + aNombre + ".txt";
     |XABRE "W", aFicTxt, nHandle;
     |SI FSalida < 0;
          aMenLog = "    No se pudo crear [" + aFicTxt + "].";
          nErrLog = 1;
          |HAZ Log;
     |SINO;
          aAlfa = aTipo;  |XGRABA nHandle, aAlfa;
          aAlfa = aFecha; |XGRABA nHandle, aAlfa;
          aAlfa = aVarHora;  |XGRABA nHandle, aAlfa;
          aAlfa = "ECO." + aNombre;
          |XGRABA nHandle, aAlfa;
          |PARA nCampo = 1; |SI nCampo [ 12; |HACIENDO nCampo = nCampo + 1;
               aAlfa = "";         || Des + Obs
               |XGRABA nHandle, aAlfa;
          |SIGUE;
          aAlfa = aNombre; |XGRABA nHandle, aAlfa;
          aAlfa = 0;  |XGRABA nHandle, aAlfa;
          aConta = aNombre % -103;
          |QBF aConta;
          |XGRABA nHandle, aConta;
          |XGRABA nHandle, aOrigen;
          |XGRABA nHandle, aDVers;
          aAlfa = aDirOri; |XGRABA nHandle, aAlfa;
          |XCIERRA nHandle;

          aFicTar = aPathEco + aNombre + ".tar";
          aAlfa = aFicTar + " " + aNomTxt;
          |ATAR aAlfa, aPathEco;
          |SI FSalida = 0;
               enSwCentral = 1;
               eaFicEnvio = aFicTar;
               eaDestino = aDirEco;
               aPrograma = "iber0094";
               ||ENVIA EL ECO.
               |SUB_EJECUTA aPrograma;
          |FINSI;
     |FINSI;
     ||Falta unir el paquete con el txt, creando el paquete bueno.(*.tar)
|FPRC;

|PROCESO Log;
     aMensaje = aMenLog;
     |MENAV aMensaje;
|FPRC;

|PROCESO SacaVersion;
     aDVers = "00.00.000";
     |DBASE aDBase;
     aDMenu = aDBase + "/" + aDNMen;
     |XABRE "U", aDMenu, nDHand;
     |SI FSalida ] 0;
          |XLEE nDHand, 250, aDAlfa;
          aDVers = (aDAlfa % -105) + "." + (aDAlfa % 403);
          |XCIERRA nDHand;
     |FINSI;
|FPRC;
