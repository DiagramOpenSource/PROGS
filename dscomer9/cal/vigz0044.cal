|FICHEROS;
     vigz0044 #0;
     agifa065 #65;
     agifa019 #19;
     tempo600 #10;  || Tmp
     agifa007 #7;
|FIN;

|VARIABLES;
     nUni = 0;
     aArt = "";
     nFactor = 0;
     nVal = 0;
     nTotal = 0;
     nPcm = 0;
     aMil = "";
     aAlfa = "";
|FIN;

|PROCESO CalPcm;
     |SI #65#2 = "FB"; |O #65#2 = "SA";
          #10#0 = #65#0;
          |LEE 000#10=;
          |SI FSalida = 0; |ACABA; |FINSI;   ||Tiene Compra

          #19#0 = #65#0;
          |LEE 101#19=;
          |SI FSalida = 0;
               aMil = #19#110; |QBF aMil; aMil = aMil > "";
               |SI aMil = "S";
                    nFactor = 1000;
               |SINO;
                    nFactor = 1;
               |FINSI;
               nUni = (#65#6/nFactor);

               |SI #65#0 ! aArt;
                    aArt = #65#0;
                    nVal = 0; nTotal = 0;
               |FINSI;

               nVal = nVal + (nUni * #65#9);
               nTotal = nTotal + nUni;
               nPcm = nVal / nTotal;

               #19#9 = nPcm;

               |GRABA 020#19a;
               |LIBERA #19;
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE CalPcm;
     #65#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, CalPcm;
|FIN;

|PROCESO CargaCompra;
     #7#26 = #65#11;
     |LEE 000#7=;
     |SI #65#2 = "AC"; |Y #7#37 = "N";  ||Serie NO interna
          #10#0 = #65#0;
          |LEE 000#10=;
          |SI FSalida ! 0; |GRABA 020#10n; |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE CargaCompra;
     #65#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, CargaCompra;
|FIN;

|PROCESO Tipo3; |TIPO 3;
     |SI #0#0 = "SI";
          aAlfa = Usuario;
          |NOME_DAT #10 aAlfa;
          |DELINDEX #10;

          |ABRE #10;

          |ABRE #7;
          |BUCLE CargaCompra;
          |CIERRA #7;

          |ABRE #19;
          |BUCLE CalPcm;
          |CIERRA #19;

          |CIERRA #10;

          |DELINDEX #10;
     |FINSI;
|FPRC;
