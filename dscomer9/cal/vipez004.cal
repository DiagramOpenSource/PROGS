|FICHEROS;
     vipez004 #0;
     vipem017 #917;

     agifa010 #10;
     agifa019 #19;
     agifa071 #71;
     agifa024 #24;
     agifa025 #25;
     agifa059 #59;
     agifa071 #71;
     agifa134 #134;
     agifa142 #142;
     agifa324 #324;
     agis0002 #702;
     agifa704 #704;
     agifa716 #716;
     agifa722 #722;
     dsm00133 #124;
     dsm00289 #289;
     dscamrec@ #600;     ||Fichero puente cartera

     ref1z002@ #997;
     ref2z002@ #998;
     referen@  #999;

     vipez013  #513,MANTE;
|FIN;

|VARIABLES;
     aMarca = "";
     nNumContrato = 0
     nA¤oContrato = 0;
     nMesContrato = 0;
     aSerFactura = "";
     nNumFactura = 0;

     aDSerie = "";
     aHSerie = "";
     nDNumFac = 0;
     nHNumFac = 0;
     aSerAlbaran = "";
     nNumAlbaran = 0;

     nSwGridCargado = 0;
     aSQL = "";
     aFichSQL = "";
     nBotonLimpia = 0;

     nSwFiltrado = 0;
     nSalida = 0;
     nVentana = 0;
     nPantaFil = 0;
     aUsuario = "";

|| Del borrado

     &PRMNT_enError = 0;
     &enSwNoAcotaFecha = 0;
     &enSwEsAutoFac    = 0;
     &enSwAutoFactura  = 0;
     &qSerie       = "";
     &qAlbar       = 0;
     &qFecha       = @;
     &qAbono       = "";
     &qSufac       = "";
     &eaCalcAF = 0;
     &eaSerProforma = "";
     &enNumProforma = 0;
     &eaSerie116 = "";
     &enFactura116 = 0;
     &efFecha      = @;
     &enMrgKit     = 0;
     &enImpMar     = 0;
     &enImpKit     = 0;
     &enImpRepCom  = 0;
     &enImpRepVta  = 0;
     &enImpRepRete = 0;
     &enImpRepComi = 0;
     &enImpCliPen  = 0;
     &enImpCliCom  = 0;
     &enImpCliDto  = 0;
     &enImpCliMar  = 0;
     &enImpCliFac  = 0;
     &eaCliente    = "";
     &eaRepre      = "";
     &eaModo116 = "";
     &eaTipo116 = "";
     &eaTipoMov = "";
     &eaCli     = "";
     &eaCuenta  = "";
     &eaNombre  = "";
     &eaCif     = "";
     &eaImprime = "";
     &eaImpre   = "";
     &eaSERFAC = "";
     &enNUMFAC = 0;
     &eaSEREXP = "";
     &enNUMEXP = 0;
     &ser_alb  = "";
     &num_alb  = 0;
     &abo_alb  = "";
     &abono    = "";   || Lo utilizo en agifa014
     &fec_alb = @;
     &eaArticulo = "";
     &enImpVta   = 0;
     &enDFuera   = 0;
     &enImporte  = 0;
     &enSwHay    = 0;
     &eaInforme  = "";

     aDescPro  = "";
     nImpo  = 0;
     nDto   = 0;
     FEstado = 0;
     aSerie  = "";
     nContador = 0;
     Comodin1 = "";
     margen = 0;
     nLinMulti    = 1;
     nLinFac      = 0;
     aCodMayo = "";

     nSwBorradoOK = 0;
     imneto  = 0;
     mes     = 0;
     fmes    = " ";
     con     = 0;
     nForma  = 0;
     nModo   = 0;
     aAlfa1  = "";
     fFechaAnt = @;
     aContab = "";
     nSwFlag = 0;
     nSw     = 0;
     &eaProg = "";
     &enErrCarte = 0;
     nSwErr  = 0;
     nSal    = 0;
     nSwHay  = 0;
     primera = 0;
     nInterno = 0;

     nUnitsLCmp = 0;
     nTotalCoste = 0;
     nTotalCosteSI = 0;
     nCalc   = 0;
     nCalc1  = 0;
     aCtaCli = "";
     &eaCta    = "";
     nSwPax = 0;
     nPax = 0;
     aCP = "";
     ||Variables de la cartera
     aPath = ""; aDef00 = ""; aMensa = "";
     Comodin = ""; aSubProg = ":dscarter/dsrecibo";
     aTempo600 = "";
     &aCPath = ""; &aCNome = "";
     &nDeci_PreBaseMx = 0;
     &nDeci_IVA = 0;
     &nDeci_IVAC = 0;
     &nDeci_Div = 0;
     &enErr   = 0;
     &TipoCta = "";
     Divisa = ""; CambioBase = 0; CambioDiv = 0;
     &SPedido = "";
     &NPedido = 0;
     nFlag     = 0;
     nSwError = 0;
     &eaTag = "";

     nSwIva = 0;
     nMayorista = 0;
     nMayoCli = 0;
     &aMayoAnt = "";
     &nPrimero = 0;

     &nAlbVta = 0;
     aArtLinCom = "";
     nImpLinCom = 0;
     &aArtIva = "";
     &nImpSal = 0;
     &nImpEnt = 0;
     &nPre = 0;

     nLineaCom = 0;
     aDirEmp = "";
     aDatos = "";
     &enSwMenu = 0;
     nAlbCompra = 0;
     &aSerMayo = "";
     &cCodMonTrab = "";

     &nDeci_PreDiv = 0;
     nPPort1   = 0;
     nPPort2   = 0;
     &eaDocu   = "";

     &aArtLin = "";
     &nUniLin = 0;
     &nPreLin = 0;

     nImpDiv = 0;
     nMult = 0;
     &enForma = 1;

     aMensaje = "";

     nSwHayCompras = 0;
     nLinea = 0;

     nGastos = 0;
     nTasas = 0;
     nSFcon = 0;
     nSFsin = 0;

     aTempo703 = "";
     &Tempo = "";

     &EURODB  = 0;

     &aDivisa = "";
     &aMoneda = "";
     &eaProgVta = "";

|| Mias

     nPanta     = 0;
     nBoton1    = 0;
     nBoton2    = 0;
     nBoton3    = 0;
     nBoton4    = 0;
     nBoton5    = 0;
     nBoton6    = 0;
     nRango     = 0;
     nGrid1     = 0;
     nGrid2     = 0;
     nHandle    = 0;
     nDContrato = 0;
     nHContrato = 0;
     nDMes      = 0;
     nHMes      = 0;
     nDAnyo     = 0;
     nHAnyo     = 0;

     aFichero   = "";
     aFichero1  = "";
     aFichero2  = "";
     aNFicher   = "";
     aBase      = "";
     aMas       = "";
     aEsc1      = "";
     aEsc2      = "";
     aRetu      = "";
     aTecla     = "";
     aPatrones  = "";
     aDir       = "";
     aAlfa      = "";
     aFich      = "";
     aQueQuiero = "";
     aAbre      = "";
     aDFichero  = "";
     aHFichero  = "";
     aOrigen    = "";
     aDestino   = "";
|FIN;

|PROCESO Baja997;
     #997#0 = 0;
     #997#1 = 0;
     #997#2 = 1;
|FPRC;

|PROCESO Alta997;
     #997#0 = 99999999;
     #997#1 = 9999;
     #997#2 = 12;
|FPRC;
|FPRC;

|PROCESO Baja998;
     #998#0 = 0;
     #998#1 = 0;
     #998#2 = 1;
|FPRC;

|PROCESO Alta998;
     #998#0 = 99999999;
     #998#1 = 9999;
     #998#2 = 12;
|FPRC;

|PROCESO vipem017_marca;
     #997#11 = aMarca;
     |GRABA 020#997.a;
|FPRC;

|DEFBUCLE vipem017_marca;
 #997#2002;
 ;
 aSerFactura, nNumFactura;
 aSerFactura, nNumFactura;
 be;
 NULL, vipem017_marca;
|FIN;


|PROCESO Evento997;
     |SI FSalida = 1;  |O FSalida = 2;
         |LEE 000#997c;
     |FINSI;

     |SI FSalida = 4;
          |LEE 000#997c;
          |SI FSalida ! 0; |ACABA; |FINSI;

          aSerAlbaran = #997#4;
          nNumAlbaran = #997#5;
          nNumContrato = #997#0;
          nA¤oContrato = #997#1;
          nMesContrato = #997#2;
          aSerFactura = #997#7;
          nNumFactura = #997#8;
          |SI #997#11 = "*";
               aMarca = "";
          |SINO;
               aMarca = "*";
          |FINSI;

          |BUCLE vipem017_marca;
          |SELECT #3#997;
          #997#0 = nNumContrato;
          #997#1 = nA¤oContrato;
          #997#2 = nMesContrato;
          |LEE 000#997.=;
          |PARA; |SI; |HACIENDO;
               |SI FSalida = 0; |Y #997#0 = nNumContrato; |Y #997#1 = nA¤oContrato; |Y #997#2 = nMesContrato;
                    |SI aSerAlbaran = #997#4; |Y nNumAlbaran = #997#5;
                         |SAL;
                         ||OKIE. LO HEMOS ENCONTRADO. ES EN EL QUE ESTABAMOS
                    |SINO;
                         |LEE 000#997.s;
                    |FINSI;
               |SINO;
                    |SAL;
               |FINSI;
          |SIGUE;

          |REFRESCACONTROL nGrid1;
     |FINSI;
|FPRC;

|PROCESO Evento998;
     |SI FSalida = 1;  |O FSalida = 2;
         |LEE 000#998=;
     |FINSI;
|FPRC;

|PROCESO HallaMoneda;
     |SI TipoCta = "A";
          |DDEFECTO #10;
          |ABRE #10;
          #10#52 = SPedido;
          #10#0  = NPedido;
          |LEE 000#10=;
          |CIERRA #10;
          Divisa  = #10#68;
          CambioBase = #10#69;
          CambioDiv = #10#74;
     |FINSI;
|FPRC;

|PROCESO ActCartera;          ||Esta cogido del agis0002
     |DDEFECTO #24;
     #24#0 = #agis0002#6;
     |LEE 000#24=;

     |DBASS aAlfa;
     aAlfa = aAlfa + "dscarter/fich/dscam000.dat";
     |XABRE "E", aAlfa, nHandle;
     |SI FSalida = 0;
        |DBASS aPath;
        aPath = aPath + "dscarter/";
        aDef00 = aPath + "def/dscamrec";

        |CARGA_DEF aDef00, dscamrec@;
        |SI FSalida ! 0;
           aMensa = "0000Error al cargar:" + aDef00;
           |MENAV aMensa;
           aDef00 = "";
        |SINO;
           |HAZ CreaTempo; aTempo600 = Tempo;
           |PATH_DAT #600 aPath; |NOME_DAT #600 aTempo600; |DELINDEX #600;
           |ABRE #600;
           aCPath = aPath; aCNome = aTempo600;
        |FINSI;

        |HAZ HallaMoneda;

        |DFICE Comodin;
        Comodin = Comodin % -106;
        Comodin = Comodin % 0105;

        |ABRE #agifa324;
        #agifa324#0 = Divisa;
        |LEE 020#agifa324.=;
        |CIERRA #agifa324;
        nDeci_Div = #agifa324#7;

        |DDEFECTO #dscamrec@;
        #600#1  = #agis0002#20;||Serie
        #600#2  = #agis0002#17;||Numero
        #600#3  = #agis0002#18;||N§ Rec
        #600#4  = #agis0002#8; ||F.V
        #600#5  = #agis0002#7; ||F.E
        #600#6  = #agis0002#12;||Cuenta Cli
        #600#7  = #agis0002#3; ||Nom Cli
        #600#8  = #agis0002#4; ||Nom Banco
        #600#9  = #agifa024#32;      ||Entidad
        #600#10 = #agifa024#33;      ||Sucursal
        #600#11 = #agifa024#31;      ||DC
        #600#12 = #agifa024#30;      ||Cuenta
        #600#13 = #agis0002#22;||Tipo Rec
        #600#15 = #agis0002#9; ||Importe
        #600#16 = #agis0002#15;||A aceptar
        #600#17 = #agis0002#6; ||Aceptado
        #600#18 = #agis0002#2; ||Cliente
        #600#21 = #agifa024#3; ||Zona
        #600#22 = 1;           ||Documento
        #600#25 = Comodin;       ||Empresa ges
        #600#27 = "V";         ||Tipo
        |SI nFlag > 0;
           #600#28 = "A";         ||Ope
        |SINO;
           #600#28 = "B";
        |FINSI;
        #600#29 = #agifa324#0;      ||Divisa
        #600#30 = #agifa324#1;      ||Nombre
        #600#31 = CambioDiv;        ||Cambio Divisa
        #600#32 = CambioBase;       ||Cambio Base
        #600#33 = #agifa024#203;
        #600#34 = 0;
        #600#35 = TipoCta;
        #600#36 = #agis0002#28;
        #600#38 = #agis0002#16;
        #600#39 = #agis0002#10;
        aAlfa = "|NC"; aAlfa = #600#aAlfa#;
        |SI aAlfa > 48;
             #600#48 = #agifa024#215;
             #600#49 = #agifa024#216;
        |FINSI;
        |SI aAlfa > 50;
             #600#50 = #agifa024#217;
             |ABRE #289;
             #289#0 = #agifa024#0;
             |LEE 000#289=;
             |SI FSalida ! 0; |DDEFECTO #289; |FINSI;
             |CIERRA #289;
             #600#51 = #289#1;
        |FINSI;

        |GRABA 020#600n;

        |SUB_EJECUTA_NP aSubProg;

        #600#22 = 1;           ||Documento
        |LEE 000#600=;
        |SI FSalida ! 0; |O #600#37 = "ERROR               ";
           nSwError = 1;
           |MENAV "    La entrega tiene movimientos en Cartera.";
           |SI aDef00 ! "";
              |CIERRA #600; |DELINDEX #600; |DESCARGA_DEF #dscamrec@;
           |FINSI;
           |ACABA;
        |FINSI;

        |SI aDef00 ! "";
             |CIERRA #600;
             |DELINDEX #600;
             |DESCARGA_DEF #dscamrec@;
        |FINSI;

        |SI TipoCta = "P";
           eaTag = "RAP";
        |SINO;
           eaTag = "RAA";
        |FINSI;
     |FINSI;
|FPRC;

|PROCESO Asiento;
     nModo = 3;

     aAlfa1 = #agifa134#1; |QBF aAlfa1;
     aAlfa1 = aAlfa1 % -104;
     nSwHay = 0;

     |ABRE #agifa722;
     #agifa722#0 = "FV";
     #agifa722#1 = #agifa134#49;
     #agifa722#2 = #agifa134#0;
     #agifa722#11 = 1;
     |LEE 000#agifa722.];

     FEstado = FSalida;
     aAlfa   = #agifa722#1;
     aAlfa   = aAlfa % 0105;
     nCalc   = #agifa722#2;
     FSalida = FEstado;


     |PARA; |SI FSalida = 0; |Y #agifa722#0 = "FV";
            |Y aAlfa = #agifa134#49;  |Y nCalc = #agifa134#0;  |HACIENDO;

            aAlfa = #agifa722#3; |QBF aAlfa;
            |SI aAlfa = aAlfa1; nSwHay = 1; |SAL; |FINSI;
            |LEE 000#agifa722.s;

            FEstado = FSalida;
            aAlfa   = #agifa722#1;
            aAlfa   = aAlfa % 0105;
            nCalc   = #agifa722#2;
            FSalida = FEstado;
     |SIGUE;
     |CIERRA #agifa722;

     aAlfa1 = "N";
     |SI nSwHay = 1;
         aAlfa = "ac" + Usuario; |NOME_DAT #agifa716#aAlfa#;
         |DELINDEX #agifa716;
          |ABRE #agifa716;

          |ABRE #agifa704;
          #agifa704#0 = #agifa722#10;
          |LEE 000#agifa704.=;
          |SI FSalida = 0;
              |SI #agifa704#8 = "S";
                  |DDEFECTO #agifa716;
                  #agifa716#15 = #agifa722#10;  #agifa716#0  = 1;
                  #agifa716#13 = "agifa071";    #agifa716#14 = 30;
                  #agifa716#11 = "A";           #agifa716#12 = 5;
                  #agifa716#3  = #agifa134#49;  #agifa716#4  = #agifa134#49;
                  |GRABA 020#agifa716.n;

                  |DDEFECTO #agifa716;
                  #agifa716#15 = #agifa722#10;  #agifa716#0  = 2;
                  #agifa716#13 = "agifa071";    #agifa716#14 = 26;
                  #agifa716#11 = "N";           #agifa716#12 = 6;
                  #agifa716#5  = #agifa134#0;   #agifa716#6  = #agifa134#0;
                  |GRABA 020#agifa716.n;
                  |LIBERA #agifa716; |CIERRA #agifa716;
             |SINO;
                  |DDEFECTO #agifa716;
                  #agifa716#15 = #agifa722#10;  #agifa716#0  = 1;
                  #agifa716#13 = "agifa134";    #agifa716#14 = 49;
                  #agifa716#11 = "A";           #agifa716#12 = 5;
                  #agifa716#3  = #agifa134#49;  #agifa716#4  = #agifa134#49;
                  |GRABA 020#agifa716.n;

                  |DDEFECTO #agifa716;
                  #agifa716#15 = #agifa722#10;  #agifa716#0  = 2;
                  #agifa716#13 = "agifa134";    #agifa716#14 = 0;
                  #agifa716#11 = "N";           #agifa716#12 = 6;
                  #agifa716#5  = #agifa134#0;   #agifa716#6  = #agifa134#0;
                  |GRABA 020#agifa716.n;
                  |LIBERA #agifa716; |CIERRA #agifa716;
             |FINSI;
          |FINSI;
          |CIERRA #agifa704;

          aSerie = #agifa134#49;
          nContador = #agifa134#0;
          |GRABA 020#agifa134.a;
          |LIBERA #agifa134;
          |CIERRA #agifa134;

          Comodin1 = #agifa134#1; |QBF Comodin1;
          Comodin1 = Comodin1 % -104;
          Comodin  = #agifa722#10; |QBF Comodin;

          Comodin  = "agifa711.tab;" + Comodin + ",B*" + Comodin1;
          aAlfa1   = "N";
          |SUB_EJECUTA_NP Comodin;

          |ABRE #agifa134;
          #agifa134#49 = aSerie; #agifa134#0 = nContador;
          |LEE 101#agifa134.=;
     |FINSI;

     |SI PRMNT_enError = 0;
         #agifa134#48 = aAlfa1; |GRABA 020#agifa134.a;
     |FINSI;

     nSw = PRMNT_enError;
|FPRC;

|PROCESO BorraRec;
     eaProg = "agifa134";
     |HAZ MiraRecVenta;
     |SI enErrCarte ! 0;
          aAlfa = "0000Recibos con movimientos, NO SE DARA DE BAJA... [" + enErrCarte + "]";
          |MENAV aAlfa; nSwErr = 1; |ACABA;
     |FINSI;
     eaProg = "agifa134";
     |HAZ BorraRecVenta;
|FPRC;

|PROCESO PermiteConta;
     |AVISO;
     |SI #142#216 = "N";
          |MENAV "0000Esta Factura Esta Contabilizada...";
          FSalida = 1;
     |SINO;
          |SI #142#216 = "R";
               aAlfa = Usuario % 810;
               |ABRE #124;
               #124#0 = aAlfa;
               |LEE 000#124=;
               |SI FSalida = 0;
                    |CONFI "0000NEsta Factura esta Contabilizada: Desea Continuar? ";
               |SINO;
                    |MENAV "0000Esta Factura esta Contabilizada...";
               |FINSI;
               aAlfa = FSalida;
               |CIERRA #124;
               FSalida = aAlfa;
          |SINO; || = "T"
               |CONFI "0000NEsta factura esta contabilizada: Desea continuar? ";
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO BorraHisEnlace;
     aAlfa1 = #agifa134#1; |QBF aAlfa1; aAlfa1 = aAlfa1 % -104; nSw = 0;
     |ABRE #agifa722;
     #agifa722#0 = "FV";
     #agifa722#1 = #agifa134#49;
     #agifa722#2 = #agifa134#0;
     #agifa722#11 = 1;
     |LEE 000#agifa722.];
     FEstado = FSalida; aAlfa  = #agifa722#1; aAlfa = aAlfa % 0105; nCalc = #agifa722#2; FSalida = FEstado;
     |PARA; |SI FSalida = 0; |Y #agifa722#0 = "FV"; |Y aAlfa = #agifa134#49;
      |Y nCalc = #agifa134#0; |HACIENDO;
        aAlfa = #agifa722#3; |QBF aAlfa;
        |SI aAlfa = aAlfa1; nSwHay = 1; |SAL; |FINSI;
        |LEE 000#agifa722.s;
        FEstado = FSalida; aAlfa  = #agifa722#1; aAlfa = aAlfa % 0105; nCalc = #agifa722#2; FSalida = FEstado;
      |SIGUE;
     |SI nSwHay = 1; |BORRA 020#agifa722.a; |FINSI;
     |CIERRA #agifa722;
|FPRC;

|PROCESO BorraAdelan;
  |ABRE #702;
  |SELECT #2#702;
  FSalida = 0;
  |PARA; |SI FSalida = 0;  |HACIENDO;
       #702#2   = #agifa134#2; ||Cliente;
       #702#19  = #agifa134#49;||Serie;
       #702#0   = #agifa134#0; ||Factura;
       #702#21  = "S";  ||En FActura S/N;
       |LEE 101#702=;
       |SI FSalida = 0; |Y #702#2 = #agifa134#2;  |Y #702#19 = #agifa134#49;  |Y #702#0 = #agifa134#0;
           #702#19 = "";
           #702#0  = "";
           #702#1  = 0;
           |GRABA 000#702a;
       |SINO;
           |SAL;
       |FINSI;
       |LIBERA #702;
  |SIGUE;
  |CIERRA #702;
|FPRC;

|PROCESO ActuaCliente;
     |ABRE #24;
     #24#0 = #agifa134#2;
     |LEE 121#24=;
     |SI 0 = FSalida;
          fmes = #agifa134#1 % A402;
          mes = ((fmes - 1) * 4) + 54;
          imneto = (#agifa134#18 - #agifa134#17);
          |SI #agifa142#115 = "N";
               nImpo = (imneto * 100) / (100 - #agifa134#6);
               nDto = #agifa134#17 + (imneto - nImpo);
          |SINO;
               nImpo = imneto;
               nDto = #agifa134#17;
          |FINSI;
          #24#50 = #24#50 -. imneto;        || pendiente de facturar
          #24mes = #24mes +. nImpo;    ||imneto;
          mes = mes + 1;
          #24mes = #24mes +. nDto;     || #agifa134#17;
          mes = mes + 1;
          #24mes = #24mes +. #agifa134#46;
          #24#102 = #24#102 +. nImpo;  || imneto;
          #24#103 = #24#103 +. nDto;   || #agifa134#17;
          #24#104 = #24#104 +. #agifa134#46;
          ||*************** CALCULO DEL RIESGO;
          eaTag = "FAC";  |HAZ Riesgos;

          |SI con = 1;
               #24#45 = #agifa134#1;
               #24#46 = imneto;
          |FINSI;
          #24#110 = #24#110 +. imneto;
          |GRABA 020#24a;

          enImpCliPen = -imneto * nForma;
          enImpCliCom = nImpo * nForma;
          enImpCliDto = nDto * nForma;
          enImpCliMar = #agifa134#46 * nForma;
          enImpCliFac = imneto * nForma;
          eaCliente = #agifa134#2;
          efFecha = #agifa134#1;
          |HAZ AcumulaCli;

     |FINSI;
     |LIBERA #24;
     |CIERRA #24;
|FPRC;

|PROCESO ActuaRepre;
     |ABRE #25;
     #25#0 = #agifa134#7;
     |LEE 121#25=;
     |SI 0 = FSalida;
          imneto = #agifa134#18 - #agifa134#17;
          fmes = #agifa134#1 % A402;
          mes = ((fmes - 1) * 2) + 11;
          #25mes = #25mes +. #agifa134#8;

          mes = mes + 1;
          #25mes = #25mes +. imneto;
          #25#35 = #25#35 +. #agifa134#8;
          #25#36 = #25#36 +. imneto;
          mes = (fmes - 1) + 40;
          #25mes = #25mes +. #agifa134#55;
          #25#52 = #25#52 +. #agifa134#55;
          |GRABA 020#25a;

          enImpRepComi = #agifa134#8 * nForma;
          enImpRepVta = imneto * nForma;
          enImpRepRete = #agifa134#55 * nForma;
          eaRepre = #agifa134#7;
          efFecha = #agifa134#1;
          |HAZ AcumulaRep;
     |FINSI;
     |LIBERA #25;
     |CIERRA #25;
|FPRC;

|PROCESO kactuar;
     |LEE 121#71c;
     #19#0 = #71#2;
     |LIBERA #19;
     |LEE 101#19=;
     |SI 0 = FSalida;
          |SI #19#194 = 2;
               nImpo = ((((#71#48 * #71#6) < #71#8) < #71#33) < #10#5) < #10#32;
          |SINO;
               nImpo = ((((#71#5 * #71#6) < #71#8) < #71#33) < #10#5) < #10#32;
          |FINSI;
          |SI #142#115 = "S";
               nImpo = nImpo < #10#7;
          |FINSI;
          margen = #71#14;             ||Coste albaran !!
          |SI #134#57 = "S";
               nImpo = -nImpo;
               margen = nImpo + margen;
          |SINO;
               margen = nImpo - margen;
          |FINSI;
          #10#37 = #10#37 + (margen * nForma);

          fmes = #134#1 % A402;
          mes = ((fmes - 1) * 5) + 35;

          #19mes = #19mes + (nImpo * nForma);      ||       imneto;
          mes = mes + 1;
          #19mes = #19mes + (margen * nForma);
          #19#95 = #19#95 + (nImpo * nForma);       ||    imneto;
          #19#96 = #19#96 + (margen * nForma);
          mes = (FEntrada / 100) ! 0;
          |SI 1 = mes;
               #19#24 = #134#1;
               #19#25 = #71#5;
          |FINSI;
          |GRABA 020#19a;

          efFecha = #134#1;
          eaArticulo = #71#2;
          enImpVta = (nImpo * nForma);
          enImpMar = (margen * nForma);
          |HAZ AcumulaArt;

          enForma = nForma;
          enImpKit = nImpo;
          enMrgKit = margen;
          |HAZ ImpArtKit;    || Acumulados Articulos con Kit
     |FINSI;
     |LIBERA #19;
     |SI nForma > 0;
          #71#26 = #134#0;           || pone numero factura en linea
          #71#30 = #134#49;             ||Serie
          |GRABA 021#71a; |LIBERA #71;
     |SINO;
          #71#26 = "           ";
          #71#30 = "  ";
          |BORRA 020#71a; |LIBERA #71;
     |FINSI;
|FPRC;

|PROCESO agifa059_borra;
     nForma = -1;

     |ABRE #10;
     #10#52 = #59#15; ||Serie
     #10#0  = #59#2;
     |LEE 101#10=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     #134#63 = #134#63 +. 1;  || Numero de lineas existentes
     |SI #134#63 = 1;       || Si hay solo una linea ...
         #134#58 = #59#18;     || ... cojo la divisa ...
         #134#60 = #59#20;     || ... y la moneda de trabajo.
     |FINSI;

     |SI primera = 0; primera = 1; |FINSI;

     #10#38 = "N";
     |SI #10#58 = "R"; |O #10#58 = "E";
         #10#39 = 999999;
     |SINO;
         #10#39 = 0;
     |FINSI;
     #10#53 = "     ";          ||Serie
     #10#10 = " ";
     #10#60 = "01.01.0001";
     #10#37 = 0;

     |BUCLELIN 0kactuar#10;

     |BORRA 020#10a;
     |LIBERA #10;
     |CIERRA #10;

     |BORRA 020#agifa059.a;
|FPRC;

|DEFBUCLE agifa059_borra;
     #agifa059#1;
     ;
     #agifa134#49, #agifa134#0,   1;
     #agifa134#49, #agifa134#0, 999;
     be;
     NULL, agifa059_borra;
|FIN;

|PROCESO BorraLinFac;
     |BUCLE agifa059_borra;
|FPRC;

|PROCESO agis0002_borra;
     SPedido = #agis0002#20;
     NPedido = #agis0002#17;

     nFlag = -1;
     TipoCta = "A";
     |HAZ ActCartera;

     |BORRA 020#agis0002.a;
|FPRC;

|DEFBUCLE agis0002_borra;
     #agis0002#1;
     ;
     "A", #agifa010#52, #agifa010#0,  1;
     "A", #agifa010#52, #agifa010#0, 99;
     ;
     NULL, agis0002_borra;
|FIN;

|PROCESO agifa071_borra;
     enForma = -1;
     |HAZ AcumulaAV;

     |BORRA 020#agifa071.a;
|FPRC;

|DEFBUCLE agifa071_borra;
     #agifa071#1;
     ;
     #agifa010#52, #agifa010#0,   1;
     #agifa010#52, #agifa010#0, 999;
     be;
     NULL, agifa071_borra;
|FIN;

|PROCESO BorraAlbaranes;
     |ABRE #agifa010;
     #agifa010#52 = #998#4;
     #agifa010#0  = #998#5;
     |LEE 101#agifa010.=;
     |SI FSalida = 0;
          |BUCLE agis0002_borra;
          |BUCLE agifa071_borra;
          |BORRA 020#agifa010.a;
          |LIBERA #agifa010;
     |FINSI;
     |CIERRA #agifa010;
|FPRC;

|PROCESO BorraFactura;
     nSwBorradoOK = 0;

     eaCli        = #agifa134#2;
     |HAZ ControlUsuFV;
     |SI enErr ! 0;  |ACABA;  |FINSI;

     fFechaAnt = ~;

     |SI #agifa134#48 = "S";                   ||Contabilizada.
         |HAZ PermiteConta;

         aContab   = "N";
         fFechaAnt = #agifa134#1;
         |SI FSalida ! 0;
             |ACABA;
         |SINO;
             nSw     = 1;
             aContab = "S";
             |HAZ Asiento;
             |SI nSw < 0;   |ACABA;  |FINSI;

              #agifa134#48 = "N";
         |FINSI;
      |FINSI;

     |SI #agifa134#45 = "S";
         eaProg = "agifa134";
         |HAZ MiraRecVenta;
         |SI enErrCarte ! 0;
             aAlfa = "0000Recibos con movimientos, NO SE DARA DE BAJA... [" + enErrCarte + "]";
             |MENAV aAlfa;
             |ACABA;
         |FINSI;

         |HAZ BorraRec;
         |SI nSwErr ! 0;  |ACABA;  |FINSI;

         |HAZ BorraVenciVta;

          #agifa134#45 = "N";
     |FINSI;

     |HAZ BorraHisEnlace;
     |HAZ BorraAdelan;
     |HAZ ActuaCliente;
     |HAZ ActuaRepre;
     |HAZ BorraLinFac;

     |BORRA 020#agifa134.a;
     |SI FSalida = 0;
          |HAZ BorraAlbaranes;

          nSwBorradoOK = 1;
     |FINSI;
|FPRC;

|PROCESO BorraFGeneral;
     |ABRE #agifa134;
     #agifa134#49 = #998#7;
     #agifa134#0  = #998#8;
     |LEE 101#agifa134.=;
     |SI FSalida = 0;
         |HAZ BorraFactura;
         |LIBERA #agifa134;
     |FINSI;
     |CIERRA #agifa134;
|FPRC;

|PROCESO Borrado;
     |SALVA_DATOS #998;

     |ABRE #10;
     |ABRE #71;

     |HAZ BorraFGeneral;

     |CIERRA #10;
     |CIERRA #71;

     |SI nSwBorradoOK = 1;
         |ABRE #917;
         |SELECT #3#917;
         #917#0 = #998#0;
         #917#1 = #998#1;
         #917#2 = #998#2;
         |LEE 101#917=;
         |SI FSalida = 0;
             |BORRA 020#917a;
             |LIBERA #917;
         |FINSI;
         |CIERRA #917;

         |BORRA 020#998a;
         |LIBERA #998;
     |FINSI;

     |REFRESCACONTROL nGrid2;

     |REPON_DATOS #998;
     |LEE 000#998];

     |PULSATECLA;
|FPRC;

|DEFBUCLE Borrado;
     #998#1002;
     ;
     "     ", 000000;
     "zzzzz", 999999;
     be;
     NULL, Borrado;
|FIN;

|PROCESO Borrar;
     |CONFI "0000NEsta seguro de borrar las facturas seleccionadas?";
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |BUCLE Borrado;

     |REFRESCACONTROL nGrid1;
     |REFRESCACONTROL nGrid2;

     |LEE 000#998p;
     |SI FSalida = 0;
         |ESTADO_CONTROL nBoton5, "ENABLE";
     |SINO;
         |ESTADO_CONTROL nBoton5, "DISABLE";
     |FINSI;
|FPRC;

|PROCESO DeArribaAAbajo;
     |SELECT #1#998;
     #998#4 = #997#4;
     #998#5 = #997#5;
     |LEE 101#998=;
     |SI FSalida ! 0;
          #998#4 = #997#4;
          #998#5 = #997#5;
          |GRABA 020#998b;
     |FINSI;

     #998#0 = #997#0;
     #998#1 = #997#1;
     #998#2 = #997#2;
     #998#3 = #997#3;
     #998#6 = #997#6;
     #998#7 = #997#7;
     #998#8 = #997#8;
     #998#9 = #997#9;
     #998#10 = #997#10;
     #998#11 = #997#11;
     |GRABA 020#998a;
     |LIBERA #998;

     |BORRA 020#997a;
     |LIBERA #997;
|FPRC;

|DEFBUCLE DeArribaAAbajo;
     #997#2002;
     ;
     aDSerie, nDNumFac;
     aHSerie, nHNumFac;
     be;
     NULL, DeArribaAAbajo;
|FIN;

|PROCESO DeArribaAAbajoSel;
     |SI #997#11 = "*";
          |SELECT #1#998;
          #998#4 = #997#4;
          #998#5 = #997#5;
          |LEE 101#998=;
          |SI FSalida ! 0;
               #998#4 = #997#4;
               #998#5 = #997#5;
               |GRABA 020#998b;
          |FINSI;

          #998#0 = #997#0;
          #998#1 = #997#1;
          #998#2 = #997#2;
          #998#3 = #997#3;
          #998#6 = #997#6;
          #998#7 = #997#7;
          #998#8 = #997#8;
          #998#9 = #997#9;
          #998#10 = #997#10;
          #998#11 = #997#11;
          |GRABA 020#998a;
          |LIBERA #998;

          |BORRA 020#997a;
          |LIBERA #997;
     |FINSI;
|FPRC;

|DEFBUCLE DeArribaAAbajoSel;
     #997#1002;
     ;
     "     ", 000000;
     "zzzzz", 999999;
     be;
     NULL, DeArribaAAbajoSel;
|FIN;


|PROCESO DeAbajoAArriba;
     |SELECT #1#997;
     #997#4 = #998#4;
     #997#5 = #998#5;
     |LEE 101#997=;
     |SI FSalida ! 0;
         #997#4 = #998#4;
         #997#5 = #998#5;
         |GRABA 020#997b;
     |FINSI;

     #997#0 = #998#0;
     #997#1 = #998#1;
     #997#2 = #998#2;
     #997#3 = #998#3;
     #997#6 = #998#6;
     #997#7 = #998#7;
     #997#8 = #998#8;
     #997#8 = #998#8;
     #997#9 = #998#9;
     #997#10 = #998#10;
     #997#11 = #998#11;

     |GRABA 020#997a;
     |LIBERA #997;

     |BORRA 020#998a;
     |LIBERA #998;
|FPRC;

|DEFBUCLE DeAbajoAArriba;
     #998#2002;
     ;
     aDSerie, nDNumFac;
     aHSerie, nHNumFac;
     be;
     NULL, DeAbajoAArriba;
|FIN;

|PROCESO TodosAbajo;
     aDSerie = "     ";
     aHSerie = "zzzzz";
     nDNumFac = 000000;
     nHNumFac = 999999;

     nDContrato = 1;
     nHContrato = 99999999;
     nDAnyo     = 0;
     nHAnyo     = 9999;
     nDMes      = 1;
     nHMes      = 12;

     |BUCLE DeArribaAAbajo;

     |LEE 000#998p;
     |SI FSalida = 0;
         |ESTADO_CONTROL nBoton5, "ENABLE";
     |SINO;
         |ESTADO_CONTROL nBoton5, "DISABLE";
     |FINSI;

     |REFRESCACONTROL nGrid1;
     |REFRESCACONTROL nGrid2;
|FPRC;

|PROCESO TodosArriba;
     aDSerie = "     ";
     aHSerie = "zzzzz";
     nDNumFac = 000000;
     nHNumFac = 999999;

     nDContrato = 1;
     nHContrato = 99999999;
     nDAnyo     = 0;
     nHAnyo     = 9999;
     nDMes      = 1;
     nHMes      = 12;
     |BUCLE DeAbajoAArriba;

     |LEE 000#998p;
     |SI FSalida = 0;
         |ESTADO_CONTROL nBoton5, "ENABLE";
     |SINO;
         |ESTADO_CONTROL nBoton5, "DISABLE";
     |FINSI;

     |LEE 000#997p;

     |REFRESCACONTROL nGrid1;
     |REFRESCACONTROL nGrid2;
|FPRC;

|PROCESO RestoMismaFactura;
     |HAZ DeArribaAAbajo;
|FPRC;

|DEFBUCLE RestoMismaFactura;
     #997#1002;
     ;
     aSerAlbaran, nNumAlbaran;
     aSerAlbaran, nNumAlbaran;
     be;
     NULL, RestoMismaFactura;
|FIN;

|PROCESO FacturaAbajo;
     aDSerie = #997#7;
     aHSerie = #997#7;
     nDNumFac = #997#8;
     nHNumFac = #997#8;
     aSerAlbaran = #997#4;
     nNumAlbaran = #997#5;

     nDContrato = #997#0;
     nHContrato = #997#0;
     nDAnyo     = #997#1;
     nHAnyo     = #997#1;
     nDMes      = #997#2;
     nHMes      = #997#2;

     |BUCLE DeArribaAAbajo;

     |BUCLE RestoMismaFactura;

     ||ARA5

     |SELECT #1#998;
     |LEE 000#998p;
     |SI FSalida = 0;
         |ESTADO_CONTROL nBoton5, "ENABLE";
     |SINO;
         |ESTADO_CONTROL nBoton5, "DISABLE";
     |FINSI;

     |SELECT #1#997;
     #997#4 = aSerAlbaran;
     #997#5 = nNumAlbaran;
     |LEE 000#997];

     |SELECT #1#998;
     #998#4 = aSerAlbaran;
     #998#5 = nNumAlbaran;
     |LEE 000#998];

     |REFRESCACONTROL nGrid1;
     |REFRESCACONTROL nGrid2;
|FPRC;

|PROCESO FacturaArriba;
     aDSerie = #998#7;
     aHSerie = #998#7;
     nDNumFac = #998#8;
     nHNumFac = #998#8;
     aSerAlbaran = #998#4;
     nNumAlbaran = #998#5;

     nDContrato = #998#0;
     nHContrato = #998#0;
     nDAnyo     = #998#1;
     nHAnyo     = #998#1;
     nDMes      = #998#2;
     nHMes      = #998#2;
     |BUCLE DeAbajoAArriba;

     |LEE 000#998p;
     |SI FSalida = 0;
         |ESTADO_CONTROL nBoton5, "ENABLE";
     |SINO;
         |ESTADO_CONTROL nBoton5, "DISABLE";
     |FINSI;

     |SELECT #1#997;
     #997#4 = aSerAlbaran;
     #997#5 = nNumAlbaran;
     |LEE 000#997];

     |SELECT #1#998;
     #998#4 = aSerAlbaran;
     #998#5 = nNumAlbaran;
     |LEE 000#998];

     |REFRESCACONTROL nGrid1;
     |REFRESCACONTROL nGrid2;
|FPRC;

|PROCESO Tipo80;  |TIPO 80;
     FSalida = 3299;
|FPRC;

|PROCESO LimpiaFiltro;
     |DDEFECTO #vipez013;
     #vipez013#0 = aUsuario;
     |PINDA #0#513;
|FPRC;

|PROCESO Filtro;
     nSwFiltrado = 0;

     |VENTANA 0501, 3299, -1, 1, "";
     nVentana = FSalida;

     |PINPA #0#513; nPantaFil = FSalida;

     |CONTROL_SIMPLE nPantaFil, "BOTON,{{199}} LIMPIA FILTRO", 0878, 0896, LimpiaFiltro;
     nBotonLimpia = FSalida;

     |ABRE #vipez013;
     aUsuario = Usuario % 810;
     |QBF aUsuario;
     |DDEFECTO #vipez013;
     #vipez013#0 = aUsuario;
     |LEE 101#vipez013.=;
     nSalida = FSalida;
     |SI nSalida ! 0; |GRABA 020#vipez013.b; |FINSI;
     |PINDA #0#513;

     |ENDATOS #1#vipez013;
     |SI FSalida = 0;
          nSwFiltrado = 1;
          |GRABA 020#vipez013.a;
     |SINO;
          nSwFiltrado = 0;
          |SI nSalida ! 0;
               |BORRA 020#vipez013.a;
          |FINSI;
     |FINSI;
     |LIBERA #vipez013;

     |CIERRA #vipez013;

     |FIN_CONTROL_WINDOWS nBotonLimpia;

     |FINVENTANA nVentana;

     |SI nSwFiltrado = 1;
          |HAZ RefrescaGrid;
     |FINSI;
|FPRC;

|PROCESO RefrescaGrid;
     |SI nSwGridCargado = 1;
          nSwGridCargado = 0;
          |DESTRUYECONTROL nGrid1;
          |ABRE #997;  |CIERRA #997;  |DELINDEX #997;
          |DESCARGA_DEF #ref1z002@;
     |FINSI;

     aFichSQL = aFichero1;

     aSQL = "SELECT * FROM vipem017 INTO " + aFichSQL + ".def WHERE ";
     aSQL = aSQL + "0 BETWEEN " + #vipez013#1 + "," + #vipez013#2;
     aSQL = aSQL + " AND 1 BETWEEN " + #vipez013#3 + "," + #vipez013#4;
     aSQL = aSQL + " AND 2 BETWEEN " + #vipez013#5 + "," + #vipez013#6;
     aSQL = aSQL + " AND 7 BETWEEN " + #vipez013#7 + "," + #vipez013#8;
     aSQL = aSQL + " AND 8 BETWEEN " + #vipez013#9 + "," + #vipez013#10;
     aSQL = aSQL + " AND 9 BETWEEN " + #vipez013#11 + "," + #vipez013#12;
     aSQL = aSQL + " AND 6 BETWEEN " + #vipez013#13 + "," + #vipez013#14;

     |ABRE #vipem017;
     |SQL aSQL;
     |CIERRA #vipem017;

     |DBASE aBase;  |QBF aBase;
     aMas = aBase + "def/vipem017.mas";
     |CARGA_DEF aMas, ref1z002@;
     |SI FSalida ! 0; |ACABA; |FINSI;

     nSwGridCargado = 1;

     |NOME_DAT #997 aFichSQL;

     |ABRE #997;

     nRango = 4 + 8 + 16 + 32 + 128;

     |LINEAL_SIMPLE #3#997, nRango, 0702, 1698, Baja997, Alta997, Evento997;
     nGrid1 = FSalida;
|FPRC;

|PROCESO FacturaAbajoSel;
     aDSerie = #997#7;
     aHSerie = #997#7;
     nDNumFac = #997#8;
     nHNumFac = #997#8;
     aSerAlbaran = #997#4;
     nNumAlbaran = #997#5;

     nDContrato = #997#0;
     nHContrato = #997#0;
     nDAnyo     = #997#1;
     nHAnyo     = #997#1;
     nDMes      = #997#2;
     nHMes      = #997#2;

     |BUCLE DeArribaAAbajoSel;

     |SELECT #1#998;
     |LEE 000#998p;
     |SI FSalida = 0;
         |ESTADO_CONTROL nBoton5, "ENABLE";
     |SINO;
         |ESTADO_CONTROL nBoton5, "DISABLE";
     |FINSI;

     |SELECT #1#997;
     #997#4 = aSerAlbaran;
     #997#5 = nNumAlbaran;
     |LEE 000#997];

     |SELECT #1#998;
     #998#4 = aSerAlbaran;
     #998#5 = nNumAlbaran;
     |LEE 000#998];

     |REFRESCACONTROL nGrid1;
     |REFRESCACONTROL nGrid2;
     ||ARA55
|FPRC;

|PROGRAMA;
     |CLS;
     |CABEZA "Borrado";

     |PINPA #0#0;  nPanta = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,\/", 1812, 1815, TodosAbajo;
     nBoton1 = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,/\", 1826, 1829, TodosArriba;
     nBoton2 = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,\/", 1859, 1862, FacturaAbajoSel;
     nBoton3 = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,/\", 1878, 1881, FacturaArriba;
     nBoton4 = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,{{207}}", 1844, 1848, Borrar;
     nBoton5 = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,Filtro Facturas", 0678, 0698, Filtro;
     nBoton6 = FSalida;

     |ESTADO_CONTROL nBoton5, "DISABLE";
     |ESTADO_CONTROL nBoton6, "ENABLE";

     |DBASE aBase;  |QBF aBase;
     aMas = aBase + "def/vipem017.mas";
     |CARGA_DEF aMas, ref2z002@;
     |CARGA_DEF aMas, ref1z002@;

     aFichero1 = ("sq1" + Usuario + "zzzzzzzz") % 108;
     aFichero2 = ("sq2" + Usuario + "zzzzzzzz") % 108;

     |NOME_DAT #997 aFichero1;
     |NOME_DAT #998 aFichero2;

     |ABRE #997;  |CIERRA #997;  |DELINDEX #997;
     |ABRE #998;  |CIERRA #998;  |DELINDEX #998;

     |DFICE aFich;  |QBF aFich;
     aOrigen  = aFich + "vipem017.dat";
     aDestino = aFich + aFichero1 + ".dat";
     |COPIA_FICHERO aOrigen, aDestino;

     |ABRE #997;
     |ABRE #998;

     nRango = 4 + 8 + 16 + 32 + 128;

     |LINEAL_SIMPLE #3#997, nRango, 0702, 1698, Baja997, Alta997, Evento997;
     nGrid1 = FSalida;
     nSwGridCargado = 1;

     |LINEAL_SIMPLE #3#998, nRango, 2102, 3198, Baja998, Alta998, Evento998;
     nGrid2 = FSalida;

     |FOCOTECLADO nGrid1;

     aEsc1  = &255 + "806";
     aEsc2  = &255 + "807";
     aRetu  = &255 + "802";
     |PARA; |SI;  |HACIENDO;
         FSalida = "::{{001}}Salir," + nGrid1;
         |LEETECLA aTecla;
         |SI aTecla = aEsc1; |SAL;  |FINSI;
         |SI aTecla = aEsc2; |SAL;  |FINSI;
         |SI aTecla = aRetu; |SAL;  |FINSI;
     |SIGUE;

     |CIERRA #997;
     |CIERRA #998;

     |DELINDEX #997;
     |DELINDEX #998;

     |FIN_CONTROL_WINDOWS nBoton1;
     |FIN_CONTROL_WINDOWS nBoton2;
     |FIN_CONTROL_WINDOWS nBoton3;
     |FIN_CONTROL_WINDOWS nBoton4;
     |FIN_CONTROL_WINDOWS nBoton5;
     |FIN_CONTROL_WINDOWS nBoton6;

     |DESCARGA_DEF #ref1z002@;
     |DESCARGA_DEF #ref2z002@;
|FPRO;
