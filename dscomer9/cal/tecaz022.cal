|FICHEROS;
     tecaz022 #0;
|FIN;

|VARIABLES;
     aAlfa = "";
     aFicEstado = "";
     nHandle = 0;
     nEstado = 0;
     nEnvia = 0;
     nPara = 0;
     nSwEnvia = 1;
     nArranca = 0;
|FIN;

|PROCESO Arranca;
     |DBASE aAlfa; aAlfa = aAlfa + "p800/arranca.sh";
     |SI nSwEnvia = 0;
        aAlfa = aAlfa + " AUTO";
     |SINO;
        aAlfa = aAlfa + " ENVIA";
     |FINSI;
     |SYSTEM aAlfa;
|FPRC;

|PROCESO Para;
     |DBASE aAlfa; aAlfa = aAlfa + "p800/para.sh";
     |SYSTEM aAlfa;
|FPRC;

|PROCESO GrabaEstado;
     |XABRE "W", aFicEstado, nHandle;
     |SI FSalida ] 0;
          aAlfa = nEstado;
          |XGRABA nHandle, aAlfa;
          |XCIERRA nHandle;
     |SINO;
          aAlfa = "0000Error al grabar " + aFicEstado;
          |MENAV aAlfa;
     |FINSI;
|FPRC;

|PROCESO LeeEstado;
     nEstado = 0;
     |XABRE "U", aFicEstado, nHandle;
     |SI FSalida ] 0;
          |XLEE nHandle, 250, aAlfa;
          |XCIERRA nHandle;
          nEstado = aAlfa;
     |SINO;
          |HAZ GrabaEstado;
     |FINSI;
|FPRC;

|PROCESO Estado;
     |SI nEstado = 1;
          |SI nSwEnvia = 1;
               |MENSA "0000Arrancado (envia).";
          |SINO;
               |MENSA "0000Arrancado (no envia).";
          |FINSI;
          |ESTADO_CONTROL nPara, "ENABLE";
          |ESTADO_CONTROL nArranca, "DISABLE";
          |ESTADO_CONTROL nEnvia, "DISABLE";
     |SINO;
          |MENSA "0000Parado.";
          |ESTADO_CONTROL nPara, "DISABLE";
          |ESTADO_CONTROL nArranca, "ENABLE";
          |ESTADO_CONTROL nEnvia, "ENABLE";
     |FINSI;
|FPRC;

|PROCESO PulsaSin;
     nSwEnvia = 0;
     |HAZ Pulsa;
|FPRC;

|PROCESO PulsaCon;
     nSwEnvia = 1;
     |HAZ Pulsa;
|FPRC;

|PROCESO Pulsa;
     |ESTADO_CONTROL nArranca, "DISABLE";
     |ESTADO_CONTROL nPara, "DISABLE";
     |ESTADO_CONTROL nEnvia, "DISABLE";
     nEstado = (nEstado + 1) $ 2;
     |HAZ GrabaEstado;
     |HAZ LeeEstado;
     |SI nEstado = 1;
          |MENSA "0000Arrancando...";
          |HAZ Arranca;
          |MENSA "0000Arrancado.";
     |SINO;
          |MENSA "0000Parando...";
          |HAZ Para;
          |MENSA "0000Parado.";
     |FINSI;
     |HAZ Estado;
|FPRC;

|PROCESO Tipo0; |TIPO 0;
     |SI FSalida = 13; |HAZ Pulsa; |ERROR; |FINSI;
     |SI FSalida = 1; #0#0 = "SI"; |FINSI;
|FPRC;

|PROCESO Tipo12; |TIPO 12;
|FPRC;

|PROGRAMA;
     |CLS;
     |PINPA #0#0;
     |PINDA #0#0;

     |CONTROL_SIMPLE 0, "BOTON,ARRANCA ENVIANDO", 1018, 1231, PulsaCon;
     nArranca = FSalida;
     |ESTADO_CONTROL nArranca, "SHOW";

     |CONTROL_SIMPLE 0, "BOTON,PARAR", 1054, 1267, PulsaCon;
     nPara = FSalida;
     |ESTADO_CONTROL nPara, "SHOW";
     |ESTADO_CONTROL nPara, "DISABLE";

     |CONTROL_SIMPLE 0, "BOTON,ARRANCA SIN ENVIAR", 1036, 1249, PulsaSin;
     nEnvia = FSalida;
     |ESTADO_CONTROL nPara, "SHOW";
     |ESTADO_CONTROL nPara, "DISABLE";

     |DBASE aFicEstado;
     aFicEstado = aFicEstado + "p800/";
     |MKDIR aFicEstado;
     aFicEstado = aFicEstado + "p800.loc";
     |HAZ LeeEstado;
     |HAZ Estado;

     |PARA; |SI #0#0 ! "SI"; |HACIENDO;
          |ENDATOS #2#0;
     |SIGUE;

     |FIN_CONTROL_WINDOWS nPara;
     |FIN_CONTROL_WINDOWS nArranca;
     |FIN_CONTROL_WINDOWS nEnvia;
     |PULSATECLA;
|FPRO;
