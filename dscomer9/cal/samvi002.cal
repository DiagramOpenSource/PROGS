|FICHEROS;
     samvi002 #0;
     agifa019 #19;
|FIN;

|VARIABLES;
     nHandle = 0;
     aAlfa = "";
     aFic = "";
     nNume = 0;
     nNume1 = 0;
     nNume2 = 0;
     aPath = "";
     aDato = "";
     aCar = "";
     nLon = 0;
     i = 0;
     j = 0;
     aCodigo = "";
     aCampo1 = "";
     aCampo2 = "";
     aCampo3 = "";
     aCampo5 = "";
     aCampo9 = "";
     aCampo18 = "";
     aCampo28 = "";
     aCampo29 = "";
     aCampo30 = "";
     aCampo102 = "";
     aCampo125 = "";
     aCampo133 = "";
     aCampo163 = "";
     aCampo164 = "";
     aCampo165 = "";
|FIN;

|PROCESO Actualiza;
     |PINTA 2307, aCodigo;

     |DDEFECTO #19;
     #19#0 = aCodigo;
     |LEE 101#19=;
     |SI FSalida ! 0;
          #19#30 = aCampo30;
          #19#165 = aCampo165;
          |GRABA 020#19b;
     |FINSI;
     aAlfa = #19#1; |QBF aAlfa;
     |SI aAlfa = "";
          #19#1 = aCampo1;
     |FINSI;
     #19#2 = aCampo2;
     #19#3 = aCampo3;
     #19#5 = aCampo5;
     |SI #19#9 = 0;
          #19#9 = aCampo9;
     |FINSI;
     #19#18 = aCampo18;
     #19#28 = aCampo28;
     #19#29 = aCampo29;
     #19#102 = aCampo102;
     #19#125 = aCampo125;
     #19#133 = aCampo133;
     #19#163 = aCampo163;
     #19#164 = aCampo164;
     #19#165 = aCampo165;
     |GRABA 020#19a;
     |LIBERA #19;
|FPRC;

|PROCESO Importa;
||     Codigo 1 10
     aCodigo = aDato % 110;
     |PARA j = 1; |SI j [ 10; |HACIENDO j = j + 1;
          nLon = j * 100 + 1;
          aCar = aCodigo % nLon;
          |SI aCar ! " "; |Y aCar ! "0";
               nLon = j * 100 + (10 - j)  +1;
               aCodigo = aCodigo % nLon;
               |SAL;
          |FINSI;
     |SIGUE;

||1    Descripción    11   20
     aCampo1 = aDato % 1120;

||2    Familia   78   1
     aCampo2 = aDato % 7801;

||3    Subfamilia     79   4
     aCampo3 = aDato % 7904;

||5    Unidad Medida  90   4
     aCampo5 = aDato % 9004;

||18   PVP 1     56   9
     aCampo18 = aDato % 5609; nNume = aCampo18; nNume = nNume/100;
     aCampo18 = nNume;

||28   Ultimo Precio com.  56   9
     aCampo28 = aDato % 5609; nNume = aCampo28; nNume = nNume/100;
     aCampo28 = nNume;

||102  Peso Caja 83   7
     aCampo102 = aDato % 8307;
     nNume = aCampo102;
     nNume = nNume / 100;
     aCampo102 = nNume;

||125  Línea     31   1
     aCampo125 = aDato % 3101;

||163  UPC Div   56 9
     aCampo163 = aDato % 5609;
     nNume = aCampo163;
     nNume = nNume / 100;
     aCampo163 = nNume;

||29   Ult.Dto. Compra
     aCampo29 = 0;
     |SI aCampo125 = "R"; aCampo29 = 10; |FINSI;
     |SI aCampo125 = "A"; aCampo29 = 35; |FINSI;
     |SI aCampo125 = "B"; aCampo29 = 30; |FINSI;
     |SI aCampo125 = "C"; aCampo29 = 25; |FINSI;
     |SI aCampo125 = "D"; aCampo29 = 20; |FINSI;
     |SI aCampo125 = "E"; aCampo29 = 15; |FINSI;
     |SI aCampo125 = "F"; aCampo29 = 10; |FINSI;
     |SI aCampo125 = "G"; aCampo29 = 10; |FINSI;
     |SI aCampo125 = "H"; aCampo29 = 10; |FINSI;
     |SI aCampo125 = "I"; aCampo29 = 0; |FINSI;
     |SI aCampo125 = "J"; aCampo29 = 0; |FINSI;
     |SI aCampo125 = "K"; aCampo29 = 40; |FINSI;
     |SI aCampo125 = "L"; aCampo29 = 45; |FINSI;
     |SI aCampo125 = "N"; aCampo29 = 0; |FINSI;
     |SI aCampo125 = "P"; aCampo29 = 30; |FINSI;
     |SI aCampo125 = "1"; aCampo29 = 30; |FINSI;
     |SI aCampo125 = "2"; aCampo29 = 25; |FINSI;
     |SI aCampo125 = "3"; aCampo29 = 20; |FINSI;
     |SI aCampo125 = "4"; aCampo29 = 15; |FINSI;
     |SI aCampo125 = "5"; aCampo29 = 10; |FINSI;
     |SI aCampo125 = "Z"; aCampo29 = 65; |FINSI;

||9    Precio Coste Medio
     nNume1 = aCampo18;
     nNume2 = aCampo29;
     nNume1 = nNume1 - (nNume1 * nNume2 / 100);
     aCampo9 = nNume1;

||30   Tipo de IVA
     aCampo30 = 2;

||133  Prec.Ult.comp..dto.
     aCampo133 = aCampo9;

||164  UPC Div con dto.
     aCampo164 = aCampo9;

||165  Divisa UPC
     aCampo165 = "EUR";
|FPRC;

|PROCESO Tipo3; |TIPO 3;
     aPath = #0#1; |QBF aPath;
     aAlfa = aPath % -101;
     |SI aAlfa ! "/"; |Y aAlfa ! "\";
          aPath = aPath + "/";
     |FINSI;

     |ABRE #19;
     |PARA i = 2; |SI i [ 11; |HACIENDO i = i + 1;
          aFic = #0i; |QBF aFic;
          |SI aFic ! "";
               aFic = aPath + aFic;
               |SI #0#12 = "C";
                    |XABRE "CU", aFic, nHandle;
               |SINO;
                    |XABRE "U", aFic, nHandle;
               |FINSI;
               |SI FSalida ] 0;
                    |ATRI i; |PINTA 2207, aFic; |ATRI 0;
                    |PARA; |SI; |HACIENDO;
                         |XLEE nHandle, 250, aDato;
                         |SI FSalida < 0; |SAL; |FINSI;
                         |HAZ Importa;
                         |HAZ Actualiza;
                    |SIGUE;
                    |XCIERRA nHandle;
               |SINO;
                    aAlfa = "0000ERROR " + aFic;
                    |MENAV aAlfa;
               |FINSI;
          |FINSI;
     |SIGUE;
     |CIERRA #19;
|FPRC;

|PROGRAMA;
     |CLS;
     |ABRE #0;
     |LEE 101#0=;
     |PINPA #0#0;
     |PINDA #0#0;
     |SI FSalida ! 0; |GRABA 020#0b; |FINSI;
     |ENDATOS #1#0;
     |SI FSalida = 0;
          |GRABA 020#0a;
     |FINSI;
     |CIERRA #0;
|FPRO;
