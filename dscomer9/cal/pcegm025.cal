|FICHEROS;
     pcegm025 #0;
     pcegm026 #26;
     pcegm027 #27;
|FIN;

|VARIABLES;
     &eaSerie = "";
     &enOferta = 0;
     &eaFich = "";
     aAlfa = "";
     nBuffer = 0;
     nPuntero = 0;
     nCalc = 0;
     nLineas = 0;
     nModo = 0;
     nLin = 0;
     nCol = 0;
     nBtnTxt = 0;
     nSwTxt = 0;
     aLon = "";
     nLon = 0;
     nPos = 0;
     aAlf1 = "";
     aAlf2 = "";
|FIN;

|PROCESO BtnTxt;
     nSwTxt = 1;
     |PTEC 801;
|FPRC;

|PROCESO CogeLineas;
     aAlfa = #0#3;
     |AL_BUF nBuffer, nPuntero, aAlfa;
     nPuntero = nPuntero + 1;
     |BORRA 020#0a;
|FPRC;

|DEFBUCLE CogeLineas;
     #0#1;
     ;
     eaSerie, enOferta, 01;
     eaSerie, enOferta, 99;
     be;
     NULL, CogeLineas;
|FIN;

|PROCESO GrabaLineas;
     nLineas = 0;
     |PARA nCalc = 100; |SI nCalc ] 0; |HACIENDO nCalc = nCalc - 1;
          |DEL_BUF nBuffer, nCalc, aAlfa;
          |SI aAlfa ! ""; nLineas = nCalc; |SAL; |FINSI;
     |SIGUE;

     |ABRE #0;
     |PARA nCalc = 1; |SI nCalc [ nLineas; |HACIENDO nCalc = nCalc + 1;
          |DEL_BUF nBuffer, nCalc, aAlfa;
          |DDEFECTO #0;
          #0#0 = eaSerie;
          #0#1 = enOferta;
          #0#2 = nCalc;
          #0#3 = aAlfa;
          |GRABA 020#0n;
     |SIGUE;
     |CIERRA #0;
|FPRC;

|PROCESO PegaTexto;
     |DDEFECTO #0;
     #0#0 = eaSerie;
     #0#1 = enOferta;
     #0#2 = nCalc;
     #0#3 = #27#2;
     |GRABA 020#0n;
     nCalc = nCalc + 1;
|FPRC;

|DEFBUCLE PegaTexto;
     #27#1;
     ;
     #26#0, 01;
     #26#0, 99;
     ;
     NULL, PegaTexto;
|FIN;

|PROCESO Texto;
     |ABRE #26;
     |CONSULTA_CLAVE #1#26;
     |SI FSalida = 0;
          |SI nCalc = 1; nCalc = 2; |FINSI;
          |ABRE #0;
          |BUCLE PegaTexto;
          |CIERRA #0;
     |FINSI;
     |CIERRA #26;
|FPRC;

|PROCESO Edita;
     nSwTxt = 0;
     |DIMENSIONA 200, nBuffer, 0;
     nPuntero = 1;
     |BUCLE CogeLineas;
     nModo = 100; nLin = 99; nCol = 120;
     |EDITA nBuffer, 1, 100, 1412, 2270, nModo, "", 0, nLin, nCol;
     |HAZ GrabaLineas;
     |FINDIM nBuffer;
|FPRC;

|PROGRAMA;
     |BLANCO 1211 2371;
     #0#0 = eaSerie;
     #0#1 = enOferta;
     |PINPA #0#0;
     |PINDA #0#0;
     |CONTROL_SIMPLE 0, "BOTON,Textos predefinidos", 1350, 1365, BtnTxt;
     nBtnTxt = FSalida;

     |PARA nSwTxt = 1; |SI nSwTxt = 1; |HACIENDO;
          |HAZ Edita;
          |SI nSwTxt = 1; |HAZ Texto; |FINSI;
     |SIGUE;

     |FIN_CONTROL_WINDOWS nBtnTxt;
     |PULSATECLA;
|FPRO;
