|FICHEROS;
     inmoz016 #0;
     inmom007 #7;
|FIN;

|VARIABLES;
     fFecha = @;
     nSal = 0;
     nBoton = 0;
     aRuta = "";
     aAlfa     = "";
     aDato     = "";
     nHandle   = 0;
     nCampo    = 0;
     aLon      = "";
     i         = 0;
     nPos      = 0;
     aLetra    = "";
     aReg      = "";
     aTab      = "";
 {99}Valor     = "";
     aFic = "";
     aHora = "";
|FIN;

|PROCESO Graba;
     || 1         2        3     4    5      6         7        8
     ||CodBarra,CodBarra1,obra,serie,orden,F_lectura,H_Lectura,usuario
     |DDEFECTO #7;
     #7#2 = Valor1;
     #7#3 = Valor2;
     #7#4 = Valor3;
     #7#5 = Valor4;
     #7#6 = Valor5;
     #7#0 = Valor6;
     #7#1 = Valor7;
     #7#7 = Valor8;
     |GRABA 020#7n;
|FPRC;

|PROCESO Dato;
     nCampo = 1;
     IValor = Valor1<-;
     aLon = aDato % A0;
     aReg = "";
     |PARA i = 1; |SI i [ aLon; |HACIENDO i = i + 1;
          nPos = i * 100 + 1;
          aLetra = aDato % nPos;
          |SI aLetra = aTab;
               Valor = aReg;
               aReg = "";
               nCampo = nCampo + 1;
               IValor = IValor + 1;
          |SINO;
               aReg = aReg + aLetra;
          |FINSI;
          |SI nCampo > 90; |SAL; |FINSI;     || Por si.... :)
     |SIGUE;
     |QBF aReg;
     Valor = aReg;
|FPRC;

|PROCESO SacaFic;
     aFic = "";
     aLon = aRuta % A0;
     |PARA i = aLon; |SI i > 0; |HACIENDO i = i - 1;
          nPos = i * 100 + 1;
          aAlfa = aRuta % nPos;
          |SI aAlfa = "/"; |O aAlfa = "\"; |SAL; |FINSI;
          aFic = aAlfa + aFic;
     |SIGUE;
|FPRC;

|PROCESO Tipo3; |TIPO 3;
     |SI #0#2 ! "SI"; |ACABA; |FINSI;

     |ESTADO_CONTROL nBoton, "HIDE";

     aRuta = #0#1; |QBF aAlfa;
     |HAZ SacaFic;

     |HORA aHora;
     fFecha = ~;

     aTab = &9;

     |ABRE #7;
     |XABRE "C", aRuta, nHandle;
     |SI FSalida ] 0
          |XLEE nHandle, 250, aDato;
          |PARA; |SI FSalida > 0; |HACIENDO;
               |HAZ Dato;
               |HAZ Graba;
               |SI nSal = 1; |SAL; |FINSI;
               |XLEE nHandle, 250, aDato;
          |SIGUE;
          |XCIERRA nHandle;
     |SINO;
          |MENAV "0000No puedo abrir el fichero...";
     |FINSI;
     |CIERRA #7;
|FPRC;

|PROCESO Tipo0; |TIPO 0;
     |SI FSalida = 13;
          |ESTADO_CONTROL nBoton, "DISABLE";

          aRuta = #0#1;  |QBF aRuta;
          |SI aRuta = "";
              aRuta = "B*.*";
          |FINSI;
          aRuta = "F" + aRuta;

          |BROWSE aRuta;

          aRuta = aRuta % 2;
          |SI aRuta ! "F";
               #0#1 = aRuta;
          |FINSI;
          |PINTA #0#1;

          |ESTADO_CONTROL nBoton, "ENABLE";
          |ERROR;
     |FINSI;

     aRuta = #0#1;
     |HAZ SacaFic;

     aAlfa = aFic % 101;
     aAlfa = aAlfa > "";
     |SI aAlfa ! "B";
          |MENAV "0000Nombre fichero incorrecto debe de comezar por: 'B'";
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO Boton;
     |PTEC 827;
|FPRC;

|PROGRAMA;
     |ABRE #0;
     |LEE 101#0p;
     |SI FSalida ! 0; |GRABA 020#0b; |FINSI;
     |CLS;
     #0#2 = "NO";
     |PINPA #0#0;
     |PINDA #0#0;
     |CONTROL_SIMPLE 0, "BOTON, Fichero", 1010, 1018, Boton;
     nBoton = FSalida;
     |ENDATOS #1#0;
     |SI FSalida = 0;
          |GRABA 020#0a;
     |FINSI;
     |CIERRA #0;
     |FIN_CONTROL_WINDOWS nBoton;
|FPRO;
