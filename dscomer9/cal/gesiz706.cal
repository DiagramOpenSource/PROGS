     Recibos

|FICHEROS;
     gesiz006 #0;
     gesiw002 #2;   || Tmp informe cobros pdte
     agifa024 #24;
     gesim014 #27;
     gesim018 #18;
     dscam045@;
     dscam003@;
     dscam004@;
     referer1@;
     referer2@;
|FIN;

|VARIABLES;
     &nDeci_Base = 0; &nDeci_Div = 0;
     &Tempo = "";
     &enPestana = 0;
     &eaTmp = "";
     &eaAuto = "";
     &eaCarga7 = "";
     aAlfa = "";
     aTmp = "";
     aPath = "";
     aEmpre = "";
     aPathDat = "";
     aPathDef = "";
     aNumEmp = "";
     nNumEmp = 0;
     nOk = 0;
     aExisteCarte = "N";
     nRango = 0;
     nPos1 = 0;
     nPos2 = 0;
     nPanta = 0;
     nBtnImpre = 0;
     nBtnSalir = 0;
     nEvtFoco = 0;
     nLabel = 0;
     nGrid1 = 0;
     nGrid2 = 0;

     fFechaRecep = @;
     nTotalPdt = 0;
     aEntidad = "";
     aNumero = "";
     aInf = "";
|FIN;

|PROCESO dscam045_1;
     |SI #dscam045@#2 = "V";
          aEmpre = ("00000" + #dscam045@#6) % -105;
          aPathDat = aPath + aEmpre + "/";
          |ERROR10;
     |FINSI;
|FPRC;

|DEFBUCLE dscam045_1;
     #dscam045@#1002;
     ;
     nNumEmp, 001;
     nNumEmp, 999;
     ;
     NULL, dscam045_1;
|FIN;

|PROCESO CargaCartera;
     |DBASS aPath;
     aPath = aPath + "dscarter/";
     aPathDef = aPath + "def/";
     aAlfa = aPathDef + "dscam045";
     |CARGA_DEF aAlfa, dscam045@;
     |SI FSalida = 0;
          |DFICE aAlfa;
          aNumEmp = aAlfa % -205;
          nNumEmp = aNumEmp;
          aPath = aPath + "fich/";
          |PATH_DAT #dscam045@#aPath#;
          |BUCLE dscam045_1;
          |DESCARGA_DEF #dscam045@;
          aExisteCarte = "S";

          aAlfa = aPathDef + "dscam003";
          |CARGA_DEF aAlfa, dscam003@;
          |SI FSalida ! 0; |MENAV "0000Error al cargar 'dscam003'"; |FINSI;
          aAlfa = aPathDef + "dscam004";
          |CARGA_DEF aAlfa, dscam004@;
          |SI FSalida ! 0; |MENAV "0000Error al cargar 'dscam004'"; |FINSI;
          |SI aPathDat = "";
               |MENAV "0000Error, no encuentro empresa cartera";
          |SINO;
               |PATH_DAT #dscam003@#aPathDat#;
               |PATH_DAT #dscam004@#aPathDat#;
          |FINSI;
     |SINO;
          aExisteCarte = "N";
     |FINSI;
|FPRC;

|PROCESO DescargaCartera;
     |SI aExisteCarte = "S";
         |DESCARGA_DEF #dscam004@;
         |DESCARGA_DEF #dscam003@;
     |FINSI;
|FPRC;

|PROCESO CargaRecibos;
     #27#0 = #dscam003@#12;   ||Tipo Recibo
     #27#1 = #dscam003@#14;   ||Estado
     |LEE 000#27=;
     |SI #dscam003@#65 ! 0; |Y FSalida = 0;
          |SALVA_DATOS #dscam003@;
          |REPON_DATOS #referer1@;
          |GRABA 020#referer1@.n;
     |FINSI;
|FPRC;

|DEFBUCLE CargaRecibos;
     #dscam003@#11003;
     ;
     #0#0, "01.01.0000", "     ";
     #0#0, "31.12.9999", "zzzzz";
     ;
     NULL, CargaRecibos;
|FIN;

|PROCESO Tmp;
     #18#0 = #2#2;
     |LEE 000#18=;
     |SI FSalida ! 0; |DDEFECTO #18; |FINSI;

     aAlfa = #18#4; |QBF aAlfa;
     |SI aAlfa ! aInf; |O aInf = "";
          |SI aInf ! ""; |PIEINF; |FININF; |FINSI;
          aInf = aAlfa;
          |INFOR aInf;
          |SI FSalida ! 0;
               aAlfa = "0000No existe informe:" + aAlfa;
               |MENAV aAlfa;
               |ERROR10; |ACABA;
          |FINSI;
     |FINSI;

     |PRINT;
|FPRC;

|DEFBUCLE Tmp;
     #2#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, Tmp;
|FIN;

|PROCESO MiraPdtTP;
     nTotalPdt = nTotalPdt + #referer1@#31;

     #dscam004@#17 = #referer1@#40;
     #dscam004@#3  = 1;
     |LEE 000#dscam004@.];
     |PARA; |SI FSalida = 0; |Y #dscam004@#17 = #referer1@#40; |HACIENDO;
          |SI #dscam004@#5 = "RPT"; fFechaRecep = #dscam004@#6; |FINSI;
          |LEE 000#dscam004@.s;
     |SIGUE;
|FPRC;

|DEFBUCLE MiraPdtTP;
     #referer1@#7003;
     ;
     aEntidad, aNumero, 0000000001;
     aEntidad, aNumero, 9999999999;
     ;
     NULL, MiraPdtTP;
|FIN;

|PROCESO Recibos;
     fFechaRecep = "01.01.0000";
     nTotalPdt = 0;
     aEntidad = #referer1@#72;
     aNumero = #referer1@#70;
     |SI aEntidad ! "    "; |O aNumero ! "          ";
          |SALVA_FICHA #referer1@;
          |BUCLE MiraPdtTP;
          |REPON_FICHA #referer1@;
     |FINSI;

     |DDEFECTO #2;
     #2#0 = #referer1@#33; || Codigo representante
     #2#1 = #referer1@#32; || Codigo cliente
     #2#2 = #referer1@#0;  || Serie recibo
     #2#3 = #referer1@#1;  || N§ factura recibo
     #2#4 = #referer1@#2;  || N§ recibo recibo
     #2#5 = #referer1@#72; || Entidad Pagare/Talon
     #2#6 = #referer1@#70; || N§ Pagare/Talon
     #2#7 = #referer1@#4;  || Fecha emision recibo
     #2#8 = #referer1@#30; || Importe total recibo
     #2#9 = #referer1@#23; || Entregas recibo
     #2#10 = #referer1@#31; || Importe pendiente recibo
     #2#11 = #referer1@#3;  || Fecha vencimiento recibo
     #2#12 = #referer1@#78; || Importe total Pagare/Talon
     #2#13 = #referer1@#78 - nTotalPdt; || Importe cobrado Pagare/Talon
     #2#14 = nTotalPdt;    || Importe pendiente Pagare/Talon
     #2#15 = #referer1@#71; || Fecha vencimiento Pagare/Talon

     #24#0 = #referer1@#32;
     |LEE 000#24=;
     |SI FSalida ! 0; |DDEFECTO #24; |FINSI;

     #2#16 = #24#8;  || Direccion factura cliente
     #2#17 = #24#9;  || Poblacion factura cliente
     #2#18 = #24#10; || Provincia factura cliente
     aAlfa = (("00" + #24#181) % -102) + (("000" + #24#182) % -103);
     #2#19 = aAlfa;        || CP factura cliente
     #2#20 = #24#17; || Telefono cliente
     #2#21 = #24#196;|| Telefono movil cliente
     #2#22 = #referer1@#34; || Nombre representante
     #2#23 = #referer1@#6;  || Nombre cliente
     #2#24 = #referer1@#40; || Numero de documento
     #2#25 = fFechaRecep;  || Fecha de recepcion del pagare/talon
     |GRABA 020#2n;
|FPRC;

|DEFBUCLE Recibos;
     #referer1@#1001;
     ;
     000000001;
     999999999;
     ;
     NULL, Recibos;
|FIN;

|PROCESO BtnImpre7;
     Impresora = "CrystalReport";
     |IMPRE -1;
     |SI FSalida = 0;
          |SALVA_FICHA #referer1@;
          |HAZ CreaTempo; |NOME_DAT #2 Tempo; |DELINDEX #2;

          |ABRE #2;
          |ABRE #24;
          |BUCLE Recibos;
          |CIERRA #24;
          |CIERRA #2;
          |ABRE #18;
          aInf = "";
          |BUCLE Tmp;
          |SI aInf ! ""; |PIEINF; |FININF; |FINSI;
          |CIERRA #18;
          |REPON_FICHA #referer1@;

          |HAZ BoraTempo; |DELINDEX #2;
          |FINIMP;
     |FINSI;
|FPRC;

|PROCESO EventoFoco7;
     enPestana = 7;
     |SI eaCarga7 ! "S";
          eaCarga7 = "S";
          |HAZ Actualiza7;
     |FINSI;
|FPRC;

|PROCESO Evento17;
     |SI FSalida = 1; |O FSalida = 2;
          |LEE 000#referer1@.=;
          |REFRESCACONTROL nGrid2;
     |FINSI;
|FPRC;

|PROCESO Evento27;
     |SI FSalida = 1; |O FSalida = 2;
          |LEE 000#referer2@.=;
     |FINSI;
|FPRC;

|PROCESO NomeDat7;
     |SI #0#0 = "000000";
          aTmp = "dscam003";
          |NOME_DAT #referer1@#aTmp#;
     |SINO;
          aTmp = eaTmp;
          |NOME_DAT #referer1@#aTmp#;

          |SI eaAuto = ""; |DELINDEX #referer1@; |FINSI;
     |FINSI;
|FPRC;

|PROCESO Min27;
     #referer2@#17 = #referer1@#40
     #referer2@#3 = 1;
|FPRC;

|PROCESO Max27;
     #referer2@#17 = #referer1@#40
     #referer2@#3 = 99;
|FPRC;

|PROCESO Min17;
     #referer1@#40 = 0;
|FPRC;

|PROCESO Max17;
     #referer1@#40 = 999999999;
|FPRC;

|PROCESO Actualiza7;
     |SI aExisteCarte = "N"; |ACABA; |FINSI;

     |DESTRUYECONTROL nGrid1;
     |CIERRAT #referer1@;
     |HAZ NomeDat7;
     |ABRET #referer1@;
     |SI eaCarga7 = "S"; |Y #0#0 ! "000000";
          |ABRE #27;
          |BUCLE CargaRecibos;
          |CIERRA #27;
     |FINSI;
     |LEE 000#referer1@.p;
     |PINPA #6#0;  nPanta = FSalida;
     nRango = 4 + 8 + 16 + 32;
     nPos1 = 0702;
     nPos2 = 2298;
     |LINEAL_SIMPLE #1#referer1@, nRango, nPos1, nPos2, Min17, Max17, Evento17;
     nGrid1 = FSalida;

     #referer1@#40 = 0;    || Para que no salga nada si esta vacio
     |REFRESCACONTROL nGrid2;
     |REFRESCACONTROL nGrid1;

     |FIN_CONTROL_WINDOWS nLabel;
     |SI #0#0 = "000000";
          |ESTADO_CONTROL nBtnImpre, "DISABLE";
          |CONTROL_SIMPLE nPanta, "LABEL,{{001}}Todos los recibos", 0502, 0540, NULL;
     |SINO;
          |ESTADO_CONTROL nBtnImpre, "ENABLE";
          |CONTROL_SIMPLE nPanta, "LABEL,{{001}}Recibos pendientes", 0502, 0540, NULL;
     |FINSI;
|FPRC;

|PROCESO PintaPan7;
     |SI aExisteCarte ! "S"; |ACABA; |FINSI;

     aAlfa = aPathDef + "dscam003";
     |CARGA_DEF aAlfa, referer1@;
     aAlfa = aPathDef + "dscam004";
     |CARGA_DEF aAlfa, referer2@;

     |PATH_DAT #referer1@#aPathDat#;
     |PATH_DAT #referer2@#aPathDat#;
     |HAZ NomeDat7;

     #referer1@#40 = 0;    || Para que no salga nada si esta vacio

     |ABRET #referer1@;
     |ABRET #referer2@;

     |PINPA #6#0;  nPanta = FSalida;

     nRango = 4 + 8 + 16 + 32;
     nPos1 = 2402;
     nPos2 = 3398;
     |LINEAL_SIMPLE #1#referer2@, nRango, nPos1, nPos2, Min27, Max27, Evento27;
     nGrid2 = FSalida;
/*
     nRango = 4 + 8 + 16 + 32;
     nPos1 = 0702;
     nPos2 = 2298;
     |LINEAL_SIMPLE #1#referer1@, nRango, nPos1, nPos2, Min17, Max17, Evento17;
     nGrid1 = FSalida;
*/
     |CONTROL_SIMPLE nPanta, "EVENTOPAN", 0000, 0000, EventoFoco7;
     nEvtFoco = FSalida;

     |SI #0#0 = "000000";
          |CONTROL_SIMPLE nPanta, "LABEL,{{001}}Todos los recibos", 0502, 0540, NULL;
     |SINO;
          |CONTROL_SIMPLE nPanta, "LABEL,{{001}}Recibos pendientes", 0502, 0540, NULL;
     |FINSI;
     nLabel = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,Imprimir", 0601, 0613, BtnImpre7;
     nBtnImpre = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,{{207}} Salir", 3688, 3698, BtnSalir;
     nBtnSalir = FSalida;
|FPRC;

|PROCESO DestruyePan7;
     |SI aExisteCarte = "N"; |ACABA; |FINSI;
/*
     |DESTRUYECONTROL nGrid1;
     |DESTRUYECONTROL nGrid2;

     |FIN_CONTROL_WINDOWS nLabel;
     |FIN_CONTROL_WINDOWS nEvtFoco;
     |FIN_CONTROL_WINDOWS nBtnImpre;
     |FIN_CONTROL_WINDOWS nBtnSalir;
*/

     |CIERRAT #referer2@;
     |CIERRAT #referer1@;
     |DESCARGA_DEF #referer2@;
     |DESCARGA_DEF #referer1@;
|FPRC;
