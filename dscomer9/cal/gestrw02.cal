|FICHEROS;
     gestrw02 #0;
     gestrm01 #1;
     dsl00001 #10;
     agifa019 #19;
     dsm00024 #24;
     agifa071 #71;
     agifa073 #73;
     agifa142 #142;
|FIN;

|VARIABLES;
     nSal = 0;
     aCarSer = "";
     aCarAnt = "";
     aCar2 = "";
     &eaSerie = "";
     &enCodigo = 0;
     nHandle = 0;
     aAlfa = "";
     aFic = "";
     aTipo = "";
     aCar = "";
     aTmp = "";
     aCr = "";
     aLf = "";
     nLin = 0;
     aArt = "";
     aPar = "";
     aSer = "";
     aPed = "";
     nSw = 0;
     {1}aLocal = "";
     aMin = "";
|FIN;

|PROCESO UltLin;
      |ABRE #71;
      #71#29 = eaSerie;
      #71#0 = enCodigo;
      #71#1 = 999;
      |LEE 000#71];
      |SI FSalida = 0;
           |LEE 000#71a;
      |SINO;
           |LEE 000#71u;
      |FINSI;
      |SI FSalida = 0; |Y #71#29 = eaSerie; |Y #71#0 = enCodigo;
           nLin = #71#1 + #142#236;
      |SINO;
           nLin = #142#236;
      |FINSI;
      |CIERRA #71;
|FPRC;

|PROCESO BusLinPed;
     #0#6 = #73#5 - #73#43;
     |SI #0#6 > 0;
          #0#5 = #73#1;

          |SALVA_FICHA #0;
          |SELECT #2#0;
          |LEE 000#0=;
          nSal = FSalida;
          |SELECT #1#0;
          |REPON_FICHA #0;

          |SI nSal = 0;
               #0#5 = 1; ||sigue buscando
          |SINO;
               |ERROR10;
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE BusLinPed;
     #73#3;
     ;
     #0#7, #0#3, #0#4, "      ";
     #0#7, #0#3, #0#4, "zzzzzz";
     ;
     NULL, NULL, NULL, NULL, NULL, BusLinPed;
     #73#1;
|FIN;

|PROCESO PonUniPed;
     |SELECT #2#10;
     #10#3 = #0#1;
     |LEE 000#10=;
     |SI FSalida ! 0;
          aAlfa = "0000No existe codigo de barras:" + #0#1; |QBF aAlfa;
          |MENAV aAlfa;
          |BORRA 020#0a;
          |ACABA;
     |FINSI;
     #0#7 = #10#1;

     #19#0 = #0#7;
     |LEE 000#19=;
     |SI FSalida ! 0;
          aAlfa = "0000No existe codigo articulo:" + #0#7; |QBF aAlfa;
          |MENAV aAlfa;
          |BORRA 020#0a;
          |ACABA;
     |FINSI;
     #0#8 = #19#1;
     #0#9 = #19#30;
     #0#10 = #19#2;
     #0#5 = 1;
     |BUCLE BusLinPed;
     |GRABA 020#0a;
|FPRC;

|DEFBUCLE PonUniPed;
     #0#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, PonUniPed;
|FIN;

|PROCESO PonUni;
     |SELECT #2#10;
     #10#3 = #0#1;
     |LEE 000#10=;
     |SI FSalida ! 0;
          aAlfa = "0000No existe codigo de barras:" + #0#1; |QBF aAlfa;
          |MENAV aAlfa;
          |BORRA 020#0a;
          |ACABA;
     |FINSI;
     #0#7 = #10#1;

     #19#0 = #0#7;
     |LEE 000#19=;
     |SI FSalida ! 0;
          aAlfa = "0000No existe codigo articulo:" + #0#7; |QBF aAlfa;
          |MENAV aAlfa;
          |BORRA 020#0a;
          |ACABA;
     |FINSI;
     #0#8 = #19#1;
     #0#9 = #19#30;
     #0#10 = #19#2;

     aArt = #0#7; |QBF aArt;
     aPar = #0#2; |QBF aPar;
     |SI aArt = "";
          |HAZ UltLin;
          aAlfa = "0000Revise línea '" + nLin + "' del albarán." + &13 + "Artículo con dos piezas.";
          |MENAV aAlfa;
          |BORRA 020#0a;
          |ACABA;
     |FINSI;
     |SI aPar = "";
          aAlfa = "0000Artículo '" + aArt + "' " + #19#1; |QBF aAlfa;
          aAlfa = aAlfa + &13 + "Sin pieza introducida.";
          |MENAV aAlfa;
          |BORRA 020#0a;
          |ACABA;
     |FINSI;

     #24#0 = #0#7;
     #24#1 = 1;
     #24#2 = #0#2;
     |LEE 000#24=;
     |SI FSalida = 0;
          #0#6 = #24#22;
          |GRABA 020#0a;
     |SINO;
          aAlfa = "0000Pieza '" + aPar + "' inexistente."+ &13 +"Artículo '" + aArt + "' " + #19#1;
          |QBF aAlfa;
          |MENAV aAlfa;
          |BORRA 020#0a;
     |FINSI;
|FPRC;

|DEFBUCLE PonUni;
     #0#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, PonUni;
|FIN;

|PROCESO Graba;
     |SI aArt ! ""; |O aPar ! "";
          nLin = nLin + 1;
          #0#0 = nLin;
          #0#1 = aArt;
          #0#2 = aPar;
          #0#3 = aSer;
          #0#4 = aPed;
          |GRABA 020#0n;
     |FINSI;
     aArt = ""; aPar = "";
|FPRC;

|PROCESO Caracter;
     |SI aCar = aCr; |O aCar = aLf; |ACABA; |FINSI;

     |SI aTipo = "ALB";
          aMin = #1#33 < "";
          |SI aCar = #1#33; |O aCar = aMin;
               |HAZ Graba;
               nSw = 1;
          |SINO;
               aMin = #1#34 < "";
               |SI aCar = #1#34; |O aCar = aMin;
                    |SI aPar ! ""; |HAZ Graba; |FINSI;
                    nSw = 2;
               |SINO;
                    |SI nSw = 1; aArt = aArt + aCar; |FINSI;
                    |SI nSw = 2; aPar = aPar + aCar; |FINSI;
               |FINSI;
          |FINSI;
     |SINO;
          aMin = #1#35 < "";
          aCarSer = #1#35;         ||es de 2 caracteres
          aCar2 = aCarAnt + aCar;
          |SI aCar2 = aCarSer; |O aCar2 = aMin;
               nSw = 1;
               |HAZ Graba;
               aSer = ""; aPed = "";
          |SINO;
               aMin = #1#36 < "";
               |SI aCar = #1#36; |O aCar = aMin;
                    nSw = 2;
               |SINO;
                    aMin = #1#33 < "";
                    |SI aCar = #1#33; |O aCar = aMin;
                         |HAZ Graba;
                         nSw = 3;
                    |SINO;
                         aMin = #1#34 < "";
                         |SI aCar = #1#34; |O aCar = aMin;
                              nSw = 4;
                         |SINO;
                              ||parche porque #1#35 es de 2 caracteres
                              ||y se concatena el 1§ caracter de este
                              |SI nSw > 1;
                                   aAlfa = #1#35 % 101;
                                   aMin = aAlfa < "";
                                   |SI aCar = aAlfa; |O aCar = aMin;
                                        nSw = 0;
                                   |FINSI;
                              |FINSI;

                              |SI nSw = 1; aSer = aSer + aCar; |FINSI;
                              |SI nSw = 2; aPed = aPed + aCar; |FINSI;
                              |SI nSw = 3; aArt = aArt + aCar; |FINSI;
                              |SI nSw = 4; aPar = aPar + aCar; |FINSI;
                         |FINSI;
                    |FINSI;
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO AbreWorPad;
     |ESPECIFICA "RBASS", aLocal;
     aFic = aLocal + Usuario + ".txt";

     |XABRE "CW", aFic, nHandle;
     |XCIERRA nHandle;

     aAlfa = "cmd " + &34 + "/c START /WAIT wordpad.exe " + aFic + &34;
     |RSYSTEM aAlfa;
|FPRC;

|PROGRAMA;
     aTipo = PARAMETRO % 103;
     aTmp = PARAMETRO % 4;

     |HAZ AbreWorPad;

     |ABRE #1; |LEE 000#1p; |CIERRA #1;
     |ABRE #142; |LEE 000#142p; |CIERRA #142;
     |NOME_DAT #0 aTmp;
     |ABRE #0;

     aCr = &13;
     aLf = &10;
     |XABRE "CB", aFic, nHandle;
     |SI FSalida ] 0;
          |XLEE nHandle, 250, aTmp;
          |PARA; |SI FSalida > 0; |HACIENDO;
               |PARA; |SI aTmp ! ""; |HACIENDO;
                    aCar = aTmp % 101;
                    |SI aTmp = aCar;
                         aTmp = ""; ||con un caracter falla: "aTmp = aTmp % 2"
                    |SINO;
                         aTmp = aTmp % 2;
                    |FINSI;
                    |HAZ Caracter;
                    aCarAnt = aCar;
               |SIGUE;
               |XLEE nHandle, 250, aTmp;
          |SIGUE;
          |XCIERRA nHandle;
          |HAZ Graba;
          |RFBORRA aFic;
          |ABRE #10;
          |ABRE #19;
          |SI aTipo = "ALB";
               |ABRE #24;
               |BUCLE PonUni;
               |CIERRA #24;
          |SINO;
               |ABRE #73;
               |BUCLE PonUniPed;
               |CIERRA #73;
          |FINSI;
          |CIERRA #19;
          |CIERRA #10;
     |SINO;
          |MENAV "0000Error al leer fichero...";
     |FINSI;
     |CIERRA #0;
|FPRO;
