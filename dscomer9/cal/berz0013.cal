|FICHEROS;
     berz0013 #0;
     berm0001 #1;
     berm0002 #2;
     berm0004 #4;
     berm0005 #5;
     berm0010 #10;
     agifa019 #19;
     berw0004 #40;
     berw0005 #50;
     berz0011 #111,MANTE;
     berz0012 #112,MANTE;
     refelin@;
|FIN;

|VARIABLES;
     &Tempo = "";
     &enCtrFin = 0;
     aTmp40 = "";
     aTmp50 = "";
     aTmpLin = "";
     nPanta = 0;
     nGridLin = 0;
     nGridCab = 0;
     nRango = 0;
     nLin = 0;
     nModo = 0;
     nModoLin = 0;
     nForma = 0;
     aAlfa = "";
     nSwSal = 0;
     aPath = "";
     nBloquea = 0;
     nPanEdita = 0;
     nBtnAltaLin = 0;
     nBtnModiLin = 0;
     nBtnBajaLin = 0;
     nGrid = 0;
     i = 0;
     nCamTalla = 0;
     nCamFondo = 0;
     nCamPega = 0;
     aDato = "";
     aArti = "";
     nLinEscan = 0;
     nNume = 0;
     nNum = 0;
     nColor = 0;
     aColor = "";
     aTalla = "";
     aDes = "";
     aIzq = "";
     aDer = "";
     nPos1 = 0;
     nPos2 = 0;
|FIN;

|PROCESO Sustituye;
     aAlfa = FSalida - "03";
     nPos1 = aAlfa;

     nPos2 = 100 + nPos1 - 1;
     aIzq = aArti % nPos2;

     nPos2 = nPos1 + 3;
     aDer = aArti % nPos2;

     aDato = ("000" + aDato) % -103;
     aArti = aIzq + aDato + aDer;
|FPRC;

|PROCESO Lineas;
     aArti = #50#2; aArti = aArti > "";
     aAlfa = aArti - "TAL";
     |SI FSalida > 0;
          aDato = #1nCamTalla;
          |HAZ Sustituye;
          aAlfa = aArti;
     |FINSI;
     aAlfa = aArti - "FON";
     |SI FSalida > 0;
          aDato = #1nCamFondo;
          |HAZ Sustituye;
          aAlfa = aArti;
     |FINSI;
     aAlfa = aArti - "PEG";
     |SI FSalida > 0;
          aDato = #1nCamPega;
          |HAZ Sustituye;
          aAlfa = aArti;
     |FINSI;

     |DDEFECTO #19;
     #19#0 = aArti;
     |LEE 000#19=;

     nLinEscan = nLinEscan + 1;
     |DDEFECTO #2;
     #2#0 = #1#0;
     #2#1 = nLinEscan;
     #2#2 = #19#0;
     #2#3 = #19#1;
     #2#12 = #50#3;
     #2#22 = #50#4;
     #2#23 = #50#5;
     #2#24 = "S";
     |GRABA 020#2n;
|FPRC;

|DEFBUCLE Lineas;
     #50#1;
     ;
     #40#0, 000;
     #40#0, 999;
     ;
     NULL, Lineas;
|FIN;

|PROCESO Grabalo;
     nNum = nCamPega - 36;
     nColor = #40#2 + #1nCamFondo + nNum;
     aColor = ("000" + nColor) % -103;
     aTalla = ("000" + #1nCamTalla) % -103;

     aArti = #2#2; |QBF aArti;
     aAlfa = aArti % -101;
     |SI aAlfa ! "."; aArti = aArti + "."; |FINSI;
     aArti = aArti + aTalla + "." + aColor;

     #5#0 = #1nCamFondo;
     |LEE 000#5=;
     |SI FSalida ! 0; |DDEFECTO #5; |FINSI;

     #10#0 = #40#2;
     |LEE 000#10=;
     |SI FSalida ! 0; |DDEFECTO #10; |FINSI;

     aDes = #2#3; |QBF aDes;
     aDes = aDes + " " + #5#1; |QBF aDes;
     aDes = aDes + " " + #10#1; |QBF aDes;

     #5#0 = #1nCamPega;
     |LEE 000#5=;
     |SI FSalida ! 0; |DDEFECTO #5; |FINSI;

     aDes = aDes + " " + #5#1; |QBF aDes;

     #1#0 = aArti;
     #1#1 = aDes;
     #1#33 = "N";
     |GRABA 000#1n;
     |SI FSalida ! 0;
          aAlfa = "0000Escandallo '" + #1#0; |QBF aAlfa;
          aAlfa = aAlfa + "' existente...";
          |MENAV aAlfa;
     |SINO;
          nLinEscan = 0;
          |BUCLE Lineas;
     |FINSI;
|FPRC;

|PROCESO BusComTipo1;
     |SI #2#22 = 1;
          nLin = #2#1;
          |ERROR10;
     |FINSI;
|FPRC;

|DEFBUCLE BusComTipo1;
     #2#1;
     ;
     #1#0, 000;
     #1#0, 999;
     ;
     NULL, BusComTipo1;
|FIN;

|PROCESO CreaEscan;
     #1#0 = #40#0;
     |LEE 020#1=;
     |SI FSalida = 0;
          nLin = 0;
          |BUCLE BusComTipo1;
          |SI nLin = 0;
               aAlfa = "0000Escandallo '" + #1#0; |QBF aAlfa;
               aAlfa = aAlfa + "' sin componente tipo 1";
               |MENAV aAlfa;
               |ACABA;
          |FINSI;
          #2#0 = #40#0;
          #2#1 = nLin;
          |LEE 020#2=;

          |PARA nCamFondo = 55; |SI nCamFondo [ 59; |HACIENDO nCamFondo = nCamFondo + 1;
               nNume = #1nCamFondo;
               |SI nNume ! 0;
                    |PARA nCamPega = 37; |SI nCamPega [ 46; |HACIENDO nCamPega = nCamPega + 1;
                         nNume = #1nCamPega;
                         |SI nNume ! 0;
                              |PARA nCamTalla = 64; |SI nCamTalla [ 71; |HACIENDO nCamTalla = nCamTalla + 1;
                                   nNume = #1nCamTalla;
                                   |SI nNume ! 0;
                                        |SALVA_DATOS #1;
                                        |SALVA_DATOS #2;
                                        |HAZ Grabalo;
                                        |REPON_DATOS #2;
                                        |REPON_DATOS #1;
                                   |FINSI;
                              |SIGUE;
                         |FINSI;
                    |SIGUE;
               |FINSI;
          |SIGUE;
     |FINSI;
|FPRC;

|DEFBUCLE CreaEscan;
     #40#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, CreaEscan;
|FIN;

|PROCESO BtnCrear;
     |ABRET #1;
     |ABRET #2;
     |ABRE #5;
     |ABRE #10;
     |ABRE #19;
     |BUCLE CreaEscan;
     |CIERRA #19;
     |CIERRA #10;
     |CIERRA #5;
     |CIERRAT #2;
     |CIERRAT #1;

     nSwSal = 1;
     |PTEC 807;
|FPRC;

|PROCESO BorraLin;
     |BORRA 020#50a;
|FPRC;

|DEFBUCLE BorraLin;
     #50#1;
     ;
     #40#0, 001;
     #40#0, 999;
     ;
     NULL, BorraLin;
|FIN;

|PROCESO TipoCom; |TIPO 0;
     |ABRE #4;
     #4#0 = #112#4;
     |LEE 000#4=;
     |SI FSalida = 0;
          #112#5 = #4#1; |PINTA #112#5;
     |SINO;
          |MENAV "0000No existe tipo componente...";
          |ERROR;
     |FINSI;
     |CIERRA #4;
|FPRC;

|PROCESO EditaLin;
     |SI nModoLin = 1; |VENTANA 2501, 3198, -1, 17, "Nuevo linea escando"; |FINSI;
     |SI nModoLin = 2; |VENTANA 2501, 3198, -1, 17, "Modifica linea escandallo"; |FINSI;
     |HAZ PushVenModal;

     |SI nModoLin = 1;
          #refelin@#0 = #111#0;
          #refelin@#1 = 999;
          |LEE 000#refelin@.];
          |SI FSalida = 0;
               |LEE 000#refelin@.a;
          |SINO;
               |LEE 000#refelin@.u;
          |FINSI;
          |SI FSalida = 0; |Y #refelin@#0 = #111#0;
               nLin = #refelin@#1 + 1;
          |SINO;
               nLin = 1;
          |FINSI;
          #refelin@#0 = #111#0;
          #refelin@#1 = nLin;
          |GRABA 020#refelin@.b;
          |DDEFECTO #112;
          #112#0 = #refelin@#0;
          #112#1 = #refelin@#1;
     |SINO;
          |LEE 121#refelin@.=;
          |PARA i = 0; |SI i [ 5; |HACIENDO i = i + 1;
               #112i = #refelin@#i#;
          |SIGUE;
     |FINSI;
     |PINPA #0#112;
     |PINDA #0#112;
     |SI nModoLin = 1; |ENDATOS #1#112; |FINSI;
     |SI nModoLin = 2; |ENDATOS #2#112; |FINSI;
     |SI FSalida = 0; |Y nModoLin ! 4;
          |PARA i = 0; |SI i [ 5; |HACIENDO i = i + 1;
               #refelin@#i# = #112i;
          |SIGUE;
          |GRABA 020#refelin@.a;
          |REFRESCACONTROL nGrid;
     |SINO;
          |SI nModoLin = 1;
               |BORRA 020#refelin@.c;
          |FINSI;
     |FINSI;
     |LIBERA #refelin@;

     |HAZ PopVenModal;
|FPRC;

|PROCESO BtnAltaLin;
     nModoLin = 1; |HAZ EditaLin;
|FPRC;

|PROCESO BtnModiLin;
     |LEE 101#refelin@.=;
     |SI FSalida ! 0; |ACABA; |FINSI;
     nModoLin = 2; |HAZ EditaLin;
     |LIBERA #refelin@;
|FPRC;

|PROCESO BtnBajaLin;
     |LEE 101#refelin@.=;
     |SI FSalida ! 0; |ACABA; |FINSI;
     aAlfa = "0000N¿Borrar linea: " + #refelin@#1 + "?";
     |CONFI aAlfa;
     |SI FSalida = 0;
          |BORRA 020#refelin@.a;
          |REFRESCACONTROL nGrid;
     |FINSI;
     |LIBERA #refelin@;
|FPRC;

|PROCESO EvtLin;
     |SI FSalida = 1; |O FSalida = 2;
          |SELECT #1#refelin@;
          |LEE 000#refelin@.=;
     |FINSI;
     |SI FSalida = 4;
          |HAZ BtnModiLin;
     |FINSI;
|FPRC;

|PROCESO Termi66; |TIPO 66;
     |ABRE #10;
     #10#0 = #111#2;
     |LEE 000#10];
     |CONSULTA_CLAVE #1#10;
     |SI FSalida = 0;
          #111#2 = #10#0; |PINTA #111#2;
     |SINO;
          |ERROR;
     |FINSI;
     |CIERRA #10;
|FPRC;

|PROCESO Termi0; |TIPO 0;
     |ABRE #10;
     #10#0 = #111#2;
     |LEE 000#10=;
     |SI FSalida = 0;
          #111#3 = #10#1; |PINTA #111#3;
     |SINO;
          |MENAV "0000No existe terminaci¢n...";
          |ERROR;
     |FINSI;
     |CIERRA #10;
|FPRC;

|PROCESO Escan; |TIPO 0;
     |SI nModo = 1; |Y nBloquea = 0;
          |DDEFECTO #40;
          #40#0 = #111#0;
          |GRABA 000#40b;
          |SI FSalida ! 0;
               |MENAV "0000Registro existente...";
               |ERROR;
          |SINO;
               |C_M #111#0, 1, "N";
               nBloquea = 1;
               |ESTADO_CONTROL nBtnAltaLin, "ENABLE";
               |ESTADO_CONTROL nBtnModiLin, "ENABLE";
               |ESTADO_CONTROL nBtnBajaLin, "ENABLE";
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO GrabaLin;
     |SALVA_DATOS #refelin@;
     |REPON_DATOS #50;
     |GRABA 020#50n;
|FPRC;

|DEFBUCLE GrabaLin;
     #refelin@#1002;
     ;
     "                    ", 000;
     "zzzzzzzzzzzzzzzzzzzz", 999;
     ;
     NULL, GrabaLin;
|FIN;

|PROCESO CargaTmpLin;
     |SALVA_DATOS #50;
     |REPON_DATOS #refelin@;
     |GRABA 020#refelin@.n;
|FPRC;

|DEFBUCLE CargaTmpLin;
     #50#1;
     ;
     #40#0, 000;
     #40#0, 999;
     ;
     NULL, CargaTmpLin;
|FIN;

|PROCESO Edita;
     |DDEF aPath;
     aAlfa = aPath + "berw0005"; |CARGA_DEF aAlfa, refelin@;
     |HAZ CreaTempo; |NOME_DAT #refelin@#Tempo#; aTmpLin = Tempo; |DELINDEX #refelin@;
     |ABRET #refelin@;
     |SI nModo ! 1; |BUCLE CargaTmpLin; |LEE 000#refelin@.p; |FINSI;

     |SI nModo = 1; |VENTANA 0901, 3198, -1, 17, "Nuevo escandallo"; |FINSI;
     |SI nModo = 2; |VENTANA 0901, 3198, -1, 17, "Modifica escandallo"; |FINSI;
     |HAZ PushVenModal;

     |SI nModo = 1;
          |DDEFECTO #111;
          |C_M #111#0, 1, "S";
     |SINO;
          |LEE 000#40=;
          |PARA i = 0; |SI i [ 3; |HACIENDO i = i + 1;
               #111i = #40i;
          |SIGUE;
          |C_M #111#0, 1, "N";
     |FINSI;

     nBloquea = 0;
     |PINPA #0#111; nPanEdita = FSalida;
     |PINDA #0#111;

     |CONTROL_SIMPLE nPanEdita, "BOTON,{{193}}Alta", 0903, 0913, BtnAltaLin;
     nBtnAltaLin = FSalida;
     |CONTROL_SIMPLE nPanEdita, "BOTON,{{197}}Modificar", 0915, 0925, BtnModiLin;
     nBtnModiLin = FSalida;
     |CONTROL_SIMPLE nPanEdita, "BOTON,{{199}}Baja", 0927, 0937, BtnBajaLin;
     nBtnBajaLin = FSalida;
     |SI nModo = 1; |O nModo = 4;
          |ESTADO_CONTROL nBtnAltaLin, "DISABLE";
          |ESTADO_CONTROL nBtnModiLin, "DISABLE";
          |ESTADO_CONTROL nBtnBajaLin, "DISABLE";
     |FINSI;
     nRango = 4 + 8 + 16 + 32;
     |LINEAL_SIMPLE #1#refelin@, nRango, 1003, 2597, NULL, NULL, EvtLin;
     nGrid = FSalida;

     |SI nModo = 1; |ENDATOS #1#111; |FINSI;
     |SI nModo = 2; |ENDATOS #2#111; |FINSI;
     |SI FSalida = 0;
          |PARA i = 0; |SI i [ 3; |HACIENDO i = i + 1;
               #40i = #111i;
          |SIGUE;
          |BUCLE BorraLin;
          |BUCLE GrabaLin;
          |GRABA 020#40a;
          |REFRESCACONTROL nGridCab;
     |SINO;
          |SI nBloquea = 1;
               |BORRA 020#40a;
          |FINSI;
     |FINSI;
     |LIBERA #40;

     |DESTRUYECONTROL nGrid;
     |FIN_CONTROL_WINDOWS nBtnAltaLin;
     |FIN_CONTROL_WINDOWS nBtnModiLin;
     |FIN_CONTROL_WINDOWS nBtnBajaLin;
     |HAZ PopVenModal;

     |CIERRAT #refelin@;
     Tempo = aTmpLin; |HAZ BoraTempo; |DELINDEX #refelin@;
     |DESCARGA_DEF #refelin@;
|FPRC;

|PROCESO BtnAltaCab;
     nModo = 1;
     |HAZ Edita;
|FPRC;

|PROCESO BtnModiCab;
     |LEE 101#40=;
     |SI FSalida ! 0; |ACABA; |FINSI;
     nModo = 2;
     |HAZ Edita;
     |LIBERA #40;
|FPRC;

|PROCESO BtnBajaCab;
     |LEE 101#40.=;
     |SI FSalida ! 0; |ACABA; |FINSI;

     aAlfa = "0000N¿Borrar registro: " + #40#0 + "?";
     |CONFI aAlfa;
     |SI FSalida = 0;
          |BORRA 020#40a;
          |BUCLE BorraLin;
          |REFRESCACONTROL nGridCab;
     |FINSI;
|FPRC;

|PROCESO Min;
     #50#0 = #40#0;
     #50#1 = 1;
|FPRC;

|PROCESO Max;
     #50#0 = #40#0;
     #50#1 = 999;
|FPRC;

|PROCESO EvtCab;
     |SI FSalida = 1; |O FSalida = 2;
          |LEE 000#40=
          |REFRESCACONTROL nGridLin;
     |FINSI;
     |SI FSalida = 4;
          |HAZ BtnModiCab;
     |FINSI;
|FPRC;

|PROGRAMA;
     |PINPA #0#0; nPanta = FSalida;

     |HAZ CreaTempo; |NOME_DAT #40 Tempo; aTmp40 = Tempo; |DELINDEX #40;
     |HAZ CreaTempo; |NOME_DAT #50 Tempo; aTmp50 = Tempo; |DELINDEX #50;
     |ABRET #40;
     |ABRET #50;

     |CONTROL_SIMPLE nPanta, "BOTON,{{193}}Alta", 1002, 1012, BtnAltaCab;
     |CONTROL_SIMPLE nPanta, "BOTON,{{197}}Modificar", 1014, 1024, BtnModiCab
     |CONTROL_SIMPLE nPanta, "BOTON,{{199}}Baja", 1026, 1036, BtnBajaCab;
     |CONTROL_SIMPLE nPanta, "BOTON,{{137}}Crear escandallos", 1080, 1099, BtnCrear;

     nRango = 4 + 8 + 16 + 32;
     |LINEAL_SIMPLE #1#50, nRango, 2502, 3299, Min, Max, NULL;
     nGridLin = FSalida;
     |LINEAL_SIMPLE #1#40, nRango, 1102, 2399, NULL, NULL, EvtCab;
     nGridCab = FSalida;

     |PARA; |SI; |HACIENDO;
          enCtrFin = nGridCab;
          |HAZ EsperaFin;
          |SI nSwSal = 1; |SAL; |FINSI;
          |CONFI "0000N¨Desea salir?";
          |SI FSalida = 0; |SAL; |FINSI;
     |SIGUE;

     |CIERRAT #40;
     |CIERRAT #50;
     Tempo = aTmp40; |HAZ BoraTempo; |DELINDEX #40;
     Tempo = aTmp50; |HAZ BoraTempo; |DELINDEX #50;
|FPRO;
