|FICHEROS;
   iberz300 #0;

   iber1510 #1;

   agifa501 #10;
   agifa502 #11;

   tiquetca@ #100;
   tiquetli@ #101;
|FIN;

|VARIABLES;
   nCont1 = 0; nCont2  = 0;
   Calc1  = 0; Calc2   = 0;
   nCalc1 = 0; nCalc2  = 0;
   Long   = 0; LongDec = 0;
   nLong     = 0;
   nNegativo = 0;
   Handle    = 0;

   TipoDato = "";
   Cadena = "";
   aLong  = "";
   aAlfa  = ""; aAlfa1 = ""; aAlfa2 = "";
   Vacio1 = ""; Vacio2 = "";
|FIN;

|PROCESO ImportaLins;
   aAlfa = "|NC";
   nCalc1 = #agifa502#aAlfa#; nCalc1 = nCalc1 - 1;
   |PARA nCont2 = 0; |SI nCont2 [ nCalc1; |HACIENDO nCont2 = nCont2 + 1;
      #agifa502#nCont2# = #tiquetli@#nCont2#;
   |SIGUE;
   |GRABA 020#agifa502.n;
|FPRC;

|DEFBUCLE ImportaLins;
#tiquetli@#1003;
;
"     ",000000,001;
"zzzzz",999999,999;
;
NULL,ImportaLins;
|FIN;

|PROCESO Importa;
   aAlfa = "|NC";
   nCalc1 = #agifa501#aAlfa#; nCalc1 = nCalc1 - 1;
   |PARA nCont2 = 0; |SI nCont2 [ nCalc1; |HACIENDO nCont2 = nCont2 + 1;
      #agifa501#nCont2# = #tiquetca@#nCont2#;
   |SIGUE;
   |SALVA_DATOS #agifa501;
   |LEE 000#agifa501.=;
   |SI FSalida = 0;
      |REPON_DATOS #agifa501;
   |SINO;
      |REPON_DATOS #agifa501; |GRABA 020#agifa501.n;
      |BUCLE ImportaLins;
   |FINSI;
|FPRC;

|DEFBUCLE Importa;
#tiquetca@#1002;
;
"     ",000000;
"zzzzz",999999;
;
NULL,Importa;
|FIN;

|PROCESO ExportaLins;
   aAlfa = "|NC";
   nCalc1 = #agifa502#aAlfa#; nCalc1 = nCalc1 - 1;
   |PARA nCont2 = 0; |SI nCont2 [ nCalc1; |HACIENDO nCont2 = nCont2 + 1;
      #tiquetli@#nCont2# = #agifa502#nCont2#;
   |SIGUE;
   |GRABA 020#tiquetli@.n;
|FPRC;

|DEFBUCLE ExportaLins;
#agifa502#1;
;
#agifa501#36,#agifa501#0,001;
#agifa501#36,#agifa501#0,999;
;
NULL,ExportaLins;
|FIN;

|PROCESO Exporta;
   aAlfa = "|NC";
   nCalc1 = #agifa501#aAlfa#; nCalc1 = nCalc1 - 1;
   |PARA nCont2 = 0; |SI nCont2 [ nCalc1; |HACIENDO nCont2 = nCont2 + 1;
      #tiquetca@#nCont2# = #agifa501#nCont2#;
   |SIGUE;
   |GRABA 020#tiquetca@.n;
   |BUCLE ExportaLins;
|FPRC;

|DEFBUCLE Exporta;
#agifa501#1;
12,14;
"     ",000001,#iberz300#0,#iberz300#2;
"zzzzz",999999,#iberz300#1,#iberz300#3;
;
NULL,Exporta;
|FIN;

|PROGRAMA;
   |CLS;
   |CABEZA " Exportacion de tiquets ";
   |PINPA #0#0; |PINDA #0#0;
   |ENDATOS #1#0;
   |SI FSalida = 0;
      |DBASE aAlfa; |QBF aAlfa; aAlfa = aAlfa + "def/agifa501.mas";
      |CARGA_DEF aAlfa, tiquetca@;
      |SI FSalida = 0;
         |DBASE aAlfa; |QBF aAlfa; aAlfa = aAlfa + "def/agifa502.mas";
         |CARGA_DEF aAlfa, tiquetli@;
         aAlfa = "tiquetca"; |NOME_DAT #100 aAlfa;
         aAlfa = "tiquetli"; |NOME_DAT #101 aAlfa;
         |DFICB aAlfa; |PATH_DAT #100 aAlfa; |PATH_DAT #101 aAlfa;
         |SI #0#4 = "E"; |DELINDEX #tiquetca@; |DELINDEX #tiquetli@; |FINSI;
         |ABRE #tiquetca@;   |ABRE #tiquetli@;
         |ABRE #agifa501;    |ABRE #agifa502;
         |LEE 000#tiquetca@.p;
         |SI #0#4 = "E"; |BUCLE Exporta; |SINO; |BUCLE Importa; |FINSI;
         |CIERRA #agifa501;  |CIERRA #agifa502;
         |CIERRA #tiquetca@; |DESCARGA_DEF #tiquetca@;
         |CIERRA #tiquetli@; |DESCARGA_DEF #tiquetli@;
      |SINO;
         |MENAV "    Problemas para crear el fichero ASCII";
         |ACABA;
      |FINSI;
   |FINSI;
|FPRO;
