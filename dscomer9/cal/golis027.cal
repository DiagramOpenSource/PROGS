|FICHEROS;
     golis027 #0;
     gomantho #1;
     tmp2t600 #6;|| Tmp Articulos
     goartmer #12;
     agifa019 #19;
     agifa029 #29;
     go000024 #24;
     goalbpa5 #50;
     agifa065 #65;
     guerw071 #71; || Tmp
     guerw072 #72; || Tmp
|FIN;

|VARIABLES;
     nHayPre = 0;
     &enTotalIni = 0;
     &enTotalFin = 0;
     &Tempo = "";
     aTmp6 = "";
     aTmp71 = "";
     aTmp72 = "";
     &enStockIni = 0;
     &enPcmIni = 0;
     &enPrecio = 0;
     aArti = "";
     nSw = 0;
     fDFecha = @;
     fHFecha = @;
     nAlmacen = 0;
     nValor = 0;
     nPrecio = 0;
     aAlfa = "";
     nNume = 0;
     nHay = 0;
|FIN;

|PROCESO Agrupa; |TIPO 0;
     |SI #0#6 = "S";
          |C_M #0#7, 1, "S";
          |C_M #0#8, 1, "S";
          |C_M #0#9, 1, "S";
          |C_M #0#10, 1, "S";
     |SINO;
          |C_M #0#7, 1, "N";
          |C_M #0#8, 1, "N";
          |C_M #0#9, 1, "N";
          |C_M #0#10, 1, "N";
     |FINSI;
|FPRC;

|PROCESO ExpeAlma; |TIPO 0;
     |SI #0#5 = "E";
          |C_M #0#0, 1, "S";
          |C_M #0#1, 1, "S";
          |C_M #0#4, 1, "N";
          #0#4 = 0;
     |SINO;
          |C_M #0#0, 1, "N";
          |C_M #0#1, 1, "N";
          |C_M #0#4, 1, "S";
          |SI #0#4 = 0; #0#4 = 1; |FINSI;
     |FINSI;
     |PINTA #0#4;
|FPRC;

|PROCESO Tmp2;
     |DDEFECTO #19;
     #19#0 = #72#0;
     |LEE 000#19=;
     |SI #72#2 ! 0;
          |SI #72#3 < 0; #72#3 = 0; |FINSI;
          |PRINT;
     |FINSI;
|FPRC;

|DEFBUCLE Tmp2;
     #72#2;
     ;
     #0#9, #0#7;
     #0#10, #0#8;
     ;
     NULL, Tmp2;
|FIN;

|PROCESO Total;
     enTotalFin = enTotalFin + (#71#1 * #71#2);
|FPRC;

|DEFBUCLE Total;
     #71#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, Total;
|FIN;

|PROCESO BuscaLinea;
     nHayPre = 0;
     #50#0 = #65#11;
     #50#1 = #65#3;
     #50#2 = 1;
     |LEE 000#50];
     |PARA; |SI FSalida = 0; |Y #50#0 = #65#11; |Y #50#1 = #65#3; |HACIENDO;
          |SI #65#0 = #50#3;
               nHayPre = 1;
               |SAL;
          |FINSI;
          |LEE 000#50s;
     |SIGUE;;
|FPRC;

|PROCESO Historico;
     |DDEFECTO #19;
     #19#0 = #65#0;
     |LEE 000#19=;
     |SI #19#137 ! "S";
          |LIBERA #65;
          |ACABA;
     |FINSI;

     nPrecio = #65#14;
     |SI #65#2 = "ME";
          |HAZ BuscaLinea;
          |SI nHayPre = 1;
               nPrecio = #50#9;
          |SINO;
               |DDEFECTO #24;
               #24#0 = #65#11;
               #24#1 = #65#3;
               #24#2 = #65#4;
               |LEE 000#24=;
               |SI FSalida = 0;
                    nPrecio = #24#4;
               |SINO;
                    #12#0 = #65#0;
                    |LEE 000#12=;
                    |SI FSalida = 0; |Y #12#1 ! 0;
                         nPrecio = #12#1;
                    |SINO;
                         nPrecio = #19#9;
                    |FINSI;
                    #24#3 = #65#0;
                    #24#4 = nPrecio;
                    |GRABA 020#24n;
               |FINSI;
          |FINSI;
     |FINSI;

     |SI nSw = 3; |Y aArti ! #65#0;       || El 1§ Registro
          |DDEFECTO #71;
          #71#0 = #65#0;
          |LEE 000#71=;
          aArti = #65#0;
          enStockIni = #71#1;
          enPcmIni = #71#2;
          enTotalIni = enTotalIni + (enStockIni * enPcmIni);
     |FINSI;

     |SI nSw ! 2;
          || para estos movimientos se coge el pcm calculado
          || del movimiento anterior

          |SI #65#2 = "MS"; |O #65#2 = "SS"; |O #65#2 = "AV";
                    |O #65#2 = "FC"; |O #65#2 = "TI"; |O #65#2 = "ES";
                    |O #65#2 = "SF";

               nPrecio = #71#2;
               |SI #65#2 = "ES"; |Y nPrecio = 0; || algunos no traen precio
                    |SI #65#14 = 0;
                         #65#14 = #19#9;
                         |GRABA 020#65a;
                    |FINSI;
                    nPrecio = #65#14;
               |FINSI;
          |FINSI;

          |DDEFECTO #71;
          #71#0 = #65#0;
          |LEE 101#71=;
          |SI FSalida ! 0;
               #71#1 = #65#6;
               |SI #65#6 > 0;
                    #71#2 = nPrecio;
               |FINSI;
               |GRABA 020#71b;
          |SINO;
               |SI #71#1 < 0;
                    #71#1 = #71#1 + #65#6;
                    #71#2 = nPrecio;
               |SINO;
                    |SI #65#6 > 0;
                         nValor = #71#1 * #71#2;
                         nValor = nValor + (#65#6 * nPrecio);
                         #71#1 = #71#1 + #65#6;   || Unidades
                         #71#2 = nValor / #71#1;  || PCM
                    |SINO;
                         #71#1 = #71#1 + #65#6;
                    |FINSI;
               |FINSI;
               |GRABA 020#71a;
          |FINSI;
          |LIBERA #71;
     |FINSI;
     |SI nSw = 3;
          enPrecio = nPrecio;
          |SI #0#6 = "N"; |PRINT; |FINSI;

          |DDEFECTO #72;
          #72#0 = #65#0;
          |LEE 000#72=;
          |SI FSalida ! 0; |GRABA 020#72b; |FINSI;
          #72#1 = #19#1;
          #72#2 = #71#1;
          #72#3 = #71#1 * #71#2;
          #72#4 = #19#2;
          |GRABA 020#72a;
          |LIBERA #72;
     |SINO;
          #6#0 = #65#0;
          |LEE 000#6=;
          |SI FSalida ! 0; |GRABA 020#6n; |FINSI;
     |FINSI;
     nHay = 1;

     |LIBERA #65;
|FPRC;

|DEFBUCLE Historico;
     #65#1;
     5;
     "                    ", fDFecha, "  ", 000000, 00000, "     ", nAlmacen;
     "zzzzzzzzzzzzzzzzzzzz", fHFecha, "zz", 999999, 99999, "zzzzz", nAlmacen;
     be;
     NULL, Historico;
|FIN;

|DEFBUCLE HistoArti;
     #65#1;
     5;
     #6#0, fDFecha, "  ", 000000, 00000, "     ", nAlmacen;
     #6#0, fHFecha, "zz", 999999, 99999, "zzzzz", nAlmacen;
     be;
     NULL, Historico;
|FIN;

|PROCESO TmpArticulo;
     |DDEFECTO #19;
     #19#0 = #6#0;
     |LEE 000#19=;

     nHay = 0;
     |BUCLE HistoArti;
     |SI nHay = 0;
          |DDEFECTO #71;
          #71#0 = #6#0;
          |LEE 000#71=;
          |SI FSalida = 0; |Y #71#1 ! 0; |Y #71#2 ! 0;
               enStockIni = #71#1;
               enPcmIni = #71#2;
               |DDEFECTO #65;
               #65#0 = #6#0;
               #65#1 = "01.01.0000";
               enTotalIni = enTotalIni + (enStockIni * enPcmIni);
               |SI #0#6 = "N"; |PRINT; |FINSI;

               |DDEFECTO #72;
               #72#0 = #65#0;
               |LEE 000#72=;
               |SI FSalida ! 0; |GRABA 020#72b; |FINSI;
               #72#1 = #19#1;
               #72#2 = #71#1;
               #72#3 = #71#1 * #71#2;
               #72#4 = #19#2;
               |GRABA 020#72a;
               |LIBERA #72;
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE TmpArticulo;
     #6#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, TmpArticulo;
|FIN;

|PROCESO Calculo;
     |ABRE #19;
     |ABRE #50;
     |ABRE #24;
     |ABRE #12;

     |DELINDEX #71;
     |DELINDEX #6;
     |ABRE #71;
     |ABRE #6;

     fDFecha = "01.01.0000";
     nNume = #0#2 - 1;
     fHFecha = nNume;
     nSw = 1;
     |BUCLE Historico;

     fDFecha = #0#2;
     fHFecha = #0#3;
     nSw = 2;
     |BUCLE Historico;
     nSw = 3;
     |BUCLE TmpArticulo;

     |CIERRA #6;
     |CIERRA #71;

     |CIERRA #12;
     |CIERRA #24;
     |CIERRA #50;
     |CIERRA #19;

     nHay = 1;
|FPRC;

|PROCESO Expediente;
     nAlmacen = #1#11;

     |ABRE #29;
     #29#0 = nAlmacen;
     |LEE 000#29=;
     |CIERRA #29;

     |HAZ Calculo;
|FPRC;

|PROCESO Almacen;
     nAlmacen = #29#0;
     |HAZ Calculo;
|FPRC;

|DEFBUCLE Expediente;
     #1#1;
     ;
     #0#0, #0#1;
     #0#0, #0#1;
     ;
     NULL, Expediente;
|FIN;

|DEFBUCLE Almacen;
     #29#1;
     ;
     #0#4;
     #0#4;
     ;
     NULL, Almacen;
|FIN;

|PROCESO Tipo3; |TIPO 3;
     |HAZ CreaTempo; aTmp72 = Tempo; |NOME_DAT #72#Tempo#;
     |HAZ CreaTempo; aTmp71 = Tempo; |NOME_DAT #71#Tempo#;
     |HAZ CreaTempo; aTmp6 = Tempo; |NOME_DAT #6#Tempo#;
     |DELINDEX #72;

     Impresora = "Crystal Reports";
     |IMPRE 0;
     |SI FSalida = 0;
          |SI Impresora = "CrystalReport";
               |SI #0#6 = "S";
                    |INFOR "golisa27";
               |SINO;
                    |INFOR "golis027";
               |FINSI;
               |SI FSalida = 0;
                    |ABRE #72;
                    |SI #0#5 = "E";
                         |BUCLE Expediente;
                    |SINO;
                         |BUCLE Almacen;
                    |FINSI;
                    |SI nHay = 0;
                         |MENAV "0000No hay registros...";
                    |FINSI;
                    |CIERRA #72;
                    |BUCLE Total;
                    |SI #0#6 = "S"; ||Agrupado
                         |ABRE #29;
                         #29#0 = #0#4;
                         |LEE 000#29=;
                         |CIERRA #29;

                         |ABRE #19;
                         |BUCLE Tmp2;
                         |CIERRA #19;
                    |FINSI;
                    |PIEINF;
                    |FININF;
               |FINSI;
          |SINO;
               |MENAV "0000Informe confeccionado solo para Crystal Reports";
          |FINSI;
          |FINIMP;
     |FINSI;

     Tempo = aTmp72; |HAZ BoraTempo; |DELINDEX #72;
     Tempo = aTmp71; |HAZ BoraTempo; |DELINDEX #71;
     Tempo = aTmp6; |HAZ BoraTempo; |DELINDEX #6;
|FPRC;
