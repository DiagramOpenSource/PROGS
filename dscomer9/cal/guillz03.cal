|FICHEROS;
     guillz03 #0;
     guillw05 #1;||Temporal;
     agifa010 #10;
     agifa071 #71;
     agifa084 #84;
     agifa069 #69;
|FIN;

|VARIABLES;
     &eaArticulo = "";
     aDFecha = "";
     aHFecha = "";
     mes   = "";
     mesnu = 0;
     informe = "guillz03";
     repre = "";
     abono = 0;
     aAlfa = "";
     f = @;
|FIN;

|PROCESO Defecto; |TIPO 7;
     |SI #0#5 = 0;
          f = ~;
          aAlfa = f % -104;
          #0#5 = aAlfa; |PINTA #0#5;
          #0#6 = aAlfa; |PINTA #0#6;
     |FINSI;
|FPRC;

|PROCESO agifa069;
     |SI #69#2 < #0#2; |O #69#2 > #0#3;
          |ACABA;
     |FINSI;

     mes = #84#1 % 402;||Coge el mes de la fecha;

     |DDEFECTO #1;
     #1#0 = #84#6;||repre;
     #1#1 = #69#2; ||Articulo;
     |LEE 101#1=;
     |SI FSalida = 0;
         mesnu   = mes;
         mesnu   = mesnu + 1;
         #1mesnu = #1mesnu + #69#5; ||Acumula unidades;
         |GRABA 020#1a;
         |LIBERA #1;
     |SINO;
         mesnu   = mes;
         mesnu   = mesnu + 1;
         #1mesnu = #69#5;              ||Acumula unidades;
         |GRABA 020#1b;
     |FINSI;
|FPRC;

|DEFBUCLE agifa084;
     #84#1;
     1, 6;
     "     ", 000001, aDFecha, #0#0;
     "zzzzz", 999999, aHFecha, #0#1;
     ;
     NULL, NULL, agifa069;
|FIN;

|PROCESO agifa071;
     |SI #71#2 < #0#2; |O #71#2 > #0#3;
          |ACABA;
     |FINSI;

     mes = #10#1 % 402;||Coge el mes de la fecha;

     |SI #10#43 = "S";
          abono = -1;
     |SINO;
          abono = 1;
     |FINSI;

     |DDEFECTO #1;
     #1#0 = #10#6; ||repre;
     #1#1 = #71#2; ||Articulo;
     |LEE 101#1=;
     |SI FSalida = 0;
         mesnu   = mes;
         mesnu   = mesnu + 1;
         #1mesnu = #1mesnu + (#71#5 * abono) ;||Acumula unidades;
         |GRABA 020#1a;
         |LIBERA #1;
     |SINO;
         mesnu   = mes;
         mesnu   = mesnu + 1;
         #1mesnu = #71#5 * abono;              ||Acumula unidades;
         |GRABA 020#1b;
     |FINSI;
|FPRC;

|DEFBUCLE agifa010;
     #10#7;
     6;
     aDFecha, "     ", 000001, #0#0;
     aHFecha, "zzzzz", 999999, #0#1;
     ;
     NULL, NULL, agifa071;
|FIN;

|PROCESO impre;
     #1#14=#1#2+#1#3+#1#4+#1#5+#1#6+#1#7+#1#8+#1#9+#1#10+#1#11+#1#12+#1#13;

     |SI repre = ""; repre = #1#0; |FINSI;

     |SI repre ! #1#0;
          repre = #1#0;
          |PIEINF;
          |PRINT;
     |SINO;
          |PRINT;
     |FINSI;
|FPRC;

|DEFBUCLE temporal;
     #1#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL,impre;
|FIN;

|PROCESO Llena569; |TIPO 6;
     eaArticulo = #0Campo;
     |HAZ LlenaArticulo;
     |SI FSalida = 0;
          #0Campo = eaArticulo;
          |PINTA #0Campo;
     |SINO;
          |ERROR; |ACABA;
     |FINSI;
|FPRC;

|PROCESO tempo;|TIPO 3;
     aAlfa = Usuario;
     |NOME_DAT #1 aAlfa; |DELINDEX #1;

     aDFecha = "01." + (("00" + #0#7) % -102) + "." + (("0000" + #0#5) % -104);
     aHFecha = "01." + (("00" + #0#8) % -102) + "." + (("0000" + #0#6) % -104);
     |PARA f = aHFecha; |SI; |HACIENDO f = f + 1;
          aAlfa = f % 402;
          |SI aAlfa ! #0#8; |SAL; |FINSI;
          aHFecha = f;
     |SIGUE;

     |IMPRE 0;
     |SI FSalida = 0;
          |INFOR informe;
          |SI FSalida = 0;
               |PINTA 2224," Calculando Meses ........ ";
               |ABRE #1;
               |SI #0#4 = "A"; |O #0#4 = "B";
                    |BUCLE agifa010;
               |FINSI;
               |SI #0#4 = "F"; |O #0#4 = "B";
                    |BUCLE agifa084;
               |FINSI;
               |CIERRA #1;

               |BUCLE temporal;
               |PIEINF;
               |FININF;
          |FINSI;
          |FINIMP;
     |FINSI;

     |DELINDEX #1;
|FPRC;
