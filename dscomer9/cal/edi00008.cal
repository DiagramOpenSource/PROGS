|FICHEROS;
     edi00008 #0;
     agifa019 #2;   ||Articulos.
     agifa024 #3;   ||Clientes.
     agifa010 #11;
     agifa071 #12;
     dsm00109 #109;

     edi00002 #80;  || Empresa (EDI).
     edi00003 #91;  || Codigos Edi Cliente...

|FIN;

|VARIABLES;
     b35          = "                                   "; || 35 Blancos.
     b27          = "                           ";         || 27 Blancos.
     b22          = "                      ";              || 22 Blancos.
     b15          = "               ";                     || 15 Blancos.
     b7           = "       ";                             ||  7 Blancos.
     b3           = "   ";                                 ||  3 Blancos.
     contenido    = "";
     swk          = 0;
     feca         = "";
     fecm         = "";
     fecd         = "";
     numer        = 0;
     numerl       = 0;
     nnume        = 0;
     anume        = "";
     anume2       = "";
     lineas       = 0;
     ficher       = "";
     desde        = "                              ";
     hasta        = "zzzzzzzzzzzzzzzzzzzzzzzzzzzzzz";
     nTu          = 0;
     handle       = 0;
     qDir         = "";
     aAlfa1       = "";
     aAlfa2       = "";
|FIN;

||INC-LUYE llena_c1;

|PROCESO agifa071;
     |DDEFECTO #2;

     #109#0 = #11#3;
     #109#2 = #12#2;
     |LEE 000#109=;
     |SI FSalida = 0;
          #2#0 = #109#3;
     |SINO;
          #2#0 = #12#2;
     |FINSI;
     |LEE 000#2=;
     ||
     ||---------- Graba Linea Albaran
     ||
     ||---------------------------------- LIN  Linea Articulo.
     contenido = "";                      || LIN = Numero Linea
     numerl = numerl + 1;
     contenido  = "LIN+" + anume + "++" + #2#128 + ":EN'";
     |XGRABA handle , contenido;
     lineas = lineas + 1;
     ||---------------------------------- PIA  Linea Articulo. Cod.Art Proveedor
     contenido = "";                      || PIA = Cod Art.Proveedor
     contenido  = "PIA+1+" +  #2#0 + ":SA'";
     |XGRABA handle , contenido;
     lineas = lineas + 1;
     ||---------------------------------- IMD  Descripcion Articulo
     contenido = "";                      || IMD = Descripcion
     contenido  = "IMD+F++:::" +  #12#4 + "'";
     |XGRABA handle , contenido;
     lineas = lineas + 1;
     ||---------------------------------- QTY  Cantidad
     contenido = "";                      || QTY = Cantidad Albaran.
     contenido  = "QTY+12:" +  #12#5 + "'";
     |XGRABA handle , contenido;
     lineas = lineas + 1;


|FPRC;

|PROCESO agifa071u;
     nTu = nTu + #12#5;
|FPRC;

|PROCESO Pasalos;
     |DDEFECTO #91;
     #91#0 = #11#3;
     |LEE 000#91=;
     |DDEFECTO #3;
     #3#0  = #11#3;
     |LEE 000#3=;
     ||
     ||--------- Graba Cabecera de Albaran.
     ||
     lineas = 0;
     numerl = 0;
     ||---------------------------------- UNH Cabecera Mensaje.....
     contenido = "";
     numer = numer + 1;
     anume = numer;
     aAlfa2 = #11#0;
     aAlfa2 = ("000000" + aAlfa2 ) % -106;
     aAlfa1 = #11#52 +  aAlfa2;
     contenido = "UNH+" + aAlfa1 + "+DESADV:" + "D:" +  "96A:UN:EAN005'";
     |XGRABA handle , contenido;
     lineas = lineas + 1;
     ||---------------------------------- BGM Principio de Mensaje.
     |SI #11#43 = "N";
          contenido = "";                     || 351  = Aviso Expedicion
          contenido = "BGM+351+" +  #11#52 + aAlfa2 +  "+9'";
          |XGRABA handle , contenido;
          lineas = lineas + 1;
     |SINO;
          contenido = "";                     || 35E  = Aviso Devolucion
          contenido = "BGM+35E+" + "9+" + #11#52 + aAlfa2  + "+9'";
          |XGRABA handle , contenido;
          lineas = lineas + 1;
     |FINSI;
     ||---------------------------------- DTM Fecha Albaran.
     contenido = "";                                || #11#1 = Fecha Albaran.
     feca = #11#1 % -104;                           || 102   = Formato AAAAMMDD
     fecm = #11#1 % 402;
     fecd = #11#1 % 102;
     contenido = "DTM+137:" + feca + fecm + fecd + ":102'";
     |XGRABA handle , contenido;
     lineas = lineas + 1;
     ||---------------------------------- RFF Referencias
     contenido = "";                                || DQ    = N§ Albaran
     contenido = "RFF+DQ:"  + #11#52 + "/"+ aAlfa2 + "'";
     |XGRABA handle , contenido;
     lineas = lineas + 1;
     ||---------------------------------- NAD Emisor Mensaje.
     contenido =  "";                               || MS   = Envia Mensaje.
                       ||-------- NAD Codigo EAN-13
     contenido = "NAD+MS+" + #80#6 + "::" + "9+";
                       ||-------- NAD Empresa que envia Mensaje.
     contenido = contenido + "+" + #80#7 + "+" + #80#9 + "+" + #80#10 + "+" + #80#8 + "+" + #80#11 + "'";
     |XGRABA handle , contenido;
     lineas = lineas + 1;
     ||---------------------------------- NAD Receptor Mensaje.
     contenido =  "";                               || MR   = Receptor Mensaje.
                       ||-------- NAD Codigo EAN-13
     contenido = "NAD+MR+" + #91#7 + "::" + "9+";
                       ||-------- NAD Empresa que Recibo Mensaje.
     contenido = contenido + "+" + #11#4 + "+" + #3#8 + "+" + #3#9 + "+" + #3#10 + "+" + #3#183 + "'";
     |XGRABA handle , contenido;
     lineas = lineas + 1;
     ||---------------------------------- NAD Proveedor.
     contenido =  "";                               || SU   = Proveedor.
                       ||-------- NAD Codigo EAN-13
     contenido = "NAD+SU+" + #80#0 + "::" + "9+";
                       ||-------- NAD Nombre Empresa Emisora Factura.
     contenido = contenido + "+" + #80#1 + "+" + #80#3 + "+" + #80#4 + "+" + #80#2 + "+" + #80#5 + "'";
     |XGRABA handle , contenido;
     lineas = lineas + 1;
     ||---------------------------------- NAD Receptor Mercancia.
     contenido =  "";                               || BY   = Comprador.
                       ||-------- NAD Codigo EAN-13
     contenido = "NAD+BY+" + #91#1 + "::" + "9+";
                       ||-------- NAD Nombre Empresa Emisora Factura.
     contenido = contenido + "+" + #11#29 + "+" + #11#30 + "+" + #11#31 + "+" + #11#15 + "+" + #11#62 + "'";
     |XGRABA handle , contenido;
     lineas = lineas + 1;
     ||
     ||
     |BUCLELIN 0 agifa071 #11;
     nTu = 0;
     |BUCLELIN 0 agifa071u #11;
     ||---------------------------------- CNT  Total Unidades
     contenido = "";                      || CNT = Total Unidades.
     contenido  = "CNT+2:" +  nTu + "'";
     |XGRABA handle , contenido;
     lineas = lineas + 1;
     ||---------------------------------- UNT  Total Segmentos
     contenido = "";                      || UNT = Total Segmentos.
     aAlfa1 = #11#52 +  aAlfa2;
     lineas = lineas + 1;
     contenido  = "UNT+" +  lineas + "+" + #11#52 + aAlfa2 + "'";
     |XGRABA handle , contenido;
     ||
     ||
|FPRC;

|DEFBUCLE agifa010;
     #11#3;
     43;
     #0#0, #0#4, #0#9 , #0#2, #0#8;
     #0#1, #0#5, #0#10, #0#3, #0#8;
     ;
     NULL, Pasalos;
|FIN;

|PROCESO Tipo3;
     |HAZ AbreEmpr;
     |HAZ AbreFich;
     |ABRE #109; |SELECT #2#109;
     |ABRE #2;
     |ABRE #3;
     |ABRE #91;
     |BUCLE agifa010;
     |CIERRA #2;
     |CIERRA #3;
     |CIERRA #91;
     |CIERRA #109;
     |HAZ CierFich;
|FPRC;

|PROGRAMA;

     |CLS;
     |CABEZA "Pase Albaranes a EDI";
     |PINPA #0#0;
     |PINDA #0#0;
     |ENDATOS #1#0;
     |SI FSalida = 0;  |HAZ Tipo3;  |FINSI;
|FPRO;

|PROCESO AbreFich;

  |DFICB qDir;
  ficher = qDir + "traaledi.seq";
  |QBF ficher;
  |XABRE "W" , ficher , handle;

|FPRC;

|PROCESO CierFich;

  |XCIERRA handle;

|FPRC;

|PROCESO AbreEmpr;
     |ABRE #80;
     |LEE 000#80p;
     |SI FSalida ! 0;
          |DDEFECTO #80;
     |FINSI;
     |CIERRA #80;
|FPRC;
