|FICHEROS;
     goprexls #0;
     gopre001 #1;
     gopre002 #2;
     gopre003 #3;
     gopre004 #4;
     goartive #5
     goartdes #6;
     gopue002 #7;   ||Des.Pres
     godeprec #8;   ||Des.Capi y Arti
     godeprel #9;   ||Des.Capi y Arti
     goartive #10;  ||Partida C
     goartdes #11;  ||Partida L
     goamplia #12;  ||Des.Partida
     godeivel #13;  ||Des.Parida
     agifa019 #19;
     agifa007 #70;
|FIN;

|VARIABLES;
     aCopia = "";
     aRuta = "";
     nBoton1 = 0;
     aLon = "";
     nLon = 0;
     nLonAnt = 0;
     aAlfa = "";
     nCanal = 0;
     nXls = 0;
     nNum = 0;
     aMensaje = "";
     aAlfA = "";
     aAlfC = "";
     aAlfD = "";
     aAlfK = "";
     aAlfL = "";
     aAlfM = "";
     aCod = "";
     aUni = "";
     aDes = "";
     nUni = 0;
     nImp = 0;
     nTot = 0;
     i = 0;
     nLinCapi = 0;
     nLinParti = 0;
     nLinArti = 0;
     nLinDes = 0;
     aCar = "";
     nPos = "";
     ascii10 = "";
     ascii13 = "";
|FIN;

|PROCESO DesObra;
     aAlfa = "";
     |QBF aDes;
     |SI aDes = ""; |ACABA; |FINSI;

     |PARA i = 1; |SI; |HACIENDO i = i + 1;
          |DDEFECTO #7;
          #7#0 = #1#0;
          #7#1 = #1#1;
          #7#2 = i;
          aLon = aDes % A0;
          |SI aLon > 73;
               aAlfa = aDes % 173;
          |SINO;
               aAlfa = aDes;
          |FINSI;
          #7#3 = aAlfa;
          |GRABA 000#7n;
          aDes = aDes % 74;
          |SI aDes = ""; |O aDes = aAlfa; |SAL; |FINSI;
     |SIGUE;
|FPRC;

|PROCESO DesCapitulo;
     aAlfa = "";
     |QBF aDes;
     |SI aDes = ""; |ACABA; |FINSI;

     |DDEFECTO #8;
     #8#0 = #2#12;
     #8#1 = #1#0;
     #8#2 = #1#1;
     #8#3 = #2#13;
     #8#4 = "";
     |GRABA 000#8n;

     |PARA i = 1; |SI; |HACIENDO i = i + 1;
          |DDEFECTO #9;
          #9#0 = #8#0;
          #9#1 = #8#1;
          #9#2 = #8#2;
          #9#3 = i;
          aLon = aDes % A0;
          |SI aLon > 58;
               aAlfa = aDes % 158;
          |SINO;
               aAlfa = aDes;
          |FINSI;
          #9#4 = aAlfa;
          #9#5 = #8#4;
          |GRABA 000#9n;
          aDes = aDes % 59;
          |SI aDes = ""; |O aDes = aAlfa; |SAL; |FINSI;
     |SIGUE;
|FPRC;

|PROCESO DesPartida;
     aAlfa = "";
     |QBF aDes;
     |SI aDes = ""; |ACABA; |FINSI;
     aCopia = aDes;

     |DDEFECTO #8;
     #8#0 = #3#4;
     #8#1 = #1#0;
     #8#2 = #1#1;
     #8#3 = #3#5;
     #8#4 = #3#2;
     |GRABA 000#8n;

     |DDEFECTO #12;
     #12#0 = #3#4;
     #12#1 = #3#5;
     #12#2 = #3#2;
     |GRABA 000#12n;

     |PARA i = 1; |SI; |HACIENDO i = i + 1;
          |DDEFECTO #9;
          #9#0 = #8#0;
          #9#1 = #8#1;
          #9#2 = #8#2;
          #9#3 = i;
          aLon = aDes % A0;
          |SI aLon > 59;
               aAlfa = aDes % 159;
          |SINO;
               aAlfa = aDes;
          |FINSI;
          #9#4 = aAlfa;
          #9#5 = #8#4;
          |GRABA 000#9n;

          |DDEFECTO #13;
          #13#0 = #12#0;
          #13#1 = i;
          #13#2 = aAlfa;
          |GRABA 000#13n;

          aDes = aDes % 60;
          |SI aDes = ""; |O aDes = aAlfa; |SAL; |FINSI;
     |SIGUE;
|FPRC;

|PROCESO DesArticulo;
     aAlfa = "";
     |QBF aDes;
     |SI aDes = ""; |ACABA; |FINSI;

     |DDEFECTO #8;
     #8#0 = #4#5;
     #8#1 = #1#0;
     #8#2 = #1#1;
     #8#3 = #4#6;
     #8#4 = #4#2;
     |GRABA 000#8n;

     |PARA i = 1; |SI; |HACIENDO i = i + 1;
          |DDEFECTO #9;
          #9#0 = #8#0;
          #9#1 = #8#1;
          #9#2 = #8#2;
          #9#3 = i;
          aLon = aDes % A0;
          |SI aLon > 59;
               aAlfa = aDes % 159;
          |SINO;
               aAlfa = aDes;
          |FINSI;
          #9#4 = aAlfa;
          #9#5 = #8#4;
          |GRABA 000#9n;
          aDes = aDes % 60;
          |SI aDes = ""; |O aDes = aAlfa; |SAL; |FINSI;
     |SIGUE;
|FPRC;

|PROCESO Obra;
     nLinCapi = 0;
     nLinParti = 0;
     nLinArti = 0;
     nLinDes = 0;

     |DDEFECTO #70;
     #70#26 = aCod;
     |LEE 000#70=;
     |SI FSalida ! 0; |GRABA 020#70b; |FINSI;
     #70#27 = aDes;
     |GRABA 020#70a;

     #1#0 = #70#26;
     #1#1 = 999999;
     |LEE 000#1];
     |SI FSalida = 0;
          |LEE 000#1a;
     |SINO;
          |LEE 000#1u;
     |FINSI;
     |SI FSalida = 0; |Y #1#0 = #70#26;
          nNum = #1#1 + 1;
     |SINO;
          nNum = 1;
     |FINSI;
     |DDEFECTO #1;
     #1#0 = #70#26;
     #1#1 = nNum;
     #1#8 = aDes;
     #1#24 = 2;
     #1#7 = "EUR";
     |GRABA 020#1n;
|FPRC;

|PROCESO Capitulo;
     nLinParti = 0;
     nLinArti = 0;
     nLinDes = 0;
     nLinCapi = nLinCapi + 1;
     |DDEFECTO #2;
     #2#0 = #1#0;
     #2#1 = #1#1;
     #2#2 = nLinCapi;
     #2#3 = aCod;
     #2#4 = aDes;
     #2#5 = aUni;
     #2#6 = nUni;
     #2#7 = nTot;
     #2#8 = nTot;
     #2#9 = nTot;
     #2#10 = nTot;
     #2#12 = aCod;
     #2#13 = aDes;
     |GRABA 020#2n;
|FPRC;

|PROCESO Partida;
     nLinArti = 0;
     nLinDes = 0;
     nLinParti = nLinParti + 1;
     |DDEFECTO #3;
     #3#0 = #1#0;
     #3#1 = #1#1;
     #3#2 = #2#12;
     #3#3 = nLinParti;
     #3#4 = aCod;
     #3#5 = aDes;
     #3#6 = aUni;
     #3#7 = nUni;
     #3#8 = nImp;
     #3#9 = nImp;
     #3#10 = nTot;
     #3#11 = nTot;
     #3#14 = #3#7;
     #3#20 = #3#8;
     aAlfa = ("0000" + #3#3) % -104;
     #3#25 = aAlfa;
     |GRABA 020#3n;

     |DDEFECTO #10;
     #10#0 = #3#4;
     #10#1 = #3#5;
     #10#2 = aUni;
     #10#5 = "EUR";
     #10#13 = #2#12;
     #10#14 = #2#13;
     #10#18 = Usuario % 810;
     |GRABA 000#10n;

     |DDEFECTO #19;
     #19#0 = aCod;
     #19#1 = aDes;
     #19#5 = #3#6;
     #19#127 = #3#2;
     #19#134 = aCod;
     |GRABA 000#19n;
|FPRC;

|PROCESO Articulo;
     nLinArti = nLinArti + 1;
     |DDEFECTO #4;
     #4#0 = #1#0;
     #4#1 = #1#1;
     #4#2 = #2#12;
     #4#3 = #3#4;
     #4#4 = nLinArti;
     #4#5 = aCod;
     #4#6 = aDes;
     #4#7 = aUni;
     #4#8 = nUni;
     #4#9 = nImp;
     #4#10 = nImp;
     #4#11 = nTot;
     #4#12 = nTot;
     #4#21 =  #4#8 * #3#7;
     |GRABA 020#4n;

     |SELECT #3#11;
     #11#0 = #4#3;
     #11#2 = #4#5;
     |LEE 000#11=;
     |SI FSalida ! 0;
          nLinDes = nLinDes + 1;
          |DDEFECTO #11;
          |SELECT #1#11;
          #11#0 = #4#3;
          #11#1 = nLinDes;
          #11#2 = #4#5;
          #11#3 = #4#6;
          #11#4 = #4#7;
          #11#5 = #4#8;
          #11#6 = #4#9;
          #11#7 = #11#5 * #11#6;
          |GRABA 020#11n;

          #10#0 = #11#0;
          |LEE 121#10=;
          |SI FSalida = 0;
               #10#4 = "S";
               #10#15 = #10#15 + #11#7;
               |GRABA 020#10a;
               |LIBERA #10;
          |FINSI;
     |FINSI;

     |DDEFECTO #19;
     #19#0 = aCod;
     #19#1 = aDes;
     #19#5 = #4#7;
     #19#127 = #4#2;
     #19#134 = aCod;
     |GRABA 000#19n;
|FPRC;

|PROCESO Registro;
     aLon = aDes % A0;
     |PARA i = 1; |SI i [ aLon; |HACIENDO i = i + 1;
          nPos = i * 100 + 1;
          aCar = aDes % nPos;
          |SI aCar = ascii10; aCar = ""; |FINSI;
          |SI aCar = ascii13; aCar = " "; |FINSI;
          aAlfa = aAlfa + aCar;
     |SIGUE;
     aDes = aCar;

     aCod = aAlfA; |QBF aCod;
     aUni = aAlfC;
     aDes = aAlfD; |QBF aDes; |OEM_ANSI aDes, aDes;
     nUni = aAlfK;
     nImp = aAlfL;
     nTot = aAlfM;
     aLon = aCod % A0;
     nLon = aLon;
     |SI nLon = 2;
          |HAZ Obra;
     |FINSI;
     |SI nLon = 5;
          |HAZ Capitulo;
     |FINSI;
     |SI nLon = 8;
          |HAZ Partida;
     |FINSI;
     |SI nLon = 11;
          |HAZ Articulo;
     |FINSI;
     |SI nLon = 0;
          |SI nLonAnt = 2;
               |HAZ DesObra;
          |FINSI;
          |SI nLonAnt = 5;
               |HAZ DesCapitulo;
          |FINSI;
          |SI nLonAnt = 8;
               |HAZ DesPartida;
          |FINSI;
          |SI nLonAnt = 11;
               |HAZ DesArticulo;
          |FINSI;
     |FINSI;
     nLonAnt = nLon;
|FPRC;

|PROCESO ImportaFic;
     |CARGADLL "agixl97.dll";
     |SI FSalida < 0;
          |MENAV "0000No se puede usar agixl97.dll";
     |SINO;
          aAlfa = #0#0; |QBF aAlfa;
          |ALFACALLDLL "agixl97.dll", "carga_book", aAlfa;
          |ALFACALLDLL "agixl97.dll", "selecciona_hoja", "Hoja1";
          |PARA nXls = #0#1; |SI nXls [ #0#2; |HACIENDO nXls = nXls + 1;
               aMensaje = "0000Procesando linea:" + nXls;
               |MENSA aMensaje;
               aAlfA = "A" + nXls; |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfA;
               aAlfC = "C" + nXls; |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfC;
               aAlfD = "D" + nXls; |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfD;
               aAlfK = "K" + nXls; |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfK;
               aAlfL = "L" + nXls; |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfL;
               aAlfM = "M" + nXls; |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfM;
               |HAZ Registro;
          |SIGUE;
          |ALFACALLDLL "agixl97.dll", "fin_book", "salva";
          |DESCARGADLL "agixl97.dll";
     |FINSI;
|FPRC;

|PROCESO Tipo3; |TIPO 3;
     aAlfa = #0#0; |QBF aAlfa;
     |XABRE "CE", aAlfa, nCanal;
     |SI FSalida ] 0;
          |ABRE #70;
          |ABRE #1;
          |ABRE #2;
          |ABRE #3;
          |ABRE #4;
          |ABRE #5;
          |ABRE #6;
          |ABRE #7;
          |ABRE #8;
          |ABRE #9;
          |ABRE #10;
          |ABRE #11;
          |ABRE #12;
          |ABRE #13;
          |ABRE #19;
          |HAZ ImportaFic;
          |CIERRA #1;
          |CIERRA #2;
          |CIERRA #3;
          |CIERRA #4;
          |CIERRA #5;
          |CIERRA #6;
          |CIERRA #7;
          |CIERRA #8;
          |CIERRA #9;
          |CIERRA #10;
          |CIERRA #11;
          |CIERRA #12;
          |CIERRA #13;
          |CIERRA #19;
          |CIERRA #70;
     |SINO;
          aMensaje = "0000No puedo abrir " + aAlfa;
          |MENAV aMensaje;
     |FINSI;
|FPRC;

|PROCESO Boton1;
     |PTEC 824;
|FPRC;

|PROCESO Browser;
     |SI FSalida ! 10; |ACABA; |FINSI;

     aRuta = #0#0;  |QBF aRuta;
     |SI aRuta = "";
         aRuta = "*.xls";
     |FINSI;
     aRuta = "F" + aRuta;

     |BROWSE aRuta;
     aRuta = aRuta % 2;
     |SI aRuta = "F"; aRuta = ""; |FINSI;

     #0#0 = aRuta;
     |PINTA #0#0;
     |QBF aRuta;
     |SI aRuta = ""; |ERROR; |FINSI;
|FPRC;

|PROCESO Quita; |TIPO 0;
     |ESTADO_CONTROL nBoton1, "DISABLE";
|FPRC;

|PROCESO Pone; |TIPO 7;
     |ESTADO_CONTROL nBoton1, "ENABLE";
|FPRC;

|PROGRAMA;
     ascii10 = &10;
     ascii13 = &13;

     |ABRE #0;
     |LEE 101#0p;
     |SI FSalida ! 0; |GRABA 020#0b; |FINSI;

     |CLS;
     |CONTROL_SIMPLE 0, "BOTON,  Fichero ", 1017, 1025, Boton1;
     nBoton1 = FSalida;
     |HAZ Quita;
     |PINPA #0#0;
     |PINDA #0#0;
     |ENDATOS #1#0;
     |SI FSalida = 0; |GRABA 020#0a; |FINSI;
     |FIN_CONTROL_WINDOWS nBoton1;
     |PULSATECLA;

     |CIERRA #0;
|FPRO;
