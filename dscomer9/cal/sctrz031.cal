|FICHEROS;
     sctrz031 #0;
     sctrm012 #12;
     sctrm036 #36;
     sctrm038 #38;
     sctrm052 #52;
     sctrm057 #57;
     sctrm001 #100;
     sctrz032 #132,MANTE;
     referen@;
|FIN;

|VARIABLES;
     aInf = "";
     &eaCliente = "";
     &eaProg = "";
     &eaNomLogo = "";
     &enPantaLogo = 0;
     &enCtrFin = 0;
     aAlfa = "";
     nModo = 0;
     nPanta = 0;
     nRango = 0;
     nGrid = 0;
     nBtnAlta = 0;
     nBtnModi = 0;
     nBtnBaja = 0;
     nBtnCons = 0;
     nBtnImpr = 0;
     nBtnTodos = 0;
     aReg0 = "";
     nReg2 = 0;
     nBloquea = 0;
     i = 0;
     nId = 0;
     aConte = "";
     nLin = 0;
     nSwFin = 0;
     nSwBaja = 0;
|FIN;

|PROCESO ModReseComi;
     |SI nSwBaja = 0;
          #52#55 = #38#4;
     |SINO;
          #12#0 = #38#2;
          |LEE 000#12=;
          |SI FSalida = 0;
               #52#55 = #12#9;
          |FINSI;
     |FINSI;
     |GRABA 020#52a;
|FPRC;

|DEFBUCLE ModReseComi;
     #52#4;
     ;
     #38#0, "N", "01.01.0000", 0000000;
     #38#0, "N", "31.12.9999", 9999999;
     be;
     NULL, ModReseComi;
|FIN;

|PROCESO LinServi; |TIPO 0;
     |SI nModo = 1; |Y nBloquea = 0;
          |ABRE #12;
          #12#0 = #132#2;
          |LEE 000#12=;
          |SI FSalida ! 0;
               |MENAV "0000No existe l¡neas de servicio...";
               |ERROR;
          |SINO;
               #132#3 = #12#1; |PINTA #132#3;
               |DDEFECTO #38;
               #38#0 = #132#0;
               #38#2 = #132#2;
               |GRABA 000#38b;
               |SI FSalida ! 0;
                    |MENAV "0000Registro existente...";
                    |ERROR;
               |SINO;
                    |C_M #132#0, 1, "N"; |PINTA #132#0;
                    |C_M #132#2, 1, "N"; |PINTA #132#2;
                    nBloquea = 1;

                    aAlfa = "|X000"; aAlfa = #132aAlfa; nId = aAlfa;
                    |ESTADO_CONTROL nId, "ENABLE";
               |FINSI;
          |FINSI;
          |CIERRA #12;
     |FINSI;
|FPRC;

|PROCESO Cliente; |TIPO 0;
     |ABRE #36;
     #36#1 = #132#0;
     |LEE 000#36=;
     |SI FSalida = 0;
          #132#1 = #36#2; |PINTA #132#1;
     |SINO;
          |MENAV "0000No existe c¢digo de cliente...";
          |ERROR;
     |FINSI;
     |CIERRA #36;
|FPRC;

|PROCESO Conte; |TIPO 7;
     aConte = #132Campo;
|FPRC;

|PROCESO Valida; |TIPO 7;
     |SI nModo = 1;
          aAlfa = "|X000"; aAlfa = #132aAlfa; nId = aAlfa;
          |ESTADO_CONTROL nId, "DISABLE";
     |FINSI;
|FPRC;

|PROCESO Edita;
     aReg0 = #38#0;
     nReg2 = #38#2;

     |SI nModo = 1; |VENTANA 2101, 2898, -1, 17, "Nueva Comisi¢n Cliente"; |FINSI;
     |SI nModo = 2; |VENTANA 2101, 2898, -1, 17, "Modifica Comisi¢n Cliente"; |FINSI;
     |SI nModo = 4; |VENTANA 2101, 2898, -1, 17, "Consulta Comisi¢n Cliente"; |FINSI;
     |HAZ PushVenModal;

     |SI nModo = 1;
          |DDEFECTO #132;
          |SI eaCliente ! "";
               |C_M #132#0, 1, "N";
               #132#0 = eaCliente;
               |HAZ Cliente;
          |SINO;
               |C_M #132#0, 1, "S";
          |FINSI;
          |C_M #132#2, 1, "S";
     |SINO;
          |PARA i = 0; |SI i [ 5; |HACIENDO i = i + 1;
               #132i = #38i;
          |SIGUE;
          |C_M #132#0, 1, "N";
          |C_M #132#2, 1, "N";
     |FINSI;

     |PINPA #0#132;
     |PINDA #0#132;

     nBloquea = 0;
     |SI nModo = 1; |ENDATOS #1#132; |FINSI;
     |SI nModo = 2; |ENDATOS #2#132; |FINSI;
     |SI nModo = 4; |PAUSA; |FINSI;
     |SI FSalida = 0; |Y nModo ! 4;
          #38#0 = #132#0;
          #38#2 = #132#2;
          |LEE 121#38=;
          |PARA i = 0; |SI i [ 5; |HACIENDO i = i + 1;
               #38i = #132i;
          |SIGUE;
          |GRABA 020#38a;
          |CONFI "0000S¨Recalcular Comisiones Reservas Pendientes Liquidar?";
          |SI FSalida = 0;
               nSwBaja = 0;
               |BUCLE ModReseComi;
          |FINSI;
          aReg0 = #38#0;
          nReg2 = #38#2;
     |SINO;
          |SI nBloquea = 1;
               #38#0 = #132#0;
               #38#2 = #132#2;
               |BORRA 020#38c;
          |FINSI;
     |FINSI;
     |LIBERA #38;

     |HAZ PopVenModal;

     #38#0 = aReg0;
     #38#2 = nReg2;
     |LEE 000#38=;
     |REFRESCACONTROL nGrid;
|FPRC;

|PROCESO BtnAlta;
     nModo = 1; |HAZ Edita;
     |HAZ TieneLineas;
|FPRC;

|PROCESO BtnModi;
     |LEE 100#38=;
     |SI FSalida ! 0; |ACABA; |FINSI;
     nModo = 2; |HAZ Edita;
|FPRC;

|PROCESO BtnBaja;
     |LEE 100#38=;
     |SI FSalida ! 0; |ACABA; |FINSI;
     aAlfa = "0000N¨Borrar comisi¢n: " + #38#0 + "/" + #38#2;
     |QBF aAlfa;
     aAlfa = aAlfa + "?";
     |CONFI aAlfa;
     |SI FSalida = 0;
          |BORRA 020#38a;
          |CONFI "0000S¨Recalcular Comisiones Reservas Pendientes Liquidar?";
          |SI FSalida = 0;
               |ABRE #12;
               nSwBaja = 1;
               |BUCLE ModReseComi;
               |CIERRA #12;
          |FINSI;
          |HAZ TieneLineas;
          |REFRESCACONTROL nGrid;
     |FINSI;
     |LIBERA #38;
|FPRC;

|PROCESO BtnCons;
     |LEE 000#38=;
     |SI FSalida ! 0; |ACABA; |FINSI;
     nModo = 4; |HAZ Edita;
|FPRC;

|PROCESO Imprime;
     Impresora = "CrystalReport";
     |IMPRE -1;
     |SI FSalida = 0;
          aInf = #57#6; |QBF aInf;
          |INFOR aInf;
          |SI FSalida = 0;
               |PRINT;
               |PIEINF;
               |FININF;
          |SINO;
               aAlfa = "0000Error en in forme:" + aInf;
               |MENAV aAlfa;
          |FINSI;
          |FINIMP;
     |FINSI;
|FPRC;

|PROCESO BtnImpr;
     |LEE 000#38=;
     |SI FSalida ! 0; |ACABA; |FINSI;
     |HAZ Imprime;
|FPRC;

|PROCESO Evento;
     |SI FSalida = 1; |O FSalida = 2;
          |SELECT #1#38;
          |LEE 000#38=;
          |HAZ TieneLineas;
     |FINSI;
     |SI FSalida = 4;
          |HAZ BtnCons;
     |FINSI;
|FPRC;

|PROCESO TieneLineas;
     |DDEF aAlfa;
     aAlfa = aAlfa + "sctrm038";
     |CARGA_DEF aAlfa, referen@;
     |SI FSalida = 0;
          |ABRE #referen@;
          |SI eaCliente = "";
               |LEE 000#referen@.p;
          |SINO;
               #referen@#0 = eaCliente;
               #referen@#1 = 000;
               |LEE 000#referen@.];
               |SI FSalida ! 0; |O #referen@#0 ! eaCliente;
                    FSalida = "1";
               |FINSI;
          |FINSI;
          |SI FSalida = 0;
               |ESTADO_CONTROL nBtnModi, "ENABLE";
               |ESTADO_CONTROL nBtnBaja, "ENABLE";
               |ESTADO_CONTROL nBtnCons, "ENABLE";
               |ESTADO_CONTROL nBtnImpr, "ENABLE";
               |ESTADO_CONTROL nBtnImpr, "ENABLE";
          |SINO;
               |ESTADO_CONTROL nBtnModi, "DISABLE";
               |ESTADO_CONTROL nBtnBaja, "DISABLE";
               |ESTADO_CONTROL nBtnCons, "DISABLE";
               |ESTADO_CONTROL nBtnImpr, "DISABLE";
          |FINSI;
          |CIERRA #referen@;
          |DESCARGA_DEF #referen@;
     |FINSI;
|FPRC;

|PROCESO LogoPanta;
     eaNomLogo = #100#1;
     |HAZ TraeLogoPan;
     enPantaLogo = nPanta;
     |HAZ PintaLogoPan;
|FPRC;

|PROCESO BtnTodos;
     nSwFin = 1;
     eaCliente = "";
     |PTEC 807;
|FPRC;

|PROCESO Min;
     #38#0 = eaCliente;
     #38#2 = 000;
|FPRC;

|PROCESO Max;
     #38#0 = eaCliente;
     #38#2 = 999;
|FPRC;

|PROGRAMA;
     |ABRE #100; |LEE 000#100p; |CIERRA #100;
     |ABRE #57; |LEE 000#57p; |CIERRA #57;

     |PINPA #0#0; nPanta = FSalida;
     |PINDA #0#0;
     |HAZ LogoPanta;

     |CONTROL_SIMPLE nPanta, "BOTON,{{193}}Alta", 1002, 1012, BtnAlta;
     nBtnAlta = FSalida;
     |CONTROL_SIMPLE nPanta, "BOTON,{{197}}Modificar", 1014, 1024, BtnModi;
     nBtnModi = FSalida;
     |CONTROL_SIMPLE nPanta, "BOTON,{{199}}Baja", 1026, 1037, BtnBaja;
     nBtnBaja = FSalida;
     |CONTROL_SIMPLE nPanta, "BOTON,{{155}}Consulta", 1038, 1048, BtnCons;
     nBtnCons = FSalida;
     |CONTROL_SIMPLE nPanta, "BOTON,{{205}}Impresi¢n", 1050, 1060, BtnImpr;
     nBtnImpr = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,{{230}}Todos", 1088, 1098, BtnTodos;
     nBtnTodos = FSalida;

     |ABRET #38;
     |PARA nSwFin = 1; |SI nSwFin = 1; |HACIENDO;
          |HAZ TieneLineas;
          nSwFin = 0;
          nRango = 4 + 8 + 16 + 32;
          |SI eaCliente = "";
               |ESTADO_CONTROL nBtnTodos, "DISABLE";
               |LINEAL_SIMPLE #1#38, nRango, 1102, 2999, NULL, NULL, Evento;
          |SINO;
               |LINEAL_SIMPLE #1#38, nRango, 1102, 2999, Min, Max, Evento;
          |FINSI;
          nGrid = FSalida;
          enCtrFin = nGrid;
          |HAZ EsperaFin;
          |PULSATECLA;
          |DESTRUYECONTROL nGrid;
     |SIGUE;
     |CIERRAT #38;
|FPRO;
