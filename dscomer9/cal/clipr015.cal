|FICHEROS;
 clipr015 #0;
 clipr001 #1;
 clipr079 #79;
 clipr080 #80;
 clipr084 #84,MANTE;
|FIN;

|VARIABLES;
     nModo = 0;
     nForma = 0;
     aBaja = "";
     aDir = "";
     aAlfa = "";
|FIN;

|CALCULO medic;
 #0#18 = #0#11 + #0#12 + #0#13 + #0#14 + #0#15 + #0#16 + #0#17;
 |PINTA #0#18;
|FCAL;


|CALCULO proxima;
 #0#34 = #0#32 + #0#33;
 |PINTA #0#34;
|FCAL;

|CALCULO mayus15;
 #0Campo = #0Campo > " ";
 |PINTA #0Campo;
|FCAL;

|PROCESO CentrosPre;
     #80#4 = #0#2;
     |GRABA 020#80a;
     |LIBERA #80;
|FPRC;

|DEFBUCLE CentrosPre;
     #80#2;
     ;
     #0#0, #0#1;
     #0#0, #0#1;
     be;
     NULL, CentrosPre;
|FIN;

|PROCESO CentrosCto;
     #79#4 = #0#2;
     |GRABA 020#79a;
     |LIBERA #79;
|FPRC;

|DEFBUCLE CentrosCto;
     #79#2;
     ;
     #0#0, #0#1;
     #0#0, #0#1;
     be;
     NULL, CentrosCto;
|FIN;

|CALCULO cuenta;|TIPO 2;
     nModo = (FEntrada / 100) ! 0;
     nForma = 0 +. 1;
     |ABRE #1;
     #1#0 = #0#0;
     |LEE 010#1=;
     |SI FSalida = 0;
       #1#57 = #1#57 +. 1;
       |GRABA 010#1a;
     |FINSI;
     |LIBERA #1;
     |CIERRA #1;

     |SI nModo = 2; |Y nForma = 1;
          |CONFI "0000SActualizar fecha modificacion?";
          |SI FSalida = 0;
               #0#47 = ~;
               |SI aDir ! #0#2;
                    #0#50 = "N"; |PINTA #0#49;
               |FINSI;
          |FINSI;
          |SI #0#38 = "S"; |Y aBaja = "N";
               #0#49 = "N"; |PINTA #0#49;
          |FINSI;
     |FINSI;

     |MODULO aAlfa;                || para que no entre al pulsar el F5
     |SI aAlfa ! "clipr007";       || en las lineas de cto/pres
          |BUCLE CentrosCto;
     |FINSI;
     |SI aAlfa ! "clipr006";
          |BUCLE CentrosPre;
     |FINSI;
|FCAL;

|PROCESO AltaModi; |TIPO 0;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 1;
          #0#46 = ~;
     |FINSI;
|FPRC;

|PROCESO Dir7; |TIPO 7;
     aDir = #0#2;
|FPRC;

|PROCESO Baja7; |TIPO 7;
     aBaja = #0#38;
|FPRC;

|PROCESO FecAlta; |TIPO 0;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 1;
          #0#46 = ~;
     |FINSI;
|FPRC;

|PROCESO CP15; |TIPO 0;
     aAlfa = (("00" + #0#4) % -102) + (("000" + #0#5) % -103);
     #0#6 = aAlfa;
|FPRC;

|PROCESO CtrlBaja49; |TIPO 0;
     |SI #0#49 = "N";
          |C_M #0#38, 1, "S";
     |SINO;
          |C_M #0#38, 1, "N";
     |FINSI;
|FPRC;

|PROCESO CtrlBaja38; |TIPO 0;
     |SI #0#38 = "S";
          |C_M #0#39, 1, "S";
     |SINO;
          |C_M #0#39, 1, "N";
     |FINSI;
|FPRC;

|PROCESO CtrlBaja39; |TIPO 0;
     |C_M #0#39, 0, aAlfa;
     |SI aAlfa = "S"; |Y #0#39 = "01.01.0000";
          |MENAV "0000Debe de indicar una fecha de baja...";
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO FechaBaja; |TIPO 7;
     |C_M #0#39, 0, aAlfa;
     |SI aAlfa = "S"; |Y #0#39 = "01.01.0000";
          #0#39 = ~;
     |FINSI;
|FPRC;

|PROCESO Min84;
     #84#0 = #0#0;
     #84#8 = #0#1;
     #84#1 = 1;
|FPRC;

|PROCESO Max84;
     #84#0 = #0#0;
     #84#8 = #0#1;
     #84#1 = 99;
|FPRC;

|PROCESO DirAnt; |TIPO 0;
     |SI #0#50 = "N"; |Y #0#50 ! Contenido;
          |CONFI "0000S¿Introducir dirección previa al cambio de domicilio?";
          |SI FSalida = 0;
               |PUSHV 0501 2380;
               |ENTLINEAL #1#84, 3, 2, 21, 1, Min84, Max84;
               |POPV;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO CodPostal; |TIPO 0;
     aAlfa = (("00" + #84#4) % -102) + (("000" + #84#5) % -103);
     #84#6 = aAlfa;
|FPRC;
