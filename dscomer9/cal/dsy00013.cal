|| LISTADO DE VENTAS POR CLIENTE / FAMILIA


|FICHEROS;
     dsy00013 #0;   || Listado 1
     tmp00001 #1;   || Temporal listado 1
     agifa010 #2;   || Albaran
     agifa071 #3;   || Lineas
     lisinfor #4;   || Nombres informes de listados standar
     agifa024 #5;   || Clientes
     agifa060 #6;   || Familia
     agifa084 #7;   || Factura Contado
     agifa069 #8;   || Lineas Factura Contado
|FIN;

|VARIABLES;
     &Tempo = "";
     aTmp1 = "";
    nmargen     =  0;
    nleer       =  0;
    nprimera    =  0;
    ant_cliente = "";
    &sw_inf     = 0;
    informe     = "";
|FIN;

|INCLUYE i_varart;

|| ************************ IMPRESION DEL TEMPORAL ***************************
|PROCESO leeclien;
    |DDEFECTO #agifa024;
    #agifa024#0 = #tmp00001#0;
    |LEE 001#agifa024.=;
|FPRC;


|PROCESO imprimir;
|| Tipos de linea: 0 -> Cabecera, 1 -> Linea, 2 -> Total

   |SI nprimera  = 0;
       nprimera = 1;
       |HAZ leeclien;
       sw_inf = 0;
       |PRINT;
   |SINO;
       |SI #tmp00001#0 ! ant_cliente;
           sw_inf = 2;
           |PRINT;
           |HAZ leeclien;
           sw_inf = 0;
           |PRINT;
       |FINSI;
   |FINSI;
   sw_inf = 1;
   |PRINT;
   ant_cliente = #tmp00001#0;
|FPRC;

|DEFBUCLE imprimir;
    #tmp00001#1;
    ;
    "      " ,"  ";
    "zzzzzz", "zz";
    e;
    NULL, imprimir;
|FIN;

|| ************************ CREACION DEL TEMPORAL ***************************
|| Leo la familia para descargarla en el temporal. El informe lo tengo muy
|| cargado de operaciones y cuantas menos cosas haga el informe mejor.
|PROCESO leefami;
   |DDEFECTO #agifa060;
   #agifa060#0 = #tmp00001#1;
   |LEE 001#agifa060.=;
|FPRC;


|| Si ha pasado todos los filtros cargamos en el temporal
|PROCESO cargatemp;
   |DDEFECTO #tmp00001;
   #tmp00001#0 = #agifa071#15;  || Cliente
   #tmp00001#1 = #agifa071#13;  || Familia
   |LEE 101#tmp00001.=;

   |SI FSalida ! 0;
       |GRABA 021#tmp00001.b;
   |FINSI;

   || Sumo valores, tengo en cuenta si es abono o no, multiplicando por dicho
   || campo
   #tmp00001#3 = #tmp00001#3 + ((#agifa071#5) - (2*#agifa071#5*#agifa010#43)); || Unidades
   #tmp00001#4 = #tmp00001#4 + ((#agifa071#9) - (2*#agifa071#9*#agifa010#43)); || Ptas

   nmargen = #agifa071#9 - #agifa071#14;
   nmargen = nmargen - (2*nmargen*#agifa010#43);
   #tmp00001#5 = #tmp00001#5 + nmargen;

   #tmp00001#8 = #tmp00001#8 + #agifa071#14;

   |HAZ leefami;
   #tmp00001#6 = #agifa060#1;     || Nombre de la familia

          ||Nuevo, son los descuentos

     |SI #agifa010#5 ! 0; |O #agifa010#7 ! 0; |O #agifa010#32 ! 0;
          nmargen = #agifa071#9 < #agifa010#5;
          nmargen = nmargen < #agifa010#7;
          nmargen = nmargen < #agifa010#32;
          nmargen = nmargen - (2*nmargen*#agifa010#43); || Ptas
          #tmp00001#7 = #tmp00001#7 + nmargen;
     |FINSI;
   |GRABA 021#tmp00001.a;
   |LIBERA #tmp00001;
|FPRC;

|PROCESO leelin2;
    || Articulo entre limites
    || Familia entre limites
    |SI #agifa071#2 ] #dsy00013#13; |Y #agifa071#2 [ #dsy00013#14;
    |Y #agifa071#13 ] #dsy00013#15 ; |Y #agifa071#13 [ #dsy00013#16;
        || Con coste o sin el
        |SI #dsy00013#10 = "S" ;|Y #agifa071#14 ! 0;
            |HAZ cargatemp;
        |FINSI;
        |SI #dsy00013#10 = "T";
            |HAZ cargatemp;
        |FINSI;
    |FINSI;
|FPRC;

|PROCESO leelin;
    nleer = 0;

    || Este montaje con los SI es porque no se pueden encadenar con parentesis
    || y cuando comprueba que con dos operandos no se cumple la condicion no
    || sigue.

    || Albaranes facturados o no
    |SI #dsy00013#9 ! "T";
       |SI  #dsy00013#9  = "F";|Y #agifa010#38 = "S";
           nleer = 1;
       |SINO;
           |SI  #dsy00013#9  = "S";|Y #agifa010#38 = "N";
              nleer = 1;
           |FINSI;
       |FINSI;
    |SINO;
       nleer = 1;
    |FINSI;

    |SI nleer = 1;
       |BUCLELIN 0leelin2#agifa010;
    |FINSI;

|FPRC;

|DEFBUCLE leealba;
   #agifa010#3;
   ;
   #dsy00013#1,#dsy00013#7,#dsy00013#3,#dsy00013#5;
   #dsy00013#2,#dsy00013#8,#dsy00013#4,#dsy00013#6;
   e;
   NULL, leelin;
|FIN;



|| ************************ COMUN ************************************
|| Mira si existe algun informe predefinido
|PROCESO mirainf;
    |ABRE #lisinfor;
    #lisinfor#0 = "dsy00013";
    |LEE 001#lisinfor.=;
    |SI FSalida = 0;
       informe = #lisinfor#1;
       |QBF informe;
    |SINO;
       informe = "dsy00013";
    |FINSI;

    |CIERRA #lisinfor;
|FPRC;

||___________________________________/ LECTURA DE FACTURAS CONTADO

|PROCESO lee_c;
     |BUCLELIN 2leelin_c#7;
|FPRC;

|PROCESO leelin_c;
    || Articulo entre limites
    || Familia entre limites
     |SI #agifa069#2 ] #dsy00013#13; |Y #agifa069#2 [ #dsy00013#14;
     |Y #agifa069#13 ] #dsy00013#15 ; |Y #agifa069#13 [ #dsy00013#16;
          || Con coste o sin el
          |SI #dsy00013#10 = "S" ;|Y #agifa069#14 ! 0;
               |HAZ cargatemp_c;
          |FINSI;
          |SI #dsy00013#10 = "T";
               |HAZ cargatemp_c;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO cargatemp_c;
   |DDEFECTO #tmp00001;
   #tmp00001#0 = #agifa069#15;  || Cliente
   #tmp00001#1 = #agifa069#13;  || Familia
   |LEE 101#tmp00001.=;

   |SI FSalida ! 0;
       |GRABA 021#tmp00001.b;
   |FINSI;

   || Sumo valores, tengo en cuenta si es abono o no, multiplicando por dicho
   || campo
   #tmp00001#3 = #tmp00001#3 + #agifa069#5;
   #tmp00001#4 = #tmp00001#4 + #agifa069#9;

   nmargen = #agifa069#9 - #agifa069#14;
   #tmp00001#5 = #tmp00001#5 + nmargen;

   #tmp00001#8 = #tmp00001#8 + #agifa069#14;

   |HAZ leefami;
   #tmp00001#6 = #agifa060#1;     || Nombre de la familia

          ||Nuevo, son los descuentos

     |SI #agifa084#5 ! 0; |O #agifa084#7 ! 0; |O #agifa084#32 ! 0;
          nmargen = #agifa069#9 < #agifa084#5;
          nmargen = nmargen < #agifa084#7;
          nmargen = nmargen < #agifa084#32;
          #tmp00001#7 = #tmp00001#7 + nmargen;
     |FINSI;
   |GRABA 021#tmp00001.a;
   |LIBERA #tmp00001;
|FPRC;


|DEFBUCLE lee_conta;
   #agifa084#3;
   ;
   #dsy00013#1,#dsy00013#7,#dsy00013#3,#dsy00013#5;
   #dsy00013#2,#dsy00013#8,#dsy00013#4,#dsy00013#6;
   e;
   NULL, lee_c;
|FIN;

|| ************************** PRINCIPAL ***********************************
|PROGRAMA;
     |CLS;
     |CABEZA "Ventas Cliente por Familia";

     || Abro y cargo el fichero de definicion donde tengo guardados los
     || valores del ultimo listado
     |PINPA #0#dsy00013;
     |ABRE #dsy00013;
     #dsy00013#0 = "dsy00013";
     |LEE 101#dsy00013.=
     |SI FSalida ! 0;
         |GRABA 021#dsy00013.b;
     |FINSI;
     |PINDA #0#dsy00013;
     |ENDATOS #2#dsy00013;
     |SI FSalida =0;
         |IMPRE 0;
         |SI FSalida = 0;
              |HAZ mirainf;
              |INFOR informe;
              |SI FSalida = 0;
                  |ATRI P;
                  |MENSA "0000 Ordenando Albaranes ...";
                  |ATRI p;
                  |HAZ CreaTempo; aTmp1 = Tempo;
                  |NOME_DAT #tmp00001#aTmp1#; |DELINDEX #tmp00001;
                  |ABRE #tmp00001;
                  |ABRE #agifa060;    || Familia
                  |BUCLE leealba;
                  |SI #dsy00013#17 = "S";
                       |ATRI P;
                       |MENSA "0000 Ordenando Facturas Contado ...";
                       |ATRI p;
                       |BUCLE lee_conta;
                  |FINSI;
                  |CIERRA #tmp00001;
                  |CIERRA #agifa060;    || Familia

                  |GRABA 021#dsy00013.a;
                  |LIBERA #dsy00013;
                  |CIERRA #dsy00013;

                  |BLIN 4;
                  |ATRI P;
                  |MENSA "0000 Imprimiendo ...";
                  |ATRI p;

                  || Lo abro aqui porque desde el informe me hacia cosas raras
                  |ABRE #agifa024;
                  |BUCLE imprimir;
                  |CIERRA #agifa024;
                  || Subtotal del ultimo
                  sw_inf = 2;
                  |PRINT;
                  |PIEINF;
                  Tempo = aTmp1; |HAZ BoraTempo; |DELINDEX #tmp00001;
          |FINSI;
          |FININF;
       |FINSI;
     |FINSI;
     |FINIMP;
|FPRO;

|PROCESO Llena001; |TIPO 6;
     eaArticulo = #0Campo;
     |HAZ LlenaArticulo;
     |SI FSalida = 0;
          #0Campo = eaArticulo;
          |PINTA #0Campo;
     |SINO;
          |ERROR; |ACABA;
     |FINSI;
|FPRC;
