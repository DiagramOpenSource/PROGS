|FICHEROS;
     iber0212 #0;
     iber0213 #1;
     iber0214 #2,MANTE;
     iber0215 #3;
     agifa019 #19;
|FIN;

|VARIABLES;
     nModo     = 0;
     nSwMedida = 0;
     aRuta     = "";
     nBoton    = 0;
     aAlfa     = "";
     aDato     = "";
     nHandle   = 0;
     nCampo    = 0;
     aLon      = "";
     i         = 0;
     nPos      = 0;
     aLetra    = "";
     aReg      = "";
     aTab      = "";
 {99}Valor     = "";
|FIN;
컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
|PROCESO ActArticulo;
     #19#0 = #3#1;
     |LEE 121#19=;
     |SI FSalida = 0;
          #19#5 = #3#5;
          |GRABA 020#19a;
          |LIBERA #19;
     |FINSI;
|FPRC;

|DEFBUCLE ActArticulo;
     #3#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, ActArticulo;
|FIN;

|PROCESO BorraLin;
     |BORRA 020#1a;
|FPRC;

|DEFBUCLE BorraLin;
     #1#1;
     ;
     #0#0, "                    ";
     #0#0, "zzzzzzzzzzzzzzzzzzzz";
     be;
     NULL, BorraLin;
|FIN;

|PROCESO ActLineas;
     |DDEFECTO #1;
     #1#0 = #3#0;
     #1#1 = #3#1;
     #1#2 = #3#2;
     #1#3 = #3#3;
     #1#4 = #3#4;
     #1#5 = #3#5;
     |LEE 101#1=;
     |SI FSalida ! 0;
          |GRABA 020#1n;
     |FINSI;
|FPRC;

|DEFBUCLE ActLineas;
     #3#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, ActLineas;
|FIN;

|PROCESO BorraIgual;
     |SI #3#5 = #3#6;
          |BORRA 020#3a;
     |FINSI;
|FPRC;

|DEFBUCLE BorraIgual;
     #3#1;
     ;
     INICIO;
     FINAL;
     be;
     NULL, BorraIgual;
|FIN;

|PROCESO Min;
     #3#0 = #0#0;
     #3#1 = "                    ";
|FPRC;

|PROCESO Max;
     #3#0 = #0#0;
     #3#1 = "zzzzzzzzzzzzzzzzzzzz";
|FPRC;

|PROCESO Graba;
     |DDEFECTO #3;
     #3#0 = #0#0;
     #3#1 = Valor2; || Arti
     #3#2 = Valor3; || Des
     #3#3 = Valor1; || Alma
     #3#4 = Valor7; || Unidades
     #3#5 = Valor4; || Medida
     #19#0 = #3#1;
     |LEE 000#19=;
     |SI FSalida = 0;
          #3#6 = #19#5; ||Medida
          |GRABA 000#3n;
          |SI #3#6 ! #3#5;
               nSwMedida = 1;
          |FINSI;
          |SI FSalida =  100;
               aAlfa = "0000Registro existente " + #3#1;
               |MENAV aAlfa;
          |FINSI;
     |SINO;
          aAlfa = "0000Articulo inexistente " + #3#1;
          |MENAV aAlfa;
     |FINSI;
|FPRC;

|PROCESO Dato;
     nCampo = 1;
     IValor = Valor1<-;
     aLon = aDato % A0;
     aReg = "";
     |PARA i = 1; |SI i [ aLon; |HACIENDO i = i + 1;
          nPos = i * 100 + 1;
          aLetra = aDato % nPos;
          |SI aLetra = aTab;
               Valor = aReg;
               aReg = "";
               nCampo = nCampo + 1;
               IValor = IValor + 1;
          |SINO;
               aReg = aReg + aLetra;
          |FINSI;
          |SI nCampo > 90; |SAL; |FINSI;     || Por si.... :)
     |SIGUE;
     Valor = aReg;
|FPRC;

|PROCESO Importa;
     nSwMedida = 0;
     |ABRE #19;
     aAlfa = Usuario; |QBF aAlfa;
     |NOME_DAT #3 aAlfa;
     |DELINDEX #3;
     |ABRE #3;
     aRuta = #2#1; |QBF aAlfa;
     aTab = &9;
     |XABRE "CU", aRuta, nHandle;
     |SI FSalida ] 0
          |XLEE nHandle, 250, aDato;    || El 1 no...
          |XLEE nHandle, 250, aDato;
          |PARA; |SI FSalida > 0; |HACIENDO;
               |HAZ Dato;
               |HAZ Graba;
               |XLEE nHandle, 250, aDato;
          |SIGUE;
          |XCIERRA nHandle;
     |FINSI;
     |CIERRA #3;

     #1#0 = #0#0;
     #1#1 = "";
     |LEE 000#1];
     |SI FSalida = 0; |Y #1#0 = #0#0;
          |CONFI "0000SBorrar Registros actuales...";
          |SI FSalida = 0;
               |BUCLE BorraLin;
          |FINSI;
     |FINSI;
     |BUCLE ActLineas;

     |SI nSwMedida ! 0;
          |BUCLE BorraIgual;
          |ENTLINEAL #1#3, -1, 4, 22, 1, Min, Max;
          |CONFI "2400SActualizar Fichero Articulo...";
          |SI FSalida = 0;
               |ABRE #19;
               |BUCLE ActArticulo;
               |CIERRA #19;
          |FINSI;
     |FINSI;

     |DELINDEX #3;
     |CIERRA #19;
|FPRC;
컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
|PROCESO Browser;
     |SI FSalida ! 10; |ACABA; |FINSI;
     |ESTADO_CONTROL nBoton, "HIDE";

     aRuta = #2#1;  |QBF aRuta;
     |SI aRuta = "";
         aRuta = "\*.*";
     |FINSI;
     aRuta = "F" + aRuta;

     |BROWSE aRuta;

     aRuta = aRuta % 299;
     #2#1 = aRuta;
     |PINTA #2#1;

     |ESTADO_CONTROL nBoton, "SHOW";
     |ERROR;
|FPRC;

|PROCESO Boton1;
     |PTEC 824;
|FPRC;

|PROCESO Captura; |TIPO 11;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 4; |ACABA; |FINSI;

     |SI FSalida ! 13; |ACABA; |FINSI;

     |PUSHV 0501 2380;
     |PINPA #0#2;
     |PINDA #0#2;
     |CONTROL_SIMPLE 0, "BOTON, Fichero", 1415, 1423, Boton1;
     nBoton = FSalida;

     |ABRE #2;
     |LEE 101#2=;
     |SI FSalida ! 0; |GRABA 020#2b; |FINSI;
     |ENDATOS #2#2;
     |SI FSalida = 0;
          |ESTADO_CONTROL nBoton, "HIDE";
          |HAZ Importa;
          |GRABA 020#2a;
     |FINSI;
     |CIERRA #2;
     |POPV;
     |FIN_CONTROL_WINDOWS nBoton;
     |PONCONTROL 23, "SI";
     |ERROR;
|FPRC;
