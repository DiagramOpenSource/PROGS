|FICHEROS;
     dsy00003 #0;            || Yo
     agifa080 #71;           || Lineas Albaranes
     agifa067 #73;           || Lineas Pedidos
     agifa077 #10;           || Cabeceras Albaranes
     agifa081 #104;          || Cabeceras Pedidos
     agifa019 #19;           || articulos

     dsy00000 #900;
     dsm00005 #905;
     dsm00010 #910;

     dsw00009 #1009;
     dsw00010 #1010;
     dsw00012 #1012;
     dsw00013 #1013;
     agifa142 #142;
|FIN;

|VARIABLES;
     nLinV     = 0;
     aInf      = "";
     &aDivisa  = "";
     &aMoneda  = "";
     &Tempo     = "";
     aTempo1012 = "";
     aTempo1013 = "";
     aTempo1009 = "";
     nPrecioCompra=0;
     nAux1=0;

     &eaTipo      = "";
     &eaSerie     = "";
     &enCodig     = 0;
     &enLinea     = 0;
     &eaArticulo  = "";
     &eaNArticulo = "";
     &enUnidades  = 0;
     &enPrecio    = 0;
     &enSw        = 0;
     &ser_alb     = "";
     &num_alb     = 0;
     &enLineaAlb  = 0;
     &enSwPanta   = 0;
     &ser_ped     = "";
     &num_ped     = 0;
     &lin_ped     = 0;

      aAlfa       = "";
      aAlfa1      = "";
      aSerie      = "";
      nPedido     = 0;
      nConta      = 0;
      nOrden      = 0;
      nPasada     = 0;
      nCampo      = 0;
      nLinea      = 0;
      nSwPasaPie  = 0;
      nSwPasa     = 0;
      nSwPie      = 0;
      nSwCab      = 0;
|FIN;

|PROCESO Divisa77;
     aDivisa = #10#56;
     aMoneda = #10#58;
     |HAZ Divisas077;
|FPRC;

|PROCESO Divisa81;
     aDivisa = #104#27;
     aMoneda = #104#29;
     |HAZ Divisas081;
|FPRC;

|PROCESO AbreTemporales;
     |SI #0#10   = "A";  |Y nSwCab = 1;  |ACABA;  |FINSI;

     |DELINDEX #1012;
     |DELINDEX #1013;
     |DELINDEX #1009;

     |ABRE #1012;
     |ABRE #1013;
     |ABRE #1009;

     nConta = 1;
     nOrden = 1;
|FPRC;

|PROCESO CargaTempo;
     nSwPasa = 1;

     #1012#0  = #73#28;
     #1012#1  = #73#0;
     #1012#2  = #73#1;
     #1012#3  = #73#2;
     #1012#4  = #905#4;
     #1012#9  = #104#4;
     |LEE 101#1012=;
     |SI FSalida ! 0;
         |DDEFECTO #1012;
         #1012#0  = #73#28;
         #1012#1  = #73#0;
         #1012#2  = #73#1;
         #1012#3  = #73#2;
         #1012#4  = #905#4;
         #1012#9  = #104#4;
         #1012#5  = #905#6;
         #1012#6  = #905#7;
         #1012#7  = #73#4;
         #1012#10 = #905#11;
         #1012#11 = #905#9;
         #1012#12 = #905#12;
         #1012#13 = #1012#5 - #1012#12;

         #1012#16 = #73#8; || dto1
         #1012#17 = #73#27;
         #1012#28 = #73#57;
         #1012#18 = #agifa081#1; || fecha pedido

         #1012#22 = #73#13;   ||F.Entrega

         |ABRE #19;
         #19#0=#73#2;
         |LEE 000#19=;
         #1012#19 = #19#128; || CB Articulo
         |CIERRA #19;

         #1012#20 = #73#33;
         #1012#21 = #73#6;

         aAlfa    = #905#5 % 106;  |QBT aAlfa;
         aAlfa    = ("      " + aAlfa) % -106;
         #1012#8  = aAlfa;

         |GRABA 020#1012n;
         |LIBERA #1012;
     |FINSI;

     #1009#0 = #905#4;
     |LEE 101#1009=;
     |SI FSalida ! 0;
         |DDEFECTO #1009;
         #1009#0 = #905#4;
         |GRABA 020#1009b;
     |FINSI;

     #1009#1 = #1009#1 + (#905#6 - #905#12);

     |SI #1009#2 = 0;
         |SI nConta > #900#1;
             nConta = 1;
             nOrden = nOrden + 1;
         |FINSI;

         #1009#2 = nOrden;
         #1009#3 = nConta;
         nConta = nConta + 1;
     |FINSI;

     |GRABA 020#1009a;
     |LIBERA #1009;
|FPRC;

|DEFBUCLE dsm00005;
     #905#1;
     ;
     "PC", #73#28, #73#0, nLinV, "      ";
     "PC", #73#28, #73#0, nLinV, "zzzzzz";
     ;
     NULL, CargaTempo;
|FIN;

|PROCESO TrataCompo;
     nSwPasa = 0;
     nLinV = #73#1;
     |BUCLE dsm00005;

||     |SI nSwPasa ! 0;  |Y  enSwPanta = 0;  |ACABA;  |FINSI;

     #1013#0  = #104#4;
     |LEE 101#1013=;
     |SI FSalida ! 0;
         #1013#0  = #104#4;
         |GRABA 020#1013n;
         |LIBERA #1013
     |FINSI;

     |SI nSwPasa ! 0;  |Y  enSwPanta = 0;  |ACABA;  |FINSI;


     #1012#0  = #73#28;
     #1012#1  = #73#0;
     #1012#2  = #73#1;
     #1012#3  = #73#2;
     #1012#4  = "TOTAL";
     #1012#9  = #104#4;
     |LEE 101#1012=;
     |SI FSalida ! 0;
         |DDEFECTO #1012;
         #1012#0  = #73#28;
         #1012#1  = #73#0;
         #1012#2  = #73#1;
         #1012#3  = #73#2;
         #1012#4  = "TOTAL";
         #1012#9  = #104#4;
         #1012#6  = #73#6;
         #1012#7  = #73#4;
         #1012#8  = "";

         #1012#10 = #73#37;
         #1012#11 = #73#12;
         #1012#5  = #73#5;
         #1012#12 = #73#38;
         #1012#13 = #1012#5 - #1012#12;

         #1012#24 = #73#45;
         #1012#25 = #73#44;
         #1012#23 = #73#41;
         #1012#26 = #73#46;
         #1012#27 = #1012#23 - #1012#26;

         #1012#16 = #73#8; || dto1
         #1012#17 = #73#27;
         #1012#28 = #73#57;
         #1012#18 = #agifa081#1; || fecha pedido

         #1012#22 = #73#13;   ||F.Entrega

         |ABRE #19;
         #19#0=#73#2;
         |LEE 000#19=;
         #1012#19 = #19#128; || CB Articulo
         |CIERRA #19;

         #1012#20 = #73#33;
         #1012#21 = #73#6;

         |GRABA 020#1012n;
         |LIBERA #1012;
     |FINSI;

     #1009#0 = "";
     |LEE 101#1009=;
     |SI FSalida ! 0;
         |DDEFECTO #1009;
         #1009#0 = "";
         |GRABA 020#1009b;
     |FINSI;

     #1009#1 = #1009#1 + (#73#5 - #73#38);
     #1009#2 = 0;
|FPRC;

|PROCESO CargaCabe;
     #1009#0 = #1012#4;
     |LEE 040#1009=;
     |SI FSalida ! 0;  |DDEFECTO #1009;  |FINSI;

     |SI #910#19 ! "S";  nPasada = 0;  |FINSI;

     |SI #1009#2 ! nPasada;  |ACABA;  |FINSI;

     aAlfa   = #1012#4;  |QBF aAlfa;
     aAlfa   = ("      " + aAlfa) % -106

     nCampo   = 1 + (#1009#3 * 3);  #1010nCampo = aAlfa;
     nCampo   = nCampo + 1;         #1010nCampo = #1012#8;
|FPRC;

|DEFBUCLE dsw00012a;
     #1012#1;
     ;
     #1013#0, "     ",      0,   0, "                    ", "      ";
     #1013#0, "zzzzz", 999999, 999, "zzzzzzzzzzzzzzzzzzzz", "zzzzzz";
     ;
     NULL, CargaCabe;
|FIN;

|PROCESO Limpia;
     |PARA nCampo = 6;  |SI nCampo [ 63;  |HACIENDO nCampo = nCampo + 3;
           #1010nCampo = 0;
     |SIGUE;
     #1010#64 = 0;
|FPRC;

|PROCESO CargaImpre;
     #1009#0 = #1012#4;
     |LEE 040#1009=;
     |SI FSalida ! 0;  |DDEFECTO #1009;  |FINSI;

     |SI #910#19 ! "S";  nPasada = 0;  |FINSI;

     |SI #1009#2 ! nPasada;  |ACABA;  |FINSI;

     |SI #1009#2 = 0;
         #1010#0  = #1012#3;
         #1010#1  = #1012#7;
         #1010#2  = #1012#6;
         #1010#3  = #1012#13 * #1012#6;
         #1010#64 = #1012#13;
         #1010#65 = #1012#0;
         #1010#66 = #1012#1;
         #1010#67 = #1012#2;
         #1010#68 = #1012#9;

         #1010#71=#1012#16;
         #1010#72=#1012#17;
         #1010#81=#1012#28;
         #1010#73=#1012#18;
         #1010#74=#1012#19;
         #1010#75=#1012#20;
         #1010#76=#1012#21;

         nAux1=#1012#21 % #1012#16;
         nPrecioCompra=#1012#21 - nAux1;
         nAux1=nPrecioCompra % #1012#17;
         nPrecioCompra=nPrecioCompra-nAux1;
         nAux1=nPrecioCompra % #1012#28;
         nPrecioCompra=nPrecioCompra-nAux1;
         #1010#77=nPrecioCompra;    || precio compra base - dto1 - dto2

         #1010#78 = #1012#22; ||F.Entrega
         #1010#79 = #1012#10; ||Pedidas
         #1010#80 = #1012#12; ||Recibidas

         |SI #1010#64 ! 0;
             enSw  = 0;  |PRINT;
         |FINSI;
         |ACABA;
     |FINSI;

     |SI nLinea = 0;
         aSerie  = #1012#0;
         nPedido = #1012#1;
         nLinea  = #1012#2;
     |FINSI;

     |SI nLinea ! #1012#2; |O nPedido ! #1012#1;  |O aSerie ! #1012#0;
         |SI #1010#64 ! 0;
             enSw  = 1;  |PRINT;
             |HAZ Limpia;
         |FINSI;
         aSerie  = #1012#0;
         nPedido = #1012#1;
         nLinea  = #1012#2;
     |FINSI;

     aAlfa   = #1012#4;  |QBF aAlfa;
     aAlfa   = ("      " + aAlfa) % -106

     nCampo   = 1 + (#1009#3 * 3);  #1010nCampo = aAlfa;
     nCampo   = nCampo + 1;         #1010nCampo = #1012#8;
     nCampo   = nCampo + 1;         #1010nCampo = #1012#13;
     #1010#0  = #1012#3;
     #1010#1  = #1012#7;
     #1010#2  = #1012#6;
     #1010#64 = #1010#6 + #1010#9  + #1010#12 + #1010#15 + #1010#18 + #1010#21;
     #1010#64 = #1010#64 + #1010#24 + #1010#27 + #1010#30 + #1010#33 + #1010#36;
     #1010#64 = #1010#64 + #1010#39 + #1010#42 + #1010#45 + #1010#48 + #1010#51;
     #1010#64 = #1010#64 + #1010#54 + #1010#57 + #1010#60 + #1010#63;
     #1010#3  = #1010#64 * #1010#2;
     #1010#65 = #1012#0;
     #1010#66 = #1012#1;
     #1010#67 = #1012#2;
     #1010#68 = #1012#9;

     #1010#71=#1012#16;
     #1010#72=#1012#17;
     #1010#81=#1012#28;
     #1010#73=#1012#18;
     #1010#74=#1012#19;
     #1010#75=#1012#20;
     #1010#76=#1012#21;

     nAux1=#1012#21 % #1012#16;
     nPrecioCompra=#1012#21 - nAux1;
     nAux1=nPrecioCompra % #1012#17;
     nPrecioCompra=nPrecioCompra-nAux1;
     nAux1=nPrecioCompra % #1012#28;
     nPrecioCompra=nPrecioCompra-nAux1;
     #1010#77=nPrecioCompra;    || precio compra base - dto1 - dto2

     #1010#78 = #1012#22; ||F.Entrega
     #1010#79 = #1012#10; ||Pedidas
     #1010#80 = #1012#12; ||Recibidas
|FPRC;

|DEFBUCLE dsw00012b;
     #1012#1;
     ;
     #1013#0, "     ",      0,   0, "                    ", "      ";
     #1013#0, "zzzzz", 999999, 999, "zzzzzzzzzzzzzzzzzzzz", "zzzzzz";
     ;
     NULL, CargaImpre;
|FIN;

|PROCESO Impresion;
     |SI #910#19 = "S";
         |PARA nPasada = 0;  |SI nPasada [ nOrden;  |HACIENDO nPasada = nPasada + 1;
               aSerie  = "";
               nPedido = 0;
               nLinea = 0;
               |DDEFECTO #1010;
               |SI nPasada ! 0;  |BUCLE dsw00012a;  |FINSI;
               |BUCLE dsw00012b;
               |SI nLinea ! 0;
                   enSw  = 1;  |PRINT;
               |FINSI;
         |SIGUE;
    |SINO;
         |DDEFECTO #1010;
         |BUCLE dsw00012b;
    |FINSI;
|FPRC;

|DEFBUCLE dsw00013;
     #1013#1;
     ;
     "      ";
     "zzzzzz";
     ;
     NULL, Impresion;
|FIN;

|RUTINA ClaveBaja1012;
     #1012#0 = "  ";
     #1012#1 = 1;
     #1012#2 = 1;
     #1012#3 = "                    ";
     #1012#4 = "      ";
     #1012#9 = "      ";
|FRUT;

|RUTINA ClaveAlta1012;
     #1012#0 = "zz";
     #1012#1 = 999999;
     #1012#2 = 999;
     #1012#3 = "zzzzzzzzzzzzzzzzzzzz";
     #1012#4 = "zzzzzz";
     #1012#9 = "zzzzzz";
|FRUT;

|PROCESO ElRemate;
     |SI #0#10  = "A";  |Y nSwPie = 0;  |ACABA;  |FINSI;

     |SI enSwPanta = 1;
         |SI nSwPasaPie = 1;
             |PINPA #0#1012;
             |ATRI R;  |PINTA 1254, "Recibida";  |ATRI 0;
             |PINTA 1276, " ";
             |ENTLINEAL #1#1012, 6, 4, 22, 0, ClaveBaja1012, ClaveAlta1012;
         |SINO;
             |MENAV "     No quedan unidades Pendientes de Recibir";
         |FINSI;
         |ACABA;
     |SINO;
         |SI nOrden ! 0;
             |ABRE #1009;
             |BUCLE dsw00013;
             |CIERRA #1009;
         |FINSI;

         nOrden  = 0;
     |FINSI;

     |CIERRA #1012;  |DELINDEX #1012;
     |CIERRA #1013;  |DELINDEX #1013;
     |CIERRA #1009;  |DELINDEX #1009;
|FPRC;

|PROCESO ImpreHorizo1;
     |HAZ TrataCompo;
|FPRC;

|PROCESO PorAlbaran;
     |SI enLineaAlb ! 0;
         |SI #71#1 ! enLineaAlb;  |ACABA;  |FINSI;
     |FINSI;

     #73#28 = #71#29;
     #73#0  = #71#12;
     #73#1  = #71#21;
     |LEE 040#73=;
     |SI FSalida ! 0;     |ACABA;  |FINSI;

     |SI #73#13 < #0#8;   |ACABA;  |FINSI;
     |SI #73#13 > #0#9;   |ACABA;  |FINSI;
     |SI #73#28 < #0#12;  |ACABA;  |FINSI;
     |SI #73#28 > #0#13;  |ACABA;  |FINSI;
     |SI #73#0  < #0#14;  |ACABA;  |FINSI;
     |SI #73#0  > #0#15;  |ACABA;  |FINSI;

     |SI #73#38 = #73#5;  |ACABA;  |FINSI;

     #104#25 = #73#28;
     #104#0  = #73#0;
     |LEE 040#104=;
     |SI FSalida ! 0;  |DDEFECTO #104;  |FINSI;

     |HAZ TrataCompo;

     nSwPasaPie = 1;
|FPRC;

|DEFBUCLE agifa077;
     #10#3;
     ;
     #0#4, #0#6, #0#0, #0#2;
     #0#5, #0#7, #0#1, #0#3;
     ;
     AbreTemporales, Divisa77, PorAlbaran, NULL, ElRemate;
|FIN;

|PROCESO PorPedidoAlb;
     #104#25 = #73#28;
     #104#0  = #73#0;
     |LEE 040#104=;
     |SI FSalida ! 0;  |DDEFECTO #104;  |FINSI;
     |HAZ Divisa81;

     |SI #73#13 < #0#8;   |ACABA;  |FINSI;
     |SI #73#13 > #0#9;   |ACABA;  |FINSI;
     |SI #104#1 < #0#6;   |ACABA;  |FINSI;
     |SI #104#1 > #0#7;   |ACABA;  |FINSI;

     |SI #73#38 ! 0;      |ACABA;  |FINSI;
     |HAZ TrataCompo;

     nSwPasaPie = 1;
|FPRC;

|DEFBUCLE agifa067Alb;
     #73#3;
     ;
     "                    ", #0#12, #0#14, #0#4;
     "zzzzzzzzzzzzzzzzzzzz", #0#13, #0#15, #0#5;
     ;
     AbreTemporales, PorPedidoAlb, NULL, NULL, ElRemate;
|FIN;

|PROCESO PorPedidoPed;
     #104#25 = #73#28;
     #104#0  = #73#0;
     |LEE 040#104=;
     |SI FSalida ! 0;  |DDEFECTO #104;  |FINSI;
     |HAZ Divisa81;

     |HAZ TrataCompo;

     nSwPasaPie = 1;
|FPRC;

|DEFBUCLE agifa067Ped;
     #73#1;
     ;
     #0#12, #0#14, lin_ped;
     #0#13, #0#15, lin_ped;
     ;
     AbreTemporales, PorPedidoPed, NULL, NULL, ElRemate;
|FIN;

|PROCESO Tipo3;
     |SI enSwPanta = 0;
          |IMPRE 1;
          |SI FSalida ! 0;  |ACABA;  |FINSI;

          |SI #0#16="R";    || informe reducido
               |SI #910#19 = "S";
                    aInf = "dsy10003";
               |SINO;
                    aInf = "dsy20003";
               |FINSI;
          |SINO;            || informe extensivo
               |SI #910#19 = "S";
                    aInf = "dsy30003";
               |SINO;
                    aInf = "dsy40003";
               |FINSI;
          |FINSI;
          aAlfa = #0#17; |QBF aAlfa;
          |SI aAlfa ! "";
               aInf = aAlfa;
          |FINSI;

          |HAZ EsRocai;
          |SI FSalida = 0; aInf = "rocai003"; |FINSI;


          |INFOR aInf;
          |SI FSalida ! 0;
               |MENAV "0000No existe informe...";
               |FINIMP;
               |ACABA;
          |FINSI;
     |FINSI;

     |SI #0#10 = "P";
         #0#11 = "PEDIDOS PARCIALMENTE PENDIENTES DE RECIBIR";

         |SI ser_ped ! ""; |O num_ped ! 0;
             #0#12 = ser_ped;
             #0#13 = ser_ped;
             #0#14 = num_ped;
             #0#15 = num_ped;

             |ABRE #104;
             |BUCLE agifa067Ped;
             |CIERRA #104;
         |SINO;
             |ABRE #73;
             |ABRE #104;
             |BUCLE agifa077;
             |CIERRA #73;
             |CIERRA #104;
         |FINSI;
     |FINSI;

     |SI #0#10 = "T";
         #0#11 = "PEDIDOS TOTALMENTE PENDIENTES DE RECIBIR";
         |ABRE #104;
         |BUCLE agifa067Alb;
         |CIERRA #104;
     |FINSI;

     |SI #0#10 = "A";
         #0#11 = "PENDIENTE DE RECIBIR DE TODOS LOS PEDIDOS";
         nSwPie = 0;
         nSwCab = 0;

         |HAZ AbreTemporales;

         nSwCab = 1;
         |ABRE #73;
         |ABRE #104;
         |BUCLE agifa077;
         |CIERRA #73;

         |BUCLE agifa067Alb;
         |CIERRA #104;

         nSwPie = 1;
         |HAZ ElRemate;
     |FINSI;


     |SI enSwPanta = 0;
         |SI nSwPasaPie = 1;  |PIEINF;  |FINSI;
         |FININF;
         |FINIMP;
     |FINSI;
|FPRC;

|PROGRAMA;
     |ABRE #142; |LEE 000#142p; |CIERRA #142;

     |SI #142#117 = "S";
          |C_V #1012#23, 1, "S";
          |C_V #1012#24, 1, "S";
          |C_V #1012#25, 1, "S";
          |C_V #1012#26, 1, "S";
          |C_V #1012#27, 1, "S";
     |FINSI;

     |CLS;
     |ABRE #0
     |LEE 101#0p;
     |SI FSalida ! 0; |GRABA 020#0b; |FINSI;

     |PDEFECTO #0, 1, 17; #0#0 = "";
     |HAZ EsGuerola;
     |SI FSalida = 0;
          |C_V #0#17, 1, "S";
     |SINO;
          |C_V #0#17, 1, "N";
          #0#17 = "";
     |FINSI;


     |HAZ DecimalesBase;

     |HAZ CreaTempo; aTempo1012 = Tempo;  |NOME_DAT #1012 aTempo1012;
     |HAZ CreaTempo; aTempo1013 = Tempo;  |NOME_DAT #1013 aTempo1013;
     |HAZ CreaTempo; aTempo1009 = Tempo;  |NOME_DAT #1009 aTempo1009;

     |DELINDEX #1012;
     |DELINDEX #1013;
     |DELINDEX #1009;

     |ABRE #910;
     |LEE 040#910p;
     |SI FSalida ! 0;  |DDEFECTO #910;  |FINSI;
     |CIERRA #910;

     |ABRE #900;
     |LEE 040#900p;
     |SI FSalida ! 0;  |DDEFECTO #900;  |FINSI;
     |CIERRA #900;

     |SI ser_alb = ""; |Y num_alb = 0; |Y ser_ped = "";  |Y num_ped = 0;
         |CLS;
         |CABEZA "Pdtes.de Recibir";
         |PINPA #0#0;
         |PINDA #0#0;
         |ENDATOS #1#0;
         |SI FSalida = 0;
               |GRABA 020#0a;
               |HAZ Tipo3;
         |FINSI;
     |FINSI;

     |SI ser_ped ! ""; |O num_ped ! 0;
         #0#10 = "P";
         |HAZ Tipo3;
     |FINSI;

     |SI ser_alb ! ""; |O num_alb ! 0;
         #0#0  = ser_alb;
         #0#1  = ser_alb;
         #0#2  = num_alb;
         #0#3  = num_alb;
         #0#10 = "P";
         |HAZ Tipo3;
     |FINSI;

     |CIERRA #1012;  |DELINDEX #1012; Tempo = aTempo1012; |HAZ BoraTempo;
     |CIERRA #1013;  |DELINDEX #1013; Tempo = aTempo1013; |HAZ BoraTempo;
     |CIERRA #1009;  |DELINDEX #1009; Tempo = aTempo1009; |HAZ BoraTempo;

     |CIERRA #0;
|FPRO;
