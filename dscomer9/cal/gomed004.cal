|FICHEROS;
     gomed001 #1;        ||PROYECTOS
     gomed002 #2;        ||PROYECTOS-PLANTAS (LIN)
     gomed003 #3;        ||PROYECTOS-PLANTAS-VIVIENDAS (LIN)
     gomed004 #4;        ||Proyectos medidas PROYECTOS
     gomed005 #5,MANTE;  ||Proyectos medidas PROYECTOS-CAPITULOS (LIN)
     gomed006 #6,MANTE;  ||Proyectos medidas PROYECTOS-CAPITULOS-PARTIDAS (LIN)
     gomed007 #7,MANTE;  ||Proyectos medidas PROYECTOS-CAPITULOS-PARTIDAS-MEDIDAS (LIN)
     gomed010 #10;       ||PLANTILLAS (CAB)
     gomed011 #11;       ||PLANTILLAS-CAPITULOS (LIN)
     gomed012 #12;       ||PLANTILLAS-CAPITULOS-PARTIDAS (LIN)
     gotmpme1 #13;       ||Temporal CAPITULOS
     gofamive #14;       ||FAMILIAS
     gosubive #15;       ||SUBFAMILIAS
     gomedive #16;       ||MEDIDAS
     gotmpart #17,MANTE; ||Temporal articulos IVE
     gomed013 #18;       ||Proyectos medidas BLOQUES/PLANTAS/VIVIENDAS
|FIN;
-----------------------------------------------------------------------------
|VARIABLES;
     &Tempo = "";
     aTmp13 = "";
     Tecla     = 0;
     nModo     = 0;
     nnModo    = 0;
     cFich     = "";
     nLin5     = 0;
     nLin6     = 0;
     nLin13    = 0;
     nLinea    = 0;
     cCampo    = "";
     nBotParti = 0;
     nBotCap   = 0;
     nBotPar   = 0;
     nBotMed   = 0;
     &cParam   = "";
     &cPart    = "";
     &cPartAct = "";
|FIN;
-----------------------------------------------------------------------------
|PROCESO RellenaPro; |TIPO 0;
     nModo = (FEntrada/100)!0;
     |SI nModo = 1;
          |LEE 000#4=;
          |SI FSalida = 0;
               |MENAV "0000ALTA EXISTENTE. PROCESO INACCESIBLE"
               |ERROR10;
               |ACABA;
          |FINSI;
          |ABRE #18;
          |ABRE #1;
          #1#0 = #4#0;                  ||BUSCO EL PROYECTO PARA RELLENAR
          |LEE 000#1=;                  ||LOS BLOQUES-PLANTAS-VIVIENDAS
          |SI FSalida = 0;
               nLin13 = 0;
               nLin13 = nLin13 + 1;
               #18#1 = nLin13;          ||LINEA
               #18#2 = 1;               ||BLOQUE
               |GRABA 020#18c;
               |HAZ RellenaCap;
               |BUCLELIN 2 Plantas #1;
          |FINSI;
          #4#3 = "S";
          |CIERRA #1;
          |CIERRA #18;
     |FINSI;
|FPRC;
-----------------------------------------------------------------------------
|PROCESO Viviendas;
     nLin13 = nLin13 + 1;
     #18#1 = nLin13;               ||LINEA
     #18#2 = 1;                    ||BLOQUE
     #18#3 = #2#1;                 ||PLANTAS
     #18#4 = #3#2;                 ||VIVIENDAS
     |GRABA 020#18c;
     |HAZ RellenaCap;
|FPRC;
-----------------------------------------------------------------------------
|DEFBUCLE Viviendas;
     #3#1;
     ;
     #4#0,#2#1,1;
     #4#0,#2#1,999;
     ;
     NULL,Viviendas;
|FIN;
-----------------------------------------------------------------------------
|PROCESO Plantas;
     nLin13 = nLin13 + 1;
     #18#1 = nLin13;
     #18#2 = 1;                    ||BLOQUE
     #18#3 = #2#1;                 ||PLANTAS
     |GRABA 020#18c;
     |HAZ RellenaCap;
     |BUCLE Viviendas;
     #18#4 = 0;
|FPRC;
-------------------------------------------------------------------------------
|PROCESO MiraSubFam;
     |DDEFECTO #13;
     #13#0 = #15#0;
     #13#1 = #15#1;
     |GRABA 020#13c;
|FPRC;
-------------------------------------------------------------------------------
|DEFBUCLE MiraSubFam;
     #15#4;
     ;
     #14#0;
     #14#0;
     ;
     NULL, MiraSubFam;
|FIN;
-------------------------------------------------------------------------------
|PROCESO MiraFami;
     |BUCLE MiraSubFam;
|FPRC;
-------------------------------------------------------------------------------
|DEFBUCLE MiraFami;
     #14#4;
     ;
     #10#2;
     #10#2;
     ;
     NULL, MiraFami;
|FIN;
-----------------------------------------------------------------------------
|PROCESO MiraCap; |TIPO 7;
     |DELINDEX #13;
     |ABRE #13;
     |ABRE #10;
     #10#0 = #4#2;
     |LEE 000#10=;
     |SI FSalida = 0;
          |BUCLE MiraFami;
     |FINSI;
     |CIERRA #13;
     |CIERRA #10;
|FPRC;
-----------------------------------------------------------------------------
|PROCESO PintaPar; |TIPO 7;
     |ENTLINEAL #1#6,-6,7,15,0,MiniPar,MaxiPar;
     |ENTLINEAL #1#7,-7,7,22,0,MiniMed,MaxiMed;
|FPRC;
-----------------------------------------------------------------------------
|PROCESO EntPar; |TIPO 11;
     |SI nModo = 1; |O nModo = 2;
          |SI FSalida = 10;
               |SI #5#2 = "   ";
                    |MENAV "0000DEBE INTRODUCIR UN CAPITULO";
                    |ERROR10;
                    |ACABA;
               |FINSI;
               |ENTLINEAL #1#6,6,2,15,0,MiniPar,MaxiPar;
               |ERROR10;
               |ESTADO_CONTROL nBotPar, "SHOW";
          |FINSI;
     |FINSI;
     |SI nModo = 4;
          |SI FSalida = 10;
               |ENTLINEAL #1#6,6,4,15,0,MiniPar,MaxiPar;
               |ERROR10;
               |ESTADO_CONTROL nBotPar, "SHOW";
          |FINSI;
     |FINSI;
|FPRC;
-----------------------------------------------------------------------------
|PROCESO MiniMed;
     #7#0 = #4#0;
     #7#9 = #18#2;
     #7#10 = #18#3;
     #7#11 = #18#4;
     #7#1 = #5#2;
     #7#2 = #6#3;
     #7#3 = 1;
|FPRC;
-----------------------------------------------------------------------------
|PROCESO MaxiMed;
     #7#0 = #4#0;
     #7#9 = #18#2;
     #7#10 = #18#3;
     #7#11 = #18#4;
     #7#1 = #5#2;
     #7#2 = #6#3;
     #7#3 = 999;
|FPRC;
-----------------------------------------------------------------------------
|PROCESO EntMed; |TIPO 11;
     |SI nModo = 1; |O nModo = 2;
          |SI FSalida = 10;
               |SI #6#3 = "                    ";
                    |MENAV "0000DEBE INTRODUCIR UNA PARTIDA";
                    |ERROR10;
                    |ACABA;
               |FINSI;
               |ABRE #16;
               #16#0 = #6#5;
               |LEE 000#16=;
               |SI FSalida = 0;
                    |SI #16#2 = "N";
                         |MENAV "0000MEDICION NO PERMITIDA. REVISE UNIDAD DE MEDIDA";
                         |ERROR10;
                         |ESTADO_CONTROL nBotMed, "SHOW";
                         |ACABA;
                    |FINSI;
               |FINSI;
               |CIERRA #16;
               |ENTLINEAL #1#7,7,2,22,0,MiniMed,MaxiMed;
               |ERROR10;
               |ESTADO_CONTROL nBotMed, "SHOW";
          |FINSI;
     |FINSI;
     |SI nModo = 4;
          |SI FSalida = 10;
               |ENTLINEAL #1#7,7,4,22,0,MiniMed,MaxiMed;
               |ERROR10;
               |ESTADO_CONTROL nBotMed, "SHOW";
          |FINSI;
     |FINSI;
|FPRC;
-----------------------------------------------------------------------------
|PROCESO MuMed; |TIPO 7;
     |ENTLINEAL #1#7,-7,7,22,0,MiniMed,MaxiMed;
|FPRC;
-----------------------------------------------------------------------------
|PROCESO PideMed; |TIPO 7;
     |ESTADO_CONTROL nBotMed, "HIDE";
     |ABRE #16;
     #16#0 = #6#5;
     |LEE 000#16=;
     |SI FSalida = 0;
          |SI #16#3 = "S";
               |C_M #7#5, 1, "S";
          |SINO;
               |C_M #7#5, 1, "N";
          |FINSI;
          |SI #16#4 = "S";
               |C_M #7#6, 1, "S";
          |SINO;
               |C_M #7#6, 1, "N";
          |FINSI;
          |SI #16#5 = "S";
               |C_M #7#7, 1, "S";
          |SINO;
               |C_M #7#7, 1, "N";
          |FINSI;
     |FINSI;
     |CIERRA #16;
|FPRC;
-----------------------------------------------------------------------------
|PROCESO Calcula; |TIPO 2;
     |SI #7#5 ! 0; |Y #7#6 ! 0;|Y #7#7 ! 0;  ||LARGO, ANCHO Y ALTO
          #7#8 = #7#4 * #7#5 * #7#6 * #7#7;
     |FINSI;
     |SI #7#5 ! 0; |Y #7#6 ! 0;|Y #7#7 = 0;  ||LARGO Y ANCHO
          #7#8 = #7#4 * #7#5 * #7#6;
     |FINSI;
     |SI #7#5 ! 0; |Y #7#6 = 0;|Y #7#7 ! 0;  ||LARGO Y ALTO
          #7#8 = #7#4 * #7#5 * #7#7;
     |FINSI;
     |SI #7#5 ! 0; |Y #7#6 = 0;|Y #7#7 = 0;  ||LARGO
          #7#8 = #7#4 * #7#5;
     |FINSI;
     |SI #7#5 = 0; |Y #7#6 ! 0;|Y #7#7 ! 0;  ||ANCHO Y ALTO
          #7#8 = #7#4 * #7#6 * #7#7;
     |FINSI;
     |SI #7#5 = 0; |Y #7#6 ! 0;|Y #7#7 = 0;  ||ANCHO
          #7#8 = #7#4 * #7#6;
     |FINSI;
     |SI #7#5 = 0; |Y #7#6 = 0;|Y #7#7 ! 0;  ||ALTO
          #7#8 = #7#4 * #7#7;
     |FINSI;
     |PINTA #7#8;
|FPRC;
-----------------------------------------------------------------------------
|PROCESO No0; |TIPO 0;
     |C_M #7Campo, 0, cCampo;
     |SI #7Campo = 0; |Y cCampo = "S";
          |MENAV "0000TIENE QUE ASIGNAR LA MEDIDA EN ESTE CAMPO";
          |ERROR10;
     |FINSI;
|FPRC;
-----------------------------------------------------------------------------
|PROCESO PintaTot; |TIPO 13;
     |ENTLINEAL #1#5,-5,7,22,0,MiniCap,MaxiCap;
     |ENTLINEAL #1#6,-6,7,15,0,MiniPar,MaxiPar;
     |ENTLINEAL #1#7,-7,7,22,0,MiniMed,MaxiMed;
|FPRC;
-----------------------------------------------------------------------------
|PROCESO BorraMed;
     |BORRA 010#7a;
|FPRC;
-----------------------------------------------------------------------------
|DEFBUCLE BorraMed;
     #7#1;
     ;
     #4#0,#18#2,#18#3,#18#4,#5#2,#6#3,1;
     #4#0,#18#2,#18#3,#18#4,#5#2,#6#3,999;
     ;
     NULL, BorraMed;
|FIN;
-----------------------------------------------------------------------------
|PROCESO BorraPar;
     |BUCLE BorraMed;
     |BORRA 010#6a;
|FPRC;
-----------------------------------------------------------------------------
|DEFBUCLE BorraPar;
     #6#1;
     ;
     #4#0,#18#2,#18#3,#18#4,#5#2,1;
     #4#0,#18#2,#18#3,#18#4,#5#2,999;
     ;
     NULL, BorraPar;
|FIN;
-----------------------------------------------------------------------------
|PROCESO BorraCap;
     |BUCLE BorraPar;
     |BORRA 010#5a;
|FPRC;
-----------------------------------------------------------------------------
|DEFBUCLE BorraCap;
     #5#1;
     ;
     #4#0,#18#2,#18#3,#18#4,1;
     #4#0,#18#2,#18#3,#18#4,999;
     ;
     NULL, BorraCap;
|FIN;
-----------------------------------------------------------------------------
|PROCESO BajaLin; |TIPO 2;
     nModo = (FEntrada/100)!0;
     |SI nModo = 3;
          |BUCLE BorraCap;
     |FINSI;
|FPRC;
-----------------------------------------------------------------------------
|PROCESO BajaCap; |TIPO 2;
     nModo = (FEntrada/100)!0;
     |SI nModo = 3;
          |BUCLE BorraPar;
     |FINSI;
|FPRC;
-----------------------------------------------------------------------------
|PROCESO BajaMed; |TIPO 2;
     nModo = (FEntrada/100)!0;
     |SI nModo = 3;
          |BUCLE BorraMed;
     |FINSI;
|FPRC;
-----------------------------------------------------------------------------
|PROCESO ListaPar; |TIPO 0;
     |SI FSalida = 11;
          |ABRE #17;
          |PUSHV 0101 2380;
          |CONSULTA_F_CLAVE #8#17,MinCap,MaxCap;
          |POPV;
          |SI FSalida = 0;
               #6#3 = #17#0;            ||PARTIDA
               #6#4 = #17#1;            ||DESCRIPCION
               #6#5 = #17#2;            ||UNIDAD MEDIDA
               |PINTA #6#3;
               |PINTA #6#4;
               |PINTA #6#5;
          |SINO;
               |ERROR10;
          |FINSI;
          |CIERRA #17;
     |SINO;
          |ABRE #17;
          |SELECT #8#17;
          #17#11 = #5#2;                ||CAPITULO
          #17#0 = #6#3;                 ||PARTIDA
          |LEE 000#17=;
          |SI FSalida ! 0;
               |MENAV "0000PARTIDA NO ENCONTRADA EN ESTE CAPITULO";
               |ERROR10;
          |SINO;
               #6#4 = #17#1;            ||DESCRIPCION
               #6#5 = #17#2;            ||UNIDAD MEDIDA
               |PINTA #6#3; |PINTA #6#4; |PINTA #6#5;
          |FINSI;
          |CIERRA #17;
     |FINSI;
|FPRC;
-----------------------------------------------------------------------------
|PROCESO MinCap;
     #17#11 = #6#1;
     #17#0 = " "*20;
|FPRC;
-----------------------------------------------------------------------------
|PROCESO MaxCap;
     #17#11 = #6#1;
     #17#0 = "z"*20;
|FPRC;
-----------------------------------------------------------------------------
|PROCESO BInic; |TIPO 8;
     |CLS;
     |CONTROL_SIMPLE 0, "BOTON,F2 - Capitulos", 2430, 2455, F2;
     nBotCap = FSalida;
     |ESTADO_CONTROL nBotCap, "HIDE";
     |CONTROL_SIMPLE 0, "BOTON,F2 - Partidas", 2430, 2455, F2;
     nBotPar = FSalida;
     |ESTADO_CONTROL nBotPar, "HIDE";
     |CONTROL_SIMPLE 0, "BOTON,F2 - Medidas", 2430, 2455, F2;
     nBotMed = FSalida;
     |ESTADO_CONTROL nBotMed, "HIDE";
     |CONTROL_SIMPLE 0, "BOTON,F3 - Listado Partidas", 2425, 2460, F3;
     nBotParti = FSalida;
     |ESTADO_CONTROL nBotParti, "HIDE";


     |HAZ CreaTempo; aTmp13 = Tempo;
     |NOME_DAT #13 aTmp13;
|FPRC;
-----------------------------------------------------------------------------
|PROCESO BFinal; |TIPO 9;
     aTmp13 = Tempo; |HAZ BoraTempo; |DELINDEX #13;

     |PULSATECLA;
     |FIN_CONTROL_WINDOWS nBotParti;
     |PULSATECLA;
     |FIN_CONTROL_WINDOWS nBotCap;
     |PULSATECLA;
     |FIN_CONTROL_WINDOWS nBotPar;
     |PULSATECLA;
     |FIN_CONTROL_WINDOWS nBotMed;
     |PULSATECLA;
|FPRC;
-----------------------------------------------------------------------------
|PROCESO F2;
     |PTEC 824;
     Tecla = 10;
|FPRC;
-----------------------------------------------------------------------------
|PROCESO F3;
     |PTEC 825;
|FPRC;
-----------------------------------------------------------------------------
|PROCESO BCap; |TIPO 7;
     |ESTADO_CONTROL nBotCap, "SHOW";
|FPRC;
-----------------------------------------------------------------------------
|PROCESO BPar; |TIPO 7;
     |ESTADO_CONTROL nBotPar, "SHOW";
|FPRC;
-----------------------------------------------------------------------------
|PROCESO BMed; |TIPO 7;
     |ESTADO_CONTROL nBotMed, "SHOW";
|FPRC;
-----------------------------------------------------------------------------
|PROCESO BLisPar; |TIPO 7;
     |ESTADO_CONTROL nBotParti, "SHOW";
|FPRC;
-----------------------------------------------------------------------------
|PROCESO BBot; |TIPO 0;
     |ESTADO_CONTROL nBotParti, "HIDE";
     |ESTADO_CONTROL nBotPar, "HIDE";
     |ESTADO_CONTROL nBotCap, "HIDE";
     |ESTADO_CONTROL nBotMed, "HIDE";
|FPRC;
-----------------------------------------------------------------------------
|PROCESO BBoto; |TIPO 11;
     |ESTADO_CONTROL nBotParti, "HIDE";
     |ESTADO_CONTROL nBotPar, "HIDE";
     |ESTADO_CONTROL nBotCap, "HIDE";
     |ESTADO_CONTROL nBotMed, "HIDE";
|FPRC;
-----------------------------------------------------------------------------
|PROCESO EntCap; |TIPO 11;
     nModo = (FEntrada/100)!0;
     |SI Tecla = 10;
        nModo = 2; FSalida = 10;
     |FINSI;
     Tecla = 0;
     |SI nModo = 2; |O nModo = 1;       ||MODO MODIFICACION O ALTA
          |SI FSalida = 10;
               |SI #18#2 = "   ";
                    |MENAV "0000DEBE SELECCIONAR UN BLOQUE";
                    |ERROR10;
                    |ACABA;
               |FINSI;
               |ENTLINEAL #1#5,5,2,22,0,MiniCap,MaxiCap;
               |ERROR10;
               |ESTADO_CONTROL nBotCap, "SHOW";
          |FINSI;
     |FINSI;
     |SI nModo = 4;                     ||MODO CONSULTA
          |SI FSalida = 10;
               |ENTLINEAL #1#5,5,4,22,0,MiniCap,MaxiCap;
               |ERROR10;
               |ESTADO_CONTROL nBotCap, "SHOW";
          |FINSI;
     |FINSI;
|FPRC;
-----------------------------------------------------------------------------
|PROCESO Partidas;
     nLin6 = nLin6 + 1;
     #6#0 = #4#0;             ||PLANTILLA
     #6#1 = #11#2;            ||CAPITULOS
     #6#2 = nLin6;            ||LINEA
     #6#3 = #12#3;            ||PARTIDAS
     #6#4 = #12#4;            ||DESCRIPCIONES
     #6#5 = #12#8;            ||UNIDAD MEDIDA
     #6#6 = #18#2;            ||BLOQUE
     #6#7 = #18#3;            ||PLANTAS
     #6#8 = #18#4;            ||VIVIENDAS
     |GRABA 020#6c;
|FPRC;
-----------------------------------------------------------------------------
|DEFBUCLE PartidasBlo;
     #12#1;
     5;
     #4#2,#11#2,1,"S";
     #4#2,#11#2,999,"S";
     ;
     NULL, Partidas;
|FIN;
-----------------------------------------------------------------------------
|DEFBUCLE PartidasPla;
     #12#1;
     6;
     #4#2,#11#2,1,"S";
     #4#2,#11#2,999,"S";
     ;
     NULL, Partidas;
|FIN;
-----------------------------------------------------------------------------
|DEFBUCLE PartidasViv;
     #12#1;
     7;
     #4#2,#11#2,1,"S";
     #4#2,#11#2,999,"S";
     ;
     NULL, Partidas;
|FIN;
-----------------------------------------------------------------------------
|PROCESO Capitulos;
     nLin5 = nLin5 + 1;
     #5#0 = #4#0;             ||PLANTILLA
     #5#1 = nLin5;            ||LINEA
     #5#2 = #11#2;            ||CAPITULOS
     #5#3 = #11#3;            ||DESCRIPCIONES
     #5#4 = #18#2;            ||BLOQUE
     #5#5 = #18#3;            ||PLANTAS
     #5#6 = #18#4;            ||VIVIENDAS
     |GRABA 020#5c;
     |SI #18#2 ! 0; |Y #18#3 = 0; |Y #18#4 = 0;
          |BUCLE PartidasBlo;
     |FINSI;
     |SI #18#2 ! 0; |Y #18#3 ! 0; |Y #18#4 = 0;
          |BUCLE PartidasPla;
     |FINSI;
     |SI #18#2 ! 0; |Y #18#3 !0; |Y #18#4 ! 0;
          |BUCLE PartidasViv;
     |FINSI;
|FPRC;
-----------------------------------------------------------------------------
|PROCESO RellenaCap;
     |ABRE #10;
     #10#0 = #4#2;                 ||BUSCO LA PLANTILLA, PARA RELLENA EL
     |LEE 000#10=;                 ||LINEAL DE CAPITULOS Y PLANTILLAS
     |SI FSalida = 0;
          |ABRE #5;
          |ABRE #6;
          nLin5 = 0;
          nLin6 = 0;
          |BUCLELIN 2 Capitulos #10;
          |CIERRA #5;
          |CIERRA #6;
     |FINSI;
     |CIERRA #10;
|FPRC;
-----------------------------------------------------------------------------
|PROCESO MiniCap;
     #5#0 = #4#0;             ||PLANTILLA
     #5#4 = #18#2;            ||BLOQUE
     #5#5 = #18#3;            ||PLANTAS
     #5#6 = #18#4;            ||VIVIENDAS
     #5#1 = 1;                ||LINEA
|FPRC;
-----------------------------------------------------------------------------
|PROCESO MaxiCap;
     #5#0 = #4#0;             ||PLANTILLA
     #5#4 = #18#2;            ||BLOQUE
     #5#5 = #18#3;            ||PLANTAS
     #5#6 = #18#4;            ||VIVIENDAS
     #5#1 = 999;              ||LINEA
|FPRC;
-----------------------------------------------------------------------------
|PROCESO MiniPar;
     #6#0 = #4#0;             ||PLANTILLA
     #6#6 = #18#2;            ||BLOQUE
     #6#7 = #18#3;            ||PLANTAS
     #6#8 = #18#4;            ||VIVIENDAS
     #6#1 = #5#2;             ||CAPITULOS
     #6#2 = 1;                ||LINEA
|FPRC;
-----------------------------------------------------------------------------
|PROCESO MaxiPar;
     #6#0 = #4#0;             ||PLANTILLA
     #6#6 = #18#2;            ||BLOQUE
     #6#7 = #18#3;            ||PLANTAS
     #6#8 = #18#4;            ||VIVIENDAS
     #6#1 = #5#2;             ||CAPITULOS
     #6#2 = 999;              ||LINEA
|FPRC;
-----------------------------------------------------------------------------
|PROCESO PintaCap; |TIPO 7;
     |ENTLINEAL #1#5,-5,7,22,0,MiniCap,MaxiCap;
     |ENTLINEAL #1#6,-6,7,15,0,MiniPar,MaxiPar;
     |ENTLINEAL #1#7,-7,7,22,0,MiniMed,MaxiMed;
|FPRC;
-----------------------------------------------------------------------------
|PROCESO NoPermisos; |TIPO 0;
     nModo = (FEntrada/100)!0;
     |SI nModo ! 4;
          |ERROR10;
          |ESTADO_CONTROL nBotCap, "SHOW";
     |FINSI;
|FPRC;
-----------------------------------------------------------------------------
|PROCESO BuscaCap; |TIPO 0;
     |SALVA_FICHA #5;
     nLinea = #5#1;
     |SELECT #2#5;
     |LEE 000#5=;
     |SI FSalida = 0; |Y nLinea ! #5#1;
          |MENAV "0000CAPITULO REPETIDO. ALTA NO PERMITIDA";
          |ERROR10;
     |FINSI;
     |SELECT #1#5;
     |REPON_FICHA #5;
|FPRC;
-----------------------------------------------------------------------------
|PROCESO CambioCap; |TIPO 0;
     |SI Contenido ! #5#2;
          |BUCLE BorraPar;
     |FINSI;
|FPRC;
-----------------------------------------------------------------------------
|PROCESO GeneraCap; |TIPO 0;
     |SI Contenido ! #5#2;
          |ABRE #11;
          |SELECT #2#11;
          #11#0 = #4#2;
          #11#2 = #5#2;
          |LEE 000#11=;
          |SI FSalida = 0;
               |CONFI "0000S¨Desea importar partidas?";
               |SI FSalida = 0;
                    |ABRE #6;
                    nLin6 = 0;
                    |HAZ GeneraPar;
                    |CIERRA #6;
               |FINSI;
          |FINSI;
          |CIERRA #11;
     |FINSI;
|FPRC;
-----------------------------------------------------------------------------
|PROCESO GeneraPar;
     |SI #18#2 ! 0; |Y #18#3 = 0; |Y #18#4 = 0;
          |BUCLE PartidasBlo;
     |FINSI;
     |SI #18#2 ! 0; |Y #18#3 ! 0; |Y #18#4 = 0;
          |BUCLE PartidasPla;
     |FINSI;
     |SI #18#2 ! 0; |Y #18#3 !0; |Y #18#4 ! 0;
          |BUCLE PartidasViv;
     |FINSI;
|FPRC;
-----------------------------------------------------------------------------
|PROCESO BuscaPar; |TIPO 0;
     |HAZ Parametri;
     |SALVA_FICHA #6;
     nLinea = #6#2;
     |SELECT #2#6;
     |LEE 000#6=;
     |SI FSalida = 0; |Y nLinea ! #6#2;
          |MENAV "0000PARTIDA REPETIDA. ALTA NO PERMITIDA";
          |ERROR10;
     |FINSI;
     |SELECT #1#6;
     |REPON_FICHA #6;
|FPRC;
-----------------------------------------------------------------------------
|PROCESO CambioPar; |TIPO 0;
     |SI Contenido ! #6#3;
          |BUCLE BorraMed;
     |FINSI;
|FPRC;
-----------------------------------------------------------------------------
|PROCESO Parametri;
     cParam = #6#3;                     ||PARTIDA
     |QBF cParam;
     cPart = cParam;
     cParam = cParam % -101;
     |SI cParam = "$";                  ||COMPRUEBO SI ES PARAMETRICO
          |HAZ CargaIve;
          |SI FSalida ] 0;
               |HAZ Parametrico;
               #6#3 = cPartAct;
               |ABRE #17;               ||BUSCO DESCRIPCION Y UNIDAD MEDIDA
               #17#0 = #6#3;
               |LEE 000#17=;
               |SI FSalida = 0;
                    #6#4 = #17#1;       ||DESCRIPCION
                    #6#5 = #17#2;       ||UNIDAD DE MEDIDA
                    |PINTA #6#3;
                    |PINTA #6#4;
                    |PINTA #6#5;
               |FINSI;
               |CIERRA #17;
          |FINSI;
          |HAZ LiberaIve;
     |FINSI;
|FPRC;
-----------------------------------------------------------------------------
