|FICHEROS;
     marhu040 #0;    ||Seleccion.
     agifa024 #1;    ||Cliente
     agifa073 #2;    ||Lineas de Pedido
     agifa017 #3;    ||Articulo/Almacen
     marhu061 #5;    ||Este Def.
     tmpmarhu #10;   ||Temporal Con los pedidos asignados
     marhu041 #20;   ||Temporal para Clasificar Por Ubicacion.
     marhu042 #30;   ||Orden Actuacion.
     marhu060 #40;   ||Temporal Sin Localizacion.
|FIN;

|VARIABLES;
     &Tempo   = "";
     aTempo20 = "";
     aTempo40 = "";
     &nTip = 0;
     swUna = 0;
     aFich = "";
     aFic2 = "";
     nGrup = 0;
     nSum  = 0;
     nSum6 = 0;
     sWimp = 0;
     aPlan = "";
|FIN;

|PROCESO marhu041;
     |PRINT;
     sWimp = 1;
|FPRC;

|DEFBUCLE marhu041;
     #20#2;
     ;
     aPlan,0001;
     aPlan,9999;
     ;
     NULL,marhu041;
|FIN;

|PROCESO marhu060;
     |PRINT;
     sWimp = 1;
|FPRC;

|DEFBUCLE marhu060;
     #40#1;
     ;
     "     ",000001,  1;
     "zzzzz",999999,999;
     ;
     NULL,marhu060;
|FIN;

|PROCESO agifa073;

     |DDEFECTO #20;
     nSum = nSum + 1;
     #20#0  = nSum;      ||Secuencial
     #20#1  = #2#2;      ||Cod.Articulo
     #20#2  = #2#4;      ||Descripcion.
     #20#3  = #2#5;      ||Unidades.
     #20#4  = #2#28;     ||Serie Pedido.
     #20#5  = #2#0;      ||Numero.
     #20#6  = #10#4;     ||Serie Albaran
     #20#7  = #10#5;     ||Numero.
     #20#9  = "";        ||Observaciones.
     #20#13 = #10#3;     ||Grupo.

     |DDEFECTO #3;
     #3#0 = #2#2;        ||Cod.Articulo.
     #3#1 = #2#3;        ||Almacen.
     |LEE 000#3=;

     #20#8  = #3#39;     ||Stock
     #20#10 = #3#2;      ||Ubicacion.


     ||------------- Sacar El Orden de Actuacion.
     #30#2 = #20#10 % 101;
     #30#3 = #20#10 % 201;
     #30#4 = #20#10 % 301;
     #30#5 = #20#10 % 401;
     #30#6 = #20#10 % 502;

     |LEE 000#30=;
     |SI FSalida = 0;
          #20#11 = #30#0;
          #20#12 = #30#1;
     |SINO;
          |SI swUna = 0;
             |MENAV "0000Hay Localizaciones Sin Definir..........."
             swUna = 1;
          |FINSI;
          |DDEFECTO #40;
          #40#4  = #2#28;     ||Serie Pedido.
          #40#5  = #2#0;      ||Numero.
          #40#14 = #2#1;      ||Numero Linea.
          |LEE 000#40=;
          |SI FSalida ! 0;
               #40#0  = 0;         ||Secuencial
               #40#1  = #2#2;      ||Cod.Articulo
               #40#2  = #2#4;      ||Descripcion.
               #40#3  = #2#5;      ||Unidades.
               #40#6  = #10#4;     ||Serie Albaran
               #40#7  = #10#5;     ||Numero.
               #40#9  = "";        ||Observaciones.
               #40#13 = #10#3;     ||Grupo.

               |DDEFECTO #3;
               #3#0 = #2#2;        ||Cod.Articulo.
               #3#1 = #2#3;        ||Almacen.
               |LEE 000#3=;

               #40#8  = #3#39;     ||Stock
               #40#10 = #3#2;      ||Ubicacion.
               #40#11 =  999;
               #40#12 =  999;
               |GRABA 020#40n;
          |FINSI;
     |FINSI;

     |GRABA 020#20n;

|FPRC;

|DEFBUCLE agifa073;
     #2#1;
     ;
     #10#0,#10#1,  1;
     #10#0,#10#1,999;
     ;
     NULL,agifa073;
|FIN;

|PROCESO tmpmarhub;
     |BUCLE agifa073;
|FPRC;

|DEFBUCLE tmpmarhub;
     #10#2;
     ;
     nGrup;
     nGrup;
     ;
     NULL,tmpmarhub;
|FIN;

|PROCESO marhu040b;
     nSum = 0;
     nGrup = #0#0;
     |ABRE #20;
     |BUCLE tmpmarhub;
     |CIERRA #20;
     |SI nSum ! 0;
          sWimp = 0;
          |BUCLE marhu041;
          |SI sWimp ! 0;
               |PIEINF;
          |FINSI;
     |FINSI;
     |DELINDEX #20;
|FPRC;

|DEFBUCLE marhu040b;
     #0#1;
     ;
       1;
     999;
     ;
     NULL,marhu040b;
|FIN;


|PROCESO marhu040c;
     nSum = 0;
     nGrup = 999;
     |ABRE #20;
     |BUCLE tmpmarhub;
     |CIERRA #20;
     |SI nSum ! 0;
          sWimp = 0;
          |BUCLE marhu041;
          |SI sWimp ! 0;
               |PIEINF;
          |FINSI;
     |FINSI;
     |DELINDEX #20;
|FPRC;

|PROCESO marhu040;
     |SI #1#189 = #0#1; |O #1#189 = #0#2; |O #1#189 = #0#3; |O #1#189 = #0#4;
          nGrup = #0#0;
          |ERROR10;
     |FINSI;
|FPRC;

|DEFBUCLE marhu040;
     #0#1;
     ;
       1;
     999;
     ;
     NULL,marhu040;
|FIN;

|PROCESO tmpmarhu;
     #1#0 = #10#2;
     |LEE 000#1=;
     |SI FSalida ! 0; |ACABA; |FINSI;
     nGrup = 0;
     |BUCLE marhu040;
     |SI nGrup ! 0;
          #10#3 = nGrup;
          |GRABA 020#10a;
     |FINSI;
|FPRC;

|DEFBUCLE tmpmarhu;
     #10#1;
     ;
     "  ",000001;
     "zz",999999;
     ;
     NULL,tmpmarhu;
|FIN;

|PROCESO ejec; |TIPO 12;
     |SI #5#0 ! "SI"; |ACABA; |FINSI;
     |IMPRE #0;
     |SI FSalida ! 0; |ACABA; |FINSI;
     |INFOR "marhu040";
     |SI FSalida ! 0; |FINIMP; |ACABA; |FINSI;
     |HAZ CreaTempo; aTempo40 = Tempo;
     |NOME_DAT #40 aTempo40; |DELINDEX #40;
     |HAZ CreaTempo; aTempo20 = Tempo;
     |NOME_DAT #20 aTempo20; |DELINDEX #20;
     |ABRE #1;
     |ABRE #3;
     |ABRE #0;
     |ABRE #40;
     |BUCLE tmpmarhu;
     |ABRE #30;
     |SELECT #2#30;
     aPlan = "P";
     |BUCLE marhu040b;
     aPlan = "A";
     |BUCLE marhu040b;
     |CIERRA #40;
     nTip  = 1;
     sWimp = 0;
     |BUCLE marhu060;
     |SI sWimp ! 0;
          |PIEINF;
     |FINSI;
     |FININF;
     |FINIMP;
     |CIERRA #1;
     |CIERRA #3;
     |CIERRA #0;
     |CIERRA #30;
     |DELINDEX #40; Tempo = aTempo40; |HAZ BoraTempo;
     |DELINDEX #20; Tempo = aTempo20; |HAZ BoraTempo;
|FPRC;
