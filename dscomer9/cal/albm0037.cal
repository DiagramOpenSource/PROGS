|FICHEROS;
     albm0037 #0;
     albm0038 #1,MANTE;
     albm0040 #2;   ||Pedidos
     albm0039 #3;   ||Especifiaciones
     agifa017 #4;
     albw0017 #17,MANTE; ||Tmp
     albw0018 #18,MANTE;
     agifa019 #19;
     agifa024 #24;
     albm0028 #28;       ||Auxiliar pedido
     agifa029 #29;
     albm0041 #41;
     albm0042 #42;
     agifa049 #49;
     agifa073 #73;
     agifa104 #104;

     albm0025 #25;       ||Lineas Fabricacion
     albm0034 #34;       ||Mantenimto.Trabajado

     albw0039 #39;       ||Aux. ArticuloPedido

     albm0029 #529;      ||Formulas Cabs.  (Mezcladas)
     albm0030 #530;      ||Formulas Cabs.  (Mezcladas)
     agifa048 #548;      ||Escandallos Cabs.

     albm0061 #61;       ||Mezclad.PorConfirmar (aka. PreMezcladas)
     albw0040 #40;       ||Rel. PreMezcla/Orden

     albm0062 #62;       ||Rel.Orden-Partidas
     albw0042 #142;      ||Aux.Partidas Orden

     albm0026 #26;       ||Articulos Lin Fab(C)

     albm0013 #13;       ||Parametros Albero

     albw0038 #38;       ||Aux. ArticuloCantida. Para crear nuevas O.F.

     albm0040@ #540;     ||CargaDef para los pedidos.

     albm0060 #60;       ||Rel. Orden/Mezclada

     agifa324 #324;
|FIN;

|VARIABLES;
     &EURODB = 0;
     aArtAnt = "";
     aArtDos = "";
     nLinea = 0;
     nLineasGraba = 0;
     nSwHaSaltado = 0;
     aArtAra = "";
     nAlmAra = 0;
     nCantAra = 0;

     &enNoRecalcula = 0;
     &enNumOFOrig = 0;
     &eaSerOFOrig = "";

     aMas = "";

     nSwHayTempo38 = 0;
     aTempo38 = "";

     nModoSer = 0;
     nSwEsEscan = 0;
     nSwEsMezcla = 0;

     aSerPara = "";
     aSerie = "";
     nDif = 0;
     aTempo142 = "";

     nSwEsta = 0;
     nUltimo = 0;
     nNumCodigo = 0;

     nSwHayPed = 0;
     &eaTempo39 = "";
     &eaArticulo = "";
     aTempo39 = "";
     aArticulo = "";

     nSalida = 0;
     nSinPedido = 0;     ||No se asocia ningun pedido a esta orden = 1.

     aParametro = "";
     &enCantidad = 0;

     aMensaje = "";
     &enNumOrden = 0;
     &eaSerOrden = "";
     aPrograma = "";

     &Tempo = "";
     nSwCheq = 0;
     nLin = 0;
     nLinEs = 0;
     aCliente = "";
     aAlfa = "";
     nHay = 0;
     nConta = 0;
     nModo = 0;
     nModoCab = 0;
     nModoAra = 0;
     nEsta = 0;
|FIN;

|INCLUYE i_escank;

|PROCESO Contador; |TIPO 7;
     nModoCab = (FEntrada / 100) ! 0;
     |SI nModoCab = 1;
          aSerie = #0#16;
          #0#16 = aSerie;
          #0#0 = 999999;
          |LEE 000#0];
          |SI FSalida = 0;
               |LEE 000#0a;
          |SINO;
               |LEE 000#0u;
          |FINSI;
          |SI FSalida = 0; |Y #0#16 = aSerie;
               nConta = #0#0 + 1;
          |SINO;
               nConta = 1;
          |FINSI;
          |DDEFECTO #0;
          #0#16 = aSerie;
          #0#0 = nConta;
          |PINTA #0#16;
          |PINTA #0#0;
     |FINSI;
|FPRC;

|PROCESO Borra40;
     |BORRA 020#2a;
|FPRC;

|DEFBUCLE Borra40;
     #2#1;
     ;
     #0#16, #0#0, "     ", 000001, 001;
     #0#16, #0#0, "zzzzz", 999999, 999;
     be;
     NULL, Borra40;
|FIN;

|PROCESO Borra39;
     |BORRA 020#3a;
|FPRC;

|DEFBUCLE Borra39;
     #3#1;
     ;
     #0#16, #0#0, 001;
     #0#16, #0#0, 999;
     be;
     NULL, Borra39;
|FIN;

|PROCESO Selecciona; |TIPO 0;
     |SI FSalida = 0; |Y #17Campo = Contenido;
          |SI #17#6 = "*";
               #17#6 = "";
          |SINO;
               #17#6 = "*";
          |FINSI;
          |GRABA 020#17a;
          |PINTA #17#6;
     |FINSI;
|FPRC;

|PROCESO Min17;
     #17#0 = "     ";
     #17#1 = 0;
     #17#2 = 1;
|FPRC;

|PROCESO Max17;
     #17#0 = "zzzzz";
     #17#1 = 999999;
     #17#2 = 999;
|FPRC;

|PROCESO Tmp17;
     |SI #17#6 = "*";
          |SI nHay = 0;
               aCliente = #17#7;        ||Solo hay  uno
          |SINO;
               aCliente = "000000";
          |FINSI;
          nHay = 1;

          |DDEFECTO #2;
          #2#4 = #0#16;
          #2#0 = #0#0;
          #2#1 = #17#0;
          #2#2 = #17#1;
          #2#3 = #17#2;
          |GRABA 020#2n;

          #0#12 = #0#12 + #17#5; ||Kilos
     |FINSI;
|FPRC;

|DEFBUCLE Tmp17;
     #17#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, Tmp17;
|FIN;

|PROCESO Tmp17Copia;
     |SI #17#6 = "*";
          |DDEFECTO #2;
          #2#0 = #0#0;
          #2#1 = #17#0;
          #2#2 = #17#1;
          #2#3 = #17#2;
          #2#4 = #0#16;
          |GRABA 020#2n;
          #0#12 = #0#12 + #17#5; ||Kilos
     |FINSI;
|FPRC;

|DEFBUCLE Tmp17Copia;
     #17#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, Tmp17Copia;
|FIN;

|PROCESO agifa073_2;
     ||ARA

     nDif = #73#5 - #73#43;
     |SI nDif > 0; |Y #73#5 > 0;

/*
          |DDEFECTO #28;
          #28#0 = #73#28;
          #28#1 = #73#0;
          #28#2 = #73#1;
          |LEE 000#28=;
          |SI #28#3 = "N";
*/

          |ABRE #albm0040;
          |SELECT #2#albm0040;
          #albm0040#1 = #73#28;
          #albm0040#2 = #73#0;
          #albm0040#3 = #73#1;
          |LEE 000#albm0040.=;
          |SI FSalida ! 0;
                    |DDEFECTO #17;
                    #17#0 = #73#28;
                    #17#1 = #73#0;
                    #17#2 = #73#1;
                    #17#3 = #73#2;
                    #17#4 = #73#4;
                    #17#5 = #73#5;

                    |SELECT #1#2;
                    #2#4 = #0#16;
                    #2#0 = #0#0;
                    #2#1 = #17#0;
                    #2#2 = #17#1;
                    #2#3 = #17#2;
                    |LEE 000#2=;
                    nEsta = 0;
                    |SI FSalida = 0; |O #0#5 ! 0;
                         #17#6 = "*";
                    |SINO;
                         |SELECT #2#2;
                         |LEE 000#2];
                         |PARA; |SI FSalida = 0; |Y #2#1 = #17#0; |Y #2#2 = #17#1;
                                   |Y #2#3 = #17#2; |HACIENDO;
                              |SI #2#0 ! #0#0; |Y #2#4 ! #0#16;
                                   nEsta = 1;
                              |FINSI;
                              |LEE 000#2s;
                         |SIGUE;
                    |FINSI;
                    #17#7 = #73#20; ||Cli

                    |ABRE #agifa024;
                    |DDEFECTO #agifa024;
                    #agifa024#0 = #17#7;
                    |LEE 000#agifa024.=;
                    |CIERRA #agifa024;

                    #17#8 = #agifa024#2;

                    |SI nEsta = 0;
                         |GRABA 020#17n;
                         nHay = 1;
                    |FINSI;
          |SINO;
               |SI #albm0037#16 = #albm0040#4; |Y #albm0037#0 = #albm0040#0;
                    ||ARA
                    |DDEFECTO #17;
                    #17#0 = #73#28;
                    #17#1 = #73#0;
                    #17#2 = #73#1;
                    #17#3 = #73#2;
                    #17#4 = #73#4;
                    #17#5 = #73#5;

                    |SELECT #1#2;
                    #2#4 = #0#16;
                    #2#0 = #0#0;
                    #2#1 = #17#0;
                    #2#2 = #17#1;
                    #2#3 = #17#2;
                    |LEE 000#2=;
                    nEsta = 0;
                    |SI FSalida = 0; |O #0#5 ! 0;
                         #17#6 = "*";
                    |SINO;
                         |SELECT #2#2;
                         |LEE 000#2];
                         |PARA; |SI FSalida = 0; |Y #2#1 = #17#0; |Y #2#2 = #17#1;
                                   |Y #2#3 = #17#2; |HACIENDO;
                              |SI #2#0 ! #0#0; |Y #2#4 ! #0#16;
                                   nEsta = 1;
                              |FINSI;
                              |LEE 000#2s;
                         |SIGUE;
                    |FINSI;
                    #17#7 = #73#20; ||Cli

                    |ABRE #agifa024;
                    |DDEFECTO #agifa024;
                    #agifa024#0 = #17#7;
                    |LEE 000#agifa024.=;
                    |CIERRA #agifa024;

                    #17#8 = #agifa024#2;

                    |SI nEsta = 0;
                         |GRABA 020#17n;
                         nHay = 1;
                    |FINSI;
               |FINSI;
          |FINSI;
          |CIERRA #albm0040;
     |FINSI;
|FPRC;

|DEFBUCLE agifa073_2;
     #73#3;
     ;
     #0#2, "     ", 000001, "      ";
     #0#2, "zzzzz", 999999, "zzzzzz";
     ;
     NULL, agifa073_2;
|FIN;

|DEFBUCLE agifa073_1;
     #73#3;
     ;
     #0#2, #0#4, #0#5, "      ";
     #0#2, #0#4, #0#5, "zzzzzz";
     ;
     NULL, agifa073_2;
|FIN;

|PROCESO CargaEspe;
     nLinEs = nLinEs + 1;
     #3#3 = #0#16;
     #3#0 = #0#0;
     #3#1 = nLinEs;
     #3#2 = #42#3;
     |GRABA 020#3n;
|FPRC;

|DEFBUCLE CargaEspe;
     #42#1;
     ;
     aCliente, #0#2, 001;
     aCliente, #0#2, 999;
     ;
     NULL, CargaEspe;
|FIN;

|PROCESO DescargaEspe;
     nLinEs = nLinEs + 1;
     #42#0 = aCliente;
     #42#1 = #0#2;
     #42#2 = nLinEs;
     #42#3 = #3#2;
     |GRABA 020#42n;
|FPRC;

|DEFBUCLE DescargaEspe;
     #3#1;
     ;
     #0#16, #0#0, 001;
     #0#16, #0#0, 999;
     ;
     NULL, DescargaEspe;
|FIN;

|PROCESO BorraEspe;
     |BORRA 020#42a;
|FPRC;

|DEFBUCLE BorraEspe;
     #42#1;
     ;
     aCliente, #0#2, 001;
     aCliente, #0#2, 999;
     be;
     NULL, BorraEspe;
|FIN;

|PROCESO SerPedido;
     |SI FSalida = 1; |ERROR; |ACABA; |FINSI;
|FPRC;

|PROCESO Pedidos; |TIPO 0;
     |SI FSalida = 1; |ERROR; |ACABA; |FINSI;
     |SI FSalida = 2; |ACABA; |FINSI;

     |SI enNoRecalcula = 0;
          nModo = (FEntrada / 100) ! 0;
          nHay = 0;

          |SI #0#5 ! 0;
               |ABRE #104;
               #104#25 = #0#4;
               #104#0 = #0#5;
               |LEE 000#104=;
               |SI FSalida ! 0;
                    |MENAV "0000Pedido Inexistente...";
                    |ERROR; |ACABA;
               |FINSI;
               |CIERRA #104;
          |FINSI;

          |DELINDEX #17;
          |ABRE #17;
          |ABRE #28;
          |ABRE #2;
          |SI #0#5 = 0;
               |BUCLE agifa073_2;
               #0#4 = ""; |PINTA #0#4;
          |SINO;
               |BUCLE agifa073_1;
          |FINSI;
          |CIERRA #2;
          |CIERRA #28;

          |SI nHay = 0;
               |MENAV "0000No hay pedidos de este articulo...";
          |SINO;
               |SI #0#5 = 0;
                    |PUSHV 0501 2380;
                    |ENTLINEAL #1#17, 1, 4, 22, 1, Min17, Max17;
                    |POPV;
               |FINSI;

               nHay = 0;
               |BUCLE Borra40;
               #0#12 = 0;     ||Kilos
               |ABRE #2;
               |BUCLE Tmp17;
               |CIERRA #2;
               |PINTA #0#12;
          |FINSI;
          |CIERRA #17;

          nSinPedido = 0;
          |SI nHay = 0;
               ||CCC. Aqui va el |CONFI
               aMensaje = "0000NContinuar sin asociar ningun Pedido?";
               |CONFI aMensaje;
               |SI FSalida = 0;
                    nSinPedido = 1;
                    |HAZ TemaEspecificaciones;
               |SINO;
                    |ERROR;
               |FINSI;
          |SINO;
               nSinPedido = 0;
               |HAZ TemaEspecificaciones;
          |FINSI;
     |SINO;
          nSinPedido = 1;
          |HAZ TemaEspecificaciones;
     |FINSI;
|FPRC;

|PROCESO TemaEspecificaciones;
     |SI nModo = 1;
          |SI aCliente ! "000000"; |Y aCliente ! "      "; |Y aCliente ! "";
               |BUCLE Borra39;
               |ABRE #3;
               nLinEs = 0;
               |BUCLE CargaEspe;
               |CIERRA #3;
          |FINSI;
     |FINSI;

     |DDEFECTO #24;
     |ABRE #24;
     #24#0 = aCliente;
     |LEE 000#24=;
     |CIERRA #24;

     |DDEFECTO #18;
     #18#0 = aCliente;
     #18#1 = #0#2;
     #18#2 = #24#1;
     #18#3 = #0#3;
     |PUSHV 0501 2380;
     |BLANCO 714 2269;
     |PINPA #0#18;
     |PINDA #0#18;
     |HAZ Nota;
     |POPV;
     |SI aCliente ! "000000";
          |BUCLE BorraEspe;
          |ABRE #41;
          #41#0 = #24#0;
          #41#1 = #0#2;
          #41#2 = #24#1;
          #41#3 = #0#3;
          |GRABA 000#41n;
          |CIERRA #41;
          |ABRE #42;
          nLinEs = 0;
          |BUCLE DescargaEspe;
          |CIERRA #42;
     |FINSI;
|FPRC;


|PROCESO Tipo2Cab; |TIPO 2;
     nModoAra = (FEntrada / 100) ! 0;
     |SI nModoAra = 3;
          |BUCLE Borra40;
          |BUCLE Borra39;
     |FINSI;
|FPRC;

|PROCESO albm0030;
     aArtAra = #albm0030#2;
     nAlmAra = nAlmaEscan;
     nCantAra = nCantiEscan % #albm0030#4;

     |ABRE #4;
     |DDEFECTO #4;
     #4#0 = aArtAra;
     #4#1 = nAlmAra;
     |LEE 000#4=;
     |SI nCantAra > #4#39;
          aAlfa = "0000Falta Art:" + #4#0 + " Al:" + #4#1;
          aAlfa = aAlfa + &13+" Stock:" +#4#39+ " Nece:" + nCantAra;
          |MENAV aAlfa;
     |FINSI;
     |CIERRA #4;
|FPRC;

|DEFBUCLE albm0030;
 #albm0030#1;
 ;
 aEscan, 01;
 aEscan, 99;
 ;
 NULL, albm0030;
|FIN;

|PROCESO LinKit;
     |ABRE #19;
     #19#0 = aEscan;
     |LEE 020#19=;
     |CIERRA #19;

     nSwEsEscan = 0;
     |ABRE #agifa048;
     #agifa048#0 = aEscan;
     |LEE 000#agifa048.=;
     |SI FSalida = 0;
          nSwEsEscan = 1;
     |FINSI;
     |CIERRA #agifa048;

     nSwEsMezcla = 0;
     |ABRE #albm0029
     #albm0029#0 = aEscan;
     |LEE 000#albm0029.=;
     |SI FSalida = 0;
          nSwEsMezcla = 1;
     |FINSI;
     |CIERRA #albm0029;

     |SI nSwCheq = 1;
          ||TODO CCC
          ||Tengo que ver si es un parte (CABECERA) de una mezclada o subescandallo.

          |SI nSwEsEscan = 0; |Y nSwEsMezcla = 0;
               |ABRE #4;
               |DDEFECTO #4;
               #4#0 = aEscan;
               #4#1 = nAlmaEscan;
               |LEE 000#4=;
               |SI nCantiEscan > #4#39;
                    aAlfa = "0000Falta Art:" + #4#0 + " Al:" + #4#1;
                    aAlfa = aAlfa + &13+" Stock:" +#4#39+ " Nece:" + nCantiEscan;
                    |MENAV aAlfa;
                    nHay = 1;
               |FINSI;
               |CIERRA #4;
          |SINO;
               |SI nSwEsMezcla = 1;
                    ||ARA
                    |BUCLE albm0030;
               |FINSI;
          |FINSI;
     |SINO;
||          |SI nSwEsEscan = 0; |Y nSwEsMezcla = 0;
          |SI nNivelEscan = 1;
               nLin = nLin + 1;
               #1#0 = #0#0;
               #1#1 = nLin;
               #1#2 = aEscan;
               #1#3 = #49#3;
               #1#4 = nAlmaEscan;
               #1#8 = nCantiEscan;
               #1#16 = #0#16;

               |DDEFECTO #29;
               #29#0 = #1#4;
               |LEE 000#29=;

               #1#5 = #29#1;
               |GRABA 020#1n;

               nLineasGraba = nLineasGraba + 1;
               aAlfa = #19#125; |QBT aAlfa;
               |SI nLineasGraba = 1; |Y aAlfa = "M";
                    #albm0037#18 = 1;
               |SINO;
                    #albm0037#18 = 2;
               |FINSI;

               |SI nSwEsEscan = 1;
                    ||Metelo en el temporal, para crear nuevas Ordenes de Fabrica
                    nSwHayTempo38 = 1;
                    |ABRE #38;
                    |DDEFECTO #38;
                    #38#0 = aEscan;
                    |LEE 101#38=;
                    |SI FSalida ! 0; |GRABA 020#38b; |FINSI;
                    #38#1 = #38#1 + nCantiEscan;
                    |GRABA 020#38a;
                    |LIBERA #38;
                    |CIERRA #38;
                    |GRABA 020#albm0037.a;
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Tipo7C2_m0037; |TIPO 7;
     aArtAnt = #albm0037#2;
     |QBF aArtAnt;
|FPRC;

|PROCESO CreaLineas; |TIPO 0;
     ||ARA
     nSalida = FSalida;
     |SI nSalida = 0; |O nSalida = 3; |O nSalida = 5; |O nSalida = 1;
          nModoCab = (FEntrada / 100) ! 0;
          |SI nModoCab = 1;
               |SI enNoRecalcula = 0;
                    |BUCLE Borra40;

                    |ABRE #2;
                    |SI nSinPedido = 0; |O aParametro = "AUTO";
                         #0#12 = 0;     ||Kilos
                         |BUCLE Tmp17Copia;
                    |FINSI;
                    |CIERRA #2;

                    |PINTA #0#12;
               |FINSI;

               |ABRE #1;
               |ABRE #29;
               aEscan = #0#2;
               nCantiEscan = #0#12;
               nSwCheq = 0;

               #albm0037#18 = 2;
               |GRABA 020#albm0037.a;
               nLineasGraba = 0;
               |HAZ DesglosaKit;
               |CIERRA #29;
               |CIERRA #1;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO RecalTeoricas;
     #49#2 = #1#2;
     #49#0 = #0#2;
     |LEE 000#49=;
     |SI FSalida = 0;
          #1#8 = #0#12 * #49#4;
          |GRABA 020#1a;
          |LIBERA #1;
     |FINSI;
|FPRC;

|PROCESO MiraStock;
     nSalida = FSalida;
     |SI nSalida = 1; |ERROR; |ACABA; |FINSI;
     |ABRE #17;
     aEscan = #0#2;
     nCantiEscan = #0#12;
     nSwCheq = 1;
     nHay = 0;
     |HAZ DesglosaKit;
     |CIERRA #17;
     ||Antes si no habia suficiente no dejaba pasar de aqui
||     |SI nHay = 1; |ERROR; |FINSI;
/*
     |SI Campo = 12; |Y nSalida = 1;
          |HAZ CreaLineas;
     |FINSI;
*/


     |SI Campo = 12;
          |SI #0Campo = 0;
               |MENAV "0000Debe introducir un valor...";
               |ERROR;
          |SINO;
               nModo = (FEntrada / 100) ! 0;
               |SI nModo = 2; |Y Contenido ! #0Campo;
                    |ABRE #49; |SELECT #2#49;
                    |BUCLELIN 0RecalTeoricas#0;
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Tipo37_8; |TIPO 8;
     aAlfa = Usuario;
     |NOME_DAT #17 aAlfa; |DELINDEX #17;
|FPRC;

|PROCESO Tipo37_9; |TIPO 9;
     |DELINDEX #17;
|FPRC;

|PROCESO PintaLin; |TIPO 0;
     |SI FSalida = 1; |ERROR; |ACABA; |FINSI;

     |ABRE #albm0025;
     |DDEFECTO #albm0025;
     #albm0025#0 = #albm0037#6;
     |LEE 000#albm0025.=;
     #albm0037#7 = #albm0025#1;
     |PINTA #albm0037#7;
     |CIERRA #albm0025;
|FPRC;

|PROCESO PintaTrab; |TIPO 0;
     |ABRE #albm0034;
     |DDEFECTO #albm0034;
     #albm0034#0 = #albm0037#8;
     |LEE 000#albm0034.=;
     #albm0037#9 = #albm0034#1;
     |PINTA #albm0037#9;
     |CIERRA #albm0034;
|FPRC;

|PROCESO PintaAlm; |TIPO 0;
     |ABRE #agifa029;
     |DDEFECTO #agifa029;
     #agifa029#0 = #albm0037#10;
     |LEE 000#agifa029.=;
     #albm0037#11 = #agifa029#1;
     |PINTA #albm0037#11;
     |CIERRA #agifa029;
|FPRC;

|PROCESO Tipo4Imp; |TIPO 4;           ||Impresion
     enNumOrden = #albm0037#0;
     eaSerOrden = #albm0037#16;
     aPrograma = "albz0023;AUTO";

     |SUB_EJECUTA_NP aPrograma;
|FPRC;

|PROCESO albm0040;
     |ABRE #540;
     |DDEFECTO #540;
     #540#4 = #0#16;
     #540#0 = #0#0;
     #540#1 = #albm0040#1;
     #540#2 = #albm0040#2;
     #540#3 = #albm0040#3;
     |LEE 101#540.=;
     |SI FSalida ! 0;
          |GRABA 020#540n;
     |FINSI;
     |CIERRA #540;
|FPRC;

|DEFBUCLE albm0040;
 #albm0040#1;
 ;
 eaSerOFOrig, enNumOFOrig, "     ", 000000, 001;
 eaSerOFOrig, enNumOFOrig, "zzzzz", 999999, 999;
 ;
 NULL, albm0040;
|FIN;

|PROCESO CopiaPedidos;
     |BUCLE albm0040;
|FPRC;


|PROGRAMA;
     |ABRE #agifa324;
     |DDEFECTO #agifa324;
     #agifa324#0 = "EUR";
     |LEE 000#agifa324.=;
     EURODB = #agifa324#9;
     |CIERRA #agifa324;



     nSwHayTempo38 = 0;
     |HAZ CreaTempo; aTempo38 = Tempo;
     |NOME_DAT #38 aTempo38; |DELINDEX #38;

     aParametro = PARAMETRO;
     |QBF aParametro;
     |SI aParametro = "";
          |MANTE #0;
     |SINO;
          |ABRE #albm0013;
          |DDEFECTO #albm0013;
          |LEE 000#albm0013.p;
          |CIERRA #albm0013;
          aSerPara = #albm0013#9;

          |ABRE #0;
          |PINPA #0#0;
          #0#16 = aSerPara;
          #0#0 = 999999;
          |LEE 000#0];
          |SI FSalida = 0;
               |LEE 000#0a;
          |SINO;
               |LEE 000#0u;
          |FINSI;
          |SI FSalida = 0; |Y #0#16 = aSerPara;
               nConta = #0#0 + 1;
          |SINO;
               nConta = 1;
          |FINSI;

          |DDEFECTO #0;
          #0#16 = aSerPara;
          #0#0 = nConta;

          #0#2 = eaArticulo;
          #0#12 = enCantidad;
          |PINDA #0#0;

          |ABRE #2;
          |SI enNoRecalcula = 0;
               |HAZ MetePedidos;
          |SINO;
               |DBASE aAlfa; |QBF aAlfa;
               aMas = aAlfa + "def/albm0040.mas";
               |CARGA_DEF aMas, albm0040@;
               |SI FSalida ! 0; |ACABA; |FINSI;
               |HAZ CopiaPedidos;
               |DESCARGA_DEF #albm0040@;
          |FINSI;
          |CIERRA #2;

          |C_M #albm0037#8, 1, "N";
          |C_M #albm0037#10, 1, "N";

          |GRABA 020#albm0037.b;

          |ENDATOS #1#0;
          |SI FSalida = 0;
               |SI enNoRecalcula = 1;
                    ||#albm0037#18 = 2;
               |FINSI;
               |GRABA 020#albm0037.a;

               enNumOrden = #albm0037#0;
               eaSerOrden = #albm0037#16;
          |SINO;
               |BORRA 020#albm0037.a;
          |FINSI;

          |CIERRA #0;
     |FINSI;

     |DELINDEX #38; Tempo = aTempo38; |HAZ BoraTempo;
|FPRO;

|PROCESO albw0039;
     nSwHayPed = 1;

     |DDEFECTO #2;
     #2#4 = #albm0037#16;
     #2#0 = #albm0037#0;
     #2#1 = #albw0039#1;
     #2#2 = #albw0039#2;
     #2#3 = 1;                ||No tengo en cuenta las lineas
     |LEE 101#2=;
     |SI FSalida ! 0;
          |GRABA 020#2n;
     |FINSI;
|FPRC;

|DEFBUCLE albw0039;
 #albw0039#2;
 ;
 aArticulo;
 aArticulo;
 ;
 NULL, albw0039;
|FIN;

|PROCESO MetePedidos;
     aTempo39 = eaTempo39;
     |QBF aTempo39;

     |NOME_DAT #39 aTempo39;

     aArticulo = eaArticulo;
     nSwHayPed = 0;

     |BUCLE albw0039;
|FPRC;

|PROCESO Tipo80; |TIPO 80;
     FSalida = 2590;
|FPRC;

|PROCESO albw0038;
     enNumOrden = 0;
     eaSerOrden = "";

     enNumOFOrig = #albm0037#0;
     eaSerOFOrig = #albm0037#16;
     enNoRecalcula = 1;
     eaArticulo = #albw0038#0;
     enCantidad = #albw0038#1;
     aPrograma = "albm0037;AUTO";
     |SUB_EJECUTA aPrograma;
     enNoRecalcula = 0;
     aParametro = "";

     |SI enNumOrden ! 0;
          |ABRE #albm0060;
          |DDEFECTO #albm0060;
          #albm0060#2 = #albm0037#16;
          #albm0060#0 = #albm0037#0;
          #albm0060#3 = eaSerOrden;
          #albm0060#1 = enNumOrden;
          |LEE 101#albm0060.=;
          |SI FSalida ! 0; |GRABA 020#albm0060.b; |FINSI;
          #albm0060#4 = "O";
          #albm0060#5 = #albm0038#1;
          |GRABA 020#albm0060.a;
          |LIBERA #albm0060;

          ||ARA5
||          aMensaje = "0000Mete OF. Relacion " + #albm0037#0 + " con --> " + enNumOrden;
||          |MENAV aMensaje;
     |FINSI;

     |BORRA 020#38a;
|FPRC;

|DEFBUCLE albw0038;
 #albw0038#1;
 ;
 INICIO;
 FINAL;
 be;
 NULL, albw0038;
|FIN;

|PROCESO FinLineas; |TIPO 3;
     |SI nModoCab = 1;
          aMensaje = "0000NGenerar las Mezcladas correspondientes.?";
          |CONFI aMensaje;
          |SI FSalida = 0;
               |HAZ GeneraMezcladas;
          |FINSI;

          |SI nSwHayTempo38 = 1;
               aMensaje = "0000NCrear las Ordenes de Fabricacion?";
               |CONFI aMensaje;
               |SI FSalida = 0;
                    |BUCLE albw0038;
               |FINSI;
          |FINSI;

          aMensaje = "0000SDesea imprimir la Orden de Fabricacion?";
          |CONFI aMensaje;
          |SI FSalida = 0;
               |GRABA 020#albm0037.a;
               |CIERRAT #albm0037;
               enNumOrden = #albm0037#0;
               eaSerOrden = #albm0037#16;
               aPrograma = "albz0023;AUTO";

               |SUB_EJECUTA_NP aPrograma;
               |ABRET #albm0037;
               |LEE 101#albm0037.=;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO albm0061;
     |SI #albm0061#3 = #albm0038#2;          ||Esta el articulo
          nSwEsta = 1;
          nNumCodigo = #albm0061#24;
          |ERROR10;
     |FINSI;
     nUltimo = #albm0061#24;
|FPRC;

|DEFBUCLE albm0061;
 #albm0061#1;
 ;
 INICIO;
 FINAL;
 ;
 NULL, albm0061;
|FIN;

|PROCESO albm0038;
     |DDEFECTO #albm0029;
     #albm0029#0 = #albm0038#2;
     |LEE 000#albm0029.=;
     |SI FSalida = 0;
          nSwEsta = 0;
          nUltimo = 0;
          |BUCLE albm0061;
          |ABRE #albm0061;
          |SI nSwEsta = 1;
               aMensaje = "0000SAcumular Cantidad en PreMezclada Existente?";
               |CONFI aMensaje;
               |SI FSalida = 0;
                    |DDEFECTO #albm0061;
                    #albm0061#24 = nNumCodigo;
                    |LEE 101#albm0061.=;
                    |SI FSalida ! 0; |GRABA 020#albm0061.b; |FINSI;

                    #albm0061#13 = #albm0061#13 + #albm0038#8;

                    |GRABA 020#albm0061.a;
                    |LIBERA #albm0061;
               |SINO;
                    nUltimo = nUltimo + 1;
                    |DDEFECTO #albm0061;
                    #albm0061#24 = nUltimo;
                    |LEE 101#albm0061.=;
                    |SI FSalida ! 0; |GRABA 020#albm0061.b; |FINSI;

                    ||TODO CCC. Resto de campos
                    #albm0061#1 = #albm0038#3;
                    #albm0061#2 = #albm0037#1;
                    #albm0061#3 = #albm0029#0;         ||Cod. Formula
                    #albm0061#4 = #albm0029#1;         ||Desc. Formula
                    #albm0061#11 = #albm0038#4;        ||Almacen
                    #albm0061#12 = #albm0038#5;        ||Desc. Almacen
                    #albm0061#13 = #albm0061#13 + #albm0038#8;

                    |GRABA 020#albm0061.a;
                    |LIBERA #albm0061;
               |FINSI;
          |SINO;
               nUltimo = nUltimo + 1;
               |DDEFECTO #albm0061;
               #albm0061#24 = nUltimo;
               |LEE 101#albm0061.=;
               |SI FSalida ! 0; |GRABA 020#albm0061.b; |FINSI;

               ||TODO CCC. Resto de campos
               #albm0061#1 = #albm0038#3;
               #albm0061#2 = #albm0037#1;
               #albm0061#3 = #albm0029#0;         ||Cod. Formula
               #albm0061#4 = #albm0029#1;         ||Desc. Formula
               #albm0061#11 = #albm0038#4;        ||Almacen
               #albm0061#12 = #albm0038#5;        ||Desc. Almacen
               #albm0061#13 = #albm0061#13 + #albm0038#8;

               |GRABA 020#albm0061.a;
               |LIBERA #albm0061;
          |FINSI;
          |CIERRA #albm0061;

          |DDEFECTO #albw0040;
          #albw0040#0 = #albm0061#24;
          #albw0040#2 = #albm0037#16;
          #albw0040#1 = #albm0037#0;
          |LEE 101#albw0040.=;
          |SI FSalida ! 0;
               |GRABA 020#albw0040.n;
          |FINSI;
          |LIBERA #albw0040;
     |FINSI;
|FPRC;

|DEFBUCLE albm0038;
 #albm0038#1;
 ;
 #albm0037#16, #albm0037#0, 01;
 #albm0037#16, #albm0037#0, 99;
 ;
 NULL, albm0038;
|FIN;

|PROCESO GeneraMezcladas;
     |ABRE #albm0029;
     |ABRE #albw0040;
     |BUCLE albm0038;
     |CIERRA #albw0040;
     |CIERRA #albm0029;
|FPRC;

|PROCESO CtrlModo; |TIPO 0;
     |SI FSalida = 1; |ERROR; |ACABA; |FINSI;
     nModoCab = (FEntrada / 100) ! 0;
     nSwHaSaltado = 0;
|FPRC;


|PROCESO ConsultaPar; |TIPO 0;
     |SI FSalida = 13;        ||F5.
          |HAZ LasPartidas;
     |FINSI;
|FPRC;


|PROCESO MiniPar;
     #142#4 = #albm0038#16;
     #142#0 = #albm0038#0;
     #142#1 = #albm0038#1;
     #142#2 = 00001;
     #142#3 = "               ";
|FPRC;

|PROCESO MaxiPar;
     #142#4 = #albm0038#16;
     #142#0 = #albm0038#0;
     #142#1 = #albm0038#1;
     #142#2 = 99999;
     #142#3 = "zzzzzzzzzzzzzzz";
|FPRC;

|PROCESO albm0062_borra;
     |BORRA 020#albm0062.a;
|FPRC;

|DEFBUCLE albm0062_borra;
 #albm0062#1;
 ;
|| #albm0038#16, #albm0038#0, #albm0038#1, 00001, "               ";
|| #albm0038#16, #albm0038#0, #albm0038#1, 99999, "zzzzzzzzzzzzzzz";
 #albm0038#16, #albm0038#0, 00001, "               ";
 #albm0038#16, #albm0038#0, 99999, "zzzzzzzzzzzzzzz";
 ;
 NULL, albm0062_borra;
|FIN;

|PROCESO albm0062;
     |DDEFECTO #albw0042;
     #albw0042#0 = #albm0062#0;
     #albw0042#1 = #albm0062#1;
     #albw0042#2 = #albm0062#2;
     #albw0042#3 = #albm0062#3;
     #albw0042#4 = #albm0062#4;
     |LEE 101#albw0042.=;
     |SI FSalida ! 0;
          #albw0042#5 = #albm0062#5;
          |GRABA 020#albw0042.n;
     |SINO;
          #albw0042#5 = #albm0062#5;
          |GRABA 020#albw0042.a;
     |FINSI;
     |LIBERA #albw0042;
|FPRC;

|DEFBUCLE albm0062;
 #albm0062#1;
 ;
|| #albm0038#16, #albm0038#0, #albm0038#1, 00001, "               ";
|| #albm0038#16, #albm0038#0, #albm0038#1, 99999, "zzzzzzzzzzzzzzz";
 #albm0038#16, #albm0038#0, 00001, "               ";
 #albm0038#16, #albm0038#0, 99999, "zzzzzzzzzzzzzzz";
 ;
 NULL, albm0062;
|FIN;

|PROCESO albw0042;
     |DDEFECTO #albm0062;
     #albm0062#0 = #albw0042#0;
     #albm0062#1 = #albw0042#1;
     #albm0062#2 = #albw0042#2;
     #albm0062#3 = #albw0042#3;
     #albm0062#4 = #albw0042#4;
     |LEE 101#albm0062.=;
     |SI FSalida ! 0;
          #albm0062#5 = #albw0042#5;
          |GRABA 020#albm0062.n;
     |SINO;
          #albm0062#5 = #albw0042#5;
          |GRABA 020#albm0062.a;
     |FINSI;
     |LIBERA #albm0062;
|FPRC;

|DEFBUCLE albw0042;
 #albw0042#1;
 ;
|| #albm0038#16, #albm0038#0, #albm0038#1, 00001, "               ";
|| #albm0038#16, #albm0038#0, #albm0038#1, 99999, "zzzzzzzzzzzzzzz";
 #albm0038#16, #albm0038#0, 00001, "               ";
 #albm0038#16, #albm0038#0, 99999, "zzzzzzzzzzzzzzz";
 ;
 NULL, albw0042;
|FIN;

|PROCESO LasPartidas;
     |HAZ CreaTempo; aTempo142 = Tempo;
     |NOME_DAT #142 aTempo142; |DELINDEX #142;

     |ABRE #albw0042;
     |BUCLE albm0062;

     |PUSHV 1558 2380;
     |PINPA #0#142;
     |ENTLINEAL #1#142, 3, 1, 22, 0, MiniPar, MaxiPar;
     |POPV;
     |CIERRA #albw0042;

     |BUCLE albm0062_borra;
     |ABRE #albm0062;
     |BUCLE albw0042;
     |CIERRA #albm0062;

     |DELINDEX #142; Tempo = aTempo142; |HAZ BoraTempo;
|FPRC;

|PROCESO LineaFabri; |TIPO 0;
     |SI FSalida = 1; |ERROR; |ACABA; |FINSI;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 2;
          aArtDos = #albm0037#2;
          |QBF aArtDos;
          |SI aArtDos ! aArtAnt;
               aMensaje = "0000No es posible modificiar el Articulo de la Orden de Fabricacion";
               |MENAV aMensaje;
               #albm0037#2 = aArtAnt;
               |PINTA #albm0037#2;
               |ERROR;
               |ACABA;
          |FINSI;
     |FINSI;

     |ABRE #albm0026;
     |DDEFECTO #albm0026;
     #albm0026#0 = #albm0037#2;
     |LEE 000#albm0026.=;
     |SI FSalida = 0;
          #albm0037#6 = #albm0026#3;
          |PINTA #albm0037#6;
     |FINSI;
     |CIERRA #albm0026;
|FPRC;


|PROCESO SerDefecto; |TIPO 7;
     nModoSer = (FEntrada / 100) ! 0;
     |SI nModoSer = 1;
          |ABRE #albm0013;
          |DDEFECTO #albm0013;
          |LEE 000#albm0013.p;
          |CIERRA #albm0013;
          #albm0037#16 = #albm0013#9;
          |PINTA #albm0037#16;

          |SI nSwHaSaltado = 0;
               nSwHaSaltado = 1;
               |PTEC 802;
          |FINSI;
     |FINSI;
|FPRC;


|PROCESO CtrlSalto; |TIPO 11;
     |SI FSalida = 1; |O FSalida = 7;        ||ESC o Ctrl + C.
          nSwHaSaltado = 0;
     |FINSI;
|FPRC;

|PROCESO albm0039_temp;
     nLinea = #albm0039#1;
     nLinea = nLinea + 3;          ||La primera es la CUATRO = 3 + 1
     #albw0018#nLinea# = #albm0039#2;
|FPRC;

|DEFBUCLE albm0039_temp;
 #albm0039#1;
 ;
 #albm0037#16, #albm0037#0, 001;
 #albm0037#16, #albm0037#0, 008;
 ;
 NULL, albm0039_temp;
|FIN;

|PROCESO PonCliente; |TIPO 0;
     aCliente = #albw0018#0;

     #albw0018#4 = "";
     #albw0018#5 = "";
     #albw0018#6 = "";
     #albw0018#7 = "";
     #albw0018#8 = "";
     #albw0018#9 = "";
     #albw0018#10 = "";
     #albw0018#11 = "";
     #albw0018#12 = "";
     #albw0018#13 = "";
     #albw0018#14 = "";
     #albw0018#15 = "";
     |PINTA #albw0018#4;

     |BUCLE Borra39;
     |ABRE #3;
     nLinEs = 0;
     |BUCLE CargaEspe;
     |CIERRA #3;

     |BUCLE albm0039_temp;

     |PINTA #albw0018#4;
|FPRC;
