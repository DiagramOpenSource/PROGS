|FICHEROS;
     berz0004 #0;
     berm0001 #1;
     berm0002 #2;
     berm0004 #4;
     referen@;

     agifa019 #19;       ||Articulos
     agifa060 #60;       ||Familias
     agifa120 #120;      ||Subfamilias
|FIN;

|VARIABLES;
     aProg = "";
     aOrigen = "";
     aDestino = "";
     aIPRemoto = "";
     aPathPaquete = "";
     fSistema = @;
     aSistema = "";
     aDia = "";
     aMes = "";
     aAny = "";
     aHora = "";
     aHH = "";
     aMM = "";
     aNombre = "";
     aAlfa1 = "";
     aAlfa2 = "";

     aLetra = "";
     aMensaje = "";
     aPathExpo = "";
     aPathExpo2 = "";
     aArticulo = "";
     {100}aFic = "";
     aAlfa = "";
     aTab = "";
     aLF = "";
     aPath = "";
     nHandle = 0;
     nCampos = "";
     i = 0;
     nSw = 0;
     aShell = "";
     aParam1 = "";
     aParam2 = "";
     aParam3 = "";
     &enTipo = 0;
     &Tempo = "";
     nInfor = -1;
     nImpre = -1;
     nPrime = 0;
     aHost = "";
     aUsu = "";
     aPass = "";
     aClave = "";
|FIN;

|PROCESO EscanFin;
     |LEE 000#referen@.p;
     |SI FSalida = 0;
          |SI nImpre = -1; |Y nPrime = 0;
               nPrime = 1;
               |CONFI "0000SImprimir errores en componentes S/N:";
               |SI FSalida = 0;
                    |IMPRE 0;
                    nImpre = FSalida;
               |FINSI;
               |SI FSalida = 0;
                    |INFOR "berz0004";
                    nInfor = FSalida;
                    |SI FSalida ! 0;
                         |MENAV "0000Error en informe 'berz0004'";
                    |FINSI;
               |FINSI;
          |FINSI;
          |SI nInfor = 0;
               enTipo = 1; |PRINT;
               |PARA FSalida = 0; |SI FSalida = 0; |HACIENDO;
                    |SALVA_DATOS #referen@;
                    |REPON_DATOS #4;
                    enTipo = 2; |PRINT;
                    |LEE 000#referen@.s;
               |SIGUE;
               enTipo = 3; |PRINT;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO EscanLin;
     #referen@#0 = #2#22;
     |BORRA 000#referen@.c;
|FPRC;

|DEFBUCLE EscanLin;
     #2#1;
     ;
     #1#0, 001;
     #1#0, 999;
     ;
     NULL, EscanLin;
|FIN;

|PROCESO TipoCom;
     |SI #4#3 = "S";
          |SALVA_DATOS #4;
          |REPON_DATOS #referen@;
          |GRABA 020#referen@.n;
     |FINSI;
|FPRC;

|DEFBUCLE TipoCom;
     #4#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, TipoCom;
|FIN;

|PROCESO EscanCab;
     #19#0 = #1#0;
     |LEE 000#19=;
     aAlfa = #19#2; |QBT aAlfa;
     aAlfa = ("000" + aAlfa) % -103;
     |SI aAlfa ] "100"; |Y aAlfa [ "199";
          |DELINDEX #referen@;
          |ABRE #referen@;
          |BUCLE TipoCom;
          |BUCLE EscanLin;
          |HAZ EscanFin;
          |CIERRA #referen@;
     |FINSI;
|FPRC;

|DEFBUCLE Escandallo;
     #1#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, EscanCab;
|FIN;

|PROCESO CheqEscan;
     |DDEF aAlfa;
     aAlfa = aAlfa + "berm0004";
     |CARGA_DEF aAlfa, referen@;
     |SI FSalida = 0;
          |HAZ CreaTempo; |NOME_DAT #referen@#Tempo#;

          |ABRE #19;
          |BUCLE Escandallo;
          |CIERRA #19;

          |DELINDEX #referen@; |HAZ BoraTempo;
          |DESCARGA_DEF #referen@;
     |FINSI;
     |SI nInfor = 0; |PIEINF; |FININF; |FINSI;
     |SI nImpre = 0; |FINIMP; |FINSI;
|FPRC;

|PROCESO Browse; |TIPO 0;
     |SI FSalida = 13;
          aAlfa = "DSeleccione el directorio local de exportaci¢n";
          |BROWSE aAlfa;
          aAlfa = aAlfa % 2;
          |QBF aAlfa;
          |SI aAlfa ! "D";
               aAlfa = aAlfa + "\";
               #0#1 = aAlfa;
               |PINTA #0#1;
          |FINSI;
     |SINO;
          aAlfa = #0#1;
          |QBF aAlfa;
          |SI aAlfa ! "";
               aLetra = aAlfa % -101;
               |SI aLetra ! "\"; |Y aLetra ! "/";
                    aAlfa = aAlfa + "\";
                    #0#1 = aAlfa;
                    |PINTA #0#1;
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;


|PROCESO Shell;
     ||Esto ya no se usa

     |DBASE aPath;
     aParam1 = aPath + "logos/tipo/"
     aParam2 = aPath + "logos/cara/";
     aParam3 = aPath + "txt/";

     |QUE_SISTEMA aAlfa;
     |SI aAlfa = "ESDOS";
          aShell = "berria.bat";
          aShell = aPath + "plugins/" + aShell + " " + aParam1 + " " + aParam2 + " " + aParam3;
          aShell =  "cmd " + &34 + " /c START /MIN /WAIT " + aShell + &34;
          |SYSTEM aShell;
     |SINO;
          aShell = "berria.sh";
          aShell = aPath + "plugins/" + aShell + " " + aParam1 + " " + aParam2 + " " + aParam3;
          |SYSTEM aShell;
     |FINSI;
|FPRC;

|PROCESO Exporta;
     aAlfa = aPath + aFic + ".txt";
     |XABRE "BW", aAlfa, nHandle;
     |SI FSalida ] 0;
          |DDEF aAlfa;
          aAlfa = aAlfa + aFic;
          |CARGA_DEF aAlfa, referen@;
          |SI FSalida = 0;
               aAlfa = "|NC";
               nCampos = #referen@#aAlfa#;
               |ABRE #referen@;
               |LEE 000#referen@.p;
               |PARA; |SI FSalida = 0; |HACIENDO;
                    |PARA i = 0; |SI i < nCampos; |HACIENDO i = i + 1;
                         aAlfa = #referen@#i# + aTab;
                         |XGRABA nHandle, aAlfa;

                         |SI i = 0; |Y aFic = "berm0001";
                              aArticulo = #referen@#i#;
                         |FINSI;
                    |SIGUE;

                    ||Grabamos para cada linea de escadallo,
                    ||la linea, familia y subfamilia
                    |DDEFECTO #agifa019;
                    #agifa019#0 = aArticulo;
                    |LEE 000#agifa019.=;
                    aAlfa = #agifa019#125 + aTab;
                    |XGRABA nHandle, aAlfa;
                    aAlfa = #agifa019#2 + aTab;
                    |XGRABA nHandle, aAlfa;
                    aAlfa = #agifa019#3 + aTab;
                    |XGRABA nHandle, aAlfa;

                    |XGRABA nHandle, aLF;
                    |LEE 000#referen@.s;
               |SIGUE;
               |CIERRA #referen@;
               |DESCARGA_DEF #referen@;
          |SINO;
               aAlfa = "0000Error al cargar:" + aFic;
               |SI nSw = 0; |MENAV aAlfa; |FINSI;
          |FINSI;
          |XCIERRA nHandle;
     |SINO;
          aAlfa = "0000Error al crear txt:" + aFic;
          |SI nSw = 0; |MENAV aAlfa; |FINSI;
     |FINSI;
|FPRC;

|PROCESO ExportaFamilias;
     |PARA i = 0; |SI i < nCampos; |HACIENDO i = i + 1;
          aAlfa = #agifa060#i# + aTab;
          |XGRABA nHandle, aAlfa;
     |SIGUE;
     |XGRABA nHandle, aLF;
|FPRC;

|DEFBUCLE ExportaFamilias;
 #agifa060#1;
 ;
 "100";
 "199";
 ;
 NULL, ExportaFamilias;
|FIN;

|PROCESO ExportaSubFamilias;
     |PARA i = 0; |SI i < nCampos; |HACIENDO i = i + 1;
          aAlfa = #agifa120#i# + aTab;
          |XGRABA nHandle, aAlfa;
     |SIGUE;
     |XGRABA nHandle, aLF;
|FPRC;

|DEFBUCLE ExportaSubFamilias;
 #agifa120#1;
 ;
 "10000";
 "19999";
 ;
 NULL, ExportaSubFamilias;
|FIN;

|PROCESO Tipo3; |TIPO 3;
     |SI #0#0 ! "SI"; |ACABA; |FINSI;

     |HAZ CheqEscan;
     |SI nPrime = 1;
          |MENAV "0000Exportaci¢n anulada por errores en componentes...";
          |ACABA;
     |FINSI;

     aFic1 = "berm0001";
     aFic2 = "berm0002";
     aFic3 = "berm0003";
     aFic4 = "berm0004";
     aFic5 = "berm0005";
     aFic6 = "berm0006";
     aFic7 = "berm0007";
     aFic8 = "berm0010";
     aFic9 = "berm0011";
     aFic10 = "berm0012";
     aFic11 = "berm0013";
     aFic12 = "berm0014";
     aFic13 = "berm0015";
     aFic14 = "agifa553";
     aFic15 = "FIN";

     aTab = &9;
     aLF = &13; aLF = aLF + &10;
     |DBASE aPath;
     aPath = aPath + "logos/";
     |MKDIR aPath;
     aPathPaquete = aPath;
     aPath  = aPath + "txt/";
     |MKDIR aPath;

     |ABRE #agifa019;

     IaFic = aFic1<-;
     |PARA; |SI; |HACIENDO;
          |SI aFic = "FIN"; |SAL; |FINSI;
          |HAZ Exporta;
          IaFic = IaFic + 1;
     |SIGUE;

     aFic = "agifa060";
     aAlfa = aPath + aFic + ".txt";
     |XABRE "BW", aAlfa, nHandle;
     |SI FSalida ] 0;
          aAlfa = "|NC";
          nCampos = #agifa060#aAlfa#;
          |BUCLE ExportaFamilias;
          |XCIERRA nHandle;
     |SINO;
          aAlfa = "0000Error al crear txt:" + aFic;
          |SI nSw = 0; |MENAV aAlfa; |FINSI;
     |FINSI;

     aFic = "agifa120";
     aAlfa = aPath + aFic + ".txt";
     |XABRE "BW", aAlfa, nHandle;
     |SI FSalida ] 0;
          aAlfa = "|NC";
          nCampos = #agifa120#aAlfa#;
          |BUCLE ExportaSubFamilias;
          |XCIERRA nHandle;
     |SINO;
          aAlfa = "0000Error al crear txt:" + aFic;
          |SI nSw = 0; |MENAV aAlfa; |FINSI;
     |FINSI;

     |CIERRA #agifa019;

     ||HAZ Shell;

     fSistema = ~;
     aSistema = fSistema;
     aDia = aSistema % 102;
     aMes = aSistema % 402;
     aAny = aSistema % -104;

     |HORA aHora;
     |QBF aHora;
     aHH = aHora % 102;
     aMM = aHora % 402;

     aNombre = aAny + aMes + aDia + aHH + aMM;

     aAlfa1 = aPathPaquete + aNombre + ".tar";
     |FBORRA aAlfa1;
     aAlfa1 = aPathPaquete + aNombre + ".tgz";
     |FBORRA aAlfa1;

     aAlfa1 = aPathPaquete + aNombre + ".tar " + "cara colores cweb tipo txt fotonoticias";
     aAlfa2 = aPathPaquete;
     |ATAR aAlfa1, aAlfa2;

     aAlfa1 = aPathPaquete + aNombre + ".tar";
     |COMPRIME aAlfa1;

     aOrigen = aPathPaquete + aNombre + ".tgz";
/*
     aDestino = #0#1;
     |QBF aDestino;
     aDestino = aDestino + aNombre + ".tgz";

     aIPRemoto = "";
     |IP_REMOTO aIPRemoto;  |QBT aIPRemoto;
     |SI aIPRemoto ! "";
          |ENVIA_FICHERO aOrigen, aDestino;
     |SINO;
          |COPIA_FICHERO aOrigen, aDestino;
     |FINSI;
^*/

     aHost = #0#2; |QBF aHost;
     aUsu = #0#4; |QBF aUsu;
     aPass = aClave; |QBF aPass;
     aDestino = aNombre + ".tgz";

     |DBASE aAlfa;
     aAlfa = aAlfa + "plugins/ftpberria.sh ";
     aProg = "chmod 777 + " + aAlfa;
     |SYSTEM aProg;

     aProg = aAlfa + aHost + " " + aUsu + " " + aPass + " " + aOrigen + " " + aDestino;
     aProg = aProg + " > /tmp/ftp.log"
     |SYSTEM aProg;

     aAlfa1 = aPathPaquete + aNombre + ".tar";
     |FBORRA aAlfa1;
|FPRC;

|PROCESO Clave0; |TIPO 0;
     aClave = #0#3;
     #0#3 = "*" * 20; |PINTA #0#3;
|FPRC;

|PROCESO Clave7; |TIPO 7;
     #0#3 = aClave;
|FPRC;

|PROGRAMA;
     |ABRE #0;
     |LEE 101#0p;
     |SI FSalida ! 0; |GRABA 020#0b; |FINSI;

     |SI PARAMETRO = "menu";
          aClave = #0#3; #0#3 = "*" * 20;
          #0#1 = aPathExpo;
          |PINPA #0#0;
          |PINDA #0#0;
          |ENDATOS #1#0;
          |SI FSalida = 0; |Y #0#0 ! "SI";
               #0#3 = aClave;
               |GRABA 020#0a;
          |FINSI;
     |SINO;
          nSw = 1;
          #0#0 = "SI";
          |HAZ Tipo3;
     |FINSI;
     |CIERRA #0;
|FPRO;
