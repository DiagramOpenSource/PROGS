|FICHEROS;
     pcegz041 #0;
     pcegm028 #28;
     pcegm029 #29;
     pcegm030 #30;
|FIN;

|VARIABLES;
     aNom = "";
     aLon = "";
     aFic = "";
     aPath = "";
     aDes = "";
     aAlfa = "";
     aIP = "";
     nPos = 0;
     nReg = 0;
     nSwAuto = 0;
     j = 0;
     nYaPaso =0;
     nRem = 0;
     aCar0 = "";
     aCar1 = "";
     nHandle = 0;
     nPrime = 0;
     nFinCod = 0;
     nNivel = 0;
     aTmp = "";
     aTmp1 = "";
     aIniCab = "";
     aIniLin = "";
     nCampo = 0;
     {100}Matriz = "";

     aTagA = "";
     aTagB = "";
     aTagC = "";
     aVal = "";
     aIniTag = "N";
     aFinTag = "N";
     aIniPro = "N";
     aFinPro = "N";
     aFinTagVacio = "N";
     nError = 0;
     nLinea = 0;
     aDia = "";
     aMes = "";
     aA¤o = "";
     nNume = 0;
     nSal = 0;
     aTagA1 = "";
     aTagA2 = "";
     aTagA3 = "";
     aTagB1 = "";
     aTagB2 = "";
     aTagB3 = "";
     aTagC1 = "";
     aTagC2 = "";
     aTagC3 = "";
     aFecha = "";
     aHora = "";
     aDatoA = "";
     aDatoB = "";
     aDatoC = "";
     aCampo = "";
     aValor = "";
     aValor1 = "";
     aValor2 = "";
     nSw = 0;
     aPos1 = "";
     aPos2 = "";
     nPos1 = 0;
     nPos2 = 0;
     nIni = 0;
     i = 0;
     aCar = "";
     nEmpieza = 0;
     aTemp1 = "";
     aTemp = "";
     aCam1 = "";
     aCam2 = "";
     aCam3 = "";
     aCam4 = "";
     aCam5 = "";
     nCar = 0;
     aCampos = "";
     ii = 0;
     aNume = "";
|FIN;

|PROCESO SoloNum;
     aAlfa = aValor;
     aLon = aAlfa % A0;
     aValor = "";
     |PARA ii = 0; |SI ii [ aLon; |HACIENDO ii = ii + 1;
          nPos = ii * 100 + 1;
          aCar = aAlfa % nPos;
          aNume = "0123456789";
          aNume = aNume - aCar;
          |SI FSalida ! 0;
               aValor = aValor + aCar;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO PonRaros;
     |OEM_ANSI #29#4, #29#4;
     |OEM_ANSI #29#5, #29#5;
     |OEM_ANSI #29#6, #29#6;
     |OEM_ANSI #29#7, #29#7;
     |OEM_ANSI #29#8, #29#8;
     |OEM_ANSI #29#11, #29#11;
     |OEM_ANSI #29#12, #29#12;
     |OEM_ANSI #29#15, #29#15;
     |OEM_ANSI #29#16, #29#16;
     |OEM_ANSI #29#18, #29#18;
     |OEM_ANSI #29#19, #29#19;
     |OEM_ANSI #29#20, #29#20;
     |OEM_ANSI #29#21, #29#21;
     |OEM_ANSI #29#22, #29#22;
     |OEM_ANSI #29#23, #29#23;
     |OEM_ANSI #29#25, #29#25;
     |OEM_ANSI #29#26, #29#26;
     |OEM_ANSI #29#35, #29#35;
     |OEM_ANSI #29#36, #29#36;
     |OEM_ANSI #29#37, #29#37;
     |OEM_ANSI #29#38, #29#38;
     |OEM_ANSI #29#39, #29#39;
|FPRC;

|PROCESO CifCarNoValidos;
     aAlfa = "";
     aLon = aValor % A0;
     |PARA i = 1; |SI i [ aLon; |HACIENDO i = i + 1;
          nPos = 100 * i + 1;
          aCar = aValor %  nPos;
          aCar = aCar > "";
          nCar = &aCar;
          |SI nCar ] 48; |Y nCar [ 57; aAlfa = aAlfa + aCar; |FINSI;  ||0-9
          |SI nCar ] 65; |Y nCar [ 90; aAlfa = aAlfa + aCar; |FINSI;  ||A-Z
          |SI nCar ] 97; |Y nCar [ 122; aAlfa = aAlfa + aCar; |FINSI; ||a-z
     |SIGUE;
     aValor = aAlfa;
|FPRC;

|PROCESO FechaHora;
     aAlfa = aValor % 501;
     aAlfa = "0123456789" - aAlfa;
     |SI FSalida > 0;    ||El formato es 08-12-2011
          aDia = aValor % 102;
          aMes = aValor % 402;
          aA¤o = aValor % 704;
     |SINO;              ||El formato es 2011-12-08
          aA¤o = aValor % 104;
          aMes = aValor % 602;
          aDia = aValor % 902;
     |FINSI;
     |SI aDia = 0; aDia = "01"; |FINSI;
     |SI aMes = 0; aMes = "01"; |FINSI;
     aFecha = aDia + "." + aMes + "." + aA¤o;
     aHora = aValor % 1208;
|FPRC;

|PROCESO BusCam;
     |SI nEmpieza = 2;
          nNume = &aCar;
          |SI nNume = 34;     || 34 = "
               nEmpieza = 3;
               |ACABA;
          |FINSI;
          aLon = aValor % A0;
          |SI aLon < 250;
               aValor = aValor + aCar;
          |SINO;
               aLon = aValor1 % A0;
               |SI aLon < 250;
                    aValor1 = aValor1 + aCar;
               |SINO;
                    aValor2 = aValor2 + aCar;
               |FINSI;
          |FINSI;
          |ACABA;
     |FINSI;

     |SI nEmpieza = 1;
          nNume = &aCar;
          |SI nNume = 34;     || 34 = "
               nEmpieza = 2;
          |FINSI;
          |ACABA;
     |FINSI;

     |SI aCar = " "; |ACABA; |FINSI;|| el nombre del campo no puede tener blancos

     aLon = aTemp1 % A0;
     |SI aLon > 250;
          aTemp1 = aTemp1 % 2;
          aTemp1 = aTemp1 + aCar;
     |SINO;
          aTemp1 = aTemp1 + aCar;
     |FINSI;

     aTemp1 = aTemp1 - aCampo;
     |SI FSalida > 0;
          nEmpieza = 1;
     |FINSI;
|FPRC;

|PROCESO BusCampo;
     aCampo = aCampo + "=";
     aValor = "";
     aValor1 = "";
     aValor2 = "";
     nEmpieza = 0;
     aTemp1 = "";
     |PARA i = 1; |SI i [ 3; |HACIENDO i = i + 1;
          |SI i = 1; aTemp = aDatoA; |FINSI;
          |SI i = 2; aTemp = aDatoB; |FINSI;
          |SI i = 3; aTemp = aDatoC; |FINSI;
          |PARA; |SI aTemp ! ""; |HACIENDO;
               aCar = aTemp % 101;
               |SI aTemp = aCar;
                    aTemp = "";
               |SINO;
                     aTemp = aTemp % 2;
               |FINSI;
               |HAZ BusCam;
               |SI nEmpieza = 3; |ACABA; |FINSI;
          |SIGUE;
     |SIGUE;
|FPRC;

|PROCESO PonObs;    || El tama¤o de los campos obs es de 70
     nCar = 0;
     aCam1 = "";
     aCam2 = "";
     aCam3 = "";
     aCam4 = "";
     aCam5 = "";
     |PARA i = 1; |SI i [ 3; |HACIENDO i = i + 1;
          |SI i = 1; aTemp = aValor; |FINSI;
          |SI i = 2; aTemp = aValor1; |FINSI;
          |SI i = 3; aTemp = aValor2; |FINSI;
          |PARA; |SI aTemp ! ""; |HACIENDO;
               aCar = aTemp % 101;
               |SI aTemp = aCar;
                    aTemp = "";
               |SINO;
                    aTemp = aTemp % 2;
               |FINSI;
               nCar = nCar + 1;
               |SI nCar < 71; aCam1 = aCam1 + aCar; |FINSI;
               |SI nCar ] 71; |Y nCar < 141; aCam2 = aCam2 + aCar; |FINSI;
               |SI nCar ] 141; |Y nCar < 211; aCam3 = aCam3 + aCar; |FINSI;
               |SI nCar ] 211; |Y nCar < 281; aCam4 = aCam4 + aCar; |FINSI;
               |SI nCar ] 281; |Y nCar < 351; aCam5 = aCam5 + aCar; |FINSI;
          |SIGUE;
     |SIGUE;
     #29#7 = aCam1;
     #29#35 = aCam2;
     #29#36 = aCam3;
     #29#37 = aCam4;
     #29#38 = aCam5;
|FPRC;

|PROCESO Graba;
     |SI nLinea = 0;     || No se ha grabado la cabecera
          aDatoA = aTagA1;
          aDatoB = aTagB1;
          aDatoC = aTagC1;
          aCampo = "Firstname";    |HAZ BusCampo; aNom = aValor;
          aCampo = "Lastname";     |HAZ BusCampo; aNom = aNom + " " + aValor; #29#11 = aNom;
          aCampo = "Company";      |HAZ BusCampo; #29#12 = aValor;
          aCampo = "Street";       |HAZ BusCampo; #29#15 = aValor;
          aCampo = "Suburb";       |HAZ BusCampo; ||?
          aCampo = "City";         |HAZ BusCampo; #29#16 = aValor;
          aCampo = "Postcode";     |HAZ BusCampo; #29#17 = aValor;
          aCampo = "State";        |HAZ BusCampo; #29#18 = aValor;
          aCampo = "Country_Code"; |HAZ BusCampo; #29#19 = aValor;

          aDatoA = aTagA2;
          aDatoB = aTagB2;
          aDatoC = aTagC2;
          aCampo = "Firstname";    |HAZ BusCampo; aNom = aValor;
          aCampo = "Lastname";     |HAZ BusCampo; aNom = aNom + " " + aValor; #29#20 = aNom;
          aCampo = "Company";      |HAZ BusCampo; #29#21 = aValor;
          aCampo = "Street";       |HAZ BusCampo; #29#22 = aValor;
          aCampo = "Suburb";       |HAZ BusCampo; ||?
          aCampo = "City";         |HAZ BusCampo; #29#23 = aValor;
          aCampo = "Postcode";     |HAZ BusCampo; #29#24 = aValor;
          aCampo = "State";        |HAZ BusCampo; #29#25 = aValor;
          aCampo = "Country_Code"; |HAZ BusCampo; #29#26 = aValor;

          aDatoA = aTagA3;
          aDatoB = aTagB3;
          aDatoC = aTagC3;
          aCampo = "OrderId";      |HAZ BusCampo; #29#0 = aValor;
          aCampo = "Payment";      |HAZ BusCampo; #29#8 = aValor;
          aCampo = "BasketId";     |HAZ BusCampo; #29#41 = aValor;
          aCampo = "ShippingMethod";|HAZ BusCampo;#29#39 = aValor;
          aCampo = "Shipping";     |HAZ BusCampo; #29#9 = aValor;
          aCampo = "Comment";      |HAZ BusCampo; |HAZ PonObs;
          aCampo = "Date_Purchased";|HAZ BusCampo; |HAZ FechaHora; #29#1 = aFecha; #29#2 = aHora;
          aCampo = "OrdersTotal";  |HAZ BusCampo; #29#43 = aValor;
          aCampo = "CustomerId";   |HAZ BusCampo; #29#3 = aValor;
          aCampo = "CustomersTaxId";|HAZ BusCampo; #29#13 = aValor;
          aCampo = "CustomersVatId";|HAZ BusCampo; #29#14 = aValor;
          aCampo = "CustomerEmail";|HAZ BusCampo; #29#4 = aValor;
          aCampo = "CustomerPhone";|HAZ BusCampo; |HAZ SoloNum; #29#5 = aValor;
          aCampo = "CustomerFax";  |HAZ BusCampo; |HAZ SoloNum; #29#6 = aValor;
          aCampo = "Tax_Code";     |HAZ BusCampo; #29#10 = aValor;

          aAlfa = #29#11; |QBF aAlfa;
          |SI aAlfa = ""; #29#11 = #29#20; |FINSI;

          aAlfa = #29#21; |QBF aAlfa;
          |SI aAlfa = ""; #29#21 = #29#20; |FINSI;

          aAlfa = #29#12; |QBF aAlfa;
          |SI aAlfa = ""; #29#12 = #29#21; |FINSI;

          |SI #29#19 = #28#9; || Espa¤a;
               aAlfa = #29#17; |QBT aAlfa;
               aAlfa = ("00000" + aAlfa) % -105;
               #29#17 = aAlfa;
          |FINSI;
          |SI #29#26 = #28#9; || Espa¤a;
               aAlfa = #29#24; |QBT aAlfa;
               aAlfa = ("00000" + aAlfa) % -105;
               #29#24 = aAlfa;
          |FINSI;
          aValor = #29#13; |HAZ CifCarNoValidos; #29#13 = aValor;
          aValor = #29#14; |HAZ CifCarNoValidos; #29#14 = aValor;

          |HAZ PonRaros;

          |GRABA 000#29n;
          |SI FSalida ! 0;
               aAlfa = ("0000000" + #29#0)% -107;
               aAlfa = "0000Pedido existente: " + aAlfa;
               |SI nSwAuto = 0; |MENAV aAlfa; |FINSI;
               nError = 1;
               |ACABA;
          |FINSI;
     |FINSI;
     aDatoA = aTagA;
     aDatoB = aTagB;
     aDatoC = aTagC;
     nLinea = nLinea + 1;
     #30#0 = #29#0;
     #30#1 = nLinea;
     aCampo = "Product_Name";      |HAZ BusCampo; #30#3 = aValor;
     aCampo = "Product_Model";     |HAZ BusCampo; #30#2 = aValor;
     aCampo = "Product_Price";     |HAZ BusCampo; #30#4 = aValor;
     aCampo = "Product_Quantity";  |HAZ BusCampo; #30#8 = aValor;
     aCampo = "Product_Discount";  |HAZ BusCampo; #30#5 = aValor;
     aCampo = "Product_Tax";       |HAZ BusCampo; #30#7 = aValor;
     aCampo = "Final_Price";       |HAZ BusCampo; #30#6 = aValor;

     aAlfa = #29#10; |QBF aAlfa;
     |SI aAlfa = "0";
          #30#7 = 0;
     |FINSI;
     |GRABA 000#30n;
|FPRC;

|PROCESO QuitaBlancos; || a Izq y Der
     |QBF aValor;
     |PARA; |SI; |HACIENDO;
          aAlfa = aValor % 101;
          |SI aAlfa ! " "; |SAL; |FINSI;
          aLon = aValor % A0;
          |SI aLon [ 1; |SAL; |FINSI;
          aValor = aValor % 2;
     |SIGUE;
|FPRC;

|PROCESO ProcesaTagPop;
|FPRC;

|PROCESO ProcesaTagPush;
|FPRC;

|PROCESO ProcesaTagVacio;
     aAlfa = aTagA % 111;
     |SI aAlfa = "InvoiceAddr";
          aTagA1 = aTagA;
          aTagB1 = aTagB;
          aTagC1 = aTagC;
     |FINSI;
     |SI aAlfa = "DeliverAddr";
          aTagA2 = aTagA;
          aTagB2 = aTagB;
          aTagC2 = aTagC;
     |FINSI;
     aAlfa = aTagA % 109;
     |SI aAlfa = "OrderInfo";
          nLinea = 0;
          aTagA3 = aTagA;
          aTagB3 = aTagB;
          aTagC3 = aTagC;
     |FINSI;
     aAlfa = aTagA % 107;
     |SI aAlfa = "Product";
          |HAZ Graba;

          aTagA1 = ""; aTagA2 = ""; aTagA3 = "";
          aTagB1 = ""; aTagB2 = ""; aTagB3 = "";
          aTagC1 = ""; aTagC2 = ""; aTagC3 = "";
     |FINSI;
|FPRC;

|PROCESO ProcesaPrologo;
|FPRC;

|PROCESO ProcesaRem;
|FPRC;

|PROCESO PopTag;
     nNivel = nNivel - 2;
     IMatriz = Matriz1<-;
     IMatriz = IMatriz + nNivel;
     FSalida = Matriz;
|FPRC;

|PROCESO PushTag;
     IMatriz = Matriz1<-;
     IMatriz = IMatriz + nNivel;
     Matriz = aTagA;
     nNivel = nNivel + 1;
     Matriz = aTagB;
     nNivel = nNivel + 1;
|FPRC;

|PROCESO Caracter;
     |SI aCar0 < " "; |O nError ! 0; |ACABA; |FINSI;

     |SI nRem > 0;
          |SI nRem < 3;
               |SI aCar0 = "-";
                    nRem = nRem + 1;
               |SINO;
                    aAlfa = "0000ERROR en XML: " + aTagA;
                    |SI nSwAuto = 0; |MENAV aAlfa; |FINSI;
                    nError = 1;
               |FINSI;
          |SINO;
               |SI aCar0 = ">"; || Un comentario no puede tener el caracter '>'
                    aAlfa = aTagA % -102;
                    |SI aAlfa = "--";
                         aLon = aTagA % A0;
                         nPos = (aLon - 2) + 100;
                         aTagA = aTagA % nPos;
                         |HAZ ProcesaRem;
                    |SINO;
                         aAlfa = "0000ERROR en XML: " + aTagA;
                         |SI nSwAuto = 0; |MENAV aAlfa; |FINSI;
                         nError = 1;
                    |FINSI;
                    aTagA = ""; nRem = 0;
               |SINO;
                    aLon = aTagA % A0;
                    |SI aLon < 250; aTagA = aTagA + aCar0; |FINSI;
               |FINSI;
          |FINSI;
          |ACABA;
     |FINSI;

     |SI aCar0 = "<";
          aIniTag = "S"; aTagA = ""; aTagB = "";
     |SINO;
          |SI aIniTag = "S";
               |SI aCar0 = "/"; |Y aCar1 = ">";
                    |SI aTagA = "";
                         aFinTag = "S";
                    |SINO;
                         aFinTagVacio = "S";
                    |FINSI;
               |SINO;
/*
Comento los REM, ya que si el caracter '!' esta en un valor no funciona
y estos xml no traen REM
                    |SI aCar0 = "!";
                         |SI aTagA = "";
                              nRem = 1;
                         |SINO;
                              aAlfa = "0000ERROR en XML: " + aTagA;
                              |SI nSwAuto = 0; |MENAV aAlfa; |FINSI;
                              nError = 1;
                         |FINSI;
                         |ACABA;
                    |FINSI;
*/
                    |SI aCar0 = "?"; |Y nYaPaso = 0;
                         |SI aIniPro = "S";
                              nYaPaso = 1;
                              aFinPro = "S";
                              aIniPro = "N";
                         |SINO;
                              |SI aFinPro = "S";
                                   aAlfa = "0000ERROR en XML: " + aTagA;
                                   |SI nSwAuto = 0; |MENAV aAlfa; |FINSI;
                                   nError = 1;
                              |SINO;
                                   aIniPro = "S"; aTagA = "";
                              |FINSI;
                         |FINSI;
                    |SINO;
                         |SI aCar0 = ">";
                              aValor = aTagA; |HAZ QuitaBlancos; aTagA = aValor;
                              aValor = aTagB; |HAZ QuitaBlancos; aTagB = aValor;
                              |SI aFinPro = "S";
                                   |HAZ ProcesaPrologo;
                                   aFinPro = "N";
                              |SINO;
                                   |SI aFinTag = "S";
                                        |HAZ PopTag;
                                        |HAZ ProcesaTagPop;
                                   |SINO;
                                        |SI aFinTagVacio = "S";
                                             |HAZ ProcesaTagVacio;
                                             aFinTagVacio = "N";
                                        |SINO;
                                             |HAZ PushTag;
                                             |HAZ ProcesaTagPush;
                                        |FINSI;
                                   |FINSI;
                              |FINSI;
                              aIniTag = "N"; aFinTag = "N"; aTagA = ""; aTagB = ""; aVal = "";
                         |SINO;
                              aLon = aTagA % A0;
                              |SI aLon < 250;
                                   aTagA = aTagA + aCar0;
                              |SINO;
                                   aLon = aTagB % A0;
                                   |SI aLon < 250;
                                        aTagB = aTagB + aCar0;
                                   |SINO;
                                        aTagC = aTagC + aCar0;
                                   |FINSI;
                              |FINSI;
                         |FINSI;
                    |FINSI;
               |FINSI;
          |SINO;
               aLon = aVal % A0;
               |SI aLon < 250; aVal = aVal + aCar0; |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO ProcesaXml;
     nNivel = 0;
     nError = 0;
     |ABRE #29;
     |ABRE #30;
     |SI aIP = "";
          |XABRE "B", aFic, nHandle;
     |SINO;
          |XABRE "BC", aFic, nHandle;
     |FINSI;
     |SI FSalida ] 0;
          |XLEE nHandle, 250, aTmp;
          |PARA; |SI FSalida > 0; |HACIENDO;
               |PARA; |SI aTmp ! ""; |HACIENDO;
                    aCar0 = aTmp % 101;
                    aCar1 = aTmp % 201;
                    |SI aTmp = aCar0;
                         aTmp = ""; ||con un caracter falla: "aTmp = aTmp % 2"
                         |XLEE nHandle, 250, aTmp1;
                         nSal = FSalida;
                         aCar1 = aTmp1 % 101;
                    |SINO;
                         aTmp = aTmp % 2;
                    |FINSI;
                    |HAZ Caracter;
                    |SI nError ! 0; |SAL; |FINSI;
               |SIGUE;
               aTmp = aTmp1;
               FSalida = nSal;
               |SI nError ! 0; |SAL; |FINSI;
          |SIGUE;
          |XCIERRA nHandle;
     |SINO;
          nError = 1;
          aAlfa = "0000ERROR al abrir" + aFic;
          |SI nSwAuto = 0; |MENAV aAlfa; |FINSI;
     |FINSI;
     |CIERRA #30;
     |CIERRA #29;
     nReg = nReg + 1;
|FPRC;

|PROCESO SacaNombre;
     aNom = "";
     aLon = aFic % A0;
     |PARA j = 1; |SI j [ aLon; |HACIENDO j = j + 1;
          nPos = -(j * 100 + 1);
          aAlfa = aFic % nPos;
          |SI aAlfa = "/"; |O aAlfa = "\"; |SAL; |FINSI;
          aNom = aAlfa + aNom;
     |SIGUE;
|FPRC;

|PROCESO Inicio;
     |ABRE #28; |LEE 000#28p; |CIERRA #28;

     aPath = #28#1; |QBF aPath;
     aPath = aPath + "*.*";

     aIP = "";
     |IP_REMOTO aIP;
     |SI aIP = "";
          |_PDIR aPath;
          |PARA; |SI FSalida = 0; |HACIENDO;
               aFic = aPath;
               |BLIN 23; |PINTA 2301, aFic;
               |HAZ ProcesaXml;
               |SI nError = 0;
                    |HAZ SacaNombre;
                    aDes = #28#2; |QBF aDes;
                    aDes = aDes + aNom;
                    |RENOMBRA_FICHERO aFic, aDes;
               |FINSI;
               |_SDIR aPath;
          |SIGUE;
     |SINO;
          |R_PDIR aPath;
          |PARA; |SI FSalida = 0; |HACIENDO;
               aFic = aPath;
               |BLIN 23; |PINTA 2301, aFic;
               |HAZ ProcesaXml;
               |SI nError = 0;
                    |HAZ SacaNombre;
                    aDes = #28#2; |QBF aDes;
                    aDes = aDes + aNom;
                    |RRENOMBRA_FICHERO aFic, aDes;
               |FINSI;
               |R_SDIR aPath;
          |SIGUE;
     |FINSI;

     aAlfa = "0000Procesados " + nReg + " ficheros.";
     |SI nSwAuto = 0; |MENAV aAlfa;  |FINSI;
|FPRC;

|PROGRAMA;
     |SI PARAMETRO = "menu";
          nSwAuto = 0;
          |CLS;
          |PINPA #0#0;
          |PINDA #0#0;
          |ENDATOS #1#0;
          |SI FSalida = 0; |Y #0#0 = "SI";
               |HAZ Inicio;
          |FINSI;
     |SINO;
          nSwAuto = 1;
          |HAZ Inicio;
     |FINSI;
|FPRO;
