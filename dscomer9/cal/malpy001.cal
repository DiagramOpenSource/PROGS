|FICHEROS;
     malpy001 #0;
     malpw002 #902;          || Previsiones Ventas

     malpw003 #903,MANTE;    || Aux Seleccion Representantes
     malpw004 #904;          || Tmp. Ventas Inf.
     agifa025 #925;          || Representantes
     malpe090 #900;          || Cabs. Albaranes Venta
     malpe091 #901;          || Lins. Albaranes Venta
     agifa019 #919;          || Articulos
     agifa553 #953;          || Lineas de Productos

     :/cmi/def/manm0012 #12; || Carpeta Remotas
|FIN;

|VARIABLES;
     fFecha     = @;
     fFechaD    = @;
     fFechaH    = @;
     aAlfa      = "";
     aAlfa1     = "";
     aAlfa2     = "";
     aNFichero  = "";
     aAbre      = "";
     aHost      = "";
     aOrigen    = "";
     aDestino   = "";
     aPathEnvio = "";

     nMarcar   = 0;
     nNum      = 0;
     aMes      = "";
     nError    = 0;
     nHandle   = 0;

     nAnyoAnt  = 0;
     aRepreAnt = "";
     aLProdAnt = "";

     &nMesInf  = 0;
     &nRealInf = 0;
     &nPrevInf = 0;

     nPara     = 0;
     nCampo1   = 0;
     nCampo2   = 0;
     nSwitch   = 0;

     aBaseCrystal = "";
     aDocRemoto   = "";
     aDocServidor = "";
     aIPRemoto    = "";
     aBase        = "";

  {1}aBaseLocal   = "";
  {1}aContiene    = "";
|FIN;

||--------------------------------------------------------

|PROCESO Tipo66Fmalpy1;  |TIPO 66;
     |ABRE #12;
     |LEE 000#12p;

     |CONSULTA_CLAVE #1#12;
     |SI FSalida = 0;
         #0#6 = #12#0;   |PINTA #0#6;
         #0#7 = #12#1;   |PINTA #0#7;
     |FINSI;
     |CIERRA #12;

     |ERROR;
|FPRC;

|PROCESO Tipo0C6Fmalpy1;  |TIPO 0;
     |ABRE #12;
     #12#0 = #0#6;
     |LEE 000#12=;
     |SI FSalida ! 0;
         |MENAV "0000 No existe el Codigo de la Carpeta Remota.";
         |ERROR;
     |SINO;
         #0#7 = #12#1;   |PINTA #0#7;
     |FINSI;
     |CIERRA #12;
|FPRC;

|PROCESO Tipo0C5Fmalpy1;  |TIPO 0;
     |C_M #0#6, 1, "S";
     |SI #0#5 = "V";
         |C_M #0#6, 1, "N";
     |FINSI;
|FPRC;

|PROCESO malpy001wLin; |TIPO 0;
     |SI FSalida = 10
          |ABRE #953;
          |CONSULTA_CLAVE #1#953;
          |SI FSalida = 0;
               #0Campo = #953#0;
          |FINSI;

          aAlfa = #953#1;
          |SI Campo = 3;
               ||PINTA 1238, aAlfa;
          |FINSI;

          |SI Campo = 4;
               ||PINTA 1338, aAlfa;
          |FINSI;

          |CIERRA #953;
          |PINTA #0Campo;
     |FINSI;
|FPRC;


|PROCESO malpy001w2;
     |SI #0#2 = 0;
          fFecha = ~;
          aAlfa = fFecha;
          aAlfa = aAlfa % -104;
          #0#2 = aAlfa;
     |FINSI;
|FPRC;

|PROCESO malpw003wCarga;
     #903#0 = nNum;
     nNum = nNum + 1;
     #903#1 = #925#0;
     #903#2 = #925#1;
     |SI nMarcar = 0; #903#3 = "*"; |FINSI;
     |SI nMarcar ! 0; #903#3 = " "; |FINSI;
     |GRABA 020#903n;
|FPRC;

|DEFBUCLE malpw003wCarga;
     #925#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, malpw003wCarga;
|FIN;

|PROCESO Baja903;
     #903#0 = 1;
|FPRC;

|PROCESO Alta903;
     #903#0 = 999999;
|FPRC;

|PROCESO parrilla;
     |SI FSalida = 0;
          |SI #0#1 = "S";
               |ATRI P;   |MENSA "    Cargando seleccion ...";    |ATRI 0;
          |FINSI;
          aAlfa = Usuario; |QBF aAlfa;  aAlfa = ("t" + aAlfa) % 108;
          |NOME_DAT #903 aAlfa;

          |SI #0#1 = "S";
               |AVISO;
               |CONFI "0000SMarcar todos los representantes:";
               nMarcar = FSalida;
          |SINO;
               nMarcar = 0;
          |FINSI;

          |ABRE #903;  |CIERRA #903;  |DELINDEX #903;
          |ABRE #925;
          |ABRE #903;
          nNum = 0;
          |BUCLE malpw003wCarga;
          |CIERRA #903;
          |CIERRA #925;

          |SI #0#1 = "S";
               |PUSHV 0501 2380;
               |ENTLINEAL #1#903, 1, 4, 22, 1, Baja903, Alta903;
               |POPV;
          |FINSI;
     |FINSI;
|FPRC;

||--------------------------------------------------------

|PROCESO malpe091;
     ||ARTICULOS
     #919#0 = #901#2;
     |LEE 000#919=;
     |SI FSalida ! 0; |ACABA; |FINSI;
     |SI #919#125 < #0#3; |O #919#125 > #0#4; |ACABA; |FINSI;
     #904#0 = #0#2;
     #904#1 = #903#1;

     ||REPRESENTANTES
     #925#0 = #903#1;
     |LEE 000#925=;
     |SI FSalida = 0;
          #904#2 = #925#1;
     |FINSI;
     #904#3 = #919#125;

     ||LINEAS DE PRODUCTO
     #953#0 = #919#125;
     |LEE 000#953=;
     |SI FSalida = 0;
          #904#4 = #953#1;
     |FINSI;

     ||PONER A 0 LAS VENTAS ANTERIORES
     #904#5 = 0;
     #904#6 = 0;
     #904#7 = 0;
     #904#8 = 0;
     #904#9 = 0;
     #904#10 = 0;
     #904#11 = 0;
     #904#12 = 0;
     #904#13 = 0;
     #904#14 = 0;
     #904#15 = 0;
     #904#16 = 0;


     |LEE 000#904=;
     aMes = #900#1;
     aMes = aMes % 402;
     |SI FSalida = 0;
          |SI aMes = "01"; #904#5 = #904#5 + #901#7; |FINSI;
          |SI aMes = "02"; #904#6 = #904#6 + #901#7; |FINSI;
          |SI aMes = "03"; #904#7 = #904#7 + #901#7; |FINSI;
          |SI aMes = "04"; #904#8 = #904#8 + #901#7; |FINSI;
          |SI aMes = "05"; #904#9 = #904#9 + #901#7; |FINSI;
          |SI aMes = "06"; #904#10 = #904#10 + #901#7; |FINSI;
          |SI aMes = "07"; #904#11 = #904#11 + #901#7; |FINSI;
          |SI aMes = "08"; #904#12 = #904#12 + #901#7; |FINSI;
          |SI aMes = "09"; #904#13 = #904#13 + #901#7; |FINSI;
          |SI aMes = "10"; #904#14 = #904#14 + #901#7; |FINSI;
          |SI aMes = "11"; #904#15 = #904#15 + #901#7; |FINSI;
          |SI aMes = "12"; #904#16 = #904#16 + #901#7; |FINSI;
          |GRABA 020#904a;
     |SINO;
          |SI aMes = "01"; #904#5 = #901#7; |FINSI;
          |SI aMes = "02"; #904#6 = #901#7; |FINSI;
          |SI aMes = "03"; #904#7 = #901#7; |FINSI;
          |SI aMes = "04"; #904#8 = #901#7; |FINSI;
          |SI aMes = "05"; #904#9 = #901#7; |FINSI;
          |SI aMes = "06"; #904#10 = #901#7; |FINSI;
          |SI aMes = "07"; #904#11 = #901#7; |FINSI;
          |SI aMes = "08"; #904#12 = #901#7; |FINSI;
          |SI aMes = "09"; #904#13 = #901#7; |FINSI;
          |SI aMes = "10"; #904#14 = #901#7; |FINSI;
          |SI aMes = "11"; #904#15 = #901#7; |FINSI;
          |SI aMes = "12"; #904#16 = #901#7; |FINSI;
          |GRABA 020#904n;
     |FINSI;
|FPRC;

|DEFBUCLE malpe091;
     #901#1;
     ;
     #900#52,#900#0,1;
     #900#52,#900#0,999;
     ;
     NULL, malpe091;
|FIN;

|PROCESO malpe090;
     |BUCLE malpe091;
|FPRC;

|DEFBUCLE malpe090;
     #900#1;
     1,6;
     "     ",000000,fFechaD,#903#1;
     "zzzzz",999999,fFechaH,#903#1;
     ;
     NULL, malpe090;
|FIN;

|PROCESO malpw903;
     aAlfa = "01.01." + #0#2;
     fFechaD = aAlfa;
     aAlfa = "31.12." + #0#2;
     fFechaH = aAlfa;
     |BUCLE malpe090;
|FPRC;

|DEFBUCLE malpw903;
     #903#1;
     3;
     1,"*";
     999999,"*";
     ;
     NULL, malpw903;
|FIN;

|PROCESO AbreImpresora;
     nError = 0;

     |SI #0#5 = "V";
         Impresora = "CrystalReport";
     |SINO;
         aAlfa1    = #904#1;  |QBF aAlfa1;
         aAlfa1    = (aAlfa1 + "__") % 102;
         aAlfa2    = #904#3;  |QBF aAlfa2;
         aAlfa2    = (aAlfa2 + "__") % 102;
         aAlfa     = "CrystalReport;" + aBaseCrystal;
         aNFichero = (("0000" + #904#0) % -104) + aAlfa1 + aAlfa2 + "COM.pdf";
         aAlfa     = aAlfa + aNFichero;
         Impresora = aAlfa;
     |FINSI;

     |IMPRE -1;
     nError = FSalida;

     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |INFOR "malpy001";
     nError = FSalida;

     |SI FSalida ! 0;  |ACABA;  |FINSI;
|FPRC;

|PROCESO CierraImpresora;
     |PIEINF;
     |FININF;
     |FINIMP;

     |SI #0#5 = "E";
         |ABRE #12;
         #12#0 = #0#6;
         |LEE 000#12=;
         |SI FSalida = 0;
             aAbre = aBaseCrystal + "ftp.txt";
             |XABRE "WBC", aAbre, nHandle;
             |SI FSalida < 0;  |ACABA;  |FINSI;

             aAlfa = #12#2 + &13 + &10;                  |XGRABA nHandle, aAlfa;
             aAlfa = #12#3 + &13 + &10;                  |XGRABA nHandle, aAlfa;
             aAlfa = "lcd " + aBaseCrystal + &13 + &10;  |XGRABA nHandle, aAlfa;
             aAlfa = "cd " + #12#4;                      |QBF aAlfa;
             aAlfa = aAlfa + " " + &13 + &10;            |XGRABA nHandle, aAlfa;
             aAlfa = "bin " + &13 + &10;                 |XGRABA nHandle, aAlfa;
             aAlfa = "put " + aNFichero + &13 + &10;     |XGRABA nHandle, aAlfa;
             aAlfa = "bye " + &13 + &10;                 |XGRABA nHandle, aAlfa;

             |XCIERRA nHandle;

             aHost  = #12#1;  |QBF aHost;
             aAlfa  = "cmd " + &34 + "/c START /WAIT " + aBaseCrystal + "dsftp " + aAbre + " " + aHost + &34;
             |RSYSTEM aAlfa;
         |FINSI;
         |CIERRA #12;

         aAlfa = aBaseCrystal + aNFichero;
         |RFBORRA aAlfa;
     |FINSI;
|FPRC;

|PROCESO malpw004wImp;
     |SI nNum ! 1;
         |SI nAnyoAnt ! #904#0; |O aRepreAnt ! #904#1; |O aLProdAnt ! #904#3;
             |SI #0#5 = "V";
                 |PIEINF;
             |SINO;
                 |HAZ CierraImpresora;
                 |HAZ AbreImpresora;
             |FINSI;
             nRealInf = 0;
             nPrevInf = 0;
         |FINSI;
     |SINO;
         |SI #0#5 = "E";
             |HAZ AbreImpresora;
         |FINSI;
     |FINSI;

     nNum      = 0;
     nAnyoAnt  = #904#0;
     aRepreAnt = #904#1;
     aLProdAnt = #904#3;
     #902#0    = #904#0;
     #902#1    = #904#1;
     #902#2    = #904#3;
     |LEE 000#902=;
     nSwitch = FSalida;
     |PARA nPara = 1; |SI nPara [ 12; |HACIENDO nPara = nPara + 1;
          nMesInf = nPara;
          nCampo1 = nPara + 4;
          nCampo2 = nPara + 2;

          nRealInf = #904nCampo1 + nRealInf;

          |SI nSwitch = 0;
               nPrevInf = #902nCampo2 + nPrevInf;
          |SINO;
               nPrevInf = nPrevInf;
          |FINSI;

          |PRINT;
     |SIGUE;
|FPRC;

|DEFBUCLE malpw004wImp;
     #904#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, malpw004wImp;
|FIN;

|PROCESO Impresion;
     nError = 0;
     |SI #0#5 = "V";
         |HAZ AbreImpresora;
     |FINSI;

     |SI nError = 1;  |ERROR10;  |ACABA;  |FINSI;

     |ABRE #902;
     |BUCLE malpw004wImp;
     |CIERRA #902;

     |HAZ CierraImpresora;
|FPRC;

|PROCESO Tipo3Fmalpy1;  |TIPO 3;
     |ABRE #904;  |CIERRA #904;  |DELINDEX #904;

     |ABRE #904;
     |ABRE #919;
     |ABRE #925;
     |ABRE #953;

     |BUCLE malpw903;

     |LEE 000#904p;
     |SI FSalida = 0;
         nNum = 1;
         |HAZ Impresion;
     |SINO;
         |SI PARAMETRO = "";
             |MENAV "0000 Seleccion vacia.";
         |FINSI;
     |FINSI;

     |CIERRA #904;
     |CIERRA #919;
     |CIERRA #925;
     |CIERRA #953;
|FPRC;

||--------------------------------------------------------

|PROCESO TraeDsFtp;
     aOrigen  = aPathEnvio  + "/dsftp.exe";
     aDestino = aBaseCrystal + "dsftp.exe";

     aContiene1 = aDestino;
     |ESPECIFICA "REMOTOMD5", aContiene;
     aDocRemoto = FSalida;  |QBF aDocRemoto;

     aContiene1 = aOrigen;
     |ESPECIFICA "MD5", aContiene;
     aDocServidor = FSalida;  |QBF aDocServidor;

     |SI aDocRemoto ! aDocServidor;
         aIPRemoto = "";
         |IP_REMOTO aIPRemoto;  |QBT aIPRemoto;

         |SI aIPRemoto ! "";
             |ENVIA_FICHERO aOrigen, aDestino;
         |SINO;
             |COPIA_FICHERO aOrigen, aDestino;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO TraeDscristal;
     aOrigen  = aPathEnvio  + "/dscristal.dll";
     aDestino = aBaseLocal1 + "bin/dscristal.dll";

     aContiene1 = aDestino;
     |ESPECIFICA "REMOTOMD5", aContiene;
     aDocRemoto = FSalida;  |QBF aDocRemoto;

     aContiene1 = aOrigen;
     |ESPECIFICA "MD5", aContiene;
     aDocServidor = FSalida;  |QBF aDocServidor;

     |SI aDocRemoto ! aDocServidor;
         aIPRemoto = "";
         |IP_REMOTO aIPRemoto;  |QBT aIPRemoto;

         |SI aIPRemoto ! "";
             |ENVIA_FICHERO aOrigen, aDestino;
         |SINO;
             |COPIA_FICHERO aOrigen, aDestino;
         |FINSI;
     |FINSI;
|FPRC;

|PROGRAMA;
     aBaseLocal1 = "";
     |ESPECIFICA "RBASS", aBaseLocal;

     aBaseCrystal = aBaseLocal1 + "crystal/";

     |DBASE aBase;  |QBF aBase;
     aPathEnvio = aBase + "001254";  |MKDIR aPathEnvio;

     |HAZ TraeDsFtp;
     |HAZ TraeDscristal;

     |ABRE #0;
     |SI PARAMETRO = "";
         |CLS;
         |LEE 101#0p;
         |SI FSalida ! 0;
             |DDEFECTO #0;
             |GRABA 020#0b;
         |FINSI;

         |ABRE #903;  |CIERRA #903;  |DELINDEX #903;
         |ABRE #925;
         |ABRE #903;
         nNum = 0;
         |BUCLE malpw003wCarga;
         |CIERRA #903;
         |CIERRA #925;

         |PINPA #0#0;
         |PINDA #0#0;

         |ENDATOS #1#0;
         |SI FSalida = 0;
             |GRABA 020#0a;
             |LIBERA #0;
         |FINSI;
     |SINO;
         |LEE 000#0p;
         |SI FSalida ! 0;  |ACABA;  |FINSI;
         |SI #0#6 = "  ";  |ACABA;  |FINSI;

         |ABRE #903;  |CIERRA #903;  |DELINDEX #903;
         |ABRE #925;
         |ABRE #903;
         nNum = 0;
         |BUCLE malpw003wCarga;
         |CIERRA #903;
         |CIERRA #925;

         #0#5 = "E";
         |HAZ Tipo3Fmalpy1;
     |FINSI;
     |CIERRA #0;
|FPRO;
