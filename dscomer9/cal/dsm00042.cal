|FICHEROS;
     dsm00042 #0; || Informe Inventario......
     agifa065 #1; || Historico Movimientos...
     agifa019 #2; || Articulos...............
     dsm00084 #84,MANTE;|| Informes
|FIN;

|VARIABLES;
     nSum  = 0;
     nPcmIni = 0;
     nPcmFin = 0;
     aFecha = "";
     fFecha = @;
     aAlfa = "";
     aAlfa1 = "";
     aInforme = "";
     fDfe = @;
     nDfe = 0;
     nSw  = 0;
     nSwi = 0;
     &aArt = "";
     nPcu = 0;
     nUni = 0;
     &nUin = 0;
     &nKin = 0;
     &nPin = 0;
     &nUfi = 0;
     &nKfi = 0;
     &nPfi = 0;
     &nUco = 0;
     &nKco = 0;
     &nPco = 0;
     &nUve = 0;
     &nKve = 0;
     &nPve = 0;
     &nPmc = 0;
     &nPmv = 0;

     &nUint = 0;
     &nKint = 0;
     &nPint = 0;
     &nUfit = 0;
     &nKfit = 0;
     &nPfit = 0;
     &nUcot = 0;
     &nKcot = 0;
     &nPcot = 0;
     &nUvet = 0;
     &nKvet = 0;
     &nPvet = 0;

     &nUes  = 0;
     &nUss  = 0;

     &aFam    = "";
     &nVisual= 0;  || 2: Total por familia

     nImporte = 0;
     &enBen = 0;
|FIN;

|PROCESO &Beneficio;
     nSum = nPin + nPco;
     nSum = nPfi - nSum;
     |SI nSum < 0; nSum = nSum * -1; |FINSI;
     enBen = nPve - nSum;
|FPRC;

|PROCESO DefFecha; |TIPO 7;
     fFecha = ~;
     aFecha = fFecha;
     aAlfa1 = aFecha % -104;

     aAlfa = "01.01." + aAlfa1;
     #0#0 = aAlfa;
     aAlfa = "31.12." + aAlfa1;
     #0#1 = aAlfa;
     |PINTA #0#0;
     |PINTA #0#1;
|FPRC;

|PROCESO Elige; |TIPO 11;
     |SI FSalida = 13;
          aInforme = #84#0;
          |ERROR;
          |PTEC 807;
     |FINSI;
|FPRC;

|PROCESO InfDefecto;
     |ABRE #84;
     #84#0 = "dsm00042";
     #84#1 = "Inventario Stock";
     |GRABA 000#84n;
     #84#0 = "dsm10042";
     #84#1 = "I.Stock con conversion de Pesos segun articulo";
     |GRABA 000#84n;
     |CIERRA #84;
|FPRC;

|PROCESO QuitaNega;
     |SI #0#9 = "S"; |ACABA; |FINSI;

     |SI nUin < 0; nUin = 0; |FINSI;
     |SI nUco < 0; nUco = 0; |FINSI;
     |SI nUve < 0; nUve = 0; |FINSI;
     |SI nUfi < 0; nUfi = 0; |FINSI;
     |SI nUes < 0; nUes = 0; |FINSI;
     |SI nUss < 0; nUss = 0; |FINSI;
|FPRC;

|PROCESO agifa065_2;
      |DDEFECTO #2;
      #2#0 = #1#0;
      |LEE 000#2=;
      |SI #2#2 < #0#3; |O #2#2 > #0#4; |ACABA; |FINSI;

      nImporte = #1#14;

      |SI nSw  = 0;
          aArt = #1#0;
          aFam = #2#2;
          nSw  = 1;
      |FINSI;
      |SI aArt ! #1#0;
          |SI nUin ! 0; |O nUco ! 0; |O nUve ! 0;   |O nUes ! 0; |O nUss ! 0;
               #2#0 = aArt;
               |LEE 000#2=;
               |SI FSalida ! 0;
                    |DDEFECTO #2;
               |FINSI;
               nUfi = nUin + nUco - nUve + nUes - nUss;
               |HAZ QuitaNega;
               |SI #0#2 = 1;
                    nPin = nUin * #2#9; || Valor a P.M.Coste.
                    nPfi = nUfi * #2#9; ||  ''      ''  ''
                    nPmc = #2#9;
               |FINSI;
               |SI #0#2 = 2;
                    nPin = nUin * nPcu; || Valor a P.Ult.Compra.
                    nPfi = nUfi * nPcu; ||   ''     ''  ''  ''
                    nPmc = nPcu;
               |FINSI;
               |SI #0#2 = 3;
                    nPin = nUin * nPcmIni;
                    nPfi = nUfi * nPcmFin;
                    nPmc = nPcmFin;
               |FINSI;
               nKin = nUin * #2#102;
               nKco = nUco * #2#102;
               nKve = nUve * #2#102;
               nKfi = nUfi * #2#102;
               nPmv = nPve / nUve;
               |PRINT;
               nSwi = 1;

               #2#0 = #1#0;
               |LEE 000#2=;
               || Cambia la familia del articulo
               |SI aFam ! #2#2;

                 |SI aFam ! "";
                    nVisual = 2;
                    |PRINT;
                    nVisual = 0;
                 |FINSI;
                 aFam = #2#2;

               |FINSI;

          |FINSI;  /* |SI nUin ! 0; |O nUco ! 0; |O nUve ! 0; */
          aArt = #1#0;

          nUint = nUint + nUin;
          nKint = nKint + nKin;
          nPint = nPint + nPin;         || totales finales
          nUfit = nUfit + nUfi;
          nKfit = nKfit + nKfi;
          nPfit = nPfit + nPfi;
          nUcot = nUcot + nUco;
          nKcot = nKcot + nKco;
          nPcot = nPcot + nPco;
          nUvet = nUvet + nUve;
          nKvet = nKvet + nKve;
          nPvet = nPvet + nPve;

          nUin = 0; nKin = 0; nPin = 0;
          nUfi = 0; nKfi = 0; nPfi = 0;
          nUco = 0; nKco = 0; nPco = 0;
          nUve = 0; nKve = 0; nPve = 0;
          nPcu = 0; nPmc = 0; nPmv = 0;
          nUes = 0; nUss = 0;
          nPcmIni = 0; nPcmFin = 0;
      |FINSI; /* |SI aArt ! #1#0; */

      |SI #1#1 < #0#0;        || Acumula en el stock inicial los movimientos
          nUin = nUin + #1#6; || anteriores a la fecha dada
          |SI #1#2 = "AC";
               nPcu = #1#14;              || Ult.Precio Compra.
          |FINSI;
          |SI nUin ! 0; nSwi = 1; |FINSI;
          nPcmIni = #1#8;
      |FINSI;

      |SI #1#1 ] #0#0;|Y #1#1 [ #0#1;
          |SI #1#8 ! 0;
               nPcmFin = #1#8;
          |FINSI;
          || Se ha quitado de compras operaciones ES y SA
          |SI #1#2 = "AC"; |O #1#2 = "BC"; |O #1#2 = "EV";
               nUco = nUco + #1#6;             || Unidades.
               nPco = nPco + ( #1#6 * #1#14 ); || Importe..
               |SI #1#2 = "AC";
                    nPcu = #1#14;              || Ult.Precio Compra.
               |FINSI;
               nPco = nPco ! 0;
               |SI nUco ! 0; nSwi = 1; |FINSI;
          |FINSI;
          || Se ha quitado de ventas operacion SS
          |SI #1#2 = "AV"; |O #1#2 = "BV"; |O #1#2 = "FC"; |O #1#2 = "TI";
               nUni = (#1#6 * -1);
               nUve = nUve + nUni;             || Unidades.

               nPve = nPve + ( nUni * #1#14 ); || Importe..
               nPve = nPve ! 0;
               |SI nUve ! 0; nSwi = 1; |FINSI;
          |FINSI;

          || Tambien se recoge en el informe las unidades operaciones ES,SS y SA
          |SI #1#2 = "ES"; |O #1#2 = "SA"; |O #1#2 = "ME"; |O #1#2 = "FB";
               nUes = nUes + #1#6;
               |SI nUes ! 0; nSwi = 1; |FINSI;
          |FINSI;

          |SI #1#2 = "SS"; |O #1#2 = "MS"; |O #1#2 = "SF";
               nUss = nUss + (#1#6 * -1);
               |SI nUss ! 0; nSwi = 1; |FINSI;
          |FINSI;

      |FINSI;

|FPRC;

|PROCESO agifa065_1;
      |DDEFECTO #2;
      #2#0 = #1#0;
      |LEE 000#2=;
      |SI #2#2 < #0#3; |O #2#2 > #0#4;
          |ERROR;
      |FINSI;
|FPRC;

|DEFBUCLE agifa065Orden;
     #1#1;
     5;
     #0#7, "01.01.0000", "  ", 000000,     0, "     ", #0#5;
     #0#8, "31.12.9999", "zz", 999999, 99999, "zzzzz", #0#6;
     ;
     NULL,agifa065_1,NULL,NULL,NULL,agifa065_2;
     #2#127,#1#0,#1#1,#1#2,#1#3,#1#4,#1#11;
|FIN;

|DEFBUCLE agifa065;
     #1#1;
     5;
     #0#7, "01.01.0000", "  ", 000000,     0, "     ", #0#5;
     #0#8, "31.12.9999", "zz", 999999, 99999, "zzzzz", #0#6;
     ;
     NULL, agifa065_2;
|FIN;

|PROCESO Min;
     #84#0 = "        ";
|FPRC;

|PROCESO Max;
     #84#0 = "zzzzzzzz";
|FPRC;

|PROCESO ejec; |TIPO 3;
     |HAZ InfDefecto;
     |PUSHV 0501 2380;
     aInforme = "dsm00042";
     |ENTLINEAL  #1#84, 1, 2, 20, 1, Min, Max;
     |QBF aInforme;
     |POPV;

     nDfe = #0#0;
     nDfe = nDfe - 1;
     fDfe = nDfe;
     |IMPRE 0;
     |SI FSalida = 0;
          |INFOR aInforme;
          |SI FSalida ! 0;
               aAlfa = "0000No existe informe: '" + aInforme + "'";
               |MENAV aAlfa;
          |SINO;
               |ABRE #2;
               |SI #0#10 = "S";
                    |BUCLE agifa065;
               |SINO;
                    |BUCLE agifa065Orden;
               |FINSI;

               |SI nSwi  = 1;
                    |SI nUin ! 0; |O nUco ! 0; |O nUve ! 0;   |O nUes ! 0; |O nUss ! 0;
                         #2#0 = aArt;
                         |LEE 041#2=;
                         |SI FSalida ! 0;
                             |DDEFECTO #2;
                         |FINSI;
                         nUfi = nUin + nUco - nUve + nUes - nUss;
                         |HAZ QuitaNega;
                         |SI #0#2 = 1;
                             nPin = nUin * #2#9; || Valor a P.M.Coste.
                             nPfi = nUfi * #2#9; ||  ''      ''  ''
                            || nPfi = nPfi ! 0;
                            || nPin = nPin ! 0;
                             nPmc = #2#9;
                         |FINSI;
                         |SI #0#2 = 2;
                             nPin = nUin * nPcu; || Valor a P.Ult.Compra.
                             nPfi = nUfi * nPcu; ||   ''     ''  ''  ''
                            || nPfi = nPfi ! 0;
                            || nPin = nPin ! 0;
                             nPmc = nPcu;
                         |FINSI;
                         |SI #0#2 = 3;
                              nPin = nUin * nPcmIni;
                              nPfi = nUfi * nPcmFin;
                              nPmc = nPcmFin;
                         |FINSI;
                         nKin = nUin * #2#102;
                         nKco = nUco * #2#102;
                         nKve = nUve * #2#102;
                         nKfi = nUfi * #2#102;
                         nPmv = nPve / nUve;
                         |PRINT;
                    |FINSI;
                    nVisual = 2;
                    |PRINT;
                    nVisual = 0;

                    nUint = nUint + nUin;
                    nKint = nKint + nKin;
                    nPint = nPint + nPin;         || totales finales
                    nUfit = nUfit + nUfi;
                    nKfit = nKfit + nKfi;
                    nPfit = nPfit + nPfi;
                    nUcot = nUcot + nUco;
                    nKcot = nKcot + nKco;
                    nPcot = nPcot + nPco;
                    nUvet = nUvet + nUve;
                    nKvet = nKvet + nKve;
                    nPvet = nPvet + nPve;

                    |PIEINF;
               |FINSI;
               |CIERRA #2;
               |FININF;
          |FINSI;
          |FINIMP;
     |FINSI;
|FPRC;
