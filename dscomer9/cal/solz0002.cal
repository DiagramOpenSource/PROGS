|FICHEROS;
     solz0002 #0;
     solw0001 #1,MANTE; ||Tmp
     solm0002 #2;
     solm0003 #3;
     solm0004 #4;
     solm0005 #5;
     solm0013 #13;
     solm0017 #17;
     agifa019 #19;
     solw0002 #20; || Tmp
|FIN;

|VARIABLES;
     x = 0;
     nValAntes = 0;
     nEnergia = 0;
     nNume = 0;
     &Tempo = "";
     nPrime = 0;
     nInfor = 0;
     nImpre = 0;
     nLinea = 0;
     nSal = 0;
     nXls = 0;
     aAlf1 = "";
     aAlf2 = "";
     aAlf3 = "";
     aAlf4 = "";
     aAlf5 = "";
     aAlf6 = "";
     aAlf7 = "";
     aTmp1 = "";
     aTmp20 = "";
     aRuta = "";
     aAlfa = "";
     fFecha = @;
     nClave = 0;
     nTecla = 0;
     nHay = 0;
     i = 0;
     aDato     = "";
     nHandle   = 0;
     nCampo    = 0;
     aLon      = "";
     nPos      = 0;
     aLetra    = "";
     aReg      = "";
     aTab      = "";
 {99}Valor     = "";
|FIN;

|PROCESO ActualizaImp;
     |SELECT #2#1;
     #1#2 = #20#2;
     #1#0 = #20#0;
     #1#1 = #20#1;
     |LEE 000#1=;
     |SI FSalida = 0;
          #1#7 = #20#3;
          #1#8 = #20#4;

          #5#0 = #1#0;
          #5#1 = #1#1;
          #5#2 = #1#2;
          #5#3 = #1#3;
          #5#4 = #1#4;
          |LEE 121#5=;
          |SI FSalida = 0;
               |ABRE #17;
               nValAntes = 0;
               |HAZ Actua5;
               |GRABA 020#17a;
               |GRABA 020#5a;
               |GRABA 020#1a;
               |LIBERA #5;
               |CIERRA #17;
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE ActualizaImp;
     #20#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, ActualizaImp;
|FIN;

|PROCESO Imprime2;
     |SI nPrime = 0;
          nPrime = 1;
          |IMPRE 0;
          nImpre = FSalida;
          |SI FSalida = 0;
               |INFOR "solz00b2";
               nInfor = FSalida;
               |SI FSalida ! 0;
                    |MENAV "0000Error en informe 'solz00b2'";
               |FINSI;
          |FINSI;
     |FINSI;
     |SI nImpre = 0; |Y nInfor = 0;
          |PRINT;
     |FINSI;
|FPRC;

|PROCESO Imprime1;
     |SI nPrime = 0;
          nPrime = 1;
          |IMPRE 0;
          nImpre = FSalida;
          |SI FSalida = 0;
               |INFOR "solz00a2";
               nInfor = FSalida;
               |SI FSalida ! 0;
                    |MENAV "0000Error en informe 'solz00a2'";
               |FINSI;
          |FINSI;
     |FINSI;
     |SI nImpre = 0; |Y nInfor = 0;
          |PRINT;
     |FINSI;
|FPRC;

|PROCESO Chequea2;
     |SELECT #2#1;
     #1#2 = #20#2;
     #1#0 = #20#0;
     #1#1 = #20#1;
     |LEE 020#1=;

     #5#0 = #1#0;
     #5#1 = #1#1;
     #5#2 = #1#2;
     #5#3 = #1#3;
     #5#4 = #1#4;
     |LEE 020#5=;
     |SI #5#7 ! 0;
          |HAZ Imprime2;
     |FINSI;
|FPRC;

|DEFBUCLE Chequea2;
     #20#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, Chequea2;
|FIN;

|PROCESO Chequea1;
     |SELECT #2#1;
     #1#2 = #20#2;
     #1#0 = #20#0;
     #1#1 = #20#1;
     |LEE 000#1=;
     |SI FSalida ! 0;
          |HAZ Imprime1;
     |FINSI;
|FPRC;

|DEFBUCLE Chequea1;
     #20#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, Chequea1;
|FIN;

|PROCESO Dato;
     nCampo = 1;
     IValor = Valor1<-;
     aLon = aDato % A0;
     aReg = "";
     |PARA i = 1; |SI i [ aLon; |HACIENDO i = i + 1;
          nPos = i * 100 + 1;
          aLetra = aDato % nPos;
          |SI aLetra = aTab;
               Valor = aReg;
               aReg = "";
               nCampo = nCampo + 1;
               IValor = IValor + 1;
          |SINO;
               aReg = aReg + aLetra;
          |FINSI;
          |SI nCampo > 90; |SAL; |FINSI;     || Por si.... :)
     |SIGUE;
     Valor = aReg;
|FPRC;

|PROCESO Graba;
     #20#0 = Valor1; || huerto
     #20#1 = Valor2; || contrato
     #20#2 = Valor3; || contador
     #20#6 = Valor4; || mes
     #20#5 = Valor5; || a¤o
     #20#4 = Valor6; || fecha
     #20#3 = Valor7; || lectura
     #20#7 = nXls;

     |SI #20#6 = #0#0; |Y #20#5 = #0#1;
          |GRABA 000#20n;
     |FINSI;
|FPRC;

|PROCESO ImportaTxt;
     nLinea = 0;
     aTab = &9;
     |XABRE "C", aRuta, nHandle;
     |SI FSalida ] 0
          |XLEE nHandle, 250, aDato;
          |PARA; |SI FSalida > 0; |HACIENDO;
               aAlfa = "Importando:" + nLinea;
               |PINTA 2307, aAlfa;
               nLinea = nLinea + 1;
               nXls = nLinea;

               |HAZ Dato;
               |HAZ Graba;
               |XLEE nHandle, 250, aDato;
          |SIGUE;
          |XCIERRA nHandle;
     |SINO;
          |MENAV "0000Error al abrir el fichero...";
     |FINSI;
|FPRC;

|PROCESO ImportaXLS;
     aTab = &9;
     |XABRE "CE", aRuta, nHandle;
     |SI FSalida ] 0
          |CARGADLL "agixl97.dll";
          |SI FSalida < 0;
               |MENAV "0000No se puede usar agixl97.dll";
          |SINO;
               |ALFACALLDLL "agixl97.dll", "carga_book", aRuta;
               |ALFACALLDLL "agixl97.dll", "selecciona_hoja", "Hoja1";
               |SI FSalida = 0;
                    |PARA nXls = 1; |SI nXls < 65536; |HACIENDO nXls = nXls + 1;
                         aAlfa = "Importando:" + nXls;
                         |PINTA 2307, aAlfa;

                         aAlf1 = "A" + nXls; |ALFACALLDLL "agixl97.dll", "peek_dato", aAlf1;
                         |SI FSalida ! 0; |SAL; |FINSI;
                         |QBF aAlf1;
                         |SI aAlf1 = ""; |SAL; |FINSI;

                         aAlf2 = "B" + nXls; |ALFACALLDLL "agixl97.dll", "peek_dato", aAlf2;
                         |SI FSalida ! 0; |SAL; |FINSI;

                         aAlf3 = "C" + nXls; |ALFACALLDLL "agixl97.dll", "peek_dato", aAlf3;
                         |SI FSalida ! 0; |SAL; |FINSI;

                         aAlf4 = "D" + nXls; |ALFACALLDLL "agixl97.dll", "peek_dato", aAlf4;
                         |SI FSalida ! 0; |SAL; |FINSI;

                         aAlf5 = "E" + nXls; |ALFACALLDLL "agixl97.dll", "peek_dato", aAlf5;
                         |SI FSalida ! 0; |SAL; |FINSI;

                         aAlf6 = "F" + nXls; |ALFACALLDLL "agixl97.dll", "peek_dato", aAlf6;
                         |SI FSalida ! 0; |SAL; |FINSI;

                         aAlf7 = "G" + nXls; |ALFACALLDLL "agixl97.dll", "peek_dato", aAlf7;
                         |SI FSalida ! 0; |SAL; |FINSI;

                         Valor1 = aAlf1;
                         Valor2 = aAlf2;
                         Valor3 = aAlf3;
                         Valor4 = aAlf4;
                         Valor5 = aAlf5;
                         Valor6 = aAlf6;
                         Valor7 = aAlf7;

                         |HAZ Graba;
                         |SI nSal = 1; |SAL; |FINSI;
                    |SIGUE;
                    |ALFACALLDLL "agixl97.dll", "fin_book", "salva";
               |FINSI;
          |FINSI;
          |DESCARGADLL "agixl97.dll";
     |SINO;
          |MENAV "0000Error al abrir el fichero...";
     |FINSI;
|FPRC;

|PROCESO ImportaFic;
     |DELINDEX #20;
     |ABRE #20;
     aRuta = "F*.*";
     |BROWSE aRuta;
     aRuta = aRuta % 2;
     |SI aRuta ! "F";
          aAlfa = aRuta % -103;
          aAlfa = aAlfa < "";
          |SI aAlfa = "txt";
               |HAZ ImportaTxt;
          |SINO;
               |SI aAlfa = "xls";
                    |HAZ ImportaXLS;
               |SINO;
                    |MENAV "0000Fichero con extension incorrecta, las posibles son .xls y .txt";
               |FINSI;
          |FINSI;
     |FINSI;
     |CIERRA #20;
|FPRC;

|PROCESO Tipo7; |TIPO 7;
     |SI #0#0 = 0;
          fFecha = ~;
          aAlfa = fFecha % 402;
          #0#0 = aAlfa; |PINTA #0#0;
          aAlfa = fFecha % -104;
          #0#1 = aAlfa; |PINTA #0#1;
     |FINSI;
|FPRC;

|PROCESO Actua5;
     |ABRE #3;
     |SELECT #2#3;
     #3#0 = #1#0;
     #3#1 = #1#1;
     #3#3 = #1#2;
     |LEE 020#3=;
     |CIERRA #3;

     |ABRE #2;
     #2#0 = #3#0;
     #2#1 = #3#1;
     |LEE 020#2=;
     |CIERRA #2;

     |ABRE #4;
     #4#2 = #1#0;   ||huerto
     #4#0 = #1#2; ||contador
     |LEE 000#4=;
     |CIERRA #4;

     |SI #1#7 = 0;                           || Actual
          #1#9 = 0;                          || Energia
     |SINO;
          |SI #4#4 = 0;
               #1#9 = #1#7 - #1#6;           || Energia = Actual - Anterior
          |SINO;
               nNume = (#1#7 - #1#6) * #4#4;
               #1#9 = nNume;
          |FINSI;
          #1#9 = #1#9 < #2#6;
     |FINSI;

     |DDEFECTO #17;
     #17#0 = #5#0;
     #17#1 = #5#1;
     #17#2 = #5#2;
     #17#3 = #5#3;
     |LEE 121#17=;

     |ABRE #19;
     |DDEFECTO #19;
     #19#0 = #3#10; || Art Energia
     |LEE 000#19=;
     nNume = #1#9 * #19#18;
     #1#10 = nNume;      || Imp. energia

     nNume = #1#10 * #3#7 / 100;
     #1#11 = nNume;      || Imp. manteni

     nEnergia = #17#6 + #1#9;
     |SI nEnergia > #17#5;
          nNume = #17#5 - #5#24;
          |SI nNume < 0;
               #5#25 = 0;
               #5#26 = #1#9;
          |SINO;
               #5#25 = nNume;
               #5#26 = nEnergia - #17#5;
          |FINSI;
     |SINO;
          #5#25 = #1#9;
     |FINSI;

     nNume = #5#25 * #19#18; #5#27 = nNume;
     nNume = #5#26 * #19#19; #5#28 = nNume;
     |SI #13#3 ! 3;
          #5#29 = #5#27 * #3#7 / 100;
          #5#30 = #5#28 * #3#7 / 100;
          #5#37 = 0;
     |SINO;
          ||TODO CCC
          ||Calculo nuevo.  3

          #5#29 = #5#25 * #5#35 * #3#7 / 100;
          #5#30 = #1#9 * #5#36 * #3#7 / 100;
          #5#37 = ((#3#22 * #5#34) / 12) * (#3#7 / 100);
     |FINSI;

     nNume = #1#9 * #19#18;
     #5#31 = nNume * #3#7 / 100;

     #5#10 = #5#27 + #5#28;
     |SI #13#3 = 1;
          #5#11 = #5#29 + #5#30;
     |SINO;
          |SI #13#3 = 2;
               ||#5#11 = (#5#10 < #2#6) * #3#7 / 100;
               #5#11 = #5#10 * #3#7 / 100;
          |SINO;    ||Tipo de Calculo 3.
               #5#11 = #5#29 + #5#30 + #5#37;
          |FINSI;
     |FINSI;

     #17#6 = #17#6 + #1#9 - nValAntes;


     |DDEFECTO #19;
     #19#0 = #3#16; || Art Coste energia
     |LEE 000#19=;
     nNume = #1#9 * #19#18;
     #5#22 = nNume;      || Coste energia


     |DDEFECTO #19;
     #19#0 = #3#15; || Art Coste mantenimiento
     |LEE 000#19=;
     aAlfa = #19#0; |QBF aAlfa;
     |SI aAlfa = "";
          nNume = 0;
     |SINO;
          nNume = #1#9;
     |FINSI;
     #5#23 = nNume;    || Coste manteni

     |CIERRA #19;

     #5#7 = #1#7;
     #5#8 = #1#8;
     #5#9 = #1#9;
|FPRC;

|PROCESO Tecla0; |TIPO 0;
     |SI #1Campo = Contenido; |Y FSalida = 0; |Y Campo = nCampo;
          #5#0 = #1#0;
          #5#1 = #1#1;
          #5#2 = #1#2;
          #5#3 = #1#3;
          #5#4 = #1#4;
          |LEE 101#5=;
          |SI FSalida = 0;
               |SI #5#13 ! 0; |O #5#16 ! 0;
                    |SI #5#13 ! 0;
                         |MENAV "0000Factura de mantenimiento realizada...";
                    |FINSI;
                    |SI #5#16 ! 0;
                         |MENAV "0000Factura de autofactura realizada...";
                    |FINSI;
               |SINO;
                    |ENCAMPO #7#1;
                    |SI FSalida = 0;
                         |SI #1#8 = "01.01.0000"; #1#8 = ~; |FINSI;
                         nValAntes = #1#9;
                         |ENCAMPO #8#1;
                         |SI FSalida = 0;
                              |ABRE #17;
                              |HAZ Actua5;
                              |SI #1#9 < 0;
                                   |MENAV "0000La lectura actual no puede ser inferior a la anterior";
                                   |LEE 000#1=;
                                   |PINDA #0#1;
                                   |ERROR;
                              |SINO;
                                   |GRABA 020#17a;
                                   |GRABA 020#5a;
                                   |GRABA 020#1a;
                                   |PINTA #1#9;
                              |FINSI;
                              |CIERRA #17;
                         |FINSI;
                    |FINSI;
               |FINSI;
          |FINSI;
          |LIBERA #5;
     |FINSI;
|FPRC;

|PROCESO Tecla11; |TIPO 11;
     |SI nTecla ! 0; |ACABA; |FINSI;

     |SI FSalida = 10; |O FSalida = 11; ||F2/F3
          nTecla = FSalida;
          |SI FSalida = 10;
               nClave = nClave - 1;
               |SI nClave < 1; nClave = 3; |FINSI;
          |SINO;
               nClave = nClave + 1;
               |SI nClave > 3; nClave = 1; |FINSI;
          |FINSI;
          |PTEC 807;
     |FINSI;
     |SI FSalida = 12;   || F4
          nTecla = FSalida;
          |PTEC 807;
     |FINSI;
|FPRC;

|PROCESO CargaTmp;
     |PARA i = 0; |SI i [ 17; |HACIENDO i = i + 1;
          #1i = #5i;
     |SIGUE;
     |GRABA 020#1n;
     nHay = nHay + 1;
|FPRC;

|DEFBUCLE CargaTmp;
     #5#2;
     0;
     #0#1, #0#0, #0#2;   || A¤o, Mes, Huerto
     #0#1, #0#0, #0#3;
     ;
     NULL, CargaTmp;
|FIN;

|DEFBUCLE CargaTmp1;     || Este esta para probar, ya que el
     #5#1;               || solm0005 estaba corrupto
     ;
     #0#2, 0000, 0000, #0#1, #0#0;
     #0#3, 9999, 9999, #0#1, #0#0;
     ;
     NULL, CargaTmp;
|FIN;

|PROCESO Min;
     #1#0 = 0000;
     #1#1 = 0000;
     #1#2 = 0000;
     #1#5 = "      ";
|FPRC;

|PROCESO Max;
     #1#0 = 9999;
     #1#1 = 9999;
     #1#2 = 9999;
     #1#5 = "zzzzzz";
|FPRC;

|PROCESO Tipo3; |TIPO 3;
     |ABRE #13; |LEE 000#13p; |CIERRA #13;

     |HAZ CreaTempo; aTmp1 = Tempo; |NOME_DAT #1#Tempo#; |DELINDEX #1;
     |HAZ CreaTempo; aTmp20 = Tempo; |NOME_DAT #20#Tempo#; |DELINDEX #20;

     |ABRE #1;
     |BUCLE CargaTmp;
     |CIERRA #1;
     |SI nHay = 0;
          |MENAV "0000No hay registros...";
     |SINO;
          |ABRE #5;
          nClave = 1;
          |PARA nTecla = 1; |SI nTecla ! 0; |HACIENDO;
               nTecla = 0;
               |PINPA #0#1;
               |C_M #1#0, 1, "N";
               |C_M #1#1, 1, "N";
               |C_M #1#2, 1, "N";
               |C_M #1#5, 1, "N";
               |SI nClave = 1;
                    |ATRI R;
                    |PINTA 722, "Contador";
                    |PINTA 553, "Clave: Contador        ";
                    |ATRI 0;
                    |C_M #1#2, 1, "S";
                    nCampo = 2;
                    |ENTLINEAL #2#1, 1, 4, 22, 0, Min, Max;
               |SINO;
                    |SI nClave = 2;
                         |ATRI R;
                         |PINTA 706, "Huerto"; |PINTA 722, "Contador";
                         |PINTA 553, "Clave: Huerto/Contador ";
                         |ATRI 0;
                         |C_M #1#0, 1, "S";
                         nCampo = 0;
                         |ENTLINEAL #3#1, 1, 4, 22, 0, Min, Max;
                    |SINO;
                         |ATRI R;
                         |PINTA 731, "Cliente"; |PINTA 722, "Contador";
                         |PINTA 553, "Clave: Cliente/Contador";
                         |ATRI 0;
                         |C_M #1#5, 1, "S";
                         nCampo = 5;
                         |ENTLINEAL #4#1, 1, 4, 22, 0, Min, Max;
                    |FINSI;
               |FINSI;
               |SI nTecla = 12; || F4
                    |HAZ ImportaFic;

                    nPrime = 0;
                    nInfor = -1;
                    nImpre = -1;

                    |ABRE #1;
                    |ABRE #5;
                    |BUCLE Chequea1;
                    |SI nPrime = 0;
                         |BUCLE Chequea2;
                         |SI nPrime = 0;
                              |BUCLE ActualizaImp;
                         |FINSI;
                    |FINSI;
                    |CIERRA #5;
                    |CIERRA #1;

                    |SI nPrime ! 0;
                         |SI nInfor = 0; |PIEINF; |FININF; |FINSI;
                         |SI nImpre = 0; |FINIMP; |FINSI;
                    |FINSI;
               |FINSI;
          |SIGUE;
          |CIERRA #5;
     |FINSI;

     Tempo = aTmp1; |HAZ BoraTempo; |DELINDEX #1;
     Tempo = aTmp20; |HAZ BoraTempo; |DELINDEX #20;
|FPRC;
