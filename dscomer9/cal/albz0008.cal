|FICHEROS;
     albz0008 #0;
     albm0001 #1;
     albm0002 #2;
     albm0003 #3;
     albm0006 #6;
     albm0044 #44;
     albm0052 #52;
     albm0053 #53;
     albm0054 #54;

     agifa007 #7;
     agifa017 #17;
     agifa019 #19;
     agif0041 #41;
     agifa067 #67;
     agifa081 #81;
     agifa112 #112;
     agifa142 #142;
     dsm00239 #239;
     albm0013 #13;
     albw0026 #726;
     albw0027 #727;
     dsm00115 #115;
     agifa321;
     agifa322;
     agifa323;
     agifa324;

     albz8@   #1008;          ||CargaDef del albz0008.
|FIN;

|INCLUYE i_varart;

|VARIABLES;
     nHandle = 0;
     nSal = 0;
     fFechaAntes = @;

     nVeces = 0;
     &eaImpresora = "";
     nVentanaI = 0;
     nBotonI1 = 0;
     nBotonI2 = 0;
     nBotonI3 = 0;

     nSwModifica = 0;         ||Ticket 3036/337.
                              ||Al dar a Modificar, en el tipo 7, aumentaba en 1 el numero de pedido.

     aMenu = "";

     &enTipoCal = 0;
     &efDFecha = @;
     &efHFecha = @;
     nUni1 = 0;
     nUni2 = 0;
     nDias = 0;
     nNume = 0;
     fFecha = @;

     &eaDefImpre = "";
     nSwSerie = 0;
     &ser_ped       = "";
     &num_ped       = 0;
     aParametro = "";
     aProve    = "";
     aArticulo = "";
     aAlmacen  = "";
     aUnidades = "";
     aFechaEnt = "";
     aFechaPre = "";
     aHora     = "";
     aAlfa     = "";
     aBase     = "";
     aTecla    = "";

     nBoton1   = 0;
     nBoton2   = 0;
     nBoton3   = 0;
     nBoton4   = 0;
     nBoton5   = 0;
     nBoton6   = 0;
     nBoton7   = 0;
     nBoton8   = 0;
     nBoton9   = 0;
     nPtec     = 0;

     cambiomb     = 0;
     cambio       = 0;
     div_act      = 0;
     nfecha       = 0;
     nCodEmp      = 0;
     nImpDiv      = 0;
     nPend        = 0;
     nImpo        = 0;
     mes          = 0;
     nPPort1      = 0;
     nPPort2      = 0;
     nNume7       = 0;
     nNume11      = 0;
     importe      = 0;
     importeD     = 0;
     nPendiente   = 0;
     nPendiente2  = 0;
     nVaria1      = 0;
     nVaria2      = 0;
     nMes         = 0;
     nError       = 0;

     aDCerrado    = "";
     aHCerrado    = "";
     monba        = "";
     montra       = "";
     aDatos       = "";
     aDirEmp      = "";
     aMes         = "";

     fDFecha      = @;
     fHFecha      = @;
     fechasis     = @;
     fmes1        = "  ";

     &efFecVto        = @;

     &decim_base      = 0;
     &precio_base     = 0;

     &decim_divisa    = 0;  || Decimales de la divisa
     &precio_divisa   = 0; || Decimales del precio en divisa
     &prov_divisa     = "";
     &enAlbaran       = 0;

     &nDeci_Base      = 2;   || Decimales en Totales de la moneda base
     &nDeci_PreBase   = 2;   || Decimales en el Precio en moneda base
     &nDeci_BaseMxP   = 2;   || Decimales en Totales de la moneda base (Maxima precision)
     &nDeci_BaseMx    = 2;   || Decimales en Totales de la moneda base (Maxima precision)
     &nDeci_PreBasMxP = 2;   || Decimales en el Precio en moneda base (Maxima precision)
     &nDeci_PreBaseMx = 2;   || Decimales en el Precio en moneda base (Maxima precision)
     &nDeci_Div       = 2;   || Decimales en Totales de la divisa
     &nDeci_DivP      = 2;   || Decimales en Totales de la divisa
     &nDeci_PreDivP   = 2;  || Decimales en el Precio en divisa
     &nDeci_PreDiv    = 2;  || Decimales en el Precio en divisa
     &nDeci_TotVisP   = 2;  || Decimales en Totales visibles (el de la moneda de trabajo)
     &nDeci_TotNoVisP = 2;  || Decimales en Totales no visibles (el de la otra moneda)
     &nDeci_PreVisP   = 2;  || Decimales en el Precio visual. (lineas)
     &nDeci_ImpVisP   = 2;  || Decimales en el Importe visual. (lineas)
     &nDeci_DivF      = 2;   || Decimales en Totales de la divisa
     &nDeci_PreDivF   = 2;   || Decimales en el Precio en divisa
     &nDeci_TotVisF   = 2;   || Decimales en Totales visibles (el de la moneda de trabajo)
     &nDeci_TotNoVisF = 2;   || Decimales en Totales no visibles (el de la otra moneda)
     &nDeci_PreVisF   = 2;   || Decimales en el Precio visual. (lineas)
     &nDeci_ImpVisF   = 2;   || Decimales en el Importe visual. (lineas)
     &EURODB          = 2;
     &eaHoraPed = "";
     {-1}get = 0;
     get1 = 0;
     get2 = 0;
     aMensaje = "";
     aFechaOk = "";
     fFechaOk = @;
|FIN;

|PROCESO Salirz8;
     |PTEC 806;
|FPRC;

|PROCESO Tipo7z8;  |TIPO 7;

     |SI aParametro = "MODIFICA";  |ACABA;  |FINSI;
     |SI nSwModifica = 1; |ACABA; |FINSI;

     |ABRE #81;
     #81#25 = #0#0;
     #81#0  = 999999;
     |LEE 040#81];
     |SI FSalida = 0;
         |LEE 040#81a;
     |SINO;
         |LEE 040#81u;
     |FINSI;

     #0#1 = 1;
     |SI FSalida = 0;  |Y  #81#25 = #0#0;
         #0#1 = #81#0 + 1;
     |FINSI;

     |CIERRA #81;

     |PINTA #0#1;
|FPRC;

|PROCESO Tipo0z8;  |TIPO 0;
     |SI FSalida = 13;
          |HAZ SacaAnterior;
          |ERROR;
          |ACABA;
     |FINSI;

     |SI aParametro = "MODIFICA";  |ACABA;  |FINSI;
     |SI nSwModifica = 1; |ACABA; |FINSI;

     |ABRE #81;
     #81#25 = #0#0;
     #81#0  = #0#1;
     |LEE 040#81=;
     |SI FSalida = 0;
         |MENAV "0000 El Pedido ya existe.";
         |ERROR;
     |FINSI;
     |CIERRA #81;
|FPRC;

|PROCESO MiraCalidadProv;  |TIPO 0;
     |SI FSalida = 13;
          |HAZ SacaAnterior;
          |ERROR;
          |ACABA;
     |FINSI;

     |ABRE #52;
     |LEE 000#52p;
     |SI FSalida ! 0;  |DDEFECTO #52;  |FINSI;
     |CIERRA #52;

     |ABRE #53;
     #53#0 = #0#5;
     |LEE 000#53=;
     |SI FSalida ! 0;  |DDEFECTO #53;  |FINSI;
     |CIERRA #53;

     |SI #53#10 [ #52#55;
         aAlfa = "0000NEl proveedor tiene una valoracion de " + #53#10 + " puntos, " + #53#18;
         |QBF aAlfa;
         aAlfa = aAlfa + ". Desea continuar ";

         |CONFI aAlfa;
         |SI FSalida ! 0;  |ERROR;  |FINSI;
     |FINSI;
|FPRC;

|PROCESO MiraCalidadArt;  |TIPO 0;
     |SI FSalida = 13;
          |HAZ SacaAnterior;
          |ERROR;
          |ACABA;
     |FINSI;

     |ABRE #52;
     |LEE 000#52p;
     |SI FSalida ! 0;  |DDEFECTO #52;  |FINSI;
     |CIERRA #52;

     |ABRE #54;
     #54#0 = #0#5;
     #54#1 = #0#11;
     |LEE 000#54=;
     |SI FSalida ! 0;  |DDEFECTO #54;  |FINSI;
     |CIERRA #54;

     |SI #54#5 = 0;  |Y #54#6 = 0;  |Y #54#7 = 0;  |Y #54#8 = 0;
         aAlfa = "0000 El proveedor-articulo es NUEVO";
         |MENAV aAlfa;
     |SINO;
         |SI #54#12 [ #52#55;
             aAlfa = "0000NEl proveedor-articulo tiene una valoracion de " + #54#12 + " puntos, " + #54#20;
             |QBF aAlfa;
             aAlfa = aAlfa + ". Desea continuar ";

             |CONFI aAlfa;
             |SI FSalida ! 0;  |ERROR;  |FINSI;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO CogeArticulo;  |TIPO 7;
     |ABRE #19;
     #19#0 = #0#11;
     |LEE 040#19=;
     |SI FSalida ! 0;  |DDEFECTO #19;  |FINSI;
     |CIERRA #19;

     |ABRE #239;
     #239#0 = #19#5;
     |LEE 040#239=;
     |SI FSalida ! 0;  |DDEFECTO #239;  |FINSI;
     |CIERRA #239;

     |ABRE #44;
     #44#0 = #0#5;
     #44#1 = #0#11;
     |LEE 101#44=;
     |SI FSalida ! 0;
         |DDEFECTO #44;
         #44#0 = #0#5;
         #44#1 = #0#11;
         ||No tiene que coger estos valores. Segun 3026/160
         ||#44#2 = #19#134;
         ||#44#3 = #19#195;
         |GRABA 020#44n;
         |LIBERA #44;
     |FINSI;
     |CIERRA #44;

     |SI aParametro ! "MODIFICA";
          #0#15 = #44#2;
          #0#16 = #44#3;
     |SINO;
          #0#15 = #3#15;
          #0#16 = #3#16;
     |FINSI;
     |PINTA #0#15;
     |PINTA #0#16;

     aAlfa = #19#195;  |QBF aAlfa;
     |SI aAlfa = "";  aAlfa = #19#1;  |FINSI;
     #0#12 = aAlfa;
     |PINTA #0#12;

     aAlfa = #239#1;  |QBF aAlfa;
     |SI aAlfa = "";  aAlfa = "Unidades";  |FINSI;
     #0#29 = aAlfa;
     |PINTA #0#29;
|FPRC;

|PROCESO GrabaArtProv;
     |ABRE #44;
     #44#0 = #0#5;
     #44#1 = #0#11;
     |LEE 101#44=;
     |SI FSalida = 0;
         |SI #0#15 ! #44#2;
             |CONFI "0000SEl codigo del Articulo proveedor a cambiado. Desea Guardarlo.";
             |SI FSalida = 0;
                 #44#2 = #0#15;
                 #44#3 = #0#16;
                 |GRABA 020#44a;
             |FINSI;
         |FINSI;
         |SI #0#16 ! #44#3;
             |CONFI "0000SLa descripcion del Articulo proveedor a cambiado. Desea Guardarlo.";
             |SI FSalida = 0;
                 #44#2 = #0#15;
                 #44#3 = #0#16;
                 |GRABA 020#44a;
             |FINSI;
         |FINSI;
     |FINSI;
     |LIBERA #44;
     |CIERRA #44;
|FPRC;

|PROCESO CalcuPrecio;
     enPrecio = 0;
     |DDEFECTO #67;
     #67#2  = #0#11;            || ARTICULO.
     #67#3  = #0#13;            || ALMACEN.
     #67#4  = #0#12;            || DESCRIPCION.
     #67#5  = #0#17;            || UNIDADES.
     #67#41 = 0;                || uni2
     #81#1  = #0#2;             || FECHA PEDIDO
     #81#4  = #0#5;             || PROVEEDOR.

     eaDocu   = "agifa067";
     |HAZ LeePreDtoCom;

     #0#18  = enPrecio;  |PINTA #0#18;
     #0#26  = enDto1;    |PINTA #0#26;
     #0#27  = enDto2;    |PINTA #0#27;
|FPRC;

|PROCESO TotalPedido;
     #0#19 = (((#0#17 * #0#18) < #0#26) < #0#27);
     |PINTA #0#19;
|FPRC;

|PROCESO Tipo7C20_z0008; |TIPO 7;
     |SI nSwModifica = 1;
          fFechaAntes = #0#20;
     |FINSI;
|FPRC;

|PROCESO FechaV;  |TIPO 0;
     |SI nSwModifica = 1;
          |SI fFechaAntes = #0#20;
               |ACABA;
          |FINSI;
     |FINSI;

     |SI aParametro = "MODIFICA"; |Y #0#22 = "S";
          ||NO SE MODIFICA AUTOMATICAMENTE LA FECHA.
          ||Frases me confirma si campo Valor Acordado (22)
          ||Se usa para evitar este recalculo de la fecha.
          ||Tiquet. 3026/912
     |SINO;
          #81#4  = #0#5;
          #81#2  = #0#20;
          #81#9  = #112#17;
          |HAZ AlbePedFec2;
          #0#21 = efFecVto;
          |PINTA #0#21;
     |FINSI;
|FPRC;

|PROCESO PideDias;
     |PUSHV 0501 2395;
     nNume = 1;
     E_sup = 9999;
     E_inf = 1;
     |PINTA 2188, aAlfa;
     nDias = 2287 ? 1;
     |POPV;
|FPRC;

|PROCESO Boton1;
     |ESTADO_CONTROL nBoton1, "DISABLE";
     |ESTADO_CONTROL nBoton2, "DISABLE";

     |REFERENCIA_DEF get1, #albz0008;
     get2 = 20;                         ||Numero de campo
     Iget = get1<-;
     |ESPECIFICA "GETDATACONTROL",get;
     aFechaOk = FSalida;
     fFechaOk = aFechaOk;

     |SI nDias = 0;
          |ABRE #albm0013;
          |DDEFECTO #albm0013;
          |LEE 000#albm0013.p;
          |CIERRA #albm0013;
          nDias = #albm0013#1;
     |FINSI;


     aAlfa = "Dias"; |HAZ PideDias;
     nNume = fFechaOk - ((nDias/2)!0);
     fDFecha = nNume;
     nNume = nNume + nDias;
     fHFecha = nNume;

     #67#13  = fFechaOk;
     #67#2   = #0#11;
     ||HAZ AlbeConsuLin;
     ||HAZ AlbeEntLin;
     |ABRE #81;
     |HAZ FecEntrega;
     |CIERRA #81;

     |ESTADO_CONTROL nBoton1, "ENABLE";
     |ESTADO_CONTROL nBoton2, "ENABLE";
     |ENCAMPO #20#albz0008;
|FPRC;

|PROCESO Boton2;
     |ESTADO_CONTROL nBoton1, "DISABLE";
     |ESTADO_CONTROL nBoton2, "DISABLE";
     |REFERENCIA_DEF get1, #albz0008;
     get2 = 21;                         ||Numero de campo
     Iget = get1<-;
     |ESPECIFICA "GETDATACONTROL",get;
     aFechaOk = FSalida;
     fFechaOk = aFechaOk;

     |SI nDias = 0;
          |ABRE #albm0013;
          |DDEFECTO #albm0013;
          |LEE 000#albm0013.p;
          |CIERRA #albm0013;
          nDias = #albm0013#1;
     |FINSI;

     aAlfa = "Vtos"; |HAZ PideDias;
     nNume = fFechaOk - ((nDias/2)!0);
     efDFecha = nNume;
     nNume = nNume + nDias;
     efHFecha = nNume;

     enTipoCal = 1;
     |HAZ PedComVal;
     enTipoCal = 0;
     |ESTADO_CONTROL nBoton1, "ENABLE";
     |ESTADO_CONTROL nBoton2, "ENABLE";
     |ENCAMPO #21#albz0008;
|FPRC;

|| --------------------------------------------------------------------------
|| Procesos de Creacion del Pedido
|| --------------------------------------------------------------------------

|PROCESO MiraMonedaBase;
     |ABRE #agifa321;
     |ABRE #agifa324;

     |LEE 010#agifa321.p;   ||Leo parametros de divisa
     monba = #agifa321#9;  ||Cojo la Moneda Base de parametros

     #agifa324#0 = #agifa321#9;
     |LEE 010#agifa324.=;         ||Leo la ficha de la moneda base en Divisas
     cambiomb    = #agifa324#3;  ||Asigno el cambio de la moneda base con el euro
     decim_base  = #agifa324#7;    ||Asigno los decimales de la moneda base
     precio_base = #agifa324#9;   ||Asigno los decimales del precio en la mon. base

     |CIERRA #agifa324;
     |CIERRA #agifa321;
|FPRC;

|PROCESO Param_Divisas; |TIPO 0;
     |ABRE #agifa324;
     #agifa324#0 = #112#81;
     |LEE 001#agifa324.=;
     |CIERRA #agifa324;

     decim_divisa  = #agifa324#7;
     precio_divisa = #agifa324#9;
     nDeci_DivP    = decim_divisa;
     nDeci_PreDivP = precio_divisa;
     cambio        = #agifa324#3; ||Cambio divisa

     |SI montra = "B";
         nDeci_PreVisP   = precio_divisa;
         nDeci_ImpVisP   = decim_divisa;
         nDeci_Base      = decim_base;
         nDeci_PreBase   = precio_base;
         nDeci_BaseMxP   = decim_base;
         nDeci_BaseMx    = decim_base;
         nDeci_PreBasMxP = precio_base;
         nDeci_PreBaseMx = precio_base;
         nDeci_TotVisP   = decim_base;
         nDeci_TotNoVisP = decim_divisa;
     |SINO;
         nDeci_PreVisP   = precio_base;
         nDeci_ImpVisP   = decim_base;
         nDeci_Base      = decim_base;
         nDeci_PreBase   = precio_base;
         |SI cambio = 1;
              nDeci_BaseMxP   = decim_base;    || sin triangulacion
              nDeci_BaseMx    = decim_base;    || sin triangulacion
              nDeci_PreBasMxP = precio_base;
              nDeci_PreBaseMx = precio_base;
         |SINO;
              nDeci_BaseMxP   = #agifa321#10;  || con triangulacion
              nDeci_BaseMx    = #agifa321#10;  || con triangulacion
              nDeci_PreBasMxP = #agifa321#11;
              nDeci_PreBaseMx = #agifa321#11;
         |FINSI;
         nDeci_TotVisP   = decim_divisa;
         nDeci_TotNoVisP = decim_base;
     |FINSI;
|FPRC;

|PROCESO CambioDivisa; |TIPO 0;
      |SI #agifa112#80 = "S"; || Si el proveedor es de divisa pactada
          |ABRE #agifa323;
          |ABRE #agifa322;
          #agifa322#0 = #agifa081#4;
          #agifa322#2 = #agifa081#27;
          #agifa322#7 = "N";
          |SELECT #2#agifa322;  || Con esta clave ...
          |LEE 011#agifa322.=;  || ... busco periodos no finalizados de esa divisa
          |SI FSalida = 0;  || Si existe un periodo no finalizado
              |PARA div_act = 0; |SI div_act = 0; |HACIENDO;
                 #agifa081#28 = #agifa322#3;        || ...y el cambio
                 |LEE 001#agifa322.=;   || Leo las lineas de Proveed/Divisas
                 nfecha = fechasis - #agifa322#6;  || Resto la fecha de fin de periodo a la actual
                 |SI nfecha > 0;  || Si la fecha actual supera a la del periodo asignado ...
                     |MENAV "0000ATENCION deberia Actualizar primero la divisa en Proveedores/Divisas";
                     prov_divisa = #agifa081#4;  || Le envio el cod. de proveedor a "agifa323"
                     |SUB_EJECUTA_NP "agifa323.tab";  || Ejecuto Proveedores/Divisas
                     |ERROR;
                 |SINO;
                     div_act = 1;  || Para salir del PARA
                 |FINSI;
              |SIGUE;
           |SINO;  || Si no existe un periodo no finalizado para esa divisa
              |MENAV "0000No existe un periodo activo para la divisa en Proveedores / Divisas";
              prov_divisa = #agifa081#4;  || Le paso a "agifa323" el proveedor
              |SUB_EJECUTA_NP "agifa323.tab";  || Ejecuto Proveedores / Divisas
              |ERROR;
           |FINSI;
           |SELECT #1#agifa322;
           |CIERRA #agifa322;
           |CIERRA #agifa323;
      |SINO; || Si el Proveedor no es de Divisa pactada ...
           |ABRE #agifa324; || ...miro directamente en divisas
           #agifa324#0 = #agifa081#27;
           |LEE 001#agifa324.=;
           |SI FSalida = 0; ||Si existe la divisa...
               #agifa081#28 = #agifa324#3; || la recojo
               |SI #agifa324#6 ! -1; || Si los dias de margen de actualizacion no
                                  || son = -1 (indefinidos)
                   |PARA div_act = 0; |SI div_act = 0; |HACIENDO;
                      #agifa081#28 = #agifa324#3;        ||Cargo el valor del cambio
                      |LEE 001#agifa324.=;  || Leo la divisa
                      nfecha = fechasis - #agifa324#4;
                      |SI nfecha > #agifa324#6; |Y #agifa324#6 ! -1;  || Si la fecha actual
                          |MENAV "0000ATENCION deberia Actualizar primero la divisa";
                          |SUB_EJECUTA_NP "agifa326.tab";
                          |ERROR;
                      |SINO;
                          div_act = 1;
                      |FINSI;
                    |SIGUE;
                |FINSI;
            |FINSI;
            |CIERRA #agifa324;
      |FINSI;
|FPRC;

|PROCESO CreaCabeceraPed;
     #112#0 = #0#5;
     |LEE 000#112=;

     |ABRE #6;
     #6#0 = #0#0;
     #6#1 = #0#1;
     |LEE 101#6=;
     |SI FSalida ! 0;
         |DDEFECTO #6;
         #6#0 = #0#0;
         #6#1 = #0#1;
         |GRABA 020#6b;
     |FINSI;

     #6#2 = #0#21;
     #6#3 = #0#22;
     |GRABA 020#6a;
     |LIBERA #6;
     |CIERRA #6;

     |DDEFECTO #81;

     #81#25 = #0#0;
     #81#0  = #0#1;
     #81#1  = #0#2;                  || FECHA PEDIDO
     #81#2  = #0#20;                 || FECHA ENTREGA
     #81#4  = #0#5;                  || PROVEEDOR.
     #81#8  = #112#2;                || NOMBRE.
     #81#9  = #112#17;               || FORMA DE PAGO.
     #81#15 = #0#23;                 || OBSERVACIONES
     #81#26 = #81#25 + "/" + #81#0;  || REFERENCIA INTERNA.
     #81#29 = "B";                   || MONEDA DE TRABAJO
     #81#27 = #112#81;               || DIVISA
     #81#32 = monba;                 || MONEDA BASE
     #81#33 = cambiomb;              || CAMBIO MON. BASE A EURO
     #81#5  = #112#24;               || DTO1 del proveedor
     #81#6  = #112#25;               || DTO P.P. del proveedor
     #81#17 = #112#41;                || DTO Acumulado del proveedor

     |DFICE aDirEmp;            || tomo el directorio de la empresa
     |DFICB aDatos;             || tomo el directorio de los datos
     aDirEmp = aDirEmp % -205;  || me quedo con los 5 digitos de la empresa
     nCodEmp = aDirEmp;         || y los paso a una variable numerica
     |PATH_DAT #41 aDatos;      || pongo el path al fichero agifa041

     |ABRE #41;                 || abre el fichero agifa041
     #41#0 = nCodEmp;
     |LEE 020#41=;              || y busco la empresa en la que estoy
     |SI FSalida = 0;
          #81#19 = #41#1;        || nombre de la empresa
          #81#18 = #41#2;        || direccion de la empresa
          #81#20 = #41#3;        || poblacion de la empresa
          #81#21 = #41#4;        || provincia de la empresa
     |FINSI;
     |CIERRA #41;               || cierro el fichero agifa041

     |HAZ Param_Divisas;
     |HAZ CambioDivisa;

     |GRABA 021#81b;
|FPRC;

|PROCESO TotalLinea;
     #19#0 = #0#11;
     |LEE 000#19=;

     #67#6  = #0#18;
     #67#33 = #0#18;
     #67#8  = #0#26;
     #67#27 = #0#27;

     |HAZ TotalLin67;
|FPRC;

|PROCESO CreaLineaPed;      || CREA LAS LINEAS DEL PEDIDO DE COMPRAS...................
     |DDEFECTO #67;
     #67#0  = #81#0;           || N.PEDIDO.
     #67#1  = 1;               || N.LINEA.
     #67#2  = #0#11;           || ARTICULO.
     #67#3  = #0#13;           || ALMACEN.
     #67#4  = #0#12;           || DESCRIPCION.
     #67#5  = #0#17;           || UNIDADES.
     #67#41 = 0;               || uni2

     |HAZ TotalLinea;

     #67#14 = #19#30;          || TIPO DE IVA.
     #67#13 = #0#20;           || FECHA ENTREGA.
     #67#20 = #0#5;            || PROVEEDOR.
     #67#26 = #81#9;           || FORMA DE PAGO.
     #67#28 = #81#25;          || SERIE PEDIDO.
     #67#37 = #67#5;
     #67#45 = #67#41;

     |GRABA 021#67n;
|FPRC;

|PROCESO VisualTotales;
   |SI montra = "D";
       #81#37 = #81#34;
       #81#38 = #81#35;
       #81#39 = #81#36;
       #81#40 = #81#11;
       #81#41 = #81#12;
       #81#42 = #81#13;
   |SINO;
       #81#37 = #81#11;
       #81#38 = #81#12;
       #81#39 = #81#13;
       #81#40 = #81#34;
       #81#41 = #81#35;
       #81#42 = #81#36;
   |FINSI;
|FPRC;

|PROCESO TotalLizaPed;
     || ACTUALIZA LA CABECERA DEL PEDIDO DE COMPRAS..........................
     #81#11 = #81#11 + #67#9;              || IMPORTE TOTAL CABECERA Base.
     #81#12 = #81#12 + #67#11;             || IMPORTE POR RECIBIR Base.
     #81#13 = #81#11 - #81#12;             || IMPORTE RECIBIDO

     |SI #19#196 = 2;
         nPend = (#67#41 - #67#38) * #67#33;
     |SINO;
         nPend = (#67#5 - #67#38) * #67#33;
     |FINSI;
     nPend  = (((nPend < #67#8) < #81#5) < #81#17) < #81#6;

     #81#34 = #81#34 + #67#34;             || IMPORTE TOTAL CAB
     #81#35 = #81#35 + nPend;
     #81#36 = #81#34 - #81#35;             || IMPORTE RECIBIDO CAB

     |HAZ VisualTotales;

     |GRABA 021#81a;
     |LIBERA #81;
|FPRC;

|PROCESO Desactualiza;
     #81#25 = #0#0;
     #81#0 = #0#1;
     |LEE 101#81=;
     |SI FSalida = 0;
         |BORRA 020#81a;

         |LIBERA #81;
     |FINSI;

     #67#28 = #0#0;
     #67#0  = #0#1;
     #67#1  = 1;
     |LEE 101#67=;
     |SI FSalida = 0;
          |BORRA 020#67a;
          |LIBERA #67;
     |FINSI;

     |ABRE #2;
     #2#0 = #0#0;
     #2#1 = #0#1;
     |LEE 101#2=;
     |SI FSalida = 0;
         |BORRA 020#2a;
         |LIBERA #2;
     |FINSI;
     |CIERRA #2;

     |ABRE #3;
     #3#0 = #0#0;
     #3#1 = #0#1;
     |LEE 101#3=;
     |SI FSalida = 0;
         |BORRA 020#3a;
         |LIBERA #3;
     |FINSI;
     |CIERRA #3;

     |ABRE #112;
     |ABRE #19;
     #112#0 = #0#5;
     |LEE 121#112=;
     |SI FSalida = 0;
          fmes1 = #81#1 % A402;
          mes = fmes1 + 42;

          #19#0 = #67#2;
          |LEE 000#19=;

          |SI #19#196 = 2;
               nImpo = ((((#67#41 * #67#6) < #67#8) < #67#27) < #81#5) < #81#17;
          |SINO;
               nImpo = ((((#67#5 * #67#6) < #67#8) < #67#27) < #81#5) < #81#17;
          |FINSI;
          |SI #142#114 = "S";          || si DTO PP a PCM
               nImpo = nImpo < #81#6;
          |FINSI;
          #112mes = #112mes - nImpo;
          #112#55 = #112#55 - nImpo;
          |GRABA 020#112a;
          |LIBERA #112;

          enImpPrvPed = -nImpo;
          efFecha     = #81#1;
          eaProve     = #81#4;
          |HAZ AcumulaPrv;
     |FINSI;
     |CIERRA #112;

     enForma = -1;
     eaDocu  = "agifa067";
     |HAZ StockEntra;

     |HAZ AutoEmpBaja;
|FPRC;

|PROCESO ActualizaPed;
     |SI aParametro = "MODIFICA"; |O nSwModifica = 1;
         |HAZ Desactualiza;

         |HAZ CreaCabeceraPed;
         |HAZ CreaLineaPed;
         |HAZ TotalLizaPed;
         |HAZ AutoEmpAlta;
     |FINSI;

     ||ACTUALIZA LA FICHA DE PROVEEDORES...

     |ABRE #19;
     |ABRE #112;
     #112#0 = #0#5;
     |LEE 121#112=;
     |SI 0 = FSalida;
          fmes1 = #81#1 % A402;
          mes = fmes1 + 42;

          #19#0 = #67#2;
          |LEE 000#19=;

          |SI #19#196 = 2;
               nImpo = ((((#67#41 * #67#6) < #67#8) < #67#27) < #81#5) < #81#17;
          |SINO;
               nImpo = ((((#67#5 * #67#6) < #67#8) < #67#27) < #81#5) < #81#17;
          |FINSI;
          |SI #142#114 = "S";          || si DTO PP a PCM
               nImpo = nImpo < #81#6;
          |FINSI;
          #112mes = #112mes + nImpo;
          #112#55 = #112#55 + nImpo;
          |GRABA 020#112a;
          |LIBERA #112;

          enImpPrvPed = nImpo;
          efFecha = #81#1;
          eaProve = #81#4;
          |HAZ AcumulaPrv;
     |FINSI;
     |CIERRA #112;
     |CIERRA #19;

     enForma = 1;
     eaDocu  = "agifa067";
     |HAZ StockEntra;

     |ABRE #2;
     |DDEFECTO #2;
     #2#0  = #0#0;
     #2#1  = #0#1;
     |LEE 101#2=;
     |SI FSalida ! 0; |GRABA 020#2b; |FINSI;
     #2#2  = #0#2;
     #2#3  = #0#13;
     #2#4  = #0#11;
     #2#5  = #0#12;
     #2#6  = #0#17;
     #2#7  = #0#5;
     #2#8  = #0#23;
     #2#9  = #0#20;
     #2#10 = "N";
     #2#13 = #0#21;
     #2#16 = (("00" + #0#3) % -102) + ":" + (("00" + #0#4) % -102);
     #2#17 = #0#22;
     #2#18 = #112#1;
     |GRABA 020#2a;
     |CIERRA #2;

     |ABRE #3;
     |DDEFECTO #3;
     #3#0  = #0#0;
     #3#1  = #0#1;
     #3#2  = #0#2;
     #3#3  = #0#13;
     #3#4  = #0#11;
     #3#5  = #0#12;
     #3#6  = #0#17;
     #3#7  = #0#5;
     #3#8  = #67#6;
     #3#9  = (("00" + #0#3) % -102) + ":" + (("00" + #0#4) % -102);
     #3#12 = #112#1;
     #3#14 = #0#20;
     #3#15 = #0#15;
     #3#16 = #0#16;
     |GRABA 020#3n;
     |CIERRA #3;

     |SI aParametro ! "MANUAL"; |Y aParametro ! "MODIFICA";
         |ABRE #1;
         #1#0 = aFechaPre;
         #1#1 = aArticulo;
         #1#2 = aAlmacen;
         |LEE 101#1=;
         |SI FSalida = 0;
             |BORRA 020#1a;
         |FINSI;
         |CIERRA #1;
     |FINSI;

/*
     |SI #0#28 ! "          ";
         |ABRE #1;
         #1#0 = #0#28;
         #1#1 = #0#11;
         #1#2 = #0#13;
         |LEE 101#1=;
         |SI FSalida = 0;
             |BORRA 020#1a;
         |FINSI;
         |CIERRA #1;
    |FINSI;
*/
|FPRC;


|PROCESO Tipo3;  |TIPO 12;
     ||Solo se sale si se presiona Ctrl + Q.  O luego desde Cancelar del menu.
     |SI FSalida = 7; |ACABA; |FINSI;
     ||SI FSalida ! 0; |ACABA; |FINSI;

     |ABRE #13; |LEE 000#13p; |CIERRA #13;
     |ABRE #142;    |LEE 020#142p; |CIERRA #142;

     |HAZ MiraMonedaBase;

     |ABRE #81;
     |ABRE #67;
     |ABRE #112;
     |ABRET #19;

     |SI aParametro ! "MODIFICA"; |Y nSwModifica = 0;
         |HAZ CreaCabeceraPed;
         |HAZ CreaLineaPed;
         |HAZ TotalLizaPed;
     |FINSI;

     |ESTADO_CONTROL nBoton3, "ENABLE";
     |ESTADO_CONTROL nBoton4, "ENABLE";
     |ESTADO_CONTROL nBoton5, "ENABLE";
     |ESTADO_CONTROL nBoton6, "ENABLE";
     |ESTADO_CONTROL nBoton7, "ENABLE";
     |ESTADO_CONTROL nBoton8, "ENABLE";
     |ESTADO_CONTROL nBoton9, "ENABLE";

     |PARA aMenu = ""; |SI aMenu = ""; |HACIENDO;
           |LEETECLA aTecla;

           |SI aMenu = "1"; |O aMenu = "5"; |O aMenu = "6"; |O aMenu = "7";  ||Imprimir
               |SI aParametro = "MODIFICA"; |O nSwModifica = 1;
                   |HAZ Desactualiza;

                   |HAZ CreaCabeceraPed;
                   |HAZ CreaLineaPed;
                   |HAZ TotalLizaPed;
               |FINSI;

               eaHoraPed = (("00" + #0#3) % -102) + ":" + (("00" + #0#4) % -102);
               eaDefImpre = #13#6;
               ser_ped = #81#25;
               num_ped = #81#0;
               ||HAZ MenuImpre;

               |SI aMenu = "1";
                    eaImpresora = "";
                    |SUB_EJECUTA_NP "agifa100";
               |FINSI;
               |SI aMenu = "5";
                    |ABRE #albm0013;
                    |DDEFECTO #albm0013;
                    |LEE 000#albm0013.p;
                    |CIERRA #albm0013;
                    eaImpresora = #albm0013#6;
                    |SUB_EJECUTA_NP "agifa100";
               |FINSI;
               |SI aMenu = "6";
                    |SUB_EJECUTA_NP "agifa100;FAX";
               |FINSI;
               |SI aMenu = "7";
                    |SUB_EJECUTA_NP "agifa100;EMAIL";
               |FINSI;
               aMenu = "";
           |SINO;
               |SI aMenu ! "";   |SAL;  |FINSI;
           |FINSI;
     |SIGUE;

     |SI aMenu = "3";     ||Aceptar
          |HAZ ActualizaPed;
     |FINSI;

     |SI aMenu = "4";     ||Cancelar
          |SI aParametro ! "MODIFICA";
              #81#25 = #0#0;
              #81#0 = #0#1;
              |LEE 101#81=;
              |SI FSalida = 0;
                  |BORRA 020#81a;
              |FINSI;

              #67#28 = #0#0;
              #67#0 = #0#1;
              #67#1 = 1;
              |LEE 101#67=;
              |SI FSalida = 0;
                  |BORRA 020#67a;
              |FINSI;
              |HAZ AutoEmpBaja;
          |FINSI;
     |FINSI;

     |CIERRA #81;
     |CIERRA #67;
     |CIERRA #112;
     |CIERRAT #19;
|FPRC;

|PROCESO Tipo80;  |TIPO 80;
     FSalida = 3399;
|FPRC;

|PROCESO Modificacion;
     nError = 0;

     |ABRE #81;
     #81#25 = ser_ped;
     #81#0  = num_ped;
     |LEE 000#81=;
     |SI FSalida ! 0;  |CIERRA #81;  nError = 1;  |ACABA;  |FINSI;
     |CIERRA #81;

     |ABRE #67;
     #67#28 = #81#25;
     #67#0  = #81#0;
     #67#1  = 1;
     |LEE 000#67=;
     |SI FSalida ! 0;  |CIERRA #67;  nError = 1;  |ACABA;  |FINSI;
     |CIERRA #67;

     |ABRE #3;
     #3#0 = #81#25;
     #3#1 = #81#0;
     |LEE 000#3=;
     |SI FSalida ! 0;  |DDEFECTO #3;  |FINSI;
     |CIERRA #3;

     |ABRE #112;
     #112#0 = #81#4;
     |LEE 000#112=;
     |SI FSalida ! 0;  |DDEFECTO #112;  |FINSI;
     |CIERRA #112;

     |ABRE #17;
     #17#0 = #67#2;
     #17#1 = #67#3;
     |LEE 040#17=;
     |SI FSalida ! 0;  |DDEFECTO #17;  |FINSI;
     |CIERRA #17;

     #0#0  = #81#25;
     #0#1  = #81#0;

     |ABRE #6;
     #6#0 = #0#0;
     #6#1 = #0#1;
     |LEE 000#6=;
     |SI FSalida ! 0;  |DDEFECTO #6;  |FINSI;
     |CIERRA #6;

     #0#2  = #81#1;
     aAlfa = #3#9 % 102;  #0#3 = aAlfa;
     aAlfa = #3#9 % 402;  #0#4 = aAlfa;
     #0#5  = #81#4;
     #0#6  = #81#8;
     #0#7  = #112#23;
     #0#8  = #112#6;
     #0#9  = #112#7;
     #0#10 = #112#83;
     #0#11 = #67#2;
     #0#12 = #67#4;
     #0#13 = #67#3;
     #0#14 = #17#41;
     #0#15 = #67#3;
     #0#17 = #67#5;
     #0#18 = #67#6;
     #0#20 = #81#2;
     #0#21 = #6#2;

     #0#22 = #6#3;
     #0#23 = #81#15;
     #0#26 = #67#8;
     #0#27 = #67#27;
     #0#28 = "";
     #0#19 = (((#0#17 * #0#18) < #0#26) < #0#27);

     |HAZ CogeArticulo;
|FPRC;

|PROCESO Boton3;
     aMenu = "1";
     nPtec  = &"A";  |PTEC nPtec;
|FPRC;

|PROCESO Boton7;
     aMenu = "5";
     nPtec  = &"A";  |PTEC nPtec;
|FPRC;

|PROCESO Boton8;
     aMenu = "6";
     nPtec  = &"A";  |PTEC nPtec;
|FPRC;

|PROCESO Boton9;
     aMenu = "7";
     nPtec  = &"A";  |PTEC nPtec;
|FPRC;

|PROCESO Boton4;
     nSwModifica = 1;
     aMenu = "2";
     nPtec  = &"A";  |PTEC nPtec;
|FPRC;

|PROCESO Boton5;
     nSwModifica = 0;
     aMenu = "3";
     nPtec  = &"A";  |PTEC nPtec;
|FPRC;

|PROCESO Boton6;
     nSwModifica = 0;
     aMenu = "4";
     nPtec  = &"A";  |PTEC nPtec;
|FPRC;

|PROGRAMA;
     |ABRE #agifa324;
     |DDEFECTO #agifa324;
     #agifa324#0 = "EUR";
     |LEE 000#agifa324.=;
     EURODB = #agifa324#9;
     |CIERRA #agifa324;

     nSwModifica = 0;
     |ABRE #142;    |LEE 020#142p; |CIERRA #142;

     aParametro = PARAMETRO;  |QBF aParametro;

     |SI aParametro = "FICHERO";
          |DFICE aAlfa;
          aAlfa = aAlfa + Usuario;
          |XABRE "", aAlfa, nHandle;
          |SI FSalida = 0;
               |XLEE nHandle, 250, aParametro;
               |XCIERRA nHandle;
               |FBORRA aAlfa;
          |FINSI;
     |FINSI;

     |SI aParametro = "MANUAL";
         nSwSerie = 1;
         |DDEFECTO #0;
     |FINSI;

     |SI aParametro = "MODIFICA";
         nError   = 0;
         nSwSerie = 0;
         |HAZ Modificacion;
         |SI nError = 1;
             |MENAV "0000 No encuentro el Pedido en el Fichero de Pedidos Compras.";
             |ACABA;
         |FINSI;
     |FINSI;

     |SI aParametro ! "MANUAL"; |Y aParametro ! "MODIFICA";
         aProve    = aParametro % 106;
         aArticulo = aParametro % 720;
         aAlmacen  = aParametro % 2705;
         aUnidades = aParametro % 3215;
         aFechaEnt = aParametro % 4710;
         aFechaPre = aParametro % 5710;

         |SI aAlmacen = "00000";
              nSwSerie = 1;
         |SINO;
              nSwSerie = 0;
         |FINSI;

         #0#5  = aProve;
         #0#11 = aArticulo;
         #0#13 = aAlmacen;
         #0#17 = aUnidades;
         #0#20 = aFechaEnt;
         #0#28 = aFechaPre;

         |ABRE #112;
         #112#0 = #0#5;
         |LEE 040#112=;
         |SI FSalida ! 0;  |DDEFECTO #112;  |FINSI;
         |CIERRA #112;

         |ABRE #19;
         #19#0 = #0#11;
         |LEE 040#19=;
         |SI FSalida ! 0;  |DDEFECTO #19;  |FINSI;
         |CIERRA #19;

         |ABRE #17;
         #17#0 = #0#11;
         #17#1 = #0#13;
         |LEE 040#17=;
         |SI FSalida ! 0;  |DDEFECTO #17;  |FINSI;
         |CIERRA #17;

         #0#6  = #112#1;
         #0#7  = #112#23;
         #0#8  = #112#6;
         #0#9  = #112#7;
         #0#10 = #112#83;

         #0#15 = #19#134;
         #0#16 = #19#195;

         #0#14 = #17#41;

         ||SI #0#5  ! "      ";                |C_M #0#5,  1, "N";  |FINSI;
         ||SI #0#11 ! "                    ";  |C_M #0#11, 1, "N";  |FINSI;
         ||SI #0#13 ! 0;                       |C_M #0#13, 1, "N";  |FINSI;
         ||SI #0#17 ! 0;                       |C_M #0#17, 1, "N";  |FINSI;

         |HAZ CogeArticulo;
         |HAZ CalcuPrecio;
         |HAZ TotalPedido;
         |HAZ FechaV;
     |FINSI;

     |HORA aHora;
     aAlfa = aHora % 102;  #0#3  = aAlfa;
     aAlfa = aHora % 402;  #0#4  = aAlfa;

     |PINPA #0#0;
     |PINDA #0#0;

     |CONTROL_SIMPLE 1,"BOTON,{{155}} ",  2181, 2184, Boton1;
     nBoton1  = FSalida;

     |CONTROL_SIMPLE 1,"BOTON,{{155}} ",  2281, 2284, Boton2;
     nBoton2  = FSalida;

     |SI aParametro ! "MODIFICA";
         |SI #142#185 = "S"; #0#0 = #142#192; |PTEC 802; |FINSI;
     |FINSI;

     |C_M #0#0, 1, "S";|C_M #0#1, 1, "S";

     |SI aParametro = "MODIFICA";
         |C_M #0#0, 1, "N"; |C_M #0#1, 1, "N";
     |FINSI;

     |CONTROL_SIMPLE 1,"BOTON,{{205}} Imprimir",  2805, 2820, Boton7;
     nBoton7  = FSalida;

     |CONTROL_SIMPLE 1,"BOTON,{{205}} FAX",  2825, 2840, Boton8;
     nBoton8  = FSalida;

     |CONTROL_SIMPLE 1,"BOTON,{{205}} Mail",  2845, 2860, Boton9;
     nBoton9  = FSalida;

     |CONTROL_SIMPLE 1,"BOTON,{{205}} Listar",  2865, 2880, Boton3;
     nBoton3  = FSalida;

     |CONTROL_SIMPLE 1,"BOTON,{{219}} Modificar",  3025, 3040, Boton4;
     nBoton4  = FSalida;

     |CONTROL_SIMPLE 1,"BOTON,{{218}} Aceptar",  3045, 3060, Boton5;
     nBoton5  = FSalida;

     |CONTROL_SIMPLE 1,"BOTON,{{217}} Cancelar",  3065, 3080, Boton6;
     nBoton6  = FSalida;

     |PARA aMenu = "2"; |SI aMenu = "2"; |HACIENDO;
          |ESTADO_CONTROL nBoton3, "DISABLE";
          |ESTADO_CONTROL nBoton4, "DISABLE";
          |ESTADO_CONTROL nBoton5, "DISABLE";
          |ESTADO_CONTROL nBoton6, "DISABLE";
          |ESTADO_CONTROL nBoton7, "DISABLE";
          |ESTADO_CONTROL nBoton8, "DISABLE";
          |ESTADO_CONTROL nBoton9, "DISABLE";

          aMenu = "";
          |ENDATOS #1#0;
          |C_M #0#0, 1, "N";|C_M #0#1, 1, "N";
     |SIGUE;

     |DESTRUYECONTROL nBoton1;
     |DESTRUYECONTROL nBoton2;
     |DESTRUYECONTROL nBoton3;
     |DESTRUYECONTROL nBoton4;
     |DESTRUYECONTROL nBoton5;
     |DESTRUYECONTROL nBoton6;
     |DESTRUYECONTROL nBoton7;
     |DESTRUYECONTROL nBoton8;
     |DESTRUYECONTROL nBoton9;

     |PULSATECLA;
|FPRO;

|PROCESO AlmSerie; |TIPO 0;
     |SI nSwSerie = 1;
          |ABRE #agifa007;
          |DDEFECTO #agifa007;
          #agifa007#26 = #albz0008#0;
          |LEE 000#agifa007.=;
          #albz0008#13 = #agifa007#31;
          |PINTA #albz0008#13;
          |CIERRA #agifa007;
     |FINSI;

     |SI FSalida = 0; |Y aParametro ! "MANUAL";  |Y aParametro ! "MODIFICA";
          |PTEC 802;
          |PTEC 802;
          |PTEC 802;
          |PTEC 802;
          |PTEC 802;
     |FINSI;
|FPRC;

컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴 Calculo F.Valor 컴컴컴컴컴컴컴컴컴컴
|PROCESO Baja027;
     #727#0 = #726#0;
     #727#1 = "     ";
     #727#2 = 0;
     #727#3 = 0;
|FPRC;

|PROCESO Alta027;
     #727#0 = #726#0;
     #727#1 = "zzzzz";
     #727#2 = 999999;
     #727#3 = 999;
|FPRC;

|PROCESO EntPediFe;
     |DDEFECTO #81;
     #81#25 = #67#28;
     #81#0 = #67#0;
     |LEE 000#81=;

     nUni1 = #67#5 - #67#38;
     nUni2 = #67#41 - #67#46;
     |SI nUni1 ! 0; |O nUni2 ! 0;
          |DDEFECTO #726;
          #726#0 = #67#13;
          |LEE 101#726=;
          |SI FSalida ! 0; |GRABA 020#726b; |FINSI;
          #726#1 = #726#1 + nUni1;
          #726#2 = #726#2 + nUni2;
          #726#3 = #726#3 + 1;
          |GRABA 020#726a;
          |LIBERA #726;

          #727#0 = #726#0;
          #727#1 = #67#28;
          #727#2 = #67#0;
          #727#3 = #67#1;
          |LEE 101#727=;
          |SI FSalida ! 0;
              |DDEFECTO #727;
              #727#0 = #726#0;
              #727#1 = #67#28;
              #727#2 = #67#0;
              #727#3 = #67#1;
              #727#6 = #81#4;
              #727#7 = #81#8;
              #727#8 = #67#2;
              #727#9 = #67#4;
              |GRABA 020#727b;
          |FINSI;
          #727#4 = #727#4 + nUni1;
          #727#5 = #727#5 + nUni2;
          |GRABA 020#727a;
          |LIBERA #727;
     |FINSI;
|FPRC;

|DEFBUCLE EntPediFe;
     #67#2;
     ;
     "      ", fDFecha,     0,     0,     0, "  ", "     ", 000001;
     "zzzzzz", fHFecha, 99.99, 99.99, 99.99, "zz", "zzzzz", 999999;
     ;
     NULL, EntPediFe;
|FIN;

|PROCESO FecEntrega;
     aAlfa = Usuario; aAlfa = "3" + aAlfa;
     |NOME_DAT #726 aAlfa; |DELINDEX #726; |ABRE #726;

     aAlfa = Usuario; aAlfa = "4" + aAlfa;
     |NOME_DAT #727 aAlfa; |DELINDEX #727; |ABRE #727;

     |BUCLE EntPediFe;
     |PARA fFecha = fDFecha; |SI fFecha [ fHFecha; |HACIENDO fFecha = fFecha + 1;
          |DDEFECTO #726;
          #726#0 = fFecha;
          |LEE 000#726=;
          |SI FSalida ! 0; |GRABA 020#726n; |FINSI;
     |SIGUE;

     |LEE 000#726p;
     |PARA;  |SI;  |HACIENDO;
          |CONSULTA_CLAVE #1#726;
          |SI FSalida ! 0;  |SAL;  |FINSI;
          |CONSULTA_F_CLAVE #1#727, Baja027, Alta027;
     |SIGUE;

     |CIERRA #726; |DELINDEX #726;
     |CIERRA #727; |DELINDEX #727;
|FPRC;
컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴

|PROCESO SacaAnterior;
     aMensaje = "0000Ver Pedido Anterior";
||     |MENAV aMensaje;
     |DBASE aAlfa;
     |QBF aAlfa;
     aAlfa = aAlfa + "def/albz0008";
     |CARGA_DEF aAlfa, albz8@;
     |SI FSalida = 0;
          |ABRE #81;
          #81#25 = #0#0;
          #81#0  = 999999;
          |LEE 040#81];
          |SI FSalida = 0;
              |LEE 040#81a;
          |SINO;
              |LEE 040#81u;
          |FINSI;
          |CIERRA #81;
          |SI FSalida = 0;  |Y  #81#25 = #0#0;
               |ABRE #albz8@;
               |DDEFECTO #albz8@;

               |ABRE #67;
               #67#28 = #81#25;
               #67#0  = #81#0;
               #67#1  = 1;
               |LEE 000#67=;
               |SI FSalida ! 0;  |DDEFECTO #67;  |FINSI;
               |CIERRA #67;

               |ABRE #3;
               #3#0 = #81#25;
               #3#1 = #81#0;
               |LEE 000#3=;
               |SI FSalida ! 0;  |DDEFECTO #3;  |FINSI;
               |CIERRA #3;

               |ABRE #112;
               #112#0 = #81#4;
               |LEE 000#112=;
               |SI FSalida ! 0;  |DDEFECTO #112;  |FINSI;
               |CIERRA #112;

               |ABRE #17;
               #17#0 = #67#2;
               #17#1 = #67#3;
               |LEE 040#17=;
               |SI FSalida ! 0;  |DDEFECTO #17;  |FINSI;
               |CIERRA #17;

               |ABRE #6;
               #6#0 = #1008#0;
               #6#1 = #1008#1;
               |LEE 000#6=;
               |SI FSalida ! 0;  |DDEFECTO #6;  |FINSI;
               |CIERRA #6;

               #1008#0  = #81#25;
               #1008#1  = #81#0;
               #1008#2  = #81#1;
               aAlfa = #3#9 % 102;  #1008#3 = aAlfa;
               aAlfa = #3#9 % 402;  #1008#4 = aAlfa;
               #1008#5  = #81#4;
               #1008#6  = #81#8;
               #1008#7  = #112#23;
               #1008#8  = #112#6;
               #1008#9  = #112#7;
               #1008#10 = #112#83;
               #1008#11 = #67#2;
               #1008#12 = #67#4;
               #1008#13 = #67#3;
               #1008#14 = #17#41;
               #1008#17 = #67#5;
               #1008#18 = #67#6;
               #1008#20 = #81#2;
               #1008#21 = #6#2;
               #1008#22 = #6#3;
               #1008#23 = #81#15;
               #1008#26 = #67#8;
               #1008#27 = #67#27;
               #1008#28 = "";
               #1008#19 = (((#1008#17 * #1008#18) < #1008#26) < #1008#27);

               |ABRE #44;
               #44#0 = #1008#5;
               #44#1 = #1008#11;
               |LEE 101#44=;
               |SI FSalida ! 0;
                   |DDEFECTO #44;
                   #44#0 = #1008#5;
                   #44#1 = #1008#11;
                   ||No tiene que coger estos valores. Segun 3026/160
                   ||#44#2 = #19#134;
                   ||#44#3 = #19#195;
                   |GRABA 020#44n;
                   |LIBERA #44;
               |FINSI;
               |CIERRA #44;

               #1008#15 = #44#2;
               #1008#16 = #44#3;

               |ABRE #19;
               #19#0 = #1008#11;
               |LEE 040#19=;
               |SI FSalida ! 0;  |DDEFECTO #19;  |FINSI;
               |CIERRA #19;

               |ABRE #239;
               #239#0 = #19#5;
               |LEE 040#239=;
               |SI FSalida ! 0;  |DDEFECTO #239;  |FINSI;
               |CIERRA #239;

               aAlfa = #19#195;  |QBF aAlfa;
               |SI aAlfa = "";  aAlfa = #19#1;  |FINSI;
               #1008#12 = aAlfa;

               aAlfa = #239#1;  |QBF aAlfa;
               |SI aAlfa = "";  aAlfa = "Unidades";  |FINSI;
               #1008#29 = aAlfa;

               |PINPA #0#albz8@;
               |PINDA #0#albz8@;

               |PINTA 0650, "--- Consulta Ultimo Pedido Introducido ---";

               |PAUSA;

               |CIERRA #albz8@;
          |SINO;
               aMensaje = "0000No se ha introducido ningun Pedido en esta Serie";
               |MENAV aMensaje;
          |FINSI;
          |DESCARGA_DEF #albz8@;
     |FINSI;
|FPRC;

컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴횵mpresion컴컴컴컴컴컴컴컴컴컴컴컴
|PROCESO BotonI1;
     |SUB_EJECUTA_NP "agifa100";
|FPRC;

|PROCESO BotonI2;
     |SUB_EJECUTA_NP "agifa100;FAX";
/*
     ||Para el numero de copias, pero me da error si lo saco por pantalla
     ||de fichero ccc.spo ya en uso. ||A sharing violation occured ...
     ||Ahora esto lo hace en el propio agifa100. :)
     |ABRE #albm0013;
     |DDEFECTO #albm0013;
     |LEE 000#albm0013.p;
     |CIERRA #albm0013;
     eaImpresora = #albm0013#6;

     |SI #albm0013#10 ! 0;
          |PARA nVeces = 1; |SI nVeces [ #albm0013#10; |HACIENDO nVeces = nVeces + 1;
               |SUB_EJECUTA_NP "agifa100";
          |SIGUE;
     |FINSI;
*/
|FPRC;

|PROCESO BotonI3;
     |SUB_EJECUTA_NP "agifa100;EMAIL";
|FPRC;

|PROCESO MenuImpre;
     |VENTANA 0835, 2065, -1, -1, "";
     nVentanaI = FSalida;

     |CONTROL_SIMPLE 0,"BOTON,{{205}}Imprimir", 0936, 0964,  BotonI1;
     nBotonI1 = FSalida;

     |CONTROL_SIMPLE 0,"BOTON,{{205}}Envio Fax", 1136, 1164,  BotonI2;
     nBotonI2 = FSalida;

     |CONTROL_SIMPLE 0,"BOTON,{{205}}Envio Email", 1336, 1364,  BotonI3;
     nBotonI3 = FSalida;

     |ESTADO_CONTROL nBotonI1, "ENABLE";
     |ESTADO_CONTROL nBotonI2, "ENABLE";
     |ESTADO_CONTROL nBotonI3, "ENABLE";

     |LEETECLA aTecla;

     |FIN_CONTROL_WINDOWS nBotonI1;
     |FIN_CONTROL_WINDOWS nBotonI2;
     |FIN_CONTROL_WINDOWS nBotonI3;

     |SI nVentanaI ! -1;  |FINVENTANA nVentanaI;  |FINSI;

     |PULSATECLA;
|FPRC;

|PROCESO AutoEmpAlta;
     |ABRE #dsm00115; |LEE 000#dsm00115.p; nSal = FSalida; |CIERRA #dsm00115;
     |SI nSal = 0;
          eaProve = #81#4;
          aAlfa = "dsz99982;PEDALTA" + #81#25 + (("000000" + #81#0) % -106);
          |SUB_EJECUTA aAlfa;
     |FINSI;
|FPRC;

|PROCESO AutoEmpBaja;
     |ABRE #dsm00115; |LEE 000#dsm00115.p; nSal = FSalida; |CIERRA #dsm00115;
     |SI nSal = 0;
          eaProve = #81#4;
          aAlfa = "dsz99982;PEDBAJA" + #81#25 + (("000000" + #81#0) % -106);
          |SUB_EJECUTA aAlfa;
     |FINSI;
|FPRC;
