|FICHEROS;
 gomay001 #0;

 gomantho #1;

 goman001 #2;
 goman002 #3;
 gopartia #4;
 gocerhis #5;
 gopre001 #6;
 gomaz001 #7;

 agifa077 #77;
 agifa080 #80;

 agifa141 #141;
 agifa142 #142;
 agifa724 #724;

 cacuent@ #900;
 gomaw001 #999;
 goparame #100;
|FIN;

|VARIABLES;

 &cCodMonBase   = "";
 &cDesMonBase   = "";
 &cCodMonTrab   = "";
 &enSwInf  = 0;
 aFichero = "";
 nInkey   = 0;

 nSelec   = 0;
 nSwCta   = 0;
 nSwDes   = 0;
 nEmpresa = 0;
 nPeriodo = 0;

 aAlfa1    = "";
 aAlfa2    = "";
 nNume1    = 0;
 nNume2    = 0;
 nDigCta   = 0;
 aDirBase  = "";
 aPathDef  = "";
 aPathFic0 = "";
 aDirFich  = "";
 aDef900   = "";
 aQueQuiero = "";
 nNumCampos = 0;
 aNumCampos = "";

 nSwAlguno  = 0;
 aSerie   = "";
 nNumero  = 0;
 aArticul = "";
 aCta     = "";
 aCtaG    = "";
 aCtaI    = "";
 aNomCta  = "";
 nGasto   = 0;
 nIngreso = 0;
 nGastoN  = 0;
 nAnGas   = 0;
 nAnIng   = 0;
 nSw1     = 0;
 nSw2     = 0;
 aFactu   = "";
|FIN;

|| ***************** Procesos De Pantalla

|CALCULO Primero; |TIPO 8;
 |HAZ MonBas;
 nSwDes = 0;

 |ABRE #142;
 |DDEFECTO #142;
 |LEE 000#142p;
 |CIERRA #142;
 nDigCta = #142#11;

|| ............. Busco Directorio y Empresa contable
 |ABRE #7;
 |DDEFECTO #7;
 |LEE 000#7p;
 |SI FSalida = 0;
     nSwDes   = 1;
     aDirBase = #7#1;
     nEmpresa = #7#2;
     nPeriodo = #7#3;
 |SINO;
     |ABRE #141;
     |DDEFECTO #141;
     |LEE 000#141p;
     |SI FSalida = 0;
         nSwDes   = 1;
         aDirBase = #141#11;
         nEmpresa = #141#0;
         nPeriodo = #141#8;
     |FINSI;
     |CIERRA #141;
 |FINSI;
 |CIERRA #7;

 |SI nSwDes = 1;
     || ... Carga Defs de la contabilidad y Directorio
     |QBF aDirBase;
     aPathDef  = aDirBase + "def/";
     aPathFic0 = aDirBase + "fich/";

     aAlfa1 = nEmpresa; |QBF aAlfa1; aAlfa1 = ("00000" + aAlfa1) % -105;
     aAlfa2 = nPeriodo; |QBF aAlfa2; aAlfa2 = ("0"     + aAlfa2) % -101;

     aDirFich = aPathFic0 + aAlfa1 + aAlfa2 + "/";

     aDef900 = aPathDef + "cacuenta";
     |CARGA_DEF aDef900, cacuent@;
     |SI FSalida ! 0;
         aDef900  = "";
         nSwDes   = 0;
     |SINO;
         aQueQuiero = "|NC";
         aNumCampos = #cacuent@#aQueQuiero#;
         nNumCampos = aNumCampos;

         |PATH_DAT #900 aDirFich
     |FINSI;

 |FINSI;
|FCAL;

|CALCULO Ultimo; |TIPO 9;
     |SI aDef900 ! "";   |DESCARGA_DEF #cacuent@; aDef900 = ""; |FINSI;
|FCAL;


|CALCULO serieh; |TIPO 0;
 |SI #0#0 > #0#1; |ERROR; |FINSI;
|FCAL;

|CALCULO expeh; |TIPO 0;
 |SI #0#2 > #0#3; |ERROR; |FINSI;
|FCAL;

|CALCULO ctah; |TIPO 0;
 |SI #0#4 > #0#5; |ERROR; |FINSI;
|FCAL;


|RUTINA infC;
 #900#0 = "6           ";
 #900#1 = 1;
|FRUT;

|RUTINA supC;
 #900#0 = "8           ";
 #900#1 = 9;
|FRUT;

|CALCULO LaCta; |TIPO 7;
 |SI nSwDes = 1;
     |MENSA "    Pulsa <F2> para consultar cuentas ";
 |FINSI;
|FCAL;

|CALCULO LaCuenta; |TIPO 0;
 |BLIN 04;
 nInkey = FSalida;

 nNume1   = Campo + 1;
 |C_M #0nNume1,1,"S";

 aCta = #0Campo;
 |SI nSwDes = 1;
     |SI nInkey = 10;
         |ABRE #900;
         |CONSULTA_F_CLAVE #1#900,infC,supC;
         #0Campo  = #900#0; |PINTA #0Campo;
         #0nNume1 = #900#2; |PINTA #0nNume1;
         |C_M #0nNume1,1,"N";
         |CIERRA #900;
         |ACABA;
     |FINSI;

     nSwCta = 0;
     |HAZ BuscarCta;
     |SI nSwCta = 1;
         #0Campo  = #900#0; |PINTA #0Campo;
         #0nNume1 = #900#2; |PINTA #0nNume1;
         |C_M #0nNume1,1,"N";
     |SINO;
         |CONFI "    SNo existe cuenta contable. Continuar S/N ";
         |SI FSalida ! 0;
             |ERROR;
         |FINSI;
     |FINSI;
  |FINSI;
|FCAL;

||*********************************************************************
|| //////////////////////////////////
|PROCESO BuscoCta;
 nSwCta  = 1;
 aNomCta = #900#2;
 |ERROR10;
|FPRC;

|DEFBUCLE BuscoCta;
 #900#1002;
 3;
 aCta, 0, "S";
 aCta, 9, "S";
 ;
 NULL, BuscoCta;
|FIN;

|PROCESO BuscarCta;
     aNomCta = "";
     nSwCta  = 0;
     |SI nNumCampos > 60;
         |ABRE #900;
         #900#0 = #0Campo;
         |LEE 000#900=;
         |SI FSalida = 0;
             aNomCta = #900#2;
             nSwCta = 1;
         |FINSI;
         |CIERRA #900;
     |SINO;
         |BUCLE BuscoCta;
     |FINSI;
|FPRC;
|| *********************************************************

|PROCESO GrabaAuxi;

 nSelec = 0;
 |SI aSerie  < #0#0; |O aSerie  > #0#1; nSelec = 1; |FINSI;
 |SI nNumero < #0#2; |O nNumero > #0#3; nSelec = 1; |FINSI;
 |SI aCta    < #0#4; |O aCta    > #0#5; nSelec = 1; |FINSI;
 |SI nSelec = 1; |ACABA; |FINSI;

 #999#0 = aSerie;
 #999#1 = nNumero;
 #999#2 = aCta;
 |LEE 001#999=;
 |SI FSalida ! 0;
     |DDEFECTO #999;
     #999#0 = aSerie;
     #999#1 = nNumero;
     #999#2 = aCta;
     |SI aNomCta = "";
         |SI #999#2 = #0#6;
             aNomCta = #0#7;
         |SINO;
             |SI #999#2 = #0#8;
                 aNomCta = #0#8;
             |SINO;
                 |SI nSwDes = 1; |HAZ BuscarCta; |FINSI;
             |FINSI;
         |FINSI;
     |FINSI;
     #999#3 = aNomCta;
     |GRABA 020#999c;
 |FINSI;

 #999#4 = #999#4 + nGasto;
 #999#5 = #999#5 + nIngreso;
 #999#6 = #999#6 + nGastoN;
 #999#7 = #999#7 + nAnGas;
 #999#8 = #999#8 + nAnIng;
 |GRABA 020#999a;
 nSwAlguno  = 1;
|FPRC;

|| *********************************************************
|PROCESO LeeArticul;
 nSw2  = 0;
 aCtaG = "";
 aCtaI = "";
 |ABRE #724;
 #724#0 = aArticul;
 |LEE 001#724=;
 |SI FSalida = 0;
     nSw2    = 1;
     aCtaG   = #724#3;
     aCtaI   = #724#2;
 |FINSI;
 |CIERRA #724;
|FPRC;


|PROCESO BuscoPre001;
 |ABRE #6;
 #6#0 = #5#0;
 #6#1 = #5#1;
 |LEE 001#6=;
 |SI FSalida = 0;
     nSw1 = 1;
     aSerie  = #6#15;
     nNumero = #6#16;
 |FINSI;
 |CIERRA #6;
|FPRC;


|PROCESO BuscoPartia;
 nSw1 = 1;
 aSerie  = #4#2;
 nNumero = #4#3;
 |ERROR10;
|FPRC;

|DEFBUCLE BuscoPartia;
 #4#1;
 ;
 " ", #80#27, #80#0, #80#1, 001;
 "z", #80#27, #80#0, #80#1, 999;
 e;
 NULL, BuscoPartia;
|FIN;
|| *********************************************************

|PROCESO LinSalAn;
 aCta     = #3#1;
 aNomCta  = #3#2;
 nAnGas   = #3#3;
 nAnIng   = #3#4;

 |HAZ GrabaAuxi;
|FPRC;

|DEFBUCLE LinSalAn;
 #3#1;
 ;
 #2#0, #0#4;
 #2#0, #0#5;
 e;
 NULL, LinSalAn;
|FIN;

|PROCESO SalAntes;

 aSerie   = #2#2;
 nNumero  = #2#3;

 aCta     = "";
 aNomCta  = "";
 nGasto   = 0;
 nIngreso = 0;
 nGastoN  = 0;
 nAnGas   = 0;
 nAnIng   = 0;

 |BUCLE LinSalAn;
|FPRC;

|DEFBUCLE SalAntes;
 #2#2;
 ;
 #0#0, #0#2, "       ";
 #0#1, #0#3, "zzzzzzz";
 e;
 NULL, SalAntes;
|FIN;

|| *********************************************************

|PROCESO Certificado;
 aSerie   = "";
 nNumero  = 0;
 aCta     = "";
 aNomCta  = "";
 nGasto   = 0;
 nIngreso = 0;
 nGastoN  = 0;
 nAnGas   = 0;
 nAnIng   = 0;

 nSw1 = 0;
 |HAZ BuscoPre001;
 |SI nSw1 = 1;
     aArticul = #5#16;
     |HAZ LeeArticul;
     |SI aCtaI [ "           ";
         aCta    = #0#8;
     |SINO;
         aCta    = aCtaI;
     |FINSI;
     nIngreso = #5#10;
     |HAZ GrabaAuxi;
 |FINSI;
|FPRC;

|DEFBUCLE Certificado;
 #5#1;
 ;
 "     ", 000001, 001, 1;
 "zzzzz", 999999, 999, 999;
 e;
 NULL, Certificado;
|FIN;

|| *********************************************************

|PROCESO LinAlbaran;
 aSerie   = "";
 nNumero  = 0;
 aCta     = "";
 aNomCta  = "";
 nGasto   = 0;
 nIngreso = 0;
 nGastoN  = 0;
 nAnGas   = 0;
 nAnIng   = 0;

 nSw1 = 0;
 |BUCLE BuscoPartia;
 |SI nSw1 = 1;
     aArticul = #80#2;
     |HAZ LeeArticul;
     |SI aCtaG [ "           ";
         aCta    = #0#6;
     |SINO;
         aCta    = aCtaG;
     |FINSI;

     |SI aFactu = "S";
         nGasto = #80#31;
     |SINO;
         nGastoN = #80#31;
     |FINSI;

     |HAZ GrabaAuxi;
 |FINSI;
|FPRC;

|PROCESO AlbaranC;
 aFactu   = #77#38;
 |BUCLELIN 2LinAlbaran#77;
|FPRC;

|DEFBUCLE AlbaranC;
 #77#1;
 ;
 "     ", 000000;
 "zzzzz", 999999;
 e;
 NULL, AlbaranC;
|FIN;

||*********************************************************************

|PROCESO LeeExped;
 |DDEFECTO #1;
 #1#0 = #999#0;
 #1#1 = #999#1;
 |LEE 001#1=;
|FPRC;

|PROCESO LeeAuxiliar;

 |SI nSw1 = 0;
     aSerie  = #999#0;
     nNumero = #999#1;
     nSw1 = 1;
     |HAZ LeeExped;
     enSwInf = 1; |PRINT;
 |FINSI;

 |SI aSerie ! #999#0; |O nNumero ! #999#1;
     enSwInf = 2; |PRINT;
     aSerie  = #999#0;
     nNumero = #999#1;
     |HAZ LeeExped;
     enSwInf = 1; |PRINT;
 |FINSI;

 enSwInf = 0;
 |PRINT;
|FPRC;

|DEFBUCLE LeeAuxiliar;
 #999#1;
 ;
 #0#0, #0#2, #0#4;
 #0#1, #0#3, #0#5;
 ;
 NULL, LeeAuxiliar;
|FIN;

|PROCESO Imprimir;
 |ATRI R;
 |PINTA 2221, " Imprimiendo ...   ";
 |ATRI 0;

 |INFOR "gomay001";
 |SI FSalida ! 0;
     |MENAV "    Error. No existe el Informe ";
     |ACABA;
 |FINSI;

 |ABRE #1;
 nSw1 = 0;
 |BUCLE LeeAuxiliar;
 |SI nSw1 ! 0;
     enSwInf = 2; |PRINT;
     |PIEINF;
 |FINSI;

 |CIERRA #1;
 |FININF;
|FPRC;

||*********************************************************************
||*********************************************************************
||*********************************************************************
|CALCULO ElInforme; |TIPO 3;
  |IMPRE 0;
  |SI FSalida ! 0; |ACABA; |FINSI;

  |ATRI N;
  |D_CUADRO 2120, 2340;
  |ATRI R;
  |PINTA 2221, " Grabando temporal ";
  |ATRI 0;

  aFichero = ("1w" + Usuario) % 108;
  |NOME_DAT #999 aFichero;

  |ABRE #999; |CIERRA #999; |DELINDEX #999;

  nSwAlguno = 0;

  |ABRE #999;
  |BUCLE AlbaranC;
  |BUCLE Certificado;
  |SI #0#10 = "S";
      |BUCLE SalAntes;
  |FINSI;
  |CIERRA #999;
  |SI nSwAlguno = 1;
      |HAZ Imprimir;
  |FINSI;

  |FINIMP;
  |DELINDEX #999;
|FCAL;

|PROCESO Tipo13; |TIPO 13;
     |ABRE #100; |LEE 000#100p; |CIERRA #100;
     |SI #100#141 = "O";
          |ATRI R; |PINTA 809, " Serie O.T.          "; |ATRI 0;
          |ATRI R; |PINTA 1009, " Numero O.T.         "; |ATRI 0;
     |FINSI;
|FPRC;
