|FICHEROS;
     congz003 #0;
     cong0001 #1;
     cong0002 #2;
     cong0003 #3;
     cong0004 #4;
     cong0005 #5;
     cong0007 #7;   ||Tmp
     agifa010 #10;
     agifa019 #19;
     agifa024 #24;
|FIN;

|VARIABLES;
     &Tempo = "";
     aTmp7 = "";
     aSistema = "";
     &enError = 0;
     &eaFichero = "";
     &eaPath = "";
     i = 0;
     j = 0;
     f = @;
     fHoy = @;
     aMes = "";
     aFicDir = "";
     aFic = "";
     aHora = "";
     aAlfa = "";
     nNume = 0;
     fDFecha = @;
     fHFecha = @;
     aNomFic = "";
     nHandle = 0;
     aValor = "";
     aFin = "";
     aDes = "";
     aLon = "";
     aCar = "";
     nPos = 0;
     nError = 0;
     aDato = "";
     aReg = "";
     aLetra = "";
     aSepara = "";
     nCampo = 0;
     {99}Valor = "";
     nReg = 0;
     nNumLin = 0;
     aAlfa1 = "";
     aAlfa2 = "";
     aNom = "";
|FIN;

|PROCESO Punto2Comas;
     aAlfa = "";
     aLon = aValor % A0;
     |PARA j = 1; |SI j [ aLon; |HACIENDO j = j + 1;
          nPos = j * 100 + 1;
          aCar = aValor % nPos;
          |SI aCar = "."; aCar = ","; |FINSI;
          aAlfa = aAlfa + aCar;
     |SIGUE;
     aValor = aAlfa;
|FPRC;

|PROCESO Tgz;
     aNom = aNomFic - ".txt";

     aAlfa1 = eaPath +  aNom + ".tar";
     |FBORRA aAlfa1;
     aAlfa1 = eaPath +  aNom + ".tgz";
     |FBORRA aAlfa1;

     aAlfa1 = eaPath + aNom + ".tar " + aNomFic;
     aAlfa2 = eaPath;
     |ATAR aAlfa1, aAlfa2;

     aAlfa1 = eaPath + aNom + ".tar";
     |COMPRIME aAlfa1;

     aNomFic = aNom + ".tgz";
|FPRC;

|PROCESO MueveE;
     |QUE_SISTEMA aSistema;
     |DFICE aAlfa;
     |SI aSistema = "ESDOS";
          aAlfa = aAlfa + "europastry\entregas\"
     |SINO;
          aAlfa = aAlfa + "europastry/entregas/"
     |FINSI;
     |MKDIR aAlfa;
     aDes = aAlfa + aNomFic;
     aAlfa = eaPath + aNomFic;
     |COPIA_FICHERO aAlfa, aDes;
|FPRC;

|PROCESO HistoricoE;
     |ABRE #5;
     #5#0 = "E";
     #5#1 = aNomFic;
     #5#2 = fHoy;
     #5#3 = aHora;
     |SI enError = 0;
          #5#4 = "OK";
     |SINO;
          #5#4 = "ERROR";
     |FINSI;
     #5#5 = nNumLin;
     |GRABA 020#5n;
     |CIERRA #5;
|FPRC;

|PROCESO cong0007;
     |SI #7#3 ! 0;
          #24#0 = #7#1;
          |LEE 000#24=;
          |SI FSalida = 0; |Y #24#111 = "N"; |ACABA; |FINSI;

          #10#52 = #7#41;
          #10#0 = #7#42;
          |LEE 000#10=;
          |SI FSalida ! 0; |DDEFECTO #10; |FINSI;
          |SI #10#51 = "N";
               #7#7 = #7#3; ||Num Fac = Num Alb
               #7#8 = #7#6; ||Fec Fac = Fec Alb
          |FINSI;

          |PARA i = 0; |SI i [ 37; |HACIENDO i = i + 1;
               aAlfa = "|C" + (("000" + i) % -103) + "TIPO";
               aAlfa = #7#aAlfa#;

               |SI aAlfa = "F";
                    |SI #7i = "01.01.0000";
                         aValor = "";
                    |SINO;
                         aValor = #7i;
                         aValor = (aValor % 102) + "/" + (aValor % 402) + "/" + (aValor % 704);
                    |FINSI;
               |SINO;
                    aValor = #7i;
                    |SI aAlfa = "A";
                         |QBF aValor;
                    |SINO;
                         |SI #7i = 0;
                              aValor = "";
                         |SINO;
                              |SI i = 24; |O i = 28; |O i = 29; ||importes y cantidad
                                   |SI #7i > 0;
                                        |HAZ Punto2Comas;
                                        |SI i = 24;         ||cantidad
                                             aValor = "+" + aValor;
                                        |FINSI;
                                   |FINSI;
                              |FINSI;
                         |FINSI;
                    |FINSI;
               |FINSI;
               aValor = aValor + ";";
               |XGRABA nHandle, aValor;
          |SIGUE;
          aValor = &13 + &10;
          |XGRABA nHandle, aValor;
          |XGRABA nHandle, aFin;
          nNumLin = nNumLin + 1;
     |FINSI;
|FPRC;

|DEFBUCLE cong0007;
     #7#2;
     ;
     "     ", 000000, 000, "01.01.0000", fDFecha, 0000001;
     "zzzzz", 999999, 999, "31.12.9999", fHFecha, 9999999;
     ;
     NULL, cong0007;
|FIN;

|PROCESO Dias45Menos;
     f = fHoy;
     |PARA i = 1; |SI i [ 45; |HACIENDO i = i + 1;
          nNume = f;
          nNume = nNume - 1;
          f = nNume;
     |SIGUE;
     fDFecha = f;
|FPRC;

|PROCESO Agrupa;
     |SI #2#3 ! 0;
          |DDEFECTO #7;
          #7#41 = #2#41;
          #7#42 = #2#42;
          #7#22 = #2#22;
          #7#27 = #2#27;
          |LEE 101#7=;
          |SI FSalida ! 0; |GRABA 020#7b; |FINSI;
          |PARA i = 0; |SI i [ 21; |HACIENDO i = i + 1;
               #7i = #2i;
          |SIGUE;
          #7#23 = #2#23;
          #7#24 = #7#24 + #2#24;
          #7#25 = #2#25;
          #7#26 = #2#26;
          #7#28 = #7#28 + #2#28;
          #7#29 = #7#29 + #2#29;
          |PARA i = 30; |SI i [ 40; |HACIENDO i = i + 1;
               #7i = #2i;
          |SIGUE;
          #7#43 = #2#43;
          #7#44 = #2#44;
          #7#45 = #2#45;
          #7#46 = #2#46;
          #7#47 = #2#47;
          #7#48 = #2#48;
          |GRABA 020#7a;
          |LIBERA #7;
     |FINSI;
|FPRC;

|DEFBUCLE Agrupa;
     #2#5;
     ;
     fDFecha, 0000001;
     fHFecha, 9999999;
     ;
     NULL, Agrupa;
|FIN;

|PROCESO Entregas;
     |HAZ Path;
     aNomFic = #1#1 + "_";
     aNomFic = aNomFic + (fHoy % 102) + (fHoy % 402) + (fHoy % 704);
     aNomFic = aNomFic + (aHora % 102) + (aHora % 402) + (aHora % 702)+".txt";
     aFic = eaPath + aNomFic;
     |XABRE "BW", aFic, nHandle;
     |SI FSalida ] 0;

          nNumLin = 0;
          |HAZ Dias45Menos;
          fHFecha = fHoy;

          |HAZ CreaTempo; |NOME_DAT #7 Tempo; aTmp7 = Tempo; |DELINDEX #7;
          |ABRE #7;
          |BUCLE Agrupa;
          |CIERRA #7;

          |ABRE #24;
          |ABRE #10;
          |BUCLE cong0007;
          |CIERRA #10;
          |CIERRA #24;

          Tempo = aTmp7; |HAZ BoraTempo; |DELINDEX #7;


          |XCIERRA nHandle;
          |SI nNumLin > 0;
               /* |HAZ Tgz; */
               eaFichero = aNomFic;
               |HAZ EnviaFicheroE;
               |HAZ HistoricoE;
               |HAZ MueveE;
          |FINSI;
     |FINSI;
     |HAZ RmDir;
|FPRC;

|PROCESO SacaNombre;
     aLon = aFic % A0;
     aNomFic = "";
     |PARA i = 1; |SI i [ aLon; |HACIENDO i = i + 1;
          nPos = -(i * 100 + 1)
          aCar = aFic % nPos;
          |SI aCar = "\"; |O aCar = "/"; |SAL; |FINSI;
          aNomFic = aCar + aNomFic;
     |SIGUE;
|FPRC;

|PROCESO MueveP;
     |DFICE aAlfa;
     aAlfa = aAlfa + "europastry\pedidos\"
     |MKDIR aAlfa;
     aAlfa1 = aNomFic; |QBT aAlfa1;
     aDes = aAlfa + aAlfa1;
     |COPIA_FICHERO aFicDir, aDes;
|FPRC;

|PROCESO HistoricoP;
     |HAZ SacaNombre;
     aAlfa1 = aNomFic; |QBT aAlfa1;
     #5#0 = "P";
     #5#1 = aAlfa1;
     #5#2 = fHoy;
     #5#3 = aHora;
     |GRABA 000#5b;
     nError = FSalida;
|FPRC;

|PROCESO Dato;
     aSepara = ";"
     nCampo = 1;
     IValor = Valor1<-;
     aLon = aDato % A0;
     aReg = "";
     |PARA i = 1; |SI i [ aLon; |HACIENDO i = i + 1;
          nPos = i * 100 + 1;
          aLetra = aDato % nPos;
          |SI aLetra = aSepara;
               Valor = aReg;
               aReg = "";
               nCampo = nCampo + 1;
               IValor = IValor + 1;
          |SINO;
               aReg = aReg + aLetra;
          |FINSI;
          |SI nCampo > 90; |SAL; |FINSI;     || Por si.... :)
     |SIGUE;
     Valor = aReg;
|FPRC;

|PROCESO ImportaFic;
/*
     aAlfa = aFic % -103; aAlfa1 = aAlfa > "";
     |SI aAlfa1 = "TGZ";
          |DESCOMPRIME aFic;
          |DETAR aFic, eaPath;
          aAlfa2 = aFic - aAlfa;
          aFic = aAlfa2 + "txt";
     |FINSI;
*/
     nNumLin = 0;
     |ABRE #3;
     |ABRE #4;
     |XABRE "", aFic, nHandle;
     |SI FSalida ] 0;
          |XLEE nHandle, 250, aDato;
          |PARA; |SI FSalida ] 0; |HACIENDO;
               |HAZ Dato;
               |LEE 000#3u;
               |SI FSalida = 0;
                    nReg = #3#18 + 1;
               |SINO;
                    nReg = 1;
               |FINSI;
               IValor = Valor1<-;
               |DDEFECTO #3;
               |PARA i = 0; |SI i [ 17; |HACIENDO i = i + 1;
                    #3i = Valor;
                    IValor = IValor + 1;
               |SIGUE;
               #3#18 = nReg;
               #3#19 = "N";
               #3#20 = aNomFic;
               |GRABA 020#3n;

               nNumLin = nNumLin + 1;
               |XLEE nHandle, 250, aDato;
          |SIGUE;
          |XCIERRA nHandle;
     |SINO;
          nError = 1;
     |FINSI;
     |CIERRA #4;
     |CIERRA #3;
|FPRC;

|PROCESO Pedidos;
     |HAZ Path;
     |HAZ ReciveFicheros;
     |ABRE #5;
     aFicDir = eaPath;
     |_PDIR aFicDir;
     |PARA; |SI FSalida = 0; |HACIENDO;
          aFic = aFicDir;
          aAlfa = aFic - "sftp";
          |SI FSalida [ 0;
               |HAZ HistoricoP;
               |SI nError = 0;
                    |HAZ ImportaFic;
                    |SI nError = 0; #5#4 = "OK"; |SINO; #5#4 = "ERROR"; |FINSI;
                    #5#5 = nNumLin;
                    |GRABA 020#5a;
                    |HAZ MueveP;
/*
                    |SI aAlfa1 = "TGZ";
                         aAlfa1 = aAlfa2 + "tar";
                         |FBORRA aAlfa1;
                         aAlfa1 = aAlfa2 + "tgz";
                         |FBORRA aAlfa1;
                    |FINSI;
*/
               |FINSI;
               |LIBERA #5;
          |FINSI;
          |_SDIR aFicDir;
     |SIGUE;
     |CIERRA #5;
     |HAZ RmDir;
|FPRC;

|PROCESO MueveS;
     |DFICE aAlfa;
     aAlfa = aAlfa + "europastry\seguimiento\"
     |MKDIR aAlfa;
     aDes = aAlfa + aNomFic;
     aAlfa = eaPath + aNomFic;
     |COPIA_FICHERO aAlfa, aDes;
|FPRC;

|PROCESO HistoricoS;
     |ABRE #5;
     #5#0 = "S";
     #5#1 = aNomFic;
     #5#2 = fHoy;
     #5#3 = aHora;
     |SI enError = 0;
          #5#4 = "OK";
     |SINO;
          #5#4 = "ERROR";
     |FINSI;
     #5#5 = nNumLin;
     |GRABA 020#5n;
     |CIERRA #5;
|FPRC;

|PROCESO cong0004;
     |PARA i = 0; |SI i [ 11; |HACIENDO i = i + 1;
          aAlfa = "|C" + (("000" + i) % -103) + "TIPO";
          aAlfa = #4#aAlfa#;
          |SI aAlfa = "F";
               |SI #4i = "01.01.0000";
                    aValor = "";
               |SINO;
                    aValor = #4i;
                    aValor = (aValor % 102) + "/" + (aValor % 402) + "/" + (aValor % 704);
               |FINSI;
          |SINO;
               aValor = #4i;
               |SI aAlfa = "A";
                    |QBF aValor;
               |SINO;
                    |SI #4i = 0;
                         aValor = "";
                    |SINO;
                         |SI i = 8; |Y  #4i > 0;  || Cantidad
                              |HAZ Punto2Comas;
                              aValor = "+" + aValor;
                         |FINSI;
                    |FINSI;
               |FINSI;
          |FINSI;
          aValor = aValor + ";";
          |XGRABA nHandle, aValor;
     |SIGUE;
     aValor = &13 + &10;
     |XGRABA nHandle, aValor;
     |XGRABA nHandle, aFin;
     nNumLin = nNumLin + 1;
|FPRC;

|DEFBUCLE cong0004;
     #4#2;
     ;
     fDFecha, 0000001;
     fHFecha, 9999999;
     ;
     NULL, cong0004;
|FIN;

|PROCESO Seguimiento;
     |HAZ Path;
     aNomFic = #1#1 + "_";
     aNomFic = aNomFic + (fHoy % 102) + (fHoy % 402) + (fHoy % 704);
     aNomFic = aNomFic + (aHora % 102) + (aHora % 402) + (aHora % 702)+".txt";
     aFic = eaPath + aNomFic;
     |XABRE "BW", aFic, nHandle;
     |SI FSalida ] 0;
          nNumLin = 0;
          nNume = fHoy - 2;
          fDFecha = nNume;
          fHFecha = fHoy;
          |BUCLE cong0004;
          |XCIERRA nHandle;
          |SI nNumLin > 0;
               /* |HAZ Tgz; */
               eaFichero = aNomFic;
               |HAZ EnviaFicheroS;
               |HAZ HistoricoS;
               |HAZ MueveS;
          |FINSI;
     |FINSI;
     |HAZ RmDir;
|FPRC;

|PROCESO T3; |TIPO 3;
     fHoy = ~;
     |HORA aHora;
     |ABRE #1; |LEE 000#1p; |CIERRA #1;

     |SI #0#0 = "S";
          |HAZ Entregas;      ||Exporta
     |FINSI;
     |SI #0#1 = "S";
          aAlfa = #1#13; |QBF aAlfa;
          |SI aAlfa ! "";
               |HAZ Pedidos;       ||Importa
          |FINSI;
          |HAZ CreaPedidos;
     |FINSI;
     |SI #0#2 = "S";
          |HAZ Seguimiento;   ||Exporta
     |FINSI;
|FPRC;

|PROGRAMA;
     |SI PARAMETRO = "menu";
          |MANTE #0;
     |SINO;
          |SI PARAMETRO = "entregas"; #0#0 = "S"; |FINSI;
          |SI PARAMETRO = "pedidos"; #0#1 = "S"; |FINSI;
          |SI PARAMETRO = "seguimiento"; #0#2 = "S"; |FINSI;
          |SI PARAMETRO = "";
               #0#0 = "S";
               #0#1 = "S";
               #0#2 = "S";
          |FINSI;
          |HAZ T3;
     |FINSI;
|FPRO;
