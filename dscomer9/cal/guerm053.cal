|FICHEROS;
     guerm053 #0;
     guerm054 #1,MANTE;
     gotrabaj #2;
     gopartia #3;
     goptrbli #4;
     gopartli #5;
     gotipoho #6;
     gopartca #7;
     guerm017 #17;
     guerm018 #18;
     dsw00080 #80; ||Tmp para recalcular

     gotmpnma #100;
     agifa007 #101;
     agifa142 #142;
     canempr@ #1000;
     caenlac@ #1001;
     caconas@ #1002;
     camasic@ #1003;
|FIN;

|VARIABLES;
     aRecalcula = "N";
     nError = 0;
     &efFecha = @;
     &enTraba = 0;
     &Tempo = "";
     nImpAntes = 0;
  {-1}aMenu = "";
     aMenu1 = "2400";
     aMenu2 = "1";
     aMenu3 = "Opciones:  ";
     aMenu4 = "IARC";
     aMenu5 = " Importar ";
     aMenu6 = " Actualizar ";
     aMenu7 = " Recalcular";
     aMenu8 = " Contabilizar";
     nModo = 0;
     nForma = 0;
     aAlfa = "";
     aAlfb = "";
     aAlfc = "";
     aAlfd = "";
     aAlfl = "";
     aAlfo = "";
     aAlfp = "";
     aAlfq = "";
     nDeven = 0;
     nSegur = 0;
     nCoste = 0;
     aRuta = "";
     nSal = 0;
     nXls = 0;
     aDFecha = "";
     aHFecha = "";
     fFecha = @;
     aMes = "";
     aNom = "";
     aLon = "";
     i = 0;
     nPos = 0;

     aAlfa1    = "";
     aAlfa2    = "";
     nCalc     = 0;
     nSw       = 0;
     nSwError  = 0;
     nContador = 0;
     nLinea    = 0;
     nLineasA  = 0;
     nLineasB  = 0;
     nTotal    = 0;
     fDFecha   = @;
     fHFecha   = @;

     &PRMNT_enError = 0;
|FIN;

|PROCESO Tipo3; |TIPO 3;
     |SI #0#5 = "N";
          |CONFI "0000N¿Recalcular totales?";
          |SI FSalida = 0; |HAZ CalculaLin; |FINSI;
     |SINO;
          |SI aRecalcula = "S";
               aRecalcula = "N";
               |MENAV "0000PROCESO MENSUAL ACUALIZADO. SE VOLVERA A ACTUALIZAR";
               |HAZ CalculaLin;
               |HAZ LlamaAct;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Tipo5; |TIPO 5;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 1;
          |HAZ Importa;
     |FINSI;
|FPRC;

|PROCESO Recalcula;
     #0#2 = #0#2 + #1#4;
     #0#3 = #0#3 + #1#5;
     #0#4 = #0#4 + #1#6;
     #0#6 = #0#6 + #1#9;
     #0#7 = #0#7 + #1#7;
     #0#8 = #0#9 / #0#7;
     #0#9 = #0#4 + #0#6;

     #1#8 = #1#6 / #1#7;
     #1#10 = #1#9 / #1#7;
     #1#11 = #1#8 + #1#10;
     #1#13 = #1#6 + #1#9;
     |GRABA 020#1a;
     |LIBERA #1;
|FPRC;

|DEFBUCLE Recalcula;
     #1#1;
     ;
     #0#0, #0#1, 0;
     #0#0, #0#1, 99999;
     be;
     NULL, Recalcula;
|FIN;

|PROCESO Horas;
     #4#20 = #3#0;
     #4#21 = #3#1;
     #4#2 = #3#28;
     #4#3 = #3#6;
     |LEE 000#4=;
     |SI FSalida ! 0; |ACABA; |FINSI;

     #1#0 = #0#0;
     #1#1 = #0#1;
     #1#2 = #4#4;
     |LEE 101#1=;
     |SI FSalida ! 0; |ACABA; |FINSI;

     #2#0 = #1#2;
     |LEE 000#2=;
     |SI #2#128 ! "N";  || computa Dietas
          #6#0 = #4#26;
          |LEE 000#6=;
          |SI FSalida = 0; |Y #6#12 = "S";
               #1#7 = #1#7 + #3#10;
          |SINO;
               #1#12 = #1#12 + #3#10;
          |FINSI;
          |GRABA 020#1a;
     |FINSI;
     |LIBERA #1;
|FPRC;

|DEFBUCLE Horas;
     #3#1;
     28;
     "T", "     ", 000001, 001, 001, aDFecha;
     "T", "zzzzz", 999999, 999, 999, aHFecha;
     ;
     NULL, Horas;
|FIN;

|PROCESO DietasComputas;
     #2#0 = #1#2;
     |LEE 000#2=;
     |SI #2#128 = "N";    ||  computa Dietas
          #1#7 = #2#129;
          |GRABA 020#1a;
     |FINSI;
     |LIBERA #1;
|FPRC;

|DEFBUCLE DietasComputas;
     #1#1;
     ;
     #0#0, #0#1, 00001;
     #0#0, #0#1, 99999;
     ;
     NULL, DietasComputas;
|FIN;

|PROCESO AceroHoras;
     #1#7 = 0;
     #1#12 = 0;
     |GRABA 020#1a;
     |LIBERA #1;
|FPRC;

|DEFBUCLE AceroHoras;
     #1#1;
     ;
     #0#0, #0#1, 0;
     #0#0, #0#1, 99999;
     be;
     NULL, AceroHoras;
|FIN;

|PROCESO CalculaLin;
     aAlfa = ("0000" + #0#0) % -104;
     aDFecha = ("00" + #0#1) % -102;
     aDFecha = "01." + aDFecha + "." + aAlfa;
     |PARA fFecha = aDFecha; |SI; |HACIENDO fFecha = fFecha + 1;
          aMes = fFecha % 402;
          |SI aMes ! #0#1; |SAL; |FINSI;
          aHFecha = fFecha;
     |SIGUE;
     |BUCLE AceroHoras;
     |ABRE #1;
     |ABRE #2;
     |ABRE #4;
     |ABRE #5;
     |ABRE #6;
     |BUCLE Horas;
     |BUCLE DietasComputas;
     |CIERRA #1;
     |CIERRA #2;
     |CIERRA #4;
     |CIERRA #5;
     |CIERRA #6;
     #0#2 = 0;
     #0#3 = 0;
     #0#4 = 0;
     #0#6 = 0;
     #0#7 = 0;
     |BUCLE Recalcula;
     |PINTA #0#2;
     |PINTA #0#3;
     |PINTA #0#4;
     |PINTA #0#6;
     |PINTA #0#7;
|FPRC;

|PROCESO Min;
     #1#0 = #0#0;
     #1#1 = #0#1;
     #1#2 = 0;
|FPRC;

|PROCESO Max;
     #1#0 = #0#0;
     #1#1 = #0#1;
     #1#2 = 99999;
|FPRC;

|PROCESO BorraLin;
     |BORRA 020#1a;
|FPRC;

|DEFBUCLE BorraLin;
     #1#1;
     ;
     #0#0, #0#1, 0;
     #0#0, #0#1, 99999;
     be;
     NULL, BorraLin;
|FIN;

|PROCESO Tipo2Cab; |TIPO 2;
     nModo = (FEntrada / 100) ! 0;
     nForma = 0 +. 1;
     |SI nError = 1;
          nError = 0;
          |ERROR; |ACABA;
     |FINSI;
     |SI #0#5 = "S";
          |SI nForma = -1;
               |CONFI "0000PROCESO MENSUAL TOTALMENTE ACTUALIZADO. CONTINUAR?";
               |SI FSalida ! 0;
                    nError = 1;
                    |ERROR; |ACABA;
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Tipo1; |TIPO 1;
     nError = 0;
|FPRC;

|PROCESO Tipo2; |TIPO 2;
     nModo = (FEntrada / 100) ! 0;
     nForma = 0 +. 1;
     |SI nError = 1;
          nError = 0;
          |ERROR; |ACABA;
     |FINSI;
     |SI #0#5 = "S";
          |SI nForma = -1;
               |CONFI "0000NTRABAJADOR YA ACTUALIZADO. CONTINUAR?";
               |SI FSalida ! 0; nError = 1; |ERROR; |ACABA; |FINSI;
               |MENAV "0000SE VOLVERAN A CALCULAR EL PERIDO";
               aRecalcula = "S";
          |FINSI;
     |FINSI;

     #0#2 = #0#2 +. #1#4; |PINTA #0#2;
     #0#3 = #0#3 +. #1#5; |PINTA #0#3;
     #0#4 = #0#4 +. #1#6; |PINTA #0#4;
     #0#6 = #0#6 +. #1#9; |PINTA #0#6;
     #0#7 = #0#7 +. #1#7; |PINTA #0#7;
     #0#8 = #0#6 / #0#7; |PINTA #0#8;
|FPRC;

|PROCESO Tipo20; |TIPO 20;
     |SI FSalida = "OPCION";
          FSalida = "Opciones";
     |SINO;
          |LEE 101#0=;
          |SI FSalida ! 0; |ACABA; |FINSI;
          |MENU aMenu;
          |SI #0#5 = "S"; |Y FSalida ! 4;
               |MENAV "0000Ya esta actualizado...";
          |SINO;
               |SI FSalida = 1;
                    |HAZ Importa;
                    |ENTLINEAL #1#1, 1, 7, 22, 0, Min, Max;
               |FINSI;
               |SI FSalida = 2; |HAZ Actualiza; |FINSI;
               |SI FSalida = 3; |HAZ CalculaLin; |FINSI;
               |SI FSalida = 4; |HAZ Contabiliza; |FINSI;
               |GRABA 020#0a;
          |FINSI;
          |LIBERA #0;
     |FINSI;
|FPRC;

|PROCESO Trabajador;
     |DDEFECTO #2;
     |ABRE #2;
     #2#0 = aAlfb;
     |LEE 000#2=;
     |SI FSalida ! 0;
          |MENAV "0000No existe Trabajador...";
          |PUSHV 0501 2380;
          #2#1 = aAlfc;
          |PINPA #0#2;
          |PINDA #0#2;
          |PARA FSalida = 1; |SI FSalida ! 0; |HACIENDO;
               |ENDATOS #1#2;
               |SI FSalida ! 0;
                    |CONFI "0000NAbandonar la importacion?";
                    |SI FSalida = 0; nSal = 1; |SAL; |FINSI;
               |FINSI;
          |SIGUE;
          |SI nSal = 0; |GRABA 020#2n; |FINSI;
          |POPV;
     |FINSI;
     |CIERRA #2;
|FPRC;

|PROCESO Registro;
     |QBT aAlfb;
     aAlfb = ("00000" + aAlfb) % -106;
     |HAZ Trabajador; |SI nSal = 1; |ACABA; |FINSI;
     |DDEFECTO #1;
     #1#0 = #0#0;
     #1#1 = #0#1;
     #1#2 = aAlfb;
     |LEE 101#1=;
     |SI FSalida ! 0; |GRABA 020#1b; |FINSI;
     #1#3 = aAlfc;
     #1#4 = aAlfd;
     #1#5 = aAlfl;
     #1#6 = aAlfo;
     #1#9 = aAlfp;
     #1#14 = aAlfq;
     #1#6 = #1#6 + #1#14;
     #1#13 = #1#6 + #1#9;
     |GRABA 020#1a;
     |LIBERA #1;
|FPRC;

|PROCESO Importa;
     |ABRE #1;
     #1#0 = #0#0;
     #1#1 = #0#1;
     #1#2 = 1;
     |LEE 000#1];
     |SI FSalida = 0; |Y #1#0 = #0#0; |Y #1#1 = #0#1;
          |CONFI "0000NExisten lineas en este periodo. ¿Desea actualizarlas?";
          |SI FSalida ! 0; |ACABA; |FINSI;
     |FINSI;

     aRuta = "F*.xls";
     |BROWSE aRuta;
     aRuta = aRuta % 2; |QBF aRuta;
     |SI aRuta = "F"; |ACABA; |FINSI;

     aLon = aRuta % A0;
     aNom = "";
     |PARA i = aLon; |SI i ] 1; |HACIENDO i = i - 1;
          nPos = i * 100 + 1;
          aAlfa = aRuta % nPos;
          |SI aAlfa = "\"; |SAL; |FINSI;
          aNom = aAlfa + aNom;
     |SIGUE;

     aAlfa = "0000S¿Importar fichero " + &13 + aNom + "?";
     |CONFI aAlfa;
     |SI FSalida ! 0; |ACABA; |FINSI;

     |XABRE "CE", aRuta, 0;
     |SI FSalida < 0
          |MENAV "0000No puedo abrir el fichero a importar";
     |SINO;
          |CARGADLL "agixl97.dll";
          |SI FSalida < 0;
               |MENAV "0000No se puede usar agixl97.dll";
          |SINO;
               |ALFACALLDLL "agixl97.dll", "carga_book", aRuta;
               |ALFACALLDLL "agixl97.dll", "selecciona_hoja", "Hoja1";
               |SI FSalida = 0;
                    |PARA nXls = 9; |SI nXls < 65536; |HACIENDO nXls = nXls + 1;
                         aAlfa = "Importando:" + nXls;
                         |PINTA 2303, aAlfa;
                         aAlfb = "B" + nXls; |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfb;
                         |SI FSalida ! 0; |SAL; |FINSI;
                         aAlfc = "C" + nXls; |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfc;
                         |SI FSalida ! 0; |SAL; |FINSI;
                         aAlfd = "D" + nXls; |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfd;
                         |SI FSalida ! 0; |SAL; |FINSI;
                         aAlfl = "L" + nXls; |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfl;
                         |SI FSalida ! 0; |SAL; |FINSI;
                         aAlfo = "O" + nXls; |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfo;
                         |SI FSalida ! 0; |SAL; |FINSI;
                         aAlfp = "P" + nXls; |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfp;
                         |SI FSalida ! 0; |SAL; |FINSI;
                         aAlfq = "Q" + nXls; |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfq;
                         |SI FSalida ! 0; |SAL; |FINSI;

                         aAlfa = aAlfb; |QBT aAlfa;
                         |SI nXls > 9; || el primer registro puede venir en blanco
                              |SI aAlfa = ""; |SAL; |FINSI;
                         |FINSI;

                         |SI aAlfa ! "";
                              aAlfa = "0000Procesando " + aAlfa; |MENSA aAlfa;
                              |HAZ Registro;
                              |SI nSal = 1; |SAL; |FINSI;
                         |FINSI;
                    |SIGUE;
                    |ALFACALLDLL "agixl97.dll", "fin_book", "salva";
               |FINSI;
          |FINSI;
          |DESCARGADLL "agixl97.dll";
     |FINSI;
     |CIERRA #1;

     |BLIN 23;
     |MENSA "0000Calculando totales...";
     |HAZ CalculaLin;
     |MENSA "0000.";
|FPRC;

|PROCESO Actualiza;
     |CONFI "0000N¿Actualizar los partes de trabajo de este periodo con el precio de coste calculado?";
     |SI FSalida ! 0; |ACABA; |FINSI;
     |HAZ LlamaAct;
|FPRC;

|PROCESO LlamaAct;
     aAlfa = "01." + (("00" + #0#1) % -102) + "." + (("0000" + #0#0) % -104);
     efFecha = aAlfa;
     enTraba = 0;
     |SALVA_DATOS #0;
     |CIERRAT #0;
     |SUB_EJECUTA_NP "guerm055";
     |ABRET #0;
     |REPON_DATOS #0;
     |LEE 121#0=;
     #0#5 = "S"; |PINTA #0#5;
|FPRC;

|PROCESO LineasPartes;
   #agifa007#26 = #goptrbli#0;
   |LEE 000#agifa007.=;
   |SI FSalida ! 0; |ACABA; |FINSI;

   #gotipoho#0 = #goptrbli#26;
   |LEE 000#gotipoho.=;
   |SI FSalida ! 0; |O #gotipoho#12 ! "S"; |ACABA; |FINSI;

   #gotmpnma#0 = #agifa007#43;
   |LEE 101#gotmpnma.=;
   |SI FSalida ! 0;
      |DDEFECTO #gotmpnma;
      #gotmpnma#0 = #agifa007#43;
      |GRABA 020#gotmpnma.b;
   |FINSI;
   #gotmpnma#1 = #gotmpnma#1 + ((#guerm054#4 * #goptrbli#8) / #guerm054#7);
   #gotmpnma#2 = #gotmpnma#2 + ((#guerm054#5 * #goptrbli#8) / #guerm054#7);
   |GRABA 020#gotmpnma.a; |LIBERA #gotmpnma;
|FPRC;

|DEFBUCLE LineasPartes;
#goptrbli#1;
4;
"     ", 000001, fDFecha, 0001, #guerm054#2;
"zzzzz", 999999, fHFecha, 9999, #guerm054#2;
;
NULL, LineasPartes;
|FIN;

|PROCESO LineasCoste;
   |BUCLE LineasPartes;
|FPRC;

|DEFBUCLE LineasCoste;
#guerm054#1;
;
#guerm053#0, #guerm053#1, 00000;
#guerm053#0, #guerm053#1, 99999;
;
NULL, LineasCoste;
|FIN;

|PROCESO Contador;
   |LEE 101#caconas@.p;
   |SI FSalida ! 0;
      |DDEFECTO #caconas@;
      |GRABA 020#caconas@.n;
      |LEE 101#caconas@.p;
   |FINSI;

   #caconas@#1 = #caconas@#1 + 1;
   aAlfa = "|NC"; nCalc = #caconas@#aAlfa#;
   |SI nCalc > 2; #caconas@#2 = #caconas@#2 + 1; |FINSI;
   nContador = #caconas@#1;

   |GRABA 020#caconas@.a;
   |LIBERA #caconas@;

   #camasic@#0 = #caenlac@#0;
   #camasic@#1 = #caenlac@#1;
   #camasic@#2 = nContador;
   |LEE 000#camasic@.=;
   |SI FSalida = 0;
      #camasic@#0 = #caenlac@#0;
      #camasic@#1 = #caenlac@#1;
      #camasic@#2 = 999999;
      |LEE 000#camasic@.];
      |SI FSalida ! 0;
         |LEE 000#camasic@.u;
      |SINO;
         |LEE 000#camasic@.a;
      |FINSI;
      |SI FSalida = 0; |Y #camasic@#0 = #caenlac@#0; |Y #camasic@#1 = #caenlac@#1;
         nContador = #camasic@#2 + 1;
      |SINO;
         nContador = -1;
      |FINSI;
   |FINSI;
|FPRC;

|PROCESO LineasAnaliticas;
   |DDEFECTO #caenlac@;

   #caenlac@#0 = 1;  || Diario
   #caenlac@#1 = fHFecha;
   #caenlac@#2 = nContador;
   #caenlac@#3 = nLinea; nLinea = nLinea + 10;
   #caenlac@#5 = 5;
   #caenlac@#6 = 001;
   #caenlac@#8 = "D";
   |SI nSw = 1;
      #caenlac@#4 = "640000000";
      aAlfa1 = "NOMINAS " + aAlfa2 + " ";
      aAlfa1 = aAlfa1 + #guerm053#0;
      #caenlac@#7 = aAlfa1;
      nCalc = #gotmpnma#1; nCalc = nCalc ! 2;
      #caenlac@#9 = nCalc;
      nTotal = nTotal + #gotmpnma#1;
   |FINSI;
   |SI nSw = 2;
      #caenlac@#4 = "642000000";
      aAlfa1 = "SS " + aAlfa2 + " ";
      aAlfa1 = aAlfa1 + #guerm053#0;
      #caenlac@#7 = aAlfa1;
      nCalc = #gotmpnma#2; nCalc = nCalc ! 2;
      #caenlac@#9 = nCalc;
      nTotal = nTotal + nCalc;
   |FINSI;
   aAlfa = #agifa142#152; |QBF aAlfa;
   aAlfa1 = aAlfa % -299;
   #caenlac@#37 = aAlfa1;
   aAlfa1 = aAlfa % -101;
   #caenlac@#38 = aAlfa1;
   #caenlac@#39 = #gotmpnma#0;
   |GRABA 020#caenlac@.n;
|FPRC;

|DEFBUCLE LineasAnaliticas;
#gotmpnma#1;
;
INICIO;
FINAL;
;
NULL, LineasAnaliticas;
|FIN;

|PROCESO Contabiliza;
   |SI #guerm053#10 = "S";
      |MENAV "    Periodo contabilizado";
      |ERROR; |ACABA;
   |FINSI;

    nSwError = 0;

    |ABRE #agifa142;
    |LEE 000#agifa142.p;
    |SI FSalida ! 0; |DDEFECTO #agifa142; |FINSI;
    |CIERRA #agifa142;

    aAlfa = #agifa142#151; |QBF aAlfa;
    |SI aAlfa ! "";
       aAlfa1 = aAlfa + "def/canempre.mas";
       |CARGA_DEF aAlfa1, canempr@;
       |SI FSalida = 0;
          |ABRE #canempr@;
          aAlfa = #agifa142#152; |QBF aAlfa;
          aAlfa1 = aAlfa % -299;
          #canempr@#2 = aAlfa1;
          aAlfa1 = aAlfa % -101;
          #canempr@#3 = aAlfa1;
          |LEE 000#canempr@.=;
          |SI FSalida = 0;
             nCalc = #canempr@#26;
             |SI nCalc ] 80;
                nCalc = nCalc + 1900;
             |SINO;
                nCalc = nCalc + 2000;
             |FINSI;
             |SI nCalc ! #guerm053#0;
                nSwError = 1;
                |MENAV "    El ejercicio de la contabilidad indicada en los parametros no coincide con el año indicado.";
                |ERROR;
             |FINSI;
          |SINO;
             nSwError = 1;
             |MENAV "     La empresa indicada en los parametros no existe.";
             |ERROR;
          |FINSI;
          |CIERRA #canempr@; |DESCARGA_DEF #canempr@;
       |SINO;
          nSwError = 1;
          |MENAV "    El path a la aplicacion de contabilidad es incorrecto";
          |ERROR;
       |FINSI;
    |SINO;
       nSwError = 1;
       |MENAV "    El path a la aplicacion de contabilidad es incorrecto";
       |ERROR;
    |FINSI;

    |SI nSwError ! 0;
       |MENAV "    Se han encontrado errores. Proceso cancelado.";
       |ACABA;
    |FINSI;

    aAlfa = "tmp" + Usuario;
    |NOME_DAT #gotmpnma#aAlfa#; |DELINDEX #gotmpnma;
    |ABRE #gotmpnma; |ABRE #agifa007; |ABRE #gotipoho;

    aAlfa = "01." + (("00" + #guerm053#1) % -102) + "." + #guerm053#0; fDFecha = aAlfa;
    nCalc = #guerm053#1 + 1; aAlfa = nCalc;
    aAlfa = "01." + (("00" + aAlfa) % -102) + "." + #guerm053#0; fHFecha = aAlfa;
    nCalc = fHFecha; nCalc = nCalc - 1; fHFecha = nCalc;
    |BUCLE LineasCoste;
    |CIERRA #agifa007; |CIERRA #gotipoho;

    aAlfa = #agifa142#151; |QBF aAlfa; aAlfa1 = aAlfa;
    aAlfa = aAlfa + "fich/" + (("000000" + #agifa142#152) % -106) + "/";
    aAlfa2 = aAlfa1 + "def/caenlace.mas";
    |CARGA_DEF aAlfa2, caenlac@;
    |SI FSalida = 0;
       aAlfa2 = aAlfa1 + "def/caconasi.mas";
       |CARGA_DEF aAlfa2, caconas@;
       aAlfa2 = aAlfa1 + "def/camaasic.mas";
       |CARGA_DEF aAlfa2, camasic@;

       |PATH_DAT #caenlac@#aAlfa#; |PATH_DAT #caconas@#aAlfa#; |PATH_DAT #camasic@#aAlfa#;
       aAlfa1 = "pu" + Usuario; |NOME_DAT #caenlac@#aAlfa1#;
       |DELINDEX #caenlac@; |ABRE #caenlac@; |ABRE #caconas@; |ABRE #camasic@;
       |HAZ Contador;
       |SI nContador < 0;
          |MENAV "    Problemas con el contador de contabilidad";
          |CIERRA #caenlac@; |DELINDEX #caenlac@;
          |CIERRA #caconas@; |CIERRA #camasic@;
          |DESCARGA_DEF #camasic@; |DESCARGA_DEF #caconas@; |DESCARGA_DEF #caenlac@;
          |ACABA;
       |FINSI;

       nCalc = #guerm053#1;
       |SI nCalc = 1;  aAlfa2 = "ENERO "; |FINSI;
       |SI nCalc = 2;  aAlfa2 = "FEBRERO "; |FINSI;
       |SI nCalc = 3;  aAlfa2 = "MARZO "; |FINSI;
       |SI nCalc = 4;  aAlfa2 = "ABRIL "; |FINSI;
       |SI nCalc = 5;  aAlfa2 = "MAYO "; |FINSI;
       |SI nCalc = 6;  aAlfa2 = "JUNIO "; |FINSI;
       |SI nCalc = 7;  aAlfa2 = "JULIO "; |FINSI;
       |SI nCalc = 8;  aAlfa2 = "AGOSTO "; |FINSI;
       |SI nCalc = 9;  aAlfa2 = "SEPTIEMBRE "; |FINSI;
       |SI nCalc = 10; aAlfa2 = "OCTUBRE "; |FINSI;
       |SI nCalc = 11; aAlfa2 = "NOVIEMBRE "; |FINSI;
       |SI nCalc = 12; aAlfa2 = "DICIEMBRE "; |FINSI;

       nLinea = 1; nTotal = 0; nSw = 1;
       |BUCLE LineasAnaliticas;
       |SI nLinea > 1;
          || Compruebo el posible descuadre y lo ajusto
          nCalc = (nTotal - #guerm053#2) ! 2;
          |SI nCalc ! 0;
             #caenlac@#0 = 1;
             #caenlac@#1 = fHFecha;
             #caenlac@#2 = nContador;
             #caenlac@#3 = 1;
             aAlfa = #agifa142#152; |QBF aAlfa;
             aAlfa1 = aAlfa % -299;
             #caenlac@#37 = aAlfa1;
             aAlfa1 = aAlfa % -101;
             #caenlac@#38 = aAlfa1;
             |LEE 000#caenlac@.=;
             |SI FSalida = 0;
                #caenlac@#9 = #caenlac@#9 - nCalc;
                |GRABA 020#caenlac@.a;
             |FINSI;
          |FINSI;

          |DDEFECTO #caenlac@;
          #caenlac@#0 = 1;  || Diario
          #caenlac@#1 = fHFecha;
          #caenlac@#2 = nContador;
          #caenlac@#3 = nLinea; nLinea = nLinea + 10;
          #caenlac@#4 = "465000000";
          #caenlac@#5 = 5;
          #caenlac@#6 = 001;
          aAlfa1 = "NOMINAS " + aAlfa2 + " ";
          aAlfa1 = aAlfa1 + #guerm053#0;
          #caenlac@#7 = aAlfa1;
          #caenlac@#8 = "H";
          #caenlac@#9 = #guerm053#2;
          aAlfa = #agifa142#152; |QBF aAlfa;
          aAlfa1 = aAlfa % -299;
          #caenlac@#37 = aAlfa1;
          aAlfa1 = aAlfa % -101;
          #caenlac@#38 = aAlfa1;
          |GRABA 020#caenlac@.n;
       |FINSI;
       nLineasA = nLinea;

       |HAZ Contador;
       nLinea = 1; nTotal = 0; nSw = 2;
       |BUCLE LineasAnaliticas;

       |SI nLinea > 1;
          || Compruebo el posible descuadre y lo ajusto
          nCalc = (nTotal - #guerm053#3) ! 2;
          |SI nCalc ! 0;
             #caenlac@#0 = 1;
             #caenlac@#1 = fHFecha;
             #caenlac@#2 = nContador;
             #caenlac@#3 = 1;
             aAlfa = #agifa142#152; |QBF aAlfa;
             aAlfa1 = aAlfa % -299;
             #caenlac@#37 = aAlfa1;
             aAlfa1 = aAlfa % -101;
             #caenlac@#38 = aAlfa1;
             |LEE 000#caenlac@.=;
             |SI FSalida = 0;
                #caenlac@#9 = #caenlac@#9 - nCalc;
                |GRABA 020#caenlac@.a;
             |FINSI;
          |FINSI;

          |DDEFECTO #caenlac@;
          #caenlac@#0 = 1;  || Diario
          #caenlac@#1 = fHFecha;
          #caenlac@#2 = nContador;
          #caenlac@#3 = nLinea; nLinea = nLinea + 10;
          #caenlac@#4 = "476000000";
          #caenlac@#5 = 5;
          #caenlac@#6 = 001;
          aAlfa1 = "SS " + aAlfa2 + " ";
          aAlfa1 = aAlfa1 + #guerm053#0;
          #caenlac@#7 = aAlfa1;
          #caenlac@#8 = "H";
          #caenlac@#9 = #guerm053#3;
          aAlfa = #agifa142#152; |QBF aAlfa;
          aAlfa1 = aAlfa % -299;
          #caenlac@#37 = aAlfa1;
          aAlfa1 = aAlfa % -101;
          #caenlac@#38 = aAlfa1;
          |GRABA 020#caenlac@.n;
       |FINSI;
       nLineasB = nLinea;

       |CIERRA #caenlac@; |CIERRA #caconas@;

       |SI nLineasA > 1; |O nLineasB > 1;
          aAlfa = ":contagen/dsapunte.tab;" + #agifa142#151; |QBF aAlfa;
          aAlfa1 = ("000000" + #agifa142#152) % -106;
          aAlfa = aAlfa + "fich/" + aAlfa1 + "/,pu" + Usuario;
          |SUB_EJECUTA_NP aAlfa;

          |SI PRMNT_enError = 0;
             #guerm053#10 = "S"; |PINTA #guerm053#10;
             |GRABA 020#guerm053.a;
          |FINSI;
       |SINO;
          |MENAV "    Sin datos";
       |FINSI;

       |CIERRA #gotmpnma;  |DELINDEX #gotmpnma;
       |DELINDEX #caenlac@;

       |DESCARGA_DEF #camasic@; |DESCARGA_DEF #caconas@; |DESCARGA_DEF #caenlac@;
    |FINSI;

|FPRC;
