     Modificar gestrz06, para recalculo escandallo

|FICHEROS;
     gestrm05 #0;
     gestrm01 #1;
     gestrm02 #2;
     gestrm03 #3;
     gestrm04 #4;
     agi00009 #9;
     agifa019 #19;
     gestrz01 #101,MANTE;  ||Pan. Costes
     gestrz02 #102,MANTE;  ||Pan. Duplica
     gestrm06 #106,MANTE;
     gestrm07 #107,MANTE;
     agifa142 #142;
     agifa048 #480;
     agifa049 #490;
     gestrm06@;
     gestrm07@;
     referen@;
|FIN;

|VARIABLES;
     &Tempo = "";
     &enLin = 0;
     &enTipo = 0;
     aAlfa = "";
     aPath = "";
     nPanta = 0;
     nPanEdi = 0;
     nGridUrd = -1;
     nGridTrm = -1;
     nRango = 0;
     nBtnAltaUrd = 0;
     nBtnModiUrd = 0;
     nBtnBajaUrd = 0;
     nBtnAltaTrm = 0;
     nBtnModiTrm = 0;
     nBtnBajaTrm = 0;
     nBtnImg = 0;
     nBtnCom = 0;
     nBtnCos = 0;
     nBtnDup = 0;
     nBtnOtraAlta = 0;
 {-1}aVent = "";
     aVent1 = 0;
     aVent2 = 0;
     aVent3 = 0;
     aVent4 = 0;
     aVent5 = "";
     nVentana = 0;
     nModo = 0;
     nModoUrd = 0;
     nModoTrm = 0;
     nExiste = 0;
     aDes = "";
     aOrg = "";
     aImg = "";
     aIP = "";
     nHandle = 0;
     aArt = "";
     nSwOtra = 0;
     nLin = 0;
     i = 0;
     aConte = "";
     nPre = 0;
     nCal1 = 0;
     nCal2 = 0;
     nCal3 = 0;
     nTpeso = 0;
     nTcoste = 0;
     nTpasada = 0;
     nNume = 0;
|FIN;

|PROCESO PreVenta;
     #0#25 = #0#24 / (1 - (#0#26 / 100)); |PINTA #0#25;
|FPRC;

|PROCESO CosteAcabado;
     #0#22 = #0#25 * #0#9 / 100; |PINTA #0#22;
|FPRC;

|PROCESO PreTeje;
/*
     #0#11 = #0#12 * 166.386;
     |PINTA #0#11;
*/
|FPRC;

|PROCESO PreTejeEuro;
     #0#12 = #0#11 * #0#13 * #0#33 / 100 / 166.386 * (1+ (#0#5/100));
     #0#34 = #0#11 * #0#13 * #0#33 / 100 / 166.386;
     |PINTA #0#34;
|FPRC;

|PROCESO CosteTeje;
     #0#17 = ((#0#12 * #0#13 + #0#31) / 100) * (1 + (#0#5/100));
     |PINTA #0#17;
|FPRC;

|PROCESO CosteMermaTeje;
     #0#18 = (#0#16 + #0#12) * (#0#5 / 100);
     |PINTA #0#18;
|FPRC;

|PROCESO CosteComiCrudo;
     #0#19 = (#0#16 + #0#17) * (#0#6 / 100);
     |PINTA #0#19;
|FPRC;

|PROCESO CosteTotaTeje;
     #0#20 = #0#16 + #0#12 + #0#18 + #0#19;
     |PINTA #0#20;
|FPRC;

|PROCESO CosteMermaAcaba;
     #0#21 = #0#20 * #0#9 / 100;
     |PINTA #0#21;
|FPRC;

|PROCESO CosteTotaAcaba;
     #0#23 = #0#21 + #0#10;
     |PINTA #0#23;
|FPRC;

|PROCESO CosteTotaTejeAcaba;
     #0#24 = #0#20 + #0#23 + #0#32;
     |PINTA #0#24;
|FPRC;

|PROCESO Margen;
     #0#26 = ((#0#25 - #0#24) / #0#25) * 100;
     |PINTA #0#26;
|FPRC;

|PROCESO MargenEuro;
     #0#27 = #0#25 - #0#24;
     |PINTA #0#27;
|FPRC

|PROCESO PreTejeDra;
     #0#28 = ((#0#25 - #0#24) * 100 / #0#13 / #0#33 * 166.386) + #0#11;
     |PINTA #0#28;
|FPRC;

|PROCESO ComPreVta;
     nPre = #0#28 * #0#31 * #0#13 / 100 / 166.386;
     nPre = nPre + #0#16;
     nPre = nPre * (1 + ((#0#5 + #0#6)/100));
     nPre = nPre * (1 + (#0#9/100)) + #0#22 + (#0#25 * #0#14 / 100);
     #0#30 = nPre; |PINTA #0#30;
|FPRC;

|PROCESO CosComiVta;
     #0#32 = #0#25 * #0#14 / 100;
     |PINTA #0#32;
|FPRC;

|PROCESO MetricoTrm;
     #107#6 = (#107#4 * #0#2 * 100) / (#107#5 / 1000) * (1 + (#0#5/100));
|FPRC;

|PROCESO MetricoUrd; |TIPO 0;
     #106#6 = (#106#4 * #0#2 * 100) / (#106#5 / 1000) * (1 + (#0#5/100));
|FPRC;

|PROCESO ActTrm;
     |HAZ MetricoTrm;
     |HAZ PreTrm;
     |GRABA 020#107a;
|FPRC;

|DEFBUCLE ActTrm;
     #107#1;
     ;
     #0#0, 00;
     #0#0, 99;
     be;
     NULL, ActTrm;
|FIN;

|PROCESO ActUrd;
     |HAZ MetricoUrd;
     |HAZ PreUrd;
     |GRABA 020#106a;
|FPRC;

|DEFBUCLE ActUrd;
     #106#1;
     ;
     #0#0, 00;
     #0#0, 99;
     be;
     NULL, ActUrd;
|FIN;

|PROCESO ActLin;
     |BUCLE ActUrd;
     |BUCLE ActTrm;
     |HAZ Recalcula;
|FPRC;

|PROCESO RecalTrm;
     nTpeso = nTpeso + (#107#6 * #107#9 / 100);
     nTcoste = nTcoste + #107#8;
     nTpasada = nTpasada + (#107#4 * #107#9 / 100);
|FPRC;

|DEFBUCLE RecalTrm;
     #107#1;
     ;
     #0#0, 00;
     #0#0, 99;
     ;
     NULL, RecalTrm;
|FIN;

|PROCESO RecalUrd;
     nTpeso = nTpeso + #106#6;
     nTcoste = nTcoste + #106#8;
     nTpasada = nTpasada + #106#4;
|FPRC;

|DEFBUCLE RecalUrd;
     #106#1;
     ;
     #0#0, 00;
     #0#0, 99;
     ;
     NULL, RecalUrd;
|FIN;

|PROCESO Recalcula;
     nTpeso = 0;
     nTcoste = 0;
     nTpasada = 0;
     |BUCLE RecalTrm;

     nNume = nTpasada ! 0;      || Redondeo hacia arriba
     |SI nNume < nTpasada
          nTpasada = nNume + 1;
     |SINO;
          nTpasada = nNume;
     |FINSI;

     #0#33 = nTpasada; |PINTA #0#33;
     |BUCLE RecalUrd;
     #0#15 = nTpeso; |PINTA #0#15;
     #0#16 = nTcoste; |PINTA #0#16;
     #0#31 = nTpasada; |PINTA #0#31;
     |HAZ PreTejeEuro;
     |HAZ CosteTeje;
     |HAZ CosteMermaTeje;
     |HAZ CosteComiCrudo;
     |HAZ CosteTotaTeje;
     |HAZ CosteMermaAcaba;
     |HAZ CosteTotaAcaba;
     |HAZ CosteTotaTejeAcaba;
     |HAZ Margen;
     |HAZ PreVenta;
     |HAZ MargenEuro;
     |HAZ PreTejeDra;
     |HAZ ComPreVta;
     |HAZ ComPreVta;
|FPRC;

|PROCESO T0; |TIPO 0;
     nModo = (FEntrada / 100) ! 0;
     |SI Campo = 1;
          |ABRE #19;
          #19#0 = #0#0;
          |LEE 000#19=;
          |SI FSalida = 0;
               #0#1 = #19#1; |PINTA #0#1;
               #0#4 = #19#126 / 100; |PINTA #0#4;
          |SINO;
               |SI nModo = 1;
                    |MENAV "0000No existe art¡culo...";
                    |ERROR;
               |FINSI;
          |FINSI;
          |CIERRA #19;
          |SI nModo = 1;
               #0#13 = #1#30; |PINTA #0#13;
               #0#14 = #1#31; |PINTA #0#14;
               |HAZ PreTejeEuro;
               |HAZ CosteTeje;
          |FINSI;
     |FINSI;
     |SI Campo = 2; |Y #0Campo ! aConte;
          |HAZ ActLin;
          |REFRESCACONTROL nGridUrd;
          |REFRESCACONTROL nGridTrm;
     |FINSI;
     |SI Campo = 7;
          |ABRE #3;
          #3#0 = #0#7;
          |LEE 000#3=;
          |SI FSalida = 0;
               #0#8 = #3#1; |PINTA #0#8;
               |SI aConte ! #0Campo;
                    #0#10 = #3#2; |PINTA #0#10;
               |FINSI;
          |SINO;
               |MENAV "0000No existe c¢digo de acabado...";
               |ERROR;
          |FINSI;
          |CIERRA #3;
     |FINSI;
     |SI Campo = 5;
          |HAZ PreTejeEuro;
          |HAZ CosteTeje;
          |HAZ CosteMermaTeje;
          |HAZ CosteComiCrudo;
          |HAZ CosteTotaTeje;
          |HAZ CosteMermaAcaba;
          |HAZ CosteTotaAcaba;
          |HAZ CosteTotaTejeAcaba;
          |HAZ Margen;
          |HAZ PreVenta;
          |HAZ MargenEuro;
          |HAZ PreTejeDra;
          |HAZ ComPreVta;
     |FINSI;
     |SI Campo = 6;
          |HAZ CosteComiCrudo;
          |HAZ CosteTotaTeje;
          |HAZ CosteMermaAcaba;
          |HAZ CosteTotaAcaba;
          |HAZ CosteTotaTejeAcaba;
          |HAZ Margen;
          |HAZ PreVenta;
          |HAZ MargenEuro;
          |HAZ PreTejeDra;
          |HAZ ComPreVta;
     |FINSI;
     |SI Campo = 9;
          |HAZ ComPreVta;
          |HAZ CosteAcabado;
     |FINSI;
     |SI Campo = 10;
     |FINSI;
     |SI Campo = 11;
          |HAZ PreTejeEuro;
          |HAZ CosteTeje;
          |HAZ CosteMermaTeje;
          |HAZ CosteComiCrudo;
          |HAZ CosteTotaTeje;
          |HAZ CosteMermaAcaba;
          |HAZ CosteTotaAcaba;
          |HAZ CosteTotaTejeAcaba;
          |HAZ Margen;
          |HAZ PreVenta;
          |HAZ MargenEuro;
          |HAZ PreTejeDra;
          |HAZ ComPreVta;
     |FINSI;
     |SI Campo = 12;
          |HAZ PreTeje;
          |HAZ PreTejeEuro;
          |HAZ CosteTeje;
          |HAZ CosteMermaTeje;
          |HAZ CosteComiCrudo;
          |HAZ CosteTotaTeje;
          |HAZ CosteMermaAcaba;
          |HAZ CosteTotaAcaba;
          |HAZ CosteTotaTejeAcaba;
          |HAZ Margen;
          |HAZ PreVenta;
          |HAZ MargenEuro;
          |HAZ PreTejeDra;
          |HAZ ComPreVta;
     |FINSI;
     |SI Campo = 13;
          |HAZ CosteTeje;
          |HAZ CosteMermaTeje;
          |HAZ CosteComiCrudo;
          |HAZ CosteTotaTeje;
          |HAZ CosteMermaAcaba;
          |HAZ CosteTotaAcaba;
          |HAZ CosteTotaTejeAcaba;
          |HAZ Margen;
          |HAZ PreVenta;
          |HAZ MargenEuro;
          |HAZ PreTejeDra;
          |HAZ ComPreVta;
     |FINSI;
     |SI Campo = 14;
          |HAZ CosComiVta;
          |HAZ CosteTotaTejeAcaba;
          |HAZ Margen;
          |HAZ PreVenta;
          |HAZ MargenEuro;
          |HAZ PreTejeDra;
          |HAZ ComPreVta;
     |FINSI;
     |SI Campo = 20;
          |HAZ  CosteTotaTejeAcaba;
     |FINSI;
     |SI Campo = 23;
          |HAZ  CosteTotaTejeAcaba;
     |FINSI;
     |SI Campo = 24;
          |HAZ PreTejeDra;
     |FINSI;
     |SI Campo = 25;
          |HAZ Margen;
          |HAZ MargenEuro;
          |HAZ PreTejeDra;
          |HAZ ComPreVta;
          |HAZ CosteAcabado;
     |FINSI;
     |SI Campo = 26;
          |HAZ PreVenta;
          |HAZ CosteAcabado;
     |FINSI;
     |SI Campo = 31;
          |HAZ CosteTeje;
          |HAZ CosteMermaTeje;
          |HAZ CosteComiCrudo;
          |HAZ CosteTotaTeje;
          |HAZ CosteMermaAcaba;
          |HAZ CosteTotaAcaba;
          |HAZ CosteTotaTejeAcaba;
          |HAZ Margen;
          |HAZ PreVenta;
          |HAZ MargenEuro;
          |HAZ PreTejeDra;
          |HAZ ComPreVta;
     |FINSI;
     |SI Campo = 32;
          |HAZ  CosteTotaTejeAcaba;
     |FINSI;
     |SI Campo = 33;
          |HAZ PreTejeEuro;
          |HAZ PreTejeDra;
     |FINSI;
|FPRC;

|PROCESO Conte; |TIPO 7;
     aConte = #0Campo
|FPRC;

|PROCESO ConteUrd; |TIPO 7;
     aConte = #106Campo;
|FPRC;

|PROCESO MpUrd; |TIPO 0;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 1; |O aConte ! #106#2;
          |ABRE #19;
          #19#0 = #106#2;
          |LEE 000#19=;
          |SI FSalida = 0;
               |SI #19#125 ! #1#45;
                    aAlfa = "0000La familia del art¡culo:[" + #19#125 + "], no es urdimbre:[" + #1#45 + "]";
                    |MENAV aAlfa;
               |FINSI;
               #106#3 = #19#1; |PINTA #106#3;
               #106#5 = #19#127; |PINTA #106#5;
               |HAZ MetricoUrd;
               |PINTA #106#6;
          |SINO;
               |MENAV "0000No existe c¢digo art¡culo...";
               |ERROR;
          |FINSI;
          |CIERRA #19;
          |SI #1#32 = "U";
               #106#7 = #19#133; |PINTA #106#7;
          |SINO;
               #106#7 = #19#9; |PINTA #106#7;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO ArtiUrd;
     |SI #19#125 = #1#45;
          |SALVA_DATOS #19;
          |REPON_DATOS #referen@;
          |GRABA 020#referen@.n;
     |FINSI;
|FPRC;

|DEFBUCLE ArtiUrd;
     #19#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, ArtiUrd;
|FIN;

|PROCESO MpUrd66; |TIPO 66;
     |DDEF aAlfa;
     aAlfa = aAlfa + "agifa019";
     |CARGA_DEF aAlfa, referen@;
     |SI FSalida = 0;
          |HAZ CreaTempo; |NOME_DAT #referen@#Tempo#; |DELINDEX #referen@;
          |ABRE #referen@;
          |BUCLE ArtiUrd;
          #referen@#0 = #106#2;
          |LEE 000#referen@.=;
          |CONSULTA_CLAVE #1#referen@;
          |SI FSalida = 0;
               #106#2 = #referen@#0; |PINTA #106#2;
          |SINO;
               |ERROR;
          |FINSI;
          |CIERRA #referen@;
          |DELINDEX #referen@; |HAZ BoraTempo;
          |DESCARGA_DEF #referen@;
     |FINSI;
|FPRC;

|PROCESO MetricoUrd0; |TIPO 0;
     |SI #106Campo ! aConte;
          |HAZ MetricoUrd;
          |PINTA #106#6;
     |FINSI;
|FPRC;

|PROCESO PreUrd; |TIPO 0;
     #106#8 = #106#6 * #106#7 / 1000; |PINTA #106#8;
|FPRC;

|PROCESO ConteTrm; |TIPO 7;
     aConte = #107Campo;
|FPRC;

|PROCESO MpTrm; |TIPO 0;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 1; |O aConte ! #107#2;
          |ABRE #19;
          #19#0 = #107#2;
          |LEE 000#19=;
          |SI FSalida = 0;
               |SI #19#125 ! #1#46;
                    aAlfa = "0000La familia del art¡culo:[" + #19#125 + "], no es trama:[" + #1#46 + "]";
                    |MENAV aAlfa;
               |FINSI;
               #107#3 = #19#1; |PINTA #107#3;
               #107#5 = #19#127; |PINTA #107#5;
               |HAZ MetricoTrm;
               |PINTA #107#6;
          |SINO;
               |MENAV "0000No existe c¢digo art¡culo...";
               |ERROR;
          |FINSI;
          |CIERRA #19;
          |SI #1#32 = "U";
               #107#7 = #19#133; |PINTA #107#7;
          |SINO;
               #107#7 = #19#9; |PINTA #107#7;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO ArtiTrm;
     |SI #19#125 = #1#46;
          |SALVA_DATOS #19;
          |REPON_DATOS #referen@;
          |GRABA 020#referen@.n;
     |FINSI;
|FPRC;

|DEFBUCLE ArtiTrm;
     #19#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, ArtiTrm;
|FIN;

|PROCESO MpTrm66; |TIPO 66;
     |DDEF aAlfa;
     aAlfa = aAlfa + "agifa019";
     |CARGA_DEF aAlfa, referen@;
     |SI FSalida = 0;
          |HAZ CreaTempo; |NOME_DAT #referen@#Tempo#; |DELINDEX #referen@;
          |ABRE #referen@;
          |BUCLE ArtiTrm;
          #referen@#0 = #107#2;
          |LEE 000#referen@.=;
          |CONSULTA_CLAVE #1#referen@;
          |SI FSalida = 0;
               #107#2 = #referen@#0; |PINTA #107#2;
          |SINO;
               |ERROR;
          |FINSI;
          |CIERRA #referen@;
          |DELINDEX #referen@; |HAZ BoraTempo;
          |DESCARGA_DEF #referen@;
     |FINSI;
|FPRC;

|PROCESO MetricoTrm0; |TIPO 0;
     |SI #107Campo ! aConte;
          |HAZ MetricoTrm;
          |PINTA #107#6;
     |FINSI;
|FPRC;

|PROCESO PreTrm; |TIPO 0;
     #107#8 = #107#6 * #107#7 / 1000 * #107#9/100; |PINTA #107#8;
|FPRC;

|PROCESO Borra7;
     |BORRA 020#107a;
|FPRC;

|DEFBUCLE Borra7;
     #107#1;
     ;
     #0#0, 00;
     #0#0, 99;
     be;
     NULL, Borra7;
|FIN;

|PROCESO Borra6;
     |BORRA 020#106a;
|FPRC;

|DEFBUCLE Borra6;
     #106#1;
     ;
     #0#0, 00;
     #0#0, 99;
     be;
     NULL, Borra6;
|FIN;

|PROCESO Copia0;
     |ABRE #480;
     #480#0 = #0#0;
     |LEE 101#480=;
     |SI FSalida ! 0; |GRABA 020#480b; |FINSI;
     #480#0 = #0#0;
     #480#1 = #0#1;
     #480#4 = #0#24 - #0#16;
     #480#5 = #0#24 - #0#16;
     #480#6 = 1;
     #480#7 = #0#5 + #0#9;
     #480#8 = #0#16;
     #480#9 = #0#24;
     #480#10 = 1;
     #480#11 = #0#24;
     #480#12 = 1;
     #480#15 = "N";
     #480#16 = #0#26;
     #480#17 = "S";
     #480#18 = "N";
     #480#19 = "N";
     #480#20 = "S";
     #480#21 = #0#25;
     #480#22 = #0#25;
     #480#25 = #0#26;
     #480#26 = "I";
     #480#29 = "I";
     #480#33 = "N";
     #480#35 = "N";
     #480#36 = "S";
     |GRABA 020#480a;
     |CIERRA #480;
|FPRC;

|PROCESO &Ges5_15; |TIPO 15;
     |HAZ Copia0;
|FPRC;

|PROCESO &Ges5_16; |TIPO 16;
     |HAZ Copia0;
|FPRC;

|PROCESO Borra49;
     |BORRA 020#490a;
|FPRC;

|DEFBUCLE Borra49;
     #490#1;
     ;
     #0#0, 001;
     #0#0, 999;
     be;
     NULL, Borra49;
|FIN;

|PROCESO &Ges5_17; |TIPO 17;
     |ABRE #480;
     #480#0 = #0#0;
     |BORRA 000#480c;
     |CIERRA #480;
     |BUCLE Borra49;
|FPRC;

|PROCESO Copia6;
     |ABRE #490;
     |DDEFECTO #490;
     #490#0 = #106#0;
     #490#1 = #106#1;
     |LEE 101#490=;
     |SI FSalida ! 0; |GRABA 020#490b; |FINSI;
     #490#2 = #106#2;
     #490#3 = #106#3;
     #490#4 = #106#6 / 1000;
     #490#5 = #106#7;
     #490#6 = #106#8;
     #490#9 = 1;
     #490#10 = 1;
     #490#12 = #106#6 / 1000;
     #490#13 = 0;
     #490#15 = #106#7;
     |GRABA 020#490a;
     |CIERRA #490;
|FPRC;

|PROCESO &Ges6_15; |TIPO 15;
     |HAZ Copia6;
|FPRC;

|PROCESO &Ges6_16; |TIPO 16;
     |HAZ Copia6;
|FPRC;

|PROCESO &Ges6_17; |TIPO 17;
     |ABRE #490;
     #490#0 = #106#0;
     #490#1 = #106#1;
     |BORRA 000#490c;
     |CIERRA #490;
|FPRC;

|PROCESO Copia7;
     nLin = 100 + #107#1;
     |ABRE #490;
     |DDEFECTO #490;
     #490#0 = #107#0;
     #490#1 = nLin;
     |LEE 101#490=;
     |SI FSalida ! 0; |GRABA 020#490b; |FINSI;
     #490#2 = #107#2;
     #490#3 = #107#3;
     #490#4 = #107#6 / 1000;
     #490#5 = #107#7;
     #490#6 = #107#8;
     #490#9 = 1;
     #490#10 = 1;
     #490#12 = #107#6 / 1000;
     #490#13 = 0;
     #490#15 = #107#7;
     |GRABA 020#490a;

     |DDEFECTO #490;
     #490#0 = #107#0;
     #490#1 = 200;
     |LEE 101#490=;
     |SI FSalida ! 0; |GRABA 020#490b; |FINSI;
     #490#2 = "TEJER";
     #490#3 = "TEJER";
     #490#4 = 1;
     #490#5 = #0#20;
     #490#6 = #0#20;
     #490#9 = 1;
     #490#10 = 4;
     #490#12 = 1;
     #490#15 = #0#20;
     |GRABA 020#490a;

     |DDEFECTO #490;
     #490#0 = #107#0;
     #490#1 = 201;
     |LEE 101#490=;
     |SI FSalida ! 0; |GRABA 020#490b; |FINSI;
     #490#2 = ("00" + #0#7) % -102;
     #490#3 = #0#8;
     #490#4 = 1;
     #490#5 = #0#23;
     #490#6 = #0#23;
     #490#9 = 1;
     #490#10 = 4;
     #490#12 = 1;
     #490#15 = #0#23;
     |GRABA 020#490a;

     |DDEFECTO #490;
     #490#0 = #107#0;
     #490#1 = 202;
     |LEE 101#490=;
     |SI FSalida ! 0; |GRABA 020#490b; |FINSI;
     #490#2 = "COMISION";
     #490#3 = "COMISION VENTA";
     #490#4 = 1;
     #490#5 = #0#32;
     #490#6 = #0#32;
     #490#9 = 1;
     #490#10 = 4;
     #490#12 = 1;
     #490#15 = #0#32;
     |GRABA 020#490a;
     |CIERRA #490;
|FPRC;

|PROCESO &Ges7_15; |TIPO 15;
     |HAZ Copia7;
|FPRC;

|PROCESO &Ges7_16; |TIPO 16;
     |HAZ Copia7;
|FPRC;

|PROCESO &Ges7_17; |TIPO 17;
     nLin = 100 + #107#1;
     |ABRE #490;
     #490#0 = #107#0;
     #490#1 = nLin;
     |BORRA 000#490c;
     #490#1 = 200;
     |BORRA 000#490c;
     #490#1 = 201;
     |BORRA 000#490c;
     #490#1 = 202;
     |BORRA 000#490c;
     |CIERRA #490;
|FPRC;

|PROCESO T7; |TIPO 7;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 1;
          |BUCLE Borra6;
          |BUCLE Borra7;
     |FINSI;
     |SI nModo = 4;
          |ESTADO_CONTROL nBtnAltaUrd, "DISABLE";
          |ESTADO_CONTROL nBtnModiUrd, "DISABLE";
          |ESTADO_CONTROL nBtnBajaUrd, "DISABLE";
          |ESTADO_CONTROL nBtnAltaTrm, "DISABLE";
          |ESTADO_CONTROL nBtnModiTrm, "DISABLE";
          |ESTADO_CONTROL nBtnBajaTrm, "DISABLE";
          |ESTADO_CONTROL nBtnImg, "ENABLE";
          |ESTADO_CONTROL nBtnCom, "ENABLE";
          |ESTADO_CONTROL nBtnCos, "ENABLE";
          |ESTADO_CONTROL nBtnDup, "ENABLE";
     |SINO;
          |ESTADO_CONTROL nBtnAltaUrd, "ENABLE";
          |ESTADO_CONTROL nBtnModiUrd, "ENABLE";
          |ESTADO_CONTROL nBtnBajaUrd, "ENABLE";
          |ESTADO_CONTROL nBtnAltaTrm, "ENABLE";
          |ESTADO_CONTROL nBtnModiTrm, "ENABLE";
          |ESTADO_CONTROL nBtnBajaTrm, "ENABLE";
          |ESTADO_CONTROL nBtnImg, "DISABLE";
          |ESTADO_CONTROL nBtnCom, "DISABLE";
          |ESTADO_CONTROL nBtnCos, "DISABLE";
          |ESTADO_CONTROL nBtnDup, "DISABLE";
     |FINSI;
     |REFRESCACONTROL nGridUrd;
     |REFRESCACONTROL nGridTrm;
|FPRC;

|PROCESO T2; |TIPO 2;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 3;
          |BUCLE Borra6;
          |BUCLE Borra7;
     |FINSI;
|FPRC;

|PROCESO T20; |TIPO 20;
     |SI FSalida = "OPCION";
          |ESTADO_CONTROL nBtnAltaUrd, "DISABLE";
          |ESTADO_CONTROL nBtnModiUrd, "DISABLE";
          |ESTADO_CONTROL nBtnBajaUrd, "DISABLE";
          |ESTADO_CONTROL nBtnAltaTrm, "DISABLE";
          |ESTADO_CONTROL nBtnModiTrm, "DISABLE";
          |ESTADO_CONTROL nBtnBajaTrm, "DISABLE";
          |ESTADO_CONTROL nBtnImg, "ENABLE";
          |ESTADO_CONTROL nBtnCom, "ENABLE";
          |ESTADO_CONTROL nBtnCos, "ENABLE";
          |ESTADO_CONTROL nBtnDup, "ENABLE";
          FSalida = "";
     |FINSI;
|FPRC;

|PROCESO T80; |TIPO 80;
     FSalida = 4099;
|FPRC;

|PROCESO T8; |TIPO 8;
     |ABRE #142; |LEE 000#142p; |CIERRA #142;
     |ABRE #1; |LEE 000#1p; |CIERRA #1;
     |DDEF aPath;
     aAlfa = aPath + "gestrm06"; |CARGA_DEF aAlfa, gestrm06@;
     aAlfa = aPath + "gestrm07"; |CARGA_DEF aAlfa, gestrm07@;
     |ABRET #gestrm06@;
     |ABRET #gestrm07@;
|FPRC;

|PROCESO T9; |TIPO 9;
     |CIERRAT #gestrm07@;
     |CIERRAT #gestrm06@;
     |DESCARGA_DEF #gestrm07@;
     |DESCARGA_DEF #gestrm06@;
     |DESTRUYECONTROL nGridUrd;
     |DESTRUYECONTROL nGridTrm;
     |FIN_CONTROL_WINDOWS nBtnAltaUrd;
     |FIN_CONTROL_WINDOWS nBtnModiUrd;
     |FIN_CONTROL_WINDOWS nBtnBajaUrd;
     |FIN_CONTROL_WINDOWS nBtnAltaTrm;
     |FIN_CONTROL_WINDOWS nBtnModiTrm;
     |FIN_CONTROL_WINDOWS nBtnBajaTrm;
     |FIN_CONTROL_WINDOWS nBtnImg;
     |FIN_CONTROL_WINDOWS nBtnCom;
     |FIN_CONTROL_WINDOWS nBtnCos;
     |FIN_CONTROL_WINDOWS nBtnDup;
|FPRC;

|PROCESO Evt7;
     |SI FSalida = 1; |O FSalida = 2;
          |LEE 000#gestrm07@.=;
     |FINSI;
     |SI FSalida = 4;
          nModoTrm = 2;
          |HAZ EditaTrm;
     |FINSI;
|FPRC;

|PROCESO Evt6;
     |SI FSalida = 1; |O FSalida = 2;
          |LEE 000#gestrm06.=;
     |FINSI;
     |SI FSalida = 4;
          nModoUrd = 2;
          |HAZ EditaTrm;
     |FINSI;
|FPRC;

|PROCESO Min;
     #gestrm06@#0 = #0#0;
     #gestrm06@#1 = 01;

     #gestrm07@#0 = #0#0;
     #gestrm07@#1 = 01;
|FPRC;

|PROCESO Max;
     #gestrm06@#0 = #0#0;
     #gestrm06@#1 = 99;

     #gestrm07@#0 = #0#0;
     #gestrm07@#1 = 99;
|FPRC;

|PROCESO T13; |TIPO 13;
     |SI nGridUrd = -1;
          |PINPA #0#0; nPanta = FSalida;
          |CONTROL_SIMPLE nPanta, "BOTON,{{193}}Alta", 1514, 1524, BtnAltaUrd;
          nBtnAltaUrd = FSalida;
          |CONTROL_SIMPLE nPanta, "BOTON,{{197}}Modificar", 1526, 1536, BtnModiUrd
          nBtnModiUrd = FSalida;
          |CONTROL_SIMPLE nPanta, "BOTON,{{199}}Baja", 1538, 1548, BtnBajaUrd;
          nBtnBajaUrd = FSalida;

          nRango = 4 + 8 + 16 + 32;
          |LINEAL_SIMPLE #1#gestrm06@, nRango, 1601, 2099, Min, Max, Evt6;
          nGridUrd = FSalida;

          |CONTROL_SIMPLE nPanta, "BOTON,{{193}}Alta", 2114, 2124, BtnAltaTrm;
          nBtnAltaTrm = FSalida;
          |CONTROL_SIMPLE nPanta, "BOTON,{{197}}Modificar", 2126, 2136, BtnModiTrm
          nBtnModiTrm = FSalida;
          |CONTROL_SIMPLE nPanta, "BOTON,{{199}}Baja", 2138, 2148, BtnBajaTrm;
          nBtnBajaTrm = FSalida;

          |LINEAL_SIMPLE #1#gestrm07@, nRango, 2201, 3399, Min, Max, Evt7;
          nGridTrm = FSalida;

          |CONTROL_SIMPLE nPanta, "BOTON,{{137}}Imagen", 0578, 0598, BtnImg;
          nBtnImg = FSalida;
          |CONTROL_SIMPLE nPanta, "BOTON,{{137}}Composici¢n", 0678, 0698, BtnCom;
          nBtnCom = FSalida;
          |CONTROL_SIMPLE nPanta, "BOTON,{{137}}Actualizaci¢n Costes", 0778, 0798, BtnCos;
          nBtnCos = FSalida;
          |CONTROL_SIMPLE nPanta, "BOTON,{{137}}Duplicar Escandallo", 0878, 0898, BtnDup;
          nBtnDup = FSalida;
     |FINSI;
|FPRC;

|PROCESO BtnAltaUrd;
     nModoUrd = 1;
     |HAZ EditaUrd;
|FPRC;

|PROCESO BtnModiUrd;
     |LEE 000#gestrm06@.=;
     |SI FSalida = 0; |Y #gestrm06@#0 = #0#0;
          nModoUrd = 2;
          |HAZ EditaUrd;
     |FINSI;
|FPRC;

|PROCESO BtnBajaUrd;
     |LEE 101#gestrm06@.=;
     |SI FSalida = 0; |Y #gestrm06@#0 = #0#0;
          aAlfa = "0000¨Borra linea urdimbre:" +&13+ #gestrm06@#0 + "/" + #gestrm06@#1 + "?";
          |CONFI aAlfa;
          |SI FSalida = 0;
               |BORRA 020#gestrm06@.a;
               |HAZ Recalcula;
               |REFRESCACONTROL nGridUrd;
          |FINSI;
     |FINSI;
     |LIBERA #gestrm06@;
|FPRC;

|PROCESO BtnAltaTrm;
     nModoTrm = 1;
     |HAZ EditaTrm;
|FPRC;

|PROCESO BtnModiTrm;
     |LEE 000#gestrm07@.=;
     |SI FSalida = 0; |Y #gestrm07@#0 = #0#0;
          nModoTrm = 2;
          |HAZ EditaTrm;
     |FINSI;
|FPRC;

|PROCESO BtnBajaTrm;
     |LEE 101#gestrm07@.=;
     |SI FSalida = 0; |Y #gestrm07@#0 = #0#0;
          aAlfa = "0000¨Borra linea Trama:" +&13+ #gestrm07@#0 + "/" + #gestrm07@#1 + "?";
          |CONFI aAlfa;
          |SI FSalida = 0;
               |BORRA 020#gestrm07@.a;
               |HAZ Recalcula;
               |REFRESCACONTROL nGridTrm;
          |FINSI;
     |FINSI;
     |LIBERA #gestrm07@;
|FPRC;

|PROCESO CopiaImg;
     |ABRE #9;
     #9#6 = "A";
     #9#0 = #0#0;
     #9#1 = 0;
     |LEE 000#9];
     |SI FSalida = 0; |Y #9#0 = #0#0;
          aImg = #9#2; |QBF aImg;
     |SINO;
          aImg = "vacio.bmp";
     |FINSI;
     aOrg = #142#134; |QBF aOrg;
     aOrg = aOrg + "/" + aImg;
     aDes = "/DOCUMENTOS/DSCOMER9/"; |MKDIR aDes;
     aDes = aDes + "imagen.bmp";

     |XABRE "E", aOrg, 0;
     nExiste = FSalida;

     aIP = "";
     |IP_REMOTO aIP;
     |SI aIP = "";
          |SI nExiste < 0;         ||Lo crea vacio
               |XABRE "WB", aDes, nHandle;
               |XCIERRA nHandle;
          |SINO;
               |COPIA_FICHERO aOrg, aDes;
          |FINSI;
     |SINO;
          |SI nExiste < 0;         ||Lo crea vacio
               |XABRE "CWB", aDes, nHandle;
               |XCIERRA nHandle;
          |SINO;
               |ENVIA_FICHERO aOrg, aDes;
          |FINSI;
     |FINSI;
     |CIERRA #9;
|FPRC;

|PROCESO BtnImg;
     |VENTANA 1519, 3579, -1, 17, "Imagen";
     nVentana = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVentana;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |HAZ CopiaImg;
     |SI nExiste = 0;
          aImg = "IMAGEN," + aDes;
     |SINO;
          aImg = "BOTON, Sin imagen";
     |FINSI;
     |PULSATECLA;
     |CONTROL_SIMPLE 0, aImg, 1620, 3478, NULL;
     |PAUSA;
     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVentana;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA nVentana;
     |PULSATECLA;
|FPRC;

|PROCESO Composicion;
     #2#0 = #4#1;
     |LEE 000#2=;
     |SI FSalida ! 0; |DDEFECTO #2; |FINSI;
     |PRINT;
|FPRC;

|DEFBUCLE Composicion;
     #4#1;
     ;
     aArt, 00;
     aArt, 99;
     ;
     NULL, Composicion;
|FIN;

|PROCESO Trama;
     aArt = #107#2;

     enLin = #107#1;     || Paso con variable porque falla el .inf
     enTipo = 1;
     |BUCLE Composicion;
|FPRC;

|DEFBUCLE Trama;
     #107#1;
     ;
     #0#0, 00;
     #0#0, 99;
     ;
     NULL, Trama;
|FIN;

|PROCESO Urdimbre;
     aArt = #106#2;

     enLin = #106#1;     || Paso con variable porque falla el .inf
     enTipo = 0;
     |BUCLE Composicion;
|FPRC;

|DEFBUCLE Urdimbre;
     #106#1;
     ;
     #0#0, 00;
     #0#0, 99;
     ;
     NULL, Urdimbre;
|FIN;

|PROCESO BtnCom;
     |LEE 000#0=;
     |SI FSalida ! 0; |ACABA; |FINSI;

     Impresora = "CrystalReport";
     ||Impresora = "Crystal Reports";
     |IMPRE -1;
     |SI FSalida = 0;
          |INFOR "gestrm05";
          |SI FSalida = 0;
               |ABRE #2;
               |BUCLE Urdimbre;
               |BUCLE Trama;
               |CIERRA #2;
               |PIEINF;
               |FININF;
          |SINO;
               |MENAV "0000Error informe 'gestrm05'";
          |FINSI;
          |FINIMP;
     |FINSI;
|FPRC;

|PROCESO ActCosTrm;
     #19#0 = #107#2;
     |LEE 020#19=;
     |SI FSalida ! 0; |DDEFECTO #19; |FINSI;

     |SI #1#32 = "U";
          #107#7 = #19#133;
     |SINO;
          #107#7 = #19#9;
     |FINSI;
     #107#8 = #107#6 * #107#7 / 1000;
     |GRABA 020#107a;
|FPRC;

|DEFBUCLE ActCosTrm;
     #107#1;
     ;
     #101#0, 00;
     #101#1, 99;
     be;
     NULL, ActCosTrm;
|FIN;

|PROCESO ActCosUrd;
     #19#0 = #106#2;
     |LEE 020#19=;
     |SI FSalida ! 0; |DDEFECTO #19; |FINSI;

     |SI #1#32 = "U";
          #106#7 = #19#133;
     |SINO;
          #106#7 = #19#9;
     |FINSI;
     #106#8 = #106#6 * #106#7 / 1000;
     |GRABA 020#106a;
|FPRC;

|DEFBUCLE ActCosUrd;
     #106#1;
     ;
     #101#0, 00;
     #101#1, 99;
     be;
     NULL, ActCosUrd;
|FIN;

|PROCESO Recalcula0;
     |HAZ Recalcula;
     |GRABA 020#0a;
     |LIBERA #0;
|FPRC;

|DEFBUCLE Recalcula0;
     #0#1;
     ;
     #101#0;
     #101#1;
     be;
     NULL, Recalcula0;
|FIN;

|PROCESO ActCostes;
     |ABRE #19;
     |BUCLE ActCosUrd;
     |BUCLE ActCosTrm;
     |BUCLE Recalcula0;
     |CIERRA #19;
|FPRC;

|PROCESO BtnCos;
     |SALVA_FICHA #0;

     |VENTANA 1524, 2372, -1, 17, "Actualizaci¢n Costes";
     nVentana = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVentana;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;
     |DDEFECTO #101;
     |PINPA #0#101;
     |PINDA #0#101;
     |ENDATOS #1#101;
     |SI FSalida = 0;
          |SALVA_DATOS #0;
          |HAZ ActCostes;
          |REPON_DATOS #0;
          |LEE 000#0=;
          |PINDA #0#0;
     |FINSI;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVentana;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA nVentana;
     |PULSATECLA;

     |REPON_FICHA #0;
|FPRC;

|PROCESO DupTrm;
     #107#0 = #102#1;
     |GRABA 020#107n;
|FPRC;

|DEFBUCLE DupTrm;
     #107#1;
     ;
     #102#0, 00;
     #102#0, 99;
     ;
     NULL, DupTrm;
|FIN;

|PROCESO DupUrd;
     #106#0 = #102#1;
     |GRABA 020#106n;
|FPRC;

|DEFBUCLE DupUrd;
     #106#1;
     ;
     #102#0, 00;
     #102#0, 99;
     ;
     NULL, DupUrd;
|FIN;

|PROCESO Duplica;
      #0#0 = #102#0;
      |LEE 020#0=;
      |SI FSalida = 0;
          |ABRE #19;
          #19#0 = #102#1;
          |LEE 020#19=;
          |CIERRA #19;
          #0#0 = #102#1;
          #0#1 = #19#1;
          |GRABA 020#0n;
          |BUCLE Borra6;
          |BUCLE Borra7;
          |BUCLE DupUrd;
          |BUCLE DupTrm;
      |FINSI;
|FPRC;

|PROCESO Origen; |TIPO 66;
     #0#0 = #102#0;
     |CONSULTA_CLAVE #1#0;
     |SI FSalida = 0;
          #102#0 = #0#0; |PINTA #102#0;
     |SINO;
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO Destino; |TIPO 0;
     |SI FSalida = 2; |ACABA; |FINSI;
     #0#0 = #102#1;
     |LEE 000#0=;
     |SI FSalida = 0;
          |MENAV "0000Escandallo existente...";
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO Dup66; |TIPO 66;
     |ABRE #19;
     #19#0 = #102#1;
     |LEE 000#19];
     |CONSULTA_CLAVE #1#19;
     |SI FSalida = 0;
          #102#1 = #19#0;
     |SINO;
          |ERROR;
     |FINSI;
     |CIERRA #19;
|FPRC;

|PROCESO BtnDup;
     |SALVA_FICHA #0;

     |VENTANA 1524, 2372, -1, 17, "Duplicar escandallo";
     nVentana = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVentana;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;
     |DDEFECTO #102;
     |PINPA #0#102;
     |PINDA #0#102;
     |ENDATOS #1#102;
     |SI FSalida = 0;
          |REPON_FICHA #0;
          |HAZ Duplica;
          |SALVA_FICHA #0;
          |PINDA #0#0;
     |FINSI;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVentana;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA nVentana;
     |PULSATECLA;

     |REPON_FICHA #0;
|FPRC;

|PROCESO BtnOtraAlta;
     nSwOtra = 1;
     |PTEC 806;
|FPRC;

|PROCESO EditaUrd;
     |SI nModoUrd = 1; |VENTANA 1501, 2298, -1, 17, "Nueva Urdimbre"; |FINSI;
     |SI nModoUrd = 2; |VENTANA 1501, 2298, -1, 17, "Modifica Urdimbre"; |FINSI;
     |SI nModoUrd = 4; |VENTANA 1501, 2298, -1, 17, "Consulta Urdimbre"; |FINSI;
     nVentana = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVentana;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |PINPA #0#106;
     nPanEdi = FSalida;
     |SI nModoUrd = 1;
          |CONTROL_SIMPLE nPanEdi, "BOTON,{{206}}Validar y otra Alta", 0902, 0922, BtnOtraAlta;
     |FINSI;
     |SI nModoUrd = 2;
          |CONTROL_SIMPLE nPanEdi, "BOTON,{{206}}Validar y siguiente", 0902, 0922, BtnOtraAlta;
     |FINSI;
     |SI nModoUrd = 4;
          |CONTROL_SIMPLE nPanEdi, "BOTON,{{206}}Consulta siguiente", 0902, 0922, BtnOtraAlta;
     |FINSI;
     nBtnOtraAlta = FSalida;

     |ABRET #106;
|ET OtraUrd;
     nSwOtra = 0;
     |SI nModoUrd = 1;
          #106#0 = #0#0;
          #106#1 = 99;
          |LEE 000#106];
          |SI FSalida = 0;
               |LEE 000#106a;
          |SINO;
               |LEE 000#106u;
          |FINSI;
          |SI FSalida = 0; |Y #106#0 = #0#0;
               nLin = #106#1 + 1;
          |SINO;
               nLin = 1;
          |FINSI;
          |DDEFECTO #106;
          #106#0 = #0#0;
          #106#1 = nLin;
          |GRABA 020#106b;
     |SINO;
          #106#0 = #gestrm06@#0;
          #106#1 = #gestrm06@#1;
          |LEE 121#106=;
     |FINSI;
     |PINDA #0#106;
     |SI nModoUrd = 1; |ENDATOS #1#106; |FINSI;
     |SI nModoUrd = 2; |ENDATOS #2#106; |FINSI;
     |SI nModoUrd = 4; |PAUSA; |FINSI;
     |SI FSalida = 0; |Y nModoUrd ! 4;
          |GRABA 020#106a;
          |SALVA_FICHA #106;
          |HAZ Recalcula;
          |REPON_FICHA #106;
          |REFRESCACONTROL nGridUrd;
          |PULSATECLA;
          |SI nSwOtra = 1;
               |LIBERA #106;
               |SI nModoUrd = 1;
                    |VETE OtraUrd;
               |SINO;
                    |LEE 101#106s;
                    |SI FSalida = 0; |Y #106#0 = #0#0;
                         |VETE OtraUrd;
                    |SINO;
                         |MENAV "0000No hay mas registros...";
                    |FINSI;
               |FINSI;
          |FINSI;
     |SINO;
          |SI nModoUrd = 4; |Y nSwOtra = 1;
               |REFRESCACONTROL nGridUrd;
               |PULSATECLA;
               |LEE 000#106s;
               |SI FSalida = 0; |Y #106#0 = #0#0;
                    |VETE OtraUrd;
               |SINO;
                    |MENAV "0000No hay mas registros...";
               |FINSI;
          |FINSI;
          |SI nModoUrd = 1;
               |BORRA 020#106c;
               |REFRESCACONTROL nGridUrd;
          |FINSI;
     |FINSI;
     |CIERRAT #106;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVentana;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FIN_CONTROL_WINDOWS nBtnOtraAlta;
     |FINVENTANA nVentana;
     |PULSATECLA;
|FPRC;

|PROCESO EditaTrm;
     |SI nModoTrm = 1; |VENTANA 2101, 2898, -1, 17, "Nueva Trama"; |FINSI;
     |SI nModoTrm = 2; |VENTANA 2101, 2898, -1, 17, "Modifica Trama"; |FINSI;
     |SI nModoTrm = 4; |VENTANA 2101, 2898, -1, 17, "Consulta Trama"; |FINSI;
     nVentana = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVentana;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |PINPA #0#107;
     nPanEdi = FSalida;
     |SI nModoTrm = 1;
          |CONTROL_SIMPLE nPanEdi, "BOTON,{{206}}Validar y otra Alta", 0902, 0922, BtnOtraAlta;
     |FINSI;
     |SI nModoTrm = 2;
          |CONTROL_SIMPLE nPanEdi, "BOTON,{{206}}Validar y siguiente", 0902, 0922, BtnOtraAlta;
     |FINSI;
     |SI nModoTrm = 4;
          |CONTROL_SIMPLE nPanEdi, "BOTON,{{206}}Consulta siguiente", 0902, 0922, BtnOtraAlta;
     |FINSI;
     nBtnOtraAlta = FSalida;

     |ABRET #107;
|ET OtraTrm;
     nSwOtra = 0;
     |SI nModoTrm = 1;
          #107#0 = #0#0;
          #107#1 = 99;
          |LEE 000#107];
          |SI FSalida = 0;
               |LEE 000#107a;
          |SINO;
               |LEE 000#107u;
          |FINSI;
          |SI FSalida = 0; |Y #107#0 = #0#0;
               nLin = #107#1 + 1;
          |SINO;
               nLin = 1;
          |FINSI;
          |DDEFECTO #107;
          #107#0 = #0#0;
          #107#1 = nLin;
          |GRABA 020#107b;
     |SINO;
          #107#0 = #gestrm07@#0;
          #107#1 = #gestrm07@#1;
          |LEE 121#107=;
     |FINSI;
     |PINDA #0#107;
     |SI nModoTrm = 1; |ENDATOS #1#107; |FINSI;
     |SI nModoTrm = 2; |ENDATOS #2#107; |FINSI;
     |SI nModoTrm = 4; |PAUSA; |FINSI;
     |SI FSalida = 0; |Y nModoTrm ! 4;
          |GRABA 020#107a;
          |SALVA_FICHA #107;
          |HAZ Recalcula;
          |REPON_FICHA #107;
          |REFRESCACONTROL nGridTrm;
          |PULSATECLA;
          |SI nSwOtra = 1;
               |LIBERA #107;
               |SI nModoTrm = 1;
                    |VETE OtraTrm;
               |SINO;
                    |LEE 101#107s;
                    |SI FSalida = 0; |Y #107#0 = #0#0;
                         |VETE OtraTrm;
                    |SINO;
                         |MENAV "0000No hay mas registros...";
                    |FINSI;
               |FINSI;
          |FINSI;
     |SINO;
          |SI nModoTrm = 4; |Y nSwOtra = 1;
               |REFRESCACONTROL nGridTrm;
               |PULSATECLA;
               |LEE 000#107s;
               |SI FSalida = 0; |Y #107#0 = #0#0;
                    |VETE OtraTrm;
               |SINO;
                    |MENAV "0000No hay mas registros...";
               |FINSI;
          |FINSI;
          |SI nModoTrm = 1;
               |BORRA 020#107c;
               |REFRESCACONTROL nGridTrm;
          |FINSI;
     |FINSI;
     |CIERRAT #107;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVentana;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FIN_CONTROL_WINDOWS nBtnOtraAlta;
     |FINVENTANA nVentana;
     |PULSATECLA;
|FPRC;

|PROCESO ActEscan7;
     |GRABA 020#107a;
|FPRC;

|DEFBUCLE ActEscan7;
     #107#1;
     ;
     #0#0, 01;
     #0#0, 99;
     ;
     NULL, ActEscan7;
|FIN;

|PROCESO ActEscan6;
     |GRABA 020#106a;
|FPRC;

|DEFBUCLE ActEscan6;
     #106#1;
     ;
     #0#0, 01;
     #0#0, 99;
     ;
     NULL, ActEscan6;
|FIN;

|PROCESO T30; |TIPO 30;  ||Para que actualice la estandar
     |BUCLE ActEscan6;
     |BUCLE ActEscan7;
     |GRABA 020#0a;
|FPRC;
