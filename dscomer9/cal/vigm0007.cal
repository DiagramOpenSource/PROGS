|FICHEROS;
     vigm0007 #0;  || Lineas de albaran
     vigm0006 #1;
     agifa019 #2;  || Articulos
     agifa039 #5;
     agifa024 #6;
     agifa017 #8;
     agifa065 #9;
     agifa142 #20;       || Parametros Generales
     agifa007 #21;       || Series
     capsion  #22;
     agifa321 #35;       || DIV - Parametros
     dsl00001 #36;       || Codigos de barras multiples

     dsm00246 #40;       ||Control Riesgo Por Usuarios.
     dsm00247 #41;       ||Pantalla Informe Riesgo.
     dsm00248 #43;

     agis0002 #60;

     agi00100 #90;       || Lotes.
     agi00101 #91;       || Historico Lotes.

     agifa048 #480;      || escandallos, para nEsKit
     agifa553 #553;
     dsm00005 #905;
     dsm00010 #910;
     referen@;

     agifa073 #73;       ||Pedidos Ventas Lins.
|FIN;

|VARIABLES;
     &enModoCab10 = 0;
     nSal = 0;
     aTmp22 = "";
     &enUniTagRes = 0;
     &eArtChampi = "";
     &enMaxImpo = 0;
     nTotal = 0;
     nGuardaPrecio = 0;
     nObra = 0;
     aTipoEmp = "";
     aSerie = "";
     aLetra = "";
     &eaArticuloHor = "";
     &eaCliPlb = "";
     &enObraPlb = 0;
     &eaTipoPlb = "";
     &enDto = 0;
     aPtec = "";
     nPtec = 0;
     nLin = 0;
     &eaModulo  = "";

     nTecla     = 0;
     &eaIVAsn   = "";
     nConAu     = 0;
     &nMasRies  = 0;
     nTries     = 0;
     nAudito    = 0;
     aUs        = "";
     &nRieTotal = 0;
     &nRieUtili = 0;
     &aRieBloqu = "";
     &aBloquead = "";
     &eaTag     = "";
     &Tempo        = "";
     aTempo294     = "";
     &enUniMarhu   = 0;
     nModo         = 0;
     nTotRiesgo    = 0;
     aMes          = "";
     aAnyo         = "";
     fFecha        = @;
     nPosicion     = 0;

     nCamp1        = 0;
     nCamp2        = 0;
     nCamp3        = 0;
     &enDescuento1 = 0;
     &enDescuento2 = 0;
     &eaTComision  = "";
     &enComision   = 0;

     &nDeci_Base     = 0;   || Decimales en Totales de la moneda base
     &nDeci_PreBase  = 0;   || Decimales en el Precio en moneda base
     &nDeci_BaseMx   = 0;   || Decimales en Totales de la moneda base (Maxima precision)
     &nDeci_PreBaseMx= 0;   || Decimales en el Precio en moneda base (Maxima precision)
     &nDeci_Div      = 0;   || Decimales en Totales de la divisa
     &nDeci_PreDiv   = 0;   || Decimales en el Precio en divisa
     &nDeci_TotVis   = 0;   || Decimales en Totales visibles (el de la moneda de trabajo)
     &nDeci_TotNoVis = 0;   || Decimales en Totales no visibles (el de la otra moneda)
     &nDeci_PreVis   = 0;   || Decimales en el Precio visual. (lineas)
     &nDeci_ImpVis   = 0;   || Decimales en el Importe visual. (lineas)
     &ac_divisa = ""; ||
     &pintado   = 0;|| Controla el pintado de mensa para que solo lo haga 1 vez
     &Envase    = "";
     &Unidades1 = 0;
     &Unidades2 = 0;
     &VArticulo = "";
     prevul     = 0;
     precom     = 0;
     prepcm     = 0;
     &programa      = "";
     &sserie        = "";
     &nnumer        = 0;
     &ffecha        = @;
     &cclien        = "";
     &nnombr        = "";
     &consulta      = "";
     &ccoste        = 0;
     &arti          = "";
     &unid          = 0;
     &desc          = "";
     &ext1          = "";
     &c_clien       = "";
     &c_artic       = "";
     &c_serie       = "";
     &entrada       = 0;
     &serfot        = "";
     &numfot        = 0;
     &linfot        = 0;
     &clifot        = "";
     &artfot        = "";
     &aRecoge = "N";
     &aModalidad    = "V";  || Ventas
     &aCliPro  = "";
     &aProceso = "vigm0007";
     &n_dec = 0;
     &uni_dt = 0;   || Unidades
     &fed_dt = @;   || Fecha....
     &art_dt = "";  || Articulo.
     &cli_dt = "";  || Cliente..
     &fam_dt = "";  || Familia.
     &iva_dt = 0;   || Tipo Iva.
     &dtos_2 = 0;   || Descuento 2.
     &dto_pt = 0;   || Descuento Ptas.
     &dtos_1 = 0;   || Descuento 1.
     &pvp_ve = 0;   || Precio Venta.
     &tarifa = 0;   || Tarifa Cliente.
     &quedto = 0;   || 1=cli./art, 2=cli./familia, 3=cli./subfam.
     &gru_dt = "";  || Grupo Cliente.
     &nPromo = 0;
      verpre = "";
     &enMensaje = 0;
     &aParam    = "";

     aAlfa     = "";
     nPrecio    = 0;
     nForma     = 0;
     nNume      = 0;

     tecla      = "";
     mensa      = ""; || Linea de visualizacion

     titdiv = "";   || Encabezado de precio e imp. con la divisa "Imp.Tot(USD)"
     vcontrol = "";
     salidavic = 0;
     Unidades       = 0;
     sw_escan       = 0;
     mod            = 0;
     nlote          = "";
     tu             = 0;
     ind            = 0;
     {100}M         = "";
     {100}C         = "";
     hn             = 0;
     hn1            = 0;
     nn             = 0;
     nn1            = 0;
     hi             = 0;
     j              = 0;
     longi          = 0;
     halfa          = "";
     letra          = "";
     pos            = 0;
     resto          = 0;
     inc            = 0;
     numero         = 0;
     lotes          = "";
     lineaa         = 0;
     codiBarra      = "";
     i              = 0;
     ccomi          = 0;
     repr           = "";
     clie           = "";
     fami           = "";
     lin            = 0;
     path           = "";
     art            = "";
     varalfa        = "";
     fs             = 0;
     f1             = 0;
     jj             = 0;
     aux            = "";
     mensaje10      = "2400SPrepare el PSION !! Correcto (S/N): ";
     avisa_st       = "S";    || Avisa si hay stock
     recoge         = "N";    || Recoger Albaranes
     def_exp        = "N";    || Exportacion
     DescAmpli      = "N";    || Descarga descripcion ampliada de articulos          = 0;      || Decimales en unidades
     def_alm        = 0;      || Almacen Defecto
     aprograma      = "";
     fiac           = "  ";
     guarda         = "  ";
     relleno        = "                                       ";
     aponer         = "  ";
     vacio          = "                    ";
     operacion      = "  ";
     operainte      = "  ";
     articulo       = " ";
     mescli         = " ";
     mensaje3       = "";
     mensaje5       = "0000 Albaran para Exportacion. Tipo IVA = 0 !!!";
     serie          = "";
     albaran        = 0;
     albaran_o      = 0;
     serie_o        = "";
     alfa1          = "";
     alfa2          = "";
     descu          = 0;
     tf             = 0;
     pc             = 0;
     pcs            = 0;
     pcv            = 0;
     pcd            = 0;
     comi1          = 0;
     comi2          = 0;
     comi3          = 0;
     menor          = 0;
     pc1            = 0;
     pa             = 0;
     pa1            = 0;
     pa2            = 0;
     imneto         = 0;
     imnetoD        = 0;
     asto           = 0;
     margen         = 0;
     wimneto        = 0;
     wmargen        = 0;
     a              = 0;
     b              = 0;
     modo           = 0;  || La necesito en solis008
     cantidad       = 0;
     kl             = 0;
     totbu          = 0;
     flag           = 0;
     primera        = 0;
     swescan        = 0;
     sw_promocio    = 0;
     nlinea         = 0;
     linea_o        = 0;
     LineaD         = 0; || Linea destino
     campo          = 0;
     nerror         = 0;
     atmp           = 0;
     salida         = "";
     n_almacen      = 0;
     n_stock        = 0;
     n_operac       = "";
     &qArti         = "";
     &qUnid         = 0;
     &qAbono        = "";
     nGuarda        = 0;
     nEsKit         = 0;
     &nDispoKit     = 0;
     &eaProg        = "";
|FIN;

|INCLUYE i_varart;

|PROCESO Calc071; |TIPO 0;
     |SI DescAmpli = "S";
          aTipoDA = "p"; |HAZ DescriDA;
     |FINSI;
|FPRC;

|PROCESO TeclaPre;
     salida = FSalida;
     |SI salida = 12;
          |SI Campo = 6; |O Campo = 36;
               |C_V #0#9, 0, aAlfa;
               |SI aAlfa = "S";
                    nCamp1 = 9; nCamp2 = 6;
                    |C_M #0#9, 1,"S"; |ENCAMPO #9#0; |C_M #0#9, 1, "N";
               |FINSI;
               |C_V #0#37,0, aAlfa;
               |SI aAlfa = "S";
                    nCamp1 = 37; nCamp2 = 36;
                    |C_M #0#37, 1, "S"; |ENCAMPO #37#0; |C_M #0#37, 1, "N";
               |FINSI;
               |SI FSalida = 0;
                         || nCamp1 -> importe
                         || nCamp2 -> precio
                         || nCamp3 -> unidades
                    |SI eaIVAsn = "S";
                         #0nCamp1 = #0nCamp1 / (1+(enIva/100))
                    |FINSI;
                    |SI #2#194 = 2;
                         nCamp3 = 48;   || unidades doble stock S/N
                    |SINO;
                         nCamp3 = 5;
                    |FINSI;
                    |SI #0nCamp3 = 0;        || unidades cero
                         #0nCamp3 = #0nCamp1 / #0nCamp2;
                         |HAZ unidad;
                    |FINSI;
                    |SI #0nCamp2 = 0;        || precio cero
                         #0nCamp2 = #0nCamp1 / #0nCamp3;
                         |PINTA #0nCamp2;
                    |FINSI;
               |SINO;
                    |AVISO; |ERROR;
               |FINSI;
          |FINSI;
          salida = 0;
     |FINSI;
     |SI salida = 14;
          c_clien = #1#3;
          c_artic = #0#2;
          c_serie = #1#52;
          entrada = 1;
          |SUB_EJECUTA_NP "agifa283.run";
          |ERROR;
          salida = 0;
     |FINSI;

     |SI salida = 15;
          eaCli = "";
          enTiva = #0#11;
          efFiva = #1#1;
          |HAZ IVACli;
          |SI #1#70 = "D";
               #0#36 = #0#36 / (1 + enIva/100);
          |SINO;
               #0#6 = #0#6 / (1 + enIva/100);
          |FINSI;
          |HAZ visual_precio;
          salida = 0;
     |FINSI;

     |SI salida = 14;
          c_clien = #1#3;
          c_artic = #0#2;
          c_serie = #1#52;
          entrada = 1;
          |SUB_EJECUTA_NP "agifa283.run";
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO LeeParam071;
     |ABRE #20; |LEE 000#20p; |CIERRA #20;

     lotes = #20#51;
     avisa_st = #20#5;
     recoge = #20#27;
     DescAmpli = #20#198;     || Descargar descripcion ampliada
     |SI DescAmpli = "N";
          DescAmpli = #20#40;
     |FINSI;
     codiBarra = #20#55;     || Lectura Cod. Barra
     verpre = #20#101;       || Para ver PC,PMC,PUV.
     |ABRE #21;
     #21#26 = #1#52;
     |LEE 001#21=;
     |SI FSalida ! 0; |DDEFECTO #21; |FINSI;
     |CIERRA #21;
     def_alm = #21#29;
     def_exp = #21#30;
|FPRC;

|PROCESO PintaLinArt;
     |ABRE #2;
     #2#0 = #0#2;
     |LEE 000#2=;
     |CIERRA #2;
     |DDEFECTO #553;
     |ABRE #553;
     #553#0 = #2#125;
     |LEE 000#553=;
     |CIERRA #553;
     |PINTA 2303, #553#1;
|FPRC;

|PROCESO Tipo7_071; |TIPO 7;
     modo = (FEntrada / 100) ! 0;
     |HAZ LeeParam071;
     |HAZ PintaMedidaV;

     |MODO_RELACION #2, 1, "agifa019", "S", "S";
     |MODO_RELACION #8, 1, "agifa017", "S", "S";

     |ABRE #agifa321;
     |LEE 001#agifa321.p;  || Leo parametros de divisa
     |SI ac_divisa = "S"; |Y pintado = 0; || Si divisas estan activadas y no he pintado ...
       |SI #1#70 = "D";   || Si la moneda de trabajo es la divisa la pinto en
         |ATRI R;         || el encabezado del precio y el importe
         titdiv = "Precio" + "(" + #1#68 + ")";
         |PINTA 1444,titdiv;
         titdiv = "I.Tot." + "(" + #1#68 + ")";
         |PINTA 1468,titdiv;
         |ATRI r;
       |FINSI;
       |SI #agifa321#3 = "S";  || Si la visualizacion esta activada ...
         mensa = "Prec:             Imp.:              T.Bruto:             T.ALB:            ";
         |PINTA 1103,mensa;  || ... pinto la linea de visualizacion
         |PINTA #0#38; || Pinto precio visual
         |PINTA #0#39; || Pinto importe visual
         |SI #1#70 = "D"; || Si la moneda de trabajo es la divisa
           |PINTA 1149,#1#79;
           |PINTA 1167,#1#80;
         |SINO;
           |PINTA 1149,#1#75;
           |PINTA 1167,#1#76;
         |FINSI;
         pintado = 1;  || Ya hemos pintado
       |FINSI;
     |FINSI;
     |CIERRA #agifa321;

     |SI modo = 1;
          #0#1 = #0#1 + #agifa142#236 - 1;
     |FINSI;
|FPRC;

|PROCESO mirespor;|TIPO 0;
     |SI #6#28 = "S"; |Y #0#11 ! 0;
          |MENAV mensaje5; #0#11 = 0; |PINTA #0#11;
     |SINO;
          |SI #0#11 ! 0; |Y def_exp = "S";
               |MENSA mensaje5; #0#11 = 0; |PINTA #0#11;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO liinfo;|TIPO 0;
     eaArticulo = #vigm0007#2;
     |HAZ EsKit;
     nEsKit = FSalida;

     |SI verpre ! "S";
          mensaje3 = "0410Stock Disponible Almacen : " + #8#39+"     Stock Minimo : "+#8#6;
          |MENSA mensaje3;
     |SINO;
          |HAZ verpre2;
          precom = #2#133 / #1#74 * #1#69;  || Calculo el importe en divisas
          prepcm = #2#9 / #1#74 * #1#69;    || Calculo el importe en divisas
          |SI #1#70 = "D";                     || Si la moneda de trabajo es la divisa ...
               precom = precom ! nDeci_PreDiv;
               prepcm = prepcm ! nDeci_PreDiv;
         |SINO;                               || Si la moneda de trabajo es la moneda base ...
               precom = precom ! nDeci_PreBase;
               prepcm = prepcm ! nDeci_PreBase;
         |FINSI;
         mensaje3 = "0401Disponible.: [" + #8#39 + "] Min.: [" + #8#6 + "] PC.: [" + precom + "] PMC.: [" +prepcm+"] PVU.: [" + prevul+"]";
         mensaje3 = mensaje3 % 180;
         |MENSA mensaje3;
     |FINSI;
     |HAZ SacaKit;
|FPRC;

|PROCESO liinfo1;|TIPO 0;
     |SI #1#43 = "N";
          |SI avisa_st = "S"; |O avisa_st = "X";
               eaArticulo = #vigm0007#2;
               |HAZ EsKit;
               nEsKit = FSalida;
               |SI nEsKit = 0;
                    |HAZ DispoKit;
                    |SI nDispoKit ! 0;
                         |MENAV "0000Superado Stock para KIT.";
                    |FINSI;
               |SINO;
                    |ABRE #8;
                    #8#0 = #0#2;
                    #8#1 = #0#3;
                    |LEE 000#8=;
                    |CIERRA #8;
                    |SI #8#39 < #0#5;
                         aAlfa = "0000Supera el Stock disponible: " + #8#39;
                         |MENAV aAlfa;
                         |SI avisa_st = "X"; |ERROR;   |FINSI;
                    |FINSI;
               |FINSI;
          |FINSI;
          nStock = #8#5 - #0#5;
          |SI #8#6 > nStock; |Y #agifa142#219 = "S";
               |MENAV "0000Supera el Stock minimo...";
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO licogea;|TIPO 0;
      articulo = #0#2;
      eaArtiAntes = #0#2;
      enAlmaAntes = #0#3;
|FPRC;

|PROCESO PrecioArti;
     modo = (FEntrada / 100) ! 0;
     fed_dt = #1#1;   || Fecha....
     art_dt = #0#2;   || Articulo.
     cli_dt = #0#15;  || Cliente..
     fam_dt = #0#13;  || Familia.
     iva_dt = #2#30;  || Tipo Iva.
     dtos_2 = 0;      || Descuento 2.
     dto_pt = 0;      || Descuento Ptas.
     dtos_1 = 0;      || Descuento 1.
     pvp_ve = 0;      || Precio Venta.
     tarifa = #6#39;  || Tarifa Cliente....
     gru_dt = #6#158; || Grupo Cliente.
     nPromo = #1#89;
     |SI #0#35 ! 0;
          uni_dt = #0#35;
     |SINO;
          uni_dt = #0#5;
     |FINSI;
     enMensaje = 0;
     aParam = "";
     eaSerie = #0#29;
     enDirEnt = #1#84;
     enAlmacen = #0#3;
     |SI modo = 1; |O articulo ! #0#2;
          |SI #2#176 = "S";  enMensaje = 1;  |FINSI;
          |HAZ calcdtos;              || Calculo Dtos.....
          #0#6   = pvp_ve - dto_pt;   || Precio Venta. sin iva.
          #0#8   = dtos_1;            || 1 Dto.
          #0#33  = dtos_2;            || 2 Dto.
          |HAZ precio_divisa;  ||Convierte el precio en divisa
          |PINTA #0#33;
          |PINTA #0#8;

         enDescuento1 = #0#8;
         enDescuento2 = #0#33;
         eaTComision  = #0#16;
         eaCliente    = #1#3;
         eaArticulo   = #0#2;
         eaRepre      = #1#6;
         efFecha      = #1#1;
         enDirEnt = #1#84;
         enAlmacen = #0#3;
         |HAZ Comisiones;
         #0#8 = enDescuento1;
         #0#10 = enComision;
         #0#6 = pvp_ve - dto_pt;   || Precio Venta. sin iva.
         |HAZ precio_divisa;       ||Convierte el precio en divisa
         |PINTA #0#10;
         |PINTA #0#8;
     |FINSI;
|FPRC;

|PROCESO defecli;|TIPO 0;
     #0#15 = #1#3;
     #0#16 = #1#34;
     #0#17 = #1#35;
     |SI #0#2 ! #2#0;
          |ABRE #2;
          #2#0 = #0#2;
          |LEE 000#2=;
          |CIERRA #2;

          |ABRE #8;
          #8#0 = #0#2;
          #8#1 = #0#3;
          |LEE 000#8=;
          |CIERRA #8;
     |FINSI;
     #0#13 = #2#2;
     |SI #0#15 ! #6#0;
          |ABRE #6;
          #6#0 = #0#15;
          |LEE 000#6=;
          |CIERRA #6;
     |FINSI;
     |HAZ mirespor;
     |HAZ PrecioArti;
|FPRC;

|PROCESO licomis;|TIPO 0;
     |SI #0Campo ! Contenido;
         enDescuento1 = #0#8;
         enDescuento2 = #0#33;
         eaTComision  = #0#16;
         eaCliente    = #1#3;
         eaArticulo   = #0#2;
         eaRepre      = #1#6;
         enDirEnt = #1#84;
         enAlmacen = #0#3;
         |HAZ Comisiones;
         #0#10 = enComision;
         |PINTA #0#10;
     |FINSI;
|FPRC;

|PROCESO unidad; |TIPO 6;
     nModo = (FEntrada / 100) ! 0;
     nTecla = FSalida;
     |HAZ Promart;
     |SI nTecla = 13;
          enTipEnvase = 1;
          |HAZ Envase71;
     |FINSI;
     |SI nTecla = 14;             ||F6
          eaAuto = "N";
          |HAZ Equivalentes;
          |ERROR;
          |ACABA;
     |FINSI;

     #0#5 = #0#5 ! n_dec;
     |PINTA #0#5;
     |SI #0#5 [ 0; |O #1#43 = "S"; |ACABA; |FINSI;
     qArti = #0#2;
     qUnid = #0#5;
     |HAZ ComproMin;
     |SI enErr ! 0;
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO Riesgos71;
|FPRC;

|PROCESO liacarti; |TIPO 2;
     || *(2)
     nForma = 0 +. 1;
     modo = (FEntrada / 100) ! 0;
     |SI modo = 2;|O modo = 3;
          efFec = #1#1;
          enAlma = #0#3;
          |HAZ MiraHisL;
          |SI enErrHis ! 0;
               |ERROR;
               |ACABA;
          |FINSI;
     |FINSI;
     |SI #0#19 = "S"; |O #0#20 = "S";|O #1#38 = "S"; |O #1#39 = 999999;
          |SI nForma < 0;
              |SI #0#19 = "S";
                  |MENAV "0000Esta linea proviene de un Pedido (use la opcion adecuada)";
              |FINSI;

              |SI #0#20 = "S";
                  |MENAV "0000Esta linea proviene de un Deposito (use la opcion adecuada)";
              |FINSI;

              |SI #1#38 = "S";
                  |MENAV "0000Este albaran esta FACTURADO";
              |FINSI;

              |SI #1#39 = 999999;
                  |MENAV "0000Este albaran esta en modo Exportacion";
              |FINSI;
          |FINSI;
          |ERROR;
          |ACABA;
     |FINSI;
     |SI modo = 3;
          |ABRE #agifa073;
          |DDEFECTO #agifa073;
          #agifa073#28 = #0#31;
          #agifa073#0 = #0#12;
          #agifa073#1 = #0#21;
          |LEE 101#agifa073.=;
          |SI FSalida = 0;
               #agifa073#43 = #agifa073#43 - #0#5;
               |GRABA 020#agifa073.a;
               |LIBERA #agifa073;
          |FINSI;
          |CIERRA #agifa073;
     |FINSI;

     |SI #0#9 > 0;
         nerror = 1;
     |SINO;
         nerror = -1;
         #0#9  = -#0#9;
         #0#37 = -#0#37;
     |FINSI;

     imneto  = ((((#0#9  < #1#5) < #1#32) < #1#7)) * nerror;
     imnetoD = ((((#0#37 < #1#5) < #1#32) < #1#7)) * nerror;
     #0#9    = #0#9  * nerror;
     #0#37   = #0#37 * nerror;

     enTipEnvase = 2;
     |HAZ Envase71;

     #0#14 = enCoste;
     #1#37 = #1#37 + (imneto - enCoste);

     |SI modo = 1; |O modo = 2;
||         |HAZ grapre;                  || Graba Precio Cliente/Articulo
     |FINSI;

     |HAZ CalCabVigor;
     |HAZ visual_totales;

     cclien = #1#3;
     unid   = #0#5;
     desc   = #0#4;
     lineaa = #0#1;

     |SI modo = 3;
          ||컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
          aTipoDA = "p"; |HAZ BajaDA;
          ||컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
     |FINSI;
|FPRC;

|PROCESO Tipo3_071;|TIPO 3;
     aAlfa = #0#2;
     |SI aAlfa ! ""; |Y DescAmpli = "S";
          aTipoDA = "p"; |HAZ AltaDA;
     |FINSI;
|FPRC;

|PROCESO pantalla; |TIPO 13;
     |HAZ visual_totales;
|FPRC;

|PROCESO art6alv; |TIPO 6;          || Antes de la Relacion 4 Clave.

     |SI codiBarra = "N"; |ACABA; |FINSI;

     |ABRE #36;
     |SELECT #2#36;
     #36#3 = #0#2;
     |LEE 010#36=;
     |SI FSalida=0; |Y #0#2 ! #36#1;
          #0#2 = #36#1;
          |PINTA #0#2;
          |ABRE #2;
          #2#0 = #0#2;
          |LEE 000#2=;
          |CIERRA #2;
     |FINSI;
     |CIERRA #36;
|FPRC;

|PROCESO nomodifi; |TIPO 0;

     |SI #0#2 = vacio;
          |C_M #0#3,  1, "N";
          |C_M #0#5,  1, "N";
          |SI #1#70 = "B";
            |C_M #0#6,  1, "N";
          |SINO;
            |C_M #0#36,  1, "N";
          |FINSI;
          |C_M #0#8,  1, "N";
          |C_M #0#33,  1, "N";
          |C_M #0#10, 1, "N";
          |C_M #0#11, 1, "N";
          |C_M #0#27, 1, "N";
     |SINO;
          |C_M #0#3,  1, "S";
          |C_M #0#5,  1, "S";
          |SI #1#70 = "B";
            |C_M #0#6,  1, "S";
          |SINO;
            |C_M #0#36,  1, "S";
          |FINSI;
          |C_M #0#8,  1, "S";
          |C_M #0#33, 1, "S";
          |C_M #0#10, 1, "S";
          |C_M #0#11, 1, "S";
          |C_M #0#27, 1, "S";
     |FINSI;
|FPRC;

|RUTINA ClaveBaja;
     #agis0002#30 = "A";
     #agis0002#20 = #vigm0007#29;
     #agis0002#17 = #vigm0007#0;
     #agis0002#18 = 1;
|FRUT;

|RUTINA ClaveAlta;
     #agis0002#30 = "A";
     #agis0002#20 = #vigm0007#29;
     #agis0002#17 = #vigm0007#0;
     #agis0002#18 = 99;
|FRUT;

|PROCESO leeotro; |TIPO 11;
     nModo = (FEntrada / 100) ! 0;
     salida = FSalida;
     |SI salida = 10;     ||F2
          |HAZ MenuTrack71;
          |SI FSalida = 0; |ERROR; |ACABA; |FINSI;
          salida = FSalida;
     |FINSI;
     |SI salida = 9;
          |PUSHV 501 2380;
          |PINPA #0#1;
          |PINDA #0#1;
          |PAUSA;
          |POPV;
          |ERROR; |ACABA;
     |FINSI;
     |SI FSalida = 13; || F5
          |HAZ VerKit;
          eaArticulo = #0#2;
          enAlmacen = #0#3;
          eaCliente = #1#3;
          efFecha = #1#1;
          enUnidad1 = #0#5;
          enUnidad2 = #0#48;
          eaPartida = #0#49;
          enMedida1 = #0#51;
          enMedida2 = #0#52;
          enMedida3 = #0#53;
          |HAZ VerDobleUniV;
          |ERROR; |ACABA;
     |FINSI;
     aAlfa = #0#2; |QBF aAlfa;
     |SI salida = 16; |Y aAlfa ! ""; |Y DescAmpli = "S";
          aTipoDA = "p"; |HAZ VerDA;
          |ERROR;
     |FINSI;

     |C_P #0#1 ,0,atmp;
     |SI salida = 12; |O salida = 17;     || F4 o F9
         |SI salida = 12; |Y recoge ="N"; ||F4
             |PUSHV 0400 0479;
             |MENAV "0000Opcion no activada en Parametros Generales";
             |POPV;
             |ERROR;
             |ACABA;
         |FINSI;
          modo = (FEntrada/100) ! 0;
          || compruebo que este en la ultima linea y en modo alta
          LineaD = #0#1;    || Guardo puntero
          LineaD = LineaD +1;      || Porque si estoy en alta la linea en la que
                                   || estoy todavia no esta grabada
          |LEE 001#0];
          |SI FSalida ! 0;|O #0#0 ! #1#0;
            |O #0#29 ! #1#52;|Y modo = 1;
               LineaD = LineaD -1;
               |||LEE 001#0[;  || Retrocedo
               || Recupero el puntero
               #0#0  = #1#0;    || Albaran
               #0#29 = #1#52;   || Serie
               #0#1  = LineaD;         || Numero de linea
               |LEE 001#0=;
               nerror = 0;
               |SI salida = 12; ||F4
                  |HAZ entradatos;
               |FINSI;
               |SI nerror = 0 ;
                    |SI salida = 12; ||F4
                       |HAZ leelineas;
                    |FINSI;
                    |HAZ control2;
                    || Le meto 3 ptec's para cuando cambia de pantalla
                    |PTEC 809;    || Flecha abajo
                    |PTEC 809;    || Flecha abajo
                    |PTEC 809;    || Flecha abajo
               |SINO;
                    |ERROR;
               |FINSI;
          |SINO;
               || Recupero el puntero
               LineaD = LineaD -1;
               #0#0  = #1#0;    || Albaran
               #0#29 = #1#52;   || Serie
               #0#1  = LineaD;         || Numero de linea
               |LEE 001#0=;
               |MENAV "0000Debe estar en la ultima linea";
               |ERROR;
          |FINSI;    || Si #0#1 = 1 ...
          salida = 0;
     |FINSI;         || Si FSalida ...
     |SI salida = 18;
          aAlfa = "dsw00007;" + #1#3;    || Extracto Articulo
          |SUB_EJECUTA_NP aAlfa;
          |ERROR;
     |FINSI;

     |SI salida = 15;   ||F6
          |CONFI mensaje10;
          |SI FSalida = 0;
               |HAZ CreaTempo; aTmp22 = Tempo;
               |NOME_DAT #22 aTmp22; |DELINDEX #22;

               programa = "/ds/bin/capser";
               |RSYSTEM programa;              ||Captura Datos Psion
               |HAZ prepaps;
               |PUSHV 1213 2380;
               |SUB_EJECUTA_NP "capsion.run";
               |HAZ genelin;
               |POPV;
               |PTEC 807;

               Tempo = aTmp22; |HAZ BoraTempo; |DELINDEX #22;
          |FINSI;
          salida = 0;
     |FINSI;

     |SI salida = 11;   ||F3
          modo = (FEntrada / 100) ! 0;
          |SI modo = 4;
               |ABRE #6;
               #6#0 = #1#3;
               |LEE 001#6=;
               |SI FSalida ! 0; |DDEFECTO #6; |FINSI;
               |CIERRA #6;
          |FINSI;
          |PUSHV 0501 2380;
          ext1 = #6#16;
          |SUB_EJECUTA_NP "caextrap.run";
          |POPV;
          salida = 0;
     |FINSI;
     |SI salida = 25; || Ctrl-F7
          |HAZ CreaTempo;
          eaFichero = Tempo;
          |SUB_EJECUTA_NP "psion066";
          |HAZ BoraTempo;
          |SI enError = 0; |HAZ ImportaPDA71; |FINSI;
          |ERROR; |ACABA;
     |FINSI;
|FPRC;

|| Recojo la serie y numero de albaran
|PROCESO entradatos;
         |PINTA 2420 , " Recogida de datos del albaran :      /    ";
         alfa1 = "";
         E_sup = 5;
         E_inf = "";
         alfa1 = 2453 ? 1;
         serie_o = alfa1;
         alfa2 = "";
         E_sup = 6;
         E_inf = "";
         alfa2 = 2459 ? 1;
         albaran_o = alfa2;
         |BLIN 24;

         ||No puedo leer en el que estoy sino se embuclaria
         |SI serie_o = #1#52;|Y albaran_o = #1#0;
             |MENAV "0000ATENCION !!! Esta en este albaran";
             nerror = 1;
         |FINSI;
|FPRC;

|PROCESO control1;
   aponer= Asino;
   |HAZ poncontr;
|FPRC;

|PROCESO control2;
   aponer= Anosi;
   |HAZ poncontr;
|FPRC;

|PROCESO poncontr;
         fiac = Control % A109;
         fiac = fiac + relleno;
         guarda = fiac % A109;
         fiac = Control % A1001;
         Control = Control + fiac;
         Control = Control + relleno;
         Control = Control % A120;
         Control = Control + aponer;
|FPRC;

|PROCESO CreaComponente;
     #905#0 = "AV";
     #905#1 = #0#29;
     #905#2 = #0#0;
     #905#3 = #0#1;
     #905#9 = #905#6;
     |GRABA 020#905n;
     |LIBERA #905;

     enUnidades   = #905#6;
     eaTipo       = "AV";
     eaSerie      = #0#29;
     enCodigo     = #0#0;
     enLinea      = #0#1;
     efFecha      = #1#1;
     eaArticulo   = #0#2;
     enAlmacen    = #0#3;
     eaComponente = #905#4;
     efFecha      = #1#1;
     enCamBas     = #1#69;
     eaMoneda     = #1#70;
     enCamDiv     = #1#74;

     |SUB_EJECUTA_NP "dsz00002;AVC";
|FPRC;

|DEFBUCLE dsm00005;
     #905#1;
     ;
     "AV", serie, albaran, nlinea, "      ";
     "AV", serie, albaran, nlinea, "zzzzzz";
     be;
     NULL, CreaComponente;
|FIN;

|PROCESO crealineasV;
    || Clave primaria
    #0#29= #1#52;    || Serie
    #0#0 = #1#0;     || Numero de Albaran
    #0#1 = LineaD;          || Numero de linea


    |DEFECTO #0#1;   || Cargo los valores de defecto de cab.albaran

    || Procedo a cambiar lo que no es igual
    #0#12 = "";             || Numero de pedido
    || Controles de procedencia, si vienen de pedido, deposito, etc..
    |PARA i = 18;|SI i [ 25;|HACIENDO i = i +1;
       #0i="";
    |SIGUE;
    #0#31 ="";    || Serie pedido
    #0#32 ="";    || Serie deposito


    |HAZ TotalLinVigor;
    |HAZ liacarti;     || Lo envio al tipo 2 para que calcule lo mismo que si fuera
                       || una linea introducida por el operador
    |GRABA 021#0b;
    |LIBERA #0;

    |BUCLE dsm00005;

    LineaD =LineaD + 1;  || Aumento contador de lineas
|FPRC;

|PROCESO leelineas;
      #0#29= serie_o;
      #0#0 = albaran_o;
      #0#1 = 1;
      |LEE 001#0];
      |SI FSalida = 0;
           |PARA;|SI FSalida = 0;|Y #0#29=serie_o;|Y #0#0=albaran_o;
                                                     |HACIENDO;
                  || Me guardo el puntero
                  serie   = #0#29;   || Serie y albaran actual
                  albaran = #0#0;
                  nlinea  = #0#1;

                  |HAZ crealineasV;

                  || Recupero puntero
                  #0#29 = serie;
                  #0#0  = albaran;
                  #0#1  = nlinea;
                  |LEE 001#0=;

                  |LEE 001#0s;
           |SIGUE;
      |SINO;
           |MENAV "0000Atencion !!! No se ha encontrado el albaran indicado";
           |ERROR;
      |FINSI;

      || Devuelvo el puntero de la linea
      #0#29= #1#52; || Serie
      #0#0 = #1#0;  || Albaran
      #0#1 = LineaD;       || Linea
      |LEE 001#0=;
|FPRC;

|PROCESO leeps;|TIPO 0;
     |SI #1#43 = AN;
        a = -1; b = 1;
        operacion = "AV";
        operainte = "AC"; || Si es una Serie Interna
     |SINO;
        a = 1;  b = -1;
        operacion = "BV";
        operainte = "BC"; || Si es una Serie Interna
     |FINSI;

|FPRC;

|PROCESO leelin;
     |ABRE #2;
     #2#0 = #22#2;
     |LEE 000#2=;
     |CIERRA #2;

     lin = lin + 1;
     |DDEFECTO #0;
     #0#0 = #1#0;
     #0#29 = #1#52;
     #0#1 = lin;
     #0#2 = #22#2;
     #0#3 = 1;
     #0#4 = #22#3;
     #0#5 = #22#4;
     #0#11 = #2#30;

     |HAZ defecli;
     |HAZ mirespor;
     |HAZ TotalLinVigor;
     |HAZ liacarti;
     |GRABA 121#0c;
     |LIBERA #0;
|FPRC;

|DEFBUCLE leelin;
     #22#1;
     5;
       1 ,    1 , "S";
     999 , 9999 , "S";
     be;
     NULL, leelin;
|FIN;

|PROCESO genelin;
     lin = 0;
     |BUCLE leelin;
|FPRC;

|PROCESO prepaps;          ||Preparacion Lineas Recogidas Del Psion
  lin = 0;
  path = "";
  |DFICE path;
  |QBF path;
  art ="rt  " + path + "ventas";
  |QBF art;
  |FABRE art;
  |SI FSalida < 0;  |ACABA;  |FINSI;
  f1 = FSalida;
  Pos = -1;
  varalfa = "";
  fs = 1;
  |PARA jj = 1; |SI fs ] 0;|HACIENDO jj = jj + 1;
        aux = "";
        varalfa = f1 + " 80";
        |FLEE varalfa;
        |SI FSalida < 0;  |SAL;  |FINSI;
        Pos = -1;
        aux = varalfa % 0104;   ||Numero Venta
        |QBF aux;
        |SI aux ! "    ";|Y aux ! "";
            lin = lin + 1;
            |ABRE #22;
            #22#0 = lin;
            #22#1 = varalfa % 0104;   ||Numero Venta
            #22#2 = varalfa % 0613;   ||Codigo articulo
            aux = varalfa % 2107;     ||Unidades Venta
            |QBF aux;
            #22#4 = aux;
            aux = "";
            |ABRE #2;
            #2#0 = #22#2;
            |LEE 001#2=;
            #22#3 = #2#1;             ||Descripcion
            |CIERRA #2;
            |GRABA 000#22c;
            |LIBERA #22;
            |CIERRA #22;
        |FINSI;
  |SIGUE;
  varalfa = f1;
  |FCIERRA varalfa;
  Pos = -1;
|FPRC;

|PROCESO Envase71;
     qAbono    = #1#43;
     Envase    = #0#34;
     Unidades1 = #0#35;
     Unidades2 = #0#5;
     VArticulo = #0#2;
     enAlmacen = #0#3;
     eaTipo = "V";
     eaProgEnv = "albaran";
     |HAZ EntraEnvase;
     #0#34 = Envase;
     #0#35 = Unidades1;
     #0#5  = Unidades2;  |PINTA #0#5;
|FPRC;

|PROCESO Conversion;  |TIPO 7;
     aAlfa = #0#2; |QBF aAlfa;
     |SI aAlfa = ""; |ACABA; |FINSI;

     eaArticulo = #0#2;
     enAlmacen = #0#3;
     eaCliente = #1#3;
     efFecha = #1#1;
     enUnidad1 = #0#5;
     enUnidad2 = #0#48;
     eaPartida = #0#49;
     enMedida1 = #0#51;
     enMedida2 = #0#52;
     enMedida3 = #0#53;
     |HAZ DobleUniV;
     |SI eaSw2Stock = "S";
          |C_M #0#5, 1, "N";
          #0#5 = enUnidad1;
          #0#48 = enUnidad2;
          #0#49 = eaPartida;
          #0#51 = enMedida1;
          #0#52 = enMedida2;
          #0#53 = enMedida3;
     |FINSI;

     |SI #agifa142#72 = "S";
          |HAZ Envase71;
          |C_M #0#5, 1, "S";
          |SI enEntraEnvase ! 0; |C_M #0#5, 1, "N";  |FINSI;
     |FINSI;
|FPRC;

|PROCESO grapre;
     |SI nForma = 1;
          cli_dt = #0#15;
          art_dt = #0#2;
          pvp_ve = #0#6;
          dtos_1 = #0#8;
          aParam = "grapre";
          enDirEnt = #1#84;
          enAlmacen = #0#3;
          |HAZ calcdtos;
     |FINSI;
|FPRC;

|PROCESO unidad1;
     |SI #2#176 = "S";
         |SI #1#70 = "D";  || Si la moneda de trabajo es la divisa ...
             #0#36 = nPrecio;
         |SINO;
             #0#6  = nPrecio;
        |FINSI;
    |FINSI;
    |SI #1#70 = "D";  || Si la moneda de trabajo es la divisa ...
        #0#6 = #0#36 / #1#69 * #1#74;  || ... recalculo en funcion de la divisa
    |SINO;
        #0#36 = #0#6 / #1#74 * #1#69; || .. recalculo en funcion de la moneda base
    |FINSI;
    |HAZ visual_precio; || Visualiza el precio
|FPRC;

|PROCESO precio_divisa;
   #0#36 = #0#6 / #1#74 * #1#69;  || Calculo el precio en divisa
   |HAZ unidad1;
|FPRC;

|PROCESO visual_precio; || Pinta el precio visual.
|SI #1#70 = "D";  || Si la moneda de trabajo es la divisa ...
  #0#38 = #0#6;   || ... el campo visualiz. es el precio en moneda base
  |PINTA #0#36;
|SINO;            || Si la moneda de trabajo es la moneda base ...
  #0#38 = #0#36;  || ... el campo visual. es el precio en divisa
  |PINTA #0#6;
|FINSI;
|SI ac_divisa = "S"; |Y #agifa321#3= "S";
  |PINTA #0#38; || Si las divisas y la visualizacion estan activadas, pinto
|FINSI;
|FPRC;

|PROCESO visual_totales;
     |ABRE #agifa321;
     |LEE 001#agifa321.p;
     |SI #1#70 = "D";
          |SI ac_divisa = "S"; |Y #agifa321#3= "S";
               |PINTA 1149,#1#79;
               |PINTA 1167,"       ";
               |PINTA 1167,#1#80;
          |FINSI;
          |CIERRA #agifa321;
          |PINTA 1353,#1#75;
          |PINTA 1367,#1#76;
     |SINO;
          |SI ac_divisa = "S"; |Y #agifa321#3= "S";
               |PINTA 1149,#1#75;
               |PINTA 1167,"       ";
               |PINTA 1167,#1#76;
          |FINSI;
          |CIERRA #agifa321;
          |PINTA 1353,#1#15;
          |PINTA 1367,#1#67;
     |FINSI;
     |PINTA #0#9;
     |PINTA #0#37;
|FPRC;

|PROCESO Promart;
     |SI nTecla = 16;
          eaArticulo = #0#2;
          eaGrupo = #6#158;
          enAlmacen = #0#3;
          |HAZ PintaPromo;
          |ENCAMPO #5#0;
     |FINSI;

     enPromo = 1;
     |HAZ PrecioArti;
     enPromo = 0;
     ||컴컴컴컴컴컴컴컴컴컴컴컴컴
     articulo = #0#2;
|FPRC;

|PROCESO equiv;
     |SI #agifa142#218 = "S";
          eaArticulo = #0#2;
          enAlmacen = #0#3;
          |SUB_EJECUTA_NP "agifa294.tab;aTempo294";
          |SI enAlmacen = 0;
               |ERROR;
          |SINO;
               #0#2 = eaArticulo; |PINTA #0#2;
               #0#3 = enAlmacen; |PINTA #0#3;
               |ABRE #2;
               #2#0 = #0#2;
               |LEE 000#2=;
               |CIERRA #2;
               #0#4 = #2#1;
               |PINTA #0#4;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO CogeValores71;  |TIPO 7;
     |ABRE #20;
     |LEE 001#20p;
     |SI FSalida ! 0; |DDEFECTO #20; |FINSI;
     |CIERRA #20;

     aAlfa = #0#2; |QBF aAlfa;

     |SI Campo = 3; |Y aAlfa ! "";
         |C_M #0Campo, 1, "S";
         |SI #20#99 = "S";
             |C_M #0Campo, 1, "N";
             #0Campo = 1;  |PINTA #0Campo;
         |FINSI;
     |FINSI;

     |SI #2#176 = "N";  |ACABA;  |FINSI;

     |SI Campo = 3;
         |SI enAlmacen ! 0;
             #0#3 = enAlmacen;  |PINTA #0#3;
         |FINSI;
     |FINSI;

     |SI Campo = 5;
         |SI #2#176 = "S";
             |SI #0#5 ! enUnidades;  Contenido = #0#5;  |FINSI;
             #0#5 = enUnidades;  |PINTA #0#5;
         |FINSI;
     |FINSI;

     |SI Campo = 6;
         |SI nPrecio ! 0;
             #0#6 = nPrecio;  |PINTA #0#6;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO PideArt71;  |TIPO 7;
     nModo = (FEntrada / 100) ! 0;
     |SI #20#140 = "S"; |Y nModo = 1; #0#2 = eaArtAnt; |FINSI;

     |C_M #0#2, 1, "S";
     |C_M #0#3, 1, "N";

     |SI #0#2 ! "                    ";
         |ABRE #2;
         #2#0 = #0#2;
         |LEE 000#2=;
         |SI FSalida ! 0;  |DDEFECTO #2;  |FINSI;
         |CIERRA #2;
         |SI #2#176 = "S";
             |C_M #0#2, 1, "N";
             |C_M #0#3, 1, "N";
             |ACABA;
         |FINSI;
     |FINSI;

     |ABRE #910;
     |LEE 040#910p;
     |SI FSalida ! 0;  |DDEFECTO #910;  |FINSI;
     |CIERRA #910;

     |SI #910#2 [ 2;    |ACABA;  |FINSI;

     eAlfa = #0Campo;
     |SUB_EJECUTA_NP "dsz99990";                 || Por Pantalla

     |SI eAlfa = "-1";  |PTEC 807;  |ACABA;  |FINSI;
     |SI eAlfa = "-2";              |ACABA;  |FINSI;

     eaArticulo = eAlfa;      ||  700000-4215

     #0Campo = eAlfa;
     |PINTA #0Campo;
     |PTEC 802;
|FPRC;

|PROCESO Calc671;  |TIPO 6;
     nTecla = FSalida;
     eaArtAnt = #0#2;
     modo = (FEntrada / 100) ! 0;
     |SI modo = 1;
          #0#3 = def_alm;
          |PINTA #0#3;
     |FINSI;

     |SI #0Campo ! Contenido; |Y Campo = 2;
          eaArticulo = #0#2;
          eaCliente = #1#3;
          |HAZ DefectoMediV;
          #0#51 = enMedida1;
          #0#52 = enMedida2;
          #0#53 = enMedida3;
          |HAZ PintaMedidaV;
     |FINSI;
     |HAZ DobleArtiV;
     |SI eaSw2Stock = "S"; |ACABA; |FINSI;

     enAlta = 1;
     eaArticulo = #0Campo;
     |HAZ LlenaArticulo;
     |SI FSalida = 0;
          #0Campo = eaArticulo;
          |PINTA #0Campo;
     |SINO;
          |ERROR; |ACABA;
     |FINSI;

     |SI modo = 1; |HAZ equiv;  |FINSI;

     |ABRE #2;
     #2#0 = #0#2;
     |LEE 000#2=;
     |SI FSalida ! 0;  |DDEFECTO #2;  |FINSI;
     |CIERRA #2;

     |SI #2#176 = "S";
          eaTipo      = "AV";
          eaSerie     = #0#29;
          enCodigo    = #0#0;
          enLinea     = #0#1;
          efFecha     = #1#1;
          eaArticulo  = #0#2;
          enAlmacen   = #0#3;
          enUnidades  = #0#5;
          enTBruto    = #0#7;
          eaAbono     = #1#43;
          enTarifa    = #6#39;
          eaCliente   = #1#3;
          enCamBas    = #1#69;
          eaMoneda    = #1#70;
          enCamDiv    = #1#74;

          |SUB_EJECUTA_NP "dsz00002";

          #0#3    = enAlmacen;
          nPrecio = enTBruto / enUnidades;

          |PTEC 802;
          |PTEC 802;
          |PTEC 802;
          |PTEC 802;
     |FINSI;

     |HAZ art6alv;
     |HAZ nomodifi;
|FPRC;

|PROCESO Tipo11071;  |TIPO 11;
     salida = FSalida;
     |SI FSalida = 7;  |Y #2#176 = "S";
         |MENAV "      Imposible abortar;  Termine de introducir la linea y efectue una Baja.";
         |ERROR;
     |FINSI;
|FPRC;

|PROCESO verpre2;
     |ABRE #9;
     |SELECT #3#9;
     #9#10 = #1#3;  || Cliente.
     #9#0  = #0#2;  || Articulo.
     #9#1  = "31.12.2300";  || Fecha.
     #9#2  = "AV";  || Operacion.
     prevul = 0;
     |LEE 000#9];
     |SI FSalida = 0;
          |LEE 000#9a;
     |SINO;
          |LEE 000#9u;
     |FINSI;
     |SI FSalida = 0; |Y #9#10 = #1#3; |Y #9#0 = #0#2; |Y #9#2 = "AV";
          prevul = #9#9 / #1#74 * #1#69;  || Calculo el importe en divisas
          |SI #1#70 = "D";                     || Si la moneda de trabajo es la divisa ...
               prevul = prevul ! nDeci_PreDiv;
          |SINO;                               || Si la moneda de trabajo es la moneda base ...
               prevul = prevul ! nDeci_PreBase;
          |FINSI;
     |FINSI;
     |CIERRA #9;
|FPRC;

|PROCESO cierre; |TIPO 0;
     efFec = #1#1;
     enAlma = #0#3;
     |HAZ MiraHisL;
     |SI enErrHis ! 0;
          |ERROR;
          |SI #20#99 = "S";
               |PTEC 807;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Tipo5_071;  |TIPO 5;
     nModo = (FEntrada / 100) ! 0;

     nGuarda = enErr;
     enErr   = 0;
     |SI nModo ! 4; |HAZ actal; |FINSI;
     enErr   = nGuarda;
|FPRC;

|PROCESO CtaArt1;
   eaModulo = "agifa010"; |HAZ CtaArt;
|FPRC;

|PROCESO Dto071; |TIPO 0;
     |HAZ licomis;
     |HAZ TotalLinVigor;
     |PINTA #0#9;
     |PINTA #0#37;
|FPRC;

|PROCESO Equivalentes;
     eaArticulo = #0#2;
     enAlmacen = #0#3;
     |SUB_EJECUTA_NP "agifa294";
     #0#2 = eaArticulo; |PINTA #0#2;
     #0#3 = enAlmacen; |PINTA #0#3;
     |ABRE #2;
     #2#0 = #0#2;
     |LEE 000#2=;
     |CIERRA #2;
     #0#4 = #2#1;
     |PINTA #0#4;
     |SI enSalida ! 0; |Y eaAuto = "N"; |ERROR; |FINSI;
|FPRC;

|PROCESO Descri071; |TIPO 6;
     |SI FSalida = 14;             ||F6
          eaAuto = "N";
          |HAZ Equivalentes;
     |FINSI;
|FPRC;

|PROCESO MinC071;
     #referen@#29 = #1#52;
     #referen@#0 = #1#0;
     #referen@#1 = 001;
|FPRC;

|PROCESO MaxC071;
     #referen@#29 = #1#52;
     #referen@#0 = #1#0;
     #referen@#1 = 999;
|FPRC;

|PROCESO Consulta071; |TIPO 66;
     nLin = 0;
     |DDEF aAlfa;
     aAlfa = aAlfa + "vigm0007";
     |CARGA_DEF aAlfa, referen@;
     |SI FSalida = 0;
          |ABRE #referen@;
          |CONSULTA_F_CLAVE #1#referen@, MinC071, MaxC071;
          |SI FSalida = 0; nLin = #referen@#1; |FINSI;
          |CIERRA #referen@;
          |DESCARGA_DEF #referen@;
     |FINSI;
     |SI nLin ! 0;
          |PTEC 802;
          aAlfa = ("000" + nLin) % -103;
          aPtec = aAlfa % 301; nPtec = &aPtec;
          |PTEC nPtec;
          aPtec = aAlfa % 201; nPtec = &aPtec;
          |PTEC nPtec;
          aPtec = aAlfa % 101; nPtec = &aPtec;
          |PTEC nPtec;
     |FINSI;
|FPRC;

|PROCESO TotalImp7_71; |TIPO 7;
|FPRC;

|PROCESO ImportaPDA71;
     |DDEF aAlfa; aAlfa = aAlfa + "vigm0007";
     |CARGA_DEF aAlfa, referen@;
     |SI FSalida ! 0; |ACABA; |FINSI;
     |ABRE #referen@;
     #referen@#29 = #1#52;
     #referen@#0 = #1#0;
     #referen@#1 = 999;
     |LEE 000#referen@.];
     |SI FSalida = 0;
          |LEE 000#referen@.a;
     |SINO;
          |LEE 000#referen@.u;
     |FINSI;
     |SI FSalida = 0; |Y #referen@#29 = #1#52; |Y #referen@#0 = #1#0;
          nLin = #referen@#1 + 1;
     |SINO;
          nLin = 1;
     |FINSI;
     |CIERRA #referen@;
     |DESCARGA_DEF #referen@;

     |DDEF aAlfa; aAlfa = aAlfa + "psion067";
     |CARGA_DEF aAlfa, referen@;
     |SI FSalida ! 0; |ACABA; |FINSI;
     |NOME_DAT #referen@#eaFichero#;
     |ABRE #referen@;
     |LEE 000#referen@.p;
     |PARA; |SI FSalida = 0; |HACIENDO;
          #0#29 = #1#52;
          #0#0 = #1#00;
          #0#1 = nLin;
          |GRABA 020#0b;
          #0#2 = #referen@#0; ||Arti
          #0#4 = #referen@#1; ||Des
          #0#3 = #referen@#2; ||Alma
          #0#5 = #referen@#3; ||Uni
          #0#49 = #referen@#9; ||Parti
          FEntrada = 100;     ||simula alta
          |HAZ PrecioArti;
          |HAZ TotalLinVigor;
          |HAZ liacarti;
          |GRABA 020#0a;
          |LIBERA #0;
          nLin = nLin + 1;
          |LEE 000#referen@.s;
     |SIGUE;
     |CIERRA #referen@;
     |DELINDEX #referen@;
     |DESCARGA_DEF #referen@;
     |PONCONTROL 23, "SI"
     |PTEC 809;
     |PTEC 808;
|FPRC;

|PROCESO MenuTrack71;
     |VARIABLES; {20} aPopup = ""; |FINSI;
     aPopup1 = "-1";  || Si fuera -1 en la posicion
     aPopup2 = 10;
     aPopup3 = "<F3> Extracto Contable";
     aPopup4 = "<F4> Copiar Albaran";
     aPopup5 = "<F5> Doble Stock/Kit";
     aPopup6 = "                     ";
     aPopup7 = "<F7> Captura PSION"
     aPopup8 = "<F8> Descripcion Ampliada";
     aPopup9 = "<F9> Con.Trab.";
     aPopup10 = "<10> Extracto Contable";
     aPopup11 = "<F12> Consulta Cabecera";
     aPopup12 = "<Ctrl+F7> Captura PDA";
     IaPopup = aPopup1  <-;
     |ESPECIFICA "TRACKPOPUP", aPopup;
     nSal = FSalida;
     |SI FSalida = 1; nSal = 11; |FINSI;
     |SI FSalida = 2; nSal = 12; |FINSI;
     |SI FSalida = 3; nSal = 13; |FINSI;
     |SI FSalida = 4; nSal = 14; |FINSI;
     |SI FSalida = 5; nSal = 15; |FINSI;
     |SI FSalida = 6; nSal = 16; |FINSI;
     |SI FSalida = 7; nSal = 17; |FINSI;
     |SI FSalida = 8; nSal = 18; |FINSI;
     |SI FSalida = 9; nSal = 9; |FINSI;
     |SI FSalida = 10; nSal = 25; |FINSI;
     FSalida = nSal;
|FPRC;

|PROCESO TotalLin7; |TIPO 0;
     |HAZ TotalLinVigor;
     |HAZ visual_precio;
     |HAZ visual_totales;
|FPRC;

|PROCESO IVA007_66; |TIPO 66;
     enTiva = #0Campo;
     efFiva = #1#1;
     |HAZ ConsuIVA;
     |SI eaDiva = "";
          |ERROR;
     |SINO;
          #0Campo = enTiva; |PINTA #0Campo;
     |FINSI;
|FPRC;

|PROCESO IVA007_0; |TIPO 0;
     enTiva = #0Campo;
     efFiva = #1#1;
     |HAZ SacaIVA;
     |SI eaDiva = "";
          |MENAV "0000No existe tipo iva para esta fecha...";
          |ERROR; |ACABA;
     |SINO;
          |HAZ mirespor;
     |FINSI;
|FPRC;
