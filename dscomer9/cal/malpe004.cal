|FICHEROS;
     malpe004 #0;
     malpe005 #5;
     agifa027 #1;
     agifa007 #2;
     malpe002 #3;
     agifa024 #6;   || Clientes
     malpe006 #16;
     agifa019 #9;   || Articulos
     agifa017 #13;  || Stock Almacen
     dsm00024 #14;  || Stock Lotes

     agifa142 #41;  || Parametros Generales
     malpe003 #50;
     malpe001 #51;
     ||malpe085 #85,MANTE;
     dsm00246 #140; ||Control Riesgo Por Usuarios.
     dsm00247 #141; ||Pantalla Informe Riesgo.
     dsm00248 #143; ||Auditoria.
     malpe087 #87;
     malpe203 #203;
     malpe001@;
|FIN;

|INCLUYE i_varart;

|VARIABLES;
     &Tempo = "";
     &enOrden = "";
 {-1}aUtil  = "";
     aUtil1 = "2400";
     aUtil2 = "1";
     aUtil3 = "";
     aUtil4 = "IP";
     aUtil5 = "Imprimir";
     aUtil6 = "Pendientes";

     &eaCodCliente = "";
     &eaNomCliente = "";
     nConfir    = 0;
     nForma     = 0;
     aUs        = "";
     nConAu     = 0;
     &nMasRies  = 0;
     nTries     = 0;
     nAudito    = 0;
     nSal      = 0;
     aAlfa     = "";
     nMult     = 0;
     nModo     = 0;
     nGuarda   = 0;
     &nRieTotal = 0;
     &nRieUtili = 0;
     &aRieBloqu = "";
     &aBloquead = "";
     &eaTag    = "";

     &EURODB = 0;
     puente = "";
     &n_dec  = 0;
     xmodo  = 0;
     conta  = "";
     ymes      = "";
     xmes      = 0;
     fecha     = @;
     mensajer = "";
     asto      = 0;
     cant      = 0;
     mensaje   = "";
     sw        = 0;
     hay_impag = "N";
     tot_riesgo = 0;
     actual = 0;

     &eaVarDni = "";
     aNifQbf1 = "";
     aNifQbf2 = "";
|FIN;

|PROCESO CheqCli;
     #87#25 = #5#32;
     #87#0 = #5#33;
     |LEE 020#87=;
     |SI #87#4 ! #0#3; |O #87#63 ! #0#4;
          aAlfa = "0000Cliente pedido distinto a la orden, linea:" + #5#1;
          |MENAV aAlfa;
     |FINSI;
|FPRC;

|PROCESO tipo33; |TIPO 3;
     |ABRE #87;
     |BUCLELIN 2 CheqCli #0;
     |CIERRA #87;
|FPRC;

|PROCESO ElRiesgo; |TIPO 6;
     |ABRE #41; |LEE 000#41p; |CIERRA #41;
     |ABRE #6;
     #6#0 = #0#3;
     |LEE 000#6=;
     |CIERRA #6;
     ||ÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ Observaciones Clientes.
     |SI #agifa142#14 = "S"; |Y Campo = 3;
          puente = #agifa024#53;     |QBF puente;
          |SI puente ! "";
               |PUSHV 1008 1370;
               |SI #agifa142#20 = "S"; |AVISO; |FINSI;
               |BLANCO 1009 1369;
               |CUADRO 1008 1370;
               |ATRI R;
               |PINTA 1110 , #agifa024#53;
               |ATRI 0;
               |PAUSA;
               |POPV;
          |FINSI;
     |FINSI;
     ||ÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ Control Riesgo Cliente.
     nMasRies = 0;
     eaSerie = #0#0;
     eaTag = ""; eaCodCliente = #6#0; eaNomCliente =  #6#1;
     |HAZ SacaRiesgoMalpe;
     |SI #agifa142#6 = "S";
          eaDocu = "C";
          eaTipo = "A";
          enImpoDoc = #0#14;
          ||eaMuestra =
          |HAZ ControlRiesgo;
          |SI enErrRie ! 0; |ERROR; |ACABA; |FINSI;
          |SI enAudito = 0;
               |ABRE #143;
               |LEE 000#143u;
               |SI FSalida = 0;
                    nConAu = #143#0 + 1;
               |SINO;
                    nConAu = 1;
               |FINSI;
               |DDEFECTO #143;
               #143#0 = nConAu;
               #143#12 = "ORDEN  ";
               #143#1 = #0#0;
               #143#2 = #0#1;
               #143#3 = #0#2;
               #143#4 = #0#3;
               #143#5 = #0#5;
               #143#6 = Usuario % 810;
               #143#7 = #141#0;
               #143#8 = #141#1;
               #143#9 = #141#2;
               #143#10 = #141#3;
               #143#11 = #141#4;
               |GRABA 020#143n;
               |CIERRA #143;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO hay_tarjeta;
     nModo = (FEntrada / 100) ! 0;
     |ABRE #51;
     #51#0 = #0#3;
     #51#1 = #0#4;
     |LEE 000#51=;
     |CIERRA #51;
     |SI nModo = 1; |O #0Campo ! Contenido;
          #0#6 = #51#2; |PINTA #0#6;
          #0#7 = #51#3; |PINTA #0#7;
          #0#8 = #51#4; |PINTA #0#8;
          #0#19 = #51#22; |PINTA #0#19;
     |FINSI;

     |ABRE #50;
     #50#0 = #51#22;
     #50#3 = #0#2;
     |LEE 000#50];
     |SI FSalida ! 0;
          |LEE 000#50u;
     |SINO;
          |LEE 000#50a;
     |FINSI;
     |SI FSalida ! 0; |O #50#0 ! #51#22;
          |DDEFECTO #50;
     |FINSI;

     |SI #50#2 = 0;
          #0#16 = "N";
     |SINO;
          #0#16 = "S";
     |FINSI;
     |PINTA #0#16;
     |CIERRA #50;
|FPRC;

|CALCULO contorde; |TIPO 0;
     #1#2 = #0#0;
     #1#0 = "orden";
     |LEE 111#1=;
     |SI FSalida ! 0;
          #1#1 = 1;
          #1#3 = #2#27;
          |GRABA 111#1b;
     |FINSI;
     #0#1 = #1#1;
     #1#1 = #1#1 + 1;
     |GRABA 021#1a;
     |LIBERA #1;
|FCAL;

|PROCESO mirar_d; |TIPO 0;
     |ABRE #41; |LEE 000#41p; |CIERRA #41;

     n_dec = #41#8;
|FPRC;

|PROCESO matri; |TIPO 0;
     |ABRE #3;
     #3#0 = #0#9;
     |LEE 101#3=;
     |SI FSalida = 0;
          |ABRE #16;
          #16#0 = #3#1;
          |LEE 101#16=;
          |SI FSalida = 0;
               |SI #0#16 = "N";|Y #16#9 = "S";
                    |CONFI "0000SPortes Debidos. Emitir Tarjeta (S/N): ";
                    |SI FSalida = 0;
                         #0#16 = "S";
                    |FINSI;
               |FINSI;
               |PINTA #0#16;
          |FINSI;
          |CIERRA #16;
     |FINSI;
     |CIERRA #3;
|FPRC;

|PROCESO MiraLin;
     |SI #5#36 = "S";
          nConfir = 1;
          |ERROR10;
     |FINSI;
|FPRC;

|PROCESO Tipo2; |TIPO 2;
     nModo = (FEntrada / 100) ! 0;
     nForma = 0 +. 1;
     |SI nModo = 2; |O nModo = 3;
          |SI #0#15 = "S";
               |SI nForma = -1;
                    |MENAV "0000Orden confirmada...";
               |FINSI;
               |ERROR; |ACABA;
          |FINSI;
     |FINSI;
     |SI nModo = 3;
          nConfir = 0;
          |BUCLELIN 2MiraLin#0;
          |SI nConfir ! 0;
               |SI nForma = -1;
                    |MENAV "0000Lineas de Orden confirmada...";
               |FINSI;
               |ERROR; |ACABA;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Malpe80; |TIPO 80;
     |C_A #203#1, 1, 30;
|FPRC;

|PROCESO Malpe20; |TIPO 20;
     |SI FSalida = "OPCION";
          FSalida = "Opciones";
     |SINO;
          eaSerie = #0#0;
          enOrden = #0#1;
          |MENU aUtil;
          aUtil = FSalida;
          |SI aUtil = 1;
               |LEE 000#0=;
               |SI FSalida = 0;
                    |SUB_EJECUTA_NP "malpe008";
               |FINSI;
          |FINSI;
          |SI aUtil = 2;
               |SUB_EJECUTA_NP "malpe204";
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO PinDatoMatri;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 1; |O #0Campo ! Contenido;
          |ABRE #3;
          #3#0 = #0#9;
          |LEE 000#3=;
          |CIERRA #3;
          #0#10 = #3#1; |PINTA #0#10;
          #0#11 = #3#2; |PINTA #0#11;
          #0#12 = #3#5; |PINTA #0#12;
          #0#13 = #3#3; |PINTA #0#13;
          |ABRE #16;
          #16#0 = #0#10;
          |LEE 000#16=;
          |CIERRA #16;
          #0#23 = #16#11; |PINTA #0#23;
     |FINSI;
|FPRC;


|PROCESO LetraNif04; |TIPO 6;
     |SI FSalida = 9;
          eaVarDni  = #0Campo;
          |HAZ CalculoNIFCIF;
          #0Campo = eaVarDni;
          |PINTA #0Campo;
          |ERROR; |ACABA;
     |FINSI;

     eaVarDni  = #0Campo;
     |HAZ CalculoNIFCIF;
     |SI FSalida = 9;
          eaVarDni  = #0Campo;
          |HAZ CalculoNIFCIF;
          #0Campo = eaVarDni;
          |PINTA #0Campo;
          |ERROR; |ACABA;
     |FINSI;

     eaVarDni  = #0Campo;
     |HAZ CalculoNIFCIF;

     aNifQbf1 = #0Campo;   |QBF aNifQbf1;
     aNifQbf2 = eaVarDni;  |QBF aNifQbf2;

     |SI aNifQbf1 ! aNifQbf2; |Y aNifQbf1 ! "";
          aAlfa = "0000NEl NIF puede ser erróneo."+&13+"¿Desea calcularlo?";
          |CONFI aAlfa;
          |SI FSalida = 0;
               #0Campo = eaVarDni; |PINTA #0Campo;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Reexpe; |TIPO 0;
     aAlfa = #0#26;
     |C_M #0#27, 1, aAlfa;
     |C_M #0#28, 1, aAlfa;
     |C_M #0#31, 1, aAlfa;
     |SI #0Campo ! Contenido; |Y aAlfa = "S";
          |ABRE #51;
          #51#0 = #0#3;
          #51#1 = #0#4;
          |LEE 000#51=;
          |SI FSalida = 0;
               #0#31 = #51#43; |PINTA #0#31;
          |FINSI;
          |CIERRA #51;
     |FINSI;
|FPRC;

|PROCESO TransRee; |TIPO 0;
     |SI #0#26 = "N"; |ACABA; |FINSI;
     |ABRE #16;
     #16#0 = #0#27;
     |LEE 000#16=;
     |SI FSalida ! 0;
          |MENAV "0000No existe transportista...";
          |ERROR;
     |SINO;
          |SI #0Campo ! Contenido;
               #0#28 = #16#11; |PINTA #0#28;
          |FINSI;
     |FINSI;
     |CIERRA #16;
|FPRC;

|PROCESO AgeRee; |TIPO 0;
     |SI #0#26 = "N"; |ACABA; |FINSI;
     |ABRE #203;
     #203#0 = #0#28;
     |LEE 000#203=;
     |SI FSalida ! 0;
          |MENAV "0000No existe agencia...";
          |ERROR;
     |FINSI;
     |CIERRA #203;
|FPRC;

|PROCESO MatriRee; |TIPO 0;
     |SI #0#26 = "N"; |ACABA; |FINSI;
     |ABRE #3;
     #3#0 = #0#30;
     |LEE 000#3=;
     |SI FSalida ! 0;
          |MENAV "0000No existe vehiculo...";
          |ERROR;
     |FINSI;
     |CIERRA #3;
|FPRC;

|PROCESO ImpOrd; |TIPO 30;
     |CONFI "0000NImprimir orden de carga";
     |SI FSalida = 0;
          eaSerie = #0#0;
          enOrden = #0#1;
          |SUB_EJECUTA_NP "malpe008";
     |FINSI;
|FPRC;

|PROCESO EnCurso;
     |SALVA_DATOS #51;
     |REPON_DATOS #malpe001@;
     |GRABA 020#malpe001@.n;
|FPRC;

|DEFBUCLE EnCurso;
     #51#1;
     38;
     #0#3, 0001, "C";
     #0#3, 9999, "C";
     ;
     NULL, EnCurso;
|FIN;

|PROCESO Min1;
     #51#0 = #0#3;
     #51#1 = 1;
|FPRC;

|PROCESO Max1;
     #51#0 = #0#3;
     #51#1 = 9999;
|FPRC;

|PROCESO ConsuObra; |TIPO 66;
     |CONFI "0000¿Mostrar solo obras en curso?";
     |SI FSalida = 0;
          |DDEF aAlfa;
          aAlfa = aAlfa + "malpe001";
          |CARGA_DEF aAlfa, malpe001@;
          |SI FSalida = 0;
               |HAZ CreaTempo;
               |NOME_DAT #malpe001@#Tempo#; |DELINDEX #malpe001@;
               |ABRE #malpe001@;
               |BUCLE EnCurso;
               |LEE 000#malpe001@.p;
               |CONSULTA_CLAVE #1#malpe001@;
               |SI FSalida = 0;
                    #0#4 = #malpe001@#1; |PINTA #0#4;
               |SINO;
                    |ERROR;
               |FINSI;
               |CIERRA #malpe001@;
               |HAZ BoraTempo; |DELINDEX #malpe001@;
               |DESCARGA_DEF #malpe001@;
          |FINSI;
     |SINO;
          |ABRE #51;
          #51#0 = #0#3;
          #51#2 = #0#4;
          |LEE 000#51];
          |CONSULTA_F_CLAVE #1#51, Min1, Max1;
          |SI FSalida = 0;
               #0#3 = #51#0; |PINTA #0#3;
               #0#4 = #51#1; |PINTA #0#4;
          |SINO;
               |ERROR;
          |FINSI;
          |CIERRA #51;
     |FINSI;
|FPRC;
