|FICHEROS;
     pcegm003 #0;
     pcegm002 #1;
     agifa019 #2;
     agifa039 #5;
     agifa024 #6;
     agifa017 #8;

     agifa048 #13;
     agifa049 #14;
     agifa321 #35;    || DIV - Parametros
     agifa142 #142;

     dsm00005 #905;
     dsm00010 #910;
     dsm00046 #946;
     referen@;
     agifa029 #29;
     dsm00279 #279;
     pcegz039 #39,MANTE;
     pcegm039 #390,MANTE;
|FIN;

|VARIABLES;
     aArti = "";
     nNume = 0;
     nPreDtoAnt = 0;
     nTagBorLinPost = 0;
     nTagQuitaPost = 0;
     nSali = 0;
     nTagPre = 0;
     aTagDes = "";
     &enModo146 = 0;
     nForma = 0;
     nSal = 0;
     &enDto = 0;
     &nDispoKit = 0;
     aPtec = "";
     nPtec = 0;
     nLin = 0;
     &eaModulo       = "";

     nTecla         = 0;
     &eaUniMarhu    = 0;
     aTipoTar       = "";
     serie          = "";
     pedido         = 0;
     pedido_o       = 0;
     serie_o        = "";
     alfa1          = "";
     alfa2          = 0;
     recoge        = "";
     nLineaD       = 0;
     nerror        = 0;
     &Tempo        = "";
     aTempo294     = "";
     aAlfa         = "";
     nModo         = 0;

     &enDescuento1 = 0;
     &enDescuento2 = 0;
     &eaTComision  = "";
     &enComision   = 0;

     &nDeci_Base     = 0;   || Decimales en Totales de la moneda base
     &nDeci_PreBase  = 0;   || Decimales en el Precio en moneda base
     &nDeci_BaseMx   = 0;   || Decimales en Totales de la moneda base (Maxima precision)
     &nDeci_PreBaseMx= 0;   || Decimales en el Precio en moneda base (Maxima precision)
     &nDeci_Div      = 0;   || Decimales en Totales de la divisa
     &nDeci_PreDiv   = 0;   || Decimales en el Precio en divisa
     &nDeci_TotVis   = 0;   || Decimales en Totales visibles (el de la moneda de trabajo)
     &nDeci_TotNoVis = 0;   || Decimales en Totales no visibles (el de la otra moneda)
     &nDeci_PreVis   = 0;   || Decimales en el Precio visual. (lineas)
     &nDeci_ImpVis   = 0;   || Decimales en el Importe visual. (lineas)
     tecla      = "";
     &ac_divisa = ""; ||
     mensa      = ""; || Linea de visualizacion
     &pintado   = 0;|| Controla el pintado de mensa para que solo lo haga 1 vez
     titdiv = "";   || Encabezado de precio e imp. con la divisa "Imp.Tot(USD)"
     &Envase = "";
     &Unidades1 = 0;
     &Unidades2 = 0;
     &VArticulo = "";
     &qAbono    = "";
     &enMensaje = 0;
     &aParam    = "";
     nPrecio = 0;

 vacio = "                    ";
 descu = 0;
 tf = 0;
 pc = 0;
 comi1 = 0;
 comi2 = 0;
 comi3 = 0;
 menor = 0;
 pc1 = 0;
 pa  = 0;
 pa1 = 0;
 pa2 = 0;
 imneto = 0;
 margen = 0;
 a = 0;
 i = 0;
 wimneto = 0;
 cantidad = 0;
 asto = 0;
operacion = "FC";
articulo = " ";
fmes = "  ";
mes = 0;
me = 0;
kl = 0;
mescli= " ";
mensaje3 = "";
mensaje  = "0000 Factura para exportacion.Tipo IVA = 0  !!!";
totbu = 0;
flag = 1;
modo = 0;
swescan = 0;
&n_dec = 0;
&n_pre = 0;
&def_alm = 0;
&def_exp = "N";
campo = 0;
     &c_clien   = "";
     &c_artic   = "";
     &c_serie   = "";
     &entrada   = 0;
     inkey      = 0;
     &codiBarra = "";
     &r_arti    = "";
     pcs            = 0;
     pcv            = 0;
     pcd            = 0;
     &fed_dt = @;   || Fecha....
     &art_dt = "";  || Articulo.
     &cli_dt = "";  || Cliente..
     &fam_dt = "";  || Familia.
     &iva_dt = 0;   || Tipo Iva.
     &dtos_2 = 0;   || Descuento 2.
     &dto_pt = 0;   || Descuento Ptas.
     dtopts  = 0;
     &dtos_1 = 0;   || Descuento 1.
     &pvp_ve = 0;   || Precio Venta.
     &tarifa = 0;   || Tarifa Cliente.
     &gru_dt = "";  || Grupo Cliente.
     &uni_dt = 0;
     DescAmpli      = "N";    || Descarga descripcion ampliada de articulos          = 0;      || Decimales en unidades
     fiac           = "  ";
     guarda         = "  ";
     relleno        = "                                       ";
     aponer         = "  ";
     nlinea         = 0;
     &qArti         = "";
     &qUnid         = 0;
|FIN;

|INCLUYE i_varart;

|PROCESO Calc0147;
     |SI DescAmpli = "S";
          eaSerie = #0#20;
          enCodigo = #0#0;
          enLinea = #0#1;
          eaArticulo = #0#2;
          aTipoDA = "X"; |HAZ DescriDA;
     |FINSI;
|FPRC;

|CALCULO pivapro;
     inkey = FSalida;
     |SI inkey = 14;
         c_clien = #1#3;
         c_artic = #0#2;
         c_serie = #0#20;
         entrada = 1;
         |SUB_EJECUTA_NP "agifa283.run";
         inkey = 0;
         |ERROR;
     |FINSI;
     |SI FSalida = 15;
          eaCli = "";
          enTiva = #0#11;
          efFiva = #1#1;
          |HAZ IVACli;
          #0#6 = #0#6 / (1 + enIva/100);
          |HAZ visual_precio;
     |FINSI;
|FCAL;

|CALCULO mirespor;|TIPO 0;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo ! 1; |ACABA; |FINSI;

     |SI #6#28 = AS ;|Y #0#11 ! 0;
          |MENAV mensaje;#0#11 = 0; |PINTA #0#11;
     |SINO;
          |SI #0#11 ! 0; |Y def_exp = "S";
               |MENSA mensaje;#0#11 = 0;|PINTA #0#11;
          |FINSI;
    |FINSI;
|FCAL;


|CALCULO lcinfo;|TIPO 0;
     |ABRE #8;
     #8#0 = #0#2;
     #8#1 = #0#3;
     |LEE 000#8=;
     |CIERRA #8;
     mensaje3 = "0410Stock Disponible Almacen : " + #8#39+"     Stock Minimo : "+#8#6;
     |MENSA mensaje3;

     |SI Campo ! 5; |ACABA; |FINSI;

     |SI #agifa142#234 ! "N";
          eaArticulo = #pcegm003#2;
          |HAZ EsKit;
          |SI FSalida = 0;
               |HAZ DispoKit;
               |SI nDispoKit ! 0;
                    |MENAV "0000Superado Stock para KIT.";
                    |SI #agifa142#234 = "X"; |ERROR; |ACABA; |FINSI;
               |FINSI;
          |SINO;
               |ABRE #8;
               #8#0 = #0#2;
               #8#1 = #0#3;
               |LEE 000#8=;
               |CIERRA #8;
               |SI #8#39 < #0#5;
                    aAlfa = "0000Supera el Stock disponible: " + #8#39;
                    |MENAV aAlfa;
                    |SI #agifa142#234 = "X"; |ERROR; |ACABA; |FINSI;
               |FINSI;
          |FINSI;
          nStock = #8#5 - #0#5;
          |SI #8#6 > nStock; |Y #agifa142#219 = "S";
               |MENAV "0000Supera el Stock minimo...";
          |FINSI;
     |FINSI;

     |SI #8#39 < #0#5; |Y #agifa142#218 = "S"; |Y Campo = 5;
          eaAuto = "S";
          |HAZ Equivalentes;
     |FINSI;
|FCAL;

|CALCULO lccogea;|TIPO 0;
articulo = #0#2;
|FCAL;

|PROCESO MinAlma147;
     #8#0 = #0#2;
     #8#1 = 1;
|FPRC;

|PROCESO MaxAlma147;
     #8#0 = #0#2;
     #8#1 = 99999;
|FPRC;

|PROCESO Alma147_0; |TIPO 0;
     |HAZ PrecArt;
|FPRC;

|CALCULO defecfc;|TIPO 0;
     |SI FSalida = 13; || F5;
          |ABRE #8;
          #8#0 = #0#2;
          #8#1 = #0#3;
          |LEE 000#8];
          |CONSULTA_F_CLAVE #1#8, MinAlma147, MaxAlma147;
          |SI FSalida = 0;
               #0#3 = #8#1; |PINTA #0#3;
          |SINO;
               |ERROR; |ACABA;
          |FINSI;
          |CIERRA #8;
     |FINSI;

#0#15 = #1#3;
#0#16 = #1#34;
#0#17 = #1#35;

|SI #0#2 ! #2#0;
   |ABRE #2;
   #2#0 = #0#2;
   |LEE 000#2=;
   |CIERRA #2;
|FINSI;
#0#13 = #2#2;
|SI #0#15 ! #6#0;
   |ABRE #6;
   #6#0 = #0#15;
   |LEE 000#6=;
   |CIERRA #6;
|FINSI;
|FCAL;

|PROCESO PrecArt;
     modo = (FEntrada / 100) ! 0;
     mescli = #1#1 % 402;
     mes = mescli;
     fed_dt = #1#1;   || Fecha....
     art_dt = #0#2;   || Articulo.
     cli_dt = #0#15;  || Cliente..
     fam_dt = #0#13;  || Familia.
     iva_dt = #2#30;  || Tipo Iva.
     dtos_2 = 0;      || Descuento 2.
     dto_pt = 0;      || Descuento Ptas.
     dtos_1 = 0;      || Descuento 1.
     pvp_ve = 0;      || Precio Venta.
     tarifa = #6#39;  || Tarifa Cliente....
     gru_dt = #6#158; || Grupo Cliente.
     uni_dt = #0#5;
     enMensaje = 0;
     aParam = "";
     eaSerie = #0#20;
     enDirEnt = #1#80;
     |SI #2#176 = "S";  enMensaje = 1;  |FINSI;
     enAlmacen = #0#3;
     |SI modo = 1; |O articulo ! #0#2;
          enInterno = tarifa;
          |HAZ calcdtos;    || Calculo Dtos.....
          |SI eaCliArtDiviSN = "S";
               |SI eaCliArtDivi ! #1#65;
                    aAlfa = "0000La divisa del documento ("+ #1#65 + ") no se corresponde con la oferta (" + eaCliArtDivi + ").";
                    |MENAV aAlfa;
                    |PTEC 807;     || Ctrl+C
               |SINO;
                    pvp_ve = pvp_ve * #1#69 / #1#66; ||Cam.Base * Cambio
               |FINSI;
          |FINSI;
          eaCliArtDiviSN = "N";

          #0#34 = enInterno;

          #0#6   = pvp_ve;  || Precio Venta. sin iva.
          #0#8   = dtos_1;  || 1¦ Dto.
          #0#21  = dtos_2;  || 2§ Dto.
          dtopts = dto_pt;  || Dto.en ptas.
          |HAZ precio_divisa;
          |PINTA #0#21;
          |PINTA #0#8;

          enDescuento1 = #0#8;
          enDescuento2 = #0#21;
          eaTComision  = #0#16;
          eaCliente    = #1#3;
          eaArticulo   = #0#2;
          eaRepre      = #1#6;
          efFecha      = #1#1;
          eaSerie      = #0#20;
          enDirEnt = #1#80;
          enAlmacen = #0#3;
          |HAZ Comisiones;
          ||ÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄMarhuenda
          eaUniMarhu = #0#5;
          |HAZ ComiMarhu;
          |SI aTipoTar  = "S";
              |ABRE #2;
              #2#0 = art_dt;
              |LEE 000#2=;
              |SI tarifa = 1; pvp_ve = #2#18;  enDescuento1 = #2#21;  |FINSI;
              |SI tarifa = 2; pvp_ve = #2#19;  enDescuento1 = #2#22;  |FINSI;
              |SI tarifa = 3; pvp_ve = #2#20;  enDescuento1 = #2#23;  |FINSI;
              |SI tarifa = 4; pvp_ve = #2#147; enDescuento1 = #2#150; |FINSI;
              |SI tarifa = 5; pvp_ve = #2#148; enDescuento1 = #2#151; |FINSI;
              |SI tarifa = 6; pvp_ve = #2#149; enDescuento1 = #2#152; |FINSI;
              #0#21  = 0;
              dtopts = 0;
              |CIERRA #2;
          |FINSI;
          #0#8   = enDescuento1;
          #0#10  = enComision;
          #0#6   = pvp_ve;  || Precio Venta. sin iva.
          |HAZ precio_divisa;
          ||ÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ
          |PINTA #0#8;
          |PINTA #0#10;
     |FINSI;
|FPRC;

|CALCULO fccomis;|TIPO 0;
     |SI #0Campo ! Contenido;
         enDescuento1 = #0#8;
         enDescuento2 = #0#21;
         eaTComision  = #0#16;
         eaCliente    = #1#3;
         eaArticulo   = #0#2;
         eaRepre      = #1#6;
         eaSerie      = #0#20;
         enDirEnt = #1#80;
         enAlmacen = #0#3;
         |HAZ Comisiones;
         ||ÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄMarhuenda
         eaUniMarhu = #0#5;
         |HAZ ComiMarhu;
         #0#10 = enComision;
         |PINTA #0#10;
     |FINSI;
|FCAL;

|CALCULO totfc;|TIPO 0;
     |HAZ TotalLin002;
     |HAZ visual_importe;
     #0#14 = #0#5 * #2#9;
|FCAL;

|CALCULO fcacarti;|TIPO 2;
     modo = (FEntrada/100) ! 0;
     nForma = 0 +. 1;

     |SI #0#36 = -1; |Y enModo146 ! 3;
          |SI nForma = -1;
               |MENAV "0000Linea de Promocion, no se puede modificar...";
          |FINSI;
          |ERROR; |ACABA;
     |FINSI;

     |HAZ defecfc;

     |HAZ CalCabpcegm002;
     |HAZ visual_totales;

     |SI modo = 3;
          eaSerie = #0#20;
          enCodigo = #0#0;
          enLinea = #0#1;
          eaArticulo = #0#2;
          aTipoDA = "X"; |HAZ BajaDA;
          |HAZ BorraKit;

          nLin = 0;
          |HAZ BorraTxtCalibra;
     |FINSI;

     enTipEnvase = 2;
     |HAZ Envase147;

     |SI #142#250 = "S";
          |SI nForma = 1;
               |SI #0#39 > 0;
                    aponer = Asino; |HAZ poncontr;
                    |HAZ CreaLinPromo147;
                    #0#36 = nLin;
                    aponer = Anosi; |HAZ poncontr;
                    |PTEC 809; |PTEC 816; |PTEC 806;
               |SINO;
                    |HAZ BorraLinPromo147;
               |FINSI;
          |SINO;
               |SI modo = 3; |Y enModo146 ! 3;
                    |HAZ BorraLinPromo147;
                    |PTEC 809; |PTEC 808;
               |FINSI;
          |FINSI;
     |FINSI;
|FCAL;

|PROCESO CreaLinPromo147;
     |DDEF aAlfa; aAlfa = aAlfa + "pcegm003";
     |CARGA_DEF aAlfa, referen@;
     |SI FSalida ! 0; |ACABA; |FINSI;

     |ABRE #referen@;
     #referen@#20 = #0#20;
     #referen@#0 = #0#0;
     #referen@#1 = #0#36;
     |LEE 000#referen@.=;
     |SI FSalida = 0;
          nLin = #0#36;
     |SINO;
          #referen@#1 = 999;
          |LEE 000#referen@.];
          |SI FSalida = 0;
               |LEE 000#referen@.a;
          |SINO;
               |LEE 000#referen@.u;
          |FINSI;
          |SI FSalida = 0; |Y #referen@#20 = #0#20;  |Y #referen@#0 = #0#0;
               nLin = #referen@#1 + 1;
          |SINO;
               nLin = 1;
          |FINSI;
          |SI nLin = #0#1; nLin = nLin + 1; |FINSI;
          #referen@#20 = #0#20;
          #referen@#0 = #0#0;
          #referen@#1 = nLin;
          |GRABA 020#referen@.b;
     |FINSI;
     |SI #0#39 > 0;
          |SALVA_DATOS #0;
          |REPON_DATOS #referen@;

          #referen@#1 = nLin;
          #referen@#5 = #0#39;
          #referen@#6 = 0;
          #referen@#7 = 0;
          #referen@#8 = 0;
          #referen@#9 = 0;
          #referen@#24 = 0;
          #referen@#25 = 0;
          #referen@#26 = 0;
          #referen@#27 = 0;
          #referen@#36 = -1;

          |GRABA 020#referen@.a;
     |SINO;
          |BORRA 020#referen@.c;
     |FINSI;
     |CIERRA #referen@;
     |DESCARGA_DEF #referen@;
|FPRC;

|PROCESO BorraLinPromo147;
     |DDEF aAlfa; aAlfa = aAlfa + "pcegm003";
     |CARGA_DEF aAlfa, referen@;
     |SI FSalida ! 0; |ACABA; |FINSI;

     |ABRE #referen@;
     #referen@#20 = #0#20;
     #referen@#0 = #0#0;
     #referen@#1 = #0#36;
     |LEE 101#referen@.=;
     |SI FSalida = 0;
          |BORRA 020#referen@.a;
          |PONCONTROL 23, "SI";
     |FINSI;
     |CIERRA #referen@;
     |DESCARGA_DEF #referen@;
|FPRC;

|PROCESO cabecont;|TIPO 13;
     |HAZ visual_totales;
|FPRC;

|CALCULO tipo11; |TIPO 11;
     inkey = FSalida;
     modo = (FEntrada / 100) ! 0;

     |SI inkey = 10;     ||F2
          |HAZ MenuTrack147;
          |SI FSalida = 0; |ERROR; |ACABA; |FINSI;
          inkey = FSalida;
     |FINSI;
     |SI inkey = 9;
         |PUSHV 501 2380;
         |PINPA #0#1;
         |PINDA #0#1;
         |PAUSA;
         |POPV;
         inkey = 0;
         |ERROR;
     |FINSI;
     |SI inkey = 12;          || Copia
          |HAZ Recoge;
     |FINSI;
     |SI inkey = 13;     || F5
          |HAZ VerKit;
          |HAZ VerDobleUniV;
          |ERROR; |ACABA;
     |FINSI;
     |SI inkey = 14; |Y modo ! 1;
         c_clien = #1#3;
         c_artic = #0#2;
         c_serie = #0#20;
         entrada = 1;
         |SUB_EJECUTA_NP "agifa283.run";
         inkey = 0;
         |ERROR;
     |FINSI;
     aAlfa = #0#2; |QBF aAlfa;
     |SI inkey = 16; |Y aAlfa ! ""; |Y DescAmpli = "S";
          eaSerie = #0#20;
          enCodigo = #0#0;
          enLinea = #0#1;
          eaArticulo = #0#2;
          aTipoDA = "X"; |HAZ VerDA;
          |ERROR;
     |FINSI;
     |SI inkey = 25; || Ctrl-F7
          |HAZ CreaTempo;
          eaFichero = Tempo;
          |SUB_EJECUTA_NP "psion066";
          |HAZ BoraTempo;
          |SI enError = 0; |HAZ ImportaPDA147; |FINSI;
          |ERROR; |ACABA;
     |FINSI;
|FCAL;

|PROCESO art6pfv; |TIPO 6;          || Antes de la Relacion 4§ Clave.
     r_arti = "";                  || LIMPIA LA VARIABLE DEL ART.AUTOMATICO.
 |SI codiBarra = "N"; |ACABA; |FINSI;

 |ABRE #2;
 |SELECT #4#2;
 #2#128 = #0Campo;
 |LEE 101#2=;
 |SI FSalida = 0;
     #0Campo = #2#0;
    |PINTA #0Campo;
 |FINSI;
 |LIBERA #2;

 |SELECT #1#2;
 |CIERRA #2;
|FPRC;


|PROCESO nomodif1; |TIPO 0;
     |QBF r_arti;
     |SI r_arti ! "";
          #0#2 = r_arti;
          |PINTA #0#2;
     |FINSI;
     modo = (FEntrada / 100) ! 0;
     |SI modo = 1; #0#3 = def_alm; |PINTA #0#3; |FINSI;
     |SI #0#2 = vacio;
          |CAMPO_MODIFICA #0#3,  1, "N";
          |CAMPO_MODIFICA #0#5,  1, "N";
          |SI #1#67 = "B";
            |CAMPO_MODIFICA #0#6,  1, "N";
          |SINO;
            |CAMPO_MODIFICA #0#24,  1, "N";
          |FINSI;
          |CAMPO_MODIFICA #0#8,  1, "N";
          |CAMPO_MODIFICA #0#21,  1, "N";
          |CAMPO_MODIFICA #0#10, 1, "N";
          |CAMPO_MODIFICA #0#11, 1, "N";
          |CAMPO_MODIFICA #0#18, 1, "N";
     |SINO;
          |CAMPO_MODIFICA #0#3,  1, "S";
          |CAMPO_MODIFICA #0#5,  1, "S";
          |SI #1#67 = "B";
            |CAMPO_MODIFICA #0#6,  1, "S";
          |SINO;
            |CAMPO_MODIFICA #0#24,  1, "S";
          |FINSI;
          |CAMPO_MODIFICA #0#8,  1, "S";
          |CAMPO_MODIFICA #0#21,  1, "S";
          |CAMPO_MODIFICA #0#10, 1, "S";
          |CAMPO_MODIFICA #0#11, 1, "S";
          |CAMPO_MODIFICA #0#18, 1, "S";
     |FINSI;
|FPRC;

|PROCESO decim;
     nTecla = FSalida;
     nModo = (FEntrada / 100) ! 0;
     |HAZ Promart;
     |SI nTecla = 13;
          enTipEnvase = 1;
          |HAZ Envase147;
     |FINSI;

     |SI nTecla = 14;             ||F6
          eaAuto = "N";
          |HAZ Equivalentes;
          |ERROR;
          |ACABA;
     |FINSI;

     |HAZ lcinfo;

     #0#5 = #0#5 ! n_dec;
     |PINTA #0#5;
     |SI #0#5 [ 0; |ACABA; |FINSI;
     qArti = #0#2;
     qUnid = #0#5;
     |HAZ ComproMin;
     |SI enErr ! 0;
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO Envase147;
     Envase    = #0#22;
     Unidades1 = #0#23;
     Unidades2 = #0#5;
     VArticulo = #0#2;
     enAlmacen = #0#3;
     eaTipo = "V";
     eaProgEnv = "proforma";
     |HAZ EntraEnvase;
     #0#22 = Envase;
     #0#23 = Unidades1;
     #0#5  = Unidades2;  |PINTA #0#5;
|FPRC;

|PROCESO Conversion;  |TIPO 7;
     |SI #142#250 = "S";
          |HAZ PrecArt;
          eaDocu = "PR";
          eaSerie = #0#20;
          enCodigo = #0#0;
          enLinea = #0#1;
          eaArticulo = #0#2;
          eaCli = #1#3;
          enModo = (FEntrada / 100) ! 0;
          enDto = #0#8;
          enPrecio = #0#6;
          enUnid1 = #0#5;
          enUnid2 = #0#39;
          enUnid3 = #0#23;
          Envase = #0#22;
          enImpDtoPromo = #0#37;
          enDtoPrePromo = #0#38;
          eaDivisa = #1#65;
          |SUB_EJECUTA_NP "dsz00080";
          |SI enError ! 0; |ERROR; |ACABA; |FINSI;
          #0#8 = enDto;
          #0#6 = enPrecio;
          #0#5 = enUnid1;
          #0#39 = enUnid2;
          #0#23 = enUnid3;
          #0#22 = Envase;
          #0#37 = enImpDtoPromo;
          #0#38 = enDtoPrePromo;
          |SI eaProCliArt = "S"; |C_M #0#5, 1, "N"; |FINSI;
     |FINSI;

     |HAZ DobleUniV;
     |SI eaSw2Stock = "S";
          |C_M #0#5, 1, "N";
     |FINSI;
     |SI #agifa142#72 = "S";
          |HAZ Envase147;
          |C_M #0#5, 1, "S";
          |SI enEntraEnvase ! 0;  |C_M #0#5, 1, "N";  |FINSI;
     |FINSI;
|FPRC;

|PROCESO LeeParam147; |TIPO 7;
     |ABRE #142; |LEE 001#142p; |CIERRA #142;
     |ABRE #279; |LEE 000#279p; |CIERRA #279;
     nPreDtoAnt = 0;

     DescAmpli = #142#197;
     |SI DescAmpli = "N";
          DescAmpli = #142#40;
     |FINSI;
     recoge    = #142#170;
     aTipoTar  = #1#79; ||---- Tarifa Norma o Con Dtos.
     |HAZ LinDiv2;
|FPRC;

|PROCESO LinDiv2;
     |HAZ PintaMedidaV;
     |ABRE #agifa321;
     |LEE 001#agifa321.p;  || Leo parametros de divisa
     |SI ac_divisa = "S"; |Y pintado = 0; || Si divisas estan activadas y no he pintado ...
       |SI #1#67 = "D";   || Si la moneda de trabajo es la divisa la pinto en
         |ATRI R;         || el encabezado del precio y el importe
         titdiv = "Precio" + "(" + #1#65 + ")";
         |PINTA 1444,titdiv;
         titdiv = "I.Tot." + "(" + #1#65 + ")";
         |PINTA 1468,titdiv;
         |ATRI r;
       |FINSI;
       |SI #agifa321#3 = "S";  || Si la visualizacion esta activada ...
         mensa = "Precio:            Imp.:              T.Bruto:             T.FAC:             ";
         |PINTA 1102,mensa;  || ... pinto la linea de visualizacion
         |PINTA #0#26; || Pinto precio visual
         |PINTA #0#27; || Pinto importe visual
         |SI #1#67 = "D"; || Si la moneda de trabajo es la divisa
           |PINTA 1149,#1#74;
           |PINTA 1168,#1#75;
         |SINO;
           |PINTA 1149,#1#70;
           |PINTA 1168,#1#71;
         |FINSI;
         pintado = 1;  || Ya hemos pintado
       |FINSI;
     |FINSI;
     |CIERRA #agifa321;
|FPRC;

|PROCESO precio_divisa;
   #0#24 = #0#6 / #1#69 * #1#66;  || Calculo el precio en divisa
   |HAZ decim1;
|FPRC;

|PROCESO decim1;
     |SI #1#67 = "D";  || Si la moneda de trabajo es la divisa ...
       #0#6 = #0#24 / #1#66 * #1#69;  || ... recalculo en funcion de la divisa
     |SINO;
       #0#24 = #0#6 / #1#69 * #1#66; || .. recalculo en funcion de la moneda base
     |FINSI;
     |HAZ visual_precio; || Visualiza el precio
|FPRC;

|PROCESO visual_precio; || Pinta el precio visual.
|SI #1#67 = "D";  || Si la moneda de trabajo es la divisa ...
  #0#26 = #0#6;   || ... el campo visualiz. es el precio en moneda base
  |PINTA #0#24;
|SINO;            || Si la moneda de trabajo es la moneda base ...
  #0#26 = #0#24;  || ... el campo visual. es el precio en divisa
  |PINTA #0#6;
|FINSI;
|SI ac_divisa = "S"; |Y #agifa321#3= "S";
  |PINTA #0#26; || Si las divisas y la visualizacion estan activadas, pinto
|FINSI;
|FPRC;


|PROCESO visual_importe;  || Pinta el importe visual
|SI #1#67 = "D";  || Si la moneda de trabajo es la divisa ...
  #0#27 = #0#9;   || el campo visualiz. es el importe en moneda base
  |PINTA #0#25;
|SINO;            || Si la moneda de trabajo es la moneda base ...
  #0#27 = #0#25;  || el campo visualiz. es el importe en divisa
  |PINTA #0#9;
|FINSI;
|SI ac_divisa = "S"; |Y #agifa321#3= "S";
  |PINTA #0#27;   || Si las divisas y la visualizacion estan activadas, pinto
|FINSI;
|FPRC;

|PROCESO visual_totales;
   |ABRE #agifa321;
   |LEE 001#agifa321.p;
   |SI #1#67 = "D";
     |SI ac_divisa = "S"; |Y #agifa321#3= "S";
       |PINTA 1149,#1#74;
       |PINTA 1168,"        ";
       |PINTA 1168,#1#75;
     |FINSI;
     |CIERRA #agifa321;
     |PINTA 1349,#1#70;
     |PINTA 1366,#1#71;
   |SINO;
     |SI ac_divisa = "S"; |Y #agifa321#3= "S";
       |PINTA 1149,#1#70;
       |PINTA 1168,"        ";
       |PINTA 1168,#1#71;
     |FINSI;
     |CIERRA #agifa321;
     |PINTA 1349,#1#15;
     |PINTA 1366,#1#41;
 |FINSI;
|FPRC;

|CALCULO Promart;
     |SI #142#250 = "S"; |Y eaProCliArt = "S";
          articulo = #0#2;
          |ACABA;
     |FINSI;

     |SI nTecla = 16;
          eaArticulo = #0#2;
          eaGrupo = #6#158;
          enAlmacen = #0#3;
          |HAZ PintaPromo;
          |ENCAMPO #5#0;
     |FINSI;
     enPromo = 1;
     |HAZ PrecArt;
     enPromo = 0;
     ||ÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ
     articulo = #0#2;
|FCAL;

|PROCESO CogeValores147;  |TIPO 7;
     |SI Campo = 6;
          aAlfa = "0000<F6> Consulta Precios, <F7> Calculo Precio Sin Iva ";
          |MENSA aAlfa;
     |FINSI;
     |SI Campo = 3;
         |C_M #0Campo, 1, "S";
         |SI #142#99 = "S";
             |C_M #0Campo, 1, "N";
             #0Campo = 1;  |PINTA #0Campo;
         |FINSI;
     |FINSI;

     |SI #2#176 = "N";  |ACABA;  |FINSI;

     |SI Campo = 3;
         |SI enAlmacen ! 0;
             #0#3 = enAlmacen;  |PINTA #0#3;
         |FINSI;
     |FINSI;

     |SI Campo = 5;
         |SI enUnidades ! 0;
             |SI #0#5 ! enUnidades;  Contenido = #0#5;  |FINSI;
             #0#5 = enUnidades;  |PINTA #0#5;
         |FINSI;
     |FINSI;

     |SI Campo = 6;
         |SI nPrecio ! 0;
             #0#6 = nPrecio;  |PINTA #0#6;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO PideArt147;  |TIPO 7;
     nModo = (FEntrada/100) ! 0;
     |SI #142#140 = "S"; |Y nModo = 1; #0#2 = eaArtAnt; |FINSI;

     |C_M #0#2, 1, "S";
     |C_M #0#3, 1, "N";

     |SI #0#2 ! "                    ";
         |ABRE #2;
         #2#0 = #0#2;
         |LEE 000#2=;
         |SI FSalida ! 0;  |DDEFECTO #2;  |FINSI;
         |CIERRA #2;
         |SI #2#176 = "S";
             |C_M #0#2, 1, "N";
             |C_M #0#3, 1, "N";
             |ACABA;
         |FINSI;
     |FINSI;

     |ABRE #910;
     |LEE 040#910p;
     |SI FSalida ! 0;  |DDEFECTO #910;  |FINSI;
     |CIERRA #910;

     |SI #910#2 [ 2;    |ACABA;  |FINSI;

     eAlfa = #0Campo;
     |SUB_EJECUTA_NP "dsz99990";                 || Por Pantalla

     |SI eAlfa = "-1";  |PTEC 807;  |ACABA;  |FINSI;
     |SI eAlfa = "-2";              |ACABA;  |FINSI;

     eaArticulo = eAlfa;      ||  700000-4215

     #0Campo = eAlfa;
     |PINTA #0Campo;
     |PTEC 802;
|FPRC;

|PROCESO Calc6147;  |TIPO 6;
     eaArtAnt = #0#2;

     |SI #0Campo ! Contenido; |Y Campo = 2;
          eaArticulo = #0#2;
          eaCliente = #1#3;
          |HAZ DefectoMediV;
          #0#31 = enMedida1;
          #0#32 = enMedida2;
          #0#33 = enMedida3;
          |HAZ PintaMedidaV;
     |FINSI;
     |HAZ DobleArtiV;
     |SI eaSw2Stock = "S"; |ACABA; |FINSI;

     enAlta = 1;
     eaArticulo = #0Campo;
     |HAZ LlenaArticulo;
     |SI FSalida = 0;
          #0Campo = eaArticulo;
          |PINTA #0Campo;
     |SINO;
          |ERROR; |ACABA;
     |FINSI;

     |ABRE #2;
     #2#0 = #0#2;
     |LEE 000#2=;
     |SI FSalida ! 0;  |DDEFECTO #2;  |FINSI;
     |CIERRA #2;

     |SI #2#176 = "S";
         eaTipo      = "FP";
         eaSerie     = #0#20;
         enCodigo    = #0#0;
         enLinea     = #0#1;
         eaArticulo  = #0#2;
         enAlmacen   = #0#3;
         enUnidades  = #0#5;
         enTBruto    = #0#7;
         eaAbono     = "N";
         enTarifa    = #6#39;
         eaCliente   = #1#3;
         efFecha     = #1#1;
         enCamBas    = #1#66;
         eaMoneda    = #1#67;
         enCamDiv    = #1#69;

         |SUB_EJECUTA_NP "dsz00002";

         #0#3    = enAlmacen;
         nPrecio = enTBruto / enUnidades;

         |PTEC 802;
         |PTEC 802;
         |PTEC 802;
         |PTEC 802;
     |FINSI;

     |HAZ art6pfv;
     |HAZ nomodif1;

     eaCliente = #1#3;
     efFecha = #1#1;
     Contenido = articulo;
     |HAZ SacaKit;
|FPRC;

|PROCESO Tipo11147;  |TIPO 11;
     |SI FSalida = 7;  |Y #2#176 = "S";
         |MENAV "      Imposible abortar;  Termine de introducir la linea y efectue una Baja.";
         |ERROR;
     |FINSI;
|FPRC;

|PROCESO control2;
   aponer= Anosi;
   |HAZ poncontr;
|FPRC;

|PROCESO poncontr;
         fiac = Control % A109;
         fiac = fiac + relleno;
         guarda = fiac % A109;
         fiac = Control % A1001;
         Control = Control + fiac;
         Control = Control + relleno;
         Control = Control % A120;
         Control = Control + aponer;
|FPRC;

|PROCESO BorraTxtCalibra;
     |ABRE #390;
     #390#0 = #0#20;
     #390#1 = #0#0;
     #390#2 = nLin;
     |LEE 101#390>;
     |PARA; |SI FSalida = 0; |Y #390#0 = #0#20; |Y #390#1 = #0#0; |HACIENDO;
          |BORRA 020#390a;
          |LIBERA #390;
          |LEE 101#390s;
     |SIGUE;
     |CIERRA #390;
|FPRC;

|PROCESO Tipo3_147;|TIPO 3;
     aAlfa = #0#2;
     |SI aAlfa ! ""; |Y DescAmpli = "S";
          eaSerie = #0#20;
          enCodigo = #0#0;
          enLinea = #0#1;
          eaArticulo = #0#2;
          aTipoDA = "X"; |HAZ AltaDA;
     |FINSI;

     aArti = #0#2;
     aAlfa = aArti - "CAL";
     |SI aArti ! aAlfa;
          |PUSHV 0501 2380;
          |BLANCO 1608 2273;
          #39#0 = #0#40;
          #39#1 = #0#41;
          |PINPA #0#39;
          |PINDA #0#39;
          |ENDATOS #1#39;
          #0#40 = #39#0;
          #0#41 = #39#1;
          |POPV;

          nLin = #0#5;
          |HAZ BorraTxtCalibra;

          |SI #0#5 > 1;
               |PUSHV 0501 2380;
               |ENTLINEAL #1#390, 3, 2, 22, 1, Min390, Max390;
               |POPV;
          |FINSI;
     |SINO;
          nLin = 0;
          |HAZ BorraTxtCalibra;
     |FINSI;
|FPRC;

|PROCESO Min390;
     #390#0 = #0#20;
     #390#1 = #0#0;
     #390#2 = 1;
|FPRC;

|PROCESO Max390;
     #390#0 = #0#20;
     #390#1 = #0#0;
     #390#2 = 9999;
|FPRC;

|PROCESO MaxCali; |TIPO 0;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 1; |Y #390#2 > #0#5;
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO Recoge;
     |SI inkey = 12; |Y recoge ="N"; ||F4
          |PUSHV 0400 0479;
          |MENAV "0000Opcion no activada en Parametros Generales";
          |POPV;
          |ERROR;
          |ACABA;
     |FINSI;
     || compruebo que este en la ultima linea y en modo alta
     nLineaD = #0#1;    || Guardo puntero
     nLineaD = nLineaD +1;     || Porque si estoy en alta la linea en la que
                               || estoy todavia no esta grabada
     |LEE 001#0];

     |SI FSalida ! 0;|O #0#0 ! #1#0;
                      |O #0#20 ! #1#48;|Y modo = 1;

          nLineaD = nLineaD -1;

          || Recupero el puntero
          #0#0  = #1#0;    || pedido
          #0#20 = #1#48;   || Serie
          #0#1  = nLineaD;        || Numero de linea
          |LEE 001#0=;

       ||   nLineaD = nLineaD + 4;
          nerror = 0;
          |SI inkey = 12;
               |HAZ entradatos;
          |FINSI;
          |SI nerror = 0 ;
               |SI inkey = 12; ||F4
                    |HAZ leelineas;
               |FINSI;
               |HAZ control2;
               |PTEC 816;
               |PTEC 812;
          |SINO;
               |ERROR;
          |FINSI;
     |SINO;
          || Recupero el puntero
          nLineaD = nLineaD - 1;
          #0#0  = #1#0;     || pedido
          #0#20 = #1#48;    || Serie
          #0#1  = nLineaD;         || Numero de linea
          |LEE 001#0=;

          |MENAV "0000Debe estar en la ultima linea";
          |ERROR;
     |FINSI;    || Si #0#1 = 1 ...
|FPRC;

|PROCESO entradatos;
     |PINTA 2420 , " Recogida de datos F.Proforma  :   /    ";
     alfa1 = "";
     E_sup = 5;
     E_inf = "";
     alfa1 = 2453 ? 1;
     serie_o = alfa1;
     alfa2 = "";
     E_sup = 999999;
     E_inf = 000000;
     alfa2 = 2459 ? 1;
     pedido_o = alfa2;
     |BLIN 24;
     || No puedo leer en el que estoy sino se embuclaria
     |SI  serie_o = #1#48;|Y pedido_o = #1#0;
         |MENAV "0000ATENCION !!! Esta en esta F.Proforma";
         nerror = 1;
     |FINSI;
|FPRC;

|PROCESO dsm00046Kit;
     #946#0 = #0#20;
     #946#1 = #0#0;
     #946#2 = #0#1;
     |GRABA 020#946n;
|FPRC;

|DEFBUCLE dsm00046Kit;
     #946#1;
     ;
     serie, pedido, nlinea, 001;
     serie, pedido, nlinea, 999;
     ;
     NULL, dsm00046Kit;
|FIN;

|PROCESO CreaComponente;
     #905#0 = "PV";
     #905#1 = #0#20;
     #905#2 = #0#0;
     #905#3 = #0#1;
     #905#7 = #0#6;
     #905#8 = #905#6 * #905#7;
     #905#9 = 0;
     #905#11 = 0;
     #905#12 = 0;
     |GRABA 020#905n;
     |LIBERA #905;
|FPRC;

|DEFBUCLE dsm00005;
     #905#1;
     ;
     "PV", serie, pedido, nlinea, "      ";
     "PV", serie, pedido, nlinea, "zzzzzz";
     be;
     NULL, CreaComponente;
|FIN;

|PROCESO crealineasV;
    || Clave primaria
    #0#20= #1#48;        || Serie
    #0#0 = #1#0;         || Numero de Pedido
    #0#1 = nLineaD;      || Numero de linea

    |BUCLE dsm00046Kit; || Kit

    |DEFECTO #0#1;   || Cargo los valores de defecto de cab.pedido

    || Procedo a cambiar lo que no es igual

    |HAZ defecfc;                  || Este proceso carga los defectos, ya que
                                   || estos no estan puestos en D000.....
                                   || porque no iba bien.
    || Procedo a cambiar lo que no es igual
    #0#12 = "";             || Numero de pedido
    nPrecio = #0#6;

    |HAZ totfc;        || Calculo total linea
    |HAZ fcacarti;     || Lo envio al t ipo 2 para que calcule lo mismo que si fuera
                       || una linea introducida por el operador
    #0#6 = nPrecio;
    |GRABA 021#0b;
    |LIBERA #0;

    |BUCLE dsm00005;

    nLineaD = nLineaD + 5;  || Aumento contador de lineas
|FPRC;

|PROCESO leelineas;
      #0#20 = serie_o;
      #0#0  = pedido_o;
      #0#1  = 1;
      |LEE 001#0];
      |SI FSalida = 0;
           |PARA; |SI FSalida =0; |Y #0#20 = serie_o; |Y #0#0 = pedido_o;
                                                     |HACIENDO;
                  || Me guardo el puntero
                  serie   = #0#20;   || Serie y pedido actual
                  pedido  = #0#0;
                  nlinea  = #0#1;

                  |HAZ crealineasV;

                  || Recupero puntero
                  #0#20 = serie;
                  #0#0  = pedido;
                  #0#1  = nlinea;
                  |LEE 001#0=;

                  |LEE 001#0s;
           |SIGUE;
      |SINO;
           |MENAV "0000Atencion !!! No se ha encontrado la F.Proforma indicada";
           |ERROR;
      |FINSI;

      pedido_o = 0;

      || Devuelvo el puntero de la linea
      #0#20= #1#48;       || Serie
      #0#0 = #1#0;        || pedido
      #0#1 = nLineaD;     || Linea
      |LEE 001#0=;
|FPRC;

|PROCESO Tipo5_147;  |TIPO 5;
     |HAZ RecalOfe;
|FPRC;

|PROCESO CtaArt1;
   eaModulo = "agifa146"; |HAZ CtaArt;
|FPRC;

|PROCESO Equivalentes;
     eaArticulo = #0#2;
     enAlmacen = #0#3;
     |SUB_EJECUTA_NP "agifa294";
     #0#2 = eaArticulo; |PINTA #0#2;
     #0#3 = enAlmacen; |PINTA #0#3;
     |ABRE #2;
     #2#0 = #0#2;
     |LEE 000#2=;
     |CIERRA #2;
     #0#4 = #2#1;
     |PINTA #0#4;
     |SI enSalida ! 0; |Y eaAuto = "N"; |ERROR; |FINSI;
|FPRC;

|PROCESO Descri147; |TIPO 6;
     |SI FSalida = 14;             ||F6
          eaAuto = "N";
          |HAZ Equivalentes;
     |FINSI;
|FPRC;

|PROCESO Minc147;
     #referen@#20 = #1#48;
     #referen@#0 = #1#0;
     #referen@#1 = 001;
|FPRC;

|PROCESO Maxc147;
     #referen@#20 = #1#48;
     #referen@#0 = #1#0;
     #referen@#1 = 999;
|FPRC;

|PROCESO Consulta147; |TIPO 66;
     nLin = 0;
     |DDEF aAlfa;
     aAlfa = aAlfa + "pcegm003";
     |CARGA_DEF aAlfa, referen@;
     |SI FSalida = 0;
          |ABRE #referen@;
          |CONSULTA_F_CLAVE #1#referen@, Minc147, Maxc147;
          |SI FSalida = 0; nLin = #referen@#1; |FINSI;
          |CIERRA #referen@;
          |DESCARGA_DEF #referen@;
     |FINSI;
     |SI nLin ! 0;
          |PTEC 802;
          aAlfa = ("000" + nLin) % -103;
          aPtec = aAlfa % 301; nPtec = &aPtec;
          |PTEC nPtec;
          aPtec = aAlfa % 201; nPtec = &aPtec;
          |PTEC nPtec;
          aPtec = aAlfa % 101; nPtec = &aPtec;
          |PTEC nPtec;
     |FINSI;
|FPRC;

|PROCESO T7_147; |TIPO 7;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 1;
          #0#1 = #0#1 + #agifa142#236 - 1;
     |FINSI;
|FPRC;

|PROCESO ImportaPDA147;
     |DDEF aAlfa; aAlfa = aAlfa + "pcegm003";
     |CARGA_DEF aAlfa, referen@;
     |SI FSalida ! 0; |ACABA; |FINSI;
     |ABRE #referen@;
     #referen@#20 = #1#48;
     #referen@#0 = #1#0;
     #referen@#1 = 999;
     |LEE 000#referen@.];
     |SI FSalida = 0;
          |LEE 000#referen@.a;
     |SINO;
          |LEE 000#referen@.u;
     |FINSI;
     |SI FSalida = 0; |Y #referen@#20 = #1#48; |Y #referen@#0 = #1#0;
          nLin = #referen@#1 + 1;
     |SINO;
          nLin = 1;
     |FINSI;
     |CIERRA #referen@;
     |DESCARGA_DEF #referen@;

     |DDEF aAlfa; aAlfa = aAlfa + "psion067";
     |CARGA_DEF aAlfa, referen@;
     |SI FSalida ! 0; |ACABA; |FINSI;
     |NOME_DAT #referen@#eaFichero#;
     |ABRE #referen@;
     |LEE 000#referen@.p;
     |PARA; |SI FSalida = 0; |HACIENDO;
          #0#20 = #1#48;
          #0#0 = #1#00;
          #0#1 = nLin;
          |GRABA 020#0b;
          #0#2 = #referen@#0; ||Arti
          #0#4 = #referen@#1; ||Des
          #0#3 = #referen@#2; ||Alma
          #0#5 = #referen@#3; ||Uni
          #0#29 = #referen@#9; ||Parti
          FEntrada = 100;     ||simula alta
          |HAZ PrecArt;
          |HAZ TotalLin002;
          |HAZ fcacarti;
          |GRABA 020#0a;
          |LIBERA #0;
          nLin = nLin + 1;
          |LEE 000#referen@.s;
     |SIGUE;
     |CIERRA #referen@;
     |DELINDEX #referen@;
     |DESCARGA_DEF #referen@;
     |PONCONTROL 23, "SI"
     |PTEC 809;
     |PTEC 808;
|FPRC;

|PROCESO MenuTrack147;
     |VARIABLES; {20} aPopup = ""; |FINSI;
     aPopup1 = "-1";  || Si fuera -1 en la posicion
     aPopup2 = 6;
     aPopup3 = "<F4> Copia Proforma";
     aPopup4 = "<F5> Doble Stock/Kit";
     aPopup5 = "<F6> Precios Cliente";
     aPopup6 = "<F8> Descripción ampliada";
     aPopup7 = "<F12> Consulta Cabecera";
     aPopup8 = "<Ctrl+F7> Captura PDA";
     IaPopup = aPopup1  <-;
     |ESPECIFICA "TRACKPOPUP", aPopup;
     nSal = FSalida;
     |SI FSalida = 1; nSal = 12; |FINSI;
     |SI FSalida = 2; nSal = 13; |FINSI;
     |SI FSalida = 3; nSal = 14; |FINSI;
     |SI FSalida = 4; nSal = 16; |FINSI;
     |SI FSalida = 5; nSal = 9; |FINSI;
     |SI FSalida = 6; nSal = 25; |FINSI;
     FSalida = nSal;
|FPRC;

|PROCESO Alma147_66; |TIPO 66;
     |ABRE #29;
     #29#0 = #0#3;
     |LEE 000#29];
     |CONSULTA_CLAVE #1#29;
     |SI FSalida = 0;
          #0#3 = #29#0;
          |PINTA #0#3;
     |SINO;
          |ERROR;
     |FINSI;
     |CIERRA #29;
|FPRC;

|PROCESO CheqPCM_PVP147; |TIPO 0;
     eaArticulo = #0#2;
     enPrecio = #0#6;
     enDto1 = #0#8;
     enDto2 = #0#21;
     |HAZ CheqPCM_PVP;

     |SI #279#3 = "S";
          eaArticulo = #0#2;
          enPrecio = #0#6;
          enDto1 = #0#8;
          enDto2 = #0#21;
          nNume =  (enPrecio < enDto1) < enDto2;
          |SI nNume ! nPreDtoAnt;
               |SUB_EJECUTA_NP "dsz00100";
               |SI enError ! 0; |ERROR; |ACABA; |FINSI;
               nPreDtoAnt = nNume;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO T147_6_0; |TIPO 0;
     |SI #279#3 = "S";
          eaArticulo = #0#2;
          enPrecio = #0#6;
          enDto1 = #0#8;
          enDto2 = #0#21;
          nNume =  (enPrecio < enDto1) < enDto2;
          |SI nNume ! nPreDtoAnt;
               |SUB_EJECUTA_NP "dsz00100";
               |SI enError ! 0; |ERROR; |ACABA; |FINSI;
               nPreDtoAnt = nNume;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO unidades; |TIPO 6;
     aArti = #0#2; |QBF aArti;
     aAlfa = aArti - "CAL";
     |SI aAlfa ! aArti;
          #0#5 = #0#5 ! 0; |PINTA #0#5;
     |FINSI;
|FPRC;

|PROCESO IVA003_66; |TIPO 66;
     enTiva = #0Campo;
     efFiva = #1#1;
     |HAZ ConsuIVA;
     |SI eaDiva = "";
          |ERROR;
     |SINO;
          #0Campo = enTiva; |PINTA #0Campo;
     |FINSI;
|FPRC;

|PROCESO IVA003_0; |TIPO 0;
     enTiva = #0Campo;
     efFiva = #1#1;
     |HAZ SacaIVA;
     |SI eaDiva = "";
          |MENAV "0000No existe tipo iva para esta fecha...";
          |ERROR; |ACABA;
     |FINSI;
|FPRC;
