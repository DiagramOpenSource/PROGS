|FICHEROS;
     alby0001 #0;
     agifa077 #77;
     agifa080 #80;

     agifa019 #19;       ||Articulos
|FIN;

|VARIABLES;
     aAlfa    = "";
     aAbre    = "";
     aBase    = "";

     nSwHay   = 0;
     nHandle  = 0;
     nCopia   = 0;

     &eaSerie   = "";
     &enAlbaran = 0;
     nBlancos = 0;
     nEtiq = 0;
     &enPosicion = 0;
     aCodigo = "";
|FIN;

|PROCESO agifa080;
     |SI #80#37 ] #0#6; |Y #80#37 [ #0#7;
          aCodigo = #80#37;
          |QBT aCodigo;
          aCodigo = aCodigo - "/";
          aCodigo = aCodigo - "\";
          aAlfa = (("000000000000" + aCodigo) % -112) + "#";
          aAlfa = aAlfa + (("000000" + aCodigo) % -106) + "#";
          aAlfa = aAlfa + #77#3;                   |QBF aAlfa;

          |DDEFECTO #agifa019;
          #agifa019#0 = #80#2;
          |LEE 000#agifa019.=;

          aAlfa = aAlfa + "#" + (#agifa019#195 % 131);     |QBF aAlfa;
          aAlfa = aAlfa + " " + &13 + &10;

          |PARA nCopia = 1;  |SI nCopia [ #80#36;  |HACIENDO nCopia = nCopia + 1;
                |XGRABA nHandle, aAlfa;
          |SIGUE;
          nSwHay = 1;
     |FINSI;
|FPRC;

|DEFBUCLE agifa077;
     #77#1;
     1;
     #0#0, #0#2, #0#4;
     #0#1, #0#3, #0#5;
     ;
     NULL, NULL, agifa080;
|FIN;

|PROCESO Tipo3;  |TIPO 3;
     aBase = "\BARRAS\";
     aAbre = aBase + "barcods0.exe";
     |XABRE "EC", aAbre, nHandle;
     |SI FSalida < 0;
         |MENAV "0000 No tiene instalado el Programa de Emision de Codigo de Barras. Pongase en contacto con su Distribuidor.";
         |ACABA;
     |FINSI;

     aBase = "\BARRAS\";
     aAbre = aBase + "BARRAS.TXT";
     |XABRE "CW", aAbre, nHandle;
     |SI FSalida < 0;  |ACABA;  |FINSI;

     nBlancos = #alby0001#8 - 1;
     |SI nBlancos ] 1;
          aAlfa = "            #      #      #";
          aAlfa = aAlfa + " " + &13 + &10;
          |PARA nEtiq = 1; |SI nEtiq [ nBlancos; |HACIENDO nEtiq = nEtiq + 1;
               |XGRABA nHandle, aAlfa;
          |SIGUE;
     |FINSI;

     |ABRE #agifa019;
     |BUCLE agifa077;
     |CIERRA #agifa019;

     |XCIERRA nHandle;

     |SI nSwHay = 0;
         |MENAV "0000 Seleccion Vacia.";
         |RFBORRA aAbre;
         |ACABA;
     |FINSI;

     aAbre = aBase + "IMPRIME.BAT";
     |XABRE "CW", aAbre, nHandle;
     |SI FSalida < 0;  |ACABA;  |FINSI;

     aAlfa = "CD \" + &13 + &10;
     |XGRABA nHandle, aAlfa;

     aAlfa = "CD BARRAS" + &13 + &10;
     |XGRABA nHandle, aAlfa;

     aAlfa = aBase + "barcods0 " + "BARRAS.TXT";
     |XGRABA nHandle, aAlfa;

     |XCIERRA nHandle;

     aAlfa = "\BARRAS\IMPRIME.BAT";

     |RSYSTEM aAlfa;
     |RFBORRA aAbre;
|FPRC;

|PROGRAMA;
     |SI enAlbaran ! 0;
         #0#0 = eaSerie;
         #0#1 = eaSerie;
         #0#2 = enAlbaran;
         #0#3 = enAlbaran;
         #0#4 = "01.01.0000";
         #0#5 = "31.12.2999";
         #0#8 = enPosicion;
         |HAZ Tipo3;
     |SINO;
         |MANTE #0;
     |FINSI;
|FPRO;
