|FICHEROS;
     marhu101 #0 ;           || Este Fichero
     marhu004 #4 ;           || Pase de clientes
     marhu016 #16;           || CCC
     marhu014 #14;           || Sucursales
     marhu200 #200;          ||Equivalencia principio codigo cliente
     agifa024 #24;           || Clientes
     dsm00003 #3 ;           || Direcciones de entrega
|FIN;

|VARIABLES;
     nContador  = 0;
     nContador1 = 1;
     aContador  = "";
     aAlfa1     = "";
     aAlfa2     = "";
     nNume1 = 0;
     nNume2 = 0;
     aCP1 = "";
     aCP2 = "";
     aPoblacion = "";
     aProvincia = "";
     rLong = "";
     nLong = 0;
     aCaracter = "";
     bEncuentroParentesis = 0;
     EsSuperficie = "";
     nInicial = 100000;
     aActual = "";
     nDir    = 0;
     EsMismoCli = "N";
|FIN;

|RUTINA AnalizaPoblacion;
     aCP1 = #4#3 % A102;
     aCP2 = #4#3 % A303;
     aAlfa1 = #4#3;
     |QBF aAlfa1;
     rLong = aAlfa1 % A0;
     nLong = rLong;
     nLong = rLong - 6;
     nLong = (100 + nLong) * -1;
     rLong = nLong;
     aAlfa1 = aAlfa1 % rLong;
     rLong = aAlfa1 % A0;
     nLong = rLong;
     aPoblacion = "";
     aProvincia = "";
     bEncuentroParentesis = 0;
     |PARA nNume1=1;|SI nNume1 [ nLong;|HACIENDO nNume1=nNume1+1;
          nNume2 = (nNume1 * 100) + 1;
          rLong = nNume2;
          aCaracter = aAlfa1 % rLong;
          |SI aCaracter ! "(";|Y aCaracter ! ")";
               |SI bEncuentroParentesis = 1;
                    aProvincia = aProvincia + aCaracter;
               |SINO;
                    aPoblacion = aPoblacion + aCaracter;
               |FINSI;
          |SINO;
               bEncuentroParentesis = 1;
          |FINSI;
     |SIGUE;
     |SI aProvincia = "";
          aProvincia = aPoblacion;
     |FINSI;
|FRUT;

|RUTINA Graba024;
     aAlfa1 = #4#0;
     aContador = aAlfa1 % A406;
     aAlfa1 = #4#0 % A103;
     |SI aAlfa1 ! "430";
          |SI aActual = aAlfa1;
              EsMismoCli = "S";
          |SINO;
              nInicial = nInicial + 1;
              EsMismoCli = "N";
              aActual = aAlfa1;
          |FINSI;
          aContador = nInicial;
          EsSuperficie = "S";
/*
          |SI aAlfa1 ! "800";|Y aAlfa1 ! "801";
               aAlfa2 = #4#0 % -103;
          |SINO;
               |SI aAlfa1 = "800";
                    aAlfa2 = nContador1;
                    nContador1 = nContador1 + 1;
                    aAlfa2 = nContador1;
                    aAlfa2 = "000" + aAlfa2;
                    aAlfa2 = aAlfa2 % -103;
               |SINO;
                    aAlfa2 = #4#0 % 403;
               |FINSI;
          |FINSI;
          aContador = aAlfa1 + aAlfa2;
*/
     |SINO;
          EsSuperficie = "N";
          EsMismoCli="N";
     |FINSI;
     #24#0 = aContador;                    || Codigo
     aAlfa1 = "GRABANDO CLIENTE: " + #24#0;
     |PINTA 2020,aAlfa1;
     |SI EsMismoCli = "N";
       |LEE 000#24=;
       |SI FSalida ! 0;
          |DDEFECTO #24;
          #24#164 = #4#0;                  || Codigo antiguo
          #24#0 = aContador;               || Codigo
          #24#1 = #4#1;                    || Nombre fiscal
          #24#2 = #4#1;                    || Nombre Comercial
          #24#3 = #4#6;                    || Zona
          #24#5 = #4#2;                    || Direccion envio
          |HAZ AnalizaPoblacion;
          #24#6 = aPoblacion;              || Poblacion envio
          #24#8 = #4#2;                    || Direccion factura
          #24#9 = aPoblacion;              || Poblacion factura
                              || Envio
          #24#178 = aCP1;               || CP1
          #24#179 = aCP2;               || CP2
          #24#180 = aCP1+aCP2;          || CP completo
          #24#7   = aProvincia;          || Provincia
                              || Factura
          #24#181 = aCP1;               || CP1
          #24#182 = aCP2;               || CP2
          #24#183 = aCP1+aCP2;          || CP completo
          #24#10  = aProvincia;          || Provincia
                              || Cobro
          #24#184 = aCP1;               || CP1
          #24#185 = aCP2;               || CP2
          #24#186 = aCP1+aCP2;          || CP completo
          #24#13  = aProvincia;          || Provincia
          |SI #4#8 = "RE";
               #24#47 = "S";                    || R.E.
          |SINO;
               #24#47 = "N";
          |FINSI;
          #24#38 = #4#10 / 100;              || Dto. P.P.
          #24#15 = #4#12;                    || NIF
          #24#17 = #4#13;                    || Telefono
          aAlfa1 = #4#15;
          |QBF aAlfa1;
          |SI aAlfa1 = "";
               #24#11 = #4#2;
          |SINO;
               #24#11 = #4#15;                    || Direccion pago
          |FINSI;
          #24#12 = aPoblacion;               || Poblacion pago
          #24#36 = #4#36;                    || Persona de contacto
          #24#106 = (#4#17 / 100);           || Total vtas año anterior
          #24#109 = (#4#22 / 100);           || Impagados año anterior
          #24#154 = (#4#29 / 100);           || Abonos (Devoluciones) año anterior
          #24#53 = #4#46;                    || Comentario en factura
          #24#29 = #4#5;                     || Nombre banco
          #24#16 = #4#0;                     || Cuenta contable
          #24#192 = "ESP";                   || Divisa por defecto
          |SI EsSuperficie = "S";            || Tarifa
               #24#39 = 6;
          |SINO;
               #24#39 = 1;
          |FINSI;
          |ABRE #16;
          #16#0 = #4#0;
          |LEE 000#16=;
          |SI FSalida=0;
               aAlfa1 = #16#1;
               |QBF aAlfa1;
               #24#32 = aAlfa1;          || Banco (4 digitos)
               aAlfa1 = #16#2;
               |QBF aAlfa1;
               #24#33 = aAlfa1;          || Sucursal (4 digitos)
               aAlfa1 = #16#3;
               |QBF aAlfa1;
               #24#31 = aAlfa1;          || DC (2 digitos)
               aAlfa1 = #16#4;
               |QBF aAlfa1;
               #24#30 = aAlfa1;          || Cuenta corriente (10 digitos)
          |FINSI;
          |CIERRA #16;
          |ABRE #14;
          aAlfa1 = #24#32;
          |QBF aAlfa1;
          aAlfa1 = aAlfa1 + #24#33;
          |QBF aAlfa1;
          nNume1 = aAlfa1;
          #14#0 = nNume1;
          |LEE 000#14=;
          |SI FSalida = 0;
               #24#162 = #14#1;          || Direccion Sucursal
               #24#34 = #14#2;               || Poblacion Sucursal
          |FINSI;
          |CIERRA #14;
          |GRABA 020#24n;
          aAlfa1 = "GRABADO CLIENTE: " + #24#0;
          |PINTA 2020,aAlfa1;
       |FINSI;
     |SINO; ||grabo una dirección alternativa
       |LEE 000#24=;
       |SI FSalida = 0;
                 |ABRE #3;
                 nDir = 1;
                 #3#0 = aContador;
                 |PARA nDir = 1; |SI nDir [ 9999; |HACIENDO nDir = nDir + 1;
                      #3#1 = nDir;
                      |LEE 000#3=;
                      |SI FSalida ! 0 ;
                           |SAL;
                      |FINSI;
                 |SIGUE;
                 #3#2 = #4#2;
                |HAZ AnalizaPoblacion;
                #3#3 = aPoblacion;              || Poblacion envio
                #3#4 = aCP1;
                #3#5 = aCP2;
                #3#6 = aCP1 + aCP2;
                #3#7 = aProvincia;
                #3#11 = #4#6;
                #3#10 = #4#1;
                |GRABA 020#3n;
       |FINSI;
     |FINSI;
|FRUT;

|PROGRAMA;
     |CLS;
     |CABEZA "Conversion Clientes";
     |ABRE #0;
     |PINPA #0#0;
     |ENCAMPO #0#0;
     |SI FSalida=0;
         |ABRE #4;
         |ABRE #24;
         |LEE 000#4p;
         nContador = 1;
         |PARA;|SI;|HACIENDO;
               |HAZ Graba024;
               |LEE 000#4s;
               nContador = nContador + 1;
               |SI FSalida ! 0; |SAL; |FINSI;
         |SIGUE;
         |CIERRA #24;
         |CIERRA #4;
     |FINSI;
|FPRO;
