CalcuIBAN
CheqIBAN
CalculoDC

|VARIABLES;
     aCta = "";
     aIban = "";
     aLon = "";
     aPais = "";
     aNuevo = "";
     aCar = "";
     nPos = 0;
     aAlfa = "";
     i = 0;
     iy = 0;
     nNume = 0;
     nResto = 0;
     aAlfa1 = "";
     aAlfa2 = "";
     nCalc = 0;
     aDC = "";
     &eaPais = "";
     &eaCta = "";
     &eaIBAN = "";
     &eaIBANant = "";
|FIN;

|PROCESO CalcuIBAN;
     |QBF eaIBANant;
     eaIBAN = "";
     aPais  = eaPais % 102; |QBF aPais;
     aCta   = eaCta; |QBF aCta;
     |SI eaIBANant ! "";
         aPais  = eaIBANant % 102; |QBF aPais;
         aCta  = eaIBANant % 530; |QBF aCta;

          aPais = aPais > "";
          |SI aPais = "IT";   || Si es italina no se calcula
               eaIBAN = eaIBANant;
               |ACABA;
          |FINSI;
     |FINSI;

     |SI aPais = "IT";   || Si es italina no se calcula
          |ACABA;
     |FINSI;

     nNume = aCta;
     aLon  = aCta % A0;
     |SI aLon ! 20; |Y aPais = "ES";
         |ACABA;
     |FINSI;  ||si es Espa¤a siempre 20

     |SI aLon ! 0; |Y aPais ! ""; |Y nNume ! 0;
          aPais = aPais > "";
          aIban = aCta + aPais + "00";
          |HAZ CambiaLetra;
          |HAZ Mod9710;
          eaIBAN = aPais + aDC + aCta;
     |FINSI;
|FPRC;

|PROCESO CheqIBAN;
     |QBF eaIBANant;
     eaIBAN = "";
     aPais  = eaPais % 102; |QBF aPais;
     aCta  = eaCta; |QBF aCta;
     |SI eaIBANant ! "";
          aPais  = eaIBANant % 102; |QBF aPais;
          aCta   = eaIBANant % 530; |QBF aCta;
     |FINSI;

     nNume  = aCta;
     aLon   = aCta % A0;

     |SI aLon ! 20; |Y aPais = "ES";
         |ACABA;
     |FINSI;  ||si es Espa¤a siempre 20

||      (aLon = 20)
     |SI aLon ! 0; |Y aPais ! ""; |Y nNume ! 0;
          aPais = aPais > "";
          aIban = aCta + aPais + "00";

          |HAZ CambiaLetra;
          |HAZ Mod9710;

          eaIBAN = aPais + aDC + aCta;
     |FINSI;

     |QBF eaIBAN;
|FPRC;

|PROCESO Mod9710;
   aAlfa = aIban;
   aAlfa2 = aAlfa % 0101;
   |PARA; |SI aAlfa2 = "0"; |HACIENDO;
      aAlfa = aAlfa % 0299; aAlfa2 = aAlfa % 0101;
   |SIGUE;

   aAlfa1 = "";
   |PARA; |SI aAlfa ! ""; |HACIENDO;
      aAlfa2 = aAlfa % 0; nCalc = aAlfa2;
      |SI nCalc > 5;
         aAlfa2 = aAlfa % 0105; aAlfa = aAlfa % 0699;
      |SINO;
         aAlfa2 = aAlfa; aAlfa = "";
      |FINSI;
      aAlfa1 = aAlfa1 + aAlfa2;
      nCalc = aAlfa1;
      nCalc = nCalc / 97;
      aAlfa2 = nCalc;
      aAlfa2 = aAlfa2 - ".";
      |SI FSalida ! 0;
         nCalc = FSalida + 98;
         aAlfa2 = aAlfa2 % nCalc;
         aAlfa2 = "0." + aAlfa2; nCalc = aAlfa2;
      |SINO;
         nCalc = 0;
      |FINSI;
      nCalc = (nCalc * 97) ! 0;
      aAlfa1 = nCalc;
   |SIGUE;
   nCalc  = aAlfa1;
   nCalc = 98 - nCalc;
   aDC = ("00" + nCalc) % -102;
|FPRC;

|PROCESO CambiaLetra;
     aNuevo = "";
     iy     = aLon; iy = iy + 4;  || (antes 24.)
     |PARA i = 1; |SI i [ iy; |HACIENDO i = i + 1;
          nPos = i * 100 + 1;
          aCar = aIban % nPos;
          |SI aCar ] "0"; |Y aCar [ "9";
               aNuevo = aNuevo + aCar;
          |SINO;
               nNume = &aCar; nNume = nNume - 55; aCar = nNume;
               aNuevo = aNuevo + aCar;
          |FINSI;
     |SIGUE;
     aIban = aNuevo;
|FPRC;
-------------------------------
|VARIABLES;
     &eaSucursal = "";
     &eaEntidad = "";
     &eaCuenta = "";
     &eaDC = "";
     Digito1 = 0;
     Digito2 = 0;
     Calc = 0;
     aCaracter = "";
     Comodin1 = "";
     Comodin2 = "";
     Cont = 0;
|FIN;

|PROCESO CalculoDC;
   Digito1 = 0; Digito2 = 0;
   Comodin1 = ""; Comodin2 = "";

   |PARA Cont = 1; |SI Cont [ 4; |HACIENDO Cont = Cont + 1;
      Calc = 0 - ((100 * Cont) + 1);
      aCaracter = eaSucursal % Calc;
      |SI aCaracter = ""; aCaracter = "0"; |FINSI;
      Comodin1 = Comodin1 + aCaracter;
   |SIGUE;
   |PARA Cont = 1; |SI Cont [ 4; |HACIENDO Cont = Cont + 1;
      Calc = 0 - ((100 * Cont) + 1);
      aCaracter = eaEntidad % Calc;
      |SI aCaracter = ""; aCaracter = "0"; |FINSI;
      Comodin1 = Comodin1 + aCaracter;
   |SIGUE;

   |PARA Cont = 1; |SI Cont [ 8; |HACIENDO Cont = Cont + 1;
      Calc = (100 * Cont) + 1;
      aCaracter = Comodin1 % Calc; Calc = aCaracter;
      |SI Cont = 1; Digito1 = Digito1 + (Calc * 6);  |FINSI;
      |SI Cont = 2; Digito1 = Digito1 + (Calc * 3);  |FINSI;
      |SI Cont = 3; Digito1 = Digito1 + (Calc * 7);  |FINSI;
      |SI Cont = 4; Digito1 = Digito1 + (Calc * 9);  |FINSI;
      |SI Cont = 5; Digito1 = Digito1 + (Calc * 10); |FINSI;
      |SI Cont = 6; Digito1 = Digito1 + (Calc * 5);  |FINSI;
      |SI Cont = 7; Digito1 = Digito1 + (Calc * 8);  |FINSI;
      |SI Cont = 8; Digito1 = Digito1 + (Calc * 4);  |FINSI;
   |SIGUE;

   Digito1 = 11 - (Digito1 $ 11);
   |SI Digito1 = 10; Digito1 = 1; |FINSI;
   |SI Digito1 = 11; Digito1 = 0; |FINSI;

   |PARA Cont = 1; |SI Cont [ 10; |HACIENDO Cont = Cont + 1;
      Calc = 0 - ((100 * Cont) + 1);
      aCaracter = eaCuenta % Calc;
      |SI aCaracter = ""; aCaracter = "0"; |FINSI;
      Comodin2 = Comodin2 + aCaracter;
   |SIGUE;

   |PARA Cont = 1; |SI Cont [ 10; |HACIENDO Cont = Cont + 1;
      Calc = (100 * Cont) + 1;
      aCaracter = Comodin2 % Calc; Calc = aCaracter;
      |SI Cont = 1;  Digito2 = Digito2 + (Calc * 6);  |FINSI;
      |SI Cont = 2;  Digito2 = Digito2 + (Calc * 3);  |FINSI;
      |SI Cont = 3;  Digito2 = Digito2 + (Calc * 7);  |FINSI;
      |SI Cont = 4;  Digito2 = Digito2 + (Calc * 9);  |FINSI;
      |SI Cont = 5;  Digito2 = Digito2 + (Calc * 10); |FINSI;
      |SI Cont = 6;  Digito2 = Digito2 + (Calc * 5);  |FINSI;
      |SI Cont = 7;  Digito2 = Digito2 + (Calc * 8);  |FINSI;
      |SI Cont = 8;  Digito2 = Digito2 + (Calc * 4);  |FINSI;
      |SI Cont = 9;  Digito2 = Digito2 + (Calc * 2);  |FINSI;
      |SI Cont = 10; Digito2 = Digito2 + (Calc * 1);  |FINSI;
   |SIGUE;

   Digito2 = 11 - (Digito2 $ 11);
   |SI Digito2 = 10; Digito2 = 1; |FINSI;
   |SI Digito2 = 11; Digito2 = 0; |FINSI;

   eaDC = Digito1 + Digito2;

|FPRC;
