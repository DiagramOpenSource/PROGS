|FICHEROS;
     psioaz06 #0;
     psioaw03 #3;   ||Tmp
     agifa024 #24;
     agifa025 #25;
     psioa075 #75;
     psioa076 #76;
|FIN;

|VARIABLES;
     nPos = 0;
     nNume = 0;
     &Tempo = "";
     &eaMensaje = "";
     nPanta = 0;
     aTmp3 = "";
     nRango = 0;
     nGrid = 0;
     nIdAlta = 0;
     aAlfa = "";
     i = 0;
     nSwInf = 0;
     aResu = "";
     aInf = "";

     ||Informe
     &eaNomRep = "";
     &eaNomCli = "";
     &eaCli = "";
     &eaRep = "";
     &enVisitas = 0;
     &enNota1 = 0;
     &enNota2 = 0;
     &enNota3 = 0;
     &enNota4 = 0;
     &enNota5 = 0;
     &enNota6 = 0;
     &enNota7 = 0;
     &enNota8 = 0;
     &enNota9 = 0;
     &enNota10 = 0;
|FIN;

|PROCESO Evento;
     |SI FSalida = 1; |O FSalida = 2;
          |SELECT #1#3;
          |LEE 000#3=;
     |FINSI;
     |SI FSalida = 4;
          |ABRE #75;
          #75#13 = #3#0;
          #75#14 = #3#1;
          |LEE 020#75=;
          |SI FSalida = 0;
               |VENTANA 0710, 2689, -1, 17, "Consulta";
               |HAZ PushVenModal;
               |PINPA #0#75;
               |PINDA #0#75;

               aAlfa = #75#11 % 101;
               |SI aAlfa = "P";
                    aAlfa = #75#11 % 304; nNume = aAlfa;
                    |ABRE #76;
                    #76#0 = nNume;
                    #76#13 = #75#14;
                    |LEE 000#76=;
                    |SI FSalida ! 0; |DDEFECTO #76; |FINSI;
                    |CIERRA #76;

                    |C_P #75#0, 0, nPos;
                    aAlfa = #75#0;
                    |C_P #75#0, 1, 1213; #75#0 = #76#1; |PINTA #75#0;
                    |C_P #75#0, 1, 1313; #75#0 = #76#2; |PINTA #75#0;
                    #75#0 = aAlfa;
                    |C_P #75#0, 1, nPos;
               |SINO;
                    |ABRE #24;
                    #24#0 = #75#11;
                    |LEE 000#24=;
                    |SI FSalida ! 0; |DDEFECTO #24; |FINSI;
                    |CIERRA #24;

                    |C_P #75#0, 0, nPos;
                    aAlfa = #75#0;
                    |C_P #75#0, 1, 1213; #75#0 = #24#1; |PINTA #75#0;
                    |C_P #75#0, 1, 1313; #75#0 = #24#2; |PINTA #75#0;
                    #75#0 = aAlfa;
                    |C_P #75#0, 1, nPos;
               |FINSI;


               |PAUSA;
               |HAZ PopVenModal;
          |FINSI;
          |CIERRA #75;
     |FINSI;
|FPRC;

|PROCESO Anotaciones;
     #3#0 = #75#13;
     #3#1 = #75#14;
     #3#2 = #75#15;

     #25#0 = #3#2;
     |LEE 000#25=;
     |SI FSalida ! 0; |DDEFECTO #25; |FINSI;
     #3#3 = #25#1;

     #3#4 = #75#11;

     aAlfa = #75#11 % 101;
     |SI aAlfa = "P";
          aAlfa = #75#11 % 304; nNume = aAlfa;
          #76#0 = nNume;
          #76#13 = #75#14;
          |LEE 000#76=;
          |SI FSalida ! 0; |DDEFECTO #76; |FINSI;
          #3#5 = #76#1;
          #24#1 = #76#1; ||para el informe
     |SINO;
          #24#0 = #3#4;
          |LEE 000#24=;
          |SI FSalida ! 0; |DDEFECTO #24; |FINSI;
          #3#5 = #24#1;
     |FINSI;

     #3#6 = #75#12;
     #3#7 = #75#16;
     |SI nSwInf = 1;
          |SI aResu = "S";
               |SI eaCli ! #75#11; |O eaRep ! #75#15;
                    |SI eaCli ! "";
                         |PRINT;
                         enVisitas = 0;
                    |FINSI;
               |FINSI;
               enVisitas = enVisitas + 1;
          |SINO;
               enNota1 = 0;
               enNota2 = 0;
               enNota3 = 0;
               enNota4 = 0;
               enNota5 = 0;
               enNota6 = 0;
               enNota7 = 0;
               enNota8 = 0;
               enNota9 = 0;
               enNota10 = 0;
               aAlfa = #75#1; |QBF aAlfa; |SI aAlfa ! ""; enNota1 = 1; |FINSI;
               aAlfa = #75#2; |QBF aAlfa; |SI aAlfa ! ""; enNota2 = 1; |FINSI;
               aAlfa = #75#3; |QBF aAlfa; |SI aAlfa ! ""; enNota3 = 1; |FINSI;
               aAlfa = #75#4; |QBF aAlfa; |SI aAlfa ! ""; enNota4 = 1; |FINSI;
               aAlfa = #75#5; |QBF aAlfa; |SI aAlfa ! ""; enNota5 = 1; |FINSI;
               aAlfa = #75#6; |QBF aAlfa; |SI aAlfa ! ""; enNota6 = 1; |FINSI;
               aAlfa = #75#7; |QBF aAlfa; |SI aAlfa ! ""; enNota7 = 1; |FINSI;
               aAlfa = #75#8; |QBF aAlfa; |SI aAlfa ! ""; enNota8 = 1; |FINSI;
               aAlfa = #75#9; |QBF aAlfa; |SI aAlfa ! ""; enNota9 = 1; |FINSI;
               aAlfa = #75#10; |QBF aAlfa; |SI aAlfa ! ""; enNota10 = 1; |FINSI;
               |PRINT;
          |FINSI;
          eaCli = #75#11;
          eaRep = #75#15;
          eaNomRep = #25#1;
          eaNomCli = #24#1;
     |SINO;
          |GRABA 020#3n;
     |FINSI;
|FPRC;

|DEFBUCLE Anotaciones;
     #75#2;
     17, 14
     #0#0, #0#2, #0#4, "     ", 1, #0#6;
     #0#1, #0#3, #0#5, "zzzzz", 1, #0#7;
     ;
     NULL, Anotaciones;
|FIN;

|PROCESO Informe;
     nSwInf = 1;
     |IMPRE 0;
     |SI FSalida = 0;
          |INFOR aInf;
          |SI FSalida = 0;
               enVisitas = 0; eaCli = ""; eaRep = "";
               |ABRE #24;
               |ABRE #25;
               |ABRE #76;
               |BUCLE Anotaciones;
               |CIERRA #76;
               |CIERRA #25;
               |CIERRA #24;
               |SI enVisitas ! 0; |Y aResu = "S"; |PRINT; |FINSI;
               |PIEINF;
               |FININF;
          |SINO;
               aAlfa = "0000Error informe '" + aInf + "'...";
               |MENAV aAlfa;
          |FINSI;
          |FINIMP;
     |FINSI;
     nSwInf = 0;
|FPRC;

|PROCESO BtnDesglosado;
     aResu = "N";
     aInf = "psioazd6";
     |HAZ Informe;
|FPRC;

|PROCESO BtnResumido;
     aResu = "S";
     aInf = "psioazr6";
     |HAZ Informe;
|FPRC;

|PROCESO BtnRefrescar;
     |PTEC 806;
|FPRC;

|PROCESO Ptec; |TIPO 0;
     |SI FSalida = 0;
          |PTEC 806;
     |FINSI;
|FPRC;

|PROCESO QuitaAlta; |TIPO 7;
     aAlfa  = "|X000"; aAlfa = #0#aAlfa#; nIdAlta = aAlfa;
     |ESTADO_CONTROL nIdAlta, "HIDE";
|FPRC;

|PROGRAMA;
     |HAZ CreaTempo; |NOME_DAT #3 Tempo; aTmp3 = Tempo; |DELINDEX #3;
     |ABRE #3;

     |PINPA #0#0; nPanta = FSalida;
     |PINDA #0#0;
     nRango = 4 + 8 + 16 + 32;
     |LINEAL_SIMPLE #1#3, nRango, 1402, 2998, NULL, NULL, Evento;
     nGrid = FSalida;
     |CONTROL_SIMPLE nPanta, "BOTON,{{206}}Refrescar", 846, 858, BtnRefrescar;
     |CONTROL_SIMPLE nPanta, "BOTON,{{205}}Desglosado", 860, 878, BtnDesglosado;
     |CONTROL_SIMPLE nPanta, "BOTON,{{205}}Resumido", 880, 898, BtnResumido;
     |PARA; |SI; |HACIENDO;
          |ENDATOS #1#0;
          |SI FSalida ! 0; |SAL; |FINSI;

          eaMensaje = "Procesando...";
          |HAZ IniMensaje;
          |CIERRA #3; |DELINDEX #3; |ABRE #3;
          |ABRE #24;
          |ABRE #25;
          |ABRE #76;
          |BUCLE Anotaciones;
          |CIERRA #76;
          |CIERRA #25;
          |CIERRA #24;
          |LEE 000#3p;
          |REFRESCACONTROL nGrid;
          |HAZ FinMensaje;
     |SIGUE;
     |CIERRA #3;
     Tempo = aTmp3; |HAZ BoraTempo; |DELINDEX #3;
|FPRO;
