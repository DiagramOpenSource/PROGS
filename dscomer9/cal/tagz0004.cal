|FICHEROS;
     tagz0004 #0;
     agifa019 #19;
     tagm0026 #26;
     agifa024 #24;
     tagm0027 #27;
     agifa067 #67; ||Ped.Lin
     agifa060 #60;
     agifa081 #81; || Ped. Cab
     agifa120 #120; ||Subfa
     tagmpara;
|FIN;

|VARIABLES;
     &eaAgrCR1 = 0;
     &eaAgrCR2 = 0;
     &eaAgrCR3 = 0;
     &eaColor = "";
     &efFecha = @;
     nNume = 0;
     fFecha = @;
     fSystem = @;
     nCambio = 0;
     nExiste = 0;
     i = 0;
     nSwHay = 0;
     nTecla = 0;
     aAlfa = "";
     aDCurso = "";
     aHCurso = "";
|FIN;

|PROCESO Tecla; |TIPO 0;
     nTecla = FSalida;
     |SI FSalida = 0;
          |ABRE #27;
          |DDEFECTO #27;
          #27#0 = #0#0;
          |LEE 000#27=;
          |PARA i = 0; |SI i [ 13; |HACIENDO i = i + 1;
               #0i = #27i;
               |PINTA #0i;
          |SIGUE;
          |CIERRA #27;
     |FINSI;
     |SI nTecla = 13; |O nTecla = 14;
          |ABRE #27;
          #27#0 = #0#0;
          |LEE 000#27];
          |CONSULTA_CLAVE #1#27;
          |SI FSalida = 0;
               |SI nTecla = 13;
                    |PARA i = 0; |SI i [ 13; |HACIENDO i = i + 1;
                         #0i = #27i;
                         |PINTA #0i;
                    |SIGUE;
               |FINSI;
               |SI nTecla = 14;
                    |CONFI "0000NBorrar Informe:";
                    |SI FSalida = 0;
                         |BORRA 020#27c;
                    |FINSI;
                    |ERROR;
               |FINSI;
          |SINO;
               |ERROR;
          |FINSI;
          |CIERRA #27;
     |FINSI;
|FPRC;

|PROCESO Tipo66; |TIPO 66;
     FSalida = 13;
     |HAZ Tecla;
|FPRC;
----------------------------------------------------------------------
|PROCESO agifa067;
     |DDEFECTO #19;
     #19#0 = #67#2;
     |LEE 000#19=;
     |SI #19#2 < #0#7; |O #19#2 > #0#8; |ACABA; |FINSI;  ||Familia
     |SI #19#3 < #0#9; |O #19#3 > #0#10; |ACABA; |FINSI; ||Subfamilia

     |DDEFECTO #60;
     #60#0 = #19#2;
     |LEE 000#60=;
     |DDEFECTO #120;
     #120#0 = #19#3;
     |LEE 000#120=;

     efFecha = #81#1 + #26#2; ||Fecha Calculada
     nNume = #26#2 - #tagmpara#3;
     fFecha = #81#1 + nNume;
     fSystem = ~;
     eaColor = "R";

     |SI fSystem [ fFecha;
          eaColor = "N";
     |FINSI;
     |SI fSystem > fFecha; |Y fSystem [ efFecha;
          eaColor = "A";
     |FINSI;

     |PRINT;
     nSwHay = 1;
|FPRC;

|PROCESO agifa081;
     |DDEFECTO #24;
     #24#0 = #81#43;
     |LEE 000#24=;

     |DDEFECTO #26;
     #26#0 = #81#25;
     #26#1 = #81#0;
     |LEE 000#26=;
|FPRC;

|DEFBUCLE agifa081;
     #81#2;
     43,25;
     #0#4, aDCurso, "V    ";
     #0#5, aHCurso, "Vzzzz";
     ;
     NULL, agifa081, agifa067;
|FIN;
----------------------------------------------------------------------
|PROCESO Tipo3; |TIPO 3;
     |ABRE #tagmpara; |LEE 000#tagmpara.p; |CIERRA #tagmpara;

     aDCurso = #0#2 + "              ";
     aHCurso = #0#3 + "zzzzzzzzzzzzzz";
     eaAgrCR1 = #0#11;
     eaAgrCR2 = #0#12;
     eaAgrCR3 = #0#13;

     Impresora = "Crystal Reports";
     |IMPRE #0;
     |SI FSalida = 0;
          |INFOR "tagz0004";
          |SI FSalida = 0;
               |ABRE #19;
               |ABRE #24;
               |ABRE #26;
               |ABRE #60;
               |ABRE #120;
               |BUCLE agifa081;
               |CIERRA #120;
               |CIERRA #60;
               |CIERRA #26;
               |CIERRA #24;
               |CIERRA #19;
               |SI nSwHay = 1;  |PIEINF;  |FINSI;
               |FININF;
          |SINO;
               |MENAV "0000Falta Informe 'tagz0004'";
          |FINSI;
          |FINIMP;
     |FINSI;

     |ABRE #27;
     #27#0 = #0#0;
     |LEE 101#27=;
     nExiste = FSalida;
     |PARA i = 0; |SI i [ 13; |HACIENDO i = i + 1;
          |SI #27i ! #0i;
               nCambio = 1;
          |FINSI;
          #27i = #0i;
     |SIGUE;
     |SI nCambio = 1;
          |CONFI "0000SGrabar Seleccion del Informe:";
          |SI FSalida = 0;
               |SI nExiste = 0; |GRABA 020#27a; |FINSI;
               |SI nExiste ! 0; |GRABA 020#27n; |FINSI;
          |FINSI;
     |FINSI;
     |CIERRA #27;
|FPRC;
