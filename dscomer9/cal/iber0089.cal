|FICHEROS;
     iber0089 #0;
     agifa019 #19;
     iber0020 #20;
     iber0067 #67;
|FIN;

|VARIABLES;
     &enTiva = 0;
     &efFiva = @;
     &eaDiva = "";
     &enIva = 0;
     &enRec = 0;
     aSiNo     = "";
     i         = 0;
     aAlfa     = "";
     aReg      = "";
     nLonReg   = 0;
     aFinReg   = "";
     aNum      = "";
     nNum      = 0;
     nNum1     = 0;
     nNum2     = 0;
     aLon      = "";
     nLon      = 0;
     nHandle   = 0;
     aLetra    = "";
     nPrecio   = 0;
     nAlmacen  = 0;
     nPos      = 0;
     aIzq      = "";
     aDer      = "";
     aVal      = "";
     nSwNume   = 0;
     aRelleno  = "";
     aCentro   = "";
     nIVA      = 0;
     nREC      = 0;
     aIva      = "";
     aMul      = "";
     fFecha    = @;
     &EURODB = 0;
|FIN;

|PROCESO LeeIVA;
     enTiva = #agifa019#30;
     efFiva = ~;
     |HAZ SacaIVA;
     nIVA = enIva;
     nREC = enRec;
|FPRC;

|PROCESO Precio;
     |DDEFECTO #67;
     #67#0 = aCentro;  || Centro
     #67#1 = #0#36;    || Punto V.
     #67#2 = #0#37;    || Tarifa
     #67#3 = fFecha;   || Fecha
     #67#4 = #19#0;    || Arti
     |LEE 000#67=;
     nPrecio = #67#9;
     nPrecio = nPrecio ! EURODB;   || en la bascula se trunca
|FPRC;

|PROCESO MontaPrecio;
     |SI #0#28 ! 0;
          aMul = "0" * #0#28;
          aMul = "1" + aMul;
          nNum1 = aMul;
          nPrecio = (nPrecio * nNum1) ! 0;
          |SI #0#29 = " ";
               aVal = nPrecio;
          |SINO;
               aAlfa = nPrecio; |QBF aAlfa;
               aLon = aAlfa % A0;
               nLon = aLon;
               nLon = nLon * 100 + 1;
               nNum1 = 0;
               aVal = "";
               |PARA i = nLon; |SI i ] 101; |HACIENDO i = i  - 100;
                    aLetra = aAlfa % i;
                    nNum1 = nNum1 + 1;
                    aVal = aLetra + aVal;
                    |SI nNum1 = #0#28;
                         aVal = #0#29 + aVal;
                    |FINSI;
               |SIGUE;
          |FINSI;
     |SINO;
          nNum1 = (nPrecio ! 0);
          aVal = nNum1;
     |FINSI;
|FPRC;

|PROCESO Inserta;
     |SI nPos = 1;
          aIzq = "";
     |SINO;
          nNum1 = 100 + (nPos - 1);
          aIzq = aReg % nNum1;
     |FINSI;

     nNum1 = nPos + nLon;
     |SI nNum1 = nLonReg;
          aDer = "";
     |SINO;
          nNum1 = (nLon + nPos) + 1;
          nNum2 = nLonReg - nNum1;
          nNum1 = nNum1 * 100 + nNum2;
          aDer = aReg % nNum1;
     |FINSI;

     |SI nSwNume = 1;
          aRelleno = "0" * nLon;
          aVal = aRelleno + aVal;
          nNum1 = -1 * (100 + nLon);
     |SINO;
          aRelleno = " " * nLon;
          aVal = aVal + aRelleno;
          nNum1 = 100 + nLon;
     |FINSI;
     aVal = aVal % nNum1;

     aReg = aIzq + aVal + aDer;
|FPRC;

|PROCESO Registro;
     aReg = " " * nLonReg;

    ||PNUM
     |SI #0#1 = "S";
          aVal = #19#128; |QBF aVal;
          nPos = #0#2;
          nLon = #0#3;
          nSwNume = 0;
          |HAZ Inserta;
     |FINSI;

   ||ABNU
     |SI #0#4 = "S";
          aVal = #20#4; || Almacen
          nPos = #0#5;
          nLon = #0#6;
          nSwNume = 1;
          |HAZ Inserta;
     |FINSI;

   ||PLTE
     |SI #0#7 = "S";
          aVal = #19#110; |QBF aVal;
          |SI aVal = ""; aVal = #19#1; |FINSI;
          nPos = #0#8;
          nLon = #0#9;
          nSwNume = 0;
          |HAZ Inserta;
     |FINSI;

   ||GPR1
     |SI #0#10 = "S";
          |HAZ Precio;
          |HAZ MontaPrecio;

          nPos = #0#11;
          nLon = #0#12;
          nSwNume = 1;
          |HAZ Inserta;
     |FINSI;

   ||ECO1
     |SI #0#13 = "S";
          |HAZ Precio;
          |HAZ MontaPrecio;

          nPos = #0#14;
          nLon = #0#15;
          nSwNume = 1;
          |HAZ Inserta;
     |FINSI;

   ||KLAR
     |SI #0#16 = "S";
          |SI #19#117 ! 0;
               aVal = "1";
          |SINO;
               aVal = "0";
          |FINSI;
          nPos = #0#17;
          nLon = #0#18;
          nSwNume = 1;
          |HAZ Inserta;
     |FINSI;

   ||RABZ
     |SI #0#19 = "S";
          aVal = #0#32;
          nPos = #0#20;
          nLon = #0#21;
          nSwNume = 1;
          |HAZ Inserta;
     |FINSI;

   ||PREU
     |SI #0#22 = "S";
          aVal = #0#33;
          nPos = #0#23;
          nLon = #0#24;
          nSwNume = 1;
          |HAZ Inserta;
     |FINSI;

   ||WALO
     |SI #0#25 = "S";
          aVal = #0#34;
          nPos = #0#26;
          nLon = #0#27;
          nSwNume = 1;
          |HAZ Inserta;
     |FINSI;

     aReg = aReg + aFinReg;
|FPRC;

|PROCESO agifa019;
     aAlfa = #19#128; |QBF aAlfa;
     aLon = aAlfa % A0;
     |SI aLon ! 4; |ACABA; |FINSI;

     #20#0 = aCentro;
     #20#1 = #0#36;
     #20#2 = #19#0;
     |LEE 000#20=;
     |SI FSalida = 0;
          |PINTA 2141, #19#0;
          |HAZ Registro;
          |XGRABA nHandle, aReg;
     |FINSI;
|FPRC;

|DEFBUCLE agifa019;
     #19#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, agifa019;
|FIN;

|PROCESO PasaAscii;
     aNum = ("000" + aNum) % -103;
     |SI aNum = "000"; |O aNum = "027";
          aNum = &27 + aNum;
     |SINO;
          aNum = &aNum;
     |FINSI;

     aFinReg = aFinReg + aNum;
|FPRC;

|PROCESO Inicio; |TIPO 3;
     |HAZ DecimalesBase;

     |DFICE aCentro;
     aCentro = aCentro % -403;

     |ABRE #67;
     #67#0 = aCentro;
     #67#1 = #0#36;
     #67#2 = #0#37;
     #67#3 = ~;
     |LEE 000#67];
     |SI FSalida = 0;
          |LEE 000#67a;
     |SINO;
          |LEE 000#67u;
     |FINSI;
     |SI FSalida ! 0; |O #67#0 ! aCentro; |O #67#1 ! #0#36; |O #67#2 ! #0#37;
          |MENAV "0000No existe Tarifa....";
          |ACABA;
     |FINSI;
     |CIERRA #67;
     fFecha = #67#3;
     aAlfa = "Aplicando tarifa de fecha: " + fFecha;
     |ATRI S; |PINTA 2039, aAlfa; |ATRI 0;

     |PARA i = 3; |SI i [ 27; |HACIENDO i = i + 3;
          nNum = i - 2;
          |SI #0nNum = "S";
               nLonReg = nLonReg + #0i;
          |FINSI;
     |SIGUE;

     aAlfa = #0#30; |QBT aAlfa;
     aLon = aAlfa % A0;
     nLon = aLon;
     nLon = nLon * 100 + 1;
     |PARA i = 101; |SI i [ nLon; |HACIENDO i = i + 100;
          aLetra = aAlfa % i;
          |SI aLetra = ",";
               |HAZ PasaAscii;
               aNum = "";
          |SINO;
               aNum = aNum + aLetra;
          |FINSI;
     |SIGUE;
     |HAZ PasaAscii;

     aAlfa = #0#35; |QBF aAlfa;
     |XABRE "W", aAlfa, nHandle;
     |SI FSalida ] 0;
          |ABRE #20;
          |ABRE #67;
          |BUCLE agifa019;
          |CIERRA #67;
          |CIERRA #20;
          |XCIERRA nHandle;
     |SINO;
          aAlfa = "0000Error al crear: " + aAlfa;
          |MENAV aAlfa;
     |FINSI;
|FPRC;

|PROCESO Tipo0; |TIPO 0;
     |SI FSalida = 2;
          aSiNo = "S"; |HAZ Modi;
     |FINSI;
|FPRC;

|PROCESO Modi;
     |PARA i = 1; |SI i [ 35; |HACIENDO i = i + 1;
          |C_M #0i, 1, aSiNo;
     |SIGUE;
|FPRC;

|PROGRAMA;
     |CLS;

     |ABRE #0;
     |LEE 101#0p;
     |SI FSalida ! 0; |GRABA 020#0b; |FINSI;
     aAlfa = #0#35; |QBF aAlfa;
     |SI aAlfa = "";
          |DBASE aAlfa;
          #0#35 = aAlfa + "bascula";
     |FINSI;

     aSiNo = "N"; |HAZ Modi;

     |PINPA #0#0;
     |PINDA #0#0;
     |ENDATOS #1#0;
     |SI FSalida = 0;
          |GRABA 020#0a;
     |FINSI;
     |CIERRA #0;
|FPRO;
