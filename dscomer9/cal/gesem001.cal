|FICHEROS;
     gesem001 #0;
     agifa019 #19;
     agifa060 #60;
     agifa724 #724;
     gestr000@;
     gestr001@;
     gestr006@;
     gestr135@;
|FIN;

|VARIABLES;
     &eaGesMatri = "";
     &enSwAtras = 0;
     &eaArticulo = "";
     &eaSerie = "";
     &enCodigo = 0;
     &enLinea = 0;
     aPath = "";
     aAlfa = "";
     aEmp = "";
     nOk = 0;
     nSal = 0;
     nError = 0;

     nGrupo = 0;
     nCamion = 0;
     aConce = "";
     aCta = "";
     aRelle = "";
     nPosOrg = 0;
     nLon = 0;
     aParte = "";
     nPos = 0;
|FIN;

Variable entrada: aCta, nCamion, nGrupo, aConce
          salida: aCta
|PROCESO MontaCta;
|| sustituye las X por el grupo contable y las Y por el n§ camion
     |ABRE #gestr001@; |LEE 000#gestr001@.p; |CIERRA #gestr001@;
     aRelle = "0" * #gestr001@#2;
     aAlfa = aCta - "X";
     |SI FSalida > 0;
          nPosOrg = (FSalida - 1) / 100;
          |PARA nLon = 0; |SI FSalida > 0; |HACIENDO nLon = nLon + 1;
               aAlfa = aAlfa - "X";
          |SIGUE;
          nPos = -1 * (100 + nLon);
          aParte = (aRelle + nGrupo) % nPos;
          nPos = (nPosOrg + nLon) * 100 + #gestr001@#2;
          nPosOrg = nPosOrg - 1 + 100;
          aCta = (aCta % nPosOrg) + aParte + (aCta % nPos);
     |FINSI;
     aAlfa = aCta - "Y";
     |SI FSalida > 0;
          nPosOrg = (FSalida - 1) / 100;
          |PARA nLon = 0; |SI FSalida > 0; |HACIENDO nLon = nLon + 1;
               aAlfa = aAlfa - "Y";
          |SIGUE;
          nPos = -1 * (100 + nLon);
          aParte = (aRelle + nCamion) % nPos;
          nPos = (nPosOrg + nLon) * 100 + #gestr001@#2;
          nPosOrg = nPosOrg - 1 + 100;
          aCta = (aCta % nPosOrg) + aParte + (aCta % nPos);
     |FINSI;
     nPos = #gestr001@#2 + 100;
     aCta = (aCta + aRelle) % nPos;

     |ABRE #gestr135@;
     #gestr135@#0 = aConce;
     #gestr135@#1 = nCamion;
     |LEE 000#gestr135@.=;
     |SI FSalida = 0;
          aCta = #gestr135@#2;
     |FINSI;
     |CIERRA #gestr135@;
|FPRC;

|PROCESO T0; |TIPO 0;
     nSal = FSalida;
     nError = 0;

     |SI #0#3 = 0;
          |C_M #0#4, 1, "S";
     |SINO;
          |C_M #0#4, 1, "N";
     |FINSI;
     |SI nOk ! 0;
          |SI #0#3 ! 0;
               |SELECT #1#gestr006@;
               #gestr006@#0 = #0#3;
               |LEE 000#gestr006@.=;
               |SI FSalida = 0;
                    #0#4 = #gestr006@#16; |PINTA #0#4;
                    |SI #gestr006@#8 = 0;
                         |MENAV "0000El bien no tiene propietario...";
                         |ERROR; nError = 1;
                    |FINSI;
               |SINO;
                    |MENAV "0000No existe código...";
                    |ERROR; nError = 1;
               |FINSI;
          |SINO;
               |SELECT #2#gestr006@;
               #gestr006@#16 = #0#4;
               |LEE 000#gestr006@.=;
               |SI FSalida = 0;
                    #0#3 = #gestr006@#0; |PINTA #0#3;
                    |SI #gestr006@#8 = 0;
                         |MENAV "0000El bien no tiene propietario...";
                         |ERROR; nError = 1;
                    |FINSI;
               |SINO;
                    |MENAV "0000No existe matrícula...";
                    |ERROR; nError = 1;
               |FINSI;
          |FINSI;
     |FINSI;

     |SI nError = 0; |Y nSal = 2; |Y Campo = 3;
          enSwAtras = 1;
          |PTEC 806;
     |FINSI;
|FPRC;

|PROCESO T66; |TIPO 66;
     |SI nOk = 0; |ACABA; |FINSI;
     |SI Campo = 3;
          |SELECT #1#gestr006@;
          #gestr006@#0 = #0#3;
          |LEE 000#gestr006@.];
          |CONSULTA_CLAVE #1#gestr006@;
     |SINO;
          |SELECT #2#gestr006@;
          #gestr006@#16 = #0#4;
          |LEE 000#gestr006@.];
          |CONSULTA_CLAVE #2#gestr006@;
     |FINSI;
     |SI FSalida = 0;
          #0#3 = #gestr006@#0; |PINTA #0#3;
          #0#4 = #gestr006@#16; |PINTA #0#4;
     |SINO;
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO T12; |TIPO 12;
|FPRC;

|PROGRAMA;
     enSwAtras = 0;
     |ABRE #0;
     #0#0 = eaSerie;
     #0#1 = enCodigo;
     #0#2 = enLinea;
     #0#7 = 0;
     |SI PARAMETRO = "BAJA";
          |LEE 101#0];
          |PARA; |SI FSalida = 0; |Y #0#0 = eaSerie; |Y #0#1 = enCodigo;
                    |Y #0#2 = enLinea; |HACIENDO;
               |BORRA 020#0a;
               |LIBERA #0;
               |LEE 101#0s;
          |SIGUE;
     |SINO;
          |SI PARAMETRO = "DEFE";
               |LEE 101#0=;
               |SI FSalida ! 0; |GRABA 020#0b; |FINSI;
               aAlfa = eaGesMatri % 103; #0#3 = aAlfa;
               aAlfa = eaGesMatri % 4; #0#4 = aAlfa;
               |GRABA 020#0a;
          |SINO;
               |DBASS aPath;
               aAlfa = aPath + "gesenvia/def/gestr000";
               |CARGA_DEF aAlfa, gestr000@;
               |SI FSalida = 0;
                    aAlfa = aPath + "gesenvia/fich/";
                    |PATH_DAT #gestr000@#aAlfa#;
                    |DFICE aAlfa; aEmp = aAlfa % -205;
                    |ABRE #gestr000@;
                    #gestr000@#0 = aEmp;
                    |LEE 000#gestr000@.=;
                    aAlfa = FSalida;
                    |CIERRA #gestr000@;
                    |DESCARGA_DEF #gestr000@;
                    |SI aAlfa = 0;
                         aAlfa = aPath + "gesenvia/def/gestr006"; |CARGA_DEF aAlfa, gestr006@;
                         aAlfa = aPath + "gesenvia/def/gestr001"; |CARGA_DEF aAlfa, gestr001@;
                         aAlfa = aPath + "gesenvia/def/gestr135"; |CARGA_DEF aAlfa, gestr135@;
                         aAlfa = aPath + "gesenvia/fich/" + aEmp + "/";
                         |PATH_DAT #gestr006@#aAlfa#;
                         |PATH_DAT #gestr001@#aAlfa#;
                         |PATH_DAT #gestr135@#aAlfa#;
                         |ABRE #gestr006@;
                         |ABRE #gestr001@;
                         |ABRE #gestr135@;
                         nOk = 1;
                    |FINSI;
               |FINSI;
               aAlfa = eaGesMatri % 103; #0#3 = aAlfa;
               aAlfa = eaGesMatri % 4; #0#4 = aAlfa;
               |LEE 101#0=;
               |SI FSalida ! 0; |GRABA 020#0b; |FINSI;
               |PINDA #0#0;
               |ENDATOS #1#0;
               |SI FSalida = 0;
                    eaGesMatri = (("000" + #0#3) % -103) + #0#4;
                    |ABRE #19;
                    #19#0 = eaArticulo;
                    |LEE 000#19=;
                    |SI FSalida ! 0; |DDEFECTO #19; |FINSI;
                    |CIERRA #19;

                    |ABRE #724;
                    #724#0 = eaArticulo;
                    |LEE 000#724=;
                    |SI FSalida ! 0; |DDEFECTO #724; |FINSI;
                    |CIERRA #724;

                    aCta = #724#3; |QBF aCta;
                    |SI aCta = "";
                         |ABRE #60;
                         #60#0 = #19#3;
                         |LEE 000#60=;
                         |SI FSalida ! 0; |DDEFECTO #60; |FINSI;
                         |CIERRA #60;
                         aCta = #60#3;
                    |FINSI;
                    nGrupo = #19#127;
                    nCamion = #0#3;
                    aConce = #19#0;
                    |SI nOk = 1; |HAZ MontaCta; |FINSI;
                    #0#5 = aCta;
                    |GRABA 020#0a;
               |FINSI;

               |SI nOk = 1;
                    |CIERRA #gestr135@; |DESCARGA_DEF #gestr135@;
                    |CIERRA #gestr001@; |DESCARGA_DEF #gestr001@;
                    |CIERRA #gestr006@; |DESCARGA_DEF #gestr006@;
               |FINSI;
          |FINSI;
     |FINSI;
     |CIERRA #0;
|FPRO;
