|FICHEROS;
     malpe200 #0;
     malpe201 #1;
     malpe003 #3;
     agifa027 #27;
     agifa142 #142;
     agifa017 #17;
     agifa019 #19;
     dsm00024 #24;
     agifa065 #65;
|FIN;

|VARIABLES;
     nForma = 0;
     nModo = 0;
     nCampo = 0;
     aAlfa = "";
     tempo = "";
     {-1}menu = "";
     menu1 = "2400";
     menu2 = "1";
     menu3 = "Contador:  ";
     menu4 = "AM";
     menu5 = " Automatico ";
     menu6 = " Manual ";
|FIN;

|PROCESO Contador; |TIPO 7;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 1;
          |MENU menu;
          |SI FSalida = 1;
               |ABRE #27;
               #27#2 = "";
               #27#0 = "vtransfe";
               |LEE 101#27=;
               |SI FSalida ! 0;
                    #27#1 = 1;
                    |GRABA 020#27b;
               |FINSI;
               tempo = ("000000" + #27#1) % -106;
               #27#1 = #27#1 + 1;
               |GRABA 021#27a;
               |CIERRA #27;
               #0#0 = tempo;
               |PTEC 802;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Imprime;
     |PRINT;
|FPRC;

|PROCESO Tipo3; |TIPO 3;
     |CONFI "0000SImprimir Documento";
     |SI FSalida = 0;
          |HAZ Tipo4;
     |FINSI;
|FPRC;

|PROCESO Tipo4; |TIPO 4;
     |IMPRE 0;
     |SI FSalida = 0;
          |INFOR "malpe200";
          |SI FSalida ! 0;
               |MENAV "0000No existe informe 'malpe200'";
          |SINO;
               |BUCLELIN 2Imprime #0;
               |PIEINF;
               |FININF;
          |FINSI;
          |FINIMP;
     |FINSI;
|FPRC;

|PROCESO NoModi; |TIPO 7;
     nModo = (FEntrada / 100) ! 0;
     |C_M #0Campo, 1, "S";
     |SI nModo ! 1;
          |C_M #0Campo, 1, "N";
     |FINSI;
|FPRC;

|PROCESO Decim; |TIPO 0;
     |ABRE #142; |LEE 000#142p; |CIERRA #142;
     #1#4 = #1#4 ! #142#8;
     |PINTA #1#4;
|FPRC;

|PROCESO Defecto; |TIPO 0;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 1;
          #1#7 = #0#2; |PINTA #1#7;
     |FINSI;
|FPRC;

|PROCESO Tipo200_2; |TIPO 2;
|FPRC;

|PROCESO Tipo201_2; |TIPO 2;
     nForma = 0 +. 1;
     |HAZ StockEnt;
     |HAZ StockSal;
     |ABRE #65;
     |HAZ HistoEnt;
     |HAZ HistoSal;
     |CIERRA #65;
|FPRC;

|PROCESO StockEnt;
     |ABRE #19;
     |ABRE #17;
     |ABRE #24;
     #19#0 = #1#2;
     |LEE 000#19=;
     |SI FSalida = 0;
          #17#0 = #1#2;
          #17#1 = #1#6;
          |LEE 101#17=;
          |SI FSalida = 0;
               #24#0 = #1#2;
               #24#1 = #1#6;
               #24#2 = #1#9;
               |LEE 101#24=;
               |SI FSalida = 0;
                    #17#5 = #17#5 + (#1#4 * nForma);
                    #17#39 = #17#39 + (#1#4 * nForma);
                    #24#9 = #24#9 + (#1#4 * nForma);
                    #24#31 = #24#31 + (#1#4 * nForma);
                    #24#22 = #24#22 + (#1#4 * nForma);

                    aAlfa = #0#7; aAlfa = aAlfa % 402;
                    nCampo = aAlfa;
                    nCampo = (nCampo - 1) * 2 + 12;
                    #17nCampo = #17nCampo + (#1#4 * nForma);

                    |GRABA 020#17a;
                    |GRABA 020#24a;
               |FINSI;
          |FINSI;
     |FINSI;
     |CIERRA #24;
     |CIERRA #17;
     |CIERRA #19;
|FPRC;

|PROCESO StockSal;
     |ABRE #19;
     |ABRE #17;
     |ABRE #24;
     #19#0 = #1#2;
     |LEE 101#19=;
     |SI FSalida = 0;
          #17#0 = #1#2;
          #17#1 = #1#7;
          |LEE 101#17=;
          |SI FSalida = 0;
               #24#0 = #1#2;
               #24#1 = #1#7;
               #24#2 = #1#9;
               |LEE 101#24=;
               |SI FSalida = 0;
                    #17#5 = #17#5 - (#1#4 * nForma);
                    #17#39 = #17#39 - (#1#4 * nForma);
                    #24#11 = #24#11 + (#1#4 * nForma);
                    #24#31 = #24#31 - (#1#4 * nForma);
                    #24#22 = #24#22 - (#1#4 * nForma);

                    aAlfa = #0#7; aAlfa = aAlfa % 402;
                    nCampo = aAlfa;
                    nCampo = (nCampo - 1) * 2 + 11;
                    #17nCampo = #17nCampo + (#1#4 * nForma);
                    |HAZ ValoraStock;
                    |GRABA 020#19a;
                    |GRABA 020#17a;
                    |GRABA 020#24a;
               |FINSI;
          |FINSI;
     |FINSI;
     |CIERRA #24;
     |CIERRA #17;
     |CIERRA #19;
|FPRC;

|PROCESO HistoEnt;
     |DDEFECTO #65;
     #65#0 = #1#2;
     #65#1 = #0#7;
     #65#2 = "ME";
     #65#3 = #1#1;
     #65#4 = #1#0;
     #65#5 = #1#6;
     #65#6 = #1#4;
     #65#7 = #19#6;
     #65#8 = #19#9;
     #65#9 = #1#8;
     #65#18 = #1#9;
     |SI nForma = 1;
          |GRABA 020#65n;
     |SINO;
          |BORRA 020#65c;
     |FINSI;
|FPRC;

|PROCESO HistoSal;
     |DDEFECTO #65;
     #65#0 = #1#2;
     #65#1 = #0#7;
     #65#2 = "MS";
     #65#3 = #1#1;
     #65#4 = #1#0;
     #65#5 = #1#7;
     #65#6 = -#1#4;
     #65#7 = #19#6;
     #65#8 = #19#9;
     #65#9 = #1#8;
     #65#18 = #1#9;
     |SI nForma = 1;
          |GRABA 020#65n;
     |SINO;
          |BORRA 020#65c;
     |FINSI;
|FPRC;

|PROCESO Tarifa0; |TIPO 0;
     |ABRE #3;
     #3#0 = #0#13;
     #3#1 = "00.00.0000";
     |LEE 000#3];
     |SI FSalida ! 0; |O #3#0 ! #0#13;
          |MENAV "0000No existe tarifa...";
          |ERROR;
     |FINSI;
     |CIERRA #3;
|FPRC;

|PROCESO Tarifa66; |TIPO 66;
     |ABRE #3;
     #3#0 = #0#13;
     #3#1 = "00.00.0000";
     |LEE 000#3];
     |CONSULTA_CLAVE #1#3;
     |SI FSalida = 0;
          #0#13 = #3#0; |PINTA #0#13;
     |SINO;
          |ERROR;
     |FINSI;
     |CIERRA #3;
|FPRC;
