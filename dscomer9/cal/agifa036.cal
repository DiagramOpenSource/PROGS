|FICHEROS;
     agifa036 #0;
     agifa070 #3;
     agifa019 #2;
     agifa024 #4;
     agifa038 #13;
     agifa007 #40;
     agifa142 #41;  || Parametros Generales
     agifa027 #42;  || Contador.              || VIC Puesto el 15/07/98
     agifa321 #35;  || Parametros de Divisas
     agifa328 #36;  || Clientes/Divisas
     agifa329 #37;  || Lineas de Clientes/Divisas
     agifa321 #38;  || Parametros de DIVISAS
     agifa324 #38;  || Divisas
     dsm00003 #49,MANTE;

     agifa048 #148;
     agifa049 #149;
     dsm00117 #117;
     dsm00134 #134; ||Promociones cli/art
     dsm00211 #211;
     dsm00279 #279;
     referen@;
     dsm00087@;
|FIN;

|INCLUYE i_varart;

|VARIABLES;
     &PRMNT_aHisto = "";
     i = 0;
     aTieneLin = "N";
     aSinError = "";
     nSal = 0;
     aSql = "";
     aDef = "";
     aUsu = "";
     nContaLog = 0;
     aSer = "";
     aNum = "";
     aTipo = "";
     nTipo = 0;
     nLin = 0;
     nResto = 0;
     nUniPromo = 0;
     &uni_dt = 0;   || Unidades
     &uni_dt2 = 0;   || Unidades2
     &fed_dt = @;   || Fecha....
     &art_dt = "";  || Articulo.
     &cli_dt = "";  || Cliente..
     &fam_dt = "";  || Familia.
     &iva_dt = 0;   || Tipo Iva.
     &dtos_2 = 0;   || Descuento 2.
     &dto_pt = 0;   || Descuento Ptas.
     &dtos_1 = 0;   || Descuento 1.
     &pvp_ve = 0;   || Precio Venta.
     &tarifa = 0;   || Tarifa Cliente.
     &quedto = 0;   || 1=cli./art, 2=cli./familia, 3=cli./subfam.
     &gru_dt = "";  || Grupo Cliente.
     &nPromo = 0;
     &enMensaje = 0;
     &aParam    = "";
     &enModo036 = 0;
     nModo = 0;
     aAlfa = "";
     &nDeci_BaseMx    = 0;
     &nDeci_Div       = 0;

     aArti            = "";
     aOpera           = "";
     &cli             = "";
     &ser             = "";
     &ser_ped         = "";
     &num_ped         = 0;

     &nDeci_Base      = 0;   || Decimales en Totales de la moneda base
     &nDeci_PreBase   = 0;   || Decimales en el Precio en moneda base
     &nDeci_BaseMxD   = 0;   || Decimales en Totales de la moneda base (Maxima precision)
     &nDeci_PreBasMxD = 0;   || Decimales en el Precio en moneda base (Maxima precision)
     &nDeci_DivD      = 0;   || Decimales en Totales de la divisa
     &nDeci_PreDivD   = 0;   || Decimales en el Precio en divisa
     &nDeci_TotVisD   = 0;   || Decimales en Totales visibles (el de la moneda de trabajo)
     &nDeci_TotNoVisD = 0;   || Decimales en Totales no visibles (el de la otra moneda)
     &nDeci_PreVisD   = 0;   || Decimales en el Precio visual. (lineas)
     &nDeci_ImpVisD   = 0;   || Decimales en el Importe visual. (lineas)
     &nDeci_PreVis    = 0;   || para agifa023
     &nDeci_ImpVis    = 0;
     nfecha    = 0;
     &ac_divisa = "N";   || Vale "S" cuando las divisas en ventas estan
                         || activadas y la serie es de divisas
     &pintado = 0;       || Sirve para controlar los pintados de las lineas
     &decim_divisa = 0;  || Decimales de la divisa
     &precio_divisa = 0; || Decimales del precio en divisa
     &decim_base = 0;    || Decimales de la moneda base
     &precio_base = 0;   || Decimales del precio en moneda base
     div_act = 0;        || Divisa actualizada. Me sirve en un PARA para que
                         || no salga hasta que este actualizada (div_act=1)
     &cli_divisa = "";   || Codigo de Cliente. Se pasa a CLIENTES/DIVISAS
     &divisa = "";       || Codigo de Divisa. Se pasa a ACTUAL. de DIVISAS
     &nModo036 = 0;
     &ser_alb = "";
     &num_alb = 0;

     imneto = 0;
     tf = 0;
     mes = 0;
     con = 0;
     margen = 0;
     indtope = 0;
     ind = 0;
     control = 0;
     mensaje01 = "0423Actualizando linea:";
     mensaje10 = "0000Este Deposito ya esta Entregado en parte o totalmente";
     mensajei  = "2400NDesea imprimir este deposito? ";
     vacio="";
     n_dec = 0;
     n_pre = 0;
     def_alm  = 0;
     def_exp  = "N";
     alfa = "";
     importe  = 0;
     cont     = "";
     contador = 0;
     recalpre = 0;
     caltot   = 0;

     || MENU PARA INTRODUCIR EL CODIGO DEL CLIENTE DE FORMA AUTOMATICA.
 {-1}menu           = "";                   || VIC Puesto 15/07/98
     menu1          = "2400";
     menu2          = "1";
     menu3          = "Contador:  ";
     menu4          = "AM";
     menu5          = " Automatico ";
     menu6          = " Manual ";

     so             = "";
     no             = "";
     nTecla         = FSalida;
     nImpDiv        = 0;

     &eaCodigo      = "";
     &eaTipoCodigo  = "CL";
|FIN;

컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴

|PROCESO Promo036_7; |TIPO 7;
     |SI #41#129 = "S";
          |C_M #0#60, 1, "S";
     |FINSI;
|FPRC;

|PROCESO Promo036_0; |TIPO 0;
     |SI #0#60 ! 0;
          |ABRE #117;
          #117#0 = #0#60;
          |LEE 000#117=;
          |SI FSalida ! 0;
               |MENAV "0000Promocion Inexistente...";
               |ERROR;
          |FINSI;
          |CIERRA #117;
     |FINSI;

|FPRC;


|CALCULO cogecon2;|TIPO 0;
control = 0;
|FCAL;

|CALCULO pedcon2;|TIPO 6;
     con = (FEntrada / 100) ! 0;
     |SI con ! 1;|Y #0Campo ! Contenido;
       |SI #0#39 > 0;
         #0Campo = Contenido;
         |PINTA #0Campo;
         |MENAV mensaje10;
         |ERROR; nError6 = 1;
       |SINO;
         control = 1;
       |FINSI;
     |FINSI;
|FCAL;

|CALCULO acacp2;|TIPO 2;
     |HAZ ControlUsuDV;
     |SI enErr ! 0; |ERROR; |ACABA; |FINSI;

     con = (FEntrada / 100) ! 0;
     enModo036 = con;

     |SI con = 3;|Y #0#39 > 0;
          |MENAV mensaje10;
          |ERROR; |ACABA;
     |FINSI;

     |SI #0#50 = 0; || chequea el cambio
          |ABRE #agifa324;
          #agifa324#0 = #0#49;
          |LEE 020#agifa324.=;
          |SI FSalida = 0;
               #0#50 = #agifa324#2;
          |FINSI;
          |CIERRA #agifa324;
     |FINSI;

     |HAZ actal2;

     |HAZ EsGuerola;
     |SI con = 3; |Y FSalida = 0;
          |HAZ GuerBorObra;
     |FINSI;
|FCAL;

|CALCULO acttl2;|TIPO 0;
     |SI caltot = 0;
          |PINTA 445,#3#1;
          #3#15 = #0#3;
          #3#16 = #0#34;
          #3#17 = #0#35;
          |GRABA 020#3a;
     |SINO;
          |SI recalpre = 1;
               |HAZ TotalLin70; ||Si viene de caltotal y recalcular precios lineas si
               |PINTA #3#25;
               |PINTA #3#9;
               |SI ac_divisa = "S"; |Y #agifa321#3= "S";
                    |PINTA #3#27;   || Si las divisas y la visualizacion estan activadas, pinto
               |FINSI;
               |GRABA 020#3a;
          |FINSI;
     |FINSI;

     ||------------Actualiza Cabecera
     |HAZ CalCabAgifa036;
|FCAL;

|DEFBUCLE agifa070;
     #3#1;
     ;
     #0#46, #0#0, 001;
     #0#46, #0#0, 999;
     ;
     NULL, acttl2;
|FIN;

|CALCULO actal2;|TIPO 0;
     |HAZ LimCabAgifa036;
     |BUCLE agifa070;
|FCAL;

|PROCESO TieneLin036;
     aTieneLin = "S";
|FPRC;

|CALCULO siimpre1;|TIPO 3;
     aTieneLin = "N";
     |BUCLELIN 2 TieneLin036 #0;
     |SI aTieneLin = "N";
          |BORRA 020#0c;
          |ERROR; |ACABA;
     |FINSI;

     |HAZ actal2;
     |CONFI mensajei;
     |SI FSalida = 0;
          |HAZ impreal1;
     |FINSI;
|FCAL;

|PROCESO T36_4; |TIPO 4;
     |SELECT #1#0;
     |LEE 000#0=;
     |SI FSalida ! 0;
          |MENAV "0000Por favor, consulte el registro correcto antes de imprimir.";
          |ACABA;
     |SINO;
          |PINDA #0#0;
          FEntrada = 402;
     |FINSI;
     |HAZ impreal1;
|FPRC;

|CALCULO impreal1;
     con = (FEntrada / 100) ! 0;
     |SI con = 1; |O con = 2;
          |GRABA 021#0a;
          |LIBERA #0;
     |FINSI;
     |CIERRAT #0;

     ser_ped = #0#46;
     num_ped = #0#0;

     |SUB_EJECUTA_NP "agifa035";

     |ABRET #0;
     #0#46 = ser_ped;
     #0#0  = num_ped;
     |LEE 001#0=;
|FCAL;

|CALCULO totdepo;|TIPO 14;
     ||#0#48 = (#0#15-#0#25) + (#0#11+#0#13+#0#24);
|FCAL;

|PROCESO mira_g;
     |ABRE #41;
     |DDEFECTO #41;
     #41#0 = "A";
     |LEE 001#41=;
     |CIERRA #41;
     n_dec = #41#8;
     n_pre = #41#9;
     |ABRE #agifa007;
     #agifa007#26 = #agifa036#46;
     |LEE 001#agifa007.=;
     |SI FSalida ! 0; |DDEFECTO #agifa007; |FINSI;
     |CIERRA #agifa007;
     def_alm = #agifa007#29;
     def_exp = #agifa007#30;
     |HAZ LeeMonBase;
     |HAZ LeeParamDiv;
|FPRC;
_____________________________________/ BUSCA UN CODIGO DE CLIENTE AUTOMATICO.
|PROCESO altac; |TIPO 6;
     nTecla = FSalida;
                                     || VIC Puesto por vicente 15/07/98
     alfa = #0#3;
     |QBF alfa;
     |SI alfa ! "";
          |ABRE #4;
          #4#0 = #0#3;
          |LEE 000#4=;
          |SI FSalida ! 0;
               |ABRE #41;
               |LEE 000#41p;
               |SI #41#1 = "S";
                    |HAZ Menus;
               |FINSI;
               |CIERRA #41;
          |FINSI;
          |CIERRA #4;
     |FINSI;
|FPRC;

|PROCESO Menus;
     |MENU menu;
     |SI FSalida = 1;    ||Automatico
          |ABRE #42;
          #42#2 = "";
          #42#0 = "clientes";
          |LEE 101#42=;
          |SI FSalida = 0;
               #42#1 = #42#1 + 1;
               contador = #42#1;
               |GRABA 021#42a;
          |SINO;
               contador = 1;
               #42#1 = 1;
               |GRABA 021#42n;
          |FINSI;
          cont = contador;
          cont = ("000000" + cont) % -106;
          #0Campo = cont;
          |PINTA #0Campo;
          |LIBERA #42;
          |CIERRA #42;
     |FINSI;
|FPRC;
___________/ VIC 15-07-98


|PROCESO LeeMonBase; ||Lee los parametros de la moneda base
     |ABRE #agifa321;
     |ABRE #agifa324;
     |LEE 010#agifa321.p;   ||Leo parametros de divisa
     #agifa036#52 = #agifa321#9;  ||Cojo la Moneda Base de parametros
     #agifa324#0 = #agifa321#9;
     |LEE 010#agifa324.=;         ||Leo la ficha de la moneda base en Divisas
     #agifa036#53 = #agifa324#2;  ||Asigno el cambio de la moneda base con el euro
     decim_base = #agifa324#7;    ||Asigno los decimales de la moneda base
     precio_base = #agifa324#9;   ||Asigno los decimales del precio en la mon. base
     |CIERRA #agifa324;
     |CIERRA #agifa321;
|FPRC;

|PROCESO Param_Divisas; |TIPO 0;
     pintado = 0;  ||Controla pintados de las lineas

     |ABRE #agifa324;
     #agifa324#0 = #agifa036#49;
     |LEE 001#agifa324.=;   || Leo la divisa
     |CIERRA #agifa324;

     decim_divisa = #agifa324#7;  || Decimales de la divisa
     precio_divisa = #agifa324#9;  || Decimales en el precio de la divisa
     nDeci_DivD = decim_divisa;   || Decimales de la divisa
     nDeci_PreDivD = precio_divisa;  || Decimales en el precio de la divisa
     |SI #0#51 = "B";   || Si trabajo con la moneda base ...
          nDeci_PreVis    = precio_divisa;
          nDeci_ImpVis    = decim_divisa;
          nDeci_PreVisD   = precio_divisa;
          nDeci_ImpVisD   = decim_divisa;
          nDeci_Base      = decim_base;
          nDeci_PreBase   = precio_base;
          nDeci_BaseMxD   = decim_base;
          nDeci_PreBasMxD = precio_base;
          nDeci_TotVisD   = decim_base;
          nDeci_TotNoVisD = decim_divisa;
     |SINO;     || Si trabajo con la divisa ...
          nDeci_PreVis    = precio_divisa;
          nDeci_ImpVis    = decim_divisa;
          nDeci_PreVisD   = precio_base;
          nDeci_ImpVisD   = decim_base;
          nDeci_Base      = decim_base; || Max. precision en los decimales de la moneda base
          nDeci_PreBase   = precio_base;  || Max. precision en los dec. del precio de la moneda base
         |SI #0#50 = 1;
              nDeci_BaseMxD   = decim_base;    || sin triangulacion
              nDeci_PreBasMxD = precio_base;
         |SINO;
              nDeci_BaseMxD   = #agifa321#10;  || con triangulacion
              nDeci_PreBasMxD = #agifa321#11;
         |FINSI;
          nDeci_TotVisD   = decim_divisa;
          nDeci_TotNoVisD = decim_base;
     |FINSI;
     nDeci_BaseMx = nDeci_BaseMxD;
     nDeci_Div = nDeci_DivD;
     nDeci_ImpVis = nDeci_ImpVisD;
     |HAZ Deci_IVA;
|FPRC;

|PROCESO LeeParamDiv; |TIPO 0;
     |ABRE #agifa321;
     |LEE 001#agifa321.p;    || Leo parametros de divisas
     |ABRE #agifa007;
     #agifa007#26 = #agifa036#46;
     |LEE 001#agifa007.=;        || Leo la serie

     |SI #agifa007#39 = "N"; |O #agifa321#1 = "N";
          #0#51 = "B";
     |FINSI;

     |SI #0#51 = "D";      || Si la mon. de trabajo es la divisa...
       || ...escondo los campos de la moneda base :
       |CAMPO_POSICION #3#6 , 1 , 1009;
       |CAMPO_POSICION #3#9 , 1 ,1026;
       |CAMPO_VISUAL #3#6 , 1 , "N";
       |CAMPO_VISUAL #3#9 , 1 , "N";
       |CAMPO_LINEAL #3#6 , 1 , "N";
       |CAMPO_LINEAL #3#9 , 1 , "N";
       |CAMPO_MODIFICA #3#6 , 1 , "N";
       || ...y pongo en pantalla los de divisa
       |CAMPO_POSICION #3#24 , 1 , 1544;
       |CAMPO_POSICION #3#25 , 1 ,1568;
       |CAMPO_VISUAL #3#24 , 1 , "S";
       |CAMPO_VISUAL #3#25 , 1 , "S";
       |CAMPO_MODIFICA #3#24 , 1 , "S";
       |CAMPO_LINEAL #3#24 , 1 , "S";
       |CAMPO_LINEAL #3#25 , 1 , "S";
       |SI #agifa007#39 = "S"; |Y #agifa321#1 = "S"; ||Si la serie es de ...
          ac_divisa = "S";  || ... divisas, y en parametros estan activadas ...
          || ... pongo como modificables los campos relacionados con divisas
          |CAMPO_MODIFICA #0#49 , 1 , "S";
          |CAMPO_MODIFICA #0#50 , 1 , "S";
          |CAMPO_MODIFICA #0#51 , 1 , "S";
          |SI #agifa321#3 = "S";  || Si la visualizacion de parametros esta activada
            || ... pongo los campos visual. de lineas como visualizables
            |CAMPO_VISUAL #3#26, 1 , "S";
            |CAMPO_VISUAL #3#27 , 1 , "S";
          |SINO;
            || ... los pongo como no visualizables
            |CAMPO_VISUAL #3#26 , 1 , "N";
            |CAMPO_VISUAL #3#27 , 1 , "N";
          |FINSI;
       |SINO; || Si la serie no es de divisas, o no estan activados parametros
          ac_divisa = "N";
          #0#51 = "B";  || La moneda de trabajo es por fuerza la moneda base
          |PINTA #0#51;
          || Pongo como no modificables los campos relacionados con divisas
          |CAMPO_MODIFICA #0#51 , 1 , "N";
          |CAMPO_MODIFICA #0#49 , 1 , "N";
          |CAMPO_MODIFICA #0#50 , 1 , "N";
          || Pongo como no visualizables los campos visual. de lineas
          |CAMPO_VISUAL #3#26, 1 , "N";
          |CAMPO_VISUAL #3#27, 1 , "N";
       |FINSI;
     |SINO;  || Si la moneda de trabajo es la moneda base
       || ...escondo los campos en divisa :
       |CAMPO_POSICION #3#24 , 1 , 1009;
       |CAMPO_POSICION #3#25 , 1 ,1026;
       |CAMPO_VISUAL #3#24 , 1 , "N";
       |CAMPO_VISUAL #3#25 , 1 , "N";
       |CAMPO_MODIFICA #3#24 , 1 , "N";
       |CAMPO_LINEAL #3#24 , 1 , "N";
       |CAMPO_LINEAL #3#25 , 1 , "N";

       || ...coloco los campos en moneda base:
       |CAMPO_POSICION #3#6 , 1 , 1544;
       |CAMPO_POSICION #3#9 , 1 ,1568;
       |CAMPO_VISUAL #3#6 , 1 , "S";
       |CAMPO_VISUAL #3#9 , 1 , "S";
       |CAMPO_MODIFICA #3#6 , 1 , "S";
       |CAMPO_LINEAL #3#6 , 1 , "S";
       |CAMPO_LINEAL #3#9 , 1 , "S";
       |SI #agifa007#39 = "S"; |Y #agifa321#1 = "S"; || Si la serie es de divisas...
          ac_divisa = "S"; || ... y divisas estan activadas en parametros
          || Pongo como modificables los campos relacionados con divisas
          |CAMPO_MODIFICA #0#49 , 1 , "S";
          |CAMPO_MODIFICA #0#50 , 1 , "S";
          |CAMPO_MODIFICA #0#51 , 1 , "S";
          |SI #agifa321#3 = "S";  || Si la visualizacion esta activada en parametros ...
            || Pongo como visualizables los campos visual. de las lineas
            |CAMPO_VISUAL #3#26, 1 , "S";
            |CAMPO_VISUAL #3#27 , 1 , "S";
          |SINO; || Los pongo como no visualizables
            |CAMPO_VISUAL #3#26 , 1 , "N";
            |CAMPO_VISUAL #3#27 , 1 , "N";
          |FINSI;
       |SINO;  || Si la serie no es de divisas o no estan activados los parametros
          ac_divisa = "N";
          #0#51 = "B";  || La moneda de trabajo es por fuerza la moneda base
          || Pongo como no modificables los campos relacionados con divisas
          |CAMPO_MODIFICA #0#51 , 1 , "N";
          |CAMPO_MODIFICA #0#49 , 1 , "N";
          |CAMPO_MODIFICA #0#50 , 1 , "N";
          || Pongo como no visualizables los campos visual. de lineas
          |CAMPO_VISUAL #3#26, 1 , "N";
          |CAMPO_VISUAL #3#27, 1 , "N";
       |FINSI;
     |FINSI;
     |CIERRA #agifa007;
|CIERRA #agifa321;
|FPRC;

|PROCESO CambioDivisa; |TIPO 0;
     nModo036 = (FEntrada / 100) ! 0;
     |SI #0Campo ! Contenido; |Y nModo036 = 1;   ||Si cambia el valor o es alta
       |SI Campo = 3; || Si llamo al proceso desde el cod. de cliente ...
         |ABRE #agifa024;
         #agifa024#0 = #0#3;
         |LEE 001#agifa024.=;
         #0#49 = #agifa024#192;  || Asigno la divisa por defecto del cliente
         #0#51 = #agifa024#193;  || Asigno la moneda de trabajo del cliente
         |PINTA #0#51;
       |FINSI;
       #agifa007#26 = #agifa036#46;
       |LEE 001#agifa007.=;  || Leo la serie
        |SI #agifa007#39 = "S"; |Y #agifa321#1 = "S";
           |SI #agifa024#191 = "S"; || Si el cliente es de divisa pactada
              |ABRE #agifa328;
              |ABRE #agifa329;
              #agifa329#0 = #agifa036#3;
              #agifa329#2 = #agifa036#49;
              #agifa329#7 = "N";
              |SELECT #2#agifa329;  || Con esta clave ...
              |LEE 011#agifa329.=;  || ... busco periodos no finalizados de esa divisa
              |SI FSalida = 0;  || Si existe un periodo no finalizado
                |PARA div_act = 0; |SI div_act = 0; |HACIENDO;
                  #agifa036#49 = #agifa329#2;        || Cargo la divisa ...
                  #agifa036#50 = #agifa329#3;        || ...y el cambio
                  |LEE 001#agifa329.=;   || Leo las lineas de Clientes/Divisas
                  nfecha = #agifa036#1 - #agifa329#6;  || Resto la fecha de fin de periodo a la actual
                  |SI nfecha > 0;  || Si la fecha actual supera a la del periodo asignado ...
                     |MENAV "0000ATENCION deberia Actualizar primero la divisa en Clientes/Divisas";
                     cli_divisa = #agifa036#3;  || Le envio el cod. de cliente a "agifa328"
                     |SUB_EJECUTA_NP "agifa328.tab";  || Ejecuto Clientess/Divisas
                     |ERROR; nError6 = 1;
                  |SINO;
                     div_act = 1;  || Para salir del PARA
                  |FINSI;
                |SIGUE;
              |SINO;  || Si no existe un periodo no finalizado para esa divisa
                |MENAV "0000No existe un periodo activo para la divisa en Clientes / Divisas";
                cli_divisa = #agifa036#3;  || Le paso a "agifa328" el cliente
                |SUB_EJECUTA_NP "agifa328.tab";  || Ejecuto Clientes / Divisas
                |ERROR; nError6 = 1;
                ||#agifa036#49 = #agifa036#52;
                ||#agifa036#50 = #agifa036#53;
              |FINSI;
              |SELECT #1#agifa329;
              |CIERRA #agifa329;
              |CIERRA #agifa328;
           |SINO; || Si el Cliente no es de Divisa pactada ...
              |ABRE #agifa324; || ...miro directamente en divisas
              #agifa324#0 = #agifa036#49;
              |LEE 001#agifa324.=;
              |SI FSalida = 0; ||Si existe la divisa...
                 #agifa036#50 = #agifa324#2; || la recojo
                 |SI #agifa324#5 ! -1; || Si los dias de margen de actualizacion no
                                       || son = -1 (indefinidos)
                   |PARA div_act = 0; |SI div_act = 0; |HACIENDO;
                      #agifa036#49 = #agifa324#0;        ||Cargo el valor de la divisa
                      #agifa036#50 = #agifa324#2;        ||Cargo el valor del cambio
                      |LEE 001#agifa324.=;  || Leo la divisa
                      nfecha = #agifa036#1 - #agifa324#4;
                      |SI nfecha > #agifa324#5; |Y #agifa324#5 ! -1;  || Si la fecha actual
                         |MENAV "0000ATENCION deberia Actualizar primero la divisa";
                         divisa = #agifa036#49;
                         |SUB_EJECUTA_NP "agifa326.tab";
                         |ERROR; nError6 = 1;
                      |SINO;
                         div_act = 1;
                      |FINSI;
                   |SIGUE;
                 |FINSI;
              |FINSI;
              |CIERRA #agifa324;
            |FINSI;
          |CIERRA #agifa024;
          |SINO;
            #agifa036#49 = #agifa036#52;
            #agifa036#50 = #agifa036#53;
          |FINSI;
       |PINTA #agifa036#49;
       |PINTA #agifa036#50;
       |SI Campo = 3; || Si llamo al proceso desde el cod. de cliente ...
         |HAZ LeeParamDiv;
         |HAZ Param_Divisas;
       |SINO;
         |HAZ Param_Divisas;
       |FINSI;
     |FINSI;
|FPRC;

|PROCESO LeeDep; |TIPO 13;  || Sirve para cargar los valores cada vez que
                            || cambio de ficha
     pintado = 0;   || controla pintado de las lineas
     |HAZ LeeMonBase;
     |HAZ LeeParamDiv;
     |HAZ Param_Divisas;

     |HAZ EsGuerola;
     |SI FSalida = 0;
          |HAZ GuerPinObra;
     |FINSI;
|FPRC;

|CALCULO modomod; |TIPO 6;
   |SI nModo036 = 2; |Y #0Campo ! Contenido;
       |MENAV "0000No se puede modificar el campo.";
       #0Campo = Contenido;
       |PINTA #0Campo;
       |ERROR;
   |FINSI;
   |SI #0Campo ! Contenido; |Y Campo = 51;
     |HAZ LeeParamDiv;
     |HAZ Param_Divisas;
   |FINSI;
|FCAL;

|PROCESO caltotal; |TIPO 0; ||Proceso especial para casos determinados como
                            ||modificar los campos de cabecera en dtos, cambio divisas etc
                            ||Recalcula total deposito, descuentos y ivas de lineas
                            ||En el caso de cambiar el cambio de divisas recalcula tambien precio... de lineas
   |SI #0Campo ! Contenido;||Solo si cambia
          caltot = 1;
          |SI Campo = 50; ||Si viene del cambio divisa
              recalpre = 1; ||Recalcular tambien precios linea
          |FINSI;
          |HAZ actal2;

          |PINTA #0#57;
          caltot = 0;
          recalpre = 0;
   |FINSI;
|FPRC;

|PROCESO LeeDesde; |TIPO 7;
     nModo = (FEntrada / 100) ! 0;
     |HAZ ControlUsuPV;
     |SI enErr ! 0; |PTEC 807; |ACABA; |FINSI;

     |C_P #0#3, 0, enPosCli;
     |HAZ DefSerVta;
     |SI nModo = 1; |Y #279#24 = "S";
          |SI eaSerCli ! "";
               #0#46 = eaSerCli; |PTEC 802;
          |SINO;
               |PTEC 807; |PTEC 807; |ERROR; |ACABA;
          |FINSI;
     |FINSI;

     |SI PRMNT_aHisto ! "";
          ser_alb = PRMNT_aHisto % 105;
          num_alb = (A\(PRMNT_aHisto % 6));
          PRMNT_aHisto = "";

          #0#46 = ser_alb;
          #0#0 = num_alb;
          |PTEC 802;
          |PTEC 802;
          |PINTA #0#46;
          |PINTA #0#0;
          |ACABA;
     |FINSI;
|FPRC;

|PROCESO linf;
  #49#0 = #0#3;
  #49#1 = 1;
|FPRC;

|PROCESO lsup;
  #49#0 = #0#3;
  #49#1 = 9999;
|FPRC;

|PROCESO C;
     nModo036 = (FEntrada / 100) ! 0;
/*
     |SI #0#3 ! Contenido; |O nTecla = 10;
          |ABRE #49;
          #49#0 = #0#3;
          #49#1 = 1;
          |LEE 101#49];
          |SI FSalida = 0; |Y #49#0 = #0#3;
               |LEE 101#49s;
               |SI FSalida = 0; |Y #49#0 = #0#3;
                    ||El cliente tiene mas de una direccion de entrega
                    #49#1 = #0#61;
                    |LEE 000#49];
                    |CONSULTA_F_CLAVE #1#49,linf,lsup;
                    |SI FSalida = 0;
                         #0#61 = #49#1; |PINTA #0#61;
                         #0#4 = #49#10; |PINTA #0#4;
                         #0#29 = #49#10;|PINTA #0#29;
                         #0#30 = #49#2; |PINTA #0#30;
                         #0#31 = #49#3; |PINTA #0#31;
                         #0#33 = #49#7; |PINTA #0#33;
                         |SI #49#14 = "  ";
                             #0#6 = #4#25;
                         |SINO;
                             #0#6 = #49#14;
                         |FINSI;
                         |PINTA #0#6;
                    |FINSI;
               |SINO;
                    #0#61 = 1; |PINTA #0#61;
               |FINSI;
          |FINSI;
          |CIERRA #49;
     |FINSI;
*/
     |SI nTecla = 11; |O #41#102 = "S"; |O #41#102 = "X";
          eaFuerza = "N";
          |SI nTecla = 11; eaFuerza = "S"; |FINSI;
          eaCodigo = #0#3;
          |SUB_EJECUTA_NP "dsz99994";
          |SI #41#102 = "N"; |Y nModo036 ! 2; nError6 = 1; |ERROR;  |FINSI;
     |FINSI;
|FPRC;

|PROCESO ser44; |TIPO 0;              || VIC 20/07/98

          cli=#0#3;
          ser=#0#46;
          |SUB_EJECUTA_NP "agifv011.tab;por_ejemplo"
|FPRC;

|PROCESO Cli36_7; |TIPO 7;
     nModo = (FEntrada / 100) ! 0;
     |SI #279#24 = "S";
          |C_M #0#3, 1, "N";
          |SI nModo = 1;
               |SI eaSerCli ! ""; #0#3 = eaCliSer; |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Cli36_0; |TIPO 0;
     nModo = (FEntrada / 100) ! 0;
     |HAZ ObsCli36;
     |SI #0Campo ! Contenido; |O nModo = 1;
          |ABRE #49;
          #49#0 = #0#3;
          #49#1 = #0#61;
          |LEE 000#49=;
          |CIERRA #49;
          |HAZ AsignaDir036;
     |FINSI;
     |SI #0Campo ! Contenido; |Y nModo = 2;
          |SI #41#250 = "S"; |HAZ DepoModLin; |FINSI;
     |FINSI;
     |HAZ ser44;
     |HAZ CambioDivisa;
     |HAZ Pago7_036;
     |HAZ DefePromo36;
|FPRC;

|PROCESO Cli36_6; |TIPO 6;
     nError6 = 0;
     |HAZ altac;
     |HAZ pedcon2;
     |HAZ C;
     |SI nError6 = 1; |ERROR; |FINSI;
|FPRC;

|PROCESO PrecioCab10;
     |ABRE #2;
     #2#0 = #3#2;
     |LEE 020#2=;
     |CIERRA #2;

     |ABRE #4;
     #4#0 = #0#3;
     |LEE 000#4=;
     |CIERRA #4;

     fed_dt = #0#1;   || Fecha....
     art_dt = #3#2;   || Articulo.
     cli_dt = #3#15;  || Cliente..
     fam_dt = #3#13;  || Familia.
     iva_dt = #2#30;  || Tipo Iva.
     dtos_2 = 0;      || Descuento 2.
     dto_pt = 0;      || Descuento Ptas.
     dtos_1 = 0;      || Descuento 1.
     pvp_ve = 0;      || Precio Venta.
     tarifa = #4#39;  || Tarifa Cliente....
     gru_dt = #4#158; || Grupo Cliente.
     uni_dt = #3#5;
     uni_dt2 = #3#28;
     nPromo = #0#60;
     enMensaje = 0;
     aParam = "";
     eaSerie = #3#22;
     enDirEnt = #0#61;
     |SI #2#176 = "S";  enMensaje = 1;  |FINSI;
     enAlmacen = #3#3;
     |HAZ calcdtos;    || Calculo Dtos.....
     #3#6   = pvp_ve;  || Precio Venta. sin iva.
     #3#8   = dtos_1;  || 1 Dto.
     #3#23  = dtos_2;  || 2 Dto.
|FPRC;

|PROCESO CreaLinProCab36;
     |ABRE #referen@;
     #referen@#22 = #3#22;
     #referen@#0 = #3#0;
     #referen@#1 = #3#38;
     |LEE 000#referen@.=;
     |SI FSalida = 0; |O #3#38 = 0;
          #referen@#1 = 999;
          |LEE 000#referen@.];
          |SI FSalida = 0;
               |LEE 000#referen@.a;
          |SINO;
               |LEE 000#referen@.u;
          |FINSI;
          |SI FSalida = 0; |Y #referen@#22 = #3#22;  |Y #referen@#0 = #3#0;
               nLin = #referen@#1 + 1;
          |SINO;
               nLin = 1;
          |FINSI;
          |SI nLin = #0#1; nLin = nLin + 1; |FINSI;
          #referen@#22 = #3#22;
          #referen@#0 = #3#0;
          #referen@#1 = nLin;
          |GRABA 020#referen@.b;
     |SINO;
          nLin = #3#38;
          |GRABA 020#referen@.b;
     |FINSI;
     |SALVA_DATOS #3;
     |REPON_DATOS #referen@;

     #referen@#1 = nLin;
     #referen@#5 = #3#41;
     #referen@#6 = 0;
     #referen@#7 = 0;
     #referen@#8 = 0;
     #referen@#9 = 0;
     #referen@#14 = 0;
     #referen@#24 = 0;
     #referen@#25 = 0;
     #referen@#26 = 0;
     #referen@#27 = 0;
     #referen@#38 = -1;

     |SALVA_DATOS #3;
     |SALVA_DATOS #referen@;
     |REPON_DATOS #3;
     #3#1 = #referen@#1;
     enForma = 1;
     |HAZ AcumulaDE;
     |REPON_DATOS #3;

     |GRABA 020#referen@.a;
     |LIBERA #referen@;
|FPRC;

|PROCESO DepoModLinea;
     #134#0 = #0#3;
     #134#1 = #3#2;
     |LEE 000#134=;
     |SI FSalida = 0; |Y #0#1 ] #134#6; |Y #0#1 [ #134#7;
          #3#15 = #0#3; || cliente
          |HAZ PrecioCab10;
          nUniPromo = #3#5 / #134#2;
          nResto = #3#5 $ #134#2;
          nResto = nResto / #134#2;
          nUniPromo = nUniPromo  - nResto;
          #3#6 = (#3#6 - #134#4) ! nDeci_Base;
          #3#39 = #134#4;
          #3#40 = #134#5;
          #3#41 = nUniPromo * #134#3;
          #3#8 = #134#5;
          |HAZ TotalLin70;
          |SI #3#41 = 0;
               #3#38 = 0;
          |SINO;
               |HAZ CreaLinProCab36;
               #3#38 = nLin;
          |FINSI;
          |GRABA 020#3a;
     |SINO;    || No tiene promocion
          #3#38 = 0;
          #3#39 = 0;
          #3#40 = 0;
          #3#41 = 0;
          |HAZ TotalLin70;
          |GRABA 020#3a;
     |FINSI;
     |LIBERA #3;
|FPRC;

|PROCESO BorraLinPromo36;
     |SI #3#38 = -1;
          enForma = -1;
          |HAZ AcumulaDE;
          |BORRA 020#3a;
     |FINSI;
|FPRC;

|PROCESO DepoModLin;
     |CONFI "0000SActualizar promociones para el nuevo cliente?";
     |SI FSalida = 0;
          |DDEF aAlfa;
          aAlfa = aAlfa + "agifa070";
          |CARGA_DEF aAlfa, referen@;
          |SI FSalida = 0;
               |ABRE #134;
               |ABRE #referen@;
               |BUCLELIN 0 BorraLinPromo36 #0;
               |BUCLELIN 0 DepoModLinea #0;
               |CIERRA #referen@;
               |CIERRA #134;
               |DESCARGA_DEF #referen@;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO GuerBorObra;
     |DDEF aAlfa;
     aAlfa = aAlfa + "guerm056";
     |CARGA_DEF aAlfa, referen@;
     |SI FSalida = 0;
          |DDEFECTO #referen@;
          |ABRE #referen@;
          #referen@#0 = #0#46;
          #referen@#1 = #0#0;
          |LEE 101#referen@.=;
          |SI FSalida = 0; |BORRA 020#referen@.a; |FINSI;
          |CIERRA #referen@;
          |DESCARGA_DEF #referen@;
     |FINSI;
|FPRC;

|PROCESO GuerPinObra;
     |DDEF aAlfa;
     aAlfa = aAlfa + "guerm056";
     |CARGA_DEF aAlfa, referen@;
     |SI FSalida = 0;
          |DDEFECTO #referen@;
          |ABRE #referen@;
          #referen@#0 = #0#46;
          #referen@#1 = #0#0;
          |LEE 000#referen@.=;
          |CIERRA #referen@;
          |PINTA 1026, "Obra: ";
          |PINTA 1126, "Fecha Adjudicacion:";
          |ATRI I;
          |PINTA 1032, #referen@#3;
          |PINTA 1039, #referen@#5;
          |PINTA 1146, #referen@#4;
          |ATRI 0;
          |DESCARGA_DEF #referen@;
     |FINSI;
|FPRC;

|PROCESO GuerEntObra; |TIPO 7;
     |HAZ EsGuerola;
     |SI FSalida = 0;
          aAlfa = "guerm056;" + #0#3 + #0#46 + #0#0;
          |SUB_EJECUTA aAlfa;
          |HAZ GuerPinObra;
          |SI enError = 1; |ERROR; |PTEC 807; |FINSI;
     |FINSI;
|FPRC;

|PROCESO T036_15; |TIPO 15;
     nTipo = 15; |HAZ Log036;
|FPRC;

|PROCESO T036_16; |TIPO 16;
     nTipo = 16; |HAZ Log036;
|FPRC;

|PROCESO T036_17; |TIPO 17;
     nTipo = 17; |HAZ Log036;
|FPRC;

|PROCESO Log036;
     |DDEF aAlfa;
     aAlfa = aAlfa + "dsm00087";
     |CARGA_DEF aAlfa, dsm00087@;
     |SI FSalida ! 0; |ACABA; |FINSI;

     aSer = #0#46;
     aNum = ("000000" + #0#0) % -106;
     aTipo = "DE";

     |ABRE #dsm00087@;
     |DDEFECTO #dsm00087@;
     |SELECT #2#dsm00087@;
     #dsm00087@#1 = aSer;
     #dsm00087@#2 = aNum;
     #dsm00087@#3 = aTipo;
     |LEE 101#dsm00087@.=;
     |SI FSalida ! 0;
          |SELECT #1#dsm00087@;
          |LEE 000#dsm00087@.u;
          |SI FSalida = 0;
               nContaLog = #dsm00087@#0 + 1;
          |SINO;
               nContaLog = 1;
          |FINSI;
          |DDEFECTO #dsm00087@;
          #dsm00087@#0 = nContaLog;
          #dsm00087@#1 = aSer;
          #dsm00087@#2 = aNum;
          #dsm00087@#3 = aTipo;
          |GRABA 020#dsm00087@.b;
     |FINSI;
     |SI FSalida = 0;
          |HORA aAlfa;
          aUsu = Usuario % 810;
          |SI nTipo = 15;
               #dsm00087@#4 = aUsu;
               #dsm00087@#5 = ~;
               #dsm00087@#6 = aAlfa;
               #dsm00087@#7 = "";
               #dsm00087@#8 = "01.01.0000";
               #dsm00087@#9 = ""
               #dsm00087@#10 = "";
               #dsm00087@#11 = "01.01.0000";
               #dsm00087@#12 = ""
          |FINSI;
          |SI nTipo = 16;
               #dsm00087@#7 = aUsu;
               #dsm00087@#8 = ~;
               #dsm00087@#9 = aAlfa;
          |FINSI;
          |SI nTipo = 17;
               #dsm00087@#10 = aUsu;
               #dsm00087@#11 = ~;
               #dsm00087@#12 = aAlfa;
          |FINSI;
          |GRABA 020#dsm00087@.a;
     |FINSI;
     |CIERRA #dsm00087@;
     |DESCARGA_DEF #dsm00087@;
|FPRC;

|PROCESO AsignaDir036;
     #0#61 = #49#1; |PINTA #0#61;
     #0#4 = #49#10; |PINTA #0#4;
     #0#29 = #49#10;|PINTA #0#29;
     #0#30 = #49#2; |PINTA #0#30;
     #0#31 = #49#3; |PINTA #0#31;
     #0#33 = #49#7; |PINTA #0#33;
     |SI #49#14 = "  ";
         #0#6 = #4#25;
     |SINO;
         #0#6 = #49#14;
     |FINSI;
     |PINTA #0#6;
|FPRC;

|PROCESO MinDir036;
     #49#0 = #0#3;
     #49#1 = 1;
|FPRC;

|PROCESO MaxDir036;
     #49#0 = #0#3;
     #49#1 = 9999;
|FPRC;

|PROCESO Dir036_0; |TIPO 0;
     |SI FSalida = 2; |ACABA; |FINSI;
     nTecla = FSalida;
     |SI nTecla = 12;    || F4 Alta direccion
          |PUSHV 0501 2380;
          |ENTLINEAL #1#49, -2, 1, 22, 1, MinDir036, MaxDir036;
          |POPV;
          nTecla = 18;
     |FINSI;
     |ABRE #49;
     |SI nTecla = 18;   || F10  Todas las direcciones
          #49#0 = #0#3;
          #49#1 = #0#61;
          |LEE 000#49];
          |CONSULTA_F_CLAVE #1#49, MinDir036, MaxDir036;
          |SI FSalida = 0;
               |HAZ AsignaDir036;
          |SINO;
               |ERROR; |ACABA;
          |FINSI;
     |FINSI;
     #49#0 = #0#3;
     #49#1 = #0#61;
     |LEE 000#49=;
     |SI FSalida = 0;
          |SI #49#18 = "N";
               |CONFI "0000NDireccion de entrega desactiva. 풠ontinuar?";
               |SI FSalida ! 0; |ERROR; |ACABA; |FINSI;
          |FINSI;
     |SINO;
          |MENAV "0000No existe direccion de entrega...";
          |ERROR; |ACABA;
     |FINSI;
     |CIERRA #49;
     |SI #0Campo ! Contenido;
          |HAZ AsignaDir036;
     |FINSI;
|FPRC;

|PROCESO Dir036_7; |TIPO 7;
     |SI #41#71 = "S";
          |ABRE #49;
          #49#0 = #0#3;
          #49#1 = 1;
          |LEE 000#49];
          |SI FSalida = 0; |Y #49#0 = #0#3;
               |LEE 000#49s;
               |SI FSalida = 0; |Y #49#0 = #0#3;
                    aSinError = "S";
                    |HAZ Dir036_66;
                    aSinError = "N";
                    |SI nSal = 0; |ERROR; |FINSI;
               |FINSI;
          |FINSI;
          |CIERRA #49;
     |FINSI;
|FPRC;

|PROCESO Dir036_66; |TIPO 66;
     nModo = (FEntrada / 100) ! 0;
     |ABRE #49;
     aSql = "SELECT * FROM dsm00003 INTO sq" + Usuario + ".def WHERE ";
     aSql = aSql + "0 == " + #0#3 + " AND 18 == S";
     |SQL aSql;
     |SI FSalida = 0;
          |DDEF aDef; |QBF aDef;
          aDef = aDef + "sq" + Usuario + ".mas";
          |CARGA_DEF aDef, referen@;
          |SI FSalida = 0;
               |ABRE #referen@;
               #referen@#0 = #0#3;
               #referen@#1 = #0#61;
               |LEE 000#referen@.]
               |CONSULTA_CLAVE #1#referen@;
               nSal = FSalida;
               |SI FSalida = 0;
                    |SI nModo ! 2; |O #referen@#0 ! #0#3; |O #referen@#1 ! #0#61;
                         #49#0 = #referen@#0;
                         #49#1 = #referen@#1;
                         |LEE 020#49=;
                         |HAZ AsignaDir036;
                    |FINSI;
               |SINO;
                    |SI aSinError ! "S";
                         |ERROR;
                    |FINSI;
               |FINSI;
               |CIERRA #referen@;
               |DELINDEX #referen@;
               |DESCARGA_DEF #referen@;
               |FBORRA aDef;
          |FINSI;
     |FINSI;
     |CIERRA #49;
|FPRC;

|PROCESO Pago7_036; |TIPO 7;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 1;
          |HAZ EsTagloma;
          |SI FSalida = 0;
               eaAlfa = #0#46;
               |HAZ TagloDefFP;
               |SI eaAlfa ! ""; #0#8 = eaAlfa; |PINTA #0#8; |FINSI;
               |SI eaQuitaDto = "S";
                    #0#5 = 0; |PINTA #0#5;
                    #0#32 = 0; |PINTA #0#32;
                    #0#7 = 0; |PINTA #0#7;
               |FINSI;
          |FINSI;
          |HAZ EsGestral;
          |SI FSalida = 0;
               eaAlfa = #0#46;
               |HAZ GestrDefFP;
               |SI eaAlfa ! ""; #0#8 = eaAlfa; |PINTA #0#8; |FINSI;
               |SI eaQuitaDto = "S";
                    #0#5 = 0; |PINTA #0#5;
                    #0#32 = 0; |PINTA #0#32;
                    #0#7 = 0; |PINTA #0#7;
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO DefePromo36;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo ! 1; |ACABA; |FINSI;
     |SI #41#129 ! "S"; |ACABA; |FINSI;

     |HAZ EsAparecida;
     |SI FSalida = 0;
          |ABRE #4;
          #4#0 = #0#4;
          |LEE 000#4=;
          |CIERRA #4;
          |ABRE #211;
          |LEE 000#211p;
          |PARA; |SI FSalida = 0; |HACIENDO;
               |SI #211#25 = "GENERADA ";
                    |PARA i = 1; |SI i [ 24; |HACIENDO i = i + 1;
                         |SI #211i = #4#158;
                              #0#60 = #211#0; |PINTA #0#60;
                              |SAL;
                         |FINSI;
                    |SIGUE;
               |FINSI;
               |LEE 000#211s;
          |SIGUE;
          |CIERRA #211;
     |FINSI;
|FPRC;

|PROCESO IVA036_0; |TIPO 0;
     enTiva = #0Campo;
     efFiva = #0#1;
     |HAZ SacaIVA;
     |SI eaDiva = "";
          |MENAV "0000No existe tipo iva para esta fecha...";
          |ERROR; |ACABA;
     |SINO;
          |HAZ caltotal;
     |FINSI;
|FPRC;

|PROCESO IVA036_66; |TIPO 66;
     enTiva = #0Campo;
     efFiva = #0#1;
     |HAZ ConsuIVA;
     |SI eaDiva = "";
          |ERROR;
     |SINO;
          #0Campo = enTiva; |PINTA #0Campo;
     |FINSI;
|FPRC;

|PROCESO ObsCli36;
     |SI #agifa142#14 = "S";
          aAlfa = #agifa024#53;     |QBF aAlfa;
          |SI aAlfa ! "";
               |PUSHV 1008 1370;
               |SI #agifa142#20 = "S"; |AVISO; |FINSI;
               |BLANCO 1008 1370;
               |CUADRO 1008 1370;
               |ATRI R;
               |PINTA 1110 , #agifa024#53;
               |ATRI 0;
               |PAUSA;
               |POPV;
          |FINSI;
     |FINSI;
|FPRC;
