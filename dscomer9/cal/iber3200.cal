|FICHEROS;
     iber0200 #6;
     Tempor@  #0; ||Puntero para el carga_def
     ib__arti;
     ib__kits;
     ib__ticd;
     ib__tick;
     ibxxtpas;
     ib__mesa;
     ib__clie;
     ib__apre;
     ib__sala;
     ib__extr;
     ib__rese;
     ib__fami;
     ib__desg;
     ib__comp;
     ib__coma;
     ib__cama;
     ib__faco;
     ib__secc;
     ib__perm;
     ib__data;
     ib__tari;
     ibxxtarj;
     ibxxtarl;
     ibxxbloq;
     iber0042 #42;
|FIN;

|VARIABLES;
     aSwRefresca = "";
     aMensa = "";
     &eaCentro = "";
     aPos = "";

     &aIPCobro = "";          || IP de la caja (viene del iber0200)
     &nPuntoDeVenta = 0;      || punto de venta (viene del iber0200)

     &eaVarPalm1 = "";
     &eaVarPalm2 = "";
     &eaVarPalm3 = "";
     &eaVarPalm4 = "";
     &eaVarPalm5 = "";
     &eaVarPalm6 = "";
     &eaVarPalm7 = "";
     &eaVarPalm8 = "";
     &eaVarPalm9 = "";
     &enReturn   = "";
     &eaClave    = "";
     nPase       =  0;

     aPath = "";
     aFic = "";
     nHandle = 0;
     aDato = "";
     aVar = "";
     aVal = "";
     aCar = "";
     nPos = 0;
     aLon = "";
     aAlfa = "";
     i = 0;

     nCampo = 0;
 {90}Valor = "";
     aReg = "";
     aLetra = "";
     aTab = "";
|FIN;

|PROCESO &EntraClave;
     |QBF eaClave;
     |SI eaVarPalm1 =  eaClave;
          eaVarPalm1 = #iber0200#4; ||USUARIO
          eaVarPalm2 = #iber0200#5; ||NOMBRE USUARIO
          eaVarPalm3 = #iber0200#8; ||SALA
          eaVarPalm4 = #iber0200#9; ||NOMBRE SALA
          enReturn ="1";
     |SINO;
          eaVarPalm1 = "";
          eaVarPalm2 = "";
          eaVarPalm3 = "";
          eaVarPalm4 = "";
          enReturn ="0";
     |FINSI;
|FPRC;

|PROCESO &AbreDat;
     |ABRET     #ib__arti;
     |ABRET     #ib__kits;
     |ABRET     #ib__ticd;
     |ABRET     #ib__tick;
     |ABRET     #ib__mesa;
     |ABRET     #ib__clie;
     |ABRET     #ib__apre;
     |ABRET     #ib__sala;
     |ABRET     #ib__extr;
     |ABRET     #ib__rese;
     |ABRET     #ib__fami;
     |ABRET     #ib__desg;
     |ABRET     #ib__comp;
     |ABRET     #ib__coma;
     |ABRET     #ib__cama;
     |ABRET     #ib__faco;
     |ABRET     #ib__secc;
     |ABRET     #ib__perm;
     |ABRET     #ib__data;
     |ABRET     #ib__tari;
|FPRC;

|PROCESO &CierraDat;
     |CIERRAT     #ib__arti;
     |CIERRAT     #ib__kits;
     |CIERRAT     #ib__ticd;
     |CIERRAT     #ib__tick;
     |CIERRAT     #ib__mesa;
     |CIERRAT     #ib__clie;
     |CIERRAT     #ib__apre;
     |CIERRAT     #ib__sala;
     |CIERRAT     #ib__extr;
     |CIERRAT     #ib__rese;
     |CIERRAT     #ib__fami;
     |CIERRAT     #ib__desg;
     |CIERRAT     #ib__comp;
     |CIERRAT     #ib__coma;
     |CIERRAT     #ib__cama;
     |CIERRAT     #ib__faco;
     |CIERRAT     #ib__secc;
     |CIERRAT     #ib__perm;
     |CIERRAT     #ib__data;
     |CIERRAT     #ib__tari;
|FPRC;

|PROCESO &GrabaComanda;
      |VARIABLES;
         &eaCampo0Tick = "";
         &eaCampo1Tick = "";
         &eaCampo2Tick = "";
         &eaCampo3Tick = "";
         &eaCampo4Tick = "";
         &eaCampo5Tick = "";
         &eaCampo6Tick = "";
         &eaCampo7Tick = "";
         &eaCampo8Tick = "";
         &eaCampo9Tick = "";
         &eaCampo10Tick = "";
         &eaCampo11Tick = "";
         &eaCampo12Tick = "";

     |FIN;

     |DDEFECTO #ib__tick;
     |ABRE #ib__tick;
     nPase = eaCampo0Tick; ||Numero ticket
     #ib__tick#0 = nPase;
     nPase = eaCampo1Tick; ||Numero linea Ticket
     #ib__tick#1 = nPase;
     |LEE 101#ib__tick.=;
     |SI FSalida ! 0;
          |GRABA 020#ib__tick.b;
     |FINSI;
     |SI eaCampo11Tick ! "0"; ||PARAMETRO DE CONTROL DE BORRADO DE REGISTRO

          #ib__tick#2 = eaCampo2Tick; ||codigo articulo

         || nPase = eaCampo3Tick; ||Codigo camarero
         ||  #ib__tick#4 = #6#33;

          nPase = eaCampo4Tick; ||Codigo mesa
          #ib__tick#6 = nPase;

          nPase = eaCampo5Tick; ||Numero de unidades de del articulo
          #ib__tick#7 = nPase;
          nPase = eaCampo6Tick; ||precio en euros
          #ib__tick#9 = nPase;

          nPase = eaCampo7Tick; ||Numero Sala
          #ib__tick#10 = nPase;

          #ib__tick#11 = eaCampo8Tick; ||orden plato
          #ib__tick#12 = eaCampo9Tick; ||Descripcion articulo
          #ib__tick#14 = eaCampo12Tick; ||Seccion



          nPase = eaCampo10Tick; ||Numero comensales
          #ib__tick#15 = nPase;

          |GRABA 020#ib__tick.a;

          |DDEFECTO #ib__mesa;
          |ABRE #ib__mesa;
          #ib__mesa#5 = #ib__tick#10;        || numero de sala
          #ib__mesa#0 = #ib__tick#6;         || numero de mesa
          |LEE 000#ib__mesa.=;
          |CIERRA #ib__mesa;

          aSwRefresca = "TIQUET";  |HAZ RefrescaCaja;
     |SINO;
          |BORRA 020#ib__tick.a;
     |FINSI;
     |LIBERA #ib__tick;
     |CIERRA #ib__tick;
|FPRC;

|PROCESO IniVarComanda;
     eaCampo0Tick = "";
     eaCampo1Tick = "";
     eaCampo2Tick = "";
     eaCampo3Tick = "";
     eaCampo4Tick = "";
     eaCampo5Tick = "";
     eaCampo6Tick = "";
     eaCampo7Tick = "";
     eaCampo8Tick = "";
     eaCampo9Tick = "";
     eaCampo10Tick = "";
     eaCampo11Tick = "";
     eaCampo12Tick = "";
|FPRC;

|PROCESO ProcesaVar;
     aLon = aDato % A0;
     aVar = "";
     aVal = "";
     |PARA i = 10; |SI i [ aLon; |HACIENDO i = i + 1;
          nPos = 100 * i + 1;
          aCar = aDato % nPos;
          |SI aCar = ",";
               |SAL;
          |SINO;
               aVar = aVar + aCar;
          |FINSI;
     |SIGUE;
     i = i + 1;
     |PARA; |SI i [ aLon; |HACIENDO i = i + 1;
          nPos = 100 * i + 1;
          aCar = aDato % nPos;
          aVal = aVal + aCar;
     |SIGUE;
     |SI aVar = "eaCampo0Tick"; eaCampo0Tick = aVal; |FINSI;
     |SI aVar = "eaCampo1Tick"; eaCampo1Tick = aVal; |FINSI;
     |SI aVar = "eaCampo2Tick"; eaCampo2Tick = aVal; |FINSI;
     |SI aVar = "eaCampo3Tick"; eaCampo3Tick = aVal; |FINSI;
     |SI aVar = "eaCampo4Tick"; eaCampo4Tick = aVal; |FINSI;
     |SI aVar = "eaCampo5Tick"; eaCampo5Tick = aVal; |FINSI;
     |SI aVar = "eaCampo6Tick"; eaCampo6Tick = aVal; |FINSI;
     |SI aVar = "eaCampo7Tick"; eaCampo7Tick = aVal; |FINSI;
     |SI aVar = "eaCampo8Tick"; eaCampo8Tick = aVal; |FINSI;
     |SI aVar = "eaCampo9Tick"; eaCampo9Tick = aVal; |FINSI;
     |SI aVar = "eaCampo10Tick"; eaCampo10Tick = aVal; |FINSI;
     |SI aVar = "eaCampo11Tick"; eaCampo11Tick = aVal; |FINSI;
     |SI aVar = "eaCampo12Tick"; eaCampo12Tick = aVal; |FINSI;
|FPRC;

|PROCESO Dato;
     IValor = Valor1<-;
     |PARA i = 1; |SI i [ 90; |HACIENDO i = i + 1;
          Valor = "";
          IValor = IValor + 1;
     |SIGUE;
     nCampo = 1;
     IValor = Valor1<-;
     aLon = aDato % A0;
     aReg = "";
     aTab = "|";
     |PARA i = 1; |SI i [ aLon; |HACIENDO i = i + 1;
          nPos = i * 100 + 1;
          aLetra = aDato % nPos;
          |SI aLetra = aTab;
               Valor = aReg;
               aReg = "";
               nCampo = nCampo + 1;
               IValor = IValor + 1;
          |SINO;
               aReg = aReg + aLetra;
          |FINSI;
          |SI nCampo > 90; |SAL; |FINSI;     || Por si.... :)
     |SIGUE;
     Valor = aReg;
|FPRC;
/*
campo0 =numero de tiquet
campo1=linea
campo2=codigo articulo
campo3=plato
campo4=campo fijo siempre 5 ceros
campo5=??????
campo6=mesa
campo7=unidades
campo8=?????
campo9=precio
campo10=sala
campo11=orden
campo12=descripcion
campo13=??
campo14=seccion
*/
|PROCESO GraLinComanda;
     |HAZ Dato;
     |DDEFECTO #ib__tick;
     |ABRE #ib__tick;
     #ib__tick#0 = Valor1;
     #ib__tick#1 = Valor2;
     |LEE 101#ib__tick.=;
     |SI FSalida ! 0; |GRABA 020#ib__tick.b; |FINSI;
     #ib__tick#2 = Valor3;
     #ib__tick#3 = Valor4;
     #ib__tick#6 = Valor7;
     #ib__tick#7 = Valor8;
     #ib__tick#9 = Valor10;
     #ib__tick#10 = Valor11;
     #ib__tick#11 = Valor12;
     #ib__tick#12 = Valor13;
     #ib__tick#13 = Valor14;
     #ib__tick#14 = Valor15;
     #ib__tick#18 = Valor17;
     |GRABA 020#ib__tick.a;
     |CIERRA #ib__tick;
|FPRC;

|PROCESO &ImportaComanda;
     |ABRE #42;
     #42#0 = eaCentro;
     #42#1 = #6#4;
     |LEE 000#42=;
     |CIERRA #42;
     aAlfa = #42#5; |QBF aAlfa;
     aPos = aAlfa % A0;
     aPath = "";
     |PARA i = aPos; |SI i > 0; |HACIENDO i = i - 1;
          nPos = i * 100 + 1;
          aCar = aAlfa % nPos;
          |SI aCar = "/"; |O aCar = "\";
               |SI aPath ! ""; |SAL; |FINSI;
          |FINSI;
          aPath = aCar + aPath;
     |SIGUE;

     |DBASE aAlfa;
     aPath = aAlfa + aPath + "/*.pda";
     |_PDIR aPath;
     |PARA; |SI FSalida = 0; |HACIENDO;
          aFic = aPath; |QBF aFic;
          |XABRE "U", aFic, nHandle;
          |SI FSalida ] 0;
               |PARA; |SI; |HACIENDO;
                    |XLEE nHandle, 255, aDato;
                    |SI FSalida < 0; |SAL; |FINSI;
                    aAlfa = aDato % 109;
                    |SI aAlfa = "GRABAVAR:";
                         |HAZ ProcesaVar;
                    |FINSI;
                    aAlfa = aDato % 108;
                    |SI aAlfa = "PROCESO:"
                         aAlfa = aDato % 9;
                         |SI aAlfa = "GrabaComanda";
                              |HAZ GrabaComanda;
                              |HAZ IniVarComanda;
                         |FINSI;
                    |FINSI;
                    aAlfa = aDato % 115;
                    |SI aAlfa = "GRABA:ib__tick@";
                         |XLEE nHandle, 255, aDato;  || 1
                         |XLEE nHandle, 255, aDato;  || campos
                         |HAZ GraLinComanda;
                         |XLEE nHandle, 255, aDato;  || .
                      || pa probar!!!
                      ||     aSwRefresca = "TIQUET";  |HAZ RefrescaCaja;
                    |FINSI;
               |SIGUE;
               |XCIERRA nHandle;
               |FBORRA aFic;
          |FINSI;
          |_SDIR aPath;
     |SIGUE;
|FPRC;

|PROCESO &GrabaEstado;
     enReturn = "0";               || cero=fallo

     |DDEFECTO #ib__mesa;
     |ABRE #ib__mesa;
     nPase = eaVarPalm3;
     #ib__mesa#5 = nPase;          || numero de sala
     nPase = eaVarPalm6;
     #ib__mesa#0 = nPase;          || numero de mesa
     |LEE 101#ib__mesa.=;
     |SI FSalida ! 0;    |GRABA 020#ib__mesa.b;   |FINSI;
     nPase = eaVarPalm8;
     #ib__mesa#6 = nPase;          || numero de comensales
     nPase = eaVarPalm7;
     #ib__mesa#2 = nPase;          || numero de estado
     |GRABA 020#ib__mesa.a;
     |SI FSalida = 0;
          enReturn = "1";
          aSwRefresca = "ESTADO";  |HAZ RefrescaCaja;
     |FINSI;
     |LIBERA #ib__mesa;
     |CIERRA #ib__mesa;
|FPRC;

|PROCESO &CargaFiltroIb__tick;
    |VARIABLES;
         &eaCampo13Tick = "";
         &eaCampo14Tick = "";
         aDir           = "";
     ||    aAlfa          = "";
    |FIN;

    nPase = eaCampo13Tick;
    aAlfa = "SELECT * FROM ib__tick INTO TempoIb.def WHERE 6 == " + nPase;
    |SQL aAlfa;

    ||Hacemos el carga_def;

    |DBASE aDir;
    |QBF aDir;
    aDir = aDir + "def/TempoIb.mas";
    |CARGA_DEF aDir , Tempor@;
    |SI FSalida ! 0;
         |DESCARGA_DEF #Tempor@;
         |ACABA;
         eaCampo14Tick = 0;
    |FINSI;
    eaCampo14Tick = 1;
|FPRC;

|PROCESO &DesCargaFiltroIb__tick;
     |SI eaCampo14Tick = "1";
          |DESCARGA_DEF #Tempor@;
     |FINSI;
|FPRC;

|PROCESO &RefrescaCaja;
     |VARIABLES;
       ||   i = 0;
          handle = 0;
          {10}ptcp = "";
     |FIN;

     |SI aIPCobro = "";            || para refrescar sin central de cobro
          |IP_REMOTO aIPCobro;
          |SI aIPCobro = "";
               |IP_LOCAL aIPCobro;
               |SI aIPCobro = "";  aIPCobro = "127.0.0.1";  |FINSI;
          |FINSI;
     |FINSI;

     ptcp1 = "CONECTA";
     ptcp2 = aIPCobro;                  || IP Caja
     ptcp3 = "31334";                   || puerto dll tactil (esto es fijo)
     |ESPECIFICA "TCP",ptcp;
     |SI FSalida ] 0;
          handle = FSalida;

          ptcp1 = "ENVIA_S";
          ptcp2 = handle;
          ptcp3 = "0" + &009 + nPuntoDeVenta;    || punto de venta
          ptcp4 = 4;     || (incluye el ascii 0 final)
          |ESPECIFICA "TCP", ptcp;

          ptcp1 = "ENVIA_S";
          ptcp2 = handle;
          || "101" + sala + mesa + estado + cliente + tiquet + comensales + descuento
/******** ANTES **************
          |SI aSwRefresca = "ESTADO";
              ptcp3 = "101" + &009 + #ib__mesa#5 + &009 + #ib__mesa#0 + &009 + #ib__mesa#2 + &009 + "0" + &009 + "0" + &009 + "1" + &009 + "0";
          |FINSI;
          |SI aSwRefresca = "TIQUET";
              ptcp3 = "101" + &009 + #ib__tick#10 + &009 + #ib__tick#6 + &009 + #ib__mesa#2 + &009 + "0" + &009 + #ib__tick#0 + &009 + "1" + &009 + "0";
          |FINSI;
************ AHORA *********/
          |SI aSwRefresca = "ESTADO";
               ptcp3 = "101" + &009 + #ib__mesa#5 + &009 + #ib__mesa#0 + &009 + #ib__mesa#2 + &009 + "0" + &009 + eaVarPalm5 + &009 + #ib__mesa#6 + &009 + "0";
          |FINSI;
          |SI aSwRefresca = "TIQUET";
               ptcp3 = "101" + &009 + #ib__tick#10 + &009 + #ib__tick#6 + &009 + #ib__mesa#2 + &009 + "0" + &009 + #ib__tick#0 + &009 + #ib__mesa#6 + &009 + "0";
          |FINSI;
||***************************

          i = (A\ ptcp3 % 0) + 1; || incluir el ascii 0 final
          ptcp4 = i;
          |ESPECIFICA "TCP", ptcp;

          ptcp1 = "CIERRA";
          ptcp2 = handle;
          |ESPECIFICA "TCP", ptcp;
     |SINO;
          aMensa = "0000Conexion con caja en [" + aIPCobro + "] no aceptada.";
          |MENAV aMensa;
     |FINSI;
|FPRC;
