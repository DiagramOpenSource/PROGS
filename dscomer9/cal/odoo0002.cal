 OJO  PARA RODACAL SE UNIFICAN la empresa 1 y la 2 en algunos procesos


|FICHEROS;
     odoo0002 #0;
     odoo0001 #1;
     odoo0003 #3;
     odoo0004 #4;

     odoo0006 #6;
     odoo0007 #7;   ||Categorias
     odoo0008 #8;   ||F.Pago
     odoo0009 #9;   ||Relacion F.Pago y Odoo

     agifa019 #19;
     agifa024 #24;
     agifa060 #60;
     agifa091 #91;
     agifa120 #120;
     dsm00225 #225;
     referen@ #1000;

     dsm00003 #30;
     refere1@ #1001;
     refere2@ #1002;
|FIN;

|VARIABLES;
     aCli = "";
     aCodFam = "";
     aCodSub = "";
     nSwRodacal = 0;
     nSwInforpor = 0;
     aCEE = "";
     nEmpresa = 0;
     &Tempo = "";
     aArti = "";
     aSub = "";
     aFam = "";
     aIP = "";
     nBtnCarpeta = 0;
     aSepara = "";
     aRuta = "";
     aAlfa = "";
     aPath = "";
     aCsv = "";
     aMas = "";
     aCarAnt = "";
     aCar = "";
     aNom = "";
     aLon = "";
     aCampo = "";
     aCampos = "";
     aDato = "";
     aTipo = "";
     nHandle = 0;
     nUltimo = 0;
     nCampo = 0;
     nPos = 0;
     i = 0;
     aFin = "";
     aDobleComi = "";
     aValor = "";
     nReg = 0;
     nConta = 0;
     aPathDes = "";
     aFic = "";
     aPlazoPago = "";
     aFP = "";
     aModoPago = "";
     aEmp = "";
     aBass = "";
     aDef = "";
     nVtoFP = 0;
     aNameFP = "";
     aNoteFP = "";
     nAntes = 0;
     aDias = "";
     nDias = 0;
     nContaVto = 0;
     aPais = "";
|FIN;

|PROCESO LeeArticulo;
     |ABRE #19;
     #19#0 = aArti;
     |LEE 000#19=;
     |SI FSalida ! 0; |DDEFECTO #19; |FINSI;
     |CIERRA #19;
|FPRC;

|PROCESO LeeCliente;
     |ABRE #24;
     #24#0 = aCli;
     |LEE 000#24=;
     |SI FSalida ! 0; |DDEFECTO #24; |FINSI;
     |CIERRA #24;
|FPRC;

|PROCESO RelacionFP;
     |ABRE #9;
     #9#0 = aFP;
     |LEE 000#9=;
     |SI FSalida ! 0; |DDEFECTO #9; |FINSI;
     |CIERRA #9;
|FPRC;

|PROCESO RodacalDesFpago;
     aNoteFP = (("000"+#91#3)%-103)+"-"+(("000"+#91#2)%-103)+"-"+ (("000"+#91#4)%-103);
     aNoteFP = (("00"+#91#0)%-102) + "-" + aNoteFP;
     |SI #91#2 = 0; |Y #91#4 = 0;
          nVtoFP = 0;
          aNameFP = #91#1;
     |SINO;
          nVtoFP = 0;
          |PARA i = 6; |SI i [ 59; |HACIENDO i = i + 1;
               |SI #91i = 0; |SAL; |FINSI;
               nVtoFP = nVtoFP + 1;
          |SIGUE;

          aNameFP = "";
          aDias = "";
          |PARA i = 6; |SI i [ 59; |HACIENDO i = i + 1;
               |SI #91i = 0; |SAL; |FINSI;
               |SI i = 6;                    || Primero
                    nDias = #91#6;
               |SINO;
                    nAntes = i - 1;
                    nDias = #91nAntes + #91#4;
               |FINSI;

               |SI aDias = "";
                    aDias = nDias;
               |SINO;
                    aDias = aDias + ", " + nDias;
               |FINSI;
          |SIGUE;
          aNameFP = aDias + " Dias";
     |FINSI;
     |QBF aNameFP;
|FPRC;

|PROCESO RodacalFormaPago;
     |DELINDEX #8;
     |ABRE #8;
     || Unifica las 2 empresas
     |PARA nEmpresa = 1; |SI nEmpresa [ 2; |HACIENDO nEmpresa = nEmpresa + 1;
          aEmp = ("00000" + nEmpresa) % -105;
          |DBASE aAlfa;
          aAlfa = aAlfa + "fich/" + aEmp + "/";
          |PATH_DAT #91 aAlfa;
          |ABRE #91;
          |LEE 000#91p;
          |PARA; |SI FSalida = 0; |HACIENDO;
               |HAZ RodacalDesFpago;
               |PARA i = 0; |SI i [ 74; |HACIENDO i = i + 1;
                    #8i = #91i;
               |SIGUE;
               #8#76 = aNameFP;
               #8#77 = aNoteFP;
               |SI nVtoFP = 0;
                    #8#75 = nVtoFP;
                    #8#78 = "balance";
                    #8#79 = #91#6;
                    #8#80 = 0;
                    |GRABA 000#8n; || sin errores para no duplicar
               |SINO;
                    |SI nVtoFP = 1;
                         #8#75 = nVtoFP;
                         #8#78 = "balance";
                         #8#79 = #91#6;
                         #8#80 = 0;
                         |GRABA 000#8n; || sin errores para no duplicar
                    |SINO;
                         |PARA i = 6; |SI i [ 59; |HACIENDO i = i + 1;
                              |SI #91i = 0; |SAL; |FINSI;
                              nContaVto = i - 5;
                              nAntes = i - 1;
                              |SI i = 6;               ||Primero
                                   #8#75 = nContaVto;
                                   #8#78 = "procent";
                                   #8#79 = #91#6;
                                   #8#80 = 100 / #91#3;
                              |SINO;
                                   |SI nContaVto ! nVtoFP;   ||Siguientes
                                        #8#75 = nContaVto;
                                        #8#78 = "procent";
                                        #8#79 = #91nAntes + #91#4;
                                        #8#80 = 100 / #91#3;
                                   |SINO;            ||Ultimo
                                        #8#75 = 999;
                                        #8#78 = "balance";
                                        #8#79 = #91nAntes + #91#4;
                                        #8#80 = 0;
                                   |FINSI;
                              |FINSI;
                              |GRABA 000#8n; || sin errores para no duplicar
                         |SIGUE;
                    |FINSI;
               |FINSI;
               |LEE 000#91s;
          |SIGUE;
          |CIERRA #91;
     |SIGUE;
     |CIERRA #8;

     aMas = "odoo0008";
     |HAZ Exporta;


     |DBASE aAlfa;
     aEmp = ("00000" + #48#0) % -105;
     aAlfa = aAlfa + "fich/" + aEmp + "/";
     |PATH_DAT #91 aAlfa;
|FPRC;

|PROCESO RodacalCateArti;
     #60#0 = #19#2;
     |LEE 000#60=;
     |SI FSalida ! 0; |ACABA; |FINSI;

     #120#0 = #19#3;
     |LEE 000#120=;
     aArti = #19#0; |QBF aArti;

     aFam = #60#1; |QBF aFam;
     aAlfa = aFam; |HAZ CorrigeBarra; aFam = aAlfa;
     aAlfa = aFam; |HAZ CorrigePuntoComa; aFam = aAlfa;

     aSub = #120#1; |QBF aSub;
     aAlfa = aSub; |HAZ CorrigeBarra; aSub = aAlfa;
     aAlfa = aSub; |HAZ CorrigePuntoComa; aSub = aAlfa;

     |SI aSub = "";
          |SI aFam = "";
               aDato = "Todos";
          |SINO;
               aDato = "Todos/" + aFam;
          |FINSI;
     |SINO;
          aDato = "Todos/" + aFam + "/" + aSub;
     |FINSI;
     aAlfa = aDato; |HAZ CorrigeDobleComillas; aDato = aAlfa;

     #7#0 = aDato;
     |LEE 000#7=;
     |SI FSalida ! 0; |GRABA 020#7n; |FINSI;

     aDato = aArti + aSepara + aDato;

     aDato = aDato + aFin;
     |XGRABA nHandle, aDato;
|FPRC;

|DEFBUCLE RodacalCateArti;
     #19#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, RodacalCateArti;
|FIN;

|PROCESO RodacalCategoria;
     |HAZ CreaTempo; |NOME_DAT #7 Tempo; |DELINDEX #7;
     |ABRE #7;

     || Unifica las 2 empresas para sacar las categorias
     |PARA nEmpresa = 1; |SI nEmpresa [ 2; |HACIENDO nEmpresa = nEmpresa + 1;
          aEmp = ("00000" + nEmpresa) % -105;
          |DBASE aAlfa;
          aAlfa = aAlfa + "fich/" + aEmp + "/";
          |PATH_DAT #60 aAlfa;
          |PATH_DAT #120 aAlfa;
          |PATH_DAT #19 aAlfa;

          aFic = "productos_categorias" + nEmpresa;
          aCsv = aPath + aFic + ".csv";
          |XABRE "BW", aCsv, nHandle;
          |SI FSalida ] 0;
               aAlfa = "articulo" + aSepara+ "categoria" + aFin;
               |XGRABA nHandle, aAlfa;
               |ABRE #60;
               |ABRE #120;
               |BUCLE RodacalCateArti;
               |CIERRA #120;
               |CIERRA #60;
               |XCIERRA nHandle;

               aAlfa = aPathDes + aFic + ".csv";
               |SI aIP = "";
                    |COPIA_FICHERO aCsv, aAlfa;
               |SINO;
                    |ENVIA_FICHERO aCsv, aAlfa;
               |FINSI;
          |FINSI;
     |SIGUE;


     aFic = "categorias";
     aCsv = aPath + aFic + ".csv";
     |XABRE "BW", aCsv, nHandle;
     |SI FSalida ] 0;
          aAlfa = "categoria" + aFin;
          |XGRABA nHandle, aAlfa;
          |LEE 000#7p;
          |PARA; |SI FSalida = 0; |HACIENDO;
               aAlfa = #7#0; |QBF aAlfa;
               aDato = aAlfa + aFin;
               |XGRABA nHandle, aDato;
               |LEE 000#7s;
          |SIGUE;

          |XCIERRA nHandle;

          aAlfa = aPathDes + aFic + ".csv";
          |SI aIP = "";
               |COPIA_FICHERO aCsv, aAlfa;
          |SINO;
               |ENVIA_FICHERO aCsv, aAlfa;
          |FINSI;
     |FINSI;

     |CIERRA #7;
|FPRC;

|PROCESO InforporDirUsu;
     |DDEF aDef;
     aAlfa = aDef + "infom003";
     |CARGA_DEF aAlfa, refere1@;
     |SI FSalida = 0;
          aAlfa = aDef + "infom008";
          |CARGA_DEF aAlfa, refere2@;
          |SI FSalida = 0;
               |DELINDEX #refere2@;
               |ABRE #30;
               |ABRE #refere1@;
               |ABRE #refere2@;
               |LEE 000#refere1@.p;
               |PARA; |SI FSalida = 0; |HACIENDO;
                    #30#0 = #refere1@#2;
                    #30#1 = 1;
                    |LEE 000#30];
                    |PARA; |SI FSalida = 0; |Y #30#0 = #refere1@#2; |HACIENDO;
                         #refere2@#0 = #refere1@#4;
                         #refere2@#1 = #refere1@#2;
                         #refere2@#2 = #30#1;
                         |GRABA 000#refere2@.n;   || sin errores, poque se repiten y no deberia
                         |LEE 000#30s;
                    |SIGUE;
                    |LEE 000#refere1@.s;
               |SIGUE;
               |CIERRA #refere1@;
               |CIERRA #refere2@;
               |CIERRA #30;
               |DESCARGA_DEF #refere2@;
          |FINSI;
          |DESCARGA_DEF #refere1@;
     |FINSI;

     aMas = "infom008"; |QBF aMas;
     aFic = "usu_dir";
     |HAZ Exporta;
|FPRC;

|PROCESO Pinta;
     nReg = nReg + 1;
     aAlfa = aMas + "-" +  nReg + "                      ";
     |PINTA 1503, aAlfa;
|FPRC;

|PROCESO CateArti;
     #60#0 = #19#2;
     |LEE 000#60=;
     |SI FSalida ! 0; |ACABA; |FINSI;

     #120#0 = #19#3;
     |LEE 000#120=;

     aCodFam = #19#2; |QBF aCodFam;
     aCodSub = #19#3; |QBF aCodSub;

     aArti = #19#0; |QBF aArti;
     aFam = #60#1; |QBF aFam;
     aSub = #120#1; |QBF aSub;

     |SI aFam = ""; aFam = aCodFam; |FINSI;
     |SI aSub = ""; aSub = aCodSub; |FINSI;

     aAlfa = aFam; |HAZ CorrigeBarra; aFam = aAlfa;
     aAlfa = aFam; |HAZ CorrigePuntoComa; aFam = aAlfa;

     aAlfa = aSub; |HAZ CorrigeBarra; aSub = aAlfa;
     aAlfa = aSub; |HAZ CorrigePuntoComa; aSub = aAlfa;

     |SI aCodFam = "";
          aDato = "Todos";
     |SINO;
          |SI aCodSub = "";
               |SI aCodFam = "";
                    aDato = "Todos";
               |SINO;
                    aDato = "Todos/" + aFam;
               |FINSI;
          |SINO;
               aDato = "Todos/" + aFam + "/" + aSub;
          |FINSI;
     |FINSI;
     aAlfa = aDato; |HAZ CorrigeDobleComillas; aDato = aAlfa;

     #7#0 = aDato;
     |LEE 000#7=;
     |SI FSalida ! 0; |GRABA 020#7n; |FINSI;

     aDato = aArti + aSepara + aDato;

     aDato = aDato + aFin;
     |XGRABA nHandle, aDato;
|FPRC;

|DEFBUCLE CateArti;
     #19#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, CateArti;
|FIN;

|PROCESO Categorias;
     |HAZ CreaTempo; |NOME_DAT #7 Tempo; |DELINDEX #7;
     |ABRE #7;

     aFic = "productos_categorias" + nEmpresa;
     aCsv = aPath + aFic + ".csv";
     |XABRE "BW", aCsv, nHandle;
     |SI FSalida ] 0;
          aAlfa = "articulo" + aSepara+ "categoria" + aFin;
          |XGRABA nHandle, aAlfa;
          |ABRE #60;
          |ABRE #120;
          |BUCLE CateArti;
          |CIERRA #120;
          |CIERRA #60;
          |XCIERRA nHandle;

          aAlfa = aPathDes + aFic + ".csv";
          |SI aIP = "";
               |COPIA_FICHERO aCsv, aAlfa;
          |SINO;
               |ENVIA_FICHERO aCsv, aAlfa;
          |FINSI;
     |FINSI;


     aFic = "categorias";
     aCsv = aPath + aFic + ".csv";
     |XABRE "BW", aCsv, nHandle;
     |SI FSalida ] 0;
          aAlfa = "categoria" + aFin;
          |XGRABA nHandle, aAlfa;
          |LEE 000#7p;
          |PARA; |SI FSalida = 0; |HACIENDO;
               aAlfa = #7#0; |QBF aAlfa;
               aDato = aAlfa + aFin;
               |XGRABA nHandle, aDato;
               |LEE 000#7s;
          |SIGUE;

          |XCIERRA nHandle;

          aAlfa = aPathDes + aFic + ".csv";
          |SI aIP = "";
               |COPIA_FICHERO aCsv, aAlfa;
          |SINO;
               |ENVIA_FICHERO aCsv, aAlfa;
          |FINSI;
     |FINSI;

     |CIERRA #7;
|FPRC;

|PROCESO PlazoPago;
     |ABRE #91;
     #91#0 = aFP;
     |LEE 000#91=;
     |SI FSalida ! 0; |DDEFECTO #91; |FINSI;
     |CIERRA #91;
     |HAZ RodacalDesFpago;
     aPlazoPago = aNameFP;
|FPRC;

|PROCESO PerteneceCEE;
     |ABRE #225;
     #225#0 = aPais;
     |LEE 000#225=;
     |SI FSalida ! 0; |DDEFECTO #225; |FINSI;
     |CIERRA #225;
     aCEE = #225#2;
|FPRC;

|PROCESO ModoPago;
     |ABRE #91;
     #91#0 = aFP;
     |LEE 000#91=;
     |SI FSalida ! 0; |DDEFECTO #91; |FINSI;
     |CIERRA #91;
     aModoPago = "C" + (("00" + #91#69) % -102);
|FPRC;

|PROCESO RmDir;
     aAlfa = ""; |DIRECTORIO aAlfa; |QBF aAlfa;
     aAlfa = aAlfa + "bin/agirm -r " + aPath;
     |SYSTEM aAlfa;
|FPRC;

|PROCESO CorrigePuntoComa;
     aDato = "";
     aLon = aAlfa % A0;
     |PARA i = 0; |SI i [ aLon; |HACIENDO i = i + 1;
          nPos = (i + 1) * 100 + 1;
          aCar = aAlfa % nPos;
          |SI aCar = ";"; aCar = ","; |FINSI;
          aDato = aDato + aCar;
     |SIGUE;
     aAlfa = aDato;
|FPRC;

|PROCESO CorrigeBarra;
     aDato = "";
     aLon = aAlfa % A0;
     |PARA i = 0; |SI i [ aLon; |HACIENDO i = i + 1;
          nPos = (i + 1) * 100 + 1;
          aCar = aAlfa % nPos;
          |SI aCar = "/"; aCar = "|"; |FINSI;
          aDato = aDato + aCar;
     |SIGUE;
     aAlfa = aDato;
|FPRC;

|PROCESO CorrigeDobleComillas;
     aDato = "";
     aLon = aAlfa % A0;
     |PARA i = 0; |SI i [ aLon; |HACIENDO i = i + 1;
          nPos = (i + 1) * 100 + 1;
          aCar = aAlfa % nPos;
          |SI aCar = aDobleComi; aCar = aDobleComi + aDobleComi; |FINSI;
          aDato = aDato + aCar;
     |SIGUE;
     aAlfa = aDato;
|FPRC;

|PROCESO Nom;
     aCarAnt = "";
     aAlfa = "";
     aNom = aNom < "";
     aLon = aNom % A0;
     |PARA i = 1; |SI i [ aLon; |HACIENDO i = i + 1;
          nPos = i * 100 + 1;
          aCar = aNom % nPos;
          |SI aCar = "¤"; aCar = "ny"; |FINSI;
          |SI aCar < "0";
               aCar = "_";
          |SINO;
               |SI aCar > "9"; |Y aCar < "a";
                    aCar = "_";
               |SINO;
                    |SI aCar > "z"; aCar = "_"; |FINSI;
               |FINSI;
          |FINSI;
          |SI aCarAnt ! "_"; |O aCar ! "_";
               aAlfa = aAlfa + aCar;
          |FINSI;
          aCarAnt = aCar;
     |SIGUE;
     aNom = aAlfa;

     ||quita subguion final
     aLon = aAlfa % A0;
     nPos = aLon * 100 + 1;
     aCar = aAlfa % nPos;
     |SI aCar = "_";
          nPos = aLon + 99;
          aNom = aAlfa % nPos;
     |SINO;
          aNom = aAlfa;
     |FINSI;

     || quita el subguion inicial
     aLon = aNom % A0;
     |PARA i = 1; |SI i [ aLon; |HACIENDO i = i + 1;
          nPos = i * 100 + 1;
          aAlfa = aNom % nPos;
          |SI aAlfa = "_";
               aNom = aNom - "_";
          |SINO;
               |SAL;
          |FINSI;
     |SIGUE;

     || Si comiemsa por un numero le a¤ado una c
     aAlfa = aNom % 101;
     |SI aAlfa ] "0"; |Y aAlfa [ "9";
          aNom = "c" + aNom;
     |FINSI;

     ||SI aNom = ""; aNom = "x"; |FINSI;
|FPRC;

|PROCESO Campos;
     aAlfa = Usuario; |NOME_DAT #3 aAlfa; |DELINDEX #3;
     |ABRE #3;

     aAlfa = "|NC";
     aCampos = #referen@#aAlfa#;
     nUltimo = aCampos - 1;
     |PARA nCampo = 0; |SI nCampo < aCampos; |HACIENDO nCampo = nCampo + 1;
          aCampo = ("000" + nCampo) % -103;
          aAlfa = "|C" + aCampo + "NOMBRE"; aNom = #referen@#aAlfa#;
          |HAZ Nom;

          #3#0 = aNom;
          |LEE 000#3=;
          |SI FSalida = 0;
               nConta = 1;
               |PARA; |SI; |HACIENDO;
                    aAlfa = aNom + nConta;
                    #3#0 = aAlfa;
                    |GRABA 000#3n;
                    |SI FSalida = 0; |SAL; |FINSI;
                    nConta = nConta + 1;
               |SIGUE;
               aNom = aAlfa;
          |SINO;
               |GRABA 020#3n;
          |FINSI;

          |SI nCampo ! nUltimo;
               aNom = aNom + aSepara;
          |SINO;
               |SI aMas = "agifa024"; |O aMas = "agifa112";
                    aNom = aNom + aSepara + "modo_pago";
                    aNom = aNom + aSepara + "plazo_pago";
                    aNom = aNom + aSepara + "cee";
               |FINSI;
               |SI aMas = "agifa023";
                    aNom = aNom + aSepara + "tarifa_cliente";
                    aNom = aNom + aSepara + "tarifa_arti1";
                    aNom = aNom + aSepara + "tarifa_arti2";
                    aNom = aNom + aSepara + "tarifa_arti3";
                    aNom = aNom + aSepara + "tarifa_arti4";
                    aNom = aNom + aSepara + "tarifa_arti5";
                    aNom = aNom + aSepara + "tarifa_arti6";
               |FINSI;
               aNom = aNom + aFin;
          |FINSI;
          |XGRABA nHandle, aNom;
     |SIGUE;

     |CIERRA #3;
|FPRC;

|PROCESO Datos;
     nReg = 0;
     |LEE 000#referen@.p;
     |PARA; |SI FSalida = 0; |HACIENDO;

          ||... Excepciones...
          |SI aMas = "agifa024"; |O aMas = "agifa112";
               aAlfa = #referen@#0#; |QBF aAlfa;
               |SI aAlfa = ""; FSalida = 1; |FINSI;
          |FINSI;
          |SI aMas = "dsm00003"; ||no exporta la dir 1
               |SI #referen@#1 = 1; FSalida = 1; |FINSI;
          |FINSI;
          ||....

          |SI FSalida = 0;
               aAlfa = "|NC";
               aCampos = #referen@#aAlfa#;
               nUltimo = aCampos - 1;
               |PARA nCampo = 0; |SI nCampo < aCampos; |HACIENDO nCampo = nCampo + 1;
                    aCampo = ("000" + nCampo) % -103;
                    aAlfa = "|C" + aCampo + "TIPO"; aTipo = #referen@#aAlfa#;

                    aValor = #referen@#nCampo#; |QBF aValor;
                    aAlfa = aValor; |HAZ CorrigeDobleComillas; aValor = aAlfa;
                    |SI aTipo = "A";
                         aValor = aDobleComi + aValor + aDobleComi;
                    |FINSI;

                    |SI nCampo ! nUltimo;
                         |SI aMas = "agifa024"; |Y nCampo = 20;
                              aFP = #referen@#20; |HAZ PlazoPago;
                              aValor = aPlazoPago;
                         |FINSI;
                         |SI aMas = "agifa112"; |Y nCampo = 17;
                              aFP = #referen@#17; |HAZ PlazoPago;
                              aValor = aPlazoPago;
                         |FINSI;
                         |SI aMas = "agifa104"; |Y nCampo = 9;
                              aFP = #referen@#8; |HAZ PlazoPago;
                              aValor = aPlazoPago;
                         |FINSI;
                         |SI aMas = "agifa010"; |Y nCampo = 8;
                              aFP = #referen@#8; |HAZ PlazoPago;
                              aValor = aPlazoPago;
                         |FINSI;
                         |SI aMas = "agifa134"; |Y nCampo = 11;
                              aFP = #referen@#11; |HAZ PlazoPago;
                              aValor = aPlazoPago;
                         |FINSI;
                         |SI aMas = "agifa081"; |Y nCampo = 9;
                              aFP = #referen@#9; |HAZ PlazoPago;
                              aValor = aPlazoPago;
                         |FINSI;
                         |SI aMas = "agifa077"; |Y nCampo = 8;
                              aFP = #referen@#9; |HAZ PlazoPago;
                              aValor = aPlazoPago;
                         |FINSI;
                         |SI aMas = "agifa078"; |Y nCampo = 11;
                              aFP = #referen@#9; |HAZ PlazoPago;
                              aValor = aPlazoPago;
                         |FINSI;
                         aValor = aValor + aSepara;
                    |SINO;
                         |SI aMas = "agifa024";
                              aFP = #referen@#20; |HAZ RelacionFP;
                              aPais = #referen@#205; |HAZ PerteneceCEE;

                              aValor = aValor + aSepara + #9#2;
                              aValor = aValor + aSepara + #9#3;
                              aValor = aValor + aSepara + aCEE;
                         |FINSI;
                         |SI aMas = "agifa112";
                              aFP = #referen@#17; |HAZ RelacionFP;
                              aPais = #referen@#89; |HAZ PerteneceCEE;

                              aValor = aValor + aSepara + #9#2;
                              aValor = aValor + aSepara + #9#3;
                              aValor = aValor + aSepara + aCEE;
                         |FINSI;
                         |SI aMas = "agifa023";
                              aCli = #referen@#0; |HAZ LeeCliente;
                              aArti = #referen@#1; |HAZ LeeArticulo;
                              aValor = aValor + aSepara + #24#39;
                              aValor = aValor + aSepara + #19#18;
                              aValor = aValor + aSepara + #19#19;
                              aValor = aValor + aSepara + #19#20;
                              aValor = aValor + aSepara + #19#147;
                              aValor = aValor + aSepara + #19#148;
                              aValor = aValor + aSepara + #19#149;
                         |FINSI;
                         aValor = aValor + aFin;
                    |FINSI;
                    |XGRABA nHandle, aValor;
               |SIGUE;
               |HAZ Pinta;
          |FINSI;
          |LEE 000#referen@.s;
     |SIGUE;
|FPRC;

|PROCESO Exporta;
     |DDEF aAlfa;
     aAlfa = aAlfa + aMas;
     |CARGA_DEF aAlfa, referen@;
     |SI FSalida = 0;
          |ABRE #referen@;
          aCsv = aPath + aFic + ".csv";
          |XABRE "BW", aCsv, nHandle;
          |SI FSalida ] 0;
               |HAZ Campos;
               |HAZ Datos;
               |XCIERRA nHandle;

               aAlfa = aPathDes + aFic + ".csv";
               |SI aIP = "";
                    |COPIA_FICHERO aCsv, aAlfa;
               |SINO;
                    |ENVIA_FICHERO aCsv, aAlfa;
               |FINSI;
          |FINSI;
          |CIERRA #referen@;
          |DESCARGA_DEF #referen@;
     |SINO;
          aAlfa = "0000Error al cargar:" + aMas;
          |MENAV aAlfa;
     |FINSI;
|FPRC;

|PROCESO FormaPago;
     |DELINDEX #8;
     |ABRE #8;

     |ABRE #91;
     |LEE 000#91p;
     |PARA; |SI FSalida = 0; |HACIENDO;
          |HAZ RodacalDesFpago;
          |PARA i = 0; |SI i [ 74; |HACIENDO i = i + 1;
               #8i = #91i;
          |SIGUE;
          #8#76 = aNameFP;
          #8#77 = aNoteFP;
          |SI nVtoFP = 0;
               #8#75 = nVtoFP;
               #8#78 = "balance";
               #8#79 = #91#6;
               #8#80 = 0;
               |GRABA 000#8n; || sin errores para no duplicar
          |SINO;
               |SI nVtoFP = 1;
                    #8#75 = nVtoFP;
                    #8#78 = "balance";
                    #8#79 = #91#6;
                    #8#80 = 0;
                    |GRABA 000#8n; || sin errores para no duplicar
               |SINO;
                    |PARA i = 6; |SI i [ 59; |HACIENDO i = i + 1;
                         |SI #91i = 0; |SAL; |FINSI;
                         nContaVto = i - 5;
                         nAntes = i - 1;
                         |SI i = 6;               ||Primero
                              #8#75 = nContaVto;
                              #8#78 = "procent";
                              #8#79 = #91#6;
                              #8#80 = 100 / #91#3;
                         |SINO;
                              |SI nContaVto ! nVtoFP;   ||Siguientes
                                   #8#75 = nContaVto;
                                   #8#78 = "procent";
                                   #8#79 = #91nAntes + #91#4;
                                   #8#80 = 100 / #91#3;
                              |SINO;            ||Ultimo
                                   #8#75 = 999;
                                   #8#78 = "balance";
                                   #8#79 = #91nAntes + #91#4;
                                   #8#80 = 0;
                              |FINSI;
                         |FINSI;
                         |GRABA 000#8n; || sin errores para no duplicar
                    |SIGUE;
               |FINSI;
          |FINSI;
          |LEE 000#91s;
     |SIGUE;

     |CIERRA #91;
     |CIERRA #8;

     aMas = "odoo0008";
     |HAZ Exporta;
|FPRC;

|PROCESO T3; |TIPO 3;
     |SI #0#0 ! "SI"; |ACABA; |FINSI;

     |HAZ EsInforpor; nSwInforpor = FSalida;
     |HAZ EsRodacal; nSwRodacal = FSalida;

     aIP = ""; |IP_REMOTO aIP;

     |ABRE #1; |LEE 000#1p; |CIERRA #1;

     aDobleComi = &34;
     aSepara = ";";
     aFin = &13 + &10;

     |DFICE aAlfa;
     aPath = aAlfa + Usuario + "/odoo/";
     |MKDIR aPath;

     aPathDes = #1#1; |QBF aPathDes;
     |SI aPathDes = "";
          |MENAV "0000Parámetros no configurados...";
          |ACABA;
     |FINSI;

     aAlfa = aPathDes % -101;
     |SI aAlfa ! "/"; |Y aAlfa ! "\"; aPathDes = aPathDes + "/"; |FINSI;
     |RMKDIR aPathDes;

     |ABRE #4;
     |LEE 000#4p;
     |PARA; |SI FSalida = 0; |HACIENDO;
          |SI #4#2 = "*";
               aMas = #4#0; |QBF aMas;
               aFic = aMas;
               |SI aFic = "agifa091";
                    |SI nSwRodacal = 0;
                         |HAZ RodacalFormaPago;
                    |SINO;
                         |HAZ FormaPago;
                    |FINSI;
               |SINO;
                    |HAZ Exporta;
               |FINSI;
               |SI aFic = "agifa019";
                    |HAZ Categorias;
               |FINSI;
          |FINSI;
          |LEE 000#4s;
     |SIGUE;

     |SI nSwInforpor = 0;
          |HAZ InforporDirUsu;
     |FINSI;
     |SI nSwRodacal = 0;
          |HAZ RodacalCategoria;
     |FINSI;

     |HAZ RmDir;
|FPRC;
