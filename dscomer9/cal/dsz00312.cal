     Copiado del agifa068

|FICHEROS;
     dsz00312 #0;
     agifa029 #1;
     agifa067 #4;
     dsm00024 #24;  ||Partida
     agifa019 #53;  ||Articulos
     agifa017 #54;  ||Almacenes
     agifa081 #81;
     dsm00004 #904;
     dsm00005 #905
     dsm00065 #965;
|FIN;

|CAMPOS;
     #53#180 PARTI;
     #53#179 DOBLE;
     #53#204 MULTI;
     #53#16  art_prec;  #54#43  alm_prec;
     #53#183 art_prec2; #54#45  alm_prec2;
     #53#9   art_pcm;   #54#69  alm_pcm;
     #24#18  par_prec2; #24#19  par_prec;
|FIN;

|VARIABLES;
     nCanti2 = 0;
     nNume = 0;
     nCanV = 0;
     articul = "";
     almacen = 0;
     canti   = 0;
     nSwSanchez = 0;
     aParti = "";
     nHayArti = 0;
     nHayAlma = 0;
     nHayParti = 0;
     nLinV = 0;
|FIN;

|PROCESO BorraParti;
     par_prec = 0;
     par_prec2 = 0;
     |GRABA 020#24a;
|FPRC;

|DEFBUCLE dsm00024;
     #24#1;
     ;
     #0#0, 00001, "                              ";
     #0#1, 99999, "zzzzzzzzzzzzzzzzzzzzzzzzzzzzzz";
     be;
     NULL, BorraParti;
|FIN;

|PROCESO borraarti;
     art_prec = 0;
     art_prec2= 0;
     |GRABA 020#53a;
|FPRC;

|DEFBUCLE agifa019;
     #53#1;
     ;
     #0#0;
     #0#1;
     be;
     NULL , borraarti;
|FIN;

|PROCESO borraalma;
     alm_prec = 0;
     alm_prec2= 0;
     |GRABA 020#54a;
|FPRC;

|DEFBUCLE agifa017;
     #54#1;
     ;
     #0#0 , 00001;
     #0#1 , 99999;
     be;
     NULL , borraalma;
|FIN;

|PROCESO BorraCompo;
     #904#5 = 0;
     |GRABA 020#904a;
     |LIBERA #904;
|FPRC;

|DEFBUCLE dsm00004;
     #904#1;
     ;
     #0#0, 00000, "      ";
     #0#1, 99999, "zzzzzz";
     be;
     NULL, BorraCompo;
|FIN;

|PROCESO LeeParti;
     |SI PARTI = "N"; |ACABA; |FINSI;

     #24#0 = articul;
     #24#1 = almacen;
     #24#2 = aParti;
     |LEE 101#24=;
     |SI FSalida ! 0; |Y #0#2 = "S";
          |DDEFECTO #24;
          #24#0 = articul;
          #24#1 = almacen;
          #24#2 = aParti;
          |GRABA 020#24b;
     |FINSI;
     nHayParti = FSalida;
|FPRC;

|PROCESO LeeAlma;
     #54#0 = articul;
     #54#1 = almacen;
     |LEE 101#54=;
     |SI FSalida ! 0; |Y #0#2 = "S";
          |DDEFECTO #1;
          |ABRE #1;
          #1#0 = almacen;
          |LEE 000#1=;
          |SI FSalida ! 0;
               |GRABA 020#1n;
          |FINSI;
          |CIERRA #1;

          |DDEFECTO #54;
          #54#0 = articul;
          #54#1 = almacen;
          #54#40 = #53#1;
          #54#41 = #1#1;
          |GRABA 020#54b;
     |FINSI;
     nHayAlma = FSalida;
|FPRC;

|PROCESO LeeArti;
     #53#0 = articul;
     |LEE 101#53=;
     |SI FSalida ! 0; |Y #0#2 = "S";
         |DDEFECTO #53;
         #53#0 = articul;
         |GRABA 020#53b;
     |FINSI;
     nHayArti = FSalida;
|FPRC;

|PROCESO CompoPCompras;
     |DDEFECTO #904;
     #904#0 = #4#2;
     #904#1 = #4#3;
     #904#2 = #905#4;
     |LEE 101#904=;
     |SI FSalida ! 0; |GRABA 020#904b; |FINSI;

     nCanV  = nCanV + (#905#6 - #905#12);

     #904#5  = #904#5 + (#905#6 - #905#12);

     nNume = #904#12 - #904#6;
     |SI nNume > 0;
         #904#24 = nNume * art_pcm;
     |SINO;
         #904#24 = 0;
     |FINSI;

     |GRABA 020#904a;
     |LIBERA #904;
|FPRC;

|DEFBUCLE dsm00005PC;
     #905#1;
     ;
     "PC", #4#28, #4#0, nLinV, "      ";
     "PC", #4#28, #4#0, nLinV, "zzzzzz";
     ;
     NULL, CompoPCompras;
|FIN;

|PROCESO dsm00065;
     articul = #965#3;
     almacen = #965#4;
     canti   = #4#5 - #4#38;
     canti = canti * #965#5;

     |HAZ LeeArti;
     |HAZ LeeAlma;

     art_prec = art_prec + canti;

     alm_prec = alm_prec + canti;

     |HAZ ValoraStock;

     |SI nSwSanchez = 0;
         |SI #54#1 ! 0; |GRABA 020#53a; |FINSI;
     |SINO;
         |SI nHayArti = 0; |GRABA 020#53a; |FINSI;
     |FINSI;

     |SI nHayAlma = 0; |GRABA 020#54a; |FINSI;
     |LIBERA #53;
     |LIBERA #54;
|FPRC;

|DEFBUCLE dsm00065;
     #965#1;
     3;
     #4#28, #4#0, #4#1, 001, #0#0;
     #4#28, #4#0, #4#1, 999, #0#1;
     ;
     NULL, dsm00065;
|FIN;

|PROCESO pedicom;
     |SI #4#2 ] #0#0; |Y #4#2 [ #0#1;
          nCanV = 0;
          |ABRE #904;
          nLinV = #4#1;
          |BUCLE dsm00005PC;
          |CIERRA #904;

          articul = #4#2;
          almacen = #4#3;
          aParti = #4#42;
          canti   = #4#5 - #4#38;
          nCanti2 = #4#41 - #4#46;

          |HAZ LeeArti;
          |HAZ LeeAlma;
          |HAZ LeeParti;
     ||     |SI MULTI = "S"; canti = nCanti2; nCanti2 = 0; |FINSI;

          art_prec = art_prec + canti;
          art_prec2 = art_prec2 + nCanti2;

          alm_prec = alm_prec + canti;
          alm_prec2 = alm_prec2 + nCanti2;

          par_prec = par_prec + canti;
          par_prec2 = par_prec2 + nCanti2;

          |HAZ ValoraStock;

          |SI nSwSanchez = 0;
              |SI #54#1 ! 0; |GRABA 020#53a; |FINSI;
          |SINO;
              |SI nHayArti = 0; |GRABA 020#53a; |FINSI;
          |FINSI;

          |SI nHayAlma = 0; |GRABA 020#54a; |FINSI;
          |SI nHayParti = 0; |Y PARTI = "S"; |GRABA 020#24a; |FINSI;
          |LIBERA #24;
          |LIBERA #53;
          |LIBERA #54;

          |BUCLE dsm00065;
     |FINSI;
|FPRC;
/*
|DEFBUCLE pedicom;  || No se puede  acotar por la clave del articulo
     #4#1;          || por los kits o escandallos
     ;
     INICIO;
     FINAL;
     e;
     NULL,pedicom;
|FIN;
*/

|DEFBUCLE pedicom;
     #81#1;
     1;
     "     ", 000001, #0#3;
     "zzzzz", 999999, #0#4;
     ;
     NULL, NULL, pedicom;
|FIN;

|PROCESO T3; |TIPO 3;
     |HAZ EsSanchez; nSwSanchez = FSalida;

     |BUCLE agifa019;
     |BUCLE agifa017;
     |BUCLE dsm00024;
     |BUCLE dsm00004;

     |ABRE #53;
     |ABRE #54;
     |BUCLE pedicom;
     |CIERRA #53;
     |CIERRA #54;
|FPRC;
