
     Copia exacta al agit0002, solo cambia la pantalla y el pinta del #0#13

|FICHEROS;
     agit0003 #0;   || Este def.
     agifa073 #1;   || Pedidos(L).
     agifa104 #2;   || Pedidos(C).
     agifa019 #19;
     agifa024 #24;
     dsw00043 #43,MANTE;
     referen@ #10;
     agifa091 #91;
     agifa025 #25;
     dsz00115 #115,MANTE;
     agifa142 #142;
     agit0000;
|FIN;

|VARIABLES;
     nUniMarcadas = 0;
     &eaFicPdte = "";
     nSwEvaristo = 0;
     &eaArticulo = "";
     nSwBerria = 0;
     nHay = 0;
     aArti = "";
     aSer = "";
     nPed = 0;
     nLin = 0;
     aAlfa = "";
 {-1}aMenu  = "";
     aMenu1 = "0000";
     aMenu2 = "1";
     aMenu3 = "";
     aMenu4 = "CAT";
     aMenu5 = " Cancelar ";
     aMenu6 = " Aceptar ";
     aMenu7 = " Aceptar Todos ";
     nSwModi   = 0;
     nSw       = 0;
     &Tempo    = "";
     aTempo0   = "";
     &enTipo   = 0;
     &cliente  = "";
     &ser_ped_inf  = ""; || Serie del pedido que viene de
     &ser_ped_sup  = ""; || la adjudicacion
     &serie    = "";
     &pedido   = 0;
     &lineap   = 0;
     &codigo   = "";
     &descrip  = "";
     &unid     = 0;
     &enDesdeX = 0;

     dcliente  = "";
     hcliente  = "";
     fichero   = "";
     nPendiente = 0;
     nPendiente2 = 0;
     aServir   = "";
     aSerAnt   = "";
     nPedAnt   = 0;
     aDef      = "";
     nModo     = 0;
     nSal      = 0;
     nDPedido = 0;
     nHPedido = 0;
     nSali = 0;
     nTecla = 0;
     nError = 0;
     aCli = "";
     nDto1 = 0;
     nDto2 = 0;
     nDto3 = 0;
     aRep = "";
     aFp = "";
     aNif = "";
|FIN;

|PROCESO Tipo_12; |TIPO 12;
     nSali = FSalida;
|FPRC;

|PROCESO CheqPedido;
     nError = 0;

     |ABRE #2;
     #2#25 = #0#0;
     #2#0 = #0#1;
     |LEE 020#2=;

     aCli = #2#4;
     nDto1 = #2#5;
     nDto2 = #2#6;
     nDto3 = #2#17;
     aRep = #2#7;
     aFp = #2#9;
     aNif = #2#64;

     |SALVA_FICHA #0;
     |SELECT #2#0;
     #0#12 = "*";
     #0#0 = "";
     #0#1 = "";
     #0#2 = "";
     |LEE 000#0];
     |SI FSalida = 0; |Y #0#12 = "*";
          #2#25 = #0#0;
          #2#0 = #0#1;
          |LEE 020#2=;
          |SI aCli ! #2#4; |O nDto1 ! #2#5; |O nDto2 ! #2#6;
                    |O aRep ! #2#7; |O aFp ! #2#9; |O nDto3 ! #2#17;
                    |O aNif ! #2#64;
               nError = 1;
               |MENAV "0000Los datos del pedido difieren del ya marcado...";
          |FINSI;
     |FINSI;
     |REPON_FICHA #0;
     |SELECT #1#0;
     |LEE 000#0=;

     |CIERRA #2;
|FPRC;

|PROCESO ModiPedi;
     #2#25 = #0#0;
     #2#0 = #0#1;
     |LEE 101#2=;
     |SI FSalida = 0;
          |SI nSwEvaristo ! 0;
               #2#7 = #43#2;
          |FINSI;
          #2#9 = #43#0
          #2#5 = #43#4;            || especial
          #2#17 = #43#6;           || acumulado
          #2#6 = #43#5;            || p.p.
          |HAZ CalCabAgifa104;
     |FINSI;
|FPRC;

|PROCESO ModiPediTodos;
     |SI aSerAnt ! #10#0; |O nPedAnt ! #10#1;
          aSerAnt = #10#0;
          nPedAnt = #10#1;
          #2#25 = #10#0;
          #2#0 = #10#1;
          |LEE 101#2=;
          |SI FSalida = 0;
               |SI nSwEvaristo ! 0;
                    #2#7 = #43#2;
               |FINSI;
               #2#9 = #43#0
               #2#5 = #43#4;       || especial
               #2#17 = #43#6;      || acumulado
               #2#6 = #43#5;       || p.p.
               |HAZ CalCabAgifa104;
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE ModiPediTodos;
     #10#1003;
     ;
     "     ", 000001, 001;
     "zzzzz", 999999, 999;
     ;
     NULL, ModiPediTodos;
|FIN;

|PROCESO Condiciones;
     |ABRE #24;
     #24#0 = cliente;
     |LEE 000#24=;
     |CIERRA #24;

     |ABRE #91;
     #91#0 = #24#20;
     |LEE 000#91=;
     |CIERRA #91;

     |ABRE #25;
     #25#0 = #24#25;
     |LEE 000#25=;
     |CIERRA #25;

     |SI nSw = 0;
          nSw = 1;
          #43#0 = #24#20;
          #43#1 = #91#1;
          #43#2 = #24#25;
          #43#3 = #25#1;
          #43#4 = #24#37;          || dto.especial
          #43#5 = #24#38;          || dto.p.p.
          #43#6 = #24#156;         || dto.acumul.
     |FINSI;


     |BLANCO 1213 2368;
     |PINPA #0#43;
     |PINDA #0#43;

     |SI nSwEvaristo = 0;
          |C_V #43#2, 1, "N";
          |C_V #43#3, 1, "N";
          |PINTA 1715, "³                                                  ³";
     |FINSI;

     |ENDATOS #2#43;
|FPRC;

|PROCESO T12; |TIPO 12;
     |MENU aMenu;

     nSwModi = FSalida;
     |SI nSwModi > 1;
          |DDEF aDef;
          aDef = aDef + "agit0001";
          |CARGA_DEF aDef, referen@;
          |SI FSalida = 0;
               |NOME_DAT #10 aTempo0;
               aSerAnt   = "";
               nPedAnt   = 0;
               |ABRE #2;
               |SI nSwModi = 3;
                    |BUCLE ModiPediTodos;
               |SINO;
                    |HAZ ModiPedi;
               |FINSI;
               |CIERRA #2;
               |DESCARGA_DEF #referen@;
          |SINO;
               aDef = "0000Error al cargar:" + aDef;
               |MENAV aDef;
          |FINSI;
     |FINSI;
|FPRC;
ÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ
|PROCESO agifa073;
     #19#0 = #1#2;
     |LEE 000#19=;
     |SI FSalida ! 0; |DDEFECTO #19; |FINSI;

     nPendiente = #1#5 - #1#43;
     nPendiente2 = #1#44 - #1#49;
     aServir = "N";
     |SI #19#79 = "N"; |O #142#117 = "N";    || sin doble
          |SI nPendiente ! 0;
               aServir = "S";
          |FINSI;
     |SINO;
          |SI #142#224 = "B";
               |SI nPendiente ! 0; |O nPendiente2 ! 0;
                    aServir = "S";
               |FINSI;
          |SINO;
               |SI nPendiente ! 0; |Y nPendiente2 ! 0;
                    aServir = "S";
               |FINSI;
          |FINSI;
     |FINSI;

     |SI aServir = "N"; |ACABA; |FINSI;

     |SI nSwBerria = 0;
          eaArticulo = #1#2;
          |HAZ EsKit;
          |SI FSalida = 0; |ACABA; |FINSI;
     |FINSI;

     #2#25 = #1#28;
     #2#0  = #1#0;
     |LEE 000#2=;   || LEE PEDIDOS(C).
     |SI FSalida = 0;
          #0#0 = #1#28;         || SERIE.
          #0#1 = #1#0;          || NUMERO.
          #0#2 = #1#1;          || LINEA.
          #0#3 = #2#1;          || FECHA PEDIDO.
          #0#4 = #1#2;          || ARTICULO.
          #0#5 = #1#4;          || DESCRIPCION.
          #0#6 = #1#5;          || UND.PEDIDAS.
          #0#7 = #1#43;         || UND.SERVIDAS.
          #0#8 = #1#5 - #1#43;  || UND.PENDIENTES.
          #0#9 = #2#4;
          #0#10 = #2#8;
          |SI aSerAnt ! #0#0; |O nPedAnt ! #0#1;
               #0#11 = #2#45;
          |SINO;
               #0#11 = 0;
               |SI enDesdeX = 1;  |ACABA;  |FINSI;
          |FINSI;
          |GRABA 021#0n;
          aSerAnt = #0#0;
          nPedAnt = #0#1;
     |FINSI;
|FPRC;

|DEFBUCLE agifa073;
     #agifa073#1;
     ;
     #agifa104#25, #agifa104#0, 001;
     #agifa104#25, #agifa104#0, 999;
     ;
     NULL, agifa073;
|FIN;

|PROCESO agit0000;
     #agifa104#25 = #agit0000#0;
     #agifa104#0  = #agit0000#1;
     |LEE 000#agifa104.=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |BUCLE agifa073;
|FPRC;

|DEFBUCLE agit0000;
     #agit0000#2;
     ;
     "N", dcliente, "     ", nDPedido;
     "N", hcliente, "zzzzz", nHPedido;
     ;
     NULL, agit0000;
|FIN;

|PROCESO salida; |TIPO 11;
     |SI FSalida = 10;   || F2 Teclas Funcion
          |VARIABLES; {20} aPopup = ""; |FINSI;
          aPopup1 = "-1";  || Si fuera -1 en la posicion
          |SI pedido = 0; aPopup2 = 5; |SINO; aPopup2 = 4; |FINSI;
          aPopup3 = "<ENTER> Marca/Desmarca linea";
          aPopup4 = "<F3> Buscar Artículo";
          aPopup5 = "<F4> Modificar condiciones Pedido/s";
          aPopup6 = "<F5> Aceptar linea Pedido";
          aPopup7 = "<F6> Aceptar Pedido completo";
          IaPopup = aPopup1  <-;
          |ESPECIFICA "TRACKPOPUP", aPopup;
          nSali = FSalida;
          |SI FSalida = 1; nSali = 99; |FINSI;
          |SI FSalida = 2; nSali = 11; |FINSI;
          |SI FSalida = 3; nSali = 12; |FINSI;
          |SI FSalida = 4; nSali = 13; |FINSI;
          |SI FSalida = 5; nSali = 14; |FINSI;
          FSalida = nSali;
          |SI FSalida = 0; |ERROR; |ACABA; |FINSI;
     |FINSI;

     |SI FSalida = 11;   || F3
          |PUSHV 0501 2380;
          |BLANCO 921 1559;
          |PINPA #0#115;
          |PINDA #0#115;
          |ENDATOS #1#115;
          |SI nSali = 0;
               aSer = #0#0;
               nPed = #0#1;
               nLin = #0#2;
               aArti = #115#0;
               |PTEC 806;
          |FINSI;
          |ERROR;
          |POPV;
     |FINSI;

     |SI FSalida = 13; |O FSalida = 14;       || F5. F6
          nTecla = FSalida;
          |HAZ CheqPedido;
          |SI nError = 0;
               |SI nTecla = 13;
                    #0#12 = "*"; |PINTA #0#12;
                    |GRABA 020#0a;
                    |PTEC 807;
               |FINSI;
               |SI nTecla = 14;
                    aSer = #0#0;
                    nPed = #0#1;
                    nLin = #0#2;
                    #0#2 = 0;
                    |LEE 000#0];
                    |PARA; |SI FSalida = 0; |Y aSer = #0#0; |Y nPed = #0#1; |HACIENDO;
                         #0#12 = "*";
                         |GRABA 020#0a;
                         |LEE 000#0s;
                    |SIGUE;
                    #0#0 = aSer;
                    #0#1 = nPed;
                    #0#2 = nLin;
                    |LEE 000#0=;
                    |PONCONTROL 23, "SI";
                    |PTEC 807;
               |FINSI;
          |FINSI;

/*
          |SI pedido ! 0; |Y FSalida = 14; |ACABA; |FINSI;
          |SI FSalida = 13; |Y Campo = 9; |ACABA; |FINSI;
          serie   = #0#0;     || SERIE.
          pedido  = #0#1;     || NUMERO PEDIDO.
          lineap  = #0#2;     || LINEA PEDIDO.
          codigo  = #0#4;     || CODIGO ARTICULO.
          descrip = #0#5;     || DESCRIPCION.
          unid    = #0#8;     || UNIDADES.
          cliente = #0#9;     || Cliente
          |SI FSalida = 14;
               lineap = 0;    || Pedido Completo
          |FINSI;
          |PTEC 807;
*/
     |FINSI;
     |SI FSalida = 12;
          |PUSHV 05012380;
          |HAZ Condiciones;
          |POPV;
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO mini;
     |SI FSalida = "POSICION";
          #0#0 = aSer;
          #0#1 = nPed;
          #0#2 = nLin;
     |SINO;
          #0#0 = "     ";
          #0#1 = 0;
          #0#2 = 001;
          #0#9 = "      ";
     |FINSI;
|FPRC;

|PROCESO maxi;
     #0#0 = "zzzzz";
     #0#1 = 999999;
     #0#2 = 999;
|FPRC;

|PROCESO Escape;
     |SI FSalida = 1;
         |LEE 040#0c;
     |FINSI;

     |SI FSalida = 4;
         nSal = 0;
         |PTEC 807;
     |FINSI;
|FPRC;

|PROCESO BusArti2;
     #0#0 = aSer;
     #0#1 = nPed;
     #0#2 = nLin;
     |LEE 000#0];
     |LEE 000#0s;
     |PARA; |SI FSalida = 0; |HACIENDO;
          |SI #0#4 = aArti;
               aSer = #0#0;
               nPed = #0#1;
               nLin = #0#2;
               nHay = 1;
               |SAL;
          |FINSI;
          |LEE 000#0s;
     |SIGUE;
|FPRC;

|PROCESO Marca; |TIPO 0;
     |SI FSalida = 0; |Y #0Campo = Contenido;
          |SI #0#12 = "*";
               #0#12 = "";
               nUniMarcadas = nUniMarcadas - #0#8;
               #0#13 = nUniMarcadas; |PINTA 2266, #0#13;
          |SINO;
               |HAZ CheqPedido;
               |SI nError = 0;
                    #0#12 = "*";
                    nUniMarcadas = nUniMarcadas + #0#8;
                    #0#13 = nUniMarcadas; |PINTA 2266, #0#13;
               |FINSI;
          |FINSI;
          |GRABA 020#0a;
          |PINTA #0#12;
     |FINSI;
|FPRC;

|PROGRAMA;
     |HAZ EsEvaristo; nSwEvaristo = FSalida;
     |HAZ EsBerria; nSwBerria = FSalida;

     |ABRE #142; |LEE 000#142p; |CIERRA #142;

     |NOME_DAT #0 eaFicPdte; |DELINDEX #0;

     |SI enTipo = 0;                   || Desde Cabecera
          dcliente = "      ";
          hcliente = "zzzzzz";
     |SINO;                            || Desde Linea
          dcliente = cliente;
          hcliente = cliente;
     |FINSI;

     |SI pedido = 0;
          nDPedido = 1;
          nHPedido = 999999;
     |SINO;
          nDPedido = pedido;
          nHPedido = pedido;
     |FINSI;

     |ABRE #0;
     |ABRE #19;
     |ABRE #agifa104;
     |BUCLE agit0000;
     |CIERRA #agifa104;
     |CIERRA #19;

     |SI enDesdeX = 0; |PUSHV 0501 2380; |PINPA #0#0; |FINSI;

     |LEE 000#0p;

  |ET Otra;
     |SI enDesdeX = 0;
          |PINPA #0#0;
          #0#13 = nUniMarcadas; |PINTA 2266, #0#13;
          |ENTLINEAL #1#0,1,4,20,0,mini,maxi;
     |SINO;
         nSal  = 1;
         |SI FSalida = 0;
             nModo = 2 + 4 + 8 + 16;
             |LINEAL_SIMPLE #1#0, nModo, 0501, 2699, NULL, NULL, Escape;
             |SI nSal = 0;
                 serie   = #0#0;     || SERIE.
                 pedido  = #0#1;     || NUMERO PEDIDO.
                 lineap = 0;    || Pedido Completo
                 codigo  = #0#4;     || CODIGO ARTICULO.
                 descrip = #0#5;     || DESCRIPCION.
                 unid    = #0#8;     || UNIDADES.
                 cliente = #0#9;     || Cliente
             |FINSI;
         |FINSI;
     |FINSI;
     |SI aArti ! "";
          nHay = 0;
          |HAZ BusArti2;
          |SI nHay = 0;
               |MENAV "0000El articulo no se encuentra en las lineas pendientes.";
          |FINSI;
          aArti = "";
          #0#0 = aSer;
          #0#1 = nPed;
          #0#2 = nLin;
          |LEE 000#0=;
          |VETE Otra;
     |FINSI;

     |SI enDesdeX = 0; |POPV; |FINSI;

     |SELECT #2#0;
     #0#12 = "*";
     #0#0 = "";
     #0#1 = 0;
     #0#2 = 0;
     |LEE 000#0];
     |SI FSalida = 0; |Y #0#12 = "*";
          pedido = #0#1;
          cliente = #0#9;
          |CIERRA #0;
     |SINO;
          pedido = 0;
          |CIERRA #0;
          |DELINDEX #0; Tempo = eaFicPdte; |HAZ BoraTempo;
     |FINSI;
|FPRO;
