|FICHEROS;
     agifa142 #142;
     agifa072 #0;
     agifa079 #1;
     agifa077 #2;
     agifa007 #10;
     dsm00025 #222;
     dsw00030 #88;
     agifa077@ #100;
     referen@;
|FIN;

|VARIABLES;
     &eaSerFacCom = "";
     &eaNumFacCom = 0;
     nNoFactu = 0;
     nBruto = 0;
     nToDto = 0;
     nBase = 0;
     nToIva = 0;
     nTota = 0;
     &eaProve = "";
     &eaFPago = "";
     &enDto1 = 0;
     &enDto2 = 0;
     &enDto3 = 0;
     aPtec = "";
     nPtec = 0;
     nLin = 0;
     &enSalida = 0;
     &prov = "";
     &fpago = "";
     &dto = 0;
     &dtoa = 0;
     &dtop = 0;
     &factur = 1;
     &eaSerAlbUlt    = "";
     nTecla          = 0;
     &nDeci_TotVis   = 0;
     &nDeci_TotVisF  = 0;
     nCampo          = 0;
     aAlfa           = "";
     aDef            = "";
     i               = 0;
     &EURODB         = 0;
     &Tempo          = "";
     aTempo100       = "";
     nSwSuAlb        = 0;
     aSal            = "";
     aCentro         = "";
     aUsu            = "";
     &enSwMenu       = 0;
     nGuarda         = 0;
     nModo           = 0;
     sualba = "";
     serie_fac = "";
     serie_alb = "";
     alfa = "";
     aDSerie = "";
     aHSerie = "";
     fichero = "";
|FIN;

|PROCESO nofac;
     aAlfa = "|NC";
     aAlfa = #2#aAlfa#;
     nCampo = aAlfa;
     nCampo = nCampo - 1;
     |PARA i = 0; |SI i [ nCampo; |HACIENDO i = i + 1;
          #100i = #2i;
     |SIGUE;
     |GRABA 001#100n;
|FPRC;

|DEFBUCLE leealbs;
     #2#6;
     3, 42, 38;
     "     ", 000000, aDSerie, 000001, #1#2, "N", "N";
     "     ", 000000, aHSerie, 999999, #1#2, "N", "N";
     ;
     NULL, nofac;
|FIN;

/*
|DEFBUCLE leealbs;
     #2#1;
     3, 38, 42;
     aDSerie ,       0 , #1#2, "N", "N";
     aHSerie ,  999999 , #1#2, "N", "N";
     ;
     NULL, nofac;
|FIN;
*/

|PROCESO apendc; |TIPO 6;
     nModo = (FEntrada / 100) ! 0;
     nGuarda = FEntrada;
     prov  = #1#2;
     fpago = #1#11;
     dto   = #1#4;
     dtoa  = #1#5;
     dtop  = #1#6;
     |SI FSalida = 12;          || <F4> Albaranes sin facturar
          |DDEF aDef;
          aDef = aDef + "agifa077";
          |CARGA_DEF aDef, agifa077@;
          |SI FSalida = 0;
               |HAZ CreaTempo; aTempo100 = Tempo;
               |NOME_DAT #100 aTempo100; |DELINDEX #100;
               |ABRE #100;
               |SI Campo = 17; ||Llamado desde campo serie
                    aDSerie = "     ";
                    aHSerie = "zzzzz";
               |SINO; ||Llamado desde pedido
                    aDSerie = #0#17;
                    aHSerie = #0#17;
               |FINSI;
               nDeci_TotVis = nDeci_TotVisF;
               |BUCLE leealbs;
               |LEE 000#100p;
               |SI Campo = 17; ||Llamado desde campo serie
                    |C_V #100#6, 1, "S";
                    |CONSULTA_CLAVE #1#100;
                    |SI FSalida = 0; ||Intro
                         #0#17 = #100#50; ||Pongo Serie seleccionada
                         #0#2 = #100#0;   ||Pongo Npedido
                         |PINTA #0#17;
                         |PINTA #0#2;
                         |PTEC 802;
                    |SINO;
                         |ERROR;
                    |FINSI;
               |SINO;
                    |CONSULTA_CLAVE #1#100; ||Llamado desde pedido
                    |SI FSalida = 0; ||Intro
                         #0#2  = #100#0; ||Pongo Npedido
                         |PINTA #0#2;
                    |SINO;
                         |ERROR;
                    |FINSI;
               |FINSI;
               |CIERRA #100;
               |DELINDEX #100; Tempo = aTempo100; |HAZ BoraTempo;
               |DESCARGA_DEF #agifa077@;

               |ABRE #2;
               #2#50 = #0#17;
               #2#0 = #0#2;
               |LEE 000#2=;
               |CIERRA #2;
          |FINSI;
          |ACABA;
     |FINSI;
     |SI FSalida = 13;           || <F5> Nuevo Alba
          |SI nModo = 1;
               |ABRE #2;
               enSwMenu = 1;
               #2#50 = #0#17;         ||Serie
               |HAZ ContaAC7;
               |CIERRA #2;
               #0#2 = #2#0;           ||Conta
          |FINSI;
          aAlfa = "dsz99957;N" + #0#17 + #0#2;
          eaProve = #1#2;
          eaFPago = #1#11;
          enDto1 = #1#4;
          enDto2 = #1#5;
          enDto3 = #1#6;
          eaSerFacCom = #0#16;
          eaNumFacCom = #0#0;

          |SUB_EJECUTA aAlfa;
          |SI enSalida ! 0; |ERROR; |FINSI;
          FEntrada = nGuarda;
          |ACABA;
     |FINSI;
     |SI Campo = 2;
          |ABRE #2;
          #2#50 = #0#17;
          #2#0 = #0#2;
          |LEE 000#2=;
          |SI FSalida ! 0; |Y #0#2 ! 0;
               aAlfa = "dsz99957;N" + #0#17 + #0#2;
               eaProve = #1#2;
               eaFPago = #1#11;
               enDto1 = #1#4;
               enDto2 = #1#5;
               enDto3 = #1#6;
               eaSerFacCom = #0#16;
               eaNumFacCom = #0#0;
               |SUB_EJECUTA aAlfa;
               |SI enSalida ! 0; |ERROR; |FINSI;
          |FINSI;
          |CIERRA #2;
     |FINSI;

     FEntrada = nGuarda;
|FPRC;

|PROCESO CompDiv; |TIPO 0;
     |ABRE #142;
     |LEE 000#142p;
     |CIERRA #142;
     |SI #142#128 = "S"; |Y #0#17 ! #1#66;
          |MENAV "0000Las series de Factura/Albaran no se corresponden";
          |ERROR;
          |ACABA;
     |FINSI;

     |ABRE #agifa007;
     #agifa007#26 = #0#16;
     |LEE 010#agifa007.=;
     serie_fac = #agifa007#39;
     #agifa007#26 = #0#17;
     |LEE 010#agifa007.=;
     serie_alb = #agifa007#39;
     |CIERRA #agifa007;
     |SI serie_alb ! serie_fac;
          |SI serie_alb = "S";
               |MENAV "0000La serie de la factura no es de divisas";
          |SINO;
               |MENAV "0000La serie de la factura es de divisas";
          |FINSI;
          |ERROR;
     |SINO;
          eaSerAlbUlt = #0#17;
     |FINSI;
|FPRC;

|PROCESO Tipo7; |TIPO 7;
     nModo = (FEntrada/100) ! 0;
     |SI nModo = 1;
          #0#17 = #1#66; |PINTA #0#17;
          |SI #agifa142#185 = "S"; #0#17 = #agifa142#193; |FINSI;
     |FINSI;
     |HAZ MensaNoFactu;
|FPRC;

|PROCESO Tipo7Lin; |TIPO 7;
     ||컴컴컴컴컴컴컴컴컴컴컴컴
     |HAZ EsAlbero;
     |SI FSalida = 0;
          |HAZ AlbeFacComLin1;
     |FINSI;
     |HAZ EsAparecida;
     |SI FSalida = 0;
          |HAZ TotalesLinea72;
     |FINSI;
     ||컴컴컴컴컴컴컴컴컴컴컴컴
     |SI nSwSuAlb ! 0;
          nSwSuAlb = 0;
          |ABRE #2;
          |SELECT #4#2;
          #2#3 = #1#2;
          #2#54 = sualba;
          |LEE 000#2];
          |PARA; |SI FSalida = 0; |Y #2#3 = #1#2; |Y #2#54 = sualba; |HACIENDO;
               |SI #2#38 = "N";
                    #0#17 = #2#50;
                    #0#2 = #2#0;
                    |PTEC 802;
                    nSwSuAlb = 1;
                    |SAL;
               |FINSI;
               |LEE 000#2s;
          |SIGUE;
          |CIERRA #2;
          |SI nSwSuAlb = 1;
               |PTEC 802;
               |PTEC 802;
          |SINO;
               |PTEC 807;
               |ERROR;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO sualba; |TIPO 6;
     |SI FSalida = 10;
          |PUSHV 0501 2380;
          |BLANCO 1004 1335;
          |CUADRO 1004 1335;
          |ATRI I; |PINTA 1106, "Su Albaran n:"; |ATRI 0;
          sualba = "";
          E_inf = "";
          E_sup = 13;
          sualba = 1121 ? 1; |QBF sualba;
          |POPV;
          |SI sualba = ""; |ACABA; |FINSI;
          nSwSuAlb = 0;
          |ABRE #2;
          |SELECT #4#2;
          #2#3 = #1#2;
          #2#54 = sualba;
          sualba = #2#54;   ||Formatea
          |LEE 000#2];
          |PARA; |SI FSalida = 0; |Y #2#3 = #1#2; |Y #2#54 = sualba; |HACIENDO;
               |SI #2#38 = "N";
                    #0#17 = #2#50;
                    #0#2 = #2#0;
                    |PTEC 802;
                    nSwSuAlb = 1;
                    |SAL;
               |FINSI;
               |LEE 000#2s;
          |SIGUE;
          |CIERRA #2;
          |SI nSwSuAlb = 0;
               |MENAV "0000Albaran inexistente o facturado...";
               |ERROR;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO tipo11; |TIPO 11;
     nModo = (FEntrada / 100) ! 0;
     nTecla = FSalida;
     ||컴컴컴컴컴컴컴컴컴컴컴컴
     |HAZ EsAlbero;
     |SI FSalida = 0;
          |HAZ AlbeFacComLin2;
     |FINSI;
     ||컴컴컴컴컴컴컴컴컴컴컴컴

     |SI nModo = 4; |ACABA; |FINSI;

     |SI nTecla = 13;
          |HAZ AjustaFactura;
          |SI FSalida = 0;
               |HAZ defecfl;
               |HAZ actufl;
               |GRABA 020#0a;
               ||컴컴컴컴컴컴컴컴컴횺odulo 000con
               |HAZ EsConstru;
               |SI FSalida = 0;
                    aAlfa = "dsz99962;" + #0#17 + #0#2;
                    |SUB_EJECUTA aAlfa;
                    |PTEC 808; |PTEC 816; |PTEC 812;
               |FINSI;
               ||컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
          |FINSI;
          |ERROR; |ACABA;
     |FINSI;
|FPRC;

|PROCESO sacares2; |TIPO 0;
     #88#2 = #88#0 + #88#1;
     |SI #88#3 ! 0;
          #88#6 = #88#0 - #88#3;
     |SINO;
          #88#6 = 0;
     |FINSI;
     |SI #88#4 ! 0;
          #88#7 = #88#1 - #88#4;
     |SINO;
          #88#7 = 0;
     |FINSI;
     |SI #88#5 ! 0;
          #88#8 = #88#2 - #88#5;
     |SINO;
          #88#8 = 0;
     |FINSI;
|FPRC;

|PROCESO MinC072;
     #referen@#16 = #1#66;
     #referen@#0 = #1#0;
     #referen@#1 = 001;
|FPRC;

|PROCESO MaxC072;
     #referen@#16 = #1#66;
     #referen@#0 = #1#0;
     #referen@#1 = 999;
|FPRC;

|PROCESO Consulta072; |TIPO 66;
     nLin = 0;
     |DDEF aAlfa;
     aAlfa = aAlfa + "agifa072";
     |CARGA_DEF aAlfa, referen@;
     |SI FSalida = 0;
          |ABRE #referen@;
          |CONSULTA_F_CLAVE #1#referen@, MinC072, MaxC072;
          |SI FSalida = 0; nLin = #referen@#1; |FINSI;
          |CIERRA #referen@;
          |DESCARGA_DEF #referen@;
     |FINSI;
     |SI nLin ! 0;
          |PTEC 802;
          aAlfa = ("000" + nLin) % -103;
          aPtec = aAlfa % 301; nPtec = &aPtec;
          |PTEC nPtec;
          aPtec = aAlfa % 201; nPtec = &aPtec;
          |PTEC nPtec;
          aPtec = aAlfa % 101; nPtec = &aPtec;
          |PTEC nPtec;
     |FINSI;
|FPRC;

|PROCESO SumaLineas;
     nBruto = nBruto + #referen@#27;
     nToDto = nToDto + #referen@#28;
     nBase = nBase + #referen@#30;
     nToIva = nToIva + #referen@#31;
     nTota = nTota + #referen@#32;
|FPRC;

|DEFBUCLE SumaLineas;
     #referen@#1003;
     ;
     #1#66, #1#0, 001;
     #1#66, #1#0, 999;
     ;
     NULL, SumaLineas;
|FIN;

|PROCESO TotalesLinea72;
     |DDEF aAlfa;
     aAlfa = aAlfa + "agifa072";
     |CARGA_DEF aAlfa, referen@;
     |SI FSalida = 0;
          nBruto = 0;
          nToDto = 0;
          nBase = 0;
          nToIva = 0;
          nTota = 0;
          |BUCLE SumaLineas;
          #referen@#27 = nBruto;
          #referen@#28 = nToDto;
          #referen@#30 = nBase;
          #referen@#31 = nToIva;
          #referen@#32 = nTota;
          |PINTA 2301, "TOTALES (EUR) - ->";
          |PINTA 2319, #referen@#27;
          |PINTA 2331, #referen@#28;
          |PINTA 2342, #referen@#30;
          |PINTA 2355, #referen@#31;
          |PINTA 2367, #referen@#32;
          |DESCARGA_DEF #referen@;
     |FINSI;
|FPRC;


|PROCESO NoFactu;
     nNoFactu = nNoFactu + 1;
|FPRC;

|DEFBUCLE NoFactu;
     #2#6;
     3, 42, 38;
     "     ", 000000, aDSerie, 000001, #1#2, "N", "N";
     "     ", 000000, aHSerie, 999999, #1#2, "N", "N";
     ;
     NULL, NoFactu;
|FIN;

|PROCESO MensaNoFactu; |TIPO 7;
     |SI Campo = 17;
          aDSerie = "     ";
          aHSerie = "zzzzz";
     |SINO;
          aDSerie = #0#17;
          aHSerie = #0#17;
     |FINSI;

     nNoFactu = 0;
     |BUCLE NoFactu;
     |SI Campo = 17;
          aAlfa = "0000<F2> Recoge por Su Albaran N  <F4> Alb. sin facturar: [" + nNoFactu + "]  <F5>Alta Alb.";
     |SINO;
          aAlfa = "0000<F4> Albaranes sin facturar: [" + nNoFactu + "]   <F5> Alta/Modificacion Albaran";
     |FINSI;
     |MENSA aAlfa;
|FPRC;
