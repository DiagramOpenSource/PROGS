||                Estimacion de necesidades segun pedidos

|FICHEROS;
     agifa048 #0;
     agifa049 #1;
     agifa076 #2;
     agifa027 #3;
     agifa019 #4;
     agifa090 #5;
     agifa074 #6;
     agifa123 #7;
     agifa125 #8;
     agifa126 #9;
     agifa017 #10;
     agifa073 #11;
|FIN;

|CAMPOS;
     #0#0 escodigo ;     #0#9  escostot ;
     #0#1 esdescri ;     #0#10 escalmac ;

     #1#0 linesca  ;     #1#1 linlinea ;
     #1#2 linartic ;     #1#3 lindesc  ;
     #1#4 lincanti ;     #1#8 linivel  ;
     #1#9 linalmac ;

     #2#4  nivelstim;
     #2#5  ordestim ;    #2#6  fechestim ;

     #4#0    artcodig ;  #4#104  artdispo ;
     #4#13   artfabri ;  #4#16   artrecib ;
     #4#17   artpenva ;  #4#7    artminim ;
     #4#15   artservi ;  #4#12   artreser ;
     #4#11   artneces ;  #4#1    artdescri;

     #7#0 tmpescal ;     #7#1 tmp1busc ;
     #7#2 tmp2busc ;     #7#3 tmpcanti ;

     #8#0  almac ;   #8#1  fabric;   #8#2  compra;
     #8#3  artic ;   #8#4  descri;   #8#5  minimo;
     #8#6  servi ;   #8#7  recibi;   #8#8  enfabr;
     #8#9  dispo ;   #8#10 reserv;   #8#11 penval;
     #8#12 necbru;

     #9#0  artitemp;   #9#1  necetemp;

     #11#2 codestim;


|FIN;

|VARIABLES;
     &Tempo = "";
     aTmp7 = "";
     aTmp8 = "";
     aTmp9 = "";
 salir4 = 0;
 sw8 = 0;
 sw7 = 0;
 sw9 = 0;
     pepe      = "";
     nivel     = 0 ;
     salir     = 0 ;
     cantidad  = 0 ;
     guardo    = 0 ;
     informe   = "agifa076";
     hay       = 0 ;
     escan     = "                    ";
    blancos   = "                    ";
     acom      = 0 ;
     afab      = 0 ;
     &fecha    = @ ;
     clave     = "ordenfab";
     ncontador = 0;
     entrar    = 0;
     mascara   = "0000000";
     tempo     = " ";
     filtro    = "-106";
     codigo    = "       ";
     codestim  = "                    ";
     cantestim = 0;
     codeguard = "                    ";
     cabecera  = "Necesidades s/Pedi."
     mensaje1 = "0000El programa no termino normalmente ";
     &bl  = "                              ";
|FIN;



|CALCULO contador;|TIPO 0;
|ABRE #3;
 #3#0 = clave;
 |LEE 140#3=;
 |SI 0 ! FSalida;
     |DDEFECTO #3;
     #3#0 = clave;
     |GRABA 020#3b;
 |FINSI;
 ncontador = #3#1;
 #3#1 = #3#1 + 1;
 |GRABA 020#3a;
 |LIBERA #3;
 |CIERRA #3;
 tempo = mascara + ncontador;
codigo = tempo % filtro;
|FCAL;





|CALCULO mirartic;|TIPO 0;
artitemp = artcodig;
|LEE 021#4=;
|LEE 101#9= ;
|SI 0 ! FSalida ;                       || no existe ficha temporal
   cantidad = cantidad + artneces;
   necetemp = cantidad;
   |SI nivel = 0;
      tmpcanti = cantidad;
   |FINSI;
   |GRABA 101#9c;
|SINO;                                  || si existe
   guardo = necetemp;
   necetemp = cantidad + necetemp;
   |SI guardo [ 0;
      cantidad = necetemp ;
   |FINSI;
   |GRABA 101#9a;
|FINSI;
|FCAL;




|CALCULO grabatemp2;|TIPO 0;
artic  = escodigo;

|LEE 101#8=;
|SI 0 ! FSalida;
  fabric = afab      ;compra = acom    ;
  descri = artdescri ;minimo = artminim; almac  = escalmac;
  servi  = artservi  ;recibi = artrecib;
  enfabr = artfabri  ;dispo  = artdispo;
  reserv = artreser  ;penval = artpenva;
  |GRABA 101#8c;
|SINO;
  fabric =  fabric + afab; compra = compra + acom;
  |GRABA 101#8a;
|FINSI;

|FCAL;


|CALCULO grabatemp1;|TIPO 0;
tmpescal = nivel;
|LEE 101#7=;
tmp1busc = linesca;
tmp2busc = linlinea;
|SI 0 ! FSalida ;
   |GRABA 101#7c;
|SINO;
   |GRABA 101#7a;
|FINSI;
tmpcanti = cantidad;

|FCAL;



|CALCULO leetemp1;|TIPO 0;
tmpescal = nivel ;
|LEE 021#7=;
linesca  = tmp1busc;
linlinea = tmp2busc;
cantidad = tmpcanti;
escan    = linesca;
|FCAL;



|CALCULO continua;|TIPO 0;
escan = linesca;
salir4 = 0;
|PARA hay = 1; |SI nivel > 0; |Y salir4 = 0; |HACIENDO;
   |LEE 001#1>;
   hay = FSalida;
   |SI hay = 0 ;|Y escan = linesca;
      salir4 = 1;
   |SINO;
      |SI nivel > 1;
          nivel = nivel - 1;
          |HAZ leetemp1;
      |SINO ;
          salir = 1;
          salir4 = 1;
      |FINSI;
   |FINSI;
|SIGUE;
escodigo = linartic;
cantidad = tmpcanti * lincanti;
|FCAL;



|CALCULO estima;|TIPO 0;
salir = 0;
cantidad = 0;
tmpcanti = cantestim;

|PARA nivel = 0; |SI nivel [ nivelstim; |Y salir = 0; |HACIENDO;
   |LEE 001#0=;
   artcodig = escodigo;
   |SI 0 = FSalida;                  || caso que sea escandallo
      |HAZ mirartic;
      |SI cantidad > 0;
         afab   = cantidad;
         acom   = 0;
         |HAZ grabatemp2;
         |SI nivel > 0;|Y  nivel ! nivelstim;
            |HAZ grabatemp1;
         |FINSI;

         |SI nivel = nivelstim;
            |HAZ continua;
         |SINO;
            linesca  = escodigo;
            linlinea = 1;
            |LEE 001#1];
            nivel = nivel + 1;
            cantidad = tmpcanti * lincanti;
            escodigo = linartic;
         |FINSI;
      |SINO;
         |SI nivel = 0;
            salir = 1;
         |SINO;
            |HAZ continua;
         |FINSI;
      |FINSI;
   |SINO;                          || es materia prima
      |HAZ mirartic;
      |SI cantidad > 0;
         acom = cantidad;
         afab = 0;
         |HAZ grabatemp2;
      |FINSI;
      |HAZ continua;
   |FINSI;
|SIGUE;

|FCAL;



|CALCULO listado;|TIPO 0;
fecha = ~;
|IMPRE 0;
|INFOR informe;
|PARA artic = blancos;|SI 0 = FSalida ;|HACIENDO;
   |LEE 101#8>;
   |SI 0 = FSalida;
      |PRINT;
   |FINSI;
|SIGUE;
|PIEINF;
|FININF;
|FINIMP;
|FCAL;


|CALCULO cabe;|TIPO 0;
|ABRE #5;
|DDEFECTO #5;
#5#1 = fecha;#5#2 = fechestim;
|PARA FSalida = 1;|SI 0 ! FSalida;|HACIENDO FSalida = FSalida;
    |HAZ contador;
    #5#0 = codigo;
    |GRABA 000#5b;
|SIGUE;
|FCAL;



|CALCULO actua;|TIPO 0;
artcodig = linartic;
|LEE 121#4=;
|SI 0 = FSalida;
   #10#0 = linartic;    #10#1 = linalmac;
   |LEE 121#10=;
   cantidad = fabric   * lincanti;
   artdispo = artdispo - cantidad;
   artservi = artservi + cantidad;
   artreser = artreser + cantidad;
   artneces = artneces + cantidad;

   #10#39 = #10#39 - cantidad;
   #10#8  = #10#8  + cantidad;

   |GRABA 121#4a;
   |GRABA 121#10a;
   |LIBERA #4;
   |LIBERA #10;
|FINSI;
|FCAL;


|CALCULO lines;|TIPO 0;
#6#1 = #6#1 + 1;
#6#2 = artic; #6#3 = descri  ; #6#4 = fabric;
#6#5 = 0    ; #6#7 = almac ;
|GRABA 020#6b;
|SI 0 = FSalida;
   artcodig = artic;
   |LEE 121#4=;
   |SI 0 = FSalida;
      #10#0 = artic;#10#1 =  almac;
      |LEE 121#10=;
      |SI 0 = FSalida;
         artfabri = artfabri + fabric;
         artneces = artneces - fabric;
         #10#9 = #10#9 + fabric;
         |GRABA 121#10a;
         |GRABA 121#4a;
         |LIBERA #10;
         |LIBERA #4;
      |FINSI;
   |FINSI;
   escodigo = artic;
   |LEE 121#0=;
   |SI 0 = FSalida;
      |BUCLELIN 0actua#0;
   |FINSI;
|FINSI;
|LIBERA #6;
|FCAL;






|CALCULO ordfab;|TIPO 3;
|SI ordestim = AS;|O ordestim = As;
   |HAZ cabe;
   |ABRE #6;
   |ABRE #4;
   |ABRE #10;
   |ABRE #0;
   |ABRE #1;
   |DDEFECTO #6;
   #6#0 = codigo;
   #6#1 = 0;
   |PARA artic = blancos;|SI 0 = FSalida ;|HACIENDO;
      |LEE 101#8>;
      |SI 0 = FSalida;|Y fabric ! 0;
         |HAZ lines;
      |FINSI;
   |SIGUE;
   |LIBERA #5;
   |CIERRA #5;
   |CIERRA #0;
   |CIERRA #1;
   |CIERRA #10;
   |CIERRA #4;
   |CIERRA #6;
|FINSI;
|FCAL;


|CALCULO resum;|TIPO 0;

   escodigo = codeguard;

   |LEE 001#0=;
   |SI FSalida = 0;
      |HAZ estima;
   |SINO;
      cantidad = 0;
      artcodig = escodigo;
      |HAZ mirartic;
      |SI cantidad > 0;
         acom = cantidad;
         afab = 0;
         |HAZ grabatemp2;
      |FINSI;
   |FINSI;
|FCAL;


|CALCULO entneces;|TIPO 0;
|SI entrar = 0;
   codeguard = codestim;
   |QBF codeguard;
   entrar  = 1;
|FINSI;
|PINTA 1829,#11#0;
|PINTA 1853,codestim;
pepe = codestim; |QBF pepe;
|SI pepe = codeguard;
   cantestim = cantestim + (#11#5 - #11#43);
|SINO;
   |HAZ resum;
   cantestim = #11#5 - #11#43;
   codeguard = codestim; |QBF codeguard;
|FINSI;
|FCAL;


|CALCULO necescon;|TIPO 0;
     |ERROR;
|FCAL;


|DEFBUCLE agifa4920;
#11#3;
;
#2#0 ,"     " ,  #2#2 , "      ";
#2#1 ,"zzzzz" ,  #2#3 , "zzzzzz";
e;
necescon,entneces,NULL,NULL;
|FIN;



|PROCESO Tipo8; |TIPO 8;
     |HAZ CreaTempo; aTmp8 = Tempo;
     |NOME_DAT #8 aTmp8; |DELINDEX #8;
|FPRC;

|CALCULO fin;|TIPO 9;
     Tempo = aTmp8; |HAZ BoraTempo;
     |CIERRA   #8;
     |DELINDEX #8;
|FCAL;

|CALCULO necped;|TIPO 0
     |HAZ CreaTempo; aTmp7 = Tempo;
     |NOME_DAT #7 aTmp7; |DELINDEX #7;

     |HAZ CreaTempo; aTmp9 = Tempo;
     |NOME_DAT #9 aTmp9; |DELINDEX #9;

     |ABRE #0;
     |ABRE #1;
     |ABRE #4;

     sw8 = 0; sw7 = 0; sw9 = 0;

     |ABRE   #8;
     |ABRE   #7;
     |ABRE   #9;

     |BUCLE agifa4920;

     |SI entrar = 1;
          |HAZ resum;
          |HAZ listado;
     |FINSI;

     |CIERRA   #9;
     |CIERRA   #7;
     Tempo = aTmp9; |HAZ BoraTempo; |DELINDEX #9;
     Tempo = aTmp7; |HAZ BoraTempo; |DELINDEX #7;

     |CIERRA   #4;
     |CIERRA   #1;
     |CIERRA   #0;
|FCAL;



|CALCULO pintapan;|TIPO 7;
 |CLS;
 |CABEZA cabecera;
 |PINPA #0#2;
 |PINDA #0#2;
|FCAL;

|CALCULO tecla;|TIPO 0;
|SI FSalida = 2;
   |ERROR;
|FINSI;
|FCAL;
