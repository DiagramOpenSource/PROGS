|FICHEROS;
     dsz00042 #0;
     agifa019 #19;
     agifa017 #17;
     agifa029 #29;
     agifa065 #65;
     dsm00120 #120;
     dsm00121 #121;
     agifa142 #142;
     dsw00041 #41; ||Temporal , solo se usa el campo Almacen
     referen@;
     dsm00154 #154,MANTE; ||Vertical
     dsm00006 #906; ||Historico Vertical
     dsm00010 #910;
|FIN;

|INCLUYE i_varart;

|VARIABLES;
     &enPCM = 0;
     nUniV = 0;
     nStoV = 0;
     nModoV = 0;
     nNume = 0;
     aTmp41 = "";
     nAlmaElige = 0;
     aAlfa = "";
     dArt = "";
     hArt = "";
     nTecla = 0;
     nPosi = 0;
     nSw       = 0;
     &Tempo    = "";
     fFecha    = @;
     aTempo0   = "";
     nModo     = 0;
     nBoton    = 0;
     nDAlma = 0;
     nHAlma = 0;
     nDAlmaBuc = 0;
     nHAlmaBuc = 0;
     nSwConsulta = 0;
|FIN;

|PROCESO RecuentoV; |TIPO 0;
     #154#6 = #154#5 - #154#4;
     |PINTA #154#6;
|FPRC;

|PROCESO MinV;
     #154#0 = #0#0;
     #154#1 = #0#1;
     #154#2 = #0#3;
     #154#3 = "      ";
|FPRC;

|PROCESO MaxV;
     #154#0 = #0#0;
     #154#1 = #0#1;
     #154#2 = #0#3;
     #154#3 = "zzzzzz";
|FPRC;

|PROCESO Vertical;
     |PUSHV 0501 2380;
     nNume = #910#21;
     aAlfa = "\" + #910#23 + "+#";
     |CAMPO_ANCHO  #154#3, 1, nNume;
     |CAMPO_MAXIMO #154#3, 1, aAlfa;
     |BLANCO 1701 2380;
     |PINPA #0#154;
     |ATRI R; |PINTA 1902, #910#20; |ATRI 0;
     |ENTCOLUMNA #1#154, 4, nModoV, 5, 0, MinV, MaxV;
     |POPV;
|FPRC;

|PROCESO SumaUniV;
     nUniV = nUniV + #154#5;
     nStoV = nStoV + #154#4;
     #154#6 = #154#5 - #154#4;
|FPRC;

|DEFBUCLE SumaUniV;
     #154#1;
     ;
     #0#0, #0#1, #0#3, "      ";
     #0#0, #0#1, #0#3, "zzzzzz";
     be;
     NULL, SumaUniV;
|FIN;

|PROCESO Tipo66; |TIPO 66;
     nSwConsulta = 1;
     |SI Campo = 0;
          |ABRE #120;
          |CONSULTA_CLAVE #1#120;
          |SI FSalida = 0;
               #0#0 = #120#0; |PINTA #0#0;
          |SINO;
               |ERROR;
          |FINSI;
          |CIERRA #120;
     |SINO;
          |ABRE #29;
          |CONSULTA_CLAVE #1#29;
          |SI FSalida = 0;
               #0#3 = #29#0; |PINTA #0#3;
          |SINO;
               |ERROR;
          |FINSI;
          |CIERRA #29;
     |FINSI;
|FPRC;

|PROCESO HistoVertical;
     |DDEFECTO #154;
     #154#0 = #120#0;
     #154#1 = #906#0;
     #154#2 = #906#1;
     #154#3 = #906#7;
     |LEE 101#154=;
     |SI FSalida ! 0; |GRABA 020#154b; |FINSI;

     #154#4 = #154#4 + #906#9;
     #154#6 = #154#5 - #154#4;
     |GRABA 020#154a;
     |LIBERA #154;
|FPRC;

|DEFBUCLE HistoVertical;
     #906#1;
     ;
     #65#0, #65#5, #65#1, #65#2, #65#11, #65#3, #65#4, "      ";
     #65#0, #65#5, #65#1, #65#2, #65#11, #65#3, #65#4, "zzzzzz";
     ;
     NULL, HistoVertical;
|FIN;

|PROCESO Historico;
     #19#0 = #65#0;
     |LEE 000#19=;
     |SI FSalida ! 0; |ACABA; |FINSI;
     |SI #19#137 = "N"; |ACABA; |FINSI;

     |DDEFECTO #121;
     #121#0 = #120#0;
     #121#1 = #65#0;
     #121#3 = #65#5;
     |SI #120#3 = "S";
          #121#3 = #120#4;
     |FINSI;
     |SI #120#3 = "P";
          #121#11 = #65#18;
     |FINSI;
     |LEE 101#121=;
     |SI FSalida = 0;
          #121#4 = #121#4 + #65#6;
          #121#6 = #121#5 - #121#4;

          #121#12 = #121#12 + #65#17;
          #121#14 = #121#13 - #121#12;
          |GRABA 020#121a;
     |SINO;
          #121#2 = #19#1;
          #121#4 = #65#6;
          #121#5 = 0;
          #121#6 = #65#6;
          #121#7 = #19#5;

          #121#12 = #65#17;
          #121#13 = 0;
          #121#14 = #65#17;

          #121#15 = #19#28;
          #121#16 = #19#9;
          #121#17 = #19#133;
          |GRABA 020#121n;
     |FINSI;
     |LIBERA #121;

     |SI #19#176 = "S";
          |BUCLE HistoVertical;
     |FINSI;
|FPRC;

|DEFBUCLE Historico;
     #65#1;
     5;
     dArt, "01.01.1900",  "  ", 000000, 00000,"     ", nDAlmaBuc;
     hArt, #120#0,        "zz", 999999, 99999,"zzzzz", nHAlmaBuc;
     ;
     NULL, Historico;
|FIN;

|PROCESO TmpAlma;
     nDAlmaBuc = #41#3;
     nHAlmaBuc = #41#3;
     |BUCLE Historico;
|FPRC;

|DEFBUCLE TmpAlma;
     #41#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, TmpAlma;
|FIN;

|PROCESO PonCero;
     #121#4 = 0;
     #121#12 = 0;
     |GRABA 020#121a;
     |LIBERA #121;

     #41#3 = #121#3;     || Se graba en el temporal
     |LEE 000#41=;       || todos los almacenes del historio inventario
     |SI FSalida ! 0;
          |GRABA 020#41n;
     |FINSI;
|FPRC;

|DEFBUCLE PonCero;
     #121#1;
     ;
     #120#0, "                    ", 00000, "               ";
     #120#0, "zzzzzzzzzzzzzzzzzzzz", 99999, "zzzzzzzzzzzzzzz";
     be;
     NULL, PonCero;
|FIN;

|PROCESO PonCeroV;
     #154#4 = 0;
     |GRABA 020#154a;
     |LIBERA #154;
|FPRC;

|DEFBUCLE PonCeroV;
     #154#1;
     ;
     #120#0, "                    ", 00000, "      ";
     #120#0, "zzzzzzzzzzzzzzzzzzzz", 99999, "zzzzzz";
     be;
     NULL, PonCeroV;
|FIN;

|PROCESO Recalcula;
     |MENSA "0000Recalculando...";
     dArt = " " * 20;
     hArt = "z" * 20;
     |ABRE #120;
     #120#0 = #0#0;
     |LEE 121#120=;
     |SI FSalida = 0;
          |HAZ CreaTempo; aTmp41 = Tempo;
          |NOME_DAT #41 Tempo;
          |DELINDEX #41;

          |ABRE #41;
          |BUCLE PonCero;
          |CIERRA #41;
          |BUCLE PonCeroV;

          |ABRE #154;
          |ABRE #121;
          |ABRE #19;
          |SI #120#3 = "N";
               || Como se puede hacer mas de invetario de distintos
               || almacenes para la misma fecha, se recorre para todos
               || los existentes en el histo. inventario
               |BUCLE TmpAlma;
          |SINO;
               nDAlmaBuc = 1;
               nHAlmaBuc = 99999;
               |BUCLE Historico;
          |FINSI;
          |CIERRA #19;
          |CIERRA #121;
          |CIERRA #154;

          Tempo = aTmp41; |HAZ BoraTempo;
          |DELINDEX #41;
     |FINSI;
     |CIERRA #120;
|FPRC;

|PROCESO Tecla; |TIPO 11;
     |SI FSalida = 13; |O FSalida = 14; || F5, F6
          nTecla = FSalida;
          |ERROR;
          |PTEC 807;
     |FINSI;
     |SI FSalida = 12; ||F4;
          |ABRE #19;
          #19#0 = #0#1;
          |LEE 000#19=;
          |SI FSalida = 0; |Y #19#176 = "S";
               nModoV = 4; |HAZ Vertical;
          |FINSI;
          |CIERRA #19;
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO Mensa; |TIPO 7;
     aAlfa = "0000<F5>  Recalculo Stock   <F6> Imprimir Documento";
     |ABRE #19;
     #19#0 = #0#1;
     |LEE 000#19=;
     |SI FSalida = 0; |Y #19#176 = "S";
          aAlfa = "0000<F4> Componentes   <F5>  Recalculo Stock    <F6> Imprimir Documento";
     |FINSI;
     |CIERRA #19;
     |MENSA aAlfa;
|FPRC;


|PROCESO Llena042; |TIPO 6;
     nModo = (FEntrada /100) ! 0;
     |SI nModo = 1;
          |MENAV "0000Alta no permitida...";
          |ERROR;
     |SINO;
          eaArticulo = #0Campo;
          |HAZ LlenaArticulo;
          |SI FSalida = 0;
               #0Campo = eaArticulo;
               |PINTA #0Campo;
          |SINO;
               |ERROR; |ACABA;
          |FINSI;
     |FINSI;
|FPRC;

컴컴컴Iber21컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
|PROCESO Boton;
     |HAZ EsIber21;
     |SI FSalida = 0;
          |HAZ Iber21Recuento;
     |SINO;
          |HAZ CreaTempo;
          eaFichero = Tempo;
          |SUB_EJECUTA_NP "psion066;R";
          |HAZ BoraTempo;
          |SI enError = 0;
               |HAZ ImportaPDA;
          |SINO;
               |ERROR; |ACABA;
          |FINSI;
     |FINSI;
     |PTEC 807;
     nSw = 1;
|FPRC;
컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
|PROCESO BorraV;
     |BORRA 020#154a;
|FPRC;

|DEFBUCLE BorraV;
     #154#1;
     ;
     #0#0, #0#1, #0#3, "      ";
     #0#0, #0#1, #0#3, "zzzzzz";
     ;
     NULL, BorraV;
|FIN;

|PROCESO Tipo2; |TIPO 2;
     nModo = (FEntrada / 100) ! 0;

     |ABRE #121;
     #121#0 = #0#0;
     #121#1 = #0#1;
     #121#3 = #0#3;
     #121#11 = #0#8;
     |LEE 101#121=;
     |SI FSalida ! 0; |GRABA 020#121b; |FINSI;
     |SI nModo = 3;
          |BORRA 020#121a;
          |BUCLE BorraV;
     |SINO;
          #121#5 = #0#5;
          #121#6 = #0#6;
          #121#13 = #0#10;
          #121#14 = #0#11;
          #121#18 = #0#12;
          #121#19 = #0#13;
          #121#20 = #0#14;
          #121#21 = "S";
          |GRABA 020#121a;
     |FINSI;
     |CIERRA #121;

     #0#15 = "S"; |PINTA #0#15;
|FPRC;

|PROCESO Recuento; |TIPO 7;
     |ABRE #19;
     #19#0 = #0#1;
     |LEE 000#19=;
     |SI FSalida = 0; |Y #19#179 = "S"; |Y #142#117 = "S";
          eaProgVta = "dsz00042";
          enUnidad1 = #0#5;
          enUnidad2 = #0#10;
          eaArticulo = #0#1;
          enAlmacen = #0#3;
          eaPartida = #0#8;
          enMedida1 = #0#12;||Med1
          enMedida2 = #0#13;||Med2
          enMedida3 = #0#14;||Med3
          efFecha = #0#0;
          eaCliente = "";;
          |HAZ DobleUniV;
          #0#5 = enUnidad1;
          #0#10 = enUnidad2;
          #0#12 = enMedida1;
          #0#13 = enMedida2;
          #0#14 = enMedida3;
          |HAZ Calcu;
          |C_M #0#5, 1, "N";
     |SINO;
          |SI #19#176 = "S";
               |C_M #0#5, 1, "N";
               nModoV = 2; |HAZ Vertical;
               nUniV = 0; nStoV = 0;
               |BUCLE SumaUniV;
               #0#4 = nStoV; |PINTA #0#4;
               #0#5 = nUniV; |PINTA #0#5;
               |HAZ Calcu;
          |SINO;
               |C_M #0#5, 1, "S";
          |FINSI;
     |FINSI;
     |CIERRA #19;
|FPRC;

|PROCESO Calcu; |TIPO 0;
     #0#6 = #0#5 - #0#4;
     #0#11 = #0#10 - #0#9;
     |PINTA #0#6;
     |PINTA #0#11;
|FPRC;

|PROCESO dsm00121;
     #0#0 = #121#0;
     #0#1 = #121#1;
     #0#2 = #121#2;
     #0#3 = #121#3;
     #0#4 = #121#4;
     #0#5 = #121#5;
     #0#6 = #121#6;
     #0#7 = #121#7;
     #0#8 = #121#11;
     #0#9 = #121#12;
     #0#10 = #121#13;
     #0#11 = #121#14;
     #0#12 = #121#18;
     #0#13 = #121#19;
     #0#14 = #121#20;
     #0#15 = #121#21;
     |GRABA 020#0n;
|FPRC;

|DEFBUCLE dsm00121;
     #121#1;
     ;
     fFecha, "                   ", nDAlma, "               ";
     fFecha, "zzzzzzzzzzzzzzzzzzz", nHAlma, "zzzzzzzzzzzzzzz";
     ;
     NULL, dsm00121;
|FIN;

|PROCESO Min_42;
     #0#0 = fFecha;
     #0#1 = " " * 20;
     #0#3 = 00001;
     #0#8 = " " * 15;
|FPRC;

|PROCESO Max_42;
     #0#0 = fFecha;
     #0#1 = "z" * 20;
     #0#3 = 99999;
     #0#8 = "z" * 15;
|FPRC;

|PROGRAMA;
     |ABRE #142; |LEE 000#142p; |CIERRA #142;
     |ABRE #910; |LEE 000#910p; |CIERRA #910;
     |CLS;
     |PINPA #0#0;
     |SI #142#117 = "N"; || Doble Stock
          |PINTA 2150, "좔컴컴컴컴좔컴컴컴컴좔컴컴컴컴";
          |PINTA 2250, "                              ";
          |PINTA 2350, "컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴";
          |C_V #0#9, 1, "N";
          |C_V #0#10, 1, "N";
          |C_V #0#11, 1, "N";
          |C_A #0#2, 1, 50;
     |FINSI;
     |ENTLINEAL #1#0, 2, 7, 20, 0, Min_42, Max_42;
     |PUSHV 0501 2380;
     |BLANCO 1126 1656;
     |CUADRO 1126 1756;
     |CUADRO 1227 1655;
     |ATRI R;
     |PINTA 1330, "INVENTARIO:";
     |PINTA 1430, "Almacen...:";
     |C_V #0#0, 1, "S"
     |C_M #0#3, 1, "S"
     |C_P #0#0, 1, 1343;
     |C_P #0#3, 0, nPosi;
     |C_P #0#3, 1, 1443;
     #0#3 = 0;
     |PINTA #0#3;
     |ATRI 0;
     #0#0 = ~;
 |ET Fecha;
     nSwConsulta = 0;
     |ENCAMPO #0#0;
     |SI FSalida ! 0; |Y nSwConsulta = 0; |ACABA; |FINSI;

     |ABRE #120;
     #120#0 = #0#0;
     |LEE 000#120=;
     |CIERRA #120;
     |SI #120#5 ! 0;
          |MENAV "0000El recuento ya se actualizo.";
          |ACABA;
     |FINSI;

     nSwConsulta = 0;
     |ENCAMPO #3#0;
     |SI FSalida = 2; |Y nSwConsulta = 0; |VETE Fecha; |FINSI;
     fFecha = #0#0;
     nAlmaElige = #0#3;
     |SI #0#3 = 0;
          nDAlma = 1;
          nHAlma = 99999;
     |SINO;
          nDAlma = #0#3;
          nHAlma = #0#3;
     |FINSI;
     |POPV;
     |SI FSalida = 0; |O nSwConsulta = 1;
          |C_V #0#0, 1, "N";
          |C_M #0#3, 1, "N";
          |C_P #0#3, 1, nPosi;
          |ABRE #120;
          #120#0 = fFecha;
          |LEE 101#120=;
          |SI FSalida = 0;
               |SI #120#1 = "S";
                    |MENAV "0000Inventario Actualizado...";
               |SINO;
                    |CONTROL_SIMPLE 0, "BOTON, Recoger recuento", 0557, 0577, Boton;
                    nBoton = FSalida;
                 |ET Otra;
                    |HAZ CreaTempo; aTempo0 = Tempo;
                    |NOME_DAT #0 aTempo0; |DELINDEX #0;
                    |ABRE #0;

                    |BUCLE dsm00121;
                    |CIERRA #0;
                    |PARA nSw = 1; |SI nSw = 1; |HACIENDO;
                         nSw = 0;
                         |ENTLINEAL #1#0, -2, 2, 20, 0, Min_42, Max_42;
                         |SI nTecla = 14;
                              nTecla = 0; nSw = 1;
                              |HAZ Imprime;
                         |FINSI;
                    |SIGUE;
                    |DELINDEX #0; Tempo = aTempo0; |HAZ BoraTempo;
                    |SI nTecla = 13;
                         nTecla = 0;
                         |HAZ Recalcula;
                         |VETE Otra;
                    |FINSI;
                    |FIN_CONTROL_WINDOWS nBoton;
               |FINSI;
          |FINSI;
          |CIERRA #120;
     |FINSI;
|FPRO;

|PROCESO ImportaPDA;
     |DDEF aAlfa; aAlfa = aAlfa + "psion067";
     |CARGA_DEF aAlfa, referen@;
     |SI FSalida ! 0; |ACABA; |FINSI;
     |NOME_DAT #referen@#eaFichero#;
     |ABRE #referen@;
     |ABRE #19;
     |ABRE #121;
     |LEE 000#referen@.p;
     |PARA; |SI FSalida = 0; |HACIENDO;
          #0#0 = fFecha;
          #0#1 = #referen@#0; ||Arti
          |DDEFECTO #19;
          #19#0 = #0#1;
          |LEE 000#19=;
          #0#3 = #referen@#2; ||Alma
          |LEE 101#0=;
          |SI FSalida ! 0;
               #0#2 = #referen@#1; ||Des
               #0#7 = #19#5;
               |GRABA 020#0b;
          |FINSI;
          #0#5 = #0#5 + #referen@#3; ||Uni
          #0#6 = #0#5 - #0#4;
          |GRABA 020#0a;
          |LIBERA #0;

          #121#0 = #0#0;
          #121#1 = #0#1;
          #121#3 = #0#3;
          |LEE 101#121=;
          |SI FSalida ! 0;
               #121#2 = #0#2;
               #121#7 = #0#7;
               #121#8 = #19#9;
               |GRABA 020#121b;
          |FINSI;
          #121#5 = #0#5;
          #121#6 = #0#6;
          #121#15 = #19#28;
          #121#16 = #19#9;
          #121#17 = #19#133;
          |GRABA 020#121a;
          |LIBERA #121;

          |LEE 000#referen@.s;
     |SIGUE;
     |CIERRA #19;
     |CIERRA #121;
     |CIERRA #referen@;
     |DELINDEX #referen@;
     |DESCARGA_DEF #referen@;
|FPRC;

|PROCESO Tmp;
     #19#0 = #0#1;
     |LEE 000#19=;
     |SI FSalida ! 0; |DDEFECTO #19; |FINSI;

     enPCM = #19#9;

     |PRINT;
|FPRC;

|DEFBUCLE Tmp;
     #0#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, Tmp;
|FIN;

|PROCESO Imprime;
     |IMPRE 0;
     |SI FSalida = 0;
          |INFOR "dsz00042";
          |SI FSalida = 0;
               |ABRE #19;
               |BUCLE Tmp;
               |CIERRA #19;
               |PIEINF;
               |FININF;
          |SINO;
               |MENAV "0000Error en informe  'dsz00042'";
          |FINSI;
          |FINIMP;
     |FINSI;
|FPRC;
