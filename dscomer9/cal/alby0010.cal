|FICHEROS;
     alby0010 #0;
     agifa104 #104;
     agifa073 #73;
     albm0004 #4;
     agifa024 #24;
     agifa060 #60;
     agifa019 #19;
     albw0036 #22,MANTE; ||Tmp
     referen@ #100;
     agifa142 #142;

     albw0038 #38;       ||Aux. ArticuloCantida
     albw0039 #39;       ||Aux. ArticuloPedido
|FIN;

|VARIABLES;
     aPrograma = "";
     &eaTempo38 = "";
     &eaTempo39 = "";

     nSwHay = 0;
     aSerie = "";
     nVentana = 0;

     aPinta = "";
     aEsc1  = "";
     aEsc2  = "";
     aRetu  = "";
     aTecla = "";

     &enContador = 0;
     &eaNomDef = "";

     nLineal = 0;
     aMensaje = "";
     nModoLin = 0;
     nPos1 = 0;
     nPos2 = 0;

     nTecla = 0;
     nGuarda = 0;
     nConta = 0;
     aAlfa = "";
     nUni = 0;
     nHay = 0;
     &enPed = 0;
     aCli = "";
     aFam = "";
     fHoy = @;
     i = 0;
     &Tempo = "";
     aTempo1 = "";
     aDef = "";

     aTempo38 = "";
     aTempo39 = "";

     nLectura = 0;
     nWid     = 0;
     nWidGuarda = 0;
     nId      = 0;
     nIdGuarda = 0;
     nSid     = 0;
     nSidGuarda = 0;
|FIN;

|PROCESO Print;
     |SI #22#18 ! 0;
          nHay = 1;
          |PRINT;
     |FINSI;
|FPRC;

|DEFBUCLE Print;
     #22#2;
     ;
     INICIO;
     FINAL;
     ;
     NULL, Print;
|FIN;

|PROCESO Lee;
     |DDEFECTO #24;
     #24#0 = aCli;
     |LEE 000#24=;

     |DDEFECTO #60;
     #60#0 = aFam;
     |LEE 000#60=;
|FPRC;

|PROCESO albm0004;
     |DDEFECTO #19;
     #19#0 = #4#3;
     |LEE 000#19=;

     aFam = #19#2;
     aCli = #4#2;
     |HAZ Lee;

     nConta = nConta + 1;
     |DDEFECTO #22;
     #22#0 = nConta;
     #22#1 = 2;
     #22#4 = #24#0;
     #22#5 = #24#1;
     #22#6 = #4#3;
     #22#7 = #4#5;
     #22#8 = #60#0;
     #22#9 = #60#1;
     #22#10 = #4#0;
     #22#11 = #4#13;
     #22#12 = #4#6;
     #22#16 = #4#8
     |GRABA 020#22n;
     nHay = 1;
|FPRC;

|DEFBUCLE albm0004;
     #4#1;
     ;
     #0#4, "        ";
     #0#5, "zzzzzzzz";
     ;
     NULL, albm0004;
|FIN;

|PROCESO agifa073;
     aCli = #104#4;
     aFam = #73#18;
     |HAZ Lee;
     |SI #73#42 > 0;
          nUni = (#73#42 - #73#43 + #73#12) ! #142#8;
          |SI nUni ! 0;
               nConta = nConta + 1;
               |DDEFECTO #22;
               #22#0 = nConta;
               #22#1 = 1;
               #22#2 = #73#28;
               #22#3 = #73#0;
               #22#4 = #24#0;
               #22#5 = #24#1;
               #22#6 = #73#2;
               #22#7 = #73#4;
               #22#8 = #60#0;
               #22#9 = #60#1;
               #22#10 = #104#1;
               #22#11 = #73#13;
               #22#12 = #73#42;
               #22#13 = #73#43;
               #22#14 = (#73#42 - #73#43 + #73#12) ! #142#8;
               #22#15 = #104#7;
               #22#16 = #73#6;
               #22#17 = #104#51;
               |GRABA 020#22n;
               nHay = 1;
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE agifa104;
     #104#1;
     1;
     #0#0, #0#2, #0#4;
     #0#1, #0#3, #0#5;
     ;
     NULL, NULL, agifa073;
|FIN;

|PROCESO NoRepe; TIPO 0;
     |SELECT #2#100;
     #100#18 = #22#18;
     |LEE 000#100=;
     |SI FSalida = 0;
          |MENAV "0000Orden de Programacion ya introducida...";
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO Tecla; |TIPO 0;
     |SI FSalida = 0; |Y #22Campo = Contenido;
          nGuarda = #22#18;
          |ENCAMPO #18#22;
          |SI FSalida = 0;
               |GRABA 020#22a;
          |SINO;
               #22#18 = nGuarda;
          |FINSI;
          |PINTA #22#18;
     |FINSI;
|FPRC;

|PROCESO Min;
     #22#0 = 0;
     #22#2 = "     ";
     #22#3 = 0;
|FPRC;

|PROCESO Max;
     #22#0 = 999999;
     #22#2 = "zzzzz";
     #22#3 = 999999;
|FPRC;

|PROCESO PinchaLinea;
     nTecla = FSalida;
     |SI nTecla = 4;
          |PUSHV 2001 2799;
          enContador = #22#0;
          eaNomDef = aTempo1;
          |SUB_EJECUTA_NP "albw0037";
          |POPV;
          |REFRESCACONTROL nLineal;
     |FINSI;
|FPRC;

|PROCESO LlenaTmp;
     |SI #22#18 ! 0;
          aSerie = #22#2;
          |QBF aSerie;
          |SI aSerie = ""; |Y #22#3 = 0;
               ||Nada, solo se tienen en cuenta los Pedidos. NO LOS PRE-PEDIDOS
          |SINO;
               |DDEFECTO #albw0038;
               #albw0038#0 = #22#6;
               |LEE 101#albw0038.=;
               |SI FSalida ! 0; |GRABA 020#albw0038.b; |FINSI;
               #albw0038#1 = #albw0038#1 + #22#14;
               |GRABA 020#albw0038.a;
               |LIBERA #albw0038;

               |DDEFECTO #albw0039;
               #albw0039#0 = #22#6;
               #albw0039#1 = #22#2;
               #albw0039#2 = #22#3;
               |LEE 101#albw0039.=;
               |SI FSalida ! 0; |GRABA 020#albw0039.b; |FINSI;
               #albw0039#3 = #albw0039#3 + #22#14;
               |GRABA 020#albw0039.a;
               |LIBERA #albw0039;

               nSwHay = 1;
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE LlenaTmp;
     #22#2;
     ;
     INICIO;
     FINAL;
     ;
     NULL, LlenaTmp;
|FIN;

|PROCESO Tipo3; |TIPO 3;
     |ABRE #142; |LEE 000#142p; |CIERRA #142;

     |DDEF aDef; aDef = aDef + "albw0036";
     |CARGA_DEF aDef, referen@;
     |SI FSalida ! 0; |ACABA; |FINSI;

     |MENSA "0000Creando temporal...";
     |HAZ CreaTempo; aTempo1 = Tempo;
     |NOME_DAT #22 aTempo1;
     |NOME_DAT #100 aTempo1;
     |DELINDEX #22;

     |ABRE #24;
     |ABRE #60;
     |ABRE #22;
     |ABRE #19;
     |BUCLE agifa104;
     |BUCLE albm0004;
     |CIERRA #19;
     |CIERRA #22;
     |CIERRA #60;
     |CIERRA #24;
     |SI nHay = 0;
          |MENAV "0000Seleccion Vacia...";
     |SINO;
          |MENSA "0000                   ";
          |ABRE #100;

          |VENTANA 0501, 2999, -1, -1, "";
          nVentana = FSalida;

          |ABRE #22;
          nModoLin = 4 + 8 + 16 + 32;
          nPos1 = 0502;
          nPos2 = 2299;
          |LINEAL_SIMPLE #3#22, nModoLin, nPos1, nPos2, Min, Max, PinchaLinea;
          nLineal = FSalida;

          nLectura = 0;
          |PCONTEXTO nLectura, nWidGuarda, nIdGuarda, nSidGuarda;

          aPinta = "(DobleClick para introducir Numero Orden)";
          |HAZ PintaMensa;

          aEsc1  = &255 + "806";
          aEsc2  = &255 + "807";
          aRetu  = &255 + "802";
          |PARA; |SI;  |HACIENDO;
              FSalida = "::Salir";
              |LEETECLA aTecla;
              |SI aTecla = aEsc1; |SAL;  |FINSI;
              |SI aTecla = aEsc2; |SAL;  |FINSI;
              |SI aTecla = aRetu; |SAL;  |FINSI;
          |SIGUE;

          |DESTRUYECONTROL nLineal;
          |PULSATECLA;

          aPinta = "                                         ";
          |HAZ PintaMensa;

          |CIERRA #22;

          |FINVENTANA nVentana;

          |CIERRA #100;

          |CONFI "0000SImprimir?";
          |SI FSalida = 0;
               |IMPRE 0;
               |SI FSalida = 0;
                    |INFOR "alby0010";
                    |SI FSalida ! 0;
                         |MENAV "0000No existe informe 'alby0010'...";
                    |SINO;
                         |MENSA "0000Imprimiendo...";
                         nHay = 0;
                         |BUCLE Print;
                         |SI nHay ! 0; |PIEINF; |FINSI;
                         |FININF;
                    |FINSI;
                    |FINIMP;
               |FINSI;
          |FINSI;

          aMensaje = "0000NCrear las ordenes de Fabricacion?";
          |CONFI aMensaje;
          |SI FSalida = 0;
               |HAZ CreaTempo; aTempo38 = Tempo;
               |HAZ CreaTempo; aTempo39 = Tempo;
               |NOME_DAT #38 aTempo38;
               |NOME_DAT #39 aTempo39;
               |DELINDEX #38;
               |DELINDEX #39;


               |ABRE #38;
               |ABRE #39;
               nSwHay = 0;
               |BUCLE LlenaTmp;
               |CIERRA #38;
               |CIERRA #39;

               |SI nSwHay = 1;
                    eaTempo38 = aTempo38;
                    eaTempo39 = aTempo39;
                    aPrograma = "albz0024";
                    |SUB_EJECUTA_NP aPrograma;
               |FINSI;
               ||TODO CCC

               |DELINDEX #38;
               |DELINDEX #39;
               Tempo = aTempo38; |HAZ BoraTempo;
               Tempo = aTempo39; |HAZ BoraTempo;
          |FINSI;
     |FINSI;


     |DELINDEX #22;
     Tempo = aTempo1; |HAZ BoraTempo;
     |DESCARGA_DEF #referen@;
|FPRC;


|PROCESO PintaMensa;

     nLectura = 1;
     nWid     = nWidGuarda;
     nId      = nIdGuarda;
     nSid     = nSidGuarda;
     |PCONTEXTO nLectura, nWid, nId, nSid;

     |ATRI N;
     |PINTA 2320, aPinta;
     |ATRI n;

     nLectura = 1;
     |PCONTEXTO nLectura, nWidGuarda, nIdGuarda, nSidGuarda;
|FPRC;

|PROCESO Tipo80; |TIPO 80;
     FSalida = 2799;
|FPRC;
