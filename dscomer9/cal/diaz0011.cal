|FICHEROS;
     diaz0011 #0;
     diam0015 #15;
     diam0016 #16;
     diam0017 #17;
     diaw0008 #99,MANTE;

     :/dsp/def/dspm0004 #204;
|FIN;

|VARIABLES;
     aAlfa          = "";
     aAlfa1         = "";
     aAlfa2         = "";
     aAlfa3         = "";
     aDPed          = "";
     aHPed          = "";
     aFam           = "";
     aUsuario       = "";
     aOrigen        = "";
     aDestino       = "";
     aRuta          = "";
     aExtension     = "";
     aBase          = "";
     aDirDocumentos = "";
     aPunto         = "";
     aAbre          = "";
     aDocumentos    = "";

     nPanta       = 0;
     nSalir       = 0;
     nGridFichero = -1;
     nRango       = 0;
     nBoton1      = 0;
     nBoton2      = 0;
     nBoton3      = 0;
     nBoton4      = 0;
     nLinea       = 0;
     nModo        = 0;
     nForma       = 0;
     nHandle      = 0;
|FIN;

|PROCESO Tipo80;  |TIPO 80;
     FSalida = 3199;
|FPRC;

|PROCESO Pedido;
     |C_M #99#14, 1, "S";

     |SI #99#13 = "N";
         |C_M #99#14, 1, "N";
         #99#14 = "";
         |PINTA #99#14;
     |FINSI;
|FPRC;

|PROCESO Alta;  |TIPO 0;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 1;
         |MENAV "     No se permite dar de Alta.";
         |ERROR;
     |FINSI;
|FPRC;

|PROCESO TotalCoste;
     #99#12 = #99#8 * #99#11;
     |PINTA #99#12;
|FPRC;

|PROCESO Tipo2;  |TIPO 2;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 3;
         |MENAV "     No se permite dar de Baja.";
         |ERROR;
         |ACABA;
     |FINSI;

     nForma = 0 +. 1;

     |SI nForma ! 1;  |ACABA;  |FINSI;

     |ABRE #17;
     #17#0 = #99#1;
     #17#1 = #99#2;
     #17#2 = #99#3;
     #17#3 = #99#4;
     #17#4 = #99#5;
     |LEE 101#17=;
     |SI FSalida = 0;
         #17#10 = #99#11;
         #17#11 = #99#12;
         #17#12 = #99#13;
         #17#13 = #99#14;
         #17#14 = #99#15;
         #17#15 = #99#16;
         |GRABA 020#17a;
         |LIBERA #17;
     |FINSI;
     |CIERRA #17;
|FPRC;

|PROCESO EventoGrid;
     |SI FSalida = 1;
         |LEE 040#99c;
         |SI FSalida ! 0;
             |ESTADO_CONTROL nBoton2, "DISABLE";
             |ESTADO_CONTROL nBoton3, "DISABLE";
             |ESTADO_CONTROL nBoton4, "DISABLE";
             |ACABA;
         |FINSI;

         aAlfa = #99#15;  |QBF aAlfa;
         |SI aAlfa = "";
             |ESTADO_CONTROL nBoton2, "ENABLE";
             |ESTADO_CONTROL nBoton3, "DISABLE";
             |ESTADO_CONTROL nBoton4, "DISABLE";
         |SINO;
             |ESTADO_CONTROL nBoton3, "ENABLE";
             |ESTADO_CONTROL nBoton4, "ENABLE";
             |ESTADO_CONTROL nBoton2, "DISABLE";
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO Boton2;
     |LEE 101#99=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     aRuta = "F";
     |BROWSE aRuta;  |QBF aRuta;
     |SI aRuta = "F";  |ACABA;  |FINSI;

     aRuta = aRuta % 0299;
     aExtension = (aRuta % -103) > "";
     aPunto     = aRuta % -401;
     |SI aPunto ! ".";
         |MENAV "0000 Documento sin Extension. Formato exigido extension de 3 caracteres.";
         |ERROR;
         |ACABA;
     |FINSI;

     |DBASE aBase;  |QBF aBase;
     aDirDocumentos = aBase + "000577/documentos";
     |MKDIR aDirDocumentos;

     aOrigen  = aRuta;
     aAlfa1   = (#99#2 % -104) + (#99#2 % 402) + (#99#2 % 102);
     aAlfa2   = (#99#3 %  102) + (#99#3 % 402) + (#99#3 % 702);
     aAlfa3   = ("000" + #99#5) %  -103;
     aDestino = aDirDocumentos + "/" + #99#1 + aAlfa1 + aAlfa2 + aAlfa3 + "." + aExtension;
     |RECIBE_FICHERO aOrigen, aDestino;

     #99#15   = #99#1 + aAlfa1 + aAlfa2 + aAlfa3 + "." + aExtension;
     #99#16   = Usuario % 0810;

     |GRABA 020#99a;
     |LIBERA #99;

     |ABRE #17;
     #17#0 = #99#1;
     #17#1 = #99#2;
     #17#2 = #99#3;
     #17#3 = #99#4;
     #17#4 = #99#5;
     |LEE 101#17=;
     |SI FSalida = 0;
         #17#14 = #99#15;
         #17#15 = #99#16;
         |GRABA 020#17a;
         |LIBERA #17;
     |FINSI;
     |CIERRA #17;

     |ESTADO_CONTROL nBoton3, "ENABLE";
     |ESTADO_CONTROL nBoton4, "ENABLE";
     |ESTADO_CONTROL nBoton2, "DISABLE";
|FPRC;

|PROCESO Boton3;
     |LEE 040#99=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |DBASE aBase;  |QBF aBase;
     aDirDocumentos = aBase + "000577/documentos";
     |MKDIR aDirDocumentos;

     aOrigen  = "C:\DOCUMENTOS";
     |RMKDIR aOrigen;

     aAbre = aOrigen + "\wait.exe";
     |XABRE "EC", aAbre, nHandle;
     |SI FSalida < 0;
         aDestino = aBase + "000577/wait.exe";  |QBF aDestino;
         aOrigen  = "C:\DOCUMENTOS\wait.exe";
         |ENVIA_FICHERO aDestino, aOrigen;
         |SI FSalida ! 0;
             |MENAV "      No existe el wait en el directorio de Documentos.";
             |ACABA;
         |FINSI;
     |FINSI;

     aDestino = aDirDocumentos + "/" + #99#15;  |QBF aDestino;
     aOrigen  = "C:\DOCUMENTOS\" + #99#15;      |QBF aOrigen;
     |ENVIA_FICHERO aDestino, aOrigen;
     |SI FSalida = 0;
         |RSYSTEM aOrigen;

         |RECIBE_FICHERO aOrigen, aDestino;

         #99#16 = Usuario % 0810;
     |FINSI;

     |RFBORRA aOrigen;
|FPRC;

|PROCESO Boton4;
     |CONFI "0000N Seguro de Borrar el Documento ";
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |LEE 101#99=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |DBASE aDocumentos;  |QBF aDocumentos;
     aDocumentos = aDocumentos + "000577/documentos";
     aDocumentos = aDocumentos + "/" + #99#15;  |QBF aDocumentos;
     |FBORRA aDocumentos;

     #99#15 = "";
     #99#16 = "";

     |GRABA 020#99a;
     |LIBERA #99;

     |ABRE #17;
     #17#0 = #99#1;
     #17#1 = #99#2;
     #17#2 = #99#3;
     #17#3 = #99#4;
     #17#4 = #99#5;
     |LEE 101#17=;
     |SI FSalida = 0;
         #17#14 = #99#15;
         #17#15 = #99#16;
         |GRABA 020#17a;
         |LIBERA #17;
     |FINSI;
     |CIERRA #17;

     |ESTADO_CONTROL nBoton2, "ENABLE";
     |ESTADO_CONTROL nBoton3, "DISABLE";
     |ESTADO_CONTROL nBoton4, "DISABLE";
|FPRC;

|PROCESO PasaTemporal;
     nLinea = nLinea + 1;

     #16#0  = #17#0;
     #16#1  = #17#1;
     #16#13 = #17#2;
     |LEE 040#16=;
     |SI FSalida ! 0;  |DDEFECTO #16;  |FINSI;

     #99#0  = nLinea;
     #99#1  = #17#0;
     #99#2  = #17#1;
     #99#3  = #17#2;
     #99#4  = #17#3;
     #99#5  = #17#4;
     #99#6  = #17#5;
     #99#7  = #17#6;
     #99#8  = #17#7;
     #99#9  = #17#8;
     #99#10 = #17#9;
     #99#11 = #17#10;
     #99#12 = #17#11;
     #99#13 = #17#12;
     #99#14 = #17#13;
     #99#15 = #17#14;
     #99#16 = #17#15;
     #99#17 = #16#3;
     |GRABA 020#99n;
     |LIBERA #99;
|FPRC;

|DEFBUCLE diam0017;
     #17#1;
     12, 3;
     #0#0, #0#2, "        ", 001, aDPed, aFam;
     #0#1, #0#3, "zzzzzzzz", 999, aHPed, aFam;
     ;
     NULL, PasaTemporal;
|FIN;

|PROCESO Boton1;
     |SI nGridFichero ! -1;
         |CIERRAT #99;

         |FIN_CONTROL_WINDOWS nBoton2;
         |FIN_CONTROL_WINDOWS nBoton3;
         |FIN_CONTROL_WINDOWS nBoton4;

         |DESTRUYECONTROL nGridFichero;
         nGridFichero = -1;

         |PULSATECLA;
     |FINSI;

     aAlfa = Usuario; |QBF aAlfa; aAlfa = "8" + aAlfa;
     |NOME_DAT #99 aAlfa;
     |DELINDEX #99;

     aDPed = #0#4;
     aHPed = #0#4;
     |SI #0#4 = "T";
         aDPed = " ";
         aHPed = "z";
     |FINSI;

     aFam   = #15#37;
     nLinea = 0;

     |ABRE #99;
     |ABRE #16;
     |BUCLE diam0017;
     |CIERRA #16;
     |CIERRA #99;

     |SI nLinea = 0;
         |MENAV "0000 Seleccion Vacia";
         |DELINDEX #99;
         |ACABA;
     |FINSI;

     |CONTROL_SIMPLE nPanta, "BOTON,{{137}} Adjuntar Documento", 1001, 1020, Boton2;
     nBoton2 = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,{{135}} Ver Documento", 1022, 1040, Boton3;
     nBoton3 = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,{{199}} Borrar Documento", 1042, 1060, Boton4;
     nBoton4 = FSalida;

     |ESTADO_CONTROL nBoton2, "DISABLE";
     |ESTADO_CONTROL nBoton3, "DISABLE";
     |ESTADO_CONTROL nBoton4, "DISABLE";

     |ABRET #99;
     nRango = 1 + 4 + 8 + 16 + 32;
     |LINEAL_SIMPLE #1#99, nRango, 1101, 2998, NULL, NULL, EventoGrid;
     nGridFichero = FSalida;

     |PTEC 806;
|FPRC;

|PROCESO Tipo12;  |TIPO 12;
     |SI FSalida = 7;
         nSalir = 1;
         |ACABA;
     |FINSI;
|FPRC;

|PROGRAMA;
     aUsuario = Usuario % 810;
     |ABRE #204;
     #204#0 = aUsuario;
     |LEE 040#204=;
     |SI FSalida ! 0;  |DDEFECTO #204;  |FINSI;
     |CIERRA #204;

     |SI #204#7 ! "S";
         |MENAV "0000 Usted no tiene permiso para entrar en esta opcion.";
         |ACABA;
     |FINSI;

     |ABRE #15
     |LEE 040#15u;
     |SI FSalida ! 0;  |DDEFECTO #15;  |FINSI;
     |CIERRA #15;

     |CLS;
     |CABEZA "Situacion Pedidos Hard";

     |DDEFECTO #0;
     |PINPA #0#0;
     nPanta  = FSalida;

     |PINDA #0#0;

     |CONTROL_SIMPLE nPanta,"BOTON,{{137}}Refrescar Lineal", 0875, 0890, Boton1;
     nBoton1 = FSalida;

     nSalir   = 0;
     |PARA;  |SI nSalir = 0; |HACIENDO;
             |ENDATOS #1#0;
     |SIGUE;

     |SI nGridFichero ! -1;
         |FIN_CONTROL_WINDOWS nBoton2;
         |FIN_CONTROL_WINDOWS nBoton3;
         |FIN_CONTROL_WINDOWS nBoton4;

         |DESTRUYECONTROL nGridFichero;

         |CIERRAT #99;
         |DELINDEX #99;
     |FINSI;

     |FIN_CONTROL_WINDOWS nBoton1;
     |PULSATECLA;
|FPRO;
