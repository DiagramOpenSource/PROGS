|FICHEROS;
     tecaz003 #0;
     tecam005 #5;
     tecam006 #6;
     tecam007 #7;
     tecam008 #8;
     tecam012 #12;
     agifa010 #10;
     agifa017 #17;
     agifa019 #19;
     dsm00020 #20;
     agifa071 #71;
     agifa104 #104;
     agifa073 #73;
     agifa024 #24;
     agifa025 #25;
     agifa321 #321;
     agifa324 #324;
     dsm00053 #53;
     agifa007 #70;
|FIN;

|VARIABLES;
     &eaLog = "";
     &enCoste = 0;
     &enSwMenu = 0;
     &aMoneda = "";
     &aDivisa = "";
     &enForma = 0;
     aTipo = "";
     aParam = "";
     nLote = 0;
     ||nLin7 = 0;
     nLin8 = 0;
     nAge = 0;
     aCli = "";
     nLinAlb = 0;
     aAlf1 = "";
     aAlf2 = "";
     aPath = "";
     aFic = "";
     aReg = "";
     nHandle = 0;
     nPosi = 0;
     i = 0;
     aAlfa = "";
     nLon = 0;
     aDes = "";
     nHay = 0;
     nConta = 0;
     aSer = "";
     nNum = 0;
     nKilos = 0;
     nBultos = 0;
|FIN;

|PROCESO tecam008_2;
     #53#0 = "A";
     #53#1 = #71#29;
     #53#2 = #71#0;
     #53#3 = #71#1;
     #53#4 = #8#4;
     #53#5 = #8#5;
     |GRABA 020#53n;
|FPRC;

|DEFBUCLE tecam008_2;
     #8#1;
     ;
     #7#0, #7#1, #7#2, #7#3, -99999;
     #7#0, #7#1, #7#2, #7#3, 99999;
     ;
     NULL, tecam008_2;
|FIN;

|PROCESO ActPedido;
     #73#43 = #73#43 + #7#8;

     |ABRE #19;
     #19#0 = #73#2;
     |LEE 101#19=;
     |SI FSalida = 0;
          #19#15 = #19#15 - #7#8;
          |GRABA 020#19a;

          |ABRE #17;
          #17#0 = #73#2;
          #17#1 = #73#3;
          |LEE 101#17=;
          |SI FSalida = 0;
               #17#42 = #17#42 - #7#8;
               |GRABA 020#17a;
          |FINSI;
          |LIBERA #17;
          |CIERRA #17;
     |FINSI;
     |LIBERA #19;
     |CIERRA #19;
|FPRC;

|PROCESO FinCab;
     |SI aCli ! "";
          |HAZ LimCabAgifa010;
          |BUCLELIN 0 CalCabAgifa010 #10;

          aAlfa = #10#56; |QBF aAlfa;
          aAlfa = aAlfa + " " + nKilos;
          #10#56 = aAlfa;
          |GRABA 020#10a;
          nKilos = 0;
     |FINSI;
|FPRC;

|PROCESO Cabecera;
     |DDEFECTO #70;
     #70#26 = #104#25;
     |LEE 000#70=;

     |DDEFECTO #24;
     #24#0 = #104#4;
     |LEE 000#24=;

     |DDEFECTO #25;
     #25#0 = #104#7;
     |LEE 000#25=;

     |DDEFECTO #10;
     #10#52 = #104#25;
     #10#0 = 0;
     enSwMenu = 1;
     |HAZ ContaAV7;
     #10#3 = #104#4;
     #10#4 = #104#8;
     #10#5 = #104#5;
     #10#6 = #104#7;
     #10#7 = #104#6;
     #10#8 = #104#9;
     |DDEFECTO #20;
     #20#0 = #6#4;
     |LEE 000#20=;
     #10#9 = #20#1;
     #10#88 = #20#0;

     #10#29 = #104#19;
     #10#30 = #104#14;
     #10#31 = #104#20;
     #10#32 = #104#17;
     #10#33 = #104#21;
     #10#34 = #25#7;
     #10#35 = #24#4;
     #10#36 = #24#47;
     #10#40 = #24#41;
     #10#50 = #24#40;
     #10#51 = #70#28;     ||Facturable
     #10#55 = #104#53;
     #10#56 = #104#54;
     #10#57 = #104#55;
     #10#62 = #104#59;
     #10#63 = #24#15;
     #10#68 = #104#35;  ||Divisa
     #10#83 = #104#51;
     #10#91 = #24#206;  ||Rete.

     #324#0 = #10#68;
     |LEE 000#324=;

     #10#69 = #324#2; ||Cambio
     #10#70 = #104#37;
     #10#73 = #321#9; ||Moneda Base

     #324#0 = #10#73;
     |LEE 000#324=;

     #10#74 = #324#2; ||Cambio
     #10#91 = #24#206;
     |GRABA 020#10b;

     aDivisa = #10#68;
     aMoneda = #10#70;
     |HAZ Divisas010;
|FPRC;

|PROCESO Linea;
     #10#41 = #10#41 + 1;          ||Control pedido
     #10#2 = #10#2 + nBultos;
     nBultos = 0;

     |ABRE #19;
     |DDEFECTO #19;
     #19#0 = #7#4;
     |LEE 000#19=;
     |CIERRA #19;

     |SI #7#8 = 0; |ACABA; |FINSI; ||Servidas

     nLinAlb = nLinAlb + 1;
     |DDEFECTO #71;
     #71#29 = #10#52;
     #71#0 = #10#0;
     #71#1 = nLinAlb;
     |LEE 101#71=;
     |SI FSalida ! 0;    |GRABA 020#71b;     |FINSI;

     #71#2 = #73#2;
     #71#3 = #73#3;
     #71#4 = #73#4;
     #71#5 = #7#8;
     #71#6 = #73#6;
     #71#8 = #73#8;
     #71#10 = #73#10;
     #71#11 = #73#14;
     #71#12 = #73#0;
     #71#13 = #73#18;
     #71#15 = #104#4;
     #71#16 = #73#17;
     #71#17 = #73#19;
     #71#21 = #73#1;
     #71#27 = #73#27;
     #71#31 = #73#28;
     #71#33 = #73#29;
     #71#36 = #73#38;
     #71#38 = #73#41;
     |HAZ TotalLin71;

     enForma = 1;
     |HAZ AcumulaAV;
     #71#14 = enCoste;

     |GRABA 020#71a;
     |LIBERA #71;
|FPRC;

|PROCESO tecam007_2;
     #104#25 = #7#1;
     #104#0 = #7#2;
     |LEE 101#104=;
     |SI FSalida ! 0; |ACABA; |FINSI;

     #73#28 = #7#1;
     #73#0 = #7#2;
     #73#1 = #7#3;
     |LEE 101#73=;
     |SI FSalida ! 0; |ACABA; |FINSI;

     |SI aCli ! #6#5; |O nAge ! #6#4;
          |HAZ FinCab;
          |HAZ Cabecera;
          nLinAlb = 0;
          aCli = #6#5;
          nAge = #6#4;
     |FINSI;

     |HAZ ActPedido;
     |HAZ Linea;

     |ABRE #53;
     |BUCLE tecam008_2;
     |CIERRA #53;

     |GRABA 020#73a;
     |LIBERA #73;

     aDivisa = #104#35;
     aMoneda = #104#37;
     |HAZ Divisas104;
     |HAZ CalCabAgifa104;
     |GRABA 020#104a;
     |LIBERA #104;
|FPRC;

|PROCESO tecam006_2;
     nKilos = #6#18;
     nBultos = #6#16;
|FPRC;

|PROCESO tecam006_2Fin;
     #6#19 = #10#52;
     #6#20 = #10#0;
     |GRABA 020#6a;
     |LIBERA #6;
|FPRC;

|DEFBUCLE tecam006_2;
     #6#1;
     ;
     nLote, aSer, nNum;
     nLote, aSer, nNum;
     be;
     NULL, tecam006_2, tecam007_2, tecam006_2Fin;
|FIN;

|PROCESO AdjPed;
     eaLog = "Adjudica Lote:" + (("00000"+nLote)%-105) + " Pedido:" + aSer + "/" + nNum;
     |HAZ TecaGrabaLog;
     |ABRE #321; |LEE 000#321p; |CIERRA #321;
     |ABRE #324;
     |ABRE #104;
     |ABRET #73;
     |ABRE #20;
     |ABRE #24;
     |ABRE #70;
     |ABRE #25;
     |ABRE #10;
     |ABRET #71;
     |BUCLE tecam006_2;
     |HAZ FinCab;
     |CIERRAT #71;
     |CIERRA #10;
     |CIERRA #25;
     |CIERRA #70;
     |CIERRA #24;
     |CIERRA #20;
     |CIERRAT #73;
     |CIERRA #104;
     |CIERRA #324;
|FPRC;
ÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ
S                ( letra S  ) -> indica el tipo de datos que van a continuación
NÚMERO SERIE     ( A20      ) -> Número de serie de uno de los artículos

|PROCESO tecam008;
     |DDEFECTO #8;
     nLin8 = nLin8 + 1;
     aAlfa = aReg % 220; #8#5 = aAlfa;

     #8#0 = #7#0;
     #8#1 = #7#1;
     #8#2 = #7#2;
     #8#3 = #7#3;
     #8#4 = nLin8;
     |GRABA 020#8n;
|FPRC;

L                ( letra L  ) -> indica el tipo de datos que van a continuación
CÓDIGO ARTÍCULO  ( A20      ) -> Código en la gestión del artículo
DESCRIPCIÓN      ( A50      ) -> Descripción del artículo
LOCALIZACIÓN     ( A50      ) -> Localización en el almacén del artículo
UNIDADES PEDIDAS ( N8       ) -> Unidades pedidas de este artículo
UNIDADES SERVIDAS( N8       ) -> Unidades que ya se han servido
STOCK            ( N8       ) -> Número de unidades de este artículo disponibles en el almacén
PESO             ( N8       ) -> Peso especificado en la gestión para el artículo
CÓDIGO BARRAS    ( A20      ) -> Código de barras que debe tener el artículo
NÚMERO SERIE     ( A1 S/N   ) -> Si el palm deberá pedir la introducción del número de serie el artículo o no

|PROCESO tecam007;
     |DDEFECTO #7;
     ||nLin7 = nLin7 + 1;
     aAlfa = aReg %   220; #7#4 = aAlfa;
     aAlfa = aReg %  2250; #7#5 = aAlfa;
     aAlfa = aReg %  7250; #7#9 = aAlfa;
     aAlfa = aReg % 12208; #7#6 = aAlfa;
     aAlfa = aReg % 13008; #7#8 = aAlfa;
     aAlfa = aReg % 13808;
     aAlfa = aReg % 14608; #7#10 = aAlfa;
     aAlfa = aReg % 15420; #7#11 = aAlfa;
     aAlfa = aReg % 17401; #7#13 = aAlfa;
     aAlfa = aReg % 17503; #7#3 = aAlfa;
     #7#0 = #6#0;
     #7#1 = #6#1;
     #7#2 = #6#2;
     ||#7#3 = nLin7;
     #7#7 = #7#6 - #7#8;
     |GRABA 020#7n;
|FPRC;

C                ( letra C  ) -> indica el tipo de datos que van a continuación
NÚMERO DE PALM   ( A2       ) -> Valor de 00 a 99 que indica el código del palm al que este pedido está asignado
NÚMERO DE LOTE   ( N5       ) -> Valor de 0 a 7 que indica a que lote está asignado
NÚMERO DE PEDIDO ( N6       ) -> Valor de 0 a 9 que indica a que pedido del lote está asignado
CÓDIGO AGENCIA   ( N2       ) -> Código de la agencia si es relevante (este campo se podrá cambiar en el palm)
FECHA PEDIDO     (DD.MM.AAAA) -> Fecha de emisión del pedido
CÓDIGO CLIENTE   ( N6       ) -> Código del cliente según está en la gestión
NOMBRE CLIENTE   ( A30      ) -> Nombre del cliente según está en la gestión
ALBARÁN ANTERIOR ( A1  S/N  ) -> Si el palm había recibido un albarán como este
SERIE PEDIDO     ( A5       )
BULTOS           ( N8       ) -> Número de paquetes que componen el pedido cerrado
KILOS            ( N8       ) -> Número de kilos totales que supone el pedido

|PROCESO tecam006;
     |DDEFECTO #6;
     aAlfa = aReg % 0202;                    || palm (solo palm)
     aAlfa = aReg % 0405; #6#0 = aAlfa;      || lote
     aAlfa = aReg % 1502; #6#4 = aAlfa;      || agencia
     aAlfa = aReg % 1710; #6#3 = aAlfa;      || fecha pedido
     aAlfa = aReg % 2706; #6#5 = aAlfa;      || codigo cliente
     aAlfa = aReg % 3330; #6#6 = aAlfa;      || nombre
     aAlfa = aReg % 6301;                    || albaran anterior (solo palm)
     aAlfa = aReg % 6405; #6#1 = aAlfa;      || serie pedido
     aAlfa = aReg % 6906; #6#2 = aAlfa;      || numero pedido
     aAlfa = aReg % 7508; #6#16 = aAlfa;     || bultos
     aAlfa = aReg % 8308; #6#18 = aAlfa;     || gramos
     ||#6#18 = #6#18 / 1000;                   || (se pasan a kilos)
                    || ya no hay que pasar a kilos (tiquet 323-192)

     |ABRE #104;
     #104#25 = #6#1;
     #104#0 = #6#2;
     |LEE 000#104=;
     |CIERRA #104;

     |ABRE #20;
     #20#0 = #6#4;
     |LEE 000#20=;
     |CIERRA #20;

     #6#7 = #104#14;
     #6#8 = #104#20;
     #6#9 = #104#59;
     #6#10 = #104#21;
     #6#11 = #104#26;
     #6#12 = #104#51;
     #6#13 = #104#53;
     #6#14 = #104#54;
     #6#15 = #104#55;
     #6#17 = #20#1;
     |GRABA 020#6n;
|FPRC;

|PROCESO Procesa;
     aTipo = aReg % 101;
     |SI aTipo = "C";
          |SI aSer ! ""; |HAZ AdjPed;  |FINSI;
          |HAZ tecam006;
          ||nLin7 = 0;
          nLote = #6#0;
          aSer = #6#1;
          nNum = #6#2;
          ||ÄÄÄÄÄÄÄÄÄÄÄÄ
          |SELECT #2#5;
          #5#2 = #6#1;
          #5#3 = #6#2;
          #5#0 = #6#0;
          |LEE 121#5=;
          |SI FSalida = 0;
               #5#6 = "R";
               |GRABA 020#5a;
               |LIBERA #5;
          |FINSI;
          nHay = 1;
     |FINSI;
     |SI aTipo = "L";
          nLin8 = 0;
          |HAZ tecam007;
     |FINSI;
     |SI aTipo = "S";
          |HAZ tecam008;
     |FINSI;
|FPRC;

|PROCESO Tipo3; |TIPO 3;
     |SI #0#2 ! "SI"; |ACABA; |FINSI;

     |ABRET #5;
     |ABRET #6;
     |ABRET #7;
     |ABRE #8;
     aPath = #0#1; |QBF aPath;
     aFic = aPath + "p*.txt";
     |_PDIR aFic;
     |PARA; |SI FSalida = 0; |HACIENDO;
          aDes = aFic - ".txt";
          aDes = aDes + ".his";
          |COPIA_FICHERO aFic, aDes;
          |SI FSalida ! 0;
               eaLog = "Error al mover " + aFic + "->" + aDes;
               |HAZ TecaGrabaLog;
          |SINO;
               eaLog = "Tratando fichero " + aFic;
               |HAZ TecaGrabaLog;
               |XABRE "U", aDes, nHandle;
               |SI FSalida ] 0;
                    |XLEE nHandle, 250, aReg;
                    |PARA; |SI FSalida ] 0; |HACIENDO;
                         |HAZ Procesa;
                         |XLEE nHandle, 250, aReg;
                    |SIGUE;
                    |XCIERRA nHandle;
                    |FBORRA aFic;
               |SINO;
                    eaLog = "Error al abrir " + aFic;
                    |HAZ TecaGrabaLog;
               |FINSI;
          |FINSI;
          |_SDIR aFic;
     |SIGUE;

     |SI aSer ! ""; |HAZ AdjPed; |FINSI;

     |CIERRA #8;
     |CIERRAT #7;
     |CIERRAT #6;
     |CIERRAT #5;
|FPRC;

|PROCESO PonBarra; |TIPO 0;
     aAlf1 = #0#1; |QBF aAlf1;
     aAlf2 = aAlf1 % -101;
     |SI aAlf2 ! "/"; |Y aAlf2 ! "\";
          aAlf1 = aAlf1 + "\";
          #0#1 = aAlf1;
          |PINTA #0#1;
     |FINSI;
|FPRC;

|PROGRAMA;
     |ABRE #12; |LEE 000#12p; |CIERRA #12;

     aAlfa = Usuario;
     aParam = PARAMETRO;

     |ABRE #0;
     |LEE 101#0=;
     |SI FSalida ! 0; |GRABA 020#0b; |FINSI;
     |SI aParam ! "";
          #0#1 = #12#8;
          #0#2 = "SI";
          |HAZ Tipo3;
     |SINO;
          |CLS;
          #0#1 = #12#8;
          #0#2 = "NO";
          |PINPA #0#0;
          |PINDA #0#0;
          |ENDATOS #1#0;
          |SI FSalida = 0;
               |GRABA 020#0a;
          |FINSI;
     |FINSI;
     |CIERRA #0;
|FPRO;
