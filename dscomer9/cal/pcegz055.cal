|FICHEROS;
     pcegz055 #55;  || Este

     pcegz047 #0;
     pcegz048 #1;
     pcegm029 #29;
     pcegm034 #34;
     agifa142 #142;
     pcegz047@;
     pcegz048@;
     gestempo;
|FIN;

|VARIABLES;
     nPanta0 = 0;
     nClave = 0;
     nGrid0 = 0;
     nGrid1 = 1;
     nBtnAsigna = 0;
     aEsc1 = "";
     aEsc2 = "";
     aRetu = "";
     aTecla = "";

     &Tempo = "";
     i = 0;
     aTmp0 = "";
     aTmp1 = "";
     nHayPed = 0;
     nHayCob = 0;
     nCodPed = 0;
     nCodCob = 0;
     nSal = 0;
     aAlfa = "";
     nRango = 0;
     nPos1 = 0;
     nPos2 = 0;
     aFich = "";
     aEmp = "";
     aFichero = "";
     nHandle = 0;
     aTmp = "";
|FIN;

|PROCESO BtnAsigna;
     |SI nCodCob = 0; |Y nCodPed = 0;
          |MENAV "0000Debe de marcar el cobro y el pedido";
          |ACABA;
     |SINO;
          |SI nCodCob = 0;
               |MENAV "0000Debe de marcar el cobro";
               |ACABA;
          |FINSI;
          |SI nCodPed = 0;
               |MENAV "0000Debe de marcar el pedido";
               |ACABA;
          |FINSI;
     |FINSI;

     |CONFI "0000N¿Asignar cobro al pedido?";
     |SI FSalida ! 0; |ACABA; |FINSI;

     #0#0 = nCodPed;
     |BORRA 020#0c;
     #1#0 = nCodCob;
     |BORRA 020#1c;

     nCodPed = 0;
     nCodCob = 0;

     |REFRESCACONTROL nGrid0;
     |REFRESCACONTROL nGrid1;

     |ABRE #55;
     #55#0 = #0#0;
     #55#1 = #1#0;
     |GRABA 020#55n;
     |CIERRA #55;
|FPRC;

|PROCESO QuitaMarca1;
     #pcegz048@#17 = "";
     |GRABA 020#pcegz048@.a;
|FPRC;

|DEFBUCLE QuitaMarca1;
     #pcegz048@#1001;
     ;
     000000000000;
     999999999999;
     ;
     NULL, QuitaMarca1;
|FIN;

|PROCESO Desmarca1;
     |DDEF aAlfa;
     aAlfa = aAlfa + "pcegz048";
     |CARGA_DEF aAlfa, pcegz048@;
     |SI FSalida = 0;
          |PATH_DAT #pcegz048@#aFich#;
          |NOME_DAT #pcegz048@#aTmp1#;
          |BUCLE QuitaMarca1;
          |DESCARGA_DEF #pcegz048@;
     |FINSI;
|FPRC;

|PROCESO QuitaMarca0;
     #pcegz047@#43 = "";
     |GRABA 020#pcegz047@.a;
|FPRC;

|DEFBUCLE QuitaMarca0;
     #pcegz047@#1001;
     ;
     0000000;
     9999999;
     ;
     NULL, QuitaMarca0;
|FIN;

|PROCESO Desmarca0;
     |DDEF aAlfa;
     aAlfa = aAlfa + "pcegz047";
     |CARGA_DEF aAlfa, pcegz047@;
     |SI FSalida = 0;
          |PATH_DAT #pcegz047@#aFich#;
          |NOME_DAT #pcegz047@#aTmp0#;
          |BUCLE QuitaMarca0;
          |DESCARGA_DEF #pcegz047@;
     |FINSI;
|FPRC;

|PROCESO EventoGrid1;
     |SI FSalida = 4;
          |LEE 000#1=;
          |SI FSalida ! 0; |ACABA; |FINSI;
          |SI #1#17 = "*";
               #1#17 = "";
               nCodCob = 0;
          |SINO;
               |SI nCodCob ! 0;
                    |HAZ Desmarca1;
               |FINSI;
               #1#17 = "*";
               nCodCob = #1#0;
          |FINSI;
          |GRABA 020#1a;
          |REFRESCACONTROL nGrid1;
     |FINSI;
|FPRC;

|PROCESO EventoGrid0;
     |SI FSalida = 4;
          |LEE 000#0=;
          |SI FSalida ! 0; |ACABA; |FINSI;
          |SI #0#43 = "*";
               #0#43 = "";
               nCodPed = 0;
          |SINO;
               |SI nCodPed ! 0;
                    |HAZ Desmarca0;
               |FINSI;
               #0#43 = "*";
               nCodPed = #0#0;
          |FINSI;
          |GRABA 020#0a;
          |REFRESCACONTROL nGrid0;
     |FINSI;
|FPRC;

|PROCESO Cobros;
     |PARA i = 0; |SI i [ 16; |HACIENDO i = i + 1;
          #1i = #34i;
     |SIGUE;
     |GRABA 020#1n;
     nHayCob = 1;
|FPRC;

|DEFBUCLE Cobros;
     #34#3;
     11;
     0, " ";
     0, " ";
     ;
     NULL, Cobros;
|FIN;

|PROCESO Pedidos;
     |PARA i = 0; |SI i [ 42; |HACIENDO i = i + 1;
          #0i = #29i;
     |SIGUE;
     |GRABA 020#0n;
     nHayPed = 1;
|FPRC;

|DEFBUCLE Pedidos;
     #29#3;
     ;
     "T", "worldpay", 0;
     "T", "worldpay", 0;
     ;
     NULL, Pedidos;
|FIN;

|PROCESO Min0;
     #0#0 = 0000000;
|FPRC;

|PROCESO Max0;
     #0#0 = 9999999;
|FPRC;

|PROCESO Min1;
     #1#0 = 000000000000;
|FPRC;

|PROCESO Max1;
     #1#0 = 999999999999;
|FPRC;

|PROCESO Tipo80; |TIPO 80;
     FSalida = 3499;
|FPRC;

|PROCESO LeeTxt;
     |DBASE aFich;
     aFich = aFich + "fich/";
     aFichero = aFich + "R" + (Usuario % 810);
     |XABRE "", aFichero, nHandle;

     |XLEE nHandle, 250, aEmp;
     |XLEE nHandle, 250, aTmp;
     |XCIERRA nHandle;
|FPRC;

|PROGRAMA;
     |CLS;
     |PINPA #0#55;  nPanta0 = FSalida;
     |PINDA #0#55;
     |HAZ LeeTxt;

     |NOME_DAT #55 aTmp;
     |HAZ CreaTempo; |NOME_DAT #0 Tempo; aTmp0 = Tempo; |DELINDEX #0;
     |HAZ CreaTempo; |NOME_DAT #1 Tempo; aTmp1 = Tempo; |DELINDEX #1;
     |ABRE #0
     |BUCLE Pedidos;
     |CIERRA #0;
     |ABRE #1;
     |BUCLE Cobros;
     |CIERRA #1;
     |SI nHayPed ! 0 ; |Y nHayCob ! 0;
          |ABRET #0;
          |ABRET #1;

          nRango = 4 + 8 + 16 + 32;
          nPos1 = 0902;
          nPos2 = 1999;
          nClave = 4;
          |LINEAL_SIMPLE #1#0, nRango, nPos1, nPos2, Min0, Max0, EventoGrid0;
          nGrid0 = FSalida;

          nRango = 4 + 8 + 16 + 32;
          nPos1 = 2201;
          nPos2 = 3299;
          |LINEAL_SIMPLE #1#1, nRango, nPos1, nPos2, Min1, Max1, EventoGrid1;
          nGrid1 = FSalida;

          |CONTROL_SIMPLE nPanta0, "BOTON,{{193}}Asignar", 3302, 3317, BtnAsigna;
          nBtnAsigna = FSalida;

          aEsc1 = &255 + "806";
          aEsc2 = &255 + "807";
          aRetu = &255 + "802";
          |PARA; |SI;  |HACIENDO;
              FSalida = "::{{001}}Salir," + nGrid0;
              |LEETECLA aTecla;
              |SI aTecla = aEsc1; |SAL;  |FINSI;
              |SI aTecla = aEsc2; |SAL;  |FINSI;
              |SI aTecla = aRetu; |SAL;  |FINSI;
          |SIGUE;

          |CIERRAT #1;
          |CIERRAT #0;

          |DESTRUYECONTROL nGrid0;
          |DESTRUYECONTROL nGrid1;
          |FIN_CONTROL_WINDOWS nBtnAsigna;
     |SINO;
          |SI nHayPed = 0; |MENAV "0000No hay pedidos pendientes"; |FINSI;
          |SI nHayCob = 0; |MENAV "0000No hay cobros pendientes"; |FINSI;
     |FINSI;
     Tempo = aTmp0; |HAZ BoraTempo; |DELINDEX #0;
     Tempo = aTmp1; |HAZ BoraTempo; |DELINDEX #1;

     |PULSATECLA;
|FPRO;
