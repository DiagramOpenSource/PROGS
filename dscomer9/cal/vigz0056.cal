|FICHEROS;
     vigz0056 #0;
     vigm0042 #1; || Parametros
     vigm0043 #2; || Histo
     vigw0030 #3; || Tmp Regulariza

     agifa010 #10;
     agifa019 #19;
     agifa024 #24;
     dsm00043 #43;
     dsm00044 #44;
     agifa059 #59;
     agifa065 #65;
     agifa071 #71;
     agifa134 #134;

     :dscarter/dscam003 #103
     :dscarter/dscam045 #145;

     agifa010@;
     agifa059@;
     agifa071@;
     agifa134@;
     dscam003@;
     agifa065@;
|FIN;

|VARIABLES;
     nError = 0;
     aOrg = "";
     aDes = "";
     aPathLocal = "";
     fFecha = @;
     aFic = "";
     aPathTmp = "";
     aPathFic = "";
     aDefGes = "";
     aDefCar = "";
     aAlfa = "";
     nLinea = 0;
     aOpe = "";
     aTipo = "";
     aSerie = "";
     nConta = 0;
     nEmpresa = 0;
     aEmpre = "";
     aEmpGes = "";
|FIN;

|PROCESO Recibos;
     |SALVA_DATOS #103;
     |REPON_DATOS #dscam003@;
     #dscam003@#46 = aEmpGes;
     |GRABA 020#dscam003@.n;
     |BORRA 020#103a;
|FPRC;

|DEFBUCLE Recibos;
     #103#12;
     ;
     #134#49, #134#0, 001, "01.01.0000", "             ", #134#2;
     #134#49, #134#0, 999, "31.12.9999", "zzzzzzzzzzzzz", #134#2;
     be;
     NULL, Recibos;
|FIN;

|PROCESO Cartera;
     |SI #134#49 ] #145#4; |Y #134#49 [ #145#5;
          |DBASS aAlfa;
          aEmpre = ("00000" + #145#6) % -105;
          aPathFic = aAlfa + "dscarter/fich/" + aEmpre + "/";
          |PATH_DAT #103#aPathFic#;
          |BUCLE Recibos;
     |FINSI;
|FPRC;

|DEFBUCLE Cartera;
     #145#1;
     2;
     nEmpresa, 001, "V";
     nEmpresa, 999, "V";
     ;
     NULL, Cartera;
|FIN;

|PROCESO Historico;
     #2#0 = #71#29;
     #2#1 = #71#0;
     #2#2 = #71#1;
     #2#3 = #59#14;
     #2#4 = #59#0;
     #2#5 = #59#1;
     #2#6 = #10#1;
     #2#7 = #134#1;
     #2#8 = #10#3;
     #2#9 = #24#3;
     #2#10 = #10#6;
     #2#11 = #19#2;
     #2#12 = #19#0;
     |SI aTipo = "E";
          #2#13 = -#71#5;
     |SINO;
          #2#13 = #71#5;
     |FINSI;
     #2#14 = #71#6;
     #2#15 = #71#9;
     #2#16 = #71#14;
     #2#17 = #71#3;
     #2#18 = #10#5;
     #2#19 = #10#7;
     #2#20 = #10#32;
     |GRABA 020#2n;
|FPRC;

|PROCESO Conta43;
     #43#4 = aSerie;
     #43#0 = 999999;
     |LEE 000#43];
     |SI FSalida = 0;
          |LEE 000#43a;
     |SINO;
          |LEE 000#43u;
     |FINSI;
     |SI FSalida = 0; |Y #43#4 = aSerie;
          nConta = #43#0 + 1;
     |SINO;
          nConta = 1;
     |FINSI;
     |DDEFECTO #43;
     #43#0 = nConta;
     #43#4 = aSerie;
     #43#1 = #3#1;
|FPRC;

|PROCESO GraRegulariza;
     |SI nLinea = 0; |O nLinea > 99999; |O fFecha ! #3#1;
          nLinea = 0;
          aSerie = #0#0;
          fFecha = #3#1;
          |HAZ Conta43;
          |GRABA 020#43n;
     |FINSI;
     nLinea = nLinea + 1;

     |DDEFECTO #19;
     #19#0 = #3#3;
     |LEE 000#19=;

     |DDEFECTO #44;
     #44#21 = #43#4;
     #44#18 = #43#0;
     #44#8 = nLinea;

     #44#7 = #3#2;
     #44#0 = #3#3;
     #44#1 = #3#4;
     #44#4 = #3#5;
     #44#5 = #43#1;
     #44#7 = #3#7;
     |GRABA 020#44n;

     |DDEFECTO #65;
     #65#0 = #44#0;
     #65#1 = #44#5;
     #65#2 = #3#6;
     #65#3 = #44#21;
     #65#4 = #44#18;
     #65#11 = #44#8;

     #65#5 = #44#1;
     |SI #44#7 = "S";
          #65#6 = -#44#4;
     |SINO;
          #65#6 = #44#4;
     |FINSI;
     #65#8 = #19#9;

     |GRABA 020#65n;
|FPRC;

|PROCESO Regulariza;
     nLinea = nLinea + 1;
     #3#0 = nLinea;
     #3#1 = #10#1;
     #3#2 = aTipo;
     #3#3 = #71#2;
     #3#4 = #71#3;
     #3#5 = #71#5;
     #3#6 = aOpe;
     #3#7 = aTipo;
     |GRABA 020#3n;
|FPRC;

|PROCESO agifa071;
     |SALVA_DATOS #71;
     |REPON_DATOS #agifa071@;
     |GRABA 020#agifa071@.n;
     |BORRA 020#71a;

     #65#0 = #71#2;
     #65#1 = #10#1;
     |SI #10#43 = "S";
          aTipo = "E";
          aOpe = "ES";
          #65#2 = "BV";
     |SINO;
          aTipo = "S";
          aOpe = "SS";
          #65#2 = "AV";
     |FINSI;
     #65#3 = #71#0;
     #65#4 = #71#1;
     #65#11 = #71#29;
     |LEE 101#65=;
     |SI FSalida = 0;
          |SALVA_DATOS #65;
          |REPON_DATOS #agifa065@;
          |GRABA 020#agifa065@.n;

          |BORRA 020#65a;
          |LIBERA #65;
     |FINSI;

     |DDEFECTO #19;
     #19#0 = #71#2;
     |LEE 000#19=;

     |DDEFECTO #24;
     #24#0 = #10#3;
     |LEE 000#24=;

     |HAZ Regulariza;
     |HAZ Historico;
|FPRC;

|PROCESO agifa010;
     |SALVA_DATOS #10;
     |REPON_DATOS #agifa010@;
     |GRABA 020#agifa010@.n;
     |BORRA 020#10a;
     |LIBERA #10;
|FPRC;

|DEFBUCLE agifa010;
     #10#1;
     ;
     #59#15, #59#2;
     #59#15, #59#2;
     be;
     NULL, agifa010, agifa071;
|FIN;

|PROCESO agifa059;
     |SALVA_DATOS #59;
     |REPON_DATOS #agifa059@;
     |GRABA 020#agifa059@.n;
     |BORRA 020#59a;
     |LIBERA #59;
     |BUCLE agifa010;
|FPRC;

|PROCESO agifa134;
     |BUCLE Cartera;
     |SALVA_DATOS #134;
     |REPON_DATOS #agifa134@;
     |GRABA 020#agifa134@.n;
     |BORRA 020#134a;
     |LIBERA #134;
|FPRC;

|DEFBUCLE agifa134;
     #134#3;
     ;
     "      ", #0#3, #0#0, #0#1;
     "zzzzzz", #0#4, #0#0, #0#2;
     be;
     NULL, agifa134, agifa059;
|FIN;

|DEFBUCLE GraRegulariza;
     #3#2;
     ;
     INICIO;
     FINAL;
     ;
     NULL, GraRegulariza;
|FIN;

|PROCESO CopiaSer;
     aOrg = aPathLocal + aFic + ".dat";
     aDes = aPathTmp + aFic + ".dat";
     |RECIBE_FICHERO aOrg, aDes;
     |SI FSalida ! 0; nError = 1; |FINSI;

     aOrg = aPathLocal + aFic + ".ixx";
     aDes = aPathTmp + aFic + ".ixx";
     |RECIBE_FICHERO aOrg, aDes;
     |SI FSalida ! 0; nError = 1; |FINSI;

     aOrg = aPathLocal + aFic + ".ddx";
     aDes = aPathTmp + aFic + ".ddx";
     |RECIBE_FICHERO aOrg, aDes;
     |SI FSalida ! 0; nError = 1; |FINSI;
|FPRC;

|PROCESO CopiaServidor;
     aPathLocal = #1#1; |QBF aPathLocal;
     aFic = "agifa010"; |HAZ CopiaSer;
     aFic = "agifa059"; |HAZ CopiaSer;
     aFic = "agifa071"; |HAZ CopiaSer;
     aFic = "agifa134"; |HAZ CopiaSer;
     aFic = "agifa065"; |HAZ CopiaSer;

     aPathLocal = #1#2; |QBF aPathLocal;
     aFic = "dscam003"; |HAZ CopiaSer;
|FPRC;

|PROCESO CopiaPue;
     aOrg = aPathTmp + aFic + ".dat";
     aDes = aPathLocal + aFic + ".dat";
     |ENVIA_FICHERO aOrg, aDes;

     aOrg = aPathTmp + aFic + ".ixx";
     aDes = aPathLocal + aFic + ".ixx";
     |ENVIA_FICHERO aOrg, aDes;

     aOrg = aPathTmp + aFic + ".ddx";
     aDes = aPathLocal + aFic + ".ddx";
     |ENVIA_FICHERO aOrg, aDes;
|FPRC;

|PROCESO CopiaPuesto;
     aPathLocal = #1#1; |QBF aPathLocal;
     aFic = "agifa010"; |HAZ CopiaPue; ||DELINDEX #agifa010@;
     aFic = "agifa059"; |HAZ CopiaPue; ||DELINDEX #agifa059@;
     aFic = "agifa071"; |HAZ CopiaPue; ||DELINDEX #agifa071@;
     aFic = "agifa134"; |HAZ CopiaPue; ||DELINDEX #agifa134@;
     aFic = "agifa065"; |HAZ CopiaPue; ||DELINDEX #agifa065@;

     aPathLocal = #1#2; |QBF aPathLocal;
     aFic = "dscam003"; |HAZ CopiaPue; ||DELINDEX #dscam003@;
|FPRC;

|PROCESO Tipo3; |TIPO 3;
     |DFICE aPathTmp;
     aAlfa = aPathTmp % -205;
     nEmpresa = aAlfa;
     aPathTmp = aPathTmp + "T/";
     |MKDIR aPathTmp;

     |DDEF aDefGes;
     |DBASS aAlfa;
     aDefCar = aAlfa + "dscarter/def/";

     |ABRE #1; |LEE 000#1p; |CIERRA #1;

     |HAZ CopiaServidor;
     |SI nError = 1;
          |MENAV "0000Error al copiar los ficheros al servidor, proceso anulado";
          |ACABA;
     |FINSI;

     aAlfa = #1#2; |QBF aAlfa;
     aEmpGes = aAlfa % -205;

     aAlfa = aDefGes + "agifa010"; |CARGA_DEF aAlfa, agifa010@;
     aAlfa = aDefGes + "agifa059"; |CARGA_DEF aAlfa, agifa059@;
     aAlfa = aDefGes + "agifa071"; |CARGA_DEF aAlfa, agifa071@;
     aAlfa = aDefGes + "agifa134"; |CARGA_DEF aAlfa, agifa134@;
     aAlfa = aDefGes + "agifa065"; |CARGA_DEF aAlfa, agifa065@;
     |PATH_DAT #agifa010@#aPathTmp#;
     |PATH_DAT #agifa059@#aPathTmp#;
     |PATH_DAT #agifa071@#aPathTmp#;
     |PATH_DAT #agifa134@#aPathTmp#;
     |PATH_DAT #agifa065@#aPathTmp#;
     |ABRE #agifa010@;
     |ABRE #agifa059@;
     |ABRE #agifa071@;
     |ABRE #agifa134@;
     |ABRE #agifa065@;

     aAlfa = aDefCar + "dscam003"; |CARGA_DEF aAlfa, dscam003@;
     |PATH_DAT #dscam003@#aPathTmp#;
     |ABRE #dscam003@;

     aFic = aPathTmp + "agifa010.dat";
     |XABRE "E", aFic, 0;
     |SI FSalida ! 0;
          |MENAV "0000ERROR en carpeta gestion. No se pude abrir el fichero";
     |SINO;
          aFic = aPathTmp + "dscam003.dat";
          |XABRE "E", aFic, 0;
          |SI FSalida ! 0;
               |MENAV "0000ERROR en carpeta cartera. No se pude abrir el fichero";
          |SINO;
               |DBASS aAlfa;
               aPathFic = aAlfa + "dscarter/fich/";
               |PATH_DAT #145 aPathFic;

               aAlfa = Usuario;
               |NOME_DAT #3 aAlfa; |DELINDEX #3;

               |ABRE #65;
               |ABRE #43;
               |ABRE #44;
               |ABRE #19;
               |ABRE #24;
               |ABRE #2;
               |ABRE #3;
               |BUCLE agifa134;
               |CIERRA #3;
               nLinea = 0;
               |BUCLE GraRegulariza;
               |CIERRA #2;
               |CIERRA #24;
               |CIERRA #19;
               |CIERRA #44;
               |CIERRA #43;
               |CIERRA #65;

               |DELINDEX #3;

               |HAZ CopiaPuesto;
          |FINSI;
     |FINSI;

     |CIERRA #dscam003@;
     |DESCARGA_DEF #dscam003@;

     |CIERRA #agifa065@;
     |CIERRA #agifa134@;
     |CIERRA #agifa071@;
     |CIERRA #agifa059@;
     |CIERRA #agifa010@;
     |DESCARGA_DEF #agifa065@;
     |DESCARGA_DEF #agifa134@;
     |DESCARGA_DEF #agifa071@;
     |DESCARGA_DEF #agifa059@;
     |DESCARGA_DEF #agifa010@;
|FPRC;
