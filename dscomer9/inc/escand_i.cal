Los ficheros agifa048 y agifa049, hay que declararlos en el calculo
     original, para asi no tener que abrir y cerrar constantemente el fichero

El proceso inicial se llama ''InicioEscan'', que es el encargado de
     poner a cero las pilas e iniciar la lectura de los escandallos.
     y pasarle la variable aEscanOri (escandallo original).
     la variable nError, se inicializa a 0, cuando toma el valor 1 se
     interrumpe la lectura del escandallo.


El proceso LeeEscandallo es Recursivo, por lo cual se llama a el mismo
     por cada escandallo nuevo que se encuentre.

Existen 3 procesos que deben estar declarados en el calculo principal

     - EntrarPuntero --> Este proceso se llama cada vez que se encuentra
                         una linea que es escandallo, para poder incluir
                         nuevos punteros, en caso de ser necesarios.
                         SE BAJA UN NIVEL EN LA ESTRUCTURA DEL ESCANDALLO.

     - SacarPuntero  --> Se llama cada vez que se necesita retomar la
                         lectura del ultimo escandallo.
                         SE SUBE UN NIVEL EN LA ESTRUCTURA DEL ESCANDALLO

     - EscanLinea    --> Se ejecuta para cada linea de materia prima.

|VARIABLES;
     || Pilas de control de recursividad.
     {100}PaEscan     = "";

     aEscan           = "";
     aEscanLeido      = "";

     {100}PnLinea     = 0;

     &nError          = 0;
     &nSaltaEscan     = 0;

     i                = 0;
     nLinea           = 0;
|FIN;

|PROCESO LeeEscandallo;
     #agifa049#0 = aEscan;
     #agifa049#1 = nLinea;
     |LEE 001#agifa049.];
     aEscanLeido = #agifa049#0; |QBF aEscanLeido;

     |PARA;|SI FSalida = 0;|Y aEscan = aEscanLeido; |Y nError = 0; |HACIENDO;
          || Verifico si es escandallo
          #agifa048#0 = #agifa049#2;
          |LEE 001#agifa048.=;
          |SI FSalida = 0;

               |HAZ EntrarPuntero;

               || Guardo valores sobre pila actual y aumento pila
               |SI nSaltaEscan = 0;          ||Si el escandallo Leido
                                             ||es correcto

                    PaEscan = #agifa049#0;
                    PnLinea = #agifa049#1;
                    IPaEscan = IPaEscan + 1;
                    IPnLinea = IPnLinea + 1;
                    || Variables para la nueva llamada a LeeEscandallo
                    aEscan = #agifa049#2;  |QBF aEscan;
                    nLinea = 1;
                    || Nueva llamada en modo recursivo
                    |HAZ LeeEscandallo;

                    || Disminuimos Pila para recoger valores del nivel anterior
                    || y cargamos sobre variables de control
                    IPaEscan = IPaEscan -1;
                    IPnLinea = IPnLinea -1;
                    aEscan = PaEscan;  |QBF aEscan;
                    nLinea = PnLinea;

                    |HAZ SacarPuntero;
                    nSaltaEscan = 0;
               |SINO;
                    aEscan = #agifa049#0;     |QBF aEscan;
                    nLinea = #agifa049#1;
                    nSaltaEscan = 0;
               |FINSI;
          |SINO;
               nLinea = #agifa049#1;
               aEscan = #agifa049#0;  |QBF aEscan;
               ||En este punto el escandallo es MATERIA

               |HAZ EscanLinea;

          |FINSI;

          || Leer siguiente con nuevos valores
          #agifa049#0 = aEscan;
          #agifa049#1 = nLinea;
          |LEE 001#agifa049.=;
          |LEE 001#agifa049.s;
          aEscanLeido = #agifa049#0; |QBF aEscanLeido;
     |SIGUE;
|FPRC;

|PROCESO InicioEscan;
     || Inicializamos pila
     IPaEscan  = PaEscan1<-;
     IPnLinea  = PnLinea1<-;

     || Lo pasamos por atmp porque no se puede cortar sobre una pila
     aEscan = aEscanOri;
     nLinea = 1;
     |HAZ LeeEscandallo;

|FPRC;
