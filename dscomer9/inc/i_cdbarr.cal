|| Halla Articulo a partir del codigo de barras

|FICHEROS;
     dsm00010 #910;
     dsm00040 #940;
     agifa019 #19;
     dsl00001 #66;
     dsm00014 #914;
|FIN;

|VARIABLES;
     aARTI          = "";
     aELEME         = "";
     aBARRA         = "";

     aCodBarra      = "";
     aBarra         = "";
     aAlfaCB        = "";
     aLon           = "";
     aProgra        = "";
     nLon           = 0;
     aMenErr        = "";
|FIN;

|PROCESO HallaBarra;
     |ABRE #910; |LEE 000#910p; |CIERRA #910;

     aCodBarra = aBARRA;
     aBarra = "";
     |QBF aCodBarra;

     |ABRE #940;
     |ABRE #19;
     |SELECT #2#940;
     #940#2 = aBARRA;
     |LEE 000#940=;
     |SI FSalida = 0;
          aARTI = #940#0;
          aELEME = #940#1;
          #19#0 = aARTI;
          |LEE 000#19=;
          aBARRA = #19#128;
          aBarra = aBARRA;
     |SINO;
          aLon = aCodBarra % A0;
          nLon = aLon;
          nLon = (100 + nLon - #910#21);
          aBarra = aCodBarra % nLon;

          nLon = -(100 + #910#21);
          aAlfaCB = aCodBarra % nLon;
          aELEME = aAlfaCB;
          |SI aCodBarra ! "";
               |SELECT #4#19;
               #19#128 = aBARRA;
               |LEE 000#19=;
               |SI FSalida = 0;
                    aELEME = "";
                    aBarra = aBARRA;
               |FINSI;
          |FINSI;
          |QBF aBarra;
          |SI aBarra = "";        || Si es blanco pongo basura
               aBarra = "*kk_kk*";|| (no creo que exista un CB como este !!!)
          |FINSI;
     |FINSI;
     |CIERRA #19;
     |CIERRA #940;
|FPRC;

|PROCESO HallaArti;
     aBARRA = eaArti;

     |HAZ HallaBarra;

     |QBF aBARRA;

     |ABRE #19;
     |SELECT #4#19;              ||Selecciono clave Codigo  Barras
     #19#128 = aBarra;
     |LEE 000#19=;
     |SI FSalida ! 0;
          #19#128 = aBARRA;
          |LEE 000#19=;
     |FINSI;
     |SI FSalida = 0;|Y aBARRA ! "";
          aARTI = #19#0;
     |SINO;
          |ABRE #66;
          |SELECT #2#66;
          #66#3 = aBarra;
          |LEE 001#66=;
          |SI FSalida ! 0;
               #66#3 = aBARRA;
               |LEE 001#66=;
          |FINSI;
          |SI FSalida = 0;|Y aBARRA ! "";
                |SELECT #1#19;
                #19#0 = #66#1;
                |LEE 001#19=;
                |SI FSalida = 0;
                    aARTI = #19#0;
                |FINSI;
          |SINO;
               |MODULO aProgra;
               |SI aProgra = "agifa077"; |O aProgra = "agifa081";
                         |O aProgra = "dsz99957";
                    |SI #910#19 = "S";
                         |AVISO; |MENAV "0000El Codigo no EXISTE...";
                    |FINSI;
               |SINO;
                    |AVISO; |MENAV "0000El Codigo no EXISTE...";
               |FINSI;
               aARTI = "";
          |FINSI;
          |CIERRA #66;
     |FINSI;
     |CIERRA #19;

     eaArti = aARTI;
     eaEleme = aELEME;

     |SI #19#176 = "S"; |Y eaEleme ! "";
          |ABRE #914;
          #914#0 = eaEleme;
          |LEE 000#914=;
          |SI FSalida ! 0;
               aMenErr = "0000Elemento [" + eaEleme + "] no definido...";
               |MENAV aMenErr;
               eaEleme = "";
          |FINSI;
          |CIERRA #914;
     |FINSI;
|FPRC;
