|FICHEROS;
     rem00056 #20;
     rew99998 #900,MANTE;
     espejo@ #99;
|FIN;

|VARIABLES;
     aAlfa         = "";
     aAlfa1        = "";
     aLong         = "";
     aQueQuiero    = "";
     aNumCampos    = "";
     aNumPanta     = "";
     aPathRenta    = "";
     aDef          = "";
     aTecla        = "";

     nLong         = 0;
     nCaracter     = 0;
     nPosi         = 0;
     nPanta        = 0;
     nNumCampos    = 0;
     nNumPanta     = 0;
     nCampo        = 0;
     nModo         = 0;
     nEntrada      = 0;

     &enEmpresa    = 0;
     &enModo       = 0;
     &eaDeclarante = "";
     &enComunes    = 0;
     &enMinusvalia = 0;
     &eaMinuAyuda  = "";
     &eaFallece    = "";
     &eaSwVarios   = "";
     &eaVarDni     = "";
     &enSwLiquida  = 0;
     &enSwRetorno  = 0;
     &eaPrograma   = "";
     &eaPunto      = "";
|FIN;

|PROCESO Tipo12w98;  |TIPO 12;
|FPRC;

|| ************************************************************************
|| PROCESOS DE CONSULTA DE PANTALLAS NORMALES
|| ************************************************************************

|PROCESO Modificable;
     |C_M #20#13, 1, "S";
     |SI #20#11 ! 0; |Y #20#11 ! 10; |Y #20#11 ! 11;
         |C_M #20#13, 1, "N";  #20#13 = 0;  |PINTA #20#13;
     |FINSI;

     |C_M #20#15, 1, "S";
     |SI #20#11 ! 9;
         |C_M #20#15, 1, "N";  #20#15 = 0;  |PINTA #20#15;
     |FINSI;
|FPRC;

|PROCESO CuadroTipo;
     |SI FSalida ! 10;  |ACABA;  |FINSI;

     |PUSHV 0501 2380;
     |BLANCO 0518 2380;
     |ATRI I;
     |PINTA 0519, " 0  ³Saldo neto neg.ganan.perd.patri.ejer.1999 a 2002 ³  30 ³";
     |PINTA 0619, " 1  ³Rdtos.irregulares negativos pdtes.compensar 1998 ³  32 ³";
     |PINTA 0719, " 3  ³Disminuciones patrimoniales procedentes 1998     ³  37 ³";
     |PINTA 0819, " 11 ³Saldo negativo gan.y perd.patrim.ejer.1999 a 2002³  38 ³";
     |PINTA 0919, " 2  ³Comp.Bases liquidables negativas de 1998         ³  52 ³";
     |PINTA 1019, " 10 ³Base liquidable general negativa de 1999 a 2002  ³  53 ³";
     |PINTA 1119, " 8  ³Rentas exentas excepto para determinar el Tipo G.³  56 ³";
     |PINTA 1219, " 13 ³Ded.por dotacion Reserva inversiones Canarias    ³82-83³";
     |PINTA 1319, " 12 ³Ded.rdtos.deriv.ventas bienes corporales Canarias³84-85³";
     |PINTA 1419, " 4  ³Deducciones por Rentas en Ceuta y Melilla        ³86-87³";
     |PINTA 1519, " 5  ³Doble imposicion internacional (Manual)          ³ 105 ³";
     |PINTA 1619, " 9  ³Doble imposicion internacional (Automatica)      ³ 105 ³";
     |PINTA 1719, " 6  ³Doble imposicion internacional transp.fiscal     ³ 106 ³";
     |PINTA 1819, " 7  ³Plan Prever                                      ³ 121 ³";
     |PINTA 1919, " 14 ³Cuota del Impuesto sobre la Renta de no Residente³ 122 ³";
     |PINTA 2019, " 15 ³Devoluciones por la tramitacion Modelo 104       ³ 127 ³";
     |PINTA 2119, " 17 ³Maternidad. Importe de la deduccion              ³ 125 ³";
     |PINTA 2219, " 18 ³Maternidad. Abono anticipado                     ³ 126 ³";
     |PINTA 2319, " 16 ³Rentas Exentas Reduccion del Trabajo             ³     ³";
     |ATRI i;

     |LEETECLA aTecla;
     |POPV;
     |ERROR;
|FPRC;

|PROCESO CuadroTRenta;
     |SI FSalida ! 10;  |ACABA;  |FINSI;

     |PUSHV 0501 2380;
     |CUADRO 1220 2380;
     |BLANCO 1321 2279;
     |ATRI I;
     |PINTA 1425, "1 General  -Renta en Extranjero  ";
     |PINTA 1525, "2 General  -Ingreso en Extranjero";
     |PINTA 1625, "3 Especial -Renta en Extranjero  ";
     |PINTA 1725, "4 Especial -Ingreso en Extranjero";
     |ATRI i;

     |LEETECLA aTecla;
     |POPV;
     |ERROR;
|FPRC;

|PROCESO TipoRenta;
     |SI #20#15 = 0;  |Y #20#11 = 9;
         |MENAV "    Introduzca el Tipo de Renta";
         |ERROR;
     |FINSI;
|FPRC;

|PROCESO Tipo11m20;  |TIPO 11;
     |SI FSalida = 11;
         |SI #20#2 ! "VARIOS    ";  |ACABA;  |FINSI;

         #900#0 = #20#5;
         #900#1 = #20#6;
         #900#2 = #20#7;
         #900#3 = #20#8;
         #900#4 = #20#9;
         #900#5 = #20#10;

         |PUSHV 0501 2380;
         |PINPA #0#900;
         |PINDA #0#900;
         |LEETECLA aTecla;
         |POPV;
         |ERROR;
     |FINSI;
|FPRC;

|PROCESO Varios;  |TIPO 0;
     |SI #20#2 ! "VARIOS    ";
         #20#5  = 0;
         #20#6  = 0;
         #20#7  = 0;
         #20#8  = 0;
         #20#9  = 0;
         #20#10 = 0;
         |ACABA;
     |FINSI;

     |C_M #900#0,  1, "N";
     |C_M #900#1,  1, "N";
     |C_M #900#2,  1, "N";
     |C_M #900#3,  1, "N";
     |C_M #900#4,  1, "N";
     |C_M #900#5,  1, "N";

     aAlfa = eaSwVarios;
     aLong = aAlfa % 0;
     nLong = aLong;
     |PARA nCaracter = 1;  |SI nCaracter [ nLong;  |HACIENDO nCaracter = nCaracter + 1;
           nPosi = (100 * nCaracter) + 1;
           aAlfa1 = aAlfa % nPosi;
           |SI aAlfa1 = "T";  |C_M #900#0,  1, "S";  |FINSI;
           |SI aAlfa1 = "C";  |C_M #900#1,  1, "S";  |FINSI;
           |SI aAlfa1 = "1";  |C_M #900#2,  1, "S";  |FINSI;
           |SI aAlfa1 = "2";  |C_M #900#3,  1, "S";  |FINSI;
           |SI aAlfa1 = "3";  |C_M #900#4,  1, "S";  |FINSI;
           |SI aAlfa1 = "4";  |C_M #900#5,  1, "S";  |FINSI;
     |SIGUE;

     #900#0 = #20#5;
     #900#1 = #20#6;
     #900#2 = #20#7;
     #900#3 = #20#8;
     #900#4 = #20#9;
     #900#5 = #20#10;

     |PUSHV 0501 2380;

     |PINPA #0#900;
     |PINDA #0#900;
     |ENDATOS #1#900;
     |SI FSalida = 0;
         #20#5  = #900#0;
         #20#6  = #900#1;
         #20#7  = #900#2;
         #20#8  = #900#3;
         #20#9  = #900#4;
         #20#10 = #900#5;
     |FINSI;

     |POPV;
|FPRC;

|RUTINA ClaveBaja20;
     #20#0 = enEmpresa;
     #20#1 = 1;
|FRUT;

|RUTINA ClaveAlta20;
     #20#0 = enEmpresa;
     #20#1 = 99;
|FRUT;

|PROCESO PonDeclarante;  |TIPO 7;
     eaDeclarante = #20#2;
     enComunes    = 0;
     |HAZ SacaDeclarantes;
     #20#2 = eaDeclarante;
     |PINTA #20#2;
|FPRC;

|PROCESO Fallece;  |TIPO 7;
     |SI eaFallece = "S";
         |C_M #20#4, 1, "S";
     |SINO;
         |C_M #20#4, 1, "N";  #20#4 = 0;  |PINTA #20#4;
     |FINSI;
|FPRC;

|PROCESO Tipo2m20;  |TIPO 2;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 1;  |O nModo = 3;
         enSwRetorno  = 1;
         |ACABA;
     |FINSI;

     nEntrada = 0 +. 1;
     |SI nEntrada ! 1;
         |PARA nCampo = 0;  |SI nCampo [ nNumCampos;  |HACIENDO nCampo = nCampo + 1;
               #99nCampo = #20nCampo;
         |SIGUE;
     |SINO;
         |PARA nCampo = 0;  |SI nCampo [ nNumCampos;  |HACIENDO nCampo = nCampo + 1;
               |SI #99nCampo ! #20nCampo;
                   enSwRetorno  = 1;
               |FINSI;
         |SIGUE;
     |FINSI;
|FPRC;

|PROGRAMA;
     aQueQuiero = "|NP";
     aNumPanta  = #20#aQueQuiero#;
     nNumPanta  = aNumPanta;
     nNumPanta  = nNumPanta - 1;

     aQueQuiero = "|NC";
     aNumCampos = #20#aQueQuiero#;
     nNumCampos = aNumCampos;
     nNumCampos = nNumCampos - 1;

     |DBASE aPathRenta;  |QBT aPathRenta;
     aDef = aPathRenta + "def/rem00056.mas";
     |CARGA_DEF aDef, espejo@;
     |SI FSalida ! 0;
         aAlfa = "     Error en cargar el Def rem00056.mas";
         |MENAV aAlfa;
         |ERROR;
     |FINSI;

     |ENTLINEAL #1#20, -2, enModo, 22, 1, ClaveBaja20, ClaveAlta20;

     |DESCARGA_DEF #espejo@;

     eaPrograma   = "rem00056";
     eaPunto      = "  ";
     |HAZ ProgramaActivo;
|FPRO;
