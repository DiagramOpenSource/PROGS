|FICHEROS;
     dsmoz068 #0;

     dsmom004 #4;
     dsmom008 #8;
     :/basica/def/agifigen #118;
     :/basica/def/agicen14 #214;

     dsmom030 #30;
     dsmo2347 #1;
     dsmo3347 #2;
     dsmo4347 #3;
     dsmom072 #72;
     dsmom72@ #972;

     :/contagen/def/canempre #101;
     :/contagen/def/camaasil #105;
     :/contagen/def/caclipro #106;
     :/contagen/def/camanefe #107;

     dsmow113 #913;
     &regisnif@ #709;
|FIN;

|VARIABLES;
     aDepar = "";
     aSubde = "";
     aParam = "";
     aSeg   = "";
     aRend  = "";
     aOp1   = "";
     aOp2   = "";
     aOp3   = "";
     nOpera = 0;

     nCampo     = 0;
     nDEmpresa  = 0;
     nHEmpresa  = 0;
     nDPeriodo  = 0;
     nHPeriodo  = 0;

     aAlfa      = "";
     aAlfa1     = "";
     aAlfa2     = "";
     nEmpresa   = 0;
     nEjer      = 0;
     nPeriodo   = 0;
     nModelo    = 0;
     nComplem   = 0;
     aDef       = "";
     aTipo      = "";
     aFichero   = "";
     nEjerCob   = 0;
     nHEjer     = 0;
     fLaFecha   = @;

     nCampo1 = 0;
     nCampo2 = 0;
     nCampo3 = 0;
     nCampo4 = 0;
     nCampo5 = 0;

     nSwDesde = 0;
     nSwModelo = 0;
     aNif      = "";
     nImporte  = 0;
     nSwHay    = 0;
     nEjerO    = 0;

     nOp       = 0;
     nImpArren = 0;
     nUltActiv = 0;
     nUltLinea = 0;
     nSwArren  = 0;
     nResto    = 0;
     nNume1    = 0;
|FIN;

|INCLUYE i_variar;

|| /////////////////////// PROCESOS ENTRADA POR PANTALLA \\\\\\\\\\\\\\\\\\\\
|PROCESO Tipo7;  |TIPO 7;
     |SI enEmpresa ! 0;
         #0#0 = enEmpresa;  |PINTA #0#0;
         #0#1 = enEmpresa;  |PINTA #0#1;

         |C_M #0#0, 1, "N";
         |C_M #0#1, 1, "N";
     |FINSI;
|FPRC;

|PROCESO Parrilla;
     |SI #0#0 = 0;
         #0#0 = 0;  |PINTA #0#0;
         #0#1 = 0;  |PINTA #0#1;  |C_M #0#1, 1, "N";
         |PARA nCampo = 2;  |SI nCampo [ 13;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |SINO;
         |SI enEmpresa = 0;
             |C_M #0#0, 1, "S";
             |C_M #0#1, 1, "S";
         |FINSI;

         |PARA nCampo = 2;  |SI nCampo [ 13;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "N";
               #0nCampo = 0;  |PINTA #0nCampo;
         |SIGUE;
     |FINSI;
|FPRC;

|PROCESO NoMasEmpresas;
     |SI #0Campo = 0;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 13;  |HACIENDO nCampo = nCampo + 1;
               #0nCampo = 0;  |C_M #0nCampo, 1, "N";
         |SIGUE;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 13;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |FINSI;
|FPRC;

|CALCULO Eano; |TIPO 7;
     fLaFecha = ~;
     aAlfa1 = fLaFecha; aAlfa1 = aAlfa1 % -104;
     #0#14 = aAlfa1; |PINTA #0#14;
|FCAL;

|CALCULO desde; |TIPO 0;
     |C_M #0#16,1,"S";
     |SI #0#15 ! 1; |C_M #0#16,1,"N"; |FINSI;
|FCAL;
|| ///////////////////////////////////////////////////////////////////
|PROCESO LeoActi;
     |DDEFECTO #214;
     #214#0 = enEmpresa;
     #214#1 = aDepar;
     |SI #214#1 = 0;  #214#1 = 1;   |FINSI;
     |LEE 041#214=;
|FPRC;

// ... especial
|PROCESO Especial;

    |SI nOp = 0;
        nImpArren = (nImpArren + #2#12) ! 2;
        nUltActiv = #2#5;
        nUltLinea = #2#6;
        nSwArren  = nSwArren + 1;
        |LIBERA #2;
        |ACABA;
    |FINSI;

    |SI nOp = 1;
        |SI nSwArren > 1;
            nNume1 = ((#2#12 * 100) / nImpArren) ! 2;
            #2#12  = ((nImporte * nNume1 ) / 100) ! 2;
            nResto = nResto - #2#12;
        |SINO;
            #2#12  = nImporte;
            nResto = 0;
        |FINSI;
        |GRABA 020#2a;
        |LIBERA #2;
    |FINSI;

|FPRC;

|DEFBUCLE Especial;
     #2#1;
     7;
     enEmpresa, enEjer, 99, 347, enComplem, 01, 01, #1#7;
     enEmpresa, enEjer, 99, 347, enComplem, 99, 99, #1#7;
     be;
     NULL, Especial;
|FIN;

|PROCESO EspecialBS;
     nUltActiv = 0;
     nUltLinea = 0;
     nImpArren = 0;
     nSwArren  = 0;
     nOp = 0; |BUCLE Especial;
     |SI nSwArren ! 0; |Y nImpArren ! nImporte;
         nOp = 1; nResto = nImporte; |BUCLE Especial;
         |SI nResto ! 0;
             |ABRE #2;
             #2#0 = enEmpresa;
             #2#1 = enEjer;
             #2#2 = 99;
             #2#3 = 347;
             #2#4 = enComplem;
             #2#5 = nUltActiv;
             #2#6 = nUltLinea;
             |LEE 141#2=;
             |SI FSalida = 0;
                 #2#12 = #2#12 + nResto;
                 |GRABA 020#2a;
             |FINSI;
             |LIBERA #2;
             |CIERRA #2;
         |FINSI;
     |FINSI;
|FPRC;

|| ////////////////////////////////////////////////////////
|PROCESO LeeCobro_1;
     nImporte = 0;
     |DDEFECTO #72;
     #72#0 = enEmpresa;
     #72#1 = enEjer;
     #72#2 = aTipo;
     #72#3 = aNif;
     #72#4 = aSeg;
     #72#5 = aRend;
     #72#6 = enEjer;
     |LEE 041#72=;
     |SI FSalida = 0;
         nImporte = #72#7;
     |FINSI;
|FPRC;

|PROCESO BuscaCobro_1;
    #1#13 = #1#13 + #913#2;
|FPRC;

|DEFBUCLE BuscaCobro_1;
   #913#1;
   ;
   #1#7, #1#6, #1#25, #1#26, #1#8, #1#9, #1#27, 00, 00;
   #1#7, #1#6, #1#25, #1#26, #1#8, #1#9, #1#27, 99, 99;
   ;
   NULL, BuscaCobro_1;
|FIN;

|PROCESO Recalcula2;
  |SI nOpera = 0;
      #1#13 = 0;
      #1#15 = 0;
  |FINSI;
  |SI nOpera = 1;
      #1#13 = 0;
      |BUCLE BuscaCobro_1;
      |SI #1#13 [ 6000;
          #1#13 = 0;
      |FINSI;
      |SI #1#1] 2012; |Y #1#6 ! "B";
          #1#13 = 0;            || solo pasamos cobros
      |FINSI;
  |FINSI;
  |SI nOpera = 2;
      aTipo = #1#6;
      aNif  = #1#7;
      aSeg  = #1#8;
      aRend = #1#9;
      |HAZ LeeCobro_1;
      |SI nImporte > 6000;
          #1#13 = nImporte;
      |FINSI;
      |SI #1#1] 2012; |Y #1#6 ! "B";
          #1#13 = 0;            || solo pasamos cobros
      |FINSI;
      |SI aTipo = "B"; |Y aRend = "S";
          |HAZ EspecialBS;
      |FINSI;
  |FINSI;

  |GRABA 020#1a;
  |LIBERA #1;
|FPRC;

|DEFBUCLE LeeDsmo2347;
     #1#1;
     ;
     enEmpresa, enEjer, 99, 347, enComplem, 0, "A", "            ", "N", "N", "N", "N", "N";
     enEmpresa, enEjer, 99, 347, enComplem, 0, "B", "zzzzzzzzzzzz", "S", "S", "S", "S", "S";
     be;
     NULL, Recalcula2;
|FIN;

|PROCESO Recalcula3;
    |SI nOpera = 0;
        #2#12 = 0;
    |FINSI;
    |SI nOpera = 1;
        |ABRE #913;
        #913#0 = #2#7;
        #913#1 = "B";
        #913#6 = "N";
        #913#7 = "S";
        #913#20 = #2#5;
        #913#21 = #2#6;
        |LEE 000#913=;
        |SI FSalida = 0;
             #2#12 = #913#2;
        |FINSI;
        |CIERRA #913;
    |FINSI;

    |GRABA 020#2a;
    |LIBERA #2;
|FPRC;

|DEFBUCLE LeeDsmo3347;
     #2#1;
     ;
     enEmpresa, enEjer, 99, 347, enComplem, 01, 01;
     enEmpresa, enEjer, 99, 347, enComplem, 99, 99;
     be;
     NULL, Recalcula3;
|FIN;

|PROCESO Recalcula4;
    |BORRA 020#3a;
    |LIBERA #3;
|FPRC;

|DEFBUCLE LeeDsmo4347;
     #3#1;
     ;
     enEmpresa, enEjer, 99, 347, enComplem, 0, "A", "            ", "N", "N", "N", "N", "N", enEjer;
     enEmpresa, enEjer, 99, 347, enComplem, 0, "B", "zzzzzzzzzzzz", "S", "S", "S", "S", "S", 2008;
     be;
     NULL, Recalcula4;
|FIN;

|| .... Proceso especial para pasas los cobros
|PROCESO AltaEspecial;
      |SI enEjer ] 2012; |Y #72#2 ! "B";
          |ACABA;     ||solo paso cobros
      |FINSI;

      #1#0  = #4#0;
      #1#1  = #4#1;
      #1#2  = #4#2;
      #1#3  = #4#3;
      #1#4  = #4#4;
      #1#5  = 0;
      #1#6  = #72#2;
      #1#7  = #72#3;
      #1#8  = #72#4;
      #1#9  = #72#5;
      #1#25 = #72#8;
      #1#26 = #72#9;
      #1#27 = #72#10;
      |LEE 101#1=;
      |SI FSalida ! 0;
          |DDEFECTO #1;
          #1#0  = #4#0;
          #1#1  = #4#1;
          #1#2  = #4#2;
          #1#3  = #4#3;
          #1#4  = #4#4;
          #1#5  = 0;
          #1#6  = #72#2;
          #1#7  = #72#3;
          #1#8  = #72#4;
          #1#9  = #72#5;
          #1#25 = #72#8;
          #1#26 = #72#9;
          #1#27 = #72#10;
          #1#10 = "N";

          eaCodNif = #1#7;   |HAZ LeeNifs;
          #1#11 = #709#1;

          |GRABA 020#1b;
      |FINSI;

      #1#15 = #1#15 + nImporte;

      |GRABA 020#1a;
      |LIBERA #1;

      #3#0 = #1#0;
      #3#1 = #1#1;
      #3#2 = #1#2;
      #3#3 = #1#3;
      #3#4 = #1#4;
      #3#5 = #1#5;
      #3#6 = #1#6;
      #3#7 = #1#7;
      #3#8 = #1#8;
      #3#9 = #1#9;
      #3#10 = #72#6;
      #3#12 = #72#8;
      #3#13 = #72#9;
      #3#14 = #72#10;
      |LEE 141#3=;
      |SI FSalida ! 0;
          |DDEFECTO #3;
          #3#0 = #1#0;
          #3#1 = #1#1;
          #3#2 = #1#2;
          #3#3 = #1#3;
          #3#4 = #1#4;
          #3#5 = #1#5;
          #3#6 = #1#6;
          #3#7 = #1#7;
          #3#8 = #1#8;
          #3#9 = #1#9;
          #3#10 = #72#6;
          #3#12 = #72#8;
          #3#13 = #72#9;
          #3#14 = #72#10;
          |GRABA 020#3b;
      |FINSI;
      #3#11 = #3#11 + nImporte;
      |GRABA 020#3a;
      |LIBERA #3;
|FPRC;

|PROCESO GrabaDsmom072;
    #72#0 = enEmpresa;
    #72#1 = enEjer;
    #72#2 = #913#1;   || Clave A/B
    #72#3 = #913#0;   || Nif
    #72#4 = #913#6;   || Seguro
    #72#5 = #913#7;   || Arrendamiento
    #72#6 = nEjerCob;
    #72#8 = #913#22;
    #72#9 = #913#23;
    #72#10= #913#24;
    |LEE 101#72=;
    |SI FSalida ! 0;
        |DDEFECTO #72;
        #72#0 = enEmpresa;
        #72#1 = enEjer;
        #72#2 = #913#1;   || Clave A/B
        #72#3 = #913#0;   || Nif
        #72#4 = #913#6;   || Seguro
        #72#5 = #913#7;   || Arrendamiento
        #72#6 = nEjerCob;
        #72#8 = #913#22;
        #72#9 = #913#23;
        #72#10= #913#24;
        |GRABA 020#72b;
    |FINSI;
    #72#7 = #72#7 + nImporte;
    |GRABA 020#72a;
    |LIBERA #72;
    nSwHay = 1;
|FPRC;

|PROCESO LeeDsmow113;

    nEjerCob = enEjer;
    |SI #913#2 ! 0;
        nImporte = #913#2;
        |HAZ GrabaDsmom072;
    |FINSI;
    |PARA nCampo4 = 8; |SI nCampo4 [ 19; |HACIENDO nCampo4 = nCampo4 + 1;
          nEjerCob = nEjerCob - 1;
          |SI #913nCampo4 ! 0;
              nImporte = #913nCampo4;
              |HAZ GrabaDsmom072;
          |FINSI;
    |SIGUE;
|FPRC;

|DEFBUCLE LeeDsmow113;
    #913#1;
    ;
    "            ", " ", " ", " ", " ", " ", " ", 00, 00;
    "zzzzzzzzzzzz", "z", "z", "z", "z", "z", "z", 99, 99;
    ;
    NULL, LeeDsmow113;
|FIN;

|PROCESO BuscaAcumulado;
    nImporte = nImporte + #972#7;
|FPRC;

|DEFBUCLE BuscaAcumulado;
    #972#1010;
    ;
    #72#0, 2008,   #72#2, #72#3, #72#8, #72#9, #72#4, #72#5, #72#10, #72#6;
    #72#0, nHEjer, #72#2, #72#3, #72#8, #72#9, #72#4, #72#5, #72#10, #72#6;
    e;
    NULL, BuscaAcumulado;
|FIN;

|PROCESO LeeDsmom072;
    |SI nOpera = 0;
        |BORRA 020#72a;
    |FINSI;

    |SI nOpera = 1;
        |SI #72#6 ! enEjer;
            nHEjer   = enEjer - 1;
            nImporte = #72#7;
            |BUCLE BuscaAcumulado;
            |SI nImporte > 6000;
                |HAZ AltaEspecial;
            |FINSI;
        |FINSI;
    |FINSI;
    |LIBERA #72;
|FPRC;

|DEFBUCLE LeeDsmom072;
    #72#1;
    ;
    enEmpresa, enEjer, " ", "            ", " ", " ", " ", " ", " ", 2008;
    enEmpresa, enEjer, "z", "zzzzzzzzzzzz", "z", "z", "z", "z", "z", enEjer;
    be;
    NULL, LeeDsmom072;
|FIN;

|| ....... Graba el Auxiliar
|PROCESO GrabaAux913;

     |SI nEjerCob < 2008;   nEjerCob = enEjer; |FINSI;
     |SI nEjerCob > enEjer; nEjerCob = enEjer; |FINSI;

     nCampo4 =  2;
     nCampo5 = -1;
     |SI nEjerCob ! enEjer;
         nCampo4 = 4;
         nCampo5 = enEjer - nEjerCob; |SI nCampo5 > 12; nCampo5 = 12; |FINSI;
         nCampo5 = 7 + nCampo5;
     |FINSI;

     #913#0 = aNif;
     #913#1 = aTipo;
     #913#6 = aSeg;
     #913#7 = aRend;
     #913#20 = aDepar;
     #913#21 = aSubde;
     |LEE 101#913=;
     |SI FSalida ! 0;
         |DDEFECTO #913;
         #913#0 = aNif;
         #913#1 = aTipo;
         #913#6 = aSeg;
         #913#7 = aRend;
         #913#20 = aDepar;
         #913#21 = aSubde;
         |GRABA 020#913b;
     |FINSI;

     #913nCampo4 = #913nCampo4 + nImporte;
     |SI nCampo5 ! -1; #913nCampo5 = #913nCampo5 + nImporte; |FINSI;

     |GRABA 020#913a;
     |LIBERA #913;
|FPRC;

|| ////////////////////// Desde Modelo
|PROCESO DesdeDsmo4347;
     aDepar  = "01";
     aSubde  = "01";
     aTipo = #3#6;
     aNif  = #3#7;
     aSeg  = #3#8;
     aRend = #3#9;
     nEjerCob = #3#10;
     nImporte = #3#11;
     |HAZ GrabaAux913;
|FPRC;

|DEFBUCLE DesdeDsmo4347;
     #3#1;
     ;
     enEmpresa, enEjer, 99, 347, nComplem, 0, " ", "            ", "N", "N", "N", "N", "N", enEjer;
     enEmpresa, enEjer, 99, 347, nComplem, 0, "z", "zzzzzzzzzzzz", "S", "S", "S", "S", "S", 2008;
     be;
     NULL, DesdeDsmo4347;
|FIN;

|PROCESO DesdeDsmo3347;
     aDepar = #2#5;
     aSubde = #2#6;
     aTipo  = "B";
     aNif   = #2#7;
     aSeg   = "N";
     aRend  = "S";
     nEjerCob = enEjer;
     nImporte = #2#12;
     |HAZ GrabaAux913;
|FPRC;

|DEFBUCLE DesdeDsmo3347;
     #2#1;
     ;
     enEmpresa, enEjer, 99, 347, nComplem, 00, 00;
     enEmpresa, enEjer, 99, 347, nComplem, 99, 99;
     be;
     NULL, DesdeDsmo3347;
|FIN;

|PROCESO DesdeDsmo2347;
     aDepar = "01";
     aSubde = "01";
     aTipo = #1#6;
     aNif  = #1#7;
     aSeg  = #1#8;
     aRend = #1#9;
     nEjerCob = enEjer;
     nImporte = #1#13;
     |SI aRend ! "S";
         |HAZ GrabaAux913;
     |FINSI;
|FPRC;

|DEFBUCLE DesdeDsmo2347;
     #1#1;
     ;
     enEmpresa, enEjer, 99, 347, nComplem, 0, " ", "            ", "N", "N", "N", "N", "N";
     enEmpresa, enEjer, 99, 347, nComplem, 0, "z", "zzzzzzzzzzzz", "S", "S", "S", "S", "S";
     be;
     NULL, DesdeDsmo2347;
|FIN;

|PROCESO DesdeModelos;

      nComplem = -1;
      |ABRE #4;
      #4#0 = enEmpresa;
      #4#1 = enEjer;
      #4#2 = 99;
      #4#3 = 347;
      #4#4 = 99;
      |LEE 040#4];
      |SI FSalida = 0;
          |LEE 040#4a;
      |SINO;
          |LEE 040#4u;
      |FINSI;
      |SI FSalida = 0; |Y #4#0 = enEmpresa; |Y #4#1 = enEjer; |Y #4#2 = 99;
          |Y #4#3 = 347;
            nComplem = #4#4;
      |FINSI;
      |CIERRA #4;

      |SI nComplem ! -1;
          |ABRE #913;
          |BUCLE DesdeDsmo2347;
          |BUCLE DesdeDsmo3347;
          |BUCLE DesdeDsmo4347;
          |CIERRA #913;
      |FINSI;
|FPRC;

|| ////////////////////// Desde Contabilidad
|PROCESO camaasil;
     #30#0 = #105#6;
     |LEE 000#30=;
     |SI FSalida ! 0;   |ACABA;  |FINSI;
     |SI #30#7  ! "S";  |ACABA;  |FINSI;
     |SI #30#51 = "X";  |ACABA;  |FINSI;

     aTipo = "";

     #106#23 = "C";
     #106#10 = #105#4;
     |LEE 000#106=;
     |SI FSalida = 0;
         aTipo = "B";
     |SINO;
         #106#23 = "P";
         #106#10 = #105#4;
         |LEE 000#106=;
         |SI FSalida = 0;
             aTipo = "A";
         |FINSI;
     |FINSI;

     |SI aTipo = "";  |ACABA;  |FINSI;

     aNif     = #106#9;
     aDepar   = #105#21;
     aSubde   = #105#28;
     aSeg     = "N";
     aRend    = "N";
     nEjerCob = enEjer;
     #107#0 = #105#0;
     #107#1 = #105#1;
     #107#2 = #105#2;
     #107#3 = #105#3;
     |LEE 041#107=;
     |SI FSalida = 0;
         nEjerCob = #107#4;
         aSeg     = #107#7;
         aRend    = #107#8;
     |SINO;
         |SI #105#28 ! "   ";
             |HAZ LeoActi;
             |SI #214#9 ! 6; |Y #214#9 ! 7; |Y #214#9 ! 11;
                 |Y #214#9 ! 12; |Y #214#9 ! 16; |Y #214#9 ! 17;
                 aRend = "S";
             |FINSI;
         |FINSI;
     |FINSI;

     |SI aTipo = "B";
         |SI #105#8 = "D";
             nImporte = - #105#9;
         |SINO;
             nImporte = #105#9;
         |FINSI;
     |FINSI;

     |SI aTipo = "A";
         |SI #105#8 = "D";
             nImporte = #105#9;
         |SINO;
             nImporte = - #105#9;
         |FINSI;
     |FINSI;

     |HAZ GrabaAux913;
|FPRC;

|DEFBUCLE camaasil;
     #105#1;
     ;
     01, "01.01.0000", 000001, 0001;
     98, "31.12.9999", 999999, 9999;
     ;
     NULL, camaasil;
|FIN;

|PROCESO DesdeContabilidad;
     |PATH_DAT #105 aPathConta;
     |PATH_DAT #106 aPathConta;
     |PATH_DAT #107 aPathConta;

     |ABRE #214;
     |ABRE #30;
     |ABRE #106;
     |ABRE #107;
     |ABRE #913;
     |BUCLE camaasil;
     |CIERRA #107;
     |CIERRA #106;
     |CIERRA #214;
     |SI enSwPartido = 1;
         enSwOpPar   = 2;
         |HAZ FinalPartido;
     |FINSI;
|FPRC;

|PROCESO LeeAgifigen;
    enEmpresa   = #118#0;
    enEjer      = #0#14;

    nSwDesde = 0;
    |SI #0#15 = 1;
        enSwOpPar   = 0;
        |HAZ SituaEmpresa;
        |SI enSwPartido = 0;
            aAlfa = #0#14;
            aAlfa = aAlfa % -102;
            nEjer = aAlfa;

            |ABRE #101;
            |SELECT #4#101;
            #101#2  = #118#0;
            #101#26 = nEjer;
            |LEE 040#101=;
            |SI FSalida ! 0;
                |CIERRA #101;
                |SI #0#16 = "S";
                    nSwDesde = 1;
                |SINO;
                    |ACABA;
                |FINSI;
            |SINO;
                |CIERRA #101;

                |DBASS aPathConta;   |QBT aPathConta;
                aPathConta = aPathConta + "contagen/fich/" + (("00000" + #101#2) % -105) + #101#3 + "/";

                enEmpresa  = #101#2;
                enPerConta = #101#3;
                eaLaMonEmp = #101#28;
                |HAZ EnConversor;
            |FINSI;
          |SINO;
             enPerConta = enPeriodoPar;
             eaLaMonEmp = "E";
         |FINSI;
     |SINO;
         |SI #0#15 = 2;
             nSwDesde = 1;
         |SINO;
             nSwDesde = 2;
         |FINSI;
     |FINSI;

     nSwModelo = 0;
     enComplem = -1;
     |SI #0#14 ] 2011;
         |ABRE #4;
         #4#0 = enEmpresa;
         #4#1 = enEjer;
         #4#2 = 99;
         #4#3 = 347;
         #4#4 = 99;
         |LEE 040#4];
         |SI FSalida = 0;
             |LEE 040#4a;
         |SINO;
             |LEE 040#4u;
         |FINSI;
         |SI FSalida = 0; |Y #4#0 = enEmpresa; |Y #4#1 = enEjer; |Y #4#2 = 99;
             |Y #4#3 = 347;
             enComplem = #4#4;
         |FINSI;
         |CIERRA #4;
         |SI enComplem ! -1;
             |SI #4#7 = "X"; |Y #4#9 ! "X"; nSwModelo = 1; |FINSI;
         |FINSI;
     |FINSI;

     |SI nSwDesde = 2; |Y nSwModelo = 0;
         |ACABA;    || si no existe modelo no tiene sentido
     |FINSI;

     aFichero = ("47" + Usuario + "zzzzzzzz") % 108;
     |NOME_DAT #913 aFichero;
     |DELINDEX #913;

     |SI nSwDesde ! 2;
         |SI nSwDesde = 0;
             |HAZ DesdeContabilidad;
         |SINO;
             |HAZ DesdeModelos;
         |FINSI;
         nOpera = 0; |BUCLE LeeDsmom072;
     |FINSI;

     |SI nSwModelo = 1;
         nOpera = 0; |BUCLE LeeDsmo2347;
         |SI nSwDesde ! 2; nOpera = 0; |BUCLE LeeDsmo3347; |FINSI;
         nOpera = 0; |BUCLE LeeDsmo4347;
     |FINSI;

     |ABRE #1;
     |ABRE #3;
     |SI nSwDesde ! 2;
         |ABRE #72;
         |BUCLE LeeDsmow113;
         |CIERRA #72;
     |FINSI;

     |SI nSwModelo = 1;
         nOpera = 1; |BUCLE LeeDsmom072;
         |SI nSwDesde ! 2;
             nOpera = 1; |BUCLE LeeDsmo2347;
             nOpera = 1; |BUCLE LeeDsmo3347;
         |SINO;
            |ABRE #72;
            nOpera = 2; |BUCLE LeeDsmo2347;
            |CIERRA #72;
         |FINSI;
     |FINSI;

     |CIERRA #3;
     |CIERRA #1;
     |DELINDEX #913;

     |SI nSwModelo = 1;
         enEmpresa     = #118#0;
         enEjer        = #0#14;
         enPeriodo     = 99;
         enModelo      = 347;
         enActividad   = 0;
         enTipoEntrada = 98;
         enMensa       = 1;
         |SUB_EJECUTA_NP "dsmo1347";

        |ABRE #4;
        #4#0 = enEmpresa;
        #4#1 = #0#14;
        #4#2 = 99;
        #4#3 = 347;
        #4#4 = enComplem;
        |LEE 101#4=;
        |SI FSalida = 0;
            #4#7  = "X";
            #4#8  = ~;
            #4#15 = 0;
            #4#17 = "COMPLETADO";

           |GRABA 020#4a;
           |LIBERA #4;
         |FINSI;
         |CIERRA #4;
    |FINSI;
|FPRC;

|DEFBUCLE LeeAgifigen;
     #118#1;
     ;
     nDEmpresa;
     nHEmpresa;
     e;
     NULL, LeeAgifigen;
|FIN;

|PROCESO ITipo3;  |TIPO 3;

  |DBASE aDef;  |QBF aDef;
  aDef = aDef + "def/dsmom072.mas";
  |CARGA_DEF aDef, dsmom72@;
  |SI FSalida ! 0;
      |MENAV "    Error al Cargar def dsmom072";
      |ACABA;
  |FINSI;

  |SI #0#0 ! 0;
      nDEmpresa = #0#0;
      nHEmpresa = #0#1;
      |BUCLE LeeAgifigen;
  |SINO;
      |PARA nCampo = 2;  |SI nCampo [ 13;  |HACIENDO nCampo = nCampo + 1;
            nDEmpresa = #0nCampo;
            nHEmpresa = #0nCampo;
            |BUCLE LeeAgifigen;
      |SIGUE;
  |FINSI;
  |DESCARGA_DEF #dsmom72@;
|FPRC;
|| =========================================================================

|PROCESO PrimerProceso;
   |DDEFECTO #0;
   nSwHay = 0;
   #0#15 = 2;
   #0#14 = 2008; |HAZ ITipo3;
   #0#14 = 2009; |HAZ ITipo3;
   #0#14 = 2010; |HAZ ITipo3;

   |SI nSwHay = 0;
      |ABRE #72;
      #72#0 = 1;
      #72#1 = 2008;
      #72#2 = "A";
      #72#4 = "";
      #72#5 = "N";
      #72#6 = 2008;
      |LEE 001#72=;
      |SI FSalida ! 0;
         |DDEFECTO #72;
         #72#0 = 1;
         #72#1 = 2008;
         #72#2 = "A";
         #72#4 = "";
         #72#5 = "N";
         #72#6 = 2008;
         |GRABA 020#72n;
      |FINSI;
      |CIERRA #72;
  |FINSI;
|FPRC;

|PROGRAMA;
  nEmpresa = enEmpresa;
  nEjerO    = enEjer;

  aParam = PARAMETRO;
  |SI aParam = "pase";
     |HAZ PrimerProceso;
     enEmpresa = nEmpresa;
     enEjer    = nEjer;
     |ACABA;
  |FINSI;

  |SI aParam = "modi";
      |DDEFECTO #0;
      #0#0 = enEmpresa;
      #0#1 = enEmpresa;
      #0#14 = enEjer;
      #0#15 = 3;
      #0#16 = "N";
      |HAZ ITipo3;
  |SINO;
      |MANTE #0;
  |FINSI;

  enEmpresa = nEmpresa;
  enEjer    = nEjer;
|FPRO;
