|FICHEROS;
    dsmoz057 #0;

    dsmom041 #41;
    dsmom042 #42;

    :/basica/def/agifigen #1000;
|FIN;

|VARIABLES;

  nDEmpresa   = 0;
  nHEmpresa   = 0;
  &nDActividad = 0;
  &nHActividad = 0;
  &ele1       = "";
  &ele2       = "";
  &nnum1      = 0;
  &nnum2      = 0;
  nNume1      = 0;
  nCampo      = 0;
  nCampo1     = 0;
  fecalf      = "";
  fecsys      = @;
  aAlfa1      = "";
  aAlfa2      = "";

  nLaEmpresa  = 0;
  swBueno     = 0;

  &nCamSel  = 24;
  &nCamTip  = 39;
  nLinea    = 0;

  &enEjerV  = 0;
  &enEjerA  = 0;
  nPara1    = 0;

  nOk = 0;
  {30}aPopup        = "";
     aContenido     = "";
     aID            = "";
     nID            = 0;
|FIN;

|INCLUYE i_variar;
|INCLUYE i_varcon;
|INCLUYE i_numamo;
|INCLUYE i_selcon;

|| //////////////////// Calculos de Pantalla \\\\\\\\\\\\\\\\\\\\\\\\\\
|CALCULO Tipo7;  |TIPO 7;
     |SI enEmpresa ! 0;
         #0#8 = enEmpresa; |PINTA #0#8;
         #0#9 = enEmpresa; |PINTA #0#9;

         |C_M #0#8, 1, "N";
         |C_M #0#9, 1, "N";
     |FINSI;
|FCAL;

|CALCULO Parrilla;
     |SI #0#8 = 0;
         #0#8 = 0;  |PINTA #0#8;
         #0#9 = 0;  |PINTA #0#9;  |C_M #0#9, 1, "N";
         |PARA nCampo = 10;  |SI nCampo [ 21;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |SINO;
         |SI enEmpresa = 0;
             |C_M #0#8, 1, "S";
             |C_M #0#9, 1, "S";
         |FINSI;

         |PARA nCampo = 10;  |SI nCampo [ 21;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "N";
               #0nCampo = 0;  |PINTA #0nCampo;
         |SIGUE;
     |FINSI;
|FCAL;

|CALCULO NoMasEmpresas;
     |SI #0Campo = 0;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 21;  |HACIENDO nCampo = nCampo + 1;
               #0nCampo = 0;  |C_M #0nCampo, 1, "N";
         |SIGUE;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 21;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |FINSI;
|FCAL;

|CALCULO numamo; |TIPO 0;
    x_alfa1 = #0Campo;
    |HAZ x_punto;
    #0Campo = x_alfa1;
    |PINTA #0Campo;
|FCAL;

|CALCULO ElPtec; |TIPO 7;
 |PTEC 816;
|FCAL;

|CALCULO defecto; |TIPO 7;
   |C_M #0#4,0,aAlfa1;
   |SI aAlfa1 = "N"; |ACABA; |FINSI;

   fecsys = ~
   aAlfa1 = fecsys % -104;
   fecalf = "31.12." + aAlfa1;
   #0#4 = fecalf; |PINTA #0#4;
|FCAL;

|CALCULO EhLafecha; |TIPO 0;
  aAlfa1 = #0#4; aAlfa1 = aAlfa1 % -104;
  nNume1 = aAlfa1;
  #0#42 = nNume1 - 1; |PINTA #0#42;
  #0#43 = nNume1;     |PINTA #0#43;
|FCAL;

|CALCULO SiAmor;
  aAlfa1 = #0#41;
  |C_M #0#42, 1, aAlfa1;
  |C_M #0#43, 1, aAlfa1;
|FCAL;

|CALCULO Tipo66C44; |TIPO 66;
     aAlfa1 = "|C044WID";
     aID    = #0#aAlfa1#;
     nID    = aID;

     aPopup1  = nID;  || Si fuera -1 en la posicion
     aPopup2  = 06;
     aPopup3  = "01. Amortización total";
     aPopup4  = "03. Deterioro o destrucción";
     aPopup5  = "04. Retirada del bien por obsolescencia";
     aPopup6  = "05. Pérdida";
     aPopup7  = "06. Robo";
     aPopup8  = "99. Otras causas diferentes a las anteriores";

     IaPopup  = aPopup1  <-;
     |ESPECIFICA "TRACKPOPUP", aPopup;
     |SI FSalida ! 0;
         IaPopup  = aPopup3  <-;
         IaPopup  = IaPopup + (FSalida - 1);

         aAlfa1 = aPopup % 102;  #0#44= aAlfa1;
     |FINSI;

     |PINTA #0#44;
||     aPopup3  = "00. Elemento en Alta";
||     aPopup5  = "02. Venta o transmisión";
|FCAL;

|CALCULO Tipo0C44; |TIPO 0;
     nOk = 0;
     #0#45 = "";
     |SI #0#44 = 1;  #0#45 = "Amortización total";       nOk = 1; |FINSI;
     |SI #0#44 = 3;  #0#45 = "Deterioro o destrucción";  nOk = 1; |FINSI;
     |SI #0#44 = 4;  #0#45 = "Retirada del bien por obsolescencia"; nOk = 1; |FINSI;
     |SI #0#44 = 5;  #0#45 = "Pérdida";      nOk = 1; |FINSI;
     |SI #0#44 = 6;  #0#45 = "Robo";         nOk = 1; |FINSI;
     |SI #0#44 = 99; #0#45 = "Otras causas"; nOk = 1; |FINSI;

     |PINTA #0#45;
     |SI nOk = 0;
         |MENAV "0000Causa de Baja incorrecta";
         |ERROR;
     |FINSI;
|FCAL;
|| /////////////////////////// Procesos \\\\\\\\\\\\\\\\\\\\\\\\\\\\\\
|PROCESO CreaLinea;

 |ABRE #42;

 |DDEFECTO #42;
 #42#0 = #41#0;
 #42#2 = #41#2;
 #42#3 = #41#3;
 #42#4 = 999;
 |LEE 001#42];
 |SI FSalida = 0;
     |LEE 040#42a;
 |SINO;
     |LEE 040#42u;
 |FINSI;

 nLinea = 1;
 |SI FSalida = 0; |Y #42#0 = #41#0; |Y #42#2 = #41#2; |Y #42#3 = #41#3;
     nLinea =#42#4 + 1;
 |FINSI;

 |DDEFECTO #42;
 #42#0 = #41#0;
 #42#1 = #41#1;
 #42#2 = #41#2;
 #42#3 = #41#3;
 #42#4 = nLinea;
 #42#5 = enEjer;
 #42#6 = "V";
 #42#7 = 0;
 #42#8 = "N";
 #42#9 = efFechaSit;
 #42#10 = #0#6;
 #42#11 = 0;
 #42#12 = #0#5;
 #42#13 = 0;

 |GRABA 020#42n;
 |CIERRA #42;
|FPRC;


|PROCESO C_Amortiza;

   |SI nLaEmpresa ! #41#0;
       nLaEmpresa = #41#0;
       enEmpresa  = #41#0;
       swBueno = 0;
       |HAZ LaSelEmpresa;
       |SI enSwSelEmp = 1; swBueno = 1;  |FINSI;
   |FINSI;
   |SI swBueno = 1; |ACABA; |FINSI;

   |SI #41#27 > efFechaSit;  ||Comprado con posterioridad
       |ACABA;
   |FINSI;

   |SI #41#26 > "01.01.1000";  ||Ya esta dado de baja
       |ACABA;
   |FINSI;

   |SI #41#40 = "I"; |Y #41#58 ! 0;  || Ya esta Inactivo
       |ACABA;
   |FINSI;

   |SI #0#7 = "N";
       |SI #41#16 = 0; |Y #41#22 = 0;  ||Ya esta Amortizado
           |ACABA;
       |FINSI;
   |FINSI;

   |PINTA 2252, #41#0;
   |PINTA 2268, #41#2;
   |PINTA 2277, #41#3;

|| ...   |HAZ CreaLinea;

   #41#58 = #0#44;
   #41#26 = efFechaSit;
   #41#40 = "I";
   |SI #0#40 = "S";
       #41#16 = 0;
       #41#22 = 0;
   |FINSI;

   |GRABA 020#41a;
   |LIBERA #41;
|FPRC;


|DEFBUCLE dsmoz057;
   #41#1;
   1;
   nDEmpresa, ele1, nnum1, nDActividad;
   nHEmpresa, ele2, nnum2, nHActividad;
   be;
   NULL, C_Amortiza;
|FIN;

|CALCULO Tipo3;  |TIPO 3;

     ele1        = #0#0;
     ele2        = #0#1;
     nnum1       = #0#37;
     nnum2       = #0#38;
     nDActividad = #0#22;
     nHActividad = #0#23;

     efFechaSit = #0#4;
     aAlfa1     = #0#4; aAlfa1 = aAlfa1 % -104;
     enEjer     = aAlfa1; enEjerV = enEjer;

     |SI #0#41 = "S";
         |HAZ AntesAmortizo;
     |FINSI;

     |SI #0#8 ! 0;
         swBueno = 0;
         nLaEmpresa = -1;
         nDEmpresa = #0#8;
         nHEmpresa = #0#9;
         |BUCLE dsmoz057;
     |SINO;
        |PARA nCampo = 10;;  |SI nCampo [ 21;  |HACIENDO nCampo = nCampo + 1;
              swBueno = 0;
              nLaEmpresa = -1;
              nDEmpresa = #0nCampo;
              nHEmpresa = #0nCampo;
              |BUCLE dsmoz057;
        |SIGUE;
     |FINSI;

     |SI #0#41 = "S";
         |HAZ DespuesAmortizo;
     |FINSI;
|FCAL;

|| ////////////////////////////////////////////////////////////////////////

|PROCESO agifigen;

 enEmpresa  = #1000#0;
 |SI #agifigen#94 > "01.01.0000"; |Y #agifigen#94 [ efFecTope;
     |SI #0nCamSel = "N"; |ACABA; |FINSI;  || Inactiva
 |FINSI;

 enSwSelEmp = 1;
 nSNum1 = nCamSel + 1; nSNum2 = nCamSel + 2;
 nSNum3 = nCamSel + 3; nSNum4 = nCamSel + 12;
 nSPin1 = #agifigen#87;
 |SI #0nCamTip = "S";
      |SI nSPin1 ] #0nSNum1; |Y nSPin1 [ #0nSNum2;
          enSwSelEmp = 0;
      |FINSI;
 |SINO;
      |SI nSPin1 = #0nSNum3;
          enSwSelEmp = 0;
      |SINO;
          |PARA nCampo1 = nSNum3;  |SI nCampo1 [ nSNum4;  |HACIENDO nCampo1 = nCampo1 + 1;
               |SI nSPin1 = #0nCampo1; |Y nSPin1 ! 0;
                   enSwSelEmp = 0;
                   |SAL;
               |FINSI;
          |SIGUE;
      |FINSI;
  |FINSI;

  |SI enSwSelEmp = 0;
       |SI #0nCamSel = "N";  || Si las no activas no estan seleccionadas
           enSwSelEmp  = 1;  || compruebo si esta activo los ejercicios
           swerror     = 1;
           eaEstadoEmp = "S";
           nSanyMas    = 0;
           |BUCLE BuscaEmpresa;
           |SI eaEstadoEmp ! "S";
               |ACABA;
           |FINSI;  || Inactiva
       |FINSI;
       enEjerA = nPara1;
       nCamSel    = 23;
       nCamTip    = 38;
       |SUB_EJECUTA_NP "dsmoz042";
  |FINSI;

  nCamSel  = 24;
  nCamTip  = 39;
|FPRC;

|DEFBUCLE agifigen;
 #1000#1;
 ;
 nDEmpresa;
 nHEmpresa;
 ;
 NULL, agifigen;
|FIN;

|PROCESO AntesAmortizo;
 |PARA nPara1 = #0#42; |SI nPara1 [ #0#43; |HACIENDO nPara1 = nPara1 + 1;
   |SI nPara1 ! enEjerV;
    |SI #0#8 ! 0;
        nDEmpresa = #0#8;
        nHEmpresa = #0#9;
        |BUCLE agifigen;
    |SINO;
        |PARA nCampo = 10;;  |SI nCampo [ 21;  |HACIENDO nCampo = nCampo + 1;
              nDEmpresa = #0nCampo;
              nHEmpresa = #0nCampo;
              |BUCLE agifigen;
        |SIGUE;
    |FINSI;
   |FINSI;
  |SIGUE;
|FPRC;

|PROCESO DespuesAmortizo;
 |SI enEjerV ] #0#42; |Y enEjerV [ #0#43;
     nPara1 = enEjerV;
     |SI #0#8 ! 0;
         nDEmpresa = #0#8;
         nHEmpresa = #0#9;
         |BUCLE agifigen;
     |SINO;
         |PARA nCampo = 10;;  |SI nCampo [ 21;  |HACIENDO nCampo = nCampo + 1;
              nDEmpresa = #0nCampo;
              nHEmpresa = #0nCampo;
              |BUCLE agifigen;
        |SIGUE;
    |FINSI;
 |FINSI;
|FPRC;

|PROGRAMA;

 |SI enEmpresa ! 0;
     |DDEFECTO #0;
     #0#0  = "";          |C_M #0#0, 1,"N";
     #0#1  = "zzzzzzzzz"; |C_M #0#1, 1,"N";
     #0#37 = 00;          |C_M #0#37,1,"N";
     #0#38 = 99;          |C_M #0#38,1,"N";
     #0#4  = efFechaSit;  |C_M #0#4, 1,"N";
     #0#22 = enActividad; |C_M #0#22,1,"N";
     #0#23 = enActividad; |C_M #0#23,1,"N";
     |CLS;
     |PINPA #0#0;
     |PINDA #0#0;
     |ENDATOS #1#0;
 |SINO;
     |CLS;
     |MANTE #0;
 |FINSI;
|FPRO;
