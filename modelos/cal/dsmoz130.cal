|FICHEROS;
     dsmoz130 #0;
     dsmom003 #3;
     dsmom004 #4;
     dsmow131 #998,MANTE;
     dsmow130 #999,MANTE;

     dsm60001 #110;
     dsm60002 #111;
     dsm60003 #112;

     :/basica/def/agitab99 #99;
     clientes@ #1000;
|FIN;

|VARIABLES;
     &enEjercicio = 0;
     &eaDatos     = "";
     &enResult    = 0;
     &efFechaPres = @;

     aFichero   = "";
     aParametro = "";
     aAlfa      = "";
     aBase      = "";

     nSwCli    = 0;
     nCanal    = 0;
     nEmpre    = 0;
     nNumCmp   = 0;
     nNuevaEmp = 0;
     nTecla    = 0;
     nBoton1   = 0;
     nBoton2   = 0;
     nBoton3   = 0;
     nBoton4   = 0;
     nBoton5   = 0;
     nSwHay    = 0;
     nCampo    = 0;
     nDEmpresa = 0;
     nHEmpresa = 0;
     nMes      = 0;
     nAnyo     = 0;
     nMod      = 0;

     fFecha    = @;
     fDFecha   = @;
     fHFecha   = @;

     enGraba   = 0;
|FIN;

|INCLUYE i_variar;

|| /////////////////////// PROCESOS ENTRADA POR PANTALLA \\\\\\\\\\\\\\\\\\\\

|PROCESO Modelo;
     |SI aParametro = "311";  |O aParametro = "371";
         |ACABA;
     |FINSI;

     |ABRE #99;
     #99#0 = #0#0;
     |LEE 040#99=;
     |SI FSalida ! 0;  |DDEFECTO #99;  |FINSI;
     |CIERRA #99;

     |SI #0#0 ! 309;  |Y #0#0 ! 600; |Y #0#0 ! 045;
         |SI #99#4 = "N";
             |MENAV "     Modelo sin Activar.";
             |ERROR;
         |FINSI;
     |FINSI;

     fFecha = ~;
     aAlfa  = fFecha % -104;
     #0#3   = aAlfa;
     #0#3 = #0#3 - 1;
|FPRC;

|PROCESO Parrilla;
     |SI #0#4 = 0;
         #0#4 = 0;  |PINTA #0#4;
         #0#5 = 0;  |PINTA #0#5;  |C_M #0#5, 1, "N";
         |PARA nCampo = 6;  |SI nCampo [ 17;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |SINO;
         |SI enEmpresa = 0;
             |C_M #0#4, 1, "S";
             |C_M #0#5, 1, "S";
         |FINSI;

         |PARA nCampo = 6;  |SI nCampo [ 17;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "N";
               #0nCampo = 0;  |PINTA #0nCampo;
         |SIGUE;
     |FINSI;
|FPRC;

|PROCESO NoMasEmpresas;
     |SI #0Campo = 0;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 17;  |HACIENDO nCampo = nCampo + 1;
               #0nCampo = 0;  |C_M #0nCampo, 1, "N";
         |SIGUE;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 17;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |FINSI;
|FPRC;

|| /////////////////////// PROCESOS DE TRABAJO \\\\\\\\\\\\\\\\\\\\

|PROCESO Boton2;
   |PTEC 825;
|FPRC;

|PROCESO Boton3;
   |PTEC 828;
|FPRC;

|PROCESO Boton4;
   |PTEC 830;
|FPRC;

|PROCESO Boton5;
   |PTEC 831;
|FPRC;

|PROCESO Modelo600z130;
   nSwHay = 1;
   |DDEFECTO #dsmow131;
   #dsmow131#0  = #dsm60001#0;
   #dsmow131#1  = "600";
   #dsmow131#2  = #dsm60001#57;
   #dsmow131#3  = #dsm60001#59;
   |SI #dsm60001#5 ! "            "; |Y #dsm60001#27 ! "            "; |Y #dsm60001#64 ! "            ";
      #dsmow131#4 = "COMPLETADO";
   |SINO;
      |SI #dsm60001#5 ! "            "; |O #dsm60001#27 ! "            "; |O #dsm60001#64 ! "            ";
         #dsmow131#4 = "PARCIAL";
      |SINO;
         #dsmow131#4 = "SIN DATOS";
      |FINSI;
   |FINSI;
   #dsmow131#10 = #dsm60001#56;
   |GRABA 020#dsmow131.n;
|FPRC;

|DEFBUCLE Modelo600z130;
#dsm60001#1;
59;
nEmpre, 00, fDFecha;
nEmpre, 99, fHFecha;
;
NULL,Modelo600z130;
|FIN;

|PROCESO RecogeMod;
   nSwHay = 0;
   ||BUCLE Modelo309;
   nEmpre = #clientes@#0; |BUCLE Modelo600z130;

   #999#0 = #clientes@#0;
   #999#1 = #dsmoz130#3;
   #999#2 = #clientes@#1;
   |GRABA 020#999n;
|FPRC;

|DEFBUCLE RecogeEmp;
#clientes@#1001;
;
nDEmpresa;
nHEmpresa;
;
NULL,RecogeMod;
|FIN;

|RUTINA ClaveBaja999;
   |SI FSalida = "POSICION";
      #999#0 = nNuevaEmp;
   |SINO;
      #999#0 = 1;
      nNuevaEmp = 0;
   |FINSI;
|FRUT;

|RUTINA ClaveAlta999;
     #999#0 = 99999;
|FRUT;

|PROCESO Tipo3;  |TIPO 3;

     |DBASS aBase;   |QBT aBase;
     aAlfa = aBase + "integral/def/dsam0000.mas";
     |XABRE "E", aAlfa, nCanal;
     |SI FSalida ] 0;
        aAlfa = aBase + "integral/def/dsam0000.mas"; nSwCli = 1;
     |SINO;
        aAlfa = aBase + "basica/def/agifige1.mas";   nSwCli = 2;
     |FINSI;

     |CARGA_DEF aAlfa, clientes@;
     |SI nSwCli = 1;
        aAlfa = aBase + "integral/fich/";
     |SINO;
        aAlfa = aBase + "basica/fich/";
     |FINSI;
     |PATH_DAT #clientes@#aAlfa#;
     |ABRE #clientes@;

     enEjercicio = #dsmoz130#3;

     |SI #0#0 ! 309; |Y #0#0 ! 600;
         |MENAV "    Modelo Periodico, entre por su mantenimiento.";
         |ACABA;
     |FINSI;

     aFichero = ("03" + Usuario) % 108; enGraba = 0;
     |NOME_DAT #999 aFichero;
     aFichero = ("04" + Usuario) % 108; enGraba = 0;
     |NOME_DAT #998 aFichero;
     |ABRE #999;  |CIERRA #999;  |DELINDEX #999;
     |ABRE #998;  |CIERRA #998;  |DELINDEX #998;

     |ABRE #999; |ABRE #998;
     aAlfa = "01.01." + #0#3; fDFecha = aAlfa;
     aAlfa = "31.12." + #0#3; fHFecha = aAlfa;
     |SI #0#4 ! 0;
         nDEmpresa = #0#4; nHEmpresa = #0#5;
         |BUCLE RecogeEmp;
     |SINO;
         |PARA nCampo = 6;  |SI nCampo [ 17;  |HACIENDO nCampo = nCampo + 1;
               nDEmpresa = #0nCampo; nHEmpresa = #0nCampo;
               |BUCLE RecogeEmp;
         |SIGUE;
     |FINSI;
     |CIERRA #999; |CIERRA #998;

     |CONTROL_SIMPLE 0, "BOTON,F2 Entrada al Lineal", 0860, 0880, Boton1; nBoton1 = FSalida;
     |ENTLINEAL #1#998, -2, 7, 22, 1, ClaveBaja998, ClaveAlta998;

     nNuevaEmp = 1;
     |PARA; |SI nNuevaEmp ! 0; |HACIENDO;
        |ENTLINEAL #1#999, -1, 4, 22, 1, ClaveBaja999, ClaveAlta999;
     |SIGUE;

     |FIN_CONTROL_WINDOWS nBoton1;
     |DELINDEX #999;

     |CIERRA #clientes@; |DESCARGA_DEF #clientes@;
|FPRC;

|RUTINA ClaveBaja998;
   #dsmow131#0 = #dsmow130#0;
   #dsmow131#1 = #dsmoz130#0;
   #dsmow131#2 = 00;
   aAlfa = "01.01." + #0#3; #dsmow131#3 = aAlfa;
|FRUT;

|RUTINA ClaveAlta998;
   #dsmow131#0 = #dsmow130#0;
   #dsmow131#1 = #dsmoz130#0;
   #dsmow131#2 = 99;
   aAlfa = "31.12." + #0#3; #dsmow131#3 = aAlfa;
|FRUT;

|PROCESO Tipo03w130; |TIPO 3;
   #clientes@#0 = #999#0;
   |LEE 000#clientes@.=;
   |SI FSalida = 0;
      #999#1 = #dsmoz130#3; |PINTA #999#1;
      #999#2 = #clientes@#1; |PINTA #999#2;
   |FINSI;
   |CIERRA #clientes@;

   #998#0 = #999#0;      |PINTA #998#0;
   #998#1 = #dsmoz130#0; |PINTA #998#1;

   |ENTLINEAL #1#998, -1, 2, 22, 1, ClaveBaja998, ClaveAlta998;
|FPRC;

|PROCESO Tipo03w131; |TIPO 3;
   |HAZ Modelo600z130;
|FPRC;

|PROCESO Tipo07w130; |TIPO 7;
   |ENTLINEAL #1#998, -1, 7, 22, 1, ClaveBaja998, ClaveAlta998;
|FPRC;

|PROCESO Tipo11w130; |TIPO 11;
   nTecla = FSalida;

   |SI nTecla = 1; nNuevaEmp = 0; |ACABA; |FINSI;

   |SI nTecla = 10;
      |ESTADO_CONTROL nBoton1, "DISABLE";
      |CONTROL_SIMPLE 0, "BOTON,F3 Entrada datos"       , 0960, 0980, Boton2; nBoton2 = FSalida;
      |CONTROL_SIMPLE 0, "BOTON,F6 Imprimir"            , 1060, 1080, Boton3; nBoton3 = FSalida;
      |CONTROL_SIMPLE 0, "BOTON,F8 Baja modelo"         , 1160, 1180, Boton4; nBoton4 = FSalida;
      |CONTROL_SIMPLE 0, "BOTON,F9 Crear complementaria", 1260, 1280, Boton5; nBoton5 = FSalida;
      |ENTLINEAL #1#998, -1, 4, 22, 1, ClaveBaja998, ClaveAlta998;
      |FIN_CONTROL_WINDOWS nBoton2;
      |FIN_CONTROL_WINDOWS nBoton3;
      |FIN_CONTROL_WINDOWS nBoton4;
      |FIN_CONTROL_WINDOWS nBoton5;
      |ESTADO_CONTROL nBoton1, "ENABLE";
      |ACABA;
   |FINSI;

   |SI nTecla = 18;
      |CONSULTA_CLAVE #1#dsmow130;
      nNuevaEmp = #dsmow130#0; |PTEC 807;
   |FINSI;
|FPRC;

|PROCESO BorraSPT;
   |BORRA 020#dsm60002.a;
|FPRC;

|DEFBUCLE BorraSPT;
#dsm60002#1;
;
#dsmow131#0, #dsmow131#2, "            ", "S";
#dsmow131#0, #dsmow131#2, "zzzzzzzzzzzz", "T";
be;
NULL,BorraSPT;
|FIN;

|PROCESO BorraBienes;
   |BORRA 020#dsm60003.a;
|FPRC;

|DEFBUCLE BorraBienes;
#dsm60003#1;
;
#dsmow131#0, #dsmow131#2, 01;
#dsmow131#0, #dsmow131#2, 99;
be;
NULL,BorraBienes;
|FIN;

|PROCESO MiraTeclas; |TIPO 11;
   nTecla = FSalida;
   |SI nTecla = 11;
      |SI #dsmow131#1 = "600";
         nNuevaEmp = #dsmow130#0;

         aAlfa = "dsm60001.tab;" + #dsmow131#0 + "," +#dsmow131#2;
         |SUB_EJECUTA aAlfa;

         |SI #dsmow131#10 = 0;
            #dsmow131#3  = efFechaPres;
            |GRABA 020#dsmow131.c;
         |FINSI;
         #dsmow131#4  = eaDatos;
         #dsmow131#10 = enResult;
         |GRABA 020#dsmow131.a;

         |PTEC 807; ||PTEC 808; |PTEC 809;
         |ERROR;
      |FINSI;
   |FINSI;
   |SI nTecla = 14;
      aAlfa = "dsmoy600.tab;" + #dsmow131#0 + "," + #dsmow131#2;
      |SUB_EJECUTA aAlfa;
   |FINSI;
   |SI nTecla = 16;
      aAlfa = "    N" + &191 + " Esta realmente seguro de que quiere eliminar el modelo ? ";
      |CONFI aAlfa;
      |SI FSalida = 0;
         |BUCLE BorraSPT; |BUCLE BorraBienes;
         |ABRE #dsm60001;
         #dsm60001#0 = #dsmow131#0;
         #dsm60001#57 = #dsmow131#2;
         |LEE 101#dsm60001.=;
         |SI FSalida = 0;
            |BORRA 020#dsm60001.a;
            |LIBERA #dsm60001;
         |FINSI;
         |CIERRA #dsm60001;
         |BORRA 020#dsmow131.a;
         |PONCONTROL 23,"si"; |ACABA;
      |FINSI;
   |FINSI;
   |SI nTecla = 17;
      nNumCmp = 0;
      #dsmow131#0 = #dsmow130#0;
      #dsmow131#1 = #dsmoz130#0;
      #dsmow131#2 = 0;
      #dsmow131#3 = "01.01.0000";
      |LEE 000#dsmow131.];
      |PARA; |SI FSalida = 0; |Y #dsmow131#0 = #dsmow130#0; |Y #dsmow131#1 = #dsmoz130#0; |HACIENDO;
         nNumCmp = #dsmow131#2 + 1;
         |LEE 000#dsmow131.s;
      |SIGUE;
      |SI nNumCmp > 0;
         aAlfa = "    N" + &191 + " Esta realmente seguro de que quiere hacer una complementaria ? ";
      |SINO;
         aAlfa = "    N" + &191 + " Esta realmente seguro de que quiere hacer el modelo ? ";
      |FINSI;
      |CONFI aAlfa;
      |SI FSalida = 0;

         |DDEFECTO #dsmow131;
         #dsmow131#0 = #dsmow130#0;
         #dsmow131#1 = #dsmoz130#0;
         #dsmow131#2 = nNumCmp;
         #dsmow131#3 = ~;
         |GRABA 020#dsmow131.c;
         nNuevaEmp = #dsmow130#0;

         aAlfa = "dsm60001.tab;" + #dsmow131#0 + "," +#dsmow131#2;
         |SUB_EJECUTA aAlfa;

         |PTEC 807;
      |FINSI;
   |FINSI;
|FPRC;

|PROGRAMA;

     aParametro = PARAMETRO;  |QBF aParametro;
     |SI aParametro ! 0;
         |ABRE #99;
         #99#0 = aParametro;
         |LEE 040#99=;
         |SI FSalida ! 0;  |DDEFECTO #99;  |FINSI;
         |CIERRA #99;

         |SI aParametro ! 309;  |Y aParametro ! 600;
             |SI #99#4 = "N";
                 |MENAV "     Modelo sin Activar.";
                 |ACABA;
             |FINSI;
         |FINSI;
         |CIERRA #99;

         |DDEFECTO #0;

         #0#0 = aParametro;
         |C_M #0#0, 1, "N";
         |PINPA #0#0;
         |PINDA #0#0;
         |ENDATOS #1#0;
     |SINO;
         |MANTE #0;
     |FINSI;
     |PULSATECLA;
|FPRO;
