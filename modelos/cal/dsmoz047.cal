|FICHEROS;
    dsmoz047 #0;

    canempr@ #30;
    camoned@ #4;
    
    dsmom040 #40;
    dsmom041 #41;
    dsmom042 #42;
    :/basica/def/agicen14 #114;
    :/basica/def/zmonedas #100;

    dsmow046 #999;
|FIN;

|VARIABLES;
   swBorra     = 0;
   swError     = 0;
   fecalf      = "";
   nDEmpresa   = 0;
   nHEmpresa   = 0;
   nDActividad = 0;
   nHActividad = 0;
   nCampo      = 0;
   Comodin = "";  || Directorio de la Aplicacion Antigua
   Comodin1 = ""; || Dir. Ficheros Antiguos
   Comodin2 = ""; || Dir. Ficheros Nuevos
   Comodin3 = ""; || Dir. Defs Antiguos

   aFichero  = "";
   nEstado   = 0;
   nNumGrupo = 0;
   aElemento = "";
   nNumero   = 0;
   nLinea    = 0;
   swPrimero = 0;
   nume1     = 0;
   nume2     = 0;
   nume3     = 0;
   cfecha = @;
   vfecha = @;
   afiscal = 0;
   acontab = 0;
   importe = 0;
   sw = 0;
   any = "";

   aMonedaV = "";
   nImporte = 0;
   &enConverModelo      = 0;
   &enRedonModelo       = 0;
   &enRedonPaYaModelo   = 0;

   &enEmpresaV = 0;
   &enPeriodoV = 0;
   aAlfa       = "";
   nAny        = 0;
|FIN;

|INCLUYE i_variar;
|INCLUYE i_varcon;

|| //////////////////// Calculos de Pantalla \\\\\\\\\\\\\\\\\\\\\\\\\\
|CALCULO ElPath; |TIPO 7;
 |DBASS Comodin; |QBF Comodin;
 #dsmoz047#0 = Comodin; |PINTA #dsmoz047#0;
|FCAL;

|CALCULO Tipo7;  |TIPO 7;
     |SI enEmpresa ! 0;
         #0#3 = enEmpresa; |PINTA #0#3;
         #0#4 = enEmpresa; |PINTA #0#4;

         |C_M #0#3, 1, "N";
         |C_M #0#4, 1, "N";
     |FINSI;
|FCAL;

|CALCULO Parrilla;
     |SI #0#3 = 0;
         #0#3 = 0;  |PINTA #0#3;
         #0#4 = 0;  |PINTA #0#4;  |C_M #0#4, 1, "N";
         |PARA nCampo = 5;  |SI nCampo [ 16;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |SINO;
         |SI enEmpresa = 0;
             |C_M #0#3, 1, "S";
             |C_M #0#4, 1, "S";
         |FINSI;

         |PARA nCampo = 5;  |SI nCampo [ 16;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "N";
               #0nCampo = 0;  |PINTA #0nCampo;
         |SIGUE;
     |FINSI;
|FCAL;

|CALCULO NoMasEmpresas;
     |SI #0Campo = 0;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 16;  |HACIENDO nCampo = nCampo + 1;
               #0nCampo = 0;  |C_M #0nCampo, 1, "N";
         |SIGUE;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 16;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |FINSI;
|FCAL;

|| //////////////////// Procesos desde el Tipo 3   \\\\\\\\\\\\\\\\\\\\\\\\\\
|CALCULO Tipo3;  |TIPO 3;
  aFichero = ("46w" + Usuario) % 108;
  |NOME_DAT #999 aFichero;
  |ABRE #999;  |CIERRA #999;  |DELINDEX #999;

  Comodin = #dsmoz047#0; |QBF Comodin;
  Comodin1 = Comodin % -101;
  |SI Comodin1 ! "/";  Comodin = Comodin + "/"; |FINSI;
  Comodin = Comodin + "agicont/";

  Comodin3 = Comodin + "def/canempre.mas";
  |CARGA_DEF Comodin3, canempr@;
  |SI FSalida ! 0;
      |MENAV "    ERROR!. Contabilidad Anterior No Localizada Correctamente.!";
      |ACABA;
  |FINSI;

  Comodin3 = Comodin + "fich/";
  |PATH_DAT #30 Comodin3;
  |ABRE #30;

  |SI #0#3 ! 0;
      |PARA enEmpresa = #0#3; |SI enEmpresa [ #0#4; |HACIENDO enEmpresa = enEmpresa + 1;
            swBorra = 0;
            |SI  #0#17 = "S"; swBorra = 1; |FINSI;
            |PARA enPeriodo = #0#1; |SI enPeriodo [ #0#2; |HACIENDO enPeriodo = enPeriodo + 1;
                  |HAZ Recoger;
            |SIGUE;
      |SIGUE;
  |SINO;
      |PARA nCampo = 5;  |SI nCampo [ 16;  |HACIENDO nCampo = nCampo + 1;
            enEmpresa = #0nCampo;
            swBorra = 0;
            |SI  #0#17 = "S"; swBorra = 1; |FINSI;
            |PARA enPeriodo = #0#1; |SI enPeriodo [ #0#2; |HACIENDO enPeriodo = enPeriodo + 1;
                  |HAZ Recoger;
            |SIGUE;
     |SIGUE;
  |FINSI;
  |CIERRA #30;
|FCAL;

|| ==========================================================================
|| ////////////////////////////////////////// Recoger
|PROCESO Recoger;

  swError = 0;
  |HAZ LeeEmpresa;
  |SI swError = 1;
      |ACABA;
  |FINSI;

  |PINTA 1558, enEmpresa;

  |SI swBorra = 1; |HAZ BorrarTodo; |FINSI;
  enEmpresaV = enEmpresa;
  enPeriodoV = enPeriodo;

  aAlfa     = ":contagen/capastiz;" + #0#0;  |QBT aAlfa;
  |SUB_EJECUTA_NP aAlfa;

  enEmpresa = enEmpresaV;
  enPeriodo = enPeriodoV;
|FPRC;

|PROCESO LeeEmpresa;
   swError = 0;
   eaNombreEmp = "";
   enActividad = 01;
   eaNombreAct = "";

   || Lee Empresa
   #canempr@#2 = enEmpresa;
   #canempr@#3 = enPeriodo;
   |LEE 001#canempr@.=;
   |SI FSalida ! 0;
       swError = 1;
       |ACABA;
   |FINSI;
   eaNombreEmp = #canempr@#1;
   nAny = #canempr@#26

   |SI nAny < 80;
       nAny = 2000 + nAny;
   |SINO;
       nAny = 1900 + nAny;
   |FINSI;

   || Lee Actividad
   |ABRE #114;
   #114#0 = enEmpresa;
   #114#1 = enActividad;
   |LEE 041#114];
   |SI FSalida = 0; |Y #114#0 = enEmpresa;
       enActividad = #114#1;
       eaNombreAct = #114#4;
   |FINSI;
   |CIERRA #114;

   || .... Lee la Moneda Vieja
   aMonedaV = "E";
   Comodin3 = Comodin + "def/camoneda.mas";
   |CARGA_DEF Comodin3, camoned@;
   |SI FSalida = 0;
       Comodin3 = Comodin + "fich/000000/";
       |PATH_DAT #4 Comodin3;

       |ABRE #camoned@;
       #camoned@#0 = enEmpresa;
       #camoned@#1 = enPeriodo;
       |LEE 001#camoned@.=;
       |SI FSalida = 0;
           aMonedaV = #camoned@#2;
       |SINO;
           aMonedaV = "E";
       |FINSI;
       |CIERRA #camoned@;
       |DESCARGA_DEF #camoned@;
   |FINSI;

   |ABRE #zmonedas;
   |LEE 040#zmonedas.p;
   |SI FSalida ! 0;  |DDEFECTO #zmonedas;  |FINSI;
   |CIERRA #zmonedas;

   enConverModelo     = 0;
   enRedonModelo      = 2;
   enRedonPaYaModelo  = 2;
   |SI #zmonedas#9 = "P";
       enRedonModelo      = 0;
       enRedonPaYaModelo  = 0;
   |FINSI;

   |SI #zmonedas#9 = "E";
       |SI aMonedaV = "P";
           enConverModelo    = 166.386;
           enRedonModelo     = 0;
           enRedonPaYaModelo = 2;
      |FINSI;
   |SINO;
      |SI aMonedaV = "E";
          enConverModelo    = 0.00601012;
          enRedonModelo     = 2;
          enRedonPaYaModelo = 0;
      |FINSI;
   |FINSI;
|FPRC;

|| .... Borro las de hoy
|PROCESO L_Borrar1;
 |BORRA 020#dsmom042.a;
 |LIBERA #dsmom042;
|FPRC;

|DEFBUCLE L_Borrar;
   #dsmom042#1;
   ;
   #dsmom041#0,#dsmom041#2,#dsmom041#3, 000;
   #dsmom041#0,#dsmom041#2,#dsmom041#3, 999;
   e;
   NULL, L_Borrar1;
|FIN;

|PROCESO C_Borrar1;
  |BUCLE L_Borrar;
  |BORRA 020#dsmom041.a;
  |LIBERA #dsmom041;
|FPRC;

|DEFBUCLE C_Borrar;
   #dsmom041#1;
   ;
   enEmpresa,"        ",00;
   enEmpresa,"zzzzzzzz",99;
   e;
   NULL, C_Borrar1;
|FIN;

|PROCESO G_Borrar1;
  |BORRA 020#dsmom040.a;
  |LIBERA #dsmom040;
|FPRC;

|DEFBUCLE G_Borrar;
   #dsmom040#1;
   ;
   enEmpresa,0000;
   enEmpresa,9999;
   e;
   NULL, G_Borrar1;
|FIN;

|PROCESO BorrarTodo;
 |BUCLE C_Borrar;
 |BUCLE G_Borrar;
 swBorra = 0;
|FPRC;
