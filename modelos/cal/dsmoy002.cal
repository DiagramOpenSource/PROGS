|FICHEROS;
     dsmoy002 #0;
     dsmom030 #30;
     dsmom033 #33;
     dsmom107 #107;

     :/contagen/def/canempre #101;
     :/contagen/def/camaniva #102;
     :/contagen/def/caivalin #103;
     :/contagen/def/caivaasi #104;

     :/basica/def/agicen14 #114;

     dsmow032 #900;
     dsmow031 #901;
     dsmow026 #998;
     dsmow025 #999;
|FIN;

|VARIABLES;
     nSwEsp = 0;
     &dirfich0  = "";

     nDepar     = 0;
     &nCultiu   = 0;
     nCampo     = 0;
     nDEmpresa  = 0;
     nHEmpresa  = 0;
     &enTot1    = 0;
     &enTot2    = 0;
     &enTot3    = 0;
     &enTot11   = 0;
     &enTot12   = 0;
     &enTot13   = 0;
     &enTot4    = 0;
     &enTot5    = 0;
     &enTot6    = 0;
     &enTot7    = 0;
     &aD1       = "";
     &aD2       = "";
     &aD3       = "";
     &aD4       = "";
     aNum1      = "";
     nNume1     = 0;
     nSwPrint   = 0;
     nEjer      = 0;

     aFichero   = "";
     aAlfa      = "";
     aAlfa1     = "";
     aAlfa2     = "";
     aAlfa3     = "";
     fDFecha    = @;
     fHFecha    = @;
     nDPerio    = 0;
     nHPerio    = 0;
     SwAgricola = 0;
     SwAlgun    = 0;

     aCampo0    = "";
     aCampo1    = "";
     aCampo2    = "";
     nNum1      = 0;
     nNum2      = 0;
     fLaFecha   = @;
     informe    = "";
     nTotalLinea = 0;
     &nPagina  = 0;
     &SwPrim   = 0;
     nFactu    = 0;
     nSub1     = 0;
     nSub2     = 0;
     nSub3     = 0;

     aCultivo  = "";
|FIN;

|INCLUYE i_variar;
|INCLUYE i_varcon;
|INCLUYE i_varlib;

|| ************** Calculos de Pantalla
|CALCULO Eano; |TIPO 7;
 fLaFecha = ~;
 aAlfa1 = fLaFecha; aAlfa1 = aAlfa1 % -104;
 #0#0 = aAlfa1;
 |PINTA #0#0;
|FCAL;

|CALCULO defecto;
 aAlfa1 = #0#0;
 aAlfa2 = "01.01." + aAlfa1; #0#50 = aAlfa2; |PINTA #0#50;
 aAlfa2 = "31.12." + aAlfa1; #0#51 = aAlfa2; |PINTA #0#51;
|FCAL;

|PROCESO Parrilla;
     |SI #0#1 = 0;
         #0#1 = 0;  |PINTA #0#1;
         #0#2 = 0;  |PINTA #0#2;  |C_M #0#2, 1, "N";
         |PARA nCampo = 3;  |SI nCampo [ 42;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |SINO;
         |C_M #0#2, 1, "S";
         |PARA nCampo = 3;  |SI nCampo [ 42;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "N";
               #0nCampo = 0;  |PINTA #0nCampo;
         |SIGUE;
     |FINSI;
|FPRC;

|PROCESO NoMasEmpresas;
     |SI #0Campo = 0;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 42;  |HACIENDO nCampo = nCampo + 1;
               #0nCampo = 0;  |C_M #0nCampo, 1, "N";
         |SIGUE;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 42;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |FINSI;
|FPRC;

|CALCULO LaSeleccion; |TIPO 0;
  |SI #0Campo = "P";
      |C_M #0#48,1,"S"; |C_M #0#49,1,"S";
      |C_M #0#50,1,"N"; |C_M #0#51,1,"N";
      |HAZ defecto;
  |SINO;
      |C_M #0#48,1,"N"; |C_M #0#49,1,"N";
      |C_M #0#50,1,"S"; |C_M #0#51,1,"S";
      #0#48 = 00; #0#49 = 12;
      |PINTA #0#48; |PINTA #0#49;
  |FINSI;
|FCAL;

|CALCULO YaSeCual; |TIPO 7;
 |SI enEmpresa ! 0;
     #0#55 = "S"; |PINTA #0#55;
     |C_M #0#55, 1, "N";
 |FINSI;
|FCAL;

|PROCESO QueFormato; |TIPO 0;
  |SI #0#59 = 1;
      |C_M #0#57,1, "N"; #0#57 = "N"; |PINTA #0#57;
      #0#56 = "* LIBRO DE INGRESOS * ";
      |C_M #0#60,1, "N"; #0#60 = 1; |PINTA #0#60;
      |C_M #0#61,1, "N"; #0#61 = 0; |PINTA #0#61;
  |SINO;
      |C_M #0#57,1, "S";
      #0#56 = "LIBRO REGISTRO DE VENTAS E INGRESOS DEL R.E. AGRICULTURA, GANDERIA Y PESCA";
      |C_M #0#60,1, "S";
      |C_M #0#61,1, "S";
  |FINSI;
  |PINTA #0#56;
|FPRC;

|| **************************************************************************
|PROCESO ElDetalle_A;
    |DDEFECTO #999;
    #999#0 = aCultivo;
    #999#1 = #102#0;
    #999#2 = #102#1;
    |LEE 101#999=;
    |SI FSalida ! 0;
        |DDEFECTO #999;
        #999#0  = aCultivo;
        #999#1  = #102#0;
        #999#2  = #102#1;
        #999#3  = #102#2;  || Serie
        #999#4  = #102#3;  || Factura
        #999#5  = #102#4;  || Fecha Factura
        #999#6  = #102#13; || Nombre Cliente
        #999#7  = #102#14; || Nif
        #999#21 = #102#8;  || Fecha Contable
        |GRABA 020#999b;
    |FINSI;
|FPRC;

|PROCESO ElDetalle_D;

    #999#16 = (#999#8 * #33#5 ) ! EURODBMOD;

    |GRABA 020#999a;
    |LIBERA #999;
    SwAlgun = 1;
|FPRC;

|PROCESO ElTotal;
 || .... Total Recargo
   #998#0 = aCultivo;  ||Cultivo
   #998#1 = "R";
   #998#2 = "D";
   #998#3 = "N";
   #998#4 = "R";
   #998#5 = #103#9;
   |LEE 101#998=;
   |SI FSalida ! 0;
       |DDEFECTO #998;
       #998#0 = aCultivo;  ||Cultivo
       #998#4 = "R";
       #998#5 = #103#9;
       |GRABA 020#998b;
   |FINSI;
   #998#6 = #998#6 + ((#103#6  / enConversor) ! EURODBMOD);   ||Base Imponible
   #998#7 = #998#7 + ((#103#10 / enConversor) ! EURODBMOD);   ||Cuota recargo

   |GRABA 020#998a;
   |LIBERA #998;

 || .... Total Retencion
   #998#0 = aCultivo;  ||Cultivo
   #998#1 = "R";
   #998#2 = "D";
   #998#3 = "N";
   #998#4 = "T";
   #998#5 = #103#21;
   |LEE 101#998=;
   |SI FSalida ! 0;
       |DDEFECTO #998;
       #998#0 = aCultivo;  ||Cultivo
       #998#4 = "T";
       #998#5 = #103#21;
       |GRABA 020#998b;
   |FINSI;
   #998#6 = #998#6 + ((#103#20 / enConversor) ! EURODBMOD);   ||Base Imponible
   #998#7 = #998#7 + ((#103#22 / enConversor) ! EURODBMOD);   ||Cuota recargo

   |GRABA 020#998a;
   |LIBERA #998;

   |SI #0#59 = 2; |Y #0#44 ! "D";
      || ... Graba Resumen 1 por Conceptos
      |ABRE #901;
      #901#0 = aCultivo;
      #901#5 = "";
      |LEE 001#901=;
      |SI FSalida ! 0;
          |DDEFECTO #901;
          #901#0 = aCultivo;
          #901#5 = "";
          |GRABA 020#901c;
     |FINSI;
     |SI nSwEsp = 0;
         #901#1 = #901#1 + #103#6;
         #901#2 = #901#2 + #103#8;
         #901#3 = #901#3 + #103#10;
         #901#6 = #901#6 + #103#22;
     |FINSI;
     #901#4 = #901#4 + (#103#6 + #103#8 + #103#10 - #103#22);
     |SI nSwEsp ! 0;
         #901#7 = #901#7 + nSub1;
         #901#8 = #901#8 + nSub2;
         #901#9 = #901#9 + nSub3;
     |FINSI;
     |GRABA 020#901a;
     |CIERRA #901;
  |FINSI;
|FPRC;

|PROCESO LeeLinea;

   aAlfa1 = #103#30; |QBF aAlfa1;
   |SI aAlfa1 = "";
       #103#30 = #102#10;
       aAlfa1 = #103#31; |QBF aAlfa1; |SI aAlfa1 = ""; #103#31 = #102#11; |FINSI;
   |FINSI;

   |SI aMulDep = "S";
        nDepar = #103#30;
        |SI nDepar = 0;  nDepar = 1;   |FINSI;
        |ABRE #114;
        #114#0 = enEmpresa;
        #114#1 = nDepar;
        |LEE 040#114=;
        |SI FSalida ! 0;  nDepar = 1;  |FINSI;
        |CIERRA #114;

        |SI nDepar < #0#45; |O nDepar > #0#46;  ||desde/hasta actividad
            |ACABA;
        |FINSI;

        |SI #114#9 ! 6; |Y #114#9 ! 7; |Y #114#9 ! 11;
          |Y #114#9 ! 12; |Y #114#9 ! 16; |Y #114#9 ! 17;
             |ACABA;                             || No es actividad Agricola
        |FINSI;
    || .....................................................................
        aCultivo = #103#31; |QBF aCultivo;
        |SI aCultivo = ""; |ACABA; |FINSI;

       #33#0 = aCultivo;   ||  Subdepartamento
       |LEE 040#33=;
       |SI FSalida ! 0;  |ACABA;  |FINSI;

        |HAZ ElDetalle_A;
 |FINSI;

 |ABRE #30;
 |DDEFECTO #30;
 #30#0 = #103#3;
 |LEE 001#30=;
 |CIERRA #30;

 nSwEsp = 0;
 nSub1  = 0;
 nSub2  = 0;
 nSub3  = 0;

 |SI #103#3 ] 800; |Y #103#3 [ 899; |Y #0#59 = 2;
     nSwEsp = 1;
     aAlfa1 = #30#1; aAlfa1 = aAlfa1 > " ";
     aAlfa1 = aAlfa1 - "INDEMNIZA";
     |SI FSalida ! 0;  nSwEsp = 2; |FINSI;
 |FINSI;

 nTotalLinea = (#103#6 + #103#8 + #103#10 - #103#22) ! EURODBMOD;
 |SI nSwEsp = 0;
     #999#8 = #999#8 + ((#103#6 / enConversor) ! EURODBMOD);  ||Base Imponible
     |SI #103#8 ! 0;
         #999#9  = #103#7;            || % IVA
                                                 ||Cuota IVA (no tiene los agric.)
         #999#10 = #999#10 + ((#103#8 / enConversor) ! EURODBMOD);
     |FINSI;
     |SI #103#10 ! 0;
         #999#11 = #103#9;             || % recargo
         #999#12 = #999#12 + ((#103#10 / enConversor) ! EURODBMOD); ||Cuota recargo
     |FINSI;
     |SI #103#22 ! 0;
         #999#13 = #999#13 + ((#103#20 / enConversor) ! EURODBMOD); || base retencion
         #999#14 = #103#21;             || % retencion
         #999#15 = #999#15 + ((#103#22 / enConversor) ! EURODBMOD); || cuota retencion
     |FINSI;

  |SINO;

      |SI nSwEsp = 2;  || indemnizaciones
          #999#20 = #999#20 + nTotalLinea;
          nSub3 = nTotalLinea;
      |SINO;
          |SI #30#74 = "S";
              #999#19 = #999#19 + nTotalLinea;
              nSub2 = nTotalLinea;
          |SINO;
              #999#18 = #999#18 + nTotalLinea;
              nSub1 = nTotalLinea;
          |FINSI;
      |FINSI;

  |FINSI;
  #999#17 = #999#17 + nTotalLinea;

  |HAZ ElTotal;

  |SI aMulDep = "S";
      |HAZ ElDetalle_D;
  |FINSI;
|FPRC;

|DEFBUCLE caivalin;
     #103#1;
     ;
     #102#0, #102#1, 01;
     #102#0, #102#1, 99;
     ;
     NULL, LeeLinea;
|FIN;


|PROCESO LeeCabecera;
    ||..... Seleccion de Actividad
    |SI aMulDep ! "S";
        nDepar = #102#10;
        |SI nDepar = 0;  nDepar = 1;   |FINSI;
        |ABRE #114;
        #114#0 = enEmpresa;
        #114#1 = nDepar;
        |LEE 040#114=;
        |SI FSalida ! 0;  nDepar = 1;  |FINSI;
        |CIERRA #114;

        |SI nDepar < #0#45; |O nDepar > #0#46;  ||desde/hasta actividad
            |ACABA;
        |FINSI;

        |SI #114#9 ! 6; |Y #114#9 ! 7; |Y #114#9 ! 11;
          |Y #114#9 ! 12; |Y #114#9 ! 16; |Y #114#9 ! 17;
             |ACABA;                             || No es actividad Agricola
        |FINSI;
    || .....................................................................
        aCultivo = #102#11; |QBF aCultivo;
        |SI aCultivo = ""; |ACABA; |FINSI;

        #33#0 = aCultivo;   ||  Subdepartamento
        |LEE 040#33=;
        |SI FSalida ! 0;  |ACABA;  |FINSI;

        |HAZ ElDetalle_A;

        |BUCLE caivalin;

        |HAZ ElDetalle_D;

    |SINO;

        |BUCLE caivalin;

    |FINSI;
|FPRC;

|DEFBUCLE camaniva;
     #102#1;
     4,5,36;
     #0#52, 0000000000, fDFecha, nDPerio, "R";
     #0#53, 9999999999, fHFecha, nHPerio, "R";
     ;
     NULL, LeeCabecera;
|FIN;

|| -----------------------------------------------------------------------
|PROCESO ActAgric0;
 |SI #114#9 = 6; |O #114#9 = 7; |O #114#9 = 11; |O #114#9 = 12;
     |O #114#9 = 16; |O #114#9 = 17;
     SwAgricola = 1;
     |ERROR10;
 |FINSI;
|FPRC;

|DEFBUCLE ActAgric;
  #114#1;
  ;
  enEmpresa, 00;
  enEmpresa, 99;
  e;
  NULL, ActAgric0;
|FIN;

|| -----------------------------------------------------------------------
|PROCESO Imprime_R2
  nSwPrint = 1;
  nCultiu = #901#0;
  |HAZ LeeCultivo;
  |PRINT;
|FPRC;

|DEFBUCLE dsmow031;
     #901#1;
     ;
     " ", 000;
     "z", 999;
     ;
     NULL, Imprime_R2;
|FIN;

|PROCESO Imprime_R;
  nSwPrint = 1;
  nCultiu = #998#0;
  |HAZ LeeCultivo;
  |PRINT;
|FPRC;

|DEFBUCLE dsmow026;
     #998#1;
     ;
     000,"R","D","N","R",0;
     999,"R","D","N","T",999;
     ;
     NULL, Imprime_R;
|FIN;

|| -----------------------
|PROCESO LeeCultivo;
 aD1 = ""; aD2 = ""; aD3 = ""; aD4 = "";
 enTot1 = 0; enTot2 = 0; enTot3 = 0;

 |ABRE #33;
 #33#0 = nCultiu;
 |LEE 040#33=;
 |SI FSalida ! 0;  |DDEFECTO #33; |FINSI;

 aCampo0 = #33#1;
 |HAZ descripcion;
 aD1 = aCampo1; aD2 = aCampo2;
 aCampo0 = #33#4;
 |HAZ descripcion;
 aD3 = aCampo1; aD4 = aCampo2;
|FPRC;

|PROCESO Imprime_D;
   |SI nSwPrint = 0;
       nSwPrint = 1;
       nCultiu = #999#0;
       |HAZ LeeCultivo;
   |FINSI;

   |SI nCultiu ! #999#0;
       |PIEINF;
       nCultiu = #999#0;
       |HAZ LeeCultivo;
   |FINSI;
   |PRINT;
   enTot1 = enTot1 + #999#8;
   enTot2 = enTot2 + #999#13;
   enTot3 = enTot3 + #999#16;
   nSwPrint = 1;
|FPRC;

|DEFBUCLE dsmow025;
     #999#1;
     ;
     000,01, 0000000001;
     999,99, 9999999999;
     ;
     NULL, Imprime_D;
|FIN;

|PROCESO Imprime_DS;
   |SI #0#61 = 0;
       #999#22 = #999#4;
   |SINO;
       #999#22 = nFactu;
       nFactu = nFactu + 1;
   |FINSI;

   nCultiu = #999#0;
   |HAZ LeeCultivo;

   |PRINT;
   nSwPrint = 1;
   enTot11 = enTot11 + #999#8;
   enTot12 = enTot12 + #999#10;
   enTot13 = enTot13 + #999#15;
   enTot4 = enTot4 + #999#17;
   enTot5 = enTot5 + #999#18;
   enTot6 = enTot6 + #999#19;
   enTot7 = enTot7 + #999#20;
   SwPrim = 1;
|FPRC;

|DEFBUCLE dsmow025_4;
     #999#4;
     ;
     "01.01.0000", "     ", 00001;
     "31.12.2999", "zzzzz", 99999;
     ;
     NULL, Imprime_DS;
|FIN;

|DEFBUCLE dsmow025_3;
     #999#3;
     ;
     "01.01.0000", "     ", 00001;
     "31.12.2999", "zzzzz", 99999;
     ;
     NULL, Imprime_DS;
|FIN;

|DEFBUCLE dsmow025_2;
     #999#2;
     ;
     "     ", 00001;
     "zzzzz", 99999;
     ;
     NULL, Imprime_DS;
|FIN;

|PROCESO Selecciona;

     |SI #101#21 = "I";
         |SI #0#55 = "N"; |ACABA; |FINSI;
     |FINSI;

     enEmpresa  = #101#2;
     enPerConta = #101#3;
     eaLaMonEmp = #101#28;
     |HAZ EnConversor;

     eaNombreEmp = #101#1;  ||Empresa

     SwAgricola = 0;
     |BUCLE ActAgric;
     |SI SwAgricola = 0; |ACABA; |FINSI;

     |ATRI S;
     |PINTA 2306, "Empresa : ";
     |PINTA 2316, #101#2;
     |PINTA 2322, #101#1;
     |ATRI s;

     aFichero = ("25" + Usuario + "zzzzzzzz") % 108;
     |NOME_DAT #999 aFichero;
     |ABRE #999;  |CIERRA #999;  |DELINDEX #999;
     aFichero = ("26" + Usuario + "zzzzzzzz") % 108;
     |NOME_DAT #998 aFichero;
     |ABRE #998;  |CIERRA #998;  |DELINDEX #998;
     aFichero = ("31" + Usuario) % 108;
     |NOME_DAT #901 aFichero;
     |ABRE #901;  |CIERRA #901;  |DELINDEX #901;

     |DBASS aPathConta;   |QBT aPathConta;
     aPathConta = aPathConta + "contagen/fich/" + (("00000" + #101#2) % -105) + #101#3 + "/";

     |PATH_DAT #102 aPathConta;
     |PATH_DAT #103 aPathConta;
     |PATH_DAT #104 aPathConta;
     |ABRE #caivaasi;
     |DDEFECTO #caivaasi;
     |LEE 000#caivaasi.p;
     |CIERRA #caivaasi;
     aMulDep = #caivaasi#132;

     SwAlgun = 0;
     |ABRE #999;
     |ABRE #998;
     |ABRE #33;
     |BUCLE camaniva;
     |CIERRA #33;
     |CIERRA #999;
     |CIERRA #998;

  |SI SwAlgun = 1;
      eaTituloL = #0#56; ||Titulo del Libro
      |SI #0#57 = "S";
          |SUB_EJECUTA_NP ":basica/agiy0016";
      |FINSI;
      nPagina = 1;
      SwPrim  = 0;
      nFactu = #0#61;
      enTot11 = 0; enTot12 = 0; enTot13 = 0;
      enTot4  = 0; enTot5  = 0; enTot6  = 0; enTot7 = 0;
      |SI #0#44 ! "R";
         informe = "dsmoy102";
         |SI #0#59 = 2;
             informe = "dsmoy302";
             enActividad = 0;
             |HAZ LeeDatosEmpresa;
         |FINSI;
         |INFOR informe;
         |SI FSalida = 0;
             nSwPrint = 0;
             |SI #0#59 = 1; |BUCLE dsmow025;  |FINSI;
             |SI #0#59 = 2;
                 |SI #0#60 = 1; |BUCLE dsmow025_3; |FINSI;
                 |SI #0#60 = 2; |BUCLE dsmow025_4; |FINSI;
                 |SI #0#60 = 3; |BUCLE dsmow025_2; |FINSI;
             |FINSI;
             |SI nSwPrint = 1;  |PIEINF; |FINSI;
             |FININF;
         |FINSI;
     |FINSI;
     |SI #0#44 ! "D";
         informe = "dsmoy102";
         |SI #0#59 = 2; informe = "dsmoy402"; |FINSI;
         |INFOR informe;
         |SI FSalida = 0;
             nSwPrint = 0;
             |SI #0#59 = 1; |BUCLE dsmow026; |FINSI;
             |SI #0#59 = 2; |BUCLE dsmow031; |FINSI;
             |SI nSwPrint = 1;  |PIEINF; |FINSI;
             |FININF;
         |FINSI;
     |FINSI;
  |FINSI;

  |DELINDEX #901;
  |DELINDEX #999;
  |DELINDEX #998;
|FPRC;

|DEFBUCLE canempre;
     #101#4;
     ;
     nDEmpresa, nEjer;
     nHEmpresa, nEjer;
     ;
     NULL, Selecciona;
|FIN;

|| **************************************************************************
|PROCESO Tipo3;  |TIPO 3;

     enEjer = #0#0;
     aAlfa  = #0#0;
     aAlfa  = aAlfa % -102;
     nEjer  = aAlfa;        || Ejercicio dos digitos

     fDFecha  = "01.01.1900";
     fHFecha  = "31.12.2090";
     nDPerio  = 0;
     nHPerio  = 99;
     |SI #0#47 = "F";
         fDFecha  = #0#50;
         fHFecha  = #0#51;
         efDFecha = fDFecha;
         efHFecha = fHFecha;
     |SINO;
         nDPerio  = #0#48;
         nHPerio  = #0#49;

         aAlfa2 = #0#1;
         |SI #0#1 [ 2009;
             aAlfa1 = "01.01." + aAlfa2;
             |SI nDPerio = 2; aAlfa1 = "01.04." + aAlfa2; |FINSI;
             |SI nDPerio = 3; aAlfa1 = "01.07." + aAlfa2; |FINSI;
             |SI nDPerio = 4; aAlfa1 = "01.10." + aAlfa2; |FINSI;
             efDFecha = aAlfa1;

             aAlfa1 = "31.03." + aAlfa2;
             |SI nHPerio = 2; aAlfa1 = "30.06." + aAlfa2; |FINSI;
             |SI nHPerio = 3; aAlfa1 = "30.09." + aAlfa2; |FINSI;
             |SI nHPerio = 4; aAlfa1 = "31.12." + aAlfa2; |FINSI;
             efHFecha = aAlfa1;
         |SINO;
             aAlfa1 = "01.01." + aAlfa2;
             |SI nDPerio = 1;  |O nDPerio = 2;  |O nDPerio = 3;  aAlfa1 = "01.01." + aAlfa2; |FINSI;
             |SI nDPerio = 4;  |O nDPerio = 5;  |O nDPerio = 6;  aAlfa1 = "01.04." + aAlfa2; |FINSI;
             |SI nDPerio = 7;  |O nDPerio = 8;  |O nDPerio = 9;  aAlfa1 = "01.07." + aAlfa2; |FINSI;
             |SI nDPerio = 10; |O nDPerio = 11; |O nDPerio = 12; aAlfa1 = "01.10." + aAlfa2; |FINSI;
             efDFecha = aAlfa1;
             aAlfa1 = "31.03." + aAlfa2;
             |SI nHPerio = 1;  |O nDPerio = 2;  |O nDPerio = 3;  aAlfa1 = "31.03." + aAlfa2; |FINSI;
             |SI nHPerio = 4;  |O nDPerio = 5;  |O nDPerio = 6;  aAlfa1 = "30.06." + aAlfa2; |FINSI;
             |SI nHPerio = 7;  |O nDPerio = 8;  |O nDPerio = 9;  aAlfa1 = "30.09." + aAlfa2; |FINSI;
             |SI nHPerio = 10; |O nDPerio = 11; |O nDPerio = 12; aAlfa1 = "31.12." + aAlfa2; |FINSI;
             efHFecha = aAlfa1;
         |FINSI;
     |FINSI;

     |MENAV "0000 Recuerde seleccionar el papel de la impresora en Horizontal";

     |IMPRE 0;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |SI #0#1 ! 0;
         nDEmpresa = #0#1;
         nHEmpresa = #0#2;
         |BUCLE canempre;
     |SINO;
         |PARA nCampo = 3;  |SI nCampo [ 42;  |HACIENDO nCampo = nCampo + 1;
               |SI #0nCampo ! 0;
                   nDEmpresa = #0nCampo;
                   nHEmpresa = #0nCampo;
                   |BUCLE canempre;
               |FINSI;
         |SIGUE;
     |FINSI;

     |FINIMP;
|FPRC;

|PROCESO descripcion;
 aCampo1 = aCampo0;
 aCampo2 = "";
 |QBF aCampo0;
 aNum1 = aCampo0 % 0; nNume1 = aNum1;
 |SI nNume1 > 75;
     aAlfa1 = aCampo0 % 7601; |QBF aAlfa1;
     |SI aAlfa1 = "," |O aAlfa1 = "";
         aCampo1 = aCampo0 %  175;
         aCampo2 = aCampo0 % 7670;
     |SINO;
         nNum2 = 75
         |PARA nNum1 = 75; |SI nNum1 > 0; |HACIENDO nNum1 = nNum1 - 1;
               aAlfa3 = nNum1; |QBF aAlfa3;
               aAlfa3 = ("00" + aAlfa3) % -102
               aAlfa3 = aAlfa3 + "01";
               aAlfa1 = aCampo0 % aAlfa3; |QBF aAlfa1;
               |SI aAlfa1 = ","; |O aAlfa1 = "";
                   aAlfa3 = nNum1; |QBF aAlfa3;
                   aAlfa3 = ("00" + aAlfa3) % -102
                   aAlfa3 = "1" + aAlfa3;
                   aCampo1 = aCampo0 % aAlfa3;

                   nNum2  = nNum1 + 1;
                   aAlfa3 = nNum2; |QBF aAlfa3;
                   aAlfa3 = ("00" + aAlfa3) % -102
                   aAlfa3 = aAlfa3 + "75";
                   aCampo2 = aCampo0 % aAlfa3;
                   |SAL;
              |FINSI;
         |SIGUE;
     |FINSI;
 |FINSI;
|FPRC;

|PROCESO LeeDatosEmpresa;
    eaInac    = #0#55;
    eaTituloL = #0#56; ||Titulo del Libro
    |HAZ DatosEmpr_w32Con;
|FPRC;
