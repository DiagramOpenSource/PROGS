|FICHEROS;
   dsmoy026 #0;

   dsmom001 #1;
   :/integral/def/dsam0000 #2;
   dsmom014 #7;
   dsmom022 #16;
   dsmom033 #33;

   :/integral/def/dsam0003 #103;
   :/basica/def/agicen14 #114;
   :/basica/def/agicen18 #116;
   :/basica/def/agicen17 #117;
   :/basica/def/agifigen #118;

   :/contagen/def/canempre #300;
   :/contagen/def/camaasil #308;
   :/contagen/def/cacuenta #304;
   :/contagen/def/caconimp #306;  || configuracion impresiones

   dsmow032 #900;          ||Def de impresion
   dsmow075 #901;
   dsmow074 #902;

   cultivo@ #933;
   dsmow043 #999,MANTE;
|FIN;

|VARIABLES;
   &entrada = 1;
   &eSwSelec  = 0;
   &eaDDepar  = "";
   &eaHDepar  = "";
   &eaDSubde  = "";
   &eaHSubde  = "";
   &efDFecha  = @;
   &efHFecha  = @;

   &eaUrbano  = "";
   &eaExpl    = "";
   aMonMod    = "";
   informe    = "";
   inform1    = "";
   inform2    = "";
   nInkey     = 0;
   aFichero   = "";
   nCampo     = 0;
   nDEmpresa  = 0;
   nHEmpresa  = 0;
   nDActividad = 0;
   nHActividad = 0;
   nActGraba   = 0;
   nLaEmpresa  = 0;
   nGTempo     = 0;

   enLocal     = 0;
   enCulti     = 0;
   fFecha      = @;
   nSwLocal    = 0;
   nSwCulti    = 0;
   nValorCat   = 0;
   nIBI        = 0;
   nTerreno    = 0;
   nSPin1      = 0;
   aPath       = "";
   nEjercicio  = 0;

   aAlfa  = "";
   aAlfa1 = "";
   aAlfa2 = "";
   aAlfa3 = "";
   nPara1 = 0;
   nPara2 = 0;
   nNume1 = 0;
   nNume2 = 0;
   nNume3 = 0;
   nNume4 = 0;
   nNume5 = 0;
   nPrintPie = 0;
   SwHayDatos = 0;

   nSwHay    = 0;
   fDFecha   = @;
   fHFecha   = @;
   fecha1    = @;
   fecha2    = @;

   &nPagina   = 0;
   &SwPrim    = 0;
   &SwLinea   = 0;
   &eaTituloL = "";
   &eaTitul2  = "";
   &aTipIG    = "";
   &enSwCta   = 0;
   &eaT1      = "";

   swNoExis = 0;

   aElDepar  = "";
   aElSubde  = "";
   aDCuenta = "";
   aHCuenta = "";
   nSaldo = 0;
   nSald1 = 0;
   nSald2 = 0;
   nSald3 = 0;
   nSald4 = 0;
   nSald5 = 0;
   sw = 0;
   fLaFecha = @;

   nSalPar1 = 0;
   nSalPar2 = 0;
   nSalPar3 = 0;
   nSalPar4 = 0;
   nSalPar5 = 0;
   nImpVar1 = 0;
   nImpVar2 = 0;
   nImpVar3 = 0;
   nImpVar4 = 0;
   nImpVar5 = 0;

   aEjer    = "";
   fTrim1   = @;
   fTrim2   = @;
   fTrim3   = @;
   fTrim4   = @;
   nImporte = 0;
   nCasilla = 0;
   nError   = 0;
   nFactor  = 0;
   aDesc    = "";

   aParam   = "";
   aTex1    = "";
   aTex2    = "";
   aTex3    = "";
   aTex4    = "";
   aTex5    = "";
   nTp100   = 0;
   aTp100   = "";
   aRef     = "";
   nOtrosDatos = 0;

   &eaCodRenta = "";
   &eaCodR2    = "";
   &eaRenta    = "";
   &enEjerR    = 0;
   &enCodRen   = 0;
   &eaCodRen   = "";

   aElDni      = "";
   nOpTitular  = 0;
   aBase       = "";
   nCanal      = 0;
   &eaPathIntegral  = "";

   &enAuto        = 0;
   &enRendimiento = 0;

   aDirInicio = "";
   nQueRenta  = 0;
   aRenta     = "";
   aMas       = "";
   aFich      = "";
|FIN;

|INCLUYE i_variar;
|INCLUYE i_varcon;

|| //////////////////// Calculos de Pantalla \\\\\\\\\\\\\\\\\\\\\\\\\\
|CALCULO CodigoRenta; |TIPO 7;
     |DBASS aBase;  |QBF aBase;
     aAlfa = aBase + "integral/def/dsam0000.mas";
     |XABRE "E", aAlfa, nCanal;
     |SI FSalida ] 0;
         eaPathIntegral  = aBase + "integral/";
     |FINSI;

     |C_M #0#51, 1, "S";
     |SI eaPathIntegral = "";
         |C_M #0#51, 1, "N";  #0#51 = "N";  |PINTA #0#51;
     |FINSI;
|FCAL;

|CALCULO Eano; |TIPO 7;
 |SI enEjer = 0;
     fLaFecha = ~;
     aAlfa1 = fLaFecha; aAlfa1 = aAlfa1 % -104;
     #0#1 = aAlfa1;
 |SINO;
     #0#1 = enEjer;
 |FINSI;
 |PINTA #0#1;
|FCAL;

|CALCULO Tipo7;  |TIPO 7;
     |SI enEmpresa ! 0;
         #0#2  = enEmpresa;  |PINTA #0#2;
         #0#3  = enEmpresa;  |PINTA #0#3;

         |C_M #0#2, 1, "N";
         |C_M #0#3, 1, "N";
     |FINSI;
|FCAL;

|CALCULO Parrilla; |TIPO 0;
     |SI #0#2 = 0;
         #0#2 = 0;  |PINTA #0#2;
         #0#3 = 0;  |PINTA #0#3;  |C_M #0#3, 1, "N";
         |PARA nCampo = 4;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |SINO;
         |SI enEmpresa = 0;
             |C_M #0#2, 1, "S";
             |C_M #0#3, 1, "S";
         |FINSI;

         |PARA nCampo = 4;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "N";
               #0nCampo = 0;  |PINTA #0nCampo;
         |SIGUE;
     |FINSI;
|FCAL;

|CALCULO NoMasEmpresas; |TIPO 0;
     |SI #0Campo = 0;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
               #0nCampo = 0;  |C_M #0nCampo, 1, "N";
         |SIGUE;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |FINSI;
|FCAL;

|CALCULO YaSeCual; |TIPO 7;
 |SI enEmpresa ! 0;
     #0#46 = "S"; |PINTA #0#46;
     #0#24 = "S"; |PINTA #0#24;
     #0#25 = 00;  |PINTA #0#25;
     #0#26 = 99;  |PINTA #0#26;
     |C_M #0#24, 1, "N";
     |C_M #0#25, 1, "N";
     |C_M #0#26, 1, "N";
     |C_M #0#46, 1, "N";
 |FINSI;
|FCAL;

|CALCULO SelTipo0; |TIPO 0;
 |SI #0#46 = "N";
     #0#25 = 0;   |PINTA #0#25;  |C_M #0#25, 1, "N";
     #0#26 = 99;  |PINTA #0#26;  |C_M #0#26, 1, "N";
     |PARA nCampo = 27;  |SI nCampo [ 36;  |HACIENDO nCampo = nCampo + 1;
           |C_M #0nCampo, 1, "S";
     |SIGUE;
 |SINO;
     |SI enEmpresa = 0;
         |C_M #0#25, 1, "S";
         |C_M #0#26, 1, "S";
     |FINSI;
     |PARA nCampo = 27;  |SI nCampo [ 36;  |HACIENDO nCampo = nCampo + 1;
           |C_M #0nCampo, 1, "N";
           #0nCampo = 0;  |PINTA #0nCampo;
     |SIGUE;
 |FINSI;
|FCAL;

|CALCULO SelTipo1;
 |SI #0Campo = 0;
     |PARA nCampo = Campo + 1;  |SI nCampo [ 36;  |HACIENDO nCampo = nCampo + 1;
           #0nCampo = 0;  |C_M #0nCampo, 1, "N";
     |SIGUE;
 |SINO;
     |PARA nCampo = Campo + 1;  |SI nCampo [ 36;  |HACIENDO nCampo = nCampo + 1;
           |C_M #0nCampo, 1, "S";
     |SIGUE;
 |FINSI;
|FCAL;

|| **********************************************************************************
|| **********************************************************************************
|| ....................... Situa la Empresa Contable

|PROCESO LaActividad;
 |SI #agicen14#6 > "01.01.0000";
     |SI #0#24 = "N"; |ACABA; |FINSI;
 |FINSI;

 |SI aParam = "L";
     |SI #agicen14#3 = "8612"; |O #agicen14#3 = "8611"; |O #agicen14#3 = "862 ";
         enSwSelEmp = 1;
         |ERROR10;
     |FINSI;
 |SINO;
      |SI #agicen14#9 = 6; |O #agicen14#9 = 7; |O #agicen14#9 = 11; |O #agicen14#9 = 12;
          |O #agicen14#9 = 16; |O #agicen14#9 = 17;
          enSwSelEmp = 1;       || Agricola
          |ERROR10;
      |FINSI;
 |FINSI;
|FPRC;

|DEFBUCLE LaActividad;
  #agicen14#1,
  ;
  enEmpresa, 01;
  enEmpresa, 99;
  e;
  NULL, LaActividad;
|FIN;

|PROCESO VeoSiHay;
   nSwHay = 1;
   |ERROR10;
|FPRC;

|DEFBUCLE VeoSiHay;
     #308#1;
     21,28;
     01, "01.01.0000", 000001, 0001, eaDDepar, eaDSubde;
     98, "31.12.2999", 999999, 9999, eaHDepar, eaHSubde;
     ;
     NULL, VeoSiHay;
|FIN;

|| .................... graba Contabilidad
|PROCESO LEmpresaConta;

   enEmpresa  = #canempre#2;
   enPerConta = #canempre#3;   || Periodo Contable

   |SI #0#24 = "N"; |Y #canempre#21 = "I"; |ACABA; |FINSI;

|| ------------------------------------------------------------------------
   aAlfa1 = enEmpresa;  aAlfa1 = ("00000" + aAlfa1) % -105;
   aAlfa2 = enPerConta; aAlfa2 = ("0" + aAlfa2) % -101;
   aPathConta  = aPath + "contagen/fich/" + aAlfa1 + aAlfa2 + "/";

   |SI #canempre#22 ! "S";
       |PATH_DAT #308 aPathConta;
       eaDDepar  = #0#38; |QBF eaDDepar; eaDDepar  = ("00" + eaDDepar) % -102;
       eaHDepar  = #0#39; |QBF eaHDepar; eaHDepar  = ("00" + eaHDepar) % -102;
       eaDSubde  = #0#47; |QBF eaDSubde; eaDSubde  = ("000" + eaDSubde) % -103;
       eaHSubde  = #0#48; |QBF eaHSubde; eaHSubde  = ("000" + eaHSubde) % -103;

       nSwHay = 0;
       |BUCLE VeoSiHay;
       |SI nSwHay = 0;
           |ACABA;
       |FINSI;
   |FINSI;

   nGTempo = nGTempo + 1;
   #999#0 = nGTempo;       || Guia
   #999#1 = enEmpresa;     || Empresa
   #999#2 = enPerConta;    || Periodo Contable
   #999#3 = enEjer;        || A¤o
   #999#4 = #canempre#11;  || Desde Mes
   #999#5 = #canempre#13;  || N§ Digitos
   #999#6 = #canempre#14;  || Digitos Nivel 1
   #999#7 = #canempre#15;  || Digitos Nivel 2
   #999#8 = #canempre#16;  || Digitos Nivel 3
   #999#9 = #canempre#17;  || Digitos Nivel 4
   #999#10 = #canempre#18; || Digitos Nivel 5
   #999#11 = #canempre#19; || Digitos Nivel 6
   eaEstadoEmp = "S"; |SI #canempre#21 = "I"; eaEstadoEmp = "N"; |FINSI;
   #999#12 = eaEstadoEmp;  || Estado
   #999#13 = #canempre#28; || Moneda
   #999#14 = #canempre#1;  || Nombre Empresa
   #999#15 = aPathConta;   || Path
   #999#16 = #0#43;        || Emitir
   |GRABA 020#999n;
|FPRC;

|PROCESO LEmpresaEOS;
 enEmpresa  = #agifigen#0;
 enPerConta = 0;

 eaEstadoEmp = "S";
 |SI #agifigen#94 > "01.01.0000";
     eaEstadoEmp = "N";
     |SI #0#24 = "N"; |ACABA; |FINSI;
 |FINSI;

|| ... Seleccion de Tipo de Empresa
  nSPin1 = #agifigen#87;
  enSwSelEmp = 1;
  |SI #0#46 = "S";
      |SI nSPin1 ] #0#25; |Y nSPin1 [ #0#26;
          enSwSelEmp = 0;
      |FINSI;
  |SINO;
      |SI nSPin1 = #0#27;
          enSwSelEmp = 0;
      |SINO;
          |PARA nCampo = 28;  |SI nCampo [ 36;  |HACIENDO nCampo = nCampo + 1;
                |SI nSPin1 = #0nCampo; |Y nSPin1 ! 0;
                    enSwSelEmp = 0;
                    |SAL;
                |FINSI;
          |SIGUE;
      |FINSI;
  |FINSI;
  |SI enSwSelEmp = 1; |ACABA; |FINSI;
|| ------------------------------------------------------------------------

   enSwSelEmp = 0;
   |BUCLE LaActividad;
   |SI enSwSelEmp = 1;
       |SELECT #4#canempre;
       #canempre#2  = enEmpresa;
       #canempre#26 = nEjercicio;
       |LEE 001#canempre.=;
       |SI FSalida = 0;
           |HAZ LEmpresaConta;
       |FINSI;
       |SELECT #1#canempre;
   |FINSI;
|FPRC;

|DEFBUCLE agifigen;
 #agifigen#1;
 ;
 nDEmpresa;
 nHEmpresa;
 e;
 NULL, LEmpresaEOS;
|FIN;

||///////////////////////////////////////////////////////////////////
|CALCULO Datos7; |TIPO 7;
 |MENSA "    <F3> Cambiar Emitir Balance S/N";
 |ATRI R; |PINTA 1169, "Imp"; |ATRI 0;
|FCAL;

|CALCULO DatosManip; |TIPO 11;
   nInkey = FSalida;
   |BLIN 04;

   |SI nInkey = 11;
       |SI #999#16 = "S";
           #999#16 = "N";
       |SINO;
           #999#16 = "S";
       |FINSI;
       |GRABA 020#999a;
   |FINSI;
|FCAL;

|RUTINA inferior;
   #999#0 = 00001;
|FRUT;

|RUTINA superior;
   #999#0 = 99999;
|FRUT;

||///////////////////////////////////////////////////////////////////
|PROCESO MiroRenta;

    |DBASS aDirInicio;
    nQueRenta = 0;
    aRenta = #0#1;
    aRenta = "ren" + aRenta;
    aMas  = aDirInicio + aRenta + "/def/rem00500";

    |CARGA_DEF aMas, cultivo@;
    |SI FSalida ! 0;
         || ...  |MENAV  "         Error en cargar el Def ... Proceso Abortado.";
         aMas = "";
         |ACABA;      ||No existe Renta
    |FINSI;

    aFich = aDirInicio + aRenta + "/fich/";
    |PATH_DAT #cultivo@ #aFich#;
    nQueRenta = #0#1;
|FPRC;

|PROCESO DesCargaRenta;
    |SI aMas ! ""; |DESCARGA_DEF #cultivo@; aMas = ""; |FINSI;
|FPRC;

||///////////////////////////////////////////////////////////////////
|CALCULO Tipo3;  |TIPO 3;
    #0#0  = "N"; ||  No es Inmovilizado

    aMonMod = "P";
    |HAZ LeeEURODBMOD;
    |SI EURODBMOD = 2; aMonMod = "E"; |FINSI;

    eaRenta = ""; enEjerR = 0;
    |SI #0#51 = "S";  eaRenta = "Cod.Renta"; enEjerR = #0#1; |FINSI;

    |HAZ MiroRenta;

    nTp100 = #0#49;
    aTp100 = nTp100; |QBF aTp100;
    enEjer = #0#1;
    |HAZ Verm014Plan;
    |SI enPlan14 = 0; |ACABA; |FINSI;

    aAlfa1 = enEjer; |QBF aAlfa1; aEjer = aAlfa1;
    aAlfa1 = aAlfa1 % -102;
    nEjercicio = aAlfa1;
    |DBASS aPath; |QBT aPath;

    aFichero = ("5w" + Usuario) % 108;
    |NOME_DAT #901 aFichero;
    aFichero = ("4w" + Usuario) % 108;
    |NOME_DAT #902 aFichero;

    aFichero = ("3w" + Usuario) % 108;
    |NOME_DAT #999 aFichero;
    |ABRE #999;  |CIERRA #999;  |DELINDEX #999;
    eaFEmpresa = aFichero;

    |ABRE #999;
    nGTempo = 0;   ||Busco Empresas Contables
    |ABRE #canempre;
    |SI #0#2 ! 0;
        nDEmpresa = #0#2;
        nHEmpresa = #0#3;
        |BUCLE agifigen;
    |SINO;
        |PARA nPara1 = 4;  |SI nPara1 [ 23;  |HACIENDO nPara1 = nPara1 + 1;
              |SI #0nPara1 ! 0;
                  nDEmpresa = #0nPara1;
                  nHEmpresa = #0nPara1;
                  |BUCLE agifigen;
              |FINSI;
        |SIGUE;
    |FINSI;
    |CIERRA #canempre;
    |CIERRA #999;

    |SI nGTempo = 0;    || No existe ninguna empresa
        |SI enAuto = 0;
            |MENAV "    Error. No existe ninguna Empresa dentro de la Seleccion ";
        |FINSI;
        |DELINDEX #999;
        |HAZ DesCargaRenta;
        |ACABA;
    |FINSI;

    |SI enAuto = 0;
        |C_V #999#12,1,"N"; |C_P #999#16,1,1270;

        |PUSHV 0509 2375;
        |ENTLINEAL #1#999, -1, 2, 22, 1, inferior, superior;
        |POPV;
    |FINSI;

    |HAZ Imprimir;

    |DELINDEX #901;
    |DELINDEX #902;
    |DELINDEX #999;

    |HAZ DesCargaRenta;
|FCAL;
|| *************************************************************************
|| *************************************************************************
|| ----------------- Datos de la Comunidad
|PROCESO RelComuneros0;

     |PDEFECTO #0, 52,60;
     |SI #1#8 > "01.01.0000";
         |SI #1#8 > efHFecha; |ACABA;  |FINSI;
     |FINSI;
     |SI #1#9 > "01.01.0000";
         |SI #1#9 < efDFecha;  |ACABA;  |FINSI;
     |FINSI;

     |ABRE #2;
     #2#0 = #1#3;
     |LEE 040#2=;
     |SI FSalida ! 0;  |DDEFECTO #2;  |FINSI;
     |CIERRA #2;

     #0#52  = ("00000" + #1#3) % -105;   || Codigo
     #0#55  = #1#5;   || Nombre
     #0#56  = #2#3;   || NIF

     |ABRE #114;
     #114#0 = #2#0; || Empresa
     #114#1 = #1#4;
     |LEE 041#114=;
     |SI FSalida = 0;
          enCodRen = #114#18;
          |SI enEjerR ! 0; |HAZ CodigoRen; |FINSI;
          #0#53 = ("00000" + enCodRen) % -105;  ||Codigo en Renta
          #0#54 = #114#19;  ||Perceptor
     |SINO;
          aAlfa1 = eaCodRenta;
          nOpTitular = 1;
          |SI #0#51 = "S"; aElDni = #2#3; |HAZ LaRentaSI;  |FINSI;
          #0#53 = eaCodR2;
          eaCodRenta = aAlfa1;
     |FINSI;
     |CIERRA #114;
     #0#57 = #1#7;
     #0#58 = #1#8;
     #0#59 = #1#9;
     |SI #0#51 = "N";
         #0#53 = "";
         #0#54 = "";
     |FINSI;

     |PRINT;
     SwLinea = 11;
|FPRC;

|DEFBUCLE RelComumeros;
     #1#1;
     ;
     enEmpresa, enActividad, 0001;
     enEmpresa, enActividad, 9999;
      e;
     NULL, RelComuneros0;
|FIN;

|PROCESO ImpComuneros;
 nNume5 = SwLinea;
 SwLinea = 10;
 |BUCLE RelComumeros;
 |SI SwLinea = 11;
     SwLinea = 12; |PRINT;
 |FINSI;
 SwLinea = nNume5;
|FPRC;
|| *************************************************************************
|PROCESO LeeAux0;

 |SI #901#6 ! 0; |O #901#7 ! 0; |O #901#8 ! 0; |O #901#9 ! 0;

     eaCuenta = #901#11; |QBF eaCuenta;

     enSwCta  = 0;|SI eaCuenta ! ""; enSwCta = 1; |FINSI;

     |SI #901#4 ! "R";
         |SI enAuto = 0; |PRINT; |FINSI;
     |SINO;
          |SI #0#40 = "E";
              |SI enAuto = 0; |PRINT; |FINSI;
          |SINO;
              |SI enAuto = 0; |PIEINF; |FINSI;
              nPrintPie = 1;
          |FINSI;
     |FINSI;

     SwLinea = 1;
     SwPrim  = 1;
     sw      = 1;

     |SI eaCuenta = "";
         |SI #901#4 = "I";
             #900#25 = #900#25 + #901#6;
             #900#26 = #900#26 + #901#7;
             #900#27 = #900#27 + #901#8;
             #900#28 = #900#28 + #901#9;
             #900#29 = #900#29 + #901#10;
         |FINSI;
         |SI #901#4 = "G";
             #900#30 = #900#30 + #901#6;
             #900#31 = #900#31 + #901#7;
             #900#32 = #900#32 + #901#8;
             #900#33 = #900#33 + #901#9;
             #900#34 = #900#34 + #901#10;
         |FINSI;
     |FINSI;
 |FINSI;
|FPRC;

|DEFBUCLE LeeAux;
 #901#1;
 ;
 #902#0, #902#1, #902#2, 01, aTipIG, "            ";
 #902#0, #902#1, #902#2, 99, aTipIG, "zzzzzzzzzzzz";
 ;
 NULL, LeeAux0;
|FIN;

|PROCESO ImpAuxiliar;    || ... Imprimir el balance en detalle

 enActividad = #902#1;
 aElDepar = enActividad; |QBF aElDepar;
 aElDepar = ("00" + aElDepar) % -102;
 |HAZ LeeDatosActividad;

 SwPrim  = 0;
 nPagina = 1;
 |PDEFECTO #900,25,50;
 |SI enAuto = 0;
     |SI #agicen14#13 = "S"; |HAZ ImpComuneros; |FINSI;
 |FINSI;

 eaUrbano = "";
 |SI #902#9 = "U"; eaUrbano = "URBANO"; |FINSI;
 |SI #902#9 = "L"; eaUrbano = "LOCAL "; |FINSI;

 eaTitul2 = "INGRESOS INTEGROS";
 enSwCta  = 0;
 sw       = 0;
 SwLinea  = 0;
 aTipIG   = "I";
 |BUCLE LeeAux;
 |SI sw = 1;
     eaTitul2 = "Total Ingresos computables";
     SwLinea = 2;
     |SI enAuto = 0; |PRINT; |FINSI;
 |FINSI;

 eaTitul2 = "GASTOS FISCALMENTE DEDUCIBLES";
 enSwCta  = 0;
 sw       = 0;
 SwLinea  = 0;
 aTipIG   = "G";
 |BUCLE LeeAux;

|| ... Amortizacion
 enSwCta   = 0;
 nNume1 = #902#3;
 |SI #902#10 ! 0;
     nNume2 = #902#10;
 |SINO;
     nNume2 = (nNume1 % 20) ! EURODBMOD;
 |FINSI;
 nNume3 = (nNume1 - nNume2) % nTp100;
 #900#35 = nNume3;
 #900#36 = 0;
 #900#37 = 0;
 #900#38 = 0;
 #900#39 = nNume3;

|| ... Gastos Totales
 #900#40 = #900#30 + #900#35;
 #900#41 = #900#31 + #900#36;
 #900#42 = #900#32 + #900#37;
 #900#43 = #900#33 + #900#38;
 #900#44 = #900#34 + #900#39;

|| ... Rendimiento Neto
 #900#45 = #900#25 - #900#40;
 #900#46 = #900#26 - #900#41;
 #900#47 = #900#27 - #900#42;
 #900#48 = #900#28 - #900#43;
 #900#49 = #900#29 - #900#44;

 eaExpl = "";
 |SI #902#9 = "U";
     #900#45 = #900#45 % 50;
     #900#46 = #900#46 % 50;
     #900#47 = #900#47 % 50;
     #900#48 = #900#48 % 50;
     #900#49 = #900#49 % 50;
     eaExpl = "(Urbano: 50% de reduccion)";
 |FINSI;
 eaTitul2 = "          Suma parcial de gastos fiscalmente deducibles";
 SwLinea = 2;
 |SI #900#35 ! 0; |O #900#36 ! 0; |O #900#37 ! 0; |O #900#38 ! 0;
     |SI sw = 1;
         |SI enAuto = 0; |PRINT; |FINSI;
     |FINSI;
     SwLinea = 3;
     eaTitul2 = "Amortizacion Fiscal ("+aTp100+"% sobre Valor Catastral menos Valor Suelo)";
     |SI enAuto = 0; |PRINT; |FINSI;
 |FINSI;

 |SI #900#40 ! 0; |O #900#41 ! 0; |O #900#42 ! 0; |O #900#43 ! 0;
     eaTitul2 = "Total Gastos fiscalmente deducibles ";
     SwLinea = 4;
     |SI enAuto = 0; |PRINT; |FINSI;
 |SINO;
     |SI #0#40 = "E";
         eaTitul2 = "";
         SwLinea = 5;
         |SI enAuto = 0; |PRINT; |FINSI;
     |FINSI;
 |FINSI;

|| ... Retenciones
 |DDEFECTO #901;
 enSwCta   = 0;
 sw        = 0;
 SwLinea   = 1;
 aTipIG    = "R";
 nPrintPie = 0;
 |BUCLE LeeAux;
 |SI nPrintPie = 0;
     |SI enAuto = 0; |PIEINF; |FINSI;
 |FINSI;

 enRendimiento = enRendimiento + #900#49;
|FPRC;

|DEFBUCLE LosLocales;
 #902#1;
 ;
 enEmpresa, 00, "   ";
 enEmpresa, 99, "zzz";
 ;
 NULL, ImpAuxiliar;
|FIN;
|| .......................................................................

|PROCESO ImpAuxiliarC;    || ... Imprimir el balance en detalle de Cultivos

 enActividad = #902#1;
 aElDepar = enActividad; |QBF aElDepar;
 aElDepar = ("00" + aElDepar) % -102;
 |HAZ LeeDatosActividad;

 enCulti = #902#2;
 |HAZ LeeDatosCultivo;

 SwPrim  = 0;
 nPagina = 1;
 |PDEFECTO #900,25,50;
 |SI enAuto = 0;
     |SI #agicen14#13 = "S"; |HAZ ImpComuneros; |FINSI;
 |FINSI;

 eaTitul2 = "INGRESOS INTEGROS";
 enSwCta  = 0;
 sw       = 0;
 SwLinea  = 0;
 aTipIG   = "I";
 |BUCLE LeeAux;
 |SI sw = 1;
     eaTitul2 = "Total Ingresos computables";
     SwLinea = 2;
     |SI enAuto = 0; |PRINT; |FINSI;
 |FINSI;

 eaTitul2 = "PORCENTAJE DE I.R.P.F. APLICABLES";
 enSwCta  = 0;
 sw       = 0;
 SwLinea  = 0;
 aTipIG   = "G";
 #901#5  = "Porcentajes de IRPF Aplicables segun tablas del Ejercicio";
 #901#6  = #33#5;
 #901#7  = #33#5;
 #901#8  = #33#5;
 #901#9  = #33#5;
 #901#10 = #33#5;
 |SI enAuto = 0; |PRINT; |FINSI;

 sw      = 1;
 SwLinea = 1;
 aTipIG = "";
|| ... Rendimiento Neto
 #900#45 = #900#25 * #901#6;
 #900#46 = #900#26 * #901#7;
 #900#47 = #900#27 * #901#8;
 #900#48 = #900#28 * #901#9;
 #900#49 = #900#29 * #901#10;

 |SI #0#40 = "E";
     eaTitul2 = "";
     SwLinea = 5;
     |SI enAuto = 0; |PRINT; |FINSI;
 |FINSI;

|| ... Retenciones
 |DDEFECTO #901;
 enSwCta   = 0;
 sw        = 0;
 SwLinea   = 1;
 aTipIG    = "R";
 nPrintPie = 0;
 |BUCLE LeeAux;
 |SI nPrintPie = 0;
     |SI enAuto = 0; |PIEINF; |FINSI;
 |FINSI;

 enRendimiento = enRendimiento + #900#49;
|FPRC;

|DEFBUCLE LosCultivos;
 #902#1;
 ;
 enEmpresa, 00, "   ";
 enEmpresa, 99, "zzz";
 ;
 NULL, ImpAuxiliarC;
|FIN;
|| /////////////////////////////////////////////////////////////////
|PROCESO GrabaAuxi901;
     #901#0  = enEmpresa;
     #901#1  = nActGraba;   ||paco
     #901#2  = enLocal;
     #901#3  = nCasilla;
     #901#4  = aTipIG;
     #901#11 = eaCuenta;
     |LEE 001#901=;
     |SI FSalida ! 0;
         |DDEFECTO #901;
         #901#0  = enEmpresa;
         #901#1  = nActGraba;  ||paco
         #901#2  = enLocal;
         #901#3  = nCasilla;
         #901#4  = aTipIG;
         #901#11 = eaCuenta;
         |HAZ LaDescripcion;
         #901#5  = aDesc;
         |GRABA 020#901c;
     |FINSI;

     nNume1 = 6;
     |SI fFecha > fTrim2; nNume1 = 7; |FINSI;
     |SI fFecha > fTrim3; nNume1 = 8; |FINSI;
     |SI fFecha > fTrim4; nNume1 = 9; |FINSI;

     |SI nCasilla ! 7;
         #901nNume1 = #901nNume1 + nImporte;
     |SINO;
         #901nNume1 = nImporte;
     |FINSI;
     #901#10 = #901#6 + #901#7 + #901#8 + #901#9;

     |GRABA 020#901a;
     SwHayDatos = 1;
|FPRC;

|PROCESO OtrosDatos;
         #902#3 = nValorCat;   ||Valor Catastral
         #902#4 = nIBI;        ||I.B.I.
         #902#10= nTerreno;
         |SI aParam = "L";
             #902#5 = #117#3;
             aAlfa1 = #117#10; |QBF aAlfa1;
             |SI aAlfa1 ! ""; aAlfa1 = aAlfa1 + " "; |FINSI;
             aAlfa1 = aAlfa1 + #117#11; |QBF aAlfa1;
             |SI aAlfa1 ! ""; aAlfa1 = aAlfa1 + ", "; |FINSI;
             aAlfa1 = aAlfa1 + #117#12; |QBF aAlfa1;
             aAlfa2 = ("00" + #117#6) % -102;
             aAlfa3 = ("000" + #117#7) % -103;
             aAlfa1 = aAlfa1 + " " + aAlfa2 + aAlfa3 + " " + #117#8;
             #902#6 = aAlfa1;
             #902#7 = #117#4;
             #902#8 = #117#5;
             #902#9 = #117#16;
         |FINSI;
|FPRC;

|PROCESO GrabaAuxi902;
|| ... La Cabecera
     #902#0 = enEmpresa;
     #902#1 = nActGraba;   ||paco
     #902#2 = enLocal;
     |LEE 001#902=;
     |SI FSalida ! 0;
         |DDEFECTO #902;
         #902#0 = enEmpresa;
         #902#1 = nActGraba;  ||paco
         #902#2 = enLocal;
         |HAZ OtrosDatos;
         |GRABA 020#902c;
     |SINO;
         SwPrim = 1;
         |SI nOtrosDatos = 1; |Y nActGraba ! enActividad;
             |HAZ OtrosDatos;
             |GRABA 020#902a;
         |FINSI;
     |FINSI;
|FPRC;

|| /////////////////////////////////////////////////////////////////

|PROCESO ModAgricola;
     nIBI      = 0;
     nValorCat = 0;
     nTerreno  = 0;
     nCasilla  = 0;
     nImporte  = 0;

     |SI nError = 1;
         |ABRE #16;
         #16#0 = #308#4;
         |LEE 040#16=;
         |SI FSalida = 0;
             nImporte = (#308#9 / enConversor) ! 2;
             nCasilla = 5;
             #7#2 = "R";
         |FINSI;
         |CIERRA #16;
     |SINO;
         nCasilla = 1;
         |SI #7#2 ! "I";  |ACABA;  |FINSI;
         |SI #308#0 = 99; |ACABA;  |FINSI;
     |FINSI;

     |SI nError = 1;  |Y nImporte = 0;  |ACABA;  |FINSI;

     enCulti = #308#28;
     |HAZ LeeDatosCultivo;
     |SI nSwCulti ! 0;  |ACABA;  |FINSI;

     enLocal = enCulti;
     SwPrim  = 0;
     |HAZ GrabaAuxi902;

     |SI nImporte = 0;
         nFactor = 1;
         |SI #7#2 = "I";
             |SI #308#8 = "D"; nFactor = -1; |FINSI;
         |SINO;
             |SI #308#8 = "H"; nFactor = -1; |FINSI;
         |FINSI;

         nImporte = ((#308#9 / enConversor) * nFactor) ! 2;
     |FINSI;

     |SI nCasilla = 5;
         |SI #308#13 = 0; |O #308#14 = 0;
              nImporte = 0;
         |FINSI;
     |FINSI;

     |SI nImporte = 0;  |ACABA;  |FINSI;

     fFecha  = #308#1;
     aTipIG  = #7#2; |SI aTipIG = "C"; #7#2 = "G"; |FINSI;

     eaCuenta = "";
     |HAZ GrabaAuxi901;
     |SI #0#40 = "E";
         eaCuenta = #308#4;
         |HAZ GrabaAuxi901;
     |FINSI;
|FPRC;

|PROCESO Arrendamientos;
     nCasilla = 0;
     nImporte = 0;

     |ABRE #16;
     #16#0 = #308#4;
     |LEE 040#16=;
     |SI FSalida = 0;
         || |SI #308#0 = 99;
             nImporte = (#308#9 / enConversor) ! 2;
             nCasilla = 5;
             #7#2 = "R";
         || |FINSI;
     |SINO;
         |SI #308#0 = 99;  |ACABA;  |FINSI;
         nCasilla = #7#9;
     |FINSI;
     |CIERRA #16;

     |SI nError = 1;  |Y nImporte = 0;  |ACABA;  |FINSI;

     enLocal = #308#28;
     |HAZ LeeDatosLocal;
     |SI nSwLocal ! 0;  |ACABA;  |FINSI;

     SwPrim = 0;
     |HAZ GrabaAuxi902;

     |SI nImporte = 0;
         nFactor = 1;
         |SI #7#2 = "I";
             |SI #308#8 = "D"; nFactor = -1; |FINSI;
         |SINO;
             |SI #308#8 = "H"; nFactor = -1; |FINSI;
         |FINSI;

         nImporte = ((#308#9 / enConversor) * nFactor) ! 2;
     |FINSI;

     |SI nCasilla = 5;
         |SI #308#13 = 0; |O #308#14 = 0; || retenciones que no son de fras.
              nImporte = 0;
         |FINSI;
     |FINSI;

     |SI nImporte = 0;  |ACABA;  |FINSI;

     fFecha  = #308#1;
     aTipIG  = #7#2; |SI aTipIG = "C"; #7#2 = "G"; |FINSI;

     eaCuenta = "";
     |HAZ GrabaAuxi901;
     |SI #0#40 = "E";
         eaCuenta = #308#4;
         |HAZ GrabaAuxi901;
     |FINSI;
/*
  ... No Grabo el Ibi a la Fuerza
     |SI SwPrim = 0;
         fFecha = fec1;
         eaCuenta = "";
         nCasilla = 7;
         aTipIG   = "G";
         nImporte = nIBI;
         |HAZ GrabaAuxi901;
     |FINSI;
*/
|FPRC;

|PROCESO MiraCuentas;
     nError = 0;
     aAlfa  = #308#4 % 104;
     #7#10  = enPlan14;
     #7#0   = aAlfa;
     |LEE 040#7=;
     |SI FSalida ! 0;  nError = 1;  |FINSI;

     aElSubde = #308#28; |QBF aElSubde;
     aElSubde = ("000" + aElSubde) % -103;

     |SI #308#28 ! "   ";
         enActividad = #308#21;
         |SI enActividad = 0; enActividad = 1; |FINSI;
         aElDepar  = enActividad; |QBF aElDepar;
         aElDepar  = ("00" + aElDepar) % -102;
         nActGraba = enActividad;
         |HAZ LeeDatosActividad;
         |SI swNoExis = 0;
             |SI aParam = "L";
                 |HAZ Arrendamientos;
             |SINO;
                 |HAZ ModAgricola;
             |FINSI;
         |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE camaasil;
     #308#3;
     0,21,28;
     "            ", efDFecha, 000001, 0001, 01, eaDDepar, eaDSubde;
     "zzzzzzzzzzzz", efHFecha, 999999, 9999, 98, eaHDepar, eaHSubde;
     ;
     NULL, MiraCuentas;
|FIN;

||////////////////////////////////////////////////////////////////////////

|PROCESO Emision;
   enEmpresa  = #999#1;
   enPerConta = #999#2;
   eaLaMonEmp = #999#13;
   |HAZ EnConversor;
   enEjer     = #999#3;   || A¤o
   dig1       = #999#4;   || Desde Mes
   dig3       = #999#5;   || N§ Digitos
   dig4       = #999#6;   || Digitos Nivel 1
   dig5       = #999#7;   || Digitos Nivel 2
   dig6       = #999#8;   || Digitos Nivel 3
   dig7       = #999#9;   || Digitos Nivel 4
   dig8       = #999#10;  || Digitos Nivel 5
   dig9       = #999#11;  || Digitos Nivel 6
   eaEstadoEmp = #999#12; || Estado
   eaNombreEmp = #999#14; || Nombre Empresa
   aPathConta  = #999#15; |QBF aPathConta; || Path

   || Recoger informacion sobre los niveles ...
   |SI dig3 = 1; MaximoDigitos = dig4; |FINSI;
   |SI dig3 = 2; MaximoDigitos = dig5; |FINSI;
   |SI dig3 = 3; MaximoDigitos = dig6; |FINSI;
   |SI dig3 = 4; MaximoDigitos = dig7; |FINSI;
   |SI dig3 = 5; MaximoDigitos = dig8; |FINSI;
   |SI dig3 = 6; MaximoDigitos = dig9; |FINSI;

   eaTituloL = #0#45; ||Titulo del Libro

   |PATH_DAT #308 aPathConta;
   |PATH_DAT #304 aPathConta;

   |PATH_DAT #306 aPathConta;
   |DDEFECTO #306;  ||Configuracion de Informes
   |ABRE #306;
   #306#0 = 1;
   |LEE 000#306=;
   |CIERRA #306;

   |SI enAuto = 0;
       |SI #0#40 = "E";
           |SI aParam = "L"; |INFOR "dsmoy026"; |FINSI;
           |SI aParam = "A"; |INFOR "dsmoya26"; |FINSI;
       |SINO;
           |SI aParam = "L"; |INFOR "dsmoyr26"; |FINSI;
           |SI aParam = "A"; |INFOR "dsmoys26"; |FINSI;
       |FINSI;

       |SI FSalida ! 0;  |ACABA;  |FINSI;
   |FINSI;

   |HAZ LeeDatosEmpresa;

   SwHayDatos = 0;
   |ABRE #7;
   |ABRE #901;
   |ABRE #902;
   |BUCLE camaasil;
   |CIERRA #902;
   |CIERRA #901;
   |CIERRA #7;

   |SI SwHayDatos = 1;
       |SI aParam = "L";
           |BUCLE LosLocales;
       |SINO;
           |BUCLE LosCultivos;
       |FINSI;
   |FINSI;

   |SI enAuto = 0;
       |FININF;
   |FINSI;
|FPRC;

|DEFBUCLE dsmow043;
 #dsmow043#1;
 16;
 000001, "S";
 999999, "S";
 ;
 NULL, Emision;
|FIN;

|PROCESO Imprimir;
    enEjer     = #0#1;
    eaPlanilla = "dsmom014";  |HAZ PlanDePlanillas;

    eaDDepar  = #0#38; |QBF eaDDepar; eaDDepar  = ("00" + eaDDepar) % -102;
    eaHDepar  = #0#39; |QBF eaHDepar; eaHDepar  = ("00" + eaHDepar) % -102;
    eaDSubde  = #0#47; |QBF eaDSubde; eaDSubde  = ("000" + eaDSubde) % -103;
    eaHSubde  = #0#48; |QBF eaHSubde; eaHSubde  = ("000" + eaHSubde) % -103;

    aAlfa1 = "31.03." + aEjer; fTrim2 = aAlfa1;
    aAlfa1 = "30.06." + aEjer; fTrim3 = aAlfa1;
    aAlfa1 = "30.09." + aEjer; fTrim4 = aAlfa1;

    efDFecha  = "01.01.1900";
    efHFecha  = "31.12.2099";

    |SI enAuto = 0;
        |IMPRE 0;
        |SI FSalida ! 0;  |ACABA;  |FINSI;
    |FINSI;

    |BUCLE dsmow043;

    |SI enAuto = 0;
        |FINIMP;
    |FINSI;
|FPRC;

|| --------------------------
|PROCESO LeeDatosEmpresa;
  |DELINDEX #901;
  |DELINDEX #902;
  |DDEFECTO #900;
  #900#0    = enEmpresa;
  eaTituloL = #0#45; ||Titulo del Libro
  |HAZ DatosEmpr_w32Con;

  aAlfa1 = #0#1; aAlfa1 = "01.01." + aAlfa1; #900#13 = aAlfa1;
  aAlfa1 = #0#1; aAlfa1 = "31.12." + aAlfa1; #900#14 = aAlfa1;
|FPRC;

|PROCESO LeeDatosActividad;
    #900#52 = 0;
  || Lee Actividad en concreto.
    |ABRE #agicen14;
    swNoExis = 0;
    #agicen14#0 = #900#0; || Empresa
    #agicen14#1 = enActividad;
    |LEE 041#agicen14.=;
    |SI FSalida = 0;
        |SI aParam = "L";
            |SI #agicen14#3 ! "8612"; |Y #agicen14#3 ! "8611"; |Y #agicen14#3 ! "862 ";
                swNoExis = 1;
            |FINSI;
        |SINO;
            |SI #agicen14#9 ! 6; |Y #agicen14#9 ! 7; |Y #agicen14#9 ! 11; |Y #agicen14#9 ! 12;
                |Y #agicen14#9 ! 16; |Y #agicen14#9 ! 17;
                swNoExis = 1;
            |FINSI;
        |FINSI;

        #900#15 = #agicen14#1;   ||Act.
        #900#16 = #agicen14#2;   ||Tipo Epig.
        #900#17 = #agicen14#3;   ||Epigrafe
        #900#18 = #agicen14#4;   ||Descripcion Actividad
        #900#19 = #agicen14#5;   ||Fecha Inicio
        #900#20 = #agicen14#6;   ||Fecha Fin
        #900#21 = #agicen14#11;  ||Sector actividad
        #900#22 = #agicen14#12;  ||Sector actividad
        #900#23 = #agicen14#16;  ||Tipo Actividad
        #900#24 = #agicen14#17;  ||Descripcion Tipo Actividad
        #900#51 = #agicen14#20;  ||CNAE
        enCodRen = #agicen14#18;  ||Codigo Renta
        |SI enEjerR ! 0; |HAZ CodigoRen; |FINSI;
        #900#52 = enCodRen;       ||Codigo Renta

        |SI #agicen14#6 > "01.01.0000";
            |SI #0#24 = "N";
                swNoExis = 1;
            |FINSI;
        |FINSI;
    |SINO;
        swNoExis = 1;
    |FINSI;
    |CIERRA #agicen14;

    |SI swNoExis = 0;
        eaCodRenta = "";
        nOpTitular = 0;
        |SI #0#51 = "S"; aElDni = #900#1; |HAZ LaRentaSI;  |FINSI;
        nOpTitular = 1;
        eaCodR2 = "";
    |FINSI;
|FPRC;

|PROCESO BuscoRepetido;
 enLocal   = #902#2;
 nActGraba = #902#1;  ||paco
|FPRC;

|DEFBUCLE BuscoRepetido;
 #902#2;
 ;
 enEmpresa, nDActividad, aRef, "   ";
 enEmpresa, nHActividad, aRef, "zzz";
 ;
 NULL, BuscoRepetido;
|FIN;

|PROCESO LeeDatosLocal;
     nSwLocal  = 0;
     nValorCat = 0;
     nIBI      = 0;
     nTerreno  = 0;

     |ABRE #117;
     |DDEFECTO #117;
     #117#0 = enEmpresa;
     #117#1 = enActividad;
     #117#2 = enLocal;
     |LEE 040#117=;
     |SI FSalida ! 0;
         nSwLocal = 1;
         |CIERRA #117;
         |ACABA;
     |FINSI;
     |CIERRA #117;

     |SI #0#50 ! "N";   ||paco 1243_1463 (#0#50 = "S")
         aRef = #117#3;
         nDActividad = enActividad;
         nHActividad = enActividad;
         |SI #0#50 = "R";
             nDActividad = 01;
             nHActividad = 99;
         |FINSI;
         |BUCLE BuscoRepetido;
     |FINSI;

     nOtrosDatos = 0;
     |ABRE #116;
     |DDEFECTO #116;
     #116#0 = #117#0;
     #116#1 = #117#1;
     #116#2 = enLocal;
     #116#3 = enEjer;
     |LEE 040#116=;
     |SI FSalida = 0;
         nValorCat = #116#4;
         nIBI      = #116#5;
         nTerreno  = #116#6;
         |SI nValorCat ! 0;
             nOtrosDatos = 1;
         |FINSI;
     |FINSI;
     |CIERRA #116;
|FPRC;

|PROCESO LeeDatosCultivo;
     nSwCulti  = 0;
     |ABRE #33;
     |DDEFECTO #33;
     #33#0 = enCulti;
     |LEE 040#33=;
     |SI FSalida ! 0;
         nSwCulti = 1;
     |FINSI;
     |CIERRA #33;
     |SI #33#8 = "A"; eaT1 = "AGRICULTURA"; |FINSI;
     |SI #33#8 = "G"; eaT1 = "GANADERIA";   |FINSI;
     |SI #33#8 = "F"; eaT1 = "FORESTAL";    |FINSI;
     |SI #33#8 = "P"; eaT1 = "PESCA";       |FINSI;

     |SI nQueRenta ! 0;
         |ABRE #933;
         #933#0 = enCulti;
         |LEE 040#933=;
         |SI FSalida = 0;
             |SI nSwCulti = 1;
                 nSwCulti = 0;
                 #33#0 = #933#0;
                 #33#1 = #933#1;
                 #33#2 = #933#2;
             |FINSI;
             #33#3 = #933#3;
             #33#4 = #933#4;
             #33#5 = #933#5;
             #33#8 = #933#6;
             #33#9 = #933#7;
         |FINSI;
         |CIERRA #933;
     |FINSI;
|FPRC;

|PROCESO LaDescripcion;
     aDesc = "";
     |SI aParam = "L";
         |SI nCasilla = 1;   aDesc = "Importe Alquiler";  |FINSI;
     |SINO;
         |SI nCasilla = 1;   aDesc = "Importe de los Ingresos";  |FINSI;
     |FINSI;
     |SI nCasilla = 2;   aDesc = "Aprovechamiento";   |FINSI;
     |SI nCasilla = 3;   aDesc = "Otros ingresos";    |FINSI;
     |SI nCasilla = 4;   aDesc = "Importe Irregular"; |FINSI;
     |SI nCasilla = 5;   aDesc = "Retenciones Deudoras Practicadas"; |FINSI;
     |SI nCasilla = 6;   aDesc = "Intereses";         |FINSI;
     |SI nCasilla = 7;   aDesc = "I.B.I.";            |FINSI;
     |SI nCasilla = 8;   aDesc = "Otros Tributos";    |FINSI;
     |SI nCasilla = 9;   aDesc = "Primas de seguros";   |FINSI;
     |SI nCasilla = 10;  aDesc = "Otros gastos";        |FINSI;
     |SI nCasilla = 11;  aDesc = "Amortizacion";        |FINSI;
     |SI nCasilla = 12;  aDesc = "Compensacion fiscal"; |FINSI;

     |SI eaCuenta ! "";
         |ABRE #304;
         #304#0 = eaCuenta;
         |LEE 001#304=;
         |SI FSalida = 0;
             aDesc = #304#2;
         |FINSI;
         |CIERRA #304;
    |FINSI;
|FPRC;

|PROGRAMA;

 enRendimiento = 0;
 aParam = PARAMETRO; |QBF aParam;
 |SI aParam ! "L"; |Y aParam ! "A";
     aParam = "L";
 |FINSI;

 |SI enAuto ! 0;
     |DDEFECTO #0;
     #0#1 = enEjer;
     #0#2 = enEmpresa;
     #0#3 = enEmpresa;
     #0#38 = enActividad;
     #0#39 = enActividad;
     #0#40 = "R";
     |HAZ Tipo3;
     |ACABA;
 |FINSI;

 |CLS;
 |SI aParam = "L";
     aTex1 = "Rendimientos Procedentes del Arrendamiento de Locales ";
     aTex2 = "B.Rend.Arrendamiento";
     aTex3 = "Arrendamiento (Subdepartamento): ";
     #0#45 = "RENDIMIENTOS PROCEDENTES DEL ARRENDAMIENTO DE LOCALES";
     aTex4 = "Tanto por cien de Amortizacion Fiscal :    ";
     aTex5 = "Agrupar por Referencia Catastral ....:    ";
 |SINO;
     aTex1 = "Rendimientos Act. Agricolas, Ganaderas, Fores. y Pesq.";
     aTex2 = "B.Rend.Agricola y G.";
     aTex3 = "Producto (Subdepartamento) ....: ";
     #0#45 = "RENDIMIENTOS PROCEDENTES DE ACT. AGRICOLAS, GANADERAS, FORESTALES Y PESQUERAS";
     aTex4 = "                                           ";
     aTex5 = "                                          ";
     |C_M #0#49,1,"N"; |C_V #0#49, 1, "N";
     |C_M #0#50,1,"N"; |C_V #0#50, 1, "N";
 |FINSI;

 |CLS;
 |CABEZA aTex2;
 |PINPA #0#0;
 |ATRI R; |PINTA 0527, aTex1; |ATRI 0;
 |PINTA 1438, aTex3;
 |PINTA 1538, aTex3;
 |PINTA 1632, aTex5;
 |ATRI I; |PINTA 2232, aTex4; |ATRI 0;

 |PINDA #0#0;
 |ENDATOS #1#0;

 |PULSATECLA;
|FPRO;

|PROCESO LaRentaSI;
     eaCodR2    = "";
     eaCodRenta = "";
     |SI nOpTitular = 0;
         |SI #900#52 ! 0;
             eaCodRenta = "Codigo de Renta : " + (("00000" + #900#52) % -105);
             |ACABA;
         |FINSI;
     |FINSI;

     |ABRE #103;
     |SELECT #2#103;
     #103#1 = aElDni;
     |LEE 040#103=;
     |SI FSalida = 0;
         eaCodRenta = "Codigo de Renta : " + (("00000" + #103#0) % -105);
         eaCodR2    = (("00000" + #103#0) % -105);
     |SINO;
         |SELECT #3#103;
         #103#35 = aElDni;
         |LEE 040#103=;
         |SI FSalida = 0;
             eaCodRenta = "Codigo de Renta : " + (("00000" + #103#0) % -105);
             eaCodR2    = (("00000" + #103#0) % -105);
         |FINSI;
     |FINSI;
     |CIERRA #103;
|FPRC;
