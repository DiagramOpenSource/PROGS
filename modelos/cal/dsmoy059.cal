|FICHEROS;
    dsmoy059 #0;
    dsmom041 #41;
    dsmom042 #42;
    dsmom046 #46;
    :/basica/def/agicen14 #114;

    dsmom100 #100;
    dsmom107 #107;
    :/contagen/def/caconimp #306;  || configuracion impresiones

    dsmow030 #900;
|FIN;

|VARIABLES;
  swModulo  = 0;
  sw        = 0;
  swPrimero = 0;
  informe   = "dsmoy059";
  &acontab  = 0;
  &vcontab  = 0;
  &afiscal  = 0;
  &vfiscal  = 0;
  &enMod    = 0;

  fecalf      = "";
  nDEmpresa   = 0;
  nHEmpresa   = 0;
  &nDActividad = 0;
  &nHActividad = 0;
  nCampo      = 0;
  &nCamSel    = 23;
  &nCamTip    = 38;
  swBueno     = 0;

  &nAmordef   = 0;
  &nAmorpro   = 0;
  aAlfa1      = "";
  aAlfa2      = "";
  nImpor1     = 0;
  nImpor2     = 0;
  nImpor3     = 0;
  nImpor4     = 0;
  nSwSi       = 0;
  nSwSi1      = 0;
  nSwSi2      = 0;
  nLinea      = 0;

  &nNumMod    = 0;
  &nSwTot     = 0;
  aPath       = "";
  nswmensa    = 0;

  &ele1      = "";
  &ele2      = "";
  &nnum1     = 0;
  &nnum2     = 0;
  &aTAmor    = "";
  &eFAmor    = @;
  aParam     = "";

  nSw        = 0;
  nSwReparto = 0;
  nTp100     = 0;
|FIN;

|INCLUYE i_variar;
|INCLUYE i_varcon;
|INCLUYE i_varlib;
|INCLUYE i_numamo;
|INCLUYE i_selcon;

|| //////////////////// Calculos de Pantalla \\\\\\\\\\\\\\\\\\\\\\\\\\
|CALCULO Tipo7;  |TIPO 7;
     |SI enEmpresa ! 0;
         #0#7 = enEmpresa; |PINTA #0#7;
         #0#8 = enEmpresa; |PINTA #0#8;

         |C_M #0#7, 1, "N";
         |C_M #0#8, 1, "N";
     |FINSI;
|FCAL;

|CALCULO Parrilla;
     |SI #0#7 = 0;
         #0#7 = 0;  |PINTA #0#7;
         #0#8 = 0;  |PINTA #0#8;  |C_M #0#8, 1, "N";
         |PARA nCampo = 9;  |SI nCampo [ 20;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |SINO;
         |SI enEmpresa = 0;
             |C_M #0#7, 1, "S";
             |C_M #0#8, 1, "S";
         |FINSI;

         |PARA nCampo = 9;  |SI nCampo [ 20;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "N";
               #0nCampo = 0;  |PINTA #0nCampo;
         |SIGUE;
     |FINSI;
|FCAL;

|CALCULO NoMasEmpresas;
     |SI #0Campo = 0;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 20;  |HACIENDO nCampo = nCampo + 1;
               #0nCampo = 0;  |C_M #0nCampo, 1, "N";
         |SIGUE;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 20;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |FINSI;
|FCAL;

|CALCULO numamo; |TIPO 0;
    x_alfa1 = #0Campo;
    |HAZ x_punto;
    #0Campo = x_alfa1;
    |PINTA #0Campo;
|FCAL;

|PROCESO DatosEmp; |TIPO 7;

   |DDEFECTO #306;

   |ABRE #100;
   |LEE 040#100p;
   |SI FSalida = 0;
       aAlfa1 = #100#1; |QBF aAlfa1; aAlfa1 = ("00000" + aAlfa1) % -105;
       aAlfa2 = #100#2; |QBF aAlfa2; aAlfa2 = ("0"     + aAlfa2) % -101;
       |DBASS aPath; |QBT aPath;
       aPathConta = aPath + "contagen/fich/" + aAlfa1 + aAlfa2 + "/";

       |PATH_DAT #306 aPathConta;

       |ABRE #306;
       #306#0 = 1;
       |LEE 000#306=;
       |CIERRA #306;
   |FINSI;
   |CIERRA #100;

   #0#43  = #306#7; |PINTA #0#43;
   aAlfa1 = #306#8; |C_M #0#43,1,aAlfa1;

   |SI #107#28 = "S";
       |PINTA 1231, "Activ./Subdepar. ";
   |FINSI;
|FPRC;

|CALCULO porDefecto; |TIPO 7;
 fecalf = ~;
 fecalf = "31.12" + (fecalf % -104);
 #0#3 = fecalf;
|FCAL;

|CALCULO defecano;
  fecalf = #0#3;
  fecalf = fecalf % -104;
  #0#2 = fecalf;
|FCAL;

|CALCULO ElModulo; |TIPO 0;
 |SI #0#39 ! "N";
     |C_M #0#40, 1, "S";
     |C_M #0#41, 1, "S";
     |C_M #0#42, 1, "S";
     #0#40 = 0;   |PINTA #0#40;
     #0#41 = 8;   |PINTA #0#41;
     #0#42 = "S"; |PINTA #0#42;
 |SINO;
     |C_M #0#40, 1, "N";
     |C_M #0#41, 1, "N";
     |C_M #0#42, 1, "N";
     #0#40 = 0;   |PINTA #0#40;
     #0#41 = 0;   |PINTA #0#41;
     #0#42 = "N"; |PINTA #0#42;
 |FINSI;
|FCAL;

|| *******************************************************************
|| /////////////////////////// Procesos \\\\\\\\\\\\\\\\\\\\\\\\\\\\\\
|PROCESO LeeReparto;
 nSwReparto  = 1;  ||existe reparto en diferentes actividades
 |SI #46#1 ] #0#21; |Y  #46#1 [ #0#22;

     nSw = 0;
     enEmpresa   = #41#0;
     enActividad = #46#1;
     eaElemento  = #41#2;
     enElemeNum  = #41#3;
     |HAZ ActModulo;
     |SI #0#39 = "S"; |Y swModulo = 0; nSw = 1; |FINSI;
     |SI #0#39 = "N"; |Y swModulo ! 0; nSw = 1; |FINSI;

     eaCodSubdep = "   ";
     |SI #107#28 = "S"; |Y eaSubdeT = "S";
         eaCodSubdep = #46#6;
         |SI eaCodSubdep < #0#44; |O eaCodSubdep > #0#45;
             nSw = 1;
         |FINSI;
     |FINSI;

     |SI nSw = 0;
         enActividad = #46#1;
         nTp100      = nTp100 + #46#5;
     |FINSI;
 |FINSI;
|FPRC;

|DEFBUCLE LeeReparto;
   #46#1;
   ;
   #41#0, #41#2, #41#3, 01, "   ";
   #41#0, #41#2, #41#3, 99, "zzz";
   e;
   NULL, LeeReparto;
|FIN;

|PROCESO BuscaAny0;
 |SI #dsmom042#9 < #0#3;
     nImpor3 = (nImpor3 - #dsmom042#7)  ! EURODBMOD;
     nImpor4 = (nImpor4 - #dsmom042#11) ! EURODBMOD;
 |FINSI;
 |SI #dsmom042#9 = #0#3;
     nSwSi  = 1;
     nImpor1 = #dsmom042#7;
     nImpor2 = #dsmom042#11;
     nLinea  = #dsmom042#4;
 |FINSI;
|FPRC;

|DEFBUCLE BuscaAny;
   #dsmom042#1;
   6;
   #41#0, #41#2, #41#3, 001, "A";
   #41#0, #41#2, #41#3, 999, "A";
   e;
   NULL, BuscaAny0;
|FIN;

|PROCESO C_Amortiza;
  |SI #0#6 = "A";  |Y #41#24 ! "A";  |ACABA;  |FINSI;
  |SI #0#6 = "P";  |Y #41#24 ! "P";  |ACABA;  |FINSI;

  nImpor3 = #41#10;
  nImpor4 = #41#10;
  nSwSi   = 0;   || ... Busca si ya esta amortizado
  nImpor1 = 0;
  nImpor2 = 0;

  |BUCLE BuscaAny;
  |SI nSwSi = 0; |ACABA; |FINSI; || No tiene amortizacion ese a¤o

  |SI swPrimero = 0;  ||Controla el primer pase
      swPrimero = 1;
      enEmpresa   = #41#0;
      eaNombreEmp = #41#32;

      sw = 0;
      swBueno = 0;
      |HAZ LaSelEmpresa;
      |SI enSwSelEmp = 1;
          swBueno = 1;
      |SINO;
          |SI enDatos = 1;
              enActividad = 0;
              |HAZ DatosEmpr_w30;
          |FINSI;
          |SI #107#28 = "S";  |HAZ eBuscoSubdep; |FINSI;
      |FINSI;
   |FINSI;

   |SI enEmpresa ! #41#0; || Controla la Ruptura por Empresas
      |SI sw = 1;
          |SI #0#42 = "S";
              nSwTot = 1;
              |PRINT;
          |FINSI;
          |PIEINF; || imprime el pie del informe
      |FINSI;
      enEmpresa   = #41#0;
      eaNombreEmp = #41#32;
      sw     = 0;
      nSwTot = 0;

      swBueno = 0;
      |HAZ LaSelEmpresa;
      |SI enSwSelEmp = 1;
          swBueno = 1;
      |SINO;
          |SI enDatos = 1;
              enActividad = 0;
              |HAZ DatosEmpr_w30;
          |FINSI;
          |SI #107#28 = "S";  |HAZ eBuscoSubdep; |FINSI;
      |FINSI;
  |FINSI;
  |SI swBueno = 1; |ACABA; |FINSI;

  eaCodSubdep = "   ";
  nTp100      = 0;
  nSwReparto  = 0;
  |BUCLE LeeReparto;
  |SI nSwReparto = 0;
      nTp100 = 100;  || no existe reparto

      |SI #41#1 < #0#21; |O #41#1 > #0#22;  |ACABA; |FINSI;
      |SI #107#28 = "S"; |Y eaSubdeT = "S";
          eaCodSubdep = #41#54;
          |SI eaCodSubdep < #0#44; |O eaCodSubdep > #0#45;
              |ACABA;
          |FINSI;
      |FINSI;

      enEmpresa   = #41#0;
      enActividad = #41#1;
      eaElemento  = #41#2;
      enElemeNum  = #41#3;
      |HAZ ActModulo;
      |SI #0#39 = "S"; |Y swModulo = 0; |ACABA; |FINSI;
      |SI #0#39 = "N"; |Y swModulo ! 0; |ACABA; |FINSI;
  |FINSI;

  enMod = 0; |SI swModulo ! 0; enMod = 1; |FINSI;

  nAnyAmor    = #0#2;
  fFechaAmor  = #0#3;
  |SI nTp100 ! 100;
      acontab     = (nImpor2 * nTp100 / 100) ! EURODBMOD;
  |SINO;
      acontab     = nImpor2;
  |FINSI;

  |SI sw = 0;
      nNumMod = #41#28;
  |FINSI;

  |SI #0#42 = "S";
      |SI nNumMod ! #41#28;
          nSwTot = 1;
          |PRINT;
          nNumMod = #41#28;
      |FINSI;
  |FINSI;
  sw = 1;
  nSwTot = 0;
  |PRINT;
  nNumMod = #41#28;
|FPRC;

|DEFBUCLE dsmoy059;
   #41#1;
   28,35;
   nDEmpresa,#0#0,#0#36,#0#40,"N";
   nHEmpresa,#0#1,#0#37,#0#41,"N";
   e;
   NULL, C_Amortiza;
|FIN;

|DEFBUCLE dsmoyS59;
   #41#5;
   35;
   nDEmpresa, #0#40, #0#0,#0#36,"N";
   nHEmpresa, #0#41, #0#1,#0#37,"N";
   e;
   NULL, C_Amortiza;
|FIN;

|CALCULO Tipo3;  |TIPO 3;

  enDatos   = 0; |SI #0#43 = "S"; enDatos = 1; |FINSI;

  eaInac    = #0#23;
  eaTituloL = #0#4;
  aAlfa1    = #0#3; aAlfa1 = aAlfa1 % -104;
  enEjer    = aAlfa1;
  aAlfa1 = "01.01." + aAlfa1; efDFecha = aAlfa1;
  efHFecha  = #0#3;

  |SI aParam = "";
      |IMPRE 0;
      |SI FSalida ! 0;  |ACABA;  |FINSI;
  |FINSI;

  |INFOR informe;
  |SI FSalida ! 0;
      |SI aParam = ""; |FINIMP; |FINSI;
      |ACABA;
  |FINSI;

  efFecTope   = #0#3;
  nDActividad = #0#21;
  nHActividad = #0#22;

  |SI #0#7 ! 0;
      nDEmpresa = #0#7;
      nHEmpresa = #0#8;
      |SI #0#42 = "N"; |BUCLE dsmoy059; |FINSI;
      |SI #0#42 = "S"; |BUCLE dsmoyS59; |FINSI;
  |SINO;
      |PARA nCampo = 9; |SI nCampo [ 20;  |HACIENDO nCampo = nCampo + 1;
            |SI #0nCampo ! 0;
                 nDEmpresa = #0nCampo;
                 nHEmpresa = #0nCampo;
                 |SI #0#42 = "N"; |BUCLE dsmoy059; |FINSI;
                 |SI #0#42 = "S"; |BUCLE dsmoyS59; |FINSI;
            |FINSI;
      |SIGUE;
  |FINSI;

  |SI sw = 1;
      |SI #0#42 = "S";
          nSwTot = 1;
          |PRINT;
      |FINSI;
      |PIEINF; || imprime el pie del informe
  |FINSI;
  |FININF;
  |SI aParam = ""; |FINIMP; |FINSI;
|FCAL;


|PROCESO ActModulo;
  swModulo = 0;
  || Lee Actividad
    |ABRE #114;
    #114#0 = enEmpresa;
    #114#1 = enActividad;
    |LEE 041#114=;
    |SI FSalida ! 0;
        |CIERRA #114;
        |ACABA;
    |FINSI;

    swModulo = 0;
    |SI #114#9  = 5; |O #114#9  = 10;
        swModulo = 1;
    |FINSI;
    |SI #114#9 = 6; |O #114#9 = 7; |O #114#9 = 11; |O #114#9 = 12;
        swModulo = 1;   ||Agricolas  (Paco 30.05.2001)
        |SI #114#7 = 9; |Y #114#9 = 7;
            swModulo = 2;
        |FINSI;
    |FINSI;

    |CIERRA #114;
|FPRC;


|PROGRAMA;
   |ABRE #107;
   |DDEFECTO #107;
   |LEE 041#107p;
   |CIERRA #107;

   |SI #107#28 = "S";
       |C_V #0#44, 1, "S"; |C_M #0#44, 1, "S";
       |C_V #0#45, 1, "S"; |C_M #0#45, 1, "S";
   |FINSI;

   aParam = PARAMETRO; |QBF aParam;

  |SI aParam ! "";
      |DDEFECTO #0;
      #0#0 = ele1;
      #0#1 = ele2;
      #0#3 = eFAmor; aAlfa1 = #0#3;
      #0#2 = aAlfa1 % -104;
      #0#6 = aTAmor;
      #0#7 = enEmpresa;
      #0#8 = enEmpresa;
      #0#21 = nDActividad;
      #0#22 = nHActividad;
      #0#23 = eaInac;
      #0#36 = nnum1;
      #0#37 = nnum2;
      #0#43 = "N";
      |HAZ Tipo3;
  |SINO;
      |CLS;
      |MANTE #0;
  |FINSI;
|FPRO;
