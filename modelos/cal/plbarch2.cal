|FICHEROS;
     referen@;

     :/basica/def/agitab99;

     dsarm009@;

     dsarm001@;
     dsarm073@;

     dsarm002@;
     dsarm003@;
     dsarm004@;
|FIN;

|VARIABLES;
     nSwPideEstructura   = 0;
     nContador           = 0;
     nHandle             = 0;
     nSwEstaDsArchi      = 0;

     aIntePara           = "";
     aLinea              = "";
     aBass               = "";
     aMas                = "";
     aMensaje            = "";
     aUnidad             = "";
     aDef                = "";
     aUsuario            = "";
     aPathDatos          = "";
     aOrigen             = "";
     aDestino            = "";
     aIPRemoto           = "";
     aEjecuta            = "";
     aPathTmpUsu         = "";
     aUsuTmp             = "";
     aFicTar             = "";
     aBorra              = "";
     aExtension          = "";
     aSinExtension       = "";
     aNombre             = "";
     aAux                = "";
     aBase               = "";
     aBaseArchi          = "";
     aAlfa               = "";
     aAlfa1              = "";
     aAlfa2              = "";
     aAlfa3              = "";
     aAlfa4              = "";
     aAlfa5              = "";
     aAlfa6              = "";

     rnd                 = 0;
     r_semilla           = 0;
     r_vuelta            = 0;
     r_bucle             = 0;
     r_hora              = "";
     r_beta              = "";
     r_punto             = "";
     r_pi                = 3.141592;
     r_producto          = 0.000000;

     &enNoArchives       = 0;
     &enArchi            = 0;
     &enSwNomina         = 0;
     &enSwNominaEmpCen   = 0;
     &enTipo             = 0;
     &eaSwEnvioCorreo    = "";
     &enDesdeMes         = 0;
     &enHastaMes         = 0;
     &eaVengoDe          = "";
     &enModelo           = 0;
     &enGrupoTres        = 0;
     &enSubGTres         = 0;
     &enTipoTres         = 0;
     &enSwDesde9         = 0;
     &enSwCDefOk         = 0;
     &eaDocumentoArchi   = "";
     &eaNomDocArchi      = "";
     &enSwExisteDoc      = 0;
     &enNumCampoArchi    = 0;
     &eaValorArchi       = "";
     &enNumeroRegistro   = 0;
     &enSwUbicacionDoc   = "";
     &eaNomTemporal      = "";
     &enSwErrorRecDoc    = 0;
     &enMeteGrupoSubTipo = 0;
     &eaInforme          = "";
|FIN;

|PROCESO ActivaPLB_arc1;
     ||NADA
     ||Sirve para que no de error de compilacion, permitiendo que
     ||se carge primero este modulo y no indique que no encuentra los procesos
|FPRC;

|PROCESO ExisteDocFisico;
     aAux = eaDocumentoArchi;
     |SI enSwUbicacionDoc = "S";
          |XABRE "E", aAux, 0;
     |SINO;
          |XABRE "CE", aAux, 0;
     |FINSI;
     |SI FSalida ] 0;
          enSwExisteDoc = 1;
     |SINO;
          enSwExisteDoc = 0;
     |FINSI;
|FPRC;

|PROCESO EstaDsArchi;
     |SI enSwUbicacionDoc = "";
         |IP_REMOTO aIPRemoto;  |QBT aIPRemoto;
         |SI aIPRemoto ! "";
              enSwUbicacionDoc = "S";  || lo guardo en el servidor
         |SINO;
              enSwUbicacionDoc = "C";  || lo guardo en local
         |FINSI;
     |FINSI;

     |DBASS aBass;
     |QBF aBass;
     aMas = aBass + "dsarchi/def/dsarm001.mas";
     |XABRE "E", aMas, 0;
     |SI enSwUbicacionDoc = "S";
          |XABRE "E", aMas, 0;
     |SINO;
          |XABRE "CE", aMas, 0;
     |FINSI;
     |SI FSalida ] 0;
          nSwEstaDsArchi = 1;
     |SINO;
          nSwEstaDsArchi = 0;
     |FINSI;
|FPRC;

|PROCESO DimeUnidad;
     ||OUT   aUnidad

     aDef = aBass + "dsarchi/def/dsarm009.mas";
     |CARGA_DEF aDef, dsarm009@;
     |SI FSalida ! 0;
          |ACABA;
     |FINSI;

     aPathDatos = aBass + "dsarchi/fich/";
     |PATH_DAT #dsarm009@#aPathDatos#;

     |ABRE #dsarm009@;
     aUsuario = Usuario % 810;
     |QBF aUsuario;

     |DDEFECTO #dsarm009@;
     #dsarm009@#0 = aUsuario;
     |LEE 000#dsarm009@.=;
     |SI FSalida ! 0;
          aUnidad = "C";
     |SINO;
          aUnidad = #dsarm009@#1;
          |QBF aUnidad;
          |SI aUnidad = ""; aUnidad = "C"; |FINSI;
     |FINSI;
     |CIERRA #dsarm009@;

     |DESCARGA_DEF #dsarm009@;
|FPRC;

|PROCESO LeeDescripcion;
     |SI #dsarm073@#3 ! 000;

         #dsarm002@#0 = #dsarm073@#3;
         |LEE 000#dsarm002@.=;
         |SI FSalida = 0;
             #dsarm073@#6 = #dsarm002@#1;
         |FINSI;

         |SI #dsarm073@#4 ! 000;
             #dsarm003@#0 = #dsarm073@#3;
             #dsarm003@#1 = #dsarm073@#4;
             |LEE 000#dsarm003@.=;
             |SI FSalida = 0;
                 #dsarm073@#7 = #dsarm003@#2;
             |FINSI;

             |SI #dsarm073@#5 ! 000;
                 #dsarm004@#0 = #dsarm073@#3;
                 #dsarm004@#1 = #dsarm073@#4;
                 #dsarm004@#2 = #dsarm073@#5;
                 |LEE 000#dsarm004@.=;
                 |SI FSalida = 0;
                     #dsarm073@#8 = #dsarm004@#3;
                 |FINSI;
             |FINSI;

         |FINSI;
     |FINSI;
|FPRC;

|PROCESO GrabaModelo;
     aAlfa1 = aAlfa % 103;
     aAlfa2 = aAlfa % 502;
     aAlfa3 = aAlfa % 1240;
     aAlfa4 = aAlfa % 5203;
     aAlfa5 = aAlfa % 5503;
     aAlfa6 = aAlfa % 5803;

     #dsarm073@#0 = aAlfa1;
     #dsarm073@#1 = aAlfa2;
     |LEE 000#dsarm073@.=;
     |SI FSalida ! 0;
         |DDEFECTO #dsarm073@;
         #dsarm073@#0 = aAlfa1;
         #dsarm073@#1 = aAlfa2;
         #dsarm073@#3 = aAlfa4;
         #dsarm073@#4 = aAlfa5;
         #dsarm073@#5 = aAlfa6;
         #dsarm073@#9 = " ";
         |HAZ LeeDescripcion;

         |GRABA 020#dsarm073@.b;
     |FINSI;

     #dsarm073@#2 = aAlfa3;
     |GRABA 020#dsarm073@.a;
     |LIBERA #dsarm073@;
|FPRC;

|PROCESO GrabaFijos;
     |ABRE #agitab99;
     #agitab99#0 = 952;
     |LEE 000#agitab99.=;
     |SI FSalida ! 0;
         |DDEFECTO #agitab99;
         #agitab99#0 = 952;
         #agitab99#1 = "CERTIFICADOS DE IRPF";
         #agitab99#2 = "CERTIFICADOS DE LOS MODELOS 180, 184, 190 Y 193";
         #agitab99#4 = "N";
         #agitab99#5 = "C";
         #agitab99#6 = "M";
         #agitab99#7 = "N";
         #agitab99#8 = "N";
         |GRABA 020#agitab99.n;
         |LIBERA #agitab99;
     |FINSI;
     |CIERRA #agitab99;

     #dsarm073@#0 = 952;
     #dsarm073@#1 = 4;
     |LEE 000#dsarm073@.=;
     |SI FSalida = 0;
         |SI #dsarm073@#1 = "CERTIFICADO MODELO 193                  ";
             |BORRA 020#dsarm073@.a;
             |LIBERA #dsarm073@;
         |FINSI;
     |FINSI;

     aAlfa  = "901/01     LIBRO DE IVA REPERCUTIDO                003004003";  |HAZ GrabaModelo;
     aAlfa  = "902/01     LIBRO DE IVA SOPORTADO                  003004003";  |HAZ GrabaModelo;
     aAlfa  = "910/01     LIBRO DE VENTAS E INGRESOS              003004008";  |HAZ GrabaModelo;
     aAlfa  = "911/01     LIBRO DE COMPRAS Y GASTOS               003004008";  |HAZ GrabaModelo;
     aAlfa  = "952/01     CERTIFICADO MODELO 180                  002002003";  |HAZ GrabaModelo;
     aAlfa  = "952/02     CERTIFICADO MODELO 184                  002002005";  |HAZ GrabaModelo;
     aAlfa  = "952/03     CERTIFICADO MODELO 190 (TRABAJADORES)   002002001";  |HAZ GrabaModelo;
     aAlfa  = "952/04     CERTIFICADO MODELO 190 (ACT.ECONOMICAS) 002002001";  |HAZ GrabaModelo;
     aAlfa  = "952/05     CERTIFICADO MODELO 190 (PREMIOS)        002002001";  |HAZ GrabaModelo;
     aAlfa  = "952/06     CERTIFICADO MODELO 190 (DERECHOS IMAGEN)002002001";  |HAZ GrabaModelo;
     aAlfa  = "952/07     CERTIFICADO MODELO 190 (APRO.FORESTALES)002002001";  |HAZ GrabaModelo;
     aAlfa  = "952/08     CERTIFICADO MODELO 193                  002002004";  |HAZ GrabaModelo;
|FPRC;

|PROCESO CreaDefectos;
     |ABRE #dsarm002@;
     |ABRE #dsarm003@;
     |ABRE #dsarm004@;
     |ABRE #dsarm073@;
     |HAZ GrabaFijos;
     |CIERRA #dsarm073@;
     |CIERRA #dsarm004@;
     |CIERRA #dsarm003@;
     |CIERRA #dsarm002@;
|FPRC;

|PROCESO TipoModelo;
     ||TODO CCC. Adaptar para obtener el TipoModelo

     |SI eaVengoDe = "dsmoy008";  enModelo = 901;  enTipo = 1;  |FINSI;
     |SI eaVengoDe = "dsmoy009";  enModelo = 902;  enTipo = 1;  |FINSI;

     |SI eaVengoDe = "dsmoyi18";  enModelo = 910;  enTipo = 1;  |FINSI;
     |SI eaVengoDe = "dsmoyg18";  enModelo = 911;  enTipo = 1;  |FINSI;

     |SI eaVengoDe = "dsmoy021";  enModelo = 952;  enTipo = 1;  |FINSI;
     |SI eaVengoDe = "dsmoy056";  enModelo = 952;  enTipo = 2;  |FINSI;
     |SI eaVengoDe = "dsmoy027";  enModelo = 952;  enTipo = 8;  |FINSI;

     |SI eaVengoDe = "dsmoy020";
         |SI eaInforme = "dsmor120";  enModelo = 952;  enTipo = 3;  |FINSI;
         |SI eaInforme = "dsmor121";  enModelo = 952;  enTipo = 4;  |FINSI;
         |SI eaInforme = "dsmor122"; |O eaInforme = "dsmor125";
                                      enModelo = 952;  enTipo = 5;  |FINSI;
         |SI eaInforme = "dsmor123";  enModelo = 952;  enTipo = 6;  |FINSI;
         |SI eaInforme = "dsmor124";  enModelo = 952;  enTipo = 7;  |FINSI;
     |FINSI;
|FPRC;

|PROCESO CompruebaModelo;
     |SI eaVengoDe = "dsmoy073";
         enArchi = 0;

         |DBASS aBass;
         aDef = aBass + "dsarchi/def/dsarm001.mas";
         |CARGA_DEF aDef, dsarm001@;
         |SI FSalida ! 0;  |ACABA;  |FINSI;

         |ABRE #dsarm001@;
         |LEE 000#dsarm001@.p;
         |SI FSalida ! 0;  |DDEFECTO #dsarm001@;  |FINSI;
         |CIERRA #dsarm001@;

         |SI #dsarm001@#227 = "S";
             enArchi   = 1;
             enGrupoTres = #dsarm001@#228;
             enSubGTres  = #dsarm001@#229;
             enTipoTres  = #dsarm001@#230;
             |SI #dsarm001@#231 = "S";
                 nSwPideEstructura = 1;
             |SINO;
                 nSwPideEstructura  = 0;
                 enMeteGrupoSubTipo = 1;
             |FINSI;
         |FINSI;

         |DESCARGA_DEF #dsarm001@;

         |ACABA;
     |FINSI;

     |DBASS aBass;
     aDef = aBass + "dsarchi/def/dsarm073.mas";
     |CARGA_DEF aDef, dsarm073@;
     |SI FSalida ! 0;
          |ACABA;
     |FINSI;

     aDef = aBass + "dsarchi/def/dsarm002.mas";
     |CARGA_DEF aDef, dsarm002@;
     |SI FSalida ! 0;
          |DESCARGA_DEF #dsarm073@;
          |ACABA;
     |FINSI;

     aDef = aBass + "dsarchi/def/dsarm003.mas";
     |CARGA_DEF aDef, dsarm003@;
     |SI FSalida ! 0;
          |DESCARGA_DEF #dsarm002@;
          |DESCARGA_DEF #dsarm073@;
          |ACABA;
     |FINSI;

     aDef = aBass + "dsarchi/def/dsarm004.mas";
     |CARGA_DEF aDef, dsarm004@;
     |SI FSalida ! 0;
          |DESCARGA_DEF #dsarm003@;
          |DESCARGA_DEF #dsarm002@;
          |DESCARGA_DEF #dsarm073@;
          |ACABA;
     |FINSI;

     aPathDatos = aBass + "dsarchi/fich/";
     |PATH_DAT #dsarm073@#aPathDatos#;
     |PATH_DAT #dsarm002@#aPathDatos#;
     |PATH_DAT #dsarm003@#aPathDatos#;
     |PATH_DAT #dsarm004@#aPathDatos#;

     |HAZ CreaDefectos;

     enNoArchives = 0;

     |ABRE #dsarm073@;
     |DDEFECTO #dsarm073@;
     #dsarm073@#0 = enModelo;
     #dsarm073@#1 = enTipo;
     |LEE 000#dsarm073@.=;
     |SI FSalida = 0;
         enGrupoTres = #dsarm073@#3;
         enSubGTres  = #dsarm073@#4;
         enTipoTres  = #dsarm073@#5;
         |SI #dsarm073@#9 = "S";
              nSwPideEstructura = 1;
         |SINO;
              nSwPideEstructura  = 0;
              enMeteGrupoSubTipo = 1;
         |FINSI;
     |SINO;
          || SI EL MODELO NO EXISTE, NO SE ARCHIVA NADA
          nSwPideEstructura = 0;
          enNoArchives      = 1;
     |FINSI;

     |CIERRA #dsarm073@;

     |DESCARGA_DEF #dsarm004@;
     |DESCARGA_DEF #dsarm003@;
     |DESCARGA_DEF #dsarm002@;
     |DESCARGA_DEF #dsarm073@;
|FPRC;

|PROCESO FicIntepara;
     |DBASS aBass; |QBF aBass;
     aIntePara = aBass + "dsarchi/intepara/";
     |MKDIR aIntePara;

     aIntePara = aBass + "dsarchi/intepara/" + Usuario + "parametro.txt";
     |FBORRA aIntePara;

     |XABRE "W", aIntePara, nHandle;
     |SI FSalida ] 0;
         aLinea = (("000" + enGrupoTres) % -103) + (("000" + enSubGTres) % -103) + (("000" + enTipoTres) % -103);
         |XGRABA nHandle, aLinea;
     |FINSI;
     |XCIERRA nHandle;
|FPRC;

|PROCESO CtrlPedirGrupoDsArchi;
     nSwEstaDsArchi = 0;
     |HAZ EstaDsArchi;

     nContador = nContador + 1;
     |HAZ TipoModelo;

     enNoArchives = 0;
     |HAZ CompruebaModelo;

     |SI nSwEstaDsArchi = 0;  |ACABA;  |FINSI;
     |SI enNoArchives ! 0;    |ACABA;  |FINSI;

     |SI enModelo = 952;  |O eaVengoDe = "dsmoy073";
         |SI nContador > 1;
             enMeteGrupoSubTipo = 1;

             |ACABA;
         |FINSI;
     |FINSI;

     enGrupoTres = 0;
     enSubGTres = 0;
     enTipoTres = 0;

     |DBASS aBass;
     aDef = aBass + "dsarchi/def/dsarm001.mas";
     |CARGA_DEF aDef, dsarm001@;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     aPathDatos = aBass + "dsarchi/fich/";
     |PATH_DAT #dsarm001@#aPathDatos#;

     nSwPideEstructura = 0;

     |ABRE #dsarm001@;
     |DDEFECTO #dsarm001@;
     |LEE 000#dsarm001@.=;
     |SI #dsarm001@#123 = "S";
         nSwPideEstructura = 1;
     |FINSI;
     |CIERRA #dsarm001@;
     |DESCARGA_DEF #dsarm001@;

     |HAZ TipoModelo;
     |HAZ CompruebaModelo;

     |SI nSwPideEstructura = 1;
         |SI enSwDesde9 = 0;
             aEjecuta = ":dsarchi/dsarz003;DAMETRE";
         |SINO;
             |HAZ FicIntepara;

             aEjecuta = "|:dsarchi/dsarz003;DAMETRE" + Usuario + "parametro.txt";
         |FINSI;

         |SUB_EJECUTA_NP aEjecuta;
         |PULSATECLA;
         |PULSATECLA;
         |PULSATECLA;
         |PULSATECLA;
         |PULSATECLA;

         |SI enSwDesde9 = 1;
             |XABRE "U", aIntePara, nHandle;
             |SI FSalida ] 0;
                 |XLEE nHandle, 250, aLinea;
                 |SI FSalida ] 0;
                     |QBF aLinea;
                     aAux        = aLinea % 103;
                     enGrupoTres = aAux;
                     aAux        = aLinea % 403;
                     enSubGTres  = aAux;
                     aAux        = aLinea % 703;
                     enTipoTres  = aAux;
                 |FINSI;
             |FINSI;
             |XCIERRA nHandle;
             |FBORRA aIntePara;
         |FINSI;

         |SI enGrupoTres ! 0;
             enMeteGrupoSubTipo = 1;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO IniEnvioDocDSArchi;
     enSwCDefOk = 0;

     nSwEstaDsArchi = 0;
     |HAZ EstaDsArchi;
     |SI nSwEstaDsArchi = 0;
          aMensaje = "0000Atenci¢n: Modulo DSARCHI no instalado.";
          |MENAV aMensaje;
          |ACABA;
     |FINSI;

     |DBASS aBass;
     |QBF aBass;

     aUsuTmp = Usuario;
     |QBF aUsuTmp;
     aUsuTmp = "rd" + aUsuTmp;

     |SI enSwUbicacionDoc = "S";
          aBaseArchi = aBass + "dsarchi/";
          aPathTmpUsu = aBaseArchi + "rec_doc/";
          |MKDIR aPathTmpUsu;
     |SINO;
          aUnidad = "C";
          |HAZ DimeUnidad;

          aPathTmpUsu = aUnidad + ":\DOCUMENTOS\";
          |RMKDIR aPathTmpUsu;
          aPathTmpUsu = aPathTmpUsu + "DSARCHI\";
          |RMKDIR aPathTmpUsu;

          aUsuTmp = Usuario;
          |QBF aUsuTmp;
          aUsuTmp = "rd" + aUsuTmp;
          aPathTmpUsu = aPathTmpUsu + aUsuTmp + "\";

          ||Borro todo lo que haya en esta carpeta
          ||pero el RRMDIR, solo borra si la carpeta esta vacia.
          ||necesito un R_PDIR
          aBorra = aPathTmpUsu + "*.*";
          |R_PDIR aBorra;
          |PARA; |SI FSalida = 0; |HACIENDO;
               |RFBORRA aBorra;
               |R_SDIR aBorra;
          |SIGUE;

          |RRMDIR aPathTmpUsu;

          |RMKDIR aPathTmpUsu;
     |FINSI;

     aDef = aBass + "dsarchi/def/dsarm005.mas";
     |CARGA_DEF aDef, referen@;
     |SI FSalida ! 0;
          enSwCDefOk = 0;
          aMensaje = "0000Problemas al cargar el fichero: " + aDef;
          |MENAV aMensaje;
          |ACABA;
     |SINO;
          enSwCDefOk = 1;
     |FINSI;

     aPathDatos = aBass + "dsarchi/fich/";
     |PATH_DAT #referen@#aPathDatos#;
     |NOME_DAT #referen@#aUsuTmp#;

     |ABRE #referen@;
     |CIERRA #referen@;
     |DELINDEX #referen@;

     |ABRE #referen@;
|FPRC;

|PROCESO FinEnvioDocDSArchi;
     nSwEstaDsArchi = 0;
     |HAZ EstaDsArchi;
     |SI nSwEstaDsArchi = 0;
          aMensaje = "0000Atenci¢n: Modulo DSARCHI no instalado.";
          |MENAV aMensaje;
          |ACABA;
     |FINSI;

     |SI enNumeroRegistro ! 0;
          |GRABA 020#referen@.a;
          |LIBERA #referen@;
     |FINSI;

     |CIERRA #referen@;

     |SI enSwCDefOk = 1;
          |DESCARGA_DEF #referen@;
     |FINSI;

     enSwDesde9 = 1;

     |SI enSwUbicacionDoc = "S";
          eaNomTemporal = aUsuTmp;
          aEjecuta = ":dsarchi/dsarz032";
          |SUB_EJECUTA_NP aEjecuta;
          |PULSATECLA;
          |PULSATECLA;
          |PULSATECLA;
          |PULSATECLA;
          |PULSATECLA;

          |SI enSwErrorRecDoc = 1;
               aMensaje = "0000Ha ocurrido un error al archivar los documentos en DSARCHI. Revise los logs [rec_doc.log]";
               |MENAV aMensaje;
               |ACABA;
          |FINSI;
     |SINO;
          |DBASS aBass;
          |QBF aBass;
          aUnidad = "C";
          |HAZ DimeUnidad;

          aPathTmpUsu = aUnidad + ":\DOCUMENTOS\";
          |RMKDIR aPathTmpUsu;
          aPathTmpUsu = aPathTmpUsu + "DSARCHI\";
          |RMKDIR aPathTmpUsu;

          aUsuTmp = Usuario;
          |QBF aUsuTmp;
          aUsuTmp = "rd" + aUsuTmp;
          aPathTmpUsu = aPathTmpUsu + aUsuTmp + "\";

          ||Comprimir
          aFicTar = aPathTmpUsu + aUsuTmp + ".tar" + " 000*.*";
          |RATAR aFicTar, aPathTmpUsu;
          |SI FSalida = 0;
               aFicTar = aPathTmpUsu + aUsuTmp + ".tar";
               |RCOMPRIME aFicTar;
               |SI FSalida = 0;
                    aOrigen = aPathTmpUsu + aUsuTmp + ".tgz";
                    aDestino = aBass + "dsarchi/rec_doc/" + aUsuTmp + ".tgz";

                    aIPRemoto = "";
                    |IP_REMOTO aIPRemoto;  |QBT aIPRemoto;
                    |SI aIPRemoto ! "";
                         |RECIBE_FICHERO aOrigen, aDestino;
                    |SINO;
                         |COPIA_FICHERO aOrigen, aDestino;
                    |FINSI;

                    eaNomTemporal = aUsuTmp;
                    aEjecuta = ":dsarchi/dsarz032";
                    |SUB_EJECUTA_NP aEjecuta;
                    |PULSATECLA;
                    |PULSATECLA;
                    |PULSATECLA;
                    |PULSATECLA;
                    |PULSATECLA;

                    |SI enSwErrorRecDoc = 1;
                         aMensaje = "0000Ha ocurrido un error al archivar los documentos en DSARCHI. Revise los logs [rec_doc.log]";
                         |MENAV aMensaje;
                         |ACABA;
                    |FINSI;
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO DocFisicoDSArchi;
     nSwEstaDsArchi = 0;
     |HAZ EstaDsArchi;
     |SI nSwEstaDsArchi = 0;
          |ACABA;
     |FINSI;

     |DBASS aBass;
     |QBF aBass;

     |SI enNumeroRegistro ! 0;
          |GRABA 020#referen@.a;
          |LIBERA #referen@;
     |FINSI;

     |DDEFECTO #referen@;

     |LEE 000#referen@.u;
     |SI FSalida = 0;
          enNumeroRegistro = #referen@#0 + 1;
     |SINO;
          |HAZ Random;  rnd = rnd $ 999999; rnd = rnd + 1;
          enNumeroRegistro = rnd;
     |FINSI;

     |DDEFECTO #referen@;
     #referen@#0 = enNumeroRegistro;
     |LEE 101#referen@.=;
     |SI FSalida ! 0; |GRABA 020#referen@.b; |FINSI;

     aExtension    = eaNomDocArchi % -104;
     aSinExtension = (("000000000") + enNumeroRegistro) % -109;
     aNombre       = aSinExtension + aExtension;
     #referen@#9   = aNombre;

     aOrigen = eaDocumentoArchi;
     aDestino = aPathTmpUsu + aSinExtension + aExtension;

     |SI enSwUbicacionDoc = "S";
          |COPIA_FICHERO aOrigen, aDestino;
     |SINO;
          |RCOPIA_FICHERO aOrigen, aDestino;
     |FINSI;

     #referen@#93 = eaNomDocArchi;
|FPRC;

|PROCESO MeteCampoDSArchi;
     |QBF eaValorArchi;
     #referen@#enNumCampoArchi# = eaValorArchi;
|FPRC;

||-------------------- Proceso que genera aleatorios (variable 'rnd')-------

|PROCESO Random;
     r_vuelta = r_vuelta + 1;

     |SI r_semilla [ 0; |O r_vuelta > 200;
          |HORA r_hora;

          r_beta    = r_hora % 702;
          r_semilla = r_beta;
          r_vuelta  = 0;
     |FINSI;

     r_producto = r_semilla / r_pi;
     r_beta     = r_producto;

     |PARA r_bucle = 101;|SI r_bucle < 1000;|HACIENDO r_bucle = r_bucle + 100;
          r_punto = r_beta % r_bucle;

         |SI r_punto = "."; |SAL; |FINSI;
     |SIGUE;

     r_bucle   = r_bucle + 104;
     r_beta    = r_beta % r_bucle;
     r_semilla = r_beta;
     rnd       = r_semilla;
|FPRC;
