|FICHEROS;
     dsmoz018 #0;
     dsmom002 #2;
     dsmom005 #5;
     dsmod045 #45;
     dsmow097 #999;

     dsmom090 #90;
     dsmom091 #91;
     dsmom092 #92;

     :/basica/def/agifigen #100;
     :/basica/def/agitab99 #99;
     :/basica/def/agicen14 #214;
|FIN;

|VARIABLES;
     aFichero  = "";
     aAlfa     = "";

     nAnyo     = 0;
     nCampo    = 0;
     nDEmpresa = 0;
     nHEmpresa = 0;
     fFecha    = @;
     fDFecha   = @;
     fHFecha   = @;
     nGraba    = 0;

     &Fecha   = @;

     &enControla  = 0;

     nMaquina = 0;
     nEmpresa = 0;
     nHay     = 0;

     &nPeriodo  = 0;
     &nEjer     = 0;
     &nAnterior = 0;
     &aCampo1   = "";
     &aCampo2   = "";
     &aCampo3   = "";
     &aCampo4   = "";
     &nImp22    = 0;
     &nImp23    = 0;
     &nImp24    = 0;
     &nImp25    = 0;
     &nImp26    = 0;
     &nImp27    = 0;
     &nImp27N   = 0;
     aAlfa1   = "";
     aAlfa2   = "";

     nSw1 = 0;
     nSw2 = 0;
     nSw3 = 0;
     nSw4 = 0;
    nNume1 = 0;
    nMes1 = 0;
    nMes2 = 0;
    &nAnual = 0;
    &nAlMes = 0;
    &nAlMes1 = 0;
    &nAlMes2 = 0;
    &nAlMes3 = 0;
    &nAlMes4 = 0;
    &nTipoAlta = 0;
    &nTipoBaja = 0;
    &nMesAlta  = 0;
    &nMesBaja  = 0;
    &nDTipo    = 0;
    &nHTipo    = 0;
    &enDesde   = 0;
    &enHasta   = 0;
|FIN;

|INCLUYE i_variar;

|| /////////////////////// PROCESOS ENTRADA POR PANTALLA \\\\\\\\\\\\\\\\\\\\

|PROCESO Parrilla;
     |SI #0#1 = 0;
         #0#1 = 0;  |PINTA #0#1;
         #0#2 = 0;  |PINTA #0#2;  |C_M #0#2, 1, "N";
         |PARA nCampo = 3;  |SI nCampo [ 14;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |SINO;
         |SI enEmpresa = 0;
             |C_M #0#1, 1, "S";
             |C_M #0#2, 1, "S";
         |FINSI;

         |PARA nCampo = 3;  |SI nCampo [ 14;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "N";
               #0nCampo = 0;  |PINTA #0nCampo;
         |SIGUE;
     |FINSI;
|FPRC;

|PROCESO NoMasEmpresas;
     |SI #0Campo = 0;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 14;  |HACIENDO nCampo = nCampo + 1;
               #0nCampo = 0;  |C_M #0nCampo, 1, "N";
         |SIGUE;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 14;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |FINSI;
|FPRC;

|PROCESO SacaAnyo;  |TIPO 7;
     fFecha = ~;
     aAlfa  = fFecha % -104;
     #0#0   = aAlfa;
     |PINTA #0#0;
|FPRC;

|| /////////////////////// PROCESOS DE TRABAJO \\\\\\\\\\\\\\\\\\\\
|PROCESO Gr45;

 |SI #91#3 < fDFecha; |O #91#3 > fHFecha; |ACABA; |FINSI;

 |SI #91#4 = 0;
     nAnual = #91#12;
     nAlMes = (nAnual / 4) ! 2;
     aAlfa1 = fFecha;
     nMesAlta = 0;
     |SI fFecha [ fDFecha;
         nAlMes1 = nAlMes; nAlMes2 = nAlMes;
         nAlMes3 = nAlMes; nAlMes4 = nAlMes;
     |SINO;
         aAlfa1 = aAlfa1 % 402; nNume1 = aAlfa1;
         nMesAlta = nNume1;
         |SI nNume1 < 4;
             nAlMes1 = nAlMes; nAlMes2 = nAlMes;
             nAlMes3 = nAlMes; nAlMes4 = nAlMes;
         |FINSI;
         |SI nNume1 ] 4; |Y nNume1 < 7;
             nAlMes1 = 0; nAlMes2 = 2*nAlMes;
             nAlMes3 = nAlMes; nAlMes4 = nAlMes;
         |FINSI;
         |SI nNume1 ] 7; |Y nNume1 < 10;
             nAlMes1 = 0; nAlMes2 = 0;
             nAlMes3 = 3*nAlMes; nAlMes4 = nAlMes;
         |FINSI;
         |SI nNume1 ] 10;
             nAlMes1 = 0; nAlMes2 = 0;
             nAlMes3 = 0; nAlMes4 = nAnual;
         |FINSI;
     |FINSI;
 |FINSI;
 |SI #91#4 = 1; |O #91#4 = 2; |O #91#4 = 4;
     nTipoAlta = #91#4;
 |FINSI;

 |SI #91#4 ] 5; |Y #91#4 [ 8;
     nTipoBaja = #91#4;
     fFecha = #91#3;
     aAlfa1 = fFecha;
     aAlfa1 = aAlfa1 % 402; nNume1 = aAlfa1;
     nMesBaja = nNume1;
     |SI #91#4 = 5; |O #91#4 = 6;
        |SI nNume1 < 4;
            nAlMes1 = 0; nAlMes2 = 0;
            nAlMes3 = 0; nAlMes4 = 0;
        |FINSI;
        |SI nNume1 ] 4; |Y nNume1 < 7;
             nAlMes2 = 0; nAlMes3 = 0; nAlMes4 = 0;
        |FINSI;
       |SI nNume1 ] 7; |Y nNume1 < 10;
           nAlMes3 = 0; nAlMes4 = 0;
       |FINSI;
       |SI nNume1 ] 10; nAlMes4 = 0; |FINSI;
     |FINSI;
 |FINSI;
 |SI #91#4 = 4; |O #91#4 = 8;
     aCampo1 = #91#8;  aCampo2 = #91#9;
     aCampo3 = #91#10; aCampo4 = #91#11;
 |FINSI;
|FPRC;

|PROCESO LoBorro;
 |ABRE #45;
 |DDEFECTO #45;
 #45#0 = #90#0;
 #45#1 = #90#1;
 #45#2 = nEjer;
 #45#3 = nPeriodo;
 #45#4 = 0;
 |LEE 141#45=;
 |SI FSalida = 0;
     |BORRA 020#45a;
 |FINSI;
 |LIBERA #45;
 |CIERRA #45;
|FPRC;

|PROCESO AhoraLosGrabo;
 |ABRE #45;
 |DDEFECTO #45;
 #45#0 = #90#0;
 #45#1 = #90#1;
 #45#2 = nEjer;
 #45#3 = nPeriodo;
 #45#4 = 0;
 |LEE 141#45=;
 |SI FSalida ! 0;
     #45#0 = #90#0;
     #45#1 = #90#1;
     #45#2 = nEjer;
     #45#3 = nPeriodo;
     #45#4 = 0;
     |GRABA 020#45b;
     |HAZ Grabo045;
 |SINO;
     |SI #45#11 ! "S";
         |HAZ Grabo045;
     |FINSI;
 |FINSI;
 |LIBERA #45;
 |CIERRA #45;
|FPRC;

|DEFBUCLE LeoLineas;
 #91#1;
 4;
 #90#0, #90#1, 001, nDTipo;
 #90#0, #90#1, 999, nHTipo;
 ;
 NULL, Gr45;
|FIN;

|PROCESO CrearMod045;

 |SI fFecha > fHFecha; |ACABA; |FINSI;     ||No esta en este Periodo
 |SI #90#17 > "01.01.0000";
     |SI #90#17 < fDFecha; |ACABA; |FINSI;
 |FINSI;

 nMesAlta = 0;
 nMesBaja = 0;
 nAnual  = #90#23;; nAlMes  = (nAnual / 4) ! 2;
 nAlMes1 = nAlMes; nAlMes2 = nAlMes;
 nAlMes3 = nAlMes; nAlMes4 = nAlMes;
 nDTipo = 0; nHTipo = 0; |BUCLE LeoLineas;  || averiguo importe.

 aCampo1 = ""; nImp22 = 0; nImp26 = 0;
 aCampo2 = ""; nImp23 = 0; nImp27 = 0;
 aCampo3 = ""; nImp24 = 0;
 aCampo4 = ""; nImp25 = 0;

 nTipoAlta = 1; nDTipo = 1; nHTipo = 4; |BUCLE LeoLineas;  || la alta
 nTipoBaja = 0; nDTipo = 5; nHTipo = 9; |BUCLE LeoLineas;  || la baja

 nImp22   = nAnual;
 nImp23   = 0;
 |PARA nNume1 = 1; |SI nNume1 [ 4; |HACIENDO nNume1 = nNume1 + 1;
       nPeriodo = nNume1 * 3;
       |SI nNume1 = 1; nImp24 = nAlMes1; nMes1 = 1;  nMes2 = 3; |FINSI;
       |SI nNume1 = 2; nImp24 = nAlMes2; nMes1 = 4;  nMes2 = 6; |FINSI;
       |SI nNume1 = 3; nImp24 = nAlMes3; nMes1 = 7;  nMes2 = 9; |FINSI;
       |SI nNume1 = 4; nImp24 = nAlMes4; nMes1 = 10; nMes2 = 12; |FINSI;
       |SI nTipoBaja = 0;
           nAnterior = nTipoAlta + 18;
           |SI nMesAlta [ nPeriodo; |Y nImp24 ! 0;
               |HAZ AhoraLosGrabo;
           |FINSI;
           nImp23 = nImp23 + nImp24;
       |SINO;
           |SI nMesBaja > nMes1;
               nAnterior = nTipoAlta + 18;
               |SI nMesAlta [ nPeriodo; |Y nImp24 ! 0;
                   |HAZ AhoraLosGrabo;
               |FINSI;
               nImp23 = nImp23 + nImp24;
           |SINO;
               |SI nMesBaja [ nMes2;  |Y nMesBaja ] nMes1;
                   nAnterior = nTipoBaja + 18;
                   |HAZ AhoraLosGrabo;
               |SINO;
                   |HAZ LoBorro;
               |FINSI;
           |FINSI;
       |FINSI;
 |SIGUE;
|FPRC;

|PROCESO LeoMod045;
 nHay = 0;
 |ABRE #45;
 #45#0 = nEmpresa;
 #45#1 = nMaquina;
 #45#2 = #0#0;
 #45#3 = 3;
 #45#4 = 0;
 |LEE 001#45];
 |SI FSalida = 0; |Y #45#0 = nEmpresa; |Y #45#1 = nMaquina; |Y #45#2 = #0#0;
     nHay = 1;
 |FINSI;
 |CIERRA #45;
|FPRC;

|PROCESO GrabaTempo;
    |ABRE #100;
    #100#0 = #90#0;
    |LEE 040#100=;
    |SI FSalida ! 0;  |DDEFECTO #100;  |FINSI;
    |CIERRA #100;

    fFecha = #90#10;
    |SI #90#11 > "01.01.0000";
        |SI #90#11 > fFecha; fFecha = #90#11; |FINSI;
    |FINSI;

    nHay = 0;
    nEmpresa = #90#0;
    nMaquina = #90#1;
    |HAZ LeoMod045;
    |SI nHay = 0;
        |SI fFecha > fHFecha; |ACABA; |FINSI;     ||No esta en este Periodo
        |SI #90#17 > "01.01.0000";
            |SI #90#17 < fDFecha; |ACABA; |FINSI;
        |FINSI;
    |FINSI;

    #999#0 = #90#0;
    #999#1 = #90#1;
    |LEE 101#999=;
    |SI FSalida ! 0;
        |DDEFECTO #999;
        #999#0 = #90#0;
        #999#1 = #90#1;
        #999#2 = #0#0;
        #999#3 = #100#1;
        #999#4 = #90#2;
        #999#5 = "T";
        |GRABA 020#999n;

        nGraba = 1;
    |FINSI;
    |LIBERA #999;

    |HAZ CrearMod045;
|FPRC;

|DEFBUCLE dsmom090;
     #90#1;
     ;
     nDEmpresa, 00001;
     nHEmpresa, 99999;
     ;
     NULL, GrabaTempo;
|FIN;

|RUTINA ClaveBaja999;
     #999#0 = 1;
     #999#1 = 1;
|FRUT;

|RUTINA ClaveAlta999;
     #999#0 = 99999;
     #999#1 = 99999;
|FRUT;

|PROCESO Tipo3;  |TIPO 3;
     aFichero = ("97" + Usuario) % 108;
     |NOME_DAT #999 aFichero;
     |ABRE #999;  |CIERRA #999;  |DELINDEX #999;

     aAlfa1 = #0#0;
     nEjer  = #0#0;
     aAlfa2 = "01.01." + aAlfa1; fDFecha = aAlfa2;
     aAlfa2 = "31.12." + aAlfa1; fHFecha = aAlfa2;

     |ABRE #999;
     |SI #0#1 ! 0;
         nDEmpresa = #0#1;
         nHEmpresa = #0#2;
         |BUCLE dsmom090;
     |SINO;
         |PARA nCampo = 3;  |SI nCampo [ 14;  |HACIENDO nCampo = nCampo + 1;
               nDEmpresa = #0nCampo;
               nHEmpresa = #0nCampo;
               |BUCLE dsmom090;
         |SIGUE;
     |FINSI;
     |CIERRA #999;

     |SI nGraba = 1;
         |ENTLINEAL #1#999, -1, 4, 22, 1, ClaveBaja999, ClaveAlta999;
     |SINO;
         |MENAV "     Seleccion Vacia.";
     |FINSI;

     |DELINDEX #999;
|FPRC;

|PROGRAMA;
     |ABRE #99;
     #99#0 = 045;
     |LEE 040#99=;
     |SI FSalida ! 0;  |DDEFECTO #99;  |FINSI;
     |CIERRA #99;

     |SI #99#4 ! "S";  |O #99#5 ! "C";  |O #99#6 ! "F";
         enDesde = 045;
         enHasta = 045;
         eaExtension = "bas";
         eaPrograma  = "agitab99";
         |SUB_EJECUTA_NP ":basica/dspatron";
         |SUB_EJECUTA_NP "dsmom005";
     |FINSI;

     |SI enEmpresa ! 0;
         #0#0 = enEjer;
         #0#1 = enEmpresa;
         #0#2 = enEmpresa;
         |HAZ Tipo3;
     |SINO;
         |CLS;
         |CABEZA "Entrada Modelo 045";
         |DDEFECTO #0;
         |PINPA #0#0;
         |PINDA #0#0;
         |ENDATOS #1#0;
     |FINSI;

     |HAZ FinalizaEnX;
|FPRO;

|PROCESO Tipo88;  |TIPO 88;
     |HAZ Relaciones;
|FPRC;
