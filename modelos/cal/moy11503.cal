|FICHEROS;
     dsmoy000;
     dsmom002;
     dsmom004;
     dsmom107;

     dsmod115;
     dsmow014;
     t1511501;

     &clientes@;
     &datosper@;
|FIN;

|VARIABLES;
     fFecIni = @;
     fFecFin = @;
     nSw     = 0;

     &enBase1  = 0;
     &enBase2  = 0;
     &enReten1 = 0;
     &enReten2 = 0;
|FIN;

|INCLUYE i_varemi;






|PROCESO Formulario_F2_2017;
|FPRC;

|PROCESO TDeclaracion2017;
     enModelo = 115;  enForzar = 1;  |HAZ CogeDatosBancos;

     |SI #dsmod115#10 > 0;
         enNume = #dsmod115#10;  |HAZ Conver17;  #t1511501#16 = eaAlfa;

                                 #t1511501#6 = "I";
         |SI eaFormaPago = "3";  #t1511501#6 = "U";  |FINSI;
         |SI eaFormaPago = "6";  #t1511501#6 = "G";  |FINSI;

         #t1511501#19 = eaIBAN;
     |SINO;
         enNume = 0;  |HAZ Conver17;  #t1511501#16 = eaAlfa;
         #t1511501#6 = "N";
     |FINSI;
|FPRC;

|PROCESO MeteNombre;
     aAlfa = eaVarDni % 101;
     |SI aAlfa ! "X"; |Y aAlfa ! "Y";  |Y aAlfa ! "Z";
      |Y aAlfa ! "0"; |Y aAlfa ! "1";  |Y aAlfa ! "2";
      |Y aAlfa ! "3"; |Y aAlfa ! "4";  |Y aAlfa ! "5";
      |Y aAlfa ! "6"; |Y aAlfa ! "7";  |Y aAlfa ! "8";
      |Y aAlfa ! "9";
         eaAlfa = #clientes@#1;  |HAZ QuitaRaros;  #t1511501#8 = eaAlfa
                                                   #t1511501#9  = "";
         |ACABA;
     |FINSI;

     eaCadena = #clientes@#1;  |HAZ SeparaNombre;

     eaAlfa = eaCadena1;  |HAZ QuitaRaros;  aAlfa1 = eaAlfa;
     eaAlfa = eaCadena2;  |HAZ QuitaRaros;  aAlfa2 = eaAlfa;

     #t1511501#8 = aAlfa1 + " " + aAlfa2;

     eaAlfa = eaCadena3;  |HAZ QuitaRaros;  #t1511501#9 = eaAlfa;
|FPRC;

|PROCESO Carga115_2017;
     enCodCli = #dsmod115#0;
     |HAZ LeePersona;

     |DDEFECTO #t1511501;

     |SI enSwDeDonde = 1;
         eaVarDni = #clientes@#3;  |HAZ CalculameDNI;  #t1511501#7  = eaVarDni;

         aAlfa1   = #datosper@#2;  |QBT aAlfa1;
         |SI aAlfa1 ! "";
             eaAlfa = #datosper@#2;  |HAZ QuitaRaros;  aAlfa1 = eaAlfa;  |QBF aAlfa1;
             eaAlfa = #datosper@#3;  |HAZ QuitaRaros;  aAlfa2 = eaAlfa;  |QBF aAlfa2;

                                                       #t1511501#8 = aAlfa1 + " " + aAlfa2;
             eaAlfa = #datosper@#4;  |HAZ QuitaRaros;  #t1511501#9 = eaAlfa;
         |SINO;
             |HAZ MeteNombre;
         |FINSI;
     |SINO;
         eaVarDni = #clientes@#13; |HAZ CalculameDNI;  #t1511501#7   = eaVarDni;
         |HAZ MeteNombre;
     |FINSI;

     |HAZ TDeclaracion2017;

     #t1511501#10  = #dsmod115#1;

     |SI aPeriodo = "T";
         |SI #dsmod115#2 = 3;   #t1511501#11 = "1T";  |FINSI;
         |SI #dsmod115#2 = 6;   #t1511501#11 = "2T";  |FINSI;
         |SI #dsmod115#2 = 9;   #t1511501#11 = "3T";  |FINSI;
         |SI #dsmod115#2 = 12;  #t1511501#11 = "4T";  |FINSI;
     |SINO;
         #t1511501#11 = ("00" + #dsmod115#2) % -102;
     |FINSI;

     eaAlfa = #dsmod115#6; eaAlfa = ("00000000000000" + eaAlfa) % -115;
     #t1511501#12 = eaAlfa;

     enNume = #dsmod115#7;  |HAZ Conver17;   #t1511501#13 = eaAlfa;
     enNume = #dsmod115#8;  |HAZ Conver17;   #t1511501#14 = eaAlfa;
     enNume = #dsmod115#9;  |HAZ Conver17;   #t1511501#15 = eaAlfa;

     |SI #dsmod115#4 > 0;
         #t1511501#17 = "X";
         #t1511501#18 = #dsmom004#21;
     |FINSI;

     #t1511501#23 = &13 + &10;

     aAlfa = "<T1150" + #t1511501#10 + #t1511501#11 + "0000>";  |XGRABA nHandle2, aAlfa;
     aAlfa = "<AUX>";                                           |XGRABA nHandle2, aAlfa;
     aAlfa = " " * 70;                                          |XGRABA nHandle2, aAlfa;
     aAlfa = eaVersMod;                                         |XGRABA nHandle2, aAlfa;
     aAlfa = " " * 4;                                           |XGRABA nHandle2, aAlfa;
     aAlfa = eaNIFDs;                                           |XGRABA nHandle2, aAlfa;
     aAlfa = " " * 213;                                         |XGRABA nHandle2, aAlfa;
     aAlfa = "</AUX>";                                          |XGRABA nHandle2, aAlfa;

     |PARA nCampo = 1;  |SI nCampo [ 22;  |HACIENDO nCampo = nCampo + 1;
           aAlfa = #t1511501#nCampo#;
           |XGRABA nHandle2, aAlfa;
     |SIGUE;

     aAlfa = "</T1150" + #t1511501#10 + #t1511501#11 + "0000>";  |XGRABA nHandle2, aAlfa;
|FPRC;

|PROCESO dsmom002_03;
     |SI #dsmom002#8 > "01.01.0000";
         |SI #dsmom002#8 > fFecFin;  |ACABA;  |FINSI;
     |FINSI;

     |SI #dsmom002#9 > "01.01.0000";
         |SI #dsmom002#9 < fFecIni;  |ACABA;  |FINSI;
     |FINSI;

     aAlfa = #dsmom002#7 % 101;
     |SI aAlfa ! #dsmom004#22; |Y #dsmom004#22 ! " ";
         |SI #dsmom004#22 = "T"; #dsmom002#7 = "TRIMESTRAL"; |FINSI;
         |SI #dsmom004#22 = "M"; #dsmom002#7 = "MENSUAL   "; |FINSI;
         |SI #dsmom004#22 = "A"; #dsmom002#7 = "ANUAL     "; |FINSI;
     |FINSI;

     |SI #dsmom002#7 = "TRIMESTRAL";
         aPeriodo = "T";
     |SINO;
         aPeriodo = "M";
     |FINSI;

     |ERROR10;
|FPRC;

|DEFBUCLE dsmom002_03;
     #dsmom002#1;
     ;
     #dsmom004#0, 01, 115;
     #dsmom004#0, 98, 115;
     ;
     NULL, dsmom002_03;
|FIN;

|PROCESO dsmow014_2017;
     |ABRE #dsmom004;
     #dsmom004#0  = #dsmow014#0;
     #dsmom004#1  = #dsmow014#1;
     #dsmom004#2  = #dsmow014#2;
     #dsmom004#3  = #dsmow014#5;
     #dsmom004#4  = #dsmow014#7;
     |LEE 040#dsmom004.=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;
     |CIERRA #dsmom004;

     |ABRE #dsmod115;
     #dsmod115#0 = #dsmom004#0;
     #dsmod115#1 = #dsmom004#1;
     #dsmod115#2 = #dsmom004#2;
     #dsmod115#3 = #dsmom004#3;
     #dsmod115#4 = #dsmom004#4;
     #dsmod115#5 = 0;
     |LEE 040#dsmod115.=;
     |SI FSalida ! 0;  |CIERRA #dsmod115;  |ACABA;  |FINSI;
     |CIERRA #dsmod115;

     aPeriodo = "T";
     |BUCLE dsmom002_03;

     eaFicheroPlano = "115" + (("00000" + #dsmow014#0) % -105) + ".txt";
     eaFicheroImpre = "115" + (("00000" + #dsmow014#0) % -105) + ".pdf";
     eaFicheroError = "err" + (("00000" + #dsmow014#0) % -105) + ".txt";

     aAlfa = eaPathLanza + eaFicheroPlano;  |RFBORRA aAlfa;
     aAlfa = eaPathLanza + eaFicheroImpre;  |RFBORRA aAlfa;
     aAlfa = eaPathLanza + eaFicheroError;  |RFBORRA aAlfa;

     aAbre = eaPathEmision + eaFicheroPlano;
     |XABRE "BW", aAbre, nHandle2;

     |HAZ Carga115_2017;

     |XCIERRA nHandle2;

     enEmpresa    = #dsmow014#0;
     enComple     = #dsmow014#7;
     eaNomEmpresa = #dsmow014#3;

     eaFOrigen  = eaPathEmision  + eaFicheroPlano;

     |SI eaOpcion = "1";  eaFDestino = eaPathPreDecla + eaFicheroPlano;  |FINSI;
     |SI eaOpcion = "3";  eaFDestino = eaPathInternet + eaFicheroPlano;  |FINSI;

     |SI eaOpcion ! "6";
         |HAZ CopiaFichero;
         |HAZ GrabaImpreso;
     |FINSI;

     |SI eaOpcion = "6";
         eaX002_10    = #t1511501#7;
         eaX002_11    = #t1511501#8;
         eaX002_12    = #t1511501#2;
         eaX002_13    = #t1511501#10;
         eaX002_14    = #t1511501#11;

         |SI #dsmod115#4 > 0;
             eaX002_15 = "C";
             eaX002_16 = #t1511501#18;
         |FINSI;

         eaX002_17 = #dsmod115#10;
         eaX002_18 = #t1511501#6;
         eaX002_19 = #t1511501#19;
         eaX002_48 = eaNomEnt;

         |HAZ PresentaPortal;
     |FINSI;
|FPRC;

|DEFBUCLE dsmow014C_2017;
     #dsmow014#1;
     6;
     00000, 0000, 00, "S";
     99999, 9999, 99, "S";
     ;
     NULL, dsmow014_2017;
|FIN;

|DEFBUCLE dsmow014N_2017;
     #dsmow014#2;
     6;
     "                                        ", "S";
     "zzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzz", "S";
     ;
     NULL, dsmow014_2017;
|FIN;

|PROCESO Bloque2017;
     |SI #dsmoy000#6 [ 2018;
         |HAZ BtnPeriodicas2015;
     |FINSI;

     |SI #dsmoy000#6 ] 2019;
         |HAZ BtnPeriodicas2019;
     |FINSI;
     nTecla = enTecla;

     |SI nTecla = 0;  |ACABA;  |FINSI;

     |SI eaOpcion = "1";
         |MENAV "0000Opcion pendiente de activacion por Hacienda";
         |ACABA;
     |FINSI;

     nSwLee = 0;
     |SI #dsmoy000#8 = "C";
         |BUCLE dsmow014C_2017;
     |SINO;
         |BUCLE dsmow014N_2017;
     |FINSI;

     |SI eaOpcion = "1";
         eaAlfa1 = "Los ficheros generados se encuentran en su PC en el directorio:";
         eaAlfa2 = eaPathPreDecla;

         |HAZ PintaUbicacion;

         |HAZ Formulario_F2_2017;
     |FINSI;

     |SI eaOpcion = "3";
         eaAlfa1 = "Los ficheros generados se encuentran en su PC en el directorio:";
         eaAlfa2 = eaPathInternet;

         |HAZ PintaUbicacion;

         |SI #dsmoy000#6 [ 2018;
             |CONFI "0000SQuiere cargar la pagina de hacienda del modelo";
             |SI FSalida = 0;
                 |HAZ FormularioOV17;
             |FINSI;
         |FINSI;
     |FINSI;

     |SI enDirecta = 1;  |Y enMsv = 1;  |Y enParar = 0;
         |SUB_EJECUTA "|dsmox007";
     |FINSI;
|FPRC;

|PROCESO moy11503;
     |ABRE #dsmom107;
     |LEE 040#dsmom107.p;
     |SI FSalida ! 0;
         |CIERRA #dsmom107;
         |SUB_EJECUTA_NP "dsmom107";
         |ABRE #dsmom107;
     |FINSI;

     |LEE 000#dsmom107.p;
     |SI FSalida ! 0;  |DDEFECTO #dsmom107;  |FINSI;
     |CIERRA #dsmom107;

     enSwTrata      = 0;
     enCambiaMoneda = 1;

     aAlfa1 = #dsmoy000#6;
     aAlfa1 = "01.01." + aAlfa1; fFecIni = aAlfa1;
     aAlfa1 = #dsmoy000#6;
     aAlfa1 = "31.12." + aAlfa1; fFecFin = aAlfa1;

     eaPathAEAT = eaUnidadBasico + "\AEAT";        |RMKDIR eaPathAEAT;
     eaPathAEAT = eaPathAEAT + "\TMP";             |RMKDIR eaPathAEAT;
     eaPathAEAT = eaPathAEAT + "\" + #dsmoy000#0;  |RMKDIR eaPathAEAT;
     eaPathAEAT = eaPathAEAT + "\" + #dsmoy000#6;  |RMKDIR eaPathAEAT;
     |SI #dsmoy000#4 ! 99;
         eaPathAEAT = eaPathAEAT + "\" + (("00" + #dsmoy000#4) % -102);  |RMKDIR eaPathAEAT;
     |FINSI;
     eaPathAEAT = eaPathAEAT + "\";

     |HAZ Bloque2017;
|FPRC;
