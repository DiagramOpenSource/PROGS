|FICHEROS;
     dsmoy000;
     dsmom004;
     dsmom107;

     dsmod130;

     dsmow014;
     t1513001;

     &clientes@;
     &datosper@;
|FIN;

|VARIABLES;
     nImporte = 0;
|FIN;

|INCLUYE i_varemi;

|PROCESO PresentaModelo_02;
     |SI eaOpcion = "1";
         eaNIF = #t1513001#7;
         |HAZ PortalPDFPeriodicos;

         |HAZ GrabaImpreso;
     |FINSI;

     |SI eaOpcion = "3";
         eaFOrigen  = eaPathEmision  + eaFicheroPlano;
         eaFDestino = eaPathInternet + eaFicheroPlano;
         |HAZ CopiaFichero;

         |HAZ GrabaImpreso;
     |FINSI;

     |SI eaOpcion = "6";
         eaX002_10    = #t1513001#7;
         eaX002_11    = #t1513001#8;
         eaX002_12    = #t1513001#2;
         eaX002_13    = #t1513001#10;
         eaX002_14    = #t1513001#11;

         |SI #dsmod130#4 > 0;
             eaX002_15 = "C";
             eaX002_16 = #t1513001#32;
         |FINSI;

         eaX002_17 = #dsmod130#23;
         eaX002_18 = #t1513001#6;
         eaX002_19 = #t1513001#33;
         eaX002_48 = eaNomEnt;

         |HAZ PresentaPortal;
     |FINSI;
|FPRC;

|PROCESO AbrePagina;
     eaPathAEAT = eaUnidadBasico + "\AEAT";        |RMKDIR eaPathAEAT;
     eaPathAEAT = eaPathAEAT + "\" + #dsmoy000#0;  |RMKDIR eaPathAEAT;
     eaPathAEAT = eaPathAEAT + "\" + #dsmoy000#6;  |RMKDIR eaPathAEAT;
     |SI #dsmoy000#4 ! 99;
         eaPathAEAT = eaPathAEAT + "\" + (("00" + #dsmoy000#4) % -102);  |RMKDIR eaPathAEAT;
     |FINSI;
     eaPathAEAT = eaPathAEAT + "\";

     eaFOrigen  = eaPathEmision  + eaFicheroPlano;
     eaFDestino = eaPathAEAT + eaFicheroPlano;
     |HAZ CopiaFichero;

     aAlfa       = eaPathLanza;
     eaPathLanza = eaPathAEAT;
     |HAZ TraeSoloWait;
     eaPathLanza = aAlfa;

     eaForzExplorador = "N";
     |HAZ Explorador;

     aAbre = eaPathAEAT + "imp" + Usuario + ".bat";
     |XABRE "WBC", aAbre, enHandlePortal;

     eaAlfa = eaPathAEAT + "wait ";
     |HAZ GrabaPortalSin;

     eaAlfa = eaPathExplorador + " " + &34;
     |HAZ GrabaPortalSin;

     eaAlfa = "https://www1.agenciatributaria.gob.es/es13/h/ie51150a.html";
     |HAZ GrabaPortalSin;

     |XCIERRA enHandlePortal;

     |RSYSTEM aAbre;

     aAlfa = eaPathExplorador > "";
     aAlfa = aAlfa - "CHROME.EXE";
     |SI FSalida ! 0;  |SLEEP 30;  |FINSI;

     aAlfa = eaPathAEAT + aFichero;
     |RFBORRA aAlfa;
     |RFBORRA aAbre;
|FPRC;

|PROCESO TDeclaracion2015;
     enModelo = 130;  enForzar = 1;  |HAZ CogeDatosBancos;

     |SI #dsmod130#23 > 0;
                                 #t1513001#6 = "I";
         |SI eaFormaPago = "3";  #t1513001#6 = "U";  |FINSI;
         |SI eaFormaPago = "6";  #t1513001#6 = "G";  |FINSI;

         #t1513001#33 = eaIBAN;
     |SINO;
         #t1513001#6 = "N";
         |SI #dsmod130#23 ! 0;
             |SI #dsmod130#2 ! 12;  #t1513001#6 = "B";  |FINSI;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO PersonaFis2015;
     aAlfa = eaVarDni % 101;
     |SI aAlfa = "0"; |O aAlfa = "1"; |O aAlfa = "2"; |O aAlfa = "3";
      |O aAlfa = "4"; |O aAlfa = "5"; |O aAlfa = "6"; |O aAlfa = "7";
      |O aAlfa = "8"; |O aAlfa = "9"; |O aAlfa = "X"; |O aAlfa = "Y";
      |O aAlfa = "Z";
         eaAlfa = #clientes@#1;     |HAZ BuscaNombre;
         eaAlfa = eaAlfa1;    |HAZ QuitaRaros;     #t1513001#8   = eaAlfa;
         eaAlfa = eaAlfa2;    |HAZ QuitaRaros;     #t1513001#9   = eaAlfa;
      |SINO;
         eaAlfa   = #clientes@#1;    |HAZ QuitaRaros;    #t1513001#8   = eaAlfa;
                                                         #t1513001#9   = " ";
      |FINSI;
|FPRC;

|PROCESO Carga130_2015;
     enCodCli = #dsmod130#0;
     |HAZ LeePersona;

     |DDEFECTO #t1513001;

     |SI enSwDeDonde = 1;
         eaVarDni = #clientes@#3;  |HAZ CalculameDNI;   #t1513001#7  = eaVarDni;

         aAlfa1   = #datosper@#2;  |QBT aAlfa1;
         |SI aAlfa1 ! "";
             eaAlfa = #datosper@#2;  |HAZ QuitaRaros;  aAlfa1 = eaAlfa;  |QBF aAlfa1;
             eaAlfa = #datosper@#3;  |HAZ QuitaRaros;  aAlfa2 = eaAlfa;  |QBF aAlfa2;
             #t1513001#8 = aAlfa1 + " " + aAlfa2;

             eaAlfa = #datosper@#4;  |HAZ QuitaRaros;
             #t1513001#9 = eaAlfa;
         |SINO;
             |HAZ PersonaFis2015;
         |FINSI;
     |SINO;
         eaVarDni = #clientes@#13;  |HAZ CalculameDNI;   #t1513001#7   = eaVarDni;
         |HAZ PersonaFis2015;
     |FINSI;

     #t1513001#10  = #dsmod130#1;

     |SI #dsmod130#2 = 3;   #t1513001#11 = "1T";  |FINSI;
     |SI #dsmod130#2 = 6;   #t1513001#11 = "2T";  |FINSI;
     |SI #dsmod130#2 = 9;   #t1513001#11 = "3T";  |FINSI;
     |SI #dsmod130#2 = 12;  #t1513001#11 = "4T";  |FINSI;

     #dsmod130#12 = #dsmod130#9 - #dsmod130#10 - #dsmod130#11;
     #dsmod130#16 = 0;
     |SI #dsmod130#14 > 0;
         #dsmod130#16 = #dsmod130#14 - #dsmod130#15;
     |FINSI;

     enNume = #dsmod130#6;   |HAZ Conver17;  #t1513001#12 = eaAlfa;
     enNume = #dsmod130#7;   |HAZ Conver17;  #t1513001#13 = eaAlfa;
     enNume = #dsmod130#8;   |HAZ Conver17;  #t1513001#14 = eaAlfa;
     enNume = #dsmod130#9;   |HAZ Conver17;  #t1513001#15 = eaAlfa;
     enNume = #dsmod130#10;  |HAZ Conver17;  #t1513001#16 = eaAlfa;
     enNume = #dsmod130#11;  |HAZ Conver17;  #t1513001#17 = eaAlfa;
     enNume = #dsmod130#12;  |HAZ Conver17;  #t1513001#18 = eaAlfa;
     enNume = #dsmod130#13;  |HAZ Conver17;  #t1513001#19 = eaAlfa;
     enNume = #dsmod130#14;  |HAZ Conver17;  #t1513001#20 = eaAlfa;
     enNume = #dsmod130#15;  |HAZ Conver17;  #t1513001#21 = eaAlfa;
     enNume = #dsmod130#16;  |HAZ Conver17;  #t1513001#22 = eaAlfa;
     enNume = #dsmod130#17;  |HAZ Conver17;  #t1513001#23 = eaAlfa;

     enNume = #dsmod130#19;  |HAZ Conver17;  #t1513001#24 = eaAlfa;
     enNume = #dsmod130#20;  |HAZ Conver17;  #t1513001#25 = eaAlfa;
     enNume = #dsmod130#21;  |HAZ Conver17;  #t1513001#26 = eaAlfa;
     enNume = #dsmod130#24;  |HAZ Conver17;  #t1513001#27 = eaAlfa;
     enNume = #dsmod130#22;  |HAZ Conver17;  #t1513001#28 = eaAlfa;
     enNume = #dsmod130#18;  |HAZ Conver17;  #t1513001#29 = eaAlfa;
     enNume = #dsmod130#23;  |HAZ Conver17;  #t1513001#30 = eaAlfa;

     |HAZ TDeclaracion2015;

     |SI #dsmod130#4 > 0;
         #t1513001#31 = "X";
         #t1513001#32 = #dsmom004#21;
     |FINSI;

     #t1513001#37 = &13 + &10;

     aAlfa = "<T1300" + #t1513001#10 + #t1513001#11 + "0000>";  |XGRABA nHandle2, aAlfa;
     aAlfa = "<AUX>";                                           |XGRABA nHandle2, aAlfa;
     aAlfa = " " * 70;                                          |XGRABA nHandle2, aAlfa;
     aAlfa = eaVersMod;                                         |XGRABA nHandle2, aAlfa;
     aAlfa = " " * 4;                                           |XGRABA nHandle2, aAlfa;
     aAlfa = eaNIFDs;                                           |XGRABA nHandle2, aAlfa;
     aAlfa = " " * 213;                                         |XGRABA nHandle2, aAlfa;
     aAlfa = "</AUX>";                                          |XGRABA nHandle2, aAlfa;

     |PARA nCampo = 1;  |SI nCampo [ 36;  |HACIENDO nCampo = nCampo + 1;
           aAlfa = #t1513001#nCampo#;
           |XGRABA nHandle2, aAlfa;
     |SIGUE;

     aAlfa = "</T1300" + #t1513001#10 + #t1513001#11 + "0000>";  |XGRABA nHandle2, aAlfa;
|FPRC;

|PROCESO dsmow014_2015;
     |ABRE #dsmom004;
     #dsmom004#0  = #dsmow014#0;
     #dsmom004#1  = #dsmow014#1;
     #dsmom004#2  = #dsmow014#2;
     #dsmom004#3  = #dsmow014#5;
     #dsmom004#4  = #dsmow014#7;
     |LEE 040#dsmom004.=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;
     |CIERRA #dsmom004;

     |ABRE #dsmod130;
     #dsmod130#0 = #dsmom004#0;
     #dsmod130#1 = #dsmom004#1;
     #dsmod130#2 = #dsmom004#2;
     #dsmod130#3 = #dsmom004#3;
     #dsmod130#4 = #dsmom004#4;
     #dsmod130#5 = 0;
     |LEE 040#dsmod130.=;
     |SI FSalida ! 0;  |CIERRA #dsmod130;  |ACABA;  |FINSI;
     |CIERRA #dsmod130;

     eaFicheroPlano = "130" + (("00000" + #dsmow014#0) % -105) + ".txt";
     eaFicheroImpre = "130" + (("00000" + #dsmow014#0) % -105) + ".pdf";
     eaFicheroError = "err" + (("00000" + #dsmow014#0) % -105) + ".txt";

     aAlfa = eaPathLanza + eaFicheroPlano;  |RFBORRA aAlfa;
     aAlfa = eaPathLanza + eaFicheroImpre;  |RFBORRA aAlfa;
     aAlfa = eaPathLanza + eaFicheroError;  |RFBORRA aAlfa;

     aAbre = eaPathEmision + eaFicheroPlano;
     |XABRE "BW", aAbre, nHandle2;

     |HAZ Carga130_2015;

     |XCIERRA nHandle2;

     enEmpresa    = #dsmow014#0;
     enComple     = #dsmow014#7;
     eaNomEmpresa = #dsmow014#3;

     |HAZ PresentaModelo_02;
|FPRC;

|DEFBUCLE dsmow014C_2015;
     #dsmow014#1;
     6;
     00000, 0000, 00, "S";
     99999, 9999, 99, "S";
     ;
     NULL, dsmow014_2015;
|FIN;

|DEFBUCLE dsmow014N_2015;
     #dsmow014#2;
     6;
     "                                        ", "S";
     "zzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzz", "S";
     ;
     NULL, dsmow014_2015;
|FIN;

|PROCESO Bloque2015;
     |SI #dsmoy000#6 [ 2018;
         |HAZ BtnPeriodicas2015;
     |FINSI;

     |SI #dsmoy000#6 ] 2019;
         |HAZ BtnPeriodicas2019;
     |FINSI;
     nTecla = enTecla;

     |SI nTecla = 0;  |ACABA;  |FINSI;

     |SI eaOpcion = "1";
         |MENAV "0000La opcion de validacion no esta activada por hacienda.";
         |ACABA;
     |FINSI;

     nSwLee = 0;
     |SI #dsmoy000#8 = "C";
         |BUCLE dsmow014C_2015;
     |SINO;
         |BUCLE dsmow014N_2015;
     |FINSI;

     |SI eaOpcion = "1";
         |HAZ SacaErrorPtl;
     |FINSI;

     |SI eaOpcion = "3";
         aAlfa =  "      Los ficheros generados estan: " + eaPathInternet + " de su PC";
         |MENAV aAlfa;

         |SI #dsmoy000#6 [ 2018;
             |CONFI "0000SQuiere cargar la pagina de hacienda del modelo";
             |SI FSalida = 0;
                 |HAZ AbrePagina;
             |FINSI;
         |FINSI;
     |FINSI;

     |SI enDirecta = 1;  |Y enMsv = 1;  |Y enParar = 0;
         |SUB_EJECUTA "|dsmox007";
     |FINSI;
|FPRC;

|PROCESO moy13002;
     |ABRE #dsmom107;
     |LEE 040#dsmom107.p;
     |SI FSalida ! 0;
         |CIERRA #dsmom107;
         |SUB_EJECUTA_NP "dsmom107";
         |ABRE #dsmom107;
     |FINSI;

     |LEE 040#dsmom107.p;
     |SI FSalida ! 0;  |DDEFECTO #dsmom107;  |FINSI;
     |CIERRA #dsmom107;

     enCambiaMoneda = 1;
     enSwTrata      = 0;

     |HAZ Bloque2015;
|FPRC;
