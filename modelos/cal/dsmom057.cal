|FICHEROS;
   dsmom057 #57;
   dsmom058 #58,MANTE;

   dsmow053 #53,MANTE;

   :/contasoc/def/soplanca #201;

   :/contagen/def/caw00098 #901,MANTE;

   referen@ #1000;
|FIN;

|VARIABLES;
  &nBoton2 = 0;
  &nBoton3 = 0;
  &nBoton4 = 0;
  &nBoton5 = 0;

  &swUno  = 0;
  &enAnyO = 0;
  &enAnyD = 0;

  aTexto = "";

  aDTipo = "";
  aHTipo = "";
  aTipo = "";
  enEjercicio = 0;
  aF2     = "";
  aEsc    = "";
  &nTecla  = 0;
  &eaRelCta = "";
  nEstado = 0;
  nLnEstado = 0;

   aAlfa1  = "";
   nCuenta = 0;
   nNume1  = 0;
   nNume2  = 0;
   nNume3  = 0;
   nNume4  = 0;
   nModoEntrada = 0;
   nUltCod = 0;

   nCasilla   = 0;
   aMas       = "";
   &eaTabMod  = "";
   aQueQuiero = "";
   aTitulo    = "";
   aNumCampos = "";
   nNumCampos = 0;
   nCampos    = 0;
   nCampo     = 0;
   aNombCampo = "";
   aTipoCampo = "";
   aLongCampo = "";
   aDirInicio = "";
    aFichero  = "";
    nContador = 0;
   nNoClave   = 0;
    aDirBase  = "";
  aPath       = "";
  aPathImpuesto = "";
  nInkey      = 0;

  fFecha     = @;
|FIN;

|INCLUYE i_variar;
|INCLUYE i_varcon;

|| ... ... ... ... ...
|RUTINA ClauA201;
 #201#0 = 01;
|FRUT;
|RUTINA ClauB201;
 #201#0 = 99;
|FRUT;

|CALCULO LaPlanilla; |TIPO 0;
 nInkey = FSalida;
 |PINTA 1029, "                                          ";

 |ABRE #201;
 |SI nInkey = 10;
     |CONSULTA_F_CLAVE #1#201, ClauA201, ClauB201;
     #57#9 = #201#0;
 |FINSI;
 #201#0 = #57#9;
 |LEE 001#201=;
 |SI FSalida ! 0;
     |MENAV "    Planilla Inexistente ...";
     |ERROR;
 |SINO;
     |PINTA 1030, #201#1;
 |FINSI;
 |CIERRA #201;
|FCAL;

|CALCULO MTip71; |TIPO 7;
 |PINTA 1029, "                                          ";
 nEstado = (FEntrada / 100) ! 0;
 |SI nEstado > 1;
     |DDEFECTO #201;
     |ABRE #201;
     #201#0 = #57#9;
     |LEE 001#201=;
     |PINTA 1030, #201#1;
     |CIERRA #201;
 |FINSI;
|FCAL;

|PROCESO WTipo11;  |TIPO 11;
 nTecla = FSalida;
 |HAZ MTipo0;
 |SI nTecla = 14; |HAZ Copiar; |FINSI;
|FPRC;

|PROCESO MTipo7; |TIPO 7;
   |CONTROL_SIMPLE 0, "LBOTON,F2 Activo y Pasivo", 1108, 1123, Boton2;
   nBoton2 = FSalida;
   |CONTROL_SIMPLE 0, "LBOTON,F3 Debe y Haber", 1126, 1141, Boton3;
   nBoton3 = FSalida;
   |CONTROL_SIMPLE 0, "LBOTON,F4 Liquidacion 200/201", 1144, 1159, Boton4;
   nBoton4 = FSalida;
   |CONTROL_SIMPLE 0, "LBOTON,F5 Inf. Adicional", 1162, 1177, Boton5;
   nBoton5 = FSalida;

   || |CONTROL_SIMPLE 0, "LBOTON,F5 Liquidacion 225", 1162, 1177, Boton5;
   ||nBoton5 = FSalida;
|FPRC;

|RUTINA ClaveBaja58;
 #58#0 = #57#0;
 #58#1 = aTipo;
 #58#2 = 001;
|FRUT;

|RUTINA ClaveAlta58;
 #58#0 = #57#0;
 #58#1 = aTipo;
 #58#2 = 999;
|FRUT;

|PROCESO MTipo0;  |TIPO 0;
 enEjercicio = #57#0;

 |FIN_CONTROL_WINDOWS nBoton2;
 |FIN_CONTROL_WINDOWS nBoton3;
 |FIN_CONTROL_WINDOWS nBoton4;
 |FIN_CONTROL_WINDOWS nBoton5;
 |PULSATECLA;

 aTipo = "";
 |SI nTecla = 10;  aTipo = "1";  |FINSI;
 |SI nTecla = 11;  aTipo = "2";  |FINSI;
 |SI nTecla = 12;  aTipo = "3";  |FINSI;
 |SI nTecla = 13;  aTipo = "5";  |FINSI;
|| |SI nTecla = 13;  aTipo = "4";  |FINSI; ||ya no

 |SI aTipo ! "";
     |SI aTipo = "1"; aTexto = "Activo y Pasivo     "; |FINSI;
     |SI aTipo = "2"; aTexto = "Debe y Haber        "; |FINSI;
     |SI aTipo = "3"; aTexto = "Liquidacion 200/201 "; |FINSI;
     |SI aTipo = "4"; aTexto = "Liquidacion 225     "; |FINSI;
     |SI aTipo = "5"; aTexto = "Inf. Adicional      "; |FINSI;

     |ATRI P; |PINTA 1249, aTexto; |ATRI 0;
     |PINPA #0#58;
     |SI aTipo ! "3"; |Y aTipo ! "4";
         |C_M #58#8,1,"N";  |C_V #58#8,1,"N";
         |C_M #58#9,1,"N";  |C_V #58#9,1,"N";
         |C_M #58#10,1,"N"; |C_V #58#10,1,"N";
         |C_M #58#11,1,"S"; |C_V #58#11,1,"S";
         |ATRI R; |PINTA 1363, "Relacion Cuentas"; |ATRI 0;
     |SINO;
         |C_M #58#8,1,"S";  |C_V #58#8,1,"S";
         |C_M #58#9,1,"S";  |C_V #58#9,1,"S";
         |C_M #58#10,1,"S"; |C_V #58#10,1,"S";
         |C_M #58#11,1,"N"; |C_V #58#11,1,"N";
         |ATRI R; |PINTA 1363, "Segundo Campo   "; |ATRI 0;
     |FINSI;

     |ENTLINEAL #1#58, -3, 2, 22, 0, ClaveBaja58, ClaveAlta58;
 |FINSI;

|FPRC;

|PROCESO Boton2;
  |PTEC 824;
|FPRC;
|PROCESO Boton3;
  |PTEC 825;
|FPRC;
|PROCESO Boton4;
  |PTEC 826;
|FPRC;
|PROCESO Boton5;
  |PTEC 827;
|FPRC;

|PROCESO Quepasa;
 nTecla = FSalida;
 |HAZ MTipo0;
|FPRC;

|PROCESO BorraLineas0;
 |BORRA 020#58a;
 |LIBERA #58;
|FPRC;

|DEFBUCLE BorraLineas;
 #58#1;
 ;
 #57#0, aDTipo, 001;
 #57#0, aHTipo, 999;
 be;
 NULL, BorraLineas0;
|FIN;

|PROCESO MTipo1; |TIPO 1;
 nEstado = (FEntrada / 100) ! 0;
 |SI nEstado = 3;
     |CONFI "    NEsta seguro de borrar el Control ";
     |SI FSalida = 0;
         aDTipo = " ";
         aHTipo = "z";
         |BUCLE BorraLineas;
     |SINO;
         |ERROR;
     |FINSI;
 |FINSI;
|FPRC;

|| **********************************************************************

|PROCESO elTLinea; |TIPO 0;

     |SI #58#3 = "S"; |O #58#3 = "R";
         |SI #58#3 = "S"; #58#4 = "--------------- Salto Pagina"; |FINSI;
         |SI #58#3 = "R"; #58#4 = "--------------- Raya"; |FINSI;
         |PINTA #58#4;
         #58#5 = 0; |PINTA #58#5;
         #58#6 = 0; |PINTA #58#6;
         #58#7 = ""; |PINTA #58#7;
         |SI aTipo = "3";
             #58#8 = 0; |PINTA #58#8;
             #58#9 = 0; |PINTA #58#9;
             #58#10 = ""; |PINTA #58#10;
         |SINO;
             #58#11 = "N"; |PINTA #58#11;
         |FINSI;
         |PTEC 806;
     |FINSI;

     |SI #58#3 = "D";
         |C_M #58#5,1,"N"; #58#5 = 0; |PINTA #58#5;
         |C_M #58#6,1,"N"; #58#6 = 0; |PINTA #58#6;
         |C_M #58#7,1,"S";
         |SI aTipo = "3";
             |C_M #58#8,1,"N"; #58#8 = 0; |PINTA #58#8;
             |C_M #58#9,1,"N"; #58#9 = 0; |PINTA #58#9;
             |C_M #58#10,1,"S";
         |FINSI;
     |SINO;
         |C_M #58#5,1,"S";
         |C_M #58#6,1,"S";
         |C_M #58#7,1,"N";
         |SI aTipo = "3";
             |C_M #58#8,1,"S";
             |C_M #58#9,1,"S";
             |C_M #58#10,1,"N";
         |FINSI;
    |FINSI;
|FPRC;

|PROCESO LeeCodTab;
 nUltCod = 0;
 |ABRE #901;
 #901#0 = enModelo;
 #901#1 = nCasilla;
 |LEE 001#901=;
 |SI FSalida ! 0;
     nUltCod = 1;
 |FINSI;
 |CIERRA #901;
|FPRC;

|CALCULO TrasImpuesto; |TIPO 0;
 nTecla = FSalida;
 nCampo = Campo;
 |C_M #58Campo,0,aAlfa1;
 |SI aAlfa1 = "N"; |ACABA; |FINSI;

 nCasilla = #58nCampo;

 |SI aTipo = "1"; |O aTipo = "2"; enModelo = 201; |FINSI;
 |SI aTipo = "3"; enModelo = 200; |FINSI;
 |SI aTipo = "4"; enModelo = 225; |FINSI;
 |SI aTipo = "5"; enModelo = 900; |FINSI;

 |SI nTecla = 10;
     |HAZ TablaImpuesto;
 |FINSI;
 |SI nCasilla = 0;
     nNume1 = nCampo + 1; #58nNume1 = 0;  |PINTA #58nNume1;
     nNume1 = nCampo + 2; #58nNume1 = ""; |PINTA #58nNume1;
     |ACABA;
 |FINSI;

 |HAZ LeeCodTab;
 |SI nUltCod = 1;
     |ERROR;
 |SINO;
     #58nCampo = nCasilla; |PINTA #58nCampo;

     nNume1 = nCampo + 2;
     #58nNume1 = #901#2; |PINTA #58nNume1;

     nNume1 = nCampo + 1;
     aAlfa1 = #901#2; aAlfa1 = aAlfa1 - "Casilla";
     #58nNume1 = aAlfa1; |PINTA #58nNume1;
 |FINSI;
|FCAL;

|CALCULO LaRelCta; |TIPO 7;
 |C_M #58#11,0, aAlfa1;
 |SI aAlfa1 = "N"; |ACABA; |FINSI;
 |SI #58#3 ! "N";
     #58#11 = "N";
     |PINTA #58#11;
     |PTEC 802;
 |FINSI;
|FCAL;

|CALCULO LaClave7; |TIPO 7;
 #58#0 = #57#0;
 #58#1 = aTipo;

 nModoEntrada = (FEntrada / 100) ! 0;
 |SI nModoEntrada = 1;|Y FSalida ! 1;|Y FSalida ! 7;|Y #58#2 ! 1;
     #58#2 = #58#2 + 4;
     |PINTA #58#2;
 |FINSI;
|FCAL;

|CALCULO LaClave3; |TIPO 3;
 #58#0 = #57#0;
 #58#1 = aTipo;
|FCAL;

|CALCULO Linea0; |TIPO 0;
 |SI aTipo ! "3"; |Y aTipo ! "4";
     |C_M #58#8,1,"N"; |C_V #58#8,1,"N";
     |C_M #58#9,1,"N"; |C_V #58#9,1,"N";
     |C_M #58#10,1,"N"; |C_V #58#10,1,"N";
     |C_M #58#11,1,"S"; |C_V #58#11,1,"S";
 |SINO;
     |C_M #58#8,1,"S"; |C_V #58#8,1,"S";
     |C_M #58#9,1,"S"; |C_V #58#9,1,"S";
     |C_M #58#10,1,"S"; |C_V #58#10,1,"S";
     |C_M #58#11,1,"N"; |C_V #58#11,1,"N";
 |FINSI;
|FCAL;

|| ********************************************************************
|RUTINA ClauBaja57;
 |SI FSalida = "POSICION";
     #57#0 = nNume2;
 |SINO;
     #57#0 = 1990;
 |FINSI;
|FRUT;

|RUTINA ClauAlta57;
 #57#0 = 2007;
|FRUT;

|PROGRAMA;

 |DBASS aPath; |QBT aPath;
 aPathImpuesto = aPath + "contasoc/fich/";
 |PATH_DAT #201 aPathImpuesto;

 |HAZ CargaTablaAux;

 nNume2 = 2007;

 |CLS;
 |CABEZA "Control Compr. Impuesto Sociedades";
 |ENTLINEAL #1#57, -1, 2, 22, 1, ClauBaja57, ClauAlta57;
 |DELINDEX #901;
|FPRO;

|| ********************************************************************
|| ///// Carga de Auxiliar del fichero del Impuesto
|PROCESO CargaTemporal;
     aMas  = aDirInicio + "contasoc/def/" + aFichero;
     |CARGA_DEF aMas, referen@;
     |SI FSalida ! 0;
         |MENAV  "         Error en cargar el Def ... Proceso Abortado.";
         |ACABA;
     |FINSI;

     aQueQuiero = "|TITULO";
     aTitulo    = #referen@#aQueQuiero#;  |QBF aTitulo;

     nNoClave  = 2;
     #901#0    = nContador;
     #901#4    = aFichero;
     #901#5    = "contasoc";
     #901#6    = aTitulo;

     aQueQuiero = "|NC";
     aNumCampos = #referen@#aQueQuiero#;
     nNumCampos = aNumCampos;
     nNumCampos = nNumCampos - 1;
     |PARA nCampos = nNoClave;  |SI nCampos [ nNumCampos;  |HACIENDO nCampos = nCampos + 1;
           aQueQuiero = "|C" + (("000" + nCampos) % -103) + "NOMBRE";
           aNombCampo = #referen@#aQueQuiero#;

           aQueQuiero = "|C" + (("000" + nCampos) % -103) + "TIPO";
           aTipoCampo = #referen@#aQueQuiero#;

           |SI aTipoCampo = "N";
               aQueQuiero = "|C" + (("000" + nCampos) % -103) + "LONGITUD";
               aLongCampo = #referen@#aQueQuiero#;

               #901#1 = nCampos;
               #901#2 = aNombCampo;
               #901#3 = aLongCampo;
               |GRABA 020#901n;
               |LIBERA #901;
           |FINSI;
     |SIGUE;

     |DESCARGA_DEF #referen@;
|FPRC;

|PROCESO CargaTablaAux;
     eaTabMod = ("zm" + Usuario + "zzzzzzzzz") % 108;
     |NOME_DAT #901 eaTabMod;
     |ABRE #901;  |CIERRA #901;  |DELINDEX #901;

     |DBASS aDirInicio;
     |DBASE aDirBase;

     |ABRE #901;
     aFichero = "isocie59";  nContador = 200; |HAZ CargaTemporal;
     aFichero = "isocie65";  nContador = 201; |HAZ CargaTemporal;
     aFichero = "isocie91";  nContador = 225; |HAZ CargaTemporal;
     aFichero = "isocie67";  nContador = 900; |HAZ CargaTemporal;
     |CIERRA #901;
|FPRC;
|| -------------------------------------------------------------------

|RUTINA ClInf901;
 #901#0 = enModelo;
 #901#1 = 1;
 #901#2 = "                    ";
|FRUT;

|RUTINA ClSup901;
 #901#0 = enModelo;
 #901#1 = 999;
 #901#2 = "zzzzzzzzzzzzzzzzzzzz";
|FRUT;

|PROCESO TablaImpuesto;
 |PUSHV 0701 2157;
 nCasilla = #58nCampo;
 |PINPA #0#901;
 #901#0 = enModelo; |PINTA #901#0;
 |ENTLINEAL #1#901, -2, 4, 20, 0, ClInf901, ClSup901;
 |POPV;
|FPRC;

|CALCULO wTip11; |TIPO 11;
 nTecla = FSalida;
 |SI nTecla = 1; |O nTecla = 0;
     nCasilla = #901#1;
 |FINSI;
|FCAL;

|CALCULO wTip5; |TIPO 0;
 nTecla = FSalida;
 |SI nTecla = 0;
     |PTEC 806;
     |PTEC 808;
 |FINSI;
|FCAL;

|| ----------------------------------------------------------------
|CALCULO QueCopio; |TIPO 0;
 |SI #53#2 = "S";
     |C_M #53#3,1,"N"; #53#3 = "S"; |PINTA #53#3;
     |C_M #53#4,1,"N"; #53#4 = "S"; |PINTA #53#4;
     |C_M #53#5,1,"N"; #53#5 = "S"; |PINTA #53#5;
     |C_M #53#6,1,"N"; #53#5 = "S"; |PINTA #53#6;
 |SINO;
     |C_M #53#3,1,"S";
     |C_M #53#4,1,"S";
     |C_M #53#5,1,"S";
     |C_M #53#6,1,"S";
 |FINSI;
|FCAL;

|CALCULO noigual; |TIPO 0;
 |SI #53#1 = #53#0;  |AVISO; |ERROR;  |FINSI;
|FCAL;

|| ---------------------
|PROCESO CopiarLin;
     |SALVA_FICHA #58;
     #58#0 = #53#1;
     |GRABA 020#58n;
     |REPON_FICHA #58;
     |LIBERA #58;
|FPRC;

|DEFBUCLE CopiaLineas;
     #58#1;
     ;
     #53#0, aDTipo, 001;
     #53#0, aHTipo, 999;
     be;
     NULL, CopiarLin;
|FIN;

|PROCESO Copiar; ||*********** Proceso para duplicar control
 |ERROR;

 |PUSHV 0712 1738;
 |PINPA #0#53;
 #53#1 = #57#0;
 #53#0 = #53#1 - 1;
 |PINDA #0#53;
 |ENDATOS #1#53;
 |SI FSalida ! 0;
     |POPV;
     |ACABA;
 |FINSI;

 |SI #53#0 = #53#1;
     |MENAV "    Error El mismo A¤o ";
     |POPV;
     |ACABA;
 |FINSI;

 #57#0  = #53#0;
 |LEE 001#57=;
 |SI FSalida ! 0;
     |MENAV "    Copia cancelada. Tabla A¤o Origen inexistente ...";
     |POPV;
     |ACABA;
 |FINSI;
 |SALVA_DATOS #57;

 #57#0 = #53#1;
 |LEE 001#57=;
 |SI FSalida = 0;
     |AVISO;
     |CONFI "    NRegrabar Tabla del A¤o Destino YA EXISTENTE (S/N): ";
     |SI FSalida ! 0;
         |MENAV "    Copia cancelada ...";
         |POPV;
         |REPON_DATOS #57;    ||para que no se queda el salvadatos suelto
         |ACABA;
     |SINO;
         |BORRA 020#57a;
         |SI #53#2 = "S";
             aDTipo = " ";  aHTipo = "z";
             |BUCLE BorraLineas;
         |SINO;
             |SI #53#3 = "S"; aDTipo = "1"; aHTipo = "1"; |BUCLE BorraLineas; |FINSI;
             |SI #53#4 = "S"; aDTipo = "2"; aHTipo = "2"; |BUCLE BorraLineas; |FINSI;
             |SI #53#5 = "S"; aDTipo = "3"; aHTipo = "3"; |BUCLE BorraLineas; |FINSI;
             |SI #53#6 = "S"; aDTipo = "5"; aHTipo = "5"; |BUCLE BorraLineas; |FINSI;
         |FINSI;
     |FINSI;
 |FINSI;

 |REPON_DATOS #57;
 #57#0  = #53#1;
 |GRABA 020#57n;

 |SI #53#2 = "S";
     aDTipo = " ";  aHTipo = "z";
     |BUCLE CopiaLineas;
 |SINO;
     |SI #53#3 = "S"; aDTipo = "1"; aHTipo = "1"; |BUCLE CopiaLineas; |FINSI;
     |SI #53#4 = "S"; aDTipo = "2"; aHTipo = "2"; |BUCLE CopiaLineas; |FINSI;
     |SI #53#5 = "S"; aDTipo = "3"; aHTipo = "3"; |BUCLE CopiaLineas; |FINSI;
     |SI #53#6 = "S"; aDTipo = "5"; aHTipo = "5"; |BUCLE CopiaLineas; |FINSI;
 |FINSI;
 |POPV;

 |PTEC 809;
 |PTEC 808;
|FPRC;
