|FICHEROS;
     dsmozp42 #0;
     dsmow112 #41;
     dsmom042 #42;
     dsmom049 #49;

     dsmow041 #999,MANTE;

    :/basica/def/agicen14 #1014;
    :/basica/def/agifigen #1000;
|FIN;

|VARIABLES;
     aFichero  = "";

     nCampo    = 0;
     nDEmpresa = 0;
     nHEmpresa = 0;
     enGraba   = 0;
     nSwExiste = 0;
     &nLaPrimera  = 0;
     nInkey    = 0;

     &errconta = 0;
     &esalta = 0;
     alfa1 = "";

     &eaDElem  = "";
     &eaHElem  = "";
     &enDNEle  = 0;
     &enHNEle  = 0;

     &nCamSel = 24;
     &nCamTip = 37;

     nDActividad = 0;
     nHActividad = 0;

     aAlfa1      = "";
     nEstado     = 0;
     nPara1      = 0;

    &aComVen = "";
    &enSwBien = 0;
    &aCamp1 = "";
    &nCamp2 = 0;
    &fCamp3 = @;
    &nCamp4 = 0;
    &nCamp5 = 0;
    &aCamp6 = "";
    &nCamp7 = 0;
    &aCamp8 = "";
    &nCamp9 = 0;
    &nCamp41_43 = 0;
    &nCamp41_46 = 0;
    &aCamp41_47 = "";
    &aCamp41_48 = "";
    &aCamp41_49 = "";
    &aCamp41_53 = "";
    &aCamp41_50 = "";
    &nCamp41_51 = 0;
    &nCamp41_52 = 0;
|FIN;


|INCLUYE i_variar;
|INCLUYE i_varcon;
|INCLUYE i_selcon;
|INCLUYE i_numamo;

|| **********************************************************************
|CALCULO Tipo7_7;  |TIPO 7;
     |SI enEmpresa ! 0;
         #0#8 = enEmpresa; |PINTA #0#8;
         #0#9 = enEmpresa; |PINTA #0#9;

         |C_M #0#8, 1, "N";
         |C_M #0#9, 1, "N";
     |FINSI;
|FCAL;

|CALCULO Parrilla;
     |SI #0#8 = 0;
         #0#8 = 0;  |PINTA #0#8;
         #0#9 = 0;  |PINTA #0#9;  |C_M #0#9, 1, "N";
         |PARA nCampo = 10;  |SI nCampo [ 21;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |SINO;
         |SI enEmpresa = 0;
             |C_M #0#8, 1, "S";
             |C_M #0#9, 1, "S";
         |FINSI;

         |PARA nCampo = 10;  |SI nCampo [ 21;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "N";
               #0nCampo = 0;  |PINTA #0nCampo;
         |SIGUE;
     |FINSI;
|FCAL;

|CALCULO NoMasEmpresas;
     |SI #0Campo = 0;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 21;  |HACIENDO nCampo = nCampo + 1;
               #0nCampo = 0;  |C_M #0nCampo, 1, "N";
         |SIGUE;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 21;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |FINSI;
|FCAL;

|CALCULO numamo; |TIPO 0;
    x_alfa1 = #0Campo;
    |HAZ x_punto;
    #0Campo = x_alfa1;
    |PINTA #0Campo;
|FCAL;

|CALCULO ElPtec; |TIPO 7;
    |PTEC 816;
|FCAL;

|CALCULO Calcular;
 aAlfa1 = "S";
 |SI #0#5 = "N"; |Y #0#6 = "N";
     #0#0 = "        "; |PINTA #0#0;
     #0#1 = "zzzzzzzz"; |PINTA #0#1;
     #0#3 = 00;         |PINTA #0#3;
     #0#4 = 99;         |PINTA #0#4;
     #0#22 = 00;        |PINTA #0#22;
     #0#23 = 99;        |PINTA #0#23;
     aAlfa1 = "N";
 |FINSI;
 |C_M #0#0, 1, aAlfa1;
 |C_M #0#1, 1, aAlfa1;
 |C_M #0#2, 1, aAlfa1;
 |C_M #0#3, 1, aAlfa1;
 |C_M #0#4, 1, aAlfa1;
 |C_M #0#22, 1, aAlfa1;
 |C_M #0#23, 1, aAlfa1;
|FCAL;

|| ............................................................
|| **********************************************************************
|RUTINA ClaveBaja41;
     #41#0 = #999#0;
     #41#2 = "        ";
     #41#3 = 00;
|FRUT;

|RUTINA ClaveAlta41;
     #41#0 = #999#0;
     #41#2 = "zzzzzzzz";
     #41#3 = 99;
|FRUT;

|CALCULO LeelaEmpresa; |TIPO 0;
   errconta = 0;
   enEmpresa = #999#0;
   enTpEm = 0;

  || Lee Empresa
    |ABRE #1000;
    #1000#0 = enEmpresa;
    |LEE 041#1000=;
    |SI FSalida ! 0;
        |MENAV "     Empresa Inexistente";
        errconta = 1;
        |CIERRA #1000;
        |ERROR;
        |ACABA;
    |SINO;
         enTpEm = #1000#87;
         |SI #1000#23 = "N";
            || .. errconta = 1;
            || .. |MENAV "     Empresa Sin Contabilidad";
        |FINSI;
    |FINSI;
    #dsmow041#1 = #1000#1; |PINTA #dsmow041#1;
    |CIERRA #1000;

    |LEE 010#dsmow041.=;
    |SI FSalida ! 0;
        #999#0 = enEmpresa;
        #999#1 = #1000#1;
        |GRABA 000#dsmow041.b;
        |ERROR;
        |PTEC 824;
    |FINSI;
|FCAL;

|PROCESO Tipo1;
     esalta = (FEntrada / 100) ! 0;
|FPRC;

|PROCESO Tipo7;  |TIPO 7;
     |MENSA "    <F2> Entrar Elementos"
     |PINTA  #dsmow041#0; |PINTA  #dsmow041#1;
     enModo = 7;
     enEmpresa   = #999#0;
     enPerConta  = -1;
     enEjer      = 0;
     |ENTLINEAL #1#41, -3, 7, 22, 0, ClaveBaja41, ClaveAlta41;
|FPRC;

|PROCESO Tipo11;  |TIPO 11;
    nInkey = FSalida;
    |SI nInkey =  10;
          errconta = 0;
          enModo = 2;
          enEmpresa   = #999#0;
          enPerConta  = -1;
          |ENTLINEAL #1#41, -3, 4, 22, 0, ClaveBaja41, ClaveAlta41;
    |FINSI;

    |SI nInkey = 12;
        enEmpresa = #999#0;
        eaDElem   = "        ";
        eaHElem   = "zzzzzzzz";
        enDNEle   = 00;
        enHNEle   = 99;
        |SUB_EJECUTA_NP "dsmoy041";
        |ERROR;
    |FINSI;
|FPRC;

|| **********************************************************************
|| /////////////////////// PROCESOS DE TRABAJO \\\\\\\\\\\\\\\\\\\\
|PROCESO LeoParaCompra_42;
 #49#7 = #42#12;
 #49#8 = #42#10;
|FPRC;

|DEFBUCLE LeoParaCompra_42;
 #42#1;
 6;
 #49#0, #49#2, #49#3, 001, "C";
 #49#0, #49#2, #49#3, 999, "C";
 e;
 NULL, LeoParaCompra_42;
|FIN;

|PROCESO Act49;
    |ABRE #49;
    |DDEFECTO #49;
    #49#0 = #41#0;
    #49#2 = #41#2;
    #49#3 = #41#3;
    #49#4 = 1;
    |LEE 101#49=;
    nEstado = FSalida;
    #49#0  = #41#0;
    #49#1  = #41#1;
    #49#2  = #41#2;
    #49#3  = #41#3;
    #49#4  = 1;
    #49#5  = #0#2;
    #49#6  = #41#27;
    |SI nEstado ! 0;
        #49#7 = 1;
        #49#8 = "Compra del Elemento";
        |BUCLE LeoParaCompra_42;
    |FINSI;
    #49#10 = 0;
    #49#11 = "C";
    |SI nEstado = 0; |GRABA 020#49a; |FINSI;
    |SI nEstado ! 0; |GRABA 020#49b; |FINSI;
    |LIBERA #49;
    |CIERRA #49;
|FPRC;

|PROCESO CompletaDatos;
  |SI #42#6 = "C";
       #41#43 = (nCamp41_43) % #41#42;
       #41#46 = nCamp41_46;
       #41#47 = aCamp41_47;
       #41#48 = aCamp41_48;
       #41#49 = aCamp41_49;
       #41#53 = aCamp41_53;
  |SINO;
       #41#50 = aCamp41_50;
       #41#51 = nCamp41_51;
       #41#52 = nCamp41_52;
  |FINSI;
|FPRC;

|PROCESO BuscoDatos_l;
      aComVen = #42#6; ||Compra o venta
      enEmpresa   = #41#0;
      enActividad = #41#1;
      enEjer      = #42#5;
      eaElemento  = #41#2;
      enElemeNum  = #41#3;
      aCamp1      = #41#4;
      nCamp2      = #42#4;  || linea
      fCamp3      = #42#9;
      nCamp4      = #42#7;
      nCamp5      = #42#12;
      aCamp6      = #42#10;
      nCamp7      = #42#14;
      aCamp8      = #42#15;
      nCamp9      = #42#16;
      enSwBien = 0;
      |SUB_EJECUTA_NP "dsmoz038;SinPantalla";
      |SI enSwBien = 1;
          #42#14 = nCamp7;
          #42#15 = aCamp8;
          #42#16 = nCamp9;
          |SI #41#41 = "S";
              |HAZ CompletaDatos;
          |FINSI;
      |FINSI;
|FPRC;

|DEFBUCLE BuscoDatos_l;
 #42#1;
 ;
 #41#0, #41#2, #41#3, 001;
 #41#0, #41#2, #41#3, 999;
 e;
 NULL, BuscoDatos_l;
|FIN;


|PROCESO BuscoDatos;
  #41#43 = 0;
  |BUCLE BuscoDatos_l;
|FPRC;

|PROCESO GrabaCampos;
  |SI #41#1 < nDActividad; |O #41#1 > nHActividad;  |ACABA; |FINSI;

  |SI #0#5 = "S"; #41#41 = "S"; |FINSI;

  |SI #41#41 = "S";
      |SI #0#6 = "S";     ||Actualizamos campos
          |HAZ BuscoDatos;
      |FINSI;
      |HAZ Act49;
  |FINSI;
  |GRABA 020#41a;
  |LIBERA #41;
|FPRC;

|DEFBUCLE dsmom041;
     #41#1;
     ;
     enEmpresa, #0#0, #0#2;
     enEmpresa, #0#1, #0#3;
     be;
     NULL, GrabaCampos;
|FIN;

|PROCESO GrabaTempo;
     #999#0 = #41#0;
     |LEE 101#999=;
     |SI FSalida ! 0;
         #999#0 = #41#0;
         #999#1 = #41#32;
         |GRABA 020#999n;
         enGraba = 1;
     |FINSI;
     nSwExiste = 1;

     |LIBERA #999;
|FPRC;

|PROCESO GrabaTempo1;
  enEmpresa = #agifigen#0;
  eaEstadoEmp = "S";
  |SI #agifigen#94 > "01.01.0000";
      eaEstadoEmp = "N";
      |SI #0#24 = "N"; |ACABA; |FINSI;
  |FINSI;

|| ... Seleccion de Tipo de Empresa
   nSPin1 = #agifigen#87;
   enSwSelEmp = 1;
   |SI #0#37 = "S";
       |SI nSPin1 ] #0#25; |Y nSPin1 [ #0#26;
           enSwSelEmp = 0;
       |FINSI;
   |SINO;
       |SI nSPin1 = #0#27;
           enSwSelEmp = 0;
       |SINO;
           |PARA nCampo = 28;  |SI nCampo [ 36;  |HACIENDO nCampo = nCampo + 1;
                  |SI nSPin1 = #0nCampo; |Y nSPin1 ! 0;
                       enSwSelEmp = 0;
                       |SAL;
                  |FINSI;
            |SIGUE;
       |FINSI;
    |FINSI;
    |SI enSwSelEmp = 1; |ACABA; |FINSI;

    |ABRE #41;
    |DDEFECTO #41;
    #41#0 = #1000#0;
    #41#2 = "        ";
    #41#3 = 00;
    |LEE 041#41];
    |SI FSalida = 0; |Y #41#0= #1000#0;
        |HAZ GrabaTempo;
    |FINSI;
    |CIERRA #41;
    |SI #0#5 = "S"; |O #0#6 = "S";
        |BUCLE dsmom041;
    |FINSI;
|FPRC;

|DEFBUCLE dsmom041_f;
     #1000#1;
     ;
     nDEmpresa;
     nHEmpresa;
     ;
     NULL, GrabaTempo1;
|FIN;

|| ***********************************************************
|RUTINA ClaveBaja999;
  |SI FSalida = "POSICION";
      #999#0 = #0#1;
  |SINO;
      #999#0 = 1;
  |FINSI;
|FRUT;

|RUTINA ClaveAlta999;
     #999#0 = 99999;
|FRUT;

|PROCESO Tipo3;  |TIPO 3;

     nDActividad = #0#22;
     nHActividad = #0#23;

     aFichero = ("40" + Usuario) % 108;
     |NOME_DAT #999 aFichero;
     |ABRE #999;  |CIERRA #999;  |DELINDEX #999;

     nSwExiste = 0;
     |ABRE #999;
     |SI #0#8 ! 0;
         nDEmpresa = #0#8;
         nHEmpresa = #0#9;
         |BUCLE dsmom041_f;
     |SINO;
         |PARA nPara1 = 10;;  |SI nPara1 [ 21;  |HACIENDO nPara1 = nPara1 + 1;
               |SI #0nPara1 ! 0;
                   nDEmpresa = #0nPara1;
                   nHEmpresa = #0nPara1;
                   |BUCLE dsmom041_f;
               |FINSI;
         |SIGUE;
      |FINSI;

      |SI nSwExiste = 0;
          |MENAV "    Seleccion vacia";
          |CIERRA #999;
          |DELINDEX #999;
          |ACABA;
     |FINSI;
     |CIERRA #999;

     |PINPA #0#41;

     |ENTLINEAL #1#999, -1, 4, 5, 0, ClaveBaja999, ClaveAlta999;
     |DELINDEX #999;
|FPRC;

|PROCESO Tipo88;  |TIPO 88;
     |HAZ Relaciones;
|FPRC;
