|FICHEROS;
   dsmoy092 #0;

   dsmom016 #16;
   dsmom017 #17;
   dsmom018 #18;

   dsmoc130 #130;

   :/basica/def/agifigen #118;
   :/basica/def/agicen14 #114;
|FIN;

|VARIABLES;

   informe    = "dsmoy092";
   aFichero   = "";
   nCampo     = 0;
   nDEmpresa  = 0;
   nHEmpresa  = 0;
   nDActividad = 0;
   nHActividad = 0;

   aAlfa  = "";
   aAlfa1 = "";
   aAlfa2 = "";
   aAlfa3 = "";
   nPara1 = 0;
   nPara2 = 0;
   nNume1 = 0;
   nNume2 = 0;
   nNume3 = 0;
   nNume4 = 0;

   nModelo    = 0;
   nEjer      = 0;
   nError     = 0;
   sw         = 0;
   fLaFecha   = @;
   aParam     = "";
   &enError15 = 0;
   aTex       = "";
   &enSwLinea = 0;
|FIN;

|INCLUYE i_variar;

|| //////////////////// Calculos de Pantalla \\\\\\\\\\\\\\\\\\\\\\\\\\
|CALCULO Parrilla; |TIPO 0;
     |SI #0#2 = 0;
         #0#2 = 0;  |PINTA #0#2;
         #0#3 = 0;  |PINTA #0#3;  |C_M #0#3, 1, "N";
         |PARA nCampo = 4;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |SINO;
         |SI enEmpresa = 0;
             |C_M #0#2, 1, "S";
             |C_M #0#3, 1, "S";
         |FINSI;

         |PARA nCampo = 4;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "N";
               #0nCampo = 0;  |PINTA #0nCampo;
         |SIGUE;
     |FINSI;
|FCAL;

|CALCULO NoMasEmpresas; |TIPO 0;
     |SI #0Campo = 0;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
               #0nCampo = 0;  |C_M #0nCampo, 1, "N";
         |SIGUE;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |FINSI;
|FCAL;

|| **************************************************************************
|PROCESO HayError;
        enError15 = 1;

        |SI aParam = "EMPRESA";
            |ACABA;
        |FINSI;

        |SI aParam = "MASIVO";
            |SI sw = 0;
                aAlfa = "0000Hay " + aTex + " con Imp.en Reduc.Empleo incompatible ejer. 2015";
                |MENAV aAlfa;
                |CONFI "0000SObtener Un informe de las Empresas con error";
                |SI FSalida ! 0;
                    |ERROR10;
                    |ACABA;
                |FINSI;
            |FINSI;
        |FINSI;
        |SI sw = 0;
            |IMPRE 0;
            |SI FSalida ! 0; |MENAV "    Error en Impresora"; |ACABA; |FINSI;

            |INFOR informe;
            |SI FSalida ! 0;
                |MENAV "    Error en Informe. ";
                |FINIMP;
                |ERROR10;
                |ACABA;
            |FINSI;
            sw = 1;
        |FINSI;

        |PRINT;
|FPRC;

|PROCESO CargaEpigrafe;
     nError = 0;
     |SELECT #1#16;
     #16#0  = "E";
     #16#1  = eaCodEpi2;
     #16#41 = enCodEpi3;
     #16#2  = nEjer;
     |LEE 040#16=;
     |SI FSalida ! 0; nError = 1; |FINSI;
|FPRC;

|PROCESO Imprime;

 |SALVA_FICHA #17;
 #17#2 = #17#2 + 1;
 |LEE 000#17=;
 |SI FSalida = 0;
     |REPON_DATOS #17;
     |ACABA;
 |FINSI;
 |REPON_FICHA #17;

 #118#0 = #17#0;
 |LEE 041#118=;
 |SI FSalida ! 0; |ACABA; |FINSI;

|| ------------------------------------------------------------------------

    eaCodEpi2   = #17#5;
    enCodEpi3   = #17#6;
    |HAZ CargaEpigrafe;
    |SI nError = 1; |ACABA; |FINSI; ||no existe Epigrafe.

    #18#0 = #17#0;
    #18#1 = #17#1;
    #18#2 = #17#2;
    #18#3 = #17#3;
    |LEE 041#18=;
    |SI FSalida ! 0;
        |ACABA;
    |FINSI;

    |SI #17#29 ! 0; |O #17#30 ! 0;
        enSwLinea = 2;
        aTex = "Modulos";
        |HAZ HayError;
    |FINSI;
|FPRC;

|DEFBUCLE Leem017;
  #17#1;
  ;
  nDEmpresa, #0#1, 00, 01;
  nHEmpresa, #0#1, 99, 99;
  ;
  NULL, Imprime;
|FIN;

|PROCESO Imprime130;

 |SALVA_FICHA #130;
 #130#4 = #130#4 + 1;
 |LEE 000#130=;
 |SI FSalida = 0;
     |REPON_DATOS #130;
     |ACABA;
 |FINSI;
 |REPON_FICHA #130;

 #118#0 = #130#0;
 |LEE 041#118=;
 |SI FSalida ! 0; |ACABA; |FINSI;

 #0#26 = #118#1;

 #114#0 = #130#0;
 #114#1 = #130#5;
 |LEE 041#114=;
 |SI FSalida ! 0; |ACABA; |FINSI;

 #0#27 = #114#4;
 #0#28 = #114#2;
 #0#29 = #114#3;
 |SI #130#2 = 3;  #0#30 = "MARZO";      |FINSI;
 |SI #130#2 = 6;  #0#30 = "JUNIO";      |FINSI;
 |SI #130#2 = 9;  #0#30 = "SEPTIEMBRE"; |FINSI;
 |SI #130#2 = 12; #0#30 = "DICIEMBRE";  |FINSI;

 |SI #130#49 ! 0;
     enSwLinea = 1;
     aTex      = "Modelo 130";
     |HAZ HayError;
 |FINSI;
|FPRC;

|DEFBUCLE Leec130;
  #130#1;
  ;
  nDEmpresa, #0#1, 01, nModelo, 00, 01;
  nHEmpresa, #0#1, 12, nModelo, 99, 99;
  ;
  NULL, Imprime130;
|FIN;

||///////////////////////////////////////////////////////////////////

|CALCULO Tipo3;  |TIPO 3;
    nEjer     = #0#1;   ||2015
    enError15 = 0;
    sw        = 0;

    |ABRE #114;
    |ABRE #118;
    |SI #0#0 = 000; |O #0#0 = 130;
        nModelo = 130;
        |SI #0#2 ! 0;
            nDEmpresa = #0#2;
            nHEmpresa = #0#3;
            |BUCLE Leec130;
        |SINO;
            |PARA nCampo = 4;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
                  |SI #0nCampo ! 0;
                      nDEmpresa = #0nCampo;
                      nHEmpresa = #0nCampo;
                      |BUCLE Leec130;
                  |FINSI;
            |SIGUE;
        |FINSI;

        |SI sw = 1;
            enSwLinea = 0; |PRINT;
        |FINSI;
    |FINSI;

    |SI #0#0 = 000; |O #0#0 = 131;
        nModelo = 131;
        |ABRE #16;
        |ABRE #18;
        |SI #0#2 ! 0;
            nDEmpresa = #0#2;
            nHEmpresa = #0#3;
            |BUCLE Leem017;
        |SINO;
            |PARA nCampo = 4;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
                  |SI #0nCampo ! 0;
                      nDEmpresa = #0nCampo;
                      nHEmpresa = #0nCampo;
                      |BUCLE Leem017;
                  |FINSI;
            |SIGUE;
        |FINSI;
        |CIERRA #18;
        |CIERRA #16;
    |FINSI;
    |CIERRA #118;
    |CIERRA #114;

    |SI sw ! 0;
        |PIEINF;
        |FININF;
        |FINIMP;
    |FINSI;

    |SI enError15 = 0; |Y aParam = "";
        |MENAV "0000No hay incidencias";
    |FINSI;
|FCAL;

|| *************************************************************************

|PROGRAMA;
    aParam = PARAMETRO; |QBF aParam;

    |SI aParam = "";
        |MANTE #0;
    |SINO;
        |DDEFECTO #0;
        #0#0 = enModelo;
        |SI aParam = "EMPRESA";
            #0#2 = enEmpresa;
            #0#3 = enEmpresa;
        |SINO;
            #0#2 = 00001;
            #0#3 = 99999;
        |FINSI;
        |HAZ Tipo3;
        |SI enError15 ! 0; |Y aParam = "EMPRESA";
            |MENAV "0000Importe en la Reduc.creacion empleo y ejercicio 2015 incompatible. Modifiquela";
        |FINSI;
    |FINSI;
|FPRO;
