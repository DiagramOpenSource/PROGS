|FICHEROS;
     dsmow153 #583,MANTE;

     dsmom107 #107;

     espejo1@   #1004;
|FIN;

|VARIABLES;
     aAlfa      = "";
     aPathPdf   = "";
     aAbre      = "";
     aOrigen    = "";
     aDestino   = "";
     aIPRemoto  = "";
     aDirRemoto = "";
     aBase      = "";
     nPara1     = 0;
     nPara2     = 0;

     nCambio     = 0;
     nContador  = 0;
     nSinDatos  = 0;
     nLinea     = 0;

     nBoton2    = -1;
     nBoton3    = -1;
     nBoton6    = -1;
     nBoton4    = -1;
     nBoton8    = -1;
     nBoton10   = -1;
     nBoton12   = -1;
     nBoton17   = -1;
     nBoton22   = -1;

     nSwBoton2  = 0;
     nSwBoton3  = 0;
     nSwBoton6  = 0;
     nSwBoton4  = 0;
     nSwBoton8  = 0;
     nSwBoton10 = 0;
     nSwBoton12 = 0;
     nSwBoton17 = 0;
     nSwBoton22 = 0;

     nTecla     = 0;
     nModelo    = 0;
     nHandle    = 0;
     nUltimo    = 0;
     nCanal     = 0;
     nNume      = 0;
     nDPeriodo  = 0;
     nHPeriodo  = 0;
     nCampo1    = 0;
     nCampo2    = 0;

     aPathIntegral = "";
     aJustiModelo  = "";
     aElectModelo  = "";
     nEjer         = 0;

  {10}aPopup       = "";

     &enDEmpresa = 0;
     &enHEmpresa = 0;
     &enDEjer    = 0;
     &enHEjer    = 0;
     &enDModelo  = 0;
     &enHModelo  = 0;
     &eaPathAcrobat = "";

     &enArchi = 0;
     &aCm9  = "";
     &aCm11 = "";
     &aCm13 = "";
     &eaCamp4_9  = "";
     &efCamp4_10 = @;
     &eaCamp4_19 = "";
     &eaCamp4_25 = "";
     &enInfComp  = 0;
     aMas        = "";
     nBaja        = 0;
     &enDesdeAlta = 0;
     &enSw999     = 0;
|FIN;

|INCLUYE i_variar;

|PROCESO EntraMod583;
      enSw999  = 0;
      |SUB_EJECUTA_NP "dsmo5831";
      |SI enSw999 = 1;
          |PTEC 825;
          |PTEC 802;
          |PTEC 806;
          |PTEC 806;
      |FINSI;
|FPRC;

|PROCESO Tipo12;  |TIPO 12;
|FPRC;

|PROCESO ComPosterior;
     nBaja = 0;
     |DBASE aBase; |QBF aBase;
     aMas  = aBase + "def/dsmo5831.mas";
     |CARGA_DEF aMas, espejo1@;
     |SI FSalida = 0;
         |ABRE #1004;
         nPara2 = #583#2 + 1;
         |PARA nPara1 = nPara2; |SI nPara1 [ 99; |HACIENDO;
               #1004#0 = #583#0;
               #1004#1 = #583#1;
               #1004#2 = nPara1;
               |LEE 000#1004];
               |SI FSalida = 0; |Y #1004#0 = #583#0; |Y #1004#1 = #583#1;
                   |SI #1004#6 ! 0; |O #1004#7 ! 0; |O #1004#20 ! 0;
                       nBaja = 1;
                   |FINSI;
                   |SI #1004#11 = "X"; nBaja = 2; |FINSI;
                   |SI nBaja = 2; |SAL; |FINSI;
                   nPara1 = #1004#2 + 1;
               |SINO;
                   |SAL;
               |FINSI;
         |SIGUE;
         |CIERRA #1004;

         |DESCARGA_DEF #espejo1@;
     |FINSI;
|FPRC;

|PROCESO ComAnterior;
     nBaja = 1;
     |DBASE aBase; |QBF aBase;
     aMas  = aBase + "def/dsmo5831.mas";
     |CARGA_DEF aMas, espejo1@;
     |SI FSalida = 0;
         |ABRE #1004;
         #1004#0 = #583#0;
         #1004#1 = #583#1 - 1;
         #1004#2 = 99;
         |LEE 000#1004=;
         |SI FSalida = 0;
             nBaja = 0;
             |SI #1004#7 < 500000; nBaja = 2; |FINSI;
         |FINSI;
         |CIERRA #1004;
         |DESCARGA_DEF #espejo1@;
     |FINSI;
     |SI nBaja ! 0;
         |PUSHV 1017 1373;
         |CUADRO 1017 1373;
         |SI nBaja = 1;
             |ATRI R;
             |PINTA 1118, " Sin datos del Ejercicio Anterior. Puede que solo este ";
             |PINTA 1218, " obligado a realizar el pago a cuenta del 3T.          ";
             |ATRI 0;
         |SINO;
             |ATRI R;
             |PINTA 1118, " Base imponible del a¤o anterior inferior a 500.000.   ";
             |PINTA 1218, " Solo esta obligado a realizar el pago a cuenta del 3T.";
             |ATRI 0;
         |FINSI;
         |PAUSA;
         |CONFI "    NDesea continuar";
         |SI FSalida = 0;
             nBaja = 0;
         |FINSI;
         |POPV;
     |FINSI;
|FPRC;

|| \\\\\ Alta o Modificacion Modelos ////////
|PROCESO AltaModelo;

     |SI #583#2 ! 9; |Y #583#2 ! 99; |Y #583#9 = " ";
         |HAZ ComAnterior;
         |SI nBaja ! 0; |ACABA; |FINSI;
     |FINSI;

     |SI #583#16 = "X";
         |MENAV "     Modelo ya Archivado. No se puede Modificar";
         |ACABA;
     |FINSI;

     |HAZ ComPosterior;
     |SI nBaja ! 0;
         aAlfa = "    NExiste Datos en la Declaracion Periodo Posterior";
         |SI nBaja = 1;
             aAlfa = aAlfa + ". Desea Continuar";
             |CONFI  aAlfa;
             |SI FSalida ! 0;
                 |ACABA;
             |FINSI;
         |SINO;
             aAlfa = aAlfa + " y esta Impreso. No puede modificar los datos";
             |MENAV aAlfa;
             |ACABA;
         |FINSI;
     |FINSI;

     enTipoEntrada = 1;
     |HAZ EntraMod583;

     |LEE 101#583=;
     |SI enEntrada = 1;
         #583#9  = "X";
         #583#10 = ~;
         |GRABA 020#583a;
     |FINSI;
     |LIBERA #583;
|FPRC;

|| \\\\\ Baja del Modelo ////////

|PROCESO BajaModelo;

     |SI #583#9 = " ";  |ACABA;  |FINSI;

     |SI #583#16 = "X";
         |MENAV "     Modelo ya Archivado. No se puede borrar los datos";
         |ACABA;
     |FINSI;

     |HAZ ComPosterior;

     |SI nBaja ! 0;
         aAlfa = "    NExiste Datos en la Declaracion Periodo Posterior";
         |SI nBaja = 1;
             aAlfa = aAlfa + ". Desea borrar los datos";
             |CONFI  aAlfa;
             |SI FSalida ! 0;
                 |ACABA;
             |FINSI;
         |SINO;
             aAlfa = aAlfa + " y esta Impreso. No puede borrar los datos";
             |MENAV aAlfa;
             |ACABA;
         |FINSI;
     |FINSI;

     |SI #583#11 = "X";
         |CONFI "0000NModelo ya impreso, Esta Seguro de borrar los datos";
         |SI FSalida ! 0;
             |ACABA;
         |FINSI;
     |SINO;
         |CONFI "0000NEsta seguro de borrar los datos ";
         |SI FSalida ! 0;
             |ACABA;
         |FINSI;
     |FINSI;

     enTipoEntrada = 3;
     |HAZ EntraMod583;

     |LEE 101#583=;
     |LIBERA #583;
|FPRC;

|PROCESO Boton2m583;
     |PTEC 824;
|FPRC;

|PROCESO Boton3m583;
     |PTEC 825;
|FPRC;

|PROCESO Boton6m583;
     |PTEC 828;
|FPRC;

|PROCESO Boton4m583;
     |PTEC 826;
|FPRC;

|PROCESO Boton8m583;
     |PTEC 830;
|FPRC;

|PROCESO Boton10m583;
     |PTEC 832;
|FPRC;

|PROCESO Boton17m583;
     |PTEC 839;
|FPRC;

|PROCESO Boton22m583;
     |PTEC 844;
|FPRC;

|PROCESO FinBotonm583;
     |FIN_CONTROL_WINDOWS nBoton2;
     |FIN_CONTROL_WINDOWS nBoton3;
     |FIN_CONTROL_WINDOWS nBoton6;
     |FIN_CONTROL_WINDOWS nBoton22;
     |FIN_CONTROL_WINDOWS nBoton4;
     |FIN_CONTROL_WINDOWS nBoton8;
     |FIN_CONTROL_WINDOWS nBoton10;
     |FIN_CONTROL_WINDOWS nBoton17;

     |PULSATECLA;
|FPRC;

|PROCESO Tipo0m583;  |TIPO 0;
     |HAZ FinBotonm583;
|FPRC;

|PROCESO Tipo7m583;  |TIPO 7;
     |BLIN 04;

     |CONTROL_SIMPLE 0, "LBOTON,F2  Consulta Modelo     ", 1028, 1053, Boton2m583;
     nBoton2 = FSalida;

     |CONTROL_SIMPLE 0, "LBOTON,F3  Entrada de Datos    ", 1128, 1153, Boton3m583;
     nBoton3 = FSalida;

     |CONTROL_SIMPLE 0, "LBOTON,F4 Entrada Instalaciones ", 1228, 1253, Boton4m583;
     nBoton4 = FSalida;

     |CONTROL_SIMPLE 0, "LBOTON,F6  Emitir Modelo       ", 1328, 1353, Boton6m583;
     nBoton6 = FSalida;

     |CONTROL_SIMPLE 0, "LBOTON, Informe Comprobacion ", 1055, 1080, Boton22m583;
     nBoton22 = FSalida;

     |CONTROL_SIMPLE 0, "LBOTON,F8  Baja de Datos Modelo ", 1155, 1180, Boton8m583;
     nBoton8 = FSalida;

     |CONTROL_SIMPLE 0, "LBOTON,F10 Dejar No Impreso    ", 1255, 1280, Boton10m583;
     nBoton10 = FSalida;

     aAlfa = "Archivar           "; |SI #583#16 = "X"; aAlfa = "Dejar No Archivado "; |FINSI;
     aAlfa = "LBOTON,Ctrl-F7 " + aAlfa;
     |CONTROL_SIMPLE 0, aAlfa, 1355, 1380, Boton17m583;
     nBoton17 = FSalida;

     |ABRE #107;
     |DDEFECTO #107;
     |LEE 041#107=;
     |CIERRA #107;

     |ESTADO_CONTROL nBoton2,  "DISABLE";  nSwBoton2  = 0;
     |ESTADO_CONTROL nBoton3,  "DISABLE";  nSwBoton3  = 0;
     |ESTADO_CONTROL nBoton6,  "DISABLE";  nSwBoton6  = 0;
     |ESTADO_CONTROL nBoton22, "DISABLE";  nSwBoton22 = 0;
     |ESTADO_CONTROL nBoton4,  "DISABLE";  nSwBoton4  = 0;
     |ESTADO_CONTROL nBoton8,  "DISABLE";  nSwBoton8  = 0;
     |ESTADO_CONTROL nBoton10, "DISABLE";  nSwBoton10 = 0;
     |ESTADO_CONTROL nBoton17, "DISABLE";  nSwBoton17 = 0;

     |SI #583#9 = "X";
         |ESTADO_CONTROL nBoton6,   "ENABLE";  nSwBoton6  = 1;
         |ESTADO_CONTROL nBoton22,  "ENABLE";  nSwBoton22 = 1;
     |FINSI;

     |SI #583#11 = "X";  |Y #583#16 = " ";
         |ESTADO_CONTROL nBoton10,  "ENABLE";  nSwBoton10 = 1;
     |FINSI;

     |SI #583#11 = "X";
         |ESTADO_CONTROL nBoton17,  "ENABLE";  nSwBoton17 = 1;
     |FINSI;

     |SI #583#9 = " "; |Y #583#11 = " "; |Y #107#30 = "S";
         |ESTADO_CONTROL nBoton17,  "ENABLE";  nSwBoton17 = 1;
     |FINSI;

     |ESTADO_CONTROL nBoton2,  "ENABLE";  nSwBoton2  = 1;
     |ESTADO_CONTROL nBoton3,  "ENABLE";  nSwBoton3  = 1;
     |ESTADO_CONTROL nBoton4,  "ENABLE";  nSwBoton4  = 1;
     |ESTADO_CONTROL nBoton8,  "ENABLE";  nSwBoton8  = 1;
|FPRC;

|PROCESO Tipo11m583;  |TIPO 11;
     nTecla = FSalida;

     |HAZ FinBotonm583;

     enEmpresa     = #583#0;
     enEjer        = #583#1;
     enPeriodo     = #583#2;
     enModelo      = #583#3;

|| \\\\\ Consulta Modelos ////////

     |SI nTecla = 10;  |Y nSwBoton2 = 1;
         enTipoEntrada = 4;

         |HAZ EntraMod583;
         |HAZ Refresco;
     |FINSI;

|| \\\\\ Alta o Modificacion Modelos ////////

     |SI nTecla = 11;  |Y nSwBoton3 = 1;
         |HAZ AltaModelo;
         |HAZ Refresco;
         |ACABA;
     |FINSI;

|| \\\\\ Emision del Modelo

     |SI nTecla = 14;  |Y nSwBoton6 = 1;
         |SI #583#9 = "X";
             |SI #583#16 = "X";
                 |CONFI "0000NModelo Archivado. Desea continuar";
                 |SI FSalida ! 0;  |ACABA;  |FINSI;
             |FINSI;

             |LEE 101#583=;
             eaCamp4_9  = #583#11;
             efCamp4_10 = #583#12;
             #583#11 = "";
             #583#12 = "01.01.0000";
             |GRABA 020#583a;
             |LIBERA #583;

             enInfComp = 0;
             |SUB_EJECUTA_NP "dsmoy583;PLANING";
             |SI enInfComp = 1;
                 |LEE 101#583=;
                 #583#11 = eaCamp4_9;
                 #583#12 = efCamp4_10;
                 |GRABA 020#583a;
                 |LIBERA #583;
             |FINSI;
             |HAZ Refresco;
         |FINSI;
     |FINSI;

     |SI nTecla = 30;  |Y nSwBoton22 = 1;
         |SI #583#9 = "X";
             |SUB_EJECUTA_NP "dsmoy583;INFC";
             |HAZ Refresco;
         |FINSI;
     |FINSI;

|| \\\\\ Consulta Instalaciones

     |SI nTecla = 12;  |Y nSwBoton4 = 1;
         enEmpresa = #583#0;
         |SUB_EJECUTA_NP "dsmo5830";

         |HAZ Refresco;
     |FINSI;

|| \\\\\ Baja del Modelo ////////

     |SI nTecla = 16;  |Y nSwBoton8 = 1;
         |HAZ BajaModelo;
         |HAZ Refresco;
     |FINSI;

|| \\\\\ Dejar no Impreso ////////

     |SI nTecla = 18;  |Y nSwBoton10 = 1;
         |SI #583#11 = "X";  |Y #583#16 = " ";
             aAlfa =  "0000NEsta seguro de dejar el Modelo como no Impreso";
             |CONFI aAlfa;
             |SI FSalida ! 0;  |ACABA;  |FINSI;

             |LEE 101#583=;
             #583#11 = "";
             #583#12 = "01.01.0000";
             |GRABA 020#583a;
             |LIBERA #583;

             |PINTA #583#11;

         |FINSI;
     |FINSI;

|| \\\\\ Archivar o Desarchivar  ////////

     |SI nTecla = 25;  |Y nSwBoton17 = 1;
         |SI #583#16 = "X";
             |CONFI "0000NEsta seguro de dejar el Modelo como no Archivado";
             |SI FSalida ! 0;  |ACABA;  |FINSI;
             |LEE 101#583=;
             #583#16 = "";
             #583#14 = "01.01.0000";
             |GRABA 020#583a;
             |LIBERA #583;
             |PINTA #583#16;
         |SINO;
             enArchi = 0;
             aAlfa = "0000NDesea Archivar el Modelo ";
             |SI #583#11 = " "; |Y #583#9 = " ";
                 enArchi = 1;
                 aAlfa   = "0000NDesea Archivar el Modelo NO ELABORADO ";
             |FINSI;
             |CONFI aAlfa;
             |SI FSalida = 0;
                 aAlfa   = #583#3;
                 aAlfa   = "dsmoz007;" + aAlfa + "PLANING";

                 |SUB_EJECUTA_NP aAlfa;
                 |HAZ Refresco;
             |FINSI;
         |FINSI;
     |FINSI;
|FPRC;
