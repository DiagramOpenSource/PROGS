|FICHEROS;
     dsmoz048 #0;
     dsmom003 #2;
     dsmom004;

     :/contagen/def/camaniva #4;
     :/contagen/def/caivalin #5;
     dsmom030 #30;
     dsmom031 #31;

     :/basica/def/agitab99 #99;

     dsmow049 #998,MANTE;
     dsmow043 #999,MANTE;
|FIN;

|VARIABLES;
     aFichero  = ""; aParametro = ""; aAlfa = "";
     nCampo    = 0; nDEmpresa = 0; nHEmpresa = 0; enGraba   = 0; Cont = 0;
     Comodin = ""; nGTempo = 0; FEstado = 0; Calc = 0; SwHayDatos = 0; NumRegis = 0;
     Libro   = 0;  Serie = "";  Factura = 0; PrintCab = 0; aLong = ""; nLong = 0;

     &SwCabFac = 0; &SwLinFac = 0; &SwTotFac = 0; &SwSToFac = 0; &SwTToFac = 0;
     &SwBlanco = 0; &fecinf = @; &fecha_i = 0; &Prefijo = "";
     &regis = 0; &ContLin = 0; &varalf = ""; &Pagina = 0; SwPrint = 0; &SwPinta = 0;
     &TF  = 0; &TB  = 0; &TI  = 0; &TR  = 0; &TD  = 0;
     &TLF = 0; &TLB = 0; &TLI = 0; &TLR = 0; &TLD = 0;

     &EALibro  = "";  &EASerie   = ""; &EACuenta = ""; &EACtaTitulo = "";
     &EACif    = "";  &EADescLin = ""; &ENBase   = 0;  &ENPorIva = 0;
     &ENIva    = 0;   &ENPorRec  = 0;  &ENRec    = 0;  &ENDto    = 0;
     &EAPeriodo = ""; &ENTotalF  = 0;

     NumLineas = 68;
     nInkey = 0;
     nPara1 = 0;
|FIN;

|INCLUYE i_variar;
|INCLUYE i_varcon;

|| /////////////////////// PROCESOS ENTRADA POR PANTALLA \\\\\\\\\\\\\\\\\\\\

|PROCESO Libro;
     |SI #dsmoz048#0 < 900;
        |MENAV "    Codigo incorrecto. Introduzca un codigo de libro";
        |ERROR; |ACABA;
     |FINSI;

     |ABRE #99;
     #99#0 = #0#0;
     |LEE 040#99=;
     |SI FSalida ! 0;  |DDEFECTO #99;  |FINSI;
     |CIERRA #99;

     |SI #99#4 = "N";
         |MENAV "     Modelo sin Activar.";
         |ERROR;
     |FINSI;
|FPRC;

|PROCESO Periodo;
     #0#2 = "          ";
     |SI #0#1 = 1;   #0#2 = "ENERO     ";  |FINSI;
     |SI #0#1 = 2;   #0#2 = "FEBRERO   ";  |FINSI;
     |SI #0#1 = 3;   #0#2 = "MARZO     ";  |FINSI;
     |SI #0#1 = 4;   #0#2 = "ABRIL     ";  |FINSI;
     |SI #0#1 = 5;   #0#2 = "MAYO      ";  |FINSI;
     |SI #0#1 = 6;   #0#2 = "JUNIO     ";  |FINSI;
     |SI #0#1 = 7;   #0#2 = "JULIO     ";  |FINSI;
     |SI #0#1 = 8;   #0#2 = "AGOSTO    ";  |FINSI;
     |SI #0#1 = 9;   #0#2 = "SEPTIEMBRE";  |FINSI;
     |SI #0#1 = 10;  #0#2 = "OCTUBRE   ";  |FINSI;
     |SI #0#1 = 11;  #0#2 = "NOVIEMBRE ";  |FINSI;
     |SI #0#1 = 12;  #0#2 = "DICIEMBRE ";  |FINSI;
     |SI #0#1 = 99;  #0#2 = "ANUAL     ";  |FINSI;

     |SI #0#2 = "          ";
         |MENAV "     Periodo Incorrecto.";
         |ERROR;
     |FINSI;
     |PINTA #0#2;
|FPRC;

|PROCESO Parrilla;
     |SI #0#4 = 0;
         #0#4 = 0;  |PINTA #0#4;
         #0#5 = 0;  |PINTA #0#5;  |C_M #0#5, 1, "N";
         |PARA nCampo = 6;  |SI nCampo [ 17;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |SINO;
         |SI enEmpresa = 0;
             |C_M #0#4, 1, "S";
             |C_M #0#5, 1, "S";
         |FINSI;

         |PARA nCampo = 6;  |SI nCampo [ 17;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "N";
               #0nCampo = 0;  |PINTA #0nCampo;
         |SIGUE;
     |FINSI;
|FPRC;

|PROCESO NoMasEmpresas;
     |SI #0Campo = 0;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 17;  |HACIENDO nCampo = nCampo + 1;
               #0nCampo = 0;  |C_M #0nCampo, 1, "N";
         |SIGUE;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 17;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |FINSI;
|FPRC;

|CALCULO Datos7; |TIPO 7;
 |MENSA "    <F2> Modificar Datos de Acumulados Emision, <F3> Cambiar Emitir Libro S/N";
|FCAL;

|PROCESO DatosManip; |TIPO 11;
   nInkey = FSalida;

   |SI nInkey = 10;
      #dsmow049#0  = #dsmow043#1;
      #dsmow049#11 = #dsmoz048#0;
      #dsmow049#41 = 0;
      |LEE 000#dsmow049.=;
      |SI FSalida ! 0;
         |DDEFECTO #dsmow049;
         #dsmow049#0  = #dsmow043#1;
         #dsmow049#11 = #dsmoz048#0;
         #dsmow049#41 = 0;
         |GRABA 020#dsmow049.b;
      |FINSI;

      |PUSHV 0909 2375;
      |PINPA #0#998; |PINDA #0#998;
      |ENDATOS #1#998;
      |SI FSalida = 0;
         |GRABA 020#dsmow049.a;
         |LIBERA #dsmow049;
      |FINSI;
      |POPV;
   |FINSI;
   |SI nInkey = 11;
       |SI #999#16 = "S";
           #999#16 = "N";
       |SINO;
           #999#16 = "S";
       |FINSI;
       |GRABA 020#999a;
   |FINSI;
|FPRC;

|PROCESO cuadro; |TIPO 7;
   |PUSHV  1712 2371;
   |CUADRO 1712 2371;
   |ATRI R;
   |PINTA 1713, " ----------- FORMATO LIBRO FACTURAS EMITIDAS ------------ ";
   |PINTA 1813, " ( 1) Descr. Fra., Nombre cliente, varias lineas          ";
   |PINTA 1913, " ( 2) Nombre y NIF cliente, una linea                     ";
   |PINTA 2013, " ( 3) Descr. Fra., Nombre cliente, una linea              ";
   |PINTA 2113, " ( 4) Nombre y NIF cliente, una linea s/recargo           ";
   |PINTA 2213, " ( 5) Descr. Fra., Nombre cliente, una linea s/recargo    ";
   |ATRI r;
|FPRC;

|PROCESO SalCuadro;
   |POPV;
|FPRC;

|| /////////////////////// PROCESOS DE TRABAJO \\\\\\\\\\\\\\\\\\\\

|PROCESO LineasLibro;
   #dsmom030#0 = #caivalin#3;
   |LEE 000#dsmom030.=;
   |SI FSalida = 0;
      #dsmom031#0 = #caivalin#3;
      #dsmom031#1 = #dsmoz048#0;
      |LEE 000#dsmom031.=;
      |SI FSalida = 0;

         |SI PrintCab = 1;
            SwCabFac = 1;
            |SI #dsmoz048#22 = 1; |SI SwPrint = 0; |PRINT; |FINSI; ContLin = ContLin + 2; |FINSI;
            SwCabFac = 0;
            PrintCab = 0;
         |FINSI;

         EADescLin = #caivalin#4;
         ENBase    = (#caivalin#6  / enConversor) ! EURODBMOD;
         ENPorIva  = #caivalin#7;
         ENIva     = (#caivalin#8  / enConversor) ! EURODBMOD;
         ENPorRec  = #caivalin#9;
         ENRec     = (#caivalin#10 / enConversor) ! EURODBMOD;
         ENDto     = (#caivalin#11 / enConversor) ! EURODBMOD;

         Calc = ContLin + 3;
         |SI Calc ] NumLineas;
            SwTotFac = 1;
            |SI #dsmoz048#22 = 1; |SI SwPrint = 0; |PRINT; |FINSI; ContLin= ContLin + 2; |FINSI;
            SwTotFac = 0;
            |HAZ VeteFinal;
            Pagina = Pagina + 1;
            SwCabFac = 1;
            |SI #dsmoz048#22 = 1; |SI SwPrint = 0; |PRINT; |FINSI; ContLin= ContLin + 2; |FINSI;
            SwCabFac = 0;
         |FINSI;

         SwLinFac = 1;
         |SI SwPrint = 0; |PRINT; |FINSI; ContLin = ContLin + 1;
         SwLinFac = 0;
         TB  = TB  + ((#caivalin#6  / enConversor) ! EURODBMOD);
         TI  = TI  + ((#caivalin#8  / enConversor) ! EURODBMOD);
         TR  = TR  + ((#caivalin#10 / enConversor) ! EURODBMOD);
         TD  = TD  + ((#caivalin#11 / enConversor) ! EURODBMOD);
         TF  = TB + TI + TR - TD;

         |SI SwPrint = 0;
             TLB  = TLB  + ((#caivalin#6  / enConversor) ! EURODBMOD);
             TLI  = TLI  + ((#caivalin#8  / enConversor) ! EURODBMOD);
             TLR  = TLR  + ((#caivalin#10 / enConversor) ! EURODBMOD);
             TLD  = TLD  + ((#caivalin#11 / enConversor) ! EURODBMOD);
             TLF  = TLB + TLI + TLR - TLD;
         |SINO;
            |SI #dsmow049#2 = "S";
                TLB  = TLB  + ((#caivalin#6  / enConversor) ! EURODBMOD);
                TLI  = TLI  + ((#caivalin#8  / enConversor) ! EURODBMOD);
                TLR  = TLR  + ((#caivalin#10 / enConversor) ! EURODBMOD);
                TLD  = TLD  + ((#caivalin#11 / enConversor) ! EURODBMOD);
                TLF  = TLB + TLI + TLR - TLD;

                #dsmow049#5 = TLB;
                #dsmow049#6 = TLI;
                #dsmow049#7 = TLR;
                #dsmow049#8 = TLF;
            |SINO;
               TLB = #dsmow049#5;
               TLI = #dsmow049#6;
               TLR = #dsmow049#7;
               TLF = #dsmow049#8;
            |FINSI;
         |FINSI;

         SwPinta = 0;
      |FINSI;
   |FINSI;
|FPRC;

|DEFBUCLE LineasLibro;
#caivalin#1;
;
#camaniva#0,#camaniva#1,001;
#camaniva#0,#camaniva#1,999;
;
NULL,LineasLibro;
|FIN;

|PROCESO VeteFinal;
   SwBlanco = 1;
   |PARA; |SI ContLin < NumLineas; |HACIENDO ContLin = ContLin + 1;
      |SI SwPrint = 0; |PRINT; |FINSI;
   |SIGUE;
   |SI SwPrint = 0; |PIEINF; |FINSI;
   SwBlanco = 0;
   |SI TLF ! 0; ContLin = 10; |SINO; ContLin = 9; |FINSI;
|FPRC;

|PROCESO Facturas;
   |SI #camaniva#36 = "N"; |ACABA; |FINSI;

   EALibro     = ("00" + #camaniva#0) % -102;
   EASerie     = #camaniva#2;
   EACuenta    = #camaniva#12;
   EACtaTitulo = #camaniva#13;
   EACif       = #camaniva#14;
   ENTotalF    = (#camaniva#21 / enConversor) ! EURODBMOD;

   NumRegis = #dsmow049#4;
   |SI NumRegis ! 0;
      regis = regis + 1;
   |SINO;
      regis = #camaniva#3;
   |FINSI;

   |SI #dsmoz048#20 = 1; |O #dsmoz048#20 = 3;
      fecha_i = #camaniva#4;
   |SINO;
      fecha_i = #camaniva#8;
   |FINSI;

   |SI #dsmoz048#1 ! 99; |SI #dsmoz048#1 ! #camaniva#5; |ACABA; |FINSI; |FINSI;

   Calc = ContLin + 5;
   |SI Calc ] NumLineas;
      Prefijo = "SUB"; SwTToFac = 1;
      |SI SwPrint = 0; |PRINT; |FINSI; ContLin = ContLin + 2;
      Prefijo = "";    SwTToFac = 0;
      |HAZ VeteFinal;
      Pagina = Pagina + 1;
   |FINSI;

   |SI Pagina < #dsmow049#1;
      SwPrint = 1;
   |SINO;
      |SI SwPrint = 1;
         TLB = #dsmow049#5; TLI= #dsmow049#6;
         TLR = #dsmow049#7; TLF= #dsmow049#8;
      |FINSI;
      SwPrint = 0;
   |FINSI;

   SwHayDatos = 1;
   SwPinta = 1; PrintCab = 1;
   TF = 0; TB = 0; TI = 0; TR = 0; TD = 0;
   |BUCLE LineasLibro;
   |SI PrintCab = 0;
      SwTotFac = 1;
      |SI #dsmoz048#22 = 1; |SI SwPrint = 0; |PRINT; |FINSI; ContLin= ContLin + 2; |FINSI;
      SwTotFac = 0;
   |FINSI;

|FPRC;

|DEFBUCLE Facturas1;
#camaniva#1;
;
01,0000000001;
99,9999999999;
;
NULL,NULL,NULL,NULL,NULL,Facturas;
#camaniva#4;
|FIN;

|DEFBUCLE Facturas2;
#camaniva#1;
;
01,0000000001;
99,9999999999;
;
NULL,NULL,NULL,NULL,NULL,Facturas;
#camaniva#8;
|FIN;

|DEFBUCLE Facturas3;
#camaniva#1;
;
01,0000000001;
99,9999999999;
;
NULL,NULL,NULL,NULL,NULL,Facturas;
#camaniva#2,#camaniva#3,#camaniva#4;
|FIN;

|DEFBUCLE Facturas4;
#camaniva#1;
;
01,0000000001;
99,9999999999;
;
NULL,NULL,NULL,NULL,NULL,Facturas;
#camaniva#2,#camaniva#3,#camaniva#8;
|FIN;

|PROCESO Emision;
   enEmpresa  = #dsmow043#1;
   enPerConta = #dsmow043#2;
   eaLaMonEmp = #dsmow043#13;

   |HAZ SituaContagen;
   |HAZ EnConversor;

   Comodin = #dsmow043#15; |QBF Comodin;
   |PATH_DAT #4 Comodin;
   |PATH_DAT #5 Comodin;

   TB = 0;  TI = 0;  TR = 0;  TF = 0;  Pagina = 1; TD = 0;
   TLB = 0; TLI = 0; TLR = 0; TLF = 0; TLD = 0; ContLin = 9;
   SwPrint = 1; Prefijo = "";

   #dsmow049#0  = #dsmow043#1;
   #dsmow049#11 = #dsmoz048#0;
   |LEE 000#dsmow049.=;
   |SI FSalida ! 0;
      |DDEFECTO #dsmow049;
      #dsmow049#0  = #dsmow043#1;
      #dsmow049#11 = #dsmoz048#0;
      |GRABA 020#dsmow049.b;
   |SINO;
      TB = #dsmow049#5;  TI = #dsmow049#6;
      TR = #dsmow049#7;  TF = #dsmow049#8;
      regis = #dsmow049#4 - 1;
   |FINSI;

   |SI #dsmow049#2 = "S";
      SwPrint = 1;
   |FINSI;

   |ABRE #camaniva; |ABRE #caivalin;
   |SI #dsmoz048#20 = 1; |BUCLE Facturas1; |FINSI;
   |SI #dsmoz048#20 = 2; |BUCLE Facturas2; |FINSI;
   |SI #dsmoz048#20 = 3; |BUCLE Facturas3; |FINSI;
   |SI #dsmoz048#20 = 4; |BUCLE Facturas4; |FINSI;
   |SI SwHayDatos = 1;
      SwTToFac = 1; |PRINT; SwTToFac = 0; ContLin = ContLin + 2;
      |HAZ VeteFinal;
   |FINSI;
   |CIERRA #caivalin; |CIERRA #camaniva;

   #dsmow049#1 = Pagina + 1;
   #dsmow049#3 = "";
   |SI NumRegis ! 0;
      #dsmow049#4 = regis + 1;
   |SINO;
      #dsmow049#4 = 0;
   |FINSI;
   |GRABA 020#dsmow049.a; |LIBERA #dsmow049;
|FPRC;

|DEFBUCLE Emision;
 #dsmow043#1;
 16;
 000001,"S";
 999999,"S";
 ;
 NULL,Emision;
|FIN;

|PROCESO TempoLibros;
   enEmpresa = #dsmom004#0;
   enPerConta = -1;
   enEjer = #dsmoz048#3;

   swerror = 0;
   |HAZ SituaContagen;
   |SI swerror = 0;
      |HAZ GrabaTemporal;
   |FINSI;
|FPRC;

|DEFBUCLE RelleTempo;
#dsmom004#1;
;
nDEmpresa,#dsmoz048#3,#dsmoz048#1,#dsmoz048#0,00;
nHEmpresa,#dsmoz048#3,#dsmoz048#1,#dsmoz048#0,99;
;
NULL,TempoLibros;
|FIN;

|RUTINA inferior;
   #dsmow043#0 = 00001;
|FRUT;

|RUTINA superior;
   #dsmow043#0 = 99999;
|FRUT;

|PROCESO GrabaTemporal;
 nGTempo = nGTempo + 1;
 #999#0  = nGTempo;      || Guia
 #999#1  = enEmpresa;    || Empresa
 #999#2  = enPerConta;   || Periodo Contable
 #999#3  = enEjer;       || A¤o
 #999#4  = dig1;         || Desde Mes
 #999#5  = dig3;         || N§ Digitos
 #999#6  = dig4;         || Digitos Nivel 1
 #999#7  = dig5;         || Digitos Nivel 2
 #999#8  = dig6;         || Digitos Nivel 3
 #999#9  = dig7;         || Digitos Nivel 4
 #999#10 = dig8;        || Digitos Nivel 5
 #999#11 = dig9;        || Digitos Nivel 6
 #999#12 = eaEstadoEmp; || Activa / Inactiva
 #999#13 = eaMonedaCon;
 #999#14 = eaNombreEmp; || Nombre Empresa
 #999#15 = aPathConta;  || Path
 #999#16 = "S";
 |GRABA 020#999n;
|FPRC;

|PROGRAMA;
     |CLS;
     aParametro = PARAMETRO;  |QBF aParametro;
     |SI aParametro ! 0;
         |DDEFECTO #0;

         #0#0 = aParametro;
         |C_M #0#0, 1, "N";
     |FINSI;

     |PINPA #0#0; |PINDA #0#0;
     |ENDATOS #1#0;
     |SI FSalida = 0;
        |ABRE #agitab99;
        #agitab99#0 = #dsmoz048#0;
        |LEE 000#agitab99.=;
        |SI FSalida = 0;
           varalf = #agitab99#2 + #agitab99#3; |QBF varalf;
           aLong = varalf % 0; nLong = aLong;
           nLong = 75 - nLong;
           |SI nLong > 0;
              Calc = nLong / 2;
              Comodin = " " * Calc;
              varalf = Comodin + varalf;
           |FINSI;
        |FINSI;
        |CIERRA #agitab99;

        |SI #dsmoz048#1 = 99;
           EAPeriodo = "ANUAL";
        |SINO;
           EAPeriodo = #dsmoz048#1;
        |FINSI;

        |DELINDEX #dsmow043;
        |ABRE #dsmow043; |ABRE #dsmow049;
        |SI #dsmoz048#4 = 0;
           |PARA Cont = 6; |SI Cont [ 17; |HACIENDO Cont = Cont + 1;
              nDEmpresa = #dsmoz048#Cont#;
              nHEmpresa = #dsmoz048#Cont#;
              |BUCLE RelleTempo;
           |SIGUE;
        |SINO;
           nDEmpresa = #dsmoz048#4;
           nHEmpresa = #dsmoz048#5;
           |BUCLE RelleTempo;
        |FINSI;

        |PUSHV 0909 2375;
        |ENTLINEAL #1#dsmow043,-1,5,22,1,inferior,superior;
        |POPV;

        |IMPRE 0;
        |SI FSalida ] 0;
           |SI #dsmoz048#22 = 1; |INFOR "modlibr1"; |FINSI;
           |SI #dsmoz048#22 = 2; |INFOR "modlibr2"; |FINSI;
           |SI #dsmoz048#22 = 3; |INFOR "modlibr3"; |FINSI;
           |SI #dsmoz048#22 = 4; |INFOR "modlibr4"; |FINSI;
           |SI #dsmoz048#22 = 5; |INFOR "modlibr5"; |FINSI;
           |SI FSalida ] 0;
              |ABRE #dsmom030; |ABRE #dsmom031;
              |BUCLE Emision;
              |CIERRA #dsmom031; |CIERRA #dsmom030;
              |PIEINF;
              |FININF;
           |FINSI;
           |FINIMP;
        |FINSI;

        |CIERRA #dsmow049; |CIERRA #dsmow043;
     |FINSI;
|FPRO;
