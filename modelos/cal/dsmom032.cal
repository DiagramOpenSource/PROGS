|FICHEROS;
    dsmom032 #32;
    dsmom030 #30;

    dsmom03@ #330;
|FIN;

|VARIABLES;
    aAlfa      = "";
    aPath      = "";
    aMas       = "";
    aID        = "";
    nID        = 0;
    {10}aPopup = "";

    nSw        = 0;
    aFichero   = "";
    nModo      = 0;
|FIN;

|INCLUYE i_variar;

|CALCULO FiltraCon; |TIPO 6;

     aAlfa = "";
     nSw   = 0;
     |ABRE #30;
     #30#0 = #32#0;
     |LEE 041#30=;
     |SI FSalida ! 0;
         nSw = 1;
         aAlfa = "0000No existe Concepto Contable";
     |SINO;
         |SI #30#4 ! "S";
             nSw = 1;
             aAlfa = "0000El Concepto Contable no es de IVA";
         |FINSI;
     |FINSI;
     |CIERRA #30;

     |SI nSw = 1;
         |MENAV aAlfa;
         |ERROR;
     |FINSI;
|FCAL;

|PROCESO inf330;
   #330#0 = 001;
|FPRC;

|PROCESO sup330;
   #330#0 = 999;
|FPRC;

|CALCULO ConsultaCon; |TIPO 11;
   |SI FSalida = 10;
      |ABRE #330;
      #330#0 = #32#0;
      |CONSULTA_F_CLAVE #1#330,inf330,sup330;
      #32#0 = #330#0; |PINTA #32#0;
      #32#1 = #330#1; |PINTA #32#1;
      |CIERRA #330;
      |ERROR;
   |FINSI;
|FCAL;

|PROCESO Tipo66C2;  |TIPO 66;
     aAlfa = "|C002WID";
     aID   = #dsmom032#aAlfa#;
     nID   = aID;
/*
     aPopup1  = nID;  || Si fuera -1 en la posicion
     aPopup2  = 6;
     aPopup3  =  "A - Adquisiciones";
     aPopup4  =  "E - Entregas";
     aPopup5  =  "S - Prestaciones";
     aPopup6  =  "I - Adquisiciones en el territorio del impuesto";
     aPopup7  =  "M - Entregas posteriores a importacion exenta";
     aPopup8  =  "H - Entregas posteriores importacion exenta con representante  ";
*/

     aPopup1  = nID;  || Si fuera -1 en la posicion
     aPopup2  = 3;
     aPopup3  =  "A - Adquisiciones";
     aPopup4  =  "I - Adquisiciones en el territorio del impuesto";
     aPopup5  =  "X - Operaciones no Intracomunitarias";

     IaPopup  = aPopup1  <-;
     |ESPECIFICA "TRACKPOPUP", aPopup;
     |SI FSalida ! 0;
         IaPopup  = aPopup3  <-;
         IaPopup  = IaPopup + (FSalida - 1);

         aAlfa = aPopup % 101;  #32#2 = aAlfa;
     |FINSI;

     |PINTA #32#2;
|FPRC;

|PROCESO Tipo0C2;  |TIPO 0;
/*
     |SI #32#2 ! "A"; |Y #32#2 ! "E";
         |Y #32#2 ! "S"; |Y #32#2 ! "I";
            |Y #32#2 ! "M"; |Y #32#2 ! "H";
        |MENAV "0000 Clave Modelo 349 incorrecta";
        |ERROR;
        |ACABA;
    |FINSI;
*/

     |SI #32#2 ! "A"; |Y #32#2 ! "I";
         |Y #32#2 ! "X";
        |MENAV "0000 Clave Modelo 349 incorrecta";
        |ERROR;
        |ACABA;
    |FINSI;

     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 1;
         #32#3 = 6;
         |SI #32#2 = "A"; #32#3 = 1; |FINSI;
         |SI #32#2 = "I"; #32#3 = 3; |FINSI;
         |SI #32#2 = "E"; #32#3 = 4; |FINSI;
         |SI #32#2 = "S"; #32#3 = 5; |FINSI;
     |FINSI;
     |PINTA #32#3;
|FPRC;

|PROCESO Tipo66C3;  |TIPO 66;
     aAlfa = "|C003WID";
     aID   = #dsmom032#aAlfa#;
     nID   = aID;

     aPopup1  = nID;  || Si fuera -1 en la posicion
     aPopup2  = 6;
     aPopup3  =  "1 - Adquisicion intracomunitaria de bienes ";
     aPopup4  =  "2 - Adquisicion intracomunitaria de medios transporte ";
     aPopup5  =  "3 - Inversion sujeto pasivo ";
     aPopup6  =  "4 - Entregas bienes de inversion naturaleza inmobiliaria ";
     aPopup7  =  "5 - Entr.y prest.en proced.adm.y judiciales ejec.forzosa ";
     aPopup8  =  "6 - Otros supuestos no contemplados anteriormente ";

     IaPopup  = aPopup1  <-;
     |ESPECIFICA "TRACKPOPUP", aPopup;
     |SI FSalida ! 0;
         IaPopup  = aPopup3  <-;
         IaPopup  = IaPopup + (FSalida - 1);

         aAlfa = aPopup % 101;  #32#3 = aAlfa;
     |FINSI;

     |PINTA #32#3;
|FPRC;

|PROCESO Tipo0C3;  |TIPO 0;
     |SI #32#3 = 6; |ACABA; |FINSI;

     |SI #32#2 = "A";
         |SI #32#3 ! 1; |Y #32#3 ! 2; nSw = 1; |FINSI;
     |FINSI;
     |SI #32#2 = "I";
         |SI #32#3 ! 3; nSw = 1; |FINSI;
     |FINSI;
     |SI #32#2 = "E"; |O #32#2 = "S"; |O #32#2 = "M"; |O #32#2 = "H";
         |SI #32#3 ! 4; |Y #32#3 ! 5; nSw = 1; |FINSI;
     |FINSI;
     |SI nSw = 1;
         |CONFI "0000NEs correcto ";
         |SI FSalida ! 0;
             |ERROR;
         |FINSI;
     |FINSI;
     |PINTA #32#3;
|FPRC;

|RUTINA ClaveBaja32;
 #32#0 = 001;
|FRUT;

|RUTINA ClaveAlta32;
 #32#0 = 999;
|FRUT;

|PROCESO CargoConceptos;

   #330#0 = #30#0;
   |LEE 041#330=;
   |SI FSalida ! 0;
       |DDEFECTO #330;
       #330#0  = #30#0;
       #330#01 = #30#01;
       #330#02 = #30#02;
       #330#03 = #30#03;
       #330#04 = #30#04;
       #330#05 = #30#05;
       #330#06 = #30#06;
       #330#07 = #30#07;
       #330#08 = #30#08;
       #330#09 = #30#09;
       #330#10 = #30#10;
       #330#11 = #30#11;
       #330#96 = #30#96;
       #330#97 = #30#97;
       #330#98 = #30#98;
       |GRABA 020#330n;
   |FINSI;
   |LIBERA #330;
|FPRC;

|DEFBUCLE CargoConceptos;
  #30#1;
  4;
  001, "S";
  999, "S";
  e;
  NULL, CargoConceptos;
|FIN;

|PROGRAMA;

  |DBASE aPath;
  aMas = aPath + "def/dsmom030";
  |CARGA_DEF aMas, dsmom03@;
  |SI FSalida ! 0;
      |MENAV "0000Error al carga el def dsmom030";
      |ACABA;
  |FINSI;

  aFichero = "xx" + Usuario;
  |NOME_DAT #330 aFichero;
  |DELINDEX #330;

  |ABRE #330;
  |BUCLE CargoConceptos;
  |CIERRA #330;

  |ABRE #32;
  |LEE 041#32p;
  |SI FSalida ! 0;
      |CIERRA #32;
      eaExtension = "mod";
      eaPrograma  = "dsmom032";
      |SUB_EJECUTA_NP ":basica/dspatron";
  |SINO;
      |CIERRA #32;
  |FINSI;

  |CLS;
  |PINPA #0#32;
  |ENTLINEAL #1#32, -1, 1, 22, 0, ClaveBaja32, ClaveAlta32;

  |DELINDEX #330;
  |DESCARGA_DEF #330;
|FPRO;
