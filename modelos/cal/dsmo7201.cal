|FICHEROS;
     dsmo7201 #1;
     dsmo7202 #2;

     dsmow149 #149;
|FIN;

|VARIABLES;
     aFichero = "";
     nSwChequea = 0;
     nBorra     = 0;
     nCampo     = 0;
     nComp      = 0;
     nImporte   = 0;
     nImpCobro  = 0;
     nHay       = 0;
     nEjerCobro = 0;
     nNegativo  = 0;
     aCampo7    = "";
     nSw        = 0;

     nEjer      = 0;
     nAnyo      = 0;
     nComplem   = 0;
     nLinea     = 0;
     &enSw999   = 0;
|FIN;

|INCLUYE i_variar;

|RUTINA ClaveBaja2;
     #2#0 = #1#0;
     #2#1 = #1#1;
     #2#2 = #1#2;
     #2#3 = #1#3;
     #2#4 = #1#4;
     #2#5 = #1#5;
     #2#6 = 001;
|FRUT;

|RUTINA ClaveAlta2;
     #2#0 = #1#0;
     #2#1 = #1#1;
     #2#2 = #1#2;
     #2#3 = #1#3;
     #2#4 = #1#4;
     #2#5 = #1#5;
     #2#6 = 999;
|FRUT;

|PROCESO Recalcula2;

     |SI nBorra = 1;
         |BORRA 020#2a;
         |LIBERA #2;
         |ACABA;
     |FINSI;

     |SI nBorra = 2;
         nLinea = #2#6;
         |ACABA;
     |FINSI;

     |SI nSwChequea = 0;
         nSwChequea = 1;
         |ERROR10;
         |ACABA;
     |FINSI;

     |HAZ Tipo2mo7202;
|FPRC;

|DEFBUCLE dsmo7202;
     #2#1;
     ;
     #1#0, #1#1, #1#2, #1#3, #1#4, #1#5, 001;
     #1#0, #1#1, #1#2, #1#3, #1#4, #1#5, 999;
     be;
     NULL, Recalcula2;
|FIN;

|PROCESO Recalculo;
         nSwChequea = 0;  |BUCLE dsmo7202;

     |SI nSwChequea = 0;  |ACABA;  |FINSI;

     |PDEFECTO #1, 6,  9;

     |BUCLE dsmo7202;
|FPRC;

|PROCESO Menug;

     |PUSHV 0501 2380;

     |SI enTipoEntrada = 4;
         |ENTLINEAL #1#2, -7, 4, 22, 0, ClaveBaja2, ClaveAlta2;
     |SINO;
         |ENTLINEAL #1#2, -7, 2, 22, 0, ClaveBaja2, ClaveAlta2;
     |FINSI;

     |POPV;
     |HAZ Recalculo;
|FPRC;

|| ////////////////////////////////
|PROCESO Traedsmo7202;
     #2#4 = #1#4;
     |GRABA 020#2n;
     |LIBERA #2;
|FPRC;

|DEFBUCLE dsmo7202T;
     #2#1;
     ;
     #1#0, #1#1, #1#2, #1#3, nComp, #1#5, 001;
     #1#0, #1#1, #1#2, #1#3, nComp, #1#5, 999;
     be;
     NULL, Traedsmo7202;
|FIN;

|PROCESO Justificante;
     |SI #1#4 = 0;  |ACABA;  |FINSI;

     |SI #1#19 ! "C";  |Y #1#19 ! "S";
         |MENAV "      Introduzca Complementaria o Sustitutoria";
         |ERROR;
         |ACABA;
     |FINSI;

     |C_M #1#18, 1, "N";
     |SI #1#19 = "S";  |O #1#19 = "C";
||          |C_M #1#18, 1, "S";

         |SI #1#18 = "             ";
             |ABRE #149;
             #149#0 = #1#0;
             #149#1 = #1#1;
             #149#2 = #1#2;
             #149#3 = #1#3;
             #149#4 = #1#4 - 1;
             |LEE 000#149=;
             |SI FSalida ! 0;  |DDEFECTO #149;  |FINSI;
             |CIERRA #149;

             #1#18 = #149#22;  |PINTA #1#18;
         |FINSI;
         #1#18 = #1#22;  |PINTA #1#18;
     |SINO;
         #1#18 = "";  |PINTA #1#18;
     |FINSI;

     |SI #1#19 = "S";

         |SI #1#6 = 0; |Y #1#7 = 0; |Y #1#8 = 0;
             nComp = #1#4 - 1;
             |BUCLE dsmo7202T;

             |HAZ Recalculo;

             |PINPA #0#1;
             |PINDA #0#1;
             |HAZ PintaDatos;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO BuscoAnterior;
    nEjer    = #149#1;
    nComplem = #149#4;
|FPRC;

|DEFBUCLE BuscoAnterior;
    #149#1;
    ;
    #1#0, 0000,  99, enModelo, 00, 00;
    #1#0, nAnyo, 99, enModelo, 99, 99;
    e;
    NULL, BuscoAnterior;
|FIN;

|PROCESO PaseAnteriorLin;
    |SI #2#31 ! "C";
        nLinea = nLinea + 1;
        #2#1  = enEjer;
        #2#2  = enPeriodo;
        #2#3  = enModelo;
        #2#4  = enComplem;
        #2#6  = nLinea;
        #2#31 = "M";
        |GRABA 040#2n;
    |FINSI;
    |LIBERA #2;
|FPRC;

|DEFBUCLE PaseAnteriorLin;
     #2#1;
     ;
     #149#0, #149#1, #149#2, #149#3, #149#4, #149#5, 001;
     #149#0, #149#1, #149#2, #149#3, #149#4, #149#5, 999;
     be;
     NULL, PaseAnteriorLin;
|FIN;

|PROCESO PaseAnterior;
     nLinea = 0;
     |SI #149#4 = 0; |O #149#19 = "S";
         nBorra = 1;  |BUCLE dsmo7202;
         nBorra = 0;
     |SINO;
         nBorra = 2; |BUCLE dsmo7202;
         nBorra = 0;
     |FINSI;

     |BUCLE PaseAnteriorLin;

     |LIBERA #149;
|FPRC;

|DEFBUCLE PaseAnterior;
    #149#1;
    ;
    #1#0, nEjer, 99, enModelo, 00, 00;
    #1#0, nEjer, 99, enModelo, 99, 99;
    be;
    NULL, PaseAnterior;
|FIN;


|PROCESO Tipo12mo7201;  |TIPO 12;
|FPRC;


|PROGRAMA;
     |HAZ LeeEURODBMOD;

     eaNomDef    = "dsmo7201";
     enResultado = 0;

|| \\\\\\ Alta del Modelo ////////

     |SI enTipoEntrada = 1;
         |CABEZA "Modelo 720";
         |ABRE #1;
         #1#0 = enEmpresa;
         #1#1 = enEjer;
         #1#2 = enPeriodo;
         #1#3 = enModelo;
         #1#4 = enComplem;
         #1#5 = enActividad;
         |LEE 040#1=;
         |SI FSalida ! 0;
             |DDEFECTO #1;
             #1#0 = enEmpresa;
             #1#1 = enEjer;
             #1#2 = enPeriodo;
             #1#3 = enModelo;
             #1#4 = enComplem;
             #1#5 = enActividad;
             |GRABA 020#1b;
         |FINSI;

         |PINPA #0#1;
         |PINDA #0#1;
         |HAZ PintaDatos;

         |SI #1#4 ! 0;
             |C_M #1#19, 1, "S";
             |ENDATOS #2#1;
         |FINSI;

         |HAZ Menug;

         |SI #1#6 = 0; |Y #1#7 = 0; |Y #1#8 = 0;
             |BORRA 020#1a;
             enEntrada = 0;
         |SINO;
             |GRABA 020#1a;
             enEntrada = 1;
         |FINSI;
         |LIBERA #1;
         |CIERRA #1;

         |ACABA;
     |FINSI;

|| \\\\\\ Consulta del Modelo ////////

     |SI enTipoEntrada = 4;              || Consulta del Modelo
         |CABEZA "Modelo 720";
         |ABRE #1;
         #1#0 = enEmpresa;
         #1#1 = enEjer;
         #1#2 = enPeriodo;
         #1#3 = enModelo;
         #1#4 = enComplem;
         #1#5 = enActividad;
         |LEE 040#1=;
         |SI FSalida ! 0;
             |MENAV "      No existe Registro.";
             |CIERRA #1;
             |ACABA;
         |FINSI;

         |PINPA #0#1;
         |PINDA #0#1;
         |HAZ PintaDatos;

         |HAZ Menug;

         |CIERRA #1;

         |ACABA;
     |FINSI;

|| \\\\\\ Baja del Modelo ////////

     |SI enTipoEntrada = 3;              || Baja del Modelo
         |ABRE #1;
         #1#0 = enEmpresa;
         #1#1 = enEjer;
         #1#2 = enPeriodo;
         #1#3 = enModelo;
         #1#4 = enComplem;
         #1#5 = enActividad;
         |LEE 040#1=;
         |SI FSalida ! 0;
             |SI enMensa = 0;
                 |MENAV "      No existe Registro.";
             |FINSI;
             |CIERRA #1;
             |ACABA;
         |FINSI;

         nBorra = 1;  |BUCLE dsmo7202;
         nBorra = 0;

         |BORRA 020#1a;
         |LIBERA #1;

         |CIERRA #1;

         |ACABA;
     |FINSI;

|| \\\\\\ Recoger Datos Anteriores ////////

     |SI enTipoEntrada = 93;
         |ABRE #1;
         #1#0 = enEmpresa;
         #1#1 = enEjer;
         #1#2 = enPeriodo;
         #1#3 = enModelo;
         #1#4 = enComplem;
         #1#5 = enActividad;
         |LEE 040#1=;
         |SI FSalida ! 0;
             |DDEFECTO #1;
             #1#0 = enEmpresa;
             #1#1 = enEjer;
             #1#2 = enPeriodo;
             #1#3 = enModelo;
             #1#4 = enComplem;
             #1#5 = enActividad;
             |GRABA 020#1b;
         |FINSI;

         nEjer     = 0;
         nComplem = -1;
         nAnyo = enEjer - 1;
         |BUCLE BuscoAnterior;
         |SI nEjer ! 0;
             |BUCLE PaseAnterior;

             |HAZ Recalculo;
             |GRABA 020#1a;
         |SINO;
             |MENAV "    No existe Declaracion Anterior";
         |FINSI;

         |LIBERA #1;
         |CIERRA #1;

         |ACABA;
     |FINSI;

|| \\\\\\ Recalculo totales registro 1

     |SI enTipoEntrada = 92;
         |ABRE #1;
         #1#0 = enEmpresa;
         #1#1 = enEjer;
         #1#2 = enPeriodo;
         #1#3 = enModelo;
         #1#4 = enComplem;
         #1#5 = 0;
         |LEE 040#1=;
         |SI FSalida = 0;

             |HAZ Recalculo;
             |GRABA 020#1a;
         |FINSI;
         |LIBERA #1;
         |CIERRA #1;

         |ACABA;
     |FINSI;

|| \\\\\\ Comprobacion Existencia del Modelo

     |SI enTipoEntrada = 99;              || Existencia del Modelo ????
         |ABRE #1;
         #1#0 = enEmpresa;
         #1#1 = enEjer;
         #1#2 = enPeriodo;
         #1#3 = enModelo;
         #1#4 = enComplem;
         #1#5 = enActividad;
         |LEE 040#1=;
         |SI FSalida ! 0;  enExistencia = 1;  |FINSI;
         |CIERRA #1;

     |FINSI;
|FPRO;

|PROCESO Tipo88;  |TIPO 88;
     |HAZ Relaciones;
|FPRC;
