|FICHEROS;
     dsmoz069 #0;
     dsmom004 #4;
     :/basica/def/agifigen #118;

     dsmom030 #30;
     dsmom072 #72;

     dsmow129 #999,MANTE;
     dsmow144 #944,MANTE;

     &regisnif@ #709;
|FIN;

|VARIABLES;
     nOpera     = 0;
     nCampo     = 0;
     fLaFecha   = @;
     aAlfa1     = "";
     nPeriodo   = 0;
     nSwM72     = 0;
     nSwMod340  = 0;
     nSwMod347  = 0;
     aClave     = "";
     nDEmpresa  = 0;
     nHEmpresa  = 0;
     aDClave    = "";
     aHClave    = "";
     nModelo    = 0;
     nModo      = 0;
     nInkey     = 0;
     aFichero   = "";

     &enOpCobro  = 0;
     &enEjerCob  = 0;
     &enImpoCob  = 0;
     &eaClCobro  = "";
     &eaNifCobro = "";
     &eaSeguro   = "";
     &eaArrenda  = "";
     &enSwCobro  = 0;
     nModificado = 0;
|FIN;

|INCLUYE i_variar;

|| /////////////////////// PROCESOS ENTRADA POR PANTALLA \\\\\\\\\\\\\\\\\\\\
|PROCESO Tipo7;  |TIPO 7;
     |SI enEmpresa ! 0;
         #0#0 = enEmpresa;  |PINTA #0#0;
         #0#1 = enEmpresa;  |PINTA #0#1;

         |C_M #0#0, 1, "N";
         |C_M #0#1, 1, "N";
     |FINSI;
|FPRC;

|PROCESO Parrilla;
     |SI #0#0 = 0;
         #0#0 = 0;  |PINTA #0#0;
         #0#1 = 0;  |PINTA #0#1;  |C_M #0#1, 1, "N";
         |PARA nCampo = 2;  |SI nCampo [ 13;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |SINO;
         |SI enEmpresa = 0;
             |C_M #0#0, 1, "S";
             |C_M #0#1, 1, "S";
         |FINSI;

         |PARA nCampo = 2;  |SI nCampo [ 13;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "N";
               #0nCampo = 0;  |PINTA #0nCampo;
         |SIGUE;
     |FINSI;
|FPRC;

|PROCESO NoMasEmpresas;
     |SI #0Campo = 0;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 13;  |HACIENDO nCampo = nCampo + 1;
               #0nCampo = 0;  |C_M #0nCampo, 1, "N";
         |SIGUE;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 13;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |FINSI;
|FPRC;

|CALCULO Eano; |TIPO 7;
 fLaFecha = ~;
 aAlfa1 = fLaFecha; aAlfa1 = aAlfa1 % -104;
 #0#14 = aAlfa1;
 #0#14 = #0#14 - 1;
 |PINTA #0#14;
|FCAL;

|| ///////////////////////////////////////////////////////////////////
|| **********************************************************************
|PROCESO VerModelo;
     enComplem = -1;
     |ABRE #4;
     #4#0 = enEmpresa;
     #4#1 = enEjer;
     #4#2 = nPeriodo;
     #4#3 = nModelo;
     #4#4 = 99;
     |LEE 040#4];
     |SI FSalida = 0;
         |LEE 040#4a;
     |SINO;
         |LEE 040#4u;
     |FINSI;
     |SI FSalida = 0; |Y #4#0 = enEmpresa; |Y #4#1 = enEjer; |Y #4#3 = nModelo;
         enComplem = #4#4;
     |FINSI;
     |CIERRA #4;
|FPRC;

|PROCESO MiraModelos;
     nSwMod340 = 0;
     nPeriodo = 03; nModelo = 340; |HAZ VerModelo;
     |SI enComplem ! -1;  nSwMod340 = 1; |FINSI;

     nSwMod347 = 0;
     nPeriodo = 99; nModelo = 347; |HAZ VerModelo;
     |SI enComplem ! -1;
         nSwMod347 = 1;
         |SI #4#7 = "X"; |Y #4#9 ! "X";
             nSwMod347 = 2;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO Lee_m072;
  |SI nOpera = 0;
      nSwM72 = 1;
      |ERROR10;
  |FINSI;

  |SI nOpera = 1;
      #944#0 = #72#0;
      #944#1 = #72#1;
      #944#2 = #72#2;
      #944#3 = #72#3;
      #944#4 = #72#4;
      #944#5 = #72#5;
      #944#6 = #72#6;
      #944#7 = #72#7;
      eaCodNif = #72#3;  |HAZ LeeNifs;
      #944#8 = #709#1;
      |GRABA 020#944n;
  |FINSI;

  |SI nOpera = 2;
      |BORRA 020#72a;
      |LIBERA #72;
  |FINSI;
  nSwM72 = 1;
|FPRC;

|DEFBUCLE Lee_m072;
   #72#1;
   ;
   enEmpresa, enEjer, aClave, "            ", " ", " ", " ", " ", " ", 2008;
   enEmpresa, enEjer, aClave, "zzzzzzzzzzzz", "z", "z", "z", "z", "z", 2099;
   e;
   NULL, Lee_m072;
|FIN;

|PROCESO GrabaAuxiliar;
   #999#0 = enEmpresa;
   #999#1 = enEjer;
   #999#2 = aClave;
   #999#3 = nSwM72;
   #999#4 = nSwMod340;
   #999#5 = nSwMod347;
   #999#6 = enComplem;
   #999#7 = #118#1;
   |GRABA 020#999b;

   nOpera = 1; |BUCLE Lee_m072;
|FPRC;

|PROCESO  LeeAgifigen;
     enEmpresa   = #118#0;
     enEjer      = #0#14;

     nSwM72 = 0;
     nOpera = 0;
     |SI #0#15 ! "C"; aClave = "A"; |BUCLE Lee_m072; |FINSI;
     |SI #0#15 ! "P"; aClave = "B"; |BUCLE Lee_m072; |FINSI;

     |HAZ MiraModelos;

     |SI nSwM72 = 1; |O nSwMod347 ! 0; |O nSwMod340 ! 0;
         |SI nSwM72 = 1; |O #0#16 = "S";
             |SI #0#15 ! "C"; aClave = "A"; |HAZ GrabaAuxiliar; |LIBERA #999; |FINSI;
             |SI #0#15 ! "P"; aClave = "B"; |HAZ GrabaAuxiliar; |LIBERA #999; |FINSI;
         |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE LeeAgifigen;
     #118#1;
     ;
     nDEmpresa;
     nHEmpresa;
     e;
     NULL, LeeAgifigen;
|FIN;

|| ///////////////////////////////////////////////////////////////////
|| ... Mantenimiento w144
|PROCESO MiraDnimow144;  |TIPO 6;
     eaVarDni  = #944Campo;
     enCalcCif = 0;
     |HAZ CalculameDNI;
     eaVarDni = (eaVarDni + "             ") % 112;
     |SI #944Campo ! eaVarDni;
         |CONFI "0000SEl Nif puede ser Erroneo. Desea Calcularlo.";
         |SI FSalida = 0;
             #944Campo = eaVarDni;  |PINTA #944Campo;
             |ERROR;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO PonDatosNifw144;  |TIPO 0;
     eaCodNif  = #944#3;
     |HAZ LeeNifs;
     #944#8 = #709#1;   |PINTA #944#8;
|FPRC;

|PROCESO Tipo0w144; |TIPO 0;
|FPRC;

|PROCESO Tipo1w144; |TIPO 1;
  nModo = (FEntrada / 100) ! 0;
  |SI nModo = 3;
      |CONFI "    NEsta Seguro ";
      |SI FSalida ! 0;
          |ERROR;
          |ACABA;
      |FINSI;
      #944#7 = 0;
      |HAZ Actualiza;
 |FINSI;
|FPRC;

|PROCESO Tipo11w144; |TIPO 11;
   nInkey = FSalida;
   |SI FSalida = 10;
       enEmpresa  = #944#0;
       enEjerCob  = #944#6;
       eaClCobro  = #944#2;
       eaNifCobro = #944#3;
       eaSeguro   = #944#4;
       eaArrenda  = #944#5;
       enImpoCob  = #944#7;
       enOpCobro  = 1;
       |SUB_EJECUTA_NP "dsmom072";
       |ERROR;
   |FINSI;
|FPRC;

|PROCESO Actualiza;
    |ABRE #72;
    #72#0 = #944#0;
    #72#1 = #944#1;
    #72#2 = #944#2;
    #72#3 = #944#3;
    #72#4 = #944#4;
    #72#5 = #944#5;
    #72#6 = #944#6;
    |LEE 141#72=;
    |SI FSalida ! 0;
        |SI #944#7 ! 0;
            |DDEFECTO #72;
            #72#0 = #944#0;
            #72#1 = #944#1;
            #72#2 = #944#2;
            #72#3 = #944#3;
            #72#4 = #944#4;
            #72#5 = #944#5;
            #72#6 = #944#6;
            |GRABA 020#72b;
       |SINO;
            |LIBERA #72;
            |CIERRA #72;
            |ACABA;
       |FINSI;
    |FINSI;

    #72#7 = #944#7;
    |SI #72#7 ! 0;
        |GRABA 020#72a;
    |SINO;
        |BORRA 020#72a;
    |FINSI;
    |LIBERA #72;
    |CIERRA #72;
    nModificado = 1;
|FPRC;

|| ///////////////////////////////////////////////////////////////////
|| ... Mantenimiento w129
|CALCULO pintaclave; |TIPO 0;
  |SI #999#2 = "A";
      aAlfa1 = "PAGOS ";
  |SINO;
      aAlfa1 = "COBROS";
  |FINSI;
  |ATRI R; |PINTA 0662, aAlfa1; |ATRI 0;
|FCAL;

|CALCULO LeelaEmpresa; |TIPO 0;
  || Lee Empresa
    |ABRE #118;
    #118#0 = #999#0;
    |LEE 041#118=;
    |SI FSalida ! 0;
        |MENAV "     Empresa Inexistente";
        |CIERRA #118;
        |ERROR;
        |ACABA;
    |FINSI;
    #999#7 = #118#1; |PINTA #999#7;
    |CIERRA #118;

/*
    |LEE 010#999=;
    |SI FSalida ! 0;
        nSwM72 = 0;
        nOpera = 0;
        aClave = #999#2; |BUCLE Lee_m072;
        |HAZ MiraModelos;

        aClave = #999#2; |HAZ GrabaAuxiliar;
        |ERROR;

        |PTEC 824;
    |FINSI;
*/
|FCAL;

|RUTINA ClaveBaja44;
     #944#0 = #999#0;
     #944#1 = #999#1;
     #944#2 = #999#2;
     #944#3 = "            ";
     #944#4 = " ";
     #944#5 = " ";
     #944#6 = 2008;
|FRUT;

|RUTINA ClaveAlta44;
     #944#0 = #999#0;
     #944#1 = #999#1;
     #944#2 = #999#2;
     #944#3 = "zzzzzzzzzzzz";
     #944#4 = "z";
     #944#5 = "z";
     #944#6 = enEjer;
|FRUT;

|PROCESO Tipo7w129;  |TIPO 7;
     |MENSA "    <F2> Entrar Imp.Metalico, <F3> Borrar Imp.Metalico, <F4> Informe"

     |SI #0#15 = "C"; aClave = "B"; |FINSI;
     |SI #0#15 = "P"; aClave = "A"; |FINSI;
     #999#2 = aClave; |PINTA #999#2;
     #999#1 = #0#14;  |PINTA #999#1;

     enEmpresa   = #999#0;
     |ENTLINEAL #1#944, -4, 7, 22, 0, ClaveBaja44, ClaveAlta44;
|FPRC;

|PROCESO Borrarw144;
    |BORRA 020#944a;
|FPRC;

|DEFBUCLE Borrarw144;
  #944#1:
  ;
  #999#0, #999#1, #999#2, "            ", " ", " ", " ", " ", " ", 2008;
  #999#0, #999#1, #999#2, "zzzzzzzzzzzz", "z", "z", "z", "z", "z", 2099;
  ;
  NULL, Borrarw144;
|FIN;

|PROCESO ModiModelo;
  |SI nModificado = 1; |Y enEjer > 2010;
      |SI #999#5 = 1;
          |MENAV "    El Modelo 347 no se ha modificado por estar impreso ";
          |ACABA;
      |FINSI;
      |SI #999#5 = 2;
          enEmpresa = #999#0;
          enEjer    = #999#1;
          |SUB_EJECUTA "dsmoz068;modi";
          |PULSATECLA;
     |FINSI;
  |FINSI;
|FPRC;

|PROCESO Tipo11w129;  |TIPO 11;
    nInkey = FSalida;

    |SI nInkey =  10;
       nModificado = 0;
       enEmpresa   = #999#0;
       |ENTLINEAL #1#944, -4, 2, 22, 0, ClaveBaja44, ClaveAlta44;
       |HAZ ModiModelo;
    |FINSI;

    |SI nInkey = 11;

        |LEE 141#999=;

        |CONFI "    NEsta Seguro de Borrar todos las Operaciones en Metalico de esta Empresa ";
        |SI FSalida = 0;
            nOpera = 2; |BUCLE Lee_m072; nOpera = 0;
            |BUCLE Borrarw144;
            nModificado = 1;
            |HAZ ModiModelo;
        |FINSI;

        |LIBERA #999;
        |PONCONTROL  23, "SI";
    |FINSI;

    |SI nInkey = 12;
         enEmpresa = #999#0;
         enEjer    = #999#1;
         eaClCobro = #999#2;
         |SUB_EJECUTA_NP "dsmoy075";
         |ERROR;
    |FINSI;

|FPRC;
|| ///////////////////////////////////////////////////////////////

|RUTINA ClaveBaja999;
     #999#1 = #0#14;
     #999#2 = aDClave;
     #999#0 = 1;
|FRUT;

|RUTINA ClaveAlta999;
     #999#1 = #0#14;
     #999#2 = aHClave;
     #999#0 = 99999;
|FRUT;

|PROCESO ITipo3;  |TIPO 3;
     |SI #0#15 = "A";
         |C_M #999#2,1,"S";
         aDClave = " ";
         aHClave = "z";
     |SINO;
         |C_M #999#2,1,"N";
         |SI #0#15 = "C"; aDClave = "B"; |FINSI;
         |SI #0#15 = "P"; aDClave = "A"; |FINSI;
         aHClave = aDClave;
     |FINSI;

     aFichero = ("5z" + Usuario) % 108;
     |NOME_DAT #999 aFichero; |DELINDEX #999;

     aFichero = ("4z" + Usuario) % 108;
     |NOME_DAT #944 aFichero; |DELINDEX #944;

     |ABRE #999;
     |ABRE #944;
     |SI #0#0 ! 0;
          nDEmpresa = #0#0;
          nHEmpresa = #0#1;
          |BUCLE LeeAgifigen;
      |SINO;
          |PARA nCampo = 2;  |SI nCampo [ 13;  |HACIENDO nCampo = nCampo + 1;
                nDEmpresa = #0nCampo;
                nHEmpresa = #0nCampo;
                |BUCLE LeeAgifigen;
          |SIGUE;
     |FINSI;
     |CIERRA #944;
     |CIERRA #999;

     |PINPA #0#999;
     |ENTLINEAL #1#999, -3, 4, 22, 0, ClaveBaja999, ClaveAlta999;
     |DELINDEX #999;
|FPRC;

|PROCESO Tipo88;  |TIPO 88;
     |HAZ Relaciones;
|FPRC;
