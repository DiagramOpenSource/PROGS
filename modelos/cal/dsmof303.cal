|FICHEROS;
     dsmof303 #0;

     &clientes@ #715;

     :/dsarchi/def/dsarm001;
     :/dsarchi/def/dsarm002;
     :/dsarchi/def/dsarm005;
|FIN;

|VARIABLES;
     nVent        = 0;
     nContenido   = 0;
     nPanta       = 0;
     nID          = 0;
     nCampo       = 0;
     nSalir       = 0;

     nBtn1        = 0;
     nBtn2        = 0;
     nBtn3        = 0;
     nBtn4        = 0;
     nBtn5        = 0;
     nBtn6        = 0;
     nBtn7        = 0;
     nBtn8        = 0;
     nBtn9        = 0;
     nBtn10       = 0;
     nBtn11       = 0;
     nBtn12       = 0;
     nBtn13       = 0;
     nBtn14       = 0;

     aAlfa        = "";
     aAlfa1       = "";
     aAlfa2       = "";
     aAlfa3       = "";
     aAlfa4       = "";
     aAlfa5       = "";
     aTitulo      = "";
     aEjecuta     = "";
     aParametro   = "";

  {10}aPopup      = "";

     {-1}aVent    = "";
         aVent1   = 0;
         aVent2   = 0;
         aVent3   = 0;
         aVent4   = 0;
         aVent5   = "";


     &enGrupo            = 0;
     &enSubGrupo         = 0;
     &enTipo             = 0;
     &enGrupoTres        = 0;
     &enSubGTres         = 0;
     &enTipoTres         = 0;
     &eaNombre           = "";
     &eaCodigo           = "";
     &eaNif              = "";
     &enCodCli           = 0;
     &enDesdeCapturador  = 1;
     &enPrevisualizar    = 1;
     &eaCarpetaOrigen    = "";
     &eaDocCaptura       = "";
     &eaUsuArchivado     = "";
     &eaAutoSelPlantilla = "";
     &eaUnidadBasico     = "";
     &enNumRegistro      = 0;
|FIN;

|PROCESO Tipo15Fm5;
|FPRC;

|PROCESO Tipo16Fm5;
|FPRC;

|PROCESO Tipo17Fm5;
|FPRC;

|PROCESO Tipo12Fmof303;  |TIPO 12;
|FPRC;

|PROCESO CalculaCuota;
     |SI nCampo = 6;  |O nCampo = 7;  #0#8  = #0#6  % #0#7;  |PINTA #0#8;  |FINSI;
     |SI nCampo = 9;  |O nCampo = 10; #0#11 = #0#9  % #0#10; |PINTA #0#11; |FINSI;
     |SI nCampo = 12; |O nCampo = 13; #0#14 = #0#12 % #0#13; |PINTA #0#14; |FINSI;

     |SI nCampo = 15; |O nCampo = 16; #0#17 = #0#15 % #0#16; |PINTA #0#17; |FINSI;
     |SI nCampo = 18; |O nCampo = 19; #0#20 = #0#18 % #0#19; |PINTA #0#20; |FINSI;
     |SI nCampo = 21; |O nCampo = 22; #0#23 = #0#21 % #0#22; |PINTA #0#23; |FINSI;

     |SI nCampo = 24; |O nCampo = 25; #0#26 = #0#24 % #0#25; |PINTA #0#26; |FINSI;

     |SI nCampo = 30; |O nCampo = 31; #0#32 = #0#30 % #0#31; |PINTA #0#32; |FINSI;
     |SI nCampo = 33; |O nCampo = 34; #0#35 = #0#33 % #0#34; |PINTA #0#35; |FINSI;
     |SI nCampo = 36; |O nCampo = 37; #0#38 = #0#36 % #0#37; |PINTA #0#38; |FINSI;

     |SI nCampo = 39; |O nCampo = 40; #0#41 = #0#39 % #0#40; |PINTA #0#41; |FINSI;
     |SI nCampo = 42; |O nCampo = 43; #0#44 = #0#42 % #0#43; |PINTA #0#44; |FINSI;

     |SI nCampo = 45; |O nCampo = 46; #0#47 = #0#45 % #0#46; |PINTA #0#47; |FINSI;

     |SI nCampo = 48; |O nCampo = 49; #0#50 = #0#48 % #0#49; |PINTA #0#50; |FINSI;
|FPRC;

|PROCESO TotalFmof303;
     nContenido = Contenido;
     |SI #0Campo = Contenido;  |ACABA;  |FINSI;

     nCampo = Campo;
     |HAZ CalculaCuota;
|FPRC;

|PROCESO TiposIVA;
     nID   = FSalida;

     aPopup1  = nID;  || Si fuera -1 en la posicion
     aPopup2  = 4;
     aPopup3  = " ";

     |SI nID = nBtn4;  |O nID = nBtn5;  |O nID = nBtn6;
         aPopup4  = "05.20";
         aPopup5  = "01.40";
         aPopup6  = "00.50";

         |SI #0#1 = 2012;  |Y #0#2 [ 8;
             aPopup4  = "04.00";
             aPopup5  = "01.00";
             aPopup6  = "00.50";
         |FINSI;

         |SI #0#1 = 2012;  |Y #0#2 = 9;
             aPopup2  = 6;
             aPopup4  = "04.00";
             aPopup5  = "01.00";
             aPopup6  = "00.50";
             aPopup7  = "05.20";
             aPopup8  = "01.40";
         |FINSI;
     |SINO;
         aPopup4  = "21.00";
         aPopup5  = "10.00";
         aPopup6  = "04.00";

         |SI #0#1 = 2012;  |Y #0#2 [ 8;
             aPopup4  = "18.00";
             aPopup5  = "08.00";
             aPopup6  = "04.00";
         |FINSI;

         |SI #0#1 = 2012;  |Y #0#2 = 9;
             aPopup2  = 6;
             aPopup4  = "18.00";
             aPopup5  = "08.00";
             aPopup6  = "04.00";
             aPopup7  = "21.00";
             aPopup8  = "10.00";
         |FINSI;
     |FINSI;

     IaPopup  = aPopup1  <-;
     |ESPECIFICA "TRACKPOPUP", aPopup;
     |SI FSalida ! 0;
         IaPopup  = aPopup3  <-;
         IaPopup  = IaPopup + (FSalida - 1);

         aAlfa  = aPopup;
         nCampo = 0;

         |SI nID = nBtn1;  #0#7  = aAlfa;  nCampo = 7;  |FINSI;
         |SI nID = nBtn2;  #0#10 = aAlfa;  nCampo = 10; |FINSI;
         |SI nID = nBtn3;  #0#13 = aAlfa;  nCampo = 13; |FINSI;
         |SI nID = nBtn4;  #0#16 = aAlfa;  nCampo = 16; |FINSI;
         |SI nID = nBtn5;  #0#19 = aAlfa;  nCampo = 19; |FINSI;
         |SI nID = nBtn6;  #0#22 = aAlfa;  nCampo = 22; |FINSI;
         |SI nID = nBtn7;  #0#25 = aAlfa;  nCampo = 25; |FINSI;
         |SI nID = nBtn8;  #0#31 = aAlfa;  nCampo = 31; |FINSI;
         |SI nID = nBtn9;  #0#34 = aAlfa;  nCampo = 34; |FINSI;
         |SI nID = nBtn10; #0#37 = aAlfa;  nCampo = 37; |FINSI;
         |SI nID = nBtn11; #0#40 = aAlfa;  nCampo = 40; |FINSI;
         |SI nID = nBtn12; #0#43 = aAlfa;  nCampo = 43; |FINSI;
         |SI nID = nBtn13; #0#46 = aAlfa;  nCampo = 46; |FINSI;
         |SI nID = nBtn14; #0#49 = aAlfa;  nCampo = 49; |FINSI;

         |SI nCampo ! 0;
             |PINTA #0nCampo;
             |HAZ CalculaCuota;

             |POSICIONATE #0nCampo;  |PTEC 808;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO EmiteEnCrystal;
     |SI aParametro = "";
         Impresora = "CrystalReport";
     |SINO;
         Impresora = "CrystalReport;" + aParametro;
     |FINSI;

     |IMPRE -1;
     |SI FSalida ! 0;
         |MENAV "0000 No tiene instalada Impresora CrystalReport, pongase en contacto con su Administrador";
         |ACABA;
     |FINSI;

     |INFOR "dsmof303";
     |SI FSalida ! 0;
         |FINIMP;
         |ACABA;
     |FINSI;

     enCodCli = #0#0;
     |HAZ LeePersona;

     eaCodigo = ("00000" + #0#0) % -105;
     eaNif    = #715#3;

     |PRINT;
     |PIEINF;
     |FININF;
     |FINIMP;
|FPRC;

|PROCESO EnviaGAD;
     |HAZ LeeUnidad;

     eaCarpetaOrigen = eaUnidadBasico + "\MODELOS";        |RMKDIR eaCarpetaOrigen;
     eaCarpetaOrigen = eaCarpetaOrigen + "\" + #0#3;       |RMKDIR eaCarpetaOrigen;
     eaCarpetaOrigen = eaCarpetaOrigen + "\" + #0#1;       |RMKDIR eaCarpetaOrigen;
     eaCarpetaOrigen = eaCarpetaOrigen + "\" + (("00" + #0#2) % -102);  |RMKDIR eaCarpetaOrigen;
     eaCarpetaOrigen = eaCarpetaOrigen + "\";

     |SI #dsarm001#8 = "S";
          enGrupoTres = 0;
          enSubGTres  = 0;
          enTipoTres  = 0;
          aEjecuta = ":dsarchi/dsarz003;DAMETRE";
          |SUB_EJECUTA_NP aEjecuta;
          |SI enGrupoTres = 0;  |ACABA;  |FINSI;

          enGrupo    = enGrupoTres;
          enSubGrupo = enSubGTres;
          enTipo     = enTipoTres;
     |SINO;
          |ABRE #dsarm002;
          |CONSULTA_CLAVE #1#dsarm002;
          |SI FSalida = 0;
              enGrupo    = #dsarm002#0;
              enSubGrupo = 1;
              enTipoTres = 1;
          |FINSI;
          |CIERRA #dsarm002;
     |FINSI;

     eaDocCaptura  = eaCarpetaOrigen + "formulario303.pdf";
     aParametro    = eaDocCaptura;
     |HAZ EmiteEnCrystal;

     enNumRegistro      = 0;
     enDesdeCapturador  = 1;
     enPrevisualizar    = 0;
     eaUsuArchivado     = Usuario % 810;      |QBF eaUsuArchivado;
     eaAutoSelPlantilla = "N";
     aEjecuta           = ":dsarchi/dsarz001;CAPTURAESTEDOC";
     |SUB_EJECUTA_NP aEjecuta;

     |SI enNumRegistro ! 0;
         |ABRE #dsarm005;
         #dsarm005#0 = enNumRegistro;
         |LEE 101#dsarm005.=;
         |SI FSalida ! 0;  |CIERRA #dsarm005;  |ACABA;  |FINSI;

         #dsarm005#8  = #0#0;
         #dsarm005#10 = #715#3;
         #dsarm005#14 = #715#1;
         #dsarm005#17 = #0#1;
         #dsarm005#18 = #0#2;
         #dsarm005#19 = #0#3;
         #dsarm005#20 = #0#4;
         #dsarm005#21 = "FORMULARIO 303";
         #dsarm005#38 = #715#1;

         |SI #0#2 = 1;   #dsarm005#22 = "ENERO     ";  |FINSI;
         |SI #0#2 = 2;   #dsarm005#22 = "FEBRERO   ";  |FINSI;
         |SI #0#2 = 3;   #dsarm005#22 = "MARZO     ";  |FINSI;
         |SI #0#2 = 4;   #dsarm005#22 = "ABRIL     ";  |FINSI;
         |SI #0#2 = 5;   #dsarm005#22 = "MAYO      ";  |FINSI;
         |SI #0#2 = 6;   #dsarm005#22 = "JUNIO     ";  |FINSI;
         |SI #0#2 = 7;   #dsarm005#22 = "JULIO     ";  |FINSI;
         |SI #0#2 = 8;   #dsarm005#22 = "AGOSTO    ";  |FINSI;
         |SI #0#2 = 9;   #dsarm005#22 = "SEPTIEMBRE";  |FINSI;
         |SI #0#2 = 10;  #dsarm005#22 = "OCTUBRE   ";  |FINSI;
         |SI #0#2 = 11;  #dsarm005#22 = "NOVIEMBRE ";  |FINSI;
         |SI #0#2 = 12;  #dsarm005#22 = "DICIEMBRE ";  |FINSI;
         |SI #0#3 = 99;  #dsarm005#22 = "ANUAL     ";  |FINSI;

         |GRABA 020#dsarm005.a;
         |LIBERA #dsarm005;

         |CIERRA #dsarm005;

         aAlfa = "0000Formulario en el GAD con el n£mero de registro " + enNumRegistro;
         |MENAV aAlfa;
     |FINSI;
|FPRC;


|PROCESO BtnEmitir;
     nSalir = 1;
     |PTEC 806;
|FPRC;

|PROCESO BtnEnviaGAD;
     nSalir = 2;
     |PTEC 806;
|FPRC;


|PROGRAMA;
     |ABRE #dsarm001;
     |LEE 000#dsarm001.p;
     |SI FSalida ! 0;  |DDEFECTO #dsarm001;  |FINSI;
     |CIERRA #dsarm001;

     |DBASS aAlfa;
     aAlfa = aAlfa + "dsarchi/fich/";
     |PATH_DAT #dsarm002#aAlfa#;
     |PATH_DAT #dsarm005#aAlfa#;

     aAlfa1   = PARAMETRO % 105;
     aAlfa2   = PARAMETRO % 604;
     aAlfa3   = PARAMETRO % 1002;
     aAlfa4   = PARAMETRO % 1203;
     aAlfa5   = PARAMETRO % 1502;
     eaNombre = PARAMETRO % 1740;

     aTitulo = "Empresa: " + aAlfa1;
     aTitulo = aTitulo + " Ejercicio: " + aAlfa2;
     aTitulo = aTitulo + " Periodo: " + aAlfa3;
     aTitulo = aTitulo + " Complementaria: " + aAlfa5;
     aTitulo = aTitulo + " Nombre: " + eaNombre;

     |VENTANA 0501, 3499, -1, 16, aTitulo;
     nVent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |ABRE  #0;
     #0#0  = aAlfa1;
     #0#1  = aAlfa2;
     #0#2  = aAlfa3;
     #0#3  = aAlfa4;
     #0#4  = aAlfa5;
     #0#5  = 0;
     |LEE 101#0=;
     |SI FSalida ! 0;
         |DDEFECTO #0;
         #0#0  = aAlfa1;
         #0#1  = aAlfa2;
         #0#2  = aAlfa3;
         #0#3  = aAlfa4;
         #0#4  = aAlfa5;
         #0#5  = 0;
         |GRABA 020#0n;
         |LIBERA #0;
     |FINSI;

     |PINPA #0#0;  nPanta = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,{{197}}", 0835, 0836, TiposIVA; nBtn1  = FSalida;
     |CONTROL_SIMPLE nPanta, "BOTON,{{197}}", 0935, 0936, TiposIVA; nBtn2  = FSalida;
     |CONTROL_SIMPLE nPanta, "BOTON,{{197}}", 1035, 1036, TiposIVA; nBtn3  = FSalida;
     |CONTROL_SIMPLE nPanta, "BOTON,{{197}}", 1235, 1236, TiposIVA; nBtn4  = FSalida;
     |CONTROL_SIMPLE nPanta, "BOTON,{{197}}", 1335, 1336, TiposIVA; nBtn5  = FSalida;
     |CONTROL_SIMPLE nPanta, "BOTON,{{197}}", 1435, 1436, TiposIVA; nBtn6  = FSalida;
     |CONTROL_SIMPLE nPanta, "BOTON,{{197}}", 1635, 1636, TiposIVA; nBtn7  = FSalida;
     |CONTROL_SIMPLE nPanta, "BOTON,{{197}}", 0884, 0885, TiposIVA; nBtn8  = FSalida;
     |CONTROL_SIMPLE nPanta, "BOTON,{{197}}", 0984, 0985, TiposIVA; nBtn9  = FSalida;
     |CONTROL_SIMPLE nPanta, "BOTON,{{197}}", 1084, 1085, TiposIVA; nBtn10 = FSalida;
     |CONTROL_SIMPLE nPanta, "BOTON,{{197}}", 1284, 1285, TiposIVA; nBtn11 = FSalida;
     |CONTROL_SIMPLE nPanta, "BOTON,{{197}}", 1384, 1385, TiposIVA; nBtn12 = FSalida;
     |CONTROL_SIMPLE nPanta, "BOTON,{{197}}", 1684, 1685, TiposIVA; nBtn13 = FSalida;
     |CONTROL_SIMPLE nPanta, "BOTON,{{197}}", 2084, 2085, TiposIVA; nBtn14 = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,{{197}}Emitir formulario", 3402, 3420, BtnEmitir;
     |CONTROL_SIMPLE 0, "BOTON,{{197}}Enviar al GAD", 3422, 3435, BtnEnviaGAD;

     |PARA;  |SI;   |HACIENDO;
          nSalir = 0;
          |LEE 101#0=;

          |PINDA #0#0;
          |ENDATOS #1#0;
          |SI FSalida = 0;
              |GRABA 020#0a;
          |FINSI;
          |LIBERA #0;

          |SI nSalir = 0;  |SAL;  |FINSI;

          |SI nSalir = 1;
              aParametro = "";
              |HAZ EmiteEnCrystal;
          |FINSI;

          |SI nSalir = 2;
              |HAZ EnviaGAD;
          |FINSI;
     |SIGUE;

     |CIERRA #0;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA nVent;
|FPRO;
