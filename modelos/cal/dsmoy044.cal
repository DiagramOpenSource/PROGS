|FICHEROS;
   dsmoy044 #0;
   dsmom041 #41;
   dsmom100 #100;

   dsmow030 #900;
   dsmow043 #999;      || Temporal de Empresas Contables
   dsmow106 #906;

   :/contagen/def/cacuenta #300;
   :/contagen/def/caconimp #306;  || configuracion impresiones
|FIN;

|VARIABLES;
  fecsys     = @;
   ElPathPan = "";
   nGTempo   = 0;
   Cont      = 0;
   DCuenta   = "";
   HCuenta   = "";

   Parte1 = 0;
   Parte2 = 0;
   Part1  = "";
   Part2  = "";
   Calc = 0;
   NumLins = 66;
   &TotElem = 0;
   &CabCta = 0;
   &LinEle = 0;
   &PieEle = 0;
   &NewPage = 0;
   &LinBlanco = 0;
   &ContLin = 0;
   &aCtaNu = "";
   &aCtaNo = "";
   &nCtaSa = 0;
   &eaEmp  = "";

   aFichero  = "";
   nCampo    = 0;
   nDEmpresa = 0;
   nHEmpresa = 0;
   nDActividad = 0;
   nHActividad = 0;
   swCuenta = 0;
   nSwPrimero = 0;

   aAlfa1     = "";
   aAlfa2     = "";
   aPath      = "";
   aBase      = "";
   nCanal     = 0;
   aVariable  = "";
   &nCamSel = 38;
   &nCamTip = 51;

   nLaEmpresa = 0;
   nEjer      = 0;
   &nSwLinea  = 0;
|FIN;

|INCLUYE i_variar;
|INCLUYE i_varcon;
|INCLUYE i_varlib;
|INCLUYE i_selcon;

|| //////////////////// Calculos de Pantalla \\\\\\\\\\\\\\\\\\\\\\\\\\

|PROCESO DatosEmp; |TIPO 7;

   |DDEFECTO #306;

   |ABRE #100;
   |LEE 040#100p;
   |SI FSalida = 0;
       aAlfa1 = #100#1; |QBF aAlfa1; aAlfa1 = ("00000" + aAlfa1) % -105;
       aAlfa2 = #100#2; |QBF aAlfa2; aAlfa2 = ("0"     + aAlfa2) % -101;
       |DBASS aPath; |QBT aPath;
       aPathConta = aPath + "contagen/fich/" + aAlfa1 + aAlfa2 + "/";

       |PATH_DAT #306 aPathConta;

       |ABRE #306;
       #306#0 = 1;
       |LEE 000#306=;
       |CIERRA #306;
   |FINSI;
   |CIERRA #100;

   #0#52  = #306#7; |PINTA #0#52;
   aAlfa1 = #306#8; |C_M #0#52,1,aAlfa1;
|FPRC;

|CALCULO defecto; |TIPO 7;
   fecsys = ~;
   aAlfa1 = fecsys % -104;
   #0#53 = aAlfa1;
   |PINTA #0#53;
|FCAL;

|CALCULO Tipo7;  |TIPO 7;
     |SI enEmpresa ! 0;
         #0#24  = enEmpresa;  |PINTA #0#24;
         #0#25  = enEmpresa;  |PINTA #0#25;

         |C_M #0#24, 1, "N";
         |C_M #0#25, 1, "N";
     |FINSI;
|FCAL;

|CALCULO Parrilla;
     |SI #0#24 = 0;
         #0#24 = 0;  |PINTA #0#24;
         #0#25 = 0;  |PINTA #0#25;  |C_M #0#25, 1, "N";
         |PARA nCampo = 26;  |SI nCampo [ 37;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |SINO;
         |SI enEmpresa = 0;
             |C_M #0#24, 1, "S";
             |C_M #0#25, 1, "S";
         |FINSI;

         |PARA nCampo = 26;  |SI nCampo [ 37;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "N";
               #0nCampo = 0;  |PINTA #0nCampo;
         |SIGUE;
     |FINSI;
|FCAL;

|CALCULO NoMasEmpresas;
     |SI #0Campo = 0;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 37;  |HACIENDO nCampo = nCampo + 1;
               #0nCampo = 0;  |C_M #0nCampo, 1, "N";
         |SIGUE;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 37;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |FINSI;
|FCAL;

|CALCULO Seleccion; |TIPO 0;
   |SI #0#0 = "S";
       |C_M #0#1,1,"S";
       |C_M #0#2,1,"S";
       |PARA Cont = 3; |SI Cont [ 23; |HACIENDO Cont = Cont + 1;
             |C_M #0Cont,1,"N"; #0Cont = "            "; |PINTA #0Cont;
       |SIGUE;
   |SINO;
      |C_M #0#1,1,"N"; #0#1 = "            "; |PINTA #0#1;
      |C_M #0#2,1,"N"; #0#2 = "zzzzzzzzzzzz"; |PINTA #0#2;
      |PARA Cont = 3; |SI Cont [ 23; |HACIENDO Cont = Cont + 1;
            |C_M #0Cont,1,"S";
      |SIGUE;
   |FINSI;
|FCAL;


|CALCULO  BuscaCta; |TIPO 0;
     |C_M #0Campo, 0, aAlfa1;
     |SI aAlfa1 = "N"; |ACABA; |FINSI;

     emCta = 1;        || Dar mensajes de error
     salidas  = FSalida;

     |ABRE #100;
     |LEE 040#100p;
     |SI FSalida ! 0;  |DDEFECTO #100;  |FINSI;
     |CIERRA #100;

     |SI enEmpresa = 0;
         enEmpresa  = #100#1;
         enPerConta = #100#2;
     |FINSI;

     eaCuenta = #0Campo;
     |HAZ FiltraPuntos;
     #0Campo = eaCuenta;
     |PINTA #0Campo;
     enEmpresa = nLaEmpresa;

     eaCuenta = #0Campo;
     |HAZ SacaNivel;
|FCAL;

|| //////////////////// Procesos del Informe  \\\\\\\\\\\\\\\\\\\\\\\\\\
|PROCESO LeeAmortiza;

  |SI #41#26 > "01.01.1900"; |ACABA; |FINSI;
  |SI #41#27 > efHFecha; |ACABA; |FINSI;

  |SI nSwPrimero = 0;
      nSwPrimero = 1;
      nSwLinea   = 0;
      TotElem    = 0;
  |FINSI;

  |PRINT;
  TotElem = TotElem + #dsmom041#10;
  nSwLinea=1;
|FPRC;

|DEFBUCLE LeeAmortiza;
   #41#4;
   ;
   #999#1, aCtaNu;
   #999#1, aCtaNu;
   e;
   NULL, LeeAmortiza;
|FIN;

|PROCESO LeeAuxiliar;
  #41#0 = #906#0;
  #41#2 = #906#2;
  #41#3 = #906#3;
  |LEE 001#41=;
  |HAZ LeeAmortiza;
|FPRC;

|DEFBUCLE LeeAuxiliar;
  #906#3;
  ;
  #999#1, aCtaNu, "        ", 00;
  #999#1, aCtaNu, "zzzzzzzz", 99;
  e;
  NULL, LeeAuxiliar;
|FIN;

|PROCESO InfoAmo;

   nSwPrimero = 0;
   aCtaNu = #cacuenta#0;
   aCtaNo = #cacuenta#2;
   nCtaSa = #cacuenta#53;

   |SI enEjer ] 2008;
       |ABRE #41;
       |BUCLE LeeAuxiliar;
       |CIERRA #41;
   |SINO;
       |BUCLE LeeAmortiza;
   |FINSI;

   |SI nSwPrimero = 1;
       nSwLinea = 2;
       |PRINT;
       TotElem = 0;
   |FINSI;
|FPRC;

|DEFBUCLE InfoAmo;
   #cacuenta#1;
   3;
   DCuenta, "S";
   HCuenta, "S";
   ;
   NULL,InfoAmo;
|FIN;

/*
|PROCESO HastaElFinal;
   LinBlanco = 1;
   CabCta    = 0;
   PieEle    = 0;
   LinEle    = 0;
   NewPage   = 0;
   |PARA; |SI ContLin < NumLins; |HACIENDO ContLin = ContLin + 1;
          |PRINT;
   |SIGUE;
   LinBlanco = 0;
|FPRC;
*/

|| ************* Proceso para cambiar las cuentas
|PROCESO LeeAmor0;
  |SI #41#26 > "01.01.1900"; |ACABA; |FINSI;
  |SI #41#27 > efHFecha; |ACABA; |FINSI;

  #906#0 = #41#0;
  #906#1 = #41#1;
  #906#2 = #41#2;
  #906#3 = #41#3;
  |LEE 101#906=;
  |SI FSalida ! 0;
      #906#0 = #41#0;
      #906#1 = #41#1;
      #906#2 = #41#2;
      #906#3 = #41#3;
      #906#4 = #41#27;
      |GRABA 020#906b;
  |FINSI;

  aAlfa1 = #41#27;
  aAlfa1 = #41#27; aAlfa1 = aAlfa1 % -104;
  nEjer  = aAlfa1;
  #906#5 = #41#5;
  #906#8 = #41#5;
  |SI nEjer < 2008;
      enEjerCam = nEjer;
      eaCuenta  = #906#5;
      |HAZ CambioCta;
      #906#8 = eaCta2008;
  |FINSI;
  |GRABA 020#906a;
  |LIBERA #906;
|FPRC;

|DEFBUCLE LeeAmor;
  #41#1;
  ;
  #999#1, "        ", 00;
  #999#1, "zzzzzzzz", 99;
  e;
  NULL, LeeAmor0;
|FIN;

|PROCESO ElAuxiliar;
  |ABRE #906;
  |BUCLE LeeAmor;
  |CIERRA #906;
|FPRC;

|PROCESO Empresas;

    enEmpresa   = #999#1;
    |HAZ AntCambioCta;

    nSwPrimero = 0;
    |SI enEjer ] 2008;
        |HAZ ElAuxiliar;
    |FINSI;

    enEjer = #0#53;
    aPathConta = #999#15;
    |QBT aPathConta;
    |PATH_DAT #300 aPathConta;

    eaNombreEmp = #999#14;
    enEmpresa   = #999#1;
    eaEmp       = enEmpresa; |QBF eaEmp;
    eaEmp       = ("00000" + eaEmp) % -105;
    |SI enDatos = 1; enActividad = 0;  |HAZ DatosEmpr_w30; |FINSI;

    |SI #0#0 = "S";
        DCuenta = #0#1;
        HCuenta = #0#2;
        ContLin = 7;
        |BUCLE InfoAmo;
    |SINO;
        |PARA Cont = 3; |SI Cont [ 23; |HACIENDO Cont = Cont + 1;
              |SI #0Cont ! doce;
                  DCuenta = #0Cont;
                  HCuenta = #0Cont;
                  |BUCLE InfoAmo;
              |FINSI;
        |SIGUE;
    |FINSI;
    |PIEINF;
|FPRC;

|DEFBUCLE empreconta;
 #999#1;
 ;
 000000;
 999999;
 e;
 NULL, Empresas;
|FIN;


|| //////////////////// Procesos desde el Tipo 3   \\\\\\\\\\\\\\\\\\\\\\\\\\
|CALCULO Tipo3;  |TIPO 3;

     |HAZ LeeEURODBMOD;
     aFichero = "6a" + Usuario;
     |NOME_DAT #906 aFichero;
     |DELINDEX #906;

     enDatos = 0; |SI #0#52 = "S"; enDatos = 1; |FINSI;
     enEjer = #0#53;
     aAlfa1 = #0#53; aAlfa1 = "01.01." + aAlfa1; efDFecha = aAlfa1;
     aAlfa1 = #0#53; aAlfa1 = "31.12." + aAlfa1; efHFecha = aAlfa1;
     eaTituloL = "INFORME ANUAL DE AMORTIZACION FISCAL";

     |DBASS aBase; |QBT aBase;  || Directorio Base de las Aplicaciones
     aFichero = aBase + "modelos/def/cuentcon.ref";
     |XABRE "W", aFichero, nCanal;
     aVariable = ":/contagen/def/cacuenta" + &13 + &10;
     |XGRABA nCanal, aVariable;
     |XCIERRA nCanal;

     |IMPRE 0;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |INFOR "dsmoy044";
     |SI FSalida ! 0;  |FINIMP;  |ACABA;  |FINSI;

     aFichero = ("4y" + Usuario) % 108;
     |NOME_DAT #999 aFichero;
     |DELINDEX #999;

     |ABRE #999;
     nGTempo = 0;   ||Busco Empresas Contables
     |SI #0#24 ! 0;
         |PARA enEmpresa = #0#24; |SI enEmpresa [ #0#25; |HACIENDO enEmpresa = enEmpresa + 1;
               enPerConta = -1;
               enEjer = #0#53;
               |HAZ LEmpresaConta;
               |SI swerror = 0; |HAZ GrabaTemporal; |FINSI;
        |SIGUE;
     |SINO;
        |PARA nCampo = 26;;  |SI nCampo [ 37;  |HACIENDO nCampo = nCampo + 1;
              |SI #0nCampo ! 0;
                  enEmpresa = #0nCampo;
                  enPerConta = -1;
                  enEjer = #0#53;
                  |HAZ LEmpresaConta;
                  |SI swerror = 0; |HAZ GrabaTemporal; |FINSI;
              |FINSI;
       |SIGUE;
     |FINSI;
     |CIERRA #999;

     |SI nGTempo = 0;    || No existe ninguna empresa
         |MENAV "    Error. No existe ninguna Empresa Contable dentro de la Seleccion ";
     |SINO;
         |BUCLE empreconta;
     |FINSI;
     |DELINDEX #999;
     |FININF;
     |FINIMP;

     |DELINDEX #906;
|FCAL;

|| ....................... Situa la Empresa Contable
|PROCESO LEmpresaConta;
  enConcreto = 1;
  swerror = 0;
  enSwSinMen = 1;
  |HAZ SituaContagen;
  enSwSinMen = 0;
  |SI swerror = 1;    |ACABA;      |FINSI;
  |HAZ LaSelEmpresa2;
  |SI enSwSelEmp = 1; swerror = 1; |FINSI;
  ElPathPan = aPathPanConta;
  enConcreto = 0;
|FPRC;

|PROCESO GrabaTemporal;
 nGTempo = nGTempo + 1;
 #999#0 = nGTempo;      || Guia
 #999#1 = enEmpresa;    || Empresa
 #999#2 = enPerConta;   || Periodo Contable
 #999#3 = enEjer;       || A¤o
 #999#4 = dig1;         || Desde Mes
 #999#5 = dig3;         || N§ Digitos
 #999#6 = dig4;         || Digitos Nivel 1
 #999#7 = dig5;         || Digitos Nivel 2
 #999#8 = dig6;         || Digitos Nivel 3
 #999#9 = dig7;         || Digitos Nivel 4
 #999#10 = dig8;        || Digitos Nivel 5
 #999#11 = dig9;        || Digitos Nivel 6
 #999#12 = eaEstadoEmp; || Activa / Inactiva
 #999#13 = eaMonedaCon; || Moneda Empresa
 #999#14 = eaNombreEmp; || Nombre Empresa
 #999#15 = aPathConta;  || Path
 |GRABA 020#999n;
|FPRC;
