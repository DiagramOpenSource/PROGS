|FICHEROS;
    dsmom093 #93;
    dsmom094 #94;

    dsmom001 #102;
    :/basica/def/agicen14 #114;
    espejoM@ #999;
|FIN;

|VARIABLES;
   &enSwApli = 0;
   &aAplica1 = "";
   &aAplica2 = "";
   &nAplica3 = 0;
   &enModeloAp    = 0;
   &enEmpresaAp   = 0;
   &eaNombreAp    = "";
   &enEjerAp      = 0;
   &enPeriodoAp   = 0;
   &enActividadAp = 0;
   &enRend_NoAgr  = 0;
   &enRend_Agr    = 0;
   &efFechaAp     = @;
   &enApli400     = 0;
   &enImporte     = 0;
   nRendimiento   = 0;
   nTopeHM   = 0;
   nTopeM    = 0;
   nTopeV    = 0;
   nTopeV0   = 0;
   nTopeV1   = 0;
   nNume1    = 0;
   nNume2    = 0;
   nTotalM   = 0;
   nTotalV   = 0;
   nDiaT     = 0;
   nTDias    = 0;
   aAlfa1    = "";
   aAlfa2    = "";
   aAlfa3    = "";
   fFechaIni = @;
   fFechaFin = @;
   fFecha    = @;
   nEjer     = 0;
   aBase     = "";
   aMas      = "";
   nEstado   = 0;

   nEmp      = 0;
   nAct      = 0;
   nTp100    = 0;
|FIN;

|INCLUYE i_variar;

|PROCESO dsmodAnt;
    #93#15 = #999#8;
|FPRC;

|DEFBUCLE dsmodAnt;
    #espejoM@#1006;
    ;
    enEmpresaAp, nEjer, 01, enModeloAp, 00, 00;
    enEmpresaAp, nEjer, 12, enModeloAp, 99, 99;
    ;
    NULL, dsmodAnt;
|FIN;

|PROCESO dsmod18Ant;
    || Antes estaba la #999#121 Tiquet 004445/96

    |SI nTDias ! nDiaT;
        #999#123 = (( #999#123 / nDiaT) * nTDias) ! 2;
    |FINSI;

    #93#15 = #93#15 + (#999#123 % nTp100);
|FPRC;

|DEFBUCLE dsmod18Ant;
    #espejoM@#1004;
    ;
    nEmp, nEjer, 00, nAct;
    nEmp, nEjer, 00, nAct;
    ;
    NULL, dsmod18Ant;
|FIN;

|PROCESO dsmom001;
     |SI #102#8 [ "01.01.0000";  #102#8 = fFechaIni;  |FINSI;
     |SI #102#9 [ "01.01.0000";  #102#9 = fFechaFin;  |FINSI;

     |SI #102#8 > fFechaFin;  |ACABA;  |FINSI;
     |SI #102#9 < fFechaIni;  |ACABA;  |FINSI;
     nTDias = #102#9 - #102#8 + 1;

     nEmp   = #102#0;
     nAct   = #102#1;
     nTp100 = #102#7;
     |BUCLE dsmod18Ant;
|FPRC;

|DEFBUCLE dsmom001;
   #102#2;
   ;
   #114#0, #114#1;
   #114#0, #114#1;
   ;
   NULL, dsmom001;
|FIN;

|PROCESO PorActividad;
  |SI #114#13 = "S"; |ACABA; |FINSI;

  |SI #114#5 [ "01.01.0000";  #114#5 = fFechaIni;  |FINSI;
  |SI #114#6 [ "01.01.0000";  #114#6 = fFechaFin;  |FINSI;

  |SI #114#5 > fFechaFin;  |ACABA;  |FINSI;
  |SI #114#6 < fFechaIni;  |ACABA;  |FINSI;

  nTDias = nDiaT;

  nEmp = #114#0;
  nAct = #114#1;
  |SI #114#14 = "N";
      nTp100 = 100;
      |BUCLE dsmod18Ant;
  |SINO;
      |BUCLE dsmom001;
  |FINSI;
|FPRC;

|DEFBUCLE PorActividad;
   #114#1;
   ;
   enEmpresaAp, 01;
   enEmpresaAp, 99;
   ;
   NULL, PorActividad;
|FIN;

|PROCESO Calculo93_15;
     nEjer  = #93#1 - 1;
     #93#15 = 0;

     |DBASE aBase;  |QBF aBase;

     |SI enModeloAp = 130;
         aMas = aBase + "def/dsmod130.mas";
         |CARGA_DEF aMas, espejoM@;
         |SI FSalida = 0;
             |BUCLE dsmodAnt;
             |DESCARGA_DEF #espejoM@;
         |FINSI;
     |FINSI;

     |SI enModeloAp = 131;

         aAlfa1 = nEjer; aAlfa1 = "01.01." + aAlfa1; fFechaIni = aAlfa1;
         aAlfa1 = nEjer; aAlfa1 = "31.12." + aAlfa1; fFechaFin = aAlfa1;

         nDiaT  = 365; |SI nEjer = 2016; |O nEjer = 2020; |O nEjer = 2024;
                          |O nEjer = 2028 ; nDiaT = 366; |FINSI;


         aMas = aBase + "def/dsmom018.mas";
         |CARGA_DEF aMas, espejoM@;
         |SI FSalida = 0;
             |BUCLE PorActividad;
             |DESCARGA_DEF #espejoM@;
        |FINSI;
     |FINSI;
|FPRC;

|PROCESO eGrabadsmom093;

     |SI enSwApli = 3;
         |HAZ Calculo93_15;
         |ACABA;
     |FINSI;

     |ABRE #93;
     #93#0 = enModeloAp;
     #93#1 = enEjerAp;
     #93#2 = enEmpresaAp;
     |LEE 141#93=;
     nEstado = FSalida;
     |SI nEstado ! 0;
         |DDEFECTO #93;
         #93#0 = enModeloAp;
         #93#1 = enEjerAp;
         #93#2 = enEmpresaAp;
         #93#3 = eaNombreAp;
         |GRABA 020#93b;
     |FINSI;
     |SI enSwApli = 1; |O enSwApli = 6; |O nEstado ! 0;
         |HAZ Calculo93_15;
     |FINSI;
     |SI enSwApli = 2; |O enSwApli = 6;
         enImporte = #93#15;
     |FINSI;
     |SI enSwApli = 5;
         #93#15 = enImporte;
     |FINSI;
     |SI enSwApli ! 6;
         |GRABA 020#93a;
     |FINSI;
     |LIBERA #93;
     |CIERRA #93;
|FPRC;

|PROCESO Leodsmom094;
   nTotalM = nTotalM + #94#8;
   nTotalV = nTotalV + #94#9;
|FPRC;

|DEFBUCLE Leodsmom094;
   #94#1;
   10;
   #93#1, #93#2, 01, 01;
   #93#1, #93#2, 99, enPeriodoAp;
   e;
   NULL, Leodsmom094;
|FIN;

|PROCESO eLeeAplica;

     nTopeHM = 8000;
     nTopeM  = 12000;
     nTopeV  = 33007.20;
     nTopeV0 = 33007.20;
     nTopeV1 = 22000.00;

|| .................. Lee Fichero de Aplicar si o no
     enApli400 = 100;
     aAplica1  = "S";
     aAplica2  = "N";
     nAplica3  = 0;
     |ABRE #93;
     #93#0 = enModeloAp;
     #93#1 = enEjerAp;
     #93#2 = enEmpresaAp;
     |LEE 141#93=;
     |SI FSalida = 0;
         aAplica1 = #93#4;
         aAplica2 = #93#5;
         nAplica3 = #93#14;
         |SI #93#1 < 2011; nAplica3 = 0; |FINSI;
     |SINO;
         |SI enEjerAp ] 2015; |Y enSwApli = 0;
             |DDEFECTO #93;
             #93#0 = enModeloAp;
             #93#1 = enEjerAp;
             #93#2 = enEmpresaAp;
             #93#3 = eaNombreAp;
             |HAZ Calculo93_15;
             |GRABA 020#93b;
             aAplica1 = #93#4;
             aAplica2 = #93#5;
             nAplica3 = #93#14;
         |FINSI;
     |FINSI;
     |LIBERA #93;
     |CIERRA #93;

|| ......... Hasta el a¤o 2009 sin calculo, solo con este fichero
     |SI enEjerAp < 2010;                   |ACABA; |FINSI;

|| ......... Si no desea aplicar ni uno ni el otro, acaba ya.
     |SI aAplica1 = "N"; |Y aAplica2 = "N"; |ACABA; |FINSI;

     |SI enPeriodoAp =  3; nNume1 = 6; nNume2 = 10; |FINSI;
     |SI enPeriodoAp =  6; nNume1 = 7; nNume2 = 11; |FINSI;
     |SI enPeriodoAp =  9; nNume1 = 8; nNume2 = 12; |FINSI;
     |SI enPeriodoAp = 12; nNume1 = 9; nNume2 = 13; |FINSI;

     nTotalM = 0;
     nTotalV = 0;
     |BUCLE Leodsmom094;  || Sumo actividades

     |SI aAplica1 = "S";        || Calculo para Minoracion
         |SI #93#1 < 2015;
             enApli400 = 100;
             nRendimiento = (enRend_NoAgr + (enRend_Agr * 0.25)) ! 2;
             |SI #93nNume1 ! "X";   || Si no fuerzo a Deducir S
                 |SI nRendimiento > nTopeM; |O nTotalM > nTopeM;
                     aAplica1 = "N";
                 |SINO;
                     |SI nTotalM > nTopeHM;
                         enApli400 = (( 400 - (nTotalM - nTopeHM) * 0.1) / 4) ! 2;
                     |FINSI;
                 |FINSI;
             |FINSI;
         |SINO;
             enApli400 = 0;
             |SI #93#15 [ 9000;                     enApli400 = 100; |FINSI;
             |SI #93#15 > 9000;  |Y #93#15 [ 10000; enApli400 =  75; |FINSI;
             |SI #93#15 > 10000; |Y #93#15 [ 11000; enApli400 =  50; |FINSI;
             |SI #93#15 > 11000; |Y #93#15 [ 12000; enApli400 =  25; |FINSI;
         |FINSI;
     |FINSI;

     nTopeV = nTopeV0;
     |SI nAplica3 = 1; nTopeV = nTopeV1; |FINSI;

     |SI aAplica2 = "S";  || Calculo para Minoracion
         |SI #93nNume2 ! "X";
             |SI nTotalV > nTopeV;
                 aAplica2 = "N";
              |FINSI;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO eGrabadsmom094;

   |SI enActividadAp = 99;   |ACABA; |FINSI;
   |SI enEjerAp      < 2010; |ACABA; |FINSI;
   |SI enEjerAp      > 2014; |ACABA; |FINSI;

   nTDias = 365;
   aAlfa1 = enEjerAp;
   aAlfa2 = "01.01." + aAlfa1; fFecha = aAlfa2;  ||Inicio Ejercicio

   |SI enPeriodoAp =  3;
       nDiaT = 90; aAlfa2 = "01.01."; aAlfa3 = "31.03.";
       |SI enEjerAp = 2012; nDiaT = 91; nTDias = 366; |FINSI;
       |SI enEjerAp = 2016; nDiaT = 91; nTDias = 366; |FINSI;
       |SI enEjerAp = 2020; nDiaT = 91; nTDias = 366; |FINSI;
   |FINSI;
   |SI enPeriodoAp =  6; nDiaT = 91; aAlfa2 = "01.04."; aAlfa3 = "30.06."; |FINSI;
   |SI enPeriodoAp =  9; nDiaT = 92; aAlfa2 = "01.07."; aAlfa3 = "30.09."; |FINSI;
   |SI enPeriodoAp = 12; nDiaT = 92; aAlfa2 = "01.10."; aAlfa3 = "31.12."; |FINSI;

   aAlfa2 = aAlfa2 + aAlfa1; fFechaIni = aAlfa2;
   aAlfa3 = aAlfa3 + aAlfa1; fFechaFin = aAlfa3;

   |SI efFechaAp [ fFecha;
       |SI enPeriodoAp ! 3; |ACABA; |FINSI;  || No lo graba, solo el Primero
   |SINO;
       |SI efFechaAp > fFechaFin; |ACABA; |FINSI;
       |SI efFechaAp < fFechaIni; |ACABA; |FINSI;
   |FINSI;

   |ABRE #94;
   |DDEFECTO #94;
   #94#1 = enEjerAp;
   #94#2 = enEmpresaAp;
   #94#3 = enActividadAp;
   |LEE 101#94=;
   |SI FSalida ! 0;
       #94#1 = enEjerAp;
       #94#2 = enEmpresaAp;
       #94#3 = enActividadAp;
       |GRABA 020#94b;
   |FINSI;

|| ... Calculo de los dias del trimestre trabajados
   |SI efFechaAp ] fFechaIni;
       nDiaT = fFechaFin - efFechaAp + 1;
   |FINSI;

   #94#0 = enModeloAp;
   #94#4 = efFechaAp;
   #94#5 = nDiaT;
   #94#6 = enRend_NoAgr;
   #94#7 = enRend_Agr;
   #94#10 = enPeriodoAp;

   nRendimiento = (#94#6 + (#94#7 * 0.25)) ! 2;
   |SI #94#0 = 130;
       #94#8 = ((nRendimiento / #94#5) * nTDias) ! 2;
   |SINO;
       #94#8 = nRendimiento;  || 131 sin elevar al a¤o
   |FINSI;

   nRendimiento = (#94#6 + #94#7) ! 2;
   |SI #94#0 = 130;
       #94#9 = ((nRendimiento / #94#5) * nTDias) ! 2;
   |SINO;
       #94#9 = nRendimiento;  || 131 sin elevar al a¤o
   |FINSI;

   |GRABA 020#94a;
   |LIBERA #94;
   |CIERRA #94;
|FPRC;
