|FICHEROS;
   dsmoy049 #0;

   dsmom041 #41;
   dsmom042 #42;
   dsmom100 #100;

   :/basica/def/agicen14 #114;
   :/basica/def/agifigen #118;
   :/basica/def/agim0019 #119;

   :/contagen/def/canempre #300;
   :/contagen/def/caivaasi #301;
   :/contagen/def/camaniva #304;
   :/contagen/def/caivalin #305;
   :/contagen/def/caconimp #306;  || configuracion impresiones

   dsmom030 #30;
   dsmow030 #900;          ||Def de impresion
   dsmow031 #901;
   dsmow043 #999,MANTE;
|FIN;

|VARIABLES;
   &entrada = 1;
   informe = "";

   nInkey      = 0;
   aFichero    = "";
   nCampo      = 0;
   nDEmpresa   = 0;
   nHEmpresa   = 0;
   nDActividad = 0;
   nHActividad = 0;
   nLaEmpresa  = 0;
   nGTempo     = 0;

   nSPin1      = 0;
   aPath       = "";
   nEjercicio  = 0;

   aAlfa1 = "";
   aAlfa2 = "";
   aAlfa3 = "";
   nPara1 = 0;
   nPara2 = 0;
   nNume1 = 0;
   nNume2 = 0;
   nNume3 = 0;
   nNume4 = 0;

   aDTipo = "";
   aHTipo = "";
   SwHayDatos = 0;

   fDFecha   = @;
   fHFecha   = @;
   fLaFecha  = @;

   &nPagina  = 0;
   &nPagDup  = 0;
   &nSwPagDup = 0;
   &SwPrim   = 0;
   &SwLinea  = 0;

   sw = 0;
   nElDepar = 0;
   nSwOp    = 0;
   nEstado  = "";
   fDFecTope = @;
   fHFecTope = @;
|FIN;

|INCLUYE i_variar;
|INCLUYE i_varcon;
|INCLUYE i_varlib;
|INCLUYE i_numamo;
|| //////////////////// Calculos de Pantalla \\\\\\\\\\\\\\\\\\\\\\\\\\

|CALCULO Eano; |TIPO 7;
 fLaFecha = ~;
 aAlfa1 = fLaFecha; aAlfa1 = aAlfa1 % -104;
 #0#1 = aAlfa1;
 |PINTA #0#1;
|FCAL;

|CALCULO Tipo7;  |TIPO 7;
     |SI enEmpresa ! 0;
         #0#2  = enEmpresa;  |PINTA #0#2;
         #0#3  = enEmpresa;  |PINTA #0#3;

         |C_M #0#2, 1, "N";
         |C_M #0#3, 1, "N";
     |FINSI;
|FCAL;

|CALCULO Parrilla; |TIPO 0;
     |SI #0#2 = 0;
         #0#2 = 0;  |PINTA #0#2;
         #0#3 = 0;  |PINTA #0#3;  |C_M #0#3, 1, "N";
         |PARA nCampo = 4;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |SINO;
         |SI enEmpresa = 0;
             |C_M #0#2, 1, "S";
             |C_M #0#3, 1, "S";
         |FINSI;

         |PARA nCampo = 4;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "N";
               #0nCampo = 0;  |PINTA #0nCampo;
         |SIGUE;
     |FINSI;
|FCAL;

|CALCULO NoMasEmpresas; |TIPO 0;
     |SI #0Campo = 0;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
               #0nCampo = 0;  |C_M #0nCampo, 1, "N";
         |SIGUE;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |FINSI;
|FCAL;

|CALCULO YaSeCual; |TIPO 7;
 |SI enEmpresa ! 0;
     #0#54 = "S"; |PINTA #0#54;
     #0#24 = "S"; |PINTA #0#24;
     #0#25 = 00;  |PINTA #0#25;
     #0#26 = 99;  |PINTA #0#26;
     |C_M #0#24, 1, "N";
     |C_M #0#25, 1, "N";
     |C_M #0#26, 1, "N";
     |C_M #0#54, 1, "N";
 |FINSI;
|FCAL;

|CALCULO SelTipo0; |TIPO 0;
 |SI #0#54 = "N";
     #0#25 = 0;   |PINTA #0#25;  |C_M #0#25, 1, "N";
     #0#26 = 99;  |PINTA #0#26;  |C_M #0#26, 1, "N";
     |PARA nCampo = 27;  |SI nCampo [ 36;  |HACIENDO nCampo = nCampo + 1;
           |C_M #0nCampo, 1, "S";
     |SIGUE;
 |SINO;
     |SI enEmpresa = 0;
         |C_M #0#25, 1, "S";
         |C_M #0#26, 1, "S";
     |FINSI;
     |PARA nCampo = 27;  |SI nCampo [ 36;  |HACIENDO nCampo = nCampo + 1;
           |C_M #0nCampo, 1, "N";
           #0nCampo = 0;  |PINTA #0nCampo;
     |SIGUE;
 |FINSI;
|FCAL;

|CALCULO SelTipo1;
 |SI #0Campo = 0;
     |PARA nCampo = Campo + 1;  |SI nCampo [ 36;  |HACIENDO nCampo = nCampo + 1;
           #0nCampo = 0;  |C_M #0nCampo, 1, "N";
     |SIGUE;
 |SINO;
     |PARA nCampo = Campo + 1;  |SI nCampo [ 36;  |HACIENDO nCampo = nCampo + 1;
           |C_M #0nCampo, 1, "S";
     |SIGUE;
 |FINSI;
|FCAL;

|CALCULO SelAct; |TIPO 0;
 aAlfa1 = #0Campo;
 |C_M #0#56,1, aAlfa1;
 |C_M #0#57,1, aAlfa1;
|FCAL;

|CALCULO cuenta; |TIPO 0;
     salidas  = FSalida;

     |ABRE #100;
     |LEE 040#100p;
     |SI FSalida ! 0;  |DDEFECTO #100;  |FINSI;
     |CIERRA #100;

     nLaEmpresa = enEmpresa;

     |SI enEmpresa = 0;
         enEmpresa  = #100#1;
         enPerConta = #100#2;
     |FINSI;

     eaCuenta = #0Campo;
     |HAZ FiltraPuntos;
     #0Campo = eaCuenta;
     |PINTA #0Campo;
     enEmpresa = nLaEmpresa;
|FCAL;

|CALCULO numamo; |TIPO 0;
   x_alfa1 = #0Campo;
   |HAZ x_punto;
   #0Campo = x_alfa1;
   |PINTA #0Campo;
|FCAL;
|| **********************************************************************************
|| **********************************************************************************

|PROCESO Lineas;
   |SI #42#6 = "C";
       aAlfa1 = #42#12; aAlfa1 = ("000" + aAlfa1) % -103;
       #900#19 = aAlfa1;
       #900#22 = #42#10;
   |FINSI;
   |SI #42#6 = "V";
       aAlfa1 = #42#12; aAlfa1 = ("000" + aAlfa1) % -103;
       #900#20 = aAlfa1;
       #900#65 = #42#10;
  |FINSI;
  |SI #42#6 = "A"; |Y #42#9 [ fHFecTope;
      #41#15 = #41#15 + #42#7;
      #41#21 = #41#21 + #42#11;
      #41#22 = #41#22 - #42#11;
  |FINSI;
|FPRC;

|DEFBUCLE Lineas;
 #42#1;
 ;
 #41#0, #41#2, #41#3, 000;
 #41#0, #41#2, #41#3, 999;
 ;
 NULL, Lineas;
|FIN;

|PROCESO Leem041;

   |SI #0#46 = "N";
      |SI #41#25 = "N"; |ACABA; |FINSI;
   |FINSI;
   |SI #0#42 = "N";
      |SI #41#35 = "S"; |ACABA; |FINSI;
   |FINSI;

   |SI #41#26 > "01.01.1900";
       |SI #41#26 < fDFecTope; |ACABA; |FINSI; || vendido antes del Ejercicio
   |FINSI;

   |SI #41#9 > "01.01.1900";
       |SI #41#9 > fHFecTope; |ACABA; |FINSI;  || puesto en marcha despues del Ejercicio
   |SINO;
       |SI #41#27 > fHFecTope; |ACABA; |FINSI;  || comprado despues del Ejercicio
   |FINSI;

   SwHayDatos = 1;
   |SI nSwOp = 0;
       |ERROR10;
       |ACABA;
   |FINSI;

   |SI nSwOp = 1;
       #900#19 = ""; #900#22 = "";
       #900#20 = ""; #900#65 = "";
       #41#15 = 0; #41#21 = 0; #41#22 = #41#10;
       |BUCLE Lineas;
       |PRINT;
   |FINSI;
|FPRC;

|DEFBUCLE Leem041;
   #41#1;
   1,5;
   enEmpresa, #0#60, #0#62, nDActividad, #0#58;
   enEmpresa, #0#61, #0#63, nHActividad, #0#59;
   e;
   NULL, Leem041;
|FIN;

|PROCESO Emision;
   enEmpresa  = #999#1;
   enPerConta = #999#2;
   eaLaMonEmp = #999#13;
   |HAZ EnConversor;
   enEjer     = #999#3;   || A¤o
   dig1       = #999#4;   || Desde Mes
   dig3       = #999#5;   || N§ Digitos
   dig4       = #999#6;   || Digitos Nivel 1
   dig5       = #999#7;   || Digitos Nivel 2
   dig6       = #999#8;   || Digitos Nivel 3
   dig7       = #999#9;   || Digitos Nivel 4
   dig8       = #999#10;  || Digitos Nivel 5
   dig9       = #999#11;  || Digitos Nivel 6
   eaEstadoEmp = #999#12; || Estado
   eaNombreEmp = #999#14; || Nombre Empresa
   aPathConta  = #999#15; |QBF aPathConta; || Path
   || Recoger informacion sobre los niveles ...
   |SI dig3 = 1; MaximoDigitos = dig4; |FINSI;
   |SI dig3 = 2; MaximoDigitos = dig5; |FINSI;
   |SI dig3 = 3; MaximoDigitos = dig6; |FINSI;
   |SI dig3 = 4; MaximoDigitos = dig7; |FINSI;
   |SI dig3 = 5; MaximoDigitos = dig8; |FINSI;
   |SI dig3 = 6; MaximoDigitos = dig9; |FINSI;

   eaTituloL = #0#52; ||Titulo del Libro
   |SI #0#37 = "S";
       |SUB_EJECUTA_NP ":basica/agiy0016";
   |FINSI;

   |PATH_DAT #301 aPathConta;
   |PATH_DAT #304 aPathConta;
   |PATH_DAT #305 aPathConta;
   |PATH_DAT #306 aPathConta;
   |ABRE #caivaasi;
   |DDEFECTO #caivaasi;
   |LEE 000#caivaasi.p;
   |CIERRA #caivaasi;
   aMulDep = #caivaasi#132;

   |DDEFECTO #306;  ||Configuracion de Informes
   |ABRE #306;
   #306#0 = 1;
   |LEE 000#306=;
   |CIERRA #306;

   enActividad = 0;
   |HAZ LeeDatosEmpresa;

   informe = "dsmoy049";
   |SI #306#4 = "N"; informe = "drmoy049"; |FINSI;

   |INFOR informe;
   |SI FSalida ! 0;  |ACABA;  |FINSI;

   nSwOp      = 1;
   SwHayDatos = 0;
   SwPrim     = 0;
   nPagina    = #0#51;
   nPagDup    = nPagina -1;
   nSwPagDup  = 0;
   |BUCLE Leem041;

   |SI SwHayDatos = 1;
       |PIEINF;
   |FINSI;
   |FININF;
|FPRC;

|DEFBUCLE dsmow043;
 #dsmow043#1;
 16;
 000001, "S";
 999999, "S";
 ;
 NULL, Emision;
|FIN;

|PROCESO Imprimir;

    |MENAV "0000 Recuerde seleccionar el papel de la impresora en Horizontal";

    |IMPRE 0;
    |SI FSalida ! 0;  |ACABA;  |FINSI;

    aDTipo = "R"; aHTipo = "S";
    |SI #0#0 ! "I";  aDTipo = #0#0; aHTipo = #0#0; |FINSI;

    |BUCLE dsmow043;
    |FINIMP;
|FPRC;

|| ....................... Situa la Empresa Contable
|PROCESO LEmpresaConta;

 enEmpresa  = #agifigen#0;
 enPerConta = -1;

 eaEstadoEmp = "S";
 |SI #agifigen#94 > "01.01.0000";
     eaEstadoEmp = "N";
     |SI #0#24 = "N"; |ACABA; |FINSI;
 |FINSI;

|| ... Seleccion de Tipo de Empresa
  nSPin1 = #agifigen#87;

  enSwSelEmp = 1;
  |SI #0#54 = "S";
      |SI nSPin1 ] #0#25; |Y nSPin1 [ #0#26;
          enSwSelEmp = 0;
      |FINSI;
  |SINO;
      |SI nSPin1 = #0#27;
          enSwSelEmp = 0;
      |SINO;
          |PARA nCampo = 28;  |SI nCampo [ 36;  |HACIENDO nCampo = nCampo + 1;
                |SI nSPin1 = #0nCampo; |Y nSPin1 ! 0;
                    enSwSelEmp = 0;
                   |SAL;
                |FINSI;
          |SIGUE;
      |FINSI;
  |FINSI;
  |SI enSwSelEmp = 1; |ACABA; |FINSI;
|| ------------------------------------------------------------------------

   nSwOp      = 0;
   SwHayDatos = 0;
   |BUCLE Leem041;

   |SI SwHayDatos = 1;

       |ABRE #canempre;
       |SELECT #4#canempre;
       |DDEFECTO #canempre;
       #canempre#2 = enEmpresa;
       #canempre#26 = nEjercicio;
       |LEE 001#canempre.=;
       nEstado = FSalida;
       |SELECT #1#canempre;
       |CIERRA #canempre;
       enPerConta = #canempre#3;
       |SI nEstado ! 0;
           |HAZ SituaMaestra;
       |FINSI;
       aAlfa1 = enEmpresa;  aAlfa1 = ("00000" + aAlfa1) % -105;
       aAlfa2 = enPerConta; aAlfa2 = ("0" + aAlfa2) % -101;
       aPathConta  = aPath + "contagen/fich/" + aAlfa1 + aAlfa2 + "/";

       enEmpresa = #agifigen#0;
       nGTempo = nGTempo + 1;
       #999#0 = nGTempo;       || Guia
       #999#1 = enEmpresa;     || Empresa
       #999#2 = enPerConta;    || Periodo Contable
       #999#3 = enEjer;        || A¤o
       #999#4 = #canempre#11;  || Desde Mes
       #999#5 = #canempre#13;  || N§ Digitos
       #999#6 = #canempre#14;  || Digitos Nivel 1
       #999#7 = #canempre#15;  || Digitos Nivel 2
       #999#8 = #canempre#16;  || Digitos Nivel 3
       #999#9 = #canempre#17;  || Digitos Nivel 4
       #999#10 = #canempre#18; || Digitos Nivel 5
       #999#11 = #canempre#19; || Digitos Nivel 6
       eaEstadoEmp = "S"; |SI #canempre#21 = "I"; eaEstadoEmp = "N"; |FINSI;
       #999#12 = eaEstadoEmp;  || Estado
       #999#13 = #canempre#28; || Moneda
       #999#14 = #agifigen#1;  || Nombre Empresa
       #999#15 = aPathConta;   || Path
       #999#16 = #0#48;        || Emitir
       |GRABA 020#999n;
   |FINSI;
|FPRC;

|DEFBUCLE agifigen;
 #agifigen#1;
 ;
 nDEmpresa;
 nHEmpresa;
 e;
 NULL, LEmpresaConta;
|FIN;
||///////////////////////////////////////////////////////////////////

|CALCULO Datos7; |TIPO 7;
 |MENSA "    <F3> Cambiar Emitir Libro S/N";
 |ATRI R; |PINTA 1169, "Imp"; |ATRI 0;
|FCAL;

|CALCULO DatosManip; |TIPO 11;
   nInkey = FSalida;
   |BLIN 04;
   |SI nInkey = 11;
       |SI #999#16 = "S";
           #999#16 = "N";
       |SINO;
           #999#16 = "S";
       |FINSI;
       |GRABA 020#999a;
   |FINSI;
|FCAL;

|RUTINA inferior;
   #999#0 = 00001;
|FRUT;

|RUTINA superior;
   #999#0 = 99999;
|FRUT;

||///////////////////////////////////////////////////////////////////
|CALCULO Tipo3;  |TIPO 3;
    #0#0  = "I";      ||Inversion
    #0#49 = enModelo; ||Libro Oficial

    |HAZ LeeCtrlLibro;

    nDActividad = #0#56;
    nHActividad = #0#57;

    enEjer = #0#1;
    aAlfa1 = enEjer; |QBF aAlfa1;
    aAlfa2 = "01.01." + aAlfa1; fDFecTope = aAlfa2;
    aAlfa2 = "31.12." + aAlfa1; fHFecTope = aAlfa2;

    aAlfa1 = aAlfa1 % -102;
    nEjercicio = aAlfa1;
    |DBASS aPath; |QBT aPath;

    aFichero = ("031w" + Usuario) % 108;
    |NOME_DAT #901 aFichero;

    aFichero = ("043w" + Usuario) % 108;
    |NOME_DAT #999 aFichero;
    |ABRE #999;  |CIERRA #999;  |DELINDEX #999;
    eaFEmpresa = aFichero;

    |ABRE #999;
    nGTempo = 0;   ||Busco Empresas Contables
    |SI #0#2 ! 0;
        nDEmpresa = #0#2;
        nHEmpresa = #0#3;
        |BUCLE agifigen;
    |SINO;
        |PARA nPara1 = 4;  |SI nPara1 [ 23;  |HACIENDO nPara1 = nPara1 + 1;
              nDEmpresa = #0nPara1;
              nHEmpresa = #0nPara1;
              |BUCLE agifigen;
        |SIGUE;
    |FINSI;
    |CIERRA #999;

    |SI nGTempo = 0;    || No existe ninguna empresa
        |MENAV "    Error. No existe ninguna Empresa Contable dentro de la Seleccion ";
        |DELINDEX #999;
        |ACABA;
    |FINSI;

    |C_V #999#12,1,"N"; |C_P #999#16,1,1270;

    |PUSHV 0509 2375;
    |ENTLINEAL #1#999, -1, 2, 22, 1, inferior, superior;
    |POPV;

    |HAZ Imprimir;

    |DELINDEX #901;
    |DELINDEX #999;
|FCAL;

|| *************************************************************************

|PROCESO LeeDatosEmpresa;
  |DELINDEX #901;

  |DDEFECTO #900;
  #900#0 = enEmpresa;
  enEjer = #0#1;
  eaInac = #0#24;
  eaTituloL = #0#52;
  aAlfa1 = #0#1; aAlfa1 = "01.01." + aAlfa1; efDFecha = aAlfa1;
  aAlfa1 = #0#1; aAlfa1 = "31.12." + aAlfa1; efHFecha = aAlfa1;
  |HAZ DatosEmpr_w30;
|FPRC;

|PROGRAMA;

 |DDEFECTO #0;
 enModelo = #0#49;

 |ABRE #0;
 |LEE 101#0p;
 |SI FSalida ! 0;
     |DDEFECTO #0;
     |GRABA 020#0b;
 |FINSI;

 |CLS;
 |PINPA #0#0;
 |PINDA #0#0;

 |ENDATOS #1#0;
 |SI FSalida = 0;
     |GRABA 020#0a;
 |FINSI;
 |LIBERA #0;
 |CIERRA #0;

 |PULSATECLA;
|FPRO;
