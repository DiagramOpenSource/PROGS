|FICHEROS;
     dsmoc115 #0;
     dsmom002 #2;
     dsmod115 #115;
     dsmom004 #4;
     dsmom008 #8;

     :/basica/def/agicen14 #114;
     :/basica/def/agicen17 #117;
     :/basica/def/agimunic #100;

     &regisnif@ #709;
     dsmc115@  #900;
|FIN;

|VARIABLES;
     nCampo1       = 0;
     nCampo2       = 0;
     nGrupo        = 0;
     nSuma         = 0;
     i             = 0;
     nMod          = 115;
     aDn           = "            ";
     aHn           = "zzzzzzzzzzzz";
     nDd           = 0;
     nHd           = 9999;
     aTipoCaptura  = "";
     nSwEsta       = 0;
     nEjerAnt      = 0;
     nPer          = 0;
     nSwGraba      = 0;
     nTipo         = 0;

     aPathModelo   = "";
     aDef          = "";
     aBase         = "";
     aAlfa         = "";
     aAlfa1        = "";
     aAlfa2        = "";
     aPeriodicidad ="";
     nNume         = 0;
     nModo         = 0;
     nNumero       = 0;
     nInkey        = 0;
     &enLinInm     = 0;
     nDevengo      = 0;
     aNif          = "";
     fFecha        = @;
     fDFecha       = @;
     fHFecha       = @;
     &enDesModelo  = 0;
|FIN;

|INCLUYE i_variar;

|PROCESO NuevaPantalla;
   |PINPA #0#0;

   |SI enEjer < 2015;
       |C_M #0#28, 1, "N"; #0#28 = 01;

       |C_V #0#29, 1, "N";  |C_V #0#30, 1, "N";
       |C_V #0#31, 1, "N";  |C_V #0#32, 1, "N";
       |C_V #0#33, 1, "N";  |C_V #0#34, 1, "N";
       |C_V #0#35, 1, "N";  |C_V #0#36, 1, "N";
       |C_V #0#37, 1, "N";  |C_V #0#38, 1, "N";
       |C_V #0#39, 1, "N";  |C_V #0#40, 1, "N";
       |C_V #0#41, 1, "N";

       |BLANCO 1624 2275;
   |FINSI;
|FPRC;

|PROCESO Tipo7C28; |TIPO 7;

     |SI enEjer < 2015; |ACABA; |FINSI;

     nModo = (FEntrada / 100) ! 0;
     |SI nModo ! 1;   |ACABA; |FINSI;

     |ABRE #900;
     #900#0 = #0#0;
     #900#1 = #0#1;
     #900#2 = #0#2;
     #900#3 = #0#3;
     #900#4 = #0#4;
     #900#5 = #0#5;
     #900#6 = #0#6;
     #900#7 = #0#7;
     #900#28 = 99;
     |LEE 041#900];
     |SI FSalida = 0;
         |LEE 041#900a;
     |SINO;
         |LEE 041#900u;
     |FINSI;
     nNumero = 0;
     |SI FSalida = 0; |Y #900#0 = #0#0; |Y #900#1 = #0#1; |Y #900#2 = #0#2;
        |Y #900#3 = #0#3; |Y #900#4 = #0#4; |Y #900#5 = #0#5;
          |Y #900#6 = #0#6; |Y #900#7 = #0#7;
             nNumero = #900#28;
     |FINSI;
     #0#28 = nNumero + 1; |PINTA #0#28;
     |CIERRA #900;
|FPRC;

|PROCESO Tipo7;  |TIPO 7;
     |SI #0#6 ! "MANUAL      ";
         |C_M #0#27, 1, "N";  |C_V #0#27, 1, "N";
     |SINO;
         |C_M #0#27, 1, "S";  |C_V #0#27, 1, "S";
     |FINSI;

     |PINTA #0#27;
|FPRC;

|| //////////////////////////////////////////////
|PROCESO IgualaCampos;
       #0#43 = #117#1;
       #0#42 = #117#2;
       #0#29 = #117#3;   |PINTA #0#29;
       #0#30 = #117#6;   |PINTA #0#30;
       #0#31 = #117#7;   |PINTA #0#31;
       #0#32 = #117#8;   |PINTA #0#32;
       #0#33 = #117#9;   |PINTA #0#33;
       #0#34 = #117#10;  |PINTA #0#34;
       #0#35 = #117#11;  |PINTA #0#35;
       #0#36 = #117#12;  |PINTA #0#36;
       #0#37 = #117#13;  |PINTA #0#37;
       #0#38 = #117#14;  |PINTA #0#38;
       #0#39 = #117#15;  |PINTA #0#39;
       #0#40 = #117#17;  |PINTA #0#40;
       #0#41 = #117#18;  |PINTA #0#41;
|FPRC;

|RUTINA ClaveBaja114;
     #114#0 = #0#0;
     #114#1 = 01;
|FRUT;

|RUTINA ClaveAlta114;
     #114#0 = #0#0;
     #114#1 = 99;
|FRUT;

|PROCESO PideActividad;
     |SI FSalida = 10;
         |ABRE #114;
         #114#0 = #0#0;
         #114#1 = 0;
         |LEE 040#114];
         |SI FSalida = 0;  |Y #114#0 = #0#0;
             #114#1 = #0#43;
             |CONSULTA_F_CLAVE #1#114, ClaveBaja114, ClaveAlta114;

             #0#43 = #114#1;
         |SINO;
             |MENAV "     No existen Actividades en la Empresa.";
         |FINSI;
         |CIERRA #114;
     |FINSI;

     |SI #0#43 ! 0;
        |ABRE #114;
        #114#0 = #0#0;
        #114#1 = #0#43;
        |LEE 040#114=;
        |SI FSalida ! 0;
            |MENAV "     No existe la Actividad en la Empresa.";
            |ERROR;
        |FINSI;
        |CIERRA #114;
    |FINSI;
|FPRC;

|PROCESO PreguntaAct;
   |C_V #0#43,1,"S";
   |C_M #0#43,1,"S";
   |PINTA 1655, "Actividad:    "; |PINTA #0#43;
   |ENCAMPO #43#0;
   |C_M #0#43,1,"N";
   |C_V #0#43,1,"N";
   |PINTA 1655, "              ";
|FPRC;

|PROCESO Tipo7C29; |TIPO 7;
     |SI #0#1 ] 2014;
         |MENSA "0000Pulse F2-Consulta Ref.Catastrales, F3-Modificar Ref. Catastral";
     |FINSI;
|FPRC;

|RUTINA ClaveBaja117;
     #117#0 = #0#0;
     #117#1 = 00;
     #117#2 = 01;
|FRUT;

|RUTINA ClaveAlta117;
     #117#0 = #0#0;
     #117#1 = 99;
     #117#2 = 99;
|FRUT;

|PROCESO VerReferencia;
     |SI nInkey = 10;
         |ABRE #117;
         #117#0 = #0#0;
         #117#1 = 00;
         #117#2 = 00;
         |LEE 040#117];
         |SI FSalida = 0;  |Y #117#0 = #0#0;
             |C_M #117#2, 1, "S";

             #117#1 = #0#43;
             #117#2 = #0#42;
             |CONSULTA_F_CLAVE #1#117, ClaveBaja117, ClaveAlta117;
             #0#43 = #117#1;
             #0#42 = #117#2;

             |HAZ IgualaCampos;
         |SINO;
             |MENAV "     No existen Referencias Catastrales en la Empresa.";
         |FINSI;
         |CIERRA #117;
         |ERROR;
     |FINSI;

     |SI nInkey  = 11;
         |HAZ PreguntaAct;
         |SI #0#43 ! 0;
             enEmpresa   = #0#0;
             enActividad = #0#43;
             enLinInm    = #0#42;
             enDesModelo = 1;
             |SUB_EJECUTA ":basica/agicen17";
             enDesModelo = 0;
             #0#42 = enLinInm;
             enActividad = #0#5;
             |HAZ PonDatos;
         |FINSI;
         |ERROR;
     |FINSI;
|FPRC;

|PROCESO PonDatos;
     |ABRE #117;
     #117#0 = #0#0;
     #117#1 = #0#43;
     #117#2 = #0#42;
     |LEE 040#117=;
     |SI FSalida = 0;
         |HAZ IgualaCampos;
     |FINSI;
     |CIERRA #117;
|FPRC;

|PROCESO LeoEspejo;
    |SI #0#28 ! #900#28;
        |SI #0#42 = #900#42; |Y #0#43 = #900#43;
            nNumero = nNumero + 1;
        |FINSI;
    |FINSI;
|FPRC;

|DEFBUCLE LeoEspejo;
     #900#1009;
     29;
     #0#0, #0#1, #0#2, #0#3, #0#4, #0#5, #0#6, #0#7,    01, #0#29;
     #0#0, #0#1, #0#2, #0#3, #0#4, #0#5, #0#6, #0#7, nNume, #0#29;
     ;
     NULL, LeoEspejo;
|FIN;

|PROCESO Catastral;
     |C_M #0Campo, 0, aAlfa;
     |SI aAlfa = "N"; |ACABA; |FINSI;

     |SI enEjer < 2015; |ACABA; |FINSI;

     |BLIN 4;
     nInkey = FSalida;
     |SI nInkey = 10; |O nInkey = 11;
         |HAZ VerReferencia;
         |VETE VeoNuevo;
     |FINSI;

     aAlfa = #0#29;  |QBF aAlfa;
     |SI aAlfa = "";  |ACABA;  |FINSI;

     aAlfa = aAlfa % 0;
     |SI aAlfa ! "20";
         |MENAV "0000 La longitud de la Referencia Catastral deberia ser de 20 Caracteres.";
     |FINSI;

     |ET VeoNuevo;
     nNumero = 1;
     nNume   = #0#28 - 1;
     |BUCLE LeoEspejo;
     |SI nNumero > 1;
         |MENAV "    Error. Esta informando el Mismo Dni / Referencia Catastral";
         |MENAV "    Linea no Correcta";
         |ERROR;
     |FINSI;

|FPRC;

|PROCESO Localidad;
   |C_M #0Campo, 0, aAlfa;
   |SI aAlfa = "N"; |ACABA; |FINSI;

   enCodProv = #0#30;
   enCodPobl = #0#31;
   |HAZ LeeLocalidad;
   #0#32 = eaPoblacion; |PINTA #0#32;
   #0#33 = eaProvincia; |PINTA #0#33;
|FPRC;

|PROCESO Infmunic;
    #100#0 = "00000";
    #100#1 = "00000";
|FPRC;

|PROCESO Supmunic;
    #100#0 = "99999";
    #100#1 = "99999";
|FPRC;

|PROCESO Tipo66C18; |TIPO 66;

    |ABRE #100;
    aAlfa1 = #0#30; aAlfa1 = ( "00"  + aAlfa1) % -102;
    aAlfa2 = #0#31; aAlfa2 = ( "000" + aAlfa2) % -103;
    aAlfa1 = aAlfa1 + aAlfa2;
    #100#0 = aAlfa1;
    #100#1 = #0#41;

    |CONSULTA_F_CLAVE #1#100, Infmunic, Supmunic;

    aAlfa1 = #100#0; |QBF aAlfa1;
    aAlfa1 = ("00000" + aAlfa1) % -105;
    aAlfa2 = aAlfa1 % 102; #0#30  = aAlfa2; |PINTA #0#30;
    aAlfa2 = aAlfa1 % 303; #0#31  = aAlfa2; |PINTA #0#31;

    #0#41 = #100#1; |PINTA #0#41;
    |CIERRA #100;
|FPRC;

|PROCESO CalculaRetencion; |TIPO 0;
     nCampo1=Campo-1;
     nCampo2=Campo+1;
     #0nCampo2=#0nCampo1 % #0Campo;
     |PINTA #0nCampo2;
|FPRC;

|PROCESO dsmom002;              || Comprobamos todas las fechas

     aAlfa   = "31.12." + enEjer;  fHFecha = aAlfa;
     aAlfa   = "01.01." + enEjer;  fDFecha = aAlfa;

     aAlfa   = #2#8 % -104;
     |SI aAlfa ! "0000";
         nNume = aAlfa;
         |SI nNume > enEjer; |ACABA; |FINSI;

         |SI #2#8 > fDFecha; fDFecha = #2#8; |FINSI;
     |FINSI;

     aAlfa   = #2#9 % -104;
     |SI aAlfa ! "0000";
         nNume   = aAlfa;
         |SI nNume < enEjer;  |ACABA;  |FINSI;

         |SI #2#9 < fHFecha;  fHFecha = #2#9;  |FINSI;
     |FINSI;

     aAlfa2 = enPeriodo;
     aAlfa2 = ("00" + aAlfa2) % -102;
     aAlfa  = "31.";
     |SI enPeriodo = 4; |O enPeriodo = 6; |O enPeriodo = 9; |O enPeriodo = 11;
         aAlfa = "30.";
     |FINSI;
     |SI enPeriodo = 2;
         aAlfa = "28."; |SI enEjer = 2000; |O enEjer = 2004; |O enEjer = 2008;
                            |O enEjer = 2012; |O enEjer = 2016; |O enEjer = 2020;
                               aAlfa = "29.";
                        |FINSI;
     |FINSI;
     aAlfa2 = aAlfa + aAlfa2 + "." + enEjer;

     |SI #2#7 = "TRIMESTRAL";
         |SI enPeriodo = 3;  aAlfa1 = "01.01." + enEjer;  |FINSI;
         |SI enPeriodo = 6;  aAlfa1 = "01.04." + enEjer;  |FINSI;
         |SI enPeriodo = 9;  aAlfa1 = "01.07." + enEjer;  |FINSI;
         |SI enPeriodo = 12; aAlfa1 = "01.10." + enEjer;  |FINSI;
     |FINSI;

     |SI #2#7 = "MENSUAL   ";
         aAlfa1 = enPeriodo;  aAlfa1 = ("00" + aAlfa1) % -102;
         aAlfa1 = "01." + aAlfa1 + "." + enEjer;
     |FINSI;

     |SI #2#7 = "ANUAL     ";
         aAlfa1 = "01.01." + enEjer;
         aAlfa2 = "31.12." + enEjer;
     |FINSI;

     fFecha = aAlfa1;
     |SI fHFecha < fFecha;  |ACABA;  |FINSI;

     fFecha = aAlfa2;
     |SI fDFecha > fFecha;  |ACABA;  |FINSI;

     aPeriodicidad=#2#7; |QBF aPeriodicidad;
|FPRC;

|DEFBUCLE dsmom002;
   #2#1;
   ;
   enEmpresa, 01, enModelo;
   enEmpresa, 98, enModelo;
   e;
   NULL, dsmom002;
|FIN;

|PROCESO Modificables; |TIPO 7;

     aPeriodicidad="TRIMESTRAL";
     |BUCLE dsmom002;

     |SI aPeriodicidad ! "TRIMESTRAL";
          |PARA i=14;|SI i[25;|HACIENDO i=i+1;
               |CAMPO_MODIFICA #0i,1,"N";
          |SIGUE;
     |FINSI;

     |SI #0#6 ! "MANUAL      ";
         |C_M #0#27, 1, "N";  |C_V #0#27, 1, "N";
     |SINO;
         |C_M #0#27, 1, "S";  |C_V #0#27, 1, "S";
     |FINSI;
|FPRC;

|PROCESO Dnimoc115;  |TIPO 11;
     |SI FSalida = 10;
         eaVarDni  = #0Campo;
         enCalcCif = 0;
         |HAZ CalculameDNI;
         #0Campo = eaVarDni;
         |PINTA #0Campo;
         |ERROR;
     |FINSI;
|FPRC;

|PROCESO MiraDnimoc115;  |TIPO 6;
     eaVarDni  = #0Campo;
     enCalcCif = 0;
     |HAZ CalculameDNI;
     eaVarDni = (eaVarDni + "             ") % 112;
     |SI #0Campo ! eaVarDni;
         |CONFI "0000SEl Nif puede ser Erroneo. Desea Calcularlo.";
         |SI FSalida = 0;
             #0Campo = eaVarDni;  |PINTA #0Campo;
             |ERROR;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO PonNombre;  |TIPO 7;
     aAlfa = #0#26;  |QBF aAlfa;
     |SI aAlfa = "";
         eaCodNif = #0#6;
         |HAZ LeeNifs;
         #0#26 = #709#1;
         |PINTA #0#26;
     |FINSI;
|FPRC;

|PROCESO GrabaModelo;
     nSwGraba = 1;

     |SI #0#6 ! "            ";
         |SI #0#6 ! "PASEANTERIOR";  |Y #0#6 ! "MANUAL      ";
             |SI #0#6 ! aNif; |O #0#7 ! nDevengo;
                 #115#6 = #115#6 + 1;
             |FINSI;
             aNif = #0#6; nDevengo = #0#7;
         |FINSI;

         |SI #0#6 = "PASEANTERIOR";
             |SI #0#8 > 0;  |O #0#11 > 0;
                 #115#6 = #115#6 + 1;
             |FINSI;
         |FINSI;

         |SI #0#6 = "MANUAL      ";
             #115#6 = #0#27;
         |FINSI;
     |FINSI;

     #115#7  = #115#7 + #0#8  + #0#11 + #0#14 + #0#17 + #0#20 + #0#23;
     #115#8  = #115#8 + #0#10 + #0#13 + #0#16 + #0#19 + #0#22 + #0#25;
|FPRC;

|DEFBUCLE dsmoc115;
     #0#1;
     ;
     enEmpresa, enEjer, enPeriodo, nMod, enComplem, 01, aDn, nDd, 01;
     enEmpresa, enEjer, enPeriodo, nMod, enComplem, 99, aHn, nHd, 99;
     be;
     NULL, GrabaModelo;
|FIN;

|PROCESO GrabaRegistro0;
     |SI enModelo = 115;
         |ABRE #115;
         #115#0 = enEmpresa;
         #115#1 = enEjer;
         #115#2 = enPeriodo;
         #115#3 = enModelo;
         #115#4 = enComplem;
         #115#5 = 0;
         |LEE 101#115=;
         |SI FSalida = 0;
             |BORRA 020#115a;
         |FINSI;
         |LIBERA #115;
     |FINSI;

     |ABRE #8;
     #8#0 = enEmpresa;
     #8#1 = enEjer;
     #8#2 = enPeriodo;
     #8#3 = enModelo;
     #8#4 = enComplem;
     #8#5 = 0;
     |LEE 101#8=;
     |SI FSalida = 0;
         #8#11 = "SIN DATOS ";
         |GRABA 020#8a;
     |FINSI;
     |LIBERA #8;
     |CIERRA #8;
|FPRC;

|PROCESO BajaModelo;
     |BORRA 020#0a;
     |LIBERA #0;
|FPRC;

|DEFBUCLE dsmoc115B;
     #0#1;
     ;
     enEmpresa, enEjer, enPeriodo, nMod, enComplem, enActividad, aDn, nDd, 01;
     enEmpresa, enEjer, enPeriodo, nMod, enComplem, enActividad, aHn, nHd, 99;
     be;
     NULL, BajaModelo;
|FIN;

|PROCESO BorraRegistros;
     |BUCLE dsmoc115B;
     |HAZ GrabaRegistro0;

     |ABRE #8;
     #8#0 = enEmpresa;
     #8#1 = enEjer;
     #8#2 = nPer;
     #8#3 = 115;
     #8#4 = enComplem;
     #8#5 = enActividad;
     |LEE 101#8=;
     |SI FSalida = 0;
         #8#11 = "SIN DATOS";
         |GRABA 020#8a;
     |FINSI;
     |LIBERA #8;
     |CIERRA #8;
|FPRC;

|PROCESO GrabaTodo;
     |ABRE #115;
     #115#0 = enEmpresa;
     #115#1 = enEjer;
     #115#2 = enPeriodo;
     #115#3 = enModelo;
     #115#4 = enComplem;
     #115#5 = 0;
     |LEE 101#115=;
     |SI FSalida = 0;
          |BORRA 020#115a;
     |FINSI;

     |DDEFECTO #115;
     #115#0 = enEmpresa;
     #115#1 = enEjer;
     #115#2 = enPeriodo;
     #115#3 = enModelo;
     #115#4 = enComplem;
     #115#5 = 0;

     aNif     = "";
     nDevengo = -1;
     nSwGraba = 0;
     |BUCLE dsmoc115;
     |SI nSwGraba = 0;
         FSalida = 0;
         |SI enDesdePase = 0;
             |SI enMasivo = 1;
                 aAlfa = ("00000" + enEmpresa) % -105;
                 aAlfa = aAlfa + " " + eaNombreEmp; |QBF aAlfa;
                 aAlfa = "0000N"+ aAlfa + ". No hay reg. NIF desea Impreso SIN Actividad ";
            |SINO;
                 aAlfa = "0000NNo hay registros de NIF desea Impreso SIN Actividad ";
            |FINSI;
            |CONFI aAlfa;
            nNume = FSalida;
            |SI enMasivo = 1; |Y enFichW103 ! 0;
                eaCa8103 = "S"; |SI nNume ! 0; eaCa8103 = "N"; |FINSI;
                |HAZ eGrabaW103;
            |FINSI;
            FSalida = nNume;
         |FINSI;
         |SI FSalida = 0;
             |ABRE #0;
             |DDEFECTO #0;
             #0#0 = enEmpresa;
             #0#1 = enEjer;
             #0#2 = enPeriodo;
             #0#3 = enModelo;
             #0#4 = enComplem;
             #0#5 = enActividad;
             #0#6 = "            ";
             #0#7 = 0000;
             #0#28 = 01;
             |LEE 101#0=;
             || ....................... |GRABA 020#0n; No se porque se grababa antes
             |SI FSalida ! 0;
                 |GRABA 020#0b;
             |FINSI;
             |GRABA 020#0a;
             |LIBERA #0;
             |CIERRA #0;
             nSwGraba = 1;
         |FINSI;
     |FINSI;

     |SI nSwGraba ! 0;
         |SI #115#4 > 0;
             |ABRE #4;
             #4#0 = #115#0;
             #4#1 = #115#1;
             #4#2 = #115#2;
             #4#3 = #115#3;
             #4#4 = #115#4 - 1;
             |LEE 000#4=;
             |SI FSalida ! 0;  |DDEFECTO #4;  |FINSI;
             |CIERRA #4;

             |SI #115#11 = "             ";
                 #115#11 = #4#25;
             |FINSI;
             #115#9 = #4#15;
         |FINSI;

         #115#10 = #115#8 - #115#9;

         |GRABA 020#115n;
         |LIBERA #115;

          |ABRE #8;
          #8#0 = enEmpresa;
          #8#1 = enEjer;
          #8#2 = enPeriodo;
          #8#3 = enModelo;
          #8#4 = enComplem;
          #8#5 = enActividad;
          |LEE 101#8=;
          |SI FSalida = 0;
               #8#11 = "COMPLETADO";
               |GRABA 020#8a;
          |FINSI;
          |LIBERA #8;

          #8#0 = enEmpresa;
          #8#1 = enEjer;
          #8#2 = enPeriodo;
          #8#3 = enModelo;
          #8#4 = enComplem;
          #8#5 = 0;
          |LEE 101#8=;
          |SI FSalida = 0;
               #8#11 = "EXISTENTE";
               |GRABA 020#8a;
          |FINSI;
          |LIBERA #8;
          |CIERRA #8;
     |FINSI;
     |CIERRA #115;

     enSaldo1Ch = #115#8;
|FPRC;

|PROCESO AltaModelo;
     |ABRET #0;
     |HAZ PintaDatos;
     |HAZ NuevaPantalla;

     |SI enEjer ] 2015;
         |DBASE aBase;  |QBF aBase;
         aDef = aBase + "def/dsmoc115.mas";
         |CARGA_DEF aDef, dsmc115@;
         |SI FSalida ! 0;
             |MENAV "    Error al cargar def dsmoc115";
             |ACABA;
         |FINSI;
     |FINSI;

     |ENTLINEAL #1#0, -7, 2, 22, 0, ClaveBaja0, ClaveAlta0;

     |SI enEjer ] 2015;
         |DESCARGA_DEF #dsmc115@;
     |FINSI;
     |CIERRAT #0;

     |HAZ GrabaTodo;
|FPRC;

|RUTINA ClaveBaja0;
     #0#0 = enEmpresa;
     #0#1 = enEjer;
     #0#2 = enPeriodo;
     #0#3 = enModelo;
     #0#4 = enComplem;
     #0#5 = enActividad;
     #0#6 = "            ";
     #0#7 = 0000;
     #0#28 = 01;
|FRUT;

|RUTINA ClaveAlta0;
     #0#0 = enEmpresa;
     #0#1 = enEjer;
     #0#2 = enPeriodo;
     #0#3 = enModelo;
     #0#4 = enComplem;
     #0#5 = enActividad;
     #0#6 = "zzzzzzzzzzzz";
     #0#7 = 9999;
     #0#28 = 99;
     |SI enEjer [ 2014; #0#28 = 01; |FINSI;
|FRUT;

|PROCESO TipoCaptura;
     |ABRE #8;
     #8#0 = enEmpresa;
     #8#1 = enEjer;
     #8#2 = enPeriodo;
     #8#3 = enModelo;
     #8#4 = enComplem;
     #8#5 = enActividad;
     |LEE 101#8=;
     |SI FSalida = 0;
          aTipoCaptura = #8#7;
          |QBF aTipoCaptura;
     |FINSI;
     |CIERRA #8;
|FPRC;

|PROGRAMA;
     eaNoVisu = "";
     eaNomDef = "dsmoc115";

|| \\\\\\ Consulta del Modelo ////////

     |SI enTipoEntrada = 4;              || Consulta del Modelo
          |HAZ PintaDatos;
          |HAZ NuevaPantalla;
          |ENTLINEAL #1#0, -7, 4, 22, 0, ClaveBaja0, ClaveAlta0;
          |ACABA;
     |FINSI;

|| \\\\\\ Baja del Modelo ////////

     |SI enTipoEntrada = 3;              ||Baja del Modelo
         |HAZ BorraRegistros;
         |ACABA;
     |FINSI;

|| \\\\\\ Baja del Modelo desde Modulo////////

     |SI enTipoEntrada = 97;              ||Baja del Modelo
         |HAZ BorraRegistros;
         |ACABA;
     |FINSI;

|| \\\\\\ Alta o Modificacion del Modelo ////////

     |SI enTipoEntrada = 1;              || Alta del Modelo
         |ABRE #dsmom004;
         #dsmom004#0 = enEmpresa;
         #dsmom004#1 = enEjer;
         #dsmom004#2 = 99;
         #dsmom004#3 = 180;
         #dsmom004#4 = 99;
         |LEE 000#dsmom004.];
         |SI FSalida = 0;
             |LEE 000#dsmom004.a;
         |SINO;
             |LEE 000#dsmom004.u;
         |FINSI;

         |SI FSalida = 0;  |Y #dsmom004#0 = enEmpresa;  |Y #dsmom004#1 = enEjer;
          |Y #dsmom004#3 = 180;  |Y #dsmom004#7 = "X";
             |MENAV "0000El modelo anual 180 ya esta confeccionado, debera de recoger datos de nuevo.";
         |FINSI;
         |CIERRA #dsmom004;

         |CABEZA "Modelo Trabajo 115";
         |HAZ AltaModelo;
         |ACABA;
     |FINSI;

|| \\\\\\ Grabacion del Modelo desde el Exterior

     |SI enTipoEntrada = 98;
         |ABRET #0;
         |HAZ GrabaTodo;
         |CIERRAT #0;
         |ACABA;
     |FINSI;

|| \\\\\\ Comprobacion Existencia del Modelo

     |SI enTipoEntrada = 99;              || Existencia del Modelo ????
         |ABRE #0;
         #0#0  = enEmpresa;
         #0#1  = enEjer;
         #0#2  = enPeriodo;
         #0#3  = enModelo;
         #0#4  = enComplem;
         #0#5  = enActividad;
         #0#6  = "            ";
         #0#7  = 0;
         #0#28 = 01;
         |LEE 040#0];
         |SI FSalida ! 0;  |O #0#0 ! enEmpresa;  |O #0#1 ! enEjer; |O #0#2 ! enPeriodo;
          |O #0#3 ! enModelo;  |O #0#4 ! enComplem; |O #0#5 ! enActividad;
             enExistencia = 1;
         |FINSI;
         |CIERRA #0;
     |FINSI;
|FPRO;

|PROCESO Tipo88;  |TIPO 88;
     |HAZ Relaciones;
|FPRC;
