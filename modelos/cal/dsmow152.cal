|FICHEROS;
     dsmoz024 #0;

     dsmo5830 #1;
     dsmow153 #583;
     dsmo5832 #584;
     dsmow152 #999,MANTE;

    :/basica/def/agifigen #1000;
|FIN;

|VARIABLES;
     aAlfa       = "";
     aAlfa1      = "";
     nPara1      = 0;
     nBoton1     = 0;
     nBoton2     = 0;
     nBoton3     = 0;
     nBoton4     = 0;
     nBoton8     = 0;
     nTecla      = 0;

     nEmpresa    = 0;
     nEstado     = 0;
     nSwError    = 0;
     nEmpAnt     = 0;
     nEjerAnt    = 0;
     nEjer       = 0;
     aDelg       = "";
     &enDesdeAlta = 0;
     &eaNomOrigen = "";
     &eaNifOrigen = "";
     &enPrvOrigen = 0;
     &eaPrvOrigen = "";
     &eaDelOrigen = "";
     &enSw999     = 0;

    &eaTabla   = "";
    &eaAlfa    = "";
    &eaDestino = "";
    &enComp    = 0;
    &enPosicion = 0;
    &enError    = 0;
|FIN;

|INCLUYE i_variar;
|INCLUYE i_varcon;

|| ////////////////////////////////////
|PROCESO Delegacion7; |TIPO 7;
     eaTabla = "ztabla12.txt"; |HAZ CargaTabla;
     |MENSA "    <F2> Consulta Delegaciones ";
|FPRC;

|PROCESO Delegacion0;
     |BLIN 4;

     |SI FSalida = 10;
         enPosicion = 25;
         |HAZ SacaTabla;
         aAlfa  = eaDestino % 105;
         #999#6 = aAlfa; |PINTA #999#6;
     |FINSI;

     enComp = 5;
     eaAlfa = #999#6;
     |HAZ CompruebaTabla;

     |SI enError = 1;
         |MENAV "0000 Contenido Incorrecto. Consulta la Tabla con F2";
         |ERROR;
         |PULSATECLA;
         |ACABA;
     |FINSI;
     #999#6 = eaDestino % 105;

     |HAZ FinTabla;
|FPRC;

|| ////////////////////////////////////
|PROCESO Boton1;
     |PTEC 824;
|FPRC;

|PROCESO Boton2;
     |PTEC 825;
|FPRC;

|PROCESO Boton3;
     |PTEC 826;
|FPRC;

|PROCESO Boton4;
     |PTEC 827;
|FPRC;

|PROCESO Boton8;
     |PTEC 830;
|FPRC;

|PROCESO Tipo0;  |TIPO 0;
     |C_M #999Campo,0,aAlfa1;
     |SI aAlfa1 = "N"; |ACABA; |FINSI;

     |FIN_CONTROL_WINDOWS nBoton1;
     |FIN_CONTROL_WINDOWS nBoton2;
     |FIN_CONTROL_WINDOWS nBoton3;
     |FIN_CONTROL_WINDOWS nBoton4;
     |FIN_CONTROL_WINDOWS nBoton8;
     |PULSATECLA;
|FPRC;

|CALCULO LeelaEmpresa;
  || Lee Empresa
    nSwError = 0;
    |ABRE #1000;
    #1000#0 = enEmpresa;
    |LEE 041#1000=;
    |SI FSalida ! 0;
        |MENAV "     Empresa Inexistente";
        |CIERRA #1000;
        nSwError = 1;
    |SINO;
        #999#2 = #1000#1;
        #999#3 = #1000#13;
        #999#4 = #1000#9;
        #999#5 = #1000#12;
    |FINSI;
    |CIERRA #1000;
    |ABRE #583;
    #583#0 = enEmpresa;
    #583#1 = #0#0;
    #583#2 = 03;
    |LEE 041#583];
    |SI FSalida = 0; |Y #583#0 = enEmpresa; |Y #583#1 = #0#0;
        #999#6 = #583#5;
    |FINSI;
    |CIERRA #583;
    aDelg = #999#6; |QBF aDelg;
    |SI aDelg = "";
        aDelg = (("00" + #999#4) % -102) + "600";
        #999#6 = aDelg;
    |FINSI;
|FCAL;

|| ////////////////////////////////////
|PROCESO Borrolin584;
   |BORRA 020#584a;
   |LIBERA #584;
|FPRC;

|DEFBUCLE borrarLineas584;
    #584#1;
    ;
    #583#0, #583#1, #583#2, "                         ";
    #583#0, #583#1, #583#2, "zzzzzzzzzzzzzzzzzzzzzzzzz";
    be;
    NULL, Borrolin584;
|FIN;

|PROCESO BorraElemento;
   |BUCLE borrarLineas584;
   |BORRA 020#583a;
   |LIBERA #583;
|FPRC;

|DEFBUCLE borrarElementos;
    #583#1;
    ;
    #999#0, #999#1, 00;
    #999#0, #999#1, 99;
    be;
    NULL, BorraElemento;
|FIN;

|| **********************************************************************
|RUTINA ClaveBaja583;
     #583#0 = #999#0;
     #583#1 = #999#1;
     #583#2 = 00;
|FRUT;

|RUTINA ClaveAlta583;
     #583#0 = #999#0;
     #583#1 = #999#1;
     #583#2 = 99;
|FRUT;

|PROCESO Tipo7;  |TIPO 7;
     |C_M #999Campo,0,aAlfa1;
     |SI aAlfa1 = "N"; |ACABA; |FINSI;

     |PINTA 0714, #999#0;
     |PINTA 0814, #999#1; |PINTA #999#2;
                          |PINTA #999#3;
                          |PINTA #999#4;
                          |PINTA #999#5;
                          |PINTA #999#6;

     eaNomOrigen = #999#2;
     eaNifOrigen = #999#3;
     enPrvOrigen = #999#4;
     eaPrvOrigen = #999#5;
     eaDelOrigen = #999#6;

     |ENTLINEAL #1#583, -3, 7, 22, 0, ClaveBaja583, ClaveAlta583;

     |BLIN 04;

     |SI #0#19 = 1;
         aAlfa1= "LBOTON,F2 Seleccionar Empresa ";
     |SINO;
         aAlfa1= "LBOTON,F2 Seleccionar Ejercicio ";
     |FINSI;
     |CONTROL_SIMPLE 0, aAlfa1, 1033, 1053, Boton1;
     nBoton1 = FSalida;

     |CONTROL_SIMPLE 0, "LBOTON,F3 Entrada al Lineal", 1133, 1153, Boton2;
     nBoton2 = FSalida;

     |CONTROL_SIMPLE 0, "LBOTON,F4 Entrada Instalaciones", 1233, 1253, Boton3;
     nBoton3 = FSalida;

     |CONTROL_SIMPLE 0, "LBOTON,F5 Codigo Dependiencia", 1155, 1175, Boton4;
     nBoton4 = FSalida;

     |SI #0#19 = 1;
         aAlfa1 = "LBOTON,F8 Baja Empresa-Modelo";
     |SINO;
         aAlfa1 = "LBOTON,F8 Baja Ejercicio-Modelo";
     |FINSI;
     |CONTROL_SIMPLE 0, aAlfa1, 1255, 1275, Boton8;
     nBoton8 = FSalida;
|FPRC;

|PROCESO ActualDelega;
   #583#5 = #999#6;
   |GRABA 020#583a;
   |LIBERA #583;
|FPRC;

|DEFBUCLE ActualDelega;
   #583#1;
   ;
   #999#0, #999#1,00;
   #999#0, #999#1,99;
   be;
   NULL, ActualDelega;
|FIN;

|PROCESO DefPeriodo;
         |SI #583#2 =  3; #583#4 = "1T"; |FINSI;
         |SI #583#2 =  6; #583#4 = "2T"; |FINSI;
         |SI #583#2 =  9; #583#4 = "3T"; |FINSI;
         |SI #583#2 = 12; #583#4 = "4T"; |FINSI;
         |SI #583#2 = 99; #583#4 = "0A"; |FINSI;
|FPRC;

|PROCESO AltaTodos;
    |ABRE #583;
    |PARA nPara1 = 3; |SI nPara1 [ 99; |HACIENDO nPara1 = nPara1 + 3;
          #583#0 = #999#0;
          #583#1 = #999#1;
          #583#2 = nPara1;
          |LEE 041#583=;
          |SI FSalida ! 0;
              |DDEFECTO #583;
              #583#0 = #999#0;
              #583#1 = #999#1;
              #583#2 = nPara1;
              |HAZ DefPeriodo;
              #583#5 = #999#6;
              |GRABA 020#583n;
              enDesdeAlta = 1;
          |FINSI;
          |LIBERA #583;
          |SI nPara1 = 12; nPara1 = 96; |FINSI;
     |SIGUE;
     |CIERRA #583;
|FPRC;

|PROCESO VerPrimero;
    enDesdeAlta = 0;
    |HAZ AltaTodos;
    |SI enDesdeAlta = 1;
        |PTEC 826;
    |FINSI;
|FPRC;

|PROCESO Tipo11;  |TIPO 11;
     nTecla = FSalida;
     |FIN_CONTROL_WINDOWS nBoton1;
     |FIN_CONTROL_WINDOWS nBoton2;
     |FIN_CONTROL_WINDOWS nBoton3;
     |FIN_CONTROL_WINDOWS nBoton4;
     |FIN_CONTROL_WINDOWS nBoton8;
     |PULSATECLA;

         enEmpresa   = #999#0;
         enEjer      = #999#1;
         eaNomOrigen = #999#2;
         eaNifOrigen = #999#3;
         enPrvOrigen = #999#4;
         eaPrvOrigen = #999#5;
         eaDelOrigen = #999#6;

     |SI nTecla =  11;
         |HAZ MiraBloqueo;
         |SI eaBloqueo = "3";
             |ACABA;
         |FINSI;

         |HAZ VerPrimero;
         |ENTLINEAL #1#583, -3, 4, 22, 0, ClaveBaja583, ClaveAlta583;
     |FINSI;

     |SI nTecla = 16;
         |CONFI "    NEsta Seguro de Borrar todos los Modelos 583 de esta Empresa y Ejercicio ";
         |SI FSalida ! 0;  |ACABA;  |FINSI;

         |BUCLE borrarElementos;
         |BORRA 020#999a;

         |PTEC 809;
         |PTEC 808;
         |PONCONTROL  23, "SI";
     |FINSI;

     |SI nTecla = 10; |Y Campo = 0;

         nEmpAnt = #999#0;

         FSalida = 10;
         eaElProm = "dsmow152";
         |HAZ ConsuCliente9;
         nEmpresa = #999#0;
         eaElProm = "";

         #999#0    = nEmpresa;
         #999#1    = #0#0;
         enEmpresa = #999#0;
         |LEE 040#999=;
         nEstado = FSalida;
         |SI nEstado ! 0;
             |DDEFECTO #999;
             #999#0    = nEmpresa;
             #999#1    = #0#0;
         |FINSI;
         |HAZ LeelaEmpresa;
         |SI nSwError = 0;
             |SI nEstado ! 0;
                |PINTA 0714, #999#0;
                |PINTA 0814, #999#1; |PINTA #999#2; |PINTA #999#3;
                                     |PINTA #999#4; |PINTA #999#5;
                                     |PINTA #999#6;
                |CONFI "    SDesea dar de Alta el Modelo 583";
                |SI FSalida = 0;
                    |GRABA 020#999n;
                |SINO;
                    #999#0 = nEmpAnt;
                    #999#1 = #0#0;
                    |LEE 040#999=;
                    nSwError = 1;
                 |FINSI;
             |FINSI;
         |FINSI;

         |SI nSwError = 0;
             |PTEC 825;
         |FINSI;

         |PONCONTROL  23, "SI";
     |FINSI;

     |SI nTecla = 10; |Y Campo = 1;

        nEjerAnt = #999#1;
        |PUSHV 1013 1231;
        |CUADRO 1013 1231;
        |ATRI I;
        |PINTA 1114, " Ejercicio:      ";
        E_inf  = 2013;
        E_sup  = 2099;
        nEjer = enEjer;
        nEjer = 1126 ? 1;
        |POPV;

         #999#0 = #0#1;
         #999#1 = nEjer;
         |LEE 040#999=;
         nEstado = FSalida;

         |SI nEstado ! 0;
            |PINTA 0714, #999#0;
            |PINTA 0814, #999#1; |PINTA #999#2;
                                 |PINTA #999#3;
                                 |PINTA #999#4;
                                 |PINTA #999#5;
                                 |PINTA #999#6;
            |CONFI "    SDesea dar de Alta el Modelo 583";
            |SI FSalida = 0;
                |GRABA 020#999n;
            |SINO;
                #999#0 = #0#1;
                #999#1 = nEjerAnt;
                |LEE 040#999=;
                nSwError = 1;
            |FINSI;
         |FINSI;

         |SI nSwError = 0;
             |PTEC 825;
         |FINSI;

         |PONCONTROL  23, "SI";
     |FINSI;

     |SI nTecla = 12;

         |SUB_EJECUTA_NP "dsmo5830";

         |PONCONTROL  23, "SI";
     |FINSI;

     |SI nTecla = 13;
         |LEE 041#999=;

         |C_M #999#6,1,"S";
         |ENCAMPO #6#999;
         |C_M #999#6,1,"N";

         |GRABA 020#999a;
         |LIBERA #999;

         |BUCLE ActualDelega;

         |PTEC 808;
     |FINSI;
|FPRC;
