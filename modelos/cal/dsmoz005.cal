|FICHEROS;
     dsmoz005 #0;
     dsmom016 #16;
     dsmom017 #17;
     dsmom018 #18;
     dsmow007 #999;
|FIN;

|VARIABLES;
     aFichero  = "";
     aAlfa     = "";

     fFecha    = @;
     nError    = 0;
     nCampo    = 0;
     nDEmpresa = 0;
     nHEmpresa = 0;
     &enBoton1 = 0;
     &enBoton2 = 0;
     &enGraba  = 0;
     &enError15= 0;
|FIN;

|INCLUYE i_variar;

|| /////////////////////// PROCESOS ENTRADA POR PANTALLA \\\\\\\\\\\\\\\\\\\\

|PROCESO Parrilla;
     |SI #0#1 = 0;
         #0#1 = 0;  |PINTA #0#1;
         #0#2 = 0;  |PINTA #0#2;  |C_M #0#2, 1, "N";
         |PARA nCampo = 3;  |SI nCampo [ 14;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |SINO;
         |SI enEmpresa = 0;
             |C_M #0#1, 1, "S";
             |C_M #0#2, 1, "S";
         |FINSI;

         |PARA nCampo = 3;  |SI nCampo [ 14;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "N";
               #0nCampo = 0;  |PINTA #0nCampo;
         |SIGUE;
     |FINSI;
|FPRC;

|PROCESO NoMasEmpresas;
     |SI #0Campo = 0;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 14;  |HACIENDO nCampo = nCampo + 1;
               #0nCampo = 0;  |C_M #0nCampo, 1, "N";
         |SIGUE;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 14;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |FINSI;
|FPRC;

|PROCESO SacaAnyo;  |TIPO 7;
     fFecha = ~;
     aAlfa  = fFecha % -104;
     #0#0   = aAlfa;
     |PINTA #0#0;
|FPRC;

|| /////////////////////// PROCESOS DE TRABAJO \\\\\\\\\\\\\\\\\\\\

|PROCESO GrabaTempo;
     enEmpresaPINET = #17#0;
     |HAZ RestriccionPINET;
     |SI enErrorPINET = 1;  |ACABA;  |FINSI;

     #999#0 = #17#0;
     |LEE 101#999=;
     |SI FSalida ! 0;
         #999#0 = #17#0;
         #999#1 = #17#1;
         #999#2 = #17#7;
         |GRABA 020#999n;

         enGraba = 1;
     |FINSI;
     |LIBERA #999;
|FPRC;

|DEFBUCLE dsmom017;
     #17#1;
     ;
     nDEmpresa, #0#0, 00, 01;
     nHEmpresa, #0#0, 99, 99;
     ;
     NULL, GrabaTempo;
|FIN;

|RUTINA ClaveBaja999;
     #999#0 = 1;
|FRUT;

|RUTINA ClaveAlta999;
     #999#0 = 99999;
|FRUT;

|PROCESO Tipo3;  |TIPO 3;
     aFichero = ("05" + Usuario) % 108;
     |NOME_DAT #999 aFichero;
     |ABRE #999;  |CIERRA #999;  |DELINDEX #999;

     |ABRE #999;
     |SI #0#1 ! 0;
         nDEmpresa = #0#1;
         nHEmpresa = #0#2;
         |BUCLE dsmom017;
     |SINO;
         |PARA nCampo = 3;  |SI nCampo [ 14;  |HACIENDO nCampo = nCampo + 1;
               nDEmpresa = #0nCampo;
               nHEmpresa = #0nCampo;
               |BUCLE dsmom017;
         |SIGUE;
     |FINSI;
     |CIERRA #999;

     |SI enGraba = 1; |Y #0#0 = 2015;
         aAlfa = "dsmoy092;MASIVO";
         |SUB_EJECUTA aAlfa;
         |SI enError15 = 1;
             |CONFI "0000SDesea Continuar";
             |SI FSalida ! 0;
                 |DELINDEX #999;
                 |ACABA;
             |FINSI;
         |FINSI;
     |FINSI;

     |SI enGraba = 1;
         |CONTROL_SIMPLE 0, "BOTON,F3 Emision Linea", 0525, 0540, EmisionLinea;
         enBoton1 = FSalida;

         |CONTROL_SIMPLE 0, "BOTON,F4 Emision Todos", 0542, 0558, EmisionTodo;
         enBoton2 = FSalida;

         |ENTLINEAL #1#999, -1, 4, 22, 1, ClaveBaja999, ClaveAlta999;

         |FIN_CONTROL_WINDOWS enBoton1;
         |FIN_CONTROL_WINDOWS enBoton2;

         |PULSATECLA;
     |SINO;
         |MENAV "     Seleccion Vacia.";
     |FINSI;
     |DELINDEX #999;
|FPRC;

|PROCESO dsmom017V;
     |SI #17#25 = "S";  |ERROR10;  |ACABA;  |FINSI;

     #17#25  = "S";
     #17#26  = "S";

     #18#0 = #17#0;
     #18#1 = #17#1;
     #18#2 = #17#2;
     #18#3 = #17#3;
     |LEE 101#18=;
     |SI FSalida ! 0;
         |DDEFECTO #18;
         #18#0 = #17#0;
         #18#1 = #17#1;
         #18#2 = #17#2;
         #18#3 = #17#3;
         |GRABA 020#18b;
     |FINSI;

     #18#253 = #18#253 < 5;

     |GRABA 020#18a;
     |LIBERA #18;

     |GRABA 020#17a;
     |LIBERA #17;
|FPRC;

|DEFBUCLE dsmom017V;
     #17#1;
     ;
     00000, 2009, 00, 00;
     99999, 2009, 99, 99;
     be;
     NULL, dsmom017V;
|FIN;

|PROCESO Instala;
     eaExtension = "mod";
     eaPrograma  = "dsmom015";
     |SUB_EJECUTA_NP ":basica/dspatron";

     eaExtension = "mod";
     eaPrograma  = "dsmom016";
     |SUB_EJECUTA_NP ":basica/dspatron";

     eaExtension = "mod";
     eaPrograma  = "dsmom033";
     |SUB_EJECUTA_NP ":basica/dspatron";

     eaExtension = "mod";
     eaPrograma  = "dsmom133";
     |SUB_EJECUTA_NP ":basica/dspatron";
|FPRC;

|PROCESO VerPatModulos;
     nError   = 0;

     |ABRE #16;
     #16#0  = "E";
     #16#1  = "4191"
     #16#41 = 1;
     #16#2  = 2020;
     |LEE 040#16=;
     |SI FSalida ! 0;
         nError = 1;
     |FINSI;
     |CIERRA #16;

     |SI nError ! 0;
         |HAZ Instala;

         |ABRE #16;
         #16#0  = "E";
         #16#1  = "4191"
         #16#41 = 1;
         #16#2  = 2020;
         |LEE 040#16=;
         |SI FSalida ! 0;
             |MENAV "0000Fichero patron incorrecto, no estan los Modulos 2020. Pongase en contacto con su admistrador";
             nError = 1;
         |SINO;
             |SI nError = 1;
                 |MENAV "0000Se ha instalado el fichero patron de epigrafes con los modulos del 2020";
             |FINSI;
             nError = 0;
         |FINSI;
         |CIERRA #16;
     |FINSI;
|FPRC;

|PROGRAMA;
     |HAZ VerPatModulos;
     |SI nError = 1; |ACABA; |FINSI;

     |ABRE #18;
     |BUCLE dsmom017V;
     |CIERRA #18;

     |SI enEmpresa ! 0;
         #0#0 = enEjer;
         #0#1 = enEmpresa;
         #0#2 = enEmpresa;
         |HAZ Tipo3;
     |SINO;
         |CLS;
         |CABEZA "Entrada de Planillas";
         |DDEFECTO #0;
         |PINPA #0#0;
         |PINDA #0#0;
         |ENDATOS #1#0;
     |FINSI;

     |HAZ FinalizaEnX;
|FPRO;
