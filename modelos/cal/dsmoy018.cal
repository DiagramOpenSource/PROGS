|FICHEROS;
   dsmoy018;

   dsmom060;
   dsmom061;
   dsmom100;
   dsmom162;

   :/basica/def/agicen14;
   :/basica/def/agifigen;
   :/basica/def/agim0019;

   :/contagen/def/canempre;
   :/contagen/def/camaasil;
   :/contagen/def/camaniva;
   :/contagen/def/cacuenta;
   :/contagen/def/camaasic;
   :/contagen/def/calicont;
   :/contagen/def/caconimp;

   dsmow055;          ||Def de impresion
   dsmow033;
   dsmow054;
   dsmow043,MANTE;
|FIN;

|VARIABLES;

   nSwSiHay       = 0;
   aParam         = "";
   aMonMod        = "";
   informe        = "";
   nInkey         = 0;
   aFichero       = "";
   nCampo         = 0;
   nDEmpresa      = 0;
   nHEmpresa      = 0;
   nDActividad    = 0;
   nHActividad    = 0;
   fDFecha        = @;
   fHFecha        = @;
   nLaEmpresa     = 0;
   nGTempo        = 0;

   nSPin1         = 0;
   nPin1          = 0;
   aPin1          = "";
   efPn10         = @;
   efPn11         = @;
   swExiste       = 0;
   aPath          = "";
   nEjercicio     = 0;
   aAlfa0         = "";
   aAlfa1         = "";
   aAlfa2         = "";
   aAlfa3         = "";
   nPara1         = 0;
   nPara2         = 0;
   nNume1         = 0;
   nNume2         = 0;
   nNume3         = 0;
   nNume4         = 0;
   nLinea         = 0;
   SwHayDatos     = 0;
   nSwCR          = 0;
   aInformeCR     = "";
   fLaFecha       = @;
   aCta           = "";
   aDCta          = "";
   aHCta          = "";
   nSwCol         = 0;
   aSigno         = "";
   aSignoE        = "";
   aCondicion     = "";
   aNomCta        = "";
   aDesCon        = "";
   aCIF           = "";
   nLongitud      = 0;
   nLongPart      = 0;
   nConta         = 0;
   nImporte       = 0;
   nEspDos        = 0;
   nElDepar       = 0;
   swNoExis       = 0;
   fFecha         = @;

   nAsto0         = 0;
   fAsto1         = @;
   nAsto2         = 0;
   aAsto12        = "";
   nAsto13        = 0;
   nAsto14        = 0;
   aAsto15        = "";
   aAsto26        = "";

   {-1}aMenu           = "";
       aMenu1          = "2400";
       aMenu2          = "1";
       aMenu3          = "Archivo documental activado. Elija opci¢n: ";
       aMenu4          = "IAB";
       aMenu5          = "Imprimir";
       aMenu6          = "Archivar";
       aMenu7          = "Ambos";
       nOpcion         = 0;
       nSegurOp        = 0;

   &enErrorDSArchi     = 0;
   &eaVengoDe          = "";
   &enArchi            = 0;
   &eaInforme          = "";
   &enEjercicio        = 0;
   &eaNifEmp           = "";
   &enCodTrab          = 0;
   &eaNombreTra        = "";
   &eaNifTra           = "";
   &eaObservacion1     = "";
   &eaObservacion2     = "";
   &eaObservacion3     = "";
   &enM2               = 0;
   &eSwLinea           = 0;

   &eaImpresora   = "";
   &entrada       = 1;
   &efDFecha      = @;
   &efHFecha      = @;
   &enSwPortada   = 0;
   &enSwResumen   = 0;
   &eaTipoInforme = "";
   &nPagina       = 0;
   &SwPrim        = 0;
   &eaTituloL     = "";
   &eaTitul2      = "";
|FIN;

|INCLUYE i_variar;
|INCLUYE i_varcon;

|| //////////////////// Calculos de Pantalla \\\\\\\\\\\\\\\\\\\\\\\\\\
|| ***************************************************************
|CALCULO QueLibro; |TIPO 0;
 |SI #dsmoy018#0 = "I";
     #dsmoy018#44 = 910;
     #dsmoy018#45 = "LIBRO REGISTROS DE VENTAS E INGRESOS";
     aAlfa1 = "Emision Libro Registro de Ventas e Ingresos";
 |SINO;
     #dsmoy018#44 = 911;
     #dsmoy018#45 = "LIBRO REGISTROS DE COMPRAS Y GASTOS";
     aAlfa1 = "Emision Libro Registro de Compras y Gastos";
 |FINSI;
 |PINTA 0631, "                                             ";
 |ATRI R; |PINTA 0631, aAlfa1; |ATRI 0;
 |PINTA #dsmoy018#44;
 |PINTA #dsmoy018#45;
|FCAL;

|CALCULO Eano; |TIPO 7;
 fLaFecha = ~;
 aAlfa1 = fLaFecha; aAlfa1 = aAlfa1 % -104;
 #dsmoy018#1 = aAlfa1;
 |PINTA #dsmoy018#1;
|FCAL;

|CALCULO losDefectos; |TIPO 0;
 aAlfa1 = #dsmoy018#1;
 aAlfa2 = "01.01." + aAlfa1; #dsmoy018#39 = aAlfa2; |PINTA #dsmoy018#39;
 aAlfa2 = "31.12." + aAlfa1; #dsmoy018#40 = aAlfa2; |PINTA #dsmoy018#40;
|FCAL;

|CALCULO Tipo7;  |TIPO 7;
     |SI enEmpresa ! 0;
         #dsmoy018#2  = enEmpresa;  |PINTA #dsmoy018#2;
         #dsmoy018#3  = enEmpresa;  |PINTA #dsmoy018#3;

         |C_M #dsmoy018#2, 1, "N";
         |C_M #dsmoy018#3, 1, "N";
     |FINSI;
|FCAL;

|CALCULO Parrilla; |TIPO 0;
     |SI #dsmoy018#2 = 0;
         #dsmoy018#2 = 0;  |PINTA #dsmoy018#2;
         #dsmoy018#3 = 0;  |PINTA #dsmoy018#3;  |C_M #dsmoy018#3, 1, "N";
         |PARA nCampo = 4;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
               |C_M #dsmoy018#nCampo#, 1, "S";
         |SIGUE;
     |SINO;
         |SI enEmpresa = 0;
             |C_M #dsmoy018#2, 1, "S";
             |C_M #dsmoy018#3, 1, "S";
         |FINSI;

         |PARA nCampo = 4;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
               |C_M #dsmoy018#nCampo#, 1, "N";
               #dsmoy018#nCampo# = 0;  |PINTA #dsmoy018#nCampo#;
         |SIGUE;
     |FINSI;
|FCAL;

|CALCULO NoMasEmpresas; |TIPO 0;
     |SI #dsmoy018#Campo# = 0;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
               #dsmoy018#nCampo# = 0;  |C_M #dsmoy018#nCampo#, 1, "N";
         |SIGUE;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
               |C_M #dsmoy018#nCampo#, 1, "S";
         |SIGUE;
     |FINSI;
|FCAL;

|CALCULO YaSeCual; |TIPO 7;
 |SI enEmpresa ! 0;
     #dsmoy018#48 = "S"; |PINTA #dsmoy018#48;
     #dsmoy018#24 = "S"; |PINTA #dsmoy018#24;
     #dsmoy018#25 = 00;  |PINTA #dsmoy018#25;
     #dsmoy018#26 = 99;  |PINTA #dsmoy018#26;
     |C_M #dsmoy018#24, 1, "N";
     |C_M #dsmoy018#25, 1, "N";
     |C_M #dsmoy018#26, 1, "N";
     |C_M #dsmoy018#48, 1, "N";
 |FINSI;
|FCAL;

|CALCULO SelTipo0; |TIPO 0;
 |SI #dsmoy018#48 = "N";
     #dsmoy018#25 = 0;  |PINTA #dsmoy018#25;  |C_M #dsmoy018#25, 1, "N";
     #dsmoy018#26 = 99;  |PINTA #dsmoy018#26;  |C_M #dsmoy018#26, 1, "N";
     |PARA nCampo = 27;  |SI nCampo [ 36;  |HACIENDO nCampo = nCampo + 1;
           |C_M #dsmoy018#nCampo#, 1, "S";
     |SIGUE;
 |SINO;
     |SI enEmpresa = 0;
         |C_M #dsmoy018#25, 1, "S";
         |C_M #dsmoy018#26, 1, "S";
     |FINSI;
     |PARA nCampo = 27;  |SI nCampo [ 36;  |HACIENDO nCampo = nCampo + 1;
           |C_M #dsmoy018#nCampo#, 1, "N";
           #dsmoy018#nCampo# = 0;  |PINTA #dsmoy018#nCampo#;
     |SIGUE;
 |FINSI;
|FCAL;

|CALCULO SelTipo1;
 |SI #dsmoy018#Campo# = 0;
     |PARA nCampo = Campo + 1;  |SI nCampo [ 36;  |HACIENDO nCampo = nCampo + 1;
           #dsmoy018#nCampo# = 0;  |C_M #dsmoy018#nCampo#, 1, "N";
     |SIGUE;
 |SINO;
     |PARA nCampo = Campo + 1;  |SI nCampo [ 36;  |HACIENDO nCampo = nCampo + 1;
           |C_M #dsmoy018#nCampo#, 1, "S";
     |SIGUE;
 |FINSI;
|FCAL;

|CALCULO fecha; |TIPO 0;
 aAlfa1 = #dsmoy018#1;
 aAlfa2 = "01.01." + aAlfa1;
 aAlfa3 = "31.12." + aAlfa1;
 |SI #dsmoy018#Campo# < aAlfa2; |O #dsmoy018#Campo# > aAlfa3;
     |MENAV "    Error. Fecha Incorrecta ";
     |ERROR;
 |SINO;
     |SI Campo = 40; |Y #dsmoy018#39 > #dsmoy018#40;
         |MENAV "    Error. Fecha Incorrecta ";
         |ERROR;
     |FINSI;
 |FINSI;
|FCAL;

|CALCULO SelAct; |TIPO 0;
 aAlfa1 = #dsmoy018#Campo#;
 |C_M #dsmoy018#50,1, aAlfa1;
 |C_M #dsmoy018#51,1, aAlfa1;
|FCAL;

|CALCULO Cuadro; |TIPO 7;
   |PUSHV  1512 1959;
   |CUADRO 1512 1959;
   |ATRI R;
   |PINTA 1513, " -------------- FORMATO LIBRO --------------- ";
   |SI #dsmoy018#0 = "I";
       |PINTA 1613, " (1) Descripcion de Factura y Cuenta Contable ";
   |SINO;
       |PINTA 1613, " (1) Descripcion de Factura                   ";
   |FINSI;
   |PINTA 1713, " (2) Descripcion apuntes.                     ";
   |PINTA 1813, " (9) Formato Crystal Reports                  ";
   |ATRI r;
|FCAL;

|CALCULO SalCuadro; |TIPO 0;
   |SI #dsmoy018#52 > 2; |Y #dsmoy018#52 < 9; |ERROR; |ACABA; |FINSI;

   |POPV;
   |PINTA #dsmoy018#52;
|FCAL;
|| **********************************************************************************
|| **********************************************************************************
|| .................... graba Contabilidad
|PROCESO LEmpresaConta;

 enEmpresa  = #canempre#2;
 enPerConta = #canempre#3;   || Periodo Contable

 |SI #dsmoy018#24 = "N"; |Y #canempre#21 = "I"; |ACABA; |FINSI;

|| ------------------------------------------------------------------------
   aAlfa1 = enEmpresa;  aAlfa1 = ("00000" + aAlfa1) % -105;
   aAlfa2 = enPerConta; aAlfa2 = ("0" + aAlfa2) % -101;
   aPathConta  = aPath + "contagen/fich/" + aAlfa1 + aAlfa2 + "/";

   nGTempo = nGTempo + 1;
   #dsmow043#0 = nGTempo;       || Guia
   #dsmow043#1 = enEmpresa;     || Empresa
   #dsmow043#2 = enPerConta;    || Periodo Contable
   #dsmow043#3 = enEjer;        || A¤o
   #dsmow043#4 = #canempre#11;  || Desde Mes
   #dsmow043#5 = #canempre#13;  || N§ Digitos
   #dsmow043#6 = #canempre#14;  || Digitos Nivel 1
   #dsmow043#7 = #canempre#15;  || Digitos Nivel 2
   #dsmow043#8 = #canempre#16;  || Digitos Nivel 3
   #dsmow043#9 = #canempre#17;  || Digitos Nivel 4
   #dsmow043#10 = #canempre#18; || Digitos Nivel 5
   #dsmow043#11 = #canempre#19; || Digitos Nivel 6
   eaEstadoEmp = "S"; |SI #canempre#21 = "I"; eaEstadoEmp = "N"; |FINSI;
   #dsmow043#12 = eaEstadoEmp;  || Estado
   #dsmow043#13 = #canempre#28; || Moneda
   #dsmow043#14 = #canempre#1;  || Nombre Empresa
   #dsmow043#15 = aPathConta;   || Path
   #dsmow043#16 = #dsmoy018#43;        || Emitir
   |GRABA 020#dsmow043.n;
|FPRC;

|PROCESO LEmpresaEOS;
 enEmpresa  = #agifigen#0;
 enPerConta = 0;

 eaEstadoEmp = "S";
 |SI #agifigen#94 > "01.01.0000";
     eaEstadoEmp = "N";
     |SI #dsmoy018#24 = "N"; |ACABA; |FINSI;
 |FINSI;

|| ... Seleccion de Tipo de Empresa
  nSPin1 = #agifigen#87;
  enSwSelEmp = 1;
  |SI #dsmoy018#48 = "S";
      |SI nSPin1 ] #dsmoy018#25; |Y nSPin1 [ #dsmoy018#26;
          enSwSelEmp = 0;
      |FINSI;
  |SINO;
      |SI nSPin1 = #dsmoy018#27;
          enSwSelEmp = 0;
      |SINO;
          |PARA nCampo = 27;  |SI nCampo [ 36;  |HACIENDO nCampo = nCampo + 1;
                |SI nSPin1 = #dsmoy018#nCampo#; |Y nSPin1 ! 0;
                    enSwSelEmp = 0;
                    |SAL;
                |FINSI;
          |SIGUE;
      |FINSI;
  |FINSI;
  |SI enSwSelEmp = 1; |ACABA; |FINSI;
|| ------------------------------------------------------------------------

       |SELECT #4#canempre;
       #canempre#2  = enEmpresa;
       #canempre#26 = nEjercicio;
       |LEE 001#canempre.=;
       |SI FSalida = 0;
           |HAZ LEmpresaConta;
       |FINSI;
       |SELECT #1#canempre;
|FPRC;

|DEFBUCLE agifigen;
 #agifigen#1;
 ;
 nDEmpresa;
 nHEmpresa;
 e;
 NULL, LEmpresaEOS;
|FIN;

||///////////////////////////////////////////////////////////////////
|CALCULO Datos7; |TIPO 7;
   |MENSA "    <F3> Cambiar Emitir Libro S/N";
   |ATRI R; |PINTA 1169, "Imp"; |ATRI 0;
|FCAL;

|CALCULO DatosManip; |TIPO 11;
   nInkey = FSalida;
   |BLIN 04;

   |SI nInkey = 11;
       |SI #dsmow043#16 = "S";
           #dsmow043#16 = "N";
       |SINO;
           #dsmow043#16 = "S";
       |FINSI;
       |GRABA 020#dsmow043.a;
   |FINSI;
|FCAL;

|RUTINA inferior;
   #dsmow043#0 = 00001;
|FRUT;

|RUTINA superior;
   #dsmow043#0 = 99999;
|FRUT;

||///////////////////////////////////////////////////////////////////
|CALCULO Tipo3;  |TIPO 3;

    enArchi = 0;
    |SI #dsmoy018#52 = 9;
        |SI #dsmoy018#0 = "I";  eaVengoDe = "dsmoyi18"; |FINSI;
        |SI #dsmoy018#0 = "G";  eaVengoDe = "dsmoyg18"; |FINSI;
          |HAZ MiraSiArchiva;
    |FINSI;

    nOpcion = 1;
    |SI enArchi = 1;
        |MENU aMenu;
        nOpcion = FSalida;
        |SI nOpcion < 0;
            |ACABA;
        |FINSI;
    |FINSI;

    enModelo = #dsmoy018#44;
    |HAZ LeeCtrlLibro;

    enEjer = #dsmoy018#1;
    |HAZ Verm060Plan;
    |SI enPlan60 = 0; |ACABA; |FINSI;

    |ABRE #dsmom162;
    |DDEFECTO #dsmom162;
    #dsmom162#0 = enPlan60;
    |LEE 041#dsmom162.=;
    |CIERRA #dsmom162;

    aAlfa1 = enEjer; |QBF aAlfa1;
    aAlfa1 = aAlfa1 % -102;
    nEjercicio = aAlfa1;
    |DBASS aPath; |QBT aPath;

    aFichero = ("018y" + Usuario) % 108;
    |NOME_DAT #dsmow033#aFichero#;
    aFichero = ("018i" + Usuario) % 108;
    |NOME_DAT #dsmow054#aFichero#;

    aFichero = ("043w" + Usuario) % 108;
    |NOME_DAT #dsmow043#aFichero#;
    |DELINDEX #dsmow043;
    eaFEmpresa = aFichero;

    |ABRE #dsmow043;

    nGTempo = 0;   ||Busco Empresas Contables
    |ABRE #canempre;
    |SI #dsmoy018#2 ! 0;
        nDEmpresa = #dsmoy018#2;
        nHEmpresa = #dsmoy018#3;
        |BUCLE agifigen;
    |SINO;
        |PARA nPara1 = 4;  |SI nPara1 [ 23;  |HACIENDO nPara1 = nPara1 + 1;
              |SI #dsmoy018#nPara1# ! 0;
                  nDEmpresa = #dsmoy018#nPara1#;
                  nHEmpresa = #dsmoy018#nPara1#;
                  |BUCLE agifigen;
              |FINSI;
        |SIGUE;
    |FINSI;
    |CIERRA #canempre;
    |CIERRA #dsmow043;

    |SI nGTempo = 0;    || No existe ninguna empresa
        |MENAV "    Error. No existe ninguna Empresa dentro de la Seleccion ";
        |DELINDEX #dsmow043;
        |ACABA;
    |FINSI;

    |C_V #dsmow043#12,1,"N"; |C_P #dsmow043#16,1,1270;

    |PUSHV 0509 2375;
    |ENTLINEAL #1#dsmow043, -1, 2, 22, 1, inferior, superior;
    |POPV;

    efDFecha  = #dsmoy018#39;
    efHFecha  = #dsmoy018#40;

    nSegurOp = nOpcion;
    |SI nSegurOp = 1; |HAZ Imprimir; |FINSI;
    |SI nSegurOp = 2; |HAZ Archivar; |FINSI;
    |SI nSegurOp = 3; |HAZ Imprimir; |FINSI;
    |SI nSegurOp = 3; |HAZ Archivar; |FINSI;

    |DELINDEX #dsmow043;
    |DELINDEX #dsmow033;
    |DELINDEX #dsmow054;
|FCAL;

|| *************************************************************************
|| *************************************************************************
|| *************************************************************************
|PROCESO CargaVar;
     eaObservacion1 = "";
     aAlfa1 = #dsmoy018#40;
     aAlfa1 = aAlfa1 % 402;
     NumMes   = aAlfa1;
     |HAZ NombreMes;
     eaObservacion1 = "Al mes " + NombreMes;
     eaObservacion1 = eaObservacion1 > " ";

     eaNombreEmp = #dsmow055#2;
     eaNifEmp    = #dsmow055#1;
     enEjercicio = #dsmow055#12;
     enM2        = NumMes;
|FPRC;

|PROCESO Lee60;
 enClAgrupa = 0;
 |DDEFECTO #dsmom060;
 #dsmom060#6 = enPlan60;
 #dsmom060#0 = #dsmoy018#46;
 #dsmom060#1 = #dsmoy018#0;
 #dsmom060#2 = nSwCol;
 |LEE 001#dsmom060.=;
 |SI FSalida = 0;
     aAlfa1 = #dsmom060#4; |QBF aAlfa1;
     |SI aAlfa1 = "";
         #dsmom060#4 = #dsmom060#3;
     |FINSI;
      eaClave60 = #dsmom060#7;
      eaTip60   = #dsmom060#1;
      |HAZ eClaveAgrupa;
 |FINSI;
|FPRC;

|PROCESO LeeAuxi;
  |SI #dsmow054#1 ! nAsto0;
      nConta   = nConta + 1;
      nAsto0   = #dsmow054#1;
      eSwLinea = 0;
  |FINSI;
  #dsmow054#3 = nConta;
  |PRINT;
  |PARA nNume1 = 9; |SI nNume1 [ 22; |HACIENDO nNume1 = nNume1 + 1;
        nNume2 = nNume1 + 16;
        #dsmow055#nNume2# = #dsmow055#nNume2# + #dsmow054#nNume1#;
  |SIGUE;
  SwPrim   = 1;
  eSwLinea = 1;
|FPRC;

|DEFBUCLE LeeAuxi;
 #dsmow054#1;
 ;
 INICIO;
 FINAL;
 ;
 NULL, LeeAuxi;
|FIN;

|PROCESO ImpAuxiliar;
 |PARA nNume1 = 39; |SI nNume1 [ 52; |HACIENDO nNume1 = nNume1 + 1;
       nSwCol = nNume1 - 38;
       nNume2 = nNume1 + 14;
       |HAZ Lee60;
       #dsmow055#nNume1# = #dsmom060#4;
       #dsmow055#nNume2# = #dsmom060#5;

       aAlfa1 = #dsmow055#nNume2#; |QBF aAlfa1;
       |SI aAlfa1 = "";
           #dsmow055#nNume2# = #dsmow055#nNume1#; #dsmow055#nNume1# = "";
       |FINSI;
       |SI #dsmoy018#0 = "G"; |Y #dsmoy018#47 = 1;
           aAlfa1 = #dsmow055#nNume1#; |QBF aAlfa1;
           aAlfa1 = aAlfa1 % 111;
           aAlfa1 = ("           " + aAlfa1 ) % -111;
           #dsmow055#nNume1# = aAlfa1;
           aAlfa1 = #dsmow055#nNume2#; |QBF aAlfa1;
           aAlfa1 = aAlfa1 % 111;
           aAlfa1 = ("           " + aAlfa1 ) % -111;
           #dsmow055#nNume2# = aAlfa1;
       |SINO;
           aAlfa1 = #dsmow055#nNume1#; |QBF aAlfa1;
           aAlfa1 = aAlfa1 % 114;
           aAlfa1 = ("               " + aAlfa1 ) % -114;
           #dsmow055#nNume1# = aAlfa1;
           aAlfa1 = #dsmow055#nNume2#; |QBF aAlfa1;
           aAlfa1 = aAlfa1 % 114;
           aAlfa1 = ("               " + aAlfa1 ) % -114;
           #dsmow055#nNume2# = aAlfa1;
       |FINSI;
 |SIGUE;

      |SI #dsmoy018#0 = "I";
          informe = "dsmoyi18";
          |SI #dsmoy018#52 = 2; informe = "dsmoyn18"; |FINSI;
          |SI #dsmoy018#1 ] 2019;
              informe = "ds9oyi18";
              |SI #dsmoy018#52 = 2; informe = "ds9yn18"; |FINSI;
          |FINSI;
      |SINO;
          informe = "dsmoyg18";
          |SI #dsmoy018#1 ] 2019; informe = "ds9oyg18"; |FINSI;
      |FINSI;

      |SI #caconimp#4 = "N"; |Y #dsmoy018#1 < 2019;
           aAlfa1  = informe % 306;
           informe = "dr" + aAlfa1;
      |FINSI;

      |SI nSwCR = 1; informe = aInformeCR; |FINSI;

      |SI nOpcion = 1;
          |INFOR informe;
          |SI FSalida ! 0; |ACABA;  |FINSI;
      |FINSI;
      |SI nOpcion = 2;
          eaInforme = informe;
          |HAZ IMPREArchi;
      |FINSI;

      SwPrim = 0;
      nConta = 0;
      nAsto0 = -1;
      |PDEFECTO #dsmow055, 25, 39;
      |BUCLE LeeAuxi;
      |SI SwPrim = 1;
          |PIEINF;
          nPagina = nPagina + 1;
      |FINSI;
      |FININF;

      |SI nOpcion = 2;
          |FINIMP;
          |SI enErrorDSArchi = 0; |Y SwPrim = 1;
              |HAZ CargaVar;
              |HAZ Archivando;
          |FINSI;
      |FINSI;

  |SI nOpcion = 2;  Impresora = "CrystalReports"; |FINSI;
|FPRC;

|| ////////////////////////////////
|PROCESO LeeAuxiR;
  nSwCol = #dsmow033#2;
  |HAZ Lee60;
  |PRINT;
  SwPrim = 1;
|FPRC;

|DEFBUCLE LeeAuxiR;
 #dsmow033#1;
 ;
 #dsmoy018#46, #dsmoy018#0, 00;
 #dsmoy018#46, #dsmoy018#0, 99;
 ;
 NULL, LeeAuxiR;
|FIN;

|PROCESO ImpAuxiliarR;

 informe = "dsmoyr18";
 |INFOR informe;
 |SI FSalida ! 0;  |ACABA;  |FINSI;

 eaTitul2 = "GASTOS FISCALMENTE DEDUCIBLES";
 |SI #dsmoy018#0 = "I";
     eaTitul2 = "CONCEPTO DE VENTAS E INGRESOS";
 |FINSI;

 SwPrim = 0;
 |BUCLE LeeAuxiR;
 |SI SwPrim = 1;
     |PIEINF;
 |FINSI;
 |FININF;
|FPRC;

|| /////////////////////////////////////////////////////////////////
|PROCESO Graba_w33;
 #dsmow033#0 = #dsmoy018#46;
 #dsmow033#1 = #dsmoy018#0;
 #dsmow033#2 = nSwCol;
 |LEE 001#dsmow033.=;
 |SI FSalida ! 0;
     |DDEFECTO #dsmow033;
     #dsmow033#0 = #dsmoy018#46;
     #dsmow033#1 = #dsmoy018#0;
     #dsmow033#2 = nSwCol;
     #dsmow033#3 = enClAgrupa;
     |GRABA 020#dsmow033.c;
 |FINSI;

 #dsmow033#7 = #dsmow033#7 + nImporte;
 |GRABA 020#dsmow033.a;
|FPRC;

|PROCESO BuscoFra;
     aNomCta = #camaniva#13;
     aDesCon = #camaniva#28;
     aCIF    = #camaniva#14;
     nSwSiHay = 1;
|FPRC;

|DEFBUCLE BuscoFra;
 #camaniva#3;
 7,8,9;
 #camaasil#13, #camaasil#15, #camaasil#14, #camaasil#0, #camaasil#1, #camaasil#2;
 #camaasil#13, #camaasil#15, #camaasil#14, #camaasil#0, #camaasil#1, #camaasil#2;
 e;
 NULL, BuscoFra;
|FIN;

|PROCESO LeeCta;
 aNomCta = "";
 aDesCon = "";
 aCIF    = "";
 nSwSiHay = 0;
 |BUCLE BuscoFra;
 |SI nSwSiHay = 0;
     aCta = "";
     |ABRE #calicont;
     #calicont#0 = #camaasil#0;
     #calicont#1 = #camaasil#1;
     #calicont#2 = #camaasil#2;
     #calicont#3 = #camaasil#3;
     |LEE 001#calicont.=;
     |SI FSalida = 0;
         aCta = #calicont#4;
     |SINO;
         |ABRE #camaasic;
         #camaasic#0 = #camaasil#0;
         #camaasic#1 = #camaasil#1;
         #camaasic#2 = #camaasil#2;
         |LEE 001#camaasic.=;
         |SI FSalida = 0;
             |SI #camaasil#8 = "D"; aCta = #camaasic#10; |FINSI;
             |SI #camaasil#8 = "H"; aCta = #camaasic#8;  |FINSI;
         |FINSI;
         |CIERRA #camaasic;
     |FINSI;
     |CIERRA #calicont;
     |QBF aCta;
     |SI aCta = ""; aCta = #camaasil#4; |FINSI;

     |ABRE #cacuenta;
     #cacuenta#0  = aCta;
     |LEE 001#cacuenta.=;
     |SI FSalida = 0;
         aNomCta = #cacuenta#2;
     |FINSI;
     |CIERRA #cacuenta;
     aDesCon = #camaasil#7;
 |FINSI;

 |SI #dsmoy018#52 = 2;
     aDesCon = #camaasil#7;
 |FINSI;
|FPRC;

|PROCESO Graba_w54;

  |SI nConta = 0;
      nAsto0  = #camaasil#0;
      fAsto1  = #camaasil#1;
      nAsto2  = #camaasil#2;
      aAsto12 = #camaasil#12;
      nAsto13 = #camaasil#13;
      nAsto14 = #camaasil#14;
      aAsto15 = #camaasil#15;
      aAsto26 = #camaasil#26;
      nConta  = 1;
  |FINSI;

  |DDEFECTO #dsmow054;
  |SI nAsto0 ! #camaasil#0; |O fAsto1 ! #camaasil#1; |O nAsto2 ! #camaasil#2;
     |O aAsto12 ! #camaasil#12; |O nAsto13 ! #camaasil#13; |O nAsto14 ! #camaasil#14;
       |O aAsto15 ! #camaasil#15;
      nConta = nConta + 1;
  |FINSI;

  nLinea = 1;
  |SI #dsmoy018#1 ] 2019;
      nLinea = nSwCol;
  |FINSI;

  #dsmow054#0  = #camaasil#1;
  #dsmow054#1  = nConta;
  #dsmow054#25 = nLinea;
  |LEE 041#dsmow054.=;
  |SI FSalida ! 0;

      |DDEFECTO #dsmow054;
      #dsmow054#0  = #camaasil#1;
      #dsmow054#1  = nConta;
      #dsmow054#25 = nLinea;

      aAlfa1 = #dsmow054#0;
      #dsmow054#2 = aAlfa1 % 105;

      |HAZ LeeCta;
      #dsmow054#4 = aNomCta;
      #dsmow054#5 = aCIF;
      #dsmow054#6 = aDesCon;
      #dsmow054#7 = #camaasil#4;
      |GRABA 020#dsmow054.c;
  |FINSI;

  #dsmow054#8 = nSwCol;    ||para ampliar
  |SI #dsmoy018#1 ] 2019;
      nNume1 = 9;
  |SINO;
      |SI nSwCol [ 13;
          nNume1 = nSwCol + 8;
      |SINO;
          nNume1 = 22;
      |FINSI;
  |FINSI;
  #dsmow054#nNume1# = #dsmow054#nNume1# + nImporte;

  #dsmow054#23 = #dsmom060#7;
  #dsmow054#24 = #dsmom060#3;
  #dsmow054#26 = #camaasil#26;
  #dsmow054#27 = #camaasil#13;
  #dsmow054#28 = #camaasil#15;
  #dsmow054#29 = #camaasil#14;

  |GRABA 020#dsmow054.a;

  nAsto0  = #camaasil#0;
  fAsto1  = #camaasil#1;
  nAsto2  = #camaasil#2;
  aAsto12 = #camaasil#12;
  nAsto13 = #camaasil#13;
  nAsto14 = #camaasil#14;
  aAsto15 = #camaasil#15;
  aAsto26 = #camaasil#26;
|FPRC;

|PROCESO MiraSiSuma;
  enClAgrupa = 0;
  #dsmom060#6 = #dsmom061#6;
  #dsmom060#0 = #dsmom061#0;
  #dsmom060#1 = #dsmom061#1;
  #dsmom060#2 = #dsmom061#2;
  |LEE 041#dsmom060.=;
  |SI FSalida = 0;
      eaClave60 = #dsmom060#7;
      eaTip60   = #dsmom060#1;
      |SI eaClave60 ! "   ";
          |HAZ eClaveAgrupa;
      |FINSI;
   |FINSI;
|FPRC;

|PROCESO dsmom061_0;

 |HAZ MiraSiSuma;
 |SI enClAgrupa = 1; |ACABA; |FINSI;

 aAlfa3 = #dsmom061#4 % 101;
 aSignoE = "+"; |SI aAlfa3 = "7"; aSignoE = "-"; |FINSI;

 aAlfa1 = #dsmom061#4; |QBF aAlfa1;
 |SI aCta = aAlfa1; |Y aSignoE ! #dsmom061#5; |ACABA; |FINSI;

 aAlfa2 = aAlfa1 % 0; nNume1 = aAlfa2;
 aAlfa2 = (aAlfa1 + "zzzzzzzzzzzz") % nLongPart;
 aAlfa2 = (aAlfa2 + "            ") % 112;

 |SI aAlfa1 [ aHCta; |Y aAlfa2 ] aHCta;
     |SI nLongitud = 0; |O nLongitud [ nNume1;
         nSwCol     = #dsmom061#2;
         aSigno     = #dsmom061#5;
         aCondicion = #dsmom061#7;
         nLongitud  = nNume1;
     |FINSI;
 |FINSI;
|FPRC;

|DEFBUCLE dsmom061;
 #dsmom061#1;
 4;
 enPlan60, #dsmoy018#46, #dsmoy018#0, 00, 001, aDCta;
 enPlan60, #dsmoy018#46, #dsmoy018#0, 99, 999, aHCta;
 ;
 NULL, dsmom061_0;
|FIN;

|PROCESO Camaasil_0;

 #camaasil#9 = (#camaasil#9  / enConversor) ! EURODBMOD;
 |SI enActividad ! 0;
     nElDepar = #camaasil#21;
     |SI nElDepar = 0;
         nElDepar = 1;
     |FINSI;
     |SI enActividad ! nElDepar; |ACABA; |FINSI;
 |FINSI;

 |SI enSwSelCp = 1;
     enConcepto = #camaasil#6;
     |HAZ CtrlConcepto;
     |SI enSwConcep = 1;
         |ACABA;
     |FINSI;
 |FINSI;

 aCta = #camaasil#4; |QBF aCta;

 nLongitud = 0;
 nSwCol    = 0;
 aDCta     = aCta % 102;
 aDCta     = (aDCta + "            ") % 112;
 aHCta     = (aCta  + "            ") % 112;
 |BUCLE dsmom061;
 |SI nSwCol ! 0;
     nImporte = #camaasil#9;
     |SI #camaasil#8 = "D"; |Y aSigno = "-"; nImporte = - nImporte; |FINSI;
     |SI #camaasil#8 = "H"; |Y aSigno = "+"; nImporte = - nImporte; |FINSI;
     |SI aCondicion = "P"; |Y nImporte < 0; nImporte = 0;|FINSI;
     |SI aCondicion = "N"; |Y nImporte > 0; nImporte = 0;|FINSI;
     |SI nImporte ! 0;
         |HAZ Lee60;
         |HAZ Graba_w54;
         |HAZ Graba_w33;
         SwHayDatos = 1;
     |FINSI;
 |FINSI;
|FPRC;

|DEFBUCLE Camaasil;
 #camaasil#1;
 ;
 01, efDFecha, 000001, 0001;
 98, efHFecha, 999999, 9999;
 e;
 NULL, Camaasil_0;
|FIN;

||////////////////////////////////////////////////////////////////////////
|PROCESO Emision;
   enSwPartido = 0;
   enEmpresa  = #dsmow043#1;
   enPerConta = #dsmow043#2;
   eaLaMonEmp = #dsmow043#13;
   |HAZ EnConversor;

   enEjer     = #dsmow043#3;   || A¤o
   dig1       = #dsmow043#4;   || Desde Mes
   dig3       = #dsmow043#5;   || N§ Digitos
   dig4       = #dsmow043#6;   || Digitos Nivel 1
   dig5       = #dsmow043#7;   || Digitos Nivel 2
   dig6       = #dsmow043#8;   || Digitos Nivel 3
   dig7       = #dsmow043#9;   || Digitos Nivel 4
   dig8       = #dsmow043#10;  || Digitos Nivel 5
   dig9       = #dsmow043#11;  || Digitos Nivel 6
   eaEstadoEmp = #dsmow043#12; || Estado
   eaNombreEmp = #dsmow043#14; || Nombre Empresa

   |SI #dsmow043#4 ! 1;
        enSwOpPar   = 0;
        |HAZ SituaEmpresa;
        enPerConta = enPeriodoPar;
        #dsmow043#15 = aPathConta;
   |FINSI;

   aPathConta  = #dsmow043#15; |QBF aPathConta; || Path

   || Recoger informacion sobre los niveles ...
   |SI dig3 = 1; MaximoDigitos = dig4; |FINSI;
   |SI dig3 = 2; MaximoDigitos = dig5; |FINSI;
   |SI dig3 = 3; MaximoDigitos = dig6; |FINSI;
   |SI dig3 = 4; MaximoDigitos = dig7; |FINSI;
   |SI dig3 = 5; MaximoDigitos = dig8; |FINSI;
   |SI dig3 = 6; MaximoDigitos = dig9; |FINSI;

   nLongPart = 100 + MaximoDigitos;
   eaTituloL   = #dsmoy018#45; ||Titulo del Libro
   enSwPortada = #dsmoy018#37;
   enResumen   = #dsmoy018#38;

   aAlfa0 = Impresora % 113;
   |SI #dsmoy018#37 = "S"; |O aAlfa0 = "CrystalReport";
       eaImpresora = Impresora;
       |SI aAlfa0 = "CrystalReport"; eaImpresora = aAlfa0; |FINSI;
       |SUB_EJECUTA_NP ":basica/agiy0016";
       eaImpresora = "";
       |SI aAlfa0 = "CrystalReport"; |HAZ PortadaCrystal; |FINSI;
   |FINSI;

   |PATH_DAT #camaasil#aPathConta#;
   |PATH_DAT #camaniva#aPathConta#;
   |PATH_DAT #cacuenta#aPathConta#;
   |PATH_DAT #camaasic#aPathConta#;
   |PATH_DAT #calicont#aPathConta#;
   |PATH_DAT #caconimp#aPathConta#;

   |DDEFECTO #caconimp;  ||Configuracion de Informes
   |ABRE #caconimp;
   #caconimp#0 = 1;
   |LEE 000#caconimp.=;
   |CIERRA #caconimp;

  nDActividad = 0; nHActividad = 0;
  |SI #dsmoy018#49 = "S";  nDActividad = #dsmoy018#50; nHActividad = #dsmoy018#51;  |FINSI;

  |PARA enActividad = nDActividad; |SI enActividad [ nHActividad; |HACIENDO enActividad = enActividad + 1;

        |HAZ LeeDatosEmpresa;
        |SI  swNoExis = 0;       ||SI existe esta Actividad.

             SwHayDatos = 0;
             nConta     = 0;

             |ABRE #dsmom060;
             |ABRE #dsmow033;
             |ABRE #dsmow054;
             |BUCLE Camaasil;
             |CIERRA #dsmow054;
             |CIERRA #dsmow033;

             nPagina = 1;
             |SI SwHayDatos = 1;
                 |SI #dsmoy018#38 ! "R";
                    |HAZ ImpAuxiliar;
                 |FINSI;

                 |SI #dsmoy018#38 ! "N"; |Y nSwCR = 0;
                     |HAZ ImpAuxiliarR;
                |FINSI;
            |FINSI;
            |CIERRA #dsmom060;
      |FINSI;
  |SIGUE;

   |SI enSwPartido = 1;
       enSwOpPar   = 1;
       |HAZ FinalPartido;
   |FINSI;
|FPRC;

|DEFBUCLE dsmow043;
   #dsmow043#1;
   16;
   000001, "S";
   999999, "S";
   ;
   NULL, Emision;
|FIN;

|PROCESO Imprimir;
    nOpcion = 1;
    |SI #dsmoy018#52 ! 9;
       |MENAV "0000 Recuerde seleccionar el papel de la impresora en Horizontal";
       nSwCR = 0; aInformeCR = "";
       |IMPRE 0;
    |SINO;
       nSwCR = 1;
       aInformeCR = "ccmoyi18";
       |SI #dsmoy018#1 ] 2019; aInformeCR = "ccmoyy18"; |FINSI;
       Impresora = "CrystalReport";
       |IMPRE -1;
    |FINSI;
    |SI FSalida ! 0;  |ACABA;  |FINSI;

    |BUCLE dsmow043;

    |FINIMP;
|FPRC;

|PROCESO Archivar;
    nOpcion    = 2;
    nSwCR      = 1;
    aInformeCR = "ccmoyi18";
    |SI #dsmoy018#1 ] 2019; aInformeCR = "ccmoyy18"; |FINSI;
    Impresora  = "CrystalReport";

    |BUCLE dsmow043;
|FPRC;

|PROCESO PrimAct;
        #dsmow055#5 = #agicen14#9;
        #dsmow055#6 = #agicen14#10;
        #dsmow055#7 = #agicen14#7;
        #dsmow055#8 = #agicen14#8;
        efPn10 = #agicen14#5;
        efPn11 = #agicen14#6;

    |SI #agicen14#6 [ "01.01.0000"; |O #agicen14#6 ] fFecha;
        |ERROR10;
    |FINSI;
|FPRC;

|DEFBUCLE PrimAct;
    #agicen14#1;
    ;
    #dsmow055#0, 01;
    #dsmow055#0, 99;
    ;
    NULL, PrimAct;
|FIN;

|PROCESO LeeDatosEmpresa;
  |DELINDEX #dsmow033;
  |DELINDEX #dsmow054;

  |DDEFECTO #dsmow055;

  #dsmow055#0 = enEmpresa;
  nPin1 = 0;
  aPin1 = "";
  efPn10 = "01.01.0000";
  efPn11 = "01.01.0000";
  aAlfa1 = enEjer;
  aAlfa1 = "01.01."+ aAlfa1;
  fFecha = aAlfa1;

  || Lee Primera Actividad
    |BUCLE PrimAct;

  || Lee Actividad en concreto.
    |ABRE #agicen14;
    swNoExis = 0;
    |SI enActividad ! 0;
        #agicen14#0 = #dsmow055#0; || Empresa
        #agicen14#1 = enActividad;
        |LEE 041#agicen14.=;
        |SI FSalida = 0;
            #dsmow055#15 = #agicen14#1;   ||Act.
            #dsmow055#16 = #agicen14#2;   ||Tipo Epig.
            #dsmow055#17 = #agicen14#3;   ||Epigrafe
            #dsmow055#18 = #agicen14#4;   ||Descripcion Actividad
            #dsmow055#19 = #agicen14#5;   ||Fecha Inicio
            #dsmow055#20 = #agicen14#6;   ||Fecha Fin
            #dsmow055#21 = #agicen14#11;  ||Sector actividad
            #dsmow055#22 = #agicen14#12;  ||Sector actividad
            #dsmow055#23 = #agicen14#16;  ||Tipo Actividad
            #dsmow055#24 = #agicen14#17;  ||Descripcion Tipo Actividad
       |SINO;
            |CIERRA #agicen14;
            swNoExis = 1;
            |ACABA;
       |FINSI;
    |FINSI;

    |CIERRA #agicen14;

 || Lee Ficha Sociedades
    swExiste = 0;
    |ABRE #agim0019;
    #agim0019#0 = #dsmow055#0; || Empresa
    #agim0019#1 = 99;
    |LEE 041#agim0019.];
    |SI FSalida = 0;
       |LEE 041#agim0019.a;
    |SINO;
       |LEE 041#agim0019.u;
    |FINSI;
    |SI FSalida = 0; |Y #agim0019#0 = #dsmow055#0;
        swExiste = 1;
    |FINSI;
    |SI swExiste = 1;
        efPn10 = #agim0019#7;      || Fecha Activa
        efPn11 = #agim0019#55;     || Fecha Inactiva
        nPin1 = #agim0019#4;
        aPin1 = #agim0019#5;
    |FINSI;
    |CIERRA #agim0019;

    |ABRE #agifigen;
    #agifigen#0 = #dsmow055#0; || Empresa
    |LEE 041#agifigen.=;
    |SI FSalida = 0;
        efPn10  = #agifigen#93;
        efPn11  = #agifigen#94;
        nPin1   = #agifigen#87;
        aPin1   = #agifigen#88;
        #dsmow055#1 = #agifigen#13;  || Nif
        #dsmow055#2 = #agifigen#1;   || Nombre
    |FINSI;
    |CIERRA #agifigen;
    |SI efPn10 [ "01.01.1900"; efPn10 = "01.01.0000"; |FINSI;
    |SI efPn11 [ "01.01.1900"; efPn11 = "01.01.0000"; |FINSI;
    #dsmow055#3 = nPin1;
    #dsmow055#4 = aPin1;
    #dsmow055#9 = efPn10;
    #dsmow055#10 = efPn11;

    #dsmow055#11 = "ACTIVA";
    |SI #dsmow043#12 = "N"; #dsmow055#11 = "INACTIVA"; |FINSI;
    #dsmow055#12 = #dsmoy018#1;                                   || Ejercicio
    #dsmow055#13 = efDFecha;
    #dsmow055#14 = efHFecha;

    #dsmow055#67 = #dsmoy018#45; ||Titulo del Libro
|FPRC;

|| ***************************************************************
|PROGRAMA;
   aParam = PARAMETRO; |QBF aParam;
   eaTipoInforme = aParam;

   #dsmoy018#46 = "N";  ||No inmovilizado
   |SI aParam ! "I"; |Y aParam ! "G";
       aParam = "I";
   |FINSI;
   eaTipoInforme = aParam;

   eaPlanilla = "dsmom060"; |HAZ PlanDePlanillas;

   |ABRE #dsmoy018;
   |CLS;
   |CABEZA "Libros Ingresos/Gastos";

   |PINPA #0#dsmoy018;
   #dsmoy018#0 = aParam;
   |LEE 141#dsmoy018.=;
   |SI FSalida ! 0;
       |DDEFECTO #dsmoy018;
       #dsmoy018#0 = aParam;
       |HAZ Eano;
       |HAZ losDefectos;
       |GRABA 020#dsmoy018.b;
   |FINSI;

   |PINDA #0#dsmoy018;
   |HAZ QueLibro;

   |ENDATOS #1#dsmoy018;
   |SI FSalida = 0;
       |GRABA 020#dsmoy018.a;
   |FINSI;

   |LIBERA #dsmoy018;
   |CIERRA #dsmoy018;
|FPRO;
