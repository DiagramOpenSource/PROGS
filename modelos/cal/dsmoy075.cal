|FICHEROS;
     dsmoy075 #0;
     dsmom004 #4;
     :/basica/def/agifigen #118;

     dsmom072 #72;
     dsmom72@ #972;

     dsmow144 #944;

     dsmow030 #900;

     &regisnif@ #709;
|FIN;

|VARIABLES;
     nCampo     = 0;
     fLaFecha   = @;
     aAlfa1     = "";

     nSwM72     = 0;
     aClave     = "";
     nDEmpresa  = 0;
     nHEmpresa  = 0;
     aDClave    = "";
     aHClave    = "";

     &eaTituloL  = "";
     &efDFecha   = @;
     &efHFecha   = @;
     &eaClCobro  = "";
     informe     = "dsmoy075";
     &SwLinea    = 0;
     &eaTexto    = "";
     aDef        = "";
|FIN;

|INCLUYE i_variar;

|| /////////////////////// PROCESOS ENTRADA POR PANTALLA \\\\\\\\\\\\\\\\\\\\
|PROCESO Tipo7;  |TIPO 7;
     |SI enEmpresa ! 0;
         #0#0 = enEmpresa;  |PINTA #0#0;
         #0#1 = enEmpresa;  |PINTA #0#1;

         |C_M #0#0, 1, "N";
         |C_M #0#1, 1, "N";
     |FINSI;
|FPRC;

|PROCESO Parrilla;
     |SI #0#0 = 0;
         #0#0 = 0;  |PINTA #0#0;
         #0#1 = 0;  |PINTA #0#1;  |C_M #0#1, 1, "N";
         |PARA nCampo = 2;  |SI nCampo [ 13;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |SINO;
         |SI enEmpresa = 0;
             |C_M #0#0, 1, "S";
             |C_M #0#1, 1, "S";
         |FINSI;

         |PARA nCampo = 2;  |SI nCampo [ 13;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "N";
               #0nCampo = 0;  |PINTA #0nCampo;
         |SIGUE;
     |FINSI;
|FPRC;

|PROCESO NoMasEmpresas;
     |SI #0Campo = 0;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 13;  |HACIENDO nCampo = nCampo + 1;
               #0nCampo = 0;  |C_M #0nCampo, 1, "N";
         |SIGUE;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 13;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |FINSI;
|FPRC;

|CALCULO Eano; |TIPO 7;
 fLaFecha = ~;
 aAlfa1 = fLaFecha; aAlfa1 = aAlfa1 % -104;
 #0#14 = aAlfa1;
 #0#14 = #0#14 - 1;
 |PINTA #0#14;
|FCAL;

|| ///////////////////////////////////////////////////////////////////
|| **********************************************************************
|PROCESO BuscaAcumulado;
    #0#18 = #0#18 + #972#7;
|FPRC;

|DEFBUCLE BuscaAcumulado;
    #972#1010;
    ;
    #72#0, 2008,   #72#2, #72#3, #72#8, #72#9, #72#4, #72#5, #72#10, #72#6;
    #72#0, enEjer, #72#2, #72#3, #72#8, #72#9, #72#4, #72#5, #72#10, #72#6;
    e;
    NULL, BuscaAcumulado;
|FIN;

|PROCESO Lee_m072;

     |SI nSwM72 = 0;
         nSwM72  = 1;
         eaCodNif = #72#3;  |HAZ LeeNifs;
         #0#17    = #709#1;
         #0#19    = 0;
         #0#20    = 0;
         #0#21    = 0;
         SwLinea  = 0;
     |FINSI;

     |SI eaCodNif ! #72#3;
         SwLinea = 2; |PRINT;
         eaCodNif = #72#3;  |HAZ LeeNifs;
         #0#17    = #709#1;
         #0#19    = 0;
         #0#20    = 0;
         #0#21    = 0;
         SwLinea  = 0;
     |FINSI;

     #0#18 = 0;
     |SI #72#1 ! #72#6;   ||busco acumulado
         |BUCLE BuscaAcumulado;
     |FINSI;

     #944#0  = #72#0;
     #944#1  = #72#1;
     #944#2  = #72#2;
     #944#3  = #72#3;
     #944#4  = #72#4;
     #944#5  = #72#5;
     #944#6  = #72#6;
     #944#7  = #72#7;
     #944#8  = #0#17;
     #944#9  = #72#8;
     #944#10 = #72#9;
     #944#11 = #72#10;

     |PRINT;
     SwLinea = 1;
     #0#19 = #0#19 + #0#18;
     |SI #944#1 = #944#6;
         #0#20 = #0#20 + #944#7;
     |SINO;
         #0#21 = #0#21 + #944#7;
     |FINSI;
|FPRC;

|DEFBUCLE Lee_m072;
   #72#1;
   ;
   enEmpresa, enEjer, aClave, "            ", " ", " ", " ", " ", " ", 2008;
   enEmpresa, enEjer, aClave, "zzzzzzzzzzzz", "z", "z", "z", "z", "z", 2099;
   e;
   NULL, Lee_m072;
|FIN;

|PROCESO  LeeAgifigen;
     enEmpresa   = #118#0;
     enEjer      = #0#14;

     |DDEFECTO #900;
     eaTituloL = #0#16;
     |HAZ DatosEmpr_w30;

     |DBASE aDef;  |QBF aDef;
     aDef = aDef + "def/dsmom072.mas";
     |CARGA_DEF aDef, dsmom72@;
     |SI FSalida ! 0;
         |MENAV "    Error al Cargar def dsmom072";
         |ACABA;
     |FINSI;

     SwLinea = 0;
     nSwM72  = 0;
     |SI #0#15 ! "C";
         eaTexto = "PAGOS";
         aClave = "A"; |BUCLE Lee_m072;
         |SI nSwM72 = 1;
             |SI SwLinea = 1;
                 SwLinea = 2; |PRINT;
             |FINSI;
             |PIEINF;
         |FINSI;
         nSwM72 = 0;
     |FINSI;

     |SI #0#15 ! "P";
         eaTexto = "COBROS";
         aClave = "B"; |BUCLE Lee_m072;
         |SI nSwM72 = 1;
             |SI SwLinea = 1;
                 SwLinea = 2; |PRINT;
             |FINSI;
             |PIEINF;
         |FINSI;
     |FINSI;

     |DESCARGA_DEF #972;
|FPRC;

|DEFBUCLE LeeAgifigen;
     #118#1;
     ;
     nDEmpresa;
     nHEmpresa;
     e;
     NULL, LeeAgifigen;
|FIN;

|| ///////////////////////////////////////////////////////////////
|PROCESO ITipo3;  |TIPO 3;
     aAlfa1 = #0#14; aAlfa1 = "01.01." + aAlfa1; efDFecha = aAlfa1;
     aAlfa1 = #0#14; aAlfa1 = "31.12." + aAlfa1; efHFecha = aAlfa1;

     |IMPRE 0;
     |SI FSalida ! 0; |ACABA; |FINSI;

     |INFOR informe;
     |SI FSalida ! 0;
         |MENAV "    Error en el Informe";
         |FINIMP;
         |ACABA;
     |FINSI;


     |SI #0#15 = "A";
         aDClave = " ";
         aHClave = "z";
     |SINO;
         |SI #0#15 = "C"; aDClave = "B"; |FINSI;
         |SI #0#15 = "P"; aDClave = "A"; |FINSI;
         aHClave = aDClave;
     |FINSI;

     |SI #0#0 ! 0;
          nDEmpresa = #0#0;
          nHEmpresa = #0#1;
          |BUCLE LeeAgifigen;
      |SINO;
          |PARA nCampo = 2;  |SI nCampo [ 13;  |HACIENDO nCampo = nCampo + 1;
                nDEmpresa = #0nCampo;
                nHEmpresa = #0nCampo;
                |BUCLE LeeAgifigen;
          |SIGUE;
     |FINSI;

     |FININF;
     |FINIMP;

|FPRC;

|PROGRAMA;
     |SI enEmpresa ! 0;
         |DDEFECTO #0;
         #0#0 = enEmpresa;
         #0#1 = enEmpresa;
         #0#14 = enEjer;
         |SI eaClCobro = "A"; #0#15 = "P"; |FINSI;
         |SI eaClCobro = "B"; #0#15 = "C"; |FINSI;
         |HAZ ITipo3;
     |SINO;
         |MANTE #0;
     |FINSI;
|FPRO;
