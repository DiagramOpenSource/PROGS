|FICHEROS;
   dsmorcre #0,MANTE;

   dsminmex #10;
   dsmivcex #11;

   dsm210dp #20;
   dsm210in #21;
   dsm210ln #22;
   dsm210fp #23;
   dsm210tr #24;
   dsm210dv #25;
   dsm210ca #26;
   dsm210db #27;
   dsm210t1 #28;
|FIN;

|VARIABLES;
   aAlfaAux       = "";
   &eaComent      = "";
   &eaIncidencias = "";

   nPara1 = 0;
   nCont  = 0;
   nCalc  = 0;
   nSwHay = 0;
   nPant0 = 0;

   nSwImpre    = 0;
   nSwErrImp   = 0;
   nSwPrint    = 0;
   nSwPrintCab = 0;

   aAlfa     = "";
   aAlfa1    = "";
   fFecha    = @;
   fFechaIni = @;
   fFechaFin = @;
   nCalc1    = 0;
   nCalc2    = 0;
   nDias     = 0;
   nFactor   = 0;
   &nEj210tr = 0;
   &nTg210tr = 0;
   &nTr210tr = 0;
|FIN;

|INCLUYE i_pat210;

|PROCESO Tipo12; |TIPO 12;
|FPRC;

|PROCESO MiraInforme;
   |SI #dsmorcre#25 = "S";
      |C_M #dsmorcre#26, 1, "S";
   |SINO;
      |C_M #dsmorcre#26, 1, "N";
   |FINSI;
|FPRC;

|PROCESO SelecImpre;
   |SI nSwErrImp < 0; |ACABA; |FINSI;

   |IMPRE 0;
   |SI FSalida = 0;
      |INFOR "dsmorcre";
      |SI FSalida = 0;
         nSwImpre = 1; nSwErrImp = 0;
      |SINO;
         nSwImpre = -1;
         aAlfa = "    N'dsmorcre.inf' no encontrado. No se generar  el informe. " + &191 + " Continuar ?";
         |CONFI aAlfa;
         |SI FSalida ! 0; |FINIMP; nSwErrImp = -1; |FINSI;
      |FINSI;
   |SINO;
      nSwImpre = -1;
      aAlfa = "    NImpresora no seleccionada. No se generar  el informe. " + &191 + " Continuar ?";
      |CONFI aAlfa;
      |SI FSalida ! 0; nSwErrImp = -1; |FINSI;
   |FINSI;
|FPRC;

|PROCESO ImprimeIncid;
  |SI nSwImpre ! 1; |HAZ SelecImpre; |FINSI;
  |SI nSwErrImp = 0;
     |SI nSwPrintCab = 0;
        eaComent = ""; |PRINT;

        eaComent = " Contribuyente [" + (("00000" + #dsm210dp#0) % -105)+ "] ";
        eaComent = eaComent + #dsm210dp#1; |QBF eaComent;
        |PRINT;

        nSwPrintCab = 1;
     |FINSI;
     eaComent = "     - " + aAlfa;
     |PRINT; || nSwPrint = 1;
  |FINSI;
|FPRC;

|PROCESO SaltaResto;
   |SI #dsmorcre#Campo# = 0;
      |PARA nCont = Campo; |SI nCont [ 20; |HACIENDO nCont = nCont + 1;
         |C_M #dsmorcre#nCont#, 1, "N";
      |SIGUE;
   |FINSI;
|FPRC;

|PROCESO Seleccion;
   |SI #dsmorcre#0 = "S";
      |C_M #dsmorcre#1, 1, "N"; #dsmorcre#1 = "00000"; |PINTA #dsmorcre#1;
      |C_M #dsmorcre#2, 1, "N"; #dsmorcre#2 = "99999"; |PINTA #dsmorcre#2;
      |PARA nCont = 3; |SI nCont [ 20; |HACIENDO nCont = nCont + 1;
         |C_M #dsmorcre#nCont#, 1, "S";
      |SIGUE;
   |SINO;
      |C_M #dsmorcre#1, 1, "S"; |C_M #dsmorcre#2, 1, "S";
      |PARA nCont = 3; |SI nCont [ 20; |HACIENDO nCont = nCont + 1;
         |C_M #dsmorcre#nCont#, 1, "N"; #dsmorcre#nCont# = "00000"; |PINTA #dsmorcre#nCont#;
      |SIGUE;
   |FINSI;
|FPRC;

|PROCESO GrabaInmueble;
   |DDEFECTO #dsm210in;
   #dsm210in#0 = #dsminmex#0;
   #dsm210in#1 = 2;
   #dsm210in#2 = #dsminmex#1;
   #dsm210in#3 = #dsminmex#22;
   #dsm210in#4 = #dsminmex#2;
   aAlfa  = #dsminmex#3;  |QBF aAlfa;
   aAlfa1 = #dsminmex#4;  |QBF aAlfa1; |SI aAlfa1 ! ""; aAlfa = aAlfa + " " + aAlfa1; |FINSI;
   aAlfa1 = #dsminmex#5;  |QBF aAlfa1; |SI aAlfa1 ! ""; aAlfa = aAlfa + " " + aAlfa1; |FINSI;
   aAlfa1 = #dsminmex#6;  |QBF aAlfa1; |SI aAlfa1 ! "0"; |Y aAlfa1 ! ""; aAlfa = aAlfa + ", " + aAlfa1; |FINSI;
   aAlfa1 = #dsminmex#7;  |QBF aAlfa1; |SI aAlfa1 ! ""; aAlfa = aAlfa + " " + aAlfa1; |FINSI;
   aAlfa1 = #dsminmex#8;  |QBF aAlfa1; |SI aAlfa1 ! ""; aAlfa = aAlfa + ", " + aAlfa1; |FINSI;
   aAlfa1 = #dsminmex#9;  |QBF aAlfa1; |SI aAlfa1 ! ""; aAlfa = aAlfa + ", " + aAlfa1; |FINSI;
   aAlfa1 = #dsminmex#10; |QBF aAlfa1; |SI aAlfa1 ! ""; aAlfa = aAlfa + ", " + aAlfa1; |FINSI;
   aAlfa1 = #dsminmex#11; |QBF aAlfa1; |SI aAlfa1 ! ""; aAlfa = aAlfa + " " + aAlfa1; |FINSI;
   aAlfa1 = #dsminmex#12; |QBF aAlfa1; |SI aAlfa1 ! ""; aAlfa = aAlfa + "-" + aAlfa1; |FINSI;
   #dsm210in#5 = aAlfa;
   #dsm210in#6 = (("00" + #dsminmex#15) % -102) + (("000" + #dsminmex#16) % -103);
   #dsm210in#7 = #dsminmex#14;
   #dsm210in#8 = #dsminmex#19;
   |GRABA 020#dsm210in.n;
|FPRC;

|PROCESO GrabaFP;
   #dsm210db#0 = #dsm210ln#0;
   |LEE 000#dsm210db.=;
   |SI FSalida ! 0;
      |DDEFECTO #dsm210db;
      |SI #dsmorcre#25 = "S";
         aAlfa = "El contribuyente no tiene datos bancarios informados.";
         |HAZ ImprimeIncid;
      |FINSI;
   |SINO;
      |SI #dsmorcre#25 = "S";
         aAlfa  = #dsm210db#1; |QBF aAlfa;
         aAlfa1 = #dsm210db#9; |QBF aAlfa1;
         |SI aAlfa = ""; |Y aAlfa1 = "";
            aAlfa = "El titular no tiene datos bancarios informados.";
            |HAZ ImprimeIncid;
         |FINSI;
         aAlfa  = #dsm210db#26; |QBF aAlfa;
         aAlfa1 = #dsm210db#34; |QBF aAlfa1;
         |SI aAlfa = ""; |Y aAlfa1 = "";
            aAlfa = "El conyuge no tiene datos bancarios informados.";
            |HAZ ImprimeIncid;
         |FINSI;
      |FINSI;
   |FINSI;

   |DDEFECTO #dsm210fp;
   #dsm210fp#0 = #dsm210ln#0;
   #dsm210fp#1 = #dsm210ln#1;
   #dsm210fp#2 = #dsm210ln#2;
   #dsm210fp#3 = #dsm210ln#3;
   #dsm210fp#4 = #dsm210ln#5;
   #dsm210fp#37 = "P";
   |SI #dsmorcre#24 = "E";
      #dsm210fp#6  = "E";
      #dsm210fp#39 = "E";
   |SINO;
      #dsm210fp#6  = #dsmorcre#24;
      #dsm210fp#39 = #dsmorcre#24;
      |PARA nCont = 1; |SI nCont [ 24; |HACIENDO nCont = nCont + 1;
         nCalc = nCont + 10;
         #dsm210fp#nCalc# = #dsm210db#nCont#;
      |SIGUE;
      |PARA nCont = 26; |SI nCont [ 49; |HACIENDO nCont = nCont + 1;
         nCalc = nCont + 18;
         #dsm210fp#nCalc# = #dsm210db#nCont#;
      |SIGUE;
   |FINSI;
   |GRABA 020#dsm210fp.n;
|FPRC;

|PROCESO GrabaLinea;
   |DDEFECTO #dsm210ln;
   #dsm210ln#0 = #dsminmex#0;
   #dsm210ln#1 = 2;
   #dsm210ln#3 = #dsminmex#1;
   aAlfa = "31.12." + #dsmorcre#21;
   #dsm210ln#4 = aAlfa;
   #dsm210ln#5 = #dsmorcre#23;
   #dsm210ln#6 = #dsm210dv#1;

   aAlfaAux = "01.01." + #dsmorcre#21;
   fFechaIni = aAlfaAux;
   aAlfaAux = "31.12." + #dsmorcre#21;
   fFechaFin = aAlfaAux;
   nCalc = fFechaFin - fFechaIni; nDias = nCalc + 1;

   fFechaIni = #dsminmex#24;
   aAlfa = #dsminmex#24; aAlfa = aAlfa % -104; nCalc = aAlfa;
   aAlfa = #dsm210ln#4;  aAlfa = aAlfa % -104; nCalc1 = aAlfa;
   |SI nCalc ! nCalc1; aAlfa = "01.01." + nCalc1; fFechaIni = aAlfa; |FINSI;

   aAlfa = #dsminmex#29; aAlfa = aAlfa % -104; nCalc2 = aAlfa;
   |SI nCalc1 = nCalc2;
      fFechaFin = #dsminmex#29;
   |SINO;
      |SI nCalc1 > nCalc2;
         fFechaFin = #dsm210ln#4;
      |SINO;
         aAlfa = "31.12." + nCalc1; fFechaFin = aAlfa;
      |FINSI;
   |FINSI;

   nCalc = fFechaFin - fFechaIni; nCalc = nCalc + 1;
   nFactor = nCalc / nDias;

   |SI #dsmivcex#4 = "S";
      nCalc = (#dsmivcex#3 % 1.1) ! 2;
   |SINO;
      nCalc = (#dsmivcex#3 % 2) ! 2;
   |FINSI;
   nCalc = (nCalc * nFactor) ! 2;
   #dsm210ln#7  = nCalc;

   nCalc = nCalc % nTg210tr;
   #dsm210ln#34 = nCalc;
   #dsm210ln#32 = #dsminmex#31;
   #dsm210ln#33 = #dsminmex#32;
   #dsm210ln#35 = nTg210tr;

   |GRABA 020#dsm210ln.n;

   |SI #dsm210ln#34 ] 0;
      || Grabo tambien la forma de pago
      #dsm210fp#0 = #dsm210ln#0;
      #dsm210fp#1 = #dsm210ln#1;
      #dsm210fp#2 = #dsm210ln#2;
      #dsm210fp#3 = #dsm210ln#3;
      #dsm210fp#4 = #dsm210ln#5;
      #dsm210fp#37 = "P";
      |LEE 000#dsm210fp.=;
      |SI FSalida ! 0;
         |HAZ GrabaFP;
      |SINO;
         |SI #dsmorcre#22 = "S";
            |BORRA 020#dsm210fp.a
            |HAZ GrabaFP;
         |FINSI;
      |FINSI;
   |FINSI;
|FPRC;

|PROCESO Inmuebles;
   aAlfa = #dsminmex#29; |QBF aAlfa;
   aAlfa = aAlfa % -104; nCalc = aAlfa;
   |SI nCalc < #dsmorcre#21; |Y nCalc ! 0; |ACABA; |FINSI;

   #dsmivcex#0 = #dsminmex#0;
   #dsmivcex#1 = #dsminmex#1;
   #dsmivcex#2 = #dsmorcre#21;
   |LEE 000#dsmivcex.=;
   |SI FSalida ! 0; |O #dsmivcex#3 = 0;
      |DDEFECTO #dsmivcex;
      |SI #dsmorcre#25 = "S";
         aAlfa = "Inmueble " + (("000" + #dsminmex#1) % -103)+ ". ";
         aAlfa = aAlfa + "Falta valor catastral para el ejercicio " + #dsmorcre#21;
         |HAZ ImprimeIncid;
      |FINSI;
   |FINSI;
   |SI #dsmivcex#4 = " ";  #dsmivcex#4 = #dsminmex#28;  |FINSI;

   #dsm210in#0 = #dsminmex#0;
   #dsm210in#1 = 2;
   #dsm210in#2 = #dsminmex#1;
   |LEE 000#dsm210in.=;
   |SI FSalida ! 0;
      |HAZ GrabaInmueble;
   |SINO;
      |SI #dsmorcre#22 = "S";
         |BORRA 020#dsm210in.a
         |HAZ GrabaInmueble;
      |FINSI;
   |FINSI;

   |DDEFECTO #dsm210ln;
   #dsm210ln#0 = #dsminmex#0;
   #dsm210ln#1 = 2;
   #dsm210ln#3 = #dsminmex#1;
   aAlfa = "31.12." + #dsmorcre#21;
   #dsm210ln#4 = aAlfa;
   #dsm210ln#5 = #dsmorcre#23;
   |LEE 000#dsm210ln.=;
   |SI FSalida ! 0;
      |HAZ GrabaLinea; nSwHay = 1;
      |SI #dsmorcre#26 = "T";
         |SI nSwPrint = 0;
            aAlfa = "Inmueble " + (("000" + #dsminmex#1) % -103)+ ". ";
            aAlfa = aAlfa + " Renta creada satisfactoriamente.";
            |HAZ ImprimeIncid;
         |FINSI;
      |FINSI;
   |SINO;
      |SI #dsmorcre#22 = "S";
         |BORRA 020#dsm210ln.a
         |HAZ GrabaLinea; nSwHay = 1;
         |SI #dsmorcre#26 = "T";
            |SI nSwPrint = 0;
               aAlfa = "Inmueble " + (("000" + #dsminmex#1) % -103)+ ". ";
               aAlfa = aAlfa + " Renta creada satisfactoriamente.";
               |HAZ ImprimeIncid;
            |FINSI;
         |FINSI;
      |FINSI;
   |FINSI;
|FPRC;

|DEFBUCLE Inmuebles;
  #dsminmex#1;
  20;
  #dsm210dp#0, 001, "A";
  #dsm210dp#0, 999, "A";
  ;
  NULL, Inmuebles;
|FIN;

|PROCESO GrabaTR;
   |DDEFECTO #dsm210ca;
   #dsm210ca#0 = #dsm210dp#0;
   #dsm210ca#1 = 2;
   #dsm210ca#2 = #dsm210tr#1;
   #dsm210ca#5 = nTg210tr;
   |GRABA 020#dsm210ca.n;
|FPRC;

|PROCESO CreaTipoRenta;

   eaPaisCCE = #dsm210dp#7; |HAZ PaisCCE_210;
   |SI eSwPaisCCE = 1;
       nTg210tr = #dsm210tr#5;   || pais de la CEE
   |SINO;
       nTg210tr = #dsm210tr#7;   || otros paises
   |FINSI;

   nSwHay      = 0;
   nSwPrint    = 0;
   nSwPrintCab = 0;
   |BUCLE Inmuebles;

   ||Si hay datos ,crear la linea de cabecera
   |SI nSwHay > 0;
      #dsm210ca#0 = #dsm210dp#0;
      #dsm210ca#1 = 2;
      |LEE 000#dsm210ca.=;
      |SI FSalida ! 0;
         |HAZ GrabaTR;
      |SINO;
         |SI #dsmorcre#22 = "S";
            |BORRA 020#dsm210ca.a
            |HAZ GrabaTR;
         |FINSI;
      |FINSI;
   |FINSI;
|FPRC;

|DEFBUCLE CreaTipoRenta;
   #dsm210dp#1;
   ;
   #dsmorcre#1;
   #dsmorcre#2;
   ;
   NULL, CreaTipoRenta;
|FIN;

|PROCESO CreacionTR;
   nSwErrImp = 0;
   |SI #dsmorcre#25 = "S";
      |SI #dsmorcre#26 = "T";
         |HAZ SelecImpre;
         |SI nSwErrImp < 0; |ACABA; |FINSI;
      |FINSI;
   |FINSI;

   |ABRE #dsm210dp; |ABRE #dsmivcex;
   |ABRE #dsm210in; |ABRE #dsm210ln; |ABRE #dsm210ca;
   |ABRE #dsm210tr; |ABRE #dsm210dv;
   |ABRE #dsm210db; |ABRE #dsm210fp;

   nEj210tr = #dsmorcre#21;
   |SI nEj210tr < 2014; nEj210tr = 2014; |FINSI;

   #dsm210tr#0 = 2;
   #dsm210tr#6 = nEj210tr;
   |LEE 000#dsm210tr.=;
   |SI FSalida ! 0; |DDEFECTO #dsm210tr; |FINSI;

   #dsm210dv#0 = #dsmorcre#23;
   |LEE 000#dsm210dv.=;
   |SI FSalida ! 0; |DDEFECTO #dsm210dv; |FINSI;

   |SI #dsmorcre#0 = "S";
      |PARA nPara1 = 3; |SI nPara1 [ 20; |HACIENDO nPara1 = nPara1 + 1;
            |SI #dsmorcre#nPara1# ! 0;
                #dsm210dp#0 = #dsmorcre#nPara1#;
                |LEE 000#dsm210dp.=;
                |SI FSalida = 0;
                    |HAZ CreaTipoRenta;
                |FINSI;
            |FINSI;
      |SIGUE;
   |SINO;
      |BUCLE CreaTipoRenta;
   |FINSI;

   |CIERRA #dsm210db; |CIERRA #dsm210fp;
   |CIERRA #dsm210dv; |CIERRA #dsm210tr;
   |CIERRA #dsm210ca; |CIERRA #dsm210in; |CIERRA #dsm210ln;
   |CIERRA #dsm210dp; |CIERRA #dsmivcex;

   |SI nSwImpre = 1;
      |PIEINF; |FININF;
   |FINSI;
|FPRC;

|PROGRAMA;

   |HAZ Patron210;
   |CLS;
   |CABEZA "Creacion masiva rentas imputadas a inmuebles urbanos (Modelo 210)";

   fFecha = ~;
   aAlfa = fFecha; |QBF aAlfa;
   aAlfa = aAlfa % -104; #dsmorcre#21 = aAlfa;

   |PINPA #0#dsmorcre; nPant0 = FSalida;
   |PINDA #0#dsmorcre;

   |ENDATOS #1#dsmorcre;
   |SI FSalida = 0;
      |SI #dsmorcre#26 = "T";
         eaIncidencias = "  Emitiendo todos los avisos";
      |SINO;
         eaIncidencias = "  Emitiendo solo las incidencias";
      |FINSI;
      eaIncidencias = "";
      |HAZ CreacionTR;
   |FINSI;
|FPRO;
