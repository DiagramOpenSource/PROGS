|FICHEROS;
     dsmoy025 #0;
     :/integral/def/dsam0000 #1;
|FIN;

|VARIABLES;
     nCampo       = 0;
     nLong        = 0;
     nSwHay       = 0;

     aLong        = "";
     aAlfa        = "";

     &enCodCli    = 0;
     &enSwDeDonde = 0;
     &eaDescri    = "";
     &eaCampo     = "";
     &eaAlfa      = "";
     &enLinea     = 0;
     &eaVarDni    = "";
|FIN;

|PROCESO DesdeHasta;
     |C_M #0#1, 1, "S";
     |PARA nCampo = 2;  |SI nCampo [ 25;  |HACIENDO nCampo = nCampo + 1;
           |C_M #0nCampo, 1, "N";
           #0nCampo = 0;  |PINTA #0nCampo;
     |SIGUE;

     |SI #0#0 = 0;
         |C_M #0#1, 1, "N";  #0#1 = 0;  |PINTA #0#1;

         |PARA nCampo = 2;  |SI nCampo [ 25;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |FINSI;
|FPRC;

|PROCESO NoMasEmpresas;
     |SI #0Campo = 0;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 25;  |HACIENDO nCampo = nCampo + 1;
               #0nCampo = 0;  |C_M #0nCampo, 1, "N";
         |SIGUE;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 25;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |FINSI;
|FPRC;

|PROCESO MiraDNI;
     |QBF eaCampo;
     eaVarDni = eaCampo;  |HAZ CalculameDNI;
     |SI eaVarDni = "";
         eaDescri = "DNI o CIF sin Contenido";
         nSwHay   = 1;
         |PRINT;
         |ACABA;
     |FINSI;

     |QBF eaVarDni;
     aLong = eaVarDni % 0;
     nLong = aLong;
     |SI nLong ! 9;
         eaDescri = "DNI con formato erroneo. Debe ser de 9 Digitos";
         nSwHay   = 1;
         |PRINT;
         |ACABA;
     |FINSI;
|FPRC;

|PROCESO MiraNumeros;
     |PARA nCampo = 0;   |SI nCampo [ 9;  |HACIENDO nCampo = nCampo + 1;
           aAlfa = eaCampo - nCampo;
           |SI FSalida ! 0;
               eaDescri = "Apellidos y Nombre con datos Numericos";
               nSwHay   = 1;
               |PRINT;
               |SAL;
           |FINSI;
     |SIGUE;
|FPRC;

|PROCESO MiraTelefonos;
     eaAlfa   = #1#1;  enLinea = 2;  |HAZ LeeTelefono;  |QBF eaAlfa;

     |SI eaAlfa = "";
         eaAlfa = #1#1;  enLinea = 1;  |HAZ LeeTelefono;
     |FINSI;

     |SI eaAlfa = "";
         eaAlfa = #1#54;   |QBF eaAlfa;
     |FINSI;

     eaCampo = eaAlfa;

     |SI eaAlfa = "";
         eaDescri = "Telefono sin contenido.";
         nSwHay   = 1;
         |PRINT;
         |ACABA;
     |FINSI;

     |HAZ QuitaRarosTfno;
     aLong = eaAlfa % 0;
     nLong = aLong;
     |SI nLong ! 9;
         eaDescri = "Telefono con formato erroneo. Debe ser de 9 Digitos";
         nSwHay   = 1;
         |PRINT;
     |FINSI;
|FPRC;

|PROCESO MiraHacienda;
     eaCampo  = (("00" + #1#15) % -102) + (("000" + #1#16) % -103);
     |SI eaCampo = "00000";
         eaDescri = "Codigo de Hacienda sin Contenido";
         nSwHay   = 1;
         |PRINT;
     |FINSI;
|FPRC;

|PROCESO MiraCodPostal;
     eaCampo  = (("00" + #1#25) % -102) + (("000" + #1#26) % -103);
     |SI eaCampo = "00000";
         eaDescri = "Codigo Postal sin Contenido";
         nSwHay   = 1;
         |PRINT;
     |FINSI;
|FPRC;

|PROCESO MiraBlanco;
     |QBF eaCampo;
     |SI eaCampo = "";
         nSwHay   = 1;
         |PRINT;
     |FINSI;
|FPRC;

|PROCESO Chequea;
     eaCampo  = #1#3;  |HAZ MiraDNI;

     eaCampo  = #1#1;  |HAZ MiraNumeros;

     eaDescri = "Apellidos y Nombre sin Contenido";
     eaCampo  = #1#1;  |HAZ MiraBlanco;

                       |HAZ MiraTelefonos;

                       |HAZ MiraHacienda;

     eaDescri = "SG sin Contenido";
     eaCampo  = #1#19; |HAZ MiraBlanco;

     eaDescri = "Direccion sin Contenido";
     eaCampo  = #1#20; |HAZ MiraBlanco;

     eaDescri = "Poblacion sin Contenido";
     eaCampo  = #1#27; |HAZ MiraBlanco;

     eaDescri = "Provincia sin Contenido";
     eaCampo  = #1#28; |HAZ MiraBlanco;
|FPRC;

|DEFBUCLE dsam0000;
     #1#1;
     ;
     #0#0;
     #0#1;
     ;
     NULL, Chequea;
|FIN;

|PROCESO Tipo3;  |TIPO 3;
     |IMPRE 0;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |INFOR "dsmoy025";
     |SI FSalida ! 0;  |FINIMP;  |ACABA;  |FINSI;

     nSwHay = 0;
     |SI #0#0 ! 0;
         |BUCLE dsam0000;
     |SINO;
         |PARA nCampo = 2;  |SI nCampo [ 25;  |HACIENDO nCampo = nCampo + 1;
               |SI #0nCampo ! 0;
                   #0#0 = #nCampo;
                   #0#1 = #nCampo;
                   |BUCLE dsam0000;
               |FINSI;
         |SIGUE;
     |FINSI;

     |SI nSwHay = 1;  |PIEINF;  |FINSI;

     |FININF;
     |FINIMP;
|FPRC;
