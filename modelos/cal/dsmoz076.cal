|FICHEROS;
     dsmoz076;

     dsmom002;
     dsmom003;
     dsmom004;
     dsmom008;
     :/basica/def/agitab99;
     :/basica/def/agicen14;
     :/basica/def/agifigen;

     dsmocivp;   ||especial
     dsmom087;   ||especial
     dsmod310;   ||especial
     dsmoc310;   ||especial
     dsmoc311;   ||especial
     dsm32204;
     dsm32205;

     dsmow109;

     referen@ #999;
     refere1@ #998;

     dsmom009;
     dsmom010;
     dsmom011;
     dsmom012;
     dsmom013;
     dsmopl10;
     dsmopl12;
     dsmopl13;

     dsmom016;
     dsmom017;
     dsmom018;
     dsmom019;
     dsmom068;
|FIN;

|VARIABLES;

     aPassword  = "";

     nCampo     = 0;
     nDEmpresa  = 0;
     nHEmpresa  = 0;
     nDEjer     = 0;
     nHEjer     = 0;
     nDPer      = 0;
     nHPer      = 0;
     nSwHay     = 0;
     nDModelo   = 0;
     nHModelo   = 0;
     swBo       = 0;

     nError     = 0;
     aDirBase   = "";
     aMas       = "";
     aAlfa      = "";
     aAlfa1     = "";
     aAlfa2     = "";
     nPara1     = 0;
     aFicher1   = "";
     aFicher2   = "";
     aFichero   = "";
     nSw        = 0;
     aCampo6    = "";
     nEmpresa   = 0;
     nEjer      = 0;
     nPeriodo   = 0;
     nModelo    = 0;
     nComplem   = 0;
     nActividad = 0;
     aEmpresa   = "";
     aActividad = "";
     aPeriodo   = "";
     aPeriodicidad = "";
     aDatosMod    = "";
     aImpresa     = "";
     aCaptura     = "";

     aError       = "";
     aError0      = "";
     aXm002       = "";
     aXm004       = "";
     aXm008       = "";
     aDAlf1       = "";
     aDAlf2       = "";
     aDAlf3       = "";
     aDAlf4       = "";
     aDAlf5       = "";
     aDAlf6       = "";
     aDAlf7       = "";
     aHAlf1       = "";
     aHAlf2       = "";
     aHAlf3       = "";
     aHAlf4       = "";
     aHAlf5       = "";
     aHAlf6       = "";
     aHAlf7       = "";
     nDNum1       = 0;
     nDNum2       = 0;
     nDNum3       = 0;
     nHNum1       = 0;
     nHNum2       = 0;
     nHNum3       = 0;
     fDFec1       = @;
     fHFec1       = @;
     fLaFecha     = @;
     nCompruebo   = 0;
     nManual      = 0;
     nOk          = 0;
     nNumero      = 0;
     &enSwLinea   = 0;
|FIN;

|INCLUYE i_variar;

|| /////////////////////// PROCESOS ENTRADA POR PANTALLA \\\\\\\\\\\\\\\\\\\\
|PROCESO Tipo7;  |TIPO 7;
     |SI enEmpresa ! 0;
         #dsmoz076#0 = enEmpresa;  |PINTA #dsmoz076#0;
         #dsmoz076#1 = enEmpresa;  |PINTA #dsmoz076#1;

         |C_M #dsmoz076#0, 1, "N";
         |C_M #dsmoz076#1, 1, "N";
     |FINSI;
|FPRC;

|PROCESO Tipo7_16;  |TIPO 7;
    |SI enModelo ! 0;
         #dsmoz076#16 = enModelo;  |PINTA #dsmoz076#16;
         #dsmoz076#17 = enModelo;  |PINTA #dsmoz076#17;

         |C_M #dsmoz076#16, 1, "N";
         |C_M #dsmoz076#17, 1, "N";
    |FINSI;
|FPRC;

|PROCESO Parrilla;
     |SI #dsmoz076#0 = 0;
         #dsmoz076#0 = 0;  |PINTA #dsmoz076#0;
         #dsmoz076#1 = 0;  |PINTA #dsmoz076#1;  |C_M #dsmoz076#1, 1, "N";
         |PARA nCampo = 2;  |SI nCampo [ 13;  |HACIENDO nCampo = nCampo + 1;
               |C_M #dsmoz076#nCampo#, 1, "S";
         |SIGUE;
     |SINO;
         |SI enEmpresa = 0;
             |C_M #dsmoz076#0, 1, "S";
             |C_M #dsmoz076#1, 1, "S";
         |FINSI;

         |PARA nCampo = 2;  |SI nCampo [ 13;  |HACIENDO nCampo = nCampo + 1;
               |C_M #dsmoz076#nCampo#, 1, "N";
               #dsmoz076#nCampo# = 0;  |PINTA #dsmoz076#nCampo#;
         |SIGUE;
     |FINSI;
|FPRC;

|PROCESO Parrilla_16;
     |SI #dsmoz076#16 = 0;
         #dsmoz076#16 = 0;    |PINTA #dsmoz076#16;
         #dsmoz076#17 = 899;  |PINTA #dsmoz076#17;  |C_M #dsmoz076#17, 1, "N";
         |PARA nCampo = 18;  |SI nCampo [ 29;  |HACIENDO nCampo = nCampo + 1;
               |C_M #dsmoz076#nCampo#, 1, "S";
         |SIGUE;
     |SINO;
         |SI enModelo = 0;
             |C_M #dsmoz076#16, 1, "S";
             |C_M #dsmoz076#17, 1, "S";
         |FINSI;

         |PARA nCampo = 18;  |SI nCampo [ 29;  |HACIENDO nCampo = nCampo + 1;
               |C_M #dsmoz076#nCampo#, 1, "N";
               #dsmoz076#nCampo# = 0;  |PINTA #dsmoz076#nCampo#;
         |SIGUE;
     |FINSI;
|FPRC;

|PROCESO NoMasEmpresas;
     |SI #dsmoz076#Campo# = 0;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 13;  |HACIENDO nCampo = nCampo + 1;
               #dsmoz076#nCampo# = 0;  |C_M #dsmoz076#nCampo#, 1, "N";
         |SIGUE;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 13;  |HACIENDO nCampo = nCampo + 1;
               |C_M #dsmoz076#nCampo#, 1, "S";
         |SIGUE;
     |FINSI;
|FPRC;

|PROCESO NoMasModelos;
     |SI #dsmoz076#Campo# = 0;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 29;  |HACIENDO nCampo = nCampo + 1;
               #dsmoz076#nCampo# = 0;  |C_M #dsmoz076#nCampo#, 1, "N";
         |SIGUE;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 29;  |HACIENDO nCampo = nCampo + 1;
               |C_M #dsmoz076#nCampo#, 1, "S";
         |SIGUE;
     |FINSI;
|FPRC;

|PROCESO ConsuModelo;
     |ABRE #agitab99;
     #agitab99#0 = #dsmoz076#Campo#;
     |LEE 040#agitab99.=;
     |SI FSalida ! 0;  |DDEFECTO #agitab99;  |FINSI;
     |CIERRA #agitab99;

     |SI Campo = 16; |O Campo = 17;  |ACABA;  |FINSI;
     |SI #dsmoz076#Campo# = 0; |ACABA; |FINSI;
     |SI #agitab99#4 = "N";
         |MENAV "     Modelo sin Activar.";
         |ERROR;
         |ACABA;
     |FINSI;
     swBo = 0;
     nModelo = #dsmoz076#Campo#;
     |SI nModelo = 110; |O nModelo = 115;  |O nModelo = 123; swBo = 1; |FINSI;
     |SI nModelo = 130; |O nModelo = 131;                    swBo = 1; |FINSI;
     |SI nModelo = 180; |O nModelo = 184;  |O nModelo = 187; swBo = 1; |FINSI;
     |SI nModelo = 190; |O nModelo = 193;                    swBo = 1; |FINSI;
     |SI nModelo = 216; |O nModelo = 296;                    swBo = 1; |FINSI;
     |SI nModelo = 300; |O nModelo = 303;  |O nModelo = 310; swBo = 1; |FINSI;
     |SI nModelo = 320; |O nModelo = 330;  |O nModelo = 332; swBo = 1; |FINSI;
     |SI nModelo = 322; |O nModelo = 340;  |O nModelo = 353; swBo = 1; |FINSI;
     |SI nModelo = 347; |O nModelo = 349;  |O nModelo = 370; swBo = 1; |FINSI;
     |SI nModelo = 390; |O nModelo = 392;                    swBo = 1; |FINSI;
     |SI nModelo ] 901; |Y nModelo [ 999;                    swBo = 1; |FINSI;
     |SI swBo = 0;
         |MENAV "     Modelo no Valido para este Chequeo";
         |ERROR;
     |FINSI;
|FPRC;

|CALCULO Eano; |TIPO 7;
 fLaFecha = ~;
 aAlfa1 = fLaFecha;
 aAlfa1 = aAlfa1 % -104;
 #dsmoz076#14 = aAlfa1;
 #dsmoz076#14 = #dsmoz076#14 - 2; |PINTA #dsmoz076#14;
 #dsmoz076#15 = #dsmoz076#14 + 1; |PINTA #dsmoz076#15;
|FCAL;

|PROCESO Periodo;
     nCampo   = Campo + 1;
     #dsmoz076#nCampo# = "          ";
     |SI #dsmoz076#Campo# = 1;   #dsmoz076#nCampo# = "ENERO     ";  |FINSI;
     |SI #dsmoz076#Campo# = 2;   #dsmoz076#nCampo# = "FEBRERO   ";  |FINSI;
     |SI #dsmoz076#Campo# = 3;   #dsmoz076#nCampo# = "MARZO     ";  |FINSI;
     |SI #dsmoz076#Campo# = 4;   #dsmoz076#nCampo# = "ABRIL     ";  |FINSI;
     |SI #dsmoz076#Campo# = 5;   #dsmoz076#nCampo# = "MAYO      ";  |FINSI;
     |SI #dsmoz076#Campo# = 6;   #dsmoz076#nCampo# = "JUNIO     ";  |FINSI;
     |SI #dsmoz076#Campo# = 7;   #dsmoz076#nCampo# = "JULIO     ";  |FINSI;
     |SI #dsmoz076#Campo# = 8;   #dsmoz076#nCampo# = "AGOSTO    ";  |FINSI;
     |SI #dsmoz076#Campo# = 9;   #dsmoz076#nCampo# = "SEPTIEMBRE";  |FINSI;
     |SI #dsmoz076#Campo# = 10;  #dsmoz076#nCampo# = "OCTUBRE   ";  |FINSI;
     |SI #dsmoz076#Campo# = 11;  #dsmoz076#nCampo# = "NOVIEMBRE ";  |FINSI;
     |SI #dsmoz076#Campo# = 12;  #dsmoz076#nCampo# = "DICIEMBRE ";  |FINSI;
     |SI #dsmoz076#Campo# = 99;  #dsmoz076#nCampo# = "ANUAL     ";  |FINSI;

     |SI #dsmoz076#nCampo# = "          ";
         |MENAV "     Periodo Incorrecto.";
         |ERROR;
     |FINSI;

     |PINTA #dsmoz076#nCampo#;
|FPRC;

|PROCESO Tipo7C34; |TIPO 7;
     aAlfa = "N";
     swBo = 0;
     |SI #dsmoz076#16 ! 0;
         |SI #dsmoz076#16 [ 130; |Y #dsmoz076#17 ] 130; swBo = 1; |FINSI;
         |SI #dsmoz076#16 [ 131; |Y #dsmoz076#17 ] 131; swBo = 1; |FINSI;
         |SI #dsmoz076#16 [ 303; |Y #dsmoz076#17 ] 303; swBo = 1; |FINSI;
     |SINO;
         |PARA nPara1 = 18; |SI nPara1 [ 29; |HACIENDO nPara1 = nPara1 + 1;
               |SI #dsmoz076#nPara1# = 130; |O #dsmoz076#nPara1# = 131;
                  |O #dsmoz076#nPara1# = 303;
                       swBo = 1; |SAL;
               |FINSI;
         |SIGUE;
     |FINSI;
     |SI swBo = 1; aAlfa = "S"; |FINSI;
     |C_M #dsmoz076#34, 1, aAlfa;
     |SI aAlfa = "N";
         #dsmoz076#34 = "N";
         |PINTA #dsmoz076#34;
     |FINSI;
|FPRC;

|| ///////////////////////////////////////////////////////////////////
|| /////////////////////// PROCESOS DE INFORMES \\\\\\\\\\\\\\\\\\\\\
|PROCESO SelModelo;
 |ABRE #agitab99;
 #agitab99#0 = nModelo;
 |LEE 040#agitab99.=;
 |SI FSalida ! 0;  |DDEFECTO #agitab99;  |FINSI;
 |CIERRA #agitab99;

 swBo = 0;
 |SI #dsmoz076#16 = 0;
     |PARA nPara1 = 18; |SI nPara1 [ 29; |HACIENDO nPara1 = nPara1 + 1;
           |SI #dsmoz076#nPara1# = nModelo;
               swBo = 1; |SAL;
           |FINSI;
     |SIGUE;
 |SINO;
     |SI nModelo ] #dsmoz076#16; |Y nModelo [ #dsmoz076#17;
         swBo = 1;
     |FINSI;
 |FINSI;
|FPRC;

|PROCESO CargarFicheros;
 nError = 0;

 |DBASE aDirBase;

 aMas = aDirBase + "def/" + aFicher1;
 |CARGA_DEF aMas, referen@;
 |SI FSalida ! 0;
     aAlfa1 = "    Error cargando fichero " + aFicher1;
     |MENAV aAlfa1;
     nError = 1;
     |ACABA;
 |FINSI;
 |SI aFicher2 ! "";
     aMas = aDirBase + "def/" + aFicher2;
     |CARGA_DEF aMas, refere1@;
     |SI FSalida ! 0;
         aAlfa1 = "    Error cargando fichero " + aFicher2;
         |MENAV aAlfa1;
         nError = 1;
         |DESCARGA_DEF #referen@;
     |FINSI;
 |FINSI;
|FPRC;

|PROCESO DesCargarFicheros;
 |SI aFicher2 ! ""; |DESCARGA_DEF #refere1@; |FINSI;
 |DESCARGA_DEF #referen@;
|FPRC;

|PROCESO GrabaW109;
 #dsmow109#0 = nEmpresa;
 #dsmow109#1 = nEjer;
 #dsmow109#2 = nPeriodo;
 #dsmow109#3 = nModelo;
 #dsmow109#4 = nComplem;
 #dsmow109#5 = nActividad;
 |LEE 101#dsmow109.=;
 |SI FSalida ! 0;
     #dsmow109#0 = nEmpresa;
     #dsmow109#1 = nEjer;
     #dsmow109#2 = nPeriodo;
     #dsmow109#3 = nModelo;
     #dsmow109#4 = nComplem;
     #dsmow109#5 = nActividad;
     #dsmow109#6 = aEmpresa;
     #dsmow109#7 = aActividad;
     #dsmow109#8 = aPeriodo;
     #dsmow109#9 = aPeriodicidad;
     |GRABA 020#dsmow109.b;
 |FINSI;
 #dsmow109#10 = aError;
 #dsmow109#11 = aXm002;
 #dsmow109#12 = aXm004;
 #dsmow109#13 = aXm008;
 #dsmow109#14 = aError0;
 #dsmow109#15 = aCaptura;
 #dsmow109#16 = aDatosMod;
 #dsmow109#17 = aImpresa;
 #dsmow109#18 = nNumero;
 |GRABA 020#dsmow109.a;
 |LIBERA #dsmow109;
 nSwHay = 1;
 nNumero = nNumero + 1;
|FPRC;

|PROCESO LeoActividad;
      aActividad = "";
      aCaptura   = "";
      |SI nActividad ! 0;
          |DDEFECTO #agicen14;
          #agicen14#0 = nEmpresa;
          #agicen14#1 = nActividad;
          |LEE 001#agicen14.=;
          aActividad = #agicen14#4;
          aCaptura   = #agicen14#15;
      |SINO;
          |DDEFECTO #agicen14;
          #agicen14#0 = nEmpresa;
          #agicen14#1 = 1;
          |LEE 001#agicen14.];
          |SI FSalida = 0; |Y #agicen14#0 = nEmpresa;
              aActividad = #agicen14#4;
              aCaptura   = #agicen14#15;
          |FINSI;
      |FINSI;
|FPRC;

|PROCESO LeoEmpresa;
      |DDEFECTO #agifigen;
      #agifigen#0 = nEmpresa;
      |LEE 001#agifigen.=;
      aEmpresa = #agifigen#1;
|FPRC;

|PROCESO LeoPeriodo;
     aPeriodo  = "          ";
     |SI nPeriodo = 1;   aPeriodo = "ENERO     ";  |FINSI;
     |SI nPeriodo = 2;   aPeriodo = "FEBRERO   ";  |FINSI;
     |SI nPeriodo = 3;   aPeriodo = "MARZO     ";  |FINSI;
     |SI nPeriodo = 4;   aPeriodo = "ABRIL     ";  |FINSI;
     |SI nPeriodo = 5;   aPeriodo = "MAYO      ";  |FINSI;
     |SI nPeriodo = 6;   aPeriodo = "JUNIO     ";  |FINSI;
     |SI nPeriodo = 7;   aPeriodo = "JULIO     ";  |FINSI;
     |SI nPeriodo = 8;   aPeriodo = "AGOSTO    ";  |FINSI;
     |SI nPeriodo = 9;   aPeriodo = "SEPTIEMBRE";  |FINSI;
     |SI nPeriodo = 10;  aPeriodo = "OCTUBRE   ";  |FINSI;
     |SI nPeriodo = 11;  aPeriodo = "NOVIEMBRE ";  |FINSI;
     |SI nPeriodo = 12;  aPeriodo = "DICIEMBRE ";  |FINSI;
     |SI nPeriodo = 99;  aPeriodo = "ANUAL     ";  |FINSI;
|FPRC;

|| ////////////////////////////////////////////////////////////////
|PROCESO Borra10;
     |BORRA 020#dsmom010.a;
     |LIBERA #dsmom010;
|FPRC;

|DEFBUCLE dsmom010;
     #dsmom010#1;
     ;
     #dsmom009#0, #dsmom009#1, #dsmom009#2, #dsmom009#3, #dsmom009#4, "    ";
     #dsmom009#0, #dsmom009#1, #dsmom009#2, #dsmom009#3, #dsmom009#4, "zzzz";
     be;
     NULL, Borra10;
|FIN;

|PROCESO Borra11;
     |BORRA 020#dsmom011.a;
     |LIBERA #dsmom011;
|FPRC;

|DEFBUCLE dsmom011;
     #dsmom011#1;
     ;
     #dsmom009#0, #dsmom009#1, #dsmom009#2, #dsmom009#3, #dsmom009#4, "    ";
     #dsmom009#0, #dsmom009#1, #dsmom009#2, #dsmom009#3, #dsmom009#4, "zzzz";
     be;
     NULL, Borra11;
|FIN;

|PROCESO Borra12;
     |BORRA 020#dsmom012.a;
     |LIBERA #dsmom012;
|FPRC;

|DEFBUCLE dsmom012;
     #dsmom012#1;
     ;
     #dsmom009#0, #dsmom009#1, #dsmom009#2, #dsmom009#3, #dsmom009#4, "    ";
     #dsmom009#0, #dsmom009#1, #dsmom009#2, #dsmom009#3, #dsmom009#4, "zzzz";
     be;
     NULL, Borra12;
|FIN;

|PROCESO Borra13;
     |BORRA 020#dsmom013.a;
     |LIBERA #dsmom013;
|FPRC;

|DEFBUCLE dsmom013;
     #dsmom013#1;
     ;
     #dsmom009#0, #dsmom009#1, #dsmom009#2, #dsmom009#3, #dsmom009#4, "    ";
     #dsmom009#0, #dsmom009#1, #dsmom009#2, #dsmom009#3, #dsmom009#4, "zzzz";
     be;
     NULL, Borra13;
|FIN;

|PROCESO Borra10pl;
     |BORRA 020#dsmopl10.a;
     |LIBERA #dsmopl10;
|FPRC;

|DEFBUCLE dsmopl10;
     #dsmopl10#1;
     ;
     #dsmom009#0, #dsmom009#1, #dsmom009#2, #dsmom009#3, #dsmom009#4, "            ";
     #dsmom009#0, #dsmom009#1, #dsmom009#2, #dsmom009#3, #dsmom009#4, "zzzzzzzzzzzz";
     be;
     NULL, Borra10pl;
|FIN;

|PROCESO Borra12pl;
     |BORRA 020#dsmopl12.a;
     |LIBERA #dsmopl12;
|FPRC;

|DEFBUCLE dsmopl12;
     #dsmopl12#1;
     ;
     #dsmom009#0, #dsmom009#1, #dsmom009#2, #dsmom009#3, #dsmom009#4, "            ";
     #dsmom009#0, #dsmom009#1, #dsmom009#2, #dsmom009#3, #dsmom009#4, "zzzzzzzzzzzz";
     be;
     NULL, Borra12pl;
|FIN;

|PROCESO Borra13pl;
     |BORRA 020#dsmopl13.a;
     |LIBERA #dsmopl13;
|FPRC;

|DEFBUCLE dsmopl13;
     #dsmopl13#1;
     ;
     #dsmom009#0, #dsmom009#1, #dsmom009#2, #dsmom009#3, #dsmom009#4, "            ";
     #dsmom009#0, #dsmom009#1, #dsmom009#2, #dsmom009#3, #dsmom009#4, "zzzzzzzzzzzz";
     be;
     NULL, Borra13pl;
|FIN;

|PROCESO BorraModulos;
     |ABRE #dsmom018;
     #dsmom018#0 = #dsmom017#0;
     #dsmom018#1 = #dsmom017#1;
     #dsmom018#2 = #dsmom017#2;
     #dsmom018#3 = #dsmom017#3;
     |LEE 101#dsmom018.=;
     |SI FSalida = 0;
         |BORRA 020#dsmom018.a;
     |FINSI;
     |LIBERA #dsmom018;
     |CIERRA #dsmom018;

     |ABRE #dsmom068;
     #dsmom068#0 = #dsmom017#0;
     #dsmom068#1 = #dsmom017#1;
     #dsmom068#2 = #dsmom017#2;
     #dsmom068#3 = #dsmom017#3;
     |LEE 101#dsmom068.=;
     |SI FSalida = 0;
         |BORRA 020#dsmom068.a;
     |FINSI;
     |LIBERA #dsmom068;
     |CIERRA #dsmom068;

     |ABRE #dsmom019;
     #dsmom019#0 = #dsmom017#0;
     #dsmom019#1 = #dsmom017#1;
     #dsmom019#2 = #dsmom017#2;
     #dsmom019#3 = #dsmom017#3;
     |LEE 101#dsmom019.=;
     |SI FSalida = 0;
         |BORRA 020#dsmom019.a;
     |FINSI;
     |LIBERA #dsmom019;
     |CIERRA #dsmom019;
|FPRC;

|PROCESO BajaDsmom008;
     |SI #dsmom008#7 = "PLANILLAS ";
         |ABRE #dsmom009;
         #dsmom009#0 =  #dsmom008#0;
         #dsmom009#1 =  #dsmom008#1;
         #dsmom009#2 =  #dsmom008#2;
         #dsmom009#3 =  #dsmom008#4;
         #dsmom009#4 =  #dsmom008#5;
         |LEE 101#dsmom009.=;
         |SI FSalida = 0;
             |SI #dsmoz076#34 = "N";
                 |SI #dsmom008#3 = 300;  #dsmom009#7  = " ";  |FINSI;
                 |SI #dsmom008#3 = 303;  #dsmom009#7  = " ";  |FINSI;
                 |SI #dsmom008#3 = 370;  #dsmom009#8  = " ";  |FINSI;
                 |SI #dsmom008#3 = 320;  #dsmom009#9  = " ";  |FINSI;
                 |SI #dsmom008#3 = 320;  #dsmom009#9  = " ";  |FINSI;
                 |SI #dsmom008#3 = 330;  #dsmom009#10 = " ";  |FINSI;
                 |SI #dsmom008#3 = 332;  #dsmom009#11 = " ";  |FINSI;
                 |SI #dsmom008#3 = 349;  #dsmom009#12 = " ";  |FINSI;
                 |SI #dsmom008#3 = 130;  #dsmom009#14 = " ";  |FINSI;
                 |SI #dsmom008#3 = 131;  #dsmom009#15 = " ";  |FINSI;
                 |GRABA 020#dsmom009.a;
             |SINO;
                 |BORRA 020#dsmom009.a;

                 |BUCLE dsmom010;
                 |BUCLE dsmom011;
                 |BUCLE dsmom012;
                 |BUCLE dsmom013;
                 |SI #dsmom008#1 ] 2019;
                     |BUCLE dsmopl10;
                     |BUCLE dsmopl12;
                     |BUCLE dsmopl13;
                 |FINSI;
             |FINSI;
             |LIBERA #dsmom009;
             |CIERRA #dsmom009;
         |FINSI;
     |FINSI;

     |SI #dsmom008#7 = "MODULOS   ";
         |ABRE #dsmom017;
         #dsmom017#0 =  #dsmom008#0;
         #dsmom017#1 =  #dsmom008#1;
         #dsmom017#2 =  #dsmom008#4;
         #dsmom017#3 =  #dsmom008#5;
         |LEE 101#dsmom017.=;
         |SI FSalida = 0; |Y #dsmoz076#34 = "S";
             |HAZ BorraModulos;
             |BORRA 020#dsmom017.a;
          |FINSI;
         |LIBERA #dsmom017;
         |CIERRA #dsmom017;
     |FINSI;

     |BORRA 020#dsmom008.a;
     |LIBERA #dsmom008;
|FPRC;

|DEFBUCLE BajaDsmom008;
  #dsmom008#1;
  ;
  nEmpresa, nEjer, nPeriodo, nModelo, nComplem, 00;
  nEmpresa, nEjer, nPeriodo, nModelo, nComplem, 99;
  be;
  NULL, BajaDsmom008;
|FIN;

|PROCESO BajaDsmom004;
  #dsmom004#0 = nEmpresa;
  #dsmom004#1 = nEjer;
  #dsmom004#2 = nPeriodo;
  #dsmom004#3 = nModelo;
  #dsmom004#4 = nComplem;
  |LEE 141#dsmom004.=;
  |SI FSalida = 0;
      |BORRA 020#dsmom004.a;

      |SI #dsmoz076#36 = "S";
          |PRINT; nSw = 1;
      |FINSI;
      enSwLinea = 1;
  |FINSI;
  |LIBERA #dsmom004;
|FPRC;
|| .......................

|PROCESO Especial_87;
   |BORRA 020#dsmom087.a;
   |LIBERA #dsmom087;
|FPRC;

|DEFBUCLE Especial_87;
 #dsmom087#1;
 ;
 nEmpresa, nEjer, nPeriodo, nModelo, nComplem, 00, nDNum1,nDNum2;
 nEmpresa, nEjer, nPeriodo, nModelo, nComplem, 99, nHNum1,nHNum2;
 e;
 NULL, Especial_87;
|FIN;

|PROCESO Especial_civp;
   |BORRA 020#dsmocivp.a;
   |LIBERA #dsmocivp;
|FPRC;

|DEFBUCLE Especial_civp;
 #dsmocivp#1;
 ;
 nEmpresa, nEjer, nPeriodo, nModelo, nComplem, 00, nDNum1,nDNum2;
 nEmpresa, nEjer, nPeriodo, nModelo, nComplem, 99, nHNum1,nHNum2;
 e;
 NULL, Especial_civp;
|FIN;

|| .......................
|PROCESO Borro32204;
  |BORRA 020#dsm32204.a;
  |LIBERA #dsm32204;
|FPRC;

|DEFBUCLE Borro32204;
 #dsm32204#1;
 ;
 nEmpresa, nEjer, 000, nComplem, 00;
 nEmpresa, nEjer, 999, nComplem, 99;
 e;
 NULL, Borro32204;
|FIN;

|PROCESO Borro32205;
  |BORRA 020#dsm32205.a;
  |LIBERA #dsm32205;
|FPRC;

|DEFBUCLE Borro32205;
 #dsm32205#1;
 ;
 nEmpresa, nEjer, 000, nComplem, 0;
 nEmpresa, nEjer, 999, nComplem, 9;
 e;
 NULL, Borro32205;
|FIN;

|PROCESO BorroD310;
  |BORRA 020#dsmod310.a;
  |LIBERA #dsmod310;
|FPRC;

|DEFBUCLE BorroD310;
 #dsmod310#1;
 ;
 nEmpresa, nEjer, nPeriodo, 000, nComplem, 00;
 nEmpresa, nEjer, nPeriodo, 999, nComplem, 99;
 e;
 NULL, BorroD310;
|FIN;

|PROCESO Borro311;
  |BORRA 020#dsmoc311.a;
  |LIBERA #dsmoc311;
|FPRC;

|DEFBUCLE Borro311;
 #dsmoc311#1;
 ;
 nEmpresa, nEjer, nPeriodo, 000, nComplem, 00, 000;
 nEmpresa, nEjer, nPeriodo, 999, nComplem, 99, 999;
 e;
 NULL, Borro311;
|FIN;

|PROCESO Borro310;
  |BORRA 020#dsmoc310.a;
  |LIBERA #dsmoc310;
|FPRC;

|DEFBUCLE Borro310;
 #dsmoc310#1;
 ;
 nEmpresa, nEjer, nPeriodo, 000, nComplem, 00;
 nEmpresa, nEjer, nPeriodo, 999, nComplem, 99;
 e;
 NULL, Borro310;
|FIN;

|| .......................

|PROCESO LeeModelo_13X;
     enMensa       = 1;
     enTipoEntrada = 3;
     enEmpresa     = nEmpresa;
     enEjer        = nEjer;
     enPeriodo     = nPeriodo;
     enModelo      = nModelo;
     enComplem     = nComplem;
     enActividad   = #referen@#5;
     |SI nModelo   = 130; aAlfa = ":modelos/dsmoc130"; |FINSI;
     |SI nModelo   = 131; aAlfa = ":modelos/dsmoc131"; |FINSI;
     |SI nModelo   = 340; aAlfa = ":modelos/dsmo3401"; |FINSI;

     |SUB_EJECUTA aAlfa;
     |PULSATECLA;
|FPRC;

|PROCESO LeeModelo;
   |BORRA 020#referen@.a;
   |LIBERA #referen@;
|FPRC;

|DEFBUCLE LeeModelo_13X;
 #referen@#1006;
 ;
 nEmpresa, nEjer, nPeriodo, nModelo, nComplem, 00;
 nEmpresa, nEjer, nPeriodo, nModelo, nComplem, 99;
 e;
 NULL, LeeModelo_13X
|FIN;

|DEFBUCLE LeeModelo_AA;
 #referen@#1008;
 ;
 nEmpresa, nEjer, nPeriodo, nModelo, nComplem, 00, aDAlf1,aDAlf2;
 nEmpresa, nEjer, nPeriodo, nModelo, nComplem, 99, aHAlf1,aHAlf2;
 e;
 NULL, LeeModelo;
|FIN;

|DEFBUCLE LeeModelo_NN;
 #referen@#1008;
 ;
 nEmpresa, nEjer, nPeriodo, nModelo, nComplem, 00, nDNum1,nDNum2;
 nEmpresa, nEjer, nPeriodo, nModelo, nComplem, 99, nHNum1,nHNum2;
 e;
 NULL, LeeModelo;
|FIN;

|DEFBUCLE LeeModelo_ANNA;
 #referen@#1010;
 ;
 nEmpresa, nEjer, nPeriodo, nModelo, nComplem, 00, aDAlf1, nDNum1,nDNum2, aDAlf2;
 nEmpresa, nEjer, nPeriodo, nModelo, nComplem, 99, aHAlf1, nHNum1,nHNum2, aHAlf2;
 e;
 NULL, LeeModelo;
|FIN;

|DEFBUCLE LeeModelo_ANANN;
 #referen@#1011;
 ;
 nEmpresa, nEjer, nPeriodo, nModelo, nComplem, 00, aDAlf1, nDNum1, aDAlf2, nDNum2, nDNum3;
 nEmpresa, nEjer, nPeriodo, nModelo, nComplem, 99, aHAlf1, nHNum1, aHAlf2, nHNum2, nHNum3;
 e;
 NULL, LeeModelo;
|FIN;

|DEFBUCLE LeeModelo_AFNANN;
 #referen@#1012;
 ;
 nEmpresa, nEjer, nPeriodo, nModelo, nComplem, 00, aDAlf1,fDFec1,nDNum1,aDAlf2,nDNum2,nDNum3;
 nEmpresa, nEjer, nPeriodo, nModelo, nComplem, 99, aHAlf1,fHFec1,nHNum1,aHAlf2,nHNum2,nHNum3;
 e;
 NULL, LeeModelo;
|FIN;

|DEFBUCLE LeeModelo_ANN;
 #referen@#1009;
 ;
 nEmpresa, nEjer, nPeriodo, nModelo, nComplem, 00, aDAlf1, nDNum1,nDNum2;
 nEmpresa, nEjer, nPeriodo, nModelo, nComplem, 99, aHAlf1, nHNum1,nHNum2;
 e;
 NULL, LeeModelo;
|FIN;

|DEFBUCLE LeeModelo_ANNAAN;
 #referen@#1012;
 ;
 nEmpresa,nEjer, nPeriodo, nModelo, nComplem, 00, aDAlf1,nDNum1,nDNum2,aDAlf2,aDAlf3,nDNum3;
 nEmpresa,nEjer, nPeriodo, nModelo, nComplem, 99, aHAlf1,nHNum1,nHNum2,aHAlf2,aHAlf3,nHNum3;
 e;
 NULL, LeeModelo;
|FIN;

|DEFBUCLE LeeModelo_ANAA;
 #referen@#1010;
 ;
 nEmpresa, nEjer, nPeriodo, nModelo, nComplem, 00, aDAlf1, nDNum1, aDAlf2, aDAlf3;
 nEmpresa, nEjer, nPeriodo, nModelo, nComplem, 99, aHAlf1, nHNum1, aHAlf2, aHAlf3;
 e;
 NULL, LeeModelo;
|FIN;

|| .......................
|PROCESO Proc109Manual;
      nOk = 0;

      |ABRE #referen@;
      |SI nModelo = 184; |O nModelo = 390; |O nModelo = 392;
          #referen@#0 = nEmpresa;
          #referen@#1 = nEjer;
          #referen@#2 = nModelo;
          #referen@#3 = nComplem;
      |SINO;
          #referen@#0 = nEmpresa;
          #referen@#1 = nEjer;
          #referen@#2 = nPeriodo;
          #referen@#3 = nModelo;
          #referen@#4 = nComplem;
          #referen@#5 = 0;
      |FINSI;
      |LEE 141#referen@.=;
      |SI FSalida = 0;
          nOk = 1;
      |FINSI;
      |LIBERA #referen@;
      |CIERRA #referen@;

      |SI nOk = 1;
          enMensa       = 1;
          enTipoEntrada = 3;
          enEmpresa     = nEmpresa;
          enEjer        = nEjer;
          enPeriodo     = nPeriodo;
          enModelo      = nModelo;
          enComplem     = nComplem;
          enActividad   = 0;
          |SI nModelo   = 180; aAlfa = ":modelos/dsm180ca"; |FINSI;
          |SI nModelo   = 184; aAlfa = ":modelos/dsm18401"; |FINSI;
          |SI nModelo   = 187; aAlfa = ":modelos/dsm187ca"; |FINSI;
          |SI nModelo   = 190; aAlfa = ":modelos/dsm190ca"; |FINSI;
          |SI nModelo   = 193; aAlfa = ":modelos/dsm193ca"; |FINSI;
          |SI nModelo   = 296; aAlfa = ":modelos/dsm296ca"; |FINSI;
          |SI nModelo   = 347; aAlfa = ":modelos/dsmo1347"; |FINSI;
          |SI nModelo   = 390; aAlfa = ":modelos/dsm39001"; |FINSI;
          |SI nModelo   = 392; aAlfa = ":modelos/dsm39201"; |FINSI;

          |SI nModelo   = 303; aAlfa = ":modelos/dsm30301"; |FINSI;
          |SUB_EJECUTA aAlfa;

          |PULSATECLA;
      |FINSI;
|FPRC;

|| /////////////////////////////////////////////////
|PROCESO BajaModelo303;
     enActividad   = #dsmom008#5;
     |SI #dsmom008#12 = 303; aAlfa = ":modelos/dsmociva"; |FINSI;
     |SI #dsmom008#12 = 310; aAlfa = ":modelos/dsmoc310"; |FINSI;
     |SUB_EJECUTA aAlfa;
     |PULSATECLA;

     |LIBERA #dsmom008;
|FPRC;

|DEFBUCLE BajaModelo303;
  #dsmom008#1;
  ;
  nEmpresa, nEjer, nPeriodo, nModelo, nComplem, 01;
  nEmpresa, nEjer, nPeriodo, nModelo, nComplem, 99;
  e;
  NULL, BajaModelo303;
|FIN;

|PROCESO Proc109EspIVA;
      nOk = 0;

      |ABRE #referen@;
      #referen@#0 = nEmpresa;
      #referen@#1 = nEjer;
      #referen@#2 = nPeriodo;
      #referen@#3 = nModelo;
      #referen@#4 = nComplem;
      #referen@#5 = 0;
      |LEE 141#referen@.=;
      |SI FSalida = 0;
          nOk = 1;
      |FINSI;
      |LIBERA #referen@;
      |CIERRA #referen@;

      |SI nOk = 1;
          enMensa       = 1;
          enTipoEntrada = 3;
          enEmpresa     = nEmpresa;
          enEjer        = nEjer;
          enPeriodo     = nPeriodo;
          enModelo      = nModelo;
          enComplem     = nComplem;

          |BUCLE BajaModelo303;
      |FINSI;
|FPRC;

|PROCESO ProcDsmow109;
  nEmpresa   = #dsmow109#0;
  nPeriodo   = #dsmow109#2;
  nComplem   = #dsmow109#4;

  |SI nManual = 1;
      |HAZ Proc109Manual;
  |FINSI;

  |SI nManual = 2;
      |HAZ Proc109EspIVA;
  |FINSI;

  |SI nManual = 0;
      |SI aFicher2 ! "";
          #refere1@#0 = nEmpresa;
          #refere1@#1 = nEjer;
          #refere1@#2 = nPeriodo;
          #refere1@#3 = nModelo;
          #refere1@#4 = nComplem;
          #refere1@#5 = 0;
          |LEE 141#refere1@.=;
          |SI FSalida = 0;
              |BORRA 020#refere1@.a;
          |FINSI;
          |LIBERA #refere1@;
      |FINSI;

      |SI nModelo = 110;
          |SI nEjer [ 2010;  |BUCLE LeeModelo_ANAA;    |FINSI;
          |SI nEjer ] 2011;  |BUCLE LeeModelo_ANNAAN;  |FINSI;
      |FINSI;

      |SI nModelo = 115;     |BUCLE LeeModelo_ANN;     |FINSI;
      |SI nModelo = 123;     |BUCLE LeeModelo_ANANN;   |FINSI;
      |SI nModelo = 216;     |BUCLE LeeModelo_AFNANN;  |FINSI;
      |SI nModelo = 349;     |BUCLE LeeModelo_ANNA;    |FINSI;
      |SI nModelo = 353;     |BUCLE LeeModelo_AA;      |FINSI;

      |SI nModelo = 130; |O nModelo = 131; |O nModelo = 340;
         |BUCLE LeeModelo_13X;
      |FINSI;

      |SI nModelo = 300; |O nModelo = 303; |O nModelo = 320;
           |O nModelo = 330; |O nModelo = 332;
             |O nModelo = 310; |O nModelo = 370; |O nModelo = 322;
          |BUCLE LeeModelo_NN;
      |FINSI;

      |SI aFicher1 = "dsmociva"; |O aFicher1 = "dsmoc322";
          |SI nEjer [ 2010;
              |BUCLE Especial_civp;
          |SINO;
              |BUCLE Especial_87;
          |FINSI;
      |FINSI;

      |SI nModelo = 310; |O nModelo = 370;
          |BUCLE Borro310;
          |BUCLE Borro311;
          |SI nModelo = 370; |BUCLE BorroD310; |FINSI;
      |FINSI;

      |SI nModelo = 322; |Y nPeriodo = 12;
          |BUCLE Borro32204;
          |BUCLE Borro32205;
      |FINSI;
  |FINSI;

  |BUCLE BajaDsmom008;

  |HAZ BajaDsmom004;

|FPRC;

|DEFBUCLE LeeDsmow109;
  #dsmow109#2;
  ;
  nEjer, 00001, nModelo, 00, 00, 00;
  nEjer, 99999, nModelo, 99, 99, 00;
  ;
  NULL, ProcDsmow109;
|FIN;

|PROCESO ElPrincipal;

 |HAZ CargarFicheros;
 |SI nError = 1; |ACABA; |FINSI;

 |ABRE #dsmom004;

 |SI aFicher2 ! "";  |ABRE #refere1@;   |FINSI;
 |BUCLE LeeDsmow109;
 |SI aFicher2 ! "";  |CIERRA #refere1@; |FINSI;

 |CIERRA #dsmom004;

 |HAZ DesCargarFicheros;

|FPRC;

|| ///////////////////////////////////////////////////////////////////////
|PROCESO dsmXAnual;
 |HAZ SelModelo;
 |SI swBo = 0; |ACABA; |FINSI;

 nManual = 1;
 aFicher2 = "";
 |SI nModelo = 180;  aFicher1 = "dsm180ca"; |FINSI;
 |SI nModelo = 184;  aFicher1 = "dsm18401"; |FINSI;
 |SI nModelo = 187;  aFicher1 = "dsm187ca"; |FINSI;
 |SI nModelo = 190;  aFicher1 = "dsm190ca"; |FINSI;
 |SI nModelo = 193;  aFicher1 = "dsm193ca"; |FINSI;
 |SI nModelo = 296;  aFicher1 = "dsm296ca"; |FINSI;
 |SI nModelo = 347;  aFicher1 = "dsmo1347"; |FINSI;
 |SI nModelo = 390;  aFicher1 = "dsm39001"; |FINSI;
 |SI nModelo = 392;  aFicher1 = "dsm39201"; |FINSI;
 |SI nModelo = 303;  aFicher1 = "dsm30301"; nManual = 2; |FINSI;

 |HAZ CargarFicheros;
 |SI nError = 1; |ACABA; |FINSI;

 |ABRE #dsmom004;
 |ABRE #referen@;
 |BUCLE LeeDsmow109;
 |CIERRA #referen@;
 |CIERRA #dsmom004;
 nManual = 0;

 |HAZ DesCargarFicheros;
|FPRC;

|PROCESO dsmod110;
 |HAZ SelModelo;
 |SI swBo = 0; |ACABA; |FINSI;

                    aFicher2 = "dsmod110";
 |SI nEjer [ 2010;  aFicher1 = "dsmoc110";  |FINSI;
 |SI nEjer = 2011;  aFicher1 = "dsmoc111";  |FINSI;
 |SI nEjer = 2012;  aFicher1 = "dsmoc111";  |FINSI;
 |SI nEjer = 2013;  aFicher1 = "dsmoc111";  |FINSI;
 |SI nEjer = 2014;  aFicher1 = "dsmoc111";  |FINSI;
 |SI nEjer = 2015;  aFicher1 = "dsmoc111";  |FINSI;
 |SI nEjer ] 2016;  aFicher1 = "dsm11101";  |FINSI;

 aDAlf1 = "            "; aHAlf1 = "zzzzzzzzzzzz";
 aDAlf2 = " ";            aHAlf2 = "z";
 aDAlf3 = "  ";           aHAlf3 = "zz";
 nDNum1 = 0000;           nHNum1 = 9999;
 nDNum2 = 00;             nHNum2 = 99;
 nDNum3 = 0;              nHNum3 = 9;

 |HAZ ElPrincipal;
|FPRC;

|PROCESO dsmod115;
 |HAZ SelModelo;
 |SI swBo = 0; |ACABA; |FINSI;

 aFicher1 = "dsmoc115";
 aFicher2 = "dsmod115";

 aDAlf1 = "            "; aHAlf1 = "zzzzzzzzzzzz";
 nDNum1 = 0000;           nHNum1 = 9999;
 nDNum2 = 01;             nHNum2 = 99;

 |HAZ ElPrincipal;
|FPRC;

|PROCESO dsmod123;
 |HAZ SelModelo;
 |SI swBo = 0; |ACABA; |FINSI;

 aFicher1 = "dsmoc123";
 aFicher2 = "dsmod123";

 aDAlf1 = "            "; aHAlf1 = "zzzzzzzzzzzz";
 nDNum1 = 0000;           nHNum1 = 9999;
 aDAlf2 = " ";            aHAlf2 = "z";
 nDNum2 = 00;             nHNum2 = 12;
 nDNum3 = 1;              nHNum3 = 2;

 |HAZ ElPrincipal;
|FPRC;

|PROCESO dsmod216;
 |HAZ SelModelo;
 |SI swBo = 0; |ACABA; |FINSI;

 aFicher1 = "dsmoc216";
 aFicher2 = "dsmod216";

 aDAlf1 = "            "; aHAlf1 = "zzzzzzzzzzzz";
 fDFec1 = "01.01.0000";   fHFec1 = "31.12.2999";
 nDNum1 = 1;              nHNum1 = 2;
 aDAlf2 = " ";            aHAlf2 = "z";
 nDNum2 = 00;             nHNum2 = 12;
 nDNum3 = 1;              nHNum3 = 2;

 |HAZ ElPrincipal;
|FPRC;

|PROCESO dsmod349;
 |HAZ SelModelo;
 |SI swBo = 0; |ACABA; |FINSI;

 aFicher1 = "dsmoc349";
 aFicher2 = "dsmod349";

 aDAlf1 = "  ";           aHAlf1 = "zz";
 nDNum1 = 0000;           nHNum1 = 9999;
 nDNum2 = 00;             nHNum2 = 99;
 aDAlf2 = "            "; aHAlf2 = "zzzzzzzzzzzz";

 |HAZ ElPrincipal;
|FPRC;

|PROCESO dsm35301;
 |HAZ SelModelo;
 |SI swBo = 0; |ACABA; |FINSI;

 aFicher1 = "dsm35302";
 aFicher2 = "dsm35301";

 aDAlf1 = "            "; aHAlf1 = "zzzzzzzzzzzz";
 aDAlf2 = "            "; aHAlf2 = "zzzzzzzzzzzz";

 |HAZ ElPrincipal;
|FPRC;

|PROCESO dsm32201;
 |HAZ SelModelo;
 |SI swBo = 0; |ACABA; |FINSI;

 aFicher1 = "dsmoc322";
 aFicher2 = "dsm32201";

 nDNum1 = 1;  nHNum1 = 9;
 nDNum2 = 00; nHNum2 = 99;

 |HAZ ElPrincipal;
|FPRC;

|PROCESO dsmod13X;
 |HAZ SelModelo;
 |SI swBo = 0; |ACABA; |FINSI;

 |SI nModelo = 130;
     aFicher2 = "dsmod130";
     aFicher1 = "dsmoc130";
 |FINSI;
 |SI nModelo = 131;
     aFicher2 = "dsmod131";
     aFicher1 = "dsmoc131";
 |FINSI;
 |SI nModelo = 340;
     aFicher2 = "";
     aFicher1 = "dsmo3401";
 |FINSI;

 |HAZ ElPrincipal;
|FPRC;

|PROCESO dsmod3XX;
 |HAZ SelModelo;
 |SI swBo = 0; |ACABA; |FINSI;

 aFicher1 = "dsmociva";
 |SI nModelo = 300; aFicher2 = "dsmod300"; |FINSI;
 |SI nModelo = 303; aFicher2 = "dsmod303"; |FINSI;
 |SI nModelo = 310; aFicher2 = "dsmod310"; |FINSI;
 |SI nModelo = 370; aFicher2 = "dsmod370"; |FINSI;
 |SI nModelo = 320; aFicher2 = "dsmod320"; |FINSI;
 |SI nModelo = 330; aFicher2 = "dsmod330"; |FINSI;
 |SI nModelo = 332; aFicher2 = "dsmod332"; |FINSI;
 nDNum1 = 1;  nHNum1 = 9;
 nDNum2 = 00; nHNum2 = 99;

 |HAZ ElPrincipal;
|FPRC;

|PROCESO ProcDsmow109_900;
  nModelo    = #dsmow109#3;
  nEmpresa   = #dsmow109#0;
  nPeriodo   = #dsmow109#2;
  nComplem   = #dsmow109#4;

  |HAZ BajaDsmom004;
|FPRC;

|DEFBUCLE LeeDsmow109_900;
  #dsmow109#2;
  ;
  nEjer, 00001, 900, 00, 00, 00;
  nEjer, 99999, 999, 99, 99, 00;
  ;
  NULL, ProcDsmow109_900;
|FIN;

|PROCESO Lib900;

 swBo = 0;
 |SI #dsmoz076#16 = 0;
     |PARA nPara1 = 18; |SI nPara1 [ 29; |HACIENDO nPara1 = nPara1 + 1;
           |SI #dsmoz076#nPara1# ] 900;
               swBo = 1; |SAL;
           |FINSI;
     |SIGUE;
 |SINO;
     |SI 900 [ #dsmoz076#17;
         swBo = 1;
     |FINSI;
 |FINSI;

 |SI swBo = 1;
     |ABRE #dsmom004;
     |BUCLE LeeDsmow109_900;
     |CIERRA #dsmom004;
 |FINSI;
|FPRC;



|| \\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\
|PROCESO BorrarM003;
  nOk = 0;
  |DDEFECTO #dsmom004;
  #dsmom004#0 = #dsmom003#0;
  #dsmom004#1 = #dsmom003#1;
  #dsmom004#2 = #dsmom003#2;
  |LEE 041#dsmom004.];
  |SI FSalida = 0; |Y #dsmom004#0 = #dsmom003#0;
    |Y #dsmom004#1 = #dsmom003#1; |Y #dsmom004#2 = #dsmom003#2;
      nOk = 1;
  |FINSI;
  |SI nOk = 0;
     |BORRA 020#dsmom003.a;
  |FINSI;
  |LIBERA #dsmom003;
|FPRC;

|DEFBUCLE BorrarM003;
   #dsmom003#2;
   ;
   nDEmpresa, nEjer, nDPer;
   nHEmpresa, nEjer, nHPer;
   be;
   NULL, BorrarM003;
|FIN;

|| \\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\
|PROCESO Borrado;

     nSw = 0;
     |SI #dsmoz076#36 = "S";
         |IMPRE 0;
         |SI FSalida ! 0; |ACABA; |FINSI;

         |INFOR "dsmoz076";
         |SI FSalida ! 0; |FINIMP; |ACABA; |FINSI;
     |FINSI;

     |PARA nEjer = nDEjer; |SI nEjer [ nHEjer; |HACIENDO nEjer = nEjer + 1;
           enSwLinea = 0;
           nModelo = 110;  |HAZ dsmod110;
           nModelo = 115;  |HAZ dsmod115;
           nModelo = 123;  |HAZ dsmod123;
           nModelo = 216;  |HAZ dsmod216;
           nModelo = 349;  |HAZ dsmod349;

           |SI nEjer [ 2008;
               nModelo = 300;  |HAZ dsmod3XX;
               nModelo = 320;  |HAZ dsmod3XX;
               nModelo = 330;  |HAZ dsmod3XX;
               nModelo = 332;  |HAZ dsmod3XX;
               nModelo = 310;  |HAZ dsmod3XX;
               nModelo = 370;  |HAZ dsmod3XX;
           |FINSI;

           |SI nEjer ] 2009;  |Y nEjer [ 2013;
               nModelo = 303;  |HAZ dsmod3XX;
               nModelo = 310;  |HAZ dsmod3XX;
               nModelo = 370;  |HAZ dsmod3XX;
           |FINSI;

           |SI nEjer ] 2014;
               nModelo = 303;  |HAZ dsmXAnual;
           |FINSI;

           nModelo = 322;  |HAZ dsm32201;
           nModelo = 353;  |HAZ dsm35301;

           |SI #dsmoz076#32 = 99;                || Anuales
                nModelo = 180;  |HAZ dsmXAnual;
                nModelo = 184;  |HAZ dsmXAnual;
                nModelo = 187;  |HAZ dsmXAnual;
                nModelo = 190;  |HAZ dsmXAnual;
                nModelo = 193;  |HAZ dsmXAnual;
                nModelo = 296;  |HAZ dsmXAnual;
                nModelo = 347;  |HAZ dsmXAnual;
                nModelo = 390;  |HAZ dsmXAnual;
                |SI nEjer [ 2008;
                    nModelo = 392;  |HAZ dsmXAnual;
                |FINSI;
           |FINSI;

           nModelo = 130;  |HAZ dsmod13X;
           nModelo = 131;  |HAZ dsmod13X;
           nModelo = 340;  |HAZ dsmod13X;

           |HAZ Lib900;

           |SI enSwLinea = 1;
               enSwLinea = 2;
               |SI #dsmoz076#36 = "S";
                   |PRINT; nSw = 1;
               |FINSI;
           |FINSI;

           |ABRE #dsmom004;
           |SI #dsmoz076#0 ! 0;
               nDEmpresa = #dsmoz076#0;
               nHEmpresa = #dsmoz076#1;
               |BUCLE BorrarM003;
           |SINO;
               |PARA nCampo = 2;  |SI nCampo [ 13;  |HACIENDO nCampo = nCampo + 1;
                   |SI #dsmoz076#nCampo# ! 0;
                       nDEmpresa = #dsmoz076#nCampo#;
                       nHEmpresa = #dsmoz076#nCampo#;
                       |BUCLE BorrarM003;
                   |FINSI;
               |SIGUE;
           |FINSI;
           |CIERRA #dsmom004;

     |SIGUE;

     |SI #dsmoz076#36 = "S";
         |SI nSw = 1; |PIEINF; |FINSI;
         |FININF;
         |FINIMP;
     |FINSI;
|FPRC;

|| /////////////////////////////////////////////////////////
|PROCESO dsmom008;
     nActividad = #dsmom008#5;
     |HAZ LeoActividad;
     aCaptura   = #dsmom008#7;
     aDatosMod  = #dsmom008#11;

     |HAZ GrabaW109;
|FPRC;

|DEFBUCLE dsmom008;
     #dsmom008#1;
     ;
     #dsmom004#0, #dsmom004#1, #dsmom004#2, #dsmom004#3, #dsmom004#4, 01;
     #dsmom004#0, #dsmom004#1, #dsmom004#2, #dsmom004#3, #dsmom004#4, 98;
     ;
     NULL, dsmom008;
|FIN;

|PROCESO GrabaTempo;
     enEmpresaPINET = #dsmom004#0;
     |HAZ RestriccionPINET;
     |SI enErrorPINET = 1;  |ACABA;  |FINSI;

     nModelo  = #dsmom004#3;
     |HAZ SelModelo;
     |SI swBo = 0; |ACABA; |FINSI;

     aPeriodicidad = "";
     nEmpresa = #dsmom004#0;
     nEjer    = #dsmom004#1;
     nPeriodo = #dsmom004#2;
     nModelo  = #dsmom004#3;
     nComplem = #dsmom004#4;
     |SI #dsmom004#22 = "M"; aPeriodicidad = "MENSUAL"; |FINSI;
     |SI #dsmom004#22 = "T"; aPeriodicidad = "TRIMESTRAL"; |FINSI;
     |SI #dsmom004#22 = "A"; aPeriodicidad = "ANUAL"; |FINSI;
     aDatosMod  = #dsmom004#17;
     aImpresa   = #dsmom004#9;
     |SI #dsmom004#10 > "01.01.1900";
         aImpresa = aImpresa + " " + #dsmom004#10;
     |FINSI;
     nActividad = 0;
     |HAZ LeoEmpresa;
     |HAZ LeoPeriodo;
     |HAZ LeoActividad;
     nNumero = 0;
     |SI #dsmom004#5 = "ACTIVIDAD ";  |O #dsmom004#5 = "ACT.+ REG.";

         |BUCLE dsmom008;
         |ACABA;
     |FINSI;

     |HAZ GrabaW109;
|FPRC;

|DEFBUCLE dsmom004;
     #dsmom004#1;
     ;
     nDEmpresa, nDEjer, nDPer, nDModelo, 00;
     nHEmpresa, nHEjer, nHPer, nHModelo, 99;
     ;
     NULL, GrabaTempo;
|FIN;

|| \\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\
|PROCESO ITipo3; |TIPO 3;

     aFichero = "2z" + Usuario;
     |NOME_DAT #dsmow109#aFichero#;
     |DELINDEX #dsmow109;

     nDEjer   = #dsmoz076#14;
     nHEjer   = #dsmoz076#15;
     nDPer    = #dsmoz076#30;
     nHPer    = #dsmoz076#32;
     nDModelo = 001;
     nHModelo = 999;
     |SI #dsmoz076#16 ! 0;
         nDModelo = #dsmoz076#16;
         nHModelo = #dsmoz076#17;
     |FINSI;

     nSwHay   = 0;

     |ABRE #agifigen;
     |ABRE #agicen14;
     |ABRE #dsmow109;
     |SI #dsmoz076#0 ! 0;
         nDEmpresa = #dsmoz076#0;
         nHEmpresa = #dsmoz076#1;
         |BUCLE dsmom004;
     |SINO;
         |PARA nCampo = 2;  |SI nCampo [ 13;  |HACIENDO nCampo = nCampo + 1;
               |SI #dsmoz076#nCampo# ! 0;
                   nDEmpresa = #dsmoz076#nCampo#;
                   nHEmpresa = #dsmoz076#nCampo#;
                   |BUCLE dsmom004;
               |FINSI;
         |SIGUE;
     |FINSI;
     |CIERRA #dsmow109;
     |CIERRA #agicen14;
     |CIERRA #agifigen;

     |SI nSwHay = 1;
         |SI #dsmoz076#35 = "S";
             |ABRE #dsmow109;
             |DDEFECTO #dsmow109;
             |CONSULTA_CLAVE #1#dsmow109;
             |CIERRA #dsmow109;
         |FINSI;
         |CONFI "0000NEsta seguro de continuar con el borrado de los Registros";
         |SI FSalida = 0;
             |HAZ Borrado;
         |FINSI;
     |SINO;
         |MENAV "    Seleccion Vacia";
     |FINSI;

     |DELINDEX #dsmow109;
|FPRC;

|PROGRAMA;

     |CUADRO 0501 2380;
     |BLANCO 0602 2279;
     |PINTA 1320, "Introduzca Password : ";
     E_inf = "";
     E_sup = "20";
     aPassword = 1342 ? -1;
     |SI aPassword ! "<MYPASWD>     ";
         |MENAV "     Permiso denegado.";
         |ACABA;
     |FINSI;

     |CLS;
     |CABEZA "Borrar Datos Planing";
     |PINPA #0#dsmoz076;
     |PINDA #0#dsmoz076;

     |ENDATOS #1#dsmoz076;
|FPRO;
