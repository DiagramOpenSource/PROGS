|FICHEROS;
     dsmoz070 #0;
     dsmom004 #4;
     dsmom070 #70;
     dsmom071 #71;
     dsmod309 #309;

     dsmow071 #900;

     :/basica/def/agitab99 #199;

     &clientes@ #715;
     &bancosc@ #720;
|FIN;

|VARIABLES;
     aFichero = "";
     nBanco   = 0;
     nLinea   = 0;
     nChequeo = 0;
     aAlfa   = "";
     &eaCuenta = "";
|FIN;

|INCLUYE i_variar;

|PROCESO PrimeraRemesa;  |TIPO 7;
     |ABRE #70;
     |LEE 040#70u;
     |SI FSalida ! 0;
         #0#0 = 1;
     |SINO;
         #0#0 = #70#0 + 1;
     |FINSI;
     |CIERRA #70;

     |PINTA #0#0;
|FPRC;

|PROCESO MiraModelos;
     nBanco   = 1;
     enCodCli = #4#0;
     |HAZ LeePersona;
     enModelo = #4#3;  enForzar = 1;  |HAZ CogeDatosBancos;

     |SI enBanco     = 0;    nBanco = 0;  |FINSI;
     |SI eaFormaPago ! "2";  nBanco = 0;  |FINSI;

     |SI eaEntidad < #0#5;  |O eaEntidad > #0#6;
         |ACABA;
     |FINSI;

     |SI eaOficina < #0#7;  |O eaOficina > #0#8;
         |ACABA;
     |FINSI;

     |SI #4#15 [ 0;
         nBanco = -1;
         |SI #4#15 < 0; |Y #4#2 = 12;  || Ultimo periodo
             aAlfa = #4#16;
             |QBF aAlfa;
             |SI aAlfa = "DEVOLVER";
                 nBanco = enBanco;
             |FINSI;
         |FINSI;
     |FINSI;

     |SI #0#4 = "N";  |Y nBanco [ 0;
         |ACABA;
     |FINSI;

     |DDEFECTO #900;
     #900#0 = enBanco;
     #900#4 = #715#1;
     #900#5 = eaDC;
     #900#6 = eaCuenta;

     |SI nBanco [ 0;  #900#0 = nBanco;  |FINSI;

     #900#1 = #4#0;
     #900#2 = #4#3;
     #900#3 = #4#4;
     #900#7 = #0#2;
     #900#8 = #0#3;

     |ABRE #199;
     #199#0 = #900#2;
     |LEE 040#199=;
     |SI FSalida ! 0;  |DDEFECTO #199;  |FINSI;
     |CIERRA #199;
     #900#9  = #199#1;
     #900#10 = #4#15;

     |GRABA 020#900n;
     |LIBERA #900;
|FPRC;

|DEFBUCLE dsmom004;
     #4#1;
     9, 11;
     00001, #0#2, #0#3, 000, 00, "X", " ";
     99999, #0#2, #0#3, 999, 99, "X", " ";
     ;
     NULL, MiraModelos;
|FIN;

|PROCESO MiraModelos309;
     nBanco   = 1;
     enCodCli = #309#0;
     |HAZ LeePersona;

     enModelo = 309;  enForzar = 1;  |HAZ CogeDatosBancos;

     |SI enBanco     = 0;    nBanco = 0;  |FINSI;
     |SI eaFormaPago ! "2";  nBanco = 0;  |FINSI;

     |SI eaEntidad < #0#5;  |O eaEntidad > #0#6;
         |ACABA;
     |FINSI;

     |SI eaOficina < #0#7;  |O eaOficina > #0#8;
         |ACABA;
     |FINSI;

     |SI #309#18 [ 0;  nBanco = -1;  |FINSI;

     |SI #0#4 = "N";  |Y nBanco [ 0;
         |ACABA;
     |FINSI;

     |SI nChequeo = 0;
         #900#0 = enBanco;
         #900#1 = #309#0;
         #900#2 = 309;
         #900#3 = #309#4;
         |LEE 101#900=;
         |SI FSalida ! 0;
             |DDEFECTO #900;
             #900#0 = enBanco;
             #900#1 = #309#0;
             #900#2 = 309;
             #900#3 = #309#4;
             |GRABA 020#900b;
         |FINSI;

         #900#4 = #715#1;
         #900#5 = eaDC;
         #900#6 = eaCuenta;

         |SI nBanco [ 0;  #900#0 = nBanco;  |FINSI;

         #900#7 = #0#2;
         #900#8 = #0#3;

         |ABRE #199;
         #199#0 = #900#2;
         |LEE 040#199=;
         |SI FSalida ! 0;  |DDEFECTO #199;  |FINSI;
         |CIERRA #199;
         #900#9  = #199#1;
         #900#10 = #900#10 + #309#18;

         |GRABA 020#900a;
         |LIBERA #900;
     |SINO;
         #309#13  = "X";
         #309#14  = #70#0;
         #309#15  = #70#1;

         |GRABA 020#309a;
     |FINSI;
     |LIBERA #309;
|FPRC;

|DEFBUCLE dsmod309;
     #309#1;
     11, 13;
     00001, 01, #0#2, #0#3, 00, 01, "X", " ";
     99999, 98, #0#2, #0#3, 99, 99, "X", " ";
     be;
     NULL, MiraModelos309;
|FIN;

|PROCESO GrabaRemesa;
     |SI nBanco ! #900#0;
         #0#0   = #0#0 + 1;
         nBanco = #900#0;
         nLinea = 0;
     |FINSI;

     #70#0 = #0#0;
     |LEE 101#70=;
     |SI FSalida ! 0;
         |DDEFECTO #70;
         #70#0 = #0#0;
         #70#1 = #0#1;
         #70#2 = #900#0;

         |SI #900#0 > 0;
             enCodBanco = #900#0;
             |HAZ LeeBancoCliente;

             #70#3  = #720#2;
             #70#4  = #720#3;
             #70#5  = #720#1;
             #70#6  = #720#5;
         |SINO;
             #70#2  = 0;
             #70#5  = "HACIENDA ";
         |FINSI;

         |GRABA 020#70b;
     |FINSI;

     nLinea = nLinea + 1;
     #71#0  = #70#0;
     #71#1  = nLinea;
     #71#2  = #900#1;
     #71#3  = #900#4;
     #71#4  = #900#5;
     #71#5  = #900#6;
     #71#6  = #900#2;
     #71#7  = #900#7;
     #71#8  = #900#8;
     #71#9  = #900#9;
     #71#10 = #900#10;
     #71#11 = #900#3;
     |GRABA 020#71n;
     |LIBERA #71;

     #70#7  = #70#7 + #71#10;
     |GRABA 020#70a;
     |LIBERA #70;

     |SI #71#6 ! 309;
         #4#0  = #71#2;
         #4#1  = #71#7;
         #4#2  = #71#8;
         #4#3  = #71#6;
         #4#4  = #71#11;
         |LEE 101#4=;
         |SI FSalida ! 0;  |ACABA;  |FINSI;

         #4#11  = "X";
         #4#12 = #70#1;
         #4#18 = #70#0;

         |GRABA 020#4a;
         |LIBERA #4;
    |SINO;
         nChequeo = 1;  |BUCLE dsmod309;
    |FINSI;
|FPRC;

|DEFBUCLE dsmow071;
     #900#1;
     ;
      -1, 00001, "   ", 00;
     999, 99999, "zzz", 99;
     ;
     NULL, GrabaRemesa;
|FIN;

|PROCESO Tipo3;  |TIPO 3;
     aFichero = ("71" + Usuario + "zzzzzzzz") % 108;
     |NOME_DAT #900 aFichero;
     |ABRE #900;  |CIERRA #900;  |DELINDEX #900;

     |ABRE #900;
                    |BUCLE dsmom004;
     nChequeo = 0;  |BUCLE dsmod309;
     |CIERRA #900;

     nBanco = -2;
     #0#0   = #0#0 - 1;

     |ABRE #4;
     |ABRE #70;
     |ABRE #71;
     |BUCLE dsmow071;
     |CIERRA #4;
     |CIERRA #70;
     |CIERRA #71;

     |ABRE #900;  |CIERRA #900;  |DELINDEX #900;
|FPRC;
