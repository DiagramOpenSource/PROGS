|FICHEROS;
     dsmox005;
     dsmox006;
     dsmox007;
|FIN;

|VARIABLES;
     nRango              = 0;
     nGrid               = 0;
     nBtnHis             = 0;
     nBtnEr1             = 0;
     nBtnEr2             = 0;
     nBtnEr3             = 0;
     nHand1              = 0;
     nPausa              = 0;

     aAlfa               = "";
     aFic                = "";
     aRetu               = "";
     aEsc1               = "";
     aEsc2               = "";
     aTecla              = "";
     aAbre               = "";
     aEjec               = "";
     aPathPdf            = "";
     aPathErr            = "";
     aPathExe            = "";

     &eaUnidad           = "";
     &eaUnidadBasico     = "";
|FIN;

|PROCESO Baj5;
     #dsmox005#0 = #dsmox007#0;
     #dsmox005#1 = 0;
|FPRC;

|PROCESO Alt5;
     #dsmox005#0 = #dsmox007#0;
     #dsmox005#1 = 999;
|FPRC;

|PROCESO Historico;
     |CONSULTA_F_CLAVE #1#dsmox005, Baj5, Alt5;
|FPRC;

|PROCESO Baj6;
     #dsmox006#0 = #dsmox007#0;
     #dsmox006#1 = 0;
|FPRC;

|PROCESO Alt6;
     #dsmox006#0 = #dsmox007#0;
     #dsmox006#1 = 99999;
|FPRC;

|PROCESO ErrorTXT;
     aFic  = (("00000" + #dsmox007#0) % -105) + ".txt";
     aAbre = aPathErr + aFic;
     |XABRE "EC", aAbre, 0;
     |SI FSalida < 0;
         |ACABA;
     |FINSI;

     aAbre = aPathErr + "ejecuta.bat";
     |XABRE "WBC", aAbre, nHand1;

     aAlfa = aPathExe + "dsabreextension ";
     aAlfa = aAlfa + aPathErr + aFic;
     aAlfa = aAlfa + " " + aPathErr + "findsabre.txt ";
     |XGRABA nHand1, aAlfa;

     |XCIERRA nHand1;

     aEjec = aPathExe + "dsmini " + aAbre;
     |RSYSTEM aEjec;

     aAbre = aPathErr + "findsabre.txt";
     |PARA;  |SI;  |HACIENDO;
             |PARA nPausa = 1; |SI nPausa [ 10000; |HACIENDO nPausa = nPausa + 1;
             |SIGUE;

             |XABRE "EC", aAbre, 0;
             |SI FSalida ] 0;
                 |SAL;
             |FINSI;

             |SLEEP 1;
             |PULSATECLA;
     |SIGUE;

     aAlfa = aPathErr + "ejecuta.bat";       |RFBORRA aAlfa;
     aAlfa = aPathErr + "findsabre.txt";     |RFBORRA aAlfa;

     |FOCOTECLADO nGrid;
|FPRC;

|PROCESO GRIDReg;
     |CONSULTA_F_CLAVE #1#dsmox006, Baj6, Alt6;
|FPRC;

|PROCESO GRIDTodo;
     |CONSULTA_CLAVE #1#dsmox006;
|FPRC;

|PROCESO Evt7;
     |SI FSalida = 1;  |O FSalida = 2;
         |LEE 000#dsmox007.=;

         |ESTADO_CONTROL nBtnEr1, "DISABLE";
         |ESTADO_CONTROL nBtnEr2, "DISABLE";

         #dsmox006#0 = #dsmox007#0;
         #dsmox006#1 = 0;
         |LEE 000#dsmox006.];
         |SI FSalida = 0;  |Y  #dsmox006#0 = #dsmox007#0;
             |ESTADO_CONTROL nBtnEr1, "ENABLE";
             |ESTADO_CONTROL nBtnEr2, "ENABLE";
         |FINSI;
     |FINSI;
|FPRC;

|PROGRAMA;
     |LEE 000#dsmox007.p;
     |SI FSalida ! 0;
         |MENAV "0000Selecci¢n vac¡a";
         |ACABA;
     |FINSI;

     |CONTROL_SIMPLE 0, "LABEL,{{15}}Incidencias de la emisi¢n ", 0502, 0570, NULL;

     aAlfa = "LABEL,{{16}}Los pdf generados estan en: " + aPathPdf;
     |CONTROL_SIMPLE 0, aAlfa, 0602, 0690, NULL;

     |CONTROL_SIMPLE 0, "BOTON,{{197}}Historial env¡o", 3302, 3318, Historico;
     nBtnHis = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,{{197}}Ver errores TXT", 3320, 3336, ErrorTXT;
     nBtnEr1 = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,{{197}}Errores GRID", 3338, 3350, GRIDReg;
     nBtnEr2 = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,{{197}}Todos los errores", 3352, 3370, GRIDTodo;
     nBtnEr3 = FSalida;

     |ESTADO_CONTROL nBtnEr1, "DISABLE";
     |ESTADO_CONTROL nBtnEr2, "DISABLE";

     |LEE 000#dsmox006.p;
     |SI FSalida ! 0;
         |ESTADO_CONTROL nBtnEr3, "DISABLE";
     |FINSI;

     nRango = 4 + 8 + 16 + 32;
     |LINEAL_SIMPLE #1#dsmox007, nRango, 0702, 3298, NULL, NULL, Evt7;
     nGrid = FSalida;

     aRetu  = &255 + "802";
     aEsc1  = &255 + "806";
     aEsc2  = &255 + "807";
     |PARA; |SI;  |HACIENDO;
          FSalida = "::{{001}}Salir," + nGrid;
          |LEETECLA aTecla;

          |SI aTecla = aRetu; |SAL;  |FINSI;
          |SI aTecla = aEsc1; |SAL;  |FINSI;
          |SI aTecla = aEsc2; |SAL;  |FINSI;
     |SIGUE;
|FPRO;

|PROCESO T80;  |TIPO 80;
     aFic = "dsmox005" + Usuario;  |NOME_DAT #dsmox005#aFic#;
     aFic = "dsmox006" + Usuario;  |NOME_DAT #dsmox006#aFic#;
     aFic = "dsmox007" + Usuario;  |NOME_DAT #dsmox007#aFic#;

     |ABRET #dsmox005;
     |ABRET #dsmox006;
     |ABRET #dsmox007;

     |LEE 000#dsmox007.p;
     |SI FSalida ! 0;
         |ACABA;
     |FINSI;

     |HAZ LeeUnidad;
     aPathPdf = eaUnidad + "\AEAT\PDF";
     aPathPdf = aPathPdf + "\" + #dsmox007#3;
     aPathPdf = aPathPdf + "\" + #dsmox007#1;

     |SI #dsmox007#2 ! 99;
         aPathPdf = aPathPdf + "\" + (("00" + #dsmox007#2) % -102);
     |FINSI;
     aPathPdf = aPathPdf + "\";

     aPathErr = eaUnidad + "\AEAT\ERRORES";
     aPathErr = aPathErr + "\" + #dsmox007#3;
     aPathErr = aPathErr + "\" + #dsmox007#1;

     |SI #dsmox007#2 ! 99;
         aPathErr = aPathErr + "\" + (("00" + #dsmox007#2) % -102);
     |FINSI;
     aPathErr = aPathErr + "\";

     |HAZ LeeUnidadBasico;
     aPathExe = eaUnidadBasico + "\MODELOS";  |RMKDIR aPathExe;
     aPathExe = aPathExe + "\DSMAC";          |RMKDIR aPathExe;
     aPathExe = aPathExe + "\";

     FSalida = 3599;
|FPRC;

|PROCESO T90;  |TIPO 90;
     |CIERRA #dsmox005;  |DELINDEX #dsmox005;
     |CIERRA #dsmox006;  |DELINDEX #dsmox006;
     |CIERRA #dsmox007;  |DELINDEX #dsmox007;

     aAlfa = aPathErr + "*.txt";
     |R_PDIR aAlfa;
     |PARA;  |SI FSalida = 0;  |HACIENDO;
          |RFBORRA aAlfa;

          |R_SDIR aAlfa;
     |SIGUE;
|FPRC;
