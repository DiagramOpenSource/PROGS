|FICHEROS;
     dsmoz099 #99;

     :/basica/def/agifigen #1000;
     :/basica/def/agicen14 #114;

     dsmom002 #2;
     dsmom004 #4;
|FIN;

|VARIABLES;
     nCampo    = 0;
     aParam    = "";
     aBase     = "";
     aMas      = "";
     nDEmpresa = 0;
     nHEmpresa = 0;

     &enElEjer   = 0;
     &enAlta     = 0;
     &enDEmpresa = 0;
     &enHEmpresa = 0;
     aAlfa1      = "";
     nModelo     = 0;
     fFecIni     = @;
     fFecFin     = @;
     nSwConPro   = 0;
     nNoModi     = 0;
     nEmpresa    = 0;

     &enSwApli     = 0;
     &enModeloAp    = 0;
     &enEmpresaAp   = 0;
     &eaNombreAp    = "";
     &enEjerAp      = 0;
     &enImporte     = 0;
|FIN;

|INCLUYE i_variar;

|PROCESO NoModificar;
  |SI #4#9  = "X"; nNoModi = 1; |FINSI;
  |SI #4#11 = "X"; nNoModi = 1; |FINSI;
  |SI #4#13 = "X"; nNoModi = 1; |FINSI;
|FPRC;

|DEFBUCLE NoModificar;
   #4#1;
   ;
   nEmpresa, enEjer, 01, nModelo, 00;
   nEmpresa, enEjer, 99, nModelo, 99;
   ;
   NULL, NoModificar;
|FIN;

|| .... Mirar si hace el modelo de IRPF
|PROCESO MiraSiD;
 |SI #2#8 > "01.01.0000";
     |SI #2#8 > fFecFin;  |ACABA;  |FINSI;
 |FINSI;
 |SI #2#9 > "01.01.0000";
     |SI #2#9 < fFecIni;  |ACABA;  |FINSI;
 |FINSI;
 nSwConPro = 0;
 |ERROR10;
|FPRC;

|DEFBUCLE MiraSiModelo;
 #2#1;
 ;
 nEmpresa, 01, nModelo;
 nEmpresa, 99, nModelo;
 ;
 NULL, MiraSiD;
|FIN;

|PROCESO LaSeleccion;
    nSwConPro = -1;
    |BUCLE MiraSiModelo;
    |SI nSwConPro = -1;
        aAlfa1 = "    Empresa sin Modelo " + #1000#0 + " en el Planing";
        |MENAV aAlfa1;
        |ACABA;
    |FINSI;

    |ABRE #114;
    aCoBien = "N";
    #114#0 = nEmpresa;
    #114#1 = 00;
    |LEE 041#114];
    |SI FSalida = 0; |Y #114#0 = nEmpresa;
        aCoBien = #114#13;  ||Comunidad de Bienes
    |FINSI;
    |SI aCoBien = "S";
        aAlfa1 = "    Empresa COMUNIDAD DE BIENES       ";
        |MENAV aAlfa1;
        |CIERRA #114;
        |ACABA;
    |FINSI;
    |CIERRA #114;

     nNoModi = 0;
     |BUCLE NoModificar;
     |SI nNoModi = 1;
         |MENAV "    Empresa con Modelos Remesados o Archivados, no puede modificarlos";
    |FINSI;
|FPRC;

|CALCULO Recojo;
     |SI FSalida = 15;
         enSwApli = 6;
         |HAZ eGrabadsmom093;
         #99#3 = enImporte;
         |PINTA #99#3;
         |ERROR;
     |FINSI;
|FCAL;

|PROGRAMA;


      |DDEFECTO #99;
      #99#0 = enModelo;
      #99#1 = enEjer;
      #99#2 = enEmpresa;

      |ABRE #1000;
      |DDEFECTO #1000;
      #1000#0 = enEmpresa;
      |LEE 041#1000=;
      eaNombreEmp = #1000#1;
      |CIERRA #1000;

      nEmpresa = #99#2;
      nModelo  = #99#0;
      aAlfa1 = #99#1;
      aAlfa1 = "01.01." + aAlfa1; fFecIni = aAlfa1;
      aAlfa1 = #99#1;
      aAlfa1 = "31.12." + aAlfa1; fFecFin = aAlfa1;
      |HAZ LaSeleccion;
      |SI nSwConPro ! -1;
          enEmpresaAp   = enEmpresa;
          enEjerAp      = enEjer;
          enModeloAp    = nModelo;
          eaNombreAp    = eaNombreEmp;
          enSwApli      = 2;
          |HAZ eGrabadsmom093;

          #99#3 = enImporte;
          |PINPA #0#99;
          |PINDA #0#99;

          |SI nNoModi = 1;
              |PAUSA;
          |SINO;
              |ENDATOS #1#99;
              |SI FSalida = 0;
                  enSwApli  = 5;
                  enImporte = #99#3;
                  |HAZ eGrabadsmom093;

                  enSwApli   = 4;
                  enElEjer   = #99#1;
                  enDEmpresa = #99#2;
                  enHEmpresa = #99#2;
                  aAlfa1     = #99#0;
                  aAlfa1     = "dsmoz095;" + aAlfa1;
                  |SUB_EJECUTA_NP aAlfa1;

                  |PULSATECLA;
              |FINSI;
          |FINSI;
     |FINSI;
|FPRO;
