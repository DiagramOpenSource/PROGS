|FICHEROS;
   dsmoy038 #0;

   dsmom002 #2;
   dsmom005 #5;

   dsmom030 #30;

   dsmod309 #309;
   dsmow069 #999;

   :/basica/def/agicen14 #114;
   :/basica/def/agitab10 #110;
   :/basica/def/agifigen #118;
   :/basica/def/agitab99 #99;

   dsmow030 #900;
   :/contagen/def/canempre #101;
   :/contagen/def/camaniva #102;
   :/contagen/def/caivalin #103;
   :/contagen/def/caivaasi #104;
   referen@   #800;
|FIN;

|VARIABLES;
   nInkey      = "";
   informe     = "";
   nCampo      = 0;
   nDEmpresa   = 0;
   nHEmpresa   = 0;
   fFecha      = @;
   nAnyo       = 0;
   nMes        = 0;

   aAlfa  = "";
   aAlfa1 = "";
   aAlfa2 = "";
   aAlfa3 = "";
   nPara1 = 0;
   nPara2 = 0;
   nNume1 = 0;
   nNume2 = 0;
   nNume3 = 0;
   nNume4 = 0;

   nSPin1      = 0;
   nSwHay      = 0;
   nPrint      = 0;
   aPath       = "";
   aPathModelo = "";
   nSwPrint    = 0;
   aDef        = "";
   aFichero    = "";

   nEmpresa   = 0;
   nEjercicio = 0;
   nActividad = 0;
   aActividad = "";
   nSwEstaen2   = 0;
   nSwEstaen309 = 0;
   nSwEsta      = 0;
   nSwPasa      = 0;
   nDepar       = 0;
   nSwOp        = 0;
   nSwOp309     = 0;
   nDPeriodo    = 0;
   nHPeriodo    = 0;
   nCompensado  = 0;

     nCampo1           = 0;
     nCampo2           = 0;
     nCampo3           = 0;
     nCampo11          = 0;
     nCampo22          = 0;
     nCampo33          = 0;
     nPorce            = 0;
     nBaseImpo         = 0;
     nCuota            = 0;
     nSwConta          = 0;
     &nSwLinea         = 0;
   &enDesde   = 0;
   &enHasta   = 0;
|FIN;

|INCLUYE i_variar;
|INCLUYE i_varcon;
|INCLUYE i_varlib;

|| //////////////////// Calculos de Pantalla \\\\\\\\\\\\\\\\\\\\\\\\\\
|CALCULO Eano; |TIPO 7;
 fFecha = ~;
 aAlfa  = fFecha % 402;   nMes  = aAlfa;
 aAlfa  = fFecha % -104;  nAnyo = aAlfa;

     |SI nMes [ 3;
         #0#57 = 12;
         #0#1 = nAnyo - 1;
         |HAZ Periodo;
         |PINTA #0#1;
         |PINTA #0#57;
         |ACABA;
     |FINSI;

     |SI nMes [ 6;
         #0#57 = 3;
         #0#1 = nAnyo;
         |HAZ Periodo;
         |PINTA #0#1;
         |PINTA #0#57;
         |ACABA;
     |FINSI;

     |SI nMes [ 9;
         #0#57 = 6;
         #0#1 = nAnyo;
         |HAZ Periodo;
         |PINTA #0#1;
         |PINTA #0#57;
         |ACABA;
     |FINSI;

     |SI nMes [ 12;
         #0#57 = 9;
         #0#1 = nAnyo;
         |HAZ Periodo;
         |PINTA #0#1;
         |PINTA #0#57;
         |ACABA;
    |FINSI;
|FCAL;

|PROCESO Periodo;
     #0#58 = "          ";
     |SI #0#57 = 1;   #0#58 = "ENERO     ";  |FINSI;
     |SI #0#57 = 2;   #0#58 = "FEBRERO   ";  |FINSI;
     |SI #0#57 = 3;   #0#58 = "MARZO     ";  |FINSI;
     |SI #0#57 = 4;   #0#58 = "ABRIL     ";  |FINSI;
     |SI #0#57 = 5;   #0#58 = "MAYO      ";  |FINSI;
     |SI #0#57 = 6;   #0#58 = "JUNIO     ";  |FINSI;
     |SI #0#57 = 7;   #0#58 = "JULIO     ";  |FINSI;
     |SI #0#57 = 8;   #0#58 = "AGOSTO    ";  |FINSI;
     |SI #0#57 = 9;   #0#58 = "SEPTIEMBRE";  |FINSI;
     |SI #0#57 = 10;  #0#58 = "OCTUBRE   ";  |FINSI;
     |SI #0#57 = 11;  #0#58 = "NOVIEMBRE ";  |FINSI;
     |SI #0#57 = 12;  #0#58 = "DICIEMBRE ";  |FINSI;
     |SI #0#57 = 99;  #0#58 = "ANUAL     ";  |FINSI;

     |SI #0#58 = "          ";
         |MENAV "     Periodo Incorrecto.";
         |ERROR;
     |FINSI;

     |PINTA #0#58;
|FPRC;

|CALCULO Tipo7;  |TIPO 7;
     |SI enEmpresa ! 0;
         #0#2  = enEmpresa;  |PINTA #0#2;
         #0#3  = enEmpresa;  |PINTA #0#3;

         |C_M #0#2, 1, "N";
         |C_M #0#3, 1, "N";
     |FINSI;
|FCAL;

|CALCULO Parrilla; |TIPO 0;
     |SI #0#2 = 0;
         #0#2 = 0;  |PINTA #0#2;
         #0#3 = 0;  |PINTA #0#3;  |C_M #0#3, 1, "N";
         |PARA nCampo = 4;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |SINO;
         |SI enEmpresa = 0;
             |C_M #0#2, 1, "S";
             |C_M #0#3, 1, "S";
         |FINSI;

         |PARA nCampo = 4;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "N";
               #0nCampo = 0;  |PINTA #0nCampo;
         |SIGUE;
     |FINSI;
|FCAL;

|CALCULO NoMasEmpresas; |TIPO 0;
     |SI #0Campo = 0;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
               #0nCampo = 0;  |C_M #0nCampo, 1, "N";
         |SIGUE;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |FINSI;
|FCAL;

|CALCULO YaSeCual; |TIPO 7;
 |SI enEmpresa ! 0;
     #0#41 = "S"; |PINTA #0#41;
     #0#24 = "S"; |PINTA #0#24;
     #0#25 = 00;  |PINTA #0#25;
     #0#26 = 99;  |PINTA #0#26;
     |C_M #0#24, 1, "N";
     |C_M #0#25, 1, "N";
     |C_M #0#26, 1, "N";
     |C_M #0#41, 1, "N";
 |FINSI;
|FCAL;

|CALCULO SelTipo0; |TIPO 0;
 |SI #0#41 = "N";
     #0#25 = 0;  |PINTA #0#25;  |C_M #0#25, 1, "N";
     #0#26 = 99; |PINTA #0#26;  |C_M #0#26, 1, "N";
     |PARA nCampo = 27;  |SI nCampo [ 36;  |HACIENDO nCampo = nCampo + 1;
           |C_M #0nCampo, 1, "S";
     |SIGUE;
 |SINO;
     |SI enEmpresa = 0;
         |C_M #0#25, 1, "S";
         |C_M #0#26, 1, "S";
     |FINSI;
     |PARA nCampo = 27;  |SI nCampo [ 36;  |HACIENDO nCampo = nCampo + 1;
           |C_M #0nCampo, 1, "N";
           #0nCampo = 0;  |PINTA #0nCampo;
     |SIGUE;
 |FINSI;
|FCAL;

|CALCULO SelTipo1;
 |SI #0Campo = 0;
     |PARA nCampo = Campo + 1;  |SI nCampo [ 36;  |HACIENDO nCampo = nCampo + 1;
           #0nCampo = 0;  |C_M #0nCampo, 1, "N";
     |SIGUE;
 |SINO;
     |PARA nCampo = Campo + 1;  |SI nCampo [ 36;  |HACIENDO nCampo = nCampo + 1;
           |C_M #0nCampo, 1, "S";
     |SIGUE;
 |FINSI;
|FCAL;


|PROCESO ClaveAlta;
 #110#0 = 01;
|FPRC;

|PROCESO ClaveBaja;
 #110#0 = 99;
|FPRC;

|CALCULO MiraTipoIva;
     nInkey = FSalida;

     |C_M #0Campo, 0, aAlfa1;
     |SI aAlfa1 = "N"; |ACABA; |FINSI;

     |SI nInkey = 10;
         |ABRE #110;
         #110#0 = #0Campo;
         |CONSULTA_F_CLAVE #1#110, ClaveAlta, ClaveBaja;
         #0Campo = #110#0;
         |CIERRA #110;
     |FINSI;

     |SI Campo = 42;
         |SI #0Campo = 0;
             |C_M #0#43, 1, "N"; #0#43 = 0; |PINTA #0#43;
             |C_M #0#44, 1, "S";
             |C_M #0#45, 1, "S";
             |C_M #0#46, 1, "S";
             |C_M #0#47, 1, "S";
             |C_M #0#48, 1, "S";
         |SINO;
             |C_M #0#43, 1, "S";
             |C_M #0#44, 1, "N"; #0#44 = 0; |PINTA #0#44;
             |C_M #0#45, 1, "N"; #0#45 = 0; |PINTA #0#45;
             |C_M #0#46, 1, "N"; #0#46 = 0; |PINTA #0#46;
             |C_M #0#47, 1, "N"; #0#47 = 0; |PINTA #0#47;
             |C_M #0#48, 1, "N"; #0#48 = 0; |PINTA #0#48;
         |FINSI;
     |SINO;
         |SI Campo = 43;
             |SI #0#42 > #0#43;
                 |MENAV "    Error al introducir hasta Tipo IVA";
                 |ERROR;
                 |ACABA;
              |FINSI;
         |SINO;
              |SI #0Campo = 0;
                  |PARA nCampo = Campo + 1;  |SI nCampo [ 48;  |HACIENDO nCampo = nCampo + 1;
                        #0nCampo = 0;  |C_M #0nCampo, 1, "N"; |PINTA #0nCampo;
                  |SIGUE;
              |SINO;
                  |PARA nCampo = Campo + 1;  |SI nCampo [ 48;  |HACIENDO nCampo = nCampo + 1;
                  |C_M #0nCampo, 1, "S";
                  |SIGUE;
            |FINSI;
          |FINSI;
     |FINSI;

     |SI #0Campo ! 0; |Y Campo > 43;
         |ABRE #110;
         #110#0 = #0Campo;
         |LEE 001#110=;
         |SI FSalida ! 0;
             |MENAV "    No existe Tipo IVA";
             |ERROR;
         |FINSI;
         |CIERRA #110;
     |FINSI;
|FCAL;

|| *************************************************************************
|| *************************************************************************

|PROCESO PreparaConta;
   nSwConta  = 0; ||compruebo si tiene contabilidad

   enSwOpPar  = 0;
   |HAZ SituaEmpresa;
   |SI enSwPartido = 0;
       |ABRE #101;
       |SELECT #4#101;
       |DDEFECTO #101;
       #101#2  = enEmpresa;
       #101#26 = nEjercicio;
       |LEE 001#canempre.=;
       |SI FSalida = 0;
           enPerConta = #101#3;   || Periodo Contable
           aAlfa1 = enEmpresa;  aAlfa1 = ("00000" + aAlfa1) % -105;
           aAlfa2 = enPerConta; aAlfa2 = ("0" + aAlfa2) % -101;
           aPathConta  = aPath + "contagen/fich/" + aAlfa1 + aAlfa2 + "/";
           nSwConta = 1;
           eaLaMonEmp = #101#28;
       |FINSI;
       |SELECT #1#101;
       |CIERRA #101;
   |SINO;
        enPerConta = enPeriodoPar;
        eaLaMonEmp = "E";
        nSwConta   = 1;
   |FINSI;
   |SI nSwConta = 0; |ACABA; |FINSI;    || no tiene contabilidad

   |HAZ EnConversor;
   |PATH_DAT #102 aPathConta;
   |PATH_DAT #103 aPathConta;
   |PATH_DAT #104 aPathConta;
   |ABRE #caivaasi;
   |DDEFECTO #caivaasi;
   |LEE 000#caivaasi.p;
   |CIERRA #caivaasi;
   aMulDep = #caivaasi#132;
|FPRC;

|PROCESO CierraConta;
     |SI enSwPartido = 1;
         enSwOpPar   = 2;
         |HAZ FinalPartido;
     |FINSI;
|FPRC;

|| *************************************************************************
|PROCESO PonRecargo;
     nSwEsta = 0;
     |PARA nCampo2 = 58;  |SI nCampo2 [ 67;  |HACIENDO nCampo2 = nCampo2 + 3;
           nCampo1 = nCampo2 - 1;
           nCampo3 = nCampo2 + 1;
           |SI #309nCampo2 = nPorce;
               #309nCampo1 = #309nCampo1 + nBaseImpo;
               #309nCampo2 = nPorce;
               #309nCampo3 = #309nCampo3 + nCuota;
               nSwEsta = 1;
                |SAL;
           |FINSI;
     |SIGUE;

     |SI nSwEsta = 0;
         |PARA nCampo2 = 58;  |SI nCampo2 [ 67;  |HACIENDO nCampo2 = nCampo2 + 3;
               nCampo1 = nCampo2 - 1;
               nCampo3 = nCampo2 + 1;
               |SI #309nCampo2 = 0; |Y #309nCampo1 = 0;  |Y #309nCampo3 = 0;
                   #309nCampo1 = #309nCampo1 + nBaseImpo;
                   #309nCampo2 = nPorce;
                   #309nCampo3 = #309nCampo3 + nCuota;
                   nSwEsta = 1;
                   |SAL;
               |FINSI;
         |SIGUE;
     |FINSI;

     |SI nSwEsta = 0;
         #309#66 = #309#66 + nBaseImpo;
         #309#67 = 0;
         #309#68 = #309#68 + nCuota;
     |FINSI;
|FPRC;

|PROCESO PonIVA;
     nSwEsta = 0;
     |PARA nCampo2 = 49;  |SI nCampo2 [ 55;  |HACIENDO nCampo2 = nCampo2 + 3;
           nCampo1 = nCampo2 - 1;
           nCampo3 = nCampo2 + 1;
           |SI #309nCampo2 = nPorce;
               #309nCampo1 = #309nCampo1 + nBaseImpo;
               #309nCampo2 = nPorce;
               #309nCampo3 = #309nCampo3 + nCuota;
               nSwEsta = 1;
                |SAL;
           |FINSI;
     |SIGUE;

     |SI nSwEsta = 0;
         |PARA nCampo2 = 49;  |SI nCampo2 [ 55;  |HACIENDO nCampo2 = nCampo2 + 3;
               nCampo1 = nCampo2 - 1;
               nCampo3 = nCampo2 + 1;
               |SI #309nCampo2 = 0; |Y #309nCampo1 = 0;  |Y #309nCampo3 = 0;
                   #309nCampo1 = #309nCampo1 + nBaseImpo;
                   #309nCampo2 = nPorce;
                   #309nCampo3 = #309nCampo3 + nCuota;
                   nSwEsta = 1;
                   |SAL;
               |FINSI;
         |SIGUE;
     |FINSI;

     |SI nSwEsta = 0;
         #309#54 = #309#54 + nBaseImpo;
         #309#55 = 0;
         #309#56 = #309#56 + nCuota;
     |FINSI;

     |SI #103#10 ! 0;
         nBaseImpo = (#103#6 / enConversor) ! EURODBMOD;
         nPorce    = #103#9;
         nCuota    = (#103#10 / enConversor) ! EURODBMOD;
         |HAZ PonRecargo;
     |FINSI;
|FPRC;

|PROCESO LeeLinea309;
  ||    |SI #103#5 = "N"; |ACABA;  |FINSI; Quito tique 1243_1310

     aAlfa1 = #103#30; |QBF aAlfa1; |SI aAlfa1 = ""; #103#30 = #102#10; |FINSI;
     |SI aMulDep = "S";
         nDepar = #103#30;
         |SI nDepar = 0;  nDepar = 1;   |FINSI;

         |SI #309#1 ! nDepar;  |ACABA;  |FINSI;
     |FINSI;

     #30#0 = #103#3;
     |LEE 040#30=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |SI #30#4 = "N";  |ACABA;  |FINSI;

     |SI #30#0 ! #0#49;  |Y #30#0 ! #0#50;  |Y #30#0 ! #0#51;  |Y #30#0 ! #0#52;
      |Y #30#0 ! #0#53;  |Y #30#0 ! #0#54;  |Y #30#0 ! #0#55;  |Y #30#0 ! #0#56;
         |ACABA;
     |FINSI;

     |SI #30#0 = #0#49;  |O #30#0 = #0#50;  |O #30#0 = #0#51;  |O #30#0 = #0#52;
         #309#28 = "X";
     |FINSI;

     |SI #30#0 = #0#53;  |O #30#0 = #0#54;  |O #30#0 = #0#55;  |O #30#0 = #0#56;
         #309#29 = "X";
     |FINSI;

     |SI #30#0 = 611;  |O #30#0 = 612;
         nBaseImpo = (#103#6 / enConversor) ! EURODBMOD;
         nPorce    = #103#9;
         |SI nPorce = 0; nPorce = 4; |FINSI;
         nCuota    = ((#103#6 % nPorce) / enConversor) ! EURODBMOD;
         |HAZ PonRecargo;

         #103#10 = 0;
         nBaseImpo = (#103#6 / enConversor) ! EURODBMOD;
         nPorce    = #103#7;
         |SI nPorce = 0;
             nPorce = 16; |SI #102#4 ] "01.07.2010"; nPorce = 18; |FINSI;
                          |SI #102#4 ] "01.09.2012"; nPorce = 21; |FINSI;
         |FINSI;
         nCuota    = ((#103#6 % nPorce) / enConversor) ! EURODBMOD;
         nSwPasa   = 1;
         |HAZ PonIVA;
     |FINSI;

     |SI #30#0 = 615;  |O #30#0 = 616;  |O #30#0 = 620;  |O #30#0 = 623;
         nBaseImpo = (#103#6 / enConversor) ! EURODBMOD;
         nPorce    = #103#7;
         |SI nPorce = 0;
             nPorce = 16; |SI #102#4 ] "01.07.2010"; nPorce = 18; |FINSI;
                          |SI #102#4 ] "01.09.2012"; nPorce = 21; |FINSI;
         |FINSI;
         nCuota    = ((#103#6 % nPorce) / enConversor) ! EURODBMOD;
         nSwPasa   = 1;
         |HAZ PonIVA;
     |FINSI;

     |SI #30#0 = 619;  |O #30#0 = 622;
         nBaseImpo = (#103#6 / enConversor) ! EURODBMOD;
         nPorce    = #103#7;
         |SI nPorce = 0;
             nPorce = 7; |SI #102#4 ] "01.07.2010"; nPorce = 8; |FINSI;
                         |SI #102#4 ] "01.09.2012"; nPorce = 10; |FINSI;
         |FINSI;
         nCuota    = ((#103#6 % nPorce) / enConversor) ! EURODBMOD;
         nSwPasa   = 1;
         |HAZ PonIVA;
     |FINSI;
|FPRC;

|DEFBUCLE caivalin309;
     #103#1;
     ;
     #102#0, #102#1, 01;
     #102#0, #102#1, 99;
     ;
     NULL, LeeLinea309;
|FIN;

|PROCESO LeeCabecera309;
     |SI aMulDep ! "S";
         nDepar = #102#10;
         |SI nDepar = 0;  nDepar = 1;   |FINSI;

         |SI #309#1 ! nDepar;  |ACABA;  |FINSI;
     |FINSI;

     |BUCLE caivalin309;
|FPRC;

|DEFBUCLE camaniva309;
     #102#1;
     5;
     00, 0000000000, nDPeriodo;
     99, 9999999999, nHPeriodo;
     e;
     NULL, LeeCabecera309;
|FIN;
|| *************************************************************************
|PROCESO dsmod309_P;
   #0#61 = "SI";
   nSwEstaen309 = 1;
   |ERROR10;
|FPRC;

|DEFBUCLE dsmod309;
     #309#1;
     ;
     enEmpresa, nActividad, enEjer, nDPeriodo, 00, 01;
     enEmpresa, nActividad, enEjer, nHPeriodo, 99, 99;
     ;
     NULL, dsmod309_P;
|FIN;

|PROCESO dsmod800_P;
|| ......... Para Compensar
   |SI #800#3 > #309#3;  |ACABA;  |FINSI;

   |SI #309#3 = #800#3;  |Y  #309#4 = #800#4;  |Y #309#5 = #800#5;
        |ACABA;
   |FINSI;

   |PARA nCampo2 = 49;  |SI nCampo2 [ 55;  |HACIENDO nCampo2 = nCampo2 + 3;
         nCampo1     = nCampo2 - 1;
         nCampo3     = nCampo2 + 1;
         nCompensado = 0;
         |PARA nCampo22 = 49;  |SI nCampo22 [ 55;  |HACIENDO nCampo22 = nCampo22 + 3;
               nCampo11 = nCampo22 - 1;
               nCampo33 = nCampo22 + 1;
               |SI #309nCampo22 = #800nCampo2;
                   #309nCampo11 = #309nCampo11 - #800nCampo1;
                   #309nCampo33 = #309nCampo33 - #800nCampo3;
                   nCompensado = 1;
                   |SAL;
               |FINSI;
         |SIGUE;

         |SI nCompensado = 0;
               |SI #309#48 = 0;  |Y #309#49 = 0;  |Y #309#50 = 0;
                   #309#48 = #309#48 - #800nCampo1;
                   #309#49 = #800nCampo2;
                   #309#50 = #309#50 - #800nCampo3;
                   nCompensado = 1;
               |FINSI;
         |FINSI;

         |SI nCompensado = 0;
               |SI #309#51 = 0;  |Y #309#52 = 0;  |Y #309#53 = 0;
                   #309#51 = #309#51 - #800nCampo1;
                   #309#52 = #800nCampo2;
                   #309#53 = #309#53 - #800nCampo3;
                   nCompensado = 1;
               |FINSI;
         |FINSI;

         |SI nCompensado = 0;
               |SI #309#54 = 0;  |Y #309#55 = 0;  |Y #309#56 = 0;
                   #309#54 = #309#54 - #800nCampo1;
                   #309#55 = #800nCampo2;
                   #309#56 = #309#56 - #800nCampo3;
                   nCompensado = 1;
               |FINSI;
         |FINSI;
     |SIGUE;

     |PARA nCampo2 = 58;  |SI nCampo2 [ 67;  |HACIENDO nCampo2 = nCampo2 + 3;
           nCampo1     = nCampo2 - 1;
           nCampo3     = nCampo2 + 1;
           nCompensado = 0;
           |PARA nCampo22 = 58;  |SI nCampo22 [ 67;  |HACIENDO nCampo22 = nCampo22 + 3;
                 nCampo11 = nCampo22 - 1;
                 nCampo33 = nCampo22 + 1;
                 |SI #309nCampo22 = #800nCampo2;
                     #309nCampo11 = #309nCampo11 - #800nCampo1;
                     #309nCampo33 = #309nCampo33 - #800nCampo3;
                     nCompensado = 1;
                     |SAL;
                 |FINSI;
           |SIGUE;

           |SI nCompensado = 0;
               |SI #309#57 = 0;  |Y #309#58 = 0;  |Y #309#59 = 0;
                   #309#57 = #309#57 - #800nCampo1;
                   #309#58 = #800nCampo2;
                   #309#59 = #309#59 - #800nCampo3;
                   nCompensado = 1;
               |FINSI;
           |FINSI;

           |SI nCompensado = 0;
               |SI #309#60 = 0;  |Y #309#61 = 0;  |Y #309#62 = 0;
                   #309#60 = #309#60 - #800nCampo1;
                   #309#61 = #800nCampo2;
                   #309#62 = #309#62 - #800nCampo3;
                   nCompensado = 1;
               |FINSI;
           |FINSI;

           |SI nCompensado = 0;
               |SI #309#63 = 0;  |Y #309#64 = 0;  |Y #309#65 = 0;
                   #309#63 = #309#63 - #800nCampo1;
                   #309#64 = #800nCampo2;
                   #309#65 = #309#65 - #800nCampo3;
                   nCompensado = 1;
               |FINSI;
           |FINSI;

           |SI nCompensado = 0;
               |SI #309#66 = 0;  |Y #309#67 = 0;  |Y #309#68 = 0;
                   #309#66 = #309#66 - #800nCampo1;
                   #309#67 = #800nCampo2;
                   #309#68 = #309#68 - #800nCampo3;
                   nCompensado = 1;
               |FINSI;
           |FINSI;
     |SIGUE;
|FPRC;

|DEFBUCLE dsmod800;
     #800#1006;
     ;
     enEmpresa, nActividad, enEjer, nDPeriodo, 00, 01;
     enEmpresa, nActividad, enEjer, nHPeriodo, 99, 99;
     ;
     NULL, dsmod800_P;
|FIN;

|PROCESO PorActividad;

 |SI #114#5 > "01.01.0000";
     |SI #114#5 > efHFecha;  |ACABA;  |FINSI;
 |FINSI;
 |SI #114#6 > "01.01.0000";
     |SI #114#6 < efDFecha;  |ACABA; |FINSI; || Baja
 |FINSI;

  nActividad = #114#1;
  aActividad = nActividad;
  aActividad = ("00" + aActividad) % -102;

  nSwPrint = 0;
  |SI #0#42 ! 0;
      |SI #114#7 ] #0#42; |Y #114#7 [ #0#43; nSwPrint = 1; |FINSI;
  |SINO;
      |SI #0#44 ! 0; |Y #114#7 = #0#44; nSwPrint = 1; |FINSI;
      |SI #0#45 ! 0; |Y #114#7 = #0#45; nSwPrint = 1; |FINSI;
      |SI #0#46 ! 0; |Y #114#7 = #0#46; nSwPrint = 1; |FINSI;
      |SI #0#47 ! 0; |Y #114#7 = #0#47; nSwPrint = 1; |FINSI;
      |SI #0#48 ! 0; |Y #114#7 = #0#48; nSwPrint = 1; |FINSI;
  |FINSI;

  |SI nSwPrint = 0; |ACABA; |FINSI;  ||Esta actividad no esta en la seleccion

  nSwHay = 1;
  |SI nSwOp = 0; |ACABA; |FINSI;

  |DDEFECTO #999;
  #999#0 = enEmpresa;
  #999#1 = nActividad;
  #999#2 = enEjer;
  #999#3 = #agifigen#1;
  #999#4 = #114#4;
  #999#5 = "T";

  nSwEstaen2 = 1;
  |ABRE #2;
  #2#0 = enEmpresa;
  #2#1 = nActividad;
  #2#2 = 309;
  |LEE 001#2=;
  |SI FSalida = 0;
      aAlfa = #2#8 % -104;
      |SI aAlfa ! "0000"; |Y aAlfa ! "1900";
          nAnyo = aAlfa;
          |SI nAnyo > #0#1;  nSwEstaen2 = 0;  |FINSI;
      |FINSI;

      aAlfa = #2#9 % -104;
      |SI aAlfa ! "0000"; |Y aAlfa1 ! "1900";
          nAnyo = aAlfa;
          |SI nAnyo < #0#1;  nSwEstaen2 = 0;  |FINSI;
      |FINSI;
   |SINO;
      nSwEstaen2 = 0;
   |FINSI;

   #0#60 = "NO";
   |SI nSwEstaen2 = 1;
      #0#60 = "SI";
      |SI #2#7 = "TRIMESTRAL";  #999#5 = "T";  |FINSI;
      |SI #2#7 = "ANUAL     ";  #999#5 = "A";  |FINSI;
   |FINSI;
   |CIERRA #2;

   #0#61 = "NO";
   nDPeriodo    = 1;
   nHPeriodo    = enPeriodo - 1;
   nSwEstaen309 = 0;
   |SI enPeriodo > 1; |BUCLE dsmod309; |FINSI;

   |DDEFECTO #309;
   enModelo = #0#59;
   #309#0 = enEmpresa;
   #309#1 = nActividad;
   #309#2 = enEjer;
   #309#3 = enPeriodo;
   #309#4 = 0;
   #309#5 = 01;
   #309#7 = #999#3;
   #309#8 = #999#4;

   |SI #999#5 = "T";
       |SI enEjer [ 2009;
           |SI #309#3 = 1;   nDPeriodo = 1;  nHPeriodo = 1; |FINSI;
           |SI #309#3 = 2;   nDPeriodo = 1;  nHPeriodo = 1; |FINSI;
           |SI #309#3 = 3;   nDPeriodo = 1;  nHPeriodo = 1; |FINSI;
           |SI #309#3 = 4;   nDPeriodo = 1;  nHPeriodo = 2; |FINSI;
           |SI #309#3 = 5;   nDPeriodo = 1;  nHPeriodo = 2; |FINSI;
           |SI #309#3 = 6;   nDPeriodo = 1;  nHPeriodo = 2; |FINSI;
           |SI #309#3 = 7;   nDPeriodo = 1;  nHPeriodo = 3; |FINSI;
           |SI #309#3 = 8;   nDPeriodo = 1;  nHPeriodo = 3; |FINSI;
           |SI #309#3 = 9;   nDPeriodo = 1;  nHPeriodo = 3; |FINSI;
           |SI #309#3 = 10;  nDPeriodo = 1;  nHPeriodo = 4; |FINSI;
           |SI #309#3 = 11;  nDPeriodo = 1;  nHPeriodo = 4; |FINSI;
           |SI #309#3 = 12;  nDPeriodo = 1;  nHPeriodo = 4; |FINSI;
       |SINO;
           |SI #309#3 = 1;   nDPeriodo = 1;  nHPeriodo = 3; |FINSI;
           |SI #309#3 = 2;   nDPeriodo = 1;  nHPeriodo = 3; |FINSI;
           |SI #309#3 = 3;   nDPeriodo = 1;  nHPeriodo = 3; |FINSI;
           |SI #309#3 = 4;   nDPeriodo = 1;  nHPeriodo = 6; |FINSI;
           |SI #309#3 = 5;   nDPeriodo = 1;  nHPeriodo = 6; |FINSI;
           |SI #309#3 = 6;   nDPeriodo = 1;  nHPeriodo = 6; |FINSI;
           |SI #309#3 = 7;   nDPeriodo = 1;  nHPeriodo = 9; |FINSI;
           |SI #309#3 = 8;   nDPeriodo = 1;  nHPeriodo = 9; |FINSI;
           |SI #309#3 = 9;   nDPeriodo = 1;  nHPeriodo = 9; |FINSI;
           |SI #309#3 = 10;  nDPeriodo = 1;  nHPeriodo = 12;|FINSI;
           |SI #309#3 = 11;  nDPeriodo = 1;  nHPeriodo = 12;|FINSI;
           |SI #309#3 = 12;  nDPeriodo = 1;  nHPeriodo = 12;|FINSI;
       |FINSI;
   |SINO;
       |SI #309#3 = 1;   nDPeriodo = 1;  nHPeriodo = 1;   |FINSI;
       |SI #309#3 = 2;   nDPeriodo = 1;  nHPeriodo = 2;   |FINSI;
       |SI #309#3 = 3;   nDPeriodo = 1;  nHPeriodo = 3;   |FINSI;
       |SI #309#3 = 4;   nDPeriodo = 1;  nHPeriodo = 4;   |FINSI;
       |SI #309#3 = 5;   nDPeriodo = 1;  nHPeriodo = 5;   |FINSI;
       |SI #309#3 = 6;   nDPeriodo = 1;  nHPeriodo = 6;   |FINSI;
       |SI #309#3 = 7;   nDPeriodo = 1;  nHPeriodo = 7;   |FINSI;
       |SI #309#3 = 8;   nDPeriodo = 1;  nHPeriodo = 8;   |FINSI;
       |SI #309#3 = 9;   nDPeriodo = 1;  nHPeriodo = 9;   |FINSI;
       |SI #309#3 = 10;  nDPeriodo = 1;  nHPeriodo = 10;  |FINSI;
       |SI #309#3 = 11;  nDPeriodo = 1;  nHPeriodo = 11;  |FINSI;
       |SI #309#3 ] 12;  nDPeriodo = 1;  nHPeriodo = 12;  |FINSI;
   |FINSI;

   |ABRE #30;
   |BUCLE camaniva309;
   |CIERRA #30;

   |SI nSwEstaen309 = 1;
       |DBASE aPathModelo;  |QBT aPathModelo;
       aDef = aPathModelo + "def/dsmod309.mas";
       |CARGA_DEF aDef, referen@;
       |SI FSalida = 0;
           nDPeriodo = 1;
           nHPeriodo = enPeriodo - 1;
           |SI enPeriodo > 1; |BUCLE dsmod800; |FINSI;

           |DESCARGA_DEF #referen@;
       |FINSI;
   |FINSI;

   #309#69 = #309#50 + #309#53 + #309#56 + #309#59 + #309#62 + #309#65 + #309#68;
   #309#71 = #309#69 - #309#70;

   |SI #114#7 = 9;  #309#22 = "X";  |FINSI;
   |SI #114#7 = 3;  #309#23 = "X";  |FINSI;
   |SI #114#7 = 1;  #309#24 = "X";  |FINSI;

   |SI #309#69 ! 0;
       |PRINT; nPrint = 1;
       nSwHay = 2;
   |FINSI;
|FPRC;

|DEFBUCLE PorActividad;
  #114#1;
  ;
  enEmpresa, #0#37;
  enEmpresa, #0#38;
  e;
  NULL, PorActividad;
|FIN;

|PROCESO LEmpresa;

 enEmpresa  = #agifigen#0;
 enPeriodo  = #0#57;

 |DDEFECTO #900;
 #900#0 = enEmpresa;
 #900#2 = #agifigen#1;
 #900#11 = "ACTIVA";
 |SI #agifigen#94 > "01.01.0000";
     #900#11 = "INACTIVA";
     |SI #0#24 = "N"; |ACABA; |FINSI;
 |FINSI;

|| ... Seleccion de Tipo de Empresa
  nSPin1 = #agifigen#87;
  enSwSelEmp = 1;
  |SI #0#41 = "S";
      |SI nSPin1 ] #0#25; |Y nSPin1 [ #0#26;
          enSwSelEmp = 0;
      |FINSI;
  |SINO;
      |SI nSPin1 = #0#27;
          enSwSelEmp = 0;
      |SINO;
          |PARA nCampo = 28;  |SI nCampo [ 36;  |HACIENDO nCampo = nCampo + 1;
                |SI nSPin1 = #0nCampo; |Y nSPin1 ! 0;
                    enSwSelEmp = 0;
                    |SAL;
                |FINSI;
          |SIGUE;
      |FINSI;
  |FINSI;
  |SI enSwSelEmp = 1; |ACABA; |FINSI;
|| ------------------------------------------------------------------------

   || ... Primero comprueba que tiene Contabilidad
   |HAZ PreparaConta;
   |SI nSwConta = 0; |ACABA; |FINSI;
|| ------------------------------------------------------------------------

   nSwHay   = 0;
   nSwOp    = 0;
   |BUCLE PorActividad;
   |SI nSwHay = 1;
       nSwHay   = 0;
       nSwOp    = 1;
       nSwLinea = 0;
       |BUCLE PorActividad;
       || .. |SI nSwHay = 2; ; |FINSI;
   |FINSI;
   |HAZ CierraConta;
|FPRC;

|DEFBUCLE agifigen;
  #agifigen#1;
  ;
  nDEmpresa;
  nHEmpresa;
  e;
  NULL, LEmpresa;
|FIN;


|| \\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\
|CALCULO Tipo3;  |TIPO 3;

    eaInac = #0#24;
    |DBASS aPath; |QBT aPath;

    enEjer = #0#1;
    aAlfa2 = enEjer;
    aAlfa1 = aAlfa2 % -102; nEjercicio = aAlfa1;
    aAlfa1 = "01.01." + aAlfa2; efDFecha = aAlfa1;
    |SI #0#57 =  3; aAlfa1 = "31.03." + aAlfa2; efHFecha = aAlfa1; |FINSI;
    |SI #0#57 =  6; aAlfa1 = "30.06." + aAlfa2; efHFecha = aAlfa1; |FINSI;
    |SI #0#57 =  9; aAlfa1 = "30.09." + aAlfa2; efHFecha = aAlfa1; |FINSI;
    |SI #0#57 ] 12; aAlfa1 = "31.12." + aAlfa2; efHFecha = aAlfa1; |FINSI;
    eaTituloL = #0#40; ||Titulo del Libro

    nPrint = 0;

    |IMPRE 0;
    |SI FSalida ! 0; |ACABA; |FINSI;

    informe = "dsmoy038";

    |INFOR informe;
    |SI FSalida ! 0; |ACABA; |FINSI;

    |SI #0#2 ! 0;
        nDEmpresa = #0#2;
        nHEmpresa = #0#3;
        |BUCLE agifigen;
    |SINO;
        |PARA nPara1 = 4;  |SI nPara1 [ 23;  |HACIENDO nPara1 = nPara1 + 1;
              |SI #0nPara1 ! 0;
                  nDEmpresa = #0nPara1;
                  nHEmpresa = #0nPara1;
                  |BUCLE agifigen;
              |FINSI;
        |SIGUE;
    |FINSI;

    |SI nPrint = 0;
        |MENAV "     Seleccion Vacia.";
        |ACABA;
    |SINO;
         |PIEINF;
    |FINSI;
    |FININF;
    |FINIMP;
|FCAL;


|PROGRAMA;

     |ABRE #99;
     #99#0 = 309;
     |LEE 040#99=;
     |SI FSalida ! 0;  |DDEFECTO #99;  |FINSI;
     |CIERRA #99;

     |SI #99#4 ! "S";  |O #99#5 ! "C";  |O #99#6 ! "F";
         enDesde = 309;
         enHasta = 309;
         eaExtension = "bas";
         eaPrograma  = "agitab99";
         |SUB_EJECUTA_NP ":basica/dspatron";
         |SUB_EJECUTA_NP "dsmom005";
     |FINSI;

     |CLS;
     |CABEZA "Inf.Chequeo Modelo 309";

     |ABRE #0;
     |LEE 001#0p;
     |SI FSalida ! 0;
         |DDEFECTO #0;
         fFecha = ~;
         aAlfa1 = fFecha; aAlfa1 = aAlfa1 % -104;
         #0#1 = aAlfa1;

         |GRABA 020#0c;
     |FINSI;
     |PINPA #0#0;
     |PINDA #0#0;

     |ENDATOS #1#0;

     |GRABA 020#0a;
     |CIERRA #0;
|FPRO;
