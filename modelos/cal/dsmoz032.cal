|FICHEROS;
  dsmoz032 #0;

  :/modelos/def/dsmom030 #30;
  :/basica/def/agitab10  #98;
  :/modelos/def/dsmow095 #995,MANTE;
|FIN;

|VARIABLES;
  aFichero   = "";
  nInkey     = 0;
  aAlfa      = "";
  nEstado    = 0;
  nModificar = 0;
  nPara1     = 0;
  nRegimen   = 0;
  aRegimen   = "";
  nNume1     = 0;
  nNume2     = 0;
  nSi        = 0;
  nModo      = 0;
|FIN;

|INCLUYE i_variar;
|INCLUYE i_varcon;

|| ***************************************************************
|PROCESO LeoRegimen;
 |ABRE #98;
 |DDEFECTO #98;
 #98#0 = nRegimen;
 |LEE 000#98=;
 aRegimen = #98#5;
 |CIERRA #98;
|FPRC;

|PROCESO LeeLosConceptos;
 |DDEFECTO #0;
 #0#0 = #30#0;
 #0#1 = #30#1;
 #0#2 = #30#2;
 #0#3 = #30#3;
 #0#4 = #30#24;
 #0#5 = #30#25;
 #0#6 = #30#72;
 #0#7 = #30#73;
 #0#8 = #30#74;

 |PARA nPara1 = 78; |SI nPara1 [ 101; |HACIENDO nPara1 = nPara1 + 1;
       |SI nPara1 [ 89;
           nNume1 = nPara1 - 69;
       |SINO;
           |SI nPara1 [ 95;
               nNume1 = nPara1 - 73;
           |SINO;
               nNume1 = nPara1 - 74;
           |FINSI;
       |FINSI;
       #0nNume1 = #30nPara1;
       |SI #30nPara1 ! #30#24; |Y #30nPara1 ! 00;
           #995#0 = #30#0;
           #995#1 = #30nPara1;
           |LEE 040#995=;
           |SI FSalida ! 0;
               #995#0   = #30#0;
               #995#1   = #30nPara1;
               nRegimen = #995#1;
               |HAZ LeoRegimen;
               #995#2   = aRegimen;
               |GRABA 020#995n;
           |FINSI;
       |FINSI;
       |SI nPara1 = 89; nPara1 = 93; |FINSI;
       |SI nPara1 = 95; nPara1 = 99; |FINSI;
  |SIGUE;

  #0#23 = #30#96;
  #0#24 = #30#97;
  #0#25 = #30#98;

  |GRABA 040#0n;
|FPRC;

|DEFBUCLE LeeLosConceptos;
 #30#1;
 4;
 001, "S";
 999, "S";
 e;
 NULL, LeeLosConceptos;
|FIN;

|| *******************************************************************
|PROCESO CuidoTabla;
 nSi = 0;
 |ABRE #995;
 #995#0 = #0#0;
 #995#1 = #0#4;
 |LEE 001#995=;
 |SI FSalida = 0;
     |BORRA 020#995a;
     nSi = 1;
 |FINSI;
 |CIERRA #995;
 |SI nSi = 1; |HAZ Tipo7z32;  |FINSI;
|FPRC;

|PROCESO DespuesPror; |TIPO 0;
  |SI #0#0 ] 800; |Y #0#0 [ 899;
      |SI #0#7 = "U";
          |C_M #0#8,1,"N";
           #0#8 = "N"; |PINTA #0#8;
      |SINO;
          |C_M #0#8,1,"S";
      |FINSI;
  |SINO;
      |SI #0#7 = "U";
          |MENAV "    Opcion No Valida. No es un concepto de Subvencion (800 a 899)";
          |ERROR;
      |FINSI;
      |C_M #0#8,1,"N";
      #0#8 = "N"; |PINTA #0#8;
  |FINSI;
|FPRC;

|PROCESO DespuesC097; |TIPO 0;
     aAlfa = "S";
     |SI #0#24 ! "S"; aAlfa = "N"; |FINSI;
     |C_M #0#25, 1, aAlfa;
     |SI aAlfa = "N";
         #0#25 = 000; |PINTA #0#25;
     |FINSI;
|FPRC;

|PROCESO ConcIRPF; |TIPO 0;
     |C_M #0#25,0,aAlfa;
     |SI aAlfa = "N"; |ACABA; |FINSI;

     |SI #0#25 ! 0;
         |ABRE #30;
         #30#0 = #0#25;
         |LEE 040#30=;
         |SI FSalida ! 0;
             |MENAV "0000Error.! no existe Concepto";
             |ERROR;
         |SINO;
             |SI #30#5 ! "S";
                 |MENAV "0000Error.! este concepto no es de IRPF";
                 |ERROR;
             |FINSI;
         |FINSI;
         |CIERRA #30;
     |FINSI;
|FPRC;

|| *******************************************************************
|RUTINA ClaveBaja995;
 #995#0 = #0#0;
 #995#1 = 01;
|FRUT;

|RUTINA ClaveAlta995;
 #995#0 = #0#0;
 #995#1 = 17;
|FRUT;

|PROCESO Tipo12z32;  |TIPO 12;
 ||
|FPRC;

|PROCESO Tipo7z32;  |TIPO 7;
   enModo = 7;
   |ENTLINEAL #1#995, -2, 7, 21, 0, ClaveBaja995, ClaveAlta995;
|FPRC;

|PROCESO Tipo11z32;  |TIPO 11;
   nInkey = FSalida;
   |SI nInkey =  10;
       |PTEC 802;
       || |HAZ Tipo0z32;
   |FINSI;
|FPRC;

|PROCESO Tipo0z32;
    |LEE 001#0=;
    |ENDATOS #2#0;
    |SI FSalida = 0;
        |GRABA 020#0a;

        |ENTLINEAL #1#995, -2, 2, 21, 0, ClaveBaja995, ClaveAlta995;
        nModificar = 1;
    |FINSI;
|FPRC;

|| ****************************************************************
|PROCESO MiroCampo24; |TIPO 0;
 nModo = (FEntrada / 100) ! 0;
 |SI #995#1 = #0#4; |Y nModo = 1;
     |MENAV "    Regimen de IVA Ya Informado ";
     |ERROR;
 |FINSI;
|FPRC;

|| ****************************************************************
|PROCESO RecojoT;
  |SI #995#1 ! #30#24;
      nPara1 = nPara1 + 1; |SI nPara1 = 90; nPara1 = 94;  |FINSI;
                           |SI nPara1 = 96; nPara1 = 100; |FINSI;
      #30nPara1 = #995#1;
  |FINSI;
|FPRC;

|DEFBUCLE RecojoT;
 #995#1;
 ;
 #0#0, 01;
 #0#0, 99;
 ;
 NULL, RecojoT;
|FIN;

|PROCESO LoPasoTodo;

   #30#0 = #0#0;
   |LEE 101#30=;
   |SI FSalida = 0;
       #30#96 = #0#23;
       #30#97 = #0#24;
       #30#98 = #0#25;

       #30#24 = #0#4;
       #30#25 = #0#5;
       #30#72 = #0#6;
       #30#73 = #0#7;
       #30#74 = #0#8;
       |PARA nPara1 = 78; |SI nPara1 [ 89; |HACIENDO nPara1 = nPara1 + 1;
             #30nPara1 = 0;
       |SIGUE;
       #30#94  = 0;
       #30#95  = 0;
       #30#100 = 0;
       #30#101 = 0;
       nPara1 = 77;
       |BUCLE RecojoT;
       |GRABA 020#30a;
   |FINSI;
   |LIBERA #30;
|FPRC;

|DEFBUCLE LoPasoTodo;
 #0#1;
 ;
 001;
 999;
 ;
 NULL, LoPasoTodo;
|FIN;

|PROCESO Cambiar;
 |ABRE #30;
 |BUCLE LoPasoTodo;
 |CIERRA #30;
|FPRC;

|| ****************************************************************
|RUTINA ClaveBaja0;
 #0#0 = 0;
|FRUT;

|RUTINA ClaveAlta0;
 #0#0 = 999;
|FRUT;

|PROGRAMA;

  aFichero = ("32z" + Usuario) % 108;
  |NOME_DAT #0 aFichero;
  aFichero = ("95w" + Usuario) % 108;
  |NOME_DAT #995 aFichero;

  |ABRE #0;   |CIERRA #0;   |DELINDEX #0;
  |ABRE #995; |CIERRA #995; |DELINDEX #995;

  |C_P #995#1, 1, 1413; |C_P #995#2, 1, 1416;

  |ABRE #0;
  |ABRE #995;
  |BUCLE LeeLosConceptos;
  |CIERRA #995;
  |CIERRA #0;

  nModificar = 0;
  |CLS;
  |PINPA #0#0;
  |ENTLINEAL #1#0, -1, 4, 22, 0, ClaveBaja0, ClaveAlta0;
  |SI nModificar = 1;
      |CONFI "    SConforme con los cambios realizados S/N";
      |SI FSalida = 0;
          |HAZ Cambiar;
      |FINSI;
  |FINSI;
  |DELINDEX #0;
  |DELINDEX #995;
|FPRO;
|| ****************************************************************
