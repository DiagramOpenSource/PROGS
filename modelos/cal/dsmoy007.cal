|FICHEROS;
     dsmoy007 #0;

     dsmom023 #23;  ||Tabla cuentas
     dsmom024 #24;  ||Mant. Valores por Empresas
     dsmom028 #28;

     :/basica/def/agitab99 #99;
     :/basica/def/agicen14 #114;
     :/basica/def/agifigen #118;
     :/basica/def/agim0019 #119;

     dsmow029 #999;
     program@ #777;

     dsm30301;
     dsm32201;
     dsmom004;
|FIN;

|VARIABLES;
     nSwDevuelto = 0;
     nPendiente = 0;
     nPerP     = 0;
     nAnyP     = 0;
     inform1   = "dsmoyn07";
     inform2   = "dsmoy007";
     informe   = "";
     nEstado   = 0;
     nCampo    = 0;
     nDEmpresa = 0;
     nHEmpresa = 0;
     nPeriodo  = 0;
     nEjer     = 0;
     aAlfa     = "";
     aAlfa1    = "";
     aAlfa2    = "";
     aAlfa3    = "";
     aPath     = "";
     nNume1    = 0;
     nNume2    = 0;
     nNume3    = 0;
     nPin1       = 0;
     aPin1       = "";
     efPn10      = @;
     efPn11      = @;
     swExiste    = 0;
     &enSwLinea  = 0;
     nLaEmpresa  = 0;
     nElModelo   = 0;
     nMod2009    = 0;
     nElAny      = 0;
     swPrEmpresa = 0;
     swPrModelo  = 0;
     swPrAny     = 0;
     swAlgun     = 0;
     swBo        = 0;
     &eaLit1     = "";
     &eaLit2     = "";
     nReserva    = 0;
     nACompensar = 0;
     nCompensar  = 0;
     aPathModelo = "";
     aDef        = "";
     aFichero    = "";
     fLaFecha    = @;
     &eaRaya     = "";
     nSwEste     = 0;
     nDMod       = 0;
     nHMod       = 0;
     aParametro  = "";
     nParam      = 0;
     nResta      = 0;
     aResta      = "";
     nEstePer    = 0;
     nImpReal    = 0;
     nCas21      = 0;
     nCas27      = 0;
     nCas31      = 0;
     nCas34      = 0;
     nIngresar   = 0;
     nDevolver   = 0;
     aTPer       = "";
     nMes        = 0;
     nAny        = 0;

     &eaTex      = "";
     &enImpAcum  = 0;
     &enImporte  = 0;
     &enImport1  = 0;
     &enTrim     = 0;
     &enAny      = 0;
|FIN;

|INCLUYE i_variar;

|| /////////////////////// PROCESOS ENTRADA POR PANTALLA \\\\\\\\\\\\\\\\\\\\
|CALCULO Eano; |TIPO 7;
    fLaFecha = ~;
    aAlfa1 = fLaFecha; aAlfa1 = aAlfa1 % -104;
    #0#15 = aAlfa1;
    |PINTA #0#15;
|FCAL;

|CALCULO Periodo; |TIPO 0;
     #0#29 = "          ";
     |SI #0#28 = 1;   #0#29 = "ENERO     ";  |FINSI;
     |SI #0#28 = 2;   #0#29 = "FEBRERO   ";  |FINSI;
     |SI #0#28 = 3;   #0#29 = "MARZO     ";  |FINSI;
     |SI #0#28 = 4;   #0#29 = "ABRIL     ";  |FINSI;
     |SI #0#28 = 5;   #0#29 = "MAYO      ";  |FINSI;
     |SI #0#28 = 6;   #0#29 = "JUNIO     ";  |FINSI;
     |SI #0#28 = 7;   #0#29 = "JULIO     ";  |FINSI;
     |SI #0#28 = 8;   #0#29 = "AGOSTO    ";  |FINSI;
     |SI #0#28 = 9;   #0#29 = "SEPTIEMBRE";  |FINSI;
     |SI #0#28 = 10;  #0#29 = "OCTUBRE   ";  |FINSI;
     |SI #0#28 = 11;  #0#29 = "NOVIEMBRE ";  |FINSI;
     |SI #0#28 = 12;  #0#29 = "DICIEMBRE ";  |FINSI;
     |PINTA #0#29;
|FCAL;

|PROCESO Tipo7;  |TIPO 7;
     |SI enEmpresa ! 0;
         #0#0 = enEmpresa;  |PINTA #0#0;
         #0#1 = enEmpresa;  |PINTA #0#1;

         |C_M #0#0, 1, "N";
         |C_M #0#1, 1, "N";
     |FINSI;
|FPRC;

|PROCESO Tipo7_16;  |TIPO 7;
    |SI enModelo ! 0;
       ||  #0#16 = enModelo;  |PINTA #0#16;
       ||  #0#17 = enModelo;  |PINTA #0#17;

       ||  |C_M #0#16, 1, "N";
       ||  |C_M #0#17, 1, "N";
    |FINSI;
|FPRC;

|PROCESO Parrilla;
     |SI #0#0 = 0;
         #0#0 = 0;  |PINTA #0#0;
         #0#1 = 0;  |PINTA #0#1;  |C_M #0#1, 1, "N";
         |PARA nCampo = 2;  |SI nCampo [ 13;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |SINO;
         |SI enEmpresa = 0;
             |C_M #0#0, 1, "S";
             |C_M #0#1, 1, "S";
         |FINSI;

         |PARA nCampo = 2;  |SI nCampo [ 13;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "N";
               #0nCampo = 0;  |PINTA #0nCampo;
         |SIGUE;
     |FINSI;
|FPRC;

|PROCESO NoMasEmpresas;
     |SI #0Campo = 0;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 13;  |HACIENDO nCampo = nCampo + 1;
               #0nCampo = 0;  |C_M #0nCampo, 1, "N";
         |SIGUE;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 13;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |FINSI;
|FPRC;

|| /////////////////////// //////////////////////////////////////////////////
|| /////////////////////// PROCESOS DE AUXILIAR \\\\\\\ \\\\\\\\\\\\\\\\\\\\\
|| .....................................................................
|| .....................................................................
|PROCESO ElRepaso;

 |SI #777#14 ! 0; |Y #777#15 ! 0;
      |LIBERA #777;
      |ACABA;                         || Ya esta prescrito
 |FINSI;
 |SI #777#9 = 0;
      |LIBERA #777;
      |ACABA;                         || Ya esta compensado
 |FINSI;

 |SI #777#11 < #0#15;
     #777#15 = #777#11; || .. Ejercicio prescrito
     #777#14 = #777#10; || .. Periodo
     #777#16 = #777#9;  || .. Importe prescrito
     #777#9  = 0;
 |SINO;
     |SI #777#11 = #0#15;
         |SI #0#28 [ 12; nNume1 = 4; |FINSI;
         |SI #0#28 [  9; nNume1 = 3; |FINSI;
         |SI #0#28 [  6; nNume1 = 2; |FINSI;
         |SI #0#28 [  3; nNume1 = 1; |FINSI;
         |SI #777#10 < nNume1;
             #777#15 = #777#11; || .. Ejercicio prescrito
             #777#14 = #777#10; || .. Periodo
             #777#16 = #777#9;  || .. Importe prescrito
             #777#9  = 0;
         |FINSI;
     |FINSI;
 |FINSI;
 |GRABA 020#777a;
 |LIBERA #777;
|FPRC;

|DEFBUCLE ElRepaso;
 #777#1004;
 11;
 00000,000,0000,00, 0000;
 99999,999,9999,99, #0#15;
 e;
 NULL, ElRepaso;
|FIN;

|| .....................................................................
|| .....................................................................
|| *********************************************************************
|| ***** Proceso anterior
|PROCESO Auxiliar0;

 |SI #777#2 = #999#2; |Y #777#3 ] #999#3;
     |LIBERA #777;
     |ERROR10;
     |ACABA;
 |FINSI;

 |SI #777#14 ! 0; |Y #777#15 ! 0;
      |LIBERA #777;
      |ACABA;                         || Ya esta prescrito
 |FINSI;
 |SI #777#9 = 0;
      |LIBERA #777;
      |ACABA;                         || Ya esta compensado
 |FINSI;

 |SI #777#11 < #999#2;
     #777#15 = #777#11; || .. Ejercicio prescrito
     #777#14 = #777#10; || .. Periodo
     #777#16 = #777#9;  || .. Importe prescrito
     #777#9  = 0;
 |SINO;
     |SI #777#11 = #999#2;
         |SI #999#3 [ 12; nNume1 = 4; |FINSI;
         |SI #999#3 [  9; nNume1 = 3; |FINSI;
         |SI #999#3 [  6; nNume1 = 2; |FINSI;
         |SI #999#3 [  3; nNume1 = 1; |FINSI;
         |SI #777#10 [ nNume1;
             #777#15 = #777#11; || .. Ejercicio prescrito
             #777#14 = #777#10; || .. Periodo
             #777#16 = #777#9;  || .. Importe prescrito
             #777#9  = 0;
         |FINSI;
     |FINSI;
 |FINSI;

 |SI #777#9 ! 0;
      nNume1 = #777#9 - nACompensar;
      |SI nNume1 [ 0;
          nACompensar = nACompensar - #777#9;  || Pendiente Compensar
          #777#8 = #777#8 + #777#9;            || Compensado
          #777#9 = 0;

          |SI #999#3 [ 12; #777#12 = 4; |FINSI;
          |SI #999#3 [  9; #777#12 = 3; |FINSI;
          |SI #999#3 [  6; #777#12 = 2; |FINSI;
          |SI #999#3 [  3; #777#12 = 1; |FINSI;
          #777#13 = #999#2;
      |SINO;
          #777#8 = #777#8 + nACompensar;     || Compensado
          |SI #999#3 [ 12; #777#12 = 4; |FINSI;
          |SI #999#3 [  9; #777#12 = 3; |FINSI;
          |SI #999#3 [  6; #777#12 = 2; |FINSI;
          |SI #999#3 [  3; #777#12 = 1; |FINSI;
          #777#13 = #999#2;

          #777#9 = #777#9 - nACompensar;   || Pendiente Compensar
          nACompensar = 0;
      |FINSI;
  |FINSI;

  |GRABA 020#777a;
  |LIBERA #777;

  |SI nACompensar = 0;
      |ERROR10;
  |FINSI;
|FPRC;

|DEFBUCLE Auxiliar;
  #777#1004;
  ;
  #999#0, #999#1,  0000,  00;
  #999#0, #999#1, #999#2, 99;
  e;
  NULL, Auxiliar0;
|FIN;

||\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\
|PROCESO EldeAntes;
 nACompensar = 0;
 #999#4 = #24#8;   || Resultado impreso ese Periodo
 |SI swPrAny = 1;
     |SI #24#13 ] 0;  || Resultado positivo
         #999#5 = #999#5 + #24#13;
         #999#6 = #999#6 + 0;
         #999#7 = #999#7 + 0;
         nACompensar = #24#13;
     |SINO;
         #999#5 = #999#5 + 0;   || Resultado negativo
         |SI #24#11 ! 0;
             #999#6 = #999#6 + 0;
             #999#7 = #999#7 - #24#13;
         |SINO;
             #999#6 = #999#6 - #24#13;
             #999#7 = #999#7 + 0;
         |FINSI;
     |FINSI;
  |SINO;
     #999#5 = #999#5 + #24#9;  |SI #24#9  ! 0; nACompensar = #24#9; |FINSI;
     #999#6 = #999#6 + #24#10; |SI #24#10 ! 0; nACompensar = #24#10; |FINSI;
     #999#7 = #999#7 + #24#11;
  |FINSI;

  #999#9 = #999#7;  ||Pendiente de Compensar
  |SI #999#9 ! 0;
      nNume1 = #24#1 + 4; aAlfa1 = nNume1;
      nNume1 = #24#1 + 5; aAlfa2 = nNume1;
      |SI #24#2 [ 12; #999#10 = 4; aAlfa3 = "30.01." + aAlfa2; |FINSI;
      |SI #24#2 [  9; #999#10 = 3; aAlfa3 = "20.10." + aAlfa1; |FINSI;
      |SI #24#2 [  6; #999#10 = 2; aAlfa3 = "20.07." + aAlfa1; |FINSI;
      |SI #24#2 [  3; #999#10 = 1; aAlfa3 = "20.04." + aAlfa1; |FINSI;
      #999#11  = #24#1 + 4;   || Sumar 4 al a¤o
      #999#17  = aAlfa3;      || Fecha
  |FINSI;

  |SI swPrAny = 1;
      |SI nACompensar ! 0;
          nCompensar = 0;
          |BUCLE Auxiliar;
      |FINSI;
  |FINSI;

  |SI swPrAny = 0;    ||Queda el primero solo
      |SI #999#9 = 0;
          |BORRA 020#999a;
          |LIBERA #999;
          |ACABA;
      |FINSI;
  |FINSI;
  |GRABA 020#999a;
  |LIBERA #999;

  swPrAny = 1;
  swAlgun = 1;
|FPRC;
|| **************************************************************
|| **************************************************************
|PROCESO LeePrescrito;
     aResta = "X";
     nResta = 0;
     |ABRE #28;
     #28#0 = #999#0;   || Empresa
     #28#1 = #999#2;   || A¤o
     #28#2 = #999#3;   || Periodo
     #28#3 = nMod2009; || Modelo
     #28#4 = #24#4;
     #28#5 = 0;
     |LEE 040#28=;
     |SI FSalida = 0;
         aResta = #28#7;
         |SI #28#7 ! "N";
             nResta = #28#6;
         |FINSI;
     |FINSI;
     |CIERRA #28;
|FPRC;

|| **************************************************************
|PROCESO Leem024;

 #dsmom004#0 = #dsmom024#0;
 #dsmom004#1 = #dsmom024#1;
 #dsmom004#2 = #dsmom024#2;
 #dsmom004#3 = #dsmom024#3;
 #dsmom004#4 = #dsmom024#4 + 1;
 |LEE 040#dsmom004.=;
 |SI FSalida = 0; |ACABA; |FINSI;

 |DDEFECTO #dsmom004;
 #dsmom004#0 = #dsmom024#0;
 #dsmom004#1 = #dsmom024#1;
 #dsmom004#2 = #dsmom024#2;
 #dsmom004#3 = #dsmom024#3;
 #dsmom004#4 = #dsmom024#4;
 |LEE 040#dsmom004.=;

 |SI #24#1 = #0#15; |Y #24#2 > #0#28;
     |ERROR10;
     |ACABA;
 |FINSI;

 |SI #24#1 = #0#15; |Y #24#2 = #0#28;
     nEstePer = 1;
 |FINSI;

 #999#0 = #24#0;     || Empresa
 #999#1 = nMod2009;  || Modelo
 #999#2 = #24#1;     || A¤o

 #999#3 = #24#2;     || Periodo

 |LEE 101#999=;
 |SI FSalida ! 0;
     |DDEFECTO #999;
     #999#0 = #24#0;     || Empresa
     #999#1 = nMod2009;  || Modelo
     #999#2 = #24#1;     || A¤o
     #999#3 = #24#2;     || Periodo
     |HAZ LeePrescrito;
     #999#26 = nResta;
     #999#27 = aResta;
     #999#29 = #dsmom004#22;
     |GRABA 020#999b;
 |FINSI;

 |SI #0#30 = 1;
     |HAZ EldeAhora;
 |SINO;
     |HAZ EldeAntes;
 |FINSI;
|FPRC;

|PROCESO EstePeriodo;
 |SALVA_DATOS #23;
 || |SI nParam = 0; |ACABA; |FINSI;
 nImpReal  = 0;
 nCas21    = 0;
 nCas27    = 0;
 nCas31    = 0;
 nCas34    = 0;
 nIngresar = 0;
 nDevolver = 0;
 nCompensar= 0;

 |SI nElModelo = 322;
     |ABRE #dsm32201;
     |DDEFECTO #dsm32201;
     #dsm32201#0 = nLaEmpresa;
     #dsm32201#1 = #0#15;
     #dsm32201#2 = #0#28;
     #dsm32201#3 = nElModelo;
     #dsm32201#4 = 99;
     #dsm32201#5 = 00;
     |LEE 040#dsm32201.];
     |SI FSalida = 0;
         |LEE 040#dsm32201.a;
     |SINO;
         |LEE 040#dsm32201.u;
     |FINSI;
     |SI FSalida = 0; |Y #dsm32201#0 = nLaEmpresa; |Y #dsm32201#1 = #0#15;
        |Y #dsm32201#2 = #0#28; |Y #dsm32201#3 = nElModelo;
            nImpReal  = #dsm32201#81;
            nCas21    = #dsm32201#42 + #dsm32201#54;
            nCas27    = #dsm32201#60 + #dsm32201#72;
            nCas31    = #dsm32201#84;
            nCas34    = #dsm32201#88;
            nIngresar = #dsm32201#91;
            nDevolver = #dsm32201#90;
            nCompensar= #dsm32201#89;
     |FINSI;
     |CIERRA #dsm32201;
 |SINO;
     |ABRE #dsm30301;
     |DDEFECTO #dsm30301;
     #dsm30301#0 = nLaEmpresa;
     #dsm30301#1 = #0#15;
     #dsm30301#2 = #0#28;
     #dsm30301#3 = nElModelo;
     #dsm30301#4 = 99;
     #dsm30301#5 = 00;
     |LEE 040#dsm30301.];
     |SI FSalida = 0;
         |LEE 040#dsm30301.a;
     |SINO;
         |LEE 040#dsm30301.u;
     |FINSI;
     |SI FSalida = 0; |Y #dsm30301#0 = nLaEmpresa; |Y #dsm30301#1 = #0#15;
        |Y #dsm30301#2 = #0#28; |Y #dsm30301#3 = nElModelo;
            nImpReal  = #dsm30301#81;
            nCas21    = #dsm30301#42 + #dsm30301#54;
            nCas27    = #dsm30301#60 + #dsm30301#72;
            nCas31    = #dsm30301#84;
            nCas34    = #dsm30301#88;
            nIngresar = #dsm30301#91;
            nDevolver = #dsm30301#90;
            nCompensar= #dsm30301#89;
     |FINSI;
     |CIERRA #dsm30301;
 |FINSI;

 nReserva = nMod2009;
 nMod2009 = nElModelo;
 |DDEFECTO #23;
 #23#0 = nLaEmpresa;
 #23#1 = #0#15;
 #23#2 = nElModelo;

 |DDEFECTO #24;
 #24#0 = #23#0;
 #24#1 = #23#1;
 #24#2 = #0#28;
 #24#3 = #23#2;

  #dsmom024#9  = nIngresar;
  #dsmom024#10 = nDevolver;
  #dsmom024#11 = nCompensar;
  #dsmom024#13 = nImpReal;
  #dsmom024#14 = nCas21;
  #dsmom024#15 = nCas27;
  #dsmom024#16 = nCas31;
  #dsmom024#17 = nCas34;

  |HAZ Leem024;
  |REPON_DATOS #23;
  nMod2009 = nReserva;
|FPRC;

|| *************************************************************************
|PROCESO CampoCompensado;
       #777#12 = #999#3;
       |SI #999#29 = "T";
           |SI #999#3 [ 12; #777#12 = 4; |FINSI;
           |SI #999#3 [  9; #777#12 = 3; |FINSI;
           |SI #999#3 [  6; #777#12 = 2; |FINSI;
           |SI #999#3 [  3; #777#12 = 1; |FINSI;
       |FINSI;
       #777#13 = #999#2;
       #777#31 = #999#29;
|FPRC;

||\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\
|PROCESO Auxiliar0N;

 |SI #777#2 = #999#2; |Y #777#3 ] #999#3;
     |ERROR10;
     |ACABA;
 |FINSI;

 |SI #777#9 = 0;
     |ACABA;                         || Ya esta compensado
 |FINSI;

 nSwEste = 0;
 nMes    = #777#10;
 nAny    = #777#11;
 |SI #777#29 = "M"; |Y #999#29 = "T";
     |SI nMes ! 3; |Y nMes ! 6; |Y nMes ! 9; |Y nMes ! 12;
         |SI nMes = 1; |O nMes = 4; |O nMes = 7; |O nMes = 10;
             nMes = nMes - 1;
         |SINO;
             nMes = nMes - 2;
         |FINSI;
         |SI nMes = 0;
             nMes = 12;
             nAny = nAny - 1;
        |FINSI;
    |FINSI;
 |FINSI;

 |SI nAny < #999#2;
     aTPer   = #777#29;
     nAnyP   = #777#2;
     nPerP   = #777#3;
     #999#25 = #777#9;  || .. Importe prescrito
     #777#9  = 0;
 |SINO;
     |SI nAny = #999#2;
         nNume1 = nMes;
         |SI #777#29 = "T";
             |SI #777#10 = 4; nNume1 = 12; |FINSI;
             |SI #777#10 = 3; nNume1 =  9; |FINSI;
             |SI #777#10 = 2; nNume1 =  6; |FINSI;
             |SI #777#10 = 1; nNume1 =  3; |FINSI;
         |FINSI;
         |SI #999#3 > nNume1;
             aTPer   = #777#29;
             nAnyP   = #777#2;
             nPerP   = #777#3;
             #999#25 = #777#9;  || .. Importe prescrito
             #777#9  = 0;
         |SINO;
             |SI #999#3 = nNume1;
                  aTPer   = #777#29;
                  nAnyP   = #777#2;
                  nPerP   = #777#3;
                  nSwEste = 1;
                  #999#25 = #777#9;  || .. Importe prescrito
             |FINSI;
         |FINSI;
     |FINSI;
 |FINSI;

 |SI nSwDevuelto = 1;
     #777#8 = #777#8 + #777#9;            || Compensado
     #777#9 = 0;

     |HAZ CampoCompensado;
 |SINO;
     |SI #777#9 ! 0; |Y nACompensar ! 0;
          nNume1 = #777#9 - nACompensar;
          |SI nNume1 [ 0;
              nACompensar = nACompensar - #777#9;  || Pendiente Compensar
              #777#8 = #777#8 + #777#9;            || Compensado
              #777#9 = 0;

              |HAZ CampoCompensado;
          |SINO;
              #777#8 = #777#8 + nACompensar;     || Compensado
              |HAZ CampoCompensado;
              #777#9 = #777#9 - nACompensar;   || Pendiente Compensar
              nACompensar = 0;
          |FINSI;
      |FINSI;
  |FINSI;

  |SI nSwEste = 1;
      #999#28 = #777#9;  || .. Importe prescrito calculado

      |SI #999#25 = 0;
          nAnyP   = 0;
          nPerP   = 0;
          aTPer   = "";
      |FINSI;
      #777#9  = 0;
  |FINSI;

  |GRABA 020#777a;
  |LIBERA #777;

|FPRC;

|DEFBUCLE AuxiliarN;
 #777#1004;
 ;
 #999#0, #999#1,  0000,  00;
 #999#0, #999#1, #999#2, 99;
 e;
 NULL, Auxiliar0N;
|FIN;

|PROCESO EldeAhora;

  nSwDevuelto = 0;
  #999#5 = #24#9;    || a ingresar
  #999#6 = #24#10;   || a devolver
  #999#7 = #24#11;   || a compensar

  #999#19 = #24#14;  || iva devengado
  #999#20 = #24#15;  || iva deducible
  #999#21 = #24#13;  || diferencia
  #999#22 = #24#16 + #999#26;  || Cuotas a compensar de Ejercicios Anteriores
  |SI #999#22 ! 0;             || si no tengo suficiente no compenso todo
      |SI #999#21 < 0;
          #999#22 = 0;
      |SINO;
          |SI #999#22 > #999#21;
              #999#22 = #999#21;
          |FINSI;
      |FINSI;
  |FINSI;

  #999#23 = 0;
  |SI #999#7 ! 0; |Y #999#21 < 0;
      #999#23 = - #999#21;
      |SI #999#7 < #999#23;
          #999#23 = #999#7;
      |FINSI;
  |FINSI;
  #999#24 = #24#17;

  #999#9 = #999#23;  ||Pendiente de Compensar
  |SI #999#9 ! 0;
      nNume1 = #24#1 + 4; aAlfa1 = nNume1;
      nNume1 = #24#1 + 5; aAlfa2 = nNume1;
      |SI #999#29 = "T";
          |SI #24#2 [ 12; #999#10 = 4; aAlfa3 = "30.01." + aAlfa2; |FINSI;
          |SI #24#2 [  9; #999#10 = 3; aAlfa3 = "20.10." + aAlfa1; |FINSI;
          |SI #24#2 [  6; #999#10 = 2; aAlfa3 = "20.07." + aAlfa1; |FINSI;
          |SI #24#2 [  3; #999#10 = 1; aAlfa3 = "20.04." + aAlfa1; |FINSI;
      |SINO;
          #999#10 = #24#2;
          |SI #24#2 = 12; aAlfa3 = "30.01." + aAlfa2; |FINSI;
          |SI #24#2 ! 12;
              nNume2 = #24#2 + 1;
              aAlfa3 = ("00" + nNume2) % -102;
              aAlfa3 = "20." + aAlfa3 + "." + aAlfa1;
          |FINSI;
      |FINSI;
      #999#11  = #24#1 + 4;   || Sumar 4 al a¤o
      #999#17  = aAlfa3;  || Fecha
  |FINSI;

  nACompensar = 0;
  |SI #999#21 > 0;
      nACompensar = #999#21;
  |FINSI;
  |SI #999#6 ! 0;
      nACompensar = nACompensar + #999#6;
      nSwDevuelto = 1;
  |FINSI;

  nPerP = 0;
  nAnyP = 0;
  #999#14 = 0;
  #999#15 = 0;
  #999#25 = 0;
  |BUCLE AuxiliarN;
  |SI #999#25 ! 0; |O #999#26 ! 0; |O #999#28 ! 0;
      |SI aTPer = "T";
         |SI nPerP [ 12; nNume1 = 4; |FINSI;
         |SI nPerP [  9; nNume1 = 3; |FINSI;
         |SI nPerP [  6; nNume1 = 2; |FINSI;
         |SI nPerP [  3; nNume1 = 1; |FINSI;
      |SINO;
         nNume1 = nPerP;
      |FINSI;
      #999#14 = nNume1;
      #999#15 = nAnyP;
      #999#30 = aTPer;
  |FINSI;

  #999#18 = "";
  |GRABA 020#999a;
  |LIBERA #999;

  |SI swPrAny = 0; |Y #999#24 < 0;  ||Compensar anterior
      |HAZ LoAntiguo;
  |FINSI;

  swPrAny = 1;
  swAlgun = 1;
|FPRC;

|PROCESO LoAntiguo;
  nPendiente = - #999#24;
  nPendiente = nPendiente - #999#23;
  |SI nPendiente [ 0; |ACABA; |FINSI;

  #999#0 = #24#0;      || Empresa
  #999#1 = nMod2009;   || Modelo
  #999#2 = #24#1;
  #999#3 = #24#2 - 3;
  |SI #999#3 = 0;
      #999#3 = 12;
      #999#2 = #999#2  - 1;
  |FINSI;

  |LEE 101#999=;
  |SI FSalida ! 0;
       |DDEFECTO #999;
       #999#0 = #24#0;      || Empresa
       #999#1 = nMod2009;   || Modelo
       #999#2 = #24#1;      || A¤o
       #999#3 = #24#2 - 3;  || Periodo
       |SI #999#3 = 0;
           #999#3 = 12;
           #999#2 = #999#2  - 1;
       |FINSI;
       |GRABA 020#999b;
  |FINSI;

  #999#7  = nPendiente;
  #999#19 = 0;  || iva devengado
  #999#20 = 0;  || iva deducible
  #999#21 = - nPendiente;
  #999#22 = 0;
  #999#23 = nPendiente;
  #999#24 = - nPendiente;
  #999#9 = #999#23;  ||Pendiente de Compensar

      nNume1 = #999#2 + 4; aAlfa1 = nNume1;
      nNume1 = #999#2 + 5; aAlfa2 = nNume1;
      |SI #999#3 [ 12; #999#10 = 4; aAlfa3 = "30.01." + aAlfa2; |FINSI;
      |SI #999#3 [  9; #999#10 = 3; aAlfa3 = "20.10." + aAlfa1; |FINSI;
      |SI #999#3 [  6; #999#10 = 2; aAlfa3 = "20.07." + aAlfa1; |FINSI;
      |SI #999#3 [  3; #999#10 = 1; aAlfa3 = "20.04." + aAlfa1; |FINSI;
      #999#11  = #999#2 + 4;   || Sumar 4 al a¤o
      #999#17  = aAlfa3;       || Fecha

  #999#25 = 0;
  #999#18 = "P";
  |GRABA 020#999a;
  |LIBERA #999;
  swAlgun = 1;
|FPRC;

||/////////////////////////////////////////////////////////////

|DEFBUCLE Leem024;
 #24#1;
 ;
 #23#0, #23#1, 00, #23#2, 00;
 #23#0, #23#1, 99, #23#2, 99;
 e;
 NULL, Leem024;
|FIN;

|PROCESO CreaAuxiliar0;

 |SI #23#2 = 347; |O #23#2 = 349; |ACABA; |FINSI;

 nMod2009 = #23#2;
 |SI #0#15 ] 2009;
     |SI #23#2 = 300; |O #23#2 = 320; |O #23#2 = 330; |O #23#2 = 332;
         nMod2009 = 303;
     |FINSI;
 |FINSI;

 |SI swPrEmpresa = 0; |O nLaEmpresa ! #23#0;

     |SI nEstePer = 0; |Y swPrEmpresa ! 0;
         |HAZ EstePeriodo;
     |FINSI;

     swPrEmpresa = 1;
     nLaEmpresa = #23#0;
     swPrModelo = 0;
     swPrAny    = 0;
     nEstePer   = 0;
 |FINSI;

 |SI swPrModelo = 0; |O nElModelo ! nMod2009;

     |SI nEstePer = 0; |Y swPrModelo ! 0;
         |HAZ EstePeriodo;
     |FINSI;

     swPrModelo = 1;
     nElModelo  = nMod2009;
     swPrAny    = 0;
     nEstePer   = 0;
 |FINSI;


 nElAny = #23#1;   ||A¤o
 |BUCLE Leem024;

|FPRC;

|| .....................................................................
|DEFBUCLE CreaAuxiliar;
    #23#2;
    ;
    nDEmpresa, nDMod, #0#14;   ||Empresa Modelo A¤o
    nHEmpresa, nHMod, #0#15;
    e;
    NULL, CreaAuxiliar0;
|FIN;

|| \\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\
|PROCESO ITipo3;  |TIPO 3;

 eaRaya = &179;

 |DBASE aPathModelo;  |QBT aPathModelo;
 aDef = aPathModelo + "def/dsmow029.mas";
 |CARGA_DEF aDef, program@;
 |SI FSalida ! 0;
     aAlfa = "     Error en cargar el Def dsmow029";
     |MENAV aAlfa;
     |ACABA;
 |FINSI;

 aFichero = ("07" + Usuario) % 108;
 |NOME_DAT #999  aFichero;
 |ABRE #999;  |CIERRA #999;  |DELINDEX #999;
 |NOME_DAT #777 aFichero;

 |ABRE #999;
 swAlgun     = 0;
 swPrEmpresa = 0;
 swPrModelo  = 0;

 |ABRE #999;
 |ABRE #dsmom004;
 |SI #0#0 ! 0;
     nDEmpresa = #0#0;
     nHEmpresa = #0#1;
     |BUCLE CreaAuxiliar;
     |SI nEstePer = 0; |Y swAlgun = 1;
         |HAZ EstePeriodo;
     |FINSI;
 |SINO;
     |PARA nCampo = 2;  |SI nCampo [ 13;  |HACIENDO nCampo = nCampo + 1;
           nDEmpresa = #0nCampo;
           nHEmpresa = #0nCampo;
           |BUCLE CreaAuxiliar;         || CreaAuxiliar Valor
           |SI nEstePer = 0; |Y swAlgun = 1;
               |HAZ EstePeriodo;
           |FINSI;
      |SIGUE;
 |FINSI;
 |CIERRA #dsmom004;
 |CIERRA #999;

 |SI swAlgun = 1;
    |SI #0#30 = 2; |BUCLE ElRepaso; |FINSI;
    |HAZ Imprimir;
 |SINO;
    |SI aParametro = "";
        |MENAV "    Seleccion vacia ... ";
    |FINSI;
 |FINSI;

 |DESCARGA_DEF #777;
 |DELINDEX #999;
|FPRC;

|| =========================================================================


|| *********************************************************************
|| **********PROCESO PARA IMPRIMIR AUXILIAR ****************************
|| *********************************************************************
|PROCESO dsmow029_Ant0;
  |SI #999#3 = enPeriodo; |Y #999#2 = enEjer;
      enImporte = #999#25;
      enImport1 = #999#28;
      enTrim    = #999#14;
      enAny     = #999#15;
  |SINO;
      enImpAcum = enImpAcum + #999#26;
  |FINSI;
|FPRC;

|DEFBUCLE dsmow029_Ant;
 #999#1;
 ;
 enEmpresa, enModelo, 0000, 00;
 enEmpresa, enModelo, 9999, 99;
 ;
 NULL, dsmow029_Ant0;
|FIN;
|| --------------------------------

|PROCESO LeeElModelo;
|FPRC;


|PROCESO TotalModelo;
 |PIEINF;
 swAlgun = 0;
 swPrAny = 0;
|FPRC;

|PROCESO dsmow0290;
 eaTex = "";
 |SI swPrEmpresa = 0;
     swPrEmpresa = 1;
     nLaEmpresa = #999#0;
     |HAZ LeeLaEmpresa;
 |FINSI;

 |SI nLaEmpresa ! #999#0;
     |SI swAlgun = 1;
         |HAZ TotalModelo;
     |FINSI;
     nLaEmpresa = #999#0;
     |HAZ LeeLaEmpresa;
 |FINSI;

 |SI swPrModelo = 0;
     swPrModelo = 1;
     nElModelo = #999#1;
     |HAZ LeeElModelo;
 |FINSI;

 |SI nElModelo ! #999#1;
     |HAZ TotalModelo;
     nElModelo = #999#1;
     |HAZ LeeElModelo;
 |FINSI;

 |SI swPrAny = 0;
     nElAny  = #999#2;
     swPrAny = 1;
 |FINSI;

 |SI nElAny ! #999#2;
     enSwLinea = 1;
     nElAny = #999#2;
 |FINSI;

 |SI #999#29 = "T";
     |SI #999#3 = 3;  eaLit1 = "1§Trimestre"; |FINSI;
     |SI #999#3 = 6;  eaLit1 = "2§Trimestre"; |FINSI;
     |SI #999#3 = 9;  eaLit1 = "3§Trimestre"; |FINSI;
     |SI #999#3 = 12; eaLit1 = "4§Trimestre"; |FINSI;
 |SINO;
     |SI #999#3 = 1;  eaLit1 = "Enero"; |FINSI;
     |SI #999#3 = 2;  eaLit1 = "Febrero"; |FINSI;
     |SI #999#3 = 3;  eaLit1 = "Marzo"; |FINSI;
     |SI #999#3 = 4;  eaLit1 = "Abril     "; |FINSI;
     |SI #999#3 = 5;  eaLit1 = "Mayo      "; |FINSI;
     |SI #999#3 = 6;  eaLit1 = "Junio     "; |FINSI;
     |SI #999#3 = 7;  eaLit1 = "Julio     "; |FINSI;
     |SI #999#3 = 8;  eaLit1 = "Agosto    "; |FINSI;
     |SI #999#3 = 9;  eaLit1 = "Septiembre"; |FINSI;
     |SI #999#3 = 10; eaLit1 = "Octubre   "; |FINSI;
     |SI #999#3 = 11; eaLit1 = "Noviembre "; |FINSI;
     |SI #999#3 = 12; eaLit1 = "Diciembre "; |FINSI;
 |FINSI;

 |SI #999#25 ! 0; |O #999#26 ! 0; |O #999#28 ! 0;
     |SI #999#27 = "S"; eaTex = "Automatico"; |FINSI;
     |SI #999#27 = "N"; eaTex = "No aplica "; |FINSI;
     |SI #999#27 = "M"; eaTex = "Manual    "; |FINSI;
     |SI #999#27 = "X"; eaTex = "Sin (F5)  "; #999#26 = #999#28; |FINSI;
 |FINSI;

 |PRINT;

 enSwLinea = 0;
 swAlgun   = 1;
|FPRC;

|PROCESO dsmow0291;
  |SI swAlgun = 1;
      |HAZ TotalModelo;
  |FINSI;
  swAlgun = 0;
|FPRC;

|DEFBUCLE dsmow029;
 #999#1;
 ;
 00000,000,0000,00;
 99999,999,9999,99;
 ;
 NULL, dsmow0290, NULL, NULL, dsmow0291;
|FIN;

|PROCESO Imprimir;
     |SI aParametro = "";
         |IMPRE 0;
         |SI FSalida ! 0; |ACABA; |FINSI;

         informe = inform1;
         |SI #0#30 = 2; informe = inform2; |FINSI;
         |INFOR informe;
         |SI FSalida ! 0; |FINIMP; |ACABA; |FINSI;

         swAlgun     = 0;
         swPrEmpresa = 0;
         swPrModelo  = 0;
         swPrAny     = 0;
         enSwLinea   = 0;

        |BUCLE dsmow029;

        |FININF;
        |FINIMP;
     |SINO;
         enImpAcum = 0;
         enImporte = 0;
         enImport1 = 0;
         enTrim    = 0;
         enAny     = 0;

         |BUCLE dsmow029_Ant;
    |FINSI;
|FPRC;


|| ***********************************************************************

|| ***********************************************************************
|PROCESO LeeLaEmpresa;
  |PDEFECTO #0, 16,27;
  #0#16 = nLaEmpresa;

  swAlgun    = 0;
  swPrModelo = 0;
  swPrAny    = 0;
  nPin1 = 0;
  aPin1 = "";
  efPn10 = "01.01.0000";
  efPn11 = "01.01.0000";

  || Lee Actividad
    |ABRE #agicen14;
    #agicen14#0 = #0#16; || Empresa
    #agicen14#1 = 00;
    |LEE 041#agicen14.];
    |SI FSalida = 0; |Y #agicen14#0 = #0#16;
        #0#21 = #agicen14#9;
        #0#22 = #agicen14#10;
        #0#23 = #agicen14#7;
        #0#24 = #agicen14#8;
        efPn10 = #agicen14#5;
        efPn11 = #agicen14#6;
    |FINSI;
    |CIERRA #agicen14;

 || Lee Ficha Sociedades
    swExiste = 0;
    |ABRE #agim0019;
    #agim0019#0 = #0#16; || Empresa
    #agim0019#1 = 99;
    |LEE 041#agim0019.];
    |SI FSalida = 0;
       |LEE 041#agim0019.a;
    |SINO;
       |LEE 041#agim0019.u;
    |FINSI;
    |SI FSalida = 0; |Y #agim0019#0 = #0#16;
        swExiste = 1;
    |FINSI;
    |SI swExiste = 1;
        efPn10 = #agim0019#7;      || Fecha Activa
        efPn11 = #agim0019#55;     || Fecha Inactiva
        nPin1 = #agim0019#4;
        aPin1 = #agim0019#5;
    |FINSI;
    |CIERRA #agim0019;

    |ABRE #agifigen;
    #agifigen#0 = #0#16; || Empresa
    |LEE 041#agifigen.=;
    |SI FSalida = 0;
        efPn10  = #agifigen#93;
        efPn11  = #agifigen#94;
        nPin1   = #agifigen#87;
        aPin1   = #agifigen#88;
        #0#17 = #agifigen#13;  || Nif
        #0#18 = #agifigen#1;   || Nombre
    |FINSI;
    |CIERRA #agifigen;
    |SI efPn10 [ "01.01.1900"; efPn10 = ""; |FINSI;
    |SI efPn11 [ "01.01.1900"; efPn11 = ""; |FINSI;
    #0#19 = nPin1;
    #0#20 = aPin1;
    #0#25 = efPn10;
    #0#26 = efPn11;
|FPRC;

|PROGRAMA;

     nParam = 0;
     aParametro = PARAMETRO;  |QBF aParametro;
     |SI aParametro ! ""; nParam = 1;|FINSI;
     |SI aParametro = "ENESTE"; aParametro = ""; |FINSI;

     |SI aParametro = "";
         nDMod = 300;
         nHMod = 370;

         |SI enEmpresa ! 0;
             #0#0 = enEmpresa;
             #0#1 = enEmpresa;
             #0#15 = enEjer;
             #0#28 = enPeriodo;
             |HAZ ITipo3;
         |SINO;
             |CLS;
             |MANTE #0;
         |FINSI;
     |SINO;
         nDMod = enModelo;
         nHMod = enModelo;
         #0#0  = enEmpresa;
         #0#1  = enEmpresa;
         #0#15 = enEjer;
         #0#28 = enPeriodo;
         #0#30 = 1;
         |SI enEjer ] 2009;
             |SI  enModelo = 303; |O enModelo = 322;
                  nDMod = 300; nHMod = 330;
             |FINSI;
         |FINSI;
         |HAZ ITipo3;
     |FINSI;
|FPRO;
