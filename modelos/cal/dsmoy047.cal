|FICHEROS;
   dsmoy047 #0;
   dsmom041 #41;
   dsmom042 #42;
   dsmom040 #40;
   dsmom100 #100;
   dsmom107 #107;

   :/contagen/def/cacuenta #300;
   :/contagen/def/caconimp #306;  || configuracion impresiones
   dsmow030 #900;
|FIN;

|VARIABLES;
   &entrada = 1;

   &eaTitolCta ="";
   &aEmpresa  = "";
   nLaEmpresa = 0;
   informe  = "dsmoy047";

   nCampo    = 0;
   nDEmpresa = 0;
   nHEmpresa = 0;
   nDActividad = 0;
   nHActividad = 0;
   swCuenta = 0;
   swPrimero = 0;
   swBueno = 0;
   &nCamSel = 23;
   &nCamTip = 38;
   fFecha   = @;
   aAlfa1   = "";
   aAlfa2   = "";
   aPath    = "";
   nCambio  = 0;
   &eaTit   = "";
   &enSub   = 0;
|FIN;

|INCLUYE i_variar;
|INCLUYE i_varcon;
|INCLUYE i_varlib;
|INCLUYE i_numamo;
|INCLUYE i_selcon;

|| //////////////////// Calculos de Pantalla \\\\\\\\\\\\\\\\\\\\\\\\\\

|PROCESO DatosEmp; |TIPO 7;

   |DDEFECTO #306;

   |ABRE #100;
   |LEE 040#100p;
   |SI FSalida = 0;
       aAlfa1 = #100#1; |QBF aAlfa1; aAlfa1 = ("00000" + aAlfa1) % -105;
       aAlfa2 = #100#2; |QBF aAlfa2; aAlfa2 = ("0"     + aAlfa2) % -101;
       |DBASS aPath; |QBT aPath;
       aPathConta = aPath + "contagen/fich/" + aAlfa1 + aAlfa2 + "/";

       |PATH_DAT #306 aPathConta;

       |ABRE #306;
       #306#0 = 1;
       |LEE 000#306=;
       |CIERRA #306;
   |FINSI;
   |CIERRA #100;

   #0#39  = #306#7; |PINTA #0#39;
   aAlfa1 = #306#8; |C_M #0#39,1,aAlfa1;

   |ABRE #107;
   |DDEFECTO #107;
   |LEE 041#107p;
   |CIERRA #107;

   |SI #107#28 = "S";
       |C_V #0#41, 1, "S"; |C_M #0#41, 1, "S";
       |C_V #0#42, 1, "S"; |C_M #0#42, 1, "S";
       |PINTA 1332, "Activ./Subdepar. ";
       |PINTA #0#41;
       |PINTA #0#42;
   |FINSI;
|FPRC;

|CALCULO Tipo7;  |TIPO 7;
     |SI enEmpresa ! 0;
         #0#7 = enEmpresa; |PINTA #0#7;
         #0#8 = enEmpresa; |PINTA #0#8;

         |C_M #0#7, 1, "N";
         |C_M #0#8, 1, "N";
     |FINSI;

|FCAL;

|CALCULO Parrilla;
     |SI #0#7 = 0;
         #0#7 = 0;  |PINTA #0#7;
         #0#8 = 0;  |PINTA #0#8;  |C_M #0#8, 1, "N";
         |PARA nCampo = 9;  |SI nCampo [ 20;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |SINO;
         |SI enEmpresa = 0;
             |C_M #0#7, 1, "S";
             |C_M #0#8, 1, "S";
         |FINSI;

         |PARA nCampo = 9;  |SI nCampo [ 20;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "N";
               #0nCampo = 0;  |PINTA #0nCampo;
         |SIGUE;
     |FINSI;
|FCAL;

|CALCULO NoMasEmpresas;
     |SI #0Campo = 0;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 20;  |HACIENDO nCampo = nCampo + 1;
               #0nCampo = 0;  |C_M #0nCampo, 1, "N";
         |SIGUE;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 20;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |FINSI;
|FCAL;

|CALCULO numamo; |TIPO 0;
   x_alfa1 = #0Campo;
   |HAZ x_punto;
   #0Campo = x_alfa1;
   |PINTA #0Campo;
|FCAL;

|CALCULO cuenta; |TIPO 0;
     salidas  = FSalida;

     |ABRE #100;
     |LEE 040#100p;
     |SI FSalida ! 0;  |DDEFECTO #100;  |FINSI;
     |CIERRA #100;

     nLaEmpresa = enEmpresa;

||     |SI enEmpresa = 0;
         enEmpresa  = #100#1;
         enPerConta = #100#2;
||     |FINSI;

     eaCuenta = #0Campo;
     |HAZ FiltraPuntos;
     #0Campo = eaCuenta;
     |PINTA #0Campo;
     enEmpresa = nLaEmpresa;
|FCAL;


|CALCULO Tipo3;  |TIPO 3;

     enDatos = 0; |SI #0#39 = "S"; enDatos = 1; |FINSI;
     fFecha = ~;
     aAlfa1 = fFecha; aAlfa1 = aAlfa1 % -104;
     enEjer = aAlfa1;
     aAlfa2 = "01.01." + aAlfa1; efDFecha = aAlfa2;
     aAlfa2 = "31.12." + aAlfa1; efHFecha = aAlfa2;
     eaTituloL = #0#5;

     |IMPRE 0;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |INFOR "dsmoy047";
     |SI FSalida ! 0;  |FINIMP;  |ACABA;  |FINSI;

     nDActividad = #0#21;
     nHActividad = #0#22;

     |SI #0#7 ! 0;
         nDEmpresa = #0#7;
         nHEmpresa = #0#8;
         |HAZ imprimir;
     |SINO;
         |PARA nCampo = 9; |SI nCampo [ 20;  |HACIENDO nCampo = nCampo + 1;
               nDEmpresa = #0nCampo;
               nHEmpresa = #0nCampo;
               |SI nDEmpresa ! 0;
                   |HAZ imprimir;
               |FINSI;
         |SIGUE;
     |FINSI;
     |FININF;
     |FINIMP;
|FCAL;

|| //////////////////// Procesos del Informe  \\\\\\\\\\\\\\\\\\\\\\\\\\
|PROCESO ElAuxiliar;
  aAlfa1 = #41#27;
  aAlfa1 = #41#27; aAlfa1 = aAlfa1 % -104;
  enEjerCam = aAlfa1;

  |SI enEjerCam ! #0#40;
      nCambio   = 0;
      |SI enEjerCam < 2008; |Y #0#40 ] 2008;
          nCambio = 1;
      |FINSI;
      |SI enEjerCam ] 2008; |Y  #0#40 < 2008;
          nCambio = 1;
      |FINSI;
      |SI nCambio = 1;
          |HAZ CambioCta;
          |SI #0#40 ] 2008; eaCuenta = eaCta2008; |FINSI;
          |SI #0#40 < 2008; eaCuenta = eaCta1990; |FINSI;
      |FINSI;
  |FINSI;
|FPRC;

|PROCESO LeerLaCuenta;
 eaCuenta = #41#5;
 |HAZ ElAuxiliar;
 #41#5 = eaCuenta;

 eaTitolCta = "";
 |PATH_DAT #300 aPathConta;
 |ABRE #300;
 #300#0 = #41#5;
 |LEE 041#300=;
 |SI FSalida = 0;
     eaTitolCta = #300#2;
 |FINSI;
 |CIERRA #300;
|FPRC;

|PROCESO LaImpresion;

   |SI #0#6 = "N";
       |SI #dsmom041#25 = "N"; |ACABA; |FINSI;
   |FINSI;

   |SI swPrimero = 0;  ||Controla el primer pase
       swPrimero   = 1;
       enEmpresa   = #dsmom041#0;
       eaNombreEmp = #dsmom041#32;
       enPerConta  = -1;
       enEjer      = 0;
       swCuenta    = 0;
       swBueno     = 0;
       |HAZ LEmpresaConta;
       |SI enSwSelEmp = 1;
           swBueno = 1;
       |SINO;
           aEmpresa    = enEmpresa; aEmpresa = ("00000" + aEmpresa) % -105;
           |SI enDatos = 1; enActividad = 0;  |HAZ DatosEmpr_w30; |FINSI;
           |HAZ AntCambioCta;
           |SI #107#28 = "S";  |HAZ eBuscoSubdep; |FINSI;
       |FINSI;
   |FINSI;

   |SI enEmpresa ! #dsmom041#0; || Controla la Ruptura por Empresas
       |SI swCuenta = 1;
           |PIEINF; || imprime el pie del informe
       |FINSI;
       enEmpresa   = #dsmom041#0;
       eaNombreEmp = #dsmom041#32;
       enPerConta  = -1;
       enEjer      = 0;
       swCuenta    = 0;
       swBueno     = 0;
       |HAZ LEmpresaConta;
       |SI enSwSelEmp = 1;
           swBueno = 1;
       |SINO;
           aEmpresa    = enEmpresa; aEmpresa = ("00000" + aEmpresa) % -105;
           |SI enDatos = 1; enActividad = 0;  |HAZ DatosEmpr_w30; |FINSI;
           |HAZ AntCambioCta;
           |SI #107#28 = "S";  |HAZ eBuscoSubdep; |FINSI;
       |FINSI;
   |FINSI;
   |SI swBueno = 1; |ACABA; |FINSI;

   enSub = 0;
   eaTit = "Ac. Cuenta Contable ";
   |SI #107#28 = "S"; |Y eaSubdeT = "S";
       eaTit = " Ac/Sub Cta Contable";
       enSub = 1;
       |SI #41#54 < #0#41; |O #41#54 > #0#42;
           |ACABA;
       |FINSI;
   |FINSI;

   |HAZ LeerLaCuenta;
   |PRINT;
   swCuenta = 1;
|FPRC;

|DEFBUCLE dsmoy047;
   #41#1;
   1,35;                                 || Cuenta Contable, Actividad
   nDEmpresa, #0#2, #0#36, nDActividad, "S";
   nHEmpresa, #0#3, #0#37, nHActividad, "S";
   e;
   NULL, LaImpresion;
|FIN;

|PROCESO imprimir;
      swCuenta  = 0;
      swPrimero = 0;
      swBueno   = 0;
      |BUCLE dsmoy047;
      |SI swCuenta = 1;
          |PIEINF;
      |FINSI;
|FPRC;

|| ....................... Situa la Empresa Contable
|PROCESO LEmpresaConta;
  swerror = 0;
  enSwSinMen = 1;
  enConcreto = 1;
  |HAZ SituaContagen;
  enSwSinMen = 0;
  |SI swerror = 1;    |ACABA;      |FINSI;
  |HAZ LaSelEmpresa2;
  |SI enSwSelEmp = 1; swerror = 1; |FINSI;
  enConcreto = 0;
|FPRC;
