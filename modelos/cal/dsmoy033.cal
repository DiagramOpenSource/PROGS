|FICHEROS;
   dsmoy033 #0;

   dsmom051 #51;  ||Valor Patrimonial
   dsmom052 #52;  ||Socios

   :/basica/def/agicen14 #114;
   :/basica/def/agifigen #118;
   :/basica/def/agim0019 #119;
   :/basica/def/agim0025 #125;
   :/integral/def/dsam0003 #300;

   dsmow030 #900;
|FIN;

|VARIABLES;
   informe     = "";
   nCampo      = 0;
   nDEmpresa   = 0;
   nHEmpresa   = 0;

   nSPin1      = 0;

   aAlfa0 = "";
   aAlfa1 = "";
   aAlfa2 = "";
   aAlfa3 = "";
   nPara1 = 0;
   nPara2 = 0;
   nNume1 = 0;
   nNume2 = 0;
   nNume3 = 0;
   nNume4 = 0;
   nOrden = 0;
   SwHayDatos = 0;

   nCapzac  = 0;
   nImpCap  = 0;
   fLaFecha = @;

   &nPagina  = 0;
   &nPagDup  = 0;
   &nSwPagDup = 0;
   &SwPrim   = 0;

   &eaImpresora = "";
   &eaPortada   = "";
   nOper        = 0;

   aParam       = "";
   &efFechaEmi  = @;
   &enEjercicio = 0;
   Comodin      = "";
   Comodin1     = "";
   aAlfa        = "";
   Handle       = 0;
   Numeracion   = 0;
   &eaNomImp       = "";
|FIN;

|INCLUYE i_variar;
|INCLUYE i_varcon;
|INCLUYE i_varlib;
|INCLUYE dslegv_i;

|| //////////////////// Calculos de Pantalla \\\\\\\\\\\\\\\\\\\\\\\\\\
|CALCULO Eano; |TIPO 7;
 fLaFecha = ~;
 aAlfa1 = fLaFecha; aAlfa1 = aAlfa1 % -104;
 #0#1 = aAlfa1;
 |PINTA #0#1;
|FCAL;

|CALCULO Tipo7;  |TIPO 7;
     |SI enEmpresa ! 0;
         #0#2  = enEmpresa;  |PINTA #0#2;
         #0#3  = enEmpresa;  |PINTA #0#3;

         |C_M #0#2, 1, "N";
         |C_M #0#3, 1, "N";
     |FINSI;
|FCAL;

|CALCULO Parrilla; |TIPO 0;
     |SI #0#2 = 0;
         #0#2 = 0;  |PINTA #0#2;
         #0#3 = 0;  |PINTA #0#3;  |C_M #0#3, 1, "N";
         |PARA nCampo = 4;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |SINO;
         |SI enEmpresa = 0;
             |C_M #0#2, 1, "S";
             |C_M #0#3, 1, "S";
         |FINSI;

         |PARA nCampo = 4;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "N";
               #0nCampo = 0;  |PINTA #0nCampo;
         |SIGUE;
     |FINSI;
|FCAL;

|CALCULO NoMasEmpresas; |TIPO 0;
     |SI #0Campo = 0;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
               #0nCampo = 0;  |C_M #0nCampo, 1, "N";
         |SIGUE;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |FINSI;
|FCAL;

|CALCULO YaSeCual; |TIPO 7;
 |SI enEmpresa ! 0;
     #0#41 = "S"; |PINTA #0#41;
     #0#24 = "S"; |PINTA #0#24;
     #0#25 = 00;  |PINTA #0#25;
     #0#26 = 99;  |PINTA #0#26;
     |C_M #0#24, 1, "N";
     |C_M #0#25, 1, "N";
     |C_M #0#26, 1, "N";
     |C_M #0#41, 1, "N";
 |FINSI;
|FCAL;

|CALCULO SelTipo0; |TIPO 0;
 |SI #0#41 = "N";
     #0#25 = 0;  |PINTA #0#25;  |C_M #0#25, 1, "N";
     #0#26 = 99; |PINTA #0#26;  |C_M #0#26, 1, "N";
     |PARA nCampo = 27;  |SI nCampo [ 36;  |HACIENDO nCampo = nCampo + 1;
           |C_M #0nCampo, 1, "S";
     |SIGUE;
 |SINO;
     |SI enEmpresa = 0;
         |C_M #0#25, 1, "S";
         |C_M #0#26, 1, "S";
     |FINSI;
     |PARA nCampo = 27;  |SI nCampo [ 36;  |HACIENDO nCampo = nCampo + 1;
           |C_M #0nCampo, 1, "N";
           #0nCampo = 0;  |PINTA #0nCampo;
     |SIGUE;
 |FINSI;
|FCAL;

|CALCULO SelTipo1;
 |SI #0Campo = 0;
     |PARA nCampo = Campo + 1;  |SI nCampo [ 36;  |HACIENDO nCampo = nCampo + 1;
           #0nCampo = 0;  |C_M #0nCampo, 1, "N";
     |SIGUE;
 |SINO;
     |PARA nCampo = Campo + 1;  |SI nCampo [ 36;  |HACIENDO nCampo = nCampo + 1;
           |C_M #0nCampo, 1, "S";
     |SIGUE;
 |FINSI;
|FCAL;

|CALCULO Agrupa;
  aAlfa1 = "S";
  |SI #0#54 = "S";
      aAlfa1 = "N";
      #0#37  = 2; |PINTA #0#37;
  |FINSI;
  |C_M #0#37, 1, aAlfa1;
|FCAL;

|| *************************************************************************
|| *************************************************************************
|PROCESO RelSocios0;

  |SI #125#18 > "01.01.1900"; |Y #125#18 > efHFecha;
      |ACABA;
  |FINSI;
  |SI #125#19 > "01.01.1900"; |Y #125#19 < efDFecha;
      |ACABA;
  |FINSI;

  |SI nOper = 1;
      SwHayDatos = 1;
      |ERROR10;
      |ACABA;
  |FINSI;

  |PDEFECTO #0, 42, 54;
  #0#55 = 0;
  #0#56 = "";

  |DDEFECTO #52;

  nOrden = nOrden + 1;
  #52#0 = enEmpresa;
  #52#1 = enEjer;
  #52#2 = nOrden;
  #52#3 = #125#4;   || Nombre
  #52#4 = #125#3;   || NIF
  #52#10= #125#13;  || % Participacion
  #52#5 = #125#14;  || Acciones

  #52#6 = #52#5 * nPvp;
  #52#7 = #52#5 * #51#24;
  #52#8 = #52#5 * nCapzac;

  #52#9 = #52#6; |SI #52#9 < #52#7; #52#9 = #52#7; |FINSI;
                 |SI #52#9 < #52#8; #52#9 = #52#8; |FINSI;


  #0#42  = #52#2;
  #0#43  = #125#3;                       || ... NIF
  #0#44  = #125#4;                       || ... Nombre
  aAlfa1 = #125#5; |QBF aAlfa1;
  aAlfa2 = #125#6; |QBF aAlfa2;
  |SI aAlfa2 ! "";
      aAlfa1 = aAlfa1 + ", " + aAlfa2;
  |FINSI;
  #0#45 = aAlfa1;                        || ... Domicilio
  aAlfa1 = #125#7; |QBF aAlfa1; aAlfa1 = ("00"  + aAlfa1) % -102;
  aAlfa2 = #125#8; |QBF aAlfa2; aAlfa2 = ("000" + aAlfa2) % -103;
  #0#46 = aAlfa1 + aAlfa2;               || ... Codigo Postal
  #0#47 = #125#9;                        || ... Poblacion
  #0#48 = #125#10;                       || ... Provincia
  #0#49 = #125#18;                       || ... Fecha Alta
  #0#50 = #125#19;                       || ... Fecha Baja

  #0#51 = #125#14;                       || ... Participacion
  #0#52 = #125#13;                       || ... % Participacion
  #0#53 = #125#22;                       || ... Nacionalidad
  #0#55 = #125#20;                       || ... Importe
  #0#56 = #125#21;                       || ... Observaciones

  |PRINT;
  SwHayDatos = 1;
|FPRC;

|DEFBUCLE RelSocios1;
  #125#1;
  ;
  enEmpresa, nRegis, 001;
  enEmpresa, nRegis, 999;
  e;
  NULL, RelSocios0;
|FIN;

|DEFBUCLE RelSocios2;
  #125#1;
  ;
  enEmpresa, nRegis, 001;
  enEmpresa, nRegis, 999;
  e;
  NULL, NULL, NULL, NULL, NULL, RelSocios0;
  #125#4, #125#2;
|FIN;

|DEFBUCLE RelSocios3;
  #125#1;
  ;
  enEmpresa, nRegis, 001;
  enEmpresa, nRegis, 999;
  e;
  NULL, NULL, NULL, NULL, NULL, RelSocios0;
  #125#3, #125#2;
|FIN;

|PROCESO LEmpresa;
 enEmpresa  = #agifigen#0;

 eaEstadoEmp = "S";
 |SI #agifigen#94 > "01.01.0000";
     eaEstadoEmp = "N";
     |SI #0#24 = "N"; |ACABA; |FINSI;
 |FINSI;

|| ... Seleccion de Tipo de Empresa
  nSPin1 = #agifigen#87;
  enSwSelEmp = 1;
  |SI #0#41 = "S";
      |SI nSPin1 ] #0#25; |Y nSPin1 [ #0#26;
          enSwSelEmp = 0;
      |FINSI;
  |SINO;
      |SI nSPin1 = #0#27;
          enSwSelEmp = 0;
      |SINO;
          |PARA nCampo = 28;  |SI nCampo [ 36;  |HACIENDO nCampo = nCampo + 1;
                |SI nSPin1 = #0nCampo; |Y nSPin1 ! 0;
                    enSwSelEmp = 0;
                    |SAL;
                |FINSI;
          |SIGUE;
      |FINSI;
  |FINSI;
  |SI enSwSelEmp = 1; |ACABA; |FINSI;
|| ------------------------------------------------------------------------
   aRegis    = "S";
   eDatosW30 = 1;
   enModelo  = #0#39;
   eaTituloL = #0#40; ||Titulo del Libro
   enActividad = 0;
   |HAZ DatosEmpr_w30;
   |SI nRegis = 0; |ACABA; |FINSI;

   |SI enEjerBajaM > 0; |Y enEjerBajaM < #0#1;  ||Disuelta
       |ACABA;
   |FINSI;

   SwHayDatos = 0;
   nOper = 1; |BUCLE RelSocios1;
   |SI SwHayDatos = 0; |ACABA; |FINSI; || No hay informados socios

   nOper = 0;
   eaPortada = "N";
   eaTituloL = #0#40; ||Titulo del Libro

   aAlfa0 = Impresora % 113;
   |SI #0#38 = "S"; |O aAlfa0 = "CrystalReport";
       eaImpresora = Impresora;
       |SI aAlfa0 = "CrystalReport"; eaImpresora = aAlfa0; |FINSI;
       |SUB_EJECUTA_NP ":basica/agiy0016";
       eaImpresora = "";
       |SI aAlfa0 = "CrystalReport"; |HAZ PortadaCrystal; |FINSI;
       eaPortada = #0#38;
   |FINSI;

    |INFOR informe;
    |SI FSalida ! 0; |ACABA; |FINSI;

    |ABRE #51;
    |DDEFECTO #51;
    #51#0 = enEmpresa;
    #51#1 = enEjer;
    |LEE 041#51=;
    |CIERRA #51;

   SwPrim     = 0;
   nOrden     = 0;
   nPagina    = 1;
   nPagDup    = nPagina -1;
   nSwPagDup  = 0;

   SwHayDatos = 0;
   |SI #0#37 = 1; |BUCLE RelSocios1; |FINSI;
   |SI #0#37 = 2; |BUCLE RelSocios2; |FINSI;
   |SI #0#37 = 3; |BUCLE RelSocios3; |FINSI;
   |SI SwHayDatos = 1;
       |PIEINF;
   |FINSI;
   |FININF;

|FPRC;

|DEFBUCLE agifigen;
  #agifigen#1;
  ;
  nDEmpresa;
  nHEmpresa;
  e;
  NULL, LEmpresa;
|FIN;

|| \\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\
|CALCULO Tipo3;  |TIPO 3;
    |HAZ LeeUnidadBasico;

    eaInac = #0#24;

    enEjer = #0#1;
    aAlfa2 = enEjer;
    aAlfa1 = "01.01." + aAlfa2; efDFecha = aAlfa1;
    aAlfa1 = "31.12." + aAlfa2; efHFecha = aAlfa1;

    |SI aParam ! "";
        |SI eaFormato ! "S";
            Impresora = eaNomImp + "?" + eaUnidadBTmp + "\tmp1,,,?Registro Socios";
        |SINO;
            Impresora = eaNomImp + ";" + eaUnidadBTmp + "\Socios.pdf";
        |FINSI;
    |SINO;
        Impresora = "CrystalReports";
    |FINSI;

    |IMPRE -1;
    |SI FSalida ! 0;  |ACABA;  |FINSI;

    informe   = "ccmoy033";
    |SI #0#54 = "S"; informe = "ccmoys33"; |FINSI;
    |SI Impresora ! "CrystalReport"; |Y aParam = "";
        |MENAV "0000 Recuerde seleccionar el papel de la impresora en Horizontal";
        informe   = "dsmoy033";
    |FINSI;

    |SI #0#2 ! 0;
        nDEmpresa = #0#2;
        nHEmpresa = #0#3;
        |BUCLE agifigen;
    |SINO;
        |PARA nPara1 = 4;  |SI nPara1 [ 23;  |HACIENDO nPara1 = nPara1 + 1;
              |SI #0nPara1 ! 0;
                  nDEmpresa = #0nPara1;
                  nHEmpresa = #0nPara1;
                  |BUCLE agifigen;
              |FINSI;
        |SIGUE;
    |FINSI;

    |FINIMP;
|FCAL;


|PROGRAMA;
 aParam = PARAMETRO; |QBF aParam;

 |SI aParam = "";
     |SI enEmpresa ! 0;
         #0#2 = enEmpresa;
         #0#3 = enEmpresa;
         #0#1 = enEjer;
         |HAZ Tipo3;
     |SINO;
         |CLS;
         |MANTE #0;
     |FINSI;
 |SINO;

      || ... Legalia
      #0#1 = enEjercicio;
      #0#2 = eaLegEmp;
      #0#3 = eaLegEmp;

      |PDEFECTO #0,4,42;
      #0#38 = "N";  || sin caratula
      #0#54 = "N";  || sin agrupar
      |HAZ Tipo3;

      |DBASS Comodin1; |QBF Comodin1;
      Comodin1 = Comodin1 + "contagen/PresLibr/" + eaLegEmp + eaLegPer + "/";
      |SI eaFormato ! "S";
          Comodin1 = Comodin1 + "SOCIOS*.xls";
      |SINO;
          Comodin1 = Comodin1 + "SOCIOS*.pdf";
      |FINSI;

      Numeracion = enNumeLeg;
      |_PDIR Comodin1;
      |PARA; |SI FSalida = 0; |HACIENDO;
              Comodin = Comodin1 % -107; Comodin = Comodin % 0103;
              Numeracion = Comodin; Numeracion = Numeracion + 1;
              |_SDIR Comodin1;
      |SIGUE;

      |SI eaFormato ! "S";
          Comodin = eaUnidadBTmp + "/tmp1.xls";
      |SINO;
          Comodin = eaUnidadBTmp + "/Socios.pdf";
      |FINSI;

      |XABRE "C", Comodin, Handle;
      |SI FSalida ] 0;
          |XCIERRA Handle;
          |DBASS Comodin1; |QBF Comodin1;
          Comodin1 = Comodin1 + "contagen/PresLibr/";
          |MKDIR Comodin1;
          Comodin1 = Comodin1 + eaLegEmp + eaLegPer + "/";
          |MKDIR Comodin1;
          |SI eaFormato ! "S";
              Comodin1 = Comodin1 + "SOCIOS_" + (("000" + Numeracion) % -103) + ".xls";
              aNomFLeg = "SOCIOS_" + (("000" + Numeracion) % -103) + ".xls";
          |SINO;
              Comodin1 = Comodin1 + "SOCIOS_" + (("000" + Numeracion) % -103) + ".pdf";
              aNomFLeg = "SOCIOS_" + (("000" + Numeracion) % -103) + ".pdf";
          |FINSI;

          aAlfa = "";
          |IP_REMOTO aAlfa; |QBF aAlfa;
          |SI aAlfa ! "";
              |RECIBE_FICHERO Comodin, Comodin1;
          |SINO;
              |COPIA_FICHERO Comodin, Comodin1;
          |FINSI;
           nTipoL   = 14;
           nNumer   = Numeracion;
           efFecha1 = efDFecha;
           efFecha2 = efHFecha;
           enRegisLeg = 0;
           |HAZ eGrabaHistLegal;

          FSalida = 1;

          |PARA; |SI FSalida ! 0; |HACIENDO;
                 |RFBORRA Comodin;
                 |SI FSalida ! 0;
                     |SI eaFormato ! "S";
                         |MENAV "    Termine la tarea de Excel que tiene activa";
                     |SINO;
                          |MENAV "    Termine la tarea de PDF que tiene activa";
                     |FINSI;
                 |FINSI;
          |SIGUE;
      |FINSI;
 |FINSI;
|FPRO;
|| *************************************************************************
