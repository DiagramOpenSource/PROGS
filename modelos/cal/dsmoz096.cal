|FICHEROS;
     dsmoz096 #96;
     dsmom002 #2;
     dsmom004 #4;

     dsmom093 #93,MANTE;
     dsmom094 #94;

     dsmod131  #131;
     dsmod131@ #1131;

     dsmod130  #130;
     dsmod130@ #1130;

     :/basica/def/agifigen #1000;
     :/basica/def/agicen14 #114;
|FIN;

|VARIABLES;
     nCampo    = 0;

     aParam    = "";
     aBase     = "";
     aMas      = "";
     nDEmpresa = 0;
     nHEmpresa = 0;
     nHPer     = 0;
     nHComple  = 0;

     nSumaD    = 0;
     nSwNeg    = 0;

     nEmpresa  = 0;
     nPeriodo  = 0;
     nEjer     = 0;
     nModelo   = 0;
     nComplem  = 0;
     nImpAnt   = 0;
     nImpNuevo = 0;
     nEl33     = 0;
     nEl23     = 0;
     nImporte  = 0;
     nTope     = 0;
     nTope131  = 0;
     nTopeAgrario = 0;

     fFecha    = @;
     aAlfa     = "";
     aAlfa1    = "";
     errconta  = 0;
     nModo     = 0;
     nInkey    = 0;

     nSwConPro   = 0;
     fFecIni     = @;
     fFecFin     = @;
     aAplica1    = "";
     aAplica2    = "";
     nAplica3    = 0;
     nNoModi     = 0;
     nNume1      = 0;
     nNume2      = 0;
     nSuma       = 0;

     nRend_NoAgr  = 0;
     nRend_Agr    = 0;
     nRendimiento = 0;
     nTopeHM      = 0;
     nTopeM       = 0;
     nTopeV       = 0;
     nTopeV0      = 0;
     nTopeV1      = 0;
     nTotalM      = 0;
     nTotalV      = 0;
     nApli400     = 0;

     nPagAnt      = 0;
     &enElEjer    = 0;
     &enAlta      = 0;

     &enSwApli     = 0;
     &enModeloAp    = 0;
     &enEmpresaAp   = 0;
     &eaNombreAp    = "";
     &enEjerAp      = 0;
     &enImporte     = 0;
|FIN;

|INCLUYE i_variar;

|| ////////////////////////////////////////////////////////////////////////
|PROCESO Leodsmom094_z96;
   nTotalM = nTotalM + #94#8;
   nTotalV = nTotalV + #94#9;
|FPRC;

|DEFBUCLE Leodsmom094_z96;
   #94#1;
   10;
   nEjer, nEmpresa, 01, 01;
   nEjer, nEmpresa, 99, nPeriodo;
   e;
   NULL, Leodsmom094_z96;
|FIN;

|PROCESO CambioAplicar;

     nApli400 = 100;
     aAplica1 = #93#4;
     aAplica2 = #93#5;
     nAplica3 = #93#14; |SI #93#1 < 2011; nAplica3 = 0; |FINSI;

     nTopeHM  = 8000;
     nTopeM   = 12000;
     nTopeV   = 33007.20;
     nTopeV0  = 33007.20;
     nTopeV1  = 22000.00;

|| ......... Si no desea aplicar ni uno ni el otro, acaba ya.
     |SI aAplica1 = "N"; |Y aAplica2 = "N"; |ACABA; |FINSI;

     |SI nPeriodo =  3; nNume1 = 6; nNume2 = 10; |FINSI;
     |SI nPeriodo =  6; nNume1 = 7; nNume2 = 11; |FINSI;
     |SI nPeriodo =  9; nNume1 = 8; nNume2 = 12; |FINSI;
     |SI nPeriodo = 12; nNume1 = 9; nNume2 = 13; |FINSI;

     nTotalM = 0;
     nTotalV = 0;
     |BUCLE Leodsmom094_z96;  || Sumo actividades

     |SI aAplica1 = "S";        || Calculo para Minoracion

         |SI #93#1 < 2015;
             nApli400 = 100;
             nRendimiento = (nRend_NoAgr + (nRend_Agr * 0.25)) ! 2;
             |SI #93nNume1 ! "X";   || Si no fuerzo a Deducir S
                 |SI nRendimiento > nTopeM; |O nTotalM > nTopeM;
                     aAplica1 = "N";
                 |SINO;
                     |SI nTotalM > nTopeHM;
                         nApli400 = (( 400 - (nTotalM - nTopeHM) * 0.1) / 4) ! 2;
                     |FINSI;
                 |FINSI;
             |FINSI;
         |SINO;
             nApli400 = 0;
             |SI #93#15 [ 9000;                     nApli400 = 100; |FINSI;
             |SI #93#15 > 9000;  |Y #93#15 [ 10000; nApli400 =  75; |FINSI;
             |SI #93#15 > 10000; |Y #93#15 [ 11000; nApli400 =  50; |FINSI;
             |SI #93#15 > 11000; |Y #93#15 [ 12000; nApli400 =  25; |FINSI;
         |FINSI;
     |FINSI;

     nTopeV = nTopeV0;
     |SI nAplica3 = 1; nTopeV = nTopeV1; |FINSI;
     |SI aAplica2 = "S";  || Calculo para Minoracion
         |SI #93nNume2 ! "X";
             |SI nTotalV > nTopeV;
                 aAplica2 = "N";
              |FINSI;
         |FINSI;
     |FINSI;
|FPRC;

|| ////////////////////////////////////////////////////////////////////////

|PROCESO Lee_m004;
     nNoModi = 0;
     #4#0 = nEmpresa;
     #4#1 = nEjer;
     #4#2 = nPeriodo;
     #4#3 = nModelo;
     #4#4 = nComplem;
     |LEE 101#4=;
     |SI FSalida = 0;
         |SI #4#9 = "X"; || Esta Impreso
             nNoModi = 1;
         |FINSI;
         |SI #4#11 = "X"; || Modelo Remesado No modificar
             nNoModi = 1;
         |FINSI;
         |SI #4#13 = "X"; || Modelo ya Archivado. No se puede Modificar
             nNoModi = 1;
         |FINSI;
     |FINSI;
     |LIBERA #4;
|FPRC;

|PROCESO Actual_m004;
     #4#0 = nEmpresa;
     #4#1 = nEjer;
     #4#2 = nPeriodo;
     #4#3 = nModelo;
     #4#4 = nComplem;
     |LEE 101#4=;
     |SI FSalida = 0;
         #4#15 = #4#15 - nImpAnt + nImpNuevo;
         |GRABA 020#4a;
     |FINSI;
     |LIBERA #4;
|FPRC;

|PROCESO NoModificar;
  |SI #4#9  = "X"; nNoModi = 1; |FINSI;
  |SI #4#11 = "X"; nNoModi = 1; |FINSI;
  |SI #4#13 = "X"; nNoModi = 1; |FINSI;
|FPRC;

|DEFBUCLE NoModificar;
 #4#1;
 ;
 #93#2, #93#1, 01, #93#0, 00;
 #93#2, #93#1, 99, #93#0, 99;
 ;
 NULL, NoModificar;
|FIN;

|| ----------------------------------------------------------------------
|PROCESO dsmod131_neg;

     |SI #1131#33 < 0; nSwNeg = 1; |FINSI;

     |SI #1131#33 ] 0;  |Y nSwNeg = 0; |ACABA;  |FINSI;

     nSumaD = nSumaD + #1131#38;

     |SI #1131#33 < 0;
         #131#39 = #131#39 + -#1131#33;
     |SINO;
         #131#39 = #131#39 - #1131#38;
         |SI #131#39 [ 0; nSwNeg = 0; #131#39 = 0; nSumaD = 0; |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE dsmod131_neg;
     #1131#1006;
     ;
     #131#0, #131#1,     3, #131#3,  0, #131#5;
     #131#0, #131#1, nHPer, #131#3, 99, #131#5;
     ;
     NULL, dsmod131_neg;
|FIN;

|PROCESO CapturaNegativos131;
     |SI #131#38 [ 0;  |ACABA;  |FINSI;

     |DBASE aBase;  |QBF aBase;
     aMas = aBase + "def/dsmod131.mas";
     |CARGA_DEF aMas, dsmod131@;

     |ABRE #dsmod131@;
     nSwNeg   = 0;
     nSumaD   = 0;
     #131#39  = 0;
     nHPer    = #131#2 - 3;
     |PARA nNume1 = 3; |SI nNume1 [ nHPer; |HACIENDO nNume1 = nNume1 + 3;
           |DDEFECTO #1131;
           #1131#0 = #131#0;
           #1131#1 = #131#1;
           #1131#2 = nNume1;
           #1131#3 = #131#3;
           #1131#4 = 99;
           #1131#5 = #131#5;
           |LEE 040#1131];
           |SI FSalida = 0;
               |LEE 040#1131a;
           |SINO;
               |LEE 040#1131u;
           |FINSI;
           |SI FSalida = 0; |Y #1131#0 = #131#0;|Y #1131#1 = #131#1;
                       |Y #1131#2 = nNume1; |Y #1131#3 = #131#3;
               |HAZ dsmod131_neg;
           |FINSI;
     |SIGUE;
     |SI #131#39 > #131#38;  #131#39 = #131#38;  |FINSI;
     |CIERRA #dsmod131@;
     |DESCARGA_DEF #dsmod131@;
|FPRC;

|PROCESO dsmod131_2;
     |SI #1131#33 > 0;
         #131#41 = #131#41 + #1131#33;
     |FINSI;
|FPRC;

|DEFBUCLE dsmod131_2;
     #1131#1006;
     ;
     #131#0, #131#1, #131#2, #131#3,        0, #131#5;
     #131#0, #131#1, #131#2, #131#3, nHComple, #131#5;
     ;
     NULL, dsmod131_2;
|FIN;

|PROCESO CapturaOtros131;
     |DBASE aBase;  |QBF aBase;
     aMas = aBase + "def/dsmod131.mas";
     |CARGA_DEF aMas, dsmod131@;

     nHComple = #131#4 - 1;
     #131#41  = 0;

     |BUCLE dsmod131_2;

     |DESCARGA_DEF #dsmod131@;
|FPRC;

|PROCESO CapturaTope131;
     |DBASE aBase;  |QBF aBase;
     aMas = aBase + "def/dsmod131.mas";
     |CARGA_DEF aMas, dsmod131@;

     |ABRE #dsmod131@;
     nTope131 = 0;
     nHPer    = #131#2 - 3;
     |PARA nNume1 = 3; |SI nNume1 [ nHPer; |HACIENDO nNume1 = nNume1 + 3;
           |DDEFECTO #1131;
           #1131#0 = #131#0;
           #1131#1 = #131#1;
           #1131#2 = nNume1;
           #1131#3 = #131#3;
           #1131#4 = 99;
           #1131#5 = #131#5;
           |LEE 040#1131];
           |SI FSalida = 0;
               |LEE 040#1131a;
           |SINO;
               |LEE 040#1131u;
           |FINSI;
           |SI FSalida = 0; |Y #1131#0 = #131#0;|Y #1131#1 = #131#1;
                       |Y #1131#2 = nNume1; |Y #1131#3 = #131#3;
               nTope131 = nTope131 + #1131#44;
           |FINSI;
     |SIGUE;

     |CIERRA #dsmod131@;
     |DESCARGA_DEF #dsmod131@;
|FPRC;

|PROCESO CalculaDed131;
     #131#39 = 0;
     #131#38 = #131#36 - #131#37 - #131#42;
     |SI #131#38 > 0;
         |SI #131#2 ! 3;
             |HAZ CapturaNegativos131;
         |FINSI;
     |SINO;
         #131#39 = 0;
         #131#44 = 0;
         |ACABA;
     |FINSI;

     nSuma = #131#39 + #131#44;
     |SI nSuma > #131#38;
         |SI #131#39 > #131#38;
             #131#39 = #131#38;
             #131#44 = 0;
             |ACABA;
         |FINSI;

         #131#44 = #131#38 - #131#39;
     |FINSI;
|FPRC;

|PROCESO RecalModelo_131;
     nEmpresa  = #131#0;
     nEjer     = #131#1;
     nPeriodo  = #131#2;
     nComplem  = #131#4;

     |HAZ Lee_m004; |SI nNoModi = 1; |ACABA; |FINSI;

     nRend_NoAgr = #131#43; nRend_Agr = #131#29;
     |SI #131#1 ] 2010;  |HAZ CambioAplicar; |FINSI;

     #131#26 = #131#9  + #131#13 + #131#17 + #131#21 + #131#25;
     #131#43 = #131#7  + #131#11 + #131#15 + #131#19 + #131#23;

     nImpAnt = #131#33;
     #131#41 = 0;

     #131#42 = 0;
     |SI #131#1 = 2008; |Y aAplica1 ! "N";
         |SI #131#2 = 6;   #131#42 = 200;  |FINSI;
         |SI #131#2 = 9;   #131#42 = 100;  |FINSI;
         |SI #131#2 = 12;  #131#42 = 100;  |FINSI;
     |FINSI;

     |SI #131#1 = 2009; |Y aAplica1 ! "N";
         |SI #131#2 = 3;   #131#42 = 100;  |FINSI;
         |SI #131#2 = 6;   #131#42 = 100;  |FINSI;
         |SI #131#2 = 9;   #131#42 = 100;  |FINSI;
         |SI #131#2 = 12;  #131#42 = 100;  |FINSI;
     |FINSI;

     |SI #131#1 ] 2010; |Y aAplica1 ! "N";
         #131#42 = nApli400;
     |FINSI;

     #131#44 = 0;
     |SI #131#1 ] 2009; |Y aAplica2 = "S";
         |SI #131#26 ! 0;
             #131#44 = (#131#43 % .5) + (#131#34 % 2);
         |SINO;
             #131#44 = #131#29 % 2;
         |FINSI;

         |HAZ CapturaTope131;

         |SI nAplica3 = 0;
             nTope = 660.14 - nTope131;
         |SINO;
             nTope = 440 - nTope131;
         |FINSI;
         |SI nTope < 0;  nTope = 0;  |FINSI;
         |SI #131#44 > nTope;
             #131#44 = nTope;
         |FINSI;
     |FINSI;

     #131#36 = #131#26 + #131#35 + #131#30;
     #131#37 = #131#27 + #131#31;

     |HAZ CalculaDed131;

     #131#40 = #131#38 - #131#39 - #131#44;

     nEl33 = #131#40;
     |SI #131#4 > 0;
         |HAZ CapturaOtros131;
         nEl33 = nEl33 - #131#41;
     |FINSI;

     #131#33 = #131#40 - #131#41;
     |GRABA 020#131a;
     |LIBERA #131;

     nImpNuevo = #131#33;
     |HAZ Actual_m004;
|FPRC;

|DEFBUCLE dsmod131;
    #131#1;
    ;
    nEmpresa, nEjer, 01, nModelo, 00, 00;
    nEmpresa, nEjer, 12, nModelo, 99, 99;
    ;
    NULL, RecalModelo_131;
|FIN;

|| ---- 130
|PROCESO dsmod130_neg;

     |SI #1130#1 = 2008;  |Y #1130#2 [ 3;
          nImporte = #1130#17 - #1130#18;
     |SINO;
          nImporte = #1130#23;
     |FINSI;

     |SI nImporte < 0; nSwNeg = 1; |FINSI;

     |SI nImporte ] 0;  |Y nSwNeg = 0; |ACABA;  |FINSI;

     nSumaD = nSumaD + #1130#20;

     |SI nImporte < 0;
         #130#21 = #130#21 + -nImporte;
     |SINO;
         #130#21 = #130#21 - #1130#20;
         |SI #130#21 [ 0; nSwNeg = 0; #130#21 = 0; nSumaD = 0; |FINSI;
     |FINSI;
|FPRC;

|PROCESO CapturaNegativos130;
     |SI #130#20 [ 0;  |ACABA;  |FINSI;

     |DBASE aBase;  |QBF aBase;
     aMas = aBase + "def/dsmod130.mas";
     |CARGA_DEF aMas, dsmod130@;

     |ABRE #dsmod130@;
     nSwNeg   = 0;
     nSumaD   = 0;
     #130#21  = 0;
     nHPer    = #130#2 - 3;
     |PARA nNume1 = 3; |SI nNume1 [ nHPer; |HACIENDO nNume1 = nNume1 + 3;
           |DDEFECTO #1130;
           #1130#0 = #130#0;
           #1130#1 = #130#1;
           #1130#2 = nNume1;
           #1130#3 = #130#3;
           #1130#4 = 99;
           #1130#5 = #130#5;
           |LEE 040#1130];
           |SI FSalida = 0;
               |LEE 040#1130a;
           |SINO;
               |LEE 040#1130u;
           |FINSI;
           |SI FSalida = 0; |Y #1130#0 = #130#0;|Y #1130#1 = #130#1;
                       |Y #1130#2 = nNume1; |Y #1130#3 = #130#3;
               |HAZ dsmod130_neg;
           |FINSI;
     |SIGUE;
     |SI #130#21 > #130#20;  #130#21 = #130#20;  |FINSI;
     |CIERRA #dsmod130@;

     |DESCARGA_DEF #dsmod130@;
|FPRC;

|PROCESO CapturaTopeAgrario;
     |DBASE aBase;  |QBF aBase;
     aMas = aBase + "def/dsmod130.mas";
     |CARGA_DEF aMas, dsmod130@;

     |ABRE #dsmod130@;
     nTopeAgrario = 0;
     nHPer        = #130#2 - 3;
     |PARA nNume1 = 3; |SI nNume1 [ nHPer; |HACIENDO nNume1 = nNume1 + 3;
           |DDEFECTO #1130;
           #1130#0 = #130#0;
           #1130#1 = #130#1;
           #1130#2 = nNume1;
           #1130#3 = #130#3;
           #1130#4 = 99;
           #1130#5 = #130#5;
           |LEE 040#1130];
           |SI FSalida = 0;
               |LEE 040#1130a;
           |SINO;
               |LEE 040#1130u;
           |FINSI;
           |SI FSalida = 0; |Y #1130#0 = #130#0; |Y #1130#1 = #130#1;
                            |Y #1130#2 = nNume1; |Y #1130#3 = #130#3;
               nTopeAgrario = nTopeAgrario + #1130#24;
           |FINSI;
     |SIGUE;

     |CIERRA #dsmod130@;
     |DESCARGA_DEF #dsmod130@;
|FPRC;

|PROCESO dsmod130_2;
     |SI #1130#1 = 2008;  |Y #1130#2 [ 3;
          nImporte = #1130#17 - #1130#18;
     |SINO;
          nImporte = #1130#23;
     |FINSI;
     |SI nImporte < 0; nImporte = 0; |FINSI;
     #130#18 = #130#18 + nImporte;
|FPRC;

|DEFBUCLE dsmod130_2;
     #1130#1006;
     ;
     #130#0, #130#1, #130#2, #130#3,        0, #130#5;
     #130#0, #130#1, #130#2, #130#3, nHComple, #130#5;
     ;
     NULL, dsmod130_2;
|FIN;

|PROCESO CapturaOtros130;
     |DBASE aBase;  |QBF aBase;
     aMas = aBase + "def/dsmod130.mas";
     |CARGA_DEF aMas, dsmod130@;

     nHComple = #130#4 - 1;
     #130#18  = 0;

     |BUCLE dsmod130_2;

     |DESCARGA_DEF #dsmod130@;
|FPRC;

|PROCESO CalculaDed130;
     #130#20 = #130#17 - #130#19;
     |SI #130#20 > 0;
         |SI #130#2 ! 3;
             |HAZ CapturaNegativos130;
         |FINSI;
     |SINO;
         #130#21 = 0;
         #130#24 = 0;
     |FINSI;

     |SI #130#20 [ 0;
         #130#21 = 0;
         #130#24 = 0;
         |ACABA;
     |FINSI;

     nSuma = #130#21 + #130#24;
     |SI nSuma > #130#20;
         |SI #130#21 > #130#20;
             #130#21 = #130#20;
             #130#24 = 0;
             |ACABA;
         |FINSI;

         #130#24 = #130#20 - #130#21;
     |FINSI;
|FPRC;

|PROCESO RecalModelo_130;
     nEmpresa  = #130#0;
     nEjer     = #130#1;
     nPeriodo  = #130#2;
     nComplem  = #130#4;

     |HAZ Lee_m004; |SI nNoModi = 1; |ACABA; |FINSI;

     nRend_NoAgr = #130#8; nRend_Agr = #130#13;
     |SI #130#1 ] 2010;  |HAZ CambioAplicar; |FINSI;

     #130#10 = nPagAnt;
     #130#12 = #130#9 - #130#10 - #130#11;
     |SI #130#12 < 0;  #130#12 = 0;  |FINSI;
     #130#17 = #130#12 + #130#16;

     #130#19 = 0;
     |SI #130#1 = 2008; |Y aAplica1 ! "N";
         |SI #130#2 = 6;   #130#19 = 200;  |FINSI;
         |SI #130#2 = 9;   #130#19 = 100;  |FINSI;
         |SI #130#2 = 12;  #130#19 = 100;  |FINSI;
     |FINSI;

     |SI #130#1 = 2009; |Y aAplica1 ! "N";
         |SI #130#2 = 3;   #130#19 = 100;  |FINSI;
         |SI #130#2 = 6;   #130#19 = 100;  |FINSI;
         |SI #130#2 = 9;   #130#19 = 100;  |FINSI;
         |SI #130#2 = 12;  #130#19 = 100;  |FINSI;
     |FINSI;

     |SI #130#1 ] 2010; |Y aAplica1 ! "N";
         #130#19 = nApli400;
     |FINSI;

     #130#24 = 0;
     |SI #130#1 ] 2009; |Y aAplica2 = "S";
         |SI #130#8 > 0;
             #130#24 = #130#8 % 2;
             |SI nAplica3 = 0;
                 |SI #130#24 > 660.14;
                     #130#24 = 660.14;
                 |FINSI;
             |SINO;
                 |SI #130#24 > 440;
                     #130#24 = 440;
                 |FINSI;
             |FINSI;
         |SINO;
             |SI #130#13 > 0;
                 #130#24 = #130#13 % 2;
                 |HAZ CapturaTopeAgrario;
                 |SI nAplica3 = 0;
                     nTope = 660.14 - nTopeAgrario;
                 |SINO;
                     nTope = 440 - nTopeAgrario;
                 |FINSI;
                 |SI nTope < 0;  nTope = 0;  |FINSI;
                 |SI #130#24 > nTope;
                     #130#24 = nTope;
                 |FINSI;
             |FINSI;
         |FINSI;
     |FINSI;

     nImpAnt   = #130#17 - #130#18;
     |SI #130#1 ] 2008;
         |SI #130#1 > 2008; |O #130#2 ] 4;
             |SI #130#20 ! 0; |O #130#21 ! 0; |O #130#22 ! 0; |O #130#23 ! 0;
                 nImpAnt = #130#23;  ||ya estaba calculado
             |FINSI;
         |FINSI;
     |FINSI;

     |HAZ CalculaDed130;

     #130#22 = #130#20 - #130#21 - #130#24;

     #130#18 = 0;
     nEl23 = #130#22;
     |SI #130#4 > 0;
         |HAZ CapturaOtros130;
         nEl23 = nEl23 - #130#18;
     |FINSI;
     #130#23 = #130#22 - #130#18;

     |GRABA 020#130a;
     |LIBERA #130;

     nImpNuevo = #130#23;
     |HAZ Actual_m004;

     nPagAnt = #130#10;
     |SI #130#1 [ 2008;
         |SI #130#12 > 0; nPagAnt = nPagAnt + #130#12; |FINSI;
         |SI nPagAnt < 0; nPagAnt = 0; |FINSI;
     |SINO;
         |SI #130#12 > 0; nPagAnt = nPagAnt + #130#12; |FINSI;
                          nPagAnt = nPagAnt - #130#24;
     |FINSI;
|FPRC;

|DEFBUCLE dsmod130;
    #130#1;
    ;
    nEmpresa, nEjer, 01, nModelo, 00, 00;
    nEmpresa, nEjer, 12, nModelo, 99, 99;
    ;
    NULL, RecalModelo_130;
|FIN;

|PROCESO Actualiza;
    nModelo   = #93#0;
    nEjer     = #93#1;
    nEmpresa  = #93#2;
    nPagAnt   = 0;
    |ABRE #4;
    |SI nModelo = 130; |BUCLE dsmod130; |FINSI;
    |SI nModelo = 131; |BUCLE dsmod131; |FINSI;
    |CIERRA #4;
|FPRC;

|CALCULO Tipo1_93; |TIPO 1;
   nModo = (FEntrada / 100) ! 0;
   |SI nModo = 3;
       |SI #96#1 < 2015;
           |CONFI "    SConfirmacion Baja  (Aplicar Minoracion) ";
           |SI FSalida ! 0;
               |ERROR;
               |ACABA;
           |FINSI;

           #93#4 = "S"; #93#6 = ""; #93#7 = ""; #93#8 = ""; #93#9 = "";
           #93#5 = "N";

           aAplica1   = #93#4;
           aAplica2   = #93#5;
           nAplica3   = #93#14;
           |HAZ Actualiza;
       |SINO;
           |MENAV "0000No debe dar de Baja, para el nuevo calculo necesita el Rend.E.Anterior";
           |CONFI "0000NConfirma la Baja ";
           |SI FSalida ! 0;
               |ERROR;
               |ACABA;
           |FINSI;
           #93#4 = "S"; #93#6 = ""; #93#7 = ""; #93#8 = ""; #93#9 = "";
           #93#5 = "N"; #93#15 = 0;

           aAplica1   = #93#4;
           aAplica2   = #93#5;
           nAplica3   = #93#14; |SI #93#1 < 11; nAplica3 = 0; |FINSI;
           |HAZ Actualiza;
       |FINSI;
   |FINSI;
|FCAL;

|CALCULO Aplicar; |TIPO 3;
     aAplica1   = #93#4;
     aAplica2   = #93#5;
     nAplica3   = #93#14;
     |HAZ Actualiza;
|FCAL;

|CALCULO Tipo7C4F93; |TIPO 7;
   aAlfa1 = "0000Aplicar la Minoracion Articulo 110.3.c del Reglamento";
   |SI #93#1 < 2014;
       aAlfa1 = "0000Aplicar la Minoracion articulo 80 de la Ley del Impuesto";
   |FINSI;
   |MENSA aAlfa1;
|FCAL;

|CALCULO SiMino; |TIPO 0;
     |BLIN 4;
     aAlfa1 = #93#4;
     |C_M #93#6, 1, aAlfa1;
     |C_M #93#7, 1, aAlfa1;
     |C_M #93#8, 1, aAlfa1;
     |C_M #93#9, 1, aAlfa1;
     |SI #93#4 = "N";
         #93#6 = " "; |PINTA #93#6;
         #93#7 = " "; |PINTA #93#7;
         #93#8 = " "; |PINTA #93#8;
         #93#9 = " "; |PINTA #93#9;
     |FINSI;
|FCAL;

|CALCULO SiVivi; |TIPO 0;
     aAlfa1 = #93#5;
     |C_M #93#10, 1, aAlfa1;
     |C_M #93#11, 1, aAlfa1;
     |C_M #93#12, 1, aAlfa1;
     |C_M #93#13, 1, aAlfa1;
     |C_M #93#14, 1, aAlfa1;
     |SI #93#5 = "N";
         #93#10 = " "; |PINTA #93#10;
         #93#11 = " "; |PINTA #93#11;
         #93#12 = " "; |PINTA #93#12;
         #93#13 = " "; |PINTA #93#13;
     |FINSI;
     |SI #96#1 < 2011;  |C_M #93#14,1,"N"; |FINSI;
|FCAL;


|CALCULO RecojoAuto; |TIPO 0;
     |C_M #93#15,0,aAlfa1;
     |SI aAlfa1 = "N"; |ACABA; |FINSI;

     nInkey = FSalida;
     |SI nInkey = 15;
         enSwApli = 3;
         enEmpresaAp = #93#2;
         enEjerAp    = #93#1;
         enModeloAp  = #93#0;
         |HAZ eGrabadsmom093;
         |PINTA #93#15;
         |ERROR;
     |FINSI;
|FCAL;

|| ***************************************************************************
|| \\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\
|| .... Mirar si hace el modelo de IRPF
|PROCESO MiraSiD;
 |SI #2#8 > "01.01.0000";
     |SI #2#8 > fFecFin;  |ACABA;  |FINSI;
 |FINSI;
 |SI #2#9 > "01.01.0000";
     |SI #2#9 < fFecIni;  |ACABA;  |FINSI;
 |FINSI;
 nSwConPro = 0;
 |ERROR10;
|FPRC;

|DEFBUCLE MiraSiModelo;
 #2#1;
 ;
 #93#2, 01, #93#0;
 #93#2, 99, #93#0;
 ;
 NULL, MiraSiD;
|FIN;

|PROCESO LeeEmpresa;
  || Lee Empresa
    errconta = 0;
    |ABRE #1000;
    #1000#0 = nEmpresa;
    |LEE 041#1000=;
    |SI FSalida ! 0;
        errconta = 1;
    |FINSI;
    eaNombreEmp = #1000#1;
    |CIERRA #1000;
|FPRC;

|PROCESO Tipo11_93; |TIPO 11;
 nInkey = FSalida;
 |SI nInkey = 10;
     |ERROR;
     eaElProm = "dsmom093";
     |HAZ ConsuCliente7;
     eaElProm = "";
     |PTEC 802;
     |ACABA;
 |FINSI;
|FPRC;

|PROCESO HazBorrar;
  nModo = (FEntrada / 100) ! 0;
  |SI nModo = 2;
      |LEE 041#93=;

      |BORRA 020#93a;
      |LIBERA #93;
      |PONCONTROL  23, "SI";
  |FINSI;
  |ERROR;
  |PINTA #93#2;
|FPRC;

|PROCESO Tipo0_93; |TIPO 0;

    nEmpresa = #93#2;
    |HAZ LeeEmpresa;
    |SI errconta = 1;
        |MENAV "    No existe Empresa ";
        |HAZ HazBorrar;
        |ACABA;
    |FINSI;
    #93#3 = #1000#1; |PINTA #93#3;

    nSwConPro = -1;
    |BUCLE MiraSiModelo;
    |SI nSwConPro = -1;
        aAlfa1 = "    Empresa sin Modelo " + #93#0 + " en el Planing";
        |MENAV aAlfa1;
        |HAZ HazBorrar;
        |ACABA;
    |FINSI;

    aCoBien = "N";
    |ABRE #114;
    #114#0 = nEmpresa;
    #114#1 = 00;
    |LEE 041#114];
    |SI FSalida = 0; |Y #114#0 = nEmpresa;
        aCoBien = #114#13;  ||Comunidad de Bienes
    |FINSI;
    |CIERRA #114;
    |SI aCoBien = "S";
        aAlfa1 = "    Empresa COMUNIDAD DE BIENES       ";
        |MENAV aAlfa1;
        |HAZ HazBorrar;
        |ACABA;
    |FINSI;

     nNoModi = 0;
     |BUCLE NoModificar;
     |SI nNoModi = 1;
         |MENAV "    Empresa con Modelos Remesados o Archivados, no puede modificarlos";
         |ERROR;
    |FINSI;
|FPRC;

|| **************************************************************************
|PROCESO ClaveBaja;
 #93#0 = #96#0;
 #93#1 = #96#1;
 #93#2 = 1;
|FPRC;

|PROCESO ClaveAlta;
 #93#0 = #96#0;
 #93#1 = #96#1;
 #93#2 = 99999;
|FPRC;

|PROCESO AntesPantalla;
   |SI #96#1 < 2015;
       aAlfa = "Art.80 de la Ley bis del impuesto ";
       |ATRI R; |PINTA 0673, "       "; |ATRI 0;
   |SINO;
       aAlfa = "Art.110.3.c. del Reglamento ";
       |PINTA 0673, "       ";
   |FINSI;
   |PINTA 0545, "                                  ";
   |ATRI R; |PINTA 0545, aAlfa; |ATRI 0;
|FPRC;

|CALCULO Tipo7C0F96; |TIPO 7;
  |SI #96#1 ] 2015;
      aAlfa1 = "0000<F2> Copiar Configuracion Empresas a Nuevo Ejer., <F3> Recalcular Rend.Ant."
  |SINO;
      aAlfa1 = "0000<F2> Copiar Configuracion Empresas a Nuevo Ejercicio";
  |FINSI;
  |MENSA aAlfa1;
  |HAZ AntesPantalla;
|FCAL;

|CALCULO SiCopio; |TIPO 0;
  |BLIN 4;
  |HAZ AntesPantalla;
  |SI FSalida = 10;
      |ERROR;
      enAlta   = 0;
      enElEjer = #96#1;
      aAlfa1   = "dsmoz098;" + aParam;
      |SUB_EJECUTA_NP aAlfa1;
      |SI enElEjer ! 0;
          #96#1 = enElEjer;
          |PTEC 802;
      |FINSI;
  |FINSI;
  |SI FSalida = 11; |Y #96#1 ] 2015;
      |ERROR;
      enAlta   = 1;
      enElEjer = #96#1;
      aAlfa1 = "dsmoz098;" + aParam;
      |SUB_EJECUTA_NP aAlfa1;
      |SI enElEjer ! 0;
          #96#1 = enElEjer;
          |PTEC 802;
      |FINSI;
  |FINSI;
|FCAL;

|CALCULO Tipo12; |TIPO 12;
     aAlfa1 = #96#1;
     aAlfa1 = "01.01." + aAlfa1; fFecIni = aAlfa1;
     aAlfa1 = #96#1;
     aAlfa1 = "31.12." + aAlfa1; fFecFin = aAlfa1;

     |C_V #93#5, 1, "S";
     |SI #96#1 [ 2008; |C_V #93#5, 1, "N"; |FINSI;

     |SI #96#1 [ 2009;
         |PINPA #0#96;
         |PINDA #0#96;
         |C_V #93#6,  1, "N"; |C_V #93#7,  1, "N";
         |C_V #93#8,  1, "N"; |C_V #93#9,  1, "N";
         |C_V #93#10, 1, "N"; |C_V #93#11, 1, "N";
         |C_V #93#12, 1, "N"; |C_V #93#13, 1, "N";
         |C_V #93#14, 1, "N";
         |C_P #93#2,1,1205;
         |C_P #93#3,1,1211;
         |C_P #93#4,1,1258;
         |C_P #93#5,1,1268;
     |FINSI;

     |C_M #93#14,1,"S";
     |SI #96#1 < 2011;
         |C_M #93#14,1,"N";
     |FINSI;

     |C_A #93#3,1,34;
     |C_V #93#15,1,"S"; |C_M #93#15,1,"S";
     |SI #96#1 < 2015;
         |C_V #93#15,1,"N"; |C_M #93#15,1,"N";
         |C_A #93#3,1,80;
         |SI #96#1 ] 2009;
             |ATRI R; |PINTA 1042, "            ";
             |PINTA 1142, "            "; |ATRI 0;
             |PINTA 1242, "            ";
             |PINTA 1342, "            ";
             |PINTA 1442, "            ";
             |PINTA 1542, "            ";
             |PINTA 1642, "            ";
             |PINTA 1742, "            ";
             |PINTA 1842, "            ";
             |PINTA 1942, "            ";
             |PINTA 2042, "            ";
             |PINTA 2142, "Ä";
         |FINSI;
     |FINSI;

     |ENTLINEAL #1#93, -3, 1, 20, 0, ClaveBaja, ClaveAlta;
|FCAL;

|PROGRAMA;
     aParam = PARAMETRO; |QBF aParam;
     |SI aParam = ""; aParam = "130"; |FINSI;

     |CLS;
     |CABEZA "Empresas sin Aplicar Minoracion y 2% prestamo";
     |PINPA #0#93;
     #96#0 = aParam;

     fFecha = ~;
     aAlfa = fFecha;
     aAlfa = aAlfa % -104;
     #96#1 = aAlfa;
     |PINDA #0#96;

     |ENDATOS #1#96;
|FPRO;
