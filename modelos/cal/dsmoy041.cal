t|FICHEROS;
   dsmoy041 #0;

   dsmom041 #41;
   dsmom042 #42;
   dsmom040 #40;

   dsmom100 #100;
   dsmom107 #107;

   :/basica/def/agicen14 #114;
   :/basica/def/agim0019 #119;

   :/contagen/def/canempre #300;
   :/contagen/def/camaniva #304;

   dsmow030 #900;
|FIN;

|VARIABLES;
   &entrada = 1;
   nSwOp = 0;
   aAlfa1     = "";
   aAlfa2     = "";
   informe    = "";
   inform1    = "dsmoy041";
   inform2    = "dsmoyn41";
   nNume1     = 0;

   &eaDElem   = "";
   &eaHElem   = "";
   &enDNEle   = 0;
   &enHNEle   = 0;
   &tipo      = "";
   &situa     = "";
   &tiamoc    = "";
   &tiamo     = "";
   &taNuevo   = "";
   &tCampo55  = "";
   &tCampo56  = "";
   &tCampo58  = "";
   &valor     = 0;
   &amorc     = 0;
   &amor      = 0;
   &resid     = 0;
   &residc    = 0;
   &cta1      = "";
   &cta2      = "";
   &cta108    = "";
   &cta208    = "";
   &am_ap     = "";
   &laana     = "";
   &aCta      = "";
   &aAna      = "";

   &cta1_of   = "";
   &cta2_of   = "";
   &cta1_of8  = "";
   &cta2_of8  = "";
   &curso1    = "";
   &curso2    = "";
   &cliente   = "";
   &impv      = 0;
   &eaBaja    = "";
   ulti       = "";
   ene        = "N";
   nueva      = "NUEVA";
   usada      = "USADA";
   norma      = "NORMAL";
   acele      = "ACELERADA";
   degre      = "DEGRESIVA";
   residual   = "RESIDUAL";
   opefis     = "PAGOS LEASING";
   manual     = "MANUAL";
   alfa1      = "";
   &swfv      = 0;

   fDataTop1   = @;
   fDataTop2   = @;
   nCampo      = 0;
   nDEmpresa   = 0;
   nHEmpresa   = 0;
   nDActividad = 0;
   nHActividad = 0;
   swCuenta    = 0;
   swBueno     = 0;
   nLaEmpresa  = 0;
   &nCamSel    = 21;
   &nCamTip    = 36;

   nValorF     = 0;
   nValorC     = 0;
   ultfis      = @;
   aUltLinF    = "";
   ultcon      = @;
   aUltLinC    = "";

   nConcepto   = 0;
   nSal        = 0;
   nSwGraba    = 0;
   topcont     = @;
   nSwNoFis    = 0;
   nSwNoCon    = 0;
   fFecha      = @;

   aPath       = "";
   nElAny      = 0;
   nBueno      = 0;
   nPeriodo    = 0;
   fDFecha     = @;
   fHFecha     = @;
   &enImA      = 0;
   &enImD      = 0;

   nVAnyo = 0;

   nEjerV   = 0;
   nEjerC   = 0;
   &enNuevo = 0;
   &enSub   = 0;

   nAcumFisR = 0;
   nAcumConR = 0;
|FIN;

|INCLUYE i_variar;
|INCLUYE i_varcon;
|INCLUYE i_varlib;
|INCLUYE i_numamo;
|INCLUYE i_selcon;
|INCLUYE i_calamo;

|| //////////////////// Calculos de Pantalla \\\\\\\\\\\\\\\\\\\\\\\\\\
|CALCULO def; |TIPO 7;
 fFecha = ~;
 aAlfa1 = fFecha;
 aAlfa1 = aAlfa1 % -104;
 #0#46 = aAlfa1; |PINTA #0#46;

 |SI #107#28 = "S";
     |PINTA 1032, "Activ./Subdepar. ";
 |FINSI;
|FCAL;

|CALCULO Tipo7;  |TIPO 7;
     |SI enEmpresa ! 0;
         #0#5  = enEmpresa;  |PINTA #0#5;
         #0#6  = enEmpresa;  |PINTA #0#6;

         |C_M #0#5, 1, "N";
         |C_M #0#6, 1, "N";
     |FINSI;
|FCAL;

|CALCULO Parrilla;
     |SI #0#5 = 0;
         #0#5 = 0;  |PINTA #0#5;
         #0#6 = 0;  |PINTA #0#6;  |C_M #0#6, 1, "N";
         |PARA nCampo = 7;  |SI nCampo [ 18;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |SINO;
         |SI enEmpresa = 0;
             |C_M #0#5, 1, "S";
             |C_M #0#6, 1, "S";
         |FINSI;

         |PARA nCampo = 7;  |SI nCampo [ 18;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "N";
               #0nCampo = 0;  |PINTA #0nCampo;
         |SIGUE;
     |FINSI;
|FCAL;

|CALCULO NoMasEmpresas;
     |SI #0Campo = 0;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 18;  |HACIENDO nCampo = nCampo + 1;
               #0nCampo = 0;  |C_M #0nCampo, 1, "N";
         |SIGUE;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 18;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |FINSI;
|FCAL;

|CALCULO numamo; |TIPO 0;
   x_alfa1 = #0Campo;
   |HAZ x_punto;
   #0Campo = x_alfa1;
   |PINTA #0Campo;
|FCAL;

|CALCULO cuenta; |TIPO 0;
     salidas  = FSalida;

     |ABRE #100;
     |LEE 040#100p;
     |SI FSalida ! 0;  |DDEFECTO #100;  |FINSI;
     |CIERRA #100;

     nLaEmpresa = enEmpresa;
     enEmpresa  = #100#1;
     enPerConta = #100#2;

     eaCuenta = #0Campo;
     |HAZ FiltraPuntos;
     #0Campo = eaCuenta;
     |PINTA #0Campo;
     enEmpresa = nLaEmpresa;
|FCAL;

|CALCULO Completo; |TIPO 0;
 aAlfa1 = #0#37;
 |C_M #0#38,1,aAlfa1;
 |SI aAlfa1 = "N";
     #0#38 = "N"; |PINTA #0#38;
 |FINSI;
|FCAL;

|CALCULO DesHasE; |TIPO 0;
 aAlfa2 = "S"; aAlfa1 = #0#45; |SI aAlfa1 = "S"; aAlfa2 = "N"; |FINSI;
 |C_M #0#46,1,aAlfa1;
 |C_M #0#37,1,aAlfa2;
 |C_M #0#38,1,aAlfa2;
 |SI aAlfa2 = "N";
     #0#37 = "N"; |PINTA #0#37;
     #0#38 = "N"; |PINTA #0#38;
 |FINSI;
|FCAL;
|| **************************************************************
|| //////////////////// Procesos del Informe  \\\\\\\\\\\\\\\\\\\\\\\\\\
|PROCESO CalculaResto;

  |SI #41#16 = 0; |Y #41#22 = 0;
      |ACABA;   || ya esta amortizado
  |FINSI;
  |SI #41#35 = "S"; |ACABA; |FINSI; ||esta en curso
  |SI #41#40 = "I"; |Y #41#26 < "01.01.1900"; |ACABA; |FINSI; ||Elemento Inactivo

  cfecha  = #41#9;
  importe = #41#10;
  vfecha  = #41#26;

  nSal = 0;
  |PARA; |SI nSal = 0; |HACIENDO;
         ultlin     = ultlin + 1;
         nAnyAmor   = nAnyAmor + 1;
         aAlfa1     = nAnyAmor;
         aAlfa1     = "31.12." + aAlfa1;
         fFechaAmor = aAlfa1;

         #0#39 = nAnyAmor;
         #0#40 = 0;
         #0#41 = fFechaAmor;
         |SI #107#18 = "S"; |Y #41#26 > "01.01.0000";  || nuevo 10.5.2007
             |SI #0#41 > #41#26;
                 #0#41 = #41#26;
             |FINSI;
         |FINSI;
         #0#42 = "AMORTIZACION EJERCICIO/" + (fFechaAmor % -104);
         #0#43 = 0;

         |SI residc ! 0; |Y nSwNoCon = 0;  || Amortizacion Contable
             |HAZ AmorContable;
             amorcon = #0#43;
         |FINSI;
         |SI resid  ! 0; |Y nSwNoFis = 0;  || Amortizacion Fiscal
             |HAZ AmorFiscal;
         |FINSI;

             amorfis = #0#40;
             amorcon = #0#43;
             |HAZ GrabaLaLinea;
             #0#40 = amorfis;
             #0#43 = amorcon;

             |SI nAnyAmor < nAnyFin;
                 enAcumFiscal = enAcumFiscal + amorfis;
             |FINSI;
             amor     = amor  + amorfis;
             resid    = #41#16;
             amorc    = amorc + amorcon;
             residc   = #41#22;
             |PRINT;

         |SI #42#9 < "01.01.2015"; |Y #41#9 < "01.01.2015";
             nAcumFisR = nAcumFisR + #42#7;
             nAcumConR = nAcumConR + #42#11;
         |FINSI;
         |SI resid  = 0; nSwNoFis = 1; |FINSI;
         |SI residc = 0; nSwNoCon = 1; |FINSI;

         |SI nSwNoFis = 1; |Y nSwNoCon = 1;
             nSal = 1;
         |FINSI;
 |SIGUE;
|FPRC;

|| ... Calculo la Fiscal
|PROCESO AmorFiscal;
  eaTipoA = #41#13;
  enTP100 = #41#14;
  enAnys  = #41#17;
  nAcumResidual = nAcumFisR;

  |SI enTP100 = 0; |Y enAnys = 0;
      nSwNoFis = 1;
      |ACABA;
  |FINSI;

  ultFechaA  = ultfis;
  aUltLinea  = aUltLinF;
  nAcum      = amor;
  nRest      = resid;

  nImpAmor   = 0;
  |HAZ Calculo;
  |SI #41#26 >  "01.01.0000";
      aAlfa1 = #41#26; aAlfa1 = aAlfa1 % -104; nVAnyo   = aAlfa1; || a¤o venta
      |SI nAnyAmor = nVAnyo; |Y nAnyAmor ] 2005;
          nImpAmor = (#dsmom041#21 + #0#43 - #41#15);
      |FINSI;
  |FINSI;

  #0#40    = nImpAmor;
  aUltLinF = "A";
  ultfis   = fFechaAmor;
  topfisc  = topfecha;
|FPRC;

|| ... Calculo la Contable
|PROCESO AmorContable;
  eaTipoA = #41#19;
  enTP100 = #41#20;
  enAnys  = #41#23;
  nAcumResidual = nAcumConR;

  |SI enTP100 = 0; |Y enAnys = 0;
      nSwNoCon = 1;
      |ACABA;
  |FINSI;

  ultFechaA  = ultcon;
  aUltLinea  = aUltLinC;
  nAcum      = amorc;
  nRest      = residc;

  nImpAmor   = 0;
  |HAZ Calculo;

  #0#43    = nImpAmor;
  aUltLinC = "A";
  ultcon   = fFechaAmor;
  topcont  = topfecha;
|FPRC;

|| **************************************************************************
|PROCESO BuscaPeriodo;
       nBueno = 0;
       |SI #300#11 = 1;
           |SI #300#26 = nElAny;
               nBueno = 1;
           |FINSI;
       |SINO;
            |HAZ LaFecha;
            |SI fHFecha ] fec1; |Y fDFecha [ fec2;
                nBueno = 1;
            |FINSI;
       |FINSI;
       |SI nBueno = 1;
           nPeriodo = #300#3;
           |ERROR10;
       |FINSI;
|FPRC;

|DEFBUCLE BuscaPeriodo;
 #300#1;
 ;
 enEmpresa, 0;
 enEmpresa, 9;
 ;
 NULL, BuscaPeriodo;
|FIN;

|PROCESO BuscaConta;
  aAlfa1  = #42#9;
  aAlfa1  = aAlfa1 % -102;
  nElAny  = aAlfa1;
  fDFecha = #42#9;
  fHFecha = #42#9;
  cliente = "";
  nBueno  = 0;
  |BUCLE BuscaPeriodo;
  |SI nBueno = 1;
      |DBASS aPath; |QBT aPath;
      aPathConta    = aPath + "contagen/fich/";
      aAlfa1 = nPeriodo;
      aAlfa2 = enEmpresa; aAlfa2 = ("00000" + aAlfa2) % -105;
      aPath  = aPathConta + aAlfa2 + aAlfa1 + "/";
      |PATH_DAT #304 aPath;
      |ABRE #304;
      |DDEFECTO #304;
      |SELECT #3#304;
      #304#0 = #42#14;
      #304#2 = #42#15;
      #304#3 = #42#16;
      |LEE 000#304=;
      cliente = #304#13;
      |SELECT #1#304;
      |CIERRA #304;
  |FINSI;
|FPRC;

|PROCESO buscaventa;
 impv = #42#11;
 |SI #42#14 ! 0;
     |HAZ BuscaConta;
 |FINSI;
|FPRC;

|DEFBUCLE buscaventa;
 #42#1;
 6;
 #41#0,#41#2,#41#3,001,"V";
 #41#0,#41#2,#41#3,999,"V";
 ;
 NULL, buscaventa;
|FIN;

|PROCESO GrabaLaLinea;

   |ABRE #42;
   |DDEFECTO #42;

   |SI #41#10 > 0;
       |SI #41#16 < amorfis;  amorfis = #41#16;  |FINSI;
       |SI #41#22 < amorcon;  amorcon = #41#22;  |FINSI;
   |SINO;
       |SI #41#16 > amorfis;  amorfis = #41#16;  |FINSI;
       |SI #41#22 > amorcon;  amorcon = #41#22;  |FINSI;
   |FINSI;
   #41#15 = #41#15 + amorfis;
   #41#16 = #41#16 - amorfis;
   #41#21 = #41#21 + amorcon;
   #41#22 = #41#22 - amorcon;
   |SI #41#16 ! 0;
       |SI #41#26 > "01.01.0000";
           |SI #41#26 [ fFechaAmor;
               #41#16 = 0;
           |FINSI;
       |SINO;
           |SI topfisc [ fFechaAmor;
               amorfis = amorfis + #41#16;
               #41#15 = #41#15 + #41#16;
               #41#16 = 0;
           |FINSI;
       |FINSI;
   |FINSI;
   |SI #41#22 ! 0;
       |SI #41#26 > "01.01.0000";
           |SI #41#26 [ fFechaAmor;
               #41#22 = 0;
           |FINSI;
       |SINO;
           |SI topcont [ fFechaAmor;
               amorcon = amorcon + #41#22;
               #41#21 = #41#21 + #41#22;
               #41#22 = 0;
           |FINSI;
       |FINSI;
   |FINSI;

   #42#0 = #41#0;
   #42#1 = #41#1;
   #42#2 = #41#2;
   #42#3 = #41#3;
   #42#4 = ultlin;
   #42#5 = #0#39;
   #42#6 = "A";
   #42#7 = amorfis;
   #42#8 = "N";
   #42#9 = #0#41;
   #42#10 = #0#42;
   #42#11 = amorcon;
   #42#12 = nConcepto;
   #42#13 = #42#11 - #42#7;

   |SI #0#38 = "S"; |Y #41#35 = "N";
       |SI #42#11 ! 0; |O #42#7 ! 0;
           |GRABA 021#42n
           nSwGraba = 1;
      |FINSI;
  |FINSI;
  |CIERRA #42;
|FPRC;

|PROCESO ctas_grup;
   cta1 = "            ";
   cta2 = "            ";
   |ABRE #40;
   #40#0 = #41#0;
   #40#2 = #41#18;
   |LEE 001#40=;
   |SI FSalida =  0;
       cta1 = #40#10;
       cta2 = #40#12;
   |FINSI;
   cta1_of = "            ";
   cta2_of = "            ";
   |SI #41#38 ! 0;
       #40#0 = #41#0;
       #40#2 = #41#38;
       |LEE 001#dsmom040.=;
       |SI FSalida =  0;
           cta1_of = #40#10;
           cta2_of = #40#12;
       |FINSI;
   |FINSI;
   |CIERRA #40;
|FPRC;

|PROCESO imprelin;

   |SI #0#45 = "S";
       |SI #42#9 > fDataTop2; |ACABA; |FINSI;
   |FINSI;

   |SI #42#6 = "A";
       amor  = (amor  + #42#7)  ! EURODBMOD;
       amorc = (amorc + #42#11) ! EURODBMOD;
   |FINSI;
   |SI #42#6 = "C";
       valor = (valor + #42#7) ! EURODBMOD;
   |FINSI;
   resid  = (valor - amor)  ! EURODBMOD;
   residc = (valor - amorc) ! EURODBMOD;

   enImA = (#42#11 - #42#7) ! EURODBMOD;
   enImD = 0;
   |SI enImA < 0;
       enImD = (#42#7 - #42#11) ! EURODBMOD;
       enImA = 0;
   |FINSI;


   |SI nSwOp = 1; |ACABA; |FINSI;
   |PRINT;

   |SI #42#6 ! "V";
       |SI #42#9 ] ultfis;
           |SI #42#7  ! 0; |O resid ! 0;
               ultfis = #42#9; aUltLinF = #42#6;
           |FINSI;
       |FINSI;
       |SI #42#9 ] ultcon;
           |SI #42#11 ! 0; |O residc ! 0;
               ultcon = #42#9; aUltLinC = #42#6;
           |FINSI;
       |FINSI;
   |FINSI;
   ultlin    = #42#4;
   |SI #42#6 = "A";
       nAnyAmor  = #42#5;
       nConcepto = #42#12;
       |SI nAnyAmor < nAnyFin;
           enAcumFiscal = (enAcumFiscal  + #42#7) ! EURODBMOD;
       |FINSI;
       |SI #42#9 < "01.01.2015"; |Y #41#9 < "01.01.2015";
           nAcumFisR = nAcumFisR + #42#7;
           nAcumConR = nAcumConR + #42#11;
       |FINSI;
   |SINO;
       |SI #42#6 = "C";
           nAnyAmor = #42#5 - 1;
       |FINSI;
   |FINSI;

   |SI resid  = 0; nSwNoFis = 1; |FINSI;
   |SI residc = 0; nSwNoCon = 1; |FINSI;

   |SI #41#14 = 0; |Y #41#17 = 0;
       nSwNoFis = 1;
   |FINSI;
   |SI #41#20 = 0; |Y #41#23 = 0;
       nSwNoCon = 1;
   |FINSI;
|FPRC;

|DEFBUCLE LasLineas;
 #42#1;
 ;
 #41#0,#41#2,#41#3,001;
 #41#0,#41#2,#41#3,999;
 ;
 NULL, imprelin;
|FIN;

|PROCESO LaImpresion;
   enNuevo  = 0;
   nSwGraba = 0;
   |SI nLaEmpresa ! #41#0;
       nLaEmpresa = #41#0;
       enEmpresa  = #41#0;
       swBueno = 0;
       |HAZ LaSelEmpresa;
       |SI enSwSelEmp = 1;
           swBueno = 1;
       |SINO;
           |HAZ AntCambioCta;
       |FINSI;
   |FINSI;
   |SI swBueno = 1; |ACABA; |FINSI;

   |HAZ LeeDatosEmpresa;
   |SI #107#28 = "S"; |Y eaSubde = "S";
       |SI #41#54 < #0#49; |O #41#54 > #0#50;
           |ACABA;
       |FINSI;
   |FINSI;
   enSub = 0; |SI eaCodSubdep > "   "; enSub = 1; |FINSI;

   |SI #0#4 = "N";
       |SI #41#25 = "N"; |ACABA; |FINSI;
   |FINSI;
   |SI #0#44 = "N";
       |SI #41#35 = "S"; |ACABA; |FINSI;
   |FINSI;
   |SI #0#48 ! "T";
       |SI #0#48 ! #41#40; |ACABA; |FINSI;
   |FINSI;

   |SI #0#45 = "S";  || bien comprado despues del ejercicio hasta
       |SI #41#27 > fDataTop2; |ACABA; |FINSI;
   |FINSI;

   aAlfa1  = #41#27; aAlfa1 = aAlfa1 % -104;
   nEjerC  = aAlfa1;
   nEjerV  = 0;
   |SI #41#26 > "01.01.0000"; |Y #41#58 = 2;
       aAlfa1 = #41#26; aAlfa1 = aAlfa1 % -104;
       nEjerV = aAlfa1;
   |FINSI;
   |SI nEjerC < 2008;
       |SI nEjerV = 0; |O nEjerV ] 2008;
           enNuevo = 1;
       |FINSI;
   |FINSI;

   |HAZ ctas_grup;

   laana = ""; aCta = ""; aAna = "";
   |SI enSwAna = 1;
       laana = #41#34; |QBF laana;
       |SI laana ! "";
           laana = #41#34;
           aCta = "Cuenta";
           aAna = "Analitica";
       |FINSI;
   |FINSI;

   || ----- cambiar literales para la ficha -----------------------------------
   situa = "ACTIVO";
   |SI #41#40 = "I"; situa = "INACTIVO"; |FINSI;
   curso1 = "Fecha Inicio";
   curso2 = #41#9;
   |SI #0#35 = "S"; curso1 = ""; curso2 = "Elemento en Curso"; |FINSI;

   tipo = usada;
   |SI #41#11 = ene; tipo = nueva; |FINSI;
   tiamo = norma;
   |SI #41#13 = "D"; tiamo = degre;    |FINSI;
   |SI #41#13 = "A"; tiamo = acele;    |FINSI;
   |SI #41#13 = "F"; tiamo = opefis;   |FINSI;
   |SI #41#13 = "R"; tiamo = residual; |FINSI;
   |SI #41#13 = "M"; tiamo = manual;   |FINSI;
   tiamoc = norma;
   |SI #41#19 = "D"; tiamoc = degre;    |FINSI;
   |SI #41#19 = "A"; tiamoc = acele;    |FINSI;
   |SI #41#19 = "R"; tiamoc = residual; |FINSI;
   |SI #41#19 = "M"; tiamoc = manual;   |FINSI;

   taNuevo = "";
   |SI #41#57 = 1;  taNuevo = "01. Amortizacion lineal segun tablas (calculo automatico)"; |FINSI;
   |SI #41#57 = 2;  taNuevo = "02. Amor. segun porcentaje constante sobre el imp. pendiente de amor. (calculo automatico)"; |FINSI;
   |SI #41#57 = 3;  taNuevo = "03. Amortizacion segun numeros digitos - creciente (calculo automatico)"; |FINSI;
   |SI #41#57 = 4;  taNuevo = "04. Planes de amortizacion (calculo automatico)"; |FINSI;
   |SI #41#57 = 5;  taNuevo = "05. Depreciacion efectiva probada fehacientemente (calculo manual)"; |FINSI;
   |SI #41#57 = 6;  taNuevo = "06. Libertad de Amortizacion (calculo manual)"; |FINSI;
   |SI #41#57 = 7;  taNuevo = "07. Amortizacion Acelerada (calculo automatico)"; |FINSI;
   |SI #41#57 = 8;  taNuevo = "08. Amortizacion de Elementos Adquiridos Usados (calculo automatico)"; |FINSI;
   |SI #41#57 = 9;  taNuevo = "09. Amortizacion de Elementos Patrimoniales Objeto de Reinversion - DT 28º LIS (calculo manual)"; |FINSI;
   |SI #41#57 = 10; taNuevo = "10. Amortizacion de Activos Nuevos Adquiridos en 2003 y 2004 - DT 13º LIS (calculo manual)"; |FINSI;
   |SI #41#57 = 80; taNuevo = "80. Amortizacion segun valor residual a 2015 (calculo automatico)"; |FINSI;

   tCampo55 = "";
   |SI #41#55 = 11; tCampo55 = "11. Terrenos dedicados exclusivamente a excombreras"; |FINSI;
   |SI #41#55 = 12; tCampo55 = "12. Edificaciones y Construcciones"; |FINSI;
   |SI #41#55 = 21; tCampo55 = "21. Maquinaria"; |FINSI;
   |SI #41#55 = 22; tCampo55 = "22. Elementos de Transportes"; |FINSI;
   |SI #41#55 = 23; tCampo55 = "23. Ordenadores y otros equipos informaticos"; |FINSI;
   |SI #41#55 = 24; tCampo55 = "24. Mobiliario"; |FINSI;
   |SI #41#55 = 25; tCampo55 = "25. Instalaciones"; |FINSI;
   |SI #41#55 = 26; tCampo55 = "26. Barcos y Aeronaves"; |FINSI;
   |SI #41#55 = 27; tCampo55 = "27. Batea"; |FINSI;
   |SI #41#55 = 28; tCampo55 = "28. Utiles y Herramientas"; |FINSI;
   |SI #41#55 = 29; tCampo55 = "29. Otro Inmovilizado Material"; |FINSI;
   |SI #41#55 = 31; tCampo55 = "31. Patentes y Marcas"; |FINSI;
   |SI #41#55 = 32; tCampo55 = "32. Derechos de Traspaso"; |FINSI;
   |SI #41#55 = 33; tCampo55 = "33. Aplicaciones Informaticas"; |FINSI;
   |SI #41#55 = 39; tCampo55 = "39. Otro Inmovilizado Intangible"; |FINSI;
   |SI #41#55 = 41; tCampo55 = "41. Ganado vacuno, porcino, ovino y caprino"; |FINSI;
   |SI #41#55 = 42; tCampo55 = "42. Ganado equino y frutales no citricos"; |FINSI;
   |SI #41#55 = 43; tCampo55 = "43. Frutales citricos y vi¤edos"; |FINSI;
   |SI #41#55 = 44; tCampo55 = "44. Olivar"; |FINSI;
   |SI #41#55 = 49; tCampo55 = "49. Otros Bienes Semovientes y Agricolas"; |FINSI;

   tCampo58 = "";
   |SI #41#58 = 0;  tCampo58 = "    Elemento en Alta"; |FINSI;
   |SI #41#58 = 1;  tCampo58 = "01. Amortizacion total"; |FINSI;
   |SI #41#58 = 2;  tCampo58 = "02. Venta o transmision"; |FINSI;
   |SI #41#58 = 3;  tCampo58 = "03. Deterioro o destruccion"; |FINSI;
   |SI #41#58 = 4;  tCampo58 = "04. Retirada del bien por obsolescencia"; |FINSI;
   |SI #41#58 = 5;  tCampo58 = "05. Perdida"; |FINSI;
   |SI #41#58 = 6;  tCampo58 = "06. Robo"; |FINSI;
   |SI #41#58 = 99; tCampo58 = "99. Otras causas diferentes a las anteriores"; |FINSI;

   tCampo56 = "";
   |SI #41#55 = 11; |O #41#55 = 12; |O #41#55 = 22;
       tCampo56     = "Referencia Catastral .:";
       |SI #41#55 = 22;
           tCampo56 = "Matricula del vehiculo: ";
       |FINSI;
   |FINSI;

   am_ap = "Anual";
   |SI #41#24 = "P"; am_ap = "Periodica"; |FINSI;
   swfv=0;
   fecha = #41#26;
   cliente = ""; impv = 0;
   |SI #41#26 > "01.01.1900"; |Y #41#58 = 2;
       swfv=1;
       |BUCLE buscaventa;
   |FINSI;
   eaBaja = "";
   |SI #41#58 ! 0;
       |SI #41#58 =  1; eaBaja = "Amortizacion total"; |FINSI;
       |SI #41#58 =  2; eaBaja = "Venta o transmision"; |FINSI;
       |SI #41#58 =  3; eaBaja = "Deterioro o destruccion"; |FINSI;
       |SI #41#58 =  4; eaBaja = "Retirada del bien por obsolescencia"; |FINSI;
       |SI #41#58 =  5; eaBaja = "Perdida"; |FINSI;
       |SI #41#58 =  6; eaBaja = "Robo"; |FINSI;
       |SI #41#58 = 99; eaBaja = "Otras causas diferentes a las anteriores"; |FINSI;
   |FINSI;
   nValorF = #41#16;
   nValorC = #41#22;
   |SI #0#45 = "S";
       nSwOp  = 1;
       valor   = 0;
       amorc   = 0;
       amor    = 0;
       residc  = 0;
       resid   = 0;
       |BUCLE LasLineas;
       #41#16 = resid;
       #41#22 = residc;
   |FINSI;
   nSwOp   = 0;
   valor   = 0;
   amorc   = 0;
   amor    = 0;
   residc  = 0;
   resid   = 0;
   ultfis   = #41#9;  || ultima fecha amortizacion fiscal
   aUltLinF = "C";
   ultcon   = #41#9;  || ultima fecha amortizacion contable
   aUltLinC = "C";
   ultlin   = 0;

   enCodOpe = #41#36;
   fFinPago = #41#37;
   enMulFis = #41#39;
   enAcumFiscal = 0;
   aAlfa1  = fFinPago;
   aAlfa1  = aAlfa1 % -104;
   nAnyFin = aAlfa1;

   |SI #0#37 = "S";  #41#16 = 0; #41#22 = 0; |FINSI;

   nSwNoFis = 0;
   nSwNoCon = 0;
   nAcumFisR = 0;
   nAcumConR = 0;
   |BUCLE LasLineas;

   |SI #0#37 = "S";
       #41#16 = nValorF;
       #41#22 = nValorC;
       |HAZ CalculaResto;
   |FINSI;
   |PIEINF;

   |SI nSwGraba = 1;
       |GRABA 020#41a;
   |FINSI;
   |LIBERA #41;
|FPRC;

|DEFBUCLE dsmoy041;
   #41#4;
   2,3,1;                                     || 2 = campo elemento + Actividad
   nDEmpresa,#0#0,#0#2,#0#34,nDActividad;     || clave 4 + elemento
   nHEmpresa,#0#1,#0#3,#0#35,nHActividad;
   be;
   NULL, LaImpresion;
|FIN;

|PROCESO imprimir;
      nLaEmpresa = -1;
      swBueno = 0;
      |BUCLE dsmoy041;
|FPRC;

|CALCULO Tipo3;  |TIPO 3;

    |DBASS aPath; |QBT aPath;
    aPathConta    = aPath + "contagen/fich/";
    |PATH_DAT #300 aPathConta;

    |IMPRE 0;
    |SI FSalida ! 0;  |ACABA;  |FINSI;

    informe = inform1;
    |SI #0#47 = 2; informe = inform2; |FINSI;

    |INFOR informe;
    |SI FSalida ! 0;  |FINIMP;  |ACABA;  |FINSI;

     nDActividad = #0#19;
     nHActividad = #0#20;
     efFecTope   = ~;
     |SI #0#45 = "S";
         aAlfa1 = #0#46;
         aAlfa2 = "01.01." + aAlfa1;
         fDataTop1 = aAlfa2;
         aAlfa2 = "31.12." + aAlfa1;
         fDataTop2 = aAlfa2;
     |FINSI;

     |SI #0#5 ! 0;
         nDEmpresa = #0#5;
         nHEmpresa = #0#6;
         |HAZ imprimir;
     |SINO;
         |PARA nCampo = 7; |SI nCampo [ 18;  |HACIENDO nCampo = nCampo + 1;
               |SI #0nCampo ! 0;
                   nDEmpresa = #0nCampo;
                   nHEmpresa = #0nCampo;
                   |HAZ imprimir;
               |FINSI;
         |SIGUE;
     |FINSI;
     |FININF;
     |FINIMP;
|FCAL;

|PROGRAMA;

 |HAZ Analitica;
 |ABRE #107;
 |DDEFECTO #107;
 |LEE 040#107p;
 |CIERRA #107;
 |SI #107#28 = "S";
     |C_V #0#49, 1, "S"; |C_M #0#49, 1, "S";
     |C_V #0#50, 1, "S"; |C_M #0#50, 1, "S";
 |FINSI;

 |HAZ LeeEURODBMOD;
 |SI enEmpresa ! 0;
     |SI eaHElem = "";
         eaHElem = eaDElem;
         enHNEle = enHNEle;
     |FINSI;
     |DDEFECTO #0;
     #0#2 = eaDElem;
     #0#3 = eaHElem;
     #0#4 = "S";
     #0#5 = enEmpresa;
     #0#6 = enEmpresa;
     #0#21 = "S";
     #0#34 = enDNEle;
     #0#35 = enHNEle;
     |HAZ Tipo3;
 |SINO;
     |CLS;
     |MANTE #0;
 |FINSI;
|FPRO;

|PROCESO LeeDatosEmpresa;
  eaCodSubdep = "   ";
  eaNomSubdep = "";
  |SI #107#28 = "S";
      |HAZ eBuscoSubdep;
      |SI eaSubde = "S";
          eaCodSubdep = #41#54;
          enQue = 2;
          |HAZ eSubActividad;
      |FINSI;
  |FINSI;

  |DDEFECTO #900;
  #900#0 = enEmpresa;
  eaInac = #0#21; eaInac = "S";
  eaTituloL = "";
  enActividad = #41#1;
  enEjer    = #0#39;   || Ejercicio
  |HAZ DatosEmpr_w30;

  |HAZ Analitica;
  eaInac = #0#21;

|FPRC;
