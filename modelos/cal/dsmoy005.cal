|FICHEROS;
     dsmoy005 #0;
     dsmom002 #2;
     dsmom023 #23;  ||Tabla cuentas
     dsmom024 #24;  ||Mant. Valores por Empresas

     dsmom100 #100;
     :/contagen/def/caconimp #306;  || configuracion impresiones

     :/basica/def/agitab99 #99;
     :/basica/def/agicen14 #114;
     :/basica/def/agifigen #118;
     :/basica/def/agim0019 #119;
|FIN;

|VARIABLES;
     nEstado   = 0;
     nCampo    = 0;
     nDEmpresa = 0;
     nHEmpresa = 0;
     nPeriodo  = 0;
     nEjer     = 0;
     aAlfa1    = "";
     aAlfa2    = "";
     aFichero  = "";
     aPath     = "";
     nNume1    = 0;

     nPin1       = 0;
     aPin1       = "";
     efPn10      = @;
     efPn11      = @;
     swExiste    = 0;
     &enSwLinea  = 0;
     nLaEmpresa  = 0;
     nElModelo   = 0;
     swPrEmpresa = 0;
     swPrModelo  = 0;
     swPrAny     = 0;
     swAlgun     = 0;
     swBo        = 0;
     &eaLit1     = "";
     &eaLit2     = "";
     &enDatos    = 0;
     fLaFecha    = @;
|FIN;

|INCLUYE i_variar;
|INCLUYE i_masivo;

|| /////////////////////// PROCESOS ENTRADA POR PANTALLA \\\\\\\\\\\\\\\\\\\\
|PROCESO Tipo7;  |TIPO 7;
     |SI enEmpresa ! 0;
         #0#0 = enEmpresa;  |PINTA #0#0;
         #0#1 = enEmpresa;  |PINTA #0#1;

         |C_M #0#0, 1, "N";
         |C_M #0#1, 1, "N";
     |FINSI;
|FPRC;

|PROCESO Tipo7_16;  |TIPO 7;
    |SI enModelo ! 0;
         #0#16 = enModelo;  |PINTA #0#16;
         #0#17 = enModelo;  |PINTA #0#17;

         |C_M #0#16, 1, "N";
         |C_M #0#17, 1, "N";
    |FINSI;
|FPRC;

|PROCESO Parrilla;
     |SI #0#0 = 0;
         #0#0 = 0;  |PINTA #0#0;
         #0#1 = 0;  |PINTA #0#1;  |C_M #0#1, 1, "N";
         |PARA nCampo = 2;  |SI nCampo [ 13;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |SINO;
         |SI enEmpresa = 0;
             |C_M #0#0, 1, "S";
             |C_M #0#1, 1, "S";
         |FINSI;

         |PARA nCampo = 2;  |SI nCampo [ 13;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "N";
               #0nCampo = 0;  |PINTA #0nCampo;
         |SIGUE;
     |FINSI;
|FPRC;

|PROCESO Parrilla_16;
     |SI #0#16 = 0;
         #0#16 = 0;  |PINTA #0#16;
         #0#17 = 999;  |PINTA #0#17;  |C_M #0#17, 1, "N";
         |PARA nCampo = 18;  |SI nCampo [ 29;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |SINO;
         |SI enModelo = 0;
             |C_M #0#16, 1, "S";
             |C_M #0#17, 1, "S";
         |FINSI;

         |PARA nCampo = 18;  |SI nCampo [ 29;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "N";
               #0nCampo = 0;  |PINTA #0nCampo;
         |SIGUE;
     |FINSI;
|FPRC;

|PROCESO NoMasEmpresas;
     |SI #0Campo = 0;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 13;  |HACIENDO nCampo = nCampo + 1;
               #0nCampo = 0;  |C_M #0nCampo, 1, "N";
         |SIGUE;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 13;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |FINSI;
|FPRC;

|PROCESO NoMasModelos;
     |SI #0Campo = 0;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 29;  |HACIENDO nCampo = nCampo + 1;
               #0nCampo = 0;  |C_M #0nCampo, 1, "N";
         |SIGUE;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 29;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |FINSI;
|FPRC;

|PROCESO ConsuModelo;
     |ABRE #99;
     #99#0 = #0Campo;
     |LEE 040#99=;
     |SI FSalida ! 0;  |DDEFECTO #99;  |FINSI;
     |CIERRA #99;

     |SI Campo = 16; |O Campo = 17;  |ACABA;  |FINSI;
     |SI #0Campo = 0; |ACABA; |FINSI;
     |SI #99#4 = "N";
         |MENAV "     Modelo sin Activar.";
         |ERROR;
     |FINSI;
|FPRC;

|CALCULO Eano; |TIPO 7;
 fLaFecha = ~;
 aAlfa1 = fLaFecha; aAlfa1 = aAlfa1 % -104;
 #0#15 = aAlfa1;
 |PINTA #0#15;
|FCAL;

|PROCESO DatosEmp; |TIPO 7;

   |DDEFECTO #306;

   |ABRE #100;
   |LEE 040#100p;
   |SI FSalida = 0;
       aAlfa1 = #100#1; |QBF aAlfa1; aAlfa1 = ("00000" + aAlfa1) % -105;
       aAlfa2 = #100#2; |QBF aAlfa2; aAlfa2 = ("0"     + aAlfa2) % -101;
       |DBASS aPath; |QBT aPath;
       aPathConta = aPath + "contagen/fich/" + aAlfa1 + aAlfa2 + "/";

       |PATH_DAT #306 aPathConta;

       |ABRE #306;
       #306#0 = 1;
       |LEE 000#306=;
       |CIERRA #306;
   |FINSI;
   |CIERRA #100;

   #0#60  = #306#7; |PINTA #0#60;
   aAlfa1 = #306#8; |C_M #0#60,1,aAlfa1;
|FPRC;


|| /////////////////////// //////////////////////////////////////////////////
|| /////////////////////// PROCESOS DE INFORMES \\\\\\\\\\\\\\\\\\\\\
|PROCESO LeeLaEmpresa;
  |PDEFECTO #0, 30,60;
  #0#64 = 0;  #0#65 = 0;  #0#66 = 0;
  #0#30 = nLaEmpresa;

  swAlgun    = 0;
  swPrModelo = 0;
  nPin1 = 0;
  aPin1 = "";
  efPn10 = "01.01.0000";
  efPn11 = "01.01.0000";
  || Lee Actividad
    |ABRE #agicen14;
    #agicen14#0 = #0#30; || Empresa
    #agicen14#1 = 00;
    |LEE 041#agicen14.];
    |SI FSalida = 0; |Y #agicen14#0 = #0#30;
        #0#35 = #agicen14#9;
        #0#36 = #agicen14#10;
        #0#37 = #agicen14#7;
        #0#38 = #agicen14#8;
        efPn10 = #agicen14#5;
        efPn11 = #agicen14#6;
    |FINSI;
    |CIERRA #agicen14;

 || Lee Ficha Sociedades
    swExiste = 0;
    |ABRE #agim0019;
    #agim0019#0 = #0#30; || Empresa
    #agim0019#1 = 99;
    |LEE 041#agim0019.];
    |SI FSalida = 0;
        |LEE 041#agim0019.a;
    |SINO;
        |LEE 041#agim0019.u;
    |FINSI;
    |SI FSalida = 0; |Y #agim0019#0 = #0#30;
        swExiste = 1;
    |FINSI;
    |SI swExiste = 1;
        efPn10 = #agim0019#7;      || Fecha Activa
        efPn11 = #agim0019#55;     || Fecha Inactiva
        nPin1 = #agim0019#4;
        aPin1 = #agim0019#5;
    |FINSI;
    |CIERRA #agim0019;

    |ABRE #agifigen;
    #agifigen#0 = #0#30; || Empresa
    |LEE 041#agifigen.=;
    |SI FSalida = 0;
        efPn10  = #agifigen#93;
        efPn11  = #agifigen#94;
        nPin1   = #agifigen#87;
        aPin1   = #agifigen#88;
        #0#31 = #agifigen#13;  || Nif
        #0#32 = #agifigen#1;   || Nombre
    |FINSI;
    |CIERRA #agifigen;

    |SI efPn10 [ "01.01.1900"; efPn10 = ""; |FINSI;
    #0#61 = "INACTIVA"; |SI efPn11 [ "01.01.1900"; efPn11 = ""; #0#61 = "ACTIVA"; |FINSI;
    #0#33 = nPin1;
    #0#34 = aPin1;
    #0#39 = efPn10;
    #0#40 = efPn11;
    aAlfa1 = #0#14; aAlfa1 = "01.01." + aAlfa1; #0#62 = aAlfa1;
    aAlfa1 = #0#15; aAlfa1 = "31.12." + aAlfa1; #0#63 = aAlfa1;
|FPRC;

|PROCESO dsmom002;
    #0#42 = #2#7; || periodicidad
    #0#39 = #2#8;
    #0#40 = #2#9;
|FPRC;

|DEFBUCLE dsmom002;
 #2#1;
 ;
 nLaEmpresa,00,nElModelo;
 nLaEmpresa,99,nElModelo;
 e;
 NULL, dsmom002;
|FIN;

|PROCESO LeeElModelo;
  |PDEFECTO #0, 41,60;
  #0#64 = 0;
  #0#65 = 0;
  #0#66 = 0;
  #0#41 = nElModelo;
  |BUCLE dsmom002;
  eaLit1 = ""; eaLit2 = "";
  |SI #0#42 = "T";
          eaLit1 = "        1-Trimestre         2-Trimestre           3-Trimestre         4-Trimestre";
  |SINO;
      |SI #0#42 = "M";
          eaLit1 = "1-Mes         3-Mes         5-Mes         7-Mes         9-Mes        11-Mes";
          eaLit2 = "2-Mes         4-Mes         6-Mes         8-Mes        10-Mes        12-Mes";
      |FINSI;
  |FINSI;
  enSwLinea = 0;
  |PRINT;
|FPRC;

|| .....................................................................
|PROCESO TotalModelo;
 enSwLinea = 2;
 |PRINT;
|FPRC;

|| .....................................................................
|PROCESO Leem024;

 nNume1 = #24#2;
 |SI #24#2 = 99;
     nNume1 = 58;
 |SINO;
     nNume1 = #24#2 + 45;
 |FINSI;
 #0nNume1 = #0nNume1 + #24#8;

 |SI nNume1 ! 58;
     |SI nElModelo ] 300; |Y nElModelo [ 399;
         |SI swPrAny = 0;
             #0#58 = #24#8;   || el Primer Dato del A¤o
             swPrAny = 1;
          |SINO;

             #0#58 = #0#58 + #24#13;
          |FINSI;
     |SINO;
         #0#58 = #0#58 + #24#8;   || el Primer Dato del A¤o
         swPrAny = 1;
     |FINSI;
 |FINSI;

 |SI nElModelo ] 300; |Y nElModelo [ 399;
     |SI #0#58 = 0; #0#59 = ""; |FINSI;
     |SI #0#58 > 0; #0#59 = "A PAGAR "; |FINSI;
     |SI #0#58 < 0;
          #0#59 = "A DEVOLVER ";
          |SI #24#11 ! 0; #0#59 = "A COMPENSAR"; |FINSI;
     |FINSI;
 |SINO;
     |SI #0#58 = 0; #0#59 = ""; |FINSI;
     |SI #0#58 > 0; #0#59 = "A PAGAR "; |FINSI;
     |SI #0#58 < 0; #0#59 = "A DEVOLVER "; |FINSI;
 |FINSI;
|FPRC;

|DEFBUCLE Leem024;
 #24#1;
 ;
 #23#0, #23#1, 00, #23#2, 00;
 #23#0, #23#1, 99, #23#2, 99;
 e;
 NULL, Leem024;
|FIN;

|PROCESO Imprimir0;
 |SI #0#16 = 0;
     swBo = 0;
     |PARA nNume1 = 18; |SI nNume1 [ 29; |HACIENDO nNume1 = nNume1 + 1;
           |SI #0nNume1 = #23#2;
               swBo = 1; |SAL;
           |FINSI;
     |SIGUE;
     |SI swBo = 0; |ACABA; |FINSI;
 |FINSI;

 |SI swPrEmpresa = 0;
     swPrEmpresa = 1;
     nLaEmpresa = #23#0;
     |HAZ LeeLaEmpresa;
 |FINSI;

 |SI nLaEmpresa ! #23#0;
     |SI swAlgun = 1;
         |HAZ TotalModelo;
         |PIEINF;
     |FINSI;
     nLaEmpresa = #23#0;
     |HAZ LeeLaEmpresa;
 |FINSI;

 |SI swPrModelo = 0;
     swPrModelo = 1;
     nElModelo = #23#2;
     |HAZ LeeElModelo;
 |FINSI;

 |SI nElModelo ! #23#2;
     |HAZ TotalModelo;
     nElModelo = #23#2;
     |HAZ LeeElModelo;
 |FINSI;

 swPrAny = 0;
 |PDEFECTO #0, 45,60;
 #0#45 = #23#1;  ||A¤o
 |BUCLE Leem024;
 enSwLinea = 1;
 |PRINT;
 swAlgun = 1;

 |SI nElModelo ] 300; |Y nElModelo [ 399;

     |SI #0#58 ! 0;
         |SI #0#58 > 0;
             #0#64 = #0#64 + #0#58;  || pagado
             #0#66 = 0;
         |SINO;
             |SI #0#59 = "A DEVOLVER  ";
                 #0#65 = #0#65 - #0#58; || Devuelto
                 #0#66 = 0;
             |SINO;
                 #0#66 = - #0#58; || a compensar
             |FINSI;
         |FINSI;
         #0#43 = #0#64 - #0#65;
         |SI #0#43 = 0; #0#44 = ""; |FINSI;
         |SI #0#43 > 0; #0#44 = "PAGADO "; |FINSI;
         |SI #0#43 < 0; #0#44 = "DEVUELTO"; |FINSI;
         |SI #0#66 ! 0;
             #0#43 = #0#66; #0#44 = "A COMPENSAR";
             #0#65 = 0;
             #0#64 = 0;
         |FINSI;
     |FINSI;

 |SINO;

      #0#43 = #0#43 + #0#58;
      |SI #0#43 = 0; #0#44 = ""; |FINSI;
      |SI #0#43 > 0; #0#44 = "PAGADO ";  |FINSI;
      |SI #0#43 < 0; #0#44 = "DEVUELTO"; |FINSI;

 |FINSI;
|FPRC;

|PROCESO Imprimir1;
  |SI swAlgun = 1;
      |HAZ TotalModelo;
      |PIEINF;
  |FINSI;
  swAlgun = 0;
|FPRC;
|| .....................................................................
|DEFBUCLE Imprimir;
    #23#2;
    ;
    nDEmpresa, #0#16, #0#14;   ||Empresa Modelo A¤o
    nHEmpresa, #0#17, #0#15;
    be;
    NULL, Imprimir0, NULL, NULL, Imprimir1;
|FIN;

|| \\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\
|PROCESO ITipo3;  |TIPO 3;
 |SI enSwMvo = 0;
     |IMPRE 0;
     |SI FSalida ! 0; |ACABA; |FINSI;
 |FINSI;

 |INFOR "dsmoy005";
 |SI FSalida ! 0; |FINIMP; |ACABA; |FINSI;

 enDatos = 0; |SI #0#60 = "S"; enDatos = 1; |FINSI;
 swPrEmpresa = 0;
 swPrModelo  = 0;
 |SI #0#0 ! 0;
         nDEmpresa = #0#0;
         nHEmpresa = #0#1;
         |BUCLE Imprimir;
 |SINO;
         |PARA nCampo = 2;  |SI nCampo [ 13;  |HACIENDO nCampo = nCampo + 1;
               nDEmpresa = #0nCampo;
               nHEmpresa = #0nCampo;
               |BUCLE Imprimir;         || Imprimir Valor
         |SIGUE;
 |FINSI;
 |FININF;
 |SI enSwMvo = 0;
     |FINIMP;
 |FINSI;
|FPRC;

|| =========================================================================
|PROGRAMA;
 |SI enSwMvo = 1;
     enEmpresa = enEmprM;
     enEjer    = enEjerM;
 |FINSI;

 |SI enEmpresa ! 0;
     #0#0 = enEmpresa;
     #0#1 = enEmpresa;
     #0#14 = enEjer; |SI enSwMvo = 1; #0#14 = 1997; |FINSI;
     #0#15 = enEjer;
     |HAZ ITipo3;
 |SINO;
     |CLS;
     |MANTE #0;
 |FINSI;
|FPRO;
