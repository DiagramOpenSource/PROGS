|FICHEROS;
     dsmoy022 #0;

     dsmom004 #4;
     dsmom008 #8;

     dsmow038 #99;

     :/basica/def/agifigen #100;
     :/basica/def/agitab10 #110;
     :/basica/def/agitab11 #111;
     :/basica/def/agitab12 #112;
     :/basica/def/agitab13 #113;
     :/basica/def/agicen14 #114;
     :/basica/def/agitab99 #199;

     &clientes@ #715;
     referen@ #200;
|FIN;

|VARIABLES;
     nCampo       = 0;
     nCampo1      = 0;
     nMod         = 0;
     nCodModelo   = 0;
     nModelo      = 0;
     nModelo1     = 0;
     nWord        = 0;
     nEmpresas    = 0;
     nAscci       = 0;
     nGraba       = 0;
     nSw          = 0;
     nGeneral     = 0;
     nEstado      = 0;

     fFecha       = @;

     aAlfa        = "";
     aAlfa1       = "";
     aAlfa2       = "";
     nNume1       = 0;
     aFichero     = "";
     aBase        = "";
     aOrigen      = "";
     aDestino     = "";
     aIPRemoto    = "";
     aPlaning     = "";
     aLetra       = "";
     aDocumento   = "";
     aDef         = "";
     aTitulo      = "";

     nAntAgen     = 0;
     nPrimero     = 0;

     &eaUnidadBasico  = "";

     &enError       = 0;
     &fFechaDisol   = @;
     &fFechaChequeo = @;
     &fFechaChequeoF= @;
     &enRegistro    = 0;
     aDni           = "";
|FIN;

|INCLUYE i_variar;

|PROCESO Parrilla;
     |SI #0#0 = 0;
         #0#0 = 0;  |PINTA #0#0;
         #0#1 = 0;  |PINTA #0#1;  |C_M #0#1, 1, "N";
         |PARA nCampo = 2;  |SI nCampo [ 37;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |SINO;
         |C_M #0#1, 1, "S";
         |PARA nCampo = 2;  |SI nCampo [ 37;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "N";
               #0nCampo = 0;  |PINTA #0nCampo;
         |SIGUE;
     |FINSI;
|FPRC;

|PROCESO NoMasEmpresas;
     |SI #0Campo = 0;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 37;  |HACIENDO nCampo = nCampo + 1;
               #0nCampo = 0;  |C_M #0nCampo, 1, "N";
         |SIGUE;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 37;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |FINSI;
|FPRC;

|PROCESO Periodo;
     |SI #0#38 = 1;   #0#39 = "ENERO     ";  |FINSI;
     |SI #0#38 = 2;   #0#39 = "FEBRERO   ";  |FINSI;
     |SI #0#38 = 3;   #0#39 = "MARZO     ";  |FINSI;
     |SI #0#38 = 4;   #0#39 = "ABRIL     ";  |FINSI;
     |SI #0#38 = 5;   #0#39 = "MAYO      ";  |FINSI;
     |SI #0#38 = 6;   #0#39 = "JUNIO     ";  |FINSI;
     |SI #0#38 = 7;   #0#39 = "JULIO     ";  |FINSI;
     |SI #0#38 = 8;   #0#39 = "AGOSTO    ";  |FINSI;
     |SI #0#38 = 9;   #0#39 = "SEPTIEMBRE";  |FINSI;
     |SI #0#38 = 10;  #0#39 = "OCTUBRE   ";  |FINSI;
     |SI #0#38 = 11;  #0#39 = "NOVIEMBRE ";  |FINSI;
     |SI #0#38 = 12;  #0#39 = "DICIEMBRE ";  |FINSI;
     |SI #0#38 = 99;  #0#39 = "ANUAL     ";  |FINSI;

     fFecha = ~;
     aAlfa  = fFecha % -104;
     #0#40  = aAlfa;

     |PINTA #0#39;
     |PINTA #0#40;

     |C_M #0#44, 1, "S";
     |SI #0#38 = 99;
         |C_M #0#44, 1, "N";  #0#44 = "N";  |PINTA #0#44;
     |FINSI;
|FPRC;

|PROCESO Modelo66;  |TIPO 66;
     |ABRE #199;
     #199#0 = #0Campo;
     |LEE 040#199=;
     |SI FSalida ! 0;  |DDEFECTO #199;  |FINSI;

     |CONSULTA_CLAVE #1#199;
     |SI FSalida = 0;
         #0Campo = #199#0;   |PINTA #0Campo;
     |FINSI;
     |CIERRA #199;
|FPRC;

|PROCESO Modelo;
     nCampo1 = Campo + 1;
     |SI #0Campo = 0;
         #0nCampo1 = "";
         |PINTA #0nCampo1;
         |ACABA;
     |FINSI;

     |ABRE #199;
     #199#0 = #0Campo;
     |LEE 040#199=;
     |SI FSalida ! 0;  |DDEFECTO #199;  |FINSI;
     |CIERRA #199;

     |SI #0Campo = 180;  |O #0Campo = 190;  |O #0Campo = 390;  |O #0Campo = 392;
      |O #0Campo = 100;  |O #0Campo = 101;  |O #0Campo = 714;  |O #0Campo = 201;
      |O #0Campo = 200;  |O #0Campo = 193;  |O #0Campo = 184;  |O #0Campo = 296;
      |O #0Campo = 187;
         #199#4 = "S";
     |FINSI;

     |SI #199#4 ! "S";
         |MENAV "     Modelo sin Activar.";
         |ERROR;
         |ACABA;
     |FINSI;

     |SI #0#40 ] 2014;
         |SI #0Campo = 310; |O #0Campo = 370;
             |MENAV "0000Modelo no vigente a partir del Ejercicio 2014";
             |ERROR;
             |ACABA;
         |FINSI;
     |FINSI;

     |SI #0Campo = 347;  |O #0Campo = 180;  |O #0Campo = 190; |O #0Campo = 390;
      |O #0Campo = 392;  |O #0Campo = 100;  |O #0Campo = 101; |O #0Campo = 714;
      |O #0Campo = 201;  |O #0Campo = 200;  |O #0Campo = 193; |O #0Campo = 184;
      |O #0Campo = 296;  |O #0Campo = 187;
         |SI #0#38 ! 99;
             |MENAV "0000 El Periodo del Modelo no corresponde con el Periodo Seleccionado.";
             |ERROR;
         |FINSI;
     |FINSI;

     #0nCampo1 = #199#1;  |PINTA #0nCampo1;
|FPRC;

|PROCESO PorResultado;
   aAlfa = "S";
   |SI #dsmoy022#78 = "S";
       #dsmoy022#44 = "N";
       |PINTA #dsmoy022#44;
       aAlfa = "N";
   |FINSI;
   |C_M #dsmoy022#44,1,aAlfa;
|FPRC;

|| ///////////////////////////////////
|PROCESO AsignaCabecera;
     aAlfa = "A1," + aTitulo;
     |ALFACALLDLL "agixl97.dll","poke_dato", aAlfa;

     aAlfa = "B2, PERIODO : " + (("00" + #0#38) % -102) + " " + #0#39 + "  " +  #0#40;
     |ALFACALLDLL "agixl97.dll", "poke_dato", aAlfa;
|FPRC;

|PROCESO CargaVariables;
     |SI #0#43 ! "T";    ||no elige todos
         |SI #0#43 = "P";
             nSw = 0;
             |SI #99#17 ! "PENDIENTE ";  |ACABA;  |FINSI;
         |FINSI;
         |SI #0#43 = "I";
             nSw = 0;
             |SI #99#17 ! "IMPRESO   ";  |ACABA;  |FINSI;
         |FINSI;
         |SI #0#43 = "R";
             nSw = 0;
             |SI #99#17 ! "REMESADO  ";  |ACABA;  |FINSI;
         |FINSI;
         |SI #0#43 = "A";
             nSw = 0;
             |SI #99#17 ! "ARCHIVADO ";  |ACABA;  |FINSI;
         |FINSI;
         |SI #0#43 = "X";
             nSw = 0;
             |SI #99#17 = "ARCHIVADO ";  |ACABA;  |FINSI;
         |FINSI;
         |SI #0#43 = "E";
             nSw = 0;
             |SI #99#17 ! "ELABORADO ";
              |Y #99#17 ! "CALCULADO ";
              |Y #99#17 ! "COMPLETADO";
              |Y #99#17 ! "CON DATOS ";
                 |ACABA;
             |FINSI;
         |FINSI;
     |FINSI;


     |SI #0#42 = 3;
        |SI nAntAgen ! #dsmow038#18;
           |SI nPrimero = 0;
              nPrimero = 1;
           |SINO;
              |SI #0#41 = "I";
                 |ALFACALLDLL "agixl97.dll","fin_book", "imprime";
              |SINO;
                 |ALFACALLDLL "agixl97.dll","fin_book", "";
              |FINSI;

              aAlfa = eaUnidadBasico + "\MODELOS\DOCUMENTOS\" + aDocumento;
              |ALFACALLDLL "agixl97.dll", "carga_book", aAlfa;
              |ALFACALLDLL "agixl97.dll", "selecciona_hoja", "Intro";

              |HAZ AsignaCabecera;
              nEmpresas = 3;
           |FINSI;

           aAlfa = "A2,AGENTE : " + (("00" + #99#18) % -102) + " " + #99#19;
           |ALFACALLDLL "agixl97.dll", "poke_dato", aAlfa;

           nAntAgen = #dsmow038#18;
        |FINSI;
     |FINSI;

     nEmpresas  = nEmpresas  + 1;

     |SI #0#77 = "N"
         aAlfa     = "A" + nEmpresas + "," + (("00000" + #99#0) % -105) + " " + #99#2;
     |SINO;
         aAlfa     = "A" + nEmpresas + "," + (("00000" + #99#0) % -105) + " " + #99#2 + " " + #99#20;
     |FINSI;
     |ALFACALLDLL "agixl97.dll","poke_dato",aAlfa;

     |SI #dsmoy022#78 = "S"; |Y #99#21 ! 0;
         aAlfa     = "B" + nEmpresas + "," + #99#21;
         |ALFACALLDLL "agixl97.dll","poke_dato",aAlfa;
     |FINSI;

     |SI #dsmoy022#78 = "N"; |Y #99#1 ! 0;
         aAlfa     = "B" + nEmpresas + "," + (("00" + #99#1) % -102) + " " + #99#3;
         |ALFACALLDLL "agixl97.dll","poke_dato",aAlfa;
     |FINSI;

     aAlfa     = "C" + nEmpresas + "," + #99#17;
     |ALFACALLDLL "agixl97.dll","poke_dato",aAlfa;
|FPRC;

|DEFBUCLE dsmow038C;
     #99#1;
     ;
     00000, 00;
     99999, 99;
     ;
     NULL, CargaVariables;
|FIN;

|DEFBUCLE dsmow038N;
     #99#2;
     ;
     "                                        ", 00;
     "zzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzz", 99;
     ;
     NULL, CargaVariables;
|FIN;

|DEFBUCLE dsmow038A;
     #99#3;
     ;
     #dsmoy022#75, 00000, 00;
     #dsmoy022#76, 99999, 99;
     ;
     NULL, CargaVariables;
|FIN;

|PROCESO CopiaFichero;
     aAlfa = eaUnidadBasico + "\MODELOS";                   |RMKDIR aAlfa;
     aAlfa = eaUnidadBasico + "\MODELOS\DOCUMENTOS";        |RMKDIR aAlfa;

     aOrigen   = aBase + "basica/word/" + aDocumento;
     aDestino  = eaUnidadBasico + "/MODELOS/DOCUMENTOS/" + aDocumento;

     aIPRemoto = "";
     |IP_REMOTO aIPRemoto;  |QBT aIPRemoto;

     |SI aIPRemoto ! "";
         |ENVIA_FICHERO aOrigen, aDestino;
     |SINO;
         |COPIA_FICHERO aOrigen, aDestino;
     |FINSI;
|FPRC;

|PROCESO EmiteDocumento;
     |HAZ CopiaFichero;

     |CARGADLL "agixl97.dll";
     |SI FSalida < 0;  |ACABA;  |FINSI;

     aAlfa = eaUnidadBasico + "\MODELOS\DOCUMENTOS\" + aDocumento;
     |ALFACALLDLL "agixl97.dll", "carga_book", aAlfa;
     |ALFACALLDLL "agixl97.dll", "selecciona_hoja", "Intro";

     |HAZ AsignaCabecera;

     |MENSA "      Cargando Campos al Documento, espere un Momento.";

     nEmpresas = 3; nPrimero = 0;
     |SI #0#42 = 1; |BUCLE dsmow038C; |FINSI;
     |SI #0#42 = 2; |BUCLE dsmow038N; |FINSI;
     |SI #0#42 = 3; |BUCLE dsmow038A; |FINSI;

     |MENSA "      Imprimiendo el Documento, espere un Momento.";

     |SI #0#41 = "I";
         |ALFACALLDLL "agixl97.dll","fin_book", "imprime";
     |SINO;
         |ALFACALLDLL "agixl97.dll","fin_book", "";
     |FINSI;

     |DESCARGADLL "agixl97.dll";

     |RFBORRA aDestino;
|FPRC;

|PROCESO dsmom008;
     #114#0 = #8#0;
     #114#1 = #8#5;
     |LEE 040#114=;
     |SI FSalida ! 0;  |DDEFECTO #114;  |FINSI;

     |DDEFECTO #99;
     #99#0  = #8#0;
     #99#1  = #8#5;
     |LEE 141#99=;
     nEstado = FSalida;

     #99#0  = #8#0;
     #99#1  = #8#5;
     #99#2  = #715#1;
     #99#3  = #114#4;
     #99#20 = aDni;
     #99#17 = "PENDIENTE";
     |SI #8#11 = "COMPLETADO";
          #99#17 = "COMPLETADO";
     |FINSI;

     |SI #4#7  = "X";  #99#17 = "ELABORADO";  |FINSI;
     |SI #4#9  = "X";  #99#17 = "IMPRESO";    |FINSI;
     |SI #4#11 = "X";  #99#17 = "REMESADO";   |FINSI;
     |SI #4#13 = "X";  #99#17 = "ARCHIVADO";  |FINSI;

     aAlfa = "|FICHERO"; aAlfa = #715#aAlfa#; |QBF aAlfa;
     |DBASS aAlfa1; |QBF aAlfa1; aAlfa1 = aAlfa1 + "integral";
     aAlfa = aAlfa - aAlfa1;
     |SI FSalida ! 0;
        #99#18 = #715#72; || Codigo agente
        #99#19 = #715#52; || Nombre agente
     |FINSI;

     |SI nEstado ! 0;
         |GRABA 040#99n;
     |SINO;
          |GRABA 040#99a;
     |FINSI;
     |LIBERA #99;

     nGraba = 1;
|FPRC;

|DEFBUCLE dsmom008;
     #8#1;
     ;
     #4#0 , #4#1, #4#2, #4#3, #4#4, 01;
     #4#0 , #4#1, #4#2, #4#3, #4#4, 98;
     ;
     NULL, dsmom008;
|FIN;

|PROCESO dsmom004;
     enCodCli = #4#0;
     |HAZ LeePersona;
     |SI enSwDeDonde = 1;
         aDni = #715#3;
     |SINO;
         aDni = #715#13;
     |FINSI;

     nGeneral = 0;
     |SI #4#5 ! "EMPRESA   ";  |Y #4#5 ! "MANUAL    ";
         |SI #0#44 = "S";
             nGeneral = 1;
             |BUCLE dsmom008;
         |FINSI;
     |FINSI;

     |SI nGeneral = 1;  |ACABA;  |FINSI;

     |DDEFECTO #99;
     #99#0  = #4#0;
     #99#1  = 0;
     |LEE 141#99=;
     nEstado = FSalida;

     #99#0  = #4#0;
     #99#1  = 0;
     #99#2  = #715#1;
     #99#3  = "";
     #99#20 = aDni;
     #99#17 = "PENDIENTE";

     |SI #4#7  = "X";  #99#17 = "ELABORADO";  |FINSI;
     |SI #4#9  = "X";  #99#17 = "IMPRESO";    |FINSI;
     |SI #4#11 = "X";  #99#17 = "REMESADO";   |FINSI;
     |SI #4#13 = "X";  #99#17 = "ARCHIVADO";  |FINSI;

     aAlfa = "|FICHERO"; aAlfa = #715#aAlfa#; |QBF aAlfa;
     |DBASS aAlfa1; |QBF aAlfa1; aAlfa1 = aAlfa1 + "integral";
     aAlfa = aAlfa - aAlfa1;
     |SI FSalida ! 0;
        #99#18 = #715#72; || Codigo agente
        #99#19 = #715#52; || Nombre agente
     |FINSI;

     #99#21 = #dsmom004#15;

     |SI nEstado ! 0;
         |GRABA 040#99n;
     |SINO;
         |GRABA 040#99a;
     |FINSI;
     |LIBERA #99;

     nGraba = 1;
|FPRC;

|DEFBUCLE dsmom004;
     #4#1;
     ;
     #0#0, #0#40, #0#38, nCodModelo, 00;
     #0#1, #0#40, #0#38, nCodModelo, 99;
     ;
     NULL, dsmom004;
|FIN;

|PROCESO Sociedades;

    enEmpresa = #100#0;
    |HAZ ChequeaSociedad;
    |SI enError ! 0; |ACABA; |FINSI;

||    |SI #100#94 > "01.01.0000";  |Y #100#94 < fFechaChequeo;
||         |ACABA;
||    |FINSI;

     #200#0 = #100#0;
     #200#1 = #0#40;
     |LEE 040#200=;
     nEstado = FSalida;
     |SI FSalida ! 0;  |DDEFECTO #200;  |FINSI;

     #99#0  = #100#0;
     #99#1  = 0;
     |LEE 040#99=;
     |SI FSalida = 0;  |ACABA;  |FINSI;

     |DDEFECTO #99;
     #99#0  = #100#0;
     #99#1  = 0;
     #99#2  = #100#1;
     #99#3  = "";
     #99#20 = #100#13;
     |SI nEstado = 0;
         #99#17 = "CON DATOS";
     |SINO;
         #99#17 = "PENDIENTE";
     |FINSI;

     |SI #200#6 = "S";
         #99#17 = "IMPRESO";
     |FINSI;

     #99#21 = #200#8;

     |GRABA 020#99n;
     |LIBERA #99;

     nGraba = 1;
|FPRC;

|DEFBUCLE agifigen;
     #100#1;
     ;
     #0#0;
     #0#1;
     ;
     NULL, Sociedades;
|FIN;

|PROCESO Renta;
     #200#0 = #114#0;
     |LEE 040#200=;
     |SI FSalida ! 0;  |DDEFECTO #200;  |FINSI;

     #99#0  = #114#0;
     #99#1  = 0;
     |LEE 040#99=;
     |SI FSalida = 0;  |ACABA;  |FINSI;

     |DDEFECTO #99;
     #99#0  = #114#0;
     #99#1  = 0;
     #99#2  = #715#1;
     #99#20 = aDni;
     #99#3  = "";
     #99#17 = "PENDIENTE";

     |SI nCodModelo = 100;  |O nCodModelo = 101;
         |SI #200#12 = "*";
             |SI #200#14 = "INACTIVO  ";
                 #99#17  = "NO HACE";
             |SINO;
                 #99#17  = "CALCULADO";
             |FINSI;
             |SI #200#14 = "IMPRESO   ";
                 #99#17  = "IMPRESO";
             |FINSI;
         |FINSI;
     |FINSI;

     |SI nCodModelo = 714;
         |SI #200#12 = "*";
             |SI #200#15 = "INACTIVO  ";
                  #99#17 = "NO HACE";
             |SINO;
                  #99#17 = "ELABORADO";
             |FINSI;
             |SI #200#15 = "IMPRESO   ";
                  #99#17 = "IMPRESO";
             |FINSI;
         |FINSI;
     |FINSI;

     |GRABA 020#99n;
     |LIBERA #99;

     nGraba = 1;
|FPRC;

|PROCESO MiraModelos;
     enCodCli = #114#0;
     |HAZ LeePersona;
     |SI enSwDeDonde = 1;
         aDni = #715#3;
     |SINO;
         aDni = #715#13;
     |FINSI;

     |SI nCodModelo = 100;  |O nCodModelo = 101;  |O nCodModelo = 714;
         |SI #114#9 = 2;   |O #114#9 = 8;   |O #114#9 = 9;  |O #114#9 = 10;
          |O #114#9 = 11;  |O #114#9 = 12;  |O #114#0 = 18;
             |ACABA;
         |FINSI;

         |HAZ Renta;
     |FINSI;
|FPRC;

|DEFBUCLE agicen14;
     #114#1;
     ;
     #0#0, 01;
     #0#1, 99;
     ;
     NULL, MiraModelos;
|FIN;

|PROCESO ElProceso;
     |SI nCodModelo ! 100;  |Y nCodModelo ! 714;  |Y nCodModelo ! 201;
      |Y nCodModelo ! 101;  |Y nCodModelo ! 200;
         |ABRE #114;
         |BUCLE dsmom004;
         |CIERRA #114;
     |SINO;
         |SI nCodModelo = 100;  |O nCodModelo = 714;  |O nCodModelo = 101;

            aDef = aBase + "ren" + #0#40 + "/def/reacceso.mas";
            |CARGA_DEF aDef, referen@;
            |SI FSalida ! 0;
                |MENAV "      Renta no instalada.";
                |ACABA;
            |SINO;
                |ABRE #200;
                |BUCLE agicen14;
                |CIERRA #200;
            |FINSI;
         |FINSI;

         |SI nCodModelo = 200;  |O nCodModelo = 201;
             aDef = aBase + "contasoc/def/isacceso.mas";
             |CARGA_DEF aDef, referen@;
             |SI FSalida ! 0;
                 |MENAV "      Sociedades No Instaldo.";
                 |ACABA;
             |FINSI;

             |ABRE #200;
             |BUCLE agifigen;
             |CIERRA #200;
         |FINSI;

         |DESCARGA_DEF #referen@;
     |FINSI;
|FPRC;

|PROCESO MiraOrden;
   |SI #0#42 = 3;
      |C_M #dsmoy022#75, 1, "S"; |C_M #dsmoy022#76, 1, "S";
   |SINO;
      |C_M #dsmoy022#75, 1, "N"; |C_M #dsmoy022#76, 1, "N";
      #dsmoy022#75 = 0;  |PINTA #dsmoy022#75;
      #dsmoy022#76 = 99; |PINTA #dsmoy022#76;
   |FINSI;
|FPRC;

|PROCESO Tipo3;  |TIPO 3;
     |HAZ LeeUnidadBasico;

     |DBASS aBase;   |QBF aBase;

     |CARGADLL "agixl97.dll";
     |SI FSalida < 0;
         |MENAV "0000No tiene instalada el agixl97.dll, Consulte a su Distribuidor";
         |ACABA;
     |FINSI;
     |DESCARGADLL "agixl97.dll";

     |SUB_EJECUTA_NP ":basica/wordm000;CREALOS";

     aDocumento = "exce0002.xls";
     |SI #dsmoy022#78 = "S";
         aDocumento = "exce0005.xls";
     |FINSI;

     aFichero = ("w38" + Usuario + "zzzzzzzz") % 108;
     |NOME_DAT #99 aFichero;

     aAlfa1 = #0#40; aAlfa1 = "01.01." + aAlfa1;  fFechaChequeo  = aAlfa1;
     aAlfa1 = #0#40; aAlfa1 = "31.12." + aAlfa1;  fFechaChequeoF = aAlfa1;

     |PARA nModelo = 45;  |SI nModelo [ 73;  |HACIENDO nModelo = nModelo + 2;
           |SI #0nModelo ! 0;
               |ABRE #99;  |CIERRA #99;  |DELINDEX #99;

               nGraba     = 0;
               nModelo1   = nModelo + 1;
               nCodModelo = #0nModelo;
               aTitulo    = #0nModelo1;

               |ABRE #99;
               |SI #0#0 ! 0;
                   |HAZ ElProceso;
               |SINO;
                   |PARA nCampo = 2;  |SI nCampo [ 37;  |HACIENDO nCampo = nCampo + 1;
                         |SI #0nCampo ! 0;
                             #0#0 = #0nCampo;
                             #0#1 = #0nCampo;
                             |HAZ ElProceso;
                         |FINSI;
                    |SIGUE;
               |FINSI;
               |CIERRA #99;

               |SI nGraba = 1;
                   |HAZ EmiteDocumento;
               |FINSI;
           |FINSI;
     |SIGUE;
     |ABRE #99;  |CIERRA #99;  |DELINDEX #99;
|FPRC;
