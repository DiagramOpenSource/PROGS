|FICHEROS;
     dsmozp80 #0;

     dsmom002 #2;
     dsmom004 #4;
     dsmom008 #8;

     dsmom080 #80;
     dsmom081 #81;
     dsmom082 #82;
     dsmom083 #83;
     dsmom084 #84;

     dsmom085 #85;
     dsmom086 #86;

     dsmom048 #148;
     dsmom107 #107;
     dsmom109 #109;
     dsmociva #300;

     dsmow082 #982,MANTE;
     dsmow083 #983,MANTE;

     dsmom030 #30;

     :/contagen/def/canempre #101;
     :/contagen/def/camaniva #102;
     :/contagen/def/caivalin #103;
     :/contagen/def/caivaasi #104;

     :/basica/def/agifigen #1000;
     :/basica/def/agicen14 #1014;
     :/basica/def/agicen22 #1022;
|FIN;

|VARIABLES;
     &eaPathBasica = "";
     aBase       = "";
     aPathModelo = "";
     aDef        = "";
     &aFichw82   = "";
     aFichero    = "";
     aAlfa       = "";
     aAlfa1      = "";
     aAlfa2      = "";
     aAlfa3      = "";
     nNume1      = 0;
     nNume2      = 0;
     nPara1      = 0;
     nPara2      = 0;
     fFecha      = @;

     nModp80       = 0;
     nCampo        = 0;
     nDEmpresa     = 0;
     nHEmpresa     = 0;
     enGraba       = 0;
     nBaseImpo     = 0;
     nCuota        = 0;
     nDPeriodo     = 0;
     nHPeriodo     = 0;
     nEjer         = 0;

     nSwConPro   = 0;
     fFecIni     = @;
     fFecFin     = @;

     nSwRegEsp   = 0;
     nSwSubv     = 0;
     nSwContab   = 0;
     nSwExentas  = 0;
     nSwVarios   = 0;
     nSw         = 0;
     nRegIVA     = 0;
     nReg109     = 0;
     nNumAct     = 0;
     nDepar      = 0;
     nVentaCon   = 0;
     nVentaSin   = 0;
     nVentaSub   = 0;
     nVentaSuI   = 0;
     nVentaNue   = 0;
     nSwOpm81    = 0;
     nSwOpe82    = 0;
     nSwOpe83    = 0;
     nImporteMas = 0;
     nIvaSop     = 0;
     nIvaSub     = 0;

     nSwCon = 0;
     nSwSin = 0;

     nActSuma    = 0;
     aTipo       = "";
     aLit        = "";
     nActivMas   = 0;
     aCNAE       = "";
     &aTipoPr    = "";
     &nTp100     = 0;
     &nAny       = 0;

     nIvaComun   = 0;
      nSwBorro   = 0;
     &aParam     = "";
     nSwPrimAct  = 0;
     nEstado     = 0;
     aTipoAn1    = "";
     aTipoAn2    = "";
     nTipoAn1    = 0;
     nTipoAn2    = 0;

     nReg99      = 0;

     nActiv      = 0;
     nHay        = 0;
     &nSwHay84   = 0;
     nHayAlguno  = 0;

     &enEmpAmor  = 0;
     &enEjeAmor  = 0;
     &enDActAmor = 0;
     &enHActAmor = 0;
     &enModoAmor = 0;
     &enImpRegul = 0;
|FIN;

|INCLUYE i_variar;

|| /////////////////////// PROCESOS ENTRADA POR PANTALLA \\\\\\\\\\\\\\\\\\\\

|PROCESO Ejercicio;  |TIPO 7;
     fFecha = ~;
     aAlfa  = fFecha % -104;
     #0#0   = aAlfa; |SI #0#0 > 2010; #0#0 =2010; |FINSI;
     |PINTA #0#0;
     |SI aParam ! "";
         |PINTA 0603, "                                                           ";
         |PINTA 0703, "                                                           ";
         |ATRI R; |PINTA 0703, "Calcular el porcentaje de prorrata Provisional a Aplicar "; |ATRI 0;
     |FINSI;
|FPRC;

|PROCESO Parrilla;
     |SI #0#1 = 0;
         #0#1 = 0;  |PINTA #0#1;
         #0#2 = 0;  |PINTA #0#2;  |C_M #0#2, 1, "N";
         |PARA nCampo = 3;  |SI nCampo [ 14;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |SINO;
         |SI enEmpresa = 0;
             |C_M #0#1, 1, "S";
             |C_M #0#2, 1, "S";
         |FINSI;

         |PARA nCampo = 3;  |SI nCampo [ 14;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "N";
               #0nCampo = 0;  |PINTA #0nCampo;
         |SIGUE;
     |FINSI;
|FPRC;

|PROCESO NoMasEmpresas;
     |SI #0Campo = 0;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 14;  |HACIENDO nCampo = nCampo + 1;
               #0nCampo = 0;  |C_M #0nCampo, 1, "N";
         |SIGUE;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 14;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |FINSI;
|FPRC;

|| ... ... ... ... ... ... Ficheros Definitivos
|PROCESO Borra81;
  |BORRA 020#81a;
  |LIBERA #81;
|FPRC;

|DEFBUCLE Borra81;
 #81#1;
 ;
 #80#0, #80#1, 00;
 #80#0, #80#1, 99;
 be;
 NULL, Borra81;
|FIN;

|PROCESO BorraEl80;
     |ABRE #80;
     #80#0 = enEmpresa;
     #80#1 = #0#0;
     |LEE 001#80=;
     |SI FSalida = 0;
         |BUCLE Borra81;
         |BORRA 020#80a;
     |FINSI;
     |CIERRA #80;
|FPRC;

|| ////////////////////////////////////////////////////////// CALCULOS
|| .........................................................................
|| ................DATOS DESDE CONTABILIDAD ................................
|| .........................................................................

|PROCESO Grupo1;
    nSwSubv = 0;
    |SI #30#0 ] 800; |Y #30#0 [ 899; nSwSubv = 1; |FINSI;

    nBaseImpo = (#103#6 / enConversor) ! EURODBMOD;
    nCuota    = (#103#8 / enConversor) ! EURODBMOD;

    |SI #30#73 = "S";
        |SI nSwSubv = 0;
            |SI nCuota ! 0;  nVentaCon = nVentaCon + nBaseImpo;  |FINSI;
            |SI nCuota = 0;  nVentaSin = nVentaSin + nBaseImpo;  |FINSI;
        |SINO;
             nVentaSub = nVentaSub + nBaseImpo;  || concepto 804
        |FINSI;
    |FINSI;

|| ... aqui, puede ser 73 = "s" y 74 = "s", pero no es muy correcto

    |SI #30#74 = "S"; |Y nSwSubv = 1;  ||concepto 806 y 807
         nVentaSuI = nVentaSuI + nBaseImpo;
         nCuota  = (nBaseImpo % #30#75) ! EURODBMOD;
         nIvaSub = nIvaSub + nCuota;
    |FINSI;

    |SI #30#73 = "U"; |Y nSwSubv = 1;  || CONCEPTO NUEVO 808
        nVentaNue = nVentaNue + nBaseImpo;
    |FINSI;
|FPRC;

|PROCESO Grupo3;
    nBaseImpo = (#103#6 / enConversor) ! EURODBMOD;
    nCuota    = (#103#8 / enConversor) ! EURODBMOD;

    |SI #30#73 = "S";
        |SI nCuota ! 0;  nVentaCon = nVentaCon + nBaseImpo;  |FINSI;
        |SI nCuota = 0;  nVentaSin = nVentaSin + nBaseImpo;  |FINSI;
    |FINSI;
|FPRC;

|PROCESO LeeLinea;
     aAlfa1 = #103#30; |QBF aAlfa1; |SI aAlfa1 = ""; #103#30 = #102#10; |FINSI;
     |SI aMulDep = "S";
         nDepar = #103#30;
         |SI nDepar = 0; nDepar = 1; |FINSI;
         |SI enActividad ! nDepar; |ACABA; |FINSI;
     |FINSI;

     #30#0 = #103#3;
     |LEE 040#30=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |SI #30#4 = "N";  |ACABA;  |FINSI;

     |SI #30#0 ] 800; |Y #30#0 [ 899;
         |HAZ Grupo1;
         |ACABA;
     |FINSI;
     |SI #30#26 ! 0;   |HAZ Grupo1; |FINSI;   || regimen general
     |SI #30#30 ! 0;   |HAZ Grupo3; |FINSI;   || regimen simplificado

     nHay = 1;
|FPRC;

|DEFBUCLE caivalin;
     #103#1;
     ;
     #102#0, #102#1, 01;
     #102#0, #102#1, 99;
     ;
     NULL, LeeLinea;
|FIN;

|PROCESO LeeCabecera;

     |SI aMulDep ! "S";
         nDepar = #102#10;
         |SI nDepar = 0; nDepar = 1; |FINSI;
         |SI enActividad ! nDepar; |ACABA; |FINSI;
     |FINSI;

     |BUCLE caivalin;
|FPRC;

|DEFBUCLE camaniva;
     #102#1;
     5,36;
     00, 0000000000, nDPeriodo, "R";
     99, 9999999999, nHPeriodo, "S";
     e;
     NULL, LeeCabecera;
|FIN;

|PROCESO FicherosConta;

     |SI nSwPrimAct = 1;
         |SI enSwPartido = 1;
             |VETE Seguir;
         |FINSI;
     |FINSI;

     nSwContab = 0;
     enSwOpPar = 0;
     |HAZ SituaEmpresa;
     |SI enSwPartido = 0;
         |ABRE #101;
         |SELECT #4#101;
         #101#2  = enEmpresa;
         #101#26 = nEjer;
         |LEE 040#101=;
         |SI FSalida ! 0;  |CIERRA #101;  |ACABA;   |FINSI;
         |SELECT #1#101;
         |CIERRA #101;

         |DBASS aPathConta;   |QBT aPathConta;
         aPathConta = aPathConta + "contagen/fich/" + (("00000" + #101#2) % -105) + #101#3 + "/";

         enEmpresa  = #101#2;
         enPerConta = #101#3;
         eaLaMonEmp = #101#28;
     |SINO;
         enPerConta = enPeriodoPar;
         eaLaMonEmp = "E";
     |FINSI;
     |HAZ EnConversor;
|ET Seguir;
     |PATH_DAT #102 aPathConta;
     |PATH_DAT #103 aPathConta;
     |PATH_DAT #104 aPathConta;

     |ABRE #104;
     |LEE 040#104p;
     |SI FSalida ! 0;  |DDEFECTO #104;  |FINSI;
     aMulDep = #104#132;
     |CIERRA #104;

     |SI #104#46 = "T"; |Y  #0#0 [ 2009;
         nDPeriodo = 1;  nHPeriodo = 4;
     |SINO;
         nDPeriodo = 1;  nHPeriodo = 12;
     |FINSI;
     nSwContab = 1;
     #982#20 = enPerConta;
     nSwPrimAct = 1;
|FPRC;

|PROCESO dsmom048;
    nVentaSub = nVentaSub + #148#6;
    nVentaNue = nVentaNue + #148#7;

   |SI enActividad = 99;  nReg99 = 1;  |FINSI;
|FPRC;

|DEFBUCLE dsmom048;
    #148#1;
    ;
    enEmpresa, #0#0, 01, nModp80, 00, enActividad;
    enEmpresa, #0#0, 12, nModp80, 99, enActividad;
    ;
    NULL, dsmom048;
|FIN;

|PROCESO dsmociva;

  |SI #300#6 = 1; |O #300#6 = 3;
     nSwCon = 0;
     nSwSin = 0;
     |SI #300#6 = #109#6;  |Y #300#7 = #109#7;   nSwCon = 1; |FINSI;
     |SI #300#6 = #109#8;  |Y #300#7 = #109#9;   nSwCon = 1; |FINSI;
     |SI #300#6 = #109#10; |Y #300#7 = #109#11;  nSwCon = 1; |FINSI;
     |SI #300#6 = #109#12; |Y #300#7 = #109#13;  nSwCon = 1; |FINSI;
     |SI #300#6 = #109#14; |Y #300#7 = #109#15;  nSwCon = 1; |FINSI;
     |SI #300#6 = #109#39; |Y #300#7 = #109#40;  nSwCon = 1; |FINSI;
     |SI #300#6 = #109#41; |Y #300#7 = #109#42;  nSwCon = 1; |FINSI;
     |SI #300#6 = #109#43; |Y #300#7 = #109#44;  nSwCon = 1; |FINSI;

     |SI #300#6 = #109#16; |Y #300#7 = #109#17;  nSwSin = 1; |FINSI;
     |SI #300#6 = #109#18; |Y #300#7 = #109#19;  nSwSin = 1; |FINSI;
     |SI #300#6 = #109#20; |Y #300#7 = #109#21;  nSwSin = 1; |FINSI;
     |SI #300#6 = #109#22; |Y #300#7 = #109#23;  nSwSin = 1; |FINSI;
     |SI #300#6 = #109#24; |Y #300#7 = #109#25;  nSwSin = 1; |FINSI;
     |SI #300#6 = #109#45; |Y #300#7 = #109#46;  nSwSin = 1; |FINSI;
     |SI #300#6 = #109#47; |Y #300#7 = #109#48;  nSwSin = 1; |FINSI;
     |SI #300#6 = #109#49; |Y #300#7 = #109#50;  nSwSin = 1; |FINSI;

     |SI nSwCon = 1;
         |SI #300#6 = 1;
             |SI #300#15 ! 0;  nVentaCon = nVentaCon + #300#14; |FINSI;
             |SI #300#18 ! 0;  nVentaCon = nVentaCon + #300#17; |FINSI;
             |SI #300#21 ! 0;  nVentaCon = nVentaCon + #300#20; |FINSI;
             |SI #300#24 ! 0;  nVentaCon = nVentaCon + #300#23; |FINSI;
             |SI #300#27 ! 0;  nVentaCon = nVentaCon + #300#26; |FINSI;
             |SI #300#30 ! 0;  nVentaCon = nVentaCon + #300#29; |FINSI;
             |SI #300#33 ! 0;  nVentaCon = nVentaCon + #300#32; |FINSI;
         |SINO;
             nVentaCon = nVentaCon + #300#12;
         |FINSI;
     |FINSI;
     |SI nSwSin = 1;
         |SI #300#6 = 1;
             |SI #300#15 = 0;  nVentaSin = nVentaSin + #300#14; |FINSI;
             |SI #300#18 = 0;  nVentaSin = nVentaSin + #300#17; |FINSI;
             |SI #300#21 = 0;  nVentaSin = nVentaSin + #300#20; |FINSI;
             |SI #300#24 = 0;  nVentaSin = nVentaSin + #300#23; |FINSI;
             |SI #300#27 = 0;  nVentaSin = nVentaSin + #300#26; |FINSI;
             |SI #300#30 = 0;  nVentaSin = nVentaSin + #300#29; |FINSI;
             |SI #300#33 = 0;  nVentaSin = nVentaSin + #300#32; |FINSI;
         |SINO;
             nVentaSin = nVentaSin + #300#12;
         |FINSI;
     |FINSI;
   |FINSI;

   |SI #300#6 = 2; |Y #300#7 = 11;
       nVentaSuI =  nVentaSuI + #300#12;
       nIvaSub   =  nIvaSub   + #300#13;
   |FINSI;

   |SI enActividad = 99;  nReg99 = 1;  |FINSI;
   nHay = 1;
|FPRC;

|DEFBUCLE dsmociva;
    #300#1;
    ;
    enEmpresa, #0#0, 01, nModp80, 00, enActividad, 1, 00;
    enEmpresa, #0#0, 12, nModp80, 99, enActividad, 3, 99;
    ;
    NULL, dsmociva;
|FIN;

|PROCESO LeeElModelo;
 |SI #2#8 > "01.01.1900";
     |SI #2#8 > fFecFin;  |ACABA;  |FINSI;
 |FINSI;
 |SI #2#9 > "01.01.1900";
     |SI #2#9 < fFecIni;  |ACABA;  |FINSI;
 |FINSI;

 nModp80 = #2#2;
 |SI nModp80 = 300; |O nModp80 = 320; |O nModp80 = 330; |O nModp80 = 332;
  |O nModp80 = 303;
     |BUCLE dsmociva;
 |FINSI;
 |SI nModp80 = 310; |O nModp80 = 311;
     || ..falta el 310
 |FINSI;
 |SI nModp80 = 370;
     |BUCLE dsmociva;
     || ..falta el 310
 |FINSI;

 |BUCLE dsmom048;

|FPRC;

|DEFBUCLE LeeElModelo;
  #2#1;
  ;
  enEmpresa, enActividad, 300;
  enEmpresa, enActividad, 371;
  ;
  NULL, LeeElModelo;
|FIN;

|PROCESO ActivRegul99;  || Proceso nuevo para el calculo de la regul.

      nReg99   = 0;
      enActividad = 99;
      nVentaSub = 0;
      nVentaCon = 0;
      nVentaSin = 0;
      nVentaSuI = 0;
      nIvaSub   = 0;
      nVentaNue = 0;

      nModp80 = 300; |BUCLE dsmociva; |BUCLE dsmom048;
      nModp80 = 303; |BUCLE dsmociva; |BUCLE dsmom048;
      nModp80 = 320; |BUCLE dsmociva; |BUCLE dsmom048;
      nModp80 = 330; |BUCLE dsmociva; |BUCLE dsmom048;
      nModp80 = 332; |BUCLE dsmociva; |BUCLE dsmom048;
      nModp80 = 370; |BUCLE dsmociva; |BUCLE dsmom048;

      |SI nReg99 = 1;
          |ABRE #983;
          |DDEFECTO #983;
          #983#0 = #0#0;
          #983#1 = enEmpresa;
          #983#2 = 99;
          |LEE 101#983=;
          |SI FSalida ! 0;
              |DDEFECTO #983;
              #983#0  = #0#0;
              #983#1  = enEmpresa;
              #983#2  = 99;
              #983#3  = "REGISTRO DE REGULARIZACION";
              |SI #0#0 ] 2009;
                  #983#4  = #1014#20;
              |SINO;
                  #983#4  = #1014#21;
              |FINSI;
              #983#5  = #982#18;
              #983#9  = #982#18;
              #983#10 = "X";
              #983#11 = "REGULARIZACION";
              |GRABA 020#983b;
          |FINSI;
          #983#6  = #983#6  + nVentaCon;
          #983#7  = #983#7  + nVentaSin;
          #983#8  = #983#6  + #983#7;
          #983#15 = #983#15 + nVentaSub;
          #983#28 = #983#28 + nIvaSub;
          #983#29 = #983#29 + nVentaSuI;
          #983#32 = #983#32 + nVentaNue;
          |GRABA 020#983a;
          |CIERRA #983;

          #982#5  = #982#5  + nVentaCon;
          #982#6  = #982#6  + nVentaSin;
          #982#7  = #982#5  + #982#6;
          #982#9  = #982#9  + nVentaSub;
          #982#13 = #982#13 + nIvaSub;
          #982#16 = #982#16 + nVentaSuI;
          #982#22 = #982#22 + nVentaNue;
     |FINSI;
|FPRC;

|| ================================ Proceso Principal
|| \\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\
|PROCESO Aplicm084;
     nSwHay84 = 0;
     |ABRE #84;
     #84#0 = #982#1;
     #84#1 = #982#0;
     |LEE 001#84=;
     |SI FSalida = 0;
         aTipoPr = "G";  ||no procede calculo
         nTp100  = 100;
         nSwHay84 = 1;
     |FINSI;
     |CIERRA #84;
|FPRC;

|| \\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\
|PROCESO LeeElAux83;

  enActividad = #983#2;

  |SI nSwOpe83 = 0;   || = Calcular las Cuotas por Actividad

      |SI enActividad = 0; |O enActividad = 99; |ACABA; |FINSI;

      nVentaSub = 0;
      nVentaCon = 0;
      nVentaSin = 0;
      nVentaSuI = 0;
      nIvaSub   = 0;
      nVentaNue = 0;
      nHay      = 0;

      aAlfa1 = #983#30; |QBF aAlfa1;
      |SI aAlfa1 = "REGISTROS";

          |HAZ FicherosConta
          |SI nSwContab = 0;  |ACABA;  |FINSI;

          |ABRE #30;
          |BUCLE camaniva;
          |CIERRA #30;
          nIvaComun = 1;  || Tiene Contabilidad
      |SINO;

          |BUCLE LeeElModelo
      |FINSI;

      #983#6  = #983#6  + nVentaCon;
      #983#7  = #983#7  + nVentaSin;
      #983#8  = #983#6  + #983#7;
      #983#15 = #983#15 + nVentaSub;
      #983#28 = #983#28 + nIvaSub;
      #983#29 = #983#29 + nVentaSuI;
      #983#32 = #983#32 + nVentaNue;
      #983#33 = nHay;
      |GRABA 020#983a;

      #982#5  = #982#5  + nVentaCon;
      #982#6  = #982#6  + nVentaSin;
      #982#7  = #982#5  + #982#6;
      #982#9  = #982#9  + nVentaSub;
      #982#13 = #982#13 + nIvaSub;
      #982#16 = #982#16 + nVentaSuI;
      #982#22 = #982#22 + nVentaNue;
      nNumAct = nNumAct + 1;
  |FINSI;

  |SI nSwOpe83 = 1;   || = determina actividad Principal y otros detalles

      |SI enActividad = 99; |O enActividad = 0; |ACABA; |FINSI;
      |SI nSw = 0;
          nImporteMas = #983#8;
          nActivMas   = #983#2;
          nRegIVA     = #983#14;
          nSw = 1;
      |FINSI;
      |SI nImporteMas < #983#8;  || la actividad principal
          nImporteMas = #983#8;
          nActivMas   = #983#2;
      |FINSI;
      |SI nRegIVA ! #983#14;
          nSwVarios = 1;
          nRegIVA  = #983#14;
      |FINSI;

      |SI #983#14 = 3; |O #983#14 = 9; nSwRegEsp = 1; |FINSI;

      |SI #983#7 ! 0; |O #983#15 ! 0; nSwExentas = 1; |FINSI;

      |SI #983#28 ! 0; nSwSubv = 1; |FINSI;

      nNumAct = nNumAct + 1;        || todas las actividades

      |SI #983#33 = 1; nHay = 1; |FINSI;
 |FINSI;

 |SI nSwOpe83 = 2;   || Busca todas las complementarias de la Principal

     |SI enActividad = 99; |O enActividad = 0; |ACABA; |FINSI;
     |SI #983#2 = nDepar; |ACABA; |FINSI;

     |SI #983#13 = aCNAE;
         #983#10 = aTipo;
         #983#11 = aLit;
         #983#12 = nDepar;
     |SINO;
         #983#10 = "O";
         #983#11 = "OTRA";
     |FINSI;
     |GRABA 020#983a;

 |FINSI;

 |SI nSwOpe83 = 3;   || mira acumulados
     |SI #983#9 ! 100;
         nSwBorro = 0;
     |FINSI;
 |FINSI;
|FPRC;

|DEFBUCLE LeeElAux83;
 #983#1;
 ;
 #0#0, enEmpresa, 00;
 #0#0, enEmpresa, 99;
 ;
 NULL, LeeElAux83;
|FIN;

|PROCESO ActividadPrincipal;
 |ABRE #983;
 #983#0 = #0#0;
 #983#1 = enEmpresa;
 #983#2 = nDepar;
 |LEE 001#983=;
 |SI FSalida = 0;
     #983#10 = aTipo;
     #983#11 = aLit;
     #983#12 = nDepar;
     |GRABA 020#983a;
 |FINSI;
 |CIERRA #983;

 nSwOpe83 = 2;  ||Principal complementaria
 aCNAE   = #983#13;
 aLit    = "COMPLEMENTARIA";
 aTipo   = "C";
 |BUCLE LeeElAux83;
|FPRC;

|PROCESO GraboDatos;
    enEmpresa = #982#1;

    |SI nSwOpe82 = 0;   || Calculo de Los Importes de Ventas por actividad

        nSwPrimAct  = 0;
        enSwPartido = 0;

        #982#20   = -1;  || sin contabilidad
        nNumAct   = 0;
        nIvaComun = 0;
        nSwOpe83  = 0;
        |BUCLE LeeElAux83;

        |HAZ ActivRegul99;

        |GRABA 020#982a;
        |LIBERA #982;

        |SI nIvaComun = 1; |Y nNumAct > 1;
            |HAZ GraboIvaComun;
        |FINSI;

        |SI enSwPartido = 1;
            enSwOpPar   = 2;
            |HAZ FinalPartido;
        |FINSI;

        |ACABA;
    |FINSI;

    |SI nSwOpe82 = 1;   ||... Determinacion de la Prorrata

       || ... ... ... ...  Determinar la Actividad Principal, Si existe activ.
       ||                  en R.E.A.G Y P o R.E., y si ha cobrado subvenciones

        nSw         = 0;
        nSwRegEsp   = 0;  || Algun regimen especial
        nSwSubv     = 0;  || tiene subvenciones a bien concreto anterior
        nSwExentas  = 0;  || tiene operaciones exentas
        nSwVarios   = 0;  || varios Regimenes de IVA
        nNumAct     = 0;  || Numero de Actividades
        nSwOpe83    = 1;
        nImporteMas = 0;
        nActivMas   = 0;  || actividad principal
        nHay        = 0;
        |BUCLE LeeElAux83;

        nDepar     = nActivMas;
        aTipo      = "P";
        aLit       = "PRINCIPAL";
        |HAZ ActividadPrincipal;

        || ...................... Hasta Aqui tiene clasificadas las activ.

        |SI nNumAct = 1;   || Solo existe una actividad

            |SI nSwSubv = 0; |Y nSwExentas = 0;
                aTipoPr = "N";  ||no procede calculo
                nTp100  = 100;
            |SINO;
                aTipoPr = "G";
            |FINSI;

        |SINO;

            |SI nSwVarios = 0;
                |SI nSwSubv = 0; |Y nSwExentas = 0;
                    aTipoPr = "N"; || no procede Calculo
                    nTp100 = 100;
                |SINO;
                    aTipoPr = "G";
                |FINSI;
            |SINO;
                aTipoPr = #982#3;
            |FINSI;

        |FINSI;

        |SI nHay = 0;   ||no existe datos
            aTipoPr = "N";
            nTp100 = 100;
        |FINSI;

        |HAZ Aplicm084;

        #982#3  = aTipoPr;
        #982#17 = nNumAct;
        |HAZ CalNueProrr;
        #982#19 = "";
        |SI #982#3 = "E";
            aAlfa1  = #982#18; |QBF aAlfa1;
            #982#23 = " ESPECIAL ";
        |SINO;
            aAlfa1  = #982#4;  |QBF aAlfa1;
            #982#23 = " GENERAL  ";
        |FINSI;
        aAlfa1  = ("   " + aAlfa1) % -103;
        #982#19 = aAlfa1;
        |GRABA 020#982a;

    |FINSI;

    |SI nSwOpe82 = 2;
        nSwBorro = 0;
        |SI #982#13 = 0;
            |SI #982#3 = "N";
                nSwBorro = 1;                  ||  ... |HAZ BorraEl80;
            |FINSI;
            |SI #982#3 = "G";
                |SI #982#4 = 100;  nSwBorro = 1; |FINSI;
            |FINSI;
            |SI #982#3 = "E";
                nSwBorro = 1;
                nSwOpe83 = 3;
                |BUCLE LeeElAux83;
             |FINSI;
         |FINSI;
         |SI nSwBorro = 1;
            |SI aParam = "P";  || ... ... ... |HAZ BorraEl80;
                |SI #982#24 ! "E"; |Y #982#27 = 100;
                    #982#26 = "N";
                |FINSI;
            |SINO;
                |SI #982#24 ! "E"; |Y #982#25 ! "E"; |Y #982#27 = 100; |Y #982#28 = 100;
                    #982#26 = "N";
                |FINSI;
            |FINSI;
            |GRABA 020#982a;
        |FINSI;
    |FINSI;

|FPRC;

|DEFBUCLE dsmow082;
 #982#1;
 ;
 #0#0, 00001;
 #0#0, 99999;
 ;
 NULL, GraboDatos;
|FIN;

|PROCESO GraboIvaComun;
    |ABRE #983;
    |DDEFECTO #983;
    #983#0 = #0#0;
    #983#1 = enEmpresa;
    #983#2 = 00;   || iva comun el 00  22.03.2006
    |LEE 001#983=;
    |SI FSalida ! 0;
       |DDEFECTO #983;
       #983#0  = #0#0;
       #983#1  = enEmpresa;
       #983#2  = 00;
       #983#3  = "IVA DEDUCIBLE OPERACIONES COMUNES ";
       #983#4  = "";
       #983#5  = #982#18;
       #983#6  = #982#5;
       #983#7  = #982#6;
       #983#8  = #982#7;
       #983#9  = #982#18;
       #983#10 = "X";
       #983#11 = "COMUN";
       #983#15 = #982#9;
       #983#28 = #982#13;
       #983#29 = #982#16;
       #983#32 = #982#22;
       |GRABA 020#983c;
    |FINSI;
    |CIERRA #983;

|FPRC;


|| ========================================================================
|| ============= Primera Parte, ver que empresas tiene que aplicar Prorrata
|| ========================================================================
|| .... Busco en el ultimo registro del 036, si solicita la prorrata especial
|PROCESO Busco1022;
 aAlfa1 = #1022#142;
|FPRC;

|DEFBUCLE Busco1022;
 #1022#1;
 ;
 enEmpresa, 01;
 enEmpresa, 99;
 ;
 NULL, Busco1022;
|FIN;

|| ................. Grabo registro por Empresa
|PROCESO GraboAx82;
    aAlfa2 = "E";
    aAlfa1 = "S";
    |SI #109#2 = "N";
        aAlfa1 = "N";
        |BUCLE Busco1022;
        |SI aAlfa1 ! "S"; aAlfa1 = "N"; aAlfa2 = "G"; |FINSI;
    |FINSI;
    |SI nNumAct = 1; aAlfa2 = "G"; |FINSI;

    |DDEFECTO #982;
    #982#0  = #0#0;
    #982#1  = enEmpresa;
    #982#2  = #1000#1;
    #982#3  = aAlfa2;
    #982#8  = "S";
    #982#14 = aAlfa1;
    #982#15 = #1000#13;
    #982#17 = nNumAct;
    #982#24 = aTipoAn1;
    #982#25 = aTipoAn2;
    #982#26 = "S";
    #982#27 = nTipoAn1;
    #982#28 = nTipoAn2;
    |GRABA 000#982n;
    enGraba = 1;
|FPRC;

|| .................. Grabo Registro por Empresa-Actividad
|PROCESO GraboAux83;
    |DDEFECTO #983;
    #983#0 = #0#0;
    #983#1 = enEmpresa;
    #983#2 = nDepar;
    |LEE 001#983=;
    |SI FSalida ! 0;
       |DDEFECTO #983;
       #983#0 = #0#0;
       #983#1 = enEmpresa;
       #983#2 = nDepar;
       #983#3  = #1014#4;
       |SI #0#0 ] 2009;
           #983#4  = #1014#20;
           #983#13 = #1014#20;
       |SINO;
           #983#4  = #1014#21;
           #983#13 = #1014#21;
       |FINSI;
       #983#12 = nDepar;
       #983#14 = #1014#7;
       #983#16 = "Z";
       #983#19 = #1000#87;
       #983#20 = #1000#88;
       #983#21 = #1014#9;
       #983#22 = #1014#10;
       #983#23 = #1014#16;
       #983#24 = #1014#17;
       |SI #1014#5 > "01.01.1900"; #983#25 = #1014#5; |FINSI;
       |SI #1014#6 > "01.01.1900"; #983#26 = #1014#6; |FINSI;
       #983#27 = #1014#8;
       #983#30 = #1014#15;
       #983#31 = #1014#3;
       |GRABA 020#983c;
    |FINSI;
|FPRC;

|| *************************************************************
|PROCESO PrimerFiltro;  || Mas de una Actividad
 |SI #1014#5 > "01.01.1900";
     |SI #1014#5 > fFecFin;  |ACABA;  |FINSI;
 |FINSI;
 |SI #1014#6 > "01.01.1900";
     |SI #1014#6 < fFecIni;  |ACABA; |FINSI; || Baja
 |FINSI;

 |SI #1014#14 = "S"; |ACABA; |FINSI;  || dia 08.06.2005 comunero

 nDepar = #1014#1;
 |HAZ GraboAux83;
 nNumAct = nNumAct + 1;
|FPRC;

|DEFBUCLE LeeActividades;
 #1014#1;
 ;
 enEmpresa, 01;
 enEmpresa, 99;
 ;
 NULL, PrimerFiltro;
|FIN;

|| .... Mirar si hace el modelo de IVA
|PROCESO MiraSiD;
 |SI #2#8 > "01.01.1900";
     |SI #2#8 > fFecFin;  |ACABA;  |FINSI;
 |FINSI;
 |SI #2#9 > "01.01.1900";
     |SI #2#9 < fFecIni;  |ACABA;  |FINSI;
 |FINSI;
 |SI #2#2 = 309; |O #2#2 = 308; |ACABA; |FINSI;
 |SI #2#2 = 310; |O #2#2 = 311; |ACABA; |FINSI;
 |SI #2#2 = 300; |O #2#2 = 320; |O #2#2 = 330; |O #2#2 = 332; |O #2#2 = 370; |O #2#2 = 303;
     nSwConPro = 0;
     |ERROR10;
 |FINSI;
|FPRC;

|DEFBUCLE MiraSiDIVA;
 #2#1;
 ;
 enEmpresa, 01, 300;
 enEmpresa, 99, 370;
 ;
 NULL, MiraSiD;
|FIN;

|| ..........................................

|PROCESO MirarFiltros;
     enEmpresa = #1000#0;

     aTipoAn1 = "N"; nTipoAn1 = 100;
     aTipoAn2 = "N"; nTipoAn2 = 100;
     |ABRE #80;
     #80#0 = enEmpresa;
     #80#1 = enEjer;
     |LEE 001#80=;
     |SI FSalida = 0;
         aTipoAn1 = #80#2; nTipoAn1 = #80#3;
         aTipoAn2 = #80#4; nTipoAn2 = #80#5;
     |FINSI;

     |ABRE #84;
     #84#0 = #80#0;
     #84#1 = #80#1;
     |LEE 001#84=;
     |SI FSalida = 0; |Y #84#6 = "N";
         |CIERRA #80;
         |CIERRA #84;
         |ACABA;
     |FINSI;
     |CIERRA #84;

     nSwConPro = -1;
     |BUCLE MiraSiDIVA;  || Comprobar que hace algun modelo 300
     |SI nSwConPro = -1;
         #80#0 = enEmpresa;
         #80#1 = enEjer;
         |LEE 001#80=;
         |SI FSalida = 0; |BORRA 040#80a; |FINSI;
         |CIERRA #80;
         |ACABA;
     |FINSI;
     |CIERRA #80;

     nNumAct = 0;
     |ABRE #983;
     |BUCLE LeeActividades;   || Graba Actividades
     |CIERRA #983;

     |HAZ GraboAx82;
|FPRC;

|DEFBUCLE agifigen;
 #1000#1;
 ;
 nDEmpresa;
 nHEmpresa;
 ;
 NULL, MirarFiltros;
|FIN;

|| ========================================================================
|PROCESO HayAlguno;
 nHayAlguno = 1;
|FPRC;

|DEFBUCLE HayAlguno;
 #982#2;
 ;
 "S", #0#0, 00000;
 "S", #0#0, 99999;
 ;
 NULL, HayAlguno;
|FIN;

|PROCESO Sup180;
 #982#0 = #0#0;
 #982#1 = 0;
 #982#26 = "S";
|FPRC;

|PROCESO Inf180;
 #982#0 = #0#0;
 #982#1 = 99999;
 #982#26 = "S";
|FPRC;


|PROCESO Tipo3;  |TIPO 3;

     |SI aParam = "P"; #0#0 = #0#0 - 1; |FINSI;

     aAlfa = #0#0;
     aAlfa = aAlfa % -102;
     nEjer = aAlfa;       || ejercicio contable de canempre
     enEjer = #0#0;

     |HAZ LeeCtrlProrr;

     aAlfa1 = #0#0;
     aAlfa1 = "01.01." + aAlfa1; fFecIni = aAlfa1;
     aAlfa1 = #0#0;
     aAlfa1 = "31.12." + aAlfa1; fFecFin = aAlfa1;

     |ABRE #982;
     |SI #0#1 ! 0;
         nDEmpresa = #0#1;
         nHEmpresa = #0#2;
         |BUCLE agifigen;
     |SINO;
         |PARA nCampo = 3;  |SI nCampo [ 14;  |HACIENDO nCampo = nCampo + 1;
               nDEmpresa = #0nCampo;
               nHEmpresa = #0nCampo;
               |BUCLE agifigen;
         |SIGUE;
     |FINSI;
     |CIERRA #982;

     |SI enGraba = 1;

         nSwOpe82 = 0;
         |BUCLE dsmow082;  ||totales

         nSwOpe82 = 1;
         |BUCLE dsmow082;  ||Por actividades

         nSwOpe82 = 2;
         |BUCLE dsmow082;  ||Borro prorratas que no proceden

         nHayAlguno = 0;
         |BUCLE HayAlguno;  || Comprobar los que quedan
         |SI enSinPantalla = 0; |O enSinPantalla  ] 3;
             |SI nHayAlguno ! 0;
                 |PINPA #0#982;
                 |SI aParam = "P";
                     |ATRI R; |PINTA 0621, "Provisional"; |ATRI 0;
                 |FINSI;
                 |ENTLINEAL #2#982, -3, 4, 22, 0, Sup180, Inf180;
                 |SI FSalida = 0;
                     |CONFI "    SCALCULOS CORRETOS S/N";
                     |SI FSalida = 0;
                         |HAZ PasaDatosBuenos;
                     |FINSI;
                 |FINSI;
             |SINO;
                 |HAZ PasaDatosBuenos;
             |FINSI;
         |SINO;
              |HAZ PasaDatosBuenos;
         |FINSI;
     |SINO;
         |SI enSinPantalla = 0;
             |MENAV "     Seleccion Vacia.";
         |FINSI;
     |FINSI;

|FPRC;


|PROGRAMA;
     nActiv = enActividad;
     aParam = PARAMETRO; |QBF aParam;

     |ABRE #107;
     |DDEFECTO #107;
     |LEE 040#107p;
     |CIERRA #107;
     |SI #107#9 = "N";
         |ACABA;
         || .. acaba cuando no hay prorrata
     |FINSI;

     |DBASS aBase;  |QBF aBase;
     aAlfa = aBase + "basica/fich/";
     |PATH_DAT #1022 aAlfa;

     aFichw82 = ("82w" + Usuario) % 108;
     |NOME_DAT #982 aFichw82;
     aFichero = ("83w" + Usuario) % 108;
     |NOME_DAT #983 aFichero;

     |DELINDEX #982;
     |DELINDEX #983;

    |SI enEmpresa ! 0;
         |DDEFECTO #0;
         #0#0 = enEjer;
         #0#1 = enEmpresa;
         #0#2 = enEmpresa;
         |HAZ Tipo3;
    |SINO;
         |MANTE #0;
    |FINSI;

    |DELINDEX #982;
    |DELINDEX #983;

     enActividad = nActiv;
|FPRO;

|| .... .... ... .. .. ACTUALIZA FICHERO CORRECTO
|PROCESO Leem81;
 |SI nSwOpm81 = 0;
     |PDEFECTO #81, 6, 15;
     |GRABA 020#81a;
 |FINSI;
 |LIBERA #81;
|FPRC;

|DEFBUCLE Leem81;
 #81#1;
 ;
 #80#0, #80#1, 00;
 #80#0, #80#1, 99;
 be;
 NULL, Leem81;
|FIN;

|PROCESO ActActividad;
 #81#0 = #983#1;
 #81#1 = nAny;
 #81#2 = #983#2;
 |LEE 101#81=;
 |SI FSalida ! 0;
     |DDEFECTO #81;
     #81#0 = #983#1;
     #81#1 = nAny;
     #81#2 = #983#2;
     #81#3 = #983#3;
     #81#4 = #983#4;
     |GRABA 020#81b;
 |FINSI;
 #81#4 = #983#4;

 |SI nSwOpm81 = 0;
     #81#6  = #983#9;
     #81#7  = #983#6;
     #81#8  = #983#7;
     #81#10 = #983#15;
     #81#12 = #983#28;
     #81#15 = #983#29;
     #81#40 = #983#10;
     #81#41 = #983#32;
     #81#9  = #81#7 + #81#8 + #81#10 + #81#41;
 |FINSI;

 |SI nSwOpm81 = 1;
     #81#5  = #983#9;
 |FINSI;

 #81#40 = #983#10;
 |GRABA 020#81a;
 |LIBERA #81;

|FPRC;

|DEFBUCLE ActActividad;
 #983#1;
 ;
 #982#0, #982#1, 00;
 #982#0, #982#1, 99;
 ;
 NULL, ActActividad;
|FIN;

|PROCESO Leew82_Aux82;

 enEmpresa = #982#1;
 nAny      = #982#0;

 |SI aParam = "P";
     |VETE Provisional;
 |FINSI;

 #80#0 = enEmpresa;  || cliente
 #80#1 = nAny;       || a¤o
 |LEE 101#80=;
 nEstado  = FSalida;
 |SI nEstado ! 0;
     || ... ... ... ...      |SI #982#3 = "N"; |ACABA; |FINSI;
     |DDEFECTO #80;
     #80#0 = enEmpresa;
     #80#1 = nAny;
     |GRABA 020#80b;
 |FINSI;

 |SI #982#3 = "N"; || ... |Y nEstado = 0;
     #982#3 = "G";
     #982#4 = 100;
 |FINSI;
 #80#4 = #982#3;  || Definitivo
 #80#5 = #982#4;  || definitivo

 #80#6  = #982#5;
 #80#7  = #982#6;
 #80#9  = #982#9;
 #80#11 = #982#13;
 #80#14 = #982#8;
 #80#15 = #982#16;
 #80#16 = #982#17;
 #80#32 = #982#18; ||para comun
 #80#43 = #982#22;
 #80#8  = #80#6 + #80#7 + #80#9 + #80#43;
 |GRABA 020#80a;
 |LIBERA #80;

 nSwOpm81 = 0;
 |BUCLE Leem81;

 |ABRE #81;
 |BUCLE ActActividad;
 |CIERRA #81;
|| ------------------------------------------------------------------------

|ET Provisional;
 nAny = #982#0 + 1;

 |SI nAny [ 2010;
     #80#0 = #982#1;  || cliente
     #80#1 = nAny;    || a¤o
     |LEE 101#80=;
     |SI FSalida ! 0;
         |DDEFECTO #80;
         #80#0 = #982#1;
         #80#1 = nAny;
         |GRABA 020#80b;
     |FINSI;

     #80#2  = #982#3;
     #80#3  = #982#4;
     #80#14 = #982#8;
     #80#31 = #982#18; ||para comun
     |GRABA 020#80a;
     |LIBERA #80;

     nSwOpm81 = 1;
     |ABRE #81;
     |BUCLE ActActividad;
     |CIERRA #81;

     |SI enSinPantalla = 4; enSinPantalla = 1; |FINSI;
     |SI aParam ! "P";
         |SI #0#0 ] enEjerPro; |Y enSinPantalla < 2;
             enPeriodo = 12;
             |SUB_EJECUTA_NP "dsmozp81;todo";
         |FINSI;
         |SI enSinPantalla ] 2;
             enPeriodo = 12;
             |SUB_EJECUTA_NP "dsmozp81;";
             eaProrrat = #982#3;  || Definitivo
             nProrrata = #982#4;  || definitivo
         |FINSI;
     |FINSI;
 |SINO;
     nNume1 = enEjer;
     enEjer = nAny;
    |SUB_EJECUTA_NP "dsmozp03;A";
     enEjer = nNume1;
 |FINSI;
|FPRC;

|DEFBUCLE Leew82_Aux82;
 #982#1;
 ;
 #0#0, 00001;
 #0#0, 99999;
 ;
 NULL, Leew82_Aux82;
|FIN;

|PROCESO PasaDatosBuenos;
  |ABRE #80;
  |BUCLE Leew82_Aux82;
  |CIERRA #80;
|FPRC;
