|FICHEROS;
    dsmoz125 #0;

    dsmom040 #1;
    dsmom041 #2;
    dsmom042 #3;

    :/contagen/def/canempre #1000;
|FIN;

|VARIABLES;
   &enEmpresa  = 0;
   &enPerConta = 0;
   nNivel  = 0;
   nGrupo  = 0;
   UltGrupo  = 0;
   aCuenta = ""; aCuenta1 = ""; aCuenta2 = "";
   aAlfa   = "";
   aAlfa1  = "";
   Handle1 = 0; Posic1 = 0;
   Handle2 = 0; Posic2 = 0;
   Tecla   = "";

   aElemAnt = "        R";
   nLinea = 0;
   aLong = "";
   nLong = 0;
|FIN;

|PROCESO MiraGrupo;
   nGrupo = -1;
   |ABRE #dsmom040;
   #dsmom040#0 = #dsmoz125#2;
   #dsmom040#2 = 1;
   |LEE 000#dsmom040.];
   |SI FSalida = 0; |Y #dsmom040#0 = #dsmoz125#2;
      |PARA; |SI FSalida = 0; |Y #dsmom040#0 = #dsmoz125#2; |HACIENDO;
         |SI #dsmom040#5 = aCuenta1; |Y #dsmom040#3 = aCuenta2;
            nGrupo = #dsmom040#2; |SAL;
         |FINSI;
         UltGrupo = #dsmom040#2; |LEE 000#dsmom040.s;
      |SIGUE;
   |FINSI;

   |SI nGrupo = -1;
      |DDEFECTO #dsmom040;
      #dsmom040#0 = #dsmoz125#2;
      #dsmom040#2 = UltGrupo + 1;
      |GRABA 020#dsmom040.b;
      #dsmom040#1 = #dsmoz125#4;
      nGrupo = #dsmom040#2;
      aCuenta = aCuenta1;
      #dsmom040#3 = aCuenta1; |HAZ SacaNivel; #dsmom040#4 = nNivel;
      aCuenta = aCuenta2;
      #dsmom040#5 = aCuenta2; |HAZ SacaNivel; #dsmom040#6 = nNivel;
      aAlfa1 = "GRUPO NUMERO " + (("0000" + nGrupo) % -104);
      #dsmom040#7 = aAlfa1;
      #dsmom040#8 = #dsmoz125#3;
      |GRABA 020#dsmom040.a; |LIBERA #dsmom040;
   |FINSI;
|FPRC;

|PROCESO Recalculo;
   #dsmom041#0 = #dsmom042#0;
   #dsmom041#2 = #dsmom042#2;
   #dsmom041#3 = #dsmom042#3;
   |LEE 101#dsmom041.=;
   |SI FSalida = 0;
      |SI #dsmom041#14 = 0;
         #dsmom042#7  = 0;
      |SINO;
         #dsmom041#15 = #dsmom041#15 + #dsmom042#7;
         #dsmom041#16 = #dsmom041#16 - #dsmom042#7;
      |FINSI;
      |SI #dsmom041#20 = 0;
         #dsmom042#11 = 0;
      |SINO;
         #dsmom041#21 = #dsmom041#21 + #dsmom042#11;
         #dsmom041#22 = #dsmom041#22 - #dsmom042#11;
      |FINSI;
      #dsmom042#13 = #dsmom042#11 - #dsmom042#7;
      |GRABA 020#dsmom041.a; |LIBERA #dsmom041;
      |GRABA 020#dsmom042.a;
   |FINSI;
|FPRC;

|DEFBUCLE Recalculo;
#dsmom042#1;
;
#dsmoz125#2,"        ",00,002;
#dsmoz125#2,"zzzzzzzz",00,999;
be;
NULL,Recalculo
|FIN;

|PROCESO Importa;
   aAlfa = #dsmoz125#0; |QBF aAlfa;
   aAlfa = "rb  " + aAlfa;
   |FABRE aAlfa;
   |SI FSalida ] 0;
      Handle1 = FSalida;
      aAlfa = #dsmoz125#1; |QBF aAlfa;
      aAlfa = "rb  " + aAlfa;
      |FABRE aAlfa;
      |SI FSalida ] 0;
         Handle2 = FSalida;
         Pos = Posic1; aAlfa  = Handle1 + " " + 164; |FLEE aAlfa; Posic1 = Pos;
         |SI FSalida ! 0;
            |PARA; |SI FSalida ! 0; |HACIENDO;
               |DDEFECTO #dsmom041;
               aAlfa1 = ("00000" + #dsmoz125#2) % -105;     #dsmom041#0 = aAlfa1;
               #dsmom041#32 = #dsmoz125#3;
               aAlfa1 = ("00" + #dsmoz125#4) % -105;        #dsmom041#1 = aAlfa1;
               #dsmom041#33 = #dsmoz125#5;
               aAlfa1 = aAlfa % 0104;                       || Codigo Empresa
               aAlfa1 = aAlfa % 0504; |QBT aAlfa1; aAlfa1 = ("00000000" + aAlfa1) % -108;
               #dsmom041#2 = aAlfa1;                        || Codigo elemento
               aAlfa1 = "Tratando elemento " + aAlfa1;
               |ATRI I; |PINTA 2320, aAlfa1; |ATRI N;
               aAlfa1 = aAlfa % 0940;  #dsmom041#4 = aAlfa1;|| Descripcion elemento
               aAlfa1 = (aAlfa % 4902) + ".";
               aAlfa1 = aAlfa1 + (aAlfa % 5102) + ".";
               aAlfa1 = aAlfa1 + (aAlfa % 5304);            || Fecha Compra e inicio
               #dsmom041#9 = aAlfa1;   #dsmom041#27 = aAlfa1;
               aAlfa1 = aAlfa % 5712;
               aAlfa1 = aAlfa % 6912;
               aAlfa1 = aAlfa % 8112;
               #dsmom041#10 = aAlfa1;                       || Valor amortizable
               #dsmom041#11 = "N";     #dsmom041#24 = "A";
               #dsmom041#13 = "N";     #dsmom041#19 = "N";
               aAlfa1 = aAlfa % 9308;
               #dsmom041#5 = aAlfa1;                        || Cuenta Activo
               aCuenta = #dsmom041#12; |QBF aCuenta; |HAZ SacaNivel;
               #dsmom041#6 = nNivel;
               aAlfa1 = aAlfa % 10108; aCuenta  = aAlfa1;   || Cuenta Amort.Acumulada
               aAlfa1 = aAlfa % 10908; aCuenta1 = aAlfa1;   || Cuenta dotacion
               |HAZ MiraGrupo; #dsmom041#18 = nGrupo;
               aAlfa1 = aAlfa % 11705; #dsmom041#20 = aAlfa1;|| Coeficiente contable
               aAlfa1 = aAlfa % 12203; #dsmom041#23 = aAlfa1;|| A¤os amort.contable
               aAlfa1 = aAlfa % 12505; #dsmom041#14 = aAlfa1;|| Coeficiente fiscal
               aAlfa1 = aAlfa % 13003; #dsmom041#17 = aAlfa1;|| A¤os amort.fiscal
               #dsmom041#15 = 0;                            || Amortizacion acumulada Fiscal
               #dsmom041#16 = 0;                            || Valor Residual Fiscal
               #dsmom041#21 = 0;                            || Amortizacion acumulada Contable
               #dsmom041#22 = 0;                            || Valor residual Contable
               |SI #dsmom041#20 ! 0; #dsmom041#22 = #dsmom041#10; |FINSI;
               |SI #dsmom041#14 ! 0; #dsmom041#16 = #dsmom041#10; |FINSI;
               aAlfa1 = (aAlfa % 13402) + ".";
               aAlfa1 = aAlfa1 + (aAlfa % 13602) + ".";
               aAlfa1 = aAlfa1 + (aAlfa % 13804); #dsmom041#26 = aAlfa1; || Fecha Venta
               aAlfa1 = aAlfa % 14221; #dsmom041#12 = aAlfa1;   || Lugar destino
               |GRABA 020#dsmom041.c;

               || Grabo en este mismo momento la linea de compra del elemento
               |DDEFECTO #dsmom042;
               aAlfa1 = ("00000" + #dsmoz125#2) % -105;     #dsmom042#0 = aAlfa1;
               aAlfa1 = ("00" + #dsmoz125#4) % -102;        #dsmom042#1 = aAlfa1;
               #dsmom042#2 = #dsmom041#2;
               #dsmom042#4 = 1;
               aAlfa1 = #dsmom041#27; |QBF aAlfa1; aAlfa1 = aAlfa1 % -104;
               #dsmom042#5 = aAlfa1;
               #dsmom042#6 = "C";
               |SI #dsmom041#14 ! 0;
                  #dsmom042#7  = #dsmom041#10;
               |FINSI;
               |SI #dsmom041#20 ! 0;
                  #dsmom042#11 = #dsmom041#10;
               |FINSI;
               #dsmom042#8 = "N";
               #dsmom042#9 = #dsmom041#27;
               #dsmom042#10 = "Compra"; #dsmom042#12 = 1;
               #dsmom042#13 = #dsmom042#11 - #dsmom042#7;
               |GRABA 020#dsmom042.n;

               Pos = Posic1; aAlfa  = Handle1 + " " + 164; |FLEE aAlfa; Posic1 = Pos;
            |SIGUE;
         |FINSI;
         Pos = Posic2; aAlfa  = Handle2 + " " + 22; |FLEE aAlfa; Posic2 = Pos;
         |SI FSalida ! 0;
            |PARA; |SI FSalida ! 0; |HACIENDO;
               |DDEFECTO #dsmom042;
               aAlfa1 = ("00000" + #dsmoz125#2) % -105;     #dsmom042#0 = aAlfa1;
               aAlfa1 = ("00" + #dsmoz125#4) % -102;        #dsmom042#1 = aAlfa1;
               aAlfa1 = aAlfa % 0104; |QBT aAlfa1; aAlfa1 = ("00000000" + aAlfa1) % -108;
               #dsmom042#2 = aAlfa1;
               |SI aElemAnt ! aAlfa1;
                  nLinea = 2; aElemAnt = aAlfa1;
               |FINSI;
               aAlfa1 = "Tratando linea " + nLinea + " del elemento " + aAlfa1;
               |ATRI I; |PINTA 2320, aAlfa1; |ATRI N;
               #dsmom042#4 = nLinea; nLinea = nLinea + 1;
               aAlfa1 = aAlfa % 0504;
               #dsmom042#5 = aAlfa1;
               #dsmom042#6 = "A";
               aAlfa1 = aAlfa % 0912;
               #dsmom042#7  = aAlfa1;
               #dsmom042#11 = aAlfa1;
               aAlfa1 = #dsmom042#5;
               #dsmom042#10 = "AMORT.EJERCICIO " + aAlfa1;
               aAlfa1 = "31.12." + aAlfa1;
               #dsmom042#9 = aAlfa1;
               #dsmom042#12 = 1;
               |GRABA 020#dsmom042.n;
               Pos = Posic2; aAlfa  = Handle2 + " " + 22; |FLEE aAlfa; Posic2 = Pos;
            |SIGUE;
         |FINSI;
      |FINSI;
   |FINSI;
   FSalida = Handle2; |FCIERRA FSalida;
   FSalida = Handle1; |FCIERRA FSalida;
   |BUCLE Recalculo;
|FPRC;

|PROCESO SacaNivel;
   |QBF aCuenta; nNivel = 0;
   aLong = aCuenta % 0; nLong = aLong;
   |SI nLong = #canempre#14; nNivel = 1; |FINSI;
   |SI nLong = #canempre#15; nNivel = 2; |FINSI;
   |SI nLong = #canempre#16; nNivel = 3; |FINSI;
   |SI nLong = #canempre#17; nNivel = 4; |FINSI;
   |SI nLong = #canempre#18; nNivel = 5; |FINSI;
   |SI nLong = #canempre#19; nNivel = 6; |FINSI;
|FPRC;

|PROCESO ChequeaFich;
   aAlfa = #dsmoz125#Campo#; |QBF aAlfa;
   |XABRE "E",aAlfa,Handle1;
   |SI FSalida < 0;
      |MENAV "    No se encuentra el fichero";
      |ERROR; |ACABA;
   |FINSI;
|FPRC;

|PROCESO Empresa;
   enPerConta = -1; enEmpresa = #0#2; |HAZ SituaContagen;
   |SI enPerConta ] 0;
      #0#3 = #canempre#1; |PINTA #0#3;
   |SINO;
      |ERROR;
   |FINSI;
|FPRC;

|PROGRAMA;
   |ABRE #dsmoz125;
   |LEE 000#dsmoz125.p;
   |SI FSalida ! 0;
      |DDEFECTO #dsmoz125;
      |GRABA 020#dsmoz125.c;
   |FINSI;
   |CLS;
   |CABEZA "Importacion amortizaciones tipo A";
   |PINPA #0#0; |PINDA #0#0;
   |ENDATOS #1#0;
   |SI FSalida = 0;
      |ABRE #dsmom041; |ABRE #dsmom042;
      |HAZ Importa;
      |CIERRA #dsmom042; |CIERRA #dsmom041;
   |FINSI;
   |GRABA 020#dsmoz125.a;
   |CIERRA #dsmoz125;
|FPRO;
