|FICHEROS;
   dsmom056 #56;
   :/modelos/def/dsmom100 #100;
   :/contagen/def/cacuenta #2;
   :/contagen/def/caw00098 #901,MANTE;
   referen@ #1000;
|FIN;

|VARIABLES;
   aAlfa1  = "";
   nCuenta = 0;
   nNume1  = 0;
   nNume2  = 0;
   nNume3  = 0;
   nNume4  = 0;
   nModoEntrada = 0;
   nTecla  = 0;
   nUltCod = 0;

   nCasilla   = 0;
   aMas       = "";
   &eaTabMod  = "";
   aQueQuiero = "";
   aTitulo    = "";
   aNumCampos = "";
   nNumCampos = 0;
   nCampos    = 0;
   nCampo     = 0;
   aNombCampo = "";
   aTipoCampo = "";
   aLongCampo = "";
   aDirInicio = "";
    aFichero  = "";
    nContador = 0;
   nNoClave   = 0;
    aDirBase  = "";
|FIN;

|INCLUYE i_variar;
|INCLUYE i_varcon;

|CALCULO eldefecto; |TIPO 7;
 nModoEntrada = (FEntrada / 100) ! 0;
 |SI nModoEntrada = 1;
     #dsmom056#1 = #dsmom056#0 - 1; |PINTA #dsmom056#1;
 |FINSI;
|FCAL;

|CALCULO quetipo; |TIPO 0
 nNume1 = Campo + 11;
 nNume2 = Campo + 22;
 nNume3 = nNume1 + 22;
 nNume4 = nNume2 + 22;
 aAlfa1 = #dsmom056#Campo#;
 |C_M #dsmom056#nNume1#,1,"N";
 |C_M #dsmom056#nNume2#,1,"N";
 |C_M #dsmom056#nNume4#,1,"N";
 |SI aAlfa1 = "I";
     |C_M #dsmom056#nNume1#,1,"S";
     #dsmom056#nNume2# = ""; |PINTA #dsmom056#nNume2#;
     #dsmom056#nNume4# = ""; |PINTA #dsmom056#nNume4#;
 |FINSI;
 |SI aAlfa1 = "C";
     |C_M #dsmom056#nNume2#,1,"S";
     |C_M #dsmom056#nNume4#,1,"S";
     #dsmom056#nNume1# = 000; |PINTA #dsmom056#nNume1#;
 |FINSI;
 |SI aAlfa1 = "M";
     #dsmom056#nNume1# = 000; |PINTA #dsmom056#nNume1#;
     #dsmom056#nNume2# = "";  |PINTA #dsmom056#nNume2#;
     #dsmom056#nNume4# = "";  |PINTA #dsmom056#nNume4#;
 |FINSI;
 |SI #dsmom056#nNume1# = 0;
     #dsmom056#nNume3# = "";  |PINTA #dsmom056#nNume3#;
 |FINSI;
|FCAL;

|PROCESO LeeCodTab;
 nUltCod = 0;
 |ABRE #901;
 #901#0 = enModelo;
 #901#1 = nCasilla;
 |LEE 001#901=;
 |SI FSalida ! 0;
     nUltCod = 1;
 |FINSI;
 |CIERRA #901;
|FPRC;

|CALCULO TrasImpuesto; |TIPO 0;
 nTecla = FSalida;
 nCampo = Campo;
 |C_M #dsmom056#Campo#,0,aAlfa1;
 |SI aAlfa1 = "N"; |ACABA; |FINSI;

 |SI nTecla = 10;
     |HAZ TablaImpuesto;
 |FINSI;

 nCasilla = #dsmom056#nCampo#;
 enModelo = 200;
 |HAZ LeeCodTab;
 |SI nUltCod = 1;
     |ERROR;
 |SINO;
     nNume1 = nCampo + 22;
     #dsmom056#nNume1# = #901#2;
     |PINTA #dsmom056#nNume1#;
 |FINSI;
|FCAL;

|CALCULO Cuentaf; |TIPO 6;
   |C_M #dsmom056#Campo#,0,aAlfa1;
   |SI aAlfa1 = "N"; |ACABA; |FINSI;

      emCta = 0;        || Dar mensajes de error
     eaCuenta = #dsmom056#Campo#;
     salidas  = FSalida;
     |HAZ FiltraPuntos;
     #dsmom056#Campo# = eaCuenta;
     |PINTA #dsmom056#Campo#;

     |HAZ SacaNivel;
     |SI eswCta = 1;
         |MENAV "    Longitud Fuera de Nivel ";
     |FINSI;
|FCAL;

|RUTINA inf2;
   #2#0 = "            ";
|FRUT;

|RUTINA sup2;
   #2#0 = "zzzzzzzzzzzz";
|FRUT;

|CALCULO Refrescaf; |TIPO 0;
   |C_M #dsmom056#Campo#,0,aAlfa1;
   |SI aAlfa1 = "N"; |ACABA; |FINSI;

   |SI FSalida = 10;
      |ABRE #2;
      |CONSULTA_F_CLAVE #1#2,inf2,sup2;
      #dsmom056#Campo# = #2#0; |PINTA #dsmom056#Campo#;
      |ERROR;
      |CIERRA #2;
   |FINSI;
   emCta = 0;        || Dar mensajes de error
   eaCuenta = #dsmom056#Campo#;
   |HAZ LeeCuenta;
   |SI eswCta = 1;
       |MENAV "    No existe Cuenta Contable";
   |SINO;
       |SI eswCta = 2;
           |MENAV "    Esta Cuenta NO tiene pase Directo ";
       |FINSI;
   |FINSI;
|FCAL;

|CALCULO TrasSigno; |TIPO 0;
  |C_M #dsmom056#Campo#,0,aAlfa1;
  |SI aAlfa1 = "N"; |ACABA; |FINSI;

  |SI #dsmom056#Campo# = " ";
      #dsmom056#Campo# = "D"; |PINTA #dsmom056#Campo#;
  |FINSI;
|FCAL;

|RUTINA ClauBaja56;
 #56#0 = 1900;
|FRUT;

|RUTINA ClauAlta56;
 #56#0 = 2008;
|FRUT;

|PROGRAMA;
 || ..........................................
 nCuenta = 0;
 |ABRE #100;
 |LEE 040#100p;
 |SI FSalida ! 0;
     |MENAV "    No esta configurada las Empresas Maestras de Contabilidad";
     nCuenta = 1;
 |FINSI;
 |CIERRA #100;
  enEmpresa  = #100#1;
  enPerConta = #100#2;
  |HAZ LeeEmpConta;
  |PATH_DAT #2 aPathConta;

 |HAZ CargaTablaAux;
 |CLS;
 |CABEZA "Control Rendimientos Trans. Fiscal ";
 |ENTLINEAL #1#56, -1, 2, 22, 1, ClauBaja56, ClauAlta56;
 |DELINDEX #901;
|FPRO;

|| ///// Carga de Auxiliar del fichero del Impuesto
|PROCESO CargaTemporal;
     aMas  = aDirInicio + "contasoc/def/" + aFichero;
     |CARGA_DEF aMas, referen@;
     |SI FSalida ! 0;
         |MENAV  "         Error en cargar el Def ... Proceso Abortado.";
         |ACABA;
     |FINSI;

     aQueQuiero = "|TITULO";
     aTitulo    = #referen@#aQueQuiero#;  |QBF aTitulo;

     nNoClave  = 2;
     #901#0    = nContador;
     #901#4    = aFichero;
     #901#5    = "contasoc";
     #901#6    = aTitulo;

     aQueQuiero = "|NC";
     aNumCampos = #referen@#aQueQuiero#;
     nNumCampos = aNumCampos;
     nNumCampos = nNumCampos - 1;
     |PARA nCampos = nNoClave;  |SI nCampos [ nNumCampos;  |HACIENDO nCampos = nCampos + 1;
           aQueQuiero = "|C" + (("000" + nCampos) % -103) + "NOMBRE";
           aNombCampo = #referen@#aQueQuiero#;

           aQueQuiero = "|C" + (("000" + nCampos) % -103) + "TIPO";
           aTipoCampo = #referen@#aQueQuiero#;

           |SI aTipoCampo = "N";
               aQueQuiero = "|C" + (("000" + nCampos) % -103) + "LONGITUD";
               aLongCampo = #referen@#aQueQuiero#;

               #901#1 = nCampos;
               #901#2 = aNombCampo;
               #901#3 = aLongCampo;
               |GRABA 020#901n;
               |LIBERA #901;
           |FINSI;
     |SIGUE;

     |DESCARGA_DEF #referen@;
|FPRC;

|PROCESO CargaTablaAux;
     eaTabMod = ("zm" + Usuario + "zzzzzzzzz") % 108;
     |NOME_DAT #901 eaTabMod;
     |ABRE #901;  |CIERRA #901;  |DELINDEX #901;

     |DBASS aDirInicio;
     |DBASE aDirBase;

     |ABRE #901;
     aFichero = "isocie59";  nContador = 200; |HAZ CargaTemporal;
     |CIERRA #901;
|FPRC;

|RUTINA ClInf901;
 #901#0 = enModelo;
 #901#1 = 1;
|FRUT;

|RUTINA ClSup901;
 #901#0 = enModelo;
 #901#1 = 999;
|FRUT;

|PROCESO TablaImpuesto;
 enModelo = 200;
 |PUSHV 0701 2157;
 nCasilla = #dsmom056#nCampo#;
 |PINPA #0#901;
 #901#0 = enModelo; |PINTA #901#0;
 |ENTLINEAL #1#901, -2, 4, 20, 0, ClInf901, ClSup901;
 |POPV;
 #dsmom056#nCampo# = nCasilla; |PINTA #dsmom056#nCampo#;
|FPRC;

|CALCULO wTip11; |TIPO 11;
 nTecla = FSalida;
 |SI nTecla = 1; |O nTecla = 0;
     nCasilla = #901#1;
 |FINSI;
|FCAL;

|CALCULO wTip5; |TIPO 0;
 nTecla = FSalida;
 |SI nTecla = 0;
     |PTEC 806;
     |PTEC 808;
 |FINSI;
|FCAL;
