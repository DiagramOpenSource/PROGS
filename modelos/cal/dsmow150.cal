|FICHEROS;
     dsmow150 #150,MANTE;
|FIN;

|VARIABLES;
     aMensa = "";
     aAlfa  = "";
     aAlfa1 = "";
     nNume1 = 0;

  {25}aPopup        = "";
     nID            = 0;
     aID            = "";
     aTexto         = "";
     nTexto         = 0;
     nCampo         = 0;
|FIN;

|INCLUYE i_variar;

|PROCESO Tipo12w150; |TIPO 12;
|FPRC;

|| ... Textos descripcion de las opciones
|PROCESO TextoC33;
     #150#34 = "DECLARADO";
     |SI #150#33 = "N";
         #150#34 = "DESACTIVADO";
     |FINSI;
     |PINTA #150#34;
|FPRC;

|PROCESO TextoC06;
     |SI #150#6 = "C"; aTexto = "Cuentas bancarias"; |FINSI;
     |SI #150#6 = "V"; aTexto = "Valores de la participación"; |FINSI;
     |SI #150#6 = "I"; aTexto = "Valores en fondos de inversión"; |FINSI;
     |SI #150#6 = "S"; aTexto = "Seguros de vida"; |FINSI;
     |SI #150#6 = "B"; aTexto = "Inmuebles"; |FINSI;
|FPRC;

|PROCESO TextoC07;
     |SI #150#6 = "C";
         |SI #150#7 = 1; aTexto = "Cuenta corriente    "; |FINSI;
         |SI #150#7 = 2; aTexto = "Cuenta de ahorro    "; |FINSI;
         |SI #150#7 = 3; aTexto = "Imposiciones a plazo"; |FINSI;
         |SI #150#7 = 4; aTexto = "Cuentas de crédito  "; |FINSI;
         |SI #150#7 = 5; aTexto = "Otras cuentas       "; |FINSI;
     |FINSI;
     |SI #150#6 = "V";
         |SI #150#7 = 1; aTexto = "Val.participación en entidad jurídica"; |FINSI;
         |SI #150#7 = 2; aTexto = "Val.por cesión de capitales a terceros"; |FINSI;
         |SI #150#7 = 3; aTexto = "Val.aportados gestión a cualquier ins.jur."; |FINSI;
     |FINSI;
     |SI #150#6 = "S";
         |SI #150#7 = 1; aTexto = "Seguros de vida o invalidez"; |FINSI;
         |SI #150#7 = 2; aTexto = "Rentas temporales o vitalícias"; |FINSI;
     |FINSI;
     |SI #150#6 = "B";
         |SI #150#7 = 1; aTexto = "Titularidad del inmueble"; |FINSI;
         |SI #150#7 = 2; aTexto = "Derechos reales de uso o disfrute"; |FINSI;
         |SI #150#7 = 3; aTexto = "Nuda propiedad"; |FINSI;
         |SI #150#7 = 4; aTexto = "Multipropiedad"; |FINSI;
         |SI #150#7 = 5; aTexto = "Otros derechos reales"; |FINSI;
     |FINSI;
|FPRC;

|PROCESO TextoC10;
     |SI #150#10 = 1; aTexto = "Identificación por ISIN"; |FINSI;
     |SI #150#10 = 2; aTexto = "Valores sin codigo ISN";  |FINSI;
|FPRC;

|PROCESO TextoC12;
     |SI #150#12 = "I"; aTexto = "Código IBAN"; |FINSI;
     |SI #150#12 = "O"; aTexto = "Otra identificación";|FINSI;
|FPRC;

|PROCESO TextoC24;
     |SI #150#24 = "A"; aTexto = "Bien que se declara la primera vez"; |FINSI;
     |SI #150#24 = "M"; aTexto = "Bien que ya ha sido declarado en ejercicios anteriores"; |FINSI;
     |SI #150#24 = "C"; aTexto = "Bien que se extingue la titularidad"; |FINSI;
|FPRC;

|PROCESO TextoC28;
     |SI #150#28 = "A"; aTexto = "Val.representados con anotaciones a cta"; |FINSI;
     |SI #150#28 = "B"; aTexto = "Val.no representados con anotaciones a cta";|FINSI;
|FPRC;

|PROCESO TextoC30;
     |SI #150#30 = "U"; aTexto = "Urbano";  |FINSI;
     |SI #150#30 = "R"; aTexto = "Rustico"; |FINSI;
|FPRC;

|PROCESO TextoC;
     aTexto = "";

     |SI nCampo = 6;  nTexto = 30; |HAZ TextoC06; |FINSI;
     |SI nCampo = 7;  nTexto = 42; |HAZ TextoC07; |FINSI;
     |SI nCampo = 10; nTexto = 25; |HAZ TextoC10; |FINSI;
     |SI nCampo = 12; nTexto = 25; |HAZ TextoC12; |FINSI;
     |SI nCampo = 24; nTexto = 50; |HAZ TextoC24; |FINSI;
     |SI nCampo = 28; nTexto = 42; |HAZ TextoC28; |FINSI;
     |SI nCampo = 30; nTexto = 10; |HAZ TextoC30; |FINSI;

     aAlfa1 = " " * nTexto;
     nNume1 = 100 + nTexto;
     aTexto = (aTexto + aAlfa1) % nNume1;
     aTexto = aTexto > " ";

     |SI nCampo = 6;  |ATRI S; |PINTA 1015, aTexto; |ATRI 0; |FINSI;
     |SI nCampo = 7;  |ATRI S; |PINTA 1141, aTexto; |ATRI 0; |FINSI;
     |SI nCampo = 10; |ATRI S; |PINTA 1339, aTexto; |ATRI 0; |FINSI;
     |SI nCampo = 12; |ATRI S; |PINTA 1639, aTexto; |ATRI 0; |FINSI;
     |SI nCampo = 24; |ATRI S; |PINTA 1329, aTexto; |ATRI 0; |FINSI;
     |SI nCampo = 28; |ATRI S; |PINTA 1834, aTexto; |ATRI 0; |FINSI;
     |SI nCampo = 30; |ATRI S; |PINTA 2127, aTexto; |ATRI 0; |FINSI;
|FPRC;

|PROCESO Tipo7C04; |TIPO 7;
    || ... Proceso de los Pinta de los campos primera Pantalla

     nCampo = 6;  |HAZ TextoC;
     nCampo = 7;  |HAZ TextoC;
     nCampo = 10; |HAZ TextoC;
     nCampo = 12; |HAZ TextoC;
|FPRC;

|PROCESO Tipo66C04; |TIPO 66;
     |C_M #150#4,0,aAlfa1;
     |SI aAlfa1 = "N"; |ACABA; |FINSI;

     aAlfa = "|C004WID";
     aID   = #dsmow150#aAlfa#;
     nID   = aID;

     aPopup1  = nID;  || Si fuera -1 en la posicion
     aPopup2  = 8;
     aPopup3  = "1 Titular                     ";
     aPopup4  = "2 Representante               ";
     aPopup5  = "3 Autorizado                  ";
     aPopup6  = "4 Beneficiario                ";
     aPopup7  = "5 Usufructuario               ";
     aPopup8  = "6 Tomador                     ";
     aPopup9  = "7 Con poder de disposición    ";
     aPopup10 = "8 Otras formas de titularidad ";

     IaPopup  = aPopup1  <-;
     |ESPECIFICA "TRACKPOPUP", aPopup;
     |SI FSalida ! 0;
         IaPopup  = aPopup3  <-;
         IaPopup  = IaPopup + (FSalida - 1);

         aAlfa = aPopup % 101;  #150#4 = aAlfa;
     |FINSI;

     |PINTA #150#4;
|FPRC;

|PROCESO Tipo0C04; |TIPO 0;
     aAlfa = Contenido;

     aTexto = "";
     |SI #150#4 = 1; aTexto = "Titular                     "; |FINSI;
     |SI #150#4 = 2; aTexto = "Representante               "; |FINSI;
     |SI #150#4 = 3; aTexto = "Autorizado                  "; |FINSI;
     |SI #150#4 = 4; aTexto = "Beneficiario                "; |FINSI;
     |SI #150#4 = 5; aTexto = "Usufructuario               "; |FINSI;
     |SI #150#4 = 6; aTexto = "Tomador                     "; |FINSI;
     |SI #150#4 = 7; aTexto = "Con poder de disposición    "; |FINSI;
     |SI #150#4 = 8; aTexto = "Otras formas de titularidad "; |FINSI;

     |SI aTexto = "";
         |MENAV "0000 Clave de operaci¢n incorrecta";
         |ERROR;
         |ACABA;
     |FINSI;

     |SI #150#4 = 8;
         |SI aAlfa ! #150#4; |O aAlfa1 = ""; #150#5 = aTexto; |FINSI;
         |C_M #150#5,1,"S";
     |SINO;
         #150#5 = aTexto;
         |C_M #150#5,1,"N";
     |FINSI;
     |PINTA #150#5;
|FPRC;

|PROCESO Tipo66C06; |TIPO 66;
     aAlfa = "|C006WID";
     aID   = #dsmow150#aAlfa#;
     nID   = aID;

     aPopup1  = nID;  || Si fuera -1 en la posicion
     aPopup2  = 5;
     aPopup3  = "C. Cuentas bancarias situadas en el extranjero";
     aPopup4  = "V. Valor de la participación o cesión de capitales en entidades situadas en el extranjero";
     aPopup5  = "I. Valor de participaciones en fondos de inversión situados en el extranjero";
     aPopup6  = "S. Seguros de vida en entidades aseguradoras situadas en el extranjero";
     aPopup7  = "B. Inmuebles situados en el extranjero";

     IaPopup  = aPopup1  <-;
     |ESPECIFICA "TRACKPOPUP", aPopup;
     |SI FSalida ! 0;
         IaPopup  = aPopup3  <-;
         IaPopup  = IaPopup + (FSalida - 1);

         aAlfa = aPopup % 101;  #150#6 = aAlfa;
     |FINSI;

     |PINTA #150#6;
|FPRC;

|PROCESO Tipo0C06; |TIPO 0;

     nCampo = 6; |HAZ TextoC;

     |SI aTexto = aAlfa1;
         |MENAV "0000 Clave del bien incorrecta";
         |ERROR;
         |ACABA;
     |FINSI;

     |C_M #150#7,1,"S";
     |SI #150#6= "I";
         |C_M #150#7,1,"N"; #150#7 = 0; |PINTA #150#7;
     |FINSI;

     |C_M #150#10, 1, "S";
     |C_M #150#11, 1, "S";
     |C_M #150#12, 1, "S";
     |C_M #150#13, 1, "S";
     |C_M #150#14, 1, "S";
     |C_M #150#15, 1, "S";
     |C_M #150#16, 1, "S";
     |C_M #150#28, 1, "S";
     |C_M #150#29, 1, "S";
     |C_M #150#30, 1, "S";

     |SI #150#6 ! "V"; |Y #150#6 ! "I";
         |C_M #150#10,1,"N"; #150#10 = 0;  |PINTA #150#10;
         |C_M #150#11,1,"N"; #150#11 = ""; |PINTA #150#11;
     |SINO;
         |SI #150#10 = 0; #150#10 = 1; |PINTA #150#10; |FINSI;
     |FINSI;

     |SI #150#6 ! "C";
         |C_M #150#12,1,"N"; #150#12 = ""; |PINTA #150#12;
         |C_M #150#13,1,"N"; #150#13 = ""; |PINTA #150#13;
         |C_M #150#14,1,"N"; #150#14 = ""; |PINTA #150#14;
     |FINSI;

     |SI #150#6 = "B";
         |C_M #150#15,1,"N"; #150#15 = ""; |PINTA #150#15;
         |C_M #150#16,1,"N"; #150#16 = ""; |PINTA #150#16;
     |FINSI;

     |SI #150#6 ! "V"; |Y #150#6 ! "I";
         |C_M #150#28,1,"N"; #150#28 = ""; |PINTA #150#28;
         |C_M #150#29,1,"N"; #150#29 = 0;  |PINTA #150#29;
     |FINSI;

     |SI #150#6 ! "B";
         |C_M #150#30,1,"N"; #150#30 = ""; |PINTA #150#30;
     |FINSI;

     nCampo = 7;  |HAZ TextoC;
     nCampo = 10; |HAZ TextoC;
     nCampo = 12; |HAZ TextoC;
|FPRC;

|PROCESO Tipo66C07; |TIPO 66;
     |C_M #150#7,0,aAlfa1;
     |SI aAlfa1 = "N"; |ACABA; |FINSI;

     aAlfa = "|C007WID";
     aID   = #dsmow150#aAlfa#;
     nID   = aID;

     aPopup1  = nID;  || Si fuera -1 en la posicion
     |SI #150#6 = "C";
         aPopup2  = 5;
         aPopup3  = "1 Cuenta corriente";
         aPopup4  = "2 Cuenta de ahorro";
         aPopup5  = "3 Imposiciones a plazo";
         aPopup6  = "4 Cuentas de crédito";
         aPopup7  = "5 Otras cuentas";
     |FINSI;
     |SI #150#6 = "V";
         aPopup2  = 3;
         aPopup3  = "1 Valores de la participación en cualquier entidad jurídica";
         aPopup4  = "2 Valores por la cesión de capitales a terceros";
         aPopup5  = "3 Valores aportados para su gestión a cualquier instrumento jurídico";
     |FINSI;
     |SI #150#6 = "S";
         aPopup2  = 2;
         aPopup3  = "1 Seguros de vida o invalidez";
         aPopup4  = "2 Rentas temporales o vitalícias";
     |FINSI;
     |SI #150#6 = "B";
         aPopup2  = 5;
         aPopup3  = "1 Titularidad del inmueble";
         aPopup4  = "2 Derechos reales de uso o disfrute";
         aPopup5  = "3 Nuda propiedad";
         aPopup6  = "4 Multipropiedad";
         aPopup7  = "5 Otros derechos reales";
     |FINSI;

     IaPopup  = aPopup1  <-;
     |ESPECIFICA "TRACKPOPUP", aPopup;
     |SI FSalida ! 0;
         IaPopup  = aPopup3  <-;
         IaPopup  = IaPopup + (FSalida - 1);

         aAlfa = aPopup % 101;  #150#7 = aAlfa;
     |FINSI;

     |PINTA #150#7;
|FPRC;

|PROCESO Tipo0C07; |TIPO 0;

     nCampo = 7; |HAZ TextoC;

     |SI #150#6 = "C"; nNume1 = 5; |FINSI;
     |SI #150#6 = "V"; nNume1 = 3; |FINSI;
     |SI #150#6 = "I"; nNume1 = 0; |FINSI;
     |SI #150#6 = "S"; nNume1 = 2; |FINSI;
     |SI #150#6 = "B"; nNume1 = 5; |FINSI;

     |SI #150#7 > nNume1;
         |MENAV "0000 Subclave del bien incorrecta";
         |ERROR;
         |ACABA;
     |FINSI;

     |SI #150#7 = 0; |Y #150#6 ! "I";
         |MENAV "0000 Subclave del bien incorrecta";
         |ERROR;
         |ACABA;
     |FINSI;

     |C_M #150#8,  1, "S";

     |SI #150#6 ! "B"; |O #150#7 ! 5;
         |C_M #150#8,1,"N"; #150#8 = ""; |PINTA #150#8;
     |FINSI;

     nCampo = 10; |HAZ TextoC;
     nCampo = 12; |HAZ TextoC;
|FPRC;

|PROCESO FiltroPais;

     |SI #150#9 = "DE ";  |ACABA;  |FINSI;
     |SI #150#9 = "AT ";  |ACABA;  |FINSI;
     |SI #150#9 = "BE ";  |ACABA;  |FINSI;
     |SI #150#9 = "BG ";  |ACABA;  |FINSI;
     |SI #150#9 = "CY ";  |ACABA;  |FINSI;
     |SI #150#9 = "DK ";  |ACABA;  |FINSI;
     |SI #150#9 = "SI ";  |ACABA;  |FINSI;
     |SI #150#9 = "EE ";  |ACABA;  |FINSI;
     |SI #150#9 = "FI ";  |ACABA;  |FINSI;
     |SI #150#9 = "FR ";  |ACABA;  |FINSI;
     |SI #150#9 = "EL ";  |ACABA;  |FINSI;
     |SI #150#9 = "GB ";  |ACABA;  |FINSI;
     |SI #150#9 = "NL ";  |ACABA;  |FINSI;
     |SI #150#9 = "HU ";  |ACABA;  |FINSI;
     |SI #150#9 = "IT ";  |ACABA;  |FINSI;
     |SI #150#9 = "IE ";  |ACABA;  |FINSI;
     |SI #150#9 = "LV ";  |ACABA;  |FINSI;
     |SI #150#9 = "LT ";  |ACABA;  |FINSI;
     |SI #150#9 = "LU ";  |ACABA;  |FINSI;
     |SI #150#9 = "MT ";  |ACABA;  |FINSI;
     |SI #150#9 = "PL ";  |ACABA;  |FINSI;
     |SI #150#9 = "PT ";  |ACABA;  |FINSI;
     |SI #150#9 = "CZ ";  |ACABA;  |FINSI;
     |SI #150#9 = "SK ";  |ACABA;  |FINSI;
     |SI #150#9 = "RO ";  |ACABA;  |FINSI;
     |SI #150#9 = "SE ";  |ACABA;  |FINSI;
     |SI #150#9 = "GR ";  |ACABA;  |FINSI;
     |SI #150#9 = "HR ";  |ACABA;  |FINSI;

     |SI #150#9 = "ES ";
         |MENAV "     Codigo de Pais incorrecto. ";
         |ERROR;
     |FINSI;
|FPRC;

|PROCESO Tipo66C10; |TIPO 66;
     |C_M #150#10,0,aAlfa1;
     |SI aAlfa1 = "N"; |ACABA; |FINSI;

     aAlfa = "|C010WID";
     aID   = #dsmow150#aAlfa#;
     nID   = aID;

     aPopup1  = nID;  || Si fuera -1 en la posicion
     aPopup2  = 2;
     aPopup3  = "1 Identificación por ISIN";
     aPopup4  = "2 Valores sin codigo ISN";

     IaPopup  = aPopup1  <-;
     |ESPECIFICA "TRACKPOPUP", aPopup;
     |SI FSalida ! 0;
         IaPopup  = aPopup3  <-;
         IaPopup  = IaPopup + (FSalida - 1);

         aAlfa = aPopup % 101;  #150#10 = aAlfa;
     |FINSI;

     |PINTA #150#10;
|FPRC;

|PROCESO Tipo0C10; |TIPO 0;
     |C_M #150#10, 0, aAlfa1;
     |SI aAlfa1 = "N"; |ACABA; |FINSI;

     nCampo = 10; |HAZ TextoC;

     |SI aTexto = aAlfa1;
         |MENAV "0000 Clave identificación incorrecta";
         |ERROR;
         |ACABA;
     |FINSI;

     nCampo = 12; |HAZ TextoC;
|FPRC;

|PROCESO Tipo66C12; |TIPO 66;
     |C_M #150#12,0,aAlfa1;
     |SI aAlfa1 = "N"; |ACABA; |FINSI;

     aAlfa = "|C012WID";
     aID   = #dsmow150#aAlfa#;
     nID   = aID;

     aPopup1  = nID;  || Si fuera -1 en la posicion
     aPopup2  = 2;
     aPopup3  = "I. Código IBAN";
     aPopup4  = "O. Otra identificación";

     IaPopup  = aPopup1  <-;
     |ESPECIFICA "TRACKPOPUP", aPopup;
     |SI FSalida ! 0;
         IaPopup  = aPopup3  <-;
         IaPopup  = IaPopup + (FSalida - 1);

         aAlfa = aPopup % 101;  #150#12 = aAlfa;
     |FINSI;

     |PINTA #150#12;
|FPRC;

|PROCESO Tipo0C12; |TIPO 0;
     |C_M #150#12, 0, aAlfa1;
     |SI aAlfa1 = "N"; |ACABA; |FINSI;

     nCampo = 12; |HAZ TextoC;

     |SI aTexto = aAlfa1;
         |MENAV "0000 Clave identificación incorrecta";
         |ERROR;
     |FINSI;
|FPRC;

|PROCESO Tipo7C15; |TIPO 7;
     |C_M #150#15,0,aAlfa1;
     |SI aAlfa1 = "N"; |ACABA; |FINSI;

     aMensa = "    Razon Social de la entidad participada o encagarda de la gestion";
     |SI #150#6 = "C";
          aMensa = "    Razon Social o denominacion de la entidad bancaria";
     |FINSI;
     |SI #150#6 = "S";
          aMensa = "    Razon Social o denominacion de la entidad aseguradora";
     |FINSI;
     |MENSA aMensa;
|FPRC;

|PROCESO Tipo7C17; |TIPO 7;
     aMensa = "    Direccion de la entidad identifica anteriormente";
     |SI #150#6 = "B";
          aMensa = "    Direccion del inmueble objeto de la declaracion";
     |FINSI;
     |MENSA aMensa;
|FPRC;

|PROCESO Tipo7C23; |TIPO 7;
     aMensa = "    Fecha apertura cuenta, concesion autorizacion o de otra forma de titularidad";
     |SI #150#6 = "V";
          aMensa = "    Fecha de adquisicion de la titularidad de los valores";
     |FINSI;
     |SI #150#6 = "I";
          aMensa = "    Fecha de adquisicion de la titularidad  sobre las acciones ";
     |FINSI;
     |SI #150#6 = "S";
          aMensa = "    Fecha de contratacion de la entidad aseguradora";
     |FINSI;
     |SI #150#6 = "B";
          aMensa = "    Fecha de adquisicion de la titularidad sobre el inmueble";
     |FINSI;
     |MENSA aMensa;

     nCampo = 24; |HAZ TextoC;
     nCampo = 28; |HAZ TextoC;
     nCampo = 30; |HAZ TextoC;
|FPRC;

|PROCESO Tipo66C24; |TIPO 66;
     aAlfa = "|C024WID";
     aID   = #dsmow150#aAlfa#;
     nID   = aID;

     aPopup1  = nID;  || Si fuera -1 en la posicion
     aPopup2  = 3;
     aPopup3  = "A. Bien que se declara la primera vez";
     aPopup4  = "M. Bien que ya ha sido declarado en ejercicios anteriores";
     aPopup5  = "C. Bien que se extingue la titularidad";

     IaPopup  = aPopup1  <-;
     |ESPECIFICA "TRACKPOPUP", aPopup;
     |SI FSalida ! 0;
         IaPopup  = aPopup3  <-;
         IaPopup  = IaPopup + (FSalida - 1);

         aAlfa = aPopup % 101;  #150#24 = aAlfa;
     |FINSI;

     |PINTA #150#24;
|FPRC;

|PROCESO Tipo0C24; |TIPO 0;
     nCampo = 24; |HAZ TextoC;

     |SI aTexto = aAlfa1;
         |MENAV "0000 Origen del incorrecto";
         |ERROR;
         |ACABA;
     |FINSI;

     |C_M #150#25,1,"S";
     |SI #150#24 !  "C";
         |C_M #150#25,1,"N"; #150#25 = "01.01.0000"; |PINTA #150#25;
     |FINSI;
|FPRC;

|PROCESO Tipo7C27; |TIPO 7;
     aAlfa1 = "N";
     |SI #150#6 = "B"; |Y #150#24 = "C";
         aAlfa1 = "S";
     |FINSI;
     |SI #150#6 = "C"; |Y #150#24 ! "C";
         aAlfa1 = "S";
     |FINSI;
     |C_M #150#27,1,aAlfa1;
     |SI aAlfa1 = "N"; #150#27 = 0; |PINTA #150#27; |FINSI;
|FPRC;

|PROCESO Tipo66C28; |TIPO 66;
     aAlfa = "|C028WID";
     aID   = #dsmow150#aAlfa#;
     nID   = aID;

     aPopup1  = nID;  || Si fuera -1 en la posicion
     aPopup2  = 2;
     aPopup3  = "A. Valores representados con anotaciones a cuenta";
     aPopup4  = "B. Valores no representados con anotaciones a cuenta";

     IaPopup  = aPopup1  <-;
     |ESPECIFICA "TRACKPOPUP", aPopup;
     |SI FSalida ! 0;
         IaPopup  = aPopup3  <-;
         IaPopup  = IaPopup + (FSalida - 1);

         aAlfa = aPopup % 101;  #150#28 = aAlfa;
     |FINSI;

     |ATRI I;
     |PINTA 1832, #150#28;
     |ATRI 0;
|FPRC;

|PROCESO Tipo0C28; |TIPO 0;

     |C_M #150#28,0,aAlfa1;
     |SI aAlfa1 = "N"; |ACABA; |FINSI;

     nCampo = 28; |HAZ TextoC;
     |SI aTexto = aAlfa1;
         |MENAV "0000 Clave incorrecta";
         |ERROR;
     |FINSI;
|FPRC;

|PROCESO Tipo66C30; |TIPO 66;
     aAlfa = "|C030WID";
     aID   = #dsmow150#aAlfa#;
     nID   = aID;

     aPopup1  = nID;  || Si fuera -1 en la posicion
     aPopup2  = 2;
     aPopup3  = "U. Urbano";
     aPopup4  = "R. Rustico";

     IaPopup  = aPopup1  <-;
     |ESPECIFICA "TRACKPOPUP", aPopup;
     |SI FSalida ! 0;
         IaPopup  = aPopup3  <-;
         IaPopup  = IaPopup + (FSalida - 1);

         aAlfa = aPopup % 101;  #150#30 = aAlfa;
     |FINSI;

     |PINTA #150#30;
|FPRC;

|PROCESO Tipo0C30; |TIPO 0;
     |C_M #150#30,0,aAlfa1;
     |SI aAlfa1 = "N"; |ACABA; |FINSI;

     nCampo = 30; |HAZ TextoC;
     |SI aTexto = aAlfa1;
         |MENAV "0000 Tipo de inmueble";
         |ERROR;
     |FINSI;
|FPRC;
