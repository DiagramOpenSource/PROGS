|FICHEROS;
     :contagen/cacuenta;
     :modelos/dsmom107;
     :modelos/dsmom074;
     :contagen/ca8m0003;
     :contagen/ca8ctrle;       || Control Empresa
     :contagen/ca8comay;
     :contagen/ca8eqmay;

     :contagen/canempre;
|FIN;

|VARIABLES;
 aAlfa1 = "";
 aAlfa2 = "";
 aAlfa3 = "";
 nNume1 = 0;
 nNume2 = 0;
 nNume3 = 0;
 aAlfaR = "";
 nNumeR = 0;
 nHay   = 0;
 uno    = "";
 dos    = "";
 nLong1 = 0;
 nLong2 = 0;
 nLong3 = 0;
 nLong0 = 0;
 aPath  = "";
 nOk    = 0;
 fFecha = @;
|FIN;

|INCLUYE i_variar;
|INCLUYE i_varcon;

|PROCESO HayCambio2008;
  enHayCambio = 0;
  eaPath2008  = "";
  enPer2008   = -1;
  |ABRE #canempre;
  |SELECT #4#canempre;
  #canempre#2  = enEmpresa;
  #canempre#26 = 8;  || Ejercicio 2008
  |LEE 041#canempre.=;
  |SI FSalida = 0;
      enPer2008 = #canempre#3;
  |FINSI;
  |CIERRA #canempre;

  |SI enPer2008 ! -1;
      clave1 = enEmpresa;  clave1 = ("00000" + clave1) % -105;
      clave2 = enPer2008;  clave2 = ( "0"    + clave2) % -101;
      |DBASS aPath; |QBT aPath;
      eaPath2008 = aPath + "contagen/fich/" + clave1 + clave2 + "/";

      |PATH_DAT #ca8m0003#eaPath2008#;

      |ABRE #ca8m0003;
      |LEE 041#ca8m0003.p;
      |SI FSalida = 0;
          enHayCambio = 1;
      |FINSI;
      |CIERRA #ca8m0003;
  |FINSI;
|FPRC;

|PROCESO BusPeriodo07;
   |HAZ LaFecha;
   |SI fFecha < fec2;
       fFecha = fec2;
       nNume1 = #canempre#13 + 13;
       nLongMax = #canempre#nNume1#;
   |FINSI;
|FPRC;

|DEFBUCLE BusPeriodo07;
 #canempre#1;
 ;
 enEmpresa, 0;
 enEmpresa, 9;
 ;
 NULL, BusPeriodo07;
|FIN;

|PROCESO AntCambioCta;  || situa la tabla

  |ABRE #ca8ctrle;
  |DDEFECTO #ca8ctrle;
  #ca8ctrle#0 = enEmpresa;
  #ca8ctrle#1 = enPerConta;
  |LEE 001#ca8ctrle.=;
  |SI FSalida = 0;
      eNum2008 = #ca8ctrle#2;
  |SINO;
      #ca8ctrle#0 = enEmpresa;
      #ca8ctrle#1 = 0;
      |LEE 001#ca8ctrle.];
      |SI FSalida = 0; |Y #ca8ctrle#0 = enEmpresa;
          eNum2008 = #ca8ctrle#2;
      |FINSI;
  |FINSI;
  |CIERRA #ca8ctrle;

  aAlfaR = aPathConta;
  nNumeR = enPerConta;

  |HAZ HayCambio2008;
  |SI enPer2008 ! -1;
      enPerConta = enPer2008;
      enSwSinMen = 1;
      |HAZ LeeEmpConta;
      eaPath2008 = aPathConta;
      nLongMax = MaximoDigitos;
  |SINO;
      nLongMax = 0;
      fFecha = "01.01.0000";
      |BUCLE BusPeriodo07;
  |FINSI;

  enPerConta = nNumeR;
  aPathConta = aAlfaR;
|FPRC;

|PROCESO AltaDsmom074;
   |ABRE #dsmom074;
   |SELECT #1#dsmom074;
   |DDEFECTO #dsmom074;
   #dsmom074#8 = enEmpresa;
   #dsmom074#0 = eaCta1990;
   |LEE 041#dsmom074.=;
   |SI FSalida ! 0;
       #dsmom074#8 = enEmpresa;
       #dsmom074#0 = eaCta1990;
       #dsmom074#1 = eaNom1990;
       #dsmom074#2 = eaCta2008;
       #dsmom074#3 = eaNom2008;
       |GRABA 041#dsmom074.n;
   |FINSI;
   |CIERRA #dsmom074;
|FPRC;

|PROCESO CambioCta;

  |QBF eaCuenta;
  |SI eaCuenta = "";   |ACABA; |FINSI;

  eaCta2008 = eaCuenta;
  eaNom2008 = eaNomCta;
  eaCta1990 = eaCuenta;
  eaNom1990 = eaNomCta;

  |ABRE #dsmom107;
  |DDEFECTO #dsmom107;
  |LEE 040#dsmom107.p;
  |CIERRA #dsmom107;
  |SI #dsmom107#20 ] 2008; |ACABA; |FINSI;

  nHay = 0;
  nOk  = 1;
  |SI eaPath2008 ! "";
      |PATH_DAT #ca8m0003#eaPath2008#;
      |ABRE #ca8m0003;
      |DDEFECTO #ca8m0003;
      |LEE 041#ca8m0003.p;
      |SI FSalida = 0;
          nOk = 0;
      |FINSI;
      |CIERRA #ca8m0003;
  |FINSI;

  |SI nOk = 0;          || hay ejercicio 2008
      |ABRE #ca8m0003;
      |SI enEjerCam ] 2008;
          |SELECT #2#ca8m0003;
          #ca8m0003#2 = eaCuenta;
          |LEE 041#ca8m0003.];
          |SI FSalida = 0; |Y #ca8m0003#2 = eaCuenta;
              eaCta2008 = #ca8m0003#2;
              eaNom2008 = #ca8m0003#3;
              eaCta1990 = #ca8m0003#0;
              eaNom1990 = #ca8m0003#1;
              nHay = 1;
          |SINO;
              eaCta1990 = "";
              eaNom1990 = "";
          |FINSI;
      |SINO;
          #ca8m0003#0 = eaCuenta;
          |LEE 041#ca8m0003.=;
          |SI FSalida = 0;
              eaCta2008 = #ca8m0003#2;
              eaNom2008 = #ca8m0003#3;
              eaCta1990 = #ca8m0003#0;
              eaNom1990 = #ca8m0003#1;
              nHay = 1;
          |FINSI;
      |FINSI;
      |SELECT #1#ca8m0003;
      |CIERRA #ca8m0003;
   |FINSI;

   |SI nHay = 0;
      |ABRE #dsmom074;
      |SI enEjerCam ] 2008;
          |SELECT #2#dsmom074;
          #dsmom074#8 = enEmpresa;
          #dsmom074#2 = eaCuenta;
          |LEE 041#dsmom074.];
          |SI FSalida = 0; |Y #dsmom074#8 = enEmpresa;
               |Y #dsmom074#2 = eaCuenta;
              eaCta2008 = #dsmom074#2;
              eaNom2008 = #dsmom074#3;
              eaCta1990 = #dsmom074#0;
              eaNom1990 = #dsmom074#1;
              nHay = 2;
          |SINO;
              eaCta1990 = "";
              eaNom1990 = "";
          |FINSI;
      |SINO;
          #dsmom074#8 = enEmpresa;
          #dsmom074#0 = eaCuenta;
          |LEE 041#dsmom074.=;
          |SI FSalida = 0;
              eaCta2008 = #dsmom074#2;
              eaNom2008 = #dsmom074#3;
              eaCta1990 = #dsmom074#0;
              eaNom1990 = #dsmom074#1;
              nHay = 2;
          |FINSI;
      |FINSI;
      |SELECT #1#dsmom074;
      |CIERRA #dsmom074;
   |FINSI;

   |SI nHay = 0; |Y enEjerCam < 2008;  || lo transformo ahora

      |ABRE #ca8eqmay;
      |DDEFECTO #ca8eqmay;

      uno = eaCuenta; |QBF uno;
      dos = uno + "000000000000";

      nHay = 0;

      uno = dos % A105; nLong1 = 5;
      #ca8eqmay#0 = eNum2008;
      #ca8eqmay#1 = uno;
      |LEE 040#ca8eqmay.=;
      |SI FSalida = 0;  |HAZ Datos;  |FINSI;

      |SI nHay = 0;
          uno = dos % A104; nLong1 = 4;
          #ca8eqmay#0 = eNum2008;
          #ca8eqmay#1 = uno;
          |LEE 040#ca8eqmay.=;
          |SI FSalida = 0;  |HAZ Datos;  |FINSI;
      |FINSI;

      |SI nHay = 0;
          uno = dos % A103; nLong1 = 3;
          #ca8eqmay#0 = eNum2008;
          #ca8eqmay#1 = uno;
          |LEE 040#ca8eqmay.=;
          |SI FSalida = 0;  |HAZ Datos;  |FINSI;
      |FINSI;

      |SI nHay = 0;
          uno = dos % A102; nLong1 = 2;
          #ca8eqmay#0 = eNum2008;
          #ca8eqmay#1 = uno;
          |LEE 040#ca8eqmay.=;
          |SI FSalida = 0;  |HAZ Datos;  |FINSI;
      |FINSI;

      |SI nHay = 0;
          eaCta2008 = "PDTE";
          eaNom2008 = "No existe Tabla equivalencia";
      |FINSI;
      |CIERRA #ca8eqmay;
  |FINSI;

  |SI nHay = 1;
      |HAZ AltaDsmom074;
  |FINSI;
|FPRC;

|PROCESO Datos;

   aAlfa1 = eaCuenta % 0;
   nLong0 = aAlfa1;
   |SI nLongMax = 0;  nLongMax = nLong0;  |FINSI;

   nLong3 = nLongMax - nLong0;
   |SI nLong3 < 0; nLong3 = 0; |FINSI;

   eaPlan8  = #ca8eqmay#3;
   |QBF eaPlan8;

   aAlfa1 = eaPlan8 % 0;
   nNume1 = aAlfa1;     || Longitud del Cambio
   nNume2 = nNume1 + 100; nLong2 = nNume1;

   |SI eaPlan8 = "PDTE";
       eaCta2008 = eaPlan8;
       eaNom2008 = #ca8eqmay#4;
       |ACABA;
   |FINSI;

   nNume3 = 0 - ((nLong0 - nLong2) + 100)
   aAlfa1 = eaCuenta % nNume2;
   aAlfa2 = eaCuenta % nNume3;
   aAlfa3 = "0" * nLong3;

   |SI aAlfa1 = eaPlan8;
       eaCta2008 = eaCuenta;
   |SINO;
       eaCta2008 = eaPlan8 + aAlfa3 + aAlfa2;
   |FINSI;
   nHay = 1;
|FPRC;
