|FICHEROS;
    dsmoz079 #0;

    dsmom041 #41;
    dsmom042 #42;

    :/basica/def/agifigen #1000;
|FIN;

|VARIABLES;

  nDEmpresa   = 0;
  nHEmpresa   = 0;
  &nDActividad = 0;
  &nHActividad = 0;
  &ele1       = "";
  &ele2       = "";
  &nnum1      = 0;
  &nnum2      = 0;
  nNume1      = 0;
  nCampo      = 0;
  nCampo1     = 0;
  fecalf      = "";
  fecsys      = @;
  aAlfa1      = "";
  aAlfa2      = "";

  nLaEmpresa  = 0;
  swBueno     = 0;

  &nCamSel  = 24;
  &nCamTip  = 39;
  nLinea    = 0;

  &enEjerV  = 0;
  &enEjerA  = 0;
  nPara1    = 0;

  nOk = 0;
  {30}aPopup        = "";
     aContenido     = "";
     aID            = "";
     nID            = 0;
|FIN;

|INCLUYE i_variar;
|INCLUYE i_varcon;
|INCLUYE i_numamo;
|INCLUYE i_selcon;

|| //////////////////// Calculos de Pantalla \\\\\\\\\\\\\\\\\\\\\\\\\\
|CALCULO Tipo7;  |TIPO 7;
     |SI enEmpresa ! 0;
         #0#8 = enEmpresa; |PINTA #0#8;
         #0#9 = enEmpresa; |PINTA #0#9;

         |C_M #0#8, 1, "N";
         |C_M #0#9, 1, "N";
     |FINSI;
|FCAL;

|CALCULO Parrilla;
     |SI #0#8 = 0;
         #0#8 = 0;  |PINTA #0#8;
         #0#9 = 0;  |PINTA #0#9;  |C_M #0#9, 1, "N";
         |PARA nCampo = 10;  |SI nCampo [ 21;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |SINO;
         |SI enEmpresa = 0;
             |C_M #0#8, 1, "S";
             |C_M #0#9, 1, "S";
         |FINSI;

         |PARA nCampo = 10;  |SI nCampo [ 21;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "N";
               #0nCampo = 0;  |PINTA #0nCampo;
         |SIGUE;
     |FINSI;
|FCAL;

|CALCULO NoMasEmpresas;
     |SI #0Campo = 0;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 21;  |HACIENDO nCampo = nCampo + 1;
               #0nCampo = 0;  |C_M #0nCampo, 1, "N";
         |SIGUE;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 21;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |FINSI;
|FCAL;

|CALCULO numamo; |TIPO 0;
    x_alfa1 = #0Campo;
    |HAZ x_punto;
    #0Campo = x_alfa1;
    |PINTA #0Campo;
|FCAL;

|CALCULO defecto; |TIPO 7;
   |SI enEmpresa = 0;
       fecsys = ~
       aAlfa1 = fecsys % -104;

       fecalf = "01.01." + aAlfa1;
       #0#3 = fecalf; |PINTA #0#3;

       fecalf = "31.12." + aAlfa1;
       #0#4 = fecalf; |PINTA #0#4;
   |FINSI;
|FCAL;

|CALCULO EhLafecha; |TIPO 0;
|FCAL;


|CALCULO Tipo66C41; |TIPO 66;
     aAlfa1 = "|C041WID";
     aID    = #0#aAlfa1#;
     nID    = aID;

     aPopup1  = nID;  || Si fuera -1 en la posicion
     aPopup2  = 06;
     aPopup3  = "01. Amortización total";
     aPopup4  = "03. Deterioro o destrucción";
     aPopup5  = "04. Retirada del bien por obsolescencia";
     aPopup6  = "05. Pérdida";
     aPopup7  = "06. Robo";
     aPopup8  = "99. Otras causas diferentes a las anteriores";

     IaPopup  = aPopup1  <-;
     |ESPECIFICA "TRACKPOPUP", aPopup;
     |SI FSalida ! 0;
         IaPopup  = aPopup3  <-;
         IaPopup  = IaPopup + (FSalida - 1);

         aAlfa1 = aPopup % 102;  #0#41= aAlfa1;
     |FINSI;

     |PINTA #0#41;
||     aPopup3  = "00. Elemento en Alta";
||     aPopup5  = "02. Venta o transmisión";
|FCAL;

|CALCULO Tipo0C41; |TIPO 0;
     nOk = 0;
     #0#42 = "";
     |SI #0#41 = 1;  #0#42 = "Amortización total";       nOk = 1; |FINSI;
     |SI #0#41 = 3;  #0#42 = "Deterioro o destrucción";  nOk = 1; |FINSI;
     |SI #0#41 = 4;  #0#42 = "Retirada del bien por obsolescencia"; nOk = 1; |FINSI;
     |SI #0#41 = 5;  #0#42 = "Pérdida";      nOk = 1; |FINSI;
     |SI #0#41 = 6;  #0#42 = "Robo";         nOk = 1; |FINSI;
     |SI #0#41 = 99; #0#42 = "Otras causas"; nOk = 1; |FINSI;

     |PINTA #0#42;
     |SI nOk = 0;
         |MENAV "0000Causa de Baja incorrecta";
         |ERROR;
     |FINSI;
|FCAL;

|| /////////////////////////// Procesos \\\\\\\\\\\\\\\\\\\\\\\\\\\\\\
|PROCESO C_Amortiza;
   |SI nLaEmpresa ! #41#0;
       nLaEmpresa = #41#0;
       enEmpresa  = #41#0;
       swBueno = 0;
       |HAZ LaSelEmpresa;
       |SI enSwSelEmp = 1; swBueno = 1;  |FINSI;
   |FINSI;
   |SI swBueno = 1; |ACABA; |FINSI;

   |SI #41#26 [ "01.01.1000";  |ACABA; |FINSI; || En alta
   |SI #41#58 ! #0#41;         |ACABA; |FINSI; || No es la misma causa de baja

   |SI #41#26 < #0#3; |O #41#26 > #0#4;
       |ACABA;
   |FINSI;

   |PINTA 2252, #41#0;
   |PINTA 2268, #41#2;
   |PINTA 2277, #41#3;

   |SI #0#40 = "S";
       #41#16 = #41#10 - #41#15;
       #41#22 = #41#10 - #41#21;
   |FINSI;

   #41#58 = 00;
   #41#40 = "A";
   #41#26 = "01.01.0000";

   |GRABA 020#41a;
   |LIBERA #41;
|FPRC;


|DEFBUCLE dsmoz079;
   #41#1;
   1;
   nDEmpresa, ele1, nnum1, nDActividad;
   nHEmpresa, ele2, nnum2, nHActividad;
   be;
   NULL, C_Amortiza;
|FIN;

|CALCULO Tipo3;  |TIPO 3;

     ele1        = #0#0;
     ele2        = #0#1;
     nnum1       = #0#37;
     nnum2       = #0#38;
     nDActividad = #0#22;
     nHActividad = #0#23;

     efFechaSit = #0#4;

     |SI #0#8 ! 0;
         swBueno = 0;
         nLaEmpresa = -1;
         nDEmpresa = #0#8;
         nHEmpresa = #0#9;
         |BUCLE dsmoz079;
     |SINO;
        |PARA nCampo = 10;;  |SI nCampo [ 21;  |HACIENDO nCampo = nCampo + 1;
              swBueno = 0;
              nLaEmpresa = -1;
              nDEmpresa = #0nCampo;
              nHEmpresa = #0nCampo;
              |BUCLE dsmoz079;
        |SIGUE;
     |FINSI;
|FCAL;

|PROGRAMA;
 |SI enEmpresa ! 0;
     |DDEFECTO #0;
     #0#0  = "";          |C_M #0#0, 1,"N";
     #0#1  = "zzzzzzzzz"; |C_M #0#1, 1,"N";
     #0#37 = 00;          |C_M #0#37,1,"N";
     #0#38 = 99;          |C_M #0#38,1,"N";
     #0#3  = efFechaSit;  |C_M #0#3, 1,"S";
     #0#4  = efFechaSit;  |C_M #0#4, 1,"S";
     #0#22 = enActividad; |C_M #0#22,1,"N";
     #0#23 = enActividad; |C_M #0#23,1,"N";
     |CLS;
     |PINPA #0#0;
     |PINDA #0#0;
     |ENDATOS #1#0;
 |SINO;
     |CLS;
     |MANTE #0;
 |FINSI;
|FPRO;
