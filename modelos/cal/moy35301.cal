|FICHEROS;
     dsmoy000;
     dsmom107;

     dsmom004;
     dsm35301;
     dsm35302;

     dsmow014;

     t1735301;

     dsmow142;

     &regisnif@;
     &clientes@;
     &datosper@;

     dsmom017;
|FIN;

|VARIABLES;
     nConta      = 0;
     nLin        = 0;
     nClave      = 0;
     nCmp        = 0;
     nError      = 0;

     aZ12        = "";
     aFicTmp     = "";
|FIN;

|INCLUYE i_varemi;

|PROCESO t1735301;
     |PARA nCampo = 1;  |SI nCampo [ 132;  |HACIENDO nCampo = nCampo + 1;
           eaAlfa = #t1735301#nCampo#;
           |XGRABA enHandlePortal, eaAlfa;
     |SIGUE;
|FPRC;

|DEFBUCLE t1735301;
     #t1735301#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, t1735301;
|FIN;

|PROCESO AbrePag_8312_2017;
     eaPathAEAT = eaUnidadBasico + "\AEAT";        |RMKDIR eaPathAEAT;
     eaPathAEAT = eaPathAEAT + "\" + #dsmoy000#0;  |RMKDIR eaPathAEAT;
     eaPathAEAT = eaPathAEAT + "\" + #dsmoy000#6;  |RMKDIR eaPathAEAT;
     eaPathAEAT = eaPathAEAT + "\" + (("00" + #dsmoy000#4) % -102);  |RMKDIR eaPathAEAT;
     eaPathAEAT = eaPathAEAT + "\";

     aAlfa = eaPathAEAT + "*.bat";  |QBT aAlfa;
     |R_PDIR aAlfa;
     |PARA; |SI; |HACIENDO;
            |SI FSalida ! 0; |SAL; |FINSI;

            aAlfa1 = (aAlfa % -103) < "";
            |SI aAlfa1 ! "txt";
                |RFBORRA aAlfa;
            |FINSI;

            |R_SDIR aAlfa;
     |SIGUE;

     eaFOrigen  = eaPathEmision  + eaFicheroPlano;
     eaFDestino = eaPathAEAT + eaFicheroPlano;
     |HAZ CopiaFichero;

     eaForzExplorador = "N";
     |HAZ Explorador;

     aAbre = eaPathAEAT + "imp" + Usuario + ".bat";
     |XABRE "WBC", aAbre, enHandlePortal;

     eaAlfa = eaPathExplorador + " ";
     |HAZ GrabaPortalSin;

     eaAlfa = &34 + "https://www1.agenciatributaria.gob.es/wlpl/OV19-M353/index.zul";
     |HAZ GrabaPortalSin;

     eaAlfa = "?WEB=INTERNET&PRG=" + #dsmoy000#0;
     |HAZ GrabaPortalSin;

     eaAlfa = "&EJE=0003";
     |HAZ GrabaPortalSin;

     eaAlfa = "&URL=PIR" + &34 +  &13 + &10;
     |HAZ GrabaPortalSin;

     eaAlfa = "";
     |HAZ GrabaPortal;

     |XCIERRA enHandlePortal;

     aAlfa = "0000Ubicacion: " + eaPathAEAT + &13 + &10;
     aAlfa = aAlfa  + " Fichero: " + eaFicheroPlano;
     |MENAV aAlfa;

     |RSYSTEM aAbre;
|FPRC;

|PROCESO PresentaModelo_04;
     |SI eaOpcion = "1";
         |HAZ GrabaImpreso;
     |FINSI;

     |SI eaOpcion = "3";
         |SI enPresentar = 0;
             eaFOrigen  = eaPathEmision  + eaFicheroPlano;
             eaFDestino = eaPathInternet + eaFicheroPlano;
             |HAZ CopiaFichero;
         |FINSI;

         |SI enPresentar = 1;
             |HAZ AbrePag_8312_2017;
         |FINSI;

         |HAZ GrabaImpreso;
     |FINSI;

     |SI eaOpcion = "6";
         eaX002_10    = #t1735301#7;
         eaX002_11    = #t1735301#8;
         eaX002_12    = #t1735301#2;
         eaX002_13    = #t1735301#10;
         eaX002_14    = #t1735301#11;

         |SI #dsm35301#4 > 0;
             eaX002_15 = "C";
             eaX002_16 = #t1735301#26;
         |FINSI;

         eaX002_17 = #dsm35301#13;
         eaX002_18 = #t1735301#6;
         eaX002_19 = #t1735301#29;
         eaX002_48 = eaNomEnt;

         |HAZ PresentaPortal;
     |FINSI;
|FPRC;

|PROCESO PersonaFis2017;
     aAlfa = eaVarDni % 101;
     |SI aAlfa = "0"; |O aAlfa = "1"; |O aAlfa = "2"; |O aAlfa = "3";
      |O aAlfa = "4"; |O aAlfa = "5"; |O aAlfa = "6"; |O aAlfa = "7";
      |O aAlfa = "8"; |O aAlfa = "9"; |O aAlfa = "X"; |O aAlfa = "Y";
      |O aAlfa = "Z";
         eaAlfa = #clientes@#1;     |HAZ BuscaNombre;
         eaAlfa = eaAlfa1;    |HAZ QuitaRaros;     #t1735301#8   = eaAlfa;
         eaAlfa = eaAlfa2;    |HAZ QuitaRaros;     #t1735301#9   = eaAlfa;
      |SINO;
         eaAlfa   = #clientes@#1;    |HAZ QuitaRaros;    #t1735301#8   = eaAlfa;
                                                         #t1735301#9   = " ";
      |FINSI;
|FPRC;

|PROCESO dsm35302;
     |SI #dsm35302#6 = " DOMINANTE  ";
         #t1735301#0 = 1;
         |LEE 000#t1735301.=;
         |SI FSalida ! 0;
             |DDEFECTO #t1735301;
             #t1735301#0 = 1;
             |GRABA 020#t1735301.b;
         |FINSI;

                                                #t1735301#15 = #dsm35302#7;
         enNume = #dsm35302#8;   |HAZ Conver17; #t1735301#16 = eaAlfa;
                                                #t1735301#17 = "00000";
                                                #t1735301#18 = #dsm35302#10;

         |GRABA 020#t1735301.a;
         |LIBERA #t1735301;

         nLin = 15;
         |ACABA;
     |FINSI;

     nConta = nConta + 1;
     nClave = ((nConta / 20) + .49) ! 0;

     #t1735301#0 = nClave;
     |LEE 000#t1735301.=;
     |SI FSalida ! 0;
         |DDEFECTO #t1735301;
         #t1735301#0 = nClave;

         nLin = 15;
         |SI nClave > 1;
             #t1735301#5 = "C";
         |FINSI;

         |GRABA 020#t1735301.b;
     |FINSI;

     nLin = nLin + 5;

     nCmp = nLin;                             #t1735301#nCmp# = #dsm35302#7;

     nCmp = nCmp + 1;
     enNume = #dsm35302#8;   |HAZ Conver17;   #t1735301#nCmp# = eaAlfa;

     nCmp = nCmp + 1;
     enNume = #dsm35302#9 * 100;  eaAlfa = ("00000" + enNume) % -105;
     #t1735301#nCmp# = eaAlfa;

     nCmp = nCmp + 1;                         #t1735301#nCmp# = #dsm35302#10;

     |GRABA 020#t1735301.a;
     |LIBERA #t1735301;
|FPRC;

|DEFBUCLE dsm35302;
     #dsm35302#1;
     ;
#dsm35301#0,#dsm35301#1,#dsm35301#2,#dsm35301#3,#dsm35301#4,#dsm35301#5,"",  "";
#dsm35301#0,#dsm35301#1,#dsm35301#2,#dsm35301#3,#dsm35301#4,#dsm35301#5,aZ12,aZ12;
     ;
     NULL, dsm35302;
|FIN;

|PROCESO TDeclaracion2017;
     enModelo = 353;  enForzar = 1;  |HAZ CogeDatosBancos;

     |SI #dsm35301#14 ! 0;
         #t1735301#6 = "C";
     |FINSI;

     |SI #dsm35301#15 ! 0;
                                 #t1735301#6 = "D";
         |SI eaFormaPago = "6";  #t1735301#6 = "V";  |FINSI;
     |FINSI;

     |SI #dsm35301#16 ! 0;
                                 #t1735301#6 = "I";
         |SI eaFormaPago = "3";  #t1735301#6 = "U";  |FINSI;
         |SI eaFormaPago = "6";  #t1735301#6 = "G";  |FINSI;
     |FINSI;

     |SI #dsm35301#9 = 0;  |Y #dsm35301#10 = 0; |Y #dsm35301#11 = 0;
      |Y #dsm35301#12 = 0; |Y #dsm35301#13 = 0;
         #t1735301#6 = "N";
     |FINSI;

     |SI #dsm35301#13 = 0;
         #t1735301#6 = "N";
     |FINSI;
|FPRC;

|PROCESO Hoja1_2017;
     |BUCLE dsm35302;

     #t1735301#0 = 1;
     |LEE 000#t1735301.=;
     |SI FSalida ! 0;
         |DDEFECTO #t1735301;
         #t1735301#0 = 1;
         |GRABA 020#t1735301.b;
     |FINSI;

     |SI enSwDeDonde = 1;
         eaVarDni = #clientes@#3;  |HAZ CalculameDNI;    #t1735301#7 = eaVarDni;
         eaAlfa   = #clientes@#1;  |HAZ QuitaRaros;      #t1735301#8 = eaAlfa;
     |SINO;
         eaVarDni = #clientes@#13;  |HAZ CalculameDNI;   #t1735301#7 = eaVarDni;
         eaAlfa   = #clientes@#1;   |HAZ QuitaRaros;     #t1735301#8 = eaAlfa;
     |FINSI;

     |HAZ TDeclaracion2017;

     #t1735301#10 = #dsm35301#1;

     |SI #dsmom004#22 = "T";
         |SI #dsm35301#2 = 3;   #t1735301#11 = "1T";  |FINSI;
         |SI #dsm35301#2 = 6;   #t1735301#11 = "2T";  |FINSI;
         |SI #dsm35301#2 = 9;   #t1735301#11 = "3T";  |FINSI;
         |SI #dsm35301#2 = 12;  #t1735301#11 = "4T";  |FINSI;
     |SINO;
         #t1735301#11 = ("00" + #dsm35301#2) % -102;
     |FINSI;

     #t1735301#12 = #dsm35301#6;

     |SI #dsm35301#7  = "S";   #t1735301#13 = "1";  |FINSI;
     |SI #dsm35301#7  = "N";   #t1735301#13 = "2";  |FINSI;

     |SI #dsm35301#8  = "S";   #t1735301#14 = "1";  |FINSI;
     |SI #dsm35301#8  = "N";   #t1735301#14 = "2";  |FINSI;

     enNume = #dsm35301#9;     |HAZ Conver17;   #t1735301#120 = eaAlfa;
     enNume = #dsm35301#10;    |HAZ Conver17;   #t1735301#121 = eaAlfa;
     enNume = #dsm35301#11;    |HAZ Conver17;   #t1735301#122 = eaAlfa;
     enNume = #dsm35301#12;    |HAZ Conver17;   #t1735301#123 = eaAlfa;
     enNume = #dsm35301#13;    |HAZ Conver17;   #t1735301#124 = eaAlfa;

     |SI #dsm35301#4 > 0;
         #t1735301#25 = "X";
         #t1735301#26 = #dsmom004#21;
     |FINSI;

     |SI #dsm35301#9 = 0;  |Y #dsm35301#10 = 0; |Y #dsm35301#11 = 0;
      |Y #dsm35301#12 = 0; |Y #dsm35301#13 = 0;
         #t1735301#27 = "X";
     |FINSI;

     |SI #dsm35301#15 ! 0;
         #t1735301#128 = eaBIC;
         #t1735301#129 = eaIBAN;

         |QBF eaIBAN;
         |SI eaIBAN = "";  nError = 1;  |FINSI;
     |FINSI;

     |SI #dsm35301#16 ! 0;
         #t1735301#128 = eaIBAN;
     |FINSI;

     |GRABA 020#t1735301.a;
     |LIBERA #t1735301;
|FPRC;

|PROCESO Carga353_2017;
     enCodCli = #dsm35301#0;
     |HAZ LeePersona;

     aFicTmp = "t1735301" + Usuario;
     |NOME_DAT #t1735301#aFicTmp#;
     |ABRE #t1735301;  |CIERRA #t1735301;  |DELINDEX #t1735301;

     |ABRE #t1735301;
     |HAZ Hoja1_2017;
     |CIERRA #t1735301;

     aINI = "<T3530" + #t1735301#10 + #t1735301#11 + "0000>";
     aFIN = "</T3530" + #t1735301#10 + #t1735301#11 + "0000>";

     eaAlfa = aINI;        |HAZ GrabaPortalSin;
     eaAlfa = "<AUX>";     |HAZ GrabaPortalSin;
     eaAlfa = " " * 70;    |HAZ GrabaPortalSin;
     eaAlfa = eaVersMod;   |HAZ GrabaPortalSin;
     eaAlfa = " " * 4;     |HAZ GrabaPortalSin;
     eaAlfa = eaNIFDs;     |HAZ GrabaPortalSin;
     eaAlfa = " " * 213;   |HAZ GrabaPortalSin;
     eaAlfa = "</AUX>";    |HAZ GrabaPortalSin;

     |BUCLE t1735301;
     |DELINDEX #t1735301;

     eaAlfa = aFIN;
     |HAZ GrabaPortalSin;
|FPRC;

|PROCESO dsmow014_2017;
     |ABRE #dsmom004;
     #dsmom004#0  = #dsmow014#0;
     #dsmom004#1  = #dsmow014#1;
     #dsmom004#2  = #dsmow014#2;
     #dsmom004#3  = #dsmow014#5;
     #dsmom004#4  = #dsmow014#7;
     |LEE 040#dsmom004.=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;
     |CIERRA #dsmom004;

     |ABRE #dsm35301;
     #dsm35301#0 = #dsmom004#0;
     #dsm35301#1 = #dsmom004#1;
     #dsm35301#2 = #dsmom004#2;
     #dsm35301#3 = #dsmom004#3;
     #dsm35301#4 = #dsmom004#4;
     #dsm35301#5 = 0;
     |LEE 040#dsm35301.=;
     |SI FSalida ! 0;  |CIERRA #dsm35301;  |ACABA;  |FINSI;
     |CIERRA #dsm35301;

     eaFicheroPlano = "353" + (("00000" + #dsmow014#0) % -105) + ".txt";
     eaFicheroImpre = "353" + (("00000" + #dsmow014#0) % -105) + ".pdf";
     eaFicheroError = "err" + (("00000" + #dsmow014#0) % -105) + ".txt";

     aAlfa = eaPathLanza + eaFicheroPlano;  |RFBORRA aAlfa;
     aAlfa = eaPathLanza + eaFicheroImpre;  |RFBORRA aAlfa;
     aAlfa = eaPathLanza + eaFicheroError;  |RFBORRA aAlfa;

     aAbre = eaPathEmision + eaFicheroPlano;
     |XABRE "BW", aAbre, nHandle2;

     nError = 0;
     |HAZ Carga353_2017;

     |XCIERRA nHandle2;

     enEmpresa    = #dsmow014#0;
     enComple     = #dsmow014#7;
     eaNomEmpresa = #dsmow014#3;

     |HAZ PresentaModelo_04;
|FPRC;

|DEFBUCLE dsmow014C_2017;
     #dsmow014#1;
     6;
     00000, 0000, 00, "S";
     99999, 9999, 99, "S";
     ;
     NULL, dsmow014_2017;
|FIN;

|DEFBUCLE dsmow014N_2017;
     #dsmow014#2;
     6;
     "                                        ", "S";
     "zzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzz", "S";
     ;
     NULL, dsmow014_2017;
|FIN;

|PROCESO Bloque2017;
     aZ12     = "z" * 12;

     |SI #dsmoy000#6 [ 2018;
         eaOpcion = "3";
     |FINSI;

     |SI #dsmoy000#6 ] 2019;
         |HAZ BtnPeriodicas2019;
     |FINSI;

     enPresentar = 0;
     |SI eaOpcion = "3";
         |SI #dsmoy000#6 < 2019;
             |CONFI "0000SPresentamos la declaracion en el momento de crearlas";
             |SI FSalida = 0;
                 enPresentar = 1;
             |FINSI;
         |FINSI;
     |FINSI;

     nSwLee = 0;
     |SI #dsmoy000#8 = "C";
         |BUCLE dsmow014C_2017;
     |SINO;
         |BUCLE dsmow014N_2017;
     |FINSI;

     |SI eaOpcion = "3";
         |SI enPresentar = 0;
             aAlfa =  "      Los ficheros generados estan: " + eaPathInternet + " de su PC";
             |MENAV aAlfa;
         |FINSI;
     |FINSI;

     |SI enDirecta = 1;  |Y enMsv = 1;  |Y enParar = 0;
         |SUB_EJECUTA "|dsmox007";
     |FINSI;
|FPRC;

|PROCESO moy35301;
     |ABRE #dsmom107;
     |LEE 040#dsmom107.p;
     |SI FSalida ! 0;
         |CIERRA #dsmom107;
         |SUB_EJECUTA_NP "dsmom107";
         |ABRE #dsmom107;
     |FINSI;

     |LEE 000#dsmom107.p;
     |SI FSalida ! 0;  |DDEFECTO #dsmom107;  |FINSI;
     |CIERRA #dsmom107;

     enSwTrata      = 0;
     enCambiaMoneda = 1;

     |HAZ Bloque2017;
|FPRC;
