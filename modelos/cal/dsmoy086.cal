|FICHEROS;
   dsmoy086 #0;

   dsmom004 #4;
   dsmom096 #96;

   dsmoc349 #349;
   dsmod349 #350;

   :/basica/def/agifigen #118;
|FIN;

|VARIABLES;
   informe     = "dsmoy086";
   nDEmpresa   = 0;
   nHEmpresa   = 0;
   nCampo      = 0;
   nCampo1     = 0;
   fFecha      = @;
   nAnyo       = 0;
   nMes        = 0;
   aAlfa       = "";
   aAlfa1      = "";
   aAlfa2      = "";
   nPara1      = 0;
   nPrim       = 0;
   nSw         = 0;
   nSwAc       = 0;
   nSwAn       = 0;
   nSwC        = 0;
   nSwV        = 0;
   nSwCAc      = 0;
   nSwCAn      = 0;
   nSwVAc      = 0;
   nSwVAn      = 0;
   nSwHay      = 0;
   nPrint      = 0;
   nEjer       = 0;
   nPeriodo    = 0;
   nPerLeer    = 0;
   nComplem    = 0;
   nModelo     = 0;
   nDPer349    = 0;
   nHPeriodo   = 0;
   nHayModelo  = 0;

   nEmpresa    = 0;
   nImporte    = 0;
   aTipo1      = "";
   aTipo2      = "";
   &nSwLinea   = 0;

   aParam      = "";
   aCampo8     = "";
|FIN;

|INCLUYE i_variar;
|INCLUYE i_varcon;
|INCLUYE i_varlib;

|| //////////////////// Calculos de Pantalla \\\\\\\\\\\\\\\\\\\\\\\\\\
|CALCULO Eano; |TIPO 7;
     fFecha = ~;
     aAlfa  = fFecha % 402;   nMes  = aAlfa;
     aAlfa  = fFecha % -104;  nAnyo = aAlfa;

     |SI nMes [ 3;
         #0#26 = 12;
         #0#1 = nAnyo - 1;
         |HAZ defecto;
         |HAZ Periodo;
         |PINTA #0#1;
         |PINTA #0#26;
         |ACABA;
     |FINSI;

     |SI nMes [ 6;
         #0#26 = 3;
         #0#1 = nAnyo;
         |HAZ defecto;
         |HAZ Periodo;
         |PINTA #0#1;
         |PINTA #0#26;
         |ACABA;
     |FINSI;

     |SI nMes [ 9;
         #0#26 = 6;
         #0#1 = nAnyo;
         |HAZ defecto;
         |HAZ Periodo;
         |PINTA #0#1;
         |PINTA #0#26;
         |ACABA;
     |FINSI;

     |SI nMes [ 12;
         #0#26 = 9;
         #0#1 = nAnyo;
         |HAZ defecto;
         |HAZ Periodo;
         |PINTA #0#1;
         |PINTA #0#26;
    |FINSI;
|FCAL;

|PROCESO defecto; |TIPO 0;
     |SI #0#1 < 2015;
         #0#30 = 250000;
     |SINO;
         #0#30 = 400000;
     |FINSI;
     |SI aParam = "";
         |PINTA #0#30;
     |FINSI;
|FPRC;

|PROCESO Periodo;
     #0#27 = "          ";
     |SI #0#26 = 1;   #0#27 = "ENERO     ";  |FINSI;
     |SI #0#26 = 2;   #0#27 = "FEBRERO   ";  |FINSI;
     |SI #0#26 = 3;   #0#27 = "MARZO     ";  |FINSI;
     |SI #0#26 = 4;   #0#27 = "ABRIL     ";  |FINSI;
     |SI #0#26 = 5;   #0#27 = "MAYO      ";  |FINSI;
     |SI #0#26 = 6;   #0#27 = "JUNIO     ";  |FINSI;
     |SI #0#26 = 7;   #0#27 = "JULIO     ";  |FINSI;
     |SI #0#26 = 8;   #0#27 = "AGOSTO    ";  |FINSI;
     |SI #0#26 = 9;   #0#27 = "SEPTIEMBRE";  |FINSI;
     |SI #0#26 = 10;  #0#27 = "OCTUBRE   ";  |FINSI;
     |SI #0#26 = 11;  #0#27 = "NOVIEMBRE ";  |FINSI;
     |SI #0#26 = 12;  #0#27 = "DICIEMBRE ";  |FINSI;

     |SI #0#27 = "          "; |Y aParam = "";
         |MENAV "     Periodo Incorrecto. ";
         |ERROR;
     |FINSI;

     |SI aParam = ""; |PINTA #0#27; |FINSI;
|FPRC;

|CALCULO Tipo7;  |TIPO 7;
     |SI enEmpresa ! 0;
         #0#2  = enEmpresa;  |PINTA #0#2;
         #0#3  = enEmpresa;  |PINTA #0#3;

         |C_M #0#2, 1, "N";
         |C_M #0#3, 1, "N";
     |FINSI;
|FCAL;

|CALCULO Parrilla; |TIPO 0;
     |SI #0#2 = 0;
         #0#2 = 0;  |PINTA #0#2;
         #0#3 = 0;  |PINTA #0#3;  |C_M #0#3, 1, "N";
         |PARA nCampo = 4;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |SINO;
         |SI enEmpresa = 0;
             |C_M #0#2, 1, "S";
             |C_M #0#3, 1, "S";
         |FINSI;

         |PARA nCampo = 4;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "N";
               #0nCampo = 0;  |PINTA #0nCampo;
         |SIGUE;
     |FINSI;
|FCAL;

|CALCULO NoMasEmpresas; |TIPO 0;
     |SI #0Campo = 0;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
               #0nCampo = 0;  |C_M #0nCampo, 1, "N";
         |SIGUE;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |FINSI;
|FCAL;

|PROCESO AntesOrden; |TIPO 7;
     aAlfa = "S";
     |SI #0#2 = 0;
         aAlfa = "N";
         #0Campo = 1; |PINTA #0Campo;
     |FINSI;
     |C_M #0Campo, 1, aAlfa;
|FPRC;

|| *************************************************************************
|PROCESO LeeModelo;
     aTipo1 = #349#6 % 101;
     aTipo2 = #349#6 % -101;

     |SI aTipo1 = "C"; |O aTipo1 = "D"; |O aTipo1 = "R"; |ACABA; |FINSI;

     |SI aTipo1 = "M"; |O aTipo1 = "H"; |O aTipo1 = "T";
         aTipo1 = "E";
     |FINSI;

     nSw = 0;
     |SI aTipo1 = "E"; |O aTipo1 = "A";

         |SI aTipo2 = " ";  nSw = 1;  |FINSI; || Este Ejercicio

         |SI aTipo2 = "R";   || comprobar periodo rectificado

             |SI #349#25 = 0; |O #349#25 > #349#1;  ||aqui hay un error
                 #349#25 = #349#1;
             |FINSI;

             |SI #349#25 = #349#1;  nSw = 1;  |FINSI;
             |SI #349#25 = nAnyo;   nSw = 2;  |FINSI;
         |FINSI;

    |FINSI;

    |SI nSw ! 0;
        |SI aTipo2 = " "; nImporte = #349#22; |FINSI;
        |SI aTipo2 = "R"; nImporte = (#349#22 - #349#27); |FINSI;

        |SI nSw = 1;
            |SI aTipo1 = "E";  #0#40 = #0#40 + nImporte; |FINSI;
            |SI aTipo1 = "A";  #0#41 = #0#41 + nImporte; |FINSI;
        |FINSI;

        |SI nSw = 2;
            |SI aTipo1 = "E";  #0#42 = #0#42 + nImporte; |FINSI;
            |SI aTipo1 = "A";  #0#43 = #0#43 + nImporte; |FINSI;
        |FINSI;

        nSwHay   = 1;
    |FINSI;
|FPRC;

|DEFBUCLE LeeModelo;
   #349#1;
   ;
   #4#0, #4#1, nDPer349, #4#3, #4#4, 01, "  ", 0000, 00, "            ";
   #4#0, #4#1, #4#2,     #4#3, #4#4, 98, "zz", 9999, 99, "zzzzzzzzzzzz";
   e;
   NULL, LeeModelo;
|FIN;

|PROCESO LeeModelos_M;
  nHayModelo = 1;
  nComplem = #4#4;
  |SI #4#4 > 0; |Y #4#24 = "S";
      |PDEFECTO #0,40,44;     || vuelve a acumular
  |FINSI;
  |SI #4#22 = "M"; nDPer349 = #4#2;     |FINSI;
  |SI #4#22 = "T"; nDPer349 = #4#2 - 2; |FINSI;
  |BUCLE LeeModelo;
|FPRC;

|DEFBUCLE LeeModelos_M;
  #4#1;
  ;
  nEmpresa, nEjer, nPerLeer, nModelo, 00;
  nEmpresa, nEjer, nPerLeer, nModelo, 99;
  e;
  NULL, LeeModelos_M;
|FIN;

|PROCESO LeeModelos_M004;

  |PARA nPerLeer = 1; |SI nPerLeer [ nHPeriodo; |HACIENDO nPerLeer = nPerLeer + 1;

        nHayModelo = 0;
        |PDEFECTO #0,40,44;
        |BUCLE LeeModelos_M;

        |SI nHayModelo = 1;
           |SI nPrim = 1;
               #0#34 = #0#34 + #0#40;  || Acum. <E> + Ejercicio Actual
               #0#35 = #0#35 + #0#41;  || Acum. <A> + Ejercicio Actual

               #0#36 = #0#36 + #0#42;  || Acum. <E> + Ejercicio Anterior
               #0#37 = #0#37 + #0#43;  || Acum. <A> + Ejercicio Anterior
           |SINO;
               #0#36 = #0#36 + #0#40;  || Acum. <E> + Ejercicio Anterior
               #0#37 = #0#37 + #0#41;  || Acum. <A> + Ejercicio Anterior
           |FINSI;
        |FINSI;
  |SIGUE;
|FPRC;

|| *************************************************************************
|PROCESO Actualiza;
       |ABRE #96;
       #96#0 = enEmpresa;
       #96#1 = enEjer;
       #96#2 = enPeriodo;
       #96#3 = enModelo;
       #96#8 = aCampo8;
       |LEE 141#96=;
       |SI FSalida = 0;
           #96#6 = "D";
           |GRABA 020#96a;
       |FINSI;
       |LIBERA #96;
       |CIERRA #96;
|FPRC;

|PROCESO ActualizaTodos;
   |SI nSwHay = 1;

       #96#0 = nEmpresa;
       #96#1 = #0#1;
       #96#2 = nPeriodo;
       #96#3 = enModelo;
       #96#8 = aCampo8;
       |LEE 141#96=;
       |SI FSalida ! 0;
           |DDEFECTO #96;
           #96#0 = nEmpresa;
           #96#1 = #0#1;
           #96#2 = nPeriodo;
           #96#3 = enModelo;
           #96#8 = aCampo8;
           |GRABA 020#96b;
       |FINSI;

       |SI #96#4 ! #0#39; #96#6 = "A"; |FINSI;
       #96#4 = #0#39;
       |SI nSwC = 1; #96#5 = "C"; |FINSI;
       |SI nSwV = 1; #96#5 = "V"; |FINSI;
       |SI nSwC = 1; |Y nSwV = 1; #96#5 = "A"; |FINSI;
       |SI #0#45 ! "N"; #96#6 = #0#45; |FINSI;

       #96#7 = #0#38;
       |GRABA 020#96a;
       |LIBERA #96;

   |SINO;

       #96#0 = nEmpresa;
       #96#1 = #0#1;
       #96#2 = nPeriodo;
       #96#3 = enModelo;
       #96#8 = aCampo8;
       |LEE 141#96=;
       |SI FSalida = 0;
           |BORRA 041#96a;
       |FINSI;
       |LIBERA #96;
   |FINSI;
|FPRC;

|PROCESO LEmpresa;

  nEmpresa  = #agifigen#0;

  |PDEFECTO #0,31,40;
  #0#31 = nEmpresa;
  #0#32 = #agifigen#13;
  #0#33 = #agifigen#1;
|| ------------------------------------------------------------------------
  || Lee Periodicidad Modelo 349

   nSwHay    = 0;

   nPrim     = 1;
   nEjer     = #0#1;
   nAnyo     = #0#0;
   nHPeriodo = #0#26;
   |HAZ LeeModelos_M004;

   nPrim     = 2;
   nEjer     = #0#0;
   nAnyo     = - 1;
   nHPeriodo = 12;
   |HAZ LeeModelos_M004;

   nSw  = 0; nSwAc  = 0; nSwAn  = 0;
   nSwV = 0; nSwVAc = 0; nSwVAn = 0;
   nSwC = 0; nSwCAc = 0; nSwCAn = 0;

   |SI nSwHay = 1;

      |SI #0#34 > #0#30;  nSwV = 1; nSw = 1; nSwVAc = 1;  |FINSI;
      |SI #0#36 > #0#30;  nSwV = 1; nSw = 1; nSwVAn = 1;  |FINSI;

      |SI #0#35 > #0#30;  nSwC = 1; nSw = 1; nSwCAc = 1;  |FINSI;
      |SI #0#37 > #0#30;  nSwC = 1; nSw = 1; nSwCAn = 1;  |FINSI;

      |SI nSwCAc = 1; |Y nSwVAc = 1; nSwAc = 1; |FINSI;
      |SI nSwCAn = 1; |Y nSwVAn = 1; nSwAn = 1; |FINSI;

      aAlfa = "";
      |SI nSw = 1;

          #0#39 = "S";
          |SI nSwC = 1; |Y nSwV = 1;
               |Y nSwAn = 1;

              aAlfa = "superado en Ejercicio " + #0#0;
              aAlfa = aAlfa + " por Introduccion y Expedicion";

          |SINO;

              |SI nSwC = 1; |Y nSwV = 1;
                 |Y nSwAc = 1; |Y nSwCAn = 0; |Y nSwVAn = 0;
                  aAlfa = "superado en Ejercicio " + #0#1;
                  aAlfa = aAlfa + " por Introduccion y Expedicion";

              |SINO;

                  aAlfa1 = "Ejercicio "; aAlfa2 = "superado ";
                  |SI nSwV = 1; |Y nSwC = 1;
                      aAlfa1 = "Ejer. "; aAlfa2 = "";
                  |FINSI;

                  |SI nSwC = 1;

                      aAlfa = "superado en " + aAlfa1;
                      |SI nSwCAn = 1;
                          aAlfa = aAlfa + #0#0;
                      |SINO;
                          aAlfa = aAlfa + #0#1;
                      |FINSI;
                      aAlfa =  aAlfa + " por Introduccion";
                  |FINSI;

                  |SI nSwC = 1; |Y nSwV = 1;
                      aAlfa = aAlfa + " y ";
                  |FINSI;

                  |SI nSwV = 1;
                      aAlfa = aAlfa + aAlfa2 + "en " + aAlfa1;

                      |SI nSwVAn = 1;
                          aAlfa = aAlfa + #0#0;
                      |SINO;
                          aAlfa = aAlfa + #0#1;
                      |FINSI;
                      aAlfa = aAlfa + " por Expedicion";
                  |FINSI;
              |FINSI;
          |FINSI;

          #0#38 = aAlfa;
          #0#44 = #0#44 + 1;
       |FINSI;

       |SI #0#24 = "T"; |O nSw ! 0;
           |SI aParam = "";
               nSwLinea = 0;
               |SI nSw = 1; nSwLinea = 1; |FINSI;
               |PRINT;
           |FINSI;
           nPrint = 1;
       |FINSI;

   |FINSI;

   |SI aParam ! "";
       |HAZ ActualizaTodos;
   |SINO;
       enModelo = 303; |HAZ ActualizaTodos;
       enModelo = 349; |HAZ ActualizaTodos;
   |FINSI;

|FPRC;

|DEFBUCLE agifigen;
   #agifigen#1;
   ;
   nDEmpresa;
   nHEmpresa;
   e;
   NULL, LEmpresa;
|FIN;

|DEFBUCLE agifigen_2;
   #agifigen#2;
   0;
   "                                        ", nDEmpresa;
   "zzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzz", nHEmpresa;
   e;
   NULL, LEmpresa;
|FIN;

|DEFBUCLE agifigen_3;
   #agifigen#4;
   0;
   "            ", nDEmpresa;
   "zzzzzzzzzzzz", nHEmpresa;
   e;
   NULL, LEmpresa;
|FIN;

|| \\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\

|CALCULO Tipo3;  |TIPO 3;

    #0#0     = #0#1 - 1;
    nModelo  = #0#28;
    nPeriodo = #0#26;
    aAlfa = #0#27; |QBF aAlfa;
    #0#27 = ("          " + aAlfa) % -110;

    |SI aParam = "";
        |MENAV "0000 Recuerde seleccionar el papel de la impresora en Horizontal";
        |IMPRE 0;
        |SI FSalida ! 0;
            |MENAV "    Proceso Interrumpido";
            |ACABA;
        |FINSI;

        |INFOR informe;
        |SI FSalida ! 0;
            |MENAV "    Error en el Informe";
            |ACABA;
        |FINSI;
    |FINSI;

    nPrint = 0;
    |ABRE #4;
    |ABRE #96;
    |SI #0#2 ! 0;
        nDEmpresa = #0#2;
        nHEmpresa = #0#3;
        |SI #0#29 = 1; |BUCLE agifigen;   |FINSI;
        |SI #0#29 = 2; |BUCLE agifigen_2; |FINSI;
        |SI #0#29 = 3; |BUCLE agifigen_3; |FINSI;
    |SINO;
        |PARA nPara1 = 4;  |SI nPara1 [ 23;  |HACIENDO nPara1 = nPara1 + 1;
              |SI #0nPara1 ! 0;
                  nDEmpresa = #0nPara1;
                  nHEmpresa = #0nPara1;
                  |BUCLE agifigen;
              |FINSI;
        |SIGUE;
    |FINSI;
    |CIERRA #96;
    |CIERRA #4;

    |SI aParam = "";
        |SI nPrint = 0;
            |MENAV "     Seleccion Vacia.";
        |SINO;
            |PIEINF;
        |FINSI;
        |FININF;
        |FINIMP;
    |SINO;
        |SI nPrint ! 0;
            |SI #96#4 = "S"; |Y #96#6 = "A";
                |MENAV "0000Obligado a presentar MODELO INTRASTAT ";
                aAlfa = "0000" + #96#7; |QBF aAlfa;
                |MENAV aAlfa;

                |CONFI "0000NDesactivar aviso ";
                |SI FSalida = 0;
                    |HAZ Actualiza;
                |FINSI;
            |FINSI;
        |FINSI;
    |FINSI;
|FCAL;


|PROGRAMA;
     aParam = PARAMETRO; |QBF aParam;

     aCampo8 = "I";
     |SI aParam ! "";
         |DDEFECTO #0;
         #0#1 = enEjer;
         #0#2 = enEmpresa;
         #0#3 = enEmpresa;
         #0#24 = "O";
         #0#26 = enPeriodo;
         #0#45 = "N";
         |HAZ defecto;
         |HAZ Periodo;

         |HAZ Tipo3;
         |ACABA;
     |FINSI;

     |MANTE #0;
|FPRO;
