|FICHEROS;
   dsmoy040 #0;
   dsmom041 #41;
   dsmom042 #42;
   dsmom040 #40;         ||Grupos de Amortizacion
   dsmom100 #100;

   :/contagen/def/caconimp #306;  || configuracion impresiones

   dsmow030 #900;
   dsmow042 #999;       ||Temporal grabacion cuenta
|FIN;

|VARIABLES;
   informe = "dsmoy040";
   &entrada = 1;
   &tipo = "AMORTIZACIONES";
   amo = "A";
   &impo = 0;
   &total = 0;
   &totcue = 0;
   &lintot = 0;
   &linnor = 0;
   &linant = 0;
   &ulti = "";
   &impoc = 0;
   &totalc = 0;
   &totcuec = 0;
   &nof = "";
   &noc = "";
   t1 = "NORMAL";
   t2 = "DEGRESIVA";
   t3 = "ACELERADA";
   t4 = "PAGOS LEASING";
   t5 = "RESIDUAL";
   t6 = "MANUAL";

   aFichero  = "";
   nCampo    = 0;
   nDEmpresa = 0;
   nHEmpresa = 0;
   nDActividad = 0;
   nHActividad = 0;
   swCuenta = 0;
   swPrimero = 0;
   nLaEmpresa = 0;
   swBueno = 0;
   &nCamSel = 23;
   &nCamTip = 38;
   &eaEmp   = "";
   nGrupo   = 0;
   nTip     = 0;
   aAlfa1   = "";
   aAlfa2   = "";
   aPath    = "";
   nAnyFin  = 0;

   nDias  = 0;
   nNume1 = 0;
   nNume2 = 0;
   nNume3 = 0;
   nCanti1 = 0;
   nCanti2 = 0;
   nCanti3 = 0;
   nCanti4 = 0;
   &EURODB = 0;
   fecsys = @;
   aCuenta = "";
|FIN;

|INCLUYE i_variar;
|INCLUYE i_varcon;
|INCLUYE i_varlib;
|INCLUYE i_numamo;
|INCLUYE i_selcon;

|| //////////////////// Calculos de Pantalla \\\\\\\\\\\\\\\\\\\\\\\\\\
|PROCESO DatosEmp; |TIPO 7;

   |DDEFECTO #306;

   |ABRE #100;
   |LEE 040#100p;
   |SI FSalida = 0;
       aAlfa1 = #100#1; |QBF aAlfa1; aAlfa1 = ("00000" + aAlfa1) % -105;
       aAlfa2 = #100#2; |QBF aAlfa2; aAlfa2 = ("0"     + aAlfa2) % -101;
       |DBASS aPath; |QBT aPath;
       aPathConta = aPath + "contagen/fich/" + aAlfa1 + aAlfa2 + "/";

       |PATH_DAT #306 aPathConta;

       |ABRE #306;
       #306#0 = 1;
       |LEE 000#306=;
       |CIERRA #306;
   |FINSI;
   |CIERRA #100;

   #0#39  = #306#7; |PINTA #0#39;
   aAlfa1 = #306#8; |C_M #0#39,1,aAlfa1;
|FPRC;


|CALCULO Tipo7;  |TIPO 7;
     |SI enEmpresa ! 0;
         #0#9  = enEmpresa;  |PINTA #0#9;
         #0#10 = enEmpresa;  |PINTA #0#10;

         |C_M #0#9,  1, "N";
         |C_M #0#10, 1, "N";
     |FINSI;
|FCAL;

|CALCULO Parrilla;
     |SI #0#9 = 0;
         #0#9 = 0;   |PINTA #0#9;
         #0#10 = 0;  |PINTA #0#10;  |C_M #0#10, 1, "N";
         |PARA nCampo = 11;  |SI nCampo [ 22;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |SINO;
         |SI enEmpresa = 0;
             |C_M #0#9, 1, "S";
             |C_M #0#10, 1, "S";
         |FINSI;

         |PARA nCampo = 11;  |SI nCampo [ 22;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "N";
               #0nCampo = 0;  |PINTA #0nCampo;
         |SIGUE;
     |FINSI;
|FCAL;

|CALCULO NoMasEmpresas;
     |SI #0Campo = 0;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 22;  |HACIENDO nCampo = nCampo + 1;
               #0nCampo = 0;  |C_M #0nCampo, 1, "N";
         |SIGUE;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 22;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |FINSI;
|FCAL;

|CALCULO numamo; |TIPO 0;
   x_alfa1 = #0Campo;
   |HAZ x_punto;
   #0Campo = x_alfa1;
   |PINTA #0Campo;
|FCAL;

|CALCULO cuenta; |TIPO 0;
     salidas  = FSalida;

     |ABRE #100;
     |LEE 040#100p;
     |SI FSalida ! 0;  |DDEFECTO #100;  |FINSI;
     |CIERRA #100;

     nLaEmpresa = enEmpresa;

     |SI enEmpresa = 0;
         enEmpresa  = #100#1;
         enPerConta = #100#2;
     |FINSI;

     eaCuenta = #0Campo;
     |HAZ FiltraPuntos;
     #0Campo = eaCuenta;
     |PINTA #0Campo;
     enEmpresa = nLaEmpresa;
|FCAL;

|CALCULO Tipo3;  |TIPO 3;
     enDatos = 0; |SI #0#39 = "S"; enDatos = 1; |FINSI;
     enEjer = #0#5;
     aAlfa1 = #0#4; aAlfa1 = "01.01." + aAlfa1; efDFecha = aAlfa1;
     aAlfa1 = #0#5; aAlfa1 = "31.12." + aAlfa1; efHFecha = aAlfa1;
     eaTituloL = "INFORME DE " + tipo;

     |IMPRE 0;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |INFOR "dsmoy040";
     |SI FSalida ! 0;  |FINIMP;  |ACABA;  |FINSI;

     aFichero = ("40y" + Usuario) % 108;
     |NOME_DAT #999 aFichero;
     |ABRE #999;  |CIERRA #999;  |DELINDEX #999;

     nDActividad = 00;
     nHActividad = 99;
     efFecTope   = #0#6;

     |SI #0#9 ! 0;
         nDEmpresa = #0#9;
         nHEmpresa = #0#10;
         |HAZ imprimir;
     |SINO;
         |PARA nCampo = 11; |SI nCampo [ 22;  |HACIENDO nCampo = nCampo + 1;
               nDEmpresa = #0nCampo;
               nHEmpresa = #0nCampo;
               |HAZ imprimir;
         |SIGUE;
     |FINSI;
     |FININF;
     |FINIMP;

     |DELINDEX #999;
|FCAL;

|| //////////////////// Procesos del Informe  \\\\\\\\\\\\\\\\\\\\\\\\\\
|PROCESO GrabaElAuxi;
   #40#0 = #41#0;
   #40#1 = #41#1;
   #40#2 = nGrupo;
   |LEE 001#40=;
   |SI FSalida ! 0;  |ACABA;   |FINSI;

   aCuenta = #40#3; |SI #0#40 ] 2008; aCuenta = #40#10; |FINSI;

   |SI aCuenta ] #0#0; |Y aCuenta [ #0#1;
       #999#0 = #40#0;    || Empresa
       #999#2 = aCuenta;  || Cuenta Gastos
       #999#3 = #41#2;    || Elemento
       #999#4 = #41#3;    || Numero
       |LEE 041#999=;
       |SI FSalida ! 0;
           #999#0 = #40#0;   || Empresa
           #999#1 = #41#1;   || Actividad
           #999#2 = aCuenta; || Cuenta Gastos
           #999#3 = #41#2;   || Elemento
           #999#4 = #41#3;   || Numero
           #999#5 = nTip;    || Tipo 0 = normal, 1/2 = codigo financiacion
           |GRABA 020#999n;
        |SINO;
           #999#5 = 0;
           |GRABA 021#999a;
        |FINSI;
        |LIBERA #999;
   |FINSI;
|FPRC;

|PROCESO graba_tmp;

   |SI nLaEmpresa ! #41#0;
       nLaEmpresa = #41#0;
       enEmpresa  = #41#0;
       swBueno = 0;
       |HAZ LaSelEmpresa;
       |SI enSwSelEmp = 1; swBueno = 1;  |FINSI;
   |FINSI;
   |SI swBueno = 1; |ACABA; |FINSI;

   |SI #0#7 = "N";
       |SI #41#25 = "N"; |ACABA; |FINSI;
   |FINSI;

   |SI #41#36 = 0; |O #41#38 = 0;
       nTip   = 0;
       nGrupo = #41#18;
       |HAZ GrabaElAuxi;
   |SINO;
       nTip   = 1;
       nGrupo = #41#18;
       |HAZ GrabaElAuxi;
       nTip   = 2;
       nGrupo = #41#38;
       |HAZ GrabaElAuxi;
   |FINSI;
|FPRC;

|DEFBUCLE carga_tmp;
   #41#1;
   1;
   nDEmpresa,#0#2,#0#36,nDActividad;
   nHEmpresa,#0#3,#0#37,nHActividad;
   e;
   NULL , graba_tmp;
|FIN;

|| .....................................................................
|PROCESO imprelin;

      nCanti1 = 0; nCanti2 = 0; nCanti3 = 0; nCanti4 = 0;

      |SI #999#5 = 0;
          nCanti1 = #42#7;
          nCanti2 = #42#11;

      |SINO;

          |SI #42#5 ! nAnyFin;

              |SI #999#5 = 1; |Y #42#5 > nAnyFin;
                  nCanti1 = #42#7;
                  nCanti2 = #42#11;
              |FINSI;

              |SI #999#5 = 2; |Y #42#5 < nAnyFin;
                  nCanti1 = #42#7;
                  nCanti2 = #42#11;
              |FINSI;

          |SINO;

              |HAZ CalDiasAny;
              nNume1 = #41#37;  nNume2 = #42#9;
              nNume3 = nNume2 - nNume1;
              nCanti3 = ((#42#7  * nNume3) / nDias) ! EURODB;
              nCanti4 = ((#42#11 * nNume3) / nDias) ! EURODB;

              |SI #999#5 = 1;         || desde el pago
                  nCanti1 = nCanti3;
                  nCanti2 = nCanti4;
              |FINSI;
              |SI #999#5 = 2;         || hasta el pago
                  nCanti1 = #42#7  - nCanti3;
                  nCanti2 = #42#11 - nCanti4;
              |FINSI;

          |FINSI;

      |FINSI;

      impo   = impo   + nCanti1; impoc   = impoc   + nCanti2;
      total  = total  + nCanti1; totalc  = totalc  + nCanti2;
      totcue = totcue + nCanti1; totcuec = totcuec + nCanti2;

|FPRC;

|DEFBUCLE LasLineas;
 #dsmom042#1;
 6,5;
 #41#0, #41#2, #41#3, 001, "A", #0#4;
 #41#0, #41#2, #41#3, 999, "A", #0#5;
 e;
 NULL, imprelin;
|FIN;

|PROCESO LaImpresion;

   #41#0 = #999#0;
   #41#1 = #999#1;
   #41#2 = #999#3;
   #41#3 = #999#4;
   |LEE 001#dsmom041.=;
   |SI FSalida ! 0;  |ACABA;  |FINSI;

   |SI swPrimero = 0;  ||Controla el primer pase
       swPrimero = 1;
       enEmpresa   = #999#0;
       eaEmp       = enEmpresa; |QBF eaEmp;
       eaEmp       = ( "00000" + eaEmp) % -105;
       eaNombreEmp = #41#32;
       ulti        = #999#2;       || Cuenta de Gastos
       swCuenta    = 0;
       |SI enDatos = 1; enActividad = 0;  |HAZ DatosEmpr_w30; |FINSI;
   |FINSI;

   |SI enEmpresa ! #999#0; || Controla la Ruptura por Empresas
       |SI swCuenta = 1;
           lintot = 1;
           |PRINT;
           |PIEINF; || imprime el pie del informe
       |FINSI;
       enEmpresa   = #999#0;
       eaEmp       = enEmpresa; |QBF eaEmp;
       eaEmp       = ( "00000" + eaEmp) % -105;
       eaNombreEmp = #41#32;
       ulti        = #999#2;       || Cuenta de Gastos
       swCuenta    = 0;
       |SI enDatos = 1; enActividad = 0;  |HAZ DatosEmpr_w30; |FINSI;
   |FINSI;

   |SI swCuenta =  0;   || Primera cuenta de esta Empresa
       lintot = 0;
       linnor = 1;
       linant = 0;
   |SINO;
        |SI #999#2 ! ulti;
            lintot = 1;
            |PRINT; || linea del informe
            totcue = 0; totcuec = 0;
            lintot = 0;
            linant = 0;
            linnor = 1;
        |SINO;
            lintot = 0;
            linnor = 0;
            linant = 1;
         |FINSI;
    |FINSI;

    ulti = #999#2;
    nof = t1; noc = t1;
    |SI #41#13 = "D"; nof = t2; |FINSI;
    |SI #41#13 = "A"; nof = t3; |FINSI;
    |SI #41#13 = "F"; nof = t4; |FINSI;
    |SI #41#13 = "R"; nof = t5; |FINSI;
    |SI #41#13 = "M"; nof = t6; |FINSI;

    |SI #41#19 = "D"; noc = t2; |FINSI;
    |SI #41#19 = "A"; noc = t3; |FINSI;
    |SI #41#19 = "F"; noc = t4; |FINSI;
    |SI #41#19 = "R"; noc = t5; |FINSI;
    |SI #41#19 = "M"; noc = t6; |FINSI;
    nof = "  " + ("00" + #41#57) % -102;
    noc = nof;

    nAnyFin = 0;
    |SI #41#36 ! 0;
        aAlfa1 = #41#37;
        aAlfa1 = aAlfa1 % -104;
        nAnyFin = aAlfa1;
    |FINSI;
    |BUCLE LasLineas; || lee las lineas de esta cabecera
    |SI impo ! 0; |O impoc ! 0;
        |PRINT;
         swCuenta = 1;
    |SINO;
         |SI #0#8 = "S";
             |PRINT;
             swCuenta = 1;
         |FINSI;
    |FINSI;
    lintot = 0;
    linnor = 0;
    linant = 0;
    impo = 0; impoc = 0;
|FPRC;

|DEFBUCLE dsmoy040;
   #999#1;
   ;
   nDEmpresa, #0#0, #0#2, #0#36;
   nHEmpresa, #0#1, #0#3, #0#37;
   e;
   NULL, LaImpresion;
|FIN;


|PROCESO  imprimir;
      nLaEmpresa = -1;
      swBueno    = 0;

      |ABRE #40;
      |ABRE #999;
      |BUCLE carga_tmp;
      |CIERRA #999;
      |CIERRA #40;

      swPrimero = 0;
      swCuenta = 0;
      |ABRE #41;
      |BUCLE dsmoy040;
      |SI swCuenta = 1;
          lintot = 1;
          |PRINT;
          |PIEINF; || imprime el pie del informe
      |FINSI;
      |CIERRA #41;

      |DELINDEX #999;
|FPRC;

|PROCESO CalDiasAny;
  nDias = 365;
  |SI nAnyFin = 1980;  |O nAnyFin = 1984;  |O nAnyFin = 1988;  |O nAnyFin = 1992;
                       |O nAnyFin = 1996;  |O nAnyFin = 2000;  |O nAnyFin = 2004;
                       |O nAnyFin = 2008;  |O nAnyFin = 2012;  |O nAnyFin = 2016;
                       |O nAnyFin = 2020;  |O nAnyFin = 2024;  |O nAnyFin = 2028;
                       |O nAnyFin = 2032;  |O nAnyFin = 2036;  |O nAnyFin = 2040;
                       |O nAnyFin = 2044;  |O nAnyFin = 2048;  |O nAnyFin = 2052;
                       |O nAnyFin = 2056;  |O nAnyFin = 2060;  |O nAnyFin = 2064;
      nDias = 366;
  |FINSI;
|FPRC;
