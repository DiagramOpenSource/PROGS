|FICHEROS;
     dsmoy073;

     dsmom002;
     dsmom004;

     dsmod303;
     dsmod310;
     dsmod370;
     dsm30301;
     dsmod309;

     :/contasoc/def/isacceso;
     :/contasoc/def/isocie29;
     :/contasoc/def/isom9000;

     :/integral/def/dsam0001;
     :/integral/def/dsam0121;
     :/integral/def/dsam0122;
     :/integral/def/dsam0054;

     :/basica/def/agitab99;

     dsmow121;
     &clientes@;
|FIN;

|VARIABLES;
     aPathDoc         = "";
     aAbre            = "";
     aAlfa            = "";
     aAlfa1           = "";
     aAlfa2           = "";
     aAlfa3           = "";
     aOrigen          = "";
     aDestino         = "";
     aDescri          = "";
     aIPRemoto        = "";
     aCadena          = "";
     aLen             = "";
     aAsigna          = "";
     aCaracter        = "";
     aIdenti          = "";
     aMenav           = "";
     aDocumento       = "";
     aPeriodo         = "";
     aPathSoc         = "";
     aEntero          = "";
     aDecima          = "";
     aLong            = "";
     aImpor1          = "";
     aImpor2          = "";
     aImpor3          = "";
     aImpor4          = "";
     aDImpreso        = "";
     aHImpreso        = "";
     aDImp202         = "";
     aHImp202         = "";
     aPer             = "";
     aFichero         = "";
     aLista           = "";
     aBase            = "";
     aBaseApli        = "";
     aNomFic          = "";
     aAbreTxt         = "";

     nSwOpera         = 0;
     nSwHay           = 0;
     nSwGraba         = 0;
     nComplem         = 0;
     nLong            = 0;
     nDecimal         = 0;
     nCaracter        = 0;
     nPeriodo         = 0;
     nEmpresa         = 0;
     nBanco           = 0;
     nHandle          = 0;
     nPara            = 0;
     nResultado       = 0;
     nWord            = 0;
     nLinea           = 0;
     nLen             = 0;
     nPos             = 0;
     nCampo           = 0;
     nMes             = 0;
     nAnyo            = 0;
     nPago            = 0;
     nOpcion          = 0;
     nError           = 0;

     fFecha           = @;

     {-1}aMenu        = "";
         aMenu1       = "2400";
         aMenu2       = "1";
         aMenu3       = "Archivo documental activado. Elija opci¢n: ";
         aMenu4       = "IAB";
         aMenu5       = "Imprimir";
         aMenu6       = "Archivar";
         aMenu7       = "Ambos";

     &empresa         = 0;
     &anyo            = 0;
     &enSwDeDonde     = 0;
     &enModelo        = 0;
     &eaModelo        = "";
     &enForzar        = 0;
     &enCodCli        = 0;

     &eaEntidad       = "";
     &eaOficina       = "";
     &eaDC            = "";
     &eaCuenta        = "";
     &eaRutaDocumento = "";
     &enFSalida       = 0;
     &enCodUbica      = 0;
     &eaNomUbicacion  = "";
     &eaAlfa          = "";
     &eaImporte       = "";
     &enBanco         = 0;
     &eaFormaPago     = "";
     &eaNomEnt        = "";
     &eaNomOfi        = "";
     &eaUnidadBasico  = "";
     &eaDSMAC         = "";


     &enEmpresa          = 0;
     &enEjer             = 0;
     &enPeriodo          = 0;
     &enNume             = 0;
     &enErrorPINET       = 0;
     &enEmpresaPINET     = 0;

     &enErrorDSArchi     = 0;
     &eaVengoDe          = "";
     &enArchi            = 0;
     &eaInforme          = "";
     &enEjercicio        = 0;
     &eaNifEmp           = "";
     &eaNombreEmp        = "";
     &enCodTrab          = 0;
     &eaNombreTra        = "";
     &eaNifTra           = "";
     &eaObservacion1     = "";
     &eaObservacion2     = "";
     &eaObservacion3     = "";
     &enM2               = 0;
     &eaAsigna           = "";
     &eaDirTrabajo       = "";
     &eaDirTrabServi     = "";
     &eaDirPlantilla     = "";
     &eaDirSerPlan       = "";
     &eaFicheroPdf       = "";
     &eaFicheroTra       = "";
     &eaFicheroTxt       = "";
     &enHandleTxt        = 0;
     &eaOrigen           = "";
     &eaDestino          = "";
     &eaOrig             = "";
|FIN;

|PROCESO GuardaDocumento;
     |ABRE #dsam0054;
     #dsam0054#0  = nEmpresa;
     #dsam0054#1  = 9999;
     |LEE 040#dsam0054.];
     |SI FSalida = 0;
         |LEE 040#dsam0054.a;
     |SINO;
         |LEE 040#dsam0054.u;
     |FINSI;
     |SI FSalida = 0;  |Y #dsam0054#0 = nEmpresa;
         nLinea = #dsam0054#1 + 1;
     |SINO;
         nLinea = 1;
     |FINSI;

     |DDEFECTO #dsam0054;
     |HORA aAlfa;
     aAlfa1 = aAlfa % 102;
     aAlfa2 = aAlfa % 402;

     #dsam0054#0  = nEmpresa;
     #dsam0054#1  = nLinea;
     #dsam0054#2  = aDescri;
     #dsam0054#3  = "DOC" + (("00000" + #dsam0054#0) % -105) + (("0000" + #dsam0054#1) % -104) + ".DOC";
     #dsam0054#4  = ~;
     #dsam0054#5  = aAlfa1;
     #dsam0054#6  = aAlfa2;
     #dsam0054#7  = ~;
     #dsam0054#8  = aAlfa1;
     #dsam0054#9  = aAlfa2;
     #dsam0054#10 = #dsmoy073#37;
     #dsam0054#11 = "";
     #dsam0054#12 = "";
     |GRABA 020#dsam0054.n;
     |LIBERA #dsam0054;
     |CIERRA #dsam0054;

     aOrigen  = aAbre;
     aDestino = eaRutaDocumento + "/" + "DOC" + (("00000" + nEmpresa) % -105) + (("0000" + nLinea) % -104) + ".DOC";
     |QBT aOrigen;
     |QBT aDestino;

     |RCOPIA_FICHERO aOrigen, aDestino;
     |SI FSalida ! 0;
         |MENAV "     No se ha podido copia el Documento al Cliente.";
     |FINSI;
|FPRC;

|PROCESO AbreWord;
     aAlfa = eaUnidadBasico + "/MODELOS";                   |RMKDIR aAlfa;
     aAlfa = eaUnidadBasico + "/MODELOS/DOCUMENTOS";        |RMKDIR aAlfa;

     aOrigen   = aPathDoc + aDocumento;
     aDestino  = eaUnidadBasico + "/MODELOS/DOCUMENTOS/" + aDocumento;
     aDescri   = #dsmoy073#39;  |QBF aDescri;
     aDescri   = aDescri + " PERIODO " + #dsmoy073#42 + " EJERCICIO " + #dsmoy073#44;

     aIPRemoto = "";
     |IP_REMOTO aIPRemoto;  |QBT aIPRemoto;

     |SI aIPRemoto ! "";
         |ENVIA_FICHERO aOrigen, aDestino;
     |SINO;
         |COPIA_FICHERO aOrigen, aDestino;
     |FINSI;

     nWord  = 1;
     aAbre  = aDestino;

     |MENSA "      Abriendo el Documento, espere un Momento.";

     |WORD_ABRE nWord, aAbre;
     |WORD_PROPIEDADES nWord, "Visible", "NO";
|FPRC;

|PROCESO Asigna;
     |QBF aAsigna;
     aCadena = "";
     aLen    = aAsigna % 0;
     nLen    = aLen;
     |PARA nCampo = 1;  |SI nCampo [ nLen;  |HACIENDO nCampo = nCampo + 1;
           nPos = (100 * nCampo) + 1;
           aCaracter = aAsigna % nPos;
           |SI aCaracter = "¥";  aCaracter = &209; |FINSI;
           |SI aCaracter = "¤";  aCaracter = &241; |FINSI;
           |SI aCaracter = " ";  aCaracter = &225; |FINSI;
           |SI aCaracter = "‚";  aCaracter = &233; |FINSI;
           |SI aCaracter = "¡";  aCaracter = &237; |FINSI;
           |SI aCaracter = "¢";  aCaracter = &243; |FINSI;
           |SI aCaracter = "£";  aCaracter = &250; |FINSI;
           |SI aCaracter = "§";  aCaracter = &176; |FINSI;
           |SI aCaracter = "¦";  aCaracter = &170; |FINSI;
           aCadena = aCadena + aCaracter;
     |SIGUE;

     eaAlfa   = aCadena;
     eaAsigna = "V" + aIdenti;
     |HAZ AsignaWord;
|FPRC;

|PROCESO SacaFecha;
     aAlfa1 = eaAlfa % 102;
     aAlfa2 = eaAlfa % 402;
     aAlfa3 = eaAlfa % 704;
     |SI aAlfa2 = "01";  aAlfa2 = "Enero";      |FINSI;
     |SI aAlfa2 = "02";  aAlfa2 = "Febrero";    |FINSI;
     |SI aAlfa2 = "03";  aAlfa2 = "Marzo";      |FINSI;
     |SI aAlfa2 = "04";  aAlfa2 = "Abril";      |FINSI;
     |SI aAlfa2 = "05";  aAlfa2 = "Mayo";       |FINSI;
     |SI aAlfa2 = "06";  aAlfa2 = "Junio";      |FINSI;
     |SI aAlfa2 = "07";  aAlfa2 = "Julio";      |FINSI;
     |SI aAlfa2 = "08";  aAlfa2 = "Agosto";     |FINSI;
     |SI aAlfa2 = "09";  aAlfa2 = "Septiembre"; |FINSI;
     |SI aAlfa2 = "10";  aAlfa2 = "Octubre";    |FINSI;
     |SI aAlfa2 = "11";  aAlfa2 = "Noviembre";  |FINSI;
     |SI aAlfa2 = "12";  aAlfa2 = "Diciembre";  |FINSI;

     eaAlfa = aAlfa1 + " de " + aAlfa2 + " de " + aAlfa3;
|FPRC;

|PROCESO Importe;    || Calculo para poner las puntos y comas
                     || en el importe

     aDecima   = "";
     aEntero   = "";
     aLong     = eaImporte % 0;
     nLong     = aLong;
     nDecimal  = 0;
     |PARA nCaracter = 1;  |SI nCaracter [ nLong;  |HACIENDO nCaracter = nCaracter + 1;
           nPos      = (100 * nCaracter) + 1;
           aCaracter = eaImporte % nPos;
           |SI aCaracter = ".";
               nDecimal = 1;
           |SINO;
               |SI nDecimal = 0;
                   aEntero = aEntero + aCaracter;
               |SINO;
                   aDecima = aDecima + aCaracter;
               |FINSI;
           |FINSI;
     |SIGUE;
     aDecima = (aDecima + "00") % 102;
     eaImporte = aEntero;

     aLong = eaImporte % 0;
     nLong = aLong;
     eaImporte = "             " + eaImporte;
     eaImporte = eaImporte % -113;
     aImpor1   = eaImporte % -103;
     aImpor2   = eaImporte % -403;
     aImpor3   = eaImporte % -703;
     aImpor4   = eaImporte % -1004;
     |SI nLong ] 10;
         |SI aImpor4 ! "  -";
             eaImporte = aImpor4 + "." + aImpor3  + "." + aImpor2 + "." + aImpor1;
         |SINO;
             eaImporte = aImpor4 + aImpor3  + "." + aImpor2 + "." + aImpor1;
      |FINSI;
    |SINO;
        |SI nLong ] 7;
            |SI aImpor3 ! "  -";
                eaImporte = aImpor3  + "." + aImpor2 + "." + aImpor1;
            |SINO;
                eaImporte = aImpor3 + aImpor2 + "." + aImpor1;
            |FINSI;
        |SINO;
            |SI nLong ] 4;
                |SI aImpor2 ! "  -";
                    eaImporte = aImpor2 + "." + aImpor1;
                |SINO;
                    eaImporte = aImpor2 + aImpor1;
                |FINSI;
            |SINO;
                eaImporte = aImpor1;
            |FINSI;
        |FINSI;
     |FINSI;

     eaImporte = ("             " + eaImporte + "," + aDecima) % -113;

     |SI eaImporte = "            0";  eaImporte = "             ";  |FINSI;
     |SI eaImporte = "         0,00";  eaImporte = "             ";  |FINSI;
|FPRC;

|PROCESO CargaVariables;
     aAsigna = enCodCli;
     aAsigna = ("00000" + aAsigna) % -105;
     aIdenti = "#NEMP#";
     |HAZ Asigna;

     aAsigna = #clientes@#1;
     aIdenti = "#1#";
     |HAZ Asigna;

     |SI enSwDeDonde = 1;
         aAsigna = #clientes@#3;
         aIdenti = "#NIF#";
         |HAZ Asigna;

         aAsigna = #clientes@#57;
         aIdenti = "#ANAGRAMA#";
         |HAZ Asigna;

         aAsigna = "";
         |SI #clientes@#39 ! "  ";  aAsigna = #clientes@#39;  |FINSI;

         aAsigna = aAsigna + " " + #clientes@#40;  |QBF aAsigna;

         |SI #clientes@#41 ! "     ";  aAsigna = aAsigna + ", " + #clientes@#41;  |QBF aAsigna;  |FINSI;
         |SI #clientes@#42 ! "   ";    aAsigna = aAsigna + " "  + #clientes@#42;  |QBF aAsigna;  |FINSI;
         |SI #clientes@#43 ! "  ";     aAsigna = aAsigna + " "  + #clientes@#43;  |QBF aAsigna;  |FINSI;
         |SI #clientes@#44 ! "  ";     aAsigna = aAsigna + " "  + #clientes@#44;  |QBF aAsigna;  |FINSI;

         aIdenti = "#2#";
         |HAZ Asigna;

         aAsigna = "";
         aAsigna = (("00" + #clientes@#45) % -102) + (("000" + #clientes@#46) % -103);
         aAsigna = aAsigna + " " + #clientes@#47;  |QBF aAsigna;

         aIdenti = "#3#";
         |HAZ Asigna;

         aAsigna = "(" + #clientes@#48;                  |QBF aAsigna;
         aAsigna = aAsigna + ")";
         |SI aAsigna = "()";  aAsigna = "";  |FINSI;
         aIdenti = "#4#";
         |HAZ Asigna;
     |SINO;
         aAsigna = #clientes@#13;
         aIdenti = "#NIF#";
         |HAZ Asigna;

         aAsigna = "    ";
         aIdenti = "#ANAGRAMA#";
         |HAZ Asigna;

         aAsigna = "";
         |SI #clientes@#51 ! "  ";  aAsigna = #clientes@#51;  |FINSI;

         aAsigna = aAsigna + " " + #clientes@#52;  |QBF aAsigna;

         |SI #clientes@#53 ! "     ";  aAsigna = aAsigna + ", " + #clientes@#53;  |QBF aAsigna;  |FINSI;
         |SI #clientes@#54 ! "   ";    aAsigna = aAsigna + " "  + #clientes@#54;  |QBF aAsigna;  |FINSI;
         |SI #clientes@#55 ! "  ";     aAsigna = aAsigna + " "  + #clientes@#55;  |QBF aAsigna;  |FINSI;
         |SI #clientes@#56 ! "  ";     aAsigna = aAsigna + " "  + #clientes@#56;  |QBF aAsigna;  |FINSI;

         aIdenti = "#2#";
         |HAZ Asigna;

         aAsigna = "";
         aAsigna = (("00" + #clientes@#57) % -102) + (("000" + #clientes@#58) % -103);
         aAsigna = aAsigna + " " + #clientes@#59;  |QBF aAsigna;

         aIdenti = "#3#";
         |HAZ Asigna;

         aAsigna = "(" + #clientes@#60;                  |QBF aAsigna;
         aAsigna = aAsigna + ")";
         |SI aAsigna = "()";  aAsigna = "";  |FINSI;
         aIdenti = "#4#";
         |HAZ Asigna;
     |FINSI;

     aAsigna = #dsam0001#4;  |QBF aAsigna;
     eaAlfa  = #dsmoy073#34;  |HAZ SacaFecha;
     aAsigna = aAsigna + ", " + eaAlfa;

     aIdenti = "#5#";
     |HAZ Asigna;

     aAsigna = #dsam0001#1;  |QBF aAsigna;
     aIdenti = "#NOMASESORIA#";
     |HAZ Asigna;

     aAsigna = #dsam0001#2;  |QBF aAsigna;
     aIdenti = "#DESASESORIA#";
     |HAZ Asigna;

     aAsigna = #dsmoy073#44;
     aIdenti = "#EJERCICIO#";
     |HAZ Asigna;

     aAsigna = aPeriodo;
     aIdenti = "#PERIODO#";
     |HAZ Asigna;

     aAsigna = #dsmoy073#35;
     aIdenti = "#DIAFIN#";
     |HAZ Asigna;

     aAsigna = #dsmoy073#51;
     aIdenti = "#FECHATOPE#";
     |HAZ Asigna;

     eaImporte = nResultado;  |HAZ Importe;
     aAsigna   = eaImporte;
     aIdenti = "#IMPORTE#";
     |HAZ Asigna;

     aAsigna   = "";
     |SI eaEntidad ! "";
         aAsigna   = "ENTIDAD..:   " + eaEntidad + " " + eaNomEnt;
     |FINSI;
     aIdenti = "#ENTIDAD#";
     |HAZ Asigna;

     aAsigna   = "";
     |SI eaOficina ! "";
         aAsigna   = "SUCURSAL:   " + eaOficina + " " + eaNomOfi;
     |FINSI;
     aIdenti = "#SUCURSAL#";
     |HAZ Asigna;

     aAsigna   = "";
     |SI eaCuenta ! "";
         aAsigna   = "CUENTA...:   " + eaCuenta;
     |FINSI;
     aIdenti = "#CUENTA#";
     |HAZ Asigna;

     aAsigna = #dsmoy073#40;
     |SI #dsmoy073#44 ] 2011; |Y #dsmoy073#40 = 110;
         aAsigna = "111";
     |FINSI;
     aIdenti = "#MODELO#";
     |HAZ Asigna;
|FPRC;

|PROCESO dsmom002;
     |SI #dsmom002#7 = "MENSUAL   ";
         aPeriodo = "M";
     |FINSI;

     |SI #dsmom002#7 = "TRIMESTRAL";
         aPeriodo = "T";
     |FINSI;
|FPRC;

|DEFBUCLE dsmom002;
     #dsmom002#1;
     ;
     nEmpresa, 01, #dsmoy073#40;
     nEmpresa, 98, #dsmoy073#40;
     ;
     NULL, dsmom002;
|FIN;

|PROCESO CargaVar;
     eaObservacion1 = #dsmoy073#39;
     enEjercicio    = #dsmow121#1;
     enM2           = #dsmow121#2;
     enEmpresa      = #dsmow121#0;
     eaNombreEmp    = #dsmow121#3;
     eaNifEmp       = #dsmow121#10;
     enModelo       = #dsmow121#5;
     eaModelo       = #dsmoy073#41;
     eaNombreTra    = "";
     eaNifTra       = "";
     enCodTrab      = 0;
|FPRC;

|PROCESO Imprime;
     |SI #dsmoy073#40 ! 202; |Y #dsmoy073#40 ! 200;
         |BUCLE dsmom002;

         |SI aPeriodo = "T";
             |SI #dsmoy073#42 = 3;    aPeriodo = "1§ Trimestre";  |FINSI;
             |SI #dsmoy073#42 = 6;    aPeriodo = "2§ Trimestre";  |FINSI;
             |SI #dsmoy073#42 = 9;    aPeriodo = "3§ Trimestre";  |FINSI;
             |SI #dsmoy073#42 = 12;   aPeriodo = "4§ Trimestre";  |FINSI;
         |SINO;
             aPeriodo = "del mes " + (("00" + #dsmoy073#42) % -102);
         |FINSI;
     |SINO;
         |SI #dsmoy073#40 = 202;
             |SI #dsmoy073#42 = 1;    aPeriodo = "1§ pago";  |FINSI;
             |SI #dsmoy073#42 = 2;    aPeriodo = "2§ pago";  |FINSI;
             |SI #dsmoy073#42 = 3;    aPeriodo = "3§ pago";  |FINSI;
         |SINO;
             aPeriodo = "pago anual";
         |FINSI;
     |FINSI;

     aNomFic = ("00000" + #dsmow121#0) % -105;

     eaOrigen   = aPathDoc + "/" + aDocumento;
     eaDestino  = eaUnidadBasico + "/MODELOS/DOCUMENTOS/DSXML/" + aDocumento;
     |HAZ EnviaFichero;

     |MENSA "      Abriendo Documento";

     eaFicheroTxt   = aNomFic + ".txt";
     eaFicheroTra   = aNomFic + ".doc";
     eaFicheroPdf   = aNomFic + ".pdf";

     aAbreTxt = eaDirTrabServi + eaFicheroTxt;
     |XABRE "WB", aAbreTxt, enHandleTxt;

     aAlfa = "[FICHERO]#" + eaDirTrabajo + aDocumento + "#" + eaDirTrabajo + aNomFic + ".doc" + &13 + &10;
     |XGRABA enHandleTxt, aAlfa;

     aAlfa = "[LIMPIA FINAL]" + &13 + &10;
     |XGRABA enHandleTxt, aAlfa;

     |MENSA "      Asignando Campos, espere un Momento.";

     |HAZ CargaVariables;

     |XCIERRA enHandleTxt;

     eaOrigen  = eaDirTrabServi + eaFicheroTxt;
     eaDestino = eaDirTrabajo   + eaFicheroTxt;
     |HAZ EnviaFichero;

     |FBORRA eaOrigen;

     |HAZ AbreWordExterno;

     |SI nOpcion = 1;  |O nOpcion = 3;
         aAbreTxt = eaDirTrabServi + eaFicheroTxt;
         |XABRE "WB", aAbreTxt, enHandleTxt;

         aAlfa = "[FICHERO]#" + eaDirTrabajo + aNomFic + ".doc" + "#" + eaDirTrabajo + aNomFic + ".doc" + &13 + &10;
         |XGRABA enHandleTxt, aAlfa;

         eaAlfa = "[IMPRESORA]#I#0" + &13 + &10;
         |XGRABA enHandleTxt, eaAlfa;

         |XCIERRA enHandleTxt;

         eaOrigen  = eaDirTrabServi + eaFicheroTxt;
         eaDestino = eaDirTrabajo   + eaFicheroTxt;
         |HAZ EnviaFichero;

         |HAZ AbreWordExterno;

         |SI #dsmoy073#36 = "S";
             aAbre = eaDirTrabajo + aNomFic + ".doc";
             |HAZ GuardaDocumento;
         |FINSI;
     |FINSI;

     |SI nOpcion = 2;  |O nOpcion = 3;
         aAbreTxt = eaDirTrabServi + eaFicheroTxt;
         |XABRE "WB", aAbreTxt, enHandleTxt;

         aAlfa = "[FICHERO]#" + eaDirTrabajo + aNomFic + ".doc" + "#" + eaDirTrabajo + aNomFic + ".doc" + &13 + &10;
         |XGRABA enHandleTxt, aAlfa;

         eaAlfa = "[IMPRESORA]#I#2#DsPdf#2" + &13 + &10;
         |XGRABA enHandleTxt, eaAlfa;

         |XCIERRA enHandleTxt;

         eaOrigen  = eaDirTrabServi + eaFicheroTxt;
         eaDestino = eaDirTrabajo   + eaFicheroTxt;
         |HAZ EnviaFichero;

         |HAZ AbreWordExterno;

         aAlfa = eaDirTrabajo + eaFicheroPdf;
         |XABRE "EC", aAlfa, 0;
         |SI FSalida < 0;
             nError = 1;
             |ACABA;
         |FINSI;

         eaOrig = eaDirTrabajo + eaFicheroPdf;

         |HAZ CargaVar;
         |HAZ Archivando;
     |FINSI;
|FPRC;

|PROCESO GrabaAux;
     |SI #dsmoy073#40 ! 202; |Y #dsmoy073#40 ! 200;
         |BUCLE dsmom002;

         |SI aPeriodo = "T";
             |SI #dsmoy073#42 = 3;    aPeriodo = "1§ Trimestre";  |FINSI;
             |SI #dsmoy073#42 = 6;    aPeriodo = "2§ Trimestre";  |FINSI;
             |SI #dsmoy073#42 = 9;    aPeriodo = "3§ Trimestre";  |FINSI;
             |SI #dsmoy073#42 = 12;   aPeriodo = "4§ Trimestre";  |FINSI;
         |SINO;
             aPeriodo = "del mes " + (("00" + #dsmoy073#42) % -102);
         |FINSI;
     |SINO;
         |SI #dsmoy073#40 = 202;
             |SI #dsmoy073#42 = 1;    aPeriodo = "1§ pago";  |FINSI;
             |SI #dsmoy073#42 = 2;    aPeriodo = "2§ pago";  |FINSI;
             |SI #dsmoy073#42 = 3;    aPeriodo = "3§ pago";  |FINSI;
         |SINO;
             aPeriodo = "pago anual";
         |FINSI;
     |FINSI;

     #dsmow121#0  = nEmpresa;
     #dsmow121#1  = #dsmoy073#44;
     #dsmow121#2  = #dsmoy073#42;
     #dsmow121#3  = #clientes@#1;
     #dsmow121#4  = aPeriodo;
     #dsmow121#5  = #dsmoy073#40;
     #dsmow121#6  = "S";
     #dsmow121#7  = nComplem;
     #dsmow121#8  = nResultado;
     #dsmow121#9  = nResultado;
     #dsmow121#10 = #clientes@#3;
     |GRABA 020#dsmow121.n;

     nSwGraba = 1;
|FPRC;

|PROCESO BuscaDevolver;
     nResultado = #dsmom004#15;

     |SI #dsmom004#3 = "303";
         |SI #dsmom004#1 [ 2013;
             |ABRE #dsmod303;
             #dsmod303#0 = #dsmom004#0;
             #dsmod303#1 = #dsmom004#1;
             #dsmod303#2 = #dsmom004#2;
             #dsmod303#3 = #dsmom004#3;
             #dsmod303#4 = #dsmom004#4;
             #dsmod303#5 = 0;
             |LEE 000#dsmod303.=;
             |SI FSalida ! 0;  |DDEFECTO #dsmod303;  |FINSI;
             |CIERRA #dsmod303;

             |SI #dsmod303#62 = 0;  nResultado = 0;  |FINSI;
         |FINSI;

         |SI #dsmom004#1 ] 2014;
             |ABRE #dsm30301;
             #dsm30301#0 = #dsmom004#0;
             #dsm30301#1 = #dsmom004#1;
             #dsm30301#2 = #dsmom004#2;
             #dsm30301#3 = #dsmom004#3;
             #dsm30301#4 = #dsmom004#4;
             #dsm30301#5 = 0;
             |LEE 000#dsm30301.=;
             |SI FSalida ! 0;  |DDEFECTO #dsm30301;  |FINSI;
             |CIERRA #dsm30301;

             |SI #dsm30301#90 = 0;  nResultado = 0;  |FINSI;
        |FINSI;
     |FINSI;

     |SI #dsmom004#3 = "310";
         |ABRE #dsmod310;
         #dsmod310#0 = #dsmom004#0;
         #dsmod310#1 = #dsmom004#1;
         #dsmod310#2 = #dsmom004#2;
         #dsmod310#3 = #dsmom004#3;
         #dsmod310#4 = #dsmom004#4;
         #dsmod310#5 = 0;
         |LEE 000#dsmod310.=;
         |SI FSalida ! 0;  |DDEFECTO #dsmod310;  |FINSI;
         |CIERRA #dsmod310;

         |SI #dsmod310#39 = 0;  nResultado = 0;  |FINSI;
     |FINSI;

     |SI #dsmom004#3 = "370";
         |ABRE #dsmod370;
         #dsmod370#0 = #dsmom004#0;
         #dsmod370#1 = #dsmom004#1;
         #dsmod370#2 = #dsmom004#2;
         #dsmod370#3 = #dsmom004#3;
         #dsmod370#4 = #dsmom004#4;
         #dsmod370#5 = 0;
         |LEE 000#dsmod370.=;
         |SI FSalida ! 0;  |DDEFECTO #dsmod370;  |FINSI;
         |CIERRA #dsmod370;

         |SI #dsmod370#85 = 0;  nResultado = 0;  |FINSI;
     |FINSI;
|FPRC;

|PROCESO dsmom004;
     nBanco   = 1;
     enCodCli = #dsmom004#0;
     |HAZ LeePersona;

     enModelo = #dsmom004#3;  enForzar = 1;  |HAZ CogeDatosBancos;

     |SI enBanco    = 0;      nBanco = 0;  |FINSI;

     |SI #dsmoy073#50 ! 0;
         nPago = eaFormaPago;
         |SI nPago ! #dsmoy073#50;  nBanco = 0;  |FINSI;
     |FINSI;

     |SI PARAMETRO ! "4";
         |SI #dsmom004#15  [ 0;  |ACABA;  |FINSI;
     |SINO;
         |HAZ BuscaDevolver;
         |SI nResultado ] 0;  |ACABA;  |FINSI;
     |FINSI;

     |SI nBanco [ 0;  |ACABA;  |FINSI;

     nResultado = #dsmom004#15;
                               || aPeriodo   = #agitab99#5;  |QBT aPeriodo;
     aPeriodo   = #dsmom004#22;   |QBT aPeriodo;
     nEmpresa   = #dsmom004#0;
     nComplem   = #dsmom004#4;

     |HAZ GrabaAux;
|FPRC;

|DEFBUCLE dsmom004;
     #dsmom004#1;
     9;
     #dsmoy073#1, #dsmoy073#44, #dsmoy073#42, #dsmoy073#40, 00, aDImpreso;
     #dsmoy073#2, #dsmoy073#44, #dsmoy073#42, #dsmoy073#40, 99, aHImpreso;
     ;
     NULL, dsmom004;
|FIN;

|PROCESO dsmod309;
     nBanco   = 1;
     enCodCli = #dsmod309#0;
     |HAZ LeePersona;

     enModelo = 309;  enForzar = 1;  |HAZ CogeDatosBancos;

     |SI enBanco    = 0;  nBanco = 0;  |FINSI;
     |SI #dsmoy073#50 ! 0;
         nPago = eaFormaPago;
         |SI nPago ! #dsmoy073#50;  nBanco = 0;  |FINSI;
     |FINSI;

     |SI #dsmod309#18 [ 0;  |ACABA;  |FINSI;
     |SI nBanco  [ 0;  |ACABA;  |FINSI;

     nResultado = #dsmod309#18;
     aPeriodo   = #agitab99#5;  |QBT aPeriodo;
     nEmpresa   = #dsmod309#0;
     nComplem   = #dsmod309#4;

     |HAZ GrabaAux;
|FPRC;

|DEFBUCLE dsmod309;
     #dsmod309#1;
     11;
     #dsmoy073#1, 01, #dsmoy073#44, #dsmoy073#42, 00, 00, aDImpreso;
     #dsmoy073#2, 98, #dsmoy073#44, #dsmoy073#42, 99, 99, aHImpreso;
     ;
     NULL, dsmod309;
|FIN;

|PROCESO isocie29;
     nResultado = 0;

     |SI #dsmoy073#42 = 1;
         |SI PARAMETRO ! "1";  |Y PARAMETRO ! "3";
             fFecha = #isocie29#9;
             |SI fFecha [ "01.01.1900";
                 |ACABA;
             |FINSI;
         |FINSI;

         nResultado = #isocie29#6 + #isocie29#24;

         |SI PARAMETRO ! "4";
             |SI nResultado < 0;  nResultado = 0;  |FINSI;
         |SINO;
             |SI nResultado > 0;  nResultado = 0;  |FINSI;
         |FINSI;
     |FINSI;

     |SI #dsmoy073#42 = 2;
         |SI PARAMETRO ! "1";  |Y PARAMETRO ! "3";
             fFecha = #isocie29#10;
             |SI fFecha [ "01.01.1900";
                 |ACABA;
             |FINSI;
         |FINSI;

         nResultado = #isocie29#7 + #isocie29#25;

         |SI PARAMETRO ! "4";
             |SI nResultado < 0;  nResultado = 0;  |FINSI;
         |SINO;
             |SI nResultado > 0;  nResultado = 0;  |FINSI;
         |FINSI;
     |FINSI;

     |SI #dsmoy073#42 = 3;
         |SI PARAMETRO ! "1";  |Y PARAMETRO ! "3";
             fFecha = #isocie29#11;
             |SI fFecha [ "01.01.1900";
                 |ACABA;
             |FINSI;
         |FINSI;

         nResultado = #isocie29#8 + #isocie29#26;

         |SI PARAMETRO ! "4";
             |SI nResultado < 0;  nResultado = 0;  |FINSI;
         |SINO;
             |SI nResultado > 0;  nResultado = 0;  |FINSI;
         |FINSI;
     |FINSI;

     |SI PARAMETRO ! "4";
         |SI nResultado [ 0;  |ACABA;  |FINSI;
     |SINO;
         |SI nResultado ] 0;  |ACABA;  |FINSI;
     |FINSI;

     nBanco   = 1;
     enCodCli = #isocie29#0;
     |HAZ LeePersona;

     enModelo = 202;  enForzar = 1;  |HAZ CogeDatosBancos;

     |SI enBanco    = 0;      nBanco = 0;  |FINSI;

     |SI #dsmoy073#50 ! 0;
         nPago = eaFormaPago;
         |SI nPago ! #dsmoy073#50;  nBanco = 0;  |FINSI;
     |FINSI;

     |SI nBanco  [ 0;  |ACABA;  |FINSI;

     aPeriodo   = #agitab99#5;  |QBT aPeriodo;
     nEmpresa   = #isocie29#0;
     nComplem   = 0;

     |HAZ GrabaAux;
|FPRC;

|DEFBUCLE isocie29;
     #isocie29#1;
     ;
     #dsmoy073#1, #dsmoy073#44;
     #dsmoy073#2, #dsmoy073#44;
     ;
     NULL, isocie29;
|FIN;

|PROCESO isom9000;
     nBanco   = 1;
     enCodCli = #isom9000#0;
     |HAZ LeePersona;

     enModelo = 202;  enForzar = 1;  |HAZ CogeDatosBancos;

     |SI enBanco     = 0;    nBanco = 0;  |FINSI;

     |SI #dsmoy073#50 ! 0;
         nPago = eaFormaPago;
         |SI nPago ! #dsmoy073#50;  nBanco = 0;  |FINSI;
     |FINSI;

     |SI nBanco  [ 0;  |ACABA;  |FINSI;

     nResultado = #isom9000#14 + #isom9000#34;

     |SI PARAMETRO ! "4";
         |SI nResultado [ 0;  |ACABA;  |FINSI;
     |SINO;
         |SI nResultado ] 0;  |ACABA;  |FINSI;
     |FINSI;

     aPeriodo   = #agitab99#5;  |QBT aPeriodo;
     nEmpresa   = #isom9000#0;
     nComplem   = 0;

     |HAZ GrabaAux;
|FPRC;

|DEFBUCLE isom9000;
     #isom9000#1;
     8;
     #dsmoy073#1, #dsmoy073#44, aPer, 00, aDImp202;
     #dsmoy073#2, #dsmoy073#44, aPer, 99, aHImp202;
     ;
     NULL, isom9000;
|FIN;

|PROCESO isacceso;
     nResultado = #isacceso#8;

     |SI PARAMETRO ! "4";
         |SI nResultado [ 0;  |ACABA;  |FINSI;
     |SINO;
         |SI nResultado ] 0;  |ACABA;  |FINSI;
     |FINSI;

     |SI PARAMETRO ! "1";  |Y PARAMETRO ! "4";
         |SI #isacceso#6 ! "S"; |ACABA; |FINSI;
     |FINSI;

     nBanco   = 1;
     enCodCli = #isacceso#0;
     |HAZ LeePersona;

     enModelo = 200;  enForzar = 1;  |HAZ CogeDatosBancos;

     |SI enBanco    = 0;      nBanco = 0;  |FINSI;
     |SI #dsmoy073#50 ! 0;
         nPago = eaFormaPago;
         |SI nPago ! #dsmoy073#50;  nBanco = 0;  |FINSI;
     |FINSI;

     |SI nBanco  [ 0;  |ACABA;  |FINSI;

     aPeriodo   = #agitab99#5;  |QBT aPeriodo;
     nEmpresa   = #isacceso#0;

     |HAZ GrabaAux;
|FPRC;

|DEFBUCLE isacceso;
     #isacceso#1;
     ;
     #dsmoy073#44, #dsmoy073#1;
     #dsmoy073#44, #dsmoy073#2;
     ;
     NULL, isacceso;
|FIN;

|PROCESO Imprimir;
     |SI #dsmoy073#0 = "S";
         |SI #dsmoy073#40 ! 309;  |Y #dsmoy073#40 ! 202; |Y #dsmoy073#40 ! 200;
             |BUCLE dsmom004;
         |FINSI;

         |SI #dsmoy073#40 = 309;
              |BUCLE dsmod309;
         |FINSI;

         |SI #dsmoy073#40 = 202;
             |SI #dsmoy073#44 [ 2009;  |BUCLE isocie29;  |FINSI;
             |SI #dsmoy073#44 ] 2010;  |BUCLE isom9000;  |FINSI;
         |FINSI;

         |SI #dsmoy073#40 = 200;
             |BUCLE isacceso;
         |FINSI;
     |SINO;
         |PARA nPara = 3; |SI nPara [ 32; |HACIENDO nPara = nPara + 1;
               |SI #dsmoy073#nPara# ! 0;
                  #dsmoy073#1 = #dsmoy073#nPara#;
                  #dsmoy073#2 = #dsmoy073#nPara#;

                  |SI #dsmoy073#40 ! 309;  |Y #dsmoy073#40 ! 202; |Y #dsmoy073#40 ! 200;
                      |BUCLE dsmom004;
                  |FINSI;

                  |SI #dsmoy073#40 = 309;
                      |BUCLE dsmod309;
                  |FINSI;

                  |SI #dsmoy073#40 = 202;
                      |SI #dsmoy073#44 [ 2009;  |BUCLE isocie29;  |FINSI;
                      |SI #dsmoy073#44 ] 2010;  |BUCLE isom9000;  |FINSI;
                  |FINSI;

                  |SI #dsmoy073#40 = 200;
                      |BUCLE isacceso;
                  |FINSI;
               |FINSI;
         |SIGUE;
     |FINSI;
|FPRC;

|PROCESO MiraCuantos;
     |SI PARAMETRO = "1";
         |SI #dsmow121#9 [ 0;
             |ACABA;
         |FINSI;
     |FINSI;

     nSwHay = nSwHay + 1;
|FPRC;

|DEFBUCLE dsmow121M;
     #dsmow121#1;
     6;
     00000, 0000, 00, 00, "S";
     99999, 9999, 99, 99, "S";
     ;
     NULL, MiraCuantos;
|FIN;

|PROCESO MiraSiImprime;
     nResultado = #dsmow121#9;  || Importe a Imprimir

     |SI PARAMETRO = "1";
         |SI #dsmoy073#52 = "N";
             |SI nResultado [ 0;  |ACABA;  |FINSI;
         |FINSI;
     |FINSI;

     nBanco   = 1;
     enCodCli = #dsmow121#0;
     |HAZ LeePersona;
     enModelo   = #dsmow121#5;  enForzar = 1;  |HAZ CogeDatosBancos;

     nResultado = #dsmow121#9;  || Importe a Imprimir

     aPeriodo   = #agitab99#5;  |QBT aPeriodo;
     aAlfa = #dsmow121#4;  |QBF aAlfa;
     |SI aAlfa ! "";
         aAlfa1 = aAlfa - "Trimestre";
         |SI FSalida ! 0;
             aPeriodo = "T";
         |SINO;
             aPeriodo = "M";
         |FINSI;
    |FINSI;
     nEmpresa   = #dsmow121#0;
     nComplem   = #dsmow121#7;

     |HAZ Imprime;
     |SI nError = 1;
         |MENAV "0000No se puede crear documentos en PDF, revise la impresora DSPDF.";
         |ERROR10;
     |FINSI;
|FPRC;

|DEFBUCLE dsmow121I;
     #dsmow121#1;
     6;
     00000, 0000, 00, 00, "S";
     99999, 9999, 99, 99, "S";
     ;
     NULL, MiraSiImprime;
|FIN;

|PROCESO ClaveBaja999;
     #dsmow121#0 = 1;
     #dsmow121#1 = 0000;
     #dsmow121#2 = 00;
     #dsmow121#7 = 00;
|FPRC;

|PROCESO ClaveAlta999;
     #dsmow121#0 = 99999;
     #dsmow121#1 = 9999;
     #dsmow121#2 = 99;
     #dsmow121#7 = 99;
|FPRC;

|PROCESO CreaTempo;
     nSwOpera = 1;
     |HAZ Imprimir;

     |LEE 000#dsmow121.p;
     |SI FSalida ! 0;
         |MENAV "0000Seleccion vacia.";
         |ACABA;
     |FINSI;

     |SI #dsmoy073#52 = "S";
         |PINPA #0#dsmow121;
         |HAZ PonBoton;
         |ENTLINEAL #1#dsmow121, -1, 4, 21, 0, ClaveBaja999, ClaveAlta999;
         |HAZ QuitaBoton;

         nSwHay = 0;
         |BUCLE dsmow121M;

         |SI nSwHay = 0;
             |CIERRAT #dsmow121;
             |DELINDEX #dsmow121;

             |ABRET #dsmow121;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO Tipo3;  |TIPO 3;
     nSwHay   = 0;
     nSwGraba = 0;
     aFichero = "dsmow121" + Usuario;
     |NOME_DAT #dsmow121#aFichero#;
     |ABRE #dsmow121;  |CIERRA #dsmow121;  |DELINDEX #dsmow121;

     |ABRE #dsam0001;
     |LEE 000#dsam0001.p;
     |SI FSalida ! 0;  |DDEFECTO #dsam0001;  |FINSI;
     |CIERRA #dsam0001;

     |SI #dsmoy073#40 = 202; |O #dsmoy073#40 = 200;
         |DBASS aPathSoc;  |QBF aPathSoc;
         aPathSoc = aPathSoc + "contasoc/fich/";
         |PATH_DAT #isocie29#aPathSoc#;
         |PATH_DAT #isacceso#aPathSoc#;
     |FINSI;

     aPer = #dsmoy073#42 + "P";

     |ABRET #dsmow121;
     |HAZ CreaTempo;

     |LEE 000#dsmow121.p;
     |SI FSalida ! 0;
         |CIERRAT #dsmow121;
         |DELINDEX #dsmow121;
         |ACABA;
     |FINSI;

     |CONFI "0000SImprimir cartas? ";
     |SI FSalida ! 0;
         |CIERRAT #dsmow121;
         |DELINDEX #dsmow121;
         |ACABA;
     |FINSI;

     nOpcion = 1;

     |SI enArchi = 1;
         |MENU aMenu;
         nOpcion = FSalida;
     |FINSI;

     nSwOpera = 0;
     |BUCLE dsmow121I;

     |CIERRAT #dsmow121;
     |DELINDEX #dsmow121;
|FPRC;

|PROCESO Periodo;
     |SI #dsmoy073#40 ! 202;
         |SI #dsmoy073#42 = 1;   #dsmoy073#43 = "ENERO     ";  |FINSI;
         |SI #dsmoy073#42 = 2;   #dsmoy073#43 = "FEBRERO   ";  |FINSI;
         |SI #dsmoy073#42 = 3;   #dsmoy073#43 = "MARZO     ";  |FINSI;
         |SI #dsmoy073#42 = 4;   #dsmoy073#43 = "ABRIL     ";  |FINSI;
         |SI #dsmoy073#42 = 5;   #dsmoy073#43 = "MAYO      ";  |FINSI;
         |SI #dsmoy073#42 = 6;   #dsmoy073#43 = "JUNIO     ";  |FINSI;
         |SI #dsmoy073#42 = 7;   #dsmoy073#43 = "JULIO     ";  |FINSI;
         |SI #dsmoy073#42 = 8;   #dsmoy073#43 = "AGOSTO    ";  |FINSI;
         |SI #dsmoy073#42 = 9;   #dsmoy073#43 = "SEPTIEMBRE";  |FINSI;
         |SI #dsmoy073#42 = 10;  #dsmoy073#43 = "OCTUBRE   ";  |FINSI;
         |SI #dsmoy073#42 = 11;  #dsmoy073#43 = "NOVIEMBRE ";  |FINSI;
         |SI #dsmoy073#42 = 12;  #dsmoy073#43 = "DICIEMBRE ";  |FINSI;
         |SI #dsmoy073#42 = 99;  #dsmoy073#43 = "ANUAL     ";  |FINSI;
     |SINO;
         |SI #dsmoy073#42 > 3;
             |MENAV "0000 Periodo erroneo";
             |ERROR;
             |ACABA;
         |FINSI;

         |SI #dsmoy073#42 = 1;   #dsmoy073#43 = "1 - PAGO  ";  |FINSI;
         |SI #dsmoy073#42 = 2;   #dsmoy073#43 = "2 - PAGO  ";  |FINSI;
         |SI #dsmoy073#42 = 3;   #dsmoy073#43 = "3 - PAGO  ";  |FINSI;
     |FINSI;

     |PINTA #dsmoy073#42;
     |PINTA #dsmoy073#43;
|FPRC;

|PROCESO elejercicio;
  |SI FSalida = 2; |ACABA; |FINSI;
  |SI #dsmoy073#40 = 200; |Y #dsmoy073#44 < 2008;
      |MENAV "    Ejercicio no activado para Modelo 200";
      |ERROR;
  |FINSI;
|FPRC;

|PROCESO LeeModelo;
     |ABRE #agitab99;
     #agitab99#0 = #dsmoy073#40;
     |LEE 040#agitab99.=;
     |SI FSalida ! 0;  |DDEFECTO #agitab99;  |FINSI;
     |CIERRA #agitab99;
|FPRC;

|PROCESO Modelo;
     |C_M #dsmoy073#Campo#, 0, aAlfa;
     |SI aAlfa = "N"; |ACABA; |FINSI;

     |HAZ LeeModelo;

     |SI #dsmoy073#40 = 180;  |O #dsmoy073#40 = 190;  |O #dsmoy073#40 = 390;  |O #dsmoy073#40 = 392;
      |O #dsmoy073#40 = 184;  |O #dsmoy073#40 = 193;  |O #dsmoy073#40 = 296;  |O #dsmoy073#40 = 187;
         |MENAV "     Modelo no valido.";
         |ERROR;
     |FINSI;

     |SI #agitab99#4 = "N";  |Y #agitab99#0 ! 202; |Y #agitab99#0 ! 200;
         |MENAV "     Modelo sin Activar.";
         |ERROR;
     |FINSI;

     #dsmoy073#42 = 0;

     fFecha = ~;
     aAlfa  = fFecha % -104;
     #dsmoy073#44  = aAlfa;

     |C_M #dsmoy073#42, 1, "S";
     |SI #agitab99#0 = 202;
         #dsmoy073#42 = 1;
         |HAZ Periodo;
         |ACABA;
     |FINSI;

     |SI #agitab99#0 = 200;
         |C_M #dsmoy073#42, 1, "N";
         #dsmoy073#42 = 99;
         #dsmoy073#44 = #dsmoy073#44 - 1;
         |HAZ Periodo;
         |ACABA;
     |FINSI;

     fFecha = ~;
     aAlfa  = fFecha % 402;   nMes  = aAlfa;
     aAlfa  = fFecha % -104;  nAnyo = aAlfa;

     |SI #agitab99#5 = "T";  |O #agitab99#5 = "C";
         |SI nMes [ 3;
             #dsmoy073#42  = 12;
             #dsmoy073#44  = nAnyo - 1;
             |HAZ Periodo;
             |PINTA #dsmoy073#42;
             |PINTA #dsmoy073#44;
             |ACABA;
         |FINSI;

         |SI nMes [ 6;
             #dsmoy073#42 = 3;
             #dsmoy073#44 = nAnyo;
             |HAZ Periodo;
             |PINTA #dsmoy073#42;
             |PINTA #dsmoy073#44;
             |ACABA;
         |FINSI;

         |SI nMes [ 9;
             #dsmoy073#42 = 6;
             #dsmoy073#44 = nAnyo;
             |HAZ Periodo;
             |PINTA #dsmoy073#42;
             |PINTA #dsmoy073#44
             |ACABA;
         |FINSI;

         |SI nMes [ 12;
             #dsmoy073#42 = 9;
             #dsmoy073#44 = nAnyo;
             |HAZ Periodo;
             |PINTA #dsmoy073#42;
             |PINTA #dsmoy073#44
             |ACABA;
         |FINSI;
     |FINSI;

     |SI #agitab99#5 = "M";
         |SI nMes = 1;
             #dsmoy073#42 = 12;
             #dsmoy073#44 = nAnyo - 1;
             |HAZ Periodo;
         |SINO;
             #dsmoy073#42 = nMes - 1;
             #dsmoy073#44 = nAnyo;
             |HAZ Periodo;
         |FINSI;
         |PINTA #dsmoy073#42;
         |PINTA #dsmoy073#44;
     |FINSI;
|FPRC;

|PROCESO formapago;
     |C_M #dsmoy073#50, 0, aAlfa;
     |SI aAlfa = "N"; |ACABA; |FINSI;

     |SI PARAMETRO = 3;
         |C_M #dsmoy073#52, 1, "S";
         |SI #dsmoy073#50 ! 2; |Y #dsmoy073#50 ! 5;
             |ERROR;
         |SINO;
             |SI #dsmoy073#50 = 5;
                 |C_M #dsmoy073#52, 1, "N";
                 #dsmoy073#52 = "S";
                 |PINTA #dsmoy073#52;
             |FINSI;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO Seleccion; |TIPO 0;
     |C_M #dsmoy073#Campo#, 0, aAlfa;
     |SI aAlfa = "N"; |ACABA; |FINSI;

     |SI #dsmoy073#0 = "S";
         |C_M #dsmoy073#01, 1, "S";
         |C_M #dsmoy073#02, 1, "S";

         |PARA nPara = 3; |SI nPara [ 32; |HACIENDO nPara = nPara + 1;
               #dsmoy073#nPara# = 0; |PINTA #dsmoy073#nPara#;
               |C_M #dsmoy073#nPara#, 1, "N";
         |SIGUE;
     |SINO;
         #dsmoy073#1 = 00001; |PINTA #dsmoy073#1;    |C_M #dsmoy073#01, 1, "N";
         #dsmoy073#2 = 99999; |PINTA #dsmoy073#2;    |C_M #dsmoy073#02, 1, "N";

         |PARA nPara = 3; |SI nPara [ 32; |HACIENDO nPara = nPara + 1;
               |C_M #dsmoy073#nPara#, 1, "S";
         |SIGUE;
     |FINSI;
|FPRC;

|PROCESO Gestion;
     |C_M #dsmoy073#37, 1, "S";
     |SI #dsmoy073#36 = "N";
         |C_M #dsmoy073#37, 1, "N";  #dsmoy073#37 = 0;
                                     #dsmoy073#38 = "";
     |FINSI;

     |PINTA #dsmoy073#37;
     |PINTA #dsmoy073#38;
|FPRC;

|PROCESO LeeUbicaciones;
     |SI FSalida = 10;
         |ABRE #dsam0121;
         #dsam0121#0 = enCodUbica;
         |LEE 000#dsam0121.=;
         |SI FSalida ! 0;  |DDEFECTO #dsam0121;  |FINSI;

         |CONSULTA_CLAVE #1#dsam0121;
         |SI FSalida = 0;
             enCodUbica = #dsam0121#0;
         |FINSI;
         |CIERRA #dsam0121;

         eaNomUbicacion = #dsam0121#1;
         |ERROR;
         |ACABA;
     |FINSI;

     enFSalida = 0;

     |HAZ CargaMac;

     |ABRE #dsam0121;
     #dsam0121#0 = enCodUbica;
     |LEE 040#dsam0121.=;
     enFSalida = FSalida;
     |SI FSalida ! 0;
         |MENAV "      No existe Ubicacion ";
         |CIERRA #dsam0121;
         |ACABA;
     |FINSI;
     |CIERRA #dsam0121;

     |ABRE #dsam0122;
     #dsam0122#0 = enCodUbica;
     #dsam0122#1 = eaDSMAC;
     |LEE 040#dsam0122.=;
     enFSalida = FSalida;
     |SI FSalida ! 0;
         aMenav =  "      No existe Ubicacion en la Maquina : " + eaDSMAC;
         |MENAV aMenav;
         |CIERRA #dsam0122;
         |ACABA;
     |FINSI;
     |CIERRA #dsam0122;

     eaRutaDocumento = #dsam0122#3;  |QBF eaRutaDocumento;
     |SI eaRutaDocumento = "";
         aMenav = "      No esta configurada la Ruta en la Maquina " + eaDSMAC;
         |MENAV aMenav;
         enFSalida = 1;
         |ACABA;
     |FINSI;
     eaNomUbicacion = #dsam0121#1;
|FPRC;

|PROCESO BuscaUbicacion;
     |SI #dsmoy073#36 = "N";  |ACABA;  |FINSI;

     enCodUbica = #dsmoy073#37;
     |HAZ LeeUbicaciones;
     |SI enFSalida ! 0;  |ERROR;  |ACABA;  |FINSI;

     #dsmoy073#37 = enCodUbica;      |PINTA #dsmoy073#37;
     #dsmoy073#38 = eaNomUbicacion;  |PINTA #dsmoy073#38;
|FPRC;

|PROGRAMA;
     |HAZ LeeUnidadBasico;

     |CLS;

     |DDEFECTO #dsmoy073;

     |SI enArchi = 1;
         #dsmoy073#36 = "N";  |C_M #dsmoy073#36, 1, "N";
         #dsmoy073#37 = 0;    |C_M #dsmoy073#37, 1, "N";
     |FINSI;

     |SI PARAMETRO = 1;
         |CABEZA "Carta preaviso de domiciliaciones y adeudos con NCR";
         #dsmoy073#39 = "Carta preaviso de domiciliaciones y adeudos con NCR";

         #dsmoy073#45 = "Se emitir  una carta de todos aquellos  ";
         #dsmoy073#46 = "clientes seleccionados que en la        ";
         #dsmoy073#47 = "configuraci¢n de bancos tengan puesto en";
         #dsmoy073#48 = "la forma de pago el pago que se pide por";
         #dsmoy073#49 = "pantalla.                               ";

         |C_M #dsmoy073#50, 1, "S";
         |C_M #dsmoy073#51, 1, "S";

         aDocumento = "word0075.doc";
         aDImpreso  = " ";
         aHImpreso  = "z";
         aDImp202   = " ";
         aHImp202   = "z";
     |FINSI;

     |SI PARAMETRO = 2;
         |CABEZA "Carta aviso de domiciliaciones sin adeudos sin NCR";
         #dsmoy073#39 = "Carta aviso de domiciliaciones sin adeudos sin NCR";

         #dsmoy073#45 = "Se emitir  una carta de todos aquellos  ";
         #dsmoy073#46 = "clientes seleccionados que en la        ";
         #dsmoy073#47 = "configuraci¢n de bancos tengan puesto en";
         #dsmoy073#48 = "la forma de pago del modelo un 3.       ";
         #dsmoy073#49 = "El modelo deber  estar impreso.         ";

         #dsmoy073#50 = 3;             |C_M #dsmoy073#50, 1, "N";
         #dsmoy073#51 = "01.01.0000";  |C_M #dsmoy073#51, 1, "N";

         aDocumento = "word0076.doc";
         aDImpreso  = "X";
         aHImpreso  = "X";
         aDImp202   = "S";
         aHImp202   = "S";
     |FINSI;

     |SI PARAMETRO = 3;
         |CABEZA "Carta de solicitud a bancos del pago de adeudos con NCR";
         #dsmoy073#39 = "Carta de solicitud a bancos del pago de adeudos con NCR";

         #dsmoy073#45 = "Se emitir  una carta de todos aquellos  ";
         #dsmoy073#46 = "clientes seleccionados que en la        ";
         #dsmoy073#47 = "configuraci¢n de bancos tengan puesto en";
         #dsmoy073#48 = "la forma de pago del modelo un 2 o 5.   ";
         #dsmoy073#49 = "El modelo deber  estar impreso.         ";

         #dsmoy073#50 = 2;             |C_M #dsmoy073#50, 1, "S";
         #dsmoy073#51 = "01.01.0000";  |C_M #dsmoy073#51, 1, "N";

         aDocumento = "word0077.doc";
         aDImpreso  = "X";
         aHImpreso  = "X";
         aDImp202   = "S";
         aHImp202   = "S";
     |FINSI;

     |SI PARAMETRO = 4;
         |CABEZA "Carta preaviso de devoluciones";
         #dsmoy073#39 = "Carta preaviso de devoluciones";

         #dsmoy073#45 = "Se emitir  una carta de todos aquellos  ";
         #dsmoy073#46 = "clientes seleccionados que en la        ";
         #dsmoy073#47 = "configuracion del modelos salga a       ";
         #dsmoy073#48 = "DEVOLVER.";

         #dsmoy073#50 = "D";
         |C_M #dsmoy073#50, 1, "N";
         |C_M #dsmoy073#51, 1, "S";

         aDocumento = "word0110.doc";
         aDImpreso  = " ";
         aHImpreso  = "z";
         aDImp202   = " ";
         aHImp202   = "z";
     |FINSI;

     |SUB_EJECUTA_NP ":basica/wordm000;CREALOS";

     |DBASS aPathDoc; |QBF aPathDoc;
     aPathDoc = aPathDoc + "basica/word/";

     aAbre = aPathDoc + aDocumento;
     |XABRE "E", aAbre, nHandle;
     |SI FSalida < 0;
         |MENAV "     No tiene instalado la plantilla de la Carta, instale ipbasica";
         |ACABA;
     |FINSI;

     |SI enEmpresa ! 0;
         #dsmoy073#1 = enEmpresa;
         #dsmoy073#2 = enEmpresa;
         #dsmoy073#33 = enEjer;
         |PARA nCampo = 0; |SI nCampo [ 33; |HACIENDO nCampo = nCampo + 1;
               |C_M #dsmoy073#nCampo#,1,"N";
         |SIGUE;

         #dsmoy073#40 = enModelo; |C_M #dsmoy073#40,1,"N";
         |HAZ LeeModelo;
         #dsmoy073#41 = #agitab99#1;
         #dsmoy073#44 = enEjer;    |C_M #dsmoy073#44,1,"N";
         #dsmoy073#42 = enPeriodo; |C_M #dsmoy073#42,1,"N";
         |HAZ Periodo;
         #dsmoy073#50 = eaFormaPago; |C_M #dsmoy073#50,1,"N";
         |HAZ formapago;
     |FINSI;

     |PINPA #0#dsmoy073;
     |PINDA #0#dsmoy073;
     |ENDATOS #1#dsmoy073;
|FPRO;

|PROCESO BorraDocus;
     aLista = eaDirTrabajo + "*.*";
     |R_PDIR aLista;
     |PARA; |SI FSalida = 0; |HACIENDO;
         aAlfa = aLista < "";

         aAlfa = aAlfa - ".doc";
         |SI FSalida ! 0;  |RFBORRA aLista;  |FINSI;

         aAlfa = aAlfa - ".pdf";
         |SI FSalida ! 0;  |RFBORRA aLista;  |FINSI;

         aAlfa = aAlfa - ".bat";
         |SI FSalida ! 0;  |RFBORRA aLista;  |FINSI;

         aAlfa = aAlfa - ".txt";
         |SI FSalida ! 0;  |RFBORRA aLista;  |FINSI;

         |R_SDIR aLista;
     |SIGUE;
|FPRC;

|PROCESO T80;  |TIPO 80;
     eaVengoDe = "dsmoy073";
     |HAZ MiraSiArchiva;

     |HAZ LeeUnidadBasico;

     aAlfa = eaUnidadBasico + "/MODELOS";                   |RMKDIR aAlfa;
     aAlfa = eaUnidadBasico + "/MODELOS/DOCUMENTOS";        |RMKDIR aAlfa;
     aAlfa = eaUnidadBasico + "/MODELOS/DOCUMENTOS/DSXML";  |RMKDIR aAlfa;

     |DBASS aBase;
     |DBASE aBaseApli;

     eaDirPlantilla = eaUnidadBasico + "\MODELOS\DOCUMENTOS\DSXML\";
     eaDirTrabajo   = eaDirPlantilla;
     eaDirTrabServi = aBaseApli + "tmp";                   |MKDIR eaDirTrabServi;
     eaDirTrabServi = aBaseApli + "tmp/" + Usuario;        |MKDIR eaDirTrabServi;
     eaDirTrabServi = aBaseApli + "tmp/" + Usuario + "/";
     eaDirSerPlan   = aBase + "basica/plantillas/";
|FPRC;

|PROCESO T90;  |TIPO 90;
     |HAZ BorraDocus;
|FPRC;
