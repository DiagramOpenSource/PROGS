|FICHEROS;
     dsmoz023 #0;

     dsmo7201 #720;
     dsmo7202 #721;
     dsmow145 #999;

     :/basica/def/agifigen #1000;
     :/basica/def/agitab99 #99;
|FIN;

|VARIABLES;
     aParam    = "";
     aAlfa     = "";
     aAlfa1    = "";
     aAlfa2    = "";

     nEmpresa  = 0;
     nNume1    = 0;
     nCampo    = 0;
     nGraba    = 0;
     nTanto    = 0;
     nComplem  = 0;
     nLinea    = 0;

     fFecha    = @;
     fDFecha   = @;
     fHFecha   = @;

     &enEmpOrigen = 0;
     &enEjeOrigen = 0;
     &enComOrigen = 0;
     &enLinOrigen = 0;
     &enTantoEmp  = 0;
     &eaNomOrigen = "";
     &enSw999     = 0;
     aFichero     = "";
     nSw          = 0;
|FIN;

|INCLUYE i_variar;

|| /////////////////////// PROCESOS ENTRADA POR PANTALLA \\\\\\\\\\\\\\\\\\\\

|PROCESO NoMasEmpresas;
     |SI #0Campo = 0;
         nNume1 = Campo + 6;   #0nNume1 = ""; |PINTA #0nNume1;
         nNume1 = Campo + 12;  #0nNume1 = 0;  |C_M #0nNume1, 1, "N"; |PINTA #0nNume1;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 6;  |HACIENDO nCampo = nCampo + 1;
               #0nCampo = 0;  |C_M #0nCampo, 1, "N"; |PINTA #0nCampo;
               nNume1 = nCampo + 6;   #0nNume1 = ""; |PINTA #0nNume1;
               nNume1 = nCampo + 12;  #0nNume1 = 0;  |C_M #0nNume1, 1, "N"; |PINTA #0nNume1;
         |SIGUE;
     |SINO;
         nNume1 = Campo + 12;  |C_M #0nNume1, 1, "S";
         |PARA nCampo = Campo + 1;  |SI nCampo [ 6;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
               nNume1 = nCampo + 12;  |C_M #0nNume1, 1, "S";
         |SIGUE;
     |FINSI;
|FPRC;

|PROCESO SacaAnyo;  |TIPO 7;
     |SI aParam = "";
         fFecha = ~;
         aAlfa  = fFecha % -104;
         #0#0   = aAlfa;
         |PINTA #0#0;
    |FINSI;
|FPRC;

|PROCESO LaEmpresa;
    |C_M #0Campo, 0, aAlfa1;
    |SI aAlfa1 = "N"; |ACABA; |FINSI;

    |SI #0Campo = 0; |ACABA; |FINSI;

    enEmpresaPINET = #0Campo;
    |HAZ RestriccionPINET;
    |SI enErrorPINET = 1;
        |MENAV "    No tiene permiso para consultar esta empresa";
        |ERROR;
        |ACABA;
    |FINSI;

    |SI Campo ! 1;
        |SI #0Campo = #0#1;
            |MENAV "    Error. La Empresa destino es igual a la Empresa Origen";
            |ERROR;
            |ACABA;
        |FINSI;

        nSw = 0;
        |SI Campo = 3;
            |SI #0#3 = #0#2; nSw = 1; |FINSI;
        |FINSI;
        |SI Campo = 4;
            |SI #0#4 = #0#2; |O #0#4 = #0#3; nSw = 1; |FINSI;
        |FINSI;
        |SI Campo = 5;
            |SI #0#5 = #0#2; |O #0#5 = #0#3; |O #0#5 = #0#4; nSw = 1; |FINSI;
        |FINSI;
        |SI Campo = 6;
            |SI #0#6 = #0#2; |O #0#6 = #0#3; |O #0#6 = #0#4; |O #0#6 = #0#5; nSw = 1; |FINSI;
        |FINSI;
        |SI nSw = 1;
            |MENAV "    Error. Empresa destino ya introducida";
            |ERROR;
            |ACABA;
        |FINSI;
    |FINSI;

    |ABRE #1000;
    #1000#0 = #0Campo;
    |LEE 041#1000=;
    |SI FSalida ! 0;
        |MENAV "     Empresa Inexistente";
        |ERROR;
    |SINO;
        nNume1 = Campo + 6;
        #0nNume1 = #1000#1; |PINTA #0nNume1;
    |FINSI;
    |CIERRA #1000;
|FPRC;

|PROCESO ElComplemento;
    |C_M #0Campo,0,aAlfa1;
    |SI aAlfa1 = "N"; |ACABA; |FINSI;

    |SI FSalida = 2; |ACABA; |FINSI;

    |ABRE #720;
    #720#0 = #0#1;
    #720#1 = #0#0;
    #720#2 = 99;
    #720#3 = 720;
    #720#4 = #0Campo;
    #720#5 = 0;
    |LEE 041#720=;
    |SI FSalida ! 0;
        |CIERRA #720;
        |MENAV "    Error. No existe declaracion";
        |ERROR;
        |ACABA;
    |FINSI;
    |CIERRA #720;

    |ABRE #721;
    #721#0 = #720#0;
    #721#1 = #720#1;
    #721#2 = #720#2;
    #721#3 = #720#3;
    #721#4 = #720#4;
    #721#5 = #720#5;
    #721#6 = 001;
    |LEE 041#721];
    |SI FSalida = 0; |Y #721#0 = #720#0; |Y #721#1 = #720#1;
        |Y #721#2 = #720#2; |Y #721#3 = #720#3;
          |Y #721#4 = #720#4; |Y #721#5 = #720#5;
        #0#13 = #721#38; |PINTA #0#13;
    |FINSI;
    |CIERRA #721;
|FPRC;

|| ----------------------------------------
|| /////////////////////// PROCESOS DE TRABAJO \\\\\\\\\\\\\\\\\\\\
|PROCESO Recalculo;
  |SI #721#39 = "S";
      #720#6 = #720#6 + #721#33;
      #720#7 = #720#7 + #721#34;

      #720#8 = #720#8 + 1;
  |FINSI;
|FPRC;

|DEFBUCLE Recalculo;
     #721#1;
     ;
     #720#0, #720#1, #720#2, #720#3, #720#4, #720#5, 001;
     #720#0, #720#1, #720#2, #720#3, #720#4, #720#5, 999;
     ;
     NULL, Recalculo;
|FIN;

|PROCESO ActualizaCab;
    |ABRE #720;
    #720#0 = nEmpresa;
    #720#1 = enEjer;
    #720#2 = enPeriodo;
    #720#3 = enModelo;
    #720#4 = nComplem;
    #720#5 = 00;
    |LEE 101#720=;
    |SI FSalida ! 0;
        |DDEFECTO #720;
        #720#0 = nEmpresa;
        #720#1 = enEjer;
        #720#2 = enPeriodo;
        #720#3 = enModelo;
        #720#4 = nComplem;
        #720#5 = 00;
        |GRABA 020#720b;
    |FINSI;
    #720#6 = 0;
    #720#7 = 0;
    #720#8 = 0;
    #720#9 = "X";
    #720#10 = ~;
    #720#11 = " ";
    #720#12 = "01.01.0000"; || No impresa
    #720#16 = " ";
    #720#17 = "01.01.0000"; || No archivada

    |BUCLE Recalculo;
    |GRABA 020#720a;
    |LIBERA #720;

    |SI aParam ! "";
        #1000#0 = #720#0;
        |LEE 040#1000=;
        |SI FSalida ! 0;  |DDEFECTO #1000;  |FINSI;

        #999#0 = #720#0;
        #999#1 = #720#1;
        |LEE 101#999=;
        |SI FSalida ! 0;
            |DDEFECTO #999;
            #999#0 = #720#0;
            #999#1 = #720#1;
            #999#2 = #1000#1;
            enSw999 = 1;     ||Grabo en el lineal uno nuevo
            |GRABA 020#999n;
        |FINSI;
        |LIBERA #999;
    |FINSI;
|FPRC;

|PROCESO CopiaDsmo7202;
||    #721#38 = #0#13;
||    |GRABA 020#721a;   No graba la modificacion

    #721#0 = nEmpresa;    ||Empresa Destino
    #721#1 = enEjer;
    #721#2 = enPeriodo;
    #721#3 = enModelo;
    #721#4 = nComplem;    ||Complemento Destino
    #721#5 = 00;
    nLinea = nLinea + 1;
    #721#6 = nLinea;
    #721#38 = nTanto;
    |GRABA 020#721n;
    |LIBERA #721;
|FPRC;

|DEFBUCLE CopiaDsmo7202;
    #721#1;
    ;
    enEmpresa, enEjer, enPeriodo, enModelo, enComplem, 00, #0#20;
    enEmpresa, enEjer, enPeriodo, enModelo, enComplem, 00, #0#21;
    ;
    NULL, CopiaDsmo7202;
|FIN;

|PROCESO dsmo7202;
    nLinea = #721#6;
    |SI #0#22 = "S";
        |BORRA 020#721a;
        nLinea = 0;
    |FINSI;
    |LIBERA #721;
|FPRC;

|DEFBUCLE LeeDsmo7202;
    #721#1;
    ;
    nEmpresa, enEjer, enPeriodo, enModelo, nComplem, 00, 001;
    nEmpresa, enEjer, enPeriodo, enModelo, nComplem, 00, 999;
    be;
    NULL, dsmo7202;
|FIN;

|PROCESO dsmo7201;
     nComplem = #720#4;
|FPRC;

|DEFBUCLE dsmo7201;
     #720#1;
     ;
     nEmpresa, enEjer, enPeriodo, enModelo, 00, 00;
     nEmpresa, enEjer, enPeriodo, enModelo, 99, 99;
     ;
     NULL, dsmo7201;
|FIN;

|| *************************************************************

|PROCESO Tipo3;  |TIPO 3;


     aFichero = ("3z" + Usuario) % 108;
     |NOME_DAT #999 aFichero;

     aAlfa1 = #0#0;
     aAlfa2 = "01.01." + aAlfa1; fDFecha = aAlfa2;
     aAlfa2 = "31.12." + aAlfa1; fHFecha = aAlfa2;

     enPeriodo = 99;
     enModelo  = #0#19;
     enEjer    = #0#0;
     enEmpresa = #0#1;
     enComplem = #0#23;

     |SI aParam ! ""; |ABRET #999; |FINSI;
     |ABRE #1000;
     |PARA nCampo = 2; |SI nCampo [ 6; |HACIENDO nCampo = nCampo + 1;
           |SI #0nCampo ! 0;
               nEmpresa = #0nCampo;
               nNume1   = nCampo + 12; nTanto   = #0nNume1;
               nLinea   = 0;
               nComplem = -1;
               |BUCLE dsmo7201;
               |SI nComplem ! -1;
                   |BUCLE LeeDsmo7202;
               |SINO;
                   nComplem = 00;
               |FINSI;
               |BUCLE CopiaDsmo7202;
               |HAZ ActualizaCab;
           |FINSI;
     |SIGUE;
     |CIERRA #1000;
     |SI aParam ! ""; |CIERRAT #999; |FINSI;
|FPRC;

|PROGRAMA;

     aParam = PARAMETRO; |QBF aParam;

     |DDEFECTO #0;
     |SI aParam = "";
         |MANTE #0;
     |SINO;
         |C_M #0#0,1,"N";  #0#0  = enEjeOrigen;
         |C_M #0#1,1,"N";  #0#1  = enEmpOrigen;
                           #0#7  = eaNomOrigen;
                           #0#13 = enTantoEmp;
                           #0#20 = enLinOrigen;
                           #0#21 = enLinOrigen;
         |C_M #0#23,1,"N"; #0#23 = enComOrigen;
         |PINPA #0#0;
         |PINDA #0#0;
         |ENDATOS #1#0;
     |FINSI;
|FPRO;
