|FICHEROS;
    dsmoy080 #0;

    dsmom001 #1;
    dsmom004 #4;

    dsm296ca #296;
    dsm296li #297;
    dsmom107 #107;

    :/basica/def/agim0024 #24;
    :/basica/def/agim0026 #26;
    :/basica/def/agim0029 #29;

    :/integral/def/dsam0003 #300;

    &regisnif@ #709;
    &clientes@ #715;

    dsmow014 #999;
|FIN;

|VARIABLES;
     &eaParam  = "";
     nSwGraba  = 0;
     nCampo    = 0;
     nDEmpresa = 0;
     nHEmpresa = 0;
     nEjer     = 0;

     informe = "";
     aAlfa1    = "";
     aAlfa2    = "";
     aAlfa3    = "";
     aFichero  = "";
     aPath     = "";
     eaMoneda = "";
     &eaTMoneda = "";
     aElDni    = "";
     aAlfa     = "";

     &periodo = 0;
     &codemp = "";
     &nomrep = "";
     &dnirep = "";
     &carrep = "";
     &nomemp = "";
     &dniemp = "";
     &domemp = "";
     &munemp = "";
     &cpoemp = "";
     &proemp = "";
     &nomtra = "";
     &dnitra = "";
     &domtra = "";
     &muntra = "";
     &cpotra = "";
     &protra = "";

     fecsys  = @;
     &fletra = "";
     &fprese = @;

     &aTClave = "";
     &aTSubCl = "";

     &eaPathIntegral = "";
     &eaCodRenta     = "";

     SwPrimer  = 0;
      enSwHay  = 0;
     nVale     = 0;
     nHayRepre = 0;
     nLectura  = 0;
     nDComple  = 0;
     nHComple  = 0;

     aBase     = "";
     nCanal    = 0;
     nPara1    = 0;

     &eaAlfa       = "";
     &enError      = 0;
     &eaTabla      = "";
     &enPosicion   = 0;
     &eaDestino    = "";
     &enComp       = 0;
     nCalc         = 0;
     nLCod         = 0;
     aCadena       = "";
     aLong         = "";
     nLong         = 0;
     aCaracter     = "";
     nPos          = 0;
|FIN;

|INCLUYE i_variar;
|| /////////////////////// PROCESOS ENTRADA POR PANTALLA \\\\\\\\\\\\\\\\\\\\

|PROCESO CodigoRenta;
|FPRC;

|PROCESO T7;  |TIPO 7;
     |SI enEmpresa ! 0;
         #0#0 = enEmpresa;  |PINTA #0#0;
         #0#1 = enEmpresa;  |PINTA #0#1;

         |C_M #0#0, 1, "N";
         |C_M #0#1, 1, "N";
     |FINSI;
     aAlfa1 = #0#16; aAlfa1 = aAlfa1 % -104;
     #0#14 = aAlfa1;
     #0#14 = #0#14 - 1;
     |PINTA #0#14;
|FPRC;

|PROCESO Parrilla;
     |SI #0#0 = 0;
         #0#0 = 0;  |PINTA #0#0;
         #0#1 = 0;  |PINTA #0#1;  |C_M #0#1, 1, "N";
         |PARA nCampo = 2;  |SI nCampo [ 13;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |SINO;
         |SI enEmpresa = 0;
             |C_M #0#0, 1, "S";
             |C_M #0#1, 1, "S";
         |FINSI;

         |PARA nCampo = 2;  |SI nCampo [ 13;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "N";
               #0nCampo = 0;  |PINTA #0nCampo;
         |SIGUE;
     |FINSI;
|FPRC;

|PROCESO NoMasEmpresas;
     |SI #0Campo = 0;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 13;  |HACIENDO nCampo = nCampo + 1;
               #0nCampo = 0;  |C_M #0nCampo, 1, "N";
         |SIGUE;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 13;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |FINSI;
|FPRC;

|PROCESO DesdeDni;
     |SI #0#19 = "            ";
         #0#20 = "zzzzzzzzzzzz";  |C_M #0#20,1,"N"; |PINTA #0#20;
         |PARA nCampo = 21;  |SI nCampo [ 28;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |SINO;
         |C_M #0#20,1,"S";
         |PARA nCampo = 21;  |SI nCampo [ 28;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "N";
               #0nCampo = ""; |PINTA #0nCampo;
         |SIGUE;
     |FINSI;
|FPRC;

|PROCESO NoMasDni;
     |SI #0Campo = "            ";
         |PARA nCampo = Campo + 1;  |SI nCampo [ 28;  |HACIENDO nCampo = nCampo + 1;
               #0nCampo = "";  |C_M #0nCampo, 1, "N";
         |SIGUE;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 28;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |FINSI;
|FPRC;


|PROCESO leecargo;
     |ABRE #29;
     #29#0 = #0Campo;
     |LEE 001#29=;
     |SI FSalida = 0;
         #0#17 = #29#1; |PINTA #0#17;
     |FINSI;
     |CIERRA #29;
|FPRC;

|| /////////////////////// //////////////////////////////////////////////////
|| /////////////////////// PROCESOS DE INFORMES \\\\\\\\\\\\\\\\\\\\\
|| \\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\

|PROCESO ElTrabajador;
 aElDni = #297#6;

 eaCodNif = #297#6;
 |HAZ LeeNifs;

 nomtra = #709#1;
 dnitra = aElDni;

 aAlfa1 = #709#9; |QBF aAlfa1; |SI aAlfa1 ! ""; aAlfa1 = aAlfa1 + " "; |FINSI;
 aAlfa2 = #709#2; |QBF aAlfa2; |SI aAlfa2 ! ""; aAlfa2 = aAlfa2 + " "; |FINSI;
 aAlfa3 = #709#3;
 domtra = aAlfa1 + aAlfa2 + aAlfa3;
 muntra = #709#6;
 protra = #709#7;

 aAlfa1 = #709#4; |QBF aAlfa1; aAlfa1 = ("00" + aAlfa1) % -102;
 aAlfa2 = #709#5; |QBF aAlfa2; aAlfa2 = ("000" + aAlfa2) % -103;
 cpotra = aAlfa1 + aAlfa2;

 eaCodRenta = "";
 |SI #0#33 = "S";
     |HAZ LeeCodRenta;
 |FINSI;
|FPRC;

|| **********************************************************************
____________________________________/ CALCULOS DE LECTURA E IMPRESION
|PROCESO TextoClaves;
     aTClave = "";
     aTSubCl = "";

     eaTabla = "ztabla09.txt";  nLCod = 1; |HAZ CargaTabla;

     enComp = nLCod;
     eaAlfa = #297#9;
     |HAZ CompruebaTabla;

     |SI enError ! 1;
         eaAlfa = eaDestino % 0380; |HAZ QuitaRaros_A;
         aTClave = eaAlfa;
     |FINSI;
     |HAZ FinTabla;

|| ...  SacaSubClave;
     eaTabla = "ztabla10.txt";  nLCod = 2; |HAZ CargaTabla;

     enComp = nLCod;
     eaAlfa = ("00" + #297#10) % -102;
     |HAZ CompruebaTabla;

     |SI enError ! 1;
         eaAlfa = eaDestino % 0480; |HAZ QuitaRaros_A;
         aTSubCl = eaAlfa;
     |FINSI;

     |HAZ FinTabla;
|FPRC;

|PROCESO Man296Li;
 |SI #0#19 = "            ";
     nVale = 0;
     |PARA nCampo = 21; |SI nCampo [ 28; |HACIENDO nCampo = nCampo + 1;
           |SI #0nCampo ! "            ";
               nVale = 1;
               |SI #0nCampo = #297#6;
                   nVale = 2;
                   |SAL;
               |FINSI;
           |FINSI;
     |SIGUE;
 |FINSI;
 |SI nVale = 1; |ACABA; |FINSI;

 |SI SwPrimer = 0;
     |HAZ ElTrabajador;
     SwPrimer = 1;
 |FINSI;

 |SI aElDni ! #297#6;
     |HAZ ElTrabajador;
 |FINSI;

 |HAZ TextoClaves;

  |PRINT;
  |PIEINF;
|FPRC;

|DEFBUCLE Man296Li;
     #297#1;
     ;
     #296#0,#296#1,#296#2,#296#3,#296#4,#296#5,#0#19,"01.01.0000",1, " ",00;
     #296#0,#296#1,#296#2,#296#3,#296#4,#296#5,#0#20,"31.12.2999",9, "z",99;
     ;
     NULL, Man296Li;
|FIN;

|| ///////////////////////////////
|PROCESO LeeCodRenta;

 eaCodRenta = "";
     |ABRE #300;
     |SELECT #2#300;
     #300#1 = aElDni;
     |LEE 040#300=;
     |SI FSalida = 0;
         eaCodRenta = "Codigo de Renta : " + (("00000" + #300#0) % -105);
     |SINO;
         |SELECT #3#300;
         #300#35 = aElDni;
         |LEE 040#300=;
         |SI FSalida = 0;
             eaCodRenta = "Codigo de Renta : " + (("00000" + #300#0) % -105);
         |FINSI;
     |FINSI;
     |CIERRA #300;
|FPRC;

||\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\
|PROCESO FechaPres;
 |ABRE #4;
 #4#0 = #296#0;
 #4#1 = #296#1;
 #4#2 = #296#2;
 #4#3 = #296#3;
 #4#4 = #296#4;
 |LEE 000#4=;
 fprese = #4#10;
 |CIERRA #4;
|FPRC;

|PROCESO BuscaCargo296;
   nomrep = #26#4;
   dnirep = #26#3;
   carrep = #26#17;
|FPRC;

|DEFBUCLE agim0026;
    #26#1;
    16;
    #296#0, 01, 01, #0#32;
    #296#0, 99, 99, #0#32;
    ;
    NULL, BuscaCargo296;
|FIN;

|PROCESO BuscaCargo296R;
     |SI #24#15 ! "A";
         nomrep = #24#4;
         dnirep = #24#3;
         carrep = "REPRESENTANTE";
         nHayRepre = 1;
         |ERROR10;
     |FINSI;
|FPRC;

|DEFBUCLE agim0024;
     #24#2;
     ;
     #296#0, 99, 01;
     #296#0, 01, 99;
     ;
     NULL, BuscaCargo296R;
|FIN;

|PROCESO BuscaComuneros;
     aAlfa = "31.12." + #296#1;
     |SI #1#8 > aAlfa;  |ACABA;  |FINSI;

     aAlfa = "01.01." + #296#1;
     |SI #1#9 > "01.01.0000";
         |SI #1#9 < aAlfa;  |ACABA;  |FINSI;
     |FINSI;

     nHayRepre = 1;

     enCodCli = #1#3;
     |HAZ LeePersona;

     |SI enSwDeDonde = 1;
         nomrep = #715#1;
         dnirep = #715#3;
         carrep = "REPRESENTANTE";
     |SINO;
         nomrep = #715#1;
         dnirep = #715#13;
         carrep = "REPRESENTANTE";
     |FINSI;

     |ERROR10;
|FPRC;

|DEFBUCLE dsmom001;
     #1#1;
     ;
     #296#0, 01, 0001;
     #296#0, 99, 9999;
     ;
     NULL, BuscaComuneros;
|FIN;
|| //////////////////////////////////////////////////

|PROCESO Man296C;
     |SI nLectura = 0;
         |SI #296#4 ! 0;
             |SI #296#11 = "S";
                 nDComple = #296#4;
                 nHComple = #296#4;
             |SINO;
                 nHComple = #296#4;
             |FINSI;
         |SINO;
             nDComple = #296#4;
             nHComple = #296#4;
         |FINSI;
         |ACABA;
     |FINSI;

     codemp = "";
     nomrep = "";
     dnirep = "";
     carrep = "";
     nomemp = "";
     dniemp = "";
     domemp = "";
     munemp = "";
     cpoemp = "";
     proemp = "";

     |SI #107#1 = "S";
         |BUCLE agim0026;
     |SINO;
         nHayRepre = 0;
         |BUCLE agim0024;
         |SI nHayRepre = 0;
             |BUCLE dsmom001;
             |SI nHayRepre = 0;
                 nomrep  = #999#3;
                 carrep  = "TITULAR";
             |FINSI;
         |FINSI;
    |FINSI;

    codemp = #296#0; |QBF codemp; codemp = ("00000" + codemp) % -105;
    enCodCli = #296#0;
    |HAZ LeePersona;

    |SI enSwDeDonde = 1;
          nomemp = #715#1;
          dniemp = #715#3;
          aAlfa1 = #715#29; |QBF aAlfa1; |SI aAlfa1 ! ""; aAlfa1 = aAlfa1 + " "; |FINSI;
          aAlfa2 = #715#30; |QBF aAlfa2; aAlfa2 = aAlfa2 + " ";
          aAlfa3 = #715#31;
          domemp = aAlfa1 + aAlfa2 + aAlfa3;
          munemp = #715#37;
          aAlfa1 = #715#35;  |QBF aAlfa1; aAlfa1 = ("00" + aAlfa1) % -102;
          aAlfa2 = #715#36; |QBF aAlfa2; aAlfa2 = ("000" + aAlfa2) % -103;
          cpoemp = aAlfa1 + aAlfa2;
          proemp = #715#38;
     |SINO;
          nomemp = #715#2;  || tique 1222_1187
          dniemp = #715#13;
          aAlfa1 = #715#3; |QBF aAlfa1; |SI aAlfa1 ! ""; aAlfa1 = aAlfa1 + " "; |FINSI;
          aAlfa2 = #715#4; |QBF aAlfa2; aAlfa2 = aAlfa2 + " ";
          aAlfa3 = #715#5;
          domemp = aAlfa1 + aAlfa2 + aAlfa3;
          munemp = #715#11;
          aAlfa1 = #715#9;  |QBF aAlfa1; aAlfa1 = ("00" + aAlfa1) % -102;
          aAlfa2 = #715#10; |QBF aAlfa2; aAlfa2 = ("000" + aAlfa2) % -103;
          cpoemp = aAlfa1 + aAlfa2;
          proemp = #715#12;
     |FINSI;

     periodo = #0#14;

     aAlfa1 = #0#16;
     fecsys = aAlfa1 % 2;    || pasa la fecha a letras
     fletra = fecsys % 11;   || coge la parte literal de la fecha
     aAlfa1 = munemp; |QBF aAlfa1;
     fletra = aAlfa1 + ", a " + fletra;  || une la localidad a la fecha literal
     |HAZ FechaPres;

     SwPrimer = 0;
     aElDni   = "";
     |BUCLE Man296Li;
|FPRC;

|DEFBUCLE Man296C;
     #296#1;
     ;
     #999#0, #999#1, #999#2, #0#30, nDComple, 00;
     #999#0, #999#1, #999#2, #0#30, nHComple, 99;
     ;
     NULL, Man296C;
|FIN;

|PROCESO Imprime;
    enEmpresa = #999#0;
    enSwHay   = enSwHay + 1;

    nDComple  = 0;
    nHComple  = 99;

    nLectura  = 0;  |BUCLE Man296C;
    nLectura  = 1;  |BUCLE Man296C;
|FPRC;

|DEFBUCLE dsmow014M;
     #999#1;
     6;
     00000, 0000, 00, "S";
     99999, 9999, 99, "S";
     ;
     NULL, Imprime;
|FIN;

|| ///////////////////////////////////////////////////
|PROCESO GrabaTempo;
    enEmpresaPINET = #4#0;
    |HAZ RestriccionPINET;
    |SI enErrorPINET = 1;  |ACABA;  |FINSI;

    enCodCli = #4#0;
    |HAZ LeePersona;

    #999#0  = #4#0;
    #999#1  = #4#1;
    #999#2  = #4#2;
    |LEE 101#999=;
    |SI FSalida ! 0;
        |DDEFECTO #999;
        #999#0  = #4#0;
        #999#1  = #4#1;
        #999#2  = #4#2;
        |GRABA 020#999b;
    |FINSI;

    #999#3  = #715#1;
    #999#4  = "ANUAL";
    #999#5  = #0#30;
    #999#6  = "S";
    #999#7  = #4#4;
    |GRABA 020#999a;
    |LIBERA #999;

    nSwGraba = 1;
|FPRC;

|DEFBUCLE dsmom004;
     #4#1;
     ;
     nDEmpresa, #0#14, #0#31, #0#30, 00;
     nHEmpresa, #0#14, #0#31, #0#30, 99;
     ;
     NULL, GrabaTempo;
|FIN;

|PROCESO IT3;  |TIPO 3;
     |SI #0#29 = "PESETAS";
         eaTMoneda = "Ptas.";
     |SINO;
         eaTMoneda = "Euros";
     |FINSI;
     eaMoneda = #0#29;

     #0#15 = 2; || Formato (2)
     informe =  "dsmoy080";

     enEjer      = #0#14;        || A¤o Calculo
     aAlfa1      = #0#14;
     aAlfa1      = aAlfa1 % -102;
     nEjer       = aAlfa1;       || Dos ultimos digitos del A¤o

     aFichero = ("cer" + Usuario) % 108;
     |NOME_DAT #999 aFichero;
     |DELINDEX #999;

     nSwGraba = 0;
     |ABRE #999;
     |SI #0#0 ! 0;
         nDEmpresa = #0#0;
         nHEmpresa = #0#1;
         |BUCLE dsmom004;
     |SINO;
         |PARA nPara1 = 2;  |SI nPara1 [ 13;  |HACIENDO nPara1 = nPara1 + 1;
               nDEmpresa = #0nPara1;
               nHEmpresa = #0nPara1;
               |BUCLE dsmom004;
         |SIGUE;
     |FINSI;
     |CIERRA #999;

     |SI nSwGraba = 0;
          |MENAV "     Seleccion Vacia.";
          |DELINDEX #999;
          |ACABA;
     |FINSI;

     Impresora = "CrystalReport";
     |IMPRE -1;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |INFOR informe;
     |SI FSalida ! 0; |FINIMP; |ACABA; |FINSI;

     |ABRE #296;
     enSwHay = 0;
     |BUCLE dsmow014M;
     |CIERRA #296;
     |FININF;
     |FINIMP;

     |DELINDEX #999;
|FPRC;

||............... PROGRAMA ......................................

|PROGRAMA;

     eaParam = PARAMETRO; |QBF eaParam;
     |SI eaParam = "";
         eaParam = "296";
     |FINSI;
     #0#30 = eaParam;
     #0#31 = 99;

     |ABRE #107;
     |LEE 040#107p;
     |SI FSalida ! 0;
         |CIERRA #107;
         |SUB_EJECUTA_NP "dsmom107";
         |ABRE #107;
     |FINSI;

     |LEE 040#107p;
     |SI FSalida ! 0;  |DDEFECTO #107;  |FINSI;
     |CIERRA #107;

     |SI enEmpresa ! 0;
         |DDEFECTO #0;
         #0#0  = enEmpresa;
         #0#1  = enEmpresa;
         #0#14 = enEjer;
         |HAZ IT3;
     |SINO;
         |CLS;
         |MANTE #0;
     |FINSI;
|FPRO;

|PROCESO QuitaRaros_A;
     aCadena = "";
     aLong   = eaAlfa % 0;
     nLong   = aLong;
     |PARA nCampo = 1;  |SI nCampo [ aLong;  |HACIENDO nCampo = nCampo + 1;
           nPos = (100 * nCampo) + 1;
           aCaracter = eaAlfa % nPos;

           |SI aCaracter = "Ñ";  aCaracter = "¥";  |FINSI;
           |SI aCaracter = "ñ";  aCaracter = "¤";  |FINSI;
           |SI aCaracter = "º";  aCaracter = "§";  |FINSI;
           |SI aCaracter = "ª";  aCaracter = "¦";  |FINSI;
           |SI aCaracter = "á";  aCaracter = " ";  |FINSI;
           |SI aCaracter = "é";  aCaracter = "‚";  |FINSI;
           |SI aCaracter = "í";  aCaracter = "¡";  |FINSI;
           |SI aCaracter = "ó";  aCaracter = "¢";  |FINSI;
           |SI aCaracter = "ú";  aCaracter = "£";  |FINSI;

           aCadena = aCadena + aCaracter;
     |SIGUE;
     eaAlfa = aCadena;
|FPRC;

|PROCESO Tipo88;  |TIPO 88;
     |HAZ Relaciones;
|FPRC;
