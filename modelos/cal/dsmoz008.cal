|FICHEROS;
     dsmoz008 #0;
     dsmom001 #1;
     dsmom002 #2;
     dsmom003 #3;
     dsmom004 #4;
     dsmom023 #23;
     dsmom024 #24;

     :/basica/def/agitab99 #99;
     :/basica/def/agifigen #118;
     :/basica/def/agicen14 #114;
     :/basica/def/zmonedas #120;

     :/contasoc/def/maempr01 #900;
     :/contasoc/def/isocie59 #901;
     :/contasoc/def/isocie29 #902;
     :/contasoc/def/isocie91 #903;
     :/contasoc/def/isacceso #904;
     :/contasoc/def/isom9000 #905;
     reacces@ #910;
     redatos@ #911;
|FIN;

|VARIABLES;
     nDEmpresa = 0;
     nHEmpresa = 0;
     nNume     = 0;
     aAlfa     = "";
     aAlfa1    = "";
     aAlfa2    = "";
     aAlfa3    = "";
     aAlfa4    = "";
     fHFecha   = @;
     fFecha    = @;
     nCampo    = 0;
     aPathModelo = "";
     aDef        = "";

     aDescrip  = "";
     nCanti    = 0;
     aTPeriodo = "";

     nT = 0;
     aDirInicio        = "";
     aPathFichSociedad = "";
     nNume1 = 0;
     nNume2 = 0;
     nNume3 = 0;
     nNume4 = 0;

     adTIPORENTA = "";
     ahTIPORENTA = "";
     nSwPase     = 0;
     fLaFecha    = @;

     &enAnyPase  = 0;
     &enTodo     = 0;

     &enDEmpresa = 0;
     &enHEmpresa = 0;
     &enDEjer    = 0;
     &enHEjer    = 0;
     &enDModelo  = 0;
     &enHModelo  = 0;

     nSwBueno   = 0;
     nCodigoNue = 0;
     nCodigo    = 0;
     aNom       = "";
     aNif       = "";
     nDModelo   = 0;
     nHModelo   = 0;
|FIN;

|INCLUYE i_variar;
|INCLUYE i_varren;
|INCLUYE i_modhis;

|| /////////////////////// PROCESOS ENTRADA POR PANTALLA \\\\\\\\\\\\\\\\\\\\
|CALCULO Soci7; |TIPO 7;
 |C_M #0#16,1,"N";
 |SI #0#15 = 4; |O #0#15 = 2;
     |C_M #0#16,1,"S";
 |FINSI;
|FCAL;

|CALCULO Eany; |TIPO 7;
 fLaFecha = ~;
 aAlfa1 = fLaFecha; aAlfa1 = aAlfa1 % -104;
 #0#14 = aAlfa1;
 |PINTA #0Campo;
|FCAL;

|PROCESO Tipo7;  |TIPO 7;
     |SI enEmpresa ! 0;
         #0#0 = enEmpresa;  |PINTA #0#0;
         #0#1 = enEmpresa;  |PINTA #0#1;

         |C_M #0#0, 1, "N";
         |C_M #0#1, 1, "N";
     |FINSI;
|FPRC;

|PROCESO Parrilla;
     |SI #0#0 = 0;
         #0#0 = 0;  |PINTA #0#0;
         #0#1 = 0;  |PINTA #0#1;  |C_M #0#1, 1, "N";
         |PARA nCampo = 2;  |SI nCampo [ 13;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |SINO;
         |SI enEmpresa = 0;
             |C_M #0#0, 1, "S";
             |C_M #0#1, 1, "S";
         |FINSI;

         |PARA nCampo = 2;  |SI nCampo [ 13;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "N";
               #0nCampo = 0;  |PINTA #0nCampo;
         |SIGUE;
     |FINSI;
|FPRC;

|PROCESO NoMasEmpresas;
     |SI #0Campo = 0;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 13;  |HACIENDO nCampo = nCampo + 1;
               #0nCampo = 0;  |C_M #0nCampo, 1, "N";
         |SIGUE;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 13;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |FINSI;
|FPRC;

|| /////////////////////// PROCESOS CALCULO DEL Resumen \\\\\\\\\\\\\\\\\\\\\
|PROCESO Borra024;

   |SI nT ! 4;
       |SI nT ! 3;   ||Renta
           |SI #24#3 ] 100; |Y #24#3 [ 105; |ACABA; |FINSI;
           |SI #24#3 = 714;                 |ACABA; |FINSI;
       |FINSI;
       |SI nT ! 2;   ||Sociedades
          |SI #24#3 ] 200;  |Y #24#3 [ 299; |ACABA; |FINSI;
       |FINSI;
       |SI nT ! 1;   ||Modelos
           |SI #24#3 ] 110; |Y #24#3 [ 199; |ACABA; |FINSI;
           |SI #24#3 ] 300; |Y #24#3 [ 399; |ACABA; |FINSI;
       |FINSI;
   |FINSI;

   #23#0 = #24#0;
   #23#1 = #24#1;
   #23#2 = #24#3;
   |LEE 001#23=;
   |SI FSalida = 0;
       |BORRA 020#23a;
       |LIBERA #23;
   |FINSI;
   |BORRA 020#24a;
   |LIBERA #24;
|FPRC;

|DEFBUCLE dsmom024;
     #24#1;
     ;
     nDEmpresa, #0#14, 00, 000, 00;
     nHEmpresa, #0#14, 99, 899, 99;
     be;
     NULL, Borra024;
|FIN;
|| //////////////////////////////////////////////////////////////////////
|| //////////////////////////////////////////////////////////////////////
|| ---------------- Cambio de Codigos
|PROCESO CogeCodigo;
 |ABRE #118;
 #118#0 = nCodigo;
 |LEE 041#118=;
 |SI FSalida = 0;
     aAlfa1 = #118#13; |QBF aAlfa1;
     |SI aAlfa1 = aNif; |Y aNif ! "";
         nCodigoNue = #118#0;
     |SINO;
         aAlfa1 = #118#2; |QBT aAlfa1;
         |SI aAlfa1 = aNom; |Y aNom ! "";
             nCodigoNue = #118#0;
         |FINSI;
     |FINSI;
 |FINSI;
 |CIERRA #118;
 |SI nCodigoNue = 0;
     |SI aNif ! "";
         |ABRE #118;
         |SELECT #4#118;
         #118#13 = aNif;
         |LEE 041#118=;
         |SI FSalida = 0;
             nCodigoNue = #118#0;
         |FINSI;
         |SELECT #1#118;
         |CIERRA #118;
     |FINSI;
 |FINSI;
 |SI nCodigoNue ! 0;
     nSwBueno = 0;
     |SI #0#0 ! 0;
         |SI nCodigoNue ] #0#0; |Y nCodigoNue [ #0#1;
             nSwBueno = 1;
         |FINSI;
     |SINO;
         |PARA nCampo = 2;  |SI nCampo [ 13;  |HACIENDO nCampo = nCampo + 1;
               |SI nCodigoNue =  #0nCampo;
                   nSwBueno = 1;
                   |SAL;
               |FINSI;
         |SIGUE;
     |FINSI;
     |SI nSwBueno = 0; nCodigoNue = 0; |FINSI;
 |FINSI;

|FPRC;

|| ********************************************
|PROCESO CreaResumen;

     enModelo = #4#3;
     |HAZ SelModelo;
     |SI nSwModHis = 0; |ACABA; |FINSI;


     #3#0 = #4#0;
     #3#1 = #4#1;
     #3#2 = #4#2;
     |LEE 001#3=;
     |SI FSalida ! 0;    |DDEFECTO #3;  |FINSI;

     #23#0 = #4#0;
     #23#1 = #4#1;
     #23#2 = #4#3;
     |LEE 101#23=;
     |SI FSalida ! 0;
         |DDEFECTO #23;
         #23#0 = #4#0;
         #23#1 = #4#1;
         #23#2 = #4#3;
         |HAZ LeoModelo;
         #23#3 = #99#1;
         |GRABA 020#23b;
     |FINSI;


     #24#0 = #4#0;
     #24#1 = #4#1;
     #24#2 = #4#2;
     #24#3 = #4#3;
     #24#4 = #4#4;
     |LEE 141#24=;
     |SI FSalida ! 0;
         |DDEFECTO #24;
         #24#0  = #4#0;
         #24#1  = #4#1;
         #24#2  = #4#2;
         #24#3  = #4#3;
         #24#4  = #4#4;
         #24#12 = #3#3;
         |GRABA 020#24b;
     |SINO;
         |ACABA;
     |FINSI;

     #24#6 = #4#7;   ||Entrada
     #24#7 = #4#8;   ||Fecha Entrada
     #24#8 = #4#15;  ||Resultado
     |SI #24#8 ] 0;
         #24#9 = #24#8;
         #24#10 = 0;
         #24#11 = 0;
     |SINO;
         #24#9  = 0;
         aAlfa = #4#16; |QBF aAlfa;
         |SI aAlfa ! "";
             #24#11 = - #24#8;
             #24#10 = 0;
         |SINO;
             #24#10 = - #24#8;
             #24#11 = 0;
         |FINSI;
     |FINSI;
     #24#13 = #24#8;
     aAlfa = #24#3; aAlfa = aAlfa % 101;
     |SI aAlfa = 3;
         enEmpresa = #24#0;
         enEjer    = #24#1;
         enPeriodo = #24#2;
         enComplem = #24#4;
         |HAZ eLeoImpReal;
         #24#9  = enIngresar;
         #24#10 = enDevolver;
         #24#11 = enCompensar;
         #24#13 = enImpReal;
         #24#14 = enCas21;
         #24#15 = enCas27;
         #24#16 = enCas31;
         #24#17 = enCas34;
     |FINSI;

     |GRABA 020#24a;
     |LIBERA #24;

     |LIBERA #23;

     |HAZ PintaAlgo;
|FPRC;

|DEFBUCLE dsmom004;
     #4#1;
     9;
     #2#0, #0#14, 01, nDModelo, 00, "X";
     #2#0, #0#14, 99, nHModelo, 99, "X";
     ;
     NULL, CreaResumen;
|FIN;

|PROCESO Calculo;

     #114#0 = #2#0;
     #114#1 = #2#1;
     |LEE 040#114=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     aAlfa   = "31.12." + #0#14;
     fHFecha = aAlfa;

     aAlfa   = #2#8 % -104;
     |SI aAlfa ! "0000";
         nNume   = aAlfa;
         |SI nNume > #0#14;  |ACABA;  |FINSI;
     |FINSI;

     aAlfa   = #2#9 % -104;
     |SI aAlfa ! "0000";
         nNume   = aAlfa;
         |SI nNume < #0#14;  |ACABA;  |FINSI;

         |SI #2#9 < fHFecha;  fHFecha = #2#9;  |FINSI;
     |FINSI;

     nDModelo = #2#2;
     nHModelo = #2#2;
     |SI #0#14 [ 2008; |Y  #2#2 = 303;
         nDModelo = 300; nHModelo = 332;
     |FINSI;

     |SI #0#14 ] 2014;
         |SI  #2#2 = 310; |O #2#2 = 370;
              nDModelo = 303; nHModelo = 303;
         |FINSI;
     |FINSI;

     |ABRE #3;
     |ABRE #23;
     |ABRE #24;
     |BUCLE dsmom004;
     |CIERRA #23;
     |CIERRA #24;
     |CIERRA #3;
|FPRC;

|DEFBUCLE dsmom002;
     #2#1;
     ;
     nDEmpresa, 00, 000;
     nHEmpresa, 99, 899;
     ;
     NULL, Calculo;
|FIN;

||////////////////////////////////////////
|PROCESO PaseModelos;

     |ATRI P;
     |PINTA 2203, "REALIZANDO PASE DESDE MODELOS ...... ";
     |ATRI 0;

     |SI #0#0 ! 0;
         nDEmpresa = #0#0;
         nHEmpresa = #0#1;
         |BUCLE dsmom002;    || Calculo del Resumen
     |SINO;
         |PARA nCampo = 2;  |SI nCampo [ 13;  |HACIENDO nCampo = nCampo + 1;
               nDEmpresa = #0nCampo;
               nHEmpresa = #0nCampo;
               |BUCLE dsmom002;    || Calculo del Resumen
         |SIGUE;
     |FINSI;
|FPRC;
||//////////////////////////////////////////////////////////////////////
||//////////////////////////////////////////////////////////////////////

|PROCESO PasaModelo202S;
    enComplem = 00;

    enPeriodo = "03";
    |HAZ calculoFecha;

    enResultado = ( (#902#6 + #902#24) / enConversor) ! EURODBMOD;
    enImpReal   = enResultado;
    enIngresar  = enResultado;
    enDevolver  = 0;
    enCompensar = 0;
    |HAZ GrabaResumen;

    enPeriodo = "09";
    |HAZ calculoFecha;

    enResultado = ( (#902#7 + #902#25) / enConversor) ! EURODBMOD;
    enImpReal   = enResultado;
    enIngresar  = enResultado;
    enDevolver  = 0;
    enCompensar = 0;
    |HAZ GrabaResumen;

    enPeriodo = "12";
    |HAZ calculoFecha;

    enResultado = ( (#902#8 + #902#26) / enConversor) ! EURODBMOD;
    enImpReal   = enResultado;
    enIngresar  = enResultado;
    enDevolver  = 0;
    enCompensar = 0;
    |HAZ GrabaResumen;
|FPRC;

|DEFBUCLE isocie29S;
     #902#1;
     ;
     nCodigo, enEjer;
     nCodigo, enEjer;
     ;
     NULL, PasaModelo202S;
|FIN;

|PROCESO isom9000;
    enComplem = #905#3;

    |SI #905#2 = "1P";   enPeriodo = "03";  |FINSI;
    |SI #905#2 = "2P";   enPeriodo = "09";  |FINSI;
    |SI #905#2 = "3P";   enPeriodo = "12";  |FINSI;

    |HAZ calculoFecha;

    enResultado = ( (#905#14 + #905#34) / enConversor) ! EURODBMOD;
    enImpReal   = enResultado;
    enIngresar  = enResultado;
    enDevolver  = 0;
    enCompensar = 0;
    |HAZ GrabaResumen;
|FPRC;

|DEFBUCLE isom9000;
     #905#1;
     8;
     nCodigo, enEjer, "  ", 00, "S";
     nCodigo, enEjer, "zz", 99, "S";
     ;
     NULL, isom9000;
|FIN;

|PROCESO PasaModelo225S;
    enComplem = 00;
    enPeriodo = "99";
    |HAZ calculoFecha;

    enResultado = 0;
    enImpReal   = 0;
    enIngresar  = 0;
    enDevolver  = 0;
    enCompensar = 0;
    |SI #903#123 > 0;
        enResultado = (#903#123 / enConversor) ! EURODBMOD;
        enIngresar  = enResultado;
        enDevolver  = 0;
    |SINO;
        |SI #903#123 < 0;
            enResultado = (#903#123 / enConversor) ! EURODBMOD;
            enDevolver  = - enResultado;
            enIngresar  = 0;
        |FINSI;
    |FINSI;
    enImpReal = enResultado;
    |HAZ GrabaResumen;
|FPRC;

|DEFBUCLE isocie91S;
     #903#1;
     ;
     nCodigo, enEjer;
     nCodigo, enEjer;
     ;
     NULL, PasaModelo225S;
|FIN;

|PROCESO PasaModelo200S;
    enComplem = 00;
    enPeriodo = "99";
    |HAZ calculoFecha;

    enResultado = 0;
    enImpReal   = 0;
    enIngresar  = 0;
    enDevolver  = 0;
    enCompensar = 0;
    |SI #901#105 > 0;
        enResultado = (#901#105 / enConversor) ! EURODBMOD;
        enIngresar  = enResultado;
        enDevolver  = 0;
    |SINO;
        |SI #901#105 < 0;
            enResultado = (#901#105 / enConversor) ! EURODBMOD;
            enDevolver  = - enResultado;
            enIngresar  = 0;
        |FINSI;
    |FINSI;
    enImpReal = enResultado;
    |HAZ GrabaResumen;
|FPRC;

|DEFBUCLE isocie59S;
     #901#1;
     ;
     nCodigo, enEjer;
     nCodigo, enEjer;
     ;
     NULL, PasaModelo200S;
|FIN;

|PROCESO pasoImp2008;
    enModelo  = 200;
    enComplem = 00;
    enPeriodo = "99";
    |HAZ calculoFecha;

    enResultado = 0;
    enImpReal   = 0;
    enIngresar  = 0;
    enDevolver  = 0;
    enCompensar = 0;
    |SI #904#8 > 0;
        enResultado = (#904#8 / enConversor) ! EURODBMOD;
        enIngresar  = enResultado;
        enDevolver  = 0;
    |SINO;
        |SI #904#8 < 0;
            enResultado = (#904#8 / enConversor) ! EURODBMOD;
            enDevolver  = - enResultado;
            enIngresar  = 0;
        |FINSI;
    |FINSI;
    enImpReal = enResultado;
    |HAZ GrabaResumen;
|FPRC;

|PROCESO ModelosSociedad;
     nCodigoNue = 0;
     nCodigo = #900#0;
     aNom    = #900#2; |QBT aNom;
     aNif    = #900#3; |QBF aNif;
     |HAZ CogeCodigo;
     |SI nCodigoNue = 0;  |ACABA;  |FINSI;

     |SI #0#16 ! 2;
         enEmpresa = nCodigoNue;
         enEjer    = #900#1;
         enModelo  = 200;
         |SI #900#18 = "A"; enModelo = 201; |FINSI;

         |ABRE #904;
         |DDEFECTO #904;
         #904#0 = #900#0;
         #904#1 = #900#1;
         |LEE 001#904=;
         |SI FSalida = 0;
             enModelo = #904#5;
         |FINSI;
         |CIERRA #904;

         |SI #904#1 < 2008;
             |SI enModelo = 225;
                 |BUCLE isocie91S;
             |SINO;
                 |BUCLE isocie59S;
             |FINSI;
         |SINO;
             |HAZ pasoImp2008;
         |FINSI;
     |FINSI;

     |SI #0#16 ! 1;
         enEmpresa = nCodigoNue;
         enEjer    = #900#1;
         enModelo  = 202;

         |SI #900#1 [ 2009;
             |BUCLE isocie29S;
         |FINSI;

         |SI #900#1 ] 2010;
             |BUCLE isom9000;
         |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE maacceso;
     #900#1;
     ;
     nDEmpresa, #0#14;
     nHEmpresa, #0#14;
     ;
     NULL, ModelosSociedad;
|FIN;

|PROCESO PaseSocie;

   |DBASS aDirInicio; |QBF aDirInicio;
   aPathFichSociedad = aDirInicio + "contasoc/fich/";

 |SI enTodo [ 4;
   |ATRI P;
   |PINTA 2203, "REALIZANDO PASE DESDE SOCIEDADES ... ";
   |ATRI 0;
 |FINSI;

   |ABRE #zmonedas;
   |LEE 040#zmonedas.p;
   |SI FSalida ! 0;  |DDEFECTO #zmonedas;  |FINSI;
   |CIERRA #zmonedas;
   eaMonedaOtra = #zmonedas#7;
   enLeerCamoneda = 1;
   |HAZ EnConversor;


   |PATH_DAT #900 aPathFichSociedad;
   |PATH_DAT #901 aPathFichSociedad;
   |PATH_DAT #902 aPathFichSociedad;
   |PATH_DAT #903 aPathFichSociedad;
   |ABRE #23;
   |ABRE #24;

   nDEmpresa = 1;
   nHEmpresa = 99999;
   |BUCLE maacceso;

   |CIERRA #24;
   |CIERRA #23;
|FPRC;
||//////////////////////////////////////////////////////////////////////
||//////////////////////////////////////////////////////////////////////
||............................... Desde 1999
|PROCESO PasaModeloRenta98;

    enComplem = 00;
    enPeriodo = "99";
    |HAZ calculoFecha;

    enResultado = (#911enRImporte / enConversor) ! EURODBMOD;
    enImpReal   = 0;
    enIngresar  = 0;
    enDevolver  = 0;
    enCompensar = 0;
    |SI enResultado > 0;
        enIngresar = enResultado;
        enDevolver = 0;
    |SINO;
        enDevolver = - enResultado;
        enIngresar = 0;
    |FINSI;
    enImpReal = enResultado;
    |HAZ GrabaResumen;
|FPRC;

|PROCESO RentaHasta1998;

    |ABRE #911;
    #911#0 = nCodigo;
    |LEE 001#911=;
    |SI FSalida = 0;

        || .................................... Renta
        nNume1 = enRNoBlanco;
        |SI #911enRImpreso ! "C";  nNume1 = nNume1 + 1;  |FINSI;
        aAlfa1 = #911nNume1; |QBF aAlfa1;
        enModelo = 0;
        |SI aAlfa1 = "A"; enModelo = 103; |FINSI;
        |SI aAlfa1 = "S"; enModelo = 101; |FINSI;
        |SI aAlfa1 = "O"; enModelo = 100; |FINSI;
        |SI enModelo ! 0;
            nNume2 = nNume1 + 21;
            |SI #911nNume2 = "N";
                enRImporte = nNume1 + 7;
                |HAZ PasaModeloRenta98;
            |FINSI;
        |FINSI;

        || .................................... Patrimonio
        |SI #911#29 = "S";
            enModelo = 714;
            enRImporte = 35;
            |HAZ PasaModeloRenta98;
        |FINSI;
    |FINSI;
    |CIERRA #911;
|FPRC;
|| ......................................................................

||............................... Desde 1999
|PROCESO PasaModeloRenta;

    aAlfa1 = #911enRNoBlanco;
    |QBF aAlfa1;
    |SI aAlfa1 = ""; |ACABA; |FINSI;           ||No modelo

|| ...     |SI #911enRImpreso = "N"; |ACABA; |FINSI;  || No impreso no pasa nada

    nSwPase   = 1;
    enModelo  = #911enRNoBlanco;
    enComplem = #911#1;
    enPeriodo = "99";
    |HAZ calculoFecha;

    enResultado = (#911enRImporte / enConversor) ! EURODBMOD;
    enImpReal   = 0;
    enIngresar  = 0;
    enDevolver  = 0;
    enCompensar = 0;
    |SI enResultado > 0;
        enIngresar = enResultado;
        enDevolver = 0;
    |SINO;
        enDevolver = - enResultado;
        enIngresar = 0;
    |FINSI;
    enImpReal = enResultado;
    |HAZ GrabaResumen;
|FPRC;

|DEFBUCLE datorenta;
     #911#1003;
     ;
     nCodigo, 00, adTIPORENTA;
     nCodigo, 99, ahTIPORENTA;
     ;
     NULL, PasaModeloRenta;
|FIN;

|PROCESO RentaDesde1999;

    nSwPase = 0;
    adTIPORENTA = "CONJUNTA  "; ahTIPORENTA = "CONJUNTA  ";
    |BUCLE datorenta;
    |SI nSwPase = 0;
        adTIPORENTA = "TITULAR   "; ahTIPORENTA = "TITULAR   ";
        |BUCLE datorenta;
    |FINSI;

    adTIPORENTA = "ZPATRITIT "; ahTIPORENTA = "ZPATRITIT ";
    |BUCLE datorenta;

|FPRC;
|| ......................................................................
|PROCESO ModelosRenta;
    nCodigoNue = 0;
    nCodigo = #910#0;
    aNom    = #910#1; |QBT aNom;
    aNif    = #910#2; |QBF aNif;
    |HAZ CogeCodigo;
    |SI nCodigoNue = 0;  |ACABA;  |FINSI;

    enEmpresa = nCodigoNue;
    enEjer    = enAnyRen;

    |SI enAnyRen ] 1999;
        |HAZ RentaDesde1999;
    |SINO;
        |HAZ RentaHasta1998;
    |FINSI;
|FPRC;

|DEFBUCLE manrenta;
     #910#1001;
     ;
     nDEmpresa;
     nHEmpresa;
     ;
     NULL, ModelosRenta;
|FIN;

|PROCESO PaseRenta;

   |ATRI P;
   |PINTA 2203, "REALIZANDO PASE DESDE RENTA Y PATRIMONIO ... ";
   |ATRI 0;

   enAnyRen = #0#14;
   |HAZ DRentaFich;
   enLeerCamoneda = 1;
   |HAZ EnConversor;

   aDef = eaPathDefRenta + "reacceso.mas";
   |CARGA_DEF aDef, reacces@;
   |SI FSalida ! 0;
       aAlfa = "     Error en cargar el Def reacceso.mas";
       || ... |MENAV aAlfa;
       |ACABA;
   |FINSI;

   aDef = eaPathDefRenta + eaFicRen + ".mas";
   |CARGA_DEF aDef, redatos@;
   |SI FSalida ! 0;
       aAlfa = "     Error en cargar el Def " + eaFicRen;
       |MENAV aAlfa;
       |DESCARGA_DEF #reacces@;
       |ACABA;
   |FINSI;

   |PATH_DAT #910 eaPathFichRenta;
   |PATH_DAT #911 eaPathFichRenta;

   |ABRE #23;
   |ABRE #24;

   nDEmpresa = 1;
   nHEmpresa = 99999;
   |BUCLE manrenta;

   |CIERRA #24;
   |CIERRA #23;

   |DESCARGA_DEF #redatos@;
   |DESCARGA_DEF #reacces@;
|FPRC;

|| -------------------------------------------------------

||//////////////////////////////////////////////////////////////////////
||//////////////////////////////////////////////////////////////////////

|CALCULO Tipo3;  |TIPO 3;
     nT = #0#15;

     |SI #0#0 ! 0;
         nDEmpresa = #0#0;
         nHEmpresa = #0#1;
         |ABRE #23;
         |BUCLE dsmom024;    || Borrado del Resumen
         |CIERRA #23;
     |SINO;
         |PARA nCampo = 2;  |SI nCampo [ 13;  |HACIENDO nCampo = nCampo + 1;
               nDEmpresa = #0nCampo;
               nHEmpresa = #0nCampo;
               |ABRE #23;
               |BUCLE dsmom024;    || Borrado del Resumen
               |CIERRA #23;
         |SIGUE;
     |FINSI;

     |ABRE #114;
     |SI nT = 1; |O nT = 4;
         |HAZ PaseModelos;
     |FINSI;
     |SI nT = 2; |O nT = 4;
         |HAZ PaseSocie;
     |FINSI;
     |SI nT = 3; |O nT = 4;
         |HAZ PaseRenta;
     |FINSI;
     |CIERRA #114;

     |SI #0#0 ! 0;
         enDEmpresa = #0#0;   enHEmpresa = #0#1;
         enDEjer    = #0#14;  enHEjer    = #0#14;
         enDModelo  = 000;    enHModelo  = 999;
         |HAZ ReCalculaHis;
     |SINO;
         |PARA nCampo = 2;  |SI nCampo [ 13;  |HACIENDO nCampo = nCampo + 1;
               enDEmpresa = #0nCampo; enHEmpresa = #0nCampo;
               enDEjer    = #0#14;    enHEjer    = #0#14;
               enDModelo  = 000;      enHModelo  = 999;
               |HAZ ReCalculaHis;
         |SIGUE;
     |FINSI;
|FCAL;

|PROCESO calculoFecha;
       aAlfa1 = "31";
       |SI enPeriodo = 4; |O enPeriodo = 6; |O enPeriodo = 9; |O enPeriodo = 11;
           aAlfa1 = "30";
       |FINSI;
       |SI enPeriodo = 2;
           aAlfa1 = "28";
           nNume4 = (enEjer $ 4);
           |SI nNume4 = 0; aAlfa1 = "29"; |FINSI;
       |FINSI;
       |SI enPeriodo ! 99;
           aAlfa2 = enPeriodo; |QBF aAlfa2;
           aAlfa2 = ("00" + aAlfa2) % -102;
       |SINO;
           aAlfa2 = "12";
       |FINSI;
       aAlfa3 = enEjer;

       aAlfa4 = aAlfa1 + "." + aAlfa2 + "." + aAlfa3;
       fFecha = aAlfa4;      ||Fecha Impresion
|FPRC;

|PROCESO descripPer;
 |SI enPeriodo = 1;   aDescrip = "ENERO     ";  |FINSI;
 |SI enPeriodo = 2;   aDescrip = "FEBRERO   ";  |FINSI;
 |SI enPeriodo = 3;   aDescrip = "MARZO     ";  |FINSI;
 |SI enPeriodo = 4;   aDescrip = "ABRIL     ";  |FINSI;
 |SI enPeriodo = 5;   aDescrip = "MAYO      ";  |FINSI;
 |SI enPeriodo = 6;   aDescrip = "JUNIO     ";  |FINSI;
 |SI enPeriodo = 7;   aDescrip = "JULIO     ";  |FINSI;
 |SI enPeriodo = 8;   aDescrip = "AGOSTO    ";  |FINSI;
 |SI enPeriodo = 9;   aDescrip = "SEPTIEMBRE";  |FINSI;
 |SI enPeriodo = 10;  aDescrip = "OCTUBRE   ";  |FINSI;
 |SI enPeriodo = 11;  aDescrip = "NOVIEMBRE ";  |FINSI;
 |SI enPeriodo = 12;  aDescrip = "DICIEMBRE ";  |FINSI;
 |SI enPeriodo = 99;  aDescrip = "ANUAL     ";  |FINSI;
|FPRC;

|PROCESO GrabaResumen;
     #23#0 = enEmpresa;
     #23#1 = enEjer;
     #23#2 = enModelo;
     |LEE 101#23=;
     |SI FSalida ! 0;
         |DDEFECTO #23;
         #23#0 = enEmpresa;
         #23#1 = enEjer;
         #23#2 = enModelo;
         |HAZ LeoModelo;
         #23#3 = #99#1;
         |GRABA 020#23b;
     |FINSI;

     aAlfa4 = #23#3; |QBF aAlfa4;
     |SI aAlfa4 = "";
         |HAZ LeoModelo;
         #23#3 = #99#1;
     |FINSI;

     #24#0 = enEmpresa;
     #24#1 = enEjer;
     #24#2 = enPeriodo;
     #24#3 = enModelo;
     #24#4 = enComplem;
     |LEE 141#24=;
     |SI FSalida ! 0;
         |DDEFECTO #24;
         #24#0  = enEmpresa;
         #24#1  = enEjer;
         #24#2  = enPeriodo;
         #24#3  = enModelo;
         #24#4  = enComplem;
         |HAZ descripPer;
         #24#12 = aDescrip;
         |GRABA 020#24b;
     |FINSI;

     #24#7  = fFecha;                ||Fecha Entrada
     #24#8  = #24#8  + enResultado;  ||Resultado
     #24#9  = #24#9  + enIngresar;
     #24#10 = #24#10 + enDevolver;
     #24#11 = #24#11 + enCompensar;
     #24#13 = #24#13 + enImpReal;
     |GRABA 020#24a;
     |LIBERA #24;

     #23#4 = #23#4 + enIngresar;
     #23#5 = #23#5 + enDevolver;
     #23#6 = #23#4 - #23#5;
     |GRABA 020#23a;
     |LIBERA #23;

     |HAZ PintaAlgo;
|FPRC;

|PROCESO PintaAlgo;
     |SI enTodo > 4; |ACABA; |FINSI;
     |ATRI I;
     |PINTA 2303, "Modelo: ";
     |PINTA 2316, "Empresa: ";
     |PINTA 2333, "Ejercicio: ";
     |PINTA 2350, "Periodo: ";
     |ATRI 0;
     |PINTA 2311, #24#3;
     |PINTA 2325, #24#0;
     |PINTA 2344, #24#1;
     |PINTA 2359, #24#2;
|FPRC;


|PROGRAMA;
     |SI enTodo ! 0;
         |SI enTodo [ 4;
             |DDEFECTO #0;
             #0#14 = enAnyPase;
             #0#15 = enTodo;
             |HAZ Tipo3;
         |SINO;
             |DDEFECTO #0;
             #0#16 = enTodo - 4;
             #0#0 = enEmpresa;
             #0#1 = enEmpresa;
             #0#14 = enAnyPase;
             #0#15 = 2;
             |HAZ Tipo3;
         |FINSI;
     |SINO;
         |CLS;
         |MANTE #0;
     |FINSI;
|FPRO;
