|FICHEROS;
     dsmozp87 #0;

     dsmom002 #2;
     dsmom004 #4;

     dsmom084 #84,MANTE;
     dsmom080 #80;
     dsmom081 #81;
     dsmom082 #82;
     dsmom083 #83;

     dsmom085 #85;
     dsmom086 #86;
     dsmom087 #87;

     :/basica/def/agifigen #1000;

     espejo@ #900;
|FIN;

|VARIABLES;
   aAlfa1    = "";
   fFecha1   = @;

   nDEmpresa = 0;
   nHEmpresa = 0;
   nEjer     = 0;
   nModelo   = 0;
   nImpreso  = 0;
   nCalculado = 0;

   errconta  = 0;
   nModo     = 0;
   nInkey    = 0;

   nSwConPro  = 0;
   fFecIni    = @;
   fFecFin    = @;
   nSwExiste  = 0;

   f1          = "";
   tecla       = "";
   aPathModelo = "";
   aDef        = "";
|FIN;

|INCLUYE i_variar;

|| \\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\
|| .... Mirar si hace el modelo de IVA
|PROCESO MiraSiD;
   |SI #2#8 > "01.01.0000";
       |SI #2#8 > fFecFin;  |ACABA;  |FINSI;
   |FINSI;
   |SI #2#9 > "01.01.0000";
       |SI #2#9 < fFecIni;  |ACABA;  |FINSI;
   |FINSI;
   |SI #2#2 = 309; |O #2#2 = 308; |ACABA; |FINSI;
   |SI #2#2 = 310; |O #2#2 = 311; |ACABA; |FINSI;
   |SI #2#2 = 300; |O #2#2 = 303; |O #2#2 = 320; |O #2#2 = 330; |O #2#2 = 332; |O #2#2 = 370;
       nSwConPro = 0;
       |ERROR10;
   |FINSI;
|FPRC;

|DEFBUCLE MiraSiDIVA;
 #2#1;
 ;
 #84#0, 01, 300;
 #84#0, 99, 370;
 ;
 NULL, MiraSiD;
|FIN;

|PROCESO dsmom004;
  |SI #4#7 = "X";  nCalculado = 1; |FINSI;
  |SI #4#9 = "X";  |Y #4#2 = 12;  nImpreso = 1;   |FINSI;
|FPRC;

|DEFBUCLE dsmom004;
  #4#1;
  ;
  #84#0, #0#0, 01, nModelo, 00;
  #84#0, #0#0, 99, nModelo, 99;
  ;
  NULL, dsmom004;
|FIN;

|PROCESO Actualiza;
  nCalculado = 0;
  nImpreso   = 0;
  nModelo = 300; |BUCLE dsmom004;
  nModelo = 303; |BUCLE dsmom004;
  nModelo = 370; |BUCLE dsmom004;
|FPRC;

|PROCESO LeeEmpresa;
  || Lee Empresa
    errconta = 0;
    |ABRE #1000;
    #1000#0 = #84#0;
    |LEE 041#1000=;
    |SI FSalida ! 0;
        errconta = 1;
    |FINSI;
    |CIERRA #1000;
|FPRC;

|PROCESO Tipo11_84; |TIPO 11;
 nInkey = FSalida;
 |SI nInkey = 11;
     |HAZ Consulta;
     |ERROR;
 |FINSI;
 |SI nInkey = 10;
       |ERROR;
       eaElProm = "dsmom084";
       |HAZ ConsuCliente7;
       eaElProm = "";
       |PTEC 802;
 |FINSI;
|FPRC;

|PROCESO Tipo7_84; |TIPO 7;
    #0#1 = "";
    |HAZ LeeEmpresa;
    |SI errconta = 0; #0#1 = #1000#1; |FINSI;
    |PINTA #0#1;
    |PINTA 1812, "                   ";

    |SI #0#0 [ 2010;
        |ABRE #80;
        #80#0 = #84#0;
        #80#1 = #84#1;
        |LEE 001#80=;
        |SI FSalida ! 0;
            |ATRI I; |PINTA 1812, "SIN CALCULOS ... "; |ATRI 0;
        |FINSI;
        |CIERRA #80;
    |SINO;
        |ABRE #85;
        #85#0 = #84#0;
        #85#1 = #84#1;
        |LEE 001#85=;
        |SI FSalida ! 0;
            |ATRI I; |PINTA 1812, "SIN CALCULOS ... "; |ATRI 0;
        |FINSI;
        |CIERRA #85;
    |FINSI;
|FPRC;

|PROCESO Leom082;
    |SI #82#6 ! 0; #84#6 = "S"; |FINSI;
|FPRC;

|DEFBUCLE Leom082;
 #82#1;
 ;
 #80#0, #80#1, 01;
 #80#0, #80#1, 99;
 ;
 NULL, Leom082;
|FIN;

|PROCESO Leom087;
    nSwExiste = 1;
    |SI #87#10 !  0;
        #84#6 = "S";
        |ERROR10;
    |FINSI;
|FPRC;

|DEFBUCLE Leom087;
    #87#1;
    ;
    #85#0, #85#1, 01, 000, 00, 00, 2, 01;
    #85#0, #85#1, 99, 999, 99, 99, 2, 99;
    ;
    NULL, Leom087;
|FIN;

|PROCESO Tipo0_84; |TIPO 0;
    |HAZ LeeEmpresa;
    |SI errconta = 1;
        |MENAV "    No existe Empresa ";
        |ERROR;
        |ACABA;
    |FINSI;
    #0#1 = #1000#1;
    |PINTA #0#1;
|FPRC;

|PROCESO Tipo0_84_2; |TIPO 0;
    |PINTA 1812, "                   ";

    nImpreso = 0;
    nModo = (FEntrada / 100) ! 0;
    |SI nModo = 1; |HAZ Actualiza; |FINSI;

    |C_M #84#6, 1, "S";
    |SI #0#0 [ 2010;
        |ABRE #80;
        #80#0 = #84#0;
        #80#1 = #84#1;
        |LEE 001#80=;
        |SI FSalida = 0;
            #84#2 = #80#2; |PINTA #84#2;
            #84#3 = #80#3; |PINTA #84#3;
            #84#4 = #80#4; |PINTA #84#4;
            #84#5 = #80#5; |PINTA #84#5;
            #84#7 = #80#6; |PINTA #84#7;
            #84#8 = #80#7; |PINTA #84#8;
            #84#9 = #80#9; |PINTA #84#9;
            #84#10 = #80#43; |PINTA #84#10;
            #84#11 = #80#8; |PINTA #84#8;

            |SI #80#13 ! 0;
                #84#6 = "S";
            |SINO;
                #84#6 = "N";
                |BUCLE Leom082;
            |FINSI;
            |PINTA #84#6;
        |SINO;
            nSwConPro = -1;
            |BUCLE MiraSiDIVA;
            |SI nSwConPro = -1;
                |MENAV "    Esta Empresa no tiene Modelos de IVA en el Planing";
                nModo = (FEntrada / 100) ! 0;
                |SI nModo = 2;
                    |BORRA 020#84a;
                    |LIBERA #84;
                    |PONCONTROL  23, "SI";
                |FINSI;
                |ERROR;
                |PTEC 807;
            |SINO;
                |ATRI I; |PINTA 1812, "SIN CALCULOS ... "; |ATRI 0;
            |FINSI;
        |FINSI;
        |CIERRA #80;
   |SINO;

        |ABRE #85;
        #85#0 = #84#0;
        #85#1 = #84#1;
        |LEE 001#85=;
        |SI FSalida = 0;
            #84#2 = #85#2; |PINTA #84#2;
            #84#3 = #85#3; |PINTA #84#3;
            #84#4 = #85#4; |PINTA #84#4;
            #84#5 = #85#5; |PINTA #84#5;
            #84#7 = #85#6; |PINTA #84#7;
            #84#8 = #85#7; |PINTA #84#8;
            #84#11 = #80#8; |PINTA #84#8;

            #84#6 = "N";
            |BUCLE Leom087;
            |PINTA #84#6;
            |SI #84#6 = "S"; |O nImpreso = 1;
                |MENAV "    Existen datos en Modelos. Aplicara Prorrata Provisional y Regulariza en Ultimo Periodo";
                |C_M #84#6, 1, "N";
            |FINSI;
        |SINO;
            nSwConPro = -1;
            |BUCLE MiraSiDIVA;
            |SI nSwConPro = -1;
                |MENAV "    Esta Empresa no tiene Modelos de IVA en el Planing";
                nModo = (FEntrada / 100) ! 0;
                |SI nModo = 2;
                    |BORRA 020#84a;
                    |LIBERA #84;
                    |PONCONTROL  23, "SI";
                |FINSI;
                |ERROR;
                |PTEC 807;
            |SINO;
                |ATRI I; |PINTA 1812, "SIN CALCULOS ... "; |ATRI 0;
            |FINSI;
        |FINSI;
        |CIERRA #85;
   |FINSI;
|FPRC;

|PROCESO OtraVezAuto;
      |ABRE #85;
      #85#0 = #84#0;
      #85#1 = #84#1;
      |LEE 101#85=;
      |SI FSalida = 0;
          |SI #85#14 = "M";
              #85#14 = "A"; ||Para que calculo 100%
          |FINSI;
          |GRABA 020#85a;
      |FINSI;
      |LIBERA #85;
      |CIERRA #85;
|FPRC;

|PROCESO Tipo2_84; |TIPO 1;
  nModo = (FEntrada / 100) ! 0;
  |HAZ Actualiza;
  |SI nImpreso = 1;
      |MENAV "    No podemos modificar esta Empresa. Existen Modelos Impresos";
      |ERROR;
  |SINO;
      |SI nModo = 3; |HAZ OtraVezAuto; |FINSI;
  |FINSI;
|FPRC;

|PROCESO Tipo3_84; |TIPO 3;
  nModo = (FEntrada / 100) ! 0;
 |SI nModo = 1; |HAZ OtraVezAuto; |FINSI;
|FPRC;

|PROCESO LaConsulta;
 nInkey = FSalida;
 |SI nInkey = 11;
     |HAZ Consulta;
 |FINSI;
|FPRC;

|PROCESO Consulta;
     enTipoEntrada = 4;
     enEmpresa = #84#0;
     enEjer    = #84#1;
     |PUSHV 0924 2380;
     |SI #0#0 [ 2010;
         |SUB_EJECUTA_NP "dsmom080";
     |SINO;
         |SUB_EJECUTA_NP "dsmom085";
     |FINSI;
     |POPV;
|FPRC;

|| \\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\
|PROCESO BuscoDatos;
    nSwExiste = 0;
    |SI #0#0 [ 2010;
        |ABRE #80;
        #80#0 = #84#0;
        #80#1 = #84#1;
        |LEE 001#80=;
        |SI FSalida = 0;
            #84#2 = #80#2;
            #84#3 = #80#3;
            #84#4 = #80#4;
            #84#5 = #80#5;
            #84#7 = #80#6;
            #84#8 = #80#7;
            #84#9 = #80#9;
            #84#10 = #80#43;
            #84#11 = #80#8;
            |SI #80#13 ! 0;
                #84#6 = "S";
            |SINO;
                #84#6 = "N";
                |BUCLE Leom082;
            |FINSI;
            nSwExiste = 1;
        |FINSI;
        |CIERRA #80;
    |SINO;
        |ABRE #85;
        #85#0 = #84#0;
        #85#1 = #84#1;
        |LEE 001#85=;
        |SI FSalida = 0;
            #84#2 = #85#2;
            #84#3 = #85#3;
            #84#4 = #85#4;
            #84#5 = #85#5;
            #84#7 = #85#6;
            #84#8 = #85#7;
            #84#11 = #80#8;
            #84#6 = "N";
            |BUCLE Leom087;
        |FINSI;
        |CIERRA #85;
    |FINSI;

    |SI nSwExiste = 0;
        nSwConPro = -1;
        |BUCLE MiraSiDIVA;
        |SI nSwConPro = -1;
            nSwExiste = -1;
        |FINSI;
    |FINSI;

    |SI nSwExiste ! -1;
        |HAZ Actualiza;
        |SI nImpreso = 1;
            nSwExiste = -1;
        |FINSI;
    |FINSI;
|FPRC;

|PROCESO PasaEjercicioAnt;
  #900#0 = #84#0;
  #900#1 = #0#0;
  |LEE 141#900=;
  |SI FSalida ! 0;
      |DDEFECTO #900;
      #900#0 = #84#0;
      #900#1 = #0#0;
      |HAZ BuscoDatos;
      |SI nSwExiste ] 0;
          |GRABA 020#900n;
      |FINSI;
  |FINSI;
  |LIBERA #900;
  |LIBERA #84;
|FPRC;

|DEFBUCLE EjercicioAnt;
 #84#1;
 ;
 00001, nEjer;
 99999, nEjer;
 be;
 NULL, PasaEjercicioAnt;
|FIN;

|PROCESO EjercicioAnt;

     |DBASE aPathModelo;  |QBT aPathModelo;
     aDef = aPathModelo + "def/dsmom084.mas";
     |CARGA_DEF aDef, espejo@;
     |SI FSalida ! 0;
         aAlfa1 = "     Error en cargar el Def " + eaNomDef;
         |MENAV aAlfa1;
         |ERROR;
         |ACABA;
     |FINSI;
     nEjer = #0#0 - 1;

     |ABRE #900;
     |BUCLE EjercicioAnt;
     |CIERRA #900;
|FPRC;

|| \\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\
|RUTINA ClaveBaja84;
   #84#0 = nDEmpresa;
   #84#1 = nEjer;
|FRUT;

|RUTINA ClaveAlta84;
   #84#0 = nHEmpresa;
   #84#1 = nEjer;
|FRUT;

|PROGRAMA;

    |CLS;
    |CABEZA "Empresas Sin Prorrata ";

    |PINPA #0#0;

    fFecha1 = ~;
    aAlfa1 = fFecha1; aAlfa1 = aAlfa1 % -104;
    #0#0 = aAlfa1; |PINTA #0#0;
    aAlfa1 = "01.01." + aAlfa1; fFecIni = aAlfa1;
    aAlfa1 = #0#0;
    aAlfa1 = "31.12." + aAlfa1; fFecFin = aAlfa1;

    |ENCAMPO #0#0;
    |SI FSalida = 7; |O FSalida = 1; |ACABA; |FINSI;

    |MENSA "0415 Pulse <F12> para recoger Ejercicio Anterior";
    |LEETECLA tecla;
    f1 = &255 + "823";
    |BLIN 04;
    nInkey = FSalida;
    |SI tecla = f1;
        |HAZ EjercicioAnt;
    |FINSI;

    nEjer = #0#0;
    nDEmpresa = 00001;
    nHEmpresa = 99999;

    |SI #0#0 > 2010;
        |C_V #84#9, 1, "N"; |C_V #84#10, 1, "N";
        |C_P #84#11, 1, 1564;
        |PINTA 1542, " TOTAL OPERACIONES :              ";
        |ATRI P; |PINTA 1544, "TOTAL OPERACIONES : "; |ATRI 0;
        |PINTA 1642, "읕컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴켸";
        |PINTA 1742, "                                    ";
        |PINTA 1842, "                                    ";
        |PINTA 1942, "                                    ";
    |FINSI;

    |ENTLINEAL #2#84, -2, 2, 22, 0, ClaveBaja84, ClaveAlta84;
|FPRO;
