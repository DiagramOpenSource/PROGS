|FICHEROS;
     dsmoy051 #0;
     dsmom051 #51;  ||Mant. Valores por Empresas
     dsmom052 #52;  ||Socios
     dsmom053 #53;  ||Comuneros

    :/basica/def/agifigen #100;
    :/basica/def/agim0019 #119;  || Registro de Sociedades
    :/basica/def/agim0026 #126;  || Cargos
    :/basica/def/agim0024 #124;  || Representantes/Administradores

    :/integral/def/dsam0003 #300;
|FIN;

|VARIABLES;
     swExiste  = 0;
     nCampo    = 0;
     nDEmpresa = 0;
     nHEmpresa = 0;
     nPeriodo  = 0;
     nEjer     = 0;
     aAlfa     = "";
     aAlfa1    = "";
     aAlfa2    = "";

     nEmpres   = 0;
     nRegis    = 0;
     nSaldo    = 0;
     nSuma     = 0;
     nPvp      = 0;
     nTAccion  = 0;
     nCapzac   = 0;
     nImpCap   = 0;
     nTiPatr   = 0;
     fFecha    = @;
     &swLinea  = 0;
     &Dia      = "";
     &Mes      = "";
     &Ejer     = "";
     nSwSecre = 0;
     &eaPathIntegral = "";
     &eaCodRenta     = "";
     aBase     = "";
     nCanal    = 0;
     aElDni    = "";
|FIN;

|INCLUYE i_variar;
|INCLUYE i_varcon;

|| /////////////////////// PROCESOS ENTRADA POR PANTALLA \\\\\\\\\\\\\\\\\\\\
|CALCULO ElAny; |TIPO 7;
 fFecha = ~;
 aAlfa1 = fFecha;
 aAlfa1 = aAlfa1 % -104;
 #0#14 = aAlfa1;
 #0#14 = #0#14 - 1;
 |PINTA #0#14;
|FCAL;

|PROCESO CodigoRenta; |TIPO 0;
     |DBASS aBase;  |QBF aBase;
     aAlfa = aBase + "integral/def/dsam0000.mas";
     |XABRE "E", aAlfa, nCanal;
     |SI FSalida ] 0;
         eaPathIntegral  = aBase + "integral/";
     |FINSI;

     |C_M #0#22, 1, "S";
     |SI eaPathIntegral = "";
         |C_M #0#22, 1, "N";  #0#22 = "N";  |PINTA #0#22;
     |FINSI;
|FPRC;

|PROCESO Tipo7;  |TIPO 7;
     |SI enEmpresa ! 0;
         #0#0 = enEmpresa;  |PINTA #0#0;
         #0#1 = enEmpresa;  |PINTA #0#1;

         |C_M #0#0, 1, "N";
         |C_M #0#1, 1, "N";
     |FINSI;
|FPRC;

|PROCESO Parrilla;
     |SI #0#0 = 0;
         #0#0 = 0;  |PINTA #0#0;
         #0#1 = 0;  |PINTA #0#1;  |C_M #0#1, 1, "N";
         |PARA nCampo = 2;  |SI nCampo [ 13;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |SINO;
         |SI enEmpresa = 0;
             |C_M #0#0, 1, "S";
             |C_M #0#1, 1, "S";
         |FINSI;

         |PARA nCampo = 2;  |SI nCampo [ 13;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "N";
               #0nCampo = 0;  |PINTA #0nCampo;
         |SIGUE;
     |FINSI;
|FPRC;

|PROCESO NoMasEmpresas;
     |SI #0Campo = 0;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 13;  |HACIENDO nCampo = nCampo + 1;
               #0nCampo = 0;  |C_M #0nCampo, 1, "N";
         |SIGUE;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 13;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |FINSI;
|FPRC;

|| /////////////////////// //////////////////////////////////////////////////
|| /////////////////////// PROCESOS DE INFORMES \\\\\\\\\\\\\\\\\\\\\
|PROCESO RentaSI;
  eaCodRenta = "";
  |SI #0#22 = "S";
     |ABRE #300;
     |SELECT #2#300;
     #300#1 = aElDni;
     |LEE 040#300=;
     |SI FSalida = 0;
         eaCodRenta = "Codigo de Renta : " + (("00000" + #300#0) % -105);
     |SINO;
         |SELECT #3#300;
         #300#35 = aElDni;
         |LEE 040#300=;
         |SI FSalida = 0;
             eaCodRenta = "Codigo de Renta : " + (("00000" + #300#0) % -105);
         |FINSI;
     |FINSI;
     |CIERRA #300;
  |FINSI;
|FPRC;

|PROCESO ImpComuneros0;
  aElDni = #53#5;
  |HAZ RentaSI;
  |PRINT;
  |PIEINF;
|FPRC;

|DEFBUCLE ImpComuneros;
  #53#1;
  ;
  #51#0, #51#1, 01, 00001;
  #51#0, #51#1, 99, 99999;
  be;
  NULL, ImpComuneros0;
|FIN;

|PROCESO ImpSocios0;
  aElDni = #52#4;
  |HAZ RentaSI;
  |PRINT;
  |PIEINF;
|FPRC;

|DEFBUCLE ImpSocios;
    #52#1;
    ;
    #51#0, #51#1, 001;
    #51#0, #51#1, 999;
    be;
    NULL, ImpSocios0;
|FIN;

|PROCESO Imprimir0;
     enEmpresaPINET = #51#0;
     |HAZ RestriccionPINET;
     |SI enErrorPINET = 1;  |ACABA;  |FINSI;

 |PDEFECTO #0,16,22;
 |HAZ BuscaDatos;
 |SI enEjerBajaM > 0; |Y enEjerBajaM < #0#14; |ACABA; |FINSI; ||esta ya disuelta

 |SI #51#6 = 2;  |O nRegis ! 0; ||Una Sociedad
                                || ... |SI #51#4 = 2;  ||Una Sociedad
    |INFOR "dsmoys51";
    |SI FSalida = 0;
        |BUCLE ImpSocios;
        |FININF;
    |FINSI;
 |SINO;
    |INFOR "dsmoyc51";
    |SI FSalida = 0;
        |BUCLE ImpComuneros;
        |FININF;
    |FINSI;
 |FINSI;
|FPRC;

|| .....................................................................
|DEFBUCLE Imprimir;
    #51#1;
    ;
    nDEmpresa, enEjer;
    nHEmpresa, enEjer;
    be;
    NULL, Imprimir0;
|FIN;

|| \\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\
|PROCESO ITipo3;  |TIPO 3;
 |HAZ Fecha;
 |IMPRE 0;
 |SI FSalida ! 0; |ACABA; |FINSI;

|| |INFOR "dsmoy051";
|| |SI FSalida ! 0; |FINIMP; |ACABA; |FINSI;

     enEjer      = #0#14;        || A¤o Calculo
     aAlfa1      = #0#14;
     aAlfa1      = aAlfa1 % -102;
     nEjer       = aAlfa1;       || Dos ultimos digitos del A¤o
     |SI #0#0 ! 0;
         nDEmpresa = #0#0;
         nHEmpresa = #0#1;
         |BUCLE Imprimir;
     |SINO;
         |PARA nCampo = 2;  |SI nCampo [ 13;  |HACIENDO nCampo = nCampo + 1;
               nDEmpresa = #0nCampo;
               nHEmpresa = #0nCampo;
               |BUCLE Imprimir;         || Imprimir Valor
         |SIGUE;
     |FINSI;
     |FINIMP;
|FPRC;

|PROCESO Fecha;
  Dia  = #0#15 % 102;
  Mes  = #0#15 % 402;
  Ejer = #0#15 % 704;

  |SI Mes = "01"; Mes = "Enero     "; |FINSI;
  |SI Mes = "02"; Mes = "Febrero   "; |FINSI;
  |SI Mes = "03"; Mes = "Marzo     "; |FINSI;
  |SI Mes = "04"; Mes = "Abril     "; |FINSI;
  |SI Mes = "05"; Mes = "Mayo      "; |FINSI;
  |SI Mes = "06"; Mes = "Junio     "; |FINSI;
  |SI Mes = "07"; Mes = "Julio     "; |FINSI;
  |SI Mes = "08"; Mes = "Agosto    "; |FINSI;
  |SI Mes = "09"; Mes = "Septiembre"; |FINSI;
  |SI Mes = "10"; Mes = "Octubre   "; |FINSI;
  |SI Mes = "11"; Mes = "Noviembre "; |FINSI;
  |SI Mes = "12"; Mes = "Diciembre "; |FINSI;
|FPRC;
|| =========================================================================
|PROGRAMA;
 |SI enEmpresa ! 0;
     #0#0 = enEmpresa;
     #0#1 = enEmpresa;
     #0#14 = enEjer;
     |HAZ ITipo3;
 |SINO;
     |CLS;
     |MANTE #0;
 |FINSI;
|FPRO;

|| :::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
|PROCESO agim0024;
     |SI #124#15 ! "A";
          #0#16 = #124#4;              || Nombre Remitente
          #0#17 = "REPRESENTANTE";     || Cargo Remitente
          #0#18 = #124#3;              || NIF Remitente
          nSwSecre = 1;
         |ERROR10;
     |FINSI;
|FPRC;

|DEFBUCLE agim0024;
     #124#2;
     ;
     #51#0, nRegis, 01;
     #51#0, nRegis, 99;
     ;
     NULL, agim0024;
|FIN;

|PROCESO BuscaSecre;
  nSwSecre = 1;
  #0#16 = #126#4;      || Nombre Remitente
  #0#17 = #126#17;     || Cargo Remitente
  #0#18 = #126#3;      || NIF Remitente
  |ERROR10;
|FPRC;

|DEFBUCLE agim0026;
  #126#1;
  16;
  #51#0, nRegis, 01, 3;
  #51#0, nRegis, 99, 3;
  ;
  NULL, BuscaSecre;
|FIN;

|PROCESO BuscaDatos;
 enEjerBajaM = 0;

 |ABRE #agifigen;
 #agifigen#0 = #51#0; || Empresa
 |LEE 041#agifigen.=;
 |SI FSalida ! 0;
     |DDEFECTO #agifigen;
 |FINSI;
 |CIERRA #agifigen;
 #0#19 = #agifigen#4;   ||Direccion Empresa
 #0#20 = #agifigen#13;  ||NIF Empresa
 #0#21 = #agifigen#11;  ||Poblacion Empresa

 || Lee Ficha Sociedades
    swExiste = 0;
    nRegis   = 0;
    |ABRE #agim0019;
    #agim0019#0 = #51#0; || Empresa
    #agim0019#1 = 99;
    |LEE 041#agim0019.];
    |SI FSalida = 0;
        |LEE 041#agim0019.a;
    |SINO;
        |LEE 041#agim0019.u;
    |FINSI;
    |SI FSalida = 0; |Y #agim0019#0 = #51#0;
        swExiste = 1;
    |FINSI;
    |SI swExiste = 1;
        nRegis = #agim0019#1;  ||Registre
        |SI #agim0019#12 > "01.01.1000";
            aAlfa1 = #agim0019#12; aAlfa1 = aAlfa1 % -104; enEjerBajaM = aAlfa1;
        |FINSI;
    |FINSI;
    |CIERRA #agim0019;

 nSwSecre = 0;
 |BUCLE agim0026;
 |SI nSwSecre = 0;
     |BUCLE agim0024;
     |SI nSwSecre = 0;
         |ABRE #124;
         #124#0 = #51#0
         #124#1 = nRegis;
         #124#2 = 01;
         |LEE 040#124];
         |SI FSalida = 0;  |Y #124#0 = #51#0;  |Y #124#1 = #51#1;
             #0#16 = #124#4;              || Nombre Remitente
             #0#17 = "ADMINISTRADOR";     || Cargo Remitente
             #0#18 = #124#3;              || NIF Remitente
         |FINSI;
         |CIERRA #124;
     |FINSI;
 |FINSI;
|FPRC;
