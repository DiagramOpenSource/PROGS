|FICHEROS;
    dsmoz044 #0;
    dsmom042 #42;
    dsmom041 #41;
|FIN;

|VARIABLES;
  nDEmpresa   = 0;
  nHEmpresa   = 0;
  nDActividad = 0;
  nHActividad = 0;
  nCampo      = 0;

  alfa1 = "";
  cfecha = @;
  vfecha = @;
  afiscal = 0;
  acontab = 0;
  importe = 0;
  sw = 0;
  aDEle = "";
  aHEle = "";
  nDEle = 0;
  nHEle = 0;
  &nCamSel = 18;
  &nCamTip = 31;
  nLaEmpresa = 0;
  swBueno = 0;
  ultFechaA = @;
|FIN;

|INCLUYE i_variar;
|INCLUYE i_varcon;
|INCLUYE i_numamo;
|INCLUYE i_selcon;

|| //////////////////// Calculos de Pantalla \\\\\\\\\\\\\\\\\\\\\\\\\\
|CALCULO Tipo7;  |TIPO 7;
     |SI enEmpresa ! 0;
         #0#4 = enEmpresa; |PINTA #0#4;
         #0#5 = enEmpresa; |PINTA #0#5;

         |C_M #0#4, 1, "N";
         |C_M #0#5, 1, "N";
     |FINSI;
|FCAL;

|CALCULO Parrilla;
     |SI #0#4 = 0;
         #0#4 = 0;  |PINTA #0#4;
         #0#5 = 0;  |PINTA #0#5;  |C_M #0#5, 1, "N";
         |PARA nCampo = 6;  |SI nCampo [ 17;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |SINO;
         |SI enEmpresa = 0;
             |C_M #0#4, 1, "S";
             |C_M #0#5, 1, "S";
         |FINSI;

         |PARA nCampo = 6;  |SI nCampo [ 17;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "N";
               #0nCampo = 0;  |PINTA #0nCampo;
         |SIGUE;
     |FINSI;
|FCAL;

|CALCULO NoMasEmpresas;
     |SI #0Campo = 0;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 17;  |HACIENDO nCampo = nCampo + 1;
               #0nCampo = 0;  |C_M #0nCampo, 1, "N";
         |SIGUE;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 17;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |FINSI;
|FCAL;

|CALCULO numamo; |TIPO 0;
    x_alfa1 = #0Campo;
    |HAZ x_punto;
    #0Campo = x_alfa1;
    |PINTA #0Campo;
|FCAL;

|| /////////////////////////// Procesos \\\\\\\\\\\\\\\\\\\\\\\\\\\\\\
|PROCESO L_Amortiza;
   #42#1 = #41#1;    ||Recupera la Actividad
   |SI #42#6 = "C";
      importe = (importe + #42#7) ! EURODBMOD;
      #42#11 = #42#7;
      |SI sw = 0;
         cfecha = #42#9;
         sw = 1;
      |FINSI;
   |FINSI;
   |SI #42#6 = "A";
       |SI #dsmom041#28 ! 0; #42#7 = #41#11; |FINSI;
       afiscal = (afiscal + #42#7)  ! EURODBMOD;
       acontab = (acontab + #42#11) ! EURODBMOD;
       |SI ultFechaA < #42#9; ultFechaA = #42#9; |FINSI;
   |FINSI;
   |SI #42#6 = "V";
      vfecha = #42#9;
   |FINSI;
   #42#13 = #42#11 -  #42#7;
   |GRABA 020#42a;
   |LIBERA #42;
|FPRC;

|DEFBUCLE lineas;
   #42#1;
   ;
   #41#0,#41#2,#41#3, 000;
   #41#0,#41#2,#41#3, 999;
   be;
   NULL, L_Amortiza;
|FIN;

|PROCESO C_Amortiza;

   |SI nLaEmpresa ! #41#0;
       nLaEmpresa = #41#0;
       enEmpresa  = #41#0;
       swBueno = 0;
       |HAZ LaSelEmpresa;
       |SI enSwSelEmp = 1; swBueno = 1;  |FINSI;
   |FINSI;
   |SI swBueno = 1; |ACABA; |FINSI;

   #41#32 = eaNombreEmp;

   cfecha    = "01.01.0000";
   importe   = 0;
   afiscal   = 0;
   acontab   = 0;
   vfecha    = "01.01.0000";
   ultFechaA = "01.01.0000";
   sw = 0;
   #0#3 = #41#0; |PINTA #0#3;
   #0#1 = #41#2; |PINTA #0#1;
   #0#2 = #41#3; |PINTA #0#2;

   |BUCLE lineas;
   |SI sw = 1;
      #41#27 = cfecha;
      |SI #41#35 = "S";
          #41#9 = "01.01.0000";
      |SINO;
          |SI #41#9 < #41#27; #41#9 = #41#27; |FINSI;
      |FINSI;
      #41#26 = vfecha;
      #41#10 = importe;
      #41#15 = afiscal;
      #41#16 = (importe - afiscal) ! EURODBMOD;
      #41#21 = acontab;
      #41#22 = (importe - acontab) ! EURODBMOD;

      |SI #41#10 > 0;
          |SI #41#16 < 0; #41#16 = 0; |FINSI;
          |SI #41#22 < 0; #41#22 = 0; |FINSI;
      |SINO;
          |SI #41#16 > 0; #41#16 = 0; |FINSI;
          |SI #41#22 > 0; #41#22 = 0; |FINSI;
      |FINSI;

      |SI vfecha > "01.01.1900";
          |SI ultFechaA ] vfecha;
              #41#16 = 0;
              #41#22 = 0;
          |FINSI;
          #41#40 = "I";
          #41#58 = 02;
      |FINSI;
      |SI #41#58 = 0;
          |SI #41#40 = "I"; #41#58 = 99; |FINSI;
          |SI #41#16 = 0; |Y #41#22 = 0;
              #41#58 = 1;
              #41#40 = "I";
          |FINSI;
      |FINSI;

      |GRABA 020#41a;
   |FINSI;
   |LIBERA #41;
|FPRC;

|DEFBUCLE dsmoz044;
   #41#1;
   1;
   nDEmpresa, aDEle, nDEle, nDActividad;
   nHEmpresa, aHEle, nHEle, nHActividad;
   be;
   NULL, C_Amortiza;
|FIN;


|CALCULO Tipo3;  |TIPO 3;

  |SI #0#0 = "N"; |ACABA; |FINSI;
  |HAZ LeeEURODBMOD;

  efFecTope = ~;
  nDActividad = 00;
  nHActividad = 99;
  aDEle = #0#32; nDEle = #0#34;
  aHEle = #0#33; nHEle = #0#35;
  |SI #0#4 ! 0;
         nDEmpresa  = #0#4;
         nHEmpresa  = #0#5;
         nLaEmpresa = -1;
         swBueno    = 0;
         |BUCLE dsmoz044;
     |SINO;
         |PARA nCampo = 6; |SI nCampo [ 17;  |HACIENDO nCampo = nCampo + 1;
               nDEmpresa = #0nCampo;
               nHEmpresa = #0nCampo;
               nLaEmpresa = -1;
               swBueno = 0;
               |BUCLE dsmoz044;
         |SIGUE;
     |FINSI;
|FCAL;
|| ********************************************************************

|PROGRAMA;
     |SI PARAMETRO = "";
         |MANTE #dsmoz044;
         |ACABA;
     |FINSI;

     |PINPA #0#0;

     |DDEFECTO #0;
     #0#0 = "S";
     #0#4 = PARAMETRO;
     #0#5 = PARAMETRO;

     |PINDA #0#0;
     |HAZ Tipo3;
|FPRO;
