|FICHEROS;
     dsmoy000;
     dsmom002;
     dsmom004;
     dsmom107;

     dsmod123;
     dsmow014;
     t1512301;

     &clientes@;
     &datosper@;
|FIN;

|VARIABLES;
     fFecIni = @;
     fFecFin = @;
|FIN;

|INCLUYE i_varemi;

|PROCESO PresentaModelo_02;
     |SI eaOpcion = "1";
         eaNIF = #t1512301#7;
         |HAZ PortalPDFPeriodicos;

         |HAZ GrabaImpreso;
     |FINSI;

     |SI eaOpcion = "3";
         eaFOrigen  = eaPathEmision  + eaFicheroPlano;
         eaFDestino = eaPathInternet + eaFicheroPlano;
         |HAZ CopiaFichero;

         |HAZ GrabaImpreso;
     |FINSI;

     |SI eaOpcion = "6";
         eaX002_10    = #t1512301#7;
         eaX002_11    = #t1512301#8;
         eaX002_12    = #t1512301#2;
         eaX002_13    = #t1512301#10;
         eaX002_14    = #t1512301#11;

         |SI #dsmod123#4 > 0;
             eaX002_15 = "C";
             eaX002_16 = #t1512301#21;
         |FINSI;

         eaX002_17 = #dsmod123#11;
         eaX002_18 = #t1512301#6;
         eaX002_19 = #t1512301#22;
         eaX002_48 = eaNomEnt;

         |HAZ PresentaPortal;
     |FINSI;
|FPRC;

|PROCESO AbrePagina;
     eaPathAEAT = eaUnidadBasico + "\AEAT";        |RMKDIR eaPathAEAT;
     eaPathAEAT = eaPathAEAT + "\" + #dsmoy000#0;  |RMKDIR eaPathAEAT;
     eaPathAEAT = eaPathAEAT + "\" + #dsmoy000#6;  |RMKDIR eaPathAEAT;
     |SI #dsmoy000#4 ! 99;
         eaPathAEAT = eaPathAEAT + "\" + (("00" + #dsmoy000#4) % -102);  |RMKDIR eaPathAEAT;
     |FINSI;
     eaPathAEAT = eaPathAEAT + "\";

     eaFOrigen  = eaPathEmision  + eaFicheroPlano;
     eaFDestino = eaPathAEAT + eaFicheroPlano;
     |HAZ CopiaFichero;

     aAlfa       = eaPathLanza;
     eaPathLanza = eaPathAEAT;
     |HAZ TraeSoloWait;
     eaPathLanza = aAlfa;

     eaForzExplorador = "N";
     |HAZ Explorador;

     aAbre = eaPathAEAT + "imp" + Usuario + ".bat";
     |XABRE "WBC", aAbre, enHandlePortal;

     eaAlfa = eaPathAEAT + "wait ";
     |HAZ GrabaPortalSin;

     eaAlfa = eaPathExplorador + " " + &34;
     |HAZ GrabaPortalSin;

     eaAlfa = "https://www1.agenciatributaria.gob.es/wlpl/OV15-M123/index.zul";
     |HAZ GrabaPortalSin;

     |XCIERRA enHandlePortal;

     |RSYSTEM aAbre;

     aAlfa = eaPathExplorador > "";
     aAlfa = aAlfa - "CHROME.EXE";
     |SI FSalida ! 0;  |SLEEP 30;  |FINSI;

     aAlfa = eaPathAEAT + aFichero;
     |RFBORRA aAlfa;
     |RFBORRA aAbre;
|FPRC;

|PROCESO TDeclaracion_02;
     enModelo = 123;  enForzar = 1;  |HAZ CogeDatosBancos;

     |SI #dsmod123#11 > 0;
         enNume = #dsmod123#11;  |HAZ Conver17;  #t1512301#19 = eaAlfa;

                                 #t1512301#6 = "I";
         |SI eaFormaPago = "3";  #t1512301#6 = "U";  |FINSI;
         |SI eaFormaPago = "6";  #t1512301#6 = "G";  |FINSI;

         #t1512301#22 = eaIBAN;
     |SINO;
         enNume = 0;  |HAZ Conver17;  #t1512301#19 = eaAlfa;
         #t1512301#6 = "N";
     |FINSI;
|FPRC;

|PROCESO PersonaFis2015;
     aAlfa = eaVarDni % 101;
     |SI aAlfa = "0"; |O aAlfa = "1"; |O aAlfa = "2"; |O aAlfa = "3";
      |O aAlfa = "4"; |O aAlfa = "5"; |O aAlfa = "6"; |O aAlfa = "7";
      |O aAlfa = "8"; |O aAlfa = "9"; |O aAlfa = "X"; |O aAlfa = "Y";
      |O aAlfa = "Z";
         eaAlfa = #clientes@#1;     |HAZ BuscaNombre;
         eaAlfa = eaAlfa1;    |HAZ QuitaRaros;     #t1512301#8   = eaAlfa;
         eaAlfa = eaAlfa2;    |HAZ QuitaRaros;     #t1512301#9   = eaAlfa;
      |SINO;
         eaAlfa   = #clientes@#1;    |HAZ QuitaRaros;    #t1512301#8   = eaAlfa;
                                                         #t1512301#9   = " ";
      |FINSI;
|FPRC;

|PROCESO Carga123_02;
     enCodCli = #dsmod123#0;
     |HAZ LeePersona;

     |DDEFECTO #t1512301;

     |SI enSwDeDonde = 1;
         eaVarDni = #clientes@#3;  |HAZ CalculameDNI;   #t1512301#7  = eaVarDni;

         aAlfa1   = #datosper@#2;  |QBT aAlfa1;
         |SI aAlfa1 ! "";
             eaAlfa = #datosper@#2;  |HAZ QuitaRaros;  aAlfa1 = eaAlfa;  |QBF aAlfa1;
             eaAlfa = #datosper@#3;  |HAZ QuitaRaros;  aAlfa2 = eaAlfa;  |QBF aAlfa2;
             #t1512301#8 = aAlfa1 + " " + aAlfa2;

             eaAlfa = #datosper@#4;  |HAZ QuitaRaros;
             #t1512301#9 = eaAlfa;
         |SINO;
             |HAZ PersonaFis2015;
         |FINSI;
     |SINO;
         eaVarDni = #clientes@#13;  |HAZ CalculameDNI;   #t1512301#7   = eaVarDni;
         |HAZ PersonaFis2015;
     |FINSI;

     |HAZ TDeclaracion_02;

     #t1512301#10  = #dsmod123#1;

     |SI aPeriodo = "T";
         |SI #dsmod123#2 = 3;   #t1512301#11 = "1T";  |FINSI;
         |SI #dsmod123#2 = 6;   #t1512301#11 = "2T";  |FINSI;
         |SI #dsmod123#2 = 9;   #t1512301#11 = "3T";  |FINSI;
         |SI #dsmod123#2 = 12;  #t1512301#11 = "4T";  |FINSI;
     |SINO;
         #t1512301#11 = ("00" + #dsmod123#2) % -102;
     |FINSI;

     eaAlfa  = ("00000000000000000" + #dsmod123#6) % -115;    #t1512301#12 = eaAlfa;
     enNume = #dsmod123#7;    |HAZ Conver17;   #t1512301#13 = eaAlfa;
     enNume = #dsmod123#8;    |HAZ Conver17;   #t1512301#14 = eaAlfa;
     enNume = #dsmod123#9;    |HAZ Conver17;   #t1512301#15 = eaAlfa;
     enNume = #dsmod123#10;   |HAZ Conver17;   #t1512301#16 = eaAlfa;
     enNume = #dsmod123#13;   |HAZ Conver17;   #t1512301#17 = eaAlfa;
     enNume = #dsmod123#14;   |HAZ Conver17;   #t1512301#18 = eaAlfa;

     |SI #dsmod123#4 > 0;
         #t1512301#20 = "X";
         #t1512301#21 = #dsmom004#21;
     |FINSI;

     #t1512301#26 = &13 + &10;

     aAlfa = "<T1230" + #t1512301#10 + #t1512301#11 + "0000>";  |XGRABA nHandle2, aAlfa;
     aAlfa = "<AUX>";                                           |XGRABA nHandle2, aAlfa;
     aAlfa = " " * 70;                                          |XGRABA nHandle2, aAlfa;
     aAlfa = eaVersMod;                                         |XGRABA nHandle2, aAlfa;
     aAlfa = " " * 4;                                           |XGRABA nHandle2, aAlfa;
     aAlfa = eaNIFDs;                                           |XGRABA nHandle2, aAlfa;
     aAlfa = " " * 213;                                         |XGRABA nHandle2, aAlfa;
     aAlfa = "</AUX>";                                          |XGRABA nHandle2, aAlfa;

     aAlfa = "";
     |PARA nCampo = 1;  |SI nCampo [ 25;  |HACIENDO nCampo = nCampo + 1;
           aAlfa = #t1512301#nCampo#;
           |XGRABA nHandle2, aAlfa;
     |SIGUE;

     aAlfa = "</T1230" + #t1512301#10 + #t1512301#11 + "0000>";  |XGRABA nHandle2, aAlfa;
|FPRC;

|PROCESO dsmom002_02;
     |SI #dsmom002#8 > "01.01.0000";
         |SI #dsmom002#8 > fFecFin;  |ACABA;  |FINSI;
     |FINSI;

     |SI #dsmom002#9 > "01.01.0000";
         |SI #dsmom002#9 < fFecIni;  |ACABA;  |FINSI;
     |FINSI;

     aAlfa = #dsmom002#7 % 101;
     |SI aAlfa ! #dsmom004#22; |Y #dsmom004#22 ! " ";
         |SI #dsmom004#22 = "T"; #dsmom002#7 = "TRIMESTRAL"; |FINSI;
         |SI #dsmom004#22 = "M"; #dsmom002#7 = "MENSUAL   "; |FINSI;
         |SI #dsmom004#22 = "A"; #dsmom002#7 = "ANUAL     "; |FINSI;
     |FINSI;

     |SI #dsmom002#7 = "TRIMESTRAL";
         aPeriodo = "T";
     |SINO;
         aPeriodo = "M";
     |FINSI;

     |ERROR10;
|FPRC;

|DEFBUCLE dsmom002_02;
     #dsmom002#1;
     ;
     #dsmom004#0, 01, 123;
     #dsmom004#0, 98, 123;
     ;
     NULL, dsmom002_02;
|FIN;

|PROCESO dsmow014_2015;
     |ABRE #dsmom004;
     #dsmom004#0  = #dsmow014#0;
     #dsmom004#1  = #dsmow014#1;
     #dsmom004#2  = #dsmow014#2;
     #dsmom004#3  = #dsmow014#5;
     #dsmom004#4  = #dsmow014#7;
     |LEE 040#dsmom004.=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;
     |CIERRA #dsmom004;

     |ABRE #dsmod123;
     #dsmod123#0 = #dsmom004#0;
     #dsmod123#1 = #dsmom004#1;
     #dsmod123#2 = #dsmom004#2;
     #dsmod123#3 = #dsmom004#3;
     #dsmod123#4 = #dsmom004#4;
     #dsmod123#5 = 0;
     |LEE 040#dsmod123.=;
     |SI FSalida ! 0;  |CIERRA #dsmod123;  |ACABA;  |FINSI;
     |CIERRA #dsmod123;

     aPeriodo = "T";
     |BUCLE dsmom002_02;

     eaFicheroPlano = "123" + (("00000" + #dsmow014#0) % -105) + ".txt";
     eaFicheroImpre = "123" + (("00000" + #dsmow014#0) % -105) + ".pdf";
     eaFicheroError = "err" + (("00000" + #dsmow014#0) % -105) + ".txt";

     aAlfa = eaPathLanza + eaFicheroPlano;  |RFBORRA aAlfa;
     aAlfa = eaPathLanza + eaFicheroImpre;  |RFBORRA aAlfa;
     aAlfa = eaPathLanza + eaFicheroError;  |RFBORRA aAlfa;

     aAbre = eaPathEmision + eaFicheroPlano;
     |XABRE "BW", aAbre, nHandle2;

     |HAZ Carga123_02;

     |XCIERRA nHandle2;

     enEmpresa    = #dsmow014#0;
     enComple     = #dsmow014#7;
     eaNomEmpresa = #dsmow014#3;

     |HAZ PresentaModelo_02;
|FPRC;

|DEFBUCLE dsmow014C_2015;
     #dsmow014#1;
     6;
     00000, 0000, 00, "S";
     99999, 9999, 99, "S";
     ;
     NULL, dsmow014_2015;
|FIN;

|DEFBUCLE dsmow014N_2015;
     #dsmow014#2;
     6;
     "                                        ", "S";
     "zzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzz", "S";
     ;
     NULL, dsmow014_2015;
|FIN;

|PROCESO Bloque2015;
     |SI #dsmoy000#6 [ 2018;
         |HAZ BtnPeriodicas2015;
     |FINSI;

     |SI #dsmoy000#6 ] 2019;
         |HAZ BtnPeriodicas2019;
     |FINSI;

     |SI enTecla = 0;  |ACABA;  |FINSI;

     |SI eaOpcion = "1";
         |MENAV "0000La opcion de validacion no esta activada por hacienda.";
         |ACABA;
     |FINSI;

     nSwLee = 0;
     |SI #dsmoy000#8 = "C";
         |BUCLE dsmow014C_2015;
     |SINO;
         |BUCLE dsmow014N_2015;
     |FINSI;

     |SI eaOpcion = "1";
         |HAZ SacaErrorPtl;
     |FINSI;

     |SI eaOpcion = "3";
         aAlfa =  "      Los ficheros generados estan: " + eaPathInternet + " de su PC";
         |MENAV aAlfa;
         |SI #dsmoy000#6 [ 2018;
             |CONFI "0000SQuiere cargar la pagina de hacienda del modelo";
             |SI FSalida = 0;
                 |HAZ AbrePagina;
             |FINSI;
         |FINSI;
     |FINSI;

     |SI enDirecta = 1;  |Y enMsv = 1;  |Y enParar = 0;
         |SUB_EJECUTA "|dsmox007";
     |FINSI;
|FPRC;

|PROCESO moy12302;
     |ABRE #dsmom107;
     |LEE 040#dsmom107.p;
     |SI FSalida ! 0;
         |CIERRA #dsmom107;
         |SUB_EJECUTA_NP "dsmom107";
         |ABRE #dsmom107;
     |FINSI;

     |LEE 000#dsmom107.p;
     |SI FSalida ! 0;  |DDEFECTO #dsmom107;  |FINSI;
     |CIERRA #dsmom107;

     aAlfa1 = #dsmoy000#6;
     aAlfa1 = "01.01." + aAlfa1; fFecIni = aAlfa1;
     aAlfa1 = #dsmoy000#6;
     aAlfa1 = "31.12." + aAlfa1; fFecFin = aAlfa1;

     enSwTrata      = 0;
     enCambiaMoneda = 1;

     |HAZ Bloque2015;
|FPRC;
