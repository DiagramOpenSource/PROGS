|FICHEROS;
   dsmoy093;

   dsmom004 #4;
   dsmom096 #96;

   dsmoc349 #349;
   dsmod349 #350;

   :/basica/def/agifigen #118;
|FIN;

|VARIABLES;
   aParam      = "";
   informe     = "dsmoy093";
   nDEmpresa   = 0;
   nHEmpresa   = 0;
   nCampo      = 0;
   nCampo1     = 0;

   fFecha      = @;
   nAnyo       = 0;
   nMes        = 0;
   nPerAnt     = 0;
   nEjerAnt    = 0;
   aAlfa       = "";
   aAlfa1      = "";
   aAlfa2      = "";
   nNume1      = 0;
   nPara1      = 0;
   nPara2      = 0;
   nPara3      = 0;
   nPrim       = 0;

   nElPrint    = 0;
   nSw         = 0;
   nSwAc       = 0;
   nSwAn       = 0;
   nSwHay      = 0;

   nPrint      = 0;
   nEjer       = 0;
   nPeriodo    = 0;
   aPeriodo    = "";
   nPerLeer    = 0;
   nComplem    = 0;
   nModelo     = 0;
   nDPer349    = 0;
   nDPeriodo   = 0;
   nHPeriodo   = 0;
   nHayModelo  = 0;

   nEmpresa    = 0;
   nImporte    = 0;
   aTipo1      = "";
   aTipo2      = "";
   &nSwLinea   = 0;

   aCampo8     = "";
   nIncre      = 0;

   &eaTexto1  = "";
   &eaTexto2  = "";
   &eaTexto3  = "";
   &eaTexto4  = "";
   &eaTexto11 = "";
   &eaTexto12 = "";
   &eaTexto13 = "";
   &eaTexto14 = "";
|FIN;

|INCLUYE i_variar;
|INCLUYE i_varcon;
|INCLUYE i_varlib;

|| //////////////////// Calculos de Pantalla \\\\\\\\\\\\\\\\\\\\\\\\\\
|CALCULO Eano; |TIPO 7;
     fFecha = ~;
     aAlfa  = fFecha % 402;   nMes  = aAlfa;
     aAlfa  = fFecha % -104;  nAnyo = aAlfa;

     |SI nMes [ 3;
         #dsmoy093#26 = 12;
         #dsmoy093#1 = nAnyo - 1;
         |HAZ Periodo;
         |PINTA #dsmoy093#1;
         |PINTA #dsmoy093#26;
         |ACABA;
     |FINSI;

     |SI nMes [ 6;
         #dsmoy093#26 = 3;
         #dsmoy093#1 = nAnyo;
         |HAZ Periodo;
         |PINTA #dsmoy093#1;
         |PINTA #dsmoy093#26;
         |ACABA;
     |FINSI;

     |SI nMes [ 9;
         #dsmoy093#26 = 6;
         #dsmoy093#1 = nAnyo;
         |HAZ Periodo;
         |PINTA #dsmoy093#1;
         |PINTA #dsmoy093#26;
         |ACABA;
     |FINSI;

     |SI nMes [ 12;
         #dsmoy093#26 = 9;
         #dsmoy093#1 = nAnyo;
         |HAZ Periodo;
         |PINTA #dsmoy093#1;
         |PINTA #dsmoy093#26;
    |FINSI;
|FCAL;

|PROCESO defecto; |TIPO 0;
|FPRC;

|PROCESO Periodo;
     #dsmoy093#27 = "          ";

     |SI #dsmoy093#26 = 3;   #dsmoy093#27 = "MARZO     ";  |FINSI;
     |SI #dsmoy093#26 = 6;   #dsmoy093#27 = "JUNIO     ";  |FINSI;
     |SI #dsmoy093#26 = 9;   #dsmoy093#27 = "SEPTIEMBRE";  |FINSI;
     |SI #dsmoy093#26 = 12;  #dsmoy093#27 = "DICIEMBRE ";  |FINSI;

     |SI #dsmoy093#27 = "          "; |Y aParam = "";
         |MENAV "     Periodo Incorrecto. ";
         |ERROR;
     |FINSI;

     |SI aParam = ""; |PINTA #dsmoy093#27; |FINSI;
|FPRC;

|CALCULO Tipo7;  |TIPO 7;
     |SI enEmpresa ! 0;
         #dsmoy093#2  = enEmpresa;  |PINTA #dsmoy093#2;
         #dsmoy093#3  = enEmpresa;  |PINTA #dsmoy093#3;

         |C_M #dsmoy093#2, 1, "N";
         |C_M #dsmoy093#3, 1, "N";
     |FINSI;
|FCAL;

|CALCULO Parrilla; |TIPO 0;
     |SI #dsmoy093#2 = 0;
         #dsmoy093#2 = 0;  |PINTA #dsmoy093#2;
         #dsmoy093#3 = 0;  |PINTA #dsmoy093#3;  |C_M #dsmoy093#3, 1, "N";
         |PARA nCampo = 4;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
               |C_M #dsmoy093#nCampo#, 1, "S";
         |SIGUE;
     |SINO;
         |SI enEmpresa = 0;
             |C_M #dsmoy093#2, 1, "S";
             |C_M #dsmoy093#3, 1, "S";
         |FINSI;

         |PARA nCampo = 4;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
               |C_M #dsmoy093#nCampo#, 1, "N";
               #dsmoy093#nCampo# = 0;  |PINTA #dsmoy093#nCampo#;
         |SIGUE;
     |FINSI;
|FCAL;

|CALCULO NoMasEmpresas; |TIPO 0;
     |SI #dsmoy093#Campo# = 0;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
               #dsmoy093#nCampo# = 0;  |C_M #dsmoy093#nCampo#, 1, "N";
         |SIGUE;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
               |C_M #dsmoy093#nCampo#, 1, "S";
         |SIGUE;
     |FINSI;
|FCAL;

|PROCESO AntesOrden; |TIPO 7;
     aAlfa = "S";
     |SI #dsmoy093#2 = 0;
         aAlfa = "N";
         #dsmoy093#Campo# = 1; |PINTA #dsmoy093#Campo#;
     |FINSI;
     |C_M #dsmoy093#Campo#, 1, aAlfa;
|FPRC;

|| *************************************************************************
|PROCESO Actualiza;
       |ABRE #96;
       #96#0 = enEmpresa;
       #96#1 = enEjer;
       #96#2 = enPeriodo;
       #96#3 = enModelo;
       #96#8 = aCampo8;
       |LEE 141#96=;
       |SI FSalida = 0;
           #96#6 = "D";
           |GRABA 020#96a;
       |FINSI;
       |LIBERA #96;
       |CIERRA #96;
|FPRC;

|PROCESO ActualizaTodos;
   |SI nSwHay = 1;

       #96#0 = nEmpresa;
       #96#1 = #dsmoy093#1;
       #96#2 = nPeriodo;
       #96#3 = enModelo;
       #96#8 = aCampo8;
       |LEE 141#96=;
       |SI FSalida ! 0;
           |DDEFECTO #96;
           #96#0 = nEmpresa;
           #96#1 = #dsmoy093#1;
           #96#2 = nPeriodo;
           #96#3 = enModelo;
           #96#8 = aCampo8;
           |GRABA 020#96b;
       |FINSI;

       |SI #96#4 ! #dsmoy093#49; #96#6 = "A"; |FINSI;
       #96#4 = #dsmoy093#49;
       |SI nSwAc = 1; #96#5 = "C"; |FINSI;
       |SI nSwAn = 1; #96#5 = "N"; |FINSI;
       |SI nSwAn = 1; |Y nSwAc = 1; #96#5 = "A"; |FINSI;
       |SI #dsmoy093#31 ! "N"; #96#6 = #dsmoy093#31; |FINSI;

       #96#7 = #dsmoy093#50;
       |GRABA 020#96a;
       |LIBERA #96;

   |SINO;

       #96#0 = nEmpresa;
       #96#1 = #dsmoy093#1;
       #96#2 = nPeriodo;
       #96#3 = enModelo;
       #96#8 = aCampo8;
       |LEE 141#96=;
       |SI FSalida = 0;
           |BORRA 041#96a;
       |FINSI;
       |LIBERA #96;
   |FINSI;
|FPRC;

|| *********************************************
|PROCESO LeeModelo;
     aTipo1 = #349#6 % 101;
     aTipo2 = #349#6 % -101;

     |SI aTipo1 = "R"; |O aTipo1 = "D"; |O aTipo1 = "C"; |ACABA; |FINSI;

     |SI aTipo1 = "M"; |O aTipo1 = "H"; |O aTipo1 = "T";
         aTipo1 = "E";
     |FINSI;

     nSw = 0;
     |SI aTipo1 = "E"; |O aTipo1 = "S";

         |SI aTipo2 = " ";  nSw = 1;  |FINSI; || Este Ejercicio

         |SI aTipo2 = "R";   || comprobar periodo rectificado

             |SI #349#25 = 0; |O #349#25 > #349#1;  ||aqui hay un error
                 #349#25 = #349#1;
             |FINSI;

             |SI #dsmoc349#26 =  0; nPerAnt = 12; |FINSI;
             |SI #dsmoc349#26 =  1; nPerAnt =  3; |FINSI;
             |SI #dsmoc349#26 =  2; nPerAnt =  6; |FINSI;
             |SI #dsmoc349#26 =  3; nPerAnt =  9; |FINSI;
             |SI #dsmoc349#26 =  4; nPerAnt = 12; |FINSI;
             |SI #dsmoc349#26 ] 11; nPerAnt = #dsmoc349#26 - 10; |FINSI;

             |SI #349#25 = #349#1; nSw = 2; |FINSI;
             |SI #349#25 = nAnyo;  nSw = 3; |FINSI;
         |FINSI;
    |FINSI;

    |SI nSw ! 0;
        |SI aTipo2 = " "; nImporte = #349#22; |FINSI;
        |SI aTipo2 = "R"; nImporte = (#349#22 - #349#27); |FINSI;

        |SI nPrim = 1;
            |SI nSw = 1;
                #dsmoy093#66 = #dsmoy093#66 + nImporte;
            |FINSI;

            |SI nSw = 2; |Y #dsmoy093#26 ] nPerAnt;
                nEjerAnt = #dsmoc349#25;
                |HAZ VeonCampo;
                |SI nCampo ! -1;
                    #dsmoy093#nCampo# = #dsmoy093#nCampo# + nImporte;
                |FINSI;
            |FINSI;

            |SI nSw = 3; |Y #dsmoy093#26 [ nPerAnt;
                nEjerAnt = #dsmoc349#25;
                |HAZ VeonCampo;
                |SI nCampo ! -1;
                    #dsmoy093#nCampo# = #dsmoy093#nCampo# + nImporte;
                |FINSI;
            |FINSI;
        |FINSI;

        |SI nPrim = 2;
            |SI nSw = 1;
                #dsmoy093#66 = #dsmoy093#66 + nImporte;
            |FINSI;

            |SI nSw = 2; |Y #dsmoy093#26 [ nPerAnt;
                nEjerAnt = #dsmoc349#25;
                |HAZ VeonCampo;
                |SI nCampo ! -1;
                    #dsmoy093#nCampo# = #dsmoy093#nCampo# + nImporte;
                |FINSI;
            |FINSI;
        |FINSI;

        nSwHay   = 1;
    |FINSI;
|FPRC;

|DEFBUCLE LeeModelo;
   #349#1;
   ;
   #4#0, #4#1, nDPer349, #4#3, #4#4, 01, "  ", 0000, 00, "            ";
   #4#0, #4#1, #4#2,     #4#3, #4#4, 98, "zz", 9999, 99, "zzzzzzzzzzzz";
   e;
   NULL, LeeModelo;
|FIN;

|PROCESO LeeModelos_M;
  nHayModelo = 1;
  nComplem   = #4#4;
  |SI #4#4 > 0; |Y #4#24 = "S";
      #dsmoy093#66 = 0;
  |FINSI;

  |SI #4#22 = "M"; nDPer349 = #4#2;     |FINSI;
  |SI #4#22 = "T"; nDPer349 = #4#2 - 2; |FINSI;

  |BUCLE LeeModelo;
|FPRC;

|DEFBUCLE LeeModelos_M;
  #4#1;
  ;
  nEmpresa, nEjer, nPerLeer, nModelo, 00;
  nEmpresa, nEjer, nPerLeer, nModelo, 99;
  e;
  NULL, LeeModelos_M;
|FIN;

|PROCESO LeeModelos_M004;

  |PARA nPerLeer = nHPeriodo; |SI nPerLeer ] nDPeriodo; |HACIENDO nPerLeer = nPerLeer - 1;
        nHayModelo   = 0;
        #dsmoy093#66 = 0;
        |BUCLE LeeModelos_M;
        |SI nHayModelo = 1;
            nPerAnt  = nPerLeer;
            nEjerAnt = nEjer;
            |HAZ VeonCampo;
            |SI nCampo ! -1;
                #dsmoy093#nCampo# = #dsmoy093#nCampo# + #dsmoy093#66;
            |FINSI;
        |FINSI;
  |SIGUE;
|FPRC;

|| *************************************************************************
|PROCESO VeonCampo;
  nCampo = -1;
  |SI nEjerAnt = #dsmoy093#1;
      |Y  nPerAnt ] #dsmoy093#32; |Y nPerAnt [ #dsmoy093#26;
          nCampo = 61;
  |FINSI;
  |SI nEjerAnt = #dsmoy093#35;
      |Y  nPerAnt ] #dsmoy093#33; |Y nPerAnt [ #dsmoy093#34;
          nCampo = 62;
  |FINSI;
  |SI nEjerAnt = #dsmoy093#38;
      |Y  nPerAnt ] #dsmoy093#36; |Y nPerAnt [ #dsmoy093#37;
          nCampo = 63;
  |FINSI;
  |SI nEjerAnt = #dsmoy093#41;
      |Y  nPerAnt ] #dsmoy093#39; |Y nPerAnt [ #dsmoy093#40;
          nCampo = 64;
  |FINSI;
  |SI nEjerAnt = #dsmoy093#44;
      |Y  nPerAnt ] #dsmoy093#42; |Y nPerAnt [ #dsmoy093#43;
          nCampo = 65;
  |FINSI;
|FPRC;

|PROCESO LeeEstePeriodo;
  |SI nNume1 = 51;
      nDPeriodo = #dsmoy093#32;
      nHPeriodo = #dsmoy093#26;
      nEjer     = #dsmoy093#1;
  |SINO;
      nCampo = (nNume1 - 52) * 3  + 33; nDPeriodo = #dsmoy093#nCampo#;
      nCampo = nCampo + 1;  nHPeriodo = #dsmoy093#nCampo#;
      nCampo = nCampo + 1;  nEjer     = #dsmoy093#nCampo#;
  |FINSI;

  aPeriodo = "X";
  |PARA nPara3 = nDPeriodo; |SI nPara3 [ nHPeriodo; |HACIENDO nPara3 = nPara3 + 1;
        #dsmom004#0 = nEmpresa;
        #dsmom004#1 = nEjer;
        #dsmom004#2 = nPara3;
        #dsmom004#3 = 349;
        #dsmom004#4 = 0;
        |LEE 041#dsmom004.=;
        |SI FSalida = 0;
            aPeriodo = #dsmom004#22;
        |FINSI;
  |SIGUE;

  #dsmoy093#nNume1# = aPeriodo;
  nCampo = nNume1 + 5;
  aAlfa  = "No existe ";
  |SI aPeriodo = "T";
      |SI nHPeriodo = 12; aAlfa = "4"; |FINSI;
      |SI nHPeriodo =  9; aAlfa = "3"; |FINSI;
      |SI nHPeriodo =  6; aAlfa = "2"; |FINSI;
      |SI nHPeriodo =  3; aAlfa = "1"; |FINSI;
      aAlfa = aAlfa + "/Trim. " + nEjer;
  |FINSI;
  |SI aPeriodo = "M";
      |SI nHPeriodo = 12; aAlfa = "Oct/Nov/Dic"; |FINSI;
      |SI nHPeriodo =  9; aAlfa = "Jul/Ago/Sep"; |FINSI;
      |SI nHPeriodo =  6; aAlfa = "Abr/May/Jun"; |FINSI;
      |SI nHPeriodo =  3; aAlfa = "Ene/Feb/Mar"; |FINSI;
      aAlfa = aAlfa + " " + nEjer;
  |FINSI;
  #dsmoy093#nCampo# = aAlfa;
|FPRC;

|PROCESO LeeTodoslosPeriodo;
  |PARA nPara2 = 51; |SI nPara2 [ 55; |HACIENDO nPara2 = nPara2 + 1;
        nNume1 = nPara2; |HAZ LeeEstePeriodo;
  |SIGUE;
|FPRC;
|| //////////////////////////////////////

|PROCESO LEmpresa;

  nEmpresa  = #agifigen#0;

  |PDEFECTO #dsmoy093,46,69;
  #dsmoy093#46 = nEmpresa;
  #dsmoy093#47 = #agifigen#13;
  #dsmoy093#48 = #agifigen#1;

|| ------------------------------------------------------------------------
|| Lee Periodicidad Modelo 349

   nSwHay    = 0;
   |HAZ LeeTodoslosPeriodo;
   |SI #dsmoy093#51 = "X"; |ACABA; |FINSI; ||no tiene modelo

   #dsmoy093#67 = "TRIMESTRAL";
   |SI #dsmoy093#51 = "M";
       #dsmoy093#67 = "MENSUAL   ";
       |SI aParam ! ""; |ACABA; |FINSI;
   |FINSI;

   nPrim     = 1;
   nEjer     = #dsmoy093#1;
   nAnyo     = #dsmoy093#0;
   nDPeriodo = 1;
   nHPeriodo = #dsmoy093#26;
   |HAZ LeeModelos_M004;

   nPrim     = 2;
   nEjer     = #dsmoy093#0;
   nAnyo     = - 1;
   nDPeriodo = #dsmoy093#26;
   nHPeriodo = 12;
   |HAZ LeeModelos_M004;

   nSw    = 0;
   nSwAc  = 0;
   nSwAn  = 0;

   |SI nSwHay = 1;

       |SI #dsmoy093#61 > #dsmoy093#30;  nSw = 1; nSwAc = 1; |FINSI;

       |PARA nCampo = 62; |SI nCampo [ 65; |HACIENDO nCampo = nCampo + 1;
             nImporte = #dsmoy093#nCampo#;
             |SI nImporte > #dsmoy093#30; nSw = 1; nSwAn = 1; |SAL; |FINSI;
       |SIGUE;

       aAlfa = "";
       #dsmoy093#68 = "TRIMESTRAL";

       |SI nSw = 1;
           #dsmoy093#68 = "MENSUAL   ";
           #dsmoy093#49 = "S";
           |SI nSwAc = 1; |Y nSwAn = 0;
               aAlfa = "superado en el Periodo Actual";
           |FINSI;
           |SI nSwAc = 0; |Y nSwAn = 1;
               nCampo1 = nCampo - 5;
               aAlfa = "superado en " + #dsmoy093#nCampo1#;
           |FINSI;
           |SI nSwAc = 1; |Y nSwAn = 1;
               aAlfa = "superado en el Periodo actual y en los anteriores";
           |FINSI;
           #dsmoy093#50= aAlfa;
       |FINSI;

       nElPrint = 0;
       |SI #dsmoy093#24 = "T";                                 nElPrint = 1; |FINSI;
       |SI #dsmoy093#24 = "O"; |Y nSw = 1;                     nElPrint = 1; |FINSI;
       |SI #dsmoy093#24 = "D"; |Y #dsmoy093#67 ! #dsmoy093#68; nElPrint = 1; |FINSI;

       |SI nElPrint = 1;
           |SI aParam = "";
               nSwLinea = 0;
               |SI nSw = 1;                     #dsmoy093#45 = #dsmoy093#45 + 1; |FINSI;
               |SI #dsmoy093#67 ! #dsmoy093#68; #dsmoy093#69 = #dsmoy093#69 + 1; nSwLinea = 1; |FINSI;
               |PRINT;
           |FINSI;
           nPrint = 1;
       |FINSI;
   |FINSI;

   |SI aParam ! "";
       |HAZ ActualizaTodos;
   |SINO;
       enModelo = 303; |HAZ ActualizaTodos;
       enModelo = 349; |HAZ ActualizaTodos;
   |FINSI;
|FPRC;

|DEFBUCLE agifigen;
   #agifigen#1;
   ;
   nDEmpresa;
   nHEmpresa;
   e;
   NULL, LEmpresa;
|FIN;

|DEFBUCLE agifigen_2;
   #agifigen#2;
   0;
   "                                        ", nDEmpresa;
   "zzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzz", nHEmpresa;
   e;
   NULL, LEmpresa;
|FIN;

|DEFBUCLE agifigen_3;
   #agifigen#4;
   0;
   "            ", nDEmpresa;
   "zzzzzzzzzzzz", nHEmpresa;
   e;
   NULL, LEmpresa;
|FIN;

|| \\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\
|PROCESO LosPeriodos;
   nNume1 = 32;
   |PARA nPara2 = 1; |SI nPara2 [ 4; |HACIENDO nPara2 = nPara2 + 1;
         nHPeriodo = nDPeriodo - 1;
         |SI nHPeriodo < 1;
             nEjer     = nEjer - 1;
             nHPeriodo = 12;
        |FINSI;
        nDPeriodo = nHPeriodo - 2;
        nNume1 = nNume1 + 1; #dsmoy093#nNume1# = nDPeriodo;
        nNume1 = nNume1 + 1; #dsmoy093#nNume1# = nHPeriodo;
        nNume1 = nNume1 + 1; #dsmoy093#nNume1# = nEjer;
        |HAZ ElTexto;
   |SIGUE;
|FPRC;

|PROCESO ElTexto;
   aAlfa1 = ""; aAlfa2 = "";
   |SI nHPeriodo = 12; aAlfa1 = " 4o.TRIMESTRE"; aAlfa2 = "Oct/Nov/Dic "; |FINSI;
   |SI nHPeriodo =  9; aAlfa1 = "3er.TRIMESTRE"; aAlfa2 = "Jul/Ago/Sep "; |FINSI;
   |SI nHPeriodo =  6; aAlfa1 = " 2o.TRIMESTRE"; aAlfa2 = "Abr/May/Jun "; |FINSI;
   |SI nHPeriodo =  3; aAlfa1 = "1er.TRIMESTRE"; aAlfa2 = "Ene/Feb/Mar "; |FINSI;

   |SI nPara2 = 1; eaTexto1 = aAlfa1; eaTexto11 = aAlfa2; |FINSI;
   |SI nPara2 = 2; eaTexto2 = aAlfa1; eaTexto12 = aAlfa2; |FINSI;
   |SI nPara2 = 3; eaTexto3 = aAlfa1; eaTexto13 = aAlfa2; |FINSI;
   |SI nPara2 = 4; eaTexto4 = aAlfa1; eaTexto14 = aAlfa2; |FINSI;
|FPRC;

|CALCULO Tipo3;  |TIPO 3;

    #dsmoy093#0 = #dsmoy093#1 - 1;
    nModelo     = #dsmoy093#28;
    nPeriodo    = #dsmoy093#26;
    aAlfa = #dsmoy093#27; |QBF aAlfa;
    #dsmoy093#27 = ("          " + aAlfa) % -110;

|| Proceso Monta los periodos:
    nIncre = 2;
    |SI #dsmoy093#26 = 1; |O #dsmoy093#26 = 4; |O #dsmoy093#26 = 7; |O #dsmoy093#26 = 10;
        nIncre = 0;
    |FINSI;
    |SI #dsmoy093#26 = 2; |O #dsmoy093#26 = 5; |O #dsmoy093#26 = 8; |O #dsmoy093#26 = 11;
        nIncre = 1;
    |FINSI;
    #dsmoy093#32 = #dsmoy093#26 - nIncre;

    nEjer     = #dsmoy093#1;
    nDPeriodo = #dsmoy093#32; |HAZ LosPeriodos;

    #dsmoy093#45 = 0;

    |SI aParam = "";
        |MENAV "0000 Recuerde seleccionar el papel de la impresora en Horizontal";
        |IMPRE 0;
        |SI FSalida ! 0;
            |MENAV "    Proceso Interrumpido";
            |ACABA;
        |FINSI;

        |INFOR informe;
        |SI FSalida ! 0;
            |MENAV "    Error en el Informe";
            |ACABA;
        |FINSI;
    |FINSI;

    nPrint = 0;
    |ABRE #4;
    |ABRE #96;
    |SI #dsmoy093#2 ! 0;
        nDEmpresa = #dsmoy093#2;
        nHEmpresa = #dsmoy093#3;
        |SI #dsmoy093#29 = 1; |BUCLE agifigen;   |FINSI;
        |SI #dsmoy093#29 = 2; |BUCLE agifigen_2; |FINSI;
        |SI #dsmoy093#29 = 3; |BUCLE agifigen_3; |FINSI;
    |SINO;
        |PARA nPara1 = 4;  |SI nPara1 [ 23;  |HACIENDO nPara1 = nPara1 + 1;
              |SI #dsmoy093#nPara1# ! 0;
                  nDEmpresa = #dsmoy093#nPara1#;
                  nHEmpresa = #dsmoy093#nPara1#;
                  |BUCLE agifigen;
              |FINSI;
        |SIGUE;
    |FINSI;
    |CIERRA #96;
    |CIERRA #4;

    |SI aParam = "";
        |SI nPrint = 0;
            |MENAV "     Seleccion Vacia.";
        |SINO;
            |PIEINF;
        |FINSI;
        |FININF;
        |FINIMP;
    |SINO;
        |SI nPrint ! 0;
            |SI #96#4 = "S"; |Y #96#6 = "A";
                |MENAV "0000Obligado a confeccionar el MODELO 349 MENSUAL";
                aAlfa = "0000" + #96#7; |QBF aAlfa;
                |MENAV aAlfa;

                |CONFI "0000NDesactivar aviso ";
                |SI FSalida = 0;
                    |HAZ Actualiza;
                |FINSI;
            |FINSI;
        |FINSI;
    |FINSI;
|FCAL;


|PROGRAMA;
     aParam = PARAMETRO; |QBF aParam;

     aCampo8 = "M";
     |SI aParam ! "";
         |DDEFECTO #dsmoy093;
         #dsmoy093#1  = enEjer;
         #dsmoy093#2  = enEmpresa;
         #dsmoy093#3  = enEmpresa;
         #dsmoy093#24  = "O";
         #dsmoy093#26 = enPeriodo;
         #dsmoy093#31 = "N";
         |HAZ Periodo;

         |HAZ Tipo3;
         |ACABA;
     |FINSI;

     |MANTE #dsmoy093;
|FPRO;
