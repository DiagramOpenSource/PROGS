|FICHEROS;
   dsmom040 #40,MANTE;
   dsmom041 #41,MANTE;
   dsmom042 #42,MANTE;
   dsmom044 #44;
   dsmom045 #45;
   dsmom046 #46,MANTE;
   dsmow146 #946,MANTE;
   dsmom049 #49;
   dsmow179,MANTE;

   dsmom107 #107;

   :/contagen/def/cacuenta #300;
   :/contagen/def/caivaasi #304;
   :/contagen/def/cacuebal #305;
   :/contagen/def/caparbal #306;
   :/contagen/def/caparexp #307;
   :/contagen/def/caagrupa #308;
   :/contagen/def/caepigra #309;
   :/contagen/def/cafanta1 #310;
   :/contagen/def/caclipro #311;
   :/contagen/def/camoauxc #312;
   :/contagen/def/anm00001 #313;
   :/contagen/def/anm00007 #314;
   :/contagen/def/anm00014 #324;
   :/contagen/def/anm00015 #325;
   :/contagen/def/cafinanc #360;
   :/contagen/def/cafinanl #361;

   :/basica/def/agicen14 #114;
   :/basica/def/agifigen #1000;

   imagen41@ #441;
|FIN;

|VARIABLES;
  &nAmordef   = 0;
  &nAmorpro   = 0;

  &eaDElem  = "";
  &eaHElem  = "";
  &enDNEle  = 0;
  &enHNEle  = 0;

  inkey     = 0;
  aAlfa1    = "";
  aAlfa2    = "";
  nFactor   = 0;
  nPorceMin = 0;
  nPorceMax = 0;
  nSwSal    = 0;
  nSwEsta   = 0;
  swModulo  = 0;
  nContenido   = "";
  fFecha       = @;
  fFechaUso    = @;
  fFechaVenta  = @;
  fPriVenta    = @;
  fFinVenta    = @;
  fPriCompra   = @;
  fFinCompra   = @;
  aAlfa        = "";
  nVAnyo       = 0;
  nDAnyo       = 0;
  nHAnyo       = 0;
  nPara1       = 0;
  nLineaV      = 0;

   &esalta    = 0;
   esaltaL   = 0;
   &sw_lin   = 0;
   &errconta = 0;
   &entrada  = 1;
   alfa1     = "";
   alfa2     = "";
   alfa3     = "";
   alfa4     = "";
   nume1     = 0;
   nume2     = 0;
   nume3     = 0;
   nume4     = 0;

   men1 = "2402Cantidad Negativa de Amort. Fiscal";
   men2 = "2443Cantidad Negativa de Amort. Contable";
   men3 = "2402Cantidad Negativa de Amort. Fiscal       Cantidad Negativa de Amort. Contable";
   men  = "";
   any = "";
   fiac = "  ";
   guarda = "  ";
   relleno = "                                       ";
   aponer = "  ";

   import = 0;
   import1 = 0;
   traba = @;
   elcon = 0;

   &aComVen = "";
   &enSwBien = 0;
   &aCamp1 = "";
   &nCamp2 = 0;
   &fCamp3 = @;
   &nCamp4 = 0;
   &nCamp5 = 0;
   &nCamp5C = 0;
   &nCamp5V = 0;
   &aCamp6 = "";
   &aCamp6C= "";
   &aCamp6V= "";
   &nCamp7 = 0;
   &aCamp8 = "";
   &nCamp9 = 0;
   &nCamp10 = 0;
   &aCamp41_8  = "";
   &nCamp41_43 = 0;
   &nCamp41_46 = 0;
   &aCamp41_47 = "";
   &aCamp41_48 = "";
   &aCamp41_49 = "";
   &aCamp41_53 = "";
   &aCamp41_50 = "";
   &nCamp41_51 = 0;
   &nCamp41_52 = 0;
   &nCamp41_67 = 0;
   ModoEntrLin = 0;
   nSwError = 0;

   aLaImpor  = "";
   nLaImpor  = 0;
   nTantoPor = 0;
   nTan      = 0;
   nAlta     = 0;
   nColor    = 0;
   nSwOpera  = 0;
   nTantos   = 0;
   &eaGrAna    = "";
   &enQueAna   = 0;
   &nSwGrupo   = 0;
   &snFilBa    = "";
   nErGrupo    = 0;
   nGrupo      = 0;
   aCtaAcum    = "";
   nEj         = 0;

   &enSwPrim   = 0;
   nUltima     = 0;
   nModoLinea  = 0;
|FIN;

|INCLUYE i_variar;
|INCLUYE i_varcon;  || Variables de Contabilidad
|INCLUYE i_numamo;  || Calculo del Codigo del Elemento de Amortizacion

**************************************************************************
|RUTINA ClaveBaja42;
     #42#0 = enEmpresa;
     #42#2 = #dsmom041#2;
     #42#3 = #dsmom041#3;
     |SI FSalida = "POSICION";
         #42#4 = nUltima;
     |SINO;
         #42#4 = 001;
     |FINSI;
|FRUT;

|RUTINA ClaveAlta42;
     #42#0 = enEmpresa;
     #42#2 = #dsmom041#2;
     #42#3 = #dsmom041#3;
     #42#4 = 999;
|FRUT;

|CALCULO PresentaLinea;  |TIPO 7;

     ModoEntrLin = (FEntrada / 100) ! 0;
     |HAZ Igualom41aw179;

     |SI ModoEntrLin > 1; |HAZ ColorAct; |FINSI;
     nSwSal = 1;
     |HAZ TextoAI;
     |SI enSwAna = 0; |HAZ TextoCtaA; |FINSI;

     nUltima = 1;
     nCamp5C = 1;
     aCamp6C = "COMPRA";
     nCamp5V = 1;
     aCamp6V = "VENTA";

     |ABRE #42;
     |SELECT #2#dsmom042;
     #42#0 = #dsmom041#0;
     #42#2 = #dsmom041#2;
     #42#3 = #dsmom041#3;
     #42#6 = "V";
     #42#9 = "01.01.0000";
     #42#5 = 0000;
     |LEE 041#dsmom042.];
     |SI FSalida = 0; |Y #42#0 = #dsmom041#0; |Y #42#2 = #dsmom041#2;
         |Y #42#3 = #dsmom041#3;
         nCamp5V = #dsmom042#12;
         aCamp6V = #dsmom042#10;
     |FINSI;
     |SELECT #1#dsmom042;

     #42#0 = #dsmom041#0;
     #42#2 = #dsmom041#2;
     #42#3 = #dsmom041#3;
     #42#4 = 1;
     |LEE 001#42=;
     |SI FSalida = 0;
         nCamp5C = #dsmom042#12;
         aCamp6C = #dsmom042#10;
     |FINSI;

     #42#0 = #dsmom041#0;
     #42#2 = #dsmom041#2;
     #42#3 = #dsmom041#3;
     #42#4 = 999;
     |LEE 001#42];
     |SI FSalida = 0;
        |LEE 040#42a;
     |SINO;
         |LEE 040#42u;
     |FINSI;
     |SI FSalida = 0; |Y #42#0 = #41#0; |Y #42#2 = #41#2; |Y #42#3 = #41#3;
         nUltima = #42#4;
     |FINSI;
     |CIERRA #42;
     |SI nUltima [ 3; nUltima = 1; |SINO; nUltima = nUltima - 3; |FINSI;

     nModoLinea = 7;
     |ENTLINEAL #1#42, -4, 7, 22, 0, ClaveBaja42, ClaveAlta42;
     nModoLinea = 0;
|FCAL;

                               || .... Desde Campo Elemento
|CALCULO tracodi; |TIPO 0;
   |C_M #41#3,1,"S";
   esalta = (FEntrada / 100) ! 0;

   |C_M #dsmow179#27, 1, "S";|| fecha compra
   |C_M #dsmow179#35, 1, "S"; || en curso
   |C_M #dsmow179#9 , 1, "S"; || fecha inicio bien
   |SI esalta = 1;
       sw_lin = 0;
       |C_M #dsmow179#10 , 1 , "S";  || Importe
   |SINO;
       |C_M #dsmow179#10 , 1 , "N";
       sw_lin = 1;
       |SI #41#15 ! 0; |O #41#21 ! 0;
           |C_M #dsmow179#27, 1,"N";
           |C_M #dsmow179#35, 1,"N";
           |C_M #dsmow179#9,  1,"N";
       |FINSI;
   |FINSI;

   |SI esalta ! 1;  |ACABA;  |FINSI;

   alfa1 = #41#2; |QBF alfa1; |SI alfa1 = ""; |ERROR; |ACABA; |FINSI;

   |HAZ BuscaNumero;

   #41#27 = ~;
   #41#26 = "01.01.0000";
   #41#9  = "01.01.0000";

   |PINDA #0#41;
   |HAZ PresentaLinea;
|FCAL;

|CALCULO numamo; |TIPO 6;
   x_entra = entrada;
   x_alfa1 = #41Campo;
  |QBF x_alfa1; |SI x_alfa1 = ""; |ERROR; |ACABA; |FINSI;

   x_alfa1 = #41Campo;
   |HAZ x_punto;
   #41Campo = x_alfa1;
   |PINTA #41Campo;
|FCAL;

|PROCESO BuscaNumero;

  |C_M #41#3, 1, "S";
  #41#0 = enEmpresa;
  #41#2 = alfa1;
  #41#3 = 00;
  |LEE 011#41=
  |SI FSalida ! 0;
      |DDEFECTO #41;
      |C_M #41#3, 1, "N";
      #41#0 = enEmpresa;
      #41#2 = alfa1;
      #41#3 = 0;
  |SINO;
      |PARA nume1 = 1;  |SI nume1 [ 99;  |HACIENDO nume1 = nume1 + 1;
            #41#0 = enEmpresa;
            #41#2 = alfa1;
            #41#3 = nume1;
            |LEE 001#41=;
            |SI FSalida ! 0;
                #41#9  = "01.01.0000";
                #41#10 = 0;
                #41#15 = 0;
                #41#16 = 0;
                #41#21 = 0;
                #41#22 = 0;
                #41#26 = "01.01.0000";
                #41#27 = "01.01.0000";
                |SAL;
           |FINSI;
        |SIGUE;
        |SI nume1 = 100; |ERROR; |FINSI;
    |FINSI;
|FPRC;

|| ///////////////////////// Borrar Elemento
|PROCESO LinGrupo;
     |BORRA 140#325a;
     |LIBERA #325;
|FPRC;

|DEFBUCLE LinGrupo;
     #325#1;
     ;
     #324#0, #324#1, #324#2, 000;
     #324#0, #324#1, #324#2, 999;
     be;
     NULL, LinGrupo;
|FIN;

|PROCESO MiraCtaAna;
    |ABRE #324;
    #324#0 = #41#0;
    #324#1 = #41#2;
    #324#2 = #41#3;
    |LEE 101#324=;
    |SI FSalida = 0;
        |BUCLE LinGrupo;
        |BORRA 020#324a;
    |FINSI;
    |LIBERA #324;
    |CIERRA #324;
|FPRC;

|PROCESO BorroHist46;
 |BORRA 020#dsmom046.a;
 |LIBERA #dsmom046;
|FPRC;

|DEFBUCLE borrarHist46;
 #dsmom046#1;
 ;
 enEmpresa, eaDElem, enDNEle, 00, "   ";
 enEmpresa, eaHElem, enHNEle, 99, "zzz";
 be;
 NULL, BorroHist46;
|FIN;

|PROCESO BorroHist45;
 |BORRA 020#dsmom045.a;
 |LIBERA #dsmom045;
|FPRC;

|DEFBUCLE borrarHist45;
 #dsmom045#1;
 ;
 enEmpresa, eaDElem, enDNEle;
 enEmpresa, eaHElem, enHNEle;
 be;
 NULL, BorroHist45;
|FIN;

|PROCESO BorroHistorico;
 |BORRA 020#dsmom044.a;
 |LIBERA #dsmom044;
|FPRC;

|DEFBUCLE borrarHistorico;
 #dsmom044#1;
 ;
 enEmpresa, eaDElem, enDNEle;
 enEmpresa, eaHElem, enHNEle;
 be;
 NULL, BorroHistorico;
|FIN;

|PROCESO Borrolin;
 |BORRA 020#dsmom042.a;
 |LIBERA #dsmom042;
|FPRC;

|DEFBUCLE borrarLineas;
 #dsmom042#1;
 ;
 enEmpresa, eaDElem, enDNEle, 000;
 enEmpresa, eaHElem, enHNEle, 999;
 be;
 NULL, Borrolin;
|FIN;

|PROCESO Borrolin49;
 |BORRA 020#dsmom049.a;
 |LIBERA #dsmom049;
|FPRC;

|DEFBUCLE borrarLineas49;
 #dsmom049#1;
 ;
 enEmpresa, eaDElem, enDNEle, 000;
 enEmpresa, eaHElem, enHNEle, 999;
 be;
 NULL, Borrolin49;
|FIN;

|PROCESO BorrarTodo;
     |BUCLE borrarLineas;
     |BUCLE borrarLineas49;
     |BUCLE borrarHistorico;
     |BUCLE borrarHist45;
     |BUCLE borrarHist46;
|FPRC;

|CALCULO Tipo2_1;  |TIPO 2;
     esalta  = (FEntrada / 100) ! 0;
     |SI esalta ! 3;  |ACABA;  |FINSI;

     |ABRE #44;
     #44#0 = #41#0;
     #44#2 = #41#2;
     #44#3 = #41#3;
     |LEE 041#44=;
     |SI FSalida = 0;
         |BORRA 020#44a;
     |FINSI;
     |CIERRA #44;

     |ABRE #45;
     #45#0 = #41#0;
     #45#2 = #41#2;
     #45#3 = #41#3;
     |LEE 041#45=;
     |SI FSalida = 0;
         |BORRA 020#45a;
     |FINSI;
     |CIERRA #45;

     enEmpresa = #41#0;
     eaDElem   = #41#2;
     eaHElem   = #41#2;
     enDNEle   = #41#3;
     enHNEle   = #41#3;

    |BUCLE borrarLineas;
    |BUCLE borrarLineas49;
    |BUCLE borrarHist46;

    alfa1 = #41#34; alfa1 = alfa1 % 103;
    |SI alfa1 = "GRU";
        |HAZ MiraCtaAna;
    |FINSI;
|FCAL;

|PROCESO Igualom41aw179;
    |PARA nPara1 = 0; |SI nPara1 [ 66; |HACIENDO nPara1 = nPara1 + 1;
          #dsmow179#nPara1# = #dsmom041#nPara1#;
    |SIGUE;
    #dsmow179#67 = #dsmom041#2;
    #dsmow179#68 = #dsmom041#3;
    #dsmow179#69 = #dsmom041#4;
    #dsmow179#70 = #dsmom041#8;
    #dsmow179#71 = #dsmom041#27;
    #dsmow179#72 = #dsmom041#10;
    #dsmow179#73 = #dsmom041#26;
    #dsmow179#78 = #dsmom041#47;
    #dsmow179#79 = #dsmom041#67;
    |SI #dsmow179#55 = 0; #dsmow179#55 = 29; |FINSI;
    |SI #dsmow179#57 = 0; #dsmow179#57 = 1;  |FINSI;
|FPRC;

|PROCESO Igualow179am41;
    |PARA nPara1 = 0; |SI nPara1 [ 66; |HACIENDO nPara1 = nPara1 + 1;
          #dsmom041#nPara1# = #dsmow179#nPara1#;
          |PINTA #dsmom041#nPara1#;
    |SIGUE;
    #dsmom041#67 = #dsmow179#79;

    |SI ModoEntrLin > 1; |HAZ ColorAct; |FINSI;
    |HAZ TextoAI;
    |SI enSwAna = 0; |HAZ TextoCtaA; |FINSI;
|FPRC;

|CALCULO elTipo3; |TIPO 3;
    |HAZ Igualom41aw179;
    #41#32 = #agifigen#1; ||A todas las cabeceras Nombre empresa
    #41#33 = #agicen14#4;  ||                     Actividad

    |PUSHV 0501 2380;
    |PINPA #0#dsmow179;
    |POPV;
    |PUSHV 0501 2380;
    |PINDA #0#dsmow179;
    |ENDATOS #1#dsmow179;

    |SI FSalida = 0;
        |POPV;
        |HAZ Igualow179am41;
    |SINO;
        |POPV;
        |ERROR;
        |ACABA;
    |FINSI;

    nSwSal = 0;
    |HAZ CalculaCompra; ||Linea de Compra
    |SI esalta = 1;
        #dsmom041#16 = #dsmom041#10; |PINTA #dsmom041#16;
        #dsmom041#22 = #dsmom041#10; |PINTA #dsmom041#22;
        |SI #dsmom041#13 ! "M"; |O #dsmom041#19 ! "M";
            |HAZ CalculaAmort;
            |PINTA #41#15; |PINTA #41#16;
            |PINTA #41#21; |PINTA #41#22;
        |FINSI;
    |FINSI;
    |HAZ CalculaVenta; ||Linea de Venta ||  nou

    |ENTLINEAL #1#42, -4, 2, 22, 1, ClaveBaja42, ClaveAlta42;
    |LIBERA #41;
    enSwPrim   = 0;

    |SI #dsmom041#41 = "N";
        enEmpresa = #41#0;
        eaDElem   = #41#2;
        eaHElem   = #41#2;
        enDNEle   = #41#3;
        enHNEle   = #41#3;
        |BUCLE borrarLineas49;
    |FINSI;
|FCAL;

|PROCESO inf441;
 #441#0 = enEmpresa;
 #441#4 = "";
|FPRC;

|PROCESO sup441;
 #441#0 = enEmpresa;
 #441#4 = "zzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzz";
|FPRC;

|RUTINA ClauBaja49;
     #49#0 = #dsmow179#0;
     #49#2 = #dsmow179#2;
     #49#3 = #dsmow179#3;
     #49#4 = 01;
|FRUT;

|RUTINA ClauAlta49;
     #49#0 = #dsmow179#0;
     #49#2 = #dsmow179#2;
     #49#3 = #dsmow179#3;
     #49#4 = 99;
|FRUT;

|CALCULO T11; |TIPO 11;
  inkey = FSalida;
  |SI inkey = 12;
      enEmpresa = #41#0;
      eaDElem   = #41#2;
      eaHElem   = #41#2;
      enDNEle   = #41#3;
      enHNEle   = #41#3;
      |SUB_EJECUTA_NP "dsmoy041";
      |ERROR;
  |FINSI;

  |SI inkey = 10;
      enEmpresa  = #41#0;
      |DBASE aAlfa1; |QBF aAlfa1;
      aAlfa2 = aAlfa1 + "def/dsmom041.mas";
      |CARGA_DEF aAlfa2, imagen41@;
      |SI FSalida = 0;
          |ABRE #441;
          #441#0 = #41#0;
          #441#2 = #41#2;
          #441#3 = #41#3;
          #441#4 = #41#4;
          |CONSULTA_F_CLAVE #2#441,inf441,sup441;
          #41#0 = #441#0;
          #41#2 = #441#2;
          #41#3 = #441#3;
          |CIERRA #441;
          |DESCARGA_DEF #imagen41@;
          |PTEC 802;
      |FINSI;
  |FINSI;

  esalta = (FEntrada / 100) ! 0;
  |SI inkey = 11; |Y esalta > 1;
      |HAZ Igualom41aw179;

      |PUSHV 0501 2380;
      |PINPA #1#dsmow179;
      |HAZ LaSuma;
      |PINDA #1#dsmow179;
      |ENTLINEAL #1#49, -4, 7, 22, 0, ClauBaja49, ClauAlta49;
      |PAUSA;
      |POPV;
      |ERROR;
  |FINSI;
|FCAL;

|| ***************************************************************************
||   Calculos de las Lineas de Amortizacion

|CALCULO Ptec816; |TIPO 7;
 |PTEC 816;
|FCAL;

|CALCULO SalgoYa;  |TIPO 7;
     |SI nSwSal = 1;  |PTEC 807;  |FINSI;
|FCAL;

|CALCULO Tipo2_2;  |TIPO 2;
   esaltaL = (FEntrada / 100) ! 0;
   |SI #42#6 = "A";
       #41#15 = #41#15 +. #42#7;
       #41#21 = #41#21 +. #42#11;
       #41#16 = #41#16 -. #42#7;
       #41#22 = #41#22 -. #42#11;
       |SI #41#26 > "01.01.0000";
           |SI #42#9 ] #41#26;
               #41#16 = 0;
               #41#22 = 0;
           |FINSI;
       |FINSI;
    |FINSI;

    |SI esaltaL = 3;
        |SI #42#6 ! "C"; |Y #42#9 ] #41#26;
            #41#16 = #41#10 - #41#15;
            #41#22 = #41#10 - #41#21;
        |FINSI;
    |FINSI;

    |SI #42#6 = "C";
        #41#10 = #41#10 +. #42#7;
        #41#16 = #41#16 +. #42#7;
        #41#22 = #41#22 +. #42#7;
    |FINSI;

    |SI #42#6 = "V";
        #41#67 = #41#67 +. #42#7;
        #41#52 = (#41#67 % #41#51) ! 2;
    |FINSI;

    |PINTA #41#27;
    |PINTA #41#9;
    |PINTA #41#10;
    |PINTA #41#26;
    |PINTA #41#15;
    |PINTA #41#16;
    |PINTA #41#21;
    |PINTA #41#22;
    |HAZ Igualom41aw179;
|FCAL;

|CALCULO baja; |TIPO 1;
   |SI esalta = 3; |ACABA; |FINSI;

   esaltaL = (FEntrada / 100) ! 0;
   elcon = 0;
   |SI #42#4 = 1; |Y esaltaL = 3;
      |MENAV "0000*** NO PUEDE DAR DE BAJA ESTA LINEA ***";
      |ERROR;
      elcon = 1;
      |ACABA;
   |FINSI;

   |SI #42#8 = "S";
      |AVISO;
      |CONFI "2400N** Linea Contabilizada **. Continuar (S/N) ";
      |SI FSalida ! 0;
         |ERROR;
         elcon = 1;
         |ACABA;
      |FINSI;
   |FINSI;

   |SI #42#6 = "V";
       #41#26 = "01.01.0000"; |PINTA #41#26;
       #41#40 = "A";          |PINTA #41#40;
       #41#58 = 0;            |PINTA #41#58;
       #41#50 = "";
       #41#51 = 0;
       #41#52 = 0;
       #41#67 = 0;
       #41#64 = 0;
       #41#65 = 0;
       #41#66 = "";
       #41#67 = 0;
       |HAZ Igualom41aw179;
       |HAZ TextoAI;
   |FINSI;
|FCAL;

|CALCULO fecven; |TIPO 3;
   |BLIN 24;
   |SI #42#6 = "V";
       #41#26 = #42#9; |PINTA #41#26;
       #41#40 = "I";   |PINTA #41#40;
       #41#58 = 2;     |PINTA #41#58;
       |HAZ Igualom41aw179;
       |HAZ TextoAI;
   |FINSI;

   |SI #42#6 = "C";
       |SI #42#4 = 1;
           #41#27 = #42#9; |PINTA #41#27;
           |SI #41#27 > #41#9; |Y #41#35 = "N";
               #41#9 = #41#27; |PINTA #41#9;
           |FINSI;
           #42#11 = #42#7; |PINTA #42#11;
       |SINO;
           |SI #41#35 = "N";
               |SI #41#9 < #42#9; #41#9 = #42#9; |PINTA #42#9; |FINSI;
           |FINSI;
       |FINSI;
       |HAZ Igualom41aw179;
   |FINSI;

|FCAL;

|CALCULO lin_imp; |TIPO 0;
   esaltaL = (FEntrada / 100) ! 0;
   |SI esaltaL = 1;
      |SI #41#16 = 0; |Y #41#22 = 0;
         |SI #41#26 > "01.01.0000";
            |SI #41#58 = 2;
                |MENAV "0000Amortizacion Finalizada y Elemento VENDIDO";
            |SINO;
                |MENAV "0000Amortizacion Finalizada";
            |FINSI;
            |ERROR;
         |SINO;
            |MENAV "0000Amortizacion Finalizada. Solo se puede Vender";
            #42#6 = "V";
            |PINTA #42#6;
         |FINSI;
      |FINSI;
   |FINSI;
|FCAL;

             || *** IMPORTES DE LA AMORTIZACION
|CALCULO impor; |TIPO 0;
   |C_M #dsmom042#7, 0, aAlfa;
   |SI aAlfa = "N"; |ACABA; |FINSI;

   |SI #42#6 = "V"; |O #42#6 = "C";
       #42#11 = #42#7;
       |PINTA #42#11;
       |ACABA;
   |FINSI;

   |SI #42#7 = 0;  |Y #41#16 ! 0;
      |MENAV "0000********* El Importe debe ser Diferente de CERO *********";
      ||  |ERROR;
      |ACABA;
   |FINSI;

   import1 = (#41#15 + #42#7) ! EURODBMOD;
   import  = (#41#10 - import1) ! EURODBMOD;
   |SI #41#10 > 0;
      |SI import < 0;
         |MENAV "0000****La Amortizacion Fiscal supera el importe de la compra";
         |ERROR;
         |ACABA;
      |FINSI;
   |SINO;
      |SI import > 0;
         |MENAV "0000****La Amortizacion Fiscal supera el importe de la compra";
         |ERROR;
         |ACABA;
      |FINSI;
   |FINSI;
   |HAZ ladif;
|FCAL;

|CALCULO ladif; |TIPO 7;
   #42#13 = #42#11 -  #42#7;
   |PINTA #42#13;
|FCAL;

|CALCULO vert7; |TIPO 7;
 |SI #42#6 ! "A";
     |SI #41#36 = 0;
         |MENSA "    <F3> consultar Factura de Compra o Venta ";
     |SINO;
         |MENSA "    <F3> consultar Operacion Financiera";
     |FINSI;
 |SINO;
     |MENSA "     ";
 |FINSI;
|FCAL;

|CALCULO lacons_Linea; |TIPO 11;
 inkey = FSalida;
 |SI inkey = 11;
     |HAZ laconsulta;
     |ERROR;
 |FINSI;
|FCAL;

|CALCULO laconsulta; |TIPO 0;
 inkey = FSalida;
 |SI #42#6 = "A"; |ACABA; |FINSI;

 |SI inkey ! 11; |ACABA; |FINSI;

 |SI #41#36 = 0;
     |HAZ ConsulFacturaL;
 |SINO;
     |HAZ ConsulOpera;
 |FINSI;
|FCAL;

|PROCESO ConsulFacturaL
     aComVen = #42#6; ||Compra o venta
     enEmpresa   = #41#0;
     enActividad = #41#1;
     enEjer      = #42#5;
     eaElemento  = #41#2;
     enElemeNum  = #41#3;
     aCamp1      = #41#4;
     nCamp2      = #42#4;  || linea
     fCamp3      = #42#9;
     nCamp4      = #42#7;
     nCamp5      = #42#12;
     aCamp6      = #42#10;
     nCamp7      = #42#14;
     aCamp8      = #42#15;
     nCamp9      = #42#16;
     |SI aComVen  = "C"; nCamp10 = #41#60; |FINSI;
     |SI aComVen  = "V"; nCamp10 = #41#64; |FINSI;

     enSwBien = -1;
     nCamp41_43 = #41#43;
     |SUB_EJECUTA_NP "dsmoz038";
|FPRC;

|CALCULO impor1; |TIPO 0;
   |SI #42#6 ! "A"; |ACABA;  |FINSI;

   |SI #42#11 = 0;  |Y #41#22 ! 0;
      |MENAV "0000********* El Importe debe ser Diferente de CERO *********";
      ||  |ERROR;
      |ACABA;
   |FINSI;

   import1 = (#41#21 + #42#11) ! EURODBMOD;
    import = (#41#10 - import1) ! EURODBMOD;

   |SI #41#10 > 0;
      |SI import < 0;
         |MENAV "0000****La Amortizacion Contable supera el importe de la compra";
         |ERROR;
         |ACABA;
      |FINSI;
   |SINO;
      |SI import > 0;
         |MENAV "0000****La Amortizacion Contable supera el importe de la compra";
         |ERROR;
         |ACABA;
      |FINSI;
   |FINSI;

   |SI #dsmom041#28 ! 0;
       #dsmom042#7 = #dsmom042#11;
       |PINTA #dsmom042#7;
   |FINSI;
   |HAZ ladif;
|FCAL;

|CALCULO cal_any; |TIPO 0;
   traba = #42#9;
   any = traba % 704;
   #42#5 = any;
|FCAL;

|CALCULO elTipoLinea; |TIPO 0;
   aAlfa = Contenido;

   esaltaL = (FEntrada / 100) ! 0;
   |SI esaltaL = 1; |Y #42Campo = "C";
      |SI #41#15 ! 0;  |O #41#21 ! 0;
         |MENAV "0000Opcion de Compra NO permitida, se esta Amortizando";
         |ERROR;
         |ACABA;
      |FINSI;
   |FINSI;

   |SI #42#4 = 1; |Y #42Campo ! "C";
      |ERROR;
      |ACABA;
   |FINSI;

   |SI #42Campo = "V";  |Y esaltaL = 1;
       |MENAV "0000Informe la venta en los datos de cabecera";
       |ERROR;
       |ACABA;
   |FINSI;

   |SI #42Campo ! aAlfa; |Y esaltaL ! 1;
       |MENAV "0000No puede modificar tipo de Linea";
       #42Campo = aAlfa; |PINTA #42Campo;
   |FINSI;

   |C_M #42#7,  1, "S";
   |C_M #42#11, 1, "S";
   |SI #42Campo = "V";  |O #42Campo = "C";
       |C_M #42#11, 1, "N";
       #42#11 = 0; |PINTA #42#11;
   |SINO;
       |SI #dsmom041#28 ! 0;
           |C_M #42#7, 1, "N";
       |FINSI;
   |FINSI;
|FCAL;
|| **********************************************************************

|PROCESO EntraAlaLinea;  |TIPO 5;

  |SI enModo = 7;
      |SALVA_DATOS #41;
      #41#0 = enEmpresa;
      #41#2 = "        ";
      #41#3 = 00;
      |LEE 000#41];
      |SI FSalida ! 0; |O #41#0 ! enEmpresa;
          |DDEFECTO #41;
      |FINSI;
      |PINDA #0#41;
      |REPON_DATOS #41;
  |FINSI;

  |SI enModo ! 2; |ACABA; |FINSI;

/*
  #dsmow179#27 = #dsmom041#27;
  |HAZ LaConta;
*/
  || Lee Empresa
    |ABRE #1000;
    #1000#0 = enEmpresa;
    |LEE 041#1000=;
    |SI FSalida ! 0;
        |MENAV "     Empresa Inexistente";
        |CIERRA #1000;
        |ACABA;
    |FINSI;
    #41#32 = #1000#1;
    enTpEm = #1000#87;

    |CIERRA #1000;
    |HAZ Analitica;
|FPRC;

|| ***************************************************************************
|PROCESO LaConta11;
  |SI #41#27 > "01.01.0000";
      aAlfa1 = #41#27; aAlfa1 = aAlfa1 % -104;
      enEjer = aAlfa1;
  |FINSI;
  enConcreto = 1;
  enPerConta = -1;
  enSwSinMen = 1;
  |HAZ SituaContagen;
  enConcreto = 0;
  |SI swerror = 1;
     |HAZ SituaMaestra;
     |SI swerror = 1;
          |MENAV "    Atencion, no existe Contabilidad ni de esta Empresa ni de Empresa Maestra";
          errconta = 1;
          |PTEC 807;
          |ACABA;
     |FINSI;
  |FINSI;
  |PATH_DAT #300 #aPathConta#; |PATH_PAN #300 #aPathPanConta#;
  |PATH_DAT #304 #aPathConta#; |PATH_PAN #304 #aPathPanConta#;
  |PATH_DAT #305 #aPathConta#; |PATH_PAN #305 #aPathPanConta#;
  |PATH_DAT #306 #aPathConta#; |PATH_PAN #306 #aPathPanConta#;
  |PATH_DAT #307 #aPathConta#; |PATH_PAN #307 #aPathPanConta#;
  |PATH_DAT #308 #aPathConta#; |PATH_PAN #308 #aPathPanConta#;
  |PATH_DAT #309 #aPathConta#; |PATH_PAN #309 #aPathPanConta#;
                               |PATH_PAN #310 #aPathPanConta#;
  |PATH_DAT #311 #aPathConta#; |PATH_PAN #311 #aPathPanConta#;
  |PATH_DAT #312 #aPathConta#; |PATH_PAN #312 #aPathPanConta#;
  |PATH_DAT #313 #aPathConta#; |PATH_PAN #313 #aPathPanConta#;
  |PATH_DAT #314 #aPathConta#; |PATH_PAN #314 #aPathPanConta#;
  |ABRE #304;
  |DDEFECTO #304;
  |LEE 001#304p;
  |CIERRA #304;
  snFilBa = #304#119;

  eaSubde = eaSubdeT;
  enSubde = enSubdeT;
  |HAZ AntCambioCta;
|FPRC;

|| ***************************************************************************
|PROCESO CalculaAmort;

  |SI #41#21 ! 0;  |O #41#15 ! 0; |ACABA;  |FINSI;

  |SI #41#9 [ "01.01.1900"; |ACABA; |FINSI;
  |SI #41#35 = "S"; |ACABA; |FINSI;
  |SI #41#40 = "I"; |ACABA; |FINSI;

  fFechaUso   = #41#9;
  fFechaVenta = #41#26;
  aAlfa  = "31.12." + (fFechaUso % -104);     fFinCompra = aAlfa;
  aAlfa  = "01.01." + (fFechaUso % -104);     fPriCompra = aAlfa;

  aAlfa  = fFechaVenta % -104;                nVAnyo    = aAlfa;
  aAlfa  = "01.01." + (fFechaVenta % -104);   fPriVenta = aAlfa;
  aAlfa  = "31.12." + (fFechaVenta % -104);   fFinVenta = aAlfa;

  |SI fPriVenta < fFechaUso;                  fPriVenta = fFechaUso;  |FINSI;

  aAlfa       = fFechaUso % -104;
  nDAnyo      = aAlfa;
  fFecha      = ~;
  aAlfa       = fFecha % -104;
  nHAnyo      = aAlfa;
  nHAnyo      = nHAnyo - 1;

  |PARA nAnyAmor = nDAnyo;  |SI nAnyAmor [ nHAnyo;  |HACIENDO nAnyAmor = nAnyAmor + 1;
        |SI nAnyAmor > nVAnyo;  |Y nVAnyo ! 0;  |SAL;  |FINSI;

           enEmpresa   = #41#0;
           enActividad = #41#1;
           eaElemento  = #41#2;
           enElemeNum  = #41#3;
           aAlfa       = nAnyAmor;
           aAlfa       = "31.12."+ aAlfa;
           |SI nAnyAmor = nVAnyo; |Y nVAnyo ! 0; |Y #107#18 = "S";
               aAlfa = #41#26;
           |FINSI;
           fFechaAmor =  aAlfa;
           nLineaAmor = 0;
           nConcpAmor = 97;
           aConcpAmor = "AMORTIZACION A¤O: " + nAnyAmor;

           |HAZ CalculaAmor;
           |SI swAmBuena = 0;
               eSWGrabar = 0;
               nLineaAmor = ultlin + 1;
               |HAZ GrabaAmor;
               |SI #42#7 ! 0; |O #42#11 ! 0;
                   #41#0 = enEmpresa
                   #41#1 = enActividad
                   #41#2 = eaElemento
                   #41#3 = enElemeNum
                   |LEE 110#41=;
                   |PINDA #0#41;
               |FINSI;
           |FINSI;
     |SIGUE;
   |HAZ Igualom41aw179;
|FPRC;

|PROCESO BuscaM42V;
  nLineaAmor = #dsmom042#4;
  |SI #dsmom042#6 = "V";
      nLineaV = #dsmom042#4;
  |FINSI;
|FPRC;

|DEFBUCLE BuscaM42V;
  #dsmom042#1;
  ;
  #dsmom041#0, #dsmom041#2, #dsmom041#3, 001;
  #dsmom041#0, #dsmom041#2, #dsmom041#3, 999;
  e;
  NULL, BuscaM42V;
|FIN;

|PROCESO CalculaVenta;
   |SI #dsmom041#58 = 2;
       nLineaV    = 0;
       nLineaAmor = 0;
       |BUCLE BuscaM42V;
       |SI nLineaV = 0;
           nLineaV = nLineaAmor + 1;
       |FINSI;
       |ABRE #dsmom042;
       #dsmom042#0 = #dsmom041#0
       #dsmom042#2 = #dsmom041#2
       #dsmom042#3 = #dsmom041#3
       #dsmom042#4 = nLineaV;
       |LEE 141#dsmom042.=;
       |SI FSalida ! 0;
           |DDEFECTO #dsmom042;
           #dsmom042#0 = #dsmom041#0
           #dsmom042#1 = #dsmom041#1
           #dsmom042#2 = #dsmom041#2
           #dsmom042#3 = #dsmom041#3
           #dsmom042#4 = nLineaV;
           #dsmom042#6 = "V";
           |GRABA 020#dsmom042.b;
       |FINSI;
       #dsmom042#10 = aCamp6V;
       #dsmom042#12 = nCamp5V;
       any = #dsmom041#26 % 704;
       #dsmom042#5  = any
       #dsmom042#7  = #dsmom041#67;
       #dsmom042#8  = "S";
       #dsmom042#9  = #dsmom041#26;
       #dsmom042#11 = #dsmom041#67;
       #dsmom042#13 = 0;
       #dsmom042#14 = #dsmom041#63;
       #dsmom042#15 = #dsmom041#65;
       #dsmom042#16 = #dsmom041#66;
       #dsmom042#17 = #dsmom041#36;
       #dsmom042#18 = #dsmom041#54;
       |GRABA 020#dsmom042.a;
       |LIBERA #dsmom042;
       |CIERRA #dsmom042;
   |FINSI;
|FPRC;

|PROCESO CalculaCompra;
   || ........ ......    el PROCESO prim_lin
      |ABRE #dsmom042;
      #dsmom042#0 = #dsmom041#0;
      #dsmom042#1 = #dsmom041#1;
      #dsmom042#2 = #dsmom041#2;
      #dsmom042#3 = #dsmom041#3;
      #dsmom042#4 = 1;
      |LEE 041#dsmom042.=;
      sw_lin = FSalida;

      any = #dsmom041#27 % 704;
      #dsmom042#5 = any;
      #dsmom042#6 = "C";
      #dsmom042#7 = #dsmom041#10;
      #dsmom042#8 = "S";
      #dsmom042#9 = #dsmom041#27;
      #dsmom042#10 = aCamp6C;
      #dsmom042#11 = #dsmom041#10;
      #dsmom042#12 = nCamp5C;
      #dsmom042#13 = 0;
      #dsmom042#14 = #dsmom041#59;
      #dsmom042#15 = #dsmom041#61;
      #dsmom042#16 = #dsmom041#62;
      #dsmom042#18 = #dsmom041#54;
      #dsmom042#17 = "N";
      |SI #dsmom041#36 ! 0;
          #dsmom042#10 = "COMPRA POR LEASING";
          #dsmom042#16 = #dsmom041#36;
          #dsmom042#17 = "S";
      |FINSI;
      |SI sw_lin = 0; |GRABA 041#dsmom042.a; |FINSI;
      |SI sw_lin ! 0; |GRABA 041#dsmom042.n; |FINSI;
      |CIERRA #dsmom042;

|FPRC;
