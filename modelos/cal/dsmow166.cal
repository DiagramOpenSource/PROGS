|FICHEROS;
     dsmow166,MANTE;

     dsmom107;

     dsmow084,MANTE;

     :/integral/def/dsam0116;
     :/integral/def/dsam0009,MANTE;

     &clientes@;
     espejo1@;
|FIN;

|VARIABLES;
     aAlfa      = "";
     aPathPdf   = "";
     aAbre      = "";
     aOrigen    = "";
     aDestino   = "";
     aIPRemoto  = "";
     aDirRemoto = "";
     aBase      = "";

     nCambio     = 0;
     nContador  = 0;
     nSinDatos  = 0;
     nLinea     = 0;

     nBoton2    = -1;
     nBoton3    = -1;
     nBoton6    = -1;
     nBoton7    = -1;
     nBoton8    = -1;
     nBoton9    = -1;
     nBoton10   = -1;
     nBoton12   = -1;
     nBoton17   = -1;
     nBoton22   = -1;

     nSwBoton2  = 0;
     nSwBoton3  = 0;
     nSwBoton6  = 0;
     nSwBoton7  = 0;
     nSwBoton8  = 0;
     nSwBoton9  = 0;
     nSwBoton10 = 0;
     nSwBoton12 = 0;
     nSwBoton17 = 0;
     nSwBoton22 = 0;

     nTecla     = 0;
     nModelo    = 0;
     nHandle    = 0;
     nUltimo    = 0;
     nCanal     = 0;
     nNume      = 0;
     nDPeriodo  = 0;
     nHPeriodo  = 0;
     nCampo1    = 0;
     nCampo2    = 0;

     aPathIntegral = "";
     aJustiModelo  = "";
     aElectModelo  = "";
     nEjer         = 0;
     nAnual        = 0;

  {10}aPopup       = "";

     &enDEmpresa = 0;
     &enHEmpresa = 0;
     &enDEjer    = 0;
     &enHEjer    = 0;
     &enDModelo  = 0;
     &enHModelo  = 0;
     &eaPathAcrobat = "";

     &enArchi = 0;
     &aCm9  = "";
     &aCm11 = "";
     &aCm13 = "";
     &eaCamp4_9  = "";
     &efCamp4_10 = @;
     &eaCamp4_19 = "";
     &eaCamp4_25 = "";
     &enInfComp  = 0;
     aMas        = "";
     nBaja        = 0;
     &enDesdeAlta = 0;
     &enSw999     = 0;
|FIN;

|INCLUYE i_variar;

|PROCESO EntraMod182;
      enSw999  = 0;
      |SUB_EJECUTA_NP "dsm182ca";
      |SI enSw999 = 1;
          |PTEC 825;
          |PTEC 802;
          |PTEC 806;
          |PTEC 806;
      |FINSI;
|FPRC;

|PROCESO Tipo12;  |TIPO 12;
|FPRC;


|| \\\\\ Alta o Modificacion Modelos ////////
|PROCESO Tipo0C2Fw84; |TIPO 0;
     |C_M #dsmow084#2, 0, aAlfa;
     |SI aAlfa = "N"; |ACABA; |FINSI;

     aAlfa = Contenido;

     |SI #dsmow084#2 = "C"; |Y aAlfa ! "C";
         |CONFI "0000NEsta seguro de hacer una complementaria";
         |SI FSalida ! 0;
             #dsmow084#2 = "S"; |PINTA #dsmow084#2;
             |ERROR;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO PideJustiCoSAnual;
     nAnual = 1;
     |PUSHV 0501 2380;

     aAlfa = #dsmow166#21 + #dsmow166#22;  |QBT aAlfa;
     |SI aAlfa = "";
         #dsmow166#22 = aJustiModelo;
         #dsmow166#21 = aElectModelo;
     |FINSI;

     |SI #dsmow166#19 = " "; #dsmow166#19 = "S"; |FINSI;

     #dsmow166#18 = #dsmow166#22;
     #dsmow084#0 = #dsmow166#21;
     #dsmow084#1 = #dsmow166#22;
     #dsmow084#2 = #dsmow166#19;

     |C_V #dsmow084#0, 1, "N";
     |C_V #dsmow084#2, 1, "S";

     aAlfa = "S";
     |SI nTecla = 11;  aAlfa = "N";  |FINSI;
     |C_M #dsmow084#2, 1, aAlfa;

     |PINPA #0#dsmow084;
     |SI #dsmow166#1 ] 2018;
         |PINTA 1317, "                                                              ";
         |CUADRO 1016 1778;
         |ATRI S;
         |PINTA 1117, " Las complementarias  solo recogeran registros no declarados ";
         |PINTA 1217, " previamente. Si  ha modificado  importes  de algun registro ";
         |PINTA 1317, " anterior, es aconsejable presentar una sustitutiva.         ";
         |ATRI 0;
     |FINSI;
     |PINTA 1417, "                                           ";
     |PINTA 1517, "Complementaria o Sustitutiva ........... : ";
     |C_P #dsmow084#2, 1, 1560;

     |PINDA #0#dsmow084;
     |ENDATOS #1#dsmow084;
     |SI FSalida = 0;
         #dsmow166#21 = #dsmow084#0;
         #dsmow166#22 = #dsmow084#1;
         #dsmow166#19 = #dsmow084#2;
     |FINSI;
     |POPV;
     |C_P #dsmow084#2, 1, 1460;
     nAnual = 0;
|FPRC;

|PROCESO BorraFicPlano;
     eaPathInternet = eaUnidad + "/AEAT";                             |RMKDIR eaPathInternet;
     eaPathInternet = eaPathInternet + "/INTERNET";                   |RMKDIR eaPathInternet;
     eaPathInternet = eaPathInternet + "/" + #dsmow166#3;             |RMKDIR eaPathInternet;
     eaPathInternet = eaPathInternet + "/" + #dsmow166#1;             |RMKDIR eaPathInternet;
     eaPathInternet = eaPathInternet + "/";

     aAlfa = eaPathInternet + "182" + (("00000" + #dsmow166#0) % -105) + ".TXT";
     |RFBORRA aAlfa;
|FPRC;

|PROCESO AltaModelo;
     |SI #dsmow166#4 > 0;
         |LEE 101#dsmow166.=;

||         |HAZ PideJustiCoSAnual;

         |GRABA 020#dsmow166.a;
         |LIBERA #dsmow166;
     |FINSI;

     |SI #dsmow166#16 = "X";
         |MENAV "     Modelo ya Archivado. No se puede Modificar";
         |ACABA;
     |FINSI;

     |SI #dsmow166#11 = "X";
         |CONFI "0000NModelo ya impreso, ¨desea continuar?. El modelo se quedara no impreso";
         |SI FSalida ! 0;
             |ACABA;
         |FINSI;
         |HAZ BorraFicPlano;
     |FINSI;

     enTipoEntrada = 1;
     |HAZ EntraMod182;

     |LEE 101#dsmow166.=;
     |SI enEntrada = 1;
         #dsmow166#9  = "X";
         #dsmow166#10 = ~;
         #dsmow166#11 = " ";
         #dsmow166#12 = "01.01.0000";

         |GRABA 020#dsmow166.a;
     |FINSI;
     |LIBERA #dsmow166;
|FPRC;

|PROCESO BuscoAnterior;
     nBaja = 1;
|FPRC;

|DEFBUCLE BuscoAnterior;
   #espejo1@#1005;
   ;
   #dsmow166#0, 0000,  99, 182, 00;
   #dsmow166#0, nEjer, 99, 182, 99;
   e;
   NULL, BuscoAnterior;
|FIN;


|PROCESO TraeModelo;

     nBaja = 0;
     |DBASE aBase; |QBF aBase;
     aMas  = aBase + "def/dsm182ca.mas";
     |CARGA_DEF aMas, espejo1@;
     |SI FSalida = 0;
         nEjer = #dsmow166#1 - 1;
         |BUCLE BuscoAnterior;
         |DESCARGA_DEF #espejo1@;
     |FINSI;

     |SI nBaja = 0;
         |SI enDesdeAlta = 0;
             |MENAV "    No existe ninguna declaracion Anterior";
             |ACABA;
         |SINO;
             |HAZ AltaModelo;
             enDesdeAlta = 0;
             |ACABA;
         |FINSI;
     |FINSI;

     |SI #dsmow166#16 = "X";
         |MENAV "     Modelo ya Archivado. No se puede Recoger Datos";
         |ACABA;
     |FINSI;

     |SI #dsmow166#11 = "X";
         |MENAV "     Modelo ya impreso. Para recoger datos hay que darlo de Baja primero.";
         |ACABA;
     |FINSI;

     |CONFI "0000SDesea Realizar el Pase desde la ultima declaracion informada";
     |SI FSalida = 0;
         enTipoEntrada = 93;
         |HAZ EntraMod182;
         |LEE 101#dsmow166.=;
         |SI enEntrada = 1;
             #dsmow166#9  = "X";
             #dsmow166#10 = ~;
             #dsmow166#11 = " ";
             #dsmow166#12 = "01.01.0000";
             |GRABA 020#dsmow166.a;
         |FINSI;
     |SINO;
         |SI enDesdeAlta = 1;
             |HAZ AltaModelo;
         |FINSI;

         |LEE 101#dsmow166.=;
     |FINSI;
     |LIBERA #dsmow166;

     enDesdeAlta = 0;
|FPRC;

|| \\\\\ Baja del Modelo ////////

|PROCESO BajaModelo;
     nBaja = 0;
     |DBASE aBase; |QBF aBase;
     aMas  = aBase + "def/dsm182ca.mas";
     |CARGA_DEF aMas, espejo1@;
     |SI FSalida = 0;
         |ABRE #espejo1@;
         #espejo1@#0 = #dsmow166#0;
         #espejo1@#1 = #dsmow166#1;
         #espejo1@#2 = #dsmow166#2;
         #espejo1@#3 = #dsmow166#3;
         #espejo1@#4 = #dsmow166#4 + 1;
         |LEE 000#espejo1@.=;
         |SI FSalida = 0;
             nBaja = 1;
         |FINSI;
         |CIERRA #espejo1@;
         |DESCARGA_DEF #espejo1@;
     |FINSI;

     |SI nBaja = 1;
         aAlfa = "    Existe Complementaria del Modelo";
         |SI #dsmow166#4 > 0;
             aAlfa = "    Existe una Complementaria posterior";
         |FINSI;
         aAlfa = aAlfa + ", no puedo dar de Baja";
         |MENAV aAlfa;
         |ACABA;
     |FINSI;

     |SI #dsmow166#9 = " ";  |ACABA;  |FINSI;

     |SI #dsmow166#16 = "X";
         |MENAV "     Modelo ya Archivado. No se puede dar de Baja";
         |ACABA;
     |FINSI;

     |SI #dsmow166#11 = "X";
         |CONFI "0000NModelo ya impreso, Esta Seguro de la Baja : ";
         |SI FSalida ! 0;
             |ACABA;
         |FINSI;

     |SINO;
         |CONFI "0000NEsta seguro de dar de Baja el Modelo ";
         |SI FSalida ! 0;
             |ACABA;
         |FINSI;
     |FINSI;

     enTipoEntrada = 3;
     |HAZ EntraMod182;

     |LEE 101#dsmow166.=;
     |LIBERA #dsmow166;
|FPRC;

|PROCESO Boton2m182;
     |PTEC 824;
|FPRC;

|PROCESO Boton3m182;
     |PTEC 825;
|FPRC;

|PROCESO Boton6m182;
     |PTEC 828;
|FPRC;

|PROCESO Boton7m182;
     |PTEC 829;
|FPRC;

|PROCESO Boton8m182;
     |PTEC 830;
|FPRC;

|PROCESO Boton9m182;
     |PTEC 831;
|FPRC;

|PROCESO Boton10m182;
     |PTEC 832;
|FPRC;

|PROCESO Boton12m182;
     |PTEC 834;
|FPRC;

|PROCESO Boton17m182;
     |PTEC 839;
|FPRC;

|PROCESO Boton22m182;
     |PTEC 844;
|FPRC;

|PROCESO FinBotonm182;
     |FIN_CONTROL_WINDOWS nBoton2;
     |FIN_CONTROL_WINDOWS nBoton3;
     |FIN_CONTROL_WINDOWS nBoton6;
     |FIN_CONTROL_WINDOWS nBoton22;
     |FIN_CONTROL_WINDOWS nBoton7;
     |FIN_CONTROL_WINDOWS nBoton8;
     |FIN_CONTROL_WINDOWS nBoton9;
     |FIN_CONTROL_WINDOWS nBoton10;
     |FIN_CONTROL_WINDOWS nBoton12;
     |FIN_CONTROL_WINDOWS nBoton17;

     |PULSATECLA;
|FPRC;

|PROCESO Tipo0m182;  |TIPO 0;
     |HAZ FinBotonm182;
|FPRC;

|PROCESO Tipo7m182;  |TIPO 7;
     |BLIN 04;

     |CONTROL_SIMPLE 0, "LBOTON,F2  Consulta Modelo     ", 0928, 0953, Boton2m182;
     nBoton2 = FSalida;

     |CONTROL_SIMPLE 0, "LBOTON,F3  Entrada de Datos    ", 1028, 1053, Boton3m182;
     nBoton3 = FSalida;

     |CONTROL_SIMPLE 0, "LBOTON,F6  Emitir Modelo       ", 1128, 1153, Boton6m182;
     nBoton6 = FSalida;

     |CONTROL_SIMPLE 0, "LBOTON, Informe Comprobacion ", 1228, 1253, Boton22m182;
     nBoton22 = FSalida;

     |CONTROL_SIMPLE 0, "LBOTON,F7 Recoger Declaracion Ejerc.Anterior", 1328, 1353, Boton7m182;
     nBoton7 = FSalida;

     |CONTROL_SIMPLE 0, "LBOTON,F8  Baja Modelo         ", 0955, 0980, Boton8m182;
     nBoton8 = FSalida;

     |CONTROL_SIMPLE 0, "LBOTON,F9  Crear Complementaria", 1055, 1080, Boton9m182;
     nBoton9 = FSalida;

     |CONTROL_SIMPLE 0, "LBOTON,F10 Dejar No Impreso    ", 1155, 1180, Boton10m182;
     nBoton10 = FSalida;

     |CONTROL_SIMPLE 0, "LBOTON,F12 N§.Justificante / CSV", 1255, 1280, Boton12m182;
     nBoton12 = FSalida;

     aAlfa = "Archivar           "; |SI #dsmow166#16 = "X"; aAlfa = "Dejar No Archivado "; |FINSI;
     aAlfa = "LBOTON,Ctrl-F7 " + aAlfa;
     |CONTROL_SIMPLE 0, aAlfa, 1355, 1380, Boton17m182;
     nBoton17 = FSalida;

     |ABRE #dsmom107;
     |DDEFECTO #dsmom107;
     |LEE 041#dsmom107.=;
     |CIERRA #dsmom107;

     |ESTADO_CONTROL nBoton2,  "DISABLE";  nSwBoton2  = 0;
     |ESTADO_CONTROL nBoton3,  "DISABLE";  nSwBoton3  = 0;
     |ESTADO_CONTROL nBoton6,  "DISABLE";  nSwBoton6  = 0;
     |ESTADO_CONTROL nBoton22, "DISABLE";  nSwBoton22 = 0;
     |ESTADO_CONTROL nBoton7,  "DISABLE";  nSwBoton7  = 0;
     |ESTADO_CONTROL nBoton8,  "DISABLE";  nSwBoton8  = 0;
     |ESTADO_CONTROL nBoton9,  "DISABLE";  nSwBoton9  = 0;
     |ESTADO_CONTROL nBoton10, "DISABLE";  nSwBoton10 = 0;
     |ESTADO_CONTROL nBoton12, "DISABLE";  nSwBoton12 = 0;
     |ESTADO_CONTROL nBoton17, "DISABLE";  nSwBoton17 = 0;

     |SI #dsmow166#11 = "X";
         |ESTADO_CONTROL nBoton12, "ENABLE";  nSwBoton12 = 1;
     |FINSI;

     |SI #dsmow166#9 = "X";
         |ESTADO_CONTROL nBoton6,   "ENABLE";  nSwBoton6  = 1;
         |ESTADO_CONTROL nBoton22,  "ENABLE";  nSwBoton22 = 1;
     |FINSI;

     |SI #dsmow166#11 = "X";  |Y #dsmow166#16 = " ";
         |ESTADO_CONTROL nBoton10,  "ENABLE";  nSwBoton10 = 1;
     |FINSI;

     |SI #dsmow166#11 = "X";
         |ESTADO_CONTROL nBoton17,  "ENABLE";  nSwBoton17 = 1;
     |FINSI;

     |SI #dsmow166#9 = " "; |Y #dsmow166#11 = " "; |Y #dsmom107#30 = "S";
         |ESTADO_CONTROL nBoton17,  "ENABLE";  nSwBoton17 = 1;
     |FINSI;

     |ESTADO_CONTROL nBoton2,  "ENABLE";  nSwBoton2  = 1;
     |ESTADO_CONTROL nBoton3,  "ENABLE";  nSwBoton3  = 1;
     |SI #dsmow166#4 = 0;
         |ESTADO_CONTROL nBoton7,  "ENABLE";  nSwBoton7  = 1;
     |FINSI;
     |ESTADO_CONTROL nBoton8,  "ENABLE";  nSwBoton8  = 1;
     |ESTADO_CONTROL nBoton9,  "ENABLE";  nSwBoton9  = 1;
|FPRC;


|PROCESO Complementaria;
     |SALVA_DATOS #dsmow166;
     #dsmow166#4 = 0;
     |PARA;  |SI;  |HACIENDO;
          |LEE 040#dsmow166.=;
          |SI FSalida = 0;
              #dsmow166#4 = #dsmow166#4 + 1;
          |SINO;
              nLinea = #dsmow166#4;
              |SAL;
          |FINSI;
     |SIGUE;

     aJustiModelo = #dsmow166#18;   |QBF aJustiModelo;
     aElectModelo = #dsmow166#20;   |QBF aElectModelo;
     nUltimo      = nLinea - 1;

     |REPON_DATOS #dsmow166;

     #dsmow166#21        = "";
     #dsmow166#22        = "";
     #dsmow166#20        = "";
     #dsmow166#18        = "";
     #dsmow166#27        = "";

     |HAZ PideJustiCoSAnual;

     #dsmow166#4 = nLinea;
     #dsmow166#6 = 0;
     #dsmow166#7 = 0;
     #dsmow166#8 = 0;

     #dsmow166#9  = " ";
     #dsmow166#10 = "01.01.0000";
     #dsmow166#11  = " ";
     #dsmow166#12 = "01.01.0000";
     #dsmow166#16 = " ";
     #dsmow166#17 = "01.01.0000";

     aJustiModelo = "";
     aElectModelo = "";

     |GRABA 020#dsmow166.n;
     |LIBERA #dsmow166;
|FPRC;

|PROCESO Tipo11m182;  |TIPO 11;
     nTecla = FSalida;

     |HAZ FinBotonm182;

     enEmpresa     = #dsmow166#0;
     enEjer        = #dsmow166#1;
     enPeriodo     = #dsmow166#2;
     enModelo      = #dsmow166#3;
     enComplem     = #dsmow166#4;
     enActividad   = 0;
     enDComple     = 0;
     enHComple     = 0;

|| \\\\\ Consulta Modelos ////////

     |SI nTecla = 10;  |Y nSwBoton2 = 1;
         enTipoEntrada = 4;

         |HAZ EntraMod182;
         |HAZ Refresco;
     |FINSI;

|| \\\\\ Alta o Modificacion Modelos ////////

     |SI nTecla = 11;  |Y nSwBoton3 = 1;
         |HAZ AltaModelo;
         |HAZ Refresco;
         |ACABA;
     |FINSI;

|| \\\\\ Emision del Modelo

     |SI nTecla = 14;  |Y nSwBoton6 = 1;
         |SI #dsmow166#9 = "X";
             |SI #dsmow166#16 = "X";
                 |CONFI "0000NModelo Archivado. Desea continuar";
                 |SI FSalida ! 0;  |ACABA;  |FINSI;
             |SINO;
                 aAlfa = #dsmow166#20 + #dsmow166#18;  |QBF aAlfa;
                 |SI aAlfa ! "";
                     |CONFI "0000NEl modelo tiene codigo de justificante o codigo electronico. Continuamos";
                     |SI FSalida ! 0;  |ACABA;  |FINSI;
                 |FINSI;
             |FINSI;

             |LEE 101#dsmow166.=;
             eaCamp4_9  = #dsmow166#11;
             efCamp4_10 = #dsmow166#12;
             eaCamp4_19 = #dsmow166#20;
             eaCamp4_25 = #dsmow166#18;

             #dsmow166#11 = "";
             #dsmow166#12 = "01.01.0000";
             #dsmow166#20 = "";
             #dsmow166#18 = "";
             #dsmow166#27 = "";
             |GRABA 020#dsmow166.a;
             |LIBERA #dsmow166;

             enInfComp = 0;
             |SUB_EJECUTA_NP "dsmoy182;PLANING";
             |SI enInfComp = 1;
                 |LEE 101#dsmow166.=;
                 #dsmow166#11 = eaCamp4_9;
                 #dsmow166#12 = efCamp4_10;
                 #dsmow166#20 = eaCamp4_19;
                 #dsmow166#18 = eaCamp4_25;
                 |GRABA 020#dsmow166.a;
                 |LIBERA #dsmow166;
             |FINSI;
             |HAZ Refresco;
         |FINSI;
     |FINSI;

     |SI nTecla = 30;  |Y nSwBoton22 = 1;
         |SI #dsmow166#9 = "X";
             |SUB_EJECUTA_NP "dsmoy182;INFC";
             |HAZ Refresco;
         |FINSI;
     |FINSI;

|| \\\\\ Recoge Modelo desde Registro

     |SI nTecla = 15;  |Y nSwBoton7 = 1;
         |HAZ TraeModelo;
         |HAZ Refresco;
     |FINSI;

|| \\\\\ Baja del Modelo ////////

     |SI nTecla = 16;  |Y nSwBoton8 = 1;
         |HAZ BajaModelo;
         |HAZ Refresco;
     |FINSI;

|| \\\\\ Creacion de Complementaria del Modelo ////////

     |SI nTecla = 17;  |Y nSwBoton9 = 1;
         |CONFI "     Estas seguro de crear una Complementaria : ";
         |SI FSalida = 0;
             |HAZ Complementaria;
             |HAZ Refresco;
         |FINSI;
     |FINSI;

|| \\\\\ Dejar no Impreso ////////

     |SI nTecla = 18;  |Y nSwBoton10 = 1;
         |SI #dsmow166#11 = "X";  |Y #dsmow166#16 = " ";
             aAlfa =  "0000NEsta seguro de dejar el Modelo como no Impreso";
             |CONFI aAlfa;
             |SI FSalida ! 0;  |ACABA;  |FINSI;

             aAlfa = #dsmow166#20 + #dsmow166#18;  |QBF aAlfa;
             |SI aAlfa ! "";
                 |CONFI "0000NEl modelo tiene codigo de justificante o codigo electronico. Continuamos";
                 |SI FSalida ! 0;  |ACABA;  |FINSI;
             |FINSI;

             |LEE 101#dsmow166.=;
             #dsmow166#11 = "";
             #dsmow166#12 = "01.01.0000";
             #dsmow166#20 = "";
             #dsmow166#18 = "";
             #dsmow166#27 = "";
             |GRABA 020#dsmow166.a;
             |LIBERA #dsmow166;

             |PINTA #dsmow166#11;

         |FINSI;
     |FINSI;

|| \\\\\ Numero de Justificante ////////

     |SI nSwBoton12 = 1;
         |SI nTecla = 9;  |O nTecla = 20;
             |SI #dsmow166#9 = "X";
                 |PUSHV 0501 2380;

                 eaIdJus = #dsmow166#18;
                 eaIdEle = #dsmow166#20;
                 eaIdExp = #dsmow166#27;

                 |SUB_EJECUTA "dsmow115";

                 #dsmow166#18 = eaIdJus;
                 #dsmow166#20 = eaIdEle;
                 #dsmow166#27 = eaIdExp;

                 |GRABA 020#dsmow166.a;
                 |LIBERA #dsmow166;
             |FINSI;
         |FINSI;
     |FINSI;

|| \\\\\ Archivar o Desarchivar  ////////

     |SI nTecla = 25;  |Y nSwBoton17 = 1;
         |SI #dsmow166#16 = "X";
             |CONFI "0000NEsta seguro de dejar el Modelo como no Archivado";
             |SI FSalida ! 0;  |ACABA;  |FINSI;
             |LEE 101#dsmow166.=;
             #dsmow166#16 = "";
             #dsmow166#14 = "01.01.0000";
             |GRABA 020#dsmow166.a;
             |LIBERA #dsmow166;
             |PINTA #dsmow166#16;
         |SINO;
             enArchi = 0;
             aAlfa = "0000NDesea Archivar el Modelo ";
             |SI #dsmow166#11 = " "; |Y #dsmow166#9 = " ";
                 enArchi = 1;
                 aAlfa   = "0000NDesea Archivar el Modelo NO ELABORADO ";
             |FINSI;
             |CONFI aAlfa;
             |SI FSalida = 0;
                 aAlfa   = #dsmow166#3;
                 aAlfa   = "dsmoz007;" + aAlfa + "PLANING";

                 |SUB_EJECUTA_NP aAlfa;
                 |HAZ Refresco;
             |FINSI;
         |FINSI;
     |FINSI;
|FPRC;
