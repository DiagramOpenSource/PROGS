|FICHEROS;
 dsmoy039 #0;

 :/contagen/def/canempre #30;
 :/basica/def/agifigen   #100;
|FIN;

|VARIABLES;
   nCampo     = 0;
   nLaEmpresa = 0;
   nSwTipo    = 0;
   &nPin1     = 0;
   &aPin1     = "";
   nSwAlgun   = 0;
   aDnom1     = "";
   aDnom2     = "";

   aBase      = "";
   aFichero   = "";
   nCanal     = 0;
   aVariable  = "";
   aAlfa1     = "";
|FIN;

|INCLUYE i_variar;
|INCLUYE i_varcon;

|| :::::::::::::::::::::::::::::::::::::::::::

|CALCULO elMayor; |TIPO 0;
  |SI #0#1 < #0#0;
      |ERROR;
  |FINSI;
|FCAL;

|CALCULO SelTipo0;
     |SI #0#19 = "N";
         #0#5 = 0;  |PINTA #0#5;  |C_M #0#5, 1, "N";
         #0#6 = 99;  |PINTA #0#6;  |C_M #0#6, 1, "N";
         |PARA nCampo = 7;  |SI nCampo [ 16;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |SINO;
         |C_M #0#5, 1, "S";
         |C_M #0#6, 1, "S";
         |PARA nCampo = 7;  |SI nCampo [ 16;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "N";
               #0nCampo = 0;  |PINTA #0nCampo;
         |SIGUE;
     |FINSI;
|FCAL;

|CALCULO SelTipo1;
     |SI #0Campo = 0;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 16;  |HACIENDO nCampo = nCampo + 1;
               #0nCampo = 0;  |C_M #0nCampo, 1, "N";
         |SIGUE;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 16;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |FINSI;
|FCAL;

|| *************************************************************
|PROCESO Imprime0;
 |SI #0#4 ! "T";
     |SI #canempre#21 ! #0#4; |ACABA; |FINSI;
 |FINSI;

 nLaEmpresa = #canempre#2;
 |HAZ ElTipoEmpresa;
 |SI nSwTipo = 1; |ACABA; |FINSI;

 aAlfa1 = #agifigen#14; |QBF aAlfa1;
 |SI aAlfa1 ! ""; #canempre#9 = #agifigen#14; |FINSI;

 |PRINT;
 nSwAlgun = 1;
|FPRC;

|DEFBUCLE LeeEmpresas_2;
 #canempre#3;
 2,3;
 aDnom1, #0#0,#0#2;
 aDnom2, #0#1,#0#3;
 ;
 NULL, Imprime0;
|FIN;

|DEFBUCLE LeeEmpresas_1;
 #canempre#1;
 ;
 #0#0,#0#2;
 #0#1,#0#3;
 ;
 NULL, Imprime0;
|FIN;

|CALCULO Tipo3;  |TIPO 3;

     aDnom1 = "                                        ";
     aDnom2 = "zzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzz";

     |DBASS aBase; |QBT aBase;  || Directorio Base de las Aplaciones
     aFichero = aBase + "modelos/def/empresas.ref";
     |XABRE "W", aFichero, nCanal;
     aVariable = ":/contagen/def/canempre" + &13 + &10;
     |XGRABA nCanal, aVariable;
     |XCIERRA nCanal;

     |IMPRE 0;
     |SI FSalida ! 0;  |ACABA;  |FINSI;
     |INFOR "dsmoy039";
     |SI FSalida ! 0;  |FINIMP;  |ACABA;  |FINSI;
     |SI #0#18 = "C";
         |BUCLE LeeEmpresas_1;   || por codigo
     |SINO;
         |BUCLE LeeEmpresas_2;   || por nombre
     |FINSI;
     |SI nSwAlgun = 1;
         |PIEINF;
     |FINSI;
     |FININF;
     |FINIMP;
|FCAL;

||------- Proceso para seleccionar por tipo de Empresa
|PROCESO ElTipoEmpresa;
    nSwTipo = 1;
    |ABRE #agifigen;
    #agifigen#0 = nLaEmpresa; || Empresa
    |LEE 041#agifigen.=;
    |SI FSalida ! 0;
        |DDEFECTO #agifigen;
    |FINSI;
    |CIERRA #agifigen;
    nPin1 = #agifigen#87;
    aPin1 = #agifigen#88;
    |SI #0#19 = "S";
        |SI nPin1 ] #0#5; |Y nPin1 [ #0#6;
            nSwTipo = 0;
        |FINSI;
    |SINO;
        |SI nPin1 = #0#7;
            nSwTipo = 0;
        |SINO;
            |PARA nCampo = 7;  |SI nCampo [ 16;  |HACIENDO nCampo = nCampo + 1;
                  |SI nPin1 = #0nCampo; |Y nPin1 ! 0;
                      nSwTipo = 0;
                      |SAL;
                  |FINSI;
             |SIGUE;
        |FINSI;
    |FINSI;
|FPRC;
