|FICHEROS;
     dsmoz064 #0;

     dsmom009 #9;
     dsmom010 #10;
     dsmom011 #11;
     dsmom012 #12;
     dsmom013 #13;

     :/basica/def/agifigen #118;
     :/modelos/def/dsmom014 #214;
     dsmoc130 #130;
     dsmom09@ #909;
|FIN;

|VARIABLES;
     nEstado   = 0;
     nPara1    = 0;
     nCampo    = 0;
     nDEmpresa = 0;
     nHEmpresa = 0;
     nPeriodo  = 0;
     nEjer     = 0;

     aAlfa1    = "";
     aAlfa2    = "";

     fLaFecha  = @;
     nSw0      = 0;
     aPathModelo = "";
     aDef        = "";
     nSPin1      = 0;

|FIN;

|INCLUYE i_varcon;
|INCLUYE i_variar;

|| /////////////////////// PROCESOS ENTRADA POR PANTALLA \\\\\\\\\\\\\\\\\\\\
|PROCESO defecto;  |TIPO 7;
     |SI enEmpresa ! 0;
         #0#0 = enEmpresa;  |PINTA #0#0;
         #0#1 = enEmpresa;  |PINTA #0#1;

         |C_M #0#0, 1, "N";
         |C_M #0#1, 1, "N";
     |FINSI;
     fLaFecha = ~;
     aAlfa1 = fLaFecha; aAlfa1 = aAlfa1 % -104;
     #0#14 = aAlfa1;
     |PINTA #0#14;
|FPRC;

|PROCESO Periodo; |TIPO 0;
     nCampo = 30; |SI Campo = 16; nCampo = 31; |FINSI;

     #0nCampo = "          ";
     |SI #0Campo = 1;   #0nCampo = "ENERO     ";  |FINSI;
     |SI #0Campo = 2;   #0nCampo = "FEBRERO   ";  |FINSI;
     |SI #0Campo = 3;   #0nCampo = "MARZO     ";  |FINSI;
     |SI #0Campo = 4;   #0nCampo = "ABRIL     ";  |FINSI;
     |SI #0Campo = 5;   #0nCampo = "MAYO      ";  |FINSI;
     |SI #0Campo = 6;   #0nCampo = "JUNIO     ";  |FINSI;
     |SI #0Campo = 7;   #0nCampo = "JULIO     ";  |FINSI;
     |SI #0Campo = 8;   #0nCampo = "AGOSTO    ";  |FINSI;
     |SI #0Campo = 9;   #0nCampo = "SEPTIEMBRE";  |FINSI;
     |SI #0Campo = 10;  #0nCampo = "OCTUBRE   ";  |FINSI;
     |SI #0Campo = 11;  #0nCampo = "NOVIEMBRE ";  |FINSI;
     |SI #0Campo = 12;  #0nCampo = "DICIEMBRE ";  |FINSI;
     |SI #0Campo = 99;  #0nCampo = "ANUAL     ";  |FINSI;

     |SI #0nCampo = "          ";
         |MENAV "     Periodo Incorrecto.";
         |ERROR;
     |FINSI;

     |PINTA #0nCampo;
|FPRC;

|PROCESO Parrilla;
     |SI #0#0 = 0;
         #0#0 = 0;  |PINTA #0#0;
         #0#1 = 0;  |PINTA #0#1;  |C_M #0#1, 1, "N";
         |PARA nCampo = 2;  |SI nCampo [ 13;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |SINO;
         |SI enEmpresa = 0;
             |C_M #0#0, 1, "S";
             |C_M #0#1, 1, "S";
         |FINSI;

         |PARA nCampo = 2;  |SI nCampo [ 13;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "N";
               #0nCampo = 0;  |PINTA #0nCampo;
         |SIGUE;
     |FINSI;
|FPRC;

|PROCESO NoMasEmpresas;
     |SI #0Campo = 0;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 13;  |HACIENDO nCampo = nCampo + 1;
               #0nCampo = 0;  |C_M #0nCampo, 1, "N";
         |SIGUE;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 13;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |FINSI;
|FPRC;

|CALCULO YaSeCual; |TIPO 7;
|FCAL;

|CALCULO SelTipo0; |TIPO 0;
 |SI #0#29 = "N";
     #0#17 = 0;   |PINTA #0#17;  |C_M #0#17, 1, "N";
     #0#18 = 99;  |PINTA #0#18;  |C_M #0#18, 1, "N";
     |PARA nCampo = 19;  |SI nCampo [ 28;  |HACIENDO nCampo = nCampo + 1;
           |C_M #0nCampo, 1, "S";
     |SIGUE;
 |SINO;
     |SI enEmpresa = 0;
         |C_M #0#17, 1, "S";
         |C_M #0#18, 1, "S";
     |FINSI;
     |PARA nCampo = 19;  |SI nCampo [ 28;  |HACIENDO nCampo = nCampo + 1;
           |C_M #0nCampo, 1, "N";
           #0nCampo = 0;  |PINTA #0nCampo;
     |SIGUE;
 |FINSI;
|FCAL;

|CALCULO SelTipo1;
 |SI #0Campo = 0;
     |PARA nCampo = Campo + 1;  |SI nCampo [ 28;  |HACIENDO nCampo = nCampo + 1;
           #0nCampo = 0;  |C_M #0nCampo, 1, "N";
     |SIGUE;
 |SINO;
     |PARA nCampo = Campo + 1;  |SI nCampo [ 28;  |HACIENDO nCampo = nCampo + 1;
           |C_M #0nCampo, 1, "S";
     |SIGUE;
 |FINSI;
|FCAL;

|| /////////////////////// //////////////////////////////////////////////////
|PROCESO RecogeAnterior;
     #9#21 = 0;
     #9#24 = 0;

     nSw0 = 0;
     |DDEFECTO #130;
     #130#0 = #9#0;
     #130#1 = #9#1;
     #130#2 = #9#2 - 3;
     #130#3 = 130;
     #130#4 = 0;
     #130#5 = #9#4;
     |LEE 000#130=;
     |PARA;  |SI FSalida = 0;  |HACIENDO;  || Recoge de la ultima linea del
          #9#21 = #130#11;
          #9#24 = #130#14;

          nSw0 = 1;
          #130#4 = #130#4 + 1;
          |LEE 000#130=;
     |SIGUE;


     |SI nSw0 = 0;

         |DBASE aPathModelo;
         aDef = aPathModelo + "def/dsmom009.mas";
         |CARGA_DEF aDef, dsmom09@;
         |SI FSalida ! 0;
             |MENAV "     Error en Cargar el Def dsmom009.mas";
             |ACABA;
         |FINSI;

         |ABRE #909;
         #909#0 = #9#0;
         #909#1 = #9#1;
         #909#2 = #9#2 - 3;
         #909#3 = 0;
         #909#4 = #9#4;
         |LEE 000#909=;
         |PARA;  |SI FSalida = 0;  |HACIENDO;  || Recoge de la ultima linea del
                 #9#21 = #909#22;
                 #9#24 = #909#25;

                 #909#3 = #909#3 + 1;
                 |LEE 000#909=;
          |SIGUE;

          |CIERRA #909;
          |DESCARGA_DEF #dsmom09@;
     |FINSI;
|FPRC;
|| /////////////////////////////////////////////////////////////////

|PROCESO CalculaConsumo;
     |ABRE #214;
     |SELECT #2#214;
     #214#10 = enPlan14;
     #214#5  = #12#5;
     |LEE 040#214=;
     |SI FSalida = 0;
          |SI #12#7 = "+";
              #9#58 = #9#58 + #12#8;
          |SINO;
              #9#58 = #9#58 - #12#8;
          |FINSI;
     |SINO;
          |SI #12#7 = "+";
              #9#20 = #9#20 + #12#8;
          |SINO;
              #9#20 = #9#20 - #12#8;
          |FINSI;
     |FINSI;
     |CIERRA #214;
|FPRC;

|DEFBUCLE dsmom012z64;
     #12#1;
     ;
     #9#0, #9#1, #9#2, #9#3, #9#4, "    ";
     #9#0, #9#1, #9#2, #9#3, #9#4, "zzzz";
     ;
     NULL, CalculaConsumo;
|FIN;

|PROCESO CalculaGastos;
     |SI #13#7 = "+";
         #9#23 = #9#23 + #13#8;
     |SINO;
         #9#23 = #9#23 - #13#8;
     |FINSI;
|FPRC;

|DEFBUCLE dsmom013z64;
     #13#1;
     ;
     #9#0, #9#1, #9#2, #9#3, #9#4, "    ";
     #9#0, #9#1, #9#2, #9#3, #9#4, "zzzz";
     ;
     NULL, CalculaGastos;
|FIN;


|PROCESO dsmom009;

|| ... Seleccion de Tipo de Empresa
  |DDEFECTO #agifigen;
  #agifigen#0 = #9#0;
  |LEE 041#agifigen.=;
  nSPin1 = #agifigen#87;

  enSwSelEmp = 1;
  |SI #0#29 = "S";
      |SI nSPin1 ] #0#17; |Y nSPin1 [ #0#18;
          enSwSelEmp = 0;
      |FINSI;
  |SINO;
      |SI nSPin1 = #0#19;
          enSwSelEmp = 0;
      |SINO;
          |PARA nPara1 = 20;  |SI nPara1 [ 28;  |HACIENDO nPara1 = nPara1 + 1;
                |SI nSPin1 = #0nPara1; |Y nSPin1 ! 0;
                    enSwSelEmp = 0;
                    |SAL;
                |FINSI;
          |SIGUE;
      |FINSI;
  |FINSI;
  |SI enSwSelEmp = 1; |ACABA; |FINSI;
|| ------------------------------------------------------------------------


     |SI #9#2 ! 3; |Y #9#14 ! " ";
          |HAZ RecogeAnterior;
     |FINSI;

||  Consumos;
     #9#20 = 0;
     #9#58 = 0;
     |BUCLE dsmom012z64;

||  Gastos;
     #9#23 = 0;
     |BUCLE dsmom013z64;

     #9#22 = #9#20 + #9#21;
     #9#25 = #9#23 + #9#24;

     |GRABA 020#9a;
     |LIBERA #9;

     |DDEFECTO #130;
     #130#0 = #9#0;
     #130#1 = #9#1;
     #130#2 = #9#2;
     #130#3 = 130;
     #130#4 = 0;
     #130#5 = #9#4;
     |LEE 141#130=;
     |SI FSalida = 0;
         #130#9  = #9#20;
         #130#10 = #9#21;
         #130#11 = #9#22;

         #130#12 = #9#23;
         #130#13 = #9#24;
         #130#14 = #9#25;

         |GRABA 020#130a;
     |FINSI;
     |LIBERA #130;
|FPRC;

|DEFBUCLE dsmom009;
     #9#1;
     ;
     nDEmpresa, #0#14, #0#15, 00, 01;
     nHEmpresa, #0#14, #0#16, 99, 99;
     be;
     NULL, dsmom009;
|FIN;

|PROCESO Tipo3; |TIPO 3;
  enEjer = #0#14;
  |HAZ Verm014Plan;
  |SI enPlan14 = 0; |ACABA; |FINSI;

  |ABRE #130;
  |ABRE #118;
  |SI #0#0 ! 0;
      nDEmpresa = #0#0;
      nHEmpresa = #0#1;
      |BUCLE dsmom009;
  |SINO;
      |PARA nCampo = 2; |SI nCampo [ 13; |HACIENDO nCampo = nCampo + 1;
            |SI #0nCampo ! 0;
                nDEmpresa = #0nCampo;
                nHEmpresa = #0nCampo;
                |BUCLE dsmom009;
            |FINSI;
      |SIGUE;
  |FINSI;
  |CIERRA #118;
  |CIERRA #130;
|FPRC;
