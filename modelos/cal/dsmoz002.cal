|FICHEROS;
     dsmoz002 #0;

     :/basica/def/agifigen #1000;
     :/basica/def/agitab10;

     paises@  #910;
|FIN;

|VARIABLES;
 {-1} aMenu       = "";
      aMenu1      = "1315";
      aMenu2      = "1";
      aMenu3      = "6";
      aMenu4      = "Modelos Oficiales por Empresas                      ";
      aMenu5      = "Grupos de Empresas para los Modelos 110/111 y 190   ";
      aMenu6      = "Mantenimiento del Impuesto de Actividades Economicas";
      aMenu7      = "Calculo Planing de Modelos Oficiales                ";
      aMenu8      = "Introduccion y Visualizacion de Modelos por Planing ";
      aMenu9      = "Consulta Resumen Historico de Tributacion           ";

     aBase            = "";
     aAlfa            = "";
     nCanal           = 0;
     aMas             = "";

     &eaPathIntegral  = "";
     &eaMensaCad       = "";
     &enErrorCad       = 0;
|FIN;

|INCLUYE i_variar;

|PROCESO PonNombre;
     enEmpresa = #dsmoz002#0;
     |HAZ MiraBloqueo;
     |SI eaBloqueo = "3";
         |ERROR;
         |ACABA;
     |FINSI;

     |ABRE #1000;
     #1000#0 = #0#0;
     |LEE 040#1000=;
     |SI FSalida ! 0;
         |MENAV "     Empresa Inexistente";
         |ERROR;
     |SINO;
         #0#1 = #1000#1;
         |PINTA #0#1;
     |FINSI;
     |CIERRA #1000;
     eaCliNif = #1000#13;
|FPRC;

|PROCESO Salir;  |TIPO 0;
     |SI FSalida = 1;
         |PTEC 807;
     |FINSI;
|FPRC;

|PROCESO Tipo12;  |TIPO 12;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     enEmpresa = #0#0;
     enModo    = 2;

     |PUSHV 0501 2380;
     |PARA; |SI; |HACIENDO;
            |MENUG aMenu;
            aMenu2 = FSalida;
            |SI aMenu2 < "1";  |O aMenu2 > "6";  |SAL;  |FINSI;

            |SI aMenu2 = "1";
                |SUB_EJECUTA_NP ":modelos/dsmom006";
            |FINSI;

            |SI aMenu2 = "2";
                |SUB_EJECUTA_NP ":modelos/dsmom026";
            |FINSI;

            |SI aMenu2 = "3";
                |SUB_EJECUTA_NP ":basica/agicen14";
            |FINSI;

            |SI aMenu2 = "4";
                |SUB_EJECUTA_NP ":modelos/dsmoz001";
            |FINSI;

            |SI aMenu2 = "5";
                eaNombreEmp = #dsmoz002#1;
                |SUB_EJECUTA_NP ":modelos/dsmom003";
            |FINSI;

            |SI aMenu2 = "6";
                |SUB_EJECUTA_NP ":modelos/dsmom023";
            |FINSI;
     |SIGUE;
     |POPV;
|FPRC;

|PROCESO VerPaises;

     |DBASS aBase;  |QBF aBase;
     eaPathIntegral = aBase + "integral/";
     aAlfa = eaPathIntegral + "def/dsam0000.mas";
     |XABRE "E", aAlfa, nCanal;
     |SI FSalida < 0; eaPathIntegral = ""; |FINSI;

     |SI eaPathIntegral ! "";
         aMas = eaPathIntegral + "def/dsam0104.mas";
     |SINO;
         aMas = aBase + "basica/def/agim0016.mas";
     |FINSI;

     |CARGA_DEF aMas, paises@;
     |SI FSalida ! 0; |ACABA; |FINSI;

     |ABRE #910;
     #910#0 = "ES ";
     |LEE 141#910=;
     |SI FSalida ! 0; |O #910#4 ! 11;
         |CIERRA #910;
         |SI eaPathIntegral ! "";
             eaExtension = "int";
             eaPrograma  = "dsam0104";
             |SUB_EJECUTA_NP ":basica/dspatron";
         |FINSI;
         eaExtension = "bas";
         eaPrograma  = "agim0016";
         |SUB_EJECUTA_NP ":basica/dspatron";
     |SINO;
         |CIERRA #910;
     |FINSI;
     |DESCARGA_DEF #paises@;

     |ABRE #agitab10;
     #agitab10#0 = 17;
     |LEE 000#agitab10.=;
     |SI FSalida = 0;
         aAlfa = #agitab10#5; |QBF aAlfa;
         |SI aAlfa !  "ACOGIDO VOLUNTARIAMENTE AL SII";
             #agitab10#5 = "ACOGIDO VOLUNTARIAMENTE AL SII";
             #agitab10#1 = "ACOGIDO VOLUNTARIAMENTE EN EL SUMINISTRO";
             #agitab10#2 = "INMEDIATO DE INFORMACION - SII";
             |GRABA 020#agitab10.a;
          |FINSI;
      |FINSI;
      |LIBERA #agitab10;
      |CIERRA #agitab10;
|FPRC;

|PROGRAMA;
     eaMensaCad = "9";
     |HAZ ControlCaducidad;
     |SI enErrorCad = 1;
         |ACABA;
     |FINSI;

     |HAZ VerPaises;

     |CLS;
     |CABEZA "Planing de Modelos";
     |PARA;  |SI;  |HACIENDO;
          |PINPA #0#0;
          |PINDA #0#0;
          |ENDATOS #1#0;
          |SI FSalida ! 0;  |SAL;  |FINSI;
     |SIGUE;

     |HAZ FinalizaEnX;
|FPRO;
