|FICHEROS;
     dsmoz051 #0;
     dsmom054 #54;  ||Mant. Rendimiento Transparencia Fiscal
     dsmom055 #55;  ||Socios
     dsmom056 #56;  ||Control Transparencia Fiscal  ... ???

     dsmow062 #99,MANTE;  ||Temporal V.Empresas Pantalla

     :/integral/def/dsam0000 #2;   ||Clientes
     :/contasoc/def/maempr11 #9;

     :/contagen/def/cacuenta #300;
     :/contagen/def/canempre #330;

     :/basica/def/agifigen #100;
     :/basica/def/agicen14 #114;
     :/basica/def/agim0019 #119;  || Registro de Sociedades
     :/basica/def/agim0025 #125;  || Socios

     :/contasoc/def/isocie59 #259;     || Impuesto Sociedades
|FIN;

|VARIABLES;
     nEstado   = 0;
     nCampo    = 0;
     nDEmpresa = 0;
     nHEmpresa = 0;
     nPeriodo  = 0;
     nEjer     = 0;
     nEjerCal  = 0;
     aAlfa1    = "";
     aAlfa2    = "";
     aAlfa3    = "";
     aFichero  = "";
     aPath     = "";

     nPin1 = "";
     aPin1 = "";
     nPin2 = "";
     aPin2 = "";
     nPin3 = "";
     aPin3 = "";
     fPn10 = @;
     fPn11 = @;
     SwTranspa = 0;
     swExiste = 0;
     swSocie  = 0;
     nEmpres  = 0;
     nRegis   = 0;
     nSaldo   = 0;
     nSuma  = 0;
     nPvp   = 0;
     nTAccion   = 0;
     &swUnoSolo = 0;
     &enEjerCal = 0;
     aPathImpuesto = "";
     nPara1 = 0;
     nPara2 = 0;
     nNume1 = 0;
     nNume2 = 0;
     nNume3 = 0;
     nNume4 = 0;
     aCuenta = "";
     aSigno  = "";

     fLaFecha = @;
     fFecha   = @;
     fDFecha  = @;
     fHFecha  = @;
|FIN;

|INCLUYE i_variar;
|INCLUYE i_varcon;

|| /////////////////////// PROCESOS ENTRADA POR PANTALLA \\\\\\\\\\\\\\\\\\\\
|CALCULO Tipo7;  |TIPO 7;
     |SI enEmpresa ! 0;
         #0#0 = enEmpresa;  |PINTA #0#0;
         #0#1 = enEmpresa;  |PINTA #0#1;

         |C_M #0#0, 1, "N";
         |C_M #0#1, 1, "N";
     |FINSI;

     fLaFecha = ~;
     aAlfa1   = fLaFecha; aAlfa1 = aAlfa1 % -104;
     #0#14    = aAlfa1;    |PINTA #0#14;
     #0#15    = #0#14 - 1; |PINTA #0#15;
|FCAL;

|CALCULO Parrilla;
     |SI #0#0 = 0;
         #0#0 = 0;  |PINTA #0#0;
         #0#1 = 0;  |PINTA #0#1;  |C_M #0#1, 1, "N";
         |PARA nCampo = 2;  |SI nCampo [ 13;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |SINO;
         |SI enEmpresa = 0;
             |C_M #0#0, 1, "S";
             |C_M #0#1, 1, "S";
         |FINSI;

         |PARA nCampo = 2;  |SI nCampo [ 13;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "N";
               #0nCampo = 0;  |PINTA #0nCampo;
         |SIGUE;
     |FINSI;
|FCAL;

|CALCULO NoMasEmpresas;
     |SI #0Campo = 0;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 13;  |HACIENDO nCampo = nCampo + 1;
               #0nCampo = 0;  |C_M #0nCampo, 1, "N";
         |SIGUE;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 13;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |FINSI;
|FCAL;

|CALCULO elAny; |TIPO 0;
 |HAZ LeeElControl;
 #0#15 = #56#1; |PINTA #0#15;
|FCAL;

|| /////////////////////// //////////////////////////////////////////////////
|| /////////////////////// PROCESOS CALCULO VALORACION  \\\\\\\\\\\\\\\\\\\\\
|PROCESO RelSocios0;
  |SI #125#18 > "01.01.1900"; |Y #125#18 > fHFecha;
      |ACABA;
  |FINSI;
  |SI #125#19 > "01.01.1900"; |Y #125#19 < fDFecha;
      |ACABA;
  |FINSI;

  #55#0 = nEmpres;
  #55#1 = enEjer;
  #55#2 = #125#2;
  |LEE 101#55=;
  |SI FSalida ! 0;
      #55#0 = nEmpres;
      #55#1 = enEjer;
      #55#2 = #125#2;
      |GRABA 020#55b;
  |FINSI;

  #55#3 = #125#4;   || Nombre
  #55#4 = #125#3;   || NIF
  #55#5 = #125#13;  || % Participacion
  #55#6 = #125#14;  || Acciones

  |HAZ LeeEURODBMOD;
  |PARA nPara1 = 7; |SI nPara1 [ 17; |HACIENDO nPara1 = nPara1 + 1;
        nNume1 = nPara1 + 6;
        nNume2 = #54nNume1;
        nNume2 = ((nNume2 * #55#5) / 100) ! EURODBMOD;
        #55nPara1 = nNume2;
  |SIGUE;
  #55#18 = #54#27;
  |GRABA 040#55a;
  |LIBERA #55;
|FPRC;

|DEFBUCLE RelSocios;
  #125#1;
  ;
  nEmpres, nRegis, 01;
  nEmpres, nRegis, 99;
  e;
  NULL, RelSocios0;
|FIN;

|| .......................................................................
|PROCESO LeeCuentas;
  nSaldo = 0;
  #cacuenta#0 = aCuenta;
  |LEE 001#cacuenta.=;
  |SI FSalida = 0;
      |SI aSigno = "D";
          nSaldo = ((#cacuenta#51 - #cacuenta#48) / enConversor) ! EURODBMOD;
      |FINSI;
      |SI aSigno = "H";
          nSaldo = ((#cacuenta#52 - #cacuenta#49) / enConversor) ! EURODBMOD;
      |FINSI;
      |SI aSigno = "S";
          nSaldo = ((#cacuenta#53 - #cacuenta#50) / enConversor) ! EURODBMOD;
      |FINSI;
  |FINSI;
|FPRC;

|PROCESO LasCuentas;
  enEmpresa  = #canempre#2;
  enPerConta = #canempre#3;
  eaLaMonEmp = #canempre#28;
  |HAZ EnConversor;

  aAlfa1 = #canempre#2; aAlfa1 = ("00000" + aAlfa1) % -105;
  aAlfa2 = #canempre#3; aAlfa2 = ("0" + aAlfa2) % -101;
  |DBASS aPath; |QBT aPath;
  aPathConta = aPath + "contagen/fich/" + aAlfa1 + aAlfa2 + "/";
  |PATH_DAT #300 aPathConta;

  |ABRE #cacuenta;
  |PARA nPara2 = 2; |SI nPara2 [ 12; |HACIENDO nPara2 = nPara2 + 1;
        |SI #56nPara2 = "C";
            nNume2    = nPara2 + 22;
            nNume3    = nPara2 + 11;
            nNume4    = nNume2 + 22;
            aCuenta   = #56nNume2;
            aSigno    = #56nNume4;
            |HAZ LeeCuentas;
            #54nNume3 = nSaldo;
        |FINSI;
  |SIGUE;
  |CIERRA #cacuenta;
|FPRC;

|PROCESO ElImpuesto;
 |PARA nPara2 = 2; |SI nPara2 [ 12; |HACIENDO nPara2 = nPara2 + 1;
       |SI #56nPara2 = "I";
            nNume3    = nPara2 + 11;
            nNume2    = nPara2 + 11;
            nNume2    = #56nNume2;  ||Numero del Campo en Impuesto
            #54nNume3 = #259nNume2;
        |FINSI;
 |SIGUE;
|FPRC;

|| .....................................................................
|PROCESO CreaValor0;
  enEmpresa = #dsam0000#0;

  || .... Datos de la Empresa
  #54#0 = enEmpresa;
  #54#1 = enEjer;
  |LEE 101#54=;
  nEstado = FSalida;

  #54#0  = enEmpresa;   || Numero Empresa
  #54#1  = enEjer;      || A¤o
  #54#2  = 00;          || Actividad
  #54#3  = #dsam0000#1; || Nombre Empresa
  #54#12 = #0#15;       || A¤o de Imputacion Rendimientos

  nEmpres = #54#0;
  |HAZ LosDatosEmpresa;
  |SI SwTranspa = 0; |ACABA; |FINSI;
  |SI enEjerBajaM > 0; |Y enEjerBajaM < #0#14;
      |SI nEstado = 0; |BORRA 020#54a; |FINSI;
      |LIBERA #54;
      |ACABA;
  |FINSI; ||esta ya disuelta

  #54#4  = nPin1;      || Tipo Empresa
  #54#5  = aPin1;
  #54#6  = nPin2;      || Tipo Tributacion
  #54#7  = aPin2;
  #54#8  = nPin3;      ||  Tipo Actividad Principal
  #54#9  = aPin3;
  #54#10 = fPn10;      || Fecha Alta
  #54#11 = fPn11;      || Fecha Baja

|| ... Relacion de Cuentas
  |ABRE #canempre;
  |SELECT #4#canempre;
  #canempre#2  = enEmpresa;
  #canempre#26 = nEjerCal;   ||A¤o del Calculo
  |LEE 001#canempre.=;
  |SI FSalida = 0;
      |HAZ LasCuentas;
  |FINSI;
  |CIERRA #canempre;

|| ... Datos del Impuesto
  |ABRE #259;
  #259#0 = enEmpresa;
  #259#1 = enEjerCal;   ||A¤o calculo
  |LEE 001#259=;
  |SI FSalida = 0;
      |HAZ ElImpuesto;
  |FINSI;
  |CIERRA #259;

|| ........ Proceso de Acciones y Relacion de Socios
   |SI nSuma = 1; #54#24 = "Acciones Nominativas"; |FINSI;
   |SI nSuma = 2; #54#24 = "Acciones al portador"; |FINSI;
   |SI nSuma = 3; #54#24 = "Participaciones socios"; |FINSI;
   #54#25 =  nTAccion;
   #54#26 =  nPvp;             ||Valor Nominal

|| Campo Tributacion efectiva del Impuesto de Sociedades
   #54#27 = (( #54#23 /  #54#13 ) * 100) ! 2;

   |ABRE #55;
   |BUCLE RelSocios;
   |CIERRA #55;


|| ..... Grabar Registro
   |SI nEstado = 0; |GRABA 020#54a; |FINSI;
   |SI nEstado ! 0; |GRABA 020#54b; |FINSI;
   |LIBERA #54;

|| ..... Grabar Temporal
   #99#0 = #54#0;
   #99#1 = #54#1;
   |LEE 101#99=;
   |SI FSalida ! 0;
       |DDEFECTO #99;
       #99#0 = #54#0;
       #99#1 = #54#1;
       |GRABA 020#99b;
   |FINSI;
   #99#2 = #54#2;
   #99#3 = #54#3;
   |GRABA 020#99a;
   |LIBERA #99;
|FPRC;

|DEFBUCLE CreaValor;
    #dsam0000#1;
    ;
    nDEmpresa;
    nHEmpresa;
    e;
    NULL, CreaValor0;
|FIN;

|| .....................................................................
|PROCESO BorraSocio0;
||  |PDEFECTO #55, 5,18;
||  |GRABA 020#55a;
  |BORRA 020#55a;
  |LIBERA #55;
|FPRC;

|DEFBUCLE BorraSocio;
    #55#1;
    ;
    #54#0, #54#1, 001;
    #54#0, #54#1, 999;
    be;
    NULL, BorraSocio0;
|FIN;

|PROCESO Borrar0;
  |BUCLE BorraSocio;
  |PARA nPara2 = 2; |SI nPara2 [ 12; |HACIENDO nPara2 = nPara2 + 1;
        |SI #56nPara2 ! "M";
            nNume3    = nPara2 + 11;
            #54nNume3 = 0;
        |FINSI;
  |SIGUE;
  |GRABA 020#54a;
  |LIBERA #54;
|FPRC;

|DEFBUCLE BCalculoAnt;    || Borrado Calculo Anterior
    #54#1;
    ;
    nDEmpresa, enEjer;
    nHEmpresa, enEjer;
    be;
    NULL, Borrar0;
|FIN;
|| .....................................................................

|PROCESO Calcular;
  |BUCLE BCalculoAnt;    || Borrado Calculo Anterior
  |ABRE #99;
  |ABRE #54;
  |BUCLE CreaValor;
  |CIERRA #54;
  |CIERRA #99;
|FPRC;

|| \\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\
|RUTINA ClaveBaja99;
 #99#0 = 00001;
 #99#1 = enEjer;
|FRUT;

|RUTINA ClaveAlta99;
 #99#0 = 99999;
 #99#1 = enEjer;
|FRUT;

|PROCESO Tipo3;  |TIPO 3;
     aFichero = ("62" + Usuario) % 108;
     |NOME_DAT #99 aFichero;
     |ABRE #99;  |CIERRA #99;  |DELINDEX #99;

     |DBASS aPath; |QBT aPath;
     aPathImpuesto = aPath + "contasoc/fich/";
     |PATH_DAT #9 aPathImpuesto;
     |PATH_DAT #259 aPathImpuesto;

     enEjer      = #0#14;        || A¤o Calculo
     aAlfa1      = #0#14;
     aAlfa1      = aAlfa1 % -102;
     nEjer       = aAlfa1;       || Dos ultimos digitos del A¤o

     aAlfa2      = enEjer;
     aAlfa1      = "01.01." + aAlfa2; fDFecha = aAlfa1;
     aAlfa1      = "31.12." + aAlfa2; fHFecha = aAlfa1;

     enEjerCal   = #0#15;
     aAlfa1      = #0#15;
     aAlfa1      = aAlfa1 % -102;
     nEjerCal    = aAlfa1;       || Dos ultimos digitos del A¤o

     |HAZ LeeElControl;

     |SI #0#0 ! 0;
         nDEmpresa = #0#0;
         nHEmpresa = #0#1;
         |HAZ Calcular;
     |SINO;
         |PARA nCampo = 2;  |SI nCampo [ 13;  |HACIENDO nCampo = nCampo + 1;
               nDEmpresa = #0nCampo;
               nHEmpresa = #0nCampo;
               |HAZ Calcular;         || Calcular Valor
         |SIGUE;
     |FINSI;
     |SI swUnoSolo = 0;
         |ENTLINEAL #1#99, -1, 4, 9, 1, ClaveBaja99, ClaveAlta99;
     |SINO;
          |PINPA #0#54;
          |ENTLINEAL #1#99, -1, 4, 9, 0, ClaveBaja99, ClaveAlta99;
     |FINSI;
     |DELINDEX #99;
|FPRC;

|| =========================================================================
|PROCESO PrimeraAct;
        nPin2 = #agicen14#9;
        aPin2 = #agicen14#10;
        nPin3 = #agicen14#16;
        aPin3 = #agicen14#17;
        fPn10 = #agicen14#5;
        fPn11 = #agicen14#6;

    |SI #agicen14#6 [ "01.01.0000"; |O #agicen14#6 ] fFecha;
        |ERROR10;
    |FINSI;
|FPRC;

|DEFBUCLE PrimeraAct;
    #agicen14#1;
    ;
    nEmpres, 01;
    nEmpres, 99;
    ;
    NULL, PrimeraAct;
|FIN;

|PROCESO LosDatosEmpresa;
  nPin1 = "";
  aPin1 = "";
  nPin2 = "";
  aPin2 = "";
  nPin3 = "";
  aPin3 = "";
  fPn10 = "01.01.0000";
  fPn11 = "01.01.0000";

  nSuma    = 0;
  nPvp     = 0;
  nTAccion = 0;
||..................... Transparencia Fiscal
     SwTranspa = 0;
     |ABRE #9;
     #9#0 = nEmpres;
     #9#1 = enEjer;
     #9#2 = 12;
     |LEE 040#9=;
     |SI FSalida = 0; SwTranspa = 1; |FINSI;

     #9#0 = nEmpres;
     #9#1 = enEjer;
     #9#2 = 13;
     |LEE 040#9=;
     |SI FSalida = 0; SwTranspa = 1; |FINSI;

     #9#0 = nEmpres;
     #9#1 = enEjer;
     #9#2 = 15;
     |LEE 040#9=;
     |SI FSalida = 0; SwTranspa = 1;  |FINSI;
     |CIERRA #9;

     |SI SwTranspa = 0;  |ACABA;  |FINSI;

 || .............................. Lee Actividad
  fFecha = "31.12.2999";
  |SI enEjer ! 0;
      aAlfa1 = enEjer;
      aAlfa1 = "01.01."+ aAlfa1;
      fFecha = aAlfa1;
  |FINSI;

 || Lee Actividad
    |BUCLE PrimeraAct;

 || Lee Ficha Sociedades
    swExiste = 0;
    nRegis   = 0;
    |ABRE #agim0019;
    #agim0019#0 = nEmpres; || Empresa
    #agim0019#1 = 99;
    |LEE 041#agim0019.];
    |SI FSalida = 0;
        |LEE 041#agim0019.a;
    |SINO;
        |LEE 041#agim0019.u;
    |FINSI;
    |SI FSalida = 0; |Y #agim0019#0 = nEmpres;
        swExiste = 1;
    |FINSI;
    |SI swExiste = 1;
        nRegis = #agim0019#1;  ||Registre
        nPin1 = #agim0019#4;
        aPin1 = #agim0019#5;
        fPn10 = #agim0019#7;      || Fecha Activa
        fPn11 = #agim0019#55;     || Fecha Inactiva
        nSuma = 0;
        |SI #agim0019#15 ! 0; nTAccion = #agim0019#15; nPvp = #agim0019#16; nSuma = 1; |FINSI;
        |SI #agim0019#19 ! 0; nTAccion = #agim0019#19; nPvp = #agim0019#20; nSuma = 2; |FINSI;
        |SI #agim0019#23 ! 0; nTAccion = #agim0019#23; nPvp = #agim0019#24; nSuma = 3; |FINSI;
        |SI #agim0019#12 > "01.01.1000";
            aAlfa1 = #agim0019#12; aAlfa1 = aAlfa1 % -104; enEjerBajaM = aAlfa1;
        |FINSI;
    |FINSI;
    |CIERRA #agim0019;

    |ABRE #agifigen;
    #agifigen#0 = nEmpres; || Empresa
    |LEE 041#agifigen.=;
    |SI FSalida = 0;
        nPin1 = #agifigen#87;
        aPin1 = #agifigen#88;
        fPn10 = #agifigen#93;
        fPn11 = #agifigen#94;
    |FINSI;
    |CIERRA #agifigen;
|FPRC;

|PROGRAMA;
  |SI enEmpresa ! 0;
      |CLS;
      #0#0 = enEmpresa;
      #0#1 = enEmpresa;
      #0#14 = enEjer;
      |HAZ LeeElControl;
      #0#15 = #56#1;
      swUnoSolo = 1;
      |HAZ Tipo3;
  |SINO;
      |MANTE #0;
  |FINSI;
|FPRO;

|PROCESO LeeElControl;
  |ABRE #56;
  #56#0 = #0#14;
  |LEE 101#56=;
  |SI FSalida ! 0;
      |DDEFECTO #56;
      #56#0 = #0#14;
      #56#1 = #0#14 - 1;
      |GRABA 020#56n;
  |FINSI;
  |LIBERA #56;
  |CIERRA #56;
|FPRC;
