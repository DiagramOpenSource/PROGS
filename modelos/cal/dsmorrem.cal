|FICHEROS;
   dsmorrem #0;
   dsmorrel #1;

   dsm210h3 #10;
   dsm211hi #11;
   dsm213hi #12;
|FIN;

|VARIABLES;
   nGrid1  = -1;
   nPant   = 0;
   nBoton  = 0;
   nBoton1 = 0;

   nRango = 0;
   nPos1  = 0;
   nPos2  = 0;
   nModo  = 0;
   nAntRem = 0;

   aAlfa   = "";

   aEsc1   = "";
   aEsc2   = "";
   aRetu   = "";
   aTecla  = "";
|FIN;

|PROCESO Evento;
   |SI FSalida = 1; |O FSalida = 2; |LEE 000#dsmorrel.c; |FINSI;
|FPRC;

|PROCESO Min;
     #1#0 = #0#0;
     #1#1 = 1;
|FPRC;

|PROCESO Max;
     #1#0 = #0#0;
     #1#1 = 999;
|FPRC;

|PROCESO Tipo14; |TIPO 14;
   |SI nGrid1 < 0;
       nRango = 1 + 4 + 8 + 16 + 32;
       nPos1 = 1105;
       nPos2 = 2174;
       |LINEAL_SIMPLE #1#1, nRango, nPos1, nPos2, Min, Max, Evento;
       nGrid1 = FSalida;
  |SINO;
       |REFRESCACONTROL nGrid1;
  |FINSI;
|FPRC;

|PROCESO Tipo8; |TIPO 8;
   |ABRET #1;
|FPRC;

|PROCESO Tipo9; |TIPO 9;
   |CIERRAT #1;
|FPRC;

|PROCESO BorraRem210;
   #dsm210h3#37 = 0;
   |GRABA 020#dsm210h3.a; |LIBERA #dsm210h3;
|FPRC;

|DEFBUCLE BorraRem210;
#dsm210h3#2;
;
#dsmorrel#2, #dsmorrel#0;
#dsmorrel#2, #dsmorrel#0;
be;
NULL, BorraRem210;
|FIN;

|PROCESO BorraRem211;
   #dsm211hi#72 = 0;
   |GRABA 020#dsm211hi.a; |LIBERA #dsm211hi;
|FPRC;

|DEFBUCLE BorraRem211;
#dsm211hi#3;
;
#dsmorrel#2, #dsmorrel#0;
#dsmorrel#2, #dsmorrel#0;
be;
NULL, BorraRem211;
|FIN;

|PROCESO BorraRem213;
   #dsm213hi#29 = 0;
   |GRABA 020#dsm213hi.a; |LIBERA #dsm213hi;
|FPRC;

|DEFBUCLE BorraRem213;
#dsm213hi#2;
;
#dsmorrel#2, #dsmorrel#0;
#dsmorrel#2, #dsmorrel#0;
be;
NULL, BorraRem213;
|FIN;

|PROCESO BorraLin;
     |BORRA 020#1a;
     |LIBERA #1;

     |SI #dsmorrel#4 = 210; |BUCLE BorraRem210; |FINSI;
     |SI #dsmorrel#4 = 211; |BUCLE BorraRem211; |FINSI;
     |SI #dsmorrel#4 = 213; |BUCLE BorraRem213; |FINSI;
|FPRC;

|DEFBUCLE BorraLin;
     #1#1;
     ;
     #0#0, 001;
     #0#0, 999;
     be;
     NULL, BorraLin;
|FIN;

|PROCESO Tipo2; |TIPO 2; ||(*)
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 3; |BUCLE BorraLin; |FINSI;
|FPRC;

|PROCESO Tipo0; |TIPO 0;     ||(*)
     |REFRESCACONTROL nGrid1;
|FPRC

|PROCESO Tipo12; |TIPO 12;    ||(*)
     || No quitar
|FPRC;

|PROCESO Tipo30; |TIPO 30;    ||(*)
     nModo = (FEntrada / 100) ! 0;
     |ABRE #1;
     #1#0 = #0#0;
     #1#1 = 1;
     |LEE 000#1];
     |SI FSalida ! 0; |O #1#0 ! #0#0;
          |SI nModo = 1;
               |MENAV "0000Se anulan los datos entrados.";
          |SINO;
               |MENAV "0000No existen lineas, se borra la cabecera.";
          |FINSI;
          |BORRA 020#0a;
          |ERROR;
     |FINSI;
     |CIERRA #1;
|FPRC;

|PROCESO Tipo2L; |TIPO 2;

|FPRC;

|PROCESO BajaRemesa;
   aAlfa = "    N" + &191 + " Esta seguro de eliminar la remesa seleccionada ? ";
   |CONFI aAlfa;
   |SI FSalida = 0;
      |BUCLE BorraLin;

      |BORRA 020#dsmorrem.a;

      |PTEC 807;
   |FINSI;
|FPRC;

|PROCESO BajaLinea;
   aAlfa = "    N" + &191 + " Esta seguro de eliminar la linea de la remesa seleccionada ? ";
   |CONFI aAlfa;
   |SI FSalida = 0;
      |BORRA 020#1a;
      |LIBERA #1;

      #dsmorrem#4 = #dsmorrem#4 - #dsmorrel#6;
      |GRABA 020#dsmorrem.a;

      |SI #dsmorrel#4 = 210;
         |ABRE #dsm210h3;
         #dsm210h3#0 = #dsmorrel#11;
         #dsm210h3#1 = #dsmorrel#12;
         #dsm210h3#2 = #dsmorrel#13;
         #dsm210h3#3 = #dsmorrel#14;
         #dsm210h3#4 = #dsmorrel#15;
         #dsm210h3#5 = #dsmorrel#16;
         #dsm210h3#6 = #dsmorrel#17;
         #dsm210h3#7 = #dsmorrel#18;
         #dsm210h3#8 = #dsmorrel#19;
         |LEE 101#dsm210h3.=;
         |SI FSalida = 0;
            #dsm210h3#37 = 0;
            |GRABA 020#dsm210h3.a;
            |LIBERA #dsm210h3;
         |FINSI;
         |CIERRA #dsm210h3;
      |FINSI;
      |SI #dsmorrel#4 = 211;
         |ABRE #dsm211hi;
         #dsm211hi#0 = #dsmorrel#20;
         #dsm211hi#1 = #dsmorrel#21;
         #dsm211hi#2 = #dsmorrel#22;
         #dsm211hi#3 = #dsmorrel#23;
         |LEE 101#dsm211hi.=;
         |SI FSalida = 0;
            #dsm211hi#72 = 0;
            |GRABA 020#dsm211hi.a; |LIBERA #dsm211hi;
         |FINSI;
         |CIERRA #dsm211hi;
      |FINSI;
      |SI #dsmorrel#4 = 213;
         |ABRE #dsm213hi;
         #dsm213hi#0 = #dsmorrel#24;
         #dsm213hi#1 = #dsmorrel#25;
         #dsm213hi#2 = #dsmorrel#26;
         #dsm213hi#3 = #dsmorrel#27;
         #dsm213hi#4 = #dsmorrel#28;
         |LEE 101#dsm213hi.=;
         |SI FSalida = 0;
            #dsm213hi#29 = 0;
            |GRABA 020#dsm213hi.a; |LIBERA #dsm213hi;
         |FINSI;
         |CIERRA #dsm213hi;
      |FINSI;

      |SALVA_FICHA #dsmorrel;
      #dsmorrel#0 = #dsmorrem#0;
      #dsmorrel#1 = 1;
      |LEE 000#dsmorrel.];
      |SI FSalida ! 0; |O #dsmorrel#0 ! #dsmorrem#0;
         |BORRA 020#dsmorrem.a; |PTEC 807;
      |FINSI;
      |REPON_FICHA #dsmorrel;

      |REFRESCACONTROL nGrid1;
   |FINSI;
|FPRC;

|PROGRAMA;
   |CLS;
   |CABEZA " Mantenimiento de remesas ";
   |PINPA #0#0; nPant = FSalida;
   |PINDA #0#0;

   |ABRE #dsmorrel; |ABRE #dsmorrem;

   nPos1 = 2344; nPos2 = 2358;
   |CONTROL_SIMPLE nPant, "BOTON,{{193}}Eliminar remesa", nPos1, nPos2, BajaRemesa;
   nBoton = FSalida;
   |ESTADO_CONTROL nBoton, "DISABLE";

   nPos1 = 2360; nPos2 = 2374;
   |CONTROL_SIMPLE nPant, "BOTON,{{193}}Eliminar Linea", nPos1, nPos2, BajaLinea;
   nBoton1 = FSalida;
   |ESTADO_CONTROL nBoton, "DISABLE";

   nRango = 4 + 8 + 16 + 32;
   nPos1 = 1105; nPos2 = 2174;
   |LINEAL_SIMPLE #1#1, nRango, nPos1, nPos2, Min, Max, Evento;
   nGrid1 = FSalida; |PULSATECLA;

   |ENCAMPO #0#dsmorrem;
   |SI FSalida = 6; FSalida = 0; |FINSI;
   |PARA; |SI FSalida = 0; |HACIENDO;
      |LEE 000#dsmorrem.=;
      |SI FSalida = 0;
         |ESTADO_CONTROL nBoton, "ENABLE";
         |ESTADO_CONTROL nBoton1, "ENABLE";

         |PINTA #dsmorrem#0; |PINTA #dsmorrem#1;
         |PINTA #dsmorrem#2; |PINTA #dsmorrem#3;
         |PINTA #dsmorrem#4;

         |REFRESCACONTROL nGrid1;

         aEsc1  = &255 + "806";
         aEsc2  = &255 + "807";
         aRetu  = &255 + "802";
         |PARA; |SI;  |HACIENDO;
            FSalida = "::{{001}}Salir," + nGrid1;
            |LEETECLA aTecla;
            |SI aTecla = aEsc1; |SAL; |FINSI;
            |SI aTecla = aEsc2; |SAL; |FINSI;
            |SI aTecla = aRetu; |SAL; |FINSI;
         |SIGUE;

         nAntRem = #dsmorrem#0; #dsmorrem#0 = 0; |REFRESCACONTROL nGrid1;
         #dsmorrem#0 = nAntRem;

         |ESTADO_CONTROL nBoton, "DISABLE";
         |ESTADO_CONTROL nBoton1, "DISABLE";
      |SINO;
         |MENAV "    Remesa inexistente";

         nAntRem = #dsmorrem#0; |DDEFECTO #dsmorrem; #dsmorrem#0 = nAntRem;

         |PINTA #dsmorrem#0; |PINTA #dsmorrem#1;
         |PINTA #dsmorrem#2; |PINTA #dsmorrem#3;
         |PINTA #dsmorrem#4;
      |FINSI;

      |ENCAMPO #0#dsmorrem;
      |SI FSalida = 6; FSalida = 0; |FINSI;
   |SIGUE;

   |DESTRUYECONTROL nGrid1;
   |FIN_CONTROL_WINDOWS nBoton;

   |CIERRA #dsmorrem; |CIERRA #dsmorrel;
|FPRO;
