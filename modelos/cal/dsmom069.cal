|FICHEROS;
    dsmom069;
    dsmom030;

    dsmom03@ #330;
|FIN;

|VARIABLES;
    aAlfa      = "";
    aPath      = "";
    aMas       = "";
    aID        = "";
    nID        = 0;
    {10}aPopup = "";

    aTipo      = "";
    aFichero   = "";
    nSw        = 0;
    nModo      = 0;
|FIN;

|INCLUYE i_variar;

|CALCULO FiltraCon; |TIPO 6;

     nModo = (FEntrada / 100) ! 0;
     aAlfa = "";
     nSw   = 0;
     |ABRE #dsmom030;
     #dsmom030#0 = #dsmom069#0;
     |LEE 041#dsmom030.=;
     |SI FSalida ! 0;
         nSw = 1;
         aAlfa = "0000No existe Concepto Contable";
     |SINO;
         |SI #dsmom030#4 ! "S";
             nSw = 1;
             aAlfa = "0000El Concepto Contable no es de IVA";
         |FINSI;
     |FINSI;
     |CIERRA #dsmom030;
     |SI nSw = 1;
         |MENAV aAlfa;
         |ERROR;
         |ACABA;
     |FINSI;

     aTipo = "";
     |SI #dsmom030#26 =  5;                        aTipo = "A";  |FINSI;
     |SI #dsmom030#26 =  6; |Y #dsmom030#72 = "S"; aTipo = "I";  |FINSI;
     |SI #dsmom030#26 = 15; |Y #dsmom030#72 = "S"; aTipo = "I";  |FINSI;
     |SI #dsmom030#30 =  2;                        aTipo = "A";  |FINSI;
     |SI #dsmom030#30 =  3; |Y #dsmom030#72 = "S"; aTipo = "I";  |FINSI;
     |SI #dsmom030#34 =  9;                        aTipo = "E";  |FINSI;
     |SI #dsmom030#34 = 16;                        aTipo = "S";  |FINSI;

     |SI #dsmom030#26 ! 0; |O #dsmom030#28 ! 0; |O #dsmom030#30 ! 0;
       |O #dsmom030#32 ! 0; |O #dsmom030#34 ! 0; |O #dsmom030#36 ! 0;
          |SI aTipo = ""; |Y nModo = 1;
              |CONFI "0000NEste concepto no es operacion intracomunitaria. Continuar";
              |SI FSalida ! 0;
                  |ERROR;
                  |ACABA;
              |FINSI;
          |FINSI;
     |FINSI;
     |SI nModo = 1;
         #dsmom069#1 = #dsmom030#12;
         |SI aTipo ! ""; #dsmom069#2 = aTipo; |FINSI;
         |PINTA #dsmom069#1;
         |PINTA #dsmom069#2;
     |FINSI;
|FCAL;

|PROCESO inf330;
   #330#0 = 001;
|FPRC;

|PROCESO sup330;
   #330#0 = 999;
|FPRC;

|CALCULO ConsultaCon; |TIPO 11;
   |SI FSalida = 10;
      |ABRE #330;
      #330#0 = #dsmom069#0;
      |CONSULTA_F_CLAVE #1#330,inf330,sup330;
      #dsmom069#0 = #330#0; |PINTA #dsmom069#0;
      #dsmom069#1 = #330#1; |PINTA #dsmom069#1;
      |CIERRA #330;
      |ERROR;
   |FINSI;
|FCAL;

|PROCESO Tipo66C2;  |TIPO 66;
     aAlfa = "|C002WID";
     aID   = #dsmom069#aAlfa#;
     nID   = aID;

     aPopup1  = nID;  || Si fuera -1 en la posicion
     aPopup2  = 7;
     aPopup3  =  "A - Adquisiciones";
     aPopup4  =  "E - Entregas";
     aPopup5  =  "S - Prestaciones";
     aPopup6  =  "I - Adquisiciones en el territorio del impuesto";
     aPopup7  =  "M - Entregas posteriores a importacion exenta";
     aPopup8  =  "H - Entregas posteriores importacion exenta con representante  ";
     aPopup9  =  "T - Entregas en el marco de operaciones triangulares ";

     IaPopup  = aPopup1  <-;
     |ESPECIFICA "TRACKPOPUP", aPopup;
     |SI FSalida ! 0;
         IaPopup  = aPopup3  <-;
         IaPopup  = IaPopup + (FSalida - 1);

         aAlfa = aPopup % 101;  #dsmom069#2 = aAlfa;
     |FINSI;

     |PINTA #dsmom069#2;
|FPRC;

|PROCESO Tipo0C2;  |TIPO 0;

     |SI #dsmom069#2 ! "A"; |Y #dsmom069#2 ! "E";
         |Y #dsmom069#2 ! "S"; |Y #dsmom069#2 ! "I";
            |Y #dsmom069#2 ! "M"; |Y #dsmom069#2 ! "H";
              |Y #dsmom069#2 ! "T";
        |MENAV "0000 Clave Modelo 349 incorrecta";
        |ERROR;
        |ACABA;
    |FINSI;

    #dsmom069#3 = " ";
    |SI #dsmom069#2 = "T"; #dsmom069#3 = "X"; |FINSI;
|FPRC;

|RUTINA ClaveBaja69;
 #dsmom069#0 = 001;
|FRUT;

|RUTINA ClaveAlta69;
 #dsmom069#0 = 999;
|FRUT;

|PROCESO CargoConceptos;

   #330#0 = #dsmom030#0;
   |LEE 041#330=;
   |SI FSalida ! 0;
       |DDEFECTO #330;
       #330#0  = #dsmom030#0;
       #330#01 = #dsmom030#01;
       #330#02 = #dsmom030#02;
       #330#03 = #dsmom030#03;
       #330#04 = #dsmom030#04;
       #330#05 = #dsmom030#05;
       #330#06 = #dsmom030#06;
       #330#07 = #dsmom030#07;
       #330#08 = #dsmom030#08;
       #330#09 = #dsmom030#09;
       #330#10 = #dsmom030#10;
       #330#11 = #dsmom030#11;
       #330#96 = #dsmom030#96;
       #330#97 = #dsmom030#97;
       #330#98 = #dsmom030#98;
       |GRABA 020#330n;
   |FINSI;
   |LIBERA #330;
|FPRC;

|DEFBUCLE CargoConceptos;
  #dsmom030#1;
  4;
  001, "S";
  999, "S";
  e;
  NULL, CargoConceptos;
|FIN;

|PROGRAMA;

  |DBASE aPath;
  aMas = aPath + "def/dsmom030";
  |CARGA_DEF aMas, dsmom03@;
  |SI FSalida ! 0;
      |MENAV "0000Error al carga el def dsmom030";
      |ACABA;
  |FINSI;

  aFichero = "xx" + Usuario;
  |NOME_DAT #330 aFichero;
  |DELINDEX #330;

  |ABRE #330;
  |BUCLE CargoConceptos;
  |CIERRA #330;

  |CLS;
  |PINPA #0#dsmom069;
  |ENTLINEAL #1#dsmom069, -1, 1, 21, 0, ClaveBaja69, ClaveAlta69;

  |DELINDEX #330;
  |DESCARGA_DEF #330;
|FPRO;
