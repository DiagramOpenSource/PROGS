|FICHEROS;
     dsmom066;
     dsmom067;

     :/basica/def/agifigen #1000;
     &regisnif@;
     dsmom006;
     imagen@ #900;
|FIN;

|VARIABLES;
      nSwEsta     = 0;
      aAlfa       = "";
      aAlfa1      = "";
      aBase       = "";
      aMas        = "";
      aLong       = "";
      nLong       = 0;
      nError      = 0;
      aCifDom     = "";
      aNumero     = "";
      nDominante  = 0;
      nModo       = 0;
      nModoL      = 0;
      fFechaFin   = @;
      nOpera      = 0;
      nLinea      = 0;
      aParam      = "";
|FIN;

|INCLUYE i_variar;

|PROCESO TrasNGrupo; |TIPO 0;
     nModo = ( FEntrada/ 100) ! 0;
     |SI nModo ! 1; |ACABA; |FINSI;

     aAlfa = #dsmom066#0;
     |QBF aAlfa;
     |SI aAlfa = "";
         |MENAV "0000Introduzca Numero de Grupo";
         |ERROR;
         |ACABA;
     |FINSI;
|FPRC;

|PROCESO PonNif;
     |HAZ TrasDatosGrupo;
     aAlfa = "S";
     |SI #dsmom067#2 ! 0;
         aAlfa = "N";
         |ABRE #1000;
         #1000#0 = #dsmom067#2;
         |LEE 040#1000=;
         |SI FSalida ! 0;
             |MENAV "     Empresa Inexistente";
             |ERROR;
         |SINO;
             #dsmom067#3 = #1000#13; |PINTA #dsmom067#3;
             #dsmom067#6 = #1000#1;  |PINTA #dsmom067#6;
        |FINSI;
        |CIERRA #1000;
     |FINSI;
     |C_M #dsmom067#3,1,aAlfa;
|FPRC;

|PROCESO TrasDni;  |TIPO 6;
     |SI FSalida = 2; |ACABA; |FINSI;
     |C_M #dsmom067#3,0,aAlfa1;
     |SI aAlfa1 = "N"; |ACABA; |FINSI;

     |SI #dsmom067#3 = "            ";
         |MENAV "0000Introduzca CIF Dominante";
         |C_M #dsmom067#3,1,"S";
         |ERROR;
         |ACABA;
     |FINSI;

     eaVarDni  = #dsmom067#3;
     enCalcCif = 0;
     |HAZ CalculameDNI;
     eaVarDni = (eaVarDni + "             ") % 112;
     |SI #dsmom067#3 ! eaVarDni;
         |CONFI "0000SEl CIF/NIF puede ser Erroneo. Desea Calcularlo.";
         |SI FSalida = 0;
             #dsmom067#3 = eaVarDni;
             |PINTA #dsmom067#3;
             |ERROR;
             |ACABA;
         |FINSI;
     |FINSI;

     |SI #dsmom067#2 = 0;
         eaCodNif = #dsmom067#3;
         |HAZ LeeNifs;
         #dsmom067#6 = #regisnif@#1;
         |PINTA #dsmom067#6;
     |FINSI;
|FPRC;

|PROCESO ClaveAlta;
  #dsmom006#15 = #dsmom066#0;
  #dsmom006#1  = 322;
  #dsmom006#0  = 00001;
|FPRC;

|PROCESO ClaveBaja;
  #dsmom006#15 = #dsmom066#0;
  #dsmom006#1  = 322;
  #dsmom006#0  = 99999;
|FPRC;

|PROCESO TrasDatosGrupo; |TIPO 0;
     |SI FSalida = 10;
         |ABRE #dsmom006;
         #dsmom006#1  = 322;
         #dsmom006#15 = #dsmom066#0;
         |CONSULTA_F_CLAVE #2#dsmom006, ClaveAlta, ClaveBaja;
         |CIERRA #dsmom006;
     |FINSI;
|FPRC;

|PROCESO EstasLineas;
   |SI #imagen@#5 > "01.01.0000";
       fFechaFin = #imagen@#5 + 1;
       nError = 0;
   |SINO;
       nError = 1;
   |FINSI;
|FPRC;

|DEFBUCLE EstasLineas;
   #imagen@#1002;
   ;
   #dsmom067#0, 01;
   #dsmom067#0, nLinea;
   ;
   NULL, EstasLineas;
|FIN;

|PROCESO VeoFechas;

     fFechaFin = "00.00.0000";
     nError    = 0;
     nLinea    = #dsmom067#1 - 1;
     |SI nLinea = 0; |Y nOpera = 0;
         fFechaFin = "01.01.2000";
         |ACABA;
     |FINSI;

     |DBASE aBase;  |QBF aBase;
     aMas = aBase + "def/dsmom067.mas";
     |CARGA_DEF aMas, imagen@;
     |SI nOpera = 0;
         |BUCLE EstasLineas;
     |FINSI;
     |SI nOpera = 1;
         |ABRE #imagen@;
         #imagen@#0 = #dsmom067#0;
         #imagen@#1 = 99;
         |LEE 040#imagen@.];
         |SI FSalida = 0;
             |LEE 040#imagen@.a;
         |SINO;
             |LEE 040#imagen@.u;
         |FINSI;
         |SI #imagen@#0 = #dsmom067#0;
             #dsmom066#2 = #imagen@#2;
             #dsmom066#3 = #imagen@#3;
             #dsmom067#6 = #imagen@#6;

         |FINSI;
         |CIERRA #imagen@;
     |FINSI;
     |SI nOpera = 2;
         |ABRE #imagen@;
         #imagen@#0 = #dsmom067#0;
         #imagen@#1 = #dsmom067#1 + 1;
         |LEE 040#imagen@.];
         |SI FSalida = 0; |Y #imagen@#0 = #dsmom067#0;
             fFechaFin = #imagen@#4;
             nError = 1;
         |FINSI;
         |CIERRA #imagen@;
     |FINSI;
     |DESCARGA_DEF #imagen@;
|FPRC;

|PROCESO Tipo0C01;
     nOpera = 0;
     nModoL = (FEntrada / 100) ! 0;
     |HAZ VeoFechas;
     |SI nError = 1; |Y nModoL ! 3;
         |MENAV "0000Error, esta introduciendo un nuevo registro, pero el anterior no tiene fecha Fin";
         |SI nModoL = 1;
             |ERROR;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO FechaIni7; |TIPO 7;
     nModoL = (FEntrada / 100) ! 0;
     |SI nModoL = 1;
         #dsmom067#4 = fFechaFin;
         |PINTA #dsmom067#4;
     |FINSI;
|FPRC;

|PROCESO FechaIni0; |TIPO 0;
     |SI #dsmom067#1 ! 1;
         |SI #dsmom067#4 > fFechaFin;
             |CONFI "0000NEsta dejando vacios en las Fechas Desde / Hasta. Continuar";
             |SI FSalida ! 0;
                 |ERROR;
             |FINSI;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO FechaFin;
     |SI #dsmom067#5 < #dsmom067#4; |Y #dsmom067#5 > "01.01.0000";
         |MENAV "0000Error en Fecha Fin menor a la fecha Inicio";
         |ERROR;
         |ACABA;
     |FINSI;
      nOpera = 2; |HAZ VeoFechas;
      |SI nError = 1;
          |SI #dsmom067#5 = "01.01.0000";
             |MENAV "0000No puede dejar la Fecha Fin Vacia. Tiene Registros Posteriores";
             |ERROR;
         |SINO;
              |SI #dsmom067#5 > fFechaFin;
                  |MENAV "0000La Fecha Fin es mayor a la fecha de inicio de Registro Siguiente";
                  |ERROR;
              |FINSI;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO Tipo3; |TIPO 3;
     #dsmom066#2 = 0;
     #dsmom067#3 = "";
     nOpera = 1; |HAZ VeoFechas;
|FPRC;

|PROGRAMA;

  |C_V #dsmom066#2, 1, "N";
  |C_V #dsmom066#3, 1, "N";

  aParam = PARAMETRO; |QBF aParam;
  |SI aParam = "";
      |MANTE #dsmom066;
  |SINO;
      aAlfa = aParam % 1101;
      nModo = aAlfa;
      aParam = aParam % 0110;
      |CLS;
      |ABRE #dsmom066;
      |DDEFECTO #dsmom066;
      #dsmom066#0 = aParam;
      |LEE 141#dsmom066.=;
      |SI FSalida ! 0;
          #dsmom066#0 = aParam;
          |GRABA 020#dsmom066.b;
      |FINSI;
      |PINPA #0#dsmom066;
      |PINDA #0#dsmom066;
      |SI nModo = 4;
          nOpera = 1; |HAZ VeoFechas;
          |PINTA 2228,#dsmom067#6;
          |PAUSA;
      |SINO;
          |ENDATOS #1#dsmom066;
      |FINSI;
      |GRABA 020#dsmom066.a;
      |LIBERA #dsmom066;
      |CIERRA #dsmom066;
  |FINSI;
|FPRO;
