|FICHEROS;
  dsmoz073 #0;

  :/contagen/def/cacuenta #3;

  :/modelos/def/dsmom111 #711,MANTE;
  :/modelos/def/dsmom100 #100;
  :/modelos/def/dsmom107 #107;

  :/basica/def/agitab99  #99;

  imagen@ #911;
|FIN;

|VARIABLES;
  aFichero   = "";
  nInkey     = 0;
  aAlfa      = "";
  aAlfa1     = "";
  aAlfa2     = "";
  aAlfa3     = "";
  nEstado    = 0;
  aCampo     = "";
  nCamp1     = 0;
  nCamp2     = 0;
  nNume1     = 0;
  nNume2     = 0;
  nNume3     = 0;
  nPara1     = 0;
  nError     = 0;
  nSwAste    = 0;
  nSwAste1   = 0;
  aCuenta    = "";
  aCuenta1   = "";
  aDCuenta   = "";
  aHCuenta   = "";
  aDCuenta1  = "";
  aHCuenta1  = "";
  aDCuenta2  = "";
  aHCuenta2  = "";
  nSwMod     = 0;

  aBase      = "";
  aDef       = "";
  aMensa     = "";
  aParam     = "";
|FIN;

|INCLUYE i_variar;
|INCLUYE i_varcon;

|| ***************************************************************
|PROCESO LeeLosMod;
  nSwMod    = 0;
  aAlfa1    = "";
  aAlfa2    = "";
  aAlfa3    = "N";
  aDCuenta1 = ""; aDCuenta2 = "";
  aHCuenta1 = ""; aHCuenta2 = "";

  |SI #99#0 = 110; |O #99#0 = 115; |O #99#0 = 123; |O #99#0 = 216;
      nSwMod    = 1;
      aAlfa1    = "Total a Pagar Saldo Cuentas";
      aDCuenta1 = "475000000000";
      aHCuenta1 = "475999999999";
  |FINSI;

  |SI #99#0 = 130; |O #99#0 = 131;
      nSwMod    = 0;
      aAlfa1    = "Total a Pagar Saldo Cuentas";
      aDCuenta1 = "            ";
      aHCuenta1 = "799999999999";
  |FINSI;

  |SI #99#0 = 303; |O #99#0 = 370; |O #99#0 = 371;  |O #99#0 = 322;
      nSwMod    = 1;
      aAlfa1    = "Total Cuota a deducir      ";
      aAlfa2    = "Total Cuota Devengada      ";
      aAlfa3    = "S";
      aDCuenta1 = "472         ";
      aHCuenta1 = "472999999999";
      aDCuenta2 = "477         ";
      aHCuenta2 = "477999999999";
  |FINSI;

  |SI #99#0 = 310; |O #99#0 = 311;
      nSwMod    = 1;
      aAlfa1    = "Total Cuota a deducir      ";
      aDCuenta1 = "472         ";
      aHCuenta1 = "472999999999";
  |FINSI;

  |SI nSwMod = 0; |ACABA; |FINSI;

  #0#0 = #99#0;
  #0#1 = #99#1;
  #0#2 = aAlfa1;
  #0#3 = aAlfa2;
  #0#4 = aAlfa3;
  #0#5 = aDCuenta1;
  #0#6 = aHCuenta1;
  #0#7 = aDCuenta2;
  #0#8 = aHCuenta2;
  |GRABA 040#0n;
|FPRC;

|DEFBUCLE agitab99;
  #99#1;
  ;
  000;
  899;
  e;
  NULL, LeeLosMod;
|FIN;

|| *******************************************************************
|PROCESO VerBueno;

    |SI #911#1 ! #711#1;   || No estamos en la Linea

        |SI #911Campo = #711Campo;   || Es la misma cuenta o grupo

            nSwMod = 1;

        |SINO;

            nSwAste1  = 0;            || Ver si es grupo de Cuentas
            aCuenta1  = #911Campo;
            aDCuenta1 = #911Campo;
            aHCuenta1 = #911Campo;
            aAlfa     = #911Campo; |QBF aAlfa;
            aAlfa1    = aAlfa % -101;
            |SI aAlfa1 = "*";
                aCuenta1  = aAlfa - "*";
                nSwAste1  = 1;
                aDCuenta1 = (aCuenta1 + "            ") % 112;
                aHCuenta1 = (aCuenta1 + "zzzzzzzzzzzz") % 112;
           |FINSI;

           |SI nSwAste = 0;               || es una sola cuenta la actual
               |SI #711Campo ] aDCuenta1; |Y #711Campo [ aHCuenta1;
                   nSwMod = 1;                 || Ya Esta Informada
               |FINSI;
           |SINO;
               aDCuenta = (aCuenta + "            ") % 112;  || grupo de Ctas
               aHCuenta = (aCuenta + "zzzzzzzzzzzz") % 112;
               |SI nSwAste1 = 0;             || la linea anterior, es una solo cta

                   |SI #911Campo ] aDCuenta; |Y #911Campo [ aHCuenta;
                       nSwMod = 2;           || una cuenta a parte del grupo
                   |FINSI;

               |SINO;
                    || ... Dos grupos
                    aAlfa1 = aCuenta  % 0; nNume1 = aAlfa1;
                    aAlfa2 = aCuenta1 % 0; nNume2 = aAlfa2;

                    |SI nNume1 < nNume2;
                        nNume3   = 100 + nNume2;
                        aDCuenta = (aCuenta + "            ") % nNume3;
                        aHCuenta = (aCuenta + "999999999999") % nNume3;
                        |SI aCuenta1 ] aDCuenta; |Y aCuenta1 [ aHCuenta;
                            nSwMod = 3;
                        |FINSI;
                    |FINSI;
                    |SI nNume1 > nNume2;
                        nNume3    = 100 + nNume1;
                        aDCuenta1 = (aCuenta1 + "            ") % nNume3;
                        aHCuenta1 = (aCuenta1 + "999999999999") % nNume3;
                        |SI aCuenta ] aDCuenta1; |Y aCuenta [ aHCuenta1;
                            nSwMod = 3;
                        |FINSI;
                    |FINSI;
                |FINSI;
            |FINSI;
        |FINSI;

        |SI nSwMod ! 0;  |ERROR10;  |FINSI;

    |FINSI;
|FPRC;

|DEFBUCLE VerBueno;
    #911#1002;
    ;
    #711#0, 01;
    #711#0, 99;
    ;
    NULL, VerBueno;
|FIN;

|PROCESO VerQueEsBueno;
    nSwMod = 0;
    |BUCLE VerBueno;
    |SI nSwMod ! 0;
        aAlfa = "    Cuenta ya introducida en otra Linea";
        |SI nSwAste = 1; aAlfa = "    Grupo de Cuentas ya introducido en otra Linea"; |FINSI;

        |SI nSwMod = 2;
            aAlfa = "    Existe Alguna Cuenta Informada de este Grupo ";
        |FINSI;
        |SI nSwMod = 3;
            aAlfa = "    Existe Grupo de Cuentas igual a este ya informado";
        |FINSI;
        |MENAV aAlfa;
        |ERROR;
    |FINSI;
|FPRC;
|| *******************************************************************
|CALCULO FiltraCta; |TIPO 6;
     |C_M #711Campo,0,aAlfa;
     |SI aAlfa = "N"; |ACABA; |FINSI;

     eaCuenta = #711Campo;
     salidas  = FSalida;
     |HAZ FiltraPuntos;
     #711Campo = eaCuenta;
     |PINTA #711Campo;
|FCAL;

|RUTINA inf2;
     #3#0 = aDCuenta;
     #3#3 = "S";
|FRUT;

|RUTINA sup2;
     #3#0 = aHCuenta;
     #3#3 = "S";
|FRUT;

|CALCULO ConsultaCta; |TIPO 0;
     nInkey = FSalida;
     |C_M #711Campo,0,aAlfa;
     |SI aAlfa = "N"; |ACABA; |FINSI;

     aCampo = Contenido;
     nCamp1 = Campo + 1;
     nCamp2 = Campo + 2;
     |C_M #711nCamp1,1,"N";

     |SI nInkey = 10;
         aDCuenta = #0#5; aHCuenta = #0#6;
         |SI Campo = 5;
             aDCuenta = #0#7; aHCuenta = #0#8;
         |FINSI;

         |ABRE #3;
         |CONSULTA_F_CLAVE #6#3,inf2,sup2;
         #711Campo = #3#0; |PINTA #711Campo;
         |CIERRA #3;
     |FINSI;

     |C_M #711nCamp2, 1, "S";

     |SI #711Campo = doce;
         #711nCamp1 = ""; |PINTA #711nCamp1;
         #711nCamp2 = ""; |PINTA #711nCamp2;
         |C_M #711nCamp2, 1, "N";
         |SI #0#4 = "N";
             |MENAV "    NO ha introducido una Cuenta o grupo de Cuentas (*)";
             |ERROR;
         |SINO;
             |CONFI "    NNO ha introducido una Cuenta o grupo de Cuentas (*). Continuar";
             |SI FSalida ! 0;
                  |ERROR;
             |FINSI;
             |ACABA;
        |FINSI;
     |FINSI;

|| ... Ver si es correcta la introduccion de esta cuenta
     aCuenta = #711Campo;
     nNume3  = 0
     nError  = 0;
     nSwAste = 0;
     aAlfa   = #711Campo; |QBF aAlfa;
     aAlfa1  = aAlfa % 0;
     nNume1  = aAlfa1;
     |PARA nPara1 = 1; |SI nPara1 [ nNume1; |HACIENDO nPara1 = nPara1 + 1;
           nNume2 = (nPara1 * 100) + 1;
           aAlfa1 = aAlfa % nNume2;
           |SI aAlfa1 = "*";
               |SI nSwAste = 1; nError = 1; |FINSI;
               nSwAste = 1;
               nNume3  = nPara1;
               |SI nNume3 < 4; |O nNume3 > 5; nError = 1; |FINSI;
           |SINO;
               |SI aAlfa1 < "0"; |O aAlfa1 > "9";
                   nError = 1;
               |SINO;
                   |SI nSwAste = 1; nError = 1; |FINSI;  || ya esta el *
               |FINSI;
           |FINSI;
     |SIGUE;
     |SI nError = 1;
         #711nCamp1 = ""; |PINTA #711nCamp1;
         #711nCamp2 = ""; |PINTA #711nCamp2;
         |MENAV "    Cuenta Erronea. ";
         |ERROR;
         |ACABA;
     |FINSI;

     |SI nSwAste ! 0;
         aCuenta = aAlfa - "*";
     |FINSI;

     |ABRE #3;
     |DDEFECTO #3;
     #3#0 = aCuenta;
     |LEE 041#3=;
     |SI FSalida ! 0;
         aAlfa = "    NNo existe Cuenta Contable";
         |SI nSwAste = 1; aAlfa = "    NNo existe grupo de Cuentas"; |FINSI;
         aAlfa = aAlfa + ". Continuar";
         |CONFI aAlfa;
         |SI FSalida ! 0;
             |ERROR;
             #711nCamp1 = ""; |PINTA #711nCamp1;
             #711nCamp2 = ""; |PINTA #711nCamp2;
             |CIERRA #3;
             |ACABA;
         |FINSI;
         |C_M #711nCamp1,1,"S";
     |FINSI;

     |SI aCampo ! #711Campo;
         #711nCamp1 = #3#2; |PINTA #711nCamp1;
         #711nCamp2 = "+";
         |SI #3#5 = "P"; |O #3#5 = "I"; |O #3#5 = "H";
             #711nCamp2 = "-";
         |FINSI;
     |FINSI;

    |CIERRA #3;
    |PINTA #711nCamp1;
    |PINTA #711nCamp2;

    |HAZ VerQueEsBueno;
|FCAL;

|| *******************************************************************
|RUTINA ClaveBaja711;
   #711#0 = #0#0;
   #711#1 = 01;
|FRUT;

|RUTINA ClaveAlta711;
   #711#0 = #0#0;
   #711#1 = 99;
|FRUT;

|PROCESO ElLineal;
    aAlfa = #0#4;
    |C_V #711#5,1,aAlfa; |C_M #711#5,1,aAlfa;
    |C_V #711#6,1,aAlfa;
    |C_V #711#7,1,aAlfa; |C_M #711#7,1,aAlfa;

    |PINPA #0#711;
    |ENTLINEAL #1#711, -2, enModo, 22, 0, ClaveBaja711, ClaveAlta711;
|FPRC;

|PROCESO Tipo7z73;  |TIPO 7;
    enModo = 7;
    |HAZ ElLineal;
|FPRC;

|PROCESO Tipo11z73;  |TIPO 11;
|FPRC;

|PROCESO Tipo0z73;  |TIPO 0;
    enModo = 2;
    |HAZ ElLineal;
|FPRC;

|| ****************************************************************
|RUTINA ClaveBaja0;
    #0#0 = 000;
|FRUT;

|RUTINA ClaveAlta0;
    #0#0 = 999;
|FRUT;

|PROGRAMA;

   aParam = PARAMETRO; |QBF aParam;
   |SI aParam = "";
     |ABRE #107;
     |DDEFECTO #107;
     |LEE 041#107=;
     |CIERRA #107;
     |SI #107#31 = "N";
         aMensa = "NO ESTA ACTIVO EL CONTROL DEL PASE DE DATOS.";
     |FINSI;
   |FINSI;

   |ABRE #100;
   |LEE 040#100p;
   |SI FSalida ! 0;
       |CIERRA #100;
       |MENAV "    No esta configurada las Empresas Maestras de Contabilidad";
       swerror = 1;
       |ACABA;
   |FINSI;
   |CIERRA #100;

   enEmpresa  = #100#1;
   enPerConta = #100#2;
   |HAZ LeeEmpConta;
   |PATH_DAT #3 aPathConta;

   |DBASE aBase;
   aDef = aBase + "def/dsmom111.mas";
   |CARGA_DEF aDef, imagen@;
   |SI FSalida ! 0;
       |MENAV "    Error cargando def " + aDef;
       |ACABA;
   |FINSI;

   aFichero = ("7z" + Usuario) % 108;
   |NOME_DAT #0 aFichero;
   |DELINDEX #0;

   |ABRE #0;
   |BUCLE agitab99;
   |CIERRA #0;

   |CLS;
   |PINPA #0#0;
   |ATRI P; |PINTA 0509, aMensa; |ATRI 0;
   |ENTLINEAL #1#0, -1, 4, 22, 0, ClaveBaja0, ClaveAlta0;
   |DELINDEX #0;

   |DESCARGA_DEF #imagen@;
|FPRO;
