|FICHEROS;
     dsmoz095 #95;
     dsmom004 #4;

     dsmom093  #93;

     dsmod131  #131;
     dsmod131@ #1131;

     dsmod130  #130;
     dsmod130@ #1130;

     :/basica/def/agifigen #1000;
     :/basica/def/agicen14 #114;
|FIN;

|VARIABLES;
     nCampo    = 0;

     aParam    = "";
     aBase     = "";
     aMas      = "";
     nDEmpresa = 0;
     nHEmpresa = 0;
     nHPer     = 0;
     nHComple  = 0;

     nSumaD    = 0;
     nSwNeg    = 0;

     nEmpresa  = 0;
     nPeriodo  = 0;
     nEjer     = 0;
     nModelo   = 0;
     nComplem  = 0;
     nImpAnt   = 0;
     nImpNuevo = 0;
     nEl33     = 0;
     nEl23     = 0;
     nImporte  = 0;
     nNoModi   = 0;
     nTope     = 0;
     nTope131  = 0;
     nTopeAgrario = 0;
     nSuma        = 0;
     nNume1    = 0;

     &aAplica1      = "";
     &aAplica2      = "";
     &nAplica3      = 0;
     &enSwApli      = 0;
     &enModeloAp    = 0;
     &enEmpresaAp   = 0;
     &enEjerAp      = 0;
     &enPeriodoAp   = 0;
     &enActividadAp = 0;
     &enRend_NoAgr  = 0;
     &enRend_Agr    = 0;
     &eaNombreAp    = "";
     &efFechaAp     = @;
     &enApli400     = 0;
     nPagAnt        = 0;

     &enElEjer      = 0;
     &enDEmpresa    = 0;
     &enHEmpresa    = 0;
     aAlfa          = "";

|FIN;

|INCLUYE i_variar;

|| /////////////////////// PROCESOS ENTRADA POR PANTALLA \\\\\\\\\\\\\\\\\\\\
|PROCESO AntesPantalla; |TIPO 7;
   |SI #95#14 < 2015;
       aAlfa = "Recalculo Modelos 130 y 131, Minoracion por deduccion Art.80 Ley Impuesto ";
   |SINO;
       aAlfa = "Recalculo Modelos 130/131, Minoracion deduccion Art.110.3.c del Reglamento";
   |FINSI;
   |ATRI R; |PINTA 0603, aAlfa; |ATRI 0;
|FPRC;

|PROCESO ElAny;
   |HAZ AntesPantalla;
|FPRC;

|PROCESO Parrilla;
     |SI #95#0 = 0;
         #95#0 = 0;  |PINTA #95#0;
         #95#1 = 0;  |PINTA #95#1;  |C_M #95#1, 1, "N";
         |PARA nCampo = 2;  |SI nCampo [ 13;  |HACIENDO nCampo = nCampo + 1;
               |C_M #95nCampo, 1, "S";
         |SIGUE;
     |SINO;
         |SI enEmpresa = 0;
             |C_M #95#0, 1, "S";
             |C_M #95#1, 1, "S";
         |FINSI;

         |PARA nCampo = 2;  |SI nCampo [ 13;  |HACIENDO nCampo = nCampo + 1;
               |C_M #95nCampo, 1, "N";
               #95nCampo = 0;  |PINTA #95nCampo;
         |SIGUE;
     |FINSI;
|FPRC;

|PROCESO NoMasEmpresas;
     |SI #95Campo = 0;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 13;  |HACIENDO nCampo = nCampo + 1;
               #95nCampo = 0;  |C_M #95nCampo, 1, "N";
         |SIGUE;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 13;  |HACIENDO nCampo = nCampo + 1;
               |C_M #95nCampo, 1, "S";
         |SIGUE;
     |FINSI;
|FPRC;

|| ////////////////////////////////////////////////////////////////////////
|PROCESO Actual_m004;
     #4#0 = nEmpresa;
     #4#1 = nEjer;
     #4#2 = nPeriodo;
     #4#3 = nModelo;
     #4#4 = nComplem;
     |LEE 101#4=;
     |SI FSalida = 0;
         #4#15 = #4#15 - nImpAnt + nImpNuevo;
         |GRABA 020#4a;
     |FINSI;
     |LIBERA #4;
|FPRC;

|PROCESO Lee_m004;
    |DDEFECTO #agifigen;
    #agifigen#0 = nEmpresa;
    |LEE 041#agifigen.=;
    eaNombreEmp = #1000#1;

     nNoModi = 0;
     #4#0 = nEmpresa;
     #4#1 = nEjer;
     #4#2 = nPeriodo;
     #4#3 = nModelo;
     #4#4 = nComplem;
     |LEE 101#4=;
     |SI FSalida = 0;
         |SI #4#9 = "X"; || Esta Impreso
             nNoModi = 1;
         |FINSI;
         |SI #4#11 = "X"; || Modelo Remesado No modificar
             nNoModi = 1;
         |FINSI;
         |SI #4#13 = "X"; || Modelo ya Archivado. No se puede Modificar
             nNoModi = 1;
         |FINSI;
     |FINSI;
     |LIBERA #4;

     aCoBien = "N";
     #114#0 = nEmpresa;
     #114#1 = 00;
     |LEE 041#114];
     |SI FSalida = 0; |Y #114#0 = nEmpresa;
         aCoBien = #114#13;  ||Comunidad de Bienes
     |FINSI;
     |SI aCoBien = "S"; nNoModi = 1; |ACABA; |FINSI;
|FPRC;

|| ----------------------------------------------------------------------
|PROCESO dsmod131_neg;

     |SI #1131#33 < 0; nSwNeg = 1; |FINSI;

     |SI #1131#33 ] 0;  |Y nSwNeg = 0; |ACABA;  |FINSI;

     nSumaD = nSumaD + #1131#38;

     |SI #1131#33 < 0;
         #131#39 = #131#39 + -#1131#33;
     |SINO;
         #131#39 = #131#39 - #1131#38;
         |SI #131#39 [ 0; nSwNeg = 0; #131#39 = 0; nSumaD = 0; |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE dsmod131_neg;
     #1131#1006;
     ;
     #131#0, #131#1,     3, #131#3,  0, #131#5;
     #131#0, #131#1, nHPer, #131#3, 99, #131#5;
     ;
     NULL, dsmod131_neg;
|FIN;

|PROCESO CapturaNegativos131;
     |SI #131#38 [ 0;  |ACABA;  |FINSI;

     |DBASE aBase;  |QBF aBase;
     aMas = aBase + "def/dsmod131.mas";
     |CARGA_DEF aMas, dsmod131@;

     |ABRE #dsmod131@;
     nSwNeg   = 0;
     nSumaD   = 0;
     #131#39  = 0;
     nHPer    = #131#2 - 3;
     |PARA nNume1 = 3; |SI nNume1 [ nHPer; |HACIENDO nNume1 = nNume1 + 3;
           |DDEFECTO #1131;
           #1131#0 = #131#0;
           #1131#1 = #131#1;
           #1131#2 = nNume1;
           #1131#3 = #131#3;
           #1131#4 = 99;
           #1131#5 = #131#5;
           |LEE 040#1131];
           |SI FSalida = 0;
               |LEE 040#1131a;
           |SINO;
               |LEE 040#1131u;
           |FINSI;
           |SI FSalida = 0; |Y #1131#0 = #131#0;|Y #1131#1 = #131#1;
                       |Y #1131#2 = nNume1; |Y #1131#3 = #131#3;
               |HAZ dsmod131_neg;
           |FINSI;
     |SIGUE;

     |SI #131#39 > #131#38;  #131#39 = #131#38;  |FINSI;
     |CIERRA #dsmod131@;
     |DESCARGA_DEF #dsmod131@;
|FPRC;

|PROCESO dsmod131_2;
     |SI #1131#33 > 0;
         #131#41 = #131#41 + #1131#33;
     |FINSI;
|FPRC;

|DEFBUCLE dsmod131_2;
     #1131#1006;
     ;
     #131#0, #131#1, #131#2, #131#3,        0, #131#5;
     #131#0, #131#1, #131#2, #131#3, nHComple, #131#5;
     ;
     NULL, dsmod131_2;
|FIN;

|PROCESO CapturaOtros131;
     |DBASE aBase;  |QBF aBase;
     aMas = aBase + "def/dsmod131.mas";
     |CARGA_DEF aMas, dsmod131@;

     nHComple = #131#4 - 1;
     #131#41  = 0;

     |BUCLE dsmod131_2;

     |DESCARGA_DEF #dsmod131@;
|FPRC;

|PROCESO CapturaTope131;
     |DBASE aBase;  |QBF aBase;
     aMas = aBase + "def/dsmod131.mas";
     |CARGA_DEF aMas, dsmod131@;

     |ABRE #dsmod131@;
     nTope131 = 0;
     nHPer    = #131#2 - 3;
     |PARA nNume1 = 3; |SI nNume1 [ nHPer; |HACIENDO nNume1 = nNume1 + 3;
           |DDEFECTO #1131;
           #1131#0 = #131#0;
           #1131#1 = #131#1;
           #1131#2 = nNume1;
           #1131#3 = #131#3;
           #1131#4 = 99;
           #1131#5 = #131#5;
           |LEE 040#1131];
           |SI FSalida = 0;
               |LEE 040#1131a;
           |SINO;
               |LEE 040#1131u;
           |FINSI;
           |SI FSalida = 0; |Y #1131#0 = #131#0;|Y #1131#1 = #131#1;
                       |Y #1131#2 = nNume1; |Y #1131#3 = #131#3;
               nTope131 = nTope131 + #1131#44;
           |FINSI;
     |SIGUE;

     |CIERRA #dsmod131@;
     |DESCARGA_DEF #dsmod131@;
|FPRC;

|PROCESO CalculaDed131;
     #131#39 = 0;
     #131#38 = #131#36 - #131#37 - #131#42;
     |SI #131#38 > 0;
         |SI #131#2 ! 3;
             |HAZ CapturaNegativos131;
         |FINSI;
     |SINO;
         #131#39 = 0;
         #131#44 = 0;
         |ACABA;
     |FINSI;

     nSuma = #131#39 + #131#44;
     |SI nSuma > #131#38;
         |SI #131#39 > #131#38;
             #131#39 = #131#38;
             #131#44 = 0;
             |ACABA;
         |FINSI;

         #131#44 = #131#38 - #131#39;
     |FINSI;
|FPRC;

|PROCESO RecalModelo_131;
     nEmpresa  = #131#0;
     nEjer     = #131#1;
     nPeriodo  = #131#2;
     nComplem  = #131#4;

     |HAZ Lee_m004; |SI nNoModi = 1; |ACABA; |FINSI;

     #131#26 = #131#9  + #131#13 + #131#17 + #131#21 + #131#25;
     #131#43 = #131#7  + #131#11 + #131#15 + #131#19 + #131#23;

     enModeloAp   = #131#3;
     enEmpresaAp  = #131#0;
     eaNombreAp   = eaNombreEmp;
     enEjerAp     = #131#1;
     enPeriodoAp  = #131#2;
     enRend_NoAgr = #131#43;
     enRend_Agr   = #131#29;
     |HAZ eLeeAplica;

     nImpAnt   = #131#33;
     #131#39   = 0;
     #131#41   = 0;
     #131#42   = 0;
     |SI #131#1 = 2008; |Y aAplica1 ! "N";
         |SI #131#2 = 6;   #131#42 = 200;  |FINSI;
         |SI #131#2 = 9;   #131#42 = 100;  |FINSI;
         |SI #131#2 = 12;  #131#42 = 100;  |FINSI;
     |FINSI;

     |SI #131#1 = 2009; |Y aAplica1 ! "N";
         |SI #131#2 = 3;   #131#42 = 100;  |FINSI;
         |SI #131#2 = 6;   #131#42 = 100;  |FINSI;
         |SI #131#2 = 9;   #131#42 = 100;  |FINSI;
         |SI #131#2 = 12;  #131#42 = 100;  |FINSI;
     |FINSI;

     |SI #131#1 ] 2010; |Y aAplica1 ! "N";
         #131#42 = enApli400;
     |FINSI;

     #131#44 = 0;
     |SI #131#1 ] 2009; |Y aAplica2 = "S";
         |SI #131#26 ! 0;
             #131#44 = (#131#43 % .5) + (#131#34 % 2);
         |SINO;
             #131#44 = #131#29 % 2;
         |FINSI;

         |HAZ CapturaTope131;
         |SI nAplica3 = 0;
             nTope = 660.14 - nTope131;
         |SINO;
             nTope = 440 - nTope131;
         |FINSI;
         |SI nTope < 0;  nTope = 0;  |FINSI;
         |SI #131#44 > nTope;
             #131#44 = nTope;
         |FINSI;
     |FINSI;

     #131#36 = #131#26 + #131#35 + #131#30;
     #131#37 = #131#27 + #131#31;

     |HAZ CalculaDed131;

     #131#40 = #131#38 - #131#39 - #131#44;;

     nEl33 = #131#40;
     |SI #131#4 > 0;
         |HAZ CapturaOtros131;
         nEl33 = nEl33 - #131#41;
     |FINSI;

     #131#33 = #131#40 - #131#41;
     |GRABA 020#131a;
     |LIBERA #131;

     nImpNuevo = #131#33;
     |HAZ Actual_m004;
|FPRC;

|DEFBUCLE dsmod131;
    #131#1;
    ;
    nDEmpresa, nEjer, 01, nModelo, 00, 00;
    nHEmpresa, nEjer, 12, nModelo, 99, 99;
    ;
    NULL, RecalModelo_131;
|FIN;

|| ---- 130
|PROCESO dsmod130_neg;

     |SI #1130#1 = 2008;  |Y #1130#2 [ 3;
          nImporte = #1130#17 - #1130#18;
     |SINO;
          nImporte = #1130#23;
     |FINSI;

     |SI nImporte < 0; nSwNeg = 1; |FINSI;

     |SI nImporte ] 0;  |Y nSwNeg = 0; |ACABA;  |FINSI;

     nSumaD = nSumaD + #1130#20;

     |SI nImporte < 0;
         #130#21 = #130#21 + -nImporte;
     |SINO;
         #130#21 = #130#21 - #1130#20;
         |SI #130#21 [ 0; nSwNeg = 0; #130#21 = 0; nSumaD = 0; |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE dsmod130_neg;
     #1130#1006;
     ;
     #130#0, #130#1,     3, #130#3,  0, #130#5;
     #130#0, #130#1, nHPer, #130#3, 99, #130#5;
     ;
     NULL, dsmod130_neg;
|FIN;

|PROCESO CapturaNegativos130;
     |SI #130#20 [ 0;  |ACABA;  |FINSI;

     |DBASE aBase;  |QBF aBase;
     aMas = aBase + "def/dsmod130.mas";
     |CARGA_DEF aMas, dsmod130@;

     |ABRE #dsmod130@;
     nSwNeg   = 0;
     nSumaD   = 0;
     #130#21  = 0;
     nHPer    = #130#2 - 3;
     |PARA nNume1 = 3; |SI nNume1 [ nHPer; |HACIENDO nNume1 = nNume1 + 3;
           |DDEFECTO #1130;
           #1130#0 = #130#0;
           #1130#1 = #130#1;
           #1130#2 = nNume1;
           #1130#3 = #130#3;
           #1130#4 = 99;
           #1130#5 = #130#5;
           |LEE 040#1130];
           |SI FSalida = 0;
               |LEE 040#1130a;
           |SINO;
               |LEE 040#1130u;
           |FINSI;
           |SI FSalida = 0; |Y #1130#0 = #130#0;|Y #1130#1 = #130#1;
                       |Y #1130#2 = nNume1; |Y #1130#3 = #130#3;
               |HAZ dsmod130_neg;
           |FINSI;
     |SIGUE;

     |CIERRA #dsmod130@;
     |DESCARGA_DEF #dsmod130@;
|FPRC;

|PROCESO dsmod130_2;
     |SI #1130#1 = 2008;  |Y #1130#2 [ 3;
          nImporte = #1130#17 - #1130#18;
     |SINO;
          nImporte = #1130#23;
     |FINSI;
     |SI nImporte < 0; nImporte = 0; |FINSI;
     #130#18 = #130#18 + nImporte;
|FPRC;

|DEFBUCLE dsmod130_2;
     #1130#1006;
     ;
     #130#0, #130#1, #130#2, #130#3,        0, #130#5;
     #130#0, #130#1, #130#2, #130#3, nHComple, #130#5;
     ;
     NULL, dsmod130_2;
|FIN;

|PROCESO CapturaOtros130;
     |DBASE aBase;  |QBF aBase;
     aMas = aBase + "def/dsmod130.mas";
     |CARGA_DEF aMas, dsmod130@;

     nHComple = #130#4 - 1;
     #130#18  = 0;

     |BUCLE dsmod130_2;

     |DESCARGA_DEF #dsmod130@;
|FPRC;

|PROCESO CapturaTopeAgrario;
     |DBASE aBase;  |QBF aBase;
     aMas = aBase + "def/dsmod130.mas";
     |CARGA_DEF aMas, dsmod130@;

     |ABRE #dsmod130@;
     nTopeAgrario = 0;
     nHPer        = #130#2 - 3;
     |PARA nNume1 = 3; |SI nNume1 [ nHPer; |HACIENDO nNume1 = nNume1 + 3;
           |DDEFECTO #1130;
           #1130#0 = #130#0;
           #1130#1 = #130#1;
           #1130#2 = nNume1;
           #1130#3 = #130#3;
           #1130#4 = 99;
           #1130#5 = #130#5;
           |LEE 040#1130];
           |SI FSalida = 0;
               |LEE 040#1130a;
           |SINO;
               |LEE 040#1130u;
           |FINSI;
           |SI FSalida = 0; |Y #1130#0 = #130#0; |Y #1130#1 = #130#1;
                            |Y #1130#2 = nNume1; |Y #1130#3 = #130#3;
               nTopeAgrario = nTopeAgrario + #1130#24;
           |FINSI;
     |SIGUE;

     |CIERRA #dsmod130@;
     |DESCARGA_DEF #dsmod130@;
|FPRC;

|PROCESO CalculaDed130;
     #130#20 = #130#17 - #130#19;
     |SI #130#20 > 0;
         |SI #130#2 ! 3;
             |HAZ CapturaNegativos130;
         |FINSI;
     |SINO;
         #130#21 = 0;
         #130#24 = 0;
         |ACABA;
     |FINSI;

     nSuma = #130#21 + #130#24;
     |SI nSuma > #130#20;
         |SI #130#21 > #130#20;
             #130#21 = #130#20;
             #130#24 = 0;
             |ACABA;
         |FINSI;

         #130#24 = #130#20 - #130#21;
     |FINSI;
|FPRC;

|PROCESO RecalModelo_130;
     |SI nEmpresa ! #130#0; |O nEjer ! #130#1;
         nPagAnt = 0;
     |FINSI;

     nEmpresa  = #130#0;
     nEjer     = #130#1;
     nPeriodo  = #130#2;
     nComplem  = #130#4;

     |HAZ Lee_m004; |SI nNoModi = 1; |ACABA; |FINSI;

     enModeloAp  = #130#3;
     enEmpresaAp = #130#0;
     eaNombreAp  = eaNombreEmp;
     enEjerAp    = #130#1;
     enPeriodoAp = #130#2;
     enRend_NoAgr = #130#8;
     enRend_Agr   = #130#13;
     |HAZ eLeeAplica;

     #130#10 = nPagAnt;
     #130#12 = #130#9 - #130#10 - #130#11;
     |SI #130#12 < 0;  #130#12 = 0;  |FINSI;
     #130#17 = #130#12 + #130#16;

     #130#19 = 0;
     |SI #130#1 = 2008; |Y aAplica1 ! "N";
         |SI #130#2 = 6;   #130#19 = 200;  |FINSI;
         |SI #130#2 = 9;   #130#19 = 100;  |FINSI;
         |SI #130#2 = 12;  #130#19 = 100;  |FINSI;
     |FINSI;

     |SI #130#1 = 2009; |Y aAplica1 ! "N";
         |SI #130#2 = 3;   #130#19 = 100;  |FINSI;
         |SI #130#2 = 6;   #130#19 = 100;  |FINSI;
         |SI #130#2 = 9;   #130#19 = 100;  |FINSI;
         |SI #130#2 = 12;  #130#19 = 100;  |FINSI;
     |FINSI;

     |SI #130#1 ] 2010; |Y aAplica1 ! "N";
         #130#19 = enApli400;
     |FINSI;

     #130#24 = 0;
     |SI #130#1 ] 2009; |Y aAplica2 = "S";
         |SI #130#8 > 0;
             #130#24 = #130#8 % 2;
             |SI nAplica3 = 0;
                 |SI #130#24 > 660.14;
                     #130#24 = 660.14;
                 |FINSI;
             |SINO;
                 |SI #130#24 > 440;
                     #130#24 = 440;
                 |FINSI;
             |FINSI;
         |SINO;
             |SI #130#13 > 0;
                 #130#24 = #130#13 % 2;
                 |HAZ CapturaTopeAgrario;
                 |SI nAplica3 = 0;
                     nTope = 660.14 - nTopeAgrario;
                 |SINO;
                     nTope = 440 - nTopeAgrario;
                 |FINSI;
                 |SI nTope < 0;  nTope = 0;  |FINSI;
                 |SI #130#24 > nTope;
                     #130#24 = nTope;
                 |FINSI;
             |FINSI;
         |FINSI;
     |FINSI;

     nImpAnt   = #130#17 - #130#18;
     |SI #130#1 ] 2008;
         |SI #130#1 > 2008; |O #130#2 ] 4;
             |SI #130#20 ! 0; |O #130#21 ! 0; |O #130#22 ! 0; |O #130#23 ! 0;
                 nImpAnt = #130#23;  ||ya estaba calculado
             |FINSI;
         |FINSI;
     |FINSI;

     |HAZ CalculaDed130;

     #130#22 = #130#20 - #130#21 - #130#24;

     nEl23   = #130#22;
     #130#18 = 0;
     |SI #130#4 > 0;
         |HAZ CapturaOtros130;
         nEl23 = nEl23 - #130#18;
     |FINSI;
     #130#23 = #130#22 - #130#18;

     |GRABA 020#130a;
     |LIBERA #130;

     nImpNuevo = #130#23;
     |HAZ Actual_m004;

     nPagAnt = #130#10;
     |SI #130#1 [ 2008;
         |SI #130#12 > 0; nPagAnt = nPagAnt + #130#12; |FINSI;
         |SI nPagAnt < 0; nPagAnt = 0; |FINSI;
     |SINO;
         |SI #130#12 > 0; nPagAnt = nPagAnt + #130#12; |FINSI;
                          nPagAnt = nPagAnt - #130#24;
     |FINSI;
|FPRC;

|DEFBUCLE dsmod130;
    #130#1;
    ;
    nDEmpresa, nEjer, 01, nModelo, 00, 00;
    nHEmpresa, nEjer, 12, nModelo, 99, 99;
    ;
    NULL, RecalModelo_130;
|FIN;

|PROCESO Tipo3;  |TIPO 3;
     nEjer = #95#14;
     nEmpresa = 0;
     nPagAnt  = 0;
     |ABRE #4;
     |ABRE #agifigen;
     |ABRE #114;
     |SI #95#0 ! 0;
         nDEmpresa = #95#0;
         nHEmpresa = #95#1;
         |SI #95#15 ! 1; nModelo = 130; |BUCLE dsmod130; |FINSI;
         |SI #95#15 ! 0; nModelo = 131; |BUCLE dsmod131; |FINSI;
     |SINO;
         |PARA nCampo = 2;  |SI nCampo [ 13;  |HACIENDO nCampo = nCampo + 1;
               nDEmpresa = #95nCampo;
               nHEmpresa = #95nCampo;
               |SI #95#15 ! 1; nModelo = 130; |BUCLE dsmod130; |FINSI;
               |SI #95#15 ! 0; nModelo = 131; |BUCLE dsmod131; |FINSI;
         |SIGUE;
     |FINSI;
     |CIERRA #114;
     |CIERRA #agifigen;
     |CIERRA #4;
|FPRC;

|PROGRAMA;
     aParam = PARAMETRO; |QBF aParam;

     |SI aParam ! "";
         |DDEFECTO #95;
         |SI aParam = "130"; #95#15 = 0; |FINSI;
         |SI aParam = "131"; #95#15 = 1; |FINSI;

         enSwApli   = 4;
         |SI enElEjer = 0;
             #95#14 = 2008;
         |SINO;
             #95#14 = enElEjer;
             #95#0  = enDEmpresa;
             #95#1  = enHEmpresa;
         |FINSI;
         |HAZ Tipo3;
     |SINO;
          enSwApli   = 0;
          |MANTE #95;
     |FINSI;
|FPRO;
