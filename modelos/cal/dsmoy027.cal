|FICHEROS;
    dsmoy027;

    dsmom001;
    dsmom004;
    dsmom038;
    dsmom107;

    dsm193ca #193;
    dsm193li;
    dsm193ga;

    :/basica/def/agim0024;
    :/basica/def/agim0026;
    :/basica/def/agim0029;

    :/integral/def/dsam0003;

    &regisnif@;
    &clientes@;

    dsmow036;
    dsmow014;
|FIN;

|VARIABLES;
     nSwGraba            = 0;
     nCampo              = 0;
     nDEmpresa           = 0;
     nHEmpresa           = 0;
     nEjer               = 0;
     nContador           = 0;
     nSwPrimer           = 0;
     nSwHay              = 0;
     sw_valido           = 0;
     nVale               = 0;
     nHayRepre           = 0;
     nLectura            = 0;
     nDComple            = 0;
     nHComple            = 0;
     nCanal              = 0;
     nPara1              = 0;
     nArchivo            = 0;
     nOpcion             = 0;

     aBase               = "";
     aCampo10            = "";
     aAlfa1              = "";
     aAlfa2              = "";
     aAlfa3              = "";
     aFichero            = "";
     aPath               = "";
     eaMoneda            = "";
     aElDni              = "";
     aAlfa               = "";
     aSClave             = "";
     aClave              = "";
     aClave1             = "";
     aClave2             = "";
     aClave3             = "";

     fecsys              = @;

     {-1}aMenu           = "";
         aMenu1          = "2400";
         aMenu2          = "1";
         aMenu3          = "Archivo documental activado. Elija opci¢n: ";
         aMenu4          = "IAB";
         aMenu5          = "Imprimir";
         aMenu6          = "Archivar";
         aMenu7          = "Ambos";

     &eaParam  = "";
     &eaTMoneda = "";
     &periodo = 0;
     &codemp = "";
     &nomrep = "";
     &dnirep = "";
     &carrep = "";
     &nomemp = "";
     &dniemp = "";
     &domemp = "";
     &munemp = "";
     &cpoemp = "";
     &proemp = "";
     &nomtra = "";
     &dnitra = "";
     &domtra = "";
     &muntra = "";
     &cpotra = "";
     &protra = "";

     &fletra = "";
     &fprese = @;
     &tbases1 = 0;
     &tbases2 = 0;
     &tbases3 = 0;
     &tbases4 = 0;
     &tbases5 = 0;
     &tbases6 = 0;
     &treten1 = 0;
     &treten2 = 0;
     &treten3 = 0;
     &treten4 = 0;
     &treten5 = 0;
     &treten6 = 0;
     &trep1 = 0;
     &trep2 = 0;
     &trep3 = 0;
     &trep4 = 0;
     &tdescue = 0;
     &texenta = 0;
     &letrai = 0;
     &clave1  = "";
     &clave2  = "";
     &clave3  = "";
     &clave4  = "";
     &especie1    = "";
     &especie2    = "";
     &especie3    = "";
     &xespe4      = "";
     &xespe5      = "";
     &xespe6      = "";
     &xespe7      = "";
     &eaPathIntegral = "";
     &eaCodRenta     = "";

     &enErrorDSArchi     = 0;
     &eaVengoDe          = "";
     &enArchi            = 0;
     &eaInforme          = "";
     &enEjercicio        = 0;
     &eaNifEmp           = "";
     &enCodTrab          = 0;
     &eaNombreTra        = "";
     &eaNifTra           = "";
     &eaObservacion1     = "";
     &eaObservacion2     = "";
     &eaObservacion3     = "";
     &enM2               = 0;
|FIN;

|INCLUYE i_variar;

|| /////////////////////// //////////////////////////////////////////////////
|| /////////////////////// PROCESOS DE INFORMES \\\\\\\\\\\\\\\\\\\\\
|| \\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\

|PROCESO LeeCodRenta;
     eaCodRenta = "";

     |ABRE #dsam0003;
     |SELECT #2#dsam0003;
     #dsam0003#1 = aElDni;
     |LEE 040#dsam0003.=;
     |SI FSalida = 0;
         eaCodRenta = "Codigo de Renta : " + (("00000" + #dsam0003#0) % -105);
     |SINO;
         |SELECT #3#dsam0003;
         #dsam0003#35 = #dsmow036#0;
         |LEE 040#dsam0003.=;
         |SI FSalida = 0;
             eaCodRenta = "Codigo de Renta : " + (("00000" + #dsam0003#0) % -105);
         |FINSI;
     |FINSI;
     |CIERRA #dsam0003;
|FPRC;

|PROCESO ElTrabajador;
     especie1 = "";
     especie2 = "";
     especie3 = "";
     xespe4   = "N";
     xespe5   = "N";
     xespe6   = "";
     xespe7   = "";

     tbases1  = 0;
     tbases2  = 0;
     tbases3  = 0;
     tbases4  = 0;
     tbases5  = 0;
     tbases6  = 0;

     treten1  = 0;
     treten2  = 0;
     treten3  = 0;
     treten4  = 0;
     treten5  = 0;
     treten6  = 0;

     trep1    = 0;
     trep2    = 0;
     trep3    = 0;
     trep4    = 0;

     tdescue  = 0;
     texenta  = 0;
     aClave1  = "";
     aClave2  = "";
     aClave3  = "";
     aClave   = "";

     nContador = 0;
     aElDni    = #dsm193li#6;

     eaCodNif  = #dsm193li#6;
     |HAZ LeeNifs;

     nomtra = #regisnif@#1;
     dnitra = aElDni;

     aAlfa1 = #regisnif@#9; |QBF aAlfa1;
     |SI aAlfa1 ! ""; aAlfa1 = aAlfa1 + " "; |FINSI;

     aAlfa2 = #regisnif@#2; |QBF aAlfa2;
     |SI aAlfa2 ! ""; aAlfa2 = aAlfa2 + " "; |FINSI;

     aAlfa3 = #regisnif@#3;

     domtra = aAlfa1 + aAlfa2 + aAlfa3;
     muntra = #regisnif@#6;
     protra = #regisnif@#7;

     aAlfa1 = #regisnif@#4; |QBF aAlfa1; aAlfa1 = ("00" + aAlfa1) % -102;
     aAlfa2 = #regisnif@#5; |QBF aAlfa2; aAlfa2 = ("000" + aAlfa2) % -103;
     cpotra = aAlfa1 + aAlfa2;

     eaCodRenta = "";
     |SI #dsmoy027#33 = "S";
         |HAZ LeeCodRenta;
     |FINSI;
|FPRC;

|| **********************************************************************
____________________________________/ CALCULOS DE LECTURA E IMPRESION

||/////////////////////////////////////////////////////////////////////////
||///// Calculos para el Tipo 1 de Informe ////////////////////////////////
||/////////////////////////////////////////////////////////////////////////

|PROCESO Suma1;
     |SI aCampo10 = "N";
         tbases1 = tbases1 + #dsm193li#23;     || bases
         treten1 = treten1 + #dsm193li#25;     || cuota
     |SINO;
         tbases4 = tbases4 + #dsm193li#23;     || bases
         treten4 = treten4 + #dsm193li#25;     || cuota
         xespe4 = "S";
     |FINSI;

     aClave1 = #dsm193li#8;
|FPRC;

|PROCESO Suma2;
     |SI aCampo10 = "N";
         tbases2 = tbases2 + #dsm193li#23;     || bases
         treten2 = treten2 + #dsm193li#25;     || cuota
     |SINO;
         tbases5 = tbases5 + #dsm193li#23;     || bases
         treten5 = treten5 + #dsm193li#25;     || cuota
         xespe4  = "S";
     |FINSI;

     aClave2 = #dsm193li#8;
|FPRC;

|PROCESO Suma3;
     |SI aCampo10 = "N";
         tbases3 = tbases3 + #dsm193li#23;     || bases
         treten3 = treten3 + #dsm193li#25;     || cuota
     |SINO;
         tbases6 = tbases6 + #dsm193li#23;     || bases
         treten6 = treten6 + #dsm193li#25;     || cuota
         xespe4 = "S";
     |FINSI;

     aClave3 = #dsm193li#8;
|FPRC;

||/////////////////////////////////////////////////////////////////////////
||///// Calculos para el Tipo 2 de Informe ////////////////////////////////
||/////////////////////////////////////////////////////////////////////////

|PROCESO CargaVar;
     eaObservacion1 = "Certificado rdtos.Capital mobiliario ";
     || eaObservacion2 = "Ejercicio " + #dsmoy027#14;

     enEjercicio = #dsmoy027#14;
     enEmpresa   = #dsm193ca#0;
     eaNombreEmp = nomemp;
     eaNifEmp    = dniemp;
     eaNombreTra = nomtra;
     eaNifTra    = dnitra;
     enM2        = 99;
     enCodTrab   = 0;
|FPRC;

|PROCESO dsmow036;
     |SI nSwPrimer = 0; |O aElDni ! #dsmow036#0;
         #dsm193li#6  = #dsmow036#0;
         |HAZ ElTrabajador;
         nSwPrimer = 1;
     |FINSI;

     |ABRE #dsmom038;
     |DDEFECTO #dsmom038;
     #dsmom038#0 = #dsmow036#1;
     #dsmom038#1 = #dsmow036#2;
     |LEE 001#dsmom038.=;
     |CIERRA #dsmom038;

     |SI nArchivo = 1;
         |HAZ IMPREArchi;

         |PRINT;
         |PIEINF;
         |FININF;
         |FINIMP;

         |SI enErrorDSArchi = 0;
             |HAZ CargaVar;
             |HAZ Archivando;
         |FINSI;
     |FINSI;

     |SI nArchivo = 0;
         |PRINT;
         |PIEINF;
     |FINSI;
|FPRC;

|DEFBUCLE dsmow036;
     #dsmow036#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, dsmow036;
|FIN;

|PROCESO Imprimir;
     aElDni    = "";
     nSwPrimer = 0;
     aElDni    = "";
     |BUCLE dsmow036;
|FPRC;

|PROCESO FechaPres;
     |ABRE #dsmom004;
     #dsmom004#0 = #dsm193ca#0;
     #dsmom004#1 = #dsm193ca#1;
     #dsmom004#2 = #dsm193ca#2;
     #dsmom004#3 = #dsm193ca#3;
     #dsmom004#4 = #dsm193ca#4;
     |LEE 000#dsmom004.=;
     |CIERRA #dsmom004;

     fprese = #dsmom004#10;
|FPRC;


|PROCESO agim0026;
     nomrep = #agim0026#4;
     dnirep = #agim0026#3;
     carrep = #agim0026#17;
|FPRC;

|DEFBUCLE agim0026;
     #agim0026#1;
     16;
     #dsm193ca#0, 01, 01, #dsmoy027#32;
     #dsm193ca#0, 99, 99, #dsmoy027#32;
     ;
     NULL, agim0026;
|FIN;

|PROCESO BuscaCargo193R;
     |SI #agim0024#15 ! "A";
         nomrep = #agim0024#4;
         dnirep = #agim0024#3;
         carrep = "REPRESENTANTE";
         nHayRepre = 1;
         |ERROR10;
     |FINSI;
|FPRC;

|DEFBUCLE agim0024;
     #agim0024#2;
     ;
     #dsm193ca#0, 99, 01;
     #dsm193ca#0, 01, 99;
     ;
     NULL, BuscaCargo193R;
|FIN;

|PROCESO BuscaComuneros;
     aAlfa = "31.12." + #dsm193ca#1;
     |SI #dsmom001#8 > aAlfa;  |ACABA;  |FINSI;

     aAlfa = "01.01." + #dsm193ca#1;
     |SI #dsmom001#9 > "01.01.0000";
         |SI #dsmom001#9 < aAlfa;  |ACABA;  |FINSI;
     |FINSI;

     nHayRepre = 1;

     enCodCli = #dsmom001#3;
     |HAZ LeePersona;

     |SI enSwDeDonde = 1;
         nomrep = #clientes@#1;
         dnirep = #clientes@#3;
         carrep = "REPRESENTANTE";
     |SINO;
         nomrep = #clientes@#1;
         dnirep = #clientes@#13;
         carrep = "REPRESENTANTE";
     |FINSI;

     |ERROR10;
|FPRC;

|DEFBUCLE dsmom001;
     #dsmom001#1;
     ;
     #dsm193ca#0, 01, 0001;
     #dsm193ca#0, 99, 9999;
     ;
     NULL, BuscaComuneros;
|FIN;

|PROCESO dsm193li;
     #dsm193ga#0 = #dsm193li#0;
     #dsm193ga#1 = #dsm193li#1;
     #dsm193ga#2 = #dsm193li#2;
     #dsm193ga#3 = #dsm193li#3;
     #dsm193ga#4 = #dsm193li#4;
     #dsm193ga#5 = #dsm193li#5;
     #dsm193ga#6 = #dsm193li#6;
     |LEE 000#dsm193ga.=;
     |SI FSalida ! 0; |DDEFECTO #dsm193ga; |FINSI;

     |SI #dsmoy027#19 = "            ";
         nVale = 0;
         |PARA nCampo = 21; |SI nCampo [ 28; |HACIENDO nCampo = nCampo + 1;
               |SI #dsmoy027#nCampo# ! "            ";
                   nVale = 1;
                   |SI #dsmoy027#nCampo# = #dsm193li#6;
                       nVale = 2;
                       |SAL;
                   |FINSI;
               |FINSI;
          |SIGUE;
     |FINSI;

     |SI nVale = 1; |ACABA; |FINSI;

     |SI nSwPrimer = 0; |O aElDni ! #dsm193li#6;
         aElDni    = #dsm193li#6;
         nSwPrimer = 1;
     |FINSI;

     || ............................. Sumo Cantidades donde van

     aClave  = #dsm193li#8;
     aSClave = #dsm193li#9;

     #dsmow036#0 = aElDni;
     #dsmow036#1 = aClave;
     #dsmow036#2 = aSClave;
     |LEE 000#dsmow036.=;
     |SI FSalida ! 0;
         |DDEFECTO #dsmow036;
         #dsmow036#0 = aElDni;
         #dsmow036#1 = aClave;
         #dsmow036#2 = aSClave;
         |GRABA 020#dsmow036.c;
     |FINSI;

     |SI #dsm193li#7 ! 0;
          |SI #dsmow036#13 = 0; |O #dsmow036#13 = #dsm193li#7;
              #dsmow036#13 = #dsm193li#7;
              #dsmow036#14 = #dsmow036#14 + #dsm193li#25;
              #dsmow036#15 = #dsmow036#15 + #dsm193li#23;
          |SINO;
              |SI #dsmow036#16 = 0; |O #dsmow036#16 = #dsm193li#7;
                  #dsmow036#16 = #dsm193li#7;
                  #dsmow036#17 = #dsmow036#17 + #dsm193li#25;
                  #dsmow036#18 = #dsmow036#18 + #dsm193li#23;
              |SINO;
                  |SI #dsmow036#19 = 0; |O #dsmow036#19 = #dsm193li#7;
                      #dsmow036#19 = #dsm193li#7;
                      #dsmow036#20 = #dsmow036#20 + #dsm193li#25;
                      #dsmow036#21 = #dsmow036#21 + #dsm193li#23;
                  |SINO;
                      |SI #dsmow036#22 = 0; |O #dsmow036#22 = #dsm193li#7;
                          #dsmow036#22 = #dsm193li#7;
                          #dsmow036#23 = #dsmow036#23 + #dsm193li#25;
                          #dsmow036#24 = #dsmow036#24 + #dsm193li#23;
                      |FINSI;
                  |FINSI;
              |FINSI;
          |FINSI;
     |SINO;
          |SI #dsm193li#10 = 1;
              #dsmow036#3  = #dsmow036#3 + #dsm193li#23;
              #dsmow036#4  = #dsmow036#4 + #dsm193li#25;
              #dsmow036#14 = #dsmow036#14 + #dsm193li#27;
              #dsmow036#15 = #dsmow036#15 + #dsm193li#28;
          |FINSI;

          |SI #dsm193li#10 = 2;
              #dsmow036#5  = #dsmow036#5 + #dsm193li#23;
              #dsmow036#6  = #dsmow036#6 + #dsm193li#25;
              #dsmow036#7  = #dsmow036#7 + #dsm193li#26;
              #dsmow036#17 = #dsmow036#17 + #dsm193li#27;
              #dsmow036#18 = #dsmow036#18 + #dsm193li#28;
          |FINSI;
     |FINSI;

     #dsmow036#8 = #dsmow036#8 + #dsm193li#22;
     #dsmow036#9 = #dsmow036#9 + #dsm193ga#9;

     |GRABA 020#dsmow036.a;
|FPRC;

|DEFBUCLE dsm193li;
     #dsm193li#1;
     ;
     #193#0,#193#1,#193#2,#193#3,#193#4,#193#5,#dsmoy027#19,0000,"A","  ",1;
     #193#0,#193#1,#193#2,#193#3,#193#4,#193#5,#dsmoy027#20,9999,"D","zz",2;
     ;
     NULL, dsm193li;
|FIN;

|PROCESO dsm193ca;
     |SI nLectura = 0;
         |SI #dsm193ca#4 ! 0;
             |SI #dsm193ca#14 = "S";
                 nDComple = #dsm193ca#4;
                 nHComple = #dsm193ca#4;
             |SINO;
                 nHComple = #dsm193ca#4;
             |FINSI;
         |SINO;
             nDComple = #dsm193ca#4;
             nHComple = #dsm193ca#4;
         |FINSI;

         |ACABA;
     |FINSI;

     codemp = "";
     nomrep = "";
     dnirep = "";
     carrep = "";
     nomemp = "";
     dniemp = "";
     domemp = "";
     munemp = "";
     cpoemp = "";
     proemp = "";

     |SI #dsmom107#1 = "S";
         |BUCLE agim0026;
     |SINO;
         nHayRepre = 0;
         |BUCLE agim0024;
         |SI nHayRepre = 0;
             |BUCLE dsmom001;
             |SI nHayRepre = 0;
                 nomrep  = #dsmow014#3;
                 carrep  = "TITULAR";
             |FINSI;
         |FINSI;
    |FINSI;

    codemp = #dsm193ca#0; |QBF codemp; codemp = ("00000" + codemp) % -105;
    enCodCli = #dsm193ca#0;
    |HAZ LeePersona;

    |SI enSwDeDonde = 1;
          nomemp = #clientes@#1;  ||tique 1222_1187
          dniemp = #clientes@#3;
          aAlfa1 = #clientes@#29; |QBF aAlfa1; |SI aAlfa1 ! ""; aAlfa1 = aAlfa1 + " "; |FINSI;
          aAlfa2 = #clientes@#30; |QBF aAlfa2; aAlfa2 = aAlfa2 + " ";
          aAlfa3 = #clientes@#31;
          domemp = aAlfa1 + aAlfa2 + aAlfa3;
          munemp = #clientes@#37;
          aAlfa1 = #clientes@#35;  |QBF aAlfa1; aAlfa1 = ("00" + aAlfa1) % -102;
          aAlfa2 = #clientes@#36; |QBF aAlfa2; aAlfa2 = ("000" + aAlfa2) % -103;
          cpoemp = aAlfa1 + aAlfa2;
          proemp = #clientes@#38;
     |SINO;
          nomemp = #clientes@#2;  || tique 1222_1187
          dniemp = #clientes@#13;
          aAlfa1 = #clientes@#3; |QBF aAlfa1; |SI aAlfa1 ! ""; aAlfa1 = aAlfa1 + " "; |FINSI;
          aAlfa2 = #clientes@#4; |QBF aAlfa2; aAlfa2 = aAlfa2 + " ";
          aAlfa3 = #clientes@#5;
          domemp = aAlfa1 + aAlfa2 + aAlfa3;
          munemp = #clientes@#11;
          aAlfa1 = #clientes@#9;  |QBF aAlfa1; aAlfa1 = ("00" + aAlfa1) % -102;
          aAlfa2 = #clientes@#10; |QBF aAlfa2; aAlfa2 = ("000" + aAlfa2) % -103;
          cpoemp = aAlfa1 + aAlfa2;
          proemp = #clientes@#12;
     |FINSI;

     periodo = #dsmoy027#14;

     aAlfa1 = #dsmoy027#16;
     fecsys = aAlfa1 % 2;    || pasa la fecha a letras
     fletra = fecsys % 11;   || coge la parte literal de la fecha
     aAlfa1 = munemp; |QBF aAlfa1;
     fletra = aAlfa1 + ", a " + fletra;  || une la localidad a la fecha literal
     |HAZ FechaPres;

     |DELINDEX #dsmow036;

     nSwPrimer = 0;
     aElDni    = "";

     |ABRE #dsmow036;
     |ABRE #dsm193ga;
     |BUCLE dsm193li;
     |CIERRA #dsmow036;
     |CIERRA #dsm193ga;

     |SI nSwPrimer = 1;
         |HAZ Imprimir;
     |FINSI;
|FPRC;

|DEFBUCLE dsm193ca;
     #dsm193ca#1;
     ;
     #dsmow014#0, #dsmow014#1, #dsmow014#2, #dsmoy027#30, nDComple, 00;
     #dsmow014#0, #dsmow014#1, #dsmow014#2, #dsmoy027#30, nHComple, 99;
     ;
     NULL, dsm193ca;
|FIN;

|PROCESO dsmow014M;
    enEmpresa = #dsmow014#0;
    nSwHay    = nSwHay + 1;

    nDComple  = 0;
    nHComple  = 99;

    nLectura  = 0;  |BUCLE dsm193ca;
    nLectura  = 1;  |BUCLE dsm193ca;
|FPRC;

|DEFBUCLE dsmow014M;
     #dsmow014#1;
     6;
     00000, 0000, 00, "S";
     99999, 9999, 99, "S";
     ;
     NULL, dsmow014M;
|FIN;

|PROCESO Papel;
     Impresora = "CrystalReport";
     |IMPRE -1;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |INFOR eaInforme;
     |SI FSalida ! 0; |FINIMP; |ACABA; |FINSI;

     nSwHay   = 0;
     nArchivo = 0;

     |ABRE #dsm193ca;
     |BUCLE dsmow014M;
     |CIERRA #dsm193ca;
     |FININF;
     |FINIMP;
|FPRC;

|PROCESO Archivado;
     nSwHay   = 0;
     nArchivo = 1;

     |ABRE #dsm193ca;
     |BUCLE dsmow014M;
     |CIERRA #dsm193ca;
|FPRC;

|| ///////////////////////////////////////////////////

|PROCESO dsmom004;
     enEmpresaPINET = #dsmom004#0;
     |HAZ RestriccionPINET;
     |SI enErrorPINET = 1;  |ACABA;  |FINSI;

     enCodCli = #dsmom004#0;
     |HAZ LeePersona;

     #dsmow014#0  = #dsmom004#0;
     #dsmow014#1  = #dsmom004#1;
     #dsmow014#2  = #dsmom004#2;
     |LEE 101#dsmow014.=;
     |SI FSalida ! 0;
         |DDEFECTO #dsmow014;
         #dsmow014#0  = #dsmom004#0;
         #dsmow014#1  = #dsmom004#1;
         #dsmow014#2  = #dsmom004#2;
         |GRABA 020#dsmow014.b;
     |FINSI;

     #dsmow014#3  = #clientes@#1;
     #dsmow014#4  = "ANUAL";
     #dsmow014#5  = #dsmoy027#30;
     #dsmow014#6  = "S";
     #dsmow014#7  = #dsmom004#4;
     |GRABA 020#dsmow014.a;
     |LIBERA #dsmow014;

     nSwGraba = 1;
|FPRC;

|DEFBUCLE dsmom004;
     #dsmom004#1;
     ;
     nDEmpresa, #dsmoy027#14, #dsmoy027#31, #dsmoy027#30, 00;
     nHEmpresa, #dsmoy027#14, #dsmoy027#31, #dsmoy027#30, 99;
     ;
     NULL, dsmom004;
|FIN;

|PROCESO IT3;  |TIPO 3;
     |SI #dsmoy027#29 = "PESETAS";
         eaTMoneda = "Ptas.";
     |SINO;
         eaTMoneda = "Euros";
     |FINSI;

     eaMoneda  = #dsmoy027#29;
     eaInforme =  "dsmoyc27";

     enEjer    = #dsmoy027#14;        || A¤o Calculo
     aAlfa1    = #dsmoy027#14;
     aAlfa1    = aAlfa1 % -102;
     nEjer     = aAlfa1;       || Dos ultimos digitos del A¤o

     aFichero = ("cer" + Usuario) % 108;
     |NOME_DAT #dsmow014#aFichero#;
     |ABRE #dsmow014;  |CIERRA #dsmow014;  |DELINDEX #dsmow014;

     aFichero = ("w36" + Usuario) % 108;
     |NOME_DAT #dsmow036#aFichero#;

     nSwGraba = 0;
     |ABRE #dsmow014;
     |SI #dsmoy027#0 ! 0;
         nDEmpresa = #dsmoy027#0;
         nHEmpresa = #dsmoy027#1;
         |BUCLE dsmom004;
     |SINO;
         |PARA nPara1 = 2;  |SI nPara1 [ 13;  |HACIENDO nPara1 = nPara1 + 1;
               nDEmpresa = #dsmoy027#nPara1#;
               nHEmpresa = #dsmoy027#nPara1#;
               |BUCLE dsmom004;
         |SIGUE;
     |FINSI;
     |CIERRA #dsmow014;

     |SI nSwGraba = 0;
          |MENAV "     Seleccion Vacia.";
          |DELINDEX #dsmow036;
          |DELINDEX #dsmow014;
          |ACABA;
     |FINSI;

     eaVengoDe = "dsmoy027";
     |HAZ MiraSiArchiva;
     |SI enArchi = 0;
         |HAZ Papel;
     |SINO;
         |MENU aMenu;
         nOpcion = FSalida;
         |SI nOpcion = 1;  |HAZ Papel;      |FINSI;
         |SI nOpcion = 2;  |HAZ Archivado;  |FINSI;
         |SI nOpcion = 3;  |HAZ Papel;      |FINSI;
         |SI nOpcion = 3;  |HAZ Archivado;  |FINSI;
     |FINSI;

     |DELINDEX #dsmow014;
     |DELINDEX #dsmow036;
|FPRC;

|| /////////////////////// PROCESOS ENTRADA POR PANTALLA \\\\\\\\\\\\\\\\\\\\

|PROCESO CodigoRenta;
     |DBASS aBase;  |QBF aBase;
     aAlfa = aBase + "integral/def/dsam0000.mas";
     |XABRE "E", aAlfa, nCanal;
     |SI FSalida ] 0;
         eaPathIntegral  = aBase + "integral/";
     |FINSI;

     |C_M #dsmoy027#33, 1, "S";
     |SI eaPathIntegral = "";
         |C_M #dsmoy027#33, 1, "N";  #dsmoy027#33 = "N";  |PINTA #dsmoy027#33;
     |FINSI;
|FPRC;

|PROCESO T7;  |TIPO 7;
     |SI enEmpresa ! 0;
         #dsmoy027#0 = enEmpresa;  |PINTA #dsmoy027#0;
         #dsmoy027#1 = enEmpresa;  |PINTA #dsmoy027#1;

         |C_M #dsmoy027#0, 1, "N";
         |C_M #dsmoy027#1, 1, "N";
     |FINSI;
     aAlfa1 = #dsmoy027#16; aAlfa1 = aAlfa1 % -104;
     #dsmoy027#14 = aAlfa1;
     #dsmoy027#14 = #dsmoy027#14 - 1;
     |PINTA #dsmoy027#14;
|FPRC;

|PROCESO Parrilla;
     |SI #dsmoy027#0 = 0;
         #dsmoy027#0 = 0;  |PINTA #dsmoy027#0;
         #dsmoy027#1 = 0;  |PINTA #dsmoy027#1;  |C_M #dsmoy027#1, 1, "N";
         |PARA nCampo = 2;  |SI nCampo [ 13;  |HACIENDO nCampo = nCampo + 1;
               |C_M #dsmoy027#nCampo#, 1, "S";
         |SIGUE;
     |SINO;
         |SI enEmpresa = 0;
             |C_M #dsmoy027#0, 1, "S";
             |C_M #dsmoy027#1, 1, "S";
         |FINSI;

         |PARA nCampo = 2;  |SI nCampo [ 13;  |HACIENDO nCampo = nCampo + 1;
               |C_M #dsmoy027#nCampo#, 1, "N";
               #dsmoy027#nCampo# = 0;  |PINTA #dsmoy027#nCampo#;
         |SIGUE;
     |FINSI;
|FPRC;

|PROCESO NoMasEmpresas;
     |SI #dsmoy027#Campo# = 0;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 13;  |HACIENDO nCampo = nCampo + 1;
               #dsmoy027#nCampo# = 0;  |C_M #dsmoy027#nCampo#, 1, "N";
         |SIGUE;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 13;  |HACIENDO nCampo = nCampo + 1;
               |C_M #dsmoy027#nCampo#, 1, "S";
         |SIGUE;
     |FINSI;
|FPRC;

|PROCESO DesdeDni;
     |SI #dsmoy027#19 = "            ";
         #dsmoy027#20 = "zzzzzzzzzzzz";  |C_M #dsmoy027#20,1,"N"; |PINTA #dsmoy027#20;
         |PARA nCampo = 21;  |SI nCampo [ 28;  |HACIENDO nCampo = nCampo + 1;
               |C_M #dsmoy027#nCampo#, 1, "S";
         |SIGUE;
     |SINO;
         |C_M #dsmoy027#20,1,"S";
         |PARA nCampo = 21;  |SI nCampo [ 28;  |HACIENDO nCampo = nCampo + 1;
               |C_M #dsmoy027#nCampo#, 1, "N";
               #dsmoy027#nCampo# = ""; |PINTA #dsmoy027#nCampo#;
         |SIGUE;
     |FINSI;
|FPRC;

|PROCESO NoMasDni;
     |SI #dsmoy027#Campo# = "            ";
         |PARA nCampo = Campo + 1;  |SI nCampo [ 28;  |HACIENDO nCampo = nCampo + 1;
               #dsmoy027#nCampo# = "";  |C_M #dsmoy027#nCampo#, 1, "N";
         |SIGUE;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 28;  |HACIENDO nCampo = nCampo + 1;
               |C_M #dsmoy027#nCampo#, 1, "S";
         |SIGUE;
     |FINSI;
|FPRC;

|PROCESO LeeCargo;
     |ABRE #agim0029;
     #agim0029#0 = #dsmoy027#Campo#;
     |LEE 001#agim0029.=;
     |SI FSalida = 0;
         #dsmoy027#17 = #agim0029#1; |PINTA #dsmoy027#17;
     |FINSI;
     |CIERRA #agim0029;
|FPRC;

||............... PROGRAMA ......................................

|PROGRAMA;

     eaParam = PARAMETRO; |QBF eaParam;
     |SI eaParam = "";
         eaParam = "193";
     |FINSI;
     #dsmoy027#30 = eaParam;
     #dsmoy027#31 = 99;

     |ABRE #dsmom107;
     |LEE 040#dsmom107.p;
     |SI FSalida ! 0;
         |CIERRA #dsmom107;
         |SUB_EJECUTA_NP "dsmom107";
         |ABRE #dsmom107;
     |FINSI;

     |LEE 040#dsmom107.p;
     |SI FSalida ! 0;  |DDEFECTO #dsmom107;  |FINSI;
     |CIERRA #dsmom107;

     |SI enEmpresa ! 0;
         |DDEFECTO #dsmoy027;
         #dsmoy027#0  = enEmpresa;
         #dsmoy027#1  = enEmpresa;
         #dsmoy027#14 = enEjer;
         |HAZ IT3;
     |SINO;
         |CLS;
         |MANTE #dsmoy027;
     |FINSI;
|FPRO;

|PROCESO Tipo88;  |TIPO 88;
     |HAZ Relaciones;
|FPRC;
