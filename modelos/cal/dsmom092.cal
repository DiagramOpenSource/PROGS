|FICHEROS;
     dsmoz090 #0;
     dsmom090 #90;
     dsmom091 #91;
     dsmom092 #92,MANTE;

    :/basica/def/agicen14 #114;
    :/basica/def/agifigen #1000;

    &imagen91@ #441;
    &imagen92@ #442;
|FIN;

|VARIABLES;
 &errconta = 0;
 &esalta   = 0;
 nume1     = 0;
 nInkey    = 0;

 &enDMaqu = 0;
 &enHMaqu = 0;
 &nNingunaMas = 0;
 &nLaPrimera  = 0;
|FIN;

|INCLUYE i_variar;
|INCLUYE i_varcon;

|CALCULO LeelaEmpresa; |TIPO 0;
   errconta = 0;
   enEmpresa = #92#0;
   enTpEm = 0;

  || Lee Empresa
    |ABRE #1000;
    #1000#0 = enEmpresa;
    |LEE 041#1000=;
    |SI FSalida ! 0;
        |MENAV "     Empresa Inexistente";
        errconta = 1;
        |CIERRA #1000;
        |ERROR;
        |ACABA;
    |SINO;
         enTpEm = #1000#87;
         |SI #1000#23 = "N";
            || .. errconta = 1;
            || .. |MENAV "     Empresa Sin Contabilidad";
        |FINSI;
    |FINSI;
    #92#1 = #1000#1; |PINTA #92#1;
    |CIERRA #1000;

    |LEE 010#92=;
    |SI FSalida ! 0;
        |DDEFECTO #92;
        #92#0 = enEmpresa;
        #92#1 = #1000#1;
        |ENDATOS #1#92;
        |GRABA 000#92b;
        |ERROR;
        |PTEC 825;
    |FINSI;
|FCAL;

|PROCESO Borrolin;
 |BORRA 020#91a;
 |LIBERA #91;
|FPRC;

|DEFBUCLE borrarLineas;
 #91#1;
 ;
 #92#0, 00001, 001;
 #92#0, 99999, 999;
 be;
 NULL, Borrolin;
|FIN;

|PROCESO BorraElemento;
 |BORRA 020#90a;
 |LIBERA #90;
|FPRC;

|DEFBUCLE borrarElementos;
 #90#1;
 ;
 #92#0, 00001;
 #92#0, 99999;
 be;
 NULL, BorraElemento;
|FIN;

|PROCESO Tipo1; |TIPO 1;
 esalta = (FEntrada / 100) ! 0;
 |SI esalta = 2;
    |ERROR;
    |PTEC 825;
 |FINSI;
 |SI esalta = 3;
     |CONFI "    NEsta Seguro de Borrar todas las Maquinas de esta Empresa ";
     |SI FSalida ! 0;
         |ERROR;
         |ACABA;
     |FINSI;
     |BUCLE borrarElementos;
     |BUCLE borrarLineas;
 |FINSI;
|FPRC;

|PROCESO leeElemento;
 nume1 = 1;
 |ERROR10;
|FPRC;

|DEFBUCLE LeeElementos;
 #90#1;
 ;
 #92#0, 00001;
 #92#0, 99999;
 ;
 NULL, leeElemento;
|FIN;

|PROCESO ComprueboLinea;
 nume1 = 0;
 |BUCLE LeeElementos;
 |SI nume1 = 0;
     |BORRA 041#92c;
 |FINSI;
 |LIBERA #92;
|FPRC;

|| **********************************************************************
|RUTINA ClaveBaja90;
     #90#0 = #92#0;
     #90#1 = 00001;
|FRUT;

|RUTINA ClaveAlta90;
     #90#0 = #92#0;
     #90#1 = 99999;
|FRUT;

|PROCESO Tipo7;  |TIPO 7;
     |SI nNingunaMas = 1;
         |SI nLaPrimera  = 0;
             |PTEC 806;
             |ACABA;
         |FINSI;
     |FINSI;
     |MENSA "    <F2> Consulta Clientes, <F3> Entrar Maquinas, <F4> Imprimir Fichas"
     |PINTA  #92#0; |PINTA  #92#1; |PINTA #92#2;
     enModo = 7;
     enEmpresa   = #92#0;
     enPerConta  = -1;
     enEjer      = 0;
     |ENTLINEAL #1#90, -2, 7, 15, 0, ClaveBaja90, ClaveAlta90;
     nLaPrimera  = 0;
|FPRC;

|PROCESO Tipo11;  |TIPO 11;
   nInkey = FSalida;
   |SI nInkey =  11;
         errconta    = 0;
         enModo      = 2;
         enEmpresa   = #92#0;
         enPerConta  = -1;
         |ENTLINEAL #1#90, -2, 2, 15, 0, ClaveBaja90, ClaveAlta90;
         |HAZ ComprueboLinea;
   |FINSI;

    |SI nInkey = 10;                        || F2
        |ERROR;
        eaElProm = "dsmom092";
        |HAZ ConsuCliente7;
        eaElProm = "";
        |PTEC 802;
    |FINSI;

    |SI nInkey = 12;
        enEmpresa = #92#0;
        enDMaqu   = 00001;
        enHMaqu   = 99999;
        |SUB_EJECUTA_NP "dsmoy090";
        |ERROR;
    |FINSI;

|FPRC;
|| **********************************************************************
