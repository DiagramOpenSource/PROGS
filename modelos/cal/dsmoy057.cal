|FICHEROS;
   dsmoy057 #0;

   dsmom062 #62;
   dsmom063 #63;
   dsmom064 #64;

   :/basica/def/agifigen #118;

   :/contagen/def/canempre #300;
   :/contagen/def/caexistl #303;
   :/contagen/def/cacuenta #304;
   :/contagen/def/caconimp #306;
|| ...   :/contagen/def/cacuedep #305; No por actividades  por ahora

   dsmow032 #900;          ||Def de impresion
   dsmow090 #901;
   dsmow017 #917;

   dsmow043 #999,MANTE;
|FIN;

|VARIABLES;
   &entrada = 1;
   &eSwSelec  = 0;
   &eaDDepar  = "";
   &eaHDepar  = "";
   &eaNomFDep = "";  ||Nombre del Fichero Creado
   &dirfich0  = "";

   aMonMod    = "";
   informe    = "dsmoy057";
   nInkey     = 0;
   aFichero   = "";
   nCampo     = 0;
   nDEmpresa  = 0;
   nHEmpresa  = 0;
   nDActividad = 0;
   nHActividad = 0;
   nLaEmpresa  = 0;
   nGTempo     = 0;

   nSPin1      = 0;
   aPath       = "";
   nEjercicio  = 0;

   aAlfa1 = "";
   aAlfa2 = "";
   aAlfa3 = "";
   nPara1 = 0;
   nPara2 = 0;
   nNume1 = 0;
   nNume2 = 0;
   nNume3 = 0;
   nNume4 = 0;

   SwHayDatos = 0;

   fDFecha   = @;
   fHFecha   = @;

   &nPagina  = 0;
   &SwPrim   = 0;
   &SwLinea  = 0;
   &SwLineaB = 0;
   &eaTitul2 = "";

   nEnExi   = 0;
   nSigno   = 0;
   aDCuenta = "";
   aHCuenta = "";
   nSaldo = 0;
   nSald1 = 0;
   nSald2 = 0;
   nSald3 = 0;
   sw = 0;
   fLaFecha = @;

   nSalPar1 = 0;
   nImpVar1 = 0;
   nImpVar2 = 0;

   nApar = 0;
   nPart = 0;
   aCtaF = "";
   aDesc = "";
   nLinea = 0;
   eldesde = 0;
   elhasta = 0;
   nMes    = 0;
   aSiNo   = "";
|FIN;

|INCLUYE i_variar;
|INCLUYE i_varcon;
|INCLUYE i_varlib;
|INCLUYE i_masivo;

|| //////////////////// Calculos de Pantalla \\\\\\\\\\\\\\\\\\\\\\\\\\
|CALCULO Eano; |TIPO 7;
 fLaFecha = ~;
 aAlfa1 = fLaFecha; aAlfa1 = aAlfa1 % -104;
 #0#1 = aAlfa1;
 |PINTA #0#1;
|FCAL;

|CALCULO Tipo7;  |TIPO 7;
     |SI enEmpresa ! 0;
         #0#2  = enEmpresa;  |PINTA #0#2;
         #0#3  = enEmpresa;  |PINTA #0#3;

         |C_M #0#2, 1, "N";
         |C_M #0#3, 1, "N";
     |FINSI;
|FCAL;

|CALCULO Parrilla; |TIPO 0;
     |SI #0#2 = 0;
         #0#2 = 0;  |PINTA #0#2;
         #0#3 = 0;  |PINTA #0#3;  |C_M #0#3, 1, "N";
         |PARA nCampo = 4;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |SINO;
         |SI enEmpresa = 0;
             |C_M #0#2, 1, "S";
             |C_M #0#3, 1, "S";
         |FINSI;

         |PARA nCampo = 4;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "N";
               #0nCampo = 0;  |PINTA #0nCampo;
         |SIGUE;
     |FINSI;
|FCAL;

|CALCULO NoMasEmpresas; |TIPO 0;
     |SI #0Campo = 0;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
               #0nCampo = 0;  |C_M #0nCampo, 1, "N";
         |SIGUE;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |FINSI;
|FCAL;

|CALCULO YaSeCual; |TIPO 7;
 |SI enEmpresa ! 0;
     #0#46 = "S"; |PINTA #0#46;
     #0#24 = "S"; |PINTA #0#24;
     #0#25 = 00;  |PINTA #0#25;
     #0#26 = 99;  |PINTA #0#26;
     |C_M #0#24, 1, "N";
     |C_M #0#25, 1, "N";
     |C_M #0#26, 1, "N";
     |C_M #0#46, 1, "N";
 |FINSI;
|FCAL;

|CALCULO SelTipo0; |TIPO 0;
 |SI #0#46 = "N";
     #0#25 = 0;   |PINTA #0#25;  |C_M #0#25, 1, "N";
     #0#26 = 99;  |PINTA #0#26;  |C_M #0#26, 1, "N";
     |PARA nCampo = 27;  |SI nCampo [ 36;  |HACIENDO nCampo = nCampo + 1;
           |C_M #0nCampo, 1, "S";
     |SIGUE;
 |SINO;
     |SI enEmpresa = 0;
         |C_M #0#25, 1, "S";
         |C_M #0#26, 1, "S";
     |FINSI;
     |PARA nCampo = 27;  |SI nCampo [ 36;  |HACIENDO nCampo = nCampo + 1;
           |C_M #0nCampo, 1, "N";
           #0nCampo = 0;  |PINTA #0nCampo;
     |SIGUE;
 |FINSI;
|FCAL;

|CALCULO SelTipo1;
 |SI #0Campo = 0;
     |PARA nCampo = Campo + 1;  |SI nCampo [ 36;  |HACIENDO nCampo = nCampo + 1;
           #0nCampo = 0;  |C_M #0nCampo, 1, "N";
     |SIGUE;
 |SINO;
     |PARA nCampo = Campo + 1;  |SI nCampo [ 36;  |HACIENDO nCampo = nCampo + 1;
           |C_M #0nCampo, 1, "S";
     |SIGUE;
 |FINSI;
|FCAL;

|CALCULO LasExi; TIPO 0;
 aAlfa1 = #0#54;
 |C_M #0#52,1,aAlfa1;
 |C_M #0#53,1,aAlfa1;
 |SI aAlfa1 = "N";
     #0#52 = 00;     |PINTA #0#52;
     #0#53 = 12;     |PINTA #0#53;
 |FINSI;
|FCAL;

|CALCULO Acero; |TIPO 7;
 aAlfa1 = "S";
 |SI #0#40 = "R";
     aAlfa1 = "N";
     #0Campo = "N";
     |PINTA #0Campo;
 |FINSI;
 |C_M #0Campo, 1, aAlfa1;
|FCAL;

|PROCESO pintames; |TIPO 0;
  nNume1 = Campo + 2;
  NumMes = #0Campo;
  |HAZ NombreMes;
  #0nNume1 = NombreMes; |PINTA #0nNume1;
  |SI Campo = 48; |Y  #0#47 > #0#48;
      |MENAV "    Error de Entrada ...";
      |ERROR;
  |FINSI;
|FPRC;

|PROCESO pintames2; |TIPO 0;
  NumMes = #0Campo;
  |HAZ NombreMes;
  |ATRI I;
  |SI Campo = 52;  |PINTA 1665, NombreMes;  |FINSI;
  |SI Campo = 53;  |PINTA 1765, NombreMes;  |FINSI;
  |ATRI 0;
  |SI Campo = 53;  |Y #0#52 > #0#53;
      |MENAV "    Error de Entrada ...";
      |ERROR;
  |FINSI;
|FPRC;

|| **********************************************************************************
|| **********************************************************************************
|| ....................... Situa la Empresa Contable o de Planillas
|| .................... graba Contabilidad
|PROCESO LEmpresaConta;

   enEmpresa  = #canempre#2;
   enPerConta = #canempre#3;   || Periodo Contable

   |SI #0#24 = "N"; |Y #canempre#21 = "I"; |ACABA; |FINSI;

|| ------------------------------------------------------------------------
   aAlfa1 = enEmpresa;  aAlfa1 = ("00000" + aAlfa1) % -105;
   aAlfa2 = enPerConta; aAlfa2 = ("0" + aAlfa2) % -101;
   aPathConta  = aPath + "contagen/fich/" + aAlfa1 + aAlfa2 + "/";

   nGTempo = nGTempo + 1;
   #999#0 = nGTempo;       || Guia
   #999#1 = enEmpresa;     || Empresa
   #999#2 = enPerConta;    || Periodo Contable
   #999#3 = enEjer;        || A¤o
   #999#4 = #canempre#11;  || Desde Mes
   #999#5 = #canempre#13;  || N§ Digitos
   #999#6 = #canempre#14;  || Digitos Nivel 1
   #999#7 = #canempre#15;  || Digitos Nivel 2
   #999#8 = #canempre#16;  || Digitos Nivel 3
   #999#9 = #canempre#17;  || Digitos Nivel 4
   #999#10 = #canempre#18; || Digitos Nivel 5
   #999#11 = #canempre#19; || Digitos Nivel 6
   eaEstadoEmp = "S"; |SI #canempre#21 = "I"; eaEstadoEmp = "N"; |FINSI;
   #999#12 = eaEstadoEmp;  || Estado
   #999#13 = #canempre#28; || Moneda
   #999#14 = #canempre#1;  || Nombre Empresa
   #999#15 = aPathConta;   || Path
   #999#16 = #0#43;        || Emitir
   |GRABA 020#999n;
|FPRC;

|PROCESO LEmpresaEOS;
 enEmpresa  = #agifigen#0;
 enPerConta = 0;

 eaEstadoEmp = "S";
 |SI #agifigen#94 > "01.01.0000";
     eaEstadoEmp = "N";
     |SI #0#24 = "N"; |ACABA; |FINSI;
 |FINSI;

|| ... Seleccion de Tipo de Empresa
  nSPin1 = #agifigen#87;
  enSwSelEmp = 1;
  |SI #0#46 = "S";
      |SI nSPin1 ] #0#25; |Y nSPin1 [ #0#26;
          enSwSelEmp = 0;
      |FINSI;
  |SINO;
      |SI nSPin1 = #0#27;
          enSwSelEmp = 0;
      |SINO;
          |PARA nCampo = 28;  |SI nCampo [ 36;  |HACIENDO nCampo = nCampo + 1;
                |SI nSPin1 = #0nCampo; |Y nSPin1 ! 0;
                    enSwSelEmp = 0;
                    |SAL;
                |FINSI;
          |SIGUE;
      |FINSI;
  |FINSI;
  |SI enSwSelEmp = 1; |ACABA; |FINSI;
|| ------------------------------------------------------------------------

  |SELECT #4#canempre;
  #canempre#2  = enEmpresa;
  #canempre#26 = nEjercicio;
  |LEE 001#canempre.=;
  |SI FSalida = 0;
      |HAZ LEmpresaConta;
  |FINSI;
  |SELECT #1#canempre;
|FPRC;

|DEFBUCLE agifigen;
 #agifigen#1;
 ;
 nDEmpresa;
 nHEmpresa;
 e;
 NULL, LEmpresaEOS;
|FIN;

||///////////////////////////////////////////////////////////////////
|CALCULO Datos7; |TIPO 7;
 |MENSA "    <F3> Cambiar Emitir Informe S/N";
 |ATRI R; |PINTA 1169, "Imp"; |ATRI 0;
|FCAL;

|CALCULO DatosManip; |TIPO 11;
   nInkey = FSalida;
   |BLIN 04;

   |SI nInkey = 11;
       |SI #999#16 = "S";
           #999#16 = "N";
       |SINO;
           #999#16 = "S";
       |FINSI;
       |GRABA 020#999a;
   |FINSI;
|FCAL;

|RUTINA inferior;
   #999#0 = 00001;
|FRUT;

|RUTINA superior;
   #999#0 = 99999;
|FRUT;

||///////////////////////////////////////////////////////////////////

|CALCULO Tipo3;  |TIPO 3;
    enEjer = #0#1;
    eaPlanilla = "dsmom062";  |HAZ PlanDePlanillas;

    |ABRE #64;
    |LEE 040#64p;
    |SI FSalida ! 0;
        |CIERRA #64;
        eaExtension = "mod";
        eaPrograma  = "dsmom062";
        |SUB_EJECUTA_NP ":basica/dspatron";
        eaExtension = "mod";
        eaPrograma  = "dsmom063";
        |SUB_EJECUTA_NP ":basica/dspatron";
        eaExtension = "mod";
        eaPrograma  = "dsmom064";
        |SUB_EJECUTA_NP ":basica/dspatron";
    |SINO;
        |CIERRA #64;
    |FINSI;

    aMonMod = "P";
    |HAZ LeeEURODBMOD;
    |SI EURODBMOD = 2; aMonMod = "E"; |FINSI;

    enEjer = #0#1;
    aAlfa1 = enEjer; |QBF aAlfa1;
    aAlfa1 = aAlfa1 % -102;
    nEjercicio = aAlfa1;
    |DBASS aPath; |QBT aPath;

    aFichero = ("90w" + Usuario) % 108;
    |NOME_DAT #901 aFichero;
    aFichero = ("17w" + Usuario) % 108;
    |NOME_DAT #917 aFichero;

    aFichero = ("43w" + Usuario) % 108;
    |NOME_DAT #999 aFichero;
    |ABRE #999;  |CIERRA #999;  |DELINDEX #999;
    eaFEmpresa = aFichero;

    |ABRE #999;
    |ABRE #canempre;
    nGTempo = 0;   ||Busco Empresas Contables
    |SI #0#2 ! 0;
        nDEmpresa = #0#2;
        nHEmpresa = #0#3;
        |BUCLE agifigen;
    |SINO;
        |PARA nPara1 = 4;  |SI nPara1 [ 23;  |HACIENDO nPara1 = nPara1 + 1;
              |SI #0nPara1 ! 0;
                  nDEmpresa = #0nPara1;
                  nHEmpresa = #0nPara1;
                  |BUCLE agifigen;
              |FINSI;
        |SIGUE;
    |FINSI;
    |CIERRA #canempre;
    |CIERRA #999;

    |SI nGTempo = 0;    || No existe ninguna empresa
        |SI enSwMvo = 0;
            |MENAV "    Error. No existe ninguna Empresa dentro de la Seleccion ";
        |FINSI;
        |DELINDEX #999;
        |ACABA;
    |FINSI;

    |C_V #999#12,1,"N"; |C_P #999#16,1,1270;

    |SI enSwMvo = 0;
        |PUSHV 0509 2375;
        |ENTLINEAL #1#999, -1, 2, 22, 1, inferior, superior;
        |POPV;
    |FINSI;
    |HAZ Imprimir;

    |DELINDEX #917;
    |DELINDEX #999;
|FCAL;

|| *************************************************************************
|| *************************************************************************
|| *************************************************************************
|PROCESO LeeApartado;
  |ABRE #62;
  |DDEFECTO #62;
  #62#4 = enPlan;
  #62#0 = nApar;
  |LEE 001#62=;
  eaTitul2 = #62#1;
  aSiNo    = #62#2;
  aDesc    = #62#3;
  |CIERRA #62;
|FPRC;

|PROCESO LeeAux0;

 |SI nApar ! #901#0; |O SwPrim = 0;
     SwLinea  = 0;
     nApar    = #901#0;
     |HAZ LeeApartado;
     SwLineaB = 1;
     |PRINT;
     SwPrim   = 1;
     SwLineaB = 0;
 |FINSI;

 |SI #901#8 = 3;
     |SI #901#5 = 0; |Y #0#51 = "N";
         |ACABA;
     |FINSI;
 |FINSI;
 |SI #901#8 = 1;
     |SI aSiNo = "N";
         |ACABA;
     |FINSI;
 |FINSI;

 SwLinea = #901#8;
 |PRINT;
 sw      = 1;
 #900#49 = #900#49 + #901#7;
 SwLineaB = 1;
|FPRC;

|DEFBUCLE LeeAux;
 #901#1;
 ;
  01, 0001,"    ";
  99,10000,"zzzz";
 ;
 NULL, LeeAux0;
|FIN;

|PROCESO ImpAuxiliar;    || ... Imprimir el balance en detalle

 SwPrim  = 0;
 nPagina = 1;
 |PDEFECTO #900,25,50;

 sw      = 0;
 SwLinea = 0;
 nApar   = 0;
 |BUCLE LeeAux;
 |PIEINF;
|FPRC;

|| /////////////////////////////////////////////////////////////////
|PROCESO SumaSaldos;
 nSigno = 1;
 |SI #304#5 = "H"; |O #304#5 = "P"; nSigno = -1; |FINSI;

 SwHayDatos = 1;
 |PARA nPara2 = eldesde; |SI nPara2 [ elhasta;  |HACIENDO nPara2 = nPara2 + 3;
       #304nPara2  = (#304nPara2  / enConversor) ! EURODBMOD;
       nSaldo = nSaldo + (nSigno * #304nPara2);
 |SIGUE;
|FPRC;

|DEFBUCLE LeeCuentas;
 #304#1;
 3;
 aDCuenta, "S";
 aHCuenta, "S";
 e;
 NULL, SumaSaldos;
|FIN;


|PROCESO Descripcion;
 |SI aCtaF ! "";
     aDesc = #64#3;
 |SINO;
     |SI nPart ! 10000;
         |ABRE #63;
         |DDEFECTO #63;
         #63#4 = enPlan;
         #63#0 = nApar;
         #63#1 = nPart;
         |LEE 001#63=;
         aDesc = #63#2;
         |CIERRA #63;
     |SINO;
         |HAZ LeeApartado;
     |FINSI;
 |FINSI;
|FPRC;

|PROCESO GrabaAuxi;
 #901#0 = nApar;
 #901#1 = nPart;
 #901#2 = aCtaF;
 |LEE 001#901=;
 |SI FSalida ! 0;
     |DDEFECTO #901;
     #901#0 = nApar;
     #901#1 = nPart;
     #901#2 = aCtaF;
     |HAZ Descripcion;
     #901#3 = aDesc;
     #901#8 = nLinea
     |GRABA 020#901b;
 |FINSI;

 #901#5 = #901#5 + nSald1;
 #901#6 = #901#6 + nSald2;
 #901#7 = #901#7 + nSald3;
 |GRABA 020#901a;
 |LIBERA #901;
|FPRC;

|PROCESO dsmom064_0;

 nApar = #64#0;
 nPart = #64#1;
 aCtaF = #64#2;

 nSaldo = 0;
 nSald1 = 0;
 nSald2 = 0;
 nSald3 = 0;

 nEnExi = 0;
 |DDEFECTO #917;
 #917#0 = enEmpresa;
 #917#1 = enEjer;
 #917#2 = 00;
 #917#3 = #64#2;
 |LEE 000#917=;
 |SI FSalida = 0;
     nEnExi = 1;
 |FINSI;

 |SI nEnExi = 0;
     aAlfa1 = #64#2; |QBF aAlfa1;              aDCuenta = aAlfa1;
     aAlfa1 = (aAlfa1 + "zzzzzzzzzzzz") % 112; aHCuenta = aAlfa1;
     |BUCLE LeeCuentas;
 |SINO;
     nSaldo = #917#4;
 |FINSI;

 |SI #64#4 = "-"; nSaldo = - nSaldo;  |FINSI;

 |SI #0#40 = "E";
     nLinea = 3;
     nSald1 = nSaldo;
     |HAZ GrabaAuxi;
 |FINSI;

 aCtaF  = "";
 nSald1 = 0;
 nSald2 = nSaldo;
 nSald3 = 0;
 nLinea = 2;
 |HAZ GrabaAuxi;

 aCtaF  = "";
 nPart  = 10000;
 nSald1 = 0;
 nSald2 = 0;
 nSald3 = nSaldo;
 nLinea = 1;
 |HAZ GrabaAuxi;
|FPRC;

|DEFBUCLE dsmom064;
 #64#1;
 ;
 enPlan, 01, 0001, "    ";
 enPlan, 99, 9999, "zzzz";
 ;
 NULL, dsmom064_0;
|FIN;

||////////////////////////////////////////////////////////////////////////

|PROCESO LasExisConta;

 nNume1 = #0#52 + 11;
 nNume2 = #0#53 + 11;
 eaCuenta = #303#2;
 eaCuenta = eaCuenta % 104;
 nImpVar1 = #303nNume1;
 nImpVar2 = #303nNume2;

 #917#0 = enEmpresa;
 #917#1 = enEjer;
 #917#2 = 00;
 #917#3 = eaCuenta;
 |LEE 000#917=;
 |SI FSalida ! 0;
     |DDEFECTO #917;
     #917#0 = enEmpresa;
     #917#1 = enEjer;
     #917#2 = 00;
     #917#3 = eaCuenta;
     |GRABA 020#917c;
 |FINSI;
 #917#4 = #917#4 + nImpVar1;
 #917#5 = #917#5 + nImpVar2;
 |GRABA 020#917a;
|FPRC;

|DEFBUCLE CaExistL;
 #303#1;
 ;
 #0#38, 01;
 #0#39, 99;
 e;
 NULL, LasExisConta;
|FIN;
|| ....................................

|PROCESO Emision;

   enEmpresa  = #999#1;
   enPerConta = #999#2;
   eaLaMonEmp = #999#13;

   |HAZ EnConversor;
   enEjer     = #999#3;   || A¤o
   dig1       = #999#4;   || Desde Mes
   dig3       = #999#5;   || N§ Digitos
   dig4       = #999#6;   || Digitos Nivel 1
   dig5       = #999#7;   || Digitos Nivel 2
   dig6       = #999#8;   || Digitos Nivel 3
   dig7       = #999#9;   || Digitos Nivel 4
   dig8       = #999#10;  || Digitos Nivel 5
   dig9       = #999#11;  || Digitos Nivel 6
   eaEstadoEmp = #999#12; || Estado
   eaNombreEmp = #999#14; || Nombre Empresa
   aPathConta  = #999#15; |QBF aPathConta; || Path

   || Recoger informacion sobre los niveles ...
   |SI dig3 = 1; MaximoDigitos = dig4; |FINSI;
   |SI dig3 = 2; MaximoDigitos = dig5; |FINSI;
   |SI dig3 = 3; MaximoDigitos = dig6; |FINSI;
   |SI dig3 = 4; MaximoDigitos = dig7; |FINSI;
   |SI dig3 = 5; MaximoDigitos = dig8; |FINSI;
   |SI dig3 = 6; MaximoDigitos = dig9; |FINSI;

   dirfich0 = aPathConta;
   |PATH_DAT #303 aPathConta;
   |PATH_DAT #304 aPathConta;
   |PATH_DAT #306 aPathConta;

/* ... Por ahora no va por actividades
       eSwSelec  = 2;
       |ATRI I;
       |PINTA 2302, "Procesando Cuentas por Departamentos ... Espere ";
       |ATRI 0;
       |SUB_EJECUTA_NP ":contagen/cacuedep";
       |PINTA 2302, "                                                ";
       |NOME_DAT #305 eaNomFDep;
       |PATH_DAT #305 aPathConta;
*/

   |DDEFECTO #306;  ||Configuracion de Informes
   |ABRE #306;
   #306#0 = 1;
   |LEE 000#306=;
   |CIERRA #306;

   |SI #0#54 = "S";
       |ABRE #917;
       |BUCLE CaExistL;
       |CIERRA #917;
   |FINSI;

   |INFOR informe;
   |SI FSalida ! 0;  |ACABA;  |FINSI;

   enActividad = 0;
   |HAZ LeeDatosEmpresa;

   SwHayDatos = 0;
   |ABRE #901;
   |ABRE #917;
   |BUCLE dsmom064;
   |CIERRA #917;
   |CIERRA #901;

   |SI SwHayDatos = 1;
       |HAZ ImpAuxiliar;
   |FINSI;

   |FININF;
|FPRC;

|DEFBUCLE dsmow043;
 #dsmow043#1;
 16;
 000001, "S";
 999999, "S";
 ;
 NULL, Emision;
|FIN;

|PROCESO Imprimir;

    eaDDepar  = #0#38; |QBF eaDDepar;
    eaDDepar  = ("00" + eaDDepar) % -102;
    eaHDepar  = #0#39; |QBF eaHDepar;
    eaHDepar  = ("00" + eaHDepar) % -102;

    aAlfa2 = #0#1;
    nMes   = #0#47; |SI nMes = 0; nMes = 1; |FINSI;
    aAlfa1 = nMes; aAlfa1 = ("00" + aAlfa1) % -102;
    aAlfa1 = "01." + aAlfa1 + "." + aAlfa2;
    efDFecha  = aAlfa1;

    nMes   = #0#48; |SI nMes = 0; nMes = 1; |FINSI;
    aAlfa1 = nMes; aAlfa1 = ("00" + aAlfa1) % -102;
    aAlfa3 = "31.";
    |SI nMes = 4; |O nMes = 6; |O nMes = 9; |O nMes = 11;
        aAlfa3 = "31.";
    |SINO;
        |SI nMes = 2;
            aAlfa3 = "28.";
            nNume1 = #0#1 ! 4; nNume2 = (#0#1 ! 4) ! 0;
            |SI nNume1 = nNume2;
                aAlfa3 = "29.";
            |FINSI;
        |FINSI;
    |FINSI;
    aAlfa1 = aAlfa3 + aAlfa1 + "." + aAlfa2;
    efHFecha  = aAlfa1;

    eldesde = (#0#47 * 3)  + 11;
    elhasta = (#0#48 * 3)  + 11;

    |SI enSwMvo = 0;
        |IMPRE 0;
        |SI FSalida ! 0;  |ACABA;  |FINSI;
    |FINSI;

    |BUCLE dsmow043;

    |SI enSwMvo = 0;
        |FINIMP;
    |FINSI;
|FPRC;

|PROCESO LeeDatosEmpresa;
  |DELINDEX #901;

  eaInac    = #0#24;
  eaTituloL = #0#45; ||Titulo del Libro

  |HAZ DatosEmpr_w32Con;
|FPRC;

|PROGRAMA;

  |SI enSwMvo = 1;
      |DDEFECTO #0;
      #0#1 = enEjerM;
      #0#2 = enEmprM;
      #0#3 = enEmprM;
      aAlfa1 = efDFecM; aAlfa1 = aAlfa1 % 402; #0#47 = aAlfa1;
      aAlfa1 = efHFecM; aAlfa1 = aAlfa1 % 402; #0#48 = aAlfa1;
      |HAZ Tipo3;
  |SINO;
      |MANTE #0;
  |FINSI;

|FPRO;
