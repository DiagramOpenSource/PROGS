|FICHEROS;
     clientes@;
     agidanif@;
     agim0004@;
     regisnif@;
     dsam0003@;
     reprovi@;
     repobla@;
     rpaises@;

     dsm210dp;

     dsmow323;
|FIN;

|VARIABLES;
     nError              = 0;
     nCod                = 0;

     aAlfa               = "";
     aAlfa1              = "";
     aFic                = "";
     aAct                = "";
     aNif                = "";
     aBase               = "";
     aCLI                = "";
     aNIF                = "";
     aREN                = "";
     aDPE                = "";
     aREP                = "";
     aPRO                = "";
     aPOB                = "";
     aPAI                = "";
     aPath               = "";
     aPais               = "";
     aNegro              = "0,0,0;255,255,255";
     aRojo               = "0,0,0;255,80,80";

     &enSwCod            = 0;
     &enEmpresa          = 0;
     &eaBloqueo          = "";
|FIN;

|PROCESO VerPais;
     |SELECT #3#rpaises@;
     #rpaises@#4 = aPais;
     |LEE 000#rpaises@.=;
     |SI FSalida = 0;
         aPais = #rpaises@#0;
     |FINSI;
     |SELECT #1#rpaises@;
|FPRC;

|PROCESO RecogeCliente;
     |SI enSwCod = 1;
         aNif = #clientes@#3;

         #dsam0003@#0  = #clientes@#0;
         |LEE 000#dsam0003@.=;
         |SI FSalida ! 0;  |O #dsam0003@#64 = "N";
             |ACABA;
         |FINSI;
     |SINO;
         aNif = #clientes@#13;

         #agidanif@#0 = aNif;
         #agidanif@#1 = 1;
         |LEE 000#agidanif@.];
         |PARA; |SI FSalida = 0; |HACIENDO;
              |SI #agidanif@#2 = "TITULAR     ";
                  |SAL;
              |FINSI;

              |LEE 000#agidanif@.s;
         |SIGUE;

         |SI #agidanif@#2 ! "TITULAR     "; |O #agidanif@#0 ! aNif;
             |ACABA;
         |FINSI;

         |SI #agidanif@#20 = "S";
             |ACABA;
         |FINSI;
     |FINSI;

     #dsm210dp#0  = #clientes@#0;
     |LEE 000#dsm210dp.=;
     |SI FSalida = 0; |ACABA; |FINSI;

     #regisnif@#0 = aNif;
     |LEE 000#regisnif@.=;
     |SI FSalida ! 0; |DDEFECTO #regisnif@; |FINSI;

     #agim0004@#0 = #clientes@#0;
     #agim0004@#1 = 1;
     |LEE 000#agim0004@.];
     |SI FSalida ! 0; |O #agim0004@#0 ! #clientes@#0;
         |DDEFECTO #agim0004@;
     |FINSI;

     #dsm210dp#0  = #clientes@#0;
     |LEE 101#dsm210dp.=;
     |SI FSalida ! 0;
         |DDEFECTO #dsm210dp;
         #dsm210dp#0  = #clientes@#0;
         |GRABA 020#dsm210dp.b;
     |FINSI;

     #dsm210dp#10 = "NUM";
     #dsm210dp#26 = #regisnif@#17;
     #dsm210dp#36 = #regisnif@#17;

     |SI enSwCod = 2;
         #dsm210dp#1  = #agidanif@#12;
         #dsm210dp#2  = #agidanif@#0;
         #dsm210dp#4  = #agidanif@#13;
         #dsm210dp#5  = #agidanif@#32;
         #dsm210dp#8  = #agidanif@#24;
         #dsm210dp#9  = #agidanif@#25;
         #dsm210dp#11 = #agidanif@#26;
         #dsm210dp#15 = #agidanif@#27;
         #dsm210dp#16 = #agidanif@#28;
         #dsm210dp#17 = #agidanif@#29;
         #dsm210dp#20 = #agidanif@#30;
         #dsm210dp#21 = #agidanif@#31;
         #dsm210dp#22 = #agidanif@#32;
         #dsm210dp#23 = #agidanif@#33;
         #dsm210dp#24 = #agidanif@#34;
         #dsm210dp#25 = #agidanif@#35;

         aAlfa = #agidanif@#24;
         aAlfa1 = #agidanif@#25; |QBF aAlfa1;
         |SI aAlfa1 ! "";
             aAlfa = aAlfa + " " + aAlfa1;
         |FINSI;

         aAlfa1 = #agidanif@#26; |QBF aAlfa1;
         |SI aAlfa1 ! "";
             aAlfa = aAlfa + ", " + aAlfa1;
         |FINSI;

         aAlfa1 = #agidanif@#27; |QBF aAlfa1;
         |SI aAlfa1 ! "";
             aAlfa = aAlfa + ", Esc." + aAlfa1;
         |FINSI;

         aAlfa1 = #agidanif@#28; |QBF aAlfa1;
         |SI aAlfa1 ! "";
             aAlfa = aAlfa + ", " + aAlfa1 + "§";
         |FINSI;

         aAlfa1 = #agidanif@#29; |QBF aAlfa1;
         |SI aAlfa1 ! "";
             aAlfa = aAlfa + "-" + aAlfa1 + "§";
         |FINSI;

         #dsm210dp#27 = aAlfa;
         #dsm210dp#29 = #agidanif@#32;
         #dsm210dp#31 = (("00" + #agidanif@#30) % -102) + (("000" + #agidanif@#31) % -103);
         #dsm210dp#32 = #agidanif@#33;
         #dsm210dp#34 = #agidanif@#34;
         #dsm210dp#35 = #agidanif@#35;
         #dsm210dp#11 = #agidanif@#32;
     |FINSI;

     |SI enSwCod = 1;
         #dsm210dp#1  = #clientes@#1;
         #dsm210dp#2  = #dsam0003@#1;
         #dsm210dp#4  = #dsam0003@#67;

         aAlfa1 =  #dsm210dp#5; |QBF aAlfa1;
         |SI aAlfa1 = "";
             #dsm210dp#5  = #dsam0003@#22;
         |FINSI;

         |SI #dsam0003@#75 = "N";
             #dsm210dp#8  = #dsam0003@#14;
             #dsm210dp#9  = #dsam0003@#15;
             #dsm210dp#11 = #dsam0003@#16;
             #dsm210dp#15 = #dsam0003@#17;
             #dsm210dp#16 = #dsam0003@#18;
             #dsm210dp#17 = #dsam0003@#29;
             #dsm210dp#18 = #dsam0003@#80;
             #dsm210dp#19 = #dsam0003@#81;
             #dsm210dp#20 = #dsam0003@#20;
             #dsm210dp#21 = #dsam0003@#21;
             #dsm210dp#22 = #dsam0003@#22;

             #reprovi@#0 = #dsam0003@#20;
             |LEE 000#reprovi@.=;
             |SI FSalida ! 0; |DDEFECTO #reprovi@; |FINSI;

             #dsm210dp#23 = #reprovi@#1;
             #dsm210dp#24 = #dsam0003@#12;
             #dsm210dp#25 = #dsam0003@#71;
             #dsm210dp#26 = #dsam0003@#74;
         |SINO;
             #dsm210dp#27 = #dsam0003@#82;
             #dsm210dp#28 = #dsam0003@#83;
             #dsm210dp#29 = #dsam0003@#84;
             #dsm210dp#30 = #dsam0003@#85;
             #dsm210dp#31 = #dsam0003@#86;
             #dsm210dp#32 = #dsam0003@#87;

             aPais = #dsam0003@#88;
             |HAZ VerPais;
             #dsm210dp#33 = aPais;

             #dsm210dp#34 = #dsam0003@#12;
             #dsm210dp#35 = #dsam0003@#71;
             #dsm210dp#36 = #dsam0003@#74;
         |FINSI;

         #dsm210dp#3 = #clientes@#79;
     |FINSI;

     #dsm210dp#103 = "A";
     #dsm210dp#104 = aNegro;
     |SI #dsmow323#3 = "Inactivo";
         #dsm210dp#103 = "I";
         #dsm210dp#104 = aRojo;
     |FINSI;

     |GRABA 020#dsm210dp.a;
     |LIBERA #dsm210dp;
|FPRC;

|PROCESO clientes;
     aAct = "A";
     |SI enSwCod = 1;
         aNif  = #clientes@#3;
         aAct  = #clientes@#85;

         #dsam0003@#0  = #clientes@#0;
         |LEE 000#dsam0003@.=;
         |SI FSalida ! 0;  |ACABA;  |FINSI;

         |SI #dsam0003@#64 = "N"; |ACABA; |FINSI;
     |FINSI;

     |SI enSwCod = 2;
         aNif = #clientes@#13;
         |SI #clientes@#94 ] "01.01.1900";
             aAct = "I";
         |FINSI;

         #agidanif@#0 = aNif;
         #agidanif@#1 = 1;
         |LEE 000#agidanif@.];
         |PARA; |SI FSalida = 0; |HACIENDO;
              |SI #agidanif@#2 = "TITULAR     ";
                  |SAL;
              |FINSI;

              |LEE 000#agidanif@.s;
         |SIGUE;

         |SI #agidanif@#2 ! "TITULAR     "; |O #agidanif@#0 ! aNif;
             |ACABA;
         |FINSI;

         |SI #agidanif@#20 = "S";
             |ACABA;
         |FINSI;
     |FINSI;

     |DDEFECTO #dsmow323;
     #dsmow323#0 = #clientes@#0;
     #dsmow323#1 = #clientes@#1;
     #dsmow323#2 = #clientes@#2;
     #dsmow323#3 = "Activo";
     |SI aAct = "I";
         #dsmow323#3 = "Inactivo";
     |FINSI;

     |GRABA 020#dsmow323.n;
     |LIBERA #dsmow323;
|FPRC;

|DEFBUCLE clientes;
     #clientes@#1001;
     ;
     00001;
     99999;
     ;
     NULL, clientes;
|FIN;

|PROCESO AltaCliente;
     aFic = "dsmow323" + Usuario;
     |NOME_DAT #dsmow323#aFic#;
     |ABRE #dsmow323;  |CIERRA #dsmow323;  |DELINDEX #dsmow323;

     |ABRE #dsmow323;
     |BUCLE clientes;

     |PARA;  |SI;  |HACIENDO;
          nCod = 0;
          |CONSULTA_CLAVE #1#dsmow323;
          |SI FSalida ! 0;  |SAL;  |FINSI;

          |SI #dsmow323#3 = "Inactivo";
              |CONFI "0000NContribuyente Inactivo. Continuar";
              |SI FSalida = 0;
                  nCod = #clientes@#0;
                  |SAL;
              |FINSI;
          |SINO;
              nCod = #clientes@#0;
              |SAL;
          |FINSI;
     |SIGUE;

     |SI nCod = 0;
         |ACABA;
     |FINSI;

     #clientes@#0 = #dsmow323#0;
     |LEE 000#clientes@.=;
     |SI FSalida = 0;
         |HAZ RecogeCliente;
     |FINSI;

     |CIERRA #dsmow323;
     |DELINDEX #dsmow323;
|FPRC;

|PROCESO EliminaCliente;
|FPRC;

|PROCESO BajaCliente;
     #dsm210dp#0 = enEmpresa;
     |LEE 101#dsm210dp.=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     aAlfa = "0000N" + &191 + " Esta seguro de eliminar el contribuyente y toda la informacion referente a el ? ";
     |CONFI aAlfa;
     |SI FSalida ! 0;
         |LIBERA #dsm210dp;

         |ACABA;
     |FINSI;

     |HAZ EliminaCliente;

     |BORRA 020#dsm210dp.a;
     |LIBERA #dsm210dp;
|FPRC;

|PROCESO LeeActivo;
     aAct = "A";
     |SI enSwCod = 1;
         aAct  = #clientes@#85;
     |FINSI;

     |SI enSwCod = 2;
         |SI #clientes@#94 ] "01.01.1900";
             aAct = "I";
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO ChequeaCliente;
     #clientes@#0 = enEmpresa;
     |LEE 000#clientes@.=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     #dsm210dp#0  = #clientes@#0;
     |LEE 101#dsm210dp.=;
     |SI FSalida ! 0; |ACABA; |FINSI;

     |HAZ MiraBloqueo;
     |SI eaBloqueo = "3";
         |ACABA;
     |FINSI;

     |HAZ LeeActivo;

     |SI aAct ! #dsm210dp#103;
         #dsm210dp#104 = aNegro;
         #dsm210dp#103 = aAct;
         |SI #dsm210dp#103 = "I";
             #dsm210dp#104 = aRojo;
         |FINSI;
         |GRABA 020#dsm210dp.a;
         |LIBERA #dsm210dp;
     |FINSI;
|FPRC;

|PROGRAMA;
     |SI nError = 1;  |ACABA;  |FINSI;

     |SI PARAMETRO = "ALTA";
         |HAZ AltaCliente;
     |FINSI;

     |SI PARAMETRO = "BAJA";
         |HAZ BajaCliente;
     |FINSI;

     |SI PARAMETRO = "CHEQUEO";
         |HAZ ChequeaCliente;
     |FINSI;
|FPRO;

|PROCESO T80;  |TIPO 80;
     |DBASS aBase; |QBF aBase;

     |SI enSwCod = 1;
         aCLI = aBase + "integral/def/dsam0000.mas";
         aNIF = aBase + "integral/def/dsam0103.mas";
         aREN = aBase + "integral/def/dsam0003.mas";
         aPRO = aBase + "integral/def/dsam0100.mas";
         aPOB = aBase + "integral/def/dsam0101.mas";
         aPAI = aBase + "integral/def/dsam0104.mas";
     |FINSI;

     |SI enSwCod = 2;
         aCLI = aBase + "basica/def/agifigen.mas";
         aNIF = aBase + "basica/def/agim0003.mas";
         aREN = "";
         aPRO = aBase + "basica/def/agiprovi.mas";
         aPOB = aBase + "basica/def/agipobla.mas";
         aPAI = aBase + "basica/def/agim0016.mas";
     |FINSI;

     aDPE = aBase + "basica/def/agidanif.mas";
     aREP = aBase + "basica/def/agim0004.mas";

     nError = 1;

     |CARGA_DEF aCLI, clientes@;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |CARGA_DEF aDPE, agidanif@;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |CARGA_DEF aREP, agim0004@;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |CARGA_DEF aNIF, regisnif@;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |CARGA_DEF aPRO, reprovi@;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |CARGA_DEF aPOB, repobla@;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |CARGA_DEF aPAI, rpaises@;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |SI enSwCod = 1;
         |CARGA_DEF aREN, dsam0003@;
         |SI FSalida ! 0;  |ACABA;  |FINSI;

         aPath = aBase + "integral/fich/";

         |PATH_DAT #dsam0003@#aPath#;

         |ABRET #dsam0003@;
     |FINSI;

     |SI enSwCod = 2;
         aPath = aBase + "basica/fich/";
     |FINSI;

     |PATH_DAT #clientes@#aPath#;
     |PATH_DAT #agidanif@#aPath#;
     |PATH_DAT #agim0004@#aPath#;
     |PATH_DAT #regisnif@#aPath#;

     |ABRET #clientes@;
     |ABRET #agidanif@;
     |ABRET #agim0004@;
     |ABRET #regisnif@;
     |ABRET #reprovi@;
     |ABRET #repobla@;
     |ABRET #rpaises@;
     |ABRET #dsm210dp;

     nError = 0;
|FPRC;

|PROCESO T90;  |TIPO 90;
     |SI nError = 1;  |ACABA;  |FINSI;

     |SI enSwCod = 1;
         |CIERRAT #dsam0003@;  |DESCARGA_DEF #dsam0003@;
     |FINSI;

     |CIERRAT #rpaises@;   |DESCARGA_DEF #rpaises@;
     |CIERRAT #repobla@;   |DESCARGA_DEF #repobla@;
     |CIERRAT #reprovi@;   |DESCARGA_DEF #reprovi@;
     |CIERRAT #regisnif@;  |DESCARGA_DEF #regisnif@;
     |CIERRAT #agim0004@;  |DESCARGA_DEF #agim0004@;
     |CIERRAT #agidanif@;  |DESCARGA_DEF #agidanif@;
     |CIERRAT #clientes@;  |DESCARGA_DEF #clientes@;
     |CIERRAT #dsm210dp;
|FPRC;
