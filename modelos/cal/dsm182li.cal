|FICHEROS;
     dsm182ca #1;
     dsm182li #2,MANTE;

     &regisnif@ #709;
     reflejo@   #22;

     &provinc@ #700;
     :/basica/def/agiprovi #701;
     :/integral/def/dsam0100 #702;

     &regisnif@;
     :/basica/def/agim0003;
     :/integral/def/dsam0103;
|FIN;

|VARIABLES;
     aPathModelo = "";
     aDef        = "";
     aAlfa       = "";
     aAlfa1      = "";
     nPara1      = 0;
     nPara2      = 0;

     &enEmpOrigen = 0;
     &enEjeOrigen = 0;
     &enComOrigen = 0;
     &eaDniOrigen = "";
     &enLinOrigen = 0;
     &enTantoEmp  = 0;
     &eaNomOrigen = "";
     &enSw999     = 0;
     nImporte     = 0;
     aBase        = "";
     nCanal       = 0;
     nModo        = 0;
     nLinea       = 0;
     nSocie       = 0;

  {20}aPopup        = "";
     nID            = 0;
     aID            = "";
     aTexto         = "";
|FIN;

|INCLUYE i_variar;

|PROCESO DespuesRep; |TIPO 0;
|FPRC;

|PROCESO Tipo2m182li; |TIPO 2;
      nImporte = #2#14;
      |SI #2#20 = "S"; nImporte = 0; |FINSI;
      |SI #2#14 ! 0;
          #1#6 = #1#6 +. nImporte;
          #1#8 = #1#8 +. 1;
      |FINSI;
|FPRC;

|RUTINA ClaveBaja22;
     #22#0  = #1#0;
     #22#1  = #1#1;
     #22#2  = #1#2;
     #22#3  = #1#3;
     #22#4  = #1#4;
     #22#5  = #1#5;
     #22#6  = "           ";
     #22#7  = 01;
|FRUT;

|RUTINA ClaveAlta22;
     #22#0  = #1#0;
     #22#1  = #1#1;
     #22#2  = #1#2;
     #22#3  = #1#3;
     #22#4  = #1#4;
     #22#5  = #1#5;
     #22#6  = "zzzzzzzzzzz";
     #22#7  = 99;
|FRUT;

|PROCESO MiraDnim182li;  |TIPO 6;
     |SI #2Campo = "            "; |ACABA; |FINSI;

     eaVarDni  = #2Campo;
     enCalcCif = 0;
     |HAZ CalculameDNI;
     eaVarDni = (eaVarDni + "             ") % 112;
     |SI #2Campo ! eaVarDni;
         |CONFI "0000SEl Nif puede ser Erroneo. Desea Calcularlo.";
         |SI FSalida = 0;
             #2Campo = eaVarDni;  |PINTA #2Campo;
             |ERROR;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO PonDatosNif;  |TIPO 0;
     eaCodNif  = #2#6;
     |HAZ LeeNifs;

     #2#10 = #709#1;   |PINTA #2#10;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 1;
         #2#11 = #709#4;  |PINTA #2#11;
         #2#25 = #709#7;  |PINTA #2#25;
         || #2#17 = #709#14; |PINTA #2#17;
     |FINSI;
|FPRC;

|PROCESO AntesLinea; |TIPO 7;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo ! 1; |ACABA; |FINSI;

     |DBASE aPathModelo;  |QBT aPathModelo;
     aDef = aPathModelo + "def/dsm182li";
     |CARGA_DEF aDef, reflejo@;
     |SI FSalida ! 0;
         aAlfa = "     Error en cargar el Def dsm182li.mas";
         |MENAV aAlfa;
         |ACABA;
     |FINSI;

     nLinea = 0;
     |ABRE #reflejo@;
     #reflejo@#0 = #dsm182li#0;
     #reflejo@#1 = #dsm182li#1;
     #reflejo@#2 = #dsm182li#2;
     #reflejo@#3 = #dsm182li#3;
     #reflejo@#4 = #dsm182li#4;
     #reflejo@#5 = #dsm182li#5;
     #reflejo@#6 = #dsm182li#6;
     #reflejo@#7 = 99;
     |LEE 041#reflejo@.];
     |SI FSalida = 0;
         |LEE 041#reflejo@.a;
     |SINO;
         |LEE 041#reflejo@.u;
     |FINSI;
     |SI FSalida = 0; |Y #reflejo@#0 = #dsm182li#0; |Y #reflejo@#1 = #dsm182li#1;
           |Y #reflejo@#2 = #dsm182li#2; |Y #reflejo@#3 = #dsm182li#3;
            |Y #reflejo@#4 = #dsm182li#4; |Y #reflejo@#5 = #dsm182li#5;
             |Y #reflejo@#6 = #dsm182li#6;
         nLinea = #reflejo@#7;
     |FINSI;

     #dsm182li#7 = nLinea + 1;
     |PINTA #dsm182li#7;

     |CIERRA #reflejo@;

     |DESCARGA_DEF #reflejo@;
|FPRC;

|PROCESO LeeProvincia182;
     |DBASS aBase;   |QBT aBase;
     aBase = aBase + "integral/def/dsam0000.mas";
     |XABRE "E", aBase, nCanal;
     |SI FSalida ] 0;
         enSwDeDonde = 1;
         |REFERENCIA_DEF provinc@, #702;
     |SINO;
         enSwDeDonde = 0;
         |REFERENCIA_DEF provinc@, #701;
     |FINSI;

     enCodProv = #dsm182li#11;
     |ABRE #700;
     #700#0 = enCodProv;
     |LEE 040#700=;
     |SI FSalida ! 0; |DDEFECTO #700; |FINSI;
     |CIERRA #700;

     #dsm182li#25 = #700#1; |PINTA #dsm182li#25;
|FPRC;

|PROCESO Tipo7C12; |TIPO 7;
    |SI #dsm182ca#23 = 1;
        aAlfa1 = "A o B";
    |FINSI;
    |SI #dsm182ca#23 = 3;
        aAlfa1 = "C, D o E";
    |FINSI;
    |SI #dsm182ca#23 = 4;
        aAlfa1 = "F o G";
    |FINSI;
    aAlfa = "0000Entre Clave con valor: " + aAlfa1;
    aAlfa = aAlfa + ". Pulse <F11> para ver Texto Claves";
    |SI #dsm182ca#23 ! 2;
        |MENSA aAlfa;
    |FINSI;
|FPRC;

|PROCESO Tipo66C12; |TIPO 66;
     |C_M #dsm182li#Campo#, 0, aAlfa;
     |SI aAlfa = "N"; |ACABA; |FINSI;

     aAlfa = "|C012WID";
     aID   = #dsm182li#aAlfa#;
     nID   = aID;

     aPopup1  = nID;  || Si fuera -1 en la posicion
     |SI #dsm182ca#23 = 1;
         aPopup2 = 2;
         aPopup3 = "A- Donativos no incluidos en las actividades prioritarios de mecenazgo";
         aPopup4 = "B- Donativos incluidos en las actividades prioritarios de mecenazgo";
     |FINSI;
     |SI #dsm182ca#23 = 3;
         aPopup2 = 3;
         aPopup3 = "C- Aportacion al patrimonio de discapacitados";
         aPopup4 = "D- Disposicion del patrimonio de discapacitados";
         aPopup5 = "E- Gasto de dinero y consumo de bienes fungibles aportados al Pat.Prot.";
     |FINSI;
     |SI #dsm182ca#23 = 4;
         aPopup2 = 2;
         aPopup3 = "F- Cuotas de afiliacion y aportaciones previstas (art.2.Dos.a Ley O. 8/2007)";
         aPopup4 = "G- Resto de donaciones y aportaciones precibidas";
     |FINSI;

     IaPopup  = aPopup1  <-;
     |ESPECIFICA "TRACKPOPUP", aPopup;
     |SI FSalida ! 0;
         IaPopup  = aPopup3  <-;
         IaPopup  = IaPopup + (FSalida - 1);

         aAlfa = aPopup % 101;  #dsm182li#12 = aAlfa;
     |FINSI;

     |PINTA #dsm182li#12;
|FPRC;

|PROCESO Tipo0C12; |TIPO 0;
     |C_M #dsm182li#Campo#,0,aAlfa;

     |C_M #dsm182li#15,1,aAlfa;
     |SI #dsm182li#1 [ 2015; |C_M #dsm182li#15,1,"N"; |FINSI;

     |SI aAlfa = "N"; |ACABA; |FINSI;

     aTexto = "";
     |SI #dsm182ca#23 = 1;
         |SI #dsm182li#12 = "A"; aTexto = "Donativos NO incluidos prioritarios de mecenazgo"; |FINSI;
         |SI #dsm182li#12 = "B"; aTexto = "Donativos incluidos prioritarios de mecenazgo";    |FINSI;
     |FINSI;
     |SI #dsm182ca#23 = 3;
         |SI #dsm182li#12 = "C"; aTexto = "Aportacion al patrimonio de discapacitados"; |FINSI;
         |SI #dsm182li#12 = "D"; aTexto = "Disposicion del patrimonio de discapacitados"; |FINSI;
         |SI #dsm182li#12 = "E"; aTexto = "Gasto de dinero y consumo de bienes fungibles"; |FINSI;
     |FINSI;
     |SI #dsm182ca#23 = 4;
         |SI #dsm182li#12 = "F"; aTexto = "Cuotas de afiliacion (art.2.Dos.a Ley O. 8/2007)"; |FINSI;
         |SI #dsm182li#12 = "G"; aTexto = "Resto de donaciones y aportaciones precibidas"; |FINSI;
     |FINSI;
     #dsm182li#24 = aTexto;
     |SI aTexto = "";
         |MENAV "0000La Clave Introducida NO es Correcta";
         |ERROR;
     |FINSI;
     |PINTA #dsm182li#24;

     aAlfa = "N";
     |SI #dsm182li#12 = "A"; |O #dsm182li#12 = "B"; aAlfa = "S"; |FINSI;
     |C_M #dsm182li#19,1,aAlfa;
     |C_M #dsm182li#20,1,aAlfa;
     aAlfa = "N";
     |SI #dsm182li#12 = "C"; |O #dsm182li#12 = "D";
         aAlfa = "S";
     |FINSI;
     |C_M #dsm182li#22,1,aAlfa;

     |SI #dsm182li#1 ] 2016;
         aAlfa = "N";
         |SI #dsm182li#12 = "A"; |O #dsm182li#12 = "B"; |O #dsm182li#12 = "G";
             aAlfa = "S";
             |SI #dsm182li#15 = " ";
                 #dsm182li#15 = "N"; |PINTA #dsm182li#15;
             |FINSI;
         |FINSI;
         |C_M #dsm182li#15,1,aAlfa;
     |FINSI;
|FPRC;

|PROCESO Tipo66C17; |TIPO 66;
     |C_M #dsm182li#Campo#, 0, aAlfa;
     |SI aAlfa = "N"; |ACABA; |FINSI;

     aAlfa = "|C017WID";
     aID   = #dsm182li#aAlfa#;
     nID   = aID;

     aPopup1  = nID;  || Si fuera -1 en la posicion
     aPopup2  = 15;
     aPopup3 = "01. ANDALUCIA";
     aPopup4 = "02. ARAGON";
     aPopup5 = "03. PRINCIPADO DE ASTURIAS";
     aPopup6 = "04. ILLES BALEARS";
     aPopup7 = "05. CANARIAS";
     aPopup8 = "06. CANTABRIA";
     aPopup9 = "07. CASTILLA-LA MANCHA";
     aPopup10= "08. CASTILLA Y LEON";
     aPopup11= "09. CATALUNYA";
     aPopup12= "10. EXTREMADURA";
     aPopup13= "11. GALICIA";
     aPopup14= "12. MADRID";
     aPopup15= "13. REGION DE MURCIA";
     aPopup16= "16. LA RIOJA";
     aPopup17= "17. COMUNIDAD VALENCIANA";

     IaPopup  = aPopup1  <-;
     |ESPECIFICA "TRACKPOPUP", aPopup;
     |SI FSalida ! 0;
         IaPopup  = aPopup3  <-;
         IaPopup  = IaPopup + (FSalida - 1);

         aAlfa = aPopup % 102;  #dsm182li#17 = aAlfa;
     |FINSI;

     |PINTA #dsm182li#17;
|FPRC;

|PROCESO EsFisico;
     aAlfa = #dsm182li#6 % 101;
     nSocie = 1;
     |SI aAlfa ] "0"; |Y aAlfa [ "9"; nSocie = 0; |FINSI;
     |SI aAlfa = "K"; |O aAlfa= "L"; |O aAlfa = "M";
         |O aAlfa = "X"; |O aAlfa = "Y"; |O aAlfa = "Z";
            nSocie = 0;
     |FINSI;
     |SI aAlfa = "E"; nSocie = 2; |FINSI;
|FPRC;

|PROCESO Tipo0C17; |TIPO 0;
     aAlfa = "S";
     |SI #dsm182li#17 = 00; aAlfa = "N"; |FINSI;
     |SI #dsm182li#17 > 17; |O #dsm182li#17 = 14; |O #dsm182li#17 = 15;
         |MENAV "0000Comunidad Erronea";
         |ERROR;
     |FINSI;
     |C_M #dsm182li#18,1,aAlfa;
     |SI aAlfa = "N";
         #dsm182li#18 = 0; |PINTA #dsm182li#18;
     |FINSI;
|FPRC;

|PROCESO Tipo0C15; |TIPO 0;
     aAlfa = Contenido;

     |SI #dsm182li#12 = "A"; |O #dsm182li#12 = "B"; |O #dsm182li#12 = "G"
         |SI #dsm182li#15 = " ";
             |MENAV "0000Introduzca Donativo Recurrente (S/N)";
             |ERROR;
             |ACABA;
         |FINSI;
     |SINO;
         #dsm182li#15 = " "; |PINTA #dsm182li#15;
     |FINSI;

     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 1; |O aAlfa ! #dsm182li#15;
         |SI #dsm182li#19 = " ";
             |HAZ EsFisico;
         |SINO;
             nSocie = 0;
             |SI #dsm182li#19 ! "F"; nSocie = 1; |FINSI;
         |FINSI;
         |SI nSocie = 0;
             |SI #dsm182li#15 = "N"; #dsm182li#13 = 30; |FINSI;
             |SI #dsm182li#15 = "S"; #dsm182li#13 = 35; |FINSI;
         |SINO;
             |SI #dsm182li#15 = "N"; #dsm182li#13 = 35; |FINSI;
             |SI #dsm182li#15 = "S"; #dsm182li#13 = 40; |FINSI;
         |FINSI;
         |PINTA #dsm182li#13;
     |FINSI;
|FPRC;

|PROCESO Tipo7C19; |TIPO 7;
     |C_M #dsm182li#Campo#, 0, aAlfa;
     |SI aAlfa = "N"; |ACABA; |FINSI;

     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 1; |O #dsm182li#19 = " ";
         |HAZ EsFisico;
         |SI nSocie = 0; #dsm182li#19 = "F"; |FINSI;
         |SI nSocie = 1; #dsm182li#19 = "J"; |FINSI;
         |SI nSocie = 2; #dsm182li#19 = "E"; |FINSI;
         |PINTA #dsm182li#19;
     |FINSI;
|FPRC;

|PROCESO Tipo0C19; |TIPO 0;
     |SI #dsm182ca#23 = "1"; |O #dsm182ca#23 = "2";
         |SI #dsm182li#19 = " ";
             |MENAV "0000Introduzca la Naturaleza declarado (F/J/E)";
             |ERROR;
         |FINSI;
     |SINO;
         #dsm182li#19 = " "; |PINTA #dsm182li#19;
     |FINSI;
|FPRC;

|PROCESO Tipo0C20; |TIPO 0;
     |SI #dsm182li#12 = "A"; |O #dsm182li#12 = "B";
         |SI #dsm182li#20 = " ";
             |MENAV "0000Introduzca Revocacion (S/N)";
             |ERROR;
         |FINSI;
     |SINO;
         #dsm182li#20 = " "; |PINTA #dsm182li#20;
     |FINSI;
     aAlfa = "S";
     |SI #dsm182li#20 ! "S";
         aAlfa = "N";
         #dsm182li#21 = 0; |PINTA #dsm182li#21;
     |FINSI;
     |C_M #dsm182li#21,1,aAlfa;
     |SI aAlfa = "N";
         #dsm182li#21 = 0; |PINTA #dsm182li#21;
     |FINSI;
|FPRC;

|PROCESO Tipo0C21; |TIPO 0;
     |SI FSalida = 2; |ACABA; |FINSI;

     |C_M #dsm182li#21,0,aAlfa;
     |SI aAlfa = "S"; |Y #dsm182li#21 = 0;
         |MENAV "0000Introduzca el Ejercicio de Revocacion";
         |ERROR;
     |FINSI;
|FPRC;

|PROCESO Tipo7C22; |TIPO 7;
    aAlfa = "N";
    |SI #dsm182li#12 = "C"; |O #dsm182li#12 = "D";
        |SI #dsm182li#16 = "S";
            aAlfa = "S";
        |FINSI;
    |FINSI;
    |C_M #dsm182li#22,1, aAlfa;
|FPRC;

|PROCESO Tipo0C22; |TIPO 0;
     |SI #dsm182li#12 = "C"; |O #dsm182li#12 = "D";
         |SI #dsm182li#16 = "S";
             |SI #dsm182li#22 = " ";
                 |MENAV "0000Introduzca Tipo de Bien (I/V/O)";
                 |ERROR;
             |FINSI;
         |SINO;
              #dsm182li#22 = " "; |PINTA #dsm182li#22;
         |FINSI;
     |SINO;
         #dsm182li#22 = " "; |PINTA #dsm182li#22;
     |FINSI;
     aAlfa = "S";
     |SI #dsm182li#22 = " ";
         aAlfa = "N";
         #dsm182li#23 = ""; |PINTA #dsm182li#23;
     |FINSI;
     |C_M #dsm182li#23,1,aAlfa;
|FPRC;

|PROCESO Tipo11m182li;  |TIPO 11;

     |SI FSalida = 10;
         eaVarDni  = #2Campo;
         enCalcCif = 1;
         |HAZ CalculameDNI;
         #2Campo = eaVarDni;
         |PINTA #2Campo;
         |ERROR;
     |FINSI;

     |SI FSalida = 11;
         |DBASE aPathModelo;  |QBT aPathModelo;
         aDef = aPathModelo + "def/dsm182li";
         |CARGA_DEF aDef, reflejo@;
         |SI FSalida ! 0;
             aAlfa = "     Error en cargar el Def dsm182li.mas";
             |MENAV aAlfa;
             |ERROR;
         |FINSI;

         |ABRE #22;
         |CONSULTA_F_CLAVE #1#22, ClaveBaja22, ClaveAlta22;
         |SI FSalida = 0;
             #2#0 = #22#0;
             #2#1 = #22#1;
             #2#2 = #22#2;
             #2#3 = #22#3;
             #2#4 = #22#4;
             #2#5 = #22#5;
             #2#6 = #22#6;
             #2#7 = #22#7;
             |LEE 000#2=;
         |FINSI;
         |CIERRA #22;

         |DESCARGA_DEF #reflejo@;

         |PTEC 809;
         |PTEC 808;
         |PONCONTROL  23, "SI";
     |FINSI;
|FPRC;

|PROCESO Tipo88m182li;  |TIPO 88;

     |DBASS aBase;   |QBT aBase;
     aBase = aBase + "integral/def/dsam0000.mas";
     |XABRE "E", aBase, nCanal;
     |SI FSalida ] 0;
         ||Desactivado

         |MODO_RELACION #2, 1, "agim0003", "N", "N";
         |MODO_RELACION #2, 1, "agiprovi", "N", "N";
         |MODO_RELACION #2, 1, "agim0003", "X", "X";
         |MODO_RELACION #2, 1, "agiprovi", "X", "X";

         || Activado

         |MODO_RELACION #2, 0, "dsam0103", "X", "X";
         |MODO_RELACION #2, 0, "dsam0100", "X", "X";
         |MODO_RELACION #2, 0, "dsam0103", "S", "S";
         |MODO_RELACION #2, 0, "dsam0100", "S", "S";

         |REFERENCIA_DEF provinc@,  #702;
     |SINO;

         |MODO_RELACION #2, 1, "dsam0103", "N", "N";
         |MODO_RELACION #2, 1, "dsam0100", "N", "N";
         |MODO_RELACION #2, 1, "dsam0103", "X", "X";
         |MODO_RELACION #2, 1, "dsam0100", "X", "X";

         |MODO_RELACION #2, 0, "agim0003", "X", "X";
         |MODO_RELACION #2, 0, "agiprovi", "X", "X";
         |MODO_RELACION #2, 0, "agim0003", "S", "S";
         |MODO_RELACION #2, 0, "agiprovi", "S", "S";

         |REFERENCIA_DEF provinc@,  #701;
     |FINSI;
|FPRC;
