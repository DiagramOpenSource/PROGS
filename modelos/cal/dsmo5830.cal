|FICHEROS;
     &program@ #0;

     dsmo5830  #1;
     dsmo5831  #583;
     dsmo5832  #584;

     reflejo@  #21;

     &provinc@ #700;
     :/basica/def/agiprovi #701;
     :/integral/def/dsam0100 #702;

     &poblaci@ #703;
     :/basica/def/agipobla #704;
     :/integral/def/dsam0101 #705;

     &haciend@ #706;
     :/basica/def/agihacie #707;
     :/integral/def/dsam0102 #708;

     :/integral/def/dsam0115 #115;
|FIN;

|VARIABLES;
/*
     &aPrograma     = "";
     &enCodProv     = 0;
     &enCodPobl     = 0;
     &eaPoblacion   = "";
     &eaPoblaParri  = "";
     &eaProvincia   = "";
     &enModoPob     = 0;
     &enSwMensa     = 0;
*/
     aAlfa          = "";
     aAlfa1         = "";
     aAlfa2         = "";
     fFecha         = @;
     &enAny         = 0;
     &enPer         = 0;
     aBase          = "";
     nCanal         = 0;
     aDef           = "";
     aPathModelo    = "";
     nTecla         = 0;

     &enEmpOrigen   = 0;
     &eaDelOrigen   = "";
     nModo          = 0;
     nSw            = 0;
     nSwFicha       = 0;

     fFechaAlta     = @;
     fFechaBaja     = @;
     aError         = "";
     nError         = 0;
     fFecOrAlta     = @;
     fFecOrBaja     = @;
     nPrimer        = 0;
     nSwExis        = 0;
     nSwBaja        = 0;
     nCambio        = 0;
     &enDInst       = 0;
     &enHInst       = 0;
|FIN;

|INCLUYE i_variar;

|PROCESO Tipo88mo5830;  |TIPO 88;
     |REFERENCIA_DEF program@, #1;

     |DBASS aBase;   |QBT aBase;
     aBase = aBase + "integral/def/dsam0000.mas";
     |XABRE "E", aBase, nCanal;
     |SI FSalida ] 0;
         ||Desactivado

         |MODO_RELACION #0, 1, "agihacie", "N", "N";
         |MODO_RELACION #0, 1, "agiprovi", "N", "N";
         |MODO_RELACION #0, 1, "agipobla", "N", "N";

         |MODO_RELACION #0, 1, "agihacie", "X", "X";
         |MODO_RELACION #0, 1, "agiprovi", "X", "X";
         |MODO_RELACION #0, 1, "agipobla", "X", "X";

         || Activado

         |MODO_RELACION #0, 0, "dsam0102", "X", "X";
         |MODO_RELACION #0, 0, "dsam0100", "X", "X";
         |MODO_RELACION #0, 0, "dsam0101", "X", "X";
         |MODO_RELACION #0, 0, "dsam0115", "X", "X";

         |MODO_RELACION #0, 1, "dsam0102", "N", "N";
         |MODO_RELACION #0, 0, "dsam0100", "S", "S";
         |MODO_RELACION #0, 0, "dsam0101", "S", "S";
         |MODO_RELACION #0, 0, "dsam0115", "S", "S";

         |REFERENCIA_DEF haciend@,  #708;
         |REFERENCIA_DEF provinc@,  #702;
         |REFERENCIA_DEF poblaci@,  #705;
     |SINO;
         |MODO_RELACION #0, 1, "dsam0102", "N", "N";
         |MODO_RELACION #0, 1, "dsam0100", "N", "N";
         |MODO_RELACION #0, 1, "dsam0101", "N", "N";
         |MODO_RELACION #0, 1, "dsam0115", "N", "N";

         |MODO_RELACION #0, 1, "dsam0102", "X", "X";
         |MODO_RELACION #0, 1, "dsam0100", "X", "X";
         |MODO_RELACION #0, 1, "dsam0101", "X", "X";
         |MODO_RELACION #0, 1, "dsam0115", "X", "X";

         |MODO_RELACION #0, 0, "agihacie", "X", "X";
         |MODO_RELACION #0, 0, "agipobla", "X", "X";
         |MODO_RELACION #0, 0, "agiprovi", "X", "X";

         |MODO_RELACION #0, 1, "agihacie", "N", "N";
         |MODO_RELACION #0, 0, "agipobla", "S", "S";
         |MODO_RELACION #0, 0, "agiprovi", "S", "S";

         |REFERENCIA_DEF haciend@,  #707;
         |REFERENCIA_DEF provinc@,  #701;
         |REFERENCIA_DEF poblaci@,  #704;
     |FINSI;
|FPRC;

|PROCESO Tipo66C4mo5830; |TIPO 66;
     nTecla = FSalida;

     |DBASS aBase;   |QBT aBase;
     aBase = aBase + "integral/def/dsam0000.mas";
     |XABRE "E", aBase, nCanal;
     |SI FSalida ] 0;
         |REFERENCIA_DEF haciend@, #708;
     |SINO;
         |REFERENCIA_DEF haciend@,  #707;
     |FINSI;

     |ABRE #706;
     #706#0 = #1#4;
     #706#1 = 1;
     |CONSULTA_CLAVE #1#706;
     |CIERRA #706;
     #1#4 = #706#0; |PINTA #1#4;
|FPRC;

|PROCESO LeeProvincia583;
     |DBASS aBase;   |QBT aBase;
     aBase = aBase + "integral/def/dsam0000.mas";
     |XABRE "E", aBase, nCanal;
     |SI FSalida ] 0;
         enSwDeDonde = 1;
         |REFERENCIA_DEF provinc@, #702;
     |SINO;
         enSwDeDonde = 0;
         |REFERENCIA_DEF provinc@, #701;
     |FINSI;

     enCodProv = #1#9;
     |ABRE #700;
     #700#0 = enCodProv;
     |LEE 040#700=;
     |SI FSalida ! 0; |DDEFECTO #700; |FINSI;

     #1#12 = #700#1; |PINTA #1#12;
     |CIERRA #700;
|FPRC;

|PROCESO LeeLocalidad583;
     |DBASS aBase;   |QBT aBase;
     aBase = aBase + "integral/def/dsam0000.mas";
     |XABRE "E", aBase, nCanal;
     |SI FSalida ] 0;
         enSwDeDonde = 1;
         |REFERENCIA_DEF provinc@, #702;
         |REFERENCIA_DEF poblaci@, #705;
     |SINO;
         enSwDeDonde = 0;
         |REFERENCIA_DEF provinc@, #701;
         |REFERENCIA_DEF poblaci@, #704;
     |FINSI;

     enCodProv = #1#9;
     enCodPobl = #1#10;

     |ABRE #703;
     |DDEFECTO #703;
     #703#0 = enCodProv;
     #703#1 = enCodPobl;
     |LEE 040#703=;
     |SI FSalida = 0;
         |HAZ ParrillaPobla;
         #1#11 = eaPoblacion; |PINTA #1#11;
     |SINO;
         |SI enSwMensa = 0;
             |MENAV "    No existe Poblacion.";
             |ERROR;
         |FINSI;
     |FINSI;
     |CIERRA #703;

     |ABRE #700;
     #700#0 = enCodProv;
     |LEE 040#700=;
     |SI FSalida ! 0; |DDEFECTO #700; |FINSI;

     #1#12 = #700#1; |PINTA #1#12;
     |CIERRA #700;
|FPRC;

|PROCESO ParrillaPobla;
     |SI enSwDeDonde = 1;
         eaPoblacion = #703#3;
         enModoPob   = 1;
         |SUB_EJECUTA_NP ":integral/dsaw9996";
         |SI eaPoblaParri ! "";
             eaPoblacion = eaPoblaParri;
         |SINO;
             eaPoblacion = #703#3;
         |FINSI;
     |SINO;
         eaPoblacion = #703#2;
         enModoPob   = 1;
         |SUB_EJECUTA_NP ":basica/agiw9996";
         |SI eaPoblaParri ! "";
             eaPoblacion = eaPoblaParri;
         |SINO;
             eaPoblacion = #703#2;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO Tipo7C4mo5830; |TIPO 7;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 1; |Y #1#4 = 00;
         aAlfa = eaDelOrigen % 102;
         #1#4  = aAlfa; |PINTA #1#4;
     |FINSI;
|FPRC;

|| ////////////////////////////////////////////////////////////////
|PROCESO LeeLineaModelo;
       nSwFicha = 0;
       |DDEFECTO #584;
       #584#0 = #583#0;
       #584#1 = #583#1;
       #584#2 = #583#2;
       #584#4 = #1#2;
       |LEE 041#584=;
       |SI FSalida = 0; nSwFicha = 1; |FINSI;
|FPRC;

|PROCESO LeeLosModelos;

   |SI #583#9 = " "; |ACABA; |FINSI;

   enAny = #583#1;
   enPer = #583#2;
   |HAZ CalculaFecha;

   nSwExis = 1;               || Debe existir = 1
   |SI fFechaAlta > efFechaF;
        nSwExis = 0;
   |SINO;
        |SI fFechaBaja > "01.01.0000"; |Y fFechaBaja < efFechaI;
            nSwExis = 0;
        |FINSI;
   |FINSI;
   |SI nSwBaja = 1;
        nSwExis = 0;   || No debe existir
   |FINSI;

   |HAZ LeeLineaModelo;
   |SI nSwFicha = 0;  |Y nSwExis = 1; || ...   Y no existe y deberia
       aError = "1234SExisten modelos elaborados entre las fechas de alta/baja.";
       nError = 1;
       |SI #583#11 = "X";
           aError = "    Modelo del " + #583#4 + " Ejercicio " + #583#1 + " impreso. ";
           aError = aError + "Modifique las fechas de alta/baja";
           nError = 2;
           |ERROR10;
       |SINO;
            aError = aError + ". Se dara de Alta. Continuar ";
        |FINSI;
   |FINSI;
   |SI nSwFicha = 1;  |Y nSwExis = 0; || ...   Existe y no deberia
       aError = "1234SExisten datos en modelos.";
       nError = 1;
       |SI #583#11 = "X";
           aError = "    Existen modelos impresos fuera de las fechas de alta/baja";
           aError = aError + "Modifique las fechas";
           nError = 2;
           |ERROR10;
       |SINO;
           aError = aError + " Desea borrarlos";
       |FINSI;
   |FINSI;
   |SI nSwFicha = 1;  |Y nSwBaja = 1;  || ...  La baja
       aError = "1234SExisten datos en modelos.";
       nError = 1;
       |SI #583#11 = "X";
           aError = "    Existen Modelos Impresos.";
           aError = aError + ". NO PUEDE DARLA DE BAJA";
           nError = 2;
           |ERROR10;
       |SINO;
           aError = aError + " Desea dar de baja";
       |FINSI;
   |FINSI;
|FPRC;

|DEFBUCLE LeeLosModelos;
   #583#1;
   ;
   #1#0, 2013, 01;
   #1#0, 2099, 99;
   ;
   NULL, LeeLosModelos;
|FIN;

|PROCESO Comprobar;
   nError  = 0;
   aError  = "";
   |ABRE #584;
   |BUCLE LeeLosModelos;
   |CIERRA #584;
|FPRC;

|| ////////////////////////////////////////////////////////////////
|PROCESO Lee2Clave;
   |SI #21#1 ! #1#1;
       nSw = 1;
       aAlfa = ("00" + #21#1) % - 102;
   |FINSI;
|FPRC;

|DEFBUCLE Lee2Clave;
   #21#2002;
   ;
   #1#0, #1#2;
   #1#0, #1#2;
   ;
   NULL, Lee2Clave;
|FIN;

|PROCESO Tipo0C2mo5830; |TIPO 0;
         |SI nPrimer = 0;
             fFecOrAlta = #1#5;
             fFecOrBaja = #1#6;
             nPrimer    = 1;
         |FINSI;

         |DBASE aPathModelo;  |QBT aPathModelo;
         aDef = aPathModelo + "def/dsmo5830";
         |CARGA_DEF aDef, reflejo@;
         |SI FSalida ! 0;
             aAlfa = "     Error en cargar el Def dsmo5830.mas";
             |MENAV aAlfa;
             |ERROR;
         |FINSI;

         nSwFicha = 0;
         nSw      = 0;
         aAlfa    = "";
         |BUCLE Lee2Clave;
         |SI nSw = 1;
             aAlfa = "    Instalacion ya informada en la Linea: " + aAlfa;
             |MENAV aAlfa;
             |ERROR;
         |FINSI;

         |DESCARGA_DEF #reflejo@;
|FPRC;

|PROCESO Fechamo5830; |TIPO 0;
   |SI FSalida = 2; |ACABA; |FINSI;

        |SI Campo = 5; |Y #1#5 [ "01.01.0000";
            |ERROR;
            |ACABA;
        |FINSI;

        |SI Campo = 6; |Y #1#6 > "01.01.0000";
            |SI #1#6 < #1#5;
                |MENAV "    La fecha de Baja no es correcta";
                |ERROR;
                |ACABA;
            |FINSI;
        |FINSI;

  |SI Campo = 6;
        nSw = 0;
        aAlfa1 = fFecOrAlta; aAlfa1 = aAlfa1 % 407;
        aAlfa2 = #1#5;       aAlfa2 = aAlfa2 % 407;
        |SI aAlfa1 ! aAlfa2;
            nSw = 1;
        |SINO;
            aAlfa1 = fFecOrBaja; aAlfa1 = aAlfa1 % 407;
            aAlfa2 = #1#6;       aAlfa2 = aAlfa2 % 407;
            |SI aAlfa1 ! aAlfa2; nSw = 1; |FINSI;
        |FINSI;

        |SI nSw = 1;
            fFechaAlta = #1#5;
            fFechaBaja = #1#6;
            |HAZ Comprobar;
            |SI nError ! 0;
                |SI nError = 1;
                    |CONFI aError;
                    |SI FSalida ! 0; nError = 2; |FINSI;
                |SINO;
                    |MENAV aError;
                |FINSI;
                |SI nError = 2;  |ERROR;  |ACABA;  |FINSI;
            |FINSI;
        |FINSI;
   |FINSI;
   nCambio = 1;
|FPRC;

|PROCESO Tipo1mo5830; |TIPO 1;
    nModo = (FEntrada / 100) ! 0;
    |SI nModo = 3;
        nSwBaja = 1;
        |HAZ Comprobar;
        |SI nError ! 0;
            |SI nError = 1;
                |CONFI aError;
                |SI FSalida ! 0; nError = 2; |FINSI;
            |SINO;
                |MENAV aError;
            |FINSI;
            |SI nError = 2;  |ERROR;  |ACABA;  |FINSI;
        |FINSI;
        nCambio = 1;
        nSwBaja = 0;
    |FINSI;

|FPRC;

|PROCESO Tipo3mo5830; |TIPO 3;

    aAlfa = #1#2; |QBF aAlfa;
    |SI aAlfa = "";
        |MENAV "    Error. No informado el Numero de Registro de la Instalacion";
        |ERROR;
        |ACABA;
    |FINSI;

    |SI #1#5 [ "01.01.0000";
        |MENAV "    Error. No informada la fecha de alta de la Instalacion";
        |ERROR;
        |ACABA;
    |FINSI;

    nSwBaja = 0;
    nPrimer = 0;
    fFecOrAlta = "01.01.0000";
    fFecOrBaja = "01.01.0000";
|FPRC;

|| /////////////////////////////////////

|RUTINA ClaveBaja21;
     #21#0  = #1#0;
     #21#1  = 01;
     #21#2  = "                         ";
|FRUT;

|RUTINA ClaveAlta21;
     #21#0  = #1#0;
     #21#1  = 99;
     #21#2  = "zzzzzzzzzzzzzzzzzzzzzzzzz";
|FRUT;

|PROCESO Tipo11mo5830; |TIPO 11;

     |SI FSalida = 11;
         |DBASE aPathModelo;  |QBT aPathModelo;
         aDef = aPathModelo + "def/dsmo5830";
         |CARGA_DEF aDef, reflejo@;
         |SI FSalida ! 0;
             aAlfa = "     Error en cargar el Def dsmo5830.mas";
             |MENAV aAlfa;
             |ERROR;
         |FINSI;

         |ABRE #21;
         |CONSULTA_F_CLAVE #2#21, ClaveBaja21, ClaveAlta21;
         |SI FSalida = 0;
             #1#0 = #21#0;
             #1#1 = #21#1;
             |LEE 000#1=;
         |FINSI;
         |CIERRA #21;

         |DESCARGA_DEF #reflejo@;

         |PTEC 809;
         |PTEC 808;
         |PONCONTROL  23, "SI";
     |FINSI;

     |SI FSalida = 12;
         |CONFI "    SEmitir informe de Comprobacion";
         |SI FSalida = 0;
             enEmpresa = #1#0;
             enDInst = 01;
             enHInst = 99;
             |SUB_EJECUTA_NP "dsmoy082";
         |FINSI;

         |ERROR;
     |FINSI;

     |SI FSalida = 13;
         |CONFI "    SEmitir Ficha de esta instalacion";
         |SI FSalida = 0;
             enEmpresa = #1#0;
             enDInst   = #1#1;
             enHInst   = 0;
             |SUB_EJECUTA_NP "dsmoy082";
         |FINSI;

         |ERROR;
     |FINSI;
|FPRC;

|| /////////////////////////////////////////////////////////////////////////
|RUTINA ClaveBaja2;
     #1#0 = enEmpresa;
     #1#1 = 01;
|FRUT;

|RUTINA ClaveAlta2;
     #1#0 = enEmpresa;
     #1#1 = 99;
|FRUT;

|| /////////////////////////////////////////////////////////////////////////
|PROGRAMA;

     eaNomDef    = "dsmo5830";

|| \\\\\\ Alta del Modelo ////////
     nCambio = 0;
     |PUSHV 0501 2380;
     |PINPA #0#1;
     |ENTLINEAL #1#1, -2, 2, 22, 0, ClaveBaja2, ClaveAlta2;
     |SI nCambio = 1;
         |HAZ ActualTodos;
     |FINSI;
     |POPV;
|FPRO;
