|FICHEROS;
     dsmo5830  #1;
     dsmo5831  #583;
     dsmo5832  #584;
     espejo@   #23;
|FIN;

|VARIABLES;
     nPasa     = 0;
     nBorra    = 0;
     nOpera    = 0;

     fFecha    = @;
     aAlfa     = "";
     &enAny    = 0;
     &enPer    = 0;

     nCampo8   = 0;
     nCampo10  = 0;
     nCampo13  = 0;
     nDPer     = 0;
     nHPer     = 0;
     aDef      = "";
     aPathModelo = "";
|FIN;

|INCLUYE i_variar;

|PROCESO CalculaFecha;
   aAlfa = enAny;
   aAlfa = "01.01." + aAlfa; efFechaI = aAlfa;

   aAlfa = enAny;
   |SI enPer = 03; aAlfa = "31.03." + aAlfa; |FINSI;
   |SI enPer = 06; aAlfa = "30.06." + aAlfa; |FINSI;
   |SI enPer = 09; aAlfa = "30.09." + aAlfa; |FINSI;
   |SI enPer = 12; aAlfa = "31.12." + aAlfa; |FINSI;
   |SI enPer = 99; aAlfa = "31.12." + aAlfa; |FINSI;
   efFechaF = aAlfa;
|FPRC;

|| /////////////////////////////////////////////////////////////////////////
|PROCESO ElEspPas;
        nCampo8  = nCampo8  + #23#7;
        nCampo10 = nCampo10 + #23#9;
        nCampo13 = nCampo13 + #23#14;
|FPRC;

|DEFBUCLE ElEspPas;
     #23#1004;
     ;
     #584#0, #584#1, nDPer, #584#4;
     #584#0, #584#1, nHPer, #584#4;
     be;
     NULL, ElEspPas;
|FIN;


|PROCESO Leedsmo5832;
   |SI nOpera = 1;
       nBorra = 0;
       #1#0 = #584#0;
       #1#2 = #584#4;
       |LEE 141#1=;
       |SI FSalida ! 0;
           nBorra = 1;
       |SINO;
           |SI #1#5 > efFechaF; nBorra = 1; |FINSI;
           |SI #1#6 < efFechaI; |Y #1#6 > "01.01.0000"; nBorra = 1; |FINSI;
       |FINSI;
       |SI nBorra = 1;
           |BORRA 020#584a;
       |FINSI;
   |FINSI;

   |SI nOpera = 3;
       nCampo8  = 0; nDPer = 1;
       nCampo10 = 0; nHPer = #584#2 - 1;
       nCampo13 = 0;
       |BUCLE ElEspPas;
       |SI #584#2 ! 99;
           #584#8  = nCampo8  + #584#7;
           #584#10 = nCampo10 + #584#9;
           #584#12 = (#584#10 % #584#11) ! 2;
           #584#13 = nCampo13;
           #584#14 = #584#12 - #584#13;
           |SI #584#14 < 0;
               #584#14 = 0;
           |FINSI;
       |SINO;
           |SI #584#7  = #584#8;
               #584#7  = nCampo8;
               #584#8  = nCampo8;
           |SINO;
               #584#7  = nCampo8;
           |FINSI;
           |SI #584#9  = #584#10;
               #584#9   = nCampo10;
               #584#10  = nCampo10;
           |SINO;
               #584#9   = nCampo10;
           |FINSI;
           #584#12 = (#584#10 % #584#11) ! 2;
           #584#13 = nCampo13;
           #584#14 = #584#12 - #584#13;
       |FINSI;
       |GRABA 020#584a;
   |FINSI;

   |SI nOpera = 2;
       #583#8  = #583#8  + 1;
       #583#6  = #583#6  + #584#8;
       #583#7  = #583#7  + #584#10;
       #583#18 = #583#18 + #584#12;
       #583#19 = #583#19 + #584#13;
       #583#20 = #583#20 + #584#14;
   |FINSI;

   |LIBERA #584;
|FPRC;

|DEFBUCLE Leedsmo5832;
   #584#1;
   ;
   #583#0, #583#1, #583#2, "                         ";
   #583#0, #583#1, #583#2, "zzzzzzzzzzzzzzzzzzzzzzzzz";
   be;
   NULL, Leedsmo5832;
|FIN;

|PROCESO LeoInstalaciones;
   nPasa = 1;
   |SI #1#5 > efFechaF;     || la fecha alta es superior a la fecha
       nPasa = 0;           || final del devengo
   |SINO;
       |SI #1#6 > "01.01.0000";
           |SI #1#6 < efFechaI;
               nPasa = 0;   || fecha baja antes del inicio devengo
           |FINSI;
       |FINSI;
   |FINSI;

   |SI nPasa = 0; |ACABA; |FINSI;

   #584#0 = #583#0;
   #584#1 = #583#1;
   #584#2 = #583#2;
   #584#4 = #1#2;
   |LEE 141#584=;
   |SI FSalida ! 0;
       |DDEFECTO #584;
       #584#0 = #583#0;
       #584#1 = #583#1;
       #584#2 = #583#2;
       #584#4 = #1#2;
       |GRABA 020#584b;
   |FINSI;
   #584#3 = #583#3;
   #584#5 = #1#3;
   #584#6 = #1#4;
   |GRABA 020#584a;
   |LIBERA #584;

   |LIBERA #1;
|FPRC;

|DEFBUCLE LeoInstalaciones;
   #1#1;
   ;
   enEmpresa, 01;
   enEmpresa, 99;
   be;
   NULL, LeoInstalaciones;
|FIN;

|PROCESO Actualizacion;

   enAny = #583#1;
   enPer = #583#2;
   |HAZ CalculaFecha;

   |ABRE #584;
   |BUCLE LeoInstalaciones;
   |CIERRA #584;

   |ABRE #1;
   |SELECT #2#1;
   nOpera = 1;           || Lee para Borrar Lineas
   |BUCLE Leedsmo5832;
   |SELECT #1#1;
   |CIERRA #1;

   |DBASE aPathModelo;  |QBT aPathModelo;
   aDef = aPathModelo + "def/dsmo5832";
   |CARGA_DEF aDef, espejo@;
   |SI FSalida = 0;
       nOpera = 3;           || Descargar datos
       |BUCLE Leedsmo5832;
       |DESCARGA_DEF #espejo@;
   |FINSI;

   #583#6  = 0;
   #583#7  = 0;
   #583#8  = 0;
   #583#18 = 0;
   #583#19 = 0;
   #583#20 = 0;
   nOpera = 2;           || Lee para Calcular
   |BUCLE Leedsmo5832;

   |SI #583#6 = 0; |Y #583#7 = 0; |Y #583#20 = 0;
       #583#9 = " ";
       #583#10 = "01.01.0000";
       |GRABA 020#583a;
   |SINO;
       #583#9 = "X";
       #583#10 = ~;

       |GRABA 020#583a;
   |FINSI;
   |LIBERA #583;
|FPRC;

|DEFBUCLE Actualizacion;
   #583#1;
   9, 11,16;
   enEmpresa, 2013, 01, "X", " ", " ";
   enEmpresa, 2099, 99, "X", " ", " ";
   ;
   NULL, Actualizacion;
|FIN;

|PROCESO ActualTodos;
   |BUCLE Actualizacion;
|FPRC;
