|FICHEROS;
     dsmo5830 #1;
     dsmo5831 #583;
     dsmo5832 #584;

     dsmow153 #153;
     &reflejo@ #22;
|FIN;

|VARIABLES;
     aFichero = "";
     nSwChequea = 0;
     nBorra     = 0;
     nCampo     = 0;
     nCampo8    = 0;
     nCampo10   = 0;
     nCampo13   = 0;
     nComp      = 0;
     nImporte   = 0;
     nImpCobro  = 0;
     nHay       = 0;
     nEjerCobro = 0;
     nNegativo  = 0;
     aCampo7    = "";
     nSw        = 0;

     nComplem   = 0;
     nLinea     = 0;
     &enSw999   = 0;
     &enAny     = 0;
     &enPer     = 0;
     aTexto     = "";
     &aDef      = "";

     nDPer      = 0;
     nHPer      = 0;
     nPer       = 0;
     aAlfa      = "";
     nAlta99    = 0;
|FIN;

|INCLUYE i_variar;

|RUTINA ClaveBaja2;
     #584#0 = #583#0;
     #584#1 = #583#1;
     #584#2 = #583#2;
     #584#3 = #583#3;
     #584#4 = "                         ";
|FRUT;

|RUTINA ClaveAlta2;
     #584#0 = #583#0;
     #584#1 = #583#1;
     #584#2 = #583#2;
     #584#3 = #583#3;
     #584#4 = "zzzzzzzzzzzzzzzzzzzzzzzzz";
|FRUT;

|| ///////////////////////////////////////////////////////////////////
|PROCESO CalculoTotal;
       #22#8  = #22#8  + 1;
       #22#6  = #22#6  + #584#8;
       #22#7  = #22#7  + #584#10;
       #22#18 = #22#18 + #584#12;
       #22#19 = #22#19 + #584#13;
       #22#20 = #22#20 + #584#14;
|FPRC;

|DEFBUCLE CalculoTotal;
    #584#1;
    ;
    #583#0, #583#1, nPer, "                         ";
    #583#0, #583#1, nPer, "zzzzzzzzzzzzzzzzzzzzzzzzz";
    ;
    NULL, CalculoTotal;
|FIN;

|PROCESO Espejo5831;

   nPer   = #22#2;
   #22#6  = 0;
   #22#7  = 0;
   #22#8  = 0;
   #22#18 = 0;
   #22#19 = 0;
   #22#20 = 0;
   |BUCLE CalculoTotal;

   |SI #22#6 = 0; |Y #22#7 = 0; |Y #22#20 = 0;
       #22#9 = " ";
       #22#10 = "01.01.0000";
       |GRABA 020#22a;
   |SINO;
       #22#9 = "X";
       #22#10 = ~;

       |GRABA 020#22a;
   |FINSI;
   |LIBERA #22;
|FPRC;

|DEFBUCLE Espejo5831;
   #22#1003;
   ;
   #583#0, #583#1, nDPer;
   #583#0, #583#1, nHPer;
   ;
   NULL, Espejo5831;
|FIN;

|| ////////////////////////////////
|PROCESO TratarResAnual;
     nAlta99 = 0;
     aDef = "dsmo5831";
     |HAZ CargaReflejo;
     |SI aDef ! "";
         |ABRE #22;
         #22#0 = #583#0;
         #22#1 = #583#1;
         #22#2 = 99;
         |LEE 041#22=;
         |SI FSalida ! 0; |O #22#9 = " ";
             nAlta99 = 1;
         |FINSI;
         |CIERRA #22;
         |HAZ DescargaReflejo;
     |FINSI;
     |SI nAlta99 ! 0;
         |SALVA_FICHA #583;
         #583#2 = 99;
         |LEE 141#583=;
         |SI FSalida ! 0;
             |DDEFECTO #583;
             #583#0 = enEmpresa;
             #583#1 = enEjer;
             #583#2 = 99;
             #583#3 = enModelo;
             |HAZ DefPeriodo;
             |GRABA 020#583b;
         |FINSI;

         enAny = #583#1;
         enPer = 99;
         |HAZ Actualizacion;
         |GRABA 020#583a;
         |LIBERA #583;

         |REPON_FICHA #583;
     |FINSI;
|FPRC;

|PROCESO ElEspejo;
    |SI #22#2 ! 99;
        #22#8  = nCampo8  + #22#7;
        #22#10 = nCampo10 + #22#9;
        #22#12 = (#22#10 % #22#11) ! 2;
        #22#13 = nCampo13;
        #22#14 = #22#12 - #22#13;
        |SI #22#14 < 0;
            #22#14 = 0;
        |FINSI;
    |SINO;
        |SI #22#7  = #22#8;
            #22#7  = nCampo8;
            #22#8  = nCampo8;
        |SINO;
            #22#7  = nCampo8;
        |FINSI;
        |SI #22#9   = #22#10;
            #22#9   = nCampo10;
            #22#10  = nCampo10;
        |SINO;
            #22#9   = nCampo10;
        |FINSI;
        #22#12 = (#22#10 % #22#11) ! 2;
        #22#13 = nCampo13;
        #22#14 = #22#12 - #22#13;
    |FINSI;
    nCampo8  = #22#8;
    nCampo10 = #22#10;
    nCampo13 = #22#13 + #22#14;
    |GRABA 020#22a;
    |LIBERA #22;
|FPRC;

|DEFBUCLE ElEspejo;
     #22#1004;
     ;
     #584#0, #584#1, nDPer, #584#4;
     #584#0, #584#1, nHPer, #584#4;
     be;
     NULL, ElEspejo;
|FIN;

|PROCESO Recalcula2;
     |SI nSwChequea = 0;
         nSwChequea = 1;
         |ERROR10;
         |ACABA;
     |FINSI;

     |SI nBorra = 1;
         #584#8  = #584#8  - #584#7;
         #584#10 = #584#10 - #584#9;
         #584#13 = #584#13 - #584#14;
     |FINSI;

     nDPer    = #584#2 + 1;
     nHPer    = 99;
     nCampo8  = #584#8;
     nCampo10 = #584#10;
     nCampo13 = #584#13 + #584#14;
     |BUCLE ElEspejo;

     |SI nBorra = 1;
         |BORRA 020#584a;
     |FINSI;

     |LIBERA #584;
|FPRC;

|DEFBUCLE dsmo5832;
     #584#1;
     ;
     #583#0, #583#1, #583#2, "                         ";
     #583#0, #583#1, #583#2, "zzzzzzzzzzzzzzzzzzzzzzzzz";
     be;
     NULL, Recalcula2;
|FIN;

|PROCESO Recalculo;
     |SI nBorra = 0;
              nSwChequea = 0;  |BUCLE dsmo5832;

          |SI nSwChequea = 0;  |ACABA;  |FINSI;
     |FINSI;

     |SI #583#2 ! 99;
         |HAZ TratarResAnual;
     |FINSI;

     aDef = "dsmo5832";
     |HAZ CargaReflejo;
     |SI aDef ! "";
         |BUCLE dsmo5832;
     |FINSI;
     |HAZ DescargaReflejo;

     nDPer = #583#2 + 1;
     nHPer = 99;
     aDef  = "dsmo5831";
     |HAZ CargaReflejo;
     |SI aDef ! "";
         |BUCLE Espejo5831;
     |FINSI;
     |HAZ DescargaReflejo;
|FPRC;

|PROCESO Menug;

     |SI #583#8 = 0;
         aAlfa = "    No existen Reg.Instalaciones. No se puede elaborar el Modelo ";
         |SI enTipoEntrada = 4;
             aAlfa = "    No existe Modelo";
         |FINSI;
         |MENAV aAlfa;
         |ACABA;
     |FINSI;

     |ENTLINEAL #1#584, -4, 4, 22, 0, ClaveBaja2, ClaveAlta2;
     |SI enTipoEntrada ! 4;
         |HAZ Recalculo;
     |FINSI;
|FPRC;

|PROCESO TextoPeriodo;
     |SI #583#2 = 03; aTexto = "A cuenta de Enero a Marzo     "; |FINSI;
     |SI #583#2 = 06; aTexto = "A cuenta de Enero a Junio     "; |FINSI;
     |SI #583#2 = 09; aTexto = "A cuenta de Enero a Septiembre"; |FINSI;
     |SI #583#2 = 12; aTexto = "A cuenta de Enero a Diciembre "; |FINSI;
     |SI #583#2 = 99; aTexto = "Liquidacion a¤o               "; |FINSI;
     |ATRI S; |PINTA 1026, aTexto; |ATRI 0;
|FPRC;

|PROCESO Tipo12mo5831;  |TIPO 12;
     |HAZ Menug;
|FPRC;

|PROCESO DefPeriodo;
      |SI #583#2 =  3; #583#4 = "1T"; |FINSI;
      |SI #583#2 =  6; #583#4 = "2T"; |FINSI;
      |SI #583#2 =  9; #583#4 = "3T"; |FINSI;
      |SI #583#2 = 12; #583#4 = "4T"; |FINSI;
      |SI #583#2 = 99; #583#4 = "0A"; |FINSI;
|FPRC;

|PROGRAMA;
     |HAZ LeeEURODBMOD;

     eaNomDef    = "dsmo5831";
     enResultado = 0;

|| \\\\\\ Alta del Modelo ////////

     |SI enTipoEntrada = 1;

         |CABEZA "Modelo 583";
         |ABRE #583;
         #583#0 = enEmpresa;
         #583#1 = enEjer;
         #583#2 = enPeriodo;
         |LEE 040#583=;
         |SI FSalida ! 0;
             |DDEFECTO #583;
             #583#0 = enEmpresa;
             #583#1 = enEjer;
             #583#2 = enPeriodo;
             #583#3 = enModelo;
             |HAZ DefPeriodo;
             |GRABA 020#583b;
         |FINSI;

         enAny = enEjer;
         enPer = enPeriodo;
         |HAZ Actualizacion;

         |PINPA #0#583;
         |PINDA #0#583;
         |HAZ PintaDatos;

         |ENDATOS #2#583;

         |SI #583#6 ! 0; |O #583#7 ! 0; |O #583#20 ! 0;
             enEntrada = 1;
         |SINO;
             enEntrada = 0;
         |FINSI;

         |GRABA 020#583a;

         |LIBERA #583;
         |CIERRA #583;

         |ACABA;
     |FINSI;

|| \\\\\\ Consulta del Modelo ////////

     |SI enTipoEntrada = 4;              || Consulta del Modelo
         |CABEZA "Modelo 347";
         |ABRE #583;
         #583#0 = enEmpresa;
         #583#1 = enEjer;
         #583#2 = enPeriodo;
         |LEE 040#583=;
         |SI FSalida ! 0;
             |MENAV "      No existe Registro.";
             |CIERRA #583;
             |ACABA;
         |FINSI;

         |PINPA #0#583;
         |PINDA #0#583;
         |HAZ PintaDatos;

         |HAZ Menug;

         |CIERRA #583;

         |ACABA;
     |FINSI;

|| \\\\\\ Baja del Modelo ////////

     |SI enTipoEntrada = 3;              || Borrar del Modelo
         |ABRE #583;
         #583#0 = enEmpresa;
         #583#1 = enEjer;
         #583#2 = enPeriodo;
         |LEE 040#583=;
         |SI FSalida ! 0;
             |SI enMensa = 0;
                 |MENAV "      No existe Registro.";
             |FINSI;
             |CIERRA #583;
             |ACABA;
         |FINSI;

         nSwChequea = 1;
         nBorra = 1; |HAZ Recalculo;
         nBorra = 0;

         |PDEFECTO #583, 6,21;
         |GRABA 020#583a;
         |LIBERA #583;

         |CIERRA #583;

         |ACABA;
     |FINSI;

|| \\\\\\ Recalculo totales registro 1

     |SI enTipoEntrada = 92;
         |ABRE #583;
         #583#0 = enEmpresa;
         #583#1 = enEjer;
         #583#2 = enPeriodo;
         |LEE 040#583=;
         |SI FSalida = 0;

             enAny = enEjer;
             enPer = enPeriodo;
             |HAZ Actualizacion;

             |GRABA 020#583a;
         |FINSI;
         |LIBERA #583;
         |CIERRA #583;

         |ACABA;
     |FINSI;

|| \\\\\\ Comprobacion Existencia del Modelo

     |SI enTipoEntrada = 99;              || Existencia del Modelo ????
         |ABRE #583;
         #583#0 = enEmpresa;
         #583#1 = enEjer;
         #583#2 = enPeriodo;
         |LEE 040#583=;
         |SI FSalida ! 0;  enExistencia = 1;  |FINSI;
         |CIERRA #583;

     |FINSI;
|FPRO;
