|FICHEROS;
   dsmoy085 #0;

   :/basica/def/agim0002 #102;
   :/basica/def/agitab12 #112;
   :/basica/def/agicen14 #114;
   :/basica/def/agifigen #118;
   :/basica/def/agim0019 #119;

   dsmow030 #900;
   dsmow155 #999;

   dsmom088 #88;  ||Cuentas para Determinar Exc.Reg.Tributacion

   dsmom009 #09;  ||Ingresos Planillas
   dsmom010 #10;  ||Ingresos Planillas
   dsmom012 #12;  ||Consumo
   dsmom013 #13;  ||Gastos

   :/contagen/def/canempre #300;
   :/contagen/def/caivaasi #301
   :/contagen/def/camaniva #302;
   :/contagen/def/caivalin #303;
   :/contagen/def/cacuenta #304;
   :/contagen/def/cacuedep #305;

   :/contasoc/def/xxx00001 #203;     || Impuesto Sociedades 1990
   :/contasoc/def/xxx00011 #204;     || Impuesto Sociedades 1991
   :/contasoc/def/isocie01 #205;     || Impuesto Sociedades 1992
   :/contasoc/def/isocie32 #206;     || Impuesto Sociedades 1993
   :/contasoc/def/isocie44 #207;     || Impuesto Sociedades 1994 y 1995
   :/contasoc/def/isocie57 #209;     || Impuesto Sociedades 1996
   :/contasoc/def/isocie65 #219;     || Impuesto Sociedades 1997
   :/contasoc/def/isom0003 #220;     || Impuesto Sociedades  ] 2008

   dsm39001;
   dsm30301;
|FIN;

|VARIABLES;
   aFichero    = "";
   nInkey      = "";
   informe     = "";
   nCampo      = 0;
   nDEmpresa   = 0;
   nHEmpresa   = 0;
   fLaFecha    = @;

   aAlfa1 = "";
   aAlfa2 = "";
   aAlfa3 = "";
   nPara1 = 0;
   nPara2 = 0;
   nNume1 = 0;
   nNume2 = 0;
   nNume3 = 0;
   nNume4 = 0;

   nCamp1 = 0;
   nCamp2 = 0;
   nCamp3 = 0;
   nCamp4 = 0;
   nCamp5 = 0;
   nCamp6 = 0;
   nCamp7 = 0;
   nCamp8 = 0;
   nCamp9 = 0;

   nSPin1 = 0;
   &nSwLinea = 0;
   &nSwPrint = 0;
   nSwHay    = 0;
   nPrint    = 0;
   aPath     = "";
   aPathFic  = "";

   &eaPathContasoc = "";
   aAlfa      = "";
   aBase      = "";
   nCanal     = 0;

   SwSocie    = 0;
   nSociedad  = 0;
   nSwOpera   = 0;
   nPorCtas   = 0;
   nSwRendi   = 0;
   nSwConta   = 0;
   nSwPlani   = 0;
   nNumAct    = 0;
   nTotAct    = 0;

   nEmpresa   = 0;
   nModelo    = 0;
   nActividad = 0;
   aActividad = "";
   aDCta12    = "6000000000000";
   aHCta12    = "7099999999999";
   aDCta4C    = "6000";
   aHCta4C    = "6099";
   aDCta4V    = "7000";
   aHCta4V    = "7099";
   aCuenta    = "";
   nSaldo     = 0;

   &eSwSelec  = 0;
   &eaNomFDep = "";  ||Nombre del Fichero Creado
   &dirfich0  = "";
   &eaDDepar  = "";
   &eaHDepar  = "";

   nMiro      = 0;
   aReten     = "";
   nImporte   = 0;
   nImport1   = 0;
   nCompras   = 0;
   nElDepar   = 0;
   nDActividad = 0;
   nHActividad = 0;

   nSw88   = 0;
   aTipo   = "";
   aSigno  = "";
   nExis88 = 0;
   nPor    = 0;
   nDias   = 0;
   nDiasA  = 0;
   nDiasA1 = 0;
   nDiasA2 = 0;

   nEjercicio = 0;
   nEjercici1 = 0;
   nEjercici2 = 0;
   nEjer      = 0;
   nEjer1     = 0;
   nEjer2     = 0;
   nSwAntes   = 0;
|FIN;

|INCLUYE i_variar;
|INCLUYE i_varcon;
|INCLUYE i_varlib;

|| //////////////////// Calculos de Pantalla \\\\\\\\\\\\\\\\\\\\\\\\\\
|CALCULO Eano; |TIPO 7;
   fLaFecha = ~;
   aAlfa1 = fLaFecha; aAlfa1 = aAlfa1 % -104;
|FCAL;

|CALCULO ElAny;
   aAlfa = Contenido;
   |SI aAlfa ! #0#1;
       nNume1 = aAlfa;
       nNume2 = #0#1;
       |SI nNume1 [ 2014; |Y nNume2 ] 2015;
           |HAZ Eldefecto;
       |FINSI;
       |SI nNume2 [ 2014; |Y nNume1 ] 2015;
           |HAZ Eldefecto;
       |FINSI;
       |SI nNume2 ] 2015; |Y nNume1 ] 2015;
           |SI nNume1 ! nNume2;
               |HAZ Eldefecto;
           |FINSI;
       |FINSI;
   |FINSI;
|FCAL;

|CALCULO AntesOrdenar; |TIPO 7;
 |SI #0#2 = 0;
     |C_M #0Campo, 1, "N";
     #0Campo = 1; |PINTA #0Campo;
 |SINO;
     |C_M #0Campo, 1, "S";
 |FINSI;
|FCAL;

|CALCULO Tipo7;  |TIPO 7;
     |SI enEmpresa ! 0;
         #0#2  = enEmpresa;  |PINTA #0#2;
         #0#3  = enEmpresa;  |PINTA #0#3;

         |C_M #0#2, 1, "N";
         |C_M #0#3, 1, "N";
     |FINSI;
|FCAL;

|CALCULO Parrilla; |TIPO 0;
     |SI #0#2 = 0;
         #0#2 = 0;  |PINTA #0#2;
         #0#3 = 0;  |PINTA #0#3;  |C_M #0#3, 1, "N";
         |PARA nCampo = 4;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |SINO;
         |SI enEmpresa = 0;
             |C_M #0#2, 1, "S";
             |C_M #0#3, 1, "S";
         |FINSI;

         |PARA nCampo = 4;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "N";
               #0nCampo = 0;  |PINTA #0nCampo;
         |SIGUE;
     |FINSI;
|FCAL;

|CALCULO NoMasEmpresas; |TIPO 0;
     |SI #0Campo = 0;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
               #0nCampo = 0;  |C_M #0nCampo, 1, "N";
         |SIGUE;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |FINSI;
|FCAL;

|CALCULO YaSeCual; |TIPO 7;
 |SI enEmpresa ! 0;
     #0#41 = "S"; |PINTA #0#41;
     #0#24 = "S"; |PINTA #0#24;
     #0#25 = 00;  |PINTA #0#25;
     #0#26 = 99;  |PINTA #0#26;
     |C_M #0#24, 1, "N";
     |C_M #0#25, 1, "N";
     |C_M #0#26, 1, "N";
     |C_M #0#41, 1, "N";
 |FINSI;
|FCAL;

|CALCULO SelTipo0; |TIPO 0;
 |SI #0#41 = "N";
     #0#25 = 0;  |PINTA #0#25;  |C_M #0#25, 1, "N";
     #0#26 = 99; |PINTA #0#26;  |C_M #0#26, 1, "N";
     |PARA nCampo = 27;  |SI nCampo [ 36;  |HACIENDO nCampo = nCampo + 1;
           |C_M #0nCampo, 1, "S";
     |SIGUE;
 |SINO;
     |SI enEmpresa = 0;
         |C_M #0#25, 1, "S";
         |C_M #0#26, 1, "S";
     |FINSI;
     |PARA nCampo = 27;  |SI nCampo [ 36;  |HACIENDO nCampo = nCampo + 1;
           |C_M #0nCampo, 1, "N";
           #0nCampo = 0;  |PINTA #0nCampo;
     |SIGUE;
 |FINSI;
|FCAL;

|CALCULO SelTipo1;
 |SI #0Campo = 0;
     |PARA nCampo = Campo + 1;  |SI nCampo [ 36;  |HACIENDO nCampo = nCampo + 1;
           #0nCampo = 0;  |C_M #0nCampo, 1, "N";
     |SIGUE;
 |SINO;
     |PARA nCampo = Campo + 1;  |SI nCampo [ 36;  |HACIENDO nCampo = nCampo + 1;
           |C_M #0nCampo, 1, "S";
     |SIGUE;
 |FINSI;
|FCAL;

|CALCULO Eldefecto0;
   |SI FSalida = 13;
       |HAZ Eldefecto;
   |FINSI;
|FCAL;

|PROCESO Eldefecto;

  aAlfa1 = #0#45;
  aAlfa2 = #0#52;

  |PDEFECTO #0, 44, 54;
  #0#60 = 0;
  |SI #0#1 > 2014;
      #0#46 = 150000;
      #0#47 = 150000;
      #0#48 = 250000;
      #0#49 = 0;
      #0#50 = 0;
      #0#51 = 0;
      #0#60 = 75000;
  |FINSI;
  |SI #0#1 = 2015; |O #0#1 = 2016;
      #0#46 = 250000;
      #0#47 = 250000;
      #0#48 = 250000;
      #0#49 = 0;
      #0#50 = 0;
      #0#51 = 0;
      #0#60 = 125000;
  |FINSI;
  |SI #0#1 = 2017; |O #0#1 = 2018;
      #0#46 = 250000;
      #0#47 = 250000;
  |FINSI;
  #0#52 = aAlfa2;
  #0#45 = aAlfa1;

  |PARA nCampo = 44; |SI nCampo [ 53; |HACIENDO nCampo = nCampo + 1;
        |PINTA #0nCampo;
  |SIGUE;
  |PINTA #0#60;
|FPRC;

|CALCULO Modificables;
  aAlfa = #0Campo;
  |SI Campo = 43; |C_M #0#44, 1, aAlfa; |FINSI;
  |SI Campo = 45;
      |C_M #0#46, 1, aAlfa;
      |C_M #0#47, 1, aAlfa;
      |C_M #0#48, 1, aAlfa;
      |C_M #0#49, 1, aAlfa;
      |C_M #0#50, 1, aAlfa;
      |C_M #0#51, 1, aAlfa;
      |C_M #0#60, 1, aAlfa;
      |SI #0#1 [ 2014;
          |C_M #0#60, 1, "N";
      |FINSI;
  |FINSI;
  |SI Campo = 52;
      |C_M #0#53, 1, aAlfa;
      |SI eaPathContasoc ! ""; |C_M #0#54, 1, aAlfa; |FINSI;
  |FINSI;
  |SI #0#1 > 2014;
      |C_M #0#49, 1, "N";
      |C_M #0#50, 1, "N";
      |C_M #0#51, 1, "N";
  |FINSI;
|FCAL;

|CALCULO Tipo0C62; |TIPO 0;
     aAlfa = "S";
     |SI #0#62 = "M";
         aAlfa = "N";
         #0#39 = "N";
         #0#54 = "N"; |PINTA #0#54;
     |FINSI;
|FCAL;
|| *************************************************************************
|| *************************************************************************
|PROCESO ActAux999;
  #999#19 = nImporte;
  |GRABA 020#999a;
  |LIBERA #999;
|FPRC;

|DEFBUCLE ActAux999;
  #999#1;
  ;
  enEmpresa, 01;
  enEmpresa, 99;
  ;
  NULL, ActAux999;
|FIN;

|PROCESO GrabaAuxiliar;

  |SI nSwAntes = 0;
      #999#0 = enEmpresa;
      #999#3 = nActividad;
      |LEE 041#999=;
      |SI FSalida ! 0;
          |DDEFECTO #999;
          #999#0 = enEmpresa;
          #999#1 = #agifigen#1;
          #999#2 = #agifigen#13;
          #999#3 = nActividad;
          #999#4 = #114#2;
          #999#5 = #114#3;
          #999#6 = #114#4;
          #999#7 = #114#9;
          #999#8 = #114#10;
          #999#18 = #114#5;

          |GRABA 020#999b;
      |FINSI;

      |SI nSwPrint = 1; |O nSwPrint = 4;
          nCompras = 0;
      |FINSI;

      aAlfa = #999#18; aAlfa = aAlfa % -104;
      nEjer = aAlfa;
      |SI nEjer = enEjer;
          nDias = (efHFecha - #999#18) + 1;
          nPor  = (nDiasA / nDias);

          nCompras = (nCompras * nPor) ! 2;
          nImporte = (nImporte * nPor) ! 2;
          nImport1 = (nImport1 * nPor) ! 2;
      |FINSI;

      #999#9  = #999#9  + nCompras;
      #999#10 = #999#10 + nImporte;
      #999#11 = #999#11 + nImport1;
      #999#12 = nSwPrint;
      #999#13 = aReten;

      |SI #999#12 = 1;
          #999#14 = 0;
          #999#15 = #0#44 - #999#10;
          #999#16 = 0;
      |FINSI;

      |SI #999#12 = 2; |O #999#12 = 3;
          #999#14 = #0#46 - #999#9;

          |SI #999#12 = 2;
              #999#15 = #0#47 - #999#10;
              |SI #999#13 ! "S";
                  #999#16 = 0;
              |SINO;
                  aAlfa1 = #999#5; |QBF aAlfa1;
                  |SI aAlfa1 = "722"; |O aAlfa1 = "757";
                      #999#16 = #0#51 - #999#11;
                  |SINO;
                      nNume1 = ((#999#11 * 100) / #999#10) ! 2;
                      |SI nNume1 > 50;
                          #999#16 = #0#50 - #999#11;
                      |SINO;
                          #999#16 = #0#49 - #999#11;
                      |FINSI;
                  |FINSI;
              |FINSI;
          |FINSI;

          |SI #999#12 = 3;
              #999#15 = #0#48 - #999#10;
              #999#16 = 0;
          |FINSI;

          |SI #0#1 > 2014;   ||no hay control retenciones a partir del 2015
              #999#16 = 0;

              #999#17 = #0#60 - #999#10; || Ventas Empresarios
          |SINO;
              #999#17 = 0;
          |FINSI;
      |FINSI;

      |SI #999#12 = 4;
          #999#14 = 0;
          #999#15 = #0#53 - (#999#10 + #999#11);
          #999#16 = 0;
      |FINSI;

      |GRABA 020#999a;
      |LIBERA #999;
      nPrint = 1;
  |FINSI;
  |SI nSwAntes = 1; |Y nImporte ! 0;
      |CIERRA #999;
      |BUCLE ActAux999;
      |ABRE #999;
  |FINSI;
|FPRC;

|| *************************************************************************
|| *************************************************************************
|PROCESO LeoM30301;
   nSwHay  = 1;
   nImporte = #dsm30301#129;
|FPRC;

|DEFBUCLE LeoM30301;
   #dsm30301#1;
   ;
   enEmpresa, enEjer, 12, nModelo, 00, 00;
   enEmpresa, enEjer, 12, nModelo, 99, 99;
   e;
   NULL, LeoM30301;
|FIN;

|PROCESO LeoM39001;
   nSwHay  = 1;
   nImporte = #dsm39001#12;
|FPRC;

|DEFBUCLE LeoM39001;
   #dsm39001#1;
   ;
   enEmpresa, enEjer, nModelo, 00;
   enEmpresa, enEjer, nModelo, 99;
   e;
   NULL, LeoM39001;
|FIN;

|PROCESO PorModelo390;

  nImporte = 0;   || Total Ingresos
  nImport1 = 0;   || Total Consumo
  nCompras = 0;   || Total Gastos
  nModelo = 390; |BUCLE LeoM39001;
  |SI nSwHay = 0;
      nModelo = 303; |BUCLE LeoM30301;
  |FINSI;
  |SI nSwHay = 1;
      nSwPrint = 0;
      |SI #0#56 ! 0;
          nSwPrint = 1; nActividad = #0#56;
      |SINO;
          |SI #0#57 ! 0;
              nSwPrint = 2; nActividad = #0#57;
          |SINO;
              |SI #0#58 ! 0;
                  nSwPrint = 3; nActividad = #0#58;
              |SINO;
                  |SI #0#59 ! 0;
                      nSwPrint = 4; nActividad = #0#59;
                  |FINSI;
              |FINSI;
          |FINSI;
      |FINSI;
      |HAZ LeeActiv;
      |HAZ GrabaAuxiliar;
  |FINSI;
|FPRC;
|| *************************************************************************
|PROCESO PreparaConta;

   nSwConta  = 0; ||compruebo si tiene contabilidad

   |ABRE #canempre;
   |SELECT #4#canempre;
   |DDEFECTO #canempre;
   #canempre#2  = enEmpresa;
   #canempre#26 = nEjercicio;
   |LEE 001#canempre.=;
   |SI FSalida = 0;
       enPerConta = #canempre#3;   || Periodo Contable
       aAlfa1 = enEmpresa;  aAlfa1 = ("00000" + aAlfa1) % -105;
       aAlfa2 = enPerConta; aAlfa2 = ("0" + aAlfa2) % -101;
       aPathConta  = aPath + "contagen/fich/" + aAlfa1 + aAlfa2 + "/";
       nSwConta = 1;
   |FINSI;
   |SELECT #1#canempre;
   |CIERRA #canempre;

   |SI nSwConta = 0; |ACABA; |FINSI;    || no tiene contabilidad

   eaLaMonEmp = #canempre#28;
   dig1       = #canempre#11;   || Desde Mes
   dig3       = #canempre#13;   || N§ Digitos
   dig4       = #canempre#14;   || Digitos Nivel 1
   dig5       = #canempre#15;   || Digitos Nivel 2
   dig6       = #canempre#16;   || Digitos Nivel 3
   dig7       = #canempre#17;   || Digitos Nivel 4
   dig8       = #canempre#18;   || Digitos Nivel 5
   dig9       = #canempre#19;   || Digitos Nivel 6
   eaEstadoEmp = "S"; |SI #canempre#21 = "I"; eaEstadoEmp = "N"; |FINSI;
   eaNombreEmp = #canempre#1; || Nombre Empresa

   || Recoger informacion sobre los niveles ...
   |SI dig3 = 1; MaximoDigitos = dig4; |FINSI;
   |SI dig3 = 2; MaximoDigitos = dig5; |FINSI;
   |SI dig3 = 3; MaximoDigitos = dig6; |FINSI;
   |SI dig3 = 4; MaximoDigitos = dig7; |FINSI;
   |SI dig3 = 5; MaximoDigitos = dig8; |FINSI;
   |SI dig3 = 6; MaximoDigitos = dig9; |FINSI;

   |PATH_DAT #301 aPathConta;
   |PATH_DAT #302 aPathConta;
   |PATH_DAT #303 aPathConta;
   |PATH_DAT #304 aPathConta;
   |ABRE #caivaasi;
   |DDEFECTO #caivaasi;
   |LEE 000#caivaasi.p;
   |CIERRA #caivaasi;
   aMulDep = #caivaasi#132;

   nPorCtas = 0;
   |SI nNumAct  = 1; nPorCtas = 1; |FINSI;  ||solo una actividad
   |SI nSwAntes = 1; nPorCtas = 1; |FINSI;  || Acumulado Ejercicio Anterior

   |SI #0#55 = 0; |Y #0#56 = 0; |Y #0#57 = 0; |Y #0#58 ! 0;
       |SI #0#54 = "S"; nPorCtas = 1; |FINSI;   ||sociedades por el impuesto
   |FINSI;

   |SI nPorCtas = 0;             || ver por actividades
       dirfich0 = aPathConta;
       eSwSelec  = 2;
       |ATRI I;
       |PINTA 2302, "Procesando Cuentas por Departamentos ... Espere ";
       |ATRI 0;
       |SUB_EJECUTA_NP ":contagen/cacuedep";
       |PINTA 2302, "                                                ";
       |NOME_DAT #305 eaNomFDep;
       |PATH_DAT #305 aPathConta;
   |FINSI;
|FPRC;

|| *************************************************************************
|PROCESO SumoCtas;
  |SI aTipo = "C"; |O aTipo = "G";
      nCompras = nCompras + nSaldo;
      nSwHay = 1;
  |FINSI;
  |SI aTipo = "V";
      nImporte = nImporte + nSaldo;
      nSwHay = 1;
  |FINSI;
|FPRC;
|| *************************************************************************

|| ... Datos Planillas
|PROCESO SumaSP13;
  nSwPlani = 1;
  aCuenta = #13#5;
  nSaldo  = #13#8;
  |HAZ SumoCtas;
|FPRC;

|DEFBUCLE dsmom013;
 #13#1;
 ;
 enEmpresa, enEjer, 01, 00, nDActividad, aDCta4C;
 enEmpresa, enEjer, 99, 99, nHActividad, aHCta4C;
 e;
 NULL, SumaSP13;
|FIN;

|PROCESO SumaSP12;
 nSwPlani = 1;
 aCuenta = #12#5;
 nSaldo  = #12#8;
 |HAZ SumoCtas;
|FPRC;

|DEFBUCLE dsmom012;
 #12#1;
 ;
 enEmpresa, enEjer, 01, 00, nDActividad, aDCta4C;
 enEmpresa, enEjer, 99, 99, nHActividad, aHCta4C;
 e;
 NULL, SumaSP12;
|FIN;

|PROCESO SumaSP10;
 nSwPlani = 1;
 aCuenta = #10#5;
 nSaldo  = #10#8;
 |HAZ SumoCtas;
|FPRC;

|DEFBUCLE dsmom010;
 #10#1;
 ;
 enEmpresa, enEjer, 01, 00, nDActividad, aDCta4V;
 enEmpresa, enEjer, 99, 99, nHActividad, aHCta4V;
 e;
 NULL, SumaSP10;
|FIN;
|| ///////////////////////////////////////////////////////////
|| ... Por Facturas para sacar las Retenciones
|PROCESO  BuscaEnM088;
  nExis88 = 0;
  #88#0 = aCuenta;
  #88#3 = enPlan;
  |LEE 041#88=;
  |SI FSalida = 0;
      |SI #88#4 = "V";
          nExis88 = 1;
      |FINSI;
  |FINSI;
|FPRC;

|PROCESO LeeLineasIVA;
  aAlfa1 = #303#30; |QBF aAlfa1; |SI aAlfa1 = ""; #303#30 = #302#10; |FINSI;
  |SI aMulDep = "S";
      nElDepar = #303#30;
      |SI nElDepar = 0; nElDepar = 1; |FINSI;
      |SI nElDepar < nDActividad; |O nElDepar > nHActividad; |ACABA; |FINSI;
  |FINSI;

  aCuenta = #303#12 % 104;
  |HAZ BuscaEnM088;
  |SI nExis88 = 1;
      nImporte = nImporte + #303#6;
      |SI #303#22 ! 0;
          aReten   = "S";
          nImport1 = nImport1 + #303#6;
      |FINSI;
      nSwHay = 1;
  |FINSI;
|FPRC;

|DEFBUCLE LeeLineasIVA;
   #303#1;
   ;
   #302#0, #302#1, 001;
   #302#0, #302#1, 999;
   e;
   NULL, LeeLineasIVA;
|FIN;

|PROCESO PorFacturas;

   |SI aMulDep ! "S";
       nElDepar = #302#10;
       |SI nElDepar = 0;  nElDepar = 1; |FINSI;
       |SI nElDepar < nDActividad; |O nElDepar > nHActividad; |ACABA; |FINSI;
   |FINSI;

   |BUCLE LeeLineasIVA;
|FPRC;

|DEFBUCLE PorFacturas;
  #302#1;
  36;
  01, 0000000001, "R";
  99, 9999999999, "R";
  ;
  NULL, PorFacturas;
|FIN;
|| ------------------------------------------------

|| ........ Por actividades
|PROCESO LeeCuentas;
  aCuenta = #305#0 % 104;
  nSaldo  = #305#53 - #305#50;
  |SI aSigno = "-";  nSaldo = -nSaldo; |FINSI;
  |HAZ SumoCtas;
|FPRC;

|DEFBUCLE LeeCuentas;
  #305#2;
  3;
  aActividad, aDCta12, "S";
  aActividad, aHCta12, "S";
  ;
  NULL, LeeCuentas;
|FIN;

|| ... Todas
|PROCESO PorPlanCtble;
  nSwHay = 1;
  aCuenta = #304#0 % 104;
  nSaldo  = #304#53 - #304#50;
  |SI aSigno = "-";  nSaldo = -nSaldo; |FINSI;
  |HAZ SumoCtas;
|FPRC;

|DEFBUCLE PorPlanCtble;
  #304#1;
  3;
  aDCta12, "S";
  aHCta12, "S";
  ;
  NULL, PorPlanCtble;
|FIN;

|| ......... Proceso nuevo para lee las cuentas del Fichero de Control
|PROCESO Pordsmom088;
  aTipo  = #88#4;
  aSigno = #88#2;

  aAlfa  = #88#0;
  aDCta12 = (aAlfa + "000000000000") % 112;
  aHCta12 = (aAlfa + "999999999999") % 112;

  |SI aTipo = "C";
      aDCta4C = (aAlfa + "0000") % 104;
      aHCta4C = (aAlfa + "9999") % 104;
      aDCta4V = "    ";
      aHCta4V = "    ";
  |FINSI;
  |SI aTipo = "V";
      aDCta4V = (aAlfa + "0000") % 104;
      aHCta4V = (aAlfa + "9999") % 104;
      aDCta4C = "    ";
      aHCta4C = "    ";
  |FINSI;

  |SI nSw88 = 0;  |BUCLE PorPlanCtble;  |FINSI;
  |SI nSw88 = 1;  |BUCLE LeeCuentas;    |FINSI;

  |SI nSw88 = 2;
      |SI aTipo = "V";  |BUCLE dsmom010; |FINSI;
      |SI aTipo = "C"; |O aTipo = "G";
          |BUCLE dsmom012;
          |BUCLE dsmom013;
      |FINSI;
  |FINSI;
|FPRC;

|DEFBUCLE Pordsmom088;
  #88#1;
  ;
  enPlan, "    ";
  enPlan, "zzzz";
  ;
  NULL, Pordsmom088;
|FIN;
|| -------------------------------------------------------------

|PROCESO PorPlanCtble0;  || solo una actividad
  nSwPrint = 0;
  |SI #0#56 ! 0;
      nSwPrint = 1; nActividad = #0#56;
  |SINO;
      |SI #0#57 ! 0;
          nSwPrint = 2; nActividad = #0#57;
      |SINO;
          |SI #0#58 ! 0;
              nSwPrint = 3; nActividad = #0#58;
          |SINO;
              |SI #0#59 ! 0;
                  nSwPrint = 4; nActividad = #0#59;
              |FINSI;
          |FINSI;
      |FINSI;
  |FINSI;

  |SI nSwPrint = 0; |ACABA; |FINSI;

  nDActividad =  1;
  nHActividad = 99;

  nSwHay   = 0;
  aReten   = "N";
  nImporte = 0;
  nImport1 = 0;
  nCompras = 0;

  nSw88    = 0; |BUCLE Pordsmom088;    || ---BUCLE PorPlanCtble;

  |SI nSwPrint = 2;
      nImporte = 0;
      nImport1 = 0;
      |ABRE #88;
      |BUCLE PorFacturas;
      |CIERRA #88;
  |FINSI;

  |SI nSwHay = 1;
      |HAZ LeeActiv;
      |HAZ GrabaAuxiliar;
  |FINSI;
|FPRC;

|| *************************************************************************
|PROCESO Planilla;
 |SI nSwAntes = 0;
     aAlfa1 = #114#15; |QBF aAlfa1;
     |SI aAlfa1 = "MANUAL"; aAlfa1 = "PLANILLAS"; |FINSI;
     |SI aAlfa1 ! "PLANILLAS";  |ACABA; |FINSI;
 |FINSI;

 nSw88    = 2; |BUCLE Pordsmom088;  || ---BUCLE dsmom010, dsmom012 y dsmom013
|FPRC;

|| *************************************************************************
|| .... Leo el Impuesto de Sociedades

|PROCESO LeoImpuesto;
  || Lee Ficha Sociedades, lee la ficha
     swExiste = 0;
     nRegis   = 0;
     |ABRE #agim0019;
     #agim0019#0 = enEmpresa;
     #agim0019#1 = 99;
     |LEE 041#agim0019.];
     |SI FSalida = 0;
         |LEE 041#agim0019.a;
     |SINO;
         |LEE 041#agim0019.u;
     |FINSI;
     |SI FSalida = 0; |Y #agim0019#0 = enEmpresa;
         swExiste = 1;
     |FINSI;
     |SI swExiste = 1;
         nRegis = #agim0019#1;  ||Registre
         |SI #agim0019#12 > "01.01.1000";
             aAlfa1 = #agim0019#12; aAlfa1 = aAlfa1 % -104; enEjerBajaM = aAlfa1;
             |SI enEjerBajaM [ #0#1; nRegis = 0; |FINSI;
         |FINSI;
     |FINSI;
     |CIERRA #agim0019;

     aReten   = "N";
     nImporte = 0;
     nImport1 = 0;
     nCompras = 0;
     |SI nRegis ! 0; |Y eaPathContasoc ! "";
         |SI enEjer [ 1996;
             |ABRE #209;
             #209#0  = enEmpresa;
             #209#1  = enEjer;
             |LEE 040#209=;
             |SI FSalida = 0;
                 nImporte = #209#180;
                 SwSocie  = 1;
             |FINSI;
             |CIERRA #209;
         |SINO;
             |SI enEjer [ 2007;
                 |ABRE #219;
                 |DDEFECTO #219;
                 #219#0  = enEmpresa;
                 #219#1  = enEjer;
                 |LEE 040#219=;
                 |SI FSalida = 0;
                     nImporte = #219#166;
                     SwSocie  = 1;
                 |FINSI;
                 |CIERRA #219;
             |SINO;
                 aPathFic = eaPathContasoc + "fich/";
                 |PATH_DAT #220 aPathFic;
                 |ABRE #220;
                 |DDEFECTO #220;
                 |SELECT #2#220;
                 #220#0  = enEmpresa;
                 #220#1  = enEjer;
                 #220#3  = "255";
                 |LEE 040#220=;
                 |SI FSalida = 0;
                     nImporte = #220#5;
                     SwSocie  = 1;
                 |FINSI;
                 |CIERRA #220;
             |FINSI;
         |FINSI;
     |FINSI;

     |SI SwSocie = 1;
         nActividad = #0#59;
         |HAZ LeeActiv;
         nSwPrint   = 4;
         |HAZ GrabaAuxiliar;
     |FINSI;
|FPRC;

|| *************************************************************************
|PROCESO LeeActiv;
  aActividad = nActividad;
  aActividad = ("00" + aActividad) % -102;

  |ABRE #114;
  |DDEFECTO #114;
  #114#0 = enEmpresa;
  #114#1 = nActividad;
  |LEE 041#114=;
  |CIERRA #114;
|FPRC;

|PROCESO agim0002;
     nSwRendi = 1;
     nSwHay   = 1;
     nCompras = nCompras + (#102#15 + #102#16)
     nImporte = nImporte + #102#13;
|FPRC;

|DEFBUCLE agim0002;
 #102#1;
 ;
 #114#0, nDActividad, enEjer;
 #114#0, nHActividad, enEjer;
 ;
 NULL, agim0002;
|FIN;
|PROCESO PorActividad_Ant;

  nCompras = 0;   || Total Compras
  nImporte = 0;   || Total Ingresos
  nImport1 = 0;   || Total Ingresos con retencion
  nSwHay   = 0;

  nDActividad =  1;
  nHActividad = 99;
  nSwPlani = 0;
  |HAZ Planilla;
  |SI nSwPlani = 0;
      nSwRendi = 0;
      |BUCLE agim0002;
  |FINSI;

  |SI nSwHay = 1;
      |HAZ GrabaAuxiliar;
  |FINSI;
|FPRC;

|PROCESO PorActividad;

 |SI #114#5 > "01.01.0000";
     |SI #114#5 > efHFecha;  |ACABA;  |FINSI;
 |FINSI;
 |SI #114#6 > "01.01.0000";
     |SI #114#6 < efDFecha;  |ACABA; |FINSI; || Baja
 |FINSI;

  nActividad = #114#1;
  aActividad = nActividad;
  aActividad = ("00" + aActividad) % -102;

  nSwPrint = 0;
  |SI #0#43 = "S";
      |SI #114#9 = 4; |O #114#9 = 9;
          nSwPrint = 1;
          #0#56 = #114#1;
      |FINSI;
  |FINSI;

  |SI #0#45 = "S";
      |SI #114#9 = 5; |O #114#9 = 10;
          nSwPrint = 2;
          #0#57 = #114#1;
      |FINSI;
      |SI #114#9 = 6; |O #114#9 = 7;
         |O #114#9 = 11; |O #114#9 = 12;
          nSwPrint = 3;
          #0#58 = #114#1;
      |FINSI;
  |FINSI;

  |SI #0#52 = "S";
      |SI #114#9 = 2;
          nSwPrint = 4;
          #0#59 = #114#1;
      |FINSI;
  |FINSI;

  nNumAct = nNumAct + 1;
  |SI nSwPrint = 0; |ACABA; |FINSI;  ||Esta actividad no esta en la seleccion

  nTotAct = nTotAct + 1;
  |SI nSwOpera = 0;
      |SI #0#55 = 0;
          #0#55 = #114#1;
      |FINSI;
      |SI nSwPrint = 4;
          nSociedad = nSociedad + 1;
      |FINSI;
      nSwHay = 1;
      |ACABA;
  |FINSI;

  aReten   = "N";
  nCompras = 0;   || Total Compras
  nImporte = 0;   || Total Ingresos
  nImport1 = 0;   || Total Ingresos con retencion
  nSwHay   = 0;

  nDActividad = nActividad;
  nHActividad = nActividad;
  |SI nSwConta = 1;
      nSw88 = 1; |BUCLE Pordsmom088;  || --- BUCLE LeeCuentas;
      |SI nSwPrint = 2;
          nImporte = 0;
          nImport1 = 0;
          |ABRE #88;
          |BUCLE PorFacturas;
          |CIERRA #88;
      |FINSI;
  |SINO;
       nSwPlani = 0;
       |HAZ Planilla;
       |SI nSwPlani = 0;
           nSwRendi = 0;
           |ABRE #102;
           |DDEFECTO #102;
           #102#0 = #114#0;
           #102#1 = nActividad;
           #102#2 = enEjer;
           |LEE 001#102=;
           |SI FSalida = 0;
               nSwRendi = 1;
               nSwHay   = 1;
               nCompras = (#102#15 + #102#16)
               nImporte = #102#13;
           |FINSI;
           |CIERRA #102;
       |FINSI;
  |FINSI;

  |SI nSwHay = 1;
      |HAZ LeeActiv;
      |HAZ GrabaAuxiliar;
  |FINSI;
|FPRC;

|DEFBUCLE PorActividad;
  #114#1;
  ;
  enEmpresa, 00;
  enEmpresa, 99;
  e;
  NULL, PorActividad;
|FIN;


|PROCESO LEmpresa;
 enEmpresa  = #agifigen#0;

 |DDEFECTO #900;
 #900#0 = enEmpresa;
 #900#2 = #agifigen#1;
 #900#11 = "ACTIVA";
 |SI #agifigen#94 > "01.01.0000";
     #900#11 = "INACTIVA";
     |SI #0#24 = "N"; |ACABA; |FINSI;
 |FINSI;

|| ... Seleccion de Tipo de Empresa
  nSPin1 = #agifigen#87;
  enSwSelEmp = 1;
  |SI #0#41 = "S";
      |SI nSPin1 ] #0#25; |Y nSPin1 [ #0#26;
          enSwSelEmp = 0;
      |FINSI;
  |SINO;
      |SI nSPin1 = #0#27;
          enSwSelEmp = 0;
      |SINO;
          |PARA nCampo = 28;  |SI nCampo [ 36;  |HACIENDO nCampo = nCampo + 1;
                |SI nSPin1 = #0nCampo; |Y nSPin1 ! 0;
                    enSwSelEmp = 0;
                    |SAL;
                |FINSI;
          |SIGUE;
      |FINSI;
  |FINSI;
  |SI enSwSelEmp = 1; |ACABA; |FINSI;
|| ------------------------------------------------------------------------
|| ------------------------------------------------------------------------
   |PDEFECTO #0, 55,60;
   SwSocie   = 0;
   nSociedad = 0;
   nSwConta  = 0;
   nSwPlani  = 0;
   nSwRendi  = 0;
   nPorCtas  = 0;

   nTotAct  = 0;
   nNumAct  = 0;
   nSwHay   = 0;
   nSwOpera = 0;            ||Comprobar las actividades de la Empresa

   |BUCLE PorActividad;

   |SI nSwHay = 1;

      |PARA nSwAntes = 0; |SI nSwAntes [ 1; |HACIENDO nSwAntes = nSwAntes + 1;

           enEjer = nEjer1;
           nDiasA = nDiasA1;
           nEjercicio = nEjercici1;
           |SI nSwAntes = 1;
               enEjer = nEjer2;
               nDiasA = nDiasA2;
               nEjercicio = nEjercici2;
               nEjer  = nEjer2;
           |FINSI;

           |SI #0#62 = "C";
               |HAZ PreparaConta;   ||Fichero departamentos
           |FINSI;
           nNumAct  = 0;
           nTotAct  = 0;
           nSwOpera = 1;

           SwSocie = 0;
           |SI #0#54 = "S"; |Y nSociedad ! 0;     ||Para sociedades
               |HAZ LeoImpuesto;
           |FINSI;

           |SI SwSocie = 0;
               |SI #0#62 = "C";
                   |SI nSwConta = 1; |Y nPorCtas = 1;
                       |HAZ PorPlanCtble0;
                   |SINO;
                       |SI nSwAntes = 0;
                           |BUCLE PorActividad;
                       |SINO;

                       |FINSI;
                   |FINSI;
               |SINO;
                   |HAZ PorModelo390;
               |FINSI;
           |FINSI;

        |SIGUE;
   |FINSI;
|FPRC;

|DEFBUCLE agifigen;
  #agifigen#1;
  ;
  nDEmpresa;
  nHEmpresa;
  e;
  NULL, LEmpresa;
|FIN;
|| \\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\
|| \\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\
|PROCESO Emite;

   |SI nPrint = 0;
       nPrint = 1;
       nEmpresa = #999#0;
   |FINSI;

   |SI nEmpresa ! #999#0;
       |SI nSwLinea = 1;
           nSwLinea = 2;
       ||    |PRINT;
       |FINSI;
       nSwLinea = 0;
       nEmpresa = #999#0;
   |FINSI;

   nSwPrint = #999#12;

   |PRINT;
   nSwLinea = 1;
|FPRC;

|DEFBUCLE LeeAuxiliarC;
    #999#1;
    ;
    00001, 01;
    99999, 99;
    ;
    NULL, Emite;
|FIN;

|DEFBUCLE LeeAuxiliarN;
    #999#2;
    ;
    "                                        ", 00001, 01;
    "zzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzz", 99999, 99;
    ;
    NULL, Emite;
|FIN;

|PROCESO Imprimir;
    Impresora = "CrystalReport";
    |IMPRE -1;
    |SI FSalida ! 0;
        |MENAV "    Error No existe impresora Crystal Report";
        |ACABA;
    |FINSI;

    informe = "dsmoya85"; || antes dsmoy085
    |INFOR informe;
    |SI FSalida ! 0; |ACABA; |FINSI;

    nPrint   = 0;
    nSwLinea = 0;
    nEmpresa = 0;
    |SI #0#37 = 1; |BUCLE LeeAuxiliarC; |FINSI;
    |SI #0#37 = 2; |BUCLE LeeAuxiliarN; |FINSI;
    |SI nPrint = 1;  |PIEINF; |FINSI;
    |FININF;
    |FINIMP;
|FPRC;

|CALCULO Tipo3;  |TIPO 3;
    eaInac = #0#24;

    |DBASS aPath; |QBT aPath;

    enEjer = #0#1;
    nEjer1 = enEjer;
    nEjer2 = enEjer - 1;

    aAlfa2 = nEjer1;
    aAlfa1 = aAlfa2 % -102; nEjercici1 = aAlfa1;
    aAlfa2 = nEjer2;
    aAlfa1 = aAlfa2 % -102; nEjercici2 = aAlfa1;

    aAlfa2 = nEjer1;
    aAlfa1 = "01.01." + aAlfa2; efDFecha = aAlfa1;
    aAlfa1 = "31.12." + aAlfa2; efHFecha = aAlfa1;
    eaTituloL = #0#40; ||Titulo del Libro

    nDiasA1 = 365; |SI enEjer = 2008; |O enEjer = 2012; |O enEjer = 2016;
                    |O enEjer = 2020; |O enEjer = 2024; |O enEjer = 2028;
                          nDiasA1 = 366;
                  |FINSI;
    nDiasA2 = 365; |SI nEjer2 = 2008; |O nEjer2 = 2012; |O nEjer2 = 2016;
                    |O nEjer2 = 2020; |O nEjer2 = 2024; |O nEjer2 = 2028;
                          nDiasA2 = 366;
                  |FINSI;

    eaDDepar  = "  ";
    eaHDepar  = "zz";

    aFichero = ("1v" + Usuario) % 108;
    |NOME_DAT #999 aFichero; |DELINDEX #999;

    nPrint = 0;
    |ABRE #999;
    |SI #0#2 ! 0;
        nDEmpresa = #0#2;
        nHEmpresa = #0#3;
        |BUCLE agifigen;
    |SINO;
        |PARA nPara1 = 4;  |SI nPara1 [ 23;  |HACIENDO nPara1 = nPara1 + 1;
              |SI #0nPara1 ! 0;
                  nDEmpresa = #0nPara1;
                  nHEmpresa = #0nPara1;
                  |BUCLE agifigen;
              |FINSI;
        |SIGUE;
    |FINSI;
    |CIERRA #999;

    |SI nPrint = 1;
        |HAZ Imprimir;
    |SINO;
        |MENAV "    Seleccion vacia";
    |FINSI;

    |DELINDEX #999;
|FCAL;


|PROGRAMA;

 |CLS;
 |CABEZA "Inf.Exclusion Reg.Tributacion";

 |ABRE #0;
 |LEE 001#0p;
 |SI FSalida ! 0;
     |DDEFECTO #0;
     fLaFecha = ~;
     aAlfa1 = fLaFecha;
     aAlfa1 = aAlfa1 % -104;
     #0#1 = aAlfa1;
     |HAZ Eldefecto;
     |GRABA 020#0c;
 |FINSI;

 |HAZ BusquedaSocie;
 |SI eaPathContasoc  = ""; |C_M #0#54, 1, "N"; #0#54 = "N"; |FINSI;

 enPlan = 2008;
 eaPlanilla = "dsmom088";  |HAZ PlanDePlanillas;

 |PINPA #0#0;
 |PINDA #0#0;

 |SI enEmpresa ! 0;
     |DDEFECTO #0;
     #0#2 = enEmpresa;
     #0#3 = enEmpresa;
     #0#1 = enEjer;
     |HAZ Tipo3;
 |SINO;
     |ENDATOS #1#0;
 |FINSI;
 |GRABA 020#0a;
 |CIERRA #0;
|FPRO;

|PROCESO BusquedaSocie;
     eaPathContasoc  = "";
     |DBASS aBase;   |QBF aBase;

     aAlfa = aBase + "contasoc/def/maempr10.mas";
     |XABRE "E", aAlfa, nCanal;
     |SI FSalida ] 0;
         eaPathContasoc = aBase + "contasoc/";
     |FINSI;
|FPRC;
