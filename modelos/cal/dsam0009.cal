|FICHEROS;
     :integral/dsam0009,MANTE;
     :integral/dsam0003;
     :integral/dsam0116;

     :basica/agitab99;
     :integral/dsaw0003;

     dsaw0035@;
|FIN;

|VARIABLES;
     aAlfa       = "";
     aAlfa1      = "";
     aAlfa2      = "";
     aMas        = "";
     aChar       = "";
     aCadena     = "";
     aComodin1   = "";
     aComodin2   = "";
     aCaracter   = "";
     aID         = "";
     nModo       = 0;
     nNume       = 0;

     nID         = 0;
     nFSalida    = 0;
     nBanco      = 0;
     nDigito1    = 0;
     nDigito2    = 0;
     nCont       = 0;
     nCalc       = 0;

     {10} aPopup = "";

     &eaEntidad  = "";
     &eaSucursal = "";
     &eaCuenta   = "";
     &eaDC       = "";
     &eaCta      = "";
     &eaPais     = "";
     &eaIBAN     = "";
     &eaIBANant  = "";
     nSwC        = 0;

     &eaM22Prog  = "";
     &enM22Clie  = 0;
     &enM22MoFa  = 0;
     &eaM22MoFa  = "";
     &enM22Modo  = 0;
     &enM22Inkey = 0;
|FIN;

|PROCESO Tipo11m0009;
|FPRC;

|PROCESO CalculoDCm0009;
     nDigito1  = 0;
     nDigito2  = 0;
     aComodin1 = "";
     aComodin2 = "";

     |PARA nCont = 1; |SI nCont [ 4; |HACIENDO nCont = nCont + 1;
           nCalc = 0 - ((100 * nCont) + 1);
           aCaracter = eaSucursal % nCalc;
           |SI aCaracter = ""; aCaracter = "0"; |FINSI;
           aComodin1 = aComodin1 + aCaracter;
     |SIGUE;

     |PARA nCont = 1; |SI nCont [ 4; |HACIENDO nCont = nCont + 1;
           nCalc = 0 - ((100 * nCont) + 1);
           aCaracter = eaEntidad % nCalc;
           |SI aCaracter = ""; aCaracter = "0"; |FINSI;
           aComodin1 = aComodin1 + aCaracter;
     |SIGUE;

     |PARA nCont = 1; |SI nCont [ 8; |HACIENDO nCont = nCont + 1;
           nCalc = (100 * nCont) + 1;
           aCaracter = aComodin1 % nCalc;
           nCalc     = aCaracter;

           |SI nCont = 1; nDigito1 = nDigito1 + (nCalc * 6);  |FINSI;
           |SI nCont = 2; nDigito1 = nDigito1 + (nCalc * 3);  |FINSI;
           |SI nCont = 3; nDigito1 = nDigito1 + (nCalc * 7);  |FINSI;
           |SI nCont = 4; nDigito1 = nDigito1 + (nCalc * 9);  |FINSI;
           |SI nCont = 5; nDigito1 = nDigito1 + (nCalc * 10); |FINSI;
           |SI nCont = 6; nDigito1 = nDigito1 + (nCalc * 5);  |FINSI;
           |SI nCont = 7; nDigito1 = nDigito1 + (nCalc * 8);  |FINSI;
           |SI nCont = 8; nDigito1 = nDigito1 + (nCalc * 4);  |FINSI;
     |SIGUE;

     nDigito1 = 11 - (nDigito1 $ 11);
     |SI nDigito1 = 10; nDigito1 = 1; |FINSI;
     |SI nDigito1 = 11; nDigito1 = 0; |FINSI;

     |PARA nCont = 1; |SI nCont [ 10; |HACIENDO nCont = nCont + 1;
           nCalc = 0 - ((100 * nCont) + 1);
           aCaracter = eaCuenta % nCalc;
           |SI aCaracter = ""; aCaracter = "0"; |FINSI;
           aComodin2 = aComodin2 + aCaracter;
     |SIGUE;

     |PARA nCont = 1; |SI nCont [ 10; |HACIENDO nCont = nCont + 1;
           nCalc = (100 * nCont) + 1;
           aCaracter = aComodin2 % nCalc;
           nCalc     = aCaracter;

           |SI nCont = 1;  nDigito2 = nDigito2 + (nCalc * 6);  |FINSI;
           |SI nCont = 2;  nDigito2 = nDigito2 + (nCalc * 3);  |FINSI;
           |SI nCont = 3;  nDigito2 = nDigito2 + (nCalc * 7);  |FINSI;
           |SI nCont = 4;  nDigito2 = nDigito2 + (nCalc * 9);  |FINSI;
           |SI nCont = 5;  nDigito2 = nDigito2 + (nCalc * 10); |FINSI;
           |SI nCont = 6;  nDigito2 = nDigito2 + (nCalc * 5);  |FINSI;
           |SI nCont = 7;  nDigito2 = nDigito2 + (nCalc * 8);  |FINSI;
           |SI nCont = 8;  nDigito2 = nDigito2 + (nCalc * 4);  |FINSI;
           |SI nCont = 9;  nDigito2 = nDigito2 + (nCalc * 2);  |FINSI;
           |SI nCont = 10; nDigito2 = nDigito2 + (nCalc * 1);  |FINSI;
      |SIGUE;

      nDigito2 = 11 - (nDigito2 $ 11);
      |SI nDigito2 = 10; nDigito2 = 1; |FINSI;
      |SI nDigito2 = 11; nDigito2 = 0; |FINSI;

      eaDC = nDigito1 + nDigito2;
|FPRC;

|PROCESO Modelo;
     |SI #dsam0009#1 = 999;
         #dsam0009#2 = "EL RESTO DE MODELOS BAJO ESTE BANCO";
     |FINSI;

     |SI #dsam0009#1 = 998;
         #dsam0009#2 = "BANCO PARA GESTION LABORAL";
     |FINSI;

     |SI #dsam0009#1 ! 998; |Y #dsam0009#1 ! 999;
         |ABRE #agitab99;
         #agitab99#0 = #dsam0009#1;
         |LEE 040#agitab99.=;
         |SI FSalida ! 0;
             |CIERRA #agitab99;
             |MENAV "     No Existe el Modelo";
             |ERROR;
             |ACABA;
         |FINSI;
         |CIERRA #agitab99;

         |SI #agitab99#4 = "N";
             |SI #agitab99#0 ! 200;  |Y #agitab99#0 ! 201;  |Y #agitab99#0 ! 202;
                 |MENAV "     Modelo no Activado";
                 |ERROR;
             |FINSI;
         |FINSI;

         #dsam0009#2 = #agitab99#1;
     |FINSI;

     |PINTA #dsam0009#2;
     |HAZ IgualoM9aW3;
     |HAZ GrabaAntesM22;
|FPRC;

|PROCESO IgualoM9aW3;
     eaM22Prog  = "dsam0009";
     enM22Clie  = #dsam0009#0;
     enM22MoFa  = #dsam0009#1;
     eaM22MoFa  = #dsam0009#2;
     |DDEFECTO #dsaw0003;
     #dsaw0003#0 = #dsam0009#3;
     #dsaw0003#1 = #dsam0009#4;
     #dsaw0003#2 = #dsam0009#5;
     #dsaw0003#3 = #dsam0009#6;
     #dsaw0003#4 = #dsam0009#7;
     #dsaw0003#5 = #dsam0009#8;
     #dsaw0003#6 = #dsam0009#9;
     #dsaw0003#17 = #dsam0009#10;
     #dsaw0003#20 = #dsam0009#11;
     #dsaw0003#21 = #dsam0009#18;
     #dsaw0003#22 = #dsam0009#12;
|FPRC;

|PROCESO Tipo2m0009; |TIPO 2;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 2;
         nNume = 0 +. 1;
         |SI nNume < 0; |ACABA; |FINSI;
     |FINSI;
     enM22Modo = nModo;

     |HAZ IgualoM9aW3;
     |HAZ GrabaDespuesM22;
|FPRC;

|PROCESO ConsEntidadm9;  |TIPO 66;
     |DBASS aMas;  |QBF aMas;
     aMas = aMas + "integral/def/dsaw0035.mas";
     |CARGA_DEF aMas, dsaw0035@;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |PUSHV 0501 2380;
     |ABRE #dsaw0035@;
     |SELECT #4#dsaw0035@;
     #dsaw0035@#2 = #dsam0009#5;
     #dsaw0035@#3 = #dsam0009#6;
     #dsaw0035@#0 = #dsam0009#3;
     |LEE 040#dsaw0035@.];
     |SI FSalida ! 0;
         |LEE 040#dsaw0035@.p;
     |FINSI;
     |CONSULTA_CLAVE #4#dsaw0035@;
     nFSalida = FSalida;
     |CIERRA #dsaw0035@;
     |POPV;

     |SI nFSalida = 0;
         #dsam0009#3  = #dsaw0035@#0;   |PINTA #dsam0009#3;
         #dsam0009#4  = #dsaw0035@#1;   |PINTA #dsam0009#4;
         #dsam0009#5  = #dsaw0035@#2;   |PINTA #dsam0009#5;
         #dsam0009#6  = #dsaw0035@#3;   |PINTA #dsam0009#6;
         #dsam0009#10 = #dsaw0035@#5;   |PINTA #dsam0009#10;
     |FINSI;

     |DESCARGA_DEF #dsaw0035@;
|FPRC;

|PROCESO SinBancom9;
     |C_M #dsam0009#4,  1, "S";
     |C_M #dsam0009#6,  1, "S";
     |C_M #dsam0009#10, 1, "S";
     |C_M #dsam0009#7,  1, "S";
     |C_M #dsam0009#8,  1, "S";

     |SI #dsam0009#5 = "    ";
                              #dsam0009#3  = 0;   |PINTA #dsam0009#3;
         |C_M #dsam0009#4,  1, "N";  #dsam0009#4  = "";  |PINTA #dsam0009#4;
         |C_M #dsam0009#10, 1, "N";  #dsam0009#10 = "";  |PINTA #dsam0009#10;
         |C_M #dsam0009#6,  1, "N";  #dsam0009#6  = "";  |PINTA #dsam0009#6;
         |C_M #dsam0009#8,  1, "N";  #dsam0009#8  = "";  |PINTA #dsam0009#8;
         |C_M #dsam0009#7,  1, "N";  #dsam0009#7  = "";  |PINTA #dsam0009#7;
     |FINSI;
|FPRC;

|PROCESO ChequeaBancom9;
     |SI #dsam0009#5 = "    ";  |ACABA;  |FINSI;

     |ABRE #dsam0116;
     |SELECT #4#dsam0116;
     #dsam0116#2 = #dsam0009#5;
     #dsam0116#3 = #dsam0009#6;
     #dsam0116#0 = #dsam0009#3;
     |LEE 040#dsam0116.];
     |SI FSalida ! 0;  |O #dsam0116#2 ! #dsam0009#5;  |O  #dsam0116#3 ! #dsam0009#6;
         #dsam0116#2 = #dsam0009#5;
         #dsam0116#3 = #dsam0009#6;
         #dsam0116#0 = 0;
         |LEE 040#dsam0116.];
         |SI FSalida ! 0;  |O #dsam0116#2 ! #dsam0009#5;  |O  #dsam0116#3 ! #dsam0009#6;
             |CONFI "0000SNo existe el Banco, Desea crearlo.";
             |SI FSalida ! 0;  |ERROR;  |ACABA;  |FINSI;

             |SELECT #1#dsam0116;
             |PARA nBanco = 1;  |SI nBanco [ 99999;  |HACIENDO nBanco = nBanco + 1;
                   #dsam0116#0 = nBanco;
                   |LEE 000#dsam0116.=;
                   |SI FSalida ! 0;  |SAL;  |FINSI;
             |SIGUE;

             |SI nBanco > 99999;
                 |MENAV "000 No permito mas Bancos.";
                 |ERROR;
                 |ACABA;
             |FINSI;

             |DDEFECTO #dsam0116;
             #dsam0116#0 = nBanco;
             #dsam0116#2 = #dsam0009#5;
             #dsam0116#3 = #dsam0009#6;
             |GRABA 020#dsam0116.b;

             |PUSHV 0501 2380;
             |PINPA #0#dsam0116;
             |PINDA #0#dsam0116;
             |ENDATOS #1#dsam0116;
             |SI FSalida = 0;
                 |GRABA 020#dsam0116.a;
                 |LIBERA #dsam0116;
             |SINO;
                 |BORRA 020#dsam0116.a;
                 |LIBERA #dsam0116;

                 |POPV;
                 |ERROR;
                 |ACABA;
             |FINSI;
             |POPV;
         |FINSI;
     |FINSI;

     #dsam0009#3  = #dsam0116#0;   |PINTA #dsam0009#3;
     #dsam0009#4  = #dsam0116#1;   |PINTA #dsam0009#4;
     #dsam0009#10 = #dsam0116#5;   |PINTA #dsam0009#10;
     |CIERRA #dsam0116;

     aAlfa = #dsam0009#4;  |QBF aAlfa;
     |C_M #dsam0009#4, 1, "N";
     |SI aAlfa = "";  |C_M #dsam0009#4, 1, "S";  |FINSI;

     |C_M #dsam0009#10, 1, "N";
     |SI aAlfa = "";  |C_M #dsam0009#10, 1, "S";  |FINSI;
|FPRC;

|PROCESO DCm9;
     eaEntidad  = #dsam0009#5; |QBF eaEntidad;
     eaSucursal = #dsam0009#6; |QBF eaSucursal;
     eaCuenta   = #dsam0009#8; |QBF eaCuenta;
     eaDC       = "";

     |SI eaEntidad = "";  |Y eaSucursal = "";  |Y eaCuenta = "";
         |ACABA;
     |FINSI;

     |HAZ CalculoDCm0009;

     |SI FSalida ! 10;
         |SI #dsam0009#7 ! eaDC;
             aAlfa = "     El Digito de Control deberia ser " + eaDC;
             |MENAV aAlfa;
         |FINSI;
         |ACABA;
     |FINSI;

     #dsam0009#7 = eaDC;
     |PINTA #dsam0009#7;
|FPRC;

|PROCESO Tipo66C9;  |TIPO 66;
     aAlfa = "|C009WID";
     aID   = #dsam0009#aAlfa#;
     nID   = aID;

     aPopup1  = nID;  || Si fuera -1 en la posicion
     aPopup2  = 6;
     aPopup3  = "1.- Efectivo";
     aPopup4  = "2.- Adeudo en cuenta";
     aPopup5  = "3.- Domiciliacion";
     aPopup6  = "4.- Aplazamiento";
     aPopup7  = "5.- Aplazamiento + Ingreso";
     aPopup8  = "6.- Cuenta corriente tributaria (Ingreso y Devolucion)";

     IaPopup  = aPopup1  <-;
     |ESPECIFICA "TRACKPOPUP", aPopup;
     |SI FSalida ! 0;
         IaPopup  = aPopup3  <-;
         IaPopup  = IaPopup + (FSalida - 1);

         aAlfa = aPopup % 101;  #dsam0009#Campo# = aAlfa;
     |FINSI;
     |PINTA #dsam0009#Campo#;
|FPRC;

|PROCESO VerSwC_m9; |TIPO 0;
   aAlfa = Contenido;
   |SI aAlfa ! #dsam0009#Campo#;
       nSwC = 1;
   |FINSI;
|FPRC;

|PROCESO HayDatosP_m9;
    aAlfa1 = #dsam0009#5 + #dsam0009#6 + #dsam0009#7 + #dsam0009#8;

    |ABRE #dsam0003;
    #dsam0003#0 = #dsam0009#0;
    |LEE 141#dsam0003.=;
    |SI FSalida = 0;
        |SI aAlfa1 ! #dsam0003#53;
            |CONFI "0000SModificar Cuenta Declarante en Datos Personales del Cliente";
            |SI FSalida = 0;
                #dsam0003#53 = aAlfa1;
                |GRABA 020#dsam0003.a;
            |FINSI;
        |FINSI;
    |FINSI;
    |LIBERA #dsam0003;
    |CIERRA #dsam0003;
|FPRC;

|PROCESO Tipo7IBAN_Fm9;  |TIPO 7;
     aAlfa = #dsam0009#12;   |QBF aAlfa;

     |SI aAlfa ! ""; |Y nSwC = 0;  |ACABA;  |FINSI;

     |SI nSwC = 1;
         |SI aAlfa ! "";
             |CONFI "0000NSe han modificado los datos de la Cuenta Corriente. Recalcular el IBAN?";
             |SI FSalida ! 0; nSwC = 0; |FINSI;
         |FINSI;
         |SI #dsam0009#1 = 999; |HAZ HayDatosP_m9; |FINSI;
         |SI nSwC = 0;
             |ACABA;
         |FINSI;
     |FINSI;

     eaIBANant = "";
     eaCta     = #dsam0009#5 + #dsam0009#6 + #dsam0009#7 + #dsam0009#8;
     eaPais    = #dsam0009#18;
     |HAZ CalcuIBAN;

     #dsam0009#12 = eaIBAN;
     |PINTA #dsam0009#12;
     nSwC = 0;
|FPRC;

|PROCESO Tipo0IBAN_Fm9;  |TIPO 0;
     |SI FSalida = 2; |ACABA; |FINSI;

     eaCta     = #dsam0009#5 + #dsam0009#6 + #dsam0009#7 + #dsam0009#8;
     eaPais    = #dsam0009#18;
     eaIBANant = #dsam0009#12;
     |HAZ CheqIBAN;

     aAlfa = #dsam0009#12;    |QBF aAlfa;
     |SI aAlfa ! eaIBAN;
         |SI aAlfa = "";
             #dsam0009#12 = eaIBAN;
             |PINTA #dsam0009#12;
         |SINO;
             |CONFI "0000SEl codigo IBAN puede ser erroneo, quiere calcularlo.";
             |SI FSalida = 0;
                 #dsam0009#12 = eaIBAN;
                 |PINTA #dsam0009#12;
                 |ERROR;
             |FINSI;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO Tipo12Fm9;  |TIPO 12;

     eaIBANant = #dsam0009#12;
     eaCta     = #dsam0009#5 + #dsam0009#6 + #dsam0009#7 + #dsam0009#8;
     eaPais    = #dsam0009#18;
     |HAZ CheqIBAN;

     aAlfa = #dsam0009#12;    |QBF aAlfa;
     |SI aAlfa ! eaIBAN; |Y aAlfa ! "";
         |MENAV "0000El codigo IBAN puede ser erroneo.";
     |FINSI;
     nSwC = 0;
|FPRC;
