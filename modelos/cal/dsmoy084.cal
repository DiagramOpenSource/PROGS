|FICHEROS;
   dsmoy084 #0;

   dsmom001 #1;
   :/integral/def/dsam0000 #2;
   dsmom004 #4;

   dsmom017 #17;
   dsmom018 #18;

   dsmom060 #60;
   dsmom061 #61;
   dsmom162;

   :/integral/def/dsam0003 #103;
   :/basica/def/agicen14 #114;
   :/basica/def/agifigen #118;

   :/basica/def/agicen18 #116;
   :/basica/def/agicen17 #117;

   :/contagen/def/canempre #300;
   :/contagen/def/caexistl #303;
   :/contagen/def/cacuenta #304;
   :/contagen/def/cacuedep #305;

   dsmom009 #09;
   dsmom010 #10;
   dsmom011 #11;
   dsmom012 #12;
   dsmom013 #13;

   dsmopl10;
   dsmopl12;
   dsmopl13;

   dsmow032 #900;          ||Def de impresion
   dsmow033 #901;
   dsmow017 #917;
   dsmow043 #999,MANTE;

   dsmoc130 #130;

|FIN;

|VARIABLES;

   SwPrint  = 0;
   &entrada = 1;
   &eSwSelec  = 0;
   &eaDDepar  = "";
   &eaHDepar  = "";
   &efDFecha  = @;
   &efHFecha  = @;
   &eaNomFDep = "";  ||Nombre del Fichero Creado
   &dirfich0  = "";
   &enSwNo    = 0;
   informe    = "";

   nInkey     = 0;
   aFichero   = "";
   nCampo     = 0;
   nDEmpresa  = 0;
   nHEmpresa  = 0;
   nDActividad = 0;
   nHActividad = 0;
   nGTempo     = 0;
   nEstado     = 0;

   aPath       = "";
   nEjercicio  = 0;
   nQueBusco   = 0;
   aQueBusco   = "";

   aAlfa1 = "";
   aAlfa2 = "";
   aAlfa3 = "";
   nPara1 = 0;
   nPara2 = 0;
   nPara3 = 0;
   nPara4 = 0;
   nNume1 = 0;
   nNume2 = 0;
   nNume3 = 0;
   nNume4 = 0;
   nNume5 = 0;
   SwHayDatos = 0;
   SwRegEspec = 0;

   fFecha    = @;
   fDFecha   = @;
   fHFecha   = @;
   fecha1    = @;
   fecha2    = @;
   nDPeriodo = 0;
   nHPeriodo = 0;

   nComplem  = 0;
   nTanto10  = 0;
   nTanto11  = 0;
   nTanto12  = 0;
   nTanto13  = 0;
   nTotal_I  = 0;
   nTotal_C  = 0;
   nTotal_V  = 0;
   nTotal_G  = 0;

   nTanto10_3  = 0;
   nTanto11_3  = 0;
   nTanto12_3  = 0;
   nTanto13_3  = 0;

   nTanto10_6  = 0;
   nTanto11_6  = 0;
   nTanto12_6  = 0;
   nTanto13_6  = 0;

   nTanto10_9  = 0;
   nTanto11_9  = 0;
   nTanto12_9  = 0;
   nTanto13_9  = 0;

   nTanto10_12 = 0;
   nTanto11_12 = 0;
   nTanto12_12 = 0;
   nTanto13_12 = 0;

   nTotal_I_3  = 0;
   nTotal_C_3  = 0;
   nTotal_V_3  = 0;
   nTotal_G_3  = 0;

   nTotal_I_6  = 0;
   nTotal_C_6  = 0;
   nTotal_V_6  = 0;
   nTotal_G_6  = 0;

   nTotal_I_9  = 0;
   nTotal_C_9  = 0;
   nTotal_V_9  = 0;
   nTotal_G_9  = 0;

   nTotal_I_12 = 0;
   nTotal_C_12 = 0;
   nTotal_V_12 = 0;
   nTotal_G_12 = 0;

   aElDepar  = "";
   aDCuenta = "";
   aHCuenta = "";
   nSaldo = 0;
   nSald1 = 0;
   nSald2 = 0;
   nSald3 = 0;
   nSald4 = 0;
   nSald5 = 0;
   sw = 0;
   nEnExi    = 0;
   fLaFecha  = @;
   fFechaIni = @;
   fFechaFin = @;

   nSalPar1 = 0;
   nSalPar2 = 0;
   nSalPar3 = 0;
   nSalPar4 = 0;
   nSalPar5 = 0;
   nImpVar1 = 0;
   nImpVar2 = 0;
   nImpVar3 = 0;
   nImpVar4 = 0;
   nImpVar5 = 0;
   nSwPlan  = 0;

   nDModulo  = 0;
   nMixto    = 0;
   nDConta   = 0;
   nDPlani   = 0;
  nTTribut   = 0;

   &eaCodRenta = "";
   &eaCodR2    = "";
   &eaRenta    = "";
   &enEjerR    = 0;
   &enCodRen   = 0;
   &eaCodRen   = "";

   aElDni      = "";
   nOpTitular  = 0;
   aBase       = "";
   aAlfa       = "";
   nCanal      = 0;
   &eaPathIntegral  = "";

   &eaTituloL = "";
   &aTipIG    = "";

   nSPin1     = 0;
   nActividad = 0;
   swNoExis   = 0;
   nExiFin    = 0;
   swExisL    = 0;
   swExisA    = 0;

   &enRendimiento = 0;
   &enAuto        = 0;
|FIN;

|INCLUYE i_variar;
|INCLUYE i_varcon;
|INCLUYE i_masivo;

|| //////////////////// Calculos de Pantalla \\\\\\\\\\\\\\\\\\\\\\\\\\
|CALCULO CodigoRenta; |TIPO 7;
     |DBASS aBase;  |QBF aBase;
     aAlfa = aBase + "integral/def/dsam0000.mas";
     |XABRE "E", aAlfa, nCanal;
     |SI FSalida ] 0;
         eaPathIntegral  = aBase + "integral/";
     |FINSI;

     |C_M #0#55, 1, "S";
     |SI eaPathIntegral = "";
         |C_M #0#55, 1, "N";  #0#55 = "N";  |PINTA #0#55;
     |FINSI;
|FCAL;

|CALCULO Eano; |TIPO 7;
 |C_M #0#1, 1, "S";
 |SI enEjer = 0;
     fLaFecha = ~;
     aAlfa1 = fLaFecha; aAlfa1 = aAlfa1 % -104;
     #0#1 = aAlfa1;
 |SINO;
     #0#1 = enEjer;
 |FINSI;
 |PINTA #0#1;
 aAlfa2 = "01.01." + aAlfa1; #0#47 = aAlfa2; |PINTA #0#47;
 aAlfa3 = "31.12." + aAlfa1; #0#48 = aAlfa3; |PINTA #0#48;

 |C_M #0#38, 1, "S";
 |C_M #0#39, 1, "S";
 |SI enActividad ! 0;
     #0#38 = enActividad;  |PINTA #0#38;
     #0#39 = enActividad;  |PINTA #0#39;

     |C_M #0#38, 1, "N";
     |C_M #0#39, 1, "N";
 |FINSI;
|FCAL;

|CALCULO losDefectos; |TIPO 0;
   aAlfa1 = #0#1;
   aAlfa2 = "01.01." + aAlfa1;
   aAlfa3 = "31.12." + aAlfa1;
   #0#47 = aAlfa2; |PINTA #0#47; fecha1 = #0#47;
   #0#48 = aAlfa3; |PINTA #0#48; fecha2 = #0#48;
|FCAL;

|CALCULO Tipo7;  |TIPO 7;
     |SI enEmpresa ! 0;
         #0#2  = enEmpresa;  |PINTA #0#2;
         #0#3  = enEmpresa;  |PINTA #0#3;

         |C_M #0#2, 1, "N";
         |C_M #0#3, 1, "N";
     |FINSI;
|FCAL;

|CALCULO Parrilla; |TIPO 0;
     |SI #0#2 = 0;
         #0#2 = 0;  |PINTA #0#2;
         #0#3 = 0;  |PINTA #0#3;  |C_M #0#3, 1, "N";
         |PARA nCampo = 4;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |SINO;
         |SI enEmpresa = 0;
             |C_M #0#2, 1, "S";
             |C_M #0#3, 1, "S";
         |FINSI;

         |PARA nCampo = 4;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "N";
               #0nCampo = 0;  |PINTA #0nCampo;
         |SIGUE;
     |FINSI;
|FCAL;

|CALCULO NoMasEmpresas; |TIPO 0;
     |SI #0Campo = 0;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
               #0nCampo = 0;  |C_M #0nCampo, 1, "N";
         |SIGUE;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |FINSI;
|FCAL;

|CALCULO YaSeCual; |TIPO 7;
 |SI enEmpresa ! 0;
     #0#46 = "S"; |PINTA #0#46;
     #0#24 = "S"; |PINTA #0#24;
     #0#25 = 00;  |PINTA #0#25;
     #0#26 = 99;  |PINTA #0#26;
     |C_M #0#24, 1, "N";
     |C_M #0#25, 1, "N";
     |C_M #0#26, 1, "N";
     |C_M #0#46, 1, "N";
 |FINSI;
|FCAL;

|CALCULO SelTipo0; |TIPO 0;
 |SI #0#46 = "N";
     #0#25 = 0;   |PINTA #0#25;  |C_M #0#25, 1, "N";
     #0#26 = 99;  |PINTA #0#26;  |C_M #0#26, 1, "N";
     |PARA nCampo = 27;  |SI nCampo [ 36;  |HACIENDO nCampo = nCampo + 1;
           |C_M #0nCampo, 1, "S";
     |SIGUE;
 |SINO;
     |SI enEmpresa = 0;
         |C_M #0#25, 1, "S";
         |C_M #0#26, 1, "S";
     |FINSI;
     |PARA nCampo = 27;  |SI nCampo [ 36;  |HACIENDO nCampo = nCampo + 1;
           |C_M #0nCampo, 1, "N";
           #0nCampo = 0;  |PINTA #0nCampo;
     |SIGUE;
 |FINSI;
|FCAL;

|CALCULO SelTipo1;
 |SI #0Campo = 0;
     |PARA nCampo = Campo + 1;  |SI nCampo [ 36;  |HACIENDO nCampo = nCampo + 1;
           #0nCampo = 0;  |C_M #0nCampo, 1, "N";
     |SIGUE;
 |SINO;
     |PARA nCampo = Campo + 1;  |SI nCampo [ 36;  |HACIENDO nCampo = nCampo + 1;
           |C_M #0nCampo, 1, "S";
     |SIGUE;
 |FINSI;
|FCAL;

|CALCULO LasExi; TIPO 0;
 aAlfa1 = #0#54;
 |C_M #0#52,1,aAlfa1;
 |C_M #0#53,1,aAlfa1;
 |SI aAlfa1 = "N";
     #0#52 = 00;     |PINTA #0#52;
     #0#53 = 12;     |PINTA #0#53;
 |FINSI;
|FCAL;

|CALCULO laFecha; |TIPO 0;
/*
 |SI #0Campo < fecha1; |O #0Campo> fecha2;
     |ERROR;
     |ACABA;
 |FINSI;
 |SI Campo = 48; |Y #0#47 > #0#48;
     |ERROR;
     |ACABA;
 |FINSI;
 aAlfa1 = #0Campo; aAlfa1 = aAlfa1 % 402;
 nNume1 = aAlfa1;
 nNume2 = 3;
 |SI nNume1 > 3; nNume2 = 6;  |FINSI;
 |SI nNume1 > 6; nNume2 = 9;  |FINSI;
 |SI nNume1 > 9; nNume2 = 12; |FINSI;
 nNume1 = Campo + 2;
 #0nNume1 = nNume2; |PINTA #0nNume1;
 |SI #0#51 = "S"; |Y Campo = 48;
     nNume1 = Campo + 5;
     #0nNume1 = nNume2; |PINTA #0nNume1;
 |FINSI;
*/
|FCAL;

|CALCULO AntesExis; |TIPO 7;
   |C_M #0Campo, 0, aAlfa1;
   |SI aAlfa1 = "N"; |ACABA; |FINSI;

   |SI #0#51 = "S";
       nNume1 = Campo - 3;
       #0Campo = #0nNume1; |PINTA #0Campo;
   |FINSI;
|FCAL;

|| **********************************************************************************
|| **********************************************************************************
|| ....................... Situa la Empresa Contable o de Planillas

|PROCESO LEmpresaPlan;
   nGTempo = nGTempo + 1;
   #999#0 = nGTempo;       || Guia
   #999#1 = enEmpresa;     || Empresa
   #999#2 = 0;             || Periodo Contable
   #999#3 = enEjer;        || A¤o
   #999#4 = 01;            || Desde Mes
   #999#5 = 1;             || N§ Digitos
   #999#6 = 4;             || Digitos Nivel 1
   #999#7 = 0;             || Digitos Nivel 2
   #999#8 = 0;             || Digitos Nivel 3
   #999#9 = 0;             || Digitos Nivel 4
   #999#10 = 0;            || Digitos Nivel 5
   #999#11 = 0;            || Digitos Nivel 6
   #999#12 = eaEstadoEmp;  || Estado
   #999#13 = "E";          || Moneda
   #999#14 = #agifigen#1;  || Nombre Empresa
   |SI nDModulo = 1;
       #999#15 = "MODULOS";    || Path
   |SINO;
       #999#15 = "PLANILLAS";  || Path
   |FINSI;
   #999#16 = #0#43;        || Emitir
   |GRABA 020#999n;
|FPRC;

|PROCESO LeoDsmom009;
   nDPlani = 1;
   |ERROR10;
|FPRC;

|DEFBUCLE LeoDsmom009;
 #9#1;
 ;
 #agicen14#0, #0#1, 01, 00, #agicen14#1;
 #agicen14#0, #0#1, 99, 99, #agicen14#1;
 e;
 NULL, LeoDsmom009;
|FIN;

|PROCESO LaActividad;
 |SI #agicen14#6 > "01.01.0000";
     |SI #0#24 = "N"; |ACABA; |FINSI;
 |FINSI;

 |SI #0#56 = "N"; ||las actividades exentas no
     |SI #agicen14#9 = 1; |O #agicen14#9 = 18; |O #agicen14#9 = 19;
         |ACABA;
     |FINSI;
 |FINSI;

 aAlfa1 = #agicen14#15; |QBF aAlfa1;
 |SI aAlfa1 = "MODULOS";    nDModulo = 1; |FINSI;
 |SI aAlfa1 = "REGISTROS";  nDConta  = 1;  |FINSI;
 |SI aAlfa1 = "PLANILLAS";  nDPlani  = 1;  |FINSI;
 |SI aAlfa1 = "MANUAL"; |BUCLE LeoDsmom009; nDConta = 1; |FINSI;

 |SI nDConta = 0;
     |SI nDPlani = 1; |O nDModulo = 1;
         enSwSelEmp = 0;
     |FINSI;
 |FINSI;
 |SI nDConta = 1;
     |SI nDPlani = 1; |O nDModulo = 1;
         nMixto     = 1;
         enSwSelEmp = 1;
     |FINSI;
 |FINSI;
|FPRC;

|DEFBUCLE LaActividad;
  #agicen14#1,
  ;
  enEmpresa, 01;
  enEmpresa, 99;
  e;
  NULL, LaActividad;
|FIN;

|| .................... graba Contabilidad
|PROCESO LEmpresaConta;

   enEmpresa  = #canempre#2;
   enPerConta = #canempre#3;   || Periodo Contable

   |SI #0#24 = "N"; |Y #canempre#21 = "I"; |ACABA; |FINSI;

|| ------------------------------------------------------------------------
   aAlfa1 = enEmpresa;  aAlfa1 = ("00000" + aAlfa1) % -105;
   aAlfa2 = enPerConta; aAlfa2 = ("0" + aAlfa2) % -101;
   aPathConta  = aPath + "contagen/fich/" + aAlfa1 + aAlfa2 + "/";
   |SI nMixto = 1;
       aPathConta = aPathConta + ".Mixto";
   |FINSI;

   nGTempo = nGTempo + 1;
   #999#0 = nGTempo;       || Guia
   #999#1 = enEmpresa;     || Empresa
   #999#2 = enPerConta;    || Periodo Contable
   #999#3 = enEjer;        || A¤o
   #999#4 = #canempre#11;  || Desde Mes
   #999#5 = #canempre#13;  || N§ Digitos
   #999#6 = #canempre#14;  || Digitos Nivel 1
   #999#7 = #canempre#15;  || Digitos Nivel 2
   #999#8 = #canempre#16;  || Digitos Nivel 3
   #999#9 = #canempre#17;  || Digitos Nivel 4
   #999#10 = #canempre#18; || Digitos Nivel 5
   #999#11 = #canempre#19; || Digitos Nivel 6
   eaEstadoEmp = "S"; |SI #canempre#21 = "I"; eaEstadoEmp = "N"; |FINSI;
   #999#12 = eaEstadoEmp;  || Estado
   #999#13 = #canempre#28; || Moneda
   #999#14 = #canempre#1;  || Nombre Empresa
   #999#15 = aPathConta;   || Path
   #999#16 = #0#43;        || Emitir
   |GRABA 020#999n;
|FPRC;

|PROCESO LEmpresaEOS;
 enEmpresa  = #agifigen#0;
 enPerConta = 0;

 eaEstadoEmp = "S";
 |SI #agifigen#94 > "01.01.0000";
     eaEstadoEmp = "N";
     |SI #0#24 = "N"; |ACABA; |FINSI;
 |FINSI;

|| ... Seleccion de Tipo de Empresa
  nSPin1 = #agifigen#87;
  enSwSelEmp = 1;
  |SI #0#46 = "S";
      |SI nSPin1 ] #0#25; |Y nSPin1 [ #0#26;
          enSwSelEmp = 0;
      |FINSI;
  |SINO;
      |SI nSPin1 = #0#27;
          enSwSelEmp = 0;
      |SINO;
          |PARA nCampo = 28;  |SI nCampo [ 36;  |HACIENDO nCampo = nCampo + 1;
                |SI nSPin1 = #0nCampo; |Y nSPin1 ! 0;
                    enSwSelEmp = 0;
                    |SAL;
                |FINSI;
          |SIGUE;
      |FINSI;
  |FINSI;
  |SI enSwSelEmp = 1; |ACABA; |FINSI;
|| ------------------------------------------------------------------------
   nDModulo   = 0;
   nMixto     = 0;
   nDConta    = 0;
   nDPlani    = 0;
   enSwSelEmp = 1;
   |BUCLE LaActividad;
   |SI enSwSelEmp = 0;
       |HAZ LEmpresaPlan;
   |SINO;
       |SELECT #4#canempre;
       #canempre#2  = enEmpresa;
       #canempre#26 = nEjercicio;
       |LEE 001#canempre.=;
       |SI FSalida = 0;
           |HAZ LEmpresaConta;
       |SINO;
           |SI nMixto = 1;
               nMixto = 0;
               |HAZ LEmpresaPlan;
           |FINSI;
       |FINSI;
       |SELECT #1#canempre;
   |FINSI;
|FPRC;

|DEFBUCLE agifigen;
   #agifigen#1;
   ;
   nDEmpresa;
   nHEmpresa;
   e;
   NULL, LEmpresaEOS;
|FIN;

||///////////////////////////////////////////////////////////////////
|CALCULO Datos7; |TIPO 7;
 |MENSA "    <F3> Cambiar Emitir Resumen S/N";
 |ATRI R; |PINTA 1169, "Imp"; |ATRI 0;
|FCAL;

|CALCULO DatosManip; |TIPO 11;
   nInkey = FSalida;
   |BLIN 04;

   |SI nInkey = 11;
       |SI #999#16 = "S";
           #999#16 = "N";
       |SINO;
           #999#16 = "S";
       |FINSI;
       |GRABA 020#999a;
   |FINSI;
|FCAL;

|RUTINA inferior;
   #999#0 = 00001;
|FRUT;

|RUTINA superior;
   #999#0 = 99999;
|FRUT;

||///////////////////////////////////////////////////////////////////
|CALCULO Tipo3;  |TIPO 3;

    #0#0  = "N"; ||  No es Inmovilizado

    eaRenta = ""; enEjerR = 0;
    |SI #0#55 = "S";  eaRenta = "Cod.Renta"; enEjerR = #0#1; |FINSI;

    eaPlanilla = "dsmom060";  |HAZ PlanDePlanillas;

    enEjer = #0#1;
    |HAZ Verm060Plan;
    |SI enPlan60 = 0; |ACABA; |FINSI;

    |ABRE #dsmom162;
    |DDEFECTO #dsmom162;
    #dsmom162#0 = enPlan60;
    |LEE 041#dsmom162.=;
    |CIERRA #dsmom162;

    aAlfa1 = enEjer; |QBF aAlfa1;
    aAlfa1 = aAlfa1 % -102;
    nEjercicio = aAlfa1;
    |DBASS aPath; |QBT aPath;

    aFichero = ("6w" + Usuario) % 108; |NOME_DAT #901 aFichero; |DELINDEX #901;
    aFichero = ("7w" + Usuario) % 108; |NOME_DAT #917 aFichero; |DELINDEX #917;

    aFichero = ("8w" + Usuario) % 108; |NOME_DAT #999 aFichero;
    |DELINDEX #999;
    eaFEmpresa = aFichero;

    enSwNo     = 0;
    |ABRE #999;
    nGTempo = 0;   ||Busco Empresas Contables
    |ABRE #canempre;
    |SI #0#2 ! 0;
        nDEmpresa = #0#2;
        nHEmpresa = #0#3;
        |BUCLE agifigen;
    |SINO;
        |PARA nPara1 = 4;  |SI nPara1 [ 23;  |HACIENDO nPara1 = nPara1 + 1;
              |SI #0nPara1 ! 0;
                  nDEmpresa = #0nPara1;
                  nHEmpresa = #0nPara1;
                  |BUCLE agifigen;
              |FINSI;
        |SIGUE;
    |FINSI;
    |CIERRA #canempre;
    |CIERRA #999;

    |SI nGTempo = 0;    || No existe ninguna empresa
        |SI enSwMvo = 0; |MENAV "    Error. No existe ninguna Empresa dentro de la Seleccion "; |FINSI;
        |DELINDEX #999;
        |ACABA;
    |FINSI;

    |C_V #999#12,1,"N"; |C_P #999#16,1,1270;

    |SI enSwMvo = 0;
        |PUSHV 0509 2375;
        |ENTLINEAL #1#999, -1, 2, 22, 1, inferior, superior;
        |POPV;
    |FINSI;

    |HAZ Imprimir;

    |DELINDEX #901;
    |DELINDEX #917;
    |DELINDEX #999;
|FCAL;

|| *************************************************************************
|| *************************************************************************
|| *************************************************************************
|| ....................... Para las Complementarias
|PROCESO PonACero;
      nTotal_I = 0;
      nTotal_C = 0;
      nTotal_V = 0;
      nTotal_G = 0;

      nTanto10 = 100;
      nTanto11 = 100;
      nTanto12 = 100;
      nTanto13 = 100;

      nTotal_I_3  = 0;  nTotal_I_6  = 0;  nTotal_I_9  = 0;  nTotal_I_12 = 0;
      nTotal_C_3  = 0;  nTotal_C_6  = 0;  nTotal_C_9  = 0;  nTotal_C_12 = 0;
      nTotal_V_3  = 0;  nTotal_V_6  = 0;  nTotal_V_9  = 0;  nTotal_V_12 = 0;
      nTotal_G_3  = 0;  nTotal_G_6  = 0;  nTotal_G_9  = 0;  nTotal_G_12 = 0;

      nTanto10_3 = 100; nTanto10_6 = 100; nTanto10_9 = 100; nTanto10_12 = 100;
      nTanto11_3 = 100; nTanto11_6 = 100; nTanto11_9 = 100; nTanto11_12 = 100;
      nTanto12_3 = 100; nTanto12_6 = 100; nTanto12_9 = 100; nTanto12_12 = 100;
      nTanto13_3 = 100; nTanto13_6 = 100; nTanto13_9 = 100; nTanto13_12 = 100;

|FPRC;

|PROCESO PonValor;
 |SI nPara3 [ 3;
     nTotal_I = nTotal_I_3;
     nTotal_C = nTotal_C_3;
     nTotal_V = nTotal_V_3;
     nTotal_G = nTotal_G_3;

     nTanto10 = nTanto10_3; nTanto11 = nTanto11_3;
     nTanto12 = nTanto12_3; nTanto13 = nTanto13_3;
 |FINSI;
 |SI nPara3 > 3; |Y nPara3 [ 6;
     nTotal_I = nTotal_I_6;
     nTotal_C = nTotal_C_6;
     nTotal_V = nTotal_V_6;
     nTotal_G = nTotal_G_6;

     nTanto10 = nTanto10_6; nTanto11 = nTanto11_6;
     nTanto12 = nTanto12_6; nTanto13 = nTanto13_6;
 |FINSI;
 |SI nPara3 > 6; |Y nPara3 [ 9;
     nTotal_I = nTotal_I_9;
     nTotal_C = nTotal_C_9;
     nTotal_V = nTotal_V_9;
     nTotal_G = nTotal_G_9;

     nTanto10 = nTanto10_9; nTanto11 = nTanto11_9;
     nTanto12 = nTanto12_9; nTanto13 = nTanto13_9;
 |FINSI;
 |SI nPara3 > 9;
     nTotal_I = nTotal_I_12;
     nTotal_C = nTotal_C_12;
     nTotal_V = nTotal_V_12;
     nTotal_G = nTotal_G_12;

     nTanto10 = nTanto10_12; nTanto11 = nTanto11_12;
     nTanto12 = nTanto12_12; nTanto13 = nTanto13_12;
 |FINSI;
|FPRC;

|PROCESO CalculaTanto;
 |SI nTotal_I ! #9#17;
     |SI nTotal_I = 0; nTanto10 =  0; |FINSI;
     |SI #9#17    = 0; nTanto10 = -1; |FINSI;
     |SI #9#17 ! 0; |Y nTotal_I ! 0;
         nTanto10 =  ((nTotal_I * 100) / #9#17);
     |FINSI;
 |FINSI;

 |SI nTotal_C ! #9#20;
     |SI nTotal_C = 0; nTanto12 =  0; |FINSI;
     |SI #9#20    = 0; nTanto12 = -1; |FINSI;
     |SI #9#20 ! 0; |Y nTotal_C ! 0;
         nTanto12 =  ((nTotal_C * 100) / #9#20);
     |FINSI;
 |FINSI;

 |SI nTotal_V ! #9#58;
     |SI nTotal_V = 0; nTanto11 =  0; |FINSI;
     |SI #9#58    = 0; nTanto11 = -1; |FINSI;
     |SI #9#58 ! 0; |Y nTotal_V ! 0;
         nTanto11 =  ((nTotal_V * 100) / #9#58);
     |FINSI;
 |FINSI;

 |SI nTotal_G ! #9#23;
     |SI nTotal_G = 0; nTanto13 =  0; |FINSI;
     |SI #9#23    = 0; nTanto13 = -1; |FINSI;
     |SI #9#23 ! 0; |Y nTotal_G ! 0;
         nTanto13 =  ((nTotal_G * 100) / #9#23);
     |FINSI;
 |FINSI;

 |SI nPara3 [ 3;
     nTotal_I_3 = nTotal_I;
     nTotal_C_3 = nTotal_C;
     nTotal_V_3 = nTotal_V;
     nTotal_G_3 = nTotal_G;

     nTanto10_3 = nTanto10; nTanto11_3 = nTanto11;
     nTanto12_3 = nTanto12; nTanto13_3 = nTanto13;
 |FINSI;
 |SI nPara3 > 3; |Y nPara3 [ 6;
     nTotal_I_6 = nTotal_I;
     nTotal_C_6 = nTotal_C;
     nTotal_V_6 = nTotal_V;
     nTotal_G_6 = nTotal_G;

     nTanto10_6 = nTanto10; nTanto11_6 = nTanto11;
     nTanto12_6 = nTanto12; nTanto13_6 = nTanto13;
 |FINSI;
 |SI nPara3 > 6; |Y nPara3 [ 9;
     nTotal_I_9 = nTotal_I;
     nTotal_C_9 = nTotal_C;
     nTotal_V_9 = nTotal_V;
     nTotal_G_9 = nTotal_G;

     nTanto10_9 = nTanto10; nTanto11_9 = nTanto11;
     nTanto12_9 = nTanto12; nTanto13_9 = nTanto13;
 |FINSI;
 |SI nPara3 > 9;
     nTotal_I_12 = nTotal_I;
     nTotal_C_12 = nTotal_C;
     nTotal_V_12 = nTotal_V;
     nTotal_G_12 = nTotal_G;

     nTanto10_12 = nTanto10; nTanto11_12 = nTanto11;
     nTanto12_12 = nTanto12; nTanto13_12 = nTanto13;
 |FINSI;
|FPRC;

|PROCESO Comprobar130;
 nTotal_I = #130#6;   ||Ingresos
 nTotal_C = #130#9;   ||Consumo
 nTotal_V = #130#46;  ||Variacion
 nTotal_G = #130#12;  ||Gastos
 nComplem = #130#4;
|FPRC;

|DEFBUCLE Comprobar130;
 #130#1;
 ;
 #9#0, #9#1, #9#2, 130, 00, #9#4;
 #9#0, #9#1, #9#2, 130, 99, #9#4;
 ;
 NULL, Comprobar130;
|FIN;

|PROCESO EspecialPlanillas;

     |ABRE #9;
     |PARA nPara3 = #0#49; |SI nPara3 [ #0#50; |HACIENDO nPara3 = nPara3 + 1;
           nComplem = -1;
           nTanto10 = 100; nTotal_I = 0;
           nTanto11 = 100; nTotal_C = 0;
           nTanto12 = 100; nTotal_V = 0;
           nTanto13 = 100; nTotal_G = 0;
           #9#0 = enEmpresa;
           #9#1 = #0#1;
           #9#2 = nPara3;
           #9#3 = 0;
           #9#4 = enActividad;
           |LEE 040#9=;
           |SI FSalida = 0;
               nComplem = #9#3;
               |SI #9#14 = "X";  |BUCLE Comprobar130;  |FINSI;
               |SI nComplem ! #9#3;
                   |HAZ CalculaTanto;
               |FINSI;
           |FINSI;
     |SIGUE;
     |CIERRA #9;
|FPRC;

|| **********************************************************************************
|| ************************************************************************
|| ------------------------------------------------------------------------

|PROCESO LeeAux0;
 |SI #901#3 ! 0; |O #901#4 ! 0; |O #901#5 ! 0; |O #901#6 ! 0;
     sw = 1;
     |SI #901#8 ! 1;
         |SI #901#1 = "I";
             #900#25 = #900#25 + #901#3;
             #900#26 = #900#26 + #901#4;
             #900#27 = #900#27 + #901#5;
             #900#28 = #900#28 + #901#6;
             #900#29 = #900#29 + #901#7;
         |SINO;
             #900#30 = #900#30 + #901#3;
             #900#31 = #900#31 + #901#4;
             #900#32 = #900#32 + #901#5;
             #900#33 = #900#33 + #901#6;
             #900#34 = #900#34 + #901#7;
         |FINSI;
     |FINSI;
 |FINSI;
|FPRC;

|DEFBUCLE LeeAux;
  #901#1;
  ;
  #0#0, aTipIG, 01;
  #0#0, aTipIG, 99;
  ;
  NULL, LeeAux0;
|FIN;

|| //////////////////////////////////////////////////////////////////
|PROCESO ImpAuxiliarR;    || ... Imprimir el balance resumido (130)

 |PDEFECTO #900,25,50;
 |SI SwRegEspec ! 0;
     || ... Modulos o Locales/Agricultura ya viene calculado;
     #900#49 = enRendimiento;

 |SINO;
     sw      = 0;
     aTipIG = "I";
     |BUCLE LeeAux;

     sw      = 0;
     aTipIG  = "G";
     |BUCLE LeeAux;

     || ... Gastos de Dificil Justificacion
     |SI nTTribut = 4; |O nTTribut = 9; |O nTTribut = 14;
         #900#35 = #900#25 - #900#30;
         |SI #900#35 > 0; #900#35 = (#900#35 % 5) ! EURODBMOD; |SINO; #900#35 = 0; |FINSI;

         #900#36 = #900#26 - #900#31;
         |SI #900#36 > 0; #900#36 = (#900#36 % 5) ! EURODBMOD; |SINO; #900#36 = 0; |FINSI;

         #900#37 = #900#27 - #900#32;
         |SI #900#37 > 0; #900#37 = (#900#37 % 5) ! EURODBMOD; |SINO; #900#37 = 0; |FINSI;

         #900#38 = #900#28 - #900#33;
         |SI #900#38 > 0; #900#38 = (#900#38 % 5) ! EURODBMOD; |SINO; #900#38 = 0; |FINSI;

         #900#39 = #900#29 - #900#34;
         |SI #900#39 > 0; #900#39 = (#900#39 % 5) ! EURODBMOD; |SINO; #900#39 = 0; |FINSI;
     |FINSI;

     || ... Gastos Totales
         #900#40 = #900#30 + #900#35;
         #900#41 = #900#31 + #900#36;
         #900#42 = #900#32 + #900#37;
         #900#43 = #900#33 + #900#38;
         #900#44 = #900#34 + #900#39;

        || ... Rendimiento Neto
         #900#45 = #900#25 - #900#40;
         #900#46 = #900#26 - #900#41;
         #900#47 = #900#27 - #900#42;
         #900#48 = #900#28 - #900#43;
         #900#49 = #900#29 - #900#44;
 |FINSI;

 |PRINT;
 SwPrint = 1;
|FPRC;

|| /////////////////////////////////////////////////////////////////
|PROCESO LeeDsmom017;
    nComplem = #17#2;
|FPRC;

|DEFBUCLE LeeDsmom017;
    #17#1;
    ;
    enEmpresa, enEjer, 00, enActividad;
    enEmpresa, enEjer, 99, enActividad;
    e;
    NULL, LeeDsmom017;
|FIN;

|PROCESO LeeModulos;
    enRendimiento = 0;
    nComplem     = -1;
    |BUCLE LeeDsmom017;
    |SI nComplem ! -1;

        |ABRE #18;
        #18#0 = enEmpresa;
        #18#1 = enEjer;
        #18#2 = nComplem;
        #18#3 = enActividad;
        |LEE 101#18=;
        |SI FSalida = 0;
            enRendimiento = #18#123;
            SwHayDatos   = 1;
            SwRegEspec   = 1;
        |FINSI;
        |CIERRA #18;
   |FINSI;
|FPRC;
|| ***********************************************************************

|PROCESO RegEspecial;
   SwRegEspec    = 1;
   enAuto        = 1;
   enRendimiento = 0;
   |SI swExisL = 1;
       |SUB_EJECUTA "dsmoy026;L";
   |FINSI;
   |SI swExisA = 1;
        |SUB_EJECUTA "dsmoy026;A";
   |FINSI;
   |SI enRendimiento ! 0;
       SwHayDatos = 1;
   |FINSI;
|FPRC;
|| ********************************************************
|| ... ... ... ... ... ... ... ... ... Planillas
|PROCESO PonExistencias;
     |SI nTanto11 ! 100; |Y nTanto11 ! -1;
         #11#8 = (#11#8 % nTanto11) ! EURODBMOD;
         #11#9 = (#11#9 % nTanto11) ! EURODBMOD;
     |SINO;
         |SI nTanto11 = -1;
             #11#8 = nTotal_V;
             #11#9 = 0;
             nTotal_V = 0;
             |SI #11#2 [ 3;               nTotal_V_3  = 0; |FINSI;
             |SI #11#2 > 3; |Y #11#2 [ 6; nTotal_V_6  = 0; |FINSI;
             |SI #11#2 > 6; |Y #11#2 [ 9; nTotal_V_9  = 0; |FINSI;
             |SI #11#2 > 9;               nTotal_V_12 = 0; |FINSI;
         |FINSI;
     |FINSI;
     nSaldo = nSaldo + (#11#8 - #11#9);
     nEnExi = 1;
|FPRC;

|DEFBUCLE dsmom011;
     #11#2;
     ;
     #12#0, #12#1, #12#2, #12#3, #12#4, #12#5, "    ";
     #12#0, #12#1, #12#2, #12#3, #12#4, #12#5, "zzzz";
     ;
     NULL, PonExistencias;
|FIN;

|PROCESO SumaSP13;
 |SI enEjer ] 2019;
     aAlfa = (#13#5 % 101);
     |SI aAlfa = "6";
         |SI #dsmom061#5 = "-"; #13#7 = "-"; |FINSI;
     |FINSI;
     |SI aAlfa = "7";
         |SI #dsmom061#5 = "+"; #13#7 = "-"; |FINSI;
     |FINSI;
 |FINSI;

 |SI #0#51 = "S"
     |SI #13#2 < #0#49; |O #13#2 > #0#50;
         |ACABA;
     |FINSI;
 |FINSI;

 SwHayDatos = 1;
 |SI nPara3 ! #13#2;
     nPara3 = #13#2;
     |HAZ PonValor;
 |FINSI;

 nNume1 = 1;
 |SI #13#7 = "-"; nNume1 = -1; |FINSI;
 #13#8 = #13#8 * nNume1;

 |SI nTanto13 ! 100; |Y nTanto13 ! -1;
      #13#8 = (#13#8 % nTanto13) ! EURODBMOD;
 |SINO;
      |SI nTanto13 = -1;
          #13#8 = nTotal_G;
          nTotal_G = 0;
          |SI #13#2 [ 3;               nTotal_G_3  = 0; |FINSI;
          |SI #13#2 > 3; |Y #13#2 [ 6; nTotal_G_6  = 0; |FINSI;
          |SI #13#2 > 6; |Y #13#2 [ 9; nTotal_G_9  = 0; |FINSI;
          |SI #13#2 > 9;               nTotal_G_12 = 0; |FINSI;
      |FINSI;
 |FINSI;

 |SI #13#2 [ 3;                nSald1 = nSald1 + #13#8; |FINSI;
 |SI #13#2 ] 4; |Y #13#2 [ 6;  nSald2 = nSald2 + #13#8; |FINSI;
 |SI #13#2 ] 7; |Y #13#2 [ 9;  nSald3 = nSald3 + #13#8; |FINSI;
 |SI #13#2 ] 10;               nSald4 = nSald4 + #13#8; |FINSI;
 nSald5 = nSald5 + #13#8;
|FPRC;

|DEFBUCLE dsmom013;
 #13#1;
 ;
 enEmpresa, #0#1, nDPeriodo, nComplem, enActividad, aDCuenta;
 enEmpresa, #0#1, nHPeriodo, nComplem, enActividad, aHCuenta;
 e;
 NULL, SumaSP13;
|FIN;


|PROCESO SumaSP13desg;
  |PARA nPara4 = 0; |SI nPara4 [ 8; |HACIENDO nPara4 = nPara4 + 1;
        #dsmom013#nPara4# = #dsmopl13#nPara4#;
  |SIGUE;
  |HAZ SumaSP13;
|FPRC;

|DEFBUCLE dsmopl13;
 #dsmopl13#1;
 ;
 enEmpresa, #0#1, nDPeriodo, nComplem, enActividad, aDCuenta;
 enEmpresa, #0#1, nHPeriodo, nComplem, enActividad, aHCuenta;
 e;
 NULL, SumaSP13desg;
|FIN;

|PROCESO SumaSP12;
 |SI enEjer ] 2019;
     aAlfa = (#12#5 % 101);
     |SI aAlfa = "6";
         |SI #dsmom061#5 = "-"; #12#7 = "-"; |FINSI;
     |FINSI;
     |SI aAlfa = "7";
         |SI #dsmom061#5 = "+"; #12#7 = "-"; |FINSI;
     |FINSI;
 |FINSI;

 |SI #0#51 = "S"
     |SI #12#2 < #0#49; |O #12#2 > #0#50;
         |ACABA;
     |FINSI;
 |FINSI;

 SwHayDatos = 1;
 |SI nPara3 ! #12#2;
     nPara3 = #12#2;
     |HAZ PonValor;
 |FINSI;

 nEnExi = 0;
 nSaldo = 0;
 |BUCLE dsmom011;
 |SI nEnExi = 0;
      |SI nTanto12 ! 100; |Y nTanto12 ! -1;
          #12#8 = (#12#8 % nTanto12) ! EURODBMOD;
      |SINO;
          |SI nTanto12 = -1;
              #12#8 = nTotal_C;
              nTotal_C = 0;
              |SI #12#2 [ 3;               nTotal_C_3  = 0; |FINSI;
              |SI #12#2 > 3; |Y #12#2 [ 6; nTotal_C_6  = 0; |FINSI;
              |SI #12#2 > 6; |Y #12#2 [ 9; nTotal_C_9  = 0; |FINSI;
              |SI #12#2 > 9;               nTotal_C_12 = 0; |FINSI;
          |FINSI;
      |FINSI;
      nSaldo = #12#8;
 |SINO;
      |SI #0#54 = "N"; nSaldo = 0; |FINSI;  ||no tiene en cuenta existencias
 |FINSI;

 nNume1 = 1; |SI #12#7 = "-"; nNume1 = -1; |FINSI;
 nSaldo = nSaldo * nNume1;

 |SI #12#2 [ 3;                nSald1 = nSald1 + nSaldo; |FINSI;
 |SI #12#2 ] 4; |Y #12#2 [ 6;  nSald2 = nSald2 + nSaldo; |FINSI;
 |SI #12#2 ] 7; |Y #12#2 [ 9;  nSald3 = nSald3 + nSaldo; |FINSI;
 |SI #12#2 ] 10;               nSald4 = nSald4 + nSaldo; |FINSI;

 |SI nEnExi = 0;
     nSald5 = nSald5 + nSaldo;
 |FINSI;
|FPRC;

|DEFBUCLE dsmom012;
 #12#1;
 ;
 enEmpresa, #0#1, nDPeriodo, nComplem, enActividad, aDCuenta;
 enEmpresa, #0#1, nHPeriodo, nComplem, enActividad, aHCuenta;
 e;
 NULL, SumaSP12;
|FIN;


|PROCESO SumaSP12desg;
  |PARA nPara4 = 0; |SI nPara4 [ 8; |HACIENDO nPara4 = nPara4 + 1;
        #dsmom012#nPara4# = #dsmopl12#nPara4#;
  |SIGUE;
  |HAZ SumaSP12;
|FPRC;

|DEFBUCLE dsmopl12;
 #dsmopl12#1;
 ;
 enEmpresa, #0#1, nDPeriodo, nComplem, enActividad, aDCuenta;
 enEmpresa, #0#1, nHPeriodo, nComplem, enActividad, aHCuenta;
 e;
 NULL, SumaSP12desg;
|FIN;

|PROCESO SumaSP10;
 |SI enEjer ] 2019;
     aAlfa = (#10#5 % 101);
     |SI aAlfa = "6";
         |SI #dsmom061#5 = "-"; #10#7 = "-"; |FINSI;
     |FINSI;
     |SI aAlfa = "7";
         |SI #dsmom061#5 = "+"; #10#7 = "-"; |FINSI;
     |FINSI;
 |FINSI;

 |SI #0#51 = "S"
     |SI #10#2 < #0#49; |O #10#2 > #0#50;
         |ACABA;
     |FINSI;
 |FINSI;
 SwHayDatos = 1;

 |SI nPara3 ! #10#2;
     nPara3 = #10#2;
     |HAZ PonValor;
 |FINSI;

 nNume1 = 1;
 |SI #10#7 = "-"; nNume1 = -1; |FINSI;
 #10#8 = #10#8 * nNume1;

 |SI nTanto10 ! 100; |Y nTanto10 ! -1;
     #10#8 = (#10#8 % nTanto10) ! EURODBMOD;
 |SINO;
     |SI nTanto10 = -1;
         #10#8 = nTotal_I;
         nTotal_I = 0;
         |SI #10#2 [ 3;               nTotal_I_3  = 0; |FINSI;
         |SI #10#2 > 3; |Y #10#2 [ 6; nTotal_I_6  = 0; |FINSI;
         |SI #10#2 > 6; |Y #10#2 [ 9; nTotal_I_9  = 0; |FINSI;
         |SI #10#2 > 9;               nTotal_I_12 = 0; |FINSI;
     |FINSI;
 |FINSI;

 |SI #10#2 [ 3;                nSald1 = nSald1 + #10#8; |FINSI;
 |SI #10#2 ] 4; |Y #10#2 [ 6;  nSald2 = nSald2 + #10#8; |FINSI;
 |SI #10#2 ] 7; |Y #10#2 [ 9;  nSald3 = nSald3 + #10#8; |FINSI;
 |SI #10#2 ] 10;               nSald4 = nSald4 + #10#8; |FINSI;
 nSald5 = nSald5 + #10#8;
|FPRC;

|DEFBUCLE dsmom010;
 #10#1;
 ;
 enEmpresa, #0#1, nDPeriodo, nComplem, enActividad, aDCuenta;
 enEmpresa, #0#1, nHPeriodo, nComplem, enActividad, aHCuenta;
 e;
 NULL, SumaSP10;
|FIN;

|PROCESO SumaSP10desg;
  |PARA nPara4 = 0; |SI nPara4 [ 8; |HACIENDO nPara4 = nPara4 + 1;
        #dsmom010#nPara4# = #dsmopl10#nPara4#;
  |SIGUE;
  |HAZ SumaSP10;
|FPRC;

|DEFBUCLE dsmopl10;
 #dsmopl10#1;
 ;
 enEmpresa, #0#1, nDPeriodo, nComplem, enActividad, aDCuenta;
 enEmpresa, #0#1, nHPeriodo, nComplem, enActividad, aHCuenta;
 e;
 NULL, SumaSP10desg;
|FIN;

|| /////////////////////////////////////////////////////////////////

|| ... ... ... ... ... ... Cuentas Contables
|PROCESO LasExistencias;
  SwHayDatos = 1;
  nEnExi = 1;
  |SI nSwPlan = 0;
      nSald1 = nSald1 + #917#4;
      nSald2 = nSald2 + #917#5;
      nSald3 = nSald3 + #917#6;
      nSald4 = nSald4 + #917#7;
  |FINSI;
  nSald5 = nSald5 + #917#16;
|FPRC;

|DEFBUCLE ElAuxW17;
 #917#1;
 ;
 enEmpresa, enEjer, enActividad, aDCuenta;
 enEmpresa, enEjer, enActividad, aHCuenta;
 e;
 NULL, LasExistencias;
|FIN;

|| .......................
|PROCESO SumaSaldos;

 SwHayDatos = 1;
 |PARA nPara2 = 9; |SI nPara2 [ 53; |HACIENDO nPara2 = nPara2 + 1;
       #305nPara2  = (#305nPara2  / enConversor) ! EURODBMOD;
 |SIGUE;

 #917#0 = enEmpresa;
 #917#1 = enEjer;
 #917#2 = enActividad;
 #917#3 = #305#0;
 |LEE 000#917=;
 |SI FSalida ! 0;        || No existe en Existencias

     nSalPar5 = #305#14 + #305#17 + #305#20;
     nSalPar5 = nSalPar5 + #305#23 + #305#26 + #305#29;
     nSalPar5 = nSalPar5 + #305#32 + #305#35 + #305#38;
     nSalPar5 = nSalPar5 + #305#41 + #305#44 + #305#47;

     nSald1 = nSald1 + (#305#14 + #305#17 + #305#20);
     nSald2 = nSald2 + (#305#23 + #305#26 + #305#29);
     nSald3 = nSald3 + (#305#32 + #305#35 + #305#38);
     nSald4 = nSald4 + (#305#41 + #305#44 + #305#47);
     nSald5 = nSald5 + nSalPar5;

 |FINSI;
|FPRC;

|DEFBUCLE LeeCuentas;
 #305#2;
 3;
 aElDepar, aDCuenta, "S";
 aElDepar, aHCuenta, "S";
 e;
 NULL, SumaSaldos;
|FIN;

|PROCESO MiraSiSuma;
  enClAgrupa  = 0;
  eaClave60   = "   ";
  eaTip60     = #dsmom061#1;
  #dsmom060#6 = #dsmom061#6;
  #dsmom060#0 = #dsmom061#0;
  #dsmom060#1 = #dsmom061#1;
  #dsmom060#2 = #dsmom061#2;
  |LEE 041#dsmom060.=;
  |SI FSalida = 0;
      eaClave60 = #dsmom060#7;
      |HAZ eClaveAgrupa;
   |FINSI;
|FPRC;

|PROCESO dsmom061_0;
 nEnExi = 0;
 #901#0 = #61#0;
 #901#1 = #61#1;
 #901#2 = #61#2;
 |LEE 001#901=;
 |SI FSalida ! 0;
     |DDEFECTO #901;
     #901#0 = #61#0;
     #901#1 = #61#1;
     #901#2 = #61#2;
     |HAZ MiraSiSuma;
     #901#8 = enClAgrupa;
     |GRABA 020#901b;
 |FINSI;
 aAlfa1 = #61#4; |QBF aAlfa1;              aDCuenta = aAlfa1;
 aAlfa1 = (aAlfa1 + "zzzzzzzzzzzz") % 112; aHCuenta = aAlfa1;

 nSald1 = 0;  nSald2 = 0;
 nSald3 = 0;  nSald4 = 0;
 nSald5 = 0;

 |SI nQueBusco = 0;

     |ABRE #917;
     |BUCLE LeeCuentas;
     |CIERRA #917;
     nSwPlan = 0; |BUCLE ElAuxW17;

 |SINO;
     nComplem = 0;
     aAlfa = aDCuenta % 0; nNume1 = aAlfa;
     |SI nNume1 [ 4; |O enEjer < 2019;
         aDCuenta = aDCuenta % 104;
         aHCuenta = aHCuenta % 104;
         nComplem = 0;
         nPara3  = -1; |BUCLE dsmom010;
         nPara3  = -1; |BUCLE dsmom012;
         nPara3  = -1; |BUCLE dsmom013;
     |SINO;
         nPara3  = -1; |BUCLE dsmopl10;
         nPara3  = -1; |BUCLE dsmopl12;
         nPara3  = -1; |BUCLE dsmopl13;
     |FINSI;
     nSwPlan = 1; |BUCLE ElAuxW17;
 |FINSI;

 |SI #61#5 = "-"; |Y nQueBusco = 0;
     nSald1 = - nSald1; nSald2 = - nSald2;
     nSald3 = - nSald3; nSald4 = - nSald4;
     nSald5 = - nSald5;
 |FINSI;

 |SI #dsmom061#7 = "P";
     |SI nSald1 < 0; nSald1 = 0; |FINSI;
     |SI nSald2 < 0; nSald2 = 0; |FINSI;
     |SI nSald3 < 0; nSald3 = 0; |FINSI;
     |SI nSald4 < 0; nSald4 = 0; |FINSI;
     |SI nSald5 < 0; nSald5 = 0; |FINSI;
 |FINSI;
 |SI #dsmom061#7 = "N";
     |SI nSald1 > 0; nSald1 = 0; |FINSI;
     |SI nSald2 > 0; nSald2 = 0; |FINSI;
     |SI nSald3 > 0; nSald3 = 0; |FINSI;
     |SI nSald4 > 0; nSald4 = 0; |FINSI;
     |SI nSald5 > 0; nSald5 = 0; |FINSI;
 |FINSI;

 #901#3 = #901#3 + nSald1;
 #901#4 = #901#4 + nSald2;
 #901#5 = #901#5 + nSald3;
 #901#6 = #901#6 + nSald4;
 #901#7 = #901#7 + nSald5;

 |GRABA 020#901a;
 |LIBERA #901;
|FPRC;

|DEFBUCLE dsmom061;
 #61#1;
 ;
 enPlan60, #0#0, " ", 00, 001;
 enPlan60, #0#0, "z", 99, 999;
 ;
 NULL, dsmom061_0;
|FIN;

||////////////////////////////////////////////////////////////////////////
|| ... Existencias Contables
|PROCESO GrabaVariacion;
 #917#0 = enEmpresa;
 #917#1 = enEjer;
 #917#2 = #303#0;
 #917#3 = eaCuenta;
 |LEE 000#917=;
 |SI FSalida ! 0;
     |DDEFECTO #917;
     #917#0 = enEmpresa;
     #917#1 = enEjer;
     #917#2 = #303#0;
     #917#3 = eaCuenta;
     |GRABA 020#917c;
 |FINSI;
 #917#4 = #917#4 + nImpVar1;
 #917#5 = #917#5 + nImpVar2;
 #917#6 = #917#6 + nImpVar3;
 #917#7 = #917#7 + nImpVar4;
 #917#16 = #917#16 + nImpVar5;
 |GRABA 020#917a;
|FPRC;

|PROCESO LasExisConta;

 |PARA nPara2 = 11; |SI nPara2 [ 24; |HACIENDO nPara2 = nPara2 + 1;
       #303nPara2  = (#303nPara2  / enConversor) ! EURODBMOD;
       nNume3 = nPara2 - 11;
       |SI nNume3 < #0#52; |O nNume3 > #0#53;
           #303nPara2 = 0;
       |FINSI;
 |SIGUE;


 nImpVar1 = #303#11 - #303#14;
 nImpVar2 = 0;
 nImpVar3 = 0;
 nImpVar4 = 0;
 nImpVar5 = 0;
 |SI nImpVar1 ! 0;
     |SI nImpVar1 > 0;
         eaCuenta = #303#5;
     |SINO;
         eaCuenta = #303#8;
     |FINSI;
     |HAZ GrabaVariacion;
 |FINSI;

 nImpVar1 = 0;
 nImpVar2 = #303#14 - #303#17;
 nImpVar3 = 0;
 nImpVar4 = 0;
 nImpVar5 = 0;
 |SI nImpVar2 ! 0;
     |SI nImpVar2 > 0;
         eaCuenta = #303#5;
     |SINO;
         eaCuenta = #303#8;
     |FINSI;
     |HAZ GrabaVariacion;
 |FINSI;

 nImpVar1 = 0;
 nImpVar2 = 0;
 nImpVar3 = #303#17 - #303#20;
 nImpVar4 = 0;
 nImpVar5 = 0;
 |SI nImpVar3 ! 0;
     |SI nImpVar3 > 0;
         eaCuenta = #303#5;
     |SINO;
         eaCuenta = #303#8;
     |FINSI;
     |HAZ GrabaVariacion;
 |FINSI;

 nImpVar1 = 0;
 nImpVar2 = 0;
 nImpVar3 = 0;
 nImpVar4 = #303#20 - #303#23;
 nImpVar5 = 0;
 |SI nImpVar4 ! 0;
     |SI nImpVar4 > 0;
         eaCuenta = #303#5;
     |SINO;
         eaCuenta = #303#8;
     |FINSI;
     |HAZ GrabaVariacion;
 |FINSI;

 nImpVar1 = 0;
 nImpVar2 = 0;
 nImpVar3 = 0;
 nImpVar4 = 0;
     nNume1 = #0#52 + 11;  || mes iniciales
     nNume2 = #0#53 + 11;  || mes finales
     nImpVar5 = #303nNume1 - #303nNume2;

 |SI nImpVar5 ! 0;
     |SI nImpVar5 > 0;
         eaCuenta = #303#5;
     |SINO;
         eaCuenta = #303#8;
     |FINSI;
     |HAZ GrabaVariacion;
 |FINSI;

|FPRC;

|DEFBUCLE CaExistL;
 #303#1;
 ;
 #0#38, 01;
 #0#39, 99;
 e;
 NULL, LasExisConta;
|FIN;

|PROCESO LasExisPlanilla;
 |SI #0#52 [ 3;               nNume1 = 3;  |FINSI;
 |SI #0#52 > 3; |Y #0#52 [ 6; nNume1 = 6;  |FINSI;
 |SI #0#52 > 6; |Y #0#52 [ 9; nNume1 = 9;  |FINSI;
 |SI #0#52 > 9;               nNume1 = 12; |FINSI;

 |SI #0#53 [ 3;               nNume2 = 3;  |FINSI;
 |SI #0#53 > 3; |Y #0#53 [ 6; nNume2 = 6;  |FINSI;
 |SI #0#53 > 6; |Y #0#53 [ 9; nNume2 = 9;  |FINSI;
 |SI #0#53 > 9;               nNume2 = 12; |FINSI;

 |SI #11#2 = nNume1; |O #11#2 = nNume2;
     eaCuenta = #11#10;
     nSaldo   = 0;
     |SI #11#2 = nNume1; nSaldo = nSaldo + #11#8; |FINSI;
     |SI #11#2 = nNume2; nSaldo = nSaldo - #11#9; |FINSI;

     nImpVar1 = 0;
     nImpVar2 = 0;
     nImpVar3 = 0;
     nImpVar4 = 0;
     nImpVar5 = nSaldo;
     |SI nImpVar5 ! 0;
         #303#0 = #11#4;
         |HAZ GrabaVariacion;
     |FINSI;
 |FINSI;
|FPRC;

|DEFBUCLE PlExistL;
     #11#1;
     ;
     enEmpresa, #0#1, 00, 00, 00, "    ";
     enEmpresa, #0#1, 99, 00, 99, "zzzz";
     ;
     NULL, LasExisPlanilla;
|FIN;
|| ....................................
|PROCESO Emision;

   aAlfa1 = #999#15; |QBF aAlfa1;
   nQueBusco = 0;
   |SI aAlfa1 = "PLANILLAS"; |O aAlfa1 = "MODULOS";
       nQueBusco = 1;
   |SINO;
       aAlfa2 = aAlfa1 - ".Mixto"
       |SI aAlfa2 ! aAlfa1;
           nQueBusco = 2;
           #999#15 = aAlfa2;
       |FINSI;
   |FINSI;

   enSwPartido = 0;
   enEmpresa  = #999#1;
   enPerConta = #999#2;
   eaLaMonEmp = #999#13;

   |SI nQueBusco = 1;   ||Por planillas
       enConversor = 1; ||la misma moneda
   |SINO;
       |HAZ EnConversor;
   |FINSI;
   enEjer     = #999#3;   || A¤o
   dig1       = #999#4;   || Desde Mes
   dig3       = #999#5;   || N§ Digitos
   dig4       = #999#6;   || Digitos Nivel 1
   dig5       = #999#7;   || Digitos Nivel 2
   dig6       = #999#8;   || Digitos Nivel 3
   dig7       = #999#9;   || Digitos Nivel 4
   dig8       = #999#10;  || Digitos Nivel 5
   dig9       = #999#11;  || Digitos Nivel 6
   eaEstadoEmp = #999#12; || Estado
   eaNombreEmp = #999#14; || Nombre Empresa
   |SI #999#4 ! 1;
        enSwOpPar   = 0;
        |HAZ SituaEmpresa;
        enPerConta = enPeriodoPar;
        #999#15 = aPathConta;
   |FINSI;
   aPathConta  = #999#15; |QBF aPathConta; || Path

   || Recoger informacion sobre los niveles ...
   |SI dig3 = 1; MaximoDigitos = dig4; |FINSI;
   |SI dig3 = 2; MaximoDigitos = dig5; |FINSI;
   |SI dig3 = 3; MaximoDigitos = dig6; |FINSI;
   |SI dig3 = 4; MaximoDigitos = dig7; |FINSI;
   |SI dig3 = 5; MaximoDigitos = dig8; |FINSI;
   |SI dig3 = 6; MaximoDigitos = dig9; |FINSI;

   eaTituloL = #0#45; ||Titulo del Libro

   dirfich0 = "";
   |SI nQueBusco ! 1;

       eaDDepar  = #0#38; |QBF eaDDepar; eaDDepar  = ("00" + eaDDepar) % -102;
       eaHDepar  = #0#39; |QBF eaHDepar; eaHDepar  = ("00" + eaHDepar) % -102;

       |PATH_DAT #304 aPathConta;
       dirfich0 = aPathConta;
       eSwSelec  = 2;
       |ATRI I;
       |PINTA 2302, "Procesando Cuentas por Departamentos ... Espere ";
       eaDepar = "S";
       |ATRI 0;
       |SUB_EJECUTA_NP ":contagen/cacuedep";
       |PINTA 2302, "                                                ";
       |NOME_DAT #305 eaNomFDep;

       |PATH_DAT #303 aPathConta;
       |PATH_DAT #305 aPathConta;

       |SI #0#54 = "S";
           |ABRE #917;
           |BUCLE CaExistL;
           |CIERRA #917;
       |FINSI;
    |SINO;

       |SI #0#54 = "S";
           |ABRE #917;
           |BUCLE PlExistL;
           |CIERRA #917;
       |FINSI;

    |FINSI;

    informe = "dsmoy084";
    |INFOR informe;
    |SI FSalida ! 0;  |ACABA;  |FINSI;

    enActividad = 0;
    |HAZ LeeDatosEmpresa;

    SwPrint = 0;
    nActividad = 0;
    |PARA nPara1 = #0#38; |SI nPara1 [ #0#39; |HACIENDO nPara1 = nPara1 + 1;

          enActividad = nPara1;
          |HAZ LeeDatosActividad;
          |SI  swNoExis = 0;       ||SI existe esta Actividad.
              |SI nActividad = 0; nActividad = enActividad; |FINSI;

              nQueBusco = 0;
              |SI aQueBusco = "PLANILLAS";  nQueBusco = 1; |FINSI;
              |SI aQueBusco = "MODULOS";    nQueBusco = 2; |FINSI;

              aElDepar = enActividad; |QBF aElDepar;
              aElDepar = ("00" + aElDepar) % -102;

              |HAZ PonACero;

              enRendimiento = 0;
              SwHayDatos = 0;
              SwRegEspec = 0;

              |SI nQueBusco = 0; |Y dirfich0 = "";
                  || ... no existe contabilidad

              |SINO;

                  |SI nQueBusco = 2;

                      |HAZ LeeModulos;

                  |SINO;

                      |SI nQueBusco = 1;
                          |HAZ EspecialPlanillas;
                      |FINSI;
                      |SI swExisL = 1; |O swExisA = 1;
                          |SI nQueBusco = 0;
                              |HAZ RegEspecial;
                          |FINSI;
                      |FINSI;
                      |SI SwRegEspec = 0;
                          nExiFin = 0;
                          |ABRE #dsmom060;
                          |ABRE #901;
                          |BUCLE dsmom061;
                          |CIERRA #901;
                          |CIERRA #dsmom060;
                      |FINSI;
                  |FINSI;
              |FINSI;

              |SI SwHayDatos = 1;
                  |HAZ ImpAuxiliarR;
              |FINSI;
         |FINSI;
   |SIGUE;
   |SI SwPrint = 1;
       |PIEINF;
   |FINSI;
   |FININF;
   |SI enSwPartido = 1;
       enSwOpPar   = 1;
       |HAZ FinalPartido;
   |FINSI;
|FPRC;

|DEFBUCLE dsmow043;
  #dsmow043#1;
  16;
  000001, "S";
  999999, "S";
  ;
  NULL, Emision;
|FIN;

|PROCESO Imprimir;

    efDFecha  = #0#47;
    efHFecha  = #0#48;
    nDPeriodo = #0#49;
    nHPeriodo = #0#50;

    aAlfa1 = enEjer;
    aAlfa2 = "01.01." + aAlfa1; fFechaIni = aAlfa2;
    aAlfa3 = "31.12." + aAlfa1; fFechaFin = aAlfa3;

    |SI enSwMvo = 0;
        |IMPRE 0;
        |SI FSalida ! 0;  |ACABA;  |FINSI;
    |FINSI;

    |BUCLE dsmow043;

    |SI enSwMvo = 0;
        |FINIMP;
    |FINSI;
|FPRC;

|PROCESO LeeDatosEmpresa;
  |DELINDEX #901;

  |DDEFECTO #900;
  #900#0    = enEmpresa;
  eaTituloL = #0#45; ||Titulo del Libro
  |HAZ DatosEmpr_w32Con;
|FPRC;

|PROCESO LeeDatosActividad;
  |DELINDEX #901;
    #900#52 = 0;
  || Lee Actividad en concreto.

    |ABRE #agicen14;
    swNoExis = 0;
    #agicen14#0 = #900#0; || Empresa
    #agicen14#1 = enActividad;
    |LEE 041#agicen14.=;
    |SI FSalida = 0;

        #900#15 = #agicen14#1;   ||Act.
        #900#16 = #agicen14#2;   ||Tipo Epig.
        #900#17 = #agicen14#3;   ||Epigrafe
        #900#18 = #agicen14#4;   ||Descripcion Actividad
        #900#19 = #agicen14#5;   ||Fecha Inicio
        #900#20 = #agicen14#6;   ||Fecha Fin
        #900#21 = #agicen14#11;  ||Sector actividad
        #900#22 = #agicen14#12;  ||Sector actividad
        #900#23 = #agicen14#9;   ||Tipo Tributacion .. Actividad
        #900#24 = #agicen14#10;  ||    Descripcion Tipo Actividad
        #900#51 = #agicen14#20;  ||CNAE
        enCodRen = #agicen14#18;  ||Codigo Renta
        |SI enEjerR ! 0; |HAZ CodigoRen; |FINSI;
        #900#52 = enCodRen;       ||Codigo Renta

        |SI #agicen14#6 > "01.01.0000";
            |SI #0#24 = "N";
                swNoExis = 1;
            |FINSI;
        |FINSI;
    |SINO;
        swNoExis = 1;
    |FINSI;
    |CIERRA #agicen14;

    |SI swNoExis = 0;
        eaCodRenta = "";
        nOpTitular = 0;
        |SI #0#55 = "S"; aElDni = #900#1; |HAZ LaRentaSI;  |FINSI;
        nOpTitular = 1;
        eaCodR2 = "";
    |SINO;
        |ACABA;
    |FINSI;

    nTTribut  = #agicen14#9;
    |SI #0#56 = "N";
        |SI nTTribut = 1; |O nTTribut = 18; |O nTTribut = 19;
            swNoExis = 1;
            |ACABA;
        |FINSI;
    |FINSI;

   aQueBusco = #agicen14#15; |QBF aQueBusco;
   |SI aQueBusco = "MANUAL";
       aQueBusco = "REGISTROS";
       nDPlani = 0;
       |BUCLE LeoDsmom009;
       |SI nDPlani = 1; aQueBusco = "PLANILLAS"; |FINSI;
   |FINSI;

   aAlfa1 = #0#1; aAlfa1 = "01.01." + aAlfa1; #900#13 = aAlfa1;
   aAlfa1 = #0#1; aAlfa1 = "31.12." + aAlfa1; #900#14 = aAlfa1;

   swExisL = 0;
   swExisA = 0;
   |SI #agicen14#3 = "8612"; |O #agicen14#3 = "8611"; |O #agicen14#3 = "862 ";
       swExisL = 1;
   |SINO;
       |SI #agicen14#9 = 6; |O #agicen14#9 = 7; |O #agicen14#9 = 11; |O #agicen14#9 = 12;
           |O #agicen14#9 = 16; |O #agicen14#9 = 17;
              swExisA = 1;
       |FINSI;
   |FINSI;

|FPRC;

|PROCESO LaRentaSI;
     eaCodR2    = "";
     eaCodRenta = "";
     |SI nOpTitular = 0;
         |SI #900#52 ! 0;
             eaCodRenta = "Codigo de Renta : " + (("00000" + #900#52) % -105);
             |ACABA;
         |FINSI;
     |FINSI;

     |ABRE #103;
     |SELECT #2#103;
     #103#1 = aElDni;
     |LEE 040#103=;
     |SI FSalida = 0;
         eaCodRenta = "Codigo de Renta : " + (("00000" + #103#0) % -105);
         eaCodR2    = (("00000" + #103#0) % -105);
     |SINO;
         |SELECT #3#103;
         #103#35 = aElDni;
         |LEE 040#103=;
         |SI FSalida = 0;
             eaCodRenta = "Codigo de Renta : " + (("00000" + #103#0) % -105);
             eaCodR2    = (("00000" + #103#0) % -105);
         |FINSI;
     |FINSI;
     |CIERRA #103;
|FPRC;
|| **************************************************************************

|PROGRAMA;
 |SI enSwMvo = 1;
     enEmpresa = enEmprM;
     enEjer    = enEjerM;
 |FINSI;

 |SI enEmpresa ! 0;
     |DDEFECTO #0;
     #0#1 = enEjer;
     #0#2 = enEmpresa;
     #0#3 = enEmpresa;
     #0#38 = enDActM;
     #0#39 = enHActM;
     #0#47 = efDFecM;
     #0#48 = efHFecM;
     |HAZ Tipo3;
 |SINO;
     |CLS;
     |MANTE #0;
 |FINSI;
|FPRO;
