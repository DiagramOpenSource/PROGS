|FICHEROS;
     dsmoy074 #0;

     dsmom080 #80;
     dsmom081 #81;

     dsmom085 #85;
     dsmom086 #86;
     dsmom087 #87;

     dsmow117 #117;
     dsmow118 #118;
     dsmow125 #125;
     dsmow126 #126;

     :/basica/def/agicen14 #114;
     :/basica/def/agifigen #1118;
     :/contagen/def/canempre #330;

     reflejo@ #182;
     dsmow032 #900;
|FIN;

|VARIABLES;
     aParam = "";
     aPar1  = "";
     aPar2  = "";

     nDEmpresa  = 0;
     nHEmpresa  = 0;
     aDNom      = "";
     aHNom      = "";
     nEjer      = 0;
     nDPeriodo  = 0;
     nHPeriodo  = 0;
     nCampo     = 0;

     nSwAlgun    = 0;
     nSwAlg      = 0;
     aAlfa       = "";
     aAlfa1      = "";
     aAlfa2      = "";
     aAlfa3      = "";
     aAlfa4      = "";
     fLaFecha    = @;

     informe     = "";
     inform1 = "dsmowp03";
     inform2 = "dsmoya74";
     inform3 = "dsmoy074";
     inform4 = "dsmoys74";

     nNume1      = 0;
     nNume2      = 0;
     nNume3      = 0;

     nSwSal      = 0;
     &nSwLinea   = 0;
     &eaCam1     = "";
     &eaCam2     = "";
     &eaCam3     = "";
     &eaCam31    = "";
     &enCam4     = "";
     &enCam41    = "";
     &eaCa4      = "";
     &eaCam8     = "";
     &enCam10    = 0;
     &enCam11    = 0;
     &enCam13    = 0;
     &eaCam15    = "";
     &eaTit      = "";
     &enEj82     = "";
     &eaTex      = "";
     &enC1       = 0;
     &enC2       = 0;
     &enCT1      = 0;
     &enCT2      = 0;
     &eaTituloL  = "";
     &efDFecha   = @;
     &efHFecha   = @;
     &enSwHay   = 0;
     &eaFitx117 = "";
     &eaFitx118 = "";
     &eaFitx125 = "";
     &eaFitx126 = "";
     aBase      = "";
     aDef       = "";
|FIN;

|INCLUYE i_variar;

|| /////////////////////// PROCESOS ENTRADA POR PANTALLA \\\\\\\\\\\\\\\\\\\\
|CALCULO Eano; |TIPO 7;
 |SI enEmpresa = 0;
     fLaFecha = ~;
     aAlfa1 = fLaFecha; aAlfa1 = aAlfa1 % -104;
     #0#3 = aAlfa1; #0#3 = #0#3 - 1;
     |PINTA #0#3;
 |SINO;
     |PTEC 802;
 |FINSI;
|FCAL;

|PROCESO LaEmpresa7; |TIPO 7;
 |SI enEmpresa ! 0;
     |PTEC 802;
     |SI Campo = 21; enEmpresa = 0; |FINSI;
 |FINSI;
|FPRC;

|PROCESO Parrilla;
     |SI #0#4 = 0;
         #0#4 = 0;  |PINTA #0#4;
         #0#5 = 0;  |PINTA #0#5;  |C_M #0#5, 1, "N";
         |PARA nCampo = 6;  |SI nCampo [ 17;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |SINO;
         |SI enEmpresa = 0;
             |C_M #0#4, 1, "S";
             |C_M #0#5, 1, "S";
         |FINSI;

         |PARA nCampo = 6;  |SI nCampo [ 17;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "N";
               #0nCampo = 0;  |PINTA #0nCampo;
         |SIGUE;
     |FINSI;
|FPRC;

|PROCESO NoMasEmpresas;
     |SI #0Campo = 0;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 17;  |HACIENDO nCampo = nCampo + 1;
               #0nCampo = 0;  |C_M #0nCampo, 1, "N";
         |SIGUE;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 17;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |FINSI;
|FPRC;

|| /////////////////////// PROCESOS DE TRABAJO \\\\\\\\\\\\\\\\\\\\
|PROCESO ElComun;
       #900#15 = 00;
       #900#16 = "IVA DEDUCIBLE OPERACIONES COMUNES ";

       |ABRE #114;
       #114#0 = enEmpresa; || Empresa
       #114#1 = 00;
       |LEE 041#114];
       |SI FSalida = 0; |Y #114#0 = enEmpresa;
           #900#17 = #114#3;   ||Epigrafe
           #900#18 = #114#4;   ||Descripcion Actividad
           #900#19 = #1118#93;   ||Fecha Inicio
           #900#20 = #1118#94;   ||Fecha Fin
           #900#21 = #114#11;  ||Sector actividad
           #900#22 = #114#12;  ||Sector actividad
           #900#23 = #114#16;  ||Tipo Actividad
           #900#24 = #114#17;  ||Descripcion Tipo Actividad
           #900#51 = #114#20;  ||CNAE
       |FINSI;
       |CIERRA #114;
|FPRC;

|PROCESO LosDatosAct;
    |ABRE #114;
    #114#0 = enEmpresa;
    #114#1 = #86#2;
    |LEE 001#114=;
    |SI FSalida = 0;
        #900#15 = #114#1;   ||Act.
        #900#16 = #114#2;   ||Tipo Epig.
        #900#17 = #114#3;   ||Epigrafe
        #900#18 = #114#4;   ||Descripcion Actividad
        #900#19 = #114#5;   ||Fecha Inicio
        #900#20 = #114#6;   ||Fecha Fin
        #900#21 = #114#11;  ||Sector actividad
        #900#22 = #114#12;  ||Sector actividad
        #900#23 = #114#16;  ||Tipo Actividad
        #900#24 = #114#17;  ||Descripcion Tipo Actividad
        #900#51 = #114#20;  ||CNAE
   |FINSI;
   |CIERRA #114;
|FPRC;

|PROCESO LeoInfw125;
  |PRINT;
  nSwLinea = 3;
|FPRC;

|DEFBUCLE LeoInf125;
   #125#1;
   ;
   #126#0, #126#1, #126#2, 00;
   #126#0, #126#1, #126#2, 99;
   ;
   NULL, LeoInfw125;
|FIN;

|PROCESO LasActividades;
||  |SI #86#2 ! 0;
||      |HAZ LosDatosAct;
|| |SINO;
||      |HAZ ElComun;
||  |FINSI;
  |PRINT;
  nSwLinea = 1;
|FPRC;

|DEFBUCLE LasActividades;
   #86#2;
   ;
   #126#0, #126#1, #126#2, 00;
   #126#0, #126#1, #126#2, 99;
   e;
   NULL, LasActividades;
|FIN;

|PROCESO LeoInfw126;
   enCam4  = #126#6;
   enCam4  = enCam4 + "%";
   enCam4  = ("     " + enCam4) % -104;
   enCam41 = #126#5;
   enCam41 = enCam41 + "%";
   enCam41 = ("     " + enCam41) % -104;
   nSwLinea = 0;
   |BUCLE LasActividades;
   nSwLinea = 2;
   |BUCLE LeoInf125;
   nSwAlg = 1;
   |PIEINF;
|FPRC;

|DEFBUCLE LeoInfw126;
   #126#1;
   ;
   enEmpresa, enEjer, 00;
   enEmpresa, enEjer, 98;
   e;
   NULL, LeoInfw126;
|FIN;

|PROCESO LeoInfw118;
  |PRINT;
|FPRC;

|DEFBUCLE LeoInf118;
   #118#1;
   ;
   #86#0, #86#1, #86#2, 01;
   #86#0, #86#1, #86#2, 99;
   ;
   NULL, LeoInfw118;
|FIN;

|PROCESO LeoInf086;
  |PDEFECTO #900, 15, 25; #900#51 = "";

  |SI #86#2 ! 0;
      |HAZ LosDatosAct;
  |SINO;
      |HAZ ElComun;
  |FINSI;

  enCT1 = 0;
  enC1  = 0;
  enC2  = 0;
  |SI enEjer [ 2011;
      |DDEFECTO #81;
      #81#0 = enEmpresa;
      #81#1 = enEjer -1;
      #81#2 = #86#2;
      |LEE 001#81=;
      enC1 = #81#7;
      enC2 = #81#8 + #81#10 + #81#41;
  |SINO;
      |SALVA_DATOS #86;
      |DDEFECTO #182;
      #182#0 = enEmpresa;
      #182#1 = enEjer -1;
      #182#2 = #86#2;
      |LEE 001#182=;
      enC1 = #182#7;
      enC2 = #182#8;
      |REPON_DATOS #86;
  |FINSI;
  enCT2 = enC1 + enC2;
  enCT1 = #86#9;

   nSwLinea  = 0;
   enCam4    = #86#6;
   enCam4 = enCam4 + "%";
   enCam4 = ("     " + enCam4) % -104;
   enCam41 = #86#5;
   enCam41 = enCam41 + "%";
   enCam41 = ("     " + enCam41) % -104;

   eaCa4 = #86#4;
   |BUCLE LeoInf118;
   nSwAlg = 1;
   |PIEINF;
|FPRC;

|DEFBUCLE LeoInf086;
   #86#1;
   ;
   enEmpresa, enEjer, 00;
   enEmpresa, enEjer, 98;
   e;
   NULL, LeoInf086;
|FIN;

|PROCESO LeoInfw117;
  |PRINT;
|FPRC;

|DEFBUCLE LeoInf117;
   #117#1;
   ;
   enEmpresa, enEjer, 01;
   enEmpresa, enEjer, 99;
   ;
   NULL, LeoInfw117;
|FIN;

|PROCESO LEmpresa;
  nSwSal = 0;
  |SI #0#4 = 0;
      nSwSal = 1;
      |PARA nCampo = 6;  |SI nCampo [ 17;  |HACIENDO nCampo = nCampo + 1;
            |SI #0nCampo ! 0;
                |SI #0nCampo = #1118#0;
                    nSwSal = 0;
                    |SAL;
                |FINSI;
            |FINSI;
      |SIGUE;
  |FINSI;
  |SI nSwSal = 1; |ACABA; |FINSI;

  enEmpresa = #1118#0;

  nSwLinea  = 0;
  eaCam1  = #1118#0; |QBF eaCam1;
  eaCam1  = ("00000" + eaCam1) % -105;
  eaCam2  = #1118#1;
  eaCam15  = #1118#13;

  enCT1 = 0;
  |SI enEjer [ 2011;
      |DDEFECTO #80;
      #80#0 = enEmpresa;
      #80#1 = enEjer -1;
      |LEE 001#80=;
      enC1 = #80#6;
      enC2 = #80#7 + #80#9 + #80#43;
  |SINO;
      |DDEFECTO #85;
      #85#0 = enEmpresa;
      #85#1 = enEjer -1;
      |LEE 001#85=;
      enC1 = #85#6;
      enC2 = #85#7;
  |FINSI;
  enCT2 = enC1 + enC2;

  nEjer = enEjer;
  |DDEFECTO #85;
  #85#0 = enEmpresa;
  #85#1 = enEjer;
  |LEE 001#85=;
  |SI FSalida ! 0;
      |ACABA;
  |FINSI;
  enCT1 = #85#9;

  nNume1 = 4; nNume2 = 5; nNume3 = 17;
  |SI aPar2 = "P";
      nNume1 = 2; nNume2 = 3; nNume3 = 16;
  |FINSI;

   nSwLinea  = 0;
   eaCam3    = "ESPECIAL";
   enCam4    = #85nNume3;
   |SI #85nNume1 ! "E"; eaCam3 = "GENERAL "; |FINSI;
   enCam4 = enCam4 + "%";
   enCam4 = ("     " + enCam4) % -104;
   |SI aPar2 = "I";
       eaCam31 = "ESPECIAL";
       enCam41 = #85#16;
       |SI #85#2 ! "E"; eaCam31 = "GENERAL "; |FINSI;
       enCam41 = enCam41 + "%";
       enCam41 = ("     " + enCam41) % -104;
  |FINSI;

  eaProrrat = #85nNume1;
  nProrrata = #85nNume2;
  aAlfa     = #85nNume3;

  |SI aPar2 = "P";
      enCam10   = enC1;
      enCam13   = enC2;
      enCam11   = enCT2;
  |SINO;
      enCam10   = #85#6;
      enCam11   = #85#8;
      enCam13   = #85#7;
  |FINSI;

  |SI eaProrrat = "G"; |Y nProrrata = 100;
      |ACABA;
  |SINO;
      |SI eaProrrat = "E"; |Y enCam11 = 0;
          |ACABA;
      |FINSI;
  |FINSI;

  |SI aParam = "FI";
       |INFOR informe;
       |SI FSalida ! 0;  |FINIMP;  |ACABA;  |FINSI;
  |FINSI;

  |SI aPar1 = "F";

      |DDEFECTO #900;
      #900#0 = enEmpresa;
      eaTituloL = #0#20;
      |HAZ DatosEmpr_w32;

      |SUB_EJECUTA_NP "dsmom085;Inf";

      |NOME_DAT #117 eaFitx117;
      |NOME_DAT #118 eaFitx118;
      |NOME_DAT #125 eaFitx125;
      |NOME_DAT #126 eaFitx126;

      |BUCLE LeoInf117;
      |PIEINF;
      |FININF;

      |SI #0#23 ! "N";
          |SI #0#23 ! "S";
              |INFOR inform2;
              |SI FSalida ! 0;  |FINIMP;  |ACABA;  |FINSI;
              |ABRE #182;
              |BUCLE LeoInf086;
              |CIERRA #182;
              |FININF;
          |FINSI;
          |SI #0#23 ! "A";
              |INFOR inform4;
              |SI FSalida ! 0;  |FINIMP;  |ACABA;  |FINSI;
              |BUCLE LeoInfw126;
              |FININF;
          |FINSI;
      |FINSI;

      |SI #0#22 = "S";
          enPeriodo = -1;
          |ABRE #330;
          |SELECT #4#330;
          #330#2  = enEmpresa;
          aAlfa1 = #0#3;
          aAlfa1 = aAlfa1 % -102;
          nNume1 = aAlfa1;
          #330#26 = nNume1;
          |LEE 000#330=;
          |SI FSalida = 0;
              enPeriodo = #330#3;
          |FINSI;
          |SELECT #1#330;
          |CIERRA #330;
          |SI enPeriodo ! -1;
              |SUB_EJECUTA_NP ":contagen/cay3ivas;modelosR";
          |FINSI;
      |FINSI;
  |SINO;
      |PRINT;
      nSwAlgun = 1;
  |FINSI;
|FPRC;


|DEFBUCLE agifigen1;
 #1118#2;
 0;
 aDNom, nDEmpresa;
 aHNom, nHEmpresa;
 e;
 NULL, LEmpresa;
|FIN;

|DEFBUCLE agifigen;
 #1118#1;
 ;
 nDEmpresa;
 nHEmpresa;
 e;
 NULL, LEmpresa;
|FIN;


|PROCESO Tipo3;  |TIPO 3;

     |HAZ LeeCtrlProrr;

     aDNom = " " * 40;  aHNom = "z" * 40;
     nDPeriodo = 01;    nHPeriodo = 99;
     enEjer   = #0#3;
     aAlfa    = enEjer;
     aAlfa1   = "01.01." + aAlfa;
     aAlfa2   = "31.12." + aAlfa;
     efDFecha = aAlfa1;
     efHFecha = aAlfa2;

     eaTit  = "DEFINITIVA";
     |SI aPar2 = "P";
         eaTit = "PROVISIONAL";
         eaTex = "Datos Economicos del Ejercicio Anterior";
     |FINSI;
     enEj82 = enEjer;

     |DBASE aBase;  |QBT aBase;
     aDef = aBase + "def/dsmom086.mas";
     |CARGA_DEF aDef, reflejo@;
     |SI FSalida ! 0;
         aAlfa = "     Error en cargar el Def dsmom086";
         |MENAV aAlfa;
         |ACABA;
     |FINSI;

     |IMPRE 0;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |SI aParam ! "FI";
         |INFOR informe;
         |SI FSalida ! 0;  |FINIMP;  |ACABA;  |FINSI;
     |FINSI;

     nDEmpresa = 00001;
     nHEmpresa = 99999;
     |SI #0#4 ! 0;
         nDEmpresa = #0#4;
         nHEmpresa = #0#5;
     |FINSI;

     nSwAlgun = 0;
     |ABRE #85;
     |ABRE #81;
     |ABRE #80;
     |SI #0#21 = "C"; |BUCLE agifigen; |FINSI;
     |SI #0#21 = "N"; |BUCLE agifigen1; |FINSI;
     |CIERRA #80;
     |CIERRA #81;
     |CIERRA #85;

     |SI nSwAlgun = 1; |Y aParam ! "FI";
         |PIEINF;
     |FINSI;
     |SI aParam ! "FI";
         |FININF;
     |FINSI;
     |FINIMP;

     |DESCARGA_DEF #reflejo@;
|FPRC;
|| ************************************************************

|PROGRAMA;
  aParam = PARAMETRO; |QBF aParam; aParam = aParam > " ";
  |SI aParam = ""; aParam = "CD"; |FINSI;

  aPar1 = aParam % 101;
  aPar2 = aParam % 201;

  informe = inform1;   || aParam = "CD";
  aAlfa1 = "Informe Calculo Definitivo";
  aAlfa2 = "Informe Gral Empresas con Calculo de Porcentaje de Prorrata Definitiva";
  |SI aParam = "CP";
      informe = inform1;
      aAlfa1 = "Informe Calculo Provisional";
      aAlfa2 = "Informe Gral Empresas con Calculo de Porcentaje de Prorrata Provisional";
  |FINSI;

  |SI aParam = "FI";
      informe = inform3;
      aAlfa1 = "Ficha de la Prorrata Aplicada";
      aAlfa2 = "Ficha de Empresas con la Prorrata Aplicada";
  |FINSI;


  |CLS;
  |CABEZA aAlfa1;

  |PINPA #0#0;
  |SI aParam = "FI";
      |BLIN 21;
      |PINTA 2111, "Detalle Informe (A/S/D/N): ";
      |C_V #0#23,1,"S"; |C_M #0#23,1,"S";
      |PINTA 2144, "Resumen Totales Conceptos: ";
      |C_V #0#22,1,"S"; |C_M #0#22,1,"S";
      |CUADRO 1709 2273;
  |FINSI;
  |ATRI R;
  |PINTA 0706, aAlfa2;
  |ATRI 0;
  |SI enEmpresa ! 0;
      #0#3 = enEjer;
      #0#4 = enEmpresa;
      #0#5 = enEmpresa;
  |FINSI;

  aAlfa2 = aAlfa2 > " ";
  #0#20  = aAlfa2;
  |PINDA #0#0;
  |ENDATOS #1#0;
|FPRO;
