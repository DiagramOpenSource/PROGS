|FICHEROS;
     dsmoz004 #0;
     dsmom009 #9;
     dsmom010 #10;
     dsmom011 #11;
     dsmom012 #12;
     dsmom013 #13;
     dsmom014 #14;
     dsmopl10;
     dsmopl12;
     dsmopl13;
     dsmom065 #65;
     dsmow005 #999;
     dsmow006 #998;
|FIN;

|VARIABLES;
     aFichero  = "";
     aAlfa     = "";

     fFecha    = @;

     nCampo    = 0;
     nDEmpresa = 0;
     nHEmpresa = 0;
     enGraba   = 0;
     nImporte  = 0;
     nImporte1 = 0;
     nImporte2 = 0;
     nCambio   = 0;
     nOpera    = 0;
     nSw          = 0;
|FIN;

|INCLUYE i_variar;

|| /////////////////////// PROCESOS ENTRADA POR PANTALLA \\\\\\\\\\\\\\\\\\\\

|PROCESO Periodo;
     |SI #0#0 = 1;   #0#1 = "ENERO     ";  |FINSI;
     |SI #0#0 = 2;   #0#1 = "FEBRERO   ";  |FINSI;
     |SI #0#0 = 3;   #0#1 = "MARZO     ";  |FINSI;
     |SI #0#0 = 4;   #0#1 = "ABRIL     ";  |FINSI;
     |SI #0#0 = 5;   #0#1 = "MAYO      ";  |FINSI;
     |SI #0#0 = 6;   #0#1 = "JUNIO     ";  |FINSI;
     |SI #0#0 = 7;   #0#1 = "JULIO     ";  |FINSI;
     |SI #0#0 = 8;   #0#1 = "AGOSTO    ";  |FINSI;
     |SI #0#0 = 9;   #0#1 = "SEPTIEMBRE";  |FINSI;
     |SI #0#0 = 10;  #0#1 = "OCTUBRE   ";  |FINSI;
     |SI #0#0 = 11;  #0#1 = "NOVIEMBRE ";  |FINSI;
     |SI #0#0 = 12;  #0#1 = "DICIEMBRE ";  |FINSI;

     fFecha = ~;
     aAlfa  = fFecha % -104;
     #0#2   = aAlfa;

     |PINTA #0#1;
     |PINTA #0#2;
|FPRC;

|PROCESO Parrilla;
     |SI #0#3 = 0;
         #0#3 = 0;  |PINTA #0#3;
         #0#4 = 0;  |PINTA #0#4;  |C_M #0#4, 1, "N";
         |PARA nCampo = 5;  |SI nCampo [ 16;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |SINO;
         |SI enEmpresa = 0;
             |C_M #0#3, 1, "S";
             |C_M #0#4, 1, "S";
         |FINSI;

         |PARA nCampo = 5;  |SI nCampo [ 16;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "N";
               #0nCampo = 0;  |PINTA #0nCampo;
         |SIGUE;
     |FINSI;
|FPRC;

|PROCESO NoMasEmpresas;
     |SI #0Campo = 0;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 16;  |HACIENDO nCampo = nCampo + 1;
               #0nCampo = 0;  |C_M #0nCampo, 1, "N";
         |SIGUE;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 16;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |FINSI;
|FPRC;

|| /////////////////////// PROCESOS DE TRABAJO \\\\\\\\\\\\\\\\\\\\

|PROCESO GrabaTempo;
     enEmpresaPINET = #9#0;
     |HAZ RestriccionPINET;
     |SI enErrorPINET = 1;  |ACABA;  |FINSI;

     #999#0 = #9#0;
     |LEE 101#999=;
     |SI FSalida ! 0;
         #999#0 = #9#0;
         #999#1 = #9#1;
         #999#2 = #9#2;
         #999#3 = #9#5;
         #999#4 = #0#1;
         |GRABA 020#999n;

         enGraba = 1;
     |FINSI;
     |LIBERA #999;
|FPRC;

|DEFBUCLE dsmom009;
     #9#1;
     ;
     nDEmpresa, #0#2, #0#0, 00, 01;
     nHEmpresa, #0#2, #0#0, 99, 99;
     ;
     NULL, GrabaTempo;
|FIN;

|RUTINA ClaveBaja999;
     #999#0 = 1;
|FRUT;

|RUTINA ClaveAlta999;
     #999#0 = 99999;
|FRUT;

|PROCESO Tipo3;  |TIPO 3;

     |SI #0#2 = 2019;
         |HAZ Cal2019Desglose;
     |FINSI;

     enEjer     = #0#2;
     |HAZ Verm014Plan;
     |SI enPlan14 = 0; |ACABA; |FINSI;
     enEjer     = #0#2;
     |HAZ Verm060Plan;
     |SI enPlan60 = 0; |ACABA; |FINSI;

     aFichero = ("04" + Usuario) % 108;
     |NOME_DAT #999 aFichero;
     |ABRE #999;  |CIERRA #999;  |DELINDEX #999;

     |ABRE #999;
     |SI #0#3 ! 0;
         nDEmpresa = #0#3;
         nHEmpresa = #0#4;
         |BUCLE dsmom009;
     |SINO;
         |PARA nCampo = 5;  |SI nCampo [ 16;  |HACIENDO nCampo = nCampo + 1;
               nDEmpresa = #0nCampo;
               nHEmpresa = #0nCampo;
               |BUCLE dsmom009;
         |SIGUE;
     |FINSI;
     |CIERRA #999;

     |SI enGraba = 1;
         |PINPA #0#999;
         |SI #0#2 ] 2009;
             |ATRI R;
             |PINTA 1240, "303";
             |PINTA 1244, "   ";
             |PINTA 1248, "   ";
             |PINTA 1252, "   ";
             |ATRI 0;
         |FINSI;

         |ENTLINEAL #1#999, -1, 4, 22, 0, ClaveBaja999, ClaveAlta999;
     |SINO;
         |MENAV "     Seleccion Vacia.";
     |FINSI;
     |DELINDEX #999;
|FPRC;

|PROCESO RegisPlan;
 |SI nOpera = 0;
     |SI #9#57 ! "SIN DATOS   ";
         nCambio = 1; |ERROR10;
     |FINSI;
 |FINSI;
 |SI nOpera = 1;
     |SI #9#1 < 2010; |O #9#57 ! "SIN DATOS   ";
         #9#60 = "N";
         |GRABA 020#9a;
     |FINSI;
 |FINSI;
 |LIBERA #9;
|FPRC;

|DEFBUCLE RegisPlan;
  #9#1;
  60;
  00001, 0000, 00, 00, 00, " ";
  99999, 2010, 99, 99, 99, " ";
  be;
  NULL, RegisPlan;
|FIN;

|PROCESO Verm009;
 nCambio = 0;
 nOpera  = 0;
 |BUCLE RegisPlan;
 |SI nCambio = 1;
     nOpera = 1;
     |BUCLE RegisPlan;
 |FINSI;
|FPRC;

|PROCESO ModiConsumo;
  #12#7 = "+";
  |GRABA 020#12a;
  |LIBERA #12;
|FPRC;

|DEFBUCLE ModiConsumo;
 #12#1;
 ;
 00001, 2010, 01, 00, 00, "7100";
 99999, 2099, 99, 99, 99, "7199";
 be;
 NULL, ModiConsumo;
|FIN;


|PROGRAMA;
     |HAZ Verm009;
     |SI enEmpresa ! 0;
         #0#0 = enPeriodo;
         #0#1 = eaNomPer;
         #0#2 = enEjer;
         #0#3 = enEmpresa;
         #0#4 = enEmpresa;

         enPlanin = 1;

         |HAZ Tipo3;
     |SINO;
         enPlanin = 0;

         |CLS;
         |CABEZA "Entrada de Planillas";
         |DDEFECTO #0;
         |PINPA #0#0;
         |PINDA #0#0;
         |ENDATOS #1#0;
     |FINSI;

     |HAZ FinalizaEnX;
|FPRO;
