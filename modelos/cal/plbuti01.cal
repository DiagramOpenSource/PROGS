|FICHEROS;
     dsmom108 #108;

     :/integral/def/pntm0001 #1;
     :/integral/def/pntm0002 #2;
|FIN;

|VARIABLES;
     aBase        = "";
     aLinea       = "";
     aIP          = "";
     aUsuario     = "";
     aLongitud    = "";
     aDocRemoto   = "";
     aDocServidor = "";
     aIPRemoto    = "";
     aEjecuta     = "";

     nCanal          = 0;
     nVersionAgitool = 0;
     nHandle         = 0;
     nInkey          = 0;
     nAscii          = 0;
     nAsciiB         = 0;
     nLongitud       = 0;
     nCaracter       = 0;
     nPausa          = 0;
     nConta          = 0;

     {1}version      = "";
     {1}aContiene    = "";

     &enErrorCad     = 0;
     &eaMensaCad     = "";
     &eaPass         = "";
     &eaOrigen       = "";
     &eaDestino      = "";
     &eaDSMAC        = "";
|FIN;

||INCLUYE i_variar;
|INCLUYE i_varemi;

||******************** RESTRICCIONES PINET *****************

|PROCESO RestriccionPINET;
     enErrorPINET = 0;

     |DBASS aBase;  |QBF aBase;
     aAlfa = aBase + "integral/def/dsam0000.mas";
     |XABRE "E", aAlfa, nCanal;
     |SI FSalida < 0;  |ACABA;  |FINSI;

     aUsuario = Usuario % 810;

     |ABRE #pntm0001;
     |SELECT #2#pntm0001;
     #pntm0001#1 = aUsuario;
     |LEE 000#pntm0001.=;
     |SI FSalida ! 0;
         |CIERRA #pntm0001;
         |ACABA;
     |FINSI;
     |CIERRA #pntm0001;

     |ABRE #pntm0002;
     #pntm0002#0 = #pntm0001#0;
     #pntm0002#1 = enEmpresaPINET;
     |LEE 000#pntm0002.=;
     |SI FSalida ! 0;
         enErrorPINET = 1;

         #pntm0002#0 = #pntm0001#0;
         #pntm0002#1 = 1;
         |LEE 000#pntm0002.];

         enEmpresaPINET = #pntm0002#1;
     |FINSI;
     |CIERRA #pntm0002;
|FPRC;

|| ********************* CONTROL CADUCIDAD

|PROCESO ControlCaducidad;
     enErrorCad = 0;

     |DBASS aBase;  |QBF aBase;

     aArchivo = aBase + "agitool/agitool.men";

     |XABRE "U", aArchivo, nCanal;
     |SI FSalida ] 0;
          |XLEE nCanal, 100, aLinea;
     |FINSI;
     |XCIERRA nCanal;

     aLinea = aLinea - "#:Utilidades DIAGRAM ";
     aVersion = aLinea;

     enErrorCad = 0;
     aAlfa      = aVersion - "." - ".";
     nVersionAgitool = aAlfa;
     |SI nVersionAgitool < 512028;
         enErrorCad = 1;
     |FINSI;

     |SI enErrorCad = 0;
         aArchivo = aBase + "agitool/agitool.ini";
         aIP = ""; |IP_REMOTO aIP;
         |SI aIP = "";       || modo local
              |XABRE "C", aArchivo, nHandle;
         |SINO;              || modo C/S
              |XABRE "", aArchivo, nHandle;
         |FINSI;
         |SI FSalida < 0;  |ACABA;  |FINSI;

         |PARA;  |SI;  |HACIENDO;
                 |XLEE nHandle, 250, aCadena;
                 |SI FSalida < 0;  |SAL;  |FINSI;
                 |QBF aCadena;

                 |SI aCadena = "DS=1";
                      enErrorCad = 1;
                      |SAL;
                 |FINSI;
         |SIGUE;
         |XCIERRA nHandle;
     |FINSI;

     |SI enErrorCad = 1;
         |SI eaMensaCad = "9";
             aAlfa = "    Se ha detectado una diferencia de versión y el programa se interrumpirá. ";
             aAlfa = aAlfa + "Consulte con su Administrador.";
         |FINSI;

         |SI eaMensaCad = "X";
             aAlfa = "    Se ha detectado una diferencia de versi¢n y el programa se interrumpir . ";
             aAlfa = aAlfa + "Consulte con su Administrador.";
         |FINSI;

         |MENAV aAlfa;
     |FINSI;

     |PULSATECLA;
|FPRC;

|PROCESO MontaFormula;
     nAscii   = &aCaracter;
     nAsciiB  = 94;
     |PARA nContador = 32;  |SI nContador [ 126;  |HACIENDO nContador = nContador + 1;
           |SI nContador = nAscii;
               nAsciiB  = nContador + nAsciiB;
               aCaracter = &nAsciiB;
               |SAL;
           |FINSI;
           nAsciiB  = nAsciiB - 2;
     |SIGUE;

     aCadena = aCadena + aCaracter;
|FPRC;

|PROCESO PonPassword;
     aCadena   = "";
     aCaracter = eaPass % 101;   |HAZ MontaFormula;
     aCaracter = eaPass % 201;   |HAZ MontaFormula;
     aCaracter = eaPass % 301;   |HAZ MontaFormula;
     aCaracter = eaPass % 401;   |HAZ MontaFormula;
     aCaracter = eaPass % 501;   |HAZ MontaFormula;
     aCaracter = eaPass % 601;   |HAZ MontaFormula;
     aCaracter = eaPass % 701;   |HAZ MontaFormula;
     aCaracter = eaPass % 801;   |HAZ MontaFormula;
     aCaracter = eaPass % 901;   |HAZ MontaFormula;
     aCaracter = eaPass % 1001;  |HAZ MontaFormula;

     eaPass = aCadena;
|FPRC;

|PROCESO Refresco;
     |PONCONTROL 23, "SI";
|FPRC;

|PROCESO SinBarra;  |TIPO 5;
     FSalida = "SINBARRA";
|FPRC;

|PROCESO ExtraePath;
     eaPathAcrobat = version1;
     aLongitud     = eaPathAcrobat % 0;
     nLongitud     = aLongitud;
     |PARA nPos = 1; |SI nPos [ nLongitud; |HACIENDO nPos = nPos + 1;
          nCaracter = (nPos * 100) + 1;
          aCaracter = eaPathAcrobat % nCaracter;
          |SI aCaracter = ",";
               nPos          = nLongitud - nPos;
               nPos          = nPos + 100;
               nPos          = (-1) * nPos;
               eaPathAcrobat = eaPathAcrobat % nPos;
               |SAL;
          |FINSI;
     |SIGUE;

     eaPathAcrobat = eaPathAcrobat - "[";
     eaPathAcrobat = eaPathAcrobat - "]";
|FPRC;

|PROCESO CompruebaRuta;
     enError = 0;
     |QBF eaPathAcrobat;
     |XABRE "CE", eaPathAcrobat, nCanal;
     |SI eaPathAcrobat = ""; |O FSalida < 0;
          |XCIERRA nCanal;
          aAlfa = "    No se ha encontrado el directorio donde esta instalado ";
          aAlfa = aAlfa + "Adobe Acrobat Reader. ";
          |MENAV aAlfa;
          aAlfa = "    Debera instalarselo.";
          |MENAV aAlfa;

          enError = 1;
     |FINSI;
|FPRC;

|PROCESO EnviaFicheroMD5;
   aIPRemoto = ""; |IP_REMOTO aIPRemoto;

   aContiene1 = eaOrigen;
   |ESPECIFICA "MD5", aContiene;
   aDocServidor = FSalida;  |QBF aDocServidor;

   aContiene1 = eaDestino;
   |SI aIP ! "";
      |ESPECIFICA "REMOTOMD5", aContiene;
   |SINO;
      |ESPECIFICA "MD5", aContiene;
   |FINSI;
   aDocRemoto = FSalida;  |QBF aDocRemoto;

   |SI aDocRemoto ! aDocServidor;
      |SI aIPRemoto ! "";
         |ENVIA_FICHERO eaOrigen, eaDestino;
      |SINO;
         |COPIA_FICHERO eaOrigen, eaDestino;
      |FINSI;
   |FINSI;
|FPRC;

|PROCESO CargaMac;
     |SI eaDSMAC ! "";  |ACABA;  |FINSI;

     |HAZ LeeUnidadBasico;

     aAlfa = eaUnidadBasico + "\MODELOS";  |RMKDIR aAlfa;
     aAlfa = aAlfa + "\DSMAC";             |RMKDIR aAlfa;

     |DBASE aAlfa;   |QBF aAlfa;
     aAlfa     = aAlfa + "plugins/";
     aOrigen   = aAlfa + "dsmac.exe";
     aDestino  = eaUnidadBasico + "\MODELOS\DSMAC\DSMAC.EXE";

     aContiene1 = aDestino;
     |ESPECIFICA "REMOTOMD5", aContiene;
     aDocRemoto = FSalida;  |QBF aDocRemoto;

     aContiene1 = aOrigen;
     |ESPECIFICA "MD5", aContiene;
     aDocServidor = FSalida;  |QBF aDocServidor;

     aIPRemoto = "";
     |IP_REMOTO aIPRemoto;  |QBT aIPRemoto;

     |SI aDocRemoto ! aDocServidor;
         |SI aIPRemoto ! "";
             |ENVIA_FICHERO aOrigen, aDestino;
         |SINO;
             |COPIA_FICHERO aOrigen, aDestino;
         |FINSI;
     |FINSI;

     |XABRE "EC", aDestino, nHandle;
     |SI FSalida < 0;
         |SI aIPRemoto ! "";
             |ENVIA_FICHERO aOrigen, aDestino;
         |SINO;
             |COPIA_FICHERO aOrigen, aDestino;
         |FINSI;
     |FINSI;

     aAbre    = eaUnidadBasico + "\MODELOS\DSMAC\DSMAC.TXT";  |RFBORRA aAbre;

     aEjecuta = "cmd " + &34 + "/c START /WAIT " + aDestino + " ";
     aEjecuta = aEjecuta +  eaUnidadBasico + "\MODELOS\DSMAC\DSMAC.TXT" + &34;
     |RSYSTEM aEjecuta;

     aAbre  = eaUnidadBasico + "\MODELOS\DSMAC\DSMAC.TXT";
     nConta = 0;
     |PARA;  |SI;  |HACIENDO;
             |PARA nPausa = 1; |SI nPausa [ 10000; |HACIENDO nPausa = nPausa + 1;
             |SIGUE;

             |XABRE "EC", aAbre, nHandle;
             |SI FSalida ] 0;
                 |SAL;
             |FINSI;

             nConta = nConta + 1;
             |SI nConta > 100;
                 |SAL;
             |FINSI;

             |PULSATECLA;
     |SIGUE;

     |XABRE "EC", aAbre, nHandle;
     |SI FSalida < 0;
         aEjecuta = "START "+ aDestino + " ";
         aEjecuta = aEjecuta +  eaUnidadBasico + "\MODELOS\DSMAC\DSMAC.TXT" + &34;
         |RSYSTEM aEjecuta;

         nConta = 0;
         |PARA;  |SI;  |HACIENDO;
                 |PARA nPausa = 1; |SI nPausa [ 10000; |HACIENDO nPausa = nPausa + 1;
                 |SIGUE;

                 |XABRE "EC", aAbre, nHandle;
                 |SI FSalida ] 0;
                     |SAL;
                 |FINSI;

                 nConta = nConta + 1;
                 |SI nConta > 100;
                     |SAL;
                 |FINSI;

                 |PULSATECLA;
         |SIGUE;
     |FINSI;

     eaDSMAC   = "";
     aAbre    = eaUnidadBasico + "\MODELOS\DSMAC\DSMAC.TXT";
     |XABRE "CU", aAbre, nHandle;
     |SI FSalida < 0;   |ACABA;  |FINSI;

     |XLEE nHandle, 250, eaDSMAC;
     |SI FSalida < 0
         eaDSMAC   = "";
     |FINSI;
     |XCIERRA nHandle;

     |QBF eaDSMAC;
|FPRC;

|PROCESO CogeMac;
     |HAZ CargaMac;

     |ABRE #108;
     #108#0 = eaDSMAC;
     |LEE 000#108=;
     |SI FSalida = 0;
         |CIERRA #108;
         |ACABA;
     |FINSI;

     #108#0 = aIPRemoto;
     |LEE 000#108=;
     |SI FSalida = 0;
         #108#0 = eaDSMAC;
         |GRABA 020#108n;
         |LIBERA #108;
     |FINSI;
     |CIERRA #108;
|FPRC;

|PROCESO LocalizaAcrobat;
     |HAZ CogeMac;

     eaPathAcrobat = #108#1;  |QBF eaPathAcrobat;

     enError  = 0;

     |SI eaPathAcrobat ! "";  |ACABA;  |FINSI;

     version1 = "AcroExch.Document";
     |ESPECIFICA "VERSION_PROGRAMA",version;
     |HAZ ExtraePath;
     |SI eaPathAcrobat = "NO LOCALIZADO";
          version1 = "AcroExch.App";
          |ESPECIFICA "VERSION_PROGRAMA",version;
          |HAZ ExtraePath;
     |FINSI;

     |SI eaPathAcrobat ! "NO LOCALIZADO";
          |HAZ CompruebaRuta;
          |SI enError = 1;          || todo ok
               |ACABA;              || hay Acrobat pero no lo encuentra
          |FINSI;
     |SINO;
          enError = 1;
     |FINSI;

     |SI enError = 0;
         |ABRE #108;
         #108#0 = eaDSMAC;
         |LEE 101#108=;
         |SI FSalida ! 0;
             |DDEFECTO #108;
             #108#0 = eaDSMAC;
             |GRABA 020#108b;
         |FINSI;
         #108#1 = eaPathAcrobat;
         |GRABA 020#108a;
         |LIBERA #108;
         |CIERRA #108;
    |FINSI;
|FPRC;

|PROCESO LeeUnidadBasico;
     aContiene = "";
     |ESPECIFICA "RBASS", aContiene;
     eaUnidadBasico = (aContiene1 % 102) > "";

     aAlfa = eaUnidadBasico % -101;
     |SI aAlfa ! ":";
         eaUnidadBasico = "";
     |FINSI;
|FPRC;

|PROCESO FinalizaEnX;
     |SUB_EJECUTA "|:modelos/dsmox000.tab";
|FPRC;
