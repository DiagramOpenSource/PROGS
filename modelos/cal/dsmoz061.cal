|FICHEROS;
   dsmoz061;
   dsmom040;

   :/basica/def/agifigen;
   :/contagen/def/ca8m0003;
   :/contagen/def/ca8ctrle;
   :/contagen/def/ca8eqmay;

   :/contagen/def/canempre;
|FIN;

|VARIABLES;
  nCampo      = 0;
  nEmpresa    = 0;
  nDEmpresa   = 0;
  nHEmpresa   = 0;
  nPara1      = 0;

  &swUno      = 0;
  &enEmpreOri = 0;
  &enEmpreDes = 0;
  aAlfa1 = "";
  aAlfa2 = "";
  aAlfa3 = "";
  nNume1 = 0;
  nNume2 = 0;
  nNume3 = 0;
  period = 0;
  nHay   = 0;
  uno    = "";
  dos    = "";
  nLong1 = 0;
  nLong2 = 0;
  aPath  = "";
  aCtaNueva = "";
|FIN;

|INCLUYE i_variar;
|INCLUYE i_varcon;

|| =====================================================================
|CALCULO Parrilla;
     |SI #dsmoz061#2 = 0;
         #dsmoz061#2 = 0;  |PINTA #dsmoz061#2;
         #dsmoz061#3 = 0;  |PINTA #dsmoz061#3;  |C_M #dsmoz061#3, 1, "N";
         |PARA nCampo = 4;  |SI nCampo [ 15;  |HACIENDO nCampo = nCampo + 1;
               |C_M #dsmoz061#nCampo#, 1, "S";
         |SIGUE;
     |SINO;
         |SI enEmpresa = 0;
             |C_M #dsmoz061#2, 1, "S";
             |C_M #dsmoz061#3, 1, "S";
         |FINSI;

         |PARA nCampo = 4;  |SI nCampo [ 15;  |HACIENDO nCampo = nCampo + 1;
               |C_M #dsmoz061#nCampo#, 1, "N";
               #dsmoz061#nCampo# = 0;
               |PINTA #dsmoz061#nCampo#;
         |SIGUE;
     |FINSI;
|FCAL;

|CALCULO NoMasEmpresas;
     |SI #dsmoz061#Campo# = 0;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 15;  |HACIENDO nCampo = nCampo + 1;
               #dsmoz061#nCampo# = 0;  |C_M #dsmoz061#nCampo#, 1, "N";
         |SIGUE;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 15;  |HACIENDO nCampo = nCampo + 1;
               |C_M #dsmoz061#nCampo#, 1, "S";
         |SIGUE;
     |FINSI;
|FCAL;

||******************************************************************************
|PROCESO Cambio;

  |QBF eaCuenta;
  |SI eaCuenta = ""; |ACABA; |FINSI;

  aCtaNueva = eaCuenta;
  nHay = 0;
  |SI period ! -1;
      |ABRE #ca8m0003;
      #ca8m0003#0 = eaCuenta;
      |LEE 000#ca8m0003.=;
      |SI FSalida = 0;
          aCtaNueva = #ca8m0003#2;
          nHay      = 1;
      |FINSI;
      |CIERRA #ca8m0003;
  |FINSI;

  |SI nHay = 0;  || Lo transformo ahora
      |ABRE #ca8eqmay;
      |DDEFECTO #ca8eqmay;

      uno = eaCuenta; |QBF uno;
      dos = uno + "000000000000";

      uno = dos % A105; nLong1 = 5;
      #ca8eqmay#0 = eNum2008;
      #ca8eqmay#1 = uno;
      |LEE 040#ca8eqmay.=;
      |SI FSalida = 0;  |HAZ Datos;  |ACABA;  |FINSI;

      uno = dos % A104; nLong1 = 4;
      #ca8eqmay#0 = eNum2008;
      #ca8eqmay#1 = uno;
      |LEE 040#ca8eqmay.=;
      |SI FSalida = 0;  |HAZ Datos;  |ACABA;  |FINSI;

      uno = dos % A103; nLong1 = 3;
      #ca8eqmay#0 = eNum2008;
      #ca8eqmay#1 = uno;
      |LEE 040#ca8eqmay.=;
      |SI FSalida = 0;  |HAZ Datos;  |ACABA;  |FINSI;

      uno = dos % A102; nLong1 = 2;
      #ca8eqmay#0 = eNum2008;
      #ca8eqmay#1 = uno;
      |LEE 040#ca8eqmay.=;
      |SI FSalida = 0;
          |HAZ Datos;
      |SINO;
          aCtaNueva = "PDTE";
          |CIERRA #ca8eqmay;
      |FINSI;
  |FINSI;
|FPRC;

|PROCESO Datos;
   eaPlan8  = #ca8eqmay#3;
   |QBF eaPlan8;
   |CIERRA #ca8eqmay;

   aAlfa1 = eaPlan8 % 0;
   nNume1 = aAlfa1;     || Longitud del Cambio
   nNume2 = nNume1 + 100; nLong2 = nNume1;

   |SI eaPlan8 = "PDTE";
       aCtaNueva = eaPlan8;
       |ACABA;
   |FINSI;

   nNume3 = 0 - ((nLongMax - nLong2) + 100)
   aAlfa1 = eaCuenta % nNume2;
   aAlfa2 = eaCuenta % nNume3;

   |SI aAlfa1 = eaPlan8;
       aCtaNueva = eaCuenta;
   |SINO;
       aCtaNueva = eaPlan8 + aAlfa2;
   |FINSI;
|FPRC;

|PROCESO GrupsAmor0;

   eaCuenta  = #dsmom040#3;
   aCtaNueva = #dsmom040#10;
   |HAZ Cambio;
   #dsmom040#10 = aCtaNueva;

   eaCuenta  = #dsmom040#5;
   aCtaNueva = #dsmom040#12;
   |HAZ Cambio;
   #dsmom040#12 = aCtaNueva;

   #dsmom040#8 = #agifigen#1;
   |GRABA 040#dsmom040.a;
   |LIBERA #dsmom040;
|FPRC;

|DEFBUCLE GrupsAmor;
 #dsmom040#1;
 ;
 nEmpresa, 0001;
 nEmpresa, 9999;
 be;
 NULL, GrupsAmor0;
|FIN;

|PROCESO locreo;

  |DBASS aPath; |QBT aPath;
  aPath  = aPath + "contagen/fich/";

  nLongMax = 12;
  nEmpresa = #agifigen#0;
  period   = -1;

  |ABRE #canempre;
  |DDEFECTO #canempre;
  |SELECT #4#canempre;
  #canempre#2  = nEmpresa;
  #canempre#26 = 8;
  |LEE 001#canempre.=;
  |SI FSalida = 0;
      period = #canempre#3;
  |FINSI;
  |SELECT #1#canempre;
  |CIERRA #canempre;

  |ABRE #ca8ctrle;
  |DDEFECTO #ca8ctrle;
  #ca8ctrle#0 = nEmpresa;
  #ca8ctrle#1 = period;
  |LEE 001#ca8ctrle.=;
  |SI FSalida = 0;
      eNum2008 = #ca8ctrle#2;
  |SINO;
      #ca8ctrle#0 = nEmpresa;
      #ca8ctrle#1 = 0;
      |LEE 001#ca8ctrle.];
      |SI FSalida = 0; |Y #ca8ctrle#0 = nEmpresa;
           eNum2008 = #ca8ctrle#2;
      |FINSI;
  |FINSI;
  |CIERRA #ca8ctrle;

  |SI period ! -1;
      |SI #canempre#13 = 1; nLongMax = #canempre#14; |FINSI;
      |SI #canempre#13 = 2; nLongMax = #canempre#15; |FINSI;
      |SI #canempre#13 = 3; nLongMax = #canempre#16; |FINSI;
      |SI #canempre#13 = 4; nLongMax = #canempre#17; |FINSI;
      |SI #canempre#13 = 5; nLongMax = #canempre#18; |FINSI;
      |SI #canempre#13 = 6; nLongMax = #canempre#19; |FINSI;

      aAlfa1 = nEmpresa; aAlfa1 = ("00000" + aAlfa1) % -105;
      aAlfa2 = period;   aAlfa2 = ("0"     + aAlfa2) % -101;
      aPath  = aPath + aAlfa1 + aAlfa2 + "/";
      |PATH_DAT #ca8m0003#aPath#;
  |FINSI;

  |BUCLE GrupsAmor;
|FPRC;

|DEFBUCLE locreo;
 #agifigen#1;
 ;
 nDEmpresa;
 nHEmpresa;
 ;
 NULL, locreo;
|FIN;

|CALCULO pasalo; |TIPO 3;
  |SI #dsmoz061#2 ! 0;
      nDEmpresa = #dsmoz061#2;
      nHEmpresa = #dsmoz061#3;
      |BUCLE locreo;
  |SINO;
      |PARA nPara1 = 4; |SI nPara1 [ 15; |HACIENDO nPara1 = nPara1 + 1;
            nDEmpresa = #dsmoz061#nPara1#;
            nHEmpresa = #dsmoz061#nPara1#;
            |BUCLE locreo;
       |SIGUE;
  |FINSI;
|FCAL;

|| **************************************************************************
|PROGRAMA;
 |SI swUno = 0;
     |CLS;
     |MANTE #dsmoz061;
 |SINO;
     |DDEFECTO #dsmoz061;
     #dsmoz061#2  = enEmpreOri;
     #dsmoz061#3  = enEmpreDes;
     |HAZ pasalo;
 |FINSI;
|FPRO;
