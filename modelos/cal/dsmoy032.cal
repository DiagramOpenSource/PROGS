|FICHEROS;
     dsmoy032 #0;

     dsmom030 #30;
     dsmom033 #33;

    :/contagen/def/canempre #300;
    :/contagen/def/caivaasi #301;
    :/contagen/def/camaniva #304;
    :/contagen/def/caivalin #305;
    :/contagen/def/caconimp #306;  || configuracion impresiones

     :/basica/def/agicen14 #114;
     :/basica/def/agifigen #118;
     :/basica/def/agim0019 #119;


     dsmow030 #900;          ||Def de impresion
     dsmow157 #957;
     dsmow049 #998;
     dsmow043 #999,MANTE;
|FIN;

|VARIABLES;
   &entrada = 1;
   informe = "";

   &aTipo      = "DE COMPRA";
   nInkey      = 0;
   aFichero    = "";
   nCampo      = 0;
   nDEmpresa   = 0;
   nHEmpresa   = 0;
   nDActividad = 0;
   nHActividad = 0;
   nLaEmpresa  = 0;
   nGTempo     = 0;

   nDepar     = 0;
   nDPerio    = 0;
   nHPerio    = 0;
   SwAgricola = 0;

   nSPin1      = 0;
   aPath       = "";
   nEjercicio  = 0;

   aAlfa1 = "";
   aAlfa2 = "";
   aAlfa3 = "";
   nPara1 = 0;
   nPara2 = 0;
   nNume1 = 0;
   nNume2 = 0;
   nNume3 = 0;
   nNume4 = 0;
   nNume5 = 0;
   nNume6 = 0;

   aDTipo = "";
   aHTipo = "";
   SwHayDatos = 0;

   fDFecha   = @;
   fHFecha   = @;
   fLaFecha  = @;
   &nFactura = 0;
   &nPagina  = 0;
   &nPagDup  = 0;
   &nSwPagDup = 0;
   &SwPrim   = 0;
   &SwLinea  = 0;
   &nLExen   = 0;
   &nLBase   = 0;
   &nLTipoI  = 0;
   &nLCuoI   = 0;
   &nLCuoIN  = 0;
   &nLTipoR  = 0;
   &nLCuoR   = 0;
   &nLCuoRN  = 0;

   &nLBasP   = 0;
   &nLTipP   = 0;
   &nLCuoP   = 0;

   nSwReten = 0;
   nElDepar = 0;
   nEstado = 0;

   aParam       = "";
   &enEjercicio = 0;
   &eaLegEmp    = "";
   &eaLegPer    = "";
   Comodin      = "";
   Comodin1     = "";
   aAlfa        = "";
   Handle       = 0;
   Numeracion   = 0;
   &efFecLeg    = @;

   nSigFactu    = 0;
   aClave       = "";

   nOperacion   = 0;
   nOpMdep      = 0;
   nAlSi        = 0;
   nAlNo        = 0;
   nAlConcepto  = 0;
|FIN;

|INCLUYE i_variar;
|INCLUYE i_varcon;
|INCLUYE i_varlib;

|| ************** Calculos de Pantalla
|CALCULO Eano; |TIPO 7;
 fLaFecha = ~;
 aAlfa1 = fLaFecha; aAlfa1 = aAlfa1 % -104;
 #0#0 = aAlfa1;
 |PINTA #0#0;
|FCAL;

|CALCULO defecto;
 aAlfa1 = #0#0;
 aAlfa2 = "01.01." + aAlfa1; #0#50 = aAlfa2; |PINTA #0#50;
 aAlfa2 = "31.12." + aAlfa1; #0#51 = aAlfa2; |PINTA #0#51;
|FCAL;

|PROCESO Parrilla;
     |SI #0#1 = 0;
         #0#1 = 0;  |PINTA #0#1;
         #0#2 = 0;  |PINTA #0#2;  |C_M #0#2, 1, "N";
         |PARA nCampo = 3;  |SI nCampo [ 42;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |SINO;
         |C_M #0#2, 1, "S";
         |PARA nCampo = 3;  |SI nCampo [ 42;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "N";
               #0nCampo = 0;  |PINTA #0nCampo;
         |SIGUE;
     |FINSI;
|FPRC;

|PROCESO NoMasEmpresas;
     |SI #0Campo = 0;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 42;  |HACIENDO nCampo = nCampo + 1;
               #0nCampo = 0;  |C_M #0nCampo, 1, "N";
         |SIGUE;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 42;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |FINSI;
|FPRC;

|CALCULO LaSeleccion; |TIPO 0;
  |SI #0Campo = "P";
      |C_M #0#48,1,"S"; |C_M #0#49,1,"S";
      |C_M #0#50,1,"N"; |C_M #0#51,1,"N";
      |HAZ defecto;
  |SINO;
      |C_M #0#48,1,"N"; |C_M #0#49,1,"N";
      |C_M #0#50,1,"S"; |C_M #0#51,1,"S";
      #0#48 = 00; #0#49 = 12;
      |PINTA #0#48; |PINTA #0#49;
  |FINSI;
|FCAL;

|CALCULO YaSeCual; |TIPO 7;
 |SI enEmpresa ! 0;
     #0#55 = "S"; |PINTA #0#55;
     |C_M #0#55, 1, "N";
 |FINSI;
|FCAL;

|| **************************************************************************

|PROCESO Auxiliar57;

 |SI nOperacion = 0;
     nCampo = nCampo + 1;
 |FINSI;

 |SI nOperacion = 1;
     nCampo = nCampo + 1;
     nLBase  = #957#1;
     nLTipoI = #957#2;
     nLCuoI  = #957#3;
     nLTipoR = #957#4;
     nLCuoR  = #957#5;
     nLCuoIN = #957#6;
     nLCuoRN = #957#7;
     nLExen = 0;
     |SI nPara1 = 23; |Y nCampo = 1;
         |SI #900#27 ! 0; nLExen = #900#27; |FINSI;
     |FINSI;
     |SI nLCuoI = 0; |Y nLCuoIN = 0; nLTipoI = 0; |FINSI;
     |SI nLCuoR = 0; |Y nLCuoRN = 0; nLTipoR = 0; |FINSI;
     |SI nLBase ! 0; |O nLCuoI ! 0; |O nLCuoR ! 0; |O nLCuoIN ! 0; |O nLCuoRN ! 0; |O nLExen ! 0;
         |PRINT;
         SwLinea = 1;
     |FINSI;
 |FINSI;
|FPRC;

|DEFBUCLE Auxiliar57;
  #957#1;
  ;
  nPara1,  000,  000;
  nPara1, 1000, 1000;
  ;
  NULL, Auxiliar57;
|FIN;

|PROCESO VariasLineas;
 SwLinea = 0;
 nLBasP = #900#66;
 nLTipP = #900#67;
 nLCuoP = #900#68;
 |PARA nPara1 = 23; |SI nPara1 [ 26; |HACIENDO nPara1 = nPara1 + 1;
       nCampo = 0;
       nOperacion = 0; |BUCLE Auxiliar57;
       |SI nCampo [ 1;
           nLBase = #900nPara1;
           nNume1 = nPara1 +  6; nLTipoI = #900nNume1;
           nNume2 = nPara1 + 11; nLCuoI  = #900nNume2;
           nNume3 = nPara1 + 17; nLTipoR = #900nNume3;
           nNume4 = nPara1 + 22; nLCuoR  = #900nNume4;
           nNume5 = nPara1 + 29; nLCuoIN = #900nNume5;
           nNume6 = nPara1 + 35; nLCuoRN = #900nNume6;
           nLExen = 0;
           |SI nPara1 = 23;
               |SI #900#27 ! 0; nLExen = #900#27; |FINSI;
           |FINSI;
           |SI nLCuoI = 0; |Y nLCuoIN = 0; nLTipoI = 0; |FINSI;
           |SI nLCuoR = 0; |Y nLCuoRN = 0; nLTipoR = 0; |FINSI;
           |SI nLBase ! 0; |O nLCuoI ! 0; |O nLCuoR ! 0; |O nLCuoIN ! 0; |O nLCuoRN ! 0; |O nLExen ! 0;
               |PRINT;
               SwLinea = 1;
           |FINSI;
       |SINO;
           nOperacion = 1; |BUCLE Auxiliar57;
       |FINSI;

 |SIGUE;
|FPRC;

|PROCESO LinLibro0;

  aAlfa1 = #caivalin#30; |QBF aAlfa1; |SI aAlfa1 = ""; #caivalin#30 = #camaniva#10; |FINSI;
  |SI aMulDep = "S";
      nDepar = #caivalin#10;
      |SI nDepar = 0;  nDepar = 1;   |FINSI;
      |ABRE #114;
      #114#0 = enEmpresa;
      #114#1 = nDepar;
      |LEE 040#114=;
      |SI FSalida ! 0;  nDepar = 1;  |FINSI;
      |CIERRA #114;

      |SI nDepar < #0#45; |O nDepar > #0#46;  ||desde/hasta actividad
           |ACABA;
      |FINSI;

||       |SI #114#9 ! 6; |Y #114#9 ! 7; |Y #114#9 ! 11;
||          |Y #114#9 ! 12; |Y #114#9 ! 16; |Y #114#9 ! 17;
||            |ACABA;                             || No es actividad Agricola
||      |FINSI;           Tique 1243_2044 11.03.2011
  |FINSI;

  nAlSi = 1;
  |SI nOpMdep = 0;  |ACABA;  |FINSI;  || Solo comprueba que exista

  enConcepto = #caivalin#3;
  |SI enSwSelCp = 1;
      |HAZ CtrlConcepto;
      |SI enSwConcep = 1;
          |ACABA;
      |FINSI;
  |SINO;
      enSwReg = 1; || Agricultura
      |HAZ CtrlConcepReg;
      |SI enSwConcep = 1;
          |ACABA;
      |FINSI;
  |FINSI;

  nAlConcepto = 1;
  |SI nOpMdep = 2;  |ACABA;  |FINSI;  || Solo comprueba que exista

 #caivalin#6  = (#caivalin#6  / enConversor) ! EURODBMOD;
 #caivalin#8  = (#caivalin#8  / enConversor) ! EURODBMOD;
 #caivalin#10 = (#caivalin#10 / enConversor) ! EURODBMOD;
 #caivalin#20 = (#caivalin#20 / enConversor) ! EURODBMOD;
 #caivalin#22 = (#caivalin#22 / enConversor) ! EURODBMOD;

 nCampo = -1;
 |SI #caivalin#7 = 0;               ||  0%
     #900#27 = #900#27 + #caivalin#6;
     #900#28 = #900#28 + #caivalin#6;
 |SINO;
     #900#28 = #900#28 + #caivalin#6;
     |SI #caivalin#5 = "S";
          #900#39 = #900#39 + #caivalin#8;
          |SI #caivalin#7 = 4;         ||  4%
              #900#23 = #900#23 + #caivalin#6;
              #900#29 = #caivalin#7;
              #900#34 = #900#34 + #caivalin#8;
              nCampo = 23;
          |SINO;
              |SI #caivalin#7 = 7; |O #caivalin#7 = 8; |O #caivalin#7 = 10;         ||  7% 8% 10%
                  #900#24 = #900#24 + #caivalin#6;
                  #900#30 = #caivalin#7;
                  #900#35 = #900#35 + #caivalin#8;
                  nCampo = 24;
              |SINO;
                  |SI #caivalin#7 = 16;  |O #caivalin#7 = 18; |O #caivalin#7 = 21;  || 16% 18% 21%
                      #900#25 = #900#25 + #caivalin#6;
                      #900#31 = #caivalin#7;
                      #900#36 = #900#36 + #caivalin#8;
                      nCampo = 25;
                  |SINO;
                      #900#26 = #900#26 + #caivalin#6;   || ??%
                      #900#32 = #caivalin#7;
                      #900#37 = #900#37 + #caivalin#8;
                      nCampo = 26;
                  |FINSI;
              |FINSI;
          |FINSI;
      |SINO;                                    ||NO DEDUCIBLE
          #900#57 = #900#57 + #caivalin#8;
          |SI #caivalin#7 = 4;         ||  4%
              #900#23 = #900#23 + #caivalin#6;
              #900#29 = #caivalin#7;
              #900#52 = #900#52 + #caivalin#8;
              nCampo = 23;
          |SINO;
              |SI #caivalin#7 = 7;  |O #caivalin#7 = 8;  |O #caivalin#7 = 10;    ||  7% 8% 10%
                  #900#24 = #900#24 + #caivalin#6;
                  #900#30 = #caivalin#7;
                  #900#53 = #900#53 + #caivalin#8;
                  nCampo = 24;
              |SINO;
                  |SI #caivalin#7 = 16; |O #caivalin#7 = 18; |O #caivalin#7 = 21; || 16% 18% 21%
                      #900#25 = #900#25 + #caivalin#6;
                      #900#31 = #caivalin#7;
                      #900#54 = #900#54 + #caivalin#8;
                      nCampo = 25;
                  |SINO;
                      #900#26 = #900#26 + #caivalin#6;   || ??%
                      #900#32 = #caivalin#7;
                      #900#55 = #900#55 + #caivalin#8;
                      nCampo = 26;
                  |FINSI;
              |FINSI;
          |FINSI;
     |FINSI;
  |FINSI;

  |SI #caivalin#9 ! 0; |Y #caivalin#5 = "S";
      #900#50 = #900#50 + #caivalin#10;
      |SI #caivalin#9 = 0.5;            || 0.5%
          #900#40 = #caivalin#9;
          #900#45 = #900#45 + #caivalin#10;
      |SINO;
          |SI #caivalin#9 = 1;  |O #caivalin#9 = 1.40;    ||
              #900#41 = #caivalin#9;
              #900#46 = #900#46 + #caivalin#10;
         |SINO;
              |SI #caivalin#9 = 4; |O #caivalin#9 = 5.20; || 4
                 #900#42 = #caivalin#9;
                 #900#47 = #900#47 + #caivalin#10;
             |SINO;
                 #900#43 = #caivalin#9;
                 #900#48 = #900#48 + #caivalin#10; || %
             |FINSI;
         |FINSI;
     |FINSI;
 |FINSI;
 |SI #caivalin#9 ! 0; |Y #caivalin#5 = "N";
     #900#63 = #900#63 + #caivalin#10;
     |SI #caivalin#9 = 0.5;        || 0.5%
         #900#40 = #caivalin#9;
         #900#58 = #900#58 + #caivalin#10;
     |SINO;
         |SI #caivalin#9 = 1; |O #caivalin#9 = 1.40;   ||
             #900#41 = #caivalin#9;
             #900#59 = #900#59 + #caivalin#10;
         |SINO;
             |SI #caivalin#9 = 4; |O #caivalin#9 = 5.20; || 4%
                 #900#42 = #caivalin#9;
                 #900#60 = #900#60 + #caivalin#10;
             |SINO;
                 #900#43 = #caivalin#9;
                 #900#61 = #900#61 + #caivalin#10; || %
             |FINSI;
         |FINSI;
     |FINSI;
 |FINSI;

 #900#51 = #900#51 + #caivalin#6 + #caivalin#8 + #caivalin#10;

 #900#66 = #900#66 + #caivalin#20;
 #900#67 = #caivalin#21;
 #900#68 = #900#68 + #caivalin#22;

|| ... Graba Detalle %
     |SI nCampo ! -1;
         |ABRE #957;
         #957#0 = nCampo;
         #957#2 = #caivalin#7;
         #957#4 = #caivalin#9;
         |LEE 041#957=;
         |SI FSalida ! 0;
             |DDEFECTO #957;
             #957#0 = nCampo;
             #957#2 = #caivalin#7;
             #957#4 = #caivalin#9;
             |GRABA 020#957c;
         |FINSI;
         #957#1  = #957#1 + #caivalin#6;
         #957#2  = #caivalin#7;
         #957#4  = #caivalin#9;
         |SI #caivalin#5 = "S";
             #957#3  = #957#3 + #caivalin#8;
             #957#5  = #957#5 + #caivalin#10;
         |SINO;
             #957#6  = #957#6 + #caivalin#8;
             #957#7  = #957#7 + #caivalin#10;
         |FINSI;
         |GRABA 020#957a;
         |CIERRA #957;
     |FINSI;
|FPRC;

|DEFBUCLE LinLibro;
 #caivalin#1;
 ;
 #camaniva#0,#camaniva#1,001;
 #camaniva#0,#camaniva#1,999;
 ;
 NULL, LinLibro0;
|FIN;

|PROCESO Facturas;

   ||..... Seleccion de Actividad Cabecera
    |SI aMulDep ! "S";
        nDepar = #304#10;
        |SI nDepar = 0;  nDepar = 1;   |FINSI;
        |ABRE #114;
        #114#0 = enEmpresa;
        #114#1 = nDepar;
        |LEE 040#114=;
        |SI FSalida ! 0;  nDepar = 1;  |FINSI;
        |CIERRA #114;

        |SI nDepar < #0#45; |O nDepar > #0#46;  ||desde/hasta actividad
             |ACABA;
        |FINSI;

||        |SI #114#9 ! 6; |Y #114#9 ! 7; |Y #114#9 ! 11;
||            |Y #114#9 ! 12; |Y #114#9 ! 16; |Y #114#9 ! 17;
||              |ACABA;                             || No es actividad Agricola
||        |FINSI;     || Tique 1243_2044 11.03.2011
    |SINO;
          nOpMdep = 0;
          nAlSi   = 0;
          nAlNo   = 0;
          |BUCLE LinLibro;
          |SI nAlSi = 0; |ACABA; |FINSI;
    |FINSI;

|| ... Comprueba que los conceptos de facturas son buenos
|| ... Siempre   |SI enSwSelCp = 1; |O nEspecial ! 0;
        nOpMdep     = 2;
        nAlConcepto = 0;
        |BUCLE LinLibro;
        |SI nAlConcepto = 0; |ACABA; |FINSI;
||   |FINSI;

    nOpMdep = 1;
    |PDEFECTO #900, 16, 64;
    #900#66 = 0; #900#67 = 0; #900#68 = 0;
    #900#79 = "01.01.0000";
    #900#81 = "01.01.0000";
    #900#82 = "";
    #900#87 = 0;
    #900#88 = 0;
    |DELINDEX #957;
/*
    enConcepto = #camaniva#27;
    |SI enSwSelCp = 1;
       |HAZ CtrlConcepto;
       |SI enSwConcep = 1;
           |ACABA;
       |FINSI;
    |SINO;
       enSwReg = 1; || Agricultura
       |HAZ CtrlConcepReg;
       |SI enSwConcep = 1;
           |ACABA;
       |FINSI;
    |FINSI;
*/

    |SI #0#61 = 0;
        #900#16 = #camaniva#2;
        #900#17 = #camaniva#3;
    |SINO;
        #900#16 = "";
        #900#17 = #0#61;
        #0#61 = #0#61 + 1;
    |FINSI;

    fLaFecha = #camaniva#4; |SI #0#60 = 2; fLaFecha = #camaniva#8; |FINSI;
    aAlfa1   = fLaFecha; aAlfa1   = aAlfa1 % 105;
    #900#18  = aAlfa1;
    #900#79  = fLaFecha;
    |SI #camaniva#41 > "01.01.0000";
        #900#81  = #camaniva#41;
        aAlfa1   = #900#81; aAlfa1   = aAlfa1 % 105;
        #900#82  = aAlfa1;
    |FINSI;

    #900#19  = #camaniva#12;
    #900#20  = #camaniva#13;
    #900#21  = #camaniva#14;
    #900#22  = #camaniva#28;
    #900#87  = #camaniva#5;
    #900#88  = #camaniva#40;

    |BUCLE LinLibro;

    |HAZ VariasLineas;
    SwHayDatos = 1;
    SwPrim     = 1;

  #998#5  = #998#5  + #900#28;
  #998#6  = #998#6  + #900#39;
  #998#7  = #998#7  + #900#50;
  #998#8  = #998#8  + #900#51;
  #998#27 = #998#27 + #900#57;
  #998#28 = #998#28 + #900#63;
|FPRC;

|DEFBUCLE Facturas1;
 #camaniva#5;
 36,5;
 fDFecha, #0#52, 0000000001, aDTipo, nDPerio;
 fHFecha, #0#53, 9999999999, aHTipo, nHPerio;
 ;
 NULL, Facturas;
|FIN;

|DEFBUCLE Facturas2;
 #camaniva#1;
 8,36,5;
 #0#52,0000000001, fDFecha, aDTipo, nDPerio;
 #0#53,9999999999, fHFecha, aHTipo, nHPerio;
 ;
 NULL,NULL,NULL,NULL,NULL,Facturas;
 #camaniva#8, #camaniva#0, #camaniva#1;
|FIN;

|DEFBUCLE Facturas3;
 #camaniva#3;
 8,36,5;
 #0#52,"     ", 000001, fDFecha, aDTipo, nDPerio;
 #0#53,"zzzzz", 999999, fHFecha, aHTipo, nHPerio;
 ;
 NULL, Facturas;
|FIN;

|PROCESO Emision;
   enSwPartido = 0;
   enEmpresa  = #999#1;
   enPerConta = #999#2;
   eaLaMonEmp = #999#13;
   |HAZ EnConversor;
   enEjer     = #999#3;   || A¤o
   dig1       = #999#4;   || Desde Mes
   dig3       = #999#5;   || N§ Digitos
   dig4       = #999#6;   || Digitos Nivel 1
   dig5       = #999#7;   || Digitos Nivel 2
   dig6       = #999#8;   || Digitos Nivel 3
   dig7       = #999#9;   || Digitos Nivel 4
   dig8       = #999#10;  || Digitos Nivel 5
   dig9       = #999#11;  || Digitos Nivel 6
   eaEstadoEmp = #999#12; || Estado
   eaNombreEmp = #999#14; || Nombre Empresa

   |SI #999#4 ! 1;
        enSwOpPar   = 0;
        |HAZ SituaEmpresa;
        enPerConta = enPeriodoPar;
        #999#15 = aPathConta;
   |FINSI;

   aPathConta  = #999#15; |QBF aPathConta; || Path
   || Recoger informacion sobre los niveles ...
   |SI dig3 = 1; MaximoDigitos = dig4; |FINSI;
   |SI dig3 = 2; MaximoDigitos = dig5; |FINSI;
   |SI dig3 = 3; MaximoDigitos = dig6; |FINSI;
   |SI dig3 = 4; MaximoDigitos = dig7; |FINSI;
   |SI dig3 = 5; MaximoDigitos = dig8; |FINSI;
   |SI dig3 = 6; MaximoDigitos = dig9; |FINSI;

   |DDEFECTO #998;
   eaTituloL = #0#56; ||Titulo del Libro
   |SI #0#57 = "S";
       |SUB_EJECUTA_NP ":basica/agiy0016";
   |FINSI;

   |PATH_DAT #301 aPathConta;
   |PATH_DAT #304 aPathConta;
   |PATH_DAT #305 aPathConta;
   |PATH_DAT #306 aPathConta;
   |ABRE #caivaasi;
   |DDEFECTO #caivaasi;
   |LEE 000#caivaasi.p;
   |CIERRA #caivaasi;
   aMulDep = #caivaasi#132;

   |DDEFECTO #306;  ||Configuracion de Informes
   |ABRE #306;
   #306#0 = 1;
   |LEE 000#306=;
   |CIERRA #306;

   enActividad = 0;

   |HAZ LeeDatosEmpresa;

   informe = "dsmoy032";
   |SI #306#4 = "N";
       aAlfa1  = informe % 306;
       informe = "dr" + aAlfa1;
   |FINSI;

   |INFOR informe;
   |SI FSalida ! 0;  |ACABA;  |FINSI;

   SwHayDatos = 0;
   SwPrim     = 0;
   nFactura   = #0#61;
   nPagina    = 1;
   nPagDup = nPagina -1;
   nSwPagDup = 0;

   |SI #0#60 = 1; |BUCLE Facturas1; |FINSI;
   |SI #0#60 = 2; |BUCLE Facturas2; |FINSI;
   |SI #0#60 = 3; |BUCLE Facturas3; |FINSI;

   |SI SwHayDatos = 1;
       |PIEINF;
   |FINSI;
   |FININF;
   |SI enSwPartido = 1;
       enSwOpPar   = 1;
       |HAZ FinalPartido;
   |FINSI;
|FPRC;

|DEFBUCLE dsmow043;
 #dsmow043#1;
 16;
 000001, "S";
 999999, "S";
 ;
 NULL, Emision;
|FIN;

|PROCESO Imprimir;

    |MENAV "0000 Recuerde seleccionar el papel de la impresora en Horizontal";

    |IMPRE 0;
    |SI FSalida ! 0;  |ACABA;  |FINSI;

    aDTipo = "S"; aHTipo = "S";

    |BUCLE dsmow043;
    |FINIMP;
|FPRC;

|| *************************************************************************
|CALCULO Datos7; |TIPO 7;
 |MENSA "    <F3> Cambiar Emitir Libro S/N";
 |ATRI R; |PINTA 1169, "Imp"; |ATRI 0;
|FCAL;

|CALCULO DatosManip; |TIPO 11;
   nInkey = FSalida;
   |BLIN 04;
   |SI nInkey = 11;
       |SI #999#16 = "S";
           #999#16 = "N";
       |SINO;
           #999#16 = "S";
       |FINSI;
       |GRABA 020#999a;
   |FINSI;
|FCAL;

|RUTINA inferior;
   #999#0 = 00001;
|FRUT;

|RUTINA superior;
   #999#0 = 99999;
|FRUT;

|| *************************************************************************
|| -----------------------------------------------------------------------
|PROCESO ActAgric0;
 |SI #114#9 = 6; |O #114#9 = 7; |O #114#9 = 11; |O #114#9 = 12;
     |O #114#9 = 16; |O #114#9 = 17;
     SwAgricola = 1;
     |ERROR10;
 |FINSI;
|FPRC;

|DEFBUCLE ActAgric;
  #114#1;
  ;
  enEmpresa, 00;
  enEmpresa, 99;
  e;
  NULL, ActAgric0;
|FIN;

|| -----------------------------------------------------------------------
|| ....................... Situa la Empresa Contable
|PROCESO LEmpresaConta;

     |SI #canempre#21 = "I";
         |SI #0#55 = "N"; |ACABA; |FINSI;
     |FINSI;

     enEmpresa   = #canempre#2;
     enPerConta  = #canempre#3;;   || Periodo Contable
     eaNombreEmp = #canempre#1;    ||Empresa

||     SwAgricola = 0;         Tique 1243_2044 (11.03.2011)
||     |BUCLE ActAgric;
||     |SI SwAgricola = 0; |ACABA; |FINSI;

|| ------------------------------------------------------------------------
   aAlfa1 = enEmpresa;  aAlfa1 = ("00000" + aAlfa1) % -105;
   aAlfa2 = enPerConta; aAlfa2 = ("0" + aAlfa2) % -101;
   aPathConta  = aPath + "contagen/fich/" + aAlfa1 + aAlfa2 + "/";

   nGTempo = nGTempo + 1;
   #999#0 = nGTempo;       || Guia
   #999#1 = enEmpresa;     || Empresa
   #999#2 = enPerConta;    || Periodo Contable
   #999#3 = enEjer;        || A¤o
   #999#4 = #canempre#11;  || Desde Mes
   #999#5 = #canempre#13;  || N§ Digitos
   #999#6 = #canempre#14;  || Digitos Nivel 1
   #999#7 = #canempre#15;  || Digitos Nivel 2
   #999#8 = #canempre#16;  || Digitos Nivel 3
   #999#9 = #canempre#17;  || Digitos Nivel 4
   #999#10 = #canempre#18; || Digitos Nivel 5
   #999#11 = #canempre#19; || Digitos Nivel 6
   eaEstadoEmp = "S"; |SI #canempre#21 = "I"; eaEstadoEmp = "N"; |FINSI;
   #999#12 = eaEstadoEmp;  || Estado
   #999#13 = #canempre#28; || Moneda
   #999#14 = #canempre#1;  || Nombre Empresa
   #999#15 = aPathConta;   || Path
   #999#16 = #0#62;        || Emitir
   |GRABA 020#999n;
|FPRC;

|DEFBUCLE canempre;
 #300#4;
 ;
 nDEmpresa, nEjercicio;
 nHEmpresa, nEjercicio;
 ;
 NULL, LEmpresaConta;
|FIN;
|| **************************************************************************
||///////////////////////////////////////////////////////////////////////////

|CALCULO Tipo3;  |TIPO 3;

    #0#59  = "S";      ||Soportado
    enModelo = #0#58;  ||Libro Oficial
    |HAZ LeeCtrlLibro;

    enEjer = #0#0;
    aAlfa1 = enEjer; |QBF aAlfa1;
    aAlfa1 = aAlfa1 % -102;
    nEjercicio = aAlfa1;
    aAlfa  = #0#0;

    fDFecha  = "01.01.1900";
    fHFecha  = "31.12.2090";
    nDPerio  = 0;
    nHPerio  = 99;
    |SI #0#47 = "F";
        fDFecha  = #0#50;
        fHFecha  = #0#51;
        efDFecha = fDFecha;
        efHFecha = fHFecha;
    |SINO;
        nDPerio  = #0#48;
        nHPerio  = #0#49;
        aAlfa2 = #0#0;
        |SI #0#0 [ 2009;
             aAlfa1 = "01.01." + aAlfa2;
             |SI nDPerio = 2; aAlfa1 = "01.04." + aAlfa2; |FINSI;
             |SI nDPerio = 3; aAlfa1 = "01.07." + aAlfa2; |FINSI;
             |SI nDPerio = 4; aAlfa1 = "01.10." + aAlfa2; |FINSI;
             efDFecha = aAlfa1;

             aAlfa1 = "31.03." + aAlfa2;
             |SI nHPerio = 2; aAlfa1 = "30.06." + aAlfa2; |FINSI;
             |SI nHPerio = 3; aAlfa1 = "30.09." + aAlfa2; |FINSI;
             |SI nHPerio = 4; aAlfa1 = "31.12." + aAlfa2; |FINSI;
             efHFecha = aAlfa1;
        |SINO;
            aAlfa1 = "01.01." + aAlfa2;
            |SI nDPerio = 4;  |O nDPerio = 5;  |O nDPerio = 6;  aAlfa1 = "01.04." + aAlfa2; |FINSI;
            |SI nDPerio = 7;  |O nDPerio = 8;  |O nDPerio = 9;  aAlfa1 = "01.07." + aAlfa2; |FINSI;
            |SI nDPerio = 10; |O nDPerio = 11; |O nDPerio = 12; aAlfa1 = "01.10." + aAlfa2; |FINSI;
            efDFecha = aAlfa1;

            aAlfa1 = "31.03." + aAlfa2;
            |SI nHPerio = 4;  |O nDPerio = 5;  |O nDPerio = 6;  aAlfa1 = "30.06." + aAlfa2; |FINSI;
            |SI nHPerio = 7;  |O nDPerio = 8;  |O nDPerio = 9;  aAlfa1 = "30.09." + aAlfa2; |FINSI;
            |SI nHPerio = 10; |O nDPerio = 11; |O nDPerio = 12; aAlfa1 = "31.12." + aAlfa2; |FINSI;
            efHFecha = aAlfa1;
         |FINSI;
     |FINSI;

    aFichero = ("3d" + Usuario) % 108;
    |NOME_DAT #957 aFichero;

    |DBASS aPath; |QBT aPath;
    aFichero = ("043w" + Usuario) % 108;
    |NOME_DAT #999 aFichero;
    |ABRE #999;  |CIERRA #999;  |DELINDEX #999;
    eaFEmpresa = aFichero;

    |ABRE #999;
    nGTempo = 0;   ||Busco Empresas Contables
    |SI #0#1 ! 0;
        nDEmpresa = #0#1;
        nHEmpresa = #0#2;
        |BUCLE canempre;
    |SINO;
        |PARA nPara1 = 3;  |SI nPara1 [ 42;  |HACIENDO nPara1 = nPara1 + 1;
              nDEmpresa = #0nPara1;
              nHEmpresa = #0nPara1;
              |BUCLE canempre;
        |SIGUE;
    |FINSI;
    |CIERRA #999;

    |SI nGTempo = 0;    || No existe ninguna empresa
        |SI aParam = "";
            |MENAV "    Error. No existe ninguna Empresa Contable dentro de la Seleccion ";
        |FINSI;
        |DELINDEX #999;
        |ACABA;
    |FINSI;

    |C_V #999#12,1,"N"; |C_P #999#16,1,1270;

    |SI aParam = "";
        |PUSHV 0509 2375;
        |ENTLINEAL #1#999, -1, 2, 22, 1, inferior, superior;
        |POPV;
    |FINSI;

    |HAZ Imprimir;

    |DELINDEX #957;
    |DELINDEX #999;
|FCAL;

|PROCESO LeeDatosEmpresa;
  |DDEFECTO #900;
  #900#0 = enEmpresa;
  enEjer = #0#0;
  eaInac = #0#24;
  eaTituloL = #0#56;
  |HAZ DatosEmpr_w30;
|FPRC;
