|FICHEROS;
   dsmoy066 #0;

   dsmom041 #41;
   dsmom046 #46;
   dsmom049 #49;

   :/basica/def/agicen14 #114;
   :/basica/def/agifigen #118;
   :/basica/def/agim0019 #119;

   :/contagen/def/canempre #300;
   :/contagen/def/caconimp #306;  || configuracion impresiones

   dsmow030 #900;          ||Def de impresion
   dsmow043 #999,MANTE;
|FIN;

|VARIABLES;
   &entrada = 1;
   informe = "";
   inform1 = "";
   inform2 = "";
   nEstado = 0;

   nInkey      = 0;
   aFichero    = "";
   nCampo      = 0;
   nDEmpresa   = 0;
   nHEmpresa   = 0;
   nDActividad = 0;
   nHActividad = 0;
   nDComAct    = 0;
   nHComAct    = 0;
   aDNombre    = "";
   aHNombre    = "";
   nEjercicio  = 0;

   nLaEmpresa  = 0;
   nGTempo     = 0;

   nSPin1      = 0;
   aPath       = "";

   aAlfa0 = "";
   aAlfa1 = "";
   aAlfa2 = "";
   aAlfa3 = "";
   nPara1 = 0;
   nPara2 = 0;
   nNume1 = 0;
   nNume2 = 0;
   nNume3 = 0;
   nNume4 = 0;

   nDesAny     = 0;
   nHasAny     = 0;
   nSwRegul    = 0;
   nSwOp       = 0;
   nSwOpLinea  = 0;
   nSwAct      = 0;
   nSwDesglose = 0;
   nTpCien     = 100;

   aDTipo      = "";
   aHTipo      = "";
   SwHayDatos  = 0;
   &pagina     = 0;
   sw_pagina   = 0;

   fDFecha     = @;
   fHFecha     = @;
   fLaFecha    = @;

   &nPagina    = 0;
   &nPagDup    = 0;
   &nSwPagDup  = 0;
   &SwPrim     = 0;
   &SwLinea    = 0;
   &nLCuoI     = 0;

   aParam       = "";
   &efFechaEmi  = @;
   &enEjercicio = 0;
   &enBasImp    = 0;
   &eaNomImp    = "";
   Comodin      = "";
   Comodin1     = "";
   aAlfa        = "";
   Handle       = 0;
   Numeracion   = 0;

   &eaImpresora = "";
   &eaPortada   = "";
|FIN;

|INCLUYE i_variar;
|INCLUYE i_varcon;
|INCLUYE i_varlib;
|INCLUYE i_numamo;
|INCLUYE dslegv_i;

|| //////////////////// Calculos de Pantalla \\\\\\\\\\\\\\\\\\\\\\\\\\

|CALCULO Eano; |TIPO 7;
 |SI nEstado ! 0;
     fLaFecha = ~;
     aAlfa1 = fLaFecha; aAlfa1 = aAlfa1 % -104;
     #0#1 = aAlfa1;
     |PINTA #0#1;
 |FINSI;
|FCAL;

|CALCULO Tipo7;  |TIPO 7;
     |SI enEmpresa ! 0;
         #0#2  = enEmpresa;  |PINTA #0#2;
         #0#3  = enEmpresa;  |PINTA #0#3;

         |C_M #0#2, 1, "N";
         |C_M #0#3, 1, "N";
     |FINSI;
|FCAL;

|CALCULO Parrilla; |TIPO 0;
     |SI #0#2 = 0;
         #0#2 = 0;  |PINTA #0#2;
         #0#3 = 0;  |PINTA #0#3;  |C_M #0#3, 1, "N";
         |PARA nCampo = 4;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |SINO;
         |SI enEmpresa = 0;
             |C_M #0#2, 1, "S";
             |C_M #0#3, 1, "S";
         |FINSI;

         |PARA nCampo = 4;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "N";
               #0nCampo = 0;  |PINTA #0nCampo;
         |SIGUE;
     |FINSI;
|FCAL;

|CALCULO NoMasEmpresas; |TIPO 0;
     |SI #0Campo = 0;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
               #0nCampo = 0;  |C_M #0nCampo, 1, "N";
         |SIGUE;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |FINSI;
|FCAL;

|CALCULO YaSeCual; |TIPO 7;
 |SI enEmpresa ! 0;
     #0#54 = "S"; |PINTA #0#54;
     #0#24 = "S"; |PINTA #0#24;
     #0#25 = 00;  |PINTA #0#25;
     #0#26 = 99;  |PINTA #0#26;
     |C_M #0#24, 1, "N";
     |C_M #0#25, 1, "N";
     |C_M #0#26, 1, "N";
     |C_M #0#54, 1, "N";
 |FINSI;
|FCAL;

|CALCULO SelTipo0; |TIPO 0;
 |SI #0#54 = "N";
     #0#25 = 0;   |PINTA #0#25;  |C_M #0#25, 1, "N";
     #0#26 = 99;  |PINTA #0#26;  |C_M #0#26, 1, "N";
     |PARA nCampo = 27;  |SI nCampo [ 36;  |HACIENDO nCampo = nCampo + 1;
           |C_M #0nCampo, 1, "S";
     |SIGUE;
 |SINO;
     |SI enEmpresa = 0;
         |C_M #0#25, 1, "S";
         |C_M #0#26, 1, "S";
     |FINSI;
     |PARA nCampo = 27;  |SI nCampo [ 36;  |HACIENDO nCampo = nCampo + 1;
           |C_M #0nCampo, 1, "N";
           #0nCampo = 0;  |PINTA #0nCampo;
     |SIGUE;
 |FINSI;
|FCAL;

|CALCULO SelTipo1;
 |SI #0Campo = 0;
     |PARA nCampo = Campo + 1;  |SI nCampo [ 36;  |HACIENDO nCampo = nCampo + 1;
           #0nCampo = 0;  |C_M #0nCampo, 1, "N";
     |SIGUE;
 |SINO;
     |PARA nCampo = Campo + 1;  |SI nCampo [ 36;  |HACIENDO nCampo = nCampo + 1;
           |C_M #0nCampo, 1, "S";
     |SIGUE;
 |FINSI;
|FCAL;

|CALCULO SelAct; |TIPO 0;
 aAlfa1 = #0Campo;
 |C_M #0#56,1, aAlfa1;
 |C_M #0#57,1, aAlfa1;
|FCAL;

|CALCULO numamo; |TIPO 0;
   x_alfa1 = #0Campo;
   |HAZ x_punto;
   #0Campo = x_alfa1;
   |PINTA #0Campo;
|FCAL;

|| *************************************************************************
|CALCULO &c_pagina;
   |SI sw_pagina = 0;
      pagina = #0#7 + 1;
      sw_pagina = 1;
   |SINO;
      pagina = pagina + 1;
   |FINSI;
|FCAL;

|| *************************************************************************
|| *************************************************************************
|PROCESO VerRegulAnyo;
  |SI #49#11 = "R"; |Y #49#10 ! 0;
      nSwRegul = 1;
  |FINSI;
  |SI nSwOpLinea = 0; |ACABA; |FINSI;

  |SI #0#55 = "S";
      |SI nTpCien ! 100; |Y nTpCien ! 0;
          #49#10 = (#49#10 % nTpCien) ! 2;
      |FINSI;
  |FINSI;

  |SI #49#5 ! #0#1;
       |SI SwLinea = 0; SwLinea = 1; |PRINT; |FINSI;
       SwLinea = 2;
  |SINO;
       |SI SwLinea = 0; SwLinea = 1; |PRINT; |FINSI;
       |SI #41#26 > "01.01.0000";
           |SI #41#26 [ efFecTope;
               SwLinea = 3;
           |SINO;
               SwLinea = 2;
           |FINSI;
       |SINO;
           SwLinea = 2;
       |FINSI;
       #900#39 = #900#39 + #49#10;
  |FINSI;
  |SI SwLinea = 3; |O #49#10 ! 0;
      |PRINT;
  |FINSI;
|FPRC;

|DEFBUCLE VerRegulAnyo;
  #49#2;
  ;
  #41#0, #41#2, #41#3, nDesAny, " ";
  #41#0, #41#2, #41#3, nHasAny, "z";
  e;
  NULL, VerRegulAnyo;
|FIN;

|PROCESO dsmom046;
  nSwDesglose = 1;
  |SI nDComAct [ #46#1; |Y nHComAct ] #46#1;
      nSwAct = 1;
      nTpCien = nTpCien + #46#5;
  |FINSI;
|FPRC;

|DEFBUCLE dsmom046;
  #46#1;
  ;
  #41#0, #41#2, #41#3, 00, "   ";
  #41#0, #41#2, #41#3, 99, "zzz";
  ;
  NULL, dsmom046;
|FIN;

|PROCESO LeeElemento;

   nSwAct      = 0;
   nSwDesglose = 0;
   |SI #0#55 = "S";
       nTpCien = 0;
       |BUCLE dsmom046;
   |FINSI;
   |SI nSwDesglose = 0;
       nTpCien     = 100;
       |SI nDComAct [ #41#1; |Y nHComAct ] #41#1;
           nSwAct = 1;
       |FINSI;
   |FINSI;
   |SI nSwAct = 0; |ACABA; |FINSI;

   |SI #41#27 > "01.01.0000";
       |SI #41#27 > efFecTope; |ACABA; |FINSI;
   |FINSI;

   nSwOpLinea = 0;
   nSwRegul = 0;
   nDesAny  = #0#1;
   nHasAny  = #0#1;
   |BUCLE VerRegulAnyo;
   |SI nSwRegul = 0; |ACABA; |FINSI;

   SwHayDatos = 1;
   |SI nSwOp = 0;
       |ERROR10;
       |ACABA;
   |FINSI;

   |SI #0#55 = "S";
       |SI nTpCien ! 100; |Y nTpCien ! 0;
           #41#10 = (#41#10 % nTpCien) ! 2;
           #41#43 = (#41#43 % nTpCien) ! 2;
       |FINSI;
   |FINSI;
   #900#8 = ( (#900#6 % #900#11) ) % #900#7;
   nLCuoI = (#41#10 % #41#46 ) ! 2;

   SwLinea     = 0; |PRINT;
   nSwRegul    = 0;
   nSwOpLinea = 1;
   nDesAny  = #0#1; |SI #0#50 = "T"; nDesAny = 0; |FINSI;
   nHasAny  = #0#1;
   |BUCLE VerRegulAnyo;
   |SI SwLinea ! 0;
       SwLinea = 4; |PRINT;
   |FINSI;
|FPRC;

|DEFBUCLE dsmom041_F;
   #41#1;
   41;
   enEmpresa, #0#39, #0#41, "S";
   enEmpresa, #0#40, #0#42, "S";
   ;
   NULL, NULL, NULL, NULL, NULL, LeeElemento;
   #41#0, #41#27, #41#2, #41#3;
|FIN;

|DEFBUCLE dsmom041_N;
   #41#2;
   41;
   enEmpresa, aDNombre, "S";
   enEmpresa, aHNombre, "S";
   ;
   NULL, LeeElemento;
|FIN;

|DEFBUCLE dsmom041_C;
   #41#1;
   41;
   enEmpresa, #0#39, #0#41, "S";
   enEmpresa, #0#40, #0#42, "S";
   ;
   NULL, LeeElemento;
|FIN;

|| **********************************************************************************
|PROCESO LeeDatosEmpresa;
    |DDEFECTO #900;
    eaIna   c = #0#24;
    eaTituloL = #0#52;
    enEjer    = #0#1;                                   || Ejercicio
    #900#0    = enEmpresa;
    |HAZ DatosEmpr_w30;
    |SI aParam = "LEGALIA";
        |SI #900#13 < #900#9;  |Y #900#9  > "01.01.0000"; #900#13 = #900#9;  |FINSI;
        |SI #900#14 > #900#10; |Y #900#10 > "01.01.0000"; #900#14 = #900#10; |FINSI;
    |FINSI;
|FPRC;

|PROCESO Emision;
   enEmpresa  = #999#1;
   enEjer     = #999#3;   || A¤o
   eaEstadoEmp = #999#12; || Estado
   eaNombreEmp = #999#14; || Nombre Empresa
   aPathConta  = #999#15; |QBF aPathConta; || Path

   |SI aPathConta ! "";
       |PATH_DAT #306 aPathConta;
       |DDEFECTO #306;  ||Configuracion de Informes
       |ABRE #306;
        #306#0 = 1;
       |LEE 000#306=;
       |CIERRA #306;
   |SINO;
       #306#4 = "N";
   |FINSI;
   |SI aParam ! ""; #306#4 = "N"; |FINSI;

   eaPortada = "N";
   eaTituloL = #0#52; ||Titulo del Libro

   aAlfa0 = Impresora % 113;
   |SI #0#37 = "S"; |O aAlfa0 = "CrystalReport";
       eaImpresora = Impresora;
       |SI aAlfa0 = "CrystalReport"; eaImpresora = aAlfa0; |FINSI;
       |SUB_EJECUTA_NP ":basica/agiy0016";
       eaImpresora = "";
       |SI aAlfa0 = "CrystalReport"; |HAZ PortadaCrystal; |FINSI;
       eaPortada = #0#37;
   |FINSI;

   nDActividad = 0; nHActividad = 0;
   |SI #0#55 = "S"; nDActividad = #0#56; nHActividad = #0#57;  |FINSI;

   |PARA enActividad = nDActividad; |SI enActividad [ nHActividad; |HACIENDO enActividad = enActividad + 1;

         |HAZ LeeDatosEmpresa;
         |SI  swNoExis = 0;       ||SI existe esta Actividad.

              informe = inform1;
              |SI #306#4 = "N"; informe = inform2; |FINSI;

              |SI #0#58 ! "N";
                 aAlfa = informe; |QBF aAlfa;
                 aAlfa = (aAlfa % 0103) + "ap" + (aAlfa % 0603);
                 informe = aAlfa;
              |FINSI;

              |INFOR informe;
              |SI FSalida ! 0;  |ACABA;  |FINSI;

              SwHayDatos = 0;
              SwPrim     = 0;
              nPagina = #0#51;
              nPagDup = nPagina -1;
              nSwPagDup = 0;

              nDComAct = 00;
              nHComAct = 99;
              |SI enActividad ! 0;
                  nDComAct = enActividad;
                  nHComAct = enActividad;
              |FINSI;

              nSwOp = 1;
              |SI #0#44 = 1; |BUCLE dsmom041_C; |FINSI;
              |SI #0#44 = 2; |BUCLE dsmom041_N; |FINSI;
              |SI #0#44 = 3; |BUCLE dsmom041_F; |FINSI;

              |SI SwHayDatos = 1;
                  |PIEINF;
                  |FININF;
              |SINO;
                  |FININF;
              |FINSI;
         |FINSI;
     |SIGUE;
|FPRC;

|DEFBUCLE dsmow043;
   #dsmow043#1;
   16;
   000001, "S";
   999999, "S";
   ;
   NULL, Emision;
|FIN;

|PROCESO Imprimir;
    |HAZ LeeUnidadBasico;

    |SI aParam ! "";
        |SI eaFormato ! "S";
            Impresora = eaNomImp + "?" + eaUnidadBTmp + "\tmp1,,,?Facturas Inversion";
            inform1   = "drmoy066";
            inform2   = "drmoy066";
        |SINO;
            Impresora = eaNomImp + ";" + eaUnidadBTmp + "\FacInver.pdf";
            inform1   = "ccmoy066";
            inform2   = "ccmoy066";
        |FINSI;
        |SI enBasImp ! 0;
            |IMPRE -1;
        |SINO;
            |IMPRE 0;
        |FINSI;
    |SINO;
        Impresora = "Crystal Reports";
        inform1   = "ccmoy066";
        inform2   = "ccmoy066";

        |IMPRE 0;
        |SI FSalida ! 0;  |ACABA;  |FINSI;

        |SI Impresora ! "CrystalReport";
            |MENAV "0000 Recuerde seleccionar el papel de la impresora en Horizontal";
            inform1   = "dsmoy066";
            inform2   = "drmoy066";
        |SINO;
            #0#55 = "N";
            #0#56 = 01;
            #0#57 = 99; || si elige crystal, es oficial, por tanto no puede sacar por actividades
        |FINSI;
    |FINSI;

    |BUCLE dsmow043;
    |FINIMP;
|FPRC;

|| **********************************************************************************
|| **********************************************************************************
|| ....................... Situa la Empresa Contable
|PROCESO LEmpresaConta;

 enEmpresa  = #118#0;
 enPerConta = 0;

 eaEstadoEmp = "S";
 |SI #118#94 > "01.01.0000";
     eaEstadoEmp = "N";
     |SI #0#24 = "N"; |ACABA; |FINSI;
 |FINSI;

|| ... Seleccion de Tipo de Empresa
  nSPin1 = #118#87;

  enSwSelEmp = 1;
  |SI #0#54 = "S";
      |SI nSPin1 ] #0#25; |Y nSPin1 [ #0#26;
          enSwSelEmp = 0;
      |FINSI;
  |SINO;
      |SI nSPin1 = #0#27;
          enSwSelEmp = 0;
      |SINO;
          |PARA nCampo = 28;  |SI nCampo [ 36;  |HACIENDO nCampo = nCampo + 1;
                |SI nSPin1 = #0nCampo; |Y nSPin1 ! 0;
                    enSwSelEmp = 0;
                   |SAL;
                |FINSI;
          |SIGUE;
      |FINSI;
  |FINSI;
  |SI enSwSelEmp = 1; |ACABA; |FINSI;
|| ------------------------------------------------------------------------
   nDComAct = 00;
   nHComAct = 99;
   |SI #0#55 = "S"; nDComAct = #0#56; nHComAct = #0#57; |FINSI;
   SwHayDatos = 0;
   nSwOp      = 0;
   |BUCLE dsmom041_C;
   |SI SwHayDatos = 0; |ACABA; |FINSI;

|| ------------------------------------------------------------------------
    aPathConta = "";
    #300#2  = enEmpresa;
    #300#26 = nEjercicio;
    |LEE 001#300=;
    |SI FSalida = 0;
        enPerConta = #300#3;
        aAlfa1 = enEmpresa;  aAlfa1 = ("00000" + aAlfa1) % -105;
        aAlfa2 = enPerConta; aAlfa2 = ("0" + aAlfa2) % -101;
        aPathConta  = aPath + "contagen/fich/" + aAlfa1 + aAlfa2 + "/";
    |FINSI;
|| ------------------------------------------------------------------------

   |DDEFECTO #999;
   nGTempo = nGTempo + 1;
   #999#0 = nGTempo;       || Guia
   #999#1 = enEmpresa;     || Empresa
   #999#2 = enPerConta;    || Periodo Contable
   #999#3 = enEjer;        || A¤o
   #999#12 = eaEstadoEmp;  || Estado
   #999#13 = "E";          || Moneda
   #999#14 = #118#1;       || Nombre Empresa
   #999#15 = aPathConta;   || Path
   #999#16 = #0#48;        || Emitir
   |GRABA 020#999n;
|FPRC;

|DEFBUCLE agifigen;
   #118#1;
   ;
   nDEmpresa;
   nHEmpresa;
   ;
   NULL, LEmpresaConta;
|FIN;
||///////////////////////////////////////////////////////////////////

|CALCULO Datos7; |TIPO 7;
 |MENSA "    <F3> Cambiar Emitir Libro S/N";
 |ATRI R; |PINTA 1169, "Imp"; |ATRI 0;
|FCAL;

|CALCULO DatosManip; |TIPO 11;
   nInkey = FSalida;
   |BLIN 04;
   |SI nInkey = 11;
       |SI #999#16 = "S";
           #999#16 = "N";
       |SINO;
           #999#16 = "S";
       |FINSI;
       |GRABA 020#999a;
   |FINSI;
|FCAL;

||///////////////////////////////////////////////////////////////////
|RUTINA inferior;
   #999#0 = 00001;
|FRUT;

|RUTINA superior;
   #999#0 = 99999;
|FRUT;

|CALCULO Tipo3;  |TIPO 3;
    efFechaEmi = #dsmoy066#45;

    #0#0  = "I";      ||Inversion
    #0#49 = enModelo; ||Libro Oficial
    |HAZ LeeCtrlLibro;

    enEjer = #0#1;
    aAlfa1 = enEjer; |QBF aAlfa1;
    aAlfa1 = aAlfa1 % -102;
    nEjercicio = aAlfa1;
    |DBASS aPath; |QBT aPath;

    aAlfa1 = #0#1; aAlfa1 = "01.01." + aAlfa1; efDFecha = aAlfa1;
    aAlfa1 = #0#1; aAlfa1 = "31.12." + aAlfa1; efHFecha = aAlfa1;
    efFecTope = aAlfa1;

    aDNombre = " " * 40;
    aHNombre = "z" * 40;

    aFichero = ("3w" + Usuario) % 108;
    |NOME_DAT #999 aFichero;
    |ABRE #999;  |CIERRA #999;  |DELINDEX #999;
    eaFEmpresa = aFichero;

    |ABRE #999;
    |ABRE #300;
    |SELECT #4#300;
    nGTempo = 0;   ||Busco Empresas Contables
    |SI #0#2 ! 0;
        nDEmpresa = #0#2;
        nHEmpresa = #0#3;
        |BUCLE agifigen;
    |SINO;
        |PARA nPara2 = 4;  |SI nPara2 [ 23;  |HACIENDO nPara2 = nPara2 + 1;
              nDEmpresa = #0nPara2;
              nHEmpresa = #0nPara2;
              |BUCLE agifigen;
        |SIGUE;
    |FINSI;
    |CIERRA #300;
    |CIERRA #999;

    |SI nGTempo = 0;    || No existe ninguna empresa
        |SI aParam = "";
            |MENAV "    Error. No existe ninguna Empresa dentro de la Seleccion ";
        |FINSI;
        |DELINDEX #999;
        |ACABA;
    |FINSI;

    |SI aParam = "";
        |C_V #999#12,1,"N"; |C_P #999#16,1,1270;

        |PUSHV 0509 2375;
        |ENTLINEAL #1#999, -1, 2, 22, 1, inferior, superior;
        |POPV;
    |FINSI;

    |HAZ Imprimir;

    |DELINDEX #999;
|FCAL;


|PROGRAMA;

 aParam = PARAMETRO; |QBF aParam;

 nEstado  = 0;
 |DDEFECTO #0;
 enModelo = #0#49;

 |ABRE #0;
 |LEE 101#0p;
 |SI FSalida ! 0;
     |DDEFECTO #0;
     |GRABA 020#0b;
     nEstado  = 1;
 |FINSI;

 |SI aParam = "";
     |CLS;
     |PINPA #0#0;
     |PINDA #0#0;

     |ENDATOS #1#0;
     |SI FSalida = 0;
         |GRABA 020#0a;
     |FINSI;
     |LIBERA #0;
     |CIERRA #0;
 |SINO;
      || ... Legalia
      |LIBERA #0;
      |CIERRA #0;

      #0#1 = enEjercicio;
      #0#2 = eaLegEmp;
      #0#3 = eaLegEmp;

      |PDEFECTO #0,4,44;
      #0#37 = "N";  || sin caratula
      #0#38 = "N";  || sin resumen
      #0#45 = efFecLeg;
      #0#48 = "S";
      |PDEFECTO #0,51,58;
      #0#51 = 1;
      #0#54 = "S";
      |HAZ Tipo3;

      |DBASS Comodin1; |QBF Comodin1;
       Comodin1 = Comodin1 + "contagen/PresLibr/" + eaLegEmp + eaLegPer + "/";
       |SI eaFormato ! "S";
            Comodin1 = Comodin1 + "OTROS*.xls";
       |SINO;
            Comodin1 = Comodin1 + "OTROS*.pdf";
       |FINSI;
       Numeracion = enNumeLeg;
       |_PDIR Comodin1;
       |PARA; |SI FSalida = 0; |HACIENDO;
              Comodin = Comodin1 % -107; Comodin = Comodin % 0103;
              Numeracion = Comodin; Numeracion = Numeracion + 1;
              |_SDIR Comodin1;
       |SIGUE;

       |SI eaFormato ! "S";
            Comodin = eaUnidadBTmp + "/tmp1.xls";
       |SINO;
            Comodin = eaUnidadBTmp + "/FacInver.pdf";
       |FINSI;
       |XABRE "C", Comodin, Handle;
       |SI FSalida ] 0;
           |XCIERRA Handle;
           |DBASS Comodin1; |QBF Comodin1;
           Comodin1 = Comodin1 + "contagen/PresLibr/";
           |MKDIR Comodin1;
           Comodin1 = Comodin1 + eaLegEmp + eaLegPer + "/";
           |MKDIR Comodin1;
           |SI eaFormato ! "S";
               Comodin1 = Comodin1 + "OTROS_" + (("000" + Numeracion) % -103) + ".xls";
               aNomFLeg = "OTROS_" + (("000" + Numeracion) % -103) + ".xls";
           |SINO;
               Comodin1 = Comodin1 + "OTROS_" + (("000" + Numeracion) % -103) + ".pdf";
               aNomFLeg = "OTROS_" + (("000" + Numeracion) % -103) + ".pdf";
           |FINSI;
           aAlfa = "";
           |IP_REMOTO aAlfa; |QBF aAlfa;
           |SI aAlfa ! "";
               |RECIBE_FICHERO Comodin, Comodin1;
           |SINO;
               |COPIA_FICHERO Comodin, Comodin1;
           |FINSI;
           nTipoL   = 20;
           nNumer   = Numeracion;
           efFecha1 = efDFecha;
           efFecha2 = efHFecha;
           enRegisLeg = 0;
           |HAZ eGrabaHistLegal;

           FSalida = 1;

           |PARA; |SI FSalida ! 0; |HACIENDO;
                  |RFBORRA Comodin;
                  |SI FSalida ! 0;
                      |SI eaFormato ! "S";
                          |MENAV "    Termine la tarea de Excel que tiene activa";
                      |SINO;
                          |MENAV "    Termine la tarea de PDF que tiene activa";
                      |FINSI;
                   |FINSI;
            |SIGUE;
       |FINSI;
 |FINSI;

 |PULSATECLA;
|FPRO;
