|FICHEROS;
   dsmoy095 #0;

   :/basica/def/agicen14 #114;
   :/basica/def/agifigen #118;
   :/basica/def/agim0019 #119;
   :/basica/def/agitab99 #99;

   :/contagen/def/canempre #300;
   :/contagen/def/caivaasi #301;
   :/contagen/def/camaniva #304;
   :/contagen/def/caivalin #305;
   :/contagen/def/caconimp #306;  || configuracion impresiones
   :/contagen/def/camacc01 #501;
   :/contagen/def/camacc02 #502;
   :/contagen/def/camacc03 #503;

   dsmom030 #30;
   dsmow030 #900;          ||Def de impresion

   dsmow157 #957;
   dsmow043 #999,MANTE;
   dsmow049;

   dsmow172;
   dsmow173;

   &regisnif@ #709;
|FIN;

|VARIABLES;
   aParam       = "";
   informe      = "";
   aInfoRes     = "";
   nInkey       = 0;
   aFichero     = "";
   nCampo       = 0;
   nDEmpresa    = 0;
   nHEmpresa    = 0;
   nDActividad  = 0;
   nHActividad  = 0;
   nLaEmpresa   = 0;
   nGTempo      = 0;
   nSPin1       = 0;
   aPath        = "";
   nEjercicio   = 0;

   aAlfa        = "";
   aAlfa1       = "";
   aAlfa2       = "";
   aAlfa3       = "";
   nPara1       = 0;
   nPara2       = 0;
   nPara3       = 0;
   nNume1       = 0;
   nNume2       = 0;
   nNume3       = 0;
   nNume4       = 0;
   nNume5       = 0;
   nNume6       = 0;
   fFecha       = @;
   nClave       = 0;

   aDTipo       = "";
   aHTipo       = "";
   SwHayDatos   = 0;
   ffecha1      = @;
   fDFecha      = @;
   fHFecha      = @;
   nDLiqui      = 0;
   nHLiqui      = 0;
   fLaFecha     = @;
   nElDepar     = 0;
   nEstado      = 0;
   nOperacion   = 0;
   nOpMdep      = 0;
   nAlSi        = 0;
   nAlNo        = 0;
   nAlConcepto  = 0;
   aClave       = "";

   aClaveOpe    = "";
   fFechaPago   = @;
   nImporPago   = 0;
   aMedioPago   = "";
   aDesPago     = "";
   nLibros      = 0;

   &enSwCaja    = 0;
   &enError     = 0;
   &eaAlfa      = "";
   &efFechaEmi  = @;
   &nLBase      = 0;
   &nLTipoI     = 0;
   &nLCuoI      = 0;
   &nLCuoIN     = 0;
   &nLTipoR     = 0;
   &nLCuoR      = 0;
   &nLCuoRN     = 0;
   &nLBasP      = 0;
   &nLTipP      = 0;
   &nLCuoP      = 0;
   &SwLinea     = 0;
   &aTipoIVA    = "";
   &aTipo       = "";
   &aTipoRS     = "";
   &nFactura    = 0;
   &enPorCsv    = 0;
   &eaPathDoc   = "";
   &eaPathLocal = "";
   &eaNomFic    = "";
   &enCab       = 0;
   &eaAbre      = "";

   &entrada     = 1;
|FIN;

|INCLUYE i_variar;
|INCLUYE i_varcon;
|INCLUYE i_varlib;

|| //////////////////// Calculos de Pantalla \\\\\\\\\\\\\\\\\\\\\\\\\\
|CALCULO Eano; |TIPO 7;
 |SI nEstado ! 0;
     fLaFecha = ~;
     aAlfa1 = fLaFecha; aAlfa1 = aAlfa1 % -104;
     #dsmoy095#1  = aAlfa1; |PINTA #dsmoy095#1;
 |FINSI;
|FCAL;

|CALCULO losDefectos; |TIPO 0;
 aAlfa1 = #dsmoy095#1;
 aAlfa2 = #dsmoy095#41;
 aAlfa2 = aAlfa2 % -104;
 |SI aAlfa1 ! aAlfa2; |O nEstado ! 0;
     aAlfa2 = "31.12." + aAlfa1; #dsmoy095#42 = aAlfa2; |PINTA #dsmoy095#42;
 |FINSI;
 |SI enSwSelFc = 1; aAlfa2 = aAlfa1; |FINSI;
 |SI aAlfa1 ! aAlfa2; |O nEstado ! 0;
     aAlfa2 = "01.01." + aAlfa1; #dsmoy095#41 = aAlfa2; |PINTA #dsmoy095#41;
 |FINSI;
|FCAL;

|CALCULO Tipo7;  |TIPO 7;
     |SI enEmpresa ! 0;
         #dsmoy095#2  = enEmpresa;  |PINTA #dsmoy095#2;
         #dsmoy095#3  = enEmpresa;  |PINTA #dsmoy095#3;

         |C_M #dsmoy095#2, 1, "N";
         |C_M #dsmoy095#3, 1, "N";
     |FINSI;
|FCAL;

|CALCULO Parrilla; |TIPO 0;
     |SI #dsmoy095#2 = 0;
         #dsmoy095#2 = 0;  |PINTA #dsmoy095#2;
         #dsmoy095#3 = 0;  |PINTA #dsmoy095#3;  |C_M #dsmoy095#3, 1, "N";
         |PARA nCampo = 4;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
               |C_M #dsmoy095#nCampo#, 1, "S";
         |SIGUE;
     |SINO;
         |SI enEmpresa = 0;
             |C_M #dsmoy095#2, 1, "S";
             |C_M #dsmoy095#3, 1, "S";
         |FINSI;

         |PARA nCampo = 4;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
               |C_M #dsmoy095#nCampo#, 1, "N";
               #dsmoy095#nCampo# = 0;  |PINTA #dsmoy095#nCampo#;
         |SIGUE;
     |FINSI;
|FCAL;

|CALCULO NoMasEmpresas; |TIPO 0;
     |SI #dsmoy095#Campo# = 0;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
               #dsmoy095#nCampo# = 0;  |C_M #dsmoy095#nCampo#, 1, "N";
         |SIGUE;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
               |C_M #dsmoy095#nCampo#, 1, "S";
         |SIGUE;
     |FINSI;
|FCAL;

|CALCULO YaSeCual; |TIPO 7;
 |SI enEmpresa ! 0;
     #dsmoy095#24 = "S"; |PINTA #dsmoy095#24;
     #dsmoy095#43 = "S";
     #dsmoy095#25 = 00;  |PINTA #dsmoy095#25;
     #dsmoy095#26 = 99;  |PINTA #dsmoy095#26;
     |C_M #dsmoy095#24, 1, "N";
     |C_M #dsmoy095#25, 1, "N";
     |C_M #dsmoy095#26, 1, "N";
     |C_M #dsmoy095#43, 1, "N";
 |FINSI;
|FCAL;

|CALCULO SelTipo0; |TIPO 0;
 |SI #dsmoy095#43 = "N";
     #dsmoy095#25 = 0;   |PINTA #dsmoy095#25;  |C_M #dsmoy095#25, 1, "N";
     #dsmoy095#26 = 99;  |PINTA #dsmoy095#26;  |C_M #dsmoy095#26, 1, "N";
     |PARA nCampo = 27;  |SI nCampo [ 36;  |HACIENDO nCampo = nCampo + 1;
           |C_M #dsmoy095#nCampo#, 1, "S";
     |SIGUE;
 |SINO;
     |SI enEmpresa = 0;
         |C_M #dsmoy095#25, 1, "S";
         |C_M #dsmoy095#26, 1, "S";
     |FINSI;
     |PARA nCampo = 27;  |SI nCampo [ 36;  |HACIENDO nCampo = nCampo + 1;
           |C_M #dsmoy095#nCampo#, 1, "N";
           #dsmoy095#nCampo# = 0;  |PINTA #dsmoy095#nCampo#;
     |SIGUE;
 |FINSI;
|FCAL;

|CALCULO SelTipo1;
 |SI #dsmoy095#Campo# = 0;
     |PARA nCampo = Campo + 1;  |SI nCampo [ 36;  |HACIENDO nCampo = nCampo + 1;
           #dsmoy095#nCampo# = 0;  |C_M #dsmoy095#nCampo#, 1, "N";
     |SIGUE;
 |SINO;
     |PARA nCampo = Campo + 1;  |SI nCampo [ 36;  |HACIENDO nCampo = nCampo + 1;
           |C_M #dsmoy095#nCampo#, 1, "S";
     |SIGUE;
 |FINSI;
|FCAL;

|CALCULO fecha; |TIPO 0;
 |C_M #dsmoy095#Campo#,0,aAlfa1;
 |SI aAlfa1 = "N"; |ACABA; |FINSI;

 aAlfa1 = #dsmoy095#1;
 aAlfa2 = "01.01." + aAlfa1;
 aAlfa3 = "31.12." + aAlfa1;
 |SI enSwSelFc = 1; aAlfa2 = "01.01.2000"; |FINSI;
 |SI #dsmoy095#Campo# < aAlfa2; |O #dsmoy095#Campo# > aAlfa3;
     |MENAV "    Error. Fecha Incorrecta ";
     |ERROR;
 |SINO;
     |SI Campo = 42; |Y #dsmoy095#41 > #dsmoy095#42;
         |MENAV "    Error. Fecha Incorrecta ";
         |ERROR;
     |FINSI;
 |FINSI;
|FCAL;

|CALCULO DesdePeriodo; |TIPO 0;

  |SI #dsmoy095#Campo# > 12; |Y #dsmoy095#Campo# ! 99;
      |ERROR;
      |ACABA;
  |FINSI;

  |SI #dsmoy095#37 = 99;
      aAlfa1 = "N";
      aAlfa2 = "S";
      #dsmoy095#38 = 99; |PINTA #dsmoy095#38;
  |SINO;
      aAlfa1 = #dsmoy095#1;
      |SI enSwSelFc = 0;
          aAlfa2 = "01.01." + aAlfa1;
      |SINO;
          aAlfa2 = "01.01.2000";
      |FINSI;
      #dsmoy095#41 = aAlfa2; |PINTA #dsmoy095#41;
      aAlfa3 = "31.12." + aAlfa1; #dsmoy095#42 = aAlfa3; |PINTA #dsmoy095#42;

      aAlfa1 = "S";
      aAlfa2 = "N";
      |SI #dsmoy095#38 = 99;
          #dsmoy095#38 = 12; |PINTA #dsmoy095#38;
      |FINSI;
  |FINSI;
  |C_M #dsmoy095#38,1,aAlfa1;
  |C_M #dsmoy095#41,1,aAlfa2;
  |C_M #dsmoy095#42,1,aAlfa2;
|FCAL;

|CALCULO HastaPeriodo; |TIPO 0;
  |C_M #dsmoy095#38, 0, aAlfa1;
  |SI aAlfa1 = "N"; |ACABA; |FINSI;

  |SI #dsmoy095#Campo# > 12;
      |ERROR;
      |ACABA;
  |FINSI;

  |SI #dsmoy095#37 > #dsmoy095#38;
      |ERROR;
  |FINSI;
|FCAL;

|CALCULO SelAct; |TIPO 0;
 aAlfa1 = #dsmoy095#Campo#;
 |C_M #dsmoy095#51,1, aAlfa1;
 |C_M #dsmoy095#52,1, aAlfa1;
|FCAL;

|| **********************************************************************************
|| **********************************************************************************
|| ....................... Situa la Empresa Contable
|PROCESO LEmpresaConta;

 enEmpresa  = #canempre#2;
 enPerConta = #canempre#3;;   || Periodo Contable

 |SI #dsmoy095#24 = "N"; |Y #canempre#21 = "I"; |ACABA; |FINSI;

|| ... Seleccion de Tipo de Empresa
  |ABRE #agifigen;
  #agifigen#0 = enEmpresa;
  |LEE 041#agifigen.=;
  |SI FSalida ! 0;
      |DDEFECTO #agifigen;
  |FINSI;
  |CIERRA #agifigen;
  nSPin1 = #agifigen#87;

  enSwSelEmp = 1;
  |SI #dsmoy095#43 = "S";
      |SI nSPin1 ] #dsmoy095#25; |Y nSPin1 [ #dsmoy095#26;
          enSwSelEmp = 0;
      |FINSI;
  |SINO;
      |SI nSPin1 = #dsmoy095#27;
          enSwSelEmp = 0;
      |SINO;
          |PARA nCampo = 28;  |SI nCampo [ 36;  |HACIENDO nCampo = nCampo + 1;
                |SI nSPin1 = #dsmoy095#nCampo#; |Y nSPin1 ! 0;
                    enSwSelEmp = 0;
                    |SAL;
                |FINSI;
          |SIGUE;
      |FINSI;
  |FINSI;
  |SI enSwSelEmp = 1; |ACABA; |FINSI;
|| ------------------------------------------------------------------------
   aAlfa1 = enEmpresa;  aAlfa1 = ("00000" + aAlfa1) % -105;
   aAlfa2 = enPerConta; aAlfa2 = ("0" + aAlfa2) % -101;
   aPathConta = aPath + "contagen/fich/" + aAlfa1 + aAlfa2 + "/";

   nGTempo = nGTempo + 1;
   #999#0 = nGTempo;       || Guia
   #999#1 = enEmpresa;     || Empresa
   #999#2 = enPerConta;    || Periodo Contable
   #999#3 = enEjer;        || A¤o
   #999#4 = #canempre#11;  || Desde Mes
   #999#5 = #canempre#13;  || N§ Digitos
   #999#6 = #canempre#14;  || Digitos Nivel 1
   #999#7 = #canempre#15;  || Digitos Nivel 2
   #999#8 = #canempre#16;  || Digitos Nivel 3
   #999#9 = #canempre#17;  || Digitos Nivel 4
   #999#10 = #canempre#18; || Digitos Nivel 5
   #999#11 = #canempre#19; || Digitos Nivel 6
   eaEstadoEmp = "S"; |SI #canempre#21 = "I"; eaEstadoEmp = "N"; |FINSI;
   #999#12 = eaEstadoEmp;  || Estado
   #999#13 = #canempre#28; || Moneda
   #999#14 = #canempre#1;  || Nombre Empresa
   #999#15 = aPathConta;   || Path
   #999#16 = #dsmoy095#48;        || Emitir
   |GRABA 020#999n;
|FPRC;

|DEFBUCLE canempre;
   #300#4;
   ;
   nDEmpresa, nEjercicio;
   nHEmpresa, nEjercicio;
   ;
   NULL, LEmpresaConta;
|FIN;
||///////////////////////////////////////////////////////////////////

|CALCULO Datos7; |TIPO 7;
     |MENSA "    <F3> Cambiar Emitir Libro S/N";
     |ATRI R; |PINTA 1169, "Imp"; |ATRI 0;
|FCAL;

|CALCULO DatosManip; |TIPO 11;
   nInkey = FSalida;
   |BLIN 04;

   |SI nInkey = 11;
       |SI #999#16 = "S";
           #999#16 = "N";
       |SINO;
           #999#16 = "S";
       |FINSI;
       |GRABA 020#999a;
   |FINSI;
|FCAL;

|RUTINA inferior;
   #999#0 = 00001;
|FRUT;

|RUTINA superior;
   #999#0 = 99999;
|FRUT;

||///////////////////////////////////////////////////////////////////
|CALCULO Tipo3;  |TIPO 3;

    efFechaEmi   = #dsmoy095#45;
    #dsmoy095#49 = enModelo; ||Libro Oficial

    enEjer = #dsmoy095#1;
    aAlfa1 = enEjer; |QBF aAlfa1;
    aAlfa1 = aAlfa1 % -102;
    nEjercicio = aAlfa1;
    |DBASS aPath; |QBT aPath;

    aFichero = ("2d" + Usuario) % 108;
    |NOME_DAT #957 aFichero;

    aFichero = ("4w" + Usuario) % 108;
    |NOME_DAT #999 aFichero;
    |DELINDEX #999;
    eaFEmpresa = aFichero;

    |ABRE #999;
    nGTempo = 0;   ||Busco Empresas Contables
    |SI #dsmoy095#2 ! 0;
        nDEmpresa = #dsmoy095#2;
        nHEmpresa = #dsmoy095#3;
        |BUCLE canempre;
    |SINO;
        |PARA nPara3 = 4;  |SI nPara3 [ 23;  |HACIENDO nPara3 = nPara3 + 1;
              |SI #0nPara3 ! 0;
                  nDEmpresa = #0nPara3;
                  nHEmpresa = #0nPara3;
                  |BUCLE canempre;
              |FINSI;
        |SIGUE;
    |FINSI;
    |CIERRA #999;

    |SI nGTempo = 0;    || No existe ninguna empresa
        |SI aParam = "";
            |MENAV "    Error. No existe ninguna Empresa Contable dentro de la Seleccion ";
        |FINSI;
        |DELINDEX #999;
        |ACABA;
    |FINSI;

    |C_V #999#12,1,"N"; |C_P #999#16,1,1270;

    |PUSHV 0509 2375;
    |ENTLINEAL #1#999, -1, 5, 22, 1, inferior, superior;
    |POPV;


    aDTipo = "R"; aHTipo = "S";
    |SI enSwSelCp = 0;
        aDTipo = #dsmoy095#0; aDTipo = aDTipo > " ";
        aHTipo = #dsmoy095#0; aHTipo = aHTipo > " ";
    |FINSI;

    |HAZ Imprimir;

    |DELINDEX #957;
    |DELINDEX #999;
|FCAL;

|| *************************************************************************
|PROCESO Cabw172;   || Soportado
        |DDEFECTO #dsmow172;
        #dsmow172#0  = nClave;
        #dsmow172#1  = #900#79;
        #dsmow172#2  = #900#81;
        #dsmow172#3  = #900#22;
        #dsmow172#4  = "";
        aAlfa = #900#16; |QBF aAlfa;
        |SI aAlfa ! ""; aAlfa = aAlfa + "/"; |FINSI;
        aAlfa = aAlfa + #900#17;
        #dsmow172#5  = aAlfa;
        |SI #camaniva#38 > 1;
            nNume1 = #900#17 + (#camaniva#38) - 1;
            aAlfa  = nNume1;
            #dsmow172#6  = aAlfa;
        |FINSI;
        #dsmow172#7  = (#900#65 % 402);
        #dsmow172#8  = (#900#65 % 103);
        #dsmow172#9  = #900#21;
        #dsmow172#10 = #900#20;

        #dsmow172#12 = aClaveOpe;
        #dsmow172#13 = #900#51;
        #900#51 = 0;
|FPRC;

|PROCESO Cabw173;  || Repercutido
        |DDEFECTO #dsmow173;
        #dsmow173#0  = nClave;
        #dsmow173#1  = #900#79;
        #dsmow173#2  = #900#81;
        #dsmow173#3  = #900#16;
        #dsmow173#4  = #900#17;
        |SI #camaniva#38 > 1;
            nNume1 = #900#17 + (#camaniva#38) - 1;
            aAlfa  = nNume1;
            #dsmow173#5  = aAlfa;
        |FINSI;

        #dsmow173#6  = (#900#65 % 402);
        #dsmow173#7  = (#900#65 % 103);
        #dsmow173#8  = #900#21;
        #dsmow173#9  = #900#20;

        #dsmow173#11 = aClaveOpe;
        #dsmow173#12 = #900#51;
        #900#51 = 0;
|FPRC;

|PROCESO Graba_Req_RECC;
    nClave = nClave + 1;

    |SI aTipoIVA = "S";

        |HAZ Cabw172;
        #dsmow172#20 = fFechaPago;
        #dsmow172#21 = nImporPago;
        #dsmow172#22 = aMedioPago;
        #dsmow172#23 = aDesPago;

        |GRABA 020#dsmow172.n;
    |FINSI;

    |SI aTipoIVA = "R";

        |HAZ Cabw173;
        #dsmow173#18 = fFechaPago;
        #dsmow173#19 = nImporPago;
        #dsmow173#20 = aMedioPago;
        #dsmow173#21 = aDesPago;

        |GRABA 020#dsmow173.n;
    |FINSI;
|FPRC;

|PROCESO Graba_Req;

    nClave = nClave + 1;
    |SI aTipoIVA = "S";

        |HAZ Cabw172;
        #dsmow172#14 = nLBase;
        #dsmow172#15 = nLTipoI;
        #dsmow172#16 = nLCuoI + nLCuoIN;
        #dsmow172#17 = nLCuoI;
        #dsmow172#18 = nLTipoR;
        #dsmow172#19 = nLCuoR;

        |GRABA 020#dsmow172.n;
    |FINSI;

    |SI aTipoIVA = "R";

        |HAZ Cabw173;
        #dsmow173#13 = nLBase;
        #dsmow173#14 = nLTipoI;
        #dsmow173#15 = nLCuoI;
        #dsmow173#16 = nLTipoR;
        #dsmow173#17 = nLCuoR;

        |GRABA 020#dsmow173.n;
    |FINSI;
|FPRC;

|| *************************
|PROCESO camacc02;
    fFechaPago = #camacc02#4;
    nImporPago = #camacc02#10;
    aAlfa = #camacc02#15 - "DEVENGO";
    |SI FSalida ! 0;
        aMedioPago = "03";
    |SINO;
        |SI #camacc02#14 = "T"; aMedioPago = "01"; |FINSI;
        |SI #camacc02#14 = "C"; aMedioPago = "02"; |FINSI;
        |SI #camacc02#14 = "O"; aMedioPago = "04"; |FINSI;
    |FINSI;
    aDesPago = #camacc02#15;

    |HAZ Graba_Req_RECC;
|FPRC;

|DEFBUCLE camacc02;
    #camacc02#1;
    ;
    0000, #camaniva#0, #camaniva#1, 01;
    0000, #camaniva#0, #camaniva#1, 99;
    e;
    NULL, camacc02;
|FIN;
|| ***************************************************************

|PROCESO CalTipoNif;
   aAlfa1     = #709#12; aAlfa2 = "  ";
   |SI aAlfa1 = "   "; aAlfa1 = "ES "; |FINSI;
   |SI aAlfa1 ! "ES ";
       aAlfa = aAlfa1; |QBF aAlfa;
       |SI aAlfa = "GR";  aAlfa = "EL";  |FINSI;
                          aAlfa2 = "06";
       |SI aAlfa = "DE";  aAlfa2 = "02";  |FINSI;
       |SI aAlfa = "AT";  aAlfa2 = "02";  |FINSI;
       |SI aAlfa = "BE";  aAlfa2 = "02";  |FINSI;
       |SI aAlfa = "BG";  aAlfa2 = "02";  |FINSI;
       |SI aAlfa = "CY";  aAlfa2 = "02";  |FINSI;
       |SI aAlfa = "DK";  aAlfa2 = "02";  |FINSI;
       |SI aAlfa = "SI";  aAlfa2 = "02";  |FINSI;
       |SI aAlfa = "EE";  aAlfa2 = "02";  |FINSI;
       |SI aAlfa = "FI";  aAlfa2 = "02";  |FINSI;
       |SI aAlfa = "FR";  aAlfa2 = "02";  |FINSI;
       |SI aAlfa = "EL";  aAlfa2 = "02";  |FINSI;
       |SI aAlfa = "GB";  aAlfa2 = "02";  |FINSI;
       |SI aAlfa = "NL";  aAlfa2 = "02";  |FINSI;
       |SI aAlfa = "HU";  aAlfa2 = "02";  |FINSI;
       |SI aAlfa = "IT";  aAlfa2 = "02";  |FINSI;
       |SI aAlfa = "IE";  aAlfa2 = "02";  |FINSI;
       |SI aAlfa = "LV";  aAlfa2 = "02";  |FINSI;
       |SI aAlfa = "LT";  aAlfa2 = "02";  |FINSI;
       |SI aAlfa = "LU";  aAlfa2 = "02";  |FINSI;
       |SI aAlfa = "MT";  aAlfa2 = "02";  |FINSI;
       |SI aAlfa = "PL";  aAlfa2 = "02";  |FINSI;
       |SI aAlfa = "PT";  aAlfa2 = "02";  |FINSI;
       |SI aAlfa = "CZ";  aAlfa2 = "02";  |FINSI;
       |SI aAlfa = "SK";  aAlfa2 = "02";  |FINSI;
       |SI aAlfa = "RO";  aAlfa2 = "02";  |FINSI;
       |SI aAlfa = "SE";  aAlfa2 = "02";  |FINSI;
       |SI aAlfa = "HR";  aAlfa2 = "02";  |FINSI;
   |FINSI;
   aAlfa = aAlfa1 + aAlfa2;
|FPRC;

|| **************
|PROCESO Auxiliar57;

 |SI nOperacion = 0;
     nCampo = nCampo + 1;
 |FINSI;

 |SI nOperacion = 1;
     nLBase  = #957#1;
     nLTipoI = #957#2;
     nLCuoI  = #957#3;
     nLTipoR = #957#4;
     nLCuoR  = #957#5;
     nLCuoIN = #957#6;
     nLCuoRN = #957#7;
     |SI nLCuoI = 0; |Y nLCuoIN = 0; nLTipoI = 0; |FINSI;
     |SI nLCuoR = 0; |Y nLCuoRN = 0; nLTipoR = 0; |FINSI;
     |SI nLBase ! 0; |O nLCuoI ! 0; |O nLCuoR ! 0; |O nLCuoIN ! 0; |O nLCuoRN ! 0;
         |HAZ Graba_Req;
         SwLinea = SwLinea + 1;
     |FINSI;
 |FINSI;
|FPRC;

|DEFBUCLE Auxiliar57;
  #957#1;
  ;
  nPara1,  000,  000;
  nPara1, 1000, 1000;
  ;
  NULL, Auxiliar57;
|FIN;

|PROCESO VariasLineas;

 SwLinea = 0;
 nLBasP = #900#66;
 nLTipP = #900#67;
 nLCuoP = #900#68;
 |PARA nPara1 = 23; |SI nPara1 [ 27; |HACIENDO nPara1 = nPara1 + 1;
       nCampo = 0;
       nOperacion = 0; |BUCLE Auxiliar57;
       |SI nCampo [ 1;
           nLBase = #900nPara1;
           nNume1 = nPara1 +  6; nLTipoI = #900nNume1;
           nNume2 = nPara1 + 11; nLCuoI  = #900nNume2;
           nNume3 = nPara1 + 17; nLTipoR = #900nNume3;
           nNume4 = nPara1 + 22; nLCuoR  = #900nNume4;
           nNume5 = nPara1 + 29; nLCuoIN = #900nNume5;
           nNume6 = nPara1 + 35; nLCuoRN = #900nNume6;
           |SI nLCuoI = 0; nLTipoI = 0; |FINSI;
           |SI nLCuoR = 0; nLTipoR = 0; |FINSI;

           |SI nLBase ! 0; |O nLCuoI ! 0; |O nLCuoR ! 0;  |O nLCuoIN ! 0; |O nLCuoRN ! 0;
               |HAZ Graba_Req;
               SwLinea = SwLinea + 1;
           |FINSI;
       |SINO;
           nOperacion = 1; |BUCLE Auxiliar57;
       |FINSI;
 |SIGUE;

 |SI SwLinea = 0;
     |HAZ Graba_Req;
     SwLinea = 1;
 |FINSI;

 |SI enSwCaja = 1;
    |BUCLE camacc02;
 |FINSI;

|FPRC;

|PROCESO LinLibro0;

  aAlfa1 = #caivalin#30; |QBF aAlfa1; |SI aAlfa1 = ""; #caivalin#30 = #camaniva#10; |FINSI;
  |SI enActividad ! 0;      ||quiere separar por actividades
      |SI aMulDep = "S";
          nElDepar = #caivalin#30;
          |SI nElDepar = 0;  nElDepar = 1; |FINSI;
          |SI enActividad ! nElDepar; nAlNo = 1; |ACABA; |FINSI;
      |FINSI;
  |FINSI;

  |SI aMulDep = "S";
      nElDepar = #caivalin#30;
      |SI nElDepar = 0;  nElDepar = 1; |FINSI;
      |SI nElDepar < #dsmoy095#51; |O nElDepar > #dsmoy095#52; nAlNo = 1; |ACABA; |FINSI;
  |FINSI;

  nAlSi = 1;
  |SI nOpMdep = 0;  |ACABA;  |FINSI;  || Solo comprueba que exista

  enConcepto = #caivalin#3;
  |SI enSwSelCp = 1;
      |HAZ CtrlConcepto;
      |SI enSwConcep = 1;
          |ACABA;
      |FINSI;
  |FINSI;

  nAlConcepto = 1;
  |SI nOpMdep = 2;  |ACABA;  |FINSI;  || Solo comprueba que exista

  #caivalin#6  = (#caivalin#6  / enConversor) ! EURODBMOD;
  #caivalin#8  = (#caivalin#8  / enConversor) ! EURODBMOD;
  #caivalin#10 = (#caivalin#10 / enConversor) ! EURODBMOD;
  #caivalin#20 = (#caivalin#20 / enConversor) ! EURODBMOD;
  #caivalin#22 = (#caivalin#22 / enConversor) ! EURODBMOD;

  |SI aTipoIVA = "R";
      |HAZ SumaRepercutido;
  |SINO;
      |HAZ SumaSoportado;
  |FINSI;
|FPRC;

|PROCESO SumaRepercutido;
 nCampo = -1;
 |SI #caivalin#7 = 0;               ||  0%
     #900#27 = #900#27 + #caivalin#6;
     #900#28 = #900#28 + #caivalin#6;
 |SINO;
     #900#28 = #900#28 + #caivalin#6;
     #900#39 = #900#39 + #caivalin#8;
     |SI #caivalin#7 = 4;         ||  4%
         #900#23 = #900#23 + #caivalin#6;
         #900#29 = #caivalin#7;
         #900#34 = #900#34 + #caivalin#8;
         nCampo = 23;
     |SINO;
         |SI #caivalin#7 = 7; |O #caivalin#7 = 8; |O #caivalin#7 = 10;  ||  7% 8% 10%
             #900#24 = #900#24 + #caivalin#6;
             #900#30 = #caivalin#7;
             #900#35 = #900#35 + #caivalin#8;
             nCampo = 24;
         |SINO;
             |SI #caivalin#7 = 16;  |O #caivalin#7 = 18; |O #caivalin#7 = 21;   || 16% 18%
                 #900#25 = #900#25 + #caivalin#6;
                 #900#31 = #caivalin#7;
                 #900#36 = #900#36 + #caivalin#8;
                 nCampo = 25;
             |SINO;
                 #900#26 = #900#26 + #caivalin#6;   || ??%
                 #900#32 = #caivalin#7;
                 #900#37 = #900#37 + #caivalin#8;
                 nCampo = 26;
             |FINSI;
         |FINSI;
     |FINSI;
  |FINSI;

 |SI #caivalin#9 ! 0;
     #900#50 = #900#50 + #caivalin#10;
     |SI #caivalin#9 = 0.50;
         #900#40 = #caivalin#9;
         #900#45 = #900#45 + #caivalin#10;
     |SINO;
         |SI #caivalin#9 = 1; |O #caivalin#9 = 1.40;   || 1
             #900#41 = #caivalin#9;
             #900#46 = #900#46 + #caivalin#10;
         |SINO;
             |SI #caivalin#9 = 4; |O #caivalin#9 = 5.20; || 4%
                 #900#42 = #caivalin#9;
                 #900#47 = #900#47 + #caivalin#10;
             |SINO;
                 #900#43 = #caivalin#9;
                 #900#48 = #900#48 + #caivalin#10; || %
             |FINSI;
         |FINSI;
     |FINSI;
 |FINSI;

 #900#51 = #900#51 + #caivalin#6 + #caivalin#8 + #caivalin#10;

 #900#66 = #900#66 + #caivalin#20;
 #900#67 = #caivalin#21;
 #900#68 = #900#68 + #caivalin#22;

|| ... Graba Detalle %
     |SI nCampo ! -1;
         |ABRE #957;
         #957#0 = nCampo;
         #957#2 = #caivalin#7;
         #957#4 = #caivalin#9;
         |LEE 041#957=;
         |SI FSalida ! 0;
             |DDEFECTO #957;
             #957#0 = nCampo;
             #957#2 = #caivalin#7;
             #957#4 = #caivalin#9;
             |GRABA 020#957c;
         |FINSI;
         #957#1  = #957#1 + #caivalin#6;
         #957#2  = #caivalin#7;
         #957#3  = #957#3 + #caivalin#8;
         #957#4  = #caivalin#9;
         #957#5  = #957#5 + #caivalin#10;
         |GRABA 020#957a;
         |CIERRA #957;
     |FINSI;
|FPRC;

|PROCESO SumaSoportado;
 nCampo = -1;
 |SI #caivalin#7 = 0;               ||  0%
     #900#27 = #900#27 + #caivalin#6;
     #900#28 = #900#28 + #caivalin#6;
 |SINO;
     #900#28 = #900#28 + #caivalin#6;
     |SI #caivalin#5 = "S";
          #900#39 = #900#39 + #caivalin#8;
          |SI #caivalin#7 = 4;         ||  4%
              #900#23 = #900#23 + #caivalin#6;
              #900#29= #caivalin#7;
              #900#34 = #900#34 + #caivalin#8;
              nCampo = 23;
          |SINO;
              |SI #caivalin#7 = 7; |O #caivalin#7 = 8; |O #caivalin#7 = 10;  ||  7% y 8% 10%
                  #900#24 = #900#24 + #caivalin#6;
                  #900#30 = #caivalin#7;
                  #900#35 = #900#35 + #caivalin#8;
                  nCampo = 24;
              |SINO;
                  |SI #caivalin#7 = 16;  |O #caivalin#7 = 18;  |O #caivalin#7 = 21;    || 16% 18 % 21%
                      #900#25 = #900#25 + #caivalin#6;
                      #900#31 = #caivalin#7;
                      #900#36 = #900#36 + #caivalin#8;
                      nCampo = 25;
                  |SINO;
                      #900#26 = #900#26 + #caivalin#6;   || ??%
                      #900#32 = #caivalin#7;
                      #900#37 = #900#37 + #caivalin#8;
                      nCampo = 26;
                  |FINSI;
              |FINSI;
          |FINSI;
      |SINO;                                    ||NO DEDUCIBLE
          #900#57 = #900#57 + #caivalin#8;
          |SI #caivalin#7 = 4;         ||  4%
              #900#23 = #900#23 + #caivalin#6;
              #900#29 = #caivalin#7;
              #900#52 = #900#52 + #caivalin#8;
              nCampo = 23;

          |SINO;
              |SI #caivalin#7 = 7; |O #caivalin#7 = 8; |O #caivalin#7 = 10;       ||  7% 8% 10%
                  #900#24 = #900#24 + #caivalin#6;
                  #900#30 = #caivalin#7;
                  #900#53 = #900#53 + #caivalin#8;
                  nCampo = 24;
              |SINO;
                  |SI #caivalin#7 = 16; |O #caivalin#7 = 18; |O #caivalin#7 = 21;   || 16% 18% 21%
                      #900#25 = #900#25 + #caivalin#6;
                      #900#31 = #caivalin#7;
                      #900#54 = #900#54 + #caivalin#8;
                      nCampo = 25;
                  |SINO;
                      #900#26 = #900#26 + #caivalin#6;   || ??%
                      #900#32 = #caivalin#7;
                      #900#55 = #900#55 + #caivalin#8;
                      nCampo = 26;
                  |FINSI;
              |FINSI;
          |FINSI;
     |FINSI;
  |FINSI;

  |SI #caivalin#9 ! 0; |Y #caivalin#5 = "S";
      #900#50 = #900#50 + #caivalin#10;
      |SI #caivalin#9 = 0.5;        || 0.5%
          #900#40 = #caivalin#9;
          #900#45 = #900#45 + #caivalin#10;
      |SINO;
          |SI #caivalin#9 = 1; |O #caivalin#9 = 1.40;   || 1%
              #900#41 = #caivalin#9;
              #900#46 = #900#46 + #caivalin#10;
         |SINO;
              |SI #caivalin#9 = 4; |O #caivalin#9 = 5.20; || 4
                  #900#42 = #caivalin#9;
                  #900#47 = #900#47 + #caivalin#10;
             |SINO;
                 #900#43 = #caivalin#9;
                 #900#48 = #900#48 + #caivalin#10; || %
             |FINSI;
         |FINSI;
     |FINSI;
 |FINSI;

 |SI #caivalin#9 ! 0; |Y #caivalin#5 = "N";
     #900#63 = #900#63 + #caivalin#10;
     |SI #caivalin#9 = 0.5;        || 0.5%
         #900#40 = #caivalin#9;
         #900#58 = #900#58 + #caivalin#10;
     |SINO;
         |SI #caivalin#9 = 1; |O #caivalin#9 = 1.40;   || 1.5%
             #900#41 = #caivalin#9;
             #900#59 = #900#59 + #caivalin#10;
         |SINO;
             |SI #caivalin#9 = 4; |O #caivalin#9 = 5.20; || 1%
                 #900#42 = #caivalin#9;
                 #900#60 = #900#60 + #caivalin#10;
             |SINO;
                 #900#43 = #caivalin#9;
                 #900#61 = #900#61 + #caivalin#10; || %
             |FINSI;
         |FINSI;
     |FINSI;
 |FINSI;

 #900#51 = #900#51 + #caivalin#6 + #caivalin#8 + #caivalin#10;

 #900#66 = #900#66 + #caivalin#20;
 #900#67 = #caivalin#21;
 #900#68 = #900#68 + #caivalin#22;

|| ... Graba Detalle %
     |SI nCampo ! -1;
         |ABRE #957;
         #957#0 = nCampo;
         #957#2 = #caivalin#7;
         #957#4 = #caivalin#9;
         |LEE 041#957=;
         |SI FSalida ! 0;
             |DDEFECTO #957;
             #957#0 = nCampo;
             #957#2 = #caivalin#7;
             #957#4 = #caivalin#9;
             |GRABA 020#957c;
         |FINSI;
         #957#1  = #957#1 + #caivalin#6;
         #957#2  = #caivalin#7;
         #957#4  = #caivalin#9;
         |SI #caivalin#5 = "S";
             #957#3  = #957#3 + #caivalin#8;
             #957#5  = #957#5 + #caivalin#10;
         |SINO;
             #957#6  = #957#6 + #caivalin#8;
             #957#7  = #957#7 + #caivalin#10;
         |FINSI;
         |GRABA 020#957a;
         |CIERRA #957;
     |FINSI;
|FPRC;

|DEFBUCLE LinLibro;
 #caivalin#1;
 ;
 #camaniva#0,#camaniva#1,001;
 #camaniva#0,#camaniva#1,999;
 ;
 NULL, LinLibro0;
|FIN;

|PROCESO Facturas;
   |SI aMulDep ! "S";
       nElDepar = #304#10;
       |SI nElDepar = 0;  nElDepar = 1;  |FINSI;
       |SI nElDepar < #dsmoy095#51; |O nElDepar > #dsmoy095#52; |ACABA; |FINSI;
   |SINO;
        nOpMdep = 0;
        nAlSi   = 0;
        nAlNo   = 0;
        |BUCLE LinLibro;
        |SI nAlSi = 0; |ACABA; |FINSI;
   |FINSI;

|| ................ Comprueba que los conceptos de facturas son buenos
   |SI enSwSelCp = 1;
        nOpMdep     = 2;
        nAlConcepto = 0;
        |BUCLE LinLibro;
        |SI nAlConcepto = 0; |ACABA; |FINSI;
   |FINSI;

   nOpMdep = 1;
   |PDEFECTO #900, 16, 64;
   #900#66 = 0; #900#67 = 0; #900#68 = 0;

   |SI enActividad = 0;
       #900#69 = #304#10;
   |FINSI;

   #900#79 = "01.01.0000";
   #900#81 = "01.01.0000";
   #900#82 = "";
   #900#83 = 0;
   #900#84 = 0;
   #900#65 = "";
   |DELINDEX #957;

   #900#16 = #camaniva#2;
   #900#17 = #camaniva#3;
   |SI nFactura = 0; nFactura = 1; |FINSI;

   fLaFecha = #camaniva#4;
   aAlfa1   = fLaFecha; aAlfa1   = aAlfa1 % 105;
   #900#18  = aAlfa1;
   #900#19  = #camaniva#12;
   #900#20  = #camaniva#13;
   #900#21  = #camaniva#14;
   eaCodNif = #camaniva#14;  ||El Dni
   |HAZ LeeNifs;
   |HAZ CalTipoNif;
   #900#65 = aAlfa;

   #900#22 = #camaniva#22;

   #900#79  = fLaFecha;
   |SI #camaniva#41 > "01.01.0000";
       #900#81  = #camaniva#41;
       aAlfa1   = #900#81; aAlfa1   = aAlfa1 % 105;
       #900#82  = aAlfa1;
   |FINSI;

   #900#87 = #camaniva#5;
   #900#88 = #camaniva#40;

   |BUCLE LinLibro;

   aClaveOpe = "";
   enSwCaja = 0
   |SI #camaniva#42 = "Z"; |O #camaniva#42 = "1"; |O #camaniva#42 = "2";
        |O #camaniva#42 = "3"; |O #camaniva#42 = "4"; |O #camaniva#42 = "5";
        |O #camaniva#42 = "6"; |O #camaniva#42 = "7"; |O #camaniva#42 = "8";
            enSwCaja = 1;
            aClaveOpe = "07";
   |FINSI;
   |SI enSwCaja = 1;
       |DDEFECTO #501;
       #501#17 = 0;
       #501#0  = #camaniva#0;
       #501#1  = #camaniva#1;
       |LEE 040#501=;
       |SI FSalida ! 0;  enSwCaja = 0; aClaveOpe = ""; |FINSI;
   |FINSI;
   |SI enSwCaja = 0;
       |ABRE #dsmom030;
       #dsmom030#0 = #camaniva#27;
       |LEE 041#dsmom030.=;
       |SI #dsmom030#28 = 7; aClaveOpe = "02"; |FINSI;
       |CIERRA #dsmom030;
   |FINSI;

   |SI #dsmoy095#37 = 99;     || Seleccion por Fechas
       |SI fLaFecha ] #dsmoy095#41;
           |HAZ VariasLineas;
           SwHayDatos = 1;
       |FINSI;
   |SINO;
       |SI #900#88 ] #dsmoy095#37;   || Seleccion por Periodo
           |HAZ VariasLineas;
           SwHayDatos = 1;
       |FINSI;
   |FINSI;

   nFactura = nFactura + 1;
|FPRC;

|DEFBUCLE Facturas3;
  #camaniva#3;
  4,36,40;
  #dsmoy095#39, "     ", 000001, fDFecha, aDTipo, nDLiqui;
  #dsmoy095#40, "zzzzz", 999999, fHFecha, aHTipo, nHLiqui;
  ;
  NULL, Facturas;
|FIN;
|| //////////////////////////////////////////////////////////////////

|PROCESO ImprimeLinea;
    |SI enPorCsv = 1;
         |HAZ GrabaCsv;
    |SINO;
         |HAZ GrabaExcel;
    |FINSI;
    SwLinea = 1;
|FPRC;

|DEFBUCLE dsmow172;
   #dsmow172#1;
   ;
   00000001;
   99999999;
   ;
   NULL, ImprimeLinea;
|FIN;

|DEFBUCLE dsmow173;
   #dsmow173#1;
   ;
   00000001;
   99999999;
   ;
   NULL, ImprimeLinea;
|FIN;

|PROCESO Emision;
   enSwPartido = 0;
   enEmpresa  = #999#1;
   enPerConta = #999#2;
   eaLaMonEmp = #999#13;
   |HAZ EnConversor;
   enEjer      = #999#3;   || A¤o
   dig1        = #999#4;   || Desde Mes
   dig3        = #999#5;   || N§ Digitos
   dig4        = #999#6;   || Digitos Nivel 1
   dig5        = #999#7;   || Digitos Nivel 2
   dig6        = #999#8;   || Digitos Nivel 3
   dig7        = #999#9;   || Digitos Nivel 4
   dig8        = #999#10;  || Digitos Nivel 5
   dig9        = #999#11;  || Digitos Nivel 6
   eaEstadoEmp = #999#12; || Estado
   eaNombreEmp = #999#14; || Nombre Empresa

   |SI #999#4 ! 1;
       enSwOpPar  = 0;
       |HAZ SituaEmpresa;
       enPerConta = enPeriodoPar;
       #999#15    = aPathConta;
   |FINSI;

   aPathConta  = #999#15; |QBF aPathConta; || Path
   || Recoger informacion sobre los niveles ...
   |SI dig3 = 1; MaximoDigitos = dig4; |FINSI;
   |SI dig3 = 2; MaximoDigitos = dig5; |FINSI;
   |SI dig3 = 3; MaximoDigitos = dig6; |FINSI;
   |SI dig3 = 4; MaximoDigitos = dig7; |FINSI;
   |SI dig3 = 5; MaximoDigitos = dig8; |FINSI;
   |SI dig3 = 6; MaximoDigitos = dig9; |FINSI;

   |PATH_DAT #301 aPathConta;
   |PATH_DAT #304 aPathConta;
   |PATH_DAT #305 aPathConta;
   |PATH_DAT #306 aPathConta;
   |PATH_DAT #501 aPathConta;
   |PATH_DAT #502 aPathConta;
   |PATH_DAT #503 aPathConta;
   |ABRE #caivaasi;
   |DDEFECTO #caivaasi;
   |LEE 000#caivaasi.p;
   |CIERRA #caivaasi;
   aMulDep = #caivaasi#132;

   |DDEFECTO #306;  ||Configuracion de Informes
   |ABRE #306;
   #306#0 = 1;
   |LEE 000#306=;
   |CIERRA #306;

   nClave    = 0;
   eaTituloL = #dsmoy095#46; ||Titulo del Libro
   |QBF eaTituloL;

   nDActividad = 0;
   nHActividad = 0;

   |HAZ LeeDatosEmpresa;
   |SI swNoExis = 0;       ||SI existe esta Actividad.

       eaNomFic = enEjer;
       eaNomFic = eaNomFic + #dsmow030#1; |QBF eaNomFic;
       |SI aTipoIVA = "R";
           eaNomFic = eaNomFic + "E";
       |SINO;
           eaNomFic = eaNomFic + "R";
       |FINSI;

       eaAlfa = eaNombreEmp; |HAZ QuitaRarosNom;
       eaNomFic = eaNomFic + eaAlfa; |QBF eaNomFic;

       SwHayDatos = 0;
       fDFecha = #dsmoy095#41;
       fHFecha = #dsmoy095#42;
       nDLiqui = #dsmoy095#37;
       nHLiqui = #dsmoy095#38;
       |SI #dsmoy095#37 = 99;
            nDLiqui = 0;
            nHLiqui = 99;
       |FINSI;

       || ... Creo auxiliar.
       |ABRE #dsmow172;
       |ABRE #dsmow173;

       |ABRE #501;
       nFactura = 0;
       fDFecha  = #dsmoy095#41;
       fHFecha  = #dsmoy095#42;
       nDLiqui  = #dsmoy095#37;
       nHLiqui  = #dsmoy095#38;
       |SI #dsmoy095#37 = 99;
           nDLiqui = 0;
           nHLiqui = 99;
       |FINSI;
       |BUCLE Facturas3;
       |CIERRA #501;

       enError = 0;
/*
|SI aTipoIVA = "S"; |CONSULTA_CLAVE #1#dsmow172; |FINSI;
|SI aTipoIVA = "R"; |CONSULTA_CLAVE #1#dsmow173; |FINSI;
*/
       |CIERRA #dsmow172;
       |CIERRA #dsmow173;

       |SI SwHayDatos = 1;
           |SI enPorCsv = 1;
               |HAZ PorCsv_2;
           |SINO;
               |HAZ PorExcel_2;
           |FINSI;

           |SI enError =  0;

               |SI aTipoIVA = "S"; |BUCLE dsmow172; |FINSI;
               |SI aTipoIVA = "R"; |BUCLE dsmow173; |FINSI;

               |SI enPorCsv = 1;
                   |HAZ PresentaDocCsv;
               |SINO;
                   |HAZ PresentaDocExcel;
               |FINSI;
               nLibros = nLibros + 1;
           |FINSI;
       |FINSI;
   |FINSI;

   |SI enSwPartido = 1;
       enSwOpPar   = 1;
       |HAZ FinalPartido;
   |FINSI;

   |DELINDEX #dsmow172;
   |DELINDEX #dsmow173;

   |SI enError ! 0;
       |ERROR10;
   |FINSI;
|FPRC;

|DEFBUCLE dsmow043;
   #dsmow043#1;
   16;
   000001, "S";
   999999, "S";
   ;
   NULL, Emision;
|FIN;

|PROCESO Imprimir;

   aFichero = ("9s" + Usuario) % 108;
   |NOME_DAT #dsmow172#aFichero#;
   |DELINDEX #dsmow172;

   aFichero = ("9r" + Usuario) % 108;
   |NOME_DAT #dsmow173#aFichero#;
   |DELINDEX #dsmow173;

   |HAZ LeeUnidadBasico;

   |SI #dsmoy095#44 = 1;
       enPorCsv = 1;
       |HAZ PorCsv_1;
   |SINO;
       enPorCsv  = 2;
       |HAZ PorExcel_1;
   |FINSI;

   nLibros = 0;
   |BUCLE dsmow043;
   |SI nLibros ! 0;
       aAlfa = "Los libros generados estan: ";
       |SI nLibros = 1; aAlfa = "El libro generado esta: "; |FINSI;
       aAlfa = "0000" + aAlfa + eaPathLocal + " de su PC";
       |MENAV aAlfa;
   |FINSI;
|FPRC;

|PROCESO LeeDatosEmpresa;

  |DDEFECTO #900;
  eaInac = #dsmoy095#24;
  eaTituloL = #dsmoy095#46; |QBF eaTituloL;
  efDFecha  = #dsmoy095#41;
  efHFecha  = #dsmoy095#42;
  aAlfa1    = #dsmoy095#1;
  aAlfa1    = "01.01." + aAlfa1; fFecha    = aAlfa1;
  |SI efDFecha < fFecha; efDFecha = fFecha; |FINSI;
  |HAZ DatosEmpr_w30;
|FPRC;

|PROCESO LeeDatosActividad;
   swNoExis = 0;
   |SI enActividad ! 0;
       swNoExis = 1;
       |ABRE #114;
       #114#0 = enEmpresa;
       #114#1 = enActividad;
       |LEE 041#114=;
       |SI FSalida = 0;  swNoExis = 0; |FINSI;
       |CIERRA #114;
   |FINSI;
|FPRC;

|PROGRAMA;

  aParam = PARAMETRO; |QBF aParam;
  |SI aParam = ""; aParam = "R"; |FINSI;

  |DDEFECTO #dsmoy095;
  aClave    = aParam;

  #dsmoy095#49 = 901;
  |SI aParam = "S"; #dsmoy095#49 = 902; |FINSI;

  enModelo = #dsmoy095#49;
  |ABRE #99;
  |DDEFECTO #99;
  #99#0 = enModelo;
  |LEE 041#99=;
  |CIERRA #99;

  |ABRE #dsmoy095;
  #dsmoy095#0 = aClave;
  |LEE 101#dsmoy095.=;
  |SI FSalida ! 0;
      |DDEFECTO #dsmoy095;
      #dsmoy095#0  = aClave;
      #dsmoy095#49 = enModelo;
      #dsmoy095#46 = "LIBRO REGISTRO DE FACTURAS EXPEDIDAS";
      |SI aClave = "S";
          #dsmoy095#46 = "LIBRO REGISTRO DE FACTURAS RECIBIDAS";
      |FINSI;
      |GRABA 020#dsmoy095.b;
      nEstado = 1;
  |FINSI;
  #dsmoy095#45 = ~;

  |HAZ LeeCtrlLibro;

  aTipoIVA   = #dsmoy095#0;
  |SI aClave = "R";
      aTipo   = "EXPEDIDAS";
      aTipoRS = "REPERCUTIDO";
      aAlfa1  = "FACTURAS EMITIDAS                                 ";
  |SINO;
      aTipo   = "RECIBIDAS";
      aTipoRS = "SOPORTADO";
      aAlfa1  = "FACTURAS RECIBIDAS                                ";
  |FINSI;

  |CLS;
  |PINPA #0#dsmoy095;
  |ATRI R; |PINTA 0630, aAlfa1; |ATRI 0;
  |PINDA #0#dsmoy095;

  |ENDATOS #1#dsmoy095;
  |SI FSalida = 0;
      #dsmoy095#0 = aClave;
      |GRABA 020#dsmoy095.a;
  |FINSI;
  |LIBERA #dsmoy095;
  |CIERRA #dsmoy095;
|FPRO;
