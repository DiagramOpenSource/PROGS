|FICHEROS;
     dsm180ca #1;
     dsm180li #2;
     dsmom004 #4;

     &ds180li@ #900;
     &ds180ca@;
|FIN;

|VARIABLES;
     nSwChequea = 0;
     nSwComplem = 0;
     nSw        = 0;
     nBorra     = 0;
     nComp      = 0;
     nSuma      = 0;
     nBoton1    = 0;
     nPara1     = 0;
     nPara2     = 0;
     nPara3     = 0;

     aAlfa      = "";
     aTecla     = "";
     aF2        = "";
     aEsc       = "";
     nOpera     = 0;

     aBase      = "";
     aMas       = "";
|FIN;

|INCLUYE i_variar;

|RUTINA ClaveBaja2;
     #2#0  = #1#0;
     #2#1  = #1#1;
     #2#2  = #1#2;
     #2#3  = #1#3;
     #2#4  = #1#4;
     #2#5  = #1#5;
     #2#6  = "            ";
     #2#7  = 1;
     #2#8  = 0000;
     #2#15 = 01;
|FRUT;

|RUTINA ClaveAlta2;
     #2#0  = #1#0;
     #2#1  = #1#1;
     #2#2  = #1#2;
     #2#3  = #1#3;
     #2#4  = #1#4;
     #2#5  = #1#5;
     #2#6  = "zzzzzzzzzzzz";
     #2#7  = 2;
     #2#8  = 9999;
     #2#15 = 99;
|FRUT;

|PROCESO Boton1;
     |PUSHV 0501 2380;
     |ESTADO_CONTROL nBoton1, "HIDE";

     |PINPA #0#2;

     |SI enEjer < 2014;
         |C_M #2#15, 1, "N"; #2#15 = 01;

         |C_V #2#16, 1, "N";  |C_V #2#17, 1, "N";
         |C_V #2#18, 1, "N";  |C_V #2#19, 1, "N";
         |C_V #2#20, 1, "N";  |C_V #2#21, 1, "N";
         |C_V #2#22, 1, "N";  |C_V #2#23, 1, "N";
         |C_V #2#24, 1, "N";  |C_V #2#25, 1, "N";
         |C_V #2#26, 1, "N";  |C_V #2#27, 1, "N";
         |C_V #2#28, 1, "N";

         |BLANCO 1630 2279;
     |FINSI;

     |SI enTipoEntrada = 4;
         |ENTLINEAL #1#2, -7, 4, 22, 0, ClaveBaja2, ClaveAlta2;
     |SINO;
         |DBASE aBase;  |QBF aBase;
         aMas = aBase + "def/dsm180li.mas";
         |CARGA_DEF aMas, ds180li@;
         |SI FSalida ! 0;  |ACABA;  |FINSI;

         |ENTLINEAL #1#2, -7, 2, 22, 0, ClaveBaja2, ClaveAlta2;

         |DESCARGA_DEF #ds180li@;
     |FINSI;

     |ESTADO_CONTROL nBoton1, "SHOW";
     |POPV;

     |PINTA #1#6;
     |PINTA #1#7;
     |PINTA #1#8;
|FPRC;

|PROCESO EnOtraAnterior;
     nSw = 1;
|FPRC;

|DEFBUCLE EnOtraAnterior;
     #ds180li@#1010;
     ;
     #2#0, #2#1, #2#2, #2#3, nPara2, #2#5, #2#6, #2#7, #2#8, #2#15;
     #2#0, #2#1, #2#2, #2#3, nPara3, #2#5, #2#6, #2#7, #2#8, #2#15;
     e;
     NULL, EnOtraAnterior;
|FIN;

|PROCESO RecalculaLi;

     |SI nSwComplem = 1;
         nSw = 0;
         |BUCLE EnOtraAnterior;
         |SI nSw = 1;
             |BORRA 020#2a;
         |FINSI;
         |LIBERA #2;
         |ACABA;
     |FINSI;

     |SI nBorra = 1;
         |BORRA 020#2a;
         |LIBERA #2;
         |ACABA;
     |FINSI;

     |SI nSwChequea = 0;
         nSwChequea = 1;
         |ERROR10;
         |ACABA;
     |FINSI;

     |HAZ Tipo2m180li;
|FPRC;

|DEFBUCLE dsm180li;
     #2#1;
     ;
     #1#0, #1#1, #1#2, #1#3, #1#4, #1#5, "            ", 1, 0000, 01;
     #1#0, #1#1, #1#2, #1#3, #1#4, #1#5, "zzzzzzzzzzzz", 2, 9999, 99;
     be;
     NULL, RecalculaLi;
|FIN;

|PROCESO Recalculo;
     nSwChequea = 0;  |BUCLE dsm180li;

     |SI nSwChequea = 0;  |ACABA;  |FINSI;

     #1#6 = 0;
     #1#7 = 0;
     #1#8 = 0;

     |BUCLE dsm180li;
|FPRC;

|PROCESO BRepComplem;

      |DBASE aBase;  |QBF aBase;
      aMas = aBase + "def/dsm180li.mas";
      |CARGA_DEF aMas, ds180li@;
      |SI FSalida ! 0;  |ACABA;  |FINSI;

      aMas = aBase + "def/dsm180ca.mas";
      |CARGA_DEF aMas, ds180ca@;
      |SI FSalida ! 0;
          |DESCARGA_DEF #ds180li@;
          |ACABA;
      |FINSI;

      |ABRE #ds180ca@;
      nPara2 = 0;
      nPara3 = 0;
      nPara1 = #1#4 - 1;
      |PARA nComp = 0; |SI nComp [ nPara1; |HACIENDO nComp = nComp + 1;
            #ds180ca@#0 = #dsm180ca#0;
            #ds180ca@#1 = #dsm180ca#1;
            #ds180ca@#2 = #dsm180ca#2;
            #ds180ca@#3 = #dsm180ca#3;
            #ds180ca@#4 = nComp;
            |LEE 041#ds180ca@.=;
            |SI FSalida = 0;
                |SI #ds180ca@#4 = 0; |O #ds180ca@#10 = "S";
                    nPara2 = nComp;
                |FINSI;
                nPara3 = nComp;
            |FINSI;
       |SIGUE;
       |CIERRA #ds180ca@;
       |DESCARGA_DEF #ds180ca@;


       nSwComplem = 1;  |BUCLE dsm180li;
       nSwComplem = 0;

       |DESCARGA_DEF #ds180li@;
|FPRC;

|PROCESO Traedsm180li;
     #2#4 = #1#4;
     |GRABA 020#2n;
     |LIBERA #2;
|FPRC;

|DEFBUCLE dsm180liT;
     #2#1;
     ;
     #1#0, #1#1, #1#2, #1#3, nComp, #1#5, "            ", 1, 0000, 01;
     #1#0, #1#1, #1#2, #1#3, nComp, #1#5, "zzzzzzzzzzzz", 2, 9999, 99;
     be;
     NULL, Traedsm180li;
|FIN;

|PROCESO Justificante;
     |SI #1#4 = 0;  |ACABA;  |FINSI;

     |SI #1#10 ! "C";  |Y #1#10 ! "S";
         |MENAV "      Introduzca Complementaria o Sustitutoria";
         |ERROR;
     |FINSI;

     |C_M #1#9, 1, "N";
     |SI #1#10 = "S"; |O #1#10 = "C";
         |C_M #1#9, 1, "S";

         |SI #1#9 = "             ";
             |ABRE #4;
             #4#0 = #1#0;
             #4#1 = #1#1;
             #4#2 = #1#2;
             #4#3 = #1#3;
             #4#4 = #1#4 - 1;
             |LEE 000#4=;
             |SI FSalida ! 0;  |DDEFECTO #4;  |FINSI;
             |CIERRA #4;

             #1#9 = #4#25;  |PINTA #1#9;
         |FINSI;
      |SINO;
          #1#9 = "";  |PINTA #1#9;
      |FINSI;

     |SI #1#10 = "S";
         nSuma = 0;
         nSuma = #1#6 + #1#7 + #1#8;

         |SI nSuma = 0;
             |DBASE aBase;  |QBF aBase;
             aMas = aBase + "def/dsm180ca.mas";
             |CARGA_DEF aMas, ds180ca@;
             |SI FSalida ! 0;  |ACABA;  |FINSI;

             |ABRE #ds180ca@;

             nPara1 = #1#4 - 1;
             |PARA nComp = 0; |SI nComp [ nPara1; |HACIENDO nComp = nComp + 1;

                   #ds180ca@#0 = #dsm180ca#0;
                   #ds180ca@#1 = #dsm180ca#1;
                   #ds180ca@#2 = #dsm180ca#2;
                   #ds180ca@#3 = #dsm180ca#3;
                   #ds180ca@#4 = nComp;
                   |LEE 041#ds180ca@.=;
                   |SI FSalida = 0;
                       |SI #ds180ca@#4 = 0; |O #ds180ca@#10 = "S";
                           nBorra = 1;  |BUCLE dsm180li;
                           nBorra = 0;
                       |FINSI;
                       |BUCLE dsm180liT;
                   |FINSI;
             |SIGUE;
             |CIERRA #ds180ca@;

             |DESCARGA_DEF #ds180ca@;

             |HAZ Recalculo;

             |HAZ LaPantalla;
             |PINDA #0#1;
             |HAZ PintaDatos;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO Tipo12moc1180;  |TIPO 12;
|FPRC;

|PROCESO PulsoF2;
     |PTEC 824;
|FPRC;

|PROCESO dsmom004;
     enIngresado = enIngresado + #dsmom004#15;
|FPRC;

|DEFBUCLE dsmom004;
     #dsmom004#1;
     ;
     enEmpresa, enEjer, 00, 115, 00;
     enEmpresa, enEjer, 99, 115, 99;
     ;
     NULL, dsmom004;
|FIN;

|PROCESO dsm180ca;
     |SI #dsm180ca#10 = "S";
         enInformado = #dsm180ca#8;
     |SINO;
         enInformado = enInformado + #dsm180ca#8;
     |FINSI;
|FPRC;

|DEFBUCLE dsm180ca;
     #dsm180ca#1;
     ;
     enEmpresa, enEjer, 99, 180, 00, 00;
     enEmpresa, enEjer, 99, 180, 99, 99;
     ;
     NULL, dsm180ca;
|FIN;

|PROCESO Chequeo;
     enIngresado = 0;
     |BUCLE dsmom004;
     enIngresado = enIngresado ! 2;
     enInformado = #dsm180ca#8 ! 2;

     |SI #dsm180ca#4 ! 0;  |Y #dsm180ca#10 = "C";
         enInformado = 0;
         |BUCLE dsm180ca;
         enInformado = enInformado ! 2;
     |FINSI;

     |SI enInformado ! enIngresado;
         aAlfa = "0000Total retenciones del modelo 180: " + enInformado;
         aAlfa = aAlfa + " no coinciden con lo declarado en el modelo 115: " + enIngresado;
         |MENAV aAlfa;
         |MENAV "0000Revise los datos";
     |FINSI;
|FPRC;

|PROCESO VerComplem;
  |SI #1#4 > 0;
      |ABRE #4;
      #4#0 = #1#0;
      #4#1 = #1#1;
      #4#2 = #1#2;
      #4#3 = #1#3;
      #4#4 = #1#4;
      |LEE 141#4=;
      |SI FSalida = 0;
          |SI nOpera = 0;
              #1#10 = #4#24;
              #1#9  = #4#21;
          |SINO;
              #4#24 = #1#10;
              #4#21 = #1#9;
              |GRABA 020#4a;
          |FINSI;
      |FINSI;
      |LIBERA #4;
      |CIERRA #4;
   |FINSI;
|FPRC;

|PROCESO LaPantalla;
     |PINPA #0#1;
     |SI enEjer ] 2018; |Y enComplem > 0;
         |CUADRO 1902 2364;
         |ATRI P;
         |PINTA 2003, " Las complementarias  solo recogeran registros no declarados ";
         |PINTA 2103, " previamente. Si  ha modificado  importes  de algun registro ";
         |PINTA 2203, " anterior, es aconsejable presentar una sustitutiva.         ";
         |ATRI 0;
     |FINSI;
|FPRC;

|PROGRAMA;
     |HAZ LeeEURODBMOD;

     eaNomDef    = "dsm180ca";
     enResultado = 0;

|| \\\\\\ Alta del Modelo ////////

     |SI enTipoEntrada = 1;              || Alta del Modelo
         |CABEZA "Modelo 180";
         |ABRE #1;
         #1#0 = enEmpresa;
         #1#1 = enEjer;
         #1#2 = enPeriodo;
         #1#3 = enModelo;
         #1#4 = enComplem;
         #1#5 = enActividad;
         |LEE 040#1=;
         |SI FSalida ! 0;
             |DDEFECTO #1;
             #1#0 = enEmpresa;
             #1#1 = enEjer;
             #1#2 = enPeriodo;
             #1#3 = enModelo;
             #1#4 = enComplem;
             #1#5 = enActividad;
             nOpera = 0; |HAZ VerComplem;
             |GRABA 020#1b;
         |FINSI;

         |HAZ LaPantalla;
         |PINDA #0#1;
         |HAZ PintaDatos;

         |CONTROL_SIMPLE 0, "BOTON,<F2> Entrada Perceptores  ", 1155, 1379, PulsoF2;
         nBoton1 = FSalida;

         |SI #1#4 ! 0;
             |SI enEjer < 2018;
                 |C_M #1#10, 1, "S";
             |FINSI;
             |ENDATOS #1#1;
         |FINSI;

         aF2  = &255 + "824";
         aEsc = &255 + "806";
         |PARA; |SI;  |HACIENDO;
             |LEETECLA aTecla;
             |SI aTecla = aF2;    |HAZ Boton1;  |FINSI;
             |SI aTecla = aEsc;   |SAL;         |FINSI;
         |SIGUE;

         |FIN_CONTROL_WINDOWS nBoton1;

         |PULSATECLA;

         nSuma = #1#6  + #1#7  + #1#8;

         |SI nSuma = 0;
             |CONFI "0000NNo hay perceptores introducidos, quiere emitirlo sin actividad";
             |SI FSalida ! 0;
                 |BORRA 020#1a;
                 enEntrada = 0;
             |SINO;
                 |GRABA 020#1a;
                 nOpera = 1; |HAZ VerComplem;
                 enEntrada = 1;
             |FINSI;
         |SINO;
             |GRABA 020#1a;
             nOpera = 1; |HAZ VerComplem;
             enEntrada = 1;
         |FINSI;
         |LIBERA #1;
         |CIERRA #1;

         |HAZ Chequeo;

         |ACABA;
     |FINSI;

|| \\\\\\ Consulta del Modelo ////////

     |SI enTipoEntrada = 4;              || Consulta del Modelo
         |CABEZA "Modelo 180";
         |ABRE #1;
         #1#0 = enEmpresa;
         #1#1 = enEjer;
         #1#2 = enPeriodo;
         #1#3 = enModelo;
         #1#4 = enComplem;
         #1#5 = enActividad;
         |LEE 040#1=;
         |SI FSalida ! 0;
             |MENAV "      No existe Registro.";
             |CIERRA #1;
             |ACABA;
         |FINSI;

         |HAZ LaPantalla;
         |PINDA #0#1;
         |HAZ PintaDatos;

         |CONTROL_SIMPLE 0, "BOTON,<F2> Entrada Perceptores  ", 1155, 1379, Boton1;
         nBoton1 = FSalida;

         aF2  = &255 + "824";
         aEsc = &255 + "806";
         |PARA; |SI;  |HACIENDO;
             |LEETECLA aTecla;
             |SI aTecla = aF2;    |HAZ Boton1;  |FINSI;
             |SI aTecla = aEsc;   |SAL;         |FINSI;
         |SIGUE;

         |FIN_CONTROL_WINDOWS nBoton1;

         |PULSATECLA;

         |CIERRA #1;

         |HAZ Chequeo;

         |ACABA;
     |FINSI;

|| \\\\\\ Baja del Modelo ////////

     |SI enTipoEntrada = 3;              || Baja del Modelo
         |ABRE #1;
         #1#0 = enEmpresa;
         #1#1 = enEjer;
         #1#2 = enPeriodo;
         #1#3 = enModelo;
         #1#4 = enComplem;
         #1#5 = enActividad;
         |LEE 040#1=;
         |SI FSalida ! 0;
             |SI enMensa = 0;
                 |MENAV "      No existe Registro.";
             |FINSI;
             |CIERRA #1;
             |ACABA;
         |FINSI;

         nBorra = 1;  |BUCLE dsm180li;
         nBorra = 0;

         |BORRA 020#1a;
         |LIBERA #1;

         |CIERRA #1;

         |ACABA;
     |FINSI;

|| \\\\\\ Alta desde el Pase del Modelo

     |SI enTipoEntrada = 98;
         |ABRE #1;
         #1#0 = enEmpresa;
         #1#1 = enEjer;
         #1#2 = enPeriodo;
         #1#3 = enModelo;
         #1#4 = enComplem;
         #1#5 = enActividad;
         |LEE 040#1=;
         |SI FSalida ! 0;
             #1#0 = enEmpresa;
             #1#1 = enEjer;
             #1#2 = enPeriodo;
             #1#3 = enModelo;
             #1#4 = enComplem;
             #1#5 = enActividad;
             nOpera = 0; |HAZ VerComplem;
             |GRABA 020#1b;
         |FINSI;

         |SI #1#4 > 0; |Y #1#10 = "C"; |Y enEjer ] 2018;
             |HAZ BRepComplem;
         |FINSI;

         |HAZ Recalculo;
         |GRABA 020#1a;

         |LIBERA #1;
         |CIERRA #1;

         |SI enEjer = 2014;
             |HAZ Chequeo;
         |FINSI;

         |ACABA;
     |FINSI;

|| \\\\\\ Comprobacion Existencia del Modelo

     |SI enTipoEntrada = 99;              || Existencia del Modelo ????
         |ABRE #1;
         #1#0 = enEmpresa;
         #1#1 = enEjer;
         #1#2 = enPeriodo;
         #1#3 = enModelo;
         #1#4 = enComplem;
         #1#5 = enActividad;
         |LEE 040#1=;
         |SI FSalida ! 0;  enExistencia = 1;  |FINSI;
         |CIERRA #1;

         |SI enActividad = 0;  enResultado = 0;  |FINSI;
     |FINSI;
|FPRO;
