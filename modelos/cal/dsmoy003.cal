|FICHEROS;
     dsmoy003 #0;

     dsmom001 #1;
    :/integral/def/dsam0000 #2;

     dsmom016 #16;
     dsmom017 #17;
     dsmom018 #18;
     dsmom068 #68;
     dsmom019 #19;
     dsmoz006 #99;

    :/integral/def/dsam0003 #103;
    :/basica/def/agicen14 #114;
     dsmom100 #100;
     :/contagen/def/caconimp #306;  || configuracion impresiones
     dsmow030 #900;          ||Def de impresion

     dsmom901 #901;          ||Comentarios
|FIN;

|VARIABLES;
     informe    = "";
     inform1    = "dsmoz006";
     inform3    = "dsmozn06";
     inform2    = "dsmozm06";
     nCampo     = 0;
     fFecha     = @;
     aAlfa      = "";
     aAlfa1     = "";
     aAlfa2     = "";
     aPath      = "";
     &enDatos   = 0;
     &eaInac    = "";
     &eaTituloL = "";
     &efDFecha  = @;
     &efHFecha  = @;

   &eaCodRenta = "";
   &eaCodR2    = "";
   &eaRenta    = "";
   &enEjerR    = 0;
   &enCodRen   = 0;
   &eaCodRen   = "";

   aElDni      = "";
   nOpTitular  = 0;
   aBase       = "";
   nCanal      = 0;
   &eaPathIntegral  = "";
     nNume5   = 0;
   &nCoBien   = 0;
|FIN;

|INCLUYE i_variar;

|PROCESO DatosEmp; |TIPO 7;

   |DDEFECTO #306;

   |ABRE #100;
   |LEE 040#100p;
   |SI FSalida = 0;
       aAlfa1 = #100#1; |QBF aAlfa1; aAlfa1 = ("00000" + aAlfa1) % -105;
       aAlfa2 = #100#2; |QBF aAlfa2; aAlfa2 = ("0"     + aAlfa2) % -101;
       |DBASS aPath; |QBT aPath;
       aPathConta = aPath + "contagen/fich/" + aAlfa1 + aAlfa2 + "/";

       |PATH_DAT #306 aPathConta;

       |ABRE #306;
       #306#0 = 1;
       |LEE 000#306=;
       |CIERRA #306;
   |FINSI;
   |CIERRA #100;

   #0#27  = #306#7; |PINTA #0#27;
   aAlfa1 = #306#8; |C_M #0#27,1,aAlfa1;
|FPRC;

|CALCULO CodigoRenta; |TIPO 7;
     |DBASS aBase;  |QBF aBase;
     aAlfa = aBase + "integral/def/dsam0000.mas";
     |XABRE "E", aAlfa, nCanal;
     |SI FSalida ] 0;
         eaPathIntegral  = aBase + "integral/";
     |FINSI;

     |C_M #0#29, 1, "S";
     |SI eaPathIntegral = "";
         |C_M #0#29, 1, "N";  #0#29 = "N";  |PINTA #0#29;
     |FINSI;
|FCAL;

|PROCESO Ejercicio;  |TIPO 7;
     fFecha = ~;
     aAlfa  = fFecha % -104;
     #0#26   = aAlfa;
     |PINTA #0#26;
|FPRC;

|PROCESO DesdeHasta;
     |C_M #0#1, 1, "S";
     |PARA nCampo = 2;  |SI nCampo [ 25;  |HACIENDO nCampo = nCampo + 1;
           |C_M #0nCampo, 1, "N";
           #0nCampo = 0;  |PINTA #0nCampo;
     |SIGUE;

     |SI #0#0 = 0;
         |C_M #0#1, 1, "N";  #0#1 = 0;  |PINTA #0#1;

         |PARA nCampo = 2;  |SI nCampo [ 25;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |FINSI;
|FPRC;

|PROCESO NoMasEmpresas;
     |SI #0Campo = 0;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 25;  |HACIENDO nCampo = nCampo + 1;
               #0nCampo = 0;  |C_M #0nCampo, 1, "N";
         |SIGUE;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 25;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |FINSI;
|FPRC;

|| ----------------- Datos de la Comunidad

|PROCESO RelComuneros0;

     |PDEFECTO #0, 30,38;
     |SI #1#8 > "01.01.0000";
         |SI #1#8 > efHFecha; |ACABA;  |FINSI;
     |FINSI;
     |SI #1#9 > "01.01.0000";
         |SI #1#9 < efDFecha;  |ACABA;  |FINSI;
     |FINSI;

     |ABRE #2;
     #2#0 = #1#3;
     |LEE 040#2=;
     |SI FSalida ! 0;  |DDEFECTO #2;  |FINSI;
     |CIERRA #2;

     #0#30  = ("00000" + #1#3) % -105;   || Codigo
     #0#33  = #1#5;   || Nombre
     #0#34  = #2#3;   || NIF

     |ABRE #114;
     #114#0 = #2#0; || Empresa
     #114#1 = #1#4;
     |LEE 041#114=;
     |SI FSalida = 0;
          enCodRen = #114#18;
          |SI enEjerR ! 0; |HAZ CodigoRen; |FINSI;
          #0#31 = ("00000" + enCodRen) % -105;  ||Codigo en Renta
          #0#32 = #114#19;  ||Perceptor
     |SINO;
          aAlfa1 = eaCodRenta;
          nOpTitular = 1;
          |SI #0#29 = "S"; aElDni = #2#3; |HAZ LaRentaSI;  |FINSI;
          #0#31 = eaCodR2;
          eaCodRenta = aAlfa1;
     |FINSI;
     |CIERRA #114;
     #0#35 = #1#7;
     #0#36 = #1#8;
     #0#37 = #1#9;
     |SI #0#29 = "N";
         #0#31 = "";
         #0#32 = "";
     |FINSI;
     |PRINT;
|FPRC;

|DEFBUCLE RelComumeros;
     #1#1;
     ;
     enEmpresa, enActividad, 0001;
     enEmpresa, enActividad, 9999;
      e;
     NULL, RelComuneros0;
|FIN;

|PROCESO Emite;

     enEmpresa   = #17#0;
     enActividad = #17#3;
||     |SI enDatos = 1;
         |DDEFECTO #900;
         |HAZ DatosEmpr_w30;
         eaCodRenta = "";
         nOpTitular = 0;
         |SI #0#29 = "S"; aElDni = #900#1; |HAZ LaRentaSI;  |FINSI;
         nOpTitular = 1;
         eaCodR2 = "";
||     |FINSI;

     |ABRE #16;
     |SELECT #1#16;
     #16#0  = "E";
     #16#1  = #17#5;
     #16#41 = #17#6;
     #16#2  = #17#1;
     |LEE 040#16=;
     |SI FSalida ! 0;  |DDEFECTO #16;  |FINSI;
     |CIERRA #16;

     |ABRE #18;
     #18#0 = #17#0;
     #18#1 = #17#1;
     #18#2 = #17#2;
     #18#3 = #17#3;
     |LEE 040#18=;
     |SI FSalida ! 0;
         |DDEFECTO #18;
         #18#4  = #16#5;           || Elemento  1
         #18#5  = #16#9;           || Elemento  2
         #18#6  = #16#13;          || Elemento  3
         #18#7  = #16#17;          || Elemento  4
         #18#8  = #16#21;          || Elemento  5
         #18#9  = #16#25;          || Elemento  6
         #18#10 = #16#29;          || Elemento  7
         #18#11 = #16#33;          || Elemento  8
         #18#12 = #16#37;          || Elemento  9
     |FINSI;
     |CIERRA #18;

     |ABRE #19;
     #19#0 = #17#0;
     #19#1 = #17#1;
     #19#2 = #17#2;
     #19#3 = #17#3;
     |LEE 040#19=;
     |SI FSalida ! 0;  |DDEFECTO #19;  |FINSI;
     |CIERRA #19;

     #99#0  = #17#5;
     #99#1  = #17#6;
     #99#2  = #17#1;
     #99#3  = #17#8;
     #99#4  = #17#15;
     #99#5  = #17#16;
     #99#6  = #17#17;
     #99#7  = #17#18;
     #99#8  = #17#19;
     #99#9  = #17#20;
     #99#10 = #17#21;
     #99#11 = #17#22;
     #99#12 = #17#23;
     #99#13 = #17#24;

     |SI #agicen14#13 = "N"; |O enDatos = 0;
         nCoBien = 0;
         |PRINT;
     |SINO;
         nCoBien = 1;
         |BUCLE RelComumeros;
     |FINSI;

     |PIEINF;
|FPRC;

|DEFBUCLE dsmom017;
     #17#1;
     ;
     #0#0, #0#26, 00, 01;
     #0#1, #0#26, 99, 99;
     ;
     NULL, Emite;
|FIN;

|PROCESO Tipo3;  |TIPO 3;
     enDatos = 0; |SI #0#27 = "S"; enDatos = 1; |FINSI;
     eaRenta = ""; enEjerR = 0;
     |SI #0#29 = "S"; eaRenta="Cod.Renta"; enEjerR = #0#26; |FINSI;

     enEjer    = #0#26;
     eaInac    = "S";
     eaTituloL = #0#28;
     aAlfa1 = #0#26; aAlfa1 = "01.01." + aAlfa1; efDFecha = aAlfa1;
     aAlfa1 = #0#26; aAlfa1 = "31.12." + aAlfa1; efHFecha = aAlfa1;

     |IMPRE 0;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     inform1 = "dsmoz006";
     |SI #0#26 ] 2009;
         inform1 = "dsmoz106";
     |FINSI;

     informe = inform1; |SI enDatos = 1; informe = inform2; |FINSI;
     |INFOR informe;
     |SI FSalida ! 0;  |FINIMP;  |ACABA;  |FINSI;

     |SI #0#0 ! 0;
         |BUCLE dsmom017;
     |SINO;
         |PARA nCampo = 2;  |SI nCampo [ 25;  |HACIENDO nCampo = nCampo + 1;
               |SI #0nCampo ! 0;
                   #0#0 = #nCampo;
                   #0#1 = #nCampo;
                   |BUCLE dsmom017;
               |FINSI;
         |SIGUE;
     |FINSI;

     |FININF;
     |FINIMP;
|FPRC;

|PROCESO LaRentaSI;
     eaCodR2    = "";
     eaCodRenta = "";
     |SI nOpTitular = 0;
         |SI #900#83 ! 0;
             eaCodRenta = "Codigo de Renta : " + (("00000" + #900#83) % -105);
             |ACABA;
         |FINSI;
     |FINSI;

     |ABRE #103;
     |SELECT #2#103;
     #103#1 = aElDni;
     |LEE 040#103=;
     |SI FSalida = 0;
         eaCodRenta = "Codigo de Renta : " + (("00000" + #103#0) % -105);
         eaCodR2    = (("00000" + #103#0) % -105);
     |SINO;
         |SELECT #3#103;
         #103#35 = aElDni;
         |LEE 040#103=;
         |SI FSalida = 0;
             eaCodRenta = "Codigo de Renta : " + (("00000" + #103#0) % -105);
             eaCodR2    = (("00000" + #103#0) % -105);
         |FINSI;
     |FINSI;
     |CIERRA #103;
|FPRC;
