|FICHEROS;
     dsmow136 #936;

     :/integral/def/dsam0000;
     :/integral/def/dsam0116 #116;
     :/integral/def/dsam0117 #117;

     :/basica/def/agifigen;
     :/basica/def/agibanco;

     :/contagen/def/canempre #800;
     :/modelos/def/dsmom100  #810;
     :/facturar/def/asclient #701;

     dsaw0035@ #916;
|FIN;

|VARIABLES;
     aBase       = "";
     aAlfa       = "";
     aMas        = "";
     aID         = "";

     nID         = 0;
     nNume       = 0;
     nFSalida    = 0;
     nBanco      = 0;

     {10} aPopup = "";

     &enSwDeDonde = 0;
     &eaEntidad   = "";
     &eaSucursal  = "";
     &eaCuenta    = "";
     &eaDC        = "";
     &eaCta       = "";
     &eaPais      = "";
     &eaIBAN      = "";
     &eaIBANant   = "";
     nSwC         = 0;
|FIN;

|PROCESO ConsEntidad;  |TIPO 66;

     |DBASS aMas;  |QBF aMas;
     |SI enSwDeDonde = 1;
         aMas = aMas + "integral/def/dsaw0035.mas";
     |SINO;
         aMas = aMas + "integral/def/agibanco.mas";
     |FINSI;
     |CARGA_DEF aMas, dsaw0035@;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |PUSHV 0501 2380;
     |ABRE #916;
     |SELECT #4#916;
     #916#2 = #936#2;
     #916#3 = #936#3;
     #916#0 = #936#0;
     |LEE 040#916];
     |SI FSalida ! 0;
         |LEE 040#916p;
     |FINSI;
     |CONSULTA_CLAVE #4#916;
     nFSalida = FSalida;
     |CIERRA #916;
     |POPV;

     |SI nFSalida = 0;
         #936#0  = #916#0;   |PINTA #936#0;
         #936#1  = #916#1;   |PINTA #936#1;
         #936#2  = #916#2;   |PINTA #936#2;
         #936#3  = #916#3;   |PINTA #936#3;
         #936#7  = #916#5;   |PINTA #936#7;
     |FINSI;

     |DESCARGA_DEF #dsaw0035@;
|FPRC;

|PROCESO SinBanco;
     |C_M #936#1,  1, "S";
     |C_M #936#3,  1, "S";
     |C_M #936#7,  1, "S";
     |C_M #936#4,  1, "S";
     |C_M #936#5,  1, "S";

     |SI #936#2 = "    ";
                                #936#0  = 0;   |PINTA #936#0;
         |C_M #936#1,  1, "N";  #936#1  = "";  |PINTA #936#1;
         |C_M #936#7, 1,  "N";  #936#7  = "";  |PINTA #936#7;
         |C_M #936#3,  1, "N";  #936#3  = "";  |PINTA #936#3;
         |C_M #936#5,  1, "N";  #936#5  = "";  |PINTA #936#5;
         |C_M #936#4,  1, "N";  #936#4  = "";  |PINTA #936#4;
     |FINSI;
|FPRC;

|PROCESO BancoBasica;
     |ABRE #agibanco;
     |SELECT #4#agibanco;
     #agibanco#2 = #936#2;
     #agibanco#3 = #936#3;
     #agibanco#0 = #936#0;
     |LEE 040#agibanco.];
     |SI FSalida ! 0;  |O #agibanco#2 ! #936#2;  |O  #agibanco#3 ! #936#3;
         #agibanco#2 = #936#2;
         #agibanco#3 = #936#3;
         #agibanco#0 = 0;
         |LEE 040#agibanco.];
         |SI FSalida ! 0;  |O #agibanco#2 ! #936#2;  |O  #agibanco#3 ! #936#3;
             |CONFI "0000SNo existe el Banco, Desea crearlo.";
             |SI FSalida ! 0;
                 |CIERRA #agibanco;
                 |ERROR;
                 |ACABA;
             |FINSI;

             |SELECT #1#agibanco;
             |PARA nBanco = 1;  |SI nBanco [ 99999;  |HACIENDO nBanco = nBanco + 1;
                   #agibanco#0 = nBanco;
                   |LEE 000#agibanco.=;
                   |SI FSalida ! 0;  |SAL;  |FINSI;
             |SIGUE;

             |SI nBanco > 99999;
                 |MENAV "000 No permito mas Bancos.";
                 |CIERRA #agibanco;
                 |ERROR;
                 |ACABA;
             |FINSI;

             |DDEFECTO #agibanco;
             #agibanco#0 = nBanco;
             #agibanco#2 = #936#2;
             #agibanco#3 = #936#3;
             |GRABA 020#agibanco.b;

             |PUSHV 0501 2380;
             |PINPA #0#agibanco;
             |PINDA #0#agibanco;
             |ENDATOS #1#agibanco;
             |SI FSalida = 0;
                 |GRABA 020#agibanco.a;
                 |LIBERA #agibanco;
             |SINO;
                 |BORRA 020#agibanco.a;
                 |LIBERA #agibanco;

                 |POPV;
                 |CIERRA #agibanco;
                 |ERROR;
                 |ACABA;
             |FINSI;
             |POPV;
         |FINSI;
     |FINSI;

     #936#0 = #agibanco#0;   |PINTA #936#0;
     #936#1 = #agibanco#1;   |PINTA #936#1;
     #936#7 = #agibanco#5;   |PINTA #936#7;
     |CIERRA #agibanco;
|FPRC;

|PROCESO BancoIntegral;
     |ABRE #116;
     |SELECT #4#116;
     #116#2 = #936#2;
     #116#3 = #936#3;
     #116#0 = #936#0;
     |LEE 040#116];
     |SI FSalida ! 0;  |O #116#2 ! #936#2;  |O  #116#3 ! #936#3;
         #116#2 = #936#2;
         #116#3 = #936#3;
         #116#0 = 0;
         |LEE 040#116];
         |SI FSalida ! 0;  |O #116#2 ! #936#2;  |O  #116#3 ! #936#3;
             |CONFI "0000SNo existe el Banco, Desea crearlo.";
             |SI FSalida ! 0;
                 |CIERRA #116;
                 |ERROR;
                 |ACABA;
             |FINSI;

             |SELECT #1#116;
             |PARA nBanco = 1;  |SI nBanco [ 99999;  |HACIENDO nBanco = nBanco + 1;
                   #116#0 = nBanco;
                   |LEE 000#116=;
                   |SI FSalida ! 0;  |SAL;  |FINSI;
             |SIGUE;

             |SI nBanco > 99999;
                 |MENAV "000 No permito mas Bancos.";
                 |CIERRA #116;
                 |ERROR;
                 |ACABA;
             |FINSI;

             |DDEFECTO #116;
             #116#0 = nBanco;
             #116#2 = #936#2;
             #116#3 = #936#3;
             |GRABA 020#116b;

             |PUSHV 0501 2380;
             |PINPA #0#116;
             |PINDA #0#116;
             |ENDATOS #1#116;
             |SI FSalida = 0;
                 |GRABA 020#116a;
                 |LIBERA #116;
             |SINO;
                 |BORRA 020#116a;
                 |LIBERA #116;

                 |POPV;
                 |CIERRA #116;
                 |ERROR;
                 |ACABA;
             |FINSI;
             |POPV;
         |FINSI;
     |FINSI;

     #936#0 = #116#0;   |PINTA #936#0;
     #936#1 = #116#1;   |PINTA #936#1;
     #936#7 = #116#5;   |PINTA #936#7;
     |CIERRA #116;
|FPRC;

|PROCESO ChequeaBanco;
     |SI #936#2 = "    ";  |ACABA;  |FINSI;

     |SI enSwDeDonde = 1;
         |HAZ BancoIntegral;
     |SINO;
         |HAZ BancoBasica;
     |FINSI;

     aAlfa = #936#1;  |QBF aAlfa;
     |C_M #936#1, 1, "N";
     |SI aAlfa = "";  |C_M #936#1, 1, "S";  |FINSI;

     aAlfa = #936#7;  |QBF aAlfa;
     |C_M #936#7, 1, "N";
     |SI aAlfa = "";  |C_M #936#7, 1, "S";  |FINSI;
|FPRC;

|PROCESO DC;
     eaEntidad  = #936#2; |QBF eaEntidad;
     eaSucursal = #936#3; |QBF eaSucursal;
     eaCuenta   = #936#5; |QBF eaCuenta;
     eaDC       = "";

     |SI eaEntidad = "";  |Y eaSucursal = "";  |Y eaCuenta = "";
         |ACABA;
     |FINSI;

     |HAZ CalculoDC;

     |SI FSalida ! 10;
         |SI #936#4 ! eaDC;
             aAlfa = "     El Digito de Control deberia ser " + eaDC;
             |MENAV aAlfa;
         |FINSI;
         |ACABA;
     |FINSI;

     #936#4 = eaDC;
     |PINTA #936#4;
|FPRC;

|PROCESO VerSwC; |TIPO 0;
   aAlfa = Contenido;
   |SI aAlfa ! #936Campo;
       nSwC = 1;
   |FINSI;
|FPRC;

|PROCESO Tipo66C6;  |TIPO 66;
     aAlfa = "|C006WID";
     aID   = #936#aAlfa#;
     nID   = aID;

     aPopup1  = nID;  || Si fuera -1 en la posicion
     aPopup2  = 6;
     aPopup3  = "1.- Efectivo";
     aPopup4  = "2.- Adeudo en cuenta";
     aPopup5  = "3.- Domiciliacion";
     aPopup6  = "4.- Aplazamiento";
     aPopup7  = "5.- Aplazamiento + Ingreso";
     aPopup8  = "6.- Cuenta corriente tributaria (Ingreso y Devolucion)";

     IaPopup  = aPopup1  <-;
     |ESPECIFICA "TRACKPOPUP", aPopup;
     |SI FSalida ! 0;
         IaPopup  = aPopup3  <-;
         IaPopup  = IaPopup + (FSalida - 1);

         aAlfa = aPopup % 101;  #936Campo = aAlfa;
     |FINSI;
     |PINTA #936Campo;
|FPRC;

|PROCESO Tipo7IBAN_Fw136;  |TIPO 7;
     aAlfa = #dsmow136#9;   |QBF aAlfa;

     |SI aAlfa ! ""; |Y nSwC = 0;  |ACABA;  |FINSI;

     |SI nSwC = 1; |Y aAlfa ! "";
         |CONFI "0000NSe han modificado los datos de la Cuenta Corriente. Recalcular el IBAN?";
         |SI FSalida ! 0;
             |ACABA;
         |FINSI;
     |FINSI;

     eaIBANant = "";
     eaCta  = #dsmow136#2 + #dsmow136#3 + #dsmow136#4 + #dsmow136#5;
     eaPais = #dsmow136#10;
     |HAZ CalcuIBAN;

     #dsmow136#9 = eaIBAN;
     |PINTA #dsmow136#9;
     nSwC = 0;
|FPRC;

|PROCESO Tipo0IBAN_Fw136;  |TIPO 0;
     |SI FSalida = 2; |ACABA; |FINSI;

     eaCta     = #dsmow136#2 + #dsmow136#3 + #dsmow136#4 + #dsmow136#5;
     eaPais    = #dsmow136#10;
     eaIBANant = #dsmow136#9;
     |HAZ CheqIBAN;

     aAlfa = #dsmow136#9;    |QBF aAlfa;
     |SI aAlfa ! eaIBAN;
         |SI aAlfa = "";
             #dsmow136#9 = eaIBAN;
             |PINTA #dsmow136#9;
         |SINO;
             |CONFI "0000SEl codigo IBAN puede ser erroneo, quiere calcularlo.";
             |SI FSalida = 0;
                 #dsmow136#9 = eaIBAN;
                 |PINTA #dsmow136#9;
                 |ERROR;
             |FINSI;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO Tipo12w136;  |TIPO 12;
     eaIBANant = #dsmow136#9;
     eaCta     = #dsmow136#2 + #dsmow136#3 + #dsmow136#4 + #dsmow136#5;
     eaPais    = #dsmow136#10;
     |HAZ CheqIBAN;

     aAlfa = #dsmow136#9;    |QBF aAlfa;
     |SI aAlfa ! eaIBAN; |Y aAlfa ! "";
         |MENAV "0000El codigo IBAN puede ser erroneo.";
     |FINSI;
     nSwC = 0;
|FPRC;
