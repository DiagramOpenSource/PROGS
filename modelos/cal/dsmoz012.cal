|FICHEROS;
     dsmoz012 #0;
     dsmom002 #2;
     dsmom005 #5;

     dsmom032 #32;
     dsmod309 #309;
     dsmow069 #999;

     :/basica/def/agifigen #100;
     :/basica/def/agitab99 #99;
     :/basica/def/agicen14 #214;
|FIN;

|VARIABLES;
     aFichero  = "";
     aAlfa     = "";

     nAnyo     = 0;
     nCampo    = 0;
     nDEmpresa = 0;
     nHEmpresa = 0;
     nGraba    = 0;

     fFecha    = @;

     &enControla  = 0;
     &enDesde   = 0;
     &enHasta   = 0;
     &enP2013   = 0;

|FIN;

|INCLUYE i_variar;

|PROCESO NoMeQuites;
     || Para que compile
     |HAZ ConsuCliente
|FPRC;

|| /////////////////////// PROCESOS ENTRADA POR PANTALLA \\\\\\\\\\\\\\\\\\\\

|PROCESO Parrilla;
     |SI #0#1 = 0;
         #0#1 = 0;  |PINTA #0#1;
         #0#2 = 0;  |PINTA #0#2;  |C_M #0#2, 1, "N";
         |PARA nCampo = 3;  |SI nCampo [ 14;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |SINO;
         |SI enEmpresa = 0;
             |C_M #0#1, 1, "S";
             |C_M #0#2, 1, "S";
         |FINSI;

         |PARA nCampo = 3;  |SI nCampo [ 14;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "N";
               #0nCampo = 0;  |PINTA #0nCampo;
         |SIGUE;
     |FINSI;
|FPRC;

|PROCESO NoMasEmpresas;
     |SI #0Campo = 0;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 14;  |HACIENDO nCampo = nCampo + 1;
               #0nCampo = 0;  |C_M #0nCampo, 1, "N";
         |SIGUE;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 14;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |FINSI;
|FPRC;

|PROCESO SacaAnyo;  |TIPO 7;
     fFecha = ~;
     aAlfa  = fFecha % -104;
     #0#0   = aAlfa;
     |PINTA #0#0;
|FPRC;

|| /////////////////////// PROCESOS DE TRABAJO \\\\\\\\\\\\\\\\\\\\

|PROCESO GrabaTempo;
    |ABRE #100;
    #100#0 = #309#0;
    |LEE 040#100=;
    |SI FSalida ! 0;  |DDEFECTO #100;  |FINSI;
    |CIERRA #100;

     #999#0 = #309#0;
     #999#1 = #309#1;
     |LEE 101#999=;
     |SI FSalida ! 0;
         |DDEFECTO #999;
         #999#0 = #309#0;
         #999#1 = #309#1;
         #999#2 = #309#2;
         #999#3 = #100#1;
         #999#4 = #309#8;
         #999#5 = "T";
         |SI #309#3 = 99;  #999#5 = "A";  |FINSI;
         |GRABA 020#999n;

         nGraba = 1;
     |FINSI;
     |LIBERA #999;
|FPRC;

|DEFBUCLE dsmod309;
     #309#1;
     ;
     nDEmpresa, 01, #0#0, 00, 00, 01;
     nHEmpresa, 98, #0#0, 99, 99, 99;
     ;
     NULL, GrabaTempo;
|FIN;

|PROCESO GrabaTempo2;
     aAlfa = #2#8 % -104;
     |SI aAlfa ! "0000";
         nAnyo = aAlfa;
         |SI nAnyo > #0#0;  |ACABA;  |FINSI;
     |FINSI;

     aAlfa = #2#9 % -104;
     |SI aAlfa ! "0000";
         nAnyo = aAlfa;
         |SI nAnyo < #0#0;  |ACABA;  |FINSI;
     |FINSI;

     #999#0 = #2#0;
     #999#1 = #2#1;
     |LEE 101#999=;
     |SI FSalida ! 0;
         |SI #0#16 = "S";
             #999#0     = #2#0;
             #999#1     = #2#1;
             #999#2     = #0#0;
             enControla = 0;

             |SI #2#7 = "TRIMESTRAL";  #999#5 = "T";  |FINSI;
             |SI #2#7 = "ANUAL     ";  #999#5 = "A";  |FINSI;

             |HAZ ControlaContab;
             |SI enControla = 0;  |ACABA;  |FINSI;
         |FINSI;

         |ABRE #100;
         #100#0 = #2#0;
         |LEE 040#100=;
         |SI FSalida ! 0;  |DDEFECTO #100;  |FINSI;
         |CIERRA #100;

         |DDEFECTO #999;
         #999#0 = #2#0;
         #999#1 = #2#1;
         #999#2 = #0#0;
         #999#3 = #100#1;
         #999#4 = #2#6;
         |SI #2#7 = "TRIMESTRAL";
             #999#5 = "T";
         |FINSI;
         |SI #2#7 = "ANUAL     ";
             #999#5 = "A";
         |FINSI;

         |GRABA 020#999n;

         nGraba = 1;
     |FINSI;
     |LIBERA #999;
|FPRC;

|DEFBUCLE dsmom002;
     #2#1;
     ;
     nDEmpresa, 01, 309;
     nHEmpresa, 98, 309;
     ;
     NULL, GrabaTempo2;
|FIN;

|PROCESO GrabaTempo3;
     |SI #214#7 ! 1;  |Y #214#7 ! 3; |Y #214#7 ! 9;
         |ACABA;
     |FINSI;

     |ABRE #5;
     #5#0 = 309;
     |LEE 000#5=;
     |SI FSalida ! 0;  |DDEFECTO #5;  |FINSI;
     |CIERRA #5;

     #999#0 = #214#0;
     #999#1 = #214#1;
     |LEE 101#999=;
     |SI FSalida ! 0;
         |ABRE #100;
         #100#0 = #214#0;
         |LEE 040#100=;
         |SI FSalida ! 0;  |DDEFECTO #100;  |FINSI;
         |CIERRA #100;

         |SI #0#16 = "S";
             #999#0     = #214#0;
             #999#1     = #214#1;
             #999#2     = #0#0;
             enControla = 0;

             |SI #5#5 = "T";  #999#5 = "T";  |FINSI;
             |SI #5#5 = "A";  #999#5 = "A";  |FINSI;
             |SI #5#5 = "C";  #999#5 = "T";  |FINSI;

             |HAZ ControlaContab;
             |SI enControla = 0;  |ACABA;  |FINSI;
         |FINSI;

         |DDEFECTO #999;
         #999#0 = #214#0;
         #999#1 = #214#1;
         #999#2 = #0#0;
         #999#3 = #100#1;
         #999#4 = #214#4;

         |SI #5#5 = "T";  #999#5 = "T";  |FINSI;
         |SI #5#5 = "A";  #999#5 = "A";  |FINSI;
         |SI #5#5 = "C";  #999#5 = "T";  |FINSI;

         |GRABA 020#999n;

         nGraba = 1;
     |FINSI;
     |LIBERA #999;
|FPRC;

|DEFBUCLE agicen14;
     #214#1;
     ;
     nDEmpresa, 01;
     nHEmpresa, 99;
     ;
     NULL, GrabaTempo3;
|FIN;

|RUTINA ClaveBaja999;
     #999#0 = 1;
     #999#1= 1;
|FRUT;

|RUTINA ClaveAlta999;
     #999#0 = 99999;
     #999#1= 98;
|FRUT;

|PROCESO Tipo3;  |TIPO 3;
     aFichero = ("69" + Usuario) % 108;
     |NOME_DAT #999 aFichero;
     |ABRE #999;  |CIERRA #999;  |DELINDEX #999;

     |ABRE #999;
     |SI #0#1 ! 0;
         nDEmpresa = #0#1;
         nHEmpresa = #0#2;
         |BUCLE dsmod309;
         |BUCLE dsmom002;
         |SI #0#15 = "S";  |BUCLE agicen14;  |FINSI;
     |SINO;
         |PARA nCampo = 3;  |SI nCampo [ 14;  |HACIENDO nCampo = nCampo + 1;
               nDEmpresa = #0nCampo;
               nHEmpresa = #0nCampo;
               |BUCLE dsmod309;
               |BUCLE dsmom002;
               |SI #0#15 = "S";  |BUCLE agicen14;  |FINSI;
         |SIGUE;
     |FINSI;
     |CIERRA #999;

     |SI nGraba = 1;
         |ENTLINEAL #1#999, -1, 4, 22, 1, ClaveBaja999, ClaveAlta999;
     |SINO;
         |MENAV "     Seleccion Vacia.";
     |FINSI;

     |DELINDEX #999;
|FPRC;

|PROGRAMA;
     |ABRE #99;
     #99#0 = 309;
     |LEE 040#99=;
     |SI FSalida ! 0;  |DDEFECTO #99;  |FINSI;
     |CIERRA #99;

     |SI #99#4 ! "S";  |O #99#5 ! "C";  |O #99#6 ! "F";
         enDesde = 309;
         enHasta = 309;
         eaExtension = "bas";
         eaPrograma  = "agitab99";
         |SUB_EJECUTA_NP ":basica/dspatron";
         |SUB_EJECUTA_NP "dsmom005";
     |FINSI;

     enP2013 = 0;
     |ABRE #32;
     |LEE 041#32p;
     |SI FSalida ! 0;
         |CIERRA #32;
         eaExtension = "mod";
         eaPrograma  = "dsmom032";
         |SUB_EJECUTA_NP ":basica/dspatron";
         |ABRE #32;
     |FINSI;
     |LEE 041#32p;
     |SI FSalida ! 0; enP2013 = 1;  |FINSI;

     |CIERRA #32;

     |SI enEmpresa ! 0;
         #0#0 = enEjer;
         #0#1 = enEmpresa;
         #0#2 = enEmpresa;
         |HAZ Tipo3;
     |SINO;
         |CLS;
         |CABEZA "Entrada Modelo 309";
         |DDEFECTO #0;
         |PINPA #0#0;
         |PINDA #0#0;
         |ENDATOS #1#0;
     |FINSI;

     |HAZ FinalizaEnX;
|FPRO;

|PROCESO Tipo88;  |TIPO 88;
     |HAZ Relaciones;
|FPRC;
