|FICHEROS;
    dsmoy045 #0;
    dsmom040 #40;
    dsmom041 #41;
    dsmom042 #42;
    dsmom046 #46;

    :/basica/def/agicen14 #114;

    dsmom100 #100;
    dsmom107 #107;
    :/contagen/def/caconimp #306;  || configuracion impresiones

    dsmow123 #123;
    dsmow030 #900;
    dsmow080 #998;
    dsmow043 #999;
|FIN;

|VARIABLES;
  nAnyV     = 0;
  swModulo  = 0;
  sw        = 0;
  swPrimero = 0;
  inform1   = "dsmoy045";
  inform2   = "dsmoym45";
  inform3   = "dsmoyn45";
  informe   = "";
  &acontab  = 0;
  &vcontab  = 0;
  &afiscal  = 0;
  &vfiscal  = 0;

  fecalf      = "";
  nDEmpresa   = 0;
  nHEmpresa   = 0;
  nDActividad = 0;
  nHActividad = 0;
  nCampo      = 0;
  &nCamSel    = 23;
  &nCamTip    = 46;
  swBueno     = 0;

  &nAmordef   = 0;
  &nAmorpro   = 0;
  aAlfa       = "";
  aAlfa1      = "";
  aAlfa2      = "";
  nImpor1     = 0;
  nImpor2     = 0;
  nImpor3     = 0;
  nImpor4     = 0;
  nSwSi       = 0;
  nSwSi1      = 0;
  nSwSi2      = 0;
  nLinea      = 0;

  &nNumMod    = 0;
  &nSwTot     = 0;
  nGTempo     = 0;

    Lineas    = 0;
    Documento = 0;
    sw_tmp    = 0;
    nImporte  = 0;

  aFichero = "";
  cuatro   = "";
  cinco    = "";
  cuenta   = "";
  cuenta1  = "";
  conter   = 0;
  signo    = "";
  aCtaAna  = "";
  enGrupo  = 0;
  x        = 0;
  aLong    = "";
  nLong    = 0;
  aPath    = "";
  nOk = 0;
  {30}aPopup        = "";
     aContenido     = "";
     aID            = "";
     nID            = 0;
|FIN;

|INCLUYE i_variar;
|INCLUYE i_varcon;
|INCLUYE i_varlib;
|INCLUYE i_numamo;
|INCLUYE i_selcon;

|| //////////////////// Calculos de Pantalla \\\\\\\\\\\\\\\\\\\\\\\\\\
|CALCULO Tipo7;  |TIPO 7;
     |SI enEmpresa ! 0;
         #0#7 = enEmpresa; |PINTA #0#7;
         #0#8 = enEmpresa; |PINTA #0#8;

         |C_M #0#7, 1, "N";
         |C_M #0#8, 1, "N";
     |FINSI;
|FCAL;

|CALCULO Parrilla;
     |SI #0#7 = 0;
         #0#7 = 0;  |PINTA #0#7;
         #0#8 = 0;  |PINTA #0#8;  |C_M #0#8, 1, "N";
         |PARA nCampo = 9;  |SI nCampo [ 20;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |SINO;
         |SI enEmpresa = 0;
             |C_M #0#7, 1, "S";
             |C_M #0#8, 1, "S";
         |FINSI;

         |PARA nCampo = 9;  |SI nCampo [ 20;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "N";
               #0nCampo = 0;  |PINTA #0nCampo;
         |SIGUE;
     |FINSI;
|FCAL;

|CALCULO NoMasEmpresas;
     |SI #0Campo = 0;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 20;  |HACIENDO nCampo = nCampo + 1;
               #0nCampo = 0;  |C_M #0nCampo, 1, "N";
         |SIGUE;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 20;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |FINSI;
|FCAL;

|CALCULO numamo; |TIPO 0;
    x_alfa1 = #0Campo;
    |HAZ x_punto;
    #0Campo = x_alfa1;
    |PINTA #0Campo;
|FCAL;

|PROCESO DatosEmp; |TIPO 7;

   |DDEFECTO #306;

   |ABRE #100;
   |LEE 040#100p;
   |SI FSalida = 0;
       aAlfa1 = #100#1; |QBF aAlfa1; aAlfa1 = ("00000" + aAlfa1) % -105;
       aAlfa2 = #100#2; |QBF aAlfa2; aAlfa2 = ("0"     + aAlfa2) % -101;
       |DBASS aPath; |QBT aPath;
       aPathConta = aPath + "contagen/fich/" + aAlfa1 + aAlfa2 + "/";

       |PATH_DAT #306 aPathConta;

       |ABRE #306;
       #306#0 = 1;
       |LEE 000#306=;
       |CIERRA #306;
   |FINSI;
   |CIERRA #100;

   #0#58  = #306#7; |PINTA #0#58;
   aAlfa1 = #306#8; |C_M #0#58,1,aAlfa1;

   |ABRE #107;
   |DDEFECTO #107;
   |LEE 041#107p;
   |CIERRA #107;

   |SI #107#28 = "S";
       |C_V #0#60, 1, "S"; |C_M #0#60, 1, "S";
       |C_V #0#61, 1, "S"; |C_M #0#61, 1, "S";
       |PINTA 0931, "Activ./Subdepar. ";
       |PINTA #0#60;
       |PINTA #0#61;
   |FINSI;
|FPRC;

|CALCULO porDefecto; |TIPO 7;
  fecalf = ~;
  fecalf = "31.12" + (fecalf % -104);
  #0#3 = fecalf;
|FCAL;

|CALCULO defecano;
  fecalf = #0#3;
  fecalf = fecalf % -104;
  #0#2 = fecalf;
  #0#53 = #0#3; |PINTA #0#53;
|FCAL;

|CALCULO SegunF; |TIPO 0;
 aAlfa1 = #0Campo;
 |C_M #0#62,1,aAlfa1;
 |C_M #0#40,1,aAlfa1;
 |C_M #0#41,1,aAlfa1;
 |C_M #0#44,1,aAlfa1;
 |C_M #0#45,1,aAlfa1;

 |SI #0#47 = "S";
     #0#62 = 00;  |PINTA #0#62;
     |C_M #0#62,1,"N";
     |C_M #0#41,1,"N";
     |C_M #0#45,1,"N";
 |FINSI;
|FCAL;

|CALCULO Tipo66C62; |TIPO 66;
     aAlfa = "|C057WID";
     aID   = #0#aAlfa#;
     nID   = aID;

     aPopup1  = nID;  || Si fuera -1 en la posicion
     aPopup2  = 12;
     aPopup3  = "00. Según lo asignado  en la ficha";
     aPopup4  = "01. Amortización lineal según tablas (cálculo automático)";
     aPopup5  = "02. Amor. según porcentaje constante sobre el imp. pendiente de amor. (cálculo automático)";
     aPopup6  = "03. Amortización según números dígitos - creciente (cálculo automático)";
     aPopup7  = "04. Planes de amortización (cálculo automático)";
     aPopup8  = "05. Depreciación efectiva probada fehacientemente (cálculo manual)";
     aPopup9  = "06. Libertad de Amortización (cálculo manual)";
     aPopup10 = "07. Amortización Acelerada (cálculo automático)";
     aPopup11 = "08. Amortización de Elementos Adquiridos Usados (cálculo automático)";
     aPopup12 = "09. Amortización de Elementos Patrimoniales Objeto de Reinversión - DT 28º LIS (cálculo manual)";
     aPopup13 = "10. Amortización de Activos Nuevos Adquiridos en 2003 y 2004 - DT 13º LIS (cálculo manual)";
     aPopup14 = "80. Amortización según valor residual a 2015 (cálculo automático)";

     IaPopup  = aPopup1  <-;
     |ESPECIFICA "TRACKPOPUP", aPopup;
     |SI FSalida ! 0;
         IaPopup  = aPopup3  <-;
         IaPopup  = IaPopup + (FSalida - 1);

         aAlfa = aPopup % 102;  #0#62= aAlfa;
     |FINSI;

     |PINTA #0#62;
|FCAL;

|CALCULO Tipo0C62; |TIPO 0;
   |BLIN 4;
   |C_M #0#62,0,aAlfa1;
   |SI aAlfa1 = "N"; |ACABA; |FINSI;

   nOk = 0;
   |SI #0#62 ] 0; |Y #0#62 [ 10; nOk = 1; |FINSI;
   |SI #0#62 = 80;               nOk = 1; |FINSI;
   |SI nOk = 0;
       |MENAV "0000 Tipo de Amortización incorrecto";
       |ERROR;
       |ACABA;
   |FINSI;

   aAlfa = "M";
   |SI #0#62 = 0; aAlfa = "X"; |FINSI;
   |SI #0#62 = 1; aAlfa = "N"; |FINSI;
   |SI #0#62 = 2; aAlfa = "D"; |FINSI;
   |SI #0#62 = 3; aAlfa = "A"; |FINSI;
   |SI #0#62 = 4; |Y #0#36 ! 0; aAlfa = "F"; |FINSI;
   |SI #0#62 = 80; aAlfa = "R"; |FINSI;
   |SI #0#62 = 7; aAlfa = "N"; |FINSI;
   |SI #0#62 = 8; aAlfa = "N"; |FINSI;
   #0#39 = aAlfa; #0#43 = aAlfa;

   |C_M #0#40, 1, "S";
   |C_M #0#41, 1, "S";
   |C_M #0#44, 1, "S";
   |C_M #0#45, 1, "S";
   |SI #0#39 = "F";
       #0#40 = 0; |PINTA #0#40;
       #0#41 = 0; |PINTA #0#41;
       #0#44 = 0; |PINTA #0#44;
       |C_M #0#40, 1, "N";
       |C_M #0#41, 1, "N";
       |C_M #0#44, 1, "N";
   |FINSI;
   |SI #0#39 = "N";
       #0#41 = 0; |PINTA #0#41;
       #0#45 = 0; |PINTA #0#45;
       |C_M #0#41, 1, "N";
       |C_M #0#45, 1, "N";
   |FINSI;
   |SI #0#39 = "M"; |O #0#39 = "X";
       #0#40 = 0; |PINTA #0#40;
       #0#41 = 0; |PINTA #0#41;
       #0#44 = 0; |PINTA #0#44;
       #0#45 = 0; |PINTA #0#45;
       |C_M #0#40, 1, "N";
       |C_M #0#41, 1, "N";
       |C_M #0#44, 1, "N";
       |C_M #0#45, 1, "N";
   |FINSI;
|FCAL;

|CALCULO LoModi; |TIPO 7;
 aAlfa1 = "S";
 |SI #0#38 = "N"; |Y #0#42 = "N";
     aAlfa1 = "N";
     #0#57 = "N"; |PINTA #0#57;
 |FINSI;
 |C_M #0#57,1,aAlfa1;
|FCAL;

|CALCULO ElModulo; |TIPO 0;
 |SI #0#47 = "S";
     |C_M #0#48, 1, "S";
     |C_M #0#49, 1, "S";
     |C_M #0#50, 1, "S";
     |C_M #0#59, 1, "N";
     #0#48 = 0;   |PINTA #0#48;
     #0#49 = 8;   |PINTA #0#49;
     #0#50 = "S"; |PINTA #0#50;
     #0#59 = 1;   |PINTA #0#59;
     |ATRI I; |PINTA 1453, "Provis:";
              |PINTA 1553, "A.Defi:"; |ATRI 0;
 |SINO;
     |C_M #0#48, 1, "N";
     |C_M #0#49, 1, "N";
     |C_M #0#50, 1, "N";
     |C_M #0#59, 1, "S";
     #0#48 = 0;   |PINTA #0#48;
     #0#49 = 0;   |PINTA #0#49;
     #0#50 = "N"; |PINTA #0#50;

     |C_M #0#38, 1, "S";
     |ATRI I; |PINTA 1453, "Fiscal:";
              |PINTA 1553, "Contab:"; |ATRI 0;
 |FINSI;
|FCAL;


|CALCULO CrearAsto;
 aAlfa1 = #0Campo;
 |C_M #0#52,1,aAlfa1; |C_M #0#53,1,aAlfa1;
 |C_M #0#54,1,aAlfa1; |C_M #0#55,1,aAlfa1;
 |C_M #0#56,1,aAlfa1;
 |SI aAlfa1 = "N";
     |PDEFECTO #0, 52,57;
     |PINTA #0#52; |PINTA #0#53;
     |PINTA #0#54; |PINTA #0#55;
     |PINTA #0#56;
 |FINSI;
|FCAL;

|CALCULO ElPtec;
 |C_M #0Campo,0,aAlfa1;
 |SI aAlfa1 = "N"; |ACABA; |FINSI;

 |PTEC 816;
|FCAL;

|CALCULO lFormato;
 |SI #0#59 = 2;
     #0#4 = "INFORME DE PREVISION DE AMORTIZACIONES CON DETALLE DE LA TRIBUTACION FISCAL ";
     |PINTA #0#4;
 |FINSI;
|FCAL;
|| *******************************************************************
|| /////////////////////////// Procesos \\\\\\\\\\\\\\\\\\\\\\\\\\\\\\

|PROCESO BuscaAny0;
 |SI #dsmom042#9 < #0#3;
     nImpor3 = (nImpor3 - #dsmom042#7)  ! EURODBMOD;
     nImpor4 = (nImpor4 - #dsmom042#11) ! EURODBMOD;
 |FINSI;
 |SI #dsmom042#9 = #0#3;
     nSwSi  = 1;
     nImpor1 = #dsmom042#7;
     nImpor2 = #dsmom042#11;
     nLinea  = #dsmom042#4;
 |FINSI;
|FPRC;

|DEFBUCLE BuscaAny;
   #dsmom042#1;
   6;
   #41#0, #41#2, #41#3, 001, "A";
   #41#0, #41#2, #41#3, 999, "A";
   e;
   NULL, BuscaAny0;
|FIN;

|PROCESO C_Amortiza;
  |SI #0#6 = "A";  |Y #41#24 ! "A";  |ACABA;  |FINSI;
  |SI #0#6 = "P";  |Y #41#24 ! "P";  |ACABA;  |FINSI;

  nImpor3 = #41#10;
  nImpor4 = #41#10;
  nSwSi   = 0;   || ... Busca si ya esta amortizado
  nImpor1 = 0;
  nImpor2 = 0;

  |BUCLE BuscaAny;

  |SI nSwSi = 0;
      |SI #41#16 = 0;  |Y #41#22 = 0;  |ACABA;  |FINSI;
  |FINSI;

  |SI #41#40 = "I";
      |SI #41#26 < "01.01.1000";
          |ACABA;
      |SINO;
          aAlfa1 = #41#26; aAlfa1 = aAlfa1 % -104;
          nAnyV = aAlfa1;
          |SI nAnyV < #0#2; |ACABA; |FINSI;
      |FINSI;
  |SINO;
      |SI #41#26 > "01.01.1000";
          aAlfa1 = #41#26; aAlfa1 = aAlfa1 % -104;
          nAnyV = aAlfa1;
          |SI nAnyV < #0#2; |ACABA; |FINSI;
      |FINSI;
  |FINSI;

  nSwSi1 = 0;     |SI #0#47 = "N"; nSwSi1 = nSwSi; |FINSI;
  nSwSi2 = nSwSi;

  |SI swPrimero = 0;  ||Controla el primer pase
      swPrimero = 1;
      enEmpresa   = #41#0;
      eaNombreEmp = #41#32;
      sw = 0;

      swBueno = 0;
      |HAZ LaSelEmpresa;
      |SI enSwSelEmp = 1;
          swBueno = 1;
      |SINO;
          |SI enDatos = 1;
              enActividad = 0;
              |HAZ DatosEmpr_w30;
          |FINSI;
          |HAZ GrabaLaEmpresa;
          |SI #107#28 = "S";  |HAZ eBuscoSubdep; |FINSI;
      |FINSI;
   |FINSI;

   |SI enEmpresa ! #41#0; || Controla la Ruptura por Empresas
      |SI sw = 1;
          |SI #0#50 = "S";
              nSwTot = 1;
              |PRINT;
          |FINSI;
          |PIEINF; || imprime el pie del informe
      |FINSI;
      enEmpresa   = #41#0;
      eaNombreEmp = #41#32;
      sw     = 0;
      nSwTot = 0;

      swBueno = 0;
      |HAZ LaSelEmpresa;
      |SI enSwSelEmp = 1;
          swBueno = 1;
      |SINO;
          |SI enDatos = 1;
              enActividad = 0;
              |HAZ DatosEmpr_w30;
          |FINSI;
          |HAZ GrabaLaEmpresa;
          |SI #107#28 = "S";  |HAZ eBuscoSubdep; |FINSI;
      |FINSI;
  |FINSI;
  |SI swBueno = 1; |ACABA; |FINSI;

  eaCodSubdep = "";
  |SI #107#28 = "S"; |Y eaSubdeT = "S";
      eaCodSubdep = #41#54;
      |SI #41#54 < #0#60; |O #41#54 > #0#61;
          |ACABA;
      |FINSI;
  |FINSI;

  enEmpresa   = #41#0;
  enActividad = #41#1;
  eaElemento  = #41#2;
  enElemeNum  = #41#3;
  |HAZ ActModulo;
  |SI #0#47 = "S";
      |SI swModulo = 0; |ACABA; |FINSI;
  |SINO;
      |SI swModulo ! 0; |ACABA; |FINSI;
  |FINSI;

  nAnyAmor    = #0#2;
  fFechaAmor  = #0#3;
  nConcpAmor  = #0#5;
  aConcpAmor  = #0#4;

  |SI #0#38 = "S";
      |SI #0#47 = "N";
          aAlfa1 = #0#39;
          |SI #0#39 = "R"; |Y #41#9 ] "01.01.2015";
              #0#39 = "X";
          |FINSI;
          |SI #0#39 ! "X";  #41#13 = #0#39;  |FINSI;
          #41#14 = #0#40;
          #41#17 = #0#41;
          nSwSi1 = 0;
          #0#39 = aAlfa1;
      |SINO;
          #41#29 = #0#40;
      |FINSI;
  |FINSI;
  |SI #0#42 = "S";
      |SI #0#47 = "N";
          aAlfa1 = #0#43;
          |SI #0#43 = "R"; |Y #41#9 ] "01.01.2015";
              #0#43 = "X";
          |FINSI;
          |SI #0#43 ! "X";  #41#19 = #0#43;  |FINSI;
          #41#20 = #0#44;
          #41#23 = #0#45;
          nSwSi2 = 0;
          #0#43  = aAlfa1;
      |SINO;
          #41#30 = #0#44;
      |FINSI;
  |FINSI;

  swAmBuena = 0;
  |SI nSwSi1 = 0; |O nSwSi2 = 0;
      |SI #0#47 = "N";            || Calculo Normal
          |HAZ CalculaAmor;
          |SI swAmBuena = 0;
               eSWGrabar = 1;  || no graba solo calcula el final
               |HAZ GrabaAmor;
          |FINSI;
      |SINO;
          |SI #41#28 = 0;
              #41#29 = 100;
              #41#30 = 100;
              #41#31 = 100;
          |FINSI;
          |HAZ CalAmorModulo;
          amorfis = nAmorpro;
          amorcon = nAmordef;
      |FINSI;
  |FINSI;

  |SI swAmBuena = 0;

      eSWGrabar = 1;
      nLineaAmor = ultlin + 1; |SI nSwSi = 1; nLineaAmor = nLinea; |FINSI;
      afiscal = amorfis; |SI nSwSi1 = 1; afiscal = nImpor1; |FINSI;
      acontab = amorcon; |SI nSwSi2 = 1; acontab = nImpor2; |FINSI;

      vfiscal = nImpor3 - afiscal;
      vcontab = nImpor4 - acontab;

      |SI sw = 0;
          nNumMod = #41#28;
      |FINSI;

      |SI #0#50 = "S";
          |SI nNumMod ! #41#28;
              nSwTot = 1;
              |PRINT;
              nNumMod = #41#28;
          |FINSI;
      |FINSI;
      sw = 1;
      nSwTot = 0;
      |PRINT;
      nNumMod = #41#28;
      |SI #0#51 = "S"; |HAZ ElAuxiliar; |FINSI;
  |FINSI;
|FPRC;

|DEFBUCLE dsmoy045;
   #41#1;
   1,28,35;
   nDEmpresa,#0#0,#0#36,nDActividad, #0#48,"N";
   nHEmpresa,#0#1,#0#37,nHActividad, #0#49,"N";
   e;
   NULL, C_Amortiza;
|FIN;

|DEFBUCLE dsmoyS45;
   #41#5;
   1,35;
   nDEmpresa, #0#48, #0#0,#0#36,nDActividad, "N";
   nHEmpresa, #0#49, #0#1,#0#37,nHActividad, "N";
   e;
   NULL, C_Amortiza;
|FIN;

|CALCULO Tipo3;  |TIPO 3;
  |HAZ LeeEURODBMOD;
  enDatos   = 0; |SI #0#58 = "S"; enDatos = 1; |FINSI;
  eaInac    = #0#23;
  eaTituloL = #0#4;
  aAlfa1    = #0#3; aAlfa1 = aAlfa1 % -104;
  enEjer    = aAlfa1;
  aAlfa1    = "01.01." + aAlfa1; efDFecha = aAlfa1;
  efHFecha  = #0#3;

  #0#42 = #0#38;

  |IMPRE 0;
  |SI FSalida ! 0;  |ACABA;  |FINSI;

  informe = inform1;
  |SI #0#47 = "S"; informe = inform2; |FINSI;
  |SI #0#59 = 2;   informe = inform3; |FINSI;

  |INFOR informe;
  |SI FSalida ! 0;  |FINIMP;  |ACABA;  |FINSI;

  efFecTope   = #0#3;
  nDActividad = #0#21;
  nHActividad = #0#22;

    |SI #0#51 = "S";

        aFichero = ("80w" + Usuario) % 108;
        |NOME_DAT #998 aFichero;
        |ABRE #998;  |CIERRA #998;  |DELINDEX #998;

        aFichero = ("123w" + Usuario) % 108;
        |NOME_DAT #123 aFichero;
        |ABRE #123;  |CIERRA #123;  |DELINDEX #123;
        eaFAsiento = aFichero;

        aFichero = ("043w" + Usuario) % 108;
        |NOME_DAT #999 aFichero;
        |ABRE #999;  |CIERRA #999;  |DELINDEX #999;
        eaFEmpresa = aFichero;

        enTipoAct   = 2;  || Tipo 2  Diario y Fecha de las Lineas, Varios Astos.
        enRelaIVA   = 0;  || Sin Relacionar con IVA
        swSinFich   = 1;  || Con Fichero Auxiliar Empresas
        swSinCheq   = 0;  || Sin Chequear Cuadre

    |FINSI;

    |SI #0#7 ! 0;
         nDEmpresa = #0#7;
         nHEmpresa = #0#8;
         |SI #0#50 = "N"; |BUCLE dsmoy045; |FINSI;
         |SI #0#50 = "S"; |BUCLE dsmoyS45; |FINSI;
     |SINO;
         |PARA nCampo = 9; |SI nCampo [ 20;  |HACIENDO nCampo = nCampo + 1;
               nDEmpresa = #0nCampo;
               nHEmpresa = #0nCampo;
               |SI #0#50 = "N"; |BUCLE dsmoy045; |FINSI;
               |SI #0#50 = "S"; |BUCLE dsmoyS45; |FINSI;
         |SIGUE;
     |FINSI;
     |SI sw = 1;
          |SI #0#50 = "S";
              nSwTot = 1;
              |PRINT;
          |FINSI;
          |PIEINF; || imprime el pie del informe
     |FINSI;
     |FININF;
     |FINIMP;

     |SI #0#51 = "S";
         |HAZ SiAsiento;
     |FINSI;
|FCAL;


|PROCESO ActModulo;
  swModulo = 0;
  || Lee Actividad
    |ABRE #114;
    #114#0 = enEmpresa;
    #114#1 = enActividad;
    |LEE 041#114=;
    |SI FSalida ! 0;
        |CIERRA #114;
        |ACABA;
    |FINSI;

    swModulo = 0;
    |SI #114#9  = 5; |O #114#9  = 10;
        swModulo = 1;
    |FINSI;
    |SI #114#9 = 6; |O #114#9 = 7; |O #114#9 = 11; |O #114#9 = 12;
        swModulo = 1;   ||Agricolas  (Paco 30.05.2001)
        |SI #114#7 = 9; |Y #114#9 = 7;
            swModulo = 2;  || adade lugo 28.05.2003
        |FINSI;
    |FINSI;

    |CIERRA #114;
|FPRC;

|| ***************************************************
|| ***** Procesos especiales para contabilizar
|| ***************************************************
|| ....................... Situa la Empresa Contable
|PROCESO GrabaLaEmpresa;

  |SI #0#51 = "N"; |ACABA; |FINSI;


  efFechaSit = #0#53;
  aAlfa1     = efFechaSit;
  aAlfa1     = aAlfa1 % -104;
  enEjer     = aAlfa1;
  enPerConta = -1;
  swerror    = 0;
  enSwSinMen = 1;
  enConcreto = 1;
  |HAZ SituaContagen;
  enSwSinMen = 0;
  |SI swerror = 1;    |ACABA;      |FINSI;

  |ABRE #999;
  nGTempo = nGTempo + 1;
  #999#0 = nGTempo;      || Guia
  #999#1 = enEmpresa;    || Empresa
  #999#2 = enPerConta;   || Periodo Contable
  #999#3 = enEjer;       || A¤o
  #999#4 = dig1;         || Desde Mes
  #999#5 = dig3;         || N§ Digitos
  #999#6 = dig4;         || Digitos Nivel 1
  #999#7 = dig5;         || Digitos Nivel 2
  #999#8 = dig6;         || Digitos Nivel 3
  #999#9 = dig7;         || Digitos Nivel 4
  #999#10 = dig8;        || Digitos Nivel 5
  #999#11 = dig9;        || Digitos Nivel 6
  #999#12 = eaEstadoEmp; || Activa / Inactiva
  #999#13 = eaMonedaCon; || Moneda
  #999#14 = eaNombreEmp; || Nombre Empresa
  #999#15 = aPathConta;  || Path
  #999#17 = eaDepar;     || Si/No departamentos
  #999#18 = eaSubde;     || Si/No subdepartamentos
  #999#19 = eaDfDepC;
  #999#20 = eaDfSubC;
  #999#21 = eaDfDepN;
  #999#22 = eaDfSubN;
  #999#23 = fec3;

  |GRABA 020#999n;
  |CIERRA #999;
  enConcreto = 0;
|FPRC;

|PROCESO ElAuxiliar;
 |ABRE #998;
 #998#0  = enEmpresa;     || Empresa
 #998#1  = enActividad;   || Actividad
 #998#2  = eaElemento;    || Elemento
 #998#3  = enElemeNum;    || Numero
 #998#4  = #0#52;         || Diario
 #998#5  = #0#53;         || Fecha
 #998#6  = #0#54;         || Concepto
 #998#7  = #0#55;         || Descripcion
 #998#8  = acontab;       || Importe
 |ABRE #40;
 |DDEFECTO #40;
 #40#0 = enEmpresa;
 #40#2 = #41#18;
 |LEE 040#40=;
 |CIERRA #40;
 #998#9  = #41#18;        || Grupo
 |SI enEjer < 2008;
     #998#10 = #40#3;         || Cuenta Gastos
     #998#11 = #40#5;         || Cuenta acum.
 |SINO;
     #998#10 = #40#10;         || Cuenta Gastos
     #998#11 = #40#12;         || Cuenta acum.
 |FINSI;
 #998#12 = #40#9;         || Cuenta Analitica
 #998#13 = eaCodSubdep;

 |GRABA 040#998n;
 |CIERRA #998;
|FPRC;
|| -------------------------------------------------

|PROCESO C_Temporal;
   |SI sw_tmp = 0;
       sw_tmp = 1;
       enGrupo = #998#9;
       nImporte = 0;
   |FINSI;
   |SI #0#56 = "S";
       |SI enGrupo ! #998#9;
           |HAZ Act_Asiento;
           nImporte = 0;
           enGrupo = #998#9;
       |FINSI;
   |FINSI;

   cuenta      = #998#10;
   nImporte    = nImporte + #998#8;
   cuenta1     = #998#11;
   cuatro      = #998#12;
   enGrupo     = #998#9;
   enActividad = #998#1;
   eaCodSubdep = #998#13;

   |SI #0#56 = "N"; |HAZ Act_Asiento; |FINSI;
|FPRC;

|DEFBUCLE ElTemporal2;
   #998#1;
   ;
   enEmpresa, "        ", 00;
   enEmpresa, "zzzzzzzz", 99;
   ;
   NULL, C_Temporal;
|FIN;

|DEFBUCLE ElTemporal1;
   #998#2;
   ;
   enEmpresa, 0000;
   enEmpresa, 9999;
   ;
   NULL, C_Temporal;
|FIN;

|PROCESO Empresas;
    enEmpresa = #999#1;
    enPeriodo = #999#2;
    dig3      = #999#5;
    dig4      = #999#6;
    dig5      = #999#7;
    dig6      = #999#8;
    dig7      = #999#9;
    dig8      = #999#10;
    dig9      = #999#11;

    aAlfa1 = enEmpresa; aAlfa1 = ("00000" + aAlfa1) % -105;
    |ATRI I; |PINTA 2153, aAlfa1; |ATRI 0;

    |SI #0#56 = "S";
        |BUCLE ElTemporal1;
        |SI sw_tmp = 1;
            |HAZ Act_Asiento;
        |FINSI;
    |SINO;
        |BUCLE ElTemporal2;
    |FINSI;
|FPRC;

|DEFBUCLE empreconta;
 #999#1;
 ;
 000000;
 999999;
 e;
 NULL, Empresas;
|FIN;

|PROCESO SiAsiento;

  |ABRE #123;
  |BUCLE empreconta;
  |CIERRA #123;
  |SUB_EJECUTA_NP "dsmoz123"; || Contabilizacion

|FPRC;

||***************************************************************

|PROCESO Act_Asiento; || crea asiento contable por linea
  Documento = Documento + 1;

  |PARA x = 1; |SI x [ 2; |HACIENDO x = x + 1;
        |SI x = 1;
            conter = cuenta; signo = "D";
            aCtaAna = cuatro;
        |SINO;
            conter = cuenta1; signo = "H";
            aCtaAna = cuatro;
        |FINSI;

        |DDEFECTO #dsmow123;
        Lineas = Lineas + 1;
        #dsmow123#0  = enEmpresa;
        #dsmow123#1  = enPeriodo;
        #dsmow123#2  = Lineas;
        #dsmow123#3  = #0#52;       || diario
        #dsmow123#4  = #0#53;       || fecha asiento
        #dsmow123#5  = Documento;   || nro. de Documento
        #dsmow123#6  = conter;      || cuenta
        eaCuenta = #dsmow123#6; |HAZ SacaNivel1;
        #dsmow123#7  = enNivel;     || digito
        #dsmow123#8  = #0#54;       || concepto contable
        #dsmow123#9  = #0#55;       || descripcion
        #dsmow123#10 = signo;       || signo
        #dsmow123#11 = nImporte;    || importe

        aAlfa2 = enActividad; |QBF aAlfa2;
        aAlfa2 = ("00" + aAlfa2) % -102;
        #dsmow123#12 = aAlfa2;    || N§ Actividad = Departamento Ctble.

        #dsmow123#28 = aCtaAna;

        |GRABA 020#dsmow123.n;
   |SIGUE;
   nImporte = 0;
|FPRC;

|PROCESO SacaNivel1;
   aLong = eaCuenta;
   |QBF aLong;
   aLong = aLong % 0;
   nLong = aLong;
   enNivel = 0;
   |SI nLong = dig4; |Y dig4 ! 0; enNivel = 1; |FINSI;
   |SI nLong = dig5; |Y dig5 ! 0; enNivel = 2; |FINSI;
   |SI nLong = dig6; |Y dig6 ! 0; enNivel = 3; |FINSI;
   |SI nLong = dig7; |Y dig7 ! 0; enNivel = 4; |FINSI;
   |SI nLong = dig8; |Y dig8 ! 0; enNivel = 5; |FINSI;
   |SI nLong = dig9; |Y dig9 ! 0; enNivel = 6; |FINSI;
|FPRC;
