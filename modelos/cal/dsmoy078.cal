|FICHEROS;
   dsmoy078 #0;

   dsmom001 #1;
   :/basica/def/agitab12 #112;
   :/basica/def/agicen14 #114;
   :/basica/def/agifigen #118;
   :/basica/def/agim0019 #119;
   :/basica/def/agicen30 #115;

   dsmow148 #900;

   :/contagen/def/canempre #300;
   :/contagen/def/cacuenta #304;
   :/contagen/def/cacuedep #305;

   :/contasoc/def/isom0003 #220;     || Impuesto Sociedades  ] 2008
   reacces@ #910;
   redatos@ #911;
   imagen@  #1114;

   dsmow043 #999;
|FIN;

|VARIABLES;
   nInkey      = "";
   informe     = "";
   nCampo      = 0;
   nDEmpresa   = 0;
   nHEmpresa   = 0;
   fLaFecha    = @;

   aAlfa1 = "";
   aAlfa2 = "";
   aAlfa3 = "";
   nPara1 = 0;
   nPara2 = 0;
   nNume1 = 0;
   nNume2 = 0;
   nNume3 = 0;
   nNume4 = 0;

   nSPin1 = 0;
   &nSwLinea = 0;
   &nSwPrint = 0;
   nSwHay    = 0;
   nPrint    = 0;
   aPathFic  = "";

   aPathContasoc  = "";
   aPathContagen  = "";
   aPathRenta     = "";
   aPathFichRenta = "";
   aRenta         = "";

   aAlfa      = "";
   aBase      = "";
   nCanal     = 0;

   SwSocPas   = 0;
   SwSocie    = 0;
   nSwOpera   = 0;
   nPorCtas   = 0;
   nSwRendi   = 0;
   nSwConta   = 0;
   nSwPlani   = 0;
   nNumAct    = 0;
   nTotAct    = 0;

   nEmpresa   = 0;
   nEjercicio = 0;
   nActividad = 0;
   aActividad = "";
   aDCta12    = "6000000000000";
   aHCta12    = "7999999999999";
   aDCta4C    = "6000";
   aHCta4C    = "6999";
   aDCta4V    = "7000";
   aHCta4V    = "7999";
   nCampoAct  = 0;

   aCuenta    = "";
   nSaldo     = 0;

   &eSwSelec  = 0;
   &eaNomFDep = "";  ||Nombre del Fichero Creado
   &dirfich0  = "";
   &eaDDepar  = "";
   &eaHDepar  = "";

   aFichero   = "",
   aDef       = "";
   aDef910    = "";
   aDef114    = "";
   aFicRen    = "";
   nSwFich    = 0; || tipo de fichero de renta, para saber como leerlo
   nCodRenta  = 0;
   aTipRenta  = "";
   &nTipoTrib = 0;
   &aTipoTrib = "";
   nIngresos  = 0;
   nGastos    = 0;
   nRendim    = 0;
   aTitular   = "";
   nSw        = 0;
   nCliente   = 0;
   aCasilla   = "";

   &enEjerR   = 0;
   &enCodRen  = 0;
   &eaCodRen  = "";
|FIN;

|INCLUYE i_variar;
|INCLUYE i_varcon;
|INCLUYE i_varlib;
|INCLUYE i_varren;

|| //////////////////// Calculos de Pantalla \\\\\\\\\\\\\\\\\\\\\\\\\\
|CALCULO Eano; |TIPO 7;
   fLaFecha = ~;
   aAlfa1 = fLaFecha; aAlfa1 = aAlfa1 % -104;
|FCAL;

|CALCULO AntesOrdenar; |TIPO 7;
  |SI #0#2 = 0;
      |C_M #0Campo, 1, "N";
      #0Campo = 1; |PINTA #0Campo;
  |SINO;
      |C_M #0Campo, 1, "S";
  |FINSI;
|FCAL;

|CALCULO Tipo7;  |TIPO 7;
     |SI enEmpresa ! 0;
         #0#2  = enEmpresa;  |PINTA #0#2;
         #0#3  = enEmpresa;  |PINTA #0#3;

         |C_M #0#2, 1, "N";
         |C_M #0#3, 1, "N";
     |FINSI;
|FCAL;

|CALCULO Parrilla; |TIPO 0;
     |SI #0#2 = 0;
         #0#2 = 0;  |PINTA #0#2;
         #0#3 = 0;  |PINTA #0#3;  |C_M #0#3, 1, "N";
         |PARA nCampo = 4;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |SINO;
         |SI enEmpresa = 0;
             |C_M #0#2, 1, "S";
             |C_M #0#3, 1, "S";
         |FINSI;

         |PARA nCampo = 4;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "N";
               #0nCampo = 0;  |PINTA #0nCampo;
         |SIGUE;
     |FINSI;
|FCAL;

|CALCULO NoMasEmpresas; |TIPO 0;
     |SI #0Campo = 0;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
               #0nCampo = 0;  |C_M #0nCampo, 1, "N";
         |SIGUE;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |FINSI;
|FCAL;

|CALCULO YaSeCual; |TIPO 7;
     |SI enEmpresa ! 0;
         #0#41 = "S"; |PINTA #0#41;
         #0#24 = "S"; |PINTA #0#24;
         #0#25 = 00;  |PINTA #0#25;
         #0#26 = 99;  |PINTA #0#26;
         |C_M #0#24, 1, "N";
         |C_M #0#25, 1, "N";
         |C_M #0#26, 1, "N";
         |C_M #0#41, 1, "N";
     |FINSI;
|FCAL;

|CALCULO SelTipo0; |TIPO 0;
 |SI #0#41 = "N";
     #0#25 = 0;  |PINTA #0#25;  |C_M #0#25, 1, "N";
     #0#26 = 99; |PINTA #0#26;  |C_M #0#26, 1, "N";
     |PARA nCampo = 27;  |SI nCampo [ 36;  |HACIENDO nCampo = nCampo + 1;
           |C_M #0nCampo, 1, "S";
     |SIGUE;
 |SINO;
     |SI enEmpresa = 0;
         |C_M #0#25, 1, "S";
         |C_M #0#26, 1, "S";
     |FINSI;
     |PARA nCampo = 27;  |SI nCampo [ 36;  |HACIENDO nCampo = nCampo + 1;
           |C_M #0nCampo, 1, "N";
           #0nCampo = 0;  |PINTA #0nCampo;
     |SIGUE;
 |FINSI;
|FCAL;

|CALCULO SelTipo1;
 |SI #0Campo = 0;
     |PARA nCampo = Campo + 1;  |SI nCampo [ 36;  |HACIENDO nCampo = nCampo + 1;
           #0nCampo = 0;  |C_M #0nCampo, 1, "N";
     |SIGUE;
 |SINO;
     |PARA nCampo = Campo + 1;  |SI nCampo [ 36;  |HACIENDO nCampo = nCampo + 1;
           |C_M #0nCampo, 1, "S";
     |SIGUE;
 |FINSI;
|FCAL;

|PROCESO ClaveAlta;
 #112#0 = 01;
|FPRC;

|PROCESO ClaveBaja;
 #112#0 = 99;
|FPRC;

|CALCULO Tribu;
 |SI #0#42 = "N";
     #0#43 = 0;  |PINTA #0#43;  |C_M #0#43, 1, "N";
     #0#44 = 99; |PINTA #0#44;  |C_M #0#44, 1, "N";
     |PARA nCampo = 45;  |SI nCampo [ 56;  |HACIENDO nCampo = nCampo + 1;
           |C_M #0nCampo, 1, "S";
     |SIGUE;
 |SINO;
     |C_M #0#43, 1, "S";
     |C_M #0#44, 1, "S";
     |PARA nCampo = 45;  |SI nCampo [ 56;  |HACIENDO nCampo = nCampo + 1;
           |C_M #0nCampo, 1, "N";
           #0nCampo = 0;  |PINTA #0nCampo;
     |SIGUE;
 |FINSI;
|FCAL;

|CALCULO MiraTributacion;
     nInkey = FSalida;
     |C_M #0Campo, 0, aAlfa1;
     |SI aAlfa1 = "N"; |ACABA; |FINSI;

     |SI nInkey = 10;
         |ABRE #112;
         #112#0 = #0Campo;
         |CONSULTA_F_CLAVE #1#112, ClaveAlta, ClaveBaja;
         #0Campo = #112#0; |PINTA #0Campo;
         |CIERRA #112;
     |FINSI;

     |SI Campo > 44;
         |SI #0Campo = 0;
             |PARA nCampo = Campo + 1;  |SI nCampo [ 56;  |HACIENDO nCampo = nCampo + 1;
                   #0nCampo = 0;  |C_M #0nCampo, 1, "N"; |PINTA #0nCampo;
             |SIGUE;
         |SINO;
             |PARA nCampo = Campo + 1;  |SI nCampo [ 56;  |HACIENDO nCampo = nCampo + 1;
                   |C_M #0nCampo, 1, "S";
             |SIGUE;
         |FINSI;
     |FINSI;

     |SI #0Campo ! 0; |Y Campo > 44;
         |ABRE #112;
         #112#0 = #0Campo;
         |LEE 001#112=;
         |SI FSalida ! 0;
             |MENAV "    No existe Tipo Tributacion";
             |ERROR;
         |FINSI;
         |CIERRA #112;
     |FINSI;
|FCAL;

|| *************************************************************************
|| *************************************************************************
|PROCESO PreparaConta;

   nSwConta  = 0; ||compruebo si tiene contabilidad

   |ABRE #canempre;
   |SELECT #4#canempre;
   |DDEFECTO #canempre;
   #canempre#2  = nEmpresa;
   #canempre#26 = nEjercicio;
   |LEE 001#canempre.=;
   |SI FSalida = 0;
       enPerConta = #canempre#3;   || Periodo Contable
       aAlfa1 = nEmpresa;   aAlfa1 = ("00000" + aAlfa1) % -105;
       aAlfa2 = enPerConta; aAlfa2 = ("0" + aAlfa2) % -101;
       aPathConta  = aPathContagen + "fich/" + aAlfa1 + aAlfa2 + "/";
       nSwConta = 1;
   |FINSI;
   |SELECT #1#canempre;
   |CIERRA #canempre;

   |SI nSwConta = 0; |ACABA; |FINSI;    || no tiene contabilidad
   enSwPartido = 0;
   enEmpresa  = nEmpresa;
   eaLaMonEmp = #canempre#28;
   dig1       = #canempre#11;   || Desde Mes
   dig3       = #canempre#13;   || N§ Digitos
   dig4       = #canempre#14;   || Digitos Nivel 1
   dig5       = #canempre#15;   || Digitos Nivel 2
   dig6       = #canempre#16;   || Digitos Nivel 3
   dig7       = #canempre#17;   || Digitos Nivel 4
   dig8       = #canempre#18;   || Digitos Nivel 5
   dig9       = #canempre#19;   || Digitos Nivel 6
   eaEstadoEmp = "S"; |SI #canempre#21 = "I"; eaEstadoEmp = "N"; |FINSI;
   eaNombreEmp = #canempre#1; || Nombre Empresa

   || Recoger informacion sobre los niveles ...
   |SI dig3 = 1; MaximoDigitos = dig4; |FINSI;
   |SI dig3 = 2; MaximoDigitos = dig5; |FINSI;
   |SI dig3 = 3; MaximoDigitos = dig6; |FINSI;
   |SI dig3 = 4; MaximoDigitos = dig7; |FINSI;
   |SI dig3 = 5; MaximoDigitos = dig8; |FINSI;
   |SI dig3 = 6; MaximoDigitos = dig9; |FINSI;

   |SI dig1 ! 1;
        enSwOpPar   = 0;
        |HAZ SituaEmpresa;
   |FINSI;

   |PATH_DAT #304 aPathConta;

   nPorCtas = 0;
   |SI nNumAct = 1; nPorCtas = 1; |FINSI;  ||solo una actividad

   |SI nPorCtas = 0;             || ver por actividades
       dirfich0 = aPathConta;
       eSwSelec  = 2;
       |ATRI I;
       |PINTA 2302, "Procesando Cuentas por Departamentos ... Espere ";
       |ATRI 0;
       |SUB_EJECUTA_NP ":contagen/cacuedep";
       |PINTA 2302, "                                                ";
       |NOME_DAT #305 eaNomFDep;
       |PATH_DAT #305 aPathConta;
   |FINSI;
|FPRC;

|| .... Leo el Impuesto de Sociedades
|PROCESO LeeCas103;
     #220#0  = enEmpresa;
     #220#1  = enEjer;
     #220#3  = aCasilla;
     |LEE 040#220=;
     |SI FSalida ! 0;
         |DDEFECTO #220;
     |SINO;
         SwSocie  = 1;
     |FINSI;

     |SI #220#6 ! "S"; #220#5 = 0; #220#7 = " "; |FINSI;
|FPRC;

|PROCESO Leoisom0003;

    |ABRE #220;
    |SELECT #2#220;

 || ... Ingresos
    aCasilla = "255"; |HAZ LeeCas103; #0#58 = #0#58 + #220#5;
    aCasilla = "258"; |HAZ LeeCas103; #0#58 = #0#58 + #220#5;
    aCasilla = "259"; |HAZ LeeCas103; #0#58 = #0#58 + #220#5;
    aCasilla = "265"; |HAZ LeeCas103; #0#58 = #0#58 + #220#5;
    aCasilla = "285"; |HAZ LeeCas103; #0#58 = #0#58 + #220#5;
    aCasilla = "286"; |HAZ LeeCas103; #0#58 = #0#58 + #220#5;
    aCasilla = "295"; |HAZ LeeCas103; |SI #220#5 > 0;  #0#58 = #0#58 + #220#5; |FINSI;
    aCasilla = "297"; |HAZ LeeCas103; #0#58 = #0#58 + #220#5;
    aCasilla = "309"; |HAZ LeeCas103; |SI #220#5 > 0;  #0#58 = #0#58 + #220#5; |FINSI;
    aCasilla = "312"; |HAZ LeeCas103; |SI #220#5 > 0;  #0#58 = #0#58 + #220#5; |FINSI;
    aCasilla = "329"; |HAZ LeeCas103; #0#58 = #0#58 + #220#5;

 || ... Gastos
    aCasilla = "260"; |HAZ LeeCas103; #0#59 = #0#59 - #220#5;
    aCasilla = "270"; |HAZ LeeCas103; #0#59 = #0#59 - #220#5;
    aCasilla = "279"; |HAZ LeeCas103; #0#59 = #0#59 - #220#5;
    aCasilla = "284"; |HAZ LeeCas103; #0#59 = #0#59 - #220#5;
    aCasilla = "287"; |HAZ LeeCas103; #0#59 = #0#59 - #220#5;
    aCasilla = "294"; |HAZ LeeCas103; #0#59 = #0#59 - #220#5;
    aCasilla = "295"; |HAZ LeeCas103; |SI #220#5 < 0;  #0#59 = #0#59 - #220#5; |FINSI;
    aCasilla = "305"; |HAZ LeeCas103; #0#59 = #0#59 - #220#5;
    aCasilla = "309"; |HAZ LeeCas103; |SI #220#5 < 0;  #0#59 = #0#59 - #220#5; |FINSI;
    aCasilla = "312"; |HAZ LeeCas103; |SI #220#5 < 0;  #0#59 = #0#59 - #220#5; |FINSI;
    aCasilla = "313"; |HAZ LeeCas103; #0#59 = #0#59 - #220#5;
    aCasilla = "326"; |HAZ LeeCas103; #0#59 = #0#59 - #220#5;

    |SELECT #1#220;
    |CIERRA #220;
|FPRC;

|PROCESO LeoImpuesto;
  || Lee Ficha Sociedades, lee la ficha
     swExiste = 0;
     nRegis   = 0;
     |ABRE #agim0019;
     #agim0019#0 = nEmpresa;
     #agim0019#1 = 99;
     |LEE 041#agim0019.];
     |SI FSalida = 0;
         |LEE 041#agim0019.a;
     |SINO;
         |LEE 041#agim0019.u;
     |FINSI;
     |SI FSalida = 0; |Y #agim0019#0 = nEmpresa;
         swExiste = 1;
     |FINSI;
     |SI swExiste = 1;
         nRegis = #agim0019#1;  ||Registre
         |SI #agim0019#12 > "01.01.1000";
             aAlfa1 = #agim0019#12; aAlfa1 = aAlfa1 % -104; enEjerBajaM = aAlfa1;
             |SI enEjerBajaM [ #0#1; nRegis = 0; |FINSI;
         |FINSI;
     |FINSI;
     |CIERRA #agim0019;

     |SI nRegis ! 0; |Y aPathContasoc ! "";
         aPathFic = aPathContasoc + "fich/";
         |PATH_DAT #220 aPathFic;

         |HAZ Leoisom0003;
     |FINSI;
|FPRC;

|| *************************************************************************
|| .... Por Renta

|PROCESO Leo_redatos;
   |SI nSwFich = 0; |O nSwFich = 3;
       |SI #911#18 = "O";
           |SI #911#19 = "    "; #911#19 = "9999"; |FINSI;
       |FINSI;
       |SI #911nCampoAct = #900#3;
           nIngresos = nIngresos + #911#29;
           |SI nSwFich = 0;
               nGastos   = nGastos   + #911#44;
           |SINO;
               nGastos   = nGastos   + #911#42;
               |SI #0#76 = "S";
                    nGastos = nGastos + #911#44;
               |FINSI;
           |FINSI;
           nRendim   = nIngresos - nGastos;
       |FINSI;
   |FINSI;

   |SI nSwFich = 1;
       |SI #911#16 = "O";
           |SI #911#17 = "    "; #911#17 = "9999"; |FINSI;
       |FINSI;
       |SI #911nCampoAct = #900#3;
           |SI nTipoTrib = 5;
               nRendim = nRendim + #911#117;
           |SINO;
               nRendim = nRendim + #911#98;
           |FINSI;
       |FINSI;
   |FINSI;

   |SI nSwFich = 2;
       |SI #911#18 = "O";
           |SI #911#19 = "    "; #911#19 = "9999"; |FINSI;
       |FINSI;
       |SI #911nCampoAct = #900#3;
           nIngresos = nIngresos + #911#25;
           |SI nTipoTrib = 6; |O nTipoTrib = 7;
               nRendim   = nRendim   + #911#52;
           |SINO;
               nRendim   = nRendim   + #911#30;
           |FINSI;
           nGastos   = nIngresos - nRendim;
      |FINSI;
   |FINSI;

   |SI nSwFich = 4;
       nRendim   = nRendim   + #911#22;
   |FINSI;

   |SI nSwFich = 5;
       nRendim   = nRendim   + #911#17;
   |FINSI;
|FPRC;

|DEFBUCLE Leo_redatos;
  #911#1002;
  2;
  #910#0, 01, aTitular;
  #910#0, 99, aTitular;
  e;
  NULL, Leo_redatos;
|FIN;

|DEFBUCLE Leo_redatos11;
  #911#1004;
  ;
  #910#0, 00, aTitular, 001;
  #910#0, 99, aTitular, 999;
  e;
  NULL, Leo_redatos;
|FIN;

|PROCESO ActivRenta;

   aDef = aPathRenta + "def/" + aFicRen + ".mas";
   |CARGA_DEF aDef, redatos@;
   |SI FSalida ! 0;
       aAlfa = "     Error en cargar el Def " + eaFicRen;
       |MENAV aAlfa;
       |DESCARGA_DEF #redatos@;
       aPathRenta = "";
       |ACABA;
   |FINSI;

   |PATH_DAT #911 aPathFichRenta;

   |SI nSwFich ! 5;
      |BUCLE Leo_redatos;
   |SINO;
      |BUCLE Leo_redatos11;
   |FINSI;

   |DESCARGA_DEF #redatos@;

|FPRC;

|PROCESO PorRenta_Lee;

   #910#0 = nCodRenta;
   |LEE 041#910=;
   |SI FSalida ! 0;
       |ACABA;
   |FINSI;

   |SI aTipRenta = "T"; aTitular = "TITULAR   "; |FINSI;
   |SI aTipRenta = "C"; aTitular = "CONYUGE   "; |FINSI;
   |SI aTipRenta = "1"; aTitular = "1 FAMILIAR"; |FINSI;
   |SI aTipRenta = "2"; aTitular = "2 FAMILIAR"; |FINSI;
   |SI aTipRenta = "3"; aTitular = "3 FAMILIAR"; |FINSI;
   |SI aTipRenta = "4"; aTitular = "4 FAMILIAR"; |FINSI;

   nCampoAct = -1;
   nSw = 0;
   nSwFich = 0;
   |SI nTipoTrib = 3; |O nTipoTrib = 13; ||NORMAL
       nCampoAct = 60;
       aFicRen = "rem00025"; |HAZ ActivRenta;
       nSw = 1;
   |FINSI;

   nSwFich = 3;
   |SI nTipoTrib = 4; |O nTipoTrib = 14; ||Simplificada
       nCampoAct = 62;
       aFicRen = "rem00026"; |HAZ ActivRenta;
       nSw = 1;
   |FINSI;

   nSwFich = 1;
   |SI nTipoTrib = 5; |O nTipoTrib = 15; || Modulos
       nCampoAct = 127;
       aFicRen = "rem00027"; |HAZ ActivRenta;
       nSw = 1;
   |FINSI;

   nSwFich = 2;
   |SI nTipoTrib = 6; |O nTipoTrib = 16; || Modulos Agricolas
      |O nTipoTrib = 7; |O nTipoTrib = 17; || Agricolas ???
       nCampoAct = 70;
       aFicRen = "rem00028"; |HAZ ActivRenta;
       nSw = 1;
   |FINSI;

   nSwFich = 4;
   |SI nTipoTrib = 8; |O nTipoTrib = 9; || Atribucion a Renta
       |O nTipoTrib = 10; |O nTipoTrib = 11;
          |O nTipoTrib = 12; |O nTipoTrib = 18;

       aFicRen = "rem00058"; |HAZ ActivRenta;  ||atribucion rentas
       nSw = 1;
   |FINSI;

   nSwFich = 5;
   |SI nTipoTrib = 1;
       aFicRen = "rem01011"; |HAZ ActivRenta;  ||atribucion rentas
       nSw = 1;
   |FINSI;
|FPRC;

|PROCESO dsmom001;   ||leo comuneros
   |SI #1#8 > "01.01.0000";
       |SI #1#8 > efHFecha;  |ACABA;  |FINSI;
   |FINSI;
   |SI #1#9 > "01.01.0000";
       |SI #1#9 < efDFecha;  |ACABA; |FINSI; || Baja
   |FINSI;

   |DDEFECTO #1114;
   #1114#0 = #1#3;
   #1114#1 = #1#4;
   |LEE 041#1114=;

   |SI #1114#18 ! 0;
       nTipoTrib = #1114#0;
       nCodRenta = #1114#18;
       aTipRenta = #1114#19;
       |HAZ PorRenta_Lee;
   |FINSI;
|FPRC;

|DEFBUCLE dsmom001;
   #1#1;
   ;
   nEmpresa, nActividad, 0001;
   nEmpresa, nActividad, 9999;
   e;
   NULL, dsmom001;
|FIN;

|PROCESO PorRenta;
   |SI aPathRenta = "";  |ACABA; |FINSI;

   |SI #900#14 = 0; |Y #900#24 = "N";
       || ... ... ... ... ... ... No tiene Cod.Renta y no es una Comunidad
       |ACABA;
   |FINSI;

   |ABRE #910;
   nIngresos = 0;
   nGastos   = 0;
   nRendim   = 0;

   |SI #900#14 ! 0;
       nTipoTrib = #900#0;
       nCodRenta = #900#14;
       aTipRenta = #900#23;
       |HAZ PorRenta_Lee;
   |SINO;
       |ABRE #1114;
       |BUCLE dsmom001;
       |CIERRA #1114;
   |FINSI;
   |CIERRA #910;

   #900#19 = nIngresos;
   #900#20 = nGastos;
   #900#21 = nRendim;
|FPRC;

|| *************************************************************************
|PROCESO SumoCtas;
   |SI aCuenta ] aDCta4C; |Y aCuenta [ aHCta4C;
       #900#17 = #900#17 + nSaldo;
   |FINSI;
   |SI aCuenta ] aDCta4V; |Y aCuenta [ aHCta4V;
       #900#16 = #900#16 + nSaldo;
   |FINSI;
   #900#18 = #900#16 - #900#17;
|FPRC;

|PROCESO PorPlanCtble;
   aCuenta = #304#0 % 104;
   nSaldo  = #304#53 - #304#50;
   aAlfa1  = aCuenta % 101;
   |SI aAlfa1 = "7";  nSaldo = -nSaldo; |FINSI;
   |HAZ SumoCtas;
|FPRC;

|DEFBUCLE PorPlanCtble;
   #304#1;
   3;
   aDCta12, "S";
   aHCta12, "S";
   ;
   NULL, PorPlanCtble;
|FIN;

|PROCESO LeeCuentas;
   aCuenta = #305#0 % 104;
   nSaldo  = #305#53 - #305#50;
   aAlfa1 = aCuenta % 101;
   |SI aAlfa1 = "7";  nSaldo = -nSaldo; |FINSI;
   |HAZ SumoCtas;
|FPRC;

|DEFBUCLE LeeCuentas;
   #305#2;
   3;
   aActividad, aDCta12, "S";
   aActividad, aHCta12, "S";
   ;
   NULL, LeeCuentas;
|FIN;

|PROCESO PorActividad;

  |SI #114#5 > "01.01.0000";
      |SI #114#5 > efHFecha;  |ACABA;  |FINSI;
  |FINSI;
  |SI #114#6 > "01.01.0000";
      |SI #114#6 < efDFecha;  |ACABA; |FINSI; || Baja
  |FINSI;

  nActividad = #114#1;
  aActividad = nActividad;
  aActividad = ("00" + aActividad) % -102;

  nSwPrint = 0;
  |SI #0#42 = "N";
      |PARA nCampo = 45; |SI nCampo [ 56; |HACIENDO nCampo = nCampo + 1;
            |SI #0nCampo ! 0; |Y #114#9 = #0nCampo;
                 nSwPrint = 1;
                 |SAL;
            |FINSI;
      |SIGUE;
  |SINO;
      |SI #0#43 [ #114#9; |Y #0#44 ] #114#9;
          nSwPrint = 1;
      |FINSI;
  |FINSI;

  nNumAct = nNumAct + 1;
  |SI nSwPrint = 0; |ACABA; |FINSI;  ||Esta actividad no esta en la seleccion

  nTotAct = nTotAct + 1;
  |SI nSwOpera = 0;
      nSwHay  = 1;
      |ACABA;
  |FINSI;

|| ------------------------------------------------------
  #900#0  = #114#9;   || Tributacion
  #900#1  = #114#10;
  #900#3  = aActividad; || #114#1;   || Actividad
  #900#6  = #114#4;
  #900#9  = #114#5;   || Fecha Alta
  #900#10 = #114#6;   || Fecha Baja
  #900#12 = #114#2;   || Tipo Epig.
  #900#13 = #114#3;   || Epigrafe

  enCodRen = #114#18;
  eaCodRen = #114#19;
  enEjerR = enEjer;
  |HAZ CodigoRen;
  #900#14 = enCodRen;  || Codigo Renta
  #900#23 = eaCodRen;  || Tipo Renta

  #900#15 = #114#20;  || CNAE
  #900#24 = #114#13;  || Comunidad de Bienes
  #900#25 = #114#14;  || Comunero

  |PDEFECTO #900,16,23;

  |SI nSwConta ! 0;
      |SI nPorCtas = 0;
          |BUCLE LeeCuentas;
      |SINO;
          |BUCLE PorPlanCtble;
      |FINSI;
  |FINSI;

  |SI #900#0 ! 1;   || si es ! sin tributacion,  no hay datos fiscales
      |SI SwSocie = 1;
          |SI SwSocPas = 0;
              #900#19 = #0#58;
              #900#20 = #0#59;
              #900#21 = #900#19 - #900#20;
              SwSocPas = 1;
          |FINSI;
      |SINO;
          |HAZ PorRenta;
      |FINSI;
  |SINO;
      |SI #900#12 = "E";   || tributacion 1 (exenta)
          |SI #900#13 = "8611";  |O  #900#13 = "8612"; |O  #900#13 = "862 ";
              |HAZ PorRenta;
          |FINSI;
      |FINSI;
  |FINSI;

  #900#22 = #900#18 - #900#21;

  |GRABA 020#900n;
  |LIBERA #900;
  nPrint = 1;
|FPRC;

|DEFBUCLE PorActividad;
  #114#1;
  ;
  nEmpresa, 00;
  nEmpresa, 99;
  e;
  NULL, PorActividad;
|FIN;


|PROCESO LEmpresa;

 nEmpresa = #agifigen#0;

 |DDEFECTO #900;
 #900#2 = nEmpresa;
 #900#4 = #agifigen#13;
 #900#5 = #agifigen#1;
 #900#11 = "ACTIVA";
 |SI #agifigen#94 > "01.01.0000";
     #900#11 = "INACTIVA";
     |SI #0#24 = "N"; |ACABA; |FINSI;
 |FINSI;

|| ... Seleccion de Tipo de Empresa
  nSPin1 = #agifigen#87;
  enSwSelEmp = 1;
  |SI #0#41 = "S";
      |SI nSPin1 ] #0#25; |Y nSPin1 [ #0#26;
          enSwSelEmp = 0;
      |FINSI;
  |SINO;
      |SI nSPin1 = #0#27;
          enSwSelEmp = 0;
      |SINO;
          |PARA nCampo = 28;  |SI nCampo [ 36;  |HACIENDO nCampo = nCampo + 1;
                |SI nSPin1 = #0nCampo; |Y nSPin1 ! 0;
                    enSwSelEmp = 0;
                    |SAL;
                |FINSI;
          |SIGUE;
      |FINSI;
  |FINSI;
  |SI enSwSelEmp = 1; |ACABA; |FINSI;
|| ------------------------------------------------------------------------
   #900#7 = #agifigen#87;
   #900#8 = #agifigen#88;
|| ------------------------------------------------------------------------

   #0#58 = 0;
   #0#59 = 0;

   SwSocPas  = 0;
   SwSocie   = 0;
   nSwConta  = 0;
   nNumAct   = 0;
   nTotAct   = 0;
   nSwHay    = 0;
   nSwOpera  = 0;            ||Comprobar las actidades de la Empresa

   |BUCLE PorActividad;
   |SI nSwHay = 1;
       enSwPartido = 0;
       |HAZ PreparaConta;   ||Fichero departamentos
||       |SI nSwConta = 0; |ACABA; |FINSI; || aunque no tenga contabilidad

       SwSocie = 0;
       |HAZ LeoImpuesto;

       nSwOpera = 1;
       |BUCLE PorActividad;

       |SI enSwPartido = 1;
           enSwOpPar   = 1;
           |HAZ FinalPartido;
       |FINSI;

   |FINSI;
|FPRC;

|DEFBUCLE agifigen;
    #agifigen#1;
    ;
    nDEmpresa;
    nHEmpresa;
    e;
    NULL, LEmpresa;
|FIN;

|| \\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\
|PROCESO LeeAuxW148;

    |SI nPrint = 0;
        nPrint = 1;
        nTipoTrib = #900#0;
        aTipoTrib = #900#1;
        nSwLinea  = 0;
        nCliente  = -1;
        |PDEFECTO #0,58,76;
    |FINSI;

    |SI nTipoTrib ! #900#0;
        |SI nSwLinea ! 0;
            |SI nTipoTrib = 2;
                |SI nSwLinea = 2;
                    |PRINT;
                    nSwLinea = 3;
                |FINSI;
                |PRINT;
            |FINSI;
            |PIEINF;
        |FINSI;
        nTipoTrib = #900#0;
        aTipoTrib = #900#1;
        nCliente  = -1;
        nSwLinea  = 0;
        |PDEFECTO #0,58,76;
    |FINSI;

    |SI #0#57 = "S"; |O #900#22 ! 0;

        |SI #900#0 ! 2;

            |SI nCliente = #900#2;
                nSwLinea = 2;
            |FINSI;
            |PRINT;
            nSwLinea = 1;
            nCliente = #900#2;

        |SINO;

           |SI nCliente ! -1;
               |SI nCliente ! #900#2;
                   |SI nSwLinea = 2;
                       |PRINT;
                       nSwLinea = 3; |PRINT;
                   |SINO;
                       nSwLinea = 1; |PRINT;
                   |FINSI;
                   nSwLinea  = 1;
                   |PDEFECTO #0,58,76;
               |SINO;
                   nNume1 = #0#58;
                   nNume2 = #0#59;
                   nNume3 = #0#60;
                   |SI nSwLinea ! 2;
                       #0#58 = 0;
                       #0#59 = 0;
                       #0#60 = 0;
                       #0#73 = 0;
                   |FINSI;
                   |PRINT;
                   nSwLinea = 2;
                   #0#58 = nNume1;
                   #0#59 = nNume2;
                   #0#60 = nNume3;
               |FINSI;
           |FINSI;

           nCliente = #900#2;
           #0#61 = #900#2;
           #0#62 = #900#3;
           #0#63 = #900#4;
           #0#64 = #900#5;
           #0#65 = #900#6;
           #0#66 = #900#14;
           #0#74 = #900#12;
           #0#75 = #900#13;

           #0#67 = #900#16;
           #0#68 = #900#17;
           #0#69 = #900#18;
           #0#70 = #0#70 + #900#16;
           #0#71 = #0#71 + #900#17;
           #0#72 = #0#72 + #900#18;

           #0#58 = #0#58 + #900#19;
           #0#59 = #0#59 + #900#20;
           #0#60 = #0#60 + #900#21;

           #0#73 = #0#72 - #0#60;
        |FINSI;
    |FINSI;
|FPRC;

|DEFBUCLE LeeAuxW148;
    #900#1;
    ;
    00, 00001, "  ";
    99, 99999, "zz";
    e;
    NULL, LeeAuxW148;
|FIN;

|| \\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\
|PROCESO BuscaAplicacion;
     aRenta = #0#1;
     aPathContagen  = "";
     aPathContasoc  = "";
     aPathRenta     = "";
     aPathFichRenta = "";

     |DBASS aBase;   |QBF aBase;

     aAlfa = aBase + "contagen/def/canempre.mas";
     |XABRE "E", aAlfa, nCanal;
     |SI FSalida ] 0;
         aPathContagen = aBase + "contagen/";
     |SINO;
         |ACABA;   ||si no hay contabilidad, sale.
     |FINSI;
     |XCIERRA nCanal;

     aAlfa = aBase + "contasoc/def/maempr10.mas";
     |XABRE "E", aAlfa, nCanal;
     |SI FSalida ] 0;
         aPathContasoc = aBase + "contasoc/";
     |FINSI;
     |XCIERRA nCanal;

     aAlfa = aBase + "ren" + aRenta + "/def/rem00100.mas";
     |XABRE "E", aAlfa, nCanal;
     |SI FSalida ] 0;
         aPathRenta     = aBase + "ren" + aRenta + "/";
         aPathFichRenta = aBase + "ren" + aRenta + "/fich/";

        aDef910 = aPathRenta + "def/reacceso.mas";
        |CARGA_DEF aDef910, reacces@;
        |SI FSalida ! 0;
            aAlfa = "     Error en cargar el Def reacceso.mas";
            |MENAV aAlfa;
            aPathRenta     = "";
            aPathFichRenta = "";
            aDef910        = "";
        |FINSI;
     |FINSI;
     |XCIERRA nCanal;
|FPRC;

|CALCULO Tipo3;  |TIPO 3;
    |HAZ BuscaAplicacion;

    |SI aPathContagen = "";
        |MENAV "    No esta instalada la Contabilidad.";
        |HAZ descarga;
        |ACABA;
    |FINSI;

    aDef114 = aBase + "basica/def/agicen14.mas";
    |CARGA_DEF aDef114, imagen@;
    |SI FSalida ! 0;
        |MENAV "     Error en cargar el Def agicen14.mas";
        aDef114 = "";
        |HAZ descarga;
        |ACABA;
    |FINSI;

    eaInac = #0#24;
    enEjer = #0#1;
    aAlfa2 = enEjer;
    aAlfa1 = aAlfa2 % -102; nEjercicio = aAlfa1;
    aAlfa1 = "01.01." + aAlfa2; efDFecha = aAlfa1;
    aAlfa1 = "31.12." + aAlfa2; efHFecha = aAlfa1;
    eaTituloL = #0#40; ||Titulo del Libro

    eaDDepar  = "  ";
    eaHDepar  = "zz";
    |MENAV "0000 Recuerde seleccionar el papel de la impresora en Horizontal";

    |IMPRE 0;
    |SI FSalida ! 0;
        |HAZ descarga;
        |ACABA;
    |FINSI;

    informe = "dsmoy078";

    |INFOR informe;
    |SI FSalida ! 0;
        |HAZ descarga;
        |ACABA;
    |FINSI;

    aFichero = "1y" + Usuario;
    |NOME_DAT #900 aFichero;
    |DELINDEX #900;

    |ABRE #900;
    nPrint = 0;
    |SI #0#2 ! 0;
        nDEmpresa = #0#2;
        nHEmpresa = #0#3;
        |BUCLE agifigen;
    |SINO;
        |PARA nPara1 = 4;  |SI nPara1 [ 23;  |HACIENDO nPara1 = nPara1 + 1;
              |SI #0nPara1 ! 0;
                  nDEmpresa = #0nPara1;
                  nHEmpresa = #0nPara1;
                  |BUCLE agifigen;
              |FINSI;
        |SIGUE;
    |FINSI;
    |CIERRA #900;

    |SI nPrint ! 0;
        nPrint = 0;
        |BUCLE LeeAuxW148;
    |FINSI;
    |SI nPrint = 1;
        |SI nSwLinea ! 0;
            |SI nTipoTrib = 2;
                |SI nSwLinea = 2;
                    |PRINT;
                    nSwLinea = 3;
                |FINSI;
                |PRINT;
            |FINSI;
            |PIEINF;
        |FINSI;
    |FINSI;

    |FININF;
    |FINIMP;

    |HAZ descarga;
|FCAL;

|PROCESO descarga;
    |SI aDef114 ! ""; |DESCARGA_DEF #1114; aDef114 = ""; |FINSI;
    |SI aDef910 ! ""; |DESCARGA_DEF #910;  aDef910 = ""; |FINSI;
|FPRC;

|PROGRAMA;

 |CLS;
 |CABEZA "Inf.Gral Rendimientos";

 |ABRE #0;
 |LEE 001#0p;
 |SI FSalida ! 0;
     |DDEFECTO #0;
     fLaFecha = ~;
     aAlfa1 = fLaFecha; aAlfa1 = aAlfa1 % -104;
     #0#1 = aAlfa1;
     |GRABA 020#0c;
 |FINSI;

 |PINPA #0#0;
 |PINDA #0#0;

 |SI enEmpresa ! 0;
     #0#2 = enEmpresa;
     #0#3 = enEmpresa;
     #0#1 = enEjer;
     |HAZ Tipo3;
 |SINO;
     |ENDATOS #1#0;
 |FINSI;

 |PDEFECTO #0,58,76;
 |GRABA 020#0a;
 |CIERRA #0;
|FPRO;
