|FICHEROS;
     dsmoz014 #0;

     dsmom004 #4;
     dsmom028 #28;
     :/basica/def/agifigen #118;

     imagen@  #998;
     dsmow076 #999,MANTE;
|FIN;

|VARIABLES;

     &enImpAcum = 0;
     &enImporte = 0;
     &enImport1 = 0;
     &enTrim    = 0;
     &enAny     = 0;
     nEstPres   = 0;

     aFichero   = "";
     aAlfa      = "";
     aAlfa1     = "";
     fFecha     = @;

     nCampo     = 0;
     nDEmpresa  = 0;
     nHEmpresa  = 0;
     enGraba    = 0;
     nInkey     = 0;
     aMas       = "";
     informe    = "dsmoz014";
     &eaCam1    = "";
     &eaCam2    = "";
     &enCam3    = 0;
     &enCam4    = 0;
     &eaCam5    = "";
     &enCam6    = 0;
     &enCam7    = 0;
     &eaCam8    = "";
     &enCam9    = 0;
     nDModelo   = 0;
     nHModelo   = 0;
|FIN;

|INCLUYE i_variar;

|| /////////////////////// PROCESOS ENTRADA POR PANTALLA \\\\\\\\\\\\\\\\\\\\

|PROCESO Periodo;
     #0#1 = "          ";
     |SI #0#0 = 1;   #0#1 = "ENERO     ";  |FINSI;
     |SI #0#0 = 2;   #0#1 = "FEBRERO   ";  |FINSI;
     |SI #0#0 = 3;   #0#1 = "MARZO     ";  |FINSI;
     |SI #0#0 = 4;   #0#1 = "ABRIL     ";  |FINSI;
     |SI #0#0 = 5;   #0#1 = "MAYO      ";  |FINSI;
     |SI #0#0 = 6;   #0#1 = "JUNIO     ";  |FINSI;
     |SI #0#0 = 7;   #0#1 = "JULIO     ";  |FINSI;
     |SI #0#0 = 8;   #0#1 = "AGOSTO    ";  |FINSI;
     |SI #0#0 = 9;   #0#1 = "SEPTIEMBRE";  |FINSI;
     |SI #0#0 = 10;  #0#1 = "OCTUBRE   ";  |FINSI;
     |SI #0#0 = 11;  #0#1 = "NOVIEMBRE ";  |FINSI;
     |SI #0#0 = 12;  #0#1 = "DICIEMBRE ";  |FINSI;

     |SI #0#1 = "          ";
         |MENAV "     Periodo Incorrecto.";
         |ERROR;
     |FINSI;
     |PINTA #0#1;

     fFecha = ~;
     aAlfa  = fFecha % -104;
     #0#2   = aAlfa;
     |PINTA #0#2;
|FPRC;

|PROCESO Parrilla;
     |SI #0#3 = 0;
         #0#3 = 0;  |PINTA #0#3;
         #0#4 = 0;  |PINTA #0#4;  |C_M #0#4, 1, "N";
         |PARA nCampo = 5;  |SI nCampo [ 16;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |SINO;
         |SI enEmpresa = 0;
             |C_M #0#3, 1, "S";
             |C_M #0#4, 1, "S";
         |FINSI;

         |PARA nCampo = 5;  |SI nCampo [ 16;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "N";
               #0nCampo = 0;  |PINTA #0nCampo;
         |SIGUE;
     |FINSI;
|FPRC;

|PROCESO NoMasEmpresas;
     |SI #0Campo = 0;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 16;  |HACIENDO nCampo = nCampo + 1;
               #0nCampo = 0;  |C_M #0nCampo, 1, "N";
         |SIGUE;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 16;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |FINSI;
|FPRC;

|| /////////////////////// PROCESOS DE TRABAJO \\\\\\\\\\\\\\\\\\\\
|PROCESO Actualiza;

 |SI #999#9 ! "X";
     #28#0 = #999#0;
     #28#1 = #999#2;
     #28#2 = #999#3;
     #28#3 = #999#1;
     #28#4 = #999#12;
     #28#5 = #999#13;
     |LEE 141#28=;
     |SI FSalida ! 0;
         |DDEFECTO #28;
         #28#0 = #999#0;
         #28#1 = #999#2;
         #28#2 = #999#3;
         #28#3 = #999#1;
         #28#4 = #999#12;
         #28#5 = #999#13;
         |GRABA 020#28b;
     |FINSI;
     #28#6 = #999#8;
     #28#7 = #999#9;
     #28#8 = #999#4;

     |GRABA 020#28a;
     |LIBERA #28;
     enGraba = 1;
 |FINSI;
|FPRC;

|DEFBUCLE LeeAuxiliar;
 #999#1;
 ;
 00001,000, 00, 00;
 99999,999, 99, 99;
 ;
 NULL, Actualiza;
|FIN;

|PROCESO Actualizar;
  enGraba = 0;
  |ABRE #28;
  |BUCLE LeeAuxiliar;
  |CIERRA #28;
  |SI enGraba = 1;
      |MENAV "0000Recuerde. Debemos entrar en el Modelo para hacer efectiva la modificacion";
  |FINSI;
|FPRC;

|| --------------------------------------------------------------------
|PROCESO LosNombres;
  |DDEFECTO #118;
  |ABRE #118;
  #118#0 = enEmpresa;
  |LEE 041#118=;
  #999#10 = #118#1;
  |CIERRA #118;
|FPRC;

|PROCESO CreaPrescripcion;

    |SI #4#3 = 300;  |O #4#3 = 370;  |O #4#3 = 320; |O #4#3 = 303;
        |O #4#3 = 330;  |O #4#3 = 332;  |O #4#3 = 310;
          |O #4#3 = 322;

           enEmpresa   = #4#0;
           enEjer      = #4#1;
           enPeriodo   = #4#2;
           enModelo    = #4#3;
           enComplem   = #4#4;
           enActividad = 00;

           enImpAcum = 0;
           enImporte = 0;
           enImport1 = 0;
           |SUB_EJECUTA_NP "dsmoy007;AUTOMATICO";

           |DDEFECTO #28;
           #28#0 = enEmpresa;
           #28#1 = enEjer;
           #28#2 = enPeriodo;
           #28#3 = enModelo;
           #28#4 = enComplem;
           #28#5 = enActividad;
           |LEE 101#28=;
           nEstPres = FSalida;

           |SI enImporte ! 0; |O nEstPres = 0; |O enImpAcum ! 0; |O enImport1 ! 0;

               |DDEFECTO #999;
               #999#0 = enEmpresa;
               #999#1 = enModelo;
               |LEE 040#999=;
               |SI FSalida ! 0;
                   #999#0  = enEmpresa;
                   #999#1  = enModelo;
                   #999#2  = #0#2;
                   #999#3  = #0#0;
                   #999#11 = #0#1;
                   #999#12 = enComplem;
                   #999#13 = enActividad;
                   |HAZ LosNombres;
                   |GRABA 020#999c;
               |FINSI;
               #999#4 = enImporte;
               #999#5 = enTrim;
               #999#6 = enAny;
               |SI #999#5 ! 0;
                   aAlfa1  = #999#5;
                   #999#7  = aAlfa1 + "T/";
                   #999#14 = #999#6;
                   #999#14 = #999#14 % -102;
               |FINSI;
               |SI nEstPres = 0;
                   #999#8  = #28#6;
                   #999#9  = #28#7;
               |SINO;
                   #999#8  = enImport1;
                   #999#9  = "S";
               |FINSI;
               #999#15 = enImpAcum;
               #999#16 = #999#9;
               #999#17 = enImport1;
               |HAZ ElCampo18;
               enGraba = 1;
               |GRABA 020#999a;
          |FINSI;

    |FINSI;
|FPRC;

|DEFBUCLE dsmom004;
     #4#1;
     ;
     nDEmpresa, #0#2, #0#0, 300, 00;
     nHEmpresa, #0#2, #0#0, 399, 99;
     ;
     NULL, CreaPrescripcion;
|FIN;

|| ----------------------------------
|CALCULO ElTipo12w76; |TIPO 12;
|FCAL;

|CALCULO ElTipo11w76; |TIPO 11;
  nInkey = FSalida;

  |SI nInkey < 10; |O nInkey > 13;
      |ACABA;
  |FINSI;

  |LEE 140#999=;

  |SI nInkey = 10;  |HAZ ImprEste; |FINSI;

  |SI nInkey = 11; |O nInkey = 12; |HAZ LaModificacion; |FINSI;

  |SI nInkey = 13; |HAZ ImprimeTodo; |FINSI;
|FCAL;

|CALCULO ElTipo13w76; |TIPO 13;

 |CAMPO_ATRIBUTO #999#15,D,0,0;
 |SI #999#15 ! 0;
     |CAMPO_ATRIBUTO #999#15,S,0,0;
 |FINSI;
 |PINTA #999#15;

 |CAMPO_ATRIBUTO #999#9,D,0,0;
 |SI #999#9 = "X";
     |CAMPO_ATRIBUTO #999#9,N,0,0;
 |FINSI;
 |PINTA #999#9;
|FCAL;

|PROCESO ElCampo18;
 |SI #999#9 = "S"; #999#18 = "Automatico";   |FINSI;
 |SI #999#9 = "N"; #999#18 = "No aplicado";  |FINSI;
 |SI #999#9 = "M"; #999#18 = "Manual    ";   |FINSI;
 |SI #999#9 = "X"; #999#18 = "No modificar"; |FINSI;
|FPRC;

|PROCESO ImprEste;
 enEmpresa = #999#0;
 enEjer    = #999#2;
 enPeriodo = #999#3;
 |SUB_EJECUTA_NP "dsmoy007;ENESTE";
 |ERROR;
|FPRC;

|| -------------------
|PROCESO ImprimePanta;
  eaCam1 = #998#0; |QBF eaCam1;
  eaCam1 = ("00000" + eaCam1 ) % -105;
  eaCam2 = #998#10;
  enCam3 = #998#1;
  enCam4 = #998#4;
  eaCam5 = #998#7;
  enCam6 = #998#6;
  enCam7 = #998#8;
  eaCam8 = #998#18;
  enCam9 = #998#15;
  |PRINT;
|FPRC;

|DEFBUCLE LeeImagen;
  #998#1004;
  ;
  00000, 000, 00, 00;
  99999, 999, 99, 99;
  ;
  NULL, ImprimePanta;
|FIN;

|PROCESO ImprimeTodo;

  |DBASE aAlfa1;
  aMas  = aAlfa1 + "def/dsmow076.mas";

  |CARGA_DEF aMas, imagen@;
  |SI FSalida = 0;

      |IMPRE 0;
      |SI FSalida ! 0;
          |DESCARGA_DEF #imagen@;
          |ACABA;
      |FINSI;

      |INFOR informe;
      |SI FSalida ! 0;
          |DESCARGA_DEF #imagen@;
          |FINIMP;
          |ACABA;
      |FINSI;

      |NOME_DAT #998 aFichero;
      |BUCLE LeeImagen;

      |PIEINF;
      |FININF;
      |FINIMP;
      |DESCARGA_DEF #imagen@;
  |FINSI;
|FPRC;

|PROCESO ElCampo9; |TIPO 0;
  aAlfa1 = Contenido;
  aAlfa  = "S";
  |SI #999#9 ! "M";
      aAlfa = "N";
      |SI #999#9 = "S"; #999#8 = #999#17; |FINSI;
      |SI #999#9 = "N"; #999#8 = 0;       |FINSI;
      |SI #999#9 = "X"; #999#16 = aAlfa1; |FINSI;
  |FINSI;
  |PINTA #999#8;
  |C_M #999#8, 1, aAlfa;
  |HAZ ElCampo18;
  |HAZ ElTipo13w76;
|FPRC;

|PROCESO LaModificacion;
  |SI nInkey = 11;
      |ENCAMPO #9#999;
      |ENCAMPO #8#999;
      |SI FSalida = 0;
          |GRABA 020#999a;
          |LIBERA #999;
      |FINSI;
  |SINO;
      |SI #999#9 ! "X";
          #999#16 = #999#9;
          #999#9  = "X";
      |SINO;
          #999#9 = #999#16;
          |SI #999#9 = "S";
              #999#8 = #999#17;
          |FINSI;
      |FINSI;
      |HAZ ElCampo18;
      |GRABA 020#999a;
      |PINTA #999#8;
      |HAZ ElTipo13w76;
   |FINSI;
|FPRC;

|| ******************************************************

|RUTINA ClaveBaja999;
     #999#0  = 1;
     #999#1  = 300;
     #999#12 = 00;
     #999#13 = 00;
|FRUT;

|RUTINA ClaveAlta999;
     #999#0 = 99999;
     #999#1 = 399;
     #999#12 = 99;
     #999#13 = 99;
|FRUT;

|PROCESO Tipo3;  |TIPO 3;

     aFichero = ("76w" + Usuario) % 108;
     |NOME_DAT #999 aFichero;
     |ABRE #999;  |CIERRA #999;  |DELINDEX #999;

     enGraba = 0;
     |ABRE #28;
     |ABRE #999;
     |SI #0#3 ! 0;
         nDEmpresa = #0#3;
         nHEmpresa = #0#4;
         |BUCLE dsmom004;
     |SINO;
         |PARA nCampo = 5;  |SI nCampo [ 16;  |HACIENDO nCampo = nCampo + 1;
               nDEmpresa = #0nCampo;
               nHEmpresa = #0nCampo;
               |BUCLE dsmom004;
         |SIGUE;
     |FINSI;
     |CIERRA #999;
     |CIERRA #28;

     |SI enGraba = 1;
         |PINPA #0#999;
         |ENTLINEAL #1#999, -1, 4, 21, 0, ClaveBaja999, ClaveAlta999;
         |CONFI "    SRealizar Pases S/N";
         |SI FSalida = 0;
             |HAZ Actualizar;
         |FINSI;
     |SINO;
         |MENAV "     No hay Prescripciones en la Seleccion ";
     |FINSI;
     |DELINDEX #999;
|FPRC;

|PROGRAMA;

    |SI enEmpresa ! 0;
         #0#0 = enPeriodo;
         #0#1 = eaNomPer;
         #0#2 = enEjer;
         #0#3 = enEmpresa;
         #0#4 = enEmpresa;
         |HAZ Tipo3;
     |SINO;
         |CLS;
         |CABEZA "Calculo Prescripcion IVA";
         |DDEFECTO #0;
         |PINPA #0#0;
         |PINDA #0#0;
         |ENDATOS #1#0;
     |FINSI;
|FPRO;
