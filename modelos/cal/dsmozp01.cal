|FICHEROS;
     dsmozp01 #0;

     dsmom002 #2;
     dsmom004 #4;
     dsmom008 #8;

     dsmom084 #84;
     dsmom085 #85;
     dsmom086 #86;
     dsmom087 #87;

     dsmow140 #982,MANTE;
     dsmow141 #983,MANTE;
     dsmow126 #126;

     :/basica/def/agifigen #1000;
     :/basica/def/agicen14 #1014;
     :/contagen/def/canempre #101;
|FIN;

|VARIABLES;
     fFecha      = @;
     nEjer       = 0;
     nEjerC      = 0;
     nEmpresa    = 0;
     nPeriodo    = 0;
     aFichero    = "";
     &aFichw82   = "";
     aAlfa       = "";
     aAlfa1      = "";
     nNume1      = 0;

     enGraba     = 0;
     aParam      = "";
     nCampo      = 0;
     nDEmpresa   = 0;
     nHEmpresa   = 0;
     nDPeriodo   = 0;
     nHPeriodo   = 0;
     fFecIni     = @;
     fFecFin     = @;
     nDepar      = 0;

     nModelo     = 0;
     nSwComplem  = 0;
     nComplem    = 0;
     nSector     = 0;
     nNumAct     = 0;
     nEstado84   = 0;
     nHayDatos   = 0;
|FIN;

|INCLUYE i_variar;

|| /////////////////////// PROCESOS ENTRADA POR PANTALLA \\\\\\\\\\\\\\\\\\\\

|PROCESO Ejercicio;  |TIPO 7;
     fFecha = ~;
     aAlfa  = fFecha % -104;
     #0#0   = aAlfa;

     aAlfa  = fFecha % 402;
     nNume1 = aAlfa;
     #0#15 = 99;
     |SI nNume1 ] 3; |Y nNume1 [ 5;  #0#15 = 3;  |FINSI;
     |SI nNume1 ] 6; |Y nNume1 [ 8;  #0#15 = 6;  |FINSI;
     |SI nNume1 ] 9; |Y nNume1 [ 11; #0#15 = 9;  |FINSI;
     |SI nNume1 < 3; #0#0 = #0#0 - 1;  |FINSI;
     |PINTA #0#0;
     |PINTA #0#15;
|FPRC;

|PROCESO Periodo;
     |HAZ NomPeriodo;
     |SI #0#16 = "          ";
         |MENAV "     Periodo Incorrecto.";
         |ERROR;
     |FINSI;

     |PINTA #0#15;
     |PINTA #0#16;
|FPRC;

|PROCESO NomPeriodo;
     #0#16 = "          ";
     |SI #0#15 = 1;   #0#16 = "ENERO     ";  |FINSI;
     |SI #0#15 = 2;   #0#16 = "FEBRERO   ";  |FINSI;
     |SI #0#15 = 3;   #0#16 = "MARZO     ";  |FINSI;
     |SI #0#15 = 4;   #0#16 = "ABRIL     ";  |FINSI;
     |SI #0#15 = 5;   #0#16 = "MAYO      ";  |FINSI;
     |SI #0#15 = 6;   #0#16 = "JUNIO     ";  |FINSI;
     |SI #0#15 = 7;   #0#16 = "JULIO     ";  |FINSI;
     |SI #0#15 = 8;   #0#16 = "AGOSTO    ";  |FINSI;
     |SI #0#15 = 9;   #0#16 = "SEPTIEMBRE";  |FINSI;
     |SI #0#15 = 10;  #0#16 = "OCTUBRE   ";  |FINSI;
     |SI #0#15 = 11;  #0#16 = "NOVIEMBRE ";  |FINSI;
     |SI #0#15 = 12;  #0#16 = "DICIEMBRE ";  |FINSI;
     |SI #0#15 = 99;  #0#16 = "TOTAL     ";  |FINSI;
|FPRC;

|PROCESO Parrilla;
     |SI #0#1 = 0;
         #0#1 = 0;  |PINTA #0#1;
         #0#2 = 0;  |PINTA #0#2;  |C_M #0#2, 1, "N";
         |PARA nCampo = 3;  |SI nCampo [ 14;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |SINO;
         |SI nEmpresa = 0;
             |C_M #0#1, 1, "S";
             |C_M #0#2, 1, "S";
         |FINSI;

         |PARA nCampo = 3;  |SI nCampo [ 14;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "N";
               #0nCampo = 0;  |PINTA #0nCampo;
         |SIGUE;
     |FINSI;
|FPRC;

|PROCESO NoMasEmpresas;
     |SI #0Campo = 0;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 14;  |HACIENDO nCampo = nCampo + 1;
               #0nCampo = 0;  |C_M #0nCampo, 1, "N";
         |SIGUE;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 14;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |FINSI;
|FPRC;

||*****************************************************************************

|| ========================================================================
|| ============= Primera Parte, ver que empresas tiene que aplicar Prorrata
|| ========================================================================
|PROCESO LeeLosSectores;
   #126#0 = #983#1;
   #126#1 = #983#0;
   #126#2 = #983#24;
   |LEE 001#126=;
   |SI FSalida ! 0;
       |DDEFECTO #126;
       #126#0 = #983#1;
       #126#1 = #983#0;
       #126#2 = #983#24;
       #126#3 = "SECTOR: " + #126#2;
       #126#6 = #983#19;
       |GRABA 020#126c;
   |FINSI;
   #126#10 = #126#10 + #983#18;
   #126#11 = #126#11 + #983#20;
   #126#12 = #126#12 + #983#21;
   #126#13 = #126#13 + #983#22;
   #126#14 = #126#14 + #983#23;
   |GRABA 020#126a;
   |LIBERA #126;

   #982#10 = #982#10 + #983#18;
   #982#11 = #982#11 + #983#20;
   #982#14 = #982#14 + #983#21;
   #982#41 = #982#41 + #983#22;
   #982#12 = #982#12 + #983#23;

   |SI #983#19 ! 100;
       #982#36 = "S";
   |FINSI;

   |SI nHayDatos = 0;  #982#36 = "N"; |FINSI;
|FPRC;

|DEFBUCLE LeeLosSectores;
  #983#1;
  ;
  #85#1, #85#0, 00;
  #85#1, #85#0, 99;
  ;
  NULL, LeeLosSectores;
|FIN;

|PROCESO LeeDsmom087;
  nDepar = #87#5;

  |DDEFECTO #86;
  #86#0 = #87#0;
  #86#1 = #87#1;
  #86#2 = nDepar;
  |LEE 001#86=;
  nSector = #86#15;

  #983#0 = #87#1;
  #983#1 = #87#0;
  #983#2 = nDepar;
  |LEE 141#983=;
  |SI FSalida ! 0;
      |HAZ AltaAux83;
  |FINSI;

  |SI #87#2 < #0#15;
      #983#21 = #983#21 + #87#13;
  |SINO;
      #983#18 = #983#18 + #87#12;
      #983#20 = #983#20 + #87#13;
  |FINSI;

  #983#23 =  #983#18 - #983#20;
  |GRABA 020#983a;
  |LIBERA #983;

  |SI #87#40 ! 0; |O #87#42 ! 0; |O #87#44 ! 0; |O #87#46 ! 0;
    |O  #87#48 ! 0; |O #87#50 ! 0; |O #87#52 ! 0;

        nDepar  = 0;
        nSector = 0;
        #983#0 = #87#1;
        #983#1 = #87#0;
        #983#2 = nDepar;
        |LEE 141#983=;
        |SI FSalida ! 0;
            |HAZ AltaAux83;
        |FINSI;

        |SI #87#2 < #0#15;
            #983#21 = #983#21 + #87#38;
        |SINO;
            #983#18 = #983#18 + #87#37;
            #983#20 = #983#20 + #87#38;
        |FINSI;
        #983#23 = #983#18 - #983#20;

        |GRABA 020#983a;
        |LIBERA #983;
  |FINSI;
  nHayDatos = 1;
|FPRC;

|DEFBUCLE LeeDsmom087;
  #87#1;
  ;
  #85#0, #85#1, nPeriodo, nModelo, nComplem, 00, 0, 00;
  #85#0, #85#1, nPeriodo, nModelo, nComplem, 99, 9, 99;
  e;
  NULL, LeeDsmom087;
|FIN;

|PROCESO MiroModelo;
        nComplem = -1;
        #87#0 = #85#0;
        #87#1 = #85#1;
        #87#2 = nPeriodo;
        #87#3 = nModelo;
        #87#4 = 99;
        #87#5 = 0;
        #87#6 = 0;
        #87#7 = 0;
        |LEE 040#87];
        |SI FSalida = 0;
            |LEE 040#87a;
        |SINO;
            |LEE 040#87u;
        |FINSI;
        |SI FSalida = 0; |Y #87#0 = #85#0; |Y #87#1 = #85#1;
            |Y #87#2 = nPeriodo; |Y #87#3 = nModelo;
               nComplem = #87#4;
        |FINSI;

        |SI nComplem ! -1;
            |SI nComplem ! 0; nSwComplem = 1; |FINSI;
            |CIERRA #87;
            |BUCLE LeeDsmom087;
            |ABRE #87;
        |FINSI;
|FPRC;

|PROCESO Acumulado83;
  nHayDatos = 0;
  nDPeriodo = 01;
  nHPeriodo = 12;

  |SI #0#15 ! 99;
      nHPeriodo = #0#15;

     |ABRE #86;
     |ABRE #87;
     |PARA nPeriodo = nDPeriodo; |SI nPeriodo [ nHPeriodo; |HACIENDO nPeriodo = nPeriodo + 1;
           nModelo = 303; |HAZ MiroModelo;
           nModelo = 322; |HAZ MiroModelo;
           nModelo = 370; |HAZ MiroModelo;
     |SIGUE;
     |CIERRA #87;
     |CIERRA #86;
  |SINO;
     nHayDatos = 1;
  |FINSI;

  |ABRE #126;
  |BUCLE LeeLosSectores;
  |CIERRA #126;

  |SI nHayDatos = 0;  #982#36 = "N"; |FINSI;
|FPRC;

|| ................. Grabo registro por Empresa
|PROCESO FicherosConta;
     enPartido  = 1;
     enPerConta = -1;
     enSwOpPar  = 0;
     |HAZ SituaEmpresa;
     |SI enSwPartido = 0;
         |ABRE #101;
         |SELECT #4#101;
         #101#2  = enEmpresa;
         #101#26 = nEjerC;
         |LEE 040#101=;
         |SI FSalida = 0;
             enPerConta = #101#3;
         |FINSI;
         |SELECT #1#101;
         |CIERRA #101;
    |SINO;
         enPerConta = enPeriodoPar;
    |FINSI;
    #982#37    = enPerConta;
    enPartido  = 0;
|FPRC;

|PROCESO GraboAx82;
    #982#0  = #0#0;
    #982#1  = enEmpresa;
    #982#2  = #1000#1;
    |SI #0#15 = 12; |O #0#15 = 99;
        #982#3  = #85#4;
        #982#4  = #85#5;
        #982#5  = #85#17;
    |SINO;
        #982#3  = #85#2;
        #982#4  = #85#3;
        #982#5  = #85#16;
    |FINSI;
    #982#6  = #1000#13;
    #982#17 = nNumAct;
    #982#32 = #0#15;
    #982#33 = #0#16;
    #982#8  = "S";

    |SI #0#15 = 99;
        #982#10 = #85#9;
        #982#11 = #85#10;
        #982#14 = #85#11;
        #982#41 = #85#12;
        #982#12 = #85#13;
    |FINSI;
    |SI #0#15 = 12;
        #982#41 = #85#12;
    |FINSI;
    |GRABA 000#982n;
    enGraba = 1;
|FPRC;

|| ... Grabo Registro por Empresa-Actividad
|PROCESO AltaAux83;
     |SI nDepar = 00; nSector = 00; |FINSI;
     |SI nDepar = 99; nSector = 99; |FINSI;

     |DDEFECTO #1014;
     |SI #86#2 ! 00; |Y #86#2 ! 99;
         #1014#0 = #86#0;
         #1014#1 = #86#2;
         |LEE 001#1014=;
     |FINSI;

     |DDEFECTO #983;
     #983#0 = #0#0;
     #983#1 = enEmpresa;
     #983#2 = nDepar;
     |LEE 001#983=;
     |SI FSalida ! 0;
         |DDEFECTO #983;
         #983#0 = #0#0;
         #983#1 = enEmpresa;
         #983#2 = nDepar;
         #983#3 = #1014#4;   ||Nombre actividad
         #983#4 = #1014#20;  || cnae
         #983#5 = #1014#3;   || epigrafe
         #983#6 = #1014#7;   || iva
         #983#7 = #1014#8;
         |SI nDepar = 99; |O nDepar = 00;
             |SI nDepar = 99; #983#3 = "REGISTRO DE REGULARIZACION"; #1014#20 = ""; |FINSI;
             |SI nDepar = 00; #983#3 = "IVA SOPORTADO OPERACIONES COMUNES (DESDE CONTABILIDAD)"; #1014#20 = ""; |FINSI;
             #983#4 = "";
             #983#5 = "";
             #983#6 = "";
             #983#7 = "";
         |FINSI;
         #983#8  = #1000#87;
         #983#9  = #1000#88;
         #983#10 = #1014#9;
         #983#11 = #1014#10;
         #983#12 = #1014#16;
         #983#13 = #1014#17;
         |SI #1014#5 > "01.01.0000"; #983#14 = #1014#5; |FINSI;
         |SI #1014#6 > "01.01.0000"; #983#15 = #1014#6; |FINSI;
         #983#16 = #1014#15;
         #983#24 = nSector;   ||Sector

         #983#19 = #86#5;
         |SI #0#15 = 12; |O #0#15 = 99; #983#19 = #86#6; |FINSI;
         |GRABA 020#983c;
     |FINSI;
|FPRC;

|PROCESO PrimerFiltro;  || Mas de una Actividad

     nDepar  = #86#2;
     nSector = #86#15;
     |SI #86#2 ! 00; |Y #86#2 ! 99;
          nNumAct = nNumAct + 1;
     |FINSI;
     |HAZ AltaAux83;

     |SI #0#15 = 99;
         #983#18 = #86#10;
         #983#20 = #86#11;
         #983#21 = #86#12;
         #983#22 = #86#13;
         #983#23 = #86#14;
         |GRABA 020#983a;
     |FINSI;
     |LIBERA #983;
|FPRC;

|DEFBUCLE dsmom086;
  #86#1;
  ;
  #85#0, #85#1, 00;
  #85#0, #85#1, 99;
  e;
  NULL, PrimerFiltro;
|FIN;

|| ..........................................

|PROCESO MirarFiltros;

     enEmpresa = #85#0;

     #1000#0 = enEmpresa;
     |LEE 001#1000=;
     |SI FSalida = 0;

         |DDEFECTO #84;
         #84#0 = #85#0;
         #84#1 = #85#1;
         |LEE 001#84=;
         nEstado84 = FSalida;

         nNumAct = 0;
         |DDEFECTO #982;
         #982#36 = "S";
         |SI #982#3 ! "E"; |Y #982#4 = 100;
             #982#36 = "N";
         |FINSI;

         |BUCLE dsmom086;   || Graba Actividades

         |HAZ Acumulado83;

         |HAZ FicherosConta
         |HAZ GraboAx82;
    |FINSI;
|FPRC;

|DEFBUCLE dsmom085;
    #85#1;
    ;
    nDEmpresa, #0#0;
    nHEmpresa, #0#0;
    ;
    NULL, MirarFiltros;
|FIN;

|| ========================================================================
|PROCESO Sup180;
 #982#0 = #0#0;
 #982#1 = 0;
 #982#36 = "S";
|FPRC;

|PROCESO Inf180;
 #982#0 = #0#0;
 #982#1 = 99999;
 #982#36 = "S";
|FPRC;

|PROCESO Tipo3;  |TIPO 3;
     aAlfa     = #0#0;
     aAlfa     = aAlfa % -102;
     nEjerC    = aAlfa;       || ejercicio contable de canempre
     enEjer    = #0#0;
     enPeriodo = #0#15;

     aAlfa1 = #0#0;
     aAlfa1 = "01.01." + aAlfa1; fFecIni = aAlfa1;
     aAlfa1 = #0#0;
     aAlfa1 = "31.12." + aAlfa1; fFecFin = aAlfa1;

     |ABRE #1000;
     |ABRE #1014;
     |ABRE #84;
     |ABRE #982;
     |ABRE #983;
     |SI #0#1 ! 0;
         nDEmpresa = #0#1;
         nHEmpresa = #0#2;
         |BUCLE dsmom085;
     |SINO;
         |PARA nCampo = 3;  |SI nCampo [ 14;  |HACIENDO nCampo = nCampo + 1;
               nDEmpresa = #0nCampo;
               nHEmpresa = #0nCampo;
               |BUCLE dsmom085;
         |SIGUE;
     |FINSI;
     |CIERRA #983;
     |CIERRA #982;
     |CIERRA #84;
     |CIERRA #1014;
     |CIERRA #1000;

     |SI enGraba = 1;
         |PINPA #0#982;
         |SI #0#15 = 99;
             |ATRI S; |PINTA 1221, "Acumulados total de Ejercicio "; |ATRI 0;
         |FINSI;
         |ENTLINEAL #2#982, -3, 4, 22, 0, Sup180, Inf180;
     |SINO;
         |MENAV "    Seleccion Vacia ... ";
     |FINSI;
|FPRC;


|PROGRAMA;

     nEmpresa = enEmpresa;
     nEjer    = enEjer;

     aParam = PARAMETRO; |QBF aParam;
     |SI aParam = "";
         |HAZ LeeCtrlProrr;
         |SI enSwProrr = 2;
             |ACABA;   || .. acaba cuando no hay prorrata
         |FINSI;
     |FINSI;

     aFichw82 = ("86w" + Usuario) % 108; |NOME_DAT #982 aFichw82;
     aFichero = ("87w" + Usuario) % 108; |NOME_DAT #983 aFichero;
     aFichero = ("6w" + Usuario)  % 108; |NOME_DAT #126 aFichero;

     |DELINDEX #982;
     |DELINDEX #983;
     |DELINDEX #126;

     |SI enEmpresa ! 0;
         |DDEFECTO #0;
         #0#0  = enEjer;
         #0#1  = enEmpresa;
         #0#2  = enEmpresa;
         #0#15 = enPeriodo;
         |HAZ NomPeriodo;
         |HAZ Tipo3;
     |SINO;
         |MANTE #0;
     |FINSI;

     |DELINDEX #982;
     |DELINDEX #983;

     enEmpresa = nEmpresa;
     enEjer    = nEjer;
|FPRO;
|| **************************************************************************
