|FICHEROS;
     dsmoy006 #0;
     dsmom023 #23;  ||Tabla cuentas
     dsmom024 #24;  ||Mant. Valores por Empresas

     dsmom100 #100;
     :/contagen/def/caconimp #306;  || configuracion impresiones

     :/basica/def/agitab99 #99;
     :/basica/def/agicen14 #114;
     :/basica/def/agifigen #118;
     :/basica/def/agim0019 #119;
|FIN;

|VARIABLES;
     nEstado   = 0;
     nCampo    = 0;
     nDEmpresa = 0;
     nHEmpresa = 0;
     nPeriodo  = 0;
     nEjer     = 0;
     aAlfa1    = "";
     aAlfa2    = "";
     aFichero  = "";
     aPath     = "";
     nNume1    = 0;

     nPin1       = 0;
     aPin1       = "";
     efPn10      = @;
     efPn11      = @;
     swExiste    = 0;
     &enSwLinea  = 0;
     nLaEmpresa  = 0;
     nElModelo   = 0;
     nElAny      = 0;
     swPrEmpresa = 0;
     swPrModelo  = 0;
     swPrAny     = 0;
     swAlgun     = 0;
     swBo        = 0;
     &eaLit1     = "";
     &eaLit2     = "";
     &enDatos    = 0;
     fLaFecha    = @;
  nSw190 = 0;
  nSw180 = 0;
  nSw193 = 0;
  nSw296 = 0;
  nSw194 = 0;
  nSw390 = 0;
|FIN;

|INCLUYE i_variar;
|INCLUYE i_masivo;

|| /////////////////////// PROCESOS ENTRADA POR PANTALLA \\\\\\\\\\\\\\\\\\\\
|PROCESO Tipo7;  |TIPO 7;
     |SI enEmpresa ! 0;
         #0#0 = enEmpresa;  |PINTA #0#0;
         #0#1 = enEmpresa;  |PINTA #0#1;

         |C_M #0#0, 1, "N";
         |C_M #0#1, 1, "N";
     |FINSI;
|FPRC;

|PROCESO Tipo7_16;  |TIPO 7;
    |SI enModelo ! 0;
         #0#16 = enModelo;  |PINTA #0#16;
         #0#17 = enModelo;  |PINTA #0#17;

         |C_M #0#16, 1, "N";
         |C_M #0#17, 1, "N";
    |FINSI;
|FPRC;

|PROCESO Parrilla;
     |SI #0#0 = 0;
         #0#0 = 0;  |PINTA #0#0;
         #0#1 = 0;  |PINTA #0#1;  |C_M #0#1, 1, "N";
         |PARA nCampo = 2;  |SI nCampo [ 13;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |SINO;
         |SI enEmpresa = 0;
             |C_M #0#0, 1, "S";
             |C_M #0#1, 1, "S";
         |FINSI;

         |PARA nCampo = 2;  |SI nCampo [ 13;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "N";
               #0nCampo = 0;  |PINTA #0nCampo;
         |SIGUE;
     |FINSI;
|FPRC;

|PROCESO Parrilla_16;
     |SI #0#16 = 0;
         #0#16 = 0;    |PINTA #0#16;
         #0#17 = 899;  |PINTA #0#17;  |C_M #0#17, 1, "N";
         |PARA nCampo = 18;  |SI nCampo [ 29;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |SINO;
         |SI enModelo = 0;
             |C_M #0#16, 1, "S";
             |C_M #0#17, 1, "S";
         |FINSI;

         |PARA nCampo = 18;  |SI nCampo [ 29;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "N";
               #0nCampo = 0;  |PINTA #0nCampo;
         |SIGUE;
     |FINSI;
|FPRC;

|PROCESO NoMasEmpresas;
     |SI #0Campo = 0;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 13;  |HACIENDO nCampo = nCampo + 1;
               #0nCampo = 0;  |C_M #0nCampo, 1, "N";
         |SIGUE;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 13;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |FINSI;
|FPRC;

|PROCESO NoMasModelos;
     |SI #0Campo = 0;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 29;  |HACIENDO nCampo = nCampo + 1;
               #0nCampo = 0;  |C_M #0nCampo, 1, "N";
         |SIGUE;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 29;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |FINSI;
|FPRC;

|PROCESO ConsuModelo;
     |ABRE #99;
     #99#0 = #0Campo;
     |LEE 040#99=;
     |SI FSalida ! 0;  |DDEFECTO #99;  |FINSI;
     |CIERRA #99;

     |SI Campo = 16; |O Campo = 17;  |ACABA;  |FINSI;
     |SI #0Campo = 0; |ACABA; |FINSI;
     |SI #99#4 = "N";
         |MENAV "     Modelo sin Activar.";
         |ERROR;
     |FINSI;
|FPRC;

|PROCESO DatosEmp; |TIPO 7;

   |DDEFECTO #306;

   |ABRE #100;
   |LEE 040#100p;
   |SI FSalida = 0;
       aAlfa1 = #100#1; |QBF aAlfa1; aAlfa1 = ("00000" + aAlfa1) % -105;
       aAlfa2 = #100#2; |QBF aAlfa2; aAlfa2 = ("0"     + aAlfa2) % -101;
       |DBASS aPath; |QBT aPath;
       aPathConta = aPath + "contagen/fich/" + aAlfa1 + aAlfa2 + "/";

       |PATH_DAT #306 aPathConta;

       |ABRE #306;
       #306#0 = 1;
       |LEE 000#306=;
       |CIERRA #306;
   |FINSI;
   |CIERRA #100;

   #0#47  = #306#7; |PINTA #0#47;
   aAlfa1 = #306#8; |C_M #0#47,1,aAlfa1;
|FPRC;

|CALCULO Eano; |TIPO 7;
 fLaFecha = ~;
 aAlfa1 = fLaFecha; aAlfa1 = aAlfa1 % -104;
 #0#15 = aAlfa1;
 |PINTA #0#15;
|FCAL;
|| /////////////////////// //////////////////////////////////////////////////
|| /////////////////////// PROCESOS DE INFORMES \\\\\\\\\\\\\\\\\\\\\
|PROCESO LeeLaEmpresa;
  |PDEFECTO #0, 30,47;
  #0#30 = nLaEmpresa;

  swAlgun    = 0;
  swPrAny    = 0;
  nPin1 = 0;
  aPin1 = "";
  efPn10 = "01.01.0000";
  efPn11 = "01.01.0000";
  || Lee Actividad
    |ABRE #agicen14;
    #agicen14#0 = #0#30; || Empresa
    #agicen14#1 = 00;
    |LEE 041#agicen14.];
    |SI FSalida = 0; |Y #agicen14#0 = #0#30;
        #0#35 = #agicen14#9;
        #0#36 = #agicen14#10;
        #0#37 = #agicen14#7;
        #0#38 = #agicen14#8;
        efPn10 = #agicen14#5;
        efPn11 = #agicen14#6;
    |FINSI;
    |CIERRA #agicen14;

 || Lee Ficha Sociedades
    swExiste = 0;
    |ABRE #agim0019;
    #agim0019#0 = #0#30; || Empresa
    #agim0019#1 = 99;
    |LEE 041#agim0019.];
    |SI FSalida = 0;
        |LEE 041#agim0019.a;
    |SINO;
        |LEE 041#agim0019.u;
    |FINSI;
    |SI FSalida = 0; |Y #agim0019#0 = #0#30;
        swExiste = 1;
    |FINSI;
    |SI swExiste = 1;
        efPn10 = #agim0019#7;      || Fecha Activa
        efPn11 = #agim0019#55;     || Fecha Inactiva
        nPin1  = #agim0019#4;
        aPin1  = #agim0019#5;
    |FINSI;
    |CIERRA #agim0019;

    |ABRE #agifigen;
    #agifigen#0 = #0#30; || Empresa
    |LEE 041#agifigen.=;
    |SI FSalida = 0;
        efPn10  = #agifigen#93;
        efPn11  = #agifigen#94;
        nPin1   = #agifigen#87;
        aPin1   = #agifigen#88;
        #0#31 = #agifigen#13;  || Nif
        #0#32 = #agifigen#1;   || Nombre
    |FINSI;
    |CIERRA #agifigen;
    |SI efPn10 [ "01.01.1900"; efPn10 = ""; |FINSI;
    |SI efPn11 [ "01.01.1900"; efPn11 = ""; |FINSI;
    #0#48 = "INACTIVA"; |SI efPn11 [ "01.01.1900"; efPn11 = ""; #0#48 = "ACTIVA"; |FINSI;
    #0#33 = nPin1;
    #0#34 = aPin1;
    #0#39 = efPn10;
    #0#40 = efPn11;
    aAlfa1 = #0#14; aAlfa1 = "01.01." + aAlfa1; #0#49 = aAlfa1;
    aAlfa1 = #0#15; aAlfa1 = "31.12." + aAlfa1; #0#50 = aAlfa1;
|FPRC;

|PROCESO LeeElAny;
  |PDEFECTO #0, 41,47;
  #0#41 = nElAny;
  enSwLinea = 0;
  |PRINT;
  nSw190 = 0;
  nSw180 = 0;
  nSw193 = 0;
  nSw194 = 0;
  nSw296 = 0;
  nSw390 = 0;
|FPRC;

|| .....................................................................
|PROCESO TotalAny;
 enSwLinea = 2;
 |PRINT;
|FPRC;

|| .....................................................................
|PROCESO Leem024;
 |SI swPrModelo = 0;
     #0#46 = #0#46 + #24#8;   || el Primer Dato del A¤o
     swPrModelo = 1;
 |SINO;
     #0#46 = #0#46 + #24#13;
 |FINSI;
|FPRC;

|DEFBUCLE Leem024;
 #24#1;
 ;
 #23#0, #23#1, 00, #23#2, 00;
 #23#0, #23#1, 99, #23#2, 99;
 e;
 NULL, Leem024;
|FIN;

|PROCESO Imprimir0;

 |SI #0#16 = 0;
     swBo = 0;
     |PARA nNume1 = 18; |SI nNume1 [ 29; |HACIENDO nNume1 = nNume1 + 1;
           |SI #0nNume1 = #23#2;
               swBo = 1; |SAL;
           |FINSI;
     |SIGUE;
     |SI swBo = 0; |ACABA; |FINSI;
 |FINSI;

 |SI swPrEmpresa = 0;
     swPrEmpresa = 1;
     nLaEmpresa = #23#0;
     |HAZ LeeLaEmpresa;
 |FINSI;

 |SI nLaEmpresa ! #23#0;
     |SI swAlgun = 1;
         |HAZ TotalAny;
         |PIEINF;
     |FINSI;
     nLaEmpresa = #23#0;
     |HAZ LeeLaEmpresa;
 |FINSI;

 |SI swPrAny = 0;
     swPrAny = 1;
     nElAny = #23#1;
     |HAZ LeeElAny;
 |FINSI;

 |SI nElAny ! #23#1;
     |HAZ TotalAny;
     nElAny = #23#1;
     |HAZ LeeElAny;
 |FINSI;

 |PDEFECTO #0, 43,47;
  #0#43 = #23#2;   ||Modelo

  |ABRE #99;
  #99#0 = #0#43;
  |LEE 040#99=;
  |SI FSalida ! 0;  |DDEFECTO #99;  |FINSI;
  |CIERRA #99;
  #0#44 = #99#1;

  swPrModelo = 0;
  |BUCLE Leem024;

  enSwLinea = 1;
  |PRINT;
  swAlgun = 1;

  |SI #0#43 = 110; |O #0#43 = 111; |O #0#43 = 130; |O #0#43 = 132; nSw190 = 1; |FINSI;
  |SI #0#43 = 115;                 nSw180 = 1; |FINSI;
  |SI #0#43 = 123;                 nSw193 = 1; |FINSI;
  |SI #0#43 = 124;                 nSw194 = 1; |FINSI;
  |SI #0#43 = 216;                 nSw296 = 1; |FINSI;
  |SI #0#43 ] 300; |Y #0#43 [ 339; nSw390 = 1; |FINSI;

  |SI #0#43 = 190; |Y nSw190 = 1; |ACABA; |FINSI;
  |SI #0#43 = 180; |Y nSw180 = 1; |ACABA; |FINSI;
  |SI #0#43 = 193; |Y nSw193 = 1; |ACABA; |FINSI;
  |SI #0#43 = 194; |Y nSw194 = 1; |ACABA; |FINSI;
  |SI #0#43 = 296; |Y nSw296 = 1; |ACABA; |FINSI;
  |SI #0#43 = 390; |Y nSw390 = 1; |ACABA; |FINSI;
  |SI #0#43 = 392; |Y nSw390 = 1; |ACABA; |FINSI;
  #0#42 = #0#42 + #0#46;
|FPRC;

|PROCESO Imprimir1;
  |SI swAlgun = 1;
      |HAZ TotalAny;
      |PIEINF;
  |FINSI;
  swAlgun = 0;
|FPRC;

|| .....................................................................
|DEFBUCLE Imprimir;
    #23#1;
    ;
    nDEmpresa, #0#14, #0#16;   ||Empresa A¤o, Modelo
    nHEmpresa, #0#15, #0#17;
    e;
    NULL, Imprimir0, NULL, NULL, Imprimir1;
|FIN;

|| \\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\
|PROCESO ITipo3;  |TIPO 3;

 |SI enSwMvo = 0;
     |IMPRE 0;
     |SI FSalida ! 0; |ACABA; |FINSI;
 |FINSI;

 |INFOR "dsmoy006";
 |SI FSalida ! 0; |FINIMP; |ACABA; |FINSI;

 enDatos = 0; |SI #0#47 = "S"; enDatos = 1; |FINSI;

 swPrEmpresa = 0;
 swPrModelo  = 0;
 |SI #0#0 ! 0;
         nDEmpresa = #0#0;
         nHEmpresa = #0#1;
         |BUCLE Imprimir;
 |SINO;
         |PARA nCampo = 2;  |SI nCampo [ 13;  |HACIENDO nCampo = nCampo + 1;
               nDEmpresa = #0nCampo;
               nHEmpresa = #0nCampo;
               |BUCLE Imprimir;         || Imprimir Valor
         |SIGUE;
 |FINSI;
 |FININF;

 |SI enSwMvo = 0;
     |FINIMP;
 |FINSI;
|FPRC;

|| =========================================================================
|PROGRAMA;

  |SI enSwMvo = 1;
      enEmpresa = enEmprM;
      enEjer    = enEjerM;
  |FINSI;

 |SI enEmpresa ! 0;
     #0#0 = enEmpresa;
     #0#1 = enEmpresa;
     #0#14 = enEjer; |SI enSwMvo = 1; #0#14 = 1997; |FINSI;
     #0#15 = enEjer;
     |HAZ ITipo3;
 |SINO;
     |CLS;
     |MANTE #0;
 |FINSI;
|FPRO;
