|FICHEROS;
    dsmoy067 #0;

   :/basica/def/agicen14 #114;
   :/basica/def/agifigen #118;
   :/basica/def/agim0019 #119;

   :/contagen/def/canempre #300;
   :/contagen/def/caivaasi #301;
   :/contagen/def/carbum01 #304;
   :/contagen/def/caconimp #306;  || configuracion impresiones

   dsmow030 #900;          ||Def de impresion
   dsmow043 #999,MANTE;
|FIN;

|VARIABLES;
   &entrada    = 1;
   informe     = "";
   aFichero    = "";
   nCampo      = 0;
   nDEmpresa   = 0;
   nHEmpresa   = 0;
   nDActividad = 0;
   nHActividad = 0;
   nLaEmpresa  = 0;
   nGTempo     = 0;

   &efFechaEmi = @;

   nSPin1      = 0;
   aPath       = "";
   nEjercicio  = 0;

   aAlfa0 = "";
   aAlfa1 = "";
   aAlfa2 = "";
   aAlfa3 = "";
   nPara1 = 0;
   nPara2 = 0;
   nPara3 = 0;
   nNume1 = 0;
   nNume2 = 0;
   nNume3 = 0;
   nNume4 = 0;

   aTexto     = "";
   nRompe     = 0;
   nCompara   = 0;
   nMes       = 0;
   SwHayDatos = 0;

   fDFecha   = @;
   fHFecha   = @;
   fLaFecha  = @;

   fDFechaV  = @;
   fHFechaV  = @;
   fDFechaC  = @;
   fHFechaC  = @;
   nInkey    = 0;

   &nFactura = 0;
   &nPagina  = 0;
   &SwPrim   = 0;
   &SwLinea  = 0;

   nEstado  = 0;
   &nPagDup   = 0;
   &nSwPagDup = 0;
   aClave     = "";

   &eaImpresora = "";
   &eaPortada   = "";
|FIN;

|INCLUYE i_variar;
|INCLUYE i_varcon;
|INCLUYE i_varlib;

|| //////////////////// Calculos de Pantalla \\\\\\\\\\\\\\\\\\\\\\\\\\
|CALCULO Eano; |TIPO 7;
 |SI nEstado ! 0;
     fLaFecha = ~;
     aAlfa1 = fLaFecha; aAlfa1 = aAlfa1 % -104;
     #0#1  = aAlfa1; |PINTA #0#1;
 |FINSI;
|FCAL;

|CALCULO losDefectos; |TIPO 0;
 aAlfa1 = #0#1;
 aAlfa2 = #0#41; aAlfa2 = aAlfa2 % -104;
 |SI aAlfa1 ! aAlfa2; |O nEstado ! 0;
     aAlfa2 = "01.01." + aAlfa1; #0#41 = aAlfa2; |PINTA #0#41;
     aAlfa2 = "31.12." + aAlfa1; #0#42 = aAlfa2; |PINTA #0#42;
 |FINSI;
|FCAL;

|CALCULO Tipo7;  |TIPO 7;
     |SI enEmpresa ! 0;
         #0#2  = enEmpresa;  |PINTA #0#2;
         #0#3  = enEmpresa;  |PINTA #0#3;

         |C_M #0#2, 1, "N";
         |C_M #0#3, 1, "N";
     |FINSI;
|FCAL;

|CALCULO Parrilla; |TIPO 0;
     |SI #0#2 = 0;
         #0#2 = 0;  |PINTA #0#2;
         #0#3 = 0;  |PINTA #0#3;  |C_M #0#3, 1, "N";
         |PARA nCampo = 4;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |SINO;
         |SI enEmpresa = 0;
             |C_M #0#2, 1, "S";
             |C_M #0#3, 1, "S";
         |FINSI;

         |PARA nCampo = 4;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "N";
               #0nCampo = 0;  |PINTA #0nCampo;
         |SIGUE;
     |FINSI;
|FCAL;

|CALCULO NoMasEmpresas; |TIPO 0;
     |SI #0Campo = 0;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
               #0nCampo = 0;  |C_M #0nCampo, 1, "N";
         |SIGUE;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |FINSI;
|FCAL;

|CALCULO YaSeCual; |TIPO 7;
 |SI enEmpresa ! 0;
     #0#24 = "S"; |PINTA #0#24;
     #0#52 = "S";
     #0#25 = 00;  |PINTA #0#25;
     #0#26 = 99;  |PINTA #0#26;
     |C_M #0#24, 1, "N";
     |C_M #0#25, 1, "N";
     |C_M #0#26, 1, "N";
     |C_M #0#52, 1, "N";
 |FINSI;
|FCAL;

|CALCULO SelTipo0; |TIPO 0;
 |SI #0#52 = "N";
     #0#25 = 0;   |PINTA #0#25;  |C_M #0#25, 1, "N";
     #0#26 = 99;  |PINTA #0#26;  |C_M #0#26, 1, "N";
     |PARA nCampo = 27;  |SI nCampo [ 36;  |HACIENDO nCampo = nCampo + 1;
           |C_M #0nCampo, 1, "S";
     |SIGUE;
 |SINO;
     |SI enEmpresa = 0;
         |C_M #0#25, 1, "S";
         |C_M #0#26, 1, "S";
     |FINSI;
     |PARA nCampo = 27;  |SI nCampo [ 36;  |HACIENDO nCampo = nCampo + 1;
           |C_M #0nCampo, 1, "N";
           #0nCampo = 0;  |PINTA #0nCampo;
     |SIGUE;
 |FINSI;
|FCAL;

|CALCULO SelTipo1;
 |SI #0Campo = 0;
     |PARA nCampo = Campo + 1;  |SI nCampo [ 36;  |HACIENDO nCampo = nCampo + 1;
           #0nCampo = 0;  |C_M #0nCampo, 1, "N";
     |SIGUE;
 |SINO;
     |PARA nCampo = Campo + 1;  |SI nCampo [ 36;  |HACIENDO nCampo = nCampo + 1;
           |C_M #0nCampo, 1, "S";
     |SIGUE;
 |FINSI;
|FCAL;

|CALCULO fecha; |TIPO 0;
 aAlfa1 = #0#1;
 aAlfa2 = "01.01." + aAlfa1;
 aAlfa3 = "31.12." + aAlfa1;
 |SI #0Campo < aAlfa2; |O #0Campo > aAlfa3;
     |MENAV "    Error. Fecha Incorrecta ";
     |ERROR;
 |SINO;
     |SI Campo = 42; |Y #0#41 > #0#42;
         |MENAV "    Error. Fecha Incorrecta ";
         |ERROR;
     |FINSI;
 |FINSI;
|FCAL;

|CALCULO Ordeno; |TIPO 0;
 |C_M #0#43, 1, "S";
 |SI #0#44 = 2;
    |C_M #0#43, 1, "N";
    #0#43 = "A"; |PINTA #0#43;
 |FINSI;
|FCAL;
|| **********************************************************************************
|| **********************************************************************************
|| ....................... Situa la Empresa Contable
|PROCESO LEmpresaConta;

 enEmpresa  = #canempre#2;
 enPerConta = #canempre#3;;   || Periodo Contable

 |SI #0#24 = "N"; |Y #canempre#21 = "I"; |ACABA; |FINSI;

|| ... Seleccion de Tipo de Empresa
  |ABRE #agifigen;
  #agifigen#0 = enEmpresa;
  |LEE 041#agifigen.=;
  |SI FSalida ! 0;
      |DDEFECTO #agifigen;
  |FINSI;
  |CIERRA #agifigen;
  nSPin1 = #agifigen#87;

  enSwSelEmp = 1;
  |SI #0#52 = "S";
      |SI nSPin1 ] #0#25; |Y nSPin1 [ #0#26;
          enSwSelEmp = 0;
      |FINSI;
  |SINO;
      |SI nSPin1 = #0#27;
          enSwSelEmp = 0;
      |SINO;
          |PARA nCampo = 28;  |SI nCampo [ 36;  |HACIENDO nCampo = nCampo + 1;
                |SI nSPin1 = #0nCampo; |Y nSPin1 ! 0;
                    enSwSelEmp = 0;
                    |SAL;
                |FINSI;
          |SIGUE;
      |FINSI;
  |FINSI;
  |SI enSwSelEmp = 1; |ACABA; |FINSI;
|| ------------------------------------------------------------------------
   aAlfa1 = enEmpresa;  aAlfa1 = ("00000" + aAlfa1) % -105;
   aAlfa2 = enPerConta; aAlfa2 = ("0" + aAlfa2) % -101;
   aPathConta = aPath + "contagen/fich/" + aAlfa1 + aAlfa2 + "/";

   nGTempo = nGTempo + 1;
   #999#0 = nGTempo;       || Guia
   #999#1 = enEmpresa;     || Empresa
   #999#2 = enPerConta;    || Periodo Contable
   #999#3 = enEjer;        || A¤o
   #999#4 = #canempre#11;  || Desde Mes
   #999#5 = #canempre#13;  || N§ Digitos
   #999#6 = #canempre#14;  || Digitos Nivel 1
   #999#7 = #canempre#15;  || Digitos Nivel 2
   #999#8 = #canempre#16;  || Digitos Nivel 3
   #999#9 = #canempre#17;  || Digitos Nivel 4
   #999#10 = #canempre#18; || Digitos Nivel 5
   #999#11 = #canempre#19; || Digitos Nivel 6
   eaEstadoEmp = "S"; |SI #canempre#21 = "I"; eaEstadoEmp = "N"; |FINSI;
   #999#12 = eaEstadoEmp;  || Estado
   #999#13 = #canempre#28; || Moneda
   #999#14 = #canempre#1;  || Nombre Empresa
   #999#15 = aPathConta;   || Path
   #999#16 = #0#48;        || Emitir
   |GRABA 020#999n;
|FPRC;

|DEFBUCLE canempre;
 #300#4;
 ;
 nDEmpresa, nEjercicio;
 nHEmpresa, nEjercicio;
 ;
 NULL, LEmpresaConta;
|FIN;

||///////////////////////////////////////////////////////////////////
|CALCULO Datos7; |TIPO 7;
 |MENSA "    <F3> Cambiar Emitir Libro S/N";
 |ATRI R; |PINTA 1169, "Imp"; |ATRI 0;
|FCAL;

|CALCULO DatosManip; |TIPO 11;
   nInkey = FSalida;
   |BLIN 04;
   |SI nInkey = 11;
       |SI #999#16 = "S";
           #999#16 = "N";
       |SINO;
           #999#16 = "S";
       |FINSI;
       |GRABA 020#999a;
   |FINSI;
|FCAL;

|RUTINA inferior;
   #999#0 = 00001;
|FRUT;

|RUTINA superior;
   #999#0 = 99999;
|FRUT;

||///////////////////////////////////////////////////////////////////
|CALCULO Tipo3;  |TIPO 3;
    enModelo   = #0#49;
    efFechaEmi = #0#45;
    |HAZ LeeCtrlLibro;

    enEjer = #0#1;
    aAlfa1 = enEjer; |QBF aAlfa1;
    aAlfa1 = aAlfa1 % -102;
    nEjercicio = aAlfa1;
    |DBASS aPath; |QBT aPath;

    aFichero = ("4w" + Usuario) % 108;
    |NOME_DAT #999 aFichero;
    |DELINDEX #999;
    eaFEmpresa = aFichero;

    |ABRE #999;
    nGTempo = 0;   ||Busco Empresas Contables
    |SI #0#2 ! 0;
        nDEmpresa = #0#2;
        nHEmpresa = #0#3;
        |BUCLE canempre;
    |SINO;
        |PARA nPara3 = 4;  |SI nPara3 [ 23;  |HACIENDO nPara3 = nPara3 + 1;
              nDEmpresa = #0nPara3;
              nHEmpresa = #0nPara3;
              |BUCLE canempre;
        |SIGUE;
    |FINSI;
    |CIERRA #999;

    |SI nGTempo = 0;    || No existe ninguna empresa
        |MENAV "    Error. No existe ninguna Empresa Contable dentro de la Seleccion ";
        |DELINDEX #999;
        |ACABA;
    |FINSI;

    |C_V #999#12,1,"N"; |C_P #999#16,1,1270;
    |PUSHV 0509 2375;
    |ENTLINEAL #1#999, -1, 2, 22, 1, inferior, superior;
    |POPV;

    |HAZ Imprimir;

    |DELINDEX #999;
|FCAL;

|| *************************************************************************
|PROCESO ElTexto;
       |SI #0#43 = "T";
           aTexto = nRompe + "§ Trimestre ...........................";
       |SINO;
           |SI nRompe =  1; aTexto = "Enero ................................."; |FINSI;
           |SI nRompe =  2; aTexto = "Febrero ..............................."; |FINSI;
           |SI nRompe =  3; aTexto = "Marzo ................................."; |FINSI;
           |SI nRompe =  4; aTexto = "Abril ................................."; |FINSI;
           |SI nRompe =  5; aTexto = "Mayo .................................."; |FINSI;
           |SI nRompe =  6; aTexto = "Junio ................................."; |FINSI;
           |SI nRompe =  7; aTexto = "Julio ................................."; |FINSI;
           |SI nRompe =  8; aTexto = "Agosto ................................"; |FINSI;
           |SI nRompe =  9; aTexto = "Septiembre ............................"; |FINSI;
           |SI nRompe = 10; aTexto = "Octubre ..............................."; |FINSI;
           |SI nRompe = 11; aTexto = "Noviembre ............................."; |FINSI;
           |SI nRompe = 12; aTexto = "Diciembre ............................."; |FINSI;
       |FINSI;
       #0#65 = "Suma y Sigue " + aTexto;
|FPRC;

|PROCESO Facturas;

   |SI #304#8 < fDFecha; |O #304#8 > fHFecha;
       |SI #304#15 [ "01.01.0000"; |ACABA; |FINSI;
       |SI #304#15 < fDFecha; |ACABA; |FINSI;
       |SI #304#15 > fHFecha; |ACABA; |FINSI;
   |FINSI;

   |SI #304#15 > "01.01.0000"; |Y #0#43 ! "A";   ||Totales parciales

       aAlfa1   = #304#15;
       aAlfa1   = aAlfa1 % 0402;
       nMes     = aAlfa1;
       nCompara = nMes;
       |SI #0#43 = "T";
           |SI nMes = 1; |O nMes =  2; |O nMes =  3; nCompara = 1; |FINSI;
           |SI nMes = 4; |O nMes =  5; |O nMes =  6; nCompara = 2; |FINSI;
           |SI nMes = 7; |O nMes =  8; |O nMes =  9; nCompara = 3; |FINSI;
           |SI nMes =10; |O nMes = 11; |O nMes = 12; nCompara = 4; |FINSI;
       |FINSI;

       |SI SwPrim = 0;
           nRompe = nCompara;
           |HAZ ElTexto;
       |FINSI;
       |SI nRompe ! nCompara;
           #0#65 = "Total " + aTexto;
           SwLinea = 1; |PRINT;
           #0#63   = 0;
           nRompe = nCompara; |HAZ ElTexto;
       |FINSI;
   |SINO;
       nRompe = 0;
   |FINSI;

   #0#53 = #0#53 + 1;
   |PDEFECTO #0, 54, 63;
   #0#54 = #304#1;
   |SI #0#46 = "S";  #0#54 = #0#53;  |FINSI;
   aAlfa1 = #0#54; |QBF aAlfa1;
   aAlfa1 = ("000000" + aAlfa1) % -106;
   #0#54  = aAlfa1;
   #0#55  = #304#2;
   #0#56  = #304#8;
   #0#57  = #304#9;
   #0#58  = #304#10;
   #0#59  = #304#15;
   #0#60  = #304#16;
   #0#61  = #304#17;
   #0#62  = #304#19;

   SwLinea = 0; |PRINT;

   SwHayDatos = 1;
   SwPrim     = 1;
   #0#63 = #0#63 + #0#62;
   #0#64 = #0#64 + #0#62;
|FPRC;

|DEFBUCLE Facturas1;
 #304#3;
 ;
 enEmpresa, fDFechaV, fDFechaC, 000001;
 enEmpresa, fHFechaV, fHFechaC, 999999;
 ;
 NULL, Facturas;
|FIN;

|DEFBUCLE Facturas2;
 #304#2;
 ;
 enEmpresa, fDFechaC, fDFechaV, 000001;
 enEmpresa, fHFechaC, fHFechaV, 999999;
 ;
 NULL, Facturas;
|FIN;

|PROCESO Emision;
   enEmpresa  = #999#1;
   enPerConta = #999#2;
   eaLaMonEmp = #999#13;
   |HAZ EnConversor;

   enEjer     = #999#3;   || A¤o
   dig1       = #999#4;   || Desde Mes
   dig3       = #999#5;   || N§ Digitos
   dig4       = #999#6;   || Digitos Nivel 1
   dig5       = #999#7;   || Digitos Nivel 2
   dig6       = #999#8;   || Digitos Nivel 3
   dig7       = #999#9;   || Digitos Nivel 4
   dig8       = #999#10;  || Digitos Nivel 5
   dig9       = #999#11;  || Digitos Nivel 6
   eaEstadoEmp = #999#12; || Estado
   eaNombreEmp = #999#14; || Nombre Empresa
   aPathConta  = #999#15; |QBF aPathConta; || Path
   || Recoger informacion sobre los niveles ...
   |SI dig3 = 1; MaximoDigitos = dig4; |FINSI;
   |SI dig3 = 2; MaximoDigitos = dig5; |FINSI;
   |SI dig3 = 3; MaximoDigitos = dig6; |FINSI;
   |SI dig3 = 4; MaximoDigitos = dig7; |FINSI;
   |SI dig3 = 5; MaximoDigitos = dig8; |FINSI;
   |SI dig3 = 6; MaximoDigitos = dig9; |FINSI;

   |PATH_DAT #301 aPathConta;
   |PATH_DAT #306 aPathConta;

   |ABRE #301;
   |DDEFECTO #301;
   |LEE 000#301p;
   |CIERRA #301;

   |DDEFECTO #306;  ||Configuracion de Informes
   |ABRE #306;
   #306#0 = 1;
   |LEE 000#306=;
   |CIERRA #306;

   eaPortada = "N";
   eaTituloL = #0#51; ||Titulo del Libro

   aAlfa0 = Impresora % 113;
   |SI #0#37 = "S"; |O aAlfa0 = "CrystalReport";
       eaImpresora = Impresora;
       |SI aAlfa0 = "CrystalReport"; eaImpresora = aAlfa0; |FINSI;
       |SUB_EJECUTA_NP ":basica/agiy0016";
       eaImpresora = "";
       |SI aAlfa0 = "CrystalReport"; |HAZ PortadaCrystal; |FINSI;
       eaPortada = #0#37;
   |FINSI;

   informe = "dsmoy067";
   |SI #306#4 = "N"; informe = "drmoy067"; |FINSI;

   enActividad = 0;
   |HAZ LeeDatosEmpresa;
   |SI  swNoExis = 0;       ||SI existe esta Actividad.

        |INFOR informe;
        |SI FSalida ! 0;  |ACABA;  |FINSI;

        |PDEFECTO #0, 53, 66;
        SwHayDatos = 0;
        SwPrim     = 0;
        nRompe     = 0;

        fDFecha = #0#41;
        fHFecha = #0#42;
        nPagina = #0#50;
        nPagDup = nPagina - 1;
        nSwPagDup = 0;

        |SI #0#38 = "V";

            fDFechaV = fDFecha;      fHFechaV = fHFecha;
            fDFechaC = "01.01.0000"; fHFechaC = "31.12.2099";

            |SI #0#44 = 1;  |BUCLE Facturas1;  |FINSI;
            |SI #0#44 = 2;  |BUCLE Facturas2;  |FINSI;

        |SINO;

            |SI #0#44 = 1;
                fDFechaV = fDFecha;      fHFechaV = fHFecha;
                fDFechaC = "01.01.0000"; fHFechaC = "31.12.2099";
                |BUCLE Facturas1;
                |SI #0#43 ! "A"; |Y  nRompe ! 0;
                    #0#65 = "Total " + aTexto;
                    SwLinea = 1; |PRINT;
                    #0#63  = 0;
                    aTexto = "";
                    nRompe = 0;
                |FINSI;

                fDFechaV = "01.01.0000"; fHFechaV = "01.01.0000";
                fDFechaC = fDFecha;      fHFechaC = fHFecha;
                |BUCLE Facturas1;
            |FINSI;

            |SI #0#44 = 2;
                fDFechaV = "01.01.0000"; fHFechaV = "31.12.2099";
                fDFechaC = "01.01.0000"; fHFechaC = "31.12.2099";
                |BUCLE Facturas2;
            |FINSI;

        |FINSI;

        |SI SwHayDatos = 1;
            |SI #0#43 ! "A"; |Y nRompe ! 0;
                #0#65 = "Total " + aTexto;
                SwLinea = 1; |PRINT;
            |FINSI;
            |PIEINF;
        |FINSI;
        |FININF;
   |FINSI;
|FPRC;

|DEFBUCLE dsmow043;
 #dsmow043#1;
 16;
 000001, "S";
 999999, "S";
 ;
 NULL, Emision;
|FIN;

|PROCESO Imprimir;
    |MENAV "0000 Recuerde seleccionar el papel de la impresora en Horizontal";

    |IMPRE 0;
    |SI FSalida ! 0;  |ACABA;  |FINSI;

    |BUCLE dsmow043;
    |FINIMP;
|FPRC;

|PROCESO LeeDatosEmpresa;
  |DDEFECTO #900;
  eaInac = #0#24;
  eaTituloL = #0#51;
  efDFecha  = #0#41;
  efHFecha  = #0#42;
  |HAZ DatosEmpr_w30;
|FPRC;

|PROGRAMA;
  aClave = "A";
  |DDEFECTO #0;

  |ABRE #0;
  #0#0 = aClave;
  |LEE 101#0=;
  |SI FSalida ! 0;
      |DDEFECTO #0;
      #0#0  = aClave;
      |GRABA 020#0b;
      nEstado = 1;
  |FINSI;

      |CLS;
      |PINPA #0#0;
      |PINDA #0#0;

      |ENDATOS #1#0;
      |SI FSalida = 0;
          #0#0 = aClave;
          |GRABA 020#0a;
      |FINSI;
      |LIBERA #0;
      |CIERRA #0;
|FPRO;
