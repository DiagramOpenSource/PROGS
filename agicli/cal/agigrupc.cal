|FICHEROS;
    agigrupl #0;
    agitrabd #1;
    agigrupc #2;
|FIN;

|VARIABLES;
    sino  = "";
    grupo = 0;
    tope  = 0;
    linea = 0;
    modo  = 0;
    campo9  = 0;
    campo10 = "";
    fiac = "  ";
    guarda = "  ";
    relleno = "                                       ";
    aponer = "  ";
|FIN;

|PROCESO vacio; |TIPO 0;
campo9  = 0;
campo10 = "";
|FPRC;

|PROCESO pantalla; |TIPO 0;
|SI FSalida ! 9; |ACABA; |FINSI;

|PUSHV 0501 2380;
|CUADRO 0856 1577;

|ATRI R;
|PINTA 0957, " 01 al 12 Meses     ";
|PINTA 1057, " 13.- Mensual       ";
|PINTA 1157, " 14.- Bimensual     ";
|PINTA 1257, " 15.- Trimestral    ";
|PINTA 1357, " 16.- Cuatrimestral ";
|PINTA 1457, " 17.- Semestral     ";
|ATRI r;

|PAUSA;
|POPV;

|ERROR;
|FPRC;

|PROCESO filtros; |TIPO 0;
tope = 31;

|SI #0#7 = 2;  tope = 29; |FINSI;
|SI #0#7 = 4;  tope = 30; |FINSI;
|SI #0#7 = 6;  tope = 30; |FINSI;
|SI #0#7 = 9;  tope = 30; |FINSI;
|SI #0#7 = 11; tope = 30; |FINSI;

|SI tope < #0#8;
    |MENAV "      El dia no coincide con los dias del mes introducido.";
    |ERROR;
|FINSI;
|FPRC;

|PROCESO importe; |TIPO 7;
sino = #0#11;
|CAMPO_MODIFICA #0#12, 1, sino;

|SI sino = "N"; #0#12 = 0; |PINTA #0#12; |FINSI;
|FPRC;

|PROCESO graba_nueva;
|ET lee_otro;
  #0#0 = #2#0;
  #0#1 = linea;
  |LIBERA #0;
  |LEE 140#0=;
  |SI FSalida = 0;
      linea = linea + 1;
      |VETE lee_otro;
  |FINSI;

  |DDEFECTO #0;
  #0#0  = #2#0;
  #0#1  = linea;
  #0#2  = #1#0;
  #0#3  = #1#2;
  #0#4  = "";
  #0#5  = "";
  #0#6  = "";
  #0#7  = #1#6;
  |SI #0#7 = 0; #0#7 = 1; |FINSI;
  #0#8  = #1#7;
  #0#9  = campo9;
  #0#10 = campo10;
  #0#11 = #1#3;
  #0#12 = 0;
  #0#13 = #1#1;
  |GRABA 140#0n;
|FPRC;

|DEFBUCLE agitrabd;
#1#1;
;
#0#2, 001;
#0#2, 999;
;
NULL, graba_nueva;
|FIN;

|PROCESO linea_0; |TIPO 0;
modo = (FEntrada / 100) ! 0;
|SI #0#13 = 0; |Y modo ! 1;
    |MENAV "     Solo en Modo Alta.";
    |ERROR;
    |ACABA;
|FINSI;

|SI #0#13 ! 0;
    |ABRE #1;
    #1#0 = #0#2;
    #1#1 = #0#13;
    |LEE 040#1=;
    |SI FSalida ! 0;
        |MENAV "     No existe el trabajo.";
        |ERROR;
    |FINSI;
    |CIERRA #1;

    #0#7 = #1#6; |PINTA #0#7;
    #0#8 = #1#7; |PINTA #0#8;
|SINO;
    grupo = #0#0;
    linea = #0#1;
    |BUCLE agitrabd;

    #0#0 = grupo;
    #0#1 = linea;
    |LEE 040#0=;

    aponer = "kksi";
    |HAZ poncontr;
    Pos = -1;

    |PTEC 809;
    |PTEC 809;
    |PTEC 809;
    |PTEC 808;
    |PTEC 808;
    |PTEC 806;
|FINSI;
|FPRC;

|PROCESO poncontr;                 || calculo 'guarda'
    fiac = Control % A109;
    fiac = fiac + relleno;
    guarda = fiac % A109;
    fiac = Control % A1001;
    Control = Control + fiac;
    Control = Control + relleno;
    Control = Control % A120;
    Control = Control + aponer;
|FPRC;

|PROCESO d_campo9; |TIPO 7;
|SI modo ! 1; |ACABA; |FINSI;
#0#9 = campo9; |PINTA #0#9;
|FPRC;

|PROCESO h_campo9; |TIPO 0;
|SI modo ! 1; |ACABA; |FINSI;
campo9 = #0#9;
|FPRC;

|PROCESO d_campo10; |TIPO 7;
|SI modo ! 1; |ACABA; |FINSI;
#0#10 = campo10; |PINTA #0#10;
|FPRC;

|PROCESO h_campo10; |TIPO 0;
|SI modo ! 1; |ACABA; |FINSI;
campo10 = #0#10;
|FPRC;
