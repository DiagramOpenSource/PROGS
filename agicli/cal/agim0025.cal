|FICHEROS;
    agim0025;
    agim0037;

    agim0058;
    agim0016;

    imagen@;
|FIN;

|VARIABLES;
      Modo1     = 0;
     aNifQbf1   = "";
     aNifQbf2   = "";

     nInkey     = 0;
     nCampo     = 0;
     nCampo1    = 0;
     nCampo2    = 0;
     nCampo3    = 0;
     nNume1     = 0;
     aAlfa      = "";
     aAlfa1     = "";
     nExiste    = 0;
     fHoy       = @;
     nDia1      = 0;
     nDia2      = 0;
     nMes1      = 0;
     nMes2      = 0;
     nAny1      = 0;
     nAny2      = 0;
     nEdad      = 0;

     aPath      = "";
     aMas       = "";
     nTPC       = 0;
     nTotalTPC  = 0;
     nLinea     = 0;
     aNif       = "";

     &Cliente   = 0;
     &Registro  = 0;
     &Modo      = 0;
     &Programa  = "";
     &TipoLinea = 0;

     &eaVarDni  = "";
     &enCalcCif = 0;

     &enRefer25 = 0;
     &enModo    = 0;
     &eaCamp4   = "";
     &eaCamp5   = "";
     &eaCamp6   = "";
     &eaCamp7   = "";
     &eaCamp8   = "";
     &eaCamp9   = "";
     &eaCamp10  = "";
     &eaCamp11  = "";
     &eaCamp12  = "";
     &enCamp13  = 0;
|FIN;

|PROCESO LeeImagen;
   |SI #imagen@#2 ! nLinea;
       nTPC = nTPC + #imagen@#13;
   |FINSI;
|FPRC;

|DEFBUCLE LeeImagen;
   #imagen@#2004;
   ;
   Cliente, Registro, aNif, 001;
   Cliente, Registro, aNif, 999;
   e;
   NULL, LeeImagen;
|FIN;

|PROCESO BuscoIguales;
   nTPC      = 0;
   nTotalTPC = 0;
   aNif      = #agim0025#3;
   nLinea    = #agim0025#2;
   |BUCLE LeeImagen;
|FPRC;

|PROCESO Tipo7;  |TIPO 7;
   |BLIN 4;

   |HAZ BuscoIguales;
   nTotalTPC = nTPC + #agim0025#13;

   |SI #agim0025#12 = "J"; |Y nTotalTPC > 25;
       |MENSA "0000<F2> Consulta Titularidad real";
   |FINSI;
|FPRC;

|PROCESO LetrasDni25; |TIPO 6;
     |SI FSalida = 9;
         eaVarDni   = #agim0025#Campo#;
         enCalcCif  = 1;
         |HAZ CalculoDNI;
         #agim0025#Campo# = eaVarDni;
         |PINTA #agim0025#Campo#;
         |ERROR;
         |ACABA;
     |FINSI;

     eaVarDni  = #agim0025#Campo#;
     enCalcCif = 0;
     |HAZ CalculoDNI;

     aNifQbf1 = #agim0025#Campo#;   |QBF aNifQbf1;
     aNifQbf2 = eaVarDni;  |QBF aNifQbf2;

     |SI aNifQbf1 ! aNifQbf2; |Y aNifQbf1 ! "";
         |CONFI "0000SEl Nif puede ser Erroneo. Desea Calcularlo.";
         |SI FSalida = 0;
             #agim0025#Campo# = eaVarDni;  |PINTA #agim0025#Campo#;
             |ERROR;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO Titularidad;
    |SI FSalida = 2; |ACABA; |FINSI;

    |HAZ BuscoIguales;
    nTotalTPC = nTPC + #agim0025#13;

    |SI nTotalTPC [ 25; |Y nExiste = 1; |Y #agim0025#12 = "J";
        |CONFI "0000NPorcentaje inferior o igual a 25%, se borraran datos Titularidad real. Continuar";
        |SI FSalida ! 0;
            |ERROR;
            |ACABA;
        |FINSI;
    |FINSI;

    |SI nTotalTPC > 25; |Y #agim0025#12 = "J";
         enModo    = 2;
         |HAZ AltaAuxiliar;
     |FINSI;
|FPRC;

|PROCESO Parrilla;

     |SI FSalida = 2; |ACABA; |FINSI;

     Cliente   = #agim0025#0;
     Registro  = #agim0025#1;
     Programa  = "agim0025";
     TipoLinea = #agim0025#2;

     |SUB_EJECUTA_NP "agim0037";

     |ABRE #agim0037;
     #agim0037#0 = Programa;
     #agim0037#1 = Cliente;
     #agim0037#2 = Registro;
     #agim0037#3 = TipoLinea;
     #agim0037#4 = 1;
     |LEE 040#agim0037.];
     |SI FSalida ! 0; |O #agim0037#0 ! Programa; |O #agim0037#1 ! Cliente;
                      |O #agim0037#2 ! Registro; |O #agim0037#3 ! TipoLinea;
        |DDEFECTO #agim0037;
     |FINSI;

     #agim0025#15 = #agim0037#6;   |PINTA #agim0025#15;
     #agim0025#16 = #agim0037#7;   |PINTA #agim0025#16;
|FPRC;

|PROCESO LeePaism25; |TIPO 6;
     |ABRE #agim0016;
     #agim0016#0 = #agim0025#Campo#;
     |LEE 041#agim0016.=;
     |SI FSalida = 0;
         |SI Campo = 23; nCampo = 22; |FINSI;
         |SI Campo = 24; nCampo = 26; |FINSI;
         #agim0025#nCampo# = #agim0016#1; |PINTA #agim0025#nCampo#;
     |FINSI;
     |CIERRA #agim0016;
|FPRC;

|PROCESO MiraDatosAux; |TIPO 0;
  |BLIN 4;

  nExiste = 0;
  |ABRE #agim0058;
  #agim0058#0 = #agim0025#0;
  #agim0058#1 = #agim0025#1;
  #agim0058#2 = #agim0025#2;
  #agim0058#3 = 01;
  |LEE 041#agim0058.=;
  |SI FSalida = 0;
      nExiste = 1;
  |FINSI;
  |CIERRA #agim0058;
|FPRC;

|PROCESO AltaAuxiliar;
        enRefer25 = #agim0025#2;
        eaCamp4   = #agim0025#12;
        eaCamp5   = #agim0025#3;
        eaCamp6   = #agim0025#4;
        aAlfa  = #agim0025#5; |QBF aAlfa;
        aAlfa1 = #agim0025#6; |QBF aAlfa1;
        |SI aAlfa1 ! "";
            |SI aAlfa ! ""; aAlfa = aAlfa + ", "; |FINSI;
            aAlfa = aAlfa + aAlfa1;
        |FINSI;
        aAlfa1 = #agim0025#9; |QBF aAlfa1;
        |SI aAlfa1 ! ""; aAlfa = aAlfa + " " + aAlfa1; |FINSI;
        aAlfa1 = #agim0025#10; |QBF aAlfa1;
        |SI aAlfa1 ! ""; aAlfa = aAlfa + " (" + aAlfa1 + ")"; |FINSI;
        eaCamp7   = aAlfa;
        eaCamp8   = #agim0025#23;
        eaCamp9   = #agim0025#22;
        eaCamp10  = #agim0025#24;
        eaCamp11  = #agim0025#26;
        eaCamp12  = #agim0025#25;
        enCamp13  = #agim0025#13;
        |SI enModo = 3; enCamp13 = 0; |FINSI;

        |PUSHV 1001 2380;
        |SUB_EJECUTA_NP "agim0058";
        |POPV;
|FPRC;

|PROCESO QuePersona; |TIPO 0;
    |SI #agim0025#12 = "F"; |Y nExiste = 1;
        |CONFI "0000NPers.Fisica, se borraran datos Titularidad real. Continuar";
        |SI FSalida ! 0;
            #agim0025#12 = "J"; |PINTA #agim0025#12;
            |ERROR;
            |ACABA;
        |FINSI;
    |FINSI;

    aAlfa = "S";
    |SI #agim0025#12 = "J";
        aAlfa = "N";
        #agim0025#25 = "01.01.0000"; |PINTA #agim0025#25;
    |FINSI;
    |C_M #agim0025#25, 1, aAlfa;

|FPRC;

|PROCESO Tipo0FecNac; |TIPO 0;
     fHoy = ~;
     |SI #agim0025#25 > fHoy;    || nacimientos futuros como que no
          |HAZ PonSiglo20;       ||/se cambia sin preguntar
     |FINSI;

     |SI #agim0025#25 ] "01.01.2001"; |Y #agim0025#25 [ fHoy;  || presuntos ni¤os,
                                                                       ||/se pregunta
         |HAZ CumpleAnyos;
         |SI nEdad < 18;
             aAlfa = "0000SLa fecha puede ser incorrecta. Ha introducido un contribuyente menor de edad. Corregir fecha?";
             |CONFI aAlfa;
             |SI FSalida = 0;
                 |ERROR;
                 || |HAZ PonSiglo20;
             |FINSI;
         |FINSI;
     |FINSI;

|FPRC;

|PROCESO CumpleAnyos;
     aAlfa1 = fHoy;         aAlfa1 = aAlfa1 % -104; nAny1 = aAlfa1;
     aAlfa1 = #agim0025#25; aAlfa1 = aAlfa1 % -104; nAny2 = aAlfa1;
     nEdad  = nAny1 - nAny2;

     |SI nEdad ! 18; |ACABA; |FINSI;

     aAlfa1 = fHoy;         aAlfa1 = aAlfa1 % 402; nMes1 = aAlfa1;
     aAlfa1 = #agim0025#25; aAlfa1 = aAlfa1 % 402; nMes2 = aAlfa1;

     |SI nMes2 > nMes1;  nEdad = nEdad - 1; |FINSI;
     |SI nMes1 ! nMes2;  |ACABA;            |FINSI;

     aAlfa1 = fHoy;         aAlfa1 = aAlfa1 % 102; nDia1 = aAlfa1;
     aAlfa1 = #agim0025#25; aAlfa1 = aAlfa1 % 102; nDia2 = aAlfa1;

     |SI nDia2 > nDia1;  nEdad = nEdad - 1; |FINSI;
|FPRC;

|PROCESO PonSiglo20;
     aAlfa1    = (#agim0025#25 % 106) + "19" + (#agim0025#25 % -102);
     #agim0025#25 = aAlfa1;
     |PINTA #agim0025#25;
|FPRC;

|PROCESO Borram058;
  nExiste = 1;
  |BORRA 020#agim0058.a;
  |LIBERA #agim0058;
|FPRC;

|DEFBUCLE Borram058;
  #agim0058#1;
  ;
  #agim0025#0, #agim0025#1, #agim0025#2, 01;
  #agim0025#0, #agim0025#1, #agim0025#2, 99;
  be;
  NULL, Borram058;
|FIN;

|PROCESO Borram037;
  |BORRA 020#agim0037.a;
  |LIBERA #agim0037;
|FPRC;

|DEFBUCLE Borram037;
  #agim0037#1;
  ;
  Programa, #agim0025#0, #agim0025#1, #agim0025#2, 01;
  Programa, #agim0025#0, #agim0025#1, #agim0025#2, 99;
  be;
  NULL, Borram037;
|FIN;


|PROCESO Tipo2;  |TIPO 2;
     Modo1 = (FEntrada / 100) ! 0;
     |SI Modo1 ! 3;  |ACABA;  |FINSI;

     |BUCLE Borram037;

     nExiste = 0;
     |BUCLE Borram058;
     |SI nExiste = 1;
         enModo = 3;
         |HAZ AltaAuxiliar;
     |FINSI;
     nTPC = 0;
|FPRC;

|PROCESO Tipo11;  |TIPO 11;
     nInkey = FSalida;

     nTotalTPC = nTPC + #agim0025#13;
     |SI nInkey = 10;
         |SI #agim0025#12 = "J"; |Y nTotalTPC > 25;
             enModo = 4;
             |HAZ AltaAuxiliar;
         |FINSI;
         |ERROR;
     |FINSI;
|FPRC;

|PROCESO Tipo3; |TIPO 3;
     nTotalTPC = nTPC + #agim0025#13;
     |SI #agim0025#12 ! "J"; |O nTotalTPC [ 25;
         nExiste = 0;
         |BUCLE Borram058;
         |SI nExiste = 1;
             enModo = 3;
             |HAZ AltaAuxiliar;
         |FINSI;
     |FINSI;

     nTPC = 0;
|FPRC;


|RUTINA ClaveBaja0;
  #agim0025#0 = Cliente;
  #agim0025#1 = Registro;
  #agim0025#2 = 1;
|FRUT;

|RUTINA ClaveAlta0;
  #agim0025#0 = Cliente;
  #agim0025#1 = Registro;
  #agim0025#2 = 99;
|FRUT;

|PROGRAMA;

  |DBASE aPath;
  aMas = aPath + "def/agim0025.mas";
  |CARGA_DEF aMas, imagen@;
  |SI FSalida ! 0;
      |MENAV  "         Error en cargar el Def ... Proceso Abortado.";
      |ACABA;
  |FINSI;

  |SI Modo ! 4;
      |ENTLINEAL #1#agim0025, -3, 2, 22, 1, ClaveBaja0, ClaveAlta0;
  |SINO;
      |ENTLINEAL #1#agim0025, -3, 4, 22, 1, ClaveBaja0, ClaveAlta0;
  |FINSI;

  |DESCARGA_DEF #imagen@;
|FPRO;
