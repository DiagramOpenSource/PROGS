|FICHEROS;
   agiz0001 #0;
   agiagenl #1;
   agiw0010 #2;

   agitrabc #3;
   agitrabl #4;

   agiusuar #90;
   agifigen #91;
|FIN;

|VARIABLES;
     pepe    = "²";
     horizo  = "";
     xxx     = 0;
     yyy     = 0;
     usuar   = "";
     clien   = 0;
     d_clien = 0;
     h_clien = 0;
     salida  = 0;
     fecha_d = @;
     fecha_h = @;
     linea   = 0;
     sw_graba = 0;
|FIN;

|INCLUYE i_fiche1;

|PROGRAMA;
|ET arriba;
    |CLS;
    |CABEZA "Asignacion Trabajos";
    horizo = pepe * 80;
    |PARA xxx = 5; |SI xxx [ 23; |HACIENDO xxx = xxx + 1;
          yyy = (xxx * 100) + 1;
          |PINTA yyy, horizo;
    |SIGUE;

    |CUADRO 1114 1666;
    |PINTA 1215, " Usuario :                                         ";
    |PINTA 1315, "                                                   ";
    |PINTA 1415, " Cliente :                                         ";
    |PINTA 1515, "                                                   ";

|ET prime;
      |BLIN 04;
      E_sup = 10;
      E_inf = "";

      usuar = 1226 ? 1;
      |SI FSalida = 1; |O FSalida = 7; |ACABA; |FINSI;

      |ABRE #90;
      #90#0 = usuar;
      |LEE 040#90=;
      |SI FSalida ! 0;
          |MENAV "     No existe Usuario";
          |CIERRA #90;
          |VETE prime;
      |FINSI;
      |CIERRA #90;
      |ATRI I; |PINTA 1326, #90#1; |ATRI i;

|ET segon;
      |MENSA "       <00000> Todos los Clientes";
      E_inf = 00000;
      E_sup = 99999;

      clien = 1426 ? 1;
      salida = FSalida;

      |SI clien ! 0;
          |ABRE #91;
          #91#0 = clien;
          |LEE 040#91=;
          |SI FSalida ! 0;
              |MENAV "     No existe Cliente";
              |CIERRA #91;
              |VETE segon;
          |FINSI;
          |CIERRA #91;
          |ATRI I; |PINTA 1526, #91#1; |ATRI i;
      |SINO;
          |ATRI I; |PINTA 1526, "TODOS LOS CLIENTES"; |ATRI i;
      |FINSI;

      |SI salida = 1; |O salida = 7; |ACABA; |FINSI;
      |SI salida = 2; |VETE prime;           |FINSI;

|CONFI "2400SEs Correcto : ";
|SI FSalida ! 0; |VETE arriba; |FINSI;

|HAZ al_rollo;
|FPRO;

|PROCESO graba_ind;
linea = linea + 1;
#0#0  = linea;
#0#1  = #1#5;
#0#2  = #1#0;
#0#3  = #1#3;
#0#4  = #1#4;
#0#5  = #1#6;
#0#6  = #1#7;
#0#7  = #1#8;
#0#8  = #1#9;
#0#9  = #1#10;
#0#10 = "N";
#0#11 = #1#14;

|GRABA 040#0n;
|FPRC;

|DEFBUCLE agiagenl;
#1#1;
11;
fecha_d, usuar, 00, 00, d_clien, "N";
fecha_h, usuar, 99, 99, h_clien, "N";
be;
NULL, graba_ind;
|FIN;

|RUTINA infer_0;
#0#0 = 1;
|FRUT;

|RUTINA super_0;
#0#0 = linea;
|FRUT;

|PROCESO al_rollo;
|BLIN 04;

fecha_d = "01.01.0000";
fecha_h = "31.12.2999";

d_clien = clien;
h_clien = clien;

|SI clien = 0; d_clien = 0; h_clien = 99999; |FINSI;

comienza_1 = "01";
|HAZ fichero_1;
|NOME_DAT #0 fichero_1;

|ABRE #0; |CIERRA #0; |DELINDEX #0;

|ABRE #0;
|BUCLE agiagenl;
|CIERRA #0;

|SI linea = 0; |ACABA; |FINSI;

|PINPA #0#0;
|ENTLINEAL #1#0, 1, 4, 20, 0, infer_0, super_0;

|DELINDEX #0;
|FPRC;

|RUTINA infer_2;
#2#0 = 1;
|FRUT;

|RUTINA super_2;
#2#0 = 99;
|FRUT;

|PROCESO que_linea;
linea = #4#1;
|FPRC;

|DEFBUCLE agitrabl;
#4#1;
;
#0#1, 0000;
#0#1, 9999;
;
NULL, que_linea;
|FIN;

|PROCESO pasalo;
|SI sw_graba = 0;
    sw_graba = 1;
    linea    = 0;
    |BUCLE agitrabl;
    |SI linea = 0;
        |ABRE #3;
        #3#0 = #0#1;
        #3#1 = #0#5;
        |GRABA 040#3n;
        |CIERRA #3;
    |FINSI;
|FINSI;

|ABRE #90;
#90#0 = #2#10;
|LEE 040#90=;
|SI FSalida ! 0; |DDEFECTO #90; |FINSI;
|CIERRA #90;

|ABRE #4;
linea = linea + 1;
#4#0  = #0#1;
#4#1  = linea;
#4#2  = #0#2;
#4#3  = #0#3;
#4#4  = #0#4;
#4#5  = #2#1;
#4#6  = #2#2;
#4#7  = #2#3;
#4#8  = #2#4;
#4#9  = #2#5;
#4#10 = #2#13;
#4#11 = #90#1;
#4#12 = #2#6;
#4#13 = #2#7;
#4#14 = #2#8;
#4#15 = #2#9;
#4#16 = #2#10;
#4#17 = "N";
#4#18 = "N";
#4#19 = #2#11;
#4#20 = #2#12;
#4#21 = #0#11;
#4#22 = #2#14;
|GRABA 040#4n;
|CIERRA #4;
|FPRC;

|DEFBUCLE agiw0010;
#2#1;
;
00;
99;
;
NULL, pasalo;
|FIN;

|PROCESO asignar; |TIPO 11;
|SI FSalida ! 9; |ACABA; |FINSI;
|SI #0#10 = "S"; |ACABA; |FINSI;

|CONFI "2400SDesea asignar la orden marcada : ";
|SI FSalida = 0;
    |LIBERA #0;
    |LEE 140#0=;

    |PUSHV 0501 2380;
    comienza_1 = "02";
    |HAZ fichero_1;
    |NOME_DAT #2 fichero_1;

    |ABRE #2; |CIERRA #2; |DELINDEX #2;

    |PINPA #0#2;
    |ENTLINEAL #1#2, 1, 1, 21, 0, infer_2, super_2;

    sw_graba = 0;
    |BUCLE agiw0010;
    |SI sw_graba = 1;
        #0#10 = "S";
        |GRABA 140#0a;

        |ABRE #1;
        |SELECT #2#1;
        #1#14 = #0#11;
        #1#2  = usuar;
        |LIBERA #1;
        |LEE 140#1=;
        |SI FSalida = 0;
            #1#11 = "S";
            |GRABA 140#1a;
        |FINSI;
        |CIERRA #1;
    |FINSI;

    |DELINDEX #2;
    |POPV;
    |PINTA #0#10;
|FINSI;
|FPRC;
