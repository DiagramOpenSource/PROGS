|FICHEROS;
     agifigen #3;         || Clientes
     agiseg09 #9;         || Cabeceras Remesas
     agiseg10 #10;        || Lineas    Remesas
     agiseg11 #08,MANTE;  || Importes Modelos Presentados
|FIN;

|VARIABLES;
    Modo       = 0;
    Forma      = 0;
    Referencia = 0;
|FIN;

|PROCESO NoAlta; |TIPO 7;
Modo = (FEntrada / 100) ! 0;
|SI Modo = 1;
    |MENAV "     No se permite la Alta.Use programa de Adjudicacion.";
    |PTEC 807;
|FINSI;
|FPRC;

|PROCESO Tipo2; |TIPO 2;
Modo = (FEntrada / 100) ! 0;
|SI Modo = 1;
    |MENAV "      Use forma correcta de dar de Alta.";
    |ERROR;
    |ACABA;
|FINSI;

Forma = 0 +. 1;

#9#7 = #9#7 +. #10#10;
|PINTA #9#7;

|ABRE #8;
#8#0 = #10#2;                          || Cliente
#8#1 = #10#3;                          || Mes
#8#2 = #10#4;                          || Ejercicio
#8#3 = #10#5;                          || Tipo
#8#4 = #10#6;                          || Modelo
|LIBERA #8;
|LEE 140#8=;
|SI FSalida = 0;
    |SI Forma = 1;
        #8#8  = #10#0;                 || Codigo Remesa
        #8#9  = #10#1;                 || Linea Remesa
        #8#10 = #9#1;                  || Fecha Remesa
    |SINO;
        #8#8  = 0;                     || Codigo Remesa
        #8#9  = 0;                     || Linea Remesa
        #8#10 = "01.01.0000";          || Fecha Remesa
    |FINSI;
    |GRABA 140#8a;
|FINSI;
|CIERRA #8;
|FPRC;

|RUTINA ClaveBaja8;
#8#0 = 00000;
#8#1 = 00;
#8#2 = 00;
#8#3 = "  ";
#8#4 = "    ";
#8#5 = #9#2;
#8#8 = 0;
|FRUT;

|RUTINA ClaveAlta8;
#8#0 = 99999;
#8#1 = 99;
#8#2 = 99;
#8#3 = "¤¤";
#8#4 = "¤¤¤¤";
#8#5 = #9#2;
#8#8 = 0;
|FRUT;

|PROCESO Tipo11; |TIPO 11;
|SI FSalida ! 9; |ACABA; |FINSI;

|PUSHV 0501 2380;
|ENTLINEAL #2#8, 5, 4, 22, 1, ClaveBaja8, ClaveAlta8;
|POPV;
|PINTA #9#7;
|ERROR;
|FPRC;

|PROCESO PulsaF1; |TIPO 11;
|SI FSalida ! 9; |ACABA; |FINSI;

|ABRE #3;
#3#0 = #8#0;
|LEE 040#3=;
|SI FSalida ! 0; |DDEFECTO #3; |FINSI;
|CIERRA #3;

|HAZ LeeUltima;

#10#00 = #9#0;              || Remesa
#10#01 = Referencia;        || Linea Remesa
#10#02 = #8#0;              || Cliente
#10#03 = #8#1;              || Mees
#10#04 = #8#2;              || Ejercicio
#10#05 = #8#3;              || Tipo
#10#06 = #8#4;              || Modelo
#10#08 = #3#1;              || Nombre Cliente
#10#09 = #3#84;             || Cuenta Corriente
#10#10 = #8#6;              || Importe

|SI #10#06 = "110 "; #10#07 = "MODELO OFICIAL DEL 110"; |FINSI;
|SI #10#06 = "130 "; #10#07 = "MODELO OFICIAL DEL 130"; |FINSI;
|SI #10#06 = "131 "; #10#07 = "MODELO OFICIAL DEL 131"; |FINSI;
|SI #10#06 = "300 "; #10#07 = "MODELO OFICIAL DEL 300"; |FINSI;
|SI #10#06 = "310 "; #10#07 = "MODELO OFICIAL DEL 310"; |FINSI;
|SI #10#06 = "330 "; #10#07 = "MODELO OFICIAL DEL 330"; |FINSI;
|SI #10#06 = "370 "; #10#07 = "MODELO OFICIAL DEL 370"; |FINSI;
|SI #10#06 = "TC1 ";|O #10#06 = "tc1 "; #10#07 = "MODELO TC1";   |FINSI;
|SI #10#06 = "FLC ";|O #10#06 = "flc "; #10#07 = "MODELO FLC";   |FINSI;
|SI #10#06 = "PMEN";|O #10#06 = "pmen"; #10#07 = "MODELO PMEN2"; |FINSI;

|GRABA 140#10n;

|ABRE #8;
#8#0 = #10#2;                          || Cliente
#8#1 = #10#3;                          || Mes
#8#2 = #10#4;                          || Ejercicio
#8#3 = #10#5;                          || Tipo
#8#4 = #10#6;                          || Modelo
|LIBERA #8;
|LEE 140#8=;
|SI FSalida = 0;
    #8#8  = #10#0;                 || Codigo Remesa
    #8#9  = #10#1;                 || Linea Remesa
    #8#10 = #9#1;                  || Fecha Remesa
    |GRABA 140#8a;
|FINSI;
|CIERRA #8;

#9#7 = #9#7 + #10#10;

|PTEC 808;
|PTEC 808;
|PTEC 807;
|FPRC;

|PROCESO LeeUltima;
Referencia = 0;
#10#0 = #9#0;
#10#1 = 999;
|LEE 000#10];
|SI FSalida = 0;
    |LEE 000#10a;
|SINO;
    |LEE 000#10u;
|FINSI;

|SI FSalida = 0; |Y #10#0 = #9#0;
        Referencia = #10#1 + 1;
|SINO;
        Referencia = 1;
|FINSI;
|FPRC;
