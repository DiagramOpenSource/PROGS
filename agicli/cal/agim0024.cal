|FICHEROS;
    agim0024 #0;
    agim0004 #1;

    agim0016 #16;
|FIN;

|VARIABLES;
     &Cliente  = 0;
     &Registro = 0;
     &Modo     = 0;

     nCampo    = 0;
     aAlfa     = "";
     aAlfa1    = "";
     fHoy      = @;
     nDia1     = 0;
     nDia2     = 0;
     nMes1     = 0;
     nMes2     = 0;
     nAny1     = 0;
     nAny2     = 0;
     nEdad     = 0;

     aNifQbf1   = "";
     aNifQbf2   = "";

     &eaVarDni  = "";
     &enCalcCif = 0;
|FIN;

|PROCESO LetrasDni24; |TIPO 6;
     |SI FSalida = 9;
         eaVarDni   = #0Campo;
         enCalcCif  = 1;
         |HAZ CalculoDNI;
         #0Campo = eaVarDni;
         |PINTA #0Campo;
         |ERROR;
         |ACABA;
     |FINSI;

     eaVarDni  = #0Campo;
     enCalcCif = 0;
     |HAZ CalculoDNI;

     aNifQbf1 = #0Campo;   |QBF aNifQbf1;
     aNifQbf2 = eaVarDni;  |QBF aNifQbf2;

     |SI aNifQbf1 ! aNifQbf2; |Y aNifQbf1 ! "";
         |CONFI "0000SEl Nif puede ser Erroneo. Desea Calcularlo.";
         |SI FSalida = 0;
             #0Campo = eaVarDni;  |PINTA #0Campo;
             |ERROR;
         |FINSI;
     |FINSI;
|FPRC;

|RUTINA ClaveBaja0;
  #0#0 = Cliente;
  #0#1 = Registro;
  #0#2 = 1;
|FRUT;

|RUTINA ClaveAlta0;
  #0#0 = Cliente;
  #0#1 = Registro;
  #0#2 = 99;
|FRUT;

|PROCESO GrabaRepre;
  |DDEFECTO #0;
  #0#0  = #1#0;
  #0#1  = Registro;
  #0#2  = #1#1;
  #0#3  = #1#2;
  #0#4  = #1#3;
  #0#5  = #1#4;
  #0#6  = #1#5;
  #0#7  = #1#6;
  #0#8  = #1#7;
  #0#9  = #1#8;
  #0#10 = #1#9;
  #0#11 = #1#10;
  #0#12 = #1#11;
  #0#13 = #1#12;

  |GRABA 140#0n;
  |LIBERA #0;
|FPRC;

|DEFBUCLE agim0004;
  #1#1;
  ;
  Cliente, 01;
  Cliente, 99;
  ;
  NULL, GrabaRepre;
|FIN;

|PROCESO Tipo7m0024;  |TIPO 7;
     |SI #0#17 = "00.00.0000";
         |ATRI I; |PINTA 1357, "INDEFINIDO";
     |FINSI;
|FPRC;

|PROCESO LeePaism24; |TIPO 6;
     |ABRE #16;
     #16#0 = #0Campo;
     |LEE 041#16=;
     |SI FSalida = 0;
         |SI Campo = 21; nCampo = 22; |FINSI;
         |SI Campo = 23; nCampo = 25; |FINSI;
         #0nCampo = #16#1; |PINTA #0nCampo;
     |FINSI;
     |CIERRA #16;
|FPRC;

|PROCESO QuePersona; |TIPO 0;
    aAlfa = "S";
    |SI #0#14 = "J";
        aAlfa = "N";
        #0#24 = "01.01.0000"; |PINTA #0#24;
    |FINSI;
    |C_M #0#24, 1, aAlfa;
|FPRC;

|PROCESO Tipo0FecNac; |TIPO 0;
     fHoy = ~;
     |SI #0#24 > fHoy;    || nacimientos futuros como que no
          |HAZ PonSiglo20;       ||/se cambia sin preguntar
     |FINSI;

     |SI #0#24 ] "01.01.2001"; |Y #0#24 [ fHoy;  || presuntos ni¤os,
                                                                       ||/se pregunta
         |HAZ CumpleAnyos;
         |SI nEdad < 18;
             aAlfa = "0000SLa fecha puede ser incorrecta. Ha introducido un contribuyente menor de edad. Corregir fecha?";
             |CONFI aAlfa;
             |SI FSalida = 0;
                 |ERROR;
                 || |HAZ PonSiglo20;
             |FINSI;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO CumpleAnyos;
     aAlfa1 = fHoy;  aAlfa1 = aAlfa1 % -104; nAny1 = aAlfa1;
     aAlfa1 = #0#24; aAlfa1 = aAlfa1 % -104; nAny2 = aAlfa1;
     nEdad  = nAny1 - nAny2;

     |SI nEdad ! 18; |ACABA; |FINSI;

     aAlfa1 = fHoy;  aAlfa1 = aAlfa1 % 402; nMes1 = aAlfa1;
     aAlfa1 = #0#24; aAlfa1 = aAlfa1 % 402; nMes2 = aAlfa1;

     |SI nMes2 > nMes1;  nEdad = nEdad - 1; |FINSI;
     |SI nMes1 ! nMes2;  |ACABA;            |FINSI;

     aAlfa1 = fHoy;  aAlfa1 = aAlfa1 % 102; nDia1 = aAlfa1;
     aAlfa1 = #0#24; aAlfa1 = aAlfa1 % 102; nDia2 = aAlfa1;

     |SI nDia2 > nDia1;  nEdad = nEdad - 1; |FINSI;
|FPRC;

|PROCESO PonSiglo20;
     aAlfa1    = (#0#24 % 106) + "19" + (#0#24 % -102);
     #0#24 = aAlfa1;
     |PINTA #0#24;
|FPRC;

|PROGRAMA;
  |ABRE #0;
  #0#0 = Cliente;
  #0#1 = Registro;
  #0#2 = 1;
  |LEE 040#0];
  |SI FSalida ! 0; |O #0#0 ! Cliente; |O #0#1 ! Registro;
      |BUCLE agim0004;
  |FINSI;
  |CIERRA #0;

  |SI Modo ! 4;
      |ENTLINEAL #1#0, -3, 2, 21, 1, ClaveBaja0, ClaveAlta0;
  |SINO;
      |ENTLINEAL #1#0, -3, 4, 21, 1, ClaveBaja0, ClaveAlta0;
  |FINSI;
|FPRO;
