|FICHEROS;
     agiz0007 #0;              || Adjudicacion
     agibanco #1;              || Bancos
     agifigen #3;              || Clientes
     agiseg08 #8;              || I.Modelos Presentados
     agiseg09 #9;              || Cabeceras Remesas
     agiseg10 #10;             || Lineas    Remesas
     agiw0019 #99,MANTE;       || Temporal
|FIN;

|VARIABLES;
     Referencia = 0;
     EjerAlfa   = "";
     VImporte   = 0;
     Banco      = 0;
|FIN;

|INCLUYE i_ficher;

|PROCESO Tipo8; |TIPO 8;
|ABRET #9;
|FPRC;

|PROCESO Tipo9; |TIPO 9;
|SI Referencia = 0; |BORRA 140#9a; |FINSI;

|CIERRAT #9;
|FPRC;

|PROCESO NumeroRemesa; |TIPO 7;
|LEE 040#9u;
|SI FSalida ! 0;
    |DDEFECTO #9;
    #0#0 = 1;
|SINO;
    #0#0 = #9#0 + 1;
|FINSI;
|PINTA #0#0;

#9#0 = #0#0;
|GRABA 140#9b;
|FPRC;

|PROCESO Mayus; |TIPO 0;
#0Campo = #0Campo > #0Campo;
|PINTA #0Campo;
|FPRC;

|PROCESO Seleccion; |TIPO 0;
EjerAlfa = #0#1 % -102;
#0#9     = EjerAlfa; |PINTA #0#9;

|SI #0#2 = "B";
    |C_M #0#3, 1, "S";
    |C_M #0#5, 1, "N"; #0#5 = ""; |PINTA #0#5; #0#6 = ""; |PINTA #0#6;
|SINO;
    |C_M #0#5, 1, "S";
    |C_M #0#3, 1, "N"; #0#3 = 0;  |PINTA #0#3; #0#4 = ""; |PINTA #0#4;
|FINSI;
|FPRC;

|PROCESO Banco; |TIPO 0;
|SI #0#2 = "E"; |ACABA; |FINSI;

|ABRE #1;
|SELECT #1#1;
#1#0 = #0#3;
|LEE 040#1=;
|SI FSalida = 0;
    #0#4 = #1#1;  |PINTA #0#4;
|SINO;
    |MENAV "     No existe Banco";
    |ERROR;
|FINSI;
|CIERRA #1;
|FPRC;

|PROCESO Entidad; |TIPO 0;
|SI #0#2 = "B"; |ACABA; |FINSI;

|ABRE #1;
|SELECT #3#1;
#1#2 = #0#5;
|LEE 040#1=;
|SI FSalida = 0;
    #0#6 = #1#1;  |PINTA #0#6;
|SINO;
    |MENAV "     No existe Entidad";
    |ERROR;
|FINSI;

|SELECT #1#1;
|CIERRA #1;
|FPRC;

|PROCESO GrabaIndex;
|SI #8#6 < 0; |ACABA; |FINSI;

#3#0 = #8#0;
|LEE 040#3=;
|SI FSalida ! 0; |DDEFECTO #3; |FINSI;

Referencia = Referencia + 1;
#99#00 = Referencia;         || Referencia
#99#01 = #8#0;               || Codigo Cliente
#99#02 = #8#1;               || Mes de Emision
#99#03 = #8#2;               || Ejercicio
#99#04 = #8#4;               || Modelo
#99#05 = #8#5;               || Banco
#99#06 = #8#6;               || Importe
#99#07 = #8#7;               || FEcha de Presentacion
#99#08 = #8#11;              || Entidad Bancaria
#99#09 = #0#10;              || Defecto de Adjudicar
#99#10 = #3#1;               || Razon Social
#99#11 = #8#3;               || Tipo
|GRABA 040#99n;
|FPRC;

|DEFBUCLE agiseg08B;
#8#2;
;
#0#3, 00000, #0#7, #0#9, "    ", 000000;
#0#3, 99999, #0#8, #0#9, "いい", 000000;
;
NULL, GrabaIndex;
|FIN;

|DEFBUCLE agiseg08E;
#8#3;
;
#0#5, 00000, #0#7, #0#9, "    ", 000000;
#0#5, 99999, #0#8, #0#9, "いい", 000000;
;
NULL, GrabaIndex;
|FIN;

|RUTINA ClaveBaja;
#99#0 = 1;
|FRUT;

|RUTINA ClaveAlta;
#99#0 = 9999;
|FRUT;

|PROCESO ARemesa;
Referencia = Referencia + 1;
Banco      = #99#5;

#3#0  = #99#1;
|LEE 040#3=;
|SI FSalida ! 0; |DDEFECTO #3; |FINSI;

#10#00 = #0#0;              || Numero Remesa
#10#01 = Referencia;        || Linea
#10#02 = #99#1;             || Codigo Cliente
#10#03 = #99#2;             || Mes Emision
#10#04 = #99#3;             || Ejercicio
#10#05 = #99#11;            || Tipo
#10#06 = #99#4;             || Modelo
#10#08 = #99#10;            || Nombre Cliente
#10#09 = #3#84;             || Cuenta Corriente
#10#10 = #99#6;             || Importe

|SI #10#06 = "110 "; #10#07 = "MODELO OFICIAL DEL 110"; |FINSI;
|SI #10#06 = "130 "; #10#07 = "MODELO OFICIAL DEL 130"; |FINSI;
|SI #10#06 = "131 "; #10#07 = "MODELO OFICIAL DEL 131"; |FINSI;
|SI #10#06 = "300 "; #10#07 = "MODELO OFICIAL DEL 300"; |FINSI;
|SI #10#06 = "310 "; #10#07 = "MODELO OFICIAL DEL 310"; |FINSI;
|SI #10#06 = "330 "; #10#07 = "MODELO OFICIAL DEL 330"; |FINSI;
|SI #10#06 = "370 "; #10#07 = "MODELO OFICIAL DEL 370"; |FINSI;
|SI #10#06 = "TC1 ";|O #10#06 = "tc1 "; #10#07 = "MODELO TC1";   |FINSI;
|SI #10#06 = "FLC ";|O #10#06 = "flc "; #10#07 = "MODELO FLC";   |FINSI;
|SI #10#06 = "PMEN";|O #10#06 = "pmen"; #10#07 = "MODELO PMEN2"; |FINSI;

|GRABA 140#10n;

VImporte = VImporte + #99#6;       || Total Importe

#8#0 = #10#2;                      || Cliente
#8#1 = #10#3;                      || Mes
#8#2 = #10#4;                      || Ejercicio
#8#3 = #10#5;                      || Tipo
#8#4 = #10#6;                      || Modelo
|LIBERA #8;
|LEE 140#8=;
|SI FSalida = 0;
    #8#8  = #10#0;                 || Codigo Remesa
    #8#9  = #10#1;                 || Linea Remesa
    #8#10 = #9#1;                  || Fecha Remesa
    |GRABA 140#8a;
|FINSI;
|FPRC;

|DEFBUCLE agiw0019;
#99#1;
9;
0000, "S";
9999, "S";
;
NULL, ARemesa;
|FIN;

|PROCESO Tipo3; |TIPO 3;
comienza = "19";
|HAZ fichero;
|NOME_DAT #99 fichero;
|ABRE #99; |CIERRA #99; |DELINDEX #99;

Referencia = 0;

|ABRE   #3;
|ABRE   #99;
|SI #0#2 = "B"; |BUCLE agiseg08B; |FINSI;
|SI #0#2 = "E"; |BUCLE agiseg08E; |FINSI;
|CIERRA #99;

|SI Referencia = 0; |CIERRA #3; |DELINDEX #99; |ACABA; |FINSI;

|ENTLINEAL #1#99, 1, 4, 21, 0, ClaveBaja, ClaveAlta;

Referencia = 0;
|CONFI "2400SConforme con los Seleccionado : ";
|SI FSalida ! 0; |CIERRA #3; |DELINDEX #99; |ACABA; |FINSI;

VImporte = 0;

|ABRE #8;
|ABRE #10;
|BUCLE agiw0019;
|CIERRA #3;
|CIERRA #8;
|CIERRA #10;

|ABRE #1;
#1#0 = Banco;
|LEE 040#1=;
|SI FSalida ! 0; |DDEFECTO #1; |FINSI;
|CIERRA #1;

#9#0 = #0#0;               || Numero de Remesa
#9#1 = #0#1;               || Fecha Remesa
#9#2 = #1#0;               || Banco
#9#3 = #1#1;               || Nombre del Banco
#9#4 = #1#2;               || Entidad
#9#5 = #1#3;               || Oficina
#9#6 = #1#4;               || Digito Control
#9#7 = VImporte;           || Importe
|GRABA 140#9a;
|LIBERA #9;

|DELINDEX #99;
|FPRC;

|PROCESO PulsaF1; |TIPO 11;
|SI FSalida ! 9; |ACABA; |FINSI;

|LIBERA #99;
|LEE 140#99=;
|SI #99#9 = "S";
    #99#9 = "N";
|SINO;
    #99#9 = "S";
|FINSI;
|PINTA #99#9;

|GRABA 140#99a;

|PTEC 809;
|FPRC;
