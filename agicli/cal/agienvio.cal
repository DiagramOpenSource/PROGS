|FICHEROS;
   agienvio #0;
   agiseccl #1;
   agiw0009 #2;
   agiagenc #3;
   agiagenl #4;
   agiusuar #5;
   agihenvi #6;
   agitipme #7;
   agifigen #8;
   agirefer #9;
|FIN;

|VARIABLES;
   sw_histo   = 0;
   refer      = 0;
   hora       = 0;
   minuto     = 0;
   salida     = 0;
   numero_men = 0;
   hora_real  = "";
   alfa       = "";
   xxx        = 0;
   tipo_l     = "";
|FIN;

|INCLUYE i_fiche1;
|INCLUYE i_usuari;

|RUTINA inferior;
#2#0 = 1;
|FRUT;

|RUTINA superior;
#2#0 = refer;
|FRUT;

|PROCESO carga_ind;
refer = refer + 1;
#2#0  = refer;
#2#1  = #1#2;
#2#2  = #1#3;
#2#3  = "S";
|GRABA 040#2n;
|FPRC;

|DEFBUCLE seseccil;
#1#1;
;
#0#1, 00;
#0#1, 99;
e;
NULL, carga_ind;
|FIN;

|PROCESO pasa_agend;
|SI sw_histo = 0;
    sw_histo = 1;
    |ERROR10;
    |ACABA;
|FINSI;

#3#0 = #0#0;
#3#1 = #0#1;
#3#2 = #2#1;
|LIBERA #3;
|LEE 140#3=;
|SI FSalida ! 0;
    #3#0 = #0#0;
    #3#1 = #0#1;
    #3#2 = #2#1;
    #3#3 = #0#2;
    |GRABA 140#3n;
|FINSI;

minuto = #0#4;
hora   = #0#3;

|ET otro;
    #4#0 = #0#0;
    #4#1 = #0#1;
    #4#2 = #2#1;
    #4#3 = hora;
    #4#4 = minuto;
    #4#5 = 0;
    |LIBERA #4;
    |LEE 140#4=;
    |SI FSalida = 0;
        minuto = minuto + 1;
        |SI minuto > 59;
            minuto = 00;
            hora   = hora + 1;
            |SI hora > 23; hora = 1; |FINSI;
        |FINSI;
        |VETE otro;
    |SINO;
        #4#0  = #0#0;
        #4#1  = #0#1;
        #4#2  = #2#1;
        #4#3  = hora;
        #4#4  = minuto;
        |SI #7#6 = "C";
            #4#5  = #0#12;
            |SI #4#5 ! 0; #4#6  = #0#13; |FINSI;
            |SI #4#5 = 0; #4#6  = #0#18; |FINSI;
        |FINSI;

        |SI #7#6 = "R";
            #4#5  = #0#19;
            |SI #4#5 ! 0; #4#6  = #0#20; |FINSI;
            |SI #4#5 = 0; #4#6  = #0#18; |FINSI;
        |FINSI;

        #4#7  = #0#5;
        #4#8  = #0#6;
        #4#9  = #0#7;
        #4#10 = #0#8;
        #4#11 = #0#9;
        #4#12 = #0#10;
        #4#13 = #0#11;
        #4#14 = numero_men;
        |GRABA 140#4n;
    |FINSI;
|FPRC;

|DEFBUCLE agiw0009;
#2#1;
3;
00, "S";
99, "S";
e;
NULL, pasa_agend;
|FIN;

|PROCESO tipo_3;
comienza_1 = "31";
|HAZ fichero_1;
|NOME_DAT #2 fichero_1;
|ABRE #2; |CIERRA #2; |DELINDEX #2;

|SI #7#2 = "D";
    refer = 0;
    |ABRE #2;
    |BUCLE seseccil;
    |CIERRA #2;

    |SI refer > 1;
        |PUSHV 0501 2380;
        |PINPA #0#2;
        |ENTLINEAL #1#2, 1, 4, 17, 0, inferior, superior;
        |POPV;
    |FINSI;
|SINO;
    |ABRE #2;
    #2#0  = 1;
    #2#1  = #0#21;
    #2#2  = #0#22;
    #2#3  = "S";
    |GRABA 040#2n;
    |CIERRA #2;
|FINSI;

sw_histo = 0;
|BUCLE agiw0009;
|SI sw_histo = 1;
    |HAZ historico;
    |ABRE #3;
    |ABRE #4;
    |BUCLE agiw0009;
    |CIERRA #3;
    |CIERRA #4;
|FINSI;

|ABRE #2; |CIERRA #2; |DELINDEX #2;
|FPRC;

|PROCESO historico;
|ABRE #6;
#6#14 = numero_men;
|LIBERA #6;
|LEE 140#6=;
|SI FSalida = 0;
    |PARA xxx = 0; |SI xxx [ 29; |HACIENDO xxx = xxx + 1;
          #6xxx = #0xxx;
    |SIGUE;
    |GRABA 140#6a;
|FINSI;
|CIERRA #6;
|FPRC;

|PROCESO pidetecl; |TIPO 11;
|SI FSalida < 9; |O FSalida > 10;
    |ACABA;
|FINSI;

|SI FSalida = 9;
    |LIBERA #2;
    |LEE 140#2=;

    #2#3 = "S";
    |PINTA #2#3;
    |GRABA 140#2a;
|FINSI;

|SI FSalida = 10;
    |LIBERA #2;
    |LEE 140#2=;

    #2#3 = "N";
    |PINTA #2#3;
    |GRABA 140#2a;
|FINSI;
|FPRC;

|PROGRAMA;
|ABRE #8;
#8#0 = 0;
|LIBERA #8;
|LEE 140#8=;
|SI FSalida ! 0;
    |DDEFECTO #8;
    #8#0 = 0;
    |GRABA 140#8b;
|FINSI;
|CIERRA #8;

|ABRE #9;
#9#0 = 0;
|LIBERA #9;
|LEE 140#9=;
|SI FSalida ! 0;
    |DDEFECTO #9;
    #9#0 = 0;
    |GRABA 140#9b;
|FINSI;
|CIERRA #9;

|CLS;
|CABEZA "Enviar Mensajes";
salida = 0;
|PARA; |SI salida = 0; |HACIENDO;
     |PINPA #0#0;
     |DDEFECTO #0;
     |HAZ numero_mens;
     |PINDA #0#0;
     |ENDATOS #1#0;
     salida = FSalida;
     tipo_l = #0#15;
     |SI FSalida = 0;
         |CONFI "2400SEnvio el Mensaje : ";
         |SI FSalida = 0;
             |HAZ tipo_3;
         |SINO;
             |ABRE #6; #6#14 = numero_men; |BORRA 140#6c; |LIBERA #6; |CIERRA #6;
         |FINSI;
     |SINO;
         |ABRE #6; #6#14 = numero_men; |BORRA 140#6c; |LIBERA #6; |CIERRA #6;
     |FINSI;
|SIGUE;
|FPRO;

|PROCESO numero_mens;
numero_men = 0;
|ABRE #6;
|LIBERA #6;
|LEE 140#6u;
|SI FSalida = 0; numero_men = #6#14; |FINSI;
numero_men = numero_men + 1;

|DDEFECTO #6;
#6#14 = numero_men;
|GRABA 140#6b;
|LIBERA #6;
|CIERRA #6;

|HAZ RecogeUsuario;
#0#17 = Usr;

|ABRE #5;
#5#0 = #0#17;
|LEE 040#5=;
|SI FSalida ! 0; |DDEFECTO #5; |FINSI;
|CIERRA #5;

#0#14 = numero_men;
#0#15 = tipo_l;
#0#18 = #5#1;
|FPRC;

|PROCESO hora; |TIPO 7;
|HORA hora_real;

alfa = hora_real % 102;
#0#3 = alfa; |PINTA #0#3;
alfa = hora_real % 402;
#0#4 = alfa; |PINTA #0#4;
|FPRC;

|PROCESO tipo; |TIPO 0;
|CAMPO_MODIFICA #0#12, 1, "S";
|CAMPO_MODIFICA #0#19, 1, "S";
|CAMPO_MODIFICA #0#1,  1, "S";
|CAMPO_MODIFICA #0#21, 1, "S";

|ABRE #7;
#7#0 = #0#15;
|LEE 040#7=;
|SI FSalida ! 0; |DDEFECTO #7; |FINSI;
|CIERRA #7;

|SI #7#2 = "U"; |CAMPO_MODIFICA #0#1,  1, "N"; #0#1  = ""; |FINSI;
|SI #7#2 = "D"; |CAMPO_MODIFICA #0#21, 1, "N"; #0#21 = ""; |FINSI;
|SI #7#6 = "C"; |CAMPO_MODIFICA #0#19, 1, "N"; #0#19 = 0;  |FINSI;
|SI #7#6 = "R"; |CAMPO_MODIFICA #0#12, 1, "N"; #0#12 = 0;  |FINSI;

#0#13 = "";
#0#20 = "";
#0#2  = "";
#0#22 = "";

|PINTA #0#1;
|PINTA #0#2;
|PINTA #0#12;
|PINTA #0#13;
|PINTA #0#19;
|PINTA #0#20;
|PINTA #0#21;
|PINTA #0#22;
|FPRC;

|PROCESO datos_cli; |TIPO 0;
|SI #7#6 = "C";
    |ABRE #8;
    #8#0 = #0#12;
    |LEE 040#8=;
    |SI FSalida ! 0; |DDEFECTO #8; |FINSI;
    |CIERRA #8;

    #0#13 = #8#1;   |PINTA #0#13;
    #0#23 = #8#14;  |PINTA #0#23;
    #0#24 = #8#15;  |PINTA #0#24;
    #0#25 = #8#4;   |PINTA #0#25;
    #0#26 = #8#5;   |PINTA #0#26;
    #0#27 = #8#9;   |PINTA #0#27;
    #0#28 = #8#10;  |PINTA #0#28;
    #0#29 = #8#11;  |PINTA #0#29;
|FINSI;

|SI #7#6 = "R";
    |ABRE #9;
    #9#0 = #0#19;
    |LEE 040#9=;
    |SI FSalida ! 0; |DDEFECTO #9; |FINSI;
    |CIERRA #9;

    #0#20 = #9#1;   |PINTA #0#20;
    #0#23 = #9#14;  |PINTA #0#23;
    #0#24 = #9#15;  |PINTA #0#24;
    #0#25 = #9#4;   |PINTA #0#25;
    #0#26 = #9#5;   |PINTA #0#26;
    #0#27 = #9#9;   |PINTA #0#27;
    #0#28 = #9#10;  |PINTA #0#28;
    #0#29 = #9#11;  |PINTA #0#29;
|FINSI;
|FPRC
