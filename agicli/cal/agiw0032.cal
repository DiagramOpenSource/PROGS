|FICHEROS;
     agiw0032 #0;
     agicen26 #26;
     agicen27 #27;
     agicen28 #28;
|FIN;

|VARIABLES;
     aAlfa        = "";
     aTipo1       = "";
     aTipo2       = "";

     nForma       = 0;
     nModo        = 0;
     nEmpresa     = 0;
     nRegistro    = 0;
     nDAct        = 0;
     nHAct        = 0;

     &enEmpresa   = 0;
     &enModo      = 0;
     &eaNombreEmp = "";
     &enActividad = 0;
     &enRegistro  = 0;
     &eaTipo      = "";
     &enOpcion    = 0;

 {-1} aMenu    = "";
      aMenu1   = "1209";
      aMenu2   = "1";
      aMenu3   = "7";
      aMenu4   = "Declaracion                                                      ";
      aMenu5   = "Representante                                                    ";
      aMenu6   = "Datos de la Actividad                                            ";
      aMenu7   = "Local afecto indirectamente a la actividad                       ";
      aMenu8   = "Elementos Tributarios                                            ";
      aMenu9   = "Relacion de locales (Anexo al modelo 840)                        ";
      aMenu10  = "Emision del Modelo en PDF                                        ";
|FIN;

|RUTINA ClaveBaja26;
     #26#0 = #0#0;
     #26#1 = #0#1;
     #26#2 = 1;
|FRUT;

|RUTINA ClaveAlta26;
     #26#0 = #0#0;
     #26#1 = #0#1;
     #26#2 = 99;
|FRUT;

|RUTINA ClaveBaja0;
     #0#0 = enEmpresa;
     #0#1 = 01;
|FRUT;

|RUTINA ClaveAlta0;
     #0#0 = enEmpresa;
     #0#1 = 99;
|FRUT;

|PROCESO agicen28G;
     #28#2 = #26#2;
     |GRABA 020#28n;
     |LIBERA #28;
|FPRC;

|DEFBUCLE agicen28G;
     #28#1;
     ;
     #27#0, #27#1, #27#2, 00;
     #27#0, #27#1, #27#2, 99;
     be;
     NULL, agicen28G;
|FIN;

|PROCESO RegistroAnterior;
     |ABRE #27;
     #27#0 = #26#0;
     #27#1 = #26#1;
     #27#2 = 99;
     |LEE 040#27];
     |SI FSalida = 0;
         |LEE 040#27a;
     |SINO;
         |LEE 040#27u;
     |FINSI;
     |SI FSalida = 0;  |Y #27#0 = #26#0;  |Y #27#1 = #26#1;
         |BUCLE agicen28G;

         #27#2 = #26#2;
         |GRABA 020#27n;
     |FINSI;
|FPRC;

|PROCESO agicen28B;
     |BORRA 020#28a;
     |LIBERA #28;
|FPRC;

|DEFBUCLE agicen28B;
     #28#1;
     ;
     #26#0, #26#1, #26#2, 00;
     #26#0, #26#1, #26#2, 99;
     be;
     NULL, agicen28B;
|FIN;

|PROCESO agicen26B;
     #27#0 = #26#0;
     #27#1 = #26#1;
     #27#2 = #26#2;
     |LEE 101#27=;
     |SI FSalida = 0;
         |BORRA 020#27a;
         |LIBERA #27;
     |FINSI;

     |BUCLE agicen28B;

     |BORRA 020#26a;
|FPRC;

|DEFBUCLE agicen26B;
     #26#1;
     ;
     #0#0, #0#1, 01;
     #0#0, #0#1, 99;
     be;
     NULL, agicen26B;
|FIN;

|PROCESO BorraFicheros;
     |ABRE #27;
     |BUCLE agicen26B;
     |CIERRA #27;

     |BORRA 020#0a;
|FPRC;

|DEFBUCLE agiw0032B;
     #0#1;
     ;
     enEmpresa, 01;
     enEmpresa, 99;
     be;
     NULL, BorraFicheros;
|FIN;

|PROCESO Tipo7Cen26;  |TIPO 7;
     #26#1 = #0#1;  |PINTA #26#1;
     #26#6 = #0#4;  |PINTA #26#6;
|FPRC;

|PROCESO Anterior;  |TIPO 7;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 1;  |Y #26#2 ! 1;
         |CONFI "0000SRecoger datos del Registro Anterior";
         |SI FSalida = 0;
             |HAZ RegistroAnterior;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO Tipo2Cen26;  |TIPO 2;
     nModo = (FEntrada / 100) ! 0;

     |SI nModo = 3;
         |CONFI "0000NSeguro de Borrar el Registro.";
         |SI FSalida = 0;
             #27#0 = #26#0;
             #27#1 = #26#1;
             #27#2 = #26#2;
             |LEE 101#27=;
             |SI FSalida = 0;
                 |BORRA 020#27a;
                 |LIBERA #27;
             |FINSI;

             |BUCLE agicen28B;
         |SINO;
             |ERROR;
         |FINSI;
         |ACABA;
     |FINSI;

     nForma = 0 +. 1;

     |SI nForma ! 1;  |ACABA;  |FINSI;

     |PUSHV 0501 2380;
     |BLANCO 1101 2380;

     enOpcion = 1;
     |PARA; |SI;  |HACIENDO;
          aMenu2 = enOpcion;
          |MENUG aMenu;
          enOpcion = FSalida;
          |SI enOpcion < 1;  |O enOpcion > 7;
              |SAL;
          |FINSI;

          enEmpresa   = #26#0;
          enActividad = #26#1;
          enRegistro  = #26#2;

          |ABRE #27;
          #27#0 = #26#0;
          #27#1 = #26#1;
          #27#2 = #26#2;
          |LEE 040#27=;
          |SI FSalida ! 0;  |DDEFECTO #27;  |FINSI;
          |CIERRA #27;

          |SI enOpcion = 1;   |SUB_EJECUTA_NP "agicen27";  |FINSI;
          |SI enOpcion = 2;   |SUB_EJECUTA_NP "agicen27";  |FINSI;
          |SI enOpcion = 3;   |SUB_EJECUTA_NP "agicen27";  |FINSI;
          |SI enOpcion = 4;   |SUB_EJECUTA_NP "agicen27";  |FINSI;
          |SI enOpcion = 5;   |SUB_EJECUTA_NP "agicen27";  |FINSI;
          |SI enOpcion = 6;   |Y #27#27 ! "M";
              |SUB_EJECUTA_NP "agicen28";
          |FINSI;
          |SI enOpcion = 7;   |HAZ EmiteEsto;              |FINSI;

          |ABRE #27;
          #27#0 = #0#0;
          #27#1 = #0#1;
          #27#2 = 99;
          |LEE 040#27];
          |SI FSalida = 0;
              |LEE 040#27a;
          |SINO;
              |LEE 040#27u;
          |FINSI;
          |SI FSalida = 0;  |Y  #27#0 = #0#0;  |Y #27#1 = #0#1;
              |SI #27#28 = "1";  #0#2 = "E";  |FINSI;
              |SI #27#28 = "2";  #0#2 = "P";  |FINSI;
              |SI #27#28 = "3";  #0#2 = "A";  |FINSI;
              #0#3 = #27#31;
              #0#4 = #27#32;

              #26#6 = #0#4;  |PINTA #26#6;
          |FINSI;
     |SIGUE;
     |POPV;
|FPRC;


|PROCESO Tipo2w32;  |TIPO 2;
     nModo = (FEntrada / 100) ! 0;

     |SI nModo = 3;
         |CONFI "0000NSeguro de Borrar el Registro.";
         |SI FSalida = 0;
             |HAZ BorraFicheros;
         |SINO;
             |ERROR;
         |FINSI;
         |ACABA;
     |FINSI;

     nForma = 0 +. 1;

     |SI nForma ! 1;  |ACABA;  |FINSI;

     |PUSHV 0501 2380;
     |ENTLINEAL #1#26, -3, enModo, 22, 1, ClaveBaja26, ClaveAlta26;
     |POPV;
|FPRC;

|PROCESO Tipo7w32;  |TIPO 7;
     #0#0 = enEmpresa;    |PINTA #0#0;
     #0#7 = eaNombreEmp;  |PINTA #0#7;
|FPRC;

|PROGRAMA;
     |SI enModo ! 3;
         |ENTLINEAL #1#0, -2, enModo, 22, 1, ClaveBaja0, ClaveAlta0;
     |SINO;
         nDAct = 1;
         nHAct = 99;
         |BUCLE agiw0032B;
     |FINSI;
|FPRO;
