|FICHEROS;
     agiy0002 #0;
     agicen00 #8;
     agicen01 #1;
     agicen02 #2;
     agicen03 #3;
     agicen04 #4;
     agicen05 #5;
     agicen06 #6;
     agicen07 #7;
     agicen11 #11;
     agicen12 #12;
     agicen13 #13;

     agiw0003 #95;
     agihacie #96;
     agifigen #97;
     agiw0002 #99,MANTE;
|FIN;

|VARIABLES;
     Tipo     = 0;
     Linea    = 0;
     Contador = 0;
     SwPrint  = 0;
     NHoja    = 0;

     &hoja    = 2;
     &t_hojas = 0;
|FIN;

|INCLUYE i_ficher;
|INCLUYE i_consul;

|| **************************
|| IMPRESION MODELO  A¥O 1997
|| **************************

|PROCESO Particip97;
  Contador = Contador + 1;

  |SI SwPrint = 0; |ACABA; |FINSI;

  |SI Contador > 4;
      |PRINT;

      NHoja = NHoja + 1;
      |SI NHoja = 4;
          NHoja = 0;
          hoja   = hoja + 1;
      |FINSI;
  |FINSI;
|FPRC;

|DEFBUCLE agicen06;
  #6#1;
  ;
  #99#1, #99#3, 01;
  #99#1, #99#3, 99;
  ;
  NULL, Particip97;
|FIN;

|PROCESO Imprime97;
  |ABRE #3;                                  || Situacion Tributaria
  #3#0 = #99#1;
  #3#1 = #99#3;
  |LEE 040#3=;
  |SI FSalida ! 0; |DDEFECTO #3; |FINSI;
  |CIERRA #3;

  |ABRE #4;                                  || Declaraciones y Cese
  #4#0 = #99#1;
  #4#1 = #99#3;
  |LEE 040#4=;
  |SI FSalida ! 0; |DDEFECTO #4; |FINSI;
  |CIERRA #4;

  |INFOR "m036h004";
  |SI FSalida  = 0;
      Contador    = 0; SwPrint = 0; |BUCLE agicen06;

      t_hojas = ((Contador / 4) + 0.49) ! 0;

      hoja   = 2;
      NHoja  = 0;
      Contador   = 0; SwPrint = 1;  |BUCLE agicen06;

      |SI Contador ! 0; |PIEINF; |FINSI;
      |FININF;
  |FINSI;
|FPRC;

|| **************************
|| IMPRESION MODELO  A¥O 1998
|| **************************

|PROCESO Particip98;
  Contador = Contador + 1;

  |SI SwPrint = 0; |ACABA; |FINSI;

  |SI Contador > 3;
      |PRINT;

      NHoja = NHoja + 1;
      |SI NHoja = 3;
          NHoja = 0;
          hoja   = hoja + 1;
      |FINSI;
  |FINSI;
|FPRC;

|DEFBUCLE agicen13;
  #13#1;
  ;
  #99#1, #99#3, 01;
  #99#1, #99#3, 99;
  ;
  NULL, Particip98;
|FIN;

|PROCESO Imprime98;
  |ABRE #11;                                  || Situacion Tributaria
  #11#0 = #99#1;
  #11#1 = #99#3;
  |LEE 040#11=;
  |SI FSalida ! 0; |DDEFECTO #11; |FINSI;
  |CIERRA #11;

  |ABRE #12;                                  || Declaraciones y Cese
  #12#0 = #99#1;
  #12#1 = #99#3;
  |LEE 040#12=;
  |SI FSalida ! 0; |DDEFECTO #12; |FINSI;
  |CIERRA #12;

  |INFOR "m036h014";
  |SI FSalida  = 0;
      Contador    = 0; SwPrint = 0; |BUCLE agicen13;

      t_hojas = ((Contador / 3) + 0.49) ! 0;

      hoja   = 2;
      NHoja  = 0;
      Contador   = 0; SwPrint = 1;  |BUCLE agicen13;

      |SI Contador ! 0; |PIEINF; |FINSI;
      |FININF;
  |FINSI;
|FPRC;

|PROCESO Imprime;
  |ABRE #97;                                  || Datos Contribuyentes
  #97#0 = #99#1;
  |LEE 040#97=;
  |SI FSalida ! 0; |DDEFECTO #97; |FINSI;
  |CIERRA #97;

  |ABRE #8;                                  || Acceso al Censo
  #8#0 = #99#1;
  |LEE 040#8=;
  |SI FSalida ! 0; |DDEFECTO #8; |FINSI;
  |CIERRA #8;

  |ABRE #96;                                  || Datos Hacienda
  #96#0 = #97#63;
  #96#1 = #97#64;
  |LEE 040#96=;
  |SI FSalida ! 0; |DDEFECTO #96; |FINSI;
  |CIERRA #96;

  |ABRE #1;                                  || Lineas Censo
  #1#0 = #99#1;
  #1#1 = #99#3;
  |LEE 040#1=;
  |SI FSalida ! 0; |DDEFECTO #1; |FINSI;
  |CIERRA #1;

  |ABRE #2;                                  || Causas y otros Datos Censales
  #2#0 = #99#1;
  #2#1 = #99#3;
  |LEE 040#2=;
  |SI FSalida ! 0; |DDEFECTO #2; |FINSI;
  |CIERRA #2;

  |ABRE #5;                                  || Representante y Obligado Trib.
  #5#0 = #99#1;
  #5#1 = #99#3;
  |LEE 040#5=;
  |SI FSalida ! 0; |DDEFECTO #5; |FINSI;
  |CIERRA #5;

  |ABRE #7;                                  || Operaciones Intracomunitarias
  #7#0 = #99#1;
  #7#1 = #99#3;
  |LEE 040#7=;
  |SI FSalida ! 0; |DDEFECTO #7; |FINSI;
  |CIERRA #7;

  |SI #0#5 = 1;  |HAZ Imprime97;  |FINSI;
  |SI #0#5 = 2;  |HAZ Imprime98;  |FINSI;
|FPRC;

|DEFBUCLE agiw0002;
  #99#1;
  5;
  00001, "S";
  Linea, "S";
  e;
  NULL, Imprime;
|FIN;

|PROCESO CargaTempo;
  Contador = 0;
  #99#1 = #1#0;
  #99#3 = #1#1;
  |SI #0#5 = 1;
      SwPrint = 0; |BUCLE agicen06;
      |SI Contador [ 4; |ACABA; |FINSI;
  |SINO;
      SwPrint = 0; |BUCLE agicen13;
      |SI Contador [ 3; |ACABA; |FINSI;
  |FINSI;

  Linea = Linea + 1;
  #99#0 = Linea;
  #99#1 = #1#0;
  #99#2 = #1#7;
  #99#3 = #1#1;
  #99#4 = #1#4;
  |SI #0#4 = "S"; #99#5 = "N"; |SINO; #99#5 = "S"; |FINSI;
  |GRABA 040#99n;
|FPRC;

|DEFBUCLE agicen01;
  #1#1;
  6;
  #0#0, #0#2, Tipo;
  #0#1, #0#3, Tipo;
  ;
  NULL, CargaTempo;
|FIN;

|RUTINA ClaveBaja;
  #99#0 = 1;
|FRUT;

|RUTINA ClaveAlta;
  #99#0 = Linea;
|FRUT;

|PROCESO Tipo3; |TIPO 3;
  |SI #0#5 = 1;  Tipo = 1;  |FINSI;
  |SI #0#5 = 2;  Tipo = 3;  |FINSI;

  |IMPRE 0;
  |SI FSalida ! 0; |ACABA; |FINSI;

  comienza = "02";
  |HAZ fichero;
  |NOME_DAT #99 fichero;
  |ABRE #99; |CIERRA #99; |DELINDEX #99;

  Linea = 0;
  |ABRE #99;
  |BUCLE agicen01;
  |CIERRA #99;

  |SI Linea = 0; |FINIMP; |ACABA; |FINSI;

  |PINPA #0#99;
  |ENTLINEAL #1#99, 1, 2, 19, 0, ClaveBaja, ClaveAlta;

  |BUCLE agiw0002;

  |FINIMP;
  |DELINDEX #99;
|FPRC;

|PROCESO funcion_1; |TIPO 11;
  |SI FSalida ! 9; |ACABA; |FINSI;

  |LIBERA #99;
  |LEE 140#99=;

  |SI #99#5 = "S"; #99#5 = "N"; |SINO; #99#5 = "S"; |FINSI;
  |PINTA #99#5;
  |GRABA 140#99a;
  |PTEC 809;
|FPRC;
