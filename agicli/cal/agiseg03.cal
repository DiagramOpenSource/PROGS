|FICHEROS;
    agiseg03 #0;
    labempre #1;         || periodo en curso
    nomhempr #2;         || historico

    agicontr #90;
|FIN;

|VARIABLES;
    &codigo = 0;
    &nombre = "";
    &Ejercicio = 0;
    dbase = "";
    alfa1 = "";
    alfa2 = "";
    nume1 = 0;
    nume2 = 0;
    para1 = 0;
    para2 = 0;
    FEstado = 0;
    pbases_agicli = 0;
    preten_agicli = 0;
    pfecha_agicli = 0;
    peltc1_agicli = 0;
    pbases_aginom = 0;
    preten_aginom = 0;
    pfecha_aginom = 0;
    peltc1_aginom = 0;
    sw_fuera = 0;
|FIN;

|PROCESO mayus; |TIPO 0;
    #0Campo = #0Campo > #0Campo;
|PINTA #0Campo;
|FPRC;

|PROCESO lee_hisempre;
|DDEFECTO #2;
|ABRE #2;
    #2#0 = #0#0;
|SI Ejercicio > 80;      #2#1 = Ejercicio + 1900; |FINSI;
|SI Ejercicio < 80;      #2#1 = Ejercicio + 2000; |FINSI;
|LEE 000#2=;
|SI FSalida ! 0;
    |MENAV "    Periodo inexistente en el historico de AGINOM1 ...";
|FINSI;
|CIERRA #2;
|FPRC;

|PROCESO periodo_histo;
|HAZ lee_hisempre;
|PARA para1 = 1; |SI para1 [ 12; |HACIENDO para1 = para1 + 1;
    |SI para1 ] #0#2;|Y para1 [ #0#3;
            pbases_aginom = para1 + 72;
            preten_aginom = para1 + 84;
            pfecha_aginom = para1 + 96;
            peltc1_aginom = para1 + 113;

            pbases_agicli = para1 + 4;
            preten_agicli = para1 + 16;
            pfecha_agicli = para1 + 28;
            peltc1_agicli = para1 + 40;

            #0pbases_agicli = #2pbases_aginom;
            #0preten_agicli = #2preten_aginom;
            #0pfecha_agicli = #2pfecha_aginom;
            #0peltc1_agicli = #2peltc1_aginom;
    |FINSI;
|SIGUE;
|SI #0#3 = 13;
        #0#53 = #2#126;       || seguros sociales de atrasos
|FINSI;
|FPRC;

|PROCESO lee_labempre;
|DDEFECTO #1;
|ABRE #1;
    #1#0 = #0#0;
|LEE 000#1=;
|SI FSalida ! 0;
    |MENAV "    Codigo inexistente en AGINOM1 ...";
|FINSI;
|CIERRA #1;
|FPRC;

|PROCESO periodo_curso;
|HAZ lee_labempre;
|PARA para1 = 1; |SI para1 [ 12; |HACIENDO para1 = para1 + 1;
    |SI para1 ] #0#2;|Y para1 [ #0#3;
            pbases_aginom = para1 + 72;
            preten_aginom = para1 + 84;
            pfecha_aginom = para1 + 96;
            peltc1_aginom = para1 + 113;

            pbases_agicli = para1 + 4;
            preten_agicli = para1 + 16;
            pfecha_agicli = para1 + 28;
            peltc1_agicli = para1 + 40;

            #0pbases_agicli = #1pbases_aginom;
            #0preten_agicli = #1preten_aginom;
            #0pfecha_agicli = #1pfecha_aginom;
            #0peltc1_agicli = #1peltc1_aginom;
    |FINSI;
|SIGUE;
|SI #0#3 = 13;
        #0#53 = #1#126;       || seguros sociales de atrasos
|FINSI;
|FPRC;

|PROCESO tipo_3;
|SI #0#54 = "S";
    |HAZ periodo_curso;       || del periodo en curso (labempre)
|SINO;
    |HAZ periodo_histo;       || del periodo de historico (nomhempr)
|FINSI;
    nume1=#0#17+#0#18+#0#19+#0#20+#0#21+#0#22+#0#23+#0#24+#0#25+#0#26+#0#27+#0#28;
    nume2=#0#41+#0#42+#0#43+#0#44+#0#45+#0#46+#0#47+#0#48+#0#49+#0#50+#0#51+#0#52+#0#53;
    #0#4 = nume1 + nume2;
|FPRC;

|PROCESO para_agicli;
    dbase = "";
|DIRECTORIO dbase;
|PATH_DAT #90 dbase;
|ABRE #90;
|LEE 020#90p;            || tiene que existir, viene comprobado
|SI FSalida = 0;
        alfa1 = #90#2;   |QBF alfa1;
    |PATH_DAT #1 alfa1;
    |PATH_DAT #2 alfa1;
|FINSI;
|CIERRA #90;
|FPRC;

|PROCESO pantalla;
|PDEFECTO #0, 4, 54;
    #0#1 = nombre;
|PINDA #0#0;
|ENDATOS #1#0;
|SI FSalida = 0;
    |HAZ tipo_3;
    |PINDA #0#0;
    |PAUSA;

    |CONFI "    SValidar (S/N): ";
    |SI FSalida = 0;
        |SI FEstado = 0; |GRABA 020#0a; |FINSI;
        |SI FEstado ! 0; |GRABA 020#0n; |FINSI;
    |SINO;
            sw_fuera = 0;
    |FINSI;
|FINSI;
|FPRC;

|PROGRAMA;
|CLS;
|CABEZA "Tributacion AGINOM1";
    sw_fuera = 0;
|PARA; |SI sw_fuera = 0; |HACIENDO;
        sw_fuera = 1;
    |HAZ para_agicli;        || lee control de AGIFIGEN para poner el path
    |PINPA #0#0;
    |DDEFECTO #0;
    |ABRE #0;
        #0#0 = codigo;
        #0#55 = Ejercicio;
    |LEE 101#0=;
        FEstado = FSalida;
    |PINDA #0#0;
    |PAUSA;

    |SI FEstado = 0;
        |CONFI "    NModificar (S/N): ";
        |SI FSalida = 0;
            |HAZ pantalla;
        |FINSI;
    |SINO;
        |HAZ pantalla;
    |FINSI;
    |LIBERA #0;
    |CIERRA #0;
|SIGUE;
|FPRO;
