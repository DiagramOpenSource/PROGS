|FICHEROS;
    agicen13 #6;
    agifigen #100;
    agiw0030 #98,MANTE;
|FIN;

|VARIABLES;
     &c_empre     = 0;
     &c_activ     = 0;
     &c_tipo      = 0;
     &n_empre     = "";
     &n_activ     = "";

     aNifQbf1   = "";
     aNifQbf2   = "";

     &eaVarDni  = "";
     &enCalcCif = 0;

     campo_6      = 0;
     Linea        = 0;
|FIN;

|PROCESO LetrasDni;
     |SI FSalida = 9;
         eaVarDni   = #6Campo;
         enCalcCif  = 1;
         |HAZ CalculoDNI;
         #6Campo = eaVarDni;
         |PINTA #6Campo;
         |ERROR;
         |ACABA;
     |FINSI;

     eaVarDni  = #6Campo;
     enCalcCif = 0;
     |HAZ CalculoDNI;

     aNifQbf1 = #6Campo;   |QBF aNifQbf1;
     aNifQbf2 = eaVarDni;  |QBF aNifQbf2;

     |SI aNifQbf1 ! aNifQbf2; |Y aNifQbf1 ! "";
         |CONFI "0000SEl Nif puede ser Erroneo. Desea Calcularlo.";
         |SI FSalida = 0;
             #6Campo = eaVarDni;  |PINTA #6Campo;
             |ERROR;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO GrabaParticipes;
  |SI Linea = 0;
      |PUSHV 0501 2380;
      |D_CUADRO 1619, 1873;
      |ATRI R;
      |ATRI R;
      |PINTA 1720, "RECOJO LOS PARTICIPES DESDE LA FICHA DE CLIENTES :   ";
      |ATRI 0;

      |CONFI "1771S";
      |SI FSalida ! 0;  |POPV;  |ERROR10;  |ACABA;  |FINSI;
      |POPV;
  |FINSI;

  Linea = Linea + 1;
  #6#0  = c_empre;             || Empresa
  #6#1  = c_activ;             || Actividad
  #6#2  = Linea;               || N§ Participe
  #6#3  = #100#13;             || Nif
  #6#4  = #100#1;              || Apellidos y Nombre
  #6#16 = #100#3;              || SG
  #6#17 = #100#4;              || Calle
  #6#18 = #100#5;              || Numero
  #6#19 = #100#6;              || Escalera
  #6#20 = #100#7;              || Piso
  #6#21 = #100#8;              || Puerta
  #6#22 = #100#14;             || Telefono
  #6#23 = #100#9;              || Codigo Provincia
  #6#24 = #100#10;             || Codigo Poblacion
  #6#25 = #100#11;             || Municipio
  #6#26 = #100#12;             || Provincia
  |GRABA 140#6n;
  |LIBERA #6;
|FPRC;

|DEFBUCLE agifigen;
  #100#1;
  90;
  00000, c_empre;
  99999, c_empre;
  ;
  NULL, GrabaParticipes;
|FIN;

|RUTINA infer_06;
  |SI FSalida = "POSICION";
      Linea = 0;
      |ABRE #6;
      #6#0 = c_empre;
      #6#1 = c_activ;
      #6#2 = 1;
      |LEE 040#6];
      |SI FSalida ! 0; |O #6#0 ! c_empre; |O #6#1 ! c_activ;
          |BUCLE agifigen;
      |FINSI;
      |CIERRA #6;
  |FINSI;

  #6#0 = c_empre;
  #6#1 = c_activ;
  #6#2 = 1;
|FRUT;

|RUTINA super_06;
  #6#0 = c_empre;
  #6#1 = c_activ;
  #6#2 = 99;
|FRUT;

|PROGRAMA;
  |PINPA #0#6;
  |ENTLINEAL #1#6, 3, 2, 21, 0, infer_06, super_06;
|FPRO;

|PROCESO ayuda_06;
  |PUSHV 0501 2380;
  |PINPA #0#98;

  |PARA campo_6 = 5; |SI campo_6 [ 26; |HACIENDO campo_6 = campo_6 + 1;
        #98campo_6 = #6campo_6;
  |SIGUE;
  |PINDA #0#98;

  |ENDATOS #2#98;
  |SI FSalida = 0;
      |PARA campo_6 = 5; |SI campo_6 [ 26; |HACIENDO campo_6 = campo_6 + 1;
            #6campo_6 = #98campo_6;
      |SIGUE;
  |FINSI;
  |POPV;
|FPRC;
