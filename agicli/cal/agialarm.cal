|FICHEROS;
    agialarm #0;
    agiagenl #1;
    agiagenc #2;
    agihenvi #3;
|FIN;

|VARIABLES;
     &usuario   = "";
     &refer     = 0;
     &sw_alarma = 0;

     alfa     = "";
     minuto   = 0;
     hora     = 0;
     mensa    = "";
|FIN;

|INCLUYE i_ficher;

|RUTINA inferior;
#0#0 = 1;
|FRUT;

|RUTINA superior;
#0#0 = refer;
|FRUT;

|PROGRAMA;
|SI sw_alarma = 0; sw_alarma = 1; |ACABA; |FINSI;

comienza = "al";
|HAZ fichero;
|NOME_DAT #0fichero;

|ABRET #1;
|ABRET #2;
|ABRET #3;
|PINPA #0#0;
|ENTLINEAL #1#0, 1, 4, 19, 0, inferior, superior;
|CIERRAT #1;
|CIERRAT #2;
|CIERRAT #3;
|FPRO;

|PROCESO tipo_11; |TIPO 11;
|SI FSalida = 9;  |HAZ anula;    |FINSI;
|SI FSalida = 10; |HAZ modifica; |FINSI;
|SI FSalida = 11; |HAZ mensaje;  |FINSI;
|FPRC;

|PROCESO anula;
|SI #0#2 = "M"; |ACABA; |FINSI;

|LIBERA #0;
|LEE 140#0=;

|SI #0#2 = "C";
    #0#2 = "A";
|SINO;
    #0#2 = "C";
|FINSI;

|GRABA 140#0a;
|PINTA #0#2;

#1#0 = #0#4;
#1#2 = usuario;
#1#3 = #0#5;
#1#4 = #0#6;
#1#5 = #0#7;
|LIBERA #1;
|LEE 140#1=;
|SI FSalida = 0;
    |SI #0#2 = "A"; #1#13 = "N"; |FINSI;
    |SI #0#2 = "C"; #1#13 = "S"; |FINSI;
    |GRABA 140#1a;
|FINSI;
|FPRC;

|PROCESO modifica; |TIPO 0;
|SI #0#2 = "A"; |ACABA; |FINSI;
|SI #0#2 = "M"; |ACABA; |FINSI;

|LIBERA #0;
|LEE 140#0=;

#0#2 = "M";
|GRABA 140#0a;
|PINTA #0#2;

|ET pide_1;
    |ENCAMPO #1#0;
    |ENCAMPO #3#0;
    |SI FSalida = 2; |VETE pide_1; |FINSI;

#1#0 = #0#4;
#1#2 = usuario;
#1#3 = #0#5;
#1#4 = #0#6;
#1#5 = #0#7;
|LIBERA #1;
|LEE 140#1=;
|SI FSalida = 0;
    mensa = #1#7 % 0139;
    #1#7  = mensa + "*";
    #1#13 = "N";
    |GRABA 140#1a;
|FINSI;

#2#0 = #0#3;
#2#2 = usuario;
|LEE 140#2=;
|SI FSalida ! 0;
    #2#0 = #0#3;
    #2#2 = usuario;
    #2#1 = "";
    #2#3 = "";
    |GRABA 140#2n;
|FINSI;

minuto = #1#4;
hora   = #1#3;

|ET otro;
    #1#0 = #0#3;
    #1#1 = "";
    #1#2 = usuario;
    #1#3 = hora;
    #1#4 = minuto;
    #1#5 = #0#7;
    |LIBERA #1;
    |LEE 140#1=;
    |SI FSalida = 0;
        minuto = minuto + 1;
        |SI minuto > 59;
            minuto = 00;
            hora   = hora + 1;
            |SI hora > 23; hora = 1; |FINSI;
        |FINSI;
        |VETE otro;
    |SINO;
        #1#0 = #0#4;
        #1#1 = "";
        #1#2 = usuario;
        #1#3 = #0#5;
        #1#4 = #0#6;
        #1#5 = #0#7;
        |LIBERA #1;
        |LEE 140#1=;
        |SI FSalida = 0;
            #1#0  = #0#3;
            #1#3  = hora;
            #1#4  = minuto;
            #1#7  = #0#1;
            #1#13 = "S";
            |GRABA 140#1n;
        |FINSI;
    |FINSI;
|FPRC;

|PROCESO mensaje;
#3#14 = #0#8;
|LEE 040#3=;
|SI FSalida = 0;
    |PUSHV 0501 2380;
    |PINPA #0#3;
    |PINDA #0#3;
    |PAUSA;
    |POPV;
|FINSI;
|FPRC;
