|FICHEROS;
   agiz0003 #0;
   agigrupl #1;
   agiclgrc #2;
   agiclgrl #3;
   agiw0011 #4;
   agifigen #5;
|FIN;

|VARIABLES;
   desde = 0;
   hasta = 0;
   xxx   = 0;
   linea = 0;
   fecha = @;
   alfa  = "";
   mes   = "";
   dia   = "";
   any   = "";
   suma  = 0;
   me_salgo = 0;
|FIN;

|INCLUYE i_ficher;
|INCLUYE i_consul;

|PROCESO anyo; |TIPO 7;
fecha = ~;
alfa  = fecha % -104;
#0#2  = alfa;
|PINTA #0#2;
|FPRC;

|PROCESO graba_ind;
desde = #1#7;
hasta = #1#7;
suma  = 1;

|SI #1#7 = 13; desde = 1; hasta = 12; suma = 1; |FINSI;
|SI #1#7 = 14; desde = 2; hasta = 12; suma = 2; |FINSI;
|SI #1#7 = 15; desde = 1; hasta = 12; suma = 3; |FINSI;
|SI #1#7 = 16; desde = 4; hasta = 12; suma = 4; |FINSI;
|SI #1#7 = 17; desde = 6; hasta = 12; suma = 6; |FINSI;

|PARA xxx = desde; |SI xxx [ hasta; |HACIENDO xxx = xxx + suma;
      #4#0 = xxx;
      #4#1 = #1#8;
      #4#2 = #1#0;
      #4#3 = #1#1;
      |GRABA 020#4n;
|SIGUE;
|FPRC;

|DEFBUCLE agigrupl;
#1#1;
;
000, 000;
999, 999;
;
NULL, graba_ind;
|FIN;

|PROCESO graba_lin;
me_salgo = 0;
|PARA xxx = 66; |SI xxx [ 72; |HACIENDO xxx = xxx + 1;
      |SI #5xxx = #4#2; me_salgo = 1; |FINSI;
|SIGUE;

|SI me_salgo = 0; |ACABA; |FINSI;

#1#0 = #4#2;
#1#1 = #4#3;
|LEE 040#1=;
|SI FSalida ! 0; |ACABA; |FINSI;

linea = linea + 5;

#3#0  = #5#0;
#3#1  = #0#2;
#3#2  = linea;
#3#3  = #4#0;
#3#4  = #4#1;
#3#5  = #1#2;
#3#6  = #1#3;
#3#7  = #1#4;
#3#8  = #1#5;
#3#9  = #1#6;
#3#10 = #1#9;
#3#11 = #1#10;
#3#12 = #1#11;
#3#13 = #1#12;
#3#14 = "N";
#3#16 = #1#13;

|HAZ fecha_comp;

|GRABA 040#3n;
|FPRC;

|PROCESO fecha_comp;
mes = #3#3;
mes = ("00" + mes) % -102;

dia = #3#4;
dia = ("00" + dia) % -102;

any = #3#1;
any = ("0000" + any) % -104;

alfa = dia + "." + mes + "." + any;
#3#15 = alfa;
|FPRC;

|DEFBUCLE agiw0011;
#4#1;
;
00, 00, 001, 000;
99, 99, 999, 999;
;
NULL, graba_lin;
|FIN;

|PROCESO al_rollo;
#2#0 = #5#0;
#2#1 = #0#2;
|LEE 040#2=;
|SI FSalida = 0; |ACABA; |FINSI;

linea = 0;
|BUCLE agiw0011;

|SI linea = 0; |ACABA; |FINSI;

#2#0 = #5#0;
#2#1 = #0#2;
#2#2 = #5#1;
|GRABA 040#2n;
|FPRC;

|DEFBUCLE agifigen;
#5#1;
;
#0#0;
#0#1;
;
NULL, al_rollo;
|FIN;

|PROCESO tipo_3; |TIPO 3;
comienza = "11";
|HAZ fichero;
|NOME_DAT #4 fichero;

|ABRE #4; |CIERRA #4; |DELINDEX #4;

|ABRE #4;
|BUCLE agigrupl;
|CIERRA #4;

|ABRE #1;
|ABRE #2;
|ABRE #3;
|BUCLE agifigen;
|CIERRA #1;
|CIERRA #2;
|CIERRA #3;
|FPRC;
