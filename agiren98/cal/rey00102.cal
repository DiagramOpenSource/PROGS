|FICHEROS;
    rey00102 #0;
    redatcon #1;    || datos contribuyentes
    rem00002 #2;    || inmuebles
    rem00003 #3;    || modelo 210
    rehacien #4;    || datos hacienda

    rew00008 #10;   || tmp.modelo 210 (a)
    rew00009 #11;   || tmp.comprobacion
|FIN;

|VARIABLES;
    alfa1 = "";
    alfa2 = "";
    alfa3 = "";
    alfa4 = "";
    nume1 = 0;
    nume2 = 0;
    nume3 = 0;
    nume4 = 0;
    para1 = 0;
    para2 = 0;
    para3 = 0;
    para4 = 0;
    fech1 = @;
    fech2 = @;
    fech3 = @;
    fech4 = @;
    elsw1 = 0;
    elsw2 = 0;
    elsw3 = 0;
    elsw4 = 0;
    FEstado = 0;
    seleccio = 0;
    modo = 0;
    sw_rup = 0;

    &Temporal = "";
    &Cliente01 = 0;
    &Cliente02 = 0;
    &Cliente03 = 0;
    &Cliente04 = 0;
    &Cliente05 = 0;
    &Cliente06 = 0;
    &Cliente07 = 0;
    &Cliente08 = 0;
    &Cliente09 = 0;
    &Cliente10 = 0;
    &Cliente11 = 0;
    &Cliente12 = 0;
    &Cliente13 = 0;
    &Cliente14 = 0;
    &Cliente15 = 0;
    &Cliente16 = 0;
    &Cliente17 = 0;
    &Cliente18 = 0;

    Cliente = 0;
    Ctipo = "";
    Parte = 0;
    Familiar = 0;
    Varios = 0;
    &Firma = "";
    &Cuenta = "";
    Valido = 0;
|FIN;

|INCLUYE i_consul;
____________________________________/

|PROCESO Mayus;
  #0Campo = #0Campo > #0Campo;  |PINTA #0Campo;
|FPRC;

|PROCESO Fichero;
|DDEFECTO #11;
    #11#0 = #3#0;
|SI Ctipo = "T";    #11#8 = 1;     |FINSI;
|SI Ctipo = "C";    #11#8 = 2;     |FINSI;
|SI Ctipo = "1";    #11#8 = 3;     |FINSI;
|SI Ctipo = "2";    #11#8 = 4;     |FINSI;
|SI Ctipo = "3";    #11#8 = 5;     |FINSI;
|SI Ctipo = "4";    #11#8 = 6;     |FINSI;
|LEE 101#11=;
    FEstado = FSalida;

    #11#1 = Ctipo;  || tipo TC1234
    #11#2 = #3#29;  || NIF
    #11#3 = #3#31;  || nombre
    #11#4 = #3#117; || cuota 210
|SI #3#95 = "X";    #11#5 = "A";   |FINSI;        || tipo 210 A
|SI #3#99 = "X";    #11#5 = "B";   |FINSI;        || tipo 210 B
|SI #3#104= "X";    #11#5 = "C";   |FINSI;        || tipo 210 C
    #11#10 = #3#121;     || impreso S/N

|SI FEstado = 0;    |GRABA 020#11a;     |FINSI;
|SI FEstado ! 0;    |GRABA 020#11n;     |FINSI;
|LIBERA #11;
|FPRC;

|PROCESO Impresora;
  |SI #0#19 = "N";
      |INFOR "m210h001";   |PRINT;   |PIEINF;  |FININF;
      |INFOR "m210h002";   |PRINT;   |PIEINF;  |FININF;
      |INFOR "m210h003";   |PRINT;   |PIEINF;  |FININF;
  |SINO;
      |INFOR "m210h003";   |PRINT;   |PIEINF;  |FININF;
      |INFOR "m210h001";   |PRINT;   |PIEINF;  |FININF;
      |INFOR "m210h002";   |PRINT;   |PIEINF;  |FININF;
  |FINSI;
|FPRC;

|PROCESO ClaveA;
    Valido = 0;

    #10#0 = #2#0;
    #10#1 = Ctipo;
|LEE 000#10=;
|SI FSalida = 0;
    |SI #10#2 ! #2#1;
            Valido = 1;
    |SINO;
            #3#98 = #10#3;         || base imponible
            #3#114 = #10#6;        || cuota integra
            #3#115 = #10#4;        || deducciones
            #3#116 = #10#5;        || retenciones
            #3#117 = #10#7;        || cuota diferencial
    |FINSI;
|FINSI;
|FPRC;

|PROCESO CargaTipo;
    Cuenta = #1#175;

|SI Ctipo = "C";
        #3#29 = #1#29;             || NIF conyuge (3)
        #3#31 = #1#28;             || nombre conyuge (3)

    |SI #3#14 = "S";
            #3#15 = #1#29;         || NIF conyuge (1) (SUJETO PASIVO)
            #3#17 = #1#28;         || nombre conyuge (1)
    |FINSI;
        Cuenta = #1#176;
|FINSI;

|SI Ctipo ] "1";|Y Ctipo [ "4";
        Familiar = 0;
    |SI Ctipo = "1";    Familiar = #1#98;   |FINSI;
    |SI Ctipo = "2";    Familiar = #1#99;   |FINSI;
    |SI Ctipo = "3";    Familiar = #1#100;  |FINSI;
    |SI Ctipo = "4";    Familiar = #1#101;  |FINSI;
    |SI Familiar ! 0;
            nume1 = Familiar + 47;
        |SI #3#14 = "S";
                #3#15 = #1nume1;        || nif (1)
        |FINSI;
            #3#29 = #1nume1;            || nif (3)
            nume1 = Familiar + 57;
        |SI #3#14 = "S";
                #3#17 = #1nume1;        || nombre (1)
        |FINSI;
            #3#31 = #1nume1;            || nombre (3)

            nume1 = Familiar + 176;
            Cuenta = #1nume1;           || cuenta corriente
    |SINO;
            alfa1 = "    Familiar [" + Ctipo + "] erroneo en [" + #1#0 + "] ...";
        |MENAV alfa1;
    |FINSI;
|FINSI;
|FPRC;

|PROCESO RecalculaParte;
    #3#98  = #3#98 % Parte;        || base imponible
    #3#100 = #3#100 % Parte;       || ingresos netos
    #3#101 = #3#101 % Parte;       || gastos personal
    #3#102 = #3#102 % Parte;       || gastos aprovis.
    #3#103 = #3#103 % Parte;       || base imponible
    #3#108 = #3#108 % Parte;       || valor transmision
    #3#109 = #3#109 % Parte;       || valor adquisicion
    #3#110 = #3#110 % Parte;       || diferencia
    #3#111 = #3#111 % Parte;       || incremento
    #3#112 = #3#112 % Parte;       || base imponible
    #3#114 = #3#114 % Parte;       || cuota integra
    #3#115 = #3#115 % Parte;       || deducciones donativo
    #3#116 = #3#116 % Parte;       || retenciones
    #3#117 = #3#117 % Parte;       || cuota diferencial
|FPRC;

|PROCESO Modelo;
    #3#0 = #2#0;
    #3#1 = #2#1;
|LEE 020#3=;             || vuelve a leer 210

|HAZ RecalculaParte;
|HAZ CargaTipo;
|HAZ ClaveA;

|SI Valido = 0;
        Firma = #3#27;  |QBT Firma;
        Firma = (#3#27 - Firma) + Firma;     || ajusta a la derecha
    |SI Temporal = "";
        |HAZ Impresora;
    |SINO;
        |HAZ Fichero;
    |FINSI;
|FINSI;
|FPRC;

|PROCESO Emite1;
    #3#0 = #2#0;
    #3#1 = #2#1;
|LEE 101#3=;             || lee modelo 210 (rem00003)
|SI FSalida ! 0;
    |ACABA;
|SINO;
    |SI Temporal = "";
            #3#121 = "S";          || impreso SI
        |GRABA 020#3a;
    |FINSI;
|FINSI;
|LIBERA #3;

    seleccio = 1;

|DDEFECTO #4;
    #4#0 = #1#40;
    #4#1 = #1#41;
|LEE 000#4=;        || datos hacienda

|SI #2#3 = "G";                                        || gananciales
        Ctipo = "T";     Parte = 50;    |HAZ Modelo;
        Ctipo = "C";     Parte = 50;    |HAZ Modelo;
|FINSI;
|SI #2#3 = "V";                                        || varios
    |PARA Varios = 8; |SI Varios [ 13; |HACIENDO Varios = Varios + 1;
        |SI #2Varios ! 0;
                Parte = #2Varios;
            |SI Varios =  8;   Ctipo = "T";   |HAZ Modelo;   |FINSI;
            |SI Varios =  9;   Ctipo = "C";   |HAZ Modelo;   |FINSI;
            |SI Varios = 10;   Ctipo = "1";   |HAZ Modelo;   |FINSI;
            |SI Varios = 11;   Ctipo = "2";   |HAZ Modelo;   |FINSI;
            |SI Varios = 12;   Ctipo = "3";   |HAZ Modelo;   |FINSI;
            |SI Varios = 13;   Ctipo = "4";   |HAZ Modelo;   |FINSI;
        |FINSI;
    |SIGUE;
|FINSI;
|SI #2#3 ! "G";|Y #2#3 ! "V";                          || resto
        Ctipo = #2#3;    Parte = 100;   |HAZ Modelo;
|FINSI;
|FPRC;

|DEFBUCLE Emite;
 #2#1;
 ;
 #1#0,  1;
 #1#0, 99;
 ;
 NULL, Emite1;
|FIN;

|PROCESO GrabaTemporal;
|DDEFECTO #10;
    #10#0 = #2#0;
    #10#1 = Ctipo;
|LEE 101#10=;
    FEstado = FSalida;

|SI #10#2 = 0; #10#2 = #2#1;  |FINSI;   || la linea sera del primero
    #10#3 = #10#3 + (#3#98 % Parte);    || base imponible
    #10#4 = #10#4 + (#3#115 % Parte);   || deducciones donativo
    #10#5 = #10#5 + (#3#116 % Parte);   || retenciones
    #10#6 = #10#6 + (#3#114 % Parte);   || cuota integra
    #10#7 = #10#7 + (#3#117 % Parte);   || cuota diferencial

|SI FEstado = 0;    |GRABA 020#10a;     |FINSI;
|SI FEstado ! 0;    |GRABA 020#10n;     |FINSI;
|LIBERA #10;
|FPRC;

|PROCESO Temporal1;
    #3#0 = #2#0;
    #3#1 = #2#1;
|LEE 000#3=;
|SI FSalida ! 0;    |ACABA;   |FINSI;        || lee modelo 210 (rem00003)
|SI #3#95 ! "X";    |ACABA;   |FINSI;        || solo modelo A

|SI #2#3 = "G";                                        || gananciales
        Ctipo = "T";     Parte = 50;    |HAZ GrabaTemporal;
        Ctipo = "C";     Parte = 50;    |HAZ GrabaTemporal;
|FINSI;
|SI #2#3 = "V";                                        || varios
    |PARA Varios = 8; |SI Varios [ 13; |HACIENDO Varios = Varios + 1;
        |SI #2Varios ! 0;
                Parte = #2Varios;
            |SI Varios =  8;   Ctipo = "T";   |HAZ GrabaTemporal; |FINSI;
            |SI Varios =  9;   Ctipo = "C";   |HAZ GrabaTemporal; |FINSI;
            |SI Varios = 10;   Ctipo = "1";   |HAZ GrabaTemporal; |FINSI;
            |SI Varios = 11;   Ctipo = "2";   |HAZ GrabaTemporal; |FINSI;
            |SI Varios = 12;   Ctipo = "3";   |HAZ GrabaTemporal; |FINSI;
            |SI Varios = 13;   Ctipo = "4";   |HAZ GrabaTemporal; |FINSI;
        |FINSI;
    |SIGUE;
|FINSI;
|SI #2#3 ! "G";|Y #2#3 ! "V";                          || resto
        Ctipo = #2#3;    Parte = 100;   |HAZ GrabaTemporal;
|FINSI;
|FPRC;

|DEFBUCLE Temporal;
 #2#1;
 ;
 #1#0,  1;
 #1#0, 99;
 ;
 NULL, Temporal1;
|FIN;

|PROCESO Tipo3;
    alfa1 = Usuario; |QBT alfa1;
|NOME_DAT #10 alfa1;
|DELINDEX #10;

|ABRE #1;       || datos contribuyentes
|ABRE #3;       || modelo 210
|ABRE #4;       || datos hacienda
|ABRE #10;      || temporal modelo 210 (A)

|PARA Cliente = 0; |SI Cliente [ 17; |HACIENDO Cliente = Cliente + 1;
    |SI #0Cliente ! 0;
            #1#0 = #0Cliente;
        |LEE 000#1=;
        |SI FSalida = 0;|Y #1#4 = "N";
            |BUCLE Temporal;
        |FINSI;
    |FINSI;
|SIGUE;

|PARA Cliente = 0; |SI Cliente [ 17; |HACIENDO Cliente = Cliente + 1;
    |SI #0Cliente ! 0;
            #1#0 = #0Cliente;
        |LEE 000#1=;
        |SI FSalida = 0;|Y #1#4 = "N";
            |BUCLE Emite;
        |FINSI;
    |FINSI;
|SIGUE;

|SI seleccio = 0;|Y Temporal = "";
    |MENAV "    Seleccion vacia ...";
|FINSI;
|CIERRA #1;
|CIERRA #3;
|CIERRA #4;
|CIERRA #10;

|DELINDEX #10;
|FPRC;

|PROGRAMA;
|SI Temporal = "";
    |CLS;
    |CABEZA "Impresion 210";
    |PINPA #0#0;
    |DDEFECTO #0;
    |PINDA #0#0;
    |ENDATOS #1#0;
    |SI FSalida = 0;
        |IMPRE 0;
        |SI FSalida = 0;
            |HAZ Tipo3;
        |SINO;
            |MENAV "    Proceso abortado ...";
        |FINSI;
        |FINIMP;
    |FINSI;
|SINO;
        #0#0 = Cliente01;     #0#1 = Cliente02;   #0#2 = Cliente03;
        #0#3 = Cliente04;     #0#4 = Cliente05;   #0#5 = Cliente06;
        #0#6 = Cliente07;     #0#7 = Cliente08;   #0#8 = Cliente09;
        #0#9 = Cliente10;     #0#10 = Cliente11;  #0#11 = Cliente12;
        #0#12 = Cliente13;    #0#13 = Cliente14;  #0#14 = Cliente15;
        #0#15 = Cliente16;    #0#16 = Cliente17;  #0#17 = Cliente18;
    |NOME_DAT #11 Temporal;
    |ABRE #11;
    |HAZ Tipo3;
    |CIERRA #11;
|FINSI;
|FPRO;
