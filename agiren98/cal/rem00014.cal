|FICHEROS;
   rem00014 #0;
   reactafe #1;
   rerendir #2;
   reparame #3;
   rem00015 #98;
   repant19 #99;
|FIN;

|VARIABLES;
   tipo         = "AG";
   suma         = 0;
   valor        = 0;
   errorlin     = 0;

   &empresa     = 0;
   &nombre      = "";
   &modo        = 0;
   &modelito    = 0;
   &indivi      = "";
   &NoEntres    = 0;

   pantalla     = 0;
   nCampos      = 0;
   avance       = "815";
   atras        = "814";
   tecla        = "";
|FIN;

|INCLUYE i_husuar;
|INCLUYE i_activo;
|INCLUYE i_patrim;

|PROCESO ficher1;
     |SI modelito = 3;
         |BORRA 020#1a;
         |LIBERA #1;

         HContribu  = #1#0;
         HNombre    = nombre;
         HPrograma  = "(reactafe) ACTIVOS AFECTOS A LA ACTIVIDAD";
         HLinea     = #1#1;
         HPerceptor = #1#5;
         HTipo      = "BAJA DESDE EMPRESARIALES AGRICOLAS";
         HRdto      = #1#26;
         |HAZ AltaHistorico;
     |SINO;
         #1#4  = #0#2;
         #1#5  = #0#3;
         #1#6  = #0#14;
         #1#37 = #0#2;
         #1#38 = #0#5;
         #1#39 = #0#6;
         #1#40 = #0#7;
         #1#41 = #0#8;
         #1#42 = #0#9;
         #1#43 = #0#10;
         #1#45 = #0#11;
         |GRABA 020#1a;   |LIBERA #1;

         npro  = 12; |HAZ sigrabo;

         HContribu  = #1#0;
         HNombre    = nombre;
         HPrograma  = "(reactafe) ACTIVOS AFECTOS A LA ACTIVIDAD";
         HLinea     = #1#1;
         HPerceptor = #1#5;
         HTipo      = "MODIFICACION DESDE EMPRESARIALES AGRICOLAS";
         HRdto      = #1#26;
         |HAZ AltaHistorico;
     |FINSI;
|FPRC;

|PROCESO ficher2;
     |SI modelito = 3;
         |BORRA 020#2a; |LIBERA #2;

         HContribu  = #2#0;
         HNombre    = nombre;
         HPrograma  = "(rerendir) RENDIMIENTOS IRREGULARES";
         HLinea     = #2#1;
         HPerceptor = #2#5;
         HTipo      = "BAJA DESDE EMPRESARIALES AGRICOLAS";
         HRdto      = #2#7;
         |HAZ AltaHistorico;
     |SINO;
         #2#4  = #0#2;
         #2#5  = #0#3;
         #2#6  = #0#14;
         #2#16 = #0#4;
         #2#17 = #0#5;
         #2#18 = #0#6;
         #2#19 = #0#7;
         #2#20 = #0#8;
         #2#21 = #0#9;
         #2#22 = #0#10;
         #2#23 = #0#11;
         |GRABA 020#2a;
         |LIBERA #2;

         npro  = 13; |HAZ sigrabo;

         HContribu  = #2#0;
         HNombre    = nombre;
         HPrograma  = "(rerendir) RENDIMIENTOS IRREGULARES";
         HLinea     = #2#1;
         HPerceptor = #2#5;
         HTipo      = "MODIFICACION DESDE EMPRESARIALES AGRICOLAS";
         HRdto      = #2#7;
         |HAZ AltaHistorico;
     |FINSI;
|FPRC;

|DEFBUCLE reactafe;
     #1#1;
     2, 3;
     #0#0, 01, tipo, #0#1;
     #0#0, 99, tipo, #0#1;
     be;
     NULL, ficher1;
|FIN;

|DEFBUCLE rerendir;
     #2#1;
     2, 3;
     #0#0, 01, tipo, #0#1;
     #0#0, 99, tipo, #0#1;
     be;
     NULL, ficher2;
|FIN;

|| **************************************************************************
|| ***********************  ACTUALIZACION DEL PATRIMONIO ********************
|| **************************************************************************

|PROCESO baja_pat;
     patr_00 = #0#0;
     patr_01 = #0#42;
     patr_02 = #0#43;
     |HAZ borra_pat;
|FPRC;

|PROCESO alta_pat;
     |SI #0#4  = "X"; |O #0#4 = "R"; |ACABA; |FINSI;
     |SI #0#16 = 0;   |ACABA; |FINSI;

     anyos_p  = 0;
     tpro_p   = "P";

     patr_00 = #0#0;
     patr_01 = "C1";
     patr_03 = #0#2;
     patr_04 = #0#3;
     patr_05 = #0#15;
     patr_07 = "";
     patr_08 = "";
     patr_09 = #0#16;
     patr_14 = #0#14;
     patr_15 = "P";
     patr_17 = #0#12 + #0#13;

     |HAZ graba_pat;

     #0#42 = patr_01;
     #0#43 = patr_02;
|FPRC;

|PROCESO Borra98;
     |BORRA 020#98a;
     |LIBERA #98;
|FPRC;

|DEFBUCLE rem00015;
     #98#1;
     ;
     #0#0, #0#1, 00;
     #0#0, #0#1, 99;
     be;
     NULL, Borra98;
|FIN;

|PROCESO Ayuda; |TIPO 2;
     modelito = (FEntrada / 100) ! 0;
     pantalla = 0 +. 1;

     |HAZ baja_pat;

     QueTipo    = (FEntrada / 100) ! 0;

     |SI #0#4 ! "X"; |Y pantalla = 1; |HAZ cargadat; |FINSI;

     HContribu  = #0#0;
     HNombre    = nombre;
     HPrograma  = "(rem00014) AGRICOLAS Y GANADERAS";
     HLinea     = #0#1;
     HPerceptor = #0#3;
     HRdto      = #0#40;

     |SI QueTipo = 1; HTipo = "ALTA FICHA";         |FINSI;
     |SI QueTipo = 2; HTipo = "MODIFICACION FICHA"; |FINSI;
     |SI QueTipo = 3; HTipo = "BAJA FICHA";         |FINSI;

     |SI QueTipo = 3; |Y pantalla ! 1;
         |HAZ AltaHistorico;
         |BUCLE rerendir;
         |BUCLE reactafe;
         |BUCLE rem00015;
     |FINSI;

     |SI QueTipo < 3; |Y pantalla = 1; |Y errorlin = 1;
         |HAZ AltaHistorico;
         |BUCLE rerendir;
         |BUCLE reactafe;
     |FINSI;

     |SI pantalla ! 1; |ACABA; |FINSI;

     |HAZ alta_pat;
|FPRC;

|RUTINA ClaveBaja;
     #98#0 = #0#0;
     #98#1 = #0#1;
     #98#2 = 1;
|FRUT;

|RUTINA ClaveAlta;
     #98#0 = #0#0;
     #98#1 = #0#1;
     #98#2 = 99;
|FRUT;

|PROCESO PintaPan1;
       |PINPA #0#99;
       |PINDA #0#99;
       |ENTLINEAL #1#98, 3, 4, 21, 0, ClaveBaja, ClaveAlta;
       |MENSA "0411  Av.Pag.= Siguiente pantalla  Re.Pag. = Pantalla Anterior";
       |HAZ PintaPan2;
|FPRC;

|PROCESO PintaPan2;
       |PINPA #1#99;
       |PINDA #1#99;
       |MENSA "0411  Av.Pag.= Siguiente Pantalla  Re.Pag. = Pantalla Anterior";
       |LEETECLA tecla;
       |SI tecla = atras; |HAZ PintaPan1; |ACABA; |FINSI;
|FPRC;

|PROCESO cargadat;
     NoEntres = 0;

     |PUSHV 0501 2380;
     |DDEFECTO #99;
     |PARA nCampos = 0; |SI nCampos [ 47; |HACIENDO nCampos = nCampos + 1;
           #99nCampos = #0nCampos;
     |SIGUE;

     |PINPA #0#99;
     |PINDA #0#99;

     |SI modelito = 4;  |HAZ PintaPan1;  |POPV;  |ACABA; |FINSI;

     |PTEC 807;
     |ENTLINEAL #1#98, 3, 4, 21, 0, ClaveBaja, ClaveAlta;

     errorlin = 0;
     |ENDATOS #1#99;
     |SI FSalida = 0;
         errorlin = 1;
         |PARA nCampos = 0; |SI nCampos [ 47; |HACIENDO nCampos = nCampos + 1;
               #0nCampos = #99nCampos;
         |SIGUE;
         npro  = 52; |HAZ sigrabo;
     |SINO;
         |SI modelito = 1;
             |BUCLE rem00015;
         |FINSI;
     |FINSI;
     |POPV;

     |PINTA #0#1;
     |PINTA #0#2;
     |PINTA #0#3;
     |PINTA #0#4;
     |PINTA #0#12;
     |PINTA #0#13;
     |PINTA #0#14;
     |PINTA #0#40;
|FPRC;

|PROCESO parametro;
     |ABRE #3;
     |LEE 040#3=;
     |SI FSalida = 0;
         #0#2 = #3#6;
         #0#4 = #3#7;
         |PINTA #0#2;
         |PINTA #0#4;
     |FINSI;
     |CIERRA #3;
|FPRC;

|PROCESO Consulta; |TIPO 0;
     modelito     = (FEntrada / 100) ! 0;
     |SI modelito = 4; |HAZ cargadat;  |FINSI;
     |SI modelito = 1; |HAZ parametro; |FINSI;
|FPRC;

|RUTINA inferior;
    #0#0 = empresa;
    #0#1 = 1;
|FRUT;

|RUTINA superior;
    #0#0 = empresa;
    #0#1 = 99;
|FRUT;

|PROGRAMA;
     avance = &255 + avance;
     atras  = &255 + atras;

     |PINPA #0#0;
     |SI modo = 1;  |ENTLINEAL #1#0, 2, 1, 15, 0, inferior, superior;  |FINSI;
     |SI modo = 2;  |ENTLINEAL #1#0, 2, 2, 15, 0, inferior, superior;  |FINSI;
     |SI modo = 4;  |ENTLINEAL #1#0, 2, 4, 15, 0, inferior, superior;  |FINSI;
|FPRO;
