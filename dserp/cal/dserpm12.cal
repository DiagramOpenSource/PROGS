||PBXCRM
||http://192.168.10.123/crm/pp_index.php?number=2001

|FICHEROS;
     pbxpara3 #333;      ||PBX Para. Telefono (pbxpara3)

     referen@ #100;
     pbxpara5 #5;
|FIN;

|VARIABLES;
     aEjecuta = "";
     aNomCampo = "";
     nError = 0;
     nCorrecto = 0;
     aRespuesta = "";

     {1}aContiene = "";
     {1}aLocal = "";
     aBaseLocal = "";
     aVersion1 = "";
     aVersion2 = "";
     aIPRemoto = "";
     aOrigen = "";
     aDestino = "";
     aBase = "";

     comando = "";
     a = "";
     nn = 0;
     nExiste = 0;
     i = 0;

   {1000} aPopup = "";
     aContenido = "";

     nCampoVoy = 0;
     aAux = "";
     aFicheroDef = "";
     nHayMarcaPBX = 0;
     aLlamarTel = "";
     nHayNumTelefono = 0;
     aMas = "";
     aMensaje = "";
     aParametro = "";
     aAplica = "";
     aNumEmp = "";
     nEmpresa = 0;
     aNomDef = "";
     aParte1 = "";
     aParte2 = "";
     aClau = "";
     aLong = "";
     nLong = 0;
     nSwSegunda = 0;
     nPos = 0;
     aSaca = "";
     aLetra = "";
     aPathDef = "";
     aPathDat = "";

     nTamanyo = 0;
     aTamanyo = "";
     nSaca = 0;
     aAlfa5 = "";
     aClaves = "";
     aTCompo = "";
     nTCompo = 0;
     aTCompo2 = "";
     nTCompo2 = 0;
     {16}clave = 0;      ||Array de 16 elementos para la clave.
     nCompo = 0;
     nPosi = 0;
     aTCampos = "";
     nTCampos = 0;
     nCampo = 0;
     aAlfa1 = "";
     nVoy = 0;
     nNumCam = 0;
     Comodin = "";
     Comodin1 = "";

     aUsuario = "";
     aExtension = "";
     aHost = "";
     aPathCRM = "";
|FIN;

|PROGRAMA;
     aParametro = PARAMETRO;
     |QBF aParametro;
     |SI aParametro = "";
          aMensaje = "0000Atencion: Proceso [dserpm12] llamado sin parametros.";
          |MENAV aMensaje;
          |ACABA;
     |SINO;

          |HAZ CopiaExe;

          aUsuario = Usuario % 810;
          |QBF aUsuario;

          aAplica = aParametro % 108;
          aNumEmp = aParametro % 905;
          nEmpresa = aNumEmp;
          aNomDef = aParametro % 1408;
          aFicheroDef = aNomDef;
          aParte1 = aParametro % 2250;
          aParte2 = aParametro % 7250;
          aClau = aParte1 + aParte2;

          aLong = aParametro % A0;
          nLong = aLong;

          nSwSegunda = 0;
          aParte1 = "";
          aParte2 = "";
          |PARA nPos = 122; |SI nPos [ nLong; |HACIENDO nPos = nPos + 1;
               aSaca = nPos;
               aSaca = aSaca + "01";
               aLetra = aParametro % aSaca;
               |SI aLetra = "@";
                    nSwSegunda = 1;
               |SINO;
                    |SI nSwSegunda = 1;
                         aParte2 = aParte2 + aLetra;
                    |SINO;
                         aParte1 = aParte1 + aLetra;
                    |FINSI;
               |FINSI;
          |SIGUE;
          |SI aParte1 = ""; |O aParte2 = "";
               |ACABA;
          |FINSI;
          aPathDef = aParte1;
          aPathDat = aParte2;

          aMas = aPathDef + aNomDef;
          |CARGA_DEF aMas, referen@;
          |SI FSalida ! 0; |ACABA; |FINSI;

          |PATH_DAT #100 aPathDat;
          |ABRE #100;

          |DDEFECTO #100;
          aAlfa5 = "|K000";
          aClaves = #referen@#aAlfa5#;
          aTCompo = aClaves % 103;
          nTCompo = aTCompo;
          Iclave = clave1<-;
          |PARA nCampo = 1;  |SI nCampo [ 16;  |HACIENDO nCampo = nCampo + 1;
               clave = 0;
               Iclave = Iclave + 1;
          |SIGUE;

          Iclave = clave1<-;
          |PARA nCompo = 1;  |SI nCompo [ nTCompo;  |HACIENDO nCompo = nCompo + 1;
               nPosi = (nCompo * 300) + 103;
               aAlfa1 = aClaves % nPosi;
               clave = aAlfa1;
               Iclave = Iclave + 1;
          |SIGUE;

          nVoy = 1;
          Iclave = clave1<-;
          |PARA nCompo = 1;  |SI nCompo [ nTCompo;  |HACIENDO nCompo = nCompo + 1;
               nNumCam = clave;
               Iclave = Iclave + 1;

               Comodin = ("000" + nNumCam) % -103;
               Comodin = "|C" + Comodin;
               ||Comodin1 = Comodin + "ILON";
               Comodin1 = Comodin + "LONGITUD";
               Comodin1 = #referen@#Comodin1#;
               nTamanyo = Comodin1;
               aTamanyo = ("00" + nTamanyo) % -102;
               nSaca = (nVoy * 100) + aTamanyo;
               aSaca = aClau % nSaca;
               #referen@#nNumCam# = aSaca;
               nVoy = nVoy + nTamanyo;
          |SIGUE;
          |LEE 000#100.=;
          |SI FSalida = 0;
               |HAZ ControlCRM;
          |FINSI;

          |CIERRA #100;
          |DESCARGA_DEF #referen@;
     |FINSI;

     ||aMensaje = "0000Estoy en el DSERP." + aParametro;
     ||MENAV aMensaje;
|FPRO;

|PROCESO CopiaExe;
     aDestino = "c:\documentos\";
     |RMKDIR aDestino;
     aDestino = "c:\documentos\dserp\";
     |RMKDIR aDestino;
     aDestino = aDestino + "dsabreweb.exe";
     aContiene1 = aDestino;
     |ESPECIFICA "REMOTOMD5", aContiene;
     aVersion1 = FSalida;  |QBF aVersion1;

     |DBASE aBase;  |QBF aBase;
     aOrigen = aBase + "plugins/dsabreweb.exe";
     aContiene1 = aOrigen;
     |ESPECIFICA "MD5", aContiene;
     aVersion2 = FSalida;  |QBF aVersion2;

     |SI aVersion1 ! aVersion2;
         aIPRemoto = "";
         |IP_REMOTO aIPRemoto;  |QBT aIPRemoto;

         |SI aIPRemoto ! "";
             |ENVIA_FICHERO aOrigen, aDestino;
         |SINO;
             |COPIA_FICHERO aOrigen, aDestino;
         |FINSI;
     |FINSI;
|FPRC;



|PROCESO pbxpara3_hay;
     |SI #pbxpara3#4 = "*";
          nHayMarcaPBX = 1;
          |ERROR10;
     |FINSI;
|FPRC;

|DEFBUCLE pbxpara3_hay;
 #pbxpara3#1;
 ;
 aAplica, aFicheroDef,   0;
 aAplica, aFicheroDef, 225;
 ;
 NULL, pbxpara3_hay;
|FIN;

|PROCESO pbxpara3_tel;
     |SI #pbxpara3#4 = "*";
          nCampoVoy = #pbxpara3#2;
          aAux = #referen@#nCampoVoy#;
          |QBF aAux;
          |SI aAux ! "";
               aLlamarTel = aAux;
               nHayNumTelefono = nHayNumTelefono + 1;
               aNomCampo = #pbxpara3#3;
               |QBF aNomCampo;
               aNomCampo = aNomCampo - ":" - ":" - ":" - ":";
               aContenido = aNomCampo + ": " + aLlamarTel;
               |QBF aContenido;
               aPopup = aContenido;
               IaPopup = IaPopup + 1;
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE pbxpara3_tel;
 #pbxpara3#1;
 ;
 aAplica, aFicheroDef,   0;
 aAplica, aFicheroDef, 225;
 ;
 NULL, pbxpara3_tel;
|FIN;

|PROCESO ControlCRM
     nHayMarcaPBX = 0;
     |BUCLE pbxpara3_hay;
     |SI nHayMarcaPBX = 0;
          aMensaje = "0000Fichero " + aFicheroDef + " no parametrizado para Modulo Centralita";
          |MENAV aMensaje;
          |ACABA;
     |SINO;
          aLlamarTel = "";
          nHayNumTelefono = 0;
          IaPopup = aPopup1  <-;
          IaPopup = IaPopup + 1;
          IaPopup = IaPopup + 1;
          |BUCLE pbxpara3_tel;
          |SI nHayNumTelefono = 0;
               aMensaje = "0000No hay datos telefonicos en esta ficha";
               |MENAV aMensaje;
               |ACABA;
          |SINO;
               |SI nHayNumTelefono = 1;
                    ||Nada, ya tengo el numero de Telefono. (aLlamarTel)
               |SINO;
                    ||Hago el Trackpopup de Seleccion de Numeros
                    aPopup1 = "0";
                    aPopup2  = nHayNumTelefono;
                    IaPopup  = aPopup1  <-;
                    |ESPECIFICA "TRACKPOPUP", aPopup;
                    |SI FSalida ! 0;
                         aLlamarTel = "";
                         IaPopup  = aPopup3  <-;
                         IaPopup  = IaPopup + (FSalida - 1);
                         aAux = aPopup;
                         |QBF aAux;
                         aLong = aAux % A0;
                         nLong = aLong;
                         |PARA nVoy = 1; |SI nVoy [ nLong; |HACIENDO nVoy = nVoy + 1;
                              nSaca = 100 * nVoy;
                              nSaca = nSaca + 1;
                              nSaca = -nSaca;
                              aLetra = aAux % nSaca;
                              |SI aLetra ! ":";
                                   aLlamarTel = aLetra + aLlamarTel;
                              |SINO;
                                   |SAL;
                              |FINSI;
                         |SIGUE;
                         |QBT aLlamarTel;
                    |SINO;
                         |ACABA;
                    |FINSI;
               |FINSI;
               |HAZ CRMNumero;
               |ACABA;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO CRMNumero;

     |ABRE #pbxpara5;
     |DDEFECTO #pbxpara5;
     |LEE 000#pbxpara5.p;
     aHost = #pbxpara5#4;
     |QBF aHost;
     aPathCRM = #pbxpara5#5;
     |QBF aPathCRM
     |CIERRA #pbxpara5;

/*
     ||aEjecuta = "start iexplore http://" + aHost + "/" + aPathCRM + aLlamarTel;
     ||SISCO: 12.06.2008
     ||No abro directamente el iexplore porque parece que elimina variables
     ||de esta manera se respeta el navegador por defecto del sistema.
     ||A¤adido el cmd para que no muestre la pantalla negra del system.
     ||aEjecuta = "cmd " + &34 + "/c start http://" + aHost + "/" + aPathCRM + aLlamarTel + &34;
     ||aEjecuta = "start http://" + aHost + "/" + aPathCRM + aLlamarTel;
     ||Esta es la buena, aunque no va rapido
     ||aEjecuta = "cmd " + &34 + "/c c:\documentos\dserp\dsabreweb.exe http://" + aHost + "/" + aPathCRM + aLlamarTel + &34;
     ||Esto NO hace nada.
     ||aEjecuta = "cmd " + &34 + "/c c:\documentos\dserp\abre.bat" + &34;
     ||Tampoco va fino. Hace lo mismo.
     ||aEjecuta = "START c:\documentos\dserp\abre.bat";
*/


     ||Esta es la buena, VA SUPER RAPIDO. Aunque pega un mini pantallazo MSDOS
     aEjecuta = "START c:\documentos\dserp\dsabreweb.exe http://" + aHost + "/" + aPathCRM + aLlamarTel;

     |RSYSTEM aEjecuta;
|FPRC;
