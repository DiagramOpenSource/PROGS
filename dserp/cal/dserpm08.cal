|| Sera muy similar a lo que se hace en Observaciones en Proveedores.
||dsz99994 y dsm00018

|FICHEROS;
     dserpm08 #0;
     dserpm09;
     dserpm10 #10;
     docuerp@ #1000;

     nifpara3 #333;

     referen@ #100;

     dserpw09 #99;       ||Auxiliar Pantalla Vacia
|FIN;

|VARIABLES;
     aAuxDos = "";
     nSwVincula = 0;
     aValor1 = "";
     aValor2 = "";
     aSubParametro = "";
     aVacio30 = "";

     ||TODO CCC. Para quitar.
     aEmpresa = "";
     aPathFic = "";

     aModoAuto = "";
     &PRMNT_autoERP = "";

     &enSwVengoERP = 0;

     {4}aVentana   = "";

     aQueBoton = "";
     aID = "";
     nBotonQuitar = 0;

     aClaves = "";
     aTCompo = "";
     nTCompo = 0;
     aTCompo2 = "";
     nTCompo2 = 0;
     {16}clave = 0;      ||Array de 16 elementos para la clave.
     nCompo = 0;
     nPosi = 0;
     aTCampos = "";
     nTCampos = 0;
     aMas = "";
     aAlfa5 = "";
     aAlfa1 = "";
     nVoy = 0;
     nNumCam = 0;
     Comodin = "";
     Comodin1 = "";
     nTamanyo = 0;
     aTamanyo = ""
     nSaca = 0;

     nHayMarcaNIF = 0;
     aCodNifCif = "";
     nHayCifNif = 0;
     nCampoVoy = 0;
     aAux = "";

     nSwArchi = 0;
     aPath = "";
     aAplica = "";
     aBase = "";
     aBass = "";
     aAplica2 = "";
     aNumEmp = "";
     nEmpresa = 0;

     &enOpcion = 0;

     {-1}aMenu  = "";
         aMenu1 = "2400";
         aMenu2 = "1";
         aMenu3 = "Elija: ";
         aMenu4 = "ODS";
         aMenu5 = " Observaciones ";
         aMenu6 = "   Documentos  ";
         aMenu7 = "     Salir     ";
     nCalc   = 0;
     nOpcion = 0;
     nSalida = 0;
     nSwModoConsulta = 0;
     nCampo = 0;
     nDos = 0;
     aClau = "";
     aNomDef = "";
     aPathDef = "";
     aPathDat = "";
     aPathPan = "";
     aMensaje = "";
     aObserva = "";
     nHay      = 0;
     &eaFuerza = "";
     &aClave   = "";
     &aMenClave= "";
     aParametro     = "";
     aDirectorio    = "";
     aNFichero      = "";
     aVariableCarga = "";
     aFichero       = "";
     aAlfa          = "";

     nVariable      = 0;
     nBaffer        = 0;
     nTotalLinea    = 0;
     nTope          = 0;
     nVari1         = 0;
     nVari2         = 0;
     nBtnEdi        = 0;
     nBtnAlt        = 0;
     nBtnMod        = 0;
     nBtnBaj        = 0;

     &eaCodigo      = "";
     &eaTipoCodigo  = "";
     &eaNombreList  = "";
     &enCuadroIni   = 0;
     &enCuadroFin   = 0;
     &enCampoAncho  = 0;
     &enModoEdita   = 0;
     &enTotalVaria  = 0;

{16}&epPuntEdita   = "";

     nLineas = 16;
     &eaNomDef = "";
     &PRMNT_Def = "";
     aParte1 = "";
     aParte2 = "";
     nSwSegunda = 0;
     nPos = 0;
     aLong = "";
     nLong = 0;
     aSaca = "";
     aLetra = "";

     {6}aPopup   = "";
     nContador = 0;

     &eaCmp7 = "";
     &enCmp6 = "";
     &eaCmp0 = "";
     &eaCmp1 = "";
|FIN;

|PROCESO GrabaSecuencial;
     IepPuntEdita  = epPuntEdita1 <-;
     |PARA nTotalLinea = 0;  |SI nTotalLinea [ enTotalVaria;  |HACIENDO nTotalLinea = nTotalLinea + 1;
          |AL_BUF nBaffer, nTotalLinea, epPuntEdita;
          IepPuntEdita = IepPuntEdita + 1;
     |SIGUE;
|FPRC;

|PROCESO Edicion;
     nTope  = nTotalLinea - 1;
     nVari1 = 0;
     nVari2 = 0;
     enModoEdita = enModoEdita + 100;
     |EDITA nBaffer, 1, nTope, enCuadroIni, enCuadroFin, enModoEdita, eaNombreList, enCampoAncho, nVari1, nVari2;
     enModoEdita = enModoEdita $ 100;
|FPRC;

|PROCESO CogePuntero;
     IepPuntEdita  = epPuntEdita1 <-;
     |PARA nTotalLinea = 0;  |SI nTotalLinea [ enTotalVaria;  |HACIENDO nTotalLinea = nTotalLinea + 1;
          |DEL_BUF nBaffer, nTotalLinea, epPuntEdita;
          IepPuntEdita = IepPuntEdita + 1;
     |SIGUE;
|FPRC;

|PROCESO Edita;
     |PUSHV 0501 2380;
     |DIMENSIONA  200, nBaffer, 0;

     |HAZ GrabaSecuencial;
     |HAZ Edicion;
     |HAZ CogePuntero;

     |FINDIM nBaffer;
     |POPV;
|FPRC;
컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
|PROCESO PintaCampos;
     |BLANCO 0611 2070;
     |PINPA #0#0;
     |ABRE #dserpm10;
     |DDEFECTO #dserpm10;
     #dserpm10#19 = aAplica;
     #dserpm10#18 = nEmpresa;
     #dserpm10#0 = aNomDef;
     #dserpm10#1 = aClau;
     |LEE 000#dserpm10.=;
     |PARA nCampo = 0; |SI nCampo [ 15; |HACIENDO nCampo = nCampo + 1;
          nDos = nCampo + 2;
          #0#nCampo# = #dserpm10#nDos#;
     |SIGUE;
     |PINDA #0#0;
     |CIERRA #dserpm10;
|FPRC;

컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
|PROCESO IniciaEdicion;
     |ABRE #dserpm10;
     |DDEFECTO #dserpm10;
     #dserpm10#19 = aAplica;
     #dserpm10#18 = nEmpresa;
     #dserpm10#0 = aNomDef;
     #dserpm10#1 = aClau;
     |LEE 101#dserpm10.=;
     nSalida = FSalida;
     |PARA nVariable = 1;  |SI nVariable [ nLineas;  |HACIENDO nVariable = nVariable + 1;
           IepPuntEdita  = epPuntEdita1 <-;
           IepPuntEdita = IepPuntEdita + (nVariable - 1);

           nDos = nVariable + 1;        ||Empieza en el 2
           epPuntEdita  = #dserpm10#nDos#;
           #dserpm10#nDos# = "";
     |SIGUE;
     |SI nSalida = 0;
          |GRABA 020#dserpm10.a;
     |FINSI;
     |LIBERA #dserpm10;
     |CIERRA #dserpm10;

     |PUSHV  0501 2380;
     |BLANCO 0811 2071;
     |ATRI R;  |PINTA 0511, "   Observaciones   ";  |ATRI 0;

     |HAZ Edita;
     |POPV;

     |ABRE #dserpm10;
     |DDEFECTO #dserpm10;
     #dserpm10#19 = aAplica;
     #dserpm10#18 = nEmpresa;
     #dserpm10#0 = aNomDef;
     #dserpm10#1 = aClau;
     |LEE 101#dserpm10.=;
     nSalida = FSalida;
     |PARA nVariable = 1;  |SI nVariable [ nLineas;  |HACIENDO nVariable = nVariable + 1;
           IepPuntEdita = epPuntEdita1 <-;
           IepPuntEdita = IepPuntEdita + (nVariable - 1);

           nDos = nVariable + 1;
           #dserpm10#nDos# = epPuntEdita;
     |SIGUE;
     |SI nSalida = 0;
           |GRABA 020#dserpm10.a;
     |SINO;
           |GRABA 020#dserpm10.n;
     |FINSI;
     |LIBERA #dserpm10;
     |CIERRA #dserpm10;
|FPRC;

|RUTINA infdoc;
   #dserpm09#7 = aAplica;
   #dserpm09#6 = nEmpresa;
   #dserpm09#0 = aNomDef;
   #dserpm09#1 = aClau;
   #dserpm09#2 = 01;
|FRUT;

|RUTINA supdoc;
   #dserpm09#7 = aAplica;
   #dserpm09#6 = nEmpresa;
   #dserpm09#0 = aNomDef;
   #dserpm09#1 = aClau;
   #dserpm09#2 = 99;
|FRUT;

|PROCESO EsArchi;
     |DBASS aPath; |QBF aPath;
     aPath = aPath + "dsarchi/def/dsarm001.mas";
     |XABRE "E", aPath, 0;
     |SI FSalida = 0;
          nSwArchi = 1;
     |FINSI;
|FPRC;

|PROCESO nifpara3_hay;
     |SI #nifpara3#4 = "*";
          nHayMarcaNIF = 1;
          |ERROR10;
     |FINSI;
|FPRC;

|DEFBUCLE nifpara3_hay;
 #nifpara3#1;
 ;
 aAplica, aNomDef,   0;
 aAplica, aNomDef, 225;
 ;
 NULL, nifpara3_hay;
|FIN;

|PROCESO nifpara3_tel;
     |SI #nifpara3#4 = "*";
          nCampoVoy = #nifpara3#2;
          aAux = #referen@#nCampoVoy#;
          |QBF aAux;
          |SI aAux ! "";
               aCodNifCif = aAux;
               nHayCifNif = 1;
               |ERROR10;
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE nifpara3_tel;
 #nifpara3#1;
 ;
 aAplica, aNomDef,   0;
 aAplica, aNomDef, 225;
 ;
 NULL, nifpara3_tel;
|FIN;


|PROCESO MenuNuevo;
     || aPopup1 = "0";   Posicion del Raton
     || aPopup1 = "-1";  Posicion del Cursor
     || Con el &002 hace un separador.
     || Con el &003 disabla la lineal
     || aPopup4 = &002 o aPopup4 = &003

     aPopup1 = "0";  || Si fuera -1 en la posicion
     aAuxDos = aAplica;
     |QBF aAuxDos;
     |SI aAuxDos = "dsman9";
          aPopup2 = 3;
          aPopup3 = " Observaciones ";
          aPopup4 = " Documentos    ";
          aPopup5 = " Salir         ";
     |SINO;
          |SI nSwArchi = 0;
               aPopup2 = 3;
               aPopup3 = " Observaciones ";
               aPopup4 = " Documentos    ";
               aPopup5 = " Salir         ";
          |SINO;
               aPopup2 = 4;
               aPopup3 = " Observaciones     ";
               aPopup4 = " Documentos        ";
               aPopup5 = " Archivo Documental (GAD)";
               aPopup6 = " Salir             ";
          |FINSI;
     |FINSI;

     IaPopup  = aPopup1  <-;
     |ESPECIFICA "TRACKPOPUP", aPopup;
     |SI FSalida ! 0;
          nOpcion  = FSalida;
     |FINSI;
|FPRC;

|PROCESO Opcion3;
     |SI nSwArchi = 0; |ACABA; |FINSI;

     nSwVincula = 0;
     |SI aNomDef = "con00601"; |Y aAplica = "dscomer9";
          ||TALLER
          nSwVincula = 1;
          aVacio30 = " " * 30;
          || 7 digitos de "VINCULA
          || 8 del tipo de vinculacion  "TALLER" -> "ESTALLER"
          || A30 Valor Vinculado 1.  Empresa.
          || A30 Valor Vinculado 2.  Matricula
          aValor1 = ("00000" + nEmpresa) % -105;
          aValor1 = (aValor1 + aVacio30) % 130;
          aValor2 = aClau;
          |QBF aValor2;
          aValor2 = (aValor2 + aVacio30) % 130;

          aSubParametro = "VINCULA" + "ESTALLER" + aValor1 + aValor2;
     |FINSI;

     |SI nSwVincula = 1;
          aAlfa = "|:dsarchi/dsarm005;" + aSubParametro;
          |SUB_EJECUTA_NP aAlfa;
     |SINO;
          nHayMarcaNIF = 0;
          |BUCLE nifpara3_hay;
          |SI nHayMarcaNIF = 0;
               aMensaje = "0000Fichero " + aNomDef + " no parametrizado para Archivo Documental" + &13 + "(Datos NIF/CIF)";
               ||aAux = aClau;
               |||QBF aAux;
               ||aMensaje = "0000Fichero " + aNomDef + ":" + aAplica + ":" + nEmpresa + ":" + aAux + " no parametrizado para Archivo Documental";
               |MENAV aMensaje;
               |ACABA;
          |SINO;
               aMas = aPathDef + aNomDef;
               |CARGA_DEF aMas, referen@;
               |SI FSalida ! 0; |ACABA; |FINSI;

               |PATH_DAT #100 aPathDat;
               |ABRE #100;

               |DDEFECTO #100;
               aAlfa5 = "|K000";
               aClaves = #referen@#aAlfa5#;
               aTCompo = aClaves % 103;
               nTCompo = aTCompo;
               Iclave = clave1<-;
               |PARA nCampo = 1;  |SI nCampo [ 16;  |HACIENDO nCampo = nCampo + 1;
                    clave = 0;
                    Iclave = Iclave + 1;
               |SIGUE;

               Iclave = clave1<-;
               |PARA nCompo = 1;  |SI nCompo [ nTCompo;  |HACIENDO nCompo = nCompo + 1;
                    nPosi = (nCompo * 300) + 103;
                    aAlfa1 = aClaves % nPosi;
                    clave = aAlfa1;
                    Iclave = Iclave + 1;
               |SIGUE;

               nVoy = 1;
               Iclave = clave1<-;
               |PARA nCompo = 1;  |SI nCompo [ nTCompo;  |HACIENDO nCompo = nCompo + 1;
                    nNumCam = clave;
                    Iclave = Iclave + 1;

                    Comodin = ("000" + nNumCam) % -103;
                    Comodin = "|C" + Comodin;
                    ||Comodin1 = Comodin + "ILON";
                    Comodin1 = Comodin + "LONGITUD";
                    Comodin1 = #referen@#Comodin1#;
                    nTamanyo = Comodin1;
                    aTamanyo = ("00" + nTamanyo) % -102;
                    nSaca = (nVoy * 100) + aTamanyo;
                    aSaca = aClau % nSaca;
                    #referen@#nNumCam# = aSaca;
                    nVoy = nVoy + nTamanyo;
               |SIGUE;
               |LEE 000#100.=;
               |SI FSalida = 0;
                    aCodNifCif = "";
                    nHayCifNif = 0;
                    |BUCLE nifpara3_tel;
                    |SI nHayCifNif = 0;
                         aMensaje = "0000No hay datos NIF/CIF en esta ficha";
                         |MENAV aMensaje;
                         |ACABA;
                    |SINO;
                         |SI nHayCifNif = 1;
                              aAlfa = aCodNifCif;  |QBF aAlfa;
                              |SI aAplica = "laboral ";
                                   aAlfa = aAlfa + "laboral";
                              |FINSI;
                              |SI aAlfa ! "";
                                   aAlfa = "|:dsarchi/dsarm005;" + aAlfa;
                                   |SUB_EJECUTA_NP aAlfa;
                              |FINSI;
                         |FINSI;
                    |FINSI;
               |FINSI;
               |CIERRA #100;
               |DESCARGA_DEF #referen@;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Opcion2;
/*  No funcina bien

     |SI enSwVengoERP = 0;
         aVentana1 = "";         || Posicion Inicial
         aVentana2 = "";         || Posicion Final
         aVentana3 = "1";        ||
         aVentana4 = "Documentos"; || Titulo
         |ESPECIFICA "POPUPMODE", aVentana;
      |SINO;
         |PUSHV 0501 2680;
      |FINSI;
*/

      |PUSHV 0501 2380;

      |HAZ EsAparecida;
      |SI FSalida = 0;
          |C_V #dserpm09#9, 1, "S";
          |C_M #dserpm09#9, 1, "S";
      |SINO;
          |C_V #dserpm09#9, 1, "N";
          |C_M #dserpm09#9, 1, "N";
      |FINSI;

      |CONTROL_SIMPLE 0, "BOTON,F5 Editar Doc", 2446, 2456, Edi;
      nBtnEdi = FSalida;

      |CONTROL_SIMPLE 0, "BOTON,F2 Alta", 2458, 2463, Alt;
      nBtnAlt = FSalida;

      |CONTROL_SIMPLE 0, "BOTON,F3 Modificar", 2465, 2472, Mod;
      nBtnMod = FSalida;

      |CONTROL_SIMPLE 0, "BOTON,F4 Baja", 2474, 2479, Baj;
      nBtnBaj = FSalida;

      eaCmp7 = aAplica;
      enCmp6 = nEmpresa;
      eaCmp0 = aNomDef;
      eaCmp1 = aClau;

      |ENTLINEAL #1#dserpm09, -5, 4, 19, 1, infdoc, supdoc;

      |FIN_CONTROL_WINDOWS nBtnAlt;
      |FIN_CONTROL_WINDOWS nBtnMod;
      |FIN_CONTROL_WINDOWS nBtnBaj;
      |FIN_CONTROL_WINDOWS nBtnEdi;

      |POPV;

/*
      |SI enSwVengoERP = 0;
          |ESPECIFICA "FIN_POPUPMODE", aVentana;
      |SINO;
          |POPV;
      |FINSI;
*/
|FPRC;

|PROCESO Opcion1;
     enTotalVaria  = nLineas;
     eaNombreList  = "";
     enCuadroIni   = 0611;
     enCuadroFin   = 2070;
     enCampoAncho  = 60;
     enModoEdita   = 0;

     ||Podemos poner un control por usuarios, para permitir o no
     ||a determinados usuarios acceder en modo EDICION o CONSULTA
     ||Tb. se puede finar mas fino, poniendo este acceso por DEF.
     ||Por ahora voy a ponerlo en modo edicion.
     nSwModoConsulta = 0;

     |SI nSwModoConsulta = 1;
        |PUSHV 0501 2380;
        |HAZ PintaCampos;
        |PAUSA;
        |POPV;
     |SINO;
        |HAZ IniciaEdicion;
     |FINSI;
|FPRC;

|PROGRAMA;
/*
     aMensaje = "0000Ya estoy en el ERP";
     |MENAV aMensaje;
     |ACABA;
*/

||DEBUG;

     aParametro = PARAMETRO;

     |QBF aParametro;
     |SI aParametro = "";
          |ACABA;
     |SINO;
          ||TODO CCC.  Para ver pq. peta tan facilmente.

          |SI aParametro = "UNO";
               aAplica = "dscomer9";
               aEmpresa = "00006";
               aNomDef = "agifa024";
               aClave = "000003                                                                                              ";
               aPathDef = "/u/dsempres/dscomer9/def/";
               aPathFic = "/u/dsempres/dscomer9/fich/00006/";
               aParametro = aAplica + aEmpresa + aNomDef + aClave + aPathDef + "@" + aPathFic;
          |FINSI;
     |FINSI;

     aAplica = aParametro % 108;
     aNumEmp = aParametro % 905;
     nEmpresa = aNumEmp;
     aNomDef = aParametro % 1408;
     aParte1 = aParametro % 2250;
     aParte2 = aParametro % 7250;
     aClau = aParte1 + aParte2;

     aLong = aParametro % A0;
     nLong = aLong;

     nSwSegunda = 0;
     aParte1 = "";
     aParte2 = "";
     |PARA nPos = 122; |SI nPos [ nLong; |HACIENDO nPos = nPos + 1;
          aSaca = nPos;
          aSaca = aSaca + "01";
          aLetra = aParametro % aSaca;
          |SI aLetra = "@";
               nSwSegunda = 1;
          |SINO;
               |SI nSwSegunda = 1;
                    aParte2 = aParte2 + aLetra;
               |SINO;
                    aParte1 = aParte1 + aLetra;
               |FINSI;
          |FINSI;
     |SIGUE;
     |SI aParte1 = ""; |O aParte2 = "";
          |ACABA;
     |FINSI;
     aPathDef = aParte1;
     aPathDat = aParte2;

     nSwArchi = 0;
     |HAZ EsArchi;

     aModoAuto = PRMNT_autoERP;
     |QBF aModoAuto;
     |SI aModoAuto = "AUTODOCS";
          enOpcion = 2;
     |FINSI;

     |SI enOpcion = 0;
         |HAZ MenuNuevo;
     |SINO;
         nOpcion = enOpcion;
     |FINSI;

     |SI nOpcion = 1;  |HAZ Opcion1;  |FINSI;
     |SI nOpcion = 2;  |HAZ Opcion2;  |FINSI;
     |SI nOpcion = 3;  |HAZ Opcion3;  |FINSI;
|FPRO;
