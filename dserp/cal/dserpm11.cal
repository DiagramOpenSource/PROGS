||Parametros:

||Ahora tiene 4 formatos:

||4) TFNOINTEGRAL:numCliente  (se pasa el numCliente (Alfa)) para
||   ir al listin telefonico de la integral y permitir que se llame
||   a los numeros de telefonos indicados. PopUp si hay de uno.
||1) LLAMATEL:TEL    (llama directamente a ese numero de telefono)
||2) MULTITEL:NOM:TEL@NOM:TEL@NOM:TEL  (Saca menu popup para selecciona numero de telefono)
||3) Formato como hasta ahora.
     ||3 .. integral00000dsam00005                                                                                                   /u/dsprofes/integral/def/@/u/dsprofes/integral/fich/


||Tiquet. 2141.98
||En la intregral, los telefonos, solo se almacenan los 2 primeros en la ficha
||del cliente, (dsam0000) el resto estan en un cabecera-lineas (dsam0107).
||Cuando me venga este caso, mirare si tengo configurado el dsam0107:
||sino --> actuo como siempre.
||Si ----> Tengo que obtener los numero de telefonos del dsam0107.


|FICHEROS;
     pbxpara3 #333;      ||PBX Para. Telefono (pbxpara3)

     referen@ #100;
     pbxpara5 #5;
     pbxpara6 #6;
     pbxpara9 #9;

     dsam0000@ #100;
     dsam0107@ #107;
|FIN;

|VARIABLES;
     aClienteInt = "";
     nClienteInt = 0;
     aPathBass = "";
     aPathIntegral = "";
     aPathDefInt = "";
     aFicDef = "";
     nHandleE = 0;
     aNombreCli = "";

     nContador = 0;
     nCentralita = 0;

     aAux3 = "";
     nHandleLee = 0;
     aLinea = "";

     nCliente = 0;
     aNomCli = "";
     aAux2 = "";

     aSwSuprimirCero = "";
     nYata = 0;
     aAuxNumero = "";


     nVoyPrimero = 0;
     nEstoy = 0;

     nLongTotal = 0;
     aNomTele = "";
     aNumTele = "";
     aDoblete = "";
     aRestoPara = "";
     aAuxPara = "";
     aNomCampo = "";
     nError = 0;
     nCorrecto = 0;
     aRespuesta = "";

     aBaseLocal = "";
     aVersion1 = "";
     aVersion2 = "";
     aIPRemoto = "";
     aOrigen = "";
     aDestino = "";
     aBase = "";

     comando = "";
     a = "";
     nn = 0;
     nExiste = 0;
     i = 0;

     aContenido = "";

     nCampoVoy = 0;
     aAux = "";
     aFicheroDef = "";
     nHayMarcaPBX = 0;
     aLlamarTel = "";
     nHayNumTelefono = 0;
     aMas = "";
     aMensaje = "";
     aParametro = "";
     aAplica = "";
     aNumEmp = "";
     nEmpresa = 0;
     aNomDef = "";
     aParte1 = "";
     aParte2 = "";
     aClau = "";
     aLong = "";
     nLong = 0;
     nSwSegunda = 0;
     nPos = 0;
     aSaca = "";
     aLetra = "";
     aPathDef = "";
     aPathDat = "";

     nTamanyo            = 0;
     nSaca               = 0;
     nTCompo             = 0;
     nTCompo2            = 0;
     nCompo              = 0;
     nPosi               = 0;
     nTCampos            = 0;
     nCampo              = 0;
     nVoy                = 0;
     nNumCam             = 0;
     nHandle             = 0;
     nConta              = 0;
     nPausa              = 0;

     aTamanyo            = "";
     aAlfa5              = "";
     aClaves             = "";
     aTCompo             = "";
     aTCompo2            = "";
     aTCampos            = "";
     aAlfa1              = "";
     Comodin             = "";
     Comodin1            = "";
     aUsuario            = "";
     aExtension          = "";
     aHost               = "";
     aUsuAM              = "";
     aPassAM             = "";
     aPathLlam           = "";
     aPathLog            = "";
     aFicRes             = "";
     aBat                = "";
     aEjec               = "";
     aAlfa               = "";
     aEjecuta            = "";
     aAbre               = "";
     aHora               = "";

     fFecHoy             = @;

     {1}aContiene        = "";
     {1}aLocal           = "";
     {16}clave           = 0;      ||Array de 16 elementos para la clave.
  {1000}aPopup           = "";
|FIN;

|PROCESO Llamar_Click2;
     fFecHoy = ~;
     |HORA aHora;

     aFicRes = (fFecHoy % -104);
     aFicRes = aFicRes + (fFecHoy % 402);
     aFicRes = aFicRes + (fFecHoy % 102);
     aFicRes = aFicRes + (aHora % 102);
     aFicRes = aFicRes + (aHora % 402);
     aFicRes = aFicRes + (Usuario % 810);  |QBF aFicRes;
     aFicRes = aFicRes + ".txt";

     aBat    = aPathLlam + "llama.bat";
     |XABRE "WC", aBat, nHandle;

     aEjec = &34 + #pbxpara5#8;  |QBF aEjec;
     aEjec = aEjec + "?tipo=1&extension=" + aExtension;
     aEjec = aEjec + "&telefono=" + aLlamarTel + &34;
     aEjec = aEjec + "> " + aPathLlam + aFicRes;

     aAlfa = aPathLlam + "ds_eje_php " +  aEjec + &13 + &10;
     |XGRABA nHandle, aAlfa;

     |XCIERRA nHandle;

     aEjecuta = "START " + aPathLlam + "dsmini.exe " + aBat;
     |RSYSTEM aEjecuta;

     |PUSHV 0501 2380;
     |BLANCO 0501 2380;
     |CUADRO 1430 1650;
     |ATRI S;
     |PINTA 1532, "Realizando llamada";
     |ATRI 0;

     nConta = 0;
     aAbre  = aPathLlam + aFicRes;
     |PARA;  |SI;  |HACIENDO;
             |PARA nPausa = 1; |SI nPausa [ 10000; |HACIENDO nPausa = nPausa + 1;
             |SIGUE;

             |XABRE "UC", aAbre, nHandle;
             |SI FSalida ] 0;
                 |XCIERRA nHandle;
                 |SAL;
             |FINSI;
             |XCIERRA nHandle;

             |PULSATECLA;
     |SIGUE;

     aOrigen  = aPathLlam + aFicRes;
     aDestino = aPathLog +  aFicRes;
     aIPRemoto = "";
     |IP_REMOTO aIPRemoto;  |QBT aIPRemoto;

     |SI aIPRemoto ! "";
         |RECIBE_FICHERO aOrigen, aDestino;
     |SINO;
         |COPIA_FICHERO aOrigen, aDestino;
     |FINSI;

     |RFBORRA aOrigen;
     |RFBORRA aBat;

     |POPV;
|FPRC;

|PROCESO ResultadoCecom;
     nError = 0;
     nCorrecto = 0;
     aRespuesta = "";

     aAux = comando % 105;
     |SI aAux = "ERROR";
          nError = 1;
          aRespuesta = comando % 7;
     |SINO;
          aAux = comando % 102;
          |SI aAux = "OK";
               nCorrecto = 1;
               aRespuesta = comando % 4;
          |SINO;
               aRespuesta = comando;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Llamar_Cecom;
     aExtension = "";
     ||aLlamarTel

     comando = "TMAVISO:100";
     |ALFACALLDLL "cecomsk.dll","cecomsk",comando;

     comando = "CANAL:" + aExtension;   || lo primero ... quien somos
     |ALFACALLDLL "cecomsk.dll","cecomsk",comando;

     comando = "CONECTAR:" + aHost;
     |ALFACALLDLL "cecomsk.dll","cecomsk",comando;
     |HAZ ResultadoCecom;
     |SI nError = 1;
         aMensaje = "0000ERROR " + aRespuesta;
         |MENAV aMensaje;
         |ACABA;
     |FINSI;

     comando = "LOGIN:" + aUsuAM + "," + aPassAM;
     |ALFACALLDLL "cecomsk.dll","cecomsk",comando;
     |HAZ ResultadoCecom;
     |SI nError = 1;
         aMensaje = "0000ERROR " + aRespuesta;
         |MENAV aMensaje;
         |ACABA;
     |FINSI;

     comando = "TMAVISO:300";
     |ALFACALLDLL "cecomsk.dll","cecomsk",comando;

     comando = "LLAMA:" + aLlamarTel;
     |ALFACALLDLL "cecomsk.dll","cecomsk",comando;
     |HAZ ResultadoCecom;
     |SI nError = 1;
         aMensaje = "0000ERROR " + aRespuesta;
         |MENAV aMensaje;
     |FINSI;

     comando = "TMAVISO:100";
     |ALFACALLDLL "cecomsk.dll","cecomsk",comando;

     comando = "LOGOFF";
     |ALFACALLDLL "cecomsk.dll","cecomsk",comando;

     comando = "TMAVISO:20";
     |ALFACALLDLL "cecomsk.dll","cecomsk",comando;
|FPRC;

|PROCESO LLamarNumero;
     |SI #pbxpara5#7 = "N";
         |HAZ Llamar_Cecom;
     |FINSI;

     |SI #pbxpara5#7 = "S";
         |HAZ Llamar_Click2;
     |FINSI;
|FPRC;

|PROCESO SuprimirCerosIzquierda;
     |SI aSwSuprimirCero ! "S";
         |ACABA;
     |FINSI;

     nYata = 0;
     aAuxNumero = "";
     |QBT aLlamarTel;

     aLong = aLlamarTel % A0;
     nLong = aLong;
     |PARA nContador = 1; |SI nContador [ nLong; |HACIENDO nContador = nContador + 1;
           nSaca = (100 * nContador) + 1;
           aSaca = nSaca;
           aLetra = aLlamarTel % aSaca;
           |SI aLetra ! "0";
               nYata = 1;
               aAuxNumero = aAuxNumero + aLetra;
           |SINO;
               |SI nYata = 1;
                   aAuxNumero = aAuxNumero + aLetra;
               |FINSI;
           |FINSI;
     |SIGUE;

     aLlamarTel = aAuxNumero;
|FPRC;

|PROCESO SacaDoblete;
     aLong = aDoblete % A0;
     nLong = aLong;
     nVoyPrimero = 1;

     |PARA nEstoy = 1; |SI nEstoy [ nLong; |HACIENDO nEstoy = nEstoy + 1;
          aSaca = nEstoy;
          aSaca = aSaca + "01";
          aLetra = aDoblete % aSaca;
          |SI aLetra = ":";
               nVoyPrimero = 0;
          |FINSI;

          |SI aLetra ! ":";
               |SI nVoyPrimero = 1;
                    aNomTele = aNomTele + aLetra;
               |SINO;
                    aNumTele = aNumTele + aLetra;
               |FINSI;
          |FINSI;
     |SIGUE;

     ||Devuelve
     ||aNomTele
     ||aNumTele
|FPRC;

|PROCESO pbxpara3_hay;
     |SI #pbxpara3#4 = "*";
          nHayMarcaPBX = 1;
          |ERROR10;
     |FINSI;
|FPRC;

|DEFBUCLE pbxpara3_hay;
     #pbxpara3#1;
     ;
     aAplica, aFicheroDef,   0;
     aAplica, aFicheroDef, 255;
     ;
     NULL, pbxpara3_hay;
|FIN;

|PROCESO ListinTfno;
     aLlamarTel = #dsam0107@#4;
     |QBF aLlamarTel;
     |SI aLlamarTel = "";
         |ACABA;
     |FINSI;

     |HAZ SuprimirCerosIzquierda;
     nHayNumTelefono = nHayNumTelefono + 1;
     aNomCampo = #dsam0107@#2;
     aNomCampo = aNomCampo % 140;  |QBF aNomCampo;
     aNomCampo = aNomCampo - ":" - ":" - ":" - ":";
     aContenido = aNomCampo + ": " + aLlamarTel;   |QBF aContenido;

     aPopup = aContenido;
     IaPopup = IaPopup + 1;

     aNumTele = aLlamarTel;
|FPRC;

|DEFBUCLE ListinTfno;
     #dsam0107@#1002;
     ;
     aNombreCli, 01;
     aNombreCli, 99;
     ;
     NULL, ListinTfno;
|FIN;

|PROCESO LlamaClienteIntegral;
     |DBASS aPathBass;
     |QBF aPathBass;

     aPathIntegral = aPathBass + "integral/";
     aPathDefInt   = aPathIntegral + "def/";
     aFicDef       = aPathDefInt + "dsam0000.mas";

     |XABRE "E", aFicDef, nHandleE;
     |SI FSalida < 0;
          aMensaje = "0000Aplicacion Integral NO instalada en el sistema";
          |MENAV aMensaje;
     |FINSI;

     |CARGA_DEF aFicDef, dsam0000@;
     |SI FSalida ! 0;
          aMensaje = "0000Error accediendo al fichero " + &13 + aFicDef;
          |MENAV aMensaje;
          |ACABA;
     |FINSI;

     nClienteInt = aClienteInt;

     |ABRE #dsam0000@;
     |DDEFECTO #dsam0000@;
     #dsam0000@#0 = nClienteInt;
     |LEE 000#dsam0000@.=;
     |SI FSalida ! 0;
         aMensaje = "0000El cliente [" + aClienteInt + "] no existe en la Aplicacion Integral";
         |MENAV aMensaje;
         |CIERRA #dsam0000@;
         |DESCARGA_DEF #dsam0000@;
         |ACABA;
     |SINO;
         aNombreCli = #dsam0000@#1;
     |FINSI;
     |CIERRA #dsam0000@;

     |DESCARGA_DEF #dsam0000@;

     aFicDef = aPathDefInt + "dsam0107.mas";
     |CARGA_DEF aFicDef, dsam0107@;
     |SI FSalida ! 0;
          aMensaje = "0000Error accediendo al fichero " + &13 + aFicDef;
          |MENAV aMensaje;
          |ACABA;
     |FINSI;

     ||Cargo el puntero con los telefonos.
     aLlamarTel = "";
     nHayNumTelefono = 0;
     IaPopup = aPopup1  <-;
     IaPopup = IaPopup + 1;
     IaPopup = IaPopup + 1;

     ||ARA39
     aNomTele = "";
     aNumTele = "";
     nHayNumTelefono = 0;
     |BUCLE ListinTfno;

     |SI nHayNumTelefono = 0;
          aMensaje = "0000No hay numeros de telefonos en el Listin Telefonico de la Integral";
          |MENAV aMensaje;
          |ACABA;
     |FINSI;

     |SI nHayNumTelefono = 1;
          aLlamarTel = aNumTele;  |QBT aLlamarTel;
          |HAZ LLamarNumero;
          |ACABA;
     |FINSI;

     aPopup1 = "0";
     aPopup2  = nHayNumTelefono;
     IaPopup  = aPopup1  <-;
     |ESPECIFICA "TRACKPOPUP", aPopup;
     |SI FSalida ! 0;
         aLlamarTel = "";
         IaPopup  = aPopup3  <-;
         IaPopup  = IaPopup + (FSalida - 1);
         aAux = aPopup;  |QBF aAux;
         aLong = aAux % A0;
         nLong = aLong;
         |PARA nVoy = 1; |SI nVoy [ nLong; |HACIENDO nVoy = nVoy + 1;
               nSaca = 100 * nVoy;
               nSaca = nSaca + 1;
               nSaca = -nSaca;
               aLetra = aAux % nSaca;
               |SI aLetra ! ":";
                   aLlamarTel = aLetra + aLlamarTel;
               |SINO;
                   |SAL;
               |FINSI;
         |SIGUE;
         |QBT aLlamarTel;

         |HAZ LLamarNumero;
     |FINSI;
     |DESCARGA_DEF #dsam0000@;
|FPRC;

|PROCESO LLamaUnTelefono;
     aLlamarTel = aRestoPara;  |QBF aLlamarTel;
     |HAZ SuprimirCerosIzquierda;
     |SI aLlamarTel = "";
         |ACABA;
     |FINSI;

     |HAZ LLamarNumero;
|FPRC;

|PROCESO LLamaMultiTel;
     aLlamarTel = "";
     nHayNumTelefono = 0;

     IaPopup = aPopup1  <-;
     IaPopup = IaPopup + 1;
     IaPopup = IaPopup + 1;
     aLong = aRestoPara % 0;
     nLongTotal = aLong;

     |PARA nVoy = 1; |SI nVoy [ nLongTotal; |HACIENDO nVoy = nVoy + 1;
          aSaca = nVoy;
          aSaca = aSaca + "01";
          aLetra = aRestoPara % aSaca;
          |SI aLetra = "@";
               aNomTele = "";
               aNumTele = "";
               |HAZ SacaDoblete;

               |SI aNumTele ! "";
                    aLlamarTel = aNumTele;
                    |HAZ SuprimirCerosIzquierda;
                    aNumTele = aLlamarTel;
                    aLlamarTel = "";

                    nHayNumTelefono = nHayNumTelefono + 1;
                    aContenido = aNomTele + ": " + aNumTele;
                    |QBF aContenido;
                    aPopup = aContenido;
                    IaPopup = IaPopup + 1;
               |FINSI;

               aDoblete = "";
          |SINO;
               aDoblete = aDoblete + aLetra;
          |FINSI;
     |SIGUE;

     |SI aDoblete ! "";
          aNomTele = "";
          aNumTele = "";
          |HAZ SacaDoblete;
          |SI aNumTele ! "";
               aLlamarTel = aNumTele;
               |HAZ SuprimirCerosIzquierda;
               aNumTele = aLlamarTel;
               aLlamarTel = "";

               nHayNumTelefono = nHayNumTelefono + 1;
               aContenido = aNomTele + ": " + aNumTele;
               |QBF aContenido;
               aPopup = aContenido;
               IaPopup = IaPopup + 1;
          |FINSI;
     |FINSI;

     aPopup1 = "0";
     aPopup2  = nHayNumTelefono;
     IaPopup  = aPopup1  <-;
     |ESPECIFICA "TRACKPOPUP", aPopup;
     |SI FSalida = 0;
         |ACABA;
     |FINSI;

     aLlamarTel = "";
     IaPopup  = aPopup3  <-;
     IaPopup  = IaPopup + (FSalida - 1);
     aAux = aPopup;   |QBF aAux;
     aLong = aAux % A0;
     nLong = aLong;
     |PARA nVoy = 1; |SI nVoy [ nLong; |HACIENDO nVoy = nVoy + 1;
           nSaca = 100 * nVoy;
           nSaca = nSaca + 1;
           nSaca = -nSaca;
           aLetra = aAux % nSaca;
           |SI aLetra ! ":";
               aLlamarTel = aLetra + aLlamarTel;
           |SINO;
               |SAL;
           |FINSI;
     |SIGUE;
     |QBT aLlamarTel;

     |HAZ LLamarNumero;
|FPRC;

|PROCESO pbxpara3_tel;
     |SI #pbxpara3#4 ! "*";
         |ACABA;
     |FINSI;

     nCampoVoy = #pbxpara3#2;
     aAux = #referen@#nCampoVoy#;
     |QBF aAux;
     |SI aAux = "";  |ACABA; |FINSI;

     aLlamarTel = aAux;
     |HAZ SuprimirCerosIzquierda;
     nHayNumTelefono = nHayNumTelefono + 1;
     aNomCampo = #pbxpara3#3;  |QBF aNomCampo;
     aNomCampo = aNomCampo - ":" - ":" - ":" - ":";
     aContenido = aNomCampo + ": " + aLlamarTel;  |QBF aContenido;

     aPopup = aContenido;
     IaPopup = IaPopup + 1;
|FPRC;

|DEFBUCLE pbxpara3_tel;
     #pbxpara3#1;
     ;
     aAplica, aFicheroDef,   0;
     aAplica, aFicheroDef, 255;
     ;
     NULL, pbxpara3_tel;
|FIN;

|PROCESO ControlPBX
     nHayMarcaPBX = 0;
     |BUCLE pbxpara3_hay;
     |SI nHayMarcaPBX = 0;
          aMensaje = "0000Fichero " + aFicheroDef + " no parametrizado para Modulo Centralita";
          |MENAV aMensaje;
          |ACABA;
     |FINSI;

     aLlamarTel = "";
     nHayNumTelefono = 0;
     IaPopup = aPopup1  <-;
     IaPopup = IaPopup + 1;
     IaPopup = IaPopup + 1;
     |BUCLE pbxpara3_tel;

     |SI nHayNumTelefono = 0;
         aMensaje = "0000No hay datos telefonicos en esta ficha";
         |MENAV aMensaje;
         |ACABA;
     |FINSI;

     |SI nHayNumTelefono ! 1;
         aPopup1 = "0";
         aPopup2  = nHayNumTelefono;
         IaPopup  = aPopup1  <-;
         |ESPECIFICA "TRACKPOPUP", aPopup;
         |SI FSalida = 0;
             |ACABA;
         |FINSI;

         aLlamarTel = "";
         IaPopup  = aPopup3  <-;
         IaPopup  = IaPopup + (FSalida - 1);

         aAux = aPopup;   |QBF aAux;
         aLong = aAux % A0;
         nLong = aLong;
         |PARA nVoy = 1; |SI nVoy [ nLong; |HACIENDO nVoy = nVoy + 1;
               nSaca = 100 * nVoy;
               nSaca = nSaca + 1;
               nSaca = -nSaca;
               aLetra = aAux % nSaca;
               |SI aLetra ! ":";
                   aLlamarTel = aLetra + aLlamarTel;
               |SINO;
                   |SAL;
               |FINSI;
         |SIGUE;
         |QBT aLlamarTel;
     |FINSI;
     |HAZ LLamarNumero;
|FPRC;

|PROCESO ModuloERP_PBX;
     aMas = aPathDef + aNomDef;
     |CARGA_DEF aMas, referen@;
     |SI FSalida ! 0; |ACABA; |FINSI;

     |PATH_DAT #100 aPathDat;
     |ABRE #100;

     |DDEFECTO #100;
     aAlfa5 = "|K000";
     aClaves = #referen@#aAlfa5#;
     aTCompo = aClaves % 103;
     nTCompo = aTCompo;
     Iclave = clave1<-;
     |PARA nCampo = 1;  |SI nCampo [ 16;  |HACIENDO nCampo = nCampo + 1;
          clave = 0;
          Iclave = Iclave + 1;
     |SIGUE;

     Iclave = clave1<-;
     |PARA nCompo = 1;  |SI nCompo [ nTCompo;  |HACIENDO nCompo = nCompo + 1;
          nPosi = (nCompo * 300) + 103;
          aAlfa1 = aClaves % nPosi;
          clave = aAlfa1;
          Iclave = Iclave + 1;
     |SIGUE;

     nVoy = 1;
     Iclave = clave1<-;
     |PARA nCompo = 1;  |SI nCompo [ nTCompo;  |HACIENDO nCompo = nCompo + 1;
          nNumCam = clave;
          Iclave = Iclave + 1;

          Comodin = ("000" + nNumCam) % -103;
          Comodin = "|C" + Comodin;
          ||Comodin1 = Comodin + "ILON";
          Comodin1 = Comodin + "LONGITUD";
          Comodin1 = #referen@#Comodin1#;
          nTamanyo = Comodin1;
          aTamanyo = ("00" + nTamanyo) % -102;
          nSaca = (nVoy * 100) + aTamanyo;
          aSaca = aClau % nSaca;
          #referen@#nNumCam# = aSaca;
          nVoy = nVoy + nTamanyo;
     |SIGUE;

     |LEE 000#100.=;
     |SI FSalida = 0;
          |HAZ ControlPBX;
     |FINSI;

     |CIERRA #100;
     |DESCARGA_DEF #referen@;
|FPRC;

|PROCESO Telefonos_dsam0107;
     aLlamarTel = #referen@#4;   |QBF aLlamarTel;
     |SI aLlamarTel = "";
         |ACABA;
     |FINSI;

     |HAZ SuprimirCerosIzquierda;
     nHayNumTelefono = nHayNumTelefono + 1;

     aNomCampo  = #referen@#2;
     aNomCampo  = aNomCampo % 120;                     |QBF aNomCampo;
     aNomCampo  = aNomCampo - ":" - ":" - ":" - ":";
     aContenido = aNomCampo + ": " + aLlamarTel;      |QBF aContenido;

     aPopup  = aContenido;
     IaPopup = IaPopup + 1;
|FPRC;

|DEFBUCLE Telefonos_dsam0107;
     #referen@#1002;
     ;
     aNomCli, 01;
     aNomCli, 99;
     ;
     NULL, Telefonos_dsam0107;
|FIN;

|PROCESO CogeNum;
     aAux = aPopup;  |QBF aAux;
     aLong = aAux % A0;
     nLong = aLong;
     |PARA nVoy = 1; |SI nVoy [ nLong; |HACIENDO nVoy = nVoy + 1;
           nSaca = 100 * nVoy;
           nSaca = nSaca + 1;
           nSaca = -nSaca;
           aLetra = aAux % nSaca;
           |SI aLetra ! ":";
               aLlamarTel = aLetra + aLlamarTel;
           |SINO;
               |SAL;
           |FINSI;
     |SIGUE;

     |QBT aLlamarTel;
|FPRC;

|PROCESO DSAM0000;
     aNomCli = "";

     aMas = aPathDef + aNomDef;
     |CARGA_DEF aMas, referen@;
     |SI FSalida ! 0; |ACABA; |FINSI;

     |PATH_DAT #100 aPathDat;
     |ABRE #100;

     |DDEFECTO #100;
     |QBF aClau;
     nCliente = aClau;
     #100#0 = nCliente;
     |LEE 000#100.=;
     |SI FSalida = 0;
          aNomCli = #100#1;
     |FINSI;

     |CIERRA #100;
     |DESCARGA_DEF #referen@;

     aAux2 = aNomCli;   |QBF aAux2;
     |SI aAux2 = "";             ||No tengo Nombre de Cliente.
         |HAZ ModuloERP_PBX;
         |ACABA;
     |FINSI;

     aMas = aPathDef + "dsam0107";
     |CARGA_DEF aMas, referen@;
     |SI FSalida ! 0; |ACABA; |FINSI;

     |PATH_DAT #100 aPathDat;
     |ABRE #100;

     aLlamarTel      = "";
     nHayNumTelefono = 0;

     IaPopup = aPopup1  <-;
     IaPopup = IaPopup + 1;
     IaPopup = IaPopup + 1;

     |BUCLE Telefonos_dsam0107;

     |CIERRA #100;
     |DESCARGA_DEF #referen@;

     |SI nHayNumTelefono = 0;
         aMensaje = "0000No hay datos telefonicos en esta ficha";
         |MENAV aMensaje;
         |ACABA;
     |FINSI;

     |SI nHayNumTelefono = 1;
         ||Nada, ya tengo el numero de Telefono. (aLlamarTel)

         aLlamarTel = "";
         IaPopup    = aPopup3  <-;
         |HAZ CogeNum;
         |HAZ LLamarNumero;

         |ACABA;
     |FINSI;

     aPopup1 = "0";
     aPopup2  = nHayNumTelefono;
     IaPopup  = aPopup1  <-;
     |ESPECIFICA "TRACKPOPUP", aPopup;
     |SI FSalida = 0;
         |ACABA;
     |FINSI;

     aLlamarTel = "";
     IaPopup    = aPopup3  <-;
     IaPopup    = IaPopup + (FSalida - 1);
     |HAZ CogeNum;

     |HAZ LLamarNumero;
|FPRC;

|PROCESO DSAM0106;
     aMensaje = "0000PBX especial para INTEGRAL. (Listin Telefonico";
     ||MENAV aMensaje;

     aNomCli = aClau;
     aAux2 = aNomCli;
     |QBF aAux2;
     |SI aAux2 = "";             ||No tengo Nombre de Cliente.
          |HAZ ModuloERP_PBX;
          |ACABA;
     |FINSI;

     aMas = aPathDef + "dsam0107";
     |CARGA_DEF aMas, referen@;
     |SI FSalida ! 0; |ACABA; |FINSI;

     aLlamarTel = "";
     nHayNumTelefono = 0;
     IaPopup = aPopup1  <-;
     IaPopup = IaPopup + 1;
     IaPopup = IaPopup + 1;

     |PATH_DAT #100 aPathDat;
     |ABRE #100;
     |BUCLE Telefonos_dsam0107;
     |CIERRA #100;
     |DESCARGA_DEF #referen@;

     |SI nHayNumTelefono = 0;
         aMensaje = "0000No hay datos telefonicos en esta ficha";
         |MENAV aMensaje;
         |ACABA;
     |FINSI;

     |SI nHayNumTelefono = 1;
         ||Nada, ya tengo el numero de Telefono. (aLlamarTel)

         aLlamarTel = "";
         IaPopup    = aPopup3  <-;
         |HAZ CogeNum;
         |HAZ LLamarNumero;

         |ACABA;
     |FINSI;

     aPopup1 = "0";
     aPopup2  = nHayNumTelefono;
     IaPopup  = aPopup1  <-;
     |ESPECIFICA "TRACKPOPUP", aPopup;
     |SI FSalida = 0;
         |ACABA;
     |FINSI;

     aLlamarTel = "";
     IaPopup    = aPopup3  <-;
     IaPopup    = IaPopup + (FSalida - 1);
     |HAZ CogeNum;

     |HAZ LLamarNumero;
|FPRC;

|PROCESO TipoLlamada;
     aAuxPara = aParametro % 113;
     |SI aAuxPara = "TFNOINTEGRAL:";
         aClienteInt = aParametro - "TFNOINTEGRAL:";  |QBF aClienteInt;
         |HAZ LlamaClienteIntegral;

         |ACABA;
     |FINSI;

     aAuxPara = aParametro % 109;
     |SI aAuxPara = "LLAMATEL:"; |O aAuxPara = "MULTITEL:";
         aRestoPara = aParametro % 10;
         |SI aAuxPara = "LLAMATEL:";
             |HAZ LLamaUnTelefono;
             |ACABA;
         |FINSI;

         |HAZ LLamaMultiTel;
     |FINSI;

     aAux3 = aParametro % 103;
     |SI aAux3 = "LEE";
         aParametro = aParametro - aAux3;

         |XABRE "U", aParametro, nHandleLee;
          |SI FSalida ] 0;
              |XLEE nHandleLee, 250, aLinea;
              |SI FSalida < 0;  |ACABA;  |FINSI;

              aParametro = aLinea;
          |FINSI;
     |FINSI;
     |XCIERRA nHandleLee;

     aAplica     = aParametro % 108;
     aNumEmp     = aParametro % 905;
     nEmpresa    = aNumEmp;
     aNomDef     = aParametro % 1408;
     aFicheroDef = aNomDef;
     aParte1     = aParametro % 2250;
     aParte2     = aParametro % 7250;
     aClau       = aParte1 + aParte2;

     aLong = aParametro % A0;
     nLong = aLong;

     nSwSegunda = 0;
     aParte1 = "";
     aParte2 = "";
     |PARA nPos = 122; |SI nPos [ nLong; |HACIENDO nPos = nPos + 1;
           aSaca = nPos;
           aSaca = aSaca + "01";
           aLetra = aParametro % aSaca;
           |SI aLetra = "@";
               nSwSegunda = 1;
           |SINO;
               |SI nSwSegunda = 1;
                   aParte2 = aParte2 + aLetra;
               |SINO;
                   aParte1 = aParte1 + aLetra;
               |FINSI;
           |FINSI;
     |SIGUE;

     |SI aParte1 = ""; |O aParte2 = "";
         |ACABA;
     |FINSI;

     aPathDef = aParte1;
     aPathDat = aParte2;

     |SI aNomDef = "dsam0000"; |Y aAplica = "integral";
         aFicheroDef = "dsam0107";
         nHayMarcaPBX = 0;
         |BUCLE pbxpara3_hay;

         |SI nHayMarcaPBX = 1;
             |HAZ DSAM0000;
             |ACABA;
         |FINSI;

         aFicheroDef = aNomDef;
         |HAZ ModuloERP_PBX;
     |FINSI;

     |SI aNomDef = "dsam0106"; |Y aAplica = "integral";
         aFicheroDef = "dsam0107";
         nHayMarcaPBX = 0;
         |BUCLE pbxpara3_hay;

         |SI nHayMarcaPBX = 1;
             |HAZ DSAM0106;
             |ACABA;
         |FINSI;

         aMensaje = "0000Fichero " + aFicheroDef + " no parametrizado para Modulo Centralita";
         |MENAV aMensaje;

         |ACABA;
     |FINSI;

     |HAZ ModuloERP_PBX;
|FPRC;

|PROCESO CopiaCescomsk_Dll;
     |ESPECIFICA "RBASS", aLocal;
     aBaseLocal = aLocal;

     aDestino = aBaseLocal + "bin/cecomsk.dll";
     aContiene1 = aDestino;
     |ESPECIFICA "REMOTOMD5", aContiene;
     aVersion1 = FSalida;  |QBF aVersion1;

     |DBASE aBase;  |QBF aBase;
     aOrigen = aBase + "plugins/cecomsk.dll";
     aContiene1 = aOrigen;
     |ESPECIFICA "MD5", aContiene;
     aVersion2 = FSalida;  |QBF aVersion2;

     |SI aVersion1 ! aVersion2;
         aIPRemoto = "";
         |IP_REMOTO aIPRemoto;  |QBT aIPRemoto;

         |SI aIPRemoto ! "";
             |ENVIA_FICHERO aOrigen, aDestino;
         |SINO;
             |COPIA_FICHERO aOrigen, aDestino;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO Central_Cecom;
     aSwSuprimirCero = #pbxpara5#6;   |QBF aSwSuprimirCero;

     aUsuario = Usuario % 810;  |QBF aUsuario;

     |SI nCentralita = 0;
         aHost   = #pbxpara5#1;  |QBF aHost;
         aUsuAM  = #pbxpara5#2;  |QBF aUsuAM;
         aPassAM = #pbxpara5#3;  |QBF aPassAM;
     |SINO;
         |ABRE #pbxpara9;
         |DDEFECTO #pbxpara9;
         #pbxpara9#0 = nCentralita;
         |LEE 000#pbxpara9.=;
         |SI FSalida = 0;
             aHost   = #pbxpara9#1;  |QBF aHost;
             aUsuAM  = #pbxpara9#2;  |QBF aUsuAM;
             aPassAM = #pbxpara9#3;  |QBF aPassAM;
             |SI aHost = ""; |O aUsuAM = ""; |O aPassAM = "";
                 aHost   = #pbxpara5#1;  |QBF aHost;
                 aUsuAM  = #pbxpara5#2;  |QBF aUsuAM;
                 aPassAM = #pbxpara5#3;  |QBF aPassAM;
             |FINSI;
         |SINO;
             aHost   = #pbxpara5#1;  |QBF aHost;
             aUsuAM  = #pbxpara5#2;  |QBF aUsuAM;
             aPassAM = #pbxpara5#3;  |QBF aPassAM;
         |FINSI;
         |CIERRA #pbxpara9;
     |FINSI;

     |HAZ CopiaCescomsk_Dll;

     |CARGADLL "cecomsk.dll";
     |SI FSalida < 0;
         aMensaje = "0000Problemas al cargar [cecomsk.dll]";
         |MENAV aMensaje;
     |FINSI;

     |HAZ TipoLlamada;
|FPRC;

|PROCESO CopiaClick2_Dll;
     |ESPECIFICA "RBASS", aLocal;
     aBaseLocal = aLocal;

     aPathLlam = aBaseLocal + "click2";  |RMKDIR aPathLlam;
     aPathLlam = aBaseLocal + "click2/";

     aDestino   = aPathLlam + "ds_eje_php.exe";
     aContiene1 = aDestino;
     |ESPECIFICA "REMOTOMD5", aContiene;
     aVersion1 = FSalida;  |QBF aVersion1;

     |DBASE aBase;  |QBF aBase;
     aOrigen = aBase + "plugins/ds_eje_php.exe";
     aContiene1 = aOrigen;
     |ESPECIFICA "MD5", aContiene;
     aVersion2 = FSalida;  |QBF aVersion2;

     |SI aVersion1 ! aVersion2;
         aIPRemoto = "";
         |IP_REMOTO aIPRemoto;  |QBT aIPRemoto;

         |SI aIPRemoto ! "";
             |ENVIA_FICHERO aOrigen, aDestino;
         |SINO;
             |COPIA_FICHERO aOrigen, aDestino;
         |FINSI;
     |FINSI;

     aDestino = aPathLlam + "dsmini.exe";
     aContiene1 = aDestino;
     |ESPECIFICA "REMOTOMD5", aContiene;
     aVersion1 = FSalida;  |QBF aVersion1;

     |DBASE aBase;  |QBF aBase;
     aOrigen = aBase + "plugins/dsmini.exe";
     aContiene1 = aOrigen;
     |ESPECIFICA "MD5", aContiene;
     aVersion2 = FSalida;  |QBF aVersion2;

     |SI aVersion1 ! aVersion2;
         aIPRemoto = "";
         |IP_REMOTO aIPRemoto;  |QBT aIPRemoto;

         |SI aIPRemoto ! "";
             |ENVIA_FICHERO aOrigen, aDestino;
         |SINO;
             |COPIA_FICHERO aOrigen, aDestino;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO Central_Click2;
     |HAZ CopiaClick2_Dll;

     |DBASE aBase;  |QBF aBase;
     aPathLog = aBase + "llamadas";   |MKDIR aPathLog;
     aPathLog = aBase + "llamadas/";

     |HAZ TipoLlamada;
|FPRC;

|PROGRAMA;
     aParametro = PARAMETRO;
     |QBF aParametro;
     |SI aParametro = "";
          aMensaje = "0000Atencion: Proceso [dserpm11] llamado sin parametros.";
          |MENAV aMensaje;
          |ACABA;
     |FINSI;

     |ABRE #pbxpara5;
     |DDEFECTO #pbxpara5;
     |LEE 000#pbxpara5.p;
     |CIERRA #pbxpara5;

     |ABRE #pbxpara6;
     #pbxpara6#0 = Usuario % 810;
     |LEE 000#pbxpara6.=;
     |SI FSalida = 0;
         nCentralita = #pbxpara6#2;
         aExtension = #pbxpara6#1;
         |QBF aExtension;
     |FINSI;
     |CIERRA #pbxpara6;

     |SI aExtension = "";
          aMensaje = "0000Usuario SIN extension asociada";
          |MENAV aMensaje;
          |ACABA;
     |FINSI;

     |SI #pbxpara5#7 = "N";
         |HAZ Central_Cecom;
     |FINSI;

     |SI #pbxpara5#7 = "S";
         |HAZ Central_Click2;
     |FINSI;
|FPRO;
