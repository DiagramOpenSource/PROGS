|FICHEROS;
     dserpw01 #1;
     dserpm02 #2;
     dserpm03 #3;
     dserpm13 #13;
     dserpw03 #98;  || Campos
     dserpw02 #99;  || Tablas

     dserpw04 #100;
     dserpw05 #101;
     dserpw06 #102;
     dserpw07 #103;
     dserpw08 #104;

     FichRel@ #998;
     referen@ #999;
     Ref0001@ #1001;
     Ref0002@ #1002;
|FIN;

|VARIABLES;
     aAuxApli = "";
     aCarga        = "";
     aDirDef       = "";
     aQueQuiero    = "";
     aTitulo       = "";
     aDef          = "";
     aFichero      = "";
     aBase         = "";
     aAplica       = "";
     aBaseDef      = "";
     aDirFiltros   = "";
     aNombreFiltro = "";
     aFuncion      = "";
     aOrigen  = "";
     aDestino = "";

     aAlfa         = "";
     aAlfa1        = "";
     aAlfa2        = "";
     aAlfa3        = "";
     aAlfa4        = "";

     aNombrePr     = "";
     aNombreRl     = "";

     nNumRels      = 0;
     nCont         = 0;
     nCont1        = 0;
     nCont2        = 0;
     nCalc         = 0;
     nCalc1        = 0;
     nCalc2        = 0;
     nHandle       = 0;
     nCampo        = 0;
     nTCampo       = 0;
     nModo         = 0;
|FIN;

|| PROCESOS DEL DSERPM13 컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴

|PROCESO Tipo66m13f;  |TIPO 66;
     |ABRE #dserpw08;
     #dserpw08#0 = #dserpm13#2;
     |LEE 000#dserpw08.=;

     |CONSULTA_CLAVE #1#dserpw08;
     |SI FSalida = 0;
         #dserpm13#2 = #dserpw08#0;  |PINTA #dserpm13#2;
         |ERROR;

         |PTEC 802;
     |FINSI;
|FPRC;

|PROCESO Tipo7m13f;  |TIPO 7;
     |ABRE #dserpw08;
     #dserpw08#0 = #dserpm13#2;
     |LEE 000#dserpw08.=;
     |SI FSalida ! 0;
         |MENAV "0000 Fichero inexistente.";
         |PTEC 807;
         |ACABA;
     |FINSI;
     |CIERRA #dserpw08;
|FPRC;

|RUTINA infw05;
     #dserpw05#0 = #dserpm13#2;
     #dserpw05#1 = 000;
|FRUT;

|RUTINA supw05;
     #dserpw05#0 = #dserpm13#2;
     #dserpw05#1 = 999;
|FRUT;

|PROCESO Tipo66m13c;  |TIPO 66;
     |ABRE #dserpw05;
     #dserpw05#0 = #dserpm13#2;
     #dserpw05#1 = #dserpm13#3;
     |LEE 000#dserpw05.=;

     |CONSULTA_F_CLAVE #1#dserpw05, infw05, supw05;
     |SI FSalida = 0;
         #dserpm13#2 = #dserpw05#0;  |PINTA #dserpm13#2;
         #dserpm13#3 = #dserpw05#1;  |PINTA #dserpm13#2;
         |ERROR;

         |PTEC 802;
     |FINSI;
|FPRC;

|PROCESO Tipo7m13c;  |TIPO 7;
     |ABRE #dserpw05;
     #dserpw05#0 = #dserpm13#2;
     #dserpw05#1 = #dserpm13#3;
     |LEE 000#dserpw05.=;
     |SI FSalida ! 0;
         |MENAV "0000 Campo inexistente.";
         |PTEC 807;
         |ACABA;
     |FINSI;
     #dserpm13#4 = #dserpw05#2;  |PINTA #dserpm13#4;
     |CIERRA #dserpw05;
|FPRC;

|| PROCESOS DEL DSERPM03 컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴

|PROCESO Tipo66m3;  |TIPO 66;
     |ABRE #98;
     #98#0 = #3#2;
     |LEE 040#98=;

     |CONSULTA_CLAVE #1#98;
     |SI FSalida = 0;
         #3#2 = #98#0;  |PINTA #3#2;
         |ERROR;

         |PTEC 802;
     |FINSI;
|FPRC;

|PROCESO Tipo7m3;  |TIPO 7;
     |ABRE #98;
     #98#0 = #3#2;
     |LEE 000#98=;
     |SI FSalida ! 0;
         |MENAV "0000 Campo inexistente.";
         |PTEC 807;
         |ACABA;
     |FINSI;
     #3#3 = #98#1;  |PINTA #3#3;
     |CIERRA #98;
|FPRC;

|| PROCESOS DEL DSERPM002 컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴

|RUTINA ClaveBaja3;
     #3#0 = #2#0;
     #3#1 = #2#1;
     #3#2 = 0;
|FRUT;

|RUTINA ClaveAlta3;
     #3#0 = #2#0;
     #3#1 = #2#1;
     #3#2 = 999;
|FRUT;

|RUTINA ClaveBaja13;
     #dserpm13#0 = #2#0;
     #dserpm13#1 = #2#1;
     #dserpm13#2 = "        ";
     #dserpm13#3 = 0;
|FRUT;

|RUTINA ClaveAlta13;
     #dserpm13#0 = #2#0;
     #dserpm13#1 = #2#1;
     #dserpm13#2 = "zzzzzzzz";
     #dserpm13#3 = 999;
|FRUT;

|PROCESO PaseCmpsFiltros;
   |DDEFECTO #dserpm13;
   #dserpm13#0 = #dserpm03#0;
   #dserpm13#1 = #dserpm03#1;
   #dserpm13#2 = #dserpm02#6;
   #dserpm13#3 = #dserpm03#2;
   #dserpm13#4 = #dserpm03#3;
   |GRABA 020#dserpm13.n;
|FPRC;

|DEFBUCLE PaseCmpsFiltros;
#dserpm03#1;
;
#dserpm02#0, #dserpm02#1, 000;
#dserpm02#0, #dserpm02#1, 999;
;
NULL,PaseCmpsFiltros;
|FIN;

|PROCESO PaseFiltros;
   |BUCLE PaseCmpsFiltros;
|FPRC;

|DEFBUCLE PaseFiltros;
#dserpm02#1;
;
INICIO;
FINAL;
be;
NULL, PaseFiltros;
|FIN;

|PROCESO Tipo7m2c1;  |TIPO 7;
     ||Compruebo si hay datos en el dserpm13. Si no es asi miro si en el dserpm03 los hay
     ||    Si en el dserpm03 si que los hay es porque es la primera vez que entramos y hay
     ||    que hacer el pase
     |ABRE #dserpm13; |ABRE #dserpm03;
     |LEE 000#13p
     |SI FSalida ! 0;
        |LEE 000#3p
        |SI FSalida = 0;
           |BUCLE PaseFiltros;
        |FINSI;
     |FINSI;
     |CIERRA #dserpm13; |CIERRA #dserpm03;

     ||ENTLINEAL #1#3, -3, 7, 22, 0, ClaveBaja3, ClaveAlta3;
     |ENTLINEAL #1#13, -3, 7, 22, 0, ClaveBaja13, ClaveAlta13;
|FPRC;

|PROCESO Tipo66m2c3;  |TIPO 66;
     #1#0 = #2#3;
     |LEE 040#1=;

     |CONSULTA_CLAVE #1#1;
     |SI FSalida = 0;
         #2#3 = #1#0;  |PINTA #2#3;
     |FINSI;
|FPRC;

|PROCESO Tipo0m2c3;  |TIPO 0;
     #1#0 = #2#3;
     |LEE 040#1=;
     |SI FSalida ! 0;
         |MENAV "0000 Codigo Aplicacion inexistente.";
         |ERROR;
         |ACABA;
     |FINSI;
     #2#4 = #1#1;  |PINTA #2#4;
     #2#5 = #1#2;  |PINTA #2#5;
|FPRC;

|PROCESO Tipo7m2c3;  |TIPO 7;
     |SI #2#3 ! 0;  |ACABA;  |FINSI;

     #2#3 = #1#0;   |PINTA #2#3;
     #2#4 = #1#1;   |PINTA #2#4;
     #2#5 = #1#2;   |PINTA #2#5;
|FPRC;

|PROCESO Tipo66m2c6;  |TIPO 66;
     |ABRE #99;
     #99#0 = #2#6;
     |LEE 040#99=;

     |CONSULTA_CLAVE #1#99;
     |SI FSalida = 0;
         #2#6 = #99#0;  |PINTA #2#6;
     |FINSI;
|FPRC;

|PROCESO Tipo0m2c6;  |TIPO 0;
     |ABRE #99;
     #99#0 = #2#6;
     |LEE 040#99=;
     |SI FSalida ! 0;
         |MENAV "0000 Tabla inexistente.";
         |ERROR;
         |ACABA;
     |FINSI;
     #2#7 = #99#1;  |PINTA #2#7;
     |CIERRA #99;

     |DELINDEX #99;

     |HAZ GrabaFichRels;
|FPRC;

|PROCESO GrabaTabla;
     aCarga = aDirDef;  |QBT aCarga;
     |CARGA_DEF aCarga, referen@;
     |SI FSalida = 0;
         aQueQuiero = "|TITULO";
         aTitulo    = #999#aQueQuiero#;

         #99#0 = aDef;
         |LEE 000#99=;
         |SI FSalida ! 0;
            |DDEFECTO #99;
            #99#0 = aDef;
            #99#1 = aTitulo;
            |GRABA 020#99n;
         |FINSI;
         |DESCARGA_DEF #referen@;
     |FINSI;
|FPRC;

|PROCESO Tipo7m2c6;  |TIPO 7;
     aFichero = ("02" + Usuario + "zzzzzzzz") % 108;
     |NOME_DAT #99 aFichero;

     |ABRE #99;  |CIERRA #99;  |DELINDEX #99;

     |ABRE #99;

     |DBASS aBase;  |QBF aBase;
     aAplica   = #2#4;  |QBF aAplica;
     aBaseDef  = aBase + aAplica + "/def/";
     aDirDef   = aBaseDef + "*.mas";
     |_PDIR aDirDef;
     |PARA;  |SI FSalida ] 0; |HACIENDO;
          aDef = aDirDef - aBaseDef;
          aDef = aDef - ".mas";

          |HAZ GrabaTabla;

          |_SDIR aDirDef;
     |SIGUE;
     |CIERRA #99;
|FPRC;

|PROCESO Tipo66m2c8;  |TIPO 66;
     |ABRE #99;
     #99#0 = #2#8;
     |LEE 040#99=;

     |CONSULTA_CLAVE #1#99;
     |SI FSalida = 0;
         #2#8 = #99#0;  |PINTA #2#8;
     |FINSI;
|FPRC;

|PROCESO Tipo0m2c8;  |TIPO 0;
     aAlfa = #2#8; |QBF aAlfa;
     |SI aAlfa = ""; |ACABA; |FINSI;
     |ABRE #99;
     #99#0 = #2#8;
     |LEE 040#99=;
     |SI FSalida ! 0;
         |MENAV "0000 Tabla inexistente.";
         |ERROR;
         |ACABA;
     |FINSI;
     #2#9 = #99#1;  |PINTA #2#9;
     |CIERRA #99;

     |DELINDEX #99;
|FPRC;

|PROCESO Tipo7m2c8;  |TIPO 7;
     aFichero = ("02" + Usuario + "zzzzzzzz") % 108;
     |NOME_DAT #99 aFichero;

     |ABRE #99;  |CIERRA #99;  |DELINDEX #99;

     |ABRE #99;

     |DBASS aBase;  |QBF aBase;
     aAplica   = #2#4;  |QBF aAplica;
     aBaseDef  = aBase + aAplica + "/def/";
     aAlfa     = aBaseDef + #2#6 + ".mas";
     |CARGA_DEF aAlfa, FichRel@;
     |SI FSalida = 0;
        aAlfa = "|NR"; aAlfa = #FichRel@#aAlfa#;
        nNumRels = aAlfa; nNumRels = nNumRels - 1;
        |PARA nCont = 0; |SI nCont [ nNumRels; |HACIENDO nCont = nCont + 1;
           aAlfa = "|R" + (("000" + nCont) % -103); aAlfa = #FichRel@#aAlfa#;
           aAlfa = aAlfa - "|";
           |SI FSalida ! 0;
              nCalc = ((FSalida / 100) ! 0) + 99;
              aAlfa = aAlfa % nCalc;
           |FINSI;
           aDef    = aAlfa;
           aDirDef = aBaseDef + aDef + ".mas";
           |HAZ GrabaTabla;
        |SIGUE;

        ||Tiquet 4030.29.
        aDef = #2#6; |QBF aDef;
        aDirDef = aBaseDef + aDef + ".mas";
        |HAZ GrabaTabla;

        |DESCARGA_DEF #FichRel@;
     |FINSI;
     |CIERRA #99;
|FPRC;

|PROCESO GrabaCampo;
     aCarga = aDirDef;  |QBT aCarga;
     |CARGA_DEF aCarga, referen@;
     |SI FSalida = 0;
         aQueQuiero = "|TITULO";
         aTitulo    = #999#aQueQuiero#;

         #99#0 = aDef;
         #99#1 = aTitulo;
         |GRABA 020#99n;

         |DESCARGA_DEF #referen@;
     |FINSI;
|FPRC;

|PROCESO CargaCampos;
     aFichero = ("03" + Usuario + "zzzzzzzz") % 108;
     |NOME_DAT #98 aFichero;

     |ABRE #98;  |CIERRA #98;  |DELINDEX #98;

     |ABRE #98;

     |DBASS aBase;  |QBF aBase;
     aAplica   = #2#4;  |QBF aAplica;
     aBaseDef  = aBase + aAplica + "/def/";
     aCarga    = aBaseDef + #2#6;  |QBF aCarga;
     |CARGA_DEF aCarga, referen@;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     aQueQuiero = "|NC";
     aAlfa      = #999#aQueQuiero#;
     nTCampo    = aAlfa;
     nTCampo    = nTCampo - 1;

     |PARA nCampo = 0;  |SI nCampo [ nTCampo;  |HACIENDO nCampo = nCampo + 1;
           aQueQuiero = "|C" + (("000" + nCampo) % -103) + "NOMBRE";
           aFichero   = (#999#aQueQuiero# + (" " * 20)) % 120;
           #98#0 = nCampo;
           #98#1 = aFichero;

           |GRABA 020#98n;
     |SIGUE;
     |CIERRA #98;

     |DESCARGA_DEF #referen@;
|FPRC;

|PROCESO Tipo3m2;  |TIPO 3;
     |HAZ CargaCampos;

     ||ENTLINEAL #1#3, -3, 2, 22, 0, ClaveBaja3, ClaveAlta3;
     |ENTLINEAL #1#13, -3, 2, 22, 0, ClaveBaja13, ClaveAlta13;

     |DELINDEX #98;

     |DBASE aBase;     |QBF aBase;
     aDirFiltros   = aBase + "filtros";                           |MKDIR aDirFiltros;
     aDirFiltros   = aDirFiltros + "/" + (("00" + #2#0) % -102);  |MKDIR aDirFiltros;
     aDirFiltros   = aDirFiltros + "/";

     aNombreFiltro =  #2#1;  |QBF aNombreFiltro;
     aNombreFiltro = aNombreFiltro + ".flt";
     aFuncion      = "{F:?" + aDirFiltros + aNombreFiltro + "}";

     |DBASS aBase;     |QBF aBase;
     aAplica   = #2#4;  |QBF aAplica;
     aBaseDef  = aBase + aAplica + "/def/";
     aCarga    = aBaseDef + #2#6;  |QBF aCarga;
     |CARGA_DEF aCarga, referen@;
     |SI FSalida = 0;
         |PATH_DAT #999 aDirFiltros;

         |ABRE #999;
         FSalida = aFuncion;
         |CONSULTA_CLAVE #1#999;
         |CIERRA #999;
         |DELINDEX #999;
     |FINSI;

     aAlfa = aDirFiltros + aNombreFiltro;
     |XABRE "E", aAlfa, nHandle;
     |SI FSalida < 0;
         |MENAV "0000 El filtro no se ha creado correctamente. Vuelva a intentarlo.";
     |FINSI;

     |HAZ BorraDefRels;

     |CIERRA #dserpm13;
|FPRC;

|PROCESO dserpm03B;
     |BORRA 020#3a;
     |LIBERA #3;
|FPRC;

|DEFBUCLE dserpm03B;
     #3#1;
     ;
     #2#0, #2#1, 000;
     #2#0, #2#1, 999;
     be;
     NULL, dserpm03B;
|FIN;

|PROCESO Tipo2m2;  |TIPO 2;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo ! 3;  |ACABA;  |FINSI;

     |BUCLE dserpm03B;

     |DBASE aBase;     |QBF aBase;
     aDirFiltros   = aBase + "filtros";                           |MKDIR aDirFiltros;
     aDirFiltros   = aDirFiltros + "/" + (("00" + #2#0) % -102);  |MKDIR aDirFiltros;
     aDirFiltros   = aDirFiltros + "/";

     aNombreFiltro =  #2#1;  |QBF aNombreFiltro;
     aNombreFiltro = aNombreFiltro + ".flt";

     aAlfa = aDirFiltros + aNombreFiltro;
     |FBORRA aAlfa;
|FPRC;

|PROCESO GrabaFichRels;
     aAlfa = "t1" + Usuario; |NOME_DAT #dserpw04#aAlfa#;
     aAlfa = "t2" + Usuario; |NOME_DAT #dserpw05#aAlfa#;
     aAlfa = "t3" + Usuario; |NOME_DAT #dserpw06#aAlfa#;
     aAlfa = "t4" + Usuario; |NOME_DAT #dserpw07#aAlfa#;
     aAlfa = "t5" + Usuario; |NOME_DAT #dserpw08#aAlfa#;
     |DELINDEX #dserpw06; |ABRE #dserpw06;
     |DELINDEX #dserpw04; |ABRE #dserpw04;
     |DELINDEX #dserpw05; |ABRE #dserpw05;
     |DELINDEX #dserpw07; |ABRE #dserpw07;
     |DELINDEX #dserpw08; |ABRE #dserpw08;

     |DDEFECTO #dserpw06;
     #dserpw06#0 = #dserpm02#6;
     |GRABA 020#dserpw06.c;

     FSalida = 0;
     |PARA; |SI FSalida = 0; |HACIENDO;
        |DBASS aAlfa; |QBF aAlfa;
        aAuxApli = #dserpm02#4;
        |QBF aAuxApli;
        aAlfa = aAlfa + aAuxApli + "/def/" + #dserpw06#0 + ".mas"; aNombrePr = #dserpw06#0;
        |CARGA_DEF aAlfa, Ref0001@;
        |SI FSalida = 0;
           || Incluyo el que estoy usando

           #dserpw08#0 = #dserpw06#0;
           |LEE 000#dserpw08.=;
           |SI FSalida ! 0;
              aAlfa = "|TITULO"; aAlfa = #Ref0001@#aAlfa#;
              |DDEFECTO #dserpw08;
              #dserpw08#0 = #dserpw06#0;
              #dserpw08#1 = aAlfa;
              |GRABA 020#dserpw08.n;
           |FINSI;

           aAlfa = "|NC"; aAlfa = #Ref0001@#aAlfa#; nCalc = aAlfa;
           |PARA nCont1 = 0; |SI nCont1 < nCalc; |HACIENDO nCont1 = nCont1 + 1;
              aAlfa = "|C" + (("000" + nCont1) % -103) + "NOMBRE";
              aAlfa = #Ref0001@#aAlfa#;
              #dserpw05#0 = #dserpw06#0;
              #dserpw05#1 = nCont1;
              |LEE 000#dserpw05.=;
              |SI FSalida ! 0;
                 |DDEFECTO #dserpw05;
                 #dserpw05#0 = #dserpw06#0;
                 #dserpw05#1 = nCont1;
                 #dserpw05#2 = aAlfa;
                 |GRABA 020#dserpw05.n;
              |FINSI;
           |SIGUE;

           |SALVA_FICHA #dserpw06;
           aAlfa = "|NR"; aAlfa = #Ref0001@#aAlfa#; nNumRels = aAlfa;
           |PARA nCont = 0; |SI nCont < nNumRels; |HACIENDO nCont = nCont + 1;
              aAlfa1 = "|R" + (("000" + nCont) % -103); aAlfa1 = #Ref0001@#aAlfa1#;
              aAlfa = aAlfa1 - "|";
              |SI FSalida ! 0;
                 nCalc = ((FSalida / 100) ! 0) + 99;
                 aAlfa = aAlfa % nCalc; aNombreRl = aAlfa;

                 #dserpw06#0 = aAlfa;
                 |LEE 000#dserpw06.=;
                 |SI FSalida ! 0; ||Y aAlfa ! #dserpm02#6;
                    |SELECT #2#dserpw04;
                    #dserpw04#0 = aNombrePr;
                    #dserpw04#2 = aNombreRl;
                    |LEE 000#dserpw04.=;
                    |SI FSalida ! 0;
                       aOrigen = aNombreRl - "dserp";
                       |SI FSalida = 0;
                          aOrigen = aNombreRl - "pbxpara";
                          |SI FSalida = 0;
                             |DBASS aOrigen; |QBF aOrigen;
                             aAuxApli = #dserpm02#4;
                             |QBF aAuxApli;
                             aOrigen = aOrigen + aAuxApli + "/def/" + aNombreRl + ".mas";
                             |DDEF aDestino; |QBF aDestino;
                             aDestino = aDestino + aNombreRl + ".mas";
                             |COPIA_FICHERO aOrigen, aDestino;
                          |FINSI;
                       |FINSI;

                       |SELECT #1#dserpw04;
                       |DDEFECTO #dserpw06;
                       #dserpw06#0 = aAlfa;
                       |GRABA 020#dserpw06.n;

                       |DDEFECTO #dserpw04;
                       #dserpw04#0 = aNombrePr;
                       #dserpw04#1 = nCont;
                       #dserpw04#2 = aNombreRl;

                       |DBASS aAlfa; |QBF aAlfa;
                       aAuxApli = #dserpm02#4;
                       |QBF aAuxApli;
                       aAlfa = aAlfa + aAuxApli + "/def/" + #dserpw04#2 + ".mas";
                       |CARGA_DEF aAlfa, Ref0002@;
                       |SI FSalida = 0;
                          aAlfa = "|TITULO"; aAlfa = #Ref0002@#aAlfa#;

                          #dserpw08#0 = aNombreRl;
                          |LEE 000#dserpw08.=;
                          |SI FSalida ! 0;
                             |DDEFECTO #dserpw08;
                             #dserpw08#0 = aNombreRl;
                             #dserpw08#1 = aAlfa;
                             |GRABA 020#dserpw08.n;
                          |FINSI;

                          #dserpw04#3 = aAlfa;

                          aAlfa = "|NC"; aAlfa = #Ref0002@#aAlfa#; nCalc = aAlfa;
                          |PARA nCont1 = 0; |SI nCont1 < nCalc; |HACIENDO nCont1 = nCont1 + 1;
                             aAlfa = "|C" + (("000" + nCont1) % -103) + "NOMBRE";
                             aAlfa = #Ref0002@#aAlfa#;

                             #dserpw05#0 = #dserpw04#2;
                             #dserpw05#1 = nCont1;
                             |LEE 000#dserpw05.=;
                             |SI FSalida ! 0;
                                |DDEFECTO #dserpw05;
                                #dserpw05#0 = #dserpw04#2;
                                #dserpw05#1 = nCont1;
                                #dserpw05#2 = aAlfa;
                                |GRABA 020#dserpw05.n;
                             |FINSI;

                             |DDEFECTO #dserpw07;
                             #dserpw07#0 = #dserpw04#0;
                             #dserpw07#1 = #dserpw04#1;
                             #dserpw07#2 = #dserpw04#2;
                             #dserpw07#3 = nCont1;
                             #dserpw07#4 = aAlfa;

                             aAlfa2 = "|K000"; aAlfa2 = #Ref0002@#aAlfa2#;
                             aAlfa3 = aAlfa2 % 0499;  || Le quito el n de componentes
                             nCont2 = 1;
                             |PARA; |SI aAlfa3 ! ""; |HACIENDO;
                                aAlfa4 = aAlfa3 % 0103; aAlfa3 = aAlfa3 % 0499;
                                nCalc2 = aAlfa4;
                                |SI nCalc2 = nCont1;
                                   #dserpw07#5 = nCont2;
                                   aAlfa3 = "|R" + (("000" + nCont) % -103); aAlfa3 = #Ref0001@#aAlfa3#;
                                   aAlfa3 = aAlfa3 - "|";
                                   |SI FSalida ! 0;
                                      nCalc2 = FSalida + 98;
                                      aAlfa3 = aAlfa3 % nCalc2;
                                      aAlfa3 = aAlfa3 % 0499; || Le quito el n de componentes
                                      |PARA; |SI aAlfa3 ! ""; |Y nCont2 > 0; |HACIENDO;
                                         aAlfa4 = aAlfa3 % 0103; aAlfa3 = aAlfa3 % 0499;
                                         nCalc2 = aAlfa4; nCalc2 = nCalc2 - 1;
                                         nCont2 = nCont2 - 1;
                                      |SIGUE;
                                      #dserpw07#6 = nCalc2;
                                   |FINSI;
                                   |SAL;
                                |FINSI;
                                nCont2 = nCont2 + 1;
                             |SIGUE;
                             |GRABA 020#dserpw07.n;
                          |SIGUE;
                          |DESCARGA_DEF #Ref0002@;
                       |FINSI;
                       |GRABA 020#dserpw04.n;
                    |FINSI;
                 |FINSI;
              |FINSI;
           |SIGUE;
           |DESCARGA_DEF #Ref0001@;
           |REPON_FICHA #dserpw06;
        |FINSI;
        |BORRA 020#dserpw06.a;

        |LEE 000#dserpw06.p;
     |SIGUE;

     |CIERRA #dserpw04; |CIERRA #dserpw07; |CIERRA #dserpw05; |CIERRA #dserpw08;
     |CIERRA #dserpw06; |DELINDEX #dserpw06;
|FPRC;

|PROCESO BorraDefRels;
     |ABRE #dserpw04;
     |LEE 000#dserpw04.p;
     |PARA; |SI FSalida = 0; |HACIENDO;
        aAlfa = #dserpw04#2; |QBF aAlfa;
        aAlfa1 = aAlfa - "dserp";
        |SI FSalida = 0;
           aAlfa1 = aAlfa - "pbxpara";
           |SI FSalida = 0;
              |DDEF aAlfa; |QBF aAlfa; aAlfa = aAlfa + #dserpw04#2 + ".mas";
              |FBORRA aAlfa;
           |FINSI;
        |FINSI;
        |BORRA 020#dserpw04.a;
        |LEE 000#dserpw04.p;
     |SIGUE;
     |CIERRA #dserpw04; |DELINDEX #dserpw04;
     |DELINDEX #dserpw05; |DELINDEX #dserpw07;
     |DELINDEX #dserpw08;
|FPRC;
