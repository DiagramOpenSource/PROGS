|FICHEROS;
     dserpx09 #5;
     dserpm04 #4;
|FIN;

|VARIABLES;
     aBeta = "";
     nSwEdita = 0;

     {1}aContiene = "";
     {1}aLocal = "";
     aBaseLocal = "";
     aVersion1 = "";
     aVersion2 = "";
     aIPRemoto = "";


     {3}aPuntero = "";
     &enAplicacion = 0;
     aTipoScaneo = "";
     aTipoScaneoT = "";
     aAlfa = "";
     aIP = "";
     aRuta          = "";
     aExtension     = "";
     aBase          = "";
     aDirDocs = "";
     aOrigen        = "";
     aDestino       = "";
     aPunto         = "";
     aAbre          = "";
     aDocumentos    = "";
     aComodin       = "";
     aDocRemoto     = "";
     aDocServidor   = "";
     aFicEdita      = "";
     aEjecuta       = "";

     nComodNum      = 0;
     nHandle        = 0;
     nTecla         = 0;
     nForma         = 0;
     nModo          = 0;
     nNumAnt        = 0;
     nPausa         = 0;

     aAlfa1 = "";
|FIN;

|PROCESO NombreFic;
     |DBASE aBase;  |QBF aBase;
     aDirDocs = aBase + "documentos";
     |MKDIR aDirDocs;

     aDestino = aDirDocs + "/*";
     |_PDIR aDestino;
     nNumAnt = 0;
     |PARA; |SI FSalida = 0; |HACIENDO;
        aComodin  = aDestino % -509;
        nComodNum = aComodin;
        |SI nComodNum > nNumAnt;
            nNumAnt = nComodNum;
        |FINSI;
        |_SDIR aDestino;
     |SIGUE;
     aComodin = aDestino % -101;

     nComodNum = nNumAnt;
     |SI aComodin = "*";
        aDestino = aDirDocs + "/DOCUMENTO000000000." + aExtension;
     |SINO;
        nComodNum = nComodNum + 1;
        aDestino  = aDirDocs + "/DOCUMENTO" + (("000000000" + nComodNum) % -109) + "." + aExtension;
     |FINSI;
|FPRC;

|PROCESO CogeFichero;
     |SI #4#3 = "S"; |ACABA; |FINSI;

     |IP_REMOTO aIP;
     |QBF aIP;

     aRuta = "F";
     |BROWSE aRuta;  |QBF aRuta;
     |SI aRuta = "F";  |ACABA;  |FINSI;

     aRuta = aRuta % 0299;
     aExtension = (aRuta % -103) > "";
     aPunto     = aRuta % -401;
     |SI aPunto ! ".";
         |MENAV "0000 Documento sin Extension. Formato exigido extension de 3 caracteres.";
         |ERROR;
         |ACABA;
     |FINSI;

     |HAZ NombreFic;

     aOrigen  = aRuta;
     |SI aIP ! "";
          |RECIBE_FICHERO aOrigen, aDestino;
     |SINO;
          |COPIA_FICHERO aOrigen, aDestino;
     |FINSI;

     aComodin = aDirDocs + "/";
     #5#3 = aDestino - aComodin;
     #5#4 = Usuario % 0810;

     |PONCONTROL 23, "SI";
|FPRC;

|PROCESO TraeDsAbreExtension;
     |DBASE aAlfa;   |QBF aAlfa;
     aAlfa     = aAlfa + "plugins/";
     aOrigen   = aAlfa + "dsabreextension.exe";
     aDestino  = "C:\DOCUMENTOS\DsAbreExtension.exe";

     aContiene1 = aDestino;
     |ESPECIFICA "REMOTOMD5", aContiene;
     aDocRemoto = FSalida;  |QBF aDocRemoto;

     aContiene1 = aOrigen;
     |ESPECIFICA "MD5", aContiene;
     aDocServidor = FSalida;  |QBF aDocServidor;

     |SI aDocRemoto ! aDocServidor;
         aIPRemoto = "";
         |IP_REMOTO aIPRemoto;  |QBT aIPRemoto;

         |SI aIPRemoto ! "";
             |ENVIA_FICHERO aOrigen, aDestino;
         |SINO;
             |COPIA_FICHERO aOrigen, aDestino;
         |FINSI;
     |FINSI;

     |XABRE "EC", aDestino, nHandle;
     |SI FSalida < 0;
         aIPRemoto = "";
         |IP_REMOTO aIPRemoto;   |QBT aIPRemoto;
         |SI aIPRemoto ! "";
             |ENVIA_FICHERO aOrigen, aDestino;
         |SINO;
             |COPIA_FICHERO aOrigen, aDestino;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO AbreDoc;
     |XABRE "EC", aFicEdita, nHandle;
     |SI FSalida < 0;  |ACABA;  |FINSI;

     aEjecuta = "START C:\DOCUMENTOS\dsabreextension ";
     aEjecuta = aEjecuta + aFicEdita;
     aEjecuta = aEjecuta + " C:\DOCUMENTOS\" + "fin" + Usuario + ".txt";

     aAlfa  = "C:\DOCUMENTOS\" + "fin" + Usuario + ".txt";
     |RFBORRA aAlfa;

     |RSYSTEM aEjecuta;
     aAlfa  = "C:\DOCUMENTOS\" + "fin" + Usuario + ".txt";
     |PARA;  |SI;  |HACIENDO;
             |PARA nPausa = 1; |SI nPausa < 10000; |HACIENDO nPausa = nPausa + 1;
             |SIGUE;

             |XABRE "EC", aAlfa, nHandle;
             |SI FSalida ] 0;
                 |SAL;
             |FINSI;
             |PULSATECLA;
     |SIGUE;

     |RECIBE_FICHERO aOrigen, aDestino;
     #5#4 = Usuario % 0810;
     |RFBORRA aOrigen;
|FPRC;

|PROCESO EditaFichero;
     |IP_REMOTO aIP;
     |QBF aIP;

     |DBASE aBase;  |QBF aBase;
     aDirDocs = aBase + "documentos";
     |MKDIR aDirDocs;

     aOrigen  = "C:\DOCUMENTOS";
     |RMKDIR aOrigen;

     |HAZ TraeDsAbreExtension;

     aDestino = aDirDocs + "/" + #5#3;  |QBF aDestino;
     aOrigen  = "C:\DOCUMENTOS\" + #5#3;      |QBF aOrigen;

     |SI aIP ! "";
          |ENVIA_FICHERO aDestino, aOrigen;
     |SINO;
          |COPIA_FICHERO aDestino, aOrigen;
     |FINSI;
     |SI FSalida = 0;
         |AVISO;
         |SI nSwEdita = 1;
              FSalida = 0;
         |SINO;
              |CONFI "0000NEditar el documento?";
         |FINSI;
         |SI FSalida = 0;
             aFicEdita = aOrigen;
             |HAZ AbreDoc;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO BorraFichero;
     |IP_REMOTO aIP;
     |QBF aIP;

     |DBASE aDocumentos;  |QBF aDocumentos;
     aDocumentos = aDocumentos + "documentos";
     aDocumentos = aDocumentos + "/" + #5#3;  |QBF aDocumentos;
     |SI aIP ! "";
          |FBORRA aDocumentos;
     |SINO;
          |RFBORRA aDocumentos;
     |FINSI;
|FPRC;

|PROCESO Tipo2m05;  |TIPO 2;
     nModo = (FEntrada / 100) ! 0;
     nForma = 0 +. 1;
     |SI nForma = 1;
          |SI nModo = 1;
               |SI #5#3 ! "                          ";
                   |HAZ EditaFichero;
               |SINO;
                   |HAZ CogeFichero;
                   |SI #5#3 ! "                          ";
                      |CONFI "0000NDesea Editar el Fichero?";
                      |SI FSalida = 0;
                          |HAZ EditaFichero;
                      |FINSI;
                   |SINO;
                      |ERROR;
                   |FINSI;
               |FINSI;
          |FINSI;
     |SINO;
         nModo = (FEntrada / 100) ! 0;
         |SI nModo = 3;
             |HAZ BorraFichero;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO CodBar7; |TIPO 7;
     nModo = (FEntrada / 100) ! 0;

     |ABRE #4;
     #4#0 = enAplicacion;
     #4#1 = #5#7;|| progra
     |LEE 000#4=;
     |SI FSalida ! 0;
          |DDEFECTO #4;
     |FINSI;
     |CIERRA #4;

     |SI #4#4 = "S";
          |C_M #5#8, 1, "N";
     |SINO;
          |C_M #5#8, 1, "S";
     |FINSI;

     |SI #4#3 = "S"; |Y nModo = 1;
          |HAZ Escaner;
     |FINSI;
|FPRC;

|PROCESO Escaner;
     |HAZ CopiaDll;
 |ET Reintentar;
     aTipoScaneo = "[SCANUI]";
     |CONFI "2400NDesea configurar las propiedades del escaner?";
     |SI FSalida ! 0; aTipoScaneo = "[SCAN]";  |FINSI;

     |ESPECIFICA "RBASS", aPuntero;
     aOrigen = aPuntero;
     aOrigen = aOrigen + Usuario;

     aTipoScaneoT = aTipoScaneo;
     |CARGADLL "dstwain.dll";
     |SI FSalida < 0;
          |MENAV "     No encuentro el driver : dstwain.dll";
          |ACABA;
     |FINSI;

     |ALFACALLDLL "dstwain.dll","DsTwainEntry", aTipoScaneoT;
     |SI aTipoScaneoT ! "OK";
          |MENAV "0000Operacion de escaneo no realizada.";
          |C_M #5#8, 1, "S";
          |ACABA;
     |FINSI;

     |SI #4#4 = "S";
          aTipoScaneoT = "[BARRAS:]";
          |ALFACALLDLL "dstwain.dll","DsTwainEntry", aTipoScaneoT;
          |SI aTipoScaneoT ! "";
              aTipoScaneoT = aTipoScaneoT % 0112;
          |FINSI;
          #5#8 = aTipoScaneoT;
          |QBF aTipoScaneoT;
     |FINSI;

     aAlfa = "[SAVE:" + aOrigen; |QBF aAlfa;
     |ALFACALLDLL "dstwain.dll", "DsTwainEntry", aAlfa;

     |DESCARGADLL "dstwain.dll";

     aExtension = "TIF";
     |HAZ NombreFic;
     aIP = "";
     |IP_REMOTO aIP; |QBF aIP;
     |SI aIP ! "";
          |RECIBE_FICHERO aOrigen, aDestino;
     |SINO;
          |COPIA_FICHERO aOrigen, aDestino;
     |FINSI;
     |FBORRA aOrigen;
     aComodin = aDirDocs + "/";
     #5#3 = aDestino - aComodin;
     #5#4 = Usuario % 0810;
     |PINTA #5#3;
     |PINTA #5#4;
     |PINTA #5#8;

     |SI #4#4 = "S"; |Y aTipoScaneoT = "";
          |CONFI "0000NCodigo de barras inaccesible, reintentar?";
          |SI FSalida = 0;
               |VETE Reintentar;
          |SINO;
               |C_M #5#8, 1, "S";
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO CopiaDll;
     |ESPECIFICA "RBASS", aLocal;
     aBaseLocal = aLocal;

     aDestino = aBaseLocal + "bin/dstwain.dll";
     aContiene1 = aDestino;
     |ESPECIFICA "REMOTOMD5", aContiene;
     aVersion1 = FSalida;  |QBF aVersion1;

     |DBASE aBase;  |QBF aBase;
     aOrigen = aBase + "plugins/dstwain.dll";
     aContiene1 = aOrigen;
     |ESPECIFICA "MD5", aContiene;
     aVersion2 = FSalida;  |QBF aVersion2;

     |SI aVersion1 ! aVersion2;
         aIPRemoto = "";
         |IP_REMOTO aIPRemoto;  |QBT aIPRemoto;

         |SI aIPRemoto ! "";
             |ENVIA_FICHERO aOrigen, aDestino;
         |SINO;
             |COPIA_FICHERO aOrigen, aDestino;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO Edita0; |TIPO 11;
     |SI FSalida = 13; || F5;
          nSwEdita = 1;
          |HAZ EditaFichero;
          nSwEdita = 0;
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO Tipo7C2; |TIPO 7;
     |HAZ EsAparecida;
     |SI FSalida = 0;
          |PINTA 2161, "Imp. con factura:";
     |SINO;
          |PINTA 2161, "                 ";
     |FINSI;
|FPRC;
