|FICHEROS;
     nifpara4 #0;

     nifpara1 #101;
     nifpara2 #102;
     nifpara3 #103;
     nifpara8 #108;

     refer@   #200;
|FIN;

|VARIABLES;
     aEjecuta = "";
     nSwIncluirSinDef = 0;

     aCodAplica = "";
     aNomAplica = "";

     aAux = "";
 {-1}get = "";
     get1 = 0;
     get2 = 0;
     get3 = 0;
     get4 = 0;
     get5 = "";

     nVentana = 0;

     nTotal = 0;
     enCampos = 0;
     Cont = 0;
     Comodin = "";
     Comodin1 = "";
     aBeta = "";

     aMensaje = "";

     nPantalla = 0;
     aEsc1 = "";
     aEsc2 = "";
     aRetu = "";
     aTecla = "";

     nGrid1 = 0;
     nGrid2 = 0;
     nGrid3 = 0;

     nRango = 0;
     nPos1 = 0;
     nPos2 = 0;
     nBoton = 0;
     nBoton2 = 0;
     nBoton3 = 0;
     nBoton4 = 0;

     CADENA01  = "{{0";
     CADENA02  = "] 0";

     aDBASS    = "";
     nFich     = 0;
     nFich02   = 0;
     aAlfa     = "";
     aAlfa02   = "";
     x         = 0;
     xx        = 0;
     naux      = 0;
     nlon      = 0;
     aLinea    = "";
     aVal      = "";
     aceros    = "";

     afApl     = "";
     afMen     = "";
     aNombApl  = "";
     afEmp     = "";
     aNombEmp  = "";
     aNombDat  = "";          || el nombre del .dat asociado
     aData     = "";
     afDef     = "";
     aNombDef  = "";

     aDirDefs  = "";
     aLosDefs  = "";
     nClaves   = 0;

     aIniEMP   = "";
     aFinEMP   = "";
     aIniDEF   = "";
     aFinDEF   = "";

     aCad      = "";
     aSub      = "";
     nPos      = 0;
     {300}vCad = "";
     nItVector = 0;
     aDelim    = " ";
|FIN;

|PROCESO Tipo80; |TIPO 80;
     FSalida = 2699;
|FPRC;

|PROCESO ActPatrones;
     aMensaje = "0000NActualizar los patrones de Datos NIF/NIF?";
     |CONFI aMensaje;
     |SI FSalida = 0;
          aEjecuta = "dserpz01";
          |SUB_EJECUTA_NP aEjecuta;

          |REFRESCACONTROL nGrid1;
     |FINSI;
|FPRC;

|PROCESO Botones;

     |CONTROL_SIMPLE nPantalla, "BOTON,{{137}} Introd. Manual Aplicacion", 0644, 0664, MeteAplica;
     nBoton3 = FSalida;

     |CONTROL_SIMPLE nPantalla, "BOTON,{{137}} Refresca Tablas", 0666, 0681, RecargaTablas;
     nBoton = FSalida;

     |CONTROL_SIMPLE nPantalla, "BOTON,{{137}} Act. Patrones ", 0683, 0698, ActPatrones;
     nBoton4 = FSalida;
|FPRC;

|PROCESO Evento1;
     |SI FSalida = 1; |O FSalida = 2;
          |LEE 000#101.=;
          |SI FSalida = 0;
               |REFRESCACONTROL nGrid2;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Evento2;
     |SI FSalida = 1; |O FSalida = 2;
          |LEE 000#102.=;
          |SI FSalida = 0;
               |REFRESCACONTROL nGrid3;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Evento3;
     |SI FSalida = 1; |O FSalida = 2;
          |LEE 000#103.=;
     |FINSI;

     |SI FSalida = 4;
          |LEE 101#103.=;
          |SI FSalida = 0;
               |SI #103#4 = "*";
                    #103#4 = "";
               |SINO;
                    #103#4 = "*";
               |FINSI;
               |GRABA 020#103.a;
               |LIBERA #103;
               |REFRESCACONTROL nGrid3;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Baja2;
     #102#0 = #101#0;
     #102#1 = "        ";
|FPRC;

|PROCESO Alta2;
     #102#0 = #101#0;
     #102#1 = "zzzzzzzz";
|FPRC;

|PROCESO Baja3;
     #103#0 = #101#0;
     #103#1 = #102#1;
     #103#2 = 0;
|FPRC;

|PROCESO Alta3;
     #103#0 = #101#0;
     #103#1 = #102#1;
     #103#2 = 255;
|FPRC;

|PROCESO Grids;
     nRango = 2 + 4 + 8 + 16 + 32;

     nPos1 = 0802;
     nPos2 = 2432;

     |LINEAL_SIMPLE #1#101, nRango, nPos1, nPos2, NULL, NULL, Evento1;
     nGrid1 = FSalida;

     nPos1 = 0834;
     nPos2 = 2464;

     |LINEAL_SIMPLE #1#102, nRango, nPos1, nPos2, Baja2, Alta2, Evento2;
     nGrid2 = FSalida;

     nPos1 = 0866;
     nPos2 = 2496;

     |LINEAL_SIMPLE #1#103, nRango, nPos1, nPos2, Baja3, Alta3, Evento3;
     nGrid3 = FSalida;
|FPRC;

|PROGRAMA;
     |ABRET #101;
     |ABRET #102;
     |ABRET #103;

     |CLS;

     |PINPA #0#0;
     nPantalla = FSalida;
     |PINDA #0#0;

     |HAZ Grids;

     |HAZ Botones;


     aEsc1  = &255 + "806";
     aEsc2  = &255 + "807";
     aRetu  = &255 + "802";
     |PARA; |SI;  |HACIENDO;
          FSalida = "::{{001}}Salir," + nGrid1;
          |LEETECLA aTecla;
          |SI aTecla = aRetu; |SAL;  |FINSI;
          |SI aTecla = aEsc1; |SAL;  |FINSI;
          |SI aTecla = aEsc2; |SAL;  |FINSI;
     |SIGUE;

     |FIN_CONTROL_WINDOWS nBoton;
     |FIN_CONTROL_WINDOWS nBoton2;
     |FIN_CONTROL_WINDOWS nBoton3

     |CIERRAT #101;
     |CIERRAT #102;
     |CIERRAT #103;
|FPRO;

|PROCESO CargaDEF;
     afApl = #101#0;
     |QBF afApl;

     || path de los defs
     aDirDefs = aDBASS + afApl + "/def/";
     aLosDefs = aDirDefs + "*.mas";
     |_PDIR aLosDefs;

     |PARA; |SI FSalida]0; |HACIENDO;
          |HAZ InfoDEF;
          |SI nClaves > 0; |O nSwIncluirSinDef = 1;
               |DDEFECTO #102;
               #102#00 = afApl;
               #102#01 = afDef;
               |LEE 101#102.=;
               |SI FSalida ! 0; |GRABA 020#102.b; |FINSI;
               #102#02 = aNombDef;
               |GRABA 040#102.a;
               |LIBERA #102;
          |FINSI;
          |_SDIR aLosDefs;
     |SIGUE;
|FPRC;

|DEFBUCLE CargaDEF;
 #101#1;
 ;
 INICIO;
 FINAL;
 ;
 NULL, CargaDEF;
|FIN;

|DEFBUCLE CargaDEF_manual;
 #101#1;
 ;
 aCodAplica;
 aCodAplica;
 ;
 NULL, CargaDEF;
|FIN;


|PROCESO RecargaTablas;
     ||vore dsdatzzz .. .del dsvisor de Mariano.

     |DBASS aDBASS; |QBT aDBASS;
     aAlfa = aDBASS + "ds.men";

     |XABRE "", aAlfa, nFich;
     |SI FSalida < 0;
          |MENAV "0000No encuentro el ds.men .... Saliendo";
          |ACABA;
     |FINSI;

     || Leemos todas las lineas del fichero ds.men
     |XLEE nFich, 255, aLinea;
     |PARA; |SI FSalida ] 0; |HACIENDO;
          aCad = aLinea % 101;
          |SI aCad = ";";               || esta comentado
               naux = -100000;          || obligamos a que sea negativo
          |FINSI;

          || comprobamos si es una linea de menu de acceso a aplicacion
          || si aparecen las cadenas CADENAXX, es buena la linea.
          aCad = aLinea;
          aSub = CADENA01;
          |HAZ ExisteSubCadena;
          naux = naux + nPos;

          aCad = aLinea;
          aSub = CADENA02;
          |HAZ ExisteSubCadena;
          naux = naux + nPos;

          || de acuerdo :: es una linea buena....
          |SI naux > 0;

               || pasamos la linea de texto en items de vector.
               aCad = aLinea;
               |HAZ Cadena2Vector;

               || recorremos los items de vector
               IvCad = vCad1 <-;
               |PARA x=1; |SI x [ nItVector; |HACIENDO x=x+1;

                    naux = 0;

                    || buscamos la palabra a partir del cual obtenemos
                    || la info que nos interesa.
                    || si es linea "tipo.1"
                    |SI vCad = CADENA01;
                         naux = 1;
                    |FINSI;

                    || y si es linea "tipo.2"
                    aAlfa = vCad % -101;
                    |SI aAlfa = "]";
                         naux = 2;
                    |FINSI;

                    || hemos encontrado la palabra a partir de la cual
                    || eobtenemos empresa, aplicacion, directorio ...
                    |SI naux > 0;

                         IvCad = IvCad + naux; afApl = vCad; || por el cero
                         IvCad = IvCad + 1;                  || fich
                         IvCad = IvCad + 1;    afEmp = vCad;
                         IvCad = IvCad + 1;    afMen = vCad;

                         |QBT afApl; |QBT afEmp; |QBT afMen;
                         afMen = afMen - "}}";
                         |SI afMen ! "NULL";

                              || en funcion de los datos extraidos
                              || del ds.men, cargamos las aplicaciones.
                              |HAZ CargaAPL;
                         |FINSI;

                    |FINSI;

                    || pasamos a la siguiente palabra
                    IvCad = IvCad + 1;
               |SIGUE;
          |FINSI;

          || leemos la siguiente linea.
          |XLEE nFich, 255, aLinea;

     |SIGUE;
     |XCIERRA nFich;

     nSwIncluirSinDef = 0;
     |BUCLE CargaDEF;

     |REFRESCACONTROL nGrid1;
|FPRC;

|PROCESO MeteAplica;          ||Mete la aplicacion manualmente, en el MENU NO la detecta

     ||Obtenemos
     ||aCodAplica;
     ||aNomAplica;

     |VENTANA 1201, 2064, -1, 1, "";
     nVentana = FSalida;

     get1 = -1;
     get2 = -1;
     get3 = 0;
     get4 = nVentana;
     get5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", get;

     |PINPA #0#nifpara8;
     |PINDA #0#nifpara8;
     |ENDATOS #1#nifpara8;
     |SI FSalida = 0;
          aCodAplica = #nifpara8#0;
          aNomAplica = #nifpara8#1;
          |QBF aCodAplica;
          |QBF aNomAplica;
          |SI #nifpara8#2 = "S";
               nSwIncluirSinDef = 1;
          |SINO;
               nSwIncluirSinDef = 0;
          |FINSI;
     |FINSI;

     get1 = -1;
     get2 = -1;
     get3 = 0;
     get4 = nVentana;
     get5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", get;
     |FINVENTANA nVentana;

     |SI aCodAplica ! "";
          |DBASS aDBASS; |QBT aDBASS;

          |DDEFECTO #101;
          #101#0 = aCodAplica;
          |LEE 101#101.=;
          |SI FSalida ! 0; |GRABA 020#101.b; |FINSI;
          #101#1 = aNomAplica;
          |GRABA 020#101.a;
          |LIBERA #101;

          |BUCLE CargaDEF_manual;

          |REFRESCACONTROL nGrid1;
     |FINSI;

|FPRC;


|| -----------------------------------------------------------------
|PROCESO CargaAPL;

     || Para obtener el nombre de la aplicacion ...
     || consultamos la primera linea de fichero.men que se apunta
     || desde el ds.men principal.

     aAlfa = aDBASS + afApl + "/" + afMen + ".men";
     |XABRE "", aAlfa, nFich02;
     |SI FSalida < 0; |ACABA; |FINSI;
     |XLEE    nFich02, 255, aAlfa;
     |QBF aAlfa;
     aNombApl = aAlfa - "#:";
     |SI aNombApl = ""; aNombApl = "<DESCONOCIDO>"; |FINSI;
     |XCIERRA nFich02;

     || Grabando en el TMP.
     #101#00 = afApl;
     |LEE 101#101.=;
     |SI FSalida ! 0; |GRABA 020#101.b; |FINSI;
     #101#01 = aNombApl;
     |GRABA 040#101.a;
     |LIBERA #101;
|FPRC;


|PROCESO InfoDEF;
     afDef    = "";
     nClaves  = 0;
     aNombDef = "";
     aNombDat = "";

     || para que solo devuelva el nombre del def
     aAlfa  = aLosDefs - ".mas";
     afDef  = aAlfa    - aDirDefs;
     |QBT afDef;

     aAlfa  = aLosDefs - ".mas";
     |CARGA_DEF aAlfa, refer@;
     |SI FSalida ! 0; |ACABA; |FINSI;

     aVal = "|TITULO";
     aNombDef = #refer@#aVal#;

     aVal = "|FICHERO";
     aNombDat = #refer@#aVal#;
     aNombDat = aNombDat % -108;

     aVal = "|NK";
     aAlfa = #refer@#aVal#;
     nClaves= aAlfa;

     aVal = "|NC";
     enCampos = #refer@#aVal#;
     nTotal = enCampos - 1;
     |PARA Cont = 0; |SI Cont [ nTotal; |HACIENDO Cont = Cont + 1;
          Comodin = ("000" + Cont) % -103;
          Comodin = "|C" + Comodin;
          Comodin1 = Comodin + "NOMBRE";
          aBeta = #refer@#Comodin1#;

          |DDEFECTO #103;
          #103#0 = #101#0;
          #103#1 = afDef;
          #103#2 = Cont;
          |LEE 101#103.=;
          |SI FSalida ! 0;
               #103#3 = aBeta;
               |GRABA 020#103.n;
          |FINSI;
          |LIBERA #103;
     |SIGUE;

     |DESCARGA_DEF #refer@;
|FPRC;















|| ----------------------------------------------------------------
|| PROCESO ExisteSubCadena
|| Entrada: aCad -> cadena principal :: aSub -> lo que buscamos
|| salida : nPos -> el caracter donde empieza la cadena buscada
||                   XXYY como
|| ----------------------------------------------------------------
|| ----------------------------------------------------------------
|| PROCESO Cadena2Vector
|| Entrada: aCad -> cadena principal :: aDelim -> delimitador
|| Salida : vCad -> vector de cadenas :: nItVector -> num items
|| ----------------------------------------------------------------

|VARIABLES;
     _xx    = 0;
     _sw    = 0;
     _alfa  = "";
     _naux  = 0;
     _nLon  = 0;
     _aChar = "";
|FIN;



|PROCESO ExisteSubCadena;
     _alfa = aCad - aSub;
     nPos = FSalida;
|FPRC;



|PROCESO Cadena2Vector;
     nItVector = 0;
     IvCad     = vCad1 <-;
     vCad     = "";

     _alfa = aCad % 0; _nLon = _alfa;  || long. de la cadena
     _alfa = aCad;
     _sw = 0;
     |PARA _xx = 1; |SI _xx [ _nLon; |HACIENDO _xx = _xx + 1;

          _naux = (100 * _xx) + 1; _aChar = (_alfa % _naux);
          |SI _aChar = aDelim;
               _sw = 1;
               |SI vCad ! "";
                    nItVector = nItVector + 1;
                    IvCad     = IvCad + 1;
                    vCad      = "";
               |FINSI;
          |SINO;
              _sw = 0;
          |FINSI;

          |SI _sw = 0; vCad = vCad + _aChar; |FINSI;

     |SIGUE;

|FPRC;
