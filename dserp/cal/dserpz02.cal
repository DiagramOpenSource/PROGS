|| Recalculo la clave del DSERP dserpm09 Documentos ERP
|| Para que el numero de la clave, este relleno por la izquierda con ceros

|FICHEROS;
     dserpm09 #9;
|FIN;

|VARIABLES;
     aAplicacion = "";
     aDef = "";
     aClave = ""
     aLong = "";
     nLong = 0;
     aSerie = "";
     aNumero = "";
     nNumero = 0;
     aMensaje = "";
     aClaveErp = "";
     aCien = "";
     nContador = 0;
|FIN;

|PROCESO dserpm09;
     aAplicacion = #dserpm09#7;
     |QBF aAplicacion;
     |SI aAplicacion ! "dscomer9";
          |ACABA;
     |FINSI;

     aDef = #dserpm09#0;
     |QBF aDef;
     |SI aDef = "agifa010"; |O aDef = "agifa104"; |O aDef = "agifa084"; |O aDef = "agifa134"; |O aDef = "agifa071";
          aClave = #dserpm09#1;
          |QBF aClave;
          aLong = aClave % A0;
          nLong = aLong;
          |SI nLong > 10;
               |ACABA;
          |FINSI;

          nContador = nContador + 1;

          aSerie = aClave % 105;
          aNumero = aClave % 606;
          |QBF aNumero;
          nNumero = aNumero;

          aClaveErp = aSerie + ("000000" + nNumero) % -106;

          aCien = " " * 100;
          |QBF aClaveErp;
          aClaveErp = (aClaveErp + aCien) % 199;
          aClaveErp = aClaveErp + " ";

          |BORRA 020#dserpm09.a;
          #dserpm09#1 = aClaveErp;
          |GRABA 040#dserpm09.n;
     |FINSI;
|FPRC;

|DEFBUCLE dserpm09;
 #dserpm09#1;
 ;
 INICIO;
 FINAL;
 be;
 NULL, dserpm09;
|FIN;

|PROGRAMA;

     |CLS;

     nContador = 0;
     |BUCLE dserpm09;

     |SI nContador ! 0;
          |SI nContador = 1;
               aMensaje = "0000Recalculado " + nContador + " registro";
          |SINO;
               aMensaje = "0000Recalculados " + nContador + " registros";
          |FINSI;
     |SINO;
          aMensaje = "0000No hay ningun registro para recalcular";
     |FINSI;
     |MENAV aMensaje;
|FPRO;
