|FICHEROS;
    recapinm #0;
    rerendtr #1;
    revalore #2;
    revalono #3;
    rectacor #4;
    reincide #5;
    retotren #6;
    redatcon #7;
    retotrea #9;
|FIN;

|INCLUYE i_varcal;

|| **************************************************************************
                    PROCESOS DE LECTURAS DE LOS DISTINTOS FICHEROS
|| **************************************************************************

|PROCESO fich_01_1;     || Trata los Rendimientos del Trabajo;
alfa1="RENDIMIENTOS DEL TRABAJO --------------------------------------------";
tipore    = #1#28;
linea     = #1#1;
descri    = #1#4;
rendi     = #1#5;
sw_fich   = 1;
tipod     = #1#2;
perceptor = #1#3;
empreac   = #1#0;
periodo   = #1#29;

|SI #1#28 = "X";  |HAZ incide_01;  |FINSI;
|SI #1#28 ! "X";  |HAZ tipode_01;    |FINSI;
|FPRC;


|PROCESO fich_01_0;            || Trata el Capital Inmobiliario
alfa1 = "CAPITAL INMOBILIARIO------------------------------------------------";
tipod     = #0#2;
tipore    = #0#48;
descri    = #0#5;
linea     = #0#1;
rendi     = #0#47;
perceptor = #0#3;
numero1   = #0#49;
numero2   = #0#50;
numero3   = #0#51;
numero4   = #0#52;
numero5   = #0#53;
numero6   = #0#54;
sw_fich   = 0;
empreac   = #0#0;
tipoprop  = #0#15;
cantidad1 = 0;
periodo   = #0#59;

|SI #0#48 = "X"; |O #0#48 = "P"; |O #0#48 = "R";
    |HAZ incide_01;
|FINSI;

|SI #0#48 = "A"; |O #0#48 = "R";
    |SI #0#15 ! "N"; |HAZ tipode_01; |FINSI;
|FINSI;
|FPRC;

|PROCESO fich_01_2;            || Trata Valores Bolsa
alfa1 = "VALORES BOLSA ------------------------------------------------------";
tipod     = #2#2;
tipore    = #2#4;
descri    = #2#5;
linea     = #2#1;
rendi     = #2#33;
perceptor = #2#3;
numero1   = #2#7;
numero2   = #2#8;
numero3   = #2#9;
numero4   = #2#10;
numero5   = #2#11;
numero6   = #2#12;
sw_fich   = 2;
empreac   = #2#0;
tipoprop  = #2#5;
cantidad1 = 0;
periodo   = #2#34;

|SI #2#4 = "X"; |O #2#4 = "P"; |O #2#4 = "R";
    |HAZ incide_01;
|FINSI;

|SI #2#4 = "A"; |O #2#4 = "R";
    |SI #2#5 ! "N"; |HAZ tipode_01; |FINSI;
|FINSI;
|FPRC;

|PROCESO fich_01_3;            || Trata los Valores No Bolsa
alfa1 = "VALORES NO BOLSA Y FONDOS DE PENSIONES -----------------------------";
tipod     = #3#2;
tipore    = #3#4;
descri    = #3#14;
linea     = #3#1;
rendi     = #3#33;
perceptor = #3#3;
numero1   = #3#7;
numero2   = #3#8;
numero3   = #3#9;
numero4   = #3#10;
numero5   = #3#11;
numero6   = #3#12;
sw_fich   = 3;
empreac   = #3#0;
tipoprop  = #3#5;
cantidad1 = 0;
periodo   = #3#34;


|SI #3#4 = "X"; |O #3#4 = "P"; |O #3#4 = "R";
    |HAZ incide_01;
|FINSI;

|SI #3#4 = "A"; |O #3#4 = "R";
    |SI #3#5 ! "N"; |HAZ tipode_01; |FINSI;
|FINSI;
|FPRC;

|PROCESO fich_01_4;            || Trata las Cuentas Corrientes
alfa1 = "CUENTAS CORRIENTES Y OTROS -----------------------------------------";
tipod     = #4#2;
tipore    = #4#4;
descri    = #4#14;
linea     = #4#1;
rendi     = #4#23;
perceptor = #4#3;
numero1   = #4#7;
numero2   = #4#8;
numero3   = #4#9;
numero4   = #4#10;
numero5   = #4#11;
numero6   = #4#12;
sw_fich   = 4;
empreac   = #4#0;
tipoprop  = #4#5;
cantidad1 = 0;
periodo   = #4#25;


|SI #4#4 = "X"; |O #4#4 = "P"; |O #4#4 = "R";
    |HAZ incide_01;
|FINSI;

|SI #4#4 = "A"; |O #4#4 = "R";
    |SI #4#5 ! "N"; |HAZ tipode_01; |FINSI;
|FINSI;
|FPRC;

|| **************************************************************************
                        TRATAMIENTO DE LA LIQUIDACION
|| **************************************************************************

|PROCESO tipode_01;
  coefi11 = 1;
  funeral = 0;
  |SI tipod = "G";  |Y tippar = 1;   || Si es Ganancial  Y MATRIMONIO;
      |SI periodo = 0;
          coefi1= 50*parte1; coefi11 = .5; zz = 25; tipod="T"; |HAZ leeliq_01;
          coefi1= 50*parte1; coefi11 = .5; zz = 25; tipod="C"; |HAZ leeliq_01;
          coefi1=100*parte2; coefi11 =  1; zz=  50;  tipod="A"; |HAZ leeliq_01;
      |SINO;
          |SI periodo = 1;
              coefi1 = 50; coefi11 = .5; zz = 50; tipod  = "T"; |HAZ leeliq_01;
              coefi1 = 50; coefi11 = .5; zz = 50; tipod  = "C"; |HAZ leeliq_01;
          |SINO;
              coefi1 = 100; coefi11 = 1; zz =100; tipod  = "A"; |HAZ leeliq_01;
          |FINSI;
      |FINSI;
      |ACABA;
  |FINSI;

  |SI tipod = "G";  |Y tippar = 3;   || Si es Ganancial  Y SEPARACION;
      |SI periodo = 0;
          coefi1= 50*parte2; coefi11 = .5; zz = 25; tipod="T"; |HAZ leeliq_01;
          coefi1= 50*parte2; coefi11 = .5; zz = 25; tipod="C"; |HAZ leeliq_01;
          coefi1=100*parte1; coefi11 =  1; zz = 50; tipod="A"; |HAZ leeliq_01;
      |SINO;
          |SI periodo = 1;
              zz = 100;
              coefi1 = 100; coefi11 = 1; tipod  = "A"; |HAZ leeliq_01;
          |SINO;
              zz = 50;
              coefi1 =  50; coefi11 = .5;  tipod  = "T"; |HAZ leeliq_01;
              coefi1 =  50; coefi11 = .5;  tipod  = "C"; |HAZ leeliq_01;
          |FINSI;
      |FINSI;
      |ACABA;
  |FINSI;


  || Si es Ganancial, conjunta y SE MUERE EL;

  |SI tipod = "G";  |Y tippar = 2; |Y muerto = "T";  |Y obitus = 1;
      |SI periodo = 0;
          coefi1= 100*parte2; coefi11 = 1; zz = 50; tipod="C"; |HAZ leeliq_01;
          coefi1= 100*parte1; coefi11 = 1; zz = 50; tipod="A"; |HAZ leeliq_01;
      |SINO;
          |SI periodo = 1;
              zz = 100;
              coefi1 = 100; coefi11 = 1; tipod  = "A"; |HAZ leeliq_01;
          |SINO;
              zz = 100;
              coefi1 = 100; coefi11 = 1; tipod  = "C"; |HAZ leeliq_01;
          |FINSI;
      |FINSI;
      |ACABA;
  |FINSI;


  || Si es Ganancial, conjunta y SE MUERE ELLA;

  |SI tipod = "G";  |Y tippar = 2; |Y muerto = "C";  |Y obitus = 1;
      |SI periodo = 0;
          coefi1= 100*parte2; coefi11 = 1; zz = 50; tipod="T"; |HAZ leeliq_01;
          coefi1= 100*parte1; coefi11 = 1; zz = 50; tipod="A"; |HAZ leeliq_01;
      |SINO;
          |SI periodo = 1;
              zz = 100;
              coefi1 = 100; coefi11 = 1; tipod  = "A"; |HAZ leeliq_01;
          |SINO;
              zz = 100;
              coefi1 = 100; coefi11 = 1; tipod  = "T"; |HAZ leeliq_01;
          |FINSI;
      |FINSI;
      |ACABA;
  |FINSI;


  ||Si es Ganancial, individual Y SE MUERE EL;

  |SI tipod = "G";  |Y tippar = 2; |Y muerto = "T"; |Y obitus = 2;
      |SI periodo = 0;
          funeral = 1;
          coefi1=  50 * parte1; coefi11=.5; zz=  50; tipod = "T"; |HAZ leeliq_01;
          coefi1= 100 - coefi1; coefi11= 1; zz=  50; tipod = "C"; |HAZ leeliq_01;
      |SINO;
          |SI periodo = 1;
              coefi1  = 50;
              coefi11 = .5;
              zz = 50;
              tipod  = "T"; |HAZ leeliq_01;
              tipod  = "C"; |HAZ leeliq_01;
          |SINO;
              zz = 100; coefi11 = 1; coefi1 = 100; tipod  = "C"; |HAZ leeliq_01;
          |FINSI;
      |FINSI;
      |ACABA;
  |FINSI;

  ||Si es Ganancial, individual Y SE MUERE ELLA;

 |SI tipod = "G"; |Y tippar = 2; |Y muerto = "C"; |Y obitus = 2;
      |SI periodo = 0;
          funeral = 1;
          coefi1=  50 * parte1;  coefi11=.5; zz= 50; tipod = "C"; |HAZ leeliq_01;
          coefi1 = 100 - coefi1; coefi11= 1; zz= 50; tipod = "T"; |HAZ leeliq_01;
      |SINO;
          |SI periodo = 1;
              coefi11 = .5;
              coefi1 = 50;
              zz = 50;
              tipod  = "T"; |HAZ leeliq_01;
              tipod  = "C"; |HAZ leeliq_01;
          |SINO;
              zz = 100; coefi11 = 1; coefi1 = 100;  tipod  = "T"; |HAZ leeliq_01;
          |FINSI;
      |FINSI;
      |ACABA;
  |FINSI;

  |SI tipod ! "V";  |Y tippar = 1;      ||SI ES TITULAR O CONYUGE Y SE CASAN;
      |SI periodo = 0;
          coefi11 = 1;
          zz = 100;
          coefi1 =  100 * parte1;                |HAZ leeliq_01;
          coefi1 =  100 * parte2;  tipod = "A";  |HAZ leeliq_01;
      |SINO;
          |SI periodo = 1;
              coefi11 = 1;
              zz = 100;
              coefi1 = 100; |HAZ leeliq_01;
          |SINO;
              coefi11 = 1;
              zz = 100;
              coefi1 = 100;  tipod = "A";  |HAZ leeliq_01;
          |FINSI;
      |FINSI;
      |ACABA;
  |FINSI;

  |SI tipod ! "V";  |Y tippar = 3;      ||SI TITULAR O CONYUGE Y SE SEPARAN;
      |SI periodo = 0;
          coefi11 = 1;
          zz = 100;
          coefi1 =  100 * parte2;                |HAZ leeliq_01;
          coefi1 =  100 * parte1;  tipod = "A";  |HAZ leeliq_01;
      |SINO;
          |SI periodo = 1;
              coefi11 = 1;
              zz = 100;
              coefi1 = 100;  tipod = "A";  |HAZ leeliq_01;
          |SINO;
              coefi11 = 1;
              zz = 100;
              coefi1 = 100;                |HAZ leeliq_01;
          |FINSI;
      |FINSI;
      |ACABA;
  |FINSI;


  ||SI TITULAR O CONYUGE, conjunta Y SE MUERE UNO;
  |SI tipod ! "V";  |Y tippar = 2; |Y obitus = 1;
      |SI periodo = 0;
          coefi11 = 1;
          zz = 100;
          |SI muerto = "T";  |Y tipod = "T";
              coefi1 =  100;  tipod = "A";  |HAZ leeliq_01;
          |SINO;
              |SI muerto = "C";  |Y tipod = "C";
                  coefi1 =  100;  tipod = "A";  |HAZ leeliq_01;
              |SINO;
                  coefi1 =  100 * parte2;                |HAZ leeliq_01;
                  coefi1 =  100 * parte1;  tipod = "A";  |HAZ leeliq_01;
              |FINSI;
          |FINSI;
      |SINO;
          |SI periodo = 1;
              coefi11 = 1;
              zz = 100;
              coefi1 = 100;  tipod = "A";  |HAZ leeliq_01;
          |SINO;
              coefi11 = 1;
              zz = 100;
              coefi1 = 100;                |HAZ leeliq_01;
          |FINSI;
      |FINSI;
      |ACABA;
  |FINSI;

||SI TITULAR O CONYUGE, individual  Y SE MUERE UNO;

  |SI tipod ! "V"; |Y tippar = 2; |Y obitus = 2;
       coefi11 = 1;
       zz = 100;
       coefi1 = 100; |HAZ leeliq_01;
      |ACABA;
  |FINSI;

  |SI tippar = 1;          ||VARIOS Y SE CASAN;
      |SI periodo = 0;
          |SI numero1 ! 0;
              zz = numero1;
              coefi11 = numero1 / 100;
              coefi1 =  numero1 * parte1;
              tipod = "T";
              |HAZ leeliq_01;
          |FINSI;
          |SI numero2 ! 0;
              zz = numero2;
              coefi11 = numero2 / 100;
              coefi1 =  numero2 * parte1;
              tipod = "C";
              |HAZ leeliq_01;
          |FINSI;
          zz = numero1 + numero2;
          coefi1 = ( numero1 + numero2 ) * parte2;
          coefi11 = ( numero1 + numero2 ) / 100;
          tipod = "A"; |HAZ leeliq_01;
      |SINO;
          |SI periodo = 1;
             |SI numero1 ! 0;
                 zz = numero1;
                 coefi11 = numero1 / 100;
                 coefi1 = numero1;
                 tipod = "T";
                 |HAZ leeliq_01;
             |FINSI;
             |SI numero2 ! 0;
                 zz = numero2;
                 coefi11 = numero2 / 100;
                 coefi1 = numero2;
                 tipod = "C";
                 |HAZ leeliq_01;
             |FINSI;
          |SINO;
             zz = numero1 + numero2;
             coefi1 = numero1 + numero2;
             coefi11 = (numero1 + numero2) / 100;
             tipod = "A"; |HAZ leeliq_01;
          |FINSI;
      |FINSI;
      |ACABA;
  |FINSI;

  |SI tippar = 3;          ||VARIOS Y SE SEPARAN;
      |SI periodo = 0;
          |SI numero1 ! 0;
              zz = numero1;
              coefi11 = numero1 / 100;
              coefi1 =  numero1 * parte2;
              tipod = "T";
              |HAZ leeliq_01;
          |FINSI;
          |SI numero2 ! 0;
              zz = numero2;
              coefi11 = numero2 / 100;
              coefi1 =  numero2 * parte2;
              tipod = "C";
              |HAZ leeliq_01;
          |FINSI;
          zz = numero1 + numero2;
          coefi1 = ( numero1 + numero2 ) * parte1;
          coefi11 = ( numero1 + numero2 ) / 100;
          tipod = "A"; |HAZ leeliq_01;
      |SINO;
          |SI periodo = 1;
              zz = numero1 + numero2;
              coefi1 = numero1 + numero2;
              coefi11 = (numero1 + numero2) / 100;
              tipod = "A"; |HAZ leeliq_01;
          |SINO;
              |SI numero1 ! 0;
                  zz = numero1;
                  coefi1 = numero1;
                  coefi11 = numero1 / 100;
                  tipod = "T";
                  |HAZ leeliq_01;
              |FINSI;
              |SI numero2 ! 0;
                  zz = numero2;
                  coefi1 = numero2;
                  coefi11 = numero2 / 100;
                  tipod = "C";
                  |HAZ leeliq_01;
              |FINSI;
          |FINSI;
      |FINSI;
      |ACABA;
  |FINSI;


  |SI tippar = 2;  |Y obitus = 1;     ||VARIOS, conjunta Y SE MUERE;
      |SI periodo = 0;
          |SI numero1 ! 0;
              zz = numero1;
              coefi11 = numero1 / 100;
              coefi1 =  numero1 * parte2;
              tipod = "T";
              |HAZ leeliq_01;
          |FINSI;
          |SI numero2 ! 0;
              zz = numero2;
              coefi11 = numero2 / 100;
              coefi1 =  numero2 * parte2;
              tipod = "C";
              |HAZ leeliq_01;
          |FINSI;
          zz = numero1 + numero2;
          coefi1 = ( numero1 + numero2 ) * parte1;
          coefi11 = ( numero1 + numero2 ) / 100;
          tipod = "A"; |HAZ leeliq_01;
      |SINO;
          |SI periodo = 1;
              zz = numero1 + numero2;
              coefi1 = numero1 + numero2;
              coefi11 = (numero1 + numero2) / 100;
              tipod = "A"; |HAZ leeliq_01;
          |SINO;
              |SI numero1 ! 0;
                  zz = numero1;
                  coefi1 = numero1;
                  coefi11 = numero1 / 100;
                  tipod = "T";
                  |HAZ leeliq_01;
              |FINSI;
              |SI numero2 ! 0;
                  zz = numero2;
                  coefi1 = numero2;
                  coefi11 = numero2 / 100;
                  tipod = "C";
                  |HAZ leeliq_01;
              |FINSI;
          |FINSI;
      |FINSI;
      |ACABA;
  |FINSI;

  |SI tippar = 2;  |Y obitus = 2;     ||VARIOS, individual Y SE MUERE ALGUIEN;
      |SI numero1 ! 0;
          zz = numero1;
          coefi1 =  numero1;
          coefi11 =  numero1 / 100;
          tipod = "T";
          |HAZ leeliq_01;
      |FINSI;
      |SI numero2 ! 0;
          zz = numero2;
          coefi1 =  numero2;
          coefi11 =  numero2 / 100;
          tipod = "C";
          |HAZ leeliq_01;
      |FINSI;
  |FINSI;
|FPRC;

|PROCESO leeliq_01;                 || Lee la Liquidacion
  #6#0 = empreac;
  #6#141 = tipod;
  |LIBERA #6; |LEE 110#6=;
  FEstado = FSalida;
  |SI FSalida ! 0;
      |DDEFECTO #6;
      #6#0 = empreac;
      #6#141 = tipod;
  |FINSI;

  #9#0 = empreac;
  #9#1 = tipod;
  |LIBERA #9; |LEE 110#9=;
  FEstad1 = FSalida;
  |SI FSalida ! 0;
      |DDEFECTO #9;
      #9#0 = empreac;
      #9#1 = tipod;
  |FINSI;
  |SI sw_fich = 0; |HAZ grabafi_01_0; |FINSI;
  |SI sw_fich = 1; |HAZ grabafi_01_1; |FINSI;
  |SI sw_fich = 2; |HAZ grabafi_01_2; |FINSI;
  |SI sw_fich = 3; |HAZ grabafi_01_3; |FINSI;
  |SI sw_fich = 4; |HAZ grabafi_01_4; |FINSI;

  #6#213 = obitus;
  |SI FEstado = 0; |GRABA 021#6a; |SINO; |GRABA 021#6n; |FINSI;
  |SI FEstad1 = 0; |GRABA 021#9a; |SINO; |GRABA 021#9n; |FINSI;
|FPRC;

|PROCESO grabafi_01_0;          || Graba Capital Inmobiliario
  mayor = #0#8;
  ||SI #0#19 > mayor; mayor = #0#19; |FINSI;
  ||SI #0#17 > mayor; mayor = #0#17; |FINSI;

  |SI #0#13 = "S";
      #6#149 = #6#149 + ((#0#9 % coefi1) ! 0) * ratio;
      #6#7   = #6#7   + ((#0#9   % coefi1) ! 0) * ratio;
      #6#8   = (#6#6 + #6#39) - #6#7;
      |ACABA;
  |FINSI;

  |HAZ pordos;

  |SI #0#13 = "H"; |Y #0#4 = "U";
      #6#147 = #6#147 + ( (#0#009 % coefi1) ! 0 ) * ratio;
      |HAZ pordos;

      |SI #0#71 ! "S"; #6#156 = #6#156 + (rendimi) ! 0; |FINSI;
      |SI #0#71 = "S"; #6#43  = #6#43  + (rendimi) ! 0; |FINSI;

      |SI #0#71 ! "S"; #6#6   = #6#6  + (rendimi) ! 0;   |FINSI;
      |SI #0#71 = "S"; #6#39  = #6#39 + (rendimi) ! 0;   |FINSI;

      #6#7 = #6#7 + ((#0#9 % coefi1) ! 0) * ratio;
      |SI tipod = "A"; tope = 1000000; |SINO; tope = 800000; |FINSI;
      interes = ((#0#10 % coefi1) ! 0) * ratio;
      #6#148 = #6#148 + interes;
      |SI #6#148 > tope;
          |SI sw = 0;  |HAZ grabaci_01;  sw = 1;  |FINSI;
          alfa1 = "INTERESES VIV. HABITUAL AJUSTADOS EN LINEA ";
          alfa1 = alfa1 + #0#1 + " T. DECL. " + tipod;
          |HAZ grabaci_01;
          #6#007 = #6#007 - (#6#148 - tope) + interes;
          #6#148 = tope;
      |SINO;
          #6#007 = #6#007 + interes;
      |FINSI;
      #6#095 = #6#095 + (((#0#14 % coefi1) % 15)) * ratio;
      #6#166 = #6#166 + ((#0#14 % coefi1) ! 0) * ratio;
  |SINO;
    |SI #0#21 = "N"; |Y #0#13 = "O"; |Y #0#4 = "U";  || No Alquilada
        #6#149 = #6#149 + ((#0#009 % coefi1) ! 0) * ratio;
        |HAZ pordos;

        |SI #0#71 ! "S"; #6#152 = #6#152 + (rendimi) ! 0; |FINSI;
        |SI #0#71 = "S"; #6#44  = #6#44  + (rendimi) ! 0; |FINSI;

        |SI #0#71 ! "S"; #6#6   = #6#6   + (rendimi) ! 0; |FINSI;
        |SI #0#71 = "S"; #6#39  = #6#39  + (rendimi) ! 0; |FINSI;

        #6#7   = #6#7   + ((#0#9   % coefi1) ! 0) * ratio;
        #6#097 = #6#097 + (((#0#14 % coefi1) % 10)) * ratio;
        #6#168 = #6#168 + ((#0#14 % coefi1) ! 0) * ratio;
    |SINO;
        #6#097 = #6#097 + (((#0#14 % coefi1) % 10)) * ratio;
        #6#168 = #6#168 + ((#0#14 % coefi1) ! 0) * ratio;
        |SI #0#22 = "N";    || No Familiar
            #6#151 = #6#151 + ((#0#009 % coefi1) ! 0) * ratio;
            suma = #0#10;
            |SI #0#10 > #0#23;
                |SI sw = 0;  |HAZ grabaci_01;  sw = 1;  |FINSI;
                alfa1 = "INTERESES VIV. ALQUILER AJUSTADOS EN LINEA ";
                alfa1 = alfa1 + #0#1 + " T. DECL. " + tipod;
                |HAZ grabaci_01;
                suma = #0#23;
            |FINSI;
            #6#151 = #6#151 + ((#0#26 % coefi1)) * ratio;
            #6#150 = #6#150 + ((suma  % coefi1)) * ratio;
            #6#153 = #6#153 + ((#0#29 % coefi1)) + ((#0#28 % coefi1)) * ratio;
            |SI #0#30 [ 0;
                #6#154 = #6#154 - ((#0#30 % coefi1)) * ratio;
            |SINO;
                #6#154 = #6#154 + ((#0#30 % coefi1)) * ratio;
            |FINSI;
            #6#155 = #6#155 + ((#0#27 % coefi1)) * ratio;
            #6#157 = #6#157 + ((#0#23 % coefi1)) * ratio;

            |SI #0#71 ! "S"; #6#158 = #6#158 + ((#0#24 % coefi1)) * ratio; |FINSI;
            |SI #0#71 = "S"; #6#45  = #6#45  + ((#0#24 % coefi1)) * ratio; |FINSI;

            |SI #0#71 ! "S"; #6#6   = #6#6   + ((#0#24 % coefi1)) * ratio; |FINSI;
            |SI #0#71 = "S"; #6#39  = #6#39  + ((#0#24 % coefi1)) * ratio; |FINSI;

            #6#159 = #6#159 + ((#0#25 % coefi1)) * ratio;
        |SINO;
            suma = #0#10;
            |SI #0#10 > #0#23;
                |SI sw = 0;  |HAZ grabaci_01;  sw = 1;  |FINSI;
                alfa1 = "INTERESES VIV. ALQUILER AJUSTADOS EN LINEA ";
                alfa1 = alfa1 + #0#1 + " T. DECL. " + tipod;
                |HAZ grabaci_01;
                suma = #0#23;
            |FINSI;
            |SI #0#30 [ 0;
                suma = ((suma+#0#9+#0#26+#0#27+#0#28+#0#29-#0#30) % coefi1)*ratio;
            |SINO;
                suma = ((suma+#0#9+#0#26+#0#27+#0#28+#0#29+#0#30) % coefi1)*ratio;
            |FINSI;
            sumaalq = (((#0#23 + #0#24 + #0#25) % coefi1)) * ratio;
            |HAZ pordos;
            #6#162 = (rendimi) ! 0;
            #6#163 = sumaalq - suma;
            |SI #6#162 > #6#163; #6#163 = #6#162; |FINSI;
            #6#160 = #6#160 + #6#163;
        |FINSI;
    |FINSI;
  |FINSI;
  |SI #0#13 = "H";  |Y #0#31 = "S";
      |SI tippar = 1;    ||SE HAN CASADO;
          |SI tipod = "A";
              |SI #0#34 ] fecpar; |HAZ calculi;  |FINSI;
          |SINO;
              |SI #0#34 [ fecpar; |HAZ calculi;  |FINSI;
          |FINSI;
      |FINSI;
      |SI tippar = 3;   ||SE SEPARAN;
          |SI tipod = "A";
              |SI #0#34 [ fecpar; |HAZ calculi; |FINSI;
          |SINO;
              |SI #0#34 ] fecpar; |HAZ calculi; |FINSI;
          |FINSI;
      |FINSI;
      |SI tippar = 2;   ||SE MUEREN;
          |SI muerto = "T";
              |SI tipod = "C";
                  |HAZ calculi;
              |SINO;
                  |SI #0#34 [ fecpar; |HAZ calculi; |FINSI;
              |FINSI;
          |SINO;
              |SI tipod = "T";
                  |HAZ calculi;
              |SINO;
                  |SI #0#34 [ fecpar; |HAZ calculi; |FINSI;
              |FINSI;
          |FINSI;
      |FINSI;
  |FINSI;
  #6#8 = (#6#6 + #6#39) - #6#7;
  #6#9 = (#6#157 + #6#159 + #6#160) - (#6#150 + #6#151 + #6#153 + #6#154 + #6#155);
|FPRC;

|PROCESO calculi;
  |SI #0#66 ! 0;  |O #0#37 ! 0;
      #9#136 = #9#136 + #0#35 - #0#36;
      #9#137 = #9#137 + ((#0#70 % coefi1)) * ratio;
      #9#138 = #9#138 + ((#0#37 % coefi1)) * ratio;
      #9#139 = #9#139 + ((#0#66 % coefi1)) * ratio;
      aaa = (#9#138 + #9#139);
      bbb = aaa / #9#136;
      #9#140 = #9#137 * bbb;
  |FINSI;
|FPRC;

|PROCESO grabafi_01_1;          || Graba los Rendimientos del Trabajo
  |SI recalcu = 1;  |HAZ leepaga; |FINSI;
  cinco = #6#5;
  #6#1 = #6#1 + (#1#7 % coefi1) ! 0;
  #6#130 = #6#130 + (#1#9 % coefi1) ! 0;
  #6#142 = #6#142 + (#1#17 % coefi1) ! 0;
  #6#143 = #6#143 + (#1#18 % coefi1) ! 0;
  #6#144 = #6#144 + (#1#19 % coefi1) ! 0;
  aaa = ((#1#7 + #1#15 + #1#16) % coefi1) ! 0;    ||RENDIMIENTOS INTEGROS;
  |SI #1#2 = "T"; minusva = minusva1; |FINSI;
  |SI #1#2 = "C"; minusva = minusva2; |FINSI;
  |SI #1#2 = "1"; minusva = minusva3; |FINSI;
  |SI #1#2 = "2"; minusva = minusva4; |FINSI;
  |SI #1#2 = "3"; minusva = minusva5; |FINSI;
  |SI #1#2 = "4"; minusva = minusva6; |FINSI;
  |SI #1#2 = "T"; |Y aaa ! 0; #6#194 = 1; |FINSI;
  |SI #1#2 = "C"; |Y aaa ! 0; #6#195 = 1; |FINSI;
  |SI #1#2 = "1"; |Y aaa ! 0; #6#196 = 1; |FINSI;
  |SI #1#2 = "2"; |Y aaa ! 0; #6#197 = 1; |FINSI;
  |SI #1#2 = "3"; |Y aaa ! 0; #6#198 = 1; |FINSI;
  |SI #1#2 = "4"; |Y aaa ! 0; #6#199 = 1; |FINSI;
  |SI minusva = "S";
      #6#220 = #6#220 + (#1#20 % coefi1) ! 0;
  |SINO;
      #6#145 = #6#145 + (#1#20 % coefi1) ! 0;
  |FINSI;
  aaa = #6#220;
  |SI #6#220 > 600000;
      aaa = 600000;
      traba = "600.000";
      |HAZ otrosgas;
  |FINSI;
  |SI #6#145 > 250000;
      aaa = aaa + 250000;
      traba = "250.000";
     |HAZ otrosgas;
  |SINO;
      aaa = aaa + #6#145;
  |FINSI;
  |SI aaa > 600000;
      aaa = 600000;
      traba = "600.000";
      |HAZ otrosgas;
  |FINSI;
  #6#2 = #6#2 + ((#1#15 + #1#16) % coefi1) ! 0;
  #6#131 = #6#131 + (#1#16 % coefi1) ! 0;
  #6#4 = #6#4 + (#1#12 % coefi1) ! 0;
  #6#146 = #6#146 + (#1#11 % coefi1) ! 0;
  #6#3 = #6#142 + #6#143 + #6#144 + aaa;
  #6#5 = #6#1 + #6#2 + #6#4 - #6#3;
  #9#150 = #9#150 + (#1#27 % coefi1) ! 0;
  #9#151 = #9#151 + (( #1#26 - #1#27 ) % coefi1 ) ! 0;
  |SI tipod = "A"; |O tipod = "T";
      |SI #1#2 = "T";
          #6#200 = #6#200 + ((#1#11 + #1#12 + #1#13) % coefi1 ) ! 0;
          #6#214 = #6#214 + (#1#13 % coefi1) ! 0;
          #6#201 = #6#201 + (#6#5 - cinco);
          #6#202 = #6#202 + (#1#26 % coefi1) ! 0;
      |FINSI;
  |FINSI;
  |SI tipod = "A";  |O tipod = "C";
      |SI #1#2 = "C";
          #6#206 = #6#206 + ((#1#11 + #1#12 + #1#13) % coefi1 ) ! 0;
          #6#215 = #6#215 + (#1#13 % coefi1) ! 0;
          #6#207 = #6#207 + (#6#5 - cinco);
          #6#208 = #6#208 + (#1#26 % coefi1) ! 0;
      |FINSI;
  |FINSI;
  #6#135 = #6#135 + (#1#23 % coefi1) ! 0;
  traba = #1#14 % 102;
  |SI traba = "**";
      #9#155 = #9#155 + (#1#15 % coefi1) ! 0;
  |FINSI;
|FPRC;

|PROCESO grabafi_01_2;
reten = #2#21;
|SI coefi1 ! 100;
    #2#21 = #2#21 - 1 + .49;
    |SI #2#21 = -1; #2#21 = 0; |FINSI;
|FINSI;

#6#10 = #6#10 + ((#2#20 + #2#45) % coefi1);
#6#12 = #6#12 + ((#2#22 + #2#23) % coefi1);
#6#132 = #6#132 + (#2#21 % coefi1);

|SI #2#42  = "1"; #6#105 = #6#105 + ((#2#20 % coefi1) % 40); |FINSI;
|SI #2#42  = "2"; #6#105 = #6#105 + ((#2#20 % coefi1) % 26); |FINSI;
|SI #2#42  = "3"; #6#105 = #6#105 + ((#2#20 % coefi1) % 25); |FINSI;
|SI #2#42  = "A"; #6#105 = #6#105 + ((#2#20 % coefi1) % 5);  |FINSI;
|SI #2#42  = "C"; #6#105 = #6#105 + ((#2#20 % coefi1) % 10); |FINSI;
|SI #2#42  = "4"; #6#105 = #6#105 + ((#2#20 % coefi1) % 40); |FINSI;
|SI #2#42  = "5"; #6#105 = #6#105 + ((#2#20 % coefi1) % 26); |FINSI;
|SI #2#42  = "6"; #6#105 = #6#105 + ((#2#20 % coefi1) % 25); |FINSI;
|SI #2#42  = "B"; #6#105 = #6#105 + ((#2#20 % coefi1) % 5);  |FINSI;
|SI #2#42  = "D"; #6#105 = #6#105 + ((#2#20 % coefi1) % 10); |FINSI;

#9#2  = #9#2 + (#2#21 % coefi1);
#9#8  = #9#8 + ((#2#20 + #2#45) % coefi1);
#9#14 = #9#14 + ((#2#22 + #2#23) % coefi1);

#2#21 = reten;
|FPRC;

|PROCESO grabafi_01_3;
reten = #3#20;
|SI coefi1 ! 100;
    #3#20 = #3#20 - 1 + .49;
    |SI #3#20 = -1; #3#20 = 0; |FINSI;
|FINSI;

#6#10 = #6#10 + ((#3#19 + #3#48) % coefi1);
#6#12 = #6#12 + ((#3#21 + #3#22) % coefi1);
#6#132 = #6#132 + (#3#20 % coefi1);

#9#2 = #9#2 + (#3#20 % coefi1);
#9#8 = #9#8 + ((#3#19 + #3#48) % coefi1);
#9#14 = #9#14 + ((#3#21 + #3#22) % coefi1);

|SI #3#44 = "1"; #6#105 = #6#105 + ((#3#19 % coefi1) % 40); |FINSI;
|SI #3#44 = "2"; #6#105 = #6#105 + ((#3#19 % coefi1) % 26); |FINSI;
|SI #3#44 = "3"; #6#105 = #6#105 + ((#3#19 % coefi1) % 25); |FINSI;
|SI #3#44 = "A"; #6#105 = #6#105 + ((#3#19 % coefi1) % 5);  |FINSI;
|SI #3#44 = "C"; #6#105 = #6#105 + ((#3#19 % coefi1) % 10); |FINSI;
|SI #3#44 = "4"; #6#105 = #6#105 + ((#3#19 % coefi1) % 40); |FINSI;
|SI #3#44 = "5"; #6#105 = #6#105 + ((#3#19 % coefi1) % 26); |FINSI;
|SI #3#44 = "6"; #6#105 = #6#105 + ((#3#19 % coefi1) % 25); |FINSI;
|SI #3#44 = "B"; #6#105 = #6#105 + ((#3#19 % coefi1) % 5);  |FINSI;
|SI #3#44 = "D"; #6#105 = #6#105 + ((#3#19 % coefi1) % 10); |FINSI;

#3#20  = reten;
|FPRC;

|PROCESO grabafi_01_4;
reten = #4#18;
|SI coefi1 ! 100;
    #4#18 = #4#18 - 1 + .49;
    |SI #4#18 = -1; #4#18 = 0; |FINSI;
|FINSI;

|SI #4#28 = "1"; #6#105 = #6#105  + ((#4#17 % coefi1) % 40); |FINSI;
|SI #4#28 = "2"; #6#105 = #6#105  + ((#4#17 % coefi1) % 26); |FINSI;
|SI #4#28 = "3"; #6#105 = #6#105  + ((#4#17 % coefi1) % 25); |FINSI;
|SI #4#28 = "A"; #6#105 = #6#105  + ((#4#17 % coefi1) % 5);  |FINSI;
|SI #4#28 = "C"; #6#105 = #6#105  + ((#4#17 % coefi1) % 10); |FINSI;
|SI #4#28 = "4"; #6#105 = #6#105  + ((#4#17 % coefi1) % 40); |FINSI;
|SI #4#28 = "5"; #6#105 = #6#105  + ((#4#17 % coefi1) % 26); |FINSI;
|SI #4#28 = "6"; #6#105 = #6#105  + ((#4#17 % coefi1) % 25); |FINSI;
|SI #4#28 = "B"; #6#105 = #6#105  + ((#4#17 % coefi1) % 5);  |FINSI;
|SI #4#28 = "D"; #6#105 = #6#105  + ((#4#17 % coefi1) % 10); |FINSI;

|SI #4#13 = "RE";
    #9#23 = #9#23 + (#4#18 % coefi1);
    #6#12 = #6#12 + ((#4#21 + #4#22) % coefi1);
    #9#14 = #9#14 + ((#4#21 + #4#22) % coefi1);
    |ACABA;
|FINSI;
|SI #4#26 = "R";
    |SI #4#13 = "CC";  |O #4#13 = "CD";   |O #4#13 = "LB";  |O #4#13 = "IP";
        #9#3 = #9#3 + (#4#18 % coefi1);
        #9#9 = #9#9 + ((#4#17 + #4#31) % coefi1);
        #9#14 = #9#14 + ((#4#21 + #4#22) % coefi1);
    |FINSI;
    |SI #4#13 = "AF";
        #9#4 = #9#4 + (#4#18 % coefi1);
        #9#10 = #9#10 + ((#4#17 + #4#31) % coefi1);
        #9#14 = #9#14 + ((#4#21 + #4#22) % coefi1);
    |FINSI;
    |SI #4#13 = "AB";
        #9#5 = #9#5 + (#4#18 % coefi1);
        #9#11 = #9#11 + ((#4#17 + #4#31) % coefi1);
        #9#14 = #9#14 + (#4#21 % coefi1);
        #9#15 = #9#15 + (#4#22 % coefi1);
    |FINSI;
    |SI #4#13 = "RV";  |O #4#13 = "RT";
        Porce = 70;
        |SI #4#27 ] 50;    Porce = 50; |FINSI;
        |SI #4#27 ] 60;    Porce = 40; |FINSI;
        |SI #4#27 ] 70;    Porce = 30; |FINSI;
        |SI #4#13 = "RT";  Porce = 60; |FINSI;

        #9#12 = #9#12 + (((#4#17 % Porce) + #4#31) % coefi1);
        #9#6 = #9#6 + (#4#18 % coefi1);
        #9#14 = #9#14 + ((#4#21 + #4#22) % coefi1);
    |FINSI;
    |SI #4#13 = "OR";  |O #4#13 = "PI";   |O #4#13 = "SI";
        #9#7 = #9#7 + (#4#18 % coefi1);
        #9#13 = #9#13 + ((#4#17 + #4#31) % coefi1);
        #9#14 = #9#14 + ((#4#21 + #4#22) % coefi1);
    |FINSI;

    |SI #4#13 = "PA"; |O #4#13 = "LT";
        |SI #4#18 ! 0;
            #9#7 = #9#7 + (#4#18 % coefi1);
            #9#13 = #9#13 + ((#4#17 + #4#31) % coefi1);
            #6#10 = #6#10 + ((#4#17 + #4#31) % coefi1);
            #9#14 = #9#14 + ((#4#21 + #4#22) % coefi1);
        |SINO;
            #6#11 = #6#11 + ((#4#17 + #4#31) % coefi1);
            #9#14 = #9#14 + ((#4#21 + #4#22) % coefi1);
            |SI #4#13 = "PA"; #6#72 = 1; |FINSI;
        |FINSI;
    |SINO;
        |SI #4#13 = "RV"; |O #4#13 = "RT";
            Porce = 70;
            |SI #4#27 ] 50;    Porce = 50; |FINSI;
            |SI #4#27 ] 60;    Porce = 40; |FINSI;
            |SI #4#27 ] 70;    Porce = 30; |FINSI;
            |SI #4#13 = "RT";  Porce = 60; |FINSI;

            #6#10 = #6#10 + (((#4#17 % Porce) + #4#31) % coefi1);
        |SINO;
            #6#10 = #6#10 + ((#4#17 + #4#31) % coefi1);
        |FINSI;
    |FINSI;

    #6#12  = #6#12 + ((#4#21 + #4#22) % coefi1);
    #6#132 = #6#132 + (#4#18 % coefi1);
|SINO;
    #6#135  = #6#135 + (#4#18 % zz);
    liquido = ((#4#17 + #4#31) - #4#21 - #4#22) % zz;
    |SI #4#27 = 0;  #4#27 = 1;  |FINSI;
    anual   = liquido / #4#27;
    resto   = liquido - anual;
    #9#150 = #9#150 + anual;
    #9#151 = #9#151 + resto;
|FINSI;
#4#18  = reten;
|FPRC;

|| **************************************************************************
                           PROCESO DE INCIDENCIAS
|| **************************************************************************

|PROCESO incide_01;
|SI incisino = "S";
   |SI sw = 0; |HAZ grabaci_01; sw = 1; |FINSI;

   linea = "00" + linea;
   linea = linea % -102;

   rendi = "          " + rendi;
   rendi = rendi % -110;

   |QBF descri;
   descri = descri + blanco;
   descri = descri % 130;

   alfa1 = "NO CALCULO ->  " + linea + " " + descri + "    " + rendi;

   |SI tipore = "R";
       alfa1 = "RENTA ------>  " + linea + " " + descri + "    " + rendi;
   |FINSI;
   |SI tipore = "P";
       alfa1 = "PATRIMONIO ->  " + linea + " " + descri + "    " + rendi;
   |FINSI;
   |HAZ grabaci_01;
|FINSI;
|FPRC;

|PROCESO grabaci_01;
swcalcu = 1;
lininci = lininci + 5;
#5#0 = empreac;
#5#1 = lininci;
#5#2 = alfa1;
#5#3 = "S";
|GRABA 020#5n;
|LIBERA #5;
|FPRC;

|| **************************************************************************
                        PROCESO BUCLES
|| **************************************************************************

|DEFBUCLE leefic1;   || Bucle Rendimientos del Trabajo
#1#1;
;
empresa, 01;
emprefi, 99;
e;
NULL, fich_01_1;
|FIN;

|DEFBUCLE leefic0;    || Bucle del Capital Inmobiliario
#0#1;
;
empresa, 01;
emprefi, 99;
e;
NULL, fich_01_0;
|FIN;

|DEFBUCLE leefic2;    || Bucle Valor Bolsa
#2#1;
;
empresa, 01;
emprefi, 99;
e;
NULL, fich_01_2;
|FIN;

|DEFBUCLE leefic3;    || Bucle No Valor Bolsa
#3#1;
;
empresa, 01;
emprefi, 99;
e;
NULL, fich_01_3;
|FIN;

|DEFBUCLE leefic4;    || Bucle No Valor Bolsa
#4#1;
;
empresa, 01;
emprefi, 99;
e;
NULL, fich_01_4;
|FIN;

|PROCESO pordos;
  rendimi = 0;
  feini = "01.01.1995";
  fefin = "31.12.1995";
  |SI #0#18 > "01.01.1995";
      feini = #0#18;
  |FINSI;
  |SI #0#31 = "S";
      fefin = #0#34;
  |FINSI;

  dias11 = fefin - feini + 1;

  |SI tippar = 1; ||SE CASAN;
      |SI tipod ! "A";     ||ES TITULAR O CONJUGE;
          |SI fefin > fecpar;  fefin = fecpar;     |FINSI;
          |SI feini > fecpar;  feini = fefin  + 1;  |FINSI;
      |SINO;
          |SI feini < fecpar;  feini =  fecpar + 1;    |FINSI;
          |SI fefin < fecpar;  fefin = feini - 1;  |FINSI;
     |FINSI;
  |FINSI;

  |SI tippar = 3; ||SE SEPARAN;
     |SI tipod = "A";  ||ES el primer periodo;
          |SI fefin > fecpar;  fefin = fecpar;     |FINSI;
          |SI feini > fecpar;  feini = fefin + 1;  |FINSI;
      |SINO;
          |SI feini < fecpar;  feini =  fecpar + 1;    |FINSI;
          |SI fefin < fecpar;  fefin = feini - 1;  |FINSI;
     |FINSI;
  |FINSI;

  |SI tippar = 2;  |Y obitus = 1;    ||conjunta y SE MUERE
     |SI tipod = "A";  ||ES el primer periodo;
          |SI fefin > fecpar;  fefin = fecpar;     |FINSI;
          |SI feini > fecpar;  feini = fefin + 1;  |FINSI;
      |SINO;
          |SI feini < fecpar;  feini =  fecpar + 1;    |FINSI;
          |SI fefin < fecpar;  fefin = feini - 1;  |FINSI;
     |FINSI;
  |FINSI;

  dias1 = fefin - feini + 1;

  |SI tippar = 2;  |Y muerto = "T"; |Y obitus = 2;
      |SI tipod = "T";                 ||SE MUERE  EL TITULAR;
          |SI fefin > fecpar;  fefin = fecpar;      |FINSI;
          |SI feini > fecpar;  feini = fefin + 1;   |FINSI;
          dias2 = fefin - feini + 1;
      |FINSI;
  |FINSI;

  |SI tippar = 2;  |Y muerto = "C";  |Y obitus = 2;
      |SI tipod = "C";                 ||SE MUERE  EL CONYUGE;
          |SI fefin > fecpar;  fefin = fecpar;     |FINSI;
          |SI feini > fecpar;  feini = fefin + 1;   |FINSI;
          dias2 = fefin - feini + 1;
      |FINSI;
  |FINSI;
  dias = fefin - feini + 1;
  |SI dias < 0;  dias = 0; |FINSI;
  ratio = dias / 365;

  |SI tippar = 2;   |Y funeral = 1;
      |SI muerto = "C";
          |SI tipod ! "C";
              dias3 = dias1 - dias2 + (dias2 / 2);
              ratio = dias3 / 365;
          |FINSI;
      |SINO;
          |SI tipod ! "T";
              dias3 = dias1 - dias2 + (dias2 / 2);
              ratio = dias3 / 365;
          |FINSI;
      |FINSI;
  |FINSI;

  |SI periodo = 1;  ratio = 1;  |FINSI;

  valor1 = mayor * ratio * coefi11;

  |SI #0#71 = "S";
      rendimi = valor1 % 1.1;
  |SINO;
      rendimi = valor1 % 2;
  |FINSI;

  dias4 = fefin - feini + 1;

  |SI tippar = 2;   |Y funeral = 1;
      |SI muerto = "C";
          |SI tipod ! "C";  dias4 = dias3;  |FINSI;
      |SINO;
          |SI tipod ! "T";  dias4 = dias3;  |FINSI;
      |FINSI;
  |FINSI;

  |SI dias4 ] 0;
      ratio = dias4 / dias11;
  |SINO;
      ratio = 0;
  |FINSI;
  |SI periodo = 1;  ratio = 1; |FINSI;
  ratio = ratio ! 4;
  coefi1 = coefi11 * 100;

  |SI tippar = 2; |Y periodo = 2;
      ratio = 1;
  |FINSI;
|FPRC;

|PROCESO otrosgas;
  |SI sw = 0; |HAZ grabaci_01;   sw = 1;  |FINSI;
  alfa1 = "OTROS GASTOS AJUSTADOS A: " + traba + " T. DECL.: ";
  alfa1 = alfa1 + tipod;
  |HAZ grabaci_01;
|FPRC;

|PROCESO leepaga;
  |ABRE #7;
  #7#0 = empreac;
  |LEE 040#7=;
  |SI FSalida ! 0; |DDEFECTO #7; |FINSI;
  minusva1 = #7#5;
  minusva2 = #7#31;
  minusva3 = #7#88;
  minusva4 = #7#89;
  minusva5 = #7#90;
  minusva6 = #7#91;
  individu = 0;
  |SI #7#34 = "X";
      individu = 1;
  |FINSI;
  |CIERRA #7;
|FPRC;

|| **************************************************************************
                            INICIO
|| **************************************************************************

|PROCESO recalpa1;
|ABRE #5;
|ABRE #6;
|ABRE #9;

lininci = 0;
swpasa = 0;
|SI progra_1=1; sw=0; lininci = 0;    |BUCLE leefic1;  swpasa = 1; |FINSI;
|SI progra_2=1; sw=0; lininci = 300;  |BUCLE leefic0;  swpasa = 1; |FINSI;
|SI progra_3=1; sw=0; lininci = 600;  |BUCLE leefic2;  swpasa = 1; |FINSI;
|SI progra_4=1; sw=0; lininci = 900;  |BUCLE leefic3;  swpasa = 1; |FINSI;
|SI progra_5=1; sw=0; lininci = 1200; |BUCLE leefic4;  swpasa = 1; |FINSI;

|CIERRA #5;
|CIERRA #6;
|CIERRA #9;
|FPRC;
