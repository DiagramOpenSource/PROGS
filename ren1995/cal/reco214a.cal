|FICHEROS;
    reco214a #0;
    recapinm #1;         || capital inmobiliario
    redatcon #2;         || datos contribuyentes
    retabla3 #6;         || tarifa patrimonio
|FIN;

|VARIABLES;
    informe  = "";
    x        = 0;

    &diap    = "";
    &mespl   = "";
    &anyop   = "";
    &pobla   = "";
    mesp     = "";

    fech1 = @;
    fech2 = @;
    fech3 = @;
    fech4 = @;
    para1 = 0;
    para2 = 0;
    para3 = 0;
    para4 = 0;
    nume1 = 0;
    nume2 = 0;
    nume3 = 0;
    nume4 = 0;
    alfa1 = "";
    alfa2 = "";
    alfa3 = "";
    alfa4 = "";


    &nif_i    = "";
    &nombre_i = "";
    &el_01    = 0;
    &el_02    = 0;
    &el_03    = 0;
    &el_04    = 0;
    &el_05    = 0;
    &el_06    = 0;
    &el_07    = 0;
    &hasta    = 0;
    &resto    = 0;
    &cuota    = 0;
    &cuot2    = 0;
    &porce    = 0;

    coefi  = 0;
    base2  = 0;
    inicio = @;
    hay    = 0;
    sw_hay = 0;
    xxx    = 0;
    d_clien = 0;
    h_clien = 0;
|FIN;

|PROCESO limpia;
    el_01 = 0;
    el_02 = 0;
    el_03 = 0;
    el_04 = 0;
    el_05 = 0;
    el_06 = 0;
    el_07 = 0;
    hasta = 0;
    resto = 0;
    cuota = 0;
    porce = 0;
    base2 = 0;

    coefi = 0;
|FPRC;

|PROCESO parrilla; |TIPO 0;
|SI #0#2 = "N";
    |PARA xxx = 3; |SI xxx [ 20; |HACIENDO xxx = xxx + 1;
          #0xxx = 0;  |PINTA #0xxx;
    |SIGUE;

    |CAMPO_MODIFICA #0#03, 1, "N";  |CAMPO_MODIFICA #0#04, 1, "N";
    |CAMPO_MODIFICA #0#05, 1, "N";  |CAMPO_MODIFICA #0#06, 1, "N";
    |CAMPO_MODIFICA #0#07, 1, "N";  |CAMPO_MODIFICA #0#08, 1, "N";
    |CAMPO_MODIFICA #0#09, 1, "N";  |CAMPO_MODIFICA #0#10, 1, "N";
    |CAMPO_MODIFICA #0#11, 1, "N";  |CAMPO_MODIFICA #0#12, 1, "N";
    |CAMPO_MODIFICA #0#13, 1, "N";  |CAMPO_MODIFICA #0#14, 1, "N";
    |CAMPO_MODIFICA #0#15, 1, "N";  |CAMPO_MODIFICA #0#16, 1, "N";
    |CAMPO_MODIFICA #0#17, 1, "N";  |CAMPO_MODIFICA #0#18, 1, "N";
    |CAMPO_MODIFICA #0#19, 1, "N";  |CAMPO_MODIFICA #0#20, 1, "N";
    |CAMPO_MODIFICA #0#00, 1, "S";  |CAMPO_MODIFICA #0#01, 1, "S";
|SINO;
    #0#0 = 1;      |PINTA #0#0;
    #0#1 = 99999;  |PINTA #0#1;

    |CAMPO_MODIFICA #0#03, 1, "S";  |CAMPO_MODIFICA #0#04, 1, "S";
    |CAMPO_MODIFICA #0#05, 1, "S";  |CAMPO_MODIFICA #0#06, 1, "S";
    |CAMPO_MODIFICA #0#07, 1, "S";  |CAMPO_MODIFICA #0#08, 1, "S";
    |CAMPO_MODIFICA #0#09, 1, "S";  |CAMPO_MODIFICA #0#10, 1, "S";
    |CAMPO_MODIFICA #0#11, 1, "S";  |CAMPO_MODIFICA #0#12, 1, "S";
    |CAMPO_MODIFICA #0#13, 1, "S";  |CAMPO_MODIFICA #0#14, 1, "S";
    |CAMPO_MODIFICA #0#15, 1, "S";  |CAMPO_MODIFICA #0#16, 1, "S";
    |CAMPO_MODIFICA #0#17, 1, "S";  |CAMPO_MODIFICA #0#18, 1, "S";
    |CAMPO_MODIFICA #0#19, 1, "S";  |CAMPO_MODIFICA #0#20, 1, "S";
    |CAMPO_MODIFICA #0#00, 1, "N";  |CAMPO_MODIFICA #0#01, 1, "N";
|FINSI;
|FPRC;

|PROCESO calculo;
|SI #2#36 = "X"; |Y #1#2 = "G";
    coefi = 50;
|SINO;
    coefi = 100;
|FINSI;

#1#8  = #1#8  % coefi;
#1#9  = #1#9  % coefi;
#1#10 = #1#10 % coefi;
#1#14 = #1#14 % coefi;
#1#17 = #1#17 % coefi;
#1#18 = #1#18 % coefi;
#1#19 = #1#19 % coefi;
#1#20 = #1#20 % coefi;

el_01 = #1#8;         || valor catastral
|SI el_01 < #1#17; el_01 = #1#17; |FINSI;
|SI el_01 < #1#19; el_01 = #1#19; |FINSI;
|SI el_01 = 0; |ACABA; |FINSI;

el_02 = #1#9 + #1#10 + #1#14 + #1#20;     || deudas [02]
el_03 = el_01 - el_02;                    || base liquidable
|PARA para1 = 8; |SI para1 ] 1; |HACIENDO para1 = para1 - 1;
      para2 = para1 + 8;
      para3 = para1 + 16;
      |SI el_03 > #6para1;
          hasta = #6para1;
          cuota = #6para2;
          porce = #6para3;
          resto = el_03 - hasta;
          cuot2 = (resto % porce) ! 0;
          el_04 = cuota + cuot2;       || cuota integra
          |SAL;
      |FINSI;
|SIGUE;

|SI inicio < #1#18;
    nume1 = 365 - (#1#18 - inicio);
    base2 = (nume1 * #1#8) / 365;
|SINO;
    base2 = #1#8;
|FINSI;

|SI #1#71 = "N";
    el_05 = base2 % 2;
|SINO;
    el_05 = base2 % 1.1;
|FINSI;

el_06 = el_05 % 25;
el_07 = el_04 + el_06;

|SI coefi = 100;
    nif_i    = #2#2;  nombre_i = #2#1;   |PRINT;
|SINO;
    nif_i    = #2#2;  nombre_i = #2#1;   |PRINT;
    nif_i    = #2#29; nombre_i = #2#28;  |PRINT;
|FINSI;
sw_hay = 1;
|FPRC;

|PROCESO lee_capital1;
    hay = 1;
|ERROR10;
|FPRC;

|PROCESO lee_capital0;
    hay = 0;
|FPRC;

|DEFBUCLE lee_capital;
 #1#1;
 13;
 #2#0,  1, "H";
 #2#0, 99, "H";
 e;
 lee_capital0, lee_capital1;
|FIN;

|CALCULO impresio;
    nume1 = #2#0;
|BUCLE lee_capital;                       || lee capital inmobiliario
|SI hay = 0;     |ACABA;   |FINSI;

|HAZ limpia;
|HAZ calculo;
|FCAL;

|DEFBUCLE redatcon;
#2#1;
4;
d_clien, "N";
h_clien, "N";
e;
NULL, impresio;
|FIN;

|PROCESO impre; |TIPO 3;
alfa1  = "01.01." + #0#21;
inicio = alfa1;

|ABRE #6;
|LEE 000#6p;
|SI FSalida ! 0;
    |MENAV "    ATENCION!!! Tarifas Patrimonio vacias ...";
    |CIERRA #6;
    |ACABA;
|FINSI;
|CIERRA #6;

|IMPRE 0;
|SI FSalida ! 0; |ACABA; |FINSI;

|INFOR "reco214a";
|SI FSalida ! 0; |FINIMP; |ACABA; |FINSI;

sw_hay = 0;

|SI #0#2 = "N";
    d_clien = #0#0;
    h_clien = #0#1;
    |BUCLE redatcon;
|SINO;
    |PARA xxx = 3; |SI xxx [ 20; |HACIENDO xxx = xxx + 1;
          |SI #0xxx ! 0;
              d_clien = #0xxx;
              h_clien = #0xxx;
              |BUCLE redatcon;
          |FINSI;
    |SIGUE;
|FINSI;

|SI sw_hay = 1; |PIEINF; |FINSI;

|FININF;
|FINIMP;
|FPRC;

|PROCESO defecto; |TIPO 7;
    fech1 = ~;
    alfa1 = fech1;
    alfa1 = alfa1 % -104;
    nume1 = alfa1;
    nume1 = nume1 - 1;
    #0Campo = nume1;
|PINTA #0Campo;
|FPRC;
