|FICHEROS;
    resimple #0;
    rerendtr #1;
    recapinm #2;
    revalore #3;
    rectacor #4;
    rededu07 #5;
    rededu06 #6;
    redatcon #8;
|FIN;

|VARIABLES;
    &empresa = 0;
    &nombre = "";
    &nif = "";
    &indivi = "";
    alfa1 = "";
    alfa2 = "";
    alfa3 = "";
    alfa4 = "";
    nume1 = 0;
    nume2 = 0;
    nume3 = 0;
    nume4 = 0;
    fech1 = @;
    fech2 = @;
    fech3 = @;
    fech4 = @;
    acum_si = 0;
    acum_no = 0;
    porce_si = 0;
    porce_no = 0;
    inter = 0;
    desde = 0;
    hasta = 0;
    linea = 0;
    integro = 0;
    segsoci = 0;
|FIN;

|INCLUYE i_activo;

***************************************************************************
          CALCULOS DE PANTALLA
***************************************************************************

|PROCESO caldd; |TIPO 0;
    #0#41 = #0#36 % 15;  || enfermedad
    #0#42 = #0#37 % 10;  || seguros
    #0#43 = #0#38 % 15;  || alquiler
|SI #0#43 > 75000;  #0#43 = 75000;  |FINSI;
    #0#44 = #0#39 % 15;  || guarderia
|SI #0#44 > 25000;  #0#44 = 25000;  |FINSI;
    #0#45 = #0#40 % 10;  || donativos
|PINTA #0#41;
|PINTA #0#42;
|PINTA #0#43;
|PINTA #0#44;
|PINTA #0#45;
|FPRC;

|PROCESO calmo; |TIPO 0;
|SI #0#27 = 0;   #0#27 = #0#26 % 25;  |FINSI;
|SI #0#29 = 0;   #0#29 = #0#28 % 25;  |FINSI;
|PINTA #0#27;
|PINTA #0#28;
|FPRC;

|PROCESO calvh; |TIPO 0;
    #0#22 = #0#18 % 2;        || 2% valor catastral
    inter = #0#20;
|SI indivi = "S";|Y inter > 800000;  inter = 800000;  |FINSI;
|SI indivi = "N";|Y inter > 1000000;  inter = 1000000;  |FINSI;
    #0#23 = #0#19 + inter;         || gastos
    #0#24 = #0#22 - #0#23;         || neto
|PINTA #0#22;
|PINTA #0#23;
|PINTA #0#24;
|FPRC;

|PROCESO calrt; |TIPO 0;
|SI Campo ! 99;
        desde=0;      hasta=2;          || calculo total
|SINO;
        desde=linea;  hasta=linea;      || calculo parcial
|FINSI;
    acum_si = 0;
    acum_no = 0;
|PARA nume1 = desde; |SI nume1 [ hasta; |HACIENDO nume1 = nume1 + 1;
        nume2 = nume1 + 12;
    |SI #0nume1 = "T";|Y #8#05 = "S";  acum_si = acum_si + #0nume2;  |FINSI;
    |SI #0nume1 = "T";|Y #8#05 = "N";  acum_no = acum_no + #0nume2;  |FINSI;
    |SI #0nume1 = "C";|Y #8#31 = "S";  acum_si = acum_si + #0nume2;  |FINSI;
    |SI #0nume1 = "C";|Y #8#31 = "N";  acum_no = acum_no + #0nume2;  |FINSI;
|SIGUE;
    porce_si = acum_si % 15;
    porce_no = acum_no % 5;
|SI porce_si > 600000;  porce_si = 600000;  |FINSI;
|SI porce_no > 250000;  porce_no = 250000;  |FINSI;
    #0#15 = porce_si + porce_no;
|SI #0#15 > 600000;  #0#15 = 600000; |FINSI;           || deducciones

|SI Campo ! 99;
        #0#16 = (#0#12+#0#13+#0#14)-(#0#6+#0#7+#0#8)-#0#15; || neto total
|SINO;
        integro = linea + 12;
        segsoci = linea + 6;
        #0#16 = #0integro - #0segsoci - #0#15;        || neto parcial
|FINSI;

|PINTA #0#15;
|PINTA #0#16;
|FPRC;

***************************************************************************
          CALCULOS PROGRAMA
***************************************************************************

|PROGRAMA;
|CABEZA "Datos Simplificada";
|HAZ lee_contri;
|HAZ lectura;
|PINPA #0#0;
|PINDA #0#0;
|ENDATOS #1#0;
|SI FSalida = 0;
    |HAZ grabacion;
|FINSI;
|FPRO;

|PROCESO lee_contri;          || lee el contribuyente
|ABRE #8;
    #8#0 = empresa;
|LEE 021#8=;
|CIERRA #8;
|FPRC;

***************************************************************************
          CALCULOS DE LECTURA
***************************************************************************

|PROCESO lectura;
|HAZ lee_rt;
    Campo = 12;
|HAZ calrt;
|HAZ lee_vh;
|HAZ calvh;
|HAZ lee_mo1;
|HAZ lee_mo2;
|HAZ calmo;
|HAZ lee_dd1;
|HAZ lee_dd2;
|HAZ caldd;
|FPRC;

|PROCESO lee_rt;
|ABRE #1;
    #1#0 = empresa;
|PARA nume1 = 1; |SI nume1 [ 3; |HACIENDO nume1 = nume1 + 1;
        #1#1 = nume1;
    |LEE 001#1=;
    |SI FSalida = 0;
             nume2 = nume1 - 1;     || tipo
            #0nume2 = #1#2;
             nume2 = nume1 + 2;     || empresa/entidad
            #0nume2 = #1#4;
             nume2 = nume1 + 5;     || s.s.
            #0nume2 = #1#17;
             nume2 = nume1 + 8;     || retenciones
            #0nume2 = #1#9;
             nume2 = nume1 + 11;    || integro
            #0nume2 = #1#7;
    |FINSI;
|SIGUE;
|CIERRA #1;
|FPRC;

|PROCESO lee_vh;
|ABRE #2;
    #2#0 = empresa;
    #2#1 = 1;
|LEE 001#2=;
|SI FSalida = 0;
        #0#17 = #2#2;
        #0#18 = #2#8;
        #0#19 = #2#9;
        #0#20 = #2#10;
        #0#21 = #2#14;
        #0#24 = #2#47;
|FINSI;
|CIERRA #2;
|FPRC;

|PROCESO lee_mo1;
|ABRE #3;
    #3#0 = empresa;
    #3#1 = 1;
|LEE 001#3=;
|SI FSalida = 0;
        #0#24 = #3#2;
        #0#26 = #3#20;
        #0#27 = #3#21;
        #0#30 = #3#22;
|FINSI;
|CIERRA #3;
|FPRC;

|PROCESO lee_mo2;
|ABRE #4;
    #4#0 = empresa;
    #4#1 = 1;
|LEE 001#4=;
|SI FSalida = 0;
        #0#28 = #4#17;
        #0#29 = #4#18;
|FINSI;
|CIERRA #4;
|FPRC;

|PROCESO lee_dd1;
|ABRE #5;
    #5#0 = empresa;
|PARA nume1 = 1; |SI nume1 [ 4; |HACIENDO nume1 = nume1 + 1;
        #5#1 = nume1;
    |LEE 001#5=;
    |SI FSalida = 0;
             nume2 = nume1 + 30;
            #0nume2 = #5#2;
             nume2 = nume1 + 35;
            #0nume2 = #5#6;
             nume2 = nume1 + 40;
            #0nume2 = #5#7;
    |FINSI;
|SIGUE;
|CIERRA #5;
|FPRC;

|PROCESO lee_dd2;
|ABRE #6;
    #6#0 = empresa;
    #6#1 = 1;
|LEE 001#6=;
|SI FSalida = 0;
        #0#35 = #6#2;
        #0#40 = #6#5;
        #0#45 = #6#6;
|FINSI;
|CIERRA #6;
|FPRC;

***************************************************************************
          CALCULOS DE GRABACION
***************************************************************************

|PROCESO grabacion;
    npro = 1;
|HAZ gra_rt;
    npro = 2;
|HAZ gra_vh;
    npro = 3;
|HAZ gra_mo1;
    npro = 5;
|HAZ gra_mo2;
    npro = 26;
|HAZ gra_dd1;
    npro = 25;
|HAZ gra_dd2;
|FPRC;

|PROCESO gra_rt;
|ABRE #1;
|PARA nume1 = 1; |SI nume1 [ 3; |HACIENDO nume1 = nume1 + 1;
        #1#0 = empresa;
        #1#1 = nume1;
    |LEE 001#1=;
    |SI FSalida = 0;  |BORRA 020#1a;  |FINSI;
    |DDEFECTO #1;
        #1#0 = empresa;
        #1#1 = nume1;
         nume2 = nume1 - 1;     || tipo
        #1#2 = #0nume2;
         nume2 = nume1 + 2;     || empresa/entidad
        #1#4 = #0nume2;
         nume2 = nume1 + 5;     || s.s.
        #1#17 = #0nume2;
         nume2 = nume1 + 8;     || retenciones
        #1#9 = #0nume2;
         nume2 = nume1 + 11;    || integro
        #1#7 = #0nume2;
    |SI #1#7 ! 0;
        |SI #1#2 = "T";  #1#3 = "TITULAR";  |FINSI;
        |SI #1#2 = "C";  #1#3 = "CONYUGE";  |FINSI;
            Campo = 99;
            linea = nume1 - 1;
        |HAZ calrt;      || para que calcule el rendimiento por linea
            #1#20 = #0#15;    || deducciones
            #1#05 = #0#16;    || rendimiento
        |GRABA 020#1n;
        |HAZ sigrabo;
    |FINSI;
|SIGUE;
|CIERRA #1;
|FPRC;

|PROCESO gra_vh;
|ABRE #2;
    #2#0 = empresa;
    #2#1 = 1;
|LEE 001#2=;
|SI FSalida = 0;  |BORRA 020#2a;  |FINSI;
|DDEFECTO #2;
    #2#0 = empresa;
    #2#1 = 1;
    #2#2 = #0#17;
    #2#8 = #0#18;
    #2#9 = #0#19;
    #2#10 = #0#20;
    #2#14 = #0#21;
    #2#47 = #0#24;
|SI #2#8 ! 0;
    |SI #2#2 = "T";  #2#3 = "TITULAR";  |FINSI;
    |SI #2#2 = "C";  #2#3 = "CONYUGE";  |FINSI;
    |SI #2#2 = "G";  #2#3 = "GANANCIAL";  |FINSI;
    |GRABA 020#2n;
    |HAZ sigrabo;
|FINSI;
|CIERRA #2;
|FPRC;

|PROCESO gra_mo1;
|ABRE #3;
    #3#0 = empresa;
    #3#1 = 1;
|LEE 001#3=;
|SI FSalida = 0;  |BORRA 020#3a;  |FINSI;
|DDEFECTO #3;
    #3#0 = empresa;
    #3#1 = 1;
    #3#2 = #0#25;
    #3#20 = #0#26;
    #3#21 = #0#27;
    #3#22 = #0#30;
    #3#33 = #3#20 - #3#22;
|SI #3#20 ! 0;|O #3#21 ! 0;
    |SI #3#2 = "T";  #3#3 = "TITULAR";  |FINSI;
    |SI #3#2 = "C";  #3#3 = "CONYUGE";  |FINSI;
    |SI #3#2 = "G";  #3#3 = "GANANCIAL";  |FINSI;
    |GRABA 020#3n;
    |HAZ sigrabo;
|FINSI;
|CIERRA #3;
|FPRC;

|PROCESO gra_mo2;
|ABRE #4;
    #4#0 = empresa;
    #4#1 = 1;
|LEE 001#4=;
|SI FSalida = 0;  |BORRA 020#4a;  |FINSI;
|DDEFECTO #4;
    #4#0 = empresa;
    #4#1 = 1;
    #4#2 = #0#25;
    #4#17 = #0#28;
    #4#18 = #0#29;
    #4#23 = #4#17;
|SI #4#17 ! 0;|O #4#18 ! 0;
    |SI #4#2 = "T";  #4#3 = "TITULAR";  |FINSI;
    |SI #4#2 = "C";  #4#3 = "CONYUGE";  |FINSI;
    |SI #4#2 = "G";  #4#3 = "GANANCIAL";  |FINSI;
    |GRABA 020#4n;
    |HAZ sigrabo;
|FINSI;
|CIERRA #4;
|FPRC;

|PROCESO gra_dd1;
|ABRE #5;
|PARA nume1 = 1; |SI nume1 [ 4; |HACIENDO nume1 = nume1 + 1;
        #5#0 = empresa;
        #5#1 = nume1;
    |LEE 001#5=;
    |SI FSalida = 0;  |BORRA 020#5a;  |FINSI;
    |DDEFECTO #5;
        #5#0 = empresa;
        #5#1 = nume1;
         nume2 = nume1 + 30;
        #5#2 = #0nume2;
         nume2 = nume1 + 35;
        #5#6 = #0nume2;
         nume2 = nume1 + 40;
        #5#7 = #0nume2;
    |SI #5#7 ! 0;
        |SI #5#2 = "T";  #5#3 = "TITULAR";  |FINSI;
        |SI #5#2 = "C";  #5#3 = "CONYUGE";  |FINSI;
        |SI #5#2 = "G";  #5#3 = "GANANCIAL";  |FINSI;
        |SI nume1 = 1;  #5#4 = "E";  |FINSI;
        |SI nume1 = 2;  #5#4 = "S";  |FINSI;
        |SI nume1 = 3;  #5#4 = "A";  |FINSI;
        |SI nume1 = 4;  #5#4 = "G";  |FINSI;
        |GRABA 020#5n;
        |HAZ sigrabo;
    |FINSI;
|SIGUE;
|CIERRA #5;
|FPRC;

|PROCESO gra_dd2;
|ABRE #6;
    #6#0 = empresa;
    #6#1 = 1;
|LEE 001#6=;
|SI FSalida = 0;  |BORRA 020#6a;  |FINSI;
|DDEFECTO #6;
    #6#0 = empresa;
    #6#1 = 1;
    #6#2 = #0#35;
    #6#5 = #0#40;
    #6#6 = #0#45;
    #6#15 = "O";
|SI #6#6 ! 0;
    |SI #6#2 = "T";  #6#3 = "TITULAR";  |FINSI;
    |SI #6#2 = "C";  #6#3 = "CONYUGE";  |FINSI;
    |SI #6#2 = "G";  #6#3 = "GANANCIAL";  |FINSI;
    |GRABA 020#6n;
    |HAZ sigrabo;
|FINSI;
|CIERRA #6;
|FPRC;
