|FICHEROS;
     rem00049 #20;
     rew99998 #900,MANTE;
     rew00039 #939;
     retabla1 #401;
     espejo@ #99;
     rew99999 #999;
|FIN;

|VARIABLES;
     aAlfa         = "";
     aAlfa1        = "";
     aLong         = "";
     aQueQuiero    = "";
     aNumCampos    = "";
     aNumPanta     = "";
     aPathRenta    = "";
     aDef          = "";
     aPerceptor    = "";
     aEdad         = "";
     aEdadPer      = "";
     aTecla        = "";

     nLong         = 0;
     nCaracter     = 0;
     nPosi         = 0;
     nPanta        = 0;
     nNumCampos    = 0;
     nNumPanta     = 0;
     nCampo        = 0;
     nModo         = 0;
     nEntrada      = 0;
     nComunidad    = 0;
     nTipo         = 0;
     nLinea        = 0;
     nNumero       = 0;
     nInversion    = 0;
     nDeduccion    = 0;
     nHijosAnt     = 0;
     nHijosDed     = 0;
     nAscend       = 0;
     nMinusv       = 0;
     nEdadMayor    = 0;
     nTotalHijos   = 0;
     nHijos        = 0;
     nCantidad     = 0;
     nPorce        = 0;

     &enEmpresa    = 0;
     &enModo       = 0;
     &eaDeclarante = "";
     &enComunes    = 0;
     &enMinusvalia = 0;
     &eaMinuAyuda  = "";
     &eaFallece    = "";
     &eaSwVarios   = "";
     &eaVarDni     = "";
     &enSwLiquida  = 0;
     &enSwRetorno  = 0;
     &eaPrograma   = "";
     &eaPunto      = "";
     &enAutonomia  = 0;
|FIN;

|PROCESO Tipo12w98;  |TIPO 12;
|FPRC;

|| ************************************************************************
|| PROCESOS DE CONSULTA DE PANTALLAS NORMALES
|| ************************************************************************

|PROCESO MensaCatalunya;
     |SI FSalida ! 10;  |ACABA;  |FINSI;

     |PUSHV 0501 2380;
     |BLANCO 0719 2080;
     |CUADRO 0719 2080;

     |ATRI R;
     |PINTA 0820, "             INTRODUZCA 1 o 2 SEGUN PROCEDA                 ";
     |PINTA 0920, "------------------------------------------------------------";
     |ATRI r;

     |ATRI I;
     |PINTA 1020, "1.- Regimen general                                         ";
     |PINTA 1120, "2.- Regimen especial                                        ";
     |ATRI i;
     |PINTA 1220, "       - Tener 32 a¤os o menos en la fecha de devengo del   ";
     |PINTA 1320, "         impuesto, siempre y cuando su base imponible no sea";
     |PINTA 1420, "         superior a 30.000 Euros.                           ";
     |PINTA 1520, "       - Haber estado en paro durante 183 dias o mas durante";
     |PINTA 1620, "         el ejercicio.                                      ";
     |PINTA 1720, "       - Tener un grado de discapacidad igual o superior 65%";
     |PINTA 1820, "       - Formar parte de la unidad familiar con al menos un ";
     |PINTA 1920, "         hijo en la fecha de devengo del impuesto.          ";

     |LEETECLA aTecla;

     |POPV;

     |ERROR;
|FPRC;

|PROCESO Fecha;
     |SI #20#16 ! 2;  |ACABA;  |FINSI;

     |SI #20#12 < "27.04.2003";  |O #20#12 > "31.12.2006";
         |MENAV "0000 Fecha Erronea. Limite del 27.04.2003 al 31.12.2006";
         |ERROR;
     |FINSI;
|FPRC;

|PROCESO Cantidad;
     |SI #20#16 ! 2;  |ACABA;  |FINSI;

     |SI #20#13 > 9000;
         |MENAV "     Limite maximo 9.000 Euros.";
         |ERROR;
     |FINSI;
|FPRC;

|PROCESO Totalm020;
     #20#15 = #20#13 % #20#14;

     |SI #20#15 > #401#24;
         #20#15 = #401#24;
     |FINSI;
     |PINTA #20#15;
|FPRC;

|PROCESO CtasCtes;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     #939#0 = #20Campo % 104;
     #939#1 = #20Campo % 504;
     #939#2 = #20Campo % 902;
     #939#3 = #20Campo % 1110;

     |PUSHV 0501 2380;
     |PINPA #0#939;

     |ATRI R;
     |PINTA 1116, "            CODIGO CUENTA DECLARANTE        ";
     |ATRI r;

     |PINDA #0#939;
     |ENDATOS #1#939;
     |SI FSalida = 0;
         aAlfa     = #939#0 + #939#1 + #939#2 + #939#3;
         #20Campo = aAlfa;
     |FINSI;
     |POPV;
     |PINTA #20Campo;
|FPRC;

|PROCESO Tipo11m20;  |TIPO 11;
     |SI FSalida = 11;
         |SI #20#2 ! "VARIOS    ";  |ACABA;  |FINSI;

         #900#0 = #20#5;
         #900#1 = #20#6;
         #900#2 = #20#7;
         #900#3 = #20#8;
         #900#4 = #20#9;
         #900#5 = #20#10;

         |PUSHV 0501 2380;
         |PINPA #0#900;
         |PINDA #0#900;
         |LEETECLA aTecla;
         |POPV;
         |ERROR;
     |FINSI;
|FPRC;

|PROCESO Varios;  |TIPO 0;
     |SI #20#2 ! "VARIOS    ";
         #20#5  = 0;
         #20#6  = 0;
         #20#7  = 0;
         #20#8  = 0;
         #20#9  = 0;
         #20#10 = 0;
         |ACABA;
     |FINSI;

     |C_M #900#0,  1, "N";
     |C_M #900#1,  1, "N";
     |C_M #900#2,  1, "N";
     |C_M #900#3,  1, "N";
     |C_M #900#4,  1, "N";
     |C_M #900#5,  1, "N";

     aAlfa = eaSwVarios;
     aLong = aAlfa % 0;
     nLong = aLong;
     |PARA nCaracter = 1;  |SI nCaracter [ nLong;  |HACIENDO nCaracter = nCaracter + 1;
           nPosi = (100 * nCaracter) + 1;
           aAlfa1 = aAlfa % nPosi;
           |SI aAlfa1 = "T";  |C_M #900#0,  1, "S";  |FINSI;
           |SI aAlfa1 = "C";  |C_M #900#1,  1, "S";  |FINSI;
           |SI aAlfa1 = "1";  |C_M #900#2,  1, "S";  |FINSI;
           |SI aAlfa1 = "2";  |C_M #900#3,  1, "S";  |FINSI;
           |SI aAlfa1 = "3";  |C_M #900#4,  1, "S";  |FINSI;
           |SI aAlfa1 = "4";  |C_M #900#5,  1, "S";  |FINSI;
     |SIGUE;

     #900#0 = #20#5;
     #900#1 = #20#6;
     #900#2 = #20#7;
     #900#3 = #20#8;
     #900#4 = #20#9;
     #900#5 = #20#10;

     |PUSHV 0501 2380;

     |PINPA #0#900;
     |PINDA #0#900;
     |ENDATOS #1#900;
     |SI FSalida = 0;
         #20#5  = #900#0;
         #20#6  = #900#1;
         #20#7  = #900#2;
         #20#8  = #900#3;
         #20#9  = #900#4;
         #20#10 = #900#5;
     |FINSI;

     |POPV;
|FPRC;

|RUTINA ClaveBaja20;
     #20#0 = enEmpresa;
     #20#1 = 1;
|FRUT;

|RUTINA ClaveAlta20;
     #20#0 = enEmpresa;
     #20#1 = 99;
|FRUT;

|PROCESO PonDeclarante;  |TIPO 7;
     eaDeclarante = #20#2;
     enComunes    = 1;
     |HAZ SacaDeclarantes;
     #20#2  = eaDeclarante;
     |PINTA #20#2;
|FPRC;

|PROCESO Catalunya;
     |ABRE #999;
     |SELECT #2#999;
     #999#1 = #20#2;
     |LEE 000#999=;
     |SI FSalida ! 0;  |DDEFECTO #999;  |FINSI;
     |SELECT #1#999;
     |CIERRA #999;

     |C_M #20#17, 1, "N";
     |C_M #20#18, 1, "N";

     |SI #999#7 = 9;  |Y #20#16 = "1";
         |SI #20#2 ! "GANANCIAL ";
             |C_M #20#17, 1, "S";
         |SINO;
             |C_M #20#17, 1, "S";
             |C_M #20#18, 1, "S";
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO Fallece;  |TIPO 7;
     |SI eaFallece = "S";
         |C_M #20#4, 1, "S";
     |SINO;
         |C_M #20#4, 1, "N";  #20#4 = 0;  |PINTA #20#4;
     |FINSI;
|FPRC;

|PROCESO Tipo2m20;  |TIPO 2;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 1;  |O nModo = 3;
         enSwRetorno  = 1;
         |ACABA;
     |FINSI;

     nEntrada = 0 +. 1;
     |SI nEntrada ! 1;
         |PARA nCampo = 0;  |SI nCampo [ nNumCampos;  |HACIENDO nCampo = nCampo + 1;
               #99nCampo = #20nCampo;
         |SIGUE;
     |SINO;
         |PARA nCampo = 0;  |SI nCampo [ nNumCampos;  |HACIENDO nCampo = nCampo + 1;
               |SI #99nCampo ! #20nCampo;
                   enSwRetorno  = 1;
               |FINSI;
         |SIGUE;
     |FINSI;
|FPRC;

|PROGRAMA;
     |ABRE #401;
     |LEE 040#401p;
     |SI FSalida ! 0;  |DDEFECTO #401;  |FINSI;
     |CIERRA #401;

     aQueQuiero = "|NP";
     aNumPanta  = #20#aQueQuiero#;
     nNumPanta  = aNumPanta;
     nNumPanta  = nNumPanta - 1;

     aQueQuiero = "|NC";
     aNumCampos = #20#aQueQuiero#;
     nNumCampos = aNumCampos;
     nNumCampos = nNumCampos - 1;

     |DBASE aPathRenta;  |QBT aPathRenta;
     aDef = aPathRenta + "def/rem00049.mas";
     |CARGA_DEF aDef, espejo@;
     |SI FSalida ! 0;
         aAlfa = "     Error en cargar el Def rem00049.mas";
         |MENAV aAlfa;
         |ERROR;
     |FINSI;

     |ENTLINEAL #1#20, -2, enModo, 22, 1, ClaveBaja20, ClaveAlta20;

     |DESCARGA_DEF #espejo@;

     eaPrograma   = "rem00049";
     eaPunto      = "  ";
     |HAZ ProgramaActivo;
|FPRO;
