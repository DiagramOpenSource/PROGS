|FICHEROS;
     rem00053 #20;
     rew99998 #900,MANTE;
     espejo@ #99;
|FIN;

|VARIABLES;
     aAlfa         = "";
     aAlfa1        = "";
     aLong         = "";
     aQueQuiero    = "";
     aNumCampos    = "";
     aNumPanta     = "";
     aPathRenta    = "";
     aDef          = "";
     aTecla        = "";

     nLong         = 0;
     nCaracter     = 0;
     nPosi         = 0;
     nPanta        = 0;
     nNumCampos    = 0;
     nNumPanta     = 0;
     nCampo        = 0;
     nModo         = 0;
     nEntrada      = 0;
     nPrimera      = 0;

     &enEmpresa    = 0;
     &enModo       = 0;
     &eaDeclarante = "";
     &enComunes    = 0;
     &enMinusvalia = 0;
     &eaMinuAyuda  = "";
     &eaFallece    = "";
     &eaSwVarios   = "";
     &eaVarDni     = "";
     &enSwLiquida  = 0;
     &enSwRetorno  = 0;
     &eaPrograma   = "";
     &eaPunto      = "";
     &eaDni        = "";
     &enLinTrabajo = 0;
     &eaDeclaTraba = "";
|FIN;

|PROCESO Tipo12w98;  |TIPO 12;
|FPRC;

|| ************************************************************************
|| PROCESOS DE CONSULTA DE PANTALLAS NORMALES
|| ************************************************************************

|PROCESO Totalm020;
     |SI #20#12 = "S";
         |C_M #20#14, 1, "N";  #20#14 = 0;
         |C_M #20#15, 1, "N";  #20#15 = 0;
         |C_M #20#16, 1, "N";  #20#16 = 0;
         |C_M #20#23, 1, "N";  #20#23 = "N";
         |C_M #20#24, 1, "N";  #20#24 = "P";
         |C_M #20#25, 1, "N";  #20#25 = 0;

         |C_M #20#13, 1, "S";
         |C_M #20#17, 1, "S";
         |C_M #20#18, 1, "S";
         |C_M #20#19, 1, "S";
         |C_M #20#20, 1, "S";
         |C_M #20#26, 1, "S";

         |SI #20#13 = "S";  |Y #20#21 = "N";
             |C_M #20#18, 1, "N";
             #20#18 = 0;  |PINTA #20#18;
         |FINSI;
     |SINO;
         |C_M #20#14, 1, "S";
         |C_M #20#15, 1, "S";

         |SI #20#21 = "S";
             |C_M #20#15, 1, "S";
             |C_M #20#16, 1, "N";  #20#16 = 0;  |PINTA #20#16;
         |SINO;
             |C_M #20#15, 1, "N";  #20#15 = 0;  |PINTA #20#15;
             |C_M #20#16, 1, "S";
         |FINSI;

         |C_M #20#23, 1, "S";
         |C_M #20#24, 1, "S";
         |C_M #20#25, 1, "S";

         |C_M #20#13, 1, "N";   #20#13 = "";
         |C_M #20#17, 1, "N";   #20#17 = "";
         |C_M #20#18, 1, "N";   #20#18 = 0;
         |C_M #20#19, 1, "N";   #20#19 = 0;
         |C_M #20#20, 1, "N";   #20#20 = 0;
         |C_M #20#26, 1, "N";   #20#26 = 0;
     |FINSI;

     |SI #20#13 = "S";
         #20#17 = eaDni;
         |C_M #20#17, 1, "N";
     |FINSI;

     |SI #20#24 ! "M";
         |C_M #20#25, 1, "N";
         #20#25 = 0;
     |FINSI;

     |SI nPrimera = 0;
         |C_M #20#24, 1, "N";
         |C_M #20#25, 1, "N";
         #20#24 = "N";
         #20#25 = 0;
     |FINSI;

     |PINTA #20#13;
     |PINTA #20#14;
     |PINTA #20#15;
     |PINTA #20#16;
     |PINTA #20#17;
     |PINTA #20#18;
     |PINTA #20#19;
     |PINTA #20#20;
     |PINTA #20#23;
     |PINTA #20#24;
     |PINTA #20#25;
     |PINTA #20#26;
|FPRC;

|PROCESO LetraNif;  |TIPO 6;
     |SI FSalida ! 10;  |ACABA;  |FINSI;

     eaVarDni = #20Campo;
     |HAZ CalculoDNI;
     #20Campo = eaVarDni;
     |PINTA #20Campo;
     |ERROR;
|FPRC;

|PROCESO CompruebaNif;
     |SI #20#12 ! "S";  |ACABA;  |FINSI;

     |SI #20#17 = "            ";
         |MENAV "     Obligado a Introducir un NIF";
         |ERROR;
     |FINSI;
|FPRC;

|PROCESO Tipo11m20;  |TIPO 11;
     |SI FSalida = 11;
         |SI #20#2 ! "VARIOS    ";  |ACABA;  |FINSI;

         #900#0 = #20#5;
         #900#1 = #20#6;
         #900#2 = #20#7;
         #900#3 = #20#8;
         #900#4 = #20#9;
         #900#5 = #20#10;

         |PUSHV 0501 2380;
         |PINPA #0#900;
         |PINDA #0#900;
         |LEETECLA aTecla;
         |POPV;
         |ERROR;
     |FINSI;
|FPRC;

|PROCESO Varios;  |TIPO 0;
     |SI #20#2 ! "VARIOS    ";
         #20#5  = 0;
         #20#6  = 0;
         #20#7  = 0;
         #20#8  = 0;
         #20#9  = 0;
         #20#10 = 0;
         |ACABA;
     |FINSI;

     |C_M #900#0,  1, "N";
     |C_M #900#1,  1, "N";
     |C_M #900#2,  1, "N";
     |C_M #900#3,  1, "N";
     |C_M #900#4,  1, "N";
     |C_M #900#5,  1, "N";

     aAlfa = eaSwVarios;
     aLong = aAlfa % 0;
     nLong = aLong;
     |PARA nCaracter = 1;  |SI nCaracter [ nLong;  |HACIENDO nCaracter = nCaracter + 1;
           nPosi = (100 * nCaracter) + 1;
           aAlfa1 = aAlfa % nPosi;
           |SI aAlfa1 = "T";  |C_M #900#0,  1, "S";  |FINSI;
           |SI aAlfa1 = "C";  |C_M #900#1,  1, "S";  |FINSI;
           |SI aAlfa1 = "1";  |C_M #900#2,  1, "S";  |FINSI;
           |SI aAlfa1 = "2";  |C_M #900#3,  1, "S";  |FINSI;
           |SI aAlfa1 = "3";  |C_M #900#4,  1, "S";  |FINSI;
           |SI aAlfa1 = "4";  |C_M #900#5,  1, "S";  |FINSI;
     |SIGUE;

     #900#0 = #20#5;
     #900#1 = #20#6;
     #900#2 = #20#7;
     #900#3 = #20#8;
     #900#4 = #20#9;
     #900#5 = #20#10;

     |PUSHV 0501 2380;

     |PINPA #0#900;
     |PINDA #0#900;
     |ENDATOS #1#900;
     |SI FSalida = 0;
         #20#5  = #900#0;
         #20#6  = #900#1;
         #20#7  = #900#2;
         #20#8  = #900#3;
         #20#9  = #900#4;
         #20#10 = #900#5;
     |FINSI;

     |POPV;
|FPRC;

|RUTINA ClaveBaja20;
     #20#0 = enEmpresa;
     #20#1 = 1;
|FRUT;

|RUTINA ClaveAlta20;
     #20#0 = enEmpresa;
     #20#1 = 99;
|FRUT;

|PROCESO PonDeclarante;  |TIPO 7;
     |SI #20#21 = "S"; |Y enLinTrabajo = 0;
         |MENAV "      La linea viene del Trabajo, Modifique desde Trabajo";
         |PTEC 807;
         |ACABA;
     |FINSI;

     |SI #20#21 = "S"; |Y enLinTrabajo ! #20#22;
         |MENAV "      La pension esta introducida por otra linea de trabajo.";
         |PTEC 807;
         |ACABA;
     |FINSI;

     |SI #20#21 = "N"; |Y enLinTrabajo ! 0;  |Y #20#2 ! "          ";
         |MENAV "      La linea no viene del Trabajo, Modifique desde Pensiones letra J1"
         |PTEC 807;
         |ACABA;
     |FINSI;

     eaDeclarante = #20#2;
     enComunes    = 3;
     |HAZ SacaDeclarantes;
     #20#2 = eaDeclarante;
     |PINTA #20#2;

     |SI eaDeclarante ! eaDeclaTraba;  |Y enLinTrabajo ! 0;
         |MENAV "     Declarante Distinto al Seleccionado en el Trabajo";
         |PTEC 807;
         |ACABA;
     |FINSI;

     |SI enLinTrabajo ! 0;
         #20#21 = "S";
         #20#22 = enLinTrabajo;
     |FINSI;
|FPRC;

|PROCESO Fallece;  |TIPO 7;
     |SI eaFallece = "S";
         |C_M #20#4, 1, "S";
     |SINO;
         |C_M #20#4, 1, "N";  #20#4 = 0;  |PINTA #20#4;
     |FINSI;
|FPRC;

|PROCESO Tipo2m20;  |TIPO 2;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 1;  |O nModo = 3;
         |SI nModo = 3;  |Y #20#21 = "S"; |Y enLinTrabajo = 0;
             |MENAV "      La linea viene del Trabajo, borrela desde Trabajo";
             |ERROR;
             |ACABA;
         |FINSI;

         enSwRetorno  = 1;
         |ACABA;
     |FINSI;

     nEntrada = 0 +. 1;
     |SI nEntrada ! 1;
         |PARA nCampo = 0;  |SI nCampo [ nNumCampos;  |HACIENDO nCampo = nCampo + 1;
               #99nCampo = #20nCampo;
         |SIGUE;
     |SINO;
         |PARA nCampo = 0;  |SI nCampo [ nNumCampos;  |HACIENDO nCampo = nCampo + 1;
               |SI #99nCampo ! #20nCampo;
                   enSwRetorno  = 1;
               |FINSI;
         |SIGUE;
     |FINSI;
|FPRC;

|PROCESO Tipo7m20;  |TIPO 7;
     nPrimera = 0;
     |ABRE #99;
     #99#0 = #20#0;
     #99#1 = 1;
     |LEE 040#99];
     |SI FSalida = 0;  |Y #99#0 = #20#0;
         |SI #99#1 = #20#1;
             nPrimera = 1;
         |FINSI;
     |SINO;
         nPrimera = 1;
     |FINSI;
     |CIERRA #99;
|FPRC;

|PROGRAMA;
     aQueQuiero = "|NP";
     aNumPanta  = #20#aQueQuiero#;
     nNumPanta  = aNumPanta;
     nNumPanta  = nNumPanta - 1;

     aQueQuiero = "|NC";
     aNumCampos = #20#aQueQuiero#;
     nNumCampos = aNumCampos;
     nNumCampos = nNumCampos - 1;

     |DBASE aPathRenta;  |QBT aPathRenta;
     aDef = aPathRenta + "def/rem00053.mas";
     |CARGA_DEF aDef, espejo@;
     |SI FSalida ! 0;
         aAlfa = "     Error en cargar el Def rem00053.mas";
         |MENAV aAlfa;
         |ERROR;
     |FINSI;

     nPrimera = 1;
     |ENTLINEAL #1#20, -2, enModo, 22, 1, ClaveBaja20, ClaveAlta20;

     |DESCARGA_DEF #espejo@;

     eaPrograma   = "rem00053";
     eaPunto      = "  ";
     |HAZ ProgramaActivo;
|FPRO;
