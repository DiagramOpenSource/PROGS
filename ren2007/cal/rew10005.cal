|FICHEROS;
     rez00100 #0;
     rem01001 #1;
     rem01033 #10;
     rem01041 #12;
     reemi100 #13;
     rew01000 #86;
     rew01001 #87,MANTE;
     rew01002 #88,MANTE;
|FIN;

|VARIABLES;
     aIPRemoto    = "";
     aAlfa        = "";
     aAlfa1       = "";
     aDirModelo   = "";
     aDirSesion   = "";
     aRaiz        = "";
     aDirectorio  = "";
     aSesion      = "";
     aError       = "";
     aFichero     = "";
     aAbre        = "";
     aHandle      = "";
     aVariable    = "";
     aContr       = "";

     nRegistro    = 0;
     nHePasado    = 0;
     nNume        = 0;
     nCanal       = 0;

     &eaTipoSec   = "";
     &enMinEmp   = 0;
     &enMaxEmp   = 0;

     nCalc  = 0;
     nCalc1 = 0;
     &VEmpresa = 0;
     &VTipo  = "";
     &nSwDatos = 0;
     SwError = 0;
     Bloque  = 0;
     aTipo = "";
     Casilla = 0;
     nSw     = 0;
|FIN;

|RUTINA ClaveBaja86;
     #86#0 = 1;
|FRUT;

|RUTINA ClaveAlta86;
     #86#0 = 9999;
|FRUT;

|RUTINA ClaveBaja87;
     #87#0 = "        ";
|FRUT;

|RUTINA ClaveAlta87;
     #87#0 = "zzzzzzzz";
|FRUT;

|RUTINA ClaveBaja88;
     #88#0 = #87#0;
     #88#1 = 00001;
     #88#2 = "           ";
|FRUT;

|RUTINA ClaveAlta88;
     #88#0 = #87#0;
     #88#1 = 99999;
     #88#2 = "zzzzzzzzzzz";
|FRUT;

|| 컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
|| PROCESOS LLAMADOS DESDE EL REW01002
|| 컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴

|PROCESO Printar;
     |PRINT;
|FPRC;

|DEFBUCLE rew01000;
     #86#1;
     ;
     0001;
     9999;
     ;
     NULL, Printar;
|FIN;

|PROCESO Impresion;
     |IMPRE 0;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |INFOR "rez00100";
     |SI FSalida ! 0;  |FINIMP;  |ACABA;  |FINSI;

     |BUCLE rew01000;

     |PIEINF; |FININF;
     |FINIMP;
|FPRC;

|PROCESO CargaEntlineal;
     aSesion      = #88#0;  |QBT aSesion;
     |SI #reemi100#34 = "L";
        aFichero     = "/AEAT/" + eaTipoSec + "/" + #88#2;
     |SINO;
        aFichero     = "/AEAT/INTERNET/" + eaTipoSec + "/" + #88#2;
     |FINSI;
     |XABRE "EC", aFichero, nCanal;
     |SI FSalida < 0;
         |MENAV "     No existe el Fichero de Errores";
         |ACABA;
     |FINSI;

     |XABRE "CU", aFichero, nCanal;
     nRegistro = 0;
     |ABRE #86;
     |PARA; |SI; |HACIENDO;
            nNume     = 250;
            |XLEE nCanal, nNume, aVariable;
            |SI FSalida < 0;  |SAL;  |FINSI;

            nRegistro = nRegistro + 1;
            #86#0 = nRegistro;
            #86#1 = aVariable % 0190;
            |GRABA 020#86n;
            |LIBERA #86;
     |SIGUE;
     |XCIERRA nCanal;
     |CIERRA #86;

     |SI nRegistro = 0;
         |MENAV "     El Fichero esta vacio.";
         |ACABA;
     |FINSI;

     |PUSHV 0501 2380;
     |PINPA #0#86;
     |ATRI R;
     |PINTA 0644, eaTipoSec;
     |PINTA 0669, #88#1;
     |ATRI 0;

     |ENTLINEAL #1#86, -1, 4, 22, 0, ClaveBaja86, ClaveAlta86;
     |CONFI "2400NDesea Imprimir el Documento : ";
     |SI FSalida = 0;
         |HAZ Impresion;
     |FINSI;

     |POPV;

     |DELINDEX #86;
|FPRC;

|PROCESO Tipo11w0088;  |TIPO 11;
     |SI FSalida = 10;
         |HAZ CargaEntlineal;
     |FINSI;

     |SI FSalida = 11;
         |SUB_EJECUTA_NP "rem00998";
     |FINSI;
|FPRC;

|| 컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
|| PROCESOS LLAMADOS DESDE EL REW01001
|| 컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
|PROCESO Tipo7w0087;  |TIPO 7;
     |PTEC 807;
     |ENTLINEAL #1#88, -1, 4, 21, 0, ClaveBaja88, ClaveAlta88;
|FPRC;

|PROCESO Tipo11w0087;  |TIPO 11;
     |SI FSalida ! 10;  |ACABA;  |FINSI;
     |ENTLINEAL #1#88, -1, 4, 21, 0, ClaveBaja88, ClaveAlta88;
|FPRC;

|| 컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
|| PROCESOS LLAMADOS DESDE EL REZ00100
|| 컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴

|PROCESO MiraEmpresas;
     aIPRemoto = ""; |IP_REMOTO aIPRemoto;
     nRegistro   = 0;
     aSesion     = #87#0;  |QBF aSesion;
     aDirSesion  = "/AEAT/" + eaTipoSec + "/";
     aFichero    = "/AEAT/" + eaTipoSec + "/*.*";
     |R_PDIR aFichero;
     |PARA; |SI FSalida = 0; |HACIENDO;
            aAlfa = aFichero; |QBF aAlfa;
            aAlfa = aAlfa < aAlfa;
            aAlfa = aAlfa - ".txt";
            |SI FSalida ! 0;
               aError = aFichero - aDirSesion;
               aAlfa  = aError % 305;
               nCalc = aAlfa; nSw = 0;
               |SI VEmpresa ! 0;
                  |SI nCalc = VEmpresa; nSw = 1; |FINSI;
               |SINO;
                  |SI enMinEmp ! 0; |Y enMaxEmp ! 0;
                     |SI nCalc ] enMinEmp; |Y nCalc [ enMaxEmp; nSw = 1; |FINSI;
                  |SINO;
                     nSw = 1
                  |FINSI;
               |FINSI;

               |SI nSw = 1;
                  aContr = (aError % 801) > " ";
                  aAlfa1 = (aError % 102) > "  ";
                  #1#0   = aAlfa;
                  #1#1   = 0;
                  |SI aContr = "T";
                     |SI aAlfa1 = "OR"; #1#2 = "TITULAR"; |SINO; #1#2 = "ZPATRITIT"; |FINSI;
                  |FINSI;
                  |SI aContr = "C";
                     |SI aAlfa1 = "OR"; #1#2 = "CONYUGE"; |SINO; #1#2 = "ZPATRICON"; |FINSI;
                  |FINSI;
                  |SI aContr = "A";  #1#2 = "CONJUNTA";    |FINSI;
                  |SI aContr = "1";  #1#2 = "1 FAMILIAR";  |FINSI;
                  |SI aContr = "2";  #1#2 = "2 FAMILIAR";  |FINSI;
                  |SI aContr = "3";  #1#2 = "3 FAMILIAR";  |FINSI;
                  |SI aContr = "4";  #1#2 = "4 FAMILIAR";  |FINSI;
                  |LEE 040#1=;
                  |SI FSalida = 0;
                      nHePasado = 1;
                      nRegistro = 1;
                      #88#0 = #87#0;
                      #88#1 = #1#0;
                      #88#2 = aError;
                      #88#3 = #1#7;
                      |GRABA 020#88n;
                      |LIBERA #88;

                      #1#6 = "N"; |GRABA 020#1a; |LIBERA #1;
                  |FINSI;
               |FINSI;
            |FINSI;
            |R_SDIR aFichero;
     |SIGUE;

     |SI nRegistro = 0;
         |BORRA 020#87a;
         |LIBERA #87;
     |FINSI;
|FPRC;

|DEFBUCLE rew01001;
     #87#1;
     ;
     "        ";
     "zzzzzzzz";
     be;
     NULL, MiraEmpresas;
|FIN;

|PROCESO Errores;
     aFichero = ("86" + Usuario + "zzzzzzzz") % 108;
     |NOME_DAT #86 aFichero;
     |ABRE #86;  |CIERRA #86;  |DELINDEX #86;

     aFichero = ("87" + Usuario + "zzzzzzzz") % 108;
     |NOME_DAT #87 aFichero;
     |ABRE #87;  |CIERRA #87;  |DELINDEX #87;

     aFichero = ("88" + Usuario + "zzzzzzzz") % 108;
     |NOME_DAT #88 aFichero;
     |ABRE #88;  |CIERRA #88;  |DELINDEX #88;

     |ABRE #87;
     #87#0 = "DIRECTA";
     |LEE 101#87=;
     |SI FSalida ! 0;
         nRegistro = 1;
         #87#0 = "DIRECTA";
         |GRABA 020#87n;
     |FINSI;
     |LIBERA #87;
     |CIERRA #87;

     nHePasado = 0;
     |ABRE #1;
     |ABRE #88;
     |BUCLE rew01001;
     |CIERRA #1;
     |CIERRA #88;

     |SI nHePasado = 1;
         #0#0 = eaTipoSec;
         |PINPA #0#0;
         |PINDA #0#0;
         |ENTLINEAL #1#87, -1, 4, 21, 0, ClaveBaja87, ClaveAlta87;
     |FINSI;

     |DELINDEX #86;
     |DELINDEX #87;
     |DELINDEX #88;
|FPRC;
