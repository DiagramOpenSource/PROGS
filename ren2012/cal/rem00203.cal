|FICHEROS;
     rem00100;
     rem00200;
     rem00201;
     rem00202;
     rem00203;

     rew00050;
     rew00051;
|FIN;

|VARIABLES;
     nVentana        = 0;
     nVentTrab       = 0;
     nVentRev        = 0;
     nPanta          = 0;
     nPanta800       = 0;
     nRango          = 0;
     nGrid           = 0;
     nGrid1          = 0;
     nGrid2          = 0;
     nGrid3          = 0;
     nGrid4          = 0;
     nGrid201        = 0;
     nBtnAlta        = 0;
     nBtnModi        = 0;
     nBtnBaja        = 0;
     nBtnReva        = 0;
     nCampo          = 0;
     nCampoTmp       = 0;
     nCampo800       = 0;
     nLinea          = 0;
     nIDE            = 0;
     nCoef           = 0;
     nBtnSal         = 0;
     nValores        = 0;
     nPorce          = 0;
     nHay            = 0;
     nLong           = 0;
     nLongDec        = 0;
     nNegativo       = 0;
     nCalc1          = 0;
     nCalc2          = 0;
     nCont1          = 0;
     nSwDatos        = 0;
     nClave          = 0;
     nActivo         = 0;
     nCanti          = 0;
     nPasada         = 0;
     nHandle         = 0;
     nCarac          = 0;
     nCont           = 0;

     aEsc1           = "";
     aEsc2           = "";
     aRetu           = "";
     aTecla          = "";
     aDef            = "";
     aMas            = "";
     aAlfa           = "";
     aAlfa1          = "";
     aAlfa2          = "";
     aAlfa4          = "";
     aTipoDato       = "";
     aVacio1         = "";
     aVacio2         = "";
     aLong           = "";
     aFichero        = "";
     aCRLF           = "";
     aTipo           = "";
     aCadena         = "";
     aNombre         = "";
     aTmp            = "";
     aFicTxt         = "";
     aCuenta         = "";
     aDirAEAT        = "";
     aDirInternet    = "";
     aOrigen         = "";
     aDestino        = "";

 {-1}aVent           = "";
     aVent1          = 0;
     aVent2          = 0;
     aVent3          = 0;
     aVent4          = 0;
     aVent5          = "";

    {1}aContiene    = "";

     &enEmpresa      = 0;
     &enModo         = 0;
     &enModoReg      = 0;
     &eaClave        = "";
     &eaLetra        = "";
|FIN;

|PROCESO CopiaFichero;
     aAlfa = "";
     |IP_REMOTO aAlfa;  |QBT aAlfa;
     |SI aAlfa ! "";
         |ENVIA_FICHERO aOrigen, aDestino;
     |SINO;
         |COPIA_FICHERO aOrigen, aDestino;
     |FINSI;
|FPRC;

|PROCESO CargaCaracter;
     |SI aAlfa1 ] "A"; |Y aAlfa1 [ "Z";
         |ACABA;
     |FINSI;

     |SI aAlfa1 ] "0"; |Y aAlfa1 [ "9";
         |ACABA;
     |FINSI;

     |SI aAlfa1 = "¥"; aAlfa1 = &209;  |ACABA; |FINSI;
     |SI aAlfa1 = "¤"; aAlfa1 = &209;  |ACABA; |FINSI;

     aAlfa4 = &128;
     |SI aAlfa1 = aAlfa4; aAlfa1 = "Ç";   |ACABA; |FINSI;

     aAlfa4 = &135;
     |SI aAlfa1 = aAlfa4; aAlfa1 = "ç";  |ACABA; |FINSI;
     |SI aAlfa1 = "-"; aAlfa1 = &45;     |ACABA; |FINSI;
     |SI aAlfa1 = "'"; aAlfa1 = &49;     |ACABA; |FINSI;
     |SI aAlfa1 = "/";                   |ACABA; |FINSI;

     |SI aTipoDato = "An";
         |SI aAlfa1 = "&"; aAlfa1 = &38;    |ACABA; |FINSI;
         |SI aAlfa1 = ","; aAlfa1 = &44;    |ACABA; |FINSI;
         |SI aAlfa1 = "."; aAlfa1 = &46;    |ACABA; |FINSI;
         |SI aAlfa1 = ":"; aAlfa1 = &58;    |ACABA; |FINSI;
         |SI aAlfa1 = ";"; aAlfa1 = &59;    |ACABA; |FINSI;
         |SI aAlfa1 = "_"; aAlfa1 = &95;    |ACABA; |FINSI;
     |FINSI;

     aAlfa1 = " ";
|FPRC;

|PROCESO Formateo;
     |QBF aAlfa;

     |SI aTipoDato = "F";
         aAlfa = aAlfa - "."; aAlfa = aAlfa - ".";  |QBF aAlfa;

         |SI aAlfa = "";         aAlfa = "0" * 8; |FINSI;
         |SI aAlfa = "01010000"; aAlfa = "0" * 8; |FINSI;

         |ACABA;
     |FINSI;

     |SI aTipoDato = "N";
         aVacio1   = "0" * nLong;
         aVacio2   = "0" * nLongDec;
         aAlfa     = aAlfa - "-";
         nNegativo = 0;
         |SI FSalida ! 0;  nNegativo = 1;  |FINSI;
     |SINO;
         aVacio1 = " " * nLong;
     |FINSI;

     |SI aTipoDato ! "N";
         nCalc1 = 100 + nLong; aAlfa = (aAlfa + aVacio1) % nCalc1;
     |SINO;
         |SI nLongDec = 0;
             nCalc1 = 0 - (100 + nLong);
             aAlfa  = (aVacio1 + aAlfa) % nCalc1;
         |SINO;
             aAlfa  = aAlfa - ".";
             |SI FSalida = 0;
                 nCalc1 = 0 - (100 + nLong);
                 aAlfa  = (aVacio1 + aAlfa) % nCalc1;
                 aAlfa1 = "0" * nLongDec;
             |SINO;
                 nCalc1 = (((FSalida / 100) ! 0) - 1) + 100;
                 nCalc2 = (((FSalida / 100) ! 0) * 100) + 99;
                 aAlfa1 = aAlfa % nCalc2;
                 aAlfa  = aAlfa % nCalc1;
                 nCalc1 = 0 - (100 + nLong);
                 aAlfa  = (aVacio1 + aAlfa)  % nCalc1;
                 nCalc1 = 100 + nLongDec;
                aAlfa1 = (aAlfa1 + aVacio2) % nCalc1;
             |FINSI;

             aAlfa = aAlfa + aAlfa1;
        |FINSI;

        |SI nNegativo = 1;
            nCalc1 = 0 - (100 + (nLong + nLongDec)); nCalc1 = nCalc1 + 1;
            aAlfa  = aAlfa % nCalc1;
            aAlfa  = "N" + aAlfa;
        |FINSI;
     |FINSI;

     aAlfa2 = "";

     |SI aTipoDato = "A"; |O aTipoDato = "An";
         aLong = aAlfa % 0; nLong = aLong;
         |PARA nCont1 = 1; |SI nCont1 [ nLong; |HACIENDO nCont1 = nCont1 + 1;
               nCalc1 = (nCont1 * 100) + 1; aAlfa1  = aAlfa % nCalc1;
               aAlfa1 = aAlfa1 > aAlfa1;
               |SI aAlfa1 ! " ";
                   |HAZ CargaCaracter;
               |FINSI;
               aAlfa2 = aAlfa2 + aAlfa1;
         |SIGUE;
         aAlfa = aAlfa2;
     |FINSI;

     nCalc1 = aAlfa;
     |SI nSwDatos = 0; |Y nCalc1 ! 0; nSwDatos = 1; |FINSI;
|FPRC;

|PROCESO Epigrafe;
     |QBF aAlfa;
     |SI aAlfa = "9999"; aAlfa = ""; |FINSI;
     aLong = aAlfa % 0; nLong = aLong;

     |SI nLong = 4;
        aAlfa = (aAlfa % 0103) + "." + (aAlfa % 0401);
     |FINSI;
|FPRC;

|PROCESO GrabaCadena;
     |XGRABA nHandle, aCadena;
|FPRC;

|PROCESO rew00050;
     |SI nPasada = 0;
         aCadena = "001";  |HAZ GrabaCadena;
         nLong   = nLong + 3;
         |ACABA;
     |FINSI;

     |PARA nCampo = 1;  |SI nCampo [ 145;  |HACIENDO nCampo = nCampo + 1;
           aCadena = #rew00050#nCampo#;  |HAZ GrabaCadena;
     |SIGUE;

     aCadena = aCRLF;  |HAZ GrabaCadena;
|FPRC;

|DEFBUCLE rew00050;
     #rew00050#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, rew00050;
|FIN;

|PROCESO rew00051;
     |SI nPasada = 0;
         aCadena = "002";  |HAZ GrabaCadena;
         nLong   = nLong + 3;
         |ACABA;
     |FINSI;

     |PARA nCampo = 1;  |SI nCampo [ 150;  |HACIENDO nCampo = nCampo + 1;
           aCadena = #rew00051#nCampo#;  |HAZ GrabaCadena;
     |SIGUE;

     aCadena = aCRLF;  |HAZ GrabaCadena;
|FPRC;

|DEFBUCLE rew00051;
     #rew00051#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, rew00051;
|FIN;

|PROCESO CargaHoja3;
     aCadena = "<T";            |HAZ GrabaCadena;
     aCadena = "108";           |HAZ GrabaCadena;
     aCadena = "03";            |HAZ GrabaCadena;
     aCadena = ">";             |HAZ GrabaCadena;
     aCadena = " ";             |HAZ GrabaCadena;
     aCadena = #rem00203#41;    |HAZ GrabaCadena;

     |SI #rem00203#1 = "T";
         aTipoDato = "An"; nLong = 9;  aAlfa = #rem00100#1;
         |HAZ Formateo;  aCadena = aAlfa;  |HAZ GrabaCadena;

         aTipoDato = "An"; nLong = 40;  aAlfa = #rem00100#5;
         |HAZ Formateo;  aCadena = aAlfa;  |HAZ GrabaCadena;

         aCuenta = #rem00100#53;  |QBT aCuenta;
     |SINO;
         aTipoDato = "An"; nLong = 9;  aAlfa = #rem00100#35;
         |HAZ Formateo;  aCadena = aAlfa;  |HAZ GrabaCadena;

         aTipoDato = "An"; nLong = 40;  aAlfa = #rem00100#39;
         |HAZ Formateo;  aCadena = aAlfa;  |HAZ GrabaCadena;

         aCuenta = #rem00100#54;  |QBT aCuenta;
     |FINSI;

     aCadena = "2012";    |HAZ GrabaCadena;
     aCadena = "0A";      |HAZ GrabaCadena;

     aTipoDato = "N"; nLong = 13;  nLongDec = 2;
     aAlfa     = #rem00203#35; |HAZ Formateo;
     aCadena = aAlfa;  |HAZ GrabaCadena;

     aTipoDato = "N"; nLong = 13;  nLongDec = 2;
     aAlfa     = #rem00203#36; |HAZ Formateo;
     aCadena = aAlfa;  |HAZ GrabaCadena;

     aTipoDato = "N"; nLong = 13;  nLongDec = 2;
     aAlfa     = #rem00203#37; |HAZ Formateo;
     aCadena = aAlfa;  |HAZ GrabaCadena;

     aTipoDato = "N"; nLong = 13;  nLongDec = 2;
     aAlfa     = #rem00203#38; |HAZ Formateo;
     aCadena = aAlfa;  |HAZ GrabaCadena;

     aAlfa     = " ";
     |SI #rem00203#39 = "S";
         aAlfa     = "X";
     |FINSI;
     aCadena = aAlfa;  |HAZ GrabaCadena;

     aTipoDato = "An"; nLong = 13;  nLongDec = 0;
     aAlfa     = #rem00203#40; |HAZ Formateo;
     aCadena = aAlfa;  |HAZ GrabaCadena;

     aCuenta = ("00000000000000000000" + aCuenta) % -120;
     aCadena = aCuenta;  |HAZ GrabaCadena;

     aCadena = "</T10803>";  |HAZ GrabaCadena;

     aCadena = aCRLF;                     |HAZ GrabaCadena;
|FPRC;

|PROCESO GeneraTxt;
     aNombre = ("00000" + #rem00203#0) % -105;
     aNombre = "108" + aNombre + #rem00203#1 + ".txt";

     |DBASE aTmp; |QBF aTmp;
     aTmp = aTmp + "tmp/"; |MKDIR aTmp;
     aFicTxt = aTmp + aNombre;
     |XABRE "WB", aFicTxt, nHandle;
     |SI FSalida < 0;
         |MENAV "    Problemas al abrir el fichero txt";
         |ACABA;
     |FINSI;

     aCadena = "<T108020120A0000>";       |HAZ GrabaCadena;
     aCadena = "<AUX>";                   |HAZ GrabaCadena;
     aCadena = (" " * 100);               |HAZ GrabaCadena;
     aCadena = (" " * 100);               |HAZ GrabaCadena;
     aCadena = (" " * 100);               |HAZ GrabaCadena;
     aCadena = "</AUX>";                  |HAZ GrabaCadena;
     aCadena = "<VECTOR>";                |HAZ GrabaCadena;

     nLong   = 0;
     nPasada = 0;
     |BUCLE rew00050;
     |BUCLE rew00051;

     aCadena = "003FIN";                  |HAZ GrabaCadena;
     nLong   = nLong + 7;

     |PARA nCarac = nLong;  |SI nCarac [ 3100;  |HACIENDO nCarac = nCarac + 1;
           aCadena = " ";  |HAZ GrabaCadena;
     |SIGUE;

     aCadena = "</VECTOR>";               |HAZ GrabaCadena;

     aCadena = aCRLF;                     |HAZ GrabaCadena;

     nPasada = 1;
     |BUCLE rew00050;
     |BUCLE rew00051;
     |HAZ CargaHoja3;

     aCadena = "</T108020120A0000>";      |HAZ GrabaCadena;
     aCadena = aCRLF;                     |HAZ GrabaCadena;

     |XCIERRA nHandle;

     aContiene = "";
     |ESPECIFICA "RBASS", aContiene;

     aDirAEAT   = aContiene1; |QBF aDirAEAT;
     aAlfa      = "";
     aAlfa2     = "";

     |PARA nCont = 101; |SI aAlfa2 ! "/"; |Y aAlfa2 ! "\"; |HACIENDO nCont = nCont + 100;
        aAlfa2 = aDirAEAT % nCont;
        |SI aAlfa2 ! "/"; |Y aAlfa2 ! "\";
           aAlfa = aAlfa + aAlfa2;
        |FINSI;
     |SIGUE;

     aDirAEAT     = aAlfa + "\";
     aDirAEAT     = aDirAEAT + "AEAT\";           |RMKDIR aDirAEAT;
     aDirInternet = aDirAEAT;
     aDirInternet = aDirInternet + "INTERNET\";    |RMKDIR aDirInternet;
     aDirInternet = aDirInternet + "108\";         |RMKDIR aDirInternet;
     aDirInternet = aDirInternet + "2012\";        |RMKDIR aDirInternet;

     aOrigen  = aFicTxt;
     aDestino = aDirInternet + aNombre;
     |HAZ CopiaFichero;

     |FBORRA aOrigen;

     aAlfa = "0000El fichero generado est  en " + aDestino + " para su presentaci¢n.";
     |MENAV aAlfa;
|FPRC;

|PROCESO LeeSiEsValido;
     nHay = 0;

     #rem00201#0 = #rem00200#0;
     #rem00201#1 = #rem00200#1;
     #rem00201#2 = #rem00200#2;
     #rem00201#3 = 1;
     |LEE 000#rem00201.];
     |SI FSalida ! 0;                |ACABA;  |FINSI;
     |SI #rem00201#0 ! #rem00200#0;  |ACABA;  |FINSI;
     |SI #rem00201#1 ! #rem00200#1;  |ACABA;  |FINSI;
     |SI #rem00201#2 ! #rem00200#2;  |ACABA;  |FINSI;

     nPorce = 100;

     |SI #rem00203#1 = "T";
         |SI #rem00200#10 = "CONYUGE   ";  |ACABA;  |FINSI;

         |SI #rem00200#10 = "GANANCIAL ";
             nPorce = 50;
         |FINSI;

         |SI #rem00200#10 = "VARIOS    ";
             |SI #rem00200#11 = 0;  |ACABA;  |FINSI;

             nPorce = #rem00200#11;
         |FINSI;
     |FINSI;

     |SI #rem00203#1 = "C";
         |SI #rem00200#10 = "TITULAR   ";  |ACABA;  |FINSI;

         |SI #rem00200#10 = "GANANCIAL ";
             nPorce = 50;
         |FINSI;

         |SI #rem00200#10 = "VARIOS    ";
             |SI #rem00200#12 = 0;  |ACABA;  |FINSI;

             nPorce = #rem00200#12;
         |FINSI;
     |FINSI;

     nHay = 1;
|FPRC;

|PROCESO rem00200Act;
     |HAZ LeeSiEsValido;
     |SI nHay = 0;  |ACABA;  |FINSI;

     nLinea = nLinea + 1;
     |SI nLinea > 5;
         nClave = nClave + 1;
         nLinea = 1;
     |FINSI;

     #rew00050#0 = nClave;
     |LEE 101#rew00050.=;
     |SI FSalida ! 0;
         |DDEFECTO #rew00050;
         #rew00050#0   = nClave;
         #rew00050#1   = "<T";
         #rew00050#2   = "108";
         #rew00050#3   = "01";
         #rew00050#4   = ">";
         #rew00050#5   = "C";
         #rew00050#145 = "</T10801>";

         |GRABA 020#rew00050.b;
     |FINSI;

     |SI aTipo = "A";
         |SI nLinea = 1;  nCampoTmp = 41;  |FINSI;
         |SI nLinea = 2;  nCampoTmp = 47;  |FINSI;
         |SI nLinea = 3;  nCampoTmp = 53;  |FINSI;
         |SI nLinea = 4;  nCampoTmp = 59;  |FINSI;
         |SI nLinea = 5;  nCampoTmp = 65;  |FINSI;
     |FINSI;

     |SI aTipo = "B";
         |SI nLinea = 1;  nCampoTmp = 71;  |FINSI;
         |SI nLinea = 2;  nCampoTmp = 77;  |FINSI;
         |SI nLinea = 3;  nCampoTmp = 83;  |FINSI;
         |SI nLinea = 4;  nCampoTmp = 89;  |FINSI;
         |SI nLinea = 5;  nCampoTmp = 95;  |FINSI;
     |FINSI;

     |SI aTipo = "C";
         |SI nLinea = 1;  nCampoTmp = 101; |FINSI;
         |SI nLinea = 2;  nCampoTmp = 107; |FINSI;
         |SI nLinea = 3;  nCampoTmp = 113; |FINSI;
         |SI nLinea = 4;  nCampoTmp = 119; |FINSI;
         |SI nLinea = 5;  nCampoTmp = 125; |FINSI;
     |FINSI;

     aTipoDato = "An"; nLong = 5;   aAlfa = #rem00200#4;  |HAZ Epigrafe;
     #rew00050#nCampoTmp# = aAlfa;

     nCampoTmp = nCampoTmp + 1;
     aTipoDato = "An"; nLong = 40;  aAlfa = #rem00200#5;  |HAZ Formateo;
     #rew00050#nCampoTmp# = aAlfa;

     |SI #rem00200#6 = "ED ";  aAlfa = "1";   |FINSI;
     |SI #rem00200#6 = "EDS";  aAlfa = "2";   |FINSI;
     |SI #rem00200#6 = "EO ";  aAlfa = "3";   |FINSI;

     nCampoTmp = nCampoTmp + 1;
     #rew00050#nCampoTmp# = aAlfa;

     nCampoTmp = nCampoTmp + 1;
     aTipoDato = "An"; nLong = 9;   aAlfa = #rem00200#7;  |HAZ Formateo;
     #rew00050#nCampoTmp# = aAlfa;

     nCampoTmp = nCampoTmp + 1;
     aTipoDato = "An"; nLong = 40;  aAlfa = #rem00200#8;  |HAZ Formateo;
     #rew00050#nCampoTmp# = aAlfa;

     nCampoTmp = nCampoTmp + 1;
     aTipoDato = "N"; nLong = 2;  nLongDec = 2; aAlfa = #rem00200#9;  |HAZ Formateo;
     #rew00050#nCampoTmp# = aAlfa;

     |GRABA 020#rew00050.a;
     |LIBERA #rew00050;
|FPRC;

|DEFBUCLE rem00200Act;
     #rem00200#2;
     ;
     #rem00203#0, aTipo, "    ";
     #rem00203#0, aTipo, "zzzz";
     ;
     NULL, rem00200Act;
|FIN;

|PROCESO rem00200Com;
     |SI #rem00200#9 = 0;  |ACABA;  |FINSI;

     |HAZ LeeSiEsValido;
     |SI nHay = 0;  |ACABA;  |FINSI;

     nLinea = nLinea + 1;
     |SI nLinea > 4;
         nClave = nClave + 1;
         nLinea = 1;
     |FINSI;

     #rew00050#0 = nClave;
     |LEE 101#rew00050.=;
     |SI FSalida ! 0;
         |DDEFECTO #rew00050;
         #rew00050#0   = nClave;
         #rew00050#5   = "C";
         #rew00050#1   = "<T";
         #rew00050#2   = "108";
         #rew00050#3   = "01";
         #rew00050#4   = ">";
         #rew00050#145 = "</T10801>";

         |GRABA 020#rew00050.b;
     |FINSI;

     |SI nLinea = 1;  nCampoTmp = 131;  |FINSI;
     |SI nLinea = 2;  nCampoTmp = 134;  |FINSI;
     |SI nLinea = 3;  nCampoTmp = 137;  |FINSI;
     |SI nLinea = 4;  nCampoTmp = 140;  |FINSI;

     aTipoDato = "An"; nLong = 40;  aAlfa = #rem00200#8;  |HAZ Formateo;
     #rew00050#nCampoTmp# = aAlfa;

     nCampoTmp = nCampoTmp + 1;
     aTipoDato = "An"; nLong = 9;   aAlfa = #rem00200#7;  |HAZ Formateo;
     #rew00050#nCampoTmp# = aAlfa;

     nCampoTmp = nCampoTmp + 1;
     aTipoDato = "N"; nLong = 2;  nLongDec = 2; aAlfa = #rem00200#9;  |HAZ Formateo;
     #rew00050#nCampoTmp# = aAlfa;

     |GRABA 020#rew00050.a;
     |LIBERA #rew00050;
|FPRC;

|DEFBUCLE rem00200Com;
     #rem00200#2;
     ;
     #rem00203#0, " ", "    ";
     #rem00203#0, "z", "zzzz";
     ;
     NULL, rem00200Com;
|FIN;

|PROCESO CargaHoja1;
     aFichero = "rew00050" + Usuario;  |NOME_DAT #rew00050#aFichero#;
     |ABRE #rew00050;  |CIERRA #rew00050;  |DELINDEX #rew00050;

     |ABRE #rew00050;
     |DDEFECTO #rew00050;
     #rew00050#0   = 0;
     #rew00050#1   = "<T";
     #rew00050#2   = "108";
     #rew00050#3   = "01";
     #rew00050#4   = ">";
     #rew00050#5   = "";
     #rew00050#145 = "</T10801>";

     |SI #rem00203#1 = "T";
         aTipoDato = "An"; nLong = 9;  aAlfa = #rem00100#1;
         |HAZ Formateo;  #rew00050#6  = aAlfa;

         aTipoDato = "An"; nLong = 40;  aAlfa = #rem00100#5;
         |HAZ Formateo;  #rew00050#7  = aAlfa;
     |SINO;
         aTipoDato = "An"; nLong = 9;  aAlfa = #rem00100#35;
         |HAZ Formateo;  #rew00050#6  = aAlfa;

         aTipoDato = "An"; nLong = 40;  aAlfa = #rem00100#39;
         |HAZ Formateo;  #rew00050#7  = aAlfa;
     |FINSI;

     nCampoTmp = 7;
     |PARA nCampo = 2;  |SI nCampo [ 32;  |HACIENDO nCampo = nCampo + 3;
           nCampoTmp = nCampoTmp + 1;
           aTipoDato = "N"; nLong = 13;  nLongDec = 2;
           aAlfa     = #rem00203#nCampo#; |HAZ Formateo;
           #rew00050#nCampoTmp# = aAlfa;
     |SIGUE;

     |PARA nCampo = 3;  |SI nCampo [ 33;  |HACIENDO nCampo = nCampo + 3;
           nCampoTmp = nCampoTmp + 1;
           aTipoDato = "N"; nLong = 13;  nLongDec = 2;
           aAlfa     = #rem00203#nCampo#; |HAZ Formateo;
           #rew00050#nCampoTmp# = aAlfa;
     |SIGUE;

     |PARA nCampo = 4;  |SI nCampo [ 34;  |HACIENDO nCampo = nCampo + 3;
           nCampoTmp = nCampoTmp + 1;
           aTipoDato = "N"; nLong = 13;  nLongDec = 2;
           aAlfa     = #rem00203#nCampo#; |HAZ Formateo;
           #rew00050#nCampoTmp# = aAlfa;
     |SIGUE;

     |GRABA 020#rew00050.n;
     |LIBERA #rew00050;

     nClave = 0;  nLinea = 0;  aTipo = "A";  |BUCLE rem00200Act;
     nClave = 0;  nLinea = 0;  aTipo = "B";  |BUCLE rem00200Act;
     nClave = 0;  nLinea = 0;  aTipo = "C";  |BUCLE rem00200Act;
     nClave = 0;  nLinea = 0;                |BUCLE rem00200Com;

     |CIERRA #rew00050;
|FPRC;

|PROCESO rem00202Activos;
     nCampoTmp = 15 + ((#rem00202#4 - 1) * 4);

     aTipoDato = "N"; nLong = 13;  nLongDec = 2;
     nCanti    = (#rem00202#6 % nPorce) ! 2;
     aAlfa     = nCanti;    |HAZ Formateo;
     #rew00051#nCampoTmp# = aAlfa;

     nCampoTmp = nCampoTmp + 1;
     aTipoDato = "N"; nLong = 13;  nLongDec = 2;
     nCanti    = (#rem00202#7 % nPorce) ! 2;
     aAlfa     = nCanti;    |HAZ Formateo;
     #rew00051#nCampoTmp# = aAlfa;

     nCampoTmp = nCampoTmp + 1;
     aTipoDato = "N"; nLong = 15;  nLongDec = 2;
     nCanti    = (#rem00202#9 % nPorce) ! 2;
     aAlfa     = nCanti;    |HAZ Formateo;
     #rew00051#nCampoTmp# = aAlfa;

     nCampoTmp = nCampoTmp + 1;
     aTipoDato = "N"; nLong = 15;  nLongDec = 2;
     nCanti    = (#rem00202#10 % nPorce) ! 2;
     aAlfa     = nCanti;    |HAZ Formateo;
     #rew00051#nCampoTmp# = aAlfa;
|FPRC;

|DEFBUCLE rem00202Activos;
     #rem00202#1;
     ;
     #rem00201#0, #rem00201#1, #rem00201#2, #rem00201#3, 00;
     #rem00201#0, #rem00201#1, #rem00201#2, #rem00201#3, 99;
     ;
     NULL, rem00202Activos;
|FIN;

|PROCESO rem00201Activos;
     #rew00051#0 = nClave;
     |LEE 101#rew00051.=;
     |SI FSalida ! 0;
         |DDEFECTO #rew00051;
         #rew00051#0   = nClave;
         #rew00051#1   = "<T";
         #rew00051#2   = "108";
         #rew00051#3   = "02";
         #rew00051#4   = ">";
         #rew00051#5   = "";

         |SI nActivo > 0;
             #rew00051#5   = "C";
         |FINSI;

         #rew00051#150 = "</T10802>";

         |GRABA 020#rew00051.b;
     |FINSI;

     |SI #rem00203#1 = "T";
         aTipoDato = "An"; nLong = 9;  aAlfa = #rem00100#1;
         |HAZ Formateo;  #rew00051#6  = aAlfa;

         aTipoDato = "An"; nLong = 40;  aAlfa = #rem00100#5;
         |HAZ Formateo;  #rew00051#7  = aAlfa;
     |SINO;
         aTipoDato = "An"; nLong = 9;  aAlfa = #rem00100#35;
         |HAZ Formateo;  #rew00051#6  = aAlfa;

         aTipoDato = "An"; nLong = 40;  aAlfa = #rem00100#39;
         |HAZ Formateo;  #rew00051#7  = aAlfa;
     |FINSI;

     aTipoDato = "An"; nLong = 9;   aAlfa = #rem00200#7;  |HAZ Formateo;
     #rew00051#8  = aAlfa;

     aTipoDato = "An"; nLong = 40;  aAlfa = #rem00200#8;  |HAZ Formateo;
     #rew00051#9  = aAlfa;

     aTipoDato = "An"; nLong = 1;   aAlfa = #rem00200#3;  |HAZ Formateo;
     #rew00051#10 = aAlfa;

     aTipoDato = "An"; nLong = 5;   aAlfa = #rem00200#4;  |HAZ Epigrafe;
     #rew00051#11 = aAlfa;

     |SI #rem00200#6 = "ED ";  aAlfa = "1";   |FINSI;
     |SI #rem00200#6 = "EDS";  aAlfa = "2";   |FINSI;
     |SI #rem00200#6 = "EO ";  aAlfa = "3";   |FINSI;

     #rew00051#12 = aAlfa;

     aTipoDato = "An"; nLong = 40;  aAlfa = #rem00200#5;  |HAZ Formateo;
     #rew00051#13 = aAlfa;

     aTipoDato = "An"; nLong = 2;  aAlfa = #rem00201#5;
     |HAZ Formateo;  #rew00051#14 = aAlfa;

     |BUCLE rem00202Activos;

     aTipoDato = "N"; nLong = 13;  nLongDec = 2;
     aAlfa     = #rem00201#13;   |HAZ Formateo;
     #rew00051#139 = aAlfa;

     aTipoDato = "N"; nLong = 13;  nLongDec = 2;
     aAlfa     = #rem00201#14;   |HAZ Formateo;
     #rew00051#140 = aAlfa;

     aTipoDato = "N"; nLong = 13;  nLongDec = 2;
     aAlfa     = #rem00201#15;   |HAZ Formateo;
     #rew00051#141 = aAlfa;

     aTipoDato = "N"; nLong = 13;  nLongDec = 2;
     aAlfa     = #rem00201#16;   |HAZ Formateo;
     #rew00051#142 = aAlfa;

     aTipoDato = "N"; nLong = 2;  nLongDec = 2;
     aAlfa     = #rem00201#17;   |HAZ Formateo;
     #rew00051#143 = aAlfa;

     aTipoDato = "N"; nLong = 13;  nLongDec = 2;
     aAlfa     = #rem00201#18;   |HAZ Formateo;
     #rew00051#144 = aAlfa;

     #rew00051#145 = #rem00201#19;

     aTipoDato = "An"; nLong = 50;  aAlfa = #rem00201#9;
     |HAZ Formateo;  #rew00051#146 = aAlfa;

     aTipoDato = "An"; nLong = 20;  aAlfa = #rem00201#10;
     |HAZ Formateo;  #rew00051#147 = aAlfa;

     aTipoDato = "N"; nLong = 13;  nLongDec = 2;  aAlfa = #rem00201#11;
     |HAZ Formateo;  #rew00051#148 = aAlfa;

     aTipoDato = "N"; nLong = 4;   nLongDec = 0;  aAlfa = #rem00201#12;
     |HAZ Formateo;  #rew00051#149 = aAlfa;

     |GRABA 020#rew00051.a;
     |LIBERA #rew00051;

     nClave  = nClave  + 1;
     nActivo = nActivo + 1;
|FPRC;

|DEFBUCLE rem00201Activos;
     #rem00201#1;
     ;
     #rem00200#0, #rem00200#1, #rem00200#2, 0001;
     #rem00200#0, #rem00200#1, #rem00200#2, 9999;
     ;
     NULL, rem00201Activos;
|FIN;

|PROCESO rem00200Activos;
     |HAZ LeeSiEsValido;
     |SI nHay = 0;  |ACABA;  |FINSI;

     nActivo = 0;
     |BUCLE rem00201Activos;
|FPRC;

|DEFBUCLE rem00200Activos;
     #rem00200#2;
     ;
     #rem00203#0, " ", "    ";
     #rem00203#0, "z", "zzzz";
     ;
     NULL, rem00200Activos;
|FIN;

|PROCESO CargaHoja2;
     aFichero = "rew00051" + Usuario;  |NOME_DAT #rew00051#aFichero#;
     |ABRE #rew00051;  |CIERRA #rew00051;  |DELINDEX #rew00051;

     nClave  = 0;
     |ABRE #rew00051;
     |BUCLE rem00200Activos;
     |CIERRA #rew00051;
|FPRC;

|PROCESO Emision;
     |SI #rem00203#42 = "S";
         |CONFI "0000NEl modelo ya est  emitido, quiere volver a emitirlo.";
         |SI FSalida ! 0;  |ACABA;  |FINSI;

         #rem00203#42 = "N";
     |FINSI;

     |ABRE #rem00100;
     #rem00100#0  = #rem00203#0;
     |LEE 000#rem00100.=;
     |CIERRA #rem00100;

     aCRLF = &13 + &10;
     aCRLF = "";

     |ABRE #rem00201;
     |HAZ CargaHoja1;
     |HAZ CargaHoja2;
     |CIERRA #rem00201;

     |HAZ GeneraTxt;

     #rem00203#42 = "S";

     |DELINDEX #rew00050;
     |DELINDEX #rew00051;

     |PTEC 806;
|FPRC;

|PROCESO Tipo12;  |TIPO 12;
|FPRC;

|PROCESO Tipo0C37Fm203;  |TIPO 0;
     |SI #rem00203#37 > #rem00203#36;
         #rem00203#37 = #rem00203#36;
         |PINTA #rem00203#37;
     |FINSI;
|FPRC;

|PROCESO Tipo0C39Fm203;  |TIPO 0;
     |C_M #rem00203#40, 1, "S";
     |C_M #rem00203#37, 1, "S";

     |SI #rem00203#39 = "N";
         |C_M #rem00203#40, 1, "N";  #rem00203#40 = "";
         |C_M #rem00203#37, 1, "N";  #rem00203#37 = 0;
     |FINSI;

     |PINTA #rem00203#40;
     |PINTA #rem00203#37;
|FPRC;

|PROCESO Total;
     #rem00203#5  = #rem00203#8  + #rem00203#11;
     #rem00203#2  = #rem00203#5  + #rem00203#14 + #rem00203#17;
     #rem00203#20 = #rem00203#23 + #rem00203#26;
     #rem00203#32 = #rem00203#2  + #rem00203#20 + #rem00203#29;

     #rem00203#6  = #rem00203#9  + #rem00203#12;
     #rem00203#3  = #rem00203#6  + #rem00203#15 + #rem00203#18;
     #rem00203#21 = #rem00203#24 + #rem00203#27;
     #rem00203#33 = #rem00203#3  + #rem00203#21 + #rem00203#30;

     #rem00203#7  = #rem00203#10 + #rem00203#13;
     #rem00203#4  = #rem00203#7  + #rem00203#16 + #rem00203#19;
     #rem00203#22 = #rem00203#25 + #rem00203#28;
     #rem00203#34 = #rem00203#4  + #rem00203#22 + #rem00203#31;

     #rem00203#36 = #rem00203#35 % 5;
     #rem00203#38 = #rem00203#36 - #rem00203#37;

     |PINTA #rem00203#2;
     |PINTA #rem00203#5;
     |PINTA #rem00203#20;
     |PINTA #rem00203#32;

     |PINTA #rem00203#3;
     |PINTA #rem00203#6;
     |PINTA #rem00203#21;
     |PINTA #rem00203#33;

     |PINTA #rem00203#4;
     |PINTA #rem00203#7;
     |PINTA #rem00203#22;
     |PINTA #rem00203#34;

     |PINTA #rem00203#35;
     |PINTA #rem00203#36;
     |PINTA #rem00203#38;
|FPRC;

|PROCESO rem00201;
     #rem00200#0 = #rem00201#0;
     #rem00200#1 = #rem00201#1;
     #rem00200#2 = #rem00201#2;
     |LEE 000#rem00200.=;
     |SI FSalida ! 0;  |DDEFECTO #rem00200;  |FINSI;

     nPorce = 100;
     |SI eaLetra = "TI";
         |SI #rem00200#10 = "CONYUGE   ";  |ACABA;  |FINSI;

         |SI #rem00200#10 = "GANANCIAL ";
             nPorce = 50;
         |FINSI;

         |SI #rem00200#10 = "VARIOS    ";
             |SI #rem00200#11 = 0;  |ACABA;  |FINSI;

             nPorce = #rem00200#11;
         |FINSI;
     |FINSI;

     |SI eaLetra = "CO";
         |SI #rem00200#10 = "TITULAR   ";  |ACABA;  |FINSI;

         |SI #rem00200#10 = "GANANCIAL ";
             nPorce = 50;
         |FINSI;

         |SI #rem00200#10 = "VARIOS    ";
             |SI #rem00200#12 = 0;  |ACABA;  |FINSI;

             nPorce = #rem00200#12;
         |FINSI;
     |FINSI;

     #rem00201#18 =  #rem00201#18  % nPorce;

     |SI nValores = 0;
         #rem00203#35 = #rem00203#35 + #rem00201#18;
         |ACABA;
     |FINSI;

     #rem00202#0 = #rem00201#0;
     #rem00202#1 = #rem00201#1;
     #rem00202#2 = #rem00201#2;
     #rem00202#3 = #rem00201#3;
     #rem00202#4 = 31;
     |LEE 000#rem00202.=;
     |SI FSalida ! 0;  |DDEFECTO #rem00202;  |FINSI;

     #rem00202#6  =  #rem00202#6  % nPorce;
     #rem00202#7  =  #rem00202#7  % nPorce;
     #rem00202#9  =  #rem00202#9  % nPorce;
     #rem00202#10 =  #rem00202#10 % nPorce;

     |SI #rem00201#5 = "10";  |O #rem00201#5 = "11";
         #rem00203#8  = #rem00203#8  + (#rem00202#6  - #rem00202#7);
         #rem00203#9  = #rem00203#9  + (#rem00202#9  - #rem00202#10);
         #rem00203#10 = #rem00203#10 + #rem00202#7;
     |FINSI;

     |SI #rem00201#5 = "20";  |O #rem00201#5 = "21";
         #rem00203#11 = #rem00203#11 + (#rem00202#6  - #rem00202#7);
         #rem00203#12 = #rem00203#12 + (#rem00202#9  - #rem00202#10);
         #rem00203#13 = #rem00203#13 + #rem00202#7;
     |FINSI;

     |SI #rem00201#5 = "30";  |O #rem00201#5 = "31";
         #rem00203#14 = #rem00203#14 + (#rem00202#6  - #rem00202#7);
         #rem00203#15 = #rem00203#15 + (#rem00202#9  - #rem00202#10);
         #rem00203#16 = #rem00203#16 + #rem00202#7;
     |FINSI;

     |SI #rem00201#5 = "40";  |O #rem00201#5 = "41";
         #rem00203#23 = #rem00203#23 + (#rem00202#6  - #rem00202#7);
         #rem00203#24 = #rem00203#24 + (#rem00202#9  - #rem00202#10);
         #rem00203#25 = #rem00203#25 + #rem00202#7;
     |FINSI;

     |SI #rem00201#5 = "50";  |O #rem00201#5 = "51";
         #rem00203#26 = #rem00203#26 + (#rem00202#6  - #rem00202#7);
         #rem00203#27 = #rem00203#27 + (#rem00202#9  - #rem00202#10);
         #rem00203#28 = #rem00203#28 + #rem00202#7;
     |FINSI;

     |SI #rem00201#5 = "60";
         #rem00203#29 = #rem00203#29 + (#rem00202#6  - #rem00202#7);
         #rem00203#30 = #rem00203#30 + (#rem00202#9  - #rem00202#10);
         #rem00203#31 = #rem00203#31 + #rem00202#7;
     |FINSI;

     #rem00203#35 = #rem00203#35 + #rem00201#18;
|FPRC;

|DEFBUCLE rem00201;
     #rem00201#1;
     ;
     enEmpresa, "  ", 00, 0001;
     enEmpresa, "zz", 99, 9999;
     ;
     NULL, rem00201;
|FIN;

|PROCESO RecogeValores;
     |SI nValores = 1;
         |PARA nCampo = 2;  |SI nCampo [ 34;  |HACIENDO nCampo = nCampo + 1;
               #rem00203#nCampo# = 0;
         |SIGUE;
     |FINSI;

     #rem00203#35 = 0;

     |ABRE #rem00200;
     |ABRE #rem00202;
     |BUCLE rem00201;
     |CIERRA #rem00200;
     |CIERRA #rem00202;

     |HAZ Total;
     |PINDA #0#rem00203;

     |POSICIONATE #rem00203#8;
     |PTEC 809;
|FPRC;

|PROCESO rem00200;
     #rem00203#1 = eaLetra % 101;
     |HAZ LeeSiEsValido;

     |SI nHay = 1;  |ERROR10; |FINSI;
|FPRC;

|DEFBUCLE rem00200;
     #rem00200#1;
     ;
     enEmpresa, "  ", 00;
     enEmpresa, "zz", 99;
     ;
     NULL, rem00200;
|FIN;

|PROGRAMA;
     nHay = 0;
     |ABRE #rem00201;
     |BUCLE rem00200;
     |CIERRA #rem00201;
     |SI nHay = 0;
         |ABRE #rem00203;
         #rem00203#0 = enEmpresa;
         #rem00203#1 = eaLetra % 101;
         |LEE 101#rem00203.=;
         |SI FSalida = 0;
             |BORRA 020#rem00203.a;
         |FINSI;
         |LIBERA #rem00203;
         |CIERRA #rem00203;

         |MENAV "0000No hay revalorizaciones en el contribuyente";
         |ACABA;
     |FINSI;

     |ABRET #rem00203;

     |VENTANA 0501, 3599, -1, 16, "Pantalla resumen";
     nVentana = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVentana;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |PTEC 802;  |PAUSA;

     #rem00203#0 = enEmpresa;
     #rem00203#1 = eaLetra % 101;
     |LEE 101#rem00203.=;
     |SI FSalida ! 0;
         |DDEFECTO #rem00203;

         #rem00203#0 = enEmpresa;
         #rem00203#1 = eaLetra % 101;
         |GRABA 020#rem00203.b;
     |FINSI;

     nValores = 0;
     |SI #rem00203#32 = 0;  |Y  #rem00203#33 = 0;  |Y  #rem00203#34 = 0;
         nValores = 1;
     |FINSI;

     |PINPA #0#rem00203;    nPanta = FSalida;

     |HAZ RecogeValores;
     nValores = 1;

     |CONTROL_SIMPLE nPanta, "BOTON,{{197}}Recoge valores de nuevo", 1980, 1999, RecogeValores;
     |CONTROL_SIMPLE nPanta, "BOTON,{{197}}Emisi¢n internet", 3180, 3499, Emision;

     |SI #rem00203#42 = "N";
         |CONTROL_SIMPLE nPanta, "LABEL,{{16}}Modelo no emitido", 3080, 3099, NULL;
     |SINO;
         |CONTROL_SIMPLE nPanta, "LABEL,{{16}}Modelo emitido", 3080, 3099, NULL;
     |FINSI;

     |ENDATOS #1#rem00203;
     |SI FSalida = 0;
         |GRABA 020#rem00203.a;
     |FINSI;

     |LIBERA #rem00203;
     |CIERRAT #rem00203;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVentana;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA nVentana;
|FPRO;
