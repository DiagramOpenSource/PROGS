|FICHEROS;
    cajainli #0;
    cajainca #1;
    cafantas #2;
    caconcep #3;
    cacuenta #4;
||    camancob #21;
||    camanpag #22;
|FIN;

|VARIABLES;
    &entrada = 1;
    &esalta = 0;
    &adocum = 0;
    &adiari = 0;
    &afecha = @;
    &dpta   = "";
    &cpartida = "";
    &dpartida = 0;
    &avisos = "";
    doce = "            ";
    lcuenta = "";
    ldigito = 0;
    signo = "";
    xx = 0;

     men2  = "     Longitud de Cuenta Fuera de Nivel !";
     men3  = "      Esta Cuenta NO tiene Pase directo";
     long = "  ";
     sw = 0;
     ini = 0;
     fecalf = "";
     mes = "";
     mesfec = 0;
     mesac = 0;
     a = 0;
     b = 0;
     c = 0;

     &cuant = "";
     &diant = 0;
     &coant = 0;
     &deant = "";
     descr = "";
     &dhant = "";
     &dpant = "";
     &imant = 0;

     &dig1 = 0;
     &dig3 = 0;
     &dig4 = 0;
     &dig5 = 0;
     &dig6 = 0;
     &dig7 = 0;
     &dig8 = 0;
     &dig9 = 0;
     &fec1 = @;
     &fec2 = @;

     alfa1 = "";
     alfa2 = "";
     alfa3 = "";
     alfa4 = "";
     nume1 = 0;
     nume2 = 0;
     nume3 = 0;
     nume4 = 0;
     salidas = 0;

    &ext1 = "";
    &ext2 = "";
    &ext3 = "";
    &ext4 = "";
    &ext5 = "";
    &inkey = 0;
    &sit1 = 0;
    &sit2 = @;
    &sit3 = 0;
    &sit4 = 0;
    &sit5 = 0;
    &sit6 = 0;

    programa = "";
    importe = 0;

    cta_ult = ".";
|FIN;

|INCLUYE puntos_i;

***************************************************************************
                         LLAMADAS DESDE CAMPOS
***************************************************************************

|| ... Campo Cuenta
|PROCESO cta_ant; |TIPO 7;
/*
|SI cta_ult ! ".";
        #0#4 = cta_ult;
    |PINTA #0#4;
    |PTEC 816;
|FINSI;
*/
|FPRC;

|PROCESO cuenta; |TIPO 6;                         || desde campo 'cuenta'
  salidas = FSalida;
  alfa1 = #0#4;     |QBF alfa1;
  |SI alfa1 = "";|Y cta_ult ! ".";|Y salidas = 0;
      #0#4 = cta_ult;
      |PINTA #0#4;
  |FINSI;
  cta_ult = #0#4;
  |SI salidas = 15;
      #0#4 = cuant;
      #0#5 = diant;
      |PINTA #0#4;
      salidas = 0;
  |FINSI;
  |SI salidas ] 10; |Y salidas [ 17; |ACABA; |FINSI;
  p_alfa1 = #0Campo;
  |HAZ punto;
  |SI #0Campo ! doce;
       |SI p_error ! 0;  |ERROR;  |ACABA;  |FINSI;
       alfa1 = #0#4;
       |QBF alfa1;
       long = alfa1 % 0;
       sw = 0;
       |SI long = dig4; sw = 1; #0#5 = 1; |FINSI;
       |SI long = dig5; sw = 1; #0#5 = 2; |FINSI;
       |SI long = dig6; sw = 1; #0#5 = 3; |FINSI;
       |SI long = dig7; sw = 1; #0#5 = 4; |FINSI;
       |SI long = dig8; sw = 1; #0#5 = 5; |FINSI;
       |SI long = dig9; sw = 1; #0#5 = 6; |FINSI;
       |SI sw = 0;
           |MENAV men2;
           |ERROR;
       |FINSI;
    |SINO;
       |MENAV "    ATENCION!!! Entrada erronea ...";
       |ERROR;
    |FINSI;
|FPRC;

|PROCESO cuefin; |TIPO 0;                    || desde campo 'cuenta'

 |SI salidas ] 9; |Y salidas [ 17;
     inkey = salidas;
     |HAZ funcion2;
 |SINO;
     |ABRE #4;
     #4#0 = #0#4;
     #4#1 = #0#5;
     |LEE 040#4=;
     |SI FSalida ! 0;
         |CIERRA #4;
         |ERROR;
         |ACABA;
     |FINSI;
     |SI #4#3 = "N";
        |MENAV men3;
        |CIERRA #4;
        |ERROR;
        |ACABA;
     |FINSI;
     |CIERRA #4;
     |ATRI I;
     |PINTA 0739, #4#51;
     |PINTA 0753, #4#52;
     |PINTA 0767, #4#53;
     |PINTA 0639, #4#2;
     |ATRI 0;
 |FINSI;
|FPRC;

|| ==========================================================================
|PROCESO c_concepto; |TIPO 7;                || desce campo 'concepto'
    #0Campo = coant;
|PINTA #0Campo;
|FPRC;

|PROCESO concepto; |TIPO 0;                  || desde campo 'concepto'
 |ABRE #3;
 #3#0 = #0#6;
 |LEE 000#3=;
 |SI #0#6 = coant;
     ||#0#7 = deant;
     #0#7 = #3#1;
 |SINO;
     #0#7 = #3#1;
 |FINSI;
 |PINTA #0#7;
 |CIERRA #3;
 coant = #0#6;
|FPRC;

|| ==========================================================================
|PROCESO final; |TIPO 7;                     || desde campo 'descripcion'
/*
|SI deant ! "";
        #0#7 = deant;
    |PINTA #0#7;
    |PTEC 816;
|FINSI;
*/
|PTEC 816;
|FPRC;

|PROCESO descripc; |TIPO 0;                  || desde campo 'descripcion'
|SI FSalida = 0;
        ||deant = #0#7;
        descr = #0#7;
|FINSI;
|SI FSalida = 15;
    |SI deant ! "";
            ||#0#7 = deant;
            #0#7 = descr;
        |PINTA #0#7;
        |PTEC 816;
    |FINSI;
    |ERROR;
|FINSI;
|FPRC;

|| ==========================================================================
|PROCESO c_departa; |TIPO 7;
 |SI dpta = "N";
    |CAMPO_MODIFICA #0#12, 1, "N";
 |SINO;
    |CAMPO_MODIFICA #0#12, 1, "S";
    #0#12= dpant; |PINTA #0#12;
 |FINSI;
|FPRC;

|| ==========================================================================
|PROCESO c_signo; |TIPO 7;   || desde campo 'cobro/pago'
 #0Campo = dhant;
 |PINTA #0Campo;
|FPRC;

|PROCESO mayus; |TIPO 0;
 #0Campo = #0Campo > #0Campo;
 |PINTA #0Campo;
|FPRC;

|| ==========================================================================
|PROCESO c_importe; |TIPO 7;   || desde campo 'importe'
 #0Campo = imant;
 |PINTA #0Campo;
|FPRC;

|PROCESO nocero; |TIPO 0;                    || desde campo 'importe'
 inkey = FSalida;
 |SI #0Campo = 0;
     |ERROR;
     |ACABA;
 |FINSI;
 |SI inkey = 14;
     inkey = 0;
     |PTEC 802;
     |PTEC 802;
     |PTEC 802;
     |PTEC 802;
     |PTEC 806;
 |FINSI;
|FPRC;

|| ==========================================================================
|| ............. DESDE LINEA APUNTE
|PROCESO saalta; |TIPO 0;               || desde campo 'linea'
|SI FEntrada = 101; |Y #0#3 ! 1;
    |SI coant ! 0;
            #0#6 = coant;
||            #0#7 = deant;
            #0#8 = dhant;
            #0#12 = dpant;
    |FINSI;
    |PINTA #0#6;
||    |PINTA #0#7;
    |PINTA #0#12;
|FINSI;
|FPRC;

|PROCESO refresca; |TIPO 2;
 |HAZ a_caja;
 |SI #0#8 = "P"; signo = "D"; |FINSI; || prov. o cli: si pago al debe
 |SI #0#8 = "C"; signo = "H"; |FINSI; ||              si cobro al Haber
 lcuenta = #0#4;
 ldigito = #0#5;
 |HAZ a_cuenta;
 |ATRI I;
 |PINTA 0739, #4#51;
 |PINTA 0753, #4#52;
 |PINTA 0767, #4#53;
 |ATRI 0;
 |SI #0#8 = "P"; signo = "H"; |FINSI;  || caja, si pago al Haber
 |SI #0#8 = "C"; signo = "D"; |FINSI;  ||       si cobro al Debe
 lcuenta = cpartida;
 ldigito = dpartida;
 |HAZ a_cuenta;
|FPRC;

|PROCESO diez; |TIPO 7;
     xx = (FEntrada / 100) ! 0;
 |SI xx = 1;|Y FSalida ! 1;|Y FSalida ! 7;|Y #0#3 ! 1;
     #0#3 = #0#3 + 9;
     |PINTA #0#3;
 |FINSI;
|FPRC;

|PROCESO entmod; |TIPO 1;
  coant = #0#6;
  ||deant = #0#7;
  descr = #0#7;
  dpant = #0#12;
  dhant = #0#8;
  imant = #0#9;
  cta_ult = #0#4;
|FPRC;

|PROCESO guarda; |TIPO 3;               || al grabar la ficha
 xx = (FEntrada / 100) ! 0;
 |SI #0#4 = doce;
     avisos = "ATENCION!!! Apunte sin cuentas ...";
     |HAZ aviso5;
     |PAUSA;
     |HAZ aviso9;
     |ERROR;
     |ACABA;
 |FINSI;
 |SI #0#9 = 0;                       || importe distinto de cero
     avisos = "ATENCION!!! Importe cero ...";
     |HAZ aviso5;
     |PAUSA;
     |HAZ aviso9;
     |ERROR;
     |ACABA;
 |FINSI;
     cuant = #0#4;
     diant = #0#5;
     coant = #0#6;
     ||deant = #0#7;
     descr = #0#7;
     dpant = #0#12;
     dhant = #0#8;
     imant = #0#9;
|FPRC;

|PROCESO pinta_cta; |TIPO 13;      || desde 'refresca', 'cuefin_t', CONSULTA
|SI #0#4 ! doce;
    |ABRET #4;
    #4#0 = #0#4;
    #4#1 = #0#5;
    |LEE 001#4=;
    |SI FSalida ! 0;  |DDEFECTO #4;  |FINSI;
    |CIERRAT #4;
|SINO;
    |DDEFECTO #4;
|FINSI;
 |ATRI I;
 |PINTA 0739, #4#51;
 |PINTA 0753, #4#52;
 |PINTA 0767, #4#53;
 |PINTA 0639, #4#2;
 |ATRI 0;
|FPRC;

|PROCESO funcion1; |TIPO 11;                  || desde campo 'linea'
  inkey = FSalida;
  xx = (FEntrada / 100) ! 0;
  |SI xx ! 1;
      |HAZ funcion2;
  |SINO;
      |SI Campo = 3;  |Y inkey = 14;  |Y  #0#3 ] 1;
          |PTEC 802;
          |PTEC 802;
          |PTEC 802;
          |PTEC 806;
          |PTEC 807;
      |SINO;
              |SI inkey ] 9;|Y inkey [ 20;
                  |AVISO;
                  |ERROR;
              |FINSI;
     |FINSI;
  |FINSI;
|FPRC;

|PROCESO funcion2;                           || desde calculo 'funcion1'
  |SI inkey ] 14;
      |AVISO;
      |ERROR;
      |ACABA;
  |FINSI;
  |SI inkey = 9;            || extractos
      ext1 = #0#4;          || cuenta
      ext2 = #0#5;          || digito
      programa = "caextrap.run";
      |HAZ externos;
      |ERROR;
      |ACABA;
  |FINSI;
  |SI inkey = 10;             || diario de trabajo
      ||ext1 = #1#0;          || desde diario
      ||ext2 = #1#0;          || hasta diario
      programa = "caditrab.run";
      |HAZ externos;
      |ERROR;
      |ACABA;
  |FINSI;
/*  |SI inkey = 11;           || anotaciones extracontables
      ext1 = #0#0;          || diario
      ext2 = #0#1;          || fecha
      ext3 = #0#2;          || documento
      programa = "caextra0.run";
      |HAZ externos;
      |ERROR;
      |ACABA;
  |FINSI;
*/
  |SI inkey = 12;           || consulta cobros
      ext1 = #0#4;          || cuenta
      ext2 = #0#5;          || digito
      programa = "caconcob.run";
      |HAZ externos;
      |ERROR;
      |ACABA;
  |FINSI;
  |SI inkey = 13;           || consulta pagos
      ext1 = #0#4;          || cuenta
      ext2 = #0#5;          || digito
      programa = "caconpag.run";
      |HAZ externos;
      |ERROR;
      |ACABA;
  |FINSI;
|FPRC;

|PROCESO externos;                      || desde calculo 'cuefin'
  |ATRI P;
  |MENSA "    Cargando Opcion ...";
  |ATRI 0;
  sit1 = #0#0;         || diario
  sit2 = #0#1;         || fecha
  sit3 = #0#2;         || documento        / las guarda para la vuelta
  sit4 = #0#3;         || apunte
  sit5 = #1#2;         || debe asiento
  sit6 = #1#3;         || haber asiento
  |LIBERA #0;
  |CIERRAT #0;        || cierra lins.
  |LIBERA #1;
  |CIERRAT #1;        || cierra cabs.

  |SUB_EJECUTA programa;

  |ABRET #0;          || abre lins.
  #0#0 = sit1;         || diario
  #0#1 = sit2;         || fecha
  #0#2 = sit3;         || documento
  #0#3 = sit4;         || apunte
  |LEE 101#0=;        || lee el apunte
  |ABRET #1;          || abre cabs.
  #1#0 = sit1;         || diario
  #1#1 = sit2;         || fecha
  #1#2 = sit3;         || documento
  |LEE 101#1=;        || lee el asiento
  #1#2 = sit5;         || debe asiento
  #1#3 = sit6;         || haber asiento
  #2#0 = #1#3 - #1#2;  || saldo asiento
|FPRC;

***************************************************************************
                         RUTINAS
***************************************************************************

|PROCESO a_caja;
 |SI #0#8 = "P";
     #1#2 = #1#2 +. #0#9;
 |SINO;
     #1#3 = #1#3 +. #0#9;
 |FINSI;
 #2#0 = #1#3 - #1#2;
 |PINTA #1#2;
 |PINTA #1#3;
 |ATRI I;
 |PINTA 0967, #2#0;
 |ATRI 0;
|FPRC;

|PROCESO a_cuenta;                          || desde PROCESO 'refresca'
  |ABRE #4;
  #4#0 = lcuenta;
  #4#1 = ldigito;
  |LEE 101#4=;
  |SI FSalida ! 0; |CIERRA #4; |ACABA; |FINSI;
  |SI #0#0 = 0;
      |SI signo = "D";
            #4#9 = #4#9 +. #0#9;
            #4#51 = #4#51 +. #0#9;
      |SINO;
            #4#10 = #4#10 +. #0#9;
            #4#52 = #4#52 +. #0#9;
      |FINSI;
      #4#11 = #4#9 - #4#10;
      #4#53 = #4#51 - #4#52;
  |SINO;
      |SI #0#0 = 99;
          |SI signo = "D";
              #4#48 = #4#48 +. #0#9;
              #4#51 = #4#51 +. #0#9;
          |SINO;
              #4#49 = #4#49 +. #0#9;
              #4#52 = #4#52 +. #0#9;
          |FINSI;
          #4#50 = #4#48 - #4#49;
          #4#53 = #4#51 - #4#52;
      |SINO;
          |HAZ actualmes;
      |FINSI;
  |FINSI;
  |SI #4#58 < #0#1;
      #4#58 = #0#1;
  |FINSI;
  |GRABA 020#4a;
  |LIBERA #4;
  |CIERRA #4;
|FPRC;

|PROCESO actualmes;
    ini = dig1;
    fecalf = #0#1;
    mes = fecalf % 402;
    mesfec = mes;
|SI ini = 1;
        mesac = mesfec;
|SINO;
        mesac = (mesfec - ini) + 1;
    |SI mesac [ 0;
            mesac = (13 - ini) + mesfec;
    |FINSI;
|FINSI;
    a = (mesac * 3) + 9;
    b = a + 1;
    c = a + 2;
|SI signo = "D";
        #4a = #4a +. #0#9;
        #4#51 = #4#51 +. #0#9;
|SINO;
        #4b = #4b +. #0#9;
        #4#52 = #4#52 +. #0#9;
|FINSI;
    #4c = #4a - #4b;
    #4#53 = #4#51 - #4#52;
|FPRC;
