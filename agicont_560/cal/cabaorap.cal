|FICHEROS;
    cabaorap #0;
    cacuenta #1;         || cuentas
    caparbal #2;         || partidas de balance
    cabaexp0 #3;         || temporal cuentas totales
    cabaexp2 #5;         || temporal SITUACION por si pide cuentas
    caexistc #6;         || descarga cabs.
    caexistl #7;         || descarga lins.
    canempre #30;        || empresas
    caconimp #8;
|FIN;

|VARIABLES;
  &pathbal = "";
  &balance = 1;
  &aparam = "";
  &entrada = 1;
     dif = 0;
     &cta = "";
     &nom = "";
     &sw_inf = 0;   || variables del informe
     &cantidad = 0;
     &cantidad2 = 0;
     &c_origen = 0;
     &c_aplica = 0;
     &variable = "";
     &canti = 0;
     &canti2 = 0;
     &doh = "";
     &anu = 0;
     &anu1 = 0;
     puntos = "..........................................................";
   agrunom = "";
   agrup1 = 0;
   agrup2 = 0;
   act_cuent = "";
   act_nivel = 0;
   nivel = 0;
   nom1 = "";
   varcamp = 0;
   sw_exp = 0;
   descn = 0;
   mes = 0;
   letrames = "";
   informe  = "";
   mes_fin = 0;
   descar = 0;
   traraf = 0;
   sumar = 0;
   dos = 0;
   acumula = 0;
   acumul1 = 0;
   potra = 0;

   activo = 0;
   activ1 = 0;
   pasivo = 0;
   pasiv1 = 0;
   explo = 0;
   expl1 = 0;
   debe1 = 0;
   haber1 = 0;
   cue_perdi = "";
   dig_perdi = 0;

   x = 0;
   &eldesde = 0;
   &elhasta = 0;
     sw = 0;
     agrupaci = 0;
     epigrafe = 0;
     sw_epi = 0;
     sw_agr = 0;
     tipo = "";
    ray1 = "            -------------------------------------------------------------------"
    ray2 = "                                              ---------------------------------"
    ray3 = "                                              ================================="

    alfa1 = "";
    alfa2 = "";
    alfa3 = "";

    &r_emp = 0;
    &r_per = 0;
    &r_eje = 0;
    &r_nom = "";
    &d_mes = 0;
    &h_mes = 0;
    &estru = 0;
|FIN;

|INCLUYE acceso_i;
|INCLUYE digito_i;

|PROCESO antesque;
  aparam = PARAMETRO;
  |QBF aparam;
  |SI aparam = "fech";
      |NOME_DAT #1 "cabalano";
  |FINSI;
|FPRC;

|| ______________________________________/ CALCULOS DE PANTALLA

|PROCESO mayor;
  #0Campo = #0Campo > "  ";
  |PINTA #0Campo;
|FPRC;

|PROCESO nivel; |TIPO 7; || desde numero de nivel
  |SI #0#4 = "N";
      |CAMPO_MODIFICA #0#5,1,"N";
  |SINO;
      |CAMPO_MODIFICA #0#5,1,"S";
  |FINSI;
|FPRC;

|PROCESO miraniv; |TIPO 0; || desde numero de nivel
  nivel = #0#5;
  |SI nivel = 1;|Y dig4 = 0;  |AVISO;  |ERROR;  |FINSI;
  |SI nivel = 2;|Y dig5 = 0;  |AVISO;  |ERROR;  |FINSI;
  |SI nivel = 3;|Y dig6 = 0;  |AVISO;  |ERROR;  |FINSI;
  |SI nivel = 4;|Y dig7 = 0;  |AVISO;  |ERROR;  |FINSI;
  |SI nivel = 5;|Y dig8 = 0;  |AVISO;  |ERROR;  |FINSI;
  |SI nivel = 6;|Y dig9 = 0;  |AVISO;  |ERROR;  |FINSI;
|FPRC;

|PROCESO deentrada;  |TIPO 7;
  |SI #0#14 = 0;  |Y #0#15 = 0;
      #0#14 = empre;
      #0#15 = ejerc - 1;
      |PINTA #0#14;
      |PINTA #0#15;
  |FINSI;
|FPRC;

|PROCESO esbuena;
  |SI FSalida = 9;  |ACABA;  |FINSI;
  |ABRE #30;
  #30#2 = #0#14;
  #30#3 = 0;
  |LEE 011#30];
  |SI FSalida ! 0;  |O #30#2 ! #0#14;
      |MENAV "     NO Existe la empresa Contable";
      |ERROR;
  |FINSI;
  |CIERRA #30;
|FPRC;

|PROCESO consulta;
  |SI FSalida ! 9;  |ACABA;  |FINSI;
  |CIERRAT #30;
  r_emp = 0;
  |SUB_EJECUTA "caconemp.run";
  |ABRET #30;
  |ERROR;
  |SI Campo = 14;
      #0#14 = r_emp;
      #0#15 = r_per;
      |PTEC 802;
      |PTEC 802;
  |FINSI;
|FPRC;

|PROCESO esbuena1;
  |SI FSalida = 2; |ACABA; |FINSI;
  |ABRE #30;
  #30#2 = #0#14;
  #30#3 = #0#15;
  |LEE 011#30=;
  |SI FSalida ! 0;
      |MENAV "     NO Existe Periodo en la empresa Contable";
      |CIERRA #30;
      |ERROR;
      |ACABA;
  |FINSI;
  |SI #30#13 ! dig3;  |O #30#14 ! dig4;  |O #30#15 ! dig5;
                      |O #30#16 ! dig6;  |O #30#17 ! dig7;
                      |O #30#18 ! dig8;  |O #30#19 ! dig9;
      |MENAV "     NO CORRESPONDEN LOS NIVELES";
      |CIERRA #30;
      |ERROR;
      |ACABA;
  |FINSI;
  #0#16 = #30#1;
  anu1 = 1900 + #30#26;
  nom1 = #30#1;
  |SI anu1 [ 1970;
      anu1 = anu1 + 100;
  |FINSI;
  |PINTA #0#16;
  #30#2 = empre;
  #30#3 = ejerc;
  |LEE 011#30=;
  anu = 1900 + #30#26;
  |SI anu [ 1970;
      anu = anu + 100;
  |FINSI;
  |CIERRA #30;
|FPRC;

|PROCESO pintames; |TIPO 0;
  varcamp = Campo + 2;                || Coge el campo del nombre del mes
  |SI #0Campo ! 0; |Y #0Campo ! 13;
      mes = #30#11 + (#0Campo - 1);   || Operacion para saber que mes es
  |SINO;
      mes = #0Campo;
  |FINSI;
  |HAZ mesletra;                          || Recoge el mes en letra
  |SI Campo = 7;  |O Campo = 8;
      |ATRI I;
      |SI Campo = 7;
          |PINTA 1626, letrames;
      |SINO;
          |PINTA 1659, letrames;
      |FINSI;
      |ATRI 0;
  |SINO;
      #0varcamp = letrames;
      potra = Campo;
      |PINTA #0varcamp;                       || Pinta el nombre del mes
      Campo = potra;
  |FINSI;
  |SI Campo = 1;
      |SI #0#0 > #0#1;
          |MENAV "    Error de Entrada ...";
          |ERROR;
      |FINSI;                         || Si el mes hasta es menor al desde
  |FINSI;
  |SI Campo = 8;
      |SI #0#7 > #0#8;
          |MENAV "     Error de Entrada ...";
          |ERROR;
      |FINSI;
  |FINSI;
|FPRC;

|PROCESO mesletra;
  |SI mes = 0;  letrames = "Apertura  "; |FINSI;
  |SI mes = 1;  letrames = "Enero     "; |FINSI;
  |SI mes = 2;  letrames = "Febrero   "; |FINSI;
  |SI mes = 3;  letrames = "Marzo     "; |FINSI;
  |SI mes = 4;  letrames = "Abril     "; |FINSI;
  |SI mes = 5;  letrames = "Mayo      "; |FINSI;
  |SI mes = 6;  letrames = "Junio     "; |FINSI;
  |SI mes = 7;  letrames = "Julio     "; |FINSI;
  |SI mes = 8;  letrames = "Agosto    "; |FINSI;
  |SI mes = 9;  letrames = "Septiembre"; |FINSI;
  |SI mes = 10; letrames = "Octubre   "; |FINSI;
  |SI mes = 11; letrames = "Noviembre "; |FINSI;
  |SI mes = 12; letrames = "Diciembre "; |FINSI;
  |SI mes = 13; letrames = "Cierre    "; |FINSI;
|FPRC;

|PROCESO estruct; |TIPO 0;
  |SI #0#6 = 0;  |ACABA;  |FINSI;
  |ABRE #6;
  #6#0 = #0#6;
  |LEE 040#6=;
  |SI FSalida ! 0;
      |MENAV "    No existe la estructura de descarga.";
      |CIERRA #6;
      |ERROR;
      |ACABA;
  |FINSI;
  |HAZ cue_per;
  |CIERRA #6;
|FPRC;

|PROCESO cue_per; || calcula la cuenta de perdidas y ganancias
  cue_perdi = #6#2;
  dig_perdi = #6#3;
  |SI #0#4 = "N";  nivel = #6#3;  |FINSI;
  |SI nivel < #6#3;
      |SI nivel = 1;  traraf = 100 + dig4;  |FINSI;
      |SI nivel = 2;  traraf = 100 + dig5;  |FINSI;
      |SI nivel = 3;  traraf = 100 + dig6;  |FINSI;
      |SI nivel = 4;  traraf = 100 + dig7;  |FINSI;
      |SI nivel = 5;  traraf = 100 + dig8;  |FINSI;
      cue_perdi = #6#2 % traraf;
      dig_perdi = nivel;
  |FINSI;
  cue_perdi = cue_perdi + "            ";
  cue_perdi = cue_perdi % 112;
|FPRC;

|| --------------------------------\ fin calculos de PANTALLA

|PROCESO conexi;
  d_mes = #0#7;
  h_mes = #0#8;
  estru = #0#6;
  r_emp = empre;
  r_per = ejerc;
  r_eje = #30#26;
  r_nom = #30#1;
  |SUB_EJECUTA "caexicon.run";
    r_emp = #0#14;
    r_per = #0#15;
    r_eje = anu1  -1900;
    r_nom = nom1;
  |SUB_EJECUTA "caexicon.run";
|FPRC;

|| ____________________________________/

|PROCESO leexplot;
  |DDEFECTO #2;
  #2#0 = #1#5;
  #2#1 = #1#6;
  #2#3 = #1#7;
  #2#5 = #1#8;
  |LEE 000#2=;
  |SI FSalida ! 0;
      #2#2 = "";
      #2#4 = "";
      #2#6 = "";
  |FINSI;
|FPRC;

|PROCESO cargatr3;
  #3#0 = #1#5;
  #3#1 = #1#4;
  #3#2 = #1#6;
  #3#3 = #1#7;
  #3#4 = #1#8;
  |LEE 101#3=;
  |SI FSalida ! 0;
      |DDEFECTO #3;
      #3#0 = #1#5;
      #3#1 = #1#4;
      #3#2 = #1#6;
      #3#3 = #1#7;
      #3#4 = #1#8;
      |HAZ leexplot;
      #3#5 = #2#2;
      #3#6 = #2#4;
      #3#7 = #2#6;
      |GRABA 020#3b;
  |FINSI;
|FPRC;

|PROCESO acortacuen;
  |SI nivel < #1#1;
      |SI nivel = 1;  traraf = 100 + dig4;  |FINSI;
      |SI nivel = 2;  traraf = 100 + dig5;  |FINSI;
      |SI nivel = 3;  traraf = 100 + dig6;  |FINSI;
      |SI nivel = 4;  traraf = 100 + dig7;  |FINSI;
      |SI nivel = 5;  traraf = 100 + dig8;  |FINSI;
      #1#0 = #1#0 % traraf;
      #1#1 = #0#5;
      #1#0 = #1#0 + "              ";
      #1#0 = #1#0 % 112;
  |FINSI;
|FPRC;

|PROCESO cargatr5;
  #5#0 = #1#5;
  #5#1 = #1#4;
  #5#2 = #1#6;
  #5#3 = #1#7;
  #5#4 = #1#8;
  |HAZ acortacuen;
  #5#5 = #1#0;
  #5#6 = #1#1;
  #5#7 = #1#2;
  |LEE 000#5=;
  |SI FSalida ! 0;
      |DDEFECTO #5;
      #5#0 = #1#5;
      #5#1 = #1#4;
      #5#2 = #1#6;
      #5#3 = #1#7;
      #5#4 = #1#8;
      |HAZ acortacuen;
      #5#5 = #1#0;
      #5#6 = #1#1;
      #5#7 = #1#2;
      #5#11 = 0;
      #5#12 = 0;
      |HAZ leexplot;
      #5#8 = #2#2;
      #5#9 = #2#4;
      #5#10 = #2#6;
      |GRABA 020#5b;
  |FINSI;
|FPRC;

|PROCESO grabatr3;                   || temporal exp0 - totales
  |SI sumar = 0; |ACABA; |FINSI;
  #3acumul1 = #3acumul1 + sumar;
  |SI #1#5 = "A";
      activo = activo + sumar;
  |SINO;
      pasivo = pasivo + sumar;
  |FINSI;
  |GRABA 020#3a;
  |LIBERA #3;
|FPRC;

|PROCESO grabatr5;                    || temporal exp2 - cuentas
  |SI sumar = 0; |ACABA; |FINSI;
  |HAZ cargatr5;
  #5acumula = #5acumula + sumar;
  |GRABA 020#5a;
  |LIBERA #5;
|FPRC;

||----------------------------  grabo CUENTAS DE EXISTENCIAS EN TRABAJO

|PROCESO descarg;
  |HAZ digitos2;
  |SI d_error ! 0;  |ACABA;  |FINSI;

  |HAZ cargatr3;
  mes_fin = #0#8 + 11;  || mes finales
  sumar = #7mes_fin;
  |SI #1#5 = "P"; sumar = -sumar; |FINSI;
  #3#9 = 1;
  |HAZ grabatr3;
  |SI  #0#4 = "S"; |HAZ grabatr5; |FINSI;
|FPRC;

|PROCESO lineades; || lee las cuentas de existencias.
  #1#0 = #7#2;
  #1#1 = #7#3;
  |LEE 000#1=;
  |SI FSalida = 0;
      |HAZ descarg;
  |FINSI;
|FPRC;
|| -----------------------------------------------------------------------

||----------------------------  grabo CUENTA DE PERDIDAS Y GANANCIAS
|PROCESO graperdi;
  #1#0 = cue_perdi;
  #1#1 = dig_perdi;
  |LEE 000#1=;
  |SI FSalida ! 0;
      |MENAV "     ATENCION NO EXISTE CUENTA DE PERDIDAS Y GANANCIAS";
      |ERROR;
      |ACABA;
  |FINSI;
  |HAZ cargatr3;
  sumar = 0;
  |SI #1#5 = "A";
      |PARA x = eldesde; |SI x [ elhasta; |HACIENDO x = x + 3;
            sumar = sumar + #1x;
      |SIGUE;
  |SINO;
      |PARA x = eldesde; |SI x [ elhasta; |HACIENDO x = x + 3;
            sumar = sumar + (#1x * -1);
      |SIGUE;
  |FINSI;
  sumar = sumar + explo;

  |HAZ grabatr3;
  |SI  #0#4 = "S"; |HAZ grabatr5; |FINSI;
|FPRC;

|| -----------------------------------------------------------------

|| ----------------- A la explotacion le quitamos las existencias;
|PROCESO ver_exp;
  mes_fin = #0#7 + 11;  || mes iniciales
  descar = #7mes_fin;
  mes_fin = #0#8 + 11;  || mes finales
  descar = descar - #7mes_fin; || Iniciales - finales
  |SI descar ] 0;
      act_cuent = #7#5;     || cuenta descarga D
      act_nivel = #7#6;     || digito descarga D
  |SINO;
      act_cuent = #7#8;     || cuenta descarga H
      act_nivel = #7#9;     || digito descarga H
  |FINSI;
  |SI descar = 0; |ACABA; |FINSI;

  |SI act_cuent = #1#0; |Y act_nivel = #1#1;
     sw_exp = 1;
       |SI #1#5 = "H"; descar = -descar; |FINSI;
     descn = descn + descar;
  |FINSI;
|FPRC;

|DEFBUCLE cabasitu9;
 #7#1;
 ;
 #0#6, 01;
 #0#6, 99;
 be;
 NULL, ver_exp;
|FIN;

|PROCESO calexpl01;
  sw_exp = 0;
  descn= 0;
  |BUCLE cabasitu9;
  |SI sw_exp = 1
      |SI #1#5 = "D";
          |PARA x = eldesde; |SI x [ elhasta; |HACIENDO x = x + 3;
               debe1 = debe1 - #1x;
          |SIGUE;
          debe1 = debe1 + descn;
      |SINO;
          |PARA x = eldesde; |SI x [ elhasta; |HACIENDO x = x + 3;
                haber1 = haber1 - (#1x * -1);
          |SIGUE;
          haber1 = haber1 + descn;
     |FINSI;
  |FINSI;
|FPRC;

|DEFBUCLE calexpl0;
 #1#4;
 3;
 "D", 1, 01, 00, 00,"S";
 "H", 4, 99, 99, 99,"S";
 e;
 NULL,calexpl01;
|FIN;
|| ----------------------------------------------------------------

|| ----- Calculamos la explotacion
|PROCESO explota1;
  |SI #1#5 = "D";
      |PARA x = eldesde; |SI x [ elhasta; |HACIENDO x = x + 3;
            debe1 = debe1 + #1x;
      |SIGUE;
  |SINO;
      |PARA x = eldesde; |SI x [ elhasta; |HACIENDO x = x + 3;
            haber1 = haber1 + (#1x * -1);
      |SIGUE;
  |FINSI;
  canti = haber1 - debe1;
  explo = canti;
|FPRC;

|DEFBUCLE explota;
 #1#4;
 3;
 " ", 1,  0,  0,  0, "S";
 "z", 4, 99, 99, 99, "S";
 e;
 NULL, explota1;
|FIN;
|| ---------------------------------------------------------------------

|| --------------------------------- Graba el resto de cuentas
|PROCESO calculit;
  |HAZ digitos2;
  |SI d_error ! 0;  |ACABA;  |FINSI;

  |SI #0#4 = "N";  |Y #1#3 = "N";                     |ACABA;  |FINSI;
  |SI #0#4 = "S";  |Y #1#55 > nivel;                  |ACABA;  |FINSI;
  |SI #0#4 = "S";  |Y #1#3 = "N";  |Y #1#55 ! nivel;  |ACABA;  |FINSI;
  |SI #1#0 = cue_perdi;                               |ACABA;  |FINSI;

  |HAZ cargatr3;
  alfa1 = #1#0 % 101;
  |SI #3#9 = 1; |Y alfa1 = 3;  |LIBERA #3;  |ACABA;  |FINSI;

  sumar = 0;
  |SI #1#5 = "A";
      |PARA x = eldesde; |SI x [ elhasta; |HACIENDO x = x + 3;
            sumar = sumar + #1x;
      |SIGUE;
  |SINO;
      |PARA x = eldesde; |SI x [ elhasta; |HACIENDO x = x + 3;
            sumar = sumar + (#1x * -1);
      |SIGUE;
  |FINSI;
  |HAZ grabatr3;
  |SI  #0#4 = "S"; |HAZ grabatr5; |FINSI;
|FPRC;

|DEFBUCLE todas_ctas;
 #1#1;
 5,4,6,7,8;
 "            ", 0, "A", 0, 01, 00, 00;
 "zzzzzzzzzzzz", 9, "P", 0, 99, 99, 99;
 e;
 NULL, calculit;
|FIN;

|| ---------------------------------------------------------------------
|PROCESO agrupacion; || imprimir agrupacion.
  sw_inf = 0; |PRINT;   || agrupacion (sin importe)
  agrunom = #3#5;
  agrup1 = 0;
  agrup2 = 0;
  sw_agr = 1;
|FPRC;

|PROCESO rup_tipo;
  sw_inf = 5;  |PRINT;                     || linea en blanco
  |SI tipo = "A";
      variable = "TOTAL ACTIVO..............................................";
      cantidad = activ1;
      cantidad2 = activo;
  |SINO;
      variable = "TOTAL PASIVO..............................................";
      cantidad = pasiv1;
      cantidad2 = pasivo;
  |FINSI;
  |HAZ ori_apli;
  sw_inf = 6; |PRINT;
  |PIEINF;
  doh = "PASIVO";
  tipo = #3#0;
  agrupaci = #3#2;
  epigrafe = #3#3;
|FPRC;

|PROCESO rup_agru;
  variable = "Total "+ agrunom;
  |QBF variable;
  variable = variable + puntos;
  variable = variable % 158;

  cantidad  = agrup1;
  cantidad2 = agrup2;
  |HAZ ori_apli;
  sw_inf = 6; |PRINT;
  sw_inf = 5; |PRINT;
  agrupaci = #3#2;
  epigrafe = #3#3;
  sw_agr = 0;
|FPRC;

|PROCESO rup_epig;
 sw_epi = 0;
 epigrafe = #3#3;
|FPRC;

|| ----------- Impresion de las cuentas \
|PROCESO cuenta;
  cta = #5#5;
  nom = #5#7;
  cantidad = #5#11;
  cantidad2 = #5#12;
  |HAZ ori_apli;
  |SI cantidad ! 0;|O cantidad2 ! 0;
  sw_inf = 1; |PRINT;
  |FINSI;
|FPRC;

|DEFBUCLE cabaorap5;
 #5#1;
 ;
 #3#0, #3#1, #3#2, #3#3, #3#4, "            ", 0;
 #3#0, #3#1, #3#2, #3#3, #3#4, "zzzzzzzzzzzz", 9;
 e;
 NULL, NULL, NULL, NULL, NULL, cuenta;
 #5#5, #5#6;
|FIN;
|| ----------- fin impresion de las cuentas /

|PROCESO impresio;
  |SI #3#8 = 0;|Y #3#10 = 0;  |ACABA;  |FINSI;
      cantidad = #3#8;
      cantidad2 = #3#10;
      |HAZ ori_apli;     ||Calcula origen y aplicacion

  |SI sw = 0;
      tipo = #3#0;
      agrupaci = #3#2;
      epigrafe = #3#3;
      |SI #3#0 = "A"; doh = "ACTIVO"; |FINSI;
      |SI #3#0 = "P"; doh = "PASIVO"; |FINSI;
      sw = 1;
  |FINSI;

  |SI tipo ! #3#0;
      |HAZ rup_epig;
      |HAZ rup_agru;
      |HAZ rup_tipo;
  |SINO;
      |SI agrupaci ! #3#2;
          |HAZ rup_epig;
          |HAZ rup_agru;
      |SINO;
          |SI epigrafe ! #3#3;
              |HAZ rup_epig;
          |FINSI;
      |FINSI;
  |FINSI;
  |SI sw_agr = 0;
      |HAZ agrupacion;
  |FINSI;
  |SI sw_epi = 0;
      |SI epigrafe ! 0;
          |HAZ ori_apli;
          sw_inf = 2;  |PRINT;        || epigrafe
      |FINSI;
  sw_epi = 1;
  |FINSI;

 || ..... calculo de cada linea normal
 |SI #3#4 ! 0;
      sw_inf=3; |PRINT;
 |FINSI;
 agrup1= agrup1 + #3#8;
 agrup2= agrup2 + #3#10;
 |SI #0#4 = "S";        || imprime detalle de cuentas
     |BUCLE cabaorap5;
     sw_inf=5; |PRINT;
 |FINSI;
|FPRC;

|DEFBUCLE cabaorap1;
 #3#1;
 ;
 "A", 0, 01, 00, 00;
 "P", 0, 99, 99, 99;
 be;
 NULL,impresio;
|FIN;

|PROCESO ori_apli;
  c_origen = 0;
  c_aplica = 0;
  dif = cantidad - cantidad2;
  |SI dif > 0;
      c_aplica = dif;
  |SINO;
      c_origen = -dif;
  |FINSI;
|FPRC;

____________________________________/ CALCULO PRINCIPAL
|PROCESO impre; |TIPO 3;
  |HAZ antesque;
  |HAZ conexi;

  |ABRE #3;
  |CIERRA #3;
  |DELINDEX #3;
  |ABRE #5;
  |CIERRA #5;
  |DELINDEX #5;

  |IMPRE 0;
  |SI FSalida ! 0;  |ACABA;  |FINSI;
  |HAZ los_informes;
  informe = que_i1;
  |INFOR informe;
  |SI FSalida ! 0;  |FINIMP;  |ACABA;  |FINSI;
  sw = 0;
  eldesde = (#0#0 * 3 ) + 11;
  elhasta = (#0#1 * 3 ) + 11;

  |PARA dos = 1;  |SI dos [ 2;  |HACIENDO dos = dos + 1;
        acumul1 = 8;
        acumula = 11;
        |SI dos = 2;
            acumul1 = 10;
            acumula = 12;
            activ1 = activo;
            pasiv1 = pasivo;
            expl1 = explo;
            |DBASE dir_fich;
            dir_fich = dir_fich + "fich/";
            alfa1 = #0#14;   alfa1 = "00000" + alfa1;   alfa1 = alfa1 % -105;
            alfa2 = #0#15;   alfa2 = "0"   + alfa2;   alfa2 = alfa2 % -101;
            alfa3 = dir_fich + alfa1 + alfa2 + "/";
            |PATH_DAT #1 alfa3;
            |PATH_DAT #2 alfa3;
            |PATH_DAT #6 alfa3;
            |PATH_DAT #7 alfa3;
            |PATH_DAT #8 alfa3;
            |ABRE #30;
            #30#2 = #0#14;
            #30#3 = #0#15;
            |LEE 011#30=;
            |CIERRA #30;
        |FINSI;

        |ABRE #8;
        |LEE 111#8p;
        |SI #8#2 = "S";  |Y #8#3 = "N";  |Y aparam = "";
            |SI dos = 2;
                pathbal = alfa3;
            |SINO;
                |DFICE pathbal;
            |FINSI;
            balance = 1;
            |CIERRA #8;
            |SUB_EJECUTA "careccue.tab";
        |FINSI;
        |CIERRA #8;

||... Modificar Cuentas a balance segun saldo
  |SI dos = 1;
      r_emp = empre;
      r_per = ejerc;
  |SINO;
      r_emp = #0#14;
      r_per = #0#15;
  |FINSI;
  |SUB_EJECUTA "carecsig.run";
|| ---------------------------------------------------------------------

        |ABRET #1;
        |ABRE #2;
        |ABRE #3;
        |ABRE #5;
        |ABRE #6;
        activo = 0;
        pasivo = 0;
        explo = 0;
        debe1 = 0;
        haber1 = 0;

        #6#0 = #0#6;                 ||  Calculo Existencias
        |LEE 000#6=;                 ||
        |HAZ cue_per;                ||
        |BUCLELIN 2lineades#6;       ||
        |CIERRA #6;                  ||

        |BUCLE calexpl0;             ||  calcula - variacion;
        |BUCLE explota;              ||  Calcula explotacion;
        |HAZ graperdi;               ||  Graba perdidas y ganancias

        |BUCLE todas_ctas;           ||  calcula resto de cuentas

        |CIERRAT #1;
        |CIERRA #2;
        |CIERRA #3;
        |CIERRA #5;
  |SIGUE;
            |ABRE #30;
            #30#2 = empre;
            #30#3 = ejerc;
            |LEE 011#30=;
            |CIERRA #30;

  || ------------- IMPRIMIR BALANCE
  |ABRET #1;
      |BUCLE cabaorap1; || lee fichero #3
  |HAZ final;
  |PIEINF;
  |FININF;
  |FINIMP;
  |CIERRAT #1;
  |DELINDEX #3;
  |DELINDEX #5;
|FPRC;

|PROCESO final;
  |HAZ rup_epig;
  |HAZ rup_agru;
  sw_inf = 5;  |PRINT;                     || linea en blanco
  |SI tipo = "A";
      variable = "TOTAL ACTIVO............................";
      cantidad = activ1;
      cantidad2 = activo;
  |SINO;
      variable = "TOTAL PASIVO............................";
      cantidad = pasiv1;
      cantidad2 = pasivo;
  |FINSI;
  |HAZ ori_apli;
  sw_inf = 6; |PRINT;
|FPRC;
