|FICHEROS;
    carecefe #0;
    camancob #1;    || Cobros
    camanpag #2;    || Pagos
    cacuenta #4;    || Cuentas
    caclipro #18;   || Clientes y Proveedores
|FIN;

|VARIABLES;
    &entrada = 1;
    alfa1  = "";
    sw     = 0;
    cuenta = "";
    digito = 0;
    x      = 0;
    long = "  ";
    cta  = 0;
    cont = 0;
    men2  = "     Longitud de Cuenta Fuera de Nivel !";

    ctacte  = "";
    elccc   = "";
    ceros   = "0000000000";
    ccc     = "";
    c_alfa1 = "";
    c_alfa2 = "";
    c_nume1 = 0;
    c_nume2 = 0;
    c_nume3 = 0;
    swgrabo = 0;
|FIN;

|INCLUYE acceso_i;
|INCLUYE puntos_i;

____________________________________/ CALCULOS DESDE PANTALLA
|PROCESO mayus; |TIPO 0;
  #0Campo = #0Campo > #0Campo;
  |PINTA #0Campo;
|FPRC;

|PROCESO def_fec; |TIPO 7;
    #0#5  = fec1;    |PINTA #0#5;
    #0#6  = fec2;    |PINTA #0#6;
    #0#14 = fec1;    |PINTA #0#14;
    #0#15 = fec2;    |PINTA #0#15;
|FPRC;

|PROCESO cuenta;|TIPO 6;
 p_alfa1 = #0Campo;
 salidas = FSalida;
 |HAZ punto;
 |SI p_error ! 0;  |ERROR;  |ACABA;  |FINSI;
    x    = Campo + 2;
    cta  = #0Campo;
    long = cta % A0;
    cont = 0;
    sw = 0;
|SI long = #30#14; sw = 1; #0x = 1; |FINSI;
|SI long = #30#15; sw = 1; #0x = 2; |FINSI;
|SI long = #30#16; sw = 1; #0x = 3; |FINSI;
|SI long = #30#17; sw = 1; #0x = 4; |FINSI;
|SI long = #30#18; sw = 1; #0x = 5; |FINSI;
|SI long = #30#19; sw = 1; #0x = 6; |FINSI;
|SI sw = 0;
    |MENAV men2;
    |ERROR;
|FINSI;
|FPRC;

|| **************************************************************************
|PROCESO leyendo;
  sw = 0;
  ctacte = "";
  elccc  = "";
  swgrabo = 0;
  || ................................................
  #18#23 = "C";
  #18#10 = cuenta;
  #18#11 = digito;
  |LEE 101#18=;
  |SI FSalida ! 0;
      #18#23 = "P";
      #18#10 = cuenta;
      #18#11 = digito;
      |LEE 101#18=;
      |SI FSalida ! 0;
          |DDEFECTO #18;
          sw     = 1;
      |FINSI;
  |FINSI;
  ctacte = #18#17;
  elccc  = #18#18;
  |SI #0#16 = "S";    || Modifica Formato C.C.C. long 10 + 10, relleno con 0 y s¢lo n£meros
      |HAZ cal_ccc;
      |SI sw = 0; |Y swgrabo = 1; || Se modifica tambien en Cliente/Proveedor
          |GRABA 020#18a;
      |FINSI;
  |FINSI;
  |LIBERA #18;
|FPRC;

|PROCESO lee_cuent;
 |ABRET #4;
    #4#0 = cuenta;
    #4#1 = digito;
    |LEE 001#4=;
    |SI FSalida ! 0;
        |DDEFECTO #4;
    |FINSI;
 |CIERRAT #4;
|FPRC;

|| **************************************************************************
|PROCESO lee_pagos1;
 |ATRI I;
 alfa1 = #2#0;   alfa1 = ("00" + alfa1) % -102;     |PINTA 2238, alfa1;
 alfa1 = #2#1;   alfa1 = ("000000" + alfa1) % -106; |PINTA 2241, alfa1;
 alfa1 = #2#2;                                      |PINTA 2248, alfa1;
 alfa1 = #2#3;   alfa1 = ("000" + alfa1) % -103;    |PINTA 2251, alfa1;
 |ATRI 0;
 cuenta = #2#4;
 digito = #2#5;
 |HAZ leyendo;
 |SI sw = 0;             || Si existe la ficha modifica, si NO lo deja tal cual
     #2#14 = #18#13;     || Banco
     #2#15 = #18#14;     || Direccion del Banco
     #2#20 = #18#19;     || Poblacion del Banco
     #2#16 = #18#15;     || Cod. Provincia Banco
     #2#17 = #18#16;     || Cod. Poblacion Banco
     #2#18 = ctacte;     || Cuenta Corriente
     #2#19 = elccc;      || C.C.C.
     |GRABA 020#2a;
 |FINSI;
 |LIBERA #2;
|FPRC;

|DEFBUCLE lee_pagos;
 #2#1;
 6,4,7,11;
 #0#1, #0#3, "  ", 001, #0#5, #0#8, #0#14, #0#12;
 #0#2, #0#4, "zz", 999, #0#6, #0#9, #0#15, #0#13;
 be;
 NULL, lee_pagos1;
|FIN;


|PROCESO lee_cobro1;
 |ATRI I;
 alfa1 = #1#0;   alfa1 = ("00" + alfa1) % -102;     |PINTA 2238, alfa1;
 alfa1 = #1#1;   alfa1 = ("000000" + alfa1) % -106; |PINTA 2241, alfa1;
 alfa1 = #1#2;                                      |PINTA 2248, alfa1;
 alfa1 = #1#3;   alfa1 = ("000" + alfa1) % -103;    |PINTA 2251, alfa1;
 |ATRI 0;
 cuenta = #1#4;
 digito = #1#5;
 |HAZ leyendo;
 |SI sw = 0;             || Si existe la ficha modifica, si NO lo deja tal cual
     #1#15 = #18#13;     || Banco
     #1#16 = #18#14;     || Direccion del Banco
     #1#21 = #18#19;     || Poblacion del Banco
     #1#17 = #18#15;     || Cod. Provincia Banco
     #1#18 = #18#16;     || Cod. Poblacion Banco
     #1#19 = ctacte;     || Cuenta Corriente
     #1#20 = elccc;      || C.C.C.
     |GRABA 020#1a;
 |FINSI;
 |LIBERA #1;
|FPRC;

|DEFBUCLE lee_cobro;
 #1#1;
 6,4,7,11;
 #0#1, #0#3, "  ", 001, #0#5, #0#8, #0#14, #0#12;
 #0#2, #0#4, "zz", 999, #0#6, #0#9, #0#15, #0#13;
 be;
 NULL, lee_cobro1;
|FIN;
|| **************************************************************************

|PROCESO tipo3; |TIPO 3;
 |SI #0#7 = "SI";
    |ABRE #18;

    |SI #0#0 ! "P"; |BUCLE lee_cobro; |FINSI;
    |SI #0#0 ! "C"; |BUCLE lee_pagos; |FINSI;

    |CIERRA #18;
 |FINSI;
|FPRC;

|| ******************************
|PROCESO cal_ccc;
  swgrabo = 0;
  ccc = #18#17; |HAZ cccBanco;
  |SI ccc ! "";
      ctacte = ceros + ccc;
      ctacte = ctacte % -110;
  |FINSI;
  |SI #18#17 ! ctacte; #18#17 = ctacte; swgrabo = 1; |FINSI;
  ccc = #18#18; |HAZ cccBanco;
  |SI ccc ! "";
      elccc = ceros + ccc;
      elccc = elccc % -110;     || Cuenta corriente  Cliente
  |FINSI;
  |SI #18#18 ! elccc; #18#18 = elccc; swgrabo = 1; |FINSI;
|FPRC;

|PROCESO cccBanco;
 |QBT ccc;
 c_alfa1 = ccc % 0;
 c_nume1 = c_alfa1;
 c_alfa1 = "";
 c_alfa2 = "";
 |PARA c_nume2 = 1; |SI c_nume2 [ c_nume1; |HACIENDO c_nume2 = c_nume2 + 1;
       c_nume3 = c_nume2 * 100 + 1;
       c_alfa2 = ccc % c_nume3;
       |SI c_alfa2 ] "0"; |Y c_alfa2 [ "9";
           c_alfa1 = c_alfa1 + c_alfa2;
       |FINSI;
 |SIGUE;
 ccc = c_alfa1;
|FPRC;
|| ************************************************************************
