|FICHEROS;
    cachedup #0;
    catraiva #4;    || iva temporal
    caivarep #5;    || iva repercutido
    caivasop #6;    || iva soportado
|FIN;

|VARIABLES;
    informe = "";
    alfa1 = "";
    alfa2 = "";
    alfa3 = "";
    alfa4 = "";
    nume1 = 0;
    nume2 = 0;
    nume3 = 0;
    nume4 = 0;
    para1 = 0;
    para2 = 0;
    para3 = 0;
    para4 = 0;
    fech1 = @;
    fech2 = @;
    fech3 = @;
    fech4 = @;
    elsw1 = 0;
    mensa1 = "    ERROR!. FECHA INCORRECTA ";
    mensa2 = "    ERROR!. LIBRO INCORRECTO ";
    swalgun = 0;

    ccc        = "";
    c_alfa1    = "";
    c_alfa2    = "";
    c_nume1    = 0;
    c_nume2    = 0;
    c_nume3    = 0;

    ceros    = "";
    espacios = "";
    blancos  = "";
    cetas    = "" ;

    concepto = "";
    camcla1  = "";
    camcla2  = "";
    &swlin   = 0;
    &tipo    = "";
    &descrip = "";

    sw       = 0;

    &serie   = "";
    &numero  = 0;
    &antnume = 0;
    &antser  = "";
    &texterr = "";
    &primero = 0;
    &ultimo  = 0;
    &impnum  = "";
|FIN;

|INCLUYE acceso_i;
|INCLUYE tempo_i;

*****************************************************************************
                       CALCULOS DESDE CAMPOS
*****************************************************************************
|PROCESO mayor;
  #0Campo = #0Campo > " ";
  |PINTA #0Campo;
  |SI Campo = 0;
      |C_M #0#11,1,"S";
      |C_M #0#13,1,"S"; |C_M #0#14,1,"S";
      |SI #0Campo = "S";
          |C_M #0#11,1,"N";
          |C_M #0#13,1,"N"; |C_M #0#14,1,"N";
          #0#11 = "D"; |PINTA #0#11;
          #0#13 = "          "; |PINTA #0#13;
          #0#14 = "zzzzzzzzzz"; |PINTA #0#14;
      |FINSI;
  |SINO;
      |C_M #0Campo,0,alfa1;
      |SI alfa1 = "N"; |ACABA; |FINSI;
      |C_M #0#11,1,"S";
      |C_M #0#13,1,"S"; |C_M #0#14,1,"S";
      |SI Campo = 11;
          |SI #0Campo ! "S";
              |C_M #0#13,1,"N"; |C_M #0#14,1,"N";
              #0#13 = "          "; |PINTA #0#13;
              #0#14 = "zzzzzzzzzz"; |PINTA #0#14;
          |FINSI;
      |FINSI;
  |FINSI;
|FPRC;

|PROCESO defectos; |TIPO 7;
 #0#3 = fec1; |PINTA #0#3;
 #0#4 = fec2; |PINTA #0#4;
|FPRC;

|PROCESO fechad;
  |SI #0Campo < fec1; |O #0Campo > fec2;
      |MENAV mensa1;
      |ERROR;
      |ACABA;
  |FINSI;
|FPRC;

|PROCESO fechah;
  |SI #0Campo < fec1; |O #0Campo > fec2; |O #0Campo < #0#3;
      |MENAV mensa1;
      |ERROR;
      |ACABA;
  |FINSI;
|FPRC;

|PROCESO libroh; |TIPO 0;
  |SI #0#2 < #0#1;
      |MENAV mensa2;
      |ERROR;
      |ACABA;
  |FINSI;
|FPRC;

|PROCESO cam_pos_2; |TIPO 0;
 nume1 = #0Campo;
 |C_M #0#8, 1, "S";
 |C_M #0#9, 1, "S";
 |SI nume1 = 0;
     |C_M #0#8,1,"N"; #0#8 = 0; |PINTA #0#8;
     |C_M #0#9,1,"N";
 ||    |C_M #0#10,1,"N"; #0#10 = 1; |PINTA #0#10;
 |FINSI;
|FPRC;

|PROCESO quelong; |TIPO 0;
 |C_M #0Campo,0,alfa1;
 |SI alfa1 = "N"; |ACABA; |FINSI;
 nume1 = Campo - 1;
 nume2 = #0nume1 + #0Campo - 1;
 |SI nume2 > 30;
     |AVISO;
     |ERROR;
 |FINSI;
|FPRC;
*****************************************************************************
                       CALCULOS DE IMPRESION
*****************************************************************************
|PROCESO poncampos_s;
  #4#0  = #6#4;             || Cuenta Proveedor
  #4#1  = camcla1;           || Campo orden 1
  #4#2  = camcla2;           || Campo orden 2
  #4#3  = " ";              || Error
  #4#4  = #6#0;             || Libro
  #4#5  = #6#2;             || Factura
  #4#6  = #6#27;            || Serie
  #4#7  = #6#3;             || Fecha
  #4#8  = #6#4;             || Cuenta Contable
  #4#9  = #6#50;            || Titulo Cuenta
  #4#10 = #6#7;             || Concepto
  #4#11 = #6#26;            || Total Factura
|FPRC;

|PROCESO tmp_sopor1;

  |SI #0#11 ! "S";
      |PINTA 2241, "            ";
      |PINTA 2241, #6#0;
      |PINTA 2244, #6#2;
      |PINTA 2251, #6#27;
  |FINSI;

  concepto = #6#7;
  |HAZ montacamcla;
  |DDEFECTO #4;

  #4#0 = #6#4;
  #4#1 = camcla1;
  #4#2 = camcla2;
  |LEE 101#4=;
  |SI FSalida = 0;
      |SI #0#11 ! "S";
          |SI #4#3 ! "X";
              swlin = 1;
              |PRINT;
          |FINSI;
          |HAZ poncampos_s;
          #4#3 = "X";
          |GRABA 020#4a;
          swlin = 0;
          |PRINT;
          swalgun = 1;
      |FINSI;
  |SINO;
      |HAZ poncampos_s;
      |GRABA 020#4n;
  |FINSI;
  |LIBERA #4;
|FPRC;

|DEFBUCLE tmp_sopor;
 #6#1;
 3;
 #0#1,      0, "  ", #0#3;
 #0#2, 999999, "zz", #0#4;
 e;
 NULL, NULL, NULL, NULL, NULL, tmp_sopor1;
 #6#4, #6#6, #6#0, #6#2, #6#27;
|FIN;

|PROCESO poncampos_r;
  #4#0  = "            ";   || Cuenta Proveedor
  #4#1  = camcla1;           || Campo orden 1
  #4#2  = camcla2;           || Campo orden 2
  #4#3  = " ";              || Error
  #4#4  = #5#0;             || Libro
  #4#5  = #5#2;             || Factura
  #4#6  = #5#27;            || Serie
  #4#7  = #5#3;             || Fecha
  #4#8  = #5#4;             || Cuenta Contable
  #4#9  = #5#50;            || Titulo Cuenta
  #4#10 = #5#7;             || Concepto
  #4#11 = #5#26;            || Total Factura
|FPRC;

|PROCESO tmp_reper1;

  |SI #0#11 ! "S";
      |PINTA 2241, "            ";
      |PINTA 2241, #5#0;
      |PINTA 2244, #5#2;
      |PINTA 2251, #5#27;
  |FINSI;

  concepto = #5#7;
  |HAZ montacamcla;
  |DDEFECTO #4;
  #4#0 = "            ";
  #4#1 = camcla1;
  #4#2 = camcla2;
  |LEE 101#4=;
  |SI FSalida = 0;
      |SI #0#11 ! "S";
          |SI #4#3 ! "X";
              swlin = 1;
              |PRINT;
          |FINSI;
          |HAZ poncampos_r;
          #4#3 = "X";
          |GRABA 020#4a;
          swlin = 0;
          |PRINT;
          swalgun = 1;
      |FINSI;
  |SINO;
      |HAZ poncampos_r;
      |GRABA 020#4n;
  |FINSI;
  |LIBERA #4;
|FPRC;

|DEFBUCLE tmp_reper;
 #5#1;
 3;
 #0#1,      0, "  ", #0#3;
 #0#2, 999999, "zz", #0#4;
 e;
 NULL, NULL, NULL, NULL, NULL, tmp_reper1;
 #5#6, #5#0, #5#2, #5#27;
|FIN;

|PROCESO saltos;
 |SI #0#10 = 1;
     alfa1  = #4#1; numero = alfa1;
                    serie  = #4#2;
 |SINO;
     alfa1  = #4#2; numero = alfa1;
                    serie  = #4#1;
 |FINSI;

 |PINTA 2241, "            ";
 |PINTA 2241, #4#4;
 |PINTA 2244, #4#5;
 |PINTA 2251, #4#6;

 |SI numero < #0#13; |ACABA; |FINSI;

 |SI sw = 0;
     antser = serie;
     antnume = numero;
     primero = numero;
     |SI primero > #0#13;
         impnum = primero;
         texterr = impnum + " ES EL PRIMER NUMERO DE FACTURA ";
         |PRINT;
         swalgun = 1;
     |FINSI;
     sw = 1;
 |FINSI;

 |SI antser ! serie;
     |SI ultimo < #0#14;
         impnum = ultimo;
         texterr = impnum + " ES EL ULTIMO NUMERO DE FACTURA ";
         |PRINT;
         swalgun = 1;
     |FINSI;
     antser = serie;
     antnume = numero;
     primero = numero;
     |SI primero > #0#13;
         impnum = primero;
         texterr = impnum + " ES EL PRIMER NUMERO DE FACTURA ";
         |PRINT;
         swalgun = 1;
     |FINSI;
 |FINSI;

 ultimo = numero;
 |SI ultimo > #0#14; |ACABA; |FINSI;

 nume1 = numero - antnume;
 |SI nume1 > 1;
     alfa1 = antnume;
     alfa2 = numero;
     impnum = "";
     texterr = "SALTA DEL NUMERO " + alfa1 + " AL NUMERO " + alfa2;
     |PRINT;
     swalgun = 1;
 |FINSI;
 antnume = numero;
|FPRC;

|DEFBUCLE comsaltos;
 #4#1;
 ;
 "            ", blancos, blancos;
 "zzzzzzzzzzzz", cetas,   cetas;
 e;
 NULL, saltos;
|FIN;

|| ************************************************************************
|| ****************** programa por tipo 3 *********************************
|| ************************************************************************
|PROCESO chequea; |TIPO 3;

  |IMPRE 0;
  |SI FSalida ! 0;  |ACABA;  |FINSI;

  |SI #0#11 ! "S";
      informe = "cachedup";  || Duplicados
  |SINO;
      informe = "cachesal";  || Saltos
  |FINSI;

  |INFOR informe;
  |SI FSalida ! 0;  |FINIMP;  |ACABA;  |FINSI;

  tipo = #0#0;
  |SI tipo = "R"; descrip = "REPERCUTIDO"; |FINSI;
  |SI tipo = "S"; descrip = "SOPORTADO  "; |FINSI;

  |HAZ nom_usu;
  alfa1 = "b" + nom_tmp;
  |NOME_DAT #4 alfa1;
  |ABRE #4;
  |CIERRA #4;
  |DELINDEX #4;

  |ABRE #4;
  |ATRI P;  |PINTA 2241, "<TEMPORAL>";  |ATRI 0;

  swalgun = 0;
  |SI tipo = "R";  |BUCLE tmp_reper;  |FINSI;
  |SI tipo = "S";  |BUCLE tmp_sopor;  |FINSI;

  |CIERRA #4;

  |SI #0#11 = "S";   || Para Controlar Saltos
      sw = 0;
      blancos = " " * 30;
      cetas   = "z" * 30;
      |BUCLE comsaltos;
      |SI sw = 1; |Y ultimo < #0#14;
          impnum = ultimo;
          texterr = impnum + " ES EL ULTIMO NUMERO DE FACTURA ";
          |PRINT;
          swalgun = 1;
      |FINSI;
  |FINSI;

  |SI swalgun = 1;
      |PIEINF;
  |FINSI;
  |FININF;
  |FINIMP;

  |DELINDEX #4;
|FPRC;

|PROCESO montacamcla;
 espacios = " " * 30;

|| ................................... Campo 1
 nume1 = (#0#5 * 100) + #0#6;
 alfa1 = concepto % nume1;
 ccc = alfa1; |HAZ quitalgo; alfa1 = ccc;
 nume2 = alfa1;
 alfa1 = nume2; |QBF alfa1;

 ceros = "0" * #0#6;
 nume1 = 100 + #0#6; nume1 = - nume1;
 alfa1 = (ceros + alfa1) % nume1;
 alfa1 = (alfa1 + espacios) % 130;

|| ................................... Campo 2
 |SI #0#7 = 0;
     alfa2 = espacios;
 |SINO;
     nume1 = (#0#7 * 100) + #0#8;
     alfa2 = concepto % nume1;
     |SI #0#9 = "N";
         ccc = alfa2; |HAZ quitalgo; alfa2 = ccc;
         nume2 = alfa2;
         alfa2 = nume2;
     |FINSI;
     |QBF alfa2;

     ceros   = "0" * #0#8;
     blancos = " " * #0#8;
     nume1   = 100 + #0#8;
     |SI #0#9 = "A";
         alfa2 = (alfa2 + blancos) % nume1;
     |SINO;
         nume1 = -nume1;
         alfa2 = (ceros + alfa2) % nume1;
     |FINSI;
     alfa2 = (alfa2 + espacios) % 130;
 |FINSI;

 |SI #0#10 = 1;
     camcla1 = alfa1;
     camcla2 = alfa2;
 |SINO;
     camcla1 = alfa2;
     camcla2 = alfa1;
 |FINSI;
|FPRC;

|PROCESO quitalgo;
 |QBT ccc;
 c_alfa1 = ccc % 0;
 c_nume1 = c_alfa1;
 c_alfa1 = "";
 c_alfa2 = "";
 |PARA c_nume2 = 1; |SI c_nume2 [ c_nume1; |HACIENDO c_nume2 = c_nume2 + 1;
       c_nume3 = c_nume2 * 100 + 1;
       c_alfa2 = ccc % c_nume3;
       |SI c_alfa2 ] "0"; |Y c_alfa2 [ "9";
           c_alfa1 = c_alfa1 + c_alfa2;
       |FINSI;
 |SIGUE;
 ccc = c_alfa1;
|FPRC;
