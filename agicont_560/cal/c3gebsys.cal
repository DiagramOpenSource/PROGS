|FICHEROS;
    c3gebsys #0;       || Informe suma y saldos Otras Empresas
    cacuenta #1;       || Plan contable
    canempre #30;      || Empresas contables
    caconimp #8;
|FIN;

|VARIABLES;
  &pathbal = "";
  &balance = 1;
    &entrada = 1;
    potra = 0;
    vermov = 0;
    x = 0;
    y = 0;
    xx = 0;
    yy = 0;
    xy = 0;
    d_mes_f = 0;
    h_mes_f = 0;
    men1 = "     NO EXISTE EMPRESA";
    men9 = "     Empresa Contable INACTIVA";
    informe = "";
    mes = 0;                  || Variable para saber el mes seleccionado
    letrames = "";            || Variable para poner el mes en letra
    varcamp = 0;              || Variable para posicionarse en un campo
    xxxxfiac = "  ";          || Variables para no modificable
    xxaponer = "  ";
    xrelleno = "                              ";
    error1 = "0000Nivel repetido";
    error2 = "0000No existe informe";
    error3 = "0000El numero de nivel no coincide con el numero de nivel de la empresa";
    swprint = 0;       || Variable para saber si imprimimos o no
    swsuma = 0;        || Variable para saber si sumamos o no
    nivel = 0;         || Variable que posee el nivel mas alto
    saldo = 0;         || Resta entre el debe y el haber;
    sw = 0;            || sw para nivel anterior
    swalgun = 0;
    nivelact = 0;      || Nivel actual
    rellenos = "";     || Variable para rellenar el hasta de la cuenta en zetas
    &i_debe = 0;       || Total del debe por registro
    &i_haber = 0;      || Total del haber por registro
    &i_acumdebe = 0;   || Total acumulado debe por registro
    &i_acumhaber = 0;  || Total acumulado haber por registro
    &i_deudor = 0;     || Total saldo deudor por registro
    &i_acreedor = 0;   || Total saldo acreedor por registro
    &t_debe = 0;       || Total del debe del informe
    &t_haber = 0;      || Total del haber del informe;
    &t_acumdebe = 0;   || Total del acumulado debe del informe
    &t_acumhaber = 0;  || Total del acumulado haber del informe
    &t_deudor = 0;     || Total saldo deudor por informe
    &t_acreedor = 0;   || Total saldo acreedor por informe
    &swlinea = 0;      || Pone o no linea en blanco en informe
    &swcomen = 0;
    men2  = "     Longitud de Cuenta Fuera de Nivel ";
    long = "  ";
    cta  = 0;
    num   = 0;
    swdig = 0;
    digito = 0;
    menor = 0;
    para1 = 0;
    nume1 = 0;
    sw_pagina = 0;
    &pagina = 0;
    alfa1 = "";
    alfa2 = "";
    sw_error = 0;
    dirfich0 = "";
    leidos = 0;
    desde = 0;
    hasta = 0;
    traba= "";
    emp1 = 0;
    per1 = 0;
    &r_emp = 0;
    &r_per = 0;
    anu = 0;
    empcon = 0;
    ejecon = 0;
|FIN;

|INCLUYE acceso_i;
|INCLUYE digito_i;
|INCLUYE puntos_i;


|| ***********************************************************************
||                       CALCULOS DE PANTALLA
|| ***********************************************************************


|PROCESO coloca; |TIPO 8;
   |DBASE dir_fich;
   dir_fich = dir_fich + "fich/";
|FPRC;

|PROCESO &c_pagina;
|SI sw_pagina = 0 ;
        pagina = 1;
        sw_pagina = 1;
|SINO;
        pagina = pagina + 1;
|FINSI;
|FPRC;

|PROCESO consulta;
  |SI FSalida ! 9;  |ACABA;  |FINSI;
  |SUB_EJECUTA "caconemp.run";
  |ERROR;
  |SI Campo = 23;
      #0#23 = r_emp;
      #0#25 = r_per;
      |PTEC 802;
      |PTEC 802;
  |FINSI;
  |SI Campo = 24;
      #0#24 = r_emp;
      #0#26 = r_per;
      |PTEC 802;
  |FINSI;
  |SI Campo ] 27;
      #0Campo = r_emp;
      varcamp = Campo + 20;
      #0varcamp = r_per;
      |PTEC 802;
      |PTEC 802;
  |FINSI;
|FPRC;

|PROCESO mayor;
  alfa1 = #0Campo > " ";
  #0Campo = alfa1;
  |PINTA #0Campo;
|FPRC;

|PROCESO mayor1;
  |HAZ mayor;
  |SI #0Campo = "N";
      |PARA x = 27;  |SI x [ 66;  |HACIENDO x = x + 1;
            |CAMPO_MODIFICA #0x,1,"S";
      |SIGUE;
      #0#23 = 0;
      #0#25 = 0;
      #0#24 = 0;
      #0#26 = 0;
      |CAMPO_MODIFICA #0#23,1,"N";
      |CAMPO_MODIFICA #0#25,1,"N";
      |CAMPO_MODIFICA #0#24,1,"N";
      |CAMPO_MODIFICA #0#26,1,"N";
      |PINTA #0#23;
      |PINTA #0#25;
      |PINTA #0#24;
      |PINTA #0#26;
  |SINO;
      |PARA x = 27;  |SI x [ 66;  |HACIENDO x = x + 1;
            #0x = 0;
            |CAMPO_MODIFICA #0x,1,"N";
      |SIGUE;
      |CAMPO_MODIFICA #0#23,1,"S";
      |CAMPO_MODIFICA #0#25,1,"S";
      |CAMPO_MODIFICA #0#24,1,"S";
      |CAMPO_MODIFICA #0#26,1,"S";
  |FINSI;
|FPRC;

|PROCESO mesconta; |TIPO 0;
  varcamp = Campo + 1;                || Coge el campo del nombre del mes
  mes = #0Campo;
|SI #0Campo ! 0;|Y #0Campo ! 13;
        mes = #30#11 + (#0Campo - 1);     || Operacion para saber que mes es
    |SI mes > 12; mes = mes - 12; |FINSI;
|FINSI;
  |HAZ mesletra;                      || Recoge el mes en letra
  #0varcamp = letrames;
  potra = Campo;
  |PINTA #0varcamp;                   || Pinta el nombre del mes
  Campo = potra;

  |SI Campo = 4;
      |SI #0#2 > #0#4;
          |ERROR;
          |ACABA;     || Si el mes hasta es menor al desde
      |FINSI;
  |FINSI;
|FPRC;

|PROCESO mesletra;
  |SI mes = 0;  letrames = "Apertura  "; |FINSI;
  |SI mes = 1;  letrames = "Enero     "; |FINSI;
  |SI mes = 2;  letrames = "Febrero   "; |FINSI;
  |SI mes = 3;  letrames = "Marzo     "; |FINSI;
  |SI mes = 4;  letrames = "Abril     "; |FINSI;
  |SI mes = 5;  letrames = "Mayo      "; |FINSI;
  |SI mes = 6;  letrames = "Junio     "; |FINSI;
  |SI mes = 7;  letrames = "Julio     "; |FINSI;
  |SI mes = 8;  letrames = "Agosto    "; |FINSI;
  |SI mes = 9;  letrames = "Septiembre"; |FINSI;
  |SI mes = 10; letrames = "Octubre   "; |FINSI;
  |SI mes = 11; letrames = "Noviembre "; |FINSI;
  |SI mes = 12; letrames = "Diciembre "; |FINSI;
  |SI mes = 13; letrames = "Cierre    "; |FINSI;
|FPRC;

|PROCESO numnivel;|TIPO 0;
  |SI #0#8 > #30#13;
      |MENAV error3;
      |ERROR;
      |ACABA;
  |FINSI;
|FPRC;

|PROCESO nivel;|TIPO 7;  || Calculo para hacer no modificable
  |SI Campo = 10; |Y #0#8 < 2; || Si solo hemos seleccionado
      #0Campo = 0;
      |PINTA #0Campo;
      |HAZ nomodifi;              || un solo nivel;
  |FINSI;
  |SI Campo = 11; |Y #0#8 < 3; || Si solo hemos seleccionado
      #0Campo = 0;
      |PINTA #0Campo;
      |HAZ nomodifi;              || dos niveles;
  |FINSI;
  |SI Campo = 12; |Y #0#8 < 4; || Si solo hemos seleccionado
      #0Campo = 0;
      |PINTA #0Campo;
      |HAZ nomodifi;              || tres niveles;
  |FINSI;
  |SI Campo = 13; |Y #0#8 < 5; || Si solo hemos seleccionado
      #0Campo = 0;
      |PINTA #0Campo;
      |HAZ nomodifi;              || cuatro niveles;
  |FINSI;
  |SI Campo = 14; |Y #0#8 < 6; || Si solo hemos seleccionado
      #0Campo = 0;
      |PINTA #0Campo;
      |HAZ nomodifi;              || cinco niveles;
  |FINSI;
|FPRC;

|PROCESO nomodifi;             || RUTINA DE CONVERSION A NO MODIFICABLES
                               || VARIABLES: A_
    xxaponer = Anono;
    xxxxfiac = Control % A1001;
    Control = Control + xxxxfiac;
    Control = Control + xrelleno;
    Control = Control % A120;
    Control = Control + xxaponer;
|FPRC;

|PROCESO nivelrep;|TIPO 0;  || Para comprobar que no repetimos los niveles

  |SI nivel = 0;        || Buscamo el nivel mas alto;
      nivel = #0#9;
  |FINSI;

  |SI Campo = 10;
      |SI #0#10 = #0#9;|Y #0#10 ! 0;    || Si hay dos niveles iguales error
          |MENAV error1;
          |ERROR;  |ACABA;
      |SINO;
          |SI #0#10 > #0#9;  || Busca el nivel mas alto
              nivel = #0#10;
          |SINO;
              nivel = #0#9;
          |FINSI;
      |FINSI;
  |FINSI;

  |SI Campo = 11; |Y #0#11 ! 0;
      |SI #0#11 = #0#9; |O #0#11 = #0#10; || Si hay dos niveles iguales error
          |MENAV error1;
          |ERROR;  |ACABA;
      |FINSI;
      |SI #0#11 > nivel;  || Busca el nivel mas alto
          nivel = #0#11;
      |FINSI;
  |FINSI;

  |SI Campo = 12; |Y #0#12 ! 0;      || Si hay dos niveles iguales error
      |SI #0#12 = #0#9; |O #0#12 = #0#10; |O #0#12 = #0#11;
          |MENAV error1;
          |ERROR; |ACABA;
      |FINSI;
      |SI #0#12 > nivel;  || Busca el nivel mas alto
          nivel = #0#12;
      |FINSI;
  |FINSI;

  |SI Campo = 13; |Y #0#13 ! 0;     || Si hay dos niveles iguales error
      |SI #0#13 = #0#9; |O #0#13 = #0#10; |O #0#13 = #0#11; |O #0#13 = #0#12;
          |MENAV error1;
          |ERROR; |ACABA;
      |FINSI;
      |SI #0#13 > nivel;  || Busca el nivel mas alto
          nivel = #0#13;
      |FINSI;
  |FINSI;

  |SI Campo = 14; |Y #0#14 ! 0;    || Si hay dos niveles iguales error
      |SI #0#14 = #0#9; |O #0#14 = #0#10; |O #0#14 = #0#11; |O #0#14 = #0#12; |O #0#14 = #0#13;
          |MENAV error1;
          |ERROR;  |ACABA;
      |FINSI;
      |SI #0#14 > nivel;  || Busca el nivel mas alto
          nivel = #0#14;
      |FINSI;
  |FINSI;
|FPRC;

|| --------------- comprueba desde-hasta empresas ---------------------------

|PROCESO buscaem1;
  |SI #30#21 = "I"; |ACABA; |FINSI;

  leidos = 1;
  |HAZ caldigitos;
  |SI sw_error = 1;
      alfa1 = #30#2;
      alfa2 = #30#3;
      traba = "      Empresa: " + alfa1 + " " + alfa2;
      |ERROR10;
      |ACABA;
  |FINSI;
  alfa1 = #30#2;
  alfa2 = #30#3;
|FPRC;

|DEFBUCLE buscaempre;
#30#1;
;
#0#23,desde;
#0#24,hasta;
e;
NULL,buscaem1;
|FIN;

|PROCESO buclempre;
  |SI #0#25 > #0#26;
      |MENAV "     ERROR EN INTRODUCCION";
      |ERROR;
      |ACABA;
  |FINSI;
  |SI #0#23 = 0;  |Y #0#24 = 0;  |ACABA;  |FINSI;
  desde = #0#25;
  hasta = #0#26;
|ET segundo;
  sw_error = 0;
  leidos = 0;
  |BUCLE buscaempre;
  |SI leidos = 0;
      |MENAV "    ATENCION SELECCION VACIA";
      |ERROR;
      |ACABA;
  |FINSI;
  |SI sw_error = 1;
      sw_error = 0;
      traba = traba + " NO SE CORRESPONDEN LOS NIVELES:";
      |MENAV traba;
      |ERROR;
      |ACABA;
  |FINSI;
|FPRC;

|PROCESO deshas;
  |SI #0#24 < #0#23;
      |MENAV "      Error en Introduccion";
      |ERROR;
  |FINSI;
|FPRC;

|| ------------------------------------------------------------------------

|PROCESO nomodi;
      |PARA x = Campo + 1;  |SI x [ 46;  |HACIENDO x = x + 1;
            #0x = 0;
            |CAMPO_MODIFICA #0x,1,"N";
            y = x + 20;
            #0y = 0;
            |CAMPO_MODIFICA #0y,1,"N";
      |SIGUE;
      y = Campo + 20;
      #0y = 0;
      |CAMPO_MODIFICA #0y,1,"N";
      |PINTA #0y;
|FPRC;


|PROCESO esbuena; || desde campo de empresas seleccion
  |SI FSalida = 2; |ACABA; |FINSI;
  |SI #0Campo = 0; |HAZ nomodi;  |ACABA;  |FINSI;
  |ABRE #30;
  #30#2 = #0Campo;
  #30#3 = 0;
  |LEE 011#30];
  |SI FSalida ! 0;  |O #30#2 ! #0Campo;
      |MENAV "     NO Existe la empresa Contable ";
      |ERROR;
  |FINSI;
  |CIERRA #30;
|FPRC;


|PROCESO caldigitos;
  |SI #30#13 < #0#08;  sw_error = 1;  |ACABA;  |FINSI;
  |SI #30#13 < #0#09;  sw_error = 1;  |ACABA;  |FINSI;
  |SI #30#13 < #0#10;  sw_error = 1;  |ACABA;  |FINSI;
  |SI #30#13 < #0#11;  sw_error = 1;  |ACABA;  |FINSI;
  |SI #30#13 < #0#12;  sw_error = 1;  |ACABA;  |FINSI;
  |SI #30#13 < #0#13;  sw_error = 1;  |ACABA;  |FINSI;
  |SI #30#13 < #0#14;  sw_error = 1;  |ACABA;  |FINSI;
|FPRC;

|PROCESO esbuena1;
  |SI FSalida = 2;|ACABA;|FINSI;
  x = Campo - 20;
  |SI #0x = 0;  |ACABA;  |FINSI;
  |ABRE #30;
  x = Campo - 20;
  #30#2 = #0x;
  #30#3 = #0Campo;
  |LEE 011#30=;
  |SI FSalida ! 0;
      |MENAV "     NO Existe Periodo en la empresa Contable";
      |CIERRA #30;
      |ERROR;
      |ACABA;
  |FINSI;
  |CIERRA #30;
  |SI #30#21 = "I";
      |MENAV men9;
      |ERROR;
      |ACABA;
  |FINSI;
  sw_error = 0;
  |HAZ caldigitos;
  |SI sw_error = 1;
      sw_error = 0;
      |MENAV "      NO SE CORRESPONDEN LOS NIVELES:";
      |ERROR;
      |ACABA;
  |FINSI;
  x = Campo + 20;
  #0x = #30#1;
  |PINTA #0x;
  sw_error = 0;
  x = Campo - 20;
  alfa1 = #0x;
  alfa2 = #0Campo;
|FPRC;

|PROCESO cuenta; |TIPO 6;
    p_alfa1 = #0Campo;
    salidas = FSalida;
|HAZ punto;
|SI p_error ! 0;  |ERROR;  |ACABA;  |FINSI;
|SI #0Campo = "            ";  |ERROR;  |ACABA;  |FINSI;
    digito = Campo + 20;
    cta  = #0Campo;
    long = cta % 0;
    swdig = 0;
|SI long = #30#14; swdig = 1; #0digito = 1; |FINSI;
|SI long = #30#15; swdig = 1; #0digito = 2; |FINSI;
|SI long = #30#16; swdig = 1; #0digito = 3; |FINSI;
|SI long = #30#17; swdig = 1; #0digito = 4; |FINSI;
|SI long = #30#18; swdig = 1; #0digito = 5; |FINSI;
|SI long = #30#19; swdig = 1; #0digito = 6; |FINSI;
|SI swdig = 0;
    |MENAV men2;
    |ERROR;
|FINSI;
|FPRC;


|| **********************************************************************
||                       CALCULOS DE IMPRESION
|| **********************************************************************

|| ---------- Seleccion DESDE / HASTA Empresa.
|PROCESO esdesd1;
  |SI #30#21 = "I"; |ACABA; |FINSI;
  |DBASE dir_fich;
  dirfich0 = dir_fich + "fich/";
  alfa1 = #30#2;
  alfa2 = #30#3;
  emp1 = #30#2;
  per1 = #30#3;
  alfa1 = "00000" + alfa1;   alfa1 = alfa1 % -105;
  alfa2 = "0"   + alfa2;   alfa2 = alfa2 % -101;
  dirfich0 = dirfich0 + alfa1 + alfa2 + "/";
  |PATH_DAT #8 dirfich0;
  |ABRE #8;
  |LEE 111#8p;
  |SI #8#2 = "S";  |Y #8#3 = "N";
      pathbal = dirfich0;
      balance = 1;
      |CIERRA #8;
      |SUB_EJECUTA "careccue.tab";
  |FINSI;
  |CIERRA #8;
  |HAZ haztodo;
|FPRC;

|DEFBUCLE esdesde;
#30#1;
;
#0#23,#0#25;
#0#24,#0#26;
e;
NULL,esdesd1;
|FIN;

|| ===== calculos propios del balance de sumas y saldos =================

|PROCESO condicion;    || Calculo condiciones de impresion
|HAZ digitos2;
|SI d_error ! 0;  |ACABA;  |FINSI;

  vermov = 0;
  swprint = 0;           || Variable para saber si imprimimos o no
  swsuma = 0;            ||Variable para saber si sumamos o no
  i_debe = 0;            || Pone a cero el total debe del registro
  i_haber = 0;           || Pone a cero el total haber del registro
  i_acumdebe = 0;        || Pone a cero el total acumulado debe del registro
  i_acumhaber = 0;       || Pone a cero el total acumulado haber del registro

  |SI sw = 0;
      nivelact = #1#55;  || Nivel actual
      sw = 1;
  |FINSI;

  |SI #1#55 ! 0;     || Condicion para saber si el nivel esta seleccionado
      |SI #1#55 = #0#9;|O #1#55 = #0#10;|O #1#55 = #0#11; || Compara 3 niveles
          swprint = 1;
      |SINO;
          |SI #1#55 = #0#12;|O #1#55 = #0#13;|O #1#55 = #0#14; || Compara 3 niveles
              swprint = 1;
          |SINO;
              swprint = 0;
          |FINSI;
     |FINSI;
  |FINSI;

  |SI swprint = 1; |Y #1#55 = nivel;  || Si es el nivel mas alto entoces
      swsuma = 1;                    || se suma.
  |FINSI;

  |SI #1#3 = "S"; |Y #1#55 [ nivel;   || Si el nivel no esta seleccionado
       swsuma = 1;                    || pero tiene en el campo pase una S
       swprint = 1;                   || entonces se imprime y se suma.
  |FINSI;

  |SI swprint = 1;    || Si se puede imprimir
      |SI #0#2 = 0;  varcamp =  9; |FINSI;    || Se situa en el campo 0  debe
      |SI #0#2 = 1;  varcamp = 12; |FINSI;    || Se situa en el campo 1  debe
      |SI #0#2 = 2;  varcamp = 15; |FINSI;    || Se situa en el campo 2  debe
      |SI #0#2 = 3;  varcamp = 18; |FINSI;    || Se situa en el campo 3  debe
      |SI #0#2 = 4;  varcamp = 21; |FINSI;    || Se situa en el campo 4  debe
      |SI #0#2 = 5;  varcamp = 24; |FINSI;    || Se situa en el campo 5  debe
      |SI #0#2 = 6;  varcamp = 27; |FINSI;    || Se situa en el campo 6  debe
      |SI #0#2 = 7;  varcamp = 30; |FINSI;    || Se situa en el campo 7  debe
      |SI #0#2 = 8;  varcamp = 33; |FINSI;    || Se situa en el campo 8  debe
      |SI #0#2 = 9;  varcamp = 36; |FINSI;    || Se situa en el campo 9  debe
      |SI #0#2 = 10; varcamp = 39; |FINSI;    || Se situa en el campo 10 debe
      |SI #0#2 = 11; varcamp = 42; |FINSI;    || Se situa en el campo 11 debe
      |SI #0#2 = 12; varcamp = 45; |FINSI;    || Se situa en el campo 12 debe
      |SI #0#2 = 13; varcamp = 48; |FINSI;    || Se situa en el campo 13 debe


   || ************ Bucle del debe y haber periodo *************

     |PARA x = #0#2; |SI x [ #0#4; |HACIENDO x = x + 1;
           i_debe = i_debe + #1varcamp;    || Suma el debe
           |SI #1varcamp ! 0; vermov = 1; |FINSI;
           varcamp = varcamp + 1;          || Se situa en  el campo del haber
           i_haber = i_haber + #1varcamp;  || Suma el haber
           |SI #1varcamp ! 0; vermov = 1; |FINSI;
           varcamp = varcamp + 2;          || Se situa en el campo debe
     |SIGUE;


   || ************ Bucle del debe y haber acumulado *************

     varcamp = 9;
     |PARA x = 0; |SI x [ #0#4; |HACIENDO x = x + 1;  || Bucle de campos
           i_acumdebe = i_acumdebe + #1varcamp;   || Suma el acumulado debe
           varcamp = varcamp + 1;                 || Se situa en  el campo del haber
           i_acumhaber = i_acumhaber + #1varcamp; || Suma el acumulado haber
           varcamp = varcamp + 2;          || Se situa en el campo debe
     |SIGUE;

     saldo = i_acumdebe - i_acumhaber;       || Coge la diferencia
     |SI saldo > 0; i_deudor = saldo; i_acreedor = 0; |FINSI;
     |SI saldo < 0; i_deudor = 0; i_acreedor = saldo * (-1); |FINSI;
     |SI saldo = 0; i_deudor = 0; i_acreedor = 0; |FINSI;

     |SI swsuma = 1;    || Si se puede sumar los totales
         t_debe      = t_debe      + i_debe;        || Suma el total del debe
         t_haber     = t_haber     + i_haber;       || Suma el total del haber
         t_acumdebe  = t_acumdebe  + i_acumdebe;    || Suma el total acumulado debe
         t_acumhaber = t_acumhaber + i_acumhaber;   || Suma el total acumulado haber
         t_deudor    = t_deudor    + i_deudor;      || Suma total saldo deudor
         t_acreedor  = t_acreedor  + i_acreedor;    || Suma total saldo acreedor
     |FINSI;

     |SI i_acumdebe = 0; |Y i_acumhaber = 0; |Y vermov = 0; |Y #0#6 = "N";
         |ACABA;
     |FINSI;

     |SI nivelact ! #1#55;    || Si el nivel actual es distinto al
         swlinea = 1;         || anterior pone una linea en blanco
         nivelact = #1#55;    || Actualiza el nivel
     |SINO;
         swlinea = 0;          || Si no no pone linea en blanco
     |FINSI;
     |PRINT;
     swcomen = 1;        || Para imprimir el saldo anterior
     swalgun = 1;
 |FINSI;
|FPRC;

|DEFBUCLE caybalsy0;     || Bucle del plan contable
 #1#3;                   || Lee por la tercera clave
 ;
 #0#0,     1;
 rellenos, 6;
 e;
 NULL, condicion;
|FIN;

|DEFBUCLE caybalsy1;     || Bucle del plan contable
 #1#1;                   || Lee por la primera clave
 ;
 #0#0, 1;
 #0#1, 6;
 e;
 NULL,condicion;
|FIN;

|| -------------------------------------------------------------------------

|PROCESO rellena;
    rellenos = "zzzzzzzzzzzz";
    menor = -1;

|PARA para1 = 9; |SI para1 [ 14; |HACIENDO para1 = para1 + 1;
    |SI #0para1 ! 0;
        |SI menor = -1;  menor = #0para1;  |FINSI;
        |SI menor > #0para1;  menor = #0para1;  |FINSI;
    |FINSI;                   || saca el nivel elegido mas peque¤o
|SIGUE;

|SI menor ! 0;
    |SI menor = 1;  nume1 = dig4;  |FINSI;
    |SI menor = 2;  nume1 = dig5;  |FINSI;   || construye el hasta segun
    |SI menor = 3;  nume1 = dig6;  |FINSI;   ||/el nivel mas bajo elegido
    |SI menor = 4;  nume1 = dig7;  |FINSI;
    |SI menor = 5;  nume1 = dig8;  |FINSI;
    |SI menor = 6;  nume1 = dig9;  |FINSI;
        nume1 = 100 + nume1;
        rellenos = #0#1 % nume1;
        rellenos = rellenos + "zzzzzzzzzzzz";
        rellenos = rellenos % 112;
|FINSI;
|FPRC;


|| *********************************************************************
||                     COMIENZO con TIPO 3
|| *********************************************************************

|PROCESO impre; |TIPO 3;      || Impresion por impresora;
   |HAZ rellena;
   |IMPRE 0;                          || Abrimos impresora
   |SI FSalida ! 0; |ACABA; |FINSI;
   |HAZ los_informes;
   informe = que_i1;
   |SI #0#22 = "S";
       |BUCLE esdesde;
   |SINO;
       |HAZ esunaauna;
   |FINSI;
   |FINIMP;
|FPRC;

|PROCESO esunaauna;
      |PARA xx = 27;  |SI xx [ 46;  |HACIENDO xx = xx + 1;
            |SI #0xx = 0;  |SAL;  |FINSI;
            |DBASE dir_fich;
            dirfich0 = dir_fich + "fich/";
            yy = xx + 20;
            emp1 = #0xx;
            per1 = #0yy;
            alfa1 = #0xx;
            alfa2 = #0yy;
            alfa1 = "00000" + alfa1;   alfa1 = alfa1 % -105;
            alfa2 = "0"   + alfa2;   alfa2 = alfa2 % -101;
            dirfich0 = dirfich0 + alfa1 + alfa2 + "/";
              || lee empresa
                 |ABRE #30;
                   #30#2 = emp1;
                   #30#3 = per1;
                 |LEE 011#30=;
                 |CIERRA #30;
            |PATH_DAT #8 dirfich0;
            |ABRE #8;
            |LEE 111#8p;
            |SI #8#2 = "S";  |Y #8#3 = "N";
                pathbal = dirfich0;
                balance = 1;
                |CIERRA #8;
                |SUB_EJECUTA "careccue.tab";
            |FINSI;
            |CIERRA #8;
            |HAZ haztodo;
      |SIGUE;
|FPRC;

|PROCESO borrar;
    || nivel = 0;
    sw = 0;
    swcomen = 0;
    i_debe = 0;
    i_haber = 0;
    i_acumdebe = 0;
    i_acumhaber = 0;
    i_deudor = 0;
    i_acreedor = 0;
    t_debe = 0;
    t_haber = 0;
    t_acumdebe = 0;
    t_acumhaber = 0;
    t_deudor = 0;
    t_acreedor = 0;
    t_debe = 0;
    t_haber = 0;
    t_deudor = 0;
    t_acreedor = 0;
|FPRC;

|PROCESO haztodo;
 enEmpresa = #canempre#2;
 enPeriodo = #canempre#3;
 swOperacion = 0;
 |SUB_EJECUTA "camoneda";

  |PATH_DAT #1 dirfich0;
  anu = 1900 + #30#26;
  |SI anu [ 1970;  anu = anu + 100;  |FINSI;
  empcon = #30#1;
  ejecon = #30#26;
|| .... Aqu¡ seleccion de Balances por mes
    d_mes_f = #0#2;
    h_mes_f = #0#4;

|PARA xy = d_mes_f; |SI xy [ h_mes_f; |HACIENDO xy = xy + 1;
    |SI #0#7 = "S";
        #0#2 = xy; mes = #0#2; |HAZ mesletra; #0#3 = letrames;
        #0#4 = xy; mes = #0#4; |HAZ mesletra; #0#5 = letrames;
    |FINSI;
    |INFOR informe;                || Abrimos informe
    |SI FSalida ! 0; |FINIMP; |ACABA; |FINSI;
    |HAZ borrar;
    sw = 0;
    swalgun = 0;
    |SI #0#16 = "D";
        |BUCLE caybalsy0;
    |SINO;
        |BUCLE caybalsy1;
    |FINSI;
    |SI swalgun = 1; |PIEINF; |FINSI;
    |FININF;
    |SI #0#7 = "N"; |SAL; |FINSI;
|SIGUE;
    |SI #0#7 = "S";
        #0#2 = d_mes_f; mes = #0#2; |HAZ mesletra; #0#3 = letrames;
        #0#4 = h_mes_f; mes = #0#4; |HAZ mesletra; #0#5 = letrames;
    |FINSI;
    pagina = 0;
    sw_pagina = 0;
|FPRC;
