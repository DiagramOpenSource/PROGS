|FICHEROS;
    caba1pag #0;
    capagcab #1;
    camanban #2;
    cacuenta #3;
    caba1pa1 #4;
    cacuadre #9;
|FIN;

|VARIABLES;
    &entrada = 1;
    informe = "caba1pag";
    banco = "";
    dbanc = 0;
    sw = 0;
 {-1}elegir = "";
    elegir1 = "2400";
    elegir2 = "1";
    elegir3 = " Elija opcion: ";
    elegir4 = "IP";
    elegir5 = " Impresora ";
    elegir6 = " Pantalla ";
    posic = 0;
    mas = 0;
    lin_pan = 0;
    alfa1 = "";
    &vto1 = @;
    &vto2 = @;
    &vto3 = @;
    &vto4 = @;
    &imp1 = 0;
    &imp2 = 0;
    &imp3 = 0;
    &imp4 = 0;
    &imp5 = 0;
    &tot1 = 0;
    &tot2 = 0;
    &tot3 = 0;
    &tot4 = 0;
    &tot5 = 0;
    &nomb = "";
    &ctab = "";
    cc = 0;
    tbanco = 0;
    vto = @;
    swfin = 0;
    sw_act = 0;
|FIN;

|INCLUYE acceso_i;

|| ************************* calculos pantalla ******************************
|PROCESO mayor;
   alfa1 = #0Campo > " ";
   #0Campo = alfa1;
   |PINTA #0Campo;
|FPRC;

|PROCESO fechas;
   cc = Campo - 1;
   |SI #0Campo < #0cc;
       |MENAV " Error en la introduccion ";
       |ERROR;
       |ACABA;
   |FINSI;
|FPRC;

|PROCESO cuadre; |TIPO 8;
  sw_act = 0;    ||  0 = N control (Carta/Pagare) junto / 1 = S por separado
  |ABRE #9;
  |DDEFECTO #9;
  #9#0 = 1;
  |LEE 001#9=;
  |SI FSalida = 0;
      |SI #9#17 = "S";
          sw_act = 1;
      |FINSI;
  |FINSI;
  |CIERRA #9;
  |SI sw_act = 0;
      |CAMPO_MODIFICA #0#10, 1, "N";
  |FINSI;
|FPRC;

|PROCESO juntos; |TIPO 0;
 |SI sw_act = 0;
     #0#10 = #0Campo;
     |PINTA #0#10;
 |FINSI;
|FPRC;

|| *************************************************************************

|PROCESO detalles;       || lee el banco y la cuenta
    ctab = banco;
    nomb = "";
    imp1 = 0;
    imp2 = 0;
    imp3 = 0;
    imp4 = 0;
    imp5 = 0;

    #2#10 = ctab;
    #2#11 = dbanc;
|LEE 001#2=;
|SI FSalida = 0;
      nomb = #2#1;
      |ACABA;
      |FINSI;

    #3#00 = ctab;
    #3#01 = dbanc;
|LEE 001#3=;
|SI FSalida ! 0;  |DDEFECTO #3;  |FINSI;
    nomb = #3#2;
|FPRC;

|PROCESO impre1;
  |SI #0#10 ! "A";
      |SI #0#10 ! #1#10;  |ACABA;  |FINSI;  || impresa carta
  |FINSI;
  |SI #0#17 ! "A";
      |SI #0#17 ! #1#12;  |ACABA;  |FINSI;  || impreso pagare
  |FINSI;
  |SI #0#11 ! "A";
      |SI #0#11 ! #1#07;  |ACABA;  |FINSI;  || emision
  |FINSI;
  |SI #0#12 ! "A";
      |SI #0#12 ! #1#08;  |ACABA;  |FINSI;  || vencimiento
  |FINSI;

|SI sw = 0;
        banco = #1#3;
        dbanc = #1#4;
        |HAZ detalles;
        sw = 1;
|FINSI;
|SI banco ! #1#3;
    |PRINT;                   || imprime total banco anterior
    tot1 = tot1 + imp1;
    tot2 = tot2 + imp2;
    tot3 = tot3 + imp3;
    tot4 = tot4 + imp4;
    tot5 = tot5 + imp5;
        banco = #1#3;
        dbanc = #1#4;
        |HAZ detalles;
|FINSI;

|| ============ Calculo del importe a columna correcta

|SI #1#6 [ vto1;
    imp1 = imp1 + #1#9;
    |VETE sumado;
|FINSI;

|SI #1#6 > vto1;|Y #1#6 [ vto2;
    imp2 = imp2 + #1#9;
    |VETE sumado;
|FINSI;

|SI #1#6 > vto2;|Y #1#6 [ vto3;
    imp3 = imp3 + #1#9;
    |VETE sumado;
|FINSI;

|SI #1#6 > vto3;|Y #1#6 [ vto4;
    imp4 = imp4 + #1#9;
    |VETE sumado;
|FINSI;

    imp5 = imp5 + #1#9;

|ET sumado;
|FPRC;

|DEFBUCLE impre;
 #1#2;
 1, 5; || ..... ordenado por banco+vto, selec. prov y emision
 #0#2, 0, #0#4, #0#0, #0#6, #0#8;
 #0#3, 9, #0#5, #0#1, #0#7, #0#9;
 e;
 NULL, impre1;
|FIN;

|PROCESO impresora;
|IMPRE 0;
|SI FSalida = 0;
    |INFOR informe;
    |ABRE #2;
    |SELECT #2#2;
    |ABRE #3;
    |BUCLE impre;
    |CIERRA #2;
    |CIERRA #3;
    |SI sw = 1;
        |PRINT;
        tot1 = tot1 + imp1;
        tot2 = tot2 + imp2;
        tot3 = tot3 + imp3;
        tot4 = tot4 + imp4;
        tot5 = tot5 + imp5;
        |PIEINF;
    |FINSI;
    |FININF;
|SINO;
    |MENAV "    Proceso Abortado ...";
|FINSI;
|FINIMP;
|FPRC;


|| ********************** PANTALLA

|PROCESO correte;
|CAMPO_POSICION #4#0,0,posic; posic=posic+mas; |CAMPO_POSICION #4#0,1,posic;
|CAMPO_POSICION #4#1,0,posic; posic=posic+mas; |CAMPO_POSICION #4#1,1,posic;
|CAMPO_POSICION #4#6,0,posic; posic=posic+mas; |CAMPO_POSICION #4#6,1,posic;
|CAMPO_POSICION #4#7,0,posic; posic=posic+mas; |CAMPO_POSICION #4#7,1,posic;
|CAMPO_POSICION #4#8,0,posic; posic=posic+mas; |CAMPO_POSICION #4#8,1,posic;
|CAMPO_POSICION #4#9,0,posic; posic=posic+mas; |CAMPO_POSICION #4#9,1,posic;
|CAMPO_POSICION #4#10,0,posic; posic=posic+mas; |CAMPO_POSICION #4#10,1,posic;
|FPRC;

|PROCESO pant4;
  |PINPA #0#4;
  |DDEFECTO #4;
      #4#2  = vto1;    || 1§ vto
      #4#3  = vto2;    || 2§ vto
      #4#4  = vto3;    || 3§ vto
      #4#5  = vto4;    || 4§ vto
  |PINTA #4#2;
  |PINTA #4#3;
  |PINTA #4#4;
  |PINTA #4#5;
|FPRC;

|PROCESO empieza;
  |HAZ pant4;
  mas = lin_pan * -100;
  lin_pan = 0;
  |HAZ correte;
|FPRC;


|PROCESO panta1;
  |SI #0#10 ! "A";
      |SI #0#10 ! #1#10;  |ACABA;  |FINSI;  || impresa
  |FINSI;
  |SI #0#17 ! "A";
      |SI #0#17 ! #1#12;  |ACABA;  |FINSI;  || impreso pagare
  |FINSI;
  |SI #0#11 ! "A";
      |SI #0#11 ! #1#07;  |ACABA;  |FINSI;  || emision
  |FINSI;
  |SI #0#12 ! "A";
      |SI #0#12 ! #1#08;  |ACABA;  |FINSI;  || vencimiento
  |FINSI;

|SI sw = 0;
        banco = #1#3;
        dbanc = #1#4;
        |HAZ detalles;
        sw = 1;
|FINSI;
|SI banco ! #1#3;
    tot1 = tot1 + imp1;
    tot2 = tot2 + imp2;
    tot3 = tot3 + imp3;
    tot4 = tot4 + imp4;
    tot5 = tot5 + imp5;
    |HAZ imppan;                   || imprime total banco anterior
        banco = #1#3;
        dbanc = #1#4;
        |HAZ detalles;
|FINSI;

|| ============ Calculo del importe a la linea correcta

|SI #1#6 [ vto1;
    imp1 = imp1 + #1#9;
    |VETE sumad1;
|FINSI;

|SI #1#6 > vto1;|Y #1#6 [ vto2;
    imp2 = imp2 + #1#9;
    |VETE sumad1;
|FINSI;

|SI #1#6 > vto2;|Y #1#6 [ vto3;
    imp3 = imp3 + #1#9;
    |VETE sumad1;
|FINSI;

|SI #1#6 > vto3;|Y #1#6 [ vto4;
    imp4 = imp4 + #1#9;
    |VETE sumad1;
|FINSI;

    imp5 = imp5 + #1#9;

|ET sumad1;

|FPRC;

|PROCESO imppan;
    lin_pan = lin_pan + 1;
|DDEFECTO #4;
    #4#0  = ctab;    || cuenta banco
    #4#1  = nomb;    || nombre banco
    #4#2  = vto1;    || 1§ vto
    #4#3  = vto2;    || 2§ vto
    #4#4  = vto3;    || 3§ vto
    #4#5  = vto4;    || 4§ vto
    #4#6  = imp1;    || importe 1
    #4#7  = imp2;    || importe 2
    #4#8  = imp3;    || importe 3
    #4#9  = imp4;    || importe 4
    #4#10 = imp5;    || importe posterior
    #4#11 = tot1;    || total 1
    #4#12 = tot2;    || total 2
    #4#13 = tot3;    || total 3
    #4#14 = tot4;    || total 4
    #4#15 = tot5;    || total posterior
|PINDA #0#4;
|SI swfin = 0;
    |SI lin_pan < 11;
            mas = 100;
    |SINO;
            mas = -1000;
            lin_pan = 0;
        |PAUSA;
        |HAZ pant4; || PINPA #0#4;
    |FINSI;
    |HAZ correte;
|FINSI;
|FPRC;

|DEFBUCLE panta;
 #1#2;
 1, 5; || ..... ordenado por banco+vto, selec. prov y emision
 #0#2, 0, #0#4, #0#0, #0#6, #0#8;
 #0#3, 9, #0#5, #0#1, #0#7, #0#9;
 e;
 NULL, panta1;
|FIN;

|PROCESO pantalla;
|HAZ pant4; || PINPA #0#4;
|ABRE #2;
|SELECT #2#2;
|ABRE #3;
|BUCLE panta;
      |SI sw = 1;
          swfin = 1
          tot1 = tot1 + imp1;
          tot2 = tot2 + imp2;
          tot3 = tot3 + imp3;
          tot4 = tot4 + imp4;
          tot5 = tot5 + imp5;
          |HAZ imppan;
          |PAUSA;
          |FINSI;
|CIERRA #2;
|CIERRA #3;

|FPRC;

|| -------------------------------------------------------------------------

|PROCESO impre; |TIPO 3;
|HAZ inicio;
         vto1 = #0#13;
         vto2 = #0#14;
         vto3 = #0#15;
         vto4 = #0#16;
|MENU elegir;
|SI FSalida > 0;
    |SI FSalida = 1;  |HAZ impresora;  |FINSI;
    |SI FSalida = 2;  |HAZ pantalla;   |FINSI;
|FINSI;
|FPRC;
