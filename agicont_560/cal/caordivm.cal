|FICHEROS;
  caordivm #0;

  camaasil #1;    || apuntes lins.

  camancob #2;    || Cobros
  camanpag #3;    || pagos

  caremesl #4;    || Lineas Remesas
  capaglin #5;    || Lineas Pagares
  camingli #6;    || Lineas Confirmes
  calibros #10;   || Libro de IVA

  caivatm2 #11;    || temporal ivas
  catmpcob #12;    || temporal cobros
  catmppag #13;    || temporal pagos

  caivarep #15;    || iva repercutido
  caivasop #16;    || iva soportado
|FIN;

|VARIABLES;
    alfa1 = "";
    alfa2 = "";
    para1 = 0;
    para2 = 0;
    fech1 = @;
    fech2 = @;
    mes   = 0;

    mensa1 = "    ERROR!. FECHA INCORRECTA ";
    mensa2 = "    ERROR!. LIBRO INCORRECTO ";

    swalgun = 0;
    inkey   = 0;
    estado  = 0;
    tipo    = "";
    libro   = 0;
    numero  = 0;
    sw_err  = 0;
    sw      = 0;
    ultfec  = @;
    feini   = @;
    fefin   = @;

    ordena  = 0;
    guarda  = 0;
|FIN;

|INCLUYE acceso_i;
|INCLUYE tempo_i;

*****************************************************************************
                       PROCESOS DESDE CAMPOS
*****************************************************************************
|PROCESO mayor;
  #0Campo = #0Campo > " ";
  |PINTA #0Campo;
|FPRC;

|PROCESO mayor1;
  |HAZ mayor;
  |C_M #0#5,1,"S";
  |SI #0Campo = "R";
      |C_M #0#5,1,"N";
      #0#5 = 1; |PINTA #0#5;
  |FINSI;
|FPRC;

|PROCESO fechad;
  |SI #0Campo < fec1; |O #0Campo > fec2;
      |MENAV mensa1;
      |ERROR;
      |ACABA;
  |FINSI;
|FPRC;

|PROCESO cheq_libro; |TIPO 0;
 |SI #10#2 ! #0#0;
    |MENAV mensa2;
    |ERROR;
 |SINO;
    |SI #0#0 = "S";
        #0#5 = #10#2;
        |PINTA #0#5;
    |FINSI;
 |FINSI;
|FPRC;

|PROCESO lee_libro1;
|ERROR10;
|FPRC;

|DEFBUCLE lee_libro;
 #10#1;
 2;
  1, #0#0;
 99, #0#0;
 e;
 NULL, lee_libro1;
|FIN;

|PROCESO deflib; |TIPO 7;
|DDEFECTO #10;
|BUCLE lee_libro;
    #0#1 = #10#0;
    |PINTA #0#1;
|FPRC;

|PROCESO calnum; |TIPO 7;
 #0#6 = #0#4 + #0#5;
 |PINTA #0#6;
|FPRC;

|| ************************************************************************
|| ****************** PROGRAMA ********************************************
|| ************************************************************************
|PROGRAMA;
  |CLS;
  |CABEZA "Ordenar IVA mensual";
  |HAZ inicio;

  |ABRE #0;
  |DDEFECTO #0;
  #0#2 = fec1;

  |PINPA #0#0;
  |PINDA #0#0;

|ET rep_sop;
  |ENCAMPO #0#0;
  inkey = FSalida;
  |SI inkey = 7; |CIERRA #0; |ACABA; |FINSI;

|ET libro;
  |ENCAMPO #1#0;
  inkey = FSalida;
  |SI inkey = 2; |VETE rep_sop; |FINSI;
  |SI inkey = 7; |CIERRA #0; |ACABA; |FINSI;

  tipo  = #0#0;
  libro = #0#1;
  |LIBERA #0;
  |LEE 101#0=;
  estado = FSalida;
  |SI estado ! 0;
      |DDEFECTO #0;
      #0#0 = tipo;
      #0#1 = libro;
      #0#2 = fec1;
  |FINSI;

  |PINDA #0#0;
  |ENDATOS #2#0;
  inkey = FSalida;
  |SI inkey ! 0; |CIERRA #0; |ACABA; |FINSI;

  |SI #0#7 = "NO";
      |CIERRA #0;
      |ACABA;
  |FINSI;

|| ...... Variables
   tipo  = #0#0;
   libro = #0#1;
   feini = "01.01.0001";
   fefin = #0#2 - 1;
|| ..................................

  sw_err = 0;
  |HAZ comprobacion;
  |SI sw_err = 1;
      |CIERRA #0;
      |ACABA;
  |FINSI;

  |HAZ ordenar;
  |SI numero ! 0;
      #0#2 = ultfec;
      #0#3 = ultfec;
      |SI #0#3 < fec2; #0#2 = ultfec + 1; |FINSI;
      #0#4 = numero;
      #0#6 = #0#4 + #0#5;
      #0#7 = "NO";
      |SI estado = 0; |GRABA 011#0a; |FINSI;
      |SI estado ! 0; |GRABA 011#0n; |FINSI;
  |FINSI;

  |LIBERA #0;
  |CIERRA #0;
|FPRO;
|| **********************************************************************
|PROCESO comsopor1;
 |SI #16#2 ] #0#6;
     sw_err = 1;
     |ERROR10;
 |FINSI;
|FPRC;

|DEFBUCLE comsopor;
 #16#1;
 46;
 libro, 000000, "  ", feini;
 libro, 999999, "zz", fefin;
 e;
 NULL, comsopor1;
|FIN;

|PROCESO comreper1;
 |SI #15#2 ] #0#6;
     sw_err = 1;
     |ERROR10;
 |FINSI;
|FPRC;

|DEFBUCLE comreper;
 #15#1;
 46;
 libro, 000000, "  ", feini;
 libro, 999999, "zz", fefin;
 e;
 NULL, comreper1;
|FIN;

|PROCESO comprobacion;
 |SI tipo = "S"; |BUCLE comsopor; |FINSI;
 |SI tipo = "R"; |BUCLE comreper; |FINSI;
 |SI sw_err = 1;
     |AVISO;
     |AVISO;
     |PUSHV 1509 2170;
     |D_CUADRO 1509, 2170;
     |ATRI I;
     |PINTA 1610, "  ATENCION!!!.                                              ";
     |PINTA 1710, "  Existen  Facturas  con  Fecha  contable  anterior  a  la  ";
     |PINTA 1810, "  seleccionada y numero de Orden mayor al numero informado. ";
     |PINTA 1910, "                                                            ";
     |ATRI R;
     |PINTA 2010, "      NO PUEDO ORDENAR EL HISTORICO. PROCESO ABORTADO.      ";
     |ATRI 0;
     |PAUSA;
     |POPV;
 |FINSI;
|FPRC;

|| **************************************************************************
||  Actualizaciones ficheros relacionados Apuntes/Remesas/Pagares/Confirmes
|| **************************************************************************
______________________________________________ACTUALIZA APUNTES
|PROCESO lee_apuntes1;
 #1#14 = numero;      || nuevo nro. de fra. en apuntes
 |GRABA 020#1a;
 |LIBERA #1;
|FPRC;

|DEFBUCLE lee_apuntes;
 #1#1;
 13,14,15;
 #11#45, #11#46, #11#47,    1, #11#0, #11#2, #11#27;
 #11#45, #11#46, #11#47, 9999, #11#0, #11#2, #11#27;
 be;
 NULL, lee_apuntes1;
|FIN;

______________________________________________ACTUALIZA REMESAS
|PROCESO lee_remesas1;
 #4#3 = numero;      || nuevo nro. de fra. en remesas
 |GRABA 020#4a;
 |LIBERA #4;
|FPRC;

|DEFBUCLE lee_remesas;
 #4#1;
 2,3,4;
 00001, 001, #11#0, #11#2, #11#27;
 99999, 999, #11#0, #11#2, #11#27;
 be;
 NULL, lee_remesas1;
|FIN;

______________________________________________ACTUALIZA PAGARES
|PROCESO lee_pagares1;
 #5#3 = numero;      || nuevo nro. de fra. en remesas
 |GRABA 020#5a;
 |LIBERA #5;
|FPRC;

|DEFBUCLE lee_pagares;
 #5#1;
 2,3,9;
 000001, 01, #11#0, #11#2, #11#27;
 999999, 99, #11#0, #11#2, #11#27;
 be;
 NULL, lee_pagares1;
|FIN;

______________________________________________ACTUALIZA PAGARES
|PROCESO lee_confirmes1;
 #6#3 = numero;      || nuevo nro. de fra. en confirmes
 |GRABA 020#6a;
 |LIBERA #6;
|FPRC;

|DEFBUCLE lee_confirmes;
 #6#1;
 2,3,4;
 00001, 001, #11#0, #11#2, #11#27;
 99999, 999, #11#0, #11#2, #11#27;
 be;
 NULL, lee_confirmes1;
|FIN;

|| *******************************************************************************
______________________________________________ACTUALIZA COBROS
|PROCESO lee_cobros1;
  |DDEFECTO #2;
  |PARA para2 = 0; |SI para2 [ 28; |HACIENDO para2 = para2 + 1;
        #2para1 = #12para1;
  |SIGUE;
  #2#1 = numero;         || nuevo nro. de fra. en cobros
  |GRABA 020#2n;
|FPRC;

|DEFBUCLE lee_cobros;
 #12#1;
 ;
 #11#0, #11#2, #11#27,   1;
 #11#0, #11#2, #11#27, 999;
 e;
 NULL, lee_cobros1;
|FIN;

______________________________________________ACTUALIZA PAGOS
|PROCESO lee_pagos1;
  |DDEFECTO #3;
  |PARA para2 = 0; |SI para2 [ 26; |HACIENDO para2 = para2 + 1;
        #3para2 = #13para2;
  |SIGUE;
  #3#1 = numero;         || nuevo nro. de fra. en pagos
  |GRABA 020#3n;
|FPRC;

|DEFBUCLE lee_pagos;
 #13#1;
 ;
 #11#0, #11#2, #11#27,   1;
 #11#0, #11#2, #11#27, 999;
 e;
 NULL, lee_pagos1;
|FIN;

|| **************************************************************************
                      || ______ACTUALIZA HISTORICO IVA
|PROCESO lee_tempo1;
  |PINTA 2143, #11#2;
  |PINTA 2150, #11#3;
  |PINTA 2161, #11#46;

  numero = ordena;
  |SI sw = 0; ultfec = #11#46; sw = 1;  |FINSI;
  |SI ultfec < #11#46; ultfec = #11#46; |FINSI;

  |SI tipo = "S";
      |DDEFECTO #16;
      |PARA para1 = 0; |SI para1 [ 61; |HACIENDO para1 = para1 + 1;
            #16para1 = #11para1;
      |SIGUE;
      #16#2 = numero;
      |GRABA 020#16n;  || graba en el temporal iva
      |BUCLE lee_pagos;
      |BUCLE lee_pagares;
      |BUCLE lee_confirmes;
  |FINSI;
  |SI tipo = "R";
      |DDEFECTO #15;
      |PARA para1 = 0; |SI para1 [ 62; |HACIENDO para1 = para1 + 1;
            #15para1 = #11para1;
      |SIGUE;
      #15#2 = numero;
      |GRABA 020#15n;  || graba en el temporal iva
      |BUCLE lee_pagos;
      |BUCLE lee_remesas;
  |FINSI;

  |BUCLE lee_apuntes;
  ordena = ordena + #0#5;
|FPRC;

|DEFBUCLE lee_tempo;
 #11#2;
 ;
 libro, 0000, fech1, feini,      0, "  ";
 libro, 9999, fech2, fefin, 999999, "zz";
 e;
 NULL, lee_tempo1;
|FIN;

|| **************************************************************************
|| ******* Proceso Para IVA Repercutido
|| **************************************************************************
|| ___________________________________________GRABACION TEMPORAL COBROS
|PROCESO tmp_cobros1;         || temporal de pagos
  |DDEFECTO #12;
  |PARA para2 = 0; |SI para2 [ 28; |HACIENDO para2 = para2 + 1;
        #12para2 = #2para2;
  |SIGUE;
  |GRABA 020#12n;
  |BORRA 020#2a;
  |LIBERA #2;
|FPRC;

|DEFBUCLE tmp_cobros;
 #2#1;
 ;
 #15#0, #15#2, #15#27,   1;
 #15#0, #15#2, #15#27, 999;
 be;
 NULL, tmp_cobros1;
|FIN;
______________________________________________GRABACION TEMPORAL IVA
|PROCESO gra_tempo1_R;          || temporal de iva Repercutido
  |DDEFECTO #11;
  |PARA para1 = 0; |SI para1 [ 62; |HACIENDO para1 = para1 + 1;
        #11para1 = #15para1;
  |SIGUE;
  #11#63 = 0;
  |SI #0#8 = "S";
      alfa1 = #11#46;
      alfa1 = (alfa1 % -102) + (alfa1 % 402); || a¤o + mes
      mes = alfa1; #11#63 = mes;
      |PINTA 0511, mes;
  |FINSI;
  |GRABA 020#11n;     || graba en el temporal iva
  |BORRA 020#15a;     || borra en el historico iva Repercutido
  |LIBERA #15;
  |BUCLE tmp_cobros;
  sw = 1;
|FPRC;

|DEFBUCLE gra_tempo_R;
 #15#1;
 46;
 libro, 000000, "  ", feini;
 libro, 999999, "zz", fefin;
 be;
 NULL, gra_tempo1_R;
|FIN;

|| **************************************************************************
|| ******* Proceso Para IVA Soportado
|| **************************************************************************
|| ___________________________________________GRABACION TEMPORAL PAGOS
|PROCESO tmp_pagos1;         || temporal de pagos
  |DDEFECTO #13;
  |PARA para2 = 0; |SI para2 [ 26; |HACIENDO para2 = para2 + 1;
        #13para2 = #3para2;
  |SIGUE;
  |GRABA 020#13n;
  |BORRA 020#3a;
  |LIBERA #3;
|FPRC;

|DEFBUCLE tmp_pagos;
 #3#1;
 ;
 #16#0, #16#2, #16#27,   1;
 #16#0, #16#2, #16#27, 999;
 be;
 NULL, tmp_pagos1;
|FIN;
______________________________________________GRABACION TEMPORAL IVA
|PROCESO gra_tempo1_S;          || temporal de iva Soportado
  |DDEFECTO #11;
  |PARA para1 = 0; |SI para1 [ 61; |HACIENDO para1 = para1 + 1;
        #11para1 = #16para1;
  |SIGUE;
  #11#63 = 0;
  |SI #0#8 = "S";
      alfa1 = #11#46;
      alfa1 = (alfa1 % -102) + (alfa1 % 402); || a¤o + mes
      mes = alfa1; #11#63 = mes;
      |PINTA 0511, mes;
  |FINSI;
  |GRABA 020#11n;     || graba en el temporal iva
  |BORRA 020#16a;     || borra en el historico iva Soportado
  |LIBERA #16;
  |BUCLE tmp_pagos;
  sw = 1;
|FPRC;

|DEFBUCLE gra_tempo_S;
 #16#1;
 46;
 libro, 000000, "  ", feini;
 libro, 999999, "zz", fefin;
 be;
 NULL, gra_tempo1_S;
|FIN;

|| ***************************************
|PROCESO ordenar;
 |NOPARA;
  |HAZ nom_usu;
  alfa1 = "S" + nom_tmp;
  |NOME_DAT #11 alfa1;
  |ABRE #11;
  |CIERRA #11;
  |DELINDEX #11;

  |SI tipo = "S";
    |HAZ nom_usu;
    alfa1 = "P" + nom_tmp;
    |NOME_DAT #13 alfa1;
    |ABRE #13;
    |CIERRA #13;
    |DELINDEX #13;
    |ABRE #13;
  |FINSI;
  |SI tipo = "R";
    |HAZ nom_usu;
    alfa1 = "C" + nom_tmp;
    |NOME_DAT #12 alfa1;
    |ABRE #12;
    |CIERRA #12;
    |DELINDEX #12;
    |ABRE #12;
  |FINSI;

  |ABRE #11;
  |CUADRO 2041 2272;
  |ATRI P;  |PINTA 2142, "    <PREPARANDO TEMPORALES>   ";  |ATRI 0;

  sw     = 0;
  numero = 0;
  feini  = #0#2;
  fefin  = fec2;
  |SI tipo = "S"; |BUCLE gra_tempo_S; |CIERRA #13; |FINSI;
  |SI tipo = "R"; |BUCLE gra_tempo_R; |CIERRA #12; |FINSI;
  |CIERRA #11;
  |PINTA 2142, "                              ";

  |SI sw = 0;
      |MENAV "    ATENCION!!!.  No existen Facturas para Ordenar. ";
  |SINO;
       fech1  = "01.01.1900";
       fech2  = "31.12.2090";
       sw     = 0;
       numero = 0;
       feini  = #0#2;
       fefin  = fec2;
       ultfec = "";
       ordena = #0#6;
       |SI tipo = "S"; |ABRE #16; |ABRE #3; |FINSI;
       |SI tipo = "R"; |ABRE #15; |ABRE #2; |FINSI;
       |BUCLE lee_tempo;
       |SI tipo = "S"; |CIERRA #16; |CIERRA #3; |FINSI;
       |SI tipo = "R"; |CIERRA #15; |CIERRA #2; |FINSI;
  |FINSI;

  |DELINDEX #11;
  |SI tipo = "S"; |DELINDEX #13; |FINSI;
  |SI tipo = "R"; |DELINDEX #12; |FINSI;
 |SIPARA;
|FPRC;
