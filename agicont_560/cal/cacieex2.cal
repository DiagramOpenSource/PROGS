FICHEROS Y VARIABLES DEL INCLUYE puntos_i

|FICHEROS;
     cacuenta #69;
     cacuentp #70;
     caclipro #71;
|FIN;

|VARIABLES;
    p_lugar = 0;
    p_cuan3 = 0;
    p_cuan4 = 0;
    p_cuan5 = 0;
    p_alfa1 = "";
    p_alfa2 = "";
    p_alfa3 = "";
    p_alfa4 = "";
    p_alfa5 = "";
    p_alfa6 = "";
    p_nume1 = 0;
    p_nume2 = 0;
    p_ceros = "";
    p_error = 0;
    p_mensa = "    LONGITUD DE CUENTA FUERA DE NIVEL!";
    desdec1 = "";
    hastac1 = "";
    varia_cod = "";
    sw_nom = 0;
    cuen11 = "";
    cuen21 = "";
    cuen31 = "";
    num_campo = 0;
    p_nifs = "";
|FIN;

FICHEROS Y VARIABLES DEL INCLUYE inici1

|FICHEROS;
    catradia #8;
    canempre #30;
|FIN;

|VARIABLES;
    &fec1 = @;
    &fec2 = @;
    &cod_emp1 = "";
    &cod_emp2 = "";
    &clave1 = "";
    &clave2 = "";
    &cod1 = 0;
    &cod2 = 0;
    &empresa = "";
    &dir_fich = "";
    &dig1 = 0;
    &dig3 = 0;
    &dig4 = 0;
    &dig5 = 0;
    &dig6 = 0;
    &dig7 = 0;
    &dig8 = 0;
    &dig9 = 0;
    &work1 = "";
    &tra1 = 0;
    &mes1 = "";
    &dia1 = "";
    &enPorce = 0;
    &codcon = 0;
|FIN;

FICHEROS Y VARIABLES DE ESTE PROGRAMA

|FICHEROS;
     cacieex4 #0;   || Auxiliar Apuntes Lineas
     cacieex2 #11;  || def de proceso Cabecera

     cacuenta #1;   || cuentas
     camaasic #2;   || apuntes cabs.
     camaasil #3;   || apuntes lins.
     caconcep #4;   || conceptos automaticos
     camandia #5;   || diarios
     caconasi #20;  || Contador Asientos
     camoline #50;  || Amortizaciones
     camocabe #51;  || Amortizaciones
     cacuebal #55;
     caconiso #100; || Control del Asiento

     :/contasoc/def/ctw00001 #900;
     :/contasoc/def/ctw00002 #901;
     :/contasoc/def/ctw00003 #902;

     :/contasoc/def/isocie59 #800;
     :/contasoc/def/maempr11 #801;
     :/contasoc/def/isocie60 #803;
     :/contasoc/def/isocie57 #804;
     :/contasoc/def/isocie61 #805;
     :/contasoc/def/isocie62 #806;
     :/contasoc/def/isocie55 #807;
     :/contasoc/def/isocie29 #808;
     :/contasoc/def/isocie65 #814;
     :/contasoc/def/isocie64 #815;
     :/contasoc/def/isocie66 #817;
     :/contasoc/def/isocie63 #818;
     :/contasoc/def/isocie68 #819;
     :/contasoc/def/ispant49 #899;

|FIN;

|VARIABLES;
     &EURODB = 0;
     &entrada   = 1;
     &modcon    = 0;
     &swpanta   = 0;
     &swcalcu   = 0;
     &algun     = 0;
     &enEmpresa = 0;
     &enPeriodo = 0;
     &anyo      = 0;
     &modo      = 0;
     &nPuntoC   = 0;
     &nPuntoT   = 0;

     aMascara  = "";
     aFichero1 = "";
     aFichero2 = "";
     aFichero3 = "";
     nNivel    = 0;

     nDiario = 0;
     fFecha  = @;
     nDocum  = 0;
     nLinea  = 0;

     estado  = 0;
     salidas = 0;
     importe = 0;
     ini = 0;
     fecalf = @;
     mes = "";
     mesfec = 0;
     mesac = 0;
     a = 0;
     b = 0;
     c = 0;
     d = 0;
     x = 0;
     y = 0;

     nCampo    = 0;
     nCampo1   = 0;
     nCampos   = 0;
     nNume     = 0;
     nPasado   = 0;
     aAlfa1    = "";
     aAlfa2    = "";
     aLong     = 0;
     nCont     = 0;
     nSw       = 0;
     nSaldo    = 0;
     nPosi     = 0;
     nAsiento  = 0;
     nTecla    = 0;
     nAnyo     = 0;

     aConcepto = "";
     aDCuenta  = "";
     aHCuenta  = "";
     aCuenta   = "";
     aAlfa     = "";
     aCaracter = "";

     am = 0;
     bm = 0;
     cm = 0;
     swalta = 0;
     pasado = 0;
     avisos = "";

     nNume1 = 0;
     nNume2 = 0;
     xx = 0;
     ctdeant = "";
     dideant = 0;
     cthaant = "";
     dihaant = 0;
     cuant = "";
     diant = 0;
     coant = 0;
     deant = "";
     dhant = "";
     imant = 0;
     trant = "";
     drant = 0;

     SwForales       = 0;
     SwCoopera       = 0;
     SwTranspa       = 0;
     SwTranspa1      = 0;
     SwTranspa2      = 0;
     SwTranspa3      = 0;
     SwPeqDimen      = 0;

     Tecla = 0;
     XPara = 0;
     Empre = 0;
     Comodin = "";
|FIN;

|| ************************************************************
|| PROCESOS GRABACION DE LOS ASIENTOS
|| ************************************************************

|PROCESO GrabaDocum;
     |ABRE #20;
     |LEE 101#20p;
     |SI FSalida ! 0;
         |DDEFECTO #20;
         |GRABA 020#20b;
     |FINSI;

     #20#1 = #20#1 + 1;
     |SI #20#1 = #11#2;
         |GRABA 020#20a;
     |FINSI;
     |LIBERA #20;
     |CIERRA #20;
|FPRC;

|PROCESO Final;
     |SI modcon ! 0;
         |SUB_EJECUTA "camaapua";
         |HAZ GrabaDocum;
     |SINO;
         |ABRE #8;
         |DDEFECTO #8;
         #8#0 = codcon;
         |LEE 001#8=;
         |SI FSalida = 0;
             |BORRA 020#8a;
         |FINSI;
         |CIERRA #8;
     |FINSI;
|FPRC;

|PROCESO AltaTra;
     modcon = 1;
     #8#0 = codcon;
     #8#1 = #3#4;
     #8#2 = #3#5;
     #8#3 = #3#1;
     #8#4 = #3#8;
     #8#6 = #3#0;
     |LEE 101#8=;
     |SI FSalida ! 0;
         |DDEFECTO #8;
         #8#0 = codcon;
         #8#1 = #3#4;
         #8#2 = #3#5;
         #8#3 = #3#1;
         #8#4 = #3#8;
         #8#6 = #3#0;
         |GRABA 101#8b;
     |FINSI;
     #8#5 = #8#5 + #3#9;
     |GRABA 101#8a;
     |LIBERA #8;
|FPRC;

|PROCESO Actualiz3;
     #5#0 = #11#1;
     |LEE 121#5=;
     |SI FSalida = 0;
         |SI #3#8 = "D";  #5#2 = #5#2 + #3#9;  |FINSI;
         |SI #3#8 = "H";  #5#3 = #5#3 + #3#9;  |FINSI;
         |GRABA 020#5a;
     |FINSI;
     |LIBERA #5;
|FPRC;

|PROCESO Actualiz2;
     ini    = dig1;      || mes inicial
     fecalf = #11#0;
     mes    = fecalf % 402;
     mesfec = mes;

     |SI ini = 1;
         mesac = mesfec;
     |SINO;
         mesac = (mesfec - ini) + 1;
         |SI mesac [ 0;
             mesac = (13 - ini) + mesfec;
         |FINSI;
     |FINSI;

     am = (mesac * 3) + 9;
     bm = am + 1;
     cm = am + 2;

     |SI #3#8 = "D";
         #1am = #1am + #3#9;
         #1#51 = #1#51 + #3#9;
     |SINO;
         #1bm = #1bm + #3#9;
         #1#52 = #1#52 + #3#9;
     |FINSI;

     #1cm = #1am - #1bm;
     #1#53 = #1#51 - #1#52;
|FPRC;

|PROCESO Saca;
     #1#4 = #55#1;
     #1#5 = #55#2;
     #1#6 = #55#3;
     #1#7 = #55#4;
     #1#8 = #55#5;
|FPRC;

|PROCESO Defecto;
     |ABRE #55;
     aAlfa1 = #1#0;
     |QBF aAlfa1;
     aAlfa2 = aAlfa1 + "000000000000";
     aAlfa1 = aAlfa2 % A101;
     #55#0 = aAlfa1;
     |LEE 000#55=;
     |SI FSalida = 0;
         |HAZ Saca;
     |SINO;
         aAlfa1 = aAlfa2 % A102;
         #55#0 = aAlfa1;
         |LEE 000#55=;
         |SI FSalida = 0;
             |HAZ Saca;
         |SINO;
             aAlfa1 = aAlfa2 % A103;
             #55#0 = aAlfa1;
             |LEE 000#5=;
             |SI FSalida = 0;
                 |HAZ Saca;
             |SINO;
                 aAlfa1 = aAlfa2 % A104;
                 #55#0 = aAlfa1;
                 |LEE 000#5=;
                 |SI FSalida = 0;
                     |HAZ Saca;
                 |SINO;
                     |MENAV "     No esta definida esta cuenta en Cuentas de Balance";
                 |FINSI;
             |FINSI;
         |FINSI;
     |FINSI;
     nNivel   = #1#1;
     aMascara = #1#0;   |QBF aMascara;
     aMascara = (aMascara +"zzzzzzzzzzzz") % 112;
     #1#54   = aMascara;
     #1#55   = nNivel;
     |SI #1#1 = #30#13; #1#3 = "S"; |FINSI;
     |CIERRA #55;
|FPRC;

|PROCESO LeeCuenta;
     |LIBERA #1;
     |LEE 101#1=;
     |SI FSalida ! 0;
         |LIBERA #1;
         |DDEFECTO #1;
         #1#0 = #3#4;
         #1#1 = #3#5;
         |HAZ Defecto;
         |PUSHV 0501 1980;
         |PINPA #0#1;
         |PINDA #0#1;
         |ENDATOS #1#1;
         |SI FSalida ! 0;
             |DDEFECTO #1;
             #1#0 = #3#4;
             #1#1 = #3#5;
         |FINSI;
         |GRABA 020#1b;
         |POPV;
     |FINSI;
|FPRC;

|PROCESO Actualiz1;
     #1#0 = #3#4;
     #1#1 = #3#5;
     |LEE 101#1=;
     |SI FSalida ! 0;
         |DDEFECTO #1;
          #1#0 = #3#4;
          #1#1 = #3#5;
         |HAZ LeeCuenta;
     |FINSI;

     |SI #3#0 = 0;
         |SI #3#8 = "D";
             #1#9 = #1#9 + #3#9;
             #1#51 = #1#51 + #3#9;
         |SINO;
             #1#10 = #1#10 + #3#9;
             #1#52 = #1#52 + #3#9;
         |FINSI;
         #1#11 = #1#9 - #1#10;
         #1#53 = #1#51 - #1#52;
     |SINO;
         |SI #3#0 = 99;
             |SI #3#8 = "D";
                 #1#48 = #1#48 + #3#9;
                 #1#51 = #1#51 + #3#9;
             |SINO;
                 #1#49 = #1#49 + #3#9;
                 #1#52 = #1#52 + #3#9;
             |FINSI;
             #1#50 = #1#48 - #1#49;
             #1#53 = #1#51 - #1#52;
         |SINO;
             |HAZ Actualiz2;
         |FINSI;
     |FINSI;

     |SI #1#58 < #11#0;
         #1#58 = #11#0;
     |FINSI;
     |GRABA 020#1a;
     |LIBERA #1;
|FPRC;

|PROCESO Cabecera;
     #2#00 = #3#0; ||  diario
     #2#01 = #3#1; ||  Fecha
     #2#02 = #3#2; ||  documento
     |LEE 101#2=;
     |SI FSalida ! 0;
         |DDEFECTO #2;
         #2#00 = #3#0; ||  diario
         #2#01 = #3#1; ||  Fecha
         #2#02 = #3#2; ||  documento
         #2#03 = #3#2; ||  asiento
         |GRABA 020#2b;
     |FINSI;

     |SI #3#08 = "D";
         #2#04 = #2#04 + #3#09;
         |SI #2#09 = 0; #2#08 = #3#4; #2#09 = #3#5; |FINSI;  || contrp.HABER
     |SINO;
         #2#05 = #2#05 + #3#09;
         |SI #2#11 = 0; #2#10 = #3#4; #2#11 = #3#5; |FINSI;  || contrp.DEBE
     |FINSI;
     |GRABA 020#2a;
     |LIBERA #2;
|FPRC;

|PROCESO GrabaPuntes;
     |SI #0#7 = 0; |ACABA; |FINSI;

     |SI nLinea = 0;
         nLinea = 1;
     |SINO;
         nLinea = nLinea + 10;
     |FINSI;

     |DDEFECTO #3;
     #3#0 = nDiario;
     #3#1 = fFecha;
     #3#2 = nDocum;
     #3#3 = nLinea;
     |SI #0#10 ! "            ";
         #3#4 = #0#10;
         #3#5 = #0#11;
         #3#8 = "D";
         #3#10 = "";
         #3#11 = 0;
         #3#16 = #0#10;
         #3#17 = #0#11;
     |SINO;
         #3#4 = #0#8;
         #3#5 = #0#9;
         #3#8 = "H";
         #3#10 = #0#8;
         #3#11 = #0#9;
         #3#16 = "";
         #3#17 = 0;
     |FINSI;

     #3#6 = #0#4;
     #3#7 = #0#5;
     #3#9 = #0#7;
     |GRABA 020#3n;

     |HAZ Cabecera;   || Cabecera Asiento
     |HAZ Actualiz1;  || Cuenta Contable
     |HAZ Actualiz3;  || Diario
     |HAZ AltaTra;
|FPRC;

|DEFBUCLE cacieex4;
     #0#1;
     ;
     "A", 001;
     "A", 999;
     ;
     NULL, GrabaPuntes;
|FIN;

|PROCESO LeeCab;
     nLinea = 0;
     #2#0 = #11#1;
     #2#1 = #11#0;
     #2#2 = #11#2;
     |LEE 001#2=;
     |SI FSalida = 0;
         #3#0 = #2#0;
         #3#1 = #2#1;
         #3#2 = #2#2;
         #3#3 = 9999;
         |LEE 040#3];
         |SI FSalida = 0;
             |LEE 040#3a;
         |SINO;
             |LEE 040#3u;
         |FINSI;
         |SI FSalida = 0; |Y #3#0 = #2#0; |Y #3#1 = #2#1; |Y #3#2 = #2#2;
             nLinea = #3#3;
         |SINO;
             nLinea = 0;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO Apuntes;
     |ABRE #8;
     |LEE 101#8u;
     |SI FSalida ! 0;
         |DDEFECTO #8;
         #8#0 = 1;
     |SINO;
         #8#0 = #8#0 + 1;
     |FINSI;
     |GRABA 101#8n;

     codcon = #8#0;
     |LIBERA #8;
     nLinea  = 0;
     nDiario = #11#1;
     fFecha  = #11#0;
     nDocum  = #11#2;

     |ABRE #1;
     |ABRE #2;
     |ABRE #3;
     |ABRE #5;

     |HAZ LeeCab;
     |BUCLE cacieex4;

     |CIERRA #1;
     |CIERRA #2;
     |CIERRA #3;
     |CIERRA #5;
     |CIERRA #8;
|FPRC;

|| ************************************************************
|| PROCESOS DE LA PANTALLA
|| ************************************************************

|PROCESO LeeDocum; |TIPO 0;
     salidas = FSalida;
     |SI salidas = 2; |ACABA; |FINSI;

     |ABRE #2;
     #2#0 = #11#1;
     #2#1 = #11#0;
     #2#2 = #11#2;
     |LEE 001#2=;
     |SI FSalida = 0;
         |MENAV "    ERROR!. El Asiento Contable YA existe ... ";
         |ERROR;
         |ACABA;
     |FINSI;
     |CIERRA #2;
|FPRC;

|PROCESO CalcuPorce; |TIPO 0;
     salidas = FSalida;

     #11#6 = #11#3 + #11#5;
     |SI #11#6 < 15000000;
         #11#8 = ((#11#6 * #11#7) / 100) ! EURODB;
     |SINO;
         #11#8 = ((15000000 * #11#7) / 100) ! EURODB;
         nNume1 = #11#6 - 15000000;
         nNume2 = ((nNume1 * #11#32) / 100) ! EURODB;
         #11#8 = #11#8 + nNume2;
     |FINSI;

     #11#10 = #11#8 + #11#9 - #11#33 - (#11#34 + #11#35 + #11#36 + #11#37);

     |PINTA #11#6;
     |PINTA #11#8;
     |PINTA #11#10;
|FPRC;

|PROCESO GrTipo2; |TIPO 7;
     |C_M #11#32,1,"S";
     #11#6 = #11#3 + #11#5;
     |SI #11#6 [ 15000000;
         |C_M #11#32,1,"N";
         #11#32 = 35; |PINTA #11#32;
     |FINSI;
|FPRC;

|PROCESO Documento;
     |ABRE #20;
     |LEE 001#20p;
     |SI FSalida ! 0;
         |DDEFECTO #20;
         |GRABA 020#20n;
     |FINSI;
     #20#1 = #20#1 + 1;
     |CIERRA #20;
|FPRC;

|PROCESO Defectos; |TIPO 7;
     |SI nPasado = 0;
         #11#0 = fec2;
         #11#1 = 1;

         |HAZ Documento;

         #11#2 = #20#1;
         nPasado = 1;

         |PINTA #11#1;  |PINTA #11#0;  |PINTA #11#2;
     |FINSI;
|FPRC;

|PROCESO Topes; |TIPO 0;
     salidas = FSalida;

     |SI #11Campo < fec1; |O #11Campo > fec2;
         |MENAV "    ESTA FECHA NO CORRESPONDE AL EJERCICIO";
         |ERROR;
     |FINSI;
|FPRC;

|PROCESO MiraSaldo;
     nSaldo = nSaldo + #1#53;

     |SI nTecla = 10;
         #900#0 = nCampos;
         #900#1 = #1#0;
         |LIBERA #900;
         |LEE 101#900=;
         |SI FSalida = 0;  |ACABA;  |FINSI;

         #900#2 = #1#2;
         #900#3 = #1#53;
         #900#4 = #1#1;
         |GRABA 020#900n;
     |FINSI;

     |SI nTecla = 11;
         #901#0 = #1#0;
         |LEE 101#901=;
         |SI FSalida = 0;
             #901#3 = (#1#53 * 100) / #11#7;
             |GRABA 020#901a;
             |LIBERA #901;
         |SINO;
             #901#1 = #1#2;
             #901#2 = #1#53;
             #901#3 = (#1#53 * 100) / #11#7;
             #901#4 = 0;
             #901#5 = #1#1;

             |GRABA 020#901n;
             |LIBERA #901;
         |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE cacuenta_1;
    #1#1;
    3;
    aDCuenta, 0, "S";
    aHCuenta, 9, "S";
    ;
    NULL, MiraSaldo;
|FIN;

|PROCESO MontaCuenta;
     |QBT aCuenta;
     aAlfa   = aCuenta % -101;
     |SI aAlfa ! "*";
         aDCuenta = aCuenta;
         aHCuenta = aCuenta;
     |SINO;
         aCuenta = (aCuenta + "            ") % 112;
         aAlfa   = "";
         |PARA nCampo = 1;  |SI nCampo [ 12;  |HACIENDO nCampo = nCampo + 1;
               nPosi = (nCampo * 100) + 1;
               aCaracter = aCuenta % nPosi;
               |SI aCaracter ! "*";  |Y aCaracter ! " ";
                   aAlfa = aAlfa + aCaracter;
               |FINSI;
         |SIGUE;

         aDCuenta = (aAlfa + "            ") % 112;
         aHCuenta = (aAlfa + "999999999999") % 112;
     |FINSI;

     nSaldo = 0;
     |BUCLE cacuenta_1;
|FPRC;

|PROCESO CantiDemas;
     #11nCampos = #11nCampos + #900#3;
|FPRC;

|DEFBUCLE caw00005;
     #900#1;
     ;
     nCampos, "            ";
     nCampos, "zzzzzzzzzzzz";
     ;
     NULL, CantiDemas;
|FIN;

|RUTINA ClaveBaja900;
     #900#0 = nCampos;
     #900#1 = "            ";
|FRUT;

|RUTINA ClaveAlta900;
     #900#0 = nCampos;
     #900#1 = "zzzzzzzzzzzz";
|FRUT;

|PROCESO MiraCuentas;
     nTecla = FSalida;

     |SI nTecla ! 10;
         nCampos = Campo;
         #11nCampos = 0;
         |BUCLE caw00005;
         |PINTA #11nCampos;
         |ACABA;
     |FINSI;

     nCampos = Campo;
     |ABRE #900;
     |SI nCampos = 5;  aCuenta = #100#10;  |HAZ MontaCuenta;  |FINSI;
     |SI nCampos = 34; aCuenta = #100#19;  |HAZ MontaCuenta;  |FINSI;
     |SI nCampos = 35; aCuenta = #100#22;  |HAZ MontaCuenta;  |FINSI;
     |SI nCampos = 36; aCuenta = #100#25;  |HAZ MontaCuenta;  |FINSI;
     |SI nCampos = 37; aCuenta = #100#28;  |HAZ MontaCuenta;  |FINSI;
     |CIERRA #900;

     |PUSHV 0501 2380;
     |ENTLINEAL #1#900, 2, 2, 22, 1, ClaveBaja900, ClaveAlta900;
     |POPV;

     #11nCampos = 0;
     |BUCLE caw00005;
     |PINTA #11nCampos;
|FPRC;

|RUTINA ClaveBaja901;
     #901#0 = "            ";
|FRUT;

|RUTINA ClaveAlta901;
     #901#0 = "zzzzzzzzzzzz";
|FRUT;

|PROCESO CantiComp;
     #11#38 = #11#38 + #901#2;
     #11#33 = #11#33 + #901#4;
|FPRC;

|DEFBUCLE caw00006;
     #901#1;
     ;
     "            ";
     "zzzzzzzzzzzz";
     ;
     NULL, CantiComp;
|FIN;

|PROCESO MiraCompensa;  |TIPO 7;
     nTecla  = 11;

     |ABRE #901;
     aCuenta = #100#16;  |HAZ MontaCuenta;
     |CIERRA #901;

     |PUSHV 0501 2380;
     |ENTLINEAL #1#901, 1, 2, 22, 1, ClaveBaja901, ClaveAlta901;
     |POPV;

     #11#38 = 0;
     #11#33 = 0;
     |BUCLE caw00006;
     |PINTA #11#38;
     |PINTA #11#33;
|FPRC;

|PROCESO GrabaW0007;
     #51#0  = #50#0;
     #51#1  = #50#1;
     |LEE 040#51=;
     |SI FSalida ! 0;  |DDEFECTO #51;  |FINSI;

     #902#0 = #50#0;
     #902#1 = #50#1;
     #902#2 = #50#2;
     |LEE 101#902=;
     |SI FSalida = 0;  |ACABA;  |FINSI;

     #902#3 = #50#3;
     #902#4 = #51#2;
     #902#5 = #50#9 - #50#5;

     |SI #902#5 = 0;  |ACABA;  |FINSI;

     |GRABA 020#902n;
     |LIBERA #902;
|FPRC;

|DEFBUCLE camoline;
     #50#2;
     4;
     "        ", 00, 000, nAnyo, " ", "A";
     "zzzzzzzz", 99, 999, nAnyo, "z", "A";
     ;
     NULL, GrabaW0007;
|FIN;

|PROCESO CantiAmort;
     #11#9 = #11#9 + #902#5;
|FPRC;

|DEFBUCLE caw00007;
     #902#1;
     ;
     "        ", 00, 000;
     "zzzzzzzz", 99, 999;
     ;
     NULL, CantiAmort;
|FIN;

|RUTINA ClaveBaja902;
     #902#0 = "        ";
     #902#1 = 0;
     #902#2 = 0;
|FRUT;

|RUTINA ClaveAlta902;
     #902#0 = "zzzzzzzz";
     #902#1 = 99;
     #902#2 = 999;
|FRUT;

|PROCESO Amortiza;  |TIPO 0;
     nTecla = FSalida;

     |SI #30#26 > 80;
         nAnyo = 1900 + #30#26;
     |SINO;
         nAnyo = 1900 + #30#26;
     |FINSI;

     |ABRE #51;
     |ABRE #902;
     |BUCLE camoline;
     |CIERRA #51;
     |CIERRA #902;

     |SI nTecla = 10;
         |PUSHV 0501 2380;
         |ENTLINEAL #1#902, 3, 2, 22, 1, ClaveBaja902, ClaveAlta902;
         |POPV;
     |FINSI;

     #11#9 = 0;
     |BUCLE caw00007;
     #11#9 = #11#9 % #11#7;
     |PINTA #11#9;
|FPRC;

|PROCESO RellenaPantalla;
     nTecla = 0;
     |ABRE #100;
     |LEE 040#100p;
     |SI FSalida ! 0;  |DDEFECTO #100;  |FINSI;
     |CIERRA #100;

     |ABRE #900;
     nTecla  = 10;
     nCampos = 5;   aCuenta = #100#10;  |HAZ MontaCuenta;  #11#5   = nSaldo;
     nCampos = 34;  aCuenta = #100#19;  |HAZ MontaCuenta;  #11#34  = nSaldo;
     nCampos = 35;  aCuenta = #100#22;  |HAZ MontaCuenta;  #11#35  = nSaldo;
     nCampos = 36;  aCuenta = #100#25;  |HAZ MontaCuenta;  #11#36  = nSaldo;
     nCampos = 37;  aCuenta = #100#28;  |HAZ MontaCuenta;  #11#37  = nSaldo;
     |CIERRA #900;

     FSalida = 0;
     |HAZ Amortiza;
|FPRC;

|PROCESO CalcuBene;
     |SI #1#3 ! "S";  |ACABA;  |FINSI;
     |SI #1#53 = 0;   |ACABA;  |FINSI;
     |SI #1#5 = "D";
         #11#3 = #11#3 + #1#53;
     |SINO;
         #11#3 = #11#3 + #1#53;
     |FINSI;
|FPRC;

|DEFBUCLE cacuenta;
     #1#4;
     3;
     "D", 1,  0,  0,  0, "S";
     "H", 9, 99, 99, 99, "S";
     ;
     NULL, CalcuBene;
|FIN;

|PROCESO Impuesto;
     modo    = 1;
     enEmpresa = #30#2;
     enPeriodo = #30#3;
     enPorce   = #11#7;

     |SI #30#26 > 80;
         anyo = 1900 + #30#26;
     |SINO;
         anyo = 2000 + #30#26;
     |FINSI;

     nPuntoC = #11#8;
     nPuntoT = #11#34 + #11#35 + #11#36;

    |SUB_EJECUTA ":sociedad/isacceco";
|FPRC;

|PROCESO LeeFicheros;
  SwForales       = 0;
  SwCoopera       = 0;
  SwTranspa       = 0;
  SwTranspa1      = 0;
  SwTranspa2      = 0;
  SwTranspa3      = 0;
  SwPeqDimen      = 0;

  |DBASS Comodin;
  |QBF Comodin;
  Comodin = Comodin + "sociedad/fich/";
  |PATH_DAT #800 Comodin;
  |PATH_DAT #801 Comodin;
  |PATH_DAT #803 Comodin;
  |PATH_DAT #804 Comodin;
  |PATH_DAT #805 Comodin;
  |PATH_DAT #806 Comodin;
  |PATH_DAT #807 Comodin;
  |PATH_DAT #808 Comodin;
  |PATH_DAT #814 Comodin;
  |PATH_DAT #815 Comodin;
  |PATH_DAT #817 Comodin;
  |PATH_DAT #818 Comodin;
  |PATH_DAT #819 Comodin;
  |PATH_DAT #899 Comodin;

  |ABRE #801;
  #801#0 = Empre;
  #801#1 = anyo;
  #801#2 = 28;
  |LEE 040#801=;
  |SI FSalida = 0; SwForales = 1; |FINSI;

  #801#0 = Empre;
  #801#1 = anyo;
  #801#2 = 17;
  |LEE 040#801=;
  |SI FSalida = 0; SwCoopera = 1; |FINSI;

  #801#0 = Empre;
  #801#1 = anyo;
  #801#2 = 18;
  |LEE 040#801=;
  |SI FSalida = 0; SwCoopera = 1; |FINSI;

  #801#0 = Empre;
  #801#1 = anyo;
  #801#2 = 19;
  |LEE 040#801=;
  |SI FSalida = 0; SwCoopera = 1; |FINSI;

  #801#0 = Empre;
  #801#1 = anyo;
  #801#2 = 12;
  |LEE 040#801=;
  |SI FSalida = 0; SwTranspa = 1; SwTranspa1 = 1; |FINSI;

  #801#0 = Empre;
  #801#1 = anyo;
  #801#2 = 13;
  |LEE 040#801=;
  |SI FSalida = 0; SwTranspa = 1; SwTranspa2 = 1; |FINSI;

  #801#0 = Empre;
  #801#1 = anyo;
  #801#2 = 15;
  |LEE 040#801=;
  |SI FSalida = 0; SwTranspa = 1; SwTranspa3 = 1; |FINSI;

  #801#0 = Empre;
  #801#1 = anyo;
  #801#2 = 6;
  |LEE 040#801=;
  |SI FSalida = 0; SwPeqDimen = 1; |FINSI;
  |CIERRA #1;

  |SI SwCoopera = 0;
      |ABRE #803; #803#0 = #800#0; #803#1 = #800#1; |BORRA 140#803c; |LIBERA #803; |CIERRA #803;
      |ABRE #806; #806#0 = #800#0; #806#1 = #800#1; #806#2 = 561; |BORRA 140#806c; |LIBERA #806;
      |CIERRA #806;
  |FINSI;

  |SI SwTranspa = 0;
      |SI #801#1 = 1996;
          |ABRE #805; #805#0 = #800#0; #805#1 = #800#1; |BORRA 140#805c; |LIBERA #805; |CIERRA #805;
      |FINSI;

      |SI #801#1 ] 1997;
          |ABRE #815; #815#0 = #800#0; #815#1 = #800#1; |BORRA 140#815c; |LIBERA #815; |CIERRA #815;
      |FINSI;
  |FINSI;

  |ABRE #803;
  #803#0 = #0#0;
  #803#1 = #0#1;
  |LEE 040#803=;
  |SI FSalida ! 0;  |DDEFECTO #803;  |FINSI;
  |CIERRA #803;

  |SI #801#1 = 1996;
      |ABRE #804;
      #804#0 = #801#0;
      #804#1 = #801#1;
      |LEE 040#804=;
      |SI FSalida ! 0;  |DDEFECTO #804;  |FINSI;
      |CIERRA #804;
  |FINSI;

  |SI #800#1 ] 1997;
      |ABRE #814;
      #814#0 = #801#0;
      #814#1 = #801#1;
      |LEE 040#814=;
      |SI FSalida ! 0;  |DDEFECTO #814;  |FINSI;
      |CIERRA #814;
  |FINSI;

  |ABRE #807;
  #807#0 = #801#0;
  #807#1 = #801#1;
  |LEE 040#807=;
  |SI FSalida ! 0;  |DDEFECTO #807;  |FINSI;
  |CIERRA #807;

  |ABRE #817;
  #817#0 = #801#0;
  #817#1 = #801#1;
  |LEE 040#817=;
  |SI FSalida ! 0;  |DDEFECTO #817;  |FINSI;
  |CIERRA #817;

  |ABRE #818;
  #818#0 = #801#0;
  #818#1 = #801#1;
  |LEE 040#818=;
  |SI FSalida ! 0;  |DDEFECTO #818;  |FINSI;
  |CIERRA #818;

  |ABRE #819;
  #819#0 = #801#0;
  #819#1 = #801#1;
  |LEE 040#819=;
  |SI FSalida ! 0;  |DDEFECTO #819;  |FINSI;
  |CIERRA #819;

  |ABRE #808;
  #808#0 = #801#0;
  #808#1 = #801#1;
  |LEE 040#800=;
  |SI FSalida ! 0;  |DDEFECTO #808;  |FINSI;
  |CIERRA #808;
|FPRC;

|PROCESO DatosExtraCont;

  |DDEFECTO #899;
  |PINPA #0#899;
  |PARA XPara = 1; |SI XPara [ 140; |HACIENDO XPara = XPara + 1;
        |C_V #899XPara, 1, "N";
        |C_M #899XPara, 1, "N";
  |SIGUE;

  |C_V #899#00,  1, "S";  |C_V #899#01,  1, "S";  |C_V #899#04,  1, "S";
  |C_V #899#05,  1, "S";  |C_V #899#06,  1, "S";
  |C_V #899#07,  1, "S";  |C_V #899#08,  1, "S";  |C_V #899#11,  1, "S";
  |C_V #899#12,  1, "S";  |C_V #899#13,  1, "S";  |C_V #899#16,  1, "S";
  |C_V #899#20,  1, "S";  |C_V #899#21,  1, "S";  |C_V #899#25,  1, "S";
  |C_V #899#26,  1, "S";  |C_V #899#31,  1, "S";  |C_V #899#33,  1, "S";
  |C_V #899#36,  1, "S";  |C_V #899#38,  1, "S";  |C_V #899#41,  1, "S";
  |C_V #899#43,  1, "S";  |C_V #899#51,  1, "S";  |C_V #899#116, 1, "S";
  |C_V #899#117, 1, "S";  |C_V #899#118, 1, "S";  |C_V #899#119, 1, "S";
  |C_V #899#46,  1, "S";  |C_V #899#54,  1, "S";  |C_V #899#32,  1, "S";
  |C_V #899#37,  1, "S";  |C_V #899#42,  1, "S";

  Empre   = #48#2;
  anyo    = #48#26;
  |SI anyo ] 80;
     anyo = anyo + 1900;
  |SINO;
     anyo = anyo + 2000;
  |FINSI;

  |HAZ LeeFicheros;

  |SI SwCoopera = 0; |Y SwTranspa = 0; |Y SwTranspa1 = 0; |Y SwTranspa2 = 0;
    |Y SwTranspa3 = 0;
       |ACABA;
  |FINSI;

  |SI SwCoopera = 1;
      |C_M #899#8,   1, "S";
      |C_M #899#13,  1, "S";
      |C_M #899#116, 1, "S";
      |C_M #899#117, 1, "S";
  |FINSI;

  |SI SwTranspa1 = 1; |O SwTranspa3 = 1;
      |C_M #899#33,  1, "S";
      |C_M #899#38,  1, "S";
      |C_M #899#43,  1, "S";
      |C_M #899#119, 1, "S";
  |FINSI;

  |SI SwTranspa2 = 1;
      |C_M #899#38,  1, "S";
  |FINSI;

  |C_M #899#118, 1, "S";

  #899#0  = " (560) Cuota integra previa.";
  #899#1  = "S¢lo sociedades cooperativas ";
  #899#4  = "552";
  #899#5  = #800#41;

  #899#6  = "    Resultados Cooperativos .........";
  #899#7  = "553";
  #899#8  = #800#42;

  #899#11 = "    Resultados Extracooperativos ....";
  #899#12 = "554";
  #899#13  = #800#43;

  #899#16  = "    Tipo Gravamen Cooperativo .......";
  #899#116 = #800#114;
  #899#20  = #800#112;

  #899#21  = "    Tipo Gravamen General ...........";
  #899#117 = #800#115;
  #899#25  = #800#113;

  #899#26 = "S¢lo sociedades transparentes ";

  #899#31 = "    Socios residentes ...............";
  #899#32 = "555";
  #899#33 = #800#44;

  #899#36 = "    Socios no residentes ............";
  #899#37 = "556";
  #899#38 = #800#45;

  #899#41 = "    Socios no identificados .........";
  #899#42 = "557";
  #899#43 = #800#46;

  #899#46  = "    Tipo Marginal m ximo I.R.P.F. ...";
  #899#119 = #800#116;

  #899#51  = "Tipo de Gravamen ...................";
  #899#54  = "558";
  #899#118 = #800#47;

  |SI SwCoopera = 0;
      #899#8   = 0;  #899#13  = 0;  #899#20  = 0;
      #899#25  = 0;  #899#116 = 0;  #899#117 = 0;
  |FINSI;

  |SI SwTranspa = 0;
      #899#33  = 0;  #899#38  = 0;  #899#43  = 0; #899#119 = 0;
  |FINSI;

  |SI SwTranspa2 = 1;
      #899#33  = 0;  #899#43  = 0; #899#119 = 0;
  |FINSI;

  |PINDA #0#899;
  |ENDATOS #1#899;
  |SI FSalida = 0;
      #800#42  = #899#8;
      #800#43  = #899#13;
      #800#44  = #899#33;
      #800#45  = #899#38;
      #800#46  = #899#43;
      #800#47  = #899#118;
      #800#112 = #899#20;
      #800#113 = #899#25;
      #800#114 = #899#116;
      #800#115 = #899#117;
      #800#116 = #899#119;
  |FINSI;

|FPRC;


|PROGRAMA;
     |HAZ comienzo;
     |CLS;
     |CABEZA "Asto. Impuesto SOCIEDADES";

||     |HAZ DatosExtraCont;

     |PINPA #0#11;

     aFichero1 = ("1" + Usuario + "zzzzzzzz") % 108;
     aFichero2 = ("2" + Usuario + "zzzzzzzz") % 108;
     aFichero3 = ("3" + Usuario + "zzzzzzzz") % 108;

     |NOME_DAT #900 aFichero1;
     |NOME_DAT #901 aFichero2;
     |NOME_DAT #902 aFichero3;

     |ABRE #11;  |CIERRA #11;  |DELINDEX #11;
     |ABRE #0;   |CIERRA #0;   |DELINDEX #0;
     |ABRE #900; |CIERRA #900; |DELINDEX #900;
     |ABRE #901; |CIERRA #901; |DELINDEX #901;
     |ABRE #902; |CIERRA #902; |DELINDEX #902;

     |ABRET #11;

     |ATRI I;
     |PINTA 2410,"ESTOY CALCULANDO EL RESULTADO CONTABLE";

     |DDEFECTO #11;
     #11#29 = "A";
     |LEE 101#11=;
     |SI FSalida ! 0;  |GRABA 020#11b;  |FINSI;

     #11#3 = 0;
     |BUCLE cacuenta;
     |HAZ RellenaPantalla;

     |ATRI 0;  |PINTA 2410,"                                      ";

     |SI #11#3 [ 0;
         #11#4 = "BENEFICIO";
     |SINO;
         #11#4 = "PERDIDAS";
     |FINSI;

     #11#3 = -#11#3;

     |HAZ CalcuPorce;

     |PINTA #11#0;
     |PINTA #11#1;
     |PINTA #11#2;
     |PINTA #11#3;
     |PINTA #11#4;
     |PINTA #11#5;
     |PINTA #11#6;
     |PINTA #11#7;
     |PINTA #11#8;
     |PINTA #11#9;
     |PINTA #11#10;
     |PINTA #11#32;
     |PINTA #11#33;
     |PINTA #11#34;
     |PINTA #11#35;
     |PINTA #11#36;
     |PINTA #11#37;

     |ABRET #1;
     |ENDATOS #2#11;
     |SI FSalida ! 0; |O #11#11 = "N";
         |CIERRAT #11;  |DELINDEX #11;
         |CIERRAT #1;   |DELINDEX #0;
         |ACABA;
     |FINSI;
     |CIERRAT #11;
     |CIERRAT #1;

     |CONFI "2455SEs correcto (S/N)      ";
     |SI FSalida = 0;
         |HAZ Apuntes;
         |HAZ Final;
         |HAZ Impuesto;
     |FINSI;

     |DELINDEX #11;
     |DELINDEX #0;
     |DELINDEX #900;
     |DELINDEX #901;
     |DELINDEX #902;
|FPRO;
