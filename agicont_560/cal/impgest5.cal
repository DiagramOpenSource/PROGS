|FICHEROS;
     impgest5 #0;    ||Pantalla importacion
     impfich  #1;    ||Fichero de control path
     encoemp5 #2;   ||fichero de apuntes enlace gestion
     camaasic #3;   ||Cabeceras Apuntes
     camaasil #4;   ||lineas de Apuntes
     caivarep #5;   ||iva Repercutido, Ventas
     caivasop #6;   ||Iva soportado compras
     camandia #7;   ||diarios
     calibros #8;   ||libros de iva
     caconcep #9;   ||Conceptos
     cacuenta #11;  ||Fichero de cuentas
     encorec  #12;  ||Fichero de Cobros Enlace Gesion
     camancob #13;  ||Fichero de Cobros
     camanrec #14;  ||Fichero de Tipos de Recibo
     camanpag #15;  ||Fichero de Pagos
     caclipro #16;  ||clientes/proveedores GENERAL
     imptmp1  #18;  ||temporal nuevos numeros de IVA
     catradia #19;  ||temporal niveles superiores
|FIN;

|VARIABLES;
  &fichero = "";
  fichero1 = "";
  &dir_fich1 = "";
  control = 0;
  control1 = 999;
  asiento = 0;
  fecha = @;
  diario = 0;
  asiehaber = 0;
  asiedebe = 0;
  c = 0;
  conta = 0;
  mensaje = "";
  librodesc = "";
  librodescc = "";
  contab = "";
  cuenta = "";
  nivel = 0;
  fecha1 = "";
  mes = ""; ||0
  fmes = "";
  lon = "";
  niv = 0;
  lon1 = "";
  ok = 0;
  p1 = "";
  cta = "";
  mess = 0;
  cuentaz = "";
  i = 0;
  pasar = 0;
  total = 0;          ||si la variable total = 0 actualizare los importes
  empresa1 = "";      ||de las cuentas, si es uno, solo las dare de alta
  empresao = "";
  periodoo = "";
  empresa2 = "";
  dia = 0;
  docu = 0;
  fech = @;
  desdedi = 0;
  hastadi = 99;
  desdenum = 0;
  hastanum = 999999999;
  ini = 0;
  mesac = 0;
  fecalf = "";
  mesfec = 0;
  a = 0;
  b = 0;
  c1 = 0;
  cuencli = "";
  nivcli = 0;
  nombre = "";
  cif = "";
  nombre1 = "";
  opera = "";
  importe = 0;
  c_conh = "";
  d_conh = 0;
  c_cond = "";
  d_cond = 0;
  &feini = @;
  &fefin = @;
  aParam = "";
  &eaFitxer = "";
|FIN;

|INCLUYE conasi_i;
|INCLUYE acceso_i;

|PROCESO mayus;
  #0Campo = #0Campo > " ";
  |PINTA #0Campo;
|FPRC;

|PROCESO miraiva;

|SI #2#47 = 0; ||No este grabado
    |HAZ contador;
    conta = #20#1;

  |ET otravez;
    #3#0 = #2#0;
    #3#1 = #2#1;
    #3#2 = conta;
    |LEE 001#3=;
    |SI FSalida = 0;
        |HAZ contador;
        conta = #20#1;
        |VETE otravez;
    |SINO;
        #2#47 = conta;
        |GRABA 001#2a;
        |LIBERA #2;
        |SI #0#7 = 3;    |HAZ grasidx;  |FINSI;
    |FINSI;
    dia  = #2#0;
    docu = #2#2;
    fech = #2#1;
    #2#4 = 1;
    |LEE 101#2];
    |PARA ; |SI FSalida = 0;
            |Y #2#0 = dia;
            |Y #2#1 = fech;
            |Y #2#2 = docu;
            |Y #2#4 ] 0;
                             |HACIENDO;
            #2#47 = conta;
            |GRABA 021#2a;
            |LIBERA #2;
            |SI #0#7 = 3;    |HAZ grasidx;  |FINSI;
            |LEE 101#2s;
    |SIGUE;
    #2#0 = dia;
    #2#1 = fech;
    #2#2 = docu;
    #2#4 = 0;
    |LEE 001#2];
|FINSI;
|FPRC;


|PROCESO altatra;
|ABRE #19;
    #19#0 = codcon;
    #19#1 = #4#4;        || cuenta
    #19#2 = #4#5;        || digito
    #19#3 = #4#1;        || fecha
    #19#4 = #4#8;        || signo
    #19#6 = #4#0;        || diario
|LEE 101#19=;
|SI FSalida ! 0;
    |DDEFECTO #19;
        #19#0 = codcon;
        #19#1 = #4#4;
        #19#2 = #4#5;
        #19#3 = #4#1;
        #19#4 = #4#8;
        #19#6 = #4#0;
    |GRABA 020#19b;
|FINSI;
    importe = #4#9;
|SI opera = "B";
        importe = -importe;
|FINSI;
    #19#5 = #19#5 + importe;
|GRABA 020#19a;
|LIBERA #19;
|CIERRA #19;
|FPRC;

|DEFBUCLE asiento1;
#2#1;
;
desdedi,feini,desdenum,desdenum;
hastadi,fefin,hastanum,hastanum;
be;
NULL, miraiva;
|FIN;

||**********************************************************************
||**********************************************************************
||**********************************************************************


|PROCESO miraasie;
control = #2#2;
c = c + 1;
|SI control ! control1;
     |SI c ! 1;
          |DDEFECTO #3;  ||Grabo la cabecera del asiento
          #3#0 = diario;
          #3#1 = fecha1;
          #3#2 = asiento;
          #3#3 = asiento;
          #3#4 = asiedebe;
          #3#5 = asiehaber;
          #3#6 = #3#4 - #3#5; ||Saldo
          #3#7 = 0;
          #3#8 = c_conh;
          #3#9 = d_conh;
          #3#10 = c_cond;
          #3#11 = d_cond;
          |GRABA 001#3n;
          |LIBERA #3;
          |HAZ grabadiario;   ||grabo en el fichero de diario
          asiehaber = 0;
          asiedebe = 0;
          c_conh = "";  d_conh = 0;
          c_cond = "";  d_cond = 0;
     |FINSI;
|FINSI;

|SI #2#22 ! 0; |O #2#23 ! 0; |O #2#24 ! 0;
               |O #2#48 ! 0; |O #2#49 ! 0;  ||Es asiento de iva

     |SI #2#13 = "R";
          |HAZ repercutido;   ||Si es Ventas Repercutido
     |FINSI;
     |SI #2#13 = "S";
          |HAZ soportado;     ||Si es Compras Soportado
     |FINSI;
|SINO;              ||No es un asiento de iva
          |ET asie1;
          |DDEFECTO #3;
          #3#0 = #2#0;        ||diario
          #3#1 = #2#1;        ||fecha
          #3#2 = #2#47;        ||documento
          |LEE 001#3=;
          |SI FSalida!0;
               #4#0 = #2#0;   ||diario
               #4#1 = #2#1;   ||fecha
               #4#2 = #2#47;  ||documento
               #4#3 = #2#4;   ||linea
               #4#8 = #2#11;  ||Signo
               |SI #4#8 = "D";     ||Es cuenta de debe
                    #4#4 = #2#5;   ||Cuenta
                    #4#5 = #2#7;   ||Digito
                    #4#16 = #2#5;  ||cuenta DEBE
                    #4#17 = #2#7;  ||Digito DEBE
                    #4#10 = "";
                    #4#11 = 0;
                    asiedebe = asiedebe + #2#12;
                    cuenta = #4#16;
                    nivel = #4#17;
                    |SI d_conh=0; c_conh=#4#4; d_conh=#4#5; |FINSI;
               |SINO;              ||Es cuenta de haber
                    #4#4 = #2#6;   ||Cuenta
                    #4#5 = #2#8;   ||Digito
                    #4#10 = #2#6;  ||cuenta HABER
                    #4#11 = #2#8;  ||Digito HABER
                    #4#16 = "";
                    #4#17 = 0;
                    asiehaber = asiehaber + #2#12;
                    cuenta = #4#10;
                    nivel = #4#11;
                    |SI d_cond=0; c_cond=#4#4; d_cond=#4#5; |FINSI;
               |FINSI;
               total = 0;
               #4#6 = #2#9;   ||concepto
               #9#0 = #2#9;        ||Miro si existe el concepto
               |LEE 001#9=;
               |SI FSalida ! 0;
                    #9#1 = "INEXISTENTE";
                    #2#10 = #9#1;
                    |GRABA 001#9n;
                    |LIBERA #9;
               |FINSI;
               #4#7 = #2#10;  ||descripcion concepto
               #4#9 = #2#12;  ||Importe

               ||Si la factura a pasar ya existe, no se graban
               ||los datos de la factura en el asiento

               |SI pasar = 0;
                    #4#12 = #2#13; ||Soportado/Repercutido
                    #4#13 = #2#14; ||libro;
                    #4#14 = #2#16; ||factura
                    #4#15 = #2#17; ||serie
                    #4#19 = #2#42; ||Acumulador
                    #4#20 = #2#43; ||Nro Acumulador
                    |SI #4#19 = " ";
                        |SI #4#4 = #2#20; |Y #4#5 = #2#21;
                            |SI #2#13 = "R"; |Y #4#8 = "D"; #4#19 = "F"; |FINSI;
                            |SI #2#13 = "S"; |Y #4#8 = "H"; #4#19 = "F"; |FINSI;
                        |FINSI;
                    |FINSI;

               |FINSI;
               #4#21 = #2#40; ||departamento
               |GRABA 011#4n;
                   opera = "A";
               |HAZ altatra;       || fichero de movimientos
               asiento = #2#47;
               fecha = #2#1;
               fecha1 = #2#1;
               diario = #2#0;
               |HAZ cuentas;       ||En el proceso cuentas, averiguo si la
                                   ||cuenta existe, si no la doy de alta
                                   ||Acumulo los importes

          |SINO;                   ||Si el numero de asiento ya existe
               |ET pp;             ||le busco uno nuevo
               dia = #2#0;
               docu = #2#2;
               fech = #2#1;
               #2#4 = 1;
               |HAZ contador;
               #3#0 = dia;
               #3#1 = fech;
               #3#2 = #20#1
               |LEE 001#3=;
               |SI FSalida = 0;    ||si todavia exsiste el numero de asiento
                    |VETE pp;      ||le busco otro hasta que no lo encuentre
               |FINSI;

               #2#0 = dia;
               #2#1 = fech;
               #2#2 = docu;
               #2#4 = 1;
               |LEE 101#2];
               |PARA ; |SI FSalida = 0;
                       |Y #2#0 = dia;
                       |Y #2#1 = fech;
                       |Y #2#2 = docu;
                       |Y #2#4 >0;
                       |HACIENDO;
                    #2#47 = #20#1;
                    |GRABA 001#2a; ||Actualizo todos los registros de esa
                    |LIBERA #2;    ||factura con el nuevo numero de registro
                    |LEE 101#2s;
                |SIGUE;
                #2#0 = dia;
                #2#1 = fech;
                #2#2 = docu;
                #2#4 = 0;
                |LEE 101#2=;
                |SI FSalida = 0;
                    #2#47 = #20#1;
                    |GRABA 001#2a;
                    |LIBERA #2;
                |FINSI;
                #2#4 = 1;
                |LEE 001#2=;
                |VETE asie1;  ||Cuando ya tiene un nuevo asiento, lo adjudico
          |FINSI;
|FINSI;
control1 = control;
|FPRC;

|PROCESO finasie;
          |DDEFECTO #3;       ||Grabo la cabecera del ultimo asiento leido
          #3#0 = diario;
          #3#1 = fecha1;
          #3#2 = asiento;
          #3#3 = asiento;
          #3#4 = asiedebe;
          #3#5 = asiehaber;
          #3#6 = #3#4 - #3#5;
          #3#7 = 0;
          #3#8 = c_conh;
          #3#9 = d_conh;
          #3#10 = c_cond;
          #3#11 = d_cond;
          |GRABA 001#3n;
          |LIBERA #3;
          |HAZ grabadiario;
|FPRC;


|DEFBUCLE asiento;
#2#1;
;
desdedi,feini,desdenum,desdenum;
hastadi,fefin,hastanum,hastanum;
e;
NULL, miraasie,NULL,NULL,finasie;
|FIN;


||***************************************************************************
||***************************************************************************
||***********************+HASTA AQUI EL ENLACE DE ASIENTOS*******************
||***************************************************************************
||***************************************************************************

||miro los cobros si son cobros o no

|PROCESO miracobro;
     |SI #12#18 = "C";   ||Si son cobros
          |HAZ cobro;
     |FINSI;
     |SI #12#18 = "P";        ||si es un pago
          |HAZ pago;
     |FINSI;
|FPRC;

|PROCESO pago;
  |DDEFECTO #15;
  #15#0 = #12#0;
  #15#1 = #12#1;
  #15#2 = #12#2;
  #15#3 = #12#3;
  |LEE 101#15=;
  |SI FSalida = 0;
      |SI #0#9 ! "S";
          mensaje = "0000El Pago " + #15#1 + "-" + #15#3 + " Ya Existe NO SE GRABARA";
          |MENAV mensaje;
      |SINO;
          |SI #15#22 ! "S";
              #15#9 = #15#9 + #12#9;        ||Gts Negociacion
              #15#10 = #15#10 + #12#10;     ||Gts Impagado
              #15#23 = #15#23 + #12#25;
              |SI #15#23 ] #15#8;  #15#22 = "S";  |FINSI;
              |GRABA 021#15a;
              |LIBERA #15;
          |FINSI;
      |FINSI;
  |SINO;
      |PARA i = 4; |SI i [ 13; |HACIENDO i = i + 1;
            #15i = #12i;
      |SIGUE;
      #15#14 = #12#19;         || nombre banco
      #15#15 = #12#20;         || direccion banco
      #15#16 = #12#22;         || cp. 1
      #15#17 = #12#23;         || cp. 2
      #15#18 = #12#17;         || cta.cte.
      #15#19 = #12#16;         || ccc
      #15#20 = #12#21;         || poblacion banco
      cuencli = #15#4;
      nivcli  = #15#5;
      |HAZ clientes;
      #15#26 = #16#28;
      |SI #12#24 = "P";
          #15#23 = #12#25;
          |SI #15#23 ] #15#8;      ||Cobrado completo
              #15#22 = "S";
          |FINSI;
      |FINSI;
      |GRABA 001#15n;
  |FINSI;
  cuenta = #15#4;
  nivel = #15#5;
  total = 1;
  |HAZ cuentas;
  #14#0 = #12#11;
  |LEE 001#14=;
  |SI FSalida ! 0;
      #14#1 = "INEXISTENTE";
      |GRABA 001#14n;
  |FINSI;
|FPRC;

|PROCESO cobro;
  |DDEFECTO #13;
  #13#0 = #12#0;
  #13#1 = #12#1;
  #13#2 = #12#2;
  #13#3 = #12#3;
  |LEE 101#13=;
  |SI FSalida = 0;
      |SI #0#9 ! "S";
          mensaje = "0000El Cobro " + #13#1 + "-" + #13#3 + " Ya Existe";
          |MENAV mensaje;
      |SINO;
          |SI #13#23 ! "S";
              |SI #12#24 = "C";             ||Paso el importe cobrado
                  #13#9 = #13#9 + #12#9;        ||Gts Negociacion
                  #13#10 = #13#10 + #12#10;     ||Gts Impagado
                  #13#24 = #13#24 + #12#25;
              |FINSI;
              |SI #13#24 ] #13#8;  #13#23 = "S";  |FINSI;
              |GRABA 021#13a;
              |LIBERA #13;
          |FINSI;
      |FINSI;
  |SINO;
      |PARA i = 4; |SI i [ 14; |HACIENDO i = i + 1;
            #13i = #12i;
      |SIGUE;
      #13#14 = #12#7;          || vto.original
      #13#15 = #12#19;         || nombre banco
      #13#16 = #12#20;         || direccion banco
      #13#17 = #12#22;         || cp. 1
      #13#18 = #12#23;         || cp. 2
      #13#19 = #12#17;         || cta.cte.
      #13#20 = #12#16;         || ccc
      #13#21 = #12#21;         || poblacion banco
      |SI #12#24 = "C";
          #13#24 = #12#25;
          |SI #13#24 ] #13#8;      ||Cobrado completo
              #13#23 = "S";
          |FINSI;
      |FINSI;
      #13#26 = #13#8;          || importe original
      #13#27 = 0;              || nro. impagos
      |GRABA 001#13n;
      |LIBERA #13;
  |FINSI;
  cuenta = #13#4;
  nivel = #13#5;
  total = 1;
  |HAZ cuentas;    ||doy de alta si no existe la cuenta del cobro
  #14#0 = #12#11;
  |LEE 001#14=;
  |SI FSalida ! 0;
      #14#1 = "INEXISTENTE";
      |GRABA 001#14n;
  |FINSI;
|FPRC;

|DEFBUCLE cobros;
#12#1;
;
0  , 0       , "  " , 0;
99 , 9999999 , "zz" , 999999;
e;
NULL,miracobro;
|FIN;


|PROCESO inicio1;  |TIPO 10;
  aParam = PARAMETRO; |QBF aParam;
  |ABRE #19;
  |LEE 101#19u;
  |SI FSalida ! 0;
      |DDEFECTO #19;
      #19#0 = 1;
  |SINO;
      #19#0 = #19#0 + 1;
  |FINSI;
  |GRABA 101#19n;
  codcon = #19#0;
  |LIBERA #19;
  |CIERRA #19;

  |SI aParam = "LAPIZ";
      #0#3 = #30#2; |PINTA #0#3;
  |FINSI;
  |ENDATOS #1#0;
  |SI FSalida ! 0;  |ERROR;  |ACABA;  |FINSI;
  |SI #0#0 ! "SI";  |ERROR;  |ACABA;  |FINSI;
  dir_fich1 = #1#2;
  |QBF dir_fich1;

  |SI #0#7 = 2;  |HAZ secuencial;  |FINSI;
  |SI #0#7 = 3;  |Y #0#8 = "S";  |HAZ indexado;  |FINSI;

|SI #0#8 = "S";
    |HAZ b_apuntes;
    |SUB_EJECUTA "camaapua.run";        || solo en importacion completa
|FINSI;
|SI aParam ! "LAPIZ"; |HAZ b_cobros; |FINSI;

|SI #0#6 = "S";
     |CIERRA #2;
     |CIERRA #12;
     |DELINDEX #2;
     |DELINDEX #12;
|FINSI;

|ERROR;
|ACABA;
|FPRC;


|PROCESO b_apuntes;
     |CIERRA #1;
     |CIERRA #2;
     |PATH_DAT #2 dir_fich1;
     ||EMpiezo a pasar el fichero de apuntes
     fichero = #0#3;     ||empresa origen
     fichero = "000000" + fichero;
     fichero = fichero % -104;
     |SI aParam = "LAPIZ"; |Y eaFitxer ! ""; fichero = eaFitxer; |FINSI;
     fichero1 = "enco" + fichero;
     |QBF fichero1;
     |NOME_DAT #2 fichero1;
     |ABRE #2;
     |LEE 001#2p;   ||miro si existe algo para saber que lo he encontrado
     |SI FSalida!0;
          |MENAV "0000No se ha encontrado el fichero";
          |ERROR;
          |ACABA;
     |SINO;
          |DFICE empresa1;
          |QBF empresa1;
          empresa1 = empresa1 % -206;
          |SI #2#45 ! 0;
              empresao = #2#45;
              periodoo = #2#46;
              empresa2 = "000000" + empresao + periodoo;
              empresa2 = empresa2 % -106;
              |SI empresa2 ! empresa1;
                   mensaje = "0000La empresa Contable destino ha de ser la : " + empresao + "/" + periodoo;
                   |MENAV mensaje;
                   |ERROR;
                   |ACABA;   ||si no encuentro la empresa, no sigo, puesto que no
              |FINSI;        ||se pasara nada si no hay asientos
           |SINO;
              empresao = empresa1 % 105;
              periodoo = empresa1 % 601;
              empresa2 = empresa1;
              #2#45 = empresao;
              #2#46 = periodoo;
           |FINSI;
     |FINSI;
     |ABRE #8;
     #8#0 = #2#14;
     |LEE 001#8=;
     |SI FSalida = 0;
          librodesc = #8#1;
     |SINO;
          #8#1 = "LIBRO INEXISTENTE";
          librodesc = #8#1;
          |GRABA 001#8n;
          |LIBERA #8;
     |FINSI;
     |CIERRA #8;
     |ABRE #9;
     #9#0 = #2#18;
     |LEE 001#9=;
     |SI FSalida = 0;
          librodescc = #9#1;
     |SINO;
          #9#1 = "INEXISTENTE";
          |GRABA 011#9n;
          |LIBERA #9;
     |FINSI;
|HAZ inicio;
     |ABRE #3;      ||asientos cab
     |ABRE #4;      ||lineas cabs
     |ABRE #5;      ||iva rep
     |ABRE #6;      ||iva sop
     feini = "00.00.0000";
     fefin = "31.12.9999";
     |BUCLE asiento1;    ||Grabo los numeros de asiento en fichero de enlace
     control1 = 999;
     feini = "00.00.0000";
     fefin = "31.12.9999";
     |BUCLE asiento;     ||Grabo los asientos e ivas
     |LIBERA #2;
     |LIBERA #3;
     |LIBERA #4;
     |LIBERA #5;
     |LIBERA #6;
     |CIERRA #3;
     |CIERRA #4;
     |CIERRA #5;
     |CIERRA #2;
     |CIERRA #6;
     |CIERRA #9;
|FPRC;

|PROCESO b_cobros;
     ||************************
     ||****ACTUALIZO LOS COBROS
     ||************************

     |CIERRA #12;

     |PATH_DAT #12 dir_fich1;
     ||EMpiezo a pasar el fichero de apuntes
     fichero = #0#3;     ||empresa origen
     fichero = "000000" + fichero;
     fichero = fichero % -104;
     fichero1 = "enre" + fichero;
     |QBF fichero1;
     |NOME_DAT #12 fichero1;
     |ABRE #12;
     |LEE 001#12p;   ||miro si existe algo para saber que lo he encontrado
     |SI FSalida!0;
          |MENAV "0000No se ha encontrado el fichero de Enlace de COBROS";
     |SINO;
          |ABRE #13;
          |ABRE #14;
          |ABRE #15;
          |BUCLE cobros;
          |CIERRA #13;
          |CIERRA #14;
          |CIERRA #15;
     |FINSI;
     |CIERRA #12;
|FPRC;

|PROCESO mira;|TIPO 0;        ||miro que exista el registro en el control
     |ABRE #1;                ||de importacines
     #1#0=#0#1;
     |LEE 001#1=;
     |SI FSalida = 0;
          #0#2 = #1#1;
          |PINTA #0#2;
     |SINO;
          |MENAV "0000No Existe el Control Path Importaciones";
          |ERROR;
     |FINSI;
     |CIERRA #1;
|FPRC;

|PROCESO cuentas;
|ABRE #11;
|QBF cuenta;
#11#0 = cuenta;
#11#1 = nivel;
|LEE 001#11=;
|SI FSalida = 0;
     |LEE 101#11c;       ||Si la cuenta ya existe
     nombre1 = #11#2;    ||nombre de la cuenta
     |SI total = 0; ||si viene de un apunte
          ini = dig1;
          fecalf = fecha;          ||la actualizo
          mes = fecalf % 402;
          mesfec = mes;
          |SI ini = 1;
               mesac = mesfec;
          |SINO;
               mesac = (mesfec - ini) + 1;
               |SI mesac [ 0;
                    mesac = (13 - ini) + mesfec;
               |FINSI;
          |FINSI;
          a = (mesac * 3) + 9;
          b = a + 1;
          c1 = a + 2;
          |SI #4#8 = "D";
               #11a = #11a + #4#9;
               #11#51 = #11#51 + #4#9;
          |SINO;
               #11b = #11b + #4#9;
               #11#52 = #11#52 + #4#9;
          |FINSI;
          #11c1 = #11a - #11b;
          #11#53 = #11#51 - #11#52;
          |GRABA 011#11a;
          |LIBERA #11;
     |FINSI;
|SINO;         ||Si la cuenta no existe, busco la anterior para saber a que
     ok =0;    ||Epigrafe ...... pertenece
     lon = cuenta % 0;
     |SI dig4 = lon; ok = 1; niv = 1; lon1 = dig4; |FINSI;
     |SI dig5 = lon; ok = 1; niv = 2; lon1 = dig4; |FINSI;
     |SI dig6 = lon; ok = 1; niv = 3; lon1 = dig5; |FINSI;
     |SI dig7 = lon; ok = 1; niv = 4; lon1 = dig6; |FINSI;
     |SI dig8 = lon; ok = 1; niv = 5; lon1 = dig7; |FINSI;
     |SI dig9 = lon; ok = 1; niv = 6; lon1 = dig8; |FINSI;
     |SI ok = 1;
          |PARA ; |SI FSalida !0; |Y niv > 0; |HACIENDO ;
               niv = niv - 1;||Le quito un nivel,para mirar la cuenta anterior
               |SI niv = 1; lon1 = dig4; |FINSI;
               |SI niv = 2; lon1 = dig5; |FINSI;
               |SI niv = 3; lon1 = dig6; |FINSI;
               |SI niv = 4; lon1 = dig7; |FINSI;
               |SI niv = 5; lon1 = dig8; |FINSI;
                    lon1 = "0000" + lon1;
                    lon1 = lon1 % -102;
                    lon1 = "1"+lon1;
                    cta = cuenta % lon1;
                    |QBF cta;
               #11#0 = cta;
               #11#1 = niv;
               |LEE 001#11=;
          |SIGUE;
               |PARA i = 9; |SI i [ 53; |HACIENDO i = i + 1;
                    #11i = 0;
               |SIGUE;
               |SI total = 0;
                    |SI FSalida =0; |O niv = 1;
                         ini = dig1;         ||Actualizo la cuenta
                         fecalf = fecha;
                         mes = fecalf % 402;
                         mesfec = mes;
                         |SI ini = 1;
                              mesac = mesfec;
                         |SINO;
                              mesac = (mesfec - ini) + 1;
                              |SI mesac [ 0;
                                   mesac = (13 - ini) + mesfec;
                              |FINSI;
                         |FINSI;
                         a = (mesac * 3) + 9;
                         b = a + 1;
                         c1 = a + 2;
                         |SI #4#8 = "D";
                              #11a = #11a + #4#9;
                              #11#51 = #11#51 + #4#9;
                         |SINO;
                              #11b = #11b + #4#9;
                              #11#52 = #11#52 + #4#9;
                         |FINSI;
                         #11c1 = #11a - #11b;
                         #11#53 = #11#51 - #11#52;

                    |FINSI;
               |FINSI;

               #11#0 = cuenta;
               #11#1 = nivel;
               #11#3 = "S";
               cuentaz = cuenta + "zzzzzzzzzzzzzzzzzzzz";
               cuentaz = cuentaz % 112;
               #11#54 = cuentaz;
               #11#55 = nivel;

               |PUSHV 0501 2380;
               |PINPA #0#11;
               |PINDA #0#11;
               |CONFI "0000SConfirma el alta : ";
               |SI FSalida = 0;
                    |ENDATOS #1#11;
                    |SI FSalida = 0;
                         nombre1 = #11#2;
                         |GRABA 011#11n;          ||Si acepta el darla de alta
                         |LIBERA #11;             ||la grabo
                         #11#0 = cta;
                         #11#1 = niv;
                         |LEE 101#11=;
                         #11#3 = "N";        ||Quito el pase directo de la cuenta
                         |GRABA 011#11a;     ||en donde he mirado a donde pertenecia
                         |LIBERA #11;        ||puesto que ya no es de pase directo
                    |FINSI;
               |FINSI;
     |FINSI;
     |POPV;
|FINSI;
|CIERRA #11;
|FPRC;

|PROCESO clientes;
     nombre = "";
     cif = "";
     |ABRE #16;
     || .... SELECT #2#16; ahora es la primera clave

     #16#23 = "C";
     #16#10 = cuencli;
     #16#11 = nivcli;
     |LEE 001#16=;
     |SI FSalida = 0;
          nombre = #16#1;     ||nombre
          cif = #16#9;        ||cif del cliente
     |SINO;
          #16#23 = "P";
          #16#10 = cuencli;
          #16#11 = nivcli;
          |LEE 001#16=;
          |SI FSalida = 0;
               nombre = #16#1;     ||nombre
               cif = #16#9;        ||cif del cliente
          |SINO;
               nombre = nombre1;   ||nombre de la cuenta
               cif = #2#58;
          |FINSI;
     |FINSI;
     |CIERRA #16;
|FPRC;

|PROCESO soportado;
     pasar = 0;
     |DDEFECTO #6;
     #6#0 = #2#14;       ||Libro
     #6#2 = #2#16;       ||factura
     #6#27 = #2#17;      ||Serie
     |LEE 001#6=;
     |SI FSalida ! 0;
          #6#27 = #2#17;      ||Serie
          #6#1 = librodesc;  ||descripcion libro
          #6#3 = #2#41;  ||Fecha factura
          #6#4 = #2#20;  ||Cuenta iva
          #6#5 = #2#21;  ||digito cuenta
          cuenta = #6#4;
          nivel = #6#5;
          total = 1;
          |HAZ cuentas;
          #6#6 = #2#18;  ||concepto iva
          ||#6#7 = librodescc;  ||DEscripcion concepto
          #6#7 = #2#19;  ||DEscripcion concepto LA MISMA QUE EL APUNTE!!!
          #6#8 = #2#22;  ||Base 1 COns
          #6#9 = #2#23;  ||Base 2 red
          #6#10 = #2#24; ||Base 3 lujo
          #6#52 = #2#48; ||Base 4
          #6#53 = #2#49; ||Base 5
          #6#11 = #2#25; ||% Iva 1
          #6#12 = #2#26; ||% Iva 2
          #6#13 = #2#27; ||% Iva 3
          #6#54 = #2#50; ||% Iva 4
          #6#55 = #2#51; ||% Iva 5
          #6#14 = #2#28; ||Cuota iva 1
          #6#15 = #2#29; ||Cuota iva 2
          #6#16 = #2#30; ||Cuota iva 3
          #6#56 = #2#52; ||Cuota iva 4
          #6#57 = #2#53; ||Cuota iva 5
          #6#17 = #2#31; ||% Rec 1
          #6#18 = #2#32; ||% Rec 2
          #6#19 = #2#33; ||% Rec 3
          #6#58 = #2#54; ||% Rec 4
          #6#59 = #2#55; ||% Rec 5
          #6#20 = #2#34; ||Cuota Rec 1
          #6#21 = #2#35; ||Cuota Rec 2
          #6#22 = #2#36; ||Cuota Rec 3
          #6#60 = #2#56; ||Cuota Rec 4
          #6#61 = #2#57; ||Cuota Rec 5
          #6#23 = #2#39; ||deducible
          #6#26 = #2#37; ||Total factura
          #6#44 = #2#44; ||Periodo
          #6#45 = #2#0;  ||diario asiento
          #6#46 = #2#1;  ||Fecha asiento
          #6#47 = #2#47; ||documento asiento
          #6#48 = #2#38; ||inversion
          #6#49 = #2#40; ||departamento
          cuencli = #6#4;
          nivcli = #6#5;
          |HAZ clientes;
          #6#50 = nombre;
          #6#51 = cif;
          |GRABA 001#6n;
          |LIBERA #6;
     |SINO;
          mensaje = "0000Factura No: " + #6#0 + " Existente";
          |MENAV mensaje;
          pasar = 1;               ||Pasar = 1 lo mismo que en el iva
          dia = #2#0;    ||Repercutido
          docu = #2#2;
          fech = #2#1;
          #2#4 = 1;
          |LEE 101#2];
          |PARA ; |SI FSalida = 0;
                  |Y #2#0 = dia;
                  |Y #2#1 = fech;
                  |Y #2#2 = docu;
                  |Y #2#4 >0;
                       |HACIENDO;
               #2#42 = "";    ||Tipo Acumulador
               #2#43 = 0;     ||Acumulador
               |GRABA 001#2a;
               |LIBERA #2;
               |LEE 101#2s;
          |SIGUE;
     |FINSI;
|FPRC;

|PROCESO repercutido;
     pasar = 0;
     |DDEFECTO #5;
     #5#0 = #2#14;       ||Libro
     #5#2 = #2#16;       ||factura
     #5#27 = #2#17;      ||Serie
     |LEE 001#5=;
     |SI FSalida!0;
          #5#1 = librodesc;  ||descripcion libro
          #5#3 = #2#41;  ||Fecha factura
          #5#4 = #2#20;  ||Cuenta iva
          #5#5 = #2#21;  ||digito cuenta
          cuenta = #5#4;
          nivel = #5#5;
          total = 1;
          |HAZ cuentas;
          #5#6 = #2#18;  ||concepto iva
          ||#5#7 = librodescc;  ||DEscripcion concepto
          #5#7 = #2#19;  ||DEscripcion concepto LA MISMA QUE EL APUNTE!!!
          #5#8 = #2#22;  ||Base 1 COns
          #5#9 = #2#23;  ||Base 2 red
          #5#10 = #2#24; ||Base 3 lujo
          #5#52 = #2#48; ||Base 4
          #5#53 = #2#49; ||Base 5
          #5#11 = #2#25; ||% Iva 1
          #5#12 = #2#26; ||% Iva 2
          #5#13 = #2#27; ||% Iva 3
          #5#54 = #2#50; ||% Iva 4
          #5#55 = #2#51; ||% Iva 5
          #5#14 = #2#28; ||Cuota iva 1
          #5#15 = #2#29; ||Cuota iva 2
          #5#16 = #2#30; ||Cuota iva 3
          #5#56 = #2#52; ||Cuota iva 4
          #5#57 = #2#53; ||Cuota iva 5
          #5#17 = #2#31; ||% Rec 1
          #5#18 = #2#32; ||% Rec 2
          #5#19 = #2#33; ||% Rec 3
          #5#58 = #2#54; ||% Rec 4
          #5#59 = #2#55; ||% Rec 5
          #5#20 = #2#34; ||Cuota Rec 1
          #5#21 = #2#35; ||Cuota Rec 2
          #5#22 = #2#36; ||Cuota Rec 3
          #5#60 = #2#56; ||Cuota Rec 4
          #5#61 = #2#57; ||Cuota Rec 5
          #5#23 = #2#39; ||deducible
          #5#26 = #2#37; ||Total factura
          #5#44 = #2#44; ||Periodo
          #5#45 = #2#0;  ||diario asiento
          #5#46 = #2#1;  ||Fecha asiento
          #5#47 = #2#47; ||documento asiento
          #5#48 = #2#38; ||inversion
          #5#49 = #2#40; ||departamento
          cuencli = #5#4;
          nivcli = #5#5;
          |HAZ clientes;
          #5#50 = nombre;
          #5#51 = cif;
          |GRABA 001#5n;
          |LIBERA #5;
     |SINO;
          mensaje = "0000Factura No: " + #5#2 + " Existente";
          |MENAV mensaje;
          pasar = 1;     ||Si la factura existe, pasar vale 1,
          dia = #2#0;    ||con lo que en las lineas de apuntes
          docu = #2#2;   ||no se grabaran los datos del IVA
          fech = #2#1;
          #2#4 = 1;
          |LEE 101#2];
          |PARA ; |SI FSalida = 0;
                  |Y #2#0 = dia;
                  |Y #2#1 = fech;
                  |Y #2#2 = docu;
                  |Y #2#4 >0;
                       |HACIENDO;
               #2#42 = "";    ||Tipo Acumulador
               #2#43 = 0;     ||Acumulador
               |GRABA 001#2a;
               |LIBERA #2;    ||Elimino parte de los datos del IVA
               |LEE 101#2s;
          |SIGUE;
     |FINSI;
|FPRC;


|PROCESO grabadiario;
          |ABRE #7;
          #7#0 = #3#0;
          |LEE 101#7=;        ||Grabo en el diario
          |SI FSalida = 0;
               #7#2 = #7#2 + #3#4; ||total debe
               #7#3 = #7#3 + #3#5; ||total haber
               |GRABA 001#7a;
               |LIBERA #7;
          |SINO;
               #7#1 = "INEXISTENTE";
               #7#2 = #3#4;
               #7#3 = #3#5;        ||Si el diario no existe lo doy de alta
               |GRABA 001#7n;
               |LIBERA #7;
          |FINSI;
          |CIERRA #7;
|FPRC;
