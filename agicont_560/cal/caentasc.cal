|FICHEROS;
    caentasl #00;   || apuntes lins.
    caentasc #01;   || apuntes cabs.
    camaasic #02;   || Asientos Cabecera;
    casieaca #11;   || Predefinidos Cabeceras
    casieali #12;   || Predefinidos Lineas
    casieaax #13;   || Consulta Predefinidos
|FIN;

|VARIABLES;
   &fecuno = @;
   &diauno = 0;
   &documuno = 0;
   &inkey = 0;
   &esnuevo = 0;
   mensa1 = "    ERROR ESTA FECHA NO CORRESPONDE AL EJERCICIO";
   &modcon = 0;
   &entrada = 1;
   &fec1 = @;
   &fec2 = @;
    alfa1 = "";
    alfa2 = "";
    alfa3 = "";
    alfa4 = "";
    nume1 = 0;
    nume2 = 0;
    nume3 = 0;
    nume4 = 0;
    xx = 0;
    &ext1 = "";
    &ext2 = "";
    &ext3 = "";
    &ext4 = "";
    &ext5 = "";
    &predefine = 0;
    &predefine2 = 0;
    &i_serie = "";
    &i_factu = 0;
    &i_fecha = @;
    &sw_ptec = 0;
    &sw_ivamerr = 0;
    ult_pre = 0;
   posic = 0;
   lin_con = 0;
   m_c = 0;
|FIN;

|INCLUYE conasi_i;

|| *************************************************************************
|| ********************** CALCULOS DE LOS CAMPOS DE CABECERA
|| *************************************************************************

|PROCESO leedocum;
  |ABRE #20;
  |DDEFECTO #20;
  |LEE 101#20p;
  |LIBERA #20;
  |CIERRA #20;
|FPRC;

|PROCESO entrada; |TIPO 0;          || desde el campo 'diario'
  |SI esnuevo = 0;
      diauno = #1#1;
      fecuno = #1#2;
      documuno = #1#3;
      |ACABA;
  |FINSI;

  diauno = #1#1;
  |HAZ leedocum;
  |ABRE #2;
  |SELECT #3#2;
  |LEE 001#2u;
  |SI FSalida ! 0; |DDEFECTO #2; |FINSI;
     #1#1 = diauno;      || diario
     #1#2 = #2#1;        || Fecha
     #1#3 = #20#1 + 1;   || Documento
  |SELECT #1#2;
  |CIERRA #2;
  |PINTA #1#1;
  |PINTA #1#2;
  |PINTA #1#3;
|FPRC;

|PROCESO fecha; |TIPO 0;               || desde campo 'fecha'
 |SI esnuevo = 0; |ACABA; |FINSI;
 inkey = FSalida;
 |SI #1#2 < fec1; |O #1#2 > fec2;
     |MENAV mensa1;
     |ERROR;
     |ACABA;
 |FINSI;
 fecuno = #1#2;
|FPRC;

|PROCESO docum; |TIPO 0;
 |SI esnuevo = 0; |ACABA; |FINSI;
 documuno = #1#3;
|FPRC;

|| ----------------------------------------------------------------------
|PROCESO ver_pre; |TIPO 7
   #1#7 = ult_pre;
   |PINTA #1#7;
|FPRC;

|PROCESO guard_pr; |TIPO 0
  ult_pre = #1#7;
|FPRC;

|PROCESO siesta; |TIPO 6;          || desde campo 'asiento predefinido'
  inkey = FSalida;
  |SI inkey = 1; |ACABA;  |FINSI;

  |SI inkey ] 10; |Y sw_ptec ! 1;
      |ERROR;
      |PTEC 807;
      |ACABA;
  |FINSI;

  xx = (FEntrada / 100) ! 0;
  predefine = 0;
  |ABRE #11;
  #11#0 = #1#7;
  |LEE 001#11=;
  |SI FSalida ! 0;
      |SI #1#7 = 0;
          |DDEFECTO #11;
          |GRABA 020#11n;       || graba la clave 'cero'
      |FINSI;
      |ATRI I;
||      |PINTA 0928, #11#1;
      |ATRI 0;
      |CIERRA #11;
      |ACABA;
  |FINSI;
  |ATRI I;
||  |PINTA 0928, #11#1;
  |ATRI 0;
  |SI #1#7 = 0;  |CIERRA #11;  |ACABA;  |FINSI;  ||NO ES OPERATIVO EL CERO
  |SI xx ! 1;  |CIERRA #11;  |ACABA;  |FINSI;    ||SOLO SI ESTAMOS EN ALTA
  |SI inkey ! 9;|Y inkey ! 10; || en <F1> presenta el asiento
      predefine = 1;           || en <F2> sube al diario
      predefine2 = 1;          ||/(cuando se esta a¤adiendo)
      |SI #11#2 = "S";
          |HAZ pide_iva;
      |FINSI;
  |SINO;
      |SI inkey = 9;               || presenta el asiento
          |HAZ consulta;
          |ERROR;
      |FINSI;
  |FINSI;
  |CIERRA #11;
|FPRC;
|| -----------------------------------------------------------------------

|PROCESO pide_iva;       || desde calculo 'siesta' / pide datos ivas
    ext1 = #0#3;        || fecha del asiento
    ext2 = #11#4;       || libro iva
|SI #11#3 = "R";  |SUB_EJECUTA "camaasiv.tab";  |FINSI;
|SI #11#3 = "S";  |SUB_EJECUTA "camaasiw.tab";  |FINSI;
    nume1 = ext3;
|SI nume1 ! 0;                || esto es el FSalida
        predefine = 0;
        predefine2 = 0;
    |ERROR;
|SINO;
        i_serie = ext4;       || (inhibida)
        i_factu = ext5;
        i_fecha = ext1;
        sw_ptec = 0;                      ||/este 'PTEC' sobra
|FINSI;
|FPRC;

|PROCESO situate;        || desde rutina 'consulta'
|CAMPO_POSICION #13#0,1,0847;
|CAMPO_POSICION #13#1,1,0860;
|FPRC;

|PROCESO correte;        || desde subrutina 'consulta2'
|CAMPO_POSICION #13#0,0,posic; posic=posic+m_c; |CAMPO_POSICION #13#0,1,posic;
|CAMPO_POSICION #13#1,0,posic; posic=posic+m_c; |CAMPO_POSICION #13#1,1,posic;
|FPRC;

|PROCESO consulta2;      || desde rutina 'consulta' /BUCLELIN
    lin_con = lin_con + 1;
|SI #12#2 = "D";  #13#0 = #12#3;  #13#1 = "     --     ";  |FINSI;
|SI #12#2 = "H";  #13#0 = "     --     ";  #13#1 = #12#3;  |FINSI;
|PINDA #0#13;
|SI lin_con < 12;
        m_c = 100;
|SINO;
        m_c = -1100;
        lin_con = 0;
    |PAUSA;
    |PINPA #0#13;
|FINSI;
|HAZ correte;
|FPRC;

|PROCESO consulta;       || desde calculo 'siesta' /presenta predefinido
|HAZ situate;
|PUSHV 05451974;
|PUSHV 19292374;
    lin_con = 0;
|DDEFECTO #13;
|PINPA #0#13;
|PINPA #1#13;
|SI #11#2 = "S";
    |SI #11#3 = "R";  #13#2 = "IVA REPERCUTIDO";  |FINSI;
    |SI #11#3 = "S";  #13#2 = "IVA SOPORTADO  ";  |FINSI;
    |SI #11#3 = "R";|Y #11#6 = "S";  #13#3 = "/COBROS";  |FINSI;
    |SI #11#3 = "S";|Y #11#6 = "S";  #13#3 = "/PAGOS ";  |FINSI;
|FINSI;
|PINDA #1#13;
|BUCLELIN 2consulta2#11;
|SI lin_con = 0;
    |MENAV "    ATENCION!!! Cabecera sin lineas ...";
|SINO;
    |PAUSA;
|FINSI;
|POPV;
|POPV;
|FPRC;
