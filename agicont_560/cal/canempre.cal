|FICHEROS;
    canempre #0;         || para que sirvan las rutinas del 'agicli'
    canempre #1;
    agicontr #10;
    agifigen #11;
|FIN;

|VARIABLES;
    &entrada = 1;
    &fich_alta = "";
    error1 = "0000Nivel repetido o valor mas bajo que los anteriores";
    traba ="";
    traba1 = 0;
    alfa1 = "";
    alfa2 = "";
    alfa3 = "";
    alfa4 = "";
    nume0 = 0;
    nume1 = 0;
    perio = 0;
    agi = "";
    agi1 = "";
    agi2 = "";
    men6 = "    NConfirma el Alta en 'Clientes General' (S/N): ";
    con = 0;
    aste = "";
    longi = "";
    num   = 0;
    limpe1 = 0;
    limpe2 = 0;
    men1 = "2405Desde Periodo: ";
    men2 = "2440Hasta Periodo: ";
    opcion = 0;
{-1}menu = "";
    menu1 = "2400";
    menu2 = "1";
    menu3 = " Elija ordenacion del informe: ";
    menu4 = "AC";
    menu5 = " Alfabetica ";
    menu6 = " Codigos ";
    dire = "";

     letrasnif = "TRWAGMYFPDXBNJZSQVHLCKE";
     letra = "";
     final = "";
     x = "";
     nume2 = 0;
     lugar = 0;
     nifnum = 0;
     resto = 0;

    &any_pos = 0;

 &EURO = 1;
 &EURODB = 0;
 &EURODV = 0;
 &EUROTEXTO = "Cambio de Moneda";
 &swOperacion = 0;  || 0 = Lectura / 1 = Alta
 &enEmpresa = 0;
 &enPeriodo = 0;
 &eaMoneda  = "";

|FIN;

|PROCESO mayuscul;
  #0Campo = #0Campo > " ";
  |PINTA #0Campo;
|FPRC;

|PROCESO letra_0;
  |SI FSalida ! 9;  |ACABA;  |FINSI;

  alfa1 = #1Campo;   |QBT alfa1;
  x    = alfa1 % 101;
  |SI x ! "A"; |Y x ! "B"; |Y x ! "C"; |Y x ! "D"; |Y x ! "E"; |Y x ! "F";
   |Y x ! "G"; |Y x ! "P"; |Y x ! "Q"; |Y x ! "S"; |Y x ! "X";
      final = "";
      alfa2 = alfa1 % 0;
      nume1 = alfa2;
      |PARA nume2 = 1;  |SI nume2 [ nume1;  |HACIENDO nume2 = nume2 + 1;
            lugar = (nume2 * 100) + 1;
            alfa2 = alfa1 % lugar;
            |SI alfa2 = "0"; |O alfa2 = "1";|O alfa2 = "2"; |O alfa2 = "3";
             |O alfa2 = "4"; |O alfa2 = "5";|O alfa2 = "6";
             |O alfa2 = "7"; |O alfa2 = "8";|O alfa2 = "9";
                final = final + alfa2;
            |FINSI;
      |SIGUE;
      nifnum = final;
      |SI nifnum ! 0; |Y final ! "";
          resto   = nifnum $ 23;
          resto   = resto + 1;
          lugar   = (resto * 100) + 1;
          letra   = letrasnif % lugar;
          final   = ("000000000" + final + letra) % -109;
          #1Campo = final;
      |FINSI;
   |SINO;
      |MENAV "    ATENCION !!! No se calcular un C.I.F. ...";
   |FINSI;
   |PINTA #1Campo;
|FPRC;


|PROCESO clave; |TIPO 0;
  traba1 = ((#1#2*10) + #1#3);
  traba = traba1;
  traba = "0000000" + traba;
  alfa1 = traba % -106;
  #1#0 = alfa1;
|FPRC;

|PROCESO mismos; |TIPO 0;
  nume1 = (FEntrada / 100) ! 0;
  |SI #1#3 ! 1;|Y nume1 = 1;
      perio = #1#3;
      #1#3 = #1#3 - 1;
      |LEE 001#1=;
      |SI FSalida = 0;  #1#26 = #1#26 + 1;  |FINSI;
      #1#3 = perio;
      traba1 = ((#1#2*10) + #1#3);
      traba = traba1;
      traba = "0000000" + traba;
      alfa1 = traba % -106;
      #1#0 = alfa1;
      |PINDA #0#1;
  |FINSI;
|FPRC;

|PROCESO mes; |TIPO 0;
 |SI #1#11 = 1;       ||Si el mes desde es 1 entonces el hasta es 12
     #1#12 = 12;
 |SINO;
     #1#12 = #1#11 - 1; || Si no es uno menos
 |FINSI;
 |PINTA #1#12;
|FPRC;

|PROCESO nivel;|TIPO 7;
  |CAMPO_MODIFICA #1#14, 1,"S";
  |CAMPO_MODIFICA #1#15, 1,"S";
  |CAMPO_MODIFICA #1#16, 1,"S";
  |CAMPO_MODIFICA #1#17, 1,"S";
  |CAMPO_MODIFICA #1#18, 1,"S";
  |CAMPO_MODIFICA #1#19, 1,"S";
  |SI #1#13 < 6;
      |CAMPO_MODIFICA #1#19, 1,"N";
      #1#19 = 0;
      |PINTA #1#19;
      |SI #1#13 < 5;
          |CAMPO_MODIFICA #1#18, 1,"N";
          #1#18 = 0;
          |PINTA #1#18;
          |SI #1#13 < 4;
              |CAMPO_MODIFICA #1#17, 1,"N";
              #1#17 = 0;
              |PINTA #1#17;
              |SI #1#13 < 3;
                  |CAMPO_MODIFICA #1#16, 1, "N";
                  #1#16 = 0;
                  |PINTA #1#16;
                  |SI #1#13 < 2;
                      |CAMPO_MODIFICA #1#15, 1,"N";
                      #1#15 = 0;
                      |PINTA #1#15;
                  |FINSI;
              |FINSI;
          |FINSI;
      |FINSI;
  |FINSI;
|FPRC;


|PROCESO nivelrep;|TIPO 0;  || Para comprobar que no repetimos los niveles
  |SI Campo = 15;  |Y #1#15 = 0; |Y #1#13 ] 2;
      |MENAV error1;
      |ERROR;
      |ACABA;
  |FINSI;

  |SI Campo = 16;  |Y #1#16 = 0; |Y #1#13 ] 3;
      |MENAV error1;
      |ERROR;
      |ACABA;
  |FINSI;
  |SI Campo = 17;  |Y #1#17 = 0; |Y #1#13 ] 4;
      |MENAV error1;
      |ERROR;
      |ACABA;
  |FINSI;
  |SI Campo = 18;  |Y #1#18 = 0; |Y #1#13 ] 5;
      |MENAV error1;
      |ERROR;
      |ACABA;
  |FINSI;
  |SI Campo = 19;  |Y #1#19 = 0; |Y #1#13 ] 6;
      |MENAV error1;
      |ERROR;
      |ACABA;
  |FINSI;

  |SI Campo = 15; |Y #1#15 ! 0;    || Si hay dos niveles iguales o menor error
      |SI #1#15 [ #1#14;
          |MENAV error1;
          |ERROR;
          |ACABA;
      |FINSI;
  |FINSI;

  |SI Campo = 16; |Y #1#16 ! 0; || Si hay dos niveles iguales o menor error
      |SI #1#16 [ #1#14; |O #1#16 [ #1#15;
          |MENAV error1;
          |ERROR;
          |ACABA;
      |FINSI;
  |FINSI;

  |SI Campo = 17; |Y #1#17 ! 0; || Si hay dos niveles iguales o menor error
      |SI #1#17 [ #1#14; |O #1#17 [ #1#15; |O #1#17 [ #1#16;
         |MENAV error1;
         |ERROR;
         |ACABA;
      |FINSI;
  |FINSI;

  |SI Campo = 18; |Y #1#18 ! 0; || Si hay dos niveles iguales o menor error
      |SI #1#18 [ #1#14; |O #1#18 [ #1#15; |O #1#18 [ #1#16; |O #1#18 [ #1#17;
          |MENAV error1;
          |ERROR;
          |ACABA;
      |FINSI;
  |FINSI;

  |SI Campo = 19; |Y #1#19 ! 0;    || Si hay dos niveles iguales o menor error
      |SI #1#19 [ #1#14; |O #1#19 [ #1#15; |O #1#19 [ #1#16; |O #1#19 [ #1#17; |O #1#19 [ #1#18;
          |MENAV error1;
          |ERROR;
          |ACABA;
      |FINSI;
  |FINSI;
|FPRC;

|PROCESO mkdir; |TIPO 30;
  nume0 = FEntrada / 100;  nume0 = nume0 ! 0;  || alta
  ||SI nume0 ! 1;  |ACABA;   |FINSI;
  |DFICE alfa2;
  alfa3 = #1#0;    alfa3 = "000000" + #1#0;    alfa3 = alfa3 % -106;
  alfa1 = alfa2 + alfa3;
  |MKDIR alfa1;
  fich_alta = alfa1 + "/";
  any_pos = #1#26;
  enEmpresa = #canempre#2;
  enPeriodo = #canempre#3;
  swOperacion = 99;
  |SUB_EJECUTA_NP "camoneda.tab";
  |SI nume0 = 1;
      |SUB_EJECUTA "cacrebal.tab";
  |FINSI;
|FPRC;

|PROCESO rmdir; |TIPO 2;
  nume0 = FEntrada / 100;
  nume0 = nume0 ! 0;
  |SI nume0 ! 3;  |ACABA;  |FINSI;
  |DFICE alfa2;
  alfa3 = #1#0;    alfa3 = "000000" + #1#0;    alfa3 = alfa3 % -106;
  dire = "";
  |DIRECTORIO dire;
  alfa1 = dire + "bin/agirm -r " + alfa2 + alfa3;
  |SYSTEM alfa1;
|FPRC;

||*****************************************************************
||  MANTENIMIENTO GENERAL
||*****************************************************************

|PROCESO bajacli; |TIPO 1;
con = (FEntrada / 100) ! 0;

|SI con = 3;  ||Baja
  enEmpresa = #canempre#2;
  enPeriodo = #canempre#3;
  swOperacion = 2;
  |SUB_EJECUTA_NP "camoneda.tab";
  swOperacion = 0;
|FINSI;

|SI aste  = "";   |HAZ tipo_8;  |FINSI;
|SI aste  = "*";  |ACABA;       |FINSI;
|SI con ! 3;      |ACABA;       |FINSI;

|ABRE #11;
#11#0 = #0#2;
|LEE 101#11=;
|SI FSalida = 0;
    #11#23 = "N";
    |GRABA 020#11a;
|FINSI;
|LIBERA #11;
|CIERRA #11;
|FPRC;

|PROCESO altacli; |TIPO 0;
con = (FEntrada / 100) ! 0;
|SI aste  = "";   |HAZ tipo_8;  |FINSI;
|SI aste  = "*";  |ACABA;       |FINSI;
|SI con ! 1;      |ACABA;       |FINSI;

|ABRE #11;
#11#0 = #0#2;
|LEE 101#11=;
|SI FSalida = 0;                || Recoge variables del agicli
    #11#23 = "S";
    |GRABA 020#11a;             || Graba el agicli
    |HAZ pinta_cli;
|SINO;
    |CONFI men6;
    |SI FSalida = 0;
        |PUSHV 0501 2380;
        |PINPA #0#11;
        |DDEFECTO #11;
        |PINDA #0#11;
        #11#0 = #0#2;
        |PINTA #11#0;
        |ENDATOS #1#11;
        |SI FSalida = 0;
            |POPV;
            #11#23 = "S";
            |GRABA 020#11n;                 || Graba el agicli
            |HAZ pinta_cli;
        |SINO;
            |POPV;
            |ERROR;
        |FINSI;
    |FINSI;
|FINSI;
|LIBERA #11;
|CIERRA #11;
|FPRC;

|PROCESO pinta_cli;
    #0#1  = #11#2;   |PINTA #0#1;       || nombre
    #0#4  = #11#1;   |PINTA #0#4;       || razon social
    alfa1 = #11#4;   |QBF alfa1;   alfa2 = #11#5;
    alfa1 = alfa1 + ", " + alfa2;
    #0#5  = alfa1;  |PINTA #0#5;        || direccion (numero incluido)
|HAZ postal;
    #0#6  = #11#16; |PINTA #0#6;        || codigo postal
    #0#7  = #11#11; |PINTA #0#7;        || poblacion
    #0#8  = #11#13; |PINTA #0#8;        || cif
    #0#9  = #11#14; |PINTA #0#9;        || telefono
    #0#10 = #11#15; |PINTA #0#10;       || fax
|FPRC;

|PROCESO postal;
    alfa1 = #11#09;  alfa1 = "00"  + alfa1;  alfa1 = alfa1 % -102;
    alfa2 = #11#10;  alfa2 = "000" + alfa2;  alfa2 = alfa2 % -103;
    #11#16 = alfa1 + alfa2;
|FPRC;

|PROCESO detalle;
|PRINT;
|FPRC;

|DEFBUCLE codigo;
 #0#1;
 ;
     1, limpe1;
 99999, limpe2;
 e;
 NULL, detalle;
|FIN;

|DEFBUCLE nombre;
 #0#1;
 ;
     1, limpe1;
 99999, limpe2;
 e;
 NULL, NULL, NULL, NULL, NULL, detalle;
 #0#1, #0#2, #0#3;
|FIN;

|PROCESO tipo4; |TIPO 4;
  || ventana de periodo
  |PUSHV 2053 2372;
  |ATRI I;
  |CUADRO 20 53 23 72;
  |ATRI N;
  |PINTA 2154 ," Desde Periodo:   ";
  |PINTA 2254 ," Hasta Periodo:   ";
  |ATRI 0;
    E_inf = 0;
    E_sup = 9;
    limpe1 = 2170 ? 1;
    |SI FSalida ! 0; |POPV; |ACABA; |FINSI;
       E_inf = 0;
       E_sup = 9;
       limpe2 = 2270 ? 1;
    |SI FSalida ! 0; |POPV; |ACABA; |FINSI;
    |POPV;
|MENU menu;
    opcion = FSalida;
|SI opcion > 0;
    |IMPRE 0;
    |SI FSalida = 0;
        |INFOR "canempre";
        |SI opcion = 1;  |BUCLE nombre; |FINSI;
        |SI opcion = 2;  |BUCLE codigo; |FINSI;
        |PIEINF;
        |FININF;
    |SINO;
        |MENAV "    Proceso abortado ...";
    |FINSI;
    |FINIMP;
|FINSI;
|FPRC;

|PROCESO tipo_8; |TIPO 8;
  |DIRECTORIO agi;
  |QBF agi;
  |PATH_DAT #10 agi;

|ABRE #10;
#10#0 = 1;
|LEE 040#10=;
|SI FSalida ! 0; aste = "*"; |CIERRA #10; |ACABA; |FINSI;
|CIERRA #10;

aste = #10#7;
|QBF aste;
|SI aste  = "*"; |ACABA; |FINSI;

agi1 = #10#8; |QBF agi1;

longi = agi1 % A0;
num   = longi;
num   = 100 + (num - 5);
longi = num;

agi2  = (agi1 % longi) + "pan/";

|PATH_DAT #11 agi1;
|PATH_PAN #11 agi2;
|FPRC;
