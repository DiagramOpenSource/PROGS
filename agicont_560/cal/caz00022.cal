|FICHEROS;
  caz00022 #0;

  calibros #1;
  caivaasi #2;
  camaasil #3;
  caentasl #4;   || No debe haber datos aqui.
  casieaca #5;
  cavengca #6;

  caiivrel #7;
  caiivsol #8;
  caivatmp #9;    ||Temporal de IVA

  caivasop #11;
  camanpag #12;    || pagos
  capaglin #13;    || pagares lins.
  camingli #14;    || confirmings lins.

  caivarep #21;
  camancob #22;    || cobros
  caremesl #23;    || remesas lins.

  catmppag #112;   || tmp. pagos
  catmpcob #122;   || tmp. cobros

  espejo@  #998;
  caw00022 #999;    ||Auxiliar de Cambio

  caselem1 #990;     ||Seleccion de empresas
|FIN;

|VARIABLES;
 nCampo = 0;
 aAlfa1 = "";
 aAlfa2 = "";
 aAlfa3 = "";
 nNume1 = 0;
 nNume2 = 0;
 nNume3 = 0;
 nPara1 = 0;
 nPara2 = 0;
 nSwError = 0;
 nSwPreci = 0;
 nSwRenum = 0;
 nInkey = 0;
 nswpase = 0;
 nNocopio = 0;
 nSwAlguno = 0;

 nLibro  = 0;
 nLibroD = 0;
 aTipo   = "";
 aDesLib = "";
 nNumFacO = 0;
 nNumFacD = 0;
 aSerFacO = "";
 aSerFacD = "";
 nUltm    = 0;
 nSw      = 0;
 aDirDef  = "";
 aMas     = "";
 aFichero = "";
 aParam   = "";

 dirfich0 = "";
 aDirFich = "";
|FIN;

|INCLUYE acceso_i;

|| **************************************************************************
|| ***************** Proceso de Pantalla ************************************
|| **************************************************************************

|CALCULO ElTipo8; |TIPO 8;
 aParam = PARAMETRO; |QBF aParam;
 |HAZ inicio;
|FCAL;

|CALCULO Quehacer; |TIPO 0;

 aAlfa1 = #0Campo;
 nNume1 = Campo + 30;
 |PARA nPara1 = Campo + 1; |SI nPara1 [ nNume1; |HACIENDO nPara1 = nPara1 + 1;
       |C_M #0nPara1, 1, aAlfa1;
 |SIGUE;
 |SI aAlfa1 = "N";
     |SI Campo = 0;
         |PDEFECTO #0,  1, 31;
     |SINO;
         |PDEFECTO #0, 32, 62;
     |FINSI;
     |PINDA #0#0;
 |FINSI;
|FCAL;

|CALCULO NoMasLibros; |TIPO 0;

    |SI Campo [ 10;  nNume3 = 10; |SINO; nNume3 = 41; |FINSI;
    nNume1 = Campo + 10; nNume2 = Campo + 20;

    |SI #0Campo = 0;
         |C_M #0nNume1, 1, "N";  #0nNume1 = 0;  |PINTA #nNume1;
         |C_M #0nNume2, 1, "N";  #0nNume2 = ""; |PINTA #nNume2;
         |PARA  nCampo = Campo + 1;  |SI nCampo [ nNume3;  |HACIENDO nCampo = nCampo + 1;
                |C_M #0nCampo, 1, "N";  #0nCampo = 0;  |PINTA #0nCampo;
                nNume1 = nCampo + 10; nNume2 = nCampo + 20;
                |C_M #0nNume1, 1, "N";  #0nNume1 = 0;  |PINTA #nNume1;
                |C_M #0nNume2, 1, "N";  #0nNume2 = ""; |PINTA #nNume2;
         |SIGUE;
     |SINO;
         |C_M #0nNume1, 1, "S";
         |C_M #0nNume2, 1, "S";
         |PARA  nCampo = Campo + 1;  |SI nCampo [ nNume3;  |HACIENDO nCampo = nCampo + 1;
                |C_M #0nCampo, 1, "S";
                nNume1 = nCampo + 10; nNume2 = nCampo + 20;
                |C_M #0nNume1, 1, "S";
                |C_M #0nNume2, 1, "S";
         |SIGUE;
    |FINSI;
|FCAL;


|| ... Libro de IVA Soportado

|CALCULO LOrigenS; |TIPO 0;
  nswpase = 0;
 |SI #0Campo = 0; |ACABA; |FINSI;

 nSwError = 0;
 |ABRE #1;
 #1#0 = #0Campo;
 |LEE 001#1=;
 |SI FSalida ! 0;
     |MENAV "    Atencion. No existe libro de IVA";
     nSwError = 1;
 |SINO;
     |SI #1#2 ! "S";
         |MENAV "    ATENCION!!! Libro de I.V.A. Repercutido ... ";
         nSwError = 1;
     |FINSI;
 |FINSI;
 |CIERRA #1;

 |SI nSwError = 1;  |ERROR;  |ACABA;  |FINSI;

 nSwError = 0;
 |SI Campo > 1;
     |PARA nCampo = Campo - 1; |SI nCampo ] 1; |HACIENDO nCampo = nCampo - 1;
           |SI #0nCampo = #0Campo;
               |MENAV "    ATENCION! Libro de IVA Soportado YA introducido";
               nSwError = 1;
           |FINSI;
     |SIGUE;
 |FINSI;

 nNume2 = Campo + 20; #0nNume2 = #1#1; |PINTA #0nNume2;
 nNume2 = Campo + 61; #0nNume2 = #1#3;
 nNume2 = Campo + 71; #0nNume2 = #1#4;

 |SI nSwError = 1;  |ERROR;  |ACABA;  |FINSI;

|FCAL;


|CALCULO LDestinoS; |TIPO 0;
 |C_M #0Campo,0,aAlfa1;
 |SI aAlfa1 = "N"; |ACABA; |FINSI;

 nInkey = FSalida;
 |SI nInkey = 2; |ACABA; |FINSI;

 |SI #0Campo = 0;
     |ERROR;
     |ACABA;
 |FINSI;

 nNume1 = Campo - 10;
 |SI #0Campo = #0nNume1;
     |MENAV "    Atencion. Ha introducido el mismo numero de Libro";
     |ERROR;
     |ACABA;
 |FINSI;

 nSwError = 0;
 |SI Campo > 11;
     |PARA nCampo = Campo - 1; |SI nCampo ] 11; |HACIENDO nCampo = nCampo - 1;
           |SI #0nCampo = #0Campo;
               |MENAV "    ATENCION! Numero de Libro Ya introducido. ?";
               nSwError = 1;
               |SAL;
           |FINSI;
     |SIGUE;
 |FINSI;
 |SI nSwError = 1; |ERROR; |FINSI;
|FCAL;


|| ... Libro de IVA Repercutido

|CALCULO Espreciso; |TIPO 7;

  |ABRE #1;
  nSwError = 0;
  nNume1   = 31;
  |PARA nPara1 = 11; |SI nPara1 [ 20; |HACIENDO nPara1 = nPara1 + 1;
        |SI #0nPara1 ! 0;
            #1#0 = #0nPara1;
            |LEE 001#1=;
            |SI FSalida = 0; |Y  #1#2 = "R";
                nNocopio = 0;
                |PARA nCampo = nNume1; |SI nCampo ] 32; |HACIENDO nCampo = nCampo - 1;
                      |SI #0nCampo = #0nPara1;
                          nNocopio = 1;
                      |FINSI;
                |SIGUE;
                |SI nNocopio = 0;
                    nNume1 = nNume1 + 1;
                    #0nNume1 = #0nPara1; |PINTA #0nNume1;
                    nSwError = 1;
                |FINSI;
            |FINSI;
        |SINO;
            |SAL;
        |FINSI;
  |SIGUE;
  |CIERRA #1;
  |SI nSwError = 1;
      #0#31 = "S"; |PINTA #0#31;
      |SI nswpase = 0;
          |PTEC 802;
          nswpase = 1;
      |FINSI;
  |FINSI;
|FCAL;

|CALCULO LOrigenR; |TIPO 0;

 |SI #0Campo = 0; |ACABA; |FINSI;

 nSwError = 0;
 |ABRE #1;
 #1#0 = #0Campo;
 |LEE 001#1=;
 |SI FSalida ! 0;
     |MENAV "    Atencion. No existe libro de IVA";
     nSwError = 1;
 |SINO;
     |SI #1#2 ! "R";
         |MENAV "    ATENCION!!! Libro de I.V.A. Soportado ... ";
         nSwError = 1;
     |FINSI;
 |FINSI;
 |CIERRA #1;

 |SI nSwError = 1;  |ERROR;  |ACABA;  |FINSI;

 nSwError = 0;
 |SI Campo > 32;
     |PARA nCampo = Campo - 1; |SI nCampo ] 32; |HACIENDO nCampo = nCampo - 1;
           |SI #0nCampo = #0Campo;
               |MENAV "    ATENCION! Libro de IVA Repercutido YA introducido";
               nSwError = 1;
           |FINSI;
     |SIGUE;
 |FINSI;

 nNume2 = Campo + 20; #0nNume2 = #1#1; |PINTA #0nNume2;
 nNume2 = Campo + 50; #0nNume2 = #1#3;
 nNume2 = Campo + 60; #0nNume2 = #1#4;

 |SI nSwError = 1;  |ERROR;  |ACABA;  |FINSI;

|FCAL;


|CALCULO LDestinoR; |TIPO 0;
 |C_M #0Campo,0,aAlfa1;
 |SI aAlfa1 = "N"; |ACABA; |FINSI;

 nInkey = FSalida;
 |SI nInkey = 2; |ACABA; |FINSI;

 |SI #0Campo = 0;
     |ERROR;
     |ACABA;
 |FINSI;

 nNume1 = Campo - 10;
 |SI #0Campo = #0nNume1;
     |MENAV "    Atencion. Ha introducido el mismo numero de Libro";
     |ERROR;
     |ACABA;
 |FINSI;

 nSwError = 0;
 |SI Campo > 42;
     |PARA nCampo = Campo - 1; |SI nCampo ] 42; |HACIENDO nCampo = nCampo - 1;
           |SI #0nCampo = #0Campo;
               |MENAV "    ATENCION! Numero de Libro Ya introducido.";
               nSwError = 1;
               |SAL;
           |FINSI;
     |SIGUE;
 |FINSI;

 nSwError = 0;
 |PARA nCampo = 11; |SI nCampo [ 20; |HACIENDO nCampo = nCampo + 1;
       |SI #0nCampo ! 0;
           |SI #0nCampo = #0Campo;
               |MENAV "    ATENCION! Numero de Libro Ya introducido como IVA Soportado ";
               nSwError = 1;
               |SAL;
           |FINSI;
       |FINSI;
 |SIGUE;

 |SI nSwError = 1; |ERROR; |FINSI;
|FCAL;

|| **************************************************************************
|PROCESO intrLin;
 nSwError = 1;
 |ERROR10;
|FPRC;

|DEFBUCLE intrRep;
 #7#1;
 ;
 nLibro, 001;
 nLibro, 999;
 ;
 NULL, intrLin;
|FIN;

|DEFBUCLE intrSop;
 #8#1;
 ;
 nLibro, 001;
 nLibro, 999;
 ;
 NULL, intrLin;
|FIN;

|| **************************************************************************
|| **************************************************************************
|PROCESO elUltimo;
 nUltm = #espejo@#2;
|FPRC;

|DEFBUCLE BuscaUltimo;
 #espejo@#1003;
 ;
 nLibroD, 000001, #9#27;
 nLibroD, 999999, #9#27;
 ;
 NULL, elUltimo;
|FIN;

|PROCESO GrAxCambio;
 #999#0 = aTipo;
 #999#1 = nLibro;
 #999#2 = nNumFacO;
 #999#3 = aSerFacO;
 #999#4 = nLibroD;
 #999#5 = nNumFacD;
 #999#6 = aSerFacD;
 |GRABA 020#999n;
 nSwAlguno = 1;
|FPRC;
|| **************************************************************************

|PROCESO RenumReper;

 |SI nSw = 0; |O #9#27 ! aSerFacO;
     |BUCLE BuscaUltimo;
     nSw = 1;
 |FINSI;
 aSerFacO = #9#27;
 nUltm    = nUltm + 1;

 |ABRE #espejo@

 |PARA nPara2 = 3; |SI nPara2 [ 65; |HACIENDO nPara2 = nPara2 + 1;
       #espejo@#nPara2# = #9nPara2;
 |SIGUE;
 #espejo@#0  = nLibroD;
 #espejo@#1  = aDesLib;
 #espejo@#2  = nUltm;
 |GRABA 020#espejo@.b;

 nNumFacO = #9#2;  nNumFacD = #espejo@#2;
 aSerFacO = #9#27; aSerFacD = aSerFacO;
 |HAZ GrAxCambio;

 |CIERRA #espejo@;
|FPRC;

|DEFBUCLE elTempRep;
 #9#3;
 ;
 nLibro, "  ", 000001;
 nLibro, "zz", 999999;
 ;
 NULL, RenumReper;
|FIN;


|PROCESO LRepercutido;
 #espejo@#0  = nLibroD;
 #espejo@#2  = #21#2;
 #espejo@#27 = #21#27;
 |LEE 101#espejo@.=;
 |SI FSalida ! 0;
     |PARA nPara2 = 2; |SI nPara2 [ 65; |HACIENDO nPara2 = nPara2 + 1;
           #espejo@#nPara2# = #21nPara2;
     |SIGUE;
     #espejo@#0 = nLibroD;
     #espejo@#1 = aDesLib;
     |GRABA 020#espejo@.b;
     nNumFacO = #espejo@#2;  nNumFacD = nNumFacO;
     aSerFacO = #espejo@#27; aSerFacD = aSerFacO;
     |HAZ GrAxCambio;
 |SINO;
     || ... Si ya existe guardamos para hacer renumeracion
     |PARA nPara2 = 0; |SI nPara2 [ 65; |HACIENDO nPara2 = nPara2 + 1;
           #9nPara2 = #21nPara2;
     |SIGUE;
     |GRABA 020#9n;
     nSwRenum = 1;
 |FINSI;
 |BORRA 020#21a;

 |LIBERA #espejo@;
 |LIBERA #21;
|FPRC;

|DEFBUCLE LRepercutido;
 #21#1;
 ;
 nLibro, 000001, "  ";
 nLibro, 999999, "zz";
 be;
 NULL, LRepercutido;
|FIN;

|PROCESO PaseReper;
 nSwError = 0;

 aFichero = ("0rw" + Usuario) % 108;
 |NOME_DAT #9 aFichero;
 |DELINDEX #9;

 |DDEF aDirDef;
 aMas  = aDirDef + "caivarep.mas";
 |CARGA_DEF aMas, espejo@;
 |SI FSalida ! 0;
     |MENAV "    Error Cargado Fichero de IVA Repercutido. Proceso Interrumpido";
     nSwError = 1;
     |ACABA;
 |FINSI;

 |PATH_DAT #998 dirfich0;

 aTipo = "R";
 |ABRE #999;
 |PARA nPara1 = 32; |SI nPara1 [ 41; |HACIENDO nPara1 = nPara1 + 1;
       |SI #0nPara1 ! 0;
           nLibro  = #0nPara1;
           nNume1  = nPara1 + 10; nLibroD = #0nNume1;
           nNume2  = nPara1 + 20; aDesLib = #0nNume2;
           |ABRE #9;
           |ABRE #espejo@;
           nSwRenum = 0;
           |BUCLE LRepercutido;
           |CIERRA #espejo@;
           |CIERRA #9;
           |SI nSwRenum = 1;  || Buscaremos nuevo numero
               nSw   = 0;
               nUltm = 0;
               |BUCLE elTempRep;
           |FINSI;
       |FINSI;
 |SIGUE;
 |CIERRA #999;

 |DESCARGA_DEF #espejo@;

 |DELINDEX #9;
|FPRC;

|| **************************************************************************
|PROCESO RenumSopor;

 |SI nSw = 0; |O #9#27 ! aSerFacO;
     |BUCLE BuscaUltimo;
     nSw = 1;
 |FINSI;
 aSerFacO = #9#27;
 nUltm    = nUltm + 1;

 |ABRE #espejo@

 |PARA nPara2 = 3; |SI nPara2 [ 61; |HACIENDO nPara2 = nPara2 + 1;
       #espejo@#nPara2# = #9nPara2;
 |SIGUE;
 #espejo@#0  = nLibroD;
 #espejo@#1  = aDesLib;
 #espejo@#2  = nUltm;
 #espejo@#63 = #9#63;
 #espejo@#62 = #9#64;
 #espejo@#65 = #9#65;
 #espejo@#64 = #9#66;
 |GRABA 020#espejo@.b;

 nNumFacO = #9#2;  nNumFacD = #espejo@#2;
 aSerFacO = #9#27; aSerFacD = aSerFacO;
 |HAZ GrAxCambio;

 |CIERRA #espejo@;
|FPRC;

|DEFBUCLE elTempSop;
 #9#3;
 ;
 nLibro, "  ", 000001;
 nLibro, "zz", 999999;
 ;
 NULL, RenumSopor;
|FIN;


|PROCESO LSoportado;
 #espejo@#0  = nLibroD;
 #espejo@#2  = #11#2;
 #espejo@#27 = #11#27;
 |LEE 101#espejo@.=;
 |SI FSalida ! 0;
     |PARA nPara2 = 2; |SI nPara2 [ 65; |HACIENDO nPara2 = nPara2 + 1;
           #espejo@#nPara2# = #11nPara2;
     |SIGUE;
     #espejo@#0 = nLibroD;
     #espejo@#1 = aDesLib;
     |GRABA 020#espejo@.b;
     nNumFacO = #espejo@#2;  nNumFacD = nNumFacO;
     aSerFacO = #espejo@#27; aSerFacD = aSerFacO;
     |HAZ GrAxCambio;
 |SINO;
     || ... Si ya existe guardamos para hacer renumeracion
     |PARA nPara2 = 0; |SI nPara2 [ 61; |HACIENDO nPara2 = nPara2 + 1;
           #9nPara2 = #11nPara2;
     |SIGUE;
     #9#63 = #11#63;
     #9#64 = #11#62;
     #9#65 = #11#65;
     #9#66 = #11#64;
     |GRABA 020#9n;
     nSwRenum = 1;
 |FINSI;
 |BORRA 020#11a;

 |LIBERA #espejo@;
 |LIBERA #11;
|FPRC;

|DEFBUCLE LSoportado;
 #11#1;
 ;
 nLibro, 000001, "  ";
 nLibro, 999999, "zz";
 be;
 NULL, LSoportado;
|FIN;

|PROCESO PaseSopor;
 nSwError = 0;

 aFichero = ("0sw" + Usuario) % 108;
 |NOME_DAT #9 aFichero;
 |DELINDEX #9;

 |DDEF aDirDef;
 aMas  = aDirDef + "caivasop.mas";
 |CARGA_DEF aMas, espejo@;
 |SI FSalida ! 0;
     |MENAV "    Error Cargado Fichero de IVA Soportado. Proceso Interrumpido";
     nSwError = 1;
     |ACABA;
 |FINSI;

 |PATH_DAT #998 dirfich0;

 aTipo = "S";
 |ABRE #999;
 |PARA nPara1 = 1; |SI nPara1 [ 10; |HACIENDO nPara1 = nPara1 + 1;
       |SI #0nPara1 ! 0;
           nLibro  = #0nPara1;
           nNume1  = nPara1 + 10; nLibroD = #0nNume1;
           nNume2  = nPara1 + 20; aDesLib = #0nNume2;
           |ABRE #9;
           |ABRE #espejo@;
           nSwRenum = 0;
           |BUCLE LSoportado;
           |CIERRA #espejo@;
           |CIERRA #9;
           |SI nSwRenum = 1;  || Buscaremos nuevo numero
               nSw   = 0;
               nUltm = 0;
               |BUCLE elTempSop;
           |FINSI;
       |FINSI;
 |SIGUE;
 |CIERRA #999;

 |DESCARGA_DEF #espejo@;
 |DELINDEX #9;
|FPRC;
|| *************************************************************************
|| *************************************************************************
|PROCESO Casieaca;
 #5#4 = nLibroD;
 |GRABA 020#5a;
 |LIBERA #5;
|FPRC;

|DEFBUCLE Casieaca;
 #5#1;
 2,3,4;
 0001, "S", aTipo, nLibro;
 9999, "S", aTipo, nLibro;
 be;
 NULL, Casieaca;
|FIN;

|PROCESO Cavengca;
 #6#1 = nLibroD;
 |GRABA 020#6a;
 |LIBERA #6;
|FPRC;

|DEFBUCLE Cavengca;
 #6#1;
 1;
 "01.01.1900", nLibro;
 "31.12.2099", nLibro;
 be;
 NULL, Cavengca;
|FIN;

|PROCESO CambiaFich1;

  |BUCLE Casieaca;

  |SI aTipo = "R";
      |BUCLE Cavengca;
  |FINSI;

  |ABRE #2;
  #2#0 = 1;
  |LEE 101#2=;
  |SI FSalida = 0;
      |SI aTipo = "R"; |Y #2#3 = nLibro;
          #2#3 = nLibroD;
      |FINSI;
      |SI aTipo = "S"; |Y #2#29 = nLibro;
          #2#29 = nLibroD;
      |FINSI;
      |GRABA 020#2a;
  |FINSI;
  |LIBERA #2;
  |CIERRA #2;
|FPRC;
|| *************************************************************************
|| *************************************************************************
|PROCESO GrabaCobros;

 |DDEFECTO #22;
 |PARA nPara2 = 0; |SI nPara2 [ 28; |HACIENDO nPara2 = nPara2 + 1;
       #22nPara2 = #122nPara2;
 |SIGUE;
 |GRABA 020#22n;
|FPRC;

|DEFBUCLE GrabaCobros;
 #122#1;
 ;
 00, 000001, "  ", 001;
 99, 999999, "zz", 999;
 ;
 NULL, GrabaCobros;
|FIN;

||...................................
|PROCESO LeeCobros;
 #999#0 = "R";
 #999#1 = #22#0;
 #999#2 = #22#1;
 #999#3 = #22#2;
 |LEE 001#999=;
 |SI FSalida = 0;
     |DDEFECTO #122;
     |PARA nPara2 = 0; |SI nPara2 [ 28; |HACIENDO nPara2 = nPara2 + 1;
           #122nPara2 = #22nPara2;
     |SIGUE;
     #122#0 = #999#4;
     #122#1 = #999#5;
     #122#2 = #999#6;
     |GRABA 020#122n;

     |BORRA 020#22a;
 |FINSI;
 |LIBERA #22;
|FPRC;

|DEFBUCLE LeeCobros;
 #22#1;
 ;
 00, 000001, "  ", 001;
 99, 999999, "zz", 999;
 be;
 NULL, LeeCobros;
|FIN;

|| *************************************************************************
|PROCESO GrabaPagos;

 |DDEFECTO #12;
 |PARA nPara2 = 0; |SI nPara2 [ 26; |HACIENDO nPara2 = nPara2 + 1;
       #12nPara2 = #112nPara2;
 |SIGUE;
 |GRABA 020#12n;
|FPRC;

|DEFBUCLE GrabaPagos;
 #112#1;
 ;
 00, 000001, "  ", 001;
 99, 999999, "zz", 999;
 ;
 NULL, GrabaPagos;
|FIN;

||...................................
|PROCESO LeePagos;
 #999#0 = "S";
 #999#1 = #12#0;
 #999#2 = #12#1;
 #999#3 = #12#2;
 |LEE 001#999=;
 |SI FSalida = 0;
     |DDEFECTO #112;
     |PARA nPara2 = 0; |SI nPara2 [ 26; |HACIENDO nPara2 = nPara2 + 1;
           #112nPara2 = #12nPara2;
     |SIGUE;
     #112#0 = #999#4;
     #112#1 = #999#5;
     #112#2 = #999#6;
     |GRABA 020#112n;

     |BORRA 020#12a;
 |FINSI;
 |LIBERA #12;
|FPRC;

|DEFBUCLE LeePagos;
 #12#1;
 ;
 00, 000001, "  ", 001;
 99, 999999, "zz", 999;
 be;
 NULL, LeePagos;
|FIN;

|| *************************************************************************
|PROCESO Caremesl;
 #999#0 = "R";
 #999#1 = #23#2;
 #999#2 = #23#3;
 #999#3 = #23#4;
 |LEE 001#999=;
 |SI FSalida = 0;
     #23#2 = #999#4;
     #23#3 = #999#5;
     #23#4 = #999#6;
     |GRABA 020#23a;
 |FINSI;
 |LIBERA #23;
|FPRC;

|DEFBUCLE Caremesl;
 #23#1;
 ;
 00001, 001;
 99999, 999;
 be;
 NULL, Caremesl;
|FIN;

|PROCESO Camingli;
 #999#0 = "S";
 #999#1 = #14#2;
 #999#2 = #14#3;
 #999#3 = #14#4;
 |LEE 001#999=;
 |SI FSalida = 0;
     #14#2 = #999#4;
     #14#3 = #999#5;
     #14#4 = #999#6;
     |GRABA 020#14a;
 |FINSI;
 |LIBERA #14;
|FPRC;

|DEFBUCLE Camingli;
 #14#1;
 ;
 00001, 001;
 99999, 999;
 be;
 NULL, Camingli;
|FIN;

|PROCESO Capaglin;
 #999#0 = "S";
 #999#1 = #13#2;
 #999#2 = #13#3;
 #999#3 = #13#9;
 |LEE 001#999=;
 |SI FSalida = 0;
     #13#2 = #999#4;
     #13#3 = #999#5;
     #13#9 = #999#6;
     |GRABA 020#13a;
 |FINSI;
 |LIBERA #13;
|FPRC;

|DEFBUCLE Capaglin;
 #13#1;
 ;
 0000001, 01;
 9999999, 99;
 be;
 NULL, Capaglin;
|FIN;

|PROCESO LosAsientos;
 aTipo = #3#12; |QBF aTipo;

 |SI aTipo ! "";
     |SI aTipo = "C"; aTipo = "R"; |FINSI;
     |SI aTipo = "P"; aTipo = "S"; |FINSI;

     |SI aTipo = "R"; |O aTipo = "S";
         #999#0 = aTipo;
         #999#1 = #3#13;
         #999#2 = #3#14;
         #999#3 = #3#15;
         |LEE 001#999=;
         |SI FSalida = 0;
             #3#13 = #999#4;
             #3#14 = #999#5;
             #3#15 = #999#6;
             |GRABA 020#3a;
         |FINSI;
    |FINSI;
 |FINSI;
 |LIBERA #3;
|FPRC;

|DEFBUCLE LosAsientos;
 #3#1;
 ;
 00, fec1, 000001, 0001;
 99, fec2, 999999, 9999;
 be;
 NULL, LosAsientos;
|FIN;

|| *************************************************************************

|PROCESO ElCambio;

 nSwError = 0;
 |SI #0#0 = "S";
     |PARA nPara1 = 1; |SI nPara1 [ 10; |HACIENDO nPara1 = nPara1 + 1;
           |SI #0nPara1 ! 0;
               nLibro = #0nPara1;
               |BUCLE intrSop;
               |SI nSwError = 1;
                   |MENAV "    No puedo realizar el cambio porque hay registro de IVA por Actualizar";
                   |SAL;
               |FINSI;
           |FINSI;
     |SIGUE;
 |FINSI;
 |SI nSwError = 1; |ACABA; |FINSI;
 nSwError = 0;
 |SI #0#31 = "S";
     |PARA nPara1 = 32; |SI nPara1 [ 41; |HACIENDO nPara1 = nPara1 + 1;
           |SI #0nPara1 ! 0;
               nLibro = #0nPara1;
               |BUCLE intrRep;
               |SI nSwError = 1;
                   |MENAV "    No puedo realizar el cambio porque hay registro de IVA por Actualizar";
                   |SAL;
               |FINSI;
           |FINSI;
     |SIGUE;
 |FINSI;
 |SI nSwError = 1; |ACABA; |FINSI;

 |NOPARA;

 aFichero  = ("22w" + Usuario) % 108;
 |NOME_DAT #999 aFichero;
 |DELINDEX #999;

 nSwError = 0;
 nSwAlguno = 0;

 |SI #0#0  = "S";  |HAZ PaseSopor;  |FINSI;
 |SI nSwError = 1; |ACABA; |FINSI;
 |SI #0#31 = "S";  |HAZ PaseReper;  |FINSI;
 |SI nSwError = 1; |ACABA; |FINSI;

 || .... .... .... .... .... .... .... ....  Cambio Libro de IVA
 |ABRE #1;
 |PARA nPara1 = 11; |SI nPara1 [ 51; |HACIENDO nPara1 = nPara1 + 1;

       |SI #0nPara1 ! 0;

           nLibroD = #0nPara1;
           nNume1  = nPara1 - 10;
           nLibro  = #0nNume1;            || ... Libro Anterior
           aTipo   = "S"; |SI nPara1 > 20; aTipo = "R"; |FINSI;

           #1#0 = #0nPara1;
           |LEE 101#1=;
           |SI FSalida ! 0;
               |DDEFECTO #1;
               #1#0 = #0nPara1;
               |GRABA 020#1b;
           |FINSI;
           nNume1 = nPara1 + 10;
           nNume2 = nPara1 + 51;
           nNume3 = nPara1 + 61;
           |SI nPara1 > 20;
               nNume2 = nPara1 + 40;
               nNume3 = nPara1 + 50;
           |FINSI;
           #1#1 = #0nNume1;
           #1#2 = aTipo;
           #1#3 = #0nNume2;
           #1#4 = #0nNume3;
           |GRABA 020#01a;
           |HAZ CambiaFich1;  ||ficheros que solo tiene el libro
       |FINSI;
       |SI nPara1 = 20; nPara1 = 41; |FINSI;
 |SIGUE;
 |CIERRA #1;

|| /////////////////////////////////////////////////////////////////
 |SI nSwAlguno = 1;
    || ... Cambio los Cobros y los Pagos.

     |ABRE #999;

     aFichero = ("0pw" + Usuario) % 108;
     |NOME_DAT #112 aFichero;
     |DELINDEX #112;
     |ABRE #112;
     |BUCLE LeePagos;
     |CIERRA #112;
     |ABRE #12;
     |BUCLE GrabaPagos;
     |CIERRA #12;
     |DELINDEX #112;

     aFichero = ("0cw" + Usuario) % 108;
     |NOME_DAT #122 aFichero;
     |DELINDEX #122;
     |ABRE #122;
     |BUCLE LeeCobros;
     |CIERRA #122;
     |ABRE #22;
     |BUCLE GrabaCobros;
     |CIERRA #22;
     |DELINDEX #122;

    || ... Cambio resto de ficheros

     |BUCLE LosAsientos;
     |BUCLE Capaglin;
     |BUCLE Camingli;
     |BUCLE Caremesl;

     |CIERRA #999;
 |FINSI;

 |DELINDEX #999;

 |SIPARA;
|FPRC;


|| *************************************************************************
|CALCULO ElTipo3; |TIPO 3;
   |SI aParam = "S";
       |HAZ MultiEmpresa;
   |SINO;
       |DFICE dirfich0; |QBF dirfich0;
       |HAZ ElCambio;
   |FINSI;
|FCAL;

|| *****************************************************************
|PROCESO MultiEmp1;
   cod1 = #caselem1#1;
   cod2 = #caselem1#2;
   emEmp = 1;  || Sin mensaje
   |HAZ leelo;
   |SI swerror ! 0;  |ACABA;   |FINSI;

   |DFICB aDirFich;
   aAlfa1 = #caselem1#1; |QBF aAlfa1; aAlfa1 = ("00000" + aAlfa1 ) % -105;
   aAlfa2 = #caselem1#2; |QBF aAlfa2; aAlfa2 = ("0"     + aAlfa2 ) % -101;
   aAlfa3 = aAlfa1 + aAlfa2 + "/";
   dirfich0 = aDirFich + "/" + aAlfa3;
   |QBF dirfich0;

   |PATH_DAT #1 dirfich0;
   |PATH_DAT #2 dirfich0;
   |PATH_DAT #3 dirfich0;
   |PATH_DAT #4 dirfich0;
   |PATH_DAT #5 dirfich0;
   |PATH_DAT #6 dirfich0;
   |PATH_DAT #7 dirfich0;
   |PATH_DAT #8 dirfich0;
   |PATH_DAT #9 dirfich0;
   |PATH_DAT #11 dirfich0;
   |PATH_DAT #12 dirfich0;
   |PATH_DAT #13 dirfich0;
   |PATH_DAT #14 dirfich0;
   |PATH_DAT #21 dirfich0;
   |PATH_DAT #22 dirfich0;
   |PATH_DAT #23 dirfich0;
   |PATH_DAT #112 dirfich0;
   |PATH_DAT #122 dirfich0;

   |HAZ ElCambio;
|FPRC;

|DEFBUCLE MultiEmp0;
 #caselem1#1;
 ;
 001;
 999;
 ;
 NULL,MultiEmp1;
|FIN;

|PROCESO MultiEmpresa;
   |SUB_EJECUTA "caselemp";
   |BUCLE MultiEmp0;
|FPRC;
