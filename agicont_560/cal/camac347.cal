|FICHEROS;
  camac347 #0;
  camal347 #1;
  eosclipr #2,MANTE;
  cauti347 #3;
  agipobla #4;
  agiprovi #5;
  caclipro #6;
  agim0016 #7;
  canempre #30;
  agicontr #23;
|FIN;

|VARIABLES;
 mod       = 0;
 men1      = "0000SEs correcto S/N : ";
 men2      = "0000NLo Grabo en el Fichero General : ";
 men6      = "    NConfirma el Alta en Fichero General (S/N): ";
 error1    = "     La empresa no tiene impreso del 347 ";
 verrfec   = "0000ATENCION !! Fecha erronea o formato incorrecto ";
 vdia1     = "";
 vme1      = "";
 vany1     = "";
 vdia      = 0;
 vme       = 0;
 vany      = 0;
 vtope     = 0;
 vsw       = 0;
 vfec      = "";
 vblanco   = "          ";
 vpuntos   = "..........";
 v         = 0;
 vacio     = "                                        ";
 cliente   = "";
 cuarenta  = "                                        ";


     letrasnif = "TRWAGMYFPDXBNJZSQVHLCKE";
     letra = "";
     alfa1 = "";
     alfa2 = "";
     final = "";
     x = "";
     nume1 = 0;
     nume2 = 0;
     lugar = 0;
     nifnum = 0;
     resto = 0;
 &elcampo0 = "";
 &elcampo1 = "";
 &elcampo2 = "";
 &elcampo3 = "";
 &elcampo4 = "";
 &elcampo5 = 0;
 &elcampo6 = 0;
 &elcampo7 = "";
 &elcampo8 = "";
 &swentra  = 0;
 &swmodi   = 0;
 swmod   = 0;
 directo = "";
 direct1 = "";
 traba = "";
 dprocli = "";
 eselbase = "";
 salida = 0;
   &aste = "";
   agi = "";
   &agi1 = "";
   &agi2 = "";
   num = 0;
   longi = "";
|FIN;

|INCLUYE acceso_i;

|PROCESO alentrar;  |TIPO 8;
  |HAZ inicio;
  |HAZ tipo_8; || comprueba la existencia de la ficha general
  |SI aste ! "*";
      |PATH_DAT #2 agi1;
      |PATH_DAT #4 agi1;
      |PATH_DAT #5 agi1;
      |PATH_DAT #7 agi1;

      |PATH_PAN #2 agi2;
      |PATH_PAN #4 agi2;
      |PATH_PAN #5 agi2;
      |PATH_PAN #7 agi2;
  |FINSI;
|FPRC;

|PROCESO antesque;  |TIPO 7;
  |ABRE #30;
  #30#2 = cod1;
  #30#3 = cod2;
  |LEE 040#30=;
  #0#0 = #30#2;
  #0#1 = #30#1;
  |PINTA #0#0;
  |PINTA #0#1;
  |CIERRA #30;
|FPRC;

|PROCESO despuesde;
  |SI #0#0 ! cod1;
      |ERROR;
      #0#0 = cod1;
      |PINTA #0#0;
      |ACABA;
  |FINSI;
|FPRC;

|PROCESO mayor11;
  alfa1 = #0Campo > " ";
  #0Campo = alfa1;
  |PINTA #0Campo;

|SI Campo = 3;
    |SI #0Campo = "N";
        #0#16 = " ";
        |PINTA #0#16;
        |CAMPO_MODIFICA #0#16,1,"N";
    |SINO;
        |CAMPO_MODIFICA #0#16,1,"S";
        |SI #0#16 = " ";
            #0#16 = "I";
            |PINTA #0#16;
        |FINSI;
    |FINSI;
|FINSI;
|SI Campo = 16;
    |SI #0#3 = "S"; |Y #0#16 = " ";
        #0#16 = "I";
        |PINTA #0#16;
    |FINSI;
|FINSI;
|SI Campo = 18;
    alfa1 = #0#18;
    |CAMPO_MODIFICA #0#19,1,alfa1;
|FINSI;
|FPRC;

|PROCESO fechacon; |TIPO 0;
  vfec = #0Campo;
  vsw = 0;
  |SI vfec = vblanco; |O vfec = vpuntos; |ACABA; |FINSI;

  vdia1 = vfec % A102;
  vme1  = vfec % A402;
  vany1 = vfec % A704;
  vdia  = vdia1;
  vme   = vme1;
  vany  = vany1;

  |SI vdia = 0; |Y vme = 0; |Y vany < 1900;
      |MENAV verrfec;
      |ERROR;
      |ACABA;
  |FINSI;

  |SI vme [ 12; |Y vme ] 1;
      |SI vme = 1; |O vme = 3; |O vme = 5; |O vme = 7; |O vme = 8; |O vme = 10; |O vme = 12;
          vtope = 31;
      |SINO;
          |SI vme = 2;
              mod = vany $ 4;
              |SI mod = 0; vtope = 29; |SINO; vtope = 28; |FINSI;
          |SINO;
              vtope = 30;
          |FINSI;
      |FINSI;
      |SI vdia [ vtope; vsw = 1; |FINSI;
  |FINSI;

  |SI vsw = 0;
      |MENAV verrfec;
      |ERROR;
  |FINSI;
|FPRC;
|| **************************************************************************

|PROCESO conficha;
  |SI aste ! "*";
      |ABRE #2;
      |PUSHV 0501 2380;
      |PINPA #0#2;
      #2#0 = #1#3;
      |LEE 000#2];
      |CONSULTA #0#2;
      #1#3 = #2#0;
      |CIERRA #2;
  |SINO;
      |ABRE #6;
      |SELECT #4#6;
      |PUSHV 0501 2380;
      |PINPA #0#6;
      #6#9 = #1#3;
      |LEE 000#6=;
      |CONSULTA #3#6;
      #1#3 = #6#9;
      |CIERRA #6;
  |FINSI;
      |POPV;
      |PINTA #1#3;
|FPRC;

|PROCESO cliente;|TIPO 0;
  salida = FSalida;
  |SI salida = 9;  |HAZ letra_0;  |FINSI;
  |SI salida = 10; |HAZ conficha; |FINSI;

  |ABRE #3;
  |DDEFECTO #3;
  swmodi = 0;
  swmod  = 0;
|SI aste ! "*"; |Y #1#3 ! "            ";
    |ABRE #2;
    #2#0 = #1#3;
    |LEE 040#2=;
    |SI FSalida = 0;
        #1#4 = #2#1; #1#6  = #2#2; #1#7  = #2#3; #1#8  = #2#4;
        #1#9 = #2#7; #1#10 = #2#8; #1#11 = #2#9; #1#12 = #2#10;
        swmod = 1;
    |SINO;
        |HAZ altaclipr;
    |FINSI;
    |CIERRA #2;
|FINSI;
|SI swmod = 0;
    |ABRE #6;
    |SELECT #4#6;
    |DDEFECTO #6;
    #6#9 = #1#3;
    |LEE 040#6=;
    |SI FSalida = 0;
        #1#4 = #6#1; #1#7  = #6#2; #1#9 = #6#3;
        #1#10 = #6#4; #1#11 = #6#5; #1#12 = #6#6;
    |FINSI;
    |CIERRA #6;
|FINSI;
 |HAZ cargavar;
 |SI swentra = 1;
     |HAZ recovar;
     |SI aste ! "*"; |Y swmodi = 0;
         |CONFI men2;
         |SI FSalida = 0; |HAZ grabagen; |FINSI;
     |FINSI;
 |SINO;
     |ERROR;
 |FINSI;
|PINTA #1#4;
|CIERRA #3;
|FPRC;

|PROCESO grabagen;
   |ABRE #2;
   #2#0 = #1#3;
   |LEE 040#2=;
   salida = FSalida;
   |SI salida ! 0; |DDEFECTO #2; |FINSI;
   #2#0 = #1#3;  #2#1 = #1#4;  #2#2  = #1#6;
   #2#3 = #1#7;  #2#4 = #1#8;  #2#7  = #1#9;
   #2#8 = #1#10; #2#9 = #1#11; #2#10 = #1#12;
   |SI salida ! 0;
       |GRABA 040#2n;
   |SINO;
       |GRABA 040#2a;
   |FINSI;
   |LIBERA #2;
   |CIERRA #2;
|FPRC;

|PROCESO cargavar;
  swentra  = 0;
  elcampo0 = #1#3;
  elcampo1 = #1#4;
  elcampo2 = #1#6;
  elcampo3 = #1#7;
  elcampo4 = #1#8;
  elcampo5 = #1#9;
  elcampo6 = #1#10;
  elcampo7 = #1#11;
  elcampo8 = #1#12;
  |SUB_EJECUTA "cauti347.tab";
|FPRC;

|PROCESO recovar;
  #1#3  = elcampo0; elcampo0 = "";
  #1#4  = elcampo1; elcampo1 = "";
  #1#6  = elcampo2; elcampo2 = "";
  #1#7  = elcampo3; elcampo3 = "";
  #1#8  = elcampo4; elcampo4 = "";
  #1#9  = elcampo5; elcampo5 = 0;
  #1#10 = elcampo6; elcampo6 = 0;
  #1#11 = elcampo7; elcampo7 = "";
  #1#12 = elcampo8; elcampo8 = "";
|FPRC;

|PROCESO mayor12;
  alfa1 = #1Campo > " ";
  #1Campo = alfa1;
  |PINTA #1Campo;

|SI Campo = 14;
    |SI #1Campo = "S";
        #1#13 = "N";
        |PINTA #1#13;
        |CAMPO_MODIFICA #1#13,1,"N";
    |SINO;
        |CAMPO_MODIFICA #1#13,1,"S";
    |FINSI;
|FINSI;
|FPRC;

|PROCESO vertipo; |TIPO 7;
    |SI #1#2 ! 1;
        #1#13 = "N";
        |PINTA #1#13;
        |CAMPO_MODIFICA #1#13,1,"N";
    |SINO;
        |CAMPO_MODIFICA #1#13,1,"S";
    |FINSI;
|FPRC;

|PROCESO importe; |TIPO 0;
|SI #1#2 ! 3;
  |SI EURODB = 0;
      |SI #1#5 < 500000;
          |AVISO;
          |MENAV "     La cantidad introducida no supera las 500.000 Pesetas";
      |FINSI;
  |FINSI;
|SINO;
  |SI EURODB = 0;
       |SI #1#5 < 50000;
           |AVISO;
           |MENAV "     La cantidad introducida no supera las 50.000 Pesetas";
       |FINSI;
   |FINSI;
|FINSI;
|FPRC;

|PROCESO actualiz; |TIPO 2;
  |SI #1#5 = 0;  |ACABA;  |FINSI;

  |SI #1#2 = 2;
      #0#6 = #0#6 +. 1;
      #0#7 = #0#7 +. #1#5;
      |PINTA #0#6;
      |PINTA #0#7;
  |FINSI;

  |SI #1#2 = 1;
      #0#8 = #0#8 +. 1;
      #0#9 = #0#9 +. #1#5;
      |PINTA #0#8;
      |PINTA #0#9;
  |FINSI;

  |SI #1#2 = 3;
      #0#10 = #0#10 +. 1;
      #0#11 = #0#11 +. #1#5;
      |PINTA #0#10;
      |PINTA #0#11;
  |FINSI;

  |SI #1#2 = 4;
      #0#12 = #0#12 +. 1;
      #0#13 = #0#13 +. #1#5;
      |PINTA #0#12;
      |PINTA #0#13;
  |FINSI;

  |SI #1#2 = 5;
      #0#14 = #0#14 +. 1;
      #0#15 = #0#15 +. #1#5;
      |PINTA #0#14;
      |PINTA #0#15;
  |FINSI;
|FPRC;

|PROCESO letra_0;
  |SI FSalida ! 9;  |ACABA;  |FINSI;

  alfa1 = #1Campo;   |QBT alfa1;
  x    = alfa1 % 101;
  |SI x ! "A"; |Y x ! "B"; |Y x ! "C"; |Y x ! "D"; |Y x ! "E"; |Y x ! "F";
   |Y x ! "G"; |Y x ! "P"; |Y x ! "Q"; |Y x ! "S"; |Y x ! "X";
      final = "";
      alfa2 = alfa1 % 0;
      nume1 = alfa2;
      |PARA nume2 = 1;  |SI nume2 [ nume1;  |HACIENDO nume2 = nume2 + 1;
            lugar = (nume2 * 100) + 1;
            alfa2 = alfa1 % lugar;
            |SI alfa2 = "0"; |O alfa2 = "1";|O alfa2 = "2"; |O alfa2 = "3";
             |O alfa2 = "4"; |O alfa2 = "5";|O alfa2 = "6";
             |O alfa2 = "7"; |O alfa2 = "8";|O alfa2 = "9";
                final = final + alfa2;
            |FINSI;
      |SIGUE;
      nifnum = final;
      |SI nifnum ! 0; |Y final ! "";
          resto   = nifnum $ 23;
          resto   = resto + 1;
          lugar   = (resto * 100) + 1;
          letra   = letrasnif % lugar;
          final   = ("000000000" + final + letra) % -109;
          #1Campo = final;
      |FINSI;
   |SINO;
      |MENAV "    ATENCION !!! No se calcular un C.I.F. ...";
   |FINSI;
   |PINTA #1Campo;
|FPRC;

|PROCESO tipo_8; || control de la ficha de clientes general
  |DIRECTORIO agi;
  |QBF agi;
  |PATH_DAT #23 agi;

|ABRE #23;
#23#0 = 1;
|LEE 040#23=;
|SI FSalida ! 0; aste = "*"; |CIERRA #23; |ACABA; |FINSI;
|CIERRA #23;
aste = #23#7;
|QBF aste;
|SI aste  = "*"; |ACABA; |FINSI;

agi1 = #23#8; |QBF agi1;

longi = agi1 % 0;
num   = longi;
num   = 100 + (num - 5);
longi = num;

agi2  = (agi1 % longi) + "pan/";
|FPRC;

|PROCESO altaclipr;
|SI aste = "*"; |ACABA; |FINSI;
    |CONFI men6;
    |SI FSalida = 0;
        |PUSHV 0501 2380;
        |PINPA #0#2;
        |DDEFECTO #2;
        |PINDA #0#2;
        #2#0 = #1#3;
        |PINTA #2#0;
        |ENDATOS #1#2;
        |SI FSalida = 0;
            |POPV;
            #1#4 = #2#1; #1#6  = #2#2; #1#7  = #2#3; #1#8  = #2#4;
            #1#9 = #2#7; #1#10 = #2#8; #1#11 = #2#9; #1#12 = #2#10;
            swmodi=1; swmod = 1;
            |GRABA 020#2n;                 || Graba el agicli
        |SINO;
            |POPV;
        |FINSI;
    |FINSI;
|LIBERA #2;
|CIERRA #2;
|FPRC;

|PROCESO TipoDecl;
   |SI #0#18 = "N";
      |C_M #0#19,1,"N";
   |SINO;
      |C_M #0#19,1,"S";
   |FINSI;
|FPRC;
