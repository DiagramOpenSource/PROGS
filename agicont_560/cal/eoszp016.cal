|FICHEROS;
     eoszp016 #0;             || Este DEf
     eos349ca #1;             || Cabeceras 349
     eos349li #2;             || Lineas 349
     eosclien #3;             || Clientes
     eoscomul #5;             || Lineas Comuneros
     eoshacie #6;             || Datos Hacienda
     eosm0013 #100;           || Planillas Cabeceras
     eosm0014 #101;           || Planillas Lineas
     eosem349 #103;           || Emision 349
     eosw0047 #104;           || Ayuda Soporte Magnetico
|FIN;

|VARIABLES;
     Vacio20    = "                    ";
     Vacio40    = "                                        ";
     Salir      = 0;
     Opcion     = 0;

     VAlfa      = "";
     VAlfa1     = "";
     VAlfa2     = "";
     VAlfa3     = "";
     VAlfa4     = "";
     VarNum     = 0;

     Unidad     = "";
     Raiz       = "";
     Sistema    = "";
     Letras     = "";
     Compl      = "";
     Refer      = "";

     Campos     = "";
     Campos1    = "";
     Campos2    = "";
     Campos3    = "";
     Campos4    = "";
     Campos5    = "";
     Campos6    = "";
     Campos7    = "";
     Campos8    = "";
     Campos9    = "";
     Campos10   = "";
     Campos11   = "";
     Campos12   = "";
     Campos13   = "";
     Campos14   = "";
     Campos15   = "";
     Campos16   = "";
     Campos17   = "";
     Campos18   = "";
     Campos19   = "";

     Clave      = "";
     Contador   = 0;
     ModoModi   = "";
     SwLee      = 0;
     Handle     = "";
     NFichero   = "";
     Fichero    = "";
     Fichero1   = "";
     FicheroD   = "";
     Directorio = "";
     Empresa    = "";

     CampoDni   = "";
     LetrasNif  = "TRWAGMYFPDXBNJZSQVHLCKE";
     Letra1     = "";
     Letra2     = "";
     Letra3     = "";
     Letra4     = "";
     Letra5     = "";
     Letra6     = "";
     Letra7     = "";
     Letra8     = "";
     Caracter   = "";
     Carac1     = 0;
     Carac2     = 0;
     Carac3     = 0;
     Carac4     = 0;
     Carac5     = 0;
     XPara      = 0;

     NACopiar   = 0;
     sw_fisica  = 0;
     vacio_20   = "                    ";

     &THojas    = 0;
     &NHojas    = 0;
     &dia       = "";
     &mes       = "";
     &anyo      = "";
     &nombrep   = "";
     &tipo_p    = "";

{-1} MGeneral   = "";
     MGeneral1  = "1860";
     MGeneral2  = "1";
     MGeneral3  = "4";
     MGeneral4  = "Creacion  Diskette ";
     MGeneral5  = "Emision   Portada  ";
     MGeneral6  = "Etiqueta  Diskette ";
     MGeneral7  = "Grabacion Diskette ";

{999}NEmpresas = "";
|FIN;

|INCLUYE i_anyode;
|INCLUYE i_consul;

|| ***********************************************************************
||                       PROCESO CARGA DE DATOS
|| ***********************************************************************

|PROGRAMA;
  |CLS;
  |CABEZA "Emision 349 Magn.";
  |DDEFECTO #0;
  |PINPA #0#0;
  |ABRE #0;
  |LEE 101#0p;
  |SI FSalida ! 0;
      |GRABA 020#0b;
  |FINSI;
  |PINDA #0#0;
  |ENDATOS #1#0;
  |SI FSalida = 0;
      |GRABA 020#0a;
  |FINSI;
  |LIBERA #0;
  |CIERRA #0;
|FPRO;

|PROCESO Relleno;
  |SI Campo = 6;
      VAlfa = #0Campo; |QBT VAlfa;
      VAlfa = ("00000" + VAlfa) % -105;
  |FINSI;

  |SI Campo = 10;
      VAlfa = #0Campo; |QBT VAlfa;
      VAlfa = ("000" + VAlfa) % -103;
  |FINSI;
  |SI Campo = 11;
      VAlfa = #0Campo; |QBT VAlfa;
      VAlfa = ("0000000" + VAlfa) % -107;
  |FINSI;

  #0Campo = VAlfa;  |PINTA #0Campo;
|FPRC;

|PROCESO Mayus; |TIPO 0;
  #0Campo = #0Campo > #0Campo;
  |PINTA #0Campo;
|FPRC;

|PROCESO PintaAnyo; |TIPO 7;
  |HAZ i_anyodef;
  #0#2 = i_anyodef - 1;
  |PINTA #0#2;
|FPRC;

|PROCESO Individual; |TIPO 0;
  |SI #0#22 = "I";
      ModoModi = "N";
      #0#23    = 0;   |PINTA #0#23;
      |C_M #0#0, 1, "S";
  |FINSI;

  |SI #0#22 = "C"; ModoModi = "S"; |FINSI;
  |SI #0#22 = "L"; ModoModi = "N"; |FINSI;

  |C_M #0#01, 1, ModoModi;
  |C_M #0#03, 1, ModoModi;
  |C_M #0#04, 1, ModoModi;
  |C_M #0#05, 1, ModoModi;
  |C_M #0#06, 1, ModoModi;
  |C_M #0#07, 1, ModoModi;
  |C_M #0#08, 1, ModoModi;
  |C_M #0#09, 1, ModoModi;
  |C_M #0#10, 1, ModoModi;
  |C_M #0#11, 1, ModoModi;
  |C_M #0#12, 1, ModoModi;
  |C_M #0#16, 1, ModoModi;
  |C_M #0#17, 1, ModoModi;
  |C_M #0#18, 1, ModoModi;
  |C_M #0#23, 1, ModoModi;

  |SI #0#22 = "L";  |C_M #0#23, 1, "S";  |FINSI;
|FPRC;

|PROCESO PonIgual;
  |SI #0#22 = "C";  |ACABA;  |FINSI;

  |SI #0#22 = "L";
      #0#9  = "";    |PINTA #0#9;              || NIF
      #0#3  = "";    |PINTA #0#3;              || Apellidos y Nombre
      #0#4  = "";    |PINTA #0#4;              || SG
      #0#5  = "";    |PINTA #0#5;              || Calle
      #0#6  = "";    |PINTA #0#6;              || Numero
      #0#11 = "";    |PINTA #0#11;             || Telefono
      #0#7  = "";    |PINTA #0#7;              || Municipio
      #0#8  = "";    |PINTA #0#8;              || Provincia
      #0#17 = 0;     |PINTA #0#17;             || Codigo Postal 1
      #0#18 = 0;     |PINTA #0#18;             || Codigo Postal 2
      #0#12 = 0;     |PINTA #0#12;             || Delegacion de Hacienda
      #0#16 = 0;     |PINTA #0#16;             || Administracion de Hacienda
  |FINSI;

  |SI #0#22 ! "I";  |ACABA;  |FINSI;

  #0#1 = #0#0;  |PINTA #0#1;
  |ABRE #1;
  #1#0 = #0#0;
  #1#1 = #0#25;
  #1#2 = #0#2;
  |LEE 040#1=;
  |SI FSalida ! 0;
      |CIERRA #1;
      |MENAV "     No Existe Movimientos en el Modelo 349 con esta Empresa";
      |ERROR;
      |ACABA;
  |FINSI;
  |CIERRA #1;

  |ABRE #3;
  #3#0 = #0#0;
  |LEE 040#3=;
  |SI FSalida ! 0;  |DDEFECTO #3;  |FINSI;
  |CIERRA #3;

  CampoDni = #3#1;  |HAZ LetraDni;
  #0#9  = CampoDni; |PINTA #0#9;              || NIF
  #0#3  = #3#2;     |PINTA #0#3;              || Apellidos y Nombre
  #0#4  = #3#4;     |PINTA #0#4;              || SG
  #0#5  = #3#5;     |PINTA #0#5;              || Calle
  #0#6  = #3#6;     |PINTA #0#6;              || Numero
  #0#11 = #3#14;    |PINTA #0#11;             || Telefono
  #0#7  = #3#13;    |PINTA #0#7;              || Municipio
  #0#8  = #3#12;    |PINTA #0#8;              || Provincia
  #0#17 = #3#10;    |PINTA #0#17;             || Codigo Postal 1
  #0#18 = #3#11;    |PINTA #0#18;             || Codigo Postal 2
  #0#12 = #3#18;    |PINTA #0#12;             || Delegacion de Hacienda
  #0#16 = #3#19;    |PINTA #0#16;             || Administracion de Hacienda
|FPRC;

|PROCESO Planilla;
  |SI #0#22 = "I";  |ACABA;  |FINSI;

  |SI #0#23 = 0;
      |C_M #0#0, 1, "S";
      |C_M #0#1, 1, "S";
  |SINO;
      |C_M #0#0, 1, "N";  #0#0 =     1;  |PINTA #0#0;
      |C_M #0#1, 1, "N";  #0#1 = 99999;  |PINTA #0#1;

      |ABRE #100;
      #100#0 = #0#23;
      |LEE 040#100=;
      |SI FSalida ! 0;
          |MENAV "       No Existe la Planilla";
          |ERROR;
      |FINSI;
      |CIERRA #100;
  |FINSI;
|FPRC;

|PROCESO LetraDni;
  |QBF CampoDni;
  Letra2 = CampoDni;
  |QBT Letra2;

      Caracter = Letra2 % 101;
  |SI Caracter!"A";|Y Caracter!"B";|Y Caracter!"C";|Y Caracter!"D";
       |Y Caracter!"E";|Y Caracter!"F";
       |Y Caracter!"G";|Y Caracter!"P";|Y Caracter!"Q";|Y Caracter!"S";|Y Caracter!"X";
          Letra4 = "";
          Letra3 = Letra2 % 0;
          Carac2 = Letra3;
      |PARA Carac5 = 1;  |SI Carac5 [ Carac2;  |HACIENDO Carac5 = Carac5 + 1;
              Carac3 = (Carac5 * 100) + 1;
              Letra3 = Letra2 % Carac3;
          |SI Letra3="0";|O Letra3="1";|O Letra3="2";|O Letra3="3";
                        |O Letra3="4";|O Letra3="5";|O Letra3="6";
                        |O Letra3="7";|O Letra3="8";|O Letra3="9";
                  Letra4 = Letra4 + Letra3;
          |FINSI;
      |SIGUE;
          Carac4 = Letra4;
      |SI Carac4 ! 0; |Y Letra4 ! "";
              Carac1 = Carac4 $ 23;
              Carac1 = Carac1 + 1;
              Carac3 = (Carac1 * 100) + 1;
              Letra1 = LetrasNif % Carac3;

              Letra4 = "000000000000" + Letra4;
              Letra7 = Letra4 % -103;
              Letra6 = Letra4 % -403;
              Letra5 = Letra4 % -703;

              Letra8 = Letra5  % 101;
              |SI Letra8 = "0"; Letra5 = Letra5 % 202; |FINSI;

              Letra8 = Letra5  % 101;
              |SI Letra8 = "0"; Letra5 = Letra5 % 201; |FINSI;

              Letra8 = Letra5  % 101;
              |SI Letra8 = "0"; Letra5 = ""; |FINSI;

              |SI Letra5 ! "";
                  Letra4 = Letra5 + Letra6 + Letra7 + Letra1;
              |SINO;
                  Letra4 = Letra6 + Letra7 + Letra1;
              |FINSI;

              CampoDni = Letra4;
      |FINSI;
  |FINSI;
  CampoDni = ("000000000" + CampoDni) % -109;
|FPRC;

|| ***********************************************************************
||                       IMPRESION DE LA PORTADA
|| ***********************************************************************

|PROCESO FechaP;
  dia  = #0#15 % 102;
  mes  = #0#15 % 402;
  anyo = #0#15 % 704;

  |SI mes = "01";  mes = "Enero     ";  |FINSI;
  |SI mes = "02";  mes = "Febrero   ";  |FINSI;
  |SI mes = "03";  mes = "Marzo     ";  |FINSI;
  |SI mes = "04";  mes = "Abril     ";  |FINSI;
  |SI mes = "05";  mes = "Mayo      ";  |FINSI;
  |SI mes = "06";  mes = "Junio     ";  |FINSI;
  |SI mes = "07";  mes = "Julio     ";  |FINSI;
  |SI mes = "08";  mes = "Agosto    ";  |FINSI;
  |SI mes = "09";  mes = "Septiembre";  |FINSI;
  |SI mes = "10";  mes = "Octubre   ";  |FINSI;
  |SI mes = "11";  mes = "Noviembre ";  |FINSI;
  |SI mes = "12";  mes = "Diciembre ";  |FINSI;
|FPRC;

|PROCESO Representante;
  #3#0 = #1#0;
  |LEE 040#3=;
  |SI FSalida ! 0; |DDEFECTO #3; |FINSI;

  nombrep   = "";
  sw_fisica = 0;

  #5#0 = #3#0;
  #5#1 = 1;
  #5#2 = 1;
  #5#7 = #0#2;
  |LEE 040#5];
  |SI FSalida = 0; |Y #5#0 = #3#0; |Y #5#7 = #0#2;
      #3#0 = #5#3;
      |LEE 040#3=;
      |SI FSalida = 0; sw_fisica = 1; nombrep = #3#2; |FINSI;
  |FINSI;

  |SI sw_fisica = 1; |ACABA; |FINSI;

  #3#0 = #1#0;
  |LEE 040#3=;
  |SI FSalida ! 0; |DDEFECTO #3; |FINSI;

  nombrep = #3#2;

  |SI #3#24 ! vacio_20; |O #3#25 ! vacio_20;
      nombrep = #3#24 + #3#25;
      |ACABA;
  |FINSI;

  |SI #3#29 ! vacio_20; |O #3#30 ! vacio_20;
      nombrep = #3#29 + #3#30;
      |ACABA;
  |FINSI;

  |SI #3#34 ! vacio_20; |O #3#35 ! vacio_20;
       nombrep = #3#34 + #3#35;
       |ACABA;
  |FINSI;
|FPRC;

|PROCESO LeePortadaP;
  #103#27 = #0#24;

  |SI #0#23 ! 0;
      #101#0 = #0#23;
      #101#1 = #1#0;
      |LEE 040#101=;
      |SI FSalida ! 0;  |ACABA;  |FINSI;
  |FINSI;

  |SI SwLee = 0;
      #103#35 = #103#35 + 1;
      #103#36 = #103#36 + (#1#6 + #1#8 + #1#10 + #1#11);
      |ACABA;
  |FINSI;

  |DDEFECTO #103;
  #103#25 = #0#2;                              || A¤o Emision
  #103#37 = #1#0;
  #103#30 = #1#6;
  #103#31 = #1#8;
  #103#32 = #1#10 + #1#11;
  #103#33 = #1#7;
  #103#34 = #1#9;
  #103#35 = 0;
  #103#36 = 0;
  #103#38 = "";                                || Nombre Representante
  #103#39 = "";
  #103#40 = "";
  #103#41 = "";
  #103#42 = "";

  |SI #0#22 = "I";  #103#39 = "X";  |FINSI;    || Soporte Individual
  |SI #0#22 = "C";  #103#40 = "X";  |FINSI;    || Soporte Colectivo Declarante
  |SI #0#22 ! "L";  #103#42 = "X";  |FINSI;    || Soporte Colectivo Declarante

  |HAZ Representante;

  |PRINT;
  |PIEINF;

  #1#4 = #0#15;
  |GRABA 140#1a;
  |LIBERA #1;
|FPRC;

|DEFBUCLE eos349caP;
  #1#1;
  ;
  #0#0, #0#2, #0#25;
  #0#1, #0#2, #0#25;
  be;
  NULL, LeePortadaP;
|FIN;

|PROCESO Portada;
  |HAZ FechaP;

  |IMPRE 0;
  |SI FSalida ! 0;  |ACABA;  |FINSI;

  |INFOR "eospo349";
  |SI FSalida ! 0;  |FINIMP;  |ACABA;  |FINSI;

  tipo_p = "T";
  |SI #103#25 = 0; tipo_p = "A"; |FINSI;

  #103#35    = 0;
  #103#36    = 0;
  |SI #0#22 = "C";
      SwLee = 0;  |BUCLE eos349caP;

      |DDEFECTO #3;
      |DDEFECTO #1;
      #103#25 = #0#2;                   || A¤o Emision

      #103#31 = "";                     || Impreso
      #103#32 = "";                     || Soporte Individual
      #103#33 = "X";                    || Soporte Colectivo Presentador
      #103#34 = "";                     || Soporte Colectivo Declarante
      #103#37 = 0;                      || empresa
      #103#38 = "";                     || Nombre Representante

      #3#0  = 0;                        || Codigo Cliente
      CampoDni = #0#9;  |HAZ LetraDni;
      #3#1  = CampoDni;                 || NIF
      #3#2  = #0#3;                     || Apellidos y Nombre
      #3#4  = #0#4;                     || SG
      #3#5  = #0#5;                     || Calle
      #3#6  = #0#6;                     || Numero
      #3#14 = #0#10 + "-" + #0#11;      || Telefono
      #3#13 = #0#7;                     || Municipio
      #3#12 = #0#8;                     || Provincia
      #3#10 = #0#17;                    || Codigo Postal 1
      #3#11 = #0#18;                    || Codigo Postal 2
      #3#18 = #0#12;                    || Delegacion de Hacienda
      #3#19 = #0#16;                    || Administracion de Hacienda

      |PRINT;
      |PIEINF;
  |FINSI;

  #103#35    = 0;
  #103#36    = 0;

  SwLee = 1; |BUCLE eos349caP;
  |FININF;
  |FINIMP;
|FPRC;

|| ***********************************************************************
||                       GRABACION SECUENCIAL DISTINTOS TIPOS
|| ***********************************************************************

|PROCESO Tipo0;
  Campos1  = "0";                                      || Tipo Registro
  VAlfa    = #0#2;   VAlfa = ("00" + VAlfa) % -102;
  Campos2  = VAlfa;                                    || Ejercicio
  Campos3  = "  ";                                     || Periodo
  VAlfa = ("00" + #0#12) % -102;
  Campos4  = VAlfa;                                    || Delegacion A.E.A.T.
  Campos5  = #0#9;                                     || N.I.F.
  Campos6  = #0#3;                                     || Apellidos y Nombre
  Campos7  = #0#4;                                     || SG
  Campos8  = #0#5 % 121;                               || Calle
  Campos9  = #0#6;                                     || Numero
  VAlfa    = ("00" + #0#17) % -102;
  Campos10 = VAlfa;                                    || Codigo Postal 1
  VAlfa    = ("000" + #0#18) % -103;
  Campos11 = VAlfa;                                    || Codigo Postal 2
  Campos12 = #0#7 % 112;                               || Municipio
  Campos13 = #0#10;                                    || Prefijo
  Campos14 = #0#11;                                    || Telefono
  VAlfa    = ("00000" + #103#35) % -105;               || Total Declarantes
  Campos15 = VAlfa;
  VAlfa    = ("0000000" + #103#36) % -107;             || Total Declarados
  Campos16 = VAlfa;
  Campos17 = "D";
  Campos18 = "349";                                    || Modelo
  Campos19 = " ";

  Campos   = Handle   + " " + Campos1 + Campos2 + Campos3 + Campos4;
  Campos   = Campos   + Campos5  + Campos6  + Campos7  + Campos8  + Campos9;
  Campos   = Campos   + Campos10 + Campos11 + Campos12 + Campos13 + Campos14;
  Campos   = Campos   + Campos15 + Campos16 + Campos17 + Campos18 + Campos19;
  Campos   = Campos   + &#0#19 + &#0#20;        || Caracter Final de Linea
  |FGRABA Campos;

  #104#14 = #104#14 + 1;
|FPRC;

|PROCESO Tipo1;
  Campos1  = "1";                                      || Tipo Registro
  VAlfa    = ("00" + #0#2) % -102;
  Campos2  = VAlfa;                                    || Ejercicio
  |SI #1#1 = 0;  VAlfa = "0A";  |FINSI;
  |SI #1#1 = 1;  VAlfa = "1T";  |FINSI;
  |SI #1#1 = 2;  VAlfa = "2T";  |FINSI;
  |SI #1#1 = 3;  VAlfa = "3T";  |FINSI;
  |SI #1#1 = 4;  VAlfa = "4T";  |FINSI;
  Campos3  = VAlfa;                                    || Periodo
  VAlfa    = ("00" + #3#18) % -102;
  Campos4  = VAlfa;                                    || Delegacion
  CampoDni = #3#1;  |HAZ LetraDni;
  Campos5  = CampoDni;                                 || N.I.F.
  Campos6  = #1#3;                                     || Apellidos y Nombre
  VAlfa    = ("0000000" + #1#6) % -107;                || Personas
  Campos7  = VAlfa;
  VAlfa    = ("0000000000000" + #1#7) % -113;          || Cantidad
  Campos8  = VAlfa;
  VAlfa    = ("0000000" + #1#8) % -107;                || Personas
  Campos9  = VAlfa;
  VAlfa    = ("0000000000000" + #1#9) % -113;          || Cantidad
  Campos10 = VAlfa;
  VarNum   = #1#10 + #1#11;
  VAlfa    = ("0000000" + VarNum) % -107;              || Personas
  Campos11 = VAlfa;
  Campos12 = "D";
  Campos13 = "349";
  Campos14 = "         ";
  |SI #0#22 = "C";
      Campos14 = #0#9;
  |FINSI;
  Campos15 = " " * 12;

  Campos   = Handle   + " " + Campos1 + Campos2 + Campos3 + Campos4;
  Campos   = Campos   + Campos5  + Campos6  + Campos7  + Campos8  + Campos9;
  Campos   = Campos   + Campos10 + Campos11 + Campos12 + Campos13 + Campos14;
  Campos   = Campos   + Campos15;
  Campos   = Campos   + &#0#19 + &#0#20;        || Caracter Final de Linea
  |FGRABA Campos;
  #104#14 = #104#14 + 1;
|FPRC;

|PROCESO Tipo2;
  Campos1  = "2";                                      || Tipo Registro
  VAlfa    = ("00" + #0#2) % -102;
  Campos2  = VAlfa;                                    || Ejercicio
  VAlfa    = ("00" + #0#25) % -102;
  Campos2  = Campos2 + VAlfa;                          || Periodo
  VAlfa    = ("00" + #3#18) % -102;
  Campos2  = Campos2 + VAlfa;                          || Periodo
  CampoDni = #3#1;  |HAZ LetraDni;
  Campos2  = Campos2 + CampoDni;                       || N.I.F.
  Campos3  = #2#5 + #2#6;
  Campos4  = #2#7;                                     || Apellidos y Nombre
  Campos5  = "1";
  |SI #2#4 = "A";  Campos5 = "2";  |FINSI;
  |SI #2#4 = "R";  Campos5 = "3";  |FINSI;
  |SI #2#4 = "X";  Campos5 = "4";  |FINSI;             || Tipo
  VAlfa    = ("0000000000000" + #2#8) % -113;          || Cantidad
  Campos6  = VAlfa;
  Campos7  = #2#9;
  Campos8  = #2#10;
  VAlfa    = ("00" + #2#11) % -102;                    || A¤o
  |SI VAlfa = "00"; VAlfa = "  ";  |FINSI;
  Campos8  = Campos8 + VAlfa;

  VAlfa    = "  ";
  |SI #2#4 = "R"; |O #2#4 = "X";
      VAlfa     = ("0" + #2#12) % -101;                 || Periodo
      |SI VAlfa = "0";
          VAlfa = "0A";
      |SINO;
          VAlfa = VAlfa + "T";
      |FINSI;
  |FINSI;

  Campos8  = Campos8 + VAlfa;
  Campos9  = #2#13
  Campos10 = " " * 37;

  Campos   = Handle   + " " + Campos1 + Campos2 + Campos3 + Campos4;
  Campos   = Campos   + Campos5  + Campos6  + Campos7  + Campos8  + Campos9 + Campos10;
  Campos   = Campos   + &#0#19 + &#0#20;        || Caracter Final de Linea
  |FGRABA Campos;
  #104#14 = #104#14 + 1;
|FPRC;

|DEFBUCLE eos349li;
  #2#1;
  ;
  #1#0, #0#2, #0#25, 000;
  #1#0, #0#2, #0#25, 999;
  ;
  NULL, Tipo2;
|FIN;

|PROCESO LeePortadaC;
  |SI #0#23 ! 0;
      #101#0 = #0#23;
      #101#1 = #1#0;
      |LEE 040#101=;
      |SI FSalida ! 0;  |ACABA;  |FINSI;
  |FINSI;

  |SI SwLee = 0;
      #103#35 = #103#35 + 1;
      #103#36 = #103#36 + (#1#6 + #1#8 + #1#10 + #1#11);

      #104#0  = #104#0 + #1#6;
      #104#1  = #104#1 + #1#8;
      #104#2  = #104#2 + #1#10 + #1#11;
      #104#6  = #104#0 + #104#1 + #104#2;
      #104#7  = #104#7 + #1#7;
      #104#8  = #104#8 + #1#9;
      #104#13 = #104#7 + #104#8;
      #104#15 = #104#15 + 1;

      |ACABA;
  |FINSI;

  #3#0  = #1#0;
  |LEE 040#3=;
  |SI FSalida ! 0; |DDEFECTO #3; |FINSI;

  SwLee = 1;  |HAZ Tipo1;
  SwLee = 2;  |BUCLE eos349li;
|FPRC;

|DEFBUCLE eos349ca;
  #1#1;
  ;
  #0#0, #0#2, #0#25;
  #0#1, #0#2, #0#25;
  be;
  NULL, LeePortadaC;
|FIN;


|| ************************************************************************
||                  CREACION DISKETTE SOPORTE COLECTIVO E INDIVIDUAL
|| ************************************************************************

|PROCESO Creacion;
  |HAZ FechaP;

  |DDEFECTO #104;

  #103#35    = 0;
  #103#36    = 0;
  SwLee = 0;  |BUCLE eos349ca;

  Directorio = "";
  |DFICE Directorio;
  Fichero  = #0#2;
  Fichero  = "ce" + ("00" + Fichero) % -102;
  NFichero = Directorio + Fichero;          |QBF NFichero;
  Fichero  = "wb  "+ NFichero;

  Pos = -1;
  |FABRE Fichero;
  Handle = FSalida;
  |SI FSalida ] 0;
      |SI #0#22 = "C";  |HAZ Tipo0;  |FINSI;  || Registro del Presentador
      SwLee = 1;  |BUCLE eos349ca;
  |FINSI;

  Campos = Handle + " " + &#0#21;
  |FGRABA Campos;

  FSalida = Handle;
  |FCIERRA FSalida;

  Pos = -1;
  |PUSHV 0501 2380;
  |PINPA #0#104;
  |PINDA #0#104;
  |CONFI "2400SEs Correcto : ";
  |SI FSalida ! 0;
      |FBORRA NFichero;
      Salir = 1;
  |FINSI;
  |POPV;
|FPRC;

|| ************************************************************************
||                  CREACION DISKETTE SOPORTE LASER
|| ************************************************************************

|PROCESO LeePortadaL;
  |SI #0#23 ! 0;
      #101#0 = #0#23;
      #101#1 = #1#0;
      |LEE 040#101=;
      |SI FSalida ! 0;  |ACABA;  |FINSI;
  |FINSI;

  #3#0  = #1#0;
  |LEE 040#3=;
  |SI FSalida ! 0; |DDEFECTO #3; |FINSI;

  #0#12 = #3#18;

  Directorio = "";
  |DBASE Directorio;
  Directorio = Directorio + "hacienda";
  |MKDIR Directorio;
  Directorio = Directorio + "/";

  Empresa  = #1#0;  Empresa  = ("00000" + Empresa) % -105;

  Fichero    = "349" + Empresa;
  INEmpresas = NEmpresas1 <-;
  INEmpresas = INEmpresas + NACopiar;
  NFichero   = Directorio + Fichero;          |QBF NFichero;
  NEmpresas  = NFichero;
  NACopiar   = NACopiar + 1;

  Fichero  = "wb  "+ NFichero;

  Pos = -1;
  |FABRE Fichero;
  Handle = FSalida;
  |SI FSalida ] 0;
      |HAZ Tipo1;
      |BUCLE eos349li;
  |FINSI;

  Campos = Handle + " " + &#0#21;
  |FGRABA Campos;

  FSalida = Handle;
  |FCIERRA FSalida;
|FPRC;

|DEFBUCLE eos349caL;
  #1#1;
  ;
  #0#0, #0#2, #0#25;
  #0#1, #0#2, #0#25;
  be;
  NULL, LeePortadaL;
|FIN;

|PROCESO CreacionLaser;
  NACopiar   = 0;
  INEmpresas = NEmpresas1 <-;
  |PARA XPara = 1; |SI XPara [ 999;  |HACIENDO XPara = XPara + 1;
        NEmpresas = "";
        INEmpresas = INEmpresas + 1;
  |SIGUE;

  |HAZ FechaP;
  |BUCLE eos349caL;
|FPRC;

|| ************************************************************************
||                  GRABACION DEL DISKETTE OP
|| ************************************************************************

|PROCESO GrabaDisketteC;
  Pos = -1;
  Directorio = "";
  |DFICE Directorio;
  Fichero  = #0#2;
  Fichero  = "ce" + ("00" + Fichero) % -102;
  NFichero = Fichero;          |QBF NFichero;
  Fichero  = "rt  "+ Directorio + NFichero;
  |FABRE Fichero;
  |SI FSalida < 0;
      VAlfa = "      No se ha creado el fichero " + NFichero + " Por favor haga la Creacion del Diskette";
      |MENAV  VAlfa;
      |ACABA;
  |FINSI;
  |FCIERRA FSalida;

  Pos = -1;
  |PUSHV 0501 2380;
  |PINPA #1#104;

  E_sup  = 1;
  E_inf  = "ABCDEFGHIJKLMNOPQRSTUVWXYZ";
  Unidad = "A";
  Unidad = 0851 ? 0;
  |ATRI I;
  |PINTA 1616, "Introduzca un diskette formateado en D.O.S. ...";
  |ATRI i;
  |PINTA 2380, ".";
  |PAUSA;

  Raiz = "";
  |DIRECTORIO Raiz; |QBF Raiz;
  |DFICE Fichero;   |QBF Fichero;
  NFichero  = #0#2;
  NFichero  = "ce" + (("00" + NFichero) % -102);
  Fichero   = Fichero + NFichero;

  |ATRI P;
  |PINTA 1616, "                  Copiando ...                 ";
  |ATRI p;
  |PINTA 2380, ".";

  |QUE_SISTEMA Sistema;
  |SI Sistema ! "ESDOS";
      VAlfa = Raiz + "bin/agcopiaa " + Fichero + " " + Unidad + ":";
      |SYSTEM VAlfa;

      |ATRI P;
      |PINTA 1616, "           FICHERO COPIADO !!!!                 ";
      |ATRI p;
      |PINTA 2380, ".";
      |PAUSA;
  |SINO;
      VAlfa1 = Fichero;
      VAlfa2 = Unidad + ":" + NFichero;

      |COPIA_FICHERO VAlfa1, VAlfa2;
      |SI FSalida ! 0;
          VAlfa3 = "Error [" + FSalida + "] en copia de ficheros ...";
          |PINTA 1616, VAlfa3;
          |PINTA 2380, ".";
          |PAUSA;
      |SINO;
          |ATRI P;
          |PINTA 1616, "           FICHERO COPIADO !!!!                 ";
          |ATRI p;
          |PINTA 2380, ".";
          |PAUSA;
      |FINSI;
  |FINSI;
  |POPV;
|FPRC;

|| ************************************************************************
||                  GRABACION DEL DISKETTE PARA LASER
|| ************************************************************************

|PROCESO GrabaDisketteL;
  INEmpresas = NEmpresas1 <-;
  |SI NEmpresas = "";
      VAlfa = "     Por favor haga la Creacion del Diskette";
      |MENAV  VAlfa;
      |ACABA;
  |FINSI;

  |PUSHV 0501 2380;
  |PINPA #1#104;

  |PINTA 0931, "Letras CG-TIMES ..:";
  |PINTA 1031, "D.Complementarias :";
  E_sup  = 1;
  E_inf  = "ABCDEFGHIJKLMNOPQRSTUVWXYZ";
  Unidad = "A";
  Unidad = 0851 ? 0;

  E_sup  = 1;
  E_inf  = "SNsn";
  Letras = "N";
  Letras = 0951 ? 0;

  E_sup  = 1;
  E_inf  = "SNsn";
  Compl  = "N";
  Compl  = 1051 ? 0;

  |SI Compl = "S";
      |PINTA 1131, "Referencia .......:";
      E_sup  = 13;
      E_inf  = "";
      Refer  = "";
      Refer  = 1151 ? 0;
  |FINSI;

  |ATRI I;
  |PINTA 1616, "Introduzca un diskette formateado en D.O.S. y con los pro- ";
  |PINTA 1716, "grama de Hacienda copiados.                                ";
  |PINTA 1816, "A continuacion el programa grabar  los ficheros generados  ";
  |PINTA 1916, "en el diskette. Introducir el diskette en una maquina en   ";
  |PINTA 2016, "D.O.S y ejecutar --> A:IMPRE349                            ";
  |ATRI i;
  |PINTA 2380, ".";
  |PAUSA;

  |PINTA 1616, "                                                          ";
  |PINTA 1716, "                                                          ";
  |PINTA 1816, "                                                          ";
  |PINTA 1916, "                                                          ";
  |PINTA 2016, "                                                          ";

  Directorio = "";
  |DBASE Directorio;
  Directorio = Directorio + "hacienda/";
  NFichero   = Directorio + "impre349.bat";
  Fichero1   = NFichero;
  NFichero   = "wb  "+ NFichero;

  Pos = -1;
  |FABRE NFichero;
  Handle = FSalida;
  |SI FSalida ] 0;
      VAlfa  = "A:";
      Campos = Handle + " " + VAlfa + &#0#19 + &#0#20;
      |FGRABA Campos;

      |ATRI P;
      |PINTA 1616, "                  Copiando ...                 ";
      |ATRI p;
      |QUE_SISTEMA Sistema;

      |PARA XPara = 1; |SI XPara [ 999; |HACIENDO XPara = XPara + 1;
            |SI NEmpresas = ""; |SAL; |FINSI;

             Fichero  = NEmpresas;
             FicheroD = Fichero % -108;

             |SI Sistema ! "ESDOS";
                 VAlfa = Raiz + "bin/agcopiaa " + Fichero + " " + Unidad + ":";
                 |PINTA 1816, "";
                 |ATRI I; |SYSTEM VAlfa; |ATRI i;
             |SINO;
                 VAlfa1 = Fichero;
                 VAlfa2 = Unidad + ":" + FicheroD;
                 |COPIA_FICHERO VAlfa1, VAlfa2;
                 |SI FSalida ! 0;
                     VAlfa3 = "Error [" + FSalida + "] en copia de ficheros ...";
                     |PINTA 1616, VAlfa3;
                     |PINTA 2380, ".";
                     |PAUSA;
                 |FINSI;
             |FINSI;

             VAlfa  = "349pdf " + FicheroD;
             |SI Letras = "S";  VAlfa = VAlfa + "   s";            |FINSI;
             |SI Compl  = "S";  VAlfa = VAlfa + "   c  " + Refer;  |FINSI;
             Campos = Handle + " " + VAlfa + &#0#19 + &#0#20;
             |FGRABA Campos;
             INEmpresas = INEmpresas + 1;
      |SIGUE;
  |FINSI;

  VAlfa  = "C:";
  Campos = Handle + " " + VAlfa + &#0#19 + &#0#20;
  |FGRABA Campos;

  Campos = Handle + " " + &#0#21;
  |FGRABA Campos;

  FSalida = Handle;
  |FCIERRA FSalida;

  |SI Sistema ! "ESDOS";
      VAlfa = Raiz + "bin/agcopiaa " + Fichero1 + " " + Unidad + ":";
      |PINTA 1816, "";
      |ATRI I; |SYSTEM VAlfa; |ATRI i;
  |SINO;
      VAlfa1 = Directorio + "impre349.bat";
      VAlfa2 = Unidad + ":impre349.bat";
      |COPIA_FICHERO VAlfa1, VAlfa2;
      |SI FSalida ! 0;
          VAlfa3 = "Error [" + FSalida + "] en copia de ficheros ...";
          |PINTA 1616, VAlfa3;
          |PINTA 2380, ".";
          |PAUSA;
      |FINSI;
  |FINSI;

  |ATRI P;
  |PINTA 1616, "           FICHEROS COPIADO !!!!                 ";
  |ATRI p;
  |PAUSA;
  |POPV;
|FPRC;

|| ***********************************************************************
||                       EMISION DE ETIQUETAS
|| ***********************************************************************

|PROCESO Etiquetas;
  |SI #104#14 = 0;
      |MENAV "     Por favor haga la creacion de Diskette.";
      |ACABA;
  |FINSI;

  |PUSHV 0501 2380;

  Campos1  = "19";
  |SI #0#2 < 80; Campos1 = "20"; |FINSI;
  VAlfa    = #0#2;   VAlfa = ("00" + VAlfa) % -102;
  Campos1  = Campos1 + VAlfa;                          || Ejercicio
  VAlfa    = #0#17;  VAlfa = ("00" + VAlfa) % -102;
  Campos2  = VAlfa;                                     || Codigo Postal 1
  VAlfa    = #0#18;  VAlfa = ("000" + VAlfa) % -103;
  Campos3  = VAlfa;                                     || Codigo Postal 2

  |ABRE #6;
  #6#0 = #0#12;
  #6#1 = #0#16;
  |LEE 040#6=;
  |SI FSalida ! 0; |DDEFECTO #6; |FINSI;
  |CIERRA #6;

  #104#16 = "Delegacion .........: " + #6#2;
  #104#17 = "Ejercicio ..........: " + Campos1;
  #104#18 = "Modelo .............: 349";
  #104#19 = "Numero Justificante : ";
  #104#20 = "N.I.F. .............: " + #0#9;
  #104#21 = "Nombre : " + #0#3;
  #104#22 = #0#4 + " " + #0#5 + " " + #0#6 + " " + Campos2 + " " + Campos3 + " " + #0#7;
  #104#23 = "P.Cont.: " + #0#3;
  #104#24 = "Telefono ...........: " + #0#10 + " " + #0#11;
  #104#25 = "Numero total Registros ..: " + #104#14;
  #104#26 = "Densidad soporte : 1,44 MB en Diskettes de 3 1/2";
  #104#27 = "";
  #104#28 = "";
  |C_M #104#27, 1, "N";
  |C_M #104#28, 1, "N";
  |SI #0#22 = "C";
      #104#27 = "Numero total Declarantes : " + #104#15;
      #104#28 = "Numero total Declarados .: " + #104#6;
      |C_M #104#27, 1, "S";
      |C_M #104#28, 1, "S";
  |FINSI;

  |PINPA #2#104;
  |PINDA #2#104;
  |ENDATOS #1#104;
  |SI FSalida = 0;
      |PINPA #2#104;
      |PINDA #2#104;

      |CONFI "2400SRealizar Impresion de la Etiqueta : ";
      |SI FSalida ! 0; |POPV; |ACABA; |FINSI;

      |IMPRE 0;
      |SI FSalida ! 0;  |ACABA;  |FINSI;

      |INFOR "eoszp016";
      |SI FSalida ! 0;  |FINIMP;  |ACABA;  |FINSI;

      |PRINT; |PIEINF; |FININF; |FINIMP;
  |FINSI;
  |POPV;
|FPRC;

|| ***********************************************************************
||                       PROCESO DE DISTRIBUCION
|| ***********************************************************************

|PROCESO Distribuye; |TIPO 3;
  |ABRE #1;
  |ABRE #3;
  |ABRE #101;
  |PARA; |SI Salir = 0; |HACIENDO;
         |MENUG MGeneral;
         MGeneral2 = FSalida;
         Opcion    = MGeneral2;
         Salir     = 1;
         |SI Opcion ] 1; |Y Opcion [ 4;
             Salir = 0;
             |SI Opcion = 1; |Y #0#22 ! "L"; |HAZ Creacion;       |FINSI;
             |SI Opcion = 1; |Y #0#22 = "L"; |HAZ CreacionLaser;  |FINSI;
             |SI Opcion = 2; |Y #0#22 ! "L"; |HAZ Portada;        |FINSI;
             |SI Opcion = 3; |Y #0#22 ! "L"; |HAZ Etiquetas;      |FINSI;
             |SI Opcion = 4; |Y #0#22 ! "L"; |HAZ GrabaDisketteC; |FINSI;
             |SI Opcion = 4; |Y #0#22 = "L"; |HAZ GrabaDisketteL; |FINSI;
         |FINSI;
  |SIGUE;
  |CIERRA #1;
  |CIERRA #3;
  |CIERRA #101;
|FPRC;
