|FICHEROS;
    paspersa #0;
    cacuenta #1;    || plan contable
    camaasic #2;    || apuntes cabs.
    camaasil #3;    || apuntes lins.
|FIN;

|VARIABLES;
    alfa1 = "";
    alfa2 = "";
    alfa3 = "";
    alfa4 = "";
    nume1 = 0;
    nume2 = 0;
    nume3 = 0;
    nume4 = 0;
    para1 = 0;
    para2 = 0;
    para3 = 0;
    para4 = 0;
    fech1 = @;
    fech2 = @;
    fech3 = @;
    fech4 = @;
    elsw1 = 0;
    elsw2 = 0;
    elsw3 = 0;
    elsw4 = 0;
    FEstado = 0;
    seleccio = 0;
    modo = 0;
    sw_rup = 0;

    fabre = "w   ";
    fcanl = "";
    field = "";
    ffich = "";
    fexit = 0;
    fcont = 0;
    sw_fabre = 0;
|FIN;

|INCLUYE acceso_i;
|INCLUYE puntos_i;
____________________________________/ CALCULOS DESDE CAMPOS
|PROCESO def_fec; |TIPO 7;
    #0#6 = fec1;         |PINTA #0#6;
    #0#7 = fec2;         |PINTA #0#7;
|FPRC;

|PROCESO mayus; |TIPO 0;
    #0Campo = #0Campo > #0Campo;
|PINTA #0Campo;
|FPRC;

|PROCESO no_entres1; |TIPO 0;
    alfa1 = #0#10;
|CAMPO_MODIFICA #0#0, 1, alfa1;
|CAMPO_MODIFICA #0#2, 1, alfa1;
|CAMPO_MODIFICA #0#3, 1, alfa1;
|FPRC;

|PROCESO no_entres2; |TIPO 0;
    alfa1 = #0#11;
|CAMPO_MODIFICA #0#1, 1, alfa1;
|CAMPO_MODIFICA #0#4, 1, alfa1;
|CAMPO_MODIFICA #0#5, 1, alfa1;
|CAMPO_MODIFICA #0#6, 1, alfa1;
|CAMPO_MODIFICA #0#7, 1, alfa1;
|CAMPO_MODIFICA #0#8, 1, alfa1;
|CAMPO_MODIFICA #0#9, 1, alfa1;
|FPRC;

|PROCESO cuentas; |TIPO 0;
    salidas = FSalida;
    p_alfa1 = #0Campo;
|HAZ punto;
|SI p_error ! 0;  |ERROR;  |ACABA;  |FINSI;
|FPRC;
____________________________________/ EXPORTACION APUNTES
|PROCESO los_apuntes2;
|SI #3#2 [ 99999;             || solo aceptan 5 digitos del documento!!!
        field = "";
        alfa1 = "999999";          field = field + alfa1;   || nro. empresa
        alfa1 = "1";               field = field + alfa1;   || nro. contabil.
    |SI #3#8 = "D";      alfa1 = "51";  |FINSI;
    |SI #3#8 = "H";      alfa1 = "52";  |FINSI;
                                   field = field + alfa1;   || signo
        alfa1 = #3#1;
        alfa1 = (alfa1 % 102) + (alfa1 % 402) + (alfa1 % -102);
                                   field = field + alfa1;   || fecha
        alfa1 = "      ";          field = field + alfa1;   || fecha banco
        alfa1 = #3#2;    alfa1 = ("00000" + alfa1) % -105;
                                   field = field + alfa1;   || documento
    |SI #3#8 = "D";      alfa1 = "19";  |FINSI;
    |SI #3#8 = "H";      alfa1 = "20";  |FINSI;
                                   field = field + alfa1;   || signo
        alfa1 = #3#4 % 110;        field = field + alfa1;   || cuenta
    |SI #3#8 = "D";      alfa1 = #2#10 % 110;     |FINSI;
    |SI #3#8 = "H";      alfa1 = #2#8  % 110;     |FINSI;
                                   field = field + alfa1;   || contra-cuenta
        alfa1 = "      ";          field = field + alfa1;   || vencimiento
    |SI #3#9 < 0;
            #3#9 = -#3#9;
            alfa2 = "-";
    |SINO;
            alfa2 = " ";
    |FINSI;
        alfa1 = #3#9;    alfa1 = ("00000000000" + alfa1) % -111;
                                   field = field + alfa1;   || importe
        alfa1 = alfa2;             field = field + alfa1;   || signo importe
        alfa1 = "A";               field = field + alfa1;   || origen

    |HAZ graba;
|FINSI;
|FPRC;

|PROCESO los_apuntes1;
|BUCLELIN 2los_apuntes2#2;
|FPRC;

|DEFBUCLE los_apuntes;
 #2#1;
 ;
 #0#4, #0#6, #0#8;
 #0#5, #0#7, #0#9;
 ;
 NULL, los_apuntes1;
|FIN;
____________________________________/ EXPORTACION PLAN CONTABLE
|PROCESO el_plan1;
    field = "";
    alfa1 = #1#0 % 110;       field = field + alfa1;   || cuenta
    alfa1 = #1#2 % 124;       field = field + alfa1;   || nombre
    alfa1 = #1#3 % 101;       field = field + alfa1;   || pase directo

|HAZ graba;
|FPRC;

|DEFBUCLE el_plan;
 #1#1;
 ;
 #0#2, 0;
 #0#3, 9;
 ;
 NULL, el_plan1;
|FIN;
____________________________________/ CALCULO PRINCIPAL Y RUTINAS COMUNES
|PROCESO cierra;
|FCIERRA fcanl;
    Pos = -1;
|FPRC;

|PROCESO graba;
    field = fcanl + " " + field;
|FGRABA field;
|FPRC;

|PROCESO abre;
    sw_fabre = 0;
|QBF ffich;
    ffich = fabre + ffich;
|FABRE ffich;
|SI FSalida ] 0;
        fcanl = FSalida;
|SINO;
        sw_fabre = 1;
|FINSI;
|FPRC;

|PROCESO tipo3; |TIPO 3;
|SI #0#10 = "S";
        ffich = #0#0;
    |HAZ abre;
    |SI sw_fabre = 0;
        |BUCLE el_plan;
        |HAZ cierra;
    |SINO;
            alfa1 = "    Problemas al intentar crear secuencial " + #0#0;
        |MENAV alfa1;
    |FINSI;
|FINSI;
|SI #0#11 = "S";
        ffich = #0#1;
    |HAZ abre;
    |SI sw_fabre = 0;
        |BUCLE los_apuntes;
        |HAZ cierra;
    |SINO;
            alfa1 = "    Problemas al intentar crear secuencial " + #0#1;
        |MENAV alfa1;
    |FINSI;
|FINSI;
|FPRC;
