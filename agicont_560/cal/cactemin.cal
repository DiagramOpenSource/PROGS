|FICHEROS;
    cactemin #00;   ||  el #5 y el #8 estan reservados para los 'INCLUYE'
    camaasic #02;   || /para 'cacuenta' y 'catradia' respect.
    camaasil #03;
    camingca #06;
    camingli #07;
    camandia #10;
    cacuebal #11;
|FIN;

|VARIABLES;
    &entrada = 1;
    alfa1 = "";
    alfa2 = "";
    alfa3 = "";
    alfa4 = "";
    nume1 = 0;
    nume2 = 0;
    nume3 = 0;
    nume4 = 0;

    diar = 0;
    fech = @;
    asie = 0;
    apun = 0;
    cuen = "";
    digi = 0;
    sign = "";
    ptas = 0;
    conc = 0;
    desc = "";

    sw = 0;
    estado = 0;

    mensa2   = "     No esta definida esta cuenta en Cuentas de Balance";
    uno = "";
    dos = "";
    mascara = "";
|FIN;

|INCLUYE inicio_i;
|INCLUYE actcta_i;
|INCLUYE conasi_i;

|| ************************************************************************
|| *********** CALCULOS PANTALLA ******************************************
|| ************************************************************************
|PROCESO mayor;
  alfa1 = #0Campo > " ";
  #0Campo = alfa1;
  |PINTA #0Campo;
  |SI Campo = 8;
      |SI #0#8 = "S"; |C_M #0#9,1,"N"; |FINSI;
      |SI #0#8 = "N"; |C_M #0#9,1,"S"; |FINSI;
  |FINSI;
|FPRC;

|PROCESO limite; |TIPO 0;
 |C_M #0#9,0,alfa1;
 |SI alfa1 = "N"; |ACABA; |FINSI;
 |SI #0Campo< fec1; |O #0Campo > fec2;
     |MENAV "    ATENCION!!! La fecha no corresponde al ejercicio contable ...";
     |ERROR;
 |FINSI;
|FPRC;

|PROCESO final; |TIPO 7;
 |PTEC 816;
|FPRC;

|| ************************************************************************
|PROCESO banco;
  apun = apun + 10;

  cuen  = #6#4;
  digi  = #6#5;             || apunte del banco
  sign  = "H";
  conc  = #0#10;
  alfa2 = #0#11; |QBF alfa2;
  alfa1 = #6#0;  alfa1 = "00000" + alfa1;  alfa1 = alfa1 % -106;
  desc = alfa2 + " " + alfa1;
  |HAZ lineas;
|FPRC;

|PROCESO proveedor;
 |SI sw = 0;
     sw = 1;
     |HAZ contador;
     asie = #20#1;
     apun = -9;
     |SI #0#8 = "S"; fech = #6#2; |FINSI;
 |FINSI;

 apun = apun + 10;
 cuen = #7#9;
 digi = #7#10;
 sign = "D";
 ptas = #7#8;
 conc  = #0#12;
 alfa2 = #0#13; |QBF alfa2;
 alfa1 = #7#3;  alfa1 = "000000" + alfa1;  alfa1 = alfa1 % -106;
 alfa1 = " " + alfa1;
 desc =  alfa2 + alfa1;

 |HAZ lineas;
 |SI #0#7 = 2;
     ptas  = #7#8;
     |HAZ banco;
 |FINSI;
|FPRC;

|PROCESO contab1;   || controla la grabacion de apuntes
 |SI #6#10 = "N"; |ACABA; |FINSI;
 sw = 0;
 |BUCLELIN 2proveedor#6;
 |SI sw = 1;
     |SI #0#7 = 1;
         ptas  = #6#9;
         |HAZ banco;
     |FINSI;
     #6#11 = "S";       || contabilizada
     |GRABA 020#6a;
 |FINSI;
 |LIBERA #6;
|FPRC;

|DEFBUCLE cacterem0;
 #6#1;
 2,3,11;
 #0#0, #0#2, #0#4, "N";
 #0#1, #0#3, #0#5, "N";
 be;
 NULL, contab1;
|FIN;

|| **************************************************************************

|PROCESO proceso; |TIPO 3;
  || .................. Abre fichero Auxiliar de Cuentas
    |ABRE #8;
    |LEE 101#8u;
    |SI FSalida ! 0;
        |DDEFECTO #8;
        #8#0 = 1;
    |SINO;
        #8#0 = #8#0 + 1;
    |FINSI;
    |GRABA 101#8n;
    codcon = #8#0;
    |LIBERA #8;
    |CIERRA #8;
  || ----------------------------------------------------------

 |ABRE #2;
 |ABRE #3;

 diar = #0#6;
 fech = #0#9;

 |BUCLE cacterem0;
 |CIERRA #2;
 |CIERRA #3;

 || ............... Terminar
 |SI modcon = 0;
      |ABRET #8
      |DDEFECTO #8;
      #8#0 = codcon;
      |LEE 040#8=;
      |BORRA 101#8a;
      |CIERRAT #8;
 |SINO;
      |CORRE "camaapua.tab"
 |FINSI;

|FPRC;

|| ************************************************************************
|PROCESO lineas;    || graba apunte

    #0#14 = #6#0;   |PINTA #0#14;
    #0#15 = asie;   |PINTA #0#15;
    #0#16 = apun;   |PINTA #0#16;

    |DDEFECTO #3;
    #3#00 = diar;   || diario
    #3#01 = fech;   || fecha
    #3#02 = asie;   || asiento
    #3#03 = apun;   || apunte
    #3#04 = cuen;   || cuenta
    #3#05 = digi;   || digito
    #3#06 = conc;   || concepto
    #3#07 = desc;   || descripcion
    #3#08 = sign;   || signo
    #3#09 = ptas;   || importe
    |SI #3#08 = "D";
        #3#16 = #3#4;
        #3#17 = #3#5;
        #3#10 = "";
        #3#11 = 0;
    |SINO;
        #3#16 = "";
        #3#17 = 0;
        #3#10 = #3#4;
        #3#11 = #3#5;
    |FINSI;
    |GRABA 020#3n;
    |HAZ cabecera;           || cabecera apunte
    ressum = #3#9;
    |HAZ actualiz1;          || cuenta
    |HAZ actualiz3;          || diario
    opera = "A";
    |HAZ altatra;            || subniveles
|FPRC;

|PROCESO cabecera;       || actualiza cabeceras
 |DDEFECTO #2;
 #2#00 = #3#0; ||  diario
 #2#01 = #3#1; ||  fecha
 #2#02 = #3#2; ||  documento
 #2#03 = #3#2; ||  asiento
 |LEE 101#2=;
 estado = FSalida;
 |SI #3#08 = "D";
     #2#04 = #2#04 + #3#09;
     |SI #2#09 = 0; #2#08 = #3#4; #2#09 = #3#5; |FINSI;  || contrp.HABER
 |SINO;
     #2#05 = #2#05 + #3#09;
     |SI #2#11 = 0; #2#10 = #3#4; #2#11 = #3#5; |FINSI;  || contrp.DEBE
 |FINSI;
 #2#6 = #2#4 - #2#5;      || saldo
 |SI estado = 0;  |GRABA 020#2a;  |FINSI;
 |SI estado ! 0;  |GRABA 020#2n;  |FINSI;
 |LIBERA #2;
|FPRC;

|PROCESO actualiz3;
 |ABRE #10;
 #10#0 = #3#0;
 |LEE 121#10=;
 |SI FSalida ! 0;
     |DDEFECTO #10;
     #10#0 = #3#0;
     #10#1 = "<DIARIO SIN DEFINIR>";
     |GRABA 020#10b;
 |FINSI;
 |SI #3#8 = "D";  #10#2 = #10#2 + #3#9;  |FINSI;
 |SI #3#8 = "H";  #10#3 = #10#3 + #3#9;  |FINSI;
 |GRABA 020#10a;
 |LIBERA #10;
 |CIERRA #10;
|FPRC;

|PROCESO cuenta;    || comprueba si la cuenta esta dada de alta
 |ABRE #5;
 #5#0 = cuen;
 #5#1 = digi;
 |LEE 001#5=;
 |SI FSalida ! 0;
     |DDEFECTO #5;
     #5#0 = cuen;
     #5#1 = digi;
     |HAZ defect1;
     |PUSHV 0501 2380;
     |PINPA #0#5;
     |PINDA #0#5;
     |ENDATOS #1#5;
     |GRABA 020#5n;
     |POPV;
 |FINSI;
 |CIERRA #5;
|FPRC;

|PROCESO defect1;
 |ABRE #11;
 uno = #5#0;
 |QBF uno;
 dos = uno + "000000000000";
 uno = dos % A101;
 #11#0 = uno;
 |LEE 000#11=;
 |SI FSalida ! 0;
     uno = dos % A102;
     #11#0 = uno;
     |LEE 000#11=;
     |SI FSalida ! 0;
         uno = dos % A103;
         #11#0 = uno;
         |LEE 000#11=;
         |SI FSalida ! 0;
             uno = dos % A104;
             #11#0 = uno;
             |LEE 000#11=;
             |SI FSalida ! 0;
                 |MENAV mensa2;
             |SINO;
                 |HAZ saca1;
             |FINSI;
         |SINO;
             |HAZ saca1;
         |FINSI;
     |SINO;
         |HAZ saca1;
     |FINSI;
 |SINO;
     |HAZ saca1;
 |FINSI;
 |CIERRA #11;
 || la 3a clave esta formada por la cta + zzzz y el campo fantasma del nivel;
 mascara = #5#0;
 |QBF mascara;
 mascara = mascara +"zzzzzzzzzzzz";
 mascara = mascara % 112;
 #5#54    = mascara;
 #5#55    = #5#1;
|FPRC;

|PROCESO saca1;
    #5#4 = #11#1;
    #5#5 = #11#2;
    #5#6 = #11#3;
    #5#7 = #11#4;
    #5#8 = #11#5;
|FPRC;
|| --------------------------------------------------------------------
