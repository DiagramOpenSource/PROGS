|FICHEROS;
     caconccu #0;
     camaasil #1;
     zconcili #2;
     caconcil #3;
     zconcil1 #4;          ||Indexado para Importes;
     cacuenta #10;
|FIN;

|VARIABLES;
     &entrada = 1;
     cta       = 0;
     cta_t     = 0;
     dig_t     = 0;
     long      = "  ";
     sw        = 0;
     error     = 0;
     &contador = 0;
     &cuenta   = "";
     &digito   = 0;
     &saldoant = 0;
     &nombre   = "";
     &SalActual = 0;
     &TipoCuadre = "";
     feini = @;
     fefin = @;
     TotDebe = 0;
     TotHabe = 0;
     HaySaldo = 0;
     BuscaImp = 0;
     DebeHaber  = "D";
     SegundaPasada = 0;
     EncoImp = 0;
|FIN;

|INCLUYE acceso_i;
|INCLUYE puntos_i;
|INCLUYE i_ficher;

|PROCESO mayor;
     #0Campo = #0Campo > "";
     |PINTA #0Campo;
|FPRC;

|PROCESO cuenta; |TIPO 6;
     salidas = FSalida;
     p_alfa1 = #0Campo;
     |HAZ punto;
     |SI p_error ! 0;  |ERROR;  |ACABA;  |FINSI;
     cta_t = Campo;
     dig_t = Campo + 1;
     |HAZ cuenta_t;
|FPRC;

|PROCESO cuenta_t;
     cta  = #0cta_t;
     long = cta % 0;
     sw   = 0;
     |SI long = dig4; sw = 1; #0dig_t = 1; |FINSI;
     |SI long = dig5; sw = 1; #0dig_t = 2; |FINSI;
     |SI long = dig6; sw = 1; #0dig_t = 3; |FINSI;
     |SI long = dig7; sw = 1; #0dig_t = 4; |FINSI;
     |SI long = dig8; sw = 1; #0dig_t = 5; |FINSI;
     |SI long = dig9; sw = 1; #0dig_t = 6; |FINSI;

     |SI sw = 0;
         #0dig_t = 0;
         |MENAV "    Longitud de cuenta fuera de Nivel !";
         |ERROR;
         |ACABA;
     |FINSI;
|FPRC;

|PROCESO cuefin; |TIPO 0;
     error = 0;
     |ABRE #10;
     #10#0 = #0cta_t;
     #10#1 = #0dig_t;
     |LEE 040#10=;
     |SI FSalida ! 0; error = 1; |CIERRA #10; |ERROR; |ACABA; |FINSI;
     |SI #10#3 = "N";
         error = 1;
         |CIERRA #10;
         |MENAV "      La cuenta no tiene pase DIRECTO !";
         |ERROR;
         |ACABA;
     |FINSI;
     #0#4 = #10#2;
     #0#6 = #10#53;
     |PINTA #0#4;
     |PINTA #0#6;
     |CIERRA #10;
|FPRC;

|PROCESO calcuant;
     |SI #1#8 = "D";
         #0#2 = #0#2 + #1#9;
     |SINO;
         #0#2 = #0#2 - #1#9;
     |FINSI;
|FPRC;

|PROCESO camaasil1;
     contador = contador + 1;
     #2#0 = contador;
     #2#1 = #1#0;
     #2#2 = #1#1;
     #2#3 = #1#2;
     #2#4 = #1#3;
     #2#5 = #1#7;
     |SI #1#8 = "D";
         #2#6 = #1#9;
         #2#7 = 0;
     |SINO;
         #2#7 = #1#9;
         #2#6 = 0;
     |FINSI;
     TotDebe = TotDebe + #2#6;
     TotHabe = TotHabe + #2#7;
     #2#9 = TotDebe - TotHabe;
     |SI #2#9 = 0;   HaySaldo = HaySaldo + 1;  |FINSI;
     #2#8 = #1#18;
     |GRABA 040#2n;
|FPRC;

|PROCESO indexa1;
     contador = contador + 1;
     #2#0 = contador;
     #2#1 = 99;
     #2#2 = #3#4;
     #2#3 = 999999;
     #2#4 = #3#2;
     #2#5 = #3#5;
     #2#6 = #3#6;
     #2#7 = #3#7;
     #2#8 = #3#8;
     TotDebe = TotDebe + #2#6;
     TotHabe = TotHabe + #2#7;
     #2#9 = TotDebe - TotHabe;
     |SI #2#9 = 0;   HaySaldo = HaySaldo + 1;  |FINSI;
     |GRABA 040#2n;
|FPRC;

|PROCESO grabaapun;
     |SI #2#1 = 99; |Y #2#3 = 999999;
        #3#0 = #0#0;
        #3#1 = #0#1;
        #3#2 = #2#4;
        |LIBERA #3;
        |LEE 110#3=;
        |SI FSalida = 0;
            |BORRA 020#3a;
        |FINSI;
     |SINO;
        #1#0 = #2#1;
        #1#1 = #2#2;
        #1#2 = #2#3;
        #1#3 = #2#4;
        |LIBERA #1;
        |LEE 110#1=;
        |SI FSalida = 0;
            #1#18 = "û";
            |GRABA 020#1a;
        |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE camaasil0;
     #1#3;
     18;
     #0#0, #0#1, feini, 000000, "û";
     #0#0, #0#1, fefin, 999998, "û";
     e;
     NULL, calcuant;
|FIN;

|DEFBUCLE camaasil1;
     #1#3;
     18;
     #0#0, #0#1, feini, 000000, " ";
     #0#0, #0#1, fefin, 999998, " ";
     e;
     NULL, camaasil1;
|FIN;

|DEFBUCLE caconcil;
     #3#1;
     3,8;
     #0#0, #0#1, 001, 2, " ";
     #0#0, #0#1, 999, 2, " ";
     e;
     NULL, indexa1;
|FIN;

|DEFBUCLE zconcili;
     #2#1;
     8;
     0001, "*";
     9999, "*";
     e;
     NULL, grabaapun;
|FIN;

|PROCESO ConCero;
     |SI HaySaldo = 0;  |ACABA;  |FINSI;
     #2#8 = "*";
     |GRABA 101#2a;
     #0#7 = #0#7 + #2#6;
     #0#8 = #0#8 + #2#7;
     #0#3 = #0#7 - #0#8;
     |SI #2#9 = 0;  HaySaldo = HaySaldo - 1;  |FINSI;
|FPRC;

|DEFBUCLE ConCero;
     #2#1;
     ;
     1;
     9999;
     e;
     NULL,ConCero;
|FIN;

|RUTINA SaldoCero;
     |BUCLE ConCero;
|FRUT;

|RUTINA GrabaImp;
     #4#0 = BuscaImp;
     #4#1 = DebeHaber;
     #4#2 = 0;
     #4#3 = " ";
     |LEE 101#4];
     |SI FSalida = 0;
         |SI #4#0 ! BuscaImp;  |LIBERA #4;  |ACABA;  |FINSI;
         |SI #4#1 ! DebeHaber;   |LIBERA #4;  |ACABA;  |FINSI;
         |SI #4#3 = " ";
             EncoImp = 1;
             #4#3 = "C";
             |GRABA 101#4a;
             |LIBERA #4;
             |ACABA;
        |FINSI;
     |FINSI;

    |ET LeoOtra;
    |LIBERA #4;
    |LEE 101#4s;
    |SI FSalida = 0;
        |SI #4#0 ! BuscaImp;  |LIBERA #4;  |ACABA;  |FINSI;
        |SI #4#1 ! DebeHaber;   |LIBERA #4;  |ACABA;  |FINSI;
        |SI #4#3 = " ";
            EncoImp = 1;
            #4#3 = "C";
            |GRABA 101#4a;
            |LIBERA #4;
            |ACABA;
        |SINO;
            |VETE LeoOtra;
        |FINSI;
    |FINSI;
|FRUT;

|PROCESO BuscaImportes;
     |SI #2#6 ! 0;
         BuscaImp = #2#6;
         DebeHaber  = "H";
     |SINO;
         BuscaImp = #2#7;
         DebeHaber  = "D";
     |FINSI;
     #4#0 = BuscaImp;
     #4#1 = DebeHaber;
     #4#2 = 0;
     EncoImp = 0;
     |HAZ GrabaImp;
     |SI EncoImp = 0;
         #4#0 = BuscaImp;
         |SI DebeHaber = "D";
             #4#1 = "H";
         |SINO;
             #4#1 = "D";
         |FINSI;
         #4#2 = #2#0;
         #4#3 = " ";
         |GRABA 101#4n;
     |SINO;
         #2#8 = "*";
         #0#7 = #0#7 + #2#6;
         #0#8 = #0#8 + #2#7;
         #0#3 = #0#7 - #0#8;
         SegundaPasada = 1;
         |GRABA 101#2a;
     |FINSI;
|FPRC;

|DEFBUCLE BuscaImportes;
     #2#1;
     ;
     1;
     9999;
     e;
     NULL,BuscaImportes;
|FIN;

|PROCESO zconcil1;
     #2#0 = #4#2;
     |LEE 101#2=;
     #2#8 = "*";
     #0#7 = #0#7 + #2#6;
     #0#8 = #0#8 + #2#7;
     #0#3 = #0#7 - #0#8;
     |GRABA 101#2a;
|FPRC;

|DEFBUCLE zconcil1;
     #4#2;
     ;
     0   , "C";
     9999, "C";
     e;
     NULL,zconcil1;
|FIN;

|RUTINA Importes;
     |BUCLE BuscaImportes;
     |CIERRA #4;
     |SI SegundaPasada = 1;
         |ABRE #2;
         |BUCLE zconcil1;
         |CIERRA #2;
     |FINSI;
|FRUT;

|PROCESO concilia; |TIPO 3;
     |SI error = 1; |ACABA; |FINSI;
     |SI #0#5 = "I";
         comienza = "k";
         |HAZ fichero;
         |NOME_DAT #4 fichero;
         |ABRE #4;
         |CIERRA #4;
         |DELINDEX #4;
         |ABRE #4;
     |FINSI;
     comienza = "z";
     |HAZ fichero;
     |NOME_DAT #2 fichero;
     |ABRE #2;
     |CIERRA #2;
     |DELINDEX #2;
     |MENSA "2400 Procesando ......";
     feini = "01.01.1900";
     fefin = "31.12.2999";

     |BUCLE camaasil0;

     |PINTA #0#2;
     |ABRE #2;
     contador = 0;
     feini = "01.01.1900";
     fefin = "31.12.2999";
     TotDebe = 0;
     TotHabe = 0;
     #0#7 = 0;
     #0#8 = 0;
     HaySaldo = 0;
     |BUCLE camaasil1;
     |BUCLE caconcil;
     |CIERRA #2;
     |BLIN 24;
     cuenta   = #0#0;
     digito   = #0#1;
     saldoant = #0#2;
     nombre   = #0#4;
     TipoCuadre = #0#5;
     |SI contador = 0;
         |MENAV "      No hay nada para Conciliar";
         |DELINDEX #2;
         |SI #0#5 = "I";
             |CIERRA #4;
             |DELINDEX #4;
         |FINSI;
         |ACABA;
     |FINSI;
     |SI #0#5 = "S";  |Y HaySaldo ] 1;
         |HAZ SaldoCero;
     |SINO;
         |SI #0#5 = "I";
             |HAZ Importes;
         |FINSI;
     |FINSI;

     |PINTA #0#3;
     |PINTA #0#7;
     |PINTA #0#8;
     SalActual = #0#3;
     |HAZ Concilia;

     |CONFI "2400SActualizar Conciliacion : ";
     |SI FSalida = 0;
         |ABRE #1;
         |ABRE #3;
         |BUCLE zconcili;
         |CIERRA #1;
         |CIERRA #3;
     |FINSI;
     |DELINDEX #2;
     |SI #0#5 = "I";
         |DELINDEX #4;
     |FINSI;
|FPRC;
