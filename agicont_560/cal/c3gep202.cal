|FICHEROS;
  c3gep202 #0;
  cacuenta #1;
  isocie29 #2;
  cacup202 #3;
  camaasic #4;
  camaasil #5;
  camandia #6;
  c3gep301 #7,MANTE;  || def de pantalla asientos de cada Empresa
  agicontr #23;
|FIN;

|VARIABLES;
  &modcon = 0;
  conx = 0;
  nume = 0;
  num1 = 0;
  num2 = 0;
  num3 = 0;
  num4 = 0;
  num5 = 0;
  num6 = 0;
  num7 = 0;
  num8 = 0;

  line = 0;
  importe = 0;
  imppago = 0;
  opera = "B";
  FEstado = 0;
  ini = 0;
  fecalf = @;
  mes = "";
  mesfec = 0;
  mesac = 0;
  a = 0;
  b = 0;
  c = 0;
  asie = 0;
  cantidad = 0;
  empcon = 0;
  ejecon = 0;
  leidos = 0;
  dirfich0 = "";
  dirfich1 = "";
  &dir_H = "";
  xx = 0;
  yy = 0;
  desde = 0;
  hasta = 0;
  x = 0;
  y = 0;
  varcamp = 0;
  &r_emp = 0;
  &r_per = 0;
  alfa1 = "";
  alfa2 = "";
  sw_error = 0;
  men1 = "     NO Existe Periodo en la empresa Contable";
  men2 = "     El a¤o contable NO esta dentro de la seleccion";
  men3 = "    No existe Pago a Cuenta - Modelo 202 ...";
  men4 = "     NO Esta Definido El ASIENTO de Pase";
  men5 = "    Pago a Cuenta igual a Cero ...";
  men9 = "     Empresa Contable INACTIVA ";
  sw = 0;
  pasar = 0;
  concepto = "";
  agi  = "";
  agi1 = "";
  agi2 = "";
  num = 0;
  aste = "";
  longi = "";
|FIN;

|INCLUYE inici1_i;
|INCLUYE conasi_i;

|| *************************************************************************
||                        CALCULOS DE PANTALLA
|| *************************************************************************
|PROCESO tipo8; |TIPO 8;
|HAZ comienzo;
|| ... Controlar Impuesto de Sociedades
  |HAZ tipo_8;
  |SI aste = "*";
       |MENAV "    ATENCION!!! Impuesto de Sociedades no instalado ...";
       |PTEC 807;
       |ACABA;
  |FINSI;
|FPRC;
|| ......................................................................
|PROCESO tipo_8; || control de la ficha de clientes general
  |DIRECTORIO agi;
  |QBF agi;
  |PATH_DAT #23 agi;

|ABRE #23;
#23#0 = 1;
|LEE 040#23=;
    || |SI FSalida ! 0; aste = "*"; |CIERRA #23; |ACABA; |FINSI;
    |SI FSalida ! 0;
        agi1 = agi + "sociedad/fich/";
        |CIERRA #23;
        |ACABA;
    |FINSI;
|CIERRA #23;
aste = #23#4;
|QBF aste;
   |SI aste  = "*"; |ACABA; |FINSI;
agi1 = #23#4; |QBF agi1;
longi = agi1 % 0;
num   = longi;
num   = 100 + (num - 5);
longi = num;
agi2  = (agi1 % longi) + "pan/";
|FPRC;

|PROCESO mayor; |TIPO 0;
  alfa1 = #0Campo > " ";
  #0Campo = alfa1;
  |PINTA #0Campo;
|FPRC;

|PROCESO def_any; |TIPO 7;
   #0#1 = #30#26;
   |PINTA #0#1;
   alfa1 = fec2 % -104;
   #0#2 = alfa1;
   |PINTA #0#2;
|FPRC;

|PROCESO mayor2; |TIPO 0;
  |HAZ mayor;
  |SI #0#4 = "N";
      |CAMPO_MODIFICA #0#5,1,"N";
      #0#5  = "S";
      |PINTA #0#5;
  |SINO;
      |CAMPO_MODIFICA #0#5,1,"S";
  |FINSI;
|FPRC;

|PROCESO fechbue; |TIPO 0;
  |SI FSalida = 2; |ACABA; |FINSI;
  |SI #0#3 < fec1;  |O #0#3 > fec2;
      |MENAV "     Fecha ERRONEA";
      |ERROR;
      |ACABA;
  |FINSI;
|FPRC;

|PROCESO mayor1; |TIPO 0;
  |HAZ mayor;
  |SI #0Campo = "N";
      |PARA x = 11;  |SI x [ 50;  |HACIENDO x = x + 1;
            |CAMPO_MODIFICA #0x,1,"S";
      |SIGUE;
      #0#7  = 0;
      #0#9  = 0;
      #0#8  = 0;
      #0#10 = 0;
      |CAMPO_MODIFICA #0#7,1,"N";
      |CAMPO_MODIFICA #0#9,1,"N";
      |CAMPO_MODIFICA #0#8,1,"N";
      |CAMPO_MODIFICA #0#10,1,"N";
      |PINTA #0#7;
      |PINTA #0#9;
      |PINTA #0#8;
      |PINTA #0#10;
  |SINO;
      |PARA x = 11;  |SI x [ 50;  |HACIENDO x = x + 1;
            #0x = 0;
            |CAMPO_MODIFICA #0x,1,"N";
      |SIGUE;
      |CAMPO_MODIFICA #0#7,1,"S";
      |CAMPO_MODIFICA #0#9,1,"S";
      |CAMPO_MODIFICA #0#8,1,"S";
      |CAMPO_MODIFICA #0#10,1,"S";
  |FINSI;
|FPRC;

|PROCESO consulta; |TIPO 0;
  |SI FSalida ! 9;  |ACABA;  |FINSI;
  |SUB_EJECUTA "caconemp.run";
  |ERROR;
  |SI Campo = 7;
      #0#7 = r_emp;
      #0#9 = r_per;
      |PTEC 802;
      |PTEC 802;
  |FINSI;
  |SI Campo = 8;
      #0#8 = r_emp;
      #0#10 = r_per;
      |PTEC 802;
  |FINSI;
  |SI Campo ] 11;
      #0Campo = r_emp;
      varcamp = Campo + 20;
      #0varcamp = r_per;
      |PTEC 802;
      |PTEC 802;
  |FINSI;
|FPRC;

|PROCESO deshas; |TIPO 0;
  |SI #0#8 < #0#7;
      |MENAV "      Error en Introduccion";
      |ERROR;
  |FINSI;
|FPRC;

|| --------------- comprueba que la empresa es buena --------------------
|PROCESO cup202; || Control de Asto. contable
  |PATH_DAT #3 dirfich0;
  |ABRE #3;
  |DDEFECTO #3;
  #3#0 = 1;
  |LEE 000#3=;
  |SI FSalida ! 0; sw_error = 4; |FINSI;
  |CIERRA #3;
|FPRC;

|PROCESO man202; || Impuesto de Sociedades
  dirfich1 = agi1;
  |PATH_DAT #2 dirfich1;
  |ABRE #2;
  |DDEFECTO #2;
  #2#0 = #30#2; || empresa
  #2#1  = #0#2; || a¤o
  |LEE 000#2=;
  |SI FSalida ! 0; sw_error = 3; |FINSI;
  |SI FSalida ! 0;
      sw_error = 3;
  |SINO;
      imppago = 0;
      |SI #2#30 = 1;
          num8 = #0#0 + 5;
      |SINO;
          num8 = #0#0 + 23;
      |FINSI;
      imppago = #2num8;
      |SI imppago = 0;  sw_error = 5; |FINSI;
   |FINSI;
   |CIERRA #2;
|FPRC;

|| --------------- comprueba desde-hasta empresas ---------------------------

|PROCESO buscaem1;
  sw_error = 0;
  |SI #30#21 = "I";  |ACABA; |FINSI;
  |SI #30#26 ! #0#1; |ACABA; |FINSI;

  |DBASE dir_fich;
  dirfich0 = dir_fich + "fich/";
  alfa1 = #30#2;
  alfa2 = #30#3;
  alfa1 = "00000" + alfa1;   alfa1 = alfa1 % -105;
  alfa2 = "0"   + alfa2;   alfa2 = alfa2 % -101;
  dirfich0 = dirfich0 + alfa1 + alfa2 + "/";

  |HAZ cup202;
  |SI sw_error ! 0;  |ACABA; |FINSI;
  |HAZ man202;
  |SI sw_error ! 0;  |ACABA; |FINSI;
  leidos = 1;
  |ERROR10;
|FPRC;

|DEFBUCLE buscaempre;
#30#1;
;
#0#7,desde;
#0#8,hasta;
e;
NULL,buscaem1;
|FIN;

|PROCESO buclempre;
  |SI #0#9 > #0#10;
      |MENAV "     Error en Introduccion";
      |ERROR;
      |ACABA;
  |FINSI;
  |SI #0#7 = 0; |Y #0#8 = 0; |ACABA; |FINSI;
  desde = #0#9;
  hasta = #0#10;
  leidos = 0;
  |BUCLE buscaempre;
  |SI leidos = 0;
      |MENAV "    ATENCION SELECCION VACIA";
      |ERROR;
      |ACABA;
  |FINSI;
|FPRC;

|| ------------------------------------------------------------------------

|PROCESO nomodi;
  |PARA x = Campo + 1;  |SI x [ 30;  |HACIENDO x = x + 1;
        #0x = 0;
        |CAMPO_MODIFICA #0x,1,"N";
        y = x + 20;
        #0y = 0;
        |CAMPO_MODIFICA #0y,1,"N";
  |SIGUE;
    y = Campo + 20;
  #0y = 0;
  |CAMPO_MODIFICA #0y,1,"N";
  |PINTA #0y;
|FPRC;

|PROCESO esbuena; || desde campo de empresas seleccion
  |SI FSalida = 2; |ACABA; |FINSI;
  |SI #0Campo = 0; |HAZ nomodi;  |ACABA;  |FINSI;
  |ABRE #30;
  #30#2 = #0Campo;
  #30#3 = 0;
  |LEE 011#30];
  |SI FSalida ! 0;  |O #30#2 ! #0Campo;
      |MENAV "     NO Existe la empresa Contable";
      |ERROR;
  |FINSI;
  |CIERRA #30;
|FPRC;

|PROCESO esbuena1;
  |SI FSalida = 2;|ACABA;|FINSI;
  x = Campo - 20;
  |SI #0x = 0;  |ACABA;  |FINSI;

  sw_error = 0;
  |ABRE #30;
  x = Campo - 20;
  #30#2 = #0x;
  #30#3 = #0Campo;
  |LEE 011#30=;
  |SI FSalida ! 0;
      sw_error = 1;
      |CIERRA #30;
      |MENAV men1;
      |ERROR;
      |ACABA;
  |FINSI;
  |CIERRA #30;

  |SI #30#21 = "I";
      |MENAV men9;
      |ERROR;
      |ACABA;
  |FINSI;

  |SI #30#26 ! #0#1; sw_error = 2; |MENAV men2; |ERROR; |ACABA; |FINSI;

  |DBASE dir_fich;
  dirfich0 = dir_fich + "fich/";
  alfa1 = #30#2;
  alfa2 = #30#3;
  alfa1 = "00000" + alfa1;   alfa1 = alfa1 % -105;
  alfa2 = "0"   + alfa2;   alfa2 = alfa2 % -101;
  dirfich0 = dirfich0 + alfa1 + alfa2 + "/";

  |HAZ man202;
  |SI sw_error ! 0;
      |SI sw_error = 3; |MENAV men3; |FINSI;
      |SI sw_error = 5; |MENAV men5; |FINSI;
      |ERROR; |ACABA;
  |FINSI;

  |HAZ cup202;
  |SI sw_error ! 0; |MENAV men4; |ERROR; |ACABA; |FINSI;

  x = Campo + 20;
  #0x = #30#1;
  |PINTA #0x;
|FPRC;

|| **********************************************************************
||                       CALCULOS DEL PROCESO
|| **********************************************************************

|| ---------- Seleccion DESDE / HASTA Empresa.

|PROCESO esdesd1;
  sw_error = 0;
  |SI #30#26 ! #0#1; |ACABA; |FINSI;
  |SI #30#21 = "I";  |ACABA; |FINSI;

  |DBASE dir_fich;
  dirfich0 = dir_fich + "fich/";
  alfa1 = #30#2;
  alfa2 = #30#3;
  alfa1 = "00000" + alfa1;   alfa1 = alfa1 % -105;
  alfa2 = "0"   + alfa2;   alfa2 = alfa2 % -101;
  dirfich0 = dirfich0 + alfa1 + alfa2 + "/";

  |HAZ man202;
  |SI sw_error ! 0; |ACABA; |FINSI;
  |HAZ cup202;
  |SI sw_error ! 0; |ACABA; |FINSI;

  |HAZ haztodo;
|FPRC;

|DEFBUCLE esdesde;
#30#1;
;
#0#7, #0#9;
#0#8,#0#10;
e;
NULL,esdesd1;
|FIN;

|| ---------- Seleccion en parrilla.

|PROCESO esunaauna;
  |PARA xx = 11;  |SI xx [ 30;  |HACIENDO xx = xx + 1;
        sw_error = 0;
        |SI #0xx = 0;  |SAL;  |FINSI;

        |DBASE dir_fich;
        dirfich0 = dir_fich + "fich/";
        yy = xx + 20;
        alfa1 = #0xx;
        alfa2 = #0yy;
        alfa1 = "00000" + alfa1;   alfa1 = alfa1 % -105;
        alfa2 = "0"   + alfa2;   alfa2 = alfa2 % -101;
        dirfich0 = dirfich0 + alfa1 + alfa2 + "/";
|| lee empresa
        |ABRE #30;
        #30#2 = #0xx;
        #30#3 = #0yy;
        |LEE 011#30=;
        |SI FSalida ! 0; |CIERRA #30; |VETE otramas; |FINSI;
        |CIERRA #30;
        |SI #30#26 ! #0#1; |O #30#21 = "I";
            |VETE otramas;
        |FINSI;
        |HAZ man202;
        |SI sw_error ! 0; |VETE otramas; |FINSI;
        |HAZ cup202;
        |SI sw_error ! 0; |VETE otramas; |FINSI;
        |HAZ haztodo;
|ET otramas;
  |SIGUE;
|FPRC;

|| *********************************************************************
||                              PROGRAMA
|| *********************************************************************

|PROCESO tipo3; |TIPO 3;
  sw = 0;
  |SI #0#6 = "S";
      |BUCLE esdesde;
  |SINO;
      |HAZ esunaauna;
  |FINSI;
|FPRC;

|| ***********************************************************************
||                    ASIENTO CONTABLE 300
|| ***********************************************************************
|PROCESO haztodo;

  enEmpresa = #canempre#2;
  enPeriodo = #canempre#3;
  swOperacion = 0;
  |SUB_EJECUTA "camoneda.tab";

  dir_H = dirfich0;
      dig1 = #30#11;
      dig3 = #30#13;
      dig4 = #30#14;
      dig5 = #30#15;
      dig6 = #30#16;
      dig7 = #30#17;
      dig8 = #30#18;
      dig9 = #30#19;
  |PATH_DAT #1  dirfich0;
  |PATH_DAT #2  dirfich1;
  |PATH_DAT #3  dirfich0;
  |PATH_DAT #4  dirfich0;
  |PATH_DAT #5  dirfich0;
  |PATH_DAT #6  dirfich0;
  |PATH_DAT #8  dirfich0;
  |PATH_DAT #20 dirfich0;

  empcon = #30#1;
  ejecon = #30#26;
  line = 1;
  modcon = 0;
  pasar = 0;
  |ABRET #1;
  |HAZ lacarga;
  |SI pasar = 1; |CIERRAT #1; |ACABA; |FINSI;

  |ATRI R;
  |MENSA "2300 CREANDO EL ASIENTO";
  |ATRI 0

  |HAZ elpase;
  |HAZ final;
  |CIERRAT #1;
|FPRC;

|PROCESO lee_cuenta;
  |DDEFECTO #1;
  #1#0 = #7num1;
  #1#1 = #7num2;
  |LEE 000#1=;
|FPRC;

|PROCESO lacarga;
  |DDEFECTO #7;
  |PINPA #0#7;
  |PARA x = 54; |SI x [ 65; |HACIENDO x = x + 1;
        |CAMPO_MODIFICA #7x,1,"N";
  |SIGUE;
|| -- Empresa
  #7#0 = #30#2;
  #7#1 = #30#3;
  #7#2 = #30#1;

|| -- Diario
  #7#3 = #3#45;
|| -- Fecha
  #7#4 = #0#3;

|| -- Documento
  |ABRE #20;
  |DDEFECTO #20;
  |LEE 101#20p;
  |CIERRA #20;
  #7#5 = #20#1 + 1;

|| -- Datos asiento
  num1 = 6;

  |SI imppago ! 0;   || importe pago a cuenta DEBE
      |HAZ sumanume;
      #7num1 = #3#1;     || cuenta
      #7num2 = #3#12;    || digito
      |HAZ lee_cuenta;
      #7num3 = #1#2;     || descripcion
      #7num4 = #3#23;    || concepto
      #7num5 = #3#34;    || descripcion
      #7num6 = imppago;  || importe
      #7num7 = #3#47;
      |SI #7num7 ! "F";
          |CAMPO_MODIFICA #7num5,1,"S";
      |FINSI;
      num1 = num1 + 1;
  |FINSI;

  |SI imppago ! 0;   ||  Importe Pago a Cuenta HABER
      |HAZ sumanume;
      #7num1 = #3#2;
      #7num2 = #3#13;
      |HAZ lee_cuenta;
      #7num3 = #1#2;
      #7num4 = #3#24;
      #7num5 = #3#35;
      #7num6 = - imppago;
      #7num7 = #3#48;
      |SI #7num7 ! "F";
          |CAMPO_MODIFICA #7num5,1,"S";
      |FINSI;
      num1 = num1 + 1;
  |FINSI;

|ET fincarga;
  |SI #0#4 = "S";
      |HAZ modifcon;
      |PINDA #0#7;
      |SI sw = 2;
          |ENDATOS #1#7;
          |SI FSalida ! 0; sw = 0; pasar = 1; |ACABA; |FINSI;
          concepto = #7conx;
          sw = 1;
          |ACABA;
      |FINSI;
  |SINO;
      |PINDA #0#7;
      |ENDATOS #1#7;
      |SI FSalida ! 0; pasar = 1; |FINSI;
      |ACABA;
  |FINSI;

  |SI #0#5 = "S";
      |CONFI "2455SEs correcto (S/N)     ";
      |SI FSalida ! 0;
          pasar = 1;
      |FINSI;
  |FINSI;
|FPRC;

|PROCESO sumanume;
  num2 = num1 + 11;
  num3 = num1 + 22;
  num4 = num1 + 33;
  num5 = num1 + 44;
  num6 = num1 + 55;
  num7 = num1 + 66;
|FPRC;

|PROCESO modifcon;
  |PARA num1 = 6; |SI num1 [ 16; |HACIENDO num1 = num1 + 1;
        |HAZ sumanume;
        |SI #7num6 ! 0;
            |SI #7num7 ! "F";
                |SI sw ! 0;
                    |CAMPO_MODIFICA #7num5,1,"N";
                    #7num5 = concepto;
                    #7num7 = "G";
                |SINO;
                    #7num7 = "G";
                    concepto = #7num5;
                    sw = 2;
                    conx = num5;
                |FINSI;
            |FINSI;
        |FINSI;
  |SIGUE;
|FPRC;

|| *************************************************************************
|| -------- Crea asiento
|PROCESO elpase;
  |ABRE #4;
  |ABRE #5;
  |PARA num1 = 6;  |SI num1 [ 16 ;  |HACIENDO num1 = num1 + 1;
        num2 = num1 + 11;     ||tengo el digito
        num3 = num1 + 22;     ||tengo la descripcion
        num4 = num1 + 33;     ||tengo el concepto
        num5 = num1 + 44;     ||tengo la descripcion
        num6 = num1 + 55;     ||tengo el importe;
        |SI #7num6 ! 0;                    ||si hay importe
            |SI line = 1;
                |HAZ contador;
                asie = #20#1;
            |FINSI;
            |DDEFECTO #5;

            #5#0 = #7#3;
            #5#1 = #7#4;
            #5#2 = asie;
            #5#3 = line;
            |LEE 101#5=;
            FEstado = FSalida;
            |SI #7num6 > 0;   ||ASIENTO DEBE
                #5#8 = "D";
                #5#9 = #7num6;
                #5#10 = "";
                #5#11 = 0;
                #5#16 = #7num1;
                #5#17 = #7num2;
            |SINO;
                #5#8 = "H";
                #5#9 = -#7num6;
                #5#10 = #7num1;
                #5#11 = #7num2;
                #5#16 = "";
                #5#17 = 0;
            |FINSI;
            #5#4 = #7num1;
            #5#5 = #7num2;
            #5#6 = #7num4;
            #5#7 = #7num5;
            |SI FEstado = 0;
                |GRABA 020#5a;
            |SINO;
                |GRABA 020#5n;
            |FINSI;
            |LIBERA #5;
            |HAZ cabecera;
            |HAZ actualiz1;
            opera = "A";
            |HAZ altatra;
            line = line + 10;
        |FINSI;
  |SIGUE;
  |CIERRA #4;
  |CIERRA #5;
|FPRC;

|PROCESO cabecera;
  |SI #5#9 = 0;  |ACABA;  |FINSI;
  |DDEFECTO #4;
  #4#0 = #5#0; ||  diario
  #4#1 = #5#1; ||  fecha
  #4#2 = #5#2; ||  documento
  #4#3 = #5#2; ||  asiento
  |LEE 101#4=;
  #4#0 = #5#0; ||  diario
  #4#1 = #5#1; ||  fecha
  #4#2 = #5#2; ||  documento
  #4#3 = #5#2; ||  asiento
  FEstado = FSalida;
  |SI #5#8 = "D";
      #4#04 = #4#4 + #5#9;
      |SI #4#9 = 0;                         ||CONTRAPARTIDA
          #4#8 = #5#4;
          #4#9 = #5#5;
      |FINSI;
  |SINO;
      #4#5 = #4#5 + #5#9;
      |SI #4#11 = 0;
          #4#10 = #5#4;
          #4#11 = #5#5;
      |FINSI;
  |FINSI;
  #4#6 = #4#4 - #4#5;
  |SI FEstado = 0;  |GRABA 020#4a;  |FINSI;
  |SI FEstado ! 0;  |GRABA 020#4n;  |FINSI;
  |LIBERA #4;
|FPRC;

|PROCESO actualiz1;
  #1#0 = #5#4;
  #1#1 = #5#5;
  |LEE 101#1=;
  |SI #5#0 = 0;
      |SI #5#8 = "D";
          #1#9 = #1#9 + #5#9;
          #1#51 = #1#51 + #5#9;
      |SINO;
          #1#10 = #1#10 + #5#9;
          #1#52 = #1#52 + #5#9;
      |FINSI;
      #1#11 = #1#9 - #1#10;
      #1#53 = #1#51 - #1#52;
  |SINO;
      |SI #5#0 = 99;
          |SI #5#8 = "D";
              #1#48 = #1#48 + #5#9;
              #1#51 = #1#51 + #5#9;
          |SINO;
              #1#49 = #1#49 + #5#9;
              #1#52 = #1#52 + #5#9;
          |FINSI;
          #1#50 = #1#48 - #1#49;
          #1#53 = #1#51 - #1#52;
      |SINO;
          |HAZ actualiz2;
      |FINSI;
  |FINSI;
  |SI #1#58 < #7#4;
      #1#58 = #7#4;
  |FINSI;
  |GRABA 020#1a;
  |LIBERA #1;      || /por lo tanto, en la primera no desbloquea
  |HAZ actualiz3;  || diario
|FPRC;

|PROCESO altatra;
  modcon = 1;
  |ABRE #8;
  #8#0 = codcon;
  #8#1 = #5#4;
  #8#2 = #5#5;
  #8#3 = #5#1;
  #8#4 = #5#8;
  #8#6 = #5#0;
  |LEE 101#8=;
  |SI FSalida ! 0;
      |DDEFECTO #8;
      #8#0 = codcon;
      #8#1 = #5#4;
      #8#2 = #5#5;
      #8#3 = #5#1;
      #8#4 = #5#8;
      #8#6 = #5#0;
      |GRABA 101#8b;
  |FINSI;
  importe = #5#9;  || aqui si que se usa el importe real
  |SI opera = "B";
      importe = -importe;
  |FINSI;
  #8#5 = #8#5 + importe;
  |GRABA 101#8a;
  |LIBERA #8;
  |CIERRA #8;
|FPRC;

|PROCESO actualiz2;
  ini = dig1;      || numero de niveles
  fecalf = #0#3;
  mes = fecalf % 402;
  mesfec = mes;
  |SI ini = 1;
      mesac = mesfec;
  |SINO;
      mesac = (mesfec - ini) + 1;
      |SI mesac [ 0;
          mesac = (13 - ini) + mesfec;
      |FINSI;
  |FINSI;
  a = (mesac * 3) + 9;
  b = a + 1;
  c = a + 2;
  |SI #5#8 = "D";
      #1a = #1a + #5#9;
      #1#51 = #1#51 + #5#9;
  |SINO;
      #1b = #1b + #5#9;
      #1#52 = #1#52 + #5#9;
  |FINSI;
  #1c = #1a - #1b;
  #1#53 = #1#51 - #1#52;
|FPRC;

|PROCESO actualiz3;
  |ABRE #6;
  #6#0 = #7#3;
  |LEE 121#6=;
  |SI FSalida = 0;
      |SI #5#8 = "D";  #6#2 = #6#2 + #5#9;  |FINSI;
      |SI #5#8 = "H";  #6#3 = #6#3 + #5#9;  |FINSI;
      |GRABA 020#6a;
  |FINSI;
  |LIBERA #6;
  |CIERRA #6;
|FPRC;

|PROCESO final;
  |SI modcon ! 0;
      |SUB_EJECUTA "camaapua.run";
  |SINO;
      |ABRE #8;
      |DDEFECTO #8;
      #8#0 = codcon;
      |LEE 001#8=;
      |SI FSalida = 0;
          |BORRA 020#8a;
      |FINSI;
      |CIERRA #8;
  |FINSI;
|FPRC;
|| -------------------------------------------------------------------------
