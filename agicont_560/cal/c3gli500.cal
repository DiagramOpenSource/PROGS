|FICHEROS;
  c3gli500 #0;
  camaasil #1;
  cacuenta #2;
  caclipro #3;
  camanban #4;
|FIN;


|VARIABLES;
  &entrada = 1;
  men1 = "     NO EXISTE IMPRESORA";
  men2 = "     NO EXISTE INFORME";
  informe = "calis500";

  dfecha = @01.01.1900;
  hfecha = @01.01.2000;

  alfa1 = "";
  alfa2 = "";
  num = 0;
  swencontra = 0;

  &cabecera = "";
  &codcue = "";
  &nomcue = "";
  &cifcue = "";
  &dircue = "";
  &pobcue = "";
  &cp = "";
  &importe  = 0;

  cta = "";
  niv = 0;
  swcta = 0;
  sw = 0;
  swalgun = 0;

  i_cm1 = 42;
|FIN;

|INCLUYE acceso_i;
|INCLUYE puntos_i;
|INCLUYE multie_i;

|| *************** Datos PANTALLA
|PROCESO tipo8;  |TIPO 8;
  |HAZ inicio;
  any = #30#26;
  |HAZ coloca;
|FPRC;

|PROCESO mayor;
  #0Campo = #0Campo > " ";
  |PINTA #0Campo;
|FPRC;

|PROCESO cuenta; |TIPO 0;
    p_alfa1 = #0Campo;
    salidas = FSalida;
|HAZ punto;
|SI p_error ! 0;  |ERROR;  |ACABA;  |FINSI;
|FPRC;

|PROCESO nomodif;|TIPO 7;
  |SI #0#3 = "N";
      |C_M #0Campo,1,"N";
  |SINO;
      |C_M #0Campo,1,"S";
  |FINSI;
|FPRC;

|PROCESO simodifi;|TIPO 7;
  |SI #0#3 = "S";
      |C_M #0Campo,1,"N";
  |SINO;
      |C_M #0Campo,1,"S";
  |FINSI;
|FPRC;

|| ***************************************************************************
|PROCESO sel_multi;
  |SI #30#26 ! any;  |ACABA; |FINSI; || SI es necesario Ejercicio
  |SI #30#21 = "I";  |ACABA; |FINSI;
  leidos = 1;
|FPRC;

|| ***********************************************************************
|| ---------- Seleccion DESDE / HASTA Empresa.
|PROCESO esdesd1;
  alfa1 = #30#2;
  alfa2 = #30#3;
  alfa1 = "00000" + alfa1;   alfa1 = alfa1 % -105;
  alfa2 = "0"   + alfa2;   alfa2 = alfa2 % -101;
  dirfich0 = dir_fich + alfa1 + alfa2 + "/";

  |SI #30#26 ! any;  |ACABA; |FINSI;
  |SI #30#21 = "I";  |ACABA; |FINSI;

  |HAZ haztodo;
|FPRC;

|DEFBUCLE esdesde;
 #30#1;
 ;
 d_empres, d_period;
 h_empres, h_period;
 e;
 NULL,esdesd1;
|FIN;

|| ---------- Seleccion en parrilla.
|PROCESO esunaauna;
  |PARA xx = i_cm6; |SI xx [ i_cm7; |HACIENDO xx = xx + 1;
        |SI #0xx = 0;  |SAL;  |FINSI;
        yy = xx + 20;
        alfa1 = #0xx;
        alfa2 = #0yy;

        || ... lee empresa
           |ABRE #30;
           |DDEFECTO #30;
           #30#2 = #0xx;
           #30#3 = #0yy;
           |LEE 011#30=;
           |CIERRA #30;
           |SI #30#26 ! any;  |VETE otramas; |FINSI;
           |SI #30#21 = "I";  |VETE otramas; |FINSI;
        || ...................

        alfa1 = "00000" + alfa1;   alfa1 = alfa1 % -105;
        alfa2 = "0"     + alfa2;   alfa2 = alfa2 % -101;
        dirfich0 = dir_fich + alfa1 + alfa2 + "/";
        |HAZ haztodo;

 |ET otramas;
  |SIGUE;
|FPRC;

|PROCESO periodo;|TIPO 3;
  |IMPRE 0;
  |SI FSalida ! 0;  |ERROR;  |ACABA;  |FINSI;
  |INFOR informe;
  |SI FSalida ! 0;  |FINIMP;  |ERROR;  |ACABA;  |FINSI;

  |SI #0i_cm1 = "S";
      |BUCLE esdesde;
  |SINO;
      |HAZ esunaauna;
  |FINSI;
  |FININF;
  |FINIMP;
|FPRC;
|| ************************************************************************
|PROCESO impresion;
  |SI #0#3 = "S";
      sw = 1;
      |VETE fuera;
  |FINSI;
  sw = 0;
  |PARA num = 6;|SI num [ 40;|HACIENDO num = num + 1;
        |SI #1#6 = #0num;
            sw = 1;
            |SAL;
        |FINSI;
        |SI num = 23; num = 26; |FINSI;
  |SIGUE;

|ET fuera;
  |SI sw ! 1; |ACABA; |FINSI;
  |SI swcta = 0;
      cta = #1#4;
      niv = #1#5;
      swcta = 1;
      importe = 0;
  |FINSI;
  |SI cta ! #1#4;
      #2#0 = cta;
      #2#1 = niv;
      |LEE 040#2=;
      |SI FSalida ! 0;
          |DDEFECTO #2;
      |FINSI;
      codcue = #2#0;
      nomcue = #2#2;
      cifcue = "";
      dircue = "";
      pobcue = "";
      cp = "";
      |SI importe ] #0#25;
          |HAZ leeclipro;
          |PRINT;
          swalgun = 1;
      |FINSI;
      cta = #1#4;
      niv = #1#5;
      importe = 0;
  |FINSI;
  |SI #0#24 = "D";
      |SI #1#8 = "D";
          importe = importe + #1#9;
      |SINO;
          importe = importe - #1#9;
      |FINSI;
  |SINO;
      |SI #1#8 = "H";
          importe = importe + #1#9;
      |SINO;
          importe = importe - #1#9;
      |FINSI;
  |FINSI;
|FPRC;

|DEFBUCLE calis5000;
 #1#3;
 6;
 #0#1,0,dfecha,000001,#0#4;
 #0#2,9,hfecha,999999,#0#5;
 be;
 NULL,impresion;
|FIN;

||-----------------------------------------------------
|| ******************************** CALCULO
|PROCESO haztodo;
  enEmpresa = #canempre#2;
  enPeriodo = #canempre#3;
  swOperacion = 0;
  |SUB_EJECUTA "camoneda.tab";

  |PATH_DAT #1 dirfich0;
  |PATH_DAT #2 dirfich0;
  |PATH_DAT #3 dirfich0;
  |PATH_DAT #4 dirfich0;

  |ABRE #2;
  |ABRE #3;
  |ABRE #4;

  |SELECT #2#4;
  cabecera = #0#26;
  swcta   = 0;
  cta     = "";
  niv     = 0;
  importe = 0;
  swalgun = 0;

  |BUCLE calis5000;

  |SI importe ] #0#25;
      #2#0 = cta;
      #2#1 = niv;
      |LEE 040#2=;
      |SI FSalida ! 0;
          |DDEFECTO #2;
      |FINSI;
      codcue = #2#0;
      nomcue = #2#2;
      cifcue = " ";
      dircue = "";
      pobcue = "";
      cp = "";
      |HAZ leeclipro;
      |PRINT;
      swalgun = 1;
  |FINSI;
  |SI swalgun = 1; |PIEINF; |FINSI;
  |CIERRA #2;
  |CIERRA #3;
  |CIERRA #4;
|FPRC;

|| ------------ Lecturas
|PROCESO leeclipro;
  |HAZ proveedor; |SI swencontra = 1; |ACABA; |FINSI;
  |HAZ cliente;   |SI swencontra = 1; |ACABA; |FINSI;
  |HAZ banco;     |SI swencontra = 1; |ACABA; |FINSI;
|FPRC;

|PROCESO cliente;
  swencontra = 0;
  #3#23 = "C";
  #3#10 = cta;
  #3#11 = niv;
  |LEE 040#3=;
  |SI FSalida = 0;
      nomcue = #3#1;
      cifcue = #3#9;
      dircue = #3#2;
      pobcue = #3#6;
      cp = #3#25; |SI cp = "00000"; cp = ""; |FINSI;
      swencontra = 1;
  |FINSI;
|FPRC;

|PROCESO proveedor;
  swencontra = 0;
  #3#23 = "P";
  #3#10 = cta;
  #3#11 = niv;
  |LEE 040#3=;
  |SI FSalida = 0;
      nomcue = #3#1;
      cifcue = #3#9;
      dircue = #3#2;
      pobcue = #3#6;
      cp = #3#25; |SI cp = "00000"; cp = ""; |FINSI;
      swencontra = 1;
  |FINSI;
|FPRC;

|PROCESO banco;
  swencontra = 0;
  #4#10 = cta;
  #4#11 = niv;
  |LEE 040#4=;
  |SI FSalida = 0;
      nomcue = #4#1;
      cifcue = #4#14;
      dircue = #4#2;
      pobcue = #4#6;
      alfa1 = #4#3; alfa1 = ("000" + alfa1 ) % -102;
      alfa2 = #4#4; alfa2 = ("000" + alfa2 ) % -103;
      cp = alfa1 + alfa2; |SI cp = "00000"; cp = ""; |FINSI;
      swencontra = 1;
  |FINSI;
|FPRC;
|| *********************************************************************
