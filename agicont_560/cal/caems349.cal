|FICHEROS;
     caems349 #0;
     camac349 #1;
     camal349 #2;
     caportad #7;
     catraso2 #104;
|FIN;

|VARIABLES;
     Vacio20    = "                    ";
     Vacio40    = "                                        ";
     Vacio45    = "                                             ";
     Salir      = 0;
     Opcion     = 0;
     Comodin    = "";

     VAlfa      = "";
     VAlfa1     = "";
     VAlfa2     = "";
     VAlfa3     = "";
     VAlfa4     = "";
     VarNum     = 0;

     Unidad     = "";
     Raiz       = "";
     Sistema    = "";
     Letras     = "";
     Compl      = "";
     Refer      = "";

     Campos     = "";
     Campos1    = "";
     Campos2    = "";
     Campos3    = "";
     Campos4    = "";
     Campos5    = "";
     Campos6    = "";
     Campos7    = "";
     Campos8    = "";
     Campos9    = "";
     Campos10   = "";
     Campos11   = "";
     Campos12   = "";
     Campos13   = "";
     Campos14   = "";
     Campos15   = "";
     Campos16   = "";
     Campos17   = "";
     Campos18   = "";
     Campos19   = "";
     Campos20   = "";

     DClave     = "";
     HClave     = "";
     Clave      = "";
     Contador   = 0;
     ModoModi   = "";
     SwLee      = 0;
     Handle     = "";
     NFichero   = "";
     Fichero    = "";
     Fichero1   = "";
     FicheroD   = "";
     Directorio = "";
     Empresa    = "";

     CampoDni   = "";
     LetrasNif  = "TRWAGMYFPDXBNJZSQVHLCKE";
     Letra1     = "";
     Letra2     = "";
     Letra3     = "";
     Letra4     = "";
     Letra5     = "";
     Letra6     = "";
     Letra7     = "";
     Letra8     = "";
     Caracter   = "";
     Carac1     = 0;
     Carac2     = 0;
     Carac3     = 0;
     Carac4     = 0;
     Carac5     = 0;
     XPara      = 0;

         alfa1    = "";
         alfa2    = "";
         tip1     = "";
         tip2     = "";
         empcon   = 0;
         ejecon   = 0;
         leidos   = 0;
         dirfich0 = "";
         xx       = 0;
         yy       = 0;
         x        = 0;
         y        = 0;
         &r_emp   = 0;
         &r_per   = 0;

         &cargoem = "";
         &any     = "";
         &dia     = "";
         &mes     = "";
         &anyo    = "";
         &preimp  = "";
         &predis  = "";
         &predi1  = "";
         &predi2  = "";
         &represen = "";
         &fecbo   = "";
         &ttes    = 0;
         &tdos    = 0;

     &THojas    = 0;
     &NHojas    = 0;
     &nombrep   = "";
     &tipo_p    = "";
     &enEjercicio = 0;

     NACopiar   = 0;
     sw_fisica  = 0;
     vacio_20   = "                    ";

{-1} MGeneral   = "";
     MGeneral1  = "1860";
     MGeneral2  = "1";
     MGeneral3  = "4";
     MGeneral4  = "Creacion  Diskette ";
     MGeneral5  = "Emision   Portada  ";
     MGeneral6  = "Etiqueta  Diskette ";
     MGeneral7  = "Grabacion Diskette ";

{10}NEmpresas = "";
|FIN;

|INCLUYE acceso_i;

|| ***********************************************************************
||                       PROCESO CARGA DE DATOS
|| ***********************************************************************
|PROCESO Relleno;
  |SI Campo = 6;
      VAlfa = #0Campo; |QBT VAlfa;
      VAlfa = ("00000" + VAlfa) % -105;
  |FINSI;

  |SI Campo = 10;
      VAlfa = #0Campo; |QBT VAlfa;
      VAlfa = ("   " + VAlfa) % -103;
  |FINSI;

  |SI Campo = 11;
      VAlfa = #0Campo; |QBT VAlfa;
      VAlfa = ("       " + VAlfa) % -107;
  |FINSI;

  #0Campo = VAlfa;  |PINTA #0Campo;
|FPRC;

|PROGRAMA;
  |HAZ inicio;
  |CLS;
  |CABEZA "Emision 349 Magn.";
  |DDEFECTO #0;
  |PINPA #0#0;
  |ABRE #0;
  |LEE 101#0p;
  |SI FSalida ! 0;
      |GRABA 020#0b;
  |FINSI;
  |PINDA #0#0;
  |ENDATOS #1#0;
  |SI FSalida = 0;
      |GRABA 020#0a;
  |FINSI;
  |LIBERA #0;
  |CIERRA #0;
|FPRO;

|| -----------------------------------------------------------------------

|PROCESO Mayus; |TIPO 0;
  #0Campo = #0Campo > #0Campo;
  |PINTA #0Campo;
|FPRC;

|PROCESO PintaAnyo; |TIPO 7;
  #0#2 = cod2;
  |PINTA #0#2;
|FPRC;

|PROCESO Individual; |TIPO 0;

  ModoModi = "N";
  |C_M #0#03, 1, ModoModi;
  |C_M #0#04, 1, ModoModi;
  |C_M #0#05, 1, ModoModi;
  |C_M #0#06, 1, ModoModi;
  |C_M #0#07, 1, ModoModi;
  |C_M #0#08, 1, ModoModi;
  |C_M #0#09, 1, ModoModi;
  |C_M #0#10, 1, ModoModi;
  |C_M #0#11, 1, ModoModi;
  |C_M #0#12, 1, ModoModi;
  |C_M #0#16, 1, ModoModi;
  |C_M #0#17, 1, ModoModi;
  |C_M #0#18, 1, ModoModi;
  |C_M #0#26, 1, ModoModi;
  |C_M #0#27, 1, ModoModi;
|FPRC;

|PROCESO PonIgual;
  |SI #0#22 = "L";
      #0#9  = "";    |PINTA #0#9;              || NIF
      #0#3  = "";    |PINTA #0#3;              || Apellidos y Nombre
      #0#4  = "";    |PINTA #0#4;              || SG
      #0#5  = "";    |PINTA #0#5;              || Calle
      #0#6  = "";    |PINTA #0#6;              || Numero
      #0#11 = "";    |PINTA #0#11;             || Telefono
      #0#7  = "";    |PINTA #0#7;              || Municipio
      #0#8  = "";    |PINTA #0#8;              || Provincia
      #0#17 = 0;     |PINTA #0#17;             || Codigo Postal 1
      #0#18 = 0;     |PINTA #0#18;             || Codigo Postal 2
      #0#12 = 0;     |PINTA #0#12;             || Delegacion de Hacienda
      #0#16 = 0;     |PINTA #0#16;             || Administracion de Hacienda
      #0#26 = "";    |PINTA #0#26;             || Delegacion de Hacienda
      #0#27 = "";    |PINTA #0#27;             || Administracion de Hacienda
  |FINSI;
|FPRC;

|PROCESO desperio;
  |SI FSalida = 2; |ACABA; |FINSI;
  |SI #0#22 ! "I";  |ACABA;  |FINSI;

  || .......... #0#1 = #0#0; |PINTA #0#1;
  |ABRE #1;
  #1#0 = #30#2;
  #1#2 = #0#2;
  #1#1 = #0#28;
  |LEE 040#1=;
  |SI FSalida ! 0;
      |CIERRA #1;
      |MENAV "     No Existe Movimientos en el Modelo 349 con esta Empresa";
      |ERROR;
      |ACABA;
  |FINSI;
  |CIERRA #1;

  |ABRE #7;
  #7#0 = #30#2;
  #7#1 = #0#2;
  |LEE 040#7=;
  |SI FSalida ! 0;
      |CIERRA #7;
      |MENAV "    No existen datos el PORTADAS MOD. OFICIALES ";
      |ERROR;
      |ACABA;
  |FINSI;
  |CIERRA #7;

  |HAZ representante;
  #0#25 = represen; |PINTA #0#25;             || Fdo.
  CampoDni = #7#13;  |HAZ LetraDni;
  #0#9  = CampoDni; |PINTA #0#9;              || NIF
  #0#3  = #7#2;     |PINTA #0#3;              || Apellidos y Nombre
  #0#4  = #7#3;     |PINTA #0#4;              || SG
  #0#5  = #7#4;     |PINTA #0#5;              || Calle
  #0#6  = #7#5;     |PINTA #0#6;              || Numero
  #0#11 = #7#46;    |PINTA #0#11;             || Telefono
  #0#7  = #7#11;    |PINTA #0#7;              || Municipio
  #0#8  = #7#12;    |PINTA #0#8;              || Provincia
  #0#17 = #7#09;    |PINTA #0#17;             || Codigo Postal 1
  #0#18 = #7#10;    |PINTA #0#18;             || Codigo Postal 2
  #0#12 = #7#16;    |PINTA #0#12;             || Delegacion de Hacienda
  #0#16 = #7#17;    |PINTA #0#16;             || Administracion de Hacienda
  #0#26 = #7#18;    |PINTA #0#26;             || Delegacion
  #0#27 = #7#19;    |PINTA #0#27;             || Administracion
  #0#29 = #7#89;                              || Persona Contacto
|FPRC;

|PROCESO representante;
 represen = "";
 |SI #7#39 ! Vacio45;
     represen = #7#39;
 |SINO;
     |SI #7#51 ! Vacio20; |O #7#52 ! Vacio20;
         represen = #7#51;
         |QBF represen;
         represen = represen + ", " + #7#52;
     |SINO;
         |SI #7#56 ! Vacio20; |O #7#57 ! Vacio20;
              represen = #7#56;
              |QBF represen;
              represen = represen + ", " + #7#57;
         |SINO;
              |SI #7#61 ! Vacio20; |O #7#62 ! Vacio20;
                  represen = #7#61;
                  |QBF represen;
                  represen = represen + ", " + #7#62;
              |FINSI;
         |FINSI;
     |FINSI;
 |FINSI;
|FPRC;

|PROCESO LetraDni;
|QBF CampoDni;
Letra2 = CampoDni;
|QBT Letra2;

    Caracter = Letra2 % 101;
|SI Caracter!"A";|Y Caracter!"B";|Y Caracter!"C";|Y Caracter!"D";
     |Y Caracter!"E";|Y Caracter!"F";
     |Y Caracter!"G";|Y Caracter!"P";|Y Caracter!"Q";|Y Caracter!"S";|Y Caracter!"X";
        Letra4 = "";
        Letra3 = Letra2 % 0;
        Carac2 = Letra3;
    |PARA Carac5 = 1;  |SI Carac5 [ Carac2;  |HACIENDO Carac5 = Carac5 + 1;
            Carac3 = (Carac5 * 100) + 1;
            Letra3 = Letra2 % Carac3;
        |SI Letra3="0";|O Letra3="1";|O Letra3="2";|O Letra3="3";
                      |O Letra3="4";|O Letra3="5";|O Letra3="6";
                      |O Letra3="7";|O Letra3="8";|O Letra3="9";
                Letra4 = Letra4 + Letra3;
        |FINSI;
    |SIGUE;
        Carac4 = Letra4;
    |SI Carac4 ! 0; |Y Letra4 ! "";
            Carac1 = Carac4 $ 23;
            Carac1 = Carac1 + 1;
            Carac3 = (Carac1 * 100) + 1;
            Letra1 = LetrasNif % Carac3;

            Letra4 = "000000000000" + Letra4;
            Letra7 = Letra4 % -103;
            Letra6 = Letra4 % -403;
            Letra5 = Letra4 % -703;

            Letra8 = Letra5  % 101;
            |SI Letra8 = "0"; Letra5 = Letra5 % 202; |FINSI;

            Letra8 = Letra5  % 101;
            |SI Letra8 = "0"; Letra5 = Letra5 % 201; |FINSI;

            Letra8 = Letra5  % 101;
            |SI Letra8 = "0"; Letra5 = ""; |FINSI;

            |SI Letra5 ! "";
                Letra4 = Letra5 + Letra6 + Letra7 + Letra1;
            |SINO;
                Letra4 = Letra6 + Letra7 + Letra1;
            |FINSI;

            CampoDni = Letra4;
            CampoDni = ("000000000" + CampoDni) % -109;
    |FINSI;
|FINSI;
 CampoDni = (CampoDni + "         ");
 CampoDni = CampoDni % 109;
|FPRC;

|| ***********************************************************************
||                       IMPRESION DE LA PORTADA
|| ***********************************************************************

|PROCESO FechaP;
  VAlfa2 = #0#15 % 102;
  VAlfa3 = #0#15 % 402;
  VAlfa4 = #0#15 % 704;

  |SI VAlfa3 = "01";  VAlfa3 = "Enero     ";  |FINSI;
  |SI VAlfa3 = "02";  VAlfa3 = "Febrero   ";  |FINSI;
  |SI VAlfa3 = "03";  VAlfa3 = "Marzo     ";  |FINSI;
  |SI VAlfa3 = "04";  VAlfa3 = "Abril     ";  |FINSI;
  |SI VAlfa3 = "05";  VAlfa3 = "Mayo      ";  |FINSI;
  |SI VAlfa3 = "06";  VAlfa3 = "Junio     ";  |FINSI;
  |SI VAlfa3 = "07";  VAlfa3 = "Julio     ";  |FINSI;
  |SI VAlfa3 = "08";  VAlfa3 = "Agosto    ";  |FINSI;
  |SI VAlfa3 = "09";  VAlfa3 = "Septiembre";  |FINSI;
  |SI VAlfa3 = "10";  VAlfa3 = "Octubre   ";  |FINSI;
  |SI VAlfa3 = "11";  VAlfa3 = "Noviembre ";  |FINSI;
  |SI VAlfa3 = "12";  VAlfa3 = "Diciembre ";  |FINSI;
  mes =VAlfa3;
  fecbo = VAlfa2 + " de " + VAlfa3 + " de " + VAlfa4;
|FPRC;

|PROCESO LeePortadaP;
  |SI SwLee = 0;
      ttes = ttes + 1;
      tdos = tdos + (#1#6 + #1#8 + #1#10 + #1#11);
      |ACABA;
  |FINSI;

  any    = #0#2;                              || A¤o Emision
  preimp = "";                                || Impreso
  predi1 = "";                                || Soporte Colectivo Presentador
  |SI #0#22 = "I";  predis = "X";  |FINSI;    || Soporte Individual
  |SI #0#22 = "C";  predi2 = "X";  |FINSI;    || Soporte Colectivo Declarante

  cargoem  = #0#24;                           || Cargo o Empleo #103#27
  |SI #0#22 = "L";
      |HAZ representante;
      #0#25 = represen;
  |FINSI;
  represen = #0#25;                           || Representante;

  |PRINT;
  |PIEINF;

  #1#2 = #0#15;
  #1#3 = "S";
  #1#13 = #0#22; |SI #1#13 = "L"; #1#13 = "I"; |FINSI;

  |GRABA 140#1a;
  |LIBERA #1;
|FPRC;

|PROCESO Portada;
  |HAZ FechaP;

  |IMPRE 0;
  |SI FSalida ! 0;  |ACABA;  |FINSI;

  |INFOR "capor349";
  |SI FSalida ! 0;  |FINIMP;  |ACABA;  |FINSI;

  tipo_p = "T";
  |SI #0#28 = 0; tipo_p = "A"; |FINSI;

  ttes    = 0;  || #103#35    = 0;
  tdos    = 0;  || #103#36    = 0;

  |SI #0#22 = "C";
      SwLee = 0;  |HAZ eosm0009C;

      |DDEFECTO #7;
      |DDEFECTO #1;
      any      = #0#2;                   || A¤o Emision  #103#25
      preimp   = "";                     || Impreso      #103#31
      predis   = "";                     || Soporte Individual  #103#32
      predi1   = "X";                    || Soporte Colectivo Presentador  #103#33
      predi2   = "";                     || Soporte Colectivo Declarante   #103#34
      represen = "";                     || Nombre Representante           #103#38

      #7#0  = 0;                        || Codigo Cliente
      CampoDni = #0#9;  |HAZ LetraDni;
      #7#13 = CampoDni;                 || NIF
      #7#2  = #0#3;                     || Apellidos y Nombre
      #7#3  = #0#4;                     || SG
      #7#4  = #0#5;                     || Calle
      #7#5  = #0#6;                     || Numero
      #7#14 = #0#10 + "-" + #0#11;      || Telefono
      #7#11 = #0#7;                     || Municipio
      #7#12 = #0#8;                     || Provincia
      #7#09 = #0#17;                    || Codigo Postal 1
      #7#10 = #0#18;                    || Codigo Postal 2
      #7#16 = #0#12;                    || Delegacion de Hacienda
      #7#17 = #0#16;                    || Administracion de Hacienda
      #7#18 = #0#26;                    || Delegacion Hacienda
      #7#19 = #0#27;                    || Adminsitracion Hacienda
      |PRINT;
      |PIEINF;
  |FINSI;

  ttes    = 0;
  tdos    = 0;

  SwLee = 1; |HAZ eosm0009C;
  |FININF;
  |FINIMP;
|FPRC;

|| ***********************************************************************
||                       GRABACION SECUENCIAL DISTINTOS TIPOS
|| ***********************************************************************

|PROCESO Tipo2;
  Campos1  = "2349";                                   || Tipo Registro
  VAlfa = #0#2;
  |SI #0#2 ] 80;
     VAlfa = "19" + VAlfa;
  |SINO;
     |SI #0#2 < 10;
        VAlfa = "200" + VAlfa;
     |SINO;
        VAlfa = "20" + VAlfa;
     |FINSI;
  |FINSI;
  Campos2  = VAlfa;                                    || Ejercicio

  |SI #0#28 = 0;  VAlfa = "0A";  |FINSI;
  |SI #0#28 = 1;  VAlfa = "1T";  |FINSI;
  |SI #0#28 = 2;  VAlfa = "2T";  |FINSI;
  |SI #0#28 = 3;  VAlfa = "3T";  |FINSI;
  |SI #0#28 = 4;  VAlfa = "4T";  |FINSI;
  Campos2  = Campos2 + VAlfa;                          || Periodo

  VAlfa    = ("00" + #0#12) % -102;
  Campos2  = Campos2 + VAlfa;                          || Delegacion

  CampoDni = #7#13;  |HAZ LetraDni;
  Campos2  = Campos2 + CampoDni;                       || N.I.F.

  Campos3  = "1";
  |SI #camal349#4 = "A";  Campos3 = "2";  |FINSI;
  |SI #camal349#4 = "R";  Campos3 = "3";  |FINSI;
  |SI #camal349#4 = "X";  Campos3 = "4";  |FINSI;      || Tipo

  Campos4  = (#2#5 % 102) + #2#6;
  Campos5  = #2#7;                                     || Apellidos y Nombre
  VAlfa    = ("0000000000000" + #2#8) % -113;          || Cantidad
  Campos6  = VAlfa;
  Campos7  = #2#13;                                    || Oper.Triangular
  Campos8  = #2#10;                                    || Signo

  VAlfa = "    ";
  |SI #2#4 = "R";  |O #2#4 = "X";
      |SI  #2#11 > 80;
           VAlfa = #2#11;   VAlfa = "19" + (("00" + VAlfa) % -102);
      |SINO;
           VAlfa = #2#11;   VAlfa = "20" + (("00" + VAlfa) % -102);
      |FINSI;
  |FINSI;

  Campos8  = Campos8 + VAlfa;

  VAlfa    = "  ";
  |SI #2#4 = "R"; |O #2#4 = "X";
      VAlfa     = ("0" + #2#12) % -101;                 || Periodo
      |SI VAlfa = "0";
          VAlfa = "0A";
      |SINO;
          VAlfa = VAlfa + "T";
      |FINSI;
  |FINSI;

  Campos8  = Campos8 + VAlfa;

  Campos9 = " " * 93;

  Campos   = Handle   + " " + Campos1 + Campos2 + Campos3 + Campos4;
  Campos   = Campos   + Campos5  + Campos6  + Campos7  + Campos8  + Campos9;
  Campos   = Campos   + &#0#19 + &#0#20;        || Caracter Final de Linea
  |FGRABA Campos;
  #104#14 = #104#14 + 1;
|FPRC;

|DEFBUCLE grlin;
 #2#2;
 ;
 #30#2, #0#2, #0#28, tip1, "            ";
 #30#2, #0#2, #0#28, tip2, "zzzzzzzzzzzz";
 be;
 NULL, Tipo2;
|FIN;

|PROCESO eosm0010;
  tip1 = "E";
  tip2 = "E";
  |BUCLE grlin; || las lineas - registro de operaciones
  tip1 = "A";
  tip2 = "A";
  |BUCLE grlin; || las lineas - registro de operaciones
  tip1 = "R";
  tip2 = "X";
  |BUCLE grlin; || las lineas - registro de operaciones
|FPRC;

|PROCESO Tipo1;
  Campos1  = "1349";                                   || Tipo Registro
  VAlfa = #0#2;
  |SI #0#2 ] 80;
     VAlfa = "19" + VAlfa;
  |SINO;
     |SI #0#2 < 10;
        VAlfa = "200" + VAlfa;
     |SINO;
        VAlfa = "20" + VAlfa;
     |FINSI;
  |FINSI;
  Campos2  = VAlfa;                                    || Ejercicio
  |SI #1#1 = 0;  VAlfa = "0A";  |FINSI;
  |SI #1#1 = 1;  VAlfa = "1T";  |FINSI;
  |SI #1#1 = 2;  VAlfa = "2T";  |FINSI;
  |SI #1#1 = 3;  VAlfa = "3T";  |FINSI;
  |SI #1#1 = 4;  VAlfa = "4T";  |FINSI;
  Campos3  = VAlfa;                                    || Periodo
  VAlfa    = ("00" + #0#12) % -102;
  Campos4  = VAlfa;                                    || Delegacion
  CampoDni = #7#13;  |HAZ LetraDni;
  Campos5  = CampoDni;                                 || N.I.F.
  Campos6  = #7#2;                                     || Apellidos y Nombre
  VAlfa    = ("000000000" + #1#6) % -109;              || Personas
  Campos7  = VAlfa;
  VAlfa    = ("0000000000000" + #1#7) % -113;          || Cantidad
  Campos8  = VAlfa;
  VAlfa    = ("000000000" + #1#8) % -109;                || Personas
  Campos9  = VAlfa;
  VAlfa    = ("0000000000000" + #1#9) % -113;          || Cantidad
  Campos10 = VAlfa;
  VarNum   = #1#10 + #1#11;
  VAlfa    = ("000000000" + VarNum) % -109;              || Personas
  Campos11 = VAlfa;
  Campos12 = "D";
  Campos13 = #0#11 % 0109;
  Campos14 = #0#25;
  Campos15 = "349" + #0#33 + "             ";

  Campos   = Handle   + " " + Campos1 + Campos2 + Campos3 + Campos4;
  Campos   = Campos   + Campos5  + Campos6  + Campos7  + Campos8  + Campos9;
  Campos   = Campos   + Campos10 + Campos11 + Campos12 + Campos13 + Campos14;
  Campos   = Campos   + Campos15;
  Campos   = Campos   + &#0#19 + &#0#20;        || Caracter Final de Linea
  |FGRABA Campos;
  #104#14 = #104#14 + 1;
|FPRC;

|PROCESO LeePortadaC;
  |SI SwLee = 0;
      ttes = ttes + 1;
      tdos = tdos + (#1#6 + #1#8 + #1#10 + #1#11);

      #104#0  = #104#0 + #1#6;
      #104#1  = #104#1 + #1#8;
      #104#2  = #104#2 + #1#10 + #1#11;
      #104#6  = #104#0 + #104#1 + #104#2;
      #104#7  = #104#7 + #1#7;
      #104#8  = #104#8 + #1#9;
      #104#13 = #104#7 + #104#8;
      #104#15 = #104#15 + 1;
      |ACABA;
  |FINSI;
  SwLee = 1;  |HAZ Tipo1;
  SwLee = 2;  |HAZ eosm0010;
|FPRC;

|| *********************************************************************
|PROCESO eosm0009C;
  empcon = #30#1;
  ejecon = #30#26;

     |ABRE #1;
     #1#0 = #30#2;
     #1#2 = #0#2;
     #1#1 = #0#28;
     |LEE 101#1=;
     |SI FSalida ! 0; |CIERRA #1; |ACABA; |FINSI;

     |ABRE #7;
     #7#0 = #30#2;
     #7#1 = #30#26;
     |LEE 001#7=;
     |SI FSalida ! 0; |CIERRA #1; |CIERRA #7; |ACABA; |FINSI;
     |CIERRA #1;
     |CIERRA #7;

  |SI Opcion = 1;
      |SI #0#22 ! "L"; |HAZ LeePortadaC; |FINSI;
      |SI #0#22 = "L"; |HAZ LeePortadaL; |FINSI;
  |FINSI;
  |SI Opcion = 2;
      |HAZ LeePortadaP;
  |FINSI;
|FPRC;
|| ************************************************************************

|PROCESO Tipo0;
  Campos1  = "0349";                                   || Tipo Registro
  VAlfa    = #0#2;      || VAlfa = ("00" + VAlfa) % -102;
  |SI #0#2 ] 80;
     VAlfa = "19" + VAlfa;
  |SINO;
     |SI #0#2 < 10;
        VAlfa = "200" + VAlfa;
     |SINO;
        VAlfa = "20" + VAlfa;
     |FINSI;
  |FINSI;
  Campos2  = VAlfa;                                    || Ejercicio
  Campos3  = "  ";                                     || Periodo
  VAlfa = ("00" + #0#12) % -102;
  Campos4  = VAlfa;                                    || Delegacion A.E.A.T.
  Campos5  = #0#9;                                     || N.I.F.
  Campos6  = #0#3;                                     || Apellidos y Nombre
  Campos7  = #0#4;                                     || SG
  Campos8  = #0#5 % 120;                               || Calle
  Campos9  = #0#6;                                     || Numero

  Campos10 = #0#30;                                    || Escalera
  Campos11 = #0#31;                                    || Piso
  Campos12 = #0#32;                                    || Puerta

  VAlfa    = ("00" + #0#17) % -102;
  Campos13 = VAlfa;                                    || Codigo Postal 1
  VAlfa    = ("000" + #0#18) % -103;
  Campos14 = VAlfa;                                    || Codigo Postal 2
  Campos15 = #0#7 % 112;                               || Municipio
  VAlfa    = ("00000" + ttes) % -105;               || Total Declarantes
  Campos16 = VAlfa;
  VAlfa    = ("0000000" + tdos) % -107;             || Total Declarados
  Campos17 = VAlfa;
  Campos18 = "D";
  Campos19 = #0#11 % 0109;
  Campos19 = #0#25;                                    || Persona contacto
  Campos20 = "349" + #caems349#33 + "  ";

  Campos   = Handle   + " " + Campos1 + Campos2 + Campos3 + Campos4;
  Campos   = Campos   + Campos5  + Campos6  + Campos7  + Campos8  + Campos9;
  Campos   = Campos   + Campos10 + Campos11 + Campos12 + Campos13 + Campos14;
  Campos   = Campos   + Campos15 + Campos16 + Campos17 + Campos18 + Campos19 + Campos20;
  Campos   = Campos   + &#0#19 + &#0#20;        || Caracter Final de Linea
  |FGRABA Campos;
  #104#14 = #104#14 + 1;
|FPRC;

|| ************************************************************************
||                  CREACION DISKETTE SOPORTE COLECTIVO E INDIVIDUAL
|| ************************************************************************
|PROCESO Creacion;
  |HAZ FechaP;
  |DDEFECTO #104;
  ttes    = 0;
  tdos    = 0;
  SwLee   = 0;

  |HAZ eosm0009C;
  Directorio = "";
  |DFICE Directorio;
  Fichero  = #0#2;
  |SI #0#2 ] 80;
     Fichero = "19" + Fichero;
  |SINO;
     |SI #0#2 < 10;
        Fichero = "200" + Fichero;
     |SINO;
        Fichero = "20" + Fichero;
     |FINSI;
  |FINSI;
  Fichero  = "ce" + Fichero;
  NFichero = Directorio + Fichero;          |QBF NFichero;
  Fichero  = "wb  "+ NFichero;

  Pos = -1;
  |FABRE Fichero;
  Handle = FSalida;
  |SI FSalida ] 0;
      |SI #0#22 = "C";  |HAZ Tipo0;  |FINSI;  || Registro del Presentador
      SwLee = 1;  |HAZ eosm0009C;
  |FINSI;

  Campos = Handle + " " + &#0#21;
  |FGRABA Campos;

  FSalida = Handle;
  |FCIERRA FSalida;

  Pos = -1;
  |PUSHV 0501 2380;
  |PINPA #0#104;
  |PINDA #0#104;
  |CONFI "2400SEs Correcto : ";
  |SI FSalida ! 0;
      |FBORRA NFichero;
      Salir = 1;
  |FINSI;
  |POPV;
|FPRC;

|| ************************************************************************
||                  CREACION DISKETTE SOPORTE LASER
|| ************************************************************************
|PROCESO LeePortadaL;

  #0#12 = #7#16;

  Directorio = "";
  |DBASE Directorio;
  Directorio = Directorio + "hacienda";
  |MKDIR Directorio;
  Directorio = Directorio + "/";

  Empresa  = #1#0;  Empresa  = ("00000" + Empresa) % -105;

  Fichero    = "349" + Empresa;
  INEmpresas = NEmpresas1 <-;
  INEmpresas = INEmpresas + NACopiar;
  NFichero   = Directorio + Fichero;          |QBF NFichero;
  NEmpresas  = NFichero;
  NACopiar   = NACopiar + 1;

  Fichero  = "wb  "+ NFichero;

  Pos = -1;
  |FABRE Fichero;
  Handle = FSalida;
  |SI FSalida ] 0;
     |HAZ Tipo1;
     SwLee = 2;  |HAZ eosm0010;
  |FINSI;

  Campos = Handle + " " + &#0#21;
  |FGRABA Campos;

  FSalida = Handle;
  |FCIERRA FSalida;
|FPRC;

|PROCESO CreacionLaser;
  NACopiar   = 0;
  INEmpresas = NEmpresas1 <-;
  |PARA XPara = 1; |SI XPara [ 10;  |HACIENDO XPara = XPara + 1;
        NEmpresas = "";
        INEmpresas = INEmpresas + 1;
  |SIGUE;

  |HAZ FechaP;
  |HAZ eosm0009C;
|FPRC;

|| ************************************************************************
||                  GRABACION DEL DISKETTE OP
|| ************************************************************************

|PROCESO GrabaDisketteC;
  Pos = -1;
  Directorio = "";
  |DFICE Directorio;
  Fichero  = #0#2;
  |SI #0#2 ] 80;
     Fichero = "19" + Fichero;
  |SINO;
     |SI #0#2 < 10;
        Fichero = "200" + Fichero;
     |SINO;
        Fichero = "20" + Fichero;
     |FINSI;
  |FINSI;
  Fichero  = "ce" + Fichero;
  NFichero = Fichero;          |QBF NFichero;
  Fichero  = "rt  "+ Directorio + NFichero;
  |FABRE Fichero;
  |SI FSalida < 0;
      VAlfa = "      No se ha creado el fichero " + NFichero + " Por favor haga la Creacion del Diskette";
      |MENAV  VAlfa;
      |ACABA;
  |FINSI;
  |FCIERRA FSalida;

  |PUSHV 0501 2380;
  |PINPA #1#104;

  E_sup  = 1;
  E_inf  = "ABCDEFGHIJKLMNOPQRSTUVWXYZ";
  Unidad = "A";
  Unidad = 0851 ? 0;
  |ATRI I;
  |PINTA 1616, "Introduzca un diskette formateado en D.O.S. ...";
  |ATRI i;
  |PINTA 2380, ".";
  |PAUSA;

  Raiz = "";
  |DIRECTORIO Raiz; |QBF Raiz;
  |DFICE Fichero;   |QBF Fichero;
  VAlfa  = #0#2;
  |SI #0#2 ] 80;
     VAlfa = "19" + VAlfa;
  |SINO;
     |SI #0#2 < 10;
        VAlfa = "200" + VAlfa;
     |SINO;
        VAlfa = "20" + VAlfa;
     |FINSI;
  |FINSI;
  Fichero   = Fichero + "ce" + VAlfa;

  |ATRI P;
  |PINTA 1616, "                  Copiando ...                 ";
  |ATRI p;
  |PINTA 2380, ".";

  |QUE_SISTEMA Sistema;
  |SI Sistema ! "ESDOS";
      VAlfa = Raiz + "bin/agcopiaa " + Fichero + " " + Unidad + ":";
      |SYSTEM VAlfa;

      |ATRI P;
      |PINTA 1616, "           FICHERO COPIADO !!!!                 ";
      |ATRI p;
      |PINTA 2380, ".";
      |PAUSA;
  |SINO;
      VAlfa1 = Fichero;
      VAlfa2 = Unidad + ":" + NFichero;
      |COPIA_FICHERO VAlfa1, VAlfa2;
      |SI FSalida ! 0;
          VAlfa3 = "Error [" + FSalida + "] en copia de ficheros ...";
          |PINTA 1616, VAlfa3;
          |PINTA 2380, ".";
          |PAUSA;
      |SINO;
          |ATRI P;
          |PINTA 1616, "           FICHERO COPIADO !!!!                 ";
          |ATRI p;
          |PINTA 2380, ".";
          |PAUSA;
      |FINSI;
  |FINSI;
  |POPV;
|FPRC;

|| ************************************************************************
||                  GRABACION DEL DISKETTE PARA LASER
|| ************************************************************************

|PROCESO GrabaDisketteL;
  INEmpresas = NEmpresas1 <-;
  |SI NEmpresas = "";
      VAlfa = "     Por favor haga la Creacion del Diskette";
      |MENAV  VAlfa;
      |ACABA;
  |FINSI;

  |PUSHV 0501 2380;
  |PINPA #1#104;

  |PINTA 0931, "Letras CG-TIMES ..:";
  |PINTA 1031, "D.Complementarias :";
  E_sup  = 1;
  E_inf  = "ABCDEFGHIJKLMNOPQRSTUVWXYZ";
  Unidad = "A";
  Unidad = 0851 ? 0;

  E_sup  = 1;
  E_inf  = "SNsn";
  Letras = "N";
  Letras = 0951 ? 0;

  E_sup  = 1;
  E_inf  = "SNsn";
  Compl  = "N";
  Compl  = 1051 ? 0;

  |SI Compl = "S";
      |PINTA 1131, "Referencia .......:";
      E_sup  = 13;
      E_inf  = "";
      Refer  = "";
      Refer  = 1151 ? 0;
  |FINSI;

  |ATRI I;
  |PINTA 1616, "Introduzca un diskette formateado en D.O.S. y con los pro- ";
  |PINTA 1716, "grama de Hacienda copiados.                                ";
  |PINTA 1816, "A continuacion el programa grabar  los ficheros generados  ";
  |PINTA 1916, "en el diskette. Introducir el diskette en una maquina en   ";
  |PINTA 2016, "D.O.S y ejecutar --> A:IMPRE349                            ";
  |ATRI i;
  |PINTA 2380, ".";
  |PAUSA;

  |PINTA 1616, "                                                          ";
  |PINTA 1716, "                                                          ";
  |PINTA 1816, "                                                          ";
  |PINTA 1916, "                                                          ";
  |PINTA 2016, "                                                          ";

  Directorio = "";
  |DBASE Directorio;
  Directorio = Directorio + "hacienda/";
  NFichero   = Directorio + "impre349.bat";
  Fichero1   = NFichero;
  NFichero   = "wb  "+ NFichero;

  Pos = -1;
  |FABRE NFichero;
  Handle = FSalida;
  |SI FSalida ] 0;
      VAlfa  = "A:";
      Campos = Handle + " " + VAlfa + &#0#19 + &#0#20;
      |FGRABA Campos;

      |ATRI P;
      |PINTA 1616, "                  Copiando ...                 ";
      |ATRI p;
      |QUE_SISTEMA Sistema;

      |PARA XPara = 1; |SI XPara [ 10; |HACIENDO XPara = XPara + 1;
            |SI NEmpresas = ""; |SAL; |FINSI;

             Fichero  = NEmpresas;
             FicheroD = Fichero % -108;

             |SI Sistema ! "ESDOS";
                 VAlfa = Raiz + "bin/agcopiaa " + Fichero + " " + Unidad + ":";
                 |PINTA 1816, "";
                 |ATRI I; |SYSTEM VAlfa; |ATRI i;
             |SINO;
                 VAlfa1 = Fichero;
                 VAlfa2 = Unidad + ":" + FicheroD;
                 |COPIA_FICHERO VAlfa1, VAlfa2;
                 |SI FSalida ! 0;
                     VAlfa3 = "Error [" + FSalida + "] en copia de ficheros ...";
                     |PINTA 1616, VAlfa3;
                     |PINTA 2380, ".";
                     |PAUSA;
                 |FINSI;
             |FINSI;

             VAlfa  = "349pdf " + FicheroD;
             |SI Letras = "S";  VAlfa = VAlfa + "   s";            |FINSI;
             |SI Compl  = "S";  VAlfa = VAlfa + "   c  " + Refer;  |FINSI;
             Campos = Handle + " " + VAlfa + &#0#19 + &#0#20;
             |FGRABA Campos;
             INEmpresas = INEmpresas + 1;
      |SIGUE;
  |FINSI;

  VAlfa  = "C:";
  Campos = Handle + " " + VAlfa + &#0#19 + &#0#20;
  |FGRABA Campos;

  Campos = Handle + " " + &#0#21;
  |FGRABA Campos;

  FSalida = Handle;
  |FCIERRA FSalida;

  |SI Sistema ! "ESDOS";
      VAlfa = Raiz + "bin/agcopiaa " + Fichero1 + " " + Unidad + ":";
      |PINTA 1816, "";
      |ATRI I; |SYSTEM VAlfa; |ATRI i;
  |SINO;
      VAlfa1 = Directorio + "impre349.bat";
      VAlfa2 = Unidad + ":impre349.bat";
      |COPIA_FICHERO VAlfa1, VAlfa2;
      |SI FSalida ! 0;
          VAlfa3 = "Error [" + FSalida + "] en copia de ficheros ...";
          |PINTA 1616, VAlfa3;
          |PINTA 2380, ".";
          |PAUSA;
      |FINSI;
  |FINSI;

  |ATRI P;
  |PINTA 1616, "           FICHEROS COPIADO !!!!                 ";
  |ATRI p;
  |PAUSA;
  |POPV;
|FPRC;
|| ***********************************************************************
||                       EMISION DE ETIQUETAS
|| ***********************************************************************

|PROCESO Etiquetas;
  |SI #104#14 = 0;
      |MENAV "     Por favor haga la creacion de Diskette.";
      |ACABA;
  |FINSI;

  |PUSHV 0501 2380;

  Campos1  = "19";
  |SI #0#2 < 80; Campos1 = "20"; |FINSI;
  VAlfa    = #0#2;   VAlfa = ("00" + VAlfa) % -102;
  Campos1  = Campos1 + VAlfa;                          || Ejercicio
  VAlfa    = #0#17;  VAlfa = ("00" + VAlfa) % -102;
  Campos2  = VAlfa;                                     || Codigo Postal 1
  VAlfa    = #0#18;  VAlfa = ("000" + VAlfa) % -103;
  Campos3  = VAlfa;                                     || Codigo Postal 2

  #104#16 = "Delegacion .........: " + #0#26;
  #104#17 = "Ejercicio ..........: " + Campos1;
  #104#18 = "Modelo .............: 349";
  #104#19 = "Numero Justificante : ";
  #104#20 = "N.I.F. .............: " + #0#9;
  #104#21 = "Nombre : " + #0#3;
  Comodin = #0#4 + " " + #0#5; |QBF Comodin;
  Comodin = Comodin + ", N§ " + #0#6 + ", C.P." + Campos2 + Campos3 + ", " + #0#7;
  #104#22 = Comodin;
  #104#23 = "P.Cont.: " + #0#25;
  #104#24 = "Telefono ...........: " + #0#11;
  #104#25 = "Numero total Registros ..: " + #104#14;
  #104#26 = "Densidad soporte : 1,44 MB en Diskettes de 3 1/2";
  #104#27 = "";
  #104#28 = "";
  |C_M #104#27, 1, "N";
  |C_M #104#28, 1, "N";
  |SI #0#22 = "C";
      #104#27 = "Numero total Declarantes : " + #104#15;
      #104#28 = "Numero total Declarados .: " + #104#6;
      |C_M #104#27, 1, "S";
      |C_M #104#28, 1, "S";
  |FINSI;

  |PINPA #2#104;
  |PINDA #2#104;
  |ENDATOS #1#104;
  |SI FSalida = 0;
      |PINPA #2#104;
      |PINDA #2#104;

      |CONFI "2400SRealizar Impresion de la Etiqueta : ";
      |SI FSalida ! 0; |POPV; |ACABA; |FINSI;

      |IMPRE 0;
      |SI FSalida ! 0;  |ACABA;  |FINSI;

      |INFOR "catraso2";
      |SI FSalida ! 0;  |FINIMP;  |ACABA;  |FINSI;

      |PRINT; |PIEINF; |FININF; |FINIMP;
  |FINSI;
  |POPV;
|FPRC;

|| ***********************************************************************
||                       PROCESO DE DISTRIBUCION
|| ***********************************************************************
|PROCESO Distribuye; |TIPO 3;
  enEjercicio = 2000 + #0#2;
  |SI #0#2 ] 80;
      enEjercicio = 1900 + #0#2;
  |FINSI;

  |PINPA #0#0;
  |PINDA #0#0;

  |PARA; |SI Salir = 0; |HACIENDO;
         |MENUG MGeneral;
         MGeneral2 = FSalida;
         Opcion    = MGeneral2;
         Salir     = 1;
         |SI Opcion ] 1; |Y Opcion [ 4;
             Salir = 0;
             |SI Opcion = 1; |Y #0#22 ! "L"; |HAZ Creacion;       |FINSI;
             |SI Opcion = 1; |Y #0#22 = "L"; |HAZ CreacionLaser;  |FINSI;
             |SI Opcion = 2; |Y #0#22 ! "L"; |HAZ Portada;        |FINSI;
             |SI Opcion = 3; |Y #0#22 ! "L"; |HAZ Etiquetas;      |FINSI;
             |SI Opcion = 4; |Y #0#22 ! "L"; |HAZ GrabaDisketteC; |FINSI;
             |SI Opcion = 4; |Y #0#22 = "L"; |HAZ GrabaDisketteL; |FINSI;
         |FINSI;
  |SIGUE;
|FPRC;
