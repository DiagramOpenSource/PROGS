|FICHEROS;
     camodest #0;
     cacuenta #1;   || plan contable
     caagrupa #2;   || agrupaciones
     caepigra #3;   || epigrafes
     caparbal #4;   || partidas balance
     caparexp #5;   || partidas resultados
     camodetm #10,MANTE; || tmp.edicion
|FIN;

|VARIABLES;
     aAlfa1 = "";
     aAlfa2 = "";
     aAlfa3 = "";
     aAlfa4 = "";
     nNume1 = 0;
     nNume2 = 0;
     nNume3 = 0;
     nNume4 = 0;
     nSwDefNivel = 0;
     nLinea = 0;
     nBloq = -1;
     aTipo = "";
     aAgru = "";
     aEpig = "";
     aPart = "";
     aCuenta = "";
     aTitulo = "";
     nInkey = 0;
|FIN;

|INCLUYE acceso_i;
|INCLUYE puntos_i;

____________________________________/ CALCULOS DE PANTALLA
|VARIABLES;
     cta = 0;
     digito = 0;
     long = "";
     swdig = 0;
     men2 = "     Longitud de Cuenta Fuera de Nivel !";
|FIN;

|PROCESO cuenta; |TIPO 6;
  p_alfa1 = #0Campo;
  salidas = FSalida;
  |HAZ punto;
  |SI p_error ! 0;  |ERROR;  |ACABA;  |FINSI;
  |SI #0Campo = "            "; |ACABA; |FINSI;
  digito = Campo + 1;
  cta  = #0Campo;
  long = cta % 0;
  swdig = 0;
  |SI long = #30#14; swdig = 1; #0digito = 1; |FINSI;
  |SI long = #30#15; swdig = 1; #0digito = 2; |FINSI;
  |SI long = #30#16; swdig = 1; #0digito = 3; |FINSI;
  |SI long = #30#17; swdig = 1; #0digito = 4; |FINSI;
  |SI long = #30#18; swdig = 1; #0digito = 5; |FINSI;
  |SI long = #30#19; swdig = 1; #0digito = 6; |FINSI;
  |SI swdig = 0;  |MENAV men2;  |ERROR;  |FINSI;
|FPRC;

|PROCESO DefNivel; |TIPO 7;
     |SI nSwDefNivel = 0;
          #0#0 = dig3;
          |PINTA #0#0;
          nSwDefNivel = 1;
     |FINSI;
|FPRC;

|PROCESO Mayus; |TIPO 0;
     #0Campo = #0Campo > #0Campo;
     |PINTA #0Campo;
|FPRC;
____________________________________/
|PROCESO GrabaTempo;
     |QBF aTitulo;
     |SI aTitulo ! "";
          |DDEFECTO #10;
          nLinea = nLinea + 50;
          #10#0 = nLinea;          || linea
          #10#1 = #1#4;            || tipo
          #10#2 = #1#5;            || bloque
          #10#3 = #1#6;            || agrupacion
          #10#4 = #1#7;            || epigrafe
          #10#5 = #1#8;            || partida
          #10#6 = aCuenta;         || cuenta
          #10#7 = aTitulo;         || descripcion
          |GRABA 020#10n;
     |FINSI;
|FPRC;

|PROCESO RupPart;
     aPart = #1#8;

     |SI nBloq = 0;
          #4#0 = aTipo;
          #4#1 = aAgru;
          #4#3 = aEpig;
          #4#5 = aPart;
          |LEE 000#4=;
          |SI FSalida = 0;
               aTitulo = #4#6;
          |FINSI;
     |SINO;
          #5#0 = nBloq;
          #5#1 = aTipo;
          #5#2 = aAgru;
          #5#4 = aEpig;
          #5#6 = aPart;
          |LEE 000#5=;
          |SI FSalida = 0;
               aTitulo = #5#7;
          |FINSI;
     |FINSI;

     aCuenta = "";
     aTitulo = "            " + aTitulo;
     |HAZ GrabaTempo;
|FPRC;

|PROCESO RupEpig;
     aEpig = #1#7;

     #3#0 = aTipo;
     #3#1 = aAgru;
     #3#3 = aEpig;
     |LEE 000#3=;
     |SI FSalida = 0;
          aTitulo = #3#4;
     |FINSI;

     aCuenta = "";
     aTitulo = "         " + aTitulo;
     |HAZ GrabaTempo;
|FPRC;

|PROCESO RupAgru;
     aAgru = #1#6;

     #2#0 = aTipo;
     #2#1 = aAgru;
     |LEE 000#2=;
     |SI FSalida = 0;
          aTitulo = #2#2;
     |FINSI;

     aCuenta = "";
     aTitulo = "      " + aTitulo;
     |HAZ GrabaTempo;
|FPRC;

|PROCESO RupBloq;
     nBloq = #1#4;

     aCuenta = "";
     aTitulo = "";
     |HAZ GrabaTempo;
|FPRC;

|PROCESO RupTipo;
     aTipo = #1#5;

     |SI aTipo = "A";    aTitulo = "<ACTIVO>";    |FINSI;
     |SI aTipo = "P";    aTitulo = "<PASIVO>";    |FINSI;
     |SI aTipo = "D";    aTitulo = "<DEBE>";      |FINSI;
     |SI aTipo = "H";    aTitulo = "<HABER>";     |FINSI;

     aCuenta = "";
     aTitulo = "   " + aTitulo;
     |HAZ GrabaTempo;
|FPRC;

|PROCESO cacuenta_1;
     |SI #1#4 ! 0;|Y #0#5 = "B";   |ACABA;   |FINSI;
     |SI #1#4 = 0;|Y #0#5 = "R";   |ACABA;   |FINSI;

     |SI aTipo ! #1#5;
          |HAZ RupTipo;
          |HAZ RupBloq;
          |HAZ RupAgru;
          |HAZ RupEpig;
          |HAZ RupPart;
     |SINO;
          |SI nBloq ! #1#4;
               |HAZ RupBloq;
               |HAZ RupAgru;
               |HAZ RupEpig;
               |HAZ RupPart;
          |SINO;
               |SI aAgru ! #1#6;
                    |HAZ RupAgru;
                    |HAZ RupEpig;
                    |HAZ RupPart;
               |SINO;
                    |SI aEpig ! #1#7;
                         |HAZ RupEpig;
                         |HAZ RupPart;
                    |SINO;
                         |SI aPart ! #1#8;
                              |HAZ RupPart;
                         |FINSI;
                    |FINSI;
               |FINSI;
          |FINSI;
     |FINSI;

     aCuenta = #1#0;
     aTitulo = #1#2;
     |QBF aTitulo;
     aTitulo = "               *" + aTitulo;
     aAlfa1 = "." * 58;
     aTitulo = (aTitulo + aAlfa1) % 158;
     |HAZ GrabaTempo;
|FPRC;

|DEFBUCLE cacuenta;
 #1#4;
 0, 1;
 " ", 0,  0,  0,  0, #0#1, #0#0;
 "z", 4, 99, 99, 99, #0#3, #0#0;
 ;
 NULL, cacuenta_1;
|FIN;

|PROCESO Min10;
     #10#0 = 1;
|FPRC;

|PROCESO Max10;
     #10#0 = 999999;
|FPRC;

|PROCESO Tipo3; |TIPO 3;
     aAlfa1 = Usuario;   |QBT aAlfa1;
     |NOME_DAT #10 aAlfa1;
     |DELINDEX #10;

     |ABRE #2;
     |ABRE #3;
     |ABRE #4;
     |ABRE #5;
     |ABRE #10;
     |BUCLE cacuenta;
     |CIERRA #2;
     |CIERRA #3;
     |CIERRA #4;
     |CIERRA #5;
     |CIERRA #10;

     |SI nLinea ! 0;
          nLinea = 0;
          |ENTLINEAL #1#10, 1, 4, 19, 1, Min10, Max10;
     |SINO;
          |MENAV "    Seleccion vacia ...";
     |FINSI;

     |DELINDEX #10;
|FPRC;

____________________________________/ DESDE LA EDICION
|PROCESO CortaCuenta;
     #10#0 = nLinea;
     |LEE 121#10=;
     |SI FSalida = 0;
          |BORRA 020#10a;
     |FINSI;
     |LIBERA #10;
|FPRC;

|PROCESO PegaCuenta;
     nNume1 = #10#0;
     |PARA; |SI FSalida = 0; |HACIENDO;
         nNume1 = nNume1 + 1;
         #10#0 = nNume1;
         |LEE 000#10=;
     |SIGUE;
     #10#0 = nNume1;
     #10#1 = #1#4;       || bloque
     #10#2 = #1#5;       || tipo
     #10#3 = #1#6;       || agrupacion
     #10#4 = #1#7;       || epigrafe
     #10#5 = #1#8;       || partida
     #10#6 = aCuenta;    || cuenta
     #10#7 = aTitulo;    || descripcion
     |GRABA 020#10n;
|FPRC;

|PROCESO ActualizaCuenta;
     |ABRE #1;
     #1#0 = aCuenta;
     #1#1 = #0#0;
     |LEE 101#1=;
     |SI FSalida = 0;
          #1#4 = #10#1;       || bloque
          #1#5 = #10#2;       || tipo
          #1#6 = #10#3;       || agrupacion
          #1#7 = #10#4;       || epigrafe
          #1#8 = #10#5;       || partida
          |GRABA 020#1a;
     |FINSI;
     |LIBERA #1;
     |CIERRA #1;
|FPRC;

|PROCESO Pulsa; |TIPO 11;
     nInkey = FSalida;

     |SI nInkey = 13;    || cortar (F5)
          aCuenta = #10#6;
          aTitulo = #10#7;
          |QBF aCuenta;
          |SI aCuenta = "";
               |MENAV "    Titulo de Balance ...";
               nLinea = 0;
          |SINO;
               nLinea = #10#0;
          |FINSI;
     |FINSI;

     |SI nInkey = 14;    || pegar (F6)
          |SI nLinea = 0;
               |MENAV "    Pulse <Cortar> antes de <Pegar> ...";
          |SINO;
               |HAZ ActualizaCuenta;
               |HAZ PegaCuenta;
               |HAZ CortaCuenta;
               nLinea = 0;
          |FINSI;
     |FINSI;
|FPRC;
