|FICHEROS;
     cainpago #0;
     camanpag #1;
     caclipro #2;
     caivasop #3;
     cacuenta #5;
|FIN;

|VARIABLES;
     informe = "cainpago";
     sw = 0;
     &ult_cta = "";
     &total = 0;
     &total1 = 0;
     &sw_inf = 0;
     &empre = "";
     &cp = "";
     &impor = 0;
     alfa1 = "";
     novale = 0;
|FIN;

|PROCESO mayor;
     alfa1 = #0Campo > " ";
     #0Campo = alfa1;
     |PINTA #0Campo;
|FPRC;

|PROCESO imprim;
     |HAZ verbueno;
     |SI novale = 1; |ACABA; |FINSI;
     |SI sw = 0;
          sw = 1;
          sw_inf = 0;
          |HAZ busca_cuen;
          |PRINT;
          ult_cta = #1#4;
     |FINSI;
     |SI ult_cta ! #1#4;
          sw_inf = 2;    ||Imprime el primer total
          |PRINT;
          sw_inf = 0;
          |HAZ busca_cuen;
          |PRINT;
          total = 0;
     |FINSI;
     sw_inf = 1;
     |HAZ leeiva;
     |PRINT;
     total = total + impor;
     total1 = total1 + impor;
     ult_cta = #1#4;
|FPRC;

|PROCESO leeiva;
     |DDEFECTO #3;
     #3#0 = #1#0;
     #3#2 = #1#1;
     #3#27 = #1#2;
     |LEE 001#3=;
|FPRC;

|PROCESO busca_cuen;
     |DDEFECTO #2;
     #2#23 = "P";
     #2#10 = #1#4;
     #2#11 = #1#5;
     |LEE 001#2=;
     |SI FSalida = 0;
         cp = (("000" + #2#3) % -102) + (("000" + #2#4) % -103);
     |SINO;
         cp = "";
         |ABRE #5;
         |DDEFECTO #5;
         #5#0 = #1#4;
         #5#1 = #1#5;
         |LEE 001#5=;
         #2#1 = #5#2;
         |CIERRA #5;
    |FINSI;
|FPRC;

|DEFBUCLE lee_cobro;
 #1#2;
 0, 1, 3, 11, 26; || selecciona libro+factura+pago+tipo recibo + banco
 #0#4, 0, #0#6, #0#0, #0#2,   0, #0#12, #0#15;
 #0#5, 9, #0#7, #0#1, #0#3, 999, #0#13, #0#16;
 e;
 NULL, imprim;
|FIN;

|PROCESO inicio; |TIPO 3;
  |IMPRE 0;
  |SI FSalida ! 0;  |ACABA;  |FINSI;
  |INFOR informe;
  |SI FSalida ! 0;  |FINIMP;  |ACABA;  |FINSI;
  empre = #48#1;
  |ABRE #3;
  |ABRE #2;
  |BUCLE lee_cobro;
  |SI sw = 1;
      sw_inf = 2;
      |PRINT;
  |FINSI;
  |CIERRA #3;
  |CIERRA #2;
  |PIEINF;
  |FININF;
  |FINIMP;
|FPRC;

|PROCESO verbueno;
   novale = 0;
   impor = 0;
   |SI #0#14 = "T";
       impor = #1#8; || importe total pago
       |ACABA;
   |FINSI;
   |SI #0#14 = "S";
       impor = #1#23; || importe pagado
       |SI impor = 0;
           novale = 1;
       |FINSI;
       |ACABA;
   |FINSI;
   |SI #0#14 = "N";
       impor = #1#8 - #1#23; || importe total - liquidado
       |SI impor = 0; |O #1#22 = "S";
           novale = 1;
           impor = 0;
       |FINSI;
       |ACABA;
   |FINSI;
|FPRC;
