|FICHEROS;
     eosclpre #0;
     eosclipr #1;
     eosaplic #99;
     :/agicli/def/agim0016 #2;
|FIN;

|VARIABLES;
     cont = 0;
     nume1 = 0;
     nume2 = 0;
     lugar = 0;
     nifnum = 0;
     resto = 0;
     men2 = "2325S Dar de Alta en Clientes-Proveedores General (S/N) : ";
     sw = 0;
     aponer = "";
     fiac = "";
     relleno = "                                                 ";
     dni = "";
     cif = "";
     empresa = 0;
     cliente = 0;
     clave = "";

     letrasnif = "TRWAGMYFPDXBNJZSQVHLCKE";
     letra = "";
     alfa1 = "";
     alfa2 = "";
     final = "";
     x = "";
     pos1 = "";
     pos2 = "";
     pos3 = "";
     pos4 = "";
|FIN;

|PROCESO Mayus;
  #2Campo = #2Campo > #2Campo;  |PINTA #2Campo;
|FPRC;

|PROCESO Filtra;
  |ABRE #2;
  #2#0 = #1#14;
  |LEE 040#2=;
  |SI FSalida ! 0;  |DDEFECTO #2;  |FINSI;
  |CIERRA #2;

  |C_M #1#7,  1, "S";
  |C_M #1#9,  1, "N";
  |C_M #1#10, 1, "N";
  |SI #2#5 = "S";
      #1#7 = 99;    |PINTA #1#7;
      #1#8 = #2#4;  |PINTA #1#8;
      |C_M #1#7,  1, "N";
      |C_M #1#9,  1, "S";
      |C_M #1#10, 1, "S";
  |FINSI;
|FPRC;

|PROCESO grabalo;
#0#3  = #1#0;
#0#4  = #1#1;
#0#5  = #1#2;
#0#6  = #1#3;
#0#7  = #1#4;
#0#8  = #1#5;
#0#9  = #1#6;
#0#10 = #1#7;
#0#11 = #1#8;
#0#12 = #1#9;
#0#13 = #1#10;
#0#14 = #1#11;
#0#15 = #1#12;
#0#18 = #1#13;
|GRABA 140#0a;
|LIBERA #0;
|FPRC;

|DEFBUCLE eosclpre;
#0#1;
3;
00000, 0, 00000, #1#0;
99999, 9, 99999, #1#0;
be;
NULL, grabalo;
|FIN;

|PROCESO modifig; |TIPO 3;
cont = (FEntrada / 100) ! 0;
|SI cont ! 2; |ACABA; |FINSI;

|ABRE #99;
|LEE 040#99p;
|SI FSalida ! 0; |DDEFECTO #99; |FINSI;
|CIERRA #99;

|SI #99#11 = 2; |ACABA; |FINSI;

|CONFI "2325S Modificar en Clientes-Proveedores Empresa (S/N) : ";
|SI FSalida ! 0; |ACABA; |FINSI;

|BUCLE eosclpre;
|FPRC;

|PROCESO letranif;
  |SI FSalida ! 9;  |ACABA;  |FINSI;

  alfa1 = #1Campo;   |QBT alfa1;
  x    = alfa1 % 101;
  |SI x ! "A"; |Y x ! "B"; |Y x ! "C"; |Y x ! "D"; |Y x ! "E"; |Y x ! "F";
   |Y x ! "G"; |Y x ! "P"; |Y x ! "Q"; |Y x ! "S"; |Y x ! "X";
      final = "";
      alfa2 = alfa1 % 0;
      nume1 = alfa2;
      |PARA nume2 = 1;  |SI nume2 [ nume1;  |HACIENDO nume2 = nume2 + 1;
            lugar = (nume2 * 100) + 1;
            alfa2 = alfa1 % lugar;
            |SI alfa2 = "0"; |O alfa2 = "1";|O alfa2 = "2"; |O alfa2 = "3";
             |O alfa2 = "4"; |O alfa2 = "5";|O alfa2 = "6";
             |O alfa2 = "7"; |O alfa2 = "8";|O alfa2 = "9";
                final = final + alfa2;
            |FINSI;
      |SIGUE;
      nifnum = final;
      |SI nifnum ! 0; |Y final ! "";
          resto   = nifnum $ 23;
          resto   = resto + 1;
          lugar   = (resto * 100) + 1;
          letra   = letrasnif % lugar;
          final   = ("000000000" + final + letra) % -109;
          #1Campo = final;
      |FINSI;
   |SINO;
      |MENAV "    ATENCION !!! No se calcular un C.I.F. ...";
   |FINSI;
   |PINTA #1Campo;
|FPRC;
