|FICHEROS;
  cadiario #0;
  camaasic #1;
  camaasil #2;
  cacuenta #4;

  cabucem1 #100;
|FIN;

|VARIABLES;
  &entrada = 1;
  sw = 0;
  fecha = 0;
  fecalf = "";
  mensa1 = "    ERROR ESTA FECHA NO CORRESPONDE AL EJERCICIO";
  traba1 = "";
  varalf = "";
  &swlin = 0;
  &descuadre = 0;
  &a_debe = 0;
  &a_haber = 0;
  &tde = 0;
  &tha = 0;
  &hoja = 0;
  d = "D";
  h = "H";
  informe = "";
  alfa1 = "";

  aAlfa    = "";
  Comodin  = "";
  Comodin1 = "";
  Handle  = 0;
  Numeracion = 1;
|FIN;

|INCLUYE acceso_i;

|PROCESO desasien;
  |SI #0#13 < #0#12;
      |MENAV "     Error en Introduccion";
      |ERROR;
  |FINSI;
|FPRC;

|PROCESO desdiari;
  |SI #0#7 < #0#6;
      |MENAV "     Error en Introduccion";
      |ERROR;
  |FINSI;
|FPRC;

|PROCESO mayor;
  alfa1 = #0Campo > " ";
  #0Campo = alfa1;
  |PINTA #0Campo;
|FPRC;

|PROCESO mayor1;
  |HAZ mayor;
  |SI #0Campo = "S";
      #0#9 = 0;
      #0#10 = 0;
      |PINTA #0#9;
      |PINTA #0#10;
      |CAMPO_MODIFICA #0#9,1,"N";
      |CAMPO_MODIFICA #0#10,1,"N";
  |SINO;
      |CAMPO_MODIFICA #0#9,1,"S";
      |CAMPO_MODIFICA #0#10,1,"S";
  |FINSI;
|FPRC;

|PROCESO estacua;
  |SI #0#9 ! #0#10;
      |MENAV "      ATENCION descuadre en DEBE y HABER";
      |ERROR;
  |FINSI;
|FPRC;

|PROCESO Empresas;
   #48#2 = #cabucem1#0;
   #48#3 = #cabucem1#1;
   |LEE 000#48=;
   |SI FSalida = 0;
      |DBASE fich_alta; |QBF fich_alta;
      fich_alta = fich_alta + "fich/" + (("00000" + #cabucem1#0) % -105) + #cabucem1#1 + "/";
      |PATH_DAT #0 fich_alta; |PATH_DAT #1 fich_alta;
      |PATH_DAT #2 fich_alta; |PATH_DAT #4 fich_alta;
      |HAZ inicio;
      |DDEFECTO #0;
      #0#0 = fec1;
      #0#1 = fec2;
      #0#8 = 1;
      sw = 0;
      #0#5 = ~;
      |HAZ impre;                 || Calculo impresion
      #0#9 = tde;
      #0#10 = tha;
      #0#4 = hoja;
      |SI #0#1 ! fec2;
          #0#0 = #0#1 + 1;
      |SINO;
          #0#0 = fec2;
      |FINSI;
      #0#1 = fec2;
      #0#11 = "S";
      |GRABA 001#0a;
      |HAZ borrar;                || Borra acumulado

      |DBASE Comodin1; |QBF Comodin1;
      Comodin1 = Comodin1 + "hacienda/PresLibr/" + (("00000" + #cabucem1#0) % -105) + #cabucem1#1 + "/";
      Comodin1 = Comodin1 + "DIARIO*.xls";
      |_PDIR Comodin1;
      |SI FSalida = 0;
         |PARA; |SI FSalida = 0; |HACIENDO;
            Comodin = Comodin1 % -107; Comodin = Comodin % 0103;
            Numeracion = Comodin; Numeracion = Numeracion + 1;
            |_SDIR Comodin1;
         |SIGUE;
      |SINO;
         Numeracion = 1;
      |FINSI;

      Comodin = "c:/tmp1.xls";
      |XABRE "C", Comodin, Handle;
      |SI FSalida ] 0;
         |XCIERRA Handle;
         |DBASE Comodin1; |QBF Comodin1;
         Comodin1 = Comodin1 + "hacienda/PresLibr/";
         |MKDIR Comodin1;
         Comodin1 = Comodin1 + (("00000" + #cabucem1#0) % -105) + #cabucem1#1 + "/";
         |MKDIR Comodin1;
         Comodin1 = Comodin1 + "DIARIO_" + (("000" + Numeracion) % -103) + ".xls";
         |IP_REMOTO aAlfa; |QBF aAlfa;
         |SI aAlfa ! "";
            |RECIBE_FICHERO Comodin, Comodin1;
         |SINO;
            |COPIA_FICHERO Comodin, Comodin1;
         |FINSI;
         FSalida = 1;

         |PARA; |SI FSalida ! 0; |HACIENDO;
            |RFBORRA Comodin;
            |SI FSalida ! 0;
               |MENAV "    Termine la tarea de Excel que tiene activa";
            |FINSI;
         |SIGUE;
      |FINSI;
   |FINSI;
|FPRC;

|DEFBUCLE Empresas;
#cabucem1#1;
;
INICIO;
FINAL;
;
NULL,Empresas;
|FIN;

|| *********************************************************************
||                             COMIENZO
|| *********************************************************************

|PROGRAMA;
   |SI PARAMETRO = "";
     |CLS;
     |CABEZA "Listado Diario Oficial";
     |ABRE #0;
     |HAZ inicio;
     #0#8 = 1;
     |LEE 141#0=;
     |SI FSalida ! 0;
         |DDEFECTO #0;
         #0#0 = fec1;
         #0#1 = fec2;
         #0#8 = 1;
         |GRABA 001#0c;
     |FINSI;
     FSalida = 1;
     |PARA;|SI FSalida = 1; |HACIENDO;
           sw = 0;
           |PINPA #0#0;
           #0#8 = 1;
           |LEE 141#0=;
           #0#5 = ~;
           |PINDA #0#0;                    || Pinta los datos por defecto
           |ENDATOS #1#0;                  || Seleccion informe
           |SI FSalida = 0;                || Si el conforme es igua a S
               |HAZ impre;                 || Calculo impresion
               #0#9 = tde;
               #0#10 = tha;
               #0#4 = hoja;
               |SI #0#1 ! fec2;
                   #0#0 = #0#1 + 1;
               |SINO;
                   #0#0 = fec2;
               |FINSI;
               #0#1 = fec2;
               #0#11 = "S";
               |GRABA 001#0a;
               |HAZ borrar;                || Borra acumulado
               FSalida = 1;                || Vuelve al bucle
           |SINO;
               FSalida = 0;
           |FINSI;
      |SIGUE;
      |CIERRA #0;
   |SINO;
       |ABRE #0; |ABRE #48;
       |BUCLE Empresas;
       |CIERRA #48; |CIERRA #0;
   |FINSI;
|FPRO;

|| *************************************************************************
||                          MEOLLO
|| *************************************************************************

|PROCESO porimpre;
  #4#0 = #2#4;
  #4#1 = #2#5;
  |LEE 040#4=;
  |SI FSalida ! 0;
      |DDEFECTO #4;
  |FINSI;

  swlin = 0;
  |SI #2#8 = "D";
      a_debe = #2#9;
      a_haber = 0;
  |SINO;
      a_debe = 0;
      a_haber = #2#9;
  |FINSI;

  |PRINT;
|FPRC;

|DEFBUCLE cadiario2;
#2#2;
3;
#1#0, #1#1, #1#2, d, 0001;
#1#0, #1#1, #1#2, h, 9999;
be;
NULL,porimpre;
|FIN;

|PROCESO bucleimp;
  |SI #0#2 = "S";
      |SI sw = 0; fecha = #1#1; sw = 1; |FINSI;
      |SI fecha ! #1#1; |PIEINF; fecha = #1#1; |FINSI;
  |FINSI;

  descuadre = #1#4 - #1#5;
  |SI descuadre = 0;
      swlin = 1;
      |PRINT;
  |SINO;
      swlin = 2;
      |SI descuadre < 0; descuadre = descuadre * (-1); |FINSI;
      |PRINT;
  |FINSI;
  |SI #0#3 = "S";
      |BUCLE cadiario2;
  |SINO;
      |BUCLELIN 0porimpre#1;
  |FINSI;
|FPRC;

|DEFBUCLE cadiario1;
#1#2;
;
#0#12, #0#0, #0#6, 000001;
#0#13, #0#1, #0#7, 999999;
be;
NULL,NULL,NULL,NULL,NULL,bucleimp;
#1#1,#1#3,#1#0,#1#2;
|FIN;

|PROCESO contar;
  |SI #1#1 < #0#0;
      tde = tde + #1#4;
      tha = tha + #1#5;
  |FINSI;
|FPRC;


|DEFBUCLE cadiario0;
#1#2;
;
     1, fec1, #0#6, 000001;
999999, #0#0, #0#7, 999999;
be;
NULL,contar;
|FIN;

|PROCESO impre;
  hoja = #0#4;
  |SI PARAMETRO ! "";
     Impresora = "?c:\tmp1,,,?Diario";
  |FINSI;
  |IMPRE 0;
  |SI FSalida ! 0;  |MENAV "     NO existe impresora";  |ACABA;  |FINSI;
  |HAZ los_informes;
  informe = que_i1;
  |INFOR informe;
  |SI FSalida ! 0; |MENAV "     NO existe informe"; |FINIMP;  |ACABA;  |FINSI;
  |ABRET #2;
  |ABRE #4;
  |SI #0#11 = "S";
      |BUCLE cadiario0;
  |SINO;
      tde = #0#9;
      tha = #0#10;
  |FINSI;
  |BUCLE cadiario1;
  |CIERRA #4;
  |CIERRAT #2;
  |PIEINF;
  |FININF;
  |FINIMP;
|FPRC;

|PROCESO borrar;
   swlin = 0;
   descuadre = 0;
   a_debe = 0;
   a_haber = 0;
   tde = 0;
   tha = 0;
   hoja = 0;
|FPRC;

|PROCESO fechad;
  |SI #0#0 < fec1; |O #0#0 > fec2;
      |MENAV mensa1;
      |ERROR;
      |ACABA;
  |FINSI;
|FPRC;

|PROCESO fechah;
  |SI #0#1 < fec1; |O #0#1 > fec2; |O #0#1 < #0#0;
      |MENAV mensa1;
      |ERROR;
      |ACABA;
  |FINSI;
|FPRC;
