|FICHEROS;
   capagaut #0;
   capagcab #6;
   capaglin #1;
   caivasop #2;
   camanpag #3;
   cacuenta #10;
   capagtmc #16,MANTE;
   capagtml #11;
|FIN;

|VARIABLES;
 &entrada = 1;

 alfa1    = "";
 nume1    = 0;
 para1    = 0;

 mens     = "";
 swErrorR = 0;
 menR1    = "    Existen Numero de Pagares Posteriores ";

 swpaso   = 0;
 cta      = 0;
 cta_t    = 0;
 dig_t    = 0;
 long     = "  ";
 sw       = 0;
 error    = 0;
 estado   = 0;
 linea    = 0;

 &quepant  = 1;
 &totrem   = 0;
 &contador = 0;
 &banco    = 0;
 &noremesa = 0;

 sw_n     = 0;
 d_prov = "";
 h_prov = "";
 swuno  = 0;
 cprov  = "";
 dprov  = 0;
 cvenc  = @;
|FIN;

|INCLUYE acces3_i;
|INCLUYE punto2_i;
|INCLUYE i_ficher;

|| **********************************************************************
|PROCESO mayus1;
 alfa1 = #0Campo > " ";
 #0Campo = alfa1;
 |PINTA #0Campo;
|FPRC;

|PROCESO contador; |TIPO 7;
 |SI swpaso = 0;
     |DDEFECTO #6;
     |ABRE #6;
     |LEE 040#6u;
     |SI FSalida = 0;
         #0#0 = #6#0 + 1;
     |SINO;
         #0#0 = 1;
     |FINSI;
     swpaso = 1;
     |CIERRA #6;
 |FINSI;
|FPRC;

|PROCESO ver_pagare; |TIPO 0;
 |SI FSalida = 2; |ACABA; |FINSI;
 |HAZ leerem;
 |SI swErrorR ! 0;
     |MENAV mens;
     |ERROR;
     |ACABA;
 |FINSI;
|FPRC;

|PROCESO vto_autom; |TIPO 7;
|SI #0#6 = "A";
        alfa1 = "N";
|SINO;
        alfa1 = "S";
|FINSI;
|C_M #0#1, 1, alfa1;
|FPRC;

|PROCESO fecha; |TIPO 0;
|SI #0#6 = "M";
    #0#10 = #0#1; |PINTA #0#10;
    #0#17 = #0#1; |PINTA #0#17;
|SINO;
    #0#10 = #0#2; |PINTA #0#10;
|FINSI;
|FPRC;

|PROCESO verfecha; |TIPO 0;
 |SI FSalida = 2; |ACABA; |FINSI;

 |SI Campo = 17;
     |SI #0#10 > #0#17;
         |MENAV "    Error de entrada ...";
         |ERROR;
         |ACABA;
     |FINSI;
 |FINSI;

 |SI #0Campo < #0#2;
     |MENAV "    Fecha Vencimiento menor a Fecha Emision Pagare ...";
     |ACABA;
 |FINSI;

 |SI #0#6 = "M";
     |SI #0Campo < #0#1;
         |MENAV "    Fecha Vencimiento MENOR Fecha Vto. Pagare ...";
         |ACABA;
     |FINSI;

     |SI #0Campo > #0#1;
         |MENAV "    Fecha Vencimiento MAYOR Fecha Vto. Pagare ...";
         |ACABA;
     |FINSI;
 |FINSI;
|FPRC;

|PROCESO leerem;
 swErrorR = 0;
 |ABRE #6;
 |DDEFECTO #6;
 #6#0 = #0#0;
 |LEE 001#6];
 estado = FSalida;
 |SI FSalida = 0;
     swErrorR = 1; mens = menR1;
 |FINSI;
 |CIERRA #6;
|FPRC;

|PROCESO cuenta1; |TIPO 6;
 p_alfa1 = #0Campo;
 salidas = FSalida;
 |HAZ punto2;
 |SI p_error ! 0;  |ERROR;  |ACABA;  |FINSI;
 |SI Campo = 4;
     alfa1 = #0Campo; |QBF alfa1;
     |SI alfa1 = ""; |ERROR; |ACABA; |FINSI;
 |FINSI;
 cta_t = Campo;
 |SI Campo [ 19;
     dig_t = Campo + 1;
 |SINO;
     dig_t = Campo + 12;
 |FINSI;
 |HAZ cuenta1_t;
|FPRC;

|PROCESO cuenta1_t;
   cta  = #0cta_t;
   long = cta % 0;
   sw   = 0;
|SI long = dig4; sw = 1; #0dig_t = 1; |FINSI;
|SI long = dig5; sw = 1; #0dig_t = 2; |FINSI;
|SI long = dig6; sw = 1; #0dig_t = 3; |FINSI;
|SI long = dig7; sw = 1; #0dig_t = 4; |FINSI;
|SI long = dig8; sw = 1; #0dig_t = 5; |FINSI;
|SI long = dig9; sw = 1; #0dig_t = 6; |FINSI;
|SI sw = 0;
    #0dig_t = 0;
    |MENAV "    Longitud de cuenta fuera de Nivel !";
    |ERROR;
    |ACABA;
|FINSI;
|FPRC;

|PROCESO cuefin1; |TIPO 0;
 error = 0;
 |ABRE #10;
 #10#0 = #0cta_t;
 #10#1 = #0dig_t;
 |LEE 040#10=;
 |SI FSalida ! 0; error = 1; |CIERRA #10; |ERROR; |ACABA; |FINSI;
 |SI #10#3 = "N";
     error = 1;
     |CIERRA #10;
     |MENAV "      La cuenta no tiene pase DIRECTO !";
     |ERROR;
     |ACABA;
 |FINSI;
 |SI Campo = 4; #0#3 = #10#2; |PINTA #0#3; |FINSI;
 |CIERRA #10;
|FPRC;

|PROCESO seleccion; |TIPO 0;
|HAZ mayus1;
|SI #0#21 = "N";
    |CAMPO_MODIFICA #0#22, 1, "S"; |CAMPO_MODIFICA #0#23, 1, "S";
    |CAMPO_MODIFICA #0#24, 1, "S"; |CAMPO_MODIFICA #0#25, 1, "S";
    |CAMPO_MODIFICA #0#26, 1, "S"; |CAMPO_MODIFICA #0#27, 1, "S";
    |CAMPO_MODIFICA #0#28, 1, "S"; |CAMPO_MODIFICA #0#29, 1, "S";
    |CAMPO_MODIFICA #0#30, 1, "S"; |CAMPO_MODIFICA #0#31, 1, "S";
    |CAMPO_MODIFICA #0#32, 1, "S"; |CAMPO_MODIFICA #0#33, 1, "S";
    |CAMPO_MODIFICA #0#12, 1, "N"; |CAMPO_MODIFICA #0#19, 1, "N";
    #0#12 = "            "; #0#13 = 0; |PINTA #0#12;
    #0#19 = "zzzzzzzzzzzz"; #0#20 = 9; |PINTA #0#19;
|SINO;
    |CAMPO_MODIFICA #0#12, 1, "S"; |CAMPO_MODIFICA #0#19, 1, "S";
    |CAMPO_MODIFICA #0#22, 1, "N"; |CAMPO_MODIFICA #0#23, 1, "N";
    |CAMPO_MODIFICA #0#24, 1, "N"; |CAMPO_MODIFICA #0#25, 1, "N";
    |CAMPO_MODIFICA #0#26, 1, "N"; |CAMPO_MODIFICA #0#27, 1, "N";
    |CAMPO_MODIFICA #0#28, 1, "N"; |CAMPO_MODIFICA #0#29, 1, "N";
    |CAMPO_MODIFICA #0#30, 1, "N"; |CAMPO_MODIFICA #0#31, 1, "N";
    |CAMPO_MODIFICA #0#32, 1, "N"; |CAMPO_MODIFICA #0#33, 1, "N";
    |PDEFECTO #0, 22,34;
    |PINTA #0#22; |PINTA #0#23; |PINTA #0#24; |PINTA #0#25;
    |PINTA #0#26; |PINTA #0#27; |PINTA #0#28; |PINTA #0#29;
    |PINTA #0#30; |PINTA #0#31; |PINTA #0#32; |PINTA #0#33;
|FINSI;
|FPRC;

|| **********************************************************************
|PROCESO grabalin;
 |PARA para1 = 0; |SI para1 [ 9; |HACIENDO para1 = para1 + 1;
       #1para1 = #11para1;
 |SIGUE;
 |GRABA 020#1n;
 sw_n = 1;

 || ... Actualiza en Alta
 #3#0 = #1#2;
 #3#1 = #1#3;
 #3#2 = "";
 #3#3 = #1#4;
 |LEE 121#3=;
 |SI FSalida = 0;
     #3#12 = #1#0;            || N§ Pagare
     #3#22 = "S";             || liquidado SI
     #3#23 = #3#23 + #1#7;    || ptas. liquidadas
     |GRABA 020#3a;
 |FINSI;
 |LIBERA #3;
|FPRC;

|DEFBUCLE capagtml;
 #11#1;
 ;
 #16#0, 01;
 #16#0, 99;
 ;
 NULL, grabalin;
|FIN;

|PROCESO grabacab;
 |PARA para1 = 0; |SI para1 [ 13; |HACIENDO para1 = para1 + 1;
       #6para1 = #16para1;
 |SIGUE;
 #6#10 = "N";
 |GRABA 020#6n;

 |BUCLE capagtml;
|FPRC;

|DEFBUCLE capagtmc;
 #16#1;
 10;
 000000,   "S";
 contador, "S";
 ;
 NULL, grabacab;
|FIN;

|PROCESO introduc;
 |ABRE #1;
 |ABRE #3;
 |ABRE #6;
 |BUCLE capagtmc;
 |CIERRA #1;
 |CIERRA #3;
 |CIERRA #6;
|FPRC;

|| ****************************************************
|PROCESO altacab;
 |DDEFECTO #16;
 #16#0 = contador;
 |LEE 101#16=;
 |SI FSalida ! 0;
     #16#0  = contador;             || Contador
     #16#1  = cprov;                || Proveedor
     #16#2  = dprov;                || Digito
     #16#3  = #0#4;                 || Cta. Banco
     #16#4  = #0#5;                 || Dig. Banco
     #16#5  = #0#2;                 || Fecha Emision
     |SI #0#6 = "M";
         #16#6  = #0#1;                 || Fecha Vencimiento (manual)
     |SINO;
         #16#6  = cvenc;                || Fecha Vencimiento (automatica)
     |FINSI;
     #16#9  = 0;                    || Importe
     #16#13 = #0#46;                || Contabilizar Efectos a Pagar
     |GRABA 020#16b;
 |FINSI;
 #16#10 = "S";                 || Impresa pero aqui pasar
 #16#9 = #16#9 + #11#7;
 |GRABA 020#16a;
 |LIBERA #16;
|FPRC;

|PROCESO altalin;
 |DDEFECTO #11;
 #11#0 = contador;             || Pagare
 linea = linea + 1;
 #11#1 = linea;                || Linea
 #11#2 = #3#0;                 || Libro
 #11#3 = #3#1;                 || Orden
 #11#4 = #3#3;                 || N§ Pago
 |HAZ leeiva;
 #11#5 = #2#7;                 || Documento
 #11#6 = #3#6;                 || Fecha Emision
 #11#7 = #3#8 - #3#23;         || Importe
 #11#8 = "";                   || Observaciones
 #11#9 = #3#2;                 || Serie
 |GRABA 020#11n;
 |HAZ altacab;     || Suma Cabecera
|FPRC;

|PROCESO pasalo;
 |SI sw_n = 0;
     contador = contador + 1;
     cprov = #3#4;
     dprov = #3#5;
     cvenc = #3#7;
     linea = 0;
     sw_n = 1;
 |FINSI;

 |SI cprov ! #3#4;
     contador = contador + 1;
     cprov = #3#4;
     dprov = #3#5;
     cvenc = #3#7;
     linea = 0;
 |SINO;
     |SI cvenc ! #3#7;|Y #0#6 = "A";
         contador = contador + 1;
         cvenc = #3#7;
         linea = 0;
     |FINSI;
 |FINSI;

 |HAZ altalin;
|FPRC;

|DEFBUCLE camanpag;
 #3#2;
 0, 1, 6, 11, 22, 12, 25;
 d_prov, 0, #0#10,  #0#7,  #0#8,  #0#9, #0#11, "N", 0, 0;
 h_prov, 9, #0#17, #0#14, #0#15, #0#16, #0#18, "N", 0, 0;
 ;
 NULL, pasalo;
|FIN;

|PROCESO esdeuna;
 |PARA para1 = 22; |SI para1 [ 33; |HACIENDO para1 = para1 + 1;
       alfa1 = #0para1; |QBF alfa1;
       |SI alfa1 ! "";
           sw_n = 0;
           d_prov = #0para1;
           h_prov = #0para1;
           |BUCLE camanpag;
       |FINSI;
 |SIGUE;
|FPRC;

|| ***********************************************************************
|PROCESO tipo11; |TIPO 11;
 |SI FSalida = 9;
     |SI #16#10 = " ";
         #16#10 = "S"; |PINTA #16#10;
     |SINO;
         #16#10 = " "; |PINTA #16#10;
     |FINSI;
     |GRABA 020#16a;
     |LIBERA #16;
     |PTEC 802;
 |FINSI;
|FPRC;

|RUTINA min;
 #16#0 = 1;
|FRUT;

|RUTINA max;
 #16#0 = contador;
|FRUT;

|PROCESO vertodos;
 noremesa = 0;
 |C_L #16#0,1,"S";
 |C_L #16#1,1,"S";
 |C_L #16#9,1,"S";
 |C_L #16#10,1,"S";
 |PINPA #0#16;
 |ENTLINEAL #1#16, 1, 4, 22, 0, min, max;
 |CONFI "    SPagares Correctos ? ";
 |SI FSalida ! 0;
     noremesa = 1;
 |FINSI;
|FPRC;

|| ***********************************************************************

|PROCESO pase; |TIPO 3;
 comienza = "q";    |HAZ fichero;  |NOME_DAT #16 fichero;   |DELINDEX #16;
 comienza = "p";    |HAZ fichero;  |NOME_DAT #11 fichero;   |DELINDEX #11;

 contador = #0#0 - 1;

 |ABRE #2;
 |ABRE #16;
 |ABRE #11;
 |SI #0#21 = "S";
     sw_n = 0;
     d_prov = #0#12;
     h_prov = #0#19;
     |BUCLE camanpag;
 |SINO;
     |HAZ esdeuna;
 |FINSI;
 |CIERRA #2;
 |CIERRA #16;
 |CIERRA #11;

 |SI contador ] #0#0;
     |HAZ vertodos;
     |SI noremesa = 0;
         |HAZ introduc;
     |FINSI;
 |SINO;
     |MENAV "    Seleccion vacia ...";
 |FINSI;

 |DELINDEX #16;
 |DELINDEX #11;
|FPRC;

|| ***********************************************************************
|PROCESO leeiva;
 |DDEFECTO #2;
 #2#0  = #3#0;
 #2#2  = #3#1;
 #2#27 = #3#2;
 |LEE 000#2=;
|FPRC;
|| ***********************************************************************
