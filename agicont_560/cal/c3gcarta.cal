|FICHEROS;
     c3gcarta #0;
     caivarep #1;
     caivasop #2;
     caclipro #3;
     cacuenta #5;
|FIN;

|VARIABLES;
     informe = "carta500";
     cuenta = "";
     digito  = 0;
     sw = 0;

     &baseimp = 0;
     &nom = "";
     &nif = "";
     &dir = "";
     &cp = "";
     &pob = "";
     &prov = "";
     &texto = "";
     &text1 = "";
     &cifra = 0;

     emp = "";
     cp1 = "";
     cp2 = "";
     var = "";
     dia = 0;
     mes = "";
     anys = "";
     blanco = "                                                                                    ";

     alfa1 = "";
     alfa2 = "";
     desde = 0;
     hasta = 0;
     modif1 = "";
     modif2 = "";
     sw_con = 0;
     xy = 0;
     i_cm1 = 82;
|FIN;

|INCLUYE acceso_i;
|INCLUYE multie_i;

|| ************************************************************************
|PROCESO tipo8;  |TIPO 8;
  |HAZ inicio;
  any = #30#26;
  |HAZ coloca;
|FPRC;

|PROCESO mayor;
  alfa1 = #0Campo > " ";
  #0Campo = alfa1;
  |PINTA #0Campo;
|FPRC;

|PROCESO mayor1sel;
  |HAZ mayor;
  |C_M #0#77, 1, "S";
  |C_M #0#78, 1, "S";
  |C_M #0#79, 1, "S";
  |C_M #0#80, 1, "S";
  |PARA desde = 1; |SI desde [ 73; |HACIENDO desde = desde + 1;
        |C_M #0desde,1,"S";
  |SIGUE;
  |SI #0#74 = "A"; |ACABA; |FINSI;
  |SI #0#74 = "P";
      |C_M #0#2, 1, "N";
      |C_M #0#79, 1, "N";
      |C_M #0#80, 1, "N";
      xy = 39; hasta = 73;
  |FINSI;
  |SI #0#74 = "C";
      |C_M #0#1, 1, "N";
      |C_M #0#77, 1, "N";
      |C_M #0#78, 1, "N";
      xy = 4; hasta = 38;
  |FINSI;
  |PARA desde = xy;  |SI desde [ hasta;  |HACIENDO desde = desde + 1;
        |CAMPO_MODIFICA #0desde, 1,"N";
  |SIGUE;
|FPRC;

|PROCESO mayorsel;
  |SI Campo =  4; |Y #0#74 = "C"; |ACABA; |FINSI;
  |SI Campo = 39; |Y #0#74 = "P"; |ACABA; |FINSI;
  |HAZ mayor;

  modif1 = "N";    |SI #0Campo = "N"; modif1 = "S"; |FINSI;
  modif2 = #0Campo;

  |SI Campo = 4;
      |C_M #0#5,1,modif2;
      |C_M #0#6,1,modif2;
      |SI modif2 = "N";
          #0#5 = 0; |PINTA #0#5; #0#6 = 0; |PINTA #0#6;
      |FINSI;
  |SINO;
      |C_M #0#40,1,modif2;
      |C_M #0#41,1,modif2;
      |SI modif2 = "N";
          #0#40 = 0; |PINTA #0#40; #0#41 = 0; |PINTA #0#41;
      |FINSI;
  |FINSI;

  hasta = Campo + 34;
  |PARA desde = Campo + 3;  |SI desde [ hasta;  |HACIENDO desde = desde + 1;
        |CAMPO_MODIFICA #0desde, 1,modif1;
        |SI modif1 = "N";
            #0desde = 0; |PINTA #0desde;
        |FINSI;
  |SIGUE;
|FPRC;

|PROCESO saltacon1;
  |SI FSalida = 2; |ACABA; |FINSI;
  xy = Campo - 3;
  |SI xy =  4; |Y #0#74 = "C"; |ACABA; |FINSI;
  |SI xy = 39; |Y #0#74 = "P"; |ACABA; |FINSI;
  |SI #0xy ! "N"; |ACABA; |FINSI;
  |SI #0Campo = 0;
      |MENAV "    Error!!! Introduzca como minimo  UN CONCEPTO ";
      |ERROR;
  |FINSI;
|FPRC;

|PROCESO saltacon;
  hasta = 73;  xy = 39;
  |SI Campo < 39; hasta = 38; xy = 4; |FINSI;
  |SI xy =  4; |Y #0#74 = "C"; |ACABA; |FINSI;
  |SI xy = 39; |Y #0#74 = "P"; |ACABA; |FINSI;

  |SI #0xy ! "N"; |ACABA; |FINSI;  || ha pedido desde/hasta

  |PARA desde = Campo + 1;  |SI desde [ hasta;  |HACIENDO desde = desde + 1;
        |SI #0Campo = 0;
            |CAMPO_MODIFICA #0desde, 1, "N";
            #0desde = 0;
        |SINO;
            |CAMPO_MODIFICA #0desde, 1, "S";
        |FINSI;
  |SIGUE;
|FPRC;

|| ***************************************************************************
|PROCESO sel_multi;
  |SI #30#26 ! any;  |ACABA; |FINSI; || SI es necesario Ejercicio
  |SI #30#21 = "I";  |ACABA; |FINSI;
  leidos = 1;
|FPRC;

|| ***********************************************************************
|| ---------- Seleccion DESDE / HASTA Empresa.
|PROCESO esdesd1;
  alfa1 = #30#2;
  alfa2 = #30#3;
  alfa1 = "00000" + alfa1;   alfa1 = alfa1 % -105;
  alfa2 = "0"   + alfa2;   alfa2 = alfa2 % -101;
  dirfich0 = dir_fich + alfa1 + alfa2 + "/";

  |SI #30#26 ! any;  |ACABA; |FINSI;
  |SI #30#21 = "I";  |ACABA; |FINSI;

  |HAZ haztodo;
|FPRC;

|DEFBUCLE esdesde;
 #30#1;
 ;
 d_empres, d_period;
 h_empres, h_period;
 e;
 NULL,esdesd1;
|FIN;

|| ---------- Seleccion en parrilla.
|PROCESO esunaauna;
  |PARA xx = i_cm6; |SI xx [ i_cm7; |HACIENDO xx = xx + 1;
        |SI #0xx = 0;  |SAL;  |FINSI;
        yy = xx + 20;
        alfa1 = #0xx;
        alfa2 = #0yy;

        || ... lee empresa
           |ABRE #30;
           |DDEFECTO #30;
           #30#2 = #0xx;
           #30#3 = #0yy;
           |LEE 011#30=;
           |CIERRA #30;
           |SI #30#26 ! any;  |VETE otramas; |FINSI;
           |SI #30#21 = "I";  |VETE otramas; |FINSI;
        || ...................

        alfa1 = "00000" + alfa1;   alfa1 = alfa1 % -105;
        alfa2 = "0"     + alfa2;   alfa2 = alfa2 % -101;
        dirfich0 = dir_fich + alfa1 + alfa2 + "/";
        |HAZ haztodo;

 |ET otramas;
  |SIGUE;
|FPRC;

|| ************************************************************************
|PROCESO prov500;

  |HAZ selconcom;
  |SI sw_con = 0; |ACABA; |FINSI;

  |SI sw = 0;
      baseimp = 0;
      cuenta = #2#4;
      digito = #2#5;
      sw = 1;
  |FINSI;

  |SI cuenta ! #2#4; |O digito ! #2#5;  ||Si hay un cambio de cuenta
      |SI baseimp ] cifra;
          |HAZ datosprov;
          |PRINT;
          |PIEINF;
      |FINSI;
      baseimp = 0;
      cuenta = #2#4;
      digito  = #2#5;
  |FINSI;

  baseimp = baseimp + #2#26;
|FPRC;

|PROCESO datosprov;
  #3#23 = "P";
  #3#10 = cuenta;
  #3#11 = digito;
  |LEE 001#3=;
  |SI FSalida ! 0;
      |DDEFECTO #3;
      |HAZ lee_cta;
      #3#1 = #5#2;
  |FINSI;
  dir = #3#2;
  pob = #3#6;
  nom = #3#1;
  nif = #3#9;
  cp1 = #3#3;
  cp2 = #3#4;
  cp = ("00" + cp1) % -102 + ("000" + cp2) % -103;
  prov = #3#5;
|FPRC;

|PROCESO finpro;
 |SI baseimp ] cifra;
     |HAZ datosprov;
     |PRINT;
     |PIEINF;
 |FINSI;
|FPRC;

|DEFBUCLE prov;
 #2#2;
 ;
 #0#77, 0,  0, "  ", 000000;
 #0#78, 9, 99, "zz", 999999;
 e;
 NULL , prov500, NULL , NULL , finpro;
|FIN;


|| ----------------------------------------------------------------------
|PROCESO cli500;

  |HAZ selconven;
  |SI sw_con = 0; |ACABA; |FINSI;

  |SI sw = 0;
      baseimp = 0;
      cuenta = #1#4;
      digito  = #1#5;
      sw    = 1;
  |FINSI;

  |SI cuenta ! #1#4; |O digito ! #1#5;  ||Si hay un cambio de cuenta

      |SI baseimp ] cifra;
          |HAZ datoscli;
          |PRINT;
          |PIEINF;
      |FINSI;
      baseimp = 0;
      cuenta   = #1#4;
      digito    = #1#5;
  |FINSI;

  baseimp = baseimp + #1#26;
|FPRC;

|PROCESO datoscli;
  #3#23 = "C";
  #3#10 = cuenta;
  #3#11 = digito;
  |LEE 001#3=;
  |SI FSalida ! 0;
      |DDEFECTO #3;
      |HAZ lee_cta;
      #3#1 = #5#2;
  |FINSI;
  dir = #3#2;
  pob = #3#6;
  nom = #3#1;
  nif = #3#9;
  cp1 = #3#3;
  cp2 = #3#4;
  cp = ("00" + cp1) % -102 + ("000" + cp2) % -103;
  prov = #3#5;
|FPRC;

|PROCESO lee_cta;
 |ABRE #5;
 |DDEFECTO #5;
 #5#0 = cuenta;
 #5#1 = digito;
 |LEE 001#5=;
 |CIERRA #5;
|FPRC;

|PROCESO fincli;
  |SI baseimp ] cifra;
      |HAZ datoscli;
      |PRINT;
      |PIEINF;
  |FINSI;
|FPRC;

|DEFBUCLE cli;
 #1#2;
 ;
 #0#79, 0,  0, "  ", 000000;
 #0#80, 9, 99, "zz", 999999;
 e;
 NULL , cli500, NULL , NULL , fincli;
|FIN;

|| ********************************************************************
|PROCESO tipo3; |TIPO 3;
  |IMPRE 0;
  |SI FSalida ! 0;  |ERROR;  |ACABA;  |FINSI;
  |INFOR informe;
  |SI FSalida ! 0;  |FINIMP;  |ERROR;  |ACABA;  |FINSI;

  |SI #0i_cm1 = "S";
      |BUCLE esdesde;
  |SINO;
      |HAZ esunaauna;
  |FINSI;
  |FININF;
  |FINIMP;
|FPRC;

|| ******************************** CALCULO
|PROCESO haztodo;
  enEmpresa = #canempre#2;
  enPeriodo = #canempre#3;
  swOperacion = 0;
  |SUB_EJECUTA "camoneda";

  |PATH_DAT #1 dirfich0;
  |PATH_DAT #2 dirfich0;
  |PATH_DAT #3 dirfich0;
  |PATH_DAT #5 dirfich0;

  |HAZ fechas;
  |ABRE #3;
  |SI #0#74 ! "C";
       cifra = #0#1;
       sw = 0;
       |BUCLE prov;
  |FINSI;
  |SI #0#74 ! "P";
       cifra = #0#2;
       sw = 0;
       |BUCLE cli;
  |FINSI;
  |CIERRA #3;
|FPRC;

|| --------------------------------------------
|PROCESO fechas;
     var = #0#75 % 402;
     dia = var;
     |SI dia = 1;   mes = "Enero";      |FINSI;
     |SI dia = 2;   mes = "Febrero";    |FINSI;
     |SI dia = 3;   mes = "Marzo";      |FINSI;
     |SI dia = 4;   mes = "Abril";      |FINSI;
     |SI dia = 5;   mes = "Mayo";       |FINSI;
     |SI dia = 6;   mes = "Junio";      |FINSI;
     |SI dia = 7;   mes = "Julio";      |FINSI;
     |SI dia = 8;   mes = "Agosto";     |FINSI;
     |SI dia = 9;   mes = "Septiempre"; |FINSI;
     |SI dia = 10;  mes = "Octubre";    |FINSI;
     |SI dia = 11;  mes = "Noviembre";  |FINSI;
     |SI dia = 12;  mes = "Diciembre";  |FINSI;
     var = #0#75 % 102;
     dia = var;
     var = #0#75 % 704;
     anys = var;
     emp = #0#76;
     |QBF emp;
     texto = blanco + blanco + emp + ", " + dia + " de " + mes + " de " + anys;
     |QBF texto;
     texto = texto % -160;
     text1 = blanco + blanco + #30#4;
     |QBF text1;
     text1 = text1 % -160;
|FPRC;

|| ***********Seleccion de Conceptos
|PROCESO selconcom;
  sw_con = 0;
|| .................................... Seleccion de Compras
  |SI #0#4 = "S";
      |SI #2#6 ] #0#5;  |Y #2#6 [ #0#6;
          sw_con = 1;
      |FINSI;
  |SINO;
      |PARA desde = 7;  |SI desde [ 38;  |HACIENDO desde = desde + 1;
            |SI #0desde = 0;  |SAL;  |FINSI;
            |SI #2#6 = #0desde;
                sw_con = 1;
                |SAL;
            |FINSI;
      |SIGUE;
  |FINSI;
|FPRC;

|PROCESO selconven;
  sw_con = 0;
|| .................................... Seleccion de Ventas
  |SI #0#39 = "S";
      |SI #1#6 ] #0#40;  |Y #1#6 [ #0#41;
          sw_con = 1;
      |FINSI;
  |SINO;
      |PARA desde = 42;  |SI desde [ 73;  |HACIENDO desde = desde + 1;
            |SI #0desde = 0;  |SAL;  |FINSI;
            |SI #1#6 = #0desde;
                sw_con = 1;
                |SAL;
            |FINSI;
      |SIGUE;
  |FINSI;
|FPRC;
