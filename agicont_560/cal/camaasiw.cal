|FICHEROS;
  camaasiw #0;
  caivasop #1;
  caivaasi #2;
  calibros #10;        || libros iva
|FIN;

|VARIABLES;
  &entrada = 1;
  &ext1 = "";          || fecha
  &ext2 = "";          || libro
  &ext3 = "";          || FSalida
  &ext4 = "";          || (inhibida)
  &ext5 = "";          || factura
  nume1 = 0;
|FIN;

|PROGRAMA;
  |ABRE #2;
  |LEE 040#2p;
  |SI FSalida = 0;
      |SI #2#25 = "N";  |Y #2#26 = "N";
          |HAZ nolosaques;
          |CIERRA #2;
          |ACABA;
      |FINSI;
  |FINSI;
  |CIERRA #2;
  |SI #2#25 = "N";
      |CAMPO_MODIFICA #0#0,1,"N";
  |FINSI;
  |SI #2#26 = "N";
      |CAMPO_MODIFICA #0#2,1,"N";
  |FINSI;
  |CABEZA "Iva Soportado";
  |DDEFECTO #0;
  |PINPA #0#0;
  #0#2 = ext1;         || fecha del asiento por defecto
  |SI #2#25 = "N";
      |HAZ ultima;
  |FINSI;
  |PINDA #0#0;
  |ENDATOS #1#0;
  ext1 = #0#2;         || fecha
  ext3 = FSalida;
  ext5 = #0#0;         || orden
|FPRO;

|PROCESO nolosaques;
  |DDEFECTO #0;
  ext3 = 0;
  ext4 = #0#1;
  |HAZ ultima;
  ext5 = #0#0;
|FPRC;

|PROCESO ultima; |TIPO 7;
  |ABRE #10;
  |DDEFECTO #10;
  #10#0 = ext2;
  |LEE 001#10=;
  |CIERRA #10;

  #0#0 = #10#3;
  |DDEFECTO #1;
  |ABRE #1;
  #1#0 = ext2;    || libro
  #1#2 = 999999;  || orden
  |LEE 001#1];
  |SI FSalida ! 0;
      |LEE 001#1u;
      |SI FSalida = 0; |Y #1#0 = ext2;
          #0#0 = #1#2 + #10#3;
      |SINO;
          |HAZ anterior;
      |FINSI;
  |SINO;
      |HAZ anterior;
  |FINSI;
  |CIERRA #1;

|ET sientrada;
  nume1 = #0#0 $ #10#3;
  |SI nume1 ! 0;
      #0#0 = #0#0 - 1;
      |VETE sientrada;
  |FINSI;
  ext5 = #0#0;
|FPRC;

|PROCESO anterior;
  |LEE 001#1a;
  |SI FSalida = 0; |Y #1#0 = ext2;
      #0#0 = #1#2 + #10#3;
  |FINSI;
|FPRC;

|PROCESO noexiste;
  |ABRE #1;
  #1#0 = ext2;
  #1#2 = #0#0;
  |LEE 011#1=;
  |SI FSalida = 0;
      |MENAV "     Atencion Numero de Orden Existente!!";
      |CIERRA #1;
      #0#0 = ext5;
      |PINTA #0#0;
      |ERROR;
  |FINSI;
  |CIERRA #1;
|FPRC;
