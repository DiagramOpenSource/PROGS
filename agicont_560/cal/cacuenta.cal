          || TENED EN CUENTA QUE SI SE MODIFICAN CAMPOS EN EL 'cacuenta.def'
          || HAY QUE HACER LO MISMO EN EL 'plancont.def'
|FICHEROS;
  cacuenta #0;    || cuentas
  cacuebal #5;    || cuentas de balance
  caparbal #6;    || partidas balances
  caparexp #7;    || partidas explotacion
  caagrupa #8;    || agrupaciones
  caepigra #9;    || epigrafes
  camaasil #10;   || lineas
  cactrlal #11;   || control altas en otras empresas.
  caclipro #21;   || Clientes y Proveedores
|FIN;

|VARIABLES;
  men1     = "     NO EXISTE EMPRESA !";
  men2     = "     LONGITUD DE CUENTA FUERA DE NIVEL !";
  men3     = "     CUENTA ERRONEA !!";
  error    = "     CLAVE EXISTENTE !";
  valida   = "     Conforme S/N.:";
  mascara  = "";
  digito2  = "  ";
  num      = 0;
  sw       = 0;
  mes      = "                ";
  me       = 0;
  cta      = "            ";
  ctanum   = 0;
  ctaalf   = " ";
  longalf  = " ";
  longnum  = " ";
  nivel    = 0;
  codcli   = 0;
  codpr    = 0;
  nompro   = "";
  mensa1   = "     No esta definida esta cuenta en Cuentas de Balance";
  uno = "";
  dos = "";
  nada = "";
  &entrada = 0;
  cuen1 = "";
  cuen2 = "";
  cuen3 = "";
  desdec = "";
  hastac = "";
  &raf_cue = "";
  &raf_dig = 0;
  &raf_des = "";
  &raf_sw = 0;
  &tipoalta = "";

  &raf_pase = "";
  &raf_blo = 0;
  &raf_tipo = "";
  &raf_agru = 0;
  &raf_epi = 0;
  &raf_par = 0;
  &raf_periodo = 0;
  mesdes = "";
  meshas = "";
  xx = 0;
  yy = 0;
  operac = 0;
  alfa1  = "";
  alfa2  = "";
  swaltas = 0;
  doce = "            ";
  swraf = 0;
|FIN;

|INCLUYE acces3_i;
|INCLUYE punto2_i;

*****************************************
*****************************************

|PROCESO bajano; |TIPO 1;
  yy = 0;
  operac = (FEntrada / 100) ! 0;
  |SI operac ! 3; |ACABA; |FINSI;
  |PARA xx = 9; |SI xx [ 53; |HACIENDO xx = xx + 1;
        |SI #0xx ! 0;
            yy = 1;
            |SAL;
        |FINSI;
  |SIGUE;
  |SI yy = 0; |Y #0#3 = "N"; |HAZ bclipro; |ACABA; |FINSI;
  |SI yy = 0;
      |ABRE #10;
      |SELECT #3#10;
      #10#4 = #0#0;
      #10#5 = #0#1;
      #10#0 = 0;
      #10#1 = "00.00.0000";
      #10#2 = 0;
      |LEE 000#10];
      |SI FSalida = 0;
          |SI #10#4 = #0#0; |Y #10#5 = #0#1;
              yy = 1;
          |FINSI;
      |FINSI;
      |SELECT #1#10;
      |CIERRA #10;
   |FINSI;

  |SI yy = 0; |HAZ bclipro; |ACABA; |FINSI;

  |AVISO;
  |CONFI "2400NCuenta con Movimientos. Se dispone a BORRARLA. ¨Esta seguro? ";
  |SI FSalida ! 0;
      |ERROR;
  |SINO;
      |HAZ bclipro;
  |FINSI;
|FPRC;

|PROCESO bclipro;
 alfa1 = #0#0; alfa1 = alfa1 % 101;
 |SI alfa1 ! "4"; |Y alfa1 ! "5";
     |ACABA;
 |FINSI;
 |ABRE #21;
 #21#23 = "C";
 #21#10 = #0#0;
 #21#11 = #0#1;
 |LEE 101#21=;
 |SI FSalida = 0;
     |BORRA 020#21a;
 |SINO;
     #21#23 = "P";
     #21#10 = #0#0;
     #21#11 = #0#1;
     |LEE 101#21=;
     |SI FSalida = 0;
         |BORRA 020#21a;
     |FINSI;
 |FINSI;
 |LIBERA #21;
 |CIERRA #21;
|FPRC;

|PROCESO mayor;
  #0Campo = #0Campo > " ";
  |PINTA #0Campo;
|FPRC;

|PROCESO filtro_cta; |TIPO 0;
 |SI FSalida = 2; |ACABA; |FINSI;
 |SI #0#4 = 0;
     |SI #0#5 = "D"; |O #0#5 = "H";
         |ERROR;
     |FINSI;
 |SINO;
     |SI #0#5 = "A"; |O #0#5 = "P";
         |ERROR;
     |FINSI;
 |FINSI;
|FPRC;

|PROCESO alentrar;  |TIPO 8;
  |HAZ inicio3;
  |PARA num = 1;|SI num [ 12;|HACIENDO num = num + 1;
        |SI #30#11 = num;
            |HAZ pinta;
            mesdes = mes;
            |SAL;
        |FINSI;
  |SIGUE;
  |PARA num = 12;|SI num [ 1;|HACIENDO num = num - 1;
        |SI #30#12 = num;
            |HAZ pinta;
            meshas = mes;
            |SAL;
        |FINSI;
  |SIGUE;
  raf_periodo = #30#3;
  |ABRE #11;
  |DDEFECTO #11;
  |LEE 101#11p;
  |SI FSalida ! 0; swaltas = 1; |FINSI;
  |LIBERA #11;
  |CIERRA #11;
|FPRC;

|PROCESO pintas2;
  |ABRE #8;
   #8#0 = #0#5;    || tipo
   #8#1 = #0#6;    || agrupacion
  |LEE 001#8=;
  |SI FSalida ! 0;  |DDEFECTO #8;  |FINSI;
  |ATRI I;
  |PINTA 1321, #8#2;
  |ATRI 0;
  |CIERRA #8;

  |ABRE #9;
  #9#0 = #0#5;    || tipo
  #9#1 = #0#6;    || agrupacion
  #9#3 = #0#7;    || epigrafe
  |LEE 001#9=;
  |SI FSalida ! 0;  |DDEFECTO #9;  |FINSI;
  |ATRI I;
  |PINTA 1421, #9#4;
  |ATRI 0;
  |CIERRA #9;
|FPRC;

|PROCESO pintas; |TIPO 14;
  |SI #0#4 = 0;
      |ABRE #6;
      #6#0 = #0#5;     || tipo
      #6#1 = #0#6;     || agrupacion
      #6#3 = #0#7;     || epigrafe
      #6#5 = #0#8;     || partida
      |LEE 001#6=;
      |SI FSalida ! 0;  |DDEFECTO #6;  |FINSI;
      |ATRI I;
      |PINTA 1521, #6#6;
      |ATRI 0;
      |CIERRA #6;
  |SINO;
      |ABRE #7;
      #7#0 = #0#4;     || bloque
      #7#1 = #0#5;     || tipo
      #7#2 = #0#6;     || agrupacion
      #7#4 = #0#7;     || epigrafe
      #7#6 = #0#8;     || partida
      |LEE 001#7=;
      |SI FSalida ! 0;  |DDEFECTO #7;  |FINSI;
      |ATRI I;
      |PINTA 1521, #7#7;
      |ATRI 0;
      |CIERRA #7;
  |FINSI;
  |HAZ pintas2;
|FPRC;

|PROCESO nivel; |TIPO 0;
  p_alfa1 = #0Campo;
  |HAZ punto2;
  |SI p_error ! 0;  |ERROR;  |ACABA;  |FINSI;
  |SI entrada = 1;  |Y #0#0 = "            ";  |ACABA;  |FINSI;
  ||SI entrada = 1;  |O #0#0 = "            ";  |ACABA;  |FINSI;
  |SI FEntrada = 402;  |ACABA;  |FINSI;
  |HAZ defect;
  cta = #0#0;
  |QBF cta;
  nada = cta % A101;
  |SI nada ! "0";
      ctanum = cta;
      ctaalf = ctanum;
      longnum = ctaalf % A0;
  |SINO;
      ctanum = 1;
      ctaalf = cta;
      longnum = ctaalf % A0;
  |FINSI;
  longalf = cta    % A0;
  |SI longnum ! longalf;|O ctanum = 0;
      |MENAV men3;
      |PINTA #0#0;
      |ERROR;
      |ACABA;
  |FINSI;

  nivel= 0;
  sw = 0;
  |SI longnum = dig4; sw = 1; nivel = 1; |FINSI;
  |SI longnum = dig5; sw = 1; nivel = 2; |FINSI;
  |SI longnum = dig6; sw = 1; nivel = 3; |FINSI;
  |SI longnum = dig7; sw = 1; nivel = 4; |FINSI;
  |SI longnum = dig8; sw = 1; nivel = 5; |FINSI;
  |SI longnum = dig9; sw = 1; nivel = 6; |FINSI;
  |SI sw = 0;
      |MENAV men2;
      |PINTA #0#0;
      |ERROR;
      |ACABA;
  |FINSI;
                         ||***** 1a Clave;
                         ||***** nivel = nivel de la cta;
  #0#1 = nivel;
|| la 3a clave esta formada por la cta + zzzz y el campo fantasma del nivel;
  mascara = #0#0;
  |QBF mascara;
  mascara = mascara +"zzzzzzzzzzzz";
  mascara = mascara % 112;
  #0#54    = mascara;
  #0#55    = nivel;
  #0#56 = mesdes;
  #0#57 = meshas;
  |SI #0#1 = #30#13;
      #0#3 = "S";
  |SINO;
      #0#3 = "N";
  |FINSI;

  |PINTA #0#3;
|FPRC;

|PROCESO altas;|TIPO 0;
  cta = #0#0;
  digito2 = cta % 102;
  |SI FSalida ! 9; |ACABA; |FINSI;
  raf_cue = #0#0;
  raf_dig = #0#1;
  raf_des = #0#2;
  raf_sw = 0;
  |SI digito2 = "57";  |Y  #30#23 = "S";
      |SUB_EJECUTA "camanba1.tab";
      |VETE salir;
  |FINSI;
  |SI digito2 = "43";  |O digito2 = "44";
      |SI #30#21 ! "N";
          tipoalta = "C";
          |SUB_EJECUTA "caclipr1.tab";
      |FINSI;
  |SINO;
      |SI digito2 = "40";  |O digito2 = "41";  |O digito2 = "52";
          |SI #30#22 = "S";
              tipoalta = "P";
              |SUB_EJECUTA "caclipr1.tab";
          |FINSI;
      |SINO;
           tipoalta = "X";
           |SUB_EJECUTA "caclipr1.tab";
      |FINSI;
  |FINSI;
  |ET salir;
  |SI raf_sw = 1;
      #0#2 = raf_des;
      |PINTA #0#2;
      raf_sw = 0;
  |FINSI;
|FPRC;


|PROCESO pinta;
  |SI num =  1; mes = "Enero";     |ACABA;  |FINSI;
  |SI num = 12; mes = "Diciembre"; |ACABA;  |FINSI;
  |SI num =  2; mes = "Febrero";   |ACABA;  |FINSI;
  |SI num =  3; mes = "Marzo";     |ACABA;  |FINSI;
  |SI num =  4; mes = "Abril";     |ACABA;  |FINSI;
  |SI num =  5; mes = "Mayo";      |ACABA;  |FINSI;
  |SI num =  6; mes = "Junio";     |ACABA;  |FINSI;
  |SI num =  7; mes = "Julio";     |ACABA;  |FINSI;
  |SI num =  8; mes = "Agosto";    |ACABA;  |FINSI;
  |SI num =  9; mes = "Septiembre";|ACABA;  |FINSI;
  |SI num = 10; mes = "Octubre";   |ACABA;  |FINSI;
  |SI num = 11; mes = "Noviembre"; |ACABA;  |FINSI;
|FPRC;


|PROCESO defect;
  |ABRE #5;
  uno = #0#0;
  |QBF uno;
  dos = uno + "000000000000";
  uno = dos % A101;
  #5#0 = uno;
  |LEE 040#5=;
  |SI FSalida = 0;  |HAZ saca;  |ACABA;  |FINSI;
  uno = dos % A102;
  #5#0 = uno;
  |LEE 040#5=;
  |SI FSalida = 0;  |HAZ saca;  |ACABA;  |FINSI;
  uno = dos % A103;
  #5#0 = uno;
  |LEE 040#5=;
  |SI FSalida = 0;  |HAZ saca;  |ACABA;  |FINSI;
  uno = dos % A104;
  #5#0 = uno;
  |LEE 040#5=;
  |SI FSalida = 0;
      |HAZ saca;
  |SINO;
      |MENAV mensa1;
      |CIERRA #5;
  |FINSI;
|FPRC;

|PROCESO saca;
  #0#4 = #5#1;
  #0#5 = #5#2;
  #0#6 = #5#3;
  #0#7 = #5#4;
  #0#8 = #5#5;
  |PINTA #0#4;
  |PINTA #0#5;
  |PINTA #0#6;
  |PINTA #0#7;
  |PINTA #0#8;
  |SI entrada = 0;
      |HAZ pintas;
      |HAZ pintas2;
  |FINSI;
  |CIERRA #5;            ||CIERRO AQUI el FICHERO PARA SALIRME
|FPRC;

|PROCESO al_emp; |TIPO 30;

 operac = (FEntrada / 100) ! 0;
 |SI operac ! 1; |ACABA; |FINSI;

  raf_periodo = #30#3;
  |ABRE #11;
  |DDEFECTO #11;
  |LEE 101#11p;
  |SI FSalida ! 0; swaltas = 1; |FINSI;
  |LIBERA #11;
  |CIERRA #11;

 |SI swaltas = 1; |ACABA; |FINSI;

 swraf = 1;
 alfa1 = #11#1; |QBF alfa1;
 alfa2 = #11#2; |QBF alfa2;
 |SI alfa1 ! ""; |O alfa2 ! ""; swraf = 0; |FINSI;
 alfa1 = #11#3; |QBF alfa1;
 alfa2 = #11#4; |QBF alfa2;
 |SI alfa1 ! ""; |O alfa2 ! ""; swraf = 0; |FINSI;
 alfa1 = #11#5; |QBF alfa1;
 alfa2 = #11#6; |QBF alfa2;
 |SI alfa1 ! ""; |O alfa2 ! ""; swraf = 0; |FINSI;

 |SI swraf = 1; |ACABA; |FINSI;

 swraf = 1;
 |SI #0#0 ] #11#1; |Y #0#0 [ #11#2; swraf = 0; |FINSI;
 |SI #0#0 ] #11#3; |Y #0#0 [ #11#4; swraf = 0; |FINSI;
 |SI #0#0 ] #11#5; |Y #0#0 [ #11#6; swraf = 0; |FINSI;

 |SI swraf = 1; |ACABA; |FINSI;

 |AVISO;
 |CONFI "2400S¨ Dar de Alta la Cuenta en las Otras Empresas ? ";
 |SI FSalida = 0;
     raf_cue  = #0#0;
     raf_dig  = #0#1;
     raf_des  = #0#2;
     raf_pase = #0#3;
     raf_blo  = #0#4;
     raf_tipo = #0#5;
     raf_agru = #0#6;
     raf_epi  = #0#7;
     raf_par  = #0#8;

     |CIERRAT #0;
     |SUB_EJECUTA "caltacta.tab";
     |ABRET #0;
 |FINSI;
|FPRC;
