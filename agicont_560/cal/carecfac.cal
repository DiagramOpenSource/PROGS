|FICHEROS;
    carecfac #0;
    camaasil #1;    || apuntes lins.
    caivatm2 #2;    || Temporal

    caivarep #3;    || IVA repercutido
    caivasop #4;    || IVA soportado
    calibros #5;    || Libros de IVA.
    cacuenta #6;    || Cuentas
    caclipro #7;    || Clientes/Proveedores
|FIN;

|VARIABLES;
  swdefe = 0;
  alfa1 = "";
  alfa2 = "";
  sw  = 0;
  swc = 0;
  salid2 = 0;
  salida = 0;
  mensa1 = "    ERROR ESTA FECHA NO CORRESPONDE AL EJERCICIO";

  tipoRS = "";
  trim1 = @;
  trim2 = @;
  trim3 = @;
  i_perio = 0;
  i_nom   = "";
  i_cif   = "";
  i_acum  = "";
  i_impo  = 0;
  puntero = 0;

  para1 = 0;
  nume1 = 0;
  nume2 = 0;
  nume3 = 0;
  cam23 = "";
  cam24 = "";
  cam28 = "";
  cam48 = "";
  cam62 = 0;
  iva1 = 0;
  iva2 = 0;
  iva3 = 0;
  iva4 = 0;
  iva5 = 0;
  req1 = 0;
  req2 = 0;
  req3 = 0;
  req4 = 0;
  req5 = 0;
|FIN;

|INCLUYE acceso_i;
|INCLUYE tempo_i;
____________________________________/ CALCULOS DESDE PANTALLA

|PROCESO mayus; |TIPO 0;
  alfa1 = #0Campo > " ";
  #0Campo = alfa1;
  |PINTA #0Campo;
|FPRC;

|PROCESO def_fec; |TIPO 7;
 |SI sw = 0;
    #0#2 = fec1;    |PINTA #0#2;
    #0#3 = fec2;    |PINTA #0#3;
    sw = 1;
 |FINSI;
|FPRC;

|PROCESO fecha; | TIPO 0;   || Desde/hasta fecha
  |SI #0Campo < fec1; |O #0Campo > fec2;
      |MENAV mensa1;
      |ERROR;
  |FINSI;
|FPRC;

|PROCESO diarioh; | TIPO 0;   ||  hasta diario
  |SI #0#1 < #0#0;  |ERROR;  |FINSI;
|FPRC;

|PROCESO fechah; | TIPO 0;   ||  hasta fecha
  |SI #0#3 < #0#2;  |ERROR;  |FINSI;
|FPRC;

|PROCESO documh; | TIPO 0;   ||  hasta documento
  |SI #0#5 < #0#4;  |ERROR;  |FINSI;
|FPRC;

|| ***********************************************************************

|PROCESO ivatitular;
  |SI #1#19 = " ";
      |SI swdefe = 1; |ACABA; |FINSI;
  |FINSI;
  #2#03 = #1#1;    || fecha
  #2#04 = #1#4;    || cuenta
  #2#05 = #1#5;    || dig.
  #2#06 = #1#6;    || concepto
  #2#07 = #1#7;    || descripcion
  #2#23 = "S";
  #2#24 = "N";
||   #2#26 = 0;
  #2#28 = "N";
  |HAZ periodo;
  #2#44 = i_perio;
  #2#45 = #1#0;
  #2#46 = #1#1;
  #2#47 = #1#2;
  #2#48 = "N";
  #2#49 = #1#21;
  |HAZ lee_titular;
  #2#50 = i_nom;      || nombre
  #2#51 = i_cif;      || cif
  |SI #1#19 = "F"; swdefe = 1; |FINSI;
|FPRC;

|PROCESO ivaimp;
 i_acum = #1#19;
 i_impo = #1#9;
 |SI i_acum = "b"; i_acum = "B"; i_impo = - i_impo; |FINSI;
 |SI i_acum = "i"; i_acum = "I"; i_impo = - i_impo; |FINSI;
 |SI i_acum = "r"; i_acum = "R"; i_impo = - i_impo; |FINSI;

    |SI i_acum = "B";  puntero =  7;  |FINSI;
    |SI i_acum = "I";  puntero = 13;  |FINSI;
    |SI i_acum = "R";  puntero = 19;  |FINSI;
    |SI #1#20 ] 1;|Y #1#20 [ 5;
        |SI #1#20 > 3;|Y i_acum = "B";  puntero = puntero + 41;  |FINSI;
        |SI #1#20 > 3;|Y i_acum = "I";  puntero = puntero + 39;  |FINSI;
        |SI #1#20 > 3;|Y i_acum = "R";  puntero = puntero + 37;  |FINSI;
        puntero = puntero + #1#20;
    |SINO;
        puntero = puntero + 1;
    |FINSI;
    #2puntero = #2puntero + i_impo;
|FPRC;

|PROCESO grabaiva;
  #2#0  = #1#13;
  #2#2  = #1#14;
  #2#27 = #1#15;
  |LEE 101#2=;
  salida = FSalida;
  |SI FSalida ! 0;
      swdefe = 0;
      |DDEFECTO #2;
      #2#0  = #1#13;
      #2#2  = #1#14;
      #2#27 = #1#15;
      |HAZ lee_libro;
      #2#1  = #5#1;
  |FINSI;

  |SI #1#19 = "F"; |O #1#19 = " ";
      |HAZ ivatitular; || R. Cuenta
  |SINO;
      |HAZ ivaimp;     || R. Bases/IVA/Rec
  |FINSI;

  |SI salida = 0;  |GRABA 020#2a;  |FINSI;
  |SI salida ! 0;  |GRABA 020#2n;  swc = swc + 1; |FINSI;
  |LIBERA #2;
|FPRC;

|DEFBUCLE lee_diario;
 #1#1;
 12,13,14;
 #0#0, #0#2, #0#4,    1, tipoRS, #0#7, #0#9;
 #0#1, #0#3, #0#5, 9999, tipoRS, #0#8, #0#10;
 e;
 NULL, grabaiva;
|FIN;

|| *************************************************************************

|PROCESO lee_libro;
    #5#0 = #2#0;
    |LEE 001#5=;
    |SI FSalida ! 0;
        #5#1 = "<LIBRO INEXISTENTE>";
    |FINSI;
|FPRC;

|PROCESO periodo;
   alfa1 = #2#3;
   alfa1 = alfa1 % -104;   || saca a¤o
   alfa2 = "31.03." + alfa1;  trim1 = alfa2;  || primer trimestre
   alfa2 = "30.06." + alfa1;  trim2 = alfa2;  || segundo trimestre
   alfa2 = "30.09." + alfa1;  trim3 = alfa2;  || tercer trimestre
   |SI #2#3 [ trim1;                  i_perio = 1;  |FINSI;
   |SI #2#3 > trim1;|Y #2#3 [ trim2;  i_perio = 2;  |FINSI;
   |SI #2#3 > trim2;|Y #2#3 [ trim3;  i_perio = 3;  |FINSI;
   |SI #2#3 > trim3;                  i_perio = 4;  |FINSI;
|FPRC;

|PROCESO lee_titular;
  i_nom = "";
  i_cif = "";
  |SI tipoRS = "R"; #7#23 = "C"; |FINSI;
  |SI tipoRS = "S"; #7#23 = "P"; |FINSI;
  #7#10 = #2#4;
  #7#11 = #2#5;
  |LEE 001#7=;
  |SI FSalida = 0;
      i_nom = #7#1;      || nombre de la ficha
      i_cif = #7#9;
  |SINO;
      #6#0 = #2#4;
      #6#1 = #2#5;
      |LEE 001#6=;
      |SI FSalida ! 0; |DDEFECTO #6; |FINSI;
      i_nom = #6#2;      || nombre de la cuenta
  |FINSI;
|FPRC;
|| ***********************************************************************

|PROCESO actuaiva;
 salid2 = 0;
 iva1 = 0;  iva2 = 0;  iva3 = 0;  iva4 = 0;  iva5 = 0;
 req1 = 0;  req2 = 0;  req3 = 0;  req4 = 0;  req5 = 0;
 cam23 = ""; cam24 = ""; cam28 = ""; cam48 = ""; cam62 = 1;

 |SI tipoRS = "R"; |HAZ leereper; |FINSI;
 |SI tipoRS = "S"; |HAZ leesopor; |FINSI;

 |SI salid2 = 0; |Y #0#11 = "N";
     |SI tipoRS ="R"; |LIBERA #3; |FINSI;
     |SI tipoRS ="S"; |LIBERA #4; |FINSI;
     |ACABA;
 |FINSI;

 |HAZ calporcien;

 |SI tipoRS = "R";
     |PARA para1 = 0; |SI para1 [ 62; |HACIENDO para1 = para1 + 1;
           #3para1 = #2para1;
     |SIGUE;
     #3#23 = cam23; #3#24 = cam24;
     #3#28 = cam28; #3#48 = cam48;
     #3#62 = cam62;
     |SI salid2 = 0;  |GRABA 020#3a;  |FINSI;
     |SI salid2 ! 0;  |GRABA 020#3n;  |FINSI;
     |LIBERA #3;
 |SINO;
     |PARA para1 = 0; |SI para1 [ 61; |HACIENDO para1 = para1 + 1;
           #4para1 = #2para1;
     |SIGUE;
     #4#23 = cam23; #4#24 = cam24;
     #4#28 = cam28; #4#48 = cam48;
     |SI salid2 = 0;  |GRABA 020#4a;  |FINSI;
     |SI salid2 ! 0;  |GRABA 020#4n;  |FINSI;
     |LIBERA #4;
 |FINSI;
 |ATRI I;
   |PINTA 1964, #2#0;
   |PINTA 1967, #2#2;
   |PINTA 1974, #2#27;
 |ATRI 0;
|FPRC;

|PROCESO leereper;
 salid2 = 0;
 #3#0 = #2#0;
 #3#2 = #2#2;
 #3#27 = #2#27;
 |LEE 101#3=;
 salid2 = FSalida;
 |SI FSalida ! 0;
     |DDEFECTO #3;
     #3#0 = #2#0;
     #3#2 = #2#2;
     #3#27 = #2#27;
 |FINSI;

 cam23 = #3#23; || deducible
 cam24 = #3#24; || exportado
 cam28 = #3#28; || liquidada
 cam48 = #3#48; || inversion
 cam62 = #3#62; || fras. Englobadas.
  iva1 = #3#11;
  iva2 = #3#12;
  iva3 = #3#13;
  iva4 = #3#54;
  iva5 = #3#55;
  req1 = #3#17;
  req2 = #3#18;
  req3 = #3#19;
  req4 = #3#58;
  req5 = #3#59;
|FPRC;

|PROCESO leesopor;
 salid2 = 0;
 #4#0 = #2#0;
 #4#2 = #2#2;
 #4#27 = #2#27;
 |LEE 101#4=;
 salid2 = FSalida;
 |SI FSalida ! 0;
     |DDEFECTO #4;
     #4#0 = #2#0;
     #4#2 = #2#2;
     #4#27 = #2#27;
 |FINSI;

 cam23 = #4#23; || deducible
 cam24 = #4#24; || exportado
 cam28 = #4#28; || liquidada
 cam48 = #4#48; || inversion
  iva1 = #4#11;
  iva2 = #4#12;
  iva3 = #4#13;
  iva4 = #4#54;
  iva5 = #4#55;
  req1 = #4#17;
  req2 = #4#18;
  req3 = #4#19;
  req4 = #4#58;
  req5 = #4#59;
|FPRC;

|PROCESO calporcien;
  |SI #2#14 ! 0;  |Y iva1 = 0;              || (%) iva 1
      #2#11 = ((#2#14 * 100) / #2#08) ! 0;
  |SINO;
      #2#11 = iva1;
  |FINSI;
  |SI #2#15 ! 0;  |Y iva2 = 0;              || (%) iva2
      #2#12 = ((#2#15 * 100) / #2#09) ! 0;
  |SINO;
      #2#12 = iva2;
  |FINSI;
  |SI #2#16 ! 0;  |Y iva3 = 0;              || (%) iva 3
      #2#13 = ((#2#16 * 100) / #2#10) ! 0;
  |SINO;
      #2#13 = iva3;
  |FINSI;
  |SI #2#56 ! 0;  |Y iva4 = 0;              || (%) iva 4
      #2#54 = ((#2#56 * 100) / #2#52) ! 0;
  |SINO;
      #2#54 = iva4;
  |FINSI;
  |SI #2#57 ! 0;  |Y iva5 = 0;              || (%) iva 5
      #2#55 = ((#2#57 * 100) / #2#53) ! 0;
  |SINO;
      #2#55 = iva5;
  |FINSI;
  |SI #2#20 ! 0;  |Y req1 = 0;              || (%) rec 1
      #2#17 = ((#2#20 * 100) / #2#08) ! 1;
  |SINO;
      #2#17 = req1;
  |FINSI;
  |SI #2#21 ! 0;  |Y req2 = 0;              || (%) rec 2
      #2#18 = ((#2#21 * 100) / #2#09) ! 1;
  |SINO;
      #2#18 = req2;
  |FINSI;
  |SI #2#22 ! 0;  |Y req3 = 0;              || (%) rec 3
      #2#19 = ((#2#22 * 100) / #2#10) ! 1;
  |SINO;
      #2#19 = req3;
  |FINSI;
  |SI #2#60 ! 0;  |Y req4 = 0;             || (%) rec 4
      #2#58 = ((#2#60 * 100) / #2#52) ! 1;
  |SINO;
      #2#58 = req4;
  |FINSI;
  |SI #2#61 ! 0;  |Y req5 = 0;             || (%) rec 5
      #2#59 = ((#2#61 * 100) / #2#53) ! 1;
  |SINO;
      #2#59 = req5;
  |FINSI;

  nume1 = #2#08 +#2#09 +#2#10 +#2#52 +#2#53;    || bases imponibles
  nume2 = #2#14 +#2#15 +#2#16 +#2#56 +#2#57;    || cuotas iva
  nume3 = #2#20 +#2#21 +#2#22 +#2#60 +#2#61;    || total cuotas rec.
  #2#26 = nume1 + nume2 + nume3;                    || TOTAL FACTURA
|FPRC;

|DEFBUCLE caivatm2;
 #2#1;
 ;
 00, 000000, "  ";
 99, 999999, "zz";
 e;
 NULL, actuaiva;
|FIN;

|PROCESO actua_iva;
 |SI tipoRS = "R"; |ABRE #3; |FINSI; || Repercutido
 |SI tipoRS = "S"; |ABRE #4; |FINSI; || Soportado
 |BUCLE caivatm2;
 |SI tipoRS = "R"; |CIERRA #3; |FINSI; || Repercutido
 |SI tipoRS = "S"; |CIERRA #4; |FINSI; || Soportado
|FPRC;

|| ***********************************************************************
|| *********************** RECUPERACION CON TIPO 3 ***********************
|| ***********************************************************************

|PROCESO recupera; |TIPO 3;
 |SI #0#12 = "SI";

    tipoRS= #0#6;

|HAZ nom_usu;
    alfa1 = "r" + nom_tmp;
    |NOME_DAT #2 alfa1;

    |ABRE #2;
    |CIERRA #2;
    |DELINDEX #2;

    |ABRE #2;
    |ABRE #5;
    |ABRE #6;
    |ABRE #7;
    |ATRI I; |PINTA 1964, " <TEMPORAL> "; |ATRI 0;
    |BUCLE lee_diario;
    |PINTA 1964, "            ";
    |CIERRA #5;
    |CIERRA #6;
    |CIERRA #7;
    |SI swc ! 0;
        |HAZ actua_iva;
    |FINSI;
    |CIERRA #2;
    |DELINDEX #2;
 |FINSI;
|FPRC;
