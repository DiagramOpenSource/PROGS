|FICHEROS;
  caimpamo #0;
  camocabe #2;
  camoline #3;
  camocab1 #4;
  camogrup #5;  || Grupo de Amortizacion
  camolin@ #6;
|FIN;

|VARIABLES;
  &entrada = 1;
  alfa1 = "";
  alfa2 = "";
  para1 = 0;
  &r_emp = 0;
  &r_per = 0;
  nom1  = "";
  VAlfa0 = "";
  VAlfa1 = "";
  VAlfa2 = "";
  VAlfa3 = "";
  VAlfa4 = "";

  estado = 0;
  linea  = 0;
  swlinea = 0;
  fich_copi = "";

 sw = 0;
 cfecha  = @;
 importe = 0;
 afiscal = 0;
 acontab = 0;
 vfecha =  @;
 nEstado = 0;

 congrupo = 0;  ||Contador de Grupos
 grupoact = 0;  ||Grupo Actual
 sw_grupo = 0;
 cta68    = "";
 dig68    = 0;
 cta28    = "";
 dig28    = 0;
 desgrupo = ""; || Descripcion

 Comodin = "";
|FIN;

|INCLUYE acceso_i;
|INCLUYE numamo_i;
|INCLUYE tempo_i;
|INCLUYE dsmone_i;

|| ********************************************************************

|PROCESO numamo; |TIPO 0;
    x_alfa1 = #0Campo;
|HAZ x_punto;
    #0Campo = x_alfa1;
    |PINTA #0Campo;
|FPRC;

|PROCESO mayor;
  alfa1 = #0Campo > " ";
  #0Campo = alfa1;
  |PINTA #0Campo;
|FPRC;

|PROCESO deentrada;  |TIPO 7;
 aMonedaA = eaMoneda;
 #0#5 = empre;     |PINTA #0#5;
 #0#6 = ejerc - 1; |SI #0#6 < 0; #0#6 = 9; |FINSI;
 |PINTA #0#6;
 #0#7 = #30#1; |PINTA #0#7;
|FPRC;

|PROCESO esbuena;
  |SI FSalida = 9;  |ACABA;  |FINSI;
  |ABRE #30;
  #30#2 = #0#5;
  #30#3 = 0;
  |LEE 011#30];
  |SI FSalida ! 0;  |O #30#2 ! #0#5;
      |MENAV "     NO Existe la empresa Contable";
      |ERROR;
  |FINSI;
  |CIERRA #30;
|FPRC;

|PROCESO consulta;
  |SI FSalida ! 9;  |ACABA;  |FINSI;
  r_emp = 0;
  |SUB_EJECUTA "caconemp.run";
  |ERROR;
  #0#5 = r_emp;
  #0#6 = r_per;
  |PTEC 802;
  |PTEC 802;
|FPRC;

|PROCESO esbuena1;
  |SI FSalida = 2; |ACABA; |FINSI;
  |SI #0#5 = empre; |Y #0#6 = ejerc;
      |MENAV "    ERROR.! NO PUEDE IMPORTAR A LA EMPRESA ACTUAL ";
      |ERROR;
      |ACABA;
  |FINSI;
  |ABRE #30;
  #30#2 = #0#5;
  #30#3 = #0#6;
  |LEE 011#30=;
  |SI FSalida ! 0;
      |MENAV "     NO Existe Periodo en la empresa Contable";
      |CIERRA #30;
      |ERROR;
      |ACABA;
  |FINSI;
  |SI #30#13 ! dig3;  |O #30#14 ! dig4;  |O #30#15 ! dig5;
                      |O #30#16 ! dig6;  |O #30#17 ! dig7;
                      |O #30#18 ! dig8;  |O #30#19 ! dig9;
      |MENAV "     NO CORRESPONDEN LOS NIVELES";
      |CIERRA #30;
      |ERROR;
      |ACABA;
  |FINSI;
  #0#7 = #30#1; |PINTA #0#7;
  nom1 = #30#1;
  #30#2 = empre;
  #30#3 = ejerc;
  |LEE 011#30=;
  |CIERRA #30;

  swOperacion = 0;
  enEmpresa = #0#5;
  enPeriodo = #0#6;
  |SUB_EJECUTA_NP "camoneda";
  aMonedaC = eaMoneda;
|FPRC;

|| *********************************************************************
|| ************** Proceso PARA Importar Amortizaciones
|| *********************************************************************
|PROCESO crea_lineas;
    || Lee y Graba con LEEDEDEF/GRABAENDEF de Empresa en la que esta (Destino)
    || y el Bucle es de la Empresa Origen.


 |DDEFECTO #6;
 #6#0 = #3#0;
 #6#1 = #3#1;
 #6#2 = #3#2;
 |LEE 001#6=;
 |SI FSalida = 0; |ACABA; |FINSI;

 |DDEFECTO #6;
 #6#0 = #3#0;
 #6#1 = #3#1;
 #6#2 = #3#2;
 #6#3 = #3#3;
 #6#4 = #3#4;
 #6#5 = #3#5;
 #6#6 = #3#6;
 #6#7 = #3#7;
 #6#8 = #3#8;
 #6#9 = #3#9;
 |SI aMonedaC ! aMonedaA;   ||Cambio de Moneda
       impMoneda = #6#5; |HAZ ElCambioMoneda; #6#5 = impMoneda;
       impMoneda = #6#9; |HAZ ElCambioMoneda; #6#9 = impMoneda;
       #6#10 = #6#9 - #6#5;
 |FINSI;
 |GRABA 020#6n;

 swlinea = 1;

 |PINTA 2246, #3#0;
 |PINTA 2255, #3#2;
 |LIBERA #3;
|FPRC;

|| -------------------------------
|DEFBUCLE laslineas;
 #3#1;
 ;
 #4#0, #4#1, 001;
 #4#0, #4#1, 999;
 be;
 NULL, crea_lineas;
|FIN;
|| -------------------------------

|PROCESO BorraLineas;
 |BORRA 020#6a;
 |LIBERA #6;
|FPRC;

|DEFBUCLE BorraLineas;
 #6#1003;
 ;
 #2#0, #2#1, 001;
 #2#0, #2#1, 999;
 be;
 NULL, BorraLineas;
|FIN;
|| -------------------------------

|PROCESO pasarla; || Crea las Cabeceras

 #2#0 = #4#0;
 #2#1 = #4#1;
 |LEE 101#2=;
 nEstado = FSalida;
 |SI nEstado = 0; |Y #0#9 = "S";
     |CIERRA #6;
     |BUCLE BorraLineas;
     |ABRE #6;
     |BORRA 020#2a;
     |LIBERA #2;
     #2#0 = #4#0;
     #2#1 = #4#1;
     |LEE 101#2=;
     nEstado = FSalida;
 |FINSI;

 |SI nEstado ! 0;
     |DDEFECTO #2;
     |PARA para1 = 0; |SI para1 [ 23; |HACIENDO para1 = para1 + 1;
           #2para1 = #4para1;
     |SIGUE;
     grupoact = #2#15;
     |HAZ vergrupo;
     #2#15 = grupoact;
     |GRABA 020#2b;
 |FINSI;

 |BUCLE laslineas;

 |LIBERA #2;
 |LIBERA #4;
|FPRC;

|DEFBUCLE pasar_a;
 #4#1;
 ;
 #0#0, 00;
 #0#1, 99;
 be;
 NULL, pasarla;
|FIN;

|| *********************************************************************
|PROCESO crea_cabecera;
 |DDEFECTO #4;
 |PARA para1 = 0; |SI para1 [ 23; |HACIENDO para1 = para1 + 1;
       #4para1 = #2para1;
 |SIGUE;
 |SI aMonedaC ! aMonedaA;   ||Cambio de Moneda
      impMoneda = #4#7;  |HAZ ElCambioMoneda; #4#7  = impMoneda;
      impMoneda = #4#12; |HAZ ElCambioMoneda; #4#12 = impMoneda;
      impMoneda = #4#13; |HAZ ElCambioMoneda; #4#13 = impMoneda;
      impMoneda = #4#18; |HAZ ElCambioMoneda; #4#18 = impMoneda;
      impMoneda = #4#19; |HAZ ElCambioMoneda; #4#19 = impMoneda;
 |FINSI;

 |GRABA 020#4n;
 |LIBERA #2;
|FPRC;

|DEFBUCLE crea_aux;
 #2#1;
 ;
 #0#0, 00;
 #0#1, 99;
 be;
 NULL, crea_cabecera;
|FIN;


|| *********************************************************************
|PROCESO pase; |TIPO 3;

  |SI #0#8 = "NO"; |ACABA; |FINSI;

  |HAZ nom_usu;
  alfa1 = "Z" + nom_tmp;
  |NOME_DAT #4 alfa1;
  |ABRE #4;
  |CIERRA #4;
  |DELINDEX #4;

|| .............. Variables
  |DFICE fich_copi;         || Directorio Actual

  |DBASE dir_fich;
  dir_fich = dir_fich + "fich/";
  alfa1 = #0#5; alfa1 = ("000000" + alfa1) % -105;
  alfa2 = #0#6; alfa2 =      ("0" + alfa2) % -101;
  fich_alta = dir_fich + alfa1 + alfa2 + "/";    || Directorio Empresa Origen

  |DDEF VAlfa0; |QBF VAlfa0;
  VAlfa1 = VAlfa0 + "camoline";       || Def Lineas Amortizacion
  VAlfa2 = fich_copi + "camoline";    || Lineas de la Empresa Destino

  VAlfa3 = VAlfa0 + "camogrup";       || Def Grupos Amortizacion
  VAlfa4 = fich_alta + "camogrup";    || Lineas de la Empresa Origen
|| ::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::

  || .......... Proceso para Auxiliar
  |D_CUADRO 2120, 2360;
  |PINTA 2221, "                                       ";

  eaMoneda = aMonedaA;
  |HAZ MoCierre;      ||Carga Variables Moneda

  |PATH_DAT #2 fich_alta;    || Lee las fichas de la Empresa Origen
  |ABRET #2;
  |ABRE #4;
  |ATRI I; |PINTA 2221, "              <TEMPORAL>               "; |ATRI 0;
  |BUCLE crea_aux;
  |CIERRA #4;
  |CIERRAT #2;
|| ::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::

  |PATH_DAT #2 fich_copi;  || Cabeceras de la Empresa Destino
  |PATH_DAT #3 fich_alta;  || Lee #3 de Empresa Origen.
  |ABRET #5;
  |LEE 001#5u;
  |SI FSalida = 0;
      congrupo = #5#0;
  |FINSI;

  Comodin = VAlfa0 + "camoline.mas";
  |CARGA_DEF Comodin, camolin@;
  |SI FSalida = 0;
     |PATH_DAT #6 fich_copi;  || Lee #3 de Empresa Origen.
     |ABRE #2;
     |ABRE #3;
     |ABRE #camolin@;
     |ATRI I;|PINTA 2221, " PASANDO AMORTIZACION ->               "; |ATRI 0;
     |BUCLE pasar_a;
     |CIERRA #2;
     |CIERRA #3;
     |CIERRA #camolin@; |DESCARGA_DEF #camolin@;
   |FINSI;
   |CIERRAT #5;
   |DELINDEX #4;
   |SI swlinea = 1; |HAZ recupera; |FINSI;
|FPRC;
|| **********************************************************************

|| Proceso para recalcular los datos de Cabecera
|PROCESO lineas;
  |SI #3#4 = "C";
      importe = importe + #3#5;
      |SI sw = 0;
          cfecha = #3#7;
          sw = 1;
      |FINSI;
  |FINSI;
  |SI #3#4 = "A";
      afiscal = afiscal + #3#5;
      acontab = acontab + #3#9;
  |FINSI;
  |SI #3#4 = "V";
      vfecha = #3#7;
  |FINSI;
|FPRC;

|PROCESO grabacab;
 cfecha = "00.00.0000";
 importe = 0;
 afiscal = 0;
 acontab = 0;
 vfecha = "00.00.0000";
 sw = 0;

 |PINTA 2250, #2#0;
 |BUCLELIN 0lineas#2;
 |SI sw = 1;
     #2#6 = cfecha;
     #2#23 = vfecha;
     #2#7 = importe;
     #2#12 = afiscal;
     #2#13 = importe - afiscal;
     #2#18 = acontab;
     #2#19 = importe - acontab;
     |GRABA 020#2a;
 |FINSI;
 |LIBERA #2;
|FPRC;

|DEFBUCLE calamo0;
 #2#1;
 ;
 #0#0, 00;
 #0#1, 99;
 e;
 NULL, grabacab;
|FIN;

|PROCESO recupera;  || Recalcular Cabeceras
  |PATH_DAT #2 fich_copi;  || Trabaja sobre los ficheros de ESTA empresa
  |PATH_DAT #3 fich_copi;
  |ATRI I;|PINTA 2221," RECUPERANDO AMORTIZACION ->           "; |ATRI 0;
  |BUCLE calamo0;
|FPRC;


|| **********************************************************************
|| Proceso para crear los grupos de Amortizacion Correctamente.

|PROCESO sigrupo;
 sw_grupo = 1;
 |ERROR10;
|FPRC;

|DEFBUCLE vergrup;
 #5#1;
 1,2,3,4;
 0000, cta68, dig68, cta28, dig28;
 9999, cta68, dig68, cta28, dig28;
 e;
 NULL, sigrupo;
|FIN;

|PROCESO vergrupo;
  || Lee con LEEDEDEF de Empresa Origen
  || y el Bucle es de la Empresa Destino.

 |LEEDEDEF VAlfa3, VAlfa4, 0, grupoact, 1, cta68, 2, dig68, 3, cta28, 4, dig28, 9, desgrupo;
 estado = FSalida;
 |SI estado ! 0;
     grupoact = 9999;
     |ACABA;
 |FINSI;

 #5#0 = grupoact;
 |LEE 001#5=;
 |SI FSalida = 0;    || Existe Grupo.
     |SI cta68 ! #5#1; |O dig68 ! #5#2; |O cta28 ! #5#3; |O dig28 ! #5#4;
         sw_grupo = 0;
         |BUCLE vergrup;
         |SI sw_grupo = 0;  || No ha encontrado grupo
             congrupo = congrupo + 1;
             grupoact = congrupo;
             |HAZ altagrupo;
         |FINSI;
     |FINSI;
 |SINO;
     |HAZ altagrupo;
 |FINSI;
 grupoact = #5#0;
|FPRC;

|PROCESO altagrupo;
 |DDEFECTO #5;
 #5#0 = grupoact;
 #5#1 = cta68;
 #5#2 = dig68;
 #5#3 = cta28;
 #5#4 = dig28;
 #5#9 = desgrupo;
 |GRABA 020#5n;
|FPRC;
