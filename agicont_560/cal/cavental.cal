|FICHEROS;
    cavental #0;
    camantal #1;
    camanban #2;
    cacuenta #3;
    caventa1 #4;
    cacuadre #9;
|FIN;

|VARIABLES;
    &entrada = 1;
    informe = "cavental";
    banco = "";
    vto = @;
    sw = 0;
    &sw1 = 0;
    &venci = 0;
    &swtot = 0;
    &tvenci = 0;
    &tbanco = 0;
    &nomban = "";
 {-1}elegir = "";
    elegir1 = "2400";
    elegir2 = "1";
    elegir3 = " Elija opcion .: ";
    elegir4 = "IP";
    elegir5 = " Impresora ";
    elegir6 = " Pantalla ";
    posic = 0;
    mas = 0;
    lin_pan = 0;
    alfa1 = "";
    sw_act = 0;
|FIN;

|INCLUYE acceso_i;

|PROCESO mayor;
   alfa1 = #0Campo > " ";
   #0Campo = alfa1;
   |PINTA #0Campo;
|FPRC;

|PROCESO cuadre; |TIPO 8;
  sw_act = 0;    ||  0 = N control (Carta/talon) junto / 1 = S por separado
  |ABRE #9;
  |DDEFECTO #9;
  #9#0 = 1;
  |LEE 001#9=;
  |SI FSalida = 0;
      |SI #9#18 = "S";
          sw_act = 1;
      |FINSI;
  |FINSI;
  |CIERRA #9;
  |SI sw_act = 0;
      |CAMPO_MODIFICA #0#10, 1, "N";
  |FINSI;
|FPRC;

|PROCESO juntos; |TIPO 0;
 |SI sw_act = 0;
     #0#10 = #0#13;
     |PINTA #0#10;
 |FINSI;
|FPRC;

|PROCESO detalles;       || lee el banco y la cuenta
  nomban = "";
  |DDEFECTO #2;
  #2#10 = #1#3;
  #2#11 = #1#4;
  |LEE 001#2=;
  |SI FSalida = 0;
      nomban = #2#1;
  |SINO;
      |DDEFECTO #3;
      #3#00 = #1#3;
      #3#01 = #1#4;
      |LEE 001#3=;
      nomban = #3#2;
 |FINSI;
 |DDEFECTO #3;
 #3#00 = #1#1;
 #3#01 = #1#2;
 |LEE 001#3=;
|FPRC;

|PROCESO impre1;
  |SI #0#10 ! "A";
      |SI #0#10 ! #1#10;  |ACABA;  |FINSI;  || impresa carta
  |FINSI;
  |SI #0#13 ! "A";
      |SI #0#13 ! #1#12;  |ACABA;  |FINSI;  || impreso talon
  |FINSI;
  |SI #0#11 ! "A";
      |SI #0#11 ! #1#07;  |ACABA;  |FINSI;  || emision
  |FINSI;
  |SI #0#12 ! "A";
      |SI #0#12 ! #1#08;  |ACABA;  |FINSI;  || vencimiento
  |FINSI;


|HAZ detalles;
|SI sw = 0;
        banco = #1#3;
        vto   = #1#6;
    |PRINT;                   || imprime vto
        sw = 1;
        sw1 = 1;
|FINSI;
|SI vto ! #1#6;
        sw1   = 2;
        swtot = 1;
        venci = 1;
    |PRINT;                   || imprime total banco y vto.
        vto = #1#6;
        tvenci = 0;
        tbanco = 0;
        swtot = 0;
        venci = 0;
        sw1 = 0;
    |PRINT;                   || imprime vto.
        sw1 = 1;
|FINSI;
|SI banco ! #1#3;
        sw1   = 2;
        venci = 1;
        swtot = 0;
    |PRINT;                   || imprime total banco
        banco = #1#3;
        tbanco = 0;
        venci = 0;
        sw1 = 1;
|FINSI;
    tvenci  = tvenci + #1#9;
    tbanco  = tbanco + #1#9;
|PRINT;                       || imprime linea
|FPRC;

|DEFBUCLE impre;
 #1#3;
 1, 5, 16; || ..... ordenado por vto+banco, selec. prov y emision y recibo
 #0#4, #0#2, 0 ,#0#0, #0#6, #0#8, #0#14;
 #0#5, #0#3 ,9 ,#0#1, #0#7, #0#9, #0#15;
 e;
 NULL, impre1;
|FIN;

|PROCESO impresora;
  |IMPRE 0;
  |SI FSalida ! 0;  |ACABA;  |FINSI;
  |INFOR informe;
  |SI FSalida ! 0;  |FINIMP;  |ACABA;  |FINSI;
  |ABRE #2;
  |ABRE #3;
  |SELECT #2#2;
  |BUCLE impre;
  |CIERRA #2;
  |CIERRA #3;
  sw1   = 2;
  venci = 1;
  swtot = 1;
  |PRINT;
  |PIEINF;
  |FININF;
  |FINIMP;
|FPRC;

|PROCESO correte;
|CAMPO_POSICION #4#0,0,posic; posic=posic+mas; |CAMPO_POSICION #4#0,1,posic;
|CAMPO_POSICION #4#2,0,posic; posic=posic+mas; |CAMPO_POSICION #4#2,1,posic;
|CAMPO_POSICION #4#3,0,posic; posic=posic+mas; |CAMPO_POSICION #4#3,1,posic;
|CAMPO_POSICION #4#4,0,posic; posic=posic+mas; |CAMPO_POSICION #4#4,1,posic;
|CAMPO_POSICION #4#5,0,posic; posic=posic+mas; |CAMPO_POSICION #4#5,1,posic;
|CAMPO_POSICION #4#7,0,posic; posic=posic+mas; |CAMPO_POSICION #4#7,1,posic;
|CAMPO_POSICION #4#10,0,posic; posic=posic+mas; |CAMPO_POSICION #4#10,1,posic;
|FPRC;

|PROCESO empieza;
  |PINPA #0#4;
  mas = lin_pan * -100;
  lin_pan = 0;
  |HAZ correte;
|FPRC;

|PROCESO panta1;
  |SI #0#10 ! "A";
      |SI #0#10 ! #1#10;  |ACABA;  |FINSI;  || impresa
  |FINSI;
  |SI #0#13 ! "A";
      |SI #0#13 ! #1#12;  |ACABA;  |FINSI;  || impreso talon
  |FINSI;
  |SI #0#11 ! "A";
      |SI #0#11 ! #1#07;  |ACABA;  |FINSI;  || emision
  |FINSI;
  |SI #0#12 ! "A";
      |SI #0#12 ! #1#08;  |ACABA;  |FINSI;  || vencimiento
  |FINSI;

|HAZ detalles;
  |SI sw = 0;
      vto   = #1#6;
      sw = 1;
  |FINSI;
  |SI vto ! #1#6;
      |PAUSA;
      vto = #1#6;
      tvenci = 0;
      |HAZ empieza;
  |FINSI;
  tvenci  = tvenci + #1#9;
  lin_pan = lin_pan + 1;
  |DDEFECTO #4;
  #4#0 = #1#3;    || cuenta banco
  #4#2 = #1#0;    || nro. talon
  #4#3 = #1#1;    || cuenta proveedor
  #4#4 = #1#14;   || nombre proveedor
  #4#5 = #1#5;    || fecha emision
  #4#6 = #1#6;    || fecha vencimiento
  #4#7 = #1#9;    || total talon
  #4#10 = #1#16;  || total talon
  #4#8 = tvenci;  || total vencimientos
  |PINDA #0#4;
  |SI lin_pan < 11;
      mas = 100;
  |SINO;
      mas = -1000;
      lin_pan = 0;
      |PAUSA;
      |PINPA #0#4;
  |FINSI;
  |HAZ correte;
|FPRC;

|DEFBUCLE panta;
 #1#3;
 1, 5, 16; || ..... ordenado por vto+banco, selec. prov y emision
 #0#4, #0#2, 0 ,#0#0, #0#6, #0#8, #0#14;
 #0#5, #0#3 ,9 ,#0#1, #0#7, #0#9, #0#15;
 e;
 NULL, panta1;
|FIN;

|PROCESO pantalla;
|PINPA #0#4;
|ABRE #2;
|ABRE #3;
|SELECT #2#2;
|BUCLE panta;
|PAUSA;
|CIERRA #2;
|CIERRA #3;
|FPRC;

|PROCESO impre; |TIPO 3;
|HAZ inicio;
|MENU elegir;
|SI FSalida > 0;
    |SI FSalida = 1;  |HAZ impresora;  |FINSI;
    |SI FSalida = 2;  |HAZ pantalla;   |FINSI;
|FINSI;
|FPRC;
