|FICHEROS;
    cacompar #0;
    cacuenta #5;         || plan contable
    cacompt1 #11;        || tmp. comparacion
    caconimp #8;
|FIN;

|VARIABLES;
  &pathbal = "";
  &balance = 1;
  &aparam  = "";
    alfa1 = "";
    alfa2 = "";
    alfa3 = "";
    alfa4 = "";
    nume1 = 0;
    nume2 = 0;
    nume3 = 0;
    nume4 = 0;
    para1 = 0;
    para2 = 0;
    para3 = 0;
    para4 = 0;
    fech1 = @;
    fech2 = @;
    fech3 = @;
    fech4 = @;
    elsw1 = 0;
    elsw2 = 0;
    elsw3 = 0;
    elsw4 = 0;
    FEstado = 0;
    seleccio = 0;
    modo = 0;
    sw_rup = 0;
    saldo  = 0;

    men2  = "     Longitud de Cuenta Fuera de Nivel !";
    p_mes = "";
    d_mes = 0;
    h_mes = 0;
    mas = 0;
    sw_chequeo = 0;
    sw_movimin = 0;
    acum = 0;
    digito = 0;
    swdig = 0;
    long = "";
    cta  = "";

    &nom1 = "";
    &nom2 = "";
    &ej1  = 0;
    &ej2  = 0;
    &colum = "";
    &des = "";
    &has = "";
    &r_emp = 0;
    &r_per = 0;
    xx = 0;
    yy = 0;

    swniv = 0;
    niv1  = 0;
    niv2  = 0;
    niv3  = 0;
    niv4  = 0;
    niv5  = 0;
    niv6  = 0;
|FIN;

|INCLUYE acceso_i;
|INCLUYE puntos_i;
____________________________________/ CALCULOS PANTALLA

|PROCESO mayus; |TIPO 0;
  #0Campo = #0Campo > #0Campo;
  |PINTA #0Campo;
|FPRC;

|PROCESO lee_empre; |TIPO 0;
  |SI FSalida = 2;  |ACABA;  |FINSI;
  nume1 = Campo - 1;
  |ABRE #30;
  #30#2 = #0nume1;
  #30#3 = #0Campo;
  |LEE 000#30=;
  |SI FSalida ! 0;
      #30#1 = "";
      |MENAV "    Empresa INEXISTENTE ...";
      |ERROR;
  |SINO;
      |SI Campo = 1; niv1 = #30#14; niv2 = #30#15; niv3 = #30#16;
                     niv4 = #30#17; niv5 = #30#18; niv6 = #30#19;
      |SINO;
          swniv = 0;
          |SI niv1 ! #30#14; swniv= 1; |FINSI;
          |SI niv2 ! #30#15; swniv= 1; |FINSI;
          |SI niv3 ! #30#16; swniv= 1; |FINSI;
          |SI niv4 ! #30#17; swniv= 1; |FINSI;
          |SI niv5 ! #30#18; swniv= 1; |FINSI;
          |SI niv6 ! #30#19; swniv= 1; |FINSI;
          |SI swniv ! 0;
              |MENAV "    NO CORRESPONDEN LOS NIVELES";
              |ERROR;
          |FINSI;
      |FINSI;
  |FINSI;
  |ATRI I;
  |SI Campo = 1;  |PINTA 1032, #30#1; nom1 = #30#1; ej1 = #30#26; |FINSI;
  |SI Campo = 3;  |PINTA 1132, #30#1; nom2 = #30#1; ej2 = #30#26; |FINSI;
  |ATRI 0;
  |CIERRA #30;
|FPRC;

|PROCESO consulta;
  |SI FSalida ! 9;  |ACABA;  |FINSI;
  |SUB_EJECUTA "caconemp.run";
  |ERROR;
  |SI Campo = 0;
      #0#0 = r_emp;
      #0#1 = r_per;
      |PTEC 802;
      |PTEC 802;
  |FINSI;
  |SI Campo = 2;
      #0#2 = r_emp;
      #0#3 = r_per;
      |PTEC 802;
      |PTEC 802;
  |FINSI;
|FPRC;

|PROCESO misma; |TIPO 0;
  |SI #0#0 = #0#2;|Y #0#1 = #0#3;|Y FSalida ! 2;
      |MENAV "    ATENCION!!! No se puede comparar la misma EMPRESA ...";
      |ERROR;
  |FINSI;
|FPRC;

|PROCESO pintames; |TIPO 0;
  |SI #0Campo =  0;  p_mes = "Apertura  ";  |FINSI;
  |SI #0Campo =  1;  p_mes = "Enero     ";  |FINSI;
  |SI #0Campo =  2;  p_mes = "Febrero   ";  |FINSI;
  |SI #0Campo =  3;  p_mes = "Marzo     ";  |FINSI;
  |SI #0Campo =  4;  p_mes = "Abril     ";  |FINSI;
  |SI #0Campo =  5;  p_mes = "Mayo      ";  |FINSI;
  |SI #0Campo =  6;  p_mes = "Junio     ";  |FINSI;
  |SI #0Campo =  7;  p_mes = "Julio     ";  |FINSI;
  |SI #0Campo =  8;  p_mes = "Agosto    ";  |FINSI;
  |SI #0Campo =  9;  p_mes = "Septiembre";  |FINSI;
  |SI #0Campo = 10;  p_mes = "Octubre   ";  |FINSI;
  |SI #0Campo = 11;  p_mes = "Noviembre ";  |FINSI;
  |SI #0Campo = 12;  p_mes = "Diciembre ";  |FINSI;
  |SI #0Campo = 13;  p_mes = "Cierre    ";  |FINSI;

  |SI Campo = 11;     |PINTA 1527, p_mes; des = p_mes;   |FINSI;
  |SI Campo = 12;     |PINTA 1541, p_mes; has = p_mes;   |FINSI;
|FPRC;

|PROCESO nosaldo; |TIPO 7;
  alfa1 = "S";
  |SI #0#18 = "S";  alfa1 = "N";  #0#19 = "S"; |FINSI;
  |CAMPO_MODIFICA #0#19, 1, alfa1;
  |PINTA #0#19;
|FPRC;

|PROCESO cuenta; |TIPO 6;
 p_alfa1 = #0Campo;
 salidas = FSalida;
 |HAZ punto;
 |SI p_error ! 0;  |ERROR;  |ACABA;  |FINSI;
 |SI #0Campo = "            ";  |ERROR;  |ACABA;  |FINSI;
     digito = Campo + 2;
     cta  = #0Campo; |QBF cta;
     long = cta % 0;
     swdig = 0;
 |SI long = #30#14; swdig = 1; #0digito = 1; |FINSI;
 |SI long = #30#15; swdig = 1; #0digito = 2; |FINSI;
 |SI long = #30#16; swdig = 1; #0digito = 3; |FINSI;
 |SI long = #30#17; swdig = 1; #0digito = 4; |FINSI;
 |SI long = #30#18; swdig = 1; #0digito = 5; |FINSI;
 |SI long = #30#19; swdig = 1; #0digito = 6; |FINSI;
 |SI swdig = 0;
     |MENAV men2;
     |ERROR;
 |FINSI;
|FPRC;

|| ****************************************************************
____________________________________/ LECTURA DEL TEMPORAL
|PROCESO lee_tempo1;
  |PRINT;
  seleccio = 1;
|FPRC;

|DEFBUCLE lee_tempo;
 #11#1;
 ;
 "            ", 0;
 "zzzzzzzzzzzz", 9;
 e;
 NULL, lee_tempo1;
|FIN;
____________________________________/ GRABACION DEL TEMPORAL

|PROCESO chequeo;
  sw_chequeo = 0;
  |SI #0#19 = "N";|Y saldo = 0;|Y sw_movimin = 1;
      sw_chequeo = 1;
  |FINSI;
  |SI sw_movimin = 0;|Y #0#18 = "N";
      sw_chequeo = 1;
  |FINSI;
|FPRC;

|PROCESO acumula;
  sw_movimin = 0;
  saldo = 0;
  |PARA para3 = d_mes; |SI para3 [ h_mes; |HACIENDO para3 = para3 + 3;

        |SI #5para3 ! 0;  sw_movimin = 1;  |FINSI;
        |SI #0#17 = "S"; |Y #5para3 = 0;
            xx = para3 - 2; yy = para3 - 1;
            |SI #5xx ! 0; |O #5yy ! 0;
                sw_movimin = 1;
            |FINSI;
        |FINSI;
        saldo = saldo + #5para3;
  |SIGUE;
|FPRC;

|PROCESO columna1_1;
  |HAZ acumula;
  |HAZ chequeo;
  |SI sw_chequeo ! 0;  |ACABA;  |FINSI;

  #11#0 = #5#0;    || cuenta
  #11#1 = #5#1;    || digito
  |LEE 101#11=;
  |SI FSalida ! 0;
      |DDEFECTO #11;
      #11#0 = #5#0;    || Cuenta
      #11#1 = #5#1;    || digito
      #11#4 = #5#2;    || Nombre
      |GRABA 020#11b;
  |FINSI;

  #11acum = saldo;    || acumulado
  |GRABA 020#11a;
  |LIBERA #11;
|FPRC;

|DEFBUCLE columna1;
 #5#1;
 ;
 #0#13, #0#4;
 #0#14, #0#4;
 e;
 NULL, columna1_1;
|FIN;

|PROCESO antesque;
  aparam = PARAMETRO;
  |QBF aparam;
  |SI aparam = "fech";
      |NOME_DAT #5 "cabalano";
  |FINSI;
|FPRC;

____________________________________/ CALCULO PRINCIPAL
|PROCESO tipo3; |TIPO 3;

  |HAZ antesque;
  |IMPRE 0;
  |SI FSalida ! 0;  |ACABA;  |FINSI;

  |INFOR "cacompar";
  |SI FSalida ! 0; |FINIMP; |ACABA; |FINSI;

|| *****************************************************************
  |SI #0#17 = "D";    mas = 0;  colum = "DEBE";     |FINSI;
  |SI #0#17 = "H";    mas = 1;  colum = "HABER";    |FINSI;
  |SI #0#17 = "S";    mas = 2;  colum = "SALDO";    |FINSI;
  d_mes = (9 + mas) + (#0#11 * 3);
  h_mes = (9 + mas) + (#0#12 * 3);

  |ABRE #11;
  |CIERRA #11;
  |DELINDEX #11;

  acum = 2;
  |ABRE #11;
  |PARA para1 = 0; |SI para1 [ 2; |HACIENDO para1 = para1 + 2;
        para2 = para1 + 1;
        |DBASE dir_fich;
        dir_fich = dir_fich + "fich/"
        alfa1 = #0para1;  alfa1 = "00000" + alfa1;   alfa1 = alfa1 % -105;
        alfa2 = #0para2;  alfa2 = "0"   + alfa2;   alfa2 = alfa2 % -101;
        alfa3 = dir_fich + alfa1 + alfa2 + "/";
        |PATH_DAT #5 alfa3;
        |PATH_DAT #8 alfa3;
        |ABRE #30;
        #30#2 = #0para1;
        #30#3 = #0para2;
        |LEE 011#30=;
        |CIERRA #30;

        |ABRE #8;
        |LEE 111#8p;
        |SI #8#2 = "S";  |Y #8#3 = "N";  |Y aparam = "";
            pathbal = alfa3;
            balance = 1;
            |CIERRA #8;
            |SUB_EJECUTA "careccue.tab";
        |FINSI;
        |CIERRA #8;
        |SI para1 = 2;  acum = 3; |FINSI;
        |BUCLE columna1;
  |SIGUE;
  |CIERRA #11;

  |BUCLE lee_tempo;
  |SI seleccio ! 0;
      |PIEINF;
  |SINO;
      |MENAV "    Seleccion vacia ...";
  |FINSI;
  |DELINDEX #11;
  |FININF;
  |FINIMP;
|FPRC;
