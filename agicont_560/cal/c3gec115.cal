|FICHEROS;
  c3gec115 #0;
  catra115 #1;
  caivasop #2;
  caclipro #4;
  caprovin #6;
  camaasil #7;
  caman115 #11;
  eosclipr #12;
  cacongru #13;
  canempre #30;
  agicontr #23;
|FIN;

|VARIABLES;
  desde = 0;
  hasta = 0;
  modif1 = "";
  modif2 = "";
  xy = 0;
  x = 0;
  y = 0;
  z = 0;
  yz = 0;

  direcc = "";
  linea = 0;
  sien = 0;
  alfa1 = "";
  alfa2 = "";
  alfa3 = "";
  alfa4 = "";
  lonagi = "";
  longi = 0;
  nume1 = 0;
  nume2 = 0;
  nume3 = 0;
  nume4 = 0;

  fech1 = @;
  fech2 = @;

  tipo0 = 0;
  totbas = 0;
  swgr = 0;
  grupo = 0;
  concepto = 0;
  swclipro = 0;

  agi = "";
  &agi1 = "";
  &agi2 = "";
  &aste = "";
  num = 0;

  ult_cont = 0;
  cclau    = "";
  ccc      = 0;
  nif      = "";
  campobas1 = 0;
  camporet1 = 0;
  campobas2 = 0;
  camporet2 = 0;
  retencion = 0;
  meses     = 0;
  cta       = "";
  dig       = 0;
  traba     = "";

  i_cm1 = 50;
|FIN;

|INCLUYE acceso_i;
|INCLUYE multie_i;
|INCLUYE tempo_i;

|| ************************************************************************
|PROCESO mayor;
  alfa1 = #0Campo > " ";
  #0Campo = alfa1;
  |PINTA #0Campo;
|FPRC;

|PROCESO gmayor1;
  |HAZ mayor;
  |SI #0Campo = "N";
      |CAMPO_MODIFICA #0#25, 1, "S";
  |SINO;
      |CAMPO_MODIFICA #0#25, 1, "N";
  |FINSI;
|FPRC;

|PROCESO gmayor;
  |C_M #0Campo,0,alfa1;
  |SI alfa1 = "N"; |ACABA; |FINSI;
  |HAZ mayor;

  modif1 = "S"; modif2 = "N"
  |SI #0Campo = "N";
      modif1 = "N"; modif2 = "S";
      |SI Campo = 3;
          #0#4 = 0;   |PINTA #0#4;
          #0#5 = 999; |PINTA #0#5;
      |SINO;
          #0#31 = 0;   |PINTA #0#31;
          #0#32 = 999; |PINTA #0#32;
      |FINSI;
  |FINSI;
  |SI Campo = 3;
      |CAMPO_MODIFICA #0#4, 1, modif1;
      |CAMPO_MODIFICA #0#5, 1, modif1;
  |SINO;
      |CAMPO_MODIFICA #0#31, 1, modif1;
      |CAMPO_MODIFICA #0#32, 1, modif1;
  |FINSI;
  nume1 = 15; nume2 = 24;
  |SI Campo = 30; nume1 = 38; nume2 = 47; |FINSI;
  |PARA desde = nume1;  |SI desde [ nume2;  |HACIENDO desde = desde + 1;
        |CAMPO_MODIFICA #0desde, 1,modif2;
        |SI modif2 = "N";
            #0desde = 0; |PINTA #0desde;
        |FINSI;
  |SIGUE;
|FPRC;

|PROCESO gmayor3;
  |HAZ mayor;
  modif1 = "S"; modif2 = "N"

  |SI Campo = 33; |Y #0Campo = "X";
     |PARA desde = 30;  |SI desde [ 47;  |HACIENDO desde = desde + 1;
           |SI desde ! 33;
               |CAMPO_MODIFICA #0desde, 1,"N";
               |SI desde = 30;
                   #0desde = "N";
               |SINO;
                   #0desde = 0;
               |FINSI;
               |PINTA #0desde;
           |FINSI;
     |SIGUE;
     |CAMPO_MODIFICA #0#48, 1, "N"; #0#48 = 0;
     |ACABA;
  |FINSI;

  |CAMPO_MODIFICA #0#48, 1, "S";
  |SI #0Campo = "N"; modif1 = "N"; modif2 = "S"; |FINSI;

  nume1 = 11; nume2 = 14; |SI Campo = 33; nume1 = 34; nume2 = 37; |FINSI;
  |PARA desde = nume1;  |SI desde [ nume2;  |HACIENDO desde = desde + 1;
        |CAMPO_MODIFICA #0desde, 1,modif1;
        |SI modif1 = "N";
            #0desde = 0; |PINTA #0desde;
        |FINSI;
  |SIGUE;

  nume1 = 3; nume2 = 24; |SI Campo = 33; nume1 = 30; nume2 = 47; |FINSI;
  |PARA desde = nume1;  |SI desde [ nume2;  |HACIENDO desde = desde + 1;
        |SI desde = 6;  desde = 15; |FINSI;
        |SI desde = 33; desde = 38; |FINSI;
        |CAMPO_MODIFICA #0desde, 1,modif2;
        |SI modif2 = "N";
            |SI desde = 3; |O desde = 30;
                #0desde = "N";
            |SINO;
                 #0desde = 0;
            |FINSI;
            |PINTA #0desde;
        |FINSI;
  |SIGUE;
|FPRC;
|| ---------------------------------------------------------
|PROCESO saltacon1;
  |SI FSalida = 2; |ACABA; |FINSI;
  |C_M #0Campo, 0, alfa1;
  |SI alfa1 = "N"; |ACABA; |FINSI;
  |SI #0Campo = 0;
      |MENAV "    Error!!! Introduzca como minimo  UN CONCEPTO ";
      |ERROR;
  |FINSI;
|FPRC;

|PROCESO saltacon;
  |SI FSalida = 2; |ACABA; |FINSI;
  |C_M #0Campo, 0, alfa1;
  |SI alfa1 = "N"; |ACABA; |FINSI;
  nume2 = 24; |SI Campo > 24; nume2 = 47; |FINSI;
  |PARA desde = Campo + 1;  |SI desde [ nume2;  |HACIENDO desde = desde + 1;
        |SI #0Campo = 0;
            |CAMPO_MODIFICA #0desde, 1, "N";
            #0desde = 0;
        |SINO;
            |CAMPO_MODIFICA #0desde, 1, "S";
        |FINSI;
  |SIGUE;
|FPRC;

|PROCESO saltagru;
  |SI #0Campo ! 0;  |ACABA;  |FINSI;
  |C_M #0Campo, 0, alfa1;
  |SI alfa1 = "N"; |ACABA; |FINSI;
  nume2 = 14; |SI Campo > 14; nume2 = 37; |FINSI;
  |PARA desde = Campo + 1;  |SI desde [ nume2;  |HACIENDO desde = desde + 1;
        |CAMPO_MODIFICA #0desde, 1, "N";
        #0desde = 0;
  |SIGUE;
|FPRC;
|| ************************************************************************

|PROCESO sel_multi;
  |SI #30#26 ! #0#2;  |ACABA; |FINSI;
  |SI #30#21 = "I";   |ACABA; |FINSI;
  leidos = 1;
|FPRC;

|| ***********************************************************************
|| ---------- Seleccion DESDE / HASTA Empresa.

|PROCESO esdesd1;
  alfa1 = #30#2;
  alfa2 = #30#3;
  alfa1 = "00000" + alfa1;   alfa1 = alfa1 % -105;
  alfa2 = "0"   + alfa2;   alfa2 = alfa2 % -101;
  dirfich0 = dir_fich + alfa1 + alfa2 + "/";
  leidos = 0;
  |HAZ sel_multi;
  |SI leidos = 1;
      |HAZ haztodo;
  |FINSI;
|FPRC;

|DEFBUCLE esdesde;
 #30#1;
 ;
 d_empres, d_period;
 h_empres, h_period;
 e;
 NULL,esdesd1;
|FIN;

|| ---------- Seleccion en parrilla.
|PROCESO esunaauna;
  |PARA xx = i_cm6; |SI xx [ i_cm7; |HACIENDO xx = xx + 1;
        |SI #0xx = 0;  |SAL;  |FINSI;
        yy = xx + 20;
        alfa1 = #0xx;
        alfa2 = #0yy;

        || ... lee empresa
           |ABRE #30;
           |DDEFECTO #30;
           #30#2 = #0xx;
           #30#3 = #0yy;
           |LEE 011#30=;
           |CIERRA #30;
           leidos = 0;
           |HAZ sel_multi;
           |SI leidos = 0; |VETE otramas; |FINSI;
        || ...................

        alfa1 = "00000" + alfa1;   alfa1 = alfa1 % -105;
        alfa2 = "0"     + alfa2;   alfa2 = alfa2 % -101;
        dirfich0 = dir_fich + alfa1 + alfa2 + "/";

        |HAZ haztodo;
 |ET otramas;
  |SIGUE;
|FPRC;

|| ************************************************************************
|PROGRAMA;
  |HAZ tipo_8; || controlar si existe la ficha de clientes
  |CLS;
  |CABEZA "Pase a Mod. 115 Gral";
  |ABRE #0;
  |LEE 141#0p;
  |SI FSalida ! 0;
      |DDEFECTO #0;
      #0#2   = any;
      |GRABA 001#0b;
  |FINSI;
  |PINPA #0#0;
  |HAZ noexfi; || controlar si existe ficha

  |PINDA #0#0;
  |ENDATOS #2#0;
  |SI FSalida = 0;
      |GRABA 001#0a;
      |HAZ reo115g;
  |FINSI;
  |CIERRA #0;
|FPRO;

|| ***********************************************************************
|| ****************** CREAR FICHERO DE MODELO 115 ************************
|| ***********************************************************************
|PROCESO encuentra;
  ccc = #11#1;
  |ERROR10;
|FPRC;

|DEFBUCLE busca_115;
 #11#2;
 0,134;
 cclau, #0#0, #0#2;
 cclau, #0#0, #0#2;
 be;
 NULL, encuentra;
|FIN;

|PROCESO lineas;
  cclau = #1#7;
  |SI #0#9 = "N";
      cta   = #1#7 %  112;
      alfa1 = #1#7 % 1301;
      dig = alfa1;
      |HAZ leepro;
      cclau = nif;
  |SINO;
      cta   = #1#127 %  112;
      alfa1 = #1#127 % 1301;
      dig = alfa1;
  |FINSI;
  ccc = 0;
  |BUCLE busca_115;
  |SI ccc = 0;
      ult_cont = ult_cont + 1;
      ccc = ult_cont;
  |FINSI;

  #11#0   = #0#0;
  #11#1   = ccc;
  #11#134 = #0#2;
  |LEE 101#11=;
  |SI FSalida ! 0;
      |DDEFECTO #11;
      #11#0   = #0#0;
      #11#1   = ccc;
      #11#134 = #0#2;
      #11#12  = 1;
      #11#13  = 2;
      |HAZ datospro;
      |GRABA 020#11b;
  |FINSI;

||  #11#35 = 0; #11#48 = 0; #11#61 = 0; #11#74 = 0;
  |PARA meses = 1; |SI meses [ 12;  |HACIENDO meses = meses + 1;
       campobas1 = meses +  22;
       camporet1 = meses +  35;
       campobas2 = meses +  48;
       camporet2 = meses +  61;
       #11campobas1 = #11campobas1 + #1campobas1;  || total bases  1
       #11camporet1 = #11camporet1 + #1camporet1;  || total reten. 1
       #11campobas2 = #11campobas2 + #1campobas2;  || total bases  2
       #11camporet2 = #11camporet2 + #1camporet2;  || total reten. 2
       #11#35 = #11#35 + #1campobas1;  || total bases  1
       #11#48 = #11#48 + #1camporet1;  || total reten. 1
       #11#61 = #11#61 + #1campobas2;  || total bases  2
       #11#74 = #11#74 + #1camporet2;  || total reten. 2
  |SIGUE;
  #11#9  = #0#26;
  |SI #0#33 ! "X"; #11#10 = #0#48; |FINSI;
  |GRABA 020#11a;
  |LIBERA #11;
|FPRC;

|DEFBUCLE escribe;
 #1#1;
 ;
 #0#0, "               ";
 #0#0, "zzzzzzzzzzzzzzz";
 e;
 NULL,lineas;
|FIN;

|| ***************** PROCESO PARA BUSCAR RETENCION
|| *********************************************** (AQUI)
|PROCESO si_hay;
 alfa2 = #0#25; |QBF alfa2;
 alfa3 = alfa2 % 0;
 nume1 = alfa3; nume1 = 100 + nume1;
 alfa1 = #7#4 % nume1;

 |SI alfa1 = alfa2;
     retencion = retencion + #7#9;
 |FINSI;
|FPRC;

|DEFBUCLE busca_reten;
 #7#1;
 ;
 #2#45,#2#46,#2#47, 0001;
 #2#45,#2#46,#2#47, 9999;
 e;
 NULL, si_hay;
|FIN;

|| ***********************************************************************
|| ********************* CREAR FICHERO DE TRABAJO ************************
|| ***********************************************************************
|PROCESO sumacomp;
  sien = 0;
  tipo0 = 1;
|| .................................... Seleccion de Compras Dinerarias
  |SI #0#10 = "S";
      swgr = 0; concepto = #2#6;
      |SI #0#11 ! 0; grupo = #0#11; |HAZ vergrupo; |FINSI;
      |SI #0#12 ! 0; |Y swgr = 0; grupo = #0#12; |HAZ vergrupo; |FINSI;
      |SI #0#13 ! 0; |Y swgr = 0; grupo = #0#13; |HAZ vergrupo; |FINSI;
      |SI #0#14 ! 0; |Y swgr = 0; grupo = #0#14; |HAZ vergrupo; |FINSI;
      |SI swgr = 1;
          sien = 1;
      |FINSI;
  |SINO;
      |SI #0#3 = "S";
          |SI #2#6 ] #0#4;  |Y #2#6 [ #0#5;
              sien = 1;
          |FINSI;
      |SINO;
          sien = 0;
          |PARA desde = 15;  |SI desde [ 24;  |HACIENDO desde = desde + 1;
                |SI #0desde = 0;  |SAL;  |FINSI;
                |SI #2#6 = #0desde;
                    sien = 1;
                    |SAL;
                |FINSI;
          |SIGUE;
      |FINSI;
  |FINSI;
  |SI sien = 1; |VETE finsel; |FINSI;
|| .................................../ Fin tipo 1

  sien = 0;
  tipo0 = 2;
|| .................................... Seleccion de Compras en Especies
  |SI #0#33 = "X"; |VETE finsel; |FINSI;

  |SI #0#33 = "S";
      swgr = 0; concepto = #2#6;
      |SI #0#34 ! 0; grupo = #0#34; |HAZ vergrupo; |FINSI;
      |SI #0#35 ! 0; |Y swgr = 0; grupo = #0#35; |HAZ vergrupo; |FINSI;
      |SI #0#36 ! 0; |Y swgr = 0; grupo = #0#36; |HAZ vergrupo; |FINSI;
      |SI #0#37 ! 0; |Y swgr = 0; grupo = #0#37; |HAZ vergrupo; |FINSI;
      |SI swgr = 1;
          sien = 1;
      |FINSI;
  |SINO;
      |SI #0#30 = "S";
          |SI #2#6 ] #0#31;  |Y #2#6 [ #0#32;
              sien = 1;
          |FINSI;
      |SINO;
          sien = 0;
          |PARA desde = 38;  |SI desde [ 47;  |HACIENDO desde = desde + 1;
                |SI #0desde = 0;  |SAL;  |FINSI;
                |SI #2#6 = #0desde;
                    sien = 1;
                    |SAL;
                |FINSI;
          |SIGUE;
      |FINSI;
  |FINSI;
|| .................................../ Fin tipo 2

|ET finsel;

  |SI sien = 0;
      |ACABA;  || NO es concepto elegido.
  |FINSI;

  |SI #0#9 = "S";
      cclau = #2#51;
  |SINO;
      alfa2 = #2#5; |QBF alfa2;
      alfa2 = ("0" + alfa2) % -101;
      alfa1 = #2#4;
      alfa1 = (alfa1 + "            ") % 112;
      cclau = alfa1 + alfa2;
  |FINSI;

  #1#0 = #0#0;
  #1#7 = cclau;
  |LEE 101#1=;
  |SI FSalida ! 0;
      |DDEFECTO #1;
      #1#0 = #0#0;
      #1#127 = #2#4 + #2#5;
      #1#3 = #2#50;
      #1#7 = cclau;
      |GRABA 020#1b;
  |FINSI;

  alfa1 = #2#3;
  alfa1 = alfa1 % 0402;
  nume1 = alfa1;
  campobas1 = nume1 +  22;
  camporet1 = nume1 +  35;
  campobas2 = nume1 +  48;
  camporet2 = nume1 +  61;

  totbas = #2#8 + #2#9 + #2#10 + #2#52 + #2#53;

  |SI tipo0 = 1; #1campobas1 = #1campobas1 + totbas; |FINSI; ||  bases  1
  |SI tipo0 = 2; #1campobas2 = #1campobas2 + totbas; |FINSI; ||  bases  2

  retencion = 0;
  |SI #0#27 = "S";
      |SI tipo0 = 1; retencion = ( totbas % #0#26) ! EURODB; |FINSI;
      |SI tipo0 = 2; retencion = ( totbas % #0#48) ! EURODB; |FINSI;
  |SINO;
      |BUCLE busca_reten;
  |FINSI;

  |SI tipo0 = 1; #1camporet1 = #1camporet1 + retencion; |FINSI; ||  reten. 1
  |SI tipo0 = 2; #1camporet2 = #1camporet2 + retencion; |FINSI; ||  reten. 2

  |GRABA 020#1a;
  |LIBERA #1;
|FPRC;

|| ............... ver grupo;
|PROCESO vergrupo;
 swgr = 0;
 |ABRE #13;
 #13#0 = grupo;
 |LEE 001#13=;
 |SI FSalida = 0;
     |PARA x = 2; |SI x [ 21; |HACIENDO x = x + 1;
           |SI #13x  = concepto;
               swgr = 1;
               |SAL;
            |FINSI;
     |SIGUE;
 |FINSI;
 |CIERRA #13;
|FPRC;
|| ---------------------------------------------------

|DEFBUCLE compras;
 #2#1;
 3;
 00, 000001, "  ", fech1;
 99, 999999, "zz", fech2;
 e;
 NULL, sumacomp;
|FIN;

|| ***********************************************************************
|PROCESO acero;
 |PARA meses = desde; |SI meses [ hasta;  |HACIENDO meses = meses + 1;
       campobas1 = meses +  22;
       camporet1 = meses +  35;
       campobas2 = meses +  48;
       camporet2 = meses +  61;
       #11#35 = #11#35 - #11campobas1;
       #11#48 = #11#48 - #11camporet1;
       #11#61 = #11#61 - #11campobas2;
       #11#74 = #11#74 - #11camporet2;
       #11campobas1 = 0;  || total bases  1
       #11camporet1 = 0;  || total reten. 1
       #11campobas2 = 0;  || total bases  2
       #11camporet2 = 0;  || total reten. 2
 |SIGUE;
 ult_cont = #11#1;
 |GRABA 020#11a;
 |LIBERA #11;
|FPRC;

|DEFBUCLE borrar_acum;
 #11#1;
 ;
 #0#0, 00001, #0#2;
 #0#0, 99999, #0#2;
 be;
 NULL, acero;
|FIN;

|PROCESO elpase;
 |HAZ nom_usu;
 alfa1 = "x" + nom_tmp;
 |NOME_DAT #1 alfa1;
 |ABRE #1;
 |CIERRA #1;
 |DELINDEX #1;

  ult_cont = 0;
  desde = 1;
  |SI #0#28 = 2; desde =  4; |FINSI;
  |SI #0#28 = 3; desde =  7; |FINSI;
  |SI #0#28 = 4; desde = 10; |FINSI;
  hasta = 3;
  |SI #0#29 = 2; hasta =  6; |FINSI;
  |SI #0#29 = 3; hasta =  9; |FINSI;
  |SI #0#29 = 4; hasta = 12; |FINSI;
  |BUCLE borrar_acum;

  |SI #0#2 [ 80;
      nume2 = 2000 + #0#2;
  |SINO;
      nume2 = 1900 + #0#2;
  |FINSI;
  alfa3 = nume2; |QBF alfa3;
  alfa3 = ("0000" + alfa3) % -104;

  alfa1 = desde; |QBF alfa1;
  alfa1 = ("00" + alfa1) % -102;
  alfa2 = "01." + alfa1 + "." + alfa3;
  fech1 = alfa2;

  alfa1 = hasta; |QBF alfa1;
  alfa1 = ("00" + alfa1) % -102;
  alfa4 = "30."; |SI hasta = 3; |O hasta = 12; alfa4 = "31."; |FINSI;
  alfa2 = alfa4 + alfa1 + "." + alfa3;
  fech2 = alfa2;

  |ABRE #1;
  |BUCLE compras;  || lee el fichero de Compras
  |CIERRA #1;

  |ABRE #4;
  |ABRE #6;
  |SI #0#6 ="S"; |Y aste ! "*"; |ABRE #12;  |FINSI;

  |ABRET #11;
  |BUCLE escribe;
  |CIERRAT #11;

  |CIERRA #1;
  |DELINDEX #1;

  |CIERRA #4;
  |CIERRA #6;
  |SI #0#6 ="S"; |Y aste ! "*"; |CIERRA #12;  |FINSI;
|FPRC;

|| ***********************************************************************
|PROCESO reo115g;
  any = #0#2;
  |PUSHV 2102 2378;
  |ATRI I;
  |CUADRO 2102 2378
  |ATRI N;
  |ATRI R;
  |PINTA 2203," EMPRESA:                                                                  ";
  |ATRI 0;
  |SI #0i_cm1 = "S";
      |BUCLE esdesde;
  |SINO;
      |HAZ esunaauna;
  |FINSI;
|FPRC;

|| ******************************** PROCESO
|PROCESO haztodo;
 enEmpresa = #canempre#2;
 enPeriodo = #canempre#3;
 swOperacion = 0;
 |SUB_EJECUTA "camoneda";

 |PATH_DAT #2 dirfich0;
 |PATH_DAT #4 dirfich0;
 |PATH_DAT #7 dirfich0;
 |PATH_DAT #11 dirfich0;
 |PATH_DAT #13 dirfich0;

  |ATRI R;
  |PINTA 2213, #30#2;
  |PINTA 2219, #30#3;
  |PINTA 2221, #30#1;
  |ATRI 0;

  #0#0 = #30#2;
  #0#1 = #30#1;
  |HAZ elpase;
|FPRC;

|| **************************************************************************
|| ........ Lecturas de clientes / proveedores
|PROCESO leepro;
 nif = "";
 #4#23 = "P";
 |HAZ clipro;
 |SI swclipro = 0;
     #4#23 = "C";
     |HAZ clipro;
 |FINSI;
 |SI swclipro = 1;
     nif = #4#9;      ||dni
 |FINSI;
|FPRC;

|PROCESO clipro;
  swclipro = 0;
  #4#10 = cta;
  #4#11 = dig;
  |LEE 001#4=;
  |SI FSalida = 0;
      swclipro = 1;
  |FINSI;
|FPRC;

|| ----------------------------------------------------
|PROCESO datospro;
  |SI #0#6 = "N"; |O aste = "*";
      |HAZ leeclipro;
      |ACABA;
  |FINSI;

  #12#0 = cclau;
  |LEE 001#12=;
  |SI FSalida = 0;
      #11#7 = #12#0;      || Nif
      #11#3 = #12#1;      || Nombre
      traba = #12#2;     || SG
      |QBF traba;
      |SI traba = "";
          #11#127 = #12#3;   || Domicilio
      |SINO;
          #11#127 = traba + " " + #12#3; || Sg. + Domicilio
      |FINSI;
      #11#128 = #12#4;  || Numero
      #11#130 = #12#5;  || Piso
      #11#131 = #12#6;  || Puerta
      #11#5   = #12#7;  || C.P.1
      #11#6   = #12#8;  || C.P.2
      #11#4   = #12#10; || Localidad
      #11#132 = #12#9;  || Provincia
  |SINO;
      |HAZ leeclipro;
  |FINSI;
|FPRC;

|PROCESO leeclipro;
 #4#23 = "C";
 |HAZ clipro;
 |SI swclipro = 0;
     #4#23 = "P";
     |HAZ clipro;
 |FINSI;
 #11#7   = cclau;
 #11#2   = #0#1;      ||nombre Empresa

 #11#3   = #1#3;      ||nombre
 |SI swclipro = 1; || lee el nombre del Plan Contable
      #11#7   = #4#9;      ||dni
      #11#3   = #4#1;      ||nombre
      #11#127 = #4#2;      || Domicilio
      #11#5   = #4#3;  || C.P.1
      #11#6   = #4#4;  || C.P.2
      #11#4   = #4#6;  || Localidad
      #11#132 = #4#5;  || Provincia
 |FINSI;
|FPRC;

|| -------------------------------------------------------------------------
|PROCESO tipo_8; || controlar si existe ficha clientes
  |DIRECTORIO agi;
  |QBF agi;
  |PATH_DAT #23 agi;
  |ABRE #23;
  #23#0 = 1;
  |LEE 040#23=;
  |SI FSalida ! 0; aste = "*"; |CIERRA #23; |ACABA; |FINSI;
  |CIERRA #23;
  aste = #23#7;
  |QBF aste;
  |SI aste  = "*"; |ACABA; |FINSI;
  agi1 = #23#8; |QBF agi1;
  lonagi = agi1 % 0;
  num   = lonagi;
  num   = 100 + (num - 5);
  lonagi = num;
  agi2  = (agi1 % lonagi) + "pan/";
  |PATH_DAT #12 agi1;

  |HAZ inicio;
  any = #30#26;
  |HAZ coloca;
  #0#2 = any;
|FPRC;

|PROCESO noexfi;
  |SI aste ! "*"; |ACABA; |FINSI;
  #0#6 = "N";
  |CAMPO_MODIFICA #0#6, 1,"N";
|FPRC;
