|FICHEROS;
    caextrp2 #4;         || display extractos
    cacuenta #5;         || cuentas
    caextrp4 #6;         || display asientos completo
    camaasil #7;         || apuntes lins.
    caextras #8;         || fichero anot.extracontables
    caextral #9;         || anot.extracontables Lineas
    caexttip #10;        || Tipo anot.extracontables
|FIN;

|VARIABLES;
    alfa1 = "";
    alfa2 = "";
    alfa3 = "";
    alfa4 = "";
    nume1 = 0;
    nume2 = 0;
    nume3 = 0;
    nume4 = 0;
    fech1 = @;
    fech2 = @;
    fech3 = @;
    fech4 = @;
    FEstado = 0;
    inkey = 0;
    seleccio = 0;

    &d_abajo = 0;
    &h_abajo = 0;
    &s_abajo = 0;
    lalinea = 0;
    totallin = 0;
|FIN;

|PROCESO pintando; |TIPO 13;
  inkey = FSalida;
  |HAZ totales;
  |SI inkey =  9;  |HAZ asiento;   |FINSI;
  |SI inkey = 10;  |HAZ extraco;   |FINSI;
|FPRC;
_____________________________________________________________________________
|PROCESO totales;                  || PINTA LOS TOTALES DEL EXTRACTO
  #4#14 = d_abajo;
  #4#15 = h_abajo;
  #4#16 = s_abajo;
  |PINTA #4#14;
  |PINTA #4#15;
  |PINTA #4#16;
|FPRC;
_____________________________________________________________________________
|PROCESO lee_asiento1;             || CARGA LA PANTALLA DEL ASIENTO COMPLETO
    seleccio = 1;
|HAZ cuenta;
|DDEFECTO #6;
    #6#00 = #7#3;        || apunte
    #6#01 = #7#16;       || cuenta DEBE
    #6#02 = #7#17;       || digito DEBE
    #6#03 = #7#10;       || cuenta HABER
    #6#04 = #7#11;       || digito HABER
    #6#05 = #7#6;        || concepto
    #6#06 = #7#7;        || descripcion
    #6#07 = #7#21;       || departamento
    #6#08 = #7#9;        || importe
    #6#09 = #5#2;        || descripcion
    #6#10 = #5#51;       || debe
    #6#11 = #5#52;       || haber
    #6#12 = #5#53;       || saldo
    #6#13 = #7#0;        || diario
    #6#14 = #7#1;        || fecha
    #6#15 = #7#2;        || documento
    |SI #6#0 = #4#19;
        lalinea = #6#0;
    |FINSI;
    |GRABA 020#6n;
    totallin = totallin + 1;
|FPRC;

|DEFBUCLE lee_asiento;
 #7#1;
 ;
 #4#18, #4#8, #4#9,    1;
 #4#18, #4#8, #4#9, 9999;
 e;
 NULL, lee_asiento1;
|FIN;

|PROCESO asiento;
  seleccio = 0;
  |DELINDEX #6;
  |ABRE #6;
  lalinea = 1;
  totallin = 0;
  |BUCLE lee_asiento;
  |CIERRA #6;
  |SI seleccio ! 0;
      |PUSHV 07012380;
      |ENTLINEAL #1#6, 1, 4, 20, 1, mini, maxi;
      |POPV;
  |SINO;
      |MENAV "    Seleccion vacia ...";
  |FINSI;
  |DELINDEX #6;
|FPRC;

|PROCESO mini;
  |SI FSalida = "POSICION";  |Y totallin > 8;
      #6#0 = lalinea;
  |SINO;
      #6#0 = 1;
  |FINSI;
|FPRC;

|PROCESO maxi;
    #6#0 = 9999;
|FPRC;

|PROCESO cuenta;
|ABRE #5;
|SELECT #1#5;
    #5#0 = #7#4;         || cuenta
    #5#1 = #7#5;         || digito
|LEE 001#5=;
|SI FSalida ! 0;
    |DDEFECTO #5;
        #5#2 = "<CUENTA INEXISTENTE>";
|FINSI;
|CIERRA #5;
|FPRC;
_____________________________________________________________________________

|PROCESO extraco;             || CARGA LA PANTALLA DE LA ANOT.EXTRACONTABLE
|ABRE #8;
    #8#0 = #4#18;        || diario
    #8#1 = #4#8;         || fecha
    #8#2 = #4#9;         || documento
|LEE 001#8=;
|SI FSalida = 0;
    |PUSHV 08092067;
    |PINPA #1#4;
    |ATRI I; |PINTA 0927, #8#0;
             |PINTA 0937, #8#1;
             |PINTA 0959, #8#2;
             |PINTA 1128, #8#3;
    |ATRI 0;
    |HAZ mod_campos;
    |ENTLINEAL #1#9, 4, 4, 18, 0, minA, maxA;
    |POPV;
|SINO;
    |MENAV "    Asiento sin Anotaciones Extracontables ...";
|FINSI;
|CIERRA #8;
|FPRC;

|RUTINA minA;
 #9#0 = #8#0;
 #9#1 = #8#1;
 #9#2 = #8#2;
 #9#3 = 1;
|FRUT;

|RUTINA maxA;
 #9#0 = #8#0;
 #9#1 = #8#1;
 #9#2 = #8#2;
 #9#3 = 9999;
|FRUT;

|PROCESO mod_campos;
 |C_M #9#4,1,"N";
 |C_P #9#3, 1, 1311;
 |C_P #9#4, 1, 1316;
 |ABRE #10;
 #10#0 = #8#3;
 |LEE 001#10=;
 |SI FSalida = 0;
     |PINTA 1131, #10#1;
 |FINSI;
 |CIERRA #10;
|FPRC;
