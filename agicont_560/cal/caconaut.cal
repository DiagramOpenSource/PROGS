|FICHEROS;
   caconaut #0;
   camaasil #1;
   caselemp #2;
   caselem1 #3;
   cacuenta #4;
   caconaul #5;

   zconcil1 #6;

|FIN;

|INCLUYE acceso_i;
|INCLUYE puntos_i;

|VARIABLES;
   Comodin  = "";
   Comodin1 = "";

   &entrada = 1;
   Cont = 0;
   Calc1 = 0;
   Calc2 = 0;
   Parte1 = "";
   Parte2 = "";
   LongAlfa = "";
   Long = 0;
   ComodNum = 0;
   MaxDigitos = 0;
   Nivel = 0;

   SwError = 0;
   Cuenta = "";

   contador = 0;
   importe = 0;
   Signo = "";
   TotDebe = 0;
   TotHabe = 0;
   HaySaldo = 0;
   Linea = 0;

   DCuenta = "";
   HCuenta = "";

   CtasConcil = 0; SwConcil = 0; LinsConcil = 0;
|FIN;

|PROCESO Camaasil;
     contador = contador + 1;
     #caconaul#0 = contador;
     #caconaul#1 = #camaasil#0;
     #caconaul#2 = #camaasil#1;
     #caconaul#3 = #camaasil#2;
     #caconaul#4 = #camaasil#3;
     #caconaul#5 = #camaasil#7;
     |SI #camaasil#8 = "D";
         #caconaul#6 = #camaasil#9;
         #caconaul#7 = 0;
     |SINO;
         #caconaul#7 = #camaasil#9;
         #caconaul#6 = 0;
     |FINSI;
     TotDebe = TotDebe + #caconaul#6;
     TotHabe = TotHabe + #caconaul#7;
     #caconaul#9 = TotDebe - TotHabe;
     |SI #caconaul#9 = 0;   HaySaldo = HaySaldo + 1;  |FINSI;
     #caconaul#8 = #camaasil#18;
     |GRABA 040#caconaul.n;
|FPRC;

|DEFBUCLE Camaasil;
     #camaasil#3;
     18;
     #cacuenta#0, #cacuenta#1, "01.01.1900", 000000, " ";
     #cacuenta#0, #cacuenta#1, "31.12.2999", 999998, " ";
     e;
     NULL, Camaasil;
|FIN;

|PROCESO BuscaImporte;
  |SI #caconaul#6 = 0;
     importe = #caconaul#7;
     Signo = "H";
  |SINO;
     importe = #caconaul#6;
     Signo = "D";
  |FINSI;

  #zconcil1#0 = importe;
  #zconcil1#1 = Signo;
  #zconcil1#3 = " ";
  #zconcil1#2 = 0;
  |LEE 010#zconcil1.];
  |SI FSalida = 0;
     |SI #zconcil1#0 = importe; |Y #zconcil1#1 = Signo; |Y #zconcil1#3 = " ";
        #zconcil1#3 = "C";
        |GRABA 020#zconcil1.a;
        Linea = #caconaul#0;
        #caconaul#0 = #zconcil1#2;
        |LEE 010#caconaul.=;
        |SI FSalida = 0;
           #caconaul#8 = "*";
           |GRABA 020#caconaul.a;
        |FINSI;
        #caconaul#0 = Linea;
        |LEE 010#caconaul.=;
        |SI FSalida = 0;
           #caconaul#8 = "*";
           |GRABA 020#caconaul.a;
        |FINSI;
     |SINO;
        #zconcil1#0 = importe;
        |SI Signo = "D"; Signo = "H"; |SINO; Signo = "D"; |FINSI;
        #zconcil1#1 = Signo;
        #zconcil1#2 = #caconaul#0;
        #zconcil1#3 = " ";
        |GRABA 020#zconcil1.n;
     |FINSI;
  |SINO;
     #zconcil1#0 = importe;
     |SI Signo = "D"; Signo = "H"; |SINO; Signo = "D"; |FINSI;
     #zconcil1#1 = Signo;
     #zconcil1#2 = #caconaul#0;
     #zconcil1#3 = " ";
     |GRABA 020#zconcil1.n;
  |FINSI;
|FPRC;

|DEFBUCLE BuscaImporte;
#caconaul#1;
;
1;
9999;
;
NULL,BuscaImporte;
|FIN;

|PROCESO Actualiza;
   |SI #0#25 = "I";
      |SI #caconaul#8 = " "; |ACABA; |FINSI;
   |FINSI;

   #camaasil#0 = #caconaul#1;
   #camaasil#1 = #caconaul#2;
   #camaasil#2 = #caconaul#3;
   #camaasil#3 = #caconaul#4;
   |LEE 010#camaasil.=;
   |SI FSalida = 0;
      #camaasil#18 = "û";
      |GRABA 020#camaasil.a;
      LinsConcil = LinsConcil + 1;
      SwConcil = 1;
   |FINSI;
|FPRC;

|DEFBUCLE Actualiza;
#caconaul#1;
;
1;
9999;
;
NULL,Actualiza;
|FIN;

|PROCESO Importe;
   |BUCLE BuscaImporte;
   |BUCLE Actualiza;
|FPRC;

|PROCESO LineasConci;
   |HAZ Actualiza;

   |SI #caconaul#9 = 0;
      HaySaldo = HaySaldo - 1;
   |FINSI;
   |SI HaySaldo = 0; |ERROR10; |FINSI;

|FPRC;

|DEFBUCLE LineasConci;
#caconaul#1;
;
1;
9999;
;
NULL,LineasConci;
|FIN;

|PROCESO SaldoCero;
   |BUCLE LineasConci;
|FPRC;

|PROCESO Conciliacion;
   SwConcil = 0;
   |DELINDEX #caconaul;
   |DELINDEX #zconcil1;
   |ABRE #caconaul;
   |ABRE #zconcil1;
   TotDebe = 0; TotHabe = 0; importe = 0;

   |BUCLE Camaasil;
   |SI #0#25 = "I"; |HAZ Importe; |FINSI;
   |SI #0#25 = "S"; |Y HaySaldo ! 0; |HAZ SaldoCero; |FINSI;

   |SI SwConcil = 1; CtasConcil = CtasConcil + 1; |FINSI;

   |CIERRA #zconcil1;
   |CIERRA #caconaul;
|FPRC;

|DEFBUCLE Conciliacion;
#cacuenta#1;
;
DCuenta,0;
HCuenta,9;
be;
NULL,Conciliacion;
|FIN;

|PROCESO Empresas;
   |DFICB Comodin;

   Comodin1 = "00000" + #caselem1#1;
   Comodin1 = Comodin1 % -105;

   Comodin = Comodin + Comodin1 + #caselem1#2 + "/";

   |PATH_DAT #1 Comodin;
   |ABRE #camaasil;
   |BUCLE Conciliacion;
   |CIERRA #camaasil;
|FPRC;

|DEFBUCLE Empresas;
#caselem1#1;
;
000001;
999999;
;
NULL,Empresas;
|FIN;

|RUTINA inf1;
   #5#0 = "            ";
|FRUT;

|RUTINA sup1;
   #5#0 = "zzzzzzzzzzzz";
|FRUT;

|PROCESO Cuenta; |TIPO 6;

   |SI #0#0 = "S"; |Y Campo > 2; |ACABA; |FINSI;

   Comodin = #0#Campo#;
   |QBF Comodin;
   LongAlfa = Comodin % 0;
   Long = LongAlfa - 1;
   Comodin = Comodin - ".";
   |SI FSalida ! 0;
      Calc1 = (((FSalida / 100) ! 0) - 1) + 100;
      Parte1 = Comodin % Calc1;
      Calc2 = (((FSalida / 100) ! 0) * 100) + 99;
      Parte2 = Comodin % Calc2;
      Long = MaxDigitos - Long;
      Comodin = "0" * Long;
      Comodin = Parte1 + Comodin + Parte2;
      #0#Campo# = Comodin;
      |PINTA #0#Campo#;
   |FINSI;

|FPRC;

|PROCESO MiraSelec;
   |SI #0#0 = "S";
      |C_M #0#1,1,"S";
      |C_M #0#2,1,"S";
      |PARA Cont = 3; |SI Cont [ 23; |HACIENDO Cont = Cont + 1;
         |C_M #0#Cont#,1,"N";
      |SIGUE;
   |SINO;
      |C_M #0#1,1,"N";
      |C_M #0#2,1,"N";
      |PARA Cont = 3; |SI Cont [ 23; |HACIENDO Cont = Cont + 1;
         |C_M #0#Cont#,1,"S";
      |SIGUE;
   |FINSI;
|FPRC;

|PROCESO BuscaCuenta; |TIPO 6;
   salidas = FSalida;
   |HAZ punto;
   |SI p_error ! 0; |ERROR; |ACABA; |FINSI;
|FPRC;

|PROGRAMA;
   |CLS;
   |HAZ inicio;

   |SI dig9 ! 0;
      MaxDigitos = dig9;
      Nivel = 6;
   |SINO;
      |SI dig8 ! 0;
         MaxDigitos = dig8;
         Nivel = 5;
      |SINO;
         |SI dig7 ! 0;
            MaxDigitos = dig7;
            Nivel = 4;
         |SINO;
            |SI dig6 ! 0;
               MaxDigitos = dig6;
               Nivel = 3;
            |SINO;
               |SI dig5 ! 0;
                  MaxDigitos = dig5;
                  Nivel = 2;
               |SINO;
                  |SI dig4 ! 0;
                     MaxDigitos = dig5;
                     Nivel = 1;
                  |FINSI;
               |FINSI;
            |FINSI;
         |FINSI;
      |FINSI;
   |FINSI;

   #0#24 = Nivel;

   |CABEZA "Conciliacion automatica";
   |PINPA #0#0;
   |PINDA #0#0;
   |ENDATOS #1#0;
   |SI FSalida ! 0; |ACABA; |FINSI;

   |ABRE #camaasil;

   CtasConcil = 0;
   LinsConcil = 0;
   |SI #0#0 = "S";
      DCuenta = #0#1;
      HCuenta = #0#2;
      |BUCLE Conciliacion;
   |SINO;
      |PARA Cont = 3; |SI Cont [ 23; |HACIENDO Cont = Cont + 1;
         |SI #0#Cont# ! "            ";
            DCuenta = #0#Cont#;
            HCuenta = #0#Cont#;
            |BUCLE Conciliacion;
         |FINSI;
      |SIGUE;
   |FINSI;

   |CIERRA #camaasil;

   Comodin = "    Se ha efectuado la conciliacion de " + CtasConcil;
   Comodin = Comodin + " cuenta y " + LinsConcil + " lineas de apuntes";
   |MENAV Comodin;

|FPRO;
