|FICHEROS;
    c3ginfic #0;
    camocabe #1;
    camoline #2;
    camogrup #3;
|FIN;

|VARIABLES;
   &entrada = 1;
   informe = "cainffic";
   &tipo = "";
   &tiamoc = "";
   &tiamo = "";
   &valor = 0;
   &amorc = 0;
   &amor = 0;
   &resid = 0;
   &residc = 0;
   &cta1 = "";
   &cta2 = "";
   &am_ap ="";
   amo = "A";
   ulti = "";
   ene = "N";
   nueva = "NUEVA";
   usada = "USADA";
   norma = "NORMAL";
   acele = "ACELERADA";
   degre = "DEGRESIVA";
   alfa1 = "";
   alfa2 = "";
   &swfv=0;
   fecha=0;

   sw_error = 0;
   i_cm1 = 5;  || Campo Desde/Hasta Empresa
|FIN;

|INCLUYE acceso_i;
|INCLUYE puntos_i;
|INCLUYE numamo_i;
|INCLUYE multie_i;

|| *************************************************************************
|PROCESO numamo; |TIPO 0;
    x_alfa1 = #0Campo;
|HAZ x_punto;
    #0Campo = x_alfa1;
    |PINTA #0Campo;
|FPRC;

|PROCESO mayor;
   alfa1  = #0Campo > " ";
   #0Campo = alfa1;
   |PINTA #0Campo;
|FPRC;

|PROCESO cuenta; |TIPO 0;
    p_alfa1 = #0Campo;
    salidas = FSalida;
|HAZ punto;
|SI p_error ! 0;  |ERROR;  |ACABA;  |FINSI;
|FPRC;

|PROCESO tipo8;  |TIPO 8;
  |HAZ inicio;
  any = #30#26;
  |HAZ coloca;
|FPRC;

|| *****************************************************************
|PROCESO sel_multi;
|| |SI #30#26 ! any;  |ACABA; |FINSI;
  |SI #30#21 = "I"; |ACABA; |FINSI;
  sw_error = 0;
  leidos = 1;
|FPRC;

|| ---------- Seleccion DESDE / HASTA Empresa.
|PROCESO esdesd1;
  alfa1 = #30#2;
  alfa2 = #30#3;
  alfa1 = "00000" + alfa1;   alfa1 = alfa1 % -105;
  alfa2 = "0"   + alfa2;   alfa2 = alfa2 % -101;
  dirfich0 = dir_fich + alfa1 + alfa2 + "/";

  sw_error = 0;
  leidos = 0;
  |HAZ sel_multi;
  |SI leidos = 1; |Y sw_error = 0;
      |HAZ haztodo;
  |FINSI;
|FPRC;

|DEFBUCLE esdesde;
 #30#1;
 ;
 d_empres, d_period;
 h_empres, h_period;
 e;
 NULL,esdesd1;
|FIN;

|| ---------- Seleccion en parrilla.
|PROCESO esunaauna;
  |PARA xx = i_cm6; |SI xx [ i_cm7; |HACIENDO xx = xx + 1;
        |SI #0xx = 0;  |SAL;  |FINSI;
        yy = xx + 20;
        alfa1 = #0xx;
        alfa2 = #0yy;

        || ... lee empresa
           |ABRE #30;
           |DDEFECTO #30;
           #30#2 = #0xx;
           #30#3 = #0yy;
           |LEE 011#30=;
           |CIERRA #30;
           leidos = 0; sw_error = 0;
           |HAZ sel_multi;
           |SI leidos = 0; |O sw_error ! 0; |VETE otramas; |FINSI;
        || ...................

        alfa1 = "00000" + alfa1;   alfa1 = alfa1 % -105;
        alfa2 = "0"     + alfa2;   alfa2 = alfa2 % -101;
        dirfich0 = dir_fich + alfa1 + alfa2 + "/";

        |HAZ haztodo;
 |ET otramas;
  |SIGUE;
|FPRC;
|| *************************************************************************

|PROCESO imprelin;
  |SI #2#4 = amo;
    amor  = amor + #2#5;
    amorc = amorc + #2#9;
  |FINSI;
  |SI #2#4= "C";
      valor = valor + #2#5;
  |FINSI;
      resid = valor - amor;
      residc = valor - amorc;
  |PRINT;
|FPRC;

|PROCESO ctas_grup;
   cta1 = "            ";
   cta2 = "            ";
   #3#0 = #1#15;
   |LEE 001#3=;
   |SI FSalida ! 0; |ACABA; |FINSI;
   cta1 = #3#1;
   cta2 = #3#3;
|FPRC;

|PROCESO fechafac;
  |SI #0#4 = "N";
      |SI #1#22 = "N"; |ACABA; |FINSI;
  |FINSI;
  |HAZ ctas_grup; || lee el grupo para saber las cuentas de amort.
|| ----- cambiar literales para la ficha -----------------------------------
  tipo = usada;
       |SI #1#8 = ene; tipo = nueva; |FINSI;
  tiamo = norma;
       |SI #1#10 = "D"; tiamo = degre; |FINSI;
       |SI #1#10 = "A"; tiamo = acele; |FINSI;
  tiamoc = norma;
       |SI #1#16 = "D"; tiamoc = degre; |FINSI;
       |SI #1#10 = "A"; tiamoc = acele; |FINSI;
  am_ap = "Anual";
       |SI #1#21 = "P"; am_ap = "Periodica"; |FINSI;
  swfv=0;
  fecha=#1#23;
  |SI fecha ] 0; swfv=1; |FINSI;
  valor = 0;
  amorc = 0;
  amor  = 0;
  residc = 0;
  resid = 0;
  |BUCLELIN 2imprelin#1;
  |PIEINF;
|FPRC;

|DEFBUCLE cainffic0;
 #1#4;
 0;           || 0 = campo elemento
 #0#0,0,#0#2; || indica campos de clave + elemento para seleccionar
 #0#1,9,#0#3;
 e;
 NULL,fechafac;
|FIN;

|| **********************************************************************
|PROCESO impre; |TIPO 3;

  |IMPRE 0;
  |SI FSalida ! 0;  |ACABA;  |FINSI;
  |INFOR informe;
  |SI FSalida ! 0; |FINIMP; |ACABA; |FINSI;

  |SI #0i_cm1 = "S";
      |BUCLE esdesde;
  |SINO;
      |HAZ esunaauna;
  |FINSI;
  |FININF;
  |FINIMP;
|FPRC;

|| ................................................................
|PROCESO haztodo;
  enEmpresa = #canempre#2;
  enPeriodo = #canempre#3;
  swOperacion = 0;
  |SUB_EJECUTA "camoneda.tab";

  |PATH_DAT #1 dirfich0;
  |PATH_DAT #2 dirfich0;
  |PATH_DAT #3 dirfich0;

  |ABRE #3;
  |BUCLE cainffic0;
  |CIERRA #3;
|FPRC;
