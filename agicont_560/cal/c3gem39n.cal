|FICHEROS;
  c3gem39n #0;
  cama3390 #7;
  caportad #2;
  caem3390 #10;
  canempre #30;
|FIN;

|VARIABLES;
       empcon = 0;
       ejecon = 0;
       leidos = 0;
       dirfich0 = "";
       emp1 = 0;
       per1 = 0;
       xx = 0;
       yy = 0;
       anu = 0;
       desde = 0;
       hasta = 0;
       x = 0;
       y = 0;
       varcamp = 0;
       &r_emp = 0;
       &r_per = 0;
     alfa1 = "";
     alfa2 = "";
     nume1 = 0;
     longi = 0;
     &dia = "";
     &mes = "";
     mesm = 0;
     &any = "";
     &anyo = "";
     &op50 ="";
     fecsys = @;
     men9 = "     Empresa Contable INACTIVA ";
     sw_impre  = 0;
|FIN;

|INCLUYE acceso_i;

|| *************************************************************************
||                        CALCULOS DE PANTALLA
|| *************************************************************************

|PROCESO alentrar;  |TIPO 8;
  |HAZ inicio;
|FPRC;

|PROCESO defecto; |TIPO 7;
  #0#0 = cod2;
  |PINTA #0#0;
|FPRC;

|PROCESO consulta;
  |SI FSalida ! 9;  |ACABA;  |FINSI;
  |SUB_EJECUTA "caconemp.run";
  |ERROR;
  |SI Campo = 5;
      #0#5 = r_emp;
      #0#7 = r_per;
      |PTEC 802;
      |PTEC 802;
  |FINSI;
  |SI Campo = 6;
      #0#6 = r_emp;
      #0#8 = r_per;
      |PTEC 802;
  |FINSI;
  |SI Campo ] 9;
      #0Campo = r_emp;
      varcamp = Campo + 20;
      #0varcamp = r_per;
      |PTEC 802;
      |PTEC 802;
  |FINSI;
|FPRC;

|PROCESO mayor;
  alfa1 = #0Campo > " ";
  #0Campo = alfa1;
  |PINTA #0Campo;
|FPRC;

|PROCESO mayor1;
  |HAZ mayor;
  |SI #0Campo = "N";
      |PARA x = 9;  |SI x [ 48;  |HACIENDO x = x + 1;
            |CAMPO_MODIFICA #0x,1,"S";
      |SIGUE;
      #0#5 = 0;
      #0#7 = 0;
      #0#6 = 0;
      #0#8 = 0;
      |CAMPO_MODIFICA #0#5,1,"N";
      |CAMPO_MODIFICA #0#7,1,"N";
      |CAMPO_MODIFICA #0#6,1,"N";
      |CAMPO_MODIFICA #0#8,1,"N";
      |PINTA #0#5;
      |PINTA #0#7;
      |PINTA #0#6;
      |PINTA #0#8;
  |SINO;
      |PARA x = 9;  |SI x [ 48;  |HACIENDO x = x + 1;
            #0x = 0;
            |CAMPO_MODIFICA #0x,1,"N";
      |SIGUE;
      |CAMPO_MODIFICA #0#5,1,"S";
      |CAMPO_MODIFICA #0#7,1,"S";
      |CAMPO_MODIFICA #0#6,1,"S";
      |CAMPO_MODIFICA #0#8,1,"S";
  |FINSI;
|FPRC;

|| --------------- comprueba desde-hasta empresas ---------------------------

|PROCESO buscaem1;
  |SI #30#21 = "I"; |ACABA; |FINSI;

  |SI #30#26 ! #0#0;
      swerror = 3;
      |ACABA;
  |FINSI;

  |DBASE dir_fich;
  dirfich0 = dir_fich + "fich/";
  alfa1 = #30#2;
  alfa2 = #30#3;
  emp1 = #30#2;
  per1 = #30#3;
  alfa1 = "00000" + alfa1;   alfa1 = alfa1 % -105;
  alfa2 = "0"   + alfa2;   alfa2 = alfa2 % -101;
  dirfich0 = dirfich0 + alfa1 + alfa2 + "/";
  |PATH_DAT #7 dirfich0;
  |PATH_DAT #2 dirfich0;
  |ABRE #7;
  #7#0 = #30#2;
  #7#1 = #0#0;
  |LEE 001#7=;
  |SI FSalida ! 0;
      swerror = 1;
      |CIERRA #7;
      |ACABA;
  |FINSI;
  |CIERRA #7;
  |ABRE #2;
  #2#0 = #30#2;
  #2#1 = #30#26;
  |LEE 001#2=;
  |SI FSalida ! 0;
      swerror = 2;
      |CIERRA #2;
      |ACABA;
  |FINSI;
  |CIERRA #2;
  leidos = 1;
|FPRC;

|DEFBUCLE buscaempre;
#30#1;
;
#0#5,desde;
#0#6,hasta;
e;
NULL,buscaem1;
|FIN;

|PROCESO buclempre;
  |SI #0#7 > #0#8;
      |MENAV "     ERROR EN INTRODUCCION";
      |ERROR;
      |ACABA;
  |FINSI;
  |SI #0#5 = 0;  |Y #0#6 = 0;  |ACABA;  |FINSI;
  desde = #0#7;
  hasta = #0#8;
  leidos = 0;
  |BUCLE buscaempre;
  |SI leidos = 0;
      |MENAV "    ATENCION SELECCION VACIA";
      |ERROR;
      |ACABA;
  |FINSI;
|FPRC;

|PROCESO deshas;
  |SI #0#6 < #0#5;
      |MENAV "      Error en Introduccion";
      |ERROR;
  |FINSI;
|FPRC;

|| ------------------------------------------------------------------------

|PROCESO nomodi;
      |PARA x = Campo + 1;  |SI x [ 28;  |HACIENDO x = x + 1;
            #0x = 0;
            |CAMPO_MODIFICA #0x,1,"N";
            y = x + 20;
            #0y = 0;
            |CAMPO_MODIFICA #0y,1,"N";
      |SIGUE;
      y = Campo + 20;
      #0y = 0;
      |CAMPO_MODIFICA #0y,1,"N";
      |PINTA #0y;
|FPRC;

|PROCESO esbuena; || desde campo de empresas seleccion
  |SI FSalida = 2; |ACABA; |FINSI;
  |SI #0Campo = 0; |HAZ nomodi;  |ACABA;  |FINSI;
  |ABRE #30;
  #30#2 = #0Campo;
  #30#3 = 0;
  |LEE 011#30];
  |SI FSalida ! 0;  |O #30#2 ! #0Campo;
      |MENAV "     NO Existe la empresa Contable";
      |ERROR;
  |FINSI;
  |CIERRA #30;
|FPRC;

|PROCESO esbuena1;
  |SI FSalida = 2;|ACABA;|FINSI;
  x = Campo - 20;
  |SI #0x = 0;  |ACABA;  |FINSI;
  |ABRE #30;
  x = Campo - 20;
  #30#2 = #0x;
  #30#3 = #0Campo;
  |LEE 011#30=;
  |SI FSalida ! 0;
      |MENAV "     NO Existe Periodo en la empresa Contable";
      |CIERRA #30;
      |ERROR;
      |ACABA;
  |FINSI;
  |CIERRA #30;
  |SI #30#21 = "I";
      |MENAV men9;
      |ERROR;
      |ACABA;
  |FINSI;
  leidos = 0;
  |HAZ buscaem1;
  |SI leidos = 0;
      |SI swerror = 1;
          |MENAV "     No existe Modelo 390";
          |FINSI;
      |SI swerror = 2
          |MENAV "0000No se encuentra el fichero de control Datos Fijos";
          |FINSI;
      |SI swerror = 3;
          |MENAV "     El a¤o contable NO corresponde ";
          |FINSI;
      |ERROR;
      |ACABA;
  |FINSI;
  x = Campo + 20;
  #0x = #30#1;
  |PINTA #0x;
  x = Campo - 20;
  alfa1 = #0x;
  alfa2 = #0Campo;
|FPRC;

|| **********************************************************************
||                       CALCULOS DE IMPRESION
|| **********************************************************************

|| ---------- Seleccion DESDE / HASTA Empresa.

|PROCESO esdesd1;
  |DBASE dir_fich;
  dirfich0 = dir_fich + "fich/";
  alfa1 = #30#2;
  alfa2 = #30#3;
  emp1 = #30#2;
  per1 = #30#3;
  alfa1 = "00000" + alfa1;   alfa1 = alfa1 % -105;
  alfa2 = "0"   + alfa2;   alfa2 = alfa2 % -101;
  dirfich0 = dirfich0 + alfa1 + alfa2 + "/";

  |SI #30#26 ! #0#0; |O #30#21 = "I";
      |ACABA;
  |FINSI;

  |HAZ haztodo;
|FPRC;


|DEFBUCLE esdesde;
#30#1;
;
#0#5,#0#7;
#0#6,#0#8;
e;
NULL,esdesd1;
|FIN;


|| ---------- Seleccion en parrilla.

|PROCESO esunaauna;
      |PARA xx = 9;  |SI xx [ 28;  |HACIENDO xx = xx + 1;
            |SI #0xx = 0;  |SAL;  |FINSI;
            |DBASE dir_fich;
            dirfich0 = dir_fich + "fich/";
            yy = xx + 20;
            emp1 = #0xx;
            per1 = #0yy;
            alfa1 = #0xx;
            alfa2 = #0yy;
            alfa1 = "00000" + alfa1;   alfa1 = alfa1 % -105;
            alfa2 = "0"   + alfa2;   alfa2 = alfa2 % -101;
            dirfich0 = dirfich0 + alfa1 + alfa2 + "/";
              || lee empresa
                 |ABRE #30;
                 #30#2 = emp1;
                 #30#3 = per1;
                 |LEE 011#30=;
                 |CIERRA #30;
              |SI #30#26 ! #0#0; |O #30#21 = "I";
                 |VETE otramas;
              |FINSI;
            |HAZ haztodo;
      |ET otramas;
      |SIGUE;
|FPRC;

|| *********************************************************************
||                    COMIENZO con TIPO 3
|| *********************************************************************

|PROCESO impre; |TIPO 3;
  #10#0 = #0#0;
  #10#1 = #0#1;
  #10#2 = #0#69;

  |SI #0#69 ! "L";
      |IMPRE 0;
      |SI FSalida ! 0; |ACABA; |FINSI;
  |FINSI;

  |SI #0#4 = "S";
      |BUCLE esdesde;
  |SINO;
      |HAZ esunaauna;
  |FINSI;
  |SI #0#69 ! "L";
      |FINIMP;
  |SINO;
      |HAZ GrabaDisketteL;
  |FINSI;
|FPRC;

|PROCESO haztodo;
  enEmpresa = #canempre#2;
  enPeriodo = #canempre#3;
  swOperacion = 0;
  |SUB_EJECUTA "camoneda.tab";

  |PATH_DAT #7 dirfich0;
  |PATH_DAT #2 dirfich0;
  anu = 1900 + #30#26;
  |SI anu [ 1970;  anu = anu + 100;  |FINSI;
  empcon = #30#1;
  ejecon = #30#26;

     |HAZ fecha;

     |ABRE #7;
     #7#0 = #30#2;
     #7#1 = #0#0;
     |LEE 101#7=;
     |SI FSalida ! 0; |CIERRA #7; |ACABA; |FINSI;
     |ABRE #2;
     #2#0 = #30#2;
     #2#1 = #30#26;
     |LEE 001#2=;
     |SI FSalida ! 0; |CIERRA #7; |CIERRA #2; |ACABA; |FINSI;

  |SI #0#69 = "L";
      |HAZ Laser;
  |SINO;
      |SI #0#69 = "O"; |HAZ impre95;   |FINSI;
      |SI #0#69 = "B"; |HAZ impblanco; |FINSI;
  |FINSI;

  |CIERRA #2;
  |LIBERA #7;
  |CIERRA #7;
|FPRC;

|PROCESO Laser;
 |HAZ hazLaser;
|FPRC;

|PROCESO fecha;
     dia = #0#1 % 102;
     any = #0#1 % 704;
     anyo = any;
     mes = #0#1 % 402;
     mesm = mes;
     |SI mesm = 1;  mes = "ENERO";      |FINSI;
     |SI mesm = 2;  mes = "FEBRERO";    |FINSI;
     |SI mesm = 3;  mes = "MARZO";      |FINSI;
     |SI mesm = 4;  mes = "ABRIL";      |FINSI;
     |SI mesm = 5;  mes = "MAYO";       |FINSI;
     |SI mesm = 6;  mes = "JUNIO";      |FINSI;
     |SI mesm = 7;  mes = "JULIO";      |FINSI;
     |SI mesm = 8;  mes = "AGOSTO";     |FINSI;
     |SI mesm = 9;  mes = "SEPTIEMBRE"; |FINSI;
     |SI mesm = 10; mes = "OCTUBRE";    |FINSI;
     |SI mesm = 11; mes = "NOVIEMBRE";  |FINSI;
     |SI mesm = 12; mes = "DICIEMBRE";  |FINSI;
|FPRC;

|| ***********************************************************************
|PROCESO impblanco;
  |INFOR "caeb139n";       ||Primera y Segunda Hoja del 390
  |SI FSalida = 0;
      |PRINT;
      |PIEINF;
      |FININF;
  |FINSI;

  |INFOR "caeb339n";       ||Tercera hoja del 390
  |SI FSalida = 0;
      |PRINT;
      |PIEINF;
      |FININF;
  |FINSI;

  |INFOR "caeb539n";       ||Quinta Hoja del 390
  |SI FSalida = 0;
      |PRINT;
      |PIEINF;
      |FININF;
  |FINSI;
|FPRC;
|| ---------------------------Imprimir Nuevo Cuadre

|PROCESO hoja1_95;
  |SI #0#0 = 97;   |INFOR "ca973901";  |FINSI;
  |SI #0#0 ] 98;   |INFOR "ca983901";  |FINSI;
  |SI FSalida ! 0; sw_impre = 1; |FININF; |ACABA; |FINSI;

  |PRINT;
  |PIEINF;
  |FININF;
|FPRC;

|PROCESO hoja2_95;
  |SI #0#0 = 97;   |INFOR "ca973902";  |FINSI;
  |SI #0#0 ] 98;   |INFOR "ca983902";  |FINSI;
  |SI FSalida ! 0; sw_impre = 1; |FININF; |ACABA; |FINSI;

  sw_impre = 0;

  |PRINT;
  |PIEINF;
  |FININF;
|FPRC;

|PROCESO hoja3_95;
  |SI #0#0 = 97;   |INFOR "ca973903";  |FINSI;
  |SI #0#0 ] 98;   |INFOR "ca983903";  |FINSI;
  |SI FSalida ! 0; sw_impre = 1; |FININF; |ACABA; |FINSI;

  sw_impre = 0;

  |PRINT;
  |PIEINF;
  |FININF;
|FPRC;

|PROCESO hoja4_95;
  |SI #0#0 = 97;   |INFOR "ca973904";  |FINSI;
  |SI #0#0 ] 98;   |INFOR "ca983904";  |FINSI;
  |SI FSalida ! 0; sw_impre = 1; |FININF; |ACABA; |FINSI;

  sw_impre = 0;

  |PRINT;
  |PIEINF;
  |FININF;
|FPRC;

|PROCESO hoja5_95;
  |SI #0#0 = 97;   |INFOR "ca973905";  |FINSI;
  |SI #0#0 ] 98;   |INFOR "ca983905";  |FINSI;
  |SI FSalida ! 0; sw_impre = 1; |FININF; |ACABA; |FINSI;

  sw_impre = 0;

  |PRINT;
  |PIEINF;
  |FININF;
|FPRC;

|PROCESO impre95;
  |SI sw_impre = 0; |HAZ hoja1_95; |FINSI;
  |SI sw_impre = 0; |HAZ hoja2_95; |FINSI;
  |SI sw_impre = 0; |HAZ hoja3_95; |FINSI;
  |SI sw_impre = 0; |HAZ hoja4_95; |FINSI;
  |SI sw_impre = 0; |HAZ hoja5_95; |FINSI;

  #7#3 = #0#1;
  #7#4 = "N";
  |GRABA 020#7a;
  |LIBERA #7;
|FPRC;
