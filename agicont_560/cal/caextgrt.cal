|FICHEROS;
    caextgrt #0;
    camaasil #1;
    camaasic #2;
    cacuenta #3;
    cacongru #6;
|FIN;

|VARIABLES;
    &entrada = 1;
    men1   = "    NO existe Grupo de Conceptos!";
    men2  = "     Longitud de Cuenta Fuera de Nivel !";
    mensa1 = "    ERROR ESTA FECHA NO CORRESPONDE AL EJERCICIO";
    x = 0;
    y = 0;
    informe = "caextgru";
    long = "  ";
    cta  = 0;
    num   = 0;
    swdig = 0;
    alfa1 = "";
    nume1 = 0;
    nume2 = 0;

  &cuenta = "";
  &asiento = 0;  || datos del informe por linea
  &debe = 0;
  &haber = 0;
  &saldo = 0;
  &t_debe  = 0; || totales por cuenta
  &t_haber = 0;
  &t_saldo = 0;
  &t_degru = 0;
  &t_hagru = 0;
  &gdebe   = 0;
  &ghaber  = 0;
  &gsaldo  = 0;

  &grup1 = 0;
  &grup2 = "";

  sw_con = 0;          || 1 = EL CONCEPTO ES BUENO
  sw_print = 0;        || 1 = NO DEBO ESCRIBIR
  sw_prim = 0;         || 0 = PRIMERA VEZ
  sw_pagina = 0;

  &swant = 0;
  &sw_inf = 0;
  &sw_cab = 0;
  &pagina = 0;

|FIN;

|INCLUYE acceso_i;
|INCLUYE puntos_i;

|| *********************************************************************
||                             COMIENZO
|| *********************************************************************
|PROCESO mayus;
  #0Campo = #0Campo > " ";
  |PINTA #0Campo;
  |CAMPO_MODIFICA #0#13,1,"S";
  |CAMPO_MODIFICA #0#14,1,"S";
  |PARA nume1 = 15; |SI nume1 [ 38; |HACIENDO nume1 = nume1 + 1;
        |CAMPO_MODIFICA #0nume1,1,"N";
  |SIGUE;
  |SI #0Campo = "N";
      |CAMPO_MODIFICA #0#13,1,"N"; #0#13 =  1; |PINTA #0#13;
      |CAMPO_MODIFICA #0#14,1,"N"; #0#14 = 99; |PINTA #0#14;
      |PARA nume1 = 15; |SI nume1 [ 38; |HACIENDO nume1 = nume1 + 1;
            |CAMPO_MODIFICA #0nume1,1,"S";
      |SIGUE;
  |FINSI;
|FPRC;

|PROCESO saltagrupo;
  |SI FSalida = 2; |ACABA; |FINSI;
  |CAMPO_MODIFICA #0Campo,0,alfa1;
  |SI alfa1 = "N"; |ACABA; |FINSI;
  |PARA nume1 = Campo + 1; |SI nume1 [ 38; |HACIENDO nume1 = nume1 + 1;
        |CAMPO_MODIFICA #0nume1,1,"S";
  |SIGUE;
  |SI #0Campo = 0;
      |PARA nume1 = Campo + 1; |SI nume1 [ 38; |HACIENDO nume1 = nume1 + 1;
            |CAMPO_MODIFICA #0nume1,1,"N";
            #0nume1 = 0; |PINTA #0nume1;
      |SIGUE;
  |FINSI;
|FPRC;

|PROCESO empieza; |TIPO 7;
    #0#2 = fec1;
    #0#3 = fec2;
    |PINTA #0#2;
    |PINTA #0#3;
|FPRC;

|PROCESO cuenta; |TIPO 6;
  p_alfa1 = #0Campo;
  salidas = FSalida;
  |HAZ punto;
  |SI p_error ! 0;  |ERROR;  |ACABA;  |FINSI;
  |SI #0Campo = "            ";  |ERROR;  |ACABA;  |FINSI;
  x = Campo + 4;
  cta  = #0Campo;
  long = cta % 0;
  swdig = 0;
  |SI long = #30#14; swdig = 1; #0x = 1; |FINSI;
  |SI long = #30#15; swdig = 1; #0x = 2; |FINSI;
  |SI long = #30#16; swdig = 1; #0x = 3; |FINSI;
  |SI long = #30#17; swdig = 1; #0x = 4; |FINSI;
  |SI long = #30#18; swdig = 1; #0x = 5; |FINSI;
  |SI long = #30#19; swdig = 1; #0x = 6; |FINSI;
  |SI swdig = 0;  |MENAV men2;  |ERROR;  |FINSI;
|FPRC;

|PROCESO fechad;
  |SI #0#2 < fec1; |O #0#2 > fec2;
      |MENAV mensa1;
      |ERROR;
      |ACABA;
  |FINSI;
|FPRC;

|PROCESO fechah;
  |SI #0#3 < fec1; |O #0#3 > fec2; |O #0#3 < #0#2;
      |MENAV mensa1;
      |ERROR;
      |ACABA;
  |FINSI;
|FPRC;

|PROCESO no_dep; |TIPO 7;
    alfa1 = #30#24;
|CAMPO_MODIFICA #0#6, 1, alfa1;
|CAMPO_MODIFICA #0#7, 1, alfa1;
|FPRC;

|PROGRAMA;
  |CLS;
  |CABEZA "Extracto Total Grupo ";
  |HAZ inicio;

  FSalida = 1;
  |PARA;|SI FSalida = 1; |HACIENDO;
        sw_prim = 0;
        sw_print = 0;
        |DDEFECTO #0;
        |PINPA #0#0;
        |PINDA #0#0;                    || Pinta los datos por defecto
        |ENDATOS #1#0;                  || Seleccion informe
        |SI FSalida = 0;                || Si el conforme es igua a S
            |HAZ impre;                 || Calculo impresion
            FSalida = 1;                || Vuelve al bucle
        |SINO;
            FSalida = 0;
        |FINSI;
  |SIGUE;
|FPRO;

|| *************************************************************************
||                               MEOLLO
|| *************************************************************************

|PROCESO cabecera;
  #2#0 = #1#0;
  #2#1 = #1#1;
  #2#2 = #1#2;
  |LEE 040#2=;
  |SI FSalida ! 0;
      |DDEFECTO #2;
  |FINSI;
  asiento = #2#3;
|FPRC;

|PROCESO campogrupo;
  nume2 = nume2 + 1;
  |SI nume2 > 24; |ERROR10; |FINSI;
  y = nume2 + 14;
  #0y = #6#0;
|FPRC;

|DEFBUCLE vergrupo;
 #6#1;
 ;
 #0#13;
 #0#14;
 e;
 NULL, campogrupo;
|FIN;

|PROCESO congru;  || Para sumar en el lugar adecuado

  |PARA nume2 = 15; |SI nume2 [ 38; |HACIENDO nume2 = nume2 + 1;

        |SI #0nume2 = 0; |VETE fingrupo; |FINSI;

        #6#0 = #0nume2;
        |LEE 001#6=;
        |SI FSalida ! 0; |VETE fingrupo; |FINSI;

        |PARA x = 2; |SI x [ 21; |HACIENDO x = x + 1;
              |SI #6x  = #1#6;
                  sw_con = 1;
                  |SI #1#8 = "D";
                      y = nume2 + 24;
                  |SINO;
                      y = nume2 + 48;
                  |FINSI;
                  #0y = #0y + #1#9;
                  |SAL;
              |FINSI;
        |SIGUE;
|ET fingrupo;
 |SIGUE;
|FPRC;

|PROCESO leecuen;
  #3#0 = #1#4;
  #3#1 = #1#5;
  |LEE 040#3=;
  |SI FSalida ! 0; |DDEFECTO #3; |FINSI;
  sw_pagina = 0;
  sw_print = 0;
  swant = 0;
  t_debe = 0;
  t_haber = 0;
  t_saldo = 0;
  gdebe   = 0;
  ghaber  = 0;
  gsaldo  = 0;
  |PDEFECTO #0,39,87;
|FPRC;

|PROCESO buclepan;
  |SI sw_prim = 0;
      cuenta = #1#4;
      sw_prim  = 1;
      |HAZ leecuen;
  |FINSI;

  |SI cuenta ! #1#4;
      |SI sw_print = 1;
          |HAZ impr_totales;
          |PIEINF;
      |FINSI;
      cuenta = #1#4;
      |HAZ leecuen;
  |FINSI;

  |SI #1#8 = "D";
      debe = #1#9;
      haber = 0;
  |SINO;
      debe = 0;
      haber = #1#9;
  |FINSI;
  saldo = t_saldo + (debe - haber);

  |SI #1#1 < #0#2;
      swant = 1;
      |VETE suma_totales;
  |FINSI;

  |SI #1#1 ] #0#2; |Y #1#1 [ #0#3;
      |HAZ cabecera;
      sw_inf = 1;
      |PRINT;
      sw_print = 1;
      swant = 1;
  |FINSI;

|ET suma_totales;
 t_debe  = t_debe  + debe;
 t_haber = t_haber + haber;
 t_saldo = t_debe - t_haber;
 |HAZ congru; || Para sumarlo en la columna adecuada
|FPRC;

|DEFBUCLE caextrim0;
 #1#3;
 0, 21;
 #0#0, 0, fec1, 000000, #0#8, #0#6;
 #0#1, 9, #0#3, 999999, #0#9, #0#7;
 e;
 NULL, buclepan;
|FIN;

|PROCESO impre;
  |IMPRE 0;
  |SI FSalida ! 0; |ACABA; |FINSI;

  |INFOR "caextgrt";

  |SI FSalida ! 0; |FINIMP; |ACABA; |FINSI;

  nume2 = 0;
  |BUCLE vergrupo; || Cargar grupos en Pantalla
  |ABRE #2;
  |ABRE #3;
  |ABRET #6;
  |BUCLE caextrim0;
  |CIERRA #2;
  |CIERRA #3;
  |SI sw_print = 1;
      |HAZ impr_totales;
      |PIEINF;
  |FINSI;
  |CIERRA #2;
  |CIERRA #3;
  |CIERRAT #6;
  |FININF;
  |FINIMP;
|FPRC;

|PROCESO &c_pagina;
  |SI sw_pagina = 0;
      pagina = 1;
      sw_pagina = 1;
  |SINO;
      pagina = pagina + 1;
  |FINSI;
|FPRC;

|PROCESO impr_totales;
  sw_inf = 2; || linea totales
  |PRINT;
  gdebe = 0; ghaber = 0;
  |PARA nume2 = 15; |SI nume2 [ 38; |HACIENDO nume2 = nume2 + 1;

        |SI #0nume2 = 0; |VETE fintgru; |FINSI;
        #6#0 = #0nume2;
        |LEE 001#6=;
        |SI FSalida ! 0; |VETE fintgru; |FINSI;
        y = nume2 + 24;
        x = nume2 + 48;
        grup1 = #6#0;
        grup2 = #6#1;
        t_degru = #0y;
        t_hagru = #0x;
        |SI t_degru ! 0; |O t_hagru ! 0;
            sw_inf = 3;
            |PRINT;
            gdebe  = gdebe  + t_degru;
            ghaber = ghaber + t_hagru;
            gsaldo = gdebe - ghaber;
        |FINSI;
|ET fintgru;
  |SIGUE;
|FPRC;
