|FICHEROS;
 cared002 #0;
 cared001 #1;  || Control Pase Secuencial
 caremesc #2;  || Remesas Cabeceras
 caremesl #3;  || Remesas Lineas
 caclipro #4;  || Clientes
 camancob #5;  || Cobros

 camoneda #7;
|FIN;

|VARIABLES;
 &EURO = 0;
 &EURODB = 0;
 &EURODV = 0;
 Cantidad = 0;
 Moneda = "";
 MonBase = 0;

 alfa1 = "";
 alfa2 = "";
 alfa3 = "";
 nume1 = 0;
 nume2 = 0;

 mens     = "";
 swErrorR = 0;
 swErrorP = 0;
 menR1    = "    No Existe Remesa !!!";
 menR2    = "    Remesa YA contabilizada ";
 menR3    = "2400SRemesa YA Emitida en Papel. Aceptar S/N ";
 menR4    = "2400NRemesa YA lanzada en Diskette. Aceptar S/N ";

 NombreP = "                                        ";
 NifP    = "000000000000";
 sufiP   = "000";
 EntP    = "0000";
 SucP    = "0000";
 banAlfa = "           ";
 NombreO = "                                        ";
 NifO    = "000000000";
 sufiO   = "000";
 EntO    = "0000";
 SucO    = "0000";
 dcO     = "00";
 CtaO    = "0000000000";
 LocO    = "                                      ";
 ProP    = "00";
 ConCli  = "";
 sino     = "";
 ceros    = "000000000000000";
 fechapre = "      ";
 fechacar = "      ";
 dd       = "";
 mm       = "";
 aa       = "";
 tiProced = "06";
 cabe1    = "170";  || Registro Presentador
 cabe2    = "370";  || Registro Ordenante
 cabe3    = "670";  || Registro Individual
 cabe4    = "676";  || Registro Individual NO domiciliado
 tord1    = "870";  || Total Ordenante
 tgen1    = "970";  || Total General
 Prefijo  = "";

 libre6   = "      ";
 libre8   = "        ";
 libre10  = "          ";
 libre12  = "            ";
 libre16  = "                ";
 libre20  = "                    ";
 libre38  = "                                      ";
 libre43  = "                                           ";
 libre52  = "                                                    ";
 libre72  = "                                                                        ";

 ca1  = "";
 ca2  = "";
 ca3  = "";
 ca4  = "";
 ca5  = "";
 ca6  = "";
 ca7  = "";
 ca8  = "";
 ca9  = "";
 ca10 = "";
 l          = 0;
 trecibos   = 0;
 timporte   = 0;

 handle     = "";
 fiche      = "";
 empresa    = "";
 varalfa1   = "";
 contenido  = "";
 swERROR    = 0;
 CampoDni   = "";
 LetrasNif  = "TRWAGMYFPDXBNJZSQVHLCKE";
 Letra1     = "";
 Letra2     = "";
 Letra3     = "";
 Letra4     = "";
 Letra5     = "";
 Letra6     = "";
 Letra7     = "";
 Letra8     = "";
 Caracter   = "";
 Carac1     = 0;
 Carac2     = 0;
 Carac3     = 0;
 Carac4     = 0;
 Carac5     = 0;
 XPara      = 0;

 ccc        = "";
 c_alfa1    = "";
 c_alfa2    = "";
 c_nume1    = 0;
 c_nume2    = 0;
 c_nume3    = 0;

 swdomi = 0;
 traba  = "";
 nume   = 0;
 &programa = "";
 &ficcopia = "";

 Calc = 0;
|FIN;

|| *************************************************************************

|PROCESO mayus1;
 alfa1 = #0Campo > " ";
 #0Campo = alfa1;
 |PINTA #0Campo;
|FPRC;

|PROCESO ver_remesa; |TIPO 6;
 |SI FSalida ! 0; |ACABA; |FINSI;
 |ABRE #2;
 |HAZ leerem;
 |CIERRA #2;
 |SI swErrorR ! 0;
     |SI swErrorR < 3;
         |MENAV mens;
         |ERROR;
         |ACABA;
     |SINO;
         |CONFI mens;
         |SI FSalida ! 0;
             |ERROR;
             |ACABA;
         |FINSI;
     |FINSI;
 |FINSI;
 #0#2 = #2#2; |PINTA #0#2;
 #0#3 = #2#1; |PINTA #0#3;
 |PINTA 0935, #2#1;
 |PINTA 1035, #2#2;
 |PINTA 0958, #2#5;
 |PINTA 1049, #2#10;
 |HAZ lee_pase;
|FPRC;

|PROCESO leerem;
 swErrorR = 0;
 |DDEFECTO #2;
 #2#0 = #0#1;
 |LEE 001#2=;
 |SI FSalida ! 0;
     swErrorR = 1; mens = menR1;
 |SINO;
     |SI #2#9 = "S";
         swErrorR = 2; mens = menR2;
     |SINO;
         |SI #2#8 = "S"; swErrorR = 3; mens = menR3; |FINSI;
         |SI #2#8 = "D"; swErrorR = 4; mens = menR4; |FINSI;
     |FINSI;
 |FINSI;
|FPRC;

|PROCESO lee_pase; |TIPO 6;
 |HAZ leepas;
 |ATRI I;
 |PINTA 1220, #1#1;
 |ATRI 0;
|FPRC;

|PROCESO leepas;
 swErrorP = 0;
 |ABRE #1;
 |DDEFECTO #1;
 #1#0 = #0#2;
 |LEE 001#1=;
 |SI FSalida ! 0; swErrorP = 1; |FINSI;
 |CIERRA #1;
|FPRC;

||...................................................................................
||***********************************************************
||************ REGISTRO DE CABECERA DE PRESENTADOR **********
||***********************************************************
|PROCESO cabecera_p;
  varalfa1 = "";
  varalfa1 =  Prefijo + cabe1 + NifP + fechapre + libre6 + NombreP;
  varalfa1 =  varalfa1 + libre20 + EntP + SucP;
  varalfa1 =  varalfa1 + libre12 + banAlfa + libre43;

  contenido = handle + " " + varalfa1 + &#0#7 + &#0#8;
  |FGRABA contenido;

  ||   l = l + 1; .. este registro no suma hasta el final
|FPRC;

||***********************************************************
||************ REGISTRO DE CABECERA DE ORDENANTE ************
||***********************************************************

|PROCESO cab_ordenante;
  varalfa1 = "";
  varalfa1 =  Prefijo + cabe2 + NifO + fechapre + fechacar + NombreO;
  varalfa1 =  varalfa1 + EntO + SucO + dcO + CtaO;
  varalfa1 =  varalfa1 + libre8 + "06";
  varalfa1 =  varalfa1 + libre10 + banAlfa + libre43;

  contenido = handle + " " + varalfa1 + &#0#7 + &#0#8;
  |FGRABA contenido;

  l = l + 1;
|FPRC;

||***********************************************************
||******************* REGISTRO INDIVIDUAL *******************
||***********************************************************

|PROCESO leecl_y_cobro;
 || .............. Lee la ficha del Cliente para obtener los datos necesarios
 |DDEFECTO #4;
 #4#23 = "C";
 #4#10 = #3#11;
 #4#11 = #3#12;
 |LEE 001#4=;
 |SI FSalida ! 0;
     #4#23 = "P";
     #4#10 = #3#11;
     #4#11 = #3#12;
     |LEE 001#4=;
     |SI FSalida ! 0; swERROR = 1; |FINSI;
 |FINSI;

 |DDEFECTO #5;
 #5#0 = #3#2;
 #5#1 = #3#3;
 #5#2 = #3#4;
 #5#3 = #3#5;
 |LEE 001#5=;
|FPRC;

|PROCESO grabarecib; ||Calculo registros individuales;
  swERROR = 0;

  |HAZ leecl_y_cobro;
  |SI swERROR = 1; |ACABA; |FINSI;   || Salta este Cobro. NO el resto

  ca1 = "";                 || Referencia Cliente
  |SI #0#4 = "S";
      ca1 = #4#9;
      CampoDni = ca1; |HAZ LetraDni; ca1 = CampoDni;
  |SINO;
      ca1 = #4#0;
  |FINSI;

  |QBF ca1;
  |SI ca1 = ""; swERROR = 1; |FINSI; || ..... |ERROR10; |ACABA; |FINSI;
  ca1 = ceros + ca1; ca1 = ca1 % -112;

  ca2 = #4#1 + libre43;                                     || Nombre Cliente
  ca2 = ca2 % 140;

  ccc = #5#20; |QBF ccc;  |HAZ cccBanco;         || Datos del banco del Cobro
  swdomi = 0;
  |SI ccc = ""; swdomi = 1; |FINSI; || ....... |ERROR10; |ACABA; |FINSI;
  traba = ccc % 0;
  nume = traba;
  |SI nume ! 10;  swdomi = 1;  |FINSI;
  ca3 = ceros + ccc; ca3 = ca3 % -110;            || C.C.C.

  ccc = #5#19; |QBF ccc;   |HAZ cccBanco;
  |SI ccc = ""; swdomi = 1; |FINSI; || ....... |ERROR10; |ACABA; |FINSI;
  traba = ccc % 0;
  nume = traba;
  |SI nume ! 10;  swdomi = 1;  |FINSI;
  ca4 = ceros + ccc; ca4 = ca4 % -110;            || Cuenta corriente  Cliente

  Cantidad = #3#7; |HAZ Conversion;
  ca5 = Cantidad; |QBF ca5;
  ca5 = ceros + ca5; ca5 = ca5 % -110;            || Importe

  ca6 = ConCli + " ";
  |SI #1#17 = "S";
      alfa1 = #3#4; |QBF alfa1;
      ca6 = ca6 + alfa1;
      alfa1 = #3#3; |QBF alfa1;
      ca6 = ca6 + alfa1 + " ";
  |FINSI;
  |SI #1#18 = "S";
      alfa1 = #3#5; |QBF alfa1;
      ca6 = ca6 + alfa1 + " ";
  |FINSI;
  |SI #1#19 = "S";
      alfa1 = #5#6; |QBF alfa1;
      ca6 = ca6 + alfa1 + " ";
  |FINSI;
|| ..........  |SI #1#20 = "S"; alfa1 = #5#28; |QBF alfa1; ca6 = ca6 + alfa1; |FINSI; Referencia de la 6.00
  ca6 = ca6 + libre43; ca6 = ca6 % 140;            || Concepto

  alfa2 = #3#6;
  ca7   = alfa2 % 102;
  ca7   = ca7 + (alfa2 % 402);
  ca7   = ca7 + (alfa2 % 902);

  ca8   = #4#2; ca8 = ca8 + libre43; ca8 = ca8 % 140;  || Domicilio
  ca9   = #4#6; ca9 = ca9 + libre43; ca9 = ca9 % 135;  || Poblacion

  alfa2 = #4#3; |QBF alfa2; alfa2 = (ceros + alfa2) % -102;
  alfa3 = #4#4; |QBF alfa3; alfa3 = (ceros + alfa3) % -103;
  ca10  = alfa2 + alfa3;                                     || Codigo

  || ... Registro Individual Obligatorio (0670) ........................
  |SI swdomi = 1;
      ca3 = "          ";
      ca4 = "          ";
  |FINSI;
  varalfa1 = "";

  varalfa1 = Prefijo + cabe3 + NifO + ca1 + ca2 + ca3 + ca4 + ca5;
  varalfa1 = varalfa1 + #0#5 + #0#6 + ca6 + ca7 + "  ";

  contenido = handle + " " + varalfa1 + &#0#7 + &#0#8;
  |FGRABA contenido;

  trecibos = trecibos + 1;
  l = l + 1;
  timporte = timporte + Cantidad;

  || ... Registro Individual Obligatorio (0676) ........................
  |SI swdomi = 1;  |O #1#23 = "S";
      varalfa1 = "";
      varalfa1 = Prefijo + cabe4 + NifO + ca1 + ca8 + ca9 + ca10 + LocO + ProP;
      varalfa1 = varalfa1 + fechapre + libre8;
      contenido = handle + " " + varalfa1 + &#0#7 + &#0#8;
      |FGRABA contenido;
      l = l + 1;
      swdomi = 0;
  |FINSI;
|FPRC;

|PROCESO regindiv;
  trecibos = 0;
  timporte = 0;
  |BUCLELIN 0grabarecib#2;
  |SI swERROR = 1;
      |MENAV "    ATENCION !!!. DATOS DE CLIENTES INCOMPLETOS. ";
||       |MENAV "    ATENCION !!!. DATOS DE CLIENTES INCOMPLETOS. OPERACION ABORTADA ";
  |FINSI;
  |SI trecibos ! 0;
      #2#8 = "D";
      |GRABA 020#2a;
      |LIBERA #2;
  |FINSI;
|FPRC;

||***********************************************************
||********** REGISTRO TOTAL ORDENANTE Y GENERAL *************
||***********************************************************
|PROCESO campo_totales;

|| **** REGISTRO TOTAL ORDENANTE

  ca1 = timporte; |QBF ca1;
  ca1 = ceros + ca1; ca1 = ca1 % -110;

  ca2 = trecibos; |QBF ca2;
  ca2 = ceros + ca2; ca2 = ca2 % -110;

  l = l + 1;

  ca3 = l; |QBF ca3;
  ca3 = ceros + ca3; ca3 = ca3 % -110;

  varalfa1 = "";
  varalfa1 = Prefijo + tord1 + NifO + libre72 + ca1 + libre6;
  varalfa1 = varalfa1 + ca2 + ca3 + libre38;

  contenido = handle + " " + varalfa1 + &#0#7 + &#0#8;
  |FGRABA contenido;

|| **** REGISTRO TOTAL GENERAL

  l = l + 2; || Sumamos este ultimo y el primero.

  ca3 = l; |QBF ca3;
  ca3 = ceros + ca3; ca3 = ca3 % -110;   || Numero total de Registros

  ca4 = "0001";                          || Numero de Ordenantes

  varalfa1 = Prefijo + tgen1 + NifP + libre52 + ca4 + libre16;
  varalfa1 = varalfa1 + ca1 + libre6 + ca2 + ca3 + libre38;

  contenido = handle + " " + varalfa1 + &#0#7 + &#0#8;
  |FGRABA contenido;

|FPRC;

|| **************************************************************************

|PROCESO pasecuencial; |TIPO 3;

  |ABRE #2;
  |HAZ leerem;
  |SI swErrorR = 1; |O swErrorR = 2;
      |MENAV "    ERROR !!! REMESA NO CORRECTA ";
      |ACABA;
  |FINSI;

  |HAZ leepas;
  |SI swErrorP ! 0;
      |MENAV "    ERROR !!! CONTROL DE PASE NO CORRECTO ";
      |ACABA;
  |FINSI;

  |HAZ varia_fich;

  |CAMPO_VISUAL #0#10,1,"S"; #0#10 = 0;
  |CAMPO_VISUAL #0#11,1,"S"; #0#11 = 0;
  |CAMPO_VISUAL #0#12,1,"S"; #0#12 = 0;

  |ABRE #camoneda;
  #camoneda#0 = #48#2;
  #camoneda#1 = #48#3;
  |LEE 000#camoneda.=;
  |SI FSalida = 0;
     Moneda = #camoneda#2;
     |SI #camoneda#2 = "P";
        MonBase = 1;
     |SINO;
        MonBase = 2;
     |FINSI;
  |SINO;
     Moneda = "P";
     MonBase = 1;
  |FINSI;
  |CIERRA #camoneda;

  |HAZ PideMoneda;

  |SI Moneda = "P"; Prefijo = "0"; |SINO; Prefijo = "5"; |FINSI;
  |SI MonBase = 1;
     EURO = 1 / 166.386;
     EURODB = 0;
     EURODV = 2;
  |SINO;
     EURO = 166.386;
     EURODB = 2;
     EURODV = 0;
  |FINSI;

|| ...................................................
  |ABRE #4;                || Abre Clientes / Proveedores
  |ABRE #5;                || Cobros

|| Abrir Fichero Secuencial

  |DFICE empresa;
  fiche = "wb  " + empresa + "casecrec.seq";  || "nomfiche.ext";
  |QBF fiche;
  |FABRE fiche;
  handle = FSalida;
  |SI FSalida ] 0;
      |HAZ cabecera_p;
      |HAZ cab_ordenante;
      |HAZ regindiv;
      ||   |SI swERROR = 0;
               |HAZ campo_totales;
      ||   |FINSI;
  |FINSI;
  contenido = handle + " " + &#0#9;
  |FGRABA contenido;

  FSalida = handle;
  |FCIERRA FSalida;

  Pos = -1;
  |CIERRA #2;
  |CIERRA #4;
  |CIERRA #5;

|| ...   |SI swERROR = 1; |ACABA; |FINSI;

  |D_CUADRO 1261, 2280;
  |ATRI R;
  |PINTA 1362, " Numero Recibos   ";
  |PINTA 1562, " Numero Registros ";
  |PINTA 1762, " Importe Total    ";
  |ATRI 0;
  |PINTA 1462, "                  ";
  |PINTA 1662, "                  ";
  |PINTA 1862, "                  ";
  |D_CUADRO 1961, 2280;
  |ATRI R;
  |PINTA 2062, " Desea grabar el  ";
  |PINTA 2162, " diskete ahora    ";
  |ATRI 0;

  #0#10 = trecibos;  |PINTA #0#10;
  #0#11 = l;         |PINTA #0#11;
  Calc = EURODB;
  |SI Moneda = "E";
     EURODB = 2;
     #0#12 = timporte / 100;
  |SINO;
     EURODB = 0;
     #0#12 = timporte;
  |FINSI;
  |PINTA #0#12;
  EURODB = Calc;

  programa = " REMESAS ";
  ficcopia = "recibos.seq";
  E_sup = 1;
  E_inf = "SNsn";
  sino  = "N";
  sino  = 2177 ? 1;
  |SI sino = "S"; |O sino = "s"; |CORRE "cared003.run"; |FINSI;
|FPRC;


|| ********** Rutinas Varias

|PROCESO LetraDni;
|QBF CampoDni;
Letra2 = CampoDni;
|QBT Letra2;

    Caracter = Letra2 % 101;
|SI Caracter!"A";|Y Caracter!"B";|Y Caracter!"C";|Y Caracter!"D";
     |Y Caracter!"E";|Y Caracter!"F";
     |Y Caracter!"G";|Y Caracter!"P";|Y Caracter!"Q";|Y Caracter!"S";|Y Caracter!"X";
        Letra4 = "";
        Letra3 = Letra2 % 0;
        Carac2 = Letra3;
    |PARA Carac5 = 1;  |SI Carac5 [ Carac2;  |HACIENDO Carac5 = Carac5 + 1;
            Carac3 = (Carac5 * 100) + 1;
            Letra3 = Letra2 % Carac3;
        |SI Letra3="0";|O Letra3="1";|O Letra3="2";|O Letra3="3";
                      |O Letra3="4";|O Letra3="5";|O Letra3="6";
                      |O Letra3="7";|O Letra3="8";|O Letra3="9";
                Letra4 = Letra4 + Letra3;
        |FINSI;
    |SIGUE;
        Carac4 = Letra4;
    |SI Carac4 ! 0; |Y Letra4 ! "";
            Carac1 = Carac4 $ 23;
            Carac1 = Carac1 + 1;
            Carac3 = (Carac1 * 100) + 1;
            Letra1 = LetrasNif % Carac3;

            Letra4 = "000000000000" + Letra4;
            Letra7 = Letra4 % -103;
            Letra6 = Letra4 % -403;
            Letra5 = Letra4 % -703;

            Letra8 = Letra5  % 101;
            |SI Letra8 = "0"; Letra5 = Letra5 % 202; |FINSI;

            Letra8 = Letra5  % 101;
            |SI Letra8 = "0"; Letra5 = Letra5 % 201; |FINSI;

            Letra8 = Letra5  % 101;
            |SI Letra8 = "0"; Letra5 = ""; |FINSI;

            |SI Letra5 ! "";
                Letra4 = Letra5 + Letra6 + Letra7 + Letra1;
            |SINO;
                Letra4 = Letra6 + Letra7 + Letra1;
            |FINSI;

            CampoDni = Letra4;
            CampoDni = ("000000000" + CampoDni) % -109;
    |FINSI;
|FINSI;
 CampoDni = (CampoDni + "         ");
 CampoDni = CampoDni % 109;
|FPRC;

|PROCESO cccBanco;
 |QBT ccc;
 c_alfa1 = ccc % 0;
 c_nume1 = c_alfa1;
 c_alfa1 = "";
 c_alfa2 = "";
 |PARA c_nume2 = 1; |SI c_nume2 [ c_nume1; |HACIENDO c_nume2 = c_nume2 + 1;
       c_nume3 = c_nume2 * 100 + 1;
       c_alfa2 = ccc % c_nume3;
       |SI c_alfa2 ] "0"; |Y c_alfa2 [ "9";
           c_alfa1 = c_alfa1 + c_alfa2;
       |FINSI;
 |SIGUE;
 ccc = c_alfa1;
|FPRC;

|PROCESO varia_fich;
|| .......... Variables del Presentador, del Ordenante y comunes
  NombreP = #1#4 + libre43;     NombreP = NombreP % 140;

  CampoDni = #1#2; |HAZ LetraDni;
  NifP    = CampoDni;
  sufiP   = #1#3; |QBT sufiP;
  sufiP   = ceros + sufiP;      sufiP = sufiP % -103;
  NifP    = NifP + sufiP;

  EntP    = #1#5; |QBT EntP;
  EntP    = ceros + EntP;       EntP    = EntP % -104;

  SucP    = #1#6; |QBT SucP;
  SucP    = ceros + SucP;       SucP    = SucP % -104;

  banAlfa = #1#7 + libre43;     banAlfa = banAlfa % 111;

  NombreO = #1#10 + libre43;    NombreO = NombreO % 140;

  CampoDni = #1#8; |HAZ LetraDni;
  NifO    = CampoDni;
  sufiO   = #1#9; |QBT sufiO;
  sufiO   = ceros + sufiO;      sufiO = sufiO % -103;
  NifO    = NifO + sufiO;

  EntO    = #1#11; |QBT EntO;
  EntO    = ceros + EntO;       EntO    = EntO % -104;

  SucO    = #1#12; |QBT SucO;
  SucO    = ceros + SucO;       SucO    = SucO % -104;

  dcO     = #1#13; |QBT dcO;
  dcO     = ceros + dcO;        dcO     = dcO % -102;

  CtaO    = #1#14; |QBT CtaO;
  CtaO    = ceros + CtaO;       CtaO    = CtaO % -110;

  CtaO    = CtaO + #1#14;       CtaO    = CtaO % -110;

  LocO    = #1#21 + libre43;    LocO    = LocO % 138;

  ProP    = #1#22; |QBT ProP;
  ProP    = ceros + ProP;       ProP    = ProP % -102;

  ConCli = #1#16; |QBF ConCli;

  alfa1 = #0#3;
  dd = alfa1 % A102;
  mm = alfa1 % A402;
  aa = alfa1 % A902;
  fechapre = dd + mm + aa;   || Fecha Formato dia + mes + a¤o

  fechacar = libre6; || Fecha Cargo = en Blanco
  |SI #0#14 = "S";
      alfa1 = #0#15;
      dd = alfa1 % A102;
      mm = alfa1 % A402;
      aa = alfa1 % A902;
      fechacar = dd + mm + aa;   || Fecha Cargo Formato dia + mes + a¤o
  |FINSI;

  tiProced = #0#13; || Tipo Procedimiento

|FPRC;

|PROCESO PideMoneda;
   |PUSHV 1943 2177;
   |BLANCO 1943 2177;
   |CUADRO 1945 2175;
   |ATRI R; |PINTA 1946 , " Seleccione moneda de emision "; |ATRI N;
   |PINTA 2053, "(Pesetas/Euros)";
   |ATRI I; |PINTA 2054, "P"; |PINTA 2062, "E"; |ATRI N;
   E_sup  = 1;
   E_inf  = "EP";
   |SI MonBase = 1; Moneda = "P"; |SINO; Moneda = "E"; |FINSI;
   Moneda = 2069 ? 0;
   |POPV;
|FPRC;

|PROCESO Conversion;
   |SI MonBase = 1;
      |SI Moneda = "E";
         Cantidad = ((Cantidad / 166.386) ! 2) * 100;
      |FINSI;
   |SINO;
      |SI Moneda = "P";
         Cantidad = (Cantidad * 166.386) ! 0;
      |SINO;
         Cantidad = Cantidad * 100;
      |FINSI;
   |FINSI;
|FPRC;
