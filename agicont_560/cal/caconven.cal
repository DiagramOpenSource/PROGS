|FICHEROS;
  calendar #3;
  cavenaut #4;
  caconven #5;
|FIN;

|VARIABLES;
  i = 0;
  repa1 = 0;
  procesa = "     Estoy procesando espere un poco Por Favor ";
  modifi = "2330S Los Datos son correctos  S/N : ";
  grabador = " Estoy grabando ";
  reparto = " Estoy repartiendo ";
  repart1 = " Estoy repartiendo el resto ";
  repart2 = "";
  dd = 0;
  x = 0;
  y = 0;
  cero = 0;
  suma = 0;
  swclave = 1;
  p1 = "";
  cantidad = 0;
  canti  = 0;
  reparto1 = 0;
  rdon = 0;
  veces = 0;
  desv = 0;
  peso = 0;
  sumar = 0;
  total = 0;
  sumar1 = 0;
  sumar2 = 0;
  sumar3 = 0;
  sumar5 = 0;
  suma4 = 0;
  resto = 0;
  repa2 = 0;
  tpor = 0;
  rnd = 0.000000;
  pesoalfa = "";
  did = 0;
  num = 0;
  producto = 0.000000;
  producto1 = 0.000000;
  variable = 0;
  mod = "";
  mod1 = "";
  numero = 0;
  longi = "";
  longi1 = "";
  modnum = 0;
  alfapunto = "";
  nueve = 9;
  {366}cant = 0;
  {367}pesodia = 0;
|FIN;

|INCLUYE acces2_i;

|PROCESO ptec;|TIPO 0;
  |SI FSalida = 2; |ACABA; |FINSI;
|PTEC 13;
|FPRC;

|PROCESO cargapesos;
  peso = 0;
  Ipesodia = pesodia1 <-;                             ||PROCESO
  |PARA x = 1;  |SI x [ 12;  |HACIENDO x = x + 1;
        num = 0;
        |PARA y = 1;  |SI y [ 31;  |HACIENDO y = y + 1;
              |SI y = 31;
                  |SI x = 4;  |O x = 6;  |O x = 9;  |O x = 11;
                      |SAL;
                  |FINSI;
              |FINSI;
              |SI y = 30;  |Y x = 2;
                  |SAL;
              |FINSI;
              num = num + 1;
              pesoalfa = num + "01";
              p1 = #3x % pesoalfa;
              peso = peso + p1;
              pesodia = p1;
              Ipesodia = Ipesodia + 1;
        |SIGUE;
  |SIGUE;
|FPRC;

|PROCESO asigna1;
  Ipesodia = pesodia1 <-;
  Icant = cant1 <-;
  |PARA x = cero;|SI x < 366;|HACIENDO x = x + 1;
        cant = sumar * pesodia;
        total = total + cant;
        Icant = Icant + 1;
        Ipesodia = Ipesodia + 1;
  |SIGUE;
|FPRC;

|PROCESO asigna2;
  dd = 1;
  |PARA dd = 1;  |SI dd [ veces;  |HACIENDO dd = dd + 1;
        resto = canti - total;
        did = veces + 1 - dd;
        |SI resto = 0;  |SAL;  |FINSI;
        repa2 = resto / did;
        |SI EURODB = 2;  repa2 = repa2 * 100;     |FINSI;
        repa2 = repa2 - 0.5;
        repa2 = repa2 ! 0;
        |SI EURODB = 2;  repa2 = repa2 / 100;     |FINSI;
        sumar1 = repa2 / peso;
        |SI EURODB = 2;  sumar1 = sumar1 * 100;     |FINSI;
        sumar1 = sumar1 - 0.5;
        sumar1 = sumar1 ! 0;
        |SI EURODB = 2;  sumar1 = sumar1 / 100;     |FINSI;

                             ||Distribucion aleatoria;
        Ipesodia = pesodia1 <-;
        Icant = cant1 <-;
        |BLIN 23;
        |ATRI R;
        repart2 = reparto + dd + "a vez. ";
        |PINTA 2350,repart2;
        |ATRI 0;
        |PARA x = cero;|SI x < 366;|HACIENDO x = x + 1;
              |HAZ randon;
              tpor = rnd * (desv * 2) - desv;
              tpor = tpor ! EURODB;
              sumar5 = (sumar1 * tpor) / 100
              sumar5 = sumar5 ! EURODB;
              sumar2 = sumar1 + sumar5;
              sumar3 = sumar2 * pesodia;
              cant = cant + sumar3;
              total = total + sumar3;
              Ipesodia = Ipesodia + 1;
              Icant = Icant + 1;
        |SIGUE;
  |SIGUE;
|FPRC;

|PROCESO resto1;
  Ipesodia = pesodia1 <-;
  Icant = cant1 <-;
  |PARA x = cero;|SI x < 366;|HACIENDO x = x + 1;
        sumar3 = sumar * pesodia;
        cant = cant + sumar3;
        total = total + sumar3;
        Ipesodia = Ipesodia + 1;
        Icant = Icant + 1;
  |SIGUE;
|FPRC;


|PROCESO automat;|TIPO 3;
  |PUSHV 2001 2380;
  |CONFI modifi;
  |POPV;
  |SI FSalida ! 0; |ACABA; |FINSI;
  |ATRI P;
  |PINTA 2125,procesa;
  |ATRI 0;
  |ABRE #3;
  #3#0 = #5#1;
  |LEE 110#3=;
  |SI FSalida ! 0; |LIBERA #3; |CIERRA #3; |ACABA; |FINSI;

  |HAZ random1;

  |HAZ cargapesos;

  |SI peso = 0; |LIBERA #3; |CIERRA #3; |ACABA; |FINSI;
  num = 0;
  cantidad = #5#2;
  ||SI EURODB = 2; cantidad = cantidad * 100; |FINSI;
  reparto1 = #5#3;
  rdon = #5#4;
  veces = #5#5;
  desv = #5#6;
  canti = cantidad / rdon;
  repa1 = reparto1 / rdon;
  repa1 = repa1 ! EURODB;
  sumar = repa1 / peso;
  sumar = sumar ! EURODB;

  |HAZ asigna1;      || asigna cantidad fija
  |HAZ asigna2;      || asigna en dd veces;

  resto = canti - total;
  |BLIN 23;
  |ATRI R;
  |PINTA 2350,repart1;
  |ATRI 0;
  |SI resto ! 0;
      sumar = resto / peso;
      |SI EURODB = 2;    sumar = sumar * 100;     |FINSI;
      sumar = sumar - 0.5;
      sumar = sumar ! 0;
      |SI EURODB = 2;    sumar = sumar / 100;     |FINSI;
      |SI sumar ! 0;
          |HAZ resto1;                          ||asigna resto entero;
          resto = canti -total;
      |FINSI;
  |FINSI;
  Ipesodia = pesodia1 <-;
  Icant = cant1 <-;
  |SI resto < 0;
      resto = -resto;
  |FINSI;
  |PARA x = cero;|SI x < 366;|HACIENDO x = x + 1;
        |SI resto = 0;  |SAL;  |FINSI;
        |SI resto < pesodia;
            sumar = resto;
        |SINO;
            sumar = pesodia;
        |FINSI;
        |SI resto < 0;
            cant = cant - sumar;
        |SINO;
            cant = cant + sumar;
        |FINSI;
        resto = resto - sumar;
        Ipesodia = Ipesodia + 1;
        Icant = Icant + 1;
  |SIGUE;

  total = total - resto;

  |BLIN 23;
  |ATRI R;
  |PINTA 2350,grabador;
  |ATRI 0;
  Icant = cant1 <-;
  |PARA x = cero;|SI x < 366;|HACIENDO x = x + 1;
        cant = cant * rdon;
        Icant = Icant + 1;
  |SIGUE;


                            ||Graba fichero de ventas automaticas;
  |ABRE #4;
  Icant = cant1 <-;
  |PARA y = 1;  |SI y [ 4;  |HACIENDO y = y + 1;
        swclave = 0;
        #4#0 = #5#0;
        #4#1 = y;
        |LEE 110#4=;
        |SI FSalida = 0;  swclave = 1;  |FINSI;
        num = 1;
        #4#94 = #3#13;
        |PARA x = 1;|SI x [ 92;|HACIENDO x = x + 1;
              num = num + 1;
              #4num = cant;
              Icant = Icant + 1;
              |SI x = 91;  |Y y [ 2;
                  |SAL;
              |FINSI;
              ||SI EURODB = 2; #4num = #4num / 100; |FINSI;
        |SIGUE;
        |SI swclave = 1; |GRABA 020#4a; |SINO; |GRABA 020#4n; |FINSI;
        |LIBERA #4;
  |SIGUE;
  |LIBERA #3;
  |CIERRA #3;
  |CIERRA #4;
|FPRC;



|PROCESO randon;
  producto = producto / 3.141592;
  mod = producto;
  numero = 0;
|ET punto;
  numero = numero + 1;
  alfapunto = numero + "01";
  mod1 = mod % alfapunto;
  |SI mod1 ! ".";
     |VETE punto;
  |FINSI;
  longi = mod % A0;
  alfapunto = numero;
  longi1 = longi % A0;
  |SI longi1 = "1";
      longi = "0" + longi;
  |FINSI;
  mod1 = alfapunto + longi;
  mod = mod % mod1;
  mod = "0" + mod;
  modnum = mod;
  rnd = modnum / 3.141592;
  |SI producto < 1;
      producto = producto * 1000000;
  |FINSI;
  |SI producto < 1;
      producto = producto1;
  |FINSI;
|FPRC;

|PROCESO random1;
  suma4 = 0;
  Ipesodia = pesodia1 <-;
  |PARA x = cero;|SI x < 183;|HACIENDO x = x + 1;
        |SI pesodia = 0;
            variable = 1;
        |SINO;
            variable = pesodia;
        |FINSI;
        suma4 = suma4 + variable;
        Ipesodia = Ipesodia + 1;
  |SIGUE;
   producto1 = suma4 * #5#1 * #5#0;
   producto = producto1;
|FPRC;
