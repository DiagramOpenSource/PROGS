|FICHEROS;
  camac349 #0;
  camal349 #1;
  eosclipr #2,MANTE;
  caclipro #3;
  agipobla #4;
  agiprovi #5;
  agim0016 #7;
  canempre #30;
  agicontr #23;
|FIN;

|VARIABLES;
 pepe = 0;
 mod       = 0;
 men1      = "0000SEs correcto S/N : ";
 men6      = "    NConfirma el Alta en Fichero General (S/N): ";
 verrfec   = "0000ATENCION !! Fecha erronea o formato incorrecto ";
 vdia1     = "";
 vme1      = "";
 vany1     = "";
 vdia      = 0;
 vme       = 0;
 vany      = 0;
 vtope     = 0;
 vsw       = 0;
 vfec      = "";
 vblanco   = "          ";
 vpuntos   = "..........";
 v         = 0;
 vacio     = "                                        ";
 cliente   = "";
 cuarenta  = "                                        ";

     letrasnif = "TRWAGMYFPDXBNJZSQVHLCKE";
     letra = "";
     alfa1 = "";
     alfa2 = "";
     final = "";
     x = "";
     nume1 = 0;
     nume2 = 0;
     lugar = 0;
     nifnum = 0;
     resto = 0;

 &swentra  = 0;
 &swmod   = 0;
 directo = "";
 direct1 = "";
 traba = "";
 dprocli = "";
 eselbase = "";
 salida = 0;
   &aste = "";
   agi = "";
   &agi1 = "";
   &agi2 = "";
   num = 0;
   longi = "";
|FIN;

|INCLUYE acceso_i;

|| **************************************************************************
|PROCESO alentrar;  |TIPO 8;
  |HAZ inicio;
  |HAZ tipo_8; || comprueba la existencia de la ficha general
  |SI aste ! "*";
      |PATH_DAT #2 agi1;
      |PATH_DAT #4 agi1;
      |PATH_DAT #5 agi1;
      |PATH_DAT #7 agi1;

      |PATH_PAN #2 agi2;
      |PATH_PAN #4 agi2;
      |PATH_PAN #5 agi2;
      |PATH_PAN #7 agi2;
  |FINSI;
|FPRC;

|PROCESO antesque;  |TIPO 7;
  |ABRE #30;
  #30#2 = cod1;
  #30#3 = cod2;
  |LEE 040#30=;
  #0#0 = #30#2;
  #0#3 = #30#1;
  #0#2 = #30#26;
  |PINTA #0#0;
  |PINTA #0#3;
  |CIERRA #30;
|FPRC;

|PROCESO despuesde;
  |SI #0#0 ! cod1;
      |ERROR;
      #0#0 = cod1;
      |PINTA #0#0;
      |ACABA;
  |FINSI;
|FPRC;

|PROCESO mayor11;
  alfa1 = #0Campo > " ";
  #0Campo = alfa1;
  |PINTA #0Campo;

|SI Campo = 12;
    |SI #0Campo = "N";
        #0#13 = " ";
        |PINTA #0#13;
        |CAMPO_MODIFICA #0#13,1,"N";
    |SINO;
        |CAMPO_MODIFICA #0#13,1,"S";
        |SI #0#13 = " ";
            #0#13 = "I";
            |PINTA #0#13;
        |FINSI;
    |FINSI;
|FINSI;
|SI Campo = 13;
    |SI #0#12 = "S"; |Y #0#13 = " ";
        #0#13 = "I";
        |PINTA #0#13;
    |FINSI;
|FINSI;
|FPRC;

|PROCESO fechacon; |TIPO 0;
 |ACABA;
  vfec = #0Campo;
  vsw = 0;
  |SI vfec = vblanco; |O vfec = vpuntos; |ACABA; |FINSI;

  vdia1 = vfec % A102;
  vme1  = vfec % A402;
  vany1 = vfec % A704;
  vdia  = vdia1;
  vme   = vme1;
  vany  = vany1;

  |SI vdia = 0; |Y vme = 0; |Y vany < 1900;
      |MENAV verrfec;
      |ERROR;
      |ACABA;
  |FINSI;

  |SI vme [ 12; |Y vme ] 1;
      |SI vme = 1; |O vme = 3; |O vme = 5; |O vme = 7; |O vme = 8; |O vme = 10; |O vme = 12;
          vtope = 31;
      |SINO;
          |SI vme = 2;
              mod = vany $ 4;
              |SI mod = 0; vtope = 29; |SINO; vtope = 28; |FINSI;
          |SINO;
              vtope = 30;
          |FINSI;
      |FINSI;
      |SI vdia [ vtope; vsw = 1; |FINSI;
  |FINSI;

  |SI vsw = 0;
      |MENAV verrfec;
      |ERROR;
  |FINSI;
|FPRC;

|| --------------------------------------------------------------------------

|PROCESO conficha;
  |SI FSalida ! 10; |ACABA; |FINSI;
  |SI aste ! "*";
      |ABRE #2;
      |PUSHV 0501 2380;
      |PINPA #0#2;
      #2#0 = #1#6;
      |LEE 000#2];
      |CONSULTA #0#2;
      #1#6 = #2#0;
      |CIERRA #2;
  |SINO;
      |ABRE #3;
      |SELECT #4#3;
      |PUSHV 0501 2380;
      |PINPA #0#3;
      #3#9 = #1#6;
      |LEE 000#3=;
      |CONSULTA #3#3;
      #1#6 = #3#9;
      |CIERRA #3;
  |FINSI;
      |POPV;
      |PINTA #1#6;
|FPRC;

|PROCESO cliente;|TIPO 0;
 salida = FSalida;
|SI salida = 9; |HAZ letra_0; |FINSI;
|SI salida = 10; |HAZ conficha; |FINSI;
swmod = 0;
|SI aste ! "*"; |Y #1#6 ! "            ";
    |ABRE #2;
    #2#0 = #1#6;
    |LEE 040#2=;
    |SI FSalida = 0;
        #1#7 = #2#1;
        #1#5 = #2#14;
        swmod = 1;
    |SINO;
        |HAZ altaclipr;
    |FINSI;
    |CIERRA #2;
|FINSI;
|SI swmod = 0;
    |ABRE #3;
    |SELECT #4#3;
    |DDEFECTO #3;
    #3#9 = #1#6;
    |LEE 040#3=;
    |SI FSalida = 0;
        #1#5 = #3#24;
        #1#7 = #3#1;
    |FINSI;
    |CIERRA #3;
|FINSI;
|PINTA #1#5;
|PINTA #1#7;
|FPRC;

|PROCESO letra_0;
  |SI #1#5 ! "ES "; |ACABA; |FINSI;
  alfa1 = #1Campo;   |QBT alfa1;
  x    = alfa1 % 101;
  |SI x ! "A"; |Y x ! "B"; |Y x ! "C"; |Y x ! "D"; |Y x ! "E"; |Y x ! "F";
   |Y x ! "G"; |Y x ! "P"; |Y x ! "Q"; |Y x ! "S"; |Y x ! "X";
      final = "";
      alfa2 = alfa1 % 0;
      nume1 = alfa2;
      |PARA nume2 = 1;  |SI nume2 [ nume1;  |HACIENDO nume2 = nume2 + 1;
            lugar = (nume2 * 100) + 1;
            alfa2 = alfa1 % lugar;
            |SI alfa2 = "0"; |O alfa2 = "1";|O alfa2 = "2"; |O alfa2 = "3";
             |O alfa2 = "4"; |O alfa2 = "5";|O alfa2 = "6";
             |O alfa2 = "7"; |O alfa2 = "8";|O alfa2 = "9";
                final = final + alfa2;
            |FINSI;
      |SIGUE;
      nifnum = final;
      |SI nifnum ! 0; |Y final ! "";
          resto   = nifnum $ 23;
          resto   = resto + 1;
          lugar   = (resto * 100) + 1;
          letra   = letrasnif % lugar;
          final   = ("000000000" + final + letra) % -109;
          #1Campo = final;
      |FINSI;
   |SINO;
      |MENAV "    ATENCION !!! No se calcular un C.I.F. ...";
   |FINSI;
   |PINTA #1Campo;
|FPRC;

|PROCESO mayor12;
  alfa1 = #1Campo > " ";
  #1Campo = alfa1;
  |PINTA #1Campo;
|FPRC;

|PROCESO actualiz; |TIPO 2;
|SI #1#4 = "E";
    #0#6 = #0#6 +. 1;
    #0#7 = #0#7 +. #1#8;
|FINSI;
|SI #1#4 = "A";
    #0#8 = #0#8 +. 1;
    #0#9 = #0#9 +. #1#8;
|FINSI;
|SI #1#4 = "R";
    #0#10 = #0#10 +. 1;
|FINSI;
|SI #1#4 = "X";
    #0#11 = #0#11 +. 1;
|FINSI;
|PINTA #0#6;
|PINTA #0#7;
|PINTA #0#8;
|PINTA #0#9;
|PINTA #0#10;
|PINTA #0#11;
|FPRC;

|PROCESO modificar; |TIPO 0;
 |HAZ mayor12;
 |CAMPO_MODIFICA #1#9,  1, "S";
 |CAMPO_MODIFICA #1#10, 1, "S";
 |CAMPO_MODIFICA #1#11, 1, "S";
 |CAMPO_MODIFICA #1#12, 1, "S";
 |CAMPO_MODIFICA #1#13, 1, "S";

|SI #1#4 = "E";
    |CAMPO_MODIFICA #1#10, 1, "N";
    |CAMPO_MODIFICA #1#11, 1, "N";
    |CAMPO_MODIFICA #1#12, 1, "N";
    #1#10 = "";
    #1#11 = 0;
    #1#12 = 0;
|FINSI;

|SI #1#4 = "A";
    |CAMPO_MODIFICA #1#10, 1, "N";
    |CAMPO_MODIFICA #1#11, 1, "N";
    |CAMPO_MODIFICA #1#12, 1, "N";
    |CAMPO_MODIFICA #1#13, 1, "N";
    #1#10 = "";
    #1#11 = 0;
    #1#12 = 0;
    #1#13 = "";
|FINSI;

 |SI #1#4 = "X";
     |CAMPO_MODIFICA #1#13, 1, "N";
     #1#13 = "";
 |FINSI;

 |PINTA #1#9;
 |PINTA #1#10;
 |PINTA #1#11;
 |PINTA #1#12;
 |PINTA #1#13;
|FPRC;

|PROCESO cuapais;
 |PUSHV 1326 2359;
 |D_CUADRO 1326, 2359;
 |ATRI R;
  |PINTA 1427 , "----- PAISES COMUNITARIOS ------";
 |ATRI N;
  |PINTA 1527 , " ES Espa¤a        NL Holanda    ";
  |PINTA 1627 , " DE Alemania      SE Suecia     ";
  |PINTA 1727 , " BE Belgica       IT Italia     ";
  |PINTA 1827 , " DK Dinamarca     IE Irlanda    ";
  |PINTA 1927 , " FI Finlandia     LU Luxemburgo ";
  |PINTA 2027 , " FR Francia       PT Portugal   ";
  |PINTA 2127 , " EL Grecia        AT Austria    ";
  |PINTA 2227 , " GB Gran Breta¤a                ";
 |ATRI I;
 |PINTA 1528 , "ES"; |PINTA 1545 , "NL";
 |PINTA 1628 , "DE"; |PINTA 1645 , "SE";
 |PINTA 1728 , "BE"; |PINTA 1745 , "IT";
 |PINTA 1828 , "DK"; |PINTA 1845 , "IE";
 |PINTA 1928 , "FI"; |PINTA 1945 , "LU";
 |PINTA 2028 , "FR"; |PINTA 2045 , "PT";
 |PINTA 2128 , "EL"; |PINTA 2145 , "AT";
 |PINTA 2228 , "GB";
 |ATRI 0;
 |PAUSA;
 |POPV;
|FPRC;

|PROCESO filtro; |TIPO 0;
 |SI FSalida = 9;
     |HAZ cuapais;
     |ERROR;
     |ACABA;
 |FINSI;
|HAZ mayor12;
pepe = 0;
|SI #1#5 = "ES "; |O #1#5 = "DE "; |O #1#5 = "BE "; |O #1#5 = "DK "; pepe = 1; |FINSI;
|SI #1#5 = "FR "; |O #1#5 = "EL "; |O #1#5 = "GB "; |O #1#5 = "NL "; pepe = 1; |FINSI;
|SI #1#5 = "IT "; |O #1#5 = "IE "; |O #1#5 = "LU "; |O #1#5 = "PT "; pepe = 1; |FINSI;
|SI #1#5 = "FI "; |O #1#5 = "SE "; |O #1#5 = "AT ";                 pepe = 1; |FINSI;
|SI pepe = 0;
    |MENAV "     Pais incorrecto.";
   || |ERROR;
|FINSI;
|FPRC;

|| *************************************************************************

|PROCESO tipo_8; || control de la ficha de clientes general
  |DIRECTORIO agi;
  |QBF agi;
  |PATH_DAT #23 agi;

|ABRE #23;
#23#0 = 1;
|LEE 040#23=;
|SI FSalida ! 0; aste = "*"; |CIERRA #23; |ACABA; |FINSI;
|CIERRA #23;
aste = #23#7;
|QBF aste;
|SI aste  = "*"; |ACABA; |FINSI;

agi1 = #23#8; |QBF agi1;

longi = agi1 % 0;
num   = longi;
num   = 100 + (num - 5);
longi = num;

agi2  = (agi1 % longi) + "pan/";
|FPRC;

|PROCESO altaclipr;
|SI aste = "*"; |ACABA; |FINSI;
    |CONFI men6;
    |SI FSalida = 0;
        |PUSHV 0501 2380;
        |PINPA #0#2;
        |DDEFECTO #2;
        |PINDA #0#2;
        #2#0 = #1#6;
        |PINTA #2#0;
        |ENDATOS #1#2;
        |SI FSalida = 0;
            |POPV;
            #1#7 = #2#1;
            #1#5 = #2#14;
            |PINTA #1#5;
            |PINTA #1#7;
            swmod=1;
            |GRABA 020#2n;                 || Graba el agicli
        |SINO;
         |POPV;
        |FINSI;
    |FINSI;
|LIBERA #2;
|CIERRA #2;
|FPRC;
