|FICHEROS;
    caditra0 #0;
    caivarep #15;
    caivasop #16;
    calicont #31;
|FIN;

|VARIABLES;
    &entrada = 1;
    lin = 0;
    sw_desc = 0;
    opcion = 0;
    fecalf = "";
    men1   = "    NO EXISTE EMPRESA";
    mensa1 = "    ERROR ESTA FECHA NO CORRESPONDE AL EJERCICIO";
    traba1 = "";

    &swlin = 0;
    &descuadre = 0;
    &a_debe = 0;
    &a_haber = 0;
    &contra = "";
    contra_d = "";
    contra_h = "";
    swex_con = 0;
    informe = "";
    inform1 = "caditra0";
    inform2 = "caditra2";
    {-1}menu = "";
        menu1 = "2400";
        menu2 = "1";
        menu3 = " Elija opcion: ";
        menu4 = "PI";
        menu5 = " Pantalla ";
        menu6 = " Impresora ";

   &hiva_1 = "";
   &hiva_2 = "";
   &hiva_3 = "";
   &hiva_4 = @;
|FIN;

|INCLUYE acceso_i;
|INCLUYE diario_i;

|| *********************************************************************
||                             COMIENZO
|| *********************************************************************

|PROCESO mayus;
 #0Campo = #0Campo > " ";
 |PINTA #0Campo;
|FPRC;


|PROGRAMA;
  |CLS;
  |CABEZA "Listado Diario de trabajo";
  FSalida = 1;
  |PARA; |SI FSalida = 1; |HACIENDO;
         |PINPA #0#0;
         |DDEFECTO #0;
         |PINDA #0#0;                    || Pinta los datos por defecto
         |ENDATOS #1#0;                  || Seleccion informe
         |SI FSalida ! 0;  |SAL;  |FINSI;
         |ABRE #4;
         |ABRET #2;
         |HAZ impre;                 || Calculo impresion
         |CIERRA #4;
         |CIERRAT #2;
         |HAZ borrar;                || Borra acumulado
         FSalida = 1;            || Vuelve al bucle
  |SIGUE;
|FPRO;

|| *************************************************************************
||                          MEOLLO
|| *************************************************************************

|PROCESO porimpre;
  |SI #2#6 < #0#9;  |O #2#6 > #0#10;  |ACABA;  |FINSI;
  #4#0 = #2#4;
  #4#1 = #2#5;
  |LEE 040#4=;
  |SI FSalida ! 0;
      |DDEFECTO #4;
  |FINSI;
  swlin = 0;

  contra = "";
  contra_d = #1#10;
  contra_h = #1#8;
  |HAZ leocontra;
  |SI #2#8 = "D";
      a_debe = #2#9;
      a_haber = 0;
      |SI #2#3 < 30; |O swex_con = 1;
          contra = contra_d;       || contrapartida DEBE
      |FINSI;
  |SINO;
      a_debe = 0;
      a_haber = #2#9;
      |SI #2#3 < 30; |O swex_con = 1;
          contra = contra_h;        || contrapartida HABER
      |FINSI;
  |FINSI;
  |SI d_iva = "S";
      |HAZ lecturaIVA;
  |FINSI;
  |PRINT;
|FPRC;

|PROCESO lecturaIVA;
 hiva_1 = ""; hiva_2 = "";
 hiva_3 = ""; hiva_4 = "";
 |SI #2#12 = "R";
     |SI #2#19 = "F"; |O #2#19 = "f";
         |ABRE #15;
         #15#0  = #2#13;
         #15#2  = #2#14;
         #15#27 = #2#15;
         |LEE 001#15=;
         |SI FSalida = 0;
             hiva_1 = #15#0;
             hiva_2 = #15#2;
             hiva_3 = #15#27;
             hiva_4 = #15#3;
         |FINSI;
         |CIERRA #15;
     |FINSI;
 |FINSI;
 |SI #2#12 = "S";
     |SI #2#19 = "F"; |O #2#19 = "f";
         |ABRE #16;
         #16#0  = #2#13;
         #16#2  = #2#14;
         #16#27 = #2#15;
         |LEE 001#16=;
         |SI FSalida = 0;
             hiva_1 = #16#0;
             hiva_2 = #16#2;
             hiva_3 = #16#27;
             hiva_4 = #16#3;
         |FINSI;
         |CIERRA #16;
     |FINSI;
 |FINSI;

 |QBF hiva_1; |SI hiva_1 ! ""; hiva_1 = ("00"+hiva_1) % -102; |FINSI;
 |QBF hiva_2; |SI hiva_2 ! ""; hiva_2 = ("000000"+hiva_2) % -106; |FINSI;
 |QBF hiva_3; |SI hiva_3 ! ""; hiva_3 = "/"+hiva_3; |FINSI;
|FPRC;

|PROCESO leocontra;
 swex_con = 0;
 #31#0 = #2#0;
 #31#1 = #2#1;
 #31#2 = #2#2;
 #31#3 = #2#3;
 |LEE 001#31=;
 |SI FSalida = 0;
     swex_con = 1;
     contra_h = #31#4;
     contra_d = #31#4;
 |FINSI;
|FPRC;

|PROCESO bucleimp;
  sw_alguno = 0;
  |BUCLELIN 2alguno#1;
  |SI sw_alguno = 0;  |ACABA;  |FINSI;
  descuadre = #1#4 - #1#5;
  |SI descuadre = 0;
      swlin = 1;
      |PRINT;
  |SINO;
      swlin = 2;
      |SI descuadre < 0; descuadre = descuadre * (-1); |FINSI;
      |PRINT;
  |FINSI;
  |BUCLELIN 0porimpre#1;
|FPRC;

|DEFBUCLE caditra01;
 #1#2;
 ;
 #0#0, #0#4, #0#7, #0#2;
 #0#1, #0#5, #0#8, #0#3;
 e;
 NULL, bucleimp;
|FIN;

|DEFBUCLE caditra02;
 #1#1;
 3;
 #0#7, #0#4, #0#2, #0#0;
 #0#8, #0#5, #0#3, #0#1;
 e;
 NULL, bucleimp;
|FIN;

|DEFBUCLE caditra03;
 #1#3;
 3,1,0;
 #0#2, #0#2,  #0#0, #0#4, #0#7;
 #0#3, #0#3,  #0#1, #0#5, #0#8;
 e;
 NULL, bucleimp;
|FIN;

|PROCESO impre;
  m_orden = #0#11;
  d_iva   = #0#12;
  |MENU menu;
  opcion = FSalida;
  |SI opcion = 1;  |HAZ pantalla;   |FINSI;
  |SI opcion = 2;  |HAZ impresora;  |FINSI;
|FPRC;

|PROCESO impresora;
  |IMPRE 0;
  |SI FSalida ! 0;  |ACABA;  |FINSI;
  informe = inform1;
  |SI d_iva = "S";
      informe = inform2;
  |FINSI;
  |INFOR informe;
  |SI FSalida ! 0;  |FINIMP;  |ACABA;  |FINSI;
  |ABRE #31;
  |SI m_orden = 1; |BUCLE caditra01; |FINSI;
  |SI m_orden = 2; |BUCLE caditra02; |FINSI;
  |SI m_orden = 3; |BUCLE caditra03; |FINSI;
  |CIERRA #31;
  |PIEINF;
  |FININF;
  |FINIMP;
|FPRC;

|| *************************************************************
