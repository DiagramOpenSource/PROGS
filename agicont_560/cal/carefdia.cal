|FICHEROS;
    carefdia #0;
    camaasic #1;
    camaasil #2;
    careftmp #3;
|FIN;

|VARIABLES;
    &entrada = 1;
    alfa1 = "";
    alfa2 = "";
    alfa3 = "";
    alfa4 = "";
    nume1 = 0;
    nume2 = 0;
    nume3 = 0;
    nume4 = 0;
    inkey = 0;
    seleccio = 0;
    fich_dir = "";
    no_depar = "";
    dia_ant = "xx";
    fec_ant = @;
    doc_ant = 0;
    sw_cambia = 0;
    codempr = 0;
    ejercic = 0;
    &r_emp = 0;
    &r_per = 0;
    varcamp = 0;
    dp1 = "";
    dp2 = "";
|FIN;

|INCLUYE acceso_i;

|PROGRAMA;
|HAZ inicio;
    no_depar = #30#24;
    codempr = #30#2;
    ejercic = #30#3;
|CLS;
|CABEZA "Refundir Diarios";
|PINPA #0#0;
|ENTLINEAL #1#0, 2, 1, 18, 0, min, max;
|SI FSalida = 0;
    |HAZ refunde;
|FINSI;
|FPRO;

|PROCESO min;
    #0#0 = 1;
    #0#1 = 1;
|FPRC;

|PROCESO max;
    #0#0 = 1;
    #0#1 = 999;
|FPRC;

****************************************************************************
               CALCULOS DESDE CAMPOS
****************************************************************************

|PROCESO nocero; |TIPO 0;
|SI #0#2 = 0;
    |ERROR;
|FINSI;
|FPRC;

|PROCESO consulta;
  |SI FSalida ! 9;  |ACABA;  |FINSI;
  r_emp = 0;
  |SUB_EJECUTA "caconemp.run";
  |ERROR;
  |SI r_emp = 0; |ACABA; |FINSI;
      #0Campo = r_emp;
      varcamp = Campo + 1;
      #0varcamp = r_per;
      |PTEC 802;
      |PTEC 802;
|FPRC;


|PROCESO empresa; |TIPO 0;
    inkey = FSalida;
    #0#6 = "";
|PINTA #0#6;
|DBASE fich_dir;
    fich_dir = fich_dir + "fich/";
|PATH_DAT #30 fich_dir;
|ABRE #30;
    #30#2 = #0#2;
    #30#3 = #0#3;
|LEE 001#30=;
|SI FSalida ! 0;|Y inkey ! 2;
    |DDEFECTO #30;
    |MENAV "    ATENCION!!! Empresa/Periodo inexistente ...";
    |ERROR;
|SINO;
        #0#6 = #30#1;
    |PINTA #0#6;
|FINSI;
|CIERRA #30;
|FPRC;

|PROCESO nodepar; |TIPO 7;
|CAMPO_MODIFICA #0#5, 1, no_depar;
|FPRC;

|PROCESO selecciona;
 |ATRI P;
 |PINTA 2330, " Preparando Temporal ...                       ";
 |ATRI 0;
 |SI no_depar = "N"; dp1 = "  "; dp2 = "zz"; |FINSI;
 |SI no_depar = "S"; dp1 = #0#5; dp2 = #0#5; |FINSI;
 |HAZ gra_temporal;
 |HAZ lee_temporal;
|FPRC;

|DEFBUCLE seleccion;
 #0#1;
 ;
 1,   1;
 1, 999;
 e;
 NULL, selecciona;
|FIN;

|PROCESO refunde;
|NOPARA;
|CONFI "2400N Desea realizar la refundicion en este momento S/N: ";
|SI FSalida = 0;
    |BUCLE seleccion;
    |PINTA 2330, "                                               ";
    |SI seleccio ! 0;
        |SUB_EJECUTA "carecupe.run";
    |SINO;
        |MENAV "    Seleccion vacia ...";
    |FINSI;
|FINSI;
|SIPARA;
|FPRC;

****************************************************************************
               GRABACION DEL TEMPORAL
****************************************************************************

|PROCESO grabando;
  #3#0  = #2#0;   #3#1  = #2#1;   #3#2  = #2#2;   #3#3  = #2#3;   #3#4  = #2#4;
  #3#5  = #2#5;   #3#6  = #2#6;   #3#7  = #2#7;   #3#8  = #2#8;   #3#9  = #2#9;
  #3#10 = #2#10;  #3#11 = #2#11;  #3#12 = #2#12;  #3#13 = #2#13;  #3#14 = #2#14;
  #3#15 = #2#15;  #3#16 = #2#16;  #3#17 = #2#17;  #3#18 = #2#18;  #3#19 = #2#19;
  #3#20 = #2#20;  #3#21 = #2#21;  #3#22 = #2#22;  #3#23 = #2#23;
  #1#0 = #2#0;
  #1#1 = #2#1;
  #1#2 = #2#2;
  |LEE 001#1=;
  |SI FSalida = 0;
      #3#22 = #1#3;    || el nro. de asiento
      #3#23 = #1#4;    || debe
      #3#24 = #1#5;    || haber
      #3#25 = #1#6;    || saldo
      #3#26 = #1#7;    || predefinido
      #3#27 = #1#8;    || contrap.HABER
      #3#28 = #1#9;    || dig.contrap.HABER
      #3#29 = #1#10;   || contrap.DEBE
      #3#30 = #1#11;   || dig.contrap.DEBE
      |GRABA 020#3n;                 || se lleva los datos de la cabecera
  |FINSI;
|FPRC;

|DEFBUCLE apuntes;
 #2#1;
 21,6;
 #0#4, fec1,      1,    1, dp1, #0#7;
 #0#4, fec2, 999999, 9999, dp2, #0#8;
 e;
 NULL, grabando;
|FIN;

|PROCESO gra_temporal;
  |ABRE #3;
  |CIERRA #3;
  |DELINDEX #3;
  |DBASE fich_dir;
  alfa1 = #0#2;  alfa1 = "00000" + alfa1;  alfa1 = alfa1 % -105;
  alfa2 = #0#3;
  fich_dir = fich_dir + "fich/" + alfa1 + alfa2 + "/";
  |PATH_DAT #1 fich_dir;
  |PATH_DAT #2 fich_dir;
  |ABRE #1;
  |ABRE #3;
  |BUCLE apuntes;
  |CIERRA #1;
  |CIERRA #3;
|FPRC;


****************************************************************************
               LECTURA DEL TEMPORAL Y GRABACION EN APUNTES
****************************************************************************

|PROCESO leyendo2;
  seleccio = 1;
  #2#0  = #3#0;   #2#1  = #3#1;   #2#2  = #3#2;   #2#3  = #3#3;   #2#4  = #3#4;
  #2#5  = #3#5;   #2#6  = #3#6;   #2#7  = #3#7;   #2#8  = #3#8;   #2#9  = #3#9;
  #2#10 = #3#10;  #2#11 = #3#11;  #2#12 = #3#12;  #2#13 = #3#13;  #2#14 = #3#14;
  #2#15 = #3#15;  #2#16 = #3#16;  #2#17 = #3#17;  #2#18 = #3#18;  #2#19 = #3#19;
  #2#20 = #3#20;  #2#21 = #3#21;  #2#22 = #3#22;  #2#23 = #3#23;
  |GRABA 020#2n;
  #1#0 = #2#0;
  #1#1 = #2#1;
  #1#2 = #2#2;
  |LEE 001#1=;
  |SI FSalida ! 0;
      #1#0 = #2#0;
      #1#1 = #2#1;
      #1#2 = #2#2;
      #1#3 = #3#22;    || nro. asiento
      #1#4 = #3#23;    || debe
      #1#5 = #3#24;    || haber
      #1#6 = #3#25;    || saldo
      #1#7 = #3#26;    || predefinido
      #1#8 = #3#27;    || contrap.HABER
      #1#9 = #3#28;    || dig.contrap.HABER
      #1#10 = #3#29;   || contrap.DEBE
      #1#11 = #3#30;   || dig.contrap.DEBE
      |GRABA 020#1n;
  |FINSI;
  |ATRI I;
  alfa1 = #2#0;  alfa1 = "00" + alfa1;  alfa1 = alfa1 % -102;
  alfa2 = #2#1;
  alfa3 = #2#2;  alfa3 = "000000" + alfa3;  alfa3 = alfa3 % -106;
  alfa4 = " Grabando:  " + alfa1 + "  " + alfa2 + "  " + alfa3;
  |PINTA 2330, alfa4;
  |ATRI 0;
|FPRC;

|DEFBUCLE temporal2;
 #3#1;
 ;
 00, fec1,      1,    1;
 99, fec2, 999999, 9999;
 e;
 NULL, leyendo2;
|FIN;

|PROCESO cambia;
|BORRA 020#3a;            || borra con diario duplicado
    #3#0 = #1#0;
|GRABA 020#3n;            || graba con nuevo diario
|FPRC;

|PROCESO leyendo;
|SI dia_ant ! #3#0; |O fec_ant ! #3#1; |O doc_ant ! #3#2;
        dia_ant = #3#0;
        fec_ant = #3#1;
        doc_ant = #3#2;
        #1#0 = #3#0;
        #1#1 = #3#1;
        #1#2 = #3#2;
    |LEE 001#1=;
    |PARA; |SI FSalida = 0; |HACIENDO;
            #1#0 = #1#0 + 1;
        |LEE 001#1=;
    |SIGUE;
    |SI #1#0 ! #3#0;
            sw_cambia = 1;
        |HAZ cambia;
    |SINO;
            sw_cambia = 0;
    |FINSI;
|SINO;
    |SI sw_cambia = 1;
        |HAZ cambia;
    |FINSI;
|FINSI;
|FPRC;

|DEFBUCLE temporal;
 #3#1;
 ;
 #0#4, fec1,      1,    1;
 #0#4, fec2, 999999, 9999;
 e;
 NULL, leyendo;
|FIN;

|PROCESO lee_temporal;
  |DBASE fich_dir;
  alfa1 = codempr;  alfa1 = "00000" + alfa1;  alfa1 = alfa1 % -105;
  alfa2 = ejercic;
  fich_dir = fich_dir + "fich/" + alfa1 + alfa2 + "/";
  |PATH_DAT #1 fich_dir;
  |PATH_DAT #2 fich_dir;
  |ABRE #1;
  |ABRE #2;
  |BUCLE temporal;
  |BUCLE temporal2;
  |CIERRA #1;
  |CIERRA #2;
  |DELINDEX #3;
|FPRC;
