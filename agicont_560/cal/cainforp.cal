|FICHEROS;
    cainforp #0;

    cacoivli #10;   || contrapartidas lins.
    cacuivli #11;   || cuentas iva lins.
    caclipro #12;   || clientes/proveedores
    caivarep #13;   || iva repercutido

    camandia #20;   || diarios AGICONT
    caconcep #21;   || conceptos AGICONT
    camaasic #22;   || asientos cabs. AGICONT
    camaasil #23;   || asientos lins. AGICONT
    cacuenta #24;   || cuentas AGICONT
    canempre #25;   || empresas AGICONT
    caconasi #26;   || contador asientos AGICONT
    catradia #27;   || diario de trabajo
    cacuebal #28;   || cuentas de balance

    cainforq #40;   || def trabajo importacion
|FIN;

|VARIABLES;
    alfa1 = "";
    alfa2 = "";
    alfa3 = "";
    alfa4 = "";
    nume1 = 0;
    nume2 = 0;
    nume3 = 0;
    nume4 = 0;
    para1 = 0;
    para2 = 0;
    para3 = 0;
    para4 = 0;
    fech1 = @;
    fech2 = @;
    fech3 = @;
    fech4 = @;
    elsw1 = 0;
    elsw2 = 0;
    elsw3 = 0;
    elsw4 = 0;
    FEstado = 0;
    seleccio = 0;
    modo = 0;
    sw_rup = 0;

    p_mes = "";
    ultimo = 0;

    fecha = "";
    signe = "";
    impor = 0;
    conte = "";
    digcu = 0;
    conce = 0;
    descr = "";
    tacu = "";
    nacu = 0;

    p_mensa = "    LONGITUD DE CUENTA FUERA DE NIVEL!";
    d_diari = 0;
    d_opera = "";
    d_impor = 0;
    &modcon = 0;
    importe = 0;
    opera = "";
    ressum = 0;
    ini = 0;
    fecalf = "";
    mes = "";
    mesfec = 0;
    mesac = 0;
    a = 0;
    b = 0;
    c = 0;
    ult_apunt = 0;
    p_alfa1 = "";
    p_error = 0;
    p_lugar = 0;
    p_cuan3 = 0;
    p_cuan4 = 0;
    p_cuan5 = 0;
    p_alfa2 = "";
    p_alfa3 = "";
    p_alfa4 = "";
    p_alfa5 = "";
    p_nume1 = 0;
    p_nume2 = 0;
    p_ceros = "";

    juan = 0;
    errant = 0;
    cuini = "            ";
    cufin = "999999999998";
    diini = 0;
    difin = 9;
    fecini = @;
    fecfin = @;
    tiini = "D";
    tifin = "H";
    diain = 0;
    diafi = 99;
    prim = 0;
    cuant = "";
    lon = 0;
    aa = 0;
    mensa = "";
    aar = "";
    traba = "";
    errcuent = "aaaaaaaaaaaa";
    mensa2   = "     No esta definida esta cuenta en Cuentas de Balance";
    uno = "";
    dos = "";
    mascara = "";
    nivel = 0;

    fabre = "";
    fcanl = "";
    field1 = "";
    field2 = "";
    fsali1 = 0;
    fsali2 = 0;
    ctiva = "";
    ctrec = "";
    ctven = "";
    ctdes = "";
    Fcuentas = 0;
    guarda_pos = 0;
    estado_iva = 0;
    descu = 0;
    tota = 0;
    base = 0;
    piva = 0;
    prec = 0;
    civa = 0;
    crec = 0;
    sw_fecha = 0;
|FIN;

|INCLUYE acceso_i;
____________________________________/ CALCULOS PANTALLA
|PROCESO mayus; |TIPO 0;
    #0Campo = #0Campo > #0Campo;
|PINTA #0Campo;
|FPRC;
____________________________________/ ACTUALIZACION NIVELES SUPERIORES
|PROCESO actualiza;
  ini = dig1;
  fecalf = #27#3;
  mes = fecalf % 402;
  mesfec = mes;
  |SI ini = 1;
      mesac = mesfec;
  |SINO;
      mesac = (mesfec - ini) + 1;
      |SI mesac [ 0;
          mesac = (13 - ini) + mesfec;
      |FINSI;
  |FINSI;
  a = (mesac * 3) + 9;
  b = a + 1;
  c = a + 2;
  |SI #27#4 = "D";
      #24a = #24a + #27#5;
      #24#51 = #24#51 + #27#5;
  |SINO;
      #24b = #24b + #27#5;
      #24#52 = #24#52 + #27#5;
  |FINSI;
  #24c = #24a - #24b;
  #24#53 = #24#51 - #24#52;
|FPRC;

|PROCESO suma;
  |SI #27#6 = 0;
      |SI #27#4 = "D";
          #24#9 = #24#9 + #27#5;
          #24#51 = #24#51 + #27#5;
      |SINO;
          #24#10 = #24#10 + #27#5;
          #24#52 = #24#52 + #27#5;
      |FINSI;
      #24#11 = #24#9 - #24#10;
      #24#53 = #24#51 - #24#52;
  |SINO;
      |SI #27#6 = 99;
          |SI #27#4 = "D";
              #24#48 = #24#48 + #27#5;
              #24#51 = #24#51 + #27#5;
          |SINO;
              #24#49 = #24#49 + #27#5;
              #24#52 = #24#52 + #27#5;
          |FINSI;
          #24#50 = #24#48 - #24#49;
          #24#53 = #24#51 - #24#52;
      |SINO;
          |HAZ actualiza;
      |FINSI;
  |FINSI;
|FPRC;

|PROCESO defect1;
  |ABRE #28;
  uno = #24#0;
  |QBF uno;
  dos = uno + "000000000000";
  uno = dos % A101;
  #28#0 = uno;
  |LEE 040#28=;
  |SI FSalida = 0;
      |HAZ saca1;
      |VETE findefe;
  |FINSI;
  uno = dos % A102;
  #28#0 = uno;
  |LEE 040#28=;
  |SI FSalida = 0;
      |HAZ saca1;
      |VETE findefe;
  |FINSI;
  uno = dos % A103;
  #28#0 = uno;
  |LEE 040#28=;
  |SI FSalida = 0;
      |HAZ saca1;
      |VETE findefe;
  |FINSI;
  uno = dos % A104;
  #28#0 = uno;
  |LEE 040#28=;
  |SI FSalida = 0;
      |HAZ saca1;
      |VETE findefe;
  |SINO;
      |MENAV mensa2;
  |FINSI;
|ET findefe;
  nivel = #24#1;
|| la 3a clave esta formada por la cta + zzzz y el campo fantasma del nivel;
  mascara = #24#0;
  |QBF mascara;
  mascara = mascara +"zzzzzzzzzzzz";
  mascara = mascara % 112;
  #24#54    = mascara;
  #24#55    = nivel;
  |CIERRA #28;
|FPRC;

|PROCESO saca1;
  #24#4 = #28#1;
  #24#5 = #28#2;
  #24#6 = #28#3;
  #24#7 = #28#4;
  #24#8 = #28#5;
|FPRC;

|PROCESO actuali;
  |SI #27#2 [ juan; |O #27#2 = 9;
      |SI prim = 1;
           |GRABA 020#24a;
      |FINSI;
      prim = 0;
      |VETE acaba;
  |FINSI;
  aa = 100 + lon;
  aar = aa;
  traba = #27#1 % aar;
|ET iniemp;
  |SI prim = 0;
      cuant = traba;
      prim = 1;
      #24#0 = cuant;
      #24#1 = juan;
      |SI #24#0 = errcuent;
          prim = 0;
          |VETE acaba;
      |FINSI;
      |LIBERA #24;
      |LEE 101#24=;
      |SI FSalida ! 0;
          |LIBERA #24;
          |DDEFECTO #24;
          #24#0 = cuant;
          #24#1 = juan;
          |HAZ defect1;
          |PUSHV 0501 1980;
          |PINPA #0#24;
          |PINDA #0#24;
          |ENDATOS #1#24;
          |SI FSalida ! 0;
              |POPV;
              mensa = "Actualizando nivel: " + juan + "        ";
              |PINTA 2410,mensa;
              errant = #24#0;
              prim = 0;
              |VETE acaba;
          |FINSI;
          |GRABA 020#24b;
          |POPV;
          mensa = "Actualizando nivel: " + juan + "        ";
          |PINTA 2410,mensa;
      |FINSI;
  |FINSI;
  |SI cuant ! traba;
      |GRABA 020#24a;
      prim = 0;
      |VETE iniemp;
  |FINSI;
  |HAZ suma;
|ET acaba;
|FPRC;

|DEFBUCLE camaapua0;
 #27#1;
 ;
 codcon,cuini,diini,fecini,tiini,diain;
 codcon,cufin,difin,fecfin,tifin,diafi;
 be;
 NULL,actuali;
|FIN;

|PROCESO borrando;
  |BORRA 020#27a;
|FPRC;

|DEFBUCLE camaapua1;
 #27#1;
 ;
 codcon,cuini,diini,fecini,tiini,diain;
 codcon,cufin,difin,fecfin,tifin,diafi;
 be;
 NULL,borrando;
|FIN;

|PROCESO niveles;
  |PINTA 2410,"Estoy Actualizando las Cuentas";
  |ABRE #24;
  |PARA juan = dig3 - 1; |SI juan ] 1; |HACIENDO juan = juan - 1;
        prim = 0;
        |SI juan = 1; lon = dig4; |FINSI;
        |SI juan = 2; lon = dig5; |FINSI;
        |SI juan = 3; lon = dig6; |FINSI;
        |SI juan = 4; lon = dig7; |FINSI;
        |SI juan = 5; lon = dig8; |FINSI;
        fecini = "01.01.0000";
        fecfin = "31.12.8999";
        |BUCLE camaapua0;
        |SI prim = 1;
            |GRABA 020#24a;
        |FINSI;
  |SIGUE;
  |CIERRA #24;
  fecini = "01.01.0000";
  fecfin = "31.12.8999";
  |BUCLE camaapua1;
  |ABRE #27;
  |LEE 000#27u;
  |SI FSalida ! 0;
       |CIERRA #27;
       |DELINDEX #27;
  |SINO;
       |CIERRA #27;
  |FINSI;
|FPRC;

|PROCESO grabatra;
|ABRE #27;
|LEE 101#27u;
|SI FSalida ! 0;
    |DDEFECTO #27;
        #27#0 = 1;
|SINO;
        #27#0 = #27#0 + 1;
|FINSI;
|GRABA 101#27n;
    codcon = #27#0;
|LIBERA #27;
|CIERRA #27;
|FPRC;
____________________________________/ GRABACION DE ASIENTOS
|PROCESO actu_diari;
    #20#0 = d_diari;
|LEE 101#20=;
|SI FSalida ! 0;
    |DDEFECTO #20;
        #20#0 = d_diari;
        #20#1 = "<DIARIO SIN DEFINIR>";
    |GRABA 020#20n;
    |LEE 121#20=;
|FINSI;
|SI d_opera = "D";
        #20#2 = #20#2 + d_impor;        || debe
|SINO;
        #20#3 = #20#3 + d_impor;        || haber
|FINSI;
|GRABA 020#20a;
|LIBERA #20;
|FPRC;

|PROCESO altatra;
    modcon = 1;

    #27#0 = codcon;
    #27#1 = #23#4;
    #27#2 = #23#5;
    #27#3 = #23#1;
    #27#4 = #23#8;
    #27#6 = #23#0;
|LEE 101#27=;
|SI FSalida ! 0;
    |DDEFECTO #27;
        #27#0 = codcon;
        #27#1 = #23#4;
        #27#2 = #23#5;
        #27#3 = #23#1;
        #27#4 = #23#8;
        #27#6 = #23#0;
    |GRABA 101#27b;
|FINSI;
    importe = #23#9;  || aqui si que se usa el importe real
|SI opera = "B";
        importe = -importe;
|FINSI;
    #27#5 = #27#5 + importe;
|GRABA 101#27a;
|LIBERA #27;
|FPRC;

|PROCESO alta_cuen;
    guarda_pos = Pos;
    Pos = -1;

    #24#2 =  #40#4;      || nombre cliente
    #24#3 = "S";         || pase directo SI
|HAZ defect1;
|PUSHV 0501 2380;
|PINPA #0#24;
|PINDA #0#24;
|ENDATOS #1#24;
|SI FSalida = 0;
    |GRABA 020#24b;
|FINSI;
|POPV;

    Pos = guarda_pos;
|FPRC;

|PROCESO alta_cli;
|HAZ digito;
|SI digcu ! 0;|Y impor ! 0;
        guarda_pos = Pos;
        Pos = -1;

        #12#0 = "C";
        #12#10 = conte;
        #12#11 = digcu;
    |LEE 000#12=;
    |SI FSalida ! 0;
            #12#1 = #24#2;        || nombre
            #12#9 = #40#6;        || cif
        |PUSHV 0501 2380;
        |PINPA #0#12;
        |PINDA #0#12;
        |ENDATOS #1#12;
        |SI FSalida = 0;
            |GRABA 020#12n;
        |FINSI;
        |POPV;
    |FINSI;

        Pos = guarda_pos;
|FINSI;
|FPRC;

|PROCESO actualiz1;
    #24#0 = #23#4;
    #24#1 = #23#5;
|LEE 101#24=;
|SI FSalida ! 0;
    |HAZ alta_cuen;
|FINSI;
|SI #23#0 = 0;
    |SI #23#8 = "D";
            #24#9 = #24#9 + ressum;
            #24#51 = #24#51 + ressum;
    |SINO;
            #24#10 = #24#10 + ressum;
            #24#52 = #24#52 + ressum;
    |FINSI;
        #24#11 = #24#9 - #24#10;
        #24#53 = #24#51 - #24#52;
|SINO;
    |SI #23#0 = 99;
        |SI #23#8 = "D";
                #24#48 = #24#48 + ressum;
                #24#51 = #24#51 + ressum;
        |SINO;
                #24#49 = #24#49 + ressum;
                #24#52 = #24#52 + ressum;
        |FINSI;
            #24#50 = #24#48 - #24#49;
            #24#53 = #24#51 - #24#52;
    |SINO;
        |HAZ actualiz2;
    |FINSI;
|FINSI;
|SI #24#58 < #23#1;
        #24#58 = #23#1;
|FINSI;
|GRABA 020#24a;
|LIBERA #24;
|FPRC;

|PROCESO actualiz2;
    ini = dig1;      || numero de niveles
    fecalf = #23#1;
    mes = fecalf % 402;
    mesfec = mes;
|SI ini = 1;
        mesac = mesfec;
|SINO;
        mesac = (mesfec - ini) + 1;
    |SI mesac [ 0;
            mesac = (13 - ini) + mesfec;
    |FINSI;
|FINSI;
    a = (mesac * 3) + 9;
    b = a + 1;
    c = a + 2;
|SI #23#8 = "D";
        #24a = #24a + ressum;
        #24#51 = #24#51 + ressum;
|SINO;
        #24b = #24b + ressum;
        #24#52 = #24#52 + ressum;
|FINSI;
    #24c = #24a - #24b;
    #24#53 = #24#51 - #24#52;
|FPRC;

|PROCESO cabecera;
|DDEFECTO #22;
    #22#0 = #23#0;
    #22#1 = #23#1;
    #22#2 = #23#2;
|LEE 101#22=;
    FEstado = FSalida;
    #22#0 = #23#0;         || diario
    #22#1 = #23#1;         || fecha
    #22#2 = #23#2;         || documento
    #22#3 = #22#2;         || asiento
|SI #23#8 = "D";
        #22#4 = #22#4 + #23#9;
    |SI #22#09 = 0; #22#08 = #23#4; #22#09 = #23#5; |FINSI; || contrp.HABER
|FINSI;
|SI #23#8 = "H";
        #22#5 = #22#5 + #23#9;
    |SI #22#11 = 0; #22#10 = #23#4; #22#11 = #23#5; |FINSI; || contrp.DEBE
|FINSI;
    #22#6 = #22#4 - #22#5;
|SI FEstado = 0;  |GRABA 020#22a;  |FINSI;
|SI FEstado ! 0;  |GRABA 020#22n;  |FINSI;
|LIBERA #22;
|FPRC;

|PROCESO la_cuenta;
  p_alfa1 = conte;
  p_error = 0;
  p_lugar = 0;
  p_cuan3 = 0;
  p_cuan4 = 0;
  p_alfa3 = "";
  p_alfa4 = "";
  |PARA p_nume1 = 1; |SI p_nume1 [ 12; |HACIENDO p_nume1 = p_nume1 + 1;
        p_nume2 = (p_nume1 * 100) + 1;
        p_alfa2 = p_alfa1 % p_nume2;
        |SI p_alfa2 ! " ";
            |SI p_alfa2 = ".";
                p_lugar = p_nume1;
            |SINO;
                |SI p_lugar=0; p_alfa3=p_alfa3+p_alfa2; p_cuan3=p_cuan3+1; |FINSI;
                |SI p_lugar!0; p_alfa4=p_alfa4+p_alfa2; p_cuan4=p_cuan4+1; |FINSI;
            |FINSI;
        |FINSI;
  |SIGUE;
  |SI p_lugar ! 0;
      |SI dig3 = 1;  p_cuan5 = dig4;  digcu = 1;  |FINSI;
      |SI dig3 = 2;  p_cuan5 = dig5;  digcu = 2;  |FINSI;
      |SI dig3 = 3;  p_cuan5 = dig6;  digcu = 3;  |FINSI;
      |SI dig3 = 4;  p_cuan5 = dig7;  digcu = 4;  |FINSI;
      |SI dig3 = 5;  p_cuan5 = dig8;  digcu = 5;  |FINSI;
      |SI dig3 = 6;  p_cuan5 = dig9;  digcu = 6;  |FINSI;
      p_nume1 = p_cuan5 - (p_cuan3 + p_cuan4);
      |SI p_nume1 > 0;
          p_ceros = "0" * p_nume1;
          p_alfa5 = p_alfa3 + p_ceros + p_alfa4;
      |SINO;
          |MENAV p_mensa;
          p_error = 1;
      |FINSI;
  |SINO;
      p_alfa5 = p_alfa1;
  |FINSI;
  conte = p_alfa5;
|FPRC;

|PROCESO digito;
    digcu = 0;
    alfa1 = conte;  |QBF alfa1;
    alfa2 = alfa1 % 0;
    nume1 = alfa2;
|SI nume1 = dig4;  digcu = 1;  |FINSI;
|SI nume1 = dig5;  digcu = 2;  |FINSI;
|SI nume1 = dig6;  digcu = 3;  |FINSI;
|SI nume1 = dig7;  digcu = 4;  |FINSI;
|SI nume1 = dig8;  digcu = 5;  |FINSI;
|SI nume1 = dig9;  digcu = 6;  |FINSI;
|SI nume1 = 0;     digcu = 0;  |FINSI;
|FPRC;

|PROCESO apuntes;
|SI impor ! 0;
    ||HAZ la_cuenta;     || por si viene en formato '43.1'
    |HAZ digito;
    |SI conte ! "            ";|Y digcu ! 0;
        |SI #40#33 = "1";     || en abonos cambia el signo
            |SI signe = "D";
                    signe = "H";
            |SINO;
                    signe = "D";
            |FINSI;
        |FINSI;
        |DDEFECTO #23;
            #23#00 = #0#0;        || diario
            #23#01 = fecha;       || fecha
            #23#02 = #26#1;       || documento
            #23#03 = ult_apunt;   || apunte
            #23#04 = conte;       || cuenta
            #23#05 = digcu;       || digito cuenta
            #23#06 = conce;       || concepto
            #23#07 = descr;       || descripcion
            #23#08 = signe;       || signo
            #23#09 = impor;       || importe
        |SI #23#08 = "D";
                #23#16 = #23#4;
                #23#17 = #23#5;
        |SINO;
                #23#10 = #23#4;
                #23#11 = #23#5;
        |FINSI;
            #23#12 = "R";     || tipo relacion
            #23#13 = #13#0;   || libro
            #23#14 = #13#2;   || nro.factura
            #23#15 = "";      || serie
            #23#19 = tacu;    || tipo acumulador
            #23#20 = nacu;    || nro. acumulador
            #23#21 = "";          || oficial
        |SI #40#34 = "1";     #23#21 = "01"; |FINSI;   || no oficial
        |GRABA 020#23n;
        |HAZ cabecera;
            ressum = #23#9;
        |HAZ actualiz1;     || actualiza cuentas
            opera = "A";
        |HAZ altatra;       || actualiza niveles inferiores
            d_diari = #23#0;
            d_opera = #23#8;
            d_impor = #23#9;
        |HAZ actu_diari;    || actualiza diario
            ult_apunt = ult_apunt + 10;
    |FINSI;
|FINSI;
|FPRC;

|PROCESO asiento;        || grabacion de asientos
    nacu = 0;
    signe="D"; impor=#40#8;  conte=#40#3; tacu=""; |HAZ apuntes; |HAZ alta_cli;
    signe="D"; impor=#40#11; conte=ctdes; tacu=""; |HAZ apuntes;
|PARA para1 = 12; |SI para1 [ 15; |HACIENDO para1 = para1 + 1;
    |SI #40para1 ! 0;
            para2 = para1 + 4;
            para3 = para1 + 8;

            nacu = para1 - 11;
            signe="H"; impor=#40para2; conte=ctiva; tacu="I"; |HAZ apuntes;
            signe="H"; impor=#40para3; conte=ctrec; tacu="R"; |HAZ apuntes;
            signe="H"; impor=#40para1; conte=ctven; tacu="B"; |HAZ apuntes;
    |FINSI;
|SIGUE;
|FPRC;
____________________________________/ GRABACION DE FACTURAS
|PROCESO gra_factura;
    #13#1  = "";         || ~
    #13#3  = fecha;      || fecha
    #13#4  = #40#3;      || cuenta
    conte = #13#4;  |HAZ digito;
    #13#5  = digcu;      || digito
    #13#6  = #40#9;      || cto.
    #13#7  = descr;      || descripcion
    #13#8  = #40#12;     || base 1
    #13#9  = #40#13;     || base 2
    #13#10 = #40#14;     || base 3
    #13#11 = #40#24;     || (%) iva 1
    #13#12 = #40#25;     || (%) iva 2
    #13#13 = #40#26;     || (%) iva 3
    #13#14 = #40#16;     || cuota iva 1
    #13#15 = #40#17;     || cuota iva 2
    #13#16 = #40#18;     || cuota iva 3
    #13#17 = #40#28;     || (%) rec. 1
    #13#18 = #40#29;     || (%) rec. 2
    #13#19 = #40#30;     || (%) rec. 3
    #13#20 = #40#20;     || cuota recargo 1
    #13#21 = #40#21;     || cuota recargo 2
    #13#22 = #40#22;     || cuota recargo 3
    #13#23 = #11#8;      || deducible
    #13#24 = "N";        || exportado S/N
    #13#25 = "";         || ~
    #13#26 = #40#8;      || total factura
    #13#28 = "N";        || liquidada
    #13#44 = 0;          || periodo
    #13#45 = #0#0;       || diario
    #13#46 = fecha;      || fecha
    #13#47 = #26#1;      || documento
    #13#48 = #11#9;      || inversiones
    #13#49 = "";         || departamento
|SI #40#34 = "1";   #13#49 = "01"; |FINSI;
    #13#50 = #40#4;      || nombre cliente
    #13#51 = #40#6;      || cif cliente
    #13#52 = #40#15;     || base 4
    #13#53 = 0;          || base 5
    #13#54 = #40#27;     || (%) iva 4
    #13#55 = 0;          || (%) iva 5
    #13#56 = #40#19;     || cuota iva 4
    #13#57 = 0;          || cuota iva 5
    #13#58 = #40#31;     || (%) rec. 4
    #13#59 = 0;          || (%) rec. 5
    #13#60 = #40#23;     || cuota recargo 4
    #13#61 = 0;          || cuota recargo 5

    alfa1 = #13#3;  alfa1 = alfa1 % -104;      || saca a¤o
    alfa2 = "31.03." + alfa1;  fech1 = alfa2;  || primer trimestre
    alfa2 = "30.06." + alfa1;  fech2 = alfa2;  || segundo trimestre
    alfa2 = "30.09." + alfa1;  fech3 = alfa2;  || tercer trimestre
|SI #13#3 [ fech1;                  #13#44 = 1;   |FINSI;
|SI #13#3 > fech1;|Y #13#3 [ fech2; #13#44 = 2;   |FINSI;
|SI #13#3 > fech2;|Y #13#3 [ fech3; #13#44 = 3;   |FINSI;
|SI #13#3 > fech3;                  #13#44 = 4;   |FINSI;

|SI #40#33 = "1";   || en abonos cambia el signo
        #13#8 = -#13#8;       #13#9 = -#13#9;     #13#10 = -#13#10;
        #13#14 = -#13#14;     #13#15 = -#13#15;   #13#16 = -#13#16;
        #13#20 = -#13#20;     #13#21 = -#13#21;   #13#22 = -#13#22;
        #13#26 = -#13#26;
        #13#52 = -#13#52;     #13#53 = -#13#53;
        #13#56 = -#13#56;     #13#57 = -#13#57;
        #13#60 = -#13#60;     #13#61 = -#13#61;
|FINSI;

|SI FEstado = 0;    |GRABA 020#13a;     |FINSI;
|SI FEstado ! 0;    |GRABA 020#13n;     |FINSI;

    estado_iva = FSalida;
|FPRC;

|PROCESO factura;
    estado_iva = -1;

|DDEFECTO #13;
    #13#0  = #40#5;      || libro
    #13#2  = #40#1;      || nro. fra.
    #13#27 = "";         || serie (inhibida)
|LEE 101#13=;
    FEstado = FSalida;
|SI FEstado = 0;
    |AVISO;
        alfa1 = "    La factura [" + #13#0 + "-" + #13#2 + "] YA existe ...";
    |MENSA alfa1;
    |CONFI "    NRemplazarla (S/N): ";
    |SI FSalida = 0;
        |HAZ gra_factura;
    |SINO;
        |MENAV "    Deberia revisar el Libro de IVA Repercutido ...";
        |DDEFECTO #13;
    |FINSI;
|SINO;
    |HAZ gra_factura;
|FINSI;
|LIBERA #13;
|FPRC;
____________________________________/ CARGA VARIABLES VARIAS IVA Y ASIENTOS
|PROCESO descuento;
    descu = #40#11;
|SI descu ! 0;      || si hay descuento
        nume1 = #40#12;
        nume1 = nume1 + descu;
        #40#12 = nume1;

        ||tota = #40#8;
        ||base = #40#12;
        ||piva = #40#24;
        ||prec = #40#28;
        ||civa = ((base - descu) % piva) ! 0;
        ||crec = ((base - descu) % prec) ! 0;
        ||tota = (base + civa + crec) - descu;

        ||#40#8 = tota;    || nuevo total
        ||#40#16 = civa;   || nuevo cuota iva 1
        ||#40#20 = crec;   || nuevo couta recargo 1
|FINSI;
|FPRC;

|PROCESO concepto;
|DDEFECTO #21;
    conce = #40#9;
    #21#0 = conce;
|LEE 000#21=;
    descr = #21#1;  |QBF descr;

    alfa1 = #40#1;  |QBT alfa1;    || nro.fra.
    descr = descr + "[" + alfa1 + "]-[" + fecha + "]";
|FPRC;

|PROCESO fecha;
    alfa1 = #40#2 % 102;      || a¤o
    alfa2 = #40#2 % 302;      || mes
    alfa3 = #40#2 % 502;      || dia

    nume1 = alfa1;
|SI nume1 > 80;  alfa1="19"+alfa1;  |SINO;  alfa1="20"+alfa1;  |FINSI;

    fecha = alfa3 + "." + alfa2 + "." + alfa1;
|FPRC;

|PROCESO ctaivas;
    #11#0 = "R";
    #11#1 = #10#4;        || tipo de iva 1
|LEE 000#11=;
    Fcuentas = FSalida;
|FPRC;

|PROCESO contrap;
    #10#0 = "R";
|SI #40#33 = "1";
        #10#1 = 99;      || para abonos usa la linea 99 (fija)
|SINO;
        #10#1 = #40#5;
|FINSI;
|LEE 000#10=;
    Fcuentas = FSalida;
|FPRC;

|PROCESO cuentas;
    ctiva = "";
    ctrec = "";
    ctven = "";
    ctdes = "";

|HAZ contrap;
|PARA; |SI Fcuentas ! 0; |HACIENDO;
    |SI #10#1 = 1;  alfa2 = "[Interior]";         |FINSI;
    |SI #10#1 = 2;  alfa2 = "[Exportacion]";      |FINSI;
    |SI #10#1 = 3;  alfa2 = "[Intracomunitaria]"; |FINSI;
        alfa1 = "    Contrapartida [" + #10#1 + "]-" + alfa2 + " sin definir ...";
    |MENAV alfa1;
    |SUB_EJECUTA "cacoivca";
    |HAZ contrap;
|SIGUE;

    ctven = #10#2;   || ventas
    ctdes = #10#11;  || descuentos

|HAZ ctaivas;
|PARA; |SI Fcuentas ! 0; |HACIENDO;
        alfa1 = "    Cuentas IVA tipo [" + #11#1 + "] sin definir ...";
    |MENAV alfa1;
    |SUB_EJECUTA "cacuivca";
    |HAZ ctaivas;
|SIGUE;
    ctiva = #11#3;    || iva
    ctrec = #11#6;    || recargo
|FPRC;

|PROCESO contador;
|LEE 101#26p;
|SI FSalida ! 0;
    |DDEFECTO #26;
    |GRABA 020#26n;
    |LEE 101#26p;
|FINSI;
    #26#1 = #26#1 + 1;
|GRABA 020#26a;
|LIBERA #26;
    ult_apunt = 1;

|PINTA 2243, #26#1;
|FPRC;

|PROCESO varios;         || variables varias
|HAZ contador;
|HAZ cuentas;
|HAZ fecha;
|HAZ concepto;
|HAZ descuento;
|FPRC;

|PROCESO conta1;         || comprobaciones y calculos
|DDEFECTO #40;
    #40#0 = field1 % 0101;    || tipo factura
    #40#1 = field1 % 0210;    || numero factura
    #40#2 = field1 % 1206;    || fecha factura
    #40#3 = field1 % 1810;    || cuenta cliente
    conte = #40#3;  |HAZ digito;
    #40#4 = field1 % 2829;    || nombre cliente
    #40#5 = field1 % 5701;    || clase factura
|SI #40#5 = "0";    #40#5 = "1";   |FINSI;        || por defecto a 1
    #40#6 = field1 % 5815;    || cif cliente
    #40#7 = field1 % 7301;    || recargo S/N
    #40#8 = field1 % 7411;    || total factura
    #40#9 = field1 % 8502;    || cto. autom.
    #40#10= field1 % 8720;    || descrip.apunte
    #40#11= field1 % 10709;   || ptas.dto.fra.
    #40#12= field2 % 0111;    || base 1
    #40#13= field2 % 1211;    || base 2
    #40#14= field2 % 2311;    || base 3
    #40#15= field2 % 3411;    || base 4
    #40#16= field2 % 4511;    || iva 1
    #40#17= field2 % 5611;    || iva 2
    #40#18= field2 % 6711;    || iva 3
    #40#19= field2 % 7811;    || iva 4
    #40#20= field2 % 8911;    || recargo 1
    #40#21= field2 % 10011;   || recargo 2
    #40#22= field2 % 11111;   || recargo 3
    #40#23= field2 % 12211;   || recargo 4
    #40#24= field2 % 13302;   || (%) iva 1
    #40#25= field2 % 13502;   || (%) iva 2
    #40#26= field2 % 13702;   || (%) iva 3
    #40#27= field2 % 13902;   || (%) iva 4
    #40#28= field2 % 14102;   || (%) rec. 1
    #40#29= field2 % 14302;   || (%) rec. 2
    #40#30= field2 % 14502;   || (%) rec. 3
    #40#31= field2 % 14702;   || (%) rec. 4
    #40#32= field2 % 14910;   || cuenta ventas
    #40#33= field2 % 15901;   || signo factura
    #40#34= field2 % 16001;   || tipo apunte

|SI #40#0 = "E";|Y digcu ! 0;      || solo las emitidas y cuentas validas
    |HAZ varios;
        fech1 = fecha;
    |SI fech1 ] fec1;|Y fech1 [ fec2;
        |SI #40#8 ! 0;|Y #40#12 = 0;|Y #40#13 = 0;|Y #40#14 = 0;|Y #40#15 = 0;
                #40#12 = #40#8;         || exentas!!
        |FINSI;
        |HAZ factura;
        |SI estado_iva = 0;
                seleccio = 1;
            |HAZ asiento;
        |FINSI;
    |SINO;
            sw_fecha = 1;
    |FINSI;
|FINSI;

|SI digcu = 0;      estado_iva = -2;    |FINSI;   || corta el bucle
|FPRC;

|PROCESO tipo3;
|SI #0#1 = "SI";
   |HAZ grabatra;

       fabre = #0#2;     |QBF fabre;
       fabre = "rb  " + fabre;
   |FABRE fabre;
   |SI FSalida ] 0;
           fcanl = FSalida;

       |ABRE #10;      || contrapartidas IVA
       |ABRE #11;      || cuentas IVA
       |ABRE #12;      || clientes/proveedores
       |ABRE #13;      || iva repercutido
       |ABRE #20;      || diarios
       |ABRE #21;      || conceptos
       |ABRE #22;      || apuntes cabs.
       |ABRE #23;      || apuntes lins.
       |ABRE #24;      || plan contable
       |ABRE #26;      || contador asientos
       |ABRE #27;      || diario de trabajo

           field1 = fcanl + " 115";   |FLEE field1;  fsali1 = FSalida;
           field2 = fcanl + " 160";   |FLEE field2;  fsali2 = FSalida;
       |PARA; |SI fsali1 = 115;|Y fsali2 = 160; |HACIENDO;
           |HAZ conta1;
           |SI estado_iva = -2;    || error en digito de cuenta
               |MENAV "    Longitud de Cuenta fuera de nivel ...";
               |SAL;
           |FINSI;
               field1 = fcanl + " 115";   |FLEE field1;  fsali1 = FSalida;
               field2 = fcanl + " 160";   |FLEE field2;  fsali2 = FSalida;
       |SIGUE;

       |CIERRA #10;
       |CIERRA #11;
       |CIERRA #12;
       |CIERRA #13;
       |CIERRA #20;
       |CIERRA #21;
       |CIERRA #22;
       |CIERRA #23;
       |CIERRA #24;
       |CIERRA #26;
       |CIERRA #27;

       |FCIERRA fcanl;
           Pos = -1;
   |SINO;
           alfa1 = "    [" + (fabre % 5) + "] inaccesible o inexistente ...";
       |MENAV alfa1;
   |FINSI;

   |HAZ niveles;

   |SI sw_fecha = 1;
       |MENAV "    Hay apuntes con fecha fuera de periodo ...";
   |FINSI;

   |SI seleccio = 0;
       |MENAV "    Seleccion vacia ...";
   |FINSI;
|FINSI;
|FPRC;

|PROGRAMA;
|CLS;
|CABEZA "Enlace KRISPIN";
|PINPA #0#0;
|HAZ inicio;
|DDEFECTO #0;
|ABRE #0;
|LEE 101#0p;
|SI FSalida ! 0;    |GRABA 020#0b; |FINSI;
|PINDA #0#0;
|ENDATOS #1#0;
|SI FSalida = 0;
    |HAZ tipo3;
        #0#1 = "NO";
    |GRABA 020#0a;
|FINSI;
|LIBERA #0;
|CIERRA #0;
|FPRO;
