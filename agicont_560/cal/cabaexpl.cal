|FICHEROS;
    cabaexpl #0;
    cacuenta #1;
    caparexp #2;
    cabaexp0 #3;
    cabaexp1 #4;
    cabaexp2 #5;
    caexistc #6;
    caexistl #7;
    canempre #30;
    caconimp #8;
|FIN;

|VARIABLES;
  &pathbal = "";
  &balance = 1;
    &entrada = 0;
    potra = 0;
    varcamp = 0;
    mes = 0;
    campito = 0;
    nivel = 0;
    sw_pasa = 0;
    linea = 0;
    FEstado = 0;
    posici = 0;
    debe1 = 0;
    x = 0;
    &eldesde = 0;
    &elhasta = 0;
    sw_d1 = 0;
    debe2 = 0;
    sw_d2 = 0;
    debe3 = 0;
    sw_d3 = 0;
    debe4 = 0;
    sw_d4 = 0;
    haber1 = 0;
    sw_h1 = 0;
    haber2 = 0;
    sw_h2 = 0;
    haber3 = 0;
    sw_h3 = 0;
    sw = 0;
    bloque = 0;
    agrupaci = 0;
    epigrafe = 0;
    partida = 0;
    bloque1 = 0;
    H = 0;
    sw_epi = 0;
    sw_d21 = 0;
    sw_d31 = 0;
    sw_h21 = 0;
    sw_h31 = 0;
    sw_h32 = 0;
    punto_h1 = 0;
    punto_d1 = 0;
    punto_h2 = 0;
    punto_d2 = 0;
    punto_h3 = 0;
    punto_d3 = 0;
    punto_h4 = 0;
    punto_d4 = 0;
    punto_h5 = 0;
    punto_d5 = 0;
    punto_h12 = 0;
    punto_d12 = 0;
    punto_h22 = 0;
    punto_d22 = 0;
    punto_h32 = 0;
    punto_d32 = 0;
    punto_h42 = 0;
    punto_d42 = 0;
    punto_h52 = 0;
    punto_d52 = 0;
    letrames = "";
    d = "D";
    h = "H";
    tipo = "";
    tipo1 = "";
    &sw_inf = 0;
    &cantidad = 0;
    &cantidad2 = 0;
    &variable = "";
    &canti = 0;
    &canti2 = 0;
    &doh = "";
    mes_fin = 0;
    descar = 0;
    traraf = 0;
    informe = "";
    alfa1 = "";
    &r_emp = 0;
    &r_per = 0;
    &anu = 0;
    &anu1 = 0;
    &cta = "";
    &nom = "";
    acumula = 0;
    e1 = 0;
    e2 = 0;
    e3 = 0;
    e4 = 0;
    dos = 0;
    acumul2 = 0;
    alfa2 = "";
    alfa3 = "";
    debe11 = 0;
    debe21 = 0;
    debe31 = 0;
    debe41 = 0;
    haber11 = 0;
    haber21 = 0;
    haber31 = 0;
    tracan5 = 0;
    &d_mes = 0;
    &h_mes = 0;
    &estru = 0;
    &r_eje = 0;
    &r_nom = "";
    &aparam = "";
|FIN;

|INCLUYE acceso_i;
|INCLUYE digito_i;
|INCLUYE protec_i;

|PROCESO antesque;
  aparam = PARAMETRO;
  |QBF aparam;
  |SI aparam = "fech";
      |NOME_DAT #1 "cabalano";
  |FINSI;
|FPRC;

|| ========================================================================
||                        CALCULOS PANTALLA
|| ========================================================================

|PROCESO mayor;
  alfa1 = #0Campo > " ";
  #0Campo = alfa1;
  |PINTA #0Campo;
|FPRC;

|PROCESO mayor1;
  |HAZ mayor;
  |SI #0Campo = "N";
      #0#14 = empre;
      #0#15 = ejerc;
      |CAMPO_MODIFICA #0#14,1,"N";
      |CAMPO_MODIFICA #0#15,1,"N";
      |PINTA #0#14;
      |PINTA #0#15;
  |SINO;
      |CAMPO_MODIFICA #0#14,1,"S";
      |CAMPO_MODIFICA #0#15,1,"S";
  |FINSI;
|FPRC;

|PROCESO deentrada;  |TIPO 7;
  |SI #0#14 = 0;  |Y #0#15 = 0;
      #0#14 = empre;
      #0#15 = ejerc - 1;
      |PINTA #0#14;
      |PINTA #0#15;
  |FINSI;
|FPRC;

|PROCESO esbuena;
  |SI FSalida = 9;  |ACABA;  |FINSI;
  |ABRE #30;
  #30#2 = #0#14;
  #30#3 = 0;
  |LEE 011#30];
  |SI FSalida ! 0;  |O #30#2 ! #0#14;
      |MENAV "     NO Existe la empresa Contable";
      |ERROR;
  |FINSI;
  |CIERRA #30;
|FPRC;

|PROCESO consulta;
  |SI FSalida ! 9;  |ACABA;  |FINSI;
  |CIERRAT #30;
  r_emp = 0;
  |SUB_EJECUTA "caconemp.run";
  |ABRET #30;
  |ERROR;
  |SI Campo = 14;
      #0#14 = r_emp;
      #0#15 = r_per;
      |PTEC 802;
      |PTEC 802;
  |FINSI;
|FPRC;

|PROCESO esbuena1;
  |SI FSalida = 2; |ACABA; |FINSI;
  |ABRE #30;
  #30#2 = #0#14;
  #30#3 = #0#15;
  |LEE 011#30=;
  |SI FSalida ! 0;
      |MENAV "     NO Existe Periodo en la empresa Contable";
      |CIERRA #30;
      |ERROR;
      |ACABA;
  |FINSI;
  |SI #30#13 ! dig3;  |O #30#14 ! dig4;  |O #30#15 ! dig5;
                      |O #30#16 ! dig6;  |O #30#17 ! dig7;
                      |O #30#18 ! dig8;  |O #30#19 ! dig9;
      |MENAV "     NO CORRESPONDEN LOS NIVELES";
      |CIERRA #30;
      |ERROR;
      |ACABA;
  |FINSI;
  #0#16 = #30#1;
  anu1 = 1900 + #30#26;
  |SI anu1 [ 1970;
      anu1 = anu1 + 100;
  |FINSI;
  |PINTA #0#16;
  #30#2 = empre;
  #30#3 = ejerc;
  |LEE 011#30=;
  anu = 1900 + #30#26;
  |SI anu [ 1970;
      anu = anu + 100;
  |FINSI;
  |CIERRA #30;
|FPRC;


|PROCESO pintames; |TIPO 0;
  varcamp = Campo + 2;                || Coge el campo del nombre del mes
  |SI #0Campo ! 0;|Y #0Campo ! 13;
      mes = #30#11 + (#0Campo - 1);   || Operacion para saber que mes es
  |SINO;
      mes = #0Campo;
  |FINSI;
  |HAZ mesletra;                      || Recoge el mes en letra
  #0varcamp = letrames;
  potra = Campo;
  |PINTA #0varcamp;                   || Pinta el nombre del mes
  Campo = potra;

  |SI Campo = 1;  |Y #0#0 > #0#1;
      |MENAV "    Error de Entrada ...";
      |ERROR;
  |FINSI;
|FPRC;

|PROCESO pintames2; |TIPO 0;
  |SI #0Campo ! 0;|Y #0Campo ! 13;
      mes = #30#11 + (#0Campo - 1);   || Operacion para saber que mes es
  |SINO;
      mes = #0Campo;
  |FINSI;
  |HAZ mesletra;
  |ATRI I;
  |SI Campo = 7;  |PINTA 1727, letrames;  |FINSI;
  |SI Campo = 8;  |PINTA 1827, letrames;  |FINSI;
  |ATRI 0;
  |SI Campo = 8;  |Y #0#7 > #0#8;
      |MENAV "    Error de Entrada ...";
      |ERROR;
   |FINSI;
|FPRC;

|PROCESO mesletra;
  |SI mes = 0;  letrames = "Apertura  "; |FINSI;
  |SI mes = 1;  letrames = "Enero     "; |FINSI;
  |SI mes = 2;  letrames = "Febrero   "; |FINSI;
  |SI mes = 3;  letrames = "Marzo     "; |FINSI;
  |SI mes = 4;  letrames = "Abril     "; |FINSI;
  |SI mes = 5;  letrames = "Mayo      "; |FINSI;
  |SI mes = 6;  letrames = "Junio     "; |FINSI;
  |SI mes = 7;  letrames = "Julio     "; |FINSI;
  |SI mes = 8;  letrames = "Agosto    "; |FINSI;
  |SI mes = 9;  letrames = "Septiembre"; |FINSI;
  |SI mes = 10; letrames = "Octubre   "; |FINSI;
  |SI mes = 11; letrames = "Noviembre "; |FINSI;
  |SI mes = 12; letrames = "Diciembre "; |FINSI;
  |SI mes = 13; letrames = "Cierre    "; |FINSI;
|FPRC;

|PROCESO nivel; |TIPO 7;
  |SI #0#4 = "N";
      |CAMPO_MODIFICA #0#5,1,"N";
  |SINO;
      |CAMPO_MODIFICA #0#5,1,"S";
  |FINSI;
|FPRC;

|PROCESO miraniv; |TIPO 0;
  nivel = #0#5;
  |SI nivel = 1;|Y dig4 = 0;  |AVISO;  |ERROR;  |FINSI;
  |SI nivel = 2;|Y dig5 = 0;  |AVISO;  |ERROR;  |FINSI;
  |SI nivel = 3;|Y dig6 = 0;  |AVISO;  |ERROR;  |FINSI;
  |SI nivel = 4;|Y dig7 = 0;  |AVISO;  |ERROR;  |FINSI;
  |SI nivel = 5;|Y dig8 = 0;  |AVISO;  |ERROR;  |FINSI;
  |SI nivel = 6;|Y dig9 = 0;  |AVISO;  |ERROR;  |FINSI;
|FPRC;

|PROCESO estruct; |TIPO 0;
|SI #0#6 ! 0;
    |ABRE #6;
    #6#0 = #0#6;
    |LEE 040#6=;
    |SI FSalida ! 0;
        |MENAV "    No existe la estructura de descarga.";
        |CIERRA #6;
        |ERROR;
        |ACABA;
    |FINSI;
    |CIERRA #6;
|FINSI;
|FPRC;


|| ========================================================================
||                        CALCULOS INFORME
|| ========================================================================
|PROCESO miracuen;
  |SI #4e3 = #1#0; |Y #4e4 = #1#1;
      descar = #4e1 - #4e2;
      |SI #1#5 = "H"; descar = -descar; |FINSI;
      cantidad = cantidad + descar;
      sw_pasa = 1;
  |FINSI;
|FPRC;

|DEFBUCLE cabaexpl4;
 #4#1;
 ;
 #0#6, 001;
 #0#6, 999;
 be;
 NULL, miracuen;
|FIN;

|PROCESO calculit;
  |HAZ digitos2;
  |SI d_error ! 0;  |ACABA;  |FINSI;
  |SI #0#4 = "N";  |Y #1#3 = "N";                     |ACABA;  |FINSI;
  |SI #0#4 = "S";  |Y #1#55 > nivel;                  |ACABA;  |FINSI;
  |SI #0#4 = "S";  |Y #1#3 = "N";  |Y #1#55 ! nivel;  |ACABA;  |FINSI;
  |HAZ grabatra;

  |SI #0#13 = "N"; |ACABA; |FINSI;
  |SI #0#4 = "N"; |ACABA; |FINSI;
     |BUCLE cabaexpl4;
     |SI sw_pasa = 1; |ACABA; |FINSI;
  |HAZ grabatr4;
|FPRC;

|DEFBUCLE cabaexpl0;
 #1#4;
 ;
 "D", 1, 01, 00, 00;
 "H", 4, 99, 99, 99;
 be;
 NULL, calculit;
|FIN;

|PROCESO impresio;
  |SI sw = 0;
      bloque = #3#1;
      tipo = #3#0;
      agrupaci = #3#2;
      epigrafe = #3#3;
      partida = #3#4;
      |SI #3#0 = "D"; doh = "DEBE";  |FINSI;
      |SI #3#0 = "H"; doh = "HABER"; |FINSI;
      sw_inf = 0;
      |PRINT;
      sw = 1;
  |FINSI;

  |SI bloque = #3#1; |Y tipo = #3#0;
      |HAZ epigraf;
  |SINO;
      |SI tipo ! #3#0;                    || ruptura por tipo
          bloque1 = bloque;
          bloque = 4;
          |HAZ mirablo;
          |PIEINF;
          sw_inf = 0;
          doh = "HABER";
          |PRINT;
          |HAZ epigraf;
          bloque = bloque1;
      |SINO;                              || ruptura por bloque
          |HAZ mirablo;
          |HAZ epigraf;
      |FINSI;
      bloque = #3#1;
      tipo = #3#0;
  |FINSI;
|FPRC;

|DEFBUCLE cabaexpl1;
 #3#1;
 ;
 "D", 1, 01, 00, 00;
 "H", 4, 99, 99, 99;
 be;
 NULL, impresio;
|FIN;

|PROCESO cuentas;
  |HAZ digitos2;
  |SI d_error ! 0;  |ACABA;  |FINSI;
  |SI #0#4 = "N";  |Y #1#3 = "N";                     |ACABA;  |FINSI;
  |SI #0#4 = "S";  |Y #1#55 > nivel;                  |ACABA;  |FINSI;
  |SI #0#4 = "S";  |Y #1#3 = "N";  |Y #1#55 ! nivel;  |ACABA;  |FINSI;
  sw_pasa = 0;
  cantidad = 0;
  sw_inf = 6;
   e1 = 4;
   e2 = 5;
   e3 = 6;
   e4 = 7;
  |BUCLE cabaexpl4;
  |SI sw_pasa = 0;
      |SI #3#0 =  "D";
          |PARA x = eldesde; |SI x [ elhasta; |HACIENDO x = x + 3;
                cantidad = cantidad + #1x;
          |SIGUE;
      |SINO;
          |PARA x = eldesde; |SI x [ elhasta; |HACIENDO x = x + 3;
                cantidad = cantidad + (#1x * -1);
          |SIGUE;
      |FINSI;
  |FINSI;
  |SI cantidad ! 0; |PRINT; |FINSI;
|FPRC;

|PROCESO cuenta1;
  sw_inf = 6;
  cantidad = 0;
  cantidad2 = 0;
      cta = #5#5;
      nom = #5#7;
      cantidad = #5#11;
      cantidad2 = #5#12;
  |SI cantidad ! 0;|O cantidad2 ! 0;
      |PRINT;
  |FINSI;
|FPRC;

|DEFBUCLE cabaexpl2;
 #1#4;
 ;
 #3#0, #3#1, #3#2, #3#3, #3#4;
 #3#0, #3#1, #3#2, #3#3, #3#4;
 be;
 NULL, NULL, NULL, NULL, NULL, cuentas;
 #1#0, #1#1;
|FIN;

|DEFBUCLE cabaexpl5;
 #5#1;
 ;
 #3#0, #3#1, #3#2, #3#3, #3#4, "            ", 0;
 #3#0, #3#1, #3#2, #3#3, #3#4, "zzzzzzzzzzzz", 9;
 e;
 NULL, NULL, NULL, NULL, NULL, cuenta1;
 #5#5, #5#6;
|FIN;

|PROCESO grabatra;
  #3#0 = #1#5;
  #3#1 = #1#4;
  #3#2 = #1#6;
  #3#3 = #1#7;
  #3#4 = #1#8;
  |LEE 101#3=;
  FEstado = FSalida;
  |SI FEstado ! 0;
      |DDEFECTO #3;
      #3#0 = #1#5;
      #3#1 = #1#4;
      #3#2 = #1#6;
      #3#3 = #1#7;
      #3#4 = #1#8;
      |HAZ leexplot;
      #3#5 = #2#3;
      #3#6 = #2#5;
      #3#7 = #2#7;
  |FINSI;
  sw_pasa = 0;
      |BUCLE cabaexpl4;
  |SI sw_pasa = 0;
      |HAZ resulta;
      #3#9 = 0;
      |SI #3acumula ! 0;
          |SI FEstado = 0;
              |GRABA 020#3a;
          |SINO;
              |GRABA 020#3n;
          |FINSI;
      |SINO;
          |SI FEstado = 0;
              |GRABA 020#3a;
          |FINSI;
      |FINSI;
  |FINSI;
  |LIBERA #3;
|FPRC;

|PROCESO grabatr1;
  #1#0 = #4e3;
  #1#1 = #4e4;
  |LEE 040#1=;
  |SI FSalida ! 0; |ACABA;  |FINSI;
  #3#0 = #1#5;
  #3#1 = #1#4;
  #3#2 = #1#6;
  #3#3 = #1#7;
  #3#4 = #1#8;
  |LEE 101#3=;
  FEstado = FSalida;
  |SI FEstado ! 0;
      |DDEFECTO #3;
      #3#0 = #1#5;
      #3#1 = #1#4;
      #3#2 = #1#6;
      #3#3 = #1#7;
      #3#4 = #1#8;
      |HAZ leexplot;
      #3#5 = #2#3;
      #3#6 = #2#5;
      #3#7 = #2#7;
  |FINSI;
         |SI #1#5 = "H"; descar = -descar; |FINSI;
  #3acumula = #3acumula + descar;
  #3#9 = 1;
  |SI #3acumula ! 0;
      |SI FEstado = 0; |GRABA 020#3a; |SINO; |GRABA 020#3n; |FINSI;
  |FINSI;
  |LIBERA #3;
  |HAZ sumasal;
|FPRC;

|PROCESO grabatr2; || graba cuenta de VARIACION DE EXISTENCIAS
      |HAZ cargatra;
         |SI #1#5 = "H"; tracan5 = -tracan5; |FINSI;
      #5acumul2 = #5acumul2 + tracan5;
      |GRABA 020#5a;
      |LIBERA #5;
|FPRC;

|PROCESO grabatr4; || graba cuenta de trabajo exp2
      |HAZ cargatra;
      #5acumul2 = 0;
      |SI #1#5 = "D";
          |PARA x = eldesde; |SI x [ elhasta; |HACIENDO x = x + 3;
                #5acumul2 = #5acumul2 + #1x;
          |SIGUE;
      |SINO;
          |PARA x = eldesde; |SI x [ elhasta; |HACIENDO x = x + 3;
                #5acumul2 = #5acumul2 + (#1x * -1);
           |SIGUE;
      |FINSI;
      |GRABA 020#5a;
      |LIBERA #5;
|FPRC;

|PROCESO cargatra;
  #5#0 = #1#5;
  #5#1 = #1#4;
  #5#2 = #1#6;
  #5#3 = #1#7;
  #5#4 = #1#8;
  |HAZ acortacuen;
  #5#5 = #1#0;
  #5#6 = #1#1;
  #5#7 = #1#2;
  |LEE 000#5=;
  |SI FSalida ! 0;
      |DDEFECTO #5;
      #5#0 = #1#5;
      #5#1 = #1#4;
      #5#2 = #1#6;
      #5#3 = #1#7;
      #5#4 = #1#8;
      |HAZ acortacuen;
      #5#5 = #1#0;
      #5#6 = #1#1;
      #5#7 = #1#2;
      #5#11 = 0;
      #5#12 = 0;
      |GRABA 020#5c;
  |FINSI;
  |DDEFECTO #2;
  #2#0 = #1#5;
  #2#1 = #1#6;
  #2#3 = #1#7;
  #2#5 = #1#8;
  |LEE 000#2=;
  |SI FSalida ! 0;
      #2#2 = "";
      #2#4 = "";
      #2#6 = "";
  |FINSI;
  #5#8 = #2#2;
  #5#9 = #2#4;
  #5#10 = #2#6;
|FPRC;

|PROCESO acortacuen;
  |SI nivel < #1#1;
      |SI nivel = 1;  traraf = 100 + dig4;  |FINSI;
      |SI nivel = 2;  traraf = 100 + dig5;  |FINSI;
      |SI nivel = 3;  traraf = 100 + dig6;  |FINSI;
      |SI nivel = 4;  traraf = 100 + dig7;  |FINSI;
      |SI nivel = 5;  traraf = 100 + dig8;  |FINSI;
      #1#0 = #1#0 % traraf;
      #1#1 = #0#5;
      #1#0 = #1#0 + "              ";
      #1#0 = #1#0 % 112;
  |FINSI;
|FPRC;


|PROCESO resulta;    || Calculamos cada cuenta
|SI #3#0 = "D"; |Y #3#1 = 1;
    |PARA x = eldesde; |SI x [ elhasta; |HACIENDO x = x + 3;
            #3acumula = #3acumula   + #1x;
            debe1 = debe1 + #1x;
    |SIGUE;
        sw_d1 = 1;
|FINSI;

|SI #3#0 = "D"; |Y #3#1 = 2;
    |PARA x = eldesde; |SI x [ elhasta; |HACIENDO x = x + 3;
            #3acumula = #3acumula   + #1x;
            debe2 = debe2 + #1x;
    |SIGUE;
        sw_d2 = 1;
|FINSI;

|SI #3#0 = "D"; |Y #3#1 = 3;
    |PARA x = eldesde; |SI x [ elhasta; |HACIENDO x = x + 3;
            #3acumula = #3acumula   + #1x;
            debe3 = debe3 + #1x;
    |SIGUE;
        sw_d3 = 1;
|FINSI;

|SI #3#0 = "D"; |Y #3#1 = 4;
    |PARA x = eldesde; |SI x [ elhasta; |HACIENDO x = x + 3;
            #3acumula = #3acumula   + #1x;
            debe4 = debe4 + #1x;
    |SIGUE;
        sw_d4 = 0;
|FINSI;

|SI #3#0 = "H"; |Y #3#1 = 1;
    |PARA x = eldesde; |SI x [ elhasta; |HACIENDO x = x + 3;
            #3acumula   = #3acumula   + (#1x * -1);
            haber1 = haber1 + (#1x * -1);
    |SIGUE;
        sw_h1 = 1;
|FINSI;

|SI #3#0 = "H"; |Y #3#1 = 2;
    |PARA x = eldesde; |SI x [ elhasta; |HACIENDO x = x + 3;
            #3acumula   = #3acumula   + (#1x * -1);
            haber2 = haber2 + (#1x * -1);
    |SIGUE;
        sw_h2 = 1;
|FINSI;

|SI #3#0 = "H"; |Y #3#1 = 3;
    |PARA x = eldesde; |SI x [ elhasta; |HACIENDO x = x + 3;
            #3acumula   = #3acumula   + (#1x * -1);
            haber3 = haber3 + (#1x * -1);
    |SIGUE;
        sw_h3 = 1;
|FINSI;
|FPRC;

|PROCESO sumasal;    || Calculamos cada cuenta
  |SI #1#5 = "D"; |Y #1#4 = 1;
      debe1 = debe1 + descar;
  |FINSI;

  |SI #1#5 = "D"; |Y #1#4 = 2;
      debe2 = debe2 + descar;
  |FINSI;

  |SI #1#5 = "D"; |Y #1#4 = 3;
      debe3 = debe3 + descar;
  |FINSI;

  |SI #1#5 = "D"; |Y #1#4 = 4;
      debe4 = debe4 + descar;
  |FINSI;

  |SI #1#5 = "H"; |Y #1#4 = 1;
      haber1 = haber1 + descar;
  |FINSI;

  |SI #1#5 = "H"; |Y #1#4 = 2;
      haber2 = haber2 + descar;
  |FINSI;

  |SI #1#5 = "H"; |Y #1#4 = 3;
      haber3 = haber3 + descar;
  |FINSI;
|FPRC;

|PROCESO epigraf;
  |SI epigrafe ! #3#3;
      sw_epi = 0;
  |FINSI;
  |HAZ rotura;
  epigrafe = #3#3;
|FPRC;

|PROCESO rotura;
  |SI #3#4 = 0;
      sw_inf = 2;
      |PRINT;
      |SI #0#4 = "S";
          |SI #0#13 = "N";
              |BUCLE cabaexpl2;
          |SINO;
              |BUCLE cabaexpl5;
          |FINSI;
          sw_inf = 5;
          |PRINT;
      |FINSI;
  |SINO;
      |SI sw_epi = 0;
          sw_inf = 3;       || epigrafe
          |PRINT;
          sw_epi = 1;       || variables
      |FINSI;
      sw_inf = 4;           || partida
      |PRINT;
      |SI #0#4 = "S";
          |SI #0#13 = "N";
              |BUCLE cabaexpl2;
          |SINO;
              |BUCLE cabaexpl5;
          |FINSI;
          sw_inf = 5;
          |PRINT;          || linea en blanco
      |FINSI;
  |FINSI;
|FPRC;

|PROCESO leexplot;  || Lee el fichero de explotacion
  #2#0 = #3#1;
  #2#1 = #3#0;
  #2#2 = #3#2;
  #2#4 = #3#3;
  #2#6 = #3#4;
  |LEE 040#2=; |SI FSalida ! 0; |DDEFECTO #2; |FINSI;
|FPRC;

|PROCESO final;    || Para cuando acaba el defbucle
  |SI tipo = "D"; bloque = 4; |FINSI;
  |SI tipo = "H"; bloque = 3; |FINSI;
  |HAZ mirablo;
|FPRC;


|PROCESO mirablo;
  sw_inf = 1;
  |SI tipo = "D";
      |HAZ bloqued1;
      |SI bloque = 1;  |Y #3#1 [ 2;  |ACABA;  |FINSI;
      |HAZ bloqued2;
      |SI bloque [ 2;  |Y #3#1 [ 3;  |ACABA;  |FINSI;
      |HAZ bloqued3;
      |SI bloque [ 3;  |ACABA;  |FINSI;
      |HAZ bloqued4;
  |FINSI;
  |SI tipo = "H";
      |HAZ bloqueh1;
      |SI bloque = 1;  |Y #3#1 [ 2;  |ACABA;  |FINSI;
      |HAZ bloqueh2;
      |SI bloque [ 2;  |ACABA;  |FINSI;
      |HAZ bloqueh3;
  |FINSI;
|FPRC;

|PROCESO bloqued1;
  |SI sw_d1 = 1; |O sw_h1 = 1; |HAZ tipod1;  |FINSI;      || I
|FPRC;

|PROCESO bloqued2;
  |SI sw_d2 = 1; |O sw_h2 = 1; |HAZ tipod2;  |FINSI;      || II
  |SI sw_d21 = 0;              |HAZ tipod21; |FINSI;      || III
|FPRC;

|PROCESO bloqued3;
  |SI sw_d3 = 1; |O sw_h3 = 1; |HAZ tipod3;  |FINSI;      || IV
  |SI sw_d31 = 0;              |HAZ tipod31; |FINSI;      || V
|FPRC;

|PROCESO bloqued4;
  |SI sw_d4 = 0;               |HAZ tipod4;  |FINSI;      || VI
|FPRC;

|PROCESO bloqueh1;
  |SI sw_d1 = 1; |O sw_h1 = 1; |HAZ tipoh1;  |FINSI;      || I
|FPRC;

|PROCESO bloqueh2;
  |SI sw_d2 = 1; |O sw_h2 = 1; |HAZ tipoh2;  |FINSI;      || II
  |SI sw_h21 = 0;              |HAZ tipoh21; |FINSI;      || III
|FPRC;

|PROCESO bloqueh3;
  |SI sw_d3 = 1; |O sw_h3 = 1; |HAZ tipoh3;  |FINSI;      || IV
  |SI sw_h31 = 0;              |HAZ tipoh31; |FINSI;      || V
  |SI sw_h32 = 0;              |HAZ tipoh32; |FINSI;      || VI
|FPRC;

|PROCESO tipod1;
  variable = "  I. BENEFICIOS DE EXPLOTACION ..........................";
  canti  = haber11 - debe11;
  canti2 = haber1 - debe1;
||------------------------------------- Solo una empresa
|SI #0#13 = "N";
  |SI canti < 0;
    punto_h1 = canti * -1;
    canti = 0;
  |SINO;
    sw_inf = 5; |PRINT;
    sw_inf = 1; |PRINT;
    sw_inf = 5; |PRINT;
    sw_d1 = 0;
    sw_h1 = 0;
    punto_d1 = canti;
  |FINSI;
|SINO; || --------------------------------------- las dos empresas
  |SI canti < 0;
    punto_h1 = canti * -1;
    canti = 0;
  |SINO;
    punto_d1 = canti;
  |FINSI;
  |SI canti2 < 0;
    punto_h12 = canti2 * -1;
    canti2 = 0;
  |SINO;
    punto_d12 = canti2;
  |FINSI;
  |SI sw_d1 = 0; |ACABA; |FINSI;
  |SI canti > 0; |Y canti2 > 0; sw_h1 = 0; |FINSI;
  |SI canti > 0; |O canti2 > 0;
    sw_inf = 5; |PRINT;
    sw_inf = 1; |PRINT;
    sw_inf = 5; |PRINT;
    sw_d1 = 0;
  |FINSI;
|FINSI;
|FPRC;

|PROCESO tipoh1;
  variable = "  I. PERDIDAS DE EXPLOTACION ............................";
  canti = debe11 - haber11;
  canti2 = debe1 - haber1;
||------------------------------------- Solo una empresa
|SI #0#13 = "N";
  |SI canti [ 0;
      punto_d1 = canti * -1;
      canti = 0;
  |SINO;
      sw_inf = 5; |PRINT;
      sw_inf = 1; |PRINT;
      sw_inf = 5; |PRINT;
      sw_d1 = 0;
      sw_h1 = 0;
      punto_h1 = canti;
  |FINSI;
|SINO; || --------------------------------------- las dos empresas
  |SI canti [ 0;
      punto_d1 = canti * -1;
      canti = 0;
  |SINO;
      punto_h1 = canti;
  |FINSI;
  |SI canti2 [ 0;
      punto_d12 = canti2 * -1;
      canti2 = 0;
  |SINO;
      punto_h12 = canti2;
  |FINSI;
  |SI sw_h1 = 0; |ACABA; |FINSI;
  |SI canti > 0; |Y canti2 > 0; sw_d1 = 0; |FINSI;
  |SI canti > 0; |O canti2 > 0;
    sw_inf = 5; |PRINT;
    sw_inf = 1; |PRINT;
    sw_inf = 5; |PRINT;
    sw_h1 = 0;
  |FINSI;
|FINSI;
|FPRC;

|PROCESO tipod2;
  variable = " II. RESULTADOS FINANCIEROS POSITIVOS ...................";
  canti = haber21 - debe21;
  canti2 = haber2 - debe2;
||------------------------------------- Solo una empresa
|SI #0#13 = "N";
  |SI canti < 0;
      punto_h2 = canti * -1;
      canti = 0;
  |SINO;
      sw_inf = 5; |PRINT;
      sw_inf = 1; |PRINT;
      sw_inf = 5; |PRINT;
      sw_d2 = 0;
      sw_h2 = 0;
      punto_d2 = canti;
  |FINSI;
|SINO; || --------------------------------------- las dos empresas
  |SI canti < 0;
      punto_h2 = canti * -1;
      canti = 0;
  |SINO;
      punto_d2 = canti;
  |FINSI;
  |SI canti2 < 0;
      punto_h22 = canti2 * -1;
      canti2 = 0;
  |SINO;
      punto_d22 = canti2;
  |FINSI;
  |SI sw_d2 = 0; |ACABA; |FINSI;
  |SI canti > 0; |Y canti2 > 0; sw_h2 = 0; |FINSI;
  |SI canti > 0; |O canti2 > 0;
    sw_inf = 5; |PRINT;
    sw_inf = 1; |PRINT;
    sw_inf = 5; |PRINT;
    sw_d2 = 0;
  |FINSI;
|FINSI;
|FPRC;

|PROCESO tipod21;
  variable = "III. BENEFICIOS DE LAS ACTIVIDADES ORDINARIAS ...........";
  canti = punto_d1 + punto_d2 - punto_h1 - punto_h2;
  canti2 = punto_d12 + punto_d22 - punto_h12 - punto_h22;
||------------------------------------- Solo una empresa
|SI #0#13 = "N";
  |SI canti < 0;
      punto_h3 = canti * -1;
      canti = 0;
  |SINO;
      sw_inf = 5; |PRINT;
      sw_inf = 1; |PRINT;
      sw_inf = 5; |PRINT;
      sw_d21 = 1;
      punto_d3 = canti;
  |FINSI;
|SINO; || --------------------------------------- las dos empresas
  |SI canti < 0;
      punto_h3 = canti * -1;
      canti = 0;
  |SINO;
      punto_d3 = canti;
  |FINSI;
  |SI canti2 < 0;
      punto_h32 = canti2 * -1;
      canti2 = 0;
  |SINO;
      punto_d32 = canti2;
  |FINSI;
  |SI canti > 0; |O canti2 > 0;
    sw_inf = 5; |PRINT;
    sw_inf = 1; |PRINT;
    sw_inf = 5; |PRINT;
    sw_d21 = 1;
  |FINSI;
|FINSI;
|FPRC;

|PROCESO tipoh2;
  variable = " II. RESULTADOS FINANCIEROS NEGATIVOS ...................";
  canti = debe21 - haber21;
  canti2 = debe2 - haber2;
||------------------------------------- Solo una empresa
|SI #0#13 = "N";
  |SI canti [ 0;
      punto_d2 = canti * -1;
      canti = 0;
  |SINO;
      sw_inf = 5; |PRINT;
      sw_inf = 1; |PRINT;
      sw_inf = 5; |PRINT;
      sw_d2 = 0;
      sw_h2 = 0;
      punto_h2 = canti;
  |FINSI;
|SINO; || --------------------------------------- las dos empresas
  |SI canti [ 0;
      punto_d2 = canti * -1;
      canti = 0;
  |SINO;
      punto_h2 = canti;
  |FINSI;
  |SI canti2 [ 0;
      punto_d22 = canti2 * -1;
      canti2 = 0;
  |SINO;
      punto_h22 = canti2;
  |FINSI;
  |SI sw_h2 = 0; |ACABA; |FINSI;
  |SI canti > 0; |Y canti2 > 0; sw_d2 = 0; |FINSI;
  |SI canti > 0; |O canti2 > 0;
    sw_inf = 5; |PRINT;
    sw_inf = 1; |PRINT;
    sw_inf = 5; |PRINT;
    sw_h2 = 0;
  |FINSI;
|FINSI;
|FPRC;

|PROCESO tipoh21;
  variable = "III. PERDIDAS DE LAS ACTIVIDADES ORDINARIAS .............";
  canti = punto_h1 + punto_h2 - punto_d1 - punto_d2;
  canti2 = punto_h12 + punto_h22 - punto_d12 - punto_d22;
||------------------------------------- Solo una empresa
|SI #0#13 = "N";
  |SI canti [ 0;
      punto_d3 = canti * -1;
      canti = 0;
  |SINO;
      sw_inf = 5; |PRINT;
      sw_inf = 1; |PRINT;
      sw_inf = 5; |PRINT;
      sw_h21 = 1;
      punto_h3 = canti;
  |FINSI;
|SINO; || --------------------------------------- las dos empresas
  |SI canti [ 0;
      punto_d3 = canti * -1;
      canti = 0;
  |SINO;
      punto_h3 = canti;
  |FINSI;
  |SI canti2 [ 0;
      punto_d32 = canti2 * -1;
      canti2 = 0;
  |SINO;
      punto_h32 = canti2;
  |FINSI;
  |SI canti > 0; |O canti2 > 0;
    sw_inf = 5; |PRINT;
    sw_inf = 1; |PRINT;
    sw_inf = 5; |PRINT;
    sw_h21 = 1;
  |FINSI;
|FINSI;
|FPRC;

|PROCESO tipod3;
  variable = " IV. RESULTADOS EXTRAORDINARIOS POSITIVOS ...............";
  canti = haber31 - debe31;
  canti2 = haber3 - debe3;
||------------------------------------- Solo una empresa
|SI #0#13 = "N";
  |SI canti < 0;
      punto_h4 = canti * -1;
      canti = 0;
  |SINO;
      sw_inf = 5; |PRINT;
      sw_inf = 1; |PRINT;
      sw_inf = 5; |PRINT;
      sw_d3 = 0;
      sw_h3 = 0;
      punto_d4 = canti;
  |FINSI;
|SINO; || --------------------------------------- las dos empresas
  |SI canti < 0;
      punto_h4 = canti * -1;
      canti = 0;
  |SINO;
      punto_d4 = canti;
  |FINSI;
  |SI canti2 < 0;
      punto_h42 = canti2 * -1;
      canti2 = 0;
  |SINO;
      punto_d42 = canti2;
  |FINSI;
  |SI sw_d3 = 0; |ACABA; |FINSI;
  |SI canti > 0; |Y canti2 > 0; sw_h3 = 0; |FINSI;
    |SI canti > 0; |O canti2 > 0;
      sw_inf = 5; |PRINT;
      sw_inf = 1; |PRINT;
      sw_inf = 5; |PRINT;
    sw_d3 = 0;
    |FINSI;
|FINSI;
|FPRC;

|PROCESO tipod31;
  variable = "  V. BENEFICIO ANTES DE IMPUESTOS .......................";
  canti = punto_d3 + punto_d4 - punto_h3 - punto_h4;
  canti2 = punto_d32 + punto_d42 - punto_h32 - punto_h42;
||------------------------------------- Solo una empresa
|SI #0#13 = "N";
  |SI canti < 0;
      punto_h5 = canti * -1;
      canti = 0;
  |SINO;
      sw_inf = 5; |PRINT;
      sw_inf = 1; |PRINT;
      sw_inf = 5; |PRINT;
      sw_d31 = 1;
      punto_d5 = canti;
  |FINSI;
|SINO; || --------------------------------------- las dos empresas
  |SI canti < 0;
      punto_h5 = canti * -1;
      canti = 0;
  |SINO;
      punto_d5 = canti;
  |FINSI;
  |SI canti2 < 0;
      punto_h52 = canti2 * -1;
      canti2 = 0;
  |SINO;
      punto_d52 = canti2;
  |FINSI;
  |SI canti > 0; |O canti2 > 0;
    sw_inf = 5; |PRINT;
    sw_inf = 1; |PRINT;
    sw_inf = 5; |PRINT;
    sw_d31 = 1;
  |FINSI;
|FINSI;
|FPRC;

|PROCESO tipoh3;
  variable = " IV. RESULTADOS EXTRAORDINARIOS NEGATIVOS ...............";
  canti = debe31 - haber31;
  canti2 = debe3 - haber3;
||------------------------------------- Solo una empresa
|SI #0#13 = "N";
  |SI canti [ 0;
      punto_d5 = canti * -1;
      canti = 0;
  |SINO;
      sw_inf = 5; |PRINT;
      sw_inf = 1; |PRINT;
      sw_inf = 5; |PRINT;
      sw_d3 = 0;
      sw_h3 = 0;
      punto_h4 = canti;
  |FINSI;
|SINO; || --------------------------------------- las dos empresas
  |SI canti [ 0;
      punto_d5 = canti * -1;
      canti = 0;
  |SINO;
      punto_h4 = canti;
  |FINSI;
  |SI canti2 [ 0;
      punto_d52 = canti2 * -1;
      canti2 = 0;
  |SINO;
      punto_h42 = canti2;
  |FINSI;
  |SI sw_h3 = 0; |ACABA; |FINSI;
  |SI canti > 0; |Y canti2 > 0; sw_d3 = 0; |FINSI;
    |SI canti > 0; |O canti2 > 0;
      sw_inf = 5; |PRINT;
      sw_inf = 1; |PRINT;
      sw_inf = 5; |PRINT;
    sw_h3 = 0;
    |FINSI;
|FINSI;
|FPRC;

|PROCESO tipoh31;
  variable = "  V. PERDIDAS ANTES DE IMPUESTOS ........................";
  canti = punto_h3 + punto_h4 - punto_d3 - punto_d4;
  canti2 = punto_h32 + punto_h42 - punto_d32 - punto_d42;
||------------------------------------- Solo una empresa
|SI #0#13 = "N";
  |SI canti [ 0;
      punto_d5 = canti * -1;
      canti = 0;
  |SINO;
      sw_inf = 5; |PRINT;
      sw_inf = 1; |PRINT;
      sw_inf = 5; |PRINT;
      sw_h31 = 1;
      punto_h5 = canti;
  |FINSI;
|SINO; || --------------------------------------- las dos empresas
  |SI canti [ 0;
      punto_d5 = canti * -1;
      canti = 0;
  |SINO;
      punto_h5 = canti;
  |FINSI;
  |SI canti2 [ 0;
      punto_d52 = canti2 * -1;
      canti2 = 0;
  |SINO;
      punto_h52 = canti2;
  |FINSI;
  |SI canti > 0; |O canti2 > 0;
    sw_inf = 5; |PRINT;
    sw_inf = 1; |PRINT;
    sw_inf = 5; |PRINT;
    sw_h31 = 1;
  |FINSI;
|FINSI;
|FPRC;

|PROCESO tipoh32;
  variable = " VI. RESULTADO DEL EJERCICO (PERDIDAS) ..................";
  canti = punto_h5;
  canti2 = punto_h52;
||------------------------------------- Solo una empresa
|SI #0#13 = "N";
  |SI punto_h5 > 0;
      canti = canti + debe41;
  |FINSI;
  |SI canti [ 0;
      canti = 0;
  |SINO;
      sw_inf = 5; |PRINT;
      sw_inf = 1; |PRINT;
      sw_inf = 5; |PRINT;
      sw_h32 = 1;
  |FINSI;
|SINO; || --------------------------------------- las dos empresas
  |SI punto_h5 > 0;
      canti = canti + debe41;
  |FINSI;
  |SI canti [ 0; canti = 0; |FINSI;
  |SI punto_h52 > 0;
      canti2 = canti2 + debe4;
  |FINSI;
  |SI canti2 [ 0; canti2 = 0; |FINSI;
  |SI canti > 0; |O canti2 > 0;
      sw_inf = 5; |PRINT;
      sw_inf = 1; |PRINT;
      sw_inf = 5; |PRINT;
      sw_h32 = 1;
  |FINSI;
|FINSI;
|FPRC;

|PROCESO tipod4;
  variable = " VI. RESULTADO DEL EJERCICIO (BENEFICIOS) ...............";
  canti = punto_d5;
  canti2 = punto_d52;
||------------------------------------- Solo una empresa
|SI #0#13 = "N";
  |SI debe41 > 0;
      canti = canti - debe41;
  |FINSI;
  |SI canti [ 0;
      canti = 0;
  |SINO;
      sw_inf = 5; |PRINT;
      sw_inf = 1; |PRINT;
      sw_inf = 5; |PRINT;
      sw_d4 = 1;
  |FINSI;
|SINO; || --------------------------------------- las dos empresas
  |SI debe41 > 0;
      canti = canti - debe41;
  |FINSI;
  |SI canti [ 0; canti = 0; |FINSI;
  |SI debe4 > 0;
      canti2 = canti2 - debe4;
  |FINSI;
  |SI canti2 [ 0; canti2 = 0; |FINSI;
  |SI canti > 0; |O canti2 > 0;
      sw_inf = 5; |PRINT;
      sw_inf = 1; |PRINT;
      sw_inf = 5; |PRINT;
      sw_d4 = 1;
  |FINSI;
|FINSI;
|FPRC;

|PROCESO conexi; |TIPO 0;
  d_mes = #0#7;
  h_mes = #0#8;
  estru = #0#6;
  r_emp = empre;
  r_per = ejerc;
  r_eje = #30#26;
  r_nom = #30#1;
  |SUB_EJECUTA "caexicon.run";
  |SI #0#13 = "N"; |ACABA; |FINSI;
  r_emp = #0#14;
  r_per = #0#15;
  r_eje = anu1  -1900;
  r_nom = #0#16;
  |SUB_EJECUTA "caexicon.run";
|FPRC;

|PROCESO impre; |TIPO 3;
  |HAZ antesque;
  |HAZ conexi;
  |ABRE #3;
  |CIERRA #3;
  |DELINDEX #3;
  |ABRE #4;
  |CIERRA #4;
  |DELINDEX #4;
  |ABRE #5;
  |CIERRA #5;
  |DELINDEX #5;

  |IMPRE 0;
  |SI FSalida ! 0;  |ACABA;  |FINSI;
  |HAZ los_informes;
  |SI #0#13 = "N";
      informe = que_i1;
  |SINO;
      informe = que_i2;
  |FINSI;
  |INFOR informe;
  |SI FSalida ! 0;
      |MENAV "     Error en informe.";
      |FINIMP;
      |ACABA;
  |FINSI;
  eldesde = (#0#0 * 3) + 11;
  elhasta = (#0#1 * 3) + 11;

  sw_d1 = 0; sw_d2 = 0; sw_d3 = 0; sw_d4 = 0;
  sw_h1 = 0; sw_h2 = 0; sw_h3 = 0;
  sw = 0;

|PARA dos = 1; |SI dos [ 2; |HACIENDO dos = dos + 1;
      acumula = 8;
      acumul2 = 11;
      e1 = 4;
      e2 = 5;
      e3 = 6;
      e4 = 7;
      |SI dos = 2; |Y #0#13 = "N";
        debe11  = debe1;
        debe21  = debe2;
        debe31  = debe3;
        debe41  = debe4;
        haber11 = haber1;
        haber21 = haber2;
        haber31 = haber3;
        |SAL;
      |FINSI;
      |SI dos = 2;
        debe11  = debe1;
        debe21  = debe2;
        debe31  = debe3;
        debe41  = debe4;
        haber11 = haber1;
        haber21 = haber2;
        haber31 = haber3;
          debe1  = 0;
          debe2  = 0;
          debe3  = 0;
          debe4  = 0;
          haber1 = 0;
          haber2 = 0;
          haber3 = 0;
          acumula = 10;
          acumul2 = 12;
          e1 =  9;
          e2 = 10;
          e3 = 11;
          e4 = 12;
          |DBASE dir_fich;
          dir_fich = dir_fich + "fich/";
          alfa1 = #0#14;   alfa1 = "00000" + alfa1;   alfa1 = alfa1 % -105;
          alfa2 = #0#15;   alfa2 = "0"   + alfa2;   alfa2 = alfa2 % -101;
          alfa3 = dir_fich + alfa1 + alfa2 + "/";
          |PATH_DAT #1 alfa3;
          |PATH_DAT #2 alfa3;
          |PATH_DAT #6 alfa3;
          |PATH_DAT #7 alfa3;
          |PATH_DAT #8 alfa3;
          |ABRE #30;
          #30#2 = #0#14;
          #30#3 = #0#15;
          |LEE 011#30=;
          |CIERRA #30;
     |FINSI;
  |ABRE #8;
  |LEE 111#8p;
  |SI #8#2 = "S";  |Y #8#3 = "N";  |Y aparam = "";
      |SI dos = 2;
          pathbal = alfa3;
      |SINO;
          |DFICE pathbal;
      |FINSI;
      balance = 1;
      |CIERRA #8;
      |SUB_EJECUTA "careccue.tab";
  |FINSI;
  |CIERRA #8;

||... Modificar Cuentas a balance segun saldo
  |SI dos = 1;
      r_emp = empre;
      r_per = ejerc;
  |SINO;
      r_emp = #0#14;
      r_per = #0#15;
  |FINSI;
  |SUB_EJECUTA "carecsig.run";
|| ---------------------------------------------------------------------

  |ABRET #1;
  |ABRE #2;
  |ABRE #3;
  |ABRE #4;
  |SI #0#13 = "S"; |Y #0#4 = "S";
      |ABRE #5; || si pide comparara y con cuentas abrimos el temporal exp2
      |FINSI;
  |ABRE #6;
  |ABRET #7;
  |HAZ pidepan;          || graba descarga de existencias
  |CIERRA #6;
  |CIERRAT #7;
  |BUCLE cabaexpl0;      || Bucle de Calculos - resto cuentas
  |CIERRAT #1;
  |CIERRA #2;
  |CIERRA #3;
  |CIERRA #4;
    |SI #0#13 = "S"; |Y #0#4 = "S";
        |CIERRA #5; || si pide comparara y con cuentas cerramos
        |FINSI;
|SIGUE; || ==================================================================
          |ABRE #30;
          #30#2 = empre;
          #30#3 = ejerc;
          |LEE 011#30=;
          |CIERRA #30;

  |ABRET #1;
  |ABRE #2;
  |ABRE #3;
  |ABRE #4;
  |SI #0#13 = "S"; |Y #0#4 = "S";
      |ABRE #5; || si pide comparara y con cuentas abrimos el temporal exp2
  |FINSI;
      |BUCLE cabaexpl1;      || Bucle de impresion
  |HAZ final;
  |PIEINF;
  |FININF;
  |CIERRAT #1;
  |CIERRA #2;
  |CIERRA #3;
  |CIERRA #4;
  |SI #0#13 = "S"; |Y #0#4 = "S";
      |CIERRA #5;
      |FINSI;
  |FINIMP;
  |DELINDEX #3;
  |DELINDEX #4;
  |DELINDEX #5;
|FPRC;

|PROCESO descarg;
  |HAZ digitos2;
  |SI d_error ! 0;  |ACABA;  |FINSI;
  linea = linea + 1;
  #4#0 = linea;
  #4#8 = #0#6;
  |LEE 001#4=;
  |SI FSalida = 0;  |BORRA 020#4a;  |FINSI;
  |DDEFECTO #4;
  #4#0 = linea;    || linea
  #4#8 = #0#6;     || estructura
  #4#1 = #1#0;     || cuenta
  #4#2 = #1#1;     || digito
  #4#3 = #1#2;     || descripcion

  mes_fin = #0#7 + 11;
  #4e1 = #7mes_fin;      || exist. iniciales

  mes_fin = #0#8 + 11;
  #4e2 = #7mes_fin;    || exist. finales

  descar = #4e1 - #4e2;
  tracan5 = descar; || diferencia entre iniciales y finales
  |SI descar ] 0;
      #4e3 = #7#5;     || cuenta descarga D
      #4e4 = #7#6;     || digito descarga D
  |SINO;
      #4e3 = #7#8;     || cuenta descarga H
      #4e4 = #7#9;     || digito descarga H
  |FINSI;
  |SI #0#4 = "N";  nivel = #4e4;  |FINSI;
  |SI nivel < #4e4;
      |SI nivel = 1;  traraf = 100 + dig4;  |FINSI;
      |SI nivel = 2;  traraf = 100 + dig5;  |FINSI;
      |SI nivel = 3;  traraf = 100 + dig6;  |FINSI; || trata cta. descarga
      |SI nivel = 4;  traraf = 100 + dig7;  |FINSI;
      |SI nivel = 5;  traraf = 100 + dig8;  |FINSI;
      #4e3 = #4e3 % traraf;
      #4e4 = nivel;
      #4e3 = #4e3 + "              ";
      #4e3 = #4e3 % 112;
  |FINSI;

  |GRABA 020#4n;

  |HAZ grabatr1;
  |SI #0#13 = "S"; |Y #0#4 = "S"; |HAZ grabatr2; |FINSI;
|FPRC;

|PROCESO lineades;
  #1#0 = #7#2;
  #1#1 = #7#3;
  |LEE 000#1=;
  |SI FSalida = 0;
      |HAZ descarg;
  |FINSI;
|FPRC;

|PROCESO pidepan;
  posici = 100;
              || linea = 0; -- no borra la linea para continuar con la 2¦ emp.
  #6#0 = #0#6;
  |LEE 001#6=;
  |BUCLELIN 0lineades#6;
|FPRC;
