|FICHEROS;
  capasven #0;
  cacuenta #1;
  cavenaut #2;
  calibros #3;
  camaasic #4;
  camaasil #5;
  camandia #6;
  caconcep #7;
  caivarep #9;
|FIN;

|VARIABLES;
  &modcon;
  line = 0;
  salidas = 0;
  cta_t = 0;
  dig_t = 0;
  alfa1 = "";
  alfa2 = "";
  alfa3 = "";
  long = 0;
  sw = 0;
  x = 0;
  y = 0;
  fecasi = @;
  importe = 0;
  importe1 = 0;
  importe2 = 0;
  importe3 = 0;
  Timporte1 = 0;
  Timporte2 = 0;
  Timporte3 = 0;
  num = 0;
  num1 = 0;
  num2 = 0;
  num3 = 0;
  num4 = 0;
  num5 = 0;
  num6 = 0;
  desde = 0;
  hasta = 0;
  opera = "B";
  FEstado = 0;
  ini = 0;
  fecalf = @;
  mes = "";
  mesfec = 0;
  mesac = 0;
  a = 0;
  b = 0;
  c = 0;
  asie = 0;
  factura = 0;
  trim1 = @;
  trim2 = @;
  trim3 = @;
  {366}cant = 0;
  traba = "";
|FIN;

|INCLUYE inici1_i;
|INCLUYE puntos_i;
|INCLUYE conasi_i;

|PROCESO fechabien;
  |SI #0Campo < fec1;  |O #0Campo > fec2;
      |MENAV "     La Fecha NO Corresponde con el periodo contable";
      |ERROR;
      |ACABA;
 |FINSI;
 |SI Campo = 2;  |Y #0#1 > #0#2;
     |MENAV "     Atencion  fecha Erronea";
     |ERROR;
 |FINSI;
|FPRC;

|PROCESO mayus;
  alfa1 = #0Campo > " ";
  #0Campo = alfa1;
  |PINTA #0Campo;
  |SI Campo = 24;
      |C_M #0#25,1,"N";
      |SI #0Campo ! "D";
          |C_M #0#25,1,"S";
          #0#25 = #0#2;
          |PINTA #0#25;
      |FINSI;
  |FINSI;
|FPRC;

|PROCESO eslibro;
  |ABRE #3;
  #3#0 = #0Campo;
  |LEE 011#3=;
  |SI FSalida ! 0;
      |MENAV "      Error no existe Libro de IVA";
      |CIERRA #3;
      |ERROR;
      |ACABA;
  |FINSI;
  |SI #3#2 = "S";
      |MENAV "     El libro NO es de IVA Repercutido";
      |ERROR;
  |FINSI;
  |CIERRA #3;
|FPRC;

|PROCESO leeconce;
  |ABRE #7;
  y = Campo + 3;
  #7#0 = #Campo;
  |LEE 011#7=;
  |CIERRA #7;
  |SI FSalida ! 0;
      |MENAV "     NO Existe Concepto Automatico";
      |ERROR;
      |ACABA;
  |FINSI;
  #0y = #7#1;
  |PINTA #0y;
|FPRC;

|PROCESO cuefin;
  cta_t = Campo;
  dig_t = Campo + 3;
  #1#0 = #0cta_t
  #1#1 = #0dig_t;
  |LEE 001#1=;
  |SI FSalida ! 0;
      |MENAV "     NO existe CUENTA CONTABLE";
      |ERROR;
      |ACABA;
  |FINSI;
  |SI #1#3 = "N";
      |MENAV "     La Cuenta NO tiene PASE DIRECTO";
      |ERROR;
  |FINSI;
|FPRC;

|PROCESO cuenta; |TIPO 6;
  p_alfa1 = #0Campo;
  salidas = FSalida;
  |HAZ punto;
  |SI p_error ! 0;
      |MENAV "     ERROR EN CUENTA";
      |ERROR;
      |ACABA;
  |FINSI;
  cta_t = Campo;
  dig_t = cta_t + 3;
  |HAZ cuenta_t;
|FPRC;

|PROCESO cuadro; |TIPO 7;
 |PUSHV 1234 1680;
 |CUADRO 1234 1680;
 |ATRI R;
 |PINTA 1335, " (D) Asiento por Dia de Venta                ";
 |PINTA 1435, " (R) Un Asiento con apuntes por Dia de Venta ";
 |PINTA 1535, " (T) Un Asiento con apuntes por el Total     ";
 |ATRI r;
|FPRC;

|PROCESO repon; |TIPO 0;
 |POPV;
|FPRC;

|PROGRAMA;
  |HAZ comienzo;
  line = 1;
  |CLS;
  |PINPA #0#0;
  |CABEZA "Astos Ventas Automaticas";
  |ABRE #2;
  |PARA x = 1;  |SI x [ 4;  |HACIENDO x = x + 1;
        #2#0 = 1;
        #2#1 = x;
        |LEE 001#2=;
        |SI FSalida ! 0;
            |MENAV "     NO existen Ventas Automaticas";
            |CIERRA #2;
            |ACABA;
        |FINSI;
  |SIGUE;
  |CIERRA #2;
  |ABRE #0;
  #0#0 = 1;
  |LEE 101#0=;
  |SI FSalida ! 0;
      |HAZ primeravez;
  |FINSI;
  |ABRET #1;
  |PINDA #0#0;
  |ENDATOS #2#0;
  |SI FSalida = 0;
      |HAZ elpase;
      |HAZ final;
      |GRABA 001#0a;
  |FINSI;
  |CIERRA #0;
  |CIERRAT #1;
|FPRO;

|PROCESO primeravez;
  |DDEFECTO #0;
  |ABRE #6;
  #6#0 = #0#3;
  |LEE 011#6=;
  |SI FSalida = 0;
      #0#4 = #6#1;
  |FINSI;
  |CIERRA #6;
  |ABRE #3;
  #3#0 = #0#5;
  |LEE 011#3=;
  |SI FSalida = 0;
      #0#6 = #3#1;
  |FINSI;
  |CIERRA #3;
  |PARA x = 9;  |SI x [ 11;  |HACIENDO x = x + 1;
        Campo = x;
        p_alfa1 = #0Campo;
        salidas = FSalida;
        |HAZ punto;
        |SI p_error = 0;
            cta_t = Campo;
            dig_t = cta_t + 3;
            |HAZ cuenta_t;
        |FINSI;
  |SIGUE;
  |ABRE #7;
  |PARA x = 15;  |SI x [ 17;  |HACIENDO x = x + 1;
        y = x + 3;
        #7#0 = #0x;
        |LEE 011#7=;
        |SI FSalida = 0;
            #0y = #7#1;
        |FINSI;
  |SIGUE;
  |CIERRA #7;
  #0#1 = fec1;
  #0#2 = fec2;
  #0#25 = fec2;
  |GRABA 011#0c;
|FPRC;

|PROCESO cuenta_t;
  alfa1 = #0cta_t;
  |QBF alfa1;
  alfa2 = alfa1 % 0;
  long = alfa2;
  sw = 0;
  |SI long = dig4;  sw = 1;  #0dig_t = 1;  |FINSI;
  |SI long = dig5;  sw = 1;  #0dig_t = 2;  |FINSI;
  |SI long = dig6;  sw = 1;  #0dig_t = 3;  |FINSI;
  |SI long = dig7;  sw = 1;  #0dig_t = 4;  |FINSI;
  |SI long = dig8;  sw = 1;  #0dig_t = 5;  |FINSI;
  |SI long = dig9;  sw = 1;  #0dig_t = 6;  |FINSI;
  |SI long = 0;     sw = 1;  #0dig_t = 0;  |FINSI;
  |SI sw = 0;
      #0dig_t = 0;
      |MENAV "      Longitud de Cuenta Fuera de Nivel ";
      |ERROR;
      |ACABA;
  |FINSI;
|FPRC;

|PROCESO cargaimpo;
  |ABRE #2;
  Icant = cant1 <-;
  |PARA y = 1;  |SI y [ 4;  |HACIENDO y = y + 1;
        #2#0 = 1;
        #2#1 = y;
        |LEE 040#2=;
        num = 1
        |PARA x = 1;  |SI x [ 92;  |HACIENDO x = x + 1;
              num = num + 1;
              cant = #2num;
              Icant = Icant + 1;
              |SI x = 91; |Y y [ 2;
                  |SAL;
              |FINSI;
        |SIGUE;
  |SIGUE;
  |CIERRA #2;
|FPRC;

|PROCESO recufecha;
  |SI num1 > 0;    num2 = 1;   num3 = num1;        |FINSI;
  |SI num1 > 31;   num2 = 2;   num3 = num1 - 31;   |FINSI;
  |SI num1 > 60;   num2 = 3;   num3 = num1 - 60;   |FINSI;
  |SI num1 > 91;   num2 = 4;   num3 = num1 - 91;   |FINSI;
  |SI num1 > 121;  num2 = 5;   num3 = num1 - 121;  |FINSI;
  |SI num1 > 152;  num2 = 6;   num3 = num1 - 152;  |FINSI;
  |SI num1 > 182;  num2 = 7;   num3 = num1 - 182;  |FINSI;
  |SI num1 > 213;  num2 = 8;   num3 = num1 - 213;  |FINSI;
  |SI num1 > 244;  num2 = 9;   num3 = num1 - 244;  |FINSI;
  |SI num1 > 274;  num2 = 10;  num3 = num1 - 274;  |FINSI;
  |SI num1 > 305;  num2 = 11;  num3 = num1 - 305;  |FINSI;
  |SI num1 > 335;  num2 = 12;  num3 = num1 - 335;  |FINSI;
  num4 = 100 + num3;
  alfa1 = num4;
  alfa3 = alfa1 % 202;
  alfa3 = alfa3 + ".";
  num4 = 100 + num2;
  alfa1 = num4;
  alfa2 = alfa1 % 202;
  alfa3 = alfa3 + alfa2 + ".";
  alfa1 = #0#1 % 704;
  alfa3 = alfa3 + alfa1;
  fecasi = alfa3;
|FPRC;

|PROCESO desdehasta;
  alfa1 = #0#1 % 402;
  num = alfa1;
  |SI num = 1;  desde = 0;   |FINSI;
  |SI num = 2;  desde = 31;  |FINSI;
  |SI num = 3;  desde = 60;  |FINSI;
  |SI num = 4;  desde = 91;  |FINSI;
  |SI num = 5;  desde = 121; |FINSI;
  |SI num = 6;  desde = 152; |FINSI;
  |SI num = 7;  desde = 182; |FINSI;
  |SI num = 8;  desde = 213; |FINSI;
  |SI num = 9;  desde = 244; |FINSI;
  |SI num = 10; desde = 274; |FINSI;
  |SI num = 11; desde = 305; |FINSI;
  |SI num = 12; desde = 335; |FINSI;
  alfa1 = #0#1 % 102;
  num = alfa1;
  desde = desde + num;
  alfa1 = #0#2 % 402;
  num = alfa1;
  |SI num = 1;  hasta = 0;   |FINSI;
  |SI num = 2;  hasta = 31;  |FINSI;
  |SI num = 3;  hasta = 60;  |FINSI;
  |SI num = 4;  hasta = 91;  |FINSI;
  |SI num = 5;  hasta = 121; |FINSI;
  |SI num = 6;  hasta = 152; |FINSI;
  |SI num = 7;  hasta = 182; |FINSI;
  |SI num = 8;  hasta = 213; |FINSI;
  |SI num = 9;  hasta = 244; |FINSI;
  |SI num = 10; hasta = 274; |FINSI;
  |SI num = 11; hasta = 305; |FINSI;
  |SI num = 12; hasta = 335; |FINSI;
  alfa1 = #0#2 % 102;
  num = alfa1;
  hasta = hasta + num;
|FPRC;

|PROCESO queimporte;
  |SI #0#8 = 0;
      importe1 = cant;
      importe2 = cant;
      importe3 = 0;
  |SINO;
      |SI #0#7 = "N";
          importe2 = cant;
          importe3 = (importe2 * #0#8) / 100;
          importe3 = importe3 ! EURODB;
          importe1 = importe2 + importe3;
      |SINO;
          importe1 = cant;
          importe3 = (#0#8 / 100) + 1;
          importe3 = importe1 / importe3;
          importe2 = importe3 ! EURODB;
          importe3 = importe1 - importe2;
      |FINSI;
  |FINSI;
|FPRC;

|PROCESO elpase;
  |ATRI I;
  |PINTA 2410,"CARGANDO IMPORTES DE VENTAS      ";
  |HAZ cargaimpo;
  |PINTA 2410,"REALIZANDO ASIENTOS CONTABLES    ";
  |ATRI 0;
  |ABRE #4;
  |ABRE #5;
  |ABRE #6;
  |ABRE #8;
  |ABRE #9;
  |HAZ primfac;
  |HAZ desdehasta;
|SI #0#24 = "T"; |HAZ elpaseT; |ACABA; |FINSI;
  Icant = cant1 <-;
  Icant = Icant + desde;
  Icant = Icant - 1;
  |PARA num1 = desde;  |SI num1 [ hasta;  |HACIENDO num1 = num1 + 1;
        |SI cant =  0;  |VETE ajugar;  |FINSI;
        |HAZ recufecha;
        |SI line = 1;
            |HAZ contador;
            asie = #20#1;
        |FINSI;
        |HAZ queimporte;
  traba = " PASANDO VENTA FECHA: " + fecasi + " ";
  |PINTA 2209, traba;
        |PARA x = 9;  |SI x [ 11;  |HACIENDO x = x + 1;
              |DDEFECTO #5;
              #5#0 = #0#3;
              #5#1 = fecasi;
              |SI #0#24 ! "D"; #5#1 = #0#25;  |FINSI;
              #5#2 = asie;
              #5#3 = line;
              |LEE 101#5=;
              FEstado = FSalida;
              num2 = x + 3;     ||tengo el digito
              num4 = x + 6;     ||tengo el concepto
              num5 = x + 9;     ||tengo la descripcion
              num6 = x + 12;    ||tengo fecha SINO;
              |SI x = 9;   ||ASIENTO DEBE
                  #5#8 = "D";
                  #5#9 = importe1;
                  #5#10 = "";
                  #5#11 = 0;
                  #5#16 = #0x;
                  #5#17 = #0num2;
                  #5#19 = "F";
                  #5#20 = 0;
              |SINO;
                  #5#8 = "H";
                  |SI x = 10;
                      #5#9 = importe2;
                      #5#19 = "B";
                  |SINO;
                      #5#9 = importe3;
                      #5#19 = "I";
                  |FINSI;
                  #5#10 = #0x;
                  #5#11 = #0num2;
                  #5#16 = "";
                  #5#17 = 0;
                  #5#20 = 1;
              |FINSI;
              #5#4 = #0x;
              #5#5 = #0num2;
              #5#6 = #0num4;
              alfa3 = #0num5;
              |QBF alfa3;
              |SI #0num6 = "S";
                  alfa3 = alfa3 + " " + fecasi;
              |FINSI;
              #5#7 = alfa3;
              |SI x = 9;
                  |HAZ pasaiva;
              |FINSI;
              #5#12 = "R";
              #5#13 = #0#5;
              #5#14 = #9#2;
              #5#15 = #9#27;
              |SI #5#9 ! 0;
                  |SI FEstado = 0;
                      |GRABA 020#5a;
                  |SINO;
                      |GRABA 020#5n;
                  |FINSI;
                  |LIBERA #5;
                  |HAZ cabecera;
                  |HAZ actualiz1;
                  opera = "A";
                  |HAZ altatra;
                  line = line + 10;
              |FINSI;
        |SIGUE;
  |SI #0#24 = "D"; line = 1; |FINSI;
|ET ajugar;
  Icant = Icant + 1;
  |SIGUE;
  traba = &178 * 33;
  |PINTA 2209, traba;
  |CIERRA #4;
  |CIERRA #5;
  |CIERRA #6;
  |CIERRA #8;
  |CIERRA #9;
|FPRC;

|PROCESO elpaseT; || Cuando solo quiere tres apuntes

  Timporte1 = 0;
  Timporte2 = 0;
  Timporte3 = 0;
  |HAZ contador;
  asie = #20#1;
  line = 1;
  #5#0 = #0#3;
  #5#1 = #0#25;
  #5#2 = asie;
  #5#4 = #0#9;
  #5#5 = #0#12;
  #5#6 = #0#15;
  #5#7 = #0#18;
  Icant = cant1 <-;
  Icant = Icant + desde;
  Icant = Icant - 1;
  |PARA num1 = desde;  |SI num1 [ hasta;  |HACIENDO num1 = num1 + 1;
        |SI cant =  0;  |VETE ajugarT;  |FINSI;
        |HAZ recufecha;
        |HAZ queimporte;
        traba = " PASANDO VENTA FECHA: " + fecasi + " ";
        |PINTA 2209, traba;
        |HAZ pasaiva;
        Timporte1 = Timporte1 + importe1
        Timporte2 = Timporte2 + importe2
        Timporte3 = Timporte3 + importe3
|ET ajugarT;
  Icant = Icant + 1;
  |SIGUE;

  |PARA x = 9;  |SI x [ 11;  |HACIENDO x = x + 1;
        |DDEFECTO #5;
         #5#0 = #0#3;
         #5#1 = #0#25;
         #5#2 = asie;
         #5#3 = line;
         |LEE 101#5=;
         FEstado = FSalida;
         num2 = x + 3;     ||tengo el digito
         num4 = x + 6;     ||tengo el concepto
         num5 = x + 9;     ||tengo la descripcion
         num6 = x + 12;    ||tengo fecha SINO;

         |SI x = 9;   ||ASIENTO DEBE
             #5#8 = "D";
             #5#9 = Timporte1;
             #5#10 = "";
             #5#11 = 0;
             #5#16 = #0x;
             #5#17 = #0num2;
             #5#19 = "F";
             #5#20 = 0;
         |SINO;
             #5#8 = "H";
             |SI x = 10;
                 #5#9 = Timporte2;
                 #5#19 = "B";
             |SINO;
                 #5#9 = Timporte3;
                 #5#19 = "I";
             |FINSI;
             #5#10 = #0x;
             #5#11 = #0num2;
             #5#16 = "";
             #5#17 = 0;
             #5#20 = 1;
         |FINSI;
         #5#4 = #0x;
         #5#5 = #0num2;
         #5#6 = #0num4;
         alfa3 = #0num5;
         |QBF alfa3;
         #5#7 = alfa3;
         #5#12 = "R";
         #5#13 = #0#5;
         |SI #5#9 ! 0;
             |SI FEstado = 0;
                 |GRABA 020#5a;
             |SINO;
                 |GRABA 020#5n;
             |FINSI;
             |LIBERA #5;
             |HAZ cabecera;
             |HAZ actualiz1;
             opera = "A";
             |HAZ altatra;
             line = line + 10;
         |FINSI;
   |SIGUE;
  traba = &178 * 33;
  |PINTA 2209, traba;
  |CIERRA #4;
  |CIERRA #5;
  |CIERRA #6;
  |CIERRA #8;
  |CIERRA #9;
|FPRC;

|PROCESO cabecera;
  |SI #5#9 = 0;  |ACABA;  |FINSI;
  |DDEFECTO #4;
  #4#0 = #5#0; ||  diario
  #4#1 = #5#1; ||  fecha
  #4#2 = #5#2; ||  documento
  #4#3 = #5#2; ||  asiento
  |LEE 101#4=;
  #4#0 = #5#0; ||  diario
  #4#1 = #5#1; ||  fecha
  #4#2 = #5#2; ||  documento
  #4#3 = #5#2; ||  asiento
  FEstado = FSalida;
  |SI #5#8 = "D";
      #4#04 = #4#4 + #5#9;
      |SI #4#9 = 0;                         ||CONTRAPARTIDA
          #4#8 = #5#4;
          #4#9 = #5#5;
      |FINSI;
  |SINO;
      #4#5 = #4#5 + #5#9;
      |SI #4#11 = 0;
          #4#10 = #5#4;
          #4#11 = #5#5;
      |FINSI;
  |FINSI;
  |SI FEstado = 0;  |GRABA 020#4a;  |FINSI;
  |SI FEstado ! 0;  |GRABA 020#4n;  |FINSI;
  |LIBERA #4;
|FPRC;

|PROCESO actualiz1;
  #1#0 = #5#4;
  #1#1 = #5#5;
  |LEE 101#1=;
  |SI #5#0 = 0;
      |SI #5#8 = "D";
          #1#9 = #1#9 + #5#9;
          #1#51 = #1#51 + #5#9;
      |SINO;
          #1#10 = #1#10 + #5#9;
          #1#52 = #1#52 + #5#9;
      |FINSI;
      #1#11 = #1#9 - #1#10;
      #1#53 = #1#51 - #1#52;
  |SINO;
      |SI #5#0 = 99;
          |SI #5#8 = "D";
              #1#48 = #1#48 + #5#9;
              #1#51 = #1#51 + #5#9;
          |SINO;
              #1#49 = #1#49 + #5#9;
              #1#52 = #1#52 + #5#9;
          |FINSI;
          #1#50 = #1#48 - #1#49;
          #1#53 = #1#51 - #1#52;
      |SINO;
          |HAZ actualiz2;
      |FINSI;
  |FINSI;
  |SI #1#58 < #5#1;
      #1#58 = #5#1;
  |FINSI;
  |GRABA 020#1a;
  |LIBERA #1;      || /por lo tanto, en la primera no desbloquea
  |HAZ actualiz3;  || diario
|FPRC;

|PROCESO altatra;
  modcon = 1;
  #8#0 = codcon;
  #8#1 = #5#4;
  #8#2 = #5#5;
  #8#3 = #5#1;
  #8#4 = #5#8;
  #8#6 = #5#0;
  |LEE 101#8=;
  |SI FSalida ! 0;
      |DDEFECTO #8;
      #8#0 = codcon;
      #8#1 = #5#4;
      #8#2 = #5#5;
      #8#3 = #5#1;
      #8#4 = #5#8;
      #8#6 = #5#0;
      |GRABA 101#8b;
  |FINSI;
  importe = #5#9;  || aqui si que se usa el importe real
  |SI opera = "B";
      importe = -importe;
  |FINSI;
  #8#5 = #8#5 + importe;
  |GRABA 101#8a;
  |LIBERA #8;
|FPRC;

|PROCESO actualiz2;
  ini = dig1;      || numero de niveles
  fecalf = #5#1;
  mes = fecalf % 402;
  mesfec = mes;
  |SI ini = 1;
      mesac = mesfec;
  |SINO;
      mesac = (mesfec - ini) + 1;
      |SI mesac [ 0;
          mesac = (13 - ini) + mesfec;
      |FINSI;
  |FINSI;
  a = (mesac * 3) + 9;
  b = a + 1;
  c = a + 2;
  |SI #5#8 = "D";
      #1a = #1a + #5#9;
      #1#51 = #1#51 + #5#9;
  |SINO;
      #1b = #1b + #5#9;
      #1#52 = #1#52 + #5#9;
  |FINSI;
  #1c = #1a - #1b;
  #1#53 = #1#51 - #1#52;
|FPRC;

|PROCESO actualiz3;
  #6#0 = #0#3;
  |LEE 121#6=;
  |SI FSalida = 0;
      |SI #5#8 = "D";  #6#2 = #6#2 + #5#9;  |FINSI;
      |SI #5#8 = "H";  #6#3 = #6#3 + #5#9;  |FINSI;
      |GRABA 020#6a;
  |FINSI;
  |LIBERA #6;
|FPRC;

|PROCESO final;
  |SI modcon ! 0;
      |SUB_EJECUTA "camaapua.tab";
  |SINO;
      |ABRE #8;
      |DDEFECTO #8;
      #8#0 = codcon;
      |LEE 001#8=;
      |SI FSalida = 0;
          |BORRA 020#8a;
      |FINSI;
      |CIERRA #8;
  |FINSI;
|FPRC;

|PROCESO pasaiva;
  |DDEFECTO #9;
  #9#0  = #0#5;
  #9#1  = #0#6;
  #9#2  = factura;
  #9#3  = fecasi;
  #9#4  = #5#4;
  #9#5  = #5#5;
  #9#6  = #5#6;
  #9#7  = #5#7;
  #9#8  = importe2;
  #9#11 = #0#8;
  #9#14 = importe3;
  #9#23 = "S";
  #9#24 = "N";
  #9#26 = importe1;
  #9#27 = "  ";
  #9#28 = "N";
  alfa1 = #9#3;
  alfa1 = alfa1 % -104;   || saca a¤o
  alfa2 = "31.03." + alfa1;  trim1 = alfa2;  || primer trimestre
  alfa2 = "30.06." + alfa1;  trim2 = alfa2;  || segundo trimestre
  alfa2 = "30.09." + alfa1;  trim3 = alfa2;  || tercer trimestre
  |SI #9#3 [ trim1;  #9#44 = 1;  |FINSI;
  |SI #9#3 > trim1;|Y #9#3 [ trim2;  #9#44 = 2;  |FINSI;
  |SI #9#3 > trim2;|Y #9#3 [ trim3;  #9#44 = 3;  |FINSI;
  |SI #9#3 > trim3;  #9#44 = 4;  |FINSI;
  #9#45 = #5#0;
  #9#46 = #5#1;
  #9#47 = #5#2;
  #9#48 = "N";
  #9#49 = "01";
  #1#0 = #0#9;
  #1#1 = #0#12;
  |LEE 011#1=;
  |LIBERA #1;
  #9#50 = #1#2;
  |GRABA 011#9n;
  factura = factura + 1;
|FPRC;

|PROCESO primfac;
  #9#0 =  #0#5 + 1;
  #9#2 = 0;
  #9#27 = "";
  |LEE 011#9];
  |SI FSalida ! 0;
      |LEE 011#9u;
      |SI FSalida ! 0;
          factura = 1;
      |SINO;
          |SI #9#0 = #0#5;
              factura = #9#2 + 1;
          |SINO;
              factura = 1;
          |FINSI;
      |FINSI;
  |SINO;
      |LEE 011#9a;
      |SI FSalida ! 0;
          factura = 1;
      |SINO;
          |SI #9#0 = #0#5;
              factura = #9#2 + 1;
          |SINO;
              factura = 1;
          |FINSI;
      |FINSI;
  |FINSI;
|FPRC;
