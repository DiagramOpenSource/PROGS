|FICHEROS;
 caredm03 #0;
 caredm01 #1;  || Datos Secuencial
 camingca #2;  || Confirming Cabeceras
 camingli #3;  || Confirming Lineas
 caclipro #4;  || Clientes/Proveedores
 camanpag #5;  || Pagos

 camoneda #12;
|FIN;

|VARIABLES;
 &EURO    = 0;
 &EURODB  = 0;
 &EURODV  = 0;
 Cantidad = 0;
 MonBase  = 0;
 Moneda   = "";
 nCalc    = 0;

 alfa1 = "";
 alfa2 = "";
 alfa3 = "";
 alfa4 = "";
 nume1 = 0;
 nume2 = 0;
 nume3 = 0;
 nume4 = 0;

 mens     = "";
 swErrorR = 0;
 swErrorP = 0;
 menR1    = "    No Existe Transferencia !!!";
 menR2    = "    Transferencia YA contabilizada ";
 menR3    = "2400STransferencia YA Emitida en Papel. Aceptar S/N ";
 menR4    = "2400NTransferencia YA lanzada en Diskette. Aceptar S/N ";

 Nombre  = "";
 DniEmp  = "";
 DirEmp  = "";
 CpoEmp  = "";
 PobEmp  = "";
 sino    = "";
 ceros   = "000000000000000";
 fechapre = "      ";
 fechapro = "      ";
 dd       = "";
 mm       = "";
 aa       = "";
 cabe1    = "306"; || Cabecera
 cabe2    = "606"; || Lineas Individuales
 cabe3    = "806"; || Totales
 Prefijo  = "";

 libre    = " ";
 libre40  = "";
 ca1  = "";
 ca2  = "";
 ca3  = "";
 ca4  = "";
 ca5  = "";
 ca6  = "";
 ca7  = "";
 ca8  = "";
 ca9  = "";
 ca10 = "";
 ca11 = "";
 ca12 = "";
 ca13 = "";
 ca14 = "";
 ca15 = "";
 ca16 = "";
 ca17 = "";
 ca18 = "";
 ca19 = "";
 ca20 = "";
 ca21 = "";
 l          = 0;
 trecibos   = 0;
 timporte   = 0;

 handle     = "";
 fiche      = "";
 dirempr    = "";
 varalfa1   = "";
 varalfa2   = "";
 contenido  = "";
 swERROR    = 0;
 CampoDni   = "";
 LetrasNif  = "TRWAGMYFPDXBNJZSQVHLCKE";
 Letra1     = "";
 Letra2     = "";
 Letra3     = "";
 Letra4     = "";
 Letra5     = "";
 Letra6     = "";
 Letra7     = "";
 Letra8     = "";
 Caracter   = "";
 Carac1     = 0;
 Carac2     = 0;
 Carac3     = 0;
 Carac4     = 0;
 Carac5     = 0;
 XPara      = 0;

 ccc        = "";
 c_alfa1    = "";
 c_alfa2    = "";
 c_nume1    = 0;
 c_nume2    = 0;
 c_nume3    = 0;

 direcc = "";
 numero = 0;
 longi  = 0;
 concepto = "";

 &programa = "";
 &ficcopia = "";
|FIN;

|| *************************************************************************

|PROCESO mayor;
 alfa1 = #0Campo > " ";
 #0Campo = alfa1;
 |PINTA #0Campo;
|FPRC;

|PROCESO ver_confirming; |TIPO 6;
 |SI FSalida = 2; |ACABA; |FINSI;
 |ABRE #2;
 |HAZ leerem;
 |CIERRA #2;
 |SI swErrorR ! 0;
     |SI swErrorR < 3;
         |MENAV mens;
         |ERROR;
         |ACABA;
     |SINO;
         |CONFI mens;
         |SI FSalida ! 0;
             |ERROR;
             |ACABA;
         |FINSI;
     |FINSI;
 |FINSI;
 #0#2 = #2#3; || Banco
 #0#3 = #2#1; |PINTA #0#3;
 #0#5 = #2#2; |PINTA #0#5;

 |PINTA 1035, #2#2;   || Vto.
 |PINTA 1058, #2#8;   || N§ Efectos
 |PINTA 1135, #2#3;   || Banco
 |PINTA 1149, #2#9;   || Importe Total
|FPRC;

|PROCESO leerem;
 swErrorR = 0;
 |DDEFECTO #2;
 #2#0 = #0#1;
 |LEE 001#2=;
 |SI FSalida ! 0;
     swErrorR = 1; mens = menR1;
 |SINO;
     |SI #2#9 = "S";
         swErrorR = 2; mens = menR2;
     |SINO;
         |SI #2#10 = "S"; swErrorR = 3; mens = menR3; |FINSI;
         |SI #2#10 = "D"; swErrorR = 4; mens = menR4; |FINSI;
     |FINSI;
 |FINSI;
|FPRC;

|PROCESO lee_pase; |TIPO 6;
 |HAZ leepas;
 |ATRI I;
|| |PINTA 1220, #1#1;
 |ATRI 0;
|FPRC;

|PROCESO leepas;
 swErrorP = 0;
 |ABRE #1;
 |DDEFECTO #1;
 #1#0 = #0#2;
 |LEE 001#1=;
 |SI FSalida ! 0; swErrorP = 1; |FINSI;
 |CIERRA #1;
|FPRC;

||...................................................................................
||*************************************************************************
||************ REGISTROS DE CABECERA **************************************
||*************************************************************************
|PROCESO cabecera;

 || ...................................... Primer Registro Cabecera
    ca1 = libre * 12;
    ca2 = "001";
    ca3 = #1#4; ca3 = (ceros + ca3) % -104;    || Banco
    ca4 = #1#5; ca4 = (ceros + ca4) % -104;    || Oficina
    ca5 = #1#6; ca5 = (ceros + ca5) % -110;    || Cuenta
    ca6 = "0";

    varalfa1 =  Prefijo + cabe1 + DniEmp + ca1 + ca2 + fechapre + fechapro;
    varalfa1 =  varalfa1 + ca3 + ca4 + ca5 + ca6 + ca1;

    contenido = handle + " " + varalfa1 + &#0#7 + &#0#8;
    |FGRABA contenido;

    l = l + 1;

 || ..................................... Segundo Registro Cabecera
    ca1 = libre * 12;
    ca2 = "002";
    ca3 = libre * 7;

    varalfa1 =  Prefijo + cabe1 + DniEmp + ca1 + ca2 + Nombre + ca3;
    contenido = handle + " " + varalfa1 + &#0#7 + &#0#8;
    |FGRABA contenido;

    l = l + 1;

 || .....................................  Tercer Registro Cabecera
    ca1 = libre * 12;
    ca2 = "003";
    ca3 = libre * 7;

    varalfa1 =  Prefijo + cabe1 + DniEmp + ca1 + ca2 + DirEmp + ca3;
    contenido = handle + " " + varalfa1 + &#0#7 + &#0#8;
    |FGRABA contenido;

    l = l + 1;

 || .....................................  Cuarto Registro Cabecera
    ca1 = libre * 12;
    ca2 = "004";
    ca3 = libre * 7;

    varalfa1 =  Prefijo + cabe1 + DniEmp + ca1 + ca2 + CpoEmp + PobEmp + ca3;
    contenido = handle + " " + varalfa1 + &#0#7 + &#0#8;
    |FGRABA contenido;

    l = l + 1;
|FPRC;

||***********************************************************
||******************* REGISTRO INDIVIDUAL *******************
||***********************************************************

|PROCESO leecl_y_cobro;
 || .............. Lee la ficha del Cliente para obtener los datos necesarios
 |DDEFECTO #4;
 #4#23 = "P";
 #4#10 = #3#9;
 #4#11 = #3#10;
 |LEE 001#4=;
 |SI FSalida ! 0;
     #4#23 = "C";
     #4#10 = #3#9;
     #4#11 = #3#10;
     |LEE 001#4=;
     |SI FSalida ! 0; |DDEFECTO #4; swERROR = 1; |FINSI;
 |FINSI;

 |DDEFECTO #5;
 #5#0 = #3#2;
 #5#1 = #3#3;
 #5#2 = #3#4;
 #5#3 = #3#5;
 |LEE 001#5=;

|FPRC;

|PROCESO grabarecib; ||Calculo registros individuales;
  swERROR = 0;

  |HAZ leecl_y_cobro;
  |SI swERROR = 1; |ACABA; |FINSI;   || Salta este Cobro. NO el resto

|| --------------------------------------- Primer Registro Individual
  ca1 = libre * 11;
  ca2 = "";               || Referencia Cliente
  |SI #0#4 = 1;
      ca2 = #3#9;         || Cuenta Contable
  |SINO;
      ca2 = #4#0;         || Codigo de Ficha Proveedor
  |FINSI;
  |QBF ca2;
  ca2 = ca2 + libre40;
  ca2 = ca2 % 112;

  ca3 = "010";

  nume1 = #3#8;     || nume1 = (#3#8 * 100) ! 0;
  ca4 = nume1; |QBF ca4;
  ca4 = ceros + ca4; ca4 = ca4 % -112;  || Importe

 || |SI nume1 ] 0; ca4 = ceros + ca4; ca4 = ca4 % -112; |FINSI;  || Importe
 || |SI nume1 < 0; ca4 = "-"+ ca17; ca17 = ca17 % -115; |FINSI;

  ccc = #5#19; |HAZ cccBanco;         || Datos del banco del Cobro
  ccc = (ccc + ceros) % 110;
  ca5 = ccc % 104;      || Banco
  ca6 = ccc % 504;      || Sucursal

  ccc = #5#18; |HAZ cccBanco;
  ccc = (ceros + ccc) % -110;
  ca7 = ccc;                 || Cuenta corriente  Proveedor

  ca8  = "1";
  ca9  = "9";

  varalfa1 = "";
  varalfa1 = Prefijo + cabe2    + DniEmp + ca2  + ca3  + ca4  + ca5;
  varalfa1 = varalfa1 + ca6    + ca7  + ca8  + ca9  + ca1;

  contenido = handle + " " + varalfa1 + &#0#7 + &#0#8;
  |FGRABA contenido;

  trecibos = trecibos + 1;
  l = l + 1;
  timporte = timporte + #3#8;

|| -------------------------------------- Segundo Registro Individual

  ca1 = libre * 7;

  ca2 = #4#9;             ||  D.N.I.
  CampoDni = ca2; |HAZ LetraDni; ca2 = CampoDni;
  ca2 = ca2 + libre40; ca2 = ca2 % 112;

  ca3 = "011";

  ca4 = (#4#1 + libre40) % 136;   || Nombre Cliente

  varalfa1 = "";
  varalfa1 = Prefijo + cabe2    + DniEmp + ca2  + ca3  + ca4  + ca1;

  contenido = handle + " " + varalfa1 + &#0#7 + &#0#8;
  |FGRABA contenido;

  l = l + 1;

|| -------------------------------------- Tercer Registro Individual

  ca3 = "012";
                     || ...........  direcc = #4#2; || Direccion
                     ||              |HAZ separa;
  ca5 = #4#2;
  ca5 = (ca5 + libre40) % 136;

  varalfa1 = "";
  varalfa1 = Prefijo + cabe2    + DniEmp + ca2  + ca3  + ca5  + ca1;

  contenido = handle + " " + varalfa1 + &#0#7 + &#0#8;
  |FGRABA contenido;

  l = l + 1;

|| -------------------------------------- Quinto Registro Individual

  ca3 = "014";

  alfa2 = #4#3; |QBF alfa2; alfa2 = (ceros + alfa2) % -102;
  alfa3 = #4#4; |QBF alfa3; alfa3 = (ceros + alfa3) % -103;
  ca6   = alfa2 + alfa3;                                     || Codigo

  ca7 = (#4#6 + libre40) % 131;  || Poblacion

  varalfa1 = "";
  varalfa1 = Prefijo + cabe2    + DniEmp + ca2 + ca3 + ca6 + ca7 + ca1;

  contenido = handle + " " + varalfa1 + &#0#7 + &#0#8;
  |FGRABA contenido;

  l = l + 1;

|| -------------------------------------- Sexto Registro Individual

  ca3 = "015";

  ca8 = (#4#5 + libre40) % 136;  || Provincia

  varalfa1 = "";
  varalfa1 = Prefijo + cabe2    + DniEmp + ca2 + ca3 + ca8 + ca1;

  contenido = handle + " " + varalfa1 + &#0#7 + &#0#8;
  |FGRABA contenido;

  l = l + 1;

|| -------------------------------------- Septimo Registro Individual
|| -------------------------------------y Octavo  Registro Individual

  |SI #1#21 = "S";

      ca3 = "016";

      concepto = #1#11; |QBF concepto;
      concepto = concepto + " ";
      |SI #1#12 = "S";
          alfa1 = #3#4; |QBF alfa1;
          concepto = concepto + alfa1;    || Serie
          alfa1 = #3#3; |QBF alfa1;
          concepto = concepto + alfa1 + " ";  || Numero Orden
      |FINSI;
      |SI #1#13 = "S";
          alfa1 = #3#5; |QBF alfa1;
          concepto = concepto + alfa1 + " ";   || N§ pago
      |FINSI;
      |SI #1#14 = "S";
          alfa1 = #5#6; |QBF alfa1;
          concepto = concepto + alfa1 + " ";   || Fecha Emision
      |FINSI;
   ||   |SI #1#15 = "S";
   ||       alfa1 = #5#25; |QBF alfa1;
   ||       concepto = concepto + alfa1;
   ||   |FINSI;
      |QBF concepto;
      |SI concepto ! "";
          ca9 = concepto + libre40; ca9 = ca9 % 136;  || Concepto 1

          varalfa1 = "";
          varalfa1 = Prefijo + cabe2    + DniEmp + ca2 + ca3 + ca9 + ca1;

          contenido = handle + " " + varalfa1 + &#0#7 + &#0#8;
          |FGRABA contenido;
          l = l + 1;

          ca3 = "017";
      |FINSI;

      concepto = #1#16; |QBF concepto;
      concepto = concepto + " ";
      |SI #1#17 = "S";
          alfa1 = #3#4; |QBF alfa1;
          concepto = concepto + alfa1;    || Serie
          alfa1 = #3#3; |QBF alfa1;
          concepto = concepto + alfa1 + " ";  || Numero Orden
      |FINSI;
      |SI #1#18 = "S";
          alfa1 = #3#5; |QBF alfa1;
          concepto = concepto + alfa1 + " ";   || N§ pago
      |FINSI;
      |SI #1#19 = "S";
          alfa1 = #5#6; |QBF alfa1;
          concepto = concepto + alfa1 + " ";   || Fecha Emision
      |FINSI;
  ||    |SI #1#20 = "S";
  ||        alfa1 = #5#25; |QBF alfa1;
  ||        concepto = concepto + alfa1;
  ||    |FINSI;
      |QBF concepto;
      |SI concepto ! "";
          ca10 = concepto + libre40; ca10 = ca10 % 136;  || Concepto 1

          varalfa1 = "";
          varalfa1 = Prefijo + cabe2    + DniEmp + ca2 + ca3 + ca10 + ca1;

          contenido = handle + " " + varalfa1 + &#0#7 + &#0#8;
          |FGRABA contenido;
          l = l + 1;
      |FINSI;

  |FINSI;
|FPRC;

|PROCESO regindiv;
  trecibos = 0;
  timporte = 0;
  |BUCLELIN 0grabarecib#2;
  |SI swERROR = 1;
      |MENAV "    ATENCION !!!. DATOS DE PROVEEDORES INCOMPLETOS. ";
  |FINSI;
  |SI trecibos ! 0;
      #2#1  = #0#3;
      #2#2  = #0#5;
      #2#10 = "D";
      |GRABA 020#2a;
      |LIBERA #2;
  |FINSI;
|FPRC;

||***********************************************************
||********** REGISTRO TOTAL *********************************
||***********************************************************
|PROCESO campo_totales;

  ca1 = libre * 115;

  nume1 = timporte;
  ca2   = nume1; |QBF ca2;
  ca2   = ceros + ca2; ca2 = ca2 % -112;    || Suma Importes
                       ||  |SI nume1 ] 0; ca2 = ceros + ca2; ca2 = ca2 % -112; |FINSI;  || Importe
                       ||  |SI nume1 < 0; ca2 = "               "+ ca2; ca2 = ca2 % -112; |FINSI;
  nume1 = trecibos;
  ca3   = nume1; |QBF ca3;
  ca3   = ceros + ca3; ca3 = ca3 % -108;    || Numero Recibos 010

  l     = l + 1; || suma tambien este registro
  nume1 = l;
  ca4   = nume1; |QBF ca4;
  ca4   = ceros + ca4; ca4 = ca4 % -110;    || Numero Registros

  ca5 = libre * 113;

  varalfa1 = "";
  varalfa1 = Prefijo + cabe3 + DniEmp + ca1 + ca2 + ca3 + ca4 + ca5;

  contenido = handle + " " + varalfa1 + &#0#7 + &#0#8;
  |FGRABA contenido;
|FPRC;

|| **************************************************************************

|PROCESO pasecuencial; |TIPO 3;
     |ABRE #camoneda;
     #camoneda#0 = #48#2;
     #camoneda#1 = #48#3;
     |LEE 000#camoneda.=;
     |SI FSalida = 0;
        Moneda = #camoneda#2;
        |SI #camoneda#2 = "P";
           MonBase = 1;
        |SINO;
           MonBase = 2;
        |FINSI;
     |SINO;
        Moneda = "P";
        MonBase = 1;
     |FINSI;
     |CIERRA #camoneda;

     |HAZ PideMoneda;

     |SI MonBase = 2;                  || euros
        EURO = 166.386; EURODB = 2; EURODV = 0;
     |SINO;
        EURO = 1 / 166.386; EURODB = 0; EURODV = 2;
     |FINSI;

     |SI Moneda = "P"; Prefijo = "0"; |SINO; Prefijo = "5"; |FINSI;

  |ABRE #2;
  |HAZ leerem;
  |SI swErrorR = 1; |O swErrorR = 2;
      |MENAV "    ERROR !!! TRANSFERENCIA NO CORRECTA ";
      |ACABA;
  |FINSI;

  |HAZ leepas;
  |SI swErrorP ! 0;
      |MENAV "    ERROR !!! CONTROL DE PASE NO CORRECTO ";
      |ACABA;
  |FINSI;

  |HAZ varia_fich;

  |CAMPO_VISUAL #0#10,1,"S"; #0#10 = 0;
  |CAMPO_VISUAL #0#11,1,"S"; #0#11 = 0;
  |CAMPO_VISUAL #0#12,1,"S"; #0#12 = 0;

|| ...................................................
  |ABRE #4;                || Abre Clientes / Proveedores
  |ABRE #5;                || Cobros

|| Abrir Fichero Secuencial

  |DFICE dirempr;
  fiche = "wb  " + dirempr + "casectra.seq";  || "nomfiche.ext";
  |QBF fiche;
  |FABRE fiche;
  handle = FSalida;
  |SI FSalida ] 0;
      |HAZ cabecera;
      |HAZ regindiv;
      ||   |SI swERROR = 0;
               |HAZ campo_totales;
      ||   |FINSI;
  |FINSI;
  contenido = handle + " " + &#0#9;
  |FGRABA contenido;

  FSalida = handle;
  |FCIERRA FSalida;

  Pos = -1;
  |CIERRA #2;
  |CIERRA #4;
  |CIERRA #5;

|| ...   |SI swERROR = 1; |ACABA; |FINSI;

  |D_CUADRO 1261, 2280;
  |ATRI R;
  |PINTA 1362, " Numero Recibos   ";
  |PINTA 1562, " Numero Registros ";
  |PINTA 1762, " Importe Total    ";
  |ATRI 0;
  |PINTA 1462, "                  ";
  |PINTA 1662, "                  ";
  |PINTA 1862, "                  ";
  |D_CUADRO 1961, 2280;
  |ATRI R;
  |PINTA 2062, " Desea grabar el  ";
  |PINTA 2162, " diskete ahora    ";
  |ATRI 0;

  #0#10 = trecibos;  |PINTA #0#10;
  #0#11 = l;         |PINTA #0#11;
  #0#12 = timporte;  |PINTA #0#12;

  programa = " TRANSFERENCIAS ";
  ficcopia = "transfer.seq";
  E_sup = 1;
  E_inf = "SNsn";
  sino  = "N";
  sino  = 2177 ? 1;
  |SI sino = "S"; |O sino = "s"; |CORRE "cared003.tab"; |FINSI;
|FPRC;

|| ********** Rutinas Varias

|PROCESO LetraDni;
|QBF CampoDni;
Letra2 = CampoDni;
|QBT Letra2;

    Caracter = Letra2 % 101;
|SI Caracter!"A";|Y Caracter!"B";|Y Caracter!"C";|Y Caracter!"D";
     |Y Caracter!"E";|Y Caracter!"F";
     |Y Caracter!"G";|Y Caracter!"P";|Y Caracter!"Q";|Y Caracter!"S";|Y Caracter!"X";
        Letra4 = "";
        Letra3 = Letra2 % 0;
        Carac2 = Letra3;
    |PARA Carac5 = 1;  |SI Carac5 [ Carac2;  |HACIENDO Carac5 = Carac5 + 1;
            Carac3 = (Carac5 * 100) + 1;
            Letra3 = Letra2 % Carac3;
        |SI Letra3="0";|O Letra3="1";|O Letra3="2";|O Letra3="3";
                      |O Letra3="4";|O Letra3="5";|O Letra3="6";
                      |O Letra3="7";|O Letra3="8";|O Letra3="9";
                Letra4 = Letra4 + Letra3;
        |FINSI;
    |SIGUE;
        Carac4 = Letra4;
    |SI Carac4 ! 0; |Y Letra4 ! "";
            Carac1 = Carac4 $ 23;
            Carac1 = Carac1 + 1;
            Carac3 = (Carac1 * 100) + 1;
            Letra1 = LetrasNif % Carac3;

            Letra4 = "000000000000" + Letra4;
            Letra7 = Letra4 % -103;
            Letra6 = Letra4 % -403;
            Letra5 = Letra4 % -703;

            Letra8 = Letra5  % 101;
            |SI Letra8 = "0"; Letra5 = Letra5 % 202; |FINSI;

            Letra8 = Letra5  % 101;
            |SI Letra8 = "0"; Letra5 = Letra5 % 201; |FINSI;

            Letra8 = Letra5  % 101;
            |SI Letra8 = "0"; Letra5 = ""; |FINSI;

            |SI Letra5 ! "";
                Letra4 = Letra5 + Letra6 + Letra7 + Letra1;
            |SINO;
                Letra4 = Letra6 + Letra7 + Letra1;
            |FINSI;

            CampoDni = Letra4;
            CampoDni = ("000000000" + CampoDni) % -109;
    |FINSI;
|FINSI;
 CampoDni = (CampoDni + "         ");
 CampoDni = CampoDni % 109;
|FPRC;

|PROCESO cccBanco;
 |QBT ccc;
 c_alfa1 = ccc % 0;
 c_nume1 = c_alfa1;
 c_alfa1 = "";
 c_alfa2 = "";
 |PARA c_nume2 = 1; |SI c_nume2 [ c_nume1; |HACIENDO c_nume2 = c_nume2 + 1;
       c_nume3 = c_nume2 * 100 + 1;
       c_alfa2 = ccc % c_nume3;
       |SI c_alfa2 ] "0"; |Y c_alfa2 [ "9";
           c_alfa1 = c_alfa1 + c_alfa2;
       |FINSI;
 |SIGUE;
 ccc = c_alfa1;
|FPRC;

|PROCESO varia_fich;
|| .......... Variables del Presentador, del Ordenante y comunes

  DniEmp = #1#2; |QBF DniEmp;
  DniEmp = ("          " + DniEmp) % -110;

  libre40 = libre * 40;
  Nombre = #1#3 + libre40;     Nombre = Nombre % 136;

  DirEmp = #1#7  + libre40;    DirEmp = DirEmp % 136;

  alfa1 = #1#8; alfa1 = (ceros + alfa1) % -102;
  alfa2 = #1#9; alfa2 = (ceros + alfa2) % -103;

  CpoEmp = alfa1 + alfa2;
  PobEmp = #1#10 + libre40;    PobEmp = PobEmp % 131;

  alfa1 = #0#3;
  dd = alfa1 % A102;
  mm = alfa1 % A402;
  aa = alfa1 % A902;
  fechapre = aa + mm + dd;   || Fecha Formato a¤o + mes + dia

  alfa1 = #0#5;
  dd = alfa1 % A102;
  mm = alfa1 % A402;
  aa = alfa1 % A902;
  fechapro = aa + mm + dd;   || Fecha Formato a¤o + mes + dia
|FPRC;

|PROCESO separa;
  alfa3  = 0;
  numero = 0;
  |QBF direcc;
  alfa1 = direcc % 0;
  longi = alfa1;
  |PARA nume4 = longi; |SI nume4 > 1; |HACIENDO nume4 = nume4 - 1;
        nume2 = (nume4 * 100) + 1;
        alfa2 = direcc % nume2;
        |SI alfa2 = ",";
            nume3  = ((longi - nume4) + 100) * -1;
            alfa3  = direcc % nume3;
            nume2  = (nume4 - 1) + 100;
            direcc = direcc % nume2;
            |SAL;
        |FINSI;
  |SIGUE;
  |QBT alfa3;
  |SI alfa3 ! ""; numero = alfa3; |FINSI;
|FPRC;

|PROCESO PideMoneda;
   |PUSHV 1943 2177;
   |BLANCO 1943 2177;
   |CUADRO 1945 2175;
   |ATRI R; |PINTA 1946 , " Seleccione moneda de emision "; |ATRI N;
   |PINTA 2053, "(Pesetas/Euros)";
   |ATRI I; |PINTA 2054, "P"; |PINTA 2062, "E"; |ATRI N;
   E_sup  = 1;
   E_inf  = "EP";
   |SI MonBase = 1; Moneda = "P"; |SINO; Moneda = "E"; |FINSI;
   Moneda = 2069 ? 0;
   |POPV;
|FPRC;

|PROCESO Conversion;
   |SI MonBase = 1;
      |SI Moneda = "E";
         Cantidad = ((Cantidad / 166.386) ! 2) * 100;
      |FINSI;
   |SINO;
      |SI Moneda = "P";
         Cantidad = (Cantidad * 166.386) ! 0;
      |SINO;
         Cantidad = Cantidad * 100;
      |FINSI;
   |FINSI;
|FPRC;
