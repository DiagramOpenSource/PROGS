|FICHEROS;
  c3geinva #0;
  camocabe #1;
  camoline #2;
  canempre #30;
|FIN;

|VARIABLES;
    error2 = "0000No existe informe";
    para1 = 0;

    alfa2 = "";

   salida = 0;
   descue = "";
   hascue = "";
   swgraba = 0;
   swalgun = 0;
  &r_emp = 0;
  &r_per = 0;
  x = 0;
  varcamp = 0;
  empcon = 0;
  ejecon = 0;
  leidos = 0;
  dirfich0 = "";
  emp1 = 0;
  per1 = 0;
  xx = 0;
  yy = 0;
  desde = 0;
  hasta = 0;
  y = 0;
  sw_error = 0;
  traba = "";

   &entrada = 1;
   informe = "";
   &lintot = 0;
   &linnor = 0;
   &linant = 0;
   &totval = 0;
   &totamo = 0;
   &totano = 0;
   &totres = 0;
   &linanyo = 0;
   &linacum = 0;
   &linresi = 0;
   &pagina = 0;
   sw_pagina = 0;
   ulti = "";
   bueno = 0;
   alfa1 = "";
   fecsys = @;
   {-1}form  = "";           || Variables menu formato <1> o <2>
       form1 = "2400";
       form2 = "1";
       form3 = "Elija formato: ";
       form4 = "12";
       form5 = " < 1 > ";
       form6 = " < 2 > ";
  swinv = 0;
  op_form = 0;
  men9 = "     Empresa Contable INACTIVA ";
|FIN;

|INCLUYE acceso_i;
|INCLUYE puntos_i;
|INCLUYE numamo_i;

|| ***********************************************************************

|PROCESO &c_pagina;
  |SI sw_pagina = 0;
      pagina =  1;
      sw_pagina = 1;
  |SINO;
      pagina = pagina + 1;
  |FINSI;
|FPRC;

|| ***********************************************************************
||                       CALCULOS DE PANTALLA
|| ***********************************************************************

|PROCESO cuenta; |TIPO 0;
  p_alfa1 = #0Campo;
  salidas = FSalida;
  |HAZ punto;
  |SI p_error ! 0;  |ERROR;  |ACABA;  |FINSI;
|FPRC;

|PROCESO numamo; |TIPO 0;
    x_alfa1 = #0Campo;
|HAZ x_punto;
    #0Campo = x_alfa1;
    |PINTA #0Campo;
|FPRC;

|PROCESO defecto; |TIPO 7;
    fecsys = fec2; || fecsys = ~;
    alfa1 = fecsys;
    alfa1 = alfa1 % -104;
    #0Campo = alfa1;
    |PINTA #0Campo;
|FPRC;

|PROCESO consulta;
  |SI FSalida ! 9;  |ACABA;  |FINSI;
  |SUB_EJECUTA "caconemp.run";
  |ERROR;
  |SI Campo = 10;
      #0#10 = r_emp;
      #0#12 = r_per;
      |PTEC 802;
      |PTEC 802;
  |FINSI;
  |SI Campo = 11;
      #0#11 = r_emp;
      #0#13 = r_per;
      |PTEC 802;
  |FINSI;
  |SI Campo ] 14;
      #0Campo = r_emp;
      varcamp = Campo + 20;
      #0varcamp = r_per;
      |PTEC 802;
      |PTEC 802;
  |FINSI;
|FPRC;

|PROCESO mayor;
  alfa1 = #0Campo > " ";
  #0Campo = alfa1;
  |PINTA #0Campo;
|FPRC;

|PROCESO mayor1;
  |HAZ mayor;
  |SI #0Campo = "N";
      |PARA x = 14;  |SI x [ 53;  |HACIENDO x = x + 1;
            |CAMPO_MODIFICA #0x,1,"S";
      |SIGUE;
      #0#10 = 0;
      #0#12 = 0;
      #0#11 = 0;
      #0#13 = 0;
      |CAMPO_MODIFICA #0#10,1,"N";
      |CAMPO_MODIFICA #0#12,1,"N";
      |CAMPO_MODIFICA #0#11,1,"N";
      |CAMPO_MODIFICA #0#13,1,"N";
      |PINTA #0#10;
      |PINTA #0#12;
      |PINTA #0#11;
      |PINTA #0#13;
  |SINO;
      |PARA x = 14;  |SI x [ 53;  |HACIENDO x = x + 1;
            #0x = 0;
            |CAMPO_MODIFICA #0x,1,"N";
      |SIGUE;
      |CAMPO_MODIFICA #0#10,1,"S";
      |CAMPO_MODIFICA #0#12,1,"S";
      |CAMPO_MODIFICA #0#11,1,"S";
      |CAMPO_MODIFICA #0#13,1,"S";
  |FINSI;
|FPRC;

|| --------------- comprueba desde-hasta empresas ---------------------------

|PROCESO buscaem1;
  |SI #30#21 = "I"; |ACABA; |FINSI;
  leidos = 1;
  alfa1 = #30#2;
  alfa2 = #30#3;
|FPRC;

|DEFBUCLE buscaempre;
#30#1;
;
#0#10,desde;
#0#11,hasta;
e;
NULL,buscaem1;
|FIN;

|PROCESO buclempre;
  |SI #0#12 > #0#13;
      |MENAV "     ERROR EN INTRODUCCION";
      |ERROR;
      |ACABA;
  |FINSI;
  |SI #0#10 = 0;  |Y #0#11 = 0;  |ACABA;  |FINSI;
  desde = #0#12;
  hasta = #0#13;
  leidos = 0;
  |BUCLE buscaempre;
  |SI leidos = 0;
      |MENAV "    ATENCION SELECCION VACIA";
      |ERROR;
      |ACABA;
  |FINSI;
|FPRC;

|PROCESO deshas;
  |SI #0#11 < #0#10;
      |MENAV "      Error en Introduccion";
      |ERROR;
  |FINSI;
|FPRC;

|| ------------------------------------------------------------------------

|PROCESO nomodi;
  |PARA x = Campo + 1;  |SI x [ 33;  |HACIENDO x = x + 1;
        #0x = 0;
        |CAMPO_MODIFICA #0x,1,"N";
        y = x + 20;
        #0y = 0;
        |CAMPO_MODIFICA #0y,1,"N";
  |SIGUE;
  y = Campo + 20;
  #0y = 0;
  |CAMPO_MODIFICA #0y,1,"N";
  |PINTA #0y;
|FPRC;

|PROCESO esbuena; || desde campo de empresas seleccion
  |SI FSalida = 2; |ACABA; |FINSI;
  |SI #0Campo = 0; |HAZ nomodi;  |ACABA;  |FINSI;
  |ABRE #30;
  #30#2 = #0Campo;
  #30#3 = 0;
  |LEE 011#30];
  |SI FSalida ! 0;  |O #30#2 ! #0Campo;
      |MENAV "     NO Existe la empresa Contable ";
      |ERROR;
  |FINSI;
  |CIERRA #30;
|FPRC;

|PROCESO esbuena1;
  |SI FSalida = 2;|ACABA;|FINSI;
  x = Campo - 20;
  |SI #0x = 0;  |ACABA;  |FINSI;
  |ABRE #30;
  x = Campo - 20;
  #30#2 = #0x;
  #30#3 = #0Campo;
  |LEE 011#30=;
  |SI FSalida ! 0;
      |MENAV "     NO Existe Periodo en la empresa Contable ";
      |CIERRA #30;
      |ERROR;
      |ACABA;
  |FINSI;
  |CIERRA #30;
  |SI #30#21 = "I";
      |MENAV men9;
      |ERROR;
      |ACABA;
  |FINSI;
  x = Campo + 20;
  #0x = #30#1;
  |PINTA #0x;
  sw_error = 0;
  x = Campo - 20;
  alfa1 = #0x;
  alfa2 = #0Campo;
|FPRC;

|| **********************************************************************
||                       CALCULOS DE IMPRESION
|| **********************************************************************
|PROCESO acumano;
 |SI #2#4 = "A";
     |SI #2#3 [ #0#5;
         linacum = linacum + #2#9;
         |SI #2#3 = #0#5; linanyo = linanyo + #2#9; |FINSI;
     |FINSI;
 |FINSI;
|FPRC;

|PROCESO fechafac;

  |SI #0#6 = "N";
      |SI #1#22 = "N"; |ACABA; |FINSI;
  |FINSI;

  linanyo = 0;
  linacum = 0;
  |BUCLELIN 0acumano#1;
  |SI linanyo = 0; |Y linacum = 0; |ACABA; |FINSI;
  |SI linanyo = 0; |Y #0#8 = "N";  |ACABA; |FINSI;
  linresi = #1#7 - linacum;

  |SI bueno ! 0;          || si es primera vez imprime cabeceras solo;
      |SI #1#3 ! ulti;
          lintot = 1;     || imprime linea de totales;
          |PRINT; swalgun = 1;
          totval = 0;
          totamo = 0;
          totano = 0;
          totres = 0;
          lintot = 0;
          linant = 0;
          linnor = 1;     || imprime primera linea de cuenta;
      |SINO;
          lintot = 0;
          linnor = 0;
          linant = 1;     || imprime siguientes lineas de misma cuenta;
      |FINSI;
  |SINO;
      lintot = 0;
      linnor = 1;
      linant = 0;
  |FINSI;

  bueno = 1;
  ulti = #1#3;
  |PRINT; swalgun = 1;
  totval = totval + #1#7;    || acumula valor inicial;
  totamo = totamo + linacum; || acumula amortizacion;
  totano = totano + linanyo; || acumula amortizacion en a¤o;
  totres = totres + linresi; || acumula valor residual;
  lintot = 0;
  linnor = 0;
  linant = 0;
|FPRC;

|DEFBUCLE cainfres0;
#1#4;
0;
#0#0,0,#0#2;
#0#1,9,#0#3;
e;
NULL,NULL,NULL,NULL,NULL,fechafac;
#1#0,#1#1,#1#2,#1#3;
|FIN;

|PROCESO fechafac1;
  |SI #0#6 = "N";
      |SI #1#22 = "N"; |ACABA; |FINSI;
  |FINSI;
  linanyo = 0;
  linacum = 0;
  |BUCLELIN 0acumano#1;
  |SI linanyo = 0; |Y linacum = 0; |ACABA; |FINSI;
  |SI linanyo = 0; |Y #0#8 = "N";  |ACABA; |FINSI;
  linresi = #1#7 - linacum;
  |PRINT; swalgun = 1;
  bueno = 1;
  swinv = 1;
  totval = totval + #1#7;    || acumula valor inicial;
  totamo = totamo + linacum; || acumula amortizacion;
  totano = totano + linanyo; || acumula amortizacion en a¤o;
  totres = totres + linresi; || acumula valor residual;
|FPRC;

|DEFBUCLE cainfres1;
#1#1;
3,4;
#0#2, 00,#0#0,0;
#0#3, 99,#0#1,9;
e;
NULL, fechafac1;
|FIN;

|| ---------- Seleccion DESDE / HASTA Empresa.

|PROCESO esdesd1;
  |DBASE dir_fich;
  dirfich0 = dir_fich + "fich/";
  alfa1 = #30#2;
  alfa2 = #30#3;
  emp1 = #30#2;
  per1 = #30#3;
  alfa1 = "00000" + alfa1;   alfa1 = alfa1 % -105;
  alfa2 = "0"   + alfa2;   alfa2 = alfa2 % -101;
  dirfich0 = dirfich0 + alfa1 + alfa2 + "/";

  |SI #30#21 = "I"; |ACABA; |FINSI;
  |HAZ haztodo;
|FPRC;

|DEFBUCLE esdesde;
#30#1;
;
#0#10,#0#12;
#0#11,#0#13;
e;
NULL,esdesd1;
|FIN;

|| ---------- Seleccion en parrilla.

|PROCESO esunaauna;
  |PARA xx = 14;  |SI xx [ 33;  |HACIENDO xx = xx + 1;
        |SI #0xx = 0;  |SAL;  |FINSI;
        |DBASE dir_fich;
        dirfich0 = dir_fich + "fich/";
        yy = xx + 20;
        emp1 = #0xx;
        per1 = #0yy;
        alfa1 = #0xx;
        alfa2 = #0yy;
        alfa1 = "00000" + alfa1;   alfa1 = alfa1 % -105;
        alfa2 = "0"   + alfa2;   alfa2 = alfa2 % -101;
        dirfich0 = dirfich0 + alfa1 + alfa2 + "/";
|| lee empresa
        |ABRE #30;
        #30#2 = emp1;
        #30#3 = per1;
        |LEE 011#30=;
        |CIERRA #30;
        |HAZ haztodo;
  |SIGUE;
|FPRC;
|| ***********************************************************************
|| ***************** PROCESO PRINCIPAL CON TIPO 3 ************************
|| ***********************************************************************

|PROCESO impre; |TIPO 3;
 |MENU form;
 op_form = FSalida;
 |SI op_form > 0;
     |HAZ los_informes;
     |SI op_form = 1;
         informe = que_i1;
     |SINO;
         informe = que_i2;
     |FINSI;
 |SINO;
     |ACABA;
 |FINSI;

 |IMPRE 0;
 |SI FSalida ! 0; |ACABA; |FINSI;

 |INFOR informe;
 |SI FSalida ! 0;
     |MENAV error2;
     |FINIMP;
     |ACABA;
 |FINSI;
  |SI #0#9 = "S";
      |BUCLE esdesde;
  |SINO;
      |HAZ esunaauna;
  |FINSI;
  |FININF;
  |FINIMP;
|FPRC;

|PROCESO haztodo;
  enEmpresa = #canempre#2;
  enPeriodo = #canempre#3;
  swOperacion = 0;
  |SUB_EJECUTA "camoneda.tab";

 |PATH_DAT #1 dirfich0;
 |PATH_DAT #2 dirfich0;
      bueno = 0;
      swalgun = 0;
      |SI op_form = 1;
          |BUCLE cainfres0;
          |SI swalgun = 1;
              lintot = 1;
              |PRINT;
              swalgun = 1;
          |FINSI;
      |SINO;
          |BUCLE cainfres1;
      |FINSI;
      |SI swalgun = 1; |PIEINF; |FINSI;
      swalgun = 0;
 sw_pagina = 0;
 totval = 0;
 totamo = 0;
 totano = 0;
 totres = 0;
|FPRC;
