|FICHEROS;
  c3gec390 #0;
  caman300 #1;
  caportad #2;
  cama3390 #3;
  caivarep #4;
  caivasop #5;
  cacon300 #7;
  canempre #30;
|FIN;

|VARIABLES;
  sw_error = 0;
  swant = 0;
  modo = 0;
  anyo = 0;
  compens = 0;
  alfa1 = "";
  alfa2 = "";
  alfa3 = "";
  nume1 = 0;
  nume2 = 0;
  a = 0;
  b = 0;
  c = 0;
  empr  = 0;
  i_cm1 = 1;
  x1 = 0;
  x2 = 0;
  x3 = 0;
  x4 = 0;
  x5 = 0;
  tot_bas = 0;
|FIN;

|INCLUYE acceso_i;
|INCLUYE multie_i;

|| ***********************************************************************
|PROCESO tipo8;  |TIPO 8;
  |HAZ inicio;
  any = #30#26;
  |HAZ coloca;
|FPRC;

|PROCESO defectos; |TIPO 7;
 #0#0 = any;
 |PINTA #0#0;
|FPRC;

|PROCESO mayor;
  alfa1 = #0Campo > " ";
  #0Campo = alfa1;
  |PINTA #0Campo;
|FPRC;

|| ***********************************************************************
|PROCESO sel_multi;
  |SI #30#26 ! #0#0;  |ACABA; |FINSI;
  |SI #30#21 = "I";   |ACABA; |FINSI;
  sw_error = 0;

  alfa1 = #30#2;
  alfa2 = #30#3;
  alfa1 = "00000" + alfa1;   alfa1 = alfa1 % -105;
  alfa2 = "0"     + alfa2;   alfa2 = alfa2 % -101;
  dirfich0 = dir_fich + alfa1 + alfa2 + "/";

  || ... Busca Datos de Portada
  |PATH_DAT #2 dirfich0;
  |DDEFECTO #2;
  |ABRE #2;
  #2#0 = #30#2;
  #2#1 = #30#26;
  |LEE 000#2=;
  |SI FSalida ! 0;
      sw_error = 1;
  |FINSI;
  |CIERRA #2;

||   |SI sw_error = 0; leidos = 1; |FINSI;
  leidos = 1;
|FPRC;

|PROCESO esbuena2;
 |SI FSalida = 2; |ACABA; |FINSI;
 |CAMPO_MODIFICA #0Campo,0,alfa1;
 |SI alfa1 = "N"; |ACABA; |FINSI;
 |SI sw_error !  0; |Y leidos = 1;
     |SI sw_error = 1; |MENAV "    No encontrado Fichero Portadas Modelos"; |FINSI;
     leidos = 0;
     |ERROR;
 |FINSI;
|FPRC;

|| ***********************************************************************
|| ---------- Seleccion DESDE / HASTA Empresa.
|PROCESO esdesd1;
  alfa1 = #30#2;
  alfa2 = #30#3;
  alfa1 = "00000" + alfa1;   alfa1 = alfa1 % -105;
  alfa2 = "0"   + alfa2;   alfa2 = alfa2 % -101;
  dirfich0 = dir_fich + alfa1 + alfa2 + "/";
  sw_error = 0;
  leidos = 0;
  |HAZ sel_multi;
  |SI leidos = 1; |Y sw_error = 0;
      |HAZ haztodo;
  |FINSI;
|FPRC;

|DEFBUCLE esdesde;
 #30#1;
 ;
 d_empres, d_period;
 h_empres, h_period;
 e;
 NULL,esdesd1;
|FIN;

|| ---------- Seleccion en parrilla.
|PROCESO esunaauna;
  |PARA xx = i_cm6; |SI xx [ i_cm7; |HACIENDO xx = xx + 1;
        |SI #0xx = 0;  |SAL;  |FINSI;
        yy = xx + 20;
        alfa1 = #0xx;
        alfa2 = #0yy;

        || ... lee empresa
           |ABRE #30;
           |DDEFECTO #30;
           #30#2 = #0xx;
           #30#3 = #0yy;
           |LEE 011#30=;
           |CIERRA #30;
           leidos = 0; sw_error = 0;
           |HAZ sel_multi;
           |SI leidos = 0; |O sw_error ! 0; |VETE otramas; |FINSI;
        || ...................

        alfa1 = "00000" + alfa1;   alfa1 = alfa1 % -105;
        alfa2 = "0"     + alfa2;   alfa2 = alfa2 % -101;
        dirfich0 = dir_fich + alfa1 + alfa2 + "/";

        |HAZ haztodo;
 |ET otramas;
  |SIGUE;
|FPRC;

|| ***********************************************************************
|PROCESO carga_390;
  |SI #1#1 = 1;
      |SI swant = 1; compens = #1#29; #3#147 = compens; |FINSI;
  |FINSI;
  |HAZ suma_bases;
  |HAZ deduccion;
  |HAZ resto;
|FPRC;

|PROCESO resto;
  #3#157 = #3#157 + #1#33;        ||Total a Ingresar

  #3#159 = #1#32;      ||A Compensar
  #3#160 = #1#31;      ||A Devolver

  #3#161 = #3#161 + #1#5 + #1#8 + #1#11;  ||Operaciones Regimen General
  #3#162 = #3#162 + #1#34;      ||Op Regimen Simplificado     (100)
  #3#163 = #3#163 + #1#38;      ||Op AGP                      (101)
  #3#164 = #3#164 + #1#39;      ||Rec Equivalencia            (102)
  #3#165 = #3#165 + #1#35;      ||Entregas intracomun.        (103)
  #3#166 = #3#166 + #1#36;      ||Op Exentas con deduccion    (104)
  #3#167 = #3#167 + #1#37;      ||Op Exentas sin deduccion    (105)
  #3#168 = #3#168 + #1#40;      ||Entrega bienes, financieros (106)
  #3#169 = #3#169 + #1#41;      ||Entraga bienes Inversion    (107)
  #3#170 = #3#170 + #1#42;      ||Total Volumen               (108)

  #3#171 = #3#171 + #1#52;      || punto 109
  #3#172 = #3#172 + #1#53;      || punto 110
  #3#173 = #3#173 + #1#59;      || punto 111
  #3#174 = #3#174 + #1#56;      || punto 112
  #3#175 = #3#175 + #1#57;      || punto 113
|FPRC;

|PROCESO deduccion;
  #3#68 = #3#68 + #1#21;      ||Bienes y servicios corrientes (INT.)
  #3#69 = #3#69 + #1#43;      ||Bienes de Inversion (INT.)
  #3#70 = #3#70 + #1#22;      ||Bienes Corrientes (IMP.)
  #3#71 = #3#71 + #1#44;      ||Bienes Inversion (IMP.)
  #3#72 = #3#72 + #1#51;      ||Bienes Corrientes (Adq.intrac.)
  #3#73 = #3#73 + #1#58;      ||Bienes de inversion (Adq.intrac.)

  #3#74 = #3#74 + #1#23;        ||Compensacion AGP
  #3#75 = #3#75 + #1#24;        ||Regularizacion Inversiones

  #3#76 = #3#76 + #1#21 + #1#43 + #1#22 + #1#44 + #1#51 + #1#23 + #1#24 + #1#58; || TOTAL
|FPRC;

|PROCESO suma_bases;
  |SI #2#73 ! 4; || ..........  no es de viajeros
                         #3nume1 = #3nume1 + #1#5;               ||BASES
      nume2 = nume1 + 2; #3nume2 = #3nume2 + #1#8;
      nume2 = nume1 + 4; #3nume2 = #3nume2 + #1#11;

      nume2 = nume1 + 1; #3nume2 = #3nume2 + #1#7;               ||CUOTAS
      nume2 = nume1 + 3; #3nume2 = #3nume2 + #1#10;
      nume2 = nume1 + 5; #3nume2 = #3nume2 + #1#13;
  |SINO;
      nume2 = nume1 + 1;
      #3nume1 = #3nume1 + #1#11;       ||Solamente para la actividad 4
      #3nume2 = #3nume2 + #1#13;
  |FINSI;
  #3#178 = #3#178 + #1#49; ||Intracomunitarias al 16%
  #3#179 = #3#179 + #1#50;

  #3#59 = #3#59 + #1#14;           ||RECARGOS BASES
  #3#61 = #3#61 + #1#17;
  #3#63 = #3#63 + #1#46;

  #3#60 = #3#60 + #1#16;
  #3#62 = #3#62 + #1#19;           ||RECARGO IVA
  #3#64 = #3#64 + #1#48;
                       ||  #3#184, #3#185 Recargo  1.75

  a = #1#5  + #1#8  + #1#11 + #1#49;     ||  BASES
  b = #1#7  + #1#10 + #1#13 + #1#50;     || IVA
  c = #1#16 + #1#19 + #1#48;              || Recargo

  #3#57 = #3#57 + a;     ||TOTAL BASES
  #3#58 = #3#58 + b;     ||TOTAL IVA
  #3#67 = #3#67 + (b + c);    ||TOTAL IVA + Recargo
|FPRC;


|DEFBUCLE carga;
 #1#1;
 ;
 " " ,  0 , anyo;
 "z" , 12 , anyo;
 e;
 NULL , carga_390;
|FIN;


|| ***********************************************************************
|PROCESO fin_carga;
  #3#77  = #3#67  - #3#76;    || Total Regimen General
  #3#145 = #3#141 - #3#144;   || Total Regimen Simplificado
  #3#146 = #3#77  + #3#145;   || Suma de Resultados
  #3#148 = #3#146 - #3#147;   || Resultado de la Liquidaci¢n

  #3#149 = #2#72;      ||Territorio Comun
  #3#150 = #2#75;      ||Alava
  #3#151 = #2#77;      ||Guipuzcoa
  #3#152 = #2#76;      ||Vizcaya
  #3#153 = #2#74;      ||Navarra

  #3#154 = #3#146 * #3#149 / 100;
  #3#155 = #3#147 * #3#149 / 100;
  #3#156 = #3#154 - #3#155;
|FPRC;

|PROCESO lee_ant;  || Lee el registro del Modelo A¤o Anterior
  compens = 0;
  swant = 0;
  anyo = anyo - 1;
  #3#0 = empr;
  #3#1 = anyo;
  |LEE 001#3=;
  |SI FSalida = 0;
      compens = #3#159;    ||Compensar A¤o anterior
  |SINO;
      swant = 1;
  |FINSI;
  anyo = anyo + 1;
|FPRC;

|PROCESO reo390g; |TIPO 3;
  anyo = #0#0;  || A¤o a pasar
  |PUSHV 2102 2378;
  |ATRI I;
  |CUADRO 2102 2378
  |ATRI N;
  |ATRI R;
  |PINTA 2203," EMPRESA:                                                                  ";
  |ATRI 0;
  |SI #0i_cm1 = "S";
      |BUCLE esdesde;
  |SINO;
      |HAZ esunaauna;
  |FINSI;
|FPRC;

|| ******************************** PROCESO
|PROCESO haztodo;
 enEmpresa = #canempre#2;
 enPeriodo = #canempre#3;
 swOperacion = 0;
 |SUB_EJECUTA "camoneda";

 |PATH_DAT #1 dirfich0;
 |PATH_DAT #2 dirfich0;
 |PATH_DAT #3 dirfich0;
 |PATH_DAT #4 dirfich0;
 |PATH_DAT #5 dirfich0;
 |PATH_DAT #7 dirfich0;

  |ATRI R;
  |PINTA 2213, #30#2;
  |PINTA 2219, #30#3;
  |PINTA 2221, #30#1;
  |ATRI 0;

 empr = #30#2;

 |ABRE #3;   || Fichero de Mantenimiento del 390 Actual
 #3#0 = empr;
 #3#1 = anyo;
 |LEE 101#3=;
 |SI FSalida = 0;
     |SI #0#66 = "S";
         |BORRA 020#3a;
     |SINO;
         |CIERRA #3;
         |ACABA;
     |FINSI;
 |FINSI;
 |LIBERA #3;

 |HAZ lee_ant;  || busca el a¤o anterior ......

 |DDEFECTO #3;

 #3#147 = compens; || Compensar de A¤os Anteriores
 #3#0 = empr;      ||Empresa
 #3#1 = anyo;      ||A¤o
|| ... Datos Alfa
 #3#2 = #2#2;                                  || Nombre Empresa
 #3#3 = "          ";
 #3#4 = "N";

 #3#5  = #2#20;  #3#11 = #2#26;               || Actividades / Clave
 #3#6  = #2#21;  #3#12 = #2#27;
 #3#7  = #2#22;  #3#13 = #2#28;
 #3#8  = #2#23;  #3#14 = #2#29;
 #3#9  = #2#24;  #3#15 = #2#30;
 #3#10 = #2#25;  #3#16 = #2#31;

 alfa1 = #2#32;  alfa2 = alfa1 % 101; alfa3 = alfa1 % 204;
 #3#17 = alfa2;  #3#23 = alfa3;

 alfa1 = #2#33;  alfa2 = alfa1 % 101; alfa3 = alfa1 % 204;
 #3#18 = alfa2;  #3#24 = alfa3;

 alfa1 = #2#34;  alfa2 = alfa1 % 101; alfa3 = alfa1 % 204;
 #3#19 = alfa2;  #3#25 = alfa3;

 alfa1 = #2#35;  alfa2 = alfa1 % 101; alfa3 = alfa1 % 204;
 #3#20 = alfa2;  #3#26 = alfa3;

 alfa1 = #2#36;  alfa2 = alfa1 % 101; alfa3 = alfa1 % 204;
 #3#21 = alfa2;  #3#27 = alfa3;

 alfa1 = #2#37;  alfa2 = alfa1 % 101; alfa3 = alfa1 % 204;
 #3#22 = alfa2;  #3#28 = alfa3;

 #3#29 = #2#97;    || > 500.000 ptas
 #3#30 = #2#98;    || -E-
 #3#31 = #2#99;    || -F-
 #3#32 = #2#100;   || -G-


 |SI #2#73 = 1;
     nume1 = 33;   ||  "Regimen Ordinario";
 |SINO;
     |SI #2#73 = 2; |O #2#73 = 3;
         nume1 = 39;  || "Regimen Especial Bienes Usados, Obj. Arte ...";
     |SINO;
         |SI #2#73 = 4;
             nume1 = 45; || "Regimen Esp. Agencias de Viaje";
         |SINO;
             nume1 = 47;  || "Regimen Esp. Det. Proporcional";
         |FINSI;
     |FINSI;
 |FINSI;

 |HAZ calbases;

 |BUCLE carga;

 |HAZ fin_carga;

 |GRABA 020#3n;
 |CIERRA #3;
|FPRC;

|| ***********************************************************************
|| ************* CARGA DATOS QUE NO ESTAN EN EL 300 **********************
|| ***********************************************************************
|PROCESO iva_rep2;
 tot_bas = #5#8 + #5#9 + #5#10 + #5#52 + #5#53;
 x1 = 0;  x2 = 0;  x3 = 0;  x4 = 0;  x5 = 0;
 |SI #5#11 = 4;  x1 = 53;   |FINSI;
 |SI #5#11 = 7;  x1 = 176;  |FINSI;
 |SI #5#11 = 16; x1 = 178;  |FINSI;

 |SI #5#12 = 4;  x2 = 53;   |FINSI;
 |SI #5#12 = 7;  x2 = 176;  |FINSI;
 |SI #5#12 = 16; x2 = 178;  |FINSI;

 |SI #5#13 = 4;  x3 = 53;   |FINSI;
 |SI #5#13 = 7;  x3 = 176;  |FINSI;
 |SI #5#13 = 16; x3 = 178;  |FINSI;

 |SI #5#54 = 4;  x4 = 53;  |FINSI;
 |SI #5#54 = 7;  x4 = 176;  |FINSI;
 |SI #5#54 = 16; x4 = 178;  |FINSI;

 |SI #5#55 = 4;  x5 = 53;  |FINSI;
 |SI #5#55 = 7;  x5 = 176;  |FINSI;
 |SI #5#55 = 16; x5 = 178;  |FINSI;

 |SI x1 ! 0;
     #3x1 = #3x1 + #5#08;                     || base 1
     x1 = x1 + 1;    #3x1 = #3x1 + #5#14;     || cuota iva 1
 |FINSI;
 |SI x2 ! 0;
     #3x2 = #3x2 + #5#09;                     || base 2
     x2 = x2 + 1;    #3x2 = #3x2 + #5#15;     || cuota iva 2
 |FINSI;
 |SI x3 ! 0;
     #3x3 = #3x3 + #5#10;                     || base 3
     x3 = x3 + 1;    #3x3 = #3x3 + #5#16;     || cuota iva 3
 |FINSI;
 |SI x4 ! 0;
     #3x4 = #3x4 + #5#52;                     || base 4
     x4 = x4 + 1;    #3x4 = #3x4 + #5#56;     || cuota iva 4
 |FINSI;
 |SI x5 ! 0;
     #3x5 = #3x5 + #5#53;                     || base 5
     x5 = x5 + 1;    #3x5 = #3x5 + #5#57;     || cuota iva 5
 |FINSI;
|FPRC;

||.........IVA SOPORTADO
|PROCESO sopor1;
 #7#0 = #5#6;
 |LEE 001#7=;
 |SI FSalida ! 0;
     #7#1 = 1;
 |FINSI;
 |SI #7#1 ! 9;
     |HAZ acum_sop;
 |FINSI;
|FPRC;

|PROCESO acum_sop;
  tot_bas = #5#8 + #5#9 + #5#10 + #5#52 + #5#53;
  |SI #5#23 = "S";
      |SI #5#48 = "N";         ||Si no es de inversion
          |HAZ noinver;
      |SINO;         ||SI es de inversiones
          |HAZ inver;
      |FINSI;
  |FINSI;
|FPRC;

|PROCESO inver;
 |SI #7#1 = 6;       ||Op. Interiores Inversiones
     #3#189 = #3#189 + tot_bas;
 |SINO;
     |SI #7#1 = 7;  ||Importaciones Inversiones
          #3#191 = #3#191 + tot_bas;
     |SINO;
          |SI #7#1 = 3;
              #3#193 = #3#193 + tot_bas;
              |SI #7#2 = 2;
                  |HAZ iva_rep2;
              |FINSI;
          |FINSI;
      |FINSI;
 |FINSI;
|FPRC;

|PROCESO noinver;
  |SI #7#1 = 1;       ||Operaciones Corrientes
      #3#188 = #3#188 + tot_bas;
  |SINO;
      |SI #7#1 = 2;   ||Importaciones
          #3#190 = #3#190 + tot_bas;
      |SINO;
          |SI #7#1 = 3;  ||Adj. Intracomunitaria
              #3#192 = #3#192 + tot_bas;
              |SI #7#2 = 2;
                  |HAZ iva_rep2;
              |FINSI;
          |SINO;
              |SI #7#1 = 4;  ||Regimen AGp
                  #3#194 = #3#194 + tot_bas;
              |FINSI;
          |FINSI;
      |FINSI;
  |FINSI;
|FPRC;

|DEFBUCLE sopor;
 #5#1;
 28;
  0,      1, "  ", "S";
 99, 999999, "zz", "S";
 e;
 NULL, sopor1;
|FIN;

|| ************************************
||.........IVA REPERCUTIDO / ADQUISICIONES INTRACOMUNITARIAS
|PROCESO reper1;
 #7#0 = #4#6;
 |LEE 001#7=;
 |SI FSalida = 0;
     |SI #7#2 = 2;
         |HAZ iva_rep;
         |SI #7#1 = 3;
             |SI #4#48 = "N"; #3#192 = #3#192 + tot_bas; |FINSI;
             |SI #4#48 = "S"; #3#193 = #3#193 + tot_bas; |FINSI;
         |FINSI;
     |FINSI;
 |FINSI;
|FPRC;

|PROCESO iva_rep;
 tot_bas = #4#8 + #4#9 + #4#10 + #4#52 + #4#53;
 x1 = 0;  x2 = 0;  x3 = 0;  x4 = 0;  x5 = 0;
 |SI #4#11 = 4;  x1 = 53;   |FINSI;
 |SI #4#11 = 7;  x1 = 176;  |FINSI;
 |SI #4#11 = 16; x1 = 178;  |FINSI;

 |SI #4#12 = 4;  x2 = 53;   |FINSI;
 |SI #4#12 = 7;  x2 = 176;  |FINSI;
 |SI #4#12 = 16; x2 = 178;  |FINSI;

 |SI #4#13 = 4;  x3 = 53;   |FINSI;
 |SI #4#13 = 7;  x3 = 176;  |FINSI;
 |SI #4#13 = 16; x3 = 178;  |FINSI;

 |SI #4#54 = 4;  x4 = 53;  |FINSI;
 |SI #4#54 = 7;  x4 = 176;  |FINSI;
 |SI #4#54 = 16; x4 = 178;  |FINSI;

 |SI #4#55 = 4;  x5 = 53;  |FINSI;
 |SI #4#55 = 7;  x5 = 176;  |FINSI;
 |SI #4#55 = 16; x5 = 178;  |FINSI;

 |SI x1 ! 0;
     #3x1 = #3x1 + #4#08;                     || base 1
     x1 = x1 + 1;    #3x1 = #3x1 + #4#14;     || cuota iva 1
 |FINSI;
 |SI x2 ! 0;
     #3x2 = #3x2 + #4#09;                     || base 2
     x2 = x2 + 1;    #3x2 = #3x2 + #4#15;     || cuota iva 2
 |FINSI;
 |SI x3 ! 0;
     #3x3 = #3x3 + #4#10;                     || base 3
     x3 = x3 + 1;    #3x3 = #3x3 + #4#16;     || cuota iva 3
 |FINSI;
 |SI x4 ! 0;
     #3x4 = #3x4 + #4#52;                     || base 4
     x4 = x4 + 1;    #3x4 = #3x4 + #4#56;     || cuota iva 4
 |FINSI;
 |SI x5 ! 0;
     #3x5 = #3x5 + #4#53;                     || base 5
     x5 = x5 + 1;    #3x5 = #3x5 + #4#57;     || cuota iva 5
 |FINSI;
|FPRC;

|DEFBUCLE reper;
 #4#1;
 28;
  0,      0 , "  ", "S";
 99, 999999 , "zz", "S";
 e;
 NULL, reper1;
|FIN;

|| ***********************************************************************
|PROCESO calbases;
 |ABRE #7;
 |BUCLE reper;
 |BUCLE sopor;
 |CIERRA #7;
|FPRC;
