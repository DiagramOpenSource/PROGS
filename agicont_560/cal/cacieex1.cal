|FICHEROS;
    cacieex1 #00;   || def de proceso
    cacuenta #01;   || cuentas
    camaasic #02;   || apuntes cabs.
    camaasil #03;   || apuntes lins.
    cabaexp1 #04;   || temporal existencias
    caexistc #06;   || cabs. descarga exist.
    caexistl #07;   || lins. descarga exist.
    caconcep #09;   || conceptos automaticos
    camandia #10;   || diarios
|FIN;

|VARIABLES;
    &entrada = 1;
    fecalf = "";
    ini = 0;
    mes = "";
    mesfec = 0;
    mesac = 0;
    a = 0;
    b = 0;
    c = 0;
    importe = 0;
    opera = "";
    &modcon = 0;

    FEstado = 0;
    nume1 = 0;
    nume2 = 0;
    nume3 = 0;
    nume4 = 0;
    alfa1 = "";
    alfa2 = "";
    alfa3 = "";
    alfa4 = "";

    posici = 0;
    linea = 0;
    asie = 0;
    asie_exis = 0;
    line = 0;
    line_exis = 0;
    impo = 0;
    nada = " ";
    zeta = "z";
    abre = 0;
    antes = 0;
    desbloquea = 0;
    letrames = "";
    mes_letra = 0;
    mes_fin = 0;
    descar = 0;
    &d_mes = 0;
    &h_mes = 0;
    &estru = 0;
    &r_emp = 0;
    &r_per = 0;
    &r_eje = 0;
    &r_nom = "";
|FIN;

|INCLUYE inici1_i;
|INCLUYE conasi_i;
|INCLUYE protec_i;

========================================================================
                        CALCULOS PANTALLA
========================================================================

|PROCESO defecto; |TIPO 7;
  #0Campo = fec2;
  |PINTA #0Campo;
|FPRC;

|PROCESO topes; |TIPO 0;
  |SI #0Campo < fec1; |O #0Campo > fec2;
      |MENAV "    ESTA FECHA NO CORRESPONDE AL EJERCICIO";
      |ERROR;
  |FINSI;
|FPRC;

|PROCESO estruct; |TIPO 0;
  |SI #0#6 = 0;  |ACABA;  |FINSI;
  |ABRE #6;
  #6#0 = #0#6;
  |LEE 001#6=;
  |SI FSalida ! 0;
      |MENAV "    No existe la estructura de descarga.";
      |ERROR;
  |FINSI;
  |CIERRA #6;
|FPRC;

|PROCESO concepto; |TIPO 0;
  nume1 = Campo - 5;
  |ABRE #9;
  #9#0 = #0Campo;
  |LEE 021#9=;
  |SI FSalida ! 0;
      |DDEFECTO #9;
  |FINSI;
  #0nume1 = #9#1;
  |PINTA #0nume1;
  |CIERRA #9;
|FPRC;

|PROCESO diario_exis; |TIPO 0;
  |ABRE #10;
  #10#0 = #0#1;
  |LEE 021#10=;
  |SI FSalida ! 0;
      |DDEFECTO #10;
  |FINSI;
  #0#12 = #10#1;
  |PINTA #0#12;
  |CIERRA #10;
|FPRC;

|PROCESO pintames2; |TIPO 0;
  mes_letra = #0Campo;
  |HAZ mesletra;
  |SI Campo = 14;  |PINTA 1737, letrames;  |FINSI;
  |SI Campo = 15;  |PINTA 1837, letrames;  |FINSI;
|FPRC;

|PROCESO mesletra;
  |SI mes_letra = 0;  letrames = "Apertura  "; |FINSI;
  |SI mes_letra = 1;  letrames = "Enero     "; |FINSI;
  |SI mes_letra = 2;  letrames = "Febrero   "; |FINSI;
  |SI mes_letra = 3;  letrames = "Marzo     "; |FINSI;
  |SI mes_letra = 4;  letrames = "Abril     "; |FINSI;
  |SI mes_letra = 5;  letrames = "Mayo      "; |FINSI;
  |SI mes_letra = 6;  letrames = "Junio     "; |FINSI;
  |SI mes_letra = 7;  letrames = "Julio     "; |FINSI;
  |SI mes_letra = 8;  letrames = "Agosto    "; |FINSI;
  |SI mes_letra = 9;  letrames = "Septiembre"; |FINSI;
  |SI mes_letra = 10; letrames = "Octubre   "; |FINSI;
  |SI mes_letra = 11; letrames = "Noviembre "; |FINSI;
  |SI mes_letra = 12; letrames = "Diciembre "; |FINSI;
  |SI mes_letra = 13; letrames = "          "; |FINSI;
|FPRC;

========================================================================
                        PROCESO PADRE
========================================================================
|PROCESO conexi;
  d_mes = #0#14;
  h_mes = #0#15;
  estru = #0#6;
  r_emp = #30#2;
  r_per = #30#3;
  r_eje = #30#26;
  r_nom = #30#1;
  |SUB_EJECUTA "caexicon.run";
|FPRC;

|PROCESO proceso; |TIPO 3;
  |HAZ conexi;
  |DELINDEX #4;
  |ABRE #2;
  |ABRE #3;
  |HAZ pidepan;   || prepara temporal existencias
  |HAZ exis0;     || graba iniciales y finales
  |HAZ expl0;     || asiento explotacion y variaciones de existencias
  |BLIN 4;
  |HAZ final;     || actualiza niveles
  |CIERRA #2;
  |CIERRA #3;
|FPRC;

|PROCESO carga;
  |SI #3#08 = "H";
      #3#10 = #3#4;
      #3#11 = #3#5;
      #3#16 = "";
      #3#17 = 0;
  |SINO;
      #3#10 = "";
      #3#11 = 0;
      #3#16 = #3#4;
      #3#17 = #3#5;
  |FINSI;
|FPRC;

========================================================================
          GRABA EXISTENCIAS INICIALES Y FINALES
========================================================================

|PROCESO exis1;
  |PINTA 0436, #4#1;
  |PARA nume1 = 1; |SI nume1 [ 2; |HACIENDO nume1 = nume1 + 1;
        |SI nume1 = 1;  impo = #4#4;  |FINSI;
        |SI nume1 = 2;  impo = #4#5;  |FINSI;
        |SI impo ! 0;
            |SI line = 1;
                |HAZ contador;
                asie = #20#1;
            |FINSI;
            |DDEFECTO #3;
            #3#00 = #0#1;  ||  diario
            #3#01 = #0#0;  ||  fecha
            #3#02 = asie;  ||  documento
            #3#03 = line;  ||  linea
            |LEE 101#3=;
            FEstado = FSalida;
            #3#04 = #4#1;  ||  cuenta
            #3#05 = #4#2;  ||  digito
            |SI nume1 = 1;
                #3#06 = #0#7;  ||  concepto (iniciales)
                #3#07 = #0#2;  ||  descripcion (iniciales)
            |SINO;
                #3#06 = #0#8;  ||  concepto (finales)
                #3#07 = #0#3;  ||  descripcion (finales)
            |FINSI;
            |SI nume1 = 1;|Y impo < 0;  #3#08 = "D";  impo = impo * -1;  |FINSI;
            |SI nume1 = 1;|Y impo > 0;  #3#08 = "H";                     |FINSI;
            |SI nume1 = 2;|Y impo < 0;  #3#08 = "H";  impo = impo * -1;  |FINSI;
            |SI nume1 = 2;|Y impo > 0;  #3#08 = "D";                     |FINSI;
            |HAZ carga;
            #3#09 = impo;  ||  importe
            |SI FEstado = 0;
                |GRABA 020#3a;
            |SINO;
                |GRABA 020#3n;
            |FINSI;
            |LIBERA #3;
            |HAZ cabecera;
            |HAZ actualiz1;   || actualiza cuentas
            opera = "A";
            |HAZ altatra;     || actualiza niveles
            line = line + 10;
       |FINSI;
  |SIGUE;
|FPRC;

|DEFBUCLE cacieexp1;
 #4#1;
 ;
 #0#6,  1;
 #0#6,999;
 e;
 NULL, exis1;
|FIN;

|PROCESO exis0;
  abre = 1;
  line = 1;
  |BUCLE cacieexp1;
  asie_exis = asie;  || lo guarda para a¤adir luego las variaciones
  line_exis = line;
|FPRC;

========================================================================
             ASIENTO VARIACIONES DE EXISTENCIAS
========================================================================


|PROCESO expl3;   || apuntes de variaciones (descarga)
  desbloquea = 1;
  line_exis = line_exis + 10;
  |DDEFECTO #3;
  #3#00 = #0#1;       ||  diario
  #3#01 = #0#0;       ||  fecha
  #3#02 = asie_exis;  ||  documento
  #3#03 = line_exis;  ||  linea
  |LEE 101#3=;
  FEstado = FSalida;
  #3#04 = #1#0;  ||  cuenta
  #3#05 = #1#1;  ||  digito
  #3#06 = #0#9;  ||  concepto (descarga)
  #3#07 = #0#4;  ||  descripcion (descarga)
  #3#08 = "H";   ||  signo (no lo cambia)
  |SI impo > 0;  #3#08 = "H";  |FINSI;
  |SI impo < 0;  #3#08 = "D";  impo = impo * -1;  |FINSI;
  |HAZ carga;
  #3#09 = impo;  ||  importe
  |SI FEstado = 0;
      |GRABA 020#3a;
  |SINO;
      |GRABA 020#3n;
  |FINSI;
  |LIBERA #3;
  |HAZ cabecera;
  |HAZ actualiz1;   || actualiza cuentas
  opera = "A";
  |HAZ altatra;     || actualiza niveles
  desbloquea = 0;
|FPRC;

|PROCESO expl2;
  |SI #4#6 = #1#0;|Y #4#7 = #1#1;
      impo = impo + (#4#5 - #4#4);
  |FINSI;
|FPRC;

|DEFBUCLE cacieexp3;
 #4#1;
 ;
 #0#6,   1;
 #0#6, 999;
 e;
 NULL, expl2;
|FIN;

|PROCESO expl1;
  |SI #1#3 ! "S";  |ACABA;  |FINSI;
  impo = 0;
  |BUCLE cacieexp3; || busca si es de variaciones
  |SI impo ! 0;     ||  si es una cuenta de variaciones graba un
      |HAZ expl3;   || /nuevo apunte en el asiento de existencias
      |PINTA 0436, #1#0;
  |FINSI;
  impo = #1#53;
|FPRC;

|DEFBUCLE cacieexp2;
 #1#4;
 ;
 nada, 1,  1,  0,  0;
 zeta, 4, 99, 99, 99;
 be;
 NULL, expl1;
|FIN;

|PROCESO expl0;
  abre = 0;
  line = 1;
  |BUCLE cacieexp2;
  abre = 1;
|FPRC;

========================================================================
                PIDE EXISTENCIAS INICIALES Y FINALES
========================================================================

|PROCESO descarg;
  linea = linea + 1;
  #4#0 = linea;
  #4#8 = #0#6;
  |LEE 101#4=;
  FEstado = FSalida;
  |DDEFECTO #4;
  #4#0 = linea;
  #4#8 = #0#6;
  #4#1 = #1#0;
  #4#2 = #1#1;
  #4#3 = #1#2;
  mes_fin = #0#14 + 11;   || mes iniciales
  #4#4 = #7mes_fin;        || exist. iniciales
  mes_fin = #0#15 + 11;   || mes finales
  #4#5 = #7mes_fin;        || exist. finales
  descar = #4#4 - #4#5;
  |SI descar ] 0;
      #4#6 = #7#5;     || cuenta descarga D
      #4#7 = #7#6;     || digito descarga D
  |SINO;
      #4#6 = #7#8;     || cuenta descarga H
      #4#7 = #7#9;     || digito descarga H
  |FINSI;
  |SI FEstado ! 0;
      |GRABA 020#4n;
  |SINO;
      |GRABA 020#4a;
  |FINSI;
  |LIBERA #4;
|FPRC;

|DEFBUCLE cacieexp0;
 #1#1;
 ;
 #7#2, #7#3;
 #7#2, #7#3;
 e;
 NULL, descarg;
|FIN;

|PROCESO lineades;
  |BUCLE cacieexp0;
|FPRC;

|PROCESO pidepan;
  |ABRE #4;
  |ABRE #6;
  linea = 0;
  #6#0 = #0#6;
  |LEE 001#6=;
  |BUCLELIN 2lineades#6;
  |CIERRA #4;
  |CIERRA #6;
|FPRC;

**********************************************************************
    ACTUALIZACION DE NIVELES INFERIORES DE CUENTAS
**********************************************************************

|PROCESO altatra;
  modcon = 1;
  |ABRE #8;
  #8#0 = codcon;
  #8#1 = #3#4;
  #8#2 = #3#5;
  #8#3 = #3#1;
  #8#4 = #3#8;
  #8#6 = #3#0;
  |LEE 101#8=;
  |SI FSalida ! 0;
      |DDEFECTO #8;
      #8#0 = codcon;
      #8#1 = #3#4;
      #8#2 = #3#5;
      #8#3 = #3#1;
      #8#4 = #3#8;
      #8#6 = #3#0;
      |GRABA 101#8b;
  |FINSI;
  importe = #3#9;  || aqui si que se usa el importe real
  |SI opera = "B";
      importe = -importe;
  |FINSI;
  #8#5 = #8#5 + importe;
  |GRABA 101#8a;
  |LIBERA #8;
  |CIERRA #8;
|FPRC;

|PROCESO final;
  |SI modcon ! 0;
      |SUB_EJECUTA "camaapua.run";
  |SINO;
      |ABRE #8;
      |DDEFECTO #8;
      #8#0 = codcon;
      |LEE 001#8=;
      |SI FSalida = 0;
          |BORRA 020#8a;
      |FINSI;
      |CIERRA #8;
  |FINSI;
|FPRC;

****************************************************************************
   ACTUALIZACION DE CUENTAS Y CABECERAS DE APUNTES Y LECTURA ULT.ASIENTO
****************************************************************************

|PROCESO cabecera;
  |SI #3#9 = 0;  |ACABA;  |FINSI;
  |DDEFECTO #2;
  #2#00 = #3#0; ||  diario
  #2#01 = #3#1; ||  fecha
  #2#02 = #3#2; ||  documento
  #2#03 = #3#2; ||  asiento
  |LEE 101#2=;
  FEstado = FSalida;
  |SI #3#08 = "D";
      #2#04 = #2#04 + #3#09;
      |SI #2#09 = 0; #2#08 = #3#4; #2#09 = #3#5; |FINSI;  || contrp.HABER
  |FINSI;
  |SI #3#08 = "H";
      #2#05 = #2#05 + #3#09;
      |SI #2#11 = 0; #2#10 = #3#4; #2#11 = #3#5; |FINSI;  || contrp.DEBE
  |FINSI;
  |SI FEstado = 0;
      |GRABA 020#2a;
  |SINO;
      |GRABA 020#2n;
  |FINSI;
  |LIBERA #2;
|FPRC;

|PROCESO actualiz1;
  |SI abre = 1;  || en 'expl' ya viene abierto y leido del 'defbucle'
      |ABRE #1;
      #1#0 = #3#4;
      #1#1 = #3#5;
      |LEE 101#1=;
  |FINSI;
  |SI #3#0 = 0;
      |SI #3#8 = "D";
          #1#9 = #1#9 + #3#9;
          #1#51 = #1#51 + #3#9;
      |SINO;
          #1#10 = #1#10 + #3#9;
          #1#52 = #1#52 + #3#9;
      |FINSI;
      #1#11 = #1#9 - #1#10;
      #1#53 = #1#51 - #1#52;
  |SINO;
      |SI #3#0 = 99;
          |SI #3#8 = "D";
              #1#48 = #1#48 + #3#9;
              #1#51 = #1#51 + #3#9;
          |SINO;
              #1#49 = #1#49 + #3#9;
              #1#52 = #1#52 + #3#9;
          |FINSI;
          #1#50 = #1#48 - #1#49;
          #1#53 = #1#51 - #1#52;
      |SINO;
          |HAZ actualiz2;
      |FINSI;
  |FINSI;
  |SI #1#58 < #0#0;
      #1#58 = #0#0;
  |FINSI;
  |GRABA 020#1a;
  |SI desbloquea = 0;  ||  en las ctas. de variaciones hace dos grabaciones
      |LIBERA #1;      || /por lo tanto, en la primera no desbloquea
  |FINSI;
  |SI abre = 1;
      |CIERRA #1;
  |FINSI;
  |HAZ actualiz3;  || diario
|FPRC;

|PROCESO actualiz2;
  ini = dig1;      || numero de niveles
  fecalf = #0#0;
  mes = fecalf % 402;
  mesfec = mes;
  |SI ini = 1;
      mesac = mesfec;
  |SINO;
      mesac = (mesfec - ini) + 1;
      |SI mesac [ 0;
          mesac = (13 - ini) + mesfec;
      |FINSI;
  |FINSI;
  a = (mesac * 3) + 9;
  b = a + 1;
  c = a + 2;
  |SI #3#8 = "D";
      #1a = #1a + #3#9;
      #1#51 = #1#51 + #3#9;
  |SINO;
      #1b = #1b + #3#9;
      #1#52 = #1#52 + #3#9;
  |FINSI;
  #1c = #1a - #1b;
  #1#53 = #1#51 - #1#52;
|FPRC;

|PROCESO actualiz3;
  |ABRE #10;
  #10#0 = #3#0;
  |LEE 121#10=;
  |SI FSalida = 0;
      |SI #3#8 = "D";
          #10#2 = #10#2 + #3#9;
      |SINO;
          #10#3 = #10#3 + #3#9;
      |FINSI;
      |GRABA 020#10a;
  |FINSI;
  |LIBERA #10;
  |CIERRA #10;
|FPRC;
