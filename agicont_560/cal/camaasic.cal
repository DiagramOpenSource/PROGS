|FICHEROS;
    camaasil #00;   || apuntes lins.
    camaasic #01;   || apuntes cabs.
    cafantas #02;   || 'pintas' de acumulados diario
    catradia #04;   || actualizacion niveles inferiores
    camandia #10;   || diarios
    casieaca #11;   || predefinidos cabs.
    casieali #12;   || predefinidos lins.
    casieaax #13;   || consulta predefinido formato asiento
    caivarep #15;   || iva repercutido
    caivasop #16;   || iva soportado
    camancob #21;   || cobros
    camanpag #22;   || pagos
    caextras #23;   || anotaciones extracontables
    caextral #25;   || anotaciones extracontables Lineas
    camemori #24;   || ultimas tres operaciones
|FIN;

|VARIABLES;
    doce = "            "
    xx = 0;
    anter = 0;
    men1  = "     NO EXISTE EMPRESA !";
    men2  = "     Longitud de Cuenta Fuera de Nivel !";
    fecalf = "";
    mensa1 = "    ERROR ESTA FECHA NO CORRESPONDE AL EJERCICIO";
    &modcon = 0;
    traba1 = "";
    mas = 0;
    &entrada = 1;
    &inkey = 0;
    &coant = 0;
    &deant = "";
    &dhant = "";
    &imant = 0;
    &dpant = "";
    &ext1 = "";
    &ext2 = "";
    &ext3 = "";
    &ext4 = "";
    &ext5 = "";
    &predefine = 0;
    &predefine2 = 0;
    &sw_debe = 0;
    &sw_habe = 0;
    &j_d = 0;
    &j_h = 0;
    &i_serie = "";
    &i_factu = 0;
    &i_fecha = @;
    &aferlamar = 0;
    relacion = 0;
    alfa1 = "";
    alfa2 = "";
    alfa3 = "";
    alfa4 = "";
    nume1 = 0;
    nume2 = 0;
    nume3 = 0;
    nume4 = 0;
    &p_tabla = 0;
    &sw_tabla = 0;
    &sw_ptec = 0;
    posic = 0;
    lin_con = 0;
    m_c = 0;
    &sw_ivamerr = 0;
    &avisos = "";
    &f1auto = 0;
    &clau = "";
    ant_dia = 1;
    &ctab1 = "";
    &ctab2 = 0;
    ult_pre = 0;
|FIN;

|INCLUYE acceso_i;
|INCLUYE conasi_i;
|INCLUYE pastil_i;

|PROCESO modbaj; |TIPO 1;
    xx = (FEntrada / 100) ! 0;
|SI xx = 2;
    |HAZ carga_con;
|FINSI;
|SI xx = 3;
    |HAZ extraco;        || borra anotaciones extracontables
|FINSI;
|FPRC;

|PROCESO f1automa; |TIPO 7;             || desde el campo 'diario'
        #1#0 = ant_dia;
|SI f1auto = 1;
        #1#0 = ant_dia;
    |PINTA #1#0;
    |PTEC 823;
        f1auto = 0;
|FINSI;
|SI aferlamar = 1;
        #1#0 = ant_dia;
    |PINTA #1#0;
    |PTEC 824;
        aferlamar = 0;
|FINSI;
|FPRC;

|PROCESO el_diario; |TIPO 13;  || desde campo 'diario' y calculo 'asie_nue'
|DDEFECTO #10;
|ABRE #10;
    #10#0 = #1#0;
|LEE 001#10=;                 || lee el diario
|CIERRA #10;
    #2#0 = #10#2;           || diario debe
    #2#1 = #10#3;           || diario haber
    #2#2 = #2#0 - #2#1;     || diario saldo
|ATRI I;
|PINTA 0639, #2#0;
|PINTA 0652, #2#1;
|PINTA 0665, #2#2;
|ATRI 0;
|FPRC;

|PROCESO entrada; |TIPO 0;              || desde el campo 'diario'
    ant_dia = #1#0;
    inkey = FSalida;
|HAZ asie_nue;
    xx = (FEntrada / 100) ! 0;
|SI xx = 1; |Y inkey ! 11;              || en <F3> no
    |HAZ contador;
        anter = #1#0;
    |SELECT #3#1;
    |LEE 001#1u;
    |SI FSalida ! 0;
        |DDEFECTO #1;
    |FINSI;
        #1#0 = anter;         || diario
        #1#2 = #20#1;         || documento
        #1#3 = #1#2;          || asiento
        #1#7 = 0;             || asiento predefinido
    |HAZ asie_nue;
    |SELECT #1#1;
|FINSI;
|SI inkey = 9;
    |PTEC 802;
    |PTEC 802;
    |PTEC 802;
    |PTEC 802;
    |PTEC 802;
|FINSI;
|SI inkey = 10;
    |PTEC 802;
    |PTEC 802;
    |PTEC 802;
|FINSI;
|SI inkey = 11;
    |SUB_EJECUTA "camemori.tab";
    |ERROR;
|FINSI;
|FPRC;

|PROCESO fecha; |TIPO 0;                || desde campo 'fecha'
    inkey = FSalida;

    p_imp = #1#1;
|HAZ pastilla;

|SI #1#1 < fec1; |O #1#1 > fec2;|O p_cen = 1;
    |MENAV mensa1;
    |ERROR;
    |ACABA;
|SINO;
    |SI inkey = 9;
        |PTEC 802;
        |PTEC 802;
        |PTEC 802;
        |PTEC 802;
    |FINSI;
    |SI inkey = 10;
        |PTEC 802;
        |PTEC 802;
    |FINSI;
    |SI inkey = 11;
        |SUB_EJECUTA "camemori.tab";
        |ERROR;
    |FINSI;
|FINSI;
|FPRC;

|PROCESO document; |TIPO 0;             || desde campo 'documento'
  inkey = FSalida;
  xx = (FEntrada / 100) ! 0;
  |SI xx = 1;
      #1#3 = #1#2;
      |PINTA #1#3;
  |FINSI;
  |SI inkey < 11;  |O inkey > 16;
      |ACABA;
  |FINSI;
  |SI inkey = 11;           || modificacion de asientos
      |ATRI P;  |MENSA "    Cargando Opcion ...";  |ATRI 0;
      ext1 = #1#0;          || diario viejo
      ext2 = #1#1;          || fecha viejo
      ext3 = #1#2;          || documento viejo
      |CIERRAT #0;
      |CIERRAT #1;
      |SUB_EJECUTA "camoddi0.tab";
      |ABRET #0;
      |ABRET #1;
      #1#0 = ext1;          || diario nuevo
      #1#1 = ext2;          || fecha nueva
      #1#2 = ext3;          || documento nuevo
      |PINTA #1#0;
      |PINTA #1#1;
      |PINTA #1#2;
      |SI sw_ivamerr ! 0;  |ERROR;  |FINSI;
      |ACABA;
  |FINSI;
  |SI xx ! 1;
      |MENAV "    Este tipo de contabilizacion esta limitado al ALTA ...";
      |ACABA;
  |FINSI;
  |ATRI P;  |MENSA "    Cargando Opcion ...";  |ATRI 0;
  ext1 = #1#0;
  ext2 = #1#1;
  ext3 = #1#2;
  |LEE 011#1=;
  |SI FSalida = 0;
      |MENAV "    ATENCION EL DOCUMETO YA EXISTE !! ";
      |ERROR;
      |ACABA;
  |FINSI;
  #1#0 = ext1;
  #1#1 = ext2;
  #1#2 = ext3;
  |CIERRAT #0;
  |CIERRAT #1;
  |SI inkey = 12;|O inkey = 13;           || iva reperc./soport. manuales
      |SI inkey = 12;   clau = "R";  |FINSI;
      |SI inkey = 13;   clau = "S";  |FINSI;
      |SUB_EJECUTA "camaasix.tab";
  |SINO;
      |SI inkey = 14;|O inkey = 15;       || cobros/pagos
          |SI inkey = 14;   clau = "C";  |FINSI;
          |SI inkey = 15;   clau = "P";  |FINSI;
          |SUB_EJECUTA "camaasim.tab";
      |SINO;
          clau = "I";                     || impagados inkey = 16
          |SUB_EJECUTA "camaasip.tab";
      |FINSI;
  |FINSI;
  |ABRET #0;
  |ABRET #1;
  |SI sw_ivamerr ! 0;  |ERROR;  |SINO;  |HAZ asie_nue;  |FINSI;
|FPRC;

|PROCESO enalta; |TIPO 7;          || desde campo 'asiento predefinido'
    xx = (FEntrada / 100) ! 0;
|SI xx = 1;
    |CAMPO_MODIFICA #1#7, 1, "S";
|SINO;
    |CAMPO_MODIFICA #1#7, 1, "N";
|FINSI;
|FPRC;

|PROCESO ver_guard; |TIPO 7;
     #1#7 = ult_pre; |PINTA #1#7;
|FPRC;

|PROCESO guard_pr; |TIPO 0
       ult_pre = #1#7;
|FPRC;

|PROCESO siesta; |TIPO 6;          || desde campo 'asiento predefinido'
  inkey = FSalida;
  |SI inkey = 1;|Y sw_ptec ! 0;  |ACABA;  |FINSI;  || ESC-ESC en repeticion
                                                   ||/desde 'ENCAMPO' 'camaasil'
  |SI inkey = 10; |Y sw_ptec ! 1;
      |ERROR;
      |SI sw_ptec = 2; |PTEC 802; |PTEC 807; |FINSI;
      |SI sw_ptec = 0;            |PTEC 807; |FINSI;
      |ACABA;
  |FINSI;

  xx = (FEntrada / 100) ! 0;
  predefine = 0;
  |ABRE #11;
  #11#0 = #1#7;
  |LEE 001#11=;
  |SI FSalida ! 0;
      |SI #1#7 = 0;
          |DDEFECTO #11;
          |GRABA 020#11n;       || graba la clave 'cero'
      |FINSI;
      |ATRI I;
      |PINTA 0928, #11#1;
      |ATRI 0;
      |CIERRA #11;
      |ACABA;
  |FINSI;
  |ATRI I;
  |PINTA 0928, #11#1;
  |ATRI 0;
  |SI #1#7 = 0;  |CIERRA #11;  |ACABA;  |FINSI;  ||NO ES OPERATIVO EL CERO
  |SI xx ! 1;  |CIERRA #11;  |ACABA;  |FINSI;    ||SOLO SI ESTAMOS EN ALTA
  |SI inkey ! 9;|Y inkey ! 10; || en <F1> presenta el asiento
      predefine = 1;           || en <F2> sube al diario
      predefine2 = 1;          ||/(cuando se esta a¤adiendo)
      |SI #11#2 = "S";
          |HAZ pide_iva;
      |FINSI;
  |SINO;
      |SI inkey = 9;               || presenta el asiento
          |HAZ consulta;
          |ERROR;
      |FINSI;
  |FINSI;
  |CIERRA #11;
|FPRC;

|PROCESO inicial; |TIPO 8;              || al principio del.run
  |HAZ inicio;
  |ABRE #10;
  #10#0 = 99;
  |LEE 040#10=;
  |SI FSalida = 0;  |Y #10#2 ! 0;  |Y #10#3 ! 0;
      |AVISO;
      |PUSHV 0501 2380;
      |D_CUADRO 1113,1466;
      |ATRI R;
      |PINTA 1214," ATENCION:  Esta empresa puede estar cerrada, tiene ";
      |PINTA 1314,"            movimientos en el diario 99             ";
      |ATRI r;
      |AVISO;
      |PAUSA;
      |POPV;
  |FINSI;
  |CIERRA #10;
  |ABRE #4;
  |LEE 101#4u;
  |SI FSalida ! 0;
      |DDEFECTO #4;
      #4#0 = 1;
  |SINO;
      #4#0 = #4#0 + 1;
  |FINSI;
  |GRABA 101#4n;
  codcon = #4#0;
  |LIBERA #4;
  |CIERRA #4;
|FPRC;

|PROCESO termino; |TIPO 9;         || al final del.run
  |SI modcon = 0;
      |ABRET #4;
      |DDEFECTO #4;
      #4#0 = codcon;
      |LEE 040#4=;
      |BORRA 101#4a;
      |CIERRAT #4;
  |SINO;
      |CORRE "camaapua.tab"
  |FINSI;
|FPRC;

|PROCESO descuadre; |TIPO 3;
|SI #1#6 ! 0;
        avisos = "AVISO!!! Asiento Descuadrado";
    |HAZ aviso5;
    |PAUSA;
    |HAZ aviso9;
|FINSI;
|FPRC;

****************************************************************************
                    RUTINAS VARIAS
****************************************************************************

|PROCESO asie_nue;            || calculo 'entrada' /prepara un nuevo asiento
|DDEFECTO #15;
|DDEFECTO #16;
    #1#4 = 0;       || asiento debe
    #1#5 = 0;       || asiento haber
    #1#6 = 0;       || asiento saldo
|PINTA #1#4;
|PINTA #1#5;
|PINTA #1#6;
    coant = 0;
    deant = "";
    dhant = "";
    imant = 0;
    dpant = "";
    predefine2 = 0;
    sw_debe = 0;
    sw_habe = 0;
    j_d = 0;
    j_h = 0;
    ctab1 = "";
    ctab2 = 0;
|HAZ el_diario;
|FPRC;

|PROCESO aviso5;         || desde calculo 'c_importe' (camaasil)
|PUSHV 09101171;
|HAZ color_f;
|CUADRO 09 10 11 71;
||            123456789012345678901234567890123456789012345678901234567890
|HAZ color_c;

|QBF avisos;
    alfa1 = avisos % 0;  nume1 = alfa1;
    nume2 = ((60 - nume1) / 2) ! 0;     nume1 = 60 - nume1 - nume2;
    avisos = (" " * nume1) + avisos + (" " * nume2);        || lo centra

|PINTA 1011, avisos;
|ATRI 0;
|AVISO;
|FPRC;

|PROCESO aviso9;         || desde calculo 'descuadre', 'modbaj'
|ATRI 0;
|POPV;
|FPRC;

|PROCESO color_c;        || activa el color caracter en avisos
|COLOR 14, 0;
|ATRI 0;
|FPRC;

|PROCESO color_f;        || activa el color graficos en avisos
|COLOR 4, 0;
|ATRI 0;
|FPRC;

|PROCESO pide_iva;       || desde calculo 'siesta' / pide datos ivas
    ext1 = #1#1;        || fecha del asiento
    ext2 = #11#4;       || libro iva
|SI #11#3 = "R";  |SUB_EJECUTA "camaasiv.tab";  |FINSI;
|SI #11#3 = "S";  |SUB_EJECUTA "camaasiw.tab";  |FINSI;
||    ext4 = "  ";
||    ext5 = "7842";
    nume1 = ext3;
|SI nume1 ! 0;                || esto es el FSalida
        predefine = 0;
        predefine2 = 0;
    |ERROR;
|SINO;
        i_serie = ext4;       || (inhibida)
        i_factu = ext5;
        i_fecha = ext1;
    ||SI sw_ptec = 0; |PTEC 802; |FINSI;   || cuando se provoca otro predef.
        sw_ptec = 0;                      ||/este 'PTEC' sobra
|FINSI;
|FPRC;

|PROCESO fuera_cob;           || desde defbucle 'cobros'
|BORRA 020#21a;
|FPRC;

|DEFBUCLE cobros;             || desde subrutina 'bor_cobros'
#21#1;
;
#15#0, #15#2, #15#27,   1;
#15#0, #15#2, #15#27, 999;
e;
NULL, fuera_cob;
|FIN;

|PROCESO bor_cobros;          || desde rutina 'a_busca' (camaasil)
|ABRE #21;
|BUCLE cobros;
|CIERRA #21;
|FPRC;

|PROCESO fuera_pag;           || desde defbucle 'pagos'
|BORRA 020#22a;
|FPRC;

|DEFBUCLE pagos;              || desde subrutina 'bor_pagos'
#22#1;
;
#16#0, #16#2, #16#27,   1;
#16#0, #16#2, #16#27, 999;
e;
NULL, fuera_pag;
|FIN;

|PROCESO bor_pagos;           || desde rutina 'a_busca' (camaasil)
|ABRE #22;
|BUCLE pagos;
|CIERRA #22;
|FPRC;

|PROCESO borraextra;
 |BORRA 020#25a;
 |LIBERA #25;
|FPRC;

|PROCESO extraco;        || desde calculo 'modbaj'  /borra anotaciones
|ABRE #23;
    #23#0 = #0#0;
    #23#1 = #0#1;
    #23#2 = #0#2;
|LEE 001#23=;
|SI FSalida = 0;
    |BUCLELIN 0borraextra#23;
    |BORRA 020#23a;
|FINSI;
|CIERRA #23;
|FPRC;

|PROCESO situate;        || desde rutina 'consulta'
|CAMPO_POSICION #13#0,1,0847;
|CAMPO_POSICION #13#1,1,0860;
|FPRC;

|PROCESO correte;        || desde subrutina 'consulta2'
|CAMPO_POSICION #13#0,0,posic; posic=posic+m_c; |CAMPO_POSICION #13#0,1,posic;
|CAMPO_POSICION #13#1,0,posic; posic=posic+m_c; |CAMPO_POSICION #13#1,1,posic;
|FPRC;

|PROCESO consulta2;      || desde rutina 'consulta' /BUCLELIN
    lin_con = lin_con + 1;
|SI #12#2 = "D";  #13#0 = #12#3;  #13#1 = "     --     ";  |FINSI;
|SI #12#2 = "H";  #13#0 = "     --     ";  #13#1 = #12#3;  |FINSI;
|PINDA #0#13;
|SI lin_con < 12;
        m_c = 100;
|SINO;
        m_c = -1100;
        lin_con = 0;
    |PAUSA;
    |PINPA #0#13;
|FINSI;
|HAZ correte;
|FPRC;

|PROCESO consulta;       || desde calculo 'siesta' /presenta predefinido
|HAZ situate;
|PUSHV 05451974;
|PUSHV 19292374;
    lin_con = 0;
|DDEFECTO #13;
|PINPA #0#13;
|PINPA #1#13;
|SI #11#2 = "S";
    |SI #11#3 = "R";  #13#2 = "IVA REPERCUTIDO";  |FINSI;
    |SI #11#3 = "S";  #13#2 = "IVA SOPORTADO  ";  |FINSI;
    |SI #11#3 = "R";|Y #11#6 = "S";  #13#3 = "/COBROS";  |FINSI;
    |SI #11#3 = "S";|Y #11#6 = "S";  #13#3 = "/PAGOS ";  |FINSI;
|FINSI;
|PINDA #1#13;
|BUCLELIN 2consulta2#11;
|SI lin_con = 0;
    |MENAV "    ATENCION!!! Cabecera sin lineas ...";
|SINO;
    |PAUSA;
|FINSI;
|POPV;
|POPV;
|FPRC;

|PROCESO cargcon;
   |SI #0#8 = "D";
       |SI j_d = 0; j_d = #0#3; |FINSI;
   |SINO;
       |SI j_h = 0; j_h = #0#3; |FINSI;
   |FINSI;
   |SI j_d ! 0; |Y j_h ! 0; |ERROR; |FINSI;
|FPRC;

|PROCESO carga_con;
  sw_debe = 0; j_d = 0;
  sw_habe = 0; j_h = 0;
  |SI #1#8  ! doce; sw_debe = 1; |FINSI;
  |SI #1#10 ! doce; sw_habe = 1; |FINSI;
  |SI sw_debe = 0; |Y sw_habe = 0; |ACABA; |FINSI;
  |BUCLELIN 2cargcon#1;
|FPRC;
