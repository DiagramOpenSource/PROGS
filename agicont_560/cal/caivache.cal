|FICHEROS;
  caivache #0;
  cacuenta #1;
  camaasil #2; ||Asientos.
  caivasop #3; ||Iva Sopordado.
  caivarep #4; ||Iva Repercutido.
|FIN;

|VARIABLES;
  vic = 0;
  cta = 0;
  long = "";
  sw = 0;
  alfa1 = "";
  swvale = 1;
  importe = 0;
  importd = 0;
  importh = 0;
  informe = "caivache";
  &tipo = 0;
  swimp = 0;
|FIN;

|INCLUYE acceso_i;
|INCLUYE puntos_i;

|PROCESO mayusc;
   alfa1 = #0Campo > " ";
   #0Campo = alfa1;
   |PINTA #0Campo;
|FPRC;

|PROCESO recoge; |TIPO 7;
  #0#2 = fec1;
  #0#3 = fec2;
  |PINTA #0#2;
  |PINTA #0#3;
|FPRC;

|PROCESO cuenta; |TIPO 6;
  p_alfa1 = #0Campo;
  salidas = FSalida;
  |HAZ punto;
  |SI p_error ! 0;  |ERROR;  |ACABA;  |FINSI;
  |SI #0#0 ! "            ";
      cta  = #0#0;
      long = cta % 0;
      sw = 0;
      |SI long = #30#14; sw = 1; #0#1 = 1; |FINSI;
      |SI long = #30#15; sw = 1; #0#1 = 2; |FINSI;
      |SI long = #30#16; sw = 1; #0#1 = 3; |FINSI;
      |SI long = #30#17; sw = 1; #0#1 = 4; |FINSI;
      |SI long = #30#18; sw = 1; #0#1 = 5; |FINSI;
      |SI long = #30#19; sw = 1; #0#1 = 6; |FINSI;
      |SI sw = 0;
          |MENAV "    Longitud de Cuenta fuera de Nivel ...";
          |ERROR;
      |FINSI;
  |SINO;
      |MENAV "    Error de Entrada ...";
      |ERROR;
  |FINSI;
|FPRC;

|PROCESO cuefin; |TIPO 0;
  |ABRE #1;
  #1#0 = #0#0;
  #1#1 = #0#1;
  |LEE 001#1=;
  |SI #1#3 = "N";|Y FSalida = 0;
      |MENAV "    Cuenta sin Pase Directo ...";
      |ERROR;
  |FINSI;
  |CIERRA #1;
|FPRC;

|PROCESO fechas; |TIPO 0;
  |SI #0Campo > fec2;|O #0Campo < fec1;
      |MENAV "    Fecha fuera de Periodo ...";
      |ERROR;
  |FINSI;
|FPRC;

|PROCESO caivarep;
     |PARA vic = 1; |SI vic [ #0#6; |HACIENDO vic = vic + 1;
          importe = #2#9 * vic;
          importd = importe - #0#5;
          importh = importe + #0#5;
          |SI #4#26 ] importd; |Y #4#26 [ importh;
               swvale = 1;
               tipo = 2;
               |PRINT;
               |SAL;
          |FINSI;
     |SIGUE;
|FPRC;


|DEFBUCLE caivarep;
     #4#3;
;
0 ,#0#2,000000,"  ";
99,#0#3,999999,"zz";
;
NULL,caivarep;
|FIN;

|PROCESO caivasop;
     |PARA vic = 1; |SI vic [ #0#6; |HACIENDO vic = vic + 1;
          importe = #2#9 * vic;
          importd = importe - #0#5;
          importh = importe + #0#5;
          |SI #3#26 ] importd; |Y #3#26 [ importh;
               swvale = 1;
               tipo = 1;
               |PRINT;
               |SAL;
          |FINSI;
     |SIGUE;
|FPRC;


|DEFBUCLE caivasop;
     #3#3;
;
0 ,#0#2,000000,"  ";
99,#0#3,999999,"zz";
;
NULL,caivasop;
|FIN;

|PROCESO camaasil;
     swvale = 0;
     tipo = 0;
     |PRINT;
     swimp  = 1;
     |SI #0#4 = "S"; |O #0#4 = "A";
          |BUCLE caivasop;
     |FINSI;
     |SI #0#4 = "R"; |O #0#4 = "A";
          |BUCLE caivarep;
     |FINSI;
     |SI swvale = 0;
          tipo = 3;
          |PRINT;
     |FINSI;
|FPRC;

|DEFBUCLE camaasil;
     #2#3;
;
#0#0,#0#1,#0#2,000000;  || cuenta , digito , fecha , documento
#0#0,#0#1,#0#3,999999;
;
NULL,camaasil;
|FIN;

|PROCESO engancha; |TIPO 3;
     |IMPRE 0;
     |SI FSalida = 0;
          |INFOR informe;
          |SI FSalida = 0;
               |BUCLE camaasil;
               |SI swimp ! 0;
                    |PIEINF;
               |FINSI;
               |FININF;
          |FINSI;
          |FINIMP;
     |FINSI;
|FPRC;
