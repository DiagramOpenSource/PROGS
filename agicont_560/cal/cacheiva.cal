|FICHEROS;
    cacheiva #0;
    cacuenta #1;    || cuentas
    camaasic #2;    || apuntes cabs.
    camaasil #3;    || apuntes lins.
    caivatmp #4;    || iva temporal
    caivarep #5;    || iva repercutido
    caivasop #6;    || iva soportado
    calibros #7;    || libros
|FIN;

|VARIABLES;
    alfa1 = "";
    alfa2 = "";
    alfa3 = "";
    alfa4 = "";
    nume1 = 0;
    nume2 = 0;
    nume3 = 0;
    nume4 = 0;
    para1 = 0;
    para2 = 0;
    para3 = 0;
    para4 = 0;
    fech1 = @;
    fech2 = @;
    fech3 = @;
    fech4 = @;
    elsw1 = 0;
    FEstado = 0;
    long = 0;
    sw = 0;
    &tipo = "";
    &fec_ant = @;
    &difa = 0;
    &acua = 0;
    &acui = 0;
    &libi = 0;
    &frai = 0;
    &desi = "";
    &impi = 0;
    &sw_lin = 0;
    sw_iva = 0;
    sw_nod = 0;
    repetir = 0;
    sw_eof = 0;
    des_signo = "";
    has_signo = "";
    sw_asegura = 0;
    &sw_ruptura = 0;
    &dia = @;
    d_cuen = "";
    h_cuen = "";
    d_digi = 0;
    h_digi = 0;
    primero = 0;
    mayor1 = "";
    ord = 0;
|FIN;

|INCLUYE acceso_i;
|INCLUYE puntos_i;

*****************************************************************************
                       CALCULOS DESDE CAMPOS
*****************************************************************************

|PROCESO mayor;
  mayor1 = #0Campo > " ";
  #0Campo = mayor1;
  |PINTA #0Campo;
|FPRC;

|PROCESO asterisco;
  elsw1 = 0;
  |PARA para1 = 1; |SI para1 [ 12; |HACIENDO para1 = para1 + 1;
        nume1 = (para1 * 100) + 1;
        alfa1 = #0#1 % nume1;
        |SI alfa1 = "*";
            elsw1 = para1;
        |FINSI;
  |SIGUE;
|FPRC;

|PROCESO cuenta; |TIPO 6;
  |HAZ asterisco;
  |SI elsw1 ! 0;  |ACABA;  |FINSI;
  salidas = FSalida;
  p_alfa1 = #0Campo;
  |HAZ punto;
  |SI p_error ! 0;  |ERROR;  |ACABA;  |FINSI;
  alfa1 = #0#1;
  |QBF alfa1;
  alfa1 = alfa1 % 0;
  long = alfa1;
  sw   = 0;
  #0#2 = 0;  || inicializa el digito
  |SI long = dig4; |Y dig4 ! 0; sw = 1; #0#2 = 1; |FINSI;
  |SI long = dig5; |Y dig5 ! 0; sw = 1; #0#2 = 2; |FINSI;
  |SI long = dig6; |Y dig6 ! 0; sw = 1; #0#2 = 3; |FINSI;
  |SI long = dig7; |Y dig7 ! 0; sw = 1; #0#2 = 4; |FINSI;
  |SI long = dig8; |Y dig8 ! 0; sw = 1; #0#2 = 5; |FINSI;
  |SI long = dig9; |Y dig9 ! 0; sw = 1; #0#2 = 6; |FINSI;
  |SI sw = 0;|Y long ! 0;
      |MENAV "    Longitud de Cuenta Fuera de Nivel !";
      |ERROR;
  |FINSI;
|FPRC;

|PROCESO cuefin; |TIPO 0;
  |SI #0#1 = "            ";
      |MENAV "    Error de Entrada ...";
      |ERROR;
      |ACABA;
  |FINSI;
  |HAZ asterisco;
  |SI elsw1 = 0;
      |ABRE #1;
      |SELECT #1#1;
      #1#0 = #0#1;
      #1#1 = #0#2;
      |LEE 001#1=;
      FEstado = FSalida;
      |SI FEstado ! 0;
          |DDEFECTO #1;
      |FINSI;
      |ATRI I;
      |PINTA 1438, #1#2;
      |ATRI 0;
      |SI FEstado = 0;|Y #1#3 = "N";
          |MENAV "    Cuenta sin pase directo ...";
          |ERROR;
          |DDEFECTO #1;
      |FINSI;
      |CIERRA #1;
      |HAZ asegura;
  |FINSI;
  |SI #0#11 = "R";
      #1#5 = "P";
  |FINSI;
|FPRC;

|PROCESO favor; |TIPO 7;
  |SI #0#11 = "R";  #0#1 = "477*";  |FINSI;
  |SI #0#11 = "S";  #0#1 = "472*";  |FINSI;
  |PINTA #0#1;
  |PTEC 816;
|FPRC;

|PROCESO asegura; |TIPO 0;
  sw_asegura = 0;
  alfa1 = #0#1 % 103;
  |SI alfa1 = "472";|Y #0#11 = "R";  sw_asegura = 1;  |FINSI;
  |SI alfa1 = "477";|Y #0#11 = "S";  sw_asegura = 1;  |FINSI;
  |SI sw_asegura = 1;
      |MENAV "    Esta Seguro ? ";
  |FINSI;
|FPRC;

|PROCESO fech_bien; |TIPO 0;
  |SI #0Campo < fec1; |O #0Campo > fec2;
      |MENAV "    Fecha fuera de Periodo ...";
      |ERROR;
  |FINSI;
|FPRC;


|PROCESO fech_defe; |TIPO 7;
  |SI FSalida ! 0;  |ACABA;  |FINSI;
  #0#12 = fec1;
  #0#13 = fec2;
  |PINTA #0#12;
  |PINTA #0#13;
|FPRC;

|PROCESO libro_bien; |TIPO 0;
  |SI #0Campo ! 0;|Y FSalida ! 2;
      |SI #7#2 = "R";|Y #0#11 = "S";
          |MENAV "    ATENCION!!! Libro de I.V.A. Repercutido ...";
          |ERROR;
      |FINSI;
      |SI #7#2 = "S";|Y #0#11 = "R";
          |MENAV "    ATENCION!!! Libro de I.V.A. Soportado ...";
          |ERROR;
      |FINSI;
  |FINSI;
|FPRC;

*****************************************************************************
                       CALCULOS DE IMPRESION
*****************************************************************************

|PROCESO ivas2;
  nume1 = #4#14 + #4#15 + #4#16 + #4#56 + #4#57;     || cuotas iva
  nume2 = #4#20 + #4#21 + #4#22 + #4#60 + #4#61;     || cuotas recargo
  nume3 = 0;
  |SI #0#4 = 1;  nume3 = nume1 + nume2;   |FINSI;   || incluido recargo
  |SI #0#4 = 2;  nume3 = nume1;           |FINSI;   || no incluido
  |SI #0#4 = 3;  nume3 = nume2;           |FINSI;   || cta. recargo
  acui = acui + nume3;
  impi = nume3;
  libi = #4#0;
  frai = #4#2;
  desi = #4#7;
  sw_lin = 0;
  |SI #0#0 = "R";  |PRINT;  |FINSI;
  libi = 0;
  frai = 0;
  impi = 0;
  desi = "";
|FPRC;


|PROCESO ivas1;
  sw_iva = 0;
  sw_nod = 0;
  |SI primero = 0;
      |LEE 001#4=;
  |SINO;
      |LEE 001#4s;
  |FINSI;
  FEstado = FSalida;
  |SI FEstado = 0;
      |SI #0#14=2;|Y #4#23="N"; sw_nod=1; |FINSI;  || no deduc. no incluido
      |SI #0#14=3;|Y #4#23="S"; sw_nod=1; |FINSI;  || no deduc. solamente
      |SI ord = 1;
          |SI #4#3 = dia;|Y sw_nod = 0;
              |HAZ ivas2;
              sw_iva = 1;
              primero = 1;
          |FINSI;
      |SINO;
          |SI #4#46 = dia;|Y sw_nod = 0;
              |HAZ ivas2;
              sw_iva = 1;
              primero = 1;
          |FINSI;
      |FINSI;
  |FINSI;

  |SI sw_iva = 0; |Y sw_eof = 0;          || imprime apunte aunque no hay iva
      |SI #0#0 = "R";  |PRINT;  |FINSI;
  |FINSI;
|FPRC;

|PROCESO ivas0;
  acua = 0;
  acui = 0;
  |DDEFECTO #4;
  |SI ord = 1; #4#3   = dia;  |FINSI;         || fecha factura
  |SI ord = 2; #4#46  = dia;  |FINSI;         || fecha factura
  #4#0 = 0;            || libro
  #4#2 = 0;            || fra.
  |LEE 000#4];
  |SI FSalida = 0;|Y primero = 1;  |LEE 001#4a;   |FINSI;        || se situa
|FPRC;

|PROCESO tot_fecha;
  difa = acua - acui;
  sw_lin = 1;
  |PRINT;                   || imprime total fecha
  sw_lin = 0;
|FPRC;

|PROCESO apuntes2;
  |SI #1#5 = "A";
      |SI #3#8 = "D";  acua = acua + #3#9;  |FINSI;
      |SI #3#8 = "H";  acua = acua - #3#9;  |FINSI;
  |FINSI;
  |SI #1#5 = "P";
      |SI #3#8 = "D";  acua = acua - #3#9;  |FINSI;
      |SI #3#8 = "H";  acua = acua + #3#9;  |FINSI;
  |FINSI;
|FPRC;

|PROCESO apuntes1;
  #2#0 = #3#0;
  #2#1 = #3#1;
  #2#2 = #3#2;
  |LEE 001#2=;        || sigue si la cabecera existe
  |SI FSalida ! 0;  |ACABA;  |FINSI;
  |SI fec_ant = "01.01.0001";
      fec_ant = #3#1;
      |HAZ ivas0;
  |FINSI;
  |HAZ apuntes2;
  |HAZ ivas1;
|FPRC;

|DEFBUCLE apuntes;
 #3#1;
 4, 5, 8;
  0, dia,      0,    0, d_cuen, d_digi, des_signo;
 99, dia, 999999, 9999, h_cuen, h_digi, has_signo;
 e;
 NULL, apuntes1;
 #3#1, #3#0, #3#2, #3#3;
|FIN;

|| -----------------------------------------------------------------------
|PROCESO tmp_sopor1;
  |SI #6#0=#0#5;  |O #6#0=#0#6;  |O #6#0=#0#7;  |O #6#0=#0#8;
                  |O #6#0=#0#9;  |O #6#0=#0#10;
      #4#0  = #6#0;   #4#1  = #6#1;   #4#2  = #6#2;   #4#3  = #6#3;
      #4#4  = #6#4;   #4#5  = #6#5;   #4#6  = #6#6;   #4#7  = #6#7;
      #4#8  = #6#8;   #4#9  = #6#9;   #4#10 = #6#10;  #4#11 = #6#11;
      #4#12 = #6#12;  #4#13 = #6#13;  #4#14 = #6#14;  #4#15 = #6#15;
      #4#16 = #6#16;  #4#17 = #6#17;  #4#18 = #6#18;  #4#19 = #6#19;
      #4#20 = #6#20;  #4#21 = #6#21;  #4#22 = #6#22;  #4#23 = #6#23;
      #4#24 = #6#24;  #4#25 = #6#25;  #4#26 = #6#26;  #4#27 = #6#27;
      #4#28 = #6#28;  #4#29 = #6#29;  #4#30 = #6#30;  #4#31 = #6#31;
      #4#32 = #6#32;  #4#33 = #6#33;  #4#34 = #6#34;  #4#35 = #6#35;
      #4#36 = #6#36;  #4#37 = #6#37;  #4#38 = #6#38;  #4#39 = #6#39;
      #4#40 = #6#40;  #4#41 = #6#41;  #4#42 = #6#42;  #4#43 = #6#43;
      #4#44 = #6#44;  #4#45 = #6#45;  #4#46 = #6#46;  #4#47 = #6#47;
      #4#48 = #6#48;  #4#49 = #6#49;  #4#50 = #6#50;  #4#51 = #6#51;
      #4#52 = #6#52;  #4#53 = #6#53;  #4#54 = #6#54;  #4#55 = #6#55;
      #4#56 = #6#56;  #4#57 = #6#57;  #4#58 = #6#58;  #4#59 = #6#59;
      #4#60 = #6#60;  #4#61 = #6#61;
      |GRABA 020#4n;
  |FINSI;
|FPRC;

|DEFBUCLE tmp_sopor;
 #6#1;
 ;
 00,      0, "  ";
 99, 999999, "zz";
 e;
 NULL, tmp_sopor1;
|FIN;

|PROCESO tmp_reper1;
  |SI #5#0=#0#5;  |O #5#0=#0#6;  |O #5#0=#0#7;  |O #5#0=#0#8;
                  |O #5#0=#0#9;  |O #5#0=#0#10;
      #4#0  = #5#0;   #4#1  = #5#1;   #4#2  = #5#2;   #4#3  = #5#3;
      #4#4  = #5#4;   #4#5  = #5#5;   #4#6  = #5#6;   #4#7  = #5#7;
      #4#8  = #5#8;   #4#9  = #5#9;   #4#10 = #5#10;  #4#11 = #5#11;
      #4#12 = #5#12;  #4#13 = #5#13;  #4#14 = #5#14;  #4#15 = #5#15;
      #4#16 = #5#16;  #4#17 = #5#17;  #4#18 = #5#18;  #4#19 = #5#19;
      #4#20 = #5#20;  #4#21 = #5#21;  #4#22 = #5#22;  #4#23 = #5#23;
      #4#24 = #5#24;  #4#25 = #5#25;  #4#26 = #5#26;  #4#27 = #5#27;
      #4#28 = #5#28;  #4#29 = #5#29;  #4#30 = #5#30;  #4#31 = #5#31;
      #4#32 = #5#32;  #4#33 = #5#33;  #4#34 = #5#34;  #4#35 = #5#35;
      #4#36 = #5#36;  #4#37 = #5#37;  #4#38 = #5#38;  #4#39 = #5#39;
      #4#40 = #5#40;  #4#41 = #5#41;  #4#42 = #5#42;  #4#43 = #5#43;
      #4#44 = #5#44;  #4#45 = #5#45;  #4#46 = #5#46;  #4#47 = #5#47;
      #4#48 = #5#48;  #4#49 = #5#49;  #4#50 = #5#50;  #4#51 = #5#51;
      #4#52 = #5#52;  #4#53 = #5#53;  #4#54 = #5#54;  #4#55 = #5#55;
      #4#56 = #5#56;  #4#57 = #5#57;  #4#58 = #5#58;  #4#59 = #5#59;
      #4#60 = #5#60;  #4#61 = #5#61;
      |GRABA 020#4n;
  |FINSI;
|FPRC;

|DEFBUCLE tmp_reper;
 #5#1;
 ;
 00,      0, "  ";
 99, 999999, "zz";
 e;
 NULL, tmp_reper1;
|FIN;
|| ------------------------------------------------------------------------

|PROCESO desde_hasta;
  d_cuen = #0#1;  h_cuen = #0#1;      || desde/hasta cuenta
  d_digi = #0#2;  h_digi = #0#2;      || desde/hasta digito
  |HAZ asterisco;
  |SI elsw1 = 0;  |ACABA;  |FINSI;
  nume1 = 99 + elsw1;
  alfa1 = #0#1 % nume1;
  d_cuen = alfa1 + "            ";     d_cuen = d_cuen % 112;
  h_cuen = alfa1 + "999999999999";     h_cuen = h_cuen % 112;
  d_digi = 0;
  h_digi = 9;
|FPRC;

|PROCESO chequea; |TIPO 3;
  ord = #0#15;
  |IMPRE 0;
  |SI FSalida ! 0;  |ACABA;  |FINSI;
  |INFOR "cacheiva";
  |SI FSalida ! 0;  |FINIMP;  |ACABA;  |FINSI;
  tipo = #0#1;
  |ABRE #4;
  |CIERRA #4;
  |DELINDEX #4;
  |ABRE #2;
  |ABRE #4;
  |SI ord = 2; |SELECT #2#4; |FINSI;  || .... Fecha Contable
  |ATRI P;  |PINTA 2138, "<TEMPORAL>";  |ATRI 0;
  |HAZ desde_hasta;
  |SI #0#11 = "R";  |BUCLE tmp_reper;  |FINSI;
  |SI #0#11 = "S";  |BUCLE tmp_sopor;  |FINSI;
  |SI #0#3 = "D";  des_signo = "D";  has_signo = "D";  |FINSI;
  |SI #0#3 = "H";  des_signo = "H";  has_signo = "H";  |FINSI;
  |SI #0#3 = "A";  des_signo = " ";  has_signo = "z";  |FINSI;
  primero = 0;
  |PARA dia = #0#12; |SI dia [ #0#13; |HACIENDO dia = dia + 1;
        |ATRI I;  |PINTA 2138, dia;  |ATRI 0;
        fec_ant = "01.01.0001";
        sw_eof = 0;
        |BUCLE apuntes;
        sw_eof = 1;
        sw_ruptura = 1;        || no imprime datos del apunte en informe
        |HAZ ruptura;
        sw_ruptura = 0;
  |SIGUE;
  |PIEINF;
  |FININF;
  |CIERRA #2;
  |CIERRA #4;
  |DELINDEX #4;
  |FINIMP;
|FPRC;

|PROCESO ruptura;

  |SI fec_ant = "01.01.0001";   || si no hay apuntes
      |HAZ ivas0;               || se situa en el iva
  |FINSI;

  FEstado = 0;
  |SI ord = 1; #4#3  = dia; |FINSI;
  |SI ord = 2; #4#46 = dia; |FINSI;
  |SI ord = 1;
     |PARA; |SI FEstado = 0; |Y #4#3 [ dia;  |HACIENDO;
            |HAZ ivas1;               || sigue imprimiendo ivas
     |SIGUE;
  |SINO;
     |PARA; |SI FEstado = 0; |Y #4#46 [ dia;  |HACIENDO;
            |HAZ ivas1;               || sigue imprimiendo ivas
     |SIGUE;
  |FINSI;
  |SI acui ! 0; |O acua ! 0;
      |HAZ tot_fecha;           || totaliza
  |FINSI;
|FPRC;
