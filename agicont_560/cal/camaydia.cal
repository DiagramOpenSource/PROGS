|FICHEROS;
  camaydia #0;
  camaycab #1;
  camaylin #2;
  cacuenta #4;

  cabucem1 #100;
|FIN;

|VARIABLES;
    &entrada = 1;
 sw = 0;
   fecalf = "";
   men1   = "    NO EXISTE EMPRESA";
   mensa1 = "    ERROR ESTA FECHA NO CORRESPONDE AL EJERCICIO";
   traba1 = "";
   asien = 0;
   varalf = "";
   &a_debe = 0;
   &a_haber = 0;
   &tde = 0;
   &tha = 0;
   &hoja = 0;
   d = "D";
   h = "H";
   informe = "camaydia";
   &sumay = "";
   &swlin = 0;


   Comodin  = "";
   Comodin1 = "";
   Numeracion = 0;
   aAlfa = "";
   Calc  = 0;
   Handle = 0;
|FIN;

|INCLUYE acceso_i;

|| *************************************************************************
||                          MEOLLO
|| *************************************************************************

|PROCESO contar;
  |SI #1#1 < #0#0;
      tde = tde + #1#4;
      tha = tha + #1#5;
  |FINSI;
|FPRC;

|PROCESO porimpre;
  |ABRE #4;
  #4#0 = #2#4;
  #4#1 = #2#5;
  |LEE 040#4=;
  |SI FSalida ! 0;
      |DDEFECTO #4;
  |FINSI;
  |CIERRA #4;
  |SI #2#8 = "D";
      a_debe = #2#9;
      a_haber = 0;
  |SINO;
      a_debe = 0;
      a_haber = #2#9;
  |FINSI;
  |PRINT;
|FPRC;


|PROCESO bucleimp;
  |SI sw = 0;
      asien = #1#2;
      sw = 1;
  |FINSI;
  |SI asien ! #1#2;
      sumay = "SUMA Y SIGUE";
      swlin = 1;
      |PIEINF;
      asien = #1#2;
  |FINSI;
  |BUCLELIN 0porimpre#1;
|FPRC;

|DEFBUCLE camaydia0;
#1#2;
;
     1, fec1, 00, 000001;
999999, #0#0, 99, 999999;
be;
NULL,contar;
|FIN;

|DEFBUCLE camaydia1;
#1#2;
;
     1, #0#0, 00, 000001;
999999, #0#1, 99, 999999;
be;
NULL,NULL,NULL,NULL,NULL,bucleimp;
#1#1,#1#3,#1#0,#1#2;
|FIN;

|PROCESO Empresas;
   #48#2 = #cabucem1#0;
   #48#3 = #cabucem1#1;
   |LEE 000#48=;
   |SI FSalida = 0;
      |DBASE Comodin; |QBF Comodin;
      Comodin = Comodin + "fich/" + (("00000" + #cabucem1#0) % -105) + #cabucem1#1 + "/";
      |PATH_DAT #0 Comodin; |PATH_DAT #1 Comodin;
      |PATH_DAT #2 Comodin; |PATH_DAT #4 Comodin;
      |DDEFECTO #camaydia;
      Calc = #48#26;
      |SI Calc ] 80;
         Calc = Calc + 1900;
      |SINO;
         Calc = Calc + 2000;
      |FINSI;
      Comodin = ("00" + #48#11) % -102;
      Comodin = "01." + Comodin + "." + Calc;
      #camaydia#0 = Comodin;
      Comodin = ("00" + #48#12) % -102;
      Comodin = "31." + Comodin + "." + Calc;
      #camaydia#1 = Comodin;
      hoja = 0;
      Impresora = "?c:\tmp1,,,?Mayor";
      |IMPRE 0;
      |SI FSalida ! 0;  |ACABA;  |FINSI;
      |INFOR informe;
      |SI FSalida ! 0;  |FINIMP;  |ACABA;  |FINSI;
      |BUCLE camaydia0;
      |BUCLE camaydia1;
      sumay = "SUMA TOTAL";
      swlin = 0;
      |PIEINF;
      |FININF;
      |FINIMP;

      |DBASE Comodin1; |QBF Comodin1;
      Comodin1 = Comodin1 + "hacienda/PresLibr/" + (("00000" + #cabucem1#0) % -105) + #cabucem1#1 + "/";
      Comodin1 = Comodin1 + "MAYOR_*.xls";
      |_PDIR Comodin1;
      |SI FSalida = 0;
         |PARA; |SI FSalida = 0; |HACIENDO;
             Comodin = Comodin1 % -107; Comodin = Comodin % 0103;
             Numeracion = Comodin; Numeracion = Numeracion + 1;
             |_SDIR Comodin1;
         |SIGUE;
      |SINO;
         Numeracion = 1;
      |FINSI;

      Comodin = "c:/tmp1.xls";
      |XABRE "C", Comodin, Handle;
      |SI FSalida ] 0;
         |XCIERRA Handle;
         |DBASE Comodin1; |QBF Comodin1;
         Comodin1 = Comodin1 + "hacienda/PresLibr/";
         |MKDIR Comodin1;
         Comodin1 = Comodin1 + (("00000" + #cabucem1#0) % -105) + #cabucem1#1 + "/";
         |MKDIR Comodin1;
         Comodin1 = Comodin1 + "MAYOR_" + (("000" + Numeracion) % -103) + ".xls";
         |IP_REMOTO aAlfa; |QBF aAlfa;
         |SI aAlfa ! "";
            |RECIBE_FICHERO Comodin, Comodin1;
         |SINO;
            |COPIA_FICHERO Comodin, Comodin1;
         |FINSI;
         FSalida = 1;

         |PARA; |SI FSalida ! 0; |HACIENDO;
            |RFBORRA Comodin;
            |SI FSalida ! 0;
               |MENAV "    Termine la tarea de Excel que tiene activa";
            |FINSI;
         |SIGUE;
      |SINO;
         |FABRE Comodin;
         |SI FSalida ] 0;
            FSalida = Handle;
            |FCIERRA FSalida;
            |DBASE Comodin1; |QBF Comodin1;
            Comodin1 = Comodin1 + "hacienda/PresLibr/";
            |MKDIR Comodin1;
            Comodin1 = Comodin1 + (("00000" + #cabucem1#0) % -105) + #cabucem1#1 + "/";
            |MKDIR Comodin1;
            Comodin1 = Comodin1 + "MAYOR_" + (("000" + Numeracion) % -103) + ".xls";
            |COPIA_FICHERO Comodin, Comodin1;
            FSalida = 1;

            |PARA; |SI FSalida ! 0; |HACIENDO;
               |FBORRA Comodin;
               |SI FSalida ! 0;
                  |MENAV "    Termine la tarea de Excel que tiene activa";
               |FINSI;
            |SIGUE;
         |FINSI;
      |FINSI;
   |FINSI;
|FPRC;

|DEFBUCLE Empresas;
#cabucem1#1;
;
INICIO;
FINAL;
;
NULL,Empresas;
|FIN;

|PROGRAMA;
  |SI PARAMETRO = "";
     |CLS;
     |CABEZA "Emision Libro Mayor";
     |PINPA #0#0; |PINDA #0#0;
     |ENDATOS #1#0;
     |SI FSalida = 0;
        hoja = #0#4;
        |IMPRE 0;
        |SI FSalida ! 0;  |ACABA;  |FINSI;
        |INFOR informe;
        |SI FSalida ! 0;  |FINIMP;  |ACABA;  |FINSI;
        |BUCLE camaydia0;
        |BUCLE camaydia1;
        sumay = "SUMA TOTAL";
        swlin = 0;
        |PIEINF;
        |FININF;
        |FINIMP;
     |FINSI;
  |SINO;
     |ABRE #48;
     |BUCLE Empresas;
     |CIERRA #48;
  |FINSI;
|FPRO;

|PROCESO empieza; |TIPO 7;
  |HAZ inicio;
  #0#0 = fec1;
  #0#1 = fec2;
  |PINTA #0#0;
  |PINTA #0#1;
|FPRC;

|PROCESO fechad;
  |SI #0#0 < fec1; |O #0#0 > fec2;
      |MENAV mensa1;
      |ERROR;
      |ACABA;
  |FINSI;
|FPRC;

|PROCESO fechah;
  |SI #0#1 < fec1; |O #0#1 > fec2; |O #0#1 < #0#0;
      |MENAV mensa1;
      |ERROR;
      |ACABA;
  |FINSI;
|FPRC;
