|FICHEROS;
    cainforr #0;

    caclipro #12;   || clientes/proveedores

    camandia #20;   || diarios AGICONT
    caconcep #21;   || conceptos AGICONT
    camaasic #22;   || asientos cabs. AGICONT
    camaasil #23;   || asientos lins. AGICONT
    cacuenta #24;   || cuentas AGICONT
    canempre #25;   || empresas AGICONT
    caconasi #26;   || contador asientos AGICONT
    catradia #27;   || diario de trabajo
    cacuebal #28;   || cuentas de balance

    cainfors #40;   || def trabajo importacion
|FIN;

|VARIABLES;
    alfa1 = "";
    alfa2 = "";
    alfa3 = "";
    alfa4 = "";
    nume1 = 0;
    nume2 = 0;
    nume3 = 0;
    nume4 = 0;
    para1 = 0;
    para2 = 0;
    para3 = 0;
    para4 = 0;
    fech1 = @;
    fech2 = @;
    fech3 = @;
    fech4 = @;
    elsw1 = 0;
    elsw2 = 0;
    elsw3 = 0;
    elsw4 = 0;
    FEstado = 0;
    seleccio = 0;
    modo = 0;
    sw_rup = 0;

    p_mes = "";
    ultimo = 0;

    fecha = "";
    signe = "";
    impor = 0;
    conte = "";
    digcu = 0;
    conce = 0;
    descr = "";

    p_mensa = "    LONGITUD DE CUENTA FUERA DE NIVEL!";
    d_diari = 0;
    d_opera = "";
    d_impor = 0;
    &modcon = 0;
    importe = 0;
    opera = "";
    ressum = 0;
    ini = 0;
    fecalf = "";
    mes = "";
    mesfec = 0;
    mesac = 0;
    a = 0;
    b = 0;
    c = 0;
    ult_apunt = 0;
    p_alfa1 = "";
    p_error = 0;
    p_lugar = 0;
    p_cuan3 = 0;
    p_cuan4 = 0;
    p_cuan5 = 0;
    p_alfa2 = "";
    p_alfa3 = "";
    p_alfa4 = "";
    p_alfa5 = "";
    p_nume1 = 0;
    p_nume2 = 0;
    p_ceros = "";

    juan = 0;
    errant = 0;
    cuini = "            ";
    cufin = "999999999998";
    diini = 0;
    difin = 9;
    fecini = @;
    fecfin = @;
    tiini = "D";
    tifin = "H";
    diain = 0;
    diafi = 99;
    prim = 0;
    cuant = "";
    lon = 0;
    aa = 0;
    mensa = "";
    aar = "";
    traba = "";
    errcuent = "aaaaaaaaaaaa";
    mensa2   = "     No esta definida esta cuenta en Cuentas de Balance";
    uno = "";
    dos = "";
    mascara = "";
    nivel = 0;

    fabre = "";
    fcanl = "";
    field1 = "";
    field2 = "";
    fsali1 = 0;
    fsali2 = 0;
    Fcuentas = 0;
    guarda_pos = 0;
    sw_contador = 0;
    estado_iva = 0;
|FIN;

|INCLUYE acceso_i;
____________________________________/ CALCULOS PANTALLA
|PROCESO mayus; |TIPO 0;
    #0Campo = #0Campo > #0Campo;
|PINTA #0Campo;
|FPRC;
____________________________________/ ACTUALIZACION NIVELES SUPERIORES
|PROCESO actualiza;
  ini = dig1;
  fecalf = #27#3;
  mes = fecalf % 402;
  mesfec = mes;
  |SI ini = 1;
      mesac = mesfec;
  |SINO;
      mesac = (mesfec - ini) + 1;
      |SI mesac [ 0;
          mesac = (13 - ini) + mesfec;
      |FINSI;
  |FINSI;
  a = (mesac * 3) + 9;
  b = a + 1;
  c = a + 2;
  |SI #27#4 = "D";
      #24a = #24a + #27#5;
      #24#51 = #24#51 + #27#5;
  |SINO;
      #24b = #24b + #27#5;
      #24#52 = #24#52 + #27#5;
  |FINSI;
  #24c = #24a - #24b;
  #24#53 = #24#51 - #24#52;
|FPRC;

|PROCESO suma;
  |SI #27#6 = 0;
      |SI #27#4 = "D";
          #24#9 = #24#9 + #27#5;
          #24#51 = #24#51 + #27#5;
      |SINO;
          #24#10 = #24#10 + #27#5;
          #24#52 = #24#52 + #27#5;
      |FINSI;
      #24#11 = #24#9 - #24#10;
      #24#53 = #24#51 - #24#52;
  |SINO;
      |SI #27#6 = 99;
          |SI #27#4 = "D";
              #24#48 = #24#48 + #27#5;
              #24#51 = #24#51 + #27#5;
          |SINO;
              #24#49 = #24#49 + #27#5;
              #24#52 = #24#52 + #27#5;
          |FINSI;
          #24#50 = #24#48 - #24#49;
          #24#53 = #24#51 - #24#52;
      |SINO;
          |HAZ actualiza;
      |FINSI;
  |FINSI;
|FPRC;

|PROCESO defect1;
  |ABRE #28;
  uno = #24#0;
  |QBF uno;
  dos = uno + "000000000000";
  uno = dos % A101;
  #28#0 = uno;
  |LEE 040#28=;
  |SI FSalida = 0;
      |HAZ saca1;
      |VETE findefe;
  |FINSI;
  uno = dos % A102;
  #28#0 = uno;
  |LEE 040#28=;
  |SI FSalida = 0;
      |HAZ saca1;
      |VETE findefe;
  |FINSI;
  uno = dos % A103;
  #28#0 = uno;
  |LEE 040#28=;
  |SI FSalida = 0;
      |HAZ saca1;
      |VETE findefe;
  |FINSI;
  uno = dos % A104;
  #28#0 = uno;
  |LEE 040#28=;
  |SI FSalida = 0;
      |HAZ saca1;
      |VETE findefe;
  |SINO;
      |MENAV mensa2;
  |FINSI;
|ET findefe;
  nivel = #24#1;
|| la 3a clave esta formada por la cta + zzzz y el campo fantasma del nivel;
  mascara = #24#0;
  |QBF mascara;
  mascara = mascara +"zzzzzzzzzzzz";
  mascara = mascara % 112;
  #24#54    = mascara;
  #24#55    = nivel;
  |CIERRA #28;
|FPRC;

|PROCESO saca1;
  #24#4 = #28#1;
  #24#5 = #28#2;
  #24#6 = #28#3;
  #24#7 = #28#4;
  #24#8 = #28#5;
|FPRC;

|PROCESO actuali;
  |SI #27#2 [ juan; |O #27#2 = 9;
      |SI prim = 1;
           |GRABA 020#24a;
      |FINSI;
      prim = 0;
      |VETE acaba;
  |FINSI;
  aa = 100 + lon;
  aar = aa;
  traba = #27#1 % aar;
|ET iniemp;
  |SI prim = 0;
      cuant = traba;
      prim = 1;
      #24#0 = cuant;
      #24#1 = juan;
      |SI #24#0 = errcuent;
          prim = 0;
          |VETE acaba;
      |FINSI;
      |LIBERA #24;
      |LEE 101#24=;
      |SI FSalida ! 0;
          |LIBERA #24;
          |DDEFECTO #24;
          #24#0 = cuant;
          #24#1 = juan;
          |HAZ defect1;
          |PUSHV 0501 1980;
          |PINPA #0#24;
          |PINDA #0#24;
          |ENDATOS #1#24;
          |SI FSalida ! 0;
              |POPV;
              mensa = "Actualizando nivel: " + juan + "        ";
              |PINTA 2410,mensa;
              errant = #24#0;
              prim = 0;
              |VETE acaba;
          |FINSI;
          |GRABA 020#24b;
          |POPV;
          mensa = "Actualizando nivel: " + juan + "        ";
          |PINTA 2410,mensa;
      |FINSI;
  |FINSI;
  |SI cuant ! traba;
      |GRABA 020#24a;
      prim = 0;
      |VETE iniemp;
  |FINSI;
  |HAZ suma;
|ET acaba;
|FPRC;

|DEFBUCLE camaapua0;
 #27#1;
 ;
 codcon,cuini,diini,fecini,tiini,diain;
 codcon,cufin,difin,fecfin,tifin,diafi;
 be;
 NULL,actuali;
|FIN;

|PROCESO borrando;
  |BORRA 020#27a;
|FPRC;

|DEFBUCLE camaapua1;
 #27#1;
 ;
 codcon,cuini,diini,fecini,tiini,diain;
 codcon,cufin,difin,fecfin,tifin,diafi;
 be;
 NULL,borrando;
|FIN;

|PROCESO niveles;
  |PINTA 2410,"Estoy Actualizando las Cuentas";
  |ABRE #24;
  |PARA juan = dig3 - 1; |SI juan ] 1; |HACIENDO juan = juan - 1;
        prim = 0;
        |SI juan = 1; lon = dig4; |FINSI;
        |SI juan = 2; lon = dig5; |FINSI;
        |SI juan = 3; lon = dig6; |FINSI;
        |SI juan = 4; lon = dig7; |FINSI;
        |SI juan = 5; lon = dig8; |FINSI;
        fecini = "01.01.0000";
        fecfin = "31.12.8999";
        |BUCLE camaapua0;
        |SI prim = 1;
            |GRABA 020#24a;
        |FINSI;
  |SIGUE;
  |CIERRA #24;
  fecini = "01.01.0000";
  fecfin = "31.12.8999";
  |BUCLE camaapua1;
  |ABRE #27;
  |LEE 000#27u;
  |SI FSalida ! 0;
       |CIERRA #27;
       |DELINDEX #27;
  |SINO;
       |CIERRA #27;
  |FINSI;
|FPRC;

|PROCESO grabatra;
|ABRE #27;
|LEE 101#27u;
|SI FSalida ! 0;
    |DDEFECTO #27;
        #27#0 = 1;
|SINO;
        #27#0 = #27#0 + 1;
|FINSI;
|GRABA 101#27n;
    codcon = #27#0;
|LIBERA #27;
|CIERRA #27;
|FPRC;
____________________________________/ GRABACION DE ASIENTOS
|PROCESO actu_diari;
    #20#0 = d_diari;
|LEE 101#20=;
|SI FSalida ! 0;
    |DDEFECTO #20;
        #20#0 = d_diari;
        #20#1 = "<DIARIO SIN DEFINIR>";
    |GRABA 020#20n;
    |LEE 121#20=;
|FINSI;
|SI d_opera = "D";
        #20#2 = #20#2 + d_impor;        || debe
|SINO;
        #20#3 = #20#3 + d_impor;        || haber
|FINSI;
|GRABA 020#20a;
|LIBERA #20;
|FPRC;

|PROCESO altatra;
    modcon = 1;

    #27#0 = codcon;
    #27#1 = #23#4;
    #27#2 = #23#5;
    #27#3 = #23#1;
    #27#4 = #23#8;
    #27#6 = #23#0;
|LEE 101#27=;
|SI FSalida ! 0;
    |DDEFECTO #27;
        #27#0 = codcon;
        #27#1 = #23#4;
        #27#2 = #23#5;
        #27#3 = #23#1;
        #27#4 = #23#8;
        #27#6 = #23#0;
    |GRABA 101#27b;
|FINSI;
    importe = #23#9;  || aqui si que se usa el importe real
|SI opera = "B";
        importe = -importe;
|FINSI;
    #27#5 = #27#5 + importe;
|GRABA 101#27a;
|LIBERA #27;
|FPRC;

|PROCESO alta_cuen;
    guarda_pos = Pos;
    Pos = -1;

    #24#2 =  "";         || nombre cliente
    #24#3 = "S";         || pase directo SI
|HAZ defect1;
|PUSHV 0501 2380;
|PINPA #0#24;
|PINDA #0#24;
|ENDATOS #1#24;
|SI FSalida = 0;
    |GRABA 020#24b;
|FINSI;
|POPV;

    Pos = guarda_pos;
|FPRC;

|PROCESO alta_cli;
    guarda_pos = Pos;
    Pos = -1;

    #12#0 = "C";
    #12#10 = #24#0;
    #12#11 = #24#1;
|LEE 000#12=;
|SI FSalida ! 0;
        #12#1 = #24#2;        || nombre
        #12#9 = #40#6;        || cif
    |PUSHV 0501 2380;
    |PINPA #0#12;
    |PINDA #0#12;
    |ENDATOS #1#12;
    |SI FSalida = 0;
        |GRABA 020#12n;
    |FINSI;
    |POPV;
|FINSI;

    Pos = guarda_pos;
|FPRC;

|PROCESO actualiz1;
    #24#0 = #23#4;
    #24#1 = #23#5;
|LEE 101#24=;
|SI FSalida ! 0;
    |HAZ alta_cuen;
|FINSI;
|SI #23#0 = 0;
    |SI #23#8 = "D";
            #24#9 = #24#9 + ressum;
            #24#51 = #24#51 + ressum;
    |SINO;
            #24#10 = #24#10 + ressum;
            #24#52 = #24#52 + ressum;
    |FINSI;
        #24#11 = #24#9 - #24#10;
        #24#53 = #24#51 - #24#52;
|SINO;
    |SI #23#0 = 99;
        |SI #23#8 = "D";
                #24#48 = #24#48 + ressum;
                #24#51 = #24#51 + ressum;
        |SINO;
                #24#49 = #24#49 + ressum;
                #24#52 = #24#52 + ressum;
        |FINSI;
            #24#50 = #24#48 - #24#49;
            #24#53 = #24#51 - #24#52;
    |SINO;
        |HAZ actualiz2;
    |FINSI;
|FINSI;
|SI #24#58 < #23#1;
        #24#58 = #23#1;
|FINSI;
|GRABA 020#24a;
|LIBERA #24;
|FPRC;

|PROCESO actualiz2;
    ini = dig1;      || numero de niveles
    fecalf = #23#1;
    mes = fecalf % 402;
    mesfec = mes;
|SI ini = 1;
        mesac = mesfec;
|SINO;
        mesac = (mesfec - ini) + 1;
    |SI mesac [ 0;
            mesac = (13 - ini) + mesfec;
    |FINSI;
|FINSI;
    a = (mesac * 3) + 9;
    b = a + 1;
    c = a + 2;
|SI #23#8 = "D";
        #24a = #24a + ressum;
        #24#51 = #24#51 + ressum;
|SINO;
        #24b = #24b + ressum;
        #24#52 = #24#52 + ressum;
|FINSI;
    #24c = #24a - #24b;
    #24#53 = #24#51 - #24#52;
|FPRC;

|PROCESO cabecera;
|DDEFECTO #22;
    #22#0 = #23#0;
    #22#1 = #23#1;
    #22#2 = #23#2;
|LEE 101#22=;
    FEstado = FSalida;
    #22#0 = #23#0;         || diario
    #22#1 = #23#1;         || fecha
    #22#2 = #23#2;         || documento
    #22#3 = #22#2;         || asiento
|SI #23#8 = "D";
        #22#4 = #22#4 + #23#9;
    |SI #22#09 = 0; #22#08 = #23#4; #22#09 = #23#5; |FINSI; || contrp.HABER
|FINSI;
|SI #23#8 = "H";
        #22#5 = #22#5 + #23#9;
    |SI #22#11 = 0; #22#10 = #23#4; #22#11 = #23#5; |FINSI; || contrp.DEBE
|FINSI;
|SI FEstado = 0;  |GRABA 020#22a;  |FINSI;
|SI FEstado ! 0;  |GRABA 020#22n;  |FINSI;
|LIBERA #22;
|FPRC;

|PROCESO la_cuenta;
  p_alfa1 = conte;
  p_error = 0;
  p_lugar = 0;
  p_cuan3 = 0;
  p_cuan4 = 0;
  p_alfa3 = "";
  p_alfa4 = "";
  |PARA p_nume1 = 1; |SI p_nume1 [ 12; |HACIENDO p_nume1 = p_nume1 + 1;
        p_nume2 = (p_nume1 * 100) + 1;
        p_alfa2 = p_alfa1 % p_nume2;
        |SI p_alfa2 ! " ";
            |SI p_alfa2 = ".";
                p_lugar = p_nume1;
            |SINO;
                |SI p_lugar=0; p_alfa3=p_alfa3+p_alfa2; p_cuan3=p_cuan3+1; |FINSI;
                |SI p_lugar!0; p_alfa4=p_alfa4+p_alfa2; p_cuan4=p_cuan4+1; |FINSI;
            |FINSI;
        |FINSI;
  |SIGUE;
  |SI p_lugar ! 0;
      |SI dig3 = 1;  p_cuan5 = dig4;  digcu = 1;  |FINSI;
      |SI dig3 = 2;  p_cuan5 = dig5;  digcu = 2;  |FINSI;
      |SI dig3 = 3;  p_cuan5 = dig6;  digcu = 3;  |FINSI;
      |SI dig3 = 4;  p_cuan5 = dig7;  digcu = 4;  |FINSI;
      |SI dig3 = 5;  p_cuan5 = dig8;  digcu = 5;  |FINSI;
      |SI dig3 = 6;  p_cuan5 = dig9;  digcu = 6;  |FINSI;
      p_nume1 = p_cuan5 - (p_cuan3 + p_cuan4);
      |SI p_nume1 > 0;
          p_ceros = "0" * p_nume1;
          p_alfa5 = p_alfa3 + p_ceros + p_alfa4;
      |SINO;
          |MENAV p_mensa;
          p_error = 1;
      |FINSI;
  |SINO;
      p_alfa5 = p_alfa1;
  |FINSI;
  conte = p_alfa5;
|FPRC;

|PROCESO digito;
    alfa1 = conte;  |QBF alfa1;
    alfa2 = alfa1 % 0;
    nume1 = alfa2;
|SI nume1 = dig4;  digcu = 1;  |FINSI;
|SI nume1 = dig5;  digcu = 2;  |FINSI;
|SI nume1 = dig6;  digcu = 3;  |FINSI;
|SI nume1 = dig7;  digcu = 4;  |FINSI;
|SI nume1 = dig8;  digcu = 5;  |FINSI;
|SI nume1 = dig9;  digcu = 6;  |FINSI;
|SI nume1 = 0;     digcu = 0;  |FINSI;
|FPRC;

|PROCESO apuntes;
|SI impor ! 0;
    ||HAZ la_cuenta;     || por si viene en formato '43.1'
    |HAZ digito;
    |SI conte ! "            ";|Y digcu ! 0;
        |DDEFECTO #23;
            #23#00 = #0#0;        || diario
            #23#01 = fecha;       || fecha
            #23#02 = #26#1;       || documento
            #23#03 = ult_apunt;   || apunte
            #23#04 = conte;       || cuenta
            #23#05 = digcu;       || digito cuenta
            #23#06 = conce;       || concepto
            #23#07 = descr;       || descripcion
            #23#08 = signe;       || signo
            #23#09 = impor;       || importe
        |SI #23#08 = "D";
                #23#16 = #23#4;
                #23#17 = #23#5;
        |SINO;
                #23#10 = #23#4;
                #23#11 = #23#5;
        |FINSI;
        |GRABA 020#23n;
        |HAZ cabecera;
            ressum = #23#9;
        |HAZ actualiz1;     || actualiza cuentas
            opera = "A";
        |HAZ altatra;       || actualiza niveles inferiores
            d_diari = #23#0;
            d_opera = #23#8;
            d_impor = #23#9;
        |HAZ actu_diari;    || actualiza diario
            ult_apunt = ult_apunt + 10;
    |FINSI;
|FINSI;
|FPRC;

|PROCESO asiento;        || grabacion de asientos
|SI sw_contador = 0;
    |HAZ contador;
        sw_contador = 1;
|FINSI;

    signe = "H";                             || apunte del cliente
    alfa1 = #40#21 % 101;
|SI alfa1 = "5";    signe = "D";   |FINSI;   || apunte del banco

    impor = #40#18; conte = #40#21;     |HAZ apuntes;

|SI signe = "H";    |HAZ alta_cli;      |FINSI;   || alta del cliente

|SI signe = "D";    sw_contador = 0;    |FINSI;   || cambio de asiento
|FPRC;
____________________________________/ CARGA VARIABLES VARIAS
|PROCESO concepto;
|DDEFECTO #21;
    conce = #0#2;
    #21#0 = conce;
|LEE 000#21=;
    descr = #21#1;  |QBF descr;

|SI #0#3 = "S";
        alfa1 = #40#0;  |QBT alfa1;    || nro.efecto
        descr = descr + "[" + alfa1 + "]";
|FINSI;
|SI #0#4 = "S";
        alfa1 = #40#2;  |QBT alfa1;    || nro.factura
        descr = descr + "[" + alfa1 + "]";
|FINSI;
|SI #0#5 = "S";
        alfa1 = #40#4;  |QBT alfa1;    || fecha vto.
        descr = descr + "[" + alfa1 + "]";
|FINSI;
|FPRC;

|PROCESO fecha;
    alfa1 = #40#17;            || dia remesa
    alfa2 = #40#16;            || mes remesa
    alfa3 = #40#15;            || a¤o remesa

    nume1 = alfa1;
|SI nume1 > 80;  alfa1="19"+alfa1;  |SINO;  alfa1="20"+alfa1;  |FINSI;

    fecha = alfa3 + "." + alfa2 + "." + alfa1;
|FPRC;

|PROCESO contador;
|LEE 101#26p;
|SI FSalida ! 0;
    |DDEFECTO #26;
    |GRABA 020#26n;
    |LEE 101#26p;
|FINSI;
    #26#1 = #26#1 + 1;
|GRABA 020#26a;
|LIBERA #26;
    ult_apunt = 1;

|PINTA 2243, #26#1;
|FPRC;

|PROCESO varios;         || variables varias
|HAZ fecha;
|HAZ concepto;
|FPRC;

|PROCESO conta1;         || comprobaciones y calculos
    estado_iva = 0;
|DDEFECTO #40;
    #40#0 = field1 % 0105;    || numero efecto
    #40#1 = field1 % 0601;    || contador efecto
    #40#2 = field1 % 0705;    || numero factura
    #40#3 = field1 % 1204;    || numero cliente
    #40#4 = field1 % 1606;    || fecha vencimiento
    #40#5 = field1 % 2206;    || numero efecto (altern.)
    #40#6 = field1 % 2802;    || dia expedicion
    #40#7 = field1 % 3002;    || mes expedicion
    #40#8 = field1 % 3202;    || a¤o expedicion
    #40#9 = field1 % 3402;    || a¤o vencimiento
    #40#10= field1 % 3602;    || mes vencimiento
    #40#11= field1 % 3802;    || dia vencimiento
    #40#12= field1 % 4002;    || numero banco
    #40#13= field1 % 4206;    || fecha vencimiento (altern.)
    #40#14= field1 % 4806;    || numero giro (altern.)
    #40#15= field1 % 5402;    || dia remesa
    #40#16= field1 % 5602;    || mes remesa
    #40#17= field1 % 5802;    || a¤o remesa
    #40#18= field1 % 6009;    || importe efecto
    #40#19= field1 % 6901;    || situacion del giro
    #40#20= field1 % 7002;    || representante
    #40#21= field1 % 7210;    || cuenta cliente
    #40#22= field1 % 8211;    || <blancos>
    conte = #40#21; |HAZ digito;

|SI digcu ! 0;
        seleccio = 1;
    |HAZ varios;
    |HAZ asiento;
|SINO;
        estado_iva = -2;
|FINSI;
|FPRC;

|PROCESO tipo3; |TIPO 3;
|SI #0#1 = "SI";
   |HAZ grabatra;

       fabre = #0#6;     |QBF fabre;
       fabre = "rb  " + fabre;
   |FABRE fabre;
   |SI FSalida ] 0;
           fcanl = FSalida;

       |ABRE #12;      || clientes/proveedores
       |ABRE #20;      || diarios
       |ABRE #21;      || conceptos
       |ABRE #22;      || apuntes cabs.
       |ABRE #23;      || apuntes lins.
       |ABRE #24;      || plan contable
       |ABRE #26;      || contador asientos
       |ABRE #27;      || diario de trabajo

           field1 = fcanl + " 92";   |FLEE field1;  fsali1 = FSalida;
       |PARA; |SI fsali1 = 92; |HACIENDO;
           |HAZ conta1;
           |SI estado_iva = -2;
               |MENAV "    Longitud de cuenta fuera de nivel ...";
               |SAL;
           |FINSI;
               field1 = fcanl + " 92";   |FLEE field1;  fsali1 = FSalida;
       |SIGUE;

       |CIERRA #12;
       |CIERRA #20;
       |CIERRA #21;
       |CIERRA #22;
       |CIERRA #23;
       |CIERRA #24;
       |CIERRA #26;
       |CIERRA #27;

       |FCIERRA fcanl;
           Pos = -1;
   |SINO;
           alfa1 = "    [" + (fabre % 5) + "] inaccesible o inexistente ...";
       |MENAV alfa1;
   |FINSI;

   |HAZ niveles;

   |SI seleccio = 0;
       |MENAV "    Seleccion vacia ...";
   |FINSI;
|FINSI;
|FPRC;
