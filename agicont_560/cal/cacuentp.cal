|FICHEROS;
    cacuentp #0;
|FIN;

|VARIABLES;
    zalfa1 = "";
    zalfa2 = "";
    znume1 = 0;
    znume2 = 0;

    zletrasnif = "TRWAGMYFPDXBNJZSQVHLCKE";
    zletra = "";
    zfinal = "";
    znifnum = 0;
    zlugar = 0;
    zresto = 0;
    zx = "";
|FIN;

|PROCESO pasamayor; |TIPO 0;
    #0Campo = #0Campo > " ";
|PINTA #0Campo;
|FPRC;

|PROCESO porcif; |TIPO 0;
|SI #0#0 = "F";
        zalfa1 = "N";
        zalfa2 = "S";
|SINO;
        zalfa1 = "S";
        zalfa2 = "N";
|FINSI;
|C_M #0#1, 1, zalfa1;
|C_M #0#4, 1, zalfa2;
|FPRC;

|PROCESO letra_99;
  |SI FSalida ! 9;  |ACABA;  |FINSI;

  zalfa1 = #0Campo;   |QBT zalfa1;
  zx    = zalfa1 % 101;
  |SI zx ! "A"; |Y zx ! "B"; |Y zx ! "C"; |Y zx ! "D"; |Y zx ! "E"; |Y zx ! "F";
   |Y zx ! "G"; |Y zx ! "P"; |Y zx ! "Q"; |Y zx ! "S"; |Y zx ! "X";
      zfinal = "";
      zalfa2 = zalfa1 % 0;
      znume1 = zalfa2;
      |PARA znume2 = 1;  |SI znume2 [ znume1;  |HACIENDO znume2 = znume2 + 1;
            zlugar = (znume2 * 100) + 1;
            zalfa2 = zalfa1 % zlugar;
            |SI zalfa2 = "0"; |O zalfa2 = "1";|O zalfa2 = "2"; |O zalfa2 = "3";
             |O zalfa2 = "4"; |O zalfa2 = "5";|O zalfa2 = "6";
             |O zalfa2 = "7"; |O zalfa2 = "8";|O zalfa2 = "9";
                zfinal = zfinal + zalfa2;
            |FINSI;
      |SIGUE;
      znifnum = zfinal;
      |SI znifnum ! 0; |Y zfinal ! "";
          zresto   = znifnum $ 23;
          zresto   = zresto + 1;
          zlugar   = (zresto * 100) + 1;
          zletra   = zletrasnif % zlugar;
          zfinal   = ("000000000" + zfinal + zletra) % -109;
          #0Campo = zfinal;
      |FINSI;
   |SINO;
      |MENAV "    ATENCION !!! No se calcular un C.I.F. ...";
   |FINSI;
   |PINTA #0Campo;
   |ERROR;
|FPRC;
