|FICHEROS;
    cacuadre #0;
    cacuenta #5;
    cacuebal #11;
|FIN;

|VARIABLES;
    FEstado = 0;
    impreso = 0;
    nombre = "";
    numero = "";
    path = "";
    l = "";
    temporal1 = "";
    temporal2 = "";
    alfa1 = "";
    alfa2 = "";
    nume1 = 0;
    long  = 0;
    sw = 0;
    swdig = 0;
    salidas = 0;
    cuen = "";
    digi = 0;
    mensa2   = "     No esta definida esta cuenta en Cuentas de Balance";
    uno = "";
    dos = "";
    mascara = "";
|FIN;

|INCLUYE inicio_i;
|INCLUYE puntos_i;

|PROCESO minus; |TIPO 0;
  alfa1 = #0Campo < " ";
  #0Campo = alfa1;
  |PINTA #0Campo;
|FPRC;

|PROCESO mayus; |TIPO 0;
  alfa1 = #0Campo > " ";
  #0Campo = alfa1;
  |PINTA #0Campo;
|FPRC;

|PROGRAMA;
  |HAZ inicio;
  |CLS;
  |CABEZA "Cuadres y Ctrl Pg./Ta.";
  |ABRE #0;
  |DDEFECTO #0;
  |LEE 001#0p;
  FEstado = FSalida;
  |PINPA #0#0;
  |PINDA #0#0;
  |ENDATOS #1#0;
  |SI FSalida = 0;
      |HAZ configu;
      |SI FEstado = 0; |BORRA 020#0c; |FINSI;
      |GRABA 020#0n;
  |FINSI;
  |CIERRA #0;
|FPRO;

|PROCESO configu;
  |SI #0#1 ! -1;
      nombre = "caimpcob.";  numero = #0#1; |HAZ copia;
  |FINSI;
  |SI #0#6 ! -1;
      nombre = "caemipag.";  numero = #0#6; |HAZ copia;
  |FINSI;
  |SI #0#37 ! -1;
      nombre = "caemital.";  numero = #0#37; |HAZ copia;
  |FINSI;
|FPRC;

|PROCESO copia;
  |DDEF path;
  numero="00"+numero;
  numero=numero % -102;
  temporal1 = path + nombre + numero;
  temporal2 = path + nombre + "inf";
  |COPIA_FICHERO temporal1,temporal2;
  |PINTA 2301, temporal1;
|FPRC;

|| **************************** PROCESOS PARA LAS CONTRAPARTIDAS

|PROCESO cuenta; |TIPO 0;
  nume1 = Campo + 1;
  #0nume1 = 0;
  alfa1 = #0Campo;
  |QBF alfa1;
  alfa2 = alfa1 % 0;
  long  = alfa2;
  nume1 = (long * 100) + 1;
  alfa2 = alfa1 % nume1;
  |SI alfa2 ! "*";|Y alfa2 ! "%";
|| ......................... Calculo de Digito.
      p_alfa1 = #0Campo;
      salidas = FSalida;
      |HAZ punto;
      |SI p_error ! 0;  |ERROR;  |ACABA;  |FINSI;
      nume1 = Campo + 1;
      alfa1 = #0Campo;
      |QBF alfa1;
      alfa1 = alfa1 % 0;
      long  = alfa1;
      swdig = 0;
      #0nume1 = 0;  || inicializa el digito
      |SI long = dig4; |Y dig4 ! 0; swdig = 1; #0nume1 = 1; |FINSI;
      |SI long = dig5; |Y dig5 ! 0; swdig = 1; #0nume1 = 2; |FINSI;
      |SI long = dig6; |Y dig6 ! 0; swdig = 1; #0nume1 = 3; |FINSI;
      |SI long = dig7; |Y dig7 ! 0; swdig = 1; #0nume1 = 4; |FINSI;
      |SI long = dig8; |Y dig8 ! 0; swdig = 1; #0nume1 = 5; |FINSI;
      |SI long = dig9; |Y dig9 ! 0; swdig = 1; #0nume1 = 6; |FINSI;
      |SI swdig = 0;|Y long ! 0;
          |MENAV "    Longitud de Cuenta Fuera de Nivel !";
          |ERROR;
      |SINO;
          cuen = #0Campo;
          digi = #0nume1;
          |HAZ alta_cuenta;
      |FINSI;
  |FINSI;
|FPRC;

|PROCESO alta_cuenta;    || comprueba si la cuenta esta dada de alta
|ABRE #5;
    #5#0 = cuen;
    #5#1 = digi;
|LEE 001#5=;
 |SI FSalida ! 0;
    |DDEFECTO #5;
        #5#0 = cuen;
        #5#1 = digi;
    |HAZ defect1;
    |PUSHV 0501 2380;
    |PINPA #0#5;
    |PINDA #0#5;
    |ENDATOS #1#5;
    |SI FSalida = 0;
        |GRABA 020#5n;
    |FINSI;
    |POPV;
 |FINSI;
 |SI #5#3 = "N";
     |MENAV "    La Cuenta NO tiene PASE DIRECTO";
     |ERROR;
 |FINSI;
 |CIERRA #5;
|FPRC;

|PROCESO defect1;
 |ABRE #11;
    uno = #5#0;
 |QBF uno;
    dos = uno + "000000000000";
    uno = dos % A101;
    #11#0 = uno;
 |LEE 000#11=;
 |SI FSalida ! 0;
     uno = dos % A102;
     #11#0 = uno;
     |LEE 000#11=;
     |SI FSalida ! 0;
         uno = dos % A103;
         #11#0 = uno;
         |LEE 000#11=;
         |SI FSalida ! 0;
             uno = dos % A104;
             #11#0 = uno;
             |LEE 000#11=;
             |SI FSalida ! 0;
                 |MENAV mensa2;
             |SINO;
                 |HAZ saca1;
             |FINSI;
         |SINO;
             |HAZ saca1;
         |FINSI;
    |SINO;
        |HAZ saca1;
    |FINSI;
|SINO;
    |HAZ saca1;
|FINSI;
|CIERRA #11;

 || la 3a clave esta formada por la cta + zzzz y el campo fantasma del nivel;
    mascara = #5#0;
|QBF mascara;
    mascara = mascara +"zzzzzzzzzzzz";
    mascara = mascara % 112;
    #5#54    = mascara;
    #5#55    = #5#1;
    #5#3     = "S";
|FPRC;

|PROCESO saca1;
    #5#4 = #11#1;
    #5#5 = #11#2;
    #5#6 = #11#3;
    #5#7 = #11#4;
    #5#8 = #11#5;
|FPRC;
