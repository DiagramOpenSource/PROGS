|FICHEROS;
  cacugene #0;
  caforpag #1;
  eosclipr #2,MANTE;
  agipobla #3;
  agiprovi #4;
  agim0016 #7;
  agicontr #23;
|FIN;

|VARIABLES;
  &entrada = 0;
  antes = 0;
  salida = 0;
  cuenta = 0;
  digito = 0;
  pos1 = "";
  pos2 = "";
  cta = "";
  long1 = "";
  long = 0;
  pepe = 0;
  sw = 0;
  num = 0;
  men6      = "    NConfirma el Alta en Fichero General (S/N): ";
  men2  = "     Longitud de Cuenta Fuera de Nivel !";

     letrasnif = "TRWAGMYFPDXBNJZSQVHLCKE";
     letra = "";
     alfa1 = "";
     alfa2 = "";
     final = "";
     x = "";
     pos3 = "";
     pos4 = "";
     nume1 = 0;
     nume2 = 0;
     cont = 0;
     lugar = 0;
     nifnum = 0;
     resto = 0;

    esalta = 0;
    directo = "";
    direct1 = "";
    traba = "";
    &agi  = "";
    &agi1 = "";
    &agi2 = "";
    &aste = "";
    longi = "";
|FIN;

|INCLUYE acceso_i;
|INCLUYE puntos_i;

|PROCESO primentra;  |TIPO 8;
  |HAZ inicio;
  |HAZ tipo_8;
|FPRC;

|PROCESO mayor11;
  #0Campo = #0Campo > " ";
  |PINTA #0Campo;
|FPRC;

|PROCESO cuenta; |TIPO 6;
  entrada = 1;
  salidas = FSalida;
  cuenta = Campo;
  digito = Campo + 1;
  p_alfa1 = #0cuenta;
  |HAZ punto;
  |SI p_error ! 0;  |ERROR;  |ACABA;  |FINSI;
  cta  = #0cuenta;
  |QBF cta;
  long1 = cta % A0;
  long = long1;
  cont = 0;
  sw = 0;
  |SI cta = 0;  |Y Campo = 21;
      sw = 1;
      #0digito = 1;
      |ACABA;
  |FINSI;
  |PARA num = 14; |SI num [ 19; |HACIENDO num = num + 1;
        cont = cont + 1
        |SI long = #30num;
            sw = 1;
            |SAL;
        |FINSI;
  |SIGUE;
  |SI sw = 0;
      |MENAV men2;
      |ERROR;
      |ACABA;
  |FINSI;
  #0digito = cont;
  |SI Campo ! 10;  |ACABA;  |FINSI;
  alfa1 = #0Campo % 102;
  |SI alfa1 = "43";  |O alfa1 = "44";
      |SI #0#23 = "P";
          |MENAV "     ATENCION Esta Cuenta DEBERIA ser de Clientes";
      |FINSI;
  |FINSI;
  |SI alfa1 = "40";  |O alfa1 = "41";  |O alfa1 = "52";
      |SI #0#23 = "C";
          |MENAV "     ATENCION Esta Cuenta DEBERIA ser de Proveedores";
      |FINSI;
  |FINSI;
|FPRC;

|PROCESO nocero; |TIPO 6;
  |SI #0#20 ! 0;  |ACABA;  |FINSI;
  |DDEFECTO #1;
  |ABRE #1;
  #1#0 = #0#20;
  |LEE 001#1=;              || graba el codigo cero para que no
  |SI FSalida ! 0;          ||/salga la pantalla
      |GRABA 020#1n;
  |FINSI;
  |CIERRA #1;
|FPRC;

|PROCESO letra_0;  |TIPO 6;
  |SI FSalida ! 9;  |ACABA;  |FINSI;

  alfa1 = #0Campo;   |QBT alfa1;
  x    = alfa1 % 101;
  |SI x ! "A"; |Y x ! "B"; |Y x ! "C"; |Y x ! "D"; |Y x ! "E"; |Y x ! "F";
   |Y x ! "G"; |Y x ! "P"; |Y x ! "Q"; |Y x ! "S"; |Y x ! "X";
      final = "";
      alfa2 = alfa1 % 0;
      nume1 = alfa2;
      |PARA nume2 = 1;  |SI nume2 [ nume1;  |HACIENDO nume2 = nume2 + 1;
            lugar = (nume2 * 100) + 1;
            alfa2 = alfa1 % lugar;
            |SI alfa2 = "0"; |O alfa2 = "1";|O alfa2 = "2"; |O alfa2 = "3";
             |O alfa2 = "4"; |O alfa2 = "5";|O alfa2 = "6";
             |O alfa2 = "7"; |O alfa2 = "8";|O alfa2 = "9";
                final = final + alfa2;
            |FINSI;
      |SIGUE;
      nifnum = final;
      |SI nifnum ! 0; |Y final ! "";
          resto   = nifnum $ 23;
          resto   = resto + 1;
          lugar   = (resto * 100) + 1;
          letra   = letrasnif % lugar;
          final   = ("000000000" + final + letra) % -109;
          #0Campo = final;
      |FINSI;
   |SINO;
      |MENAV "    ATENCION !!! No se calcular un C.I.F. ...";
   |FINSI;
   |PINTA #0Campo;
|FPRC;

|PROCESO defcodi;  |TIPO 7;
  esalta = (FEntrada /100) !0;
  |SI esalta = 1;
      #0#0 = #0#10;
      |PINTA #0#0;
  |FINSI;
|FPRC;

|PROCESO creacod1; |TIPO 0;
  pos1 = #0#3;
  pos2 = "000" + pos1;
  traba = pos2 % -102;
  pos1 = #0#4;
  pos2 = "0000" + pos1;
  pos1 = pos2 % -103;
  #0#25 = traba + pos1;
|FPRC;

|PROCESO creacod2; |TIPO 0;
  pos1 = #0#15;
  pos2 = "000" + pos1;
  traba = pos2 % -102;
  pos1 = #0#16;
  pos2 = "0000" + pos1;
  pos1 = pos2 % -103;
  #0#26 = traba + pos1;
|FPRC;

|PROCESO quetecla;
  |SI FSalida ! 9;  |ACABA;  |FINSI;
  |ENCAMPO #9#0;
|FPRC;

|PROCESO clienprov;
  traba = #0#9;
  |QBF traba;
  |SI traba = "";  |ACABA;  |FINSI;
  |SI aste = "*";  |ACABA;  |FINSI;
  |ABRE #2;
  #2#0 = #0#9;
  |LEE 001#2=;
  |SI FSalida = 0;
      #0#9 = #2#0;
      #0#1 = #2#1;
      traba = #2#2;
      |QBF traba;
      |SI traba = "";
          #0#2 = #2#3;
      |SINO;
          #0#2 = traba + " " + #2#3;
      |FINSI;
      traba = #0#2;
      |QBF traba;
      #0#2 = traba + ", " + #2#4;
      #0#3 = #2#7;
      #0#4 = #2#8;
      #0#5 = #2#9;
      #0#6 = #2#10;
      |PINTA #0#1;
      |PINTA #0#2;
      |PINTA #0#3;
      |PINTA #0#4;
      |PINTA #0#5;
      |PINTA #0#6;
  |SINO;
       |HAZ altaclipr;
  |FINSI;
  |CIERRA #2;
|FPRC;

|PROCESO tipo_8; || control de la ficha de clientes general
  |DIRECTORIO agi;
  |QBF agi;
  |PATH_DAT #23 agi;

|ABRE #23;
#23#0 = 1;
|LEE 040#23=;
|SI FSalida ! 0; aste = "*"; |CIERRA #23; |ACABA; |FINSI;
|CIERRA #23;
aste = #23#7;
|QBF aste;
|SI aste  = "*"; |ACABA; |FINSI;

agi1 = #23#8; |QBF agi1;

longi = agi1 % 0;
num   = longi;
num   = 100 + (num - 5);
longi = num;

agi2  = (agi1 % longi) + "pan/";

  |PATH_DAT #2 agi1;
  |PATH_DAT #3 agi1;
  |PATH_DAT #4 agi1;
  |PATH_DAT #7 agi1;

  |PATH_PAN #2 agi2;
  |PATH_PAN #3 agi2;
  |PATH_PAN #4 agi2;
  |PATH_PAN #7 agi2;
|FPRC;

|PROCESO altaclipr;
|SI aste = "*"; |ACABA; |FINSI;
    |CONFI men6;
    |SI FSalida = 0;
        |PUSHV 0501 2380;
        |PINPA #0#2;
        |DDEFECTO #2;
        #2#0 = #0#9;
        #2#1 = #0#1;
        #2#3 = #0#2;
        #2#7 = #0#3;
        #2#8 = #0#4;
        #2#9 = #0#5;
        #2#10= #0#6;
        #2#14= #0#24;
        |PINTA #2#0;
        |PINDA #0#2;
        |ENDATOS #1#2;
        |SI FSalida = 0;
            |POPV;
             #0#9 = #2#0;
             #0#1 = #2#1;
             traba = #2#2;
             |QBF traba;
             |SI traba = "";
                 #0#2 = #2#3;
             |SINO;
                 #0#2 = traba + " " + #2#3;
             |FINSI;
             traba = #0#2;
             |QBF traba;
             #0#2 = traba + ", " + #2#4;
             #0#3 = #2#7;
             #0#4 = #2#8;
             #0#5 = #2#9;
             #0#6 = #2#10;
             |PINTA #0#1;
             |PINTA #0#2;
             |PINTA #0#3;
             |PINTA #0#4;
             |PINTA #0#5;
             |PINTA #0#6;
            |GRABA 020#2n;                 || Graba el agicli
        |SINO;
            |POPV;
        |FINSI;
    |FINSI;
|LIBERA #2;
|FPRC;

|PROCESO conficha;
  |SI aste = "*"; |ACABA; |FINSI;
  |SI FSalida ! 10; |ACABA; |FINSI;
  antes = 0;
  |ABRE #2;
    |PUSHV 0501 2380;
    |PINPA #0#2;
  #2#0 = #0#9;
  |LEE 000#2];
  |CONSULTA #0#2;
  salida = FSalida;
     |CIERRA #2;
     |POPV;
  |SI salida = 0;
      #0#9 = #2#0;
      |PINTA #0#9;
  |SINO;
      antes = 1;
      |ERROR;
  |FINSI;
|FPRC;

|PROCESO cuapais;
 |PUSHV 1102 1850;
 |D_CUADRO 1102, 1850;
 |ATRI R;
  |PINTA 1203 , " ------------ PAISES COMUNITARIOS -------------";
 |ATRI N;
  |PINTA 1303 , " ES Espa¤a      FR Francia       IT Italia     ";
  |PINTA 1403 , " DE Alemania    EL Grecia        IE Irlanda    ";
  |PINTA 1503 , " BE Belgica     GB Gran Breta¤a  LU Luxemburgo ";
  |PINTA 1603 , " DK Dinamarca   NL Holanda       PT Portugal   ";
  |PINTA 1703 , " FI Finlandia   SE Suecia        AT Austria    ";
 |ATRI I;
 |PINTA 1304 , "ES"; |PINTA 1319 , "FR"; |PINTA 1336 , "IT";
 |PINTA 1404 , "DE"; |PINTA 1419 , "EL"; |PINTA 1436 , "IE";
 |PINTA 1504 , "BE"; |PINTA 1519 , "GB"; |PINTA 1536 , "LU";
 |PINTA 1604 , "DK"; |PINTA 1619 , "NL"; |PINTA 1636 , "PT";
 |PINTA 1704 , "FI"; |PINTA 1719 , "SE"; |PINTA 1736 , "AT";
 |ATRI 0;
 |PAUSA;
 |POPV;
|FPRC;

|PROCESO filtro; |TIPO 0;
 |SI FSalida = 9;
     |HAZ cuapais;
     |ERROR;
     |ACABA;
 |FINSI;
 |HAZ mayor11;
 pepe = 0;
 |SI #0#24 = "ES "; |O #0#24 = "DE "; |O #0#24 = "BE "; |O #0#24 = "DK ";
     pepe = 1;
 |FINSI;
 |SI #0#24 = "FR "; |O #0#24 = "EL "; |O #0#24 = "GB "; |O #0#24 = "NL ";
     pepe = 1;
 |FINSI;
 |SI #0#24 = "IT "; |O #0#24 = "IE "; |O #0#24 = "LU "; |O #0#24 = "PT ";
     pepe = 1;
 |FINSI;
 |SI #0#24 = "FI "; |O #0#24 = "SE "; |O #0#24 = "AT ";
     pepe = 1;
 |FINSI;
 |SI pepe = 0;
    || |MENAV "     Pais incorrecto.";
 |FINSI;
|FPRC;
