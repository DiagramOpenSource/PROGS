|FICHEROS;
    cainvamo #00;
    cacuenta #01;
    caparbal #02;
    cabaexp0 #03;
    cabaexp2 #05;
    caexistc #06;
    caexistl #07;
    camocabe #10;
    camoauxc #11;
    camoauxl #12;
    cauxinva #20;   ||Temporal listados inventario
    cauxinv2 #21;   ||Temporal listados inventario 2 Cabecera
|FIN;

|VARIABLES;
    &entrada = 1;
    alfa1 = "";
    mes = 0;
    sw_pasa = 0;
    linea = 0;
    FEstado = 0;

    saldo  = 0;
    activo = 0;
    pasivo = 0;
    explo  = 0;
    debe   = 0;
    debe1  = 0;
    debe2  = 0;
    debe3  = 0;
    debe4  = 0;
    haber  = 0;
    haber1 = 0;
    haber2 = 0;
    haber3 = 0;
    haber4 = 0;

    x = 0;
    y = 0;
    sw = 0;
    agrupaci = 0;
    epigrafe = 0;
    partida = 0;
    sw_epi = 0;
    letrames = "";
    informe  = "cauxinva";
    d = "A";
    h = "P";
    tipo = "";
    cue_perdi = "";
    dig_perdi = 0;
    act_cuent = "";
    act_nivel = 0;
    sw_error  = 0;

    &eldesde = 0;
    &elhasta = 0;

    &sw_inf = 0;
    &cantidad = 0;
    &variable = "";

    &canti = 0;
    &doh = "";

    des_exi = 0;
    has_exi = 0;
    des_ele = "";
    has_ele = "";
    swt_amort = 0;

    descar = 0;
    import = 0;

    &elem  = "";
    &desel = "";
    &can   = 0;

    &aux_letra = 0;
    &aux_desc  = "";
    &aux_imp   = 0;
    mensaje   = "";

    sw_exp = 0;
    &r_emp = 0;
    &r_per = 0;
    &r_eje = 0;
    &r_nom = "";
    &d_mes = 0;
    &h_mes = 0;
    &estru = 0;
|FIN;

|INCLUYE acceso_i;

|| ========================================================================
||                        CALCULOS PANTALLA
|| ========================================================================

|PROCESO mayus;
  #0Campo = #0Campo > " ";
  |PINTA #0Campo;
|FPRC;

|PROCESO pintames; |TIPO 0;
 x = Campo + 2;                || Coge el campo del nombre del mes
 |SI #0Campo ! 0; |Y #0Campo ! 13;
     mes = #30#11 + (#0Campo - 1);   || Operacion para saber que mes es
 |SINO;
     mes = #0Campo;
 |FINSI;
 |HAZ mesletra;                          || Recoge el mes en letra
 #0x = letrames;
 |PINTA #0x;                       || Pinta el nombre del mes
 |SI Campo = 1; |Y #0#0 > #0#1;
     |MENAV "    Error de Entrada ...";
     |ERROR;
     |ACABA;
 |FINSI;
|FPRC;

|PROCESO pintames2; |TIPO 0;
 |SI #0Campo ! 0; |Y #0Campo ! 13;
      mes = #30#11 + (#0Campo - 1);   || Operacion para saber que mes es
 |SINO;
      mes = #0Campo;
 |FINSI;
 |HAZ mesletra;
 |ATRI I;
 |SI Campo =  9;  |PINTA 1127, letrames;  |FINSI;
 |SI Campo = 10;  |PINTA 1161, letrames;  |FINSI;
 |ATRI 0;
 |SI Campo = 10; |Y #0#9 > #0#10;
     |MENAV "    Error de Entrada ...";
     |ERROR;
     |ACABA;
 |FINSI;
|FPRC;

|PROCESO mesletra;
 |SI mes = 0;  letrames = "Apertura  "; |FINSI;
 |SI mes = 1;  letrames = "Enero     "; |FINSI;
 |SI mes = 2;  letrames = "Febrero   "; |FINSI;
 |SI mes = 3;  letrames = "Marzo     "; |FINSI;
 |SI mes = 4;  letrames = "Abril     "; |FINSI;
 |SI mes = 5;  letrames = "Mayo      "; |FINSI;
 |SI mes = 6;  letrames = "Junio     "; |FINSI;
 |SI mes = 7;  letrames = "Julio     "; |FINSI;
 |SI mes = 8;  letrames = "Agosto    "; |FINSI;
 |SI mes = 9;  letrames = "Septiembre"; |FINSI;
 |SI mes = 10; letrames = "Octubre   "; |FINSI;
 |SI mes = 11; letrames = "Noviembre "; |FINSI;
 |SI mes = 12; letrames = "Diciembre "; |FINSI;
 |SI mes = 13; letrames = "Cierre    "; |FINSI;
|FPRC;

|PROCESO estruct; |TIPO 0;
 |SI #0#6 = 0; |ACABA; |FINSI;
 |ABRE #6;
 #6#0 = #0#6;
 |LEE 001#6=;
 |SI FSalida ! 0;
     |MENAV "    No existe la estructura de descarga.";
     |CIERRA #6;
     |ERROR;
     |ACABA;
 |SINO;
     cue_perdi = #6#2;
     dig_perdi = #6#3;
 |FINSI;
 |CIERRA #6;
|FPRC;

|PROCESO conexi;
  d_mes = #0#9;
  h_mes = #0#10;
  estru = #0#6;
  r_emp = empre;
  r_per = ejerc;
  r_eje = #30#26;
  r_nom = #30#1;
  |SUB_EJECUTA "caexicon.run";
|FPRC;

|| ========================================================================
||                     CALCULOS de FICHEROS AUXILIARES
|| ========================================================================
|PROCESO grabatra;

|SI #1#5 = "D"; |O #1#5 = "H"; |ACABA; |FINSI;

|| ... Graba temporal de Agrupaciones Exp0;
|SI saldo = 0; |ACABA; |FINSI;
  #3#0 = #1#5;
  #3#1 = #1#4;
  #3#2 = #1#6;
  #3#3 = #1#7;
  #3#4 = #1#8;
  |LEE 101#3=;
  |SI FSalida ! 0;
      |DDEFECTO #3;
      #3#0 = #1#5;
      #3#1 = #1#4;
      #3#2 = #1#6;
      #3#3 = #1#7;
      #3#4 = #1#8;
      |HAZ leexplot;
      #3#5 = #2#2;
      #3#6 = #2#4;
      #3#7 = #2#6;
      |GRABA 020#3c;
  |FINSI;
  #3#8 = #3#8 + saldo;
  |GRABA 020#3a;
  |LIBERA #3;

|| ... Graba Auxiliar de Cuentas.
  #5#0 = #1#5;
  #5#1 = #1#4;
  #5#2 = #1#6;
  #5#3 = #1#7;
  #5#4 = #1#8;
  #5#5 = #1#0;
  #5#6 = #1#1;
  #5#7 = #1#2;
  |LEE 000#5=;
  |SI FSalida ! 0;
      |DDEFECTO #5;
      #5#0 = #1#5;
      #5#1 = #1#4;
      #5#2 = #1#6;
      #5#3 = #1#7;
      #5#4 = #1#8;
      #5#5 = #1#0;
      #5#6 = #1#1;
      #5#7 = #1#2;
      |HAZ leexplot;
      #5#8 = #2#2;
      #5#9 = #2#4;
      #5#10 = #2#6;
      #5#11 = 0;
      |GRABA 020#5c;
  |FINSI;

  #5#11 = #5#11 + saldo;
  |GRABA 020#5a;
  |LIBERA #5;

|| ....... compara acumulados amortizaciones;
 |HAZ amortiza;    || lee la cuenta en las fichas de amortizacion
 |SI swt_amort ! 0;
     |SI saldo ! import;
         |SI swt_amort = 1; mensaje = "0000El saldo de la Cuenta:"+#1#0+" No coincide con lo Amortizado"; |FINSI;
         |SI swt_amort = 2; mensaje = "0000El saldo de la Cuenta:"+#1#0+" No coincide con el Desglose"; |FINSI;
         |MENAV mensaje;
     |FINSI;
 |FINSI;
|FPRC;

|PROCESO exis_expl;
  descar = #7des_exi - #7has_exi; || iniciales - finales
  |SI descar = 0; |ACABA; |FINSI;
  |SI descar ] 0;
      act_cuent = #7#5;     || cuenta descarga D
      act_nivel = #7#6;     || digito descarga D
  |SINO;
      act_cuent = #7#8;     || cuenta descarga H
      act_nivel = #7#9;     || digito descarga H
  |FINSI;

  |SI act_cuent = #1#0; |Y act_nivel = #1#1;
      sw_exp = 1;
      |SI descar ] 0;
          debe = debe + descar;
      |SINO;
          haber = haber - descar;
      |FINSI;
       saldo = debe - haber;
  |FINSI;
|FPRC;

|DEFBUCLE exis2;
 #7#1;
 ;
 #0#6, 01;
 #0#6, 99;
 be;
 NULL, exis_expl;
|FIN;

|PROCESO exis_3000;
  sw_exp  = 1;
  saldo   = saldo + #7has_exi;
|FPRC;

|DEFBUCLE exis1;
 #7#2;
 ;
 #0#6, #1#0, #1#1;
 #0#6, #1#0, #1#1;
 e;
 NULL,exis_3000;
|FIN;

|PROCESO calcular;
 saldo   = 0;
 debe    = 0;
 haber   = 0;
 sw_exp  = 0;
 alfa1   = #1#0 % 101;
 |SI alfa1 = "3";                 |BUCLE exis1; |FINSI;
 |SI alfa1 = "6"; |O alfa1 = "7"; |BUCLE exis2; |FINSI;

 |SI sw_exp = 1; |ACABA; |FINSI;

 |PARA x = eldesde; |SI x [ elhasta; |HACIENDO x = x + 3;
       y = x + 1;
       debe  = debe  + #1x;
       haber = haber + #1y;
       saldo = debe - haber;
 |SIGUE;
|FPRC;

|PROCESO grabar;
  |SI #1#0 ! cue_perdi; || no es perdidas y ganancias
      |HAZ calcular;
      |SI saldo ! 0;
          |SI #1#5 = "H"; |O #1#5 = "P";
              saldo = - saldo;
          |FINSI;
          |HAZ grabatra;
          |HAZ sumasaldo;
      |FINSI;
  |FINSI;
|FPRC;

|DEFBUCLE cainvamo0;
 #1#1;
 3;
 "            ", 0, "S";
 "zzzzzzzzzzzz", 9, "S";
 be;
 NULL, grabar;
|FIN;

|| ................................................

|PROCESO sumasaldo;    || Calculamos cada cuenta
 |SI #1#5 = "A"; activo = activo + saldo; |FINSI;
 |SI #1#5 = "P"; pasivo = pasivo + saldo; |FINSI;
 |SI #1#5 = "D";
     |SI #1#4 = 1;
         debe1 = debe1 + saldo;
     |FINSI;
     |SI #1#4 = 2;
         debe2 = debe2 + saldo;
     |FINSI;
     |SI #1#4 = 3;
         debe3 = debe3 + saldo;
     |FINSI;
     |SI #1#4 = 4;
         debe4 = debe4 + saldo;
     |FINSI;
 |FINSI;
 |SI #1#5 = "H";
     |SI #1#4 = 1;
         haber1 = haber1 + saldo;
     |FINSI;
     |SI #1#4 = 2;
         haber2 = haber2 + saldo;
     |FINSI;
     |SI #1#4 = 3;
         haber3 = haber3 + saldo;
     |FINSI;
     |SI #1#4 = 4;
         haber4 = haber4 + saldo;
     |FINSI;
 |FINSI;
|FPRC;

|| ..................................

|PROCESO gr_final; || Grabar totales
 debe = 0;
 haber = 0;
 #1#0 = cue_perdi;
 #1#1 = dig_perdi;
 |LEE 001#1=;
 |SI FSalida ! 0;
     sw_error = 1;
     |MENAV "    ATENCION !!!. NO existe la cuenta de Perdidas y Ganancias ";
     |ACABA;
 |FINSI;
 |PARA x = eldesde; |SI x [ elhasta; |HACIENDO x = x + 3;
       y = x + 1;
       debe  = debe  + #1x;
       haber = haber + #1y;
       saldo = debe - haber;
 |SIGUE;
 debe  = debe1  + debe2  + debe3  + debe4;
 haber = haber1 + haber2 + haber3 + haber4;
 saldo = saldo + ( debe - haber);
 |SI #1#5 = "P";
     saldo = - saldo;
 |FINSI;
 |HAZ grabatra; || ....................... Graba la cuenta 129. como otra mas.
 |SI #1#5 = "A"; activo = activo + saldo; |FINSI;
 |SI #1#5 = "P"; pasivo = pasivo + saldo; |FINSI;
|FPRC;

|| .......................................
|PROCESO leexplot; || lee t¡tulos balance
  #2#0 = #3#0;
  #2#1 = #3#2;
  #2#3 = #3#3;
  #2#5 = #3#4;
  |LEE 040#2=; |SI FSalida ! 0; |DDEFECTO #2; |FINSI;
|FPRC;

|| ***************************************************************************
|| *********************  CALCULO DE IMPRESION *******************************
|| ***************************************************************************
|PROCESO mira_aux;
 |SI swt_amort = 0;
     sw_inf = 10;
     aux_letra = 2;
     aux_desc = "Descripcion                                    Importe";
     swt_amort = 2;
     aux_imp = 0;
     |HAZ graba_aux;
     |SI #0#11 = "S"; |PRINT; |FINSI;
 |FINSI;
     can   = #12#6;
     desel = #12#3;
     elem  = "";
     aux_letra = 5;
     aux_desc  = elem + desel;
     aux_imp   = #12#6;
     |HAZ graba_aux;
     |SI #0#11 = "S"; |PRINT; |FINSI;
|FPRC;

|PROCESO imp_amort;
 |SI swt_amort = 0;
     swt_amort = 1;
     sw_inf = 10;
     aux_letra = 2;
     aux_desc = "Descripcion                                    Importe";
     aux_imp = 0;
     |HAZ graba_aux;
     |SI #0#11 = "S"; |PRINT; |FINSI;
  |FINSI;
     sw_inf = 9;
     can   = #10#7;
     elem  = #10#0 + "/"+#10#1;
     desel = #10#2;
     aux_letra = 5;
     aux_desc  = elem + "  " + desel;
     aux_imp   = #10#7;
     |HAZ graba_aux;
     |SI #0#11 = "S"; |PRINT; |FINSI;
|FPRC;

|DEFBUCLE amort1;
 #10#4;
 22;
 #5#5, #5#6, des_ele;
 #5#5, #5#6, has_ele;
 be;
 NULL, imp_amort;
|FIN;

|PROCESO busca_amort;
 swt_amort = 0;
 import  = 0;
 |BUCLE amort1;

 |SI swt_amort = 0;
     #11#0 = #5#5;
     #11#1 = #5#6;
     |LEE 001#11=;
     |SI FSalida = 0;
         |BUCLELIN 2mira_aux#11;
     |FINSI;
 |FINSI;
|FPRC;

|PROCESO cuentas;
 |HAZ busca_amort;

 |SI #5#11 ! 0;
     cantidad = #5#11;
     sw_inf = 6;
     aux_letra = 3;
     aux_desc = #5#5 + #5#7;
     aux_imp = cantidad;
     |HAZ graba_aux;
     |SI #0#11 = "S"; |PRINT; |FINSI;
 |FINSI;
|FPRC;

|DEFBUCLE cainvamo2;
 #5#1;
 ;
 #3#0, #3#1, #3#2, #3#3, #3#4, "            ", 0;
 #3#0, #3#1, #3#2, #3#3, #3#4, "zzzzzzzzzzzz", 9;
 e;
 NULL, NULL, NULL, NULL, NULL, cuentas;
 #5#5, #5#6;
|FIN;

|| *****************************************************************

|PROCESO canv_tipo;
  sw_epi = 0;
  tipo     = #3#0;
  agrupaci = #3#2;
  epigrafe = #3#3;
  partida  = 0;
  |SI #3#0 = "A"; doh = "ACTIVO"; |FINSI;
  |SI #3#0 = "P"; doh = "PASIVO"; |FINSI;

      aux_letra = 0;    || Graba Cabecera ACTIVO/PASIVO
      aux_desc = doh;
      aux_imp = 0;
      |HAZ graba_aux;
|FPRC;

|| ----- IMPRESION
|PROCESO impresio;

  |SI sw = 0;
      |HAZ canv_tipo;
      |HAZ agrupacion;
      sw = 1;
  |FINSI;

  |SI tipo ! #3#0;
      |HAZ final;
      |HAZ canv_tipo;
      |HAZ agrupacion;
  |FINSI;

  |HAZ epigraf;
|FPRC;

|PROCESO epigraf;
 |SI agrupaci ! #3#2;
     |HAZ gr_blanco;
     |HAZ agrupacion;
     agrupaci = #3#2;
 |FINSI;
 |HAZ rotura;
 sw_epi = 0;
 epigrafe = #3#3;
|FPRC;

|PROCESO rotura;
  |SI epigrafe ! #3#3; |O sw_epi = 1;
      |SI #3#3 ! 0;
          sw_inf = 1;  || epigrafe  ... | sin importe

          aux_letra = 1;
          aux_desc = #3#6;
          aux_imp = 0;
          |HAZ graba_aux;
          |SI #0#11 = "S"; |PRINT; |FINSI;
      |FINSI;
  |FINSI;
  |HAZ partida;                                       || partida
  |HAZ detalle;                                       || cuentas
|FPRC;

|PROCESO agrupacion;
  sw_epi = 1;                || fuerza la impresion del epigrafe
  |SI #3#3 = 0;              || epigrafe=0
      sw_inf = 7;              || agrupacion (con importe)
      aux_letra = 9;
      aux_desc = #3#5;
      aux_imp = #3#8;
      |HAZ graba_aux;
      |SI #0#11 = "S"; |PRINT; |FINSI;
  |SINO;
      sw_inf = 0;            || agrupacion (sin importe)
      aux_letra = 9;
      aux_desc = #3#5;
      aux_imp = 0;
      |HAZ graba_aux;
      |SI #0#11 = "S"; |PRINT; |FINSI;
  |FINSI;
|FPRC;

|PROCESO partida;        || imprime titulo de partidas
  |SI #3#4 ! 0;
      sw_inf = 3;
      aux_letra = 1;
      aux_desc = #3#7;
      aux_imp = #3#8;
      |HAZ graba_aux;
      |SI #0#11 = "S"; |PRINT; |FINSI;
  |FINSI;
|FPRC;

|PROCESO detalle;        || imprime detalle de cuentas
  |BUCLE cainvamo2;
  aux_letra = 8;
  aux_desc = "...................................................";
  aux_imp = 0;
  |HAZ graba_aux;
  |HAZ gr_blanco;
|FPRC;

|PROCESO final;
 |SI tipo = "A";
     sw_inf = 6;
     variable = "T O T A L   A C T I V O ..............................:";
     canti = activo;
 |SINO;
     sw_inf = 6;
     variable = "T O T A L   P A S I V O ..............................:";
     canti = pasivo;
 |FINSI;

 aux_letra = 6;
 aux_desc = variable;
 aux_imp = canti;

 |HAZ graba_aux;
 aux_letra = 7;
 aux_desc = "";
 aux_imp = 0;
 |HAZ graba_aux;
 |SI #0#11 = "S";
     |PIEINF;
 |FINSI;
|FPRC;

|| ****************************************************************
|DEFBUCLE cainvamo1;
 #3#1;
 ;
 d, 0, 00, 00, 00;
 h, 0, 99, 99, 99;
 be;
 NULL, impresio;
|FIN;

|| *************************************************************************
|PROCESO impre; |TIPO 3;
 |HAZ conexi;

 |SI #0#11 = "S";   || Impresion Directa
     |IMPRE 0;
     |SI FSalida ! 0; |ACABA; |FINSI;
     |INFOR informe;
     |SI FSalida ! 0; |FINIMP; |ACABA; |FINSI;
 |FINSI;

 || ... borrar los ficheros auxiliares
 |ABRE #3;
 |CIERRA #3;
 |DELINDEX #3; || -> Total epigrafes
 |ABRE #5;
 |CIERRA #5;
 |DELINDEX #5; || -> Total por cuentas
 || ....................................

  eldesde = 9 + #0#0 * 3;
  elhasta = 9 + #0#1 * 3;
  des_exi =  #0#9  + 11;
  has_exi =  #0#10 + 11;
  activo = 0;
  pasivo = 0;
  explo  = 0;
  debe1  = 0;
  debe2  = 0;
  debe3  = 0;
  debe4  = 0;
  haber1 = 0;
  haber2 = 0;
  haber3 = 0;
  haber4 = 0;

  des_ele = "S";
  |SI #0#13 = "S"; des_ele = "N"; |FINSI;
  has_ele = "S";

||... Modificar Cuentas a balance segun saldo
  r_emp = #48#2;
  r_per = #48#3;
  eldesde = 11 + #0#0 * 3;
  elhasta = 11 + #0#1 * 3;
  |SUB_EJECUTA "carecsig.run";
  eldesde = 9 + #0#0 * 3;
  elhasta = 9 + #0#1 * 3;
|| ---------------------------------------------------------------------
  |ABRET #1;
  |ABRE #2;
  |ABRE #3;
  |ABRE #5;
  |ABRET #10;
  |ABRE #11;

  |BUCLE cainvamo0;      || Bucle de Calculo todas las cuentas
  |HAZ gr_final;         || Graba Totales

  |CIERRAT #1;
  |CIERRA #2;
  |CIERRA #3;
  |CIERRA #5;
  |CIERRAT #10;
  |CIERRA #11;
|| ************************************************************************

 |SI sw_error = 0;
     |ABRE #20;     || Crear Fichero
     |CIERRA #20;
     |DELINDEX #20; || -> Lineas balance
     |HAZ cabecera; || graba cabecera del informe
     |ABRE #20;
     #20#0 = 0;

     |ABRE #11;
     |BUCLE cainvamo1; || Imprimir y Grabar
     |HAZ final;
     |SI #0#11 = "S";
         |FININF;
         |FINIMP;
     |FINSI;
     |CIERRA #11;
     |CIERRA #20;
 |FINSI;

 |DELINDEX #3;
 |DELINDEX #5;
|FPRC;

|| **********************************************************************

|PROCESO ver_amort;
     swt_amort = 1;
     import  = import + #10#7;
|FPRC;

|DEFBUCLE amort;
 #10#4;
 22;
 #1#0, #1#1, des_ele;
 #1#0, #1#1, has_ele;
 be;
 NULL, ver_amort;
|FIN;

|PROCESO amortiza;  || Busca la cuenta en Amortizaciones y Desglose
 swt_amort = 0;
 import  = 0;
 |BUCLE amort;

 |SI swt_amort = 1; |ACABA; |FINSI;

 |DDEFECTO #11;
 #11#0 = #1#0;
 #11#1 = #1#1;
 |LEE 001#11=;
 |SI FSalida = 0;
     swt_amort = 2;
     import  = #11#3;
 |FINSI;
|FPRC;

|| ********************** Ficheros Auxiliares del Balance Inventario
|PROCESO gr_blanco;
     #20#0 = #20#0 + 10;
     #20#1 = 4;
     #20#2 = "";
     #20#3 = 0;
     |GRABA 021#20n;
     |LIBERA #20;
     sw_inf = 4;
     |SI #0#11 = "S";
         |PRINT;
     |FINSI;
|FPRC;

|PROCESO graba_aux;
     #20#0 = #20#0 + 10;
     #20#1 = aux_letra;
     #20#2 = aux_desc;
     #20#3 = aux_imp;
     |GRABA 021#20n;
     |LIBERA #20;
|FPRC;

|PROCESO cabecera;
 |ABRE #21;
 |CIERRA #21;
 |DELINDEX #21; || -> Cabecera balance

 |ABRE #21;
   |DDEFECTO #21;
   #21#0 = "A";
   #21#1 = #0#7;
   #21#2 = #0#8;
   #21#3 = "";
   #21#4 = " Empresa ..: " + #30#1 + "    Ejercicio: " + #30#26;
   #21#5 = " Acumulados: " + #0#2 + "      -" + #0#3 + "                 Fecha ...: " + #0#12;
   #21#6 = "";
   |GRABA 021#21n;
   |LIBERA #21;
 |CIERRA #21;
|FPRC;
