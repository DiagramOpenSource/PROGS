|FICHEROS;
  caborasi #0;
  camaasil #1;    || apuntes lins.
  camaasic #2;    || apuntes cabs.
  cacuenta #3;    || plan contable
  catradia #5;    || trabajo actualizacion
  camandia #10;   || diarios
  caivasop #6;
  caivarep #7;
  camancob #8;
  camanpag #9;
  calicont #31;
|FIN;

|VARIABLES;
  &modcon = 0;
  cam = 0;
  alfa1 = "";
  desdi = 0;
  hasdi = 0;
  desfe = @;
  hasfe = @;
  desdo = 0;
  hasdo = 0;
  ini = 0;
  fecalf = "";
  mes = "";
  mesfec = 0;
  mesac = 0;
  a = 0;
  b = 0;
  c = 0;
  temporal = "";
  mensa1 = "    ERROR ESTA FECHA NO CORRESPONDE AL EJERCICIO";
  fec1c = @;
  fec2c = @;
  cierr = "";
  solo = 0;
  &entrada = 1;
  juan = 0;
  errant = 0;
  cuini = "            ";
  cufin = "999999999998";
  diini = 0;
  difin = 9;
  fecini = @;
  fecfin = @;
  tiini = "D";
  tifin = "H";
  diain = 0;
  diafi = 99;
  prim = 0;
  cuant = "";
  lon = 0;
  aa = 0;
  mensa = "";
  aar = "";
  traba = "";
  errcuent = "aaaaaaaaaaaa";
  mensa2   = "     No esta definida esta cuenta en Cuentas de Balance";
  uno = "";
  dos = "";
  mascara = "";
  nivel = 0;
|FIN;

|INCLUYE acceso_i;

|| *************************************************************************
||                          DESDE CAMPOS DE PANTALLA
|| *************************************************************************
|PROCESO defectos; |TIPO 7;
  #0#1 = fec1;
  #0#10 = fec2;
  |PINTA #0#1;
  |PINTA #0#10;
|FPRC;

|PROCESO mayor;
  alfa1 = #0Campo > " ";
  #0Campo = alfa1;
  |PINTA #0Campo;
|FPRC;

|PROCESO fecha; | TIPO 0;   || Desde/hasta fecha
  |SI #0Campo < fec1; |O #0Campo > fec2;
      |MENAV mensa1;
      |ERROR;
  |FINSI;
|FPRC;

|PROCESO diarh; | TIPO 0;   ||  hasta diario
  |SI #0#9 < #0#0;  |ERROR;  |FINSI;
|FPRC;


|PROCESO fechah; | TIPO 0;   ||  hasta fecha
  |SI #0#10 < #0#1;  |ERROR;  |FINSI;
|FPRC;

|PROCESO documh; | TIPO 0;   ||  hasta documento
  |SI #0#3 < #0#2;  |ERROR;  |FINSI;
|FPRC;

|| *************************************************************************
||                  ACTUALIZA LAS CUENTAS DE TRABAJO
|| *************************************************************************
|PROCESO a_actualiza;                || rutina 'a_cuentas'
  ini = dig1;
  fecalf = #1#1;
  mes = fecalf % 402;
  mesfec = mes;
  |SI ini = 1;
      mesac = mesfec;
  |SINO;
      mesac = (mesfec - ini) + 1;
      |SI mesac [ 0;
          mesac = (13 - ini) + mesfec;
      |FINSI;
  |FINSI;
  a = (mesac * 3) + 9;
  b = a + 1;
  c = a + 2;
  |SI #1#8 = "D";
      #3a = #3a - #1#9;
      #3#51 = #3#51 - #1#9;
  |SINO;
      #3b = #3b - #1#9;
      #3#52 = #3#52 - #1#9;
  |FINSI;
  #3c = #3a - #3b;
  #3#53 = #3#51 - #3#52;
|FPRC;

|PROCESO actuacuen;
  |SI #1#0 = 0;
      |SI #1#8 = "D";
          #3#9 = #3#9 - #1#9;        || diario cero (saldos iniciales)
          #3#51 = #3#51 - #1#9;
      |SINO;
          #3#10 = #3#10 - #1#9;
          #3#52 = #3#52 - #1#9;
      |FINSI;
      #3#11 = #3#9 - #3#10;
      #3#53 = #3#51 - #3#52;
  |SINO;
      |SI #1#0 = 99;
          |SI #1#8 = "D";
              #3#48 = #3#48 - #1#9;       || diario 99 (saldos finales)
              #3#51 = #3#51 - #1#9;
          |SINO;
              #3#49 = #3#49 - #1#9;
              #3#52 = #3#52 - #1#9;
          |FINSI;
          #3#50 = #3#48 - #3#49;
          #3#53 = #3#51 - #3#52;
      |SINO;
          |HAZ a_actualiza;           || saldos del mes correspondiente
      |FINSI;
  |FINSI;
  |GRABA 020#3a;
  |LIBERA #3;
|FPRC;
|| *************************************************************************

|PROCESO altatra;                  || calculo 'guarda' y 'entmod'
  modcon = 1;               || graba el temporal para la actualiz. de ctas.
  #5#0 = codcon;
  #5#1 = #1#4;
  #5#2 = #1#5;
  #5#3 = #1#1;
  #5#4 = #1#8;
  #5#6 = #1#0;
  |LEE 101#5=;
  |SI FSalida ! 0;
      |DDEFECTO #5;
      #5#0 = codcon;
      #5#1 = #1#4;
      #5#2 = #1#5;
      #5#3 = #1#1;
      #5#4 = #1#8;
      #5#6 = #1#0;
      |GRABA 020#5b;
  |FINSI;
  #5#5 = #5#5 - #1#9;
  |GRABA 020#5a;
  |LIBERA #5;
  cam = 1
|FPRC;
|| **************************************************************************
|PROCESO borrocont;     || contrapartidas
 |BORRA 020#31a;        || borra contrap.
|FPRC;

|PROCESO borrodia;  || trabajo por cada lineas
  #3#0 = #1#4;
  #3#1 = #1#5;
  |LEE 101#3=;
  |SI FSalida = 0;
      |HAZ actuacuen; ||   actualiza saldo en cuenta de trabajo
      |HAZ altatra;   ||   graba trabajo para act. cuentas superiores
  |FINSI;
  |BORRA 020#1a;  ||   borra l¡nea
|FPRC;

|PROCESO borrocab;     || trabajo de cabecera
 |HAZ actua_dia;       || borrar acumulado en diario
 |BORRA 020#2a;        ||   borra cabecera
|FPRC;

|PROCESO actua_dia; || actualiza diario
  #10#0 = #2#0;
  |LEE 101#10=;
  |SI FSalida ! 0;  |ACABA;  |FINSI;
  #10#2 = #10#2 - #2#4;
  #10#3 = #10#3 - #2#5;
  |GRABA 020#10a;
  |LIBERA #10;
|FPRC;

|DEFBUCLE cacierre0;
 #1#1;
 ;
 desdi, desfe, desdo,    1;
 hasdi, hasfe, hasdo, 9999;
 e;
 NULL, borrodia;
|FIN;

|DEFBUCLE cacierre1;
 #2#1;
 ;
 desdi, desfe, desdo;
 hasdi, hasfe, hasdo;
 e;
 NULL, borrocab;
|FIN;

|DEFBUCLE cacierre2;
 #31#1;
 ;
 desdi, desfe, desdo,    1;
 hasdi, hasfe, hasdo, 9999;
 e;
 NULL, borrocont;
|FIN;
|| ---------------------- IVA y Cartera
|PROCESO fuera_cob;
 |BORRA 020#8a;
 |LIBERA #8;
|FPRC;

|DEFBUCLE cobros;
 #8#1;
 ;
 #7#0, #7#2, #7#27,   1;
 #7#0, #7#2, #7#27, 999;
 e;
 NULL, fuera_cob;
|FIN;

|PROCESO borrorep;
  |BUCLE cobros;
  |BORRA 020#7a;
  |LIBERA #7;
|FPRC;

|DEFBUCLE borivarep;
 #7#4;
 ;
 desdi, desfe, desdo;
 hasdi, hasfe, hasdo;
 e;
 NULL, borrorep;
|FIN;

|PROCESO fuera_pag;
 |BORRA 020#9a;
 |LIBERA #9;
|FPRC;

|DEFBUCLE pagos;
 #9#1;
 ;
 #6#0, #6#2, #6#27,  1;
 #6#0, #6#2, #6#27, 999;
 e;
 NULL, fuera_pag;
|FIN;

|PROCESO borrosop;
 |BUCLE pagos;
 |BORRA 020#6a;
 |LIBERA #6;
|FPRC;

|DEFBUCLE borivasop;
 #6#4;
 ;
 desdi, desfe, desdo;
 hasdi, hasfe, hasdo;
 e;
 NULL, borrosop;
|FIN;
|| **************************************************************************
|PROCESO borrado; |TIPO 3;
  |SI #0#8 ! "SI";  |ACABA;  |FINSI;

  || .................. Abre fichero Auxiliar de Cuentas
    |ABRE #5;
    |LEE 101#5u;
    |SI FSalida ! 0;
        |DDEFECTO #5;
        #5#0 = 1;
    |SINO;
        #5#0 = #5#0 + 1;
    |FINSI;
    |GRABA 101#5n;
    codcon = #5#0;
    |LIBERA #5;
  || ----------------------------------------------------------
  |ABRE #3;
  |ABRE #10;
  cam = 0;
  desdi = #0#0;
  hasdi = #0#9;
  desfe = #0#1;
  hasfe = #0#10;
  desdo = #0#2;
  hasdo = #0#3;
  |BUCLE cacierre0;   || borra lineas
  |BUCLE cacierre1;   || borra cabeceras
  |BUCLE cacierre2;   || borra contrapartidas
  |CIERRA #3;
  |CIERRA #5;
  |CIERRA #10;
  |SI #0#4 = "S";
      |BUCLE borivasop;
      |BUCLE borivarep;
  |FINSI;

  || ............... Terminar
  |SI modcon = 0;
      |ABRET #5;
      |DDEFECTO #5;
      #5#0 = codcon;
      |LEE 040#5=;
      |BORRA 101#5a;
      |CIERRAT #5;
  |SINO;
      |CORRE "camaapua.tab"
  |FINSI;
|FPRC;
