|FICHEROS;
    c3gestr6 #0;       || Informe Estadistica 6 y 7
    cacuenta #1;       || Plan contable
    caestr67 #2;       || Fichero para Informe.
    caconimp #8;

    caratios #103;
    caformul #104;
|FIN;

|VARIABLES;
    &pathbal = "";
    &balance = 0;
    &entrada = 1;
    lin = 0;
    sw_error = 0;
    x = 0;
    y = 0;
    z = 0;
    nivelact = 0;
    informe = "caestr67";
    mes = 0;                  || Variable para saber el mes seleccionado
    letrames = "";            || Variable para poner el mes en letra

     &lmes1 = "";
     &lmes2 = "";
     &lmes3 = "";
     t1 = 0;
     t2 = 0;
     t3 = 0;

    error2 = "0000No existe informe";
    error3 = "0000El numero de nivel no coincide con el numero de nivel de la empresa";

    sw = 0;
    rellenos = "";
    swsuma = 0;
    swprint = 0;
    swmov = 0;
    &i_mes1 = 0;
    &i_mes2 = 0;
    &i_mes3 = 0;
    &i_trim = 0;
    &i_trimt= 0;
    &t_mes1 = 0;
    &t_mes2 = 0;
    &t_mes3 = 0;
    &t_trim = 0;
    &t_trimt= 0;
    &tan100 = 0;
    &tan102 = 0;

    &swlinea = 0;      || Pone o no linea en blanco en informe
    &swcomen = 0;
    men2  = "     Longitud de Cuenta Fuera de Nivel !";
    long = "  ";
    cta  = 0;
    num   = 0;
    swdig = 0;
    digito = 0;
    menor = 0;
    para1 = 0;
    nume1 = 0;
    nume2 = 0;
    nume3 = 0;
    nume4 = 0;
    alfa1 = "";
    alfa2 = "";
    alfa3 = "";
    alfa4 = "";
    dos_ant = "xx";
    &pagina = 0;
    sw_pagina = 0;
    &cmcta = "";
    &cmnom = "";
    &cmsal  = 0;
    &cmsal2 = 0;
    &deac = "";
    &titulo = "";
    titul2 = "";
    dif = 0;
    aparam = "";

    i_cm1 = 23;  || Campo Desde/Hasta Empresa
    pregun2 = "2400NCalcular los Ratios de cada Empresa ? ";
|FIN;

|INCLUYE acceso_i;
|INCLUYE digito_i;
|INCLUYE puntos_i;
|INCLUYE calrat_i;
|INCLUYE multie_i;


|| ***********************************************************************
||                       CALCULOS DE PANTALLA
|| ***********************************************************************

|PROCESO &c_pagina;
  |SI sw_pagina = 0;
      pagina = 1;
      sw_pagina = 1;
  |SINO;
      pagina = pagina + 1;
  |FINSI;
|FPRC;

|PROCESO tipo8;  |TIPO 8;
  |HAZ inicio;
  any = #30#26;
  |HAZ coloca;
|FPRC;

|PROCESO mayor;
    alfa1 = #0Campo > " ";
    #0Campo = alfa1;
    |PINTA #0Campo;
 |SI Campo = 20;
     |SI #0Campo = "C";
         |C_M #0#8, 1,"S";
         |C_M #0#21,1,"N";
         #0#21 = 0; |PINTA #0#21;
         |PINTA 1521, "                                    ";
     |SINO;
         |C_M #0#8, 1,"N";
         |C_M #0#21,1,"S";
         #0#8 = "            "; |PINTA #0#8;
         |PINTA 1429, "                                    ";
     |FINSI;
 |FINSI;
|FPRC;

|PROCESO cuenta; |TIPO 6;
 |SI #0#20 = "R"; |ACABA; |FINSI;
    p_alfa1 = #0Campo;
    salidas = FSalida;
|HAZ punto;
|SI p_error ! 0;  |ERROR;  |ACABA;  |FINSI;
|SI #0Campo = "            ";  |ERROR;  |ACABA;  |FINSI;
    |SI Campo = 8;
        digito = 9;
    |SINO;
        digito = Campo + 14;
    |FINSI;
    cta  = #0Campo;
    long = cta % 0;
    swdig = 0;
|SI long = #30#14; swdig = 1; #0digito = 1; |FINSI;
|SI long = #30#15; swdig = 1; #0digito = 2; |FINSI;
|SI long = #30#16; swdig = 1; #0digito = 3; |FINSI;
|SI long = #30#17; swdig = 1; #0digito = 4; |FINSI;
|SI long = #30#18; swdig = 1; #0digito = 5; |FINSI;
|SI long = #30#19; swdig = 1; #0digito = 6; |FINSI;
|SI swdig = 0;
    |MENAV men2;
    |ERROR;
|FINSI;
|FPRC;

|PROCESO nomcuenta; || leer la cuenta a comparar.
 |SI #0#20 = "R", |ACABA; |FINSI;
 |SI FSalida = 2; |ACABA; |FINSI;
  sw_error = 1;
|ABRE #1;
    |DDEFECTO #1;
    #1#0 = #0#8;
    #1#1 = #0#9;
|LEE 001#1=;
   |PINTA 1429, #1#2;
|CIERRA #1;
|FPRC;

|PROCESO mesconta; |TIPO 0;
  x = Campo + 1;                || Coge el campo del nombre del mes
  mes = #0Campo;
  |HAZ mesletra;                      || Recoge el mes en letra
  #0x = letrames;
  |PINTA #0x;                   || Pinta el nombre del mes
|FPRC;

|PROCESO mesletra;
  |SI mes = 1;  letrames = "Enero a Marzo       "; |FINSI;
  |SI mes = 2;  letrames = "Abril a Junio       "; |FINSI;
  |SI mes = 3;  letrames = "Julio a Septiembre  "; |FINSI;
  |SI mes = 4;  letrames = "Octubre a Diciembre "; |FINSI;
|FPRC;

|PROCESO numnivel;|TIPO 0;
  |SI #0#7 > #30#13;
      |MENAV error3;
      |ERROR;
      |ACABA;
  |FINSI;
|FPRC;

|PROCESO leeratio; || Lee el ratio
  |SI FSalida = 2; |ACABA; |FINSI;
  |SI #0#20 = "C"; |ACABA; |FINSI;
  |ABRE #103;
  #103#0 = #0#21;
  |LEE 001#103=;
  |SI FSalida = 0;
      |PINTA 1521, #103#1;
  |SINO;
      |PINTA 1521, "Atencion!. Ratio Inexistente.";
  |FINSI;
  |CIERRA #103;
|FPRC;

|| **********************************************************************
|PROCESO sel_multi;
|| |SI #30#26 ! any;  |ACABA; |FINSI;
  |SI #30#21 = "I"; |ACABA; |FINSI;
  |SI #30#13 < #0#7;  |ACABA;  |FINSI;
  sw_error = 0;

  alfa1 = #30#2;
  alfa2 = #30#3;
  alfa1 = "00000" + alfa1;   alfa1 = alfa1 % -105;
  alfa2 = "0"     + alfa2;   alfa2 = alfa2 % -101;
  dirfich0 = dir_fich + alfa1 + alfa2 + "/";
  |SI #0#20 = "C";
      || ... Busca que exista Cuenta
      |PATH_DAT #1 dirfich0;
      |ABRE #1;
      |DDEFECTO #1;
      #1#0 = #0#8;
      #1#1 = #0#9;
      |LEE 001#1=;
      |SI FSalida ! 0; sw_error = 1; |FINSI;
      |CIERRA #1;
  |SINO;
      || ... Buscar El Ratio
      |PATH_DAT #103 dirfich0;
      |ABRE #103;
      #103#0 = #0#21;
      |LEE 001#103=;
      |SI FSalida ! 0; sw_error = 2; |FINSI;
      |CIERRA #103;
  |FINSI;
||  |SI sw_error = 0; leidos = 1; |FINSI;
  leidos = 1;
|FPRC;

|PROCESO esbuena2;
 |SI FSalida = 2; |ACABA; |FINSI;
 |CAMPO_MODIFICA #0Campo,0,alfa1;
 |SI alfa1 = "N"; |ACABA; |FINSI;
 |SI sw_error !  0; |Y leidos = 1;
     |SI sw_error = 1; |MENAV "    NO EXISTE CUENTA A COMPARAR "; |FINSI;
     |SI sw_error = 2; |MENAV "    NO EXISTE RATIO  A COMPARAR "; |FINSI;
     leidos = 0;
     |ERROR;
 |FINSI;
|FPRC;

|| ***********************************************************************
|| ---------- Seleccion DESDE / HASTA Empresa.
|PROCESO esdesd1;
  alfa1 = #30#2;
  alfa2 = #30#3;
  alfa1 = "00000" + alfa1;   alfa1 = alfa1 % -105;
  alfa2 = "0"   + alfa2;   alfa2 = alfa2 % -101;
  dirfich0 = dir_fich + alfa1 + alfa2 + "/";

  sw_error = 0;
  leidos = 0;
  |HAZ sel_multi;
  |SI leidos = 1; |Y sw_error = 0;
      |HAZ haztodo;
  |FINSI;
|FPRC;

|DEFBUCLE esdesde;
 #30#1;
 ;
 d_empres, d_period;
 h_empres, h_period;
 e;
 NULL,esdesd1;
|FIN;

|| ---------- Seleccion en parrilla.
|PROCESO esunaauna;
  |PARA xx = i_cm6; |SI xx [ i_cm7; |HACIENDO xx = xx + 1;
        |SI #0xx = 0;  |SAL;  |FINSI;
        yy = xx + 20;
        alfa1 = #0xx;
        alfa2 = #0yy;

        || ... lee empresa
           |ABRE #30;
           |DDEFECTO #30;
           #30#2 = #0xx;
           #30#3 = #0yy;
           |LEE 011#30=;
           |CIERRA #30;
           leidos = 0; sw_error = 0;
           |HAZ sel_multi;
           |SI leidos = 0; |O sw_error ! 0; |VETE otramas; |FINSI;
        || ...................

        alfa1 = "00000" + alfa1;   alfa1 = alfa1 % -105;
        alfa2 = "0"     + alfa2;   alfa2 = alfa2 % -101;
        dirfich0 = dir_fich + alfa1 + alfa2 + "/";

        |HAZ haztodo;
 |ET otramas;
  |SIGUE;
|FPRC;

|| ....................... Tipo 3

|PROCESO imprimir; |TIPO 3;
  |SI #0#20 = "R";
      |CONFI pregun2;
      |SI FSalida = 0;
          pregun1 = "2400SDesea calcular el Ratio ? ";
      |FINSI;
  |FINSI;

  |HAZ litmes;

  |IMPRE 0;
  |SI FSalida ! 0;  |ACABA;  |FINSI;

  |INFOR informe;
  |SI FSalida ! 0; |FINIMP; |ACABA; |FINSI;

  |PARA xx = 0; |SI xx [ 21; |HACIENDO xx = xx + 1;
        #2xx = #0xx;
  |SIGUE;

  |SI #0i_cm1 = "S";
      |BUCLE esdesde;
  |SINO;
      |HAZ esunaauna;
  |FINSI;
  |FININF;
  |FINIMP;
|FPRC;

|| **********************************************************************
||                       CALCULOS DE IMPRESION
|| **********************************************************************

|PROCESO condicion;      || Calculo condiciones de impresion
  |HAZ digitos2;
  |SI d_error ! 0;  |ACABA;  |FINSI;
  swprint = 0;         || Variable para saber si imprimimos o no
  swsuma = 0;          || Variable para saber si sumamos o no

|SI #1#55 = #0#7; swprint = 1; swsuma = 1; |FINSI;

|SI #1#3 = "S";|Y #1#55 [ #0#7;    || Si el nivel no esta seleccionado
    swsuma = 1;                    || pero tiene en el campo pase una S
    swprint = 1;                   || entonces se imprime y se suma.
|FINSI;

|SI swprint = 1;    || Si se puede imprimir
    |SI sw = 0;
        nivelact = #1#55;     || Nivel actual
        sw = 1;
    |FINSI;
    |HAZ suma;
    |SI swmov = 0; |Y #0#6 = "N"; || cuentas sin mov.
        |ACABA;
    |FINSI;
    |HAZ tanto100;
    |HAZ operaimpre;
|FINSI;
|FPRC;

|PROCESO operaimpre;     || impresora

|SI nivelact ! #1#55;         || cambio de nivel, linea en blanco
        swlinea = 1;
        nivelact = #1#55;
|SINO;
        swlinea = 0;
|FINSI;

|| SI #0#8 = 1;  |HAZ artenvas; |FINSI;   || separacion lineas formato ARTENVAS

|PRINT;
swcomen = 1
|SI swsuma = 1;     || acumula totales
        t_mes1 = t_mes1 + i_mes1;
        t_mes2 = t_mes2 + i_mes2;
        t_mes3 = t_mes3 + i_mes3;
        t_trim = t_trim + i_trim;
        t_trimt = t_trimt + i_trimt;
|FINSI;
|FPRC;


|PROCESO artenvas;
    alfa1 = #1#0;
    alfa1 = alfa1 % 102;

|SI dos_ant = "xx"; dos_ant = alfa1;    |FINSI;

|SI alfa1 ! dos_ant;
        dos_ant = alfa1;
        swlinea = 1;          || cuando solo se ha seleccionado un nivel y
|SINO;                        ||/cambia en sus dos primeros digitos se
        swlinea = 0;          ||/imprime una linea en blanco!!!
|FINSI;
|FPRC;

|DEFBUCLE caestr670;      || plan contable (3ra. clave)
 #1#3;
 ;
     #0#0, 1;
 rellenos, 6;
 e;
 NULL, condicion;
|FIN;


|PROCESO suma;
 i_mes1 = 0; i_mes2 = 0; i_mes3 = 0;
 |SI #0#17 = "S"; i_mes1 = - #1t1; |FINSI;
 |SI #0#18 = "S"; i_mes2 = - #1t2; |FINSI;
 |SI #0#19 = "S"; i_mes3 = - #1t3; |FINSI;
 i_trim = i_mes1 + i_mes2 + i_mes3;
 i_trimt = 0;
 swmov = 0;
 |PARA x = 9; |SI x [ t3; |HACIENDO x = x + 3;
       y = x + 1;
       z = x + 2;
       |SI z = t1; |Y #0#17 = "N";
           |VETE nosuma;
       |FINSI;
       |SI z = t2; |Y #0#18 = "N";
           |VETE nosuma;
       |FINSI;
       |SI z = t3; |Y #0#19 = "N";
           |VETE nosuma;
       |FINSI;
       |SI #1x ! 0; |O #1y ! 0;  swmov = 1; |FINSI;
       i_trimt =  i_trimt - #1z;
       |ET nosuma;
 |SIGUE;
 |SI #0#4 = "S";
     i_mes1 = i_mes1 / 1000; i_mes1 = i_mes1 ! 0;
     i_mes2 = i_mes2 / 1000; i_mes2 = i_mes2 ! 0;
     i_mes3 = i_mes3 / 1000; i_mes3 = i_mes3 ! 0;
     i_trim = i_trim / 1000; i_trim = i_trim ! 0;
     i_trimt = i_trimt / 1000; i_trimt = i_trimt ! 0;
 |FINSI;
|FPRC;

|PROCESO tanto100;
  tan100 = 0;
  tan102 = 0;
  |SI i_trim > 0; dif = i_trim;   |FINSI;
  |SI i_trim < 0; dif = - i_trim; |FINSI;
  |SI cmsal ! 0;
      |SI i_trim ! 0;
          tan100 = (dif * 100) / cmsal;
      |FINSI;
  |FINSI;
  |SI i_trimt > 0; dif = i_trimt;   |FINSI;
  |SI i_trimt < 0; dif = - i_trimt; |FINSI;
  |SI cmsal2 ! 0;
      |SI i_trimt ! 0;
          tan102 = (dif * 100) / cmsal2;
      |FINSI;
  |FINSI;
|FPRC;

|| ***********************************************************************
||                       CALCULO ENTRADA DE DATOS
|| ***********************************************************************

|PROCESO rellena;
    rellenos = "zzzzzzzzzzzz";
    menor = #0#7;
    |SI menor = 1;  nume1 = dig4;  |FINSI;
    |SI menor = 2;  nume1 = dig5;  |FINSI;   || construye el hasta segun
    |SI menor = 3;  nume1 = dig6;  |FINSI;   ||/el nivel seleccionado
    |SI menor = 4;  nume1 = dig7;  |FINSI;
    |SI menor = 5;  nume1 = dig8;  |FINSI;
    |SI menor = 6;  nume1 = dig9;  |FINSI;
        nume1 = 100 + nume1;
        rellenos = #0#1 % nume1;
        rellenos = rellenos + "zzzzzzzzzzzz";
        rellenos = rellenos % 112;
|FPRC;

|PROCESO lecturas; || Leer el saldo de la cuenta o el ratio
 |SI #0#20 = "C"; |HAZ lectura_cta; |FINSI;
 |SI #0#20 = "R"; |HAZ lectura_ratio; |FINSI;
|FPRC;

|PROCESO lectura_cta; || leer la cuenta a comparar.
  titulo = "CUENTA";
  titul2 = "CTA:";
  sw_error = 1;
|ABRE #1;
    #1#0 = #0#8;
    #1#1 = #0#9;
|LEE 001#1=;
  |SI FSalida ! 0; |ACABA; |FINSI;
  sw_error = 0;
  cmcta = #1#0;
  cmnom = #1#2;
  cmsal = 0;
  cmsal2 = 0;
  |HAZ suma;
  |SI i_trim ] 0; cmsal =   i_trim; cmsal2 =   i_trimt; deac = "ACREEDOR";  |FINSI;
  |SI i_trim < 0; cmsal = - i_trim; cmsal2 = - i_trimt; deac = "DEUDOR";    |FINSI;
|FPRC;

|PROCESO lectura_ratio; || leer el ratio a comparar.
  titulo = "RATIO ";
  titul2 = "RAT:";
  sw_error = 1;
  |ABRE #103;
  #103#0 = #0#21;
  |LEE 001#103=;
  |SI FSalida = 0;
      sw_error = 0;
      deac = "RATIO";
      cmcta = "0000" + #0#21;
      cmcta = cmcta % -104;
      cmnom = #103#1;

      |PTEC 802;
      |HAZ calculo_ratio;

      |SI error = 0;
          |SI  #0#2 = 1; cmsal = #103#5;            cmsal2 = #103#5;  |FINSI;
          |SI  #0#2 = 2; cmsal = #103#8  - #103#5;  cmsal2 = #103#8;  |FINSI;
          |SI  #0#2 = 3; cmsal = #103#11 - #103#8;  cmsal2 = #103#11; |FINSI;
          |SI  #0#2 = 4; cmsal = #103#14 - #103#11; cmsal2 = #103#14; |FINSI;
          |SI #0#4 = "S";
              cmsal  = cmsal  / 1000; cmsal  = cmsal  ! 0;
              cmsal2 = cmsal2 / 1000; cmsal2 = cmsal2 ! 0;
          |FINSI;
      |SINO;
          sw_error = 1;
      |FINSI;
  |FINSI;
  |CIERRA #103;
|FPRC;

|PROCESO litmes;
 |SI #0#2 = 1;
     lmes1 = "     ENERO";
     lmes2 = "   FEBRERO";
     lmes3 = "     MARZO";
     t1 = 14;
     t2 = 17;
     t3 = 20;
 |FINSI;
 |SI #0#2 = 2;
     lmes1 = "     ABRIL";
     lmes2 = "      MAYO";
     lmes3 = "     JUNIO";
     t1 = 23;
     t2 = 26;
     t3 = 29;
 |FINSI;
 |SI #0#2 = 3;
     lmes1 = "     JULIO";
     lmes2 = "    AGOSTO";
     lmes3 = "SEPTIEMBRE";
     t1 = 32;
     t2 = 35;
     t3 = 38;
 |FINSI;
 |SI #0#2 = 4;
     lmes1 = "   OCTUBRE";
     lmes2 = " NOVIEMBRE";
     lmes3 = " DICIEMBRE";
     t1 = 41;
     t2 = 44;
     t3 = 47;
 |FINSI;
 |SI #0#17 = "N"; lmes1 = "          "; |FINSI;
 |SI #0#18 = "N"; lmes2 = "          "; |FINSI;
 |SI #0#19 = "N"; lmes3 = "          "; |FINSI;
|FPRC;

|| ................................................................
|PROCESO haztodo;
  enEmpresa = #canempre#2;
  enPeriodo = #canempre#3;
  swOperacion = 0;
  |SUB_EJECUTA "camoneda.tab";


  |PATH_DAT #1 dirfich0;
  |PATH_DAT #8 dirfich0;
  |PATH_DAT #103 dirfich0;
  |PATH_DAT #104 dirfich0;

  |HAZ borra_var;

  |ABRE #8;
  |LEE 111#8p;
  |SI #8#2 = "S";  |Y #8#3 = "N";  |Y aparam = "";
      pathbal = dirfich0;
      balance = 1;
      |SUB_EJECUTA "careccue.tab";
  |FINSI;
  |CIERRA #8;
  || .... .... .... .... .... .... ....

  |HAZ rellena;
  |HAZ lecturas;
  |SI sw_error = 1; |ACABA; |FINSI;

  |BUCLE caestr670;
  |SI swcomen = 1; |PIEINF; |FINSI;
|FPRC;

|| ----------------------------------------------------------------------
|PROCESO borra_var;
    sw = 0;
    swcomen = 0;
    t_mes1  = 0;
    t_mes2  = 0;
    t_mes3  = 0;
    t_trim  = 0;
    t_trimt = 0;

    cmsal   = 0;
    cmsal2  = 0;
    sw_pagina = 0;

  dig1 = #30#11;       || desde mes
  dig3 = #30#13;       || nro. niveles
  dig4 = #30#14;       || 1 nivel
  dig5 = #30#15;       || 2 nivel
  dig6 = #30#16;       || 3 nivel
  dig7 = #30#17;       || 4 nivel
  dig8 = #30#18;       || 5 nivel
  dig9 = #30#19;       || 6 nivel
|FPRC;
