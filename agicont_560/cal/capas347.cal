|FICHEROS;
  capas347 #0;
  catra347 #1;
  caivasop #2;
  caivarep #3;
  caclipro #4;
  caprovin #6;
  camac347 #10;
  camal347 #11;
  eosclipr #12;
  cacongru #13;
  canempre #30;
  agicontr #23;
|FIN;

|VARIABLES;
  desde = 0;
  hasta = 0;
  modif1 = "";
  modif2 = "";
  xy = 0;
  x = 0;
  y = 0;
  z = 0;
  yz = 0;
  juan = 0;

  direcc = "";
  linea = 0;
  sien = 0;
  alfa1 = "";
  alfa2 = "";
  lonagi = "";
  longi = 0;
  nume1 = 0;
  nume2 = 0;
  nume3 = 0;
  nume4 = 0;

  totalcomp = 0;
  totalvent = 0;
  relcompra = 0;
  relventa  = 0;
  relpagos  = 0;
  relsubv   = 0;
  numpro = 0;
  numcli = 0;
  numpag = 0;
  numsub = 0;
  tipo1 = 0;
  tipo2 = 0;
  total = 0;

  swgr = 0;
  grupo = 0;
  concepto = 0;
  swclipro = 0;

  agi = "";
  &agi1 = "";
  &agi2 = "";
  &aste = "";
  num = 0;
|FIN;

|INCLUYE acceso_i;

|| ************************************************************************

|PROCESO mayor2;
  alfa1 = #0Campo > " ";
  #0Campo = alfa1;
  |PINTA #0Campo;
|FPRC;

|PROCESO mayor1;
  |HAZ mayor2;
  |SI #0Campo = "N";
      |CAMPO_MODIFICA #0#75, 1, "S";
      |CAMPO_MODIFICA #0#76, 1, "S";
  |SINO;
      |CAMPO_MODIFICA #0#75, 1, "N";
      |CAMPO_MODIFICA #0#76, 1, "N";
  |FINSI;
|FPRC;

|PROCESO mayor;
  |HAZ mayor2;
  desde = Campo;
  xy = 34;
  |SI Campo = 79; |O Campo = 99; xy = 18; |FINSI;
  hasta = desde + xy;
  modif1 = "N";
  modif2 = "N"
  |SI #0Campo = "N";
      modif1 = "S";
      modif2 = "N";
  |SINO;
      |SI #0Campo = "S";
          modif1 = "N";
          modif2 = "S"
      |FINSI;
  |FINSI;
  juan = 0;
  |PARA desde = Campo + 1;  |SI desde [ hasta;  |HACIENDO desde = desde + 1;
        juan = juan + 1;
        |SI juan > 2;
            |CAMPO_MODIFICA #0desde, 1,modif1;
            |SI modif1 = "N";
                #0desde = 0;
                |PINTA #0desde;
            |FINSI;
        |SINO;
            |CAMPO_MODIFICA #0desde, 1,modif2;
            |SI modif2 = "N";
                #0desde = 0;
                |PINTA #0desde;
            |FINSI;
        |FINSI;
  |SIGUE;
|FPRC;

|PROCESO saltacon1;
  |SI FSalida = 2; |ACABA; |FINSI;
  x = Campo - 3;
  |SI #0x ! "N"; |ACABA; |FINSI;
  |SI #0Campo = 0;
      |MENAV "    Error!!! Introduzca como minimo  UN CONCEPTO ";
      |ERROR;
  |FINSI;
|FPRC;

|PROCESO saltacon;
  xy = 136; hasta = 144;
  |SI Campo < 136; hasta = 135; xy = 127; |FINSI;
  |SI Campo < 127; hasta = 126; xy = 118; |FINSI;
  |SI Campo < 118; hasta = 117; xy = 99;  |FINSI;
  |SI Campo < 98;  hasta = 97;  xy = 79;  |FINSI;
  |SI Campo < 74;  hasta = 73;  xy = 39;  |FINSI;
  |SI Campo < 39;  hasta = 38;  xy = 4;   |FINSI;
  |SI xy < 118;
      |SI #0xy ! "N"; |ACABA; |FINSI;  || ha pedido desde/hasta
  |SINO;
      |SI #0xy = "N"; |ACABA; |FINSI;  || no tiene Arrendamientos o seguros
  |FINSI;
  |PARA desde = Campo + 1;  |SI desde [ hasta;  |HACIENDO desde = desde + 1;
        |SI #0Campo = 0;
            |CAMPO_MODIFICA #0desde, 1, "N";
            #0desde = 0;
        |SINO;
            |CAMPO_MODIFICA #0desde, 1, "S";
        |FINSI;
  |SIGUE;
|FPRC;

|PROCESO mayor3;
  |HAZ mayor2;
  desde = Campo;
  hasta = desde + 8;
  modif2 = #0Campo;
  |PARA desde = Campo + 1;  |SI desde [ hasta;  |HACIENDO desde = desde + 1;
        |CAMPO_MODIFICA #0desde, 1,modif2;
        |SI modif2 = "N";
            #0desde = 0;
            |PINTA #0desde;
        |FINSI;
  |SIGUE;
|FPRC;

|PROCESO saltacon2; |TIPO 7;
  xy = Campo - 4;
  |SI #0xy = "N"; |ACABA; |FINSI;
  x = Campo - 1;
  y = Campo - 2;
  z = Campo - 3;
  yz = Campo + 4;
|SI #0x ! 0; |O #0y ! 0; |O #0z ! 0;
  |PARA desde = Campo;  |SI desde [ yz;  |HACIENDO desde = desde + 1;
        |CAMPO_MODIFICA #0desde, 1, "N";
        #0desde = 0;
        |PINTA #0desde;
  |SIGUE;
|SINO;
   |PARA desde = Campo;  |SI desde [ yz;  |HACIENDO desde = desde + 1;
         |CAMPO_MODIFICA #0desde, 1, "S";
   |SIGUE;
|FINSI;
|FPRC;

|PROCESO saltagru;
  |SI #0Campo ! 0;  |ACABA;  |FINSI;
  hasta = 139;
  |SI Campo < 131; hasta = 130; |FINSI;
  |SI Campo < 122; hasta = 121; |FINSI;
  |PARA desde = Campo + 1;  |SI desde [ hasta;  |HACIENDO desde = desde + 1;
        |CAMPO_MODIFICA #0desde, 1, "N";
        #0desde = 0;
  |SIGUE;
|FPRC;

|| ************************************************************************

|PROGRAMA;
  |HAZ tipo_8; || controlar si existe la ficha de clientes
  |CLS;
  |CABEZA "Pase a Mant.Modelo 347";
  |ABRE #0;
  |LEE 141#0p;
  |SI FSalida ! 0;
      |DDEFECTO #0;
      |GRABA 001#0b;
  |FINSI;
  |PINPA #0#0;
  |HAZ noexfi; || controlar si existe ficha
  |PINDA #0#0;
  |ENDATOS #2#0;
  |SI FSalida = 0;
      |GRABA 001#0a;
      |SI #0#77 = "S"; || Agrupados por Cuentas Contables
          |HAZ elpase;
      |SINO;
          |CIERRA #0;
          |CORRE "capas348";
      |FINSI;
  |FINSI;
  |CIERRA #0;
|FPRO;

|| ************************************************************************
|| ****************** CREAR FICHERO DE LINEA DEL 347 *********************
|| ***********************************************************************

|PROCESO lineas;
  total = #1#2 + #1#5 + #1#6;
  x = 2; |SI #1#4 = 2; x = 1;  |FINSI;
         |SI #1#4 = 3; x = 78; |FINSI;
         |SI #1#4 = 5; x = 98; |FINSI;

  |SI total < #0x;  |ACABA;  |FINSI; || no es superior a la cantidad indicada

  |SI #0#74 = "S"; |Y aste ! "*";
      |HAZ leeagicli;
  |SINO;
      |HAZ leeclipro;
  |FINSI;

|SI #1#2 ! 0;
    #11#0 = cod1;
    linea = linea + 1;
    #11#1 = linea;
    #11#2 = #1#4;
    #11#5 = #1#2;
    #11#13 = "N";
    #11#14 = "N";
    #11#15 = "N";
    |GRABA 011#11n;
    |LIBERA #11;
    |HAZ suma;
|FINSI;

|SI #1#5 ! 0;
    #11#0 = cod1;
    linea = linea + 1;
    #11#1 = linea;
    #11#2 = #1#4;
    #11#5 = #1#5;
    #11#13 = "N";
    #11#14 = "S";
    #11#15 = "N";
    |GRABA 011#11n;
    |LIBERA #11;
    |HAZ suma;
|FINSI;

|SI #1#6 ! 0;
    #11#0 = cod1;
    linea = linea + 1;
    #11#1 = linea;
    #11#2 = #1#4;
    #11#5 = #1#6;
    #11#13 = "S";
    #11#14 = "N";
    #11#15 = "N";
    |GRABA 011#11n;
    |LIBERA #11;
    |HAZ suma;
|FINSI;
|FPRC;

|PROCESO suma;
  |SI #1#4 = 1; numcli = numcli + 1; relventa  = relventa  + #11#5; |FINSI;
  |SI #1#4 = 2; numpro = numpro + 1; relcompra = relcompra + #11#5; |FINSI;
  |SI #1#4 = 3; numpag = numpag + 1; relpagos  = relpagos  + #11#5; |FINSI;
  |SI #1#4 = 5; numsub = numsub + 1; relsubv   = relsubv   + #11#5; |FINSI;
|FPRC;

|DEFBUCLE escribe;
#1#1;
;
"            ",0, 1;
"zzzzzzzzzzzz",9, 9;
e;
NULL,lineas;
|FIN;

|| ***********************************************************************
|| ********************* CREAR FICHERO DE TRABAJO ************************
|| ***********************************************************************

|PROCESO sumacomp;
  tipo1 = 0; || (2) compras
  tipo2 = 0; || (2) normal, (5) arrendamientos
  sien = 0;
|| .................................... Seleccion de Compras
  |SI #0#4 = "S";
      |SI #2#6 ] #0#5;  |Y #2#6 [ #0#6;
          sien = 1;
          tipo1 = 2;
          tipo2 = 2;
      |FINSI;
  |SINO;
      sien = 0;
      |PARA desde = 7;  |SI desde [ 38;  |HACIENDO desde = desde + 1;
            |SI #0desde = 0;  |SAL;  |FINSI;
            |SI #2#6 = #0desde;
                sien = 1;
                tipo1 = 2;
                tipo2 = 2;
                |SAL;
            |FINSI;
      |SIGUE;
  |FINSI;
|| .................................... Seleccion de Arrendamientos
  |SI #0#118 = "S";
      |SI #0#119 ! 0; |O #0#120 ! 0; |Y #0#121 ! 0;
          swgr = 0; concepto = #2#6;
          |SI #0#119 ! 0; grupo = #0#119; |HAZ vergrupo; |FINSI;
          |SI #0#120 ! 0; |Y swgr = 0; grupo = #0#120; |HAZ vergrupo; |FINSI;
          |SI #0#121 ! 0; |Y swgr = 0; grupo = #0#121; |HAZ vergrupo; |FINSI;
          |SI swgr = 1;
              sien = 1;
              tipo1 = 2;
              tipo2 = 5;
          |FINSI;
      |SINO;
          |PARA desde = 122;  |SI desde [ 126;  |HACIENDO desde = desde + 1;
                |SI #0desde = 0;  |SAL;  |FINSI;
                |SI #2#6 = #0desde;
                    sien = 1;
                    tipo1 = 2;
                    tipo2 = 5;
                    |SAL;
                |FINSI;
          |SIGUE;
      |FINSI;
  |FINSI;
|| .................................../ Fin compras.

|SI sien = 0; || No es de Compras.
    concepto = #2#6;
    |HAZ verpagos;
|FINSI;

|SI sien = 1; || Es concepto elegido.
    #1#0 = #2#4;
    #1#1 = #2#5;
    #1#4 = tipo1;
    |LIBERA #1;
    |LEE 101#1=;
    |SI FSalida ! 0;
        #1#0 = #2#4;
        #1#1 = #2#5;
        #1#2 = 0;
        #1#3 = #2#51;
        #1#4 = tipo1;
        #1#5 = 0;
        #1#6 = 0;
        #1#7 = #2#50;
        |GRABA 020#1b;
   |FINSI;
   #1tipo2 = #1tipo2 + (#2#26 + #2#63);
   |GRABA 020#1a;
   |LIBERA #1;
   |SI tipo1 = 2; totalcomp = totalcomp + (#2#26 + #2#63); |FINSI;
|FINSI;
|FPRC;

|DEFBUCLE compras;
#2#1;
;
00,000001, "  ";
99,999999, "zz";
e;
NULL,sumacomp;
|FIN;

|PROCESO sumavent;
  tipo1 = 0; || (1) ventas
  tipo2 = 0; || (2) normal, (5) arrendamientos, (6) seguro
  sien = 0;
|| .................................... Seleccion de Ventas
  |SI #0#39 = "S";
      |SI #3#6 ] #0#40;  |Y #3#6 [ #0#41;
          sien = 1;
          tipo1 = 1;
          tipo2 = 2;
      |FINSI;
  |SINO;
      sien = 0;
      |PARA desde = 42;  |SI desde [ 73;  |HACIENDO desde = desde + 1;
            |SI #0desde = 0;  |SAL;  |FINSI;
            |SI #3#6 = #0desde;
                sien = 1;
                tipo1 = 1;
                tipo2 = 2;
                |SAL;
            |FINSI;
      |SIGUE;
  |FINSI;
|| .................................... Seleccion de Arrendamientos
  |SI #0#127 = "S";
      |SI #0#128 ! 0; |O #0#129 ! 0; |Y #0#130 ! 0;
          swgr = 0; concepto = #3#6;
          |SI #0#128 ! 0; grupo = #0#128; |HAZ vergrupo; |FINSI;
          |SI #0#129 ! 0; |Y swgr = 0; grupo = #0#129; |HAZ vergrupo; |FINSI;
          |SI #0#130 ! 0; |Y swgr = 0; grupo = #0#130; |HAZ vergrupo; |FINSI;
          |SI swgr = 1;
              sien = 1;
              tipo1 = 1;
              tipo2 = 5;
          |FINSI;
      |SINO;
          |PARA desde = 131;  |SI desde [ 135;  |HACIENDO desde = desde + 1;
                |SI #0desde = 0;  |SAL;  |FINSI;
                |SI #3#6 = #0desde;
                    sien = 1;
                    tipo1 = 1;
                    tipo2 = 5;
                    |SAL;
                |FINSI;
          |SIGUE;
      |FINSI;
  |FINSI;
|| .................................... Seleccion de Seguros
  |SI #0#136 = "S";
      |SI #0#137 ! 0; |O #0#138 ! 0; |Y #0#139 ! 0;
          swgr = 0; concepto = #3#6;
          |SI #0#137 ! 0; grupo = #0#137; |HAZ vergrupo; |FINSI;
          |SI #0#138 ! 0; |Y swgr = 0; grupo = #0#138; |HAZ vergrupo; |FINSI;
          |SI #0#139 ! 0; |Y swgr = 0; grupo = #0#139; |HAZ vergrupo; |FINSI;
          |SI swgr = 1;
              sien = 1;
              tipo1 = 1;
              tipo2 = 6;
          |FINSI;
      |SINO;
          |PARA desde = 140;  |SI desde [ 144;  |HACIENDO desde = desde + 1;
                |SI #0desde = 0;  |SAL;  |FINSI;
                |SI #3#6 = #0desde;
                    sien = 1;
                    tipo1 = 1;
                    tipo2 = 6;
                    |SAL;
                |FINSI;
          |SIGUE;
      |FINSI;
  |FINSI;
|| .................................../ Fin ventas.

|SI sien = 0; || No es de Ventas.
    concepto = #3#6;
    |HAZ verpagos;
|FINSI;
|SI sien = 0; || tampoco es un pago.
    concepto = #3#6;
    |HAZ versubve;
|FINSI;

|SI sien = 1; || Es concepto elegido.
    #1#0 = #3#4;
    #1#1 = #3#5;
    #1#4 = tipo1;
    |LIBERA #1;
    |LEE 101#1=;
    |SI FSalida ! 0;
        #1#0 = #3#4;
        #1#1 = #3#5;
        #1#2 = 0;
        #1#3 = #3#51;
        #1#4 = tipo1;
        #1#5 = 0;
        #1#6 = 0;
        #1#7 = #3#50;
        |GRABA 020#1b;
   |FINSI;
   #1tipo2 = #1tipo2 + (#3#26 + #3#63);
   |GRABA 020#1a;
   |LIBERA #1;
   |SI tipo1 = 1; totalvent = totalvent + (#3#26 + #3#63); |FINSI;
|FINSI;
|FPRC;

|DEFBUCLE ventas;
#3#1;
;
00,000001, "  ";
99,999999, "zz";
e;
NULL,sumavent;
|FIN;

|PROCESO verpagos;
 sien = 0;
 tipo1 = 3;
 tipo2 = 2;
 |SI #0#79 = "X"; |ACABA; |FINSI; || no hay Pagos por Mediacion
 |SI #0#79 = "S";
     |SI concepto ] #0#80;  |Y concepto [ #0#81;
         sien = 1;
     |FINSI;
 |SINO;
     sien = 0;
     |PARA desde = 82;  |SI desde [ 97;  |HACIENDO desde = desde + 1;
           |SI #0desde = 0;  |SAL;  |FINSI;
           |SI concepto = #0desde;
               sien = 1;
               |SAL;
           |FINSI;
     |SIGUE;
 |FINSI;
|FPRC;

|PROCESO versubve;
 sien = 0;
 tipo1 = 5;
 tipo2 = 2;
 |SI #0#99 = "X"; |ACABA; |FINSI; || no hay Subvenciones
 |SI #0#99 = "S";
     |SI concepto ] #0#100;  |Y concepto [ #0#101;
         sien = 1;
     |FINSI;
 |SINO;
     sien = 0;
     |PARA desde = 102;  |SI desde [ 117;  |HACIENDO desde = desde + 1;
           |SI #0desde = 0;  |SAL;  |FINSI;
           |SI concepto = #0desde;
               sien = 1;
               |SAL;
           |FINSI;
     |SIGUE;
 |FINSI;
|FPRC;

|| ............... ver grupo;
|PROCESO vergrupo;
 swgr = 0;
 |ABRE #13;
 #13#0 = grupo;
 |LEE 001#13=;
 |SI FSalida = 0;
     |PARA x = 2; |SI x [ 21; |HACIENDO x = x + 1;
           |SI #13x  = concepto;
               swgr = 1;
               |SAL;
            |FINSI;
     |SIGUE;
 |FINSI;
 |CIERRA #13;
|FPRC;
|| ***********************************************************************

|PROCESO elpase;
  |HAZ inicio;
  |ABRE #10;
  |CIERRA #10;
  |DELINDEX #10;
  |ABRE #10;
  |ABRE #11;
  |CIERRA #11;
  |DELINDEX #11;
  |ABRE #11;

  |ABRE #1;
  |CIERRA #1;
  |DELINDEX #1;
  |ABRE #1;

  totalcomp = 0;
  totalvent = 0;
  relventa  = 0;
  relcompra = 0;
  relpagos  = 0;
  relsubv   = 0;

  |BUCLE compras;  || lee el fichero de Compras
  |BUCLE ventas;   || lee el fichero de Ventas

  |CIERRA #1;

  |ABRE #4;
  |ABRE #6;
  |SI #0#74 ="S"; |Y aste ! "*"; |ABRE #12;  |FINSI;
  numpro = 0;
  numcli = 0;
  numpag = 0;
  numsub = 0;
  linea  = 0;

  |BUCLE escribe;

  |CIERRA #1;
  |DELINDEX #1;

  |CIERRA #4;
  |CIERRA #6;
  |CIERRA #11;
  |SI #0#74 ="S"; |Y aste ! "*"; |CIERRA #12;  |FINSI;

|| ... Graba la cabecera del 347
  |ABRE #30;
  #30#2 = cod1;
  #30#3 = cod2;
  |LEE 040#30=;
  |CIERRA #30;
  #10#0  = cod1;
  #10#1  = #30#1;
  #10#2  = "";
  #10#3  = "N";
  #10#16 = " ";
  #10#17 = #30#26;
  #10#4 = totalcomp;
  #10#5 = totalvent;
  #10#6 = numpro;
  #10#7 = relcompra;
  #10#8 = numcli;
  #10#9 = relventa;
  #10#10 = numpag;
  #10#11 = relpagos;
  #10#12 = 0;
  #10#13 = 0;
  #10#14 = numsub;
  #10#15 = relsubv;
  |GRABA 011#10n;
  |LIBERA #10;
  |CIERRA #10;

|FPRC;


|| ........ Lecturas de clientes / proveedores

|PROCESO leeagicli;
  #12#0 = #1#3;
  |LEE 001#12=;
  |SI FSalida = 0;
      #11#3 =  #1#3;    ||dni
      #11#4 =  #12#1;   ||nombre
      #11#6 =  #12#2;   ||CL/AV
      #11#7 =  #12#3;   ||direccion
      #11#8 =  #12#4;   ||numero
      #11#9 =  #12#7;   ||cod.pos -1-
      #11#10 = #12#8;   ||cos.pos -2-
      #11#11 = #12#9;   ||provincia
      #11#12 = #12#10;  ||poblacion
  |SINO;
      |HAZ leeclipro;
  |FINSI;
|FPRC;

|PROCESO leeclipro;
 #4#23 = "C";
 |HAZ clipro;
 |SI swclipro = 0;
     #4#23 = "P";
     |HAZ clipro;
 |FINSI;
 |SI swclipro = 0; || lee el nombre del Plan Contable
     #11#3 =  #1#3;    ||dni
     #11#4 =  #1#7;    ||nombre
     #11#6 =  "";      ||CL/AV
     #11#7 =  "";      ||direccion
     #11#8 =  "";      ||numero
     #11#9 =  0;       ||cod.pos -1-
     #11#10 = 0;       ||cos.pos -2-
     #11#11 = "";      ||provincia
     #11#12 = "";      ||poblacion
 |FINSI;
|FPRC;

|PROCESO clipro;
  swclipro = 0;
  #4#10 = #1#0;
  #4#11 = #1#1;
  |LEE 001#4=;
  |SI FSalida = 0;
      swclipro = 1;
      #11#3 =  #4#9;      ||dni
      #11#4 =  #4#1;      ||nombre
      #11#9 =  #4#3;      ||cod.pos -1-
      #11#10 = #4#4;      ||cos.pos -2-
      #11#12 = #4#6;      ||poblacion
      direcc = #4#2;
      |HAZ separa;
      #6#0 = #11#9;
      |LEE 010#6=;
      |SI FSalida = 0;
          #11#11 = #6#1;
      |SINO;
          #11#11 = "";
      |FINSI;
  |FINSI;
|FPRC;

|PROCESO separa;
  #11#6 = "";
  |SI #0#75 ! 0;
      |QBF direcc;
      alfa1 = direcc % 0;
      longi = alfa1;
      nume1 = #0#75 + 100;
      #11#6 = direcc % nume1;
      nume1 = ((longi - #0#75) + 100) * -1;
      direcc = direcc % nume1;
  |FINSI;
  #11#8 = "";
  |SI #0#76 ! " ";
      |QBF direcc;
      alfa1 = direcc % 0;
      longi = alfa1;
      |PARA nume4 = longi; |SI nume4 > 1; |HACIENDO nume4 = nume4 - 1;
            nume2 = (nume4 * 100) + 1;
            alfa2 = direcc % nume2;
            |SI alfa2 = #0#76;
                nume3 = ((longi - nume4) + 100) * -1;
                #11#8 = direcc % nume3;
                nume2 = (nume4 - 1) + 100;
                direcc = direcc % nume2;
                |SAL;
            |FINSI;
      |SIGUE;
  |FINSI;
  #11#7 = direcc;
|FPRC;

|PROCESO tipo_8; || controlar si existe ficha clientes
  |DIRECTORIO agi;
  |QBF agi;
  |PATH_DAT #23 agi;
|ABRE #23;
#23#0 = 1;
|LEE 040#23=;
|SI FSalida ! 0; aste = "*"; |CIERRA #23; |ACABA; |FINSI;
|CIERRA #23;
aste = #23#7;
|QBF aste;
|SI aste  = "*"; |ACABA; |FINSI;
agi1 = #23#8; |QBF agi1;

lonagi = agi1 % 0;
num   = lonagi;
num   = 100 + (num - 5);
lonagi = num;

agi2  = (agi1 % lonagi) + "pan/";

|PATH_DAT #12 agi1;
|FPRC;

|PROCESO noexfi;
  |SI aste ! "*"; |ACABA; |FINSI;
  #0#74 = "N";
  |CAMPO_MODIFICA #0#74, 1,"N";
|FPRC;
