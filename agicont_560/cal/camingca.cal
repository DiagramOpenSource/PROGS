|FICHEROS;
   camingca #0;
   camingli #1;
   camanban #2;
   camanpag #3;
   camingau #4;
   caclipro #5;

   cacuenta #10;
|FIN;

|VARIABLES;
    &entrada = 1;
    &quepant = 0;
    alfa1 = "";
    alfa2 = "";
    nume1 = 0;
    nume2 = 0;

    modo = 0;
    long      = "  ";
    sw        = 0;
    linea     = 0;

    &contador = 0;
    feini     = @;
    fefin     = @;
    sw_n      = 0;
    sw_error  = 0;
    men       = "";
    desban = 0;
    hasban = 0;
|FIN;

|INCLUYE acces3_i;
|INCLUYE punto2_i;
|INCLUYE i_ficher;

|| **************************************************************************
|PROCESO contador; |TIPO 7;
 modo = (FEntrada / 100) ! 0;
 |SI modo = 1;
     |LEE 040#0u;
     |SI FSalida = 0;
         nume1 = #0#0;
         |DDEFECTO #0;
         #0#0 = nume1 + 1;
     |SINO;
         |DDEFECTO #0;
         #0#0 = 1;
     |FINSI;
 |FINSI;
|FPRC;

|PROCESO siconta; |TIPO 7;
 |SI #0#10 = "S";
     |MENAV "    Confirme ya EMITIDO, no se puede modificar";
     |ERROR;
     |PTEC 807;
 |FINSI;
|FPRC;

|PROCESO cuenta1; |TIPO 6;   || digito cuenta cuenta banco
  p_alfa1 = #0Campo;
  salidas = FSalida;
  |HAZ punto2;
  |SI p_error ! 0;  |ERROR;  |ACABA;  |FINSI;

  nume1 = Campo + 1;
  alfa1 = #0Campo;
  |QBF alfa1;
  alfa1 = alfa1 % 0;
  long  = alfa1;
  sw    = 0;
  #0nume1 = 0;  || inicializa el digito
  |SI long = dig4; |Y dig4 ! 0; sw = 1; #0nume1 = 1; |FINSI;
  |SI long = dig5; |Y dig5 ! 0; sw = 1; #0nume1 = 2; |FINSI;
  |SI long = dig6; |Y dig6 ! 0; sw = 1; #0nume1 = 3; |FINSI;
  |SI long = dig7; |Y dig7 ! 0; sw = 1; #0nume1 = 4; |FINSI;
  |SI long = dig8; |Y dig8 ! 0; sw = 1; #0nume1 = 5; |FINSI;
  |SI long = dig9; |Y dig9 ! 0; sw = 1; #0nume1 = 6; |FINSI;

  |SI sw = 0;|Y long ! 0;
      |MENAV "    Longitud de Cuenta Fuera de Nivel !";
      |ERROR;
  |FINSI;
|FPRC;

|PROCESO cuefin1; |TIPO 0;
 |ABRE #10;
 nume1 = Campo + 1;
 #10#0 = #0Campo;
 #10#1 = #0nume1;
 |LEE 040#10=;
 |SI FSalida ! 0;
     |MENAV "    Error No Existe Cuenta Contable ";
     |ERROR;
 |SINO;
     |SI #10#3 = "N";
         |MENAV "      La cuenta no tiene pase DIRECTO ";
         |ERROR;
     |FINSI;
 |FINSI;
 |CIERRA #10;
|FPRC;

|| ************************************************************************
|PROCESO grabali;
 linea = linea + 1;
 #1#0  = #0#0;        || Confirming
 #1#1  = linea;       || Linea
 #1#2  = #4#1;        || Libro
 #1#3  = #4#2;        || Orden
 #1#4  = #4#8;        || Serie
 #1#5  = #4#3;        || N§ Pago
 #1#6  = #4#9;        || Fecha Emision
 #1#7  = #4#4;        || Fecha Vto.
 #1#8  = #4#5;        || Importe
 #1#9  = #4#10;       || Cta Proveedor
 #1#10 = #4#11;       || Digito
 #1#11 = #4#15;       || Apdo. Correos
 #1#12 = #4#14;       || Forma de Pago
 #1#13 = #4#13;       || Nompre Proveedor
 |HAZ calculo;
 |GRABA 020#1n;
 sw_n = 1;
|FPRC;

|DEFBUCLE camingau;
 #4#1;
 7;
 000000,   "*";
 contador, "*";
 e;
 NULL, grabali;
|FIN;

|PROCESO linealin;
 linea = #1#1;
|FPRC;

|PROCESO introduc;
 linea = 0;
 |BUCLELIN 0linealin#0;
 |ABRE #1;
 |BUCLE camingau;
 |CIERRA #1;
|FPRC;

|| ............................................
|PROCESO pasalo;
 contador = contador + 1;
 #4#0  = contador;             || Contador
 #4#1  = #3#0;                 || Libro
 #4#2  = #3#1;                 || Orden
 #4#3  = #3#3;                 || N§ Pago
 #4#4  = #3#7;                 || Fecha Vto.
 #4#5  = #3#8 - #3#23;         || Importe
 #4#6  = #3#11;                || Tipo Recibo
 #4#7  = "";                   || Marcar
 #4#8  = #3#2;                 || Serie
 #4#9  = #3#6;                 || Fecha Emision
 #4#10 = #3#4;                 || Proveedor
 #4#11 = #3#5;                 || Digito
 #4#12 = #3#8;                 || Importe Recibo Original
 |HAZ leecuenta;
 #4#13 = #5#1;                 || Nombre Proveedor
 #4#14 = "C";                  || Forma Pago
 #4#15 = #5#27;                || Apdo Correos
 #4#16 = #3#26;                || N§ de Banco
 |GRABA 040#4n;
|FPRC;

|DEFBUCLE camanpag;
 #3#3;
 11, 22, 26;
 #0#2,  feini, #0#7, "N", desban;
 fefin, fefin, #0#7, "N", hasban;
 e;
 NULL, pasalo;
|FIN;

|PROCESO automatic; |TIPO 0;

 |SI FSalida = 2; |ACABA; |FINSI;
 |CONFI "2400SConfirme Automatico: ";
 |SI FSalida ! 0; |ACABA; |FINSI;

 |CONFI "2400NBuscar todos los Bancos: ";
 |SI FSalida = 0;
     desban = 0;
     hasban = 999;
 |SINO;
     desban = #0#3;
     hasban = #0#3;
 |FINSI;
 comienza = "p";
 |HAZ fichero;
 |NOME_DAT #4 fichero;
 |ABRE #4;
 |CIERRA #4;
 |DELINDEX #4;

 |ABRE #4;
 sw_n = 0;
 contador = 0;
 feini = "01.01.0001";
 fefin = "01.01.2999";
 |BUCLE camanpag;
 |CIERRA #4;
 |SI contador  ! 0;
     |SUB_EJECUTA "camingau.tab";
     |HAZ introduc;
     |SI sw_n = 1;
         |PTEC 815;
         |PTEC 808;
         |PTEC 815;
         |PTEC 808;
    |FINSI;
 |SINO;
    |MENAV "      No hay pagos para el Confirme ";
    |ERROR;
 |FINSI;
 |DELINDEX #4;
|FPRC;
|| ************************************************************************

|| ************************************************************************
|| ************ Calculos de Lineas
|| ************************************************************************

|PROCESO mayus;
 #1Campo = #1Campo > " ";
 |PINTA #1Campo;
|FPRC;

|PROCESO remesado; |TIPO 0;
 sw_error = 0;
 |ABRE #3;
 #3#0 = #1#2;
 #3#1 = #1#3;
 #3#2 = #1#4;
 #3#3 = #1#5;
 |LEE 040#3=;
 |SI FSalida ! 0;
     |CIERRA #3;
     sw_error = 1;
     |MENAV "    No existe pago !!! ";
     |ERROR;
     |ACABA;
 |FINSI;

 |SI #0#7 ! #3#11;
    sw_error = 1;
    men = "    El tipo recibo del Pago no coincide con el del Confirme";
 |FINSI;
 |SI #3#22 = "S";
     sw_error = 1;
     men = "     Pago ya liquidado !!! ";
 |FINSI;
 |SI #3#12 ! 0;
     sw_error = 1;
     men = "     Pago asignado a un Pagare !!! ";
 |FINSI;
 |SI #3#25 ! 0;
     sw_error = 1;
     men = "     Pago asignado a un Confirme !!! ";
 |FINSI;

 |CIERRA #3;

 |SI sw_error = 1;
     |MENAV men;
     |ERROR;
     |ACABA;
 |FINSI;

 modo = (FEntrada / 100) ! 0;
 |SI modo = 1;
     |SI #0#3 ! #3#26; |O #0#2 ! #3#7;
         |SI #0#3 ! #3#26;
             men = "2400NEl Banco no coincide con el del Confirme. Aceptarlo ? ";
         |SINO;
             men = "2400NLa Fecha Vto. es diferente a la del Confirme. Aceptarlo ? ";
         |FINSI;
         |CONFI men;
         |SI FSalida ! 0;
             |ERROR;
             |ACABA;
         |FINSI;
     |FINSI;
 |FINSI;

 #1#6  = #3#6;
 #1#7  = #3#7;
 #1#8  = #3#8 - #3#23;
 #1#9  = #3#4;
 #1#10 = #3#5;
 |HAZ leecuenta;
 #1#13 = #5#1;                || Nombre Proveedor
 #1#11 = #5#27;               || Apartado Correos
 |PINDA #0#1;
|FPRC;

|| ............... Proceso de Actualizacion

|PROCESO actuali; |TIPO 2;
 nume1 = 0 +.1;
 |SI nume1 = 1;
     |HAZ calculo;
 |SINO;
     |HAZ calbaja;
 |FINSI;
|FPRC;

|PROCESO calculo;  || ... Actualiza en Alta
 |ABRE #3;
 #3#0 = #1#2;
 #3#1 = #1#3;
 #3#2 = #1#4;
 #3#3 = #1#5;
 |LEE 110#3=;
 |SI FSalida = 0;
     #3#25 = #1#0;      || N§ Confirming
     #3#22 = "S";       || Liquidado
     #3#23 = #3#23 + #1#8;
     #3#26 = #0#3;
     |GRABA 020#3a;
 |FINSI;
 |CIERRA #3;

 #0#8 = #0#8 + 1;
 #0#9 = #0#9 + #1#8;

 |PINTA #0#8;
 |PINTA #0#9;
|FPRC;

|PROCESO calbaja;  || ... Actualiza en Baja
 |ABRE #3;
 #3#0 = #1#2;
 #3#1 = #1#3;
 #3#2 = #1#4;
 #3#3 = #1#5;
 |LEE 110#3=;
 |SI FSalida = 0;
     #3#25 = 0;      || N§ Confirming
     #3#22 = "N";       || Liquidado
     #3#23 = #3#23 - #1#8;
     |GRABA 020#3a;
 |FINSI;
 |CIERRA #3;

 #0#8 = #0#8 - 1;
 #0#9 = #0#9 - #1#8;

 |PINTA #0#8;
 |PINTA #0#9;
|FPRC;

|PROCESO leecuenta;
 |ABRE #5;
 #5#23 = "P";
 #5#10 = #3#4;
 #5#11 = #3#5;
 |LEE 040#5=;
 |SI FSalida ! 0;
     |DDEFECTO #5;
     #5#23 = "C";
     #5#10 = #3#4;
     #5#11 = #3#5;
     |LEE 040#5=;
     |SI FSalida ! 0;
         |DDEFECTO #5;
         |ABRE #10;
         #10#0 = #3#4;
         #10#1 = #3#5;
         |LEE 040#10=;
         |SI FSalida ! 0;
             |DDEFECTO #10;
         |FINSI;
         #5#1 = #10#2;
         |CIERRA #10;
     |FINSI;
 |FINSI;
 |CIERRA #5;
|FPRC;
