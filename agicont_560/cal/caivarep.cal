|FICHEROS;
    caivarep #0;
    cacuenta #4;
    caclipro #5;
    calibros #10;
|FIN;

|VARIABLES;
    &entrada = 1;
    potra = 0;
    mensa1 = "    ERROR ESTA FECHA NO CORRESPONDE AL EJERCICIO";
    men1  = "     NO EXISTE EMPRESA !";
    men2  = "     Longitud de Cuenta Fuera de Nivel !";
    men3  = "      Esta Cuenta NO tiene Pase directo";
    cont = 0;
    long = "  ";
    cta  = 0;
    num   = 0;
    sw = 0;
    xy = 0;
    traba1 = "";
    traba2 = "";
    trasum = 0;
    juan = 0;
    jose = 0;
    pepe = 0;
    rafa = 0;
    anter = 0;
    antnum = 0;
    seran = "";

    &libro = 0;
    &orden = 0;
    &serie = "";
    &pesetas = 0;
    &cuenta = "";
    &digito = 0;
    &emision = "";
    alfa1 = "";
    alfa2 = "";
    alfa3 = "";
    alfa4 = "";
    nume1 = 0;
    nume2 = 0;
    nume3 = 0;
    nume4 = 0;
    trim1 = @;
    trim2 = @;
    trim3 = @;
    mas = 0;
    modo = 0;

     letrasnif = "TRWAGMYFPDXBNJZSQVHLCKE";
     letra = "";
     final = "";
     x = "";
     lugar = 0;
     nifnum = 0;
     resto = 0;

  &fec_am = @;
  &imp_am = 0;
  &c_am = "";
  swf1 = 0;
|FIN;

|INCLUYE acceso_i;
|INCLUYE cobros_i;
|INCLUYE puntos_i;
|INCLUYE caliva_i;

|PROCESO mayusc;
  #0Campo = #0Campo > " ";
  |PINTA #0Campo;
|FPRC;

|PROCESO entrada; |TIPO 7;       || la serie se trata como blanco
    modo = (FEntrada / 100) ! 0;
|SI modo = 1;
        mas = 1;
        anter = #0#0;
        #0#2 = 999999;
    |LEE 001#0];
    |SI FSalida = 0;
        |LEE 001#0a;
        |SI FSalida ! 0; |O anter ! #0#0;
                mas = 0;
            |DDEFECTO #0;
        |FINSI;
            antnum = #0#2;
    |SINO;
        |LEE 001#0u;
        |SI FSalida ! 0; |O anter ! #0#0;
                mas = 0;
            |DDEFECTO #0;
        |FINSI;
            antnum = #0#2;
    |FINSI;
    |DDEFECTO #0;
        #0#0 = anter;
        #0#2 = antnum + mas;
    |PINTA #0#2;
|FINSI;
|FPRC;

|PROCESO fecha;
|SI #0#3 < fec1; |O #0#3 > fec2;
    |MENAV mensa1;
    |ERROR;
|FINSI;
|FPRC;

|PROCESO serieSi;
 |SI swf1 = 0;
     |C_V #0#27,1,"S";
     |C_M #0#27,1,"S";
   ||  |PINTA 0821, "Serie: ";
     |PINTA 0924, "S:   ";
     swf1 = 1;
 |SINO;
     |C_V #0#27,1,"N";
     |C_M #0#27,1,"N";
   ||  |PINTA 0821, "       ";
     |PINTA 0924, "     ";
     swf1 = 0;
 |FINSI;
|FPRC;

|PROCESO funcio1; |TIPO 0;
|SI FSalida = 9;
    |HAZ serieSi;
    |ERROR;
    |ACABA;
|FINSI;
|FPRC;

|PROCESO cheq_libro; |TIPO 0;
|SI FSalida = 9;
    |HAZ serieSi;
    |ERROR;
    |ACABA;
|FINSI;
|SI #10#2 ! "R";
    |MENAV "    ATENCION!!! Libro de I.V.A. Soportado ...";
    |ERROR;
|FINSI;
|FPRC;

|PROCESO lee_libro1;
|ERROR10;
|FPRC;

|DEFBUCLE lee_libro;
 #10#1;
 2;
  1, "R";
 99, "R";
 e;
 NULL, lee_libro1;
|FIN;

|PROCESO deflib; |TIPO 7;
|DDEFECTO #10;
|BUCLE lee_libro;
    #0#0 = #10#0;
|PINTA #0#0;
|FPRC;

|PROCESO cuefin; |TIPO 0;
    modo = (FEntrada / 100) ! 0;
|ABRE #4;
    #4#0 = #0#4;
    #4#1 = #0#5;
|LEE 001#4=;
|SI FSalida = 0;
    |SI #4#3 = "S";
        |SI modo = 1;  |HAZ lee_client;  |FINSI;     || solo en alta
    |SINO;
        |MENAV men3;
        |ERROR;
    |FINSI;
|SINO;
    |ERROR;
|FINSI;
|CIERRA #4;
|FPRC;

|PROCESO lee_client;
  |ABRE #5;
  #5#10 = "C";
  #5#10 = #0#4;   || cuenta
  #5#11 = #0#5;   || digito
  |LEE 001#5=;
  |SI FSalida ! 0;
      |DDEFECTO #5;
  |FINSI;
  #0#50 = #4#2;        || carga el nombre del plan contable
  #0#51 = #5#9;        || carga el CIF del cliente
  |PINTA #0#50;
  |PINTA #0#51;
  |CIERRA #5;
|FPRC;

|PROCESO cuenta;|TIPO 6;
    p_alfa1 = #0Campo;
    salidas = FSalida;
|HAZ punto;
|SI p_error ! 0;  |ERROR;  |ACABA;  |FINSI;
    cta  = #0#4;
    long = cta % A0;
    cont = 0;
    sw = 0;
|SI long = #30#14; sw = 1; #0#5 = 1; |FINSI;
|SI long = #30#15; sw = 1; #0#5 = 2; |FINSI;
|SI long = #30#16; sw = 1; #0#5 = 3; |FINSI;
|SI long = #30#17; sw = 1; #0#5 = 4; |FINSI;
|SI long = #30#18; sw = 1; #0#5 = 5; |FINSI;
|SI long = #30#19; sw = 1; #0#5 = 6; |FINSI;
|SI sw = 0;
    |MENAV men2;
    |ERROR;
|FINSI;
|FPRC;

|PROCESO auxili; |TIPO 3;
 xy = (FEntrada / 100) ! 0
 |HAZ cobros;
 |SI xy = 1;
     |HAZ el_inverv;
 |FINSI;
|FPRC;

|PROCESO borra; |TIPO 2;
|HAZ borra_cob;
|FPRC;

|PROCESO nodepar; |TIPO 7;
|SI #30#24 = "N";
    |CAMPO_MODIFICA #0#49, 1, "N";
|SINO;
    |CAMPO_MODIFICA #0#49, 1, "S";
|FINSI;
|FPRC;

|PROCESO periodo;
    alfa1 = #0#3;   alfa1 = alfa1 % -104;      || saca a¤o
    alfa2 = "31.03." + alfa1;  trim1 = alfa2;  || primer trimestre
    alfa2 = "30.06." + alfa1;  trim2 = alfa2;  || segundo trimestre
    alfa2 = "30.09." + alfa1;  trim3 = alfa2;  || tercer trimestre
|SI #0#3 [ trim1;                   #0#44 = 1;  |FINSI;
|SI #0#3 > trim1;|Y #0#3 [ trim2;   #0#44 = 2;  |FINSI;
|SI #0#3 > trim2;|Y #0#3 [ trim3;   #0#44 = 3;  |FINSI;
|SI #0#3 > trim3;                   #0#44 = 4;  |FINSI;
|PINTA #0#44;
|FPRC;
|PROCESO letra_0;  |TIPO 6;
  |SI FSalida ! 9;  |ACABA;  |FINSI;

  alfa1 = #0Campo;   |QBT alfa1;
  x    = alfa1 % 101;
  |SI x ! "A"; |Y x ! "B"; |Y x ! "C"; |Y x ! "D"; |Y x ! "E"; |Y x ! "F";
   |Y x ! "G"; |Y x ! "P"; |Y x ! "Q"; |Y x ! "S"; |Y x ! "X";
      final = "";
      alfa2 = alfa1 % 0;
      nume1 = alfa2;
      |PARA nume2 = 1;  |SI nume2 [ nume1;  |HACIENDO nume2 = nume2 + 1;
            lugar = (nume2 * 100) + 1;
            alfa2 = alfa1 % lugar;
            |SI alfa2 = "0"; |O alfa2 = "1";|O alfa2 = "2"; |O alfa2 = "3";
             |O alfa2 = "4"; |O alfa2 = "5";|O alfa2 = "6";
             |O alfa2 = "7"; |O alfa2 = "8";|O alfa2 = "9";
                final = final + alfa2;
            |FINSI;
      |SIGUE;
      nifnum = final;
      |SI nifnum ! 0; |Y final ! "";
          resto   = nifnum $ 23;
          resto   = resto + 1;
          lugar   = (resto * 100) + 1;
          letra   = letrasnif % lugar;
          final   = ("000000000" + final + letra) % -109;
          #0Campo = final;
      |FINSI;
   |SINO;
      |MENAV "    ATENCION !!! No se calcular un C.I.F. ...";
   |FINSI;
   |PINTA #0Campo;
|FPRC;



|PROCESO el_inverv;
 |SI #0#48 ! "S";
     |ACABA;
 |FINSI;
  |AVISO;
  |CONFI "2400SFra. de Inversion. Desea reflejar la venta del Bien Amortizable S/N : ";
  |SI FSalida ! 0;
      |ACABA;
  |FINSI;
  fec_am = #0#3;
  imp_am = #0#8 + #0#9 + #0#10 + #0#52 + #0#53;
  c_am = #0#7;
  |SUB_EJECUTA "camocab2.tab";
|FPRC;
