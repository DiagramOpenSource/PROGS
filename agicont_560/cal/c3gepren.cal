|FICHEROS;
    c3gepren #00;   || captura

    reproedi #01;   || profesionales estimacion directa(PC)
    reprofco #02;   || profesionales por coeficientes  (PB)
    reempedi #03;   || empresarios estimacion directa  (EC)
    reemprco #04;   || empresarios por coeficientes    (EB)
    reacceso #05;   || Acceso Renta
    redatcon #11;   || datos contribuyentes
    repagret #25;   || Pagos

    reprogra #15;
    rehusuar #100;

    caexistl #06;   || lineas de existencias
    caportad #07;   || Datos Portadas
    cacuenta #12;   || Saldos de Las Cuentas para el Pase
    caplaren #13;   || Planilla de Cuentas
    canempre #30;   || Empresas Contables
    caman130 #24;   || Pagos del 130 en Contabilidad

    agicontr #20;   || Control Aplicaciones
|FIN;

|INCLUYE acceso_i;

|VARIABLES;
  sw_error = 0;
  men1 = "     NO Existe Periodo en la empresa Contable";
  men2 = "     El a¤o contable NO esta dentro de la seleccion";
  men3 = "     NO existen Datos Portada ...";
  men4 = "     NO Esta Definida la Planilla del Pase";
  men5 = "     Datos Portadas incorrectos: Actividad no Empresarial ni Profesional ";
  men6 = "     Datos Portadas incorrectos: Actividad no Coeficientes ni Directa";
  sw = 0;

    linea_p = 0;
    FEstado = 0;

    leidos = 0;
    alfa1 = "";
    alfa2 = "";
    nume1 = 0;

    coef_gas = 0;
    agiren  = "";
    agicont = "";
    retenci = 0;
    ultima = 0;
    tipo = "";
    perc = "";
    seleccio = 0;
    grabalo = 0;
    buscanif = "";
    control = 0;
    n_cont  = 0;
    n_cant  = 0;
    n_tipo  = "";
    n_esta  = 0;
    n_busc  = 0;
    campo_e = 0;
    campo_a = 0;

    empr_ren = 0;
    campo16 = 0;
    campo17 = 0;
    campo18 = 0;
    campo19 = 0;
    campo20 = 0;
    campo21 = 0;
    campo22 = 0;
    campo23 = 0;
    campo24 = 0;
    campo25 = 0;
    campo26 = 0;
    campo27 = 0;
    campo28 = 0;
    campo29 = 0;
    campo30 = 0;
    campo31 = 0;
    campo32 = 0;
    campo33 = 0;
    campo34 = 0;
    campo35 = 0;
    campo36 = 0;
    campo37 = 0;
    campo38 = 0;
    campo39 = 0;
    campo40 = 0;
    campo41 = 0;
    fichero = "";
    anyo = "";
    porce   = 0;
    nombre  = "";
    sw_estar = 0;
    LoHago   = 0;
    Nombre   = "";

    &dni = "";
    &codigo = 0;
    &error = "";
    &empre_e = 0;
    &empre_r = 0;
    &activ_e = 0;
    &tipo_e  = "";

     &HContribu  = 0;
     &HRefer     = 0;
     &HNombre    = "";
     &HFecha     = @;
     &HHora      = 0;
     &HMinuto    = 0;
     &HTipo      = "";
     &HPrograma  = "";
     &HLinea     = 0;
     &HPerceptor = "";
     &HRdto      = 0;
     &HUsuario   = "";
     &QueTipo   = 0;

     Hora       = "";

   npro = 0;
   FEprogra = 0;

  &r_emp = 0;
  &r_per = 0;
  &r_eje = 0;
  &estru = 0;
  &d_mes = 0;
  &h_mes = 0;
  &r_nom = "";
  exini = 0;
  exfin = 0;
  cc3 = 0;
  cc4 = 0;
    ventas = 0;
    compra = 0;
    retenc = 0;
  uno = "";
  dos = "";
  sumactas = 0;
  scta = 0;
  cta = "";
  ccc = "";
  x = 0;
  y = 0;
  xx = 0;
  yy = 0;
  zz = 0;
  ww = 0;
  c01 = 0;
  c02 = 0;
  c03 = 0;
  c04 = "";
  c08 = "";
  c09 = "";
  c10 = 0;
  c13 = 0;
    &Viene   = 0;
    &Cadena  = "";
    &Cadena1 = "";
    &Cadena2 = "";
    &Cadena3 = "";

    Blanco1  = 0;
    Blanco2  = 0;
    Coma     = 0;
    Xxx      = 0;
    Posi     = 0;
    Caracter = "";
    Empresa  = 0;
    Contador = 0;
    LongAlfa = "";
    LongNume = 0;
    ParaQuitar = "";

    dirfich  = "";
    dirfich0 = "";
    men9 = "     Empresa Contable INACTIVA ";

|FIN;

     *********************************************************************
     LLAMADAS DESDE CAMPOS
     *********************************************************************

|PROCESO tipo8; |TIPO 8;
 |HAZ inicio;
 |DBASS alfa1;
 |QBF alfa1;
 |PATH_DAT #20 alfa1;
 |ABRE #20;
 |LEE 040#20p;
 |SI FSalida ! 0; |DDEFECTO #20; |FINSI;
 |CIERRA #20;

 agiren  = #20#6; |QBF agiren;
 agicont = #20#7; |QBF agicont;

 |SI agiren = "*"; |ACABA; |FINSI;

 |PATH_DAT #01 agiren;
 |PATH_DAT #02 agiren;
 |PATH_DAT #03 agiren;
 |PATH_DAT #04 agiren;
 |PATH_DAT #05 agiren;
 |PATH_DAT #11 agiren;
 |PATH_DAT #15 agiren;
 |PATH_DAT #25 agiren;
 |PATH_DAT #100 agiren;

  |DBASE alfa1;
  dirfich = alfa1 + "fich/";
|FPRC;

|PROCESO mayor; |TIPO 0;
  alfa1 = #0Campo > " ";
  #0Campo = alfa1;
  |PINTA #0Campo;
|FPRC;

|PROCESO def_any; |TIPO 7;
   #0#1 = #30#26;
   |PINTA #0#1;
   #0#2 = agiren;
   |PINTA #0#2;
|FPRC;

|PROCESO defecto; |TIPO 7;
 x = Campo - 60;
 |SI #0x ! 0; |Y #0Campo = 0;
     #0Campo = #0x; |PINTA #0Campo;
     x = Campo + 20;
     #0x = "T"; |PINTA #0x;
 |FINSI;
|FPRC;

|PROCESO lee_renta1; |TIPO 0;
 |CAMPO_MODIFICA #0Campo,0,alfa1;
 |SI alfa1 = "N"; |ACABA; |FINSI;

 |ABRE #11;
 #11#0 = #0Campo;
 |LEE 001#11=;
 |SI FSalida ! 0;
     |DDEFECTO #11;
     |MENAV "    ATENCION!!! Contribuyente inexistente en RENTA ...";
 |FINSI;
 |CIERRA #11;
|FPRC;

|PROCESO mayor1; |TIPO 0;
  |HAZ mayor;
  |SI #0Campo = "N";
      |PARA x = 11;  |SI x [ 110;  |HACIENDO x = x + 1;
            |CAMPO_MODIFICA #0x,1,"S";
            |SI x = 50; x = 70; |FINSI;
      |SIGUE;
      #0#5  = "T";
      #0#7  = 0;
      #0#9  = 0;
      #0#8  = 0;
      #0#10 = 0;
      |CAMPO_MODIFICA #0#5,1,"N";
      |CAMPO_MODIFICA #0#7,1,"N";
      |CAMPO_MODIFICA #0#9,1,"N";
      |CAMPO_MODIFICA #0#8,1,"N";
      |CAMPO_MODIFICA #0#10,1,"N";
      |PINTA #0#5;
      |PINTA #0#7;
      |PINTA #0#9;
      |PINTA #0#8;
      |PINTA #0#10;
  |SINO;
      |PDEFECTO #0,11,111;
      |PARA x = 11;  |SI x [ 110;  |HACIENDO x = x + 1;
            |CAMPO_MODIFICA #0x,1,"N";
            |SI x = 50; x = 70; |FINSI;
      |SIGUE;
      |CAMPO_MODIFICA #0#5,1,"S";
      |CAMPO_MODIFICA #0#7,1,"S";
      |CAMPO_MODIFICA #0#9,1,"S";
      |CAMPO_MODIFICA #0#8,1,"S";
      |CAMPO_MODIFICA #0#10,1,"S";
  |FINSI;
|FPRC;

|PROCESO consulta; |TIPO 0;
  |SI FSalida ! 9;  |ACABA;  |FINSI;
  |SUB_EJECUTA "caconemp.run";
  |ERROR;
  |SI Campo = 7;
      #0#7 = r_emp;
      #0#9 = r_per;
      |PTEC 802;
      |PTEC 802;
  |FINSI;
  |SI Campo = 8;
      #0#8 = r_emp;
      #0#10 = r_per;
      |PTEC 802;
  |FINSI;
  |SI Campo ] 11;
      #0Campo = r_emp;
      x = Campo + 20;
      #0x = r_per;
      |PTEC 802;
      |PTEC 802;
  |FINSI;
|FPRC;

|PROCESO deshas; |TIPO 0;
  |SI #0#8 < #0#7;
      |MENAV "      Error en Introduccion";
      |ERROR;
  |FINSI;
|FPRC;

|| --------------- comprueba que la empresa es buena --------------------
|PROCESO datospor; || Control de Asto. contable
  |PATH_DAT #7 dirfich0;
  |ABRE #7;
  |DDEFECTO #7;
  #7#0 = #30#2;
  #7#1 = #30#26;
  |LEE 000#7=;
  |SI FSalida ! 0;
      sw_error = 3;
  |SINO;
      |SI #7#82 ! "P"; |Y #7#82 ! "E"; sw_error = 5; |FINSI;
      |SI #7#81 ! "B"; |Y #7#81 ! "C"; sw_error = 6; |FINSI;
  |FINSI;
  |CIERRA #7;
|FPRC;

|| --------------- comprueba desde-hasta empresas ---------------------------
|PROCESO buscaem1;
  sw_error = 0;

  |SI #30#21 = "I"; |ACABA; |FINSI;
  |SI #30#26 ! #0#1; |ACABA; |FINSI;
  alfa1 = #30#2;
  alfa2 = #30#3;
  alfa1 = "00000" + alfa1;   alfa1 = alfa1 % -105;
  alfa2 = "0"   + alfa2;   alfa2 = alfa2 % -101;
  dirfich0 = dirfich + alfa1 + alfa2 + "/";

  |HAZ datospor; |SI sw_error ! 0;  |ACABA; |FINSI;
  leidos = 1;
  |ERROR10;
|FPRC;

|DEFBUCLE buscaempre;
#30#1;
;
#0#7,#0#9;
#0#8,#0#10;
e;
NULL,buscaem1;
|FIN;

|PROCESO buclempre;
  |SI #0#9 > #0#10;
      |MENAV "     Error en Introduccion";
      |ERROR;
      |ACABA;
  |FINSI;
  |SI #0#7 = 0; |Y #0#8 = 0; |ACABA; |FINSI;
  leidos = 0;
  |BUCLE buscaempre;
  |SI leidos = 0;
      |MENAV "    ATENCION SELECCION VACIA";
      |ERROR;
      |ACABA;
  |FINSI;
|FPRC;

|| ------------------------------------------------------------------------

|PROCESO nomodi;
  |CAMPO_MODIFICA #0Campo,0, alfa1;
  |SI alfa1 = "N"; |ACABA; |FINSI;
  |PARA x = Campo + 1;  |SI x [ 30;  |HACIENDO x = x + 1;
                    #0x = 0;  |CAMPO_MODIFICA #0x,1,"N";
        y = x + 20; #0y = 0;  |CAMPO_MODIFICA #0y,1,"N";
        y = x + 60; #0y = 0;  |CAMPO_MODIFICA #0y,1,"N";
        y = x + 80; #0y = ""; |CAMPO_MODIFICA #0y,1,"N";
  |SIGUE;
  y = Campo + 20; #0y = 0;  |CAMPO_MODIFICA #0y,1,"N"; |PINTA #0y;
  y = Campo + 60; #0y = 0;  |CAMPO_MODIFICA #0y,1,"N"; |PINTA #0y;
  y = Campo + 80; #0y = ""; |CAMPO_MODIFICA #0y,1,"N"; |PINTA #0y;
|FPRC;

|PROCESO esbuena; || desde campo de empresas seleccion
  |SI FSalida = 2; |ACABA; |FINSI;
  |SI #0Campo = 0; |HAZ nomodi;  |ACABA;  |FINSI;
  |ABRE #30;
  #30#2 = #0Campo;
  #30#3 = 0;
  |LEE 011#30];
  |SI FSalida ! 0;  |O #30#2 ! #0Campo;
      |MENAV "     NO Existe la empresa Contable";
      |ERROR;
  |FINSI;
  |CIERRA #30;
|FPRC;

|PROCESO esbuena1;
  |SI FSalida = 2;|ACABA;|FINSI;
  x = Campo - 20;
  |SI #0x = 0;  |ACABA;  |FINSI;

  sw_error = 0;
  |ABRE #30;
  x = Campo - 20;
  #30#2 = #0x;
  #30#3 = #0Campo;
  |LEE 011#30=;
  |SI FSalida ! 0;
      sw_error = 1;
      |CIERRA #30;
      |MENAV men1;
      |ERROR;
      |ACABA;
  |FINSI;
  |CIERRA #30;

  |SI #30#21 = "I";                |MENAV men9; |ERROR; |ACABA; |FINSI;
  |SI #30#26 ! #0#1; sw_error = 2; |MENAV men2; |ERROR; |ACABA; |FINSI;

  alfa1 = #30#2;
  alfa2 = #30#3;
  alfa1 = "00000" + alfa1;   alfa1 = alfa1 % -105;
  alfa2 = "0"   + alfa2;   alfa2 = alfa2 % -101;
  dirfich0 = dirfich + alfa1 + alfa2 + "/";

  |HAZ datospor;
  |SI sw_error ! 0;
      |SI sw_error = 3; |MENAV men3; |FINSI;
      |SI sw_error = 5; |MENAV men5; |FINSI;
      |SI sw_error = 6; |MENAV men6; |FINSI;
      |ERROR; |ACABA;
  |FINSI;
  x = Campo + 20;
  #0x = #30#1;
  |PINTA #0x;
|FPRC;

|| **********************************************************************
||                       CALCULOS DEL PROCESO
|| **********************************************************************

|| ---------- Seleccion DESDE / HASTA Empresa.

|PROCESO esdesd1;
  |SI #30#26 ! #0#1; |ACABA; |FINSI;
  |SI #30#21 = "I"; |ACABA; |FINSI;
  alfa1 = #30#2;
  alfa2 = #30#3;
  alfa1 = "00000" + alfa1;   alfa1 = alfa1 % -105;
  alfa2 = "0"   + alfa2;   alfa2 = alfa2 % -101;
  dirfich0 = dirfich + alfa1 + alfa2 + "/";

  |HAZ datospor;
  |SI sw_error ! 0; |ACABA; |FINSI;
  |HAZ haztodo;
|FPRC;

|DEFBUCLE esdesde;
#30#1;
;
#0#7, #0#9;
#0#8,#0#10;
e;
NULL,esdesd1;
|FIN;

|| ---------- Seleccion en parrilla.

|PROCESO esunaauna;
  |PARA xx = 11;  |SI xx [ 30;  |HACIENDO xx = xx + 1;
        |SI #0xx = 0;  |SAL;  |FINSI;

        yy = xx + 20;
        zz = xx + 60;
        ww = xx + 80;

        alfa1 = #0xx;
        alfa2 = #0yy;
        alfa1 = "00000" + alfa1;   alfa1 = alfa1 % -105;
        alfa2 = "0"   + alfa2;   alfa2 = alfa2 % -101;
        dirfich0 = dirfich + alfa1 + alfa2 + "/";
|| lee empresa
        |ABRE #30;
        #30#2 = #0xx;
        #30#3 = #0yy;
        |LEE 011#30=;
        |SI FSalida ! 0; |CIERRA #30; |VETE otramas; |FINSI;
        |CIERRA #30;
        |SI #30#21 = "I";  |VETE otramas; |FINSI;
        |SI #30#26 ! #0#1; |VETE otramas; |FINSI;
        |HAZ datospor;
        |SI sw_error ! 0; |VETE otramas; |FINSI;
        |HAZ haztodo;
|ET otramas;
  |SIGUE;
|FPRC;

|| *********************************************************************
||                              PROGRAMA
|| *********************************************************************

|PROCESO tipo3; |TIPO 3;
  sw = 0;
 |PUSHV 2102 2378;
 |ATRI I;
 |CUADRO 2102 2378
 |ATRI N;
 |ATRI R;
 |PINTA 2203," EMPRESA:                                                                  ";
 |ATRI 0;

  |SI #0#6 = "S";
      |BUCLE esdesde;
  |SINO;
      |HAZ esunaauna;
  |FINSI;
|FPRC;

|| ***********************************************************************
||                    ASIENTO CONTABLE 300
|| ***********************************************************************
|PROCESO haztodo;
  enEmpresa = #canempre#2;
  enPeriodo = #canempre#3;
  swOperacion = 0;
  |SUB_EJECUTA "camoneda.tab";

  |PATH_DAT #6  dirfich0;
  |PATH_DAT #12 dirfich0;
  |PATH_DAT #24 dirfich0;

  |ATRI R;
  |PINTA 2213, #30#2;
  |PINTA 2219, #30#3;
  |PINTA 2221, #30#1;
  |ATRI 0;

||     *********************************************************************
     c01 = #30#2;   || Empresa
     c02 = #30#3;   || Periodo
 |SI #0#6 = "S";    || Seleccion desde/hasta
     c03 = c01;     || Contribuyente
     c04 = #0#5;    || Titular / Conyuge
 |SINO;
     c03 = #0zz;     || Contribuyente
     c04 = #0ww;    || Titular / Conyuge
 |FINSI;

 c08 = #7#81;  || Tipo Empresa B = Coeficientes o C = Directa
 c09 = #7#82;  || E/P  Empresarial o Profesional
 c10 = #7#88;  || Estructura de Descarga
 c13 = #7#86;  || Coeficiente de Gastos

 |HAZ captura1;
|FPRC;

     *********************************************************************
     LECTURA ACTIVIDADES
     *********************************************************************

|PROCESO captura1;
    tipo = c04;
|SI tipo = "T";  perc = "TITULAR";  |FINSI;
|SI tipo = "C";  perc = "CONYUGE";  |FINSI;

coef_gas = c13;

    alfa1 = c09 + c08;
    n_tipo = alfa1;                               || variable conserva
|SI alfa1 = "PC";   |HAZ profdire1;  |FINSI;
|SI alfa1 = "PB";   |HAZ profcoef1;  |FINSI;
|SI alfa1 = "EC";   |HAZ emprdire1;  |FINSI;
|SI alfa1 = "EB";   |HAZ emprcoef1;  |FINSI;

|HAZ pagos;
|HAZ gr_reacceso;
|FPRC;

*********************************************************************
GRABA ACCESO RENTA Y DATOS CONTRIBUYENTES SI NO EXISTEN
*********************************************************************
|PROCESO gr_reacceso;
  |ABRE #5;
  |DDEFECTO #5;
  #5#0 = c03;
  |LEE 000#5=;
  |SI FSalida ! 0;
      #5#0 = c03;   || codigo
      |SI c04 = "T";
          #5#1 = #7#2;   || nombre
          #5#2 = #7#13;  || dni
      |FINSI;
      |GRABA 020#5n;
      |LIBERA #5;
  |FINSI;
  |CIERRA #5;
                || Lee Redatcon
  |ABRE #11;
  #11#0 = c03;
  |LEE 001#11=;
  |SI FSalida ! 0;
      |DDEFECTO #11;
      #11#0  = c03;       || codigo
      |SI c04 = "T";
          #11#1  = #7#2;       || nombre T
          #11#2  = #7#13;      || D.N.I. T
      |SINO;
          #11#28 = #7#2;       || nombre C
          #11#29 = #7#13;      || D.N.I. C
      |FINSI;
      #11#6  = #7#3;       || sg
      #11#7  = #7#4;       || calle
      #11#8  = #7#5;       || numero
      #11#9  = #7#6;       || escalera
      #11#10 = #7#7;       || piso
      #11#11 = #7#8;       || puerta
      #11#12 = #7#9;       || cod.postal 1
      #11#13 = #7#10;      || cod.postal 2
      #11#14 = #7#11;      || poblacion
      #11#15 = #7#14;      || telefono
      alfa1 = #11#12; alfa1 = ("00"  + alfa1) % -102;
      alfa2 = #11#13; alfa2 = ("000" + alfa2) % -103;
      #11#16 = alfa1+alfa2;|| cod.postal alfanumerico

      #11#17 = #11#6;      || sg envio
      #11#18 = #11#7;      || calle envio
      #11#19 = #11#8;      || numero envio
      #11#20 = #11#9;      || escalera envio
      #11#21 = #11#10;     || piso envio
      #11#22 = #11#11;     || puerta envio
      #11#23 = #11#12;     || cod.postal 1
      #11#24 = #11#13;     || cod.postal 2
      #11#25 = #11#14;     || poblacion envio
      #11#26 = #11#15;     || telefono envio
      #11#27 = #11#16;     || cod.postal alfanumerico envio

      #11#40 = #7#16;      || cod.postal 1 H.P.
      #11#41 = #7#17;      || cod.postal 2 H.P.
      #11#42 = #7#18;      || delegacion H.P.
      #11#43 = #7#19;      || administracion H.P.

      #11#45 = #7#39;      || representante legal
      #11#46 = #7#38;      || dni representante
      #11#47 = #7#46;      || Telefono representante

      ccc = #7#66 + #7#67 + #7#68 + #7#69;
      |SI c04 = "T";
          #11#174 = ccc;   || cuenta corriente conjunta
          #11#175 = ccc;   || cuenta corriente titular
      |SINO;
          #11#176 = ccc;   || cuenta corriente conyuge
      |FINSI;

      Cadena = #7#2;
      |HAZ SeparaNombre;
      |SI c04 = "T";
          #11#209= Cadena1;    || primer apellido
          #11#210= Cadena2;    || segundo apellido
          #11#211= Cadena3;    || nombre
      |SINO;
          #11#212= Cadena1;    || primer apellido
          #11#213= Cadena2;    || segundo apellido
          #11#214= Cadena3;    || nombre
      |FINSI;

      |GRABA 020#11n;
      |LIBERA #11;
  |FINSI;
  |CIERRA #11;
|FPRC;

*********************************************************************
LECTURA DEL 130 PARA PAGOS
*********************************************************************

|PROCESO lee_m3130b;
|SI #24#4 = "          "; |ACABA; |FINSI;

|SI #24#29 > 0;
    #25#0 = c03;
    #25#1 = linea_p;
    |LIBERA #25;
    |LEE 140#25=;
    FEstado = FSalida;
    |SI FEstado ! 0; |DDEFECTO #25; |FINSI;

    #25#0 = c03;
    #25#1 = linea_p;
    #25#2 = c04;
    #25#3 = perc;
    #25#4 = 0;

    |SI #24#2 = 1;  #25#5 = #24#29;  |FINSI;
    |SI #24#2 = 2;  #25#6 = #24#29;  |FINSI;
    |SI #24#2 = 3;  #25#7 = #24#29;  |FINSI;
    |SI #24#2 = 4;  #25#8 = #24#29;  |FINSI;
    #25#9  = #24#25;
    #25#10 = #24#36;

    |SI FEstado = 0;
        |GRABA 140#25a;
    |SINO;
        |GRABA 140#25n;
         |SI FSalida=0; npro=51; empr_ren=c03; |HAZ sigrabo; |FINSI;      || marca (*)
    |FINSI;

    HContribu  = #25#0;
    HNombre    = Nombre;
    HPrograma  = "(repagret) RETENCIONES Y PAGOS A CUENTA";
    HLinea     = #25#1;
    HPerceptor = #25#3;
    HRdto      = #25#5 + #25#6 + #25#7 + #25#8;
    HTipo      = "ALTA FICHA DESDE PASE AGICONT";
    |HAZ AltaHistorico;
|FINSI;
|FPRC;

|DEFBUCLE lee_m3130;
 #24#1;
 ;
 c01, 0, 1, #30#26;
 c01, 9, 4, #30#26;
 e;
 NULL, lee_m3130b;
|FIN;

|PROCESO ChequeaPagos;
 |SI #25#2 = c04; LoHago = 1; |ERROR10; |ACABA; |FINSI;
 linea_p = #25#1;
|FPRC;

|DEFBUCLE repagret;
 #25#1;
 ;
 c03,  1;
 c03, 99;
 ;
 NULL, ChequeaPagos;
|FIN;

|PROCESO pagos;
 linea_p = 0;
 LoHago  = 0;
 |BUCLE repagret;
 |SI LoHago = 1; |ACABA; |FINSI;

 |ABRE #25;
 linea_p = linea_p + 1;
 |BUCLE lee_m3130;
 |CIERRA #25;
|FPRC;


     *********************************************************************
     LECTURA DE CUENTAS Y CARGA DE CAMPOS
     *********************************************************************

|PROCESO cto_ce;
|SI n_cont ! 0;|Y n_cant ! 0;
    |SI n_cont =  1;  campo24 = campo24 + n_cant;  |FINSI;  || tributos
    |SI n_cont =  2;  campo25 = campo25 + n_cant;  |FINSI;  || mater.primas
    |SI n_cont =  3;  campo26 = campo26 + n_cant;  |FINSI;  || coste person.
    |SI n_cont =  4;  campo27 = campo27 + n_cant;  |FINSI;  || seg.social
    |SI n_cont =  5;  campo28 = campo28 + n_cant;  |FINSI;  || planes pens.
    |SI n_cont =  6;  campo29 = campo29 + n_cant;  |FINSI;  || honor.profes.
    |SI n_cont =  7;  campo30 = campo30 + n_cant;  |FINSI;  || alquileres
    |SI n_cont =  8;  campo31 = campo31 + n_cant;  |FINSI;  || int.prestamos
    |SI n_cont =  9;  campo32 = campo32 + n_cant;  |FINSI;  || amortizac.
    |SI n_cont = 10;  campo33 = campo33 + n_cant;  |FINSI;  || cons.y rep.
    |SI n_cont = 11;  campo34 = campo34 + n_cant;  |FINSI;  || agua,tfno.
    |SI n_cont = 12;  campo35 = campo35 + n_cant;  |FINSI;  || seguros
    |SI n_cont = 13;  campo36 = campo36 + n_cant;  |FINSI;  || dudoso cobro
    |SI n_cont = 14;  campo37 = campo37 + n_cant;  |FINSI;  || reduc.fond.edit.
    |SI n_cont = 15;  campo38 = campo38 + n_cant;  |FINSI;  || fondos rever.
    |SI n_cont = 16;  campo39 = campo39 + n_cant;  |FINSI;  || gtos.admon.
    |SI n_cont = 17;  campo40 = campo40 + n_cant;  |FINSI;  || cuotas corp.
    |SI n_cont = 18;  campo41 = campo41 + n_cant;  |FINSI;  || otros gastos
|FINSI;
|FPRC;

|PROCESO cto_be;
|SI n_cont ! 0;|Y n_cant ! 0;
    |SI n_cont = 1;  campo40 = campo40 + n_cant;  |FINSI;   || compras
    |SI n_cont = 2;  campo18 = campo18 + n_cant;  |FINSI;   || coste pers.
    |SI n_cont = 3;  campo19 = campo19 + n_cant;  |FINSI;   || agua,tfno.
    |SI n_cont = 4;  campo20 = campo20 + n_cant;  |FINSI;   || alquileres
    |SI n_cont = 5;  campo21 = campo21 + n_cant;  |FINSI;   || gtos.finan.
    |SI n_cont = 6;  campo22 = campo22 + n_cant;  |FINSI;   || tributos
    |SI n_cont = 7;  campo23 = campo23 + n_cant;  |FINSI;   || rep.y cons.
    |SI n_cont = 8;  campo24 = campo24 + n_cant;  |FINSI;   || trab.otras emp.
|FINSI;
|FPRC;

|PROCESO cto_cp;
|SI n_cont ! 0;|Y n_cant ! 0;
    |SI n_cont = 1;  campo18 = campo18 + n_cant;  |FINSI;   || mat.consum.
    |SI n_cont = 2;  campo19 = campo19 + n_cant;  |FINSI;   || cost.personal
    |SI n_cont = 3;  campo20 = campo20 + n_cant;  |FINSI;   || int.prestamo
    |SI n_cont = 4;  campo21 = campo21 + n_cant;  |FINSI;   || alqui.locales
    |SI n_cont = 5;  campo22 = campo22 + n_cant;  |FINSI;   || amortiz.
    |SI n_cont = 6;  campo23 = campo23 + n_cant;  |FINSI;   || agua, tfno.
    |SI n_cont = 7;  campo24 = campo24 + n_cant;  |FINSI;   || otros gtos.
|FINSI;
|FPRC;

|PROCESO cto_bp;
|SI n_cont ! 0;|Y n_cant ! 0;
    |SI n_cont = 1;  campo39 = campo39 + n_cant;  |FINSI;   || compras
    |SI n_cont = 2;  campo18 = campo18 + n_cant;  |FINSI;   || coste pers.
    |SI n_cont = 3;  campo19 = campo19 + n_cant;  |FINSI;   || agua, tfno.
    |SI n_cont = 4;  campo20 = campo20 + n_cant;  |FINSI;   || alqui.locales
    |SI n_cont = 5;  campo21 = campo21 + n_cant;  |FINSI;   || gtos.finan.
    |SI n_cont = 6;  campo22 = campo22 + n_cant;  |FINSI;   || tributos
    |SI n_cont = 7;  campo23 = campo23 + n_cant;  |FINSI;   || rep. y cons.
    |SI n_cont = 8;  campo24 = campo24 + n_cant;  |FINSI;   || trab.otras emp.
|FINSI;
|FPRC;

|PROCESO busca_cto;
  |SI n_tipo = "PB";  n_cont = #13#3;  |FINSI;
  |SI n_tipo = "PC";  n_cont = #13#5;  |FINSI;
  |SI n_tipo = "EB";  n_cont = #13#4;  |FINSI;
  |SI n_tipo = "EC";  n_cont = #13#6;  |FINSI;
  |SI n_cont ! 0;
      |SI #13#2 = "G";                      || compras
          |SI n_tipo = "PB";  |HAZ cto_bp;  |FINSI;
          |SI n_tipo = "PC";  |HAZ cto_cp;  |FINSI;
          |SI n_tipo = "EB";  |HAZ cto_be;  |FINSI;
          |SI n_tipo = "EC";  |HAZ cto_ce;  |FINSI;
      |FINSI;
      |SI #13#2 = "I";                      || ingresos
          |SI n_tipo = "PB";  campo17 = campo17 + n_cant;  |FINSI;
          |SI n_tipo = "PC";  campo16 = campo16 + n_cant;  |FINSI;
          |SI n_tipo = "EB";  campo17 = campo17 + n_cant;  |FINSI;
          |SI n_tipo = "EC";
              |SI n_cont = 1; campo17 = campo17 + n_cant;  |FINSI;
              |SI n_cont = 2; campo18 = campo18 + n_cant;  |FINSI;
              |SI n_cont = 3; campo19 = campo19 + n_cant;  |FINSI;
              |SI n_cont = 4; campo20 = campo20 + n_cant;  |FINSI;
              |SI n_cont = 5; campo21 = campo21 + n_cant;  |FINSI;
              |SI n_cont = 6; campo22 = campo22 + n_cant;  |FINSI;
          |FINSI;
      |FINSI;
  |FINSI;
|FPRC;

|PROCESO lee_libroa;
    retenci = 0;
    exini   = 0;   exfin   = 0;
    campo16 = 0;   campo17 = 0;
    campo18 = 0;   campo19 = 0;
    campo20 = 0;   campo21 = 0;
    campo22 = 0;   campo23 = 0;
    campo24 = 0;   campo25 = 0;
    campo26 = 0;   campo27 = 0;
    campo28 = 0;   campo29 = 0;
    campo30 = 0;   campo31 = 0;
    campo32 = 0;   campo33 = 0;
    campo34 = 0;   campo35 = 0;
    campo36 = 0;   campo37 = 0;
    campo38 = 0;   campo39 = 0;
    campo40 = 0;   campo41 = 0;
|FPRC;

|PROCESO cal_ctas2;
   cta = #12#0 + "            ";
   cta = cta % A112;
   |ATRI I;
   |ATRI R;
   |PINTA 2263, cta;
   |ATRI 0;
   cc3 = 9;
   cc4 = 45;
   |SI #13#7 = "H";
       cc3 = cc3 + 1;
       cc4 = cc4 + 1;
   |FINSI;
   |SI #13#7 = "S";
       cc3 = cc3 + 2;
       cc4 = cc4 + 2;
   |FINSI;
   scta = 0;
   |PARA nume1 = cc3; |SI nume1 [ cc4; |HACIENDO nume1 = nume1 + 3;
         scta = scta + #12nume1;
   |SIGUE;
   |SI #13#2 = "I"; |Y #13#7 = "S"; scta = - scta; |FINSI;
   sumactas = sumactas + scta;
|FPRC;

|DEFBUCLE lee_ctas;
 #12#1;
 3;
 #13#1, 0, "S";
 dos,   9, "S";
 e;
 NULL, cal_ctas2;
|FIN;

|PROCESO cal_ctas1;
   sumactas = 0;
   uno = #13#1;
   |QBF uno;
   dos = uno + "999999999999";
   dos = dos % A112;
   |BUCLE lee_ctas;
   |SI sumactas ! 0;  || SI existe Saldo
       grabalo = 1;
       n_cant = sumactas;  || Esto seria para C.B. '% porce;'
       |HAZ busca_cto;
       |SI #13#2 = "I"; ventas = ventas + sumactas; |FINSI;
       |SI #13#2 = "G"; compra = compra + sumactas; |FINSI;
       |SI #13#2 = "R"; retenci = retenci + sumactas; |FINSI;
   |FINSI;
|FPRC;

|DEFBUCLE ctas_ren;
 #13#1;
 ;
 001;
 999;
 e;
 NULL, cal_ctas1;
|FIN;

|| ===== Calculo de Existencias
|PROCESO sumaex;
  exini = exini + #6#11;
  exfin = exfin + #6#23;
|FPRC;

|DEFBUCLE lin_exis;
 #6#1;
 ;
 c10, 01;
 c10, 99;
 e;
 NULL, sumaex;
|FIN;

|PROCESO comun;
  grabalo = 0;
  |HAZ lee_libroa;  || Borra acumulados de campo??
  |BUCLE ctas_ren;  || Lee las cuentas para sumar conceptos
  |BUCLE lin_exis;  || Existencias inicial y final
|FPRC;

     *********************************************************************
     GRABACION DE PROFESIONALES EN ESTIMACION DIRECTA
     *********************************************************************

|PROCESO profdire3;
    #1#27 = #1#33 - #1#34;
    #1#25 = #1#16 % 1;   || 1% ingresos integros
    #1#26 = #1#18 + #1#19 + #1#20 + #1#21 + #1#22 + #1#23 + #1#24 + #1#25;
    #1#28 = #1#16 - #1#26 + #1#27;      || rendimiento neto
|FPRC;

|PROCESO profdire2;
|ABRE #1;
|HAZ ultimo1;
|DDEFECTO #1;
    #1#00 = c03;        || codigo
    #1#01 = ultima;      || linea
    #1#02 = tipo;        || tipo
    #1#03 = perc;        || perceptor
    #1#13 = #7#32 % 101; || epigrafe -1-
    #1#14 = #7#32 % -104;|| epigrafe -2-
    #1#15 = #7#20;       || nombre actividad
    #1#16 = campo16;     || ingresos integros
    #1#18 = campo18;     || material consumido
    #1#19 = campo19;     || coste personal
    #1#20 = campo20;     || inter.prestamo
    #1#21 = campo21;     || alquiler locales
    #1#22 = campo22;     || amortizaciones
    #1#23 = campo23;     || energia, agua, etc.
    #1#24 = campo24;     || otros gastos
    #1#37 = #7#09;       || Codigo Provincia
    #1#38 = #7#10;       || Codigo Poblacion
    #1#39 = #7#4;        || Domicilio

|HAZ profdire3;          || CALCULA ...
|GRABA 020#1n;
|SI FSalida=0; npro=7; empr_ren=c03; |HAZ sigrabo; |FINSI;      || marca (*)
|CIERRA #1;

HContribu  = #1#0;
HNombre    = Nombre;
HPrograma  = "(reproedi) PROFESIONALES DIRECTA";
HLinea     = #1#1;
HPerceptor = #1#3;
HRdto      = #1#28;
HTipo      = "ALTA FICHA DESDE PASE AGICONT";
|HAZ AltaHistorico;
|FPRC;

|PROCESO ultimo1;
    #1#0 = c03;
    #1#1 = 99;
|LEE 001#1];
|SI FSalida = 0;
    |LEE 001#1a;
|SINO;
    |LEE 001#1u;
|FINSI;
|SI FSalida = 0; |Y #1#0 = c03;
        ultima = #1#1 + 1;
|SINO;
        ultima = 1;
|FINSI;
|FPRC;

|PROCESO profdire1;  || Profesional estimacion directa
|HAZ comun;
|SI grabalo ! 0;
    |HAZ profdire2;
|FINSI;
|FPRC;

     *********************************************************************
     GRABACION DE PROFESIONALES EN COEFICIENTES
     *********************************************************************

|PROCESO profcoef3;
    #2#25 = #2#17 + #2#37;
    #2#25 = (#2#25-(#2#18+#2#19+#2#20+#2#21+#2#22+#2#23+#2#24+#2#38+#2#39))%10;
|SI #2#25 < 0;  #2#25 = 0;  |FINSI;
    #2#30 = 0;
|PARA nume1 = 18; |SI nume1 [ 25; |HACIENDO nume1 = nume1 + 1;
        #2#30 = #2#30 + #2nume1;
|SIGUE;
    #2#30 = #2#30 + #2#38 + #2#39 - #2#37;
    #2#40 = #2#17;
    #2#41 = #2#40 - #2#30;
    #2#31 = #2#26 - #2#27 - #2#28 - #2#29;
    #2#32 = #2#41 + #2#31;
|FPRC;

|PROCESO profcoef2;
|ABRE #2;
|HAZ ultimo2;
|DDEFECTO #2;
    #2#00 = c03;        || codigo
    #2#01 = ultima;      || linea
    #2#02 = tipo;        || tipo
    #2#03 = perc;        || perceptor
    #2#13 = #7#32 % 101; || epigrafe -1-
    #2#14 = #7#32 % -104;|| epigrafe -2-
    #2#15 = #7#20;       || nombre actividad
    #2#17 = campo17;     || ingresos integros
    #2#18 = campo18;     || coste personal
    #2#19 = campo19;     || energia, agua, etc.
    #2#20 = campo20;     || alquileres locales
    #2#21 = campo21;     || gastos financieros
    #2#22 = campo22;     || tributos
    #2#23 = campo23;     || rep. y conservacion
    #2#24 = campo24;     || trabajos otras empresas
    #2#37 = exfin;       || existencia final
    #2#38 = exini;       || existencia inicio
    #2#39 = campo39;     || compras
    #2#44 = #7#09;       || Codigo Provincia
    #2#45 = #7#10;       || Codigo Poblacion
    #2#46 = #7#4;        || Domicilio

|HAZ profcoef3;          || CALCULA ...
|GRABA 020#2n;
|SI FSalida=0; npro=8; empr_ren=c03; |HAZ sigrabo; |FINSI;      || marca (*)
|CIERRA #2;

HContribu  = #2#0;
HNombre    = Nombre;
HPrograma  = "(reprofco) PROFESIONALES COEFICIENTE";
HLinea     = #2#1;
HPerceptor = #2#3;
HRdto      = #2#32;
HTipo      = "ALTA FICHA DESDE PASE AGICONT";
|HAZ AltaHistorico;
|FPRC;

|PROCESO ultimo2;
    #2#0 = c03;
    #2#1 = 99;
|LEE 001#2];
|SI FSalida = 0;
    |LEE 001#2a;
|SINO;
    |LEE 001#2u;
|FINSI;
|SI FSalida = 0; |Y #2#0 = c03;
        ultima = #2#1 + 1;
|SINO;
        ultima = 1;
|FINSI;
|FPRC;

|PROCESO profcoef1;
|HAZ comun;
|SI grabalo ! 0;
    |HAZ profcoef2;
|FINSI;
|FPRC;

     *********************************************************************
     GRABACION DE EMPRESARIALES EN ESTIMACION DIRECTA
     *********************************************************************

|PROCESO emprdire3;
    #3#23 = #3#17 + #3#18 + #3#19 + #3#20 + #3#21 + #3#22;
    #3#42 = 0;
|PARA nume1 = 24; |SI nume1 [ 41; |HACIENDO nume1 = nume1 + 1;
        #3#42 = #3#42 + #3nume1;
|SIGUE;
    #3#43 = #3#23;
    #3#44 = #3#42;

    #3#45 = #3#43 - #3#44;
    #3#48 = #3#46 - #3#47;
    #3#49 = #3#45 + #3#48;
|FPRC;

|PROCESO emprdire2;
|ABRE #3;
|HAZ ultimo3;
|DDEFECTO #3;
    #3#00 = c03;        || codigo
    #3#01 = ultima;      || linea
    #3#02 = tipo;        || tipo
    #3#03 = perc;        || perceptor
    #3#13 = #7#32 % 101; || epigrafe -1-
    #3#14 = #7#32 % -104;|| epigrafe -2-
    #3#15 = #7#20;       || nombre actividad
    #3#17 = campo17;     || ingresos ordinarios por ventas y servicios
    #3#18 = campo18;     || autoconsumo
    #3#19 = campo19;     || subvenciones
    #3#20 = campo20;     || otras transferencias
    #3#21 = campo21;     || ingresos financieros
    #3#22 = campo22;     || otros ingresos
    #3#23 = #3#17 + #3#18 + #3#19 + #3#20 + #3#21 + #3#22;  || total ingresos
    #3#24 = campo24;     || tributos
    #3#25 = campo25;     || materias primas
    #3#26 = campo26;     || coste personal
    #3#27 = campo27;     || seguridad social
    #3#28 = campo28;     || planes pensiones
    #3#29 = campo29;     || honorarios profesionales
    #3#30 = campo30;     || alquileres
    #3#31 = campo31;     || intereses prestamos
    #3#32 = campo32;     || amortizaciones
    #3#33 = campo33;     || conservacion y reparaciones
    #3#34 = campo34;     || energia, agua, etc.
    #3#35 = campo35;     || seguros
    #3#36 = campo36;     || dudoso cobro
    #3#37 = campo37;     || reduc.fond.edit.
    #3#38 = campo38;     || fondos rever.
    #3#39 = campo39;     || gastos admon.
    #3#40 = campo40;     || cuotas corpor.
    #3#41 = campo41;     || otros gastos
    #3#43 = #3#23;       || total ingresos computados
    #3#45 = #3#23;       || rendimiento neto previo
    #3#49 = #3#23;       || rendimiento neto
    #3#55 = #7#09;       || Codigo Provincia
    #3#56 = #7#10;       || Codigo Poblacion
    #3#57 = #7#4;        || Domicilio

|HAZ emprdire3;
|GRABA 020#3n;
|SI FSalida=0; npro=9; empr_ren=c03; |HAZ sigrabo; |FINSI;      || marca (*)
|CIERRA #3;

HContribu  = #3#0;
HNombre    = Nombre;
HPrograma  = "(reempedi) EMPRESARIALES DIRECTA";
HLinea     = #3#1;
HPerceptor = #3#3;
HRdto      = #3#49;
HTipo      = "ALTA FICHA DESDE PASE AGICONT";
|HAZ AltaHistorico;
|FPRC;

|PROCESO ultimo3;
    #3#0 = c03;
    #3#1 = 99;
|LEE 001#3];
|SI FSalida = 0;
    |LEE 001#3a;
|SINO;
    |LEE 001#3u;
|FINSI;
|SI FSalida = 0; |Y #3#0 = c03;
        ultima = #3#1 + 1;
|SINO;
        ultima = 1;
|FINSI;
|FPRC;

|PROCESO emprdire1;
|HAZ comun;
|SI grabalo ! 0;
    |HAZ emprdire2;
|FINSI;
|FPRC;

     *********************************************************************
     GRABACION DE EMPRESARIALES EN COEFICIENTES
     *********************************************************************

|PROCESO emprcoef3;
    #4#25 = #4#17 + #4#38;
    #4#25 = #4#25-(#4#18+#4#19+#4#20+#4#21+#4#22+#4#23+#4#24+#4#39+#4#40);
|SI #4#25 < 0;  #4#33 = 0;  |FINSI;
    #4#25 = #4#25 % #4#33;
    #4#30 = 0;
|PARA nume1 = 18; |SI nume1 [ 25; |HACIENDO nume1 = nume1 + 1;
        #4#30 = #4#30 + #4nume1;
|SIGUE;
    #4#30 = #4#30 + #4#39 + #4#40 - #4#38;
    #4#41 = #4#17;
    #4#42 = #4#41 - #4#30;
    #4#31 = #4#26 - #4#27 - #4#28 - #4#29;
    #4#32 = #4#42 + #4#31;
|FPRC;

|PROCESO emprcoef2;
|ABRE #4;
|HAZ ultimo4;
|DDEFECTO #4;
    #4#00 = c03;       || codigo
    #4#01 = ultima;      || linea
    #4#02 = tipo;        || tipo
    #4#03 = perc;        || perceptor
    #4#13 = #7#32 % 101; || epigrafe -1-
    #4#14 = #7#32 % -104;|| epigrafe -2-
    #4#15 = #7#20;       || nombre actividad
    #4#17 = campo17;     || ingresos integros
    #4#18 = campo18;     || coste personal
    #4#19 = campo19;     || energia, agua, etc.
    #4#20 = campo20;     || alquileres locales
    #4#21 = campo21;     || gastos financieros
    #4#22 = campo22;     || tributos
    #4#23 = campo23;     || rep. y conservacion
    #4#24 = campo24;     || trabajos otras empresas
    #4#33 = coef_gas;    || (%) gastos
    #4#38 = exfin;       || existencia final
    #4#39 = exini;       || existencia inicio
    #4#40 = campo40;     || compras
    #4#47 = #7#83;       || agraria S/N
    #4#44 = #7#09;       || Codigo Provincia
    #4#45 = #7#10;       || Codigo Poblacion
    #4#46 = #7#4;        || Domicilio

|HAZ emprcoef3;          || CALCULA ...
|GRABA 020#4n;
|SI FSalida=0; npro=10; empr_ren=c03; |HAZ sigrabo; |FINSI;      || marca (*)
|CIERRA #4;

HContribu  = #4#0;
HNombre    = Nombre;
HPrograma  = "(reemprco) EMPRESARIALES COEFICIENTE";
HLinea     = #4#1;
HPerceptor = #4#3;
HRdto      = #4#32;
HTipo      = "ALTA FICHA DESDE PASE AGICONT";
|HAZ AltaHistorico;
|FPRC;

|PROCESO ultimo4;
    #4#0 = c03;
    #4#1 = 99;
|LEE 001#4];
|SI FSalida = 0;
    |LEE 001#4a;
|SINO;
    |LEE 001#4u;
|FINSI;
|SI FSalida = 0; |Y #4#0 = c03;
        ultima = #4#1 + 1;
|SINO;
        ultima = 1;
|FINSI;
|FPRC;

|PROCESO emprcoef1;
|HAZ comun;
|SI grabalo ! 0;
    |HAZ emprcoef2;
|FINSI;
|FPRC;
|| ======================================================= Fin Programa

|| Esta parte son los Incluye de Renta i_husuar / i_activo

|PROCESO AltaHistorico;
HRefer = 0;
|ABRE #100;
#100#0 = HContribu;
#100#1 = 9999;
|LEE 000#100];
|SI FSalida = 0;
    |LEE 000#100a;
|SINO;
    |LEE 000#100u;
|FINSI;

|SI FSalida = 0;|Y #100#0 = HContribu;
        HRefer = #100#1 + 1;
|SINO;
        HRefer = 1;
|FINSI;

|HORA Hora;

HFecha   = ~;
HHora    = (A\(Hora % 102));
HMinuto  = (A\(Hora % 402));
HUsuario = Usuario % 810;

#100#00 = HContribu;
#100#01 = HRefer;
#100#02 = HNombre;
#100#03 = HFecha;
#100#04 = HHora;
#100#05 = HMinuto;
#100#06 = HTipo;
#100#07 = HPrograma;
#100#08 = HLinea;
#100#09 = HPerceptor;
#100#10 = HRdto;
#100#11 = HUsuario;

|GRABA 140#100n;
|CIERRA #100;
|FPRC;

|PROCESO sigrabo;
|ABRE #15;
#15#0 = empr_ren;
|LIBERA #15;
|LEE 110#15=;
FEprogra = FSalida;
|SI #15npro  = 0; |HAZ grabaprog; |FINSI;
|CIERRA #15;
|FPRC;

|PROCESO grabaprog;
|SI FEprogra ! 0;
    |DDEFECTO #15;
    #15#0 = empr_ren;
    #15npro = 1;
    |GRABA 110#15n;
|SINO;
    #15npro = 1;
    |GRABA 110#15a;
|FINSI;
|FPRC;

|| Incluye para separar nombre y apellidos
|PROCESO SeparaNombre;
|| Busca una coma en la cadena

Cadena1 = "";
Cadena2 = "";
Cadena3 = "";

Blanco1 = 0;
Blanco2 = 0;
Coma    = 0;

|PARA Xxx = 1; |SI Xxx [ 40; |HACIENDO Xxx = Xxx + 1;
      Posi = Xxx * 100 + 1;
      Caracter = Cadena % Posi;

      |SI Caracter = ","; Coma = Xxx; |SAL; |FINSI;
|SIGUE;

|SI Coma = 0;
    || Si no hay coma busca el primer blanco empezando por detras

    LongAlfa = Cadena % 0;
    LongNume = LongAlfa;

    |PARA Xxx = LongNume; |SI Xxx ] 1; |HACIENDO Xxx = Xxx - 1;
          Posi = Xxx * 100 + 1;
          Caracter = Cadena % Posi;
          |SI Caracter = " "; Coma = Xxx; |SAL; |FINSI;
    |SIGUE;
|FINSI;

|| Coge los dos primeros caracteres del segundo apellido

Blanco1  = Coma - 1;
Posi     = Blanco1 + 100;
Cadena2  = Cadena % Posi;
LongAlfa = Cadena2 % 0;
LongNume = LongAlfa;

|PARA Xxx = LongNume; |SI Xxx ] 1; |HACIENDO Xxx = Xxx - 1;
      Posi = Xxx * 100 + 1;
      Caracter = Cadena2 % Posi;
      |SI Caracter = " "; Blanco1 = Xxx; |SAL; |FINSI;
|SIGUE;

Blanco1 = Blanco1 + 1;
Posi    = Blanco1 * 100 + LongNume;
Cadena2 = Cadena2 % Posi;

|| Coge las dos primeros caracteres del primer apellido

Blanco1 = Blanco1 - 1;
Posi    = 100 + Blanco1;
Cadena1 = Cadena % Posi;

|| Recoge los dos primero caracteres de Nombre

Posi    = ((Coma + 1) * 100) + 40;
Cadena3 = Cadena % Posi;

ParaQuitar = Cadena1; |HAZ QuitaBlanco; Cadena1 = ParaQuitar;
ParaQuitar = Cadena2; |HAZ QuitaBlanco; Cadena2 = ParaQuitar;
ParaQuitar = Cadena3; |HAZ QuitaBlanco; Cadena3 = ParaQuitar;
|FPRC;

|PROCESO QuitaBlanco;
|ET Cadena;
    |QBF ParaQuitar;
    |SI ParaQuitar ! "";
        Caracter = ParaQuitar % 101;
        |SI Caracter = " ";
            ParaQuitar = ParaQuitar % 240;
            |VETE Cadena;
        |FINSI;
    |FINSI;
|FPRC;
