|FICHEROS;
    camaylis #0;
    camaylin #1;
    camaycab #2;
    cacuenta #3;
|FIN;

|VARIABLES;
    &entrada = 1;
    sw = 0;
    sw_print = 0;
    cuenta = "";
    fecalf = "";
    men1   = "    NO EXISTE EMPRESA";
    mensa1 = "    ERROR ESTA FECHA NO CORRESPONDE AL EJERCICIO";
    traba1 = "";
    varalf = "";
    &s_debe = 0;
    &s_haber = 0;
    &s_saldo = 0;
    &debe = 0;
    &haber = 0;
    informe = "camaylis";
|FIN;

|INCLUYE acceso_i;
|INCLUYE puntos_i;

|PROCESO cuenta; |TIPO 0;
    p_alfa1 = #0Campo;
     salidas = FSalida;
|HAZ punto;
|SI p_error ! 0;  |ERROR;  |ACABA;  |FINSI;
|FPRC;

|PROCESO empieza; |TIPO 7;
  |ABRE #3;
  |LEE 040#3p;
  |SI FSalida = 0;
      #0#0 = #3#0;
  |FINSI;
  |LEE 040#3u;
  |SI FSalida = 0;
      #0#1 = #3#0;
  |FINSI;
  |CIERRA #3;
  |PINTA #0#0;
  |PINTA #0#1;

  |HAZ inicio;
  #0#2 = fec1;
  #0#3 = fec2;
  |PINTA #0#2;
  |PINTA #0#3;
|FPRC;

|PROCESO fechad;
  |SI #0#2 < fec1; |O #0#2 > fec2;
      |MENAV mensa1;
      |ERROR;
      |ACABA;
  |FINSI;
|FPRC;

|PROCESO fechah;
  |SI #0#3 < fec1; |O #0#3 > fec2; |O #0#3 < #0#2;
      |MENAV mensa1;
      |ERROR;
      |ACABA;
  |FINSI;
|FPRC;


|| *************************************************************************
||                          MEOLLO
|| *************************************************************************

|PROCESO cabecera;
  |ABRE #2;
  #2#0 = #1#0;
  #2#1 = #1#1;
  #2#2 = #1#2;
  |LEE 040#2=;
  |SI FSalida ! 0;
      |DDEFECTO #2;
  |FINSI;
  |CIERRA #2;
|FPRC;

|PROCESO leecab;
  |ABRE #3;
  #3#0 = #1#4;
  #3#1 = #1#5;
  |LEE 040#3=;
  |SI FSalida ! 0;
      |DDEFECTO #3;
  |FINSI;
  |CIERRA #3;
|FPRC;

|PROCESO buclepan;
  |SI sw = 0;
      cuenta = #1#4;
      sw = 1;
      |HAZ leecab;
  |FINSI;
  |SI cuenta ! #1#4;
      |SI sw_print = 0; |PIEINF; |FINSI;
      |HAZ borrar;
      cuenta = #1#4;
      |HAZ leecab;
  |FINSI;

  |SI #1#1 ] #0#2; |Y #1#1 [ #0#3;
      debe = 0;
      haber = 0;
      |SI #1#8 = "D";
          debe = #1#9;
      |SINO;
          haber = #1#9;
      |FINSI;
      |HAZ cabecera;
      |PRINT;
      sw_print = 0;
  |SINO;
      |SI #1#8 = "D";
          s_debe = s_debe  + #1#9;
      |SINO;
          s_haber = s_haber + #1#9;
      |FINSI;
      s_saldo = s_debe - s_haber;
      sw_print = 1;
  |FINSI;
|ET fincal;
|FPRC;

|DEFBUCLE camaylis0;
#1#3;
;
#0#0, 0, fec1, 000001;
#0#1, 9, #0#3, 999999;
be;
NULL,buclepan;
|FIN;

|PROCESO impre;  |TIPO 3;
  |IMPRE 0;
  |SI FSalida = 0;
      |INFOR informe;
      |SI FSalida = 0;
          |BUCLE camaylis0;
          |SI sw_print = 0; |PIEINF; |FINSI;
          sw = 0;
          sw_print = 0;
          |FININF;
      |SINO;
          |MENAV "     Error en informe.";
      |FINSI;
      |FINIMP;
  |SINO;
      |MENAV "    Error en impresora.";
  |FINSI;
|FPRC;

|PROCESO borrar;
  s_debe = 0;
  s_haber = 0;
  s_saldo = 0;
|FPRC;
