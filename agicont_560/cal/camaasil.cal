|FICHEROS;
    camaasil #00;        || apuntes lins.
    camaasic #01;        || apuntes cabs.
    cafantas #02;        || def para 'PINTA' de acumulados diario
    caconcep #03;        || conceptos
    cacuenta #04;        || cuentas
    catradia #05;        || actualizacion niveles inferiores
    camandia #06;        || diarios
    cacuentp #10;
    casieaca #11;        || predef. cabs.
    casieali #12;        || predef. lins.
    casiedef #13;        || consulta predef.
    casiedco #14;        || Def para Contrapartidas Predefinidos
    caivarep #15;        || iva repercutido
    caivasop #16;        || iva soportado
    calibros #17;        || libros iva
    caclipro #18;        || clientes/proveedores
    camancob #21;        || cobros
    camanpag #22;        || pagos
    camemori #24;        || ultimas operaciones
    camaasiz #25;        || entrada nombre y cif cliente/proveedor
    caivaasi #26;        || control entrada apuntes
    canempre #30;        || empresas
    calicont #31;        || Contrapartidas
|FIN;

|VARIABLES;
    &EURODB = 0;
    saldo = 0;
    men2  = "    Longitud de Cuenta Fuera de Nivel !";
    men3  = "    Esta Cuenta NO tiene Pase directo";
    cont = 0;
    long = "  ";
    cta  = 0;
    num   = 0;
    sw = 0;
    ctanum = 0;
    ctaalf = "";
    longnum = "";
    vacio = "            ";
    ini = 0;
    fecalf = "";
    mes = "";
    mesfec = 0;
    mesac = 0;
    a = 0;
    b = 0;
    c = 0;
    cuant = "";
    diant = 0;
    &coant = 0;
    &deant = "";
    &dhant = "";
    &imant = 0;
    &dpant = 0;
    &aferlamar = 0;
    trant = "";
    drant = 0;
    fiac = "  ";
    guarda = "  ";
    relleno = "                                       ";
    aponer = "  ";
    auto = 0;
    &dig1 = 0;
    &dig3 = 0;
    &dig4 = 0;
    &dig5 = 0;
    &dig6 = 0;
    &dig7 = 0;
    &dig8 = 0;
    &dig9 = 0;
    &codcon = 0;
    &modcon = 0;
    opera = "";
    importe = 0;
    cta_t = 0;
    dig_t = 0;
    doce = "            ";         || doce espacios
    &entrada = 1;
    xx = 0;
    las_dos = 0;
    otra_vez = 0;
    signo = "";
    &ext1 = "";
    &ext2 = "";
    &ext3 = "";
    &ext4 = "";
    &ext5 = "";
    &inkey = 0;
    sit1 = "";
    sit2 = "";
    sit3 = "";
    sit4 = "";
    sit5 = 0;
    sit6 = 0;
    programa = "";
    &predefine = 0;
    &predefine2 = 0;
    sw_hay = 0;
    alfa1 = "";
    alfa2 = "";
    alfa3 = "";
    alfa4 = "";
    nume1 = 0;
    nume2 = 0;
    nume3 = 0;
    nume4 = 0;
    nume5 = 0;
    nume6 = 0;
    p_eof = 0;
    p_cuenta = "";
    p_linea = 0;
    p_apunte = 0;
    calculo = 0;
    trim1 = @;
    trim2 = @;
    trim3 = @;
    &sw_debe = 0;
    &sw_habe = 0;
    &i_serie = "";
    &i_factu = 0;
    &i_fecha = @;
    i_perio = 0;
    i_cuent = "";
    i_digit = 0;
    i_conce = 0;
    i_descr = "";
    i_depar = "";
    factor = 0;
    &p_tabla = 0;
    &sw_tabla = 0;
    &sw_ptec = 0;
    c_c = 0;
    puntero = 0;
    detector = 0;
    FEstado = 0;
    &avisos = "";
    &f1auto = 0;
    &empre = 0;
    &ejerc = 0;
    i_nom = "";
    i_cif = "";
    i_cc1 = "";
    i_dc1 = 0;
    i_cc2 = "";
    i_dc2 = 0;
    i_signo = 0;
    modo = 0;
    &libro = 0;
    &orden = 0;
    &serie = "";
    &pesetas = 0;
    &cuenta = "";
    &digito = 0;
    &emision = "";
    i_acum = "";
    i_impo = 0;
    salidas = 0;
    &p_cen = 0;
    ant1 = 0;
    ant2 = @;
    ant3 = 0;
    ant4 = 0;
    rafa1 = "";
    rafa2 = "";
    raf1 = 0;
    raf2 = 0;
    sumoresto = 0;
    iva1 = 0;
    iva2 = 0;
    iva3 = 0;
    iva4 = 0;
    iva5 = 0;
    req1 = 0.00;
    req2 = 0.00;
    req3 = 0.00;
    req4 = 0.00;
    req5 = 0.00;

    &j_d = 0000;
    &j_h = 0000;
    &fec_am = @;
    &imp_am = 0;
    &ct1_am = "";
    &ct2_am = 0;
    &prv_am = "";
    &c_am = "";
    primer = "";
    &ctab1 = "";
    &ctab2 = 0;
    ctdeant = "";
    dideant = 0;
    cthaant = "";
    dihaant = 0;
    ctpre   = "";
    dipre   = 0;
    &cdiari = 0;
    &cfecha = @;
    &cdocum = 0;
    &clinea = 0;
    &cdefecta = "";
    &cdefedig = 0;
|FIN;

|INCLUYE puntos_i;
|INCLUYE tempo_i;

|PROCESO refresca; |TIPO 2;        || calculos de (des)actualizacion
    xx = (FEntrada / 100) ! 0;
|SI p_cen = 0;
    |HAZ a_contrap;     || contrapartidas asiento
    |HAZ a_asiento;     || acumulados asiento
    |HAZ a_cuentas;     || acumulados cuenta
    |HAZ pinta_cta;     || pinta los datos de la cuenta
    |HAZ a_diario;      || acumulados diario
    |SI #0#12 = "R";  |HAZ a_ivar;  |FINSI;  || iva repercutido
    |SI #0#12 = "S";  |HAZ a_ivas;  |FINSI;  || iva soportado
    |SI #0#12 = "C";  |HAZ a_cobr;  |FINSI;  || cobros
    |SI #0#12 = "P";  |HAZ a_pago;  |FINSI;  || pagos
    |SI #0#12 = "I";  |HAZ a_impa;  |FINSI;  || impagados
|FINSI;
|FPRC;

|PROCESO saalta; |TIPO 0;               || desde campo 'linea'
    inkey = FSalida;
|SI FEntrada = 101; |Y #0#3 ! 1;
    |SI coant ! 0;
            #0#6  = coant;
            #0#8  = dhant;
            #0#21 = dpant;
    |FINSI;
    |PINTA #0#6;
    |PINTA #0#21;
|FINSI;
|SI predefine ! 0;
    |SI predefine ! 0;             || carga los campos relacionados con IVAS
            #0#12 = #11#3;    || tipo (soportado/repercutido)
            #0#13 = #11#4;    || libro
            #0#14 = i_factu;  || factura/orden
            #0#15 = i_serie;  || serie
            #0#19 = #12#56;   || tipo acumulador IVA
            |SI #12#1 = #11#5; #0#19 = "F"; |FINSI;
            |SI #11#9 = 1;
                |SI #12#56 = "B"; #0#19 = "b"; |FINSI;
                |SI #12#56 = "I"; #0#19 = "i"; |FINSI;
                |SI #12#56 = "R"; #0#19 = "r"; |FINSI;
                |SI #12#56 = "b"; #0#19 = "B"; |FINSI;
                |SI #12#56 = "i"; #0#19 = "I"; |FINSI;
                |SI #12#56 = "r"; #0#19 = "R"; |FINSI;
              |FINSI;
            #0#20 = #12#57;   || nro. acumulador IVA
    |SINO;
            #0#12 = "";
            #0#13 = 0;
            #0#14 = 0;
            #0#15 = "";
            #0#19 = "";
            #0#20 = 0;
    |FINSI;
|FINSI;
|FPRC;

|PROCESO diez;                     || desde calculo 'c_apunte'
    xx = (FEntrada / 100) ! 0;
|SI xx = 1;|Y FSalida ! 1;|Y FSalida ! 7;|Y #0#3 ! 1;
        #0#3 = #0#3 + 9;
    |PINTA #0#3;
|FINSI;
|FPRC;

|PROCESO entmod; |TIPO 1;          || desde campo 'linea'
    coant = #0#6;
    deant = #0#7;
    opera = "B";
|HAZ altatra;
    xx = (FEntrada / 100) ! 0;
    |SI xx = 3;
        |ABRE #31;
        #31#0 = #0#0;
        #31#1 = #0#1;
        #31#2 = #0#2;
        #31#3 = #0#3;
        |LEE 101#31=;
        |SI FSalida = 0;
            |BORRA 020#31a;
        |FINSI;
        |LIBERA #31;
        |CIERRA #31;
    |FINSI;
|FPRC;

|PROCESO guarda; |TIPO 3;          || desde campo 'linea'
  alfa1 = #0#16;   |QBF alfa1;
  alfa2 = #0#10;   |QBF alfa2;
  |SI alfa1 = "";  |Y alfa2 = "";    ||al menos una cuenta cargada;
      avisos = "ATENCION!!! Apunte sin cuentas ...";
      |HAZ aviso5;
      |PAUSA;
      |HAZ aviso9;
      |ERROR;
      |SI predefine ! 0; #12#1 = #12#1 - 1; |FINSI;
      |ACABA;
  |FINSI;
  |SI #0#9 = 0;                       || importe distinto de cero
      avisos = "ATENCION!!! Importe cero ...";
      |HAZ aviso5;
      |PAUSA;
      |HAZ aviso9;
      |ERROR;
      |ACABA;
  |FINSI;
  |SI alfa1 ! "";
      ctdeant = #0#16;
      dideant = #0#17;
  |FINSI;
  |SI alfa2 ! "";
      cthaant = #0#10;
      dihaant = #0#11;
  |FINSI;
  cuant = #0#16;
  diant = #0#17;
  coant = #0#6;
  deant = #0#7;
  dhant = #0#8;
  imant = #0#9;
  trant = #0#10;
  drant = #0#11;
  dpant = #0#21;
  |SI #0#16 ! doce; |Y #0#10 ! doce;        || provoca otro apunte
      #0#4 = #0#16;         || cuenta DEBE
      #0#5 = #0#17;         || digito DEBE
      #0#8 = "D";           || signo DEBE
      #0#10 = "";           || vacia cuenta HABER
      #0#11 = 9;            || vacia digito HABER
      |GRABA 020#0n;        || graba el actual
      opera = "A";
      |HAZ altatra;
      #0#3 = #0#3 + 10;     || aumenta el apunte
      #0#4 = trant;         || cuenta HABER
      #0#5 = drant;         || digito HABER
      #0#16 = "";           || vacia cuenta DEBE
      #0#17 = 9;            || vacia digito DEBE
      #0#6 = coant;         || concepto
      #0#7 = deant;         || descripcion
      #0#8 = "H";           || signo
      #0#9 = imant;         || importe
      #0#10 = trant;        || cuenta HABER
      #0#11 = drant;        || digito HABER
      #0#21 = dpant;        || departamento
      |GRABA 020#0n;        || graba el provocado
      opera = "A";
      |HAZ altatra;
      |LEE 020#0=;
      #0#16 = cuant;
      #0#17 = diant;
      #0#6 = coant;
      #0#7 = deant;
      #0#8 = dhant;
      #0#9 = imant;
      #0#10 = trant;
      #0#11 = drant;
      #0#21 = dpant;          || departamento
      aponer = "kksi";
      |HAZ poncontr;
      Pos = -1;
      |PTEC 809;
      |PTEC 809;
      |PTEC 808;
  |SINO;
       opera = "A";
       |HAZ altatra;
  |FINSI;
  |SI sumoresto = 1;
      aponer = "kksi";
      |HAZ poncontr;
      Pos = -1;
      sumoresto = 0;
      |PTEC 809;
      |PTEC 809;
  |FINSI;
  ctpre = #0#4;
  dipre = #0#5;
|FPRC;

|PROCESO funcion1; |TIPO 11;                  || desde campo 'linea'
  inkey = FSalida;
  modo = (FEntrada / 100) ! 0;
  |SI modo ! 1;
      |HAZ funcion2;
  |SINO;
      |SI Campo = 3;  |Y inkey = 14;  |Y  #1#6 = 0;  |Y #1#4 ! 0;  |Y #1#5 ! 0;
          |PTEC 802;
          |PTEC 802;
          |PTEC 802;
          |PTEC 802;
          |PTEC 802;
          |PTEC 806;
          |PTEC 807;
      |SINO;
          |SI inkey ] 9;|Y inkey [ 20;
              |AVISO;
              |ERROR;
          |FINSI;
     |FINSI;
  |FINSI;
|FPRC;

|PROCESO funcion2;                           || desde calculo 'funcion1'
  |SI inkey > 14; |Y inkey [ 20; |Y inkey ! 18; |Y inkey ! 17;
      |AVISO;
      |ERROR;
      |ACABA;
  |FINSI;
  |SI inkey = 9;            || extractos
      ext1 = #0#4;          || cuenta
      ext2 = #0#5;          || digito
      programa = "caextrap.run";
      |HAZ externos;
      |ERROR;
      |ACABA;
  |FINSI;
  |SI inkey = 10;               || diario de trabajo
      ||ext1 = #1#0;          || desde diario
      ||ext2 = #1#0;          || hasta diario
      programa = "caditrab.run";
      |HAZ externos;
      |ERROR;
      |ACABA;
  |FINSI;
  |SI inkey = 11;               || anotaciones extracontables
      ext1 = #0#0;          || diario
      ext2 = #0#1;          || fecha
      ext3 = #0#2;          || documento
      programa = "caextra0.run";
      |HAZ externos;
      |ERROR;
      |ACABA;
  |FINSI;
  |SI inkey = 12;               || consulta cobros
      ext1 = #0#4;          || cuenta
      ext2 = #0#5;          || digito
      programa = "caconcob.run";
      |HAZ externos;
      |ERROR;
      |ACABA;
  |FINSI;
  |SI inkey = 13;               || consulta pagos
      ext1 = #0#4;          || cuenta
      ext2 = #0#5;          || digito
      programa = "caconpag.run";
      |HAZ externos;
      |ERROR;
      |ACABA;
  |FINSI;
  |SI inkey = 14;
      |SI Campo = 3; |ERROR; |ACABA; |FINSI;
      raf1 = (FEntrada / 100) ! 0;
      |SI raf1 ! 2;  |ERROR; |ACABA; |FINSI;
      ant1 = #0#0;
      ant2 = #0#1;
      ant3 = #0#2;
      ant4 = #0#3;
      |LEE 021#0a;
      |SI FSalida ! 0; |O #0#2 ! ant3;
          |MENAV "     NO SE PUEDE EJECUTAR ESTA OPCION";
          #0#0 = ant1;
          #0#1 = ant2;
          #0#2 = ant3;
          #0#3 = ant4;
          |LEE 121#0=;
          |ERROR;
          |ACABA;
      |FINSI;
      #0#0 = ant1;
      #0#1 = ant2;
      #0#2 = ant3;
      #0#3 = ant4;
      |LEE 021#0=;
      E_sup = 1;
      E_inf = "+-";
      rafa1 = " ";
      |MENSA "2405 DESEA SUMAR O RESTAR EL IMPORTE + o -: ";
      rafa1 = 2445 ? 1;
      |MENSA "2405                                          ";
      |SI rafa1 = "+";
          #0#9 = #0#9 + 1;
      |SINO;
          #0#9 = #0#9 - 1;
      |FINSI;
      rafa2 = #0#8;
      |PINDA #0#0;
      |GRABA 020#0a;
      |LEE 020#0a;
      opera = "B";
      |HAZ altatra;
      |ABRE #6;
      #6#0 = #0#0;
      |LEE 141#6=;
      |SI rafa1 = "+";
          |SI rafa2 = #0#8;
              #0#9 = #0#9 - 1;
              raf2 = -1;
          |SINO;
              #0#9 = #0#9 + 1;
              raf2 = 1;
          |FINSI;
      |SINO;
          |SI rafa2 = #0#8;
              #0#9 = #0#9 + 1;
              raf2 = 1;
          |SINO;
              #0#9 = #0#9 - 1;
              raf2 = -1;
          |FINSI;
      |FINSI;
      |SI #0#8 = "D";
          #6#2 = #6#2 + raf2;
          #1#4 = #1#4 + raf2;
      |SINO;
          #6#3 = #6#3 + raf2;
          #1#5 = #1#5 + raf2;
      |FINSI;
      |GRABA 020#6a;
      |LIBERA #6;
      |CIERRA #6;
      #2#0 = #6#2;
      #2#1 = #6#3;
      #2#2 = #2#0 - #2#1;
      |ATRI I;
      |PINTA 0639, #2#0;
      |PINTA 0652, #2#1;           || pinta acumulados de diario
      |PINTA 0665, #2#2;
      |ATRI 0;
      |GRABA 020#0a;
      raf1 = #0#9;
      #0#9 = raf2;
      |HAZ a_cuentas;
      |SI #0#12 = "R";
          |HAZ a_ivar;
      |FINSI;
      |SI #0#12 = "S";
          |HAZ a_ivas;
      |FINSI;
      #0#9 = raf1;
      #1#6 = #1#4 - #1#5;
      |PINTA #1#4;
      |PINTA #1#5;        || acumulados asiento
      |PINTA #1#6;
      opera = "A";
      |HAZ altatra;
      #0#0 = ant1;
      #0#1 = ant2;
      #0#2 = ant3;
      #0#3 = ant4;
      |LEE 021#0=;
      sumoresto = 1;
      |PTEC 806;
      |ACABA;
  |FINSI;
  |SI inkey = 17;
      |HAZ lacontra;
  |FINSI;
||    |ERROR;
|FPRC;

|PROCESO lacontra;
      ext1 = #0#0;          || diario
      ext2 = #0#1;          || fecha
      ext3 = #0#2;          || documento
      cdiari = #0#0;
      cfecha = #0#1;
      cdocum = #0#2;
      clinea = #0#3;
      |HAZ signo;
      cdefecta = ""; cdefedig = 0;
      |SI #0#8 = "H";
          |SI j_d ! 0;
              cdefecta = #1#8;
              cdefedig = #1#9;
          |FINSI;
      |SINO;
          |SI j_h ! 0;
              cdefecta = #1#10;
              cdefedig = #1#11;
          |FINSI;
      |FINSI;
      programa = "calicont.tab";
      |HAZ externos;
      |ERROR;
|FPRC;

|PROCESO cuefin; |TIPO 0;                    || desde campo 'cuenta'
  |HAZ signo;
  |SI inkey = 16;
      |SI predefine ! 0; |Y #0#3 = 1; predefine = 0; |PTEC 807; |FINSI;
      |PTEC 807;
      |ERROR;
  |SINO;
      cta_t = Campo;
      dig_t = cta_t + 1;
      |HAZ cuefin_t;
      |HAZ funcion2;
  |FINSI;
|FPRC;

|PROCESO otravez;
 cta_t = Campo - 1;
 dig_t = Campo;
    alfa1 = #0cta_t;
 |QBF alfa1;
 |SI alfa1 = ""; |ACABA; |FINSI;
     alfa2 = alfa1 % 0;
     long = alfa2;
     sw = 0;
 |SI long = dig4;  sw = 1;  #0dig_t = 1;  |FINSI;
 |SI long = dig5;  sw = 1;  #0dig_t = 2;  |FINSI;
 |SI long = dig6;  sw = 1;  #0dig_t = 3;  |FINSI;
 |SI long = dig7;  sw = 1;  #0dig_t = 4;  |FINSI;
 |SI long = dig8;  sw = 1;  #0dig_t = 5;  |FINSI;
 |SI long = dig9;  sw = 1;  #0dig_t = 6;  |FINSI;
 |SI long = 0;     sw = 1;  #0dig_t = 0;  |FINSI;
|FPRC;

|PROCESO cuefin_t;                      || desde calculo 'cuefin'
  |ABRET #4;
  #4#0 = #0cta_t;
  #4#1 = #0dig_t;
  |LEE 001#4=;
  |SI FSalida ! 0;
      |ERROR;
      |ACABA;
  |FINSI;
  |SI #4#3 = "N";
      |MENAV men3;
      |ERROR;
      |ACABA;
  |FINSI;
  |CIERRAT #4;
  |HAZ pinta_cta;
|FPRC;

|PROCESO cuenta; |TIPO 6;                    || desde campo 'cuenta'
  ||varia_cod = #0Campo;
  inkey = FSalida;
  |SI inkey = 15;
      |SI Campo = 10;
          #0#10 = cthaant;
          #0#11 = dihaant;
      |FINSI;
      |SI Campo = 16;
          #0#16 = ctdeant;
          #0#17 = dideant;
      |FINSI;
      |PINTA #0Campo;
      inkey = 0;
  |FINSI;
  p_alfa1 = #0Campo;
  salidas = FSalida;
  |HAZ punto;
  |SI p_error ! 0;  |ERROR;  |ACABA;  |FINSI;
  cta_t = Campo;
  dig_t = cta_t + 1;
  |HAZ blanco;
  |HAZ cuenta_t;
|FPRC;

|PROCESO blanco;              || desde calculo 'cuenta'
    alfa1 = #0cta_t;   |QBF alfa1;
|SI alfa1 = "";
    |ABRET #4;
        #4#0 = #0cta_t;
        #4#1 = #0dig_t;
    |LEE 001#4=;
    |SI FSalida ! 0;
        |DDEFECTO #4;
            #4#3 = "S";       || pase directo si
        |PARA nume1 = 1; |SI nume1 [ dig3; |HACIENDO nume1 = nume1 + 1;
            |SI nume1 = 1;  #4#1 = dig4;  |FINSI;
            |SI nume1 = 2;  #4#1 = dig5;  |FINSI;
            |SI nume1 = 3;  #4#1 = dig6;  |FINSI;
            |SI nume1 = 4;  #4#1 = dig7;  |FINSI;
            |SI nume1 = 5;  #4#1 = dig8;  |FINSI;
            |SI nume1 = 6;  #4#1 = dig9;  |FINSI;
            |GRABA 040#4n;
        |SIGUE;
            #4#1 = 0;
        |GRABA 040#4n;        || graba con el nivel 0 (por si acaso)
    |FINSI;
    |CIERRAT #4;
|FINSI;                  || graba automaticamente las cuentas en blanco
|FPRC;

|PROCESO cuenta_t;
    alfa1 = #0cta_t;
|QBF alfa1;
    alfa2 = alfa1 % 0;
    long = alfa2;
    cont = 0;
    sw = 0;
|SI long = dig4;  sw = 1;  #0dig_t = 1;  |FINSI;
|SI long = dig5;  sw = 1;  #0dig_t = 2;  |FINSI;
|SI long = dig6;  sw = 1;  #0dig_t = 3;  |FINSI;
|SI long = dig7;  sw = 1;  #0dig_t = 4;  |FINSI;
|SI long = dig8;  sw = 1;  #0dig_t = 5;  |FINSI;
|SI long = dig9;  sw = 1;  #0dig_t = 6;  |FINSI;
|SI long = 0;     sw = 1;  #0dig_t = 0;  |FINSI;
|SI sw = 0;
        #0dig_t = 0;
    |MENAV men2;
    |ERROR;
    |ACABA;
|FINSI;
|FPRC;

|PROCESO concepto; |TIPO 0;             || desde campo 'concepto'
 inkey = FSalida;
 |SI inkey = 17; |HAZ lacontra; inkey = 0; |ACABA; |FINSI;
 |ABRE #3;
 #3#0 = #0#6;
 |LEE 040#3=;
 |SI FEntrada = 101;
         #0#7 = #3#1;
 |SINO;
    |SI #0#6 = coant;
        #0#7 = deant;
    |SINO;
        #0#7 = #3#1;
    |FINSI;
 |FINSI;
 |PINTA #0#7;
 |CIERRA #3;
|FPRC;

|PROCESO descrip; |TIPO 0;        || desde campo 'descripcion'
  inkey = FSalida;
  |SI inkey = 17; |HAZ lacontra; inkey = 0; |ACABA; |FINSI;
  |SI FSalida = 15;
      #0#7 = deant;
      |PINTA #0#7;
  |FINSI;
|FPRC;

|PROCESO nocero; |TIPO 0;          || desde campo 'importe'
    inkey = FSalida;
    f1auto = 0;
    inkey = FSalida;
    |SI inkey = 17; |HAZ lacontra; inkey = 0; |ACABA; |FINSI;
|HAZ aviso9;
|SI #0Campo = 0;|Y inkey ! 10; |Y inkey ! 11;
    |ERROR;
    |ACABA;
|SINO;
    |SI inkey = 10; |O inkey = 11;   || <F2>
        |SI #0#8 = "D";  #0#9 = #1#6 * -1;  |FINSI;
        |SI #0#8 = "H";  #0#9 = #1#6;       |FINSI;
        |PINTA #0#9;
    |FINSI;
    |SI inkey ! 2;                 || cuando no es flecha arriba
        |HAZ c_predefi;            || opera en predef. al salir del apunte
    |FINSI;
    |SI inkey = 9; |O inkey = 10;
            f1auto = 1;
        |PTEC 806;
    |FINSI;
|FINSI;
|FPRC;

|PROCESO signo;                    || desde calculo 'c_importe' y 'cuefin'
|SI #0#16 ! doce;
        #0#8 = "D";
        #0#4 = #0#16;
        #0#5 = #0#17;
|FINSI;
|SI #0#10 ! doce;
        #0#8 = "H";
        #0#4 = #0#10;
        #0#5 = #0#11;
|FINSI;
|SI #0#16 ! doce;|Y #0#10 ! doce;
        #0#8 = "x";
        #0#4 = "";
        #0#5 = 9;
|FINSI;
|FPRC;

****************************************************************************
                    RUTINAS VARIAS
****************************************************************************

|PROCESO a_contrap;
|SI j_d = 0; sw_debe = 0; |FINSI;
|SI j_h = 0; sw_habe = 0; |FINSI;
xx = (FEntrada / 100) ! 0
|SI xx = 3;
    |SI j_d = #0#3; sw_debe = 0; j_d = 0; |FINSI;
    |SI j_h = #0#3; sw_habe = 0; j_h = 0; |FINSI;
    |ACABA;
|FINSI;

    |SI sw_debe = 0;
        |SI #0#8 ! "H";
            #1#08 = #0#16;             || contrap. HABER
            #1#09 = #0#17;             || digito HABER
            sw_debe = 1;
            j_d = #0#3;
        |FINSI;
    |SINO;
        |SI j_d = #0#3;
            |SI #0#8 ! "H";
                #1#08 = #0#16;             || contrap. HABER
                #1#09 = #0#17;             || digito HABER
                j_d = #0#3;
            |SINO;
                j_d = 0;
                sw_debe = 0;
            |FINSI;
        |FINSI;
    |FINSI;
 || -------------------------------------------------------------------
    |SI sw_habe = 0;
        |SI #0#8 ! "D";
            #1#10 = #0#10;             || contrap. DEBE
            #1#11 = #0#11;             || digito DEBE
            sw_habe = 1;
            j_h = #0#3;
        |FINSI;
    |SINO;
        |SI j_h = #0#3;
            |SI #0#8 ! "D";
                #1#10 = #0#10;             || contrap. DEBE
                #1#11 = #0#11;             || digito DEBE
                j_h = #0#3;
            |SINO;
                j_h = 0;
                sw_habe = 0;
            |FINSI;
        |FINSI;
    |FINSI;

|FPRC;

|PROCESO a_asiento;                || calculo 'refresca'
|SI #0#8 = "D";|O #0#8 = "x";  #1#4 = #1#4 +. #0#9;  |FINSI;
|SI #0#8 = "H";|O #0#8 = "x";  #1#5 = #1#5 +. #0#9;  |FINSI;
    #1#6 = #1#4 - #1#5;
|PINTA #1#4;
|PINTA #1#5;        || acumulados asiento
|PINTA #1#6;
|FPRC;

|PROCESO a_diario;                 || calculo 'refresca'
|ABRE #6;
    #6#0 = #0#0;
|LEE 141#6=;
|SI FSalida ! 0;              || actualiza el diario
    |DDEFECTO #6;
        #6#0 = #0#0;
        #6#1 = "<DIARIO SIN DEFINIR>";
    |GRABA 020#6b;
|FINSI;
|SI #0#8 = "D";|O #0#8 = "x";
        #6#2 = #6#2 +. #0#9;
|FINSI;
|SI #0#8 = "H";|O #0#8 = "x";
        #6#3 = #6#3 +. #0#9;
|FINSI;
|GRABA 020#6a;
|LIBERA #6;
|CIERRA #6;
    #2#0 = #6#2;
    #2#1 = #6#3;
    #2#2 = #2#0 - #2#1;
|ATRI I;
|PINTA 0639, #2#0;
|PINTA 0652, #2#1;           || pinta acumulados de diario
|PINTA 0665, #2#2;
|ATRI 0;
|FPRC;

|PROCESO a_cuentas;                || calculo 'refresca'
  las_dos = 1;
  |SI #0#10 ! doce;  cta_t = 10;  signo = "H";  |FINSI;
  |SI #0#16 ! doce;  cta_t = 16;  signo = "D";  |FINSI;|| se queda con esta
                                                       ||/si van las 2 llenas
  |PARA otra_vez=1; |SI otra_vez[ las_dos; |HACIENDO otra_vez=otra_vez+1;
        dig_t = cta_t + 1;

        |ABRET #4;
        #4#0 = #0cta_t;
        #4#1 = #0dig_t;           || actualizacion cuentas
        |LEE 101#4=;
        |SI FSalida = 0;
            |SI #0#0 = 0;
                |SI signo = "D";
                    #4#9 = #4#9 +. #0#9;        || diario cero (saldos iniciales)
                    #4#51 = #4#51 +. #0#9;
                |SINO;
                    #4#10 = #4#10 +. #0#9;
                    #4#52 = #4#52 +. #0#9;
                |FINSI;
                #4#11 = #4#9 - #4#10;
                #4#53 = #4#51 - #4#52;
            |SINO;
                |SI #0#0 = 99;
                    |SI signo = "D";
                        #4#48 = #4#48 +. #0#9;       || diario 99 (saldos finales)
                        #4#51 = #4#51 +. #0#9;
                    |SINO;
                        #4#49 = #4#49 +. #0#9;
                        #4#52 = #4#52 +. #0#9;
                    |FINSI;
                    #4#50 = #4#48 - #4#49;
                    #4#53 = #4#51 - #4#52;
                |SINO;
                    |HAZ a_actualiza;       || saldos del mes correspondiente
                |FINSI;
            |FINSI;
            |SI #4#58 < #0#1;
                #4#58 = #0#1;               || fecha ultimo apunte
            |FINSI;
            |GRABA 020#4a;
       |FINSI;
       |LIBERA #4;
       |CIERRAT #4;
       |SI #0#8 = "x";
           cta_t = 10;                 || por si lleva las dos ctas. llenas
           las_dos = 2;
           signo = "H";
       |FINSI;                             || pega otra pasada
  |SIGUE;
|FPRC;

|PROCESO a_actualiza;                || rutina 'a_cuentas'
    ini = dig1;
    fecalf = #0#1;
    mes = fecalf % 402;
    mesfec = mes;
|SI ini = 1;
        mesac = mesfec;
|SINO;
        mesac = (mesfec - ini) + 1;
    |SI mesac [ 0;
            mesac = (13 - ini) + mesfec;
    |FINSI;
|FINSI;
    a = (mesac * 3) + 9;
    b = a + 1;
    c = a + 2;
|SI signo = "D";
        #4a = #4a +. #0#9;
        #4#51 = #4#51 +. #0#9;
|SINO;
        #4b = #4b +. #0#9;
        #4#52 = #4#52 +. #0#9;
|FINSI;
    #4c = #4a - #4b;
    #4#53 = #4#51 - #4#52;
|FPRC;

|PROCESO a_ivar;                   || desde 'refresca'
|ABRE #15;
    #15#0  = #0#13;      || libro
    #15#2  = #0#14;      || factura
    #15#27 = #0#15;      || serie
|LEE 101#15=;
|SI FSalida = 0;
    |SI #15#62 > 1;
        avisos = "ATENCION!!! Facturas Englodadas. Revisar la Factura ...";
        |HAZ aviso5;
        |PAUSA;
        |HAZ aviso9;
    |FINSI;
    |HAZ a_busca;
|FINSI;
|LIBERA #15;
|CIERRA #15;
|FPRC;

|PROCESO a_ivas;                   || desde 'refresca'
|ABRE #16;
    #16#0 = #0#13;       || libro
    #16#2 = #0#14;       || orden
    #16#27 = #0#15;      || serie
|LEE 101#16=;
|SI FSalida = 0;  |HAZ a_busca;  |FINSI;
|LIBERA #16;
|CIERRA #16;
|FPRC;

|PROCESO a_busca;             || desde 'a_iva?'
  |SI #0#20 ! 0;      || si el apunte influye en algun acumulador de IVAS
      |HAZ c_impiva;  || recalcula y regraba
      |SI #0#12 = "R";
          |HAZ total_ivar;
          |SI xx = 2;                || en modificacion siempre regraba
              |GRABA 020#15a;
              |LIBERA #15;
          |FINSI;
          |SI xx = 3;
              |SI #15#26 ! 0;        || en baja lo borra si el importe es cero
                  |GRABA 020#15a;
              |SINO;
                  |BORRA 020#15a;
                  |HAZ bor_cobros;
              |FINSI;
              |LIBERA #15;
          |FINSI;
      |FINSI;
      |SI #0#12 = "S";
          |HAZ total_ivas;
          |SI xx = 2;                || en modificacion siempre regraba
              |GRABA 020#16a;
              |LIBERA #16;
          |FINSI;
          |SI xx = 3;
              |SI #16#26 ! 0;        || en baja lo borra si el importe es cero
                  |GRABA 020#16a;
              |SINO;
                  |BORRA 020#16a;
                  |HAZ bor_pagos;
              |FINSI;
              |LIBERA #16;
          |FINSI;
      |FINSI;
      |ACABA;
  |FINSI;
||___________________/ si se trata del apunte del cliente/proveedor
  detector = 0 +. 1;
  |SI detector ! 1;  |ACABA;  |FINSI;
  |SI #0#19 ! "F"; |ACABA; |FINSI;
  |SI #0#12 = "R";           || regraba la cuenta por si ha cambiado
      #15#04 = #0#4;
      #15#05 = #0#5;
      #15#06 = #0#6;
      #15#07 = #0#7;
      #11#3 = #0#12;
      i_cuent = #15#4;
      i_digit = #15#5;
      |HAZ c_nombre;
      #15#50 = i_nom;      || nombre
      #15#51 = i_cif;      || cif
      |GRABA 020#15a;
      |LIBERA #15;
      #11#4 = #15#00;      || libro
      i_factu = #15#02;    || factura
      i_serie = #15#27;    || serie
      |HAZ cobros;
  |FINSI;
  |SI #0#12 = "S";
      #16#04 = #0#4;
      #16#05 = #0#5;
      #16#06 = #0#6;
      #16#07 = #0#7;
      #11#3 = #0#12;
      i_cuent = #16#4;
      i_digit = #16#5;
      |HAZ c_nombre;
      #16#50 = i_nom;      || nombre
      #16#51 = i_cif;      || cif
      |GRABA 020#16a;
      |LIBERA #16;
      #11#4 = #16#00;      || libro
      i_factu = #16#02;    || factura
      i_serie = #16#27;    || serie
      |HAZ pagos;
  |FINSI;
|FPRC;

|PROCESO a_cobr;                   || desde 'refresca'
  |ABRE #21;
  #21#0 = #0#13;      || libro
  #21#1 = #0#14;      || factura
  #21#2 = #0#15;      || ~serie
  #21#3 = #0#22;      || recibo
  |LEE 101#21=;
  |SI FSalida ! 0;  |LIBERA #21; |CIERRA #21;  |ACABA;  |FINSI;
  #21#24 = #21#24 +. #0#9;        || ptas. liquidadas
  |SI #21#24 < #21#8;|O #21#24 = 0;
      #21#23 = "N";          || no liquidada, cuando queda pendiente
      nume1 = (#21#8 * 2);
     |SI #21#24 < 0;|Y #21#24 = nume1;    || parche para borrar bien abonos
         #21#24 = 0;
     |FINSI;
  |SINO;
      #21#23 = "S";          || liquidada, cuando queda pendiente
  |FINSI;
  |GRABA 020#21a;
  |LIBERA #21;
  |CIERRA #21;
|FPRC;

|PROCESO a_pago;                   || desde 'refresca'
|ABRE #22;
    #22#0 = #0#13;      || libro
    #22#1 = #0#14;      || orden
    #22#2 = #0#15;      || ~serie
    #22#3 = #0#22;      || recibo
|LEE 101#22=;
|SI FSalida = 0;
        #22#23 = #22#23 +. #0#9;        || ptas. liquidadas
    |SI #22#23 < #22#8;|O #22#23 = 0;
            #22#22 = "N";          || no liquidada, cuando queda pendiente
            nume1 = (#22#8 * 2);
        |SI #22#23 < 0;|Y #22#23 = nume1;    || parche para borrar bien abonos
                #22#23 = 0;
        |FINSI;
    |SINO;
            #22#22 = "S";          || liquidada, cuando queda pendiente
    |FINSI;
    |GRABA 020#22a;
|FINSI;
|LIBERA #22;
|CIERRA #22;
|FPRC;

|PROCESO a_impa;                   || desde 'refresca'
    avisos = "ATENCION!!! Impagado Automatico. Revisar Cobros ...";
|HAZ aviso5;
|PAUSA;
|HAZ aviso9;
|FPRC;

|PROCESO pinta_cta; |TIPO 13;      || desde 'refresca', 'cuefin_t', CONSULTA
|SI #0#16 ! doce;
        cta_t = 16;
    |HAZ pinta_cta_t;
|SINO;
    |SI #0#10 ! doce;
            cta_t = 10;
        |HAZ pinta_cta_t;
    |SINO;
        |DDEFECTO #4;
    |FINSI;
|FINSI;
|ATRI I;
|PINTA 2204, #4#2;
|PINTA 2242, #4#51;
|PINTA 2255, #4#52;
|PINTA 2268, #4#53;
|ATRI 0;
|FPRC;

|PROCESO pinta_cta_t;              || rutina 'pinta_cta'
    dig_t = cta_t + 1;
|ABRET #4;
    #4#0 = #0cta_t;
    #4#1 = #0dig_t;
|LEE 001#4=;
|SI FSalida ! 0;  |DDEFECTO #4;  |FINSI;
|CIERRAT #4;
|FPRC;

|PROCESO altatra;                  || calculo 'guarda' y 'entmod'
|SI opera = "A";
    |HAZ ultimos;                  || graba ultimas operaciones
|FINSI;
    dig_t = cta_t + 1;
    modcon = 1;               || graba el temporal para la actualiz. de ctas.
|ABRE #5;
    #5#0 = codcon;
    #5#1 = #0#4;
    #5#2 = #0#5;
    #5#3 = #0#1;
    #5#4 = #0#8;
    #5#6 = #0#0;
|LEE 101#5=;
|SI FSalida ! 0;
    |DDEFECTO #5;
        #5#0 = codcon;
        #5#1 = #0#4;
        #5#2 = #0#5;
        #5#3 = #0#1;
        #5#4 = #0#8;
        #5#6 = #0#0;
    |GRABA 020#5b;
|FINSI;
    importe = #0#9;
|SI opera = "B";
        importe = -importe;
|FINSI;
    #5#5 = #5#5 + importe;
|GRABA 020#5a;
|LIBERA #5;
|CIERRA #5;
|FPRC;

|PROCESO poncontr;                 || calculo 'guarda'
    fiac = Control % A109;
    fiac = fiac + relleno;
    guarda = fiac % A109;
    fiac = Control % A1001;
    Control = Control + fiac;
    Control = Control + relleno;
    Control = Control % A120;
    Control = Control + aponer;
|FPRC;

|PROCESO externos;                      || desde calculo 'cuefin'
  |ATRI P;
  |MENSA "    Cargando Opcion ...";
  |ATRI 0;
  sit1 = #0#0;         || diario
  sit2 = #0#1;         || fecha
  sit3 = #0#2;         || documento        / las guarda para la vuelta
  sit4 = #0#3;         || apunte
  sit5 = #1#4;         || debe asiento
  sit6 = #1#5;         || haber asiento
  |LIBERA #0;
  |CIERRAT #0;        || cierra lins.
  |LIBERA #1;
  |CIERRAT #1;        || cierra cabs.

  |SUB_EJECUTA programa;

  |ABRET #0;          || abre lins.
  #0#0 = sit1;         || diario
  #0#1 = sit2;         || fecha
  #0#2 = sit3;         || documento
  #0#3 = sit4;         || apunte
  |LEE 101#0=;        || lee el apunte
  |ABRET #1;          || abre cabs.
|SALVA_DATOS #1;
  #1#0 = sit1;         || diario
  #1#1 = sit2;         || fecha
  #1#2 = sit3;         || documento
  |LEE 101#1=;        || lee el asiento
|REPON_DATOS #1;
  #1#4 = sit5;         || debe asiento
  #1#5 = sit6;         || haber asiento
  #1#6 = #1#4 - #1#5;  || saldo asiento
|FPRC;

|PROCESO ultimos;                  || desde rutina 'altatra'
  alfa1 = Usuario;     |QBF alfa1;
  alfa1 = "T" + (alfa1 % 107);
  |NOME_DAT #24 alfa1;

  |DDEFECTO #24;
  |ABRE #24;
  #24#00 = empre;       || empresa
  #24#25 = ejerc;       || periodo
  |LEE 101#24=;
  FEstado = FSalida;
  #24#01 = #24#02;  #24#02 = #24#03;  #24#03 = #0#0;      || diario
  #24#04 = #24#05;  #24#05 = #24#06;  #24#06 = #0#1;      || fecha
  #24#07 = #24#08;  #24#08 = #24#09;  #24#09 = #0#2;      || documento
  #24#10 = #24#11;  #24#11 = #24#12;  #24#12 = #0#4;      || cuenta
  #24#13 = #24#14;  #24#14 = #24#15;  #24#15 = #0#5;      || digito
  #24#16 = #24#17;  #24#17 = #24#18;  #24#18 = #0#8;      || signo
  #24#19 = #24#20;  #24#20 = #24#21;  #24#21 = #0#9;      || importe
  #24#22 = #24#23;  #24#23 = #24#24;  #24#24 = #0#3;      || apunte
  |SI FEstado = 0;  |GRABA 020#24a;  |FINSI;
  |SI FEstado ! 0;  |GRABA 020#24n;  |FINSI;
  |LIBERA #24;
  |CIERRA #24;
|FPRC;

****************************************************************************
                 CALCULO PARA ASIENTOS PREDEFINIDOS
****************************************************************************

|PROCESO c_empieza;      || desde 'c_apunte'
 |DDEFECTO #13;      || def de trabajo para acumuladores y lineas
 |DDEFECTO #14;      || def de trabajo para contrapartidas
 |DDEFECTO #15;      || iva repercutido
 |DDEFECTO #16;      || iva soportado
    #12#1 = 0;      || lineas predefinido
|FPRC;

|PROCESO lee_predef;
  p_eof = 0;
  |ABRE #12;
  #12#0 = #1#7;
  |LEE 001#12>;
  |SI FSalida ! 0; |O #12#0 ! #1#7;  p_eof = 111;  |FINSI;
  |CIERRA #12;
  |SI p_eof = 111;  |ACABA;  |FINSI;
  |SI #12#56 ! "I";  |Y #12#56 ! "i";  |Y #12#56 ! "R";  |Y #12#56 ! "r";
      |ACABA;
  |FINSI;
  |SI #12#60 = 0;
      |MENAV "     Atencion No esta informado el Tipo de IVA en predefinido";
  |FINSI;
  |SI #12#56 = "I";  |O #12#56 = "i";
      |SI #12#57 = 1;  iva1 = #12#60;  |FINSI;
      |SI #12#57 = 2;  iva2 = #12#60;  |FINSI;
      |SI #12#57 = 3;  iva3 = #12#60;  |FINSI;
      |SI #12#57 = 4;  iva4 = #12#60;  |FINSI;
      |SI #12#57 = 5;  iva5 = #12#60;  |FINSI;
  |FINSI;
  |SI #12#56 = "R";  |O #12#56 = "r";
      |SI #12#57 = 1;  req1 = #12#60;  |FINSI;
      |SI #12#57 = 2;  req2 = #12#60;  |FINSI;
      |SI #12#57 = 3;  req3 = #12#60;  |FINSI;
      |SI #12#57 = 4;  req4 = #12#60;  |FINSI;
      |SI #12#57 = 5;  req5 = #12#60;  |FINSI;
  |FINSI;
|FPRC;

|PROCESO c_apunte; |TIPO 7;             || desde campo 'linea'

|HAZ diez;
|SI predefine = 0;  |ACABA;  |FINSI;

|SI #0#3 = 1;            || se supone que la primera linea del apunte es la 1
    |HAZ c_empieza;
|FINSI;
|ET arriba;
|CAMPO_MODIFICA #0#16, 1, "S";
|CAMPO_MODIFICA #0#10, 1, "S";
|HAZ lee_predef;
|SI p_eof ! 0;           || cuando se acaba el predefinido
    |SI #11#2 = "S";          || si hay iva
        |HAZ graba_iva;
        |SI #11#3 = "R";|Y #11#6 = "S";  |HAZ cobros;  |FINSI;
        |SI #11#3 = "S";|Y #11#6 = "S";  |HAZ pagos;   |FINSI;
        |SI #11#3 = "S";|Y #11#12  = "S";  |HAZ el_amo;  |FINSI;
        |SI #11#3 = "R";|Y #11#12  = "S";  |HAZ el_amov;  |FINSI;
    |FINSI;
    |HAZ todaslacon;

        predefine = 0;        || pide otro predefinido
        sw_ptec = 1; || por el 'PTEC' del 'pide_iva' de 'camaasic'(sobra)

|| ... ver que hacer aqu¡
        |SI #11#15 = 1; sw_ptec = 0; |ACABA; |FINSI;
        |SI #11#15 = 2;
            sw_ptec = 2;
            aferlamar = 1;
            |PTEC 806;
            |ACABA;
        |FINSI;
        |SI #11#15 = 3; sw_ptec = 0; inkey =0; |PTEC 806; |ACABA; |FINSI;
|| --------------------------------------------------------
    |ENCAMPO #7#1;
    |SI FSalida = 0; |Y #1#7 ! 0; |Y inkey ! 1;
            predefine = 1;
        |HAZ c_empieza;   || inicia el proceso de otro asiento predef.
        |VETE arriba;
    |SINO;
        |SI FSalida = 10;      || <F2>
                inkey = 0;     || (para no molestar en el tipo 11)
            |PTEC 806;         || pulsa ESCAPE para volver al diario
        |FINSI;
    |FINSI;
|SINO;
    |SI #12#2 = "N";          || en el tipo N solo pide importe
            #0#12 = #11#3;    || tipo (soportado/repercutido)
            #0#13 = #11#4;    || libro
            #0#14 = i_factu;  || factura/orden
            #0#15 = i_serie;  || serie
            #0#19 = #12#56;   || tipo acumulador IVA
            |SI #12#1 = #11#5; #0#19 = "F"; |FINSI;
            |SI #11#9 = 1;
                |SI #12#56 = "B"; #0#19 = "b"; |FINSI;
                |SI #12#56 = "I"; #0#19 = "i"; |FINSI;
                |SI #12#56 = "R"; #0#19 = "r"; |FINSI;
                |SI #12#56 = "b"; #0#19 = "B"; |FINSI;
                |SI #12#56 = "i"; #0#19 = "I"; |FINSI;
                |SI #12#56 = "r"; #0#19 = "R"; |FINSI;
            |FINSI;
            #0#20 = #12#57;   || nro. acumulador IVA
        |ENCAMPO #9#0;
        |VETE arriba;
    |SINO;
        |PTEC 802;
    |FINSI;
|FINSI;
|FPRC;

|PROCESO c_cuenta; |TIPO 7;
  xx = (FEntrada / 100) ! 0;
  |CAMPO_MODIFICA #0#10, 1, "S";
  |CAMPO_MODIFICA #0#16, 1, "S";
  |SI xx ! 1;              || no modificable cuenta en blanco en no alta
      |SI #0#10 ! doce; |O #0#16 ! doce;
          |SI Campo=10; |Y #0#16!doce; |CAMPO_MODIFICA #0#10,1,"N"; |FINSI;
          |SI Campo=16; |Y #0#10!doce; |CAMPO_MODIFICA #0#16,1,"N"; |FINSI;
      |FINSI;
  |FINSI;
  |SI predefine = 0;  |ACABA;  |FINSI;

  |SI #12#2="D"; |Y Campo=10; |CAMPO_MODIFICA #0#10,1,"N"; |ACABA;  |FINSI;
  |SI #12#2="H"; |Y Campo=16; |CAMPO_MODIFICA #0#16,1,"N"; |ACABA;  |FINSI;

  |HAZ c_contra;
  p_cuenta = #12#3;
  |QBF p_cuenta;
  |SI p_cuenta = "=";
      |SI #12#2 = "D";
          #0#16 = ctpre;              || cuenta DEBE (incompleta)
          #0#17 = dipre;              || digito DEBE
          #0#10 = "";                 || cuenta HABER
          #0#11 = 0;                  || digito HABER
      |SINO;
          #0#16 = "";                 || cuenta DEBE
          #0#17 = 0;                  || digito DEBE
          #0#10 = ctpre;              || cuenta HABER (incompleta)
          #0#11 = dipre;              || digito HABER
      |FINSI;
      |PINTA #0#16;
      |PINTA #0#10;
      |PTEC 802;           || pulsa un 'Intro'
      |ACABA;
  |FINSI;
  alfa3 = p_cuenta % 0;
  nume1 = alfa3;
  nume1 = nume1 + 99;
  alfa2 = p_cuenta % -101;       || coge el asterisco
  |SI alfa2 = "*";
      |SI #12#2 = "D";
          #0#16 = p_cuenta % nume1;   || cuenta DEBE (incompleta)
          #0#17 = #12#4;              || digito DEBE
          #0#10 = "";                 || cuenta HABER
          #0#11 = 0;                  || digito HABER
      |SINO;
          #0#16 = "";                 || cuenta DEBE
          #0#17 = 0;                  || digito DEBE
          #0#10 = p_cuenta % nume1;   || cuenta HABER (incompleta)
          #0#11 = #12#4;              || digito HABER
      |FINSI;
      |PINTA #0#16;
      |PINTA #0#10;
      |PTEC 816;            || pulsa un 'Fin'
  |SINO;
      |SI #12#2 = "D";
          #0#16 = #12#3;              || cuenta DEBE (completa)
          #0#17 = #12#4;              || digito DEBE
          #0#10 = "";                 || cuenta HABER
          #0#11 = 0;                  || digito HABER
      |SINO;
          #0#16 = "";                 || cuenta DEBE
          #0#17 = 0;                  || digito DEBE
          #0#10 = #12#3;              || cuenta HABER (completa)
          #0#11 = #12#4;              || digito HABER
      |FINSI;
      |PINTA #0#16;
      |PINTA #0#10;
      |PTEC 802;           || pulsa un 'Intro'
  |FINSI;
|FPRC;

|PROCESO c_concepto; |TIPO 7;
  |SI predefine = 0;  |ACABA;  |FINSI;

  #0#6 = #12#5;
  |PINTA #0#6;
  alfa1 = #12#6;
  |QBF alfa1;
  |SI alfa1 = "=";
      |PTEC 802;
  |FINSI;
|FPRC;

|PROCESO c_descrip; |TIPO 7;
 |SI predefine = 0;  |PTEC 816;  |ACABA;  |FINSI;

 alfa1 = #12#6;
 |QBF alfa1;
 |SI alfa1 = "=";
     #0#7 = deant;
     |PINTA #0#7;
     |PTEC 802;
 |SINO;
     |SI alfa1 ! "*";
         alfa2 = #3#1;
         |QBF alfa2;
         #0#7 = alfa2 + " " + #12#6;
         |PINTA #0#7;
         |PTEC 802;
     |SINO;
         |PTEC 816;
     |FINSI;
 |FINSI;
|FPRC;

|PROCESO c_departa; |TIPO 7;
|SI #30#24 = "N";
    |CAMPO_MODIFICA #0#21, 1, "N";
|SINO;
    |CAMPO_MODIFICA #0#21, 1, "S";
    |SI predefine = 0;  |ACABA;  |FINSI;
        #0#21 = #12#58;
    |PINTA #0#21;
    |PTEC 802;
|FINSI;
|FPRC;

|PROCESO c_importe; |TIPO 7;
|HAZ signo;
    xx = (FEntrada / 100) ! 0;
|SI xx = 1;
        #0#9 = imant;
    |PINTA #0#9;
|FINSI;
|SI predefine = 0;  |ACABA;  |FINSI;

|SI #12#7 = "M";
        avisos = #12#59;      || comentario importe (cuando es manual)
        alfa1 = avisos;
    |QBF alfa1;
    |SI alfa1 ! "";  |HAZ aviso5;  |FINSI;
|FINSI;

    calculo = 0;
|PARA nume1 = 24; |SI nume1 [ 31; |HACIENDO nume1 = nume1 + 1;
        nume2 = nume1 + 8;     || acum. o linea
        nume3 = nume1 + 16;    || tipo de calculo
        nume4 = nume1 + 24;    || valor
    |SI #12nume1 = "L";
            nume5 = #12nume2;
        |SI #12nume3 = "+";  calculo = calculo + #13nume5;  |FINSI;
        |SI #12nume3 = "-";  calculo = calculo - #13nume5;  |FINSI;
        |SI #12nume3 = "*";  calculo = calculo * #13nume5;  |FINSI;
        |SI #12nume3 = "/";  calculo = calculo / #13nume5;  |FINSI;
        |SI #12nume3 = "%";  calculo = calculo % #13nume5;  |FINSI;
    |FINSI;
    |SI #12nume1 = "A";
            nume5 = #12nume2 + 100;
        |SI #12nume3 = "+";  calculo = calculo + #13nume5;  |FINSI;
        |SI #12nume3 = "-";  calculo = calculo - #13nume5;  |FINSI;
        |SI #12nume3 = "*";  calculo = calculo * #13nume5;  |FINSI;
        |SI #12nume3 = "/";  calculo = calculo / #13nume5;  |FINSI;
        |SI #12nume3 = "%";  calculo = calculo % #13nume5;  |FINSI;
    |FINSI;
    |SI #12nume1 = "V";
        |SI #12nume3 = "+";  calculo = calculo + #12nume4;  |FINSI;
        |SI #12nume3 = "-";  calculo = calculo - #12nume4;  |FINSI;
        |SI #12nume3 = "*";  calculo = calculo * #12nume4;  |FINSI;
        |SI #12nume3 = "/";  calculo = calculo / #12nume4;  |FINSI;
        |SI #12nume3 = "%";  calculo = calculo % #12nume4;  |FINSI;
    |FINSI;
|SIGUE;

    calculo = calculo ! EURODB;
    #0#9 = calculo;
|PINTA #0#9;

|SI #12#7 = "A";
    |PTEC 802;
|FINSI;
|FPRC;

----------------------- OPERACIONES DE PREDEFINIDOS

|PROCESO c_predefi;                || desde 'nocero'
alfa1 = #0#16;   |QBF alfa1;
alfa2 = #0#10;   |QBF alfa2;
|SI alfa1 = "";|Y alfa2 = "";    ||al menos una cuenta cargada;
    |ACABA;
|FINSI;
|SI predefine = 0;  |ACABA;  |FINSI;
|SI #11#2 = "S";         || cuando hay IVA
    |HAZ c_cuenta2;
    |HAZ c_impiva;
|FINSI;
|HAZ c_importe2;
|HAZ c_lacontra2;
|FPRC;

|PROCESO c_cuenta2;                || desde 'c_predefi'
|SI #12#1 = #11#5;       || cuenta cliente/proveedor
        i_cuent = #0#4;
        i_digit = #0#5;
        i_conce = #0#6;
        i_descr = #0#7;
        i_depar = #0#21;
    |HAZ c_nombre;
|FINSI;
|FPRC;

|PROCESO c_impiva;                 || desde 'c_predefi','a_busca'
    i_acum = #0#19;
    i_impo = #0#9;
|SI i_acum = "b"; i_acum = "B"; i_impo = i_impo * -1; |FINSI;  || aminorar
|SI i_acum = "i"; i_acum = "I"; i_impo = i_impo * -1; |FINSI;
|SI i_acum = "r"; i_acum = "R"; i_impo = i_impo * -1; |FINSI;
|SI i_acum = "p"; i_acum = "P"; i_impo = i_impo * -1; |FINSI;

|SI i_acum = "B"; ctab1 = #0#4; ctab2 = #0#5; |FINSI;
|SI i_acum = "B";|O i_acum = "I";|O i_acum = "R";|O i_acum = "P";
    |SI i_acum = "B";  puntero =  7;  |FINSI;
    |SI i_acum = "I";  puntero = 13;  |FINSI;
    |SI i_acum = "R";  puntero = 19;  |FINSI;
    |SI i_acum = "P";  puntero = 19;  |FINSI;
    |SI #0#20 ] 1;|Y #0#20 [ 5;
        |SI #0#20 > 3;|Y i_acum = "B";  puntero = puntero + 41;  |FINSI;
        |SI #0#20 > 3;|Y i_acum = "I";  puntero = puntero + 39;  |FINSI;
        |SI #0#20 > 3;|Y i_acum = "R";  puntero = puntero + 37;  |FINSI;
            puntero = puntero + #0#20;
        |SI i_acum = "P";     puntero = 63;  |FINSI;   || irpf
        |SI #0#12 = "R";  #15puntero = #15puntero +. i_impo;  |FINSI;
        |SI #0#12 = "S";  #16puntero = #16puntero +. i_impo;  |FINSI;
    |SINO;
        |SI i_acum = "I";         || solo en el iva
            |HAZ c_impiva2;
        |SINO;
            |MENAV "    ATENCION!!! Apunte mal relacionado con IVA ...";
        |FINSI;
    |FINSI;
|FINSI;
|FPRC;

|PROCESO c_impiva2;      || (des)actualizacion del iva manual (nro. 6), no puede ser 6.
    c_c = i_impo;
    detector = 0 +. 1;
|SI detector = -1;
    |PARA nume1 = 14; |SI nume1 [ 61; |HACIENDO nume1 = nume1 + 1;
        |SI nume1 = 17;  nume1 = 20;  |FINSI;
        |SI nume1 = 23;  nume1 = 56;  |FINSI;
        |SI nume1 = 58;  nume1 = 60;  |FINSI;
        |SI #0#12 = "R";
            |SI #15nume1 [ c_c;
                    c_c = c_c - #15nume1;     #15nume1 = 0;
            |SINO;
                    #15nume1 = #15nume1 - c_c;     c_c = 0;
            |FINSI;
        |FINSI;
        |SI #0#12 = "S";
            |SI #16nume1 [ c_c;
                    c_c = c_c - #16nume1;     #16nume1 = 0;
            |SINO;
                    #16nume1 = #16nume1 - c_c;     c_c = 0;
            |FINSI;
        |FINSI;
    |SIGUE;
|SINO;
    |SI #0#12 = "R";  #15#14 = #15#14 + c_c;  |FINSI;  || lo suma en el
    |SI #0#12 = "S";  #16#14 = #16#14 + c_c;  |FINSI;  ||/primer acumulador
|FINSI;
|FPRC;

|PROCESO c_importe2;               || desde 'c_predefi'
    p_linea = #12#1;
    #13p_linea = #0#9;
|PARA nume1 = 8; |SI nume1 [ 15; |HACIENDO nume1 = nume1 + 1;
        nume2 = nume1 + 8;
    |SI #12nume1 ! 0;|Y #12nume2 ! " ";
            nume3 = #12nume1 + 100;
        |SI #12nume2 = "+";  #13nume3 = #13nume3 + #0#9;  |FINSI;
        |SI #12nume2 = "-";  #13nume3 = #13nume3 - #0#9;  |FINSI;
    |FINSI;
|SIGUE;
|FPRC;

|PROCESO c_lacontra2;  ||desde 'c_predefi'
    |SI #12#2 = "N"; |ACABA; |FINSI;
    p_linea = #12#1;
    alfa1 = #0#4;
    alfa2 = #0#5; |QBT alfa2;
    alfa2 = ("0" + alfa2) % -101;
    #14p_linea = alfa1 + alfa2;
    p_linea = p_linea + 100;
    alfa1 = #12#15; |QBT alfa1;
    alfa1 = ("00" + alfa1) % -102;
    alfa2 = #0#3; |QBT alfa2;
    alfa2 = ("00000" + alfa2) % -105;
    #14p_linea = alfa1 + alfa2;
|FPRC;

|PROCESO todaslacon;
 |ABRE #31;
 |PARA nume1 = 1; |SI nume1 [ 99; |HACIENDO nume1 = nume1 + 1;
       nume2 = nume1 + 100;

       alfa1 = #14nume2 % 102;    ||Linea Contrapartida
       alfa2 = #14nume2 % -104;   ||Linea Apunte

       nume3 = alfa1;               || Linea de la Contrapartida
       nume4 = alfa2;
       |SI nume3 > 00;
           alfa1 = #14nume3 % 112;  ||Cuenta Contrapartida
           alfa2 = #14nume3 % -101; ||Digito Contrapartida

           #31#0 = #0#0;
           #31#1 = #0#1;
           #31#2 = #0#2;
           #31#3 = nume4;
           |LEE 101#31=;
           |SI FSalida ! 0;
               #31#0 = #0#0;
               #31#1 = #0#1;
               #31#2 = #0#2;
               #31#3 = nume4;
               |GRABA 020#31b;
           |FINSI;
           #31#4 = alfa1;
           #31#5 = alfa2;
           |GRABA 020#31a;
           |LIBERA #31;
       |FINSI;
 |SIGUE;
 |CIERRA #31;
|FPRC;

----------------------- GRABA IVAS (SOPORTADO/REPERCUTIDO)

|PROCESO graba_iva;
    i_signo = 1;
|| ... |SI #11#9 = 1;   i_signo = -1;   |FINSI; || cambiados ya los param.
                                                || de IVA B->b I->i ...
|SI #11#3 = "R";  |HAZ graba_ivar;  |FINSI;
|SI #11#3 = "S";  |HAZ graba_ivas;  |FINSI;
|FPRC;

|PROCESO graba_ivar;          || repercutido
|ABRE #15;
    #15#00 = #11#4;      || libro
|HAZ lee_libro;
    #15#01 = #17#1;      || descripcion libro
    #15#02 = i_factu;    || factura
    #15#03 = i_fecha;    || fecha
    #15#04 = i_cuent;    || cuenta
    #15#05 = i_digit;    || digito
    #15#06 = i_conce;    || concepto
    #15#07 = i_descr;    || descripcion
    #15#08 = #15#08 * i_signo;                 || base 1
    #15#09 = #15#09 * i_signo;                 || base 2
    #15#10 = #15#10 * i_signo;                 || base 3
    #15#52 = #15#52 * i_signo;                 || base 4
    #15#53 = #15#53 * i_signo;                 || base 5
    #15#14 = #15#14 * i_signo;                 || cuota iva 1
    #15#15 = #15#15 * i_signo;                 || cuota iva 2
    #15#16 = #15#16 * i_signo;                 || cuota iva 3
    #15#56 = #15#56 * i_signo;                 || cuota iva 4
    #15#57 = #15#57 * i_signo;                 || cuota iva 5
    #15#20 = #15#20 * i_signo;                 || cuota rec 1
    #15#21 = #15#21 * i_signo;                 || cuota rec 2
    #15#22 = #15#22 * i_signo;                 || cuota rec 3
    #15#60 = #15#60 * i_signo;                 || cuota rec 4
    #15#61 = #15#61 * i_signo;                 || cuota rec 5
    |SI #15#14 ! 0;  |Y iva1 = 0;              || (%) iva 1
        #15#11 = ((#15#14 * 100) / #15#08) ! 0;
    |SINO;
        #15#11 = iva1;
    |FINSI;
    |SI #15#15 ! 0;  |Y iva2 = 0;              || (%) iva2
        #15#12 = ((#15#15 * 100) / #15#09) ! 0;
    |SINO;
        #15#12 = iva2;
    |FINSI;
    |SI #15#16 ! 0;  |Y iva3 = 0;              || (%) iva 3
        #15#13 = ((#15#16 * 100) / #15#10) ! 0;
    |SINO;
        #15#13 = iva3;
    |FINSI;
    |SI #15#56 ! 0;  |Y iva4 = 0;              || (%) iva 4
        #15#54 = ((#15#56 * 100) / #15#52) ! 0;
    |SINO;
        #15#54 = iva4;
    |FINSI;
    |SI #15#57 ! 0;  |Y iva5 = 0;              || (%) iva 5
        #15#55 = ((#15#57 * 100) / #15#53) ! 0;
    |SINO;
        #15#55 = iva5;
    |FINSI;
    |SI #15#20 ! 0;  |Y req1 = 0;              || (%) rec 1
        #15#17 = ((#15#20 * 100) / #15#08) ! 1;
    |SINO;
        #15#17 = req1;
    |FINSI;
    |SI #15#21 ! 0;  |Y req2 = 0;              || (%) rec 2
        #15#18 = ((#15#21 * 100) / #15#09) ! 1;
    |SINO;
        #15#18 = req2;
    |FINSI;
    |SI #15#22 ! 0;  |Y req3 = 0;              || (%) rec 3
        #15#19 = ((#15#22 * 100) / #15#10) ! 1;
    |SINO;
        #15#19 = req3;
    |FINSI;
    |SI #15#60 ! 0;  |Y req4 = 0;             || (%) rec 4
        #15#58 = ((#15#60 * 100) / #15#52) ! 1;
    |SINO;
        #15#58 = req4;
    |FINSI;
    |SI #15#61 ! 0;  |Y req5 = 0;             || (%) rec 5
        #15#59 = ((#15#61 * 100) / #15#53) ! 1;
    |SINO;
        #15#59 = req5;
    |FINSI;
    #15#23 = #11#11;     || deducible
|HAZ total_ivar;         || total factura
    #15#27 = i_serie;    || serie
    #15#28 = "N";        || liquidada
|HAZ periodo;
    #15#44 = i_perio;    || periodo
    #15#45 = #1#0;       || diario
    #15#46 = #1#1;       || fecha asiento
    #15#47 = #1#2;       || documento
    #15#48 = #11#12;     || inversiones
    #15#49 = i_depar;    || departamento
    #15#50 = i_nom;      || nombre
    #15#51 = i_cif;      || cif
|GRABA 020#15n;
|LIBERA #15;
|CIERRA #15;
  iva1 = 0;  iva2 = 0;  iva3 = 0;  iva4 = 0;  iva5 = 0;
  req1 = 0;  req2 = 0;  req3 = 0;  req4 = 0;  req5 = 0;
|FPRC;

|PROCESO cobros;
|ABRE #15;
    #15#00 = #11#4;      || libro
    #15#02 = i_factu;    || factura
    #15#27 = i_serie;    || serie
|LEE 021#15=;
|SI FSalida = 0;
        libro = #15#0;
        orden = #15#2;
        emision = #15#3;
        serie = #15#27;            || inhibida
        pesetas = #15#26;
        cuenta = #15#4;
        digito = #15#5;
    |SUB_EJECUTA "calincob.run";
|FINSI;
|SELECT #1#15;
|CIERRA #15;
|FPRC;

|PROCESO graba_ivas;
|ABRE #16;
    #16#00 = #11#4;      || libro
|HAZ lee_libro;
    #16#01 = #17#1;      || descripcion libro
    #16#02 = i_factu;    || factura
    #16#03 = i_fecha;    || fecha
    #16#04 = i_cuent;    || cuenta
    #16#05 = i_digit;    || digito
    #16#06 = i_conce;    || concepto
    #16#07 = i_descr;    || descripcion
    #16#08 = #16#08 * i_signo;                 || base 1
    #16#09 = #16#09 * i_signo;                 || base 2
    #16#10 = #16#10 * i_signo;                 || base 3
    #16#52 = #16#52 * i_signo;                 || base 4
    #16#53 = #16#53 * i_signo;                 || base 5
    #16#14 = #16#14 * i_signo;                 || cuota iva 1
    #16#15 = #16#15 * i_signo;                 || cuota iva 2
    #16#16 = #16#16 * i_signo;                 || cuota iva 3
    #16#56 = #16#56 * i_signo;                 || cuota iva 4
    #16#57 = #16#57 * i_signo;                 || cuota iva 5
    #16#20 = #16#20 * i_signo;                 || cuota rec 1
    #16#21 = #16#21 * i_signo;                 || cuota rec 2
    #16#22 = #16#22 * i_signo;                 || cuota rec 3
    #16#60 = #16#60 * i_signo;                 || cuota rec 4
    #16#61 = #16#61 * i_signo;                 || cuota rec 5
    |SI #16#14 ! 0;  |Y iva1 = 0;              || (%) iva 1
        #16#11 = ((#16#14 * 100) / #16#08) ! 0;
    |SINO;
        #16#11 = iva1;
    |FINSI;
    |SI #16#15 ! 0;  |Y iva2 = 0;              || (%) iva2
        #16#12 = ((#16#15 * 100) / #16#09) ! 0;
    |SINO;
        #16#12 = iva2;
    |FINSI;
    |SI #16#16 ! 0;  |Y iva3 = 0;              || (%) iva 3
        #16#13 = ((#16#16 * 100) / #16#10) ! 0;
    |SINO;
        #16#13 = iva3;
    |FINSI;
    |SI #16#56 ! 0;  |Y iva4 = 0;              || (%) iva 4
        #16#54 = ((#16#56 * 100) / #16#52) ! 0;
    |SINO;
        #16#54 = iva4;
    |FINSI;
    |SI #16#57 ! 0;  |Y iva5 = 0;              || (%) iva 5
        #16#55 = ((#16#57 * 100) / #16#53) ! 0;
    |SINO;
        #16#55 = iva5;
    |FINSI;
    |SI #16#20 ! 0;  |Y req1 = 0;              || (%) rec 1
        #16#17 = ((#16#20 * 100) / #16#08) ! 1;
    |SINO;
        #16#17 = req1;
    |FINSI;
    |SI #16#21 ! 0;  |Y req2 = 0;              || (%) rec 2
        #16#18 = ((#16#21 * 100) / #16#09) ! 1;
    |SINO;
        #16#18 = req2;
    |FINSI;
    |SI #16#22 ! 0;  |Y req3 = 0;              || (%) rec 3
        #16#19 = ((#16#22 * 100) / #16#10) ! 1;
    |SINO;
        #16#19 = req3;
    |FINSI;
    |SI #16#60 ! 0;  |Y req4 = 0;             || (%) rec 4
        #16#58 = ((#16#60 * 100) / #16#52) ! 1;
    |SINO;
        #16#58 = req4;
    |FINSI;
    |SI #16#61 ! 0;  |Y req5 = 0;             || (%) rec 5
        #16#59 = ((#16#61 * 100) / #16#53) ! 1;
    |SINO;
        #16#59 = req5;
    |FINSI;
    #16#23 = #11#11;     || deducible
|HAZ total_ivas;         || total factura
    #16#27 = i_serie;    || serie
    #16#28 = "N";        || liquidada
|HAZ periodo;
    #16#44 = i_perio;    || periodo
    #16#45 = #1#0;       || diario
    #16#46 = #1#1;       || fecha asiento
    #16#47 = #1#2;       || documento
    #16#48 = #11#12;     || inversiones
    #16#49 = i_depar;    || departamento
    #16#50 = i_nom;      || nombre
    #16#51 = i_cif;      || cif
|GRABA 020#16n;
|LIBERA #16;
|CIERRA #16;
  iva1 = 0;  iva2 = 0;  iva3 = 0;  iva4 = 0;  iva5 = 0;
  req1 = 0;  req2 = 0;  req3 = 0;  req4 = 0;  req5 = 0;
|FPRC;

|PROCESO pagos;
|ABRE #16;
    #16#00 = #11#4;      || libro
    #16#02 = i_factu;    || factura
    #16#27 = i_serie;    || serie
|LEE 021#16=;
|SI FSalida = 0;
        libro = #16#0;
        orden = #16#2;
        serie = #16#27;
        emision = #16#3;
        pesetas = #16#26;
        cuenta = #16#4;
        digito = #16#5;
    |SUB_EJECUTA "calinpag.run";
|FINSI;
|SELECT #1#16;
|CIERRA #16;
|FPRC;

|PROCESO lee_libro;
 |ABRE #17;
 #17#0 = #11#4;
 |LEE 001#17=;
 |SI FSalida ! 0;
     #17#1 = "<LIBRO INEXISTENTE>";
 |FINSI;
 |CIERRA #17;
|FPRC;

|PROCESO periodo;
    alfa1 = i_fecha;
|SI #11#10 = "T";
        alfa1 = alfa1 % -104;   || saca a¤o
        alfa2 = "31.03." + alfa1;  trim1 = alfa2;  || primer trimestre
        alfa2 = "30.06." + alfa1;  trim2 = alfa2;  || segundo trimestre
        alfa2 = "30.09." + alfa1;  trim3 = alfa2;  || tercer trimestre
    |SI i_fecha [ trim1;                     i_perio = 1;  |FINSI;
    |SI i_fecha > trim1;|Y i_fecha [ trim2;  i_perio = 2;  |FINSI;
    |SI i_fecha > trim2;|Y i_fecha [ trim3;  i_perio = 3;  |FINSI;
    |SI i_fecha > trim3;                     i_perio = 4;  |FINSI;
|SINO;
        alfa1 = alfa1 % 402;    || saca mes
        i_perio = alfa1;
|FINSI;
|FPRC;

|PROCESO total_ivar;
    nume1 = #15#08 +#15#09 +#15#10 +#15#52 +#15#53;    || bases imponibles
    nume2 = #15#14 +#15#15 +#15#16 +#15#56 +#15#57;    || cuotas iva
    nume3 = #15#20 +#15#21 +#15#22 +#15#60 +#15#61;    || total cuotas rec.
    #15#26 = nume1 + nume2 + nume3;                    || TOTAL FACTURA
|FPRC;

|PROCESO total_ivas;
    nume1 = #16#08 +#16#09 +#16#10 +#16#52 +#16#53;    || bases imponibles
    nume2 = #16#14 +#16#15 +#16#16 +#16#56 +#16#57;    || cuotas iva
    nume3 = #16#20 +#16#21 +#16#22 +#16#60 +#16#61;    || cuotas rec.
    #16#26 = nume1 + nume2 + nume3 - #16#63;           || TOTAL FACTURA
|FPRC;

|PROCESO lee_clien;
  |ABRE #18;
  #18#23 = "C";
  #18#10 = i_cuent;
  #18#11 = i_digit;
  |LEE 001#18=;
  |SI FSalida ! 0;
      |DDEFECTO #18;
      |HAZ lee_cuent;
      i_nom = #4#2;         || nombre de la cuenta
  |SINO;
      i_nom = #18#1;        || nombre del cliente
  |FINSI;
  |CIERRA #18;
|FPRC;

|PROCESO lee_prove;
  |ABRE #18;
  #18#23 = "P";
  #18#10 = i_cuent;
  #18#11 = i_digit;
  |LEE 001#18=;
  |SI FSalida ! 0;
      |DDEFECTO #18;
      |HAZ lee_cuent;
      i_nom = #4#2;         || nombre de la cuenta
  |SINO;
      i_nom = #18#1;        || nombre del proveedor
  |FINSI;
  |CIERRA #18;
|FPRC;

|PROCESO lee_cuent;
 |ABRET #4;
 #4#0 = i_cuent;
 #4#1 = i_digit;
 |LEE 001#4=;
 |SI FSalida ! 0;
     |DDEFECTO #4;
 |FINSI;
 |CIERRAT #4;
|FPRC;

|PROCESO c_nombre;
|DDEFECTO #26;
|ABRE #26;
|LEE 000#26p;
|CIERRA #26;

|SI #11#3 = "R"; |HAZ lee_clien; i_cif=#18#9; i_cc1=#18#21; i_dc1=#18#22; |FINSI;
|SI #11#3 = "S"; |HAZ lee_prove; i_cif=#18#9; i_cc2=#18#21; i_dc2=#18#22; |FINSI;

|SI #26#35 = "S";|O #26#36 = "S";
    |PUSHV 05012380;
    |PINPA #0#25;
        #25#0 = i_nom;
        #25#1 = i_cif;
    |PINDA #0#25;
        alfa1 = #26#35;       || modificable nombre
        alfa2 = #26#36;       || modificable cif
    |CAMPO_MODIFICA #25#0, 1, alfa1;
    |CAMPO_MODIFICA #25#1, 1, alfa2;
    |ENDATOS #1#25;
    |SI FSalida = 0;
            i_nom = #25#0;
            i_cif = #25#1;
    |FINSI;
    |POPV;
|FINSI;
|FPRC;

|PROCESO c_contra;
  alfa1 = #12#3;
  |QBF alfa1;
  alfa2 = alfa1 %  103;
  alfa3 = alfa1 % -101;
  |SI alfa2 ! "CON";  |ACABA;  |FINSI;
  |SI #11#2 = "S";
      |SI #11#3 = "R";  #12#3 = i_cc1;  #12#4 = i_dc1;  |FINSI;
      |SI #11#3 = "S";  #12#3 = i_cc2;  #12#4 = i_dc2;  |FINSI;
      |SI #12#3 = doce;
          alfa3 = "*";       || por si la contrapartida viene vacia
      |FINSI;
      |SI alfa3 = "*";
          alfa4 = #12#3;
          |QBF alfa4;
          #12#3 = alfa4 + alfa3;
      |FINSI;
  |SINO;
      #12#3 = "*";
      #12#4 = 0;
  |FINSI;
|FPRC;

|PROCESO el_amo; || facturas de inversion COMPRA
  |AVISO;
  |CONFI "2400SFra. de Inversion. Desea crear el Bien Amortizable S/N : ";
  |SI FSalida ! 0;
      |ACABA;
  |FINSI;
  fec_am = #16#3;
  imp_am = #16#8 + #16#9 + #16#10 + #16#52 + #16#53;
    c_am = #16#7;
  ct1_am = "";
  ct2_am = 0;
  prv_am = #16#50;
                            || primer = #16#4 % 101;
                            || |SI primer = "4";
                            ||     prv_am = #16#50;
                            || |FINSI;
  primer = ctab1 % 101;
  |SI primer = "2";
      ct1_am = ctab1;
      ct2_am = ctab2;
  |FINSI;
  |SUB_EJECUTA "camocab1.tab";
|FPRC;

|PROCESO el_amov; || facturas de inversion VENTA
  |AVISO;
  |CONFI "2400SFra. de Inversion. Desea reflejar la venta del Bien Amortizable S/N : ";
  |SI FSalida ! 0;
      |ACABA;
  |FINSI;
  fec_am = #15#3;
  imp_am = #15#8 + #15#9 + #15#10 + #15#52 + #15#53;
   c_am = #15#7;
  |SUB_EJECUTA "camocab2.tab";
|FPRC;
