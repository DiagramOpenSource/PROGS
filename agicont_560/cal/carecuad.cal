|FICHEROS;
    carecuad #0;
    camaasic #1;
    camaasil #2;
    catrasi1 #3;
    catrasi2 #4;
    caconasi #5;
    caivarep #15;
    caivasop #16;
    calicont #31; || Contrapartidas
|FIN;

|VARIABLES;
    alfa1 = "";
    alfa2 = "";
    alfa3 = "";
    feini = @;
    fefin = @;

    Estado5 = 0;
    reg_leidos = 0;
    reg_grabados = 0;
    a_doc = 0;
    temporal1 = "";
    temporal2 = "";

    x = 0;
    c_doc = 0;
    sw    = 0;

    Linea = 0;
    sw_error = 0;
    diario = 0;
    fecha = @;
    docum = 0;
    debe = 0;
    haber = 0;
    &antdia = 0;
    &antfec = @;
    &antdoc = 0;
    &numero = 0;
    &error = "";

    dir_fich = "";
    traba1 = "";
    traba2 = "";
    traba3 = "";
    traba4 = "";

    VAlfa1 = "";
    VAlfa2 = "";
    VAlfa3 = "";
|FIN;

|INCLUYE tempo_i;

|PROCESO mayor; |TIPO 0;
  alfa1 = #0Campo > " ";
  #0Campo = alfa1;
  |PINTA #0Campo;
|FPRC;

|| ************************************************************************
|PROCESO ivas;
 |SI #3#0 = #2#0; |Y #3#1 = #2#1; |Y #3#2 = #2#2; |ACABA; |FINSI;
 #4#0 = #3#12;
 #4#1 = #3#13;
 #4#2 = #3#14;
 #4#3 = #3#15;
 |LEE 101#4=;
 |SI FSalida = 0; |ACABA; |FINSI;
 #4#0 = #3#12;
 #4#1 = #3#13;
 #4#2 = #3#14;
 #4#3 = #3#15;
 #4#4 = #2#0;
 #4#5 = #2#1;
 #4#6 = #2#2;
 #4#7 = #3#0;
 #4#8 = #3#1;
 #4#9 = #3#2;
 |GRABA 020#4n;
 |LIBERA #4;
|FPRC;

|PROCESO impresora;
  |SI sw_error = 0;
      |IMPRE 0;
      |SI FSalida ! 0;  |ACABA;  |FINSI;
      |INFOR "cadescua";
      |SI FSalida ! 0;  |FINIMP;  |ACABA;  |FINSI;
      sw_error = 1;
  |FINSI;
  antdia = diario; antfec = fecha; antdoc = docum;
  numero = numero + 1;
  |PRINT;
|FPRC;

|PROCESO haycambio;
  a_doc = a_doc + 1;
  #0#3 = a_doc;
  ||PINTA #0#3;
  |SI debe ! haber;
      error = "Descuadre.";
      |HAZ impresora;
  |FINSI;
  |HAZ nuevoAsi;
|FPRC;

|PROCESO grabaCab;
|DDEFECTO #1;
    #1#0 = diario;
    #1#1 = fecha;
    #1#2 = c_doc;
|LEE 101#1=;
|SI FSalida ! 0;
        #1#0 = diario;
        #1#1 = fecha;
        #1#2 = c_doc;
        #1#3 = c_doc;
    |GRABA 020#1n;
    |LIBERA #1;
    #0#4 = c_doc;
    |PINTA #0#1;
    |PINTA #0#2;
    |PINTA #0#3;
    |PINTA #0#4;
    |LEE 121#1=;
|FINSI;
|SI #2#8 = "D";
    #1#4 = #1#4 + #2#9;
    |SI #1#09 = 0; #1#08 = #2#4; #1#09 = #2#5; |FINSI;      || contrp.HABER
|SINO;
    #1#5 = #1#5 + #2#9;
    |SI #1#11 = 0; #1#10 = #2#4; #1#11 = #2#5; |FINSI;      || contrp.DEBE
|FINSI;
    #1#6 = #1#4 - #1#5;
|GRABA 020#1a;
|LIBERA #1;
|FPRC;

|PROCESO grabaLinea;
    Linea = Linea + 10;

|SI diario = 0; |O diario = 99; Linea = #2#3; |FINSI;

|DDEFECTO #3;
    #3#0 = diario;
    #3#1 = fecha;
    #3#2 = c_doc;
    #3#3 = Linea;
|LEE 101#3=;
|SI FSalida = 0; |ACABA; |FINSI;
    #3#0 = diario;
    #3#1 = fecha;
    #3#2 = c_doc;
    #3#3 = Linea;
|PARA x = 4; |SI x [ 23; |HACIENDO x = x + 1;
    #3x = #2x;
|SIGUE;
    |GRABA 020#3n;
    |SI FSalida = 0;
        reg_grabados = reg_grabados + 1;
        #0#2 = reg_grabados;
        ||PINTA #0#2;
    |FINSI;
    |LIBERA #3;
    |SI #3#12 = "R"; |O #3#12 = "S"; |HAZ ivas; |FINSI;
|FPRC;

|PROCESO grabaContra;
 #31#0 = #2#0;
 #31#1 = #2#1;
 #31#2 = #2#2;
 #31#3 = #2#3;
 |LEE 001#31=;
 |SI FSalida ! 0;
     |ACABA;
 |FINSI;
 |GRABAENDEF VAlfa2, VAlfa3, 0,#3#0, 1,#3#1, 2,#3#2, 3,#3#3, 4,#31#4, 5,#31#5;
|FPRC;

|PROCESO nuevoAsi;
  c_doc = c_doc + 1;
  Linea = -9;
  debe = 0;
  haber = 0;
  diario = #2#0;
  fecha  = #2#1;
  docum  = #2#2;
|FPRC;

|PROCESO actuali;
 reg_leidos = reg_leidos + 1;
 #0#1 = reg_leidos; ||PINTA #0#1;

 |SI sw = 0;
     |HAZ nuevoAsi;
     sw = 1;
     a_doc= 1; #0#3 = a_doc;
     ||PINTA #0#3;
 |FINSI;

 |SI diario ! #2#0; |O fecha ! #2#1; |O docum ! #2#2;
     |HAZ haycambio;
 |SINO;
     |SI diario ! 0; |Y diario ! 99;
         |SI Linea > 1;
             |SI debe = haber; |HAZ nuevoAsi; |FINSI;
         |FINSI;
     |FINSI;
 |FINSI;

 |SI #2#8 = "D";
       debe = debe + #2#9;
 |SINO;
       haber = haber + #2#9;
 |FINSI;

 |HAZ grabaLinea;
 |HAZ grabaContra;
 |HAZ grabaCab;

|FPRC;

|DEFBUCLE carecuad0;
 #2#1;
 ;
  0, feini, 000000,    1;
 99, fefin, 999999, 9999;
 be;
 NULL, NULL, NULL, NULL, NULL, actuali;
 #2#1, #2#0, #2#2;
|FIN;

|| ************************************************************************
||                EMPIEZA EL PROGRAMA CON TIPO 3
|| ************************************************************************

|PROCESO acuadrar; |TIPO 3;
 |SI #0#0 = "N";  |ACABA; |FINSI;
 |NOPARA;

 |ABRE #1;
 |CIERRA #1;
 |DELINDEX #1;

|HAZ nom_usu;
    alfa1 = "o" + nom_tmp;
    alfa2 = "p" + nom_tmp;
    traba1 = alfa1;
 |NOME_DAT #3 alfa1;
 |NOME_DAT #4 alfa2;

 |ABRE #3;
 |CIERRA #3;
 |DELINDEX #3;
 |ABRE #4;
 |CIERRA #4;
 |DELINDEX #4;

|| *************************************************
  traba3 = "q" + nom_tmp;
  |DFICE dir_fich;
  |DDEF VAlfa1; |QBF VAlfa1;
   VAlfa2 = VAlfa1 + "calicont";
   VAlfa3 = dir_fich + traba3;
|| ***************************************************

 |BLIN 24;
 |PINTA 2410,"Reconstruyendo Diario ... ";

 |ABRE #1;
 |ABRE #2;
 |ABRE #3;
 |ABRE #4;
 |ABRE #31;

 c_doc = 0;
 feini = "01.01.1900"
 fefin = "31.12.2099"

  |BUCLE carecuad0;

  |SI sw ! 0;
      |SI debe ! haber;
          error = "Descuadre.";
          |HAZ impresora;
      |FINSI;
  |FINSI;
 |SI sw_error = 1;
     |PIEINF;
     |FININF;
     |FINIMP;
 |FINSI;

 |CIERRA #1;
 |CIERRA #2;
 |CIERRA #3;
 |CIERRA #4;
 |CIERRA #31;

 |HAZ final;
 |SIPARA;
|FPRC;

|| ***************************** PROCESOS FINALES

|PROCESO modi_iva;
 |SI #4#0 = "R";
     #15#0 = #4#1;
     #15#2 = #4#2;
     #15#27 = #4#3;
     |LEE 101#15=;
     |SI FSalida ! 0; |ACABA; |FINSI;
     #15#45 = #4#7;
     #15#46 = #4#8;
     #15#47 = #4#9;
     |GRABA 020#15a;
     |LIBERA #15;
 |FINSI;
 |SI #4#0 = "S";
     #16#0 = #4#1;
     #16#2 = #4#2;
     #16#27 = #4#3;
     |LEE 101#16=;
     |SI FSalida ! 0; |ACABA; |FINSI;
     #16#45 = #4#7;
     #16#46 = #4#8;
     #16#47 = #4#9;
     |GRABA 020#16a;
     |LIBERA #16;
 |FINSI;
|FPRC;

|DEFBUCLE catrasi2;
 #4#1;
 ;
 " ", 00, 000000, "  ";
 "z", 99, 999999, "zz";
 be;
 NULL, modi_iva;
|FIN;

|PROCESO final;
 |SI reg_leidos ! reg_grabados;
   |CUADRO 1003 1477;
   |ATRI R;
   |PINTA 1104, " HAY UN PROBLEMA EN EL FICHERO DE APUNTES, EL PROCESO NO PUEDE CONTINUAR ";
   |PINTA 1204, "  LANCE UNA INDEXACION DE FICHEROS (OPCIONES AVANZADAS/CONTROL INDICES)  ";
   |PINTA 1304, "                E INTENTE EJECUTAR DE NUEVO ESTE PROCESO.                ";
   |ATRI 0;
   |PAUSA;
  |ACABA;
 |FINSI;

  |DFICE dir_fich;
  traba2 = "camaasil";
  |HAZ copiando;
  |DELINDEX #3;

  |DFICE dir_fich;
  traba1 = traba3;
  traba2 = "calicont";
  |HAZ copiando;

  traba4 = VAlfa3 + ".dat";
  |FBORRA traba4;
  traba4 = VAlfa3 + ".ixx";
  |FBORRA traba4;
  traba4 = VAlfa3 + ".ddx";
  |FBORRA traba4;

|| ... Actualiza el numero de Documento Actual.
   |ABRE #5;
   |LEE 101#5p;
   Estado5 = FSalida;
   #5#1 = c_doc;
   |SI Estado5 ! 0; |GRABA 020#5n; |FINSI;
   |SI Estado5 = 0; |GRABA 020#5a; |FINSI;
   |LIBERA #5;
   |CIERRA #5;

|| ... Proceso de IVAS
 |BLIN 24;
 |PINTA 2410,"Relacionando Fichero de Facturas ... ";
   |ABRE #15;
   |ABRE #16;
   |BUCLE  catrasi2;
   |CIERRA #15;
   |CIERRA #16;
   |DELINDEX #4;
|FPRC;

|PROCESO copiando;
  || traba = " COPIANDO FICHERO: " + traba1 + "        ";
  || |PINTA 2211, traba;
  temporal1 = dir_fich + "/" + traba1 + ".ixx";
  temporal2 = dir_fich + "/" + traba2 + ".ixx";
  |COPIA_FICHERO temporal1,temporal2;
  |SI FSalida ! 0;  |ACABA;  |FINSI;
  temporal1 = dir_fich + "/" + traba1 + ".dat";
  temporal2 = dir_fich + "/" + traba2 + ".dat";
  |COPIA_FICHERO temporal1,temporal2;
  |SI FSalida ! 0;
      temporal2 = dir_fich + traba2 + ".ixx";
      |FBORRA temporal2;
      |ACABA;
  |FINSI;
  temporal1 = dir_fich + "/" + traba1 + ".ddx";
  temporal2 = dir_fich + "/" + traba2 + ".ddx";
  |COPIA_FICHERO temporal1,temporal2;
|FPRC;
