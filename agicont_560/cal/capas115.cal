|FICHEROS;
     capas115 #0;
     camar115 #1;    || Acumulado Empresas
     cacup115 #2;

     camandia #20;   || diarios AGICONT
     caconcep #21;   || conceptos AGICONT
     camaasic #22;   || asientos cabs. AGICONT
     camaasil #23;   || asientos lins. AGICONT
     cacuenta #24;   || cuentas AGICONT
     canempre #25;   || empresas AGICONT
     caconasi #26;   || contador asientos AGICONT
     catradia #27;   || diario de trabajo
     cacuebal #28;   || cuentas de balance
|FIN;

|VARIABLES;
     alfa1 = "";
     alfa2 = "";
     alfa3 = "";
     alfa4 = "";
     nume1 = 0;
     nume2 = 0;
     nume3 = 0;
     nume4 = 0;
     para1 = 0;
     para2 = 0;
     para3 = 0;
     para4 = 0;
     fech1 = @;
     fech2 = @;
     fech3 = @;
     fech4 = @;
     elsw1 = 0;
     elsw2 = 0;
     elsw3 = 0;
     elsw4 = 0;
     FEstado = 0;
     seleccio = 0;
     modo = 0;
     sw_rup = 0;

     nImporte1 = 0;
     nImporte2 = 0;
     nCampo    = 0;

     p_mes = "";
     path_agicon = "";
     path_empres = "";
     path_agicli = "";
     sw_agicli = 0;
     cod_empre = 0;
     cod_perio = 0;
     imp110 = 0;
     signe = "";
     impor = 0;
     conte = "";
     digcu = 0;
     conce = 0;
     descr = "";
     empre = 0;
     cuantos = 0;

     p_mensa = "    LONGITUD DE CUENTA FUERA DE NIVEL!";
     d_diari = 0;
     d_opera = "";
     d_impor = 0;
     &modcon = 0;
     importe = 0;
     opera = "";
     ressum = 0;
     ini    = 0;

     fecalf = "";
     mes = "";
     mesfec = 0;
     mesac = 0;
     a = 0;
     b = 0;
     c = 0;
     ult_apunt = 0;
     p_alfa1 = "";
     p_error = 0;
     p_lugar = 0;
     p_cuan3 = 0;
     p_cuan4 = 0;
     p_cuan5 = 0;
     p_alfa2 = "";
     p_alfa3 = "";
     p_alfa4 = "";
     p_alfa5 = "";
     p_nume1 = 0;
     p_nume2 = 0;
     p_ceros = "";

     juan = 0;
     errant = 0;
     cuini = "            ";
     cufin = "999999999998";
     diini = 0;
     difin = 9;
     fecini = @;
     fecfin = @;
     tiini = "D";
     tifin = "H";
     diain = 0;
     diafi = 99;
     prim = 0;
     cuant = "";
     lon = 0;
     aa = 0;
     mensa = "";
     aar = "";
     traba = "";
     errcuent = "aaaaaaaaaaaa";
     mensa2   = "     No esta definida esta cuenta en Cuentas de Balance";
     uno = "";
     dos = "";
     mascara = "";
     nivel = 0;
|FIN;

|INCLUYE inici1_i;
|INCLUYE conasi_i;
____________________________________/ CALCULOS PANTALLA

|PROCESO mayus; |TIPO 0;
     #0Campo = #0Campo > #0Campo;
     |PINTA #0Campo;
|FPRC;

|PROCESO mirames; |TIPO 7;
     fech1 = ~;
     alfa1 = fech1;
     alfa1 = alfa1 % 402;
     |SI #0#3 = "M"
         #0Campo = alfa1;
     |SINO;
         |SI alfa1 = "01";  #0Campo = 1;  |FINSI;
         |SI alfa1 = "02";  #0Campo = 1;  |FINSI;
         |SI alfa1 = "03";  #0Campo = 1;  |FINSI;
         |SI alfa1 = "04";  #0Campo = 2;  |FINSI;
         |SI alfa1 = "05";  #0Campo = 2;  |FINSI;
         |SI alfa1 = "06";  #0Campo = 2;  |FINSI;
         |SI alfa1 = "07";  #0Campo = 3;  |FINSI;
         |SI alfa1 = "08";  #0Campo = 3;  |FINSI;
         |SI alfa1 = "09";  #0Campo = 3;  |FINSI;
         |SI alfa1 = "10";  #0Campo = 4;  |FINSI;
         |SI alfa1 = "11";  #0Campo = 4;  |FINSI;
         |SI alfa1 = "12";  #0Campo = 4;  |FINSI;
     |FINSI;
     |PINTA #0Campo;
|FPRC;

|PROCESO pintames; |TIPO 0;
     |SI #0#3 = "M"
          |SI #0Campo =  1;  p_mes = "ENERO     ";  |FINSI;
          |SI #0Campo =  2;  p_mes = "FEBRERO   ";  |FINSI;
          |SI #0Campo =  3;  p_mes = "MARZO     ";  |FINSI;
          |SI #0Campo =  4;  p_mes = "ABRIL     ";  |FINSI;
          |SI #0Campo =  5;  p_mes = "MAYO      ";  |FINSI;
          |SI #0Campo =  6;  p_mes = "JUNIO     ";  |FINSI;
          |SI #0Campo =  7;  p_mes = "JULIO     ";  |FINSI;
          |SI #0Campo =  8;  p_mes = "AGOSTO    ";  |FINSI;
          |SI #0Campo =  9;  p_mes = "SEPTIEMBRE";  |FINSI;
          |SI #0Campo = 10;  p_mes = "OCTUBRE   ";  |FINSI;
          |SI #0Campo = 11;  p_mes = "NOVIEMBRE ";  |FINSI;
          |SI #0Campo = 12;  p_mes = "DICIEMBRE ";  |FINSI;
     |SINO;
          |SI #0Campo > 4;  |ERROR;  |ACABA;  |FINSI;

          |SI #0Campo =  1;  p_mes = "1§ TRIMES.";  |FINSI;
          |SI #0Campo =  2;  p_mes = "2§ TRIMES.";  |FINSI;
          |SI #0Campo =  3;  p_mes = "3§ TRIMES.";  |FINSI;
          |SI #0Campo =  4;  p_mes = "4§ TRIMES ";  |FINSI;
     |FINSI;
     |ATRI I;
     |PINTA 1147, p_mes;
     |ATRI 0;
|FPRC;
____________________________________/ ACTUALIZACION NIVELES SUPERIORES

|PROCESO actualiza;
     ini = dig1;
     fecalf = #27#3;
     mes = fecalf % 402;
     mesfec = mes;
     |SI ini = 1;
         mesac = mesfec;
     |SINO;
         mesac = (mesfec - ini) + 1;
         |SI mesac [ 0;
             mesac = (13 - ini) + mesfec;
         |FINSI;
     |FINSI;
     a = (mesac * 3) + 9;
     b = a + 1;
     c = a + 2;
     |SI #27#4 = "D";
         #24a = #24a + #27#5;
         #24#51 = #24#51 + #27#5;
     |SINO;
         #24b = #24b + #27#5;
         #24#52 = #24#52 + #27#5;
     |FINSI;
     #24c = #24a - #24b;
     #24#53 = #24#51 - #24#52;
|FPRC;

|PROCESO suma;
     |SI #27#6 = 0;
         |SI #27#4 = "D";
             #24#9 = #24#9 + #27#5;
             #24#51 = #24#51 + #27#5;
         |SINO;
             #24#10 = #24#10 + #27#5;
             #24#52 = #24#52 + #27#5;
         |FINSI;
         #24#11 = #24#9 - #24#10;
         #24#53 = #24#51 - #24#52;
     |SINO;
         |SI #27#6 = 99;
             |SI #27#4 = "D";
                 #24#48 = #24#48 + #27#5;
                 #24#51 = #24#51 + #27#5;
             |SINO;
                 #24#49 = #24#49 + #27#5;
                 #24#52 = #24#52 + #27#5;
             |FINSI;
             #24#50 = #24#48 - #24#49;
             #24#53 = #24#51 - #24#52;
         |SINO;
             |HAZ actualiza;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO saca1;
     #24#4 = #28#1;
     #24#5 = #28#2;
     #24#6 = #28#3;
     #24#7 = #28#4;
     #24#8 = #28#5;
|FPRC;

|PROCESO defect1;
     |ABRE #28;
     uno = #24#0;
     |QBF uno;
     dos = uno + "000000000000";
     uno = dos % A101;
     #28#0 = uno;
     |LEE 040#28=;
     |SI FSalida = 0;
         |HAZ saca1;
         |VETE findefe;
     |FINSI;
     uno = dos % A102;
     #28#0 = uno;
     |LEE 040#28=;
     |SI FSalida = 0;
         |HAZ saca1;
         |VETE findefe;
     |FINSI;
     uno = dos % A103;
     #28#0 = uno;
     |LEE 040#28=;
     |SI FSalida = 0;
         |HAZ saca1;
         |VETE findefe;
     |FINSI;
     uno = dos % A104;
     #28#0 = uno;
     |LEE 040#28=;
     |SI FSalida = 0;
         |HAZ saca1;
         |VETE findefe;
     |SINO;
         |MENAV mensa2;
     |FINSI;
   |ET findefe;
     nivel = #24#1;
   || la 3a clave esta formada por la cta + zzzz y el campo fantasma del nivel;
     mascara = #24#0;
     |QBF mascara;
     mascara = mascara +"zzzzzzzzzzzz";
     mascara = mascara % 112;
     #24#54    = mascara;
     #24#55    = nivel;
     |CIERRA #28;
|FPRC;

|PROCESO actuali;
     |SI #27#2 [ juan; |O #27#2 = 9;
         |SI prim = 1;  |GRABA 020#24a;  |FINSI;
         prim = 0;
         |ACABA;
     |FINSI;

     aa = 100 + lon;
     aar = aa;
     traba = #27#1 % aar;

     |ET iniemp;
         |SI prim = 0;
             cuant = traba;
             prim = 1;
             #24#0 = cuant;
             #24#1 = juan;
             |SI #24#0 = errcuent;  prim = 0;  |ACABA;  |FINSI;

             |LIBERA #24;
             |LEE 101#24=;
             |SI FSalida ! 0;
                 |LIBERA #24;
                 |DDEFECTO #24;
                 #24#0 = cuant;
                 #24#1 = juan;
                 |HAZ defect1;

                 |PUSHV 0501 2380;
                 |PINPA #0#24;
                 |PINDA #0#24;
                 |ENDATOS #1#24;
                 |SI FSalida ! 0;
                     |POPV;
                     mensa = "Actualizando nivel: " + juan + "        ";
                     |PINTA 2410,mensa;
                     errant = #24#0;
                     prim = 0;
                     |ACABA;
                 |FINSI;
                 |GRABA 020#24b;
                 |POPV;
                 mensa = "Actualizando nivel: " + juan + "        ";
                 |PINTA 2410,mensa;
                |FINSI;
             |FINSI;

             |SI cuant ! traba;
                 |GRABA 020#24a;
                 prim = 0;
                 |VETE iniemp;
             |FINSI;

       |HAZ suma;
|FPRC;

|PROCESO borrando;
  |BORRA 020#27a;
|FPRC;

|DEFBUCLE camaapua0;
 #27#1;
 ;
 codcon,cuini,diini,fecini,tiini,diain;
 codcon,cufin,difin,fecfin,tifin,diafi;
 be;
 NULL,actuali;
|FIN;

|DEFBUCLE camaapua1;
 #27#1;
 ;
 codcon,cuini,diini,fecini,tiini,diain;
 codcon,cufin,difin,fecfin,tifin,diafi;
 be;
 NULL,borrando;
|FIN;

|PROCESO niveles;
  |PINTA 2410,"Actualizando Niveles Contables Superiores ...";
  |ABRE #24;
  |PARA juan = dig3 - 1; |SI juan ] 1; |HACIENDO juan = juan - 1;
        prim = 0;
        |SI juan = 1; lon = dig4; |FINSI;
        |SI juan = 2; lon = dig5; |FINSI;
        |SI juan = 3; lon = dig6; |FINSI;
        |SI juan = 4; lon = dig7; |FINSI;
        |SI juan = 5; lon = dig8; |FINSI;
        fecini = "01.01.0000";
        fecfin = "31.12.8999";
        |BUCLE camaapua0;
        |SI prim = 1;
            |GRABA 020#24a;
        |FINSI;
  |SIGUE;
  |CIERRA #24;
  fecini = "01.01.0000";
  fecfin = "31.12.8999";
  |BUCLE camaapua1;
  |ABRE #27;
  |LEE 000#27u;
  |SI FSalida ! 0;
       |CIERRA #27;
       |DELINDEX #27;
  |SINO;
       |CIERRA #27;
  |FINSI;
  |BLIN 24;
|FPRC;




____________________________________/ GRABACION DE ASIENTOS
|PROCESO actu_diari;
|ABRE #20;
    #20#0 = d_diari;
|LEE 101#20=;
|SI FSalida ! 0;
    |DDEFECTO #20;
        #20#0 = d_diari;
        #20#1 = "<DIARIO SIN DEFINIR>";
    |GRABA 020#20n;
    |LEE 121#20=;
|FINSI;
|SI d_opera = "D";
        #20#2 = #20#2 + d_impor;        || debe
|SINO;
        #20#3 = #20#3 + d_impor;        || haber
|FINSI;
|GRABA 020#20a;
|LIBERA #20;
|CIERRA #20;
|FPRC;

|PROCESO altatra;
    modcon = 1;
|ABRE #27;
    #27#0 = codcon;
    #27#1 = #23#4;
    #27#2 = #23#5;
    #27#3 = #23#1;
    #27#4 = #23#8;
    #27#6 = #23#0;
|LEE 101#27=;
|SI FSalida ! 0;
    |DDEFECTO #27;
        #27#0 = codcon;
        #27#1 = #23#4;
        #27#2 = #23#5;
        #27#3 = #23#1;
        #27#4 = #23#8;
        #27#6 = #23#0;
    |GRABA 101#27b;
|FINSI;
    importe = #23#9;  || aqui si que se usa el importe real
|SI opera = "B";
        importe = -importe;
|FINSI;
    #27#5 = #27#5 + importe;
|GRABA 101#27a;
|LIBERA #27;
|CIERRA #27;
|FPRC;

|PROCESO alta_cuen;
    #24#2 = "";          || descripcion
    #24#3 = "S";         || pase directo
    #24#58 = #0#0;      || fecha ultima operacion
|HAZ defect1;
|PUSHV 0501 2380;
|PINPA #0#24;
|PINDA #0#24;
|ENDATOS #1#24;
|SI FSalida = 0;
    |GRABA 020#24b;
|FINSI;
|POPV;
|FPRC;

|PROCESO actualiz1;
|DDEFECTO #24;
|ABRE #24;
    #24#0 = #23#4;
    #24#1 = #23#5;
|LEE 101#24=;
|SI FSalida ! 0;
    |HAZ alta_cuen;
|FINSI;
|SI #23#0 = 0;
    |SI #23#8 = "D";
            #24#9 = #24#9 + ressum;
            #24#51 = #24#51 + ressum;
    |SINO;
            #24#10 = #24#10 + ressum;
            #24#52 = #24#52 + ressum;
    |FINSI;
        #24#11 = #24#9 - #24#10;
        #24#53 = #24#51 - #24#52;
|SINO;
    |SI #23#0 = 99;
        |SI #23#8 = "D";
                #24#48 = #24#48 + ressum;
                #24#51 = #24#51 + ressum;
        |SINO;
                #24#49 = #24#49 + ressum;
                #24#52 = #24#52 + ressum;
        |FINSI;
            #24#50 = #24#48 - #24#49;
            #24#53 = #24#51 - #24#52;
    |SINO;
        |HAZ actualiz2;
    |FINSI;
|FINSI;
|SI #24#58 < #23#1;
        #24#58 = #23#1;
|FINSI;
|GRABA 020#24a;
|LIBERA #24;
|CIERRA #24;
|FPRC;

|PROCESO actualiz2;
    ini = dig1;      || numero de niveles
    fecalf = #23#1;
    mes = fecalf % 402;
    mesfec = mes;
|SI ini = 1;
        mesac = mesfec;
|SINO;
        mesac = (mesfec - ini) + 1;
    |SI mesac [ 0;
            mesac = (13 - ini) + mesfec;
    |FINSI;
|FINSI;
    a = (mesac * 3) + 9;
    b = a + 1;
    c = a + 2;
|SI #23#8 = "D";
        #24a = #24a + ressum;
        #24#51 = #24#51 + ressum;
|SINO;
        #24b = #24b + ressum;
        #24#52 = #24#52 + ressum;
|FINSI;
    #24c = #24a - #24b;
    #24#53 = #24#51 - #24#52;
|FPRC;

|PROCESO cabecera;
|DDEFECTO #22;
    #22#0 = #23#0;
    #22#1 = #23#1;
    #22#2 = #23#2;
|LEE 101#22=;
    FEstado = FSalida;
    #22#0 = #23#0;         || diario
    #22#1 = #23#1;         || fecha
    #22#2 = #23#2;         || documento
    #22#3 = #22#2;         || asiento
|SI #23#8 = "D";
        #22#4 = #22#4 + #23#9;
    |SI #22#09 = 0; #22#08 = #23#4; #22#09 = #23#5; |FINSI; || contrp.HABER
|FINSI;
|SI #23#8 = "H";
        #22#5 = #22#5 + #23#9;
    |SI #22#11 = 0; #22#10 = #23#4; #22#11 = #23#5; |FINSI; || contrp.DEBE
|FINSI;
|SI FEstado = 0;  |GRABA 020#22a;  |FINSI;
|SI FEstado ! 0;
    |HAZ mensaje;
    |GRABA 020#22n;
|FINSI;
|LIBERA #22;
|FPRC;

|PROCESO grabatra;
     modcon = 0;
     |ABRE #27;
     |LEE 101#27u;
     |SI FSalida ! 0;
         |DDEFECTO #27;
         #27#0 = 1;
     |SINO;
          #27#0 = #27#0 + 1;
     |FINSI;
     |GRABA 101#27n;
     codcon = #27#0;
     |LIBERA #27;
     |CIERRA #27;
|FPRC;

|PROCESO contador1;
     |ABRE #26;
     |LEE 101#26p;
     |SI FSalida ! 0;
         |DDEFECTO #26;
         |GRABA 020#26n;
         |LEE 101#26p;
     |FINSI;
     #26#1 = #26#1 + 1;
     |GRABA 020#26a;
     |LIBERA #26;
     |CIERRA #26;
     ult_apunt = 1;
|FPRC;

|PROCESO mensaje;
    cuantos = cuantos + 1;
    alfa1 = "Grabando Asiento : " + #22#2;
    |D_CUADRO 1921, 2150;
    |BLANCO 2022 2049;
    |ATRI I;
    |PINTA 2022, alfa1;
    |ATRI 0;
|FPRC;

|PROCESO el_concepto;
|DDEFECTO #21;
|ABRE #21;
    #21#0 = conce;
|LEE 000#21=;
    descr = #21#1;  |QBF descr;
    descr = descr + " " + #2#2;
|CIERRA #21;
|FPRC;

|PROCESO apuntes;
     |SI impor = 0;  |ACABA;  |FINSI;

     |HAZ el_concepto;

     |DDEFECTO #23;
     #23#00 = #2#1;        || diario
     #23#01 = #0#0;        || fecha
     #23#02 = #26#1;       || documento
     #23#03 = ult_apunt;   || apunte
     #23#04 = conte;       || cuenta
     #23#05 = digcu;       || digito cuenta
     #23#06 = conce;       || concepto
     #23#07 = descr;       || descripcion
     #23#08 = signe;       || signo
     #23#09 = impor;       || importe

     |SI #23#08 = "D";
         #23#16 = #23#4;
         #23#17 = #23#5;
     |SINO;
         #23#10 = #23#4;
         #23#11 = #23#5;
     |FINSI;
     |GRABA 020#23n;

     |HAZ cabecera;

     ressum = #23#9;
     |HAZ actualiz1;     || actualiza cuentas

     opera = "A";
     |HAZ altatra;       || actualiza niveles inferiores

     d_diari = #23#0;
     d_opera = #23#8;
     d_impor = #23#9;
     |HAZ actu_diari;    || actualiza diario
     ult_apunt = ult_apunt + 10;
|FPRC;

____________________________________/ LECTURA Y CALCULOS PRINCIPALES


|PROCESO Calcula;
     |ABRE #22;
     |ABRE #23;

     |HAZ contador1;

     |SI #0#3 = "M";
         nCampo    = #0#1 + 27;  nImporte1 = #1nCampo;
         nCampo    = #0#1 + 63;  nImporte2 = #1nCampo;
     |SINO;
        |SI #0#1 = 1;
            nImporte1 = #1#28 + #1#29 + #1#30;
            nImporte2 = #1#64 + #1#65 + #1#66;
        |FINSI;

        |SI #0#1 = 2;
            nImporte1 = #1#31 + #1#32 + #1#33;
            nImporte2 = #1#67 + #1#68 + #1#69;
        |FINSI;

        |SI #0#1 = 3;
            nImporte1 = #1#34 + #1#35 + #1#36;
            nImporte2 = #1#70 + #1#71 + #1#72;
        |FINSI;

        |SI #0#1 = 4;
            nImporte1 = #1#37 + #1#38 + #1#39;
            nImporte2 = #1#73 + #1#74 + #1#75;
        |FINSI;
     |FINSI;

     |SI nImporte1 = 0;  |Y nImporte2 = 0;  |ACABA;  |FINSI;

     |SI nImporte1 ! 0;  |Y nImporte2 ! 0;
         |SI #2#3 = #2#5;  |Y #2#7 = #2#9;
             signe = "D";
             impor = nImporte1 + nImporte2;
             conte = #2#3;
             conce = #2#7;
             digcu = #2#11;
             |HAZ apuntes;
         |SINO;
             signe = "D";
             impor = nImporte1;
             conte = #2#3;
             conce = #2#7;
             digcu = #2#11;
             |HAZ apuntes;

             signe = "D";
             impor = nImporte2;
             conte = #2#5;
             conce = #2#9;
             digcu = #2#13;
             |HAZ apuntes;
         |FINSI;

         |SI #2#4 = #2#6;  |Y #2#8 = #2#10;
             signe = "H";
             impor = nImporte1 + nImporte2;
             conte = #2#4;
             conce = #2#8;
             digcu = #2#12;
             |HAZ apuntes;
         |SINO;
             signe = "H";
             impor = nImporte1;
             conte = #2#4;
             conce = #2#8;
             digcu = #2#12;
             |HAZ apuntes;

             signe = "H";
             impor = nImporte2;
             conte = #2#6;
             conce = #2#10;
             digcu = #2#14;
             |HAZ apuntes;
         |FINSI;
     |FINSI;

     |SI nImporte1 ! 0;  |Y nImporte2 = 0;
         signe = "D";
         impor = nImporte1;
         conte = #2#3;
         conce = #2#7;
         digcu = #2#11;
         |HAZ apuntes;

         signe = "H";
         impor = nImporte1;
         conte = #2#4;
         conce = #2#8;
         digcu = #2#12;
         |HAZ apuntes;
     |FINSI;

     |SI nImporte2 ! 0;  |Y nImporte1 = 0;
         signe = "D";
         impor = nImporte2;
         conte = #2#5;
         conce = #2#9;
         digcu = #2#13;
         |HAZ apuntes;

         signe = "H";
         impor = nImporte2;
         conte = #2#6;
         conce = #2#10;
         digcu = #2#14;
         |HAZ apuntes;
     |FINSI;

     |CIERRA #22;
     |CIERRA #23;
|FPRC;

|PROCESO conta1;         || comprobaciones y calculos
     |HAZ grabatra;
     |HAZ Calcula;
     |SI modcon ! 0; |HAZ niveles;  |FINSI;
|FPRC;

|PROCESO Pase;
     |ABRE #1;
     |LEE 000#1p;
     |SI FSalida = 0;  |HAZ conta1;  |FINSI;
     |CIERRA #1;
|FPRC;

____________________________________/
|PROCESO Tipo3;   |TIPO 3;
     |ABRE #2;
     |LEE 101#2p;
     |SI FSalida ! 0;
         |MENAV "     No Existe Control de Asientos. ";
         |CIERRA #2;
         |ACABA;
     |FINSI;
     |CIERRA #2;

     |CONFI "    NHa emitido los 115 de la empresa seleccionada S/N: ";
     |SI FSalida = 0;
         |HAZ Pase;
         |SI cuantos ! 0;
             alfa1 = "    Creados [" + cuantos + "] asientos ...";
             |MENAV alfa1;
         |SINO;
             |MENAV "    Seleccion vacia ...";
         |FINSI;
     |SINO;
         |MENAV "    No se hubieran creado asientos ...";
     |FINSI;
|FPRC;
