|FICHEROS;
    cabatopa #0;
    cacuenta #1;
    cabatopc #2;
    caexistc #6;
    caexistl #7;
    canempre #30;
    caconimp #8;
|FIN;

|VARIABLES;
  &pathbal = "";
  &balance = 1;
  &entrada = 0;
  &aparam = "";
  informe = "cabatopc";
  alfa1 = "";
  nume1 = 0;
    x = 0;
    y = 0;
  &r_emp = 0;
  &r_per = 0;
  &d_mes = 0;
  &h_mes = 0;
  &estru = 0;
  &r_eje = 0;
  &r_nom = "";
    varcamp = 0;
    mes = 0;
    letrames = "";
    sw_error = 0;
    cue_perdi = "";
    dig_perdi = 0;

  &eldesde = 0;
  &elhasta = 0;
  des_exi = 0;
  has_exi = 0;
  d_cuen = "";
  h_cuen = "";
  d_digi = 0;
  h_digi = 9;

 impuesto  = 0;
 act_cuent = "";
 act_nivel = 0;
 saldo   = 0;
 debe    = 0;
 haber   = 0;
 sw_exp  = 0;
 descar  = 0;
 bene    = 0;
  activo = 0;
  pasivo = 0;
   explo = 0;
  debe1  = 0;
  debe2  = 0;
  debe3  = 0;
  debe4  = 0;
  haber1 = 0;
  haber2 = 0;
  haber3 = 0;
  haber4 = 0;
  importe = 0;
  tipo = "";
  bloque = 0;
  agrupaci = 0;
  epigrafe = 0;
  partida = 0;

  canti = 0;
  punto_h1 = 0;
  punto_h2 = 0;
  punto_h3 = 0;
  punto_h4 = 0;
  punto_h5 = 0;
  punto_h6 = 0;
  punto_d1 = 0;
  punto_d2 = 0;
  punto_d3 = 0;
  punto_d4 = 0;
  punto_d5 = 0;
  punto_d6 = 0;

  sw = 0;
  sw_agr = 0;
  sw_epi  = 0;
  sw_alguno = 0;

  &titulo = "";
  &sw_inf = 0;
  &cantidad = 0;
  &variable = "";
  &mesi = "";
  &mesf = "";
  &fec  = @;
 d_tipo = "";
 h_tipo = "";
 d_bloque = 0;
 h_bloque = 0;
    tecla = "";
    t1 = "i";
    t2 = "I";
|FIN;

|INCLUYE acceso_i;
|INCLUYE digito_i;

|| ========================================================================
||                        CALCULOS PANTALLA
|| ========================================================================

|PROCESO mayor;
  alfa1 = #0Campo > " ";
  #0Campo = alfa1;
  |PINTA #0Campo;
|FPRC;

|PROCESO pintames; |TIPO 0;
  varcamp = Campo + 2;                || Coge el campo del nombre del mes
  |SI #0Campo ! 0;|Y #0Campo ! 13;
      mes = #30#11 + (#0Campo - 1);   || Operacion para saber que mes es
  |SINO;
      mes = #0Campo;
  |FINSI;
  |HAZ mesletra;                      || Recoge el mes en letra
  #0varcamp = letrames;
  |PINTA #0varcamp;                   || Pinta el nombre del mes
  |SI Campo = 1;  |Y #0#0 > #0#1;
      |MENAV "    Error de Entrada ...";
      |ERROR;
  |FINSI;
|FPRC;

|PROCESO pintames2; |TIPO 0;
  |SI #0Campo ! 0;|Y #0Campo ! 13;
      mes = #30#11 + (#0Campo - 1);   || Operacion para saber que mes es
  |SINO;
      mes = #0Campo;
  |FINSI;
  |HAZ mesletra;
  |ATRI I;
  |SI Campo = 7;  |PINTA 1324, letrames;  |FINSI;
  |SI Campo = 8;  |PINTA 1360, letrames;  |FINSI;
  |ATRI 0;
  |SI Campo = 8;  |Y #0#7 > #0#8;
      |MENAV "    Error de Entrada ...";
      |ERROR;
   |FINSI;
|FPRC;

|PROCESO mesletra;
  |SI mes = 0;  letrames = "Apertura  "; |FINSI;
  |SI mes = 1;  letrames = "Enero     "; |FINSI;
  |SI mes = 2;  letrames = "Febrero   "; |FINSI;
  |SI mes = 3;  letrames = "Marzo     "; |FINSI;
  |SI mes = 4;  letrames = "Abril     "; |FINSI;
  |SI mes = 5;  letrames = "Mayo      "; |FINSI;
  |SI mes = 6;  letrames = "Junio     "; |FINSI;
  |SI mes = 7;  letrames = "Julio     "; |FINSI;
  |SI mes = 8;  letrames = "Agosto    "; |FINSI;
  |SI mes = 9;  letrames = "Septiembre"; |FINSI;
  |SI mes = 10; letrames = "Octubre   "; |FINSI;
  |SI mes = 11; letrames = "Noviembre "; |FINSI;
  |SI mes = 12; letrames = "Diciembre "; |FINSI;
  |SI mes = 13; letrames = "Cierre    "; |FINSI;
|FPRC;

|PROCESO estruct; |TIPO 0;
  |SI #0#6 = 0;  |ACABA;  |FINSI;
  |HAZ cue_per;
  |SI sw_error ! 0;
      |MENAV "    No existe la estructura de descarga.";
      |ERROR;
  |FINSI;
|FPRC;

|PROCESO cue_per; || calcula la cuenta de perdidas y ganancias
  sw_error = 0;
  |ABRE #6;
  |DDEFECTO #6;
  #6#0 = #0#6;
  |LEE 040#6=;
  |SI FSalida ! 0;
      sw_error = 1;
      |CIERRA #6;
      |ACABA;
  |FINSI;
  cue_perdi = #6#2;
  dig_perdi = #6#3;
  |CIERRA #6;
|FPRC;

|| ... procesos iniciales
|PROCESO antesque;
  aparam = PARAMETRO;
  |QBF aparam;
  |SI aparam = "fech";
      |NOME_DAT #1 "cabalano";
  |FINSI;
|FPRC;

|PROCESO conexi; |TIPO 0;
  d_mes = #0#7;
  h_mes = #0#8;
  estru = #0#6;
  r_emp = empre;
  r_per = ejerc;
  r_eje = #30#26;
  r_nom = #30#1;
  |SUB_EJECUTA "caexicon.run";
|FPRC;

|| ========================================================================
||                     CALCULOS de FICHEROS AUXILIARES
|| ========================================================================

|PROCESO exis_expl;
  descar = #7des_exi - #7has_exi; || iniciales - finales
  |SI descar = 0; |ACABA; |FINSI;
  |SI descar ] 0;
      act_cuent = #7#5;     || cuenta descarga D
      act_nivel = #7#6;     || digito descarga D
  |SINO;
      act_cuent = #7#8;     || cuenta descarga H
      act_nivel = #7#9;     || digito descarga H
  |FINSI;

  |SI act_cuent = #1#0; |Y act_nivel = #1#1;
      sw_exp = 1;
      |SI descar ] 0;
          debe = debe + descar;
      |SINO;
          haber = haber - descar;
      |FINSI;
       saldo = debe - haber;
  |FINSI;
|FPRC;

|DEFBUCLE exis2;
 #7#1;
 ;
 #0#6, 01;
 #0#6, 99;
 be;
 NULL, exis_expl;
|FIN;

|PROCESO exis_3000;
  sw_exp  = 1;
  saldo   = saldo + #7has_exi;
|FPRC;

|DEFBUCLE exis1;
 #7#2;
 ;
 #0#6, #1#0, #1#1;
 #0#6, #1#0, #1#1;
 e;
 NULL,exis_3000;
|FIN;

|PROCESO calcular;
 saldo   = 0;
 debe    = 0;
 haber   = 0;
 sw_exp  = 0;
 alfa1   = #1#0 % 101;
 |SI alfa1 = "3";                 |BUCLE exis1; |FINSI;
 |SI alfa1 = "6"; |O alfa1 = "7"; |BUCLE exis2; |FINSI;

 |SI sw_exp = 1; |ACABA; |FINSI;

 |PARA x = eldesde; |SI x [ elhasta; |HACIENDO x = x + 3;
       y = x + 1;
       debe  = debe  + #1x;
       haber = haber + #1y;
       saldo = debe - haber;
 |SIGUE;
|FPRC;

|PROCESO grabar;
  |HAZ digitos2;
  |SI d_error ! 0;  |ACABA;  |FINSI;
  |SI #1#3 = "N";   |ACABA;  |FINSI;

  |SI #1#0 ! cue_perdi; || no es perdidas y ganancias
      |HAZ calcular;
      |SI saldo ! 0;
          |SI #1#5 = "H"; |O #1#5 = "P";
              saldo = - saldo;
          |FINSI;
          |HAZ sumasaldo;
      |FINSI;
  |FINSI;
|FPRC;

|DEFBUCLE cabatopa0;
 #1#1;
 ;
 d_cuen, d_digi;
 h_cuen, h_digi;
 be;
 NULL, grabar;
|FIN;
|| ************************************************************************

|PROCESO sumasaldo;    || Calculamos cada cuenta
 |SI #1#5 = "A"; activo = activo + saldo; |FINSI;
 |SI #1#5 = "P"; pasivo = pasivo + saldo; |FINSI;
 |SI #1#5 = "D";
     |SI #1#4 = 1;
         debe1 = debe1 + saldo;
     |FINSI;
     |SI #1#4 = 2;
         debe2 = debe2 + saldo;
     |FINSI;
     |SI #1#4 = 3;
         debe3 = debe3 + saldo;
     |FINSI;
     |SI #1#4 = 4;
         debe4 = debe4 + saldo;
         |SI #1#6 = 1; |Y #1#7 = 14; impuesto = impuesto + saldo; |FINSI;
     |FINSI;
 |FINSI;
 |SI #1#5 = "H";
     |SI #1#4 = 1;
         haber1 = haber1 + saldo;
     |FINSI;
     |SI #1#4 = 2;
         haber2 = haber2 + saldo;
     |FINSI;
     |SI #1#4 = 3;
         haber3 = haber3 + saldo;
     |FINSI;
     |SI #1#4 = 4;
         haber4 = haber4 + saldo;
         |SI #1#6 = 1; |Y #1#7 = 14; impuesto = impuesto - saldo; |FINSI;
     |FINSI;
 |FINSI;
|FPRC;

|| ***********************************************************************

|PROCESO gr_final; || Grabar totales
 debe = 0;
 haber = 0;
 #1#0 = cue_perdi;
 #1#1 = dig_perdi;
 |LEE 001#1=;
 |SI FSalida ! 0;
     sw_error = 1;
     |MENAV "    ATENCION !!!. NO existe la cuenta de Perdidas y Ganancias ";
     |ACABA;
 |FINSI;
 |PARA x = eldesde; |SI x [ elhasta; |HACIENDO x = x + 3;
       y = x + 1;
       debe  = debe  + #1x;
       haber = haber + #1y;
       saldo = debe - haber;
 |SIGUE;
 debe  = debe1  + debe2  + debe3  + debe4;
 haber = haber1 + haber2 + haber3 + haber4;
 saldo = saldo + ( debe - haber);
 |SI #1#5 = "P";
     saldo = - saldo;
 |FINSI;
 |SI #1#5 = "A"; activo = activo + saldo; |FINSI;
 |SI #1#5 = "P"; pasivo = pasivo + saldo; |FINSI;

|| .....calcula_tot;
  punto_d1 = 0;   punto_h1 = 0;
  punto_d2 = 0;   punto_h2 = 0;
  punto_d3 = 0;   punto_h3 = 0;
  punto_d4 = 0;   punto_h4 = 0;
  punto_d5 = 0;   punto_h5 = 0;
  punto_d6 = 0;   punto_h6 = 0;

  canti = haber1 - debe1;
  |SI canti < 0;
      punto_h1 = - canti;
  |SINO;
      punto_d1 = canti;
  |FINSI;

  canti = haber2 - debe2;
  |SI canti < 0;
      punto_h2 = - canti;
  |SINO;
      punto_d2 = canti;
  |FINSI;

  canti = punto_d1 + punto_d2 - punto_h1 - punto_h2;
  |SI canti < 0;
      punto_h3 = - canti;
  |SINO;
      punto_d3 = canti;
  |FINSI;

  canti = haber3 - debe3;
  |SI canti < 0;
      punto_h4 = - canti;
  |SINO;
      punto_d4 = canti;
  |FINSI;

  canti = punto_d3 + punto_d4 - punto_h3 - punto_h4;
  |SI canti < 0;
      punto_h5 = - canti;
  |SINO;
      punto_d5 = canti;
  |FINSI;

  punto_h6 = punto_h5;
  |SI punto_h5 > 0;
      punto_h6 = punto_h6 + debe4;
  |FINSI;
  |SI punto_h6 < 0; punto_h6 = 0; |FINSI;

  punto_d6 = punto_d5;
  |SI debe4 > 0;
      punto_d6 = punto_d6 - debe4;
  |FINSI;
  |SI punto_d6 < 0; punto_d6 = 0; |FINSI;
|FPRC;

|| ***********************************************************************

|PROCESO cabatopa1;
 |DDEFECTO #2;
 |PINPA #0#2;
 #2#0 = #0#12;
 #2#1 = #30#2;
 #2#2 = #30#26;
 #2#3 = #30#1;
 #2#4 = activo;
 #2#5 = pasivo;
 #2#6 = debe1 + debe2 + debe3 + debe4;
 #2#7 = haber1 + haber2 + haber3 + haber4;
 #2#8 = punto_d1;
 #2#9 = punto_d2;
 #2#10 = punto_d3;
 #2#11 = punto_d4;
 #2#12 = punto_d5;
 #2#13 = punto_d6;
 #2#14 = punto_h1;
 #2#15 = punto_h2;
 #2#16 = punto_h3;
 #2#17 = punto_h4;
 #2#18 = punto_h5;
 #2#19 = punto_h6;

|| ........... Calculo de Impuesto
   #2#27 = 0; #2#28 = 0;
   |C_V #2#27,1,"N"; |C_V #2#28,1,"N";
   |SI impuesto > 0; #2#27 = impuesto;   |C_V #2#27,1,"S"; |FINSI;
   |SI impuesto < 0; #2#28 = - impuesto; |C_V #2#28,1,"S"; |FINSI;
   |SI impuesto = 0; #2#26 = "";         |FINSI;
|| ...............................

 cantidad = punto_d1 - punto_h1;
 nume1 = 1; tipo = "H"; |SI cantidad ] 0; tipo = "D"; |FINSI;
 |HAZ titulos;
 #2#20 = variable;

 cantidad = punto_d2 - punto_h2;
 nume1 = 2;
 tipo = "H"; |SI cantidad ] 0; tipo = "D"; |FINSI;
 |HAZ titulos;
 #2#21 = variable;

 cantidad = punto_d3 - punto_h3;
 nume1 = 3;
 tipo = "H";|SI cantidad ] 0; tipo = "D"; |FINSI;
 |HAZ titulos;
 #2#22 = variable;

 cantidad = punto_d4 - punto_h4;
 nume1 = 4;
 tipo = "H";|SI cantidad ] 0; tipo = "D"; |FINSI;
 |HAZ titulos;
 #2#23 = variable;

 cantidad = punto_d5 - punto_h5;
 nume1 = 5;
 tipo = "H";|SI cantidad ] 0; tipo = "D"; |FINSI;
 |HAZ titulos;
 #2#24 = variable;

 cantidad = punto_d6 - punto_h6;
 nume1 = 6;
 tipo = "H";|SI cantidad ] 0; tipo = "D"; |FINSI;
 |HAZ titulos;
 #2#25 = variable;

 |PARA nume1 = 8; |SI nume1 [ 19; |HACIENDO nume1 = nume1 + 1;
       |SI #2nume1 ! 0;
           |C_V #2nume1,1,"S";
       |SINO;
           |C_V #2nume1,1,"N";
       |FINSI;
 |SIGUE;
 |PINDA #0#2;

 |ATRI R;
 |MENSA "2400Pulse <I> para imprimir el resultado en papel ";
 |ATRI 0;

|| t1 = &255 + "807";
 t1 = &105;
 t2 = &73;
 |LEETECLA tecla;
 |SI t1 = tecla; |O t2 = tecla;
     |BLIN 24;
     |HAZ imprimir;
 |FINSI;
|FPRC;

|PROCESO imprimir;
  |IMPRE 0;
  |SI FSalida ! 0;  |ACABA;  |FINSI;
  |INFOR informe;
  |SI FSalida ! 0;
      |MENAV "     Error en informe.";
      |FINIMP;
      |ACABA;
  |FINSI;
  |PRINT;
  |PIEINF;
  |FININF;
  |FINIMP;
|FPRC;

|| ******************** Programa por TIPO 3 **************************
|PROCESO impre; |TIPO 3;

  |HAZ antesque;
  |HAZ conexi;

  mesi = #0#2; mesf = #0#3;
  fec = #0#12;

  eldesde = (#0#0 * 3) + 9;
  elhasta = (#0#1 * 3) + 9;
  des_exi =  #0#7 + 11;
  has_exi =  #0#8 + 11;
  d_cuen = "            "; d_digi = 0;
  h_cuen = "999999999999"; h_digi = 9;

      activo = 0;
      pasivo = 0;
       explo = 0;
      debe1  = 0;
      debe2  = 0;
      debe3  = 0;
      debe4  = 0;
      haber1 = 0;
      haber2 = 0;
      haber3 = 0;
      haber4 = 0;
/*          || No necesita el Recalculo. Trabaja con Directas <S>
  |ABRE #8;
  |LEE 111#8p;
  |SI #8#2 = "S";  |Y #8#3 = "N";  |Y aparam = "";
      |DFICE pathbal;
      balance = 1;
      |CIERRA #8;
      |SUB_EJECUTA "careccue.tab";
  |FINSI;
  |CIERRA #8;
*/

||... Modificar Cuentas a balance segun saldo
  r_emp = empre;
  r_per = ejerc;
  eldesde = (#0#0 * 3) + 11;
  elhasta = (#0#1 * 3) + 11;
  |SUB_EJECUTA "carecsig.run";
  eldesde = (#0#0 * 3) + 9;
  elhasta = (#0#1 * 3) + 9;
|| ---------------------------------------------------------------------

  |HAZ cue_per;  || calculamos la cuenta de perdidas y ganancias.

  |ABRET #1;
  |BUCLE cabatopa0;      || Bucle de Calculos cuentas
  |HAZ gr_final;         || Graba Totales

  |CIERRAT #1;
  |SI sw_error = 1; |ACABA; |FINSI;
|| ==================================================================
  |HAZ cabatopa1;      || Bucle de impresion

|FPRC;

|PROCESO titulos;
variable = "";
|SI tipo = "D";
    |SI nume1 =  1; variable = "I.  BENEFICIOS DE EXPLOTACION ............................."; |FINSI;
    |SI nume1 =  2; variable = "II. RESULTADOS FINANCIEROS POSITIVOS ......................"; |FINSI;
    |SI nume1 =  3; variable = "III.BENEFICIOS DE LAS ACTIVIDADES ORDINARIAS .............."; |FINSI;
    |SI nume1 =  4; variable = "IV. RESULTADOS EXTRAORDINARIOS POSITIVOS .................."; |FINSI;
    |SI nume1 =  5; variable = "V.  BENEFICIO ANTES DE IMPUESTOS .........................."; |FINSI;
    |SI nume1 =  6; variable = "VI. RESULTADO DEL EJERCICIO (BENEFICIOS) .................."; |FINSI;
|SINO;
    |SI nume1 =  1; variable = "I.  PERDIDAS DE EXPLOTACION ..............................."; |FINSI;
    |SI nume1 =  2; variable = "II. RESULTADOS FINANCIEROS NEGATIVOS ......................"; |FINSI;
    |SI nume1 =  3; variable = "III.PERDIDAS DE LAS ACTIVIDADES ORDINARIAS ................"; |FINSI;
    |SI nume1 =  4; variable = "IV. RESULTADOS EXTRAORDINARIOS NEGATIVOS .................."; |FINSI;
    |SI nume1 =  5; variable = "V.  PERDIDAS ANTES DE IMPUESTOS ..........................."; |FINSI;
    |SI nume1 =  6; variable = "VI. RESULTADO DEL EJERCICIO (PERDIDAS) ...................."; |FINSI;
|FINSI;
|FPRC;
