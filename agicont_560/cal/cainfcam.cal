|FICHEROS;
   cainfcam #0;
   camocabe #1;
   cacuenta #2;

   caselem1 #3;
|FIN;

|VARIABLES;
   Cont = 0;
   DCuenta = "";
   HCuenta = "";

   EmpAct = 0;
   PerAct = 0;

   Parte1 = 0;
   Parte2 = 0;
   Part1  = "";
   Part2  = "";
   Comodin = "";
   Comodin1 = "";
   ComodNum = 0;
   MaxDig = 0;
   Calc = 0;

   NumLins = 66;
   &TotElem = 0;
   &CabCta = 0;
   &LinEle = 0;
   &PieEle = 0;
   &NewPage = 0;
   &LinBlanco = 0;
   &ContLin = 0;
|FIN;

|INCLUYE acceso_i;
|INCLUYE puntos_i;

|PROCESO InfoAmo;
   #camocabe#3 = #cacuenta#0;
   #camocabe#4 = #cacuenta#1;
   |LEE 010#camocabe.=;
   |SI FSalida = 0;
      CabCta = 1; |PRINT; ContLin = ContLin + 4; CabCta = 0; TotElem = 0;
      LinEle = 1; |PRINT; ContLin = ContLin + 1; TotElem = TotElem + #camocabe#7;
      PieEle = 0;
      |SI ContLin > NumLins;
         |HAZ HastaElFinal;
         |PIEINF;
         ContLin = 7;
      |FINSI;

      |PARA; |SI FSalida = 0; |HACIENDO;
         |LEE 010#camocabe.s;
         |SI FSalida = 0; |Y #camocabe#3 = #cacuenta#0;|Y #camocabe#4 = #cacuenta#1;
            Calc = NumLins - 4;
            |SI ContLin > Calc;
               LinEle = 0;
               CabCta = 0; NewPage = 0;
               PieEle = 1;  |PRINT; PieEle = 0; ContLin = ContLin + 3;
               |HAZ HastaElFinal;
               |PIEINF;
               ContLin = 7;
               NewPage = 1; |PRINT; NewPage = 0; ContLin = ContLin + 5;
               LinEle = 1;
            |FINSI;
            |PRINT; ContLin = ContLin + 1;
            TotElem = TotElem + #camocabe#7;
         |SINO;
            PieEle = 1;
            CabCta = 0;
            LinEle = 0;
            |PRINT; ContLin = ContLin + 3;
            PieEle = 0;
            FSalida = 1;
         |FINSI;
      |SIGUE;
   |FINSI;
|FPRC;

|DEFBUCLE InfoAmo;
#cacuenta#1;
;
DCuenta,0;
HCuenta,9;
;
NULL,InfoAmo;
|FIN;

|PROCESO Seleccion;
   |SI #0#0 = "S";
      |C_M #0#1,1,"S";
      |C_M #0#2,1,"S";
      |PARA Cont = 3; |SI Cont [ 23; |HACIENDO Cont = Cont + 1;
         |C_M #0#Cont#,1,"N";
      |SIGUE;
   |SINO;
      |C_M #0#1,1,"N";
      |C_M #0#2,1,"N";
      |PARA Cont = 3; |SI Cont [ 23; |HACIENDO Cont = Cont + 1;
         |C_M #0#Cont#,1,"S";
      |SIGUE;
   |FINSI;
|FPRC;

|PROCESO BuscaCta;|TIPO 6;

   |SI #0#0 = "S"; |Y Campo > 2; |ACABA; |FINSI;
   |SI #0#0 = "N"; |Y Campo [ 2; |ACABA; |FINSI;

   |SI FSalida ! 18;
      Comodin = #0#Campo#;
      Comodin = Comodin - ".";
      |SI FSalida ! 0;
         Parte1 = (FSalida / 100) ! 0;
         Parte2 = (Parte1 * 100) + 199;
         Parte1 = Parte1 + 99;
         Comodin = #0#Campo#;
         Part1 = Comodin % Parte1;
         |QBF Part1;
         Part2 = Comodin % Parte2;
         |QBF Part2;
         Comodin = Part1 % 0;
         ComodNum = Comodin;
         Comodin = Part2 % 0;
         ComodNum = ComodNum + Comodin;
         ComodNum = MaxDig - ComodNum;
         Comodin = "0" * ComodNum;
         Comodin = Part1 + Comodin + Part2;
         #0#Campo# = Comodin;
         |PINTA #0#Campo#;
      |FINSI;
   |FINSI;

   salidas = FSalida;
   |HAZ punto;
   |SI p_error ! 0;
      |ERROR;
      |ACABA;
   |FINSI;
|FPRC;

|PROCESO Empresas;
   #48#2 = #caselem1#1;
   #48#3 = #caselem1#2;
   |LEE 010#48=;
   |SI FSalida = 0;
      |DFICB Comodin;
      Comodin1 = ("00000" + #caselem1#1) % -105;
      Comodin1 = Comodin1 + #caselem1#2;
      Comodin = Comodin + Comodin1 + "/";

      |PATH_DAT #1 Comodin;
      |PATH_DAT #2 Comodin;

      |ABRE #camocabe;
      |SELECT #4#camocabe;
      |SI #0#0 = "S";
         DCuenta = #0#1;
         HCuenta = #0#2;
         ContLin = 7;
         |BUCLE InfoAmo;
      |SINO;
         |PARA Cont = 2; |SI Cont [ 23; |HACIENDO Cont = Cont + 1;
            DCuenta = #0#Cont#;
            HCuenta = #0#Cont#;
            |BUCLE InfoAmo;
         |SIGUE;
      |FINSI;
      |PIEINF;
      |CIERRA #camocabe;

      #48#2 = EmpAct;
      #48#3 = PerAct;
      |LEE 010#48=;
   |FINSI;
|FPRC;

|DEFBUCLE Empresas;
#caselem1#1;
;
000001;
999999;
;
NULL,Empresas;
|FIN;

|PROCESO MultiEmpresa;
   |SUB_EJECUTA "caselemp.tab";

   EmpAct = #48#2;
   PerAct = #48#3;
   |ABRE #48;
   |BUCLE Empresas;
   |CIERRA #48;
|FPRC;

|PROGRAMA;

   |HAZ inicio;
   |CLS;

   |SI dig4 ! 0; MaxDig = #48#14; |FINSI;
   |SI dig5 ! 0; MaxDig = #48#15; |FINSI;
   |SI dig6 ! 0; MaxDig = #48#16; |FINSI;
   |SI dig7 ! 0; MaxDig = #48#17; |FINSI;
   |SI dig8 ! 0; MaxDig = #48#18; |FINSI;
   |SI dig9 ! 0; MaxDig = #48#19; |FINSI;

   |PINPA #0#0;
   |PINDA #0#0;

   |ENDATOS #1#0;
   |SI FSalida < 0; |ACABA; |FINSI;

   |IMPRE 0;
   |SI FSalida < 0; |ACABA; |FINSI;

   |SI PARAMETRO ! "";
      |INFOR "cainfca1";
      |SI FSalida < 0; |ACABA; |FINSI;

      |HAZ MultiEmpresa;
   |SINO;
      |INFOR "cainfcam";
      |SI FSalida < 0; |ACABA; |FINSI;

      |ABRE #camocabe;
      |SELECT #4#camocabe;

      |SI #0#0 = "S";
         DCuenta = #0#1;
         HCuenta = #0#2;
         |BUCLE InfoAmo;
      |SINO;
         |PARA Cont = 2; |SI Cont [ 23; |HACIENDO Cont = Cont + 1;
            DCuenta = #0#Cont#;
            HCuenta = #0#Cont#;
            |BUCLE InfoAmo;
         |SIGUE;
      |FINSI;
      |PIEINF;
      |CIERRA #camocabe;
   |FINSI;
   |FININF;
   |FINIMP;

|FPRO;

|PROCESO HastaElFinal;
   LinBlanco = 1;
   CabCta    = 0;
   PieEle    = 0;
   LinEle    = 0;
   NewPage   = 0;
   |PARA; |SI ContLin < NumLins; |HACIENDO ContLin = ContLin + 1;
      |PRINT;
   |SIGUE;
   LinBlanco = 0;
|FPRC;
