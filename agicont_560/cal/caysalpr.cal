|FICHEROS;
   caysalpr #0;       || Informe comprobacion saldos
   cacuentm #1;       || temporal cuentas presupuestos
   cacuenta #10;      || maestro cuentas
   cacuepre #11;      || fichas presupuestarias
|FIN;

|VARIABLES;
    &entrada = 1;
    potra = 0;
    x = 0;
    men1 = "     NO EXISTE EMPRESA !";
    mes = 0;                  || Variable para saber el mes seleccionado
    letrames = "";            || Variable para poner el mes en letra
    varcamp = 0;              || Variable para posicionarse en un campo
    xxxxfiac = "  ";          || Variables para no modificable
    xxaponer = "  ";
    xrelleno = "                              ";
    error1 = "0000Nivel repetido";
    error3 = "0000El numero de nivel no coincide con el numero de nivel de la empresa";
    nivel = 0;         || Variable que posee el nivel mas alto
    saldo = 0;         || Resta entre el debe y el haber;
    sw = 0;            || sw para nivel anterior
    nivelact = 0;      || Nivel actual
    rellenos = "";     || Variable para rellenar el hasta de la cuenta en zetas
    pepe1    = 0;
    pepe2    = 0;
    runnom   = "caybalpr";

    &i_salcuep = 0;
    &i_salprep = 0;
    &i_salcuee = 0;
    &i_salpree = 0;
    &i_salprea = 0;
    &dife1     = 0;
    &dife2     = 0;
    &porce1    = 0;
    &porce2    = 0;

    &p1 = 0;
    &p2 = 0;
    &p3 = 0;
    &e1 = 0;
    &e2 = 0;
    &e3 = 0;
    &a1 = 0;
    &rp = 0;
    &re = 0;
    menor = 0;
    para1 = 0;
  aparam = "";
|FIN;

|INCLUYE presup_i;
|INCLUYE acceso_i;
|INCLUYE digito_i;
|INCLUYE puntos_i;

|PROCESO mayus;
  #0Campo = #0Campo > " ";
  |PINTA #0Campo;
|FPRC;

|PROCESO antesque;
  aparam = PARAMETRO;
  |QBF aparam;
  |SI aparam = "fech";
      |NOME_DAT #10 "cabalano";
      |NOME_DAT #11 "cabalpre";
  |FINSI;
|FPRC;

|| ***********************************************************************
||                       CALCULOS DE PANTALLA
|| ***********************************************************************

|PROCESO cuenta; |TIPO 0;
    p_alfa1 = #0Campo;
    salidas = FSalida;
|HAZ punto;
|SI p_error ! 0;  |ERROR;  |ACABA;  |FINSI;
|FPRC;

|PROCESO mesconta; |TIPO 0;
  varcamp = Campo + 1;                || Coge el campo del nobre del mes
  mes = #30#11 + (#0Campo - 1);       || Operacion para seber que mes es
  |SI mes > 12; mes = mes - 12; |FINSI;
  |HAZ mesletra;                      || Recoge el mes en letra
  #0varcamp = letrames;
  potra = Campo;
  |PINTA #0varcamp;                   || Pinta el nombre del mes
  Campo = potra;

  |SI Campo = 4;
      |SI #0#2 > #0#4;
          |ERROR;
          |ACABA;     || Si el mes hasta es menor al desde
      |FINSI;
  |FINSI;
|FPRC;

|PROCESO mesletra;
  |SI mes = 0;  letrames = "Apertura  "; |FINSI;
  |SI mes = 1;  letrames = "Enero     "; |FINSI;
  |SI mes = 2;  letrames = "Febrero   "; |FINSI;
  |SI mes = 3;  letrames = "Marzo     "; |FINSI;
  |SI mes = 4;  letrames = "Abril     "; |FINSI;
  |SI mes = 5;  letrames = "Mayo      "; |FINSI;
  |SI mes = 6;  letrames = "Junio     "; |FINSI;
  |SI mes = 7;  letrames = "Julio     "; |FINSI;
  |SI mes = 8;  letrames = "Agosto    "; |FINSI;
  |SI mes = 9;  letrames = "Septiembre"; |FINSI;
  |SI mes = 10; letrames = "Octubre   "; |FINSI;
  |SI mes = 11; letrames = "Noviembre "; |FINSI;
  |SI mes = 12; letrames = "Diciembre "; |FINSI;
  |SI mes = 13; letrames = "Cierre    "; |FINSI;
|FPRC;

|PROCESO numnivel;|TIPO 0;
  |SI #0#8 > #30#13;
      |MENAV error3;
      |ERROR;
      |ACABA;
  |FINSI;
|FPRC;

|PROCESO nivel;|TIPO 7;  || Calculo para hacer no modificable
  |SI Campo = 10; |Y #0#8 < 2; || Si solo hemos seleccionado
      #0Campo = 0;
      |PINTA #0Campo;
      |HAZ nomodifi;              || un solo nivel;
  |FINSI;
  |SI Campo = 11; |Y #0#8 < 3; || Si solo hemos seleccionado
      #0Campo = 0;
      |PINTA #0Campo;
      |HAZ nomodifi;              || dos niveles;
  |FINSI;
  |SI Campo = 12; |Y #0#8 < 4; || Si solo hemos seleccionado
      #0Campo = 0;
      |PINTA #0Campo;
      |HAZ nomodifi;              || tres niveles;
  |FINSI;
  |SI Campo = 13; |Y #0#8 < 5; || Si solo hemos seleccionado
      #0Campo = 0;
      |PINTA #0Campo;
      |HAZ nomodifi;              || cuatro niveles;
  |FINSI;
  |SI Campo = 14; |Y #0#8 < 6; || Si solo hemos seleccionado
      #0Campo = 0;
      |PINTA #0Campo;
      |HAZ nomodifi;              || cinco niveles;
  |FINSI;
|FPRC;

|PROCESO nomodifi;             || RUTINA DE CONVERSION A NO MODIFICABLES
                               || VARIABLES: A_
    xxaponer = Anono;
    xxxxfiac = Control % A1001;
    Control = Control + xxxxfiac;
    Control = Control + xrelleno;
    Control = Control % A120;
    Control = Control + xxaponer;
|FPRC;

|PROCESO nivelrep;|TIPO 0;  || Para comprobar que no repetimos los niveles
  |SI nivel = 0;        || Buscamo el nivel mas alto;
      nivel = #0#9;
  |FINSI;

  |SI Campo = 10;
      |SI #0#10 = #0#9;|Y #0#10 ! 0;    || Si hay dos niveles iguales error
          |MENAV error1;
          |ERROR;  |ACABA;
      |SINO;
          |SI #0#10 > #0#9;  || Busca el nivel mas alto
              nivel = #0#10;
          |SINO;
              nivel = #0#9;
          |FINSI;
      |FINSI;
  |FINSI;

  |SI Campo = 11; |Y #0#11 ! 0;
      |SI #0#11 = #0#9; |O #0#11 = #0#10; || Si hay dos niveles iguales error
          |MENAV error1;
          |ERROR;  |ACABA;
      |FINSI;
      |SI #0#11 > nivel;  || Busca el nivel mas alto
          nivel = #0#11;
      |FINSI;
  |FINSI;

  |SI Campo = 12; |Y #0#12 ! 0;      || Si hay dos niveles iguales error
      |SI #0#12 = #0#9; |O #0#12 = #0#10; |O #0#12 = #0#11;
          |MENAV error1;
          |ERROR; |ACABA;
      |FINSI;
      |SI #0#12 > nivel;  || Busca el nivel mas alto
          nivel = #0#12;
      |FINSI;
  |FINSI;

  |SI Campo = 13; |Y #0#13 ! 0;     || Si hay dos niveles iguales error
      |SI #0#13 = #0#9; |O #0#13 = #0#10; |O #0#13 = #0#11; |O #0#13 = #0#12;
          |MENAV error1;
          |ERROR; |ACABA;
      |FINSI;
      |SI #0#13 > nivel;  || Busca el nivel mas alto
          nivel = #0#13;
      |FINSI;
  |FINSI;

  |SI Campo = 14; |Y #0#14 ! 0;    || Si hay dos niveles iguales error
      |SI #0#14 = #0#9; |O #0#14 = #0#10; |O #0#14 = #0#11; |O #0#14 = #0#12; |O #0#14 = #0#13;
          |MENAV error1;
          |ERROR;  |ACABA;
      |FINSI;
      |SI #0#14 > nivel;  || Busca el nivel mas alto
          nivel = #0#14;
      |FINSI;
  |FINSI;
|FPRC;

|| **********************************************************************
||                       CALCULOS DE IMPRESION
|| **********************************************************************

|PROCESO &totales;            || calcula los totales del informe
    p1 = p1 + i_salcuep;
    p2 = p2 + i_salprep;
    p3 = p3 + dife1;
    e1 = e1 + i_salcuee;
    e2 = e2 + i_salpree;
    e3 = e3 + dife2;
    a1 = a1 + i_salprea;
    rp = ((p1 * 100) / p2) ! 2;
    re = ((e1 * 100) / e2) ! 2;
|FPRC;

|PROCESO condicion;    || Calculo condiciones de impresion
|HAZ digitos2;
|SI d_error ! 0;  |ACABA;  |FINSI;

  pepe1     = 0;
  pepe2     = 0;
  i_salcuep = 0;         || Pone a cero el total saldo periodo cuentas
  i_salprep = 0;         || Pone a cero el total saldo periodo presupuesto
  i_salcuee = 0;         || Pone a cero el total saldo ejercicio cuentas
  i_salpree = 0;         || Pone a cero el total saldo ejercicio presupuesto
  i_salprea = 0;

  |SI sw = 0;
      nivelact = #1#55;  || Nivel actual
      sw = 1;
  |FINSI;

  |SI #1#55 ! 0;     || Condicion para saber si el nivel esta seleccionado
      |SI #1#55 = #0#9;|O #1#55 = #0#10;|O #1#55 = #0#11; || Compara 3 niveles
      |SINO;
          |SI #1#55 = #0#12;|O #1#55 = #0#13;|O #1#55 = #0#14; || Compara 3 niveles
          |SINO;
              |ACABA;
          |FINSI;
     |FINSI;
  |FINSI;

  varcamp =  (#0#2 * 3) + 9;
 || ************ Bucle del debe y haber periodo *************

 |PARA x = #0#2; |SI x [ #0#4; |HACIENDO x = x + 1;
       |SI x = #0#4; i_salcuep = #1varcamp; |FINSI;
       pepe1   = pepe1 + #1varcamp;
       varcamp = varcamp + 1;          || Se situa en  el campo del haber

       |SI x = #0#4; i_salcuep = i_salcuep - #1varcamp; |FINSI;
       pepe2     = pepe2 + #1varcamp;
       i_salcuee = pepe1 - pepe2;
       varcamp   = varcamp + 2;          || Se situa en el campo debe
 |SIGUE;

 |PARA x = #0#2; |SI x [ #0#4; |HACIENDO x = x + 1;
       |SI x ! 0;
           varcamp = x + 58;
           i_salpree = i_salpree + #1varcamp;  || suma presupuestado
           |SI x = #0#4; i_salprep = #1varcamp; |FINSI;
       |FINSI;
 |SIGUE;

 |SI #1#71 = "H"; i_salprep = -i_salprep; i_salpree = -i_salpree; |FINSI;

 |SI i_salprep = 0; |Y i_salpree = 0; |Y #0#6 = "N"; |ACABA; |FINSI;

 |PARA x = 59; |SI x [ 70; |HACIENDO x = x + 1;
       i_salprea = i_salprea + #1x;
 |SIGUE;
 |SI #1#71 = "H"; i_salprea = -i_salprea; |FINSI;

 dife1 = i_salcuep - i_salprep;
 dife2 = i_salcuee - i_salpree;

 porce1 = ((i_salcuep * 100) / i_salprep) ! 2;
 porce2 = ((i_salcuee * 100) / i_salpree) ! 2;

 |PRINT;
|FPRC;

|DEFBUCLE caybalpr0;      || Bucle del plan contable
#1#3;           || Lee por la tercera clave
;
#0#0,     1;
rellenos, 6;
be;
NULL,condicion;
|FIN;

|DEFBUCLE caybalpr1;      || Bucle del plan contable
#1#1;           || Lee por la primera clave
;
#0#0, 1;
#0#1, 6;
be;
NULL,condicion;
|FIN;

|PROCESO rellena;
    rellenos = "zzzzzzzzzzzz";
    menor = -1;

|PARA para1 = 9; |SI para1 [ 14; |HACIENDO para1 = para1 + 1;
    |SI #0para1 ! 0;
        |SI menor = -1;  menor = #0para1;  |FINSI;
        |SI menor > #0para1;  menor = #0para1;  |FINSI;
    |FINSI;                   || saca el nivel elegido mas peque¤o
|SIGUE;

|SI menor ! 0;
    |SI menor = 1;  nume1 = dig4;  |FINSI;
    |SI menor = 2;  nume1 = dig5;  |FINSI;   || construye el hasta segun
    |SI menor = 3;  nume1 = dig6;  |FINSI;   ||/el nivel mas bajo elegido
    |SI menor = 4;  nume1 = dig7;  |FINSI;
    |SI menor = 5;  nume1 = dig8;  |FINSI;
    |SI menor = 6;  nume1 = dig9;  |FINSI;
        nume1 = 100 + nume1;
        rellenos = #0#1 % nume1;
        rellenos = rellenos + "zzzzzzzzzzzz";
        rellenos = rellenos % 112;
|FINSI;
|FPRC;

|PROCESO impre;       || Impresion por impresora;
|HAZ rellena;
|IMPRE 0;                                 || Abrimos impresora
|SI FSalida ! 0; |ACABA; |FINSI;
    t_agrupa = #0#21;
|HAZ presup0;

|INFOR "caysalpr";
|SI FSalida ! 0; |FINIMP; |ACABA; |FINSI;

    p1 = 0;   p2 = 0;   p3 = 0;
    e1 = 0;   e2 = 0;   e3 = 0;
    a1 = 0;

|SI #0#16 = "D";
    |BUCLE caybalpr0;
|SINO;
    |BUCLE caybalpr1;
|FINSI;

|PIEINF;
|FININF;
|FINIMP;
|FPRC;

|| *********************************************************************
||                             COMIENZO
|| *********************************************************************

|PROGRAMA;
  |HAZ antesque;
  |CLS;
  |CABEZA "Balance de Saldos";
  |HAZ inicio;
  |DDEFECTO #0;
  FSalida = 1;
  |PARA; |SI 0 < FSalida; |HACIENDO;
         |PINPA #0#0;
         |PINDA #0#0;
         |ENDATOS #1#0;   || Seleccion
         |SI 0 [ FSalida;
             |HAZ impre;
             FSalida = 1;
         |FINSI;
 |SIGUE;
 |DELINDEX #1;  || se carga el temporal
|FPRO;
