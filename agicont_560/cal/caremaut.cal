|FICHEROS;
   caremaut #0;
   caremesc #6;
   caremesl #1;
   camanban #2;
   camancob #3;
   caremesi #4;
   caclipro #5;
   cacuenta #10;
|FIN;

|VARIABLES;
 &entrada = 1;

 alfa1    = "";
 mens     = "";
 swErrorR = 0;
 menR1    = "    Remesa YA emitida y/o contabilizada ";
 menR2    = "2400SRemesa YA existe. Aceptar S/N ";

 swpaso   = 0;
 cta      = 0;
 cta_t    = 0;
 dig_t    = 0;
 long     = "  ";
 sw       = 0;
 error    = 0;
 estado   = 0;
 linea    = 0;
 tot      = 0;
 sw_n     = 0;

 &quepant  = 1;
 &totrem   = 0;
 &contador = 0;
 &banco    = 0;
 &noremesa = 0;
|FIN;

|INCLUYE acces3_i;
|INCLUYE punto2_i;
|INCLUYE i_ficher;

|| **********************************************************************
|PROCESO mayus1;
 alfa1 = #0Campo > " ";
 #0Campo = alfa1;
 |PINTA #0Campo;
|FPRC;

|PROCESO contador; |TIPO 7;
 |SI swpaso = 0;
     |DDEFECTO #6;
     |ABRE #6;
     |LEE 040#6u;
     |SI FSalida = 0;
         #0#0 = #6#0 + 1;
     |SINO;
         #0#0 = 1;
     |FINSI;
     swpaso = 1;
     |CIERRA #6;
 |FINSI;
|FPRC;

|PROCESO ver_remesa; |TIPO 0;
 |SI FSalida = 2; |ACABA; |FINSI;
 |C_M #0#1,1,"S"; |C_M #0#2,1,"S";
 |C_M #0#4,1,"S";
 |HAZ leerem;
 |SI swErrorR ! 0;
     |SI swErrorR = 1;
         |MENAV mens;
         |ERROR;
         |ACABA;
     |SINO;
         |CONFI mens;
         |SI FSalida ! 0;
             |ERROR;
             |ACABA;
         |FINSI;
     |FINSI;
     |C_M #0#1,1,"N"; |C_M #0#2,1,"N";
     |C_M #0#4,1,"N";
     #0#1 = #6#1; |PINTA #0#1;
     #0#2 = #6#2; |PINTA #0#2;
     #0#3 = #6#3; |PINTA #0#3;
     #0#4 = #6#14; |PINTA #0#4;
     #0#5 = #6#15;
 |FINSI;
|FPRC;

|PROCESO fecha; |TIPO 0;
 #0#10 = #0#1; |PINTA #0#10;
|FPRC;

|PROCESO verfecha; |TIPO 0;

 |SI #0#10 < #0#1;
     |MENAV "    ERROR!. Fecha Vencimiento menor a Fecha Emision Remesa";
     |ERROR;
 |FINSI;
|FPRC;

|PROCESO leerem;
 swErrorR = 0;
 |ABRE #6;
 |DDEFECTO #6;
 #6#0 = #0#0;
 |LEE 001#6=;
 estado = FSalida;
 |SI FSalida = 0;
     |SI #6#8 ! "N"; |O #6#9 ! "N";
         swErrorR = 1; mens = menR1;
     |SINO;
         swErrorR = 2; mens = menR2;
     |FINSI;
 |FINSI;
 |CIERRA #6;
|FPRC;

|PROCESO cuenta1; |TIPO 6;
 p_alfa1 = #0Campo;
 salidas = FSalida;
 |HAZ punto2;
 |SI p_error ! 0;  |ERROR;  |ACABA;  |FINSI;
 cta_t = Campo;
 dig_t = Campo + 1;
 |HAZ cuenta1_t;
|FPRC;

|PROCESO cuenta1_t;
   cta  = #0cta_t;
   long = cta % 0;
   sw   = 0;
|SI long = dig4; sw = 1; #0dig_t = 1; |FINSI;
|SI long = dig5; sw = 1; #0dig_t = 2; |FINSI;
|SI long = dig6; sw = 1; #0dig_t = 3; |FINSI;
|SI long = dig7; sw = 1; #0dig_t = 4; |FINSI;
|SI long = dig8; sw = 1; #0dig_t = 5; |FINSI;
|SI long = dig9; sw = 1; #0dig_t = 6; |FINSI;
|SI sw = 0;
    #0dig_t = 0;
    |MENAV "    Longitud de cuenta fuera de Nivel !";
    |ERROR;
    |ACABA;
|FINSI;
|FPRC;

|PROCESO cuefin1; |TIPO 0;
 error = 0;
 |ABRE #10;
 #10#0 = #0cta_t;
 #10#1 = #0dig_t;
 |LEE 040#10=;
 |SI FSalida ! 0; error = 1; |CIERRA #10; |ERROR; |ACABA; |FINSI;
 |SI #10#3 = "N";
     error = 1;
     |CIERRA #10;
     |MENAV "      La cuenta no tiene pase DIRECTO !";
     |ERROR;
     |ACABA;
 |FINSI;
 |CIERRA #10;
|FPRC;

|| **********************************************************************

|PROCESO grabali;
 linea = linea + 1;
 #1#0 = #0#0;       || n§ remesa
 #1#1 = linea;      || linea
 #1#2 = #4#1;       || libro
 #1#3 = #4#2;       || Factura
 #1#4 = #4#8;       || Serie
 #1#5 = #4#3;       || N§ cobro
 #1#6 = #4#4;       || fecha vto.
 #1#7 = #4#5;       || Importe
 #1#11 = #4#10;     || Cuenta
 #1#12 = #4#11;     || Digito
 #1#13 = #4#13;     || Nombre
 #1#14 = #4#14;     || Poblacion

  || ...........................................
  #1#8 = #1#6 - #0#1;

  |ABRE #2;
  |LIBERA #2;
  #2#0 = #0#2;
  |LEE 110#2=;
  |SI FSalida = 0;
      #2#13  = #2#13 + #1#7;
      |GRABA 020#2a;
  |SINO;
      |DDEFECTO #2;
  |FINSI;
  |CIERRA #2;

  |SI #1#8 ] #2#20; |Y #1#8 [ #2#24;
      #1#9 = #2#28;
  |FINSI;

  |SI #1#8 ] #2#21; |Y #1#8 [ #2#25;
      #1#9 = #2#29;
  |FINSI;

  |SI #1#8 ] #2#22; |Y #1#8 [ #2#26;
      #1#9 = #2#30;
  |FINSI;

  |SI #1#8 ] #2#23; |Y #1#8 [ #2#27;
      #1#9 = #2#31;
  |FINSI;

  #1#10 = 0;
  |SI #1#9 ! 0;
      tot = #1#7 % #1#9;
      #1#10 = (tot * #1#8 ) / 365;
  |FINSI;

  |ABRE #3;
  |LIBERA #3;
  #3#0 = #1#2;
  #3#1 = #1#3;
  #3#2 = #1#4;
  #3#3 = #1#5;
  |LEE 110#3=;
  |SI FSalida = 0;
      #3#12 = #1#0;
      #3#23 = "S";
      #3#24 = #3#24 + #1#7;
      #3#9  = #3#9 + (#1#10 + #6#6);
      |GRABA 020#3a;
  |SINO;
     |DDEFECTO #3;
  |FINSI;
  |CIERRA #3;

  #6#5  = #6#5  + 1;
  #6#10 = #6#10 + #1#7;
  #6#11 = #6#11 + #6#6;
  #6#12 = #6#12 + #1#10;
 || .................................
 |GRABA 020#1n;
 sw_n = 1;
|FPRC;

|DEFBUCLE caremesi;
#4#1;
7;
000000,   "S";
contador, "S";
e;
NULL, grabali;
|FIN;

|PROCESO linealin;  || Para saber cual es la ultima linea
 linea = #1#1;
|FPRC;

|PROCESO introduc;
 linea = 0;
 |SI estado = 0;
     |BUCLELIN 0linealin#6;
 |SINO;
     |ABRE #2;
     |DDEFECTO #2;
     #2#0 = #0#2;
     |LEE 110#2=;
     |CIERRA #2;
     #6#4 = #2#8
     #6#6 = #2#16;

     #6#0 = #0#0;
     #6#1 = #0#1;
     #6#2 = #0#2;
     #6#3 = #0#3;
     #6#7 = #0#11;
     #6#14 = #0#4;
     #6#15 = #0#5;
 |FINSI;

 |ABRE #1;
 |BUCLE caremesi;
 |CIERRA #1;

 |SI sw_n = 1;
     |SALVA_DATOS #6;
     |ABRE #6;
     |DDEFECTO #6;
     #6#0 = #0#0;
     |LEE 001#6=;
     estado = FSalida;
     |REPON_DATOS #6;
     |SI estado = 0; |GRABA 020#6a; |FINSI;
     |SI estado ! 0; |GRABA 020#6n; |FINSI;
     |CIERRA #6;
 |FINSI;
|FPRC;

|| ****************************************************
|PROCESO pasalo;
 contador = contador + 1;
 #4#0 = contador;
 #4#1 = #3#0;
 #4#2 = #3#1;
 #4#3 = #3#3;
 #4#4 = #3#7;
 #4#5 = #3#8 - #3#24;
 #4#6 = #3#11;
 #4#7 = #0#6; |SI #4#7 = "S"; totrem = totrem + #4#5; |FINSI;
 #4#8 = #3#2;   || serie
 #4#9 = #3#6;
 #4#10 = #3#4;
 #4#11 = #3#5;
 #4#12 = #3#8;

 |ABRE #5;
 #5#23 = "C";
 #5#10 = #3#4;
 #5#11 = #3#5;
 |LEE 040#5=;
 |SI FSalida ! 0; |DDEFECTO #5; |FINSI;
 |CIERRA #5;
 #4#13 = #5#1;
 #4#14 = #5#6;
 |GRABA 040#4n;
|FPRC;

|DEFBUCLE camancob;
 #3#3;
 0, 1, 11, 4, 12, 23;
 #0#10, #0#9,  #0#7,  #0#8,  #0#11, #0#12, 00000, "N";
 #0#17, #0#16, #0#14, #0#15, #0#18, #0#19, 00000, "N";
 e;
 NULL, pasalo;
|FIN;

|| ***********************************************************************
|PROCESO pase; |TIPO 3;

 sw_n = 0;
 contador = 0;
 comienza = "q";
 |HAZ fichero;

 |NOME_DAT #4 fichero;
 |ABRE #4;
 |CIERRA #4;
 |DELINDEX #4;

 |HAZ leerem;  || Para saber si existe ya la remesa

 |ABRE #4;
 |BUCLE camancob;
 |CIERRA #4;
 |SI contador  ! 0;
     banco  = #0#2;
     |SUB_EJECUTA "caremesi.run";
     |SI noremesa = 0;
         |HAZ introduc;
     |FINSI;
 |SINO;
    |MENAV "      No hay cobros a remesar ";
 |FINSI;
 |DELINDEX #4;
|FPRC;
|| ***********************************************************************
