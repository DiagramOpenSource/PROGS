|FICHEROS;
     dstez001 #0;
     dstem001 #1;
     dstem002 #2;
     dstem003 #3,MANTE;
     dstem004 #4,MANTE;
     dstem500 #500;
     dstem501 #501;
|FIN;

|VARIABLES;
     aHora        = "";

     nCampo1      = 0;
     nLineaO      = 0;
     nLineaD      = 0;
     nModo        = 0;
     nForma       = 0;
|FIN;

|PROCESO PintaDatosPantalla4;
     |PINTA 0620, #4#4;
     |PINTA 0720, #4#6;
|FPRC;

|PROCESO Tipo7C0F004;  |TIPO 7;
     |ABRE #500;
     |SELECT #2#500;
     #500#2 = "5";
     |LEE 000#500=;
     |SI FSalida ! 0;
         |CIERRA #500;
         |MENAV "0000 Imposible de crear un traspaso, no hay Concepto definido de Traspasos.";
         |PTEC 807;
         |ACABA;
     |FINSI;
     |CIERRA #500;

     |SI #4#0 = "31.12.2999";
         #4#0 = ~;   |PINTA #4#0;
         #4#1 = "";
     |FINSI;
|FPRC;

|PROCESO Tipo7C1F004;  |TIPO 7;
     aHora = #4#1;  |QBF aHora;
     |SI aHora ! "";  |ACABA;  |FINSI;

     |HORA aHora;
     #4#1 = aHora;
|FPRC;

|PROCESO Tipo0C3F004;  |TIPO 0;
     |ABRE #501;
     |SELECT #1#501;
     #501#0 = #4Campo;
     |LEE 000#501=;
     |SI FSalida ! 0;  |DDEFECTO #501;  |FINSI;
     |CIERRA #501;

     nCampo1 = Campo + 1;

     #4nCampo1 = #501#1;

     |HAZ PintaDatosPantalla4;
|FPRC;

|PROCESO Tipo0C5F004;  |TIPO 0;
     |SI #4#3 = #4#5;
         |MENAV "0000 Caja Origen igual que la destino. Cambielas.";
         |ERROR;
     |FINSI;
|FPRC;

|PROCESO Tipo2F004;  |TIPO 2;
     nForma = 0 +. 1;

     |SI #4#8 ! "01.01.0000";
         #1#0 = #4#8;
         |LEE 000#1=;
         |SI FSalida ! 0;  |O #1#2 ! "01.01.0000";
             |SI nForma = 1;
                 |MENAV "0000 El Traspaso esta realizado en unos movimientos ya cerrados.";
             |FINSI;
             |ERROR;
             |ACABA;
         |FINSI;
     |FINSI;

     |LEE 000#1p;
     |SI FSalida ! 0;
         |SI nForma = 1;
             |MENAV "0000 No hay Fecha de Apertura.";
         |FINSI;
         |ERROR;
         |ACABA;
     |FINSI;

     nModo = (FEntrada / 100) ! 0;

     nLineaO = #4#9;
     nLineaD = #4#10;

     |SI nModo = 1;
         nLineaO = 1;
         nLineaD = 1;

         #3#0 = #1#0;
         #3#1 = #4#3;
         #3#2 = 9999;
         |LEE 000#3];
         |SI FSalida = 0;
             |LEE 000#3a;
         |SINO;
             |LEE 000#3u;
         |FINSI;

         |SI FSalida = 0;  |Y #3#0 = #1#0;  |Y #3#1 = #4#3;
             nLineaO = #3#2 + 1;
         |FINSI;

         #3#0 = #1#0;
         #3#1 = #4#5;
         #3#2 = 9999;
         |LEE 000#3];
         |SI FSalida = 0;
             |LEE 000#3a;
         |SINO;
             |LEE 000#3u;
         |FINSI;

         |SI FSalida = 0;  |Y #3#0 = #1#0;  |Y #3#1 = #4#5;
             nLineaD = #3#2 + 1;
         |FINSI;

         |DDEFECTO #3;
         #3#0 = #1#0;
         #3#1 = #4#3;
         #3#2 = nLineaO;
         |GRABA 020#3n;
         |LIBERA #3;

         |DDEFECTO #3;
         #3#0 = #1#0;
         #3#1 = #4#5;
         #3#2 = nLineaD;
         |GRABA 020#3n;
         |LIBERA #3;
     |FINSI;

     |ABRE #501;

     #3#0 = #1#0;
     #3#1 = #4#3;
     #3#2 = nLineaO;
     |LEE 101#3=;
     |SI FSalida = 0;
         |SI nModo = 3;
             |BORRA 020#3a;
             |LIBERA #3;
         |SINO;
             #501#0 = #4#3;
             |LEE 000#501=;
             |SI FSalida ! 0;  |DDEFECTO #501;  |FINSI;

             #3#3  = #500#0;
             #3#4  = #500#1;
             #3#5  = #4#2;
             #3#6  = "5";
             #3#7  = "TRASPASO DE CAJAS / BANCOS ";
             #3#8  = "";
             #3#9  = "";
             #3#10 = "";
             #3#11 = "";
             #3#12 = "";
             #3#13 = #501#2;
             #3#14 = #4#0;
             #3#15 = -#4#7;
             |GRABA 020#3a;
             |LIBERA #3;
         |FINSI;

         #2#0 = #3#0;
         #2#1 = #3#1;
         |LEE 101#2=;
         |SI FSalida = 0;
             #2#3 = #2#3 +. #3#15;
             |GRABA 020#2a;
             |LIBERA #2;
         |FINSI;

         #1#0 = #3#0;
         |LEE 101#1=;
         |SI FSalida = 0;
             #1#1 = #1#1 +. #3#15;
             |GRABA 020#1a;
             |LIBERA #1;
         |FINSI;
     |FINSI;

     #3#0 = #1#0;
     #3#1 = #4#5;
     #3#2 = nLineaD;
     |LEE 101#3=;
     |SI FSalida = 0;
         |SI nModo = 3;
             |BORRA 020#3a;
             |LIBERA #3;
         |SINO;
             #501#0 = #4#5;
             |LEE 000#501=;
             |SI FSalida ! 0;  |DDEFECTO #501;  |FINSI;

             #3#3  = #500#0;
             #3#4  = #500#1;
             #3#5  = #4#2;
             #3#6  = "5";
             #3#7  = "TRASPASO DE CAJAS / BANCOS ";
             #3#8  = "";
             #3#9  = "";
             #3#10 = "";
             #3#11 = "";
             #3#12 = "";
             #3#13 = #501#2;
             #3#14 = #4#0;
             #3#15 = #4#7;
             |GRABA 020#3a;
             |LIBERA #3;
         |FINSI;

         #2#0 = #3#0;
         #2#1 = #3#1;
         |LEE 101#2=;
         |SI FSalida = 0;
             #2#3 = #2#3 +. #3#15;
             |GRABA 020#2a;
             |LIBERA #2;
         |FINSI;

         #1#0 = #3#0;
         |LEE 101#1=;
         |SI FSalida = 0;
             #1#1 = #1#1 +. #3#15;
             |GRABA 020#1a;
             |LIBERA #1;
         |FINSI;
     |FINSI;

     |CIERRA #501;

     #4#8  = #1#0;
     #4#9  = nLineaO;
     #4#10 = nLineaD;
|FPRC;
