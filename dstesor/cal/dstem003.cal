|FICHEROS;
     dstez001 #0;
     dstem001 #1;
     dstem002 #2;
     dstem003 #3;
     dstew006 #6,MANTE;
     dstem500 #500;
     dstem501 #501;

     dstew002 #902;

     :/contagen/def/cacuenta #1100;
     :/dscomer9/def/agifa024 #1101;
     :/dscomer9/def/agifa112 #1102;
     :/dscomer9/def/agifa134 #1103;
     :/dscomer9/def/agifa079 #1104;
     :/contagen/def/caclipro #1105;
     :/dscomer9/def/agifa066 #1166;

     :/contagen/def/canempre #1106;

     dstem003@ #803;     ||CARGA_DEF. movimientos
     caenlac@ #5000;
     Referen@ #5001;
     Refere1@ #5002;
     Refere2@ #5003;
|FIN;

|VARIABLES;
     &fich_alta = "";
     &enPosic = 25;
     &enPantw006 = 0;

     nSwEntra = 0;
     nFactura = 0;
     nTIva    = 0;
     nVent1   = 0;

     aFactura  = "";
     aTipoAnt  = "";
     aMas      = "";
     aAlfa     = "";
     aCuenta   = "";
     aDescCpto = "";
     aSigno    = "";
     aTIva     = "";
     aTAcum    = "";
     aNombre   = "";
     aNif      = "";
     aSerie    = "";

     nTRet   = 0;
     nImpRet = 0;
     nLibro  = 0;
     nNAcum  = 0;
     nDiario = 0;
     fFecha  = "00.00.0000";
     nDocum  = 0;
     nNivel  = 0;
     nCalc   = 0;

     nModo = 0;
     nLinea = 0;
     nCpto  = 0;
     nImporte = 0;

     nCampo = 0;
     aCodCta = "";
     aDirBass = "";

     aMensaje = "";
     nCampoD   = 0;
     nCampoH   = 0;
     nForma    = 0;

     &enPanta0 = 0;
     &enPanta1 = 0;
     &enPanta2 = 0;
     &enPanta3 = 0;
     &enFoco1       = 0;
     &enFoco2       = 0;
     &enFoco3       = 0;
     &enDentro902   = 0;
     &enGrid3       = 0;

     ndigC   = 0;

     aLong  = "";
     nLong  = 0;

     nNume1 = 0;
     nPos   = 0;
     aAlfa1 = "";
     aAlfa2 = "";
     aAlfa3 = "";
     aAlfa4 = "";
     aGuarda = "";
     aPunto  = "";
|FIN;

|PROCESO Clie15;
|FPRC;

|PROCESO Clie16;
|FPRC;

|PROCESO Clie17;
|FPRC;

|PROCESO PintaDatosPantalla3;
     #0#9  = #3#4;  |PINTA #0#9;
     #0#10 = #3#7;  |PINTA #0#10;
     #0#11 = #3#9;  |PINTA #0#11;
|FPRC;

|PROCESO Tipo0C2F003;  |TIPO 0;
     aTipoAnt = #3#6;
     |ABRE #500;
     |SELECT #1#500;
     #500#0 = #3#3;
     |LEE 000#500=;
     |SI FSalida ! 0;  |DDEFECTO #500;  |FINSI;
     |CIERRA #500;

     #3#4 = #500#1;
     #3#6 = #500#2;

     |SI #3#6 = "1";  #3#7 = "COBROS FACTURAS           ";  |FINSI;
     |SI #3#6 = "2";  #3#7 = "PAGOS FACTURAS            ";  |FINSI;
     |SI #3#6 = "3";  #3#7 = "COBROS VARIOS             ";  |FINSI;
     |SI #3#6 = "4";  #3#7 = "PAGOS VARIOS              ";  |FINSI;
     |SI #3#6 = "5";  #3#7 = "TRASPASO DE CAJAS / BANCOS";  |FINSI;
     |SI #3#6 = "6";  #3#7 = "SALDO ANTERIOR APERTURA   ";  |FINSI;

     |SI #3#6 ! "1"; |Y #3#6 ! "2"; |Y #3#6 ! "3"; |Y #3#6 ! "4";
         #3#13 = #500#3;  |PINTA #3#13;
     |FINSI;
     nModo = (FEntrada / 100) ! 0;
     |SI #500#4 = "S";
        #3#11 = #500#23; |PINTA #3#11;
        |SI #500#24 = "S";
           |C_M #3#12, 1, "N";
        |SINO;
           |SI nModo = 1;
              |C_M #3#12, 1, "S";
           |SINO;
              |C_M #3#12, 1, "N";
           |FINSI;
        |FINSI;
     |FINSI;

     |HAZ PintaDatosPantalla3;
     |SI aTipoAnt = ""; aTipoAnt = #3#6; |FINSI;
|FPRC;

|PROCESO Tipo0C5F003;  |TIPO 0;
     |C_M #3#8,  1, "S";
     |C_M #3#10, 1, "S";
     |C_M #3#11, 1, "S";
     |C_M #3#12, 1, "S";

     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 1; |O #3#6 ! aTipoAnt;
        |SI #3#6 ! "1"; |Y #3#6 ! "2";
           |C_M #3#8,  1, "N";  #3#8  = "";  |PINTA #3#8;
                                #3#9  = "";  |PINTA #3#9;
           |C_M #3#10, 1, "N";  #3#10 = "";  |PINTA #3#10;
           |C_M #3#11, 1, "N";  #3#11 = "";  |PINTA #3#11;
           |C_M #3#12, 1, "N";  #3#12 = 0;   |PINTA #3#12;
        |FINSI;
        |SI #3#6 = "3"; |O #3#6 = "4";
           |C_M #3#11, 1, "S"; |C_M #3#12, 1, "S";
        |FINSI;
        |SI #500#4 = "S";
           #3#11 = #500#23; |PINTA #3#11;
           |SI #500#24 = "S";
              |C_M #3#12, 1, "N";
           |SINO;
              |C_M #3#12, 1, "S";
           |FINSI;
        |FINSI;
     |FINSI;
     |SI nModo = 2;
        |C_M #3#11, 1, "N"; |PINTA #3#11;
        |C_M #3#12, 1, "N"; |PINTA #3#12;
     |FINSI;
|FPRC;

|PROCESO Tipo66C8F003;  |TIPO 66;
     |SI #3#6 = "1";
         |ABRE #1101;
         #1101#0 = #3#8;
         |LEE 000#1101];

         |CONSULTA_CLAVE #1#1101;
         |SI FSalida = 0;
             #3#8 = #1101#0;  |PINTA #3#8;
             #3#9 = #1101#1;
         |FINSI;
         |CIERRA #1101;
     |FINSI;

     |SI #3#6 = "2";
         |ABRE #1102;
         #1102#0 = #3#8;
         |LEE 000#1102];

         |CONSULTA_CLAVE #1#1102;
         |SI FSalida = 0;
             #3#8 = #1102#0;  |PINTA #3#8;
             #3#9 = #1101#1;
         |FINSI;
         |CIERRA #1102;
     |FINSI;
|FPRC;

|PROCESO Tipo0C8F003;  |TIPO 0;
     |SI #3#6 ! "1";  |Y #3#6 ! "2";
         |ACABA;
     |FINSI;

     |SI #3#6 = "1";
         |ABRE #1101;
         #1101#0 = #3#8;
         |LEE 000#1101=;
         |SI FSalida ! 0;
             |DDEFECTO #1101;
             |MENAV "0000 Cliente Inexistente.";
             |ERROR;

             #3#9 = "";
         |SINO;
             #3#9  = #1101#1;
             #3#13 = #1101#16;   |PINTA #3#13;
         |FINSI;
         |CIERRA #1101;
     |FINSI;

     |SI #3#6 = "2";
         |ABRE #1102;
         #1102#0 = #3#8;
         |LEE 000#1102=;
         |SI FSalida ! 0;
             |DDEFECTO #1102;
             |MENAV "0000 Cliente Inexistente.";
             |ERROR;

             #3#9 = "";
         |SINO;
             #3#9  = #1102#1;
             #3#13 = #1102#10;   |PINTA #3#13;
         |FINSI;
         |CIERRA #1102;
     |FINSI;

     |HAZ PintaDatosPantalla3;
|FPRC;

|PROCESO Tipo0C10F003;  |TIPO 0;
     |SI #3#6 ! "1";  |Y #3#6 ! "2";
         |ACABA;
     |FINSI;

     |C_M #3#11, 1, "S";
     |C_M #3#12, 1, "S";

     |SI #3#10 = "P";
         |C_M #3#11, 1, "N";  #3#11  = "";   |PINTA #3#11;
         |C_M #3#12, 1, "N";  #3#12  = 0;    |PINTA #3#12;
     |FINSI;
|FPRC;

|PROCESO Baja1103;
     #1103#2  = #3#8;
     #1103#1  = "01.01.0000";
     #1103#49 = "";
     #1103#0  = 1;
|FPRC;

|PROCESO Alta1103;
     #1103#2  = #3#8;
     #1103#1  = "31.12.2999";
     #1103#49 = "zzzzz";
     #1103#0  = 999999;
|FPRC;

|PROCESO Baja1104;
     #1104#2  = #3#8;
     #1104#3  = "";
|FPRC;

|PROCESO Alta1104;
     #1104#2  = #3#8;
     #1104#3  = "z" * 60;
|FPRC;

|PROCESO Tipo66C11F003;  |TIPO 66;
     |SI #3#6 = "1";
         |ABRE #1103;
         #1103#49 = #3#11;
         #1103#0  = #3#0;
         |SELECT #1#1103;
         |LEE 000#1103];

         |CONSULTA_F_CLAVE #3#1103, Baja1103, Alta1103;
         |SI FSalida = 0;
             #3#11 = #1103#49;  |PINTA #3#11;
             #3#12 = #1103#0;
         |FINSI;
         |CIERRA #1103;
     |FINSI;

     |SI #3#6 = "2";
         |ABRE #1104;
         #1104#66 = #3#11;
         #1104#0  = #3#0;
         |SELECT #1#1104;
         |LEE 000#1104];

         |CONSULTA_F_CLAVE #2#1104, Baja1104, Alta1104;
         |SI FSalida = 0;
             #3#11 = #1104#66;  |PINTA #3#11;
             #3#12 = #1104#0;
         |FINSI;
         |CIERRA #1104;
     |FINSI;

     |ERROR;
|FPRC;

|PROCESO Tipo0C12F003;  |TIPO 0;
     nModo = (FEntrada / 100) ! 0;
     |SI #3#6 = "3"; |O #3#6 = "4"; |Y nModo = 1;
         |DBASS aAlfa; |QBF aAlfa;
         aAlfa = aAlfa + "contagen/def/camaniva.mas";
         |CARGA_DEF aAlfa, Referen@;
         |SI FSalida = 0;
            |DBASS aAlfa; |QBF aAlfa;
            aAlfa = aAlfa + "modelos/def/dsmom030.mas";
            |CARGA_DEF aAlfa, Refere1@;
            |SI FSalida = 0;
               |ABRE #500;
               |SELECT #1#500;
               #500#0 = #3#3;
               |LEE 000#500=;
               |SI FSalida ! 0;  |DDEFECTO #500;  |FINSI;
               |CIERRA #500;

               |DBASS aAlfa; |QBF aAlfa;
               aAlfa = aAlfa + "contagen/fich/" + (("00000" + #48#2) % -105) + #48#3 + "/";
               |PATH_DAT #Referen@#aAlfa#;
               |ABRE #Referen@; |ABRE #Refere1@;
               |SELECT #3#Referen@;
               #Refere1@#0 = #dstem500#21;
               |LEE 000#Refere1@.=;
               |SI FSalida = 0;
                  |SI #Refere1@#4 = "S";
                     |SI #3#6 = "3";
                        #Referen@#0 = #Refere1@#12;      || libro repercutido
                     |SINO;
                        #Referen@#0 = #Refere1@#14;      || libro soportado
                     |FINSI;
                  |FINSI;
               |FINSI;
               |CIERRA #Refere1@; |DESCARGA_DEF #Refere1@;
            |FINSI;
            #Referen@#2 = #dstem003#11;
            #Referen@#3 = #dstem003#12;
            |LEE 000#Referen@.=;
            |SI FSalida = 0;
               |MENAV "    Factura ya existente en contabilidad"; |ERROR;
            |FINSI;
            |SELECT #1#Referen@;
            |CIERRA #Referen@; |DESCARGA_DEF #Referen@;
         |FINSI;
     |FINSI;

     |SI #3#10 ! "T";  |ACABA;  |FINSI;

     |SI #3#6 = "1";
         |ABRE #1103;
         #1103#49 = #3#11;
         #1103#0  = #3#12;
         |SELECT #1#1103;
         |LEE 000#1103=;
         |SI FSalida ! 0;  |O #1103#2 ! #3#8;
             |MENAV "0000 Factura Inexistente.";
             |ERROR;

             #3#15 = 0;
         |SINO;
             #3#15 = #1103#101;
         |FINSI;
         |PINTA #3#15;

         |CIERRA #1103;
     |FINSI;

     |SI #3#6 = "2";
         |ABRE #1104;
         #1104#66 = #3#11;
         #1104#0  = #3#12;
         |SELECT #1#1104;
         |LEE 000#1104=;
         |SI FSalida ! 0;  |O #1104#2 ! #3#8;
             |MENAV "0000 Factura Inexistente.";
             |ERROR;

             #3#15 = 0;
         |SINO;
             #3#15 = #1104#118;
         |FINSI;
         |PINTA #3#15;

         |CIERRA #1104;
     |FINSI;
|FPRC;

|PROCESO FiltraPtos;
    |ABRE #1106;
    |DDEFECTO #1106;
    #1106#2 = #48#2;
    #1106#3 = #48#3;
    |LEE 000#1106.=;
    |SI FSalida = 0;
         nCampo = 13 + #1106#13;   ||Empieza a partir del Campo 14
         ndigC = #1106#nCampo#;
    |SINO;
         ndigC = 12;
    |FINSI;
    |CIERRA #1106;

    |SI aCodCta = "            "; |ACABA; |FINSI;

    aAlfa1 = "";
    |PARA nNume1 = 1; |SI nNume1 [ 12; |HACIENDO nNume1 = nNume1 + 1;
          aAlfa2 = nNume1; |QBF aAlfa2;
          aAlfa2 = aAlfa2 + "01";
          aAlfa3 = aCodCta % aAlfa2;
          |SI aAlfa3 = "."; |O aAlfa3 = "0"; |O aAlfa3 = "1"; |O aAlfa3 = "2";
                            |O aAlfa3 = "3"; |O aAlfa3 = "4"; |O aAlfa3 = "5";
                            |O aAlfa3 = "6"; |O aAlfa3 = "7"; |O aAlfa3 = "8";
                            |O aAlfa3 = "9";
              aAlfa1 = aAlfa1 + aAlfa3;
          |FINSI;
    |SIGUE;
    aCodCta = aAlfa1;

    aPunto = aCodCta - ".";
    |SI FSalida ! 0;
        nNume1 = FSalida;
        nPos   = 100 + (((nNume1 / 100) ! 0) -1);
        aAlfa2 = aCodCta % nPos;   |QBF aAlfa2;
        nPos   = (((nNume1 / 100) ! 0) * 100) + 199;
        aAlfa3 = aCodCta % nPos;   |QBF aAlfa3;

        FSalida = nNume1;
        |PARA; |SI FSalida ! 0; |HACIENDO;
               aAlfa3 = aAlfa3 - "."; || limpiar mas puntos (los ignoramos)
        |SIGUE;

        nPos   = ndigC - ( (A \ aAlfa2 % 0) + (A \ aAlfa3 % 0));
        aAlfa4 = (A\ "0" * nPos);
        aCodCta  = (A\ aAlfa2 + aAlfa4 + aAlfa3);
    |FINSI;
|FPRC;

|PROCESO Tipo0C13F003;  |TIPO 0;
     aCodCta = #3#13;
     |HAZ FiltraPtos;
     #3#13 = aCodCta;
     |PINTA #3#13;

     |ABRE #1100;
     #1100#0 = #3#13;
     |LEE 000#1100=;
     |SI FSalida ! 0;
         |MENAV "0000 Cuenta Contable Inexistente.";
         |ERROR;
     |SINO;
         |SI #1100#3 ! "S";
             |MENAV "0000 Debe ser una cuenta de Pase Directo.";
             |ERROR;
         |FINSI;
     |FINSI;
     |CIERRA #1100;

     |ABRE #1105;
     |SI #dstem500#6 = "3";
        #1105#23 = "C";
     |SINO;
        #1105#23 = "P";
     |FINSI;
     #1105#10 = #3#13;
     |LEE 000#1105=;
     |SI FSalida = 0;
        aNombre = #1105#1; aNif = #1105#9;
     |FINSI;
     |CIERRA #1105;
|FPRC;

|PROCESO Tipo7C15F003;  |TIPO 7;
     |C_M #3#15, 1, "S";

     |SI #500#4 ! "S";  |ACABA;  |FINSI;

     |C_M #3#15, 1, "N";

     enDentro902 = 1;
     |BORRA_HIJOS_PAN #3#0;
     |SUB_PINPA #0#902, enPanta3;

     |PARA nCampoD = 0;  |SI nCampoD [ 35;  |HACIENDO nCampoD = nCampoD + 1;
           nCampoH     = nCampoD + 15;
           #902nCampoD = #3nCampoH;
     |SIGUE;
     #902#36 = #3#52;

     |SI #902#1 = 0; |Y #902#7 = 0; |Y #902#13 = 0; |Y #902#19 = 0; |Y #902#25 = 0;
          |HAZ TraeIvasDscomer9;
     |FINSI;

     |PINDA #0#902;
     |ENDATOS #1#902;
     |SI FSalida = 0;
         |PARA nCampoD = 0;  |SI nCampoD [ 35;  |HACIENDO nCampoD = nCampoD + 1;
               nCampoH    = nCampoD + 15;
               #3nCampoH  = #902nCampoD;
         |SIGUE;
         #3#52 = #902#36;
     |FINSI;

     |BORRA_HIJOS_PAN #3#0;

     |PINTA #3#15;
     enDentro902 = 0;

     |FOCOTECLADO enGrid3;
|FPRC;

|PROCESO Tipo2m003;  |TIPO 2;
     nForma = 0 +. 1;
     |SI #3#6 = "5";
         |SI nForma = 1;
             |MENAV "0000 El Registro es un Traspaso, use el lineal de traspasos.";
         |FINSI;
         |ERROR;
         |ACABA;
     |FINSI;

     |SI #3#6 = "6";
         |SI nForma = 1;
             |MENAV "0000 El Registro es un Saldo Anterior del Cierre, no se puede modificar";
         |FINSI;
         |ERROR;
         |ACABA;
     |FINSI;

     |SI #3#51 = "E"; |O #3#51 = "L";
         |SI nForma = 1;
             aMensaje = "0000El Registro viene del Expediente [" + #3#11 + "/" + #3#12 + "], no se puede modificar";
             |MENAV aMensaje;
         |FINSI;
         |ERROR;
         |ACABA;
     |FINSI;

     |SI #3#51 = "M";
         |SI nForma = 1;
             aMensaje = "0000El Registro viene de la Proforma [" + #3#11 + "/" + #3#12 + "], no se puede modificar";
             |MENAV aMensaje;
         |FINSI;
         |ERROR;
         |ACABA;
     |FINSI;

     |SI #3#6 = "1";  |O #3#6 = "3";  |O #3#6 = "5";  |O #3#6 = "6";
         #0#2 = #0#2 +. #3#15;
         #0#5 = #0#5 +. #3#15;  |PINTA #0#5;
         #0#8 = #0#8 +. #3#15;  |PINTA #0#8;
     |SINO;
         #0#2 = #0#2 -. #3#15;
         #0#5 = #0#5 -. #3#15;  |PINTA #0#5;
         #0#8 = #0#8 -. #3#15;  |PINTA #0#8;
     |FINSI;

     #1#0 = #3#0;
     |LEE 101#1=;
     |SI FSalida = 0;
         #1#1 = #0#2;
         |GRABA 020#1a;
         |LIBERA #1;
     |FINSI;

     #2#0 = #3#0;
     #2#1 = #3#1;
     |LEE 101#2=;
     |SI FSalida = 0;
         #2#3 = #0#8;
         |GRABA 020#2a;
         |LIBERA #2;
     |FINSI;

     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 1; |Y #dstem003#55 ! 0; nModo = 2; |FINSI;

     nSwEntra = 0;
     |SI nModo = 1; |Y nForma = 1;  nSwEntra = 1; |FINSI;
     |SI nModo = 2; |Y nForma = 1;  nSwEntra = 1; |FINSI;
     |SI nModo = 3; |Y nForma = -1; nSwEntra = 1; |FINSI;

     |SI nSwEntra = 1;
        |SI nModo = 2; |O nModo = 3;
           |SI #dstem003#55 ! 0;
              nDiario = #dstem003#53; fFecha = #dstem003#54; nDocum  = #dstem003#55;
              |HAZ Enlace;
           |FINSI;
        |FINSI;
        |SI nModo = 1;
           nDiario = 0; fFecha = "00.00.0000"; nDocum = 0; |HAZ Enlace;
           #dstem003#53 = nDiario; #dstem003#54 = fFecha; #dstem003#55 = nDocum;
        |FINSI;
     |FINSI;
|FPRC;

|PROCESO Baja1100;
     #1100#3 = "S";
     #1100#0 = "4         ";
|FPRC;

|PROCESO Alta1100;
     #1100#3 = "S";
     #1100#0 = "799999999999999";
|FPRC;

|PROCESO Tipo66C13F003; |TIPO 66;
     |ABRE #1100;
     |CONSULTA_F_CLAVE #6#1100, Baja1100, Alta1100;
     |SI FSalida = 0;
          #dstem003#13 = #1100#0;
          |PINTA #dstem003#13;
     |FINSI;
     |CIERRA #1100;
|FPRC;

|PROCESO Tipo7m003; |TIPO 7;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 1;
          nLinea = 0;
          |HAZ UltimaLinea;
          #dstem003#2 = nLinea;
          |PINTA #dstem003#2;
     |FINSI;
|FPRC;

|PROCESO UltimaLinea;
     |DBASE aAlfa; |QBF aAlfa;

     aMas = aAlfa + "def/dstem003.mas";
     |CARGA_DEF aMas, dstem003@;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |ABRE #803;

     #803#0 = #dstem003#0;
     #803#1 = #dstem003#1;
     #803#2 = 999;
     |LEE 000#803];
     |SI FSalida = 0;
           |LEE 000#803a;
     |SINO;
           |LEE 000#803u;
     |FINSI;
     |SI FSalida = 0; |Y #803#0 = #dstem003#0; |Y #803#1 = #dstem003#1;
          nLinea = #803#2 + 1;
     |SINO;
          nLinea = 1;
     |FINSI;

     |CIERRA #803;

     |DESCARGA_DEF #dstem003@;
|FPRC;

|PROCESO TiposIVA;
     nCampo = #1166#0 * 6;              || Son grupos de 6 campos
     nCampo = nCampo - 2;               || - 2
     #902#nCampo# = #1166#3;
     nCampo = nCampo - 2;               || -2 + -2 = -4
     #902#nCampo# = #1166#2;
|FPRC;

|DEFBUCLE TiposIVA;
 #1166#1;
 ;
 1;
 5;
 ;
 NULL, TiposIVA;
|FIN;

|PROCESO TraeIvasDscomer9;
     |BUCLE TiposIVA;
|FPRC;

|PROCESO Enlace;
   |DBASS aAlfa; |QBF aAlfa;
   aAlfa = aAlfa + "contagen/def/caenlace.mas";
   |CARGA_DEF aAlfa, caenlac@;
   |SI FSalida = 0;
      aAlfa = "pu" + Usuario; |NOME_DAT #caenlac@#aAlfa#;
      |DBASS aAlfa; |QBF aAlfa;
      aAlfa = aAlfa + "contagen/fich/" + (("00000" + #48#2) % -105) + #48#3 + "/";
      |PATH_DAT #caenlac@#aAlfa#;
      |DELINDEX #caenlac@; |ABRE #caenlac@;
      |SI nModo = 1;
         |HAZ NuevoAsto;
         nLinea = 1; |HAZ AltaAsto;
      |FINSI;
      |SI nModo = 2;
         |HAZ BajaAsto;
         nLinea = 1; |HAZ AltaAsto;
      |FINSI;
      |SI nModo = 3;
         |HAZ BajaAsto;
      |FINSI;
      |CIERRA #caenlac@;

      |DBASS aAlfa; |QBF aAlfa; aAlfa = aAlfa + "contagen/fich/";
      aAlfa = aAlfa + (("00000" + #48#2) % -105) + #48#3 + "/"; fich_alta = aAlfa;
      aAlfa = aAlfa + ",pu" + Usuario;
      aAlfa = ":contagen/dsapunte.tab;" + aAlfa;
      |SUB_EJECUTA_NP aAlfa;
   |FINSI;
|FPRC;

|PROCESO NuevoAsto;
   |DBASS aDirBass; |QBF aDirBass;
   aDirBass = aDirBass + "contagen/";
   aAlfa = aDirBass + "def/caconasi.mas";
   |CARGA_DEF aAlfa, Referen@;
   |SI FSalida = 0;
      aAlfa = aDirBass + "fich/" + (("00000" + #48#2) % -105) + #48#3 + "/"; |PATH_DAT #Referen@#aAlfa#;
      |ABRE #Referen@;
      |LEE 101#Referen@.p;
      |SI FSalida ! 0;
         |DDEFECTO #Referen@;
         |GRABA 020#Referen@.b;
      |FINSI;
      nDocum = #Referen@#1; #Referen@#1 = #Referen@#1 + 1;
      |GRABA 020#Referen@.a; |LIBERA #Referen@;
      |CIERRA #Referen@; |DESCARGA_DEF #Referen@;
      fFecha = #dstem003#14;

      |ABRE #dstem500;
      #dstem500#0 = #dstem003#3;
      |LEE 000#dstem500.=;
      |SI FSalida = 0; nDiario = #dstem500#5; |FINSI;
      |CIERRA #dstem500;
   |FINSI;
|FPRC;

|PROCESO BajaAsto;
   |DDEFECTO #caenlac@;
   #caenlac@#37 = #48#2;
   #caenlac@#38 = #48#3;
   #caenlac@#0 = nDiario;
   #caenlac@#1 = fFecha;
   #caenlac@#2 = nDocum;
   #caenlac@#3 = 0;
   #caenlac@#9 = 0;
   #caenlac@#25 = "S";
   |GRABA 020#caenlac@.c;
|FPRC;

|PROCESO AltaAsto;
   |ABRE #dstem501;
   #dstem501#0 = #dstem003#1;
   |LEE 000#dstem501.=;
   |SI FSalida ! 0; |DDEFECTO #dstem501; |FINSI;
   |CIERRA #dstem501;

   |ABRE #dstem500;
   #dstem500#0 = #dstem003#3;
   |LEE 000#dstem500.=;
   |SI FSalida = 0;
      |SI #dstem500#2 = 1; |O #dstem500#2 = 2; |O #dstem500#2 = 3; |O #dstem500#2 = 4;
         |SI #dstem500#2 = 1; |O #dstem500#2 = 3; aSigno = "D"; |SINO; aSigno = "H"; |FINSI;
         aCuenta = #dstem501#2;  nCpto = #dstem500#6; aDescCpto = #dstem500#13;
         aFactura = ("000000" + nFactura) % -106;
         nImporte = #dstem003#15;
         |SI #dstem500#27 = "S"; |QBF aDescCpto; aDescCpto = aDescCpto + ((aSerie + "     ") % 0105) + "/";    |FINSI;
         |SI #dstem500#28 = "S"; |QBF aDescCpto; aDescCpto = aDescCpto + aFactura + "/"; |FINSI;
         |SI #dstem500#26 = "S"; |QBF aDescCpto; aDescCpto = aDescCpto + fFecha + "/";   |FINSI;
         |HAZ AltaLinAsto;
         aCuenta = #dstem003#13; nCpto = #dstem500#6; aDescCpto = #dstem500#13;
         |SI #dstem500#27 = "S"; |QBF aDescCpto; aDescCpto = aDescCpto + ((aSerie + "     ") % 0105) + "/";    |FINSI;
         |SI #dstem500#28 = "S"; |QBF aDescCpto; aDescCpto = aDescCpto + aFactura + "/"; |FINSI;
         |SI #dstem500#26 = "S"; |QBF aDescCpto; aDescCpto = aDescCpto + fFecha + "/";   |FINSI;
         |SI #dstem500#2 = 1; |O #dstem500#2 = 3; aSigno = "H"; |SINO; aSigno = "D"; |FINSI;
         nImporte = #dstem003#15; |HAZ AltaLinAsto;
      |FINSI;
/*
      |SI #dstem500#2 = 1; |O #dstem500#2 = 2;
         nTRet   = 0; nImpRet = 0;
         |DBASS aAlfa; |QBF aAlfa;
         aAlfa = aAlfa + "modelos/def/dsmom030.mas";
         |CARGA_DEF aAlfa, Referen@;
         |SI FSalida = 0;
            |ABRE #Referen@;
            #Referen@#0 = #dstem500#21;
            |LEE 000#Referen@.=;
            |SI FSalida = 0;
               |SI aTIva = "R";
                  nLibro = #Referen@#12;      || libro repercutido
               |SINO;
                  nLibro = #Referen@#14;      || libro soportado
               |FINSI;
            |FINSI;
            |CIERRA #Referen@; |DESCARGA_DEF #Referen@;
         |FINSI;
         |SI #dstem500#2 = 3; aSigno = "D"; |SINO; aSigno = "H"; |FINSI;
         |SI #dstem500#2 = 3; aTIva  = "R"; |SINO; aTIva  = "S"; |FINSI;
         aSerie = #dstem003#11; nFactura = #dstem003#12;
         aCuenta = #dstem003#13;  nCpto = #dstem500#21; aDescCpto = #dstem500#22;
         |SI #dstem500#30 = "S"; |QBF aDescCpto; aDescCpto = aDescCpto + ((aSerie + "     ") % 0105) + "/";    |FINSI;
         |SI #dstem500#31 = "S"; |QBF aDescCpto; aDescCpto = aDescCpto + aFactura + "/"; |FINSI;
         |SI #dstem500#29 = "S"; |QBF aDescCpto; aDescCpto = aDescCpto + fFecha + "/";   |FINSI;
         nImporte = #dstem003#15; aTAcum = "F"; nNAcum = 0; |HAZ AltaLinAsto;

         aCuenta = #dstem500#7;
         |SI #dstem500#2 = 3; aSigno = "H"; |SINO; aSigno = "D"; |FINSI;
         nImporte = #dstem003#18 + #dstem003#20; aTAcum = "I"; nNAcum = 1;
         |SI nImporte ! 0; |HAZ AltaLinAsto; |FINSI;

         aCuenta = #dstem500#8;
         |SI #dstem500#2 = 3; aSigno = "H"; |SINO; aSigno = "D"; |FINSI;
         nImporte = #dstem003#24 + #dstem003#26; aTAcum = "I"; nNAcum = 2;
         |SI nImporte ! 0; |HAZ AltaLinAsto; |FINSI;

         aCuenta = #dstem500#9;
         |SI #dstem500#2 = 3; aSigno = "H"; |SINO; aSigno = "D"; |FINSI;
         nImporte = #dstem003#30 + #dstem003#32; aTAcum = "I"; nNAcum = 3;
         |SI nImporte ! 0; |HAZ AltaLinAsto; |FINSI;

         aCuenta = #dstem500#10;
         |SI #dstem500#2 = 3; aSigno = "H"; |SINO; aSigno = "D"; |FINSI;
         nImporte = #dstem003#36 + #dstem003#38; aTAcum = "I"; nNAcum = 4;
         |SI nImporte ! 0; |HAZ AltaLinAsto; |FINSI;

         aCuenta = #dstem500#11;
         |SI #dstem500#2 = 3; aSigno = "H"; |SINO; aSigno = "D"; |FINSI;
         nImporte = #dstem003#42 + #dstem003#44; aTAcum = "I"; nNAcum = 5;
         |SI nImporte ! 0; |HAZ AltaLinAsto; |FINSI;

         aCuenta = #dstem500#14; nTIva = #dstem003#17;
         |SI #dstem500#2 = 3; aSigno = "H"; |SINO; aSigno = "D"; |FINSI;
         nImporte = #dstem003#16; aTAcum = "B"; nNAcum = 1;
         |SI nImporte ! 0; |HAZ AltaLinAsto; |FINSI;

         aCuenta = #dstem500#15; nTIva = #dstem003#23;
         |SI #dstem500#2 = 3; aSigno = "H"; |SINO; aSigno = "D"; |FINSI;
         nImporte = #dstem003#22; aTAcum = "B"; nNAcum = 2;
         |SI nImporte ! 0; |HAZ AltaLinAsto; |FINSI;

         aCuenta = #dstem500#16; nTIva = #dstem003#29;
         |SI #dstem500#2 = 3; aSigno = "H"; |SINO; aSigno = "D"; |FINSI;
         nImporte = #dstem003#28; aTAcum = "B"; nNAcum = 3;
         |SI nImporte ! 0; |HAZ AltaLinAsto; |FINSI;

         aCuenta = #dstem500#17; nTIva = #dstem003#35;
         |SI #dstem500#2 = 3; aSigno = "H"; |SINO; aSigno = "D"; |FINSI;
         nImporte = #dstem003#34; aTAcum = "B"; nNAcum = 4;
         |SI nImporte ! 0; |HAZ AltaLinAsto; |FINSI;

         aCuenta = #dstem500#18;  nTIva = #dstem003#41;
         |SI #dstem500#2 = 3; aSigno = "H"; |SINO; aSigno = "D"; |FINSI;
         nImporte = #dstem003#40; aTAcum = "B"; nNAcum = 5;
         |SI nImporte ! 0; |HAZ AltaLinAsto; |FINSI;

         aCuenta = #dstem500#19;  nTIva = 0;
         |SI #dstem500#2 = 3; aSigno = "H"; |SINO; aSigno = "D"; |FINSI;
         nImporte = #dstem003#46; aTAcum = "B"; nNAcum = 99;
         |SI nImporte ! 0; |HAZ AltaLinAsto; |FINSI;

         aCuenta = #dstem500#20;
         |SI #dstem500#2 = 3; aSigno = "D"; |SINO; aSigno = "H"; |FINSI;
         nImporte = #dstem003#50; aTAcum = "P"; nNAcum = 0;
         nTRet   = #dstem003#48; nImpRet = #dstem003#49;
         |SI nImporte ! 0; |HAZ AltaLinAsto; |FINSI;
         nTRet   = 0; nImpRet = 0;
      |FINSI;
*/
   |FINSI;
   |CIERRA #dstem500;
|FPRC;

|PROCESO AltaLinAsto;
   |DDEFECTO #caenlac@;
   #caenlac@#37 = #48#2;
   #caenlac@#38 = #48#3;
   #caenlac@#0 = nDiario;
   #caenlac@#1 = fFecha;
   #caenlac@#2 = nDocum;
   #caenlac@#3 = nLinea; nLinea = nLinea + 1;
   #caenlac@#4 = aCuenta; nNivel = 0; |HAZ SacaNivel;
   #caenlac@#5 = nNivel;
   #caenlac@#6 = nCpto;      ||concepto
   #caenlac@#7 = aDescCpto;  ||descripcion concepto
   #caenlac@#8 = aSigno;     ||signo
   #caenlac@#9 = nImporte;   ||importe
   #caenlac@#13 = 0;         ||Factura
   |SI aTIva ! "";
      #caenlac@#10 = aTIva;     ||Soportado/repercutido
      #caenlac@#20 = "N";
      #caenlac@#26 = aTAcum;    ||Tipo acumulador
      #caenlac@#27 = nNAcum;    ||Numero acumulador
      #caenlac@#28 = nTIva;     ||Tipo de iva
      #caenlac@#29 = #caenlac@#1;
      #caenlac@#11 = nLibro;    ||Libro
      #caenlac@#12 = aSerie;    ||Serie
      #caenlac@#13 = nFactura;  ||Factura
      #caenlac@#41 = nTRet;
      #caenlac@#42 = nImpRet;
      #caenlac@#43 = aNombre;
      #caenlac@#44 = aNif;
   |FINSI;

   |SI nImporte < 0;
        |SI #caenlac@#8 = "D";        ||cambio el signo del apunte
             #caenlac@#8 = "H";       ||cuando el importe sea negativo
        |SINO;
             #caenlac@#8 = "D";
        |FINSI;
        #caenlac@#9 = -#caenlac@#9;        ||Cambio el signo del importe
   |FINSI;
   |GRABA 021#caenlac@.c; |LIBERA #caenlac@;
|FPRC;

|PROCESO SacaNivel;
   |QBF aCuenta; aAlfa = aCuenta % 0; nCalc = aAlfa;

   |DBASS aDirBass; |QBF aDirBass;
   aDirBass = aDirBass + "contagen/";
   aAlfa = aDirBass + "def/canempre.mas";
   |CARGA_DEF aAlfa, Referen@;
   |SI FSalida = 0;
      aAlfa = aDirBass + "fich/"; |PATH_DAT #Referen@#aAlfa#;
      |ABRE #Referen@;
      #Referen@#2 = #48#2;
      #Referen@#3 = #48#3;
      |LEE 000#Referen@.=;
      |SI FSalida = 0;
         nNivel = 0;
         |SI #Referen@#14 = nCalc; nNivel = 1; |FINSI;
         |SI #Referen@#15 = nCalc; nNivel = 2; |FINSI;
         |SI #Referen@#16 = nCalc; nNivel = 3; |FINSI;
         |SI #Referen@#17 = nCalc; nNivel = 4; |FINSI;
         |SI #Referen@#18 = nCalc; nNivel = 5; |FINSI;
         |SI #Referen@#19 = nCalc; nNivel = 6; |FINSI;
      |FINSI;
      |CIERRA #Referen@; |DESCARGA_DEF #Referen@;
   |FINSI;
|FPRC;

|PROCESO SacaCont;
   |SI FSalida = 2; |ACABA; |FINSI;
   nModo = (FEntrada / 100) ! 0;
   |SI nModo ! 1; |ACABA; |FINSI;
   |DBASS aAlfa; |QBF aAlfa;
   aAlfa = aAlfa + "contagen/def/caivaasi.mas";
   |CARGA_DEF aAlfa, Referen@;
   |SI FSalida = 0;
      |DBASS aAlfa; |QBF aAlfa;
      aAlfa = aAlfa + "contagen/fich/" + (("00000" + #48#2) % -105) + #48#3 + "/";
      |PATH_DAT #Referen@#aAlfa#;
      |ABRE #Referen@;
      |LEE 000#Referen@.p;
      |SI FSalida ! 0; |DDEFECTO #Referen@; |FINSI;
      |SI #Referen@#44 = "S";
         |DBASS aAlfa; |QBF aAlfa;
         aAlfa = aAlfa + "contagen/def/caivases.mas";
         |CARGA_DEF aAlfa, Refere1@;
         |SI FSalida = 0;
            |DBASS aAlfa; |QBF aAlfa;
            aAlfa = aAlfa + "contagen/fich/" + (("00000" + #48#2) % -105) + #48#3 + "/";
            |PATH_DAT #Refere1@#aAlfa#;
            |ABRE #Refere1@;
            #Refere1@#0 = #dstem003#11;
            |SI #dstem500#2 = 1; |O #dstem500#2 = 3; #Refere1@#1 = "R"; |FINSI;
            |SI #dstem500#2 = 2; |O #dstem500#2 = 4; #Refere1@#1 = "S"; |FINSI;
            |LEE 101#Refere1@.=;
            |SI FSalida = 0;
               #dstem003#12 = #Refere1@#3; |PINTA #dstem003#12;
            |FINSI;
            |CIERRA #Refere1@; |DESCARGA_DEF #Refere1@;
         |FINSI;
      |SINO;
         |DBASS aAlfa; |QBF aAlfa;
         aAlfa = aAlfa + "contagen/def/calibros.mas";
         |CARGA_DEF aAlfa, Refere1@;
         |SI FSalida = 0;
            |DBASS aAlfa; |QBF aAlfa;
            aAlfa = aAlfa + "modelos/def/dsmom030.mas";
            |CARGA_DEF aAlfa, Refere2@;
            |SI FSalida = 0;
               |DBASS aAlfa; |QBF aAlfa;
               aAlfa = aAlfa + "contagen/fich/" + (("00000" + #48#2) % -105) + #48#3 + "/";
               |PATH_DAT #Refere1@#aAlfa#; |ABRE #Refere1@;
               |ABRE #Refere2@;
               #Refere2@#0 = #dstem500#21;
               |LEE 000#Refere2@.=;
               |SI FSalida ! 0; |DDEFECTO #Refere2@; |FINSI;

               |SI #dstem500#6 = "3";
                  #Refere1@#0 = #Refere2@#12;
               |SINO;
                  #Refere1@#0 = #Refere2@#14;
               |FINSI;
               |LEE 101#Refere1@.=;
               |SI FSalida = 0;
                  #dstem003#12 = #Refere1@#4;
               |FINSI;
               |CIERRA #Refere2@; |DESCARGA_DEF #Refere2@;
            |FINSI;
            |CIERRA #Refere1@; |DESCARGA_DEF #Refere1@;
         |FINSI;
      |FINSI;
      |CIERRA #Referen@; |DESCARGA_DEF #Referen@;
   |FINSI;
|FPRC;

|PROCESO Tipo07C13F003; |TIPO 7;
   |ABRE #dstem500;
   #dstem500#0 = #dstem003#3;
   |LEE 000#dstem500.=;
   |SI FSalida ! 0; |DDEFECTO #dstem500; |FINSI;
   |CIERRA #dstem500;

   nModo = (FEntrada / 100) ! 0;
   |SI #dstem500#25 = "S"; |Y nModo = 1;
      |C_M #dstem003#13, 1, "N"; |DDEFECTO #dstew006;
      |BORRA_HIJOS_PAN #3#dstez001;
      |SUB_PINPA #0#dstew006, enPanta3; enPantw006 = FSalida;

      |DBASS aAlfa; |QBF aAlfa;
      aAlfa = aAlfa + "contagen/def/caclipro.mas";
      |CARGA_DEF aAlfa, Referen@;
      |SI FSalida = 0;
         |DBASS aAlfa; |QBF aAlfa;
         aAlfa = aAlfa + "contagen/fich/" + (("00000" + #48#2) % -105) + #48#3 + "/";
         |PATH_DAT #Referen@#aAlfa#;
         |ABRE #Referen@;
         |SI #dstem500#6 = "3";
            #Referen@#23 = "C";
         |SINO;
            #Referen@#23 = "P";
         |FINSI;
         #Referen@#10 = #3#13;
         |LEE 000#Referen@.=;
         |SI FSalida ! 0; |DDEFECTO #Referen@; |FINSI;

         #dstew006#0 = #Referen@#9;
         #dstew006#1 = #Referen@#1;
         #dstew006#2 = #dstem003#13;
         |CIERRA #Referen@; |DESCARGA_DEF #Referen@;
      |FINSI;

      |PINDA #0#dstew006;

|ET OtraVez;
      FSalida = 1;
      |PARA; |SI FSalida ! 0; |HACIENDO;
         |ENDATOS #1#dstew006;
      |SIGUE;
      #dstem003#13 = #dstew006#2; |PINTA #dstem003#13;

      |ABRE #1100;
      #1100#0 = #3#13;
      |LEE 000#1100=;
      |SI FSalida ! 0;
         |MENAV "0000 Cuenta Contable Inexistente.";
         |VETE OtraVez;
      |SINO;
         |SI #1100#3 ! "S";
             |MENAV "0000 Debe ser una cuenta de Pase Directo.";
             |VETE OtraVez;
         |FINSI;
      |FINSI;

      |CIERRA #1100;
      |BORRA_HIJOS_PAN #3#dstez001;
   |SINO;
      |SI nModo ! 1;
         |C_M #dstem003#13, 1, "N";
      |SINO;
         |C_M #dstem003#13, 1, "S";
      |FINSI;
   |FINSI;
|FPRC;
