|FICHEROS;
   dstew005 #0;
   Referen@ #1000;
|FIN;

|VARIABLES;
   aDirBass = "";
   aAlfa    = "";
   aAlfa1   = "";
   aLong    = "";
   aParte1  = "";
   aParte2  = "";
   aCuenta  = "";

   nSw      = 0;
   nLong    = 0;
   nCalc    = 0;
   nCalc1   = 0;
   nMaxDig  = 0;
   nTecla   = 0;
|FIN;

|PROCESO MiraCuenta;
   |DBASS aDirBass; |QBF aDirBass;
   aDirBass = aDirBass + "contagen/";
   aAlfa = aDirBass + "def/canempre.mas";
   |CARGA_DEF aAlfa, Referen@;
   |SI FSalida = 0;
      nMaxDig = 0;
      aAlfa = aDirBass + "fich/"; |PATH_DAT #1000 aAlfa;
      |ABRE #Referen@;
      #Referen@#2 = #48#2;
      #Referen@#3 = #48#3;
      |LEE 000#Referen@.=;
      |SI FSalida = 0;
         |SI #Referen@#14 > nMaxDig; nMaxDig = #Referen@#14; |FINSI;
         |SI #Referen@#15 > nMaxDig; nMaxDig = #Referen@#15; |FINSI;
         |SI #Referen@#16 > nMaxDig; nMaxDig = #Referen@#16; |FINSI;
         |SI #Referen@#17 > nMaxDig; nMaxDig = #Referen@#17; |FINSI;
         |SI #Referen@#18 > nMaxDig; nMaxDig = #Referen@#18; |FINSI;
         |SI #Referen@#19 > nMaxDig; nMaxDig = #Referen@#19; |FINSI;
      |FINSI;
      |CIERRA #Referen@; |DESCARGA_DEF #Referen@;
   |FINSI;

   aCuenta = #dstew005#Campo#; |QBF aCuenta; |HAZ QuitaPuntos;
   #dstew005#Campo# = aCuenta; |PINTA #dstew005#Campo#;

   |DBASS aDirBass; |QBF aDirBass;
   aDirBass = aDirBass + "contagen/";
   aAlfa = aDirBass + "def/cacuenta.mas";
   |CARGA_DEF aAlfa, Referen@;
   |SI FSalida = 0;
      aAlfa = aDirBass + "fich/" + (("00000" + #48#2) % -105) + #48#3 + "/";
      |PATH_DAT #1000 aAlfa;
      |ABRE #Referen@;
      |SI nSw = 1;
         #Referen@#0 = #dstew005#Campo#; |LEE 000#Referen@.];
         |CONSULTA_CLAVE #1#Referen@;
         |SI FSalida = 0; #dstew005#Campo# = #Referen@#0; |PINTA #dstew005#Campo#; |FINSI;
      |FINSI;
      |SI Campo ! 24; |Y Campo ! 25;
         nCalc = Campo + 5;
      |SINO;
         nCalc = Campo + 2;
      |FINSI;
      #Referen@#0 = #dstew005#Campo#; |LEE 000#Referen@.=;
      |SI FSalida = 0;
         #dstew005#nCalc# = #Referen@#2; |PINTA #dstew005#nCalc#;
      |SINO;
         #dstew005#nCalc# = ""; |PINTA #dstew005#nCalc#;
         |MENAV "    Cuenta inexistente"; |ERROR;
      |FINSI;
      |CIERRA #Referen@; |DESCARGA_DEF #Referen@;
   |FINSI;
|FPRC;

|PROCESO Tipo66; |TIPO 66;
   nSw = 1; |HAZ MiraCuenta; nSw = 0;
|FPRC;

|PROCESO QuitaPuntos;
   aAlfa = aCuenta; |QBF aAlfa;
   aAlfa = aAlfa - ".";
   |PARA; |SI FSalida ! 0; |HACIENDO;
      nCalc1 = FSalida;
      aLong = aAlfa % 0; nLong = aLong;
      nCalc = nMaxDig - nLong; aAlfa1 = "0" * nCalc;
      nCalc = ((nCalc1 / 100) ! 0) + 99; aParte1 = aCuenta % nCalc;
      nCalc = nCalc1 + 198;              aParte2 = aCuenta % nCalc;
      aAlfa = aParte1 + aAlfa1 + aParte2;
      aAlfa = aAlfa - ".";
   |SIGUE;
   aCuenta = aAlfa;
|FPRC;

|PROCESO MiraConcIVA;
   |DBASS aDirBass; |QBF aDirBass;
   aDirBass = aDirBass + "modelos/";
   aAlfa = aDirBass + "def/dsmom030.mas";
   |CARGA_DEF aAlfa, Referen@;
   |SI FSalida = 0;
      aAlfa = aDirBass + "fich/"; |PATH_DAT #Referen@#aAlfa#;
      |ABRE #Referen@;
      |SI nSw = 1;
         #Referen@#0 = #dstew005#28;
         |LEE 000#Referen@.];
         |CONSULTA_CLAVE #1#Referen@;
         |SI FSalida = 0;
            #dstew005#28 = #Referen@#0; |PINTA #dstew005#28;
            #dstew005#29 = #Referen@#1; |PINTA #dstew005#29;
         |FINSI;
         |ERROR;
      |FINSI;
      #Referen@#0 = #dstew005#28;
      |LEE 000#Referen@.=;
      |SI FSalida = 0;
         |SI #Referen@#4 = "N";
            |MENAV "    Este concepto no es de IVA"; |ERROR;
         |SINO;
            #dstew005#29 = #Referen@#1; |PINTA #dstew005#29;
         |FINSI;
      |SINO;
         |MENAV "    Concepto contable inexistente"; |ERROR;
      |FINSI;
      |CIERRA #Referen@; |DESCARGA_DEF #Referen@;
   |FINSI;
|FPRC;

|PROCESO T66ConcIVA; |TIPO 66;
   nSw = 1; |HAZ MiraConcIVA; nSw = 0;
|FPRC;
