|FICHEROS;
     reinfplz;

     reacceso;
     rem00100;
     rem00101;
     rem01001;

     :/integral/def/dsam0054;

     &poblaci@;
|FIN;

|VARIABLES;
     nParrilla = 0;
     nLinea    = 0;

     aFichero     = "";

     aAlfa      = "";
     aAlfa1     = "";
     aAlfa2     = "";
     aOrigen    = "";
     aDestino   = "";
     aWord      = "";
     aAbre      = "";
     aAsigna    = "";
     aIdenti    = "";
     aPath01    = "";
     aIPRemoto  = "";
     aDef       = "";
     aDModelo   = "";
     aHModelo   = "";
     aCaracter  = "";
     aCadena    = "";
     aLen       = "";
     aTipo      = "";
     aDescri    = "";
     aPathDoc   = "";

     nSwDialogo = 0;
     nWord      = 0;
     nContador  = 0;
     nCampo     = 0;
     nCampoDes  = 0;
     nCampoDes2 = 0;
     nHandle    = 0;
     nNume      = 0;
     nLen       = 0;
     nPos       = 0;
     nImporte   = 0;

     &enSwDeDonde     = 0;
     &enCodProv       = 0;
     &enCodPobl       = 0;
     &eaRutaDocumento = "";
     &enFSalida       = 0;
     &enCodUbica      = 0;
     &eaNomUbicacion  = "";
     &eaImporte       = "";
     &eaUnidadBasico  = "";
|FIN;

|PROCESO Seleccion;
   |SI #reinfplz#1 = "S";
      |C_M #reinfplz#2,1,"S";
      |C_M #reinfplz#3,1,"S";
      |PARA nParrilla = 4; |SI nParrilla [ 24; |HACIENDO nParrilla = nParrilla + 1;
         |C_M #reinfplz#nParrilla#, 1, "N";
      |SIGUE;
   |SINO;
      |C_M #reinfplz#2,1,"N";
      |C_M #reinfplz#3,1,"N";
      |PARA nParrilla = 4; |SI nParrilla [ 24; |HACIENDO nParrilla = nParrilla + 1;
         |C_M #reinfplz#nParrilla#, 1, "S";
      |SIGUE;
   |FINSI;
|FPRC;

|PROCESO Documental;  |TIPO 7;
     |C_M #reinfplz#30, 1, "S";
     |SI enSwDeDonde = 0;
         |C_M #reinfplz#30, 1, "N";  #reinfplz#30 = "N";  |PINTA #reinfplz#30;
     |FINSI;
|FPRC;

|PROCESO Ubicacion;  |TIPO 0;
     |C_M #reinfplz#31, 1, "S";
     |SI #reinfplz#30 = "N";
         |C_M #reinfplz#31, 1, "N";  #reinfplz#31 = 0;    |PINTA #reinfplz#31;
                              #reinfplz#32 = "";   |PINTA #reinfplz#32;
     |FINSI;
|FPRC;

|PROCESO BuscaUbicacion;
     |SI #reinfplz#30 = "N";  |ACABA;  |FINSI;

     enCodUbica = #reinfplz#31;
     |HAZ LeeUbicaciones;
     |SI enFSalida ! 0;  |ERROR;  |ACABA;  |FINSI;

     #reinfplz#31 = enCodUbica;      |PINTA #reinfplz#31;
     #reinfplz#32 = eaNomUbicacion;  |PINTA #reinfplz#32;
|FPRC;

|PROCESO Asigna;
     aCadena = "";
     aLen    = aAsigna % 0;
     nLen    = aLen;
     |PARA nCampo = 1;  |SI nCampo [ nLen;  |HACIENDO nCampo = nCampo + 1;
           nPos = (100 * nCampo) + 1;
           aCaracter = aAsigna % nPos;
           |SI aCaracter = "¥";  aCaracter = &209; |FINSI;
           |SI aCaracter = "¤";  aCaracter = &241; |FINSI;
           |SI aCaracter = " ";  aCaracter = &225; |FINSI;
           |SI aCaracter = "‚";  aCaracter = &233; |FINSI;
           |SI aCaracter = "¡";  aCaracter = &237; |FINSI;
           |SI aCaracter = "¢";  aCaracter = &243; |FINSI;
           |SI aCaracter = "£";  aCaracter = &250; |FINSI;
           |SI aCaracter = "§";  aCaracter = &176; |FINSI;
           |SI aCaracter = "¦";  aCaracter = &170; |FINSI;
           aCadena = aCadena + aCaracter;
     |SIGUE;
     aAsigna = aCadena;

     |PARA;  |SI;  |HACIENDO;
          |WORD_ASIGNA_TEXTO nWord, aIdenti, aAsigna;
          |SI FSalida ! 0;  |SAL;  |FINSI;
     |SIGUE;
|FPRC;

|PROCESO Mes;
   |SI aAlfa1 = "01";  aAlfa = "Enero";      |FINSI;
   |SI aAlfa1 = "02";  aAlfa = "Febrero";    |FINSI;
   |SI aAlfa1 = "03";  aAlfa = "Marzo";      |FINSI;
   |SI aAlfa1 = "04";  aAlfa = "Abril";      |FINSI;
   |SI aAlfa1 = "05";  aAlfa = "Mayo";       |FINSI;
   |SI aAlfa1 = "06";  aAlfa = "Junio";      |FINSI;
   |SI aAlfa1 = "07";  aAlfa = "Julio";      |FINSI;
   |SI aAlfa1 = "08";  aAlfa = "Agosto";     |FINSI;
   |SI aAlfa1 = "09";  aAlfa = "Septiembre"; |FINSI;
   |SI aAlfa1 = "10"; aAlfa = "Octubre";     |FINSI;
   |SI aAlfa1 = "11"; aAlfa = "Noviembre";   |FINSI;
   |SI aAlfa1 = "12"; aAlfa = "Diciembre";   |FINSI;
|FPRC;

|PROCESO AbreWord;
     aAlfa = eaUnidadBasico + "/MODELOS";                   |RMKDIR aAlfa;
     aAlfa = eaUnidadBasico + "/MODELOS/DOCUMENTOS";        |RMKDIR aAlfa;

     |SI #reinfplz#26 = "D";
         aOrigen   = aPathDoc + "word0167.doc";
         aDestino  = eaUnidadBasico + "/MODELOS/DOCUMENTOS/word0167.doc";
         aDescri   = "CARTA 2§ PAGO DOMICILIADOS RENTA 2018";
     |SINO;
         aOrigen   = aPathDoc + "word0168.doc";
         aDestino  = eaUnidadBasico + "/MODELOS/DOCUMENTOS/word0168.doc";
         aDescri   = "CARTA 2§ PAGO NO DOMICILIADOS RENTA 2018";
     |FINSI;

     aIPRemoto = "";
     |IP_REMOTO aIPRemoto;  |QBT aIPRemoto;

     |SI aIPRemoto ! "";
         |ENVIA_FICHERO aOrigen, aDestino;
     |SINO;
         |COPIA_FICHERO aOrigen, aDestino;
     |FINSI;

     nWord  = 1;
     aAbre  = aDestino;

     |MENSA "      Abriendo el Documento, espere un Momento.";

     |WORD_ABRE nWord, aAbre;
     |WORD_PROPIEDADES nWord, "Visible", "NO";
|FPRC;

|PROCESO GuardaDocumento;
     |ABRE #dsam0054;
     #dsam0054#0  = #reacceso#0;
     #dsam0054#1  = 9999;
     |LEE 040#dsam0054.];
     |SI FSalida = 0;
         |LEE 040#dsam0054.a;
     |SINO;
         |LEE 040#dsam0054.u;
     |FINSI;
     |SI FSalida = 0;  |Y #dsam0054#0 = #reacceso#0;
         nLinea = #dsam0054#1 + 1;
     |SINO;
         nLinea = 1;
     |FINSI;

     |DDEFECTO #dsam0054;
     |HORA aAlfa;
     aAlfa1 = aAlfa % 102;
     aAlfa2 = aAlfa % 402;

     #dsam0054#0  = #reacceso#0;
     #dsam0054#1  = nLinea;
     #dsam0054#2  = aDescri;
     #dsam0054#3  = "DOC" + (("00000" + #dsam0054#0) % -105) + (("0000" + #dsam0054#1) % -104) + ".DOC";
     #dsam0054#4  = ~;
     #dsam0054#5  = aAlfa1;
     #dsam0054#6  = aAlfa2;
     #dsam0054#7  = ~;
     #dsam0054#8  = aAlfa1;
     #dsam0054#9  = aAlfa2;
     #dsam0054#10 = #reinfplz#31;
     #dsam0054#11 = "";
     #dsam0054#12 = "";
     |GRABA 020#dsam0054.n;
     |LIBERA #dsam0054;
     |CIERRA #dsam0054;

     aOrigen  = aAbre;
     aDestino = eaRutaDocumento + "/" + "DOC" + (("00000" + #reacceso#0) % -105) + (("0000" + nLinea) % -104) + ".DOC";
     |QBT aOrigen;
     |QBT aDestino;

     |RCOPIA_FICHERO aOrigen, aDestino;
     |SI FSalida ! 0;
         |MENAV "     No se ha podido copia el Documento al Cliente.";
     |FINSI;
|FPRC;

|PROCESO CargaFamiliar;
     aAlfa     = #rem01001#2 % 101;
     nNume     = aAlfa;
     nContador = nContador + 1;
     |SI nContador = nNume;
         aAsigna = #rem00101#3;  |QBF aAsigna;
         aIdenti = "!#1#";
         |HAZ Asigna;
     |FINSI;
|FPRC;

|DEFBUCLE rem00101;
     #rem00101#1;
     13;
     #reacceso#0, 01, "S";
     #reacceso#0, 99, "S";
     ;
     NULL, CargaFamiliar;
|FIN;

|PROCESO CargaPersonal;
     |SI #rem01001#2 = "CONJUNTA  ";  |O #rem01001#2 = "CONJUNTAE ";  |O #rem01001#2 = "TITULAR   ";
         aAsigna = #rem00100#5;
         aIdenti = "!#1#";
         |HAZ Asigna;
     |FINSI;

     |SI #rem01001#2 = "CONYUGE   ";
         aAsigna = #rem00100#39;
         aIdenti = "!#1#";
         |HAZ Asigna;
     |FINSI;

     nContador = 0;
     |SI #rem01001#2 = "1 FAMILIAR";  |BUCLE rem00101; |FINSI;
     |SI #rem01001#2 = "2 FAMILIAR";  |BUCLE rem00101; |FINSI;
     |SI #rem01001#2 = "3 FAMILIAR";  |BUCLE rem00101; |FINSI;
     |SI #rem01001#2 = "4 FAMILIAR";  |BUCLE rem00101; |FINSI;

     aAsigna = #rem00100#24;  |QBF aAsigna;
     aAsigna = aAsigna + " " + #rem00100#25;   |QBF aAsigna;
     |SI #rem00100#26 ! "    ";
         aAsigna = aAsigna + ", " + #rem00100#26;   |QBF aAsigna;

         |SI #rem00100#28 ! "  ";
             aAsigna = aAsigna + " " + #rem00100#28;   |QBF aAsigna;

             |SI #rem00100#29 ! "  ";
                 aAsigna = aAsigna + " " + #rem00100#29;   |QBF aAsigna;
             |FINSI;
         |FINSI;
     |FINSI;
     aIdenti = "!#2#";
     |HAZ Asigna;

     aAsigna = (("00" + #rem00100#30) % -102) + (("000" + #rem00100#31) % -103);
     aAsigna = aAsigna + " " + #rem00100#32;
     aIdenti = "!#3#";
     |HAZ Asigna;

     enCodProv = #rem00100#30;
     enCodPobl = #rem00100#31;
     |HAZ LeePoblacion;

     aAsigna = "(" + #poblaci@#2;    |QBF aAsigna;
     aAsigna = aAsigna + ")";
     aIdenti = "!#4#";
     |HAZ Asigna;
|FPRC;

|PROCESO CargaCampos;
     |HAZ AbreWord;

     |MENSA "      Asignando Campos, espere un Momento.";

     #rem00100#0 = #reacceso#0;
     |LEE 040#rem00100.=;
     |SI FSalida ! 0;  |DDEFECTO #rem00100;  |FINSI;

     |HAZ CargaPersonal;

     aAsigna = #reinfplz#29;  |QBF aAsigna;
     aAsigna = aAsigna + ",";
     aAlfa   = #reinfplz#25 % 0102;   aAsigna = aAsigna + " " + aAlfa + " de ";
     aAlfa1  = #reinfplz#25 % 0402;   |HAZ Mes;
                               aAsigna = aAsigna + aAlfa + " de ";
     aAlfa   = #reinfplz#25 % 0704;   aAsigna = aAsigna + aAlfa;

     aIdenti = "!#5#";
     |HAZ Asigna;

     aAsigna = #reinfplz#27;  |QBF aAsigna;
     aAsigna = aAsigna;
     aIdenti = "!#ATENCION#";
     |HAZ Asigna;

     aAsigna = #reinfplz#28;  |QBF aAsigna;
     aAsigna = aAsigna + ",";
     aIdenti = "!#NOMASESORIA#";
     |HAZ Asigna;

     nImporte  = #rem01001#16 % 40;
     eaImporte = nImporte;  |HAZ Importe;
     aAsigna   = eaImporte + " Euros.";
     aIdenti   = "!#IMPORTE#";
     |HAZ Asigna;

     |MENSA "      Imprimiendo, espere un Momento.";

     |WORD_IMPRIME nWord, "";
     |WORD_PROPIEDADES nWord, "Visible", "NO";

     |SLEEP 10;

     |MENSA "      Cerrando Documento";

     |WORD_SALVA nWord, "";

     |SI #reinfplz#31 = "S";  |HAZ GuardaDocumento;  |FINSI;

     |RFBORRA aAbre;
|FPRC;

|DEFBUCLE rem01001;
     #rem01001#1;
     5, 14, 8, 10;
     #reacceso#0, 00, "          ", "100", "   ", "S", aTipo;
     #reacceso#0, 99, "zzzzzzzzzz", "101", "   ", "S", aTipo;
     ;
     NULL, CargaCampos;
|FIN;

|PROCESO Emision;
     aDModelo = "100";
     aHModelo = "101";
     |SI #reacceso#14 ! "IMPRESO   ";  |ACABA;  |FINSI;

     |BUCLE rem01001;
|FPRC

|DEFBUCLE reacceso;
     #reacceso#1;
     ;
     #reinfplz#2;
     #reinfplz#3;
     ;
     NULL, Emision;
|FIN;

|PROCESO Tipo3;  |TIPO 3;
     aTipo = "N";
     |SI #reinfplz#26 = "D";  aTipo = "S";  |FINSI;

     |ABRE #rem00100;
     |SI #reinfplz#1 = "S";
         |BUCLE reacceso;
     |SINO;
         |PARA nParrilla = 4; |SI nParrilla [ 24; |HACIENDO nParrilla = nParrilla + 1;
               |SI #reinfplz#nParrilla# ! 0;
                   #reinfplz#2 = #reinfplz#nParrilla#;
                   #reinfplz#3 = #reinfplz#nParrilla#;
                   |BUCLE reacceso;
               |FINSI;
         |SIGUE;
     |FINSI;
     |CIERRA #rem00100;
|FPRC;

|PROGRAMA;
     |HAZ PreparaInformes;
     |HAZ LeeUnidadBasico;

     |CLS;
     |CABEZA "Emision Carta Segundo Plazo";

     |DBASS aPathDoc; |QBF aPathDoc;
     aPathDoc = aPathDoc + "basica/word/";

     aAbre = aPathDoc + "word0167.doc";
     |XABRE "E", aAbre, nHandle;
     |SI FSalida < 0;
         |SUB_EJECUTA_NP ":basica/wordm000;CREALOS";
     |FINSI;

     aAbre = aPathDoc + "word0167.doc";
     |XABRE "E", aAbre, nHandle;
     |SI FSalida < 0;
         |MENAV "     Instale Ficheros patrones (Documentos en Word)";
         |ACABA;
     |FINSI;

     |ABRET #reinfplz;
     |LEE 101#reinfplz.p;
     |SI FSalida ! 0;
         |DDEFECTO #reinfplz;
         |GRABA 020#reinfplz.b;
     |FINSI;

     |PDEFECTO #reinfplz, 1, 25;

     |PINPA #0#reinfplz;
     |PINDA #0#reinfplz;
     |ENDATOS #1#reinfplz;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |GRABA 020#reinfplz.a;
     |LIBERA #reinfplz;
|FPRO;

|PROCESO Tipo80;  |TIPO 80;
     FSalida = 2499;
|FPRC;
