|FICHEROS;
     reinfcmp;
     :/ren2017/def/rem00100;      || Datos contribuyentes
     :/ren2017/def/reacceso;      || Acceso Renta

     :/integral/def/dsam0054;

     referen@;                  || Referencia para lectura de datos de la renta del a¤o 2017

     &poblaci@;
|FIN;

|VARIABLES;
     nParrilla = 0;

     aFichero     = "";
     nCabecera    = 0;
     nSubCabecera = 0;

     aAlfa      = "";
     aAlfa1     = "";
     aAlfa2     = "";
     aOrigen    = "";
     aDestino   = "";
     aWord      = "";
     aAbre1     = "";
     aAbre2     = "";
     aAsigna    = "";
     aIdenti    = "";
     aPath17    = "";
     aPath18    = "";
     aPathDef17 = "";
     aPathFic17 = "";
     aPathDoc17 = "";
     aIPRemoto  = "";
     aDef       = "";

     nWord      = 0;
     nWordAnt   = 0;
     nContador  = 0;
     nCampo     = 0;
     nCampoDes  = 0;
     nCampoDes2 = 0;
     nLinea     = 0;

     &enCodProv       = 0;
     &enCodPobl       = 0;
     &enSwDeDonde     = 0;

     &eaRutaDocumento = "";
     &enFSalida       = 0;
     &enCodUbica      = 0;
     &eaNomUbicacion  = "";
     &eaUnidadBasico  = "";
|FIN;

|PROCESO Seleccion;
   |SI #reinfcmp#0 = "S";
      |C_M #reinfcmp#1,1,"S";
      |C_M #reinfcmp#2,1,"S";
      |PARA nParrilla = 3; |SI nParrilla [ 23; |HACIENDO nParrilla = nParrilla + 1;
         |C_M #reinfcmp#nParrilla#, 1, "N";
      |SIGUE;
   |SINO;
      |C_M #reinfcmp#1,1,"N";
      |C_M #reinfcmp#2,1,"N";
      |PARA nParrilla = 3; |SI nParrilla [ 23; |HACIENDO nParrilla = nParrilla + 1;
         |C_M #reinfcmp#nParrilla#, 1, "S";
      |SIGUE;
   |FINSI;
|FPRC;

|PROCESO Documental;  |TIPO 7;
     |C_M #reinfcmp#27, 1, "S";
     |SI enSwDeDonde = 0;
         |C_M #reinfcmp#27, 1, "N";  #reinfcmp#27 = "N";  |PINTA #reinfcmp#27;
     |FINSI;
|FPRC;

|PROCESO Ubicacion;  |TIPO 0;
     |C_M #reinfcmp#28, 1, "S";
     |SI #reinfcmp#27 = "N";
         |C_M #reinfcmp#28, 1, "N";  #reinfcmp#28 = 0;    |PINTA #reinfcmp#28;
                              #reinfcmp#29 = "";   |PINTA #reinfcmp#29;
     |FINSI;
|FPRC;

|PROCESO BuscaUbicacion;
     |SI #reinfcmp#27 = "N";  |ACABA;  |FINSI;

     enCodUbica = #reinfcmp#28;
     |HAZ LeeUbicaciones;
     |SI enFSalida ! 0;  |ERROR;  |ACABA;  |FINSI;

     #reinfcmp#28 = enCodUbica;      |PINTA #reinfcmp#28;
     #reinfcmp#29 = eaNomUbicacion;  |PINTA #reinfcmp#29;
|FPRC;

|PROCESO AbreWord2;
     aAlfa = eaUnidadBasico + "/MODELOS";                   |RMKDIR aAlfa;
     aAlfa = eaUnidadBasico + "/MODELOS/DOCUMENTOS";        |RMKDIR aAlfa;

     aOrigen   = aPathDoc17 + "word0162.doc";
     aDestino  = eaUnidadBasico + "/MODELOS/DOCUMENTOS/word0162.doc";

     aIPRemoto = "";
     |IP_REMOTO aIPRemoto;  |QBT aIPRemoto;

     |SI aIPRemoto ! "";
         |ENVIA_FICHERO aOrigen, aDestino;
     |SINO;
         |COPIA_FICHERO aOrigen, aDestino;
     |FINSI;

     nWord  = 2;
     aAbre2 = aDestino;

     |WORD_ABRE nWord, aAbre2;
     |WORD_PROPIEDADES nWord, "Visible", "NO";
|FPRC;

|PROCESO AbreOtro;
     |WORD_SALVA nWord, "";

     nWord = nWordAnt;

     |WORD_CARGA_TEXTO nWord, "!<<PEGAR>>", aDestino;

     |RFBORRA aAbre2;

     |HAZ AbreWord2;
|FPRC;

|PROCESO Asigna;
     nContador = nContador + 1;
     aIdenti   = "!#" + nContador + "#";
     |WORD_ASIGNA_TEXTO nWord, aIdenti, aAsigna;
     |SI FSalida ! 0;
         aAlfa1 = aAsigna;
         |HAZ AbreOtro;

         aAsigna   = aAlfa1;
         nContador = 1;
         aIdenti   = "!#" + nContador + "#";
         |WORD_ASIGNA_TEXTO nWord, aIdenti, aAsigna;
     |FINSI;
|FPRC;

|PROCESO MiraSiDatos;
     |SI aFichero = "rem00020.mas";  nCampoDes = 5;                     |FINSI;
     |SI aFichero = "rem00021.mas";  nCampoDes = 13;                    |FINSI;

     |SI #reinfcmp#31 = "N";
         |SI aFichero = "rem00022.mas";  nCampoDes = 17;  nCampoDes2 = 18;  |FINSI;
         |SI aFichero = "rem00023.mas";  nCampoDes = 17;  nCampoDes2 = 18;  |FINSI;
     |SINO;
         |SI aFichero = "rem00022.mas";  nCampoDes = 17;                    |FINSI;
         |SI aFichero = "rem00023.mas";  nCampoDes = 17;                    |FINSI;
     |FINSI;

     |SI aFichero = "rem00024.mas";  nCampoDes = 15;                    |FINSI;
     |SI aFichero = "rem00051.mas";  nCampoDes = 17;                    |FINSI;
     |SI aFichero = "rem00052.mas";  nCampoDes = 16;                    |FINSI;
     |SI aFichero = "rem00055.mas";  nCampoDes = 16;                    |FINSI;
     |SI aFichero = "rem00025.mas";  nCampoDes = 22;                    |FINSI;
     |SI aFichero = "rem00026.mas";  nCampoDes = 22;                    |FINSI;
     |SI aFichero = "rem00027.mas";  nCampoDes = 20;                    |FINSI;
     |SI aFichero = "rem00028.mas";  nCampoDes = 21;                    |FINSI;

     |SI nCabecera = 0;
         |SI aFichero = "rem00020.mas";
             aAsigna   = " ";
             |HAZ Asigna;

             aAsigna   = "RENDIMIENTOS DEL TRABAJO";
             |HAZ Asigna;

             aAsigna   = " ";
             |HAZ Asigna;

             aAsigna   = "- Certificado de rendimientos y retenciones";
             |HAZ Asigna;

             aAsigna   = " ";
             |HAZ Asigna;

             aAsigna   = "En el ejercicio 2017 se declararon los siguientes rendimientos ";
             |HAZ Asigna;

             aAsigna   = " ";
             |HAZ Asigna;
         |FINSI;

         |SI aFichero = "rem00021.mas";
             aAsigna   = " ";
             |HAZ Asigna;

             aAsigna   = "RENDIMIENTOS DEL CAPITAL INMOBILIARIO";
             |HAZ Asigna;

             aAsigna   = " ";
             |HAZ Asigna;

             aAsigna   = "- Recibo del Impuesto sobre Bienes Inmuebles (Ant.Contrib.):";
             |HAZ Asigna;

             aAsigna   = " ";
             |HAZ Asigna;

             aAsigna   = "En el ejercicio 2017 se declararon los siguientes rendimientos ";
             |HAZ Asigna;

             aAsigna   = " ";
             |HAZ Asigna;
         |FINSI;

         |SI aFichero = "rem00022.mas"; |O aFichero = "rem00023.mas";
          |O aFichero = "rem00024.mas"; |O aFichero = "rem00051.mas";
          |O aFichero = "rem00052.mas"; |O aFichero = "rem00055.mas";
             aAsigna   = " ";
             |HAZ Asigna;

             aAsigna   = "RENDIMIENTOS DEL CAPITAL MOBILIARIO";
             |HAZ Asigna;

             aAsigna   = " ";
             |HAZ Asigna;

             aAsigna   = "En el ejercicio 2017 se declararon los siguientes rendimientos ";
             |HAZ Asigna;

             aAsigna   = " ";
             |HAZ Asigna;
         |FINSI;

         |SI aFichero = "rem00025.mas"; |O aFichero = "rem00026.mas";
          |O aFichero = "rem00027.mas"; |O aFichero = "rem00028.mas";
             aAsigna   = " ";
             |HAZ Asigna;

             aAsigna   = "RENDIMIENTOS POR ACTIVIDADES EMPRESARIALES";
             |HAZ Asigna;

             aAsigna   = " ";
             |HAZ Asigna;

             aAsigna   = "En el ejercicio 2017 se declararon los siguientes rendimientos ";
             |HAZ Asigna;

             aAsigna   = " ";
             |HAZ Asigna;
         |FINSI;
     |FINSI;

     |SI nSubCabecera = 0;
         aAsigna = "";
         |SI aFichero = "rem00022.mas"; aAsigna = "- Participaciones en Fondos Propios";      |FINSI;
         |SI aFichero = "rem00023.mas"; aAsigna = "- Cesion a terceros y otros rendimientos"; |FINSI;
         |SI aFichero = "rem00024.mas"; aAsigna = "- Contrato de Seguros, Capital diferido";  |FINSI;
         |SI aFichero = "rem00051.mas"; aAsigna = "- Rentas vitalicias y temporales";         |FINSI;
         |SI aFichero = "rem00052.mas"; aAsigna = "- Deuda publica";                          |FINSI;
         |SI aFichero = "rem00055.mas"; aAsigna = "- Fondos de inversion colectiva";          |FINSI;
         |SI aFichero = "rem00025.mas"; aAsigna = "- Estimacion Directa (Normal)";            |FINSI;
         |SI aFichero = "rem00026.mas"; aAsigna = "- Estimacion Directa (Simplificada)";      |FINSI;
         |SI aFichero = "rem00027.mas"; aAsigna = "- Estimacion Objetiva (Modulos)";          |FINSI;
         |SI aFichero = "rem00028.mas"; aAsigna = "- Estimacion Objetiva (Agricolas)";        |FINSI;
         |SI aAsigna ! "";
             |SI nCabecera ! 0;
                 aAlfa     = aAsigna;
                 aAsigna   = "";
                 |HAZ Asigna;
                 aAsigna   = aAlfa;
             |FINSI;

             |HAZ Asigna;

             aAsigna   = " ";
             |HAZ Asigna;
         |FINSI;

         nSubCabecera = 1;
     |FINSI;

     nCabecera = 1;

     |SI nCampoDes2 ! 0;
         aAsigna   = #referen@#nCampoDes# + " " + #referen@#nCampoDes2#;
     |SINO;
         aAsigna   = #referen@#nCampoDes#;
     |FINSI;

     |HAZ Asigna;
|FPRC;

|DEFBUCLE MiraSiDatos;
     #referen@#1002;
     ;
     #rem00100#0,01;
     #rem00100#0,99;
     ;
     NULL, MiraSiDatos;
|FIN;

|PROCESO BuscaDatos;
     aDef = aPathDef17 + aFichero;
     |CARGA_DEF aDef, referen@;
     |SI FSalida ! 0;
         aAlfa = "     Error en cargar el def " + aFichero;
         |MENAV aAlfa;
         |ACABA;
     |FINSI;

     |PATH_DAT #referen@#aPathFic17#;

     nCampoDes2   = 0;
     nSubCabecera = 0;
     |ABRE #referen@;
     |BUCLE MiraSiDatos;
     |CIERRA #referen@;

     |DESCARGA_DEF #referen@;
|FPRC;

|PROCESO Mes;
   |SI aAlfa1 = "01";  aAlfa = "Enero";      |FINSI;
   |SI aAlfa1 = "02";  aAlfa = "Febrero";    |FINSI;
   |SI aAlfa1 = "03";  aAlfa = "Marzo";      |FINSI;
   |SI aAlfa1 = "04";  aAlfa = "Abril";      |FINSI;
   |SI aAlfa1 = "05";  aAlfa = "Mayo";       |FINSI;
   |SI aAlfa1 = "06";  aAlfa = "Junio";      |FINSI;
   |SI aAlfa1 = "07";  aAlfa = "Julio";      |FINSI;
   |SI aAlfa1 = "08";  aAlfa = "Agosto";     |FINSI;
   |SI aAlfa1 = "09";  aAlfa = "Septiembre"; |FINSI;
   |SI aAlfa1 = "10"; aAlfa = "Octubre";    |FINSI;
   |SI aAlfa1 = "11"; aAlfa = "Noviembre";  |FINSI;
   |SI aAlfa1 = "12"; aAlfa = "Diciembre";  |FINSI;
|FPRC;

|PROCESO Anexo;
     nContador = 0;
     |HAZ AbreWord2;

                       aAsigna = "";
     |SI #reinfcmp#30 = "N";  aAsigna = #rem00100#0;  |FINSI;
     aIdenti = "!#CODIGO#";
     |WORD_ASIGNA_TEXTO nWord, aIdenti, aAsigna;

     aAsigna = #rem00100#5;
     aIdenti = "!#NOMBRE#";
     |WORD_ASIGNA_TEXTO nWord, aIdenti, aAsigna;

     aFichero = "rem00020.mas";  nCabecera = 0;  |HAZ BuscaDatos;
     aFichero = "rem00021.mas";  nCabecera = 0;  |HAZ BuscaDatos;
     aFichero = "rem00022.mas";  nCabecera = 0;  |HAZ BuscaDatos;
     aFichero = "rem00023.mas";                  |HAZ BuscaDatos;
     aFichero = "rem00051.mas";                  |HAZ BuscaDatos;
     aFichero = "rem00052.mas";                  |HAZ BuscaDatos;
     aFichero = "rem00055.mas";                  |HAZ BuscaDatos;
     aFichero = "rem00025.mas";  nCabecera = 0;  |HAZ BuscaDatos;
     aFichero = "rem00026.mas";                  |HAZ BuscaDatos;
     aFichero = "rem00027.mas";                  |HAZ BuscaDatos;
     aFichero = "rem00028.mas";                  |HAZ BuscaDatos;

     |PARA nCampo = nContador + 1;  |SI nCampo [ 50;  |HACIENDO nCampo = nCampo + 1;
           aAsigna = " ";
           aIdenti = "!#" + nCampo + "#";
           |WORD_ASIGNA_TEXTO nWord, aIdenti, aAsigna;
     |SIGUE;

     aAsigna = " ";
     aIdenti = "!<<PEGAR>>";
     |WORD_ASIGNA_TEXTO nWord, aIdenti, aAsigna;

     |WORD_SALVA nWord, "";

     nWord = nWordAnt;
     |WORD_CARGA_TEXTO nWord, "!<<PEGAR>>", aDestino;

     |RFBORRA aAbre2;
|FPRC;

|PROCESO AbreWord1;
     aAlfa = eaUnidadBasico + "/MODELOS";                   |RMKDIR aAlfa;
     aAlfa = eaUnidadBasico + "/MODELOS/DOCUMENTOS";        |RMKDIR aAlfa;

     aOrigen   = aPathDoc17 + "word0161.doc";
     aDestino  = eaUnidadBasico + "/MODELOS/DOCUMENTOS/word0161.doc";

     aIPRemoto = "";
     |IP_REMOTO aIPRemoto;  |QBT aIPRemoto;

     |SI aIPRemoto ! "";
         |ENVIA_FICHERO aOrigen, aDestino;
     |SINO;
         |COPIA_FICHERO aOrigen, aDestino;
     |FINSI;

     nWord  = 1;
     aAbre1 = aDestino;

     |MENSA "      Abriendo el Documento, espere un Momento.";

     |WORD_ABRE nWord, aAbre1;
     |WORD_PROPIEDADES nWord, "Visible", "NO";

     nWordAnt = nWord;
|FPRC;

|PROCESO GuardaDocumento;
     |ABRE #dsam0054;
     #dsam0054#0  = #rem00100#0;
     #dsam0054#1  = 9999;
     |LEE 040#dsam0054.];
     |SI FSalida = 0;
         |LEE 040#dsam0054.a;
     |SINO;
         |LEE 040#dsam0054.u;
     |FINSI;
     |SI FSalida = 0;  |Y #dsam0054#0 = #rem00100#0;
         nLinea = #dsam0054#1 + 1;
     |SINO;
         nLinea = 1;
     |FINSI;

     |DDEFECTO #dsam0054;
     |HORA aAlfa;
     aAlfa1 = aAlfa % 102;
     aAlfa2 = aAlfa % 402;

     #dsam0054#0  = #rem00100#0;
     #dsam0054#1  = nLinea;
     #dsam0054#2  = "CARTA DE NECESIDADES PARA LA RENTA 2017";
     #dsam0054#3  = "DOC" + (("00000" + #dsam0054#0) % -105) + (("0000" + #dsam0054#1) % -104) + ".DOC";
     #dsam0054#4  = ~;
     #dsam0054#5  = aAlfa1;
     #dsam0054#6  = aAlfa2;
     #dsam0054#7  = ~;
     #dsam0054#8  = aAlfa1;
     #dsam0054#9  = aAlfa2;
     #dsam0054#10 = #reinfcmp#28;
     #dsam0054#11 = "";
     #dsam0054#12 = "";
     |GRABA 020#dsam0054.n;
     |LIBERA #dsam0054;
     |CIERRA #dsam0054;

     aOrigen  = aAbre1;
     aDestino = eaRutaDocumento + "/" + "DOC" + (("00000" + #rem00100#0) % -105) + (("0000" + nLinea) % -104) + ".DOC";
     |QBT aOrigen;
     |QBT aDestino;

     |RCOPIA_FICHERO aOrigen, aDestino;
     |SI FSalida ! 0;
         |MENAV "     No se ha podido copia el Documento al Cliente.";
     |FINSI;
|FPRC;

|PROCESO Emision;
     #reacceso#0 = #rem00100#0;
     |LEE 040#reacceso.=;
     |SI FSalida ! 0;  |DDEFECTO #reacceso;  |FINSI;

     |SI #reacceso#19 = "S";  |ACABA;  |FINSI;

     |SI #reinfcmp#25 = "S";
         |SI #reacceso#14 ! "IMPRESO   ";
             |ACABA;
         |FINSI;
     |FINSI;

     |HAZ AbreWord1;

     |MENSA "      Asignando Campos, espere un Momento.";

     aAsigna = (("00000" + #rem00100#0) % -105) + &13 + &10 + #rem00100#5;
     |SI #reinfcmp#30 = "S";
         aAsigna = #rem00100#5;
     |FINSI;
     aIdenti = "!#1#";
     |WORD_ASIGNA_TEXTO nWord, aIdenti, aAsigna;

     aAsigna = #rem00100#24;  |QBF aAsigna;
     aAsigna = aAsigna + " " + #rem00100#25;   |QBF aAsigna;
     |SI #rem00100#26 ! "    ";
         aAsigna = aAsigna + ", " + #rem00100#26;   |QBF aAsigna;

         |SI #rem00100#28 ! "  ";
             aAsigna = aAsigna + " " + #rem00100#28;   |QBF aAsigna;

             |SI #rem00100#29 ! "  ";
                 aAsigna = aAsigna + " " + #rem00100#29;   |QBF aAsigna;
             |FINSI;
         |FINSI;
     |FINSI;
     aIdenti = "!#2#";
     |WORD_ASIGNA_TEXTO nWord, aIdenti, aAsigna;

     aAsigna = (("00" + #rem00100#30) % -102) + (("000" + #rem00100#31) % -103);
     aAsigna = aAsigna + " " + #rem00100#32;
     aIdenti = "!#3#";
     |WORD_ASIGNA_TEXTO nWord, aIdenti, aAsigna;

     enCodProv = #rem00100#30;
     enCodPobl = #rem00100#31;
     |HAZ LeePoblacion;

     aAsigna = "(" + #poblaci@#2;    |QBF aAsigna;
     aAsigna = aAsigna + ")";
     aIdenti = "!#4#";
     |WORD_ASIGNA_TEXTO nWord, aIdenti, aAsigna;

     aAlfa   = #reinfcmp#24 % 0102;   aAsigna = aAlfa + " de ";
     aAlfa1  = #reinfcmp#24 % 0402;   |HAZ Mes;
                               aAsigna = aAsigna + aAlfa + " de ";
     aAlfa   = #reinfcmp#24 % 0704;   aAsigna = aAsigna + aAlfa;

     aIdenti = "!#5#";
     |WORD_ASIGNA_TEXTO nWord, aIdenti, aAsigna;

     |SI #reinfcmp#26 = "T";
         |HAZ Anexo;
     |SINO;
         aAsigna = "";
         aIdenti = "!<<PEGAR>>";
         |WORD_ASIGNA_TEXTO nWord, aIdenti, aAsigna;
     |FINSI;

     |MENSA "      Imprimiendo, espere un Momento.";

     nWord = nWordAnt;

     |WORD_PROPIEDADES nWord, "limpiafinal", "";
     |WORD_PROPIEDADES nWord, "Paginacion", "1";
     |WORD_PROPIEDADES nWord, "Visible", "NO";

     nWord = nWordAnt;
     |WORD_IMPRIME nWord, "";
     |WORD_PROPIEDADES nWord, "Visible", "NO";

     |SLEEP 11;

     |MENSA "      Cerrando Documento";

     |WORD_SALVA nWord, "";

     |SI #reinfcmp#27 = "S";  |HAZ GuardaDocumento;  |FINSI;

     |RFBORRA aAbre1;
|FPRC

|DEFBUCLE Emision;
     #rem00100#1;
     ;
     #reinfcmp#1;
     #reinfcmp#2;
     ;
     NULL, Emision;
|FIN;

|PROCESO Tipo80;  |TIPO 80;
     FSalida = 2499;
|FPRC;

|PROGRAMA;
     |HAZ PreparaInformes;
     |HAZ LeeUnidadBasico;

     |SUB_EJECUTA_NP ":basica/wordm000;CREALOS";

     |DBASS aPath17; |QBF aPath17;
     aPathDef17 = aPath17 + "ren2017/def/";
     aPathFic17 = aPath17 + "ren2017/fich/";

     |DBASS aPath18; |QBF aPath18;
     aPathDoc17 = aPath18 + "basica/word/";

     |PATH_DAT #rem00100#aPathFic17#;
     |PATH_DAT #reacceso#aPathFic17#;

     |CLS;
     |CABEZA "Emision informe comprobacion";
     |PINPA #0#reinfcmp;
     |PINDA #0#reinfcmp;
     |ENDATOS #1#reinfcmp;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |ABRE #reacceso;
     |SI #reinfcmp#0 = "S";
         |BUCLE Emision;
     |SINO;
         |PARA nParrilla = 3; |SI nParrilla [ 23; |HACIENDO nParrilla = nParrilla + 1;
               |SI #reinfcmp#nParrilla# ! 0;
                   #reinfcmp#1 = #reinfcmp#nParrilla#;
                   #reinfcmp#2 = #reinfcmp#nParrilla#;
                   |BUCLE Emision;
               |FINSI;
         |SIGUE;
     |FINSI;
     |CIERRA #reacceso;
|FPRO;
