|FICHEROS;
     reemi102 #0;
     rem01001;
     rem01002;
     rem00100;
     rem00101;
     repoblac;

     rew00106;
     rew00060,MANTE;
     rew04000;
|FIN;

|VARIABLES;
     &aArchivo = "";
     &eaAlfa   = "";

     aComodin = "";
     aFichero = "";

     nCampos  = 0;
     nProvi   = 0;
     nPobla   = 0;
     nFamil   = 0;
     nFamil1  = 0;
     nConta   = 0;
     nSwEstar = 0;
     nDEmpre  = 0;
     nHEmpre  = 0;

     nNumCmps = 0;
     nContCmp = 0;

     nPant0   = 0;
     nPant1   = 0;
     nGrid1   = 0;
     nLabel0  = 0;
     nLabel1  = 0;
     nLabel2  = 0;
     nLabel3  = 0;
     nLabel4  = 0;
     nLabel5  = 0;
     nLabel6  = 0;
     nLabel7  = 0;
     nLabel8  = 0;
     nVent    = 0;

     nRango   = 0;
     nPos1    = 0;
     nPos2    = 0;

     aNom     = "";
     aApel1   = "";
     aApel2   = "";
     aLong    = "";
     aAlfa    = "";
     aAlfa1   = "";
     aAlfa2   = "";
     aChar1   = "";
     aChar2   = "";
     aAlfaA   = "";

     nInd     = 0;
     nLong    = 0;
     nCalc    = 0;
     nCalc1   = 0;
     nPos     = 0;
     SwSalir  = 0;
     nCampo   = 0;
     aResultado = "";

   aEsc1   = "";
   aEsc2   = "";
   aRetu   = "";
   aTecla  = "";

{-1}aVent      = "";
    aVent1     = 0;
    aVent2     = 0;
    aVent3     = 0;
    aVent4     = 0;
    aVent5     = "";
|FIN;

|PROCESO Final;
   |PTEC 801;
|FPRC;

|PROCESO Tipo11w60;  |TIPO 11;
     |SI FSalida ! 10;  |ACABA;  |FINSI;

     |LEE 101#rew00060.=;
     |SI #rew00060#30 = "N";
         #rew00060#30 = "S";
     |SINO;
         #rew00060#30 = "N";
     |FINSI;
     |PINTA #rew00060#30;
     |GRABA 020#rew00060.a;
     |LIBERA #rew00060;
|FPRC;

|PROCESO Seleccion; |TIPO 0;
  |SI #reemi102#0 = "S";
      |CAMPO_MODIFICA #reemi102#01, 1, "S";
      |CAMPO_MODIFICA #reemi102#02, 1, "S";

      |PARA nCampos = 3; |SI nCampos [ 32; |HACIENDO nCampos = nCampos + 1;
            #0nCampos = 0; |PINTA #0nCampos;
            |CAMPO_MODIFICA #0nCampos, 1, "N";
      |SIGUE;
  |SINO;
      #reemi102#1 = 00001; |PINTA #reemi102#1;    |CAMPO_MODIFICA #reemi102#01, 1, "N";
      #reemi102#2 = 99999; |PINTA #reemi102#2;    |CAMPO_MODIFICA #reemi102#02, 1, "N";

      |PARA nCampos = 3; |SI nCampos [ 32; |HACIENDO nCampos = nCampos + 1;
            |CAMPO_MODIFICA #0nCampos, 1, "S";
      |SIGUE;
  |FINSI;
|FPRC;

|PROCESO LeeProvi;
     #repoblac#0 = nProvi;
     #repoblac#1 = nPobla;
     |LEE 040#repoblac.=;
     |SI FSalida ! 0;  |DDEFECTO #repoblac;  |FINSI;
|FPRC;

|PROCESO SeparaNombre;
   aNom = ""; aApel1 = ""; aApel2 = "";
   nInd = 1;
   aLong = aAlfa % 0; nLong = aLong;
   aAlfa1 = "";
   |PARA nCalc = 1; |SI nCalc [ nLong; |HACIENDO nCalc = nCalc + 1;
      nPos = (nCalc * 100) + 1;
      aAlfa2 = aAlfa % nPos;
      |SI aAlfa2 = " "; |O aAlfa2 = ",";
         |SI aAlfa1 ! "DE";
          |Y aAlfa1 ! "LA";
          |Y aAlfa1 ! "LAS";
          |Y aAlfa1 ! "LOS";
          |Y aAlfa1 ! "DEL";
            |SI nInd = 1; aApel1 = aApel1 + aAlfa1 + " "; |FINSI;
            |SI nInd = 2; aApel2 = aApel2 + aAlfa1 + " "; |FINSI;
            |SI nInd ] 3; aNom   = aNom   + aAlfa1 + " "; |FINSI;
             nInd = nInd + 1;
         |SINO;
            |SI nInd > 1; nInd = nInd - 1; |FINSI;
            |SI nInd = 1; aApel1 = aApel1 + aAlfa1 + " "; |FINSI;
            |SI nInd = 2; aApel2 = aApel2 + aAlfa1 + " "; |FINSI;
            |SI nInd ] 3; aNom   = aNom   + aAlfa1 + " "; |FINSI;
         |FINSI;
         aAlfa1 = "";
      |SINO;
         aAlfa1 = aAlfa1 + aAlfa2;
      |FINSI;
   |SIGUE;

   |PARA; |SI SwSalir = 0; |HACIENDO;
      SwSalir = 1;
      aAlfa1 = aApel1 % 0101;
      |SI aAlfa1 = " "; aApel1 = aApel1 % 0299; SwSalir = 0; |FINSI;
      aAlfa1 = aApel2 % 0101;
      |SI aAlfa1 = " "; aApel2 = aApel2 % 0299; SwSalir = 0; |FINSI;
      aAlfa1 = aNom   % 0101;
      |SI aAlfa1 = " "; aNom   = aNom   % 0299; SwSalir = 0; |FINSI;
   |SIGUE;
|FPRC;

|PROCESO MiraPago;
   |SI #reemi102#36 = "E";
      #rew00060#22 = "X";
      #rew00060#23 = " ";
      #rew00060#37 = "    ";
      #rew00060#38 = "    ";
      #rew00060#39 = "    ";
      #rew00060#40 = "    ";
      #rew00060#41 = "    ";
      #rew00060#42 = "    ";
   |SINO;
      #rew00060#22 = " ";
      #rew00060#23 = "X";

      |SI #rew00060#1 = "TITULAR   "; aAlfa = #rem00100#147; |FINSI;
      |SI #rew00060#1 = "CONYUGE   "; aAlfa = #rem00100#148; |FINSI;
      |SI #rew00060#1 = "CONJUNTA  "; aAlfa = #rem00100#149; |FINSI;
      |SI #rew00060#1 = "CONJUNTAE "; aAlfa = #rem00100#149; |FINSI;

      aAlfa1 = #rew00060#1; |QBF aAlfa1;
      aAlfa1 = aAlfa1 - "FAMILIAR";
      |SI FSalida ! 0;
          aAlfa = #rem00101#10;
      |FINSI;
      |QBF aAlfa;
      |SI aAlfa = "";
         #rew00060#22 = "X";
         #rew00060#23 = " ";
         #rew00060#37 = "    ";
         #rew00060#38 = "    ";
         #rew00060#39 = "    ";
         #rew00060#40 = "    ";
         #rew00060#41 = "    ";
         #rew00060#42 = "    ";
      |SINO;
         aAlfa1 = aAlfa % 0104; #rew00060#37 = aAlfa1;
         aAlfa1 = aAlfa % 0504; #rew00060#38 = aAlfa1;
         aAlfa1 = aAlfa % 0904; #rew00060#39 = aAlfa1;
         aAlfa1 = aAlfa % 1304; #rew00060#40 = aAlfa1;
         aAlfa1 = aAlfa % 1704; #rew00060#41 = aAlfa1;
         aAlfa1 = aAlfa % 2104; #rew00060#42 = aAlfa1;
      |FINSI;
   |FINSI;
|FPRC;

|PROCESO GrabaTempo;
     |SI #rem01001#10 = "S";  |ACABA;  |FINSI;

     #rem01002#0 = #rem01001#0;
     #rem01002#1 = #rem01001#1;
     #rem01002#2 = #rem01001#2;
     #rem01002#3 = 43;
     #rem01002#5 = "0670";
     |LEE 040#rem01002.=;
     |SI FSalida ! 0;  |DDEFECTO #rem01002;  |FINSI;

     |SI #rem01002#7 [ 0;  |ACABA;  |FINSI;

     #rem00100#0 = #rem01001#0;
     |LEE 040#rem00100.=;
     |SI FSalida ! 0;  |DDEFECTO #rem00100;  |FINSI;

     |DDEFECTO #rew00060;
     #rew00060#0  = #rem01001#0;
     #rew00060#1  = #rem01001#2;
     #rew00060#2  = #rem00100#71;
     #rew00060#3  = #rem00100#72;
     #rew00060#4  =  (("00" + #rem00100#69) % -102) + (("000" + #rem00100#70) % -103);
     #rew00060#13 = #rem00100#68;

     |SI #rew00060#1 = "CONJUNTAE ";
         |SI #rem00100#91 = "C";
             nProvi = #rem00100#20;
             nPobla = #rem00100#21;
             |HAZ LeeProvi;

             #rew00060#5  = #rem00100#1;
             #rew00060#6  = #rem00100#5;
             #rew00060#7  = #rem00100#14;
             #rew00060#8  = #rem00100#15;
             #rew00060#9  = #rem00100#16;
             #rew00060#10 = #rem00100#17;
             #rew00060#11 = #rem00100#18;
             #rew00060#12 = #rem00100#19;
             #rew00060#14 = #rem00100#22;
             #rew00060#15 = #repoblac#2;
             #rew00060#16 =  (("00" + #rem00100#20) % -102) + (("000" + #rem00100#21) % -103);
             #rew00060#17 = #rem00100#35;
             #rew00060#18 = #rem00100#39;

             |HAZ MiraPago;

             #rew00060#31 = #rem00100#2; #rew00060#34 = #rem00100#36;
             #rew00060#32 = #rem00100#3; #rew00060#35 = #rem00100#37;
             #rew00060#33 = #rem00100#4; #rew00060#36 = #rem00100#38;
         |SINO;
             nProvi = #rem00100#50;
             nPobla = #rem00100#51;
             |HAZ LeeProvi;

             #rew00060#5  = #rem00100#35;
             #rew00060#6  = #rem00100#39;
             #rew00060#7  = #rem00100#44;
             #rew00060#8  = #rem00100#45;
             #rew00060#9  = #rem00100#46;
             #rew00060#10 = #rem00100#47;
             #rew00060#11 = #rem00100#48;
             #rew00060#12 = #rem00100#49;
             #rew00060#14 = #rem00100#52;
             #rew00060#15 = #repoblac#2;
             #rew00060#16 =  (("00" + #rem00100#50) % -102) + (("000" + #rem00100#51) % -103);
             #rew00060#17 = #rem00100#1;
             #rew00060#18 = #rem00100#5;

             |HAZ MiraPago;

             #rew00060#31 = #rem00100#36; #rew00060#34 = #rem00100#2;
             #rew00060#32 = #rem00100#37; #rew00060#35 = #rem00100#3;
             #rew00060#33 = #rem00100#38; #rew00060#36 = #rem00100#4;
         |FINSI;
     |FINSI;

     |SI #rew00060#1 = "TITULAR   ";  |O #rew00060#1 = "CONJUNTA  ";
         nProvi = #rem00100#20;
         nPobla = #rem00100#21;
         |HAZ LeeProvi;

         #rew00060#5  = #rem00100#1;
         #rew00060#6  = #rem00100#5;
         #rew00060#7  = #rem00100#14;
         #rew00060#8  = #rem00100#15;
         #rew00060#9  = #rem00100#16;
         #rew00060#10 = #rem00100#17;
         #rew00060#11 = #rem00100#18;
         #rew00060#12 = #rem00100#19;
         #rew00060#14 = #rem00100#22;
         #rew00060#15 = #repoblac#2;
         #rew00060#16 =  (("00" + #rem00100#20) % -102) + (("000" + #rem00100#21) % -103);
         #rew00060#17 = #rem00100#35;
         #rew00060#18 = #rem00100#39;

         |HAZ MiraPago;

         #rew00060#31 = #rem00100#2; #rew00060#34 = #rem00100#36;
         #rew00060#32 = #rem00100#3; #rew00060#35 = #rem00100#37;
         #rew00060#33 = #rem00100#4; #rew00060#36 = #rem00100#38;
     |FINSI;

     |SI #rew00060#1 = "CONYUGE   ";
         nProvi = #rem00100#50;
         nPobla = #rem00100#51;
         |HAZ LeeProvi;

         #rew00060#5  = #rem00100#35;
         #rew00060#6  = #rem00100#39;
         #rew00060#7  = #rem00100#44;
         #rew00060#8  = #rem00100#45;
         #rew00060#9  = #rem00100#46;
         #rew00060#10 = #rem00100#47;
         #rew00060#11 = #rem00100#48;
         #rew00060#12 = #rem00100#49;
         #rew00060#14 = #rem00100#52;
         #rew00060#15 = #repoblac#2;
         #rew00060#16 =  (("00" + #rem00100#50) % -102) + (("000" + #rem00100#51) % -103);
         #rew00060#17 = #rem00100#1;
         #rew00060#18 = #rem00100#5;

         |HAZ MiraPago;

         #rew00060#31 = #rem00100#36; #rew00060#34 = #rem00100#2;
         #rew00060#32 = #rem00100#37; #rew00060#35 = #rem00100#3;
         #rew00060#33 = #rem00100#38; #rew00060#36 = #rem00100#4;
     |FINSI;

     aComodin = #rew00060#1 - "FAMILIAR";
     |SI FSalida ! 0;
         |QBF aComodin;
         nFamil = aComodin;
         nProvi = #rem00100#20;
         nPobla = #rem00100#21;
         |HAZ LeeProvi;

         nFamil1 = 0;
         |PARA nConta = 1; |SI nConta [ 100;  |HACIENDO nConta = nConta + 1;;
               #rem00101#0 = #rem00100#0;
               #rem00101#1 = nConta;
               |LEE 040#rem00101.=;
               |SI FSalida = 0;  |Y #rem00101#13 = "S";
                   nFamil1 = nFamil1 + 1;
               |FINSI;
               |SI nFamil = nFamil1;
                   #rew00060#5  = #rem00101#2;
                   #rew00060#6  = #rem00101#3;
                   |SAL;
               |FINSI;
         |SIGUE;

         #rew00060#7  = #rem00100#14;
         #rew00060#8  = #rem00100#15;
         #rew00060#9  = #rem00100#16;
         #rew00060#10 = #rem00100#17;
         #rew00060#11 = #rem00100#18;
         #rew00060#12 = #rem00100#19;
         #rew00060#14 = #rem00100#22;
         #rew00060#15 = #repoblac#2;
         #rew00060#16 =  (("00" + #rem00100#20) % -102) + (("000" + #rem00100#21) % -103);

         |HAZ MiraPago;

         aAlfa  = #rew00060#6;  |HAZ SeparaNombre;
         #rew00060#31 = aNom;   #rew00060#34 = "";
         #rew00060#32 = aApel1; #rew00060#35 = "";
         #rew00060#33 = aApel2; #rew00060#36 = "";
     |FINSI;

     #rew00060#19   = #rem01001#16;
     #rew00060#20   = #rem01001#16 % 60;
     #rew00060#21   = #rem01001#16 % 40;

     #rew00060#28   = #reemi102#33 % 102;
     aComodin = #reemi102#33 % 402;
     |SI aComodin = "01";  #rew00060#29 = "ENERO     ";  |FINSI;
     |SI aComodin = "02";  #rew00060#29 = "FEBRERO   ";  |FINSI;
     |SI aComodin = "03";  #rew00060#29 = "MARZO     ";  |FINSI;
     |SI aComodin = "04";  #rew00060#29 = "ABRIL     ";  |FINSI;
     |SI aComodin = "05";  #rew00060#29 = "MAYO      ";  |FINSI;
     |SI aComodin = "06";  #rew00060#29 = "JUNIO     ";  |FINSI;
     |SI aComodin = "07";  #rew00060#29 = "JULIO     ";  |FINSI;
     |SI aComodin = "08";  #rew00060#29 = "AGOSTO    ";  |FINSI;
     |SI aComodin = "09";  #rew00060#29 = "SEPTIEMBRE";  |FINSI;
     |SI aComodin = "10";  #rew00060#29 = "OCTUBRE   ";  |FINSI;
     |SI aComodin = "11";  #rew00060#29 = "NOVIEMBRE ";  |FINSI;
     |SI aComodin = "12";  #rew00060#29 = "DICIEMBRE ";  |FINSI;

     |SI #reemi102#34 = "N";  #rew00060#30 = "S";  |FINSI;

     |GRABA 020#rew00060.n;
     |LIBERA #rew00060;
     nSwEstar = 1;
|FPRC;

|DEFBUCLE rem01001;
     #rem01001#1;
     5, 14, 8, 10;
     nDEmpre, 00, "          ", "100", "   ", "S", "N";
     nHEmpre, 00, "zzzzzzzzzz", "101", "   ", "S", "S";
     ;
     NULL, GrabaTempo;
|FIN;

|RUTINA ClaveBaja99;
     #rew00060#0 = 1;
     #rew00060#1 = "          ";
|FRUT;

|RUTINA ClaveAlta99;
     #rew00060#0 = 99999;
     #rew00060#1 = "zzzzzzzzzz";
|FRUT;

|PROCESO Evto99;
   |SI FSalida = 1; |O FSalida = 2;
       |LEE 000#rew00060.c;
   |FINSI;

   |SI FSalida = 4;
      |SI #rew00060#30 = "N";
         #rew00060#30 = "S";
      |SINO;
         #rew00060#30 = "N";
      |FINSI;
      |GRABA 020#rew00060.a;
      |PINTA #rew00060#30;
   |FINSI;
|FPRC;

|PROCESO CargaTempo;
     nSwEstar = 0;

     |ABRE #rem01002;  |SELECT #2#rem01002;
     |ABRE #rem00100;
     |ABRE #rem00101;
     |ABRE #repoblac;
     |SI #reemi102#0 = "S";
         nDEmpre = #reemi102#1;   nHEmpre = #reemi102#2;
         |BUCLE rem01001;
     |SINO;
         |PARA nCampos = 3; |SI nCampos [ 32; |HACIENDO nCampos = nCampos + 1;
               |SI #0nCampos ! 0;
                   nDEmpre = #0nCampos;
                   nHEmpre = #0nCampos;
                   |BUCLE rem01001;
               |FINSI;
         |SIGUE;
     |FINSI;
     |CIERRA #rem01002;
     |CIERRA #rem00100;
     |CIERRA #rem00101;
     |CIERRA #repoblac;

     |SI nSwEstar = 0; |ACABA; |FINSI;

     |SI #reemi102#34 = "N"; |ACABA; |FINSI;

     ||ENTLINEAL #rem01001#rew00060, -1, 4, 22, 1, ClaveBaja99, ClaveAlta99;
|FPRC;

|PROCESO Imprime;
     |ABRE #rem00100;
     #rem00100#0 = #rew00060#0;
     |LEE 040#rem00100.=;
     |SI FSalida ! 0;  |DDEFECTO #rem00100;  |FINSI;
     |CIERRA #rem00100;

     |HAZ DatosModelo;

     aAlfa = " Emitiendo renta cliente [" + (("000000" + #rew00060#0) % -106) + "] ";
     |VENTANA 0821, 1881, -1, 17 , aAlfa; nVent = FSalida;
     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |PINPA #0#rew04000; nPant1 = FSalida;

     aAlfa = "LABEL,{{15}}Contribuyente [" + #rew00060#1 + "] ";
     nPos1 = 0605; nPos2 = 0650;
     |CONTROL_SIMPLE nPant1, aAlfa, nPos1, nPos2, NULL; nLabel0 = FSalida;

     aAlfa = "LABEL,{{15}}NIF " + #rew00060#5;
     nPos1 = 0705; nPos2 = 0750;
     |CONTROL_SIMPLE nPant1, aAlfa, nPos1, nPos2, NULL; nLabel1 = FSalida;

     aAlfa = "LABEL,{{15}}" + #rew00060#6;
     nPos1 = 0805; nPos2 = 0850;
     |CONTROL_SIMPLE nPant1, aAlfa, nPos1, nPos2, NULL; nLabel2 = FSalida;

     nPos1 = 1105; nPos2 = 1160;
     aAlfa = "LABEL,{{15}}Cuando el modelo aparezca en pantalla, pulse en el botón";
     |CONTROL_SIMPLE nPant1, aAlfa, nPos1, nPos2, NULL; nLabel3 = FSalida;
     nPos1 = 1205; nPos2 = 1260;
     aAlfa = "LABEL,{{15}} 'Rellenar formulario' para que genere el código de barras";
     |CONTROL_SIMPLE nPant1, aAlfa, nPos1, nPos2, NULL; nLabel4 = FSalida;
     nPos1 = 1305; nPos2 = 1360;
     aAlfa = "LABEL,{{15}}identificador y para que aparezcan los datos.";
     |CONTROL_SIMPLE nPant1, aAlfa, nPos1, nPos2, NULL; nLabel6 = FSalida;
     nPos1 = 1505; nPos2 = 1560;
     aAlfa = "LABEL,{{15}}PULSE UNA TECLA PARA CONTINUAR.";
     |CONTROL_SIMPLE nPant1, aAlfa, nPos1, nPos2, NULL; nLabel7 = FSalida;

     |PAUSA;

     |FIN_CONTROL_WINDOWS nLabel0; |FIN_CONTROL_WINDOWS nLabel1;
     |FIN_CONTROL_WINDOWS nLabel2; |FIN_CONTROL_WINDOWS nLabel3;
     |FIN_CONTROL_WINDOWS nLabel4; |FIN_CONTROL_WINDOWS nLabel5;
     |FIN_CONTROL_WINDOWS nLabel6; |FIN_CONTROL_WINDOWS nLabel7;

     aVent4 = nVent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;
     |FINVENTANA nVent;
|FPRC;

|DEFBUCLE rew00060;
     #rew00060#1;
     30;
     00001, "          ", "S";
     99999, "zzzzzzzzzz", "S";
     ;
     NULL, Imprime;
|FIN;

|PROCESO Ptec807;
     |PTEC 807;
|FPRC;

|PROCESO Tipo3;  |TIPO 3;
     aFichero = "p2" + Usuario;
     |NOME_DAT #rew00060#aFichero#;
     |ABRE #rew00060;  |CIERRA #rew00060;  |DELINDEX #rew00060;
     |ABRE #rew00060;

     |HAZ CargaTempo;

     |LEE 000#rew00060.p;
     |SI FSalida ! 0;
        |MENAV "    Seleccion vacia";
        |CIERRA #rew00060; |DELINDEX #rew00060;
        |ACABA;
     |FINSI;

     |REFRESCACONTROL nGrid1;

     nPos1 = 1302; nPos2 = 1398;
     |CONTROL_SIMPLE nPant0, "LABEL,{{15}}Doble click sobre la linea para emitir/no emitir", nPos1, nPos2, NULL;
     nLabel8 = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,{{001}}Cancelar", 2470, 2480, Ptec807;

     aEsc1  = &255 + "806";
     aEsc2  = &255 + "807";
     aRetu  = &255 + "802";
     |PARA; |SI;  |HACIENDO;
         FSalida = "::{{001}}Emitir," + nGrid1;
         |LEETECLA aTecla;
         |SI aTecla = aEsc1; |SAL; |FINSI;
         |SI aTecla = aEsc2; nSwEstar = 0;  |SAL; |FINSI;
         |SI aTecla = aRetu; |SAL; |FINSI;
     |SIGUE;

     |SI nSwEstar = 1;
         |BUCLE rew00060;
     |FINSI;

     |CIERRA #rew00060; |DELINDEX #rew00060;
|FPRC;

|PROCESO Tipo8; |TIPO 8;
   ||MENAV "    En espera de los modulos de impresion"; |PTEC 807; |ACABA;
|FPRC;

|PROCESO DatosModelo;
   aAlfa1 = Usuario; |QBT aAlfa1;
   aAlfa1 = ("c1" + aAlfa1) % 108;
   |NOME_DAT #rew00106#aAlfa1#;
   |DELINDEX #rew00106; |ABRE #rew00106;

   || Grabamos los tres ficheros que participan de la emision:
   ||    reemi102, rem00100 y rew00060

   aAlfa = "|NC"; aAlfa = #reemi102#aAlfa#;
   nNumCmps = aAlfa; nNumCmps = nNumCmps - 1;
   |PARA nContCmp = 0; |SI nContCmp [ nNumCmps; |HACIENDO nContCmp = nContCmp + 1;
      |DDEFECTO #rew00106;
      #rew00106#0 = "reemi102";
      #rew00106#1 = nContCmp;
      aAlfa = "|C" + (("000" + nContCmp) % -103) + "TIPO";
      aAlfa = #reemi102#aAlfa#;
      #rew00106#3 = aAlfa;
      |SI aAlfa = "A";
         eaAlfa = #reemi102#nContCmp#;
         |HAZ QuitaRaros;
         #reemi102#nContCmp# = eaAlfa;
      |FINSI;
      #rew00106#2 = #reemi102#nContCmp#;
      |GRABA 020#rew00106.n;
   |SIGUE;

   aAlfa = "|NC"; aAlfa = #rem00100#aAlfa#;
   nNumCmps = aAlfa; nNumCmps = nNumCmps - 1;
   |PARA nContCmp = 0; |SI nContCmp [ nNumCmps; |HACIENDO nContCmp = nContCmp + 1;
      |DDEFECTO #rew00106;
      #rew00106#0 = "rem00100";
      #rew00106#1 = nContCmp;
      aAlfa = "|C" + (("000" + nContCmp) % -103) + "TIPO";
      aAlfa = #rem00100#aAlfa#;
      #rew00106#3 = aAlfa;
      |SI aAlfa = "A";
         eaAlfa = #rem00100#nContCmp#;
         |HAZ QuitaRaros;
         #rem00100#nContCmp# = eaAlfa;
      |FINSI;
      #rew00106#2 = #rem00100#nContCmp#;
      |GRABA 020#rew00106.n;
   |SIGUE;

   aAlfa = "|NC"; aAlfa = #rew00060#aAlfa#;
   nNumCmps = aAlfa; nNumCmps = nNumCmps - 1;
   |PARA nContCmp = 0; |SI nContCmp [ nNumCmps; |HACIENDO nContCmp = nContCmp + 1;
      |DDEFECTO #rew00106;
      #rew00106#0 = "rew00060";
      #rew00106#1 = nContCmp;
      aAlfa = "|C" + (("000" + nContCmp) % -103) + "TIPO";
      aAlfa = #rew00060#aAlfa#;
      #rew00106#3 = aAlfa;
      |SI aAlfa = "A";
         eaAlfa = #rew00060#nContCmp#;
         |HAZ QuitaRaros;
         #rew00060#nContCmp# = eaAlfa;
      |FINSI;
      #rew00106#2 = #rew00060#nContCmp#;
      |GRABA 020#rew00106.n;
   |SIGUE;

   |CIERRA #rew00106;
   aAlfa = "rezemite.tab;c1" + Usuario + ",102";
   |SUB_EJECUTA aAlfa;
   |DELINDEX #rew00106;
|FPRC;

|PROCESO Tipo80emi102; |TIPO 80;
   FSalida = 2499;
|FPRC;

|PROGRAMA;
   |CLS;
   |CABEZA "Emision modelo 102";

   |PINPA #0#reemi102; nPant0 = FSalida; |PINDA #0#reemi102;

   nRango = 4 + 8 + 16 + 32;
   nPos1 = 1402; nPos2 = 2398;
   |LINEAL_SIMPLE #1#rew00060, nRango, nPos1, nPos2, ClaveBaja99, ClaveAlta99, Evto99;
   nGrid1 = FSalida;

   |ENDATOS #1#0;
   |SI FSalida = 0;
      |HAZ Tipo3;
   |FINSI;
   |DESTRUYECONTROL nGrid1;
|FPRO;
