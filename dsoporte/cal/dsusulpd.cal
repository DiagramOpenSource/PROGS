|FICHEROS;
     dsusulpd #1;
     dsorm015 #15;
     dsprem04 #4;

     dsusuw01 #901,MANTE;

     dsusulpd@ #900;
|FIN;

|VARIABLES;
     aEsc                = "";
     aRetu               = "";
     aTecla              = "";
     aEsc1               = "";
     aEsc2               = "";
     aFichero            = "";
     aAlfa               = "";
     aAbre               = "";
     aBase               = "";
     aUsuario            = "";
     aNombre             = "";
     aMas                = "";

     nVentana            = 0;
     nVent901            = 0;
     nGrid1              = 0;
     nGrid900            = 0;
     nRango              = -1;
     nHandle             = -1;
     nBoton1             = -1;
     nBoton2             = -1;
     nBoton3             = -1;
     nBoton4             = -1;
     nBoton5             = -1;
     nPosi               = -1;

     {-1}sTree        = 0;
         sT_nID       = 0;
         sT_nOper     = 0;
         sT_aData     = "";

     {-1}aVent        = "";
         aVent1       = 0;
         aVent2       = 0;
         aVent3       = 0;
         aVent4       = 0;
         aVent5       = "";
|FIN;

|PROCESO Editar;
     |LEE 101#1=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |VENTANA 0501, 1999, -1, 16, "Edici¢n del usuario";
     nVent901 = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVent901;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |DDEFECTO #901;

     #901#0  = #1#0;
     #901#1  = #1#1;
     #901#2  = #1#2;
     #901#3  = #1#3;
     #901#4  = #1#4;
     #901#5  = #1#5;
     #901#6  = #1#6;

     |PINPA #0#901;
     |PINDA #0#901;

     |ENDATOS #1#901;
     |SI FSalida = 0;
         #1#0 = #901#0;
         #1#1 = #901#1;
         #1#2 = #901#2;
         #1#3 = #901#3;
         #1#4 = #901#4;
         #1#5 = #901#5;
         #1#6 = #901#6;
         |GRABA 020#1a;
     |FINSI;
     |LIBERA #1;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVent901;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA nVent901;

     |REFRESCACONTROL nGrid1;    |PULSATECLA;
     |FOCOTECLADO nGrid1;
|FPRC;

|PROCESO Evento900;
     |SI FSalida = 1;  |O FSalida = 2;
         |ESTADO_CONTROL nBoton1, "ENABLE";
         |ESTADO_CONTROL nBoton3, "ENABLE";

         |LEE 000#900=;
         |SI FSalida ! 0;
             |ESTADO_CONTROL nBoton1, "DISABLE";
             |ESTADO_CONTROL nBoton3, "DISABLE";
         |FINSI;

         |ESTADO_CONTROL nBoton2, "DISABLE";
         |ESTADO_CONTROL nBoton4, "ENABLE";
         |ESTADO_CONTROL nBoton5, "DISABLE";

         |LEE 000#1p;
         |SI FSalida ! 0;
             |ESTADO_CONTROL nBoton4, "DISABLE";
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO Evento1;
     |SI FSalida = 1;  |O FSalida = 2;
         |ESTADO_CONTROL nBoton2, "ENABLE";
         |ESTADO_CONTROL nBoton4, "ENABLE";
         |ESTADO_CONTROL nBoton5, "ENABLE";

         |LEE 000#1=;
         |SI FSalida ! 0;
             |ESTADO_CONTROL nBoton2, "DISABLE";
             |ESTADO_CONTROL nBoton4, "DISABLE";
             |ESTADO_CONTROL nBoton5, "DISABLE";
         |FINSI;

         |ESTADO_CONTROL nBoton1, "DISABLE";
         |ESTADO_CONTROL nBoton3, "ENABLE";

         |LEE 000#900p;
         |SI FSalida ! 0;
             |ESTADO_CONTROL nBoton3, "DISABLE";
         |FINSI;
     |FINSI;

     |SI FSalida = 4;
         |HAZ Editar;
     |FINSI;
|FPRC;

|PROCESO UnoAbajo;
     |LEE 101#900=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     #1#0 = #900#0;
     #1#1 = #900#1;
     |GRABA 020#1n;
     |LIBERA #1;

     |BORRA 020#900a;
     |LIBERA #900;

     aUsuario = #1#0;
     |REFRESCACONTROL nGrid1;    |PULSATECLA;

     #900#0 = aUsuario;
     |LEE 000#900];

     |REFRESCACONTROL nGrid900;  |PULSATECLA;
     |FOCOTECLADO nGrid900;
|FPRC;

|PROCESO UnoArriba;
     |LEE 101#1=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     #900#0 = #1#0;
     #900#1 = #1#1;
     #900#2 = #1#2;
     #900#3 = #1#3;
     #900#4 = #1#4;
     #900#5 = #1#5;
     #900#6 = #1#6;
     |GRABA 020#900n;
     |LIBERA #900;

     |BORRA 020#1a;
     |LIBERA #1;

     aUsuario = #900#0;
     |REFRESCACONTROL nGrid900;  |PULSATECLA;

     #1#0 = aUsuario;
     |LEE 000#1];

     |REFRESCACONTROL nGrid1;    |PULSATECLA;
     |FOCOTECLADO nGrid1;
|FPRC;

|PROCESO dsusulpdAbajo;
     #1#0 = #900#0;
     #1#1 = #900#1;
     #1#2 = #900#2;
     #1#3 = #900#3;
     #1#4 = #900#4;
     #1#5 = #900#5;
     #1#6 = #900#6;
     |GRABA 020#1n;
     |LIBERA #1;

     |BORRA 020#900a;
     |LIBERA #900;
|FPRC;

|DEFBUCLE dsusulpdAbajo;
     #900#1001;
     ;
     "          ";
     "zzzzzzzzzz";
     be;
     NULL, dsusulpdAbajo;
|FIN;

|PROCESO TodosAbajo;
     |BUCLE dsusulpdAbajo;

     |LEE 000#900];

     |REFRESCACONTROL nGrid1;     |PULSATECLA;
     |REFRESCACONTROL nGrid900;   |PULSATECLA;

     |FOCOTECLADO nGrid900;
|FPRC;

|PROCESO dsusulpdArriba;
     #900#0 = #1#0;
     #900#1 = #1#1;
     #900#2 = #1#2;
     #900#3 = #1#3;
     #900#4 = #1#4;
     #900#5 = #1#5;
     #900#6 = #1#6;
     |GRABA 020#900n;
     |LIBERA #900;

     |BORRA 020#1a;
     |LIBERA #1;
|FPRC;

|DEFBUCLE dsusulpdArriba;
     #1#1;
     ;
     INICIO;
     FINAL;
     be;
     NULL, dsusulpdArriba;
|FIN;

|PROCESO TodosArriba;
     |BUCLE dsusulpdArriba;

     |LEE 000#1];

     |REFRESCACONTROL nGrid900;   |PULSATECLA;
     |REFRESCACONTROL nGrid1;     |PULSATECLA;

     |FOCOTECLADO nGrid1;
|FPRC;

|PROCESO CogeNombre;
     |SELECT #2#15;
     #15#1 = #900#0;
     |LEE 000#15=;
     |SI FSalida ! 0;  |DDEFECTO #15;  |FINSI;

     aNombre = #15#2;   |QBF aNombre;
     |SI aNombre ! "";
         #900#1 = aNombre;
         |ACABA;
     |FINSI;

     |SELECT #2#4;
     #4#5 = #900#0;
     |LEE 000#4=;
     |SI FSalida ! 0;  |DDEFECTO #4;  |FINSI;

     aNombre = #4#6;   |QBF aNombre;
     |SI aNombre ! "";
         #900#1 = aNombre;
     |FINSI;
|FPRC;

|PROCESO RecogeUsuarios;
     |ABRE #15;
     |ABRE #4;

     |DBASS aBase; |QBF aBase;
     aAbre = aBase + "dev/usr/ds_sys.psw"
     |XABRE "U", aAbre, nHandle;
     |SI FSalida ] 0;
         |PARA; |SI; |HACIENDO;
              |XLEE nHandle, 37, aAlfa;
              |SI FSalida < 0;  |SAL;  |FINSI;

              aUsuario  = aAlfa % 410;

              |DDEFECTO #900;
              #900#0    = aUsuario;

              |HAZ CogeNombre;

              |GRABA 020#900n;
              |LIBERA #900;
         |SIGUE;
     |FINSI;
     |XCIERRA nHandle;

     |CIERRA #15;
     |CIERRA #4;
|FPRC;

|PROCESO dsusulpd;
     #900#0 = #1#0;
     |LEE 101#900=;
     |SI FSalida = 0;
         |BORRA 020#900a;
         |LIBERA #900;
         |ACABA;
     |FINSI;

     |BORRA 020#1a;
     |LIBERA #1;
|FPRC;

|DEFBUCLE dsusulpdC;
     #1#1;
     ;
     INICIO;
     FINAL;
     be;
     NULL, dsusulpd;
|FIN;

|PROCESO ChequeaUsuarios;
     |BUCLE dsusulpdC;
|FPRC;

|PROGRAMA;
     |VENTANA 0501, 3599, -1, 16,  "Usuarios LPD";
     nVentana = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVentana;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |DDEF aMas;
     aMas = aMas + "dsusulpd.mas";
     |CARGA_DEF aMas, dsusulpd@;

     aFichero = ("A1" + Usuario + "zzzzzzzz") % 108;
     |NOME_DAT #900 aFichero;
     |ABRE #900;  |CIERRA #900;  |DELINDEX #900;

     |ABRE #900;
     |ABRE #1;

     |HAZ RecogeUsuarios;
     |HAZ ChequeaUsuarios;

     |CONTROL_SIMPLE 0, "LABEL,{{6}}Usuarios a seleccionar", 0502, 0540, NULL;
     |CONTROL_SIMPLE 0, "LABEL,{{6}}Usuarios LPD", 2002, 2040, NULL;
     |CONTROL_SIMPLE 0, "LABEL,{{10}}Usuario seleccionado", 1935, 2049, NULL;
     |CONTROL_SIMPLE 0, "LABEL,{{10}}Todos los usuarios", 1968, 2081, NULL;

     |CONTROL_SIMPLE 0, "BOTON,{{232}}", 1952, 2056, UnoAbajo;
     nBoton1 = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,{{231}}", 1960, 2064, UnoArriba;
     nBoton2 = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,{{235}}", 1984, 2088, TodosAbajo;
     nBoton3 = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,{{234}}", 1992, 2096, TodosArriba;
     nBoton4 = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,{{197}}Editar", 3402, 3410, Editar;
     nBoton5 = FSalida;


     |ESTADO_CONTROL nBoton1, "DISABLE";
     |ESTADO_CONTROL nBoton2, "DISABLE";
     |ESTADO_CONTROL nBoton3, "DISABLE";
     |ESTADO_CONTROL nBoton4, "DISABLE";
     |ESTADO_CONTROL nBoton5, "DISABLE";

     |PULSATECLA;

     nRango = 4 + 8 + 16 + 32 + 128;

     |LINEAL_SIMPLE #1#900, nRango, 0602, 1898, NULL, NULL, Evento900;
     nGrid900 = FSalida;

     |LINEAL_SIMPLE #1#1, nRango, 2102, 3398, NULL, NULL, Evento1;
     nGrid1 = FSalida;

     |PULSATECLA;

     nPosi = 0;

     |LEE 000#1p;
     |SI FSalida = 0;
         nPosi = 1;
     |FINSI;

     |SI nPosi = 1;  FSalida = 1;  |HAZ Evento1;    |FINSI;
     |SI nPosi = 0;  FSalida = 1;  |HAZ Evento900;  |FINSI;

     aEsc1      = &255 + "806";
     aEsc2      = &255 + "807";
     aRetu      = &255 + "802";
     |PARA; |SI;  |HACIENDO;
         |SI nPosi = 1;
              FSalida = "::{{001}}Salir," + nGrid1;
         |FINSI;

         |SI nPosi = 0;
              FSalida = "::{{001}}Salir," + nGrid900;
         |FINSI;

         |LEETECLA aTecla;
         |SI aTecla = aEsc1; |SAL;  |FINSI;
         |SI aTecla = aEsc2; |SAL;  |FINSI;
         |SI aTecla = aRetu; |SAL;  |FINSI;
     |SIGUE;

     |DESTRUYECONTROL nGrid900;
     |DESTRUYECONTROL nGrid1;

     |FIN_CONTROL_WINDOWS nBoton1;
     |FIN_CONTROL_WINDOWS nBoton2;
     |FIN_CONTROL_WINDOWS nBoton3;
     |FIN_CONTROL_WINDOWS nBoton4;
     |FIN_CONTROL_WINDOWS nBoton5;

     |PULSATECLA;

     |CIERRA #900;   |DELINDEX #900;
     |DESCARGA_DEF #dsusulpd@;

     |CIERRA #1;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVentana;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA nVentana;
|FPRO;
