|FICHEROS;
     dscurm00 #0;
     dscurm01 #1;
     dscurm08 #8;
     dscurm14 #14;
     dscurw14 #914;

     :/gecochek/def/gecom001 #501;
|FIN;

|VARIABLES;
     nBoton3_1      = 0;
     nBoton3_2      = 0;
     nBoton3_3      = 0;

     nPanta0        = 0;
     nPanta1        = 0;
     nPanta2        = 0;
     nPanta3        = 0;
     nCampoD        = 0;
     nCampoH        = 0;
     nCampoHasta    = 0;
     nCampo1        = 0;
     nVentana       = 0;
     nGrid1         = 0;
     nGrid2         = 0;
     nRango         = 0;
     nPos1          = 0;
     nPos2          = 0;
     nResp          = 0;
     nCampo         = 0;
     nLen           = 0;
     nCampos        = 0;
     nPos           = 0;
     nCurso         = 0;

     aAlfa          = "";
     aCampoI        = "";
     aPathGecochek  = "";
     aBase          = "";

     &enCursoBase   = 0;
     &enCurso       = 0;
     &enModo        = 0;
     &eaUsuario     = "";
|FIN;

|PROCESO BajaPanta3;
     #14#0  = #914#0;
     #14#1  = #914#1;
     #14#2  = "";
|FPRC;

|PROCESO AltaPanta3;
     #14#0  = #914#0;
     #14#1  = #914#1;
     #14#2  = "zzzzzzzzzz";
|FPRC;

|| ------------------------------------------------------
|| Procesos Grabacion del Curso
|| ------------------------------------------------------

|PROCESO GrabaCurso;
     #1#0 = #914#0;
     #1#1 = #914#1;
     |LEE 101#1=;
     |SI FSalida ! 0;
         |DDEFECTO #1;
         #1#0 = #914#0;
         #1#1 = #914#1;
         |GRABA 020#1b;
     |FINSI;

     |PARA nCampoD = 2;  |SI nCampoD [ nCampoHasta;  |HACIENDO nCampoD = nCampoD + 1;
           nCampoH   = nCampoD;
           #1nCampoD = #914nCampoH;
     |SIGUE;
     |GRABA 020#1a;
     |LIBERA #1;
|FPRC;

|| ------------------------------------------------------
|| Procesos del Boton 3_1
|| ------------------------------------------------------

|PROCESO Boton3_1;
     |HAZ GrabaCurso;

     |VENTANA 0501, 2799, -1, -1, "";
     nVentana = FSalida;

     enCursoBase = #914#0;
     enCurso     = #914#1;
     eaUsuario   = "";
     enModo    = 1;
     |SUB_EJECUTA_NP "dscurw15";

     |FINVENTANA nVentana;

     |REFRESCACONTROL nGrid1;
|FPRC;

|| ------------------------------------------------------
|| Procesos del Boton 3_2
|| ------------------------------------------------------

|PROCESO Boton3_2;
     |VENTANA 0501, 2799, -1, -1, "";
     nVentana = FSalida;

     enCursoBase = #914#0;
     enCurso     = #914#1;
     eaUsuario   = #14#2;
     enModo      = 2;
     |SUB_EJECUTA_NP "dscurw15";

     |FINVENTANA nVentana;

     |REFRESCACONTROL nGrid1;
|FPRC;

|| ------------------------------------------------------
|| Procesos del Boton 3_3
|| ------------------------------------------------------

|PROCESO Boton3_3;
     aAlfa = "0000NSeguro de dar de baja a " + #14#2;
     |CONFI aAlfa;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     enCursoBase = #914#0;
     enCurso     = #914#1;
     eaUsuario   = #14#2;
     enModo      = 3;
     |SUB_EJECUTA_NP "dscurw15";

     #14#0 = #914#0;
     #14#1 = #914#1;
     #14#2 = "";
     |LEE 000#14];
     |SI FSalida = 0;  |Y  #14#0 = #914#0;  |Y #14#1 = #914#1;
         |ESTADO_CONTROL nBoton3_2, "ENABLE";
         |ESTADO_CONTROL nBoton3_3, "ENABLE";
     |SINO;
         |ESTADO_CONTROL nBoton3_2, "DISABLE";
         |ESTADO_CONTROL nBoton3_3, "DISABLE";
     |FINSI;

     |REFRESCACONTROL nGrid1;
|FPRC;

|PROCESO EventoPanta3;
     |SI FSalida = 1;
         |ESTADO_CONTROL nBoton3_1, "ENABLE";

         |LEE 000#14=;
         |SI FSalida = 0;  |Y  #14#0 = #914#0;  |Y #14#1 = #914#1;
             |ESTADO_CONTROL nBoton3_2, "ENABLE";
             |ESTADO_CONTROL nBoton3_3, "ENABLE";
         |SINO;
             |ESTADO_CONTROL nBoton3_2, "DISABLE";
             |ESTADO_CONTROL nBoton3_3, "DISABLE";
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO BotonesPanta3;
     |CONTROL_SIMPLE nPanta3, "BOTON,{{193}} Alta", 0777, 0898, Boton3_1;
     nBoton3_1 = FSalida;

     |CONTROL_SIMPLE nPanta3, "BOTON,{{197}} Modificar", 1077, 1198, Boton3_2;
     nBoton3_2 = FSalida;

     |CONTROL_SIMPLE nPanta3, "BOTON,{{199}} Baja ", 1377, 1498, Boton3_3;
     nBoton3_3 = FSalida;

     |ESTADO_CONTROL nBoton3_1, "ENABLE";
     |ESTADO_CONTROL nBoton3_2, "DISABLE";
     |ESTADO_CONTROL nBoton3_3, "DISABLE";
|FPRC;

|PROCESO Curso;  |TIPO 7;
     |SI enModo ! 1;  |ACABA;  |FINSI;

     |PARA nCurso = 1; |SI nCurso [ 100;  |HACIENDO nCurso = nCurso + 1;
           #1#0 = #914#0;
           #1#1 = nCurso;
           |LEE 000#1=;
           |SI FSalida ! 0;  |SAL;  |FINSI;
     |SIGUE;

     |SI nCurso ] 100;
         |MENAV "0000 Tenemos el cupo de mas de 99 Cursos";
         |PTEC 807;
         |ACABA;
     |FINSI;

     #914#1 = nCurso;  |PINTA #914#1;
|FPRC;

|PROCESO Tipo12;  |TIPO 12;
|FPRC;

|PROCESO Tipo80;  |TIPO 80;
     |DBASS aBase;  |QBF aBase;
     aPathGecochek = aBase + "gecochek/fich/";
     |PATH_DAT #501 aPathGecochek;

     |ABRET #14;

     FSalida = 2799;
|FPRC;

|PROCESO Tipo90;  |TIPO 90;
     |CIERRAT #14;
|FPRC;

|PROGRAMA;
     nCampoHasta = 89;

     |ABRE #0;
     #0#0 = enCursoBase;
     |LEE 000#0=;
     |SI FSalida ! 0;  |DDEFECTO #0;  |FINSI;
     |CIERRA #0;

     |ABRE #1;
     |SI enModo ! 1;
         #1#0 = #0#0;
         #1#1 = enCurso;
         |LEE 101#1=;
         |SI FSalida ! 0;  |DDEFECTO #1; |FINSI;

         |PARA nCampoD = 0;  |SI nCampoD [ nCampoHasta;   |HACIENDO nCampoD = nCampoD + 1;
               nCampoH = nCampoD;
               #914nCampoH = #1nCampoD;
         |SIGUE;

         |ABRE #8;
         #8#0 = #914#88;
         |LEE 000#8=;
         |SI FSalida ! 0;  |DDEFECTO #8;  |FINSI;
         |CIERRA #8;

         #914#90 = #8#66;
     |SINO;
         #914#0 = #0#0;
         #914#2 = #0#1;
         #914#3 = #0#2;
         #914#4 = #0#3;

         |PARA nCampoD = 28;  |SI nCampoD [ 87;   |HACIENDO nCampoD = nCampoD + 1;
               nCampoH     = nCampoD - 24;
               #914nCampoD = #0nCampoH;
         |SIGUE;
     |FINSI;

     |CABEZA "Cursos";

     |PINPA #0#914;  nPanta0 = FSalida;
     |PINDA #0#914;

     |PINPA #1#914;  nPanta1 = FSalida;
     |PINDA #1#914;

     |PINPA #2#914;  nPanta2 = FSalida;
     |PINDA #2#914;

     |PINPA #3#914;  nPanta3 = FSalida;
     |PINDA #3#914;

     nRango = 4 + 8 + 16 + 32;
     nPos1  = 0602;
     nPos2  = 2075;

     |LINEAL_SIMPLE #1#14, nRango, nPos1, nPos2, BajaPanta3, AltaPanta3, EventoPanta3;
     nGrid1 = FSalida;

     |HAZ BotonesPanta3;

     |SI enModo = 1;  |ENDATOS #1#914;  |FINSI;
     |SI enModo = 2;  |ENDATOS #2#914;  |FINSI;

     |SI FSalida = 0;
         |HAZ GrabaCurso;
     |FINSI;
     |CIERRA #1;

     |FIN_CONTROL_WINDOWS nBoton3_1;
     |FIN_CONTROL_WINDOWS nBoton3_2;
     |FIN_CONTROL_WINDOWS nBoton3_3;

     |DESTRUYECONTROL nGrid1;

     |PULSATECLA;
|FPRO;
