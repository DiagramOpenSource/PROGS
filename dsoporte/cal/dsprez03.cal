|FICHEROS;
     dsprem01;
     dsprem02;
     dsprem03;
     dsprem04;
     dsprem08;

     dsprew03,MANTE;

     maesm011@;
|FIN;

|VARIABLES;
     nRango              = 0;
     nGrid               = 0;
     n0Vent              = 0;
     n1Vent              = 0;
     nBtnEli             = -1;
     nBtnAct             = -1;
     nBtnEnv             = -1;
     nBtnMod             = -1;
     nLbl1               = -1;
     nLbl2               = -1;
     nLbl3               = -1;
     nLbl4               = -1;
     nLbl5               = -1;
     nLbl6               = -1;
     nLbl7               = -1;
     nActiv              = 0;
     nInactiv            = 0;
     nTotal              = 0;
     nUsu                = 0;

     aAlfa               = "";
     aEsc1               = "";
     aEsc2               = "";
     aRetu               = "";
     aTecla              = "";
     aRojo               = "255,80,80;255,255,255";
     aHora               = "";
     aBase               = "";
     aMas                = "";
     aFich               = "";

 {-1}aVent               = "";
     aVent1              = 0;
     aVent2              = 0;
     aVent3              = 0;
     aVent4              = 0;
     aVent5              = "";
|FIN;

|PROCESO T12;  |TIPO 12;
     |SI FSalida = 7;  |ACABA;  |FINSI;
     |SI FSalida = 1;  |ACABA;  |FINSI;

     |ERROR;
     |POSICIONATE #dsprew03#0;
|FPRC;

|PROCESO ActUsu;
     |SI nUsu = 0;  |ACABA;  |FINSI;

     #maesm011@#0 = #dsprem04#5;
     |LEE 101#maesm011@.=;
     |SI FSalida = 0;
         #maesm011@#33 = #dsprem04#19;

         |GRABA 020#maesm011@.a;
         |LIBERA #maesm011@;
     |FINSI;
|FPRC;

|PROCESO EtisTotales;
     |SI nLbl1 ! -1;
         |DESTRUYECONTROL nLbl5;  nLbl5 = -1;
         |DESTRUYECONTROL nLbl6;  nLbl6 = -1;
         |DESTRUYECONTROL nLbl7;  nLbl7 = -1;
     |FINSI;

     aAlfa = "LABEL,{{15}}Activos    " + (("    " +  nActiv) % -104);
     |CONTROL_SIMPLE 0, aAlfa, 0585, 0599, NULL;
     nLbl5 = FSalida;

     aAlfa = "LABEL,{{15}}Inactivos " + (("    " +  nInactiv) % -104);
     |CONTROL_SIMPLE 0, aAlfa, 0685, 0699, NULL;
     nLbl6 = FSalida;

     nTotal = nActiv + nInactiv;
     aAlfa = "LABEL,{{15}}Total       " + (("    " +  nTotal) % -104);
     |CONTROL_SIMPLE 0, aAlfa, 0785, 0799, NULL;
     nLbl7 = FSalida;
|FPRC;

|PROCESO PintaLabels;
     |SI nLbl1 ! -1;
         |DESTRUYECONTROL nLbl1;  nLbl1 = -1;
         |DESTRUYECONTROL nLbl2;  nLbl2 = -1;
         |DESTRUYECONTROL nLbl3;  nLbl3 = -1;
         |DESTRUYECONTROL nLbl4;  nLbl4 = -1;
     |FINSI;

     #dsprem01#0 = #dsprem04#0;
     |LEE 000#dsprem01.=;

     #dsprem08#0 = #dsprem04#0;
     #dsprem08#1 = #dsprem04#1;
     |LEE 000#dsprem08.=;

     #dsprem02#0 = #dsprem04#0;
     #dsprem02#1 = #dsprem04#1;
     #dsprem02#2 = #dsprem04#2;
     |LEE 000#dsprem02.=;

     #dsprem03#0 = #dsprem04#0;
     #dsprem03#1 = #dsprem04#1;
     #dsprem03#2 = #dsprem04#2;
     #dsprem03#3 = #dsprem04#3;
     #dsprem03#4 = #dsprem04#4;
     |LEE 000#dsprem03.=;

     aAlfa = "LABEL,{{16}}" + #dsprem01#0 + " " + #dsprem01#1;
     |CONTROL_SIMPLE 0, aAlfa, 0502, 0530, NULL;
     nLbl1 = FSalida;

     aAlfa = "LABEL,{{16}}" + (("00" + #dsprem08#1) % -102) + " " + #dsprem08#2;
     |CONTROL_SIMPLE 0, aAlfa, 0532, 0560, NULL;
     nLbl2 = FSalida;

     aAlfa = "LABEL,{{16}}" + (("00" + #dsprem02#2) % -102) + " " + #dsprem02#3;
     |CONTROL_SIMPLE 0, aAlfa, 0562, 0584, NULL;
     nLbl3 = FSalida;

     aAlfa = "LABEL,{{16}}" + (("00" + #dsprem03#3) % -102);
     aAlfa = aAlfa + (("00" + #dsprem03#4) % -102) + " " + #dsprem03#10;
     |CONTROL_SIMPLE 0, aAlfa, 0702, 0784, NULL;
     nLbl4 = FSalida;

     |HAZ EtisTotales;
|FPRC;

|PROCESO Modificar;
     |LEE 101#dsprem04.=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |VENTANA 0501, 1690, -1, 16, "Modificaci¢n";
     n1Vent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n1Vent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |PTEC 802;  |PAUSA;

     #dsprew03#0  = #dsprem04#6;
     #dsprew03#1  = #dsprem04#15;
     #dsprew03#2  = #dsprem04#9;
     #dsprew03#3  = #dsprem04#10;
     #dsprew03#4  = #dsprem04#16;
     #dsprew03#5  = #dsprem04#7;
     #dsprew03#6  = #dsprem04#11;

     |PINPA #0#dsprew03;
     |PINDA #0#dsprew03;
     |ENDATOS #2#dsprew03;
     |SI FSalida = 0;
         #dsprem04#6  = #dsprew03#0;
         #dsprem04#15 = #dsprew03#1;
         #dsprem04#9  = #dsprew03#2;
         #dsprem04#10 = #dsprew03#3;
         #dsprem04#16 = #dsprew03#4;
         #dsprem04#7  = #dsprew03#5;
         #dsprem04#11 = #dsprew03#6;

         |GRABA 020#dsprem04.a;

         |HAZ ActUsu;
     |FINSI;

     |LIBERA #dsprem04;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n1Vent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA n1Vent;
|FPRC;

|PROCESO Evnt;
     |SI FSalida = 1;  |O FSalida = 2;
         |HAZ PintaLabels;

         |ESTADO_CONTROL nBtnMod, "ENABLE";
         |ESTADO_CONTROL nBtnEnv, "ENABLE";
         |ESTADO_CONTROL nBtnAct, "ENABLE";
         |ESTADO_CONTROL nBtnEli, "ENABLE";

         aAlfa = #dsprem04#15;  |QBF aAlfa;
         |SI aAlfa = "";
             |ESTADO_CONTROL nBtnEnv, "DISABLE";
         |FINSI;
     |FINSI;

     |SI FSalida = 4;
         |HAZ Modificar;
     |FINSI;
|FPRC;

|PROCESO Eliminar;
     |CONFI "0000NSeguro que quiere eliminar el usuario seleccionado";
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |LEE 101#dsprem04.=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |BORRA 020#dsprem04.a;
     |LIBERA #dsprem04;

     |SI #dsprem04#19 = "S";
         nActiv = nActiv - 1;
     |FINSI;

     |SI #dsprem04#19 = "N";
         nInactiv = nInactiv - 1;
     |FINSI;

     |HAZ EtisTotales;

     |REFRESCACONTROL nGrid;
     |FOCOTECLADO nGrid;
|FPRC;

|PROCESO Act_Desact;
     |LEE 101#dsprem04.=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |SI #dsprem04#19 = "S";
         |CONFI "0000NEsta seguro que quiere desactivar el usuario";
     |FINSI;

     |SI #dsprem04#19 = "N";
         |CONFI "0000NEsta seguro que quiere activar el usuario";
     |FINSI;

     |SI FSalida ! 0;
         |LIBERA #dsprem04;
         |ERROR;
         |ACABA;
     |FINSI;

     |SI #dsprem04#19 = "S";
         #dsprem04#19 = "N";
         #dsprem04#23 = aRojo;

         nActiv   = nActiv - 1;
         nInactiv = nInactiv + 1;
     |SINO;
         #dsprem04#19 = "S";
         #dsprem04#23 = "";

         nActiv   = nActiv + 1;
         nInactiv = nInactiv - 1;
     |FINSI;

     |HORA aHora;
     #dsprem04#20 = ~;
     #dsprem04#21 = aHora;
     #dsprem04#22 = Usuario % 810;

     |GRABA 020#dsprem04.a;
     |LIBERA #dsprem04;

     |HAZ EtisTotales;

     |HAZ ActUsu;

     |REFRESCACONTROL nGrid;
     |FOCOTECLADO nGrid;
|FPRC;

|PROCESO Envio;
     |CONFI "0000NQuiere enviar un correo de activaci¢n al usuario";
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     aAlfa = "dsprem03;";
     aAlfa = aAlfa + #dsprem04#0;
     aAlfa = aAlfa + (("00" + #dsprem04#1) % -102);
     aAlfa = aAlfa + (("00" + #dsprem04#2) % -102);
     aAlfa = aAlfa + (("00" + #dsprem04#3) % -102);
     aAlfa = aAlfa + (("00" + #dsprem04#4) % -102);
     aAlfa = aAlfa + #dsprem04#5;
     |SUB_EJECUTA aAlfa;

     |FOCOTECLADO nGrid;
|FPRC;

|PROGRAMA;
     |VENTANA 0501, 3599, -1, 0, "";
     n0Vent = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,{{197}}Modificar datos", 3302, 3320, Modificar;
     nBtnMod = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,{{197}}Envio activaci¢n", 3350, 3366, Envio;
     nBtnEnv = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,{{197}}Activar/Desactivar", 3370, 3386, Act_Desact;
     nBtnAct = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,{{199}}Eliminar", 3388, 3398, Eliminar;
     nBtnEli = FSalida;

     |ESTADO_CONTROL nBtnMod, "DISABLE";
     |ESTADO_CONTROL nBtnEnv, "DISABLE";
     |ESTADO_CONTROL nBtnAct, "DISABLE";
     |ESTADO_CONTROL nBtnEli, "DISABLE";


     |C_V #dsprem04#05, 1, "S";
     |C_V #dsprem04#06, 1, "S";
     |C_V #dsprem04#07, 1, "N";
     |C_V #dsprem04#08, 1, "N";
     |C_V #dsprem04#09, 1, "S";
     |C_V #dsprem04#10, 1, "S";
     |C_V #dsprem04#11, 1, "N";
     |C_V #dsprem04#12, 1, "N";
     |C_V #dsprem04#13, 1, "N";
     |C_V #dsprem04#14, 1, "N";
     |C_V #dsprem04#15, 1, "S";
     |C_V #dsprem04#16, 1, "S";
     |C_V #dsprem04#17, 1, "N";
     |C_V #dsprem04#18, 1, "N";
     |C_V #dsprem04#19, 1, "S";
     |C_V #dsprem04#20, 1, "S";
     |C_V #dsprem04#21, 1, "S";
     |C_V #dsprem04#22, 1, "S";

     nRango = 4 + 8 + 16 + 32 + 128;
     |LINEAL_SIMPLE #1#dsprem04, nRango, 0802, 3298, NULL, NULL, Evnt;
     nGrid = FSalida;

     aEsc1      = &255 + "806";
     aEsc2      = &255 + "807";
     aRetu      = &255 + "802";
     |PARA; |SI;  |HACIENDO;
         FSalida = "::{{001}}Salir," + nGrid;

         |LEETECLA aTecla;
         |SI aTecla = aEsc1; |SAL;  |FINSI;
         |SI aTecla = aEsc2; |SAL;  |FINSI;
         |SI aTecla = aRetu; |SAL;  |FINSI;
     |SIGUE;

     |FINVENTANA n0Vent;
|FPRO;

|PROCESO dsprem04;
     |SI #dsprem04#19 = "S";
         nActiv = nActiv + 1;
     |FINSI;

     |SI #dsprem04#19 = "N";
         nInactiv = nInactiv + 1;
     |FINSI;
|FPRC;

|DEFBUCLE dsprem04;
     #dsprem04#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, dsprem04;
|FIN;

|PROCESO maesm011;
     #dsprem04#5 = #maesm011@#0;
     |LEE 000#dsprem04.=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |SI #dsprem04#19 = #maesm011@#33;
         |ACABA;
     |FINSI;

     #dsprem04#19 = #maesm011@#33;
     #dsprem04#23 = "";

     |SI #dsprem04#19 = "N";
         #dsprem04#23 = aRojo;

         |HORA aHora;
         #dsprem04#20 = ~;
         #dsprem04#21 = aHora;
         #dsprem04#22 = Usuario % 810;
     |FINSI;

     |GRABA 000#dsprem04.a;
     |LIBERA #dsprem04;
|FPRC;

|DEFBUCLE maesm011;
     #maesm011@#1001;
     ;
     "          ";
     "zzzzzzzzzz";
     be;
     NULL, maesm011;
|FIN;

|PROCESO T80;  |TIPO 80;
     |ABRET #dsprem04;
     |SELECT #2#dsprem04;

     nUsu = 1;
     |DBASS aBase;  |QBF aBase;
     aMas  = aBase + "dspreven/def/maesm011.mas";
     aFich = aBase + "dspreven/fich/";
     |CARGA_DEF aMas, maesm011@;
     |SI FSalida ! 0;  nUsu = 0;  |FINSI;

     |SI nUsu = 1;
         |PATH_DAT #maesm011@#aFich#;
         |ABRET #maesm011@;
         |BUCLE maesm011;
     |FINSI;

     |SELECT #1#dsprem04;

     nActiv    = 0;
     nInactiv  = 0;
     |BUCLE dsprem04;

     |ABRET #dsprem01;
     |ABRET #dsprem02;
     |ABRET #dsprem03;
     |ABRET #dsprem08;
|FPRC;

|PROCESO T90;  |TIPO 90;
     |SI nUsu = 1;
         |CIERRAT #maesm011@;
         |DESCARGA_DEF #maesm011@;
     |FINSI;

     |CIERRAT #dsprem01;
     |CIERRAT #dsprem02;
     |CIERRAT #dsprem03;
     |CIERRAT #dsprem04;
     |CIERRAT #dsprem08;

     aAlfa = "0000Si ha cambiado algun dato, ";
     aAlfa = aAlfa + "recuerde en ejecutar la instalaci¢n completa de perfiles";
     |MENAV aAlfa;
|FPRC;
