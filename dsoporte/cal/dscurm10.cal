|FICHEROS;
     dscurm10 #10;
     dscurm11 #11;
     dscurm12 #12;
|FIN;

|VARIABLES;
     nGrid1         = 0;
     nPanta0        = 0;
     nVentana       = 0;
     nPos1          = 0;
     nPos2          = 0;
     nRango         = 0;
     nBoton0_1      = 0;
     nBoton0_2      = 0;
     nBoton0_3      = 0;

     aEsc1          = "";
     aEsc2          = "";
     aRetu          = "";
     aTecla         = "";
     aAlfa          = "";

     &eaEmpresa     = "";
     &enModo        = 0;
|FIN;


|| ------------------------------------------------------------------------
|| PROCESOS DE ASIGNACION DE MENUS
|| ------------------------------------------------------------------------

|PROCESO BajaPanta1;
     #10#0 = "";
|FPRC;

|PROCESO AltaPanta1;
     #10#0 = "zzzzzzzzzzzz";
|FPRC;

|| ------------------------------------------------------------------------
|| PROCESOS DE BAJAS DE FICHEROS
|| ------------------------------------------------------------------------

|PROCESO dscurm11B;
     |BORRA 020#11a;
     |LIBERA #11;
|FPRC;

|DEFBUCLE dscurm11B;
     #11#1;
     ;
     #10#0, "                                        ";
     #10#0, "zzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzz";
     be;
     NULL, dscurm11B;
|FIN;

|PROCESO dscurm12B;
     |BORRA 020#12a;
     |LIBERA #12;
|FPRC;

|DEFBUCLE dscurm12B;
     #12#1;
     ;
     #10#0, "            ";
     #10#0, "zzzzzzzzzzzz";
     be;
     NULL, dscurm12B;
|FIN;

|| ------------------------------------------------------
|| Procesos del Boton 0_1
|| ------------------------------------------------------

|PROCESO Boton0_1;
     |VENTANA 0501, 2799, -1, -1, "";
     nVentana = FSalida;

     eaEmpresa = #10#0;
     enModo    = 1;
     |SUB_EJECUTA_NP "dscurw10";

     |FINVENTANA nVentana;

     |REFRESCACONTROL nGrid1;
|FPRC;

|| ------------------------------------------------------
|| Procesos del Boton 0_2
|| ------------------------------------------------------

|PROCESO Boton0_2;
     |VENTANA 0501, 2799, -1, -1, "";
     nVentana = FSalida;

     eaEmpresa = #10#0;
     enModo    = 2;
     |SUB_EJECUTA_NP "dscurw10";

     |FINVENTANA nVentana;

     |REFRESCACONTROL nGrid1;
|FPRC;

|| ------------------------------------------------------
|| Procesos del Boton 0_3
|| ------------------------------------------------------

|PROCESO Boton0_3;
     |LEE 000#10=;
     |SI FSalida ! 0;
         |ESTADO_CONTROL nBoton0_2, "DISABLE";
         |ESTADO_CONTROL nBoton0_3, "DISABLE";
     |FINSI;

     aAlfa = "0000NSeguro de dar de baja la Empresa: " + #10#1;
     |CONFI aAlfa;
     |SI FSalida ! 0;   |ACABA;  |FINSI;

     |LEE 101#10=;
     |SI FSalida = 0;
         |BUCLE dscurm11B;
         |BUCLE dscurm12B;

         |BORRA 020#10a;
         |LIBERA #10;
         |REFRESCACONTROL nGrid1;
     |FINSI;

     |LEE 000#10p;
     |SI FSalida = 0;
         |ESTADO_CONTROL nBoton0_2, "ENABLE";
         |ESTADO_CONTROL nBoton0_3, "ENABLE";
     |SINO;
         |ESTADO_CONTROL nBoton0_2, "DISABLE";
         |ESTADO_CONTROL nBoton0_3, "DISABLE";
     |FINSI;
|FPRC;

|| ------------------------------------------------------
|| Procesos del Inicio del Programa
|| ------------------------------------------------------

|PROCESO EventoPanta1;
     |SI FSalida = 1;  |O FSalida = 2;
         |ESTADO_CONTROL nBoton0_1, "ENABLE";

         |LEE 000#10=;
         |SI FSalida = 0;
             |ESTADO_CONTROL nBoton0_2, "ENABLE";
             |ESTADO_CONTROL nBoton0_3, "ENABLE";
         |SINO;
             |ESTADO_CONTROL nBoton0_2, "DISABLE";
             |ESTADO_CONTROL nBoton0_3, "DISABLE";
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO BotonesPanta0;
     |CONTROL_SIMPLE nPanta0, "BOTON,{{193}} Alta", 0977, 1098, Boton0_1;
     nBoton0_1 = FSalida;

     |CONTROL_SIMPLE nPanta0, "BOTON,{{197}} Modificar", 1277, 1398, Boton0_2;
     nBoton0_2 = FSalida;

     |CONTROL_SIMPLE nPanta0, "BOTON,{{199}} Baja ", 1577, 1698, Boton0_3;
     nBoton0_3 = FSalida;

     |ESTADO_CONTROL nBoton0_1, "ENABLE";
     |ESTADO_CONTROL nBoton0_2, "DISABLE";
     |ESTADO_CONTROL nBoton0_3, "DISABLE";
|FPRC;

|PROCESO Tipo80;  |TIPO 80;
     |ABRET #10;

     FSalida = 2799;
|FPRC;

|PROCESO Tipo90;  |TIPO 90;
     |CIERRAT #10;
|FPRC;

|PROGRAMA;
     |PINPA #0#10;  nPanta0 = FSalida;

     nRango = 4 + 8 + 16 + 32;
     nPos1  = 0802;
     nPos2  = 2660;

     |LINEAL_SIMPLE #1#10, nRango, nPos1, nPos2, BajaPanta1, AltaPanta1, EventoPanta1;
     nGrid1 = FSalida;

     |HAZ BotonesPanta0;

     aEsc1  = &255 + "806";
     aEsc2  = &255 + "807";
     aRetu  = &255 + "802";
     |PARA; |SI;  |HACIENDO;
         FSalida = "::Salir";
         |LEETECLA aTecla;
         |SI aTecla = aEsc1; |SAL;  |FINSI;
         |SI aTecla = aEsc2; |SAL;  |FINSI;
         |SI aTecla = aRetu; |SAL;  |FINSI;
     |SIGUE;

     |FIN_CONTROL_WINDOWS nBoton0_1;
     |FIN_CONTROL_WINDOWS nBoton0_2;
     |FIN_CONTROL_WINDOWS nBoton0_3;

     |DESTRUYECONTROL nGrid1;

     |PULSATECLA;
|FPRO;
