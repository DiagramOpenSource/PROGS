|FICHEROS;
     dsorm009 #9;
     dsorm010 #10;

     dsorw011 #101,MANTE;
     dsorw012 #102,MANTE;
     dsorw013 #103,MANTE;
     dsorw014 #104,MANTE;

     referen@ #900;
|FIN;

|VARIABLES;
     nBoton1     = 0;
     nBoton2     = 0;
     nBoton3     = 0;
     nBoton4     = 0;
     nSwHayCampo = 0;
     nSwHay      = 0;
     nCampo      = 0;
     nTCampo     = 0;
     nBusca      = 0;
     nLinea      = 0;

     aF2         = "";
     aF3         = "";
     aF4         = "";
     aF5         = "";
     aTecla      = "";
     aFichero    = "";
     aDef        = "";
     aCarga      = "";
     aDirDef     = "";
     aQueQuiero  = "";
     aAlfa       = "";
     aLongitud   = "";
     aLongitud2  = "";
     aTipo       = "";
     aTitulo     = "";
     aMensa      = "";
     aBase       = "";
     aAplica     = "";
     aBaseDef    = "";
|FIN;

|RUTINA ClaveBaja103;
     #103#0 = "        ";
|FRUT;

|RUTINA ClaveAlta103;
     #103#0 = "zzzzzzzz";
|FRUT;

|RUTINA ClaveBaja104;
     #104#0 = #103#0;
     #104#1 = 0;
|FRUT;

|RUTINA ClaveAlta104;
     #104#0 = #103#0;
     #104#1 = 999;
|FRUT;

|PROCESO Tipo7w13;  |TIPO 7;
     |ENTLINEAL #1#104, -2, 7, 22, 0, ClaveBaja104, ClaveAlta104;
|FPRC;

|PROCESO Tipo11w13;  |TIPO 11;
     |SI FSalida ! 10;  |ACABA;  |FINSI;

     |ENTLINEAL #1#104, -2, 4, 22, 0, ClaveBaja104, ClaveAlta104;
|FPRC;

|PROCESO Tipo11w14;  |TIPO 11;
     |SI FSalida ! 10;  |ACABA;  |FINSI;

     |LEE 101#104=;

     |SI #104#3 = "S";
         #104#3 = "N";
     |SINO;
         #104#3 = "S";
     |FINSI;
     |PINTA #104#3;
     |GRABA 020#104a;
     |LIBERA #104;
|FPRC;

|PROCESO FiltroTipo;
     |ABRE #103;
     |LEE 040#103p;
     |SI FSalida = 0;
         |MENAV "     Ya esta cargada la Configuracion. Actualice la Entrada Actual o salga y entre de nuevo.";
         |ACABA;
     |FINSI;
     |CIERRA #103;

     |ESTADO_CONTROL nBoton1, "HIDE";
     |ESTADO_CONTROL nBoton2, "HIDE";
     |ESTADO_CONTROL nBoton3, "HIDE";
     |ESTADO_CONTROL nBoton4, "HIDE";

     |PUSHV 0501 2380;
     |PINPA #0#101;
     |PINDA #0#101;
     |ENDATOS #1#101;
     |POPV;

     |ESTADO_CONTROL nBoton1, "SHOW";
     |ESTADO_CONTROL nBoton2, "SHOW";
     |ESTADO_CONTROL nBoton3, "SHOW";
     |ESTADO_CONTROL nBoton4, "SHOW";
|FPRC;

|PROCESO MiraSiEsta;
     nSwHayCampo = 1;
     |ERROR10;
|FPRC;

|DEFBUCLE dsorm009;
     #9#1;
     ;
     #101#0, #101#2, aDef, nCampo;
     #101#0, #101#2, aDef, nCampo;
     ;
     NULL, MiraSiEsta;
|FIN;

|PROCESO LeeDef;
     aCarga = aDirDef;  |QBT aCarga;
     |CARGA_DEF aCarga, referen@;
     |SI FSalida = 0;
         aQueQuiero = "|TITULO";
         aTitulo    = #900#aQueQuiero#;

         aQueQuiero = "|NC";
         aAlfa      = #900#aQueQuiero#;
         nTCampo    = aAlfa;
         nTCampo    = nTCampo - 1;

         nSwHay     = 0;
         |PARA nCampo = 0;  |SI nCampo [ nTCampo;  |HACIENDO nCampo = nCampo + 1;
               aQueQuiero = "|C" + (("000" + nCampo) % -103) + "NOMBRE";
               aFichero   = (#900#aQueQuiero# + (" " * 20)) % 120;

               aQueQuiero = "|C" + (("000" + nCampo) % -103) + "LONGITUD";
               aLongitud  = #900#aQueQuiero#;
               aLongitud2 = #101#8;

               |QBF aLongitud;
               |QBF aLongitud2;

               aQueQuiero = "|C" + (("000" + nCampo) % -103) + "TIPO";
               aTipo      = #900#aQueQuiero#;

               |SI aTipo = #101#9; |Y aLongitud = aLongitud2;
                   nSwHayCampo = 0;
                   |BUCLE dsorm009;
                   |SI nSwHayCampo = 0;
                       |DDEFECTO #104;
                       #104#0 = aDef;
                       #104#1 = nCampo;
                       |LEE 101#104=;
                       |SI FSalida ! 0;
                           #104#0 = aDef;
                           #104#1 = nCampo;
                           #104#2 = aFichero;
                           #104#3 = "N";
                           |GRABA 020#104n;
                       |FINSI;
                       nSwHay = 1;
                   |FINSI;
               |FINSI;
         |SIGUE;

         |SI nSwHay = 1;
             |DDEFECTO #103;
             #103#0 = aDef;
             |LEE 101#103=;
             |SI FSalida ! 0;
                 #103#0 = aDef;
                 #103#1 = aTitulo;
                 |GRABA 020#103n;
             |FINSI;
         |FINSI;

         |DESCARGA_DEF #referen@;
     |SINO;
         aMensa = "     No puedo cargar el Def : " + aDef;
         |MENAV aMensa;
     |FINSI;
|FPRC;

|PROCESO CargaConfiguracion;
     |ABRE #103;
     |ABRE #104;

     |DBASS aBase;  |QBF aBase;
     nBusca    = 0;
     aAplica   = #101#2;  |QBF aAplica;
     aBaseDef  = aBase + aAplica + "/def/";
     aDirDef   = aBaseDef + "*.mas";
     |_PDIR aDirDef;
     |PARA;  |SI FSalida ] 0; |HACIENDO;
          aDef = aDirDef - aBaseDef;
          aDef = aDef - ".mas";
          |HAZ LeeDef;
          |_SDIR aDirDef;
     |SIGUE;

     |CIERRA #103;
     |CIERRA #104;
|FPRC;

|PROCESO EntradaMasiva;
     |ABRE #102;
     |LEE 040#102p;
     |SI FSalida ! 0;
         |MENAV "     Cree el Filtro Tipo.";
         |ACABA;
     |FINSI;
     |CIERRA #102;

     |ESTADO_CONTROL nBoton1, "HIDE";
     |ESTADO_CONTROL nBoton2, "HIDE";
     |ESTADO_CONTROL nBoton3, "HIDE";
     |ESTADO_CONTROL nBoton4, "HIDE";

     |MENSA "      Cargando Configuracion";
     |HAZ CargaConfiguracion;

     |PUSHV 0501 2380;
     |ENTLINEAL #1#103, -1, 4, 22, 1, ClaveBaja103, ClaveAlta103;
     |POPV;

     |ESTADO_CONTROL nBoton1, "SHOW";
     |ESTADO_CONTROL nBoton2, "SHOW";
     |ESTADO_CONTROL nBoton3, "SHOW";
     |ESTADO_CONTROL nBoton4, "SHOW";
|FPRC;

|PROCESO Graba10;
     nLinea = nLinea + 1;

     #10#0 = #9#0;
     #10#1 = #9#1;
     #10#2 = #9#2;
     #10#3 = #9#3;
     #10#4 = nLinea;
     #10#5 = #102#1;
     #10#6 = #102#2;
     #10#7 = #102#3;
     #10#8 = #102#4;
     #10#9 = #102#5;
     |GRABA 020#10n;
     |LIBERA #10;
|FPRC;

|DEFBUCLE dsorw012;
     #102#1;
     ;
     001;
     999;
     ;
     NULL, Graba10;
|FIN;

|PROCESO Graba9;
     #103#0 = #104#0;
     |LEE 040#103=;
     |SI FSalida ! 0;  |DDEFECTO #103;  |FINSI;

     #9#0  = #101#0;
     #9#1  = #101#2;
     #9#2  = #104#0;
     #9#3  = #104#1;
     |LEE 101#9=;
     |SI FSalida ! 0;
         |DDEFECTO #9;
         #9#0  = #101#0;
         #9#1  = #101#2;
         #9#2  = #104#0;
         #9#3  = #104#1;
         |GRABA 020#9b;
     |FINSI;

     #9#4  = #101#1;
     #9#5  = #101#3;
     #9#6  = #103#1;
     #9#7  = #104#2;
     #9#8  = #101#11;
     #9#9  = #101#12;
     #9#10 = #101#13;
     #9#11 = #101#14;
     #9#12 = #101#10;
     |GRABA 020#9a;
     |LIBERA #9;

     #10#0 = #9#0;
     #10#1 = #9#1;
     #10#2 = #9#2;
     #10#3 = #9#3;
     #10#4 = 999;
     |LEE 040#10];
     |SI FSalida = 0;
         |LEE 040#10a;
     |SINO;
         |LEE 040#10u;
     |FINSI;
     nLinea = 0;
     |SI FSalida = 0;  |Y #10#0 = #9#0; |Y #10#1 = #9#1;
      |Y #10#2 = #9#2; |Y #10#3 = #9#3;
         nLinea = #10#4;
     |FINSI;

     |BUCLE dsorw012;
|FPRC;

|DEFBUCLE dsorw014;
     #104#1;
     3;
     "        ", 000, "S";
     "zzzzzzzz", 999, "S";
     ;
     NULL, Graba9;
|FIN;

|PROCESO Actualizacion;
     |CONFI "0000SSeguro de Actualizar los Campos.";
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |ABRE #9;
     |ABRE #10;
     |ABRE #103;
     |BUCLE dsorw014;
     |CIERRA #9;
     |CIERRA #10;
     |CIERRA #103;

     |DELINDEX #103;
     |DELINDEX #104;

     |MENAV "0000 Actualizacion creada con Exito. Vuelva a crear un filtro nuevo o salga del Programa";
|FPRC;

|PROCESO Boton1;
     |PTEC 824;
|FPRC;

|PROCESO Boton2;
     |PTEC 825;
|FPRC;

|PROCESO Boton3;
     |PTEC 826;
|FPRC;

|PROCESO Boton4;
     |PTEC 827;
|FPRC;

|PROGRAMA;
     |CLS;
     |CABEZA "Filtros por Campos Masivos";

     aFichero = ("12" + Usuario + "zzzzzzzz") % 108;
     |NOME_DAT #102 aFichero;
     |ABRE #102;  |CIERRA #102;  |DELINDEX #102;

     aFichero = ("13" + Usuario + "zzzzzzzz") % 108;
     |NOME_DAT #103 aFichero;
     |ABRE #103;  |CIERRA #103;  |DELINDEX #103;

     aFichero = ("14" + Usuario + "zzzzzzzz") % 108;
     |NOME_DAT #104 aFichero;
     |ABRE #104;  |CIERRA #104;  |DELINDEX #104;

     |CONTROL_SIMPLE 0, "BOTON, <F2> Configurar Filtro Tipo", 0820, 0950, Boton1;
     nBoton1 = FSalida;

     |CONTROL_SIMPLE 0, "BOTON, <F3> Entrada Masiva", 1120, 1250, Boton2;
     nBoton2 = FSalida;

     |CONTROL_SIMPLE 0, "BOTON, <F4> Actualizacion", 1420, 1550, Boton3;
     nBoton3 = FSalida;

     |CONTROL_SIMPLE 0, "BOTON, <F5> Salir", 1720, 1850, Boton4;
     nBoton4 = FSalida;

     aF2 = &255 + "824";
     aF3 = &255 + "825";
     aF4 = &255 + "826";
     aF5 = &255 + "827";
     |PARA; |SI;  |HACIENDO;
         |LEETECLA aTecla;
         |SI aTecla = aF2;  |HAZ FiltroTipo;    |FINSI;
         |SI aTecla = aF3;  |HAZ EntradaMasiva; |FINSI;
         |SI aTecla = aF4;  |HAZ Actualizacion; |FINSI;
         |SI aTecla = aF5;  |SAL;  |FINSI;
     |SIGUE;

     |FIN_CONTROL_WINDOWS nBoton1;
     |FIN_CONTROL_WINDOWS nBoton2;
     |FIN_CONTROL_WINDOWS nBoton3;
     |FIN_CONTROL_WINDOWS nBoton4;

     |ABRE #102;  |CIERRA #102;  |DELINDEX #102;
     |ABRE #103;  |CIERRA #103;  |DELINDEX #103;
     |ABRE #104;  |CIERRA #104;  |DELINDEX #104;

     |PULSATECLA;
|FPRO;
