|FICHEROS;
     dscurm00 #0;
     dscurm02 #2;
     dscurm03 #3;
     dscurm04 #4;
     dscurm05 #5;
     dscurw04 #904;
|FIN;

|VARIABLES;
     nBoton0_1      = 0;
     nBoton0_2      = 0;
     nBoton0_3      = 0;

     nPanta0        = 0;
     nCampoD        = 0;
     nCampoH        = 0;
     nCampoHasta    = 0;
     nCampo1        = 0;
     nVentana       = 0;
     nGrid1         = 0;
     nGrid2         = 0;
     nRango         = 0;
     nPos1          = 0;
     nPos2          = 0;
     nResp          = 0;
     nCampo         = 0;

     aAlfa          = "";
     aBase          = "";
     aMas           = "";
     aFichero       = "";

     &enCurso       = 0;
     &enModo        = 1;
     &eaAplica      = "";
     &eaBloque      = "";
     &eaLeccion     = "";
|FIN;

|PROCESO BajaPanta0;
     #4#0  = #904#1;
     #4#1  = #904#4;
     #4#2  = #904#6;
     #4#3  = "";
|FPRC;

|PROCESO AltaPanta0;
     #4#0  = #904#1;
     #4#1  = #904#4;
     #4#2  = #904#6;
     #4#3  = "zz";
|FPRC;

|| ------------------------------------------------------
|| Procesos Bajas de Ficheros;
|| ------------------------------------------------------

|PROCESO dscurm05B;
     |BORRA 020#5a;
     |LIBERA #5;
|FPRC;

|DEFBUCLE dscurm05B;
     #5#1;
     ;
     #4#0, #4#1, #4#2, #4#3, "  ";
     #4#0, #4#1, #4#2, #4#3, "zz";
     be;
     NULL, dscurm05B;
|FIN;

|| ------------------------------------------------------
|| Procesos Grabacion del Bloque
|| ------------------------------------------------------

|PROCESO GrabaBloque;
     #3#0 = #904#1;
     #3#1 = #904#4;
     #3#2 = #904#6;
     |LEE 101#3=;
     |SI FSalida ! 0;
         |DDEFECTO #3;
         #3#0 = #904#1;
         #3#1 = #904#4;
         #3#2 = #904#6;
         |GRABA 020#3b;
     |FINSI;

     #3#3 = #904#7;
     |GRABA 020#3a;
     |LIBERA #3;
|FPRC;

|| ------------------------------------------------------
|| Procesos del Boton 0_1
|| ------------------------------------------------------

|PROCESO Boton0_1;
     |HAZ GrabaBloque;

     |VENTANA 0501, 2799, -1, -1, "";
     nVentana = FSalida;

     enCurso   = #904#0;
     eaAplica  = #904#4;
     eaBloque  = #904#6;
     eaLeccion = "";
     enModo    = 1;
     |SUB_EJECUTA "dscurw05";

     |FINVENTANA nVentana;

     |REFRESCACONTROL nGrid1;
|FPRC;

|| ------------------------------------------------------
|| Procesos del Boton 0_2
|| ------------------------------------------------------

|PROCESO Boton0_2;
     |VENTANA 0501, 2799, -1, -1, "";
     nVentana = FSalida;

     enCurso   = #904#0;
     eaAplica  = #904#4;
     eaBloque  = #904#6;
     eaLeccion = #4#3;
     enModo    = 2;
     |SUB_EJECUTA "dscurw05";

     |FINVENTANA nVentana;

     |REFRESCACONTROL nGrid1;
|FPRC;

|| ------------------------------------------------------
|| Procesos del Boton 0_3
|| ------------------------------------------------------

|PROCESO Boton0_3;
     aAlfa = "0000NSeguro de dar de baja a " + #4#4;
     |CONFI aAlfa;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |LEE 101#4=;
     |SI FSalida = 0;
         |BUCLE dscurm05B;

         |BORRA 020#4a;
         |LIBERA #4;
     |FINSI;

     #4#0 = #904#1;
     #4#1 = #904#4;
     #4#2 = #904#6;
     #4#3 = "";
     |LEE 000#4];
     |SI FSalida = 0;  |Y  #4#0 = #904#1;  |Y #4#1 = #904#4;  |Y #4#2 = #904#6;
         |ESTADO_CONTROL nBoton0_2, "ENABLE";
         |ESTADO_CONTROL nBoton0_3, "ENABLE";
     |SINO;
         |ESTADO_CONTROL nBoton0_2, "DISABLE";
         |ESTADO_CONTROL nBoton0_3, "DISABLE";
     |FINSI;

     |REFRESCACONTROL nGrid1;
|FPRC;

|| ------------------------------------------------------
|| Procesos del Incio del Programa
|| ------------------------------------------------------

|PROCESO EventoPanta0;
     |SI FSalida = 1;  |O FSalida = 2;
         |ESTADO_CONTROL nBoton0_1, "ENABLE";

         |LEE 000#4=;
         |SI FSalida = 0;  |Y  #4#0 = #904#1;  |Y #4#1 = #904#4;  |Y #4#2 = #904#6;
             |ESTADO_CONTROL nBoton0_2, "ENABLE";
             |ESTADO_CONTROL nBoton0_3, "ENABLE";
         |SINO;
             |ESTADO_CONTROL nBoton0_2, "DISABLE";
             |ESTADO_CONTROL nBoton0_3, "DISABLE";
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO BotonesPanta0;
     |CONTROL_SIMPLE nPanta0, "BOTON,{{193}} Alta", 1277, 1398, Boton0_1;
     nBoton0_1 = FSalida;

     |CONTROL_SIMPLE nPanta0, "BOTON,{{197}} Modificar", 1577, 1698, Boton0_2;
     nBoton0_2 = FSalida;

     |CONTROL_SIMPLE nPanta0, "BOTON,{{199}} Baja ", 1877, 1998, Boton0_3;
     nBoton0_3 = FSalida;

     |ESTADO_CONTROL nBoton0_1, "DISABLE";
     |ESTADO_CONTROL nBoton0_2, "DISABLE";
     |ESTADO_CONTROL nBoton0_3, "DISABLE";
|FPRC;

|PROCESO Bloque;  |TIPO 0;
     |SI enModo ! 1;  |ACABA;  |FINSI;

     #3#0 = #904#1;
     #3#1 = #904#4;
     #3#2 = #904#6;
     |LEE 000#3=;
     |SI FSalida = 0;
         |MENAV "0000 El Bloque ya existe.";
         |ERROR;
     |FINSI;
|FPRC;

|PROCESO Tipo12;  |TIPO 12;
|FPRC;

|PROCESO Tipo80;  |TIPO 80;
     |ABRET #4;

     FSalida = 2799;
|FPRC;

|PROCESO Tipo90;  |TIPO 90;
     |CIERRAT #4;
|FPRC;

|PROGRAMA;
     |C_M #904#6, 1, "S";

     |ABRE #0;
     #0#0 = enCurso;
     |LEE 000#0=;
     |SI FSalida ! 0;  |DDEFECTO #0; |FINSI;
     |CIERRA #0;

     |ABRE #2;
     #2#0 = #0#1;
     #2#1 = eaAplica;
     |LEE 000#2=;
     |SI FSalida ! 0;  |DDEFECTO #2; |FINSI;
     |CIERRA #2;

     #904#0 = #0#0;
     #904#1 = #0#1;
     #904#2 = #0#2;
     #904#3 = #0#3;
     #904#4 = #2#1;
     #904#5 = #2#2;

     |ABRE #3;
     |SI enModo ! 1;
         #3#0 = #904#1;
         #3#1 = #904#4;
         #3#2 = eaBloque;
         |LEE 101#3=;
         |SI FSalida ! 0;  |DDEFECTO #3; |FINSI;

         #904#6 = #3#2;
         #904#7 = #3#3;

         |C_M #904#6, 1, "N";
     |FINSI;

     |CABEZA "Bloques";

     |PINPA #0#904;  nPanta0 = FSalida;
     |PINDA #0#904;

     nRango = 4 + 8 + 16 + 32;
     nPos1  = 1202;
     nPos2  = 2675;

     |LINEAL_SIMPLE #1#4, nRango, nPos1, nPos2, BajaPanta0, AltaPanta0, EventoPanta0;
     nGrid1 = FSalida;

     |HAZ BotonesPanta0;

     |SI enModo = 1;  |ENDATOS #1#904;  |FINSI;
     |SI enModo = 2;  |ENDATOS #2#904;  |FINSI;

     |SI FSalida = 0;
         |HAZ GrabaBloque;
     |FINSI;
     |CIERRA #3;

     |FIN_CONTROL_WINDOWS nBoton0_1;
     |FIN_CONTROL_WINDOWS nBoton0_2;
     |FIN_CONTROL_WINDOWS nBoton0_3;

     |DESTRUYECONTROL nGrid1;

     |PULSATECLA;
|FPRO;
