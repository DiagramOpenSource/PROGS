|FICHEROS;
     dsorm001 #1;
     dsorm050 #50;
     dsorm051 #51,MANTE;
     dsorw050 #900;
     dsorw051 #901;
     dsorw052 #902;

     agiuser1@ #101;
|FIN;

|VARIABLES;
     aMaster   = "";
     aHora     = "";
     aBase     = "";
     aAbre     = "";
     aAlfa     = "";
     aAlfa1    = "";
     aDef      = "";
     aFich     = "";
     aFichero  = "";

     nModo     = 0;
     nForma    = 0;
     nFormaL   = 0;
     nUsuario  = 0;
     nHandle   = 0;
     nHandle1  = 0;
     nHandle2  = 0;
     nError    = 0;
     nBufUsr   = 0;
     nEleUsr   = 0;
     nRegis    = 0;
     nCaracter = 0;
     nPos      = 0;
     nCamuf    = 0;
     nNum      = 0;
     nMayor    = 0;
     nRegistro = 0;
     nMaster   = 0;

     fFecha    = @;

     &eaPass        = "";
     &eaPassEnc     = "";
|FIN;

|PROCESO ActualizaUsuario;
     eaPass = #51#4;
     |HAZ EncriptaUsuario;

     |ABRE #900;
     |SELECT #2#900;
     #900#1 = (("000" + #51#0) % -103) + (("000" + #51#1) % -103) + (("00" + #51#2) % -102);
     |LEE 101#900=;
     |SI FSalida = 0;
         #900#2 = eaPassEnc;
         #900#5 = #51#5;
         |GRABA 020#900a;
         |LIBERA #900;
     |SINO;
         nMayor = 1;
         |SELECT #3#900;
         |LEE 040#900u;
         |SI FSalida = 0;
             nMayor = #900#4 + 1;
             |SI nMayor > 999;
                 |PARA nMayor = 1;  |SI nMayor [ 999;  |HACIENDO nMayor = nMayor + 1;
                       #900#4 = nMayor;
                       |LEE 040#900=;
                       |SI FSalida ! 0;  |SAL;  |FINSI;
                 |SIGUE;
             |FINSI;
         |FINSI;

         |SI nMayor ] 999;
             |MENAV "     Mas de 1000 Usuarios, pongase en contacto con su distribuidor.";
             |CIERRA #900;
             |ACABA;
         |FINSI;

         nRegistro = 0;
         |SELECT #1#900;
         |LEE 040#900u;
         |SI FSalida = 0;
             nRegistro = #900#0 + 1;
         |FINSI;

         #900#0 = nRegistro;
         #900#1 = (("000" + #51#0) % -103) + (("000" + #51#1) % -103) + (("00" + #51#2) % -102);
         #900#2 = eaPassEnc;
         #900#3 = "N";
         #900#4 = nMayor;
         #900#5 = #51#5;
         |GRABA 020#900n;
         |LIBERA #900;
     |FINSI;
     |CIERRA #900;
|FPRC;

|PROCESO ActualizaTabla;
     |ABRE #901;
     #901#0 = (("000" + #51#0) % -103) + (("000" + #51#1) % -103) + (("00" + #51#2) % -102);
     |LEE 101#901=;
     |SI FSalida ! 0;
         |DDEFECTO #901;
         #901#0 = (("000" + #51#0) % -103) + (("000" + #51#1) % -103) + (("00" + #51#2) % -102);
         |GRABA 020#901b;
     |FINSI;

     #901#1 = "fich_" + (("000" + #51#1) % -103);
     #901#2 = "_" + (("000" + #51#0) % -103);
     |GRABA 020#901a;

     |LIBERA #901;
     |CIERRA #901;
|FPRC;

|PROCESO Tipo2m51;  |TIPO 2;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 3;  |O nModo = 1;
         |MENAV "0000 Opcion no permitida.";
         |ERROR;
         |ACABA;
     |FINSI;

     nFormaL = 0 +. 1;
     |SI nFormaL ! 1;  |ACABA;  |FINSI;

     |HAZ ActualizaUsuario;
     |HAZ ActualizaTabla;
|FPRC;

|PROCESO Tipo6C5m51;  |TIPO 6;
     |ABRE #902;
     #902#0 = #51#5;
     |LEE 040#902=;
     |SI FSalida ! 0;
         |MENAV "0000 Grupo fuera de Rango. Use un Grupo Asignado a su Propiedad.";
         |ERROR;
     |FINSI;
     |CIERRA #902;
|FPRC;

|PROCESO Tipo0C4m51;  |TIPO 0;
     aAlfa = #51#4 - "¥";
     |SI FSalida ! 0;
         aAlfa = "0000 Por favor no introduzca " + &0209;
         |MENAV aAlfa;
         |ERROR;
         |ACABA;
     |FINSI;

     aAlfa = #51#4 - "¤";
     |SI FSalida ! 0;
         aAlfa = "0000 Por favor no introduzca " + &0241;
         |MENAV aAlfa;
         |ERROR;
         |ACABA;
     |FINSI;
|FPRC;

|RUTINA Baja50;
     #50#0 = aMaster;
     #50#1 = 001;
|FRUT;

|RUTINA Alta50;
     #50#0 = aMaster;
     #50#1 = 999;
|FRUT;

|RUTINA Baja51;
     #51#0 = #50#0;
     #51#1 = #50#1;
     #51#2 = 01;
|FRUT;

|RUTINA Alta51;
     #51#0 = #50#0;
     #51#1 = #50#1;
     #51#2 = #50#3;
|FRUT;

|PROCESO Tipo7C1m50;  |TIPO 7;
     |ENTLINEAL #1#51, -3, 7, 22, 0, Baja51, Alta51;
|FPRC;

|PROCESO dsorm051;
     |SI #51#2 > #50#3;
         |SELECT #2#900;
         #900#1 = (("000" + #51#0) % -103) + (("000" + #51#1) % -103) + (("00" + #51#2) % -102);
         |LEE 101#900=;
         |SI FSalida = 0;
             |BORRA 020#900a;
             |LIBERA #900;
         |FINSI;

         #901#0 = (("000" + #51#0) % -103) + (("000" + #51#1) % -103) + (("00" + #51#2) % -102);
         |LEE 101#901=;
         |SI FSalida = 0;
             |BORRA 020#901a;
             |LIBERA #901;
         |FINSI;

         |BORRA 020#51a;
         |LIBERA #51;
         |ACABA;
     |FINSI;
|FPRC;

|DEFBUCLE dsorm051;
     #51#1;
     ;
     #50#0, #50#1, 01;
     #50#0, #50#1, 99;
     be;
     NULL, dsorm051;
|FIN;

|PROCESO Tipo2m50;  |TIPO 2;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 3;
         #50#3 = 0;

         |ABRE #900;
         |ABRE #901;
         |BUCLE dsorm051;
         |CIERRA #900;
         |CIERRA #901;
         |ACABA;
     |FINSI;

     nForma = 0 +. 1;

     |SI nForma = 1;
         |ABRE #900;
         |ABRE #901;
         |BUCLE dsorm051;
         |CIERRA #900;
         |CIERRA #901;

         |ABRE #51;
         |PARA nUsuario = 1;  |SI nUsuario [ #50#3;  |HACIENDO nUsuario = nUsuario + 1;
               #51#0 = #50#0;
               #51#1 = #50#1;
               #51#2 = nUsuario;
               |LEE 101#51=;
               |SI FSalida ! 0;
                   |DDEFECTO #51;
                   #51#0 = #50#0;
                   #51#1 = #50#1;
                   #51#2 = nUsuario;
                   |GRABA 020#51n;
                   |LIBERA #51;
               |FINSI;
         |SIGUE;
         |CIERRA #51;

         |ENTLINEAL #1#51, -3, 2, 22, 0, Baja51, Alta51;
     |FINSI;
|FPRC;

|PROCESO Tipo0C3m50;
     |ABRE #51;
     #51#0 = #50#0;
     #51#1 = #50#1;
     #51#2 = 99;
     |LEE 040#51];
     |SI FSalida = 0;
         |LEE 040#51a;
     |SINO;
         |LEE 040#51u;
     |FINSI;
     |SI FSalida = 0;  |Y #51#0 = #50#0;  |Y #51#1 = #50#1;
         |SI #51#2 > #50#3;
             |CONFI "0000NHemos introducido menos Usuarios de los que hay definidos, Continuar";
             |SI FSalida ! 0;
                 |ERROR;
             |FINSI;
         |FINSI;
     |FINSI;
     |CIERRA #51;
|FPRC;

|PROCESO GrabaSemaforo;
     |DBASS aBase; |QBF aBase;
     aAbre  = aBase + "dev/usr/ds_sys.smf";
     |XABRE "W", aAbre, nHandle;
     |SI FSalida ] 0;
         fFecha = ~;
         |HORA aHora;
         aAlfa  = "Usuario : " + (((Usuario % 810) + "        ") % 108) + " Fecha : " + fFecha + " Hora : " + aHora;
         |XGRABA nHandle, aAlfa;
     |FINSI;
     |XCIERRA nHandle;
|FPRC;

|PROCESO IniSemaforo;
     |DBASS aBase; |QBF aBase;
     aAbre  = aBase + "dev/usr/ds_sys.smf";

     nError = 0;
     |XABRE "E", aAbre, nHandle;
     |SI FSalida ] 0;
         nError = 1;
         |ACABA;
     |FINSI;

     |HAZ GrabaSemaforo;
|FPRC;

|PROCESO FinSemaforo;
     |DBASS aBase; |QBF aBase;
     aAbre  = aBase + "dev/usr/ds_sys.smf";
     |FBORRA aAbre;
|FPRC;

|PROCESO CargaSecuencial;
     aAlfa = aBase + "dev/usr/ds_sys.psw"
     |LEESECUENCIAL aAlfa, nBufUsr, nEleUsr, 0, 0;
     |PARA nRegis = 0; |SI nRegis < nEleUsr; |HACIENDO nRegis = nRegis + 1;
           |DEL_BUF nBufUsr, nRegis, aAlfa;

           |DDEFECTO #900;
           #900#0 = nRegis + 1;
           aAlfa1 = aAlfa % 410;   #900#1 = aAlfa1;
           aAlfa1 = aAlfa % 1420;  #900#2 = aAlfa1;
           aAlfa1 = aAlfa % 3401;  #900#3 = aAlfa1;
           aAlfa1 = aAlfa % 103;   #900#4 = aAlfa1;

           #101#0 = #900#4;
           |LEE 000#101=;
           |SI FSalida = 0;
               #900#5 = #101#2;
           |FINSI;
           |GRABA 020#900n;
     |SIGUE;
     |FINDIM nBufUsr;
|FPRC;

|PROCESO LeeUsuarios;
     |DBASS aBase; |QBF aBase;
     aDef = aBase + "agitool/def/agiuser1.mas";
     |CARGA_DEF aDef, agiuser1@;
     |SI FSalida ! 0;
         |MENAV "    [agitool/def/agiuser1.mas] inaccesible";
         |ACABA;
     |FINSI;

     aFich = aBase + "agitool/fich/";
     |PATH_DAT #101 aFich;

     aFichero = ("user" + Usuario + "zzzzzzzz") % 108;
     |NOME_DAT #900 aFichero;

     |ABRE #900;
     |ABRE #101;
     |HAZ CargaSecuencial;
     |CIERRA #101;
     |CIERRA #900;
|FPRC;

|PROCESO TABLAUSERFICH;
     |DBASS aBase; |QBF aBase;
     aAbre = aBase + "tablasnet";  |MKDIR aAbre;
     aAbre = aAbre + "/tablauserfich.txt";

     |XABRE "U", aAbre, nHandle;
     |SI FSalida < 0;  |ACABA;  |FINSI;

     |PARA;  |SI;  |HACIENDO;
         |XLEE nHandle, 250, aAlfa;
         |SI FSalida < 0;  |SAL;  |FINSI;

         |DDEFECTO #901;
         aAlfa1 = aAlfa % 108;
         #901#0 = aAlfa1;
         |LEE 101#901=;
         |SI FSalida ! 0;
             |DDEFECTO #901;
             aAlfa1 = aAlfa % 108;
             #901#0 = aAlfa1;
             |GRABA 020#901b;
         |FINSI;

         aAlfa1 = aAlfa % 1008;  #901#1 = aAlfa1;
         |GRABA 020#901a;
         |LIBERA #901;
     |SIGUE;

     |XCIERRA nHandle;
|FPRC;

|PROCESO TABLAUSERAPLI;
     |DBASS aBase; |QBF aBase;
     aAbre = aBase + "tablasnet";  |MKDIR aAbre;
     aAbre = aAbre + "/tablauserapli.txt";

     |XABRE "U", aAbre, nHandle;
     |SI FSalida < 0;  |ACABA;  |FINSI;

     |PARA;  |SI;  |HACIENDO;
         |XLEE nHandle, 250, aAlfa;
         |SI FSalida < 0;  |SAL;  |FINSI;

         |DDEFECTO #901;
         aAlfa1 = aAlfa % 108;
         #901#0 = aAlfa1;
         |LEE 101#901=;
         |SI FSalida ! 0;
             |DDEFECTO #901;
             aAlfa1 = aAlfa % 108;
             #901#0 = aAlfa1;
             |GRABA 020#901b;
         |FINSI;

         aAlfa1 = aAlfa % 1004;  #901#2 = aAlfa1;
         |GRABA 020#901a;
         |LIBERA #901;
     |SIGUE;

     |XCIERRA nHandle;
|FPRC;

|PROCESO dsorm001;
     #902#0 = #1#0;
     |LEE 101#902=;
     |SI FSalida ! 0;
         |BORRA 020#1a;
         |LIBERA #1;
     |SINO;
         #902#1 = #1#1;
         |GRABA 020#902a;
         |LIBERA #902;
     |FINSI;
|FPRC;

|DEFBUCLE dsorm001;
     #1#1;
     ;
     0001;
     9899;
     be;
     NULL, dsorm001;
|FIN;

|PROCESO TABLAGRUPOS;
     |DBASS aBase; |QBF aBase;
     aAbre = aBase + "tablasnet";  |MKDIR aAbre;
     aAbre = aAbre + "/tablagrupos.txt";

     |XABRE "U", aAbre, nHandle;
     |SI FSalida < 0;  |ACABA;  |FINSI;

     |PARA;  |SI;  |HACIENDO;
         |XLEE nHandle, 250, aAlfa;
         |SI FSalida < 0;  |SAL;  |FINSI;

         aAlfa1 = aAlfa % 103;
         |SI aAlfa1 = aMaster;
             |DDEFECTO #902;
             aAlfa1 = aAlfa % 504;
             #902#0 = aAlfa1;
             |LEE 101#902=;
             |SI FSalida ! 0;
                 |DDEFECTO #902;
                  aAlfa1 = aAlfa % 504;
                  #902#0 = aAlfa1;
                  |GRABA 020#902n;
             |FINSI;
         |FINSI;
     |SIGUE;

     |XCIERRA nHandle;

     |BUCLE dsorm001;
|FPRC;

|PROCESO LeeTablas;
     aFichero = ("tabl" + Usuario + "zzzzzzzz") % 108;
     |NOME_DAT #901 aFichero;

     |ABRE #901;
     |HAZ TABLAUSERFICH;
     |HAZ TABLAUSERAPLI;
     |CIERRA #901;

     |ABRE #902;
     |HAZ TABLAGRUPOS;
     |CIERRA #902;
|FPRC;

|PROCESO dsorw050;
     aAlfa = ("000" + #900#4) % -103;
     aAlfa = aAlfa + #900#1;
     aAlfa = aAlfa + #900#2;
     aAlfa = aAlfa + #900#3;
     |XGRABA nHandle, aAlfa;

     #101#0 = #900#4;
     |LEE 101#101=;
     |SI FSalida ! 0;
         |DDEFECTO #101;
         #101#0 = #900#4;
         |GRABA 020#101b;
     |FINSI;

     #101#1 = #900#1;
     #101#2 = #900#5;
     |GRABA 020#101a;
     |LIBERA #101;
|FPRC;

|DEFBUCLE dsorw050;
     #900#1;
     ;
     000;
     999;
     ;
     NULL, dsorw050;
|FIN;

|PROCESO GrabaUsuarios;
     |DBASS aBase; |QBF aBase;
     aAbre = aBase + "dev/usr/ds_sys.psw"
     |XABRE "W", aAbre, nHandle;

     |ABRE #101;
     |BUCLE dsorw050;
     |CIERRA #101;

     |XCIERRA nHandle;

     |DELINDEX #900;

     |DESCARGA_DEF #agiuser1@;
|FPRC;

|PROCESO dsorw051;
     aAlfa = #901#0 + "," + #901#1;
     |XGRABA nHandle1, aAlfa;

     aAlfa = #901#0 + "," + #901#2;
     |XGRABA nHandle2, aAlfa;
|FPRC;

|DEFBUCLE dsorw051;
     #901#1;
     ;
     "        ";
     "zzzzzzzz";
     ;
     NULL, dsorw051;
|FIN;

|PROCESO GrabaTablas;
     |DBASS aBase; |QBF aBase;
     aAbre = aBase + "tablasnet/tablauserfich.txt";
     |XABRE "W", aAbre, nHandle1;

     aAbre = aBase + "tablasnet/tablauserapli.txt";
     |XABRE "W", aAbre, nHandle2;

     |BUCLE dsorw051;

     |XCIERRA nHandle1;
     |XCIERRA nHandle2;

     |DELINDEX #901;
|FPRC;

|PROCESO dsorm051Chequeo;
     aAlfa = #51#3;  |QBF aAlfa;
     |SI aAlfa = "";  nError = 1;  |ERROR10;  |ACABA;  |FINSI;

     |SI #51#5 = 0;   nError = 1;  |ERROR10;  |ACABA;  |FINSI;
|FPRC;

|DEFBUCLE dsorm051Chequeo;
     #51#1;
     ;
     nMaster, 000, 01;
     nMaster, 9991, 99;
     be;
     NULL, dsorm051Chequeo;
|FIN;

|PROGRAMA;
     |HAZ IniSemaforo;

     |SI nError = 1;
         |MENAV "0000 El programa esta siendo usuado por otro usuario, intentelo mas tarde";
         |ACABA;
     |FINSI;

     aMaster = Usuario % 0803;

     |HAZ LeeUsuarios;
     |HAZ LeeTablas;

     |PARA;  |SI;  |HACIENDO;
          |ENTLINEAL #1#50, -2, 2, 22, 1, Baja50, Alta50;

          nError  = 0;
          nMaster = aMaster;
          |BUCLE dsorm051Chequeo;
          |SI nError = 0;  |SAL;  |FINSI;

          |MENAV "0000 Usuarios sin Nombre o sin Codigo de Grupo, Reviselos.";
     |SIGUE;

     |HAZ GrabaUsuarios;
     |HAZ GrabaTablas;

     |DELINDEX #902;

     |HAZ FinSemaforo;
|FPRO;
