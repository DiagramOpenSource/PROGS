|FICHEROS;
     dsorz011 #0;
     dsorm001 #1;
     dsorm002 #2;
     dsorm004 #4;
     dsorm009 #9,MANTE;
     dsorm010 #10,MANTE;
     dsorw005 #91,MANTE;

     dsorw007 #97,MANTE;
     dsorw008 #98,MANTE;
     dsorw009 #99,MANTE;

     referen@ #900;
|FIN;

|VARIABLES;
     aBase       = "";
     aMas        = "";
     aQueQuiero  = "";
     aAlfa       = "";
     aAlfa1      = "";
     aAlfa2      = "";
     aTitulo     = "";
     aFichero    = "";
     aCarga      = "";
     aDirDef     = "";
     aMensa      = "";
     aDef        = "";
     aAplica     = "";
     aBaseDef    = "";
     aParametro  = "";

     nTCampo     = 0;
     nModo       = 0;
     nCampo      = 0;
     nSwHay      = 0;
     nBusca      = 0;

     &eaAlta         = "";
     &eaConsulta     = "";
     &eaModificacion = "";
     &eaBaja         = "";

     &enGrupo        = 0;
     &eaTitulo       = "";
|FIN;

|PROCESO Mensa07;  |TIPO 7;
     |MENSA "0000 Pulse <F2> Lineal Aplicaciones ";
|FPRC;

|PROCESO Mensa08;  |TIPO 7;
     |MENSA "0000 Pulse <F2> Lineal Tablas ";
|FPRC;

|PROCESO Mensa09;  |TIPO 7;
     |MENSA "0000 Introduzca tabla y pulse INTRO";
|FPRC;

|RUTINA ClaveBaja6;
    #9#0 = #99#0;
    #9#1 = #99#1;
    #9#2 = #99#2;
    #9#3 = 000;
|FRUT;

|RUTINA ClaveAlta6;
    #9#0 = #99#0;
    #9#1 = #99#1;
    #9#2 = #99#2;
    #9#3 = 999;
|FRUT;

|PROCESO Tipo3;
     |ENTLINEAL #1#9, -4, 2, 22, 1, ClaveBaja6, ClaveAlta6;

     |DELINDEX #91;
|FPRC;

|| --------------------------------------------------------------------------
|| PROGRAMAS DE ACCESO A LOS FILTROS
|| --------------------------------------------------------------------------

|PROCESO CargaDatos;  |TIPO 7;
     aFichero = "dsorw005" + Usuario;
     |NOME_DAT #91 aFichero;
     |ABRE #91;  |CIERRA #91;  |DELINDEX #91;

     |DBASS aBase;    |QBF aBase;
     aMas = aBase + #99#1 + "/def/" + #99#2 + ".mas";  |QBT aMas;
     |CARGA_DEF aMas, referen@;
     |SI FSalida ! 0;
         |MENAV "     No existe la Tabla";
         |PTEC 807;
         |ACABA;
     |FINSI;

     aQueQuiero = "|TITULO";
     #99#3       = #900#aQueQuiero#;  |PINTA #99#3;

     aQueQuiero = "|NC";
     aAlfa      = #900#aQueQuiero#;
     nTCampo    = aAlfa;
     nTCampo    = nTCampo - 1;

     |ABRE #91;
     |PARA nCampo = 0;  |SI nCampo [ nTCampo;  |HACIENDO nCampo = nCampo + 1;
           #91#0 = nCampo;

           aQueQuiero = "|C" + (("000" + nCampo) % -103) + "NOMBRE";
           aFichero   = (#900#aQueQuiero# + (" " * 20)) % 120;
           #91#1      = aFichero;

           |GRABA 020#91n;
           |LIBERA #91;
     |SIGUE;
     |CIERRA #91;

     |DESCARGA_DEF #referen@;

     |PUSHV 0501 2380;
     |HAZ Tipo3;
     |POPV;
|FPRC;

|PROCESO MiraTabla;  |TIPO 0;
     |DBASS aBase;    |QBF aBase;
     aMas = aBase + #99#1 + "/def/" + #99#2 + ".mas";  |QBT aMas;
     |CARGA_DEF aMas, referen@;
     |SI FSalida ! 0;
         |MENAV "     No existe la Tabla";
         |ERROR;
         |ACABA;
     |FINSI;
     |DESCARGA_DEF #referen@;
|FPRC;

|PROCESO Borra06;
     |BORRA 020#9a;
     |LIBERA #9;
|FPRC;

|DEFBUCLE dsorm009B;
     #9#1;
     ;
     #99#0, #99#1, #99#2, 000;
     #99#0, #99#1, #99#2, 999;
     be;
     NULL, Borra06;
|FIN;

|PROCESO Borra10;
     |BORRA 020#10a;
     |LIBERA #10;
|FPRC;

|DEFBUCLE dsorm010B;
     #10#1;
     ;
     #99#0, #99#1, #99#2, 000, 000;
     #99#0, #99#1, #99#2, 999, 999;
     be;
     NULL, Borra10;
|FIN;

|PROCESO Baja99;  |TIPO 2;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo ! 3;  |ACABA;  |FINSI;

     |BUCLE dsorm009B;
     |BUCLE dsorm010B;
|FPRC;

|RUTINA ClaveBaja98;
     #98#0 = #97#0;
     #98#1 = "        ";
|FRUT;

|RUTINA ClaveAlta98;
     #98#0 = #97#0;
     #98#1 = "zzzzzzzz";
|FRUT;

|RUTINA ClaveBaja99;
     #99#0 = #98#0;
     #99#1 = #98#1;
     #99#2 = "        ";
|FRUT;

|RUTINA ClaveAlta99;
     #99#0 = #98#0;
     #99#1 = #98#1;
     #99#2 = "zzzzzzzz";
|FRUT;

|PROCESO Saca98;  |TIPO 7;
     |ENTLINEAL #1#98, -2, 7, 22, 1, ClaveBaja98, ClaveAlta98;
     |ENTLINEAL #1#99, -3, 7, 22, 1, ClaveBaja99, ClaveAlta99;
|FPRC;

|PROCESO Entra98;  |TIPO 11;
     |SI FSalida ! 10;  |ACABA;  |FINSI;

     |ENTLINEAL #1#98, -2, 4, 22, 1, ClaveBaja98, ClaveAlta98;
|FPRC;

|PROCESO Saca99;  |TIPO 7;
     |ENTLINEAL #1#99, -3, 7, 22, 1, ClaveBaja99, ClaveAlta99;
|FPRC;

|PROCESO Entra99;  |TIPO 11;
     |SI FSalida ! 10;  |ACABA;  |FINSI;

     |ENTLINEAL #1#99, -3, 2, 22, 1, ClaveBaja99, ClaveAlta99;
|FPRC;

|PROCESO Graba97;
     #97#0 = #1#0;
     #97#1 = #1#1;
     |GRABA 020#97n;
     |LIBERA #97;
|FPRC;

|DEFBUCLE dsorm001;
     #1#1;
     ;
     0001;
     9999;
     ;
     NULL, Graba97;
|FIN;

|PROCESO Graba98;
     #98#0 = #97#0;
     #98#1 = #2#0;
     |LEE 101#98=;
     |SI FSalida ! 0;
         |DDEFECTO #98;
         #98#0 = #97#0;
         #98#1 = #2#0;
         #98#2 = #2#2;
         |GRABA 020#98n;
     |FINSI;
     |LIBERA #98;
|FPRC;

|DEFBUCLE dsorw007;
     #97#1;
     ;
     0001;
     9999;
     ;
     NULL, Graba98;
|FIN;

|PROCESO Mira97;
     |BUCLE dsorw007;
|FPRC;

|DEFBUCLE dsorm002;
     #2#1;
     ;
     "        ", "        ";
     "zzzzzzzz", "zzzzzzzz";
     ;
     NULL, Mira97;
|FIN;

|PROCESO Graba99;
     #99#0 = #9#0;
     #99#1 = #9#1;
     #99#2 = #9#2;
     |LEE 101#99=;
     |SI FSalida ! 0;
         |DDEFECTO #99;
         #99#0 = #9#0;
         #99#1 = #9#1;
         #99#2 = #9#2;
         #99#3 = #9#6;
         |GRABA 020#99n;
     |FINSI;
     |LIBERA #99;
|FPRC;

|DEFBUCLE dsorm009;
     #9#1;
     ;
     0001, "        ", "        ", 000;
     9999, "zzzzzzzz", "zzzzzzzz", 999;
     ;
     NULL, Graba99;
|FIN;

|RUTINA ClaveBaja97;
     #97#0 = 1;
|FRUT;

|RUTINA ClaveAlta97;
     #97#0 = 9999;
|FRUT;

|PROGRAMA;
     aParametro = PARAMETRO;  |QBF aParametro;

     |CLS;
     |CABEZA "Asistente Filtros Campos Programa";

     aFichero = "dsorw007" + Usuario;
     |NOME_DAT #97 aFichero;
     |ABRE #97;  |CIERRA #97;  |DELINDEX #97;

     aFichero = "dsorw008" + Usuario;
     |NOME_DAT #98 aFichero;
     |ABRE #98;  |CIERRA #98;  |DELINDEX #98;

     aFichero = "dsorw009" + Usuario;
     |NOME_DAT #99 aFichero;
     |ABRE #99;  |CIERRA #99;  |DELINDEX #99;

     |ABRE #97;
     |SI aParametro = "";
         |BUCLE dsorm001;
     |SINO;
         #97#0  = enGrupo;
         #97#1  = eaTitulo;
         |GRABA 020#97n;
         |LIBERA #97;
     |FINSI;
     |CIERRA #97;

     |ABRE #98;
     |BUCLE dsorm002;
     |CIERRA #98;

     |ABRE #99;
     |BUCLE dsorm009;
     |CIERRA #99;

     |ENTLINEAL #1#97, -1, 4, 22, 1, ClaveBaja97, ClaveAlta97;

     |DELINDEX #97;
     |DELINDEX #98;
     |DELINDEX #99;
|FPRO;
