|FICHEROS;
     dsorz004 #0;

     dsorm001 #1;
     dsorm002 #2;
     dsorm004 #4;
     dsorm006 #6,MANTE;
     dsorm007 #7,MANTE;
     dsorm008 #8;

     dsorw005 #91,MANTE;
     dsorw006 #92,MANTE;

     dsorw007 #97,MANTE;
     dsorw008 #98,MANTE;
     dsorw009 #99,MANTE;

     referen@ #900;
|FIN;

|VARIABLES;
     aBase       = "";
     aMas        = "";
     aQueQuiero  = "";
     aAlfa       = "";
     aAlfa1      = "";
     aAlfa2      = "";
     aTitulo     = "";
     aFichero    = "";
     aCarga      = "";
     aDirDef     = "";
     aMensa      = "";
     aRelacion   = "";
     aDef        = "";
     aAplica     = "";
     aBaseDef    = "";

     nTCampo     = 0;
     nModo       = 0;
     nCampo      = 0;
     nSwHay      = 0;
     nRelacion   = 0;
     nBusca      = 0;
     nDGrupo     = 0;
     nHGrupo     = 0;

     &eaAlta         = "";
     &eaConsulta     = "";
     &eaModificacion = "";
     &eaBaja         = "";
     &eaParametro    = "";
     &enArea         = 0;
     &enNivel        = 0;
     &eaTitulo       = "";
|FIN;

|PROCESO Mensa07;  |TIPO 7;
     |SI eaParametro = "DSPREVEN";
         |MENSA "0000 Pulse <F2> Lineal Aplicaciones; <F3> Recoger del FILTRO MASTER";
     |SINO;
         |MENSA "0000 Pulse <F2> Lineal Aplicaciones; ";
     |FINSI;
|FPRC;

|PROCESO Mensa08;  |TIPO 7;
     |MENSA "0000 Pulse <F2> Lineal Tablas ";
|FPRC;

|PROCESO Mensa09;  |TIPO 7;
     |MENSA "0000 Introduzca tabla y pulse INTRO";
|FPRC;

|RUTINA ClaveBaja6;
    #6#0 = #99#0;
    #6#1 = #99#1;
    #6#2 = #99#2;
    #6#3 = 001;
|FRUT;

|RUTINA ClaveAlta6;
    #6#0 = #99#0;
    #6#1 = #99#1;
    #6#2 = #99#2;
    #6#3 = 999;
|FRUT;

|PROCESO MiraRelacion;
     aCarga = aDirDef;  |QBT aCarga;
     |CARGA_DEF aCarga, referen@;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |MODO_RELACION #900, 0, aRelacion, aAlfa1, aAlfa2;
     |SI FSalida ] 0;
         nRelacion  = FSalida;
         aQueQuiero = "|TITULO";
         aTitulo    = #900#aQueQuiero#;

         |DDEFECTO #92;
         #92#0  = aDef;
         #92#1  = aTitulo;
         #92#2  = nRelacion;
         #92#3  = "N";

         #8#0   = #99#0;
         #8#1   = #99#1;
         #8#2   = #99#2;
         #8#3   = aDef;
         |LEE 040#8=;
         |SI FSalida = 0;
             #92#3  = "S";
             #92#4  = #8#6;
             #92#5  = #8#7;
             #92#6  = #8#8;
             #92#7  = #8#9;
         |FINSI;

         eaAlta         = #6#15;
         eaConsulta     = #6#16;
         eaModificacion = #6#17;
         eaBaja         = #6#18;

         nBusca = 1;

         |GRABA 020#92n;
         |LIBERA #92;
     |FINSI;

     |DESCARGA_DEF #referen@;
|FPRC;

|RUTINA ClaveBaja92;
     #92#0  = "        ";
|FRUT;

|RUTINA ClaveAlta92;
     #92#0  = "zzzzzzzz";
|FRUT;

|PROCESO Borra8;
     |BORRA 020#8a;
     |LIBERA #8;
|FPRC;

|DEFBUCLE dsorm008;
     #8#1;
     ;
     #99#0, #99#1, #99#2, "        ";
     #99#0, #99#1, #99#2, "zzzzzzzz";
     be;
     NULL, Borra8;
|FIN;

|PROCESO Graba8;
     #8#0   = #99#0;
     #8#1   = #99#1;
     #8#2   = #99#2;
     #8#3   = #92#0;
     #8#4   = #92#1;
     #8#5   = #92#2;
     #8#6   = #92#4;
     #8#7   = #92#5;
     #8#8   = #92#6;
     #8#9   = #92#7;
     |GRABA 020#8n;
     |LIBERA #8;
|FPRC;

|DEFBUCLE dsorw006;
     #92#1;
     3;
     "        ", "S";
     "zzzzzzzz", "S";
     ;
     NULL, Graba8;
|FIN;

|PROCESO Relaciones;
     aFichero = "dsorw006" + Usuario;
     |NOME_DAT #92 aFichero;
     |ABRE #92;  |CIERRA #92;  |DELINDEX #92;

     |ABRE #92;
     |ABRE #8;
     |DBASS aBase;  |QBF aBase;
     nBusca    = 0;
     aRelacion = #99#2;  |QBF aRelacion;
     aAplica   = #99#1;  |QBF aAplica;
     aBaseDef  = aBase + aAplica + "/def/";
     aDirDef   = aBaseDef + "*.mas";
     |_PDIR aDirDef;
     |PARA;  |SI FSalida ] 0; |HACIENDO;
          aDef = aDirDef - aBaseDef;
          aDef = aDef - ".mas";
          |HAZ MiraRelacion;
          |_SDIR aDirDef;
     |SIGUE;
     |CIERRA #92;

     |SI nBusca = 1;
         |PUSHV 0501 2380;
         |ENTLINEAL #1#92, -1, 4, 22, 1, ClaveBaja92, ClaveAlta92;
         |POPV;

         |BUCLE dsorm008;
         |BUCLE dsorw006;
     |SINO;
         |MENAV "     No hay Tablas Relacionadas.";
     |FINSI;

     |CIERRA #8;
     |DELINDEX #92;

     |ERROR;
|FPRC;

|PROCESO Tipo3;
     |MENSA "0000_";

     |ENTLINEAL #1#6, -4, 2, 22, 1, ClaveBaja6, ClaveAlta6;

     |DELINDEX #91;

     nSwHay = 0;
     |ABRE #6;
     #6#0 = #99#0;
     #6#1 = #99#1;
     #6#2 = #99#2;
     #6#3 = 1;
     |LEE 040#6];
     |SI FSalida = 0; |Y #6#0 = #99#0;  |Y #6#1 = #99#1;  |Y #6#2 = #99#2;
         nSwHay = 1;
         |CONFI "0000SDesea Activar las Tablas Relacionadas con este Fichero.";
         |SI FSalida = 0;  |HAZ Relaciones;  |FINSI;
     |FINSI;
     |CIERRA #6;

     |SI nSwHay = 0;
         |BUCLE dsorm008;
     |FINSI;
|FPRC;

|| --------------------------------------------------------------------------
|| PROGRAMAS DE ACCESO A LOS FILTROS
|| --------------------------------------------------------------------------

|PROCESO CargaDatos;  |TIPO 7;
     aFichero = "dsorw005" + Usuario;
     |NOME_DAT #91 aFichero;
     |ABRE #91;  |CIERRA #91;  |DELINDEX #91;

     |DBASS aBase;    |QBF aBase;
     aMas = aBase + #99#1 + "/def/" + #99#2 + ".mas";  |QBT aMas;
     |CARGA_DEF aMas, referen@;
     |SI FSalida ! 0;
         |MENAV "     No existe la Tabla";
         |PTEC 807;
         |ACABA;
     |FINSI;

     aQueQuiero = "|TITULO";
     #99#3       = #900#aQueQuiero#;  |PINTA #99#3;

     aQueQuiero = "|NC";
     aAlfa      = #900#aQueQuiero#;
     nTCampo    = aAlfa;
     nTCampo    = nTCampo - 1;

     |ABRE #91;
     |PARA nCampo = 0;  |SI nCampo [ nTCampo;  |HACIENDO nCampo = nCampo + 1;
           #91#0 = nCampo;

           aQueQuiero = "|C" + (("000" + nCampo) % -103) + "NOMBRE";
           aFichero   = (#900#aQueQuiero# + (" " * 20)) % 120;
           #91#1      = aFichero;

           |GRABA 020#91n;
           |LIBERA #91;
     |SIGUE;
     |CIERRA #91;

     |DESCARGA_DEF #referen@;

     |PUSHV 0501 2380;
     |HAZ Tipo3;
     |POPV;
|FPRC;

|PROCESO MiraTabla;  |TIPO 0;
     |DBASS aBase;    |QBF aBase;
     aMas = aBase + #99#1 + "/def/" + #99#2 + ".mas";  |QBT aMas;
     |CARGA_DEF aMas, referen@;
     |SI FSalida ! 0;
         |MENAV "     No existe la Tabla";
         |ERROR;
         |ACABA;
     |FINSI;
     |DESCARGA_DEF #referen@;
|FPRC;

|PROCESO Borra06;
     |BORRA 020#6a;
     |LIBERA #6;
|FPRC;

|DEFBUCLE dsorm006B;
     #6#1;
     ;
     #99#0, #99#1, #99#2, 001;
     #99#0, #99#1, #99#2, 999;
     be;
     NULL, Borra06;
|FIN;

|PROCESO Borra07;
     |BORRA 020#7a;
     |LIBERA #7;
|FPRC;

|DEFBUCLE dsorm007B;
     #7#1;
     ;
     #99#0, #99#1, #99#2, 001, 000;
     #99#0, #99#1, #99#2, 999, 999;
     be;
     NULL, Borra07;
|FIN;

|PROCESO Borra08;
     |BORRA 020#8a;
     |LIBERA #8;
|FPRC;

|DEFBUCLE dsorm008B;
     #8#1;
     ;
     #99#0, #99#1, #99#2, "        ";
     #99#0, #99#1, #99#2, "zzzzzzzz";
     be;
     NULL, Borra08;
|FIN;

|PROCESO Baja99;  |TIPO 2;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo ! 3;  |ACABA;  |FINSI;

     |BUCLE dsorm006B;
     |BUCLE dsorm007B;
     |BUCLE dsorm008B;
|FPRC;

|RUTINA ClaveBaja98;
     #98#0 = #97#0;
     #98#1 = "        ";
|FRUT;

|RUTINA ClaveAlta98;
     #98#0 = #97#0;
     #98#1 = "zzzzzzzz";
|FRUT;

|RUTINA ClaveBaja99;
     #99#0 = #98#0;
     #99#1 = #98#1;
     #99#2 = "        ";
|FRUT;

|RUTINA ClaveAlta99;
     #99#0 = #98#0;
     #99#1 = #98#1;
     #99#2 = "zzzzzzzz";
|FRUT;

|PROCESO Saca98;  |TIPO 7;
     |ENTLINEAL #1#98, -2, 7, 22, 1, ClaveBaja98, ClaveAlta98;
     |ENTLINEAL #1#99, -3, 7, 22, 1, ClaveBaja99, ClaveAlta99;
|FPRC;

|PROCESO dsorm007M;
     #7#0 = #97#0;
     |GRABA 020#7n;
     |LIBERA #7;

     #7#0 = 0;
     |LEE 000#7=;
|FPRC;

|DEFBUCLE dsorm007M;
     #7#1;
     ;
     #6#0, #6#1, #6#2, #6#3, 000;
     #6#0, #6#1, #6#2, #6#3, 999;
     ;
     NULL, dsorm007M;
|FIN;

|PROCESO dsorm008M;
     #8#0 = #97#0;
     |GRABA 020#8n;
     |LIBERA #8;

     #8#0 = 0;
     |LEE 000#8=;
|FPRC;

|DEFBUCLE dsorm008M;
     #8#1;
     ;
     0, "        ", "        ", "        ";
     0, "zzzzzzzz", "zzzzzzzz", "zzzzzzzz";
     ;
     NULL, dsorm008M;
|FIN;

|PROCESO dsorm006M;
     |BUCLE dsorm007M;

     #6#0 = #97#0;
     |GRABA 020#6n;
     |LIBERA #6;

     #6#0 = 0;
     |LEE 000#6=;
|FPRC;

|DEFBUCLE dsorm006M;
     #6#1;
     ;
     0, "        ", "        ", 001;
     0, "zzzzzzzz", "zzzzzzzz", 999;
     ;
     NULL, dsorm006M;
|FIN;

|PROCESO Entra98;  |TIPO 11;
     |SI FSalida = 10;
         |ENTLINEAL #1#98, -2, 4, 22, 1, ClaveBaja98, ClaveAlta98;
     |FINSI;

     |SI FSalida = 11;   |Y eaParametro = "DSPREVEN";
         |ABRE #6;
         #6#0 = #97#0;
         #6#1 = "";
         #6#2 = "";
         #6#3 = 1;
         |LEE 000#6];
         |SI FSalida = 0;  |Y  #6#0 = #97#0;
             |MENAV "0000 Ya hay registros introducidos en este grupo";
             |CIERRA #6;
             |ACABA;
         |FINSI;
         |CIERRA #6;

         |BUCLE dsorm006M;
         |BUCLE dsorm008M;

         |MENAV "0000 Recogido datos del codigo MASTER";

         |PTEC 806;
     |FINSI;
|FPRC;

|PROCESO Saca99;  |TIPO 7;
     |MENSA "0000 <F2>Lineal Tablas";

     |ENTLINEAL #1#99, -3, 7, 22, 1, ClaveBaja99, ClaveAlta99;
|FPRC;

|PROCESO Entra99;  |TIPO 11;
     |SI FSalida ! 10;  |ACABA;  |FINSI;

     |ENTLINEAL #1#99, -3, 2, 22, 1, ClaveBaja99, ClaveAlta99;
|FPRC;

|PROCESO Graba97;
     #97#0 = #1#0;
     #97#1 = #1#1;
     |GRABA 020#97n;
     |LIBERA #97;
|FPRC;

|DEFBUCLE dsorm001;
     #1#1;
     ;
     nDGrupo;
     nHGrupo;
     ;
     NULL, Graba97;
|FIN;

|PROCESO Graba98;
     #98#0 = #97#0;
     #98#1 = #2#0;
     |LEE 101#98=;
     |SI FSalida ! 0;
         |DDEFECTO #98;
         #98#0 = #97#0;
         #98#1 = #2#0;
         #98#2 = #2#2;
         |GRABA 020#98n;
     |FINSI;
     |LIBERA #98;
|FPRC;

|DEFBUCLE dsorw007;
     #97#1;
     ;
     nDGrupo;
     nHGrupo;
     ;
     NULL, Graba98;
|FIN;

|PROCESO Mira97;
     |BUCLE dsorw007;
|FPRC;

|DEFBUCLE dsorm002;
     #2#1;
     ;
     "        ", "        ";
     "zzzzzzzz", "zzzzzzzz";
     ;
     NULL, Mira97;
|FIN;

|PROCESO Graba99;
     #99#0 = #6#0;
     #99#1 = #6#1;
     #99#2 = #6#2;
     |LEE 101#99=;
     |SI FSalida ! 0;
         |DDEFECTO #99;
         #99#0 = #6#0;
         #99#1 = #6#1;
         #99#2 = #6#2;
         #99#3 = #6#6;
         |GRABA 020#99n;
     |FINSI;
     |LIBERA #99;
|FPRC;

|DEFBUCLE dsorm006;
     #6#1;
     ;
     nDGrupo, "        ", "        ", 001;
     nHGrupo, "zzzzzzzz", "zzzzzzzz", 999;
     ;
     NULL, Graba99;
|FIN;

|RUTINA ClaveBaja97;
     #97#0 = nDGrupo;
|FRUT;

|RUTINA ClaveAlta97;
     #97#0 = nHGrupo;
|FRUT;

|PROGRAMA;
     eaParametro = PARAMETRO;  |QBF eaParametro;

     |CLS;
     |CABEZA "Asistente Filtros Campos General";

     aFichero = "dsorw007" + Usuario;
     |NOME_DAT #97 aFichero;
     |ABRE #97;  |CIERRA #97;  |DELINDEX #97;

     aFichero = "dsorw008" + Usuario;
     |NOME_DAT #98 aFichero;
     |ABRE #98;  |CIERRA #98;  |DELINDEX #98;

     aFichero = "dsorw009" + Usuario;
     |NOME_DAT #99 aFichero;
     |ABRE #99;  |CIERRA #99;  |DELINDEX #99;

     |ABRE #97;
     |SI eaParametro = "";
         nDGrupo = 1;
         nHGrupo = 9999;
         |BUCLE dsorm001;
     |FINSI;

     |SI eaParametro = "0000";
         nDGrupo = 0;
         nHGrupo = 0;
         #97#0  = eaParametro;
         #97#1  = "FILTRO MASTER";
         |GRABA 020#97n;
         |LIBERA #97;
     |FINSI;

     |SI eaParametro = "DSPREVEN";
         nDGrupo = 1;
         nHGrupo = 9999;

         #97#0  = (enArea * 100) + enNivel;
         #97#1  = eaTitulo;
         |GRABA 020#97n;
         |LIBERA #97;
     |FINSI;
     |CIERRA #97;

     |ABRE #98;
     |BUCLE dsorm002;
     |CIERRA #98;

     |ABRE #99;
     |BUCLE dsorm006;
     |CIERRA #99;

     |ENTLINEAL #1#97, -1, 4, 22, 1, ClaveBaja97, ClaveAlta97;

     |DELINDEX #97;
     |DELINDEX #98;
     |DELINDEX #99;
|FPRO;
