|FICHEROS;
     dscurz02 #999;
     dscurm00 #0;
     dscurm14 #14;
     dscurw16 #916;
|FIN;

|VARIABLES;
     nGrid1         = 0;
     nPanta0        = 0;
     nVentana       = 0;
     nPos1          = 0;
     nPos2          = 0;
     nRango         = 0;
     nBoton0_1      = 0;
     nBoton0_2      = 0;
     nBoton0_3      = 0;

     aEsc1          = "";
     aEsc2          = "";
     aRetu          = "";
     aTecla         = "";
     aAlfa          = "";

     &enCursoBase   = 0;
|FIN;


|| ------------------------------------------------------------------------
|| PROCESOS DE ASIGNACION DE MENUS
|| ------------------------------------------------------------------------

|PROCESO BajaPanta1;
     #0#0 = 0;
|FPRC;

|PROCESO AltaPanta1;
     #0#0 = 9;
|FPRC;

|| ------------------------------------------------------
|| Procesos del Boton 0_1
|| ------------------------------------------------------

|PROCESO Boton0_1;
     |VENTANA 0501, 2799, -1, -1, "";
     nVentana = FSalida;

     enCursoBase = #0#0;
     |SUB_EJECUTA_NP "dscurw13";

     |FINVENTANA nVentana;

     |REFRESCACONTROL nGrid1;
|FPRC;

|| ------------------------------------------------------
|| Procesos del Boton 0_2
|| ------------------------------------------------------

|PROCESO Boton0_2;
     |CONFI "0000SSeguro de Instalar los Perfiles";
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |ESTADO_CONTROL nBoton0_1, "DISABLE";
     |ESTADO_CONTROL nBoton0_2, "DISABLE";
     |ESTADO_CONTROL nBoton0_3, "DISABLE";

     |BORRA_HIJOS_PAN #0#999;
     |SUB_PINPA #0#916, nPanta0;

     |SUB_EJECUTA "dsprez02;AUTO";

     |BORRA_HIJOS_PAN #0#999;

     |ESTADO_CONTROL nBoton0_1, "ENABLE";
     |ESTADO_CONTROL nBoton0_2, "ENABLE";
     |ESTADO_CONTROL nBoton0_3, "ENABLE";
|FPRC;

|| ------------------------------------------------------
|| Procesos del Boton 0_3
|| ------------------------------------------------------

|PROCESO Boton0_3;
     |CONFI "0000SSeguro de Generar los Ficheros Planos para el Curso";
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |ESTADO_CONTROL nBoton0_1, "DISABLE";
     |ESTADO_CONTROL nBoton0_2, "DISABLE";
     |ESTADO_CONTROL nBoton0_3, "DISABLE";

     |BORRA_HIJOS_PAN #0#999;
     |SUB_PINPA #0#916, nPanta0;

     |SUB_EJECUTA "dscurz01";

     |BORRA_HIJOS_PAN #0#999;

     |ESTADO_CONTROL nBoton0_1, "ENABLE";
     |ESTADO_CONTROL nBoton0_2, "ENABLE";
     |ESTADO_CONTROL nBoton0_3, "ENABLE";
|FPRC;

|| ------------------------------------------------------
|| Procesos del Inicio del Programa
|| ------------------------------------------------------

|PROCESO EventoPanta1;
     |SI FSalida = 1;  |O FSalida = 2;
         |ESTADO_CONTROL nBoton0_1, "ENABLE";

         |ABRE #14;
         |LEE 000#14p;
         |SI FSalida = 0;
             |ESTADO_CONTROL nBoton0_2, "ENABLE";
             |ESTADO_CONTROL nBoton0_3, "ENABLE";
         |SINO;
             |ESTADO_CONTROL nBoton0_2, "DISABLE";
             |ESTADO_CONTROL nBoton0_3, "DISABLE";
         |FINSI;
         |CIERRA #14;
     |FINSI;
|FPRC;

|PROCESO BotonesPanta0;
     |CONTROL_SIMPLE nPanta0, "BOTON,{{218}} ENTRAR", 1177, 1598, Boton0_1;
     nBoton0_1 = FSalida;

     |CONTROL_SIMPLE nPanta0, "BOTON,{{217}} Instalar PERFILES", 2277, 2398, Boton0_2;
     nBoton0_2 = FSalida;

     |CONTROL_SIMPLE nPanta0, "BOTON,{{219}} Generar CURSOS", 2577, 2698, Boton0_3;
     nBoton0_3 = FSalida;

     |ESTADO_CONTROL nBoton0_1, "DISABLE";

     |ABRE #14;
     |LEE 000#14p;
     |SI FSalida ! 0;
         |ESTADO_CONTROL nBoton0_2, "DISABLE";
         |ESTADO_CONTROL nBoton0_3, "DISABLE";
     |SINO;
         |ESTADO_CONTROL nBoton0_2, "ENABLE";
         |ESTADO_CONTROL nBoton0_3, "ENABLE";
     |FINSI;
     |CIERRA #14;
|FPRC;

|PROCESO Tipo80;  |TIPO 80;
     |ABRET #0;

     FSalida = 2799;
|FPRC;

|PROCESO Tipo90;  |TIPO 90;
     |CIERRAT #0;
|FPRC;

|PROGRAMA;
     |PINPA #0#999;  nPanta0 = FSalida;

     nRango = 4 + 8 + 16 + 32;
     nPos1  = 0802;
     nPos2  = 2675;

     |LINEAL_SIMPLE #1#0, nRango, nPos1, nPos2, BajaPanta1, AltaPanta1, EventoPanta1;
     nGrid1 = FSalida;

     |HAZ BotonesPanta0;

     aEsc1  = &255 + "806";
     aEsc2  = &255 + "807";
     aRetu  = &255 + "802";
     |PARA; |SI;  |HACIENDO;
         FSalida = "::Salir";
         |LEETECLA aTecla;
         |SI aTecla = aEsc1; |SAL;  |FINSI;
         |SI aTecla = aEsc2; |SAL;  |FINSI;
         |SI aTecla = aRetu; |SAL;  |FINSI;
     |SIGUE;

     |FIN_CONTROL_WINDOWS nBoton0_1;
     |FIN_CONTROL_WINDOWS nBoton0_2;
     |FIN_CONTROL_WINDOWS nBoton0_3;

     |DESTRUYECONTROL nGrid1;

     |PULSATECLA;
|FPRO;
