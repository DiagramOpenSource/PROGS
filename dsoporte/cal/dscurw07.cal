|FICHEROS;
     dscurm00 #0;
     dscurm02 #2;
     dscurm06 #6;
     dscurm07 #7;
     dscurw07 #907;
|FIN;

|VARIABLES;
     nBoton0_1      = 0;
     nBoton0_2      = 0;
     nBoton0_3      = 0;

     nPanta0        = 0;
     nCampoD        = 0;
     nCampoH        = 0;
     nCampoHasta    = 0;
     nCampo1        = 0;
     nVentana       = 0;
     nGrid1         = 0;
     nGrid2         = 0;
     nRango         = 0;
     nPos1          = 0;
     nPos2          = 0;
     nResp          = 0;
     nCampo         = 0;

     aAlfa          = "";
     aBase          = "";
     aMas           = "";
     aFichero       = "";

     &enCurso       = 0;
     &enModo        = 1;
     &eaAplica      = "";
     &eaPracti      = "";
|FIN;

|PROCESO BajaPanta0;
     #6#0  = #907#1;
     #6#1  = #907#4;
     #6#2  = "";
|FPRC;

|PROCESO AltaPanta0;
     #6#0  = #907#1;
     #6#1  = #907#4;
     #6#2  = "zz";
|FPRC;

|| ------------------------------------------------------
|| Procesos Bajas de Ficheros;
|| ------------------------------------------------------

|PROCESO dscurm07B;
     |BORRA 020#7a;
     |LIBERA #7;
|FPRC;

|DEFBUCLE dscurm07B;
     #7#1;
     ;
     #6#0, #6#1, #6#2, "  ";
     #6#0, #6#1, #6#2, "zz";
     be;
     NULL, dscurm07B;
|FIN;

|| ------------------------------------------------------
|| Procesos Grabacion de la Aplicacion
|| ------------------------------------------------------

|PROCESO GrabaAplicacion;
     #2#0 = #907#1;
     #2#1 = #907#4;
     |LEE 101#2=;
     |SI FSalida ! 0;
         |DDEFECTO #2;
         #2#0 = #907#1;
         #2#1 = #907#4;
         |GRABA 020#2b;
     |FINSI;

     #2#2 = #907#5;
     |GRABA 020#2a;
     |LIBERA #2;
|FPRC;

|| ------------------------------------------------------
|| Procesos del Boton 0_1
|| ------------------------------------------------------

|PROCESO Boton0_1;
     |HAZ GrabaAplicacion;

     |VENTANA 0501, 2799, -1, -1, "";
     nVentana = FSalida;

     enCurso   = #907#0;
     eaAplica  = #907#4;
     eaPracti  = "";
     enModo    = 1;
     |SUB_EJECUTA "dscurw08";

     |FINVENTANA nVentana;

     |REFRESCACONTROL nGrid1;
|FPRC;

|| ------------------------------------------------------
|| Procesos del Boton 0_2
|| ------------------------------------------------------

|PROCESO Boton0_2;
     |VENTANA 0501, 2799, -1, -1, "";
     nVentana = FSalida;

     enCurso   = #907#0;
     eaAplica  = #907#4;
     eaPracti  = #6#2;
     enModo    = 2;
     |SUB_EJECUTA "dscurw08";

     |FINVENTANA nVentana;

     |REFRESCACONTROL nGrid1;
|FPRC;

|| ------------------------------------------------------
|| Procesos del Boton 0_3
|| ------------------------------------------------------

|PROCESO Boton0_3;
     aAlfa = "0000NSeguro de dar de baja a " + #6#3;
     |CONFI aAlfa;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |LEE 101#6=;
     |SI FSalida = 0;
         |BUCLE dscurm07B;

         |BORRA 020#6a;
         |LIBERA #6;
     |FINSI;

     #6#0 = #907#1;
     #6#1 = #907#4;
     #6#2 = "";
     |LEE 000#6];
     |SI FSalida = 0;  |Y  #6#0 = #907#1;  |Y #6#1 = #907#4;
         |ESTADO_CONTROL nBoton0_2, "ENABLE";
         |ESTADO_CONTROL nBoton0_3, "ENABLE";
     |SINO;
         |ESTADO_CONTROL nBoton0_2, "DISABLE";
         |ESTADO_CONTROL nBoton0_3, "DISABLE";
     |FINSI;

     |REFRESCACONTROL nGrid1;
|FPRC;

|| ------------------------------------------------------
|| Procesos del Incio del Programa
|| ------------------------------------------------------

|PROCESO EventoPanta0;
     |SI FSalida = 1;  |O FSalida = 2;
         |ESTADO_CONTROL nBoton0_1, "ENABLE";

         |LEE 000#6=;
         |SI FSalida = 0;  |Y  #6#0 = #907#1;  |Y #6#1 = #907#4;
             |ESTADO_CONTROL nBoton0_2, "ENABLE";
             |ESTADO_CONTROL nBoton0_3, "ENABLE";
         |SINO;
             |ESTADO_CONTROL nBoton0_2, "DISABLE";
             |ESTADO_CONTROL nBoton0_3, "DISABLE";
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO BotonesPanta0;
     |CONTROL_SIMPLE nPanta0, "BOTON,{{193}} Alta", 1277, 1398, Boton0_1;
     nBoton0_1 = FSalida;

     |CONTROL_SIMPLE nPanta0, "BOTON,{{197}} Modificar", 1577, 1698, Boton0_2;
     nBoton0_2 = FSalida;

     |CONTROL_SIMPLE nPanta0, "BOTON,{{199}} Baja ", 1877, 1998, Boton0_3;
     nBoton0_3 = FSalida;

     |ESTADO_CONTROL nBoton0_1, "DISABLE";
     |ESTADO_CONTROL nBoton0_2, "DISABLE";
     |ESTADO_CONTROL nBoton0_3, "DISABLE";
|FPRC;

|PROCESO Aplicacion;  |TIPO 0;
     |SI enModo ! 1;  |ACABA;  |FINSI;

     #2#0 = #907#1;
     #2#1 = #907#4;
     |LEE 000#2=;
     |SI FSalida = 0;
         |MENAV "0000 La Aplicacion ya Existe.";
         |ERROR;
     |FINSI;
|FPRC;

|PROCESO Tipo12;  |TIPO 12;
|FPRC;

|PROCESO Tipo80;  |TIPO 80;
     |ABRET #6;

     FSalida = 2799;
|FPRC;

|PROCESO Tipo90;  |TIPO 90;
     |CIERRAT #6;
|FPRC;

|PROGRAMA;
     |C_M #907#4, 1, "S";

     |ABRE #0;
     #0#0 = enCurso;
     |LEE 000#0=;
     |SI FSalida ! 0;  |DDEFECTO #0; |FINSI;
     |CIERRA #0;

     #907#0 = #0#0;
     #907#1 = #0#1;
     #907#2 = #0#2;
     #907#3 = #0#3;

     |ABRE #2;
     |SI enModo ! 1;
         #2#0 = #907#1;
         #2#1 = eaAplica;
         |LEE 101#2=;
         |SI FSalida ! 0;  |DDEFECTO #2; |FINSI;

         #907#4 = #2#1;
         #907#5 = #2#2;

         |C_M #907#4, 1, "N";
     |FINSI;

     |CABEZA "Aplicaciones";

     |PINPA #0#907;  nPanta0 = FSalida;
     |PINDA #0#907;

     nRango = 4 + 8 + 16 + 32;
     nPos1  = 1102;
     nPos2  = 2672;

     |LINEAL_SIMPLE #1#6, nRango, nPos1, nPos2, BajaPanta0, AltaPanta0, EventoPanta0;
     nGrid1 = FSalida;

     |HAZ BotonesPanta0;

     |SI enModo = 1;  |ENDATOS #1#907;  |FINSI;
     |SI enModo = 2;  |ENDATOS #2#907;  |FINSI;

     |SI FSalida = 0;
         |HAZ GrabaAplicacion;
     |FINSI;
     |CIERRA #2;

     |FIN_CONTROL_WINDOWS nBoton0_1;
     |FIN_CONTROL_WINDOWS nBoton0_2;
     |FIN_CONTROL_WINDOWS nBoton0_3;

     |DESTRUYECONTROL nGrid1;

     |PULSATECLA;
|FPRO;
