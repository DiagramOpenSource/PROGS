|FICHEROS;
     dscurm10 #10;
     dscurm11 #11;
     dscurm12 #12;
     dscurw10 #910;
     dscurw11 #911,MANTE;
|FIN;

|VARIABLES;
     nBoton3_1      = 0;
     nBoton3_2      = 0;
     nBoton3_3      = 0;
     nBoton4_1      = 0;
     nBoton4_2      = 0;
     nBoton4_3      = 0;

     nPanta0        = 0;
     nPanta1        = 0;
     nPanta2        = 0;
     nPanta3        = 0;
     nPanta4        = 0;
     nPanta5        = 0;
     nCampoD        = 0;
     nCampoH        = 0;
     nCampoHasta    = 0;
     nCampo1        = 0;
     nVentana       = 0;
     nGrid1         = 0;
     nGrid2         = 0;
     nRango         = 0;
     nPos1          = 0;
     nPos2          = 0;
     nResp          = 0;
     nCampo         = 0;
     nLen           = 0;
     nCampos        = 0;
     nPos           = 0;

     aAlfa          = "";

     &eaEmpresa     = "";
     &eaTrabaja     = "";
     &enModo        = 0;
|FIN;

|PROCESO BajaPanta3;
     #11#0 = #910#0;
     #11#1 = "";
|FPRC;

|PROCESO AltaPanta3;
     #11#0 = #910#0;
     #11#1 = "z" * 40;
|FPRC;

|PROCESO BajaPanta4;
     #12#0 = #910#0;
     #12#1 = "";
|FPRC;

|PROCESO AltaPanta4;
     #12#0 = #910#0;
     #12#1 = "zzzzzzzzzzzz";
|FPRC;

|| ------------------------------------------------------
|| Procesos Grabacion del Curso
|| ------------------------------------------------------

|PROCESO GrabaEmpresa;
     #10#0 = #910#0;
     |LEE 101#10=;
     |SI FSalida ! 0;
         |DDEFECTO #10;
         #10#0 = #910#0;
         |GRABA 020#10b;
     |FINSI;

     |PARA nCampoD = 1;  |SI nCampoD [ nCampoHasta;  |HACIENDO nCampoD = nCampoD + 1;
           nCampoH   = nCampoD;
           #10nCampoD = #910nCampoH;
     |SIGUE;
     |GRABA 020#10a;
     |LIBERA #10;
|FPRC;

|| ------------------------------------------------------
|| Procesos del Boton 3_1
|| ------------------------------------------------------

|PROCESO Boton3_1;
     |BORRA_HIJOS_PAN #3#910;
     |SUB_PINPA #0#911, nPanta3;

     enModo = 1;
     |DDEFECTO #911;
     |C_M #911#0, 1, "S";

     |PINDA #0#911;
     |ENDATOS #1#911;
     |SI FSalida = 0;
         #11#0 = #910#0;
         #11#1 = #911#0;
         #11#2 = #911#1;
         |GRABA 020#11n;
         |LIBERA #11;
     |FINSI;

     |BORRA_HIJOS_PAN #3#910;
     |REFRESCACONTROL nGrid1;
|FPRC;

|| ------------------------------------------------------
|| Procesos del Boton 3_2
|| ------------------------------------------------------

|PROCESO Boton3_2;
     |BORRA_HIJOS_PAN #3#910;
     |SUB_PINPA #0#911, nPanta3;

     enModo = 2;
     |DDEFECTO #911;
     #911#0 = #11#1;
     #911#1 = #11#2;

     |C_M #911#0, 1, "N";

     |PINDA #0#911;
     |ENDATOS #1#911;
     |SI FSalida = 0;
         #11#0 = #910#0;
         #11#1 = #911#0;
         |LEE 101#11=;
         |SI FSalida = 0;
             #11#2 = #911#1;
             |GRABA 020#11a;
             |LIBERA #11;
         |FINSI;
     |FINSI;

     |BORRA_HIJOS_PAN #3#910;
     |REFRESCACONTROL nGrid1;
|FPRC;

|| ------------------------------------------------------
|| Procesos del Boton 3_3
|| ------------------------------------------------------

|PROCESO Boton3_3;
     aAlfa = "0000NSeguro de dar de baja a " + #11#1;
     |CONFI aAlfa;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |LEE 101#11=;
     |SI FSalida = 0;
         |BORRA 020#11a;
         |LIBERA #11;
     |FINSI;

     #11#0 = #910#0;
     #11#1 = "";
     |LEE 000#11];
     |SI FSalida = 0;  |Y  #11#0 = #910#0;
         |ESTADO_CONTROL nBoton3_2, "ENABLE";
         |ESTADO_CONTROL nBoton3_3, "ENABLE";
     |SINO;
         |ESTADO_CONTROL nBoton3_2, "DISABLE";
         |ESTADO_CONTROL nBoton3_3, "DISABLE";
     |FINSI;

     |REFRESCACONTROL nGrid1;
|FPRC;

|| ------------------------------------------------------
|| Procesos del Boton 4_1
|| ------------------------------------------------------

|PROCESO Boton4_1;
     |HAZ GrabaEmpresa;

     |VENTANA 0501, 2799, -1, -1, "";
     nVentana = FSalida;

     eaEmpresa = #910#0;
     eaTrabaja = "";
     enModo    = 1;
     |SUB_EJECUTA_NP "dscurw12";

     |FINVENTANA nVentana;

     |REFRESCACONTROL nGrid2;
|FPRC;

|| ------------------------------------------------------
|| Procesos del Boton 4_2
|| ------------------------------------------------------

|PROCESO Boton4_2;
     |VENTANA 0501, 2799, -1, -1, "";
     nVentana = FSalida;

     eaEmpresa = #910#0;
     eaTrabaja = #12#1;
     enModo    = 2;
     |SUB_EJECUTA_NP "dscurw12";

     |FINVENTANA nVentana;

     |REFRESCACONTROL nGrid2;
|FPRC;

|| ------------------------------------------------------
|| Procesos del Boton 4_3
|| ------------------------------------------------------

|PROCESO Boton4_3;
     aAlfa = "0000NSeguro de dar de baja a " + #12#2;
     |CONFI aAlfa;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |LEE 101#12=;
     |SI FSalida = 0;
         |BORRA 020#12a;
         |LIBERA #12;
     |FINSI;

     #12#0 = #910#0;
     #12#1 = "";
     |LEE 000#12];
     |SI FSalida = 0;  |Y  #12#0 = #910#0;
         |ESTADO_CONTROL nBoton4_2, "ENABLE";
         |ESTADO_CONTROL nBoton4_3, "ENABLE";
     |SINO;
         |ESTADO_CONTROL nBoton4_2, "DISABLE";
         |ESTADO_CONTROL nBoton4_3, "DISABLE";
     |FINSI;

     |REFRESCACONTROL nGrid2;
|FPRC;

|| ------------------------------------------------------
|| Procesos del Inicio del Programa
|| ------------------------------------------------------

|PROCESO EventoPanta3;
     |SI FSalida = 1;
         |ESTADO_CONTROL nBoton3_1, "ENABLE";

         |LEE 000#11=;
         |SI FSalida = 0;  |Y  #11#0 = #910#0;
             |ESTADO_CONTROL nBoton3_2, "ENABLE";
             |ESTADO_CONTROL nBoton3_3, "ENABLE";
         |SINO;
             |ESTADO_CONTROL nBoton3_2, "DISABLE";
             |ESTADO_CONTROL nBoton3_3, "DISABLE";
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO EventoPanta4;
     |SI FSalida = 1;
         |ESTADO_CONTROL nBoton4_1, "ENABLE";

         |LEE 000#12=;
         |SI FSalida = 0;  |Y  #12#0 = #910#0;
             |ESTADO_CONTROL nBoton4_2, "ENABLE";
             |ESTADO_CONTROL nBoton4_3, "ENABLE";
         |SINO;
             |ESTADO_CONTROL nBoton4_2, "DISABLE";
             |ESTADO_CONTROL nBoton4_3, "DISABLE";
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO BotonesPanta3;
     |CONTROL_SIMPLE nPanta3, "BOTON,{{193}} Alta", 0777, 0898, Boton3_1;
     nBoton3_1 = FSalida;

     |CONTROL_SIMPLE nPanta3, "BOTON,{{197}} Modificar", 1077, 1198, Boton3_2;
     nBoton3_2 = FSalida;

     |CONTROL_SIMPLE nPanta3, "BOTON,{{199}} Baja ", 1377, 1498, Boton3_3;
     nBoton3_3 = FSalida;

     |ESTADO_CONTROL nBoton3_1, "ENABLE";
     |ESTADO_CONTROL nBoton3_2, "DISABLE";
     |ESTADO_CONTROL nBoton3_3, "DISABLE";
|FPRC;

|PROCESO BotonesPanta4;
     |CONTROL_SIMPLE nPanta4, "BOTON,{{193}} Alta", 0777, 0898, Boton4_1;
     nBoton4_1 = FSalida;

     |CONTROL_SIMPLE nPanta4, "BOTON,{{197}} Modificar", 1077, 1198, Boton4_2;
     nBoton4_2 = FSalida;

     |CONTROL_SIMPLE nPanta4, "BOTON,{{199}} Baja ", 1377, 1498, Boton4_3;
     nBoton4_3 = FSalida;

     |ESTADO_CONTROL nBoton4_1, "ENABLE";
     |ESTADO_CONTROL nBoton4_2, "DISABLE";
     |ESTADO_CONTROL nBoton4_3, "DISABLE";
|FPRC;

|PROCESO Empresa;  |TIPO 0;
     |SI enModo ! 1;  |ACABA;  |FINSI;

     #10#0 = #910#0;
     |LEE 000#10=;
     |SI FSalida = 0;
         |MENAV "0000 La Empresa ya Existe.";
         |ERROR;
     |FINSI;
|FPRC;

|PROCESO Tipo12;  |TIPO 12;
|FPRC;

|PROCESO Tipo80;  |TIPO 80;
     |ABRET #11;
     |ABRET #12;

     FSalida = 2799;
|FPRC;

|PROCESO Tipo90;  |TIPO 90;
     |CIERRAT #11;
     |CIERRAT #12;
|FPRC;

|PROGRAMA;
     nCampoHasta = 35;

     |C_M #910#0, 1, "S";

     |ABRE #10;
     |SI enModo ! 1;
         #10#0 = eaEmpresa;
         |LEE 101#10=;
         |SI FSalida ! 0;  |DDEFECTO #10; |FINSI;

         |PARA nCampoD = 0;  |SI nCampoD [ nCampoHasta;   |HACIENDO nCampoD = nCampoD + 1;
               nCampoH = nCampoD;
               #910nCampoH = #10nCampoD;
         |SIGUE;

         |C_M #910#0, 1, "N";
     |FINSI;

     |CABEZA "Empresas";

     |PINPA #0#910;  nPanta0 = FSalida;
     |PINDA #0#910;

     |PINPA #1#910;  nPanta1 = FSalida;
     |PINDA #1#910;

     |PINPA #2#910;  nPanta2 = FSalida;
     |PINDA #2#910;

     |PINPA #3#910;  nPanta3 = FSalida;
     |PINDA #3#910;

     nRango = 4 + 8 + 16 + 32;
     nPos1  = 0602;
     nPos2  = 2270;

     |LINEAL_SIMPLE #1#11, nRango, nPos1, nPos2, BajaPanta3, AltaPanta3, EventoPanta3;
     nGrid1 = FSalida;

     |PINPA #4#910;  nPanta4 = FSalida;
     |PINDA #4#910;

     nRango = 4 + 8 + 16 + 32;
     nPos1  = 0602;
     nPos2  = 2258;

     |LINEAL_SIMPLE #1#12, nRango, nPos1, nPos2, BajaPanta4, AltaPanta4, EventoPanta4;
     nGrid2 = FSalida;

     |HAZ BotonesPanta3;
     |HAZ BotonesPanta4;

     |SI enModo = 1;  |ENDATOS #1#910;  |FINSI;
     |SI enModo = 2;  |ENDATOS #2#910;  |FINSI;

     |SI FSalida = 0;
         |HAZ GrabaEmpresa;
     |FINSI;
     |CIERRA #10;

     |FIN_CONTROL_WINDOWS nBoton3_1;
     |FIN_CONTROL_WINDOWS nBoton3_2;
     |FIN_CONTROL_WINDOWS nBoton3_3;
     |FIN_CONTROL_WINDOWS nBoton4_1;
     |FIN_CONTROL_WINDOWS nBoton4_2;
     |FIN_CONTROL_WINDOWS nBoton4_3;

     |DESTRUYECONTROL nGrid1;
     |DESTRUYECONTROL nGrid2;

     |PULSATECLA;
|FPRO;
