|FICHEROS;
     dsorm001 #1;
     dsorm002 #2;
     dsorm005 #5,MANTE;
     dsorw002 #90,MANTE;
     dsorw005 #91,MANTE;

     dsorw007 #97,MANTE;
     dsorw008 #98,MANTE;
     dsorw009 #99,MANTE;

     referen@ #900;
|FIN;

|VARIABLES;
     aBase        = "";
     aMas         = "";
     aQueQuiero   = "";
     aAlfa        = "";
     aTitulo      = "";
     aTituloCampo = "";
     aFichero     = "";
     aParametro  = "";

     nTCampo      = 0;
     nCampo       = 0;
     nModo        = 0;
     nSwHay       = 0;

     &enGrupo    = 0;
     &eaTitulo   = "";
|FIN;

|PROCESO Mensa07;  |TIPO 7;
     |MENSA "0000 Pulse <F2> Lineal Aplicaciones ";
|FPRC;

|PROCESO Mensa08;  |TIPO 7;
     |MENSA "0000 Pulse <F2> Lineal Tablas ";
|FPRC;

|PROCESO Mensa09;  |TIPO 7;
     |MENSA "0000 Introduzca tabla y pulse INTRO";
|FPRC;

|RUTINA ClaveBaja90;
     #90#0 = #99#0;
     #90#1 = #99#1;
     #90#2 = #99#2;
|FRUT;

|RUTINA ClaveAlta90;
     #90#0 = #99#0;
     #90#1 = #99#1;
     #90#2 = #99#2;
|FRUT;

|RUTINA ClaveBaja5;
     #5#0  = #90#0;
     #5#1  = #90#1;
     #5#2  = #90#2;
     #5#3  = 000;
|FRUT;

|RUTINA ClaveAlta5;
     #5#0  = #90#0;
     #5#1  = #90#1;
     #5#2  = #90#2;
     #5#3  = 999;
|FRUT;

|RUTINA ClaveBaja91;
     #91#0  = 000;
|FRUT;

|RUTINA ClaveAlta91;
     #91#0  = 999;
|FRUT;

|PROCESO Intro;  |TIPO 0;
     nCampo = #91#0;
     |PTEC 807;
|FPRC;

|PROCESO Tipo11m05;  |TIPO 11;
     |SI FSalida ! 10;  |ACABA;  |FINSI;

     nCampo = -1;
     |PUSHV 0501 2380;
     |ENTLINEAL #1#91, -1, 4, 22, 1, ClaveBaja91, ClaveAlta91;
     |PULSATECLA;
     |POPV;

     |SI nCampo = -1;  |ACABA;  |FINSI;

     #5#3 = nCampo;  |PINTA #5#3;
     |ERROR;
|FPRC;

|PROCESO Campo;
     |DBASS aBase;    |QBF aBase;
     aMas = aBase + #90#1 + "/def/" + #90#2 + ".mas";  |QBT aMas;
     |CARGA_DEF aMas, referen@;
     |SI FSalida ! 0;
         |MENAV "     No existe la Tabla";
         |ERROR;
         |ACABA;
     |FINSI;

     aQueQuiero   = "|C" + (("000" + #5#3) % -103) + "NOMBRE";
     aFichero     = (#900#aQueQuiero# + (" " * 20)) % 120;
     aTituloCampo = aFichero;  |QBF aTituloCampo;
     |SI aTituloCampo = "";
         |MENAV "     Campo Inexistente";
         |ERROR;
     |FINSI;

     |DESCARGA_DEF #referen@;
|FPRC;

|PROCESO TituloCampo;  |TIPO 7;
     #5#5 = aTituloCampo;  |PINTA #5#5;
     #5#4 = #90#3;
|FPRC;

|PROCESO Tipo7w90;  |TIPO 7;
     |ENTLINEAL #1#5, -4, 7, 22, 0, ClaveBaja5, ClaveAlta5;
|FPRC;

|PROCESO Borra5;
     |BORRA 020#5a;
     |LIBERA #5;
|FPRC;

|DEFBUCLE dsorm005B;
     #5#1;
     ;
     #99#0, #99#1, #99#2, 000;
     #99#0, #99#1, #99#2, 999;
     be;
     NULL, Borra5;
|FIN;

|PROCESO Tipo11w90;  |TIPO 11;
     |SI FSalida = 10;
        |ENTLINEAL #1#5, -4, 2, 22, 0, ClaveBaja5, ClaveAlta5;
     |FINSI;

     |SI FSalida = 11;
         |BUCLE dsorm005B;
     |FINSI;
|FPRC;

|PROCESO GrabaTempo;
     #90#0 = #5#0;
     #90#1 = #5#1;
     #90#2 = #5#2;
     |LEE 101#90=;
     |SI FSalida ! 0;
         #90#0 = #5#0;
         #90#1 = #5#1;
         #90#2 = #5#2;
         #90#3 = #99#3;
         |GRABA 020#90n;
     |FINSI;
     |LIBERA #90;
     nSwHay = 1;
|FPRC;

|DEFBUCLE dsorm005;
     #5#1;
     ;
     #99#0, #99#1, #99#2, 000;
     #99#0, #99#1, #99#2, 999;
     ;
     NULL, GrabaTempo;
|FIN;

|| --------------------------------------------------------------------------
|| PROGRAMAS DE ACCESO A LOS FILTROS
|| --------------------------------------------------------------------------

|PROCESO CargaDatos;  |TIPO 7;
     aFichero = "dsorw002" + Usuario;
     |NOME_DAT #90 aFichero;
     |ABRE #90;  |CIERRA #90;  |DELINDEX #90;

     aFichero = "dsorw005" + Usuario;
     |NOME_DAT #91 aFichero;
     |ABRE #91;  |CIERRA #91;  |DELINDEX #91;

     |DBASS aBase;    |QBF aBase;
     aMas = aBase + #99#1 + "/def/" + #99#2 + ".mas";  |QBT aMas;
     |CARGA_DEF aMas, referen@;
     |SI FSalida ! 0;
         |ACABA;
     |FINSI;

     aQueQuiero = "|TITULO";
     aTitulo    = #900#aQueQuiero#;
     #99#3      = aTitulo;  |PINTA #99#3;

     aQueQuiero = "|NC";
     aAlfa      = #900#aQueQuiero#;
     nTCampo    = aAlfa;
     nTCampo    = nTCampo - 1;

     |ABRE #91;
     |PARA nCampo = 0;  |SI nCampo [ nTCampo;  |HACIENDO nCampo = nCampo + 1;
           #91#0 = nCampo;

           aQueQuiero = "|C" + (("000" + nCampo) % -103) + "NOMBRE";
           aFichero   = (#900#aQueQuiero# + (" " * 20)) % 120;
           #91#1      = aFichero;

           |GRABA 020#91n;
           |LIBERA #91;
     |SIGUE;
     |CIERRA #91;

     |DESCARGA_DEF #referen@;

     |PUSHV 0501 2380;

     |ABRE #90;
     nSwHay = 0;
     |BUCLE dsorm005;
     |SI nSwHay = 0;
         #90#0 = #99#0;
         #90#1 = #99#1;
         #90#2 = #99#2;
         #90#3 = #99#3;
         |GRABA 020#90n;
         |LIBERA #90;
     |FINSI;
     |CIERRA #90;

     |ENTLINEAL #1#90, -3, 4, 22, 1, ClaveBaja90, ClaveAlta90;

     |POPV;

     |DELINDEX #90;
     |DELINDEX #91;
|FPRC;

|PROCESO MiraTabla;  |TIPO 0;
     |DBASS aBase;    |QBF aBase;
     aMas = aBase + #99#1 + "/def/" + #99#2 + ".mas";  |QBT aMas;
     |CARGA_DEF aMas, referen@;
     |SI FSalida ! 0;
         |MENAV "     No existe la Tabla";
         |ERROR;
         |ACABA;
     |FINSI;
     |DESCARGA_DEF #referen@;
|FPRC;

|PROCESO Baja99;  |TIPO 2;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo ! 3;  |ACABA;  |FINSI;

     |BUCLE dsorm005B;
|FPRC;

|RUTINA ClaveBaja98;
     #98#0 = #97#0;
     #98#1 = "        ";
|FRUT;

|RUTINA ClaveAlta98;
     #98#0 = #97#0;
     #98#1 = "zzzzzzzz";
|FRUT;

|RUTINA ClaveBaja99;
     #99#0 = #98#0;
     #99#1 = #98#1;
     #99#2 = "        ";
|FRUT;

|RUTINA ClaveAlta99;
     #99#0 = #98#0;
     #99#1 = #98#1;
     #99#2 = "zzzzzzzz";
|FRUT;

|PROCESO Saca98;  |TIPO 7;
     |ENTLINEAL #1#98, -2, 7, 22, 1, ClaveBaja98, ClaveAlta98;
     |ENTLINEAL #1#99, -3, 7, 22, 1, ClaveBaja99, ClaveAlta99;
|FPRC;

|PROCESO Entra98;  |TIPO 11;
     |SI FSalida ! 10;  |ACABA;  |FINSI;

     |ENTLINEAL #1#98, -2, 4, 22, 1, ClaveBaja98, ClaveAlta98;
|FPRC;

|PROCESO Saca99;  |TIPO 7;
     |ENTLINEAL #1#99, -3, 7, 22, 1, ClaveBaja99, ClaveAlta99;
|FPRC;

|PROCESO Entra99;  |TIPO 11;
     |SI FSalida ! 10;  |ACABA;  |FINSI;

     |ENTLINEAL #1#99, -3, 2, 22, 1, ClaveBaja99, ClaveAlta99;
|FPRC;

|PROCESO Graba97;
     #97#0 = #1#0;
     #97#1 = #1#1;
     |GRABA 020#97n;
     |LIBERA #97;
|FPRC;

|DEFBUCLE dsorm001;
     #1#1;
     ;
     0001;
     9999;
     ;
     NULL, Graba97;
|FIN;

|PROCESO Graba98;
     #98#0 = #97#0;
     #98#1 = #2#0;
     |LEE 101#98=;
     |SI FSalida ! 0;
         |DDEFECTO #98;
         #98#0 = #97#0;
         #98#1 = #2#0;
         #98#2 = #2#2;
         |GRABA 020#98n;
     |FINSI;
     |LIBERA #98;
|FPRC;

|DEFBUCLE dsorw007;
     #97#1;
     ;
     0001;
     9999;
     ;
     NULL, Graba98;
|FIN;

|PROCESO Mira97;
     |BUCLE dsorw007;
|FPRC;

|DEFBUCLE dsorm002;
     #2#1;
     ;
     "        ", "        ";
     "zzzzzzzz", "zzzzzzzz";
     ;
     NULL, Mira97;
|FIN;

|PROCESO Graba99;
     #99#0 = #5#0;
     #99#1 = #5#1;
     #99#2 = #5#2;
     |LEE 101#99=;
     |SI FSalida ! 0;
         |DDEFECTO #99;
         #99#0 = #5#0;
         #99#1 = #5#1;
         #99#2 = #5#2;
         #99#3 = #5#4;
         |GRABA 020#99n;
     |FINSI;
     |LIBERA #99;
|FPRC;

|DEFBUCLE dsorm005L;
     #5#1;
     ;
     0001, "        ", "        ", 000;
     9999, "zzzzzzzz", "zzzzzzzz", 999;
     ;
     NULL, Graba99;
|FIN;

|RUTINA ClaveBaja97;
     #97#0 = 1;
|FRUT;

|RUTINA ClaveAlta97;
     #97#0 = 9999;
|FRUT;

|PROGRAMA;
     aParametro = PARAMETRO;  |QBF aParametro;

     |CLS;
     |CABEZA "Asistente Ocultacion Campos";

     aFichero = "dsorw007" + Usuario;
     |NOME_DAT #97 aFichero;
     |ABRE #97;  |CIERRA #97;  |DELINDEX #97;

     aFichero = "dsorw008" + Usuario;
     |NOME_DAT #98 aFichero;
     |ABRE #98;  |CIERRA #98;  |DELINDEX #98;

     aFichero = "dsorw009" + Usuario;
     |NOME_DAT #99 aFichero;
     |ABRE #99;  |CIERRA #99;  |DELINDEX #99;

     |ABRE #97;
     |SI aParametro = "";
         |BUCLE dsorm001;
     |SINO;
         #97#0  = enGrupo;
         #97#1  = eaTitulo;
         |GRABA 020#97n;
         |LIBERA #97;
     |FINSI;
     |CIERRA #97;

     |ABRE #98;
     |BUCLE dsorm002;
     |CIERRA #98;

     |ABRE #99;
     |BUCLE dsorm005L;
     |CIERRA #99;

     |ENTLINEAL #1#97, -1, 4, 22, 1, ClaveBaja97, ClaveAlta97;

     |DELINDEX #97;
     |DELINDEX #98;
     |DELINDEX #99;
|FPRO;
