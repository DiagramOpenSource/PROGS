|FICHEROS;
     dsorz004 #0;
     dsorm006 #6;
     dsorm007 #7;
     dsorm008 #8;
     dsorw005 #91;
     dsorw006 #92;
     dsorw007 #97;
     dsorw008 #98;
     dsorw009 #99;

     referen@ #900;
|FIN;

|VARIABLES
     aBase       = "";
     aMas        = "";
     aQueQuiero  = "";
     aFichero    = "";
     aTitulo     = "";
     aAlfa       = "";
     aLongitud   = "";
     nCampo      = 0;
     nModo       = 0;
     nBorra      = 0;
     nNume       = 0;

     &eaParametro  = "";
|FIN;

|PROCESO Mensa;  |TIPO 7;
     |SI eaParametro = "";  |ACABA;  |FINSI;

     |MENSA "0000 (%1 Dpto)(%2 Subdpto.)(%3 Serie)(%4 Almacen)(%5 Zona)(%6 Empresa Admin.)";
|FPRC;

|PROCESO Formula;
     |SI aAlfa = "";
         aAlfa = "(" + #6#7;
     |SINO;
         |SI #6#14 = "S";
             aAlfa = aAlfa + ")" + " O " + "(" + #6#7;
         |SINO;
             aAlfa = aAlfa + " Y " + #6#7;
         |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE dsorm006F;
     #6#1;
     ;
     #99#0, #99#1, #99#2, 001;
     #99#0, #99#1, #99#2, 999;
     be;
     NULL, Formula;
|FIN;

|PROCESO MontaFormula;
     aAlfa = "";
     |BUCLE dsorm006F;
     aAlfa = aAlfa + ")";
     #0#0  = aAlfa;  |PINTA 801, #0#0;
|FPRC;

|PROCESO LeeCampo;
     |DBASS aBase;    |QBF aBase;
     aMas = aBase + #6#1 + "/def/" + #6#2 + ".mas";  |QBT aMas;
     |CARGA_DEF aMas, referen@;
     |SI FSalida ! 0;
         |MENAV "     No existe la Tabla";
         |ERROR;
         |ACABA;
     |FINSI;

     aQueQuiero = "|C" + (("000" + nCampo) % -103) + "NOMBRE";
     aFichero   = (#900#aQueQuiero# + (" " * 20)) % 120;
     aTitulo    = aFichero;  |QBF aTitulo;
     |SI aTitulo = "";
         |MENAV "     Campo Inexistente";
         |ERROR;
     |FINSI;

     aQueQuiero = "|C" + (("000" + nCampo) % -103) + "LONGITUD";
     aLongitud  = #900#aQueQuiero#;

     |DESCARGA_DEF #referen@;
|FPRC;

|RUTINA ClaveBaja7;
     #7#0 = #6#0;
     #7#1 = #6#1;
     #7#2 = #6#2;
     #7#3 = #6#3;
     #7#4 = 000;
|FRUT;

|RUTINA ClaveAlta7;
     #7#0 = #6#0;
     #7#1 = #6#1;
     #7#2 = #6#2;
     #7#3 = #6#3;
     #7#4 = 999;
|FRUT;

|RUTINA ClaveBaja91;
     #91#0  = 000;
|FRUT;

|RUTINA ClaveAlta91;
     #91#0  = 999;
|FRUT;

|PROCESO Intro;  |TIPO 0;
     nCampo = #91#0;
     |PTEC 807;
|FPRC;

|PROCESO Tipo7m06;  |TIPO 7;
     |SI nBorra = 1;
         nBorra = 0;
         |SALVA_DATOS #6;
         |HAZ MontaFormula;
         |REPON_DATOS #6;
         |LEE 040#6=;
     |FINSI;
     |ENTLINEAL #1#7, -5, 7, 22, 0,ClaveBaja7, ClaveAlta7;
|FPRC;

|PROCESO PonDescri;  |TIPO 7;
     #6#4 = #97#1;  |PINTA #6#4;
     #6#5 = #98#2;  |PINTA #6#5;
     #6#6 = #99#3;  |PINTA #6#6;
|FPRC;

|PROCESO TipoCampo;
     |SI #6#8 = "N";
         |C_M #6#9, 1, "S";
     |SINO;
         |C_M #6#9, 1, "N";  #6#9 = 0;  |PINTA #6#9;
         #6#10 = "CAMPOS COMPUESTOS";   |PINTA #6#10;
     |FINSI;
|FPRC;

|PROCESO TipoOperacion;
     |C_M #6#13, 1, "S";
     |SI #6#11 = "=";  |O #6#11 = "!";
         |C_M #6#13, 1, "N";  #6#13 = "";  |PINTA #6#13;
     |FINSI;
|FPRC;

|PROCESO LeeCampoM06;
     nNume = 70;

     |CAMPO_ANCHO  #6#12, 1, nNume;
     |CAMPO_ANCHO  #6#13, 1, nNume;

     |SI #6#8 ! "N";  |ACABA;  |FINSI;

     |SI FSalida = 10;
         aFichero = "dsorw005" + Usuario;
         |NOME_DAT #91#aFichero#;

         nCampo = -1;
         |PUSHV 0501 2380;
         |ENTLINEAL #1#91, -1, 4, 22, 1, ClaveBaja91, ClaveAlta91;
         |PULSATECLA;
         |POPV;

         |SI nCampo = -1;  |ACABA;  |FINSI;

         #6#9 = nCampo;  |PINTA #6#9;
     |FINSI;

     nCampo = #6#9;
     |HAZ LeeCampo;

     #6#10 = aTitulo;  |PINTA #6#10;

     nNume = aLongitud;

     |CAMPO_ANCHO  #6#12, 1, nNume;
     |CAMPO_ANCHO  #6#13, 1, nNume;

     |PINTA #6#12;
     |PINTA #6#13;
|FPRC;

|PROCESO Tipo11M07;  |TIPO 11;
     |SI FSalida ! 10;  |ACABA;  |FINSI;

     nCampo = -1;
     |PUSHV 0501 2380;
     |ENTLINEAL #1#91, -1, 4, 22, 1, ClaveBaja91, ClaveAlta91;
     |PULSATECLA;
     |POPV;

     |SI nCampo = -1;  |ACABA;  |FINSI;

     #7#4 = nCampo;  |PINTA #7#4;
     |ERROR;
|FPRC;

|PROCESO LeeCampoM07;
     nCampo = #7#4;
     |HAZ LeeCampo;

     #7#5 = aTitulo;  |PINTA #7#5;
|FPRC;

|PROCESO Tipo3m06;  |TIPO 3;
     |GRABA 020#6a;
     |LIBERA #6;

     |SALVA_DATOS #6;
     |HAZ MontaFormula;
     |REPON_DATOS #6;
     |LEE 040#6=;

     |SI #6#8 = "C";
         |ENTLINEAL #1#7, -5, 2, 22, 0,ClaveBaja7, ClaveAlta7;
     |FINSI;

     |PONCONTROL 23, "SI";
     |PTEC 808;
|FPRC;

|PROCESO Tipo5m06;  |TIPO 5;
     |HAZ MontaFormula;
|FPRC;

|PROCESO Borra7;
     |BORRA 020#7a;
     |LIBERA #7;
|FPRC;

|DEFBUCLE dsorm007B;
     #7#1;
     ;
     #6#0, #6#1, #6#2, #6#3, 000;
     #6#0, #6#1, #6#2, #6#3, 999;
     be;
     NULL, Borra7;
|FIN;

|PROCESO Tipo2m06;  |TIPO 2;
     nBorra = 0;
     nModo  = (FEntrada / 100) ! 0;
     |SI nModo ! 3;  |ACABA;  |FINSI;

     |BUCLE dsorm007B;

     nBorra = 1;
|FPRC;
