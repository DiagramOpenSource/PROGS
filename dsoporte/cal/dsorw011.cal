|FICHEROS;
     dsorw011 #0;
     dsorw012 #1;

     dsorm002 #2;

     dsorw005 #91,MANTE;

     referen@ #900;
|FIN;

|VARIABLES;
     aBase      = "";
     aDef       = "";
     aMas       = "";
     aQueQuiero = "";
     aFichero   = "";
     aLongitud  = "";
     aTipo      = "";
     aTitulo    = "";
     aAlfa      = "";

     nCampo     = 0;
     nTCampo    = 0;
     nNume      = 0;
|FIN;

|PROCESO Formula;
     |SI aAlfa = "";
         aAlfa = "(" + #1#1;
     |SINO;
         |SI #1#5 = "S";
             aAlfa = aAlfa + ")" + " O " + "(" + #1#1;
         |SINO;
             aAlfa = aAlfa + " Y " + #1#1;
         |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE dsorw012F;
     #1#1;
     ;
     001;
     999;
     ;
     NULL, Formula;
|FIN;

|PROCESO MontaFormula;
     aAlfa = "";
     |BUCLE dsorw012F;
     aAlfa = aAlfa + ")";
     #0#10  = aAlfa;  |PINTA #0#10;
|FPRC;

|PROCESO Tipo3w12;  |TIPO 3;
     |GRABA 020#1a;
     |LIBERA #1;

     |SALVA_DATOS #1;
     |HAZ MontaFormula;
     |REPON_DATOS #1;
     |LEE 040#1=;

     |PONCONTROL 23, "SI";
     |PTEC 808;
|FPRC;

|PROCESO TipoOperacion;
     |C_M #1#4, 1, "S";
     |SI #1#2 = "=";  |O #1#2 = "!";
         |C_M #1#4, 1, "N";  #1#4 = "";
     |FINSI;

     nNume = #0#8;

     |CAMPO_ANCHO  #1#3, 1, nNume;
     |CAMPO_ANCHO  #1#4, 1, nNume;

     |PINTA #1#3;
     |PINTA #1#4;
|FPRC;

|RUTINA ClaveBaja1;
     #1#0 = 1;
|FRUT;

|RUTINA ClaveAlta1;
     #1#0 = 999;
|FRUT;

|PROCESO VerLineal;  |TIPO 7;
     aFichero = ("12" + Usuario + "zzzzzzzz") % 108;
     |NOME_DAT #1 aFichero;

     |HAZ MontaFormula;
     |ENTLINEAL #1#1, -1, 7, 22, 0, ClaveBaja1, ClaveAlta1;
|FPRC;

|PROCESO Aplicacion;
     |ABRE #2;
     #2#0 = #0#2;
     #2#1 = "";
     |LEE 040#2];
     |SI FSalida = 0;  |Y #2#0 = #0#2;
         #0#3 = #2#2;
     |SINO;
         |MENAV "      No Existe Aplicacion";
         |ERROR;
         |ACABA;
     |FINSI;

     |PINTA #0#3;
|FPRC;

|PROCESO Tabla;  |TIPO 0;
     aFichero = ("05" + Usuario + "zzzzzzzz") % 108;
     |NOME_DAT #91 aFichero;
     |ABRE #91;  |CIERRA #91;  |DELINDEX #91;

     |DBASS aBase;    |QBF aBase;
     aMas = aBase + #0#2 + "/def/" + #0#4 + ".mas";  |QBT aMas;
     |CARGA_DEF aMas, referen@;
     |SI FSalida ! 0;
         |MENAV "     No existe la Tabla";
         |ERROR;
         |ACABA;
     |FINSI;

     aQueQuiero = "|TITULO";
     #0#5       = #900#aQueQuiero#;  |PINTA #0#5;

     aQueQuiero = "|NC";
     aAlfa      = #900#aQueQuiero#;
     nTCampo    = aAlfa;
     nTCampo    = nTCampo - 1;

     |ABRE #91;
     |PARA nCampo = 0;  |SI nCampo [ nTCampo;  |HACIENDO nCampo = nCampo + 1;
           #91#0 = nCampo;

           aQueQuiero = "|C" + (("000" + nCampo) % -103) + "NOMBRE";
           aFichero   = (#900#aQueQuiero# + (" " * 20)) % 120;
           #91#1      = aFichero;

           |GRABA 020#91n;
           |LIBERA #91;
     |SIGUE;
     |CIERRA #91;

     |DESCARGA_DEF #referen@;
|FPRC;

|RUTINA ClaveBaja91;
     #91#0  = 000;
|FRUT;

|RUTINA ClaveAlta91;
     #91#0  = 999;
|FRUT;

|PROCESO Intro;  |TIPO 0;
     nCampo = #91#0;
     |PTEC 807;
|FPRC;

|PROCESO LeeCampo;
     |SI FSalida = 10;
         nCampo = -1;
         |PUSHV 0501 2380;
         |ENTLINEAL #1#91, -1, 4, 22, 1, ClaveBaja91, ClaveAlta91;
         |PULSATECLA;
         |POPV;

         |SI nCampo = -1;  |ACABA;  |FINSI;

         #0#6 = nCampo;  |PINTA #0#6;
     |FINSI;

     |DBASS aBase;    |QBF aBase;
     aMas = aBase + #0#2 + "/def/" + #0#4 + ".mas";  |QBT aMas;
     |CARGA_DEF aMas, referen@;
     |SI FSalida ! 0;
         |MENAV "     No existe la Tabla";
         |ERROR;
         |ACABA;
     |FINSI;

     aQueQuiero = "|C" + (("000" + #0#6) % -103) + "NOMBRE";
     aFichero   = (#900#aQueQuiero# + (" " * 20)) % 120;
     aTitulo    = aFichero;  |QBF aTitulo;
     |SI aTitulo = "";
         |MENAV "     Campo Inexistente";
         |ERROR;
     |FINSI;

     aQueQuiero = "|C" + (("000" + #0#6) % -103) + "LONGITUD";
     aLongitud  = #900#aQueQuiero#;

     aQueQuiero = "|C" + (("000" + #0#6) % -103) + "TIPO";
     aTipo      = #900#aQueQuiero#;

     |DESCARGA_DEF #referen@;

     #0#7 = aTitulo;    |PINTA #0#7;
     #0#8 = aLongitud;  |PINTA #0#8;
     #0#9 = aTipo;      |PINTA #0#9;
|FPRC;

|PROCESO Tipo12;  |TIPO12;
     |DELINDEX #91;

     aFichero = ("12" + Usuario + "zzzzzzzz") % 108;
     |NOME_DAT #1 aFichero;

     |HAZ MontaFormula;
     |ENTLINEAL #1#1, -1, 2, 22, 0, ClaveBaja1, ClaveAlta1;
|FPRC;
