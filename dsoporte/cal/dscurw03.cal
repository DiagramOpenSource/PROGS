|FICHEROS;
     dscurm00 #0;
     dscurm02 #2;
     dscurm03 #3;
     dscurm04 #4;
     dscurm05 #5;
     dscurw03 #903;
|FIN;

|VARIABLES;
     nBoton0_1      = 0;
     nBoton0_2      = 0;
     nBoton0_3      = 0;

     nPanta0        = 0;
     nCampoD        = 0;
     nCampoH        = 0;
     nCampoHasta    = 0;
     nCampo1        = 0;
     nVentana       = 0;
     nGrid1         = 0;
     nGrid2         = 0;
     nRango         = 0;
     nPos1          = 0;
     nPos2          = 0;
     nResp          = 0;
     nCampo         = 0;

     aAlfa          = "";
     aBase          = "";
     aMas           = "";
     aFichero       = "";

     &enCurso       = 0;
     &enModo        = 1;
     &eaAplica      = "";
     &eaBloque      = "";
|FIN;

|PROCESO BajaPanta0;
     #3#0  = #903#1;
     #3#1  = #903#4;
     #3#2  = "";
|FPRC;

|PROCESO AltaPanta0;
     #3#0  = #903#1;
     #3#1  = #903#4;
     #3#2  = "zz";
|FPRC;

|| ------------------------------------------------------
|| Procesos Bajas de Ficheros;
|| ------------------------------------------------------

|PROCESO dscurm05B;
     |BORRA 020#5a;
     |LIBERA #5;
|FPRC;

|DEFBUCLE dscurm05B;
     #5#1;
     ;
     #4#0, #4#1, #4#2, #4#3, "  ";
     #4#0, #4#1, #4#2, #4#3, "zz";
     be;
     NULL, dscurm05B;
|FIN;

|PROCESO dscurm04B;
     |BUCLE dscurm05B;

     |BORRA 020#4a;
     |LIBERA #4;
|FPRC;

|DEFBUCLE dscurm04B;
     #4#1;
     ;
     #3#0, #3#1, #3#2, "  ";
     #3#0, #3#1, #3#2, "zz";
     be;
     NULL, dscurm04B;
|FIN;

|| ------------------------------------------------------
|| Procesos Grabacion de la Aplicacion
|| ------------------------------------------------------

|PROCESO GrabaAplicacion;
     #2#0 = #903#1;
     #2#1 = #903#4;
     |LEE 101#2=;
     |SI FSalida ! 0;
         |DDEFECTO #2;
         #2#0 = #903#1;
         #2#1 = #903#4;
         |GRABA 020#2b;
     |FINSI;

     #2#2 = #903#5;
     |GRABA 020#2a;
     |LIBERA #2;
|FPRC;

|| ------------------------------------------------------
|| Procesos del Boton 0_1
|| ------------------------------------------------------

|PROCESO Boton0_1;
     |HAZ GrabaAplicacion;

     |VENTANA 0501, 2799, -1, -1, "";
     nVentana = FSalida;

     enCurso   = #903#0;
     eaAplica  = #903#4;
     eaBloque  = "";
     enModo    = 1;
     |SUB_EJECUTA "dscurw04";

     |FINVENTANA nVentana;

     |REFRESCACONTROL nGrid1;
|FPRC;

|| ------------------------------------------------------
|| Procesos del Boton 0_2
|| ------------------------------------------------------

|PROCESO Boton0_2;
     |VENTANA 0501, 2799, -1, -1, "";
     nVentana = FSalida;

     enCurso   = #903#0;
     eaAplica  = #903#4;
     eaBloque  = #3#2;
     enModo    = 2;
     |SUB_EJECUTA "dscurw04";

     |FINVENTANA nVentana;

     |REFRESCACONTROL nGrid1;
|FPRC;

|| ------------------------------------------------------
|| Procesos del Boton 0_3
|| ------------------------------------------------------

|PROCESO Boton0_3;
     aAlfa = "0000NSeguro de dar de baja a " + #3#3;
     |CONFI aAlfa;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |LEE 101#3=;
     |SI FSalida = 0;
         |BUCLE dscurm04B;

         |BORRA 020#3a;
         |LIBERA #3;
     |FINSI;

     #3#0 = #903#1;
     #3#1 = #903#4;
     #3#2 = "";
     |LEE 000#3];
     |SI FSalida = 0;  |Y  #3#0 = #903#1;  |Y #3#1 = #903#4;
         |ESTADO_CONTROL nBoton0_2, "ENABLE";
         |ESTADO_CONTROL nBoton0_3, "ENABLE";
     |SINO;
         |ESTADO_CONTROL nBoton0_2, "DISABLE";
         |ESTADO_CONTROL nBoton0_3, "DISABLE";
     |FINSI;

     |REFRESCACONTROL nGrid1;
|FPRC;

|| ------------------------------------------------------
|| Procesos del Incio del Programa
|| ------------------------------------------------------

|PROCESO EventoPanta0;
     |SI FSalida = 1;  |O FSalida = 2;
         |ESTADO_CONTROL nBoton0_1, "ENABLE";

         |LEE 000#3=;
         |SI FSalida = 0;  |Y  #3#0 = #903#1;  |Y #3#1 = #903#4;
             |ESTADO_CONTROL nBoton0_2, "ENABLE";
             |ESTADO_CONTROL nBoton0_3, "ENABLE";
         |SINO;
             |ESTADO_CONTROL nBoton0_2, "DISABLE";
             |ESTADO_CONTROL nBoton0_3, "DISABLE";
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO BotonesPanta0;
     |CONTROL_SIMPLE nPanta0, "BOTON,{{193}} Alta", 1277, 1398, Boton0_1;
     nBoton0_1 = FSalida;

     |CONTROL_SIMPLE nPanta0, "BOTON,{{197}} Modificar", 1577, 1698, Boton0_2;
     nBoton0_2 = FSalida;

     |CONTROL_SIMPLE nPanta0, "BOTON,{{199}} Baja ", 1877, 1998, Boton0_3;
     nBoton0_3 = FSalida;

     |ESTADO_CONTROL nBoton0_1, "DISABLE";
     |ESTADO_CONTROL nBoton0_2, "DISABLE";
     |ESTADO_CONTROL nBoton0_3, "DISABLE";
|FPRC;

|PROCESO Aplicacion;  |TIPO 0;
     |SI enModo ! 1;  |ACABA;  |FINSI;

     #2#0 = #903#1;
     #2#1 = #903#4;
     |LEE 000#2=;
     |SI FSalida = 0;
         |MENAV "0000 La Aplicacion ya Existe.";
         |ERROR;
     |FINSI;
|FPRC;

|PROCESO Tipo12;  |TIPO 12;
|FPRC;

|PROCESO Tipo80;  |TIPO 80;
     |ABRET #3;

     FSalida = 2799;
|FPRC;

|PROCESO Tipo90;  |TIPO 90;
     |CIERRAT #3;
|FPRC;

|PROGRAMA;
     |C_M #903#4, 1, "S";

     |ABRE #0;
     #0#0 = enCurso;
     |LEE 000#0=;
     |SI FSalida ! 0;  |DDEFECTO #0; |FINSI;
     |CIERRA #0;

     #903#0 = #0#0;
     #903#1 = #0#1;
     #903#2 = #0#2;
     #903#3 = #0#3;

     |ABRE #2;
     |SI enModo ! 1;
         #2#0 = #903#1;
         #2#1 = eaAplica;
         |LEE 101#2=;
         |SI FSalida ! 0;  |DDEFECTO #2; |FINSI;

         #903#4 = #2#1;
         #903#5 = #2#2;

         |C_M #903#4, 1, "N";
     |FINSI;

     |CABEZA "Aplicaciones";

     |PINPA #0#903;  nPanta0 = FSalida;
     |PINDA #0#903;

     nRango = 4 + 8 + 16 + 32;
     nPos1  = 1102;
     nPos2  = 2660;

     |LINEAL_SIMPLE #1#3, nRango, nPos1, nPos2, BajaPanta0, AltaPanta0, EventoPanta0;
     nGrid1 = FSalida;

     |HAZ BotonesPanta0;

     |SI enModo = 1;  |ENDATOS #1#903;  |FINSI;
     |SI enModo = 2;  |ENDATOS #2#903;  |FINSI;

     |SI FSalida = 0;
         |HAZ GrabaAplicacion;
     |FINSI;
     |CIERRA #2;

     |FIN_CONTROL_WINDOWS nBoton0_1;
     |FIN_CONTROL_WINDOWS nBoton0_2;
     |FIN_CONTROL_WINDOWS nBoton0_3;

     |DESTRUYECONTROL nGrid1;

     |PULSATECLA;
|FPRO;
