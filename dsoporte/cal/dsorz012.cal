|FICHEROS;
     dsorz012 #0;
     dsorm001 #1;
     dsorm002 #2;
     dsorm011 #11,MANTE;

     dsorw007 #97,MANTE;
     dsorw008 #98,MANTE;

     empresas@ #900;
|FIN;

|VARIABLES;
     aBase       = "";
     aMas        = "";
     aQueQuiero  = "";
     aAlfa       = "";
     aAlfa1      = "";
     aAlfa2      = "";
     aTitulo     = "";
     aFichero    = "";
     aFich       = "";
     aCarga      = "";
     aDirDef     = "";
     aMensa      = "";
     aRelacion   = "";
     aDef        = "";
     aAplica     = "";
     aBaseDef    = "";
     aClave      = "";

     nTCampo      = 0;
     nModo        = 0;
     nCampo       = 0;
     nSwHay       = 0;
     nRelacion    = 0;
     nBusca       = 0;
     nDGrupo      = 0;
     nHGrupo      = 0;
     nTCompo      = 0;
     nCampoClave1 = 0;
     nCampoClave2 = 0;
     nCampoClave3 = 0;
     nLongClave1  = 0;
     nLongClave2  = 0;
     nCompo       = 0;
     nCampoD      = 0;
     nPosi        = 0;
     nPos1        = 0;
     nPos2        = 0;
     nDClave1     = 0;
     nHClave1     = 0;
     nDClave2     = 0;
     nHClave2     = 0;

     &eaAlta         = "";
     &eaConsulta     = "";
     &eaModificacion = "";
     &eaBaja         = "";
     &eaParametro    = "";
|FIN;

|PROCESO Mensa07;  |TIPO 7;
     |MENSA "0000 Pulse <F2> Lineal Aplicaciones ";
|FPRC;

|PROCESO Mensa08;  |TIPO 7;
     |MENSA "0000 <F2> Restringir Empresas.";
|FPRC;

|RUTINA ClaveBaja97;
     #97#0 = nDGrupo;
|FRUT;

|RUTINA ClaveAlta97;
     #97#0 = nHGrupo;
|FRUT;

|RUTINA ClaveBaja98;
     #98#0 = #97#0;
     #98#1 = "        ";
|FRUT;

|RUTINA ClaveAlta98;
     #98#0 = #97#0;
     #98#1 = "zzzzzzzz";
|FRUT;

|RUTINA ClaveBaja11;
    #11#0 = #98#0;
    #11#1 = #98#1;
    #11#2 = "";
|FRUT;

|RUTINA ClaveAlta11;
    #11#0 = #98#0;
    #11#1 = #98#1;
    #11#2 = "zzzzzzzzzzzzzzz";
|FRUT;

|| --------------------------------------------------------------------------
|| PROGRAMAS DE ACCESO A LOS FILTROS
|| --------------------------------------------------------------------------

|PROCESO Entram11;  |TIPO 0;
     |LEE 101#11=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |ET Campo6;
         |ENCAMPO #6#11;
         |SI FSalida = 2;  |VETE Campo6;  |FINSI;
         |SI FSalida = 7;  |VETE Acaba;   |FINSI;
         |SI FSalida = 1;  |VETE Graba;   |FINSI;

     |ET Campo7;
         |ENCAMPO #7#11;
         |SI FSalida = 2;  |VETE Campo6;  |FINSI;
         |SI FSalida = 7;  |VETE Acaba;   |FINSI;
         |SI FSalida = 1;  |VETE Graba;   |FINSI;

     |ET Campo8;
         |ENCAMPO #8#11;
         |SI FSalida = 2;  |VETE Campo7;  |FINSI;
         |SI FSalida = 7;  |VETE Acaba;   |FINSI;
         |SI FSalida = 1;  |VETE Graba;   |FINSI;

     |ET Campo9;
         |ENCAMPO #9#11;
         |SI FSalida = 2;  |VETE Campo8;  |FINSI;
         |SI FSalida = 7;  |VETE Acaba;   |FINSI;
         |SI FSalida = 1;  |VETE Graba;   |FINSI;

     |ET Graba;
         |GRABA 020#11a;

     |ET Acaba;
         |LIBERA #11;
|FPRC;

|PROCESO Saca98;  |TIPO 7;
     |ENTLINEAL #1#98, -2, 7, 22, 1, ClaveBaja98, ClaveAlta98;
|FPRC;

|PROCESO Entra98;  |TIPO 11;
     |SI FSalida ! 10;  |ACABA;  |FINSI;

     |ENTLINEAL #1#98, -2, 4, 22, 1, ClaveBaja98, ClaveAlta98;
|FPRC;

|PROCESO Saca99;  |TIPO 0;
|FPRC;

|PROCESO Entra99;  |TIPO 11;
     |SI FSalida ! 10;  |ACABA;  |FINSI;

     |PUSHV 0501 2380;
     |ENTLINEAL #1#11, -3, 4, 22, 1, ClaveBaja11, ClaveAlta11;
     |POPV;
|FPRC;

|PROCESO Graba97;
     #97#0 = #1#0;
     #97#1 = #1#1;
     |GRABA 020#97n;
     |LIBERA #97;
|FPRC;

|DEFBUCLE dsorm001;
     #1#1;
     ;
     nDGrupo;
     nHGrupo;
     ;
     NULL, Graba97;
|FIN;

|PROCESO MiraClaves;
     aAlfa         = "|K000";
     aClave        = #900#aAlfa#;
     aAlfa         = aClave % 103;
     nTCompo       = aAlfa;

     nCampoClave1  = -1;
     nCampoClave2  = -1;
     nCampoClave3  = -1;

     |PARA nCompo = 1;  |SI nCompo [ nTCompo;  |HACIENDO nCompo = nCompo + 1;
           nPosi    = (nCompo * 300) + 103;
           aAlfa1   = aClave % nPosi;  |QBF aAlfa1;
           |SI aAlfa1 = "";  |SAL;  |FINSI;

           |SI nCompo = 1;
               nCampoClave1  = aAlfa1;
               aAlfa         = "|C" + (("000" + nCampoClave1) % -103) + "LONGITUD";
               aAlfa         = ((#900#aAlfa# + (" " * 3)) % 103);
               nLongClave1   = aAlfa;
           |FINSI;

           |SI nCompo = 2;
               nCampoClave2  = aAlfa1;
               aAlfa         = "|C" + (("000" + nCampoClave2) % -103) + "LONGITUD";
               aAlfa         = ((#900#aAlfa# + (" " * 3)) % 103);
               nLongClave2   = aAlfa;
           |FINSI;
     |SIGUE;
|FPRC;

|PROCESO Graba11;
     |SI nTCompo = 1;
         nPos1   = -(100 + nLongClave1);
         aAlfa   = ((("0" * nLongClave1) + #900nCampoClave1)) % nPos1;
         nCampoD = nCampoClave1 + 1;
     |FINSI;

     |SI nTCompo = 2;
         nPos1   = -(100 + nLongClave1);
         nPos2   = -(100 + nLongClave2);
         aAlfa   = ((("0" * nLongClave1) + #900nCampoClave1)) % nPos1;
         aAlfa   = aAlfa + ((("0" * nLongClave2) + #900nCampoClave2)) % nPos2;
         nCampoD = nCampoClave2 + 1;
     |FINSI;

     #11#0 = #98#0;
     #11#1 = #98#1;
     #11#2 = aAlfa;
     |LEE 101#11=;
     |SI FSalida = 0;  |LIBERA #11;  |ACABA;  |FINSI;

     |DDEFECTO #11;
     #11#0 = #98#0;
     #11#1 = #98#1;
     #11#2 = aAlfa;
     #11#3 = #97#1;
     #11#4 = #98#2;
     #11#5 = #900nCampoD;
     |GRABA 020#11n;
     |LIBERA #11;
|FPRC;

|DEFBUCLE Clave1;
     #900#1001;
     ;
     nDClave1;
     nHClave1;
     ;
     NULL, Graba11;
|FIN;

|DEFBUCLE Clave2;
     #900#1002;
     ;
     nDClave1, nDClave2;
     nHClave1, nHClave2;
     ;
     NULL, Graba11;
|FIN;

|PROCESO Graba98;
     #98#0 = #97#0;
     #98#1 = #2#0;
     |LEE 101#98=;
     |SI FSalida ! 0;
         |DDEFECTO #98;
         #98#0 = #97#0;
         #98#1 = #2#0;
         #98#2 = #2#2;
         |GRABA 020#98n;
     |FINSI;
     |LIBERA #98;

     |ABRE #2;
     #2#0 = #98#1;
     |LEE 040#2=;
     |SI FSalida ! 0;  |CIERRA #2;  |ACABA;  |FINSI;
     |CIERRA #2;

     |DBASS aBase;  |QBF aBase;
     aMas = aBase + #98#1 + "/def/" + #2#3 + ".mas";  |QBT aMas;
     |CARGA_DEF aMas, empresas@;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     aFich = aBase + #98#1 + "/fich/";  |QBT aFich;
     |PATH_DAT #900 aFich;

     |HAZ MiraClaves;

     |SI nTCompo = 1;
         aAlfa = "0" * nLongClave1;  nDClave1 = aAlfa;
         aAlfa = "9" * nLongClave1;  nHClave1 = aAlfa;
         |BUCLE Clave1;
     |FINSI;

     |SI nTCompo = 2;
         aAlfa = "0" * nLongClave1;  nDClave1 = aAlfa;
         aAlfa = "9" * nLongClave1;  nHClave1 = aAlfa;
         aAlfa = "0" * nLongClave2;  nDClave2 = aAlfa;
         aAlfa = "9" * nLongClave2;  nHClave2 = aAlfa;
         |BUCLE Clave2;
     |FINSI;

     |DESCARGA_DEF #empresas@;
|FPRC;

|DEFBUCLE dsorw007;
     #97#1;
     ;
     nDGrupo;
     nHGrupo;
     ;
     NULL, Graba98;
|FIN;

|PROCESO Mira97;
     |SI #2#3 = "        ";  |ACABA;  |FINSI;

     |BUCLE dsorw007;
|FPRC;

|DEFBUCLE dsorm002;
     #2#1;
     ;
     "        ", "        ";
     "zzzzzzzz", "zzzzzzzz";
     ;
     NULL, Mira97;
|FIN;

|PROCESO dsorm011B;
     |BORRA 020#11a;
     |LIBERA #11;
|FPRC;

|DEFBUCLE dsorm011B;
     #11#1;
     6, 7, 8, 9;
     0000, "        ", "               ", " ", " ", " ", " ";
     9999, "zzzzzzzz", "zzzzzzzzzzzzzzz", " ", " ", " ", " ";
     be;
     NULL, dsorm011B;
|FIN;

|PROGRAMA;
     eaParametro = PARAMETRO;  |QBF eaParametro;

     |CLS;
     |CABEZA "Asistente Filtros por Empresas";

     aFichero = "dsorw007" + Usuario;
     |NOME_DAT #97 aFichero;
     |ABRE #97;  |CIERRA #97;  |DELINDEX #97;

     aFichero = "dsorw008" + Usuario;
     |NOME_DAT #98 aFichero;
     |ABRE #98;  |CIERRA #98;  |DELINDEX #98;

     |MENSA "0000 Recogiendo Empresas, esto puede tardar varios minutos.";
     |PINPA #0#97;
     |PINPA #0#98;

     |ABRE #97;
     |SI eaParametro = "";
         nDGrupo = 1;
         nHGrupo = 9999;
         |BUCLE dsorm001;
     |SINO;
         nDGrupo = 0;
         nHGrupo = 0;
         #97#0  = eaParametro;
         #97#1  = "FILTRO MASTER";
         |GRABA 020#97n;
         |LIBERA #97;
     |FINSI;
     |CIERRA #97;

     |ABRE #98;
     |ABRE #11;
     |BUCLE dsorm002;
     |CIERRA #98;
     |CIERRA #11;

     |ENTLINEAL #1#97, -1, 4, 22, 1, ClaveBaja97, ClaveAlta97;

     |DELINDEX #97;
     |DELINDEX #98;

     |BUCLE dsorm011B;
|FPRO;
