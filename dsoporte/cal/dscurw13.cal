|FICHEROS;
     dscurm00 #0;
     dscurm01 #1;
     dscurm14 #14;
     dscurw13 #913;
|FIN;

|VARIABLES;
     nBoton0_1      = 0;
     nBoton0_2      = 0;
     nBoton0_3      = 0;

     nPanta0        = 0;
     nCampoD        = 0;
     nCampoH        = 0;
     nCampoHasta    = 0;
     nCampo1        = 0;
     nVentana       = 0;
     nGrid1         = 0;
     nGrid2         = 0;
     nRango         = 0;
     nPos1          = 0;
     nPos2          = 0;
     nResp          = 0;
     nCampo         = 0;

     aAlfa          = "";
     aBase          = "";
     aMas           = "";
     aFichero       = "";
     aEsc1          = "";
     aEsc2          = "";
     aRetu          = "";
     aTecla         = "";

     &enCursoBase   = 0;
     &enCurso       = 0;
     &eaUsuario     = "";
     &enModo        = 1;
|FIN;

|PROCESO BajaPanta0;
     #1#0  = #913#0;
     #1#1  = 01;
|FPRC;

|PROCESO AltaPanta0;
     #1#0  = #913#0;
     #1#1  = 99;
|FPRC;

|| ------------------------------------------------------
|| Procesos Bajas de Ficheros;
|| ------------------------------------------------------

|PROCESO dscurm14B;
     enCursoBase = #14#0;
     enCurso     = #14#1;
     eaUsuario   = #14#2;
     enModo      = 3;
     |SUB_EJECUTA_NP "dscurw15";
|FPRC;

|DEFBUCLE dscurm14B;
     #14#1;
     ;
     #1#0, #1#1, "          ";
     #1#0, #1#1, "zzzzzzzzzz";
     ;
     NULL, dscurm14B;
|FIN;

|| ------------------------------------------------------
|| Procesos del Boton 0_1
|| ------------------------------------------------------

|PROCESO Boton0_1;
     |VENTANA 0501, 2799, -1, -1, "";
     nVentana = FSalida;

     enCursoBase = #913#0;
     enCurso     = 0;
     enModo    = 1;
     |SUB_EJECUTA "dscurw14";

     |FINVENTANA nVentana;

     |REFRESCACONTROL nGrid1;
|FPRC;

|| ------------------------------------------------------
|| Procesos del Boton 0_2
|| ------------------------------------------------------

|PROCESO Boton0_2;
     |VENTANA 0501, 2799, -1, -1, "";
     nVentana = FSalida;

     enCursoBase = #913#0;
     enCurso     = #1#1;
     enModo      = 2;
     |SUB_EJECUTA "dscurw14";

     |FINVENTANA nVentana;

     |REFRESCACONTROL nGrid1;
|FPRC;

|| ------------------------------------------------------
|| Procesos del Boton 0_3
|| ------------------------------------------------------

|PROCESO Boton0_3;
     aAlfa = "0000NSeguro de dar de baja a " + #1#5;
     |CONFI aAlfa;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |LEE 101#1=;
     |SI FSalida = 0;
         |BUCLE dscurm14B;

         |BORRA 020#1a;
         |LIBERA #1;
     |FINSI;

     #1#0 = #913#0;
     #1#1 = "";
     |LEE 000#1];
     |SI FSalida = 0;  |Y  #1#0 = #913#0;
         |ESTADO_CONTROL nBoton0_2, "ENABLE";
         |ESTADO_CONTROL nBoton0_3, "ENABLE";
     |SINO;
         |ESTADO_CONTROL nBoton0_2, "DISABLE";
         |ESTADO_CONTROL nBoton0_3, "DISABLE";
     |FINSI;

     |REFRESCACONTROL nGrid1;
|FPRC;

|| ------------------------------------------------------
|| Procesos del Incio del Programa
|| ------------------------------------------------------

|PROCESO EventoPanta0;
     |SI FSalida = 1;  |O FSalida = 2;
         |ESTADO_CONTROL nBoton0_1, "ENABLE";

         |LEE 000#1=;
         |SI FSalida = 0;  |Y  #1#0 = #913#0;
             |ESTADO_CONTROL nBoton0_2, "ENABLE";
             |ESTADO_CONTROL nBoton0_3, "ENABLE";
         |SINO;
             |ESTADO_CONTROL nBoton0_2, "DISABLE";
             |ESTADO_CONTROL nBoton0_3, "DISABLE";
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO BotonesPanta0;
     |CONTROL_SIMPLE nPanta0, "BOTON,{{193}} Alta", 1277, 1398, Boton0_1;
     nBoton0_1 = FSalida;

     |CONTROL_SIMPLE nPanta0, "BOTON,{{197}} Modificar", 1577, 1698, Boton0_2;
     nBoton0_2 = FSalida;

     |CONTROL_SIMPLE nPanta0, "BOTON,{{199}} Baja ", 1877, 1998, Boton0_3;
     nBoton0_3 = FSalida;

     |ESTADO_CONTROL nBoton0_1, "ENABLE";
     |ESTADO_CONTROL nBoton0_2, "DISABLE";
     |ESTADO_CONTROL nBoton0_3, "DISABLE";
|FPRC;

|PROCESO Tipo12;  |TIPO 12;
|FPRC;

|PROCESO Tipo80;  |TIPO 80;
     |ABRET #1;

     FSalida = 2799;
|FPRC;

|PROCESO Tipo90;  |TIPO 90;
     |CIERRAT #1;
|FPRC;

|PROGRAMA;
     |ABRE #0;
     #0#0 = enCursoBase;
     |LEE 000#0=;
     |SI FSalida ! 0;  |DDEFECTO #0; |FINSI;
     |CIERRA #0;

     #913#0 = #0#0;
     #913#1 = #0#1;
     #913#2 = #0#2;
     #913#3 = #0#3;

     |CABEZA "Cursos";

     |PINPA #0#913;  nPanta0 = FSalida;
     |PINDA #0#913;

     nRango = 4 + 8 + 16 + 32;
     nPos1  = 1002;
     nPos2  = 2673;

     |LINEAL_SIMPLE #1#1, nRango, nPos1, nPos2, BajaPanta0, AltaPanta0, EventoPanta0;
     nGrid1 = FSalida;

     |HAZ BotonesPanta0;

     aEsc1  = &255 + "806";
     aEsc2  = &255 + "807";
     aRetu  = &255 + "802";
     |PARA; |SI;  |HACIENDO;
         FSalida = "::Salir";
         |LEETECLA aTecla;
         |SI aTecla = aEsc1; |SAL;  |FINSI;
         |SI aTecla = aEsc2; |SAL;  |FINSI;
         |SI aTecla = aRetu; |SAL;  |FINSI;
     |SIGUE;

     |FIN_CONTROL_WINDOWS nBoton0_1;
     |FIN_CONTROL_WINDOWS nBoton0_2;
     |FIN_CONTROL_WINDOWS nBoton0_3;

     |DESTRUYECONTROL nGrid1;

     |PULSATECLA;
|FPRO;
