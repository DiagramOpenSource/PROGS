|FICHEROS;
     dscurm00 #0;
     dscurm02 #2;
     dscurm03 #3;
     dscurm04 #4;
     dscurm05 #5;
     dsorm002 #102;

     dscurw01 #901,MANTE;
     dscurw05 #905;
|FIN;

|VARIABLES;
     nBoton0_1      = 0;
     nBoton0_2      = 0;
     nBoton0_3      = 0;
     nBoton0_4      = 0;

     nPanta0        = 0;
     nCampoD        = 0;
     nCampoH        = 0;
     nCampoHasta    = 0;
     nCampo1        = 0;
     nGrid1         = 0;
     nGrid2         = 0;
     nRango         = 0;
     nPos1          = 0;
     nPos2          = 0;
     nResp          = 0;
     nCampo         = 0;
     nComas         = 0;
     nError         = 0;
     nLong          = 0;
     nSw            = 0;
     nCaracter      = 0;
     nPos           = 0;
     nContador      = 0;
     nHandle        = 0;
     nVentana       = 0;
     nNume          = 0;

     aAlfa          = "";
     aAlfa1         = "";
     aBase          = "";
     aMas           = "";
     aFichero       = "";
     aCaracter      = "";
     aDescri        = "";
     aPrograma      = "";
     aLong          = "";
     aNumero        = "";
     aMenu          = "";

     &enNumero      = 0;
     &enCurso       = 0;
     &enModo        = 1;
     &eaAplica      = "";
     &eaBloque      = "";
     &eaLeccion     = "";
     &eaApartad     = "";
|FIN;

|PROCESO BajaPanta0;
     #5#0  = #905#1;
     #5#1  = #905#4;
     #5#2  = #905#6;
     #5#3  = #905#8;
     #5#4  = "";
|FPRC;

|PROCESO AltaPanta0;
     #5#0  = #905#1;
     #5#1  = #905#4;
     #5#2  = #905#6;
     #5#3  = #905#8;
     #5#4  = "zz";
|FPRC;

|| ------------------------------------------------------
|| Procesos Asignacion del Menu
|| ------------------------------------------------------

|PROCESO CuentaComas;
     |SI aCaracter = "\";  |ACABA;  |FINSI;

     |SI aCaracter = ",";
         nComas = nComas + 1;
     |FINSI;
|FPRC;

|PROCESO Descripcion;
     |SI aCaracter = ":";  |ACABA;  |FINSI;

     aDescri = aDescri + aCaracter;
|FPRC;

|PROCESO Programa;
     aPrograma = aPrograma + aCaracter;
|FPRC;

|PROCESO GrabaMenu;
     nComas     = 0;
     aDescri    = "";
     aPrograma  = "";

     |QBF aAlfa1;
     |SI aAlfa1 = "";  |ACABA;  |FINSI;

     aAlfa = aAlfa1 % 101;
     |SI aAlfa = "#";  |ACABA;  |FINSI;
     |SI aAlfa = ";";  |ACABA;  |FINSI;
     |SI aAlfa ! "\";  nError = 1;  |ACABA;  |FINSI;

     aLong   = aAlfa1 % 0;
     aNumero = "";
     nLong   = aLong;
     nSw     = 1;
     |PARA nCaracter = 1;  |SI nCaracter [ nLong;  |HACIENDO nCaracter = nCaracter + 1;
           nPos = (nCaracter * 100) + 1;
           aCaracter = aAlfa1 % nPos;

           |SI aCaracter = "\";
               |SI nSw = 1;
                   nSw = 0;
               |SINO;
                   nSw = 1;
               |FINSI;
           |SINO;
               |SI nSw = 0;
                   aNumero = aNumero + aCaracter;
               |FINSI;
           |FINSI;

           |SI aCaracter = ":"; |Y nSw = 1;    nSw = 2;   |FINSI;
           |SI aCaracter = "{"; |Y nSw = 2;    nSw = 3;   |FINSI;

           |SI nSw = 1;  |HAZ CuentaComas;  |FINSI;
           |SI nSw = 2;  |HAZ Descripcion;  |FINSI;
           |SI nSw = 3;  |HAZ Programa;     |FINSI;
     |SIGUE;

     nContador = nContador + 1;
     aDescri   = (("     ") * nComas) + aDescri;

     #901#0  = nContador;
     #901#1  = aDescri;
     #901#2  = aPrograma;
     #901#3  = aNumero;

     |GRABA 020#901n;
     |LIBERA #901;
|FPRC;

|PROCESO Baja901;
     |SI FSalida = "POSICION";
         #901#0 = nNume;
         |ACABA;
     |FINSI;
     #901#0 = 1;
|FPRC;

|PROCESO Alta901;
     #901#0 = 99999;
|FPRC;

|PROCESO AsignaMenu;
     enNumero = #905#12;

     |ABRE #102;
     #102#0 = #905#10;
     #102#1 = #905#11;
     |LEE 000#102];

     |CONSULTA_CLAVE #1#102;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |LEE 000#102=;
     |SI FSalida ! 0;
         |CIERRA #102;
         |MENAV "0000 Aplicacion - Menu Inexistente";
         |ACABA;
     |FINSI;
     |CIERRA #102;

     aFichero = ("01" + Usuario + "zzzzzzzz") % 108;
     |NOME_DAT #901 aFichero;
     |ABRE #901;  |CIERRA #901;  |DELINDEX #901;

     |ABRE #901;

     |DBASS aBase;  |QBF aBase;
     aMenu = aBase + #102#0 + "/" + #102#1 + ".men";  |QBT aMenu;

     |XABRE "U", aMenu, nHandle;
     |SI FSalida < 0;  |ACABA;  |FINSI;

     nError    = 0;
     nContador = 0;
     |PARA;  |SI;  |HACIENDO;
         |XLEE nHandle, 250, aAlfa1;
         |SI FSalida < 0;  |SAL;  |FINSI;
         |HAZ GrabaMenu;
         |SI nError = 1;  |SAL;  |FINSI;
     |SIGUE;
     |XCIERRA nHandle;

     |SI nError = 1;
         |MENAV "0000 Menu sin Numerar. Debe estar Numerado. No puedo asignar nada de este menu.";
         |DELINDEX #901;
         |ACABA;
     |FINSI;

     |VENTANA 0501, 2799, -1, -1, "";
     nVentana = FSalida;

     |ABRE #901;
     |SELECT #2#901;
     #901#3 = enNumero;
     |LEE 000#901=;
     |SI FSalida ! 0;  |DDEFECTO #901;  |FINSI;
     |CIERRA #901;
     |SELECT #1#901;

     nNume    = #901#0;
     enNumero = 0;
     |ENTLINEAL #1#901, -1, 4, 26, 1, Baja901, Alta901;

     |FINVENTANA nVentana;

     |SI enNumero = 0;  |DELINDEX #901;  |ACABA;  |FINSI;

     |ABRE #901;
     #901#0 = enNumero;
     |LEE 000#901=;
     |SI FSalida ! 0;  |DDEFECTO #901;  |FINSI;
     |CIERRA #901;

     aAlfa = #901#2;  |QBF aAlfa;
     |SI aAlfa = "";
         |MENAV "0000 El Punto del Menu seleccionado no tiene programa invocado.";
         #905#10 = "";
         #905#11 = "";
         #905#12 = 0;
     |SINO;
         #905#10 = #102#0;
         #905#11 = #102#1;
         #905#12 = #901#3;
     |FINSI;

     |PINTA #905#10;
     |PINTA #905#11;
     |PINTA #905#12;

     |DELINDEX #901;
|FPRC;

|| ------------------------------------------------------
|| Procesos Grabacion de la Leccion
|| ------------------------------------------------------

|PROCESO GrabaLeccion;
     #4#0 = #905#1;
     #4#1 = #905#4;
     #4#2 = #905#6;
     #4#3 = #905#8;
     |LEE 101#4=;
     |SI FSalida ! 0;
         |DDEFECTO #4;
         #4#0 = #905#1;
         #4#1 = #905#4;
         #4#2 = #905#6;
         #4#3 = #905#8;
         |GRABA 020#4b;
     |FINSI;

     #4#4 = #905#9;
     #4#5 = #905#10;
     #4#6 = #905#11;
     #4#7 = #905#12;
     |GRABA 020#4a;
     |LIBERA #4;
|FPRC;

|| ------------------------------------------------------
|| Procesos del Boton 0_1
|| ------------------------------------------------------

|PROCESO Boton0_1;
     |HAZ GrabaLeccion;

     |VENTANA 0501, 2799, -1, -1, "";
     nVentana = FSalida;

     enCurso   = #905#0;
     eaAplica  = #905#4;
     eaBloque  = #905#6;
     eaLeccion = #905#8;
     enModo    = 1;
     |SUB_EJECUTA "dscurw06";

     |FINVENTANA nVentana;

     |REFRESCACONTROL nGrid1;
|FPRC;

|| ------------------------------------------------------
|| Procesos del Boton 0_2
|| ------------------------------------------------------

|PROCESO Boton0_2;
     |VENTANA 0501, 2799, -1, -1, "";
     nVentana = FSalida;

     enCurso   = #905#0;
     eaAplica  = #905#4;
     eaBloque  = #905#6;
     eaLeccion = #905#8;
     eaApartad = #5#4;
     enModo    = 2;
     |SUB_EJECUTA "dscurw06";

     |FINVENTANA nVentana;

     |REFRESCACONTROL nGrid1;
|FPRC;

|| ------------------------------------------------------
|| Procesos del Boton 0_3
|| ------------------------------------------------------

|PROCESO Boton0_3;
     aAlfa = "0000NSeguro de dar de baja a " + #5#5;
     |CONFI aAlfa;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |LEE 101#5=;
     |SI FSalida = 0;
         |BORRA 020#5a;
         |LIBERA #5;
     |FINSI;

     #5#0 = #905#1;
     #5#1 = #905#4;
     #5#2 = #905#6;
     #5#3 = #905#8;
     #5#4 = "";
     |LEE 000#5];
     |SI FSalida = 0;    |Y  #5#0 = #905#1;  |Y #5#1 = #905#4;
      |Y #5#2 = #905#6;  |Y  #5#3 = #905#8;
         |ESTADO_CONTROL nBoton0_2, "ENABLE";
         |ESTADO_CONTROL nBoton0_3, "ENABLE";
     |SINO;
         |ESTADO_CONTROL nBoton0_2, "DISABLE";
         |ESTADO_CONTROL nBoton0_3, "DISABLE";
     |FINSI;

     |REFRESCACONTROL nGrid1;
|FPRC;

|| ------------------------------------------------------
|| Procesos del Boton 0_4
|| ------------------------------------------------------

|PROCESO Boton0_4;
     |HAZ AsignaMenu;
|FPRC;

|| ------------------------------------------------------
|| Procesos del Incio del Programa
|| ------------------------------------------------------

|PROCESO EventoPanta0;
     |SI FSalida = 1;  |O FSalida = 2;
         |ESTADO_CONTROL nBoton0_1, "ENABLE";

         |LEE 000#5=;
         |SI FSalida = 0;    |Y  #5#0 = #905#1;  |Y #5#1 = #905#4;
          |Y #5#2 = #905#6;  |Y  #5#3 = #905#8;
             |ESTADO_CONTROL nBoton0_2, "ENABLE";
             |ESTADO_CONTROL nBoton0_3, "ENABLE";
         |SINO;
             |ESTADO_CONTROL nBoton0_2, "DISABLE";
             |ESTADO_CONTROL nBoton0_3, "DISABLE";
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO BotonesPanta0;
     |CONTROL_SIMPLE nPanta0, "BOTON,{{193}} Alta", 1677, 1798, Boton0_1;
     nBoton0_1 = FSalida;

     |CONTROL_SIMPLE nPanta0, "BOTON,{{197}} Modificar", 1977, 2098, Boton0_2;
     nBoton0_2 = FSalida;

     |CONTROL_SIMPLE nPanta0, "BOTON,{{199}} Baja ", 2277, 2398, Boton0_3;
     nBoton0_3 = FSalida;

     |CONTROL_SIMPLE nPanta0, "BOTON,{{219}} Buscar Punto Menu", 1275, 1298, Boton0_4;
     nBoton0_4 = FSalida;

     |ESTADO_CONTROL nBoton0_1, "DISABLE";
     |ESTADO_CONTROL nBoton0_2, "DISABLE";
     |ESTADO_CONTROL nBoton0_3, "DISABLE";
     |ESTADO_CONTROL nBoton0_4, "ENABLE";
|FPRC;

|PROCESO Leccion;  |TIPO 0;
     |SI enModo ! 1;  |ACABA;  |FINSI;

     #4#0 = #905#1;
     #4#1 = #905#4;
     #4#2 = #905#6;
     #4#3 = #905#8;
     |LEE 000#4=;
     |SI FSalida = 0;
         |MENAV "0000 La Leccion ya existe.";
         |ERROR;
     |FINSI;
|FPRC;

|PROCESO Tipo12;  |TIPO 12;
|FPRC;

|PROCESO Tipo80;  |TIPO 80;
     |ABRET #5;

     FSalida = 2799;
|FPRC;

|PROCESO Tipo90;  |TIPO 90;
     |CIERRAT #5;
|FPRC;

|PROGRAMA;
     |C_M #905#8, 1, "S";

     |ABRE #0;
     #0#0 = enCurso;
     |LEE 000#0=;
     |SI FSalida ! 0;  |DDEFECTO #0; |FINSI;
     |CIERRA #0;

     |ABRE #2;
     #2#0 = #0#1;
     #2#1 = eaAplica;
     |LEE 000#2=;
     |SI FSalida ! 0;  |DDEFECTO #2; |FINSI;
     |CIERRA #2;

     |ABRE #3;
     #3#0 = #0#1;
     #3#1 = #2#1;
     #3#2 = eaBloque;
     |LEE 000#3=;
     |SI FSalida ! 0;  |DDEFECTO #3; |FINSI;
     |CIERRA #3;

     #905#0 = #0#0;
     #905#1 = #0#1;
     #905#2 = #0#2;
     #905#3 = #0#3;
     #905#4 = #2#1;
     #905#5 = #2#2;
     #905#6 = #3#2;
     #905#7 = #3#3;

     |ABRE #4;
     |SI enModo ! 1;
         #4#0 = #905#1;
         #4#1 = #905#4;
         #4#2 = #905#6;
         #4#3 = eaLeccion;
         |LEE 101#4=;
         |SI FSalida ! 0;  |DDEFECTO #4; |FINSI;

         #905#8  = #4#3;
         #905#9  = #4#4;
         #905#10 = #4#5;
         #905#11 = #4#6;
         #905#12 = #4#7;

         |C_M #905#8, 1, "N";
     |FINSI;

     |CABEZA "Lecciones";

     |PINPA #0#905;  nPanta0 = FSalida;
     |PINDA #0#905;

     nRango = 4 + 8 + 16 + 32;
     nPos1  = 1502;
     nPos2  = 2670;

     |LINEAL_SIMPLE #1#5, nRango, nPos1, nPos2, BajaPanta0, AltaPanta0, EventoPanta0;
     nGrid1 = FSalida;

     |HAZ BotonesPanta0;

     |SI enModo = 1;  |ENDATOS #1#905;  |FINSI;
     |SI enModo = 2;  |ENDATOS #2#905;  |FINSI;

     |SI FSalida = 0;
         |HAZ GrabaLeccion;
     |FINSI;
     |CIERRA #4;

     |FIN_CONTROL_WINDOWS nBoton0_1;
     |FIN_CONTROL_WINDOWS nBoton0_2;
     |FIN_CONTROL_WINDOWS nBoton0_3;
     |FIN_CONTROL_WINDOWS nBoton0_4;

     |DESTRUYECONTROL nGrid1;

     |PULSATECLA;
|FPRO;
