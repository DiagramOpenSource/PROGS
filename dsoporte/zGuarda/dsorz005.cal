|FICHEROS;
     dsorz005 #0;
     dsorm001 #1;
     dsorm003 #3;
     dsorm004 #4;
     dsorm005 #5;
     dsorm006 #6;
     dsorm007 #7;
     dsorm008 #8;
     dsorm009 #9;
     dsorm010 #10;
     dsorm011 #11;

     dsorw003 #90;
     dsorw004 #91;
     dsorw008 #98;

     referen@  #900;
     preemes@  #901;
     gecom001@ #902;
     gecom004@ #904;
     gecom011@ #911;
|FIN;

|VARIABLES;
     aBase          = "";
     aBaseAqui      = "";
     aBaseChek      = "";
     aFich          = "";
     aFic           = "";
     aDirGrupo      = "";
     aDirFichero    = "";
     aDClave        = "";
     aHClave        = "";
     aOrigen        = "";
     aDestino       = "";
     aAplicacion    = "";
     aMenu          = "";
     aAlfa          = "";
     aAlfa1         = "";
     aAlfa2         = "";
     aAlfa3         = "";
     aFichero       = "";
     aLong          = "";
     aAbre          = "";
     aPrograma      = "";
     aCaracter      = "";
     aTabla         = "";
     aDCaracter     = "";
     aHCaracter     = "";
     aCondicion     = "";
     aCampo         = "";
     aClaveMenu     = "";
     aLongitud      = "";
     aMas           = "";
     aQueQuiero     = "";
     aTipo          = "";
     aDAplica       = "";
     aHAplica       = "";
     aDTabla        = "";
     aHTabla        = "";
     aParametro     = "";
     aCodDepar      = "";
     aCodSubDepar   = "";
     aCodSerie      = "";
     aCodAlmacen    = "";
     aCodZona       = "";
     aCodFactura    = "";
     aNumero        = "";
     aPRM           = "";
     aPunto         = "";

     nHandle        = 0;
     nContador90    = 0;
     nContador91    = 0;
     nLong          = 0;
     nPos           = 0;
     nCaracter      = 0;
     nPon           = 0;
     nLinea         = 0;
     nLineaPrime    = 0;
     nNumDef        = 0;
     nContaDef      = 0;
     nLongitud      = "";
     nMenu          = 0;
     nGeneracion    = 0;
     nCampo         = 0;
     nCodMenu       = 0;
     nCodResto      = 0;
     nCodRegion     = 0;
     nCodDele       = 0;
     nCodAgencia    = 0;
     nPasada        = 0;
     nEmpresas      = 0;
     nAuditor       = 0;
     nGrupo         = 0;
     nCod6          = 0;
     nDGrupo        = 0;
     nHGrupo        = 0;

     &eaAplica      = "";
     &eaMenu        = "";
     &enGrupo       = 0;
|FIN;

/*
|PROCESO LimpiaDirectorio;
     |DBASS aBase;   |QBF aBase;
     |DFICE aFich;   |QBF aFich;
     aDirGrupo = aFich + "grupos";                              |MKDIR aDirGrupo;
     aDirGrupo = aDirGrupo + "/" + (("0000" + #0#0) % -104);    |MKDIR aDirGrupo;
     aDirGrupo = aDirGrupo + "/";

     aDirFichero = aDirGrupo + "*";
     |_PDIR aDirFichero;
     |PARA;  |SI FSalida ] 0; |HACIENDO;
             |FBORRA aDirFichero;
             |_SDIR aDirFichero;
     |SIGUE;
|FPRC;
*/

|PROCESO CreaMenu;
     aAlfa1 = #90#1;
     |XGRABA nHandle, aAlfa1;
|FPRC;

|DEFBUCLE dsorw003;
     #90#1;
     ;
     000001;
     999999;
     ;
     NULL, CreaMenu;
|FIN;

|PROCESO CreaPRM;
     aAlfa1 = #91#2;
     |XGRABA nHandle, aAlfa1;
|FPRC;

|DEFBUCLE dsorw004A;
     #91#1;
     ;
     "        ", 000001;
     "zzzzzzzz", 999999;
     ;
     NULL, CreaPRM;
|FIN;

|| __________________________________________________________________________
||                        PROCESOS MENUS
|| __________________________________________________________________________

|PROCESO CogeHastaDosPuntos;
     aLong     = aAlfa1 % 0;
     nLong     = aLong;
     aPrograma = "";
     aNumero   = "";
     nPon      = 0;
     |PARA nCaracter = 1;  |SI nCaracter [ nLong;  |HACIENDO nCaracter = nCaracter + 1;
           nPos      = (nCaracter * 100) + 1;
           aCaracter = aAlfa1 % nPos;
           |SI aCaracter = "\";  |Y nPon = 1;
               |SAL;
           |FINSI;

           |SI aCaracter = "\";  |Y nPon = 0;
               nPon = 1;
           |FINSI;

           |SI aCaracter ! "\";  |Y nPon = 1;
               aNumero = aNumero + aCaracter;
           |FINSI;
     |SIGUE;

     |SI aNumero ! "";  |ACABA;  |FINSI;

     |PARA nCaracter = 1;  |SI nCaracter [ nLong;  |HACIENDO nCaracter = nCaracter + 1;
           nPos      = (nCaracter * 100) + 1;
           aCaracter = aAlfa1 % nPos;
           nPon      = 0;
           |SI aCaracter = ":";
               aClaveMenu = aClaveMenu + aCaracter;
               |SAL;
           |SINO;
               aClaveMenu = aClaveMenu + aCaracter;
           |FINSI;
     |SIGUE;
|FPRC;

|PROCESO CogePrograma;
     aLong     = aAlfa3 % 0;
     nLong     = aLong;
     aPrograma = "";
     |PARA nCaracter = 1;  |SI nCaracter [ nLong;  |HACIENDO nCaracter = nCaracter + 1;
           nPos = (nCaracter * 100) + 1;
           aCaracter = aAlfa3 % nPos;
           nPon = 0;
           |SI aCaracter = "{";  aPrograma = "";  nPon = 1; |FINSI;
           |SI aCaracter = "/";  aPrograma = "";  nPon = 1; |FINSI;
           |SI aCaracter = ":";  aPrograma = "";  nPon = 1; |FINSI;
           |SI aCaracter = " ";  |SAL;            |FINSI;
           |SI aCaracter = "}";  |SAL;            |FINSI;
           |SI aCaracter = ".";  |SAL;            |FINSI;

           |SI nPon = 0;
               aPrograma = aPrograma + aCaracter;
           |FINSI;
     |SIGUE;

     |SI aPrograma = "";  |ACABA;  |FINSI;

     aLong = aPrograma % 0;
     nLong = aLong;
     |SI nLong > 8;  |ACABA;  |FINSI;
     |SI nLong < 5;  |ACABA;  |FINSI;

     nContador91 = nContador91 + 1;
     #91#0       = "";
     #91#1       = nContador91;
     #91#2       = aPrograma;
     |GRABA 020#91n;
     |LIBERA #91;
|FPRC;

|PROCESO IndexaMenu;
     |SI #3#4 = "S";
         aAbre = aDirGrupo + "ds.txt";
         |XABRE "A", aAbre, nHandle;
         |SI FSalida ] 0;
             aAlfa = #3#2;
             |XGRABA nHandle, aAlfa;
         |FINSI;
         |XCIERRA nHandle;
         |ACABA;
     |FINSI;

     |ABRE #90;  |CIERRA #90;  |DELINDEX #90;

     nContador90 = 0;
     |SI #3#1 ! "menugnrl";
         aOrigen     = aBase     + #3#1 + "/" + #3#2 + ".men";  |QBT aOrigen;
     |SINO;
         aOrigen     = aBase     + #3#2 + ".men";  |QBT aOrigen;
     |FINSI;
     aDestino    = aDirGrupo + #3#2 + ".men";               |QBT aDestino;

     |ABRE #90;
     |SELECT #1#90;
     |XABRE "U", aOrigen, nHandle;
     |SI FSalida ] 0;
         |PARA; |SI; |HACIENDO;
              |XLEE nHandle, 250, aAlfa1;
              |SI FSalida < 0;  |SAL;  |FINSI;

              nContador90 = nContador90 + 1;
              #90#0       = nContador90;
              #90#1       = aAlfa1;

              aClaveMenu  = "";
              aNumero     = "";
              aAlfa1 = #90#1;  |QBF aAlfa1;
              |HAZ CogeHastaDosPuntos;
              |SI aNumero ! "";
                  #90#2 = aNumero;
              |SINO;
                  #90#2 = aClaveMenu;
              |FINSI;

              |GRABA 020#90n;
              |LIBERA #90;
         |SIGUE;
     |FINSI;
     |XCIERRA nHandle;
|FPRC;

|PROCESO PonAcceso;
     aPrograma = aAlfa3 - "{{";
     |SI FSalida = 0;  aPrograma = "";  |ACABA;  |FINSI;

     aLong     = aAlfa3 % 0;
     nLong     = aLong;
     aPrograma = "";
     nPon      = 0;
     |PARA nCaracter = 1;  |SI nCaracter [ nLong;  |HACIENDO nCaracter = nCaracter + 1;
           nPos          = (nCaracter * 100) + 2;
           aCaracter     = aAlfa3 % nPos;
           |SI aCaracter = "{{";  nPon = 1; |FINSI;

           |SI nPon = 0;
               nPos      = (nCaracter * 100) + 1;
               aCaracter = aAlfa3 % nPos;
               aPrograma = aPrograma + aCaracter;
           |FINSI;
     |SIGUE;

     |SI aPrograma = "";  |ACABA;  |FINSI;

     #90#1 = aPrograma + " {{:dsoporte/dsorz015}}";
|FPRC;

|PROCESO GeneraMenu;
     |SI aAplicacion ! #3#1;  |O aMenu ! #3#2;
         |SI aDestino ! "";
             |XABRE "W", aDestino, nHandle;
             |BUCLE dsorw003;
             |XCIERRA nHandle;

             |SI aAplicacion ! "menugnrl";  |Y aPRM = "S";
                 aAbre   = aDirGrupo + aAplicacion + ".prm";   |QBT aAbre;
                 |XABRE "W", aAbre, nHandle;
                 |SI FSalida ] 0;
                     aAlfa = "-" + (("0000" + nContador91) % -104) + &13 + &10;
                     |XGRABA nHandle, aAlfa;
                     |BUCLE dsorw004A;

                     #98#0 = nCodResto;
                     #98#1 = aAplicacion;
                     |LEE 040#98=;
                     |SI FSalida ! 0;
                         #98#0 = nCodResto;
                         #98#1 = aAplicacion;
                         |GRABA 020#98b;
                     |FINSI;
                     #98#2 = "*";
                     |GRABA 020#98a;
                     |LIBERA #98;
                 |FINSI;
                 |XCIERRA nHandle;
             |FINSI;

             |ABRE #91;  |CIERRA #91;  |DELINDEX #91;
             nContador91 = 0;
             aDestino    = "";
         |FINSI;

         aAplicacion = #3#1;
         aMenu       = #3#2;
         aPRM        = #3#7;
         |HAZ IndexaMenu;
     |FINSI;

     |ABRE #91;
     |ABRE #90;
     |SELECT #2#90;

     |SI #3#8 = 1;
         aAlfa3 = #3#3;   |QBF aAlfa3;
         aAlfa2 = #3#6;   |QBF aAlfa2;
         aAlfa2 = aAlfa2 + ":";
         #90#2  = aAlfa2;
         |LEE 101#90=;
         |SI FSalida = 0;
             |BORRA 020#90a;
             |HAZ CogePrograma;
             |LIBERA #90;
         |SINO;
             aAlfa2 = #3#6;   |QBF aAlfa2;
             #90#2  = aAlfa2;
             |LEE 101#90=;
             |SI FSalida = 0;
                 |BORRA 020#90a;
                 |HAZ CogePrograma;
             |FINSI;
             |LIBERA #90;
         |FINSI;
     |FINSI;

     |SI #3#8 = 2;
         aAlfa3 = #3#3;   |QBF aAlfa3;
         aAlfa2 = #3#6;   |QBF aAlfa2;
         aAlfa2 = aAlfa2 + ":";
         #90#2  = aAlfa2;
         |LEE 101#90=;
         |SI FSalida = 0;
             |HAZ CogePrograma;

             aAlfa3 = #90#1;
             |HAZ PonAcceso;
             |GRABA 020#90a;
             |LIBERA #90;
         |SINO;
             aAlfa2 = #3#6;   |QBF aAlfa2;
             #90#2  = aAlfa2;
             |LEE 101#90=;
             |SI FSalida = 0;
                 |HAZ CogePrograma;

                 aAlfa3 = #90#1;
                 |HAZ PonAcceso;
                 |GRABA 020#90a;
                 |LIBERA #90;
             |FINSI;
         |FINSI;
     |FINSI;

     |CIERRA #90;
     |CIERRA #91;
|FPRC;

|DEFBUCLE dsorm003;
     #3#1;
     ;
     nCodMenu, "        ", "        ", aDClave;
     nCodMenu, "zzzzzzzz", "zzzzzzzz", aHClave;
     ;
     NULL, GeneraMenu;
|FIN;

|PROCESO Menus;
     |MENSA "     Generando Menus de Usuarios";

     aOrigen  = "";
     aDestino = "";

     |BUCLE dsorm003;

     |SI aDestino ! "";
         |XABRE "W", aDestino, nHandle;
         |BUCLE dsorw003;
         |XCIERRA nHandle;

         |SI aAplicacion ! "menugnrl";  |Y aPRM = "S";
             aAbre   = aDirGrupo + aAplicacion + ".prm";   |QBT aAbre;
             |XABRE "W", aAbre, nHandle;
             |SI FSalida ] 0;
                 aAlfa = "-" + (("0000" + nContador91) % -104) + &13 + &10;
                 |XGRABA nHandle, aAlfa;

                 #98#0 = nCodResto;
                 #98#1 = aAplicacion;
                 |LEE 040#98=;
                 |SI FSalida ! 0;
                     #98#0 = nCodResto;
                     #98#1 = aAplicacion;
                     |GRABA 020#98b;
                 |FINSI;
                 #98#2 = "*";
                 |GRABA 020#98a;
                 |LIBERA #98;

                 |BUCLE dsorw004A;
             |FINSI;
             |XCIERRA nHandle;
         |FINSI;

         |ABRE #91;  |CIERRA #91;  |DELINDEX #91;

         nContador91 = 0;
         aDestino    = "";
     |FINSI;

     |ABRE #90; |CIERRA #90; |DELINDEX #90;
     |ABRE #91; |CIERRA #91; |DELINDEX #91;
|FPRC;

|| __________________________________________________________________________
||                        PROCESOS TABLAS
|| __________________________________________________________________________

|PROCESO GeneraTabla;
     #91#0 = #4#1;
     #91#1 = 999999;
     |LEE 040#91];
     |SI FSalida = 0;
         |LEE 040#91a;
     |SINO;
         |LEE 040#91u;
     |FINSI;

     nLinea  = 1;
     nNumDef = 1;
     |SI FSalida = 0;  |Y #91#0 = #4#1;
         nLinea   = #91#1 + 1;
         nNumDef  = #91#3 + 1;
     |FINSI;

     #91#0 = #4#1;
     #91#1 = nLinea;

     aCaracter = "#";

     |SI #4#4 = " ";  |Y #4#7 = " ";  |Y #4#6 = " ";  |Y #4#5 = "X";
         aCaracter = "@";
     |FINSI;

     |SI #4#4 = "X";  |Y #4#7 = " ";  |Y #4#6 = " ";
         aCaracter = "A";
     |FINSI;

     |SI #4#4 = " ";  |Y #4#7 = "X";  |Y #4#6 = " ";
         aCaracter = "B";
     |FINSI;

     |SI #4#4 = " ";  |Y #4#7 = " ";  |Y #4#6 = "X";
         aCaracter = "C";
     |FINSI;

     |SI #4#4 = "X";  |Y #4#7 = "X";  |Y #4#6 = " ";
         aCaracter = "D";
     |FINSI;

     |SI #4#4 = "X";  |Y #4#7 = " ";  |Y #4#6 = "X";
         aCaracter = "E";
     |FINSI;

     |SI #4#4 = " ";  |Y #4#7 = "X";  |Y #4#6 = "X";
         aCaracter = "F";
     |FINSI;

     aAlfa   = #4#2 + "-000" + aCaracter + "-000";

     #91#2 = aAlfa;
     #91#3 = nNumDef;
     #91#4 = #4#2;
     |GRABA 020#91n;
     |LIBERA #91;
|FPRC;

|DEFBUCLE dsorm004;
     #4#1;
     ;
     nCodMenu, "        ", "        ";
     nCodMenu, "zzzzzzzz", "zzzzzzzz";
     ;
     NULL, GeneraTabla;
|FIN;

|PROCESO Tablas;
     |MENSA "     Generando Restricciones Tablas";

     |BUCLE dsorm004;
|FPRC;

|| __________________________________________________________________________
||                        PROCESOS OCULTACION DE CAMPOS
|| __________________________________________________________________________

|PROCESO GeneraOcultaCampos;
     aCaracter = "#";

     #91#0 = #5#1;
     #91#1 = 999999;
     |LEE 040#91];
     |SI FSalida = 0;
         |LEE 040#91a;
     |SINO;
         |LEE 040#91u;
     |FINSI;

     nLinea  = 1;
     nNumDef = 0;
     |SI FSalida = 0;  |Y #91#0 = #5#1;
         nLinea  = #91#1 + 1;
         nNumDef = #91#3;
     |FINSI;

     |DDEFECTO #91;

     |SI aTabla ! #5#2;  |O aAplicacion ! #5#1;
         nLineaPrime = nLinea;
         aTabla      = #5#2;
         aAplicacion = #5#1;
         nNumDef     = nNumDef + 1;
         aAlfa       = "";
         nContaDef   = 0;

         #91#0 = #5#1;
         #91#1 = nLinea;
         #91#2 = aAlfa;
         #91#3 = nNumDef;
         |GRABA 020#91n;
         |LIBERA #91;

         nLinea = nLinea + 1;
     |FINSI;

     nContaDef = nContaDef + 1;
     |SI #5#6 = " ";
         aAlfa     = ("000" + #5#3) % -103;
     |FINSI;
     |SI #5#6 = "A";
         aAlfa     = (("000" + #5#3) % -103) + "#";
     |FINSI;
     |SI #5#6 = "C";
         aAlfa     = (("000" + #5#3) % -103) + "@";
     |FINSI;

     #91#0     = #5#1;
     #91#1     = nLinea;
     #91#2     = aAlfa;
     #91#3     = nNumDef;
     |GRABA 020#91n;
     |LIBERA #91;

     #91#0     = #5#1;
     #91#1     = nLineaPrime;
     |LEE 101#91=;
     |SI FSalida = 0;
         aAlfa = #5#2 + "-" + (("000" + nContaDef) % -103) + aCaracter + "-000";
         #91#2 = aAlfa;
         |GRABA 020#91a;
     |FINSI;
     |LIBERA #91;
|FPRC;

|DEFBUCLE dsorm005;
     #5#1;
     ;
     nCodResto, "        ", "        ", 000;
     nCodResto, "zzzzzzzz", "zzzzzzzz", 999;
     ;
     NULL, GeneraOcultaCampos;
|FIN;

|PROCESO OcultaCampos;
     |MENSA "     Generando Ocultacion de Campos";

     aTabla      = "";
     aAplicacion = "";
     |BUCLE dsorm005;
|FPRC;

|| __________________________________________________________________________
||                        PROCESOS FILTROS CAMPOSO
|| __________________________________________________________________________

|PROCESO CampoCompuesto;
     aCampo = aCampo + (("000" + #7#4) % -103);
|FPRC;

|DEFBUCLE dsorm007;
     #7#1;
     ;
     #6#0, #6#1, #6#2, #6#3, 000;
     #6#0, #6#1, #6#2, #6#3, 999;
     ;
     NULL, CampoCompuesto;
|FIN;

|PROCESO GeneraRelacionCampos;
     |SI nGeneracion = 0;
         #8#10 = " ";
         |GRABA 020#8a;
         |LIBERA #8;
         |ACABA;
     |FINSI;

     |SI #8#10 = "X";  |ACABA;  |FINSI;

     |SI aDTabla = "";
         aCaracter = "#";

         |SI #8#6 = " ";  |Y #8#9 = " ";  |Y #8#8 = " ";  |Y #8#7 = "X";
             aCaracter = "@";
         |FINSI;

         |SI #8#6 = "X";  |Y #8#9 = " ";  |Y #8#8 = " ";
             aCaracter = "A";
         |FINSI;

         |SI #8#6 = " ";  |Y #8#9 = "X";  |Y #8#8 = " ";
             aCaracter = "B";
         |FINSI;

         |SI #8#6 = " ";  |Y #8#9 = " ";  |Y #8#8 = "X";
             aCaracter = "C";
         |FINSI;

         |SI #8#6 = "X";  |Y #8#9 = "X";  |Y #8#8 = " ";
             aCaracter = "D";
         |FINSI;

         |SI #8#6 = "X";  |Y #8#9 = " ";  |Y #8#8 = "X";
             aCaracter = "E";
         |FINSI;

         |SI #8#6 = " ";  |Y #8#9 = "X";  |Y #8#8 = "X";
             aCaracter = "F";
         |FINSI;
     |FINSI;

     #91#0 = #8#1;
     #91#1 = 999999;
     |LEE 040#91];
     |SI FSalida = 0;
         |LEE 040#91a;
     |SINO;
         |LEE 040#91u;
     |FINSI;

     nLinea  = 1;
     nNumDef = 0;
     |SI FSalida = 0;  |Y #91#0 = #8#1;
         nLinea  = #91#1;
         nNumDef = #91#3;
     |FINSI;

     |DDEFECTO #91;

     nLinea      = nLinea + 1;
     nNumDef     = nNumDef + 1;
     aAlfa       = #8#3 + "+001" + aCaracter + "-000";
     #91#0       = #8#1;
     #91#1       = nLinea;
     #91#2       = aAlfa;
     #91#3       = nNumDef;
     nLineaPrime = nLinea;
     nContaDef   = 1;
     |GRABA 020#91n;
     |LIBERA #91;

     nLinea    = nLinea + 1;
     aAlfa     = "-" + (("00" + #8#5) % -102);
     #91#0     = #8#1;
     #91#1     = nLinea;
     #91#2     = aAlfa;
     #91#3     = nNumDef;
     |GRABA 020#91n;
     |LIBERA #91;

     #8#10 = "X";
     |GRABA 020#8a;

     aTabla       = #8#3;
     aAplicacion  = #6#1;
     aCondicion   = #6#14;
|FPRC;

|DEFBUCLE dsorm008;
     #8#1;
     ;
     nCodResto, aDAplica, "        ", aDTabla;
     nCodResto, aHAplica, "zzzzzzzz", aHTabla;
     ;
     NULL, GeneraRelacionCampos;
|FIN;

|PROCESO LeeCampo;
     |DBASS aBase;    |QBF aBase;
     aMas = aBase + aAplicacion + "/def/" + aTabla + ".mas";  |QBT aMas;
     |CARGA_DEF aMas, referen@;
     |SI FSalida ! 0;
         |MENAV "     No existe la Tabla";
         |ERROR;
         |ACABA;
     |FINSI;

     aQueQuiero = "|C" + aCampo + "TIPO";
     aTipo      = #900#aQueQuiero#;

     aLongitud = "";

     |SI aTipo = "A";
         aQueQuiero = "|C" + aCampo + "LONGITUD";
         aLongitud  = #900#aQueQuiero#;
     |FINSI;

     |DESCARGA_DEF #referen@;
|FPRC;

|PROCESO DesgloseCampos;
     aAlfa1    = #6#12;  |QBF aAlfa1;
     aAlfa2    = #6#13;  |QBF aAlfa2;

     |SI #6#8 = "N";
         aCampo = ("000" + #6#9) % -103;
     |SINO;
         aCampo = "";
         |BUCLE dsorm007;
     |FINSI;

     |HAZ LeeCampo;
     |SI aLongitud ! "";
         nLongitud = aLongitud;
         nPos      = 100 + nLongitud;
         aAlfa1    = (aAlfa1 + (" " * nLongitud)) % nPos;
         aAlfa2    = (aAlfa2 + (" " * nLongitud)) % nPos;
     |FINSI;

     |SI #6#11 = "=";  |O #6#11 = "!";
         nLinea    = nLinea + 1;
         nContaDef = nContaDef + 1;
         aAlfa     = aCampo + #6#11 + &34 + aAlfa1 + &34;
         #91#0     = #6#1;
         #91#1     = nLinea;
         #91#2     = aAlfa;
         #91#3     = nNumDef;
         |GRABA 020#91n;
         |LIBERA #91;
     |SINO;
         |SI #6#11 = "[";
             aDCaracter = "]";
             aHCaracter = "[";
         |SINO;
             aDCaracter = ">";
             aHCaracter = "<";
         |FINSI;

         nLinea    = nLinea + 1;
         nContaDef = nContaDef + 1;
         aAlfa     = aCampo + aDCaracter + &34 + aAlfa1 + &34;
         #91#0     = #6#1;
         #91#1     = nLinea;
         #91#2     = aAlfa;
         #91#3     = nNumDef;
         |GRABA 020#91n;
         |LIBERA #91;

         nLinea    = nLinea + 1;
         nContaDef = nContaDef + 1;
         aAlfa     = aCampo + aHCaracter + &34 + aAlfa2 + &34;
         #91#0     = #6#1;
         #91#1     = nLinea;
         #91#2     = aAlfa;
         #91#3     = nNumDef;
         |GRABA 020#91n;
         |LIBERA #91;
     |FINSI;
|FPRC;

|PROCESO GeneraFiltrosCampos;
     aAlfa = #6#12;  |QBF aAlfa;
     |SI aAlfa = "%1"; #6#12 = aCodDepar;     |FINSI;
     |SI aAlfa = "%2"; #6#12 = aCodSubDepar;  |FINSI;
     |SI aAlfa = "%3"; #6#12 = aCodSerie;     |FINSI;
     |SI aAlfa = "%4"; #6#12 = aCodAlmacen;   |FINSI;
     |SI aAlfa = "%5"; #6#12 = aCodZona;      |FINSI;
     |SI aAlfa = "%6"; #6#12 = aCodFactura;   |FINSI;

     aAlfa = #6#13;  |QBF aAlfa;
     |SI aAlfa = "%1"; #6#13 = aCodDepar;     |FINSI;
     |SI aAlfa = "%2"; #6#13 = aCodSubDepar;  |FINSI;
     |SI aAlfa = "%3"; #6#13 = aCodSerie;     |FINSI;
     |SI aAlfa = "%4"; #6#13 = aCodAlmacen;   |FINSI;
     |SI aAlfa = "%5"; #6#13 = aCodZona;      |FINSI;
     |SI aAlfa = "%6"; #6#13 = aCodFactura;   |FINSI;

     aCaracter = "#";

     |SI #6#15 = " ";  |Y #6#18 = " ";  |Y #6#17 = " ";  |Y #6#16 = "X";
         aCaracter = "@";
     |FINSI;

     |SI #6#15 = "X";  |Y #6#18 = " ";  |Y #6#17 = " ";
         aCaracter = "A";
     |FINSI;

     |SI #6#15 = " ";  |Y #6#18 = "X";  |Y #6#17 = " ";
         aCaracter = "B";
     |FINSI;

     |SI #6#15 = " ";  |Y #6#18 = " ";  |Y #6#17 = "X";
         aCaracter = "C";
     |FINSI;

     |SI #6#15 = "X";  |Y #6#18 = "X";  |Y #6#17 = " ";
         aCaracter = "D";
     |FINSI;

     |SI #6#15 = "X";  |Y #6#18 = " ";  |Y #6#17 = "X";
         aCaracter = "E";
     |FINSI;

     |SI #6#15 = " ";  |Y #6#18 = "X";  |Y #6#17 = "X";
         aCaracter = "F";
     |FINSI;

     |SI #6#14 = "S";  aCondicion = "";  |FINSI;

     aDTabla  = #6#2;
     aHTabla  = #6#2;
     aDAplica = #6#1;
     aHAplica = #6#1;
     |BUCLE dsorm008;

     #91#0 = #6#1;
     #91#1 = 999999;
     |LEE 040#91];
     |SI FSalida = 0;
         |LEE 040#91a;
     |SINO;
         |LEE 040#91u;
     |FINSI;

     nLinea  = 1;
     nNumDef = 0;
     |SI FSalida = 0;  |Y #91#0 = #6#1;
         nLinea  = #91#1;
         nNumDef = #91#3;
     |FINSI;

     |DDEFECTO #91;
     |SI aTabla ! #6#2; |O aCondicion ! #6#14;  |O aAplicacion ! #6#1;
         nLineaPrime  = nLinea + 1;
         aTabla       = #6#2;
         aAplicacion  = #6#1;
         aCondicion   = #6#14;
         nLinea       = nLinea + 1;
         nNumDef      = nNumDef + 1;
         aAlfa        = "";
         nContaDef    = 0;

         #91#0 = #6#1;
         #91#1 = nLinea;
         #91#2 = aAlfa;
         #91#3 = nNumDef;
         |GRABA 020#91n;
         |LIBERA #91;
     |FINSI;

     |HAZ DesgloseCampos;

     #91#0     = #6#1;
     #91#1     = nLineaPrime;
     |LEE 101#91=;
     |SI FSalida = 0;
         aAlfa = #6#2 + "+" + (("000" + nContaDef) % -103) + aCaracter + "-000";
         #91#2 = aAlfa;
         |GRABA 020#91a;
     |FINSI;
     |LIBERA #91;
|FPRC;

|DEFBUCLE dsorm006;
     #6#1;
     ;
     nCod6, "        ", "        ", 001;
     nCod6, "zzzzzzzz", "zzzzzzzz", 999;
     ;
     NULL, GeneraFiltrosCampos;
|FIN;

|PROCESO FiltrosCamposG;
     nCod6  = nCodResto;

     |ABRE #6;
     #6#0 = 1;
     #6#1 = "";
     #6#2 = "";
     #6#3 = 1;
     |LEE 000#6];
     |SI FSalida = 0;  |Y #6#0 ! 8888;
         nCod6  = nCodMenu;
     |FINSI;
     |CIERRA #6;

     |MENSA "     Generando Filtracion de Campos General";

     nGeneracion = 0;
     aDAplica    = "";
     aHAplica    = "zzzzzzzz";
     aDTabla     = "";
     aHTabla     = "zzzzzzzz";
     |BUCLE dsorm008;

     nGeneracion = 1;
     aCondicion  = "";
     aTabla      = "";
     aAplicacion = "";
     |BUCLE dsorm006;

     aDAplica    = "";
     aHAplica    = "zzzzzzzz";
     aDTabla     = "";
     aHTabla     = "zzzzzzzz";
     aTabla      = "";
     aAplicacion = "";
     |BUCLE dsorm008;
|FPRC;

|PROCESO DesgloseCamposP;
     aAlfa1    = #10#7;  |QBF aAlfa1;
     aAlfa2    = #10#8;  |QBF aAlfa2;

     aCampo = ("000" + #10#3) % -103;

     |HAZ LeeCampo;
     |SI aLongitud ! "";
         nLongitud = aLongitud;
         nPos      = 100 + nLongitud;
         aAlfa1    = (aAlfa1 + (" " * nLongitud)) % nPos;
         aAlfa2    = (aAlfa2 + (" " * nLongitud)) % nPos;
     |FINSI;

     |SI #10#6 = "=";  |O #10#6 = "!";
         nLinea    = nLinea + 1;
         nContaDef = nContaDef + 1;
         aAlfa     = aCampo + #10#6 + &34 + aAlfa1 + &34;
         #91#0     = #10#1;
         #91#1     = nLinea;
         #91#2     = aAlfa;
         #91#3     = nNumDef;
         |GRABA 020#91n;
         |LIBERA #91;
     |SINO;
         |SI #10#6 = "[";
             aDCaracter = "]";
             aHCaracter = "[";
         |SINO;
             aDCaracter = ">";
             aHCaracter = "<";
         |FINSI;

         nLinea    = nLinea + 1;
         nContaDef = nContaDef + 1;
         aAlfa     = aCampo + aDCaracter + &34 + aAlfa1 + &34;
         #91#0     = #10#1;
         #91#1     = nLinea;
         #91#2     = aAlfa;
         #91#3     = nNumDef;
         |GRABA 020#91n;
         |LIBERA #91;

         nLinea    = nLinea + 1;
         nContaDef = nContaDef + 1;
         aAlfa     = aCampo + aHCaracter + &34 + aAlfa2 + &34;
         #91#0     = #10#1;
         #91#1     = nLinea;
         #91#2     = aAlfa;
         #91#3     = nNumDef;
         |GRABA 020#91n;
         |LIBERA #91;
     |FINSI;
|FPRC;

|PROCESO GeneraFiltrosCamposP;
     aCaracter = "#";

     |SI #9#8 = " ";  |Y #9#11 = " ";  |Y #9#10 = " ";  |Y #9#9 = "X";
         aCaracter = "@";
     |FINSI;

     |SI #9#8 = "X";  |Y #9#11 = " ";  |Y #9#10 = " ";
         aCaracter = "A";
     |FINSI;

     |SI #9#8 = " ";  |Y #9#11 = "X";  |Y #9#10 = " ";
         aCaracter = "B";
     |FINSI;

     |SI #9#8 = " ";  |Y #9#11 = " ";  |Y #9#10 = "X";
         aCaracter = "C";
     |FINSI;

     |SI #9#8 = "X";  |Y #9#11 = "X";  |Y #9#10 = " ";
         aCaracter = "D";
     |FINSI;

     |SI #9#8 = "X";  |Y #9#11 = " ";  |Y #9#10 = "X";
         aCaracter = "E";
     |FINSI;

     |SI #9#8 = " ";  |Y #9#11 = "X";  |Y #9#10 = "X";
         aCaracter = "F";
     |FINSI;

     |SI #10#9 = "S";  aCondicion = "";  |FINSI;

     #91#0 = #9#1;
     #91#1 = 999999;
     |LEE 040#91];
     |SI FSalida = 0;
         |LEE 040#91a;
     |SINO;
         |LEE 040#91u;
     |FINSI;

     nLinea  = 1;
     nNumDef = 0;
     |SI FSalida = 0;  |Y #91#0 = #9#1;
         nLinea  = #91#1;
         nNumDef = #91#3;
     |FINSI;

     |DDEFECTO #91;
     |SI aTabla ! #9#2; |O aCondicion ! #10#9;  |O aAplicacion ! #9#1;
      |O nCampo ! #9#3;
         nLineaPrime  = nLinea + 1;
         aTabla       = #9#2;
         aAplicacion  = #9#1;
         aCondicion   = #10#9;
         nCampo       = #9#3;
         nLinea       = nLinea + 1;
         nNumDef      = nNumDef + 1;
         aAlfa        = "";
         nContaDef    = 0;

         #91#0 = #9#1;
         #91#1 = nLinea;
         #91#2 = aAlfa;
         #91#3 = nNumDef;
         |GRABA 020#91n;
         |LIBERA #91;
     |FINSI;

     |HAZ DesgloseCamposP;

     #91#0     = #9#1;
     #91#1     = nLineaPrime;
     |LEE 101#91=;
     |SI FSalida = 0;
         aAlfa = #9#2 + "+" + (("000" + nContaDef) % -103) + aCaracter + "*" + (("000" + #9#3) % -103);
         #91#2 = aAlfa;
         |GRABA 020#91a;
     |FINSI;
     |LIBERA #91;
|FPRC;

|DEFBUCLE dsorm010;
     #10#1;
     ;
     #9#0, #9#1, #9#2, #9#3, 001;
     #9#0, #9#1, #9#2, #9#3, 999;
     ;
     NULL, GeneraFiltrosCamposP;
|FIN;

|PROCESO GeneraFiltrosPrograma;
     aCondicion  = "";
     aTabla      = "";
     aAplicacion = "";
     nCampo      = 0;
     |BUCLE dsorm010;
|FPRC;

|DEFBUCLE dsorm009;
     #9#1;
     ;
     nCodResto, "        ", "        ", 000;
     nCodResto, "zzzzzzzz", "zzzzzzzz", 999;
     ;
     NULL, GeneraFiltrosPrograma;
|FIN;

|PROCESO FiltrosCamposP;
     |MENSA "     Generando Filtracion de Campos Programa";

     |BUCLE dsorm009;
|FPRC;

|| __________________________________________________________________________
||                        PROCESOS GRABAPRM
|| __________________________________________________________________________

|PROCESO MiraAplicacionesPRM;
     |SI aAplicacion ! #91#0;
         |SI aAbre ! "";
             |XCIERRA nHandle;
         |FINSI;

         |SALVA_DATOS #91;
         aAplicacion = #91#0;
         #91#0 = aAplicacion;
         #91#1 = 999999;
         |LEE 040#91];
         |SI FSalida = 0;
             |LEE 040#91a;
         |SINO;
             |LEE 040#91u;
         |FINSI;
         |SI FSalida = 0;  |Y #91#0 = aAplicacion;
             nNumDef = #91#3;
         |FINSI;
         |REPON_DATOS #91;
         |LEE 040#91=;

         aAbre       = aDirGrupo + aAplicacion + ".prm";   |QBT aAbre;
         |XABRE "A", aAbre, nHandle;

         #98#0 = nCodResto;
         #98#1 = aAplicacion;
         |LEE 101#98=;
         |SI FSalida ! 0;
             aAlfa = "-0000" + &13 + &10;
             |XGRABA nHandle, aAlfa;

             #98#0 = nCodResto;
             #98#1 = aAplicacion;
             #98#2 = " *";
             |GRABA 020#98n;
         |SINO;
             #98#2 = "**";
             |GRABA 020#98a;
         |FINSI;
         |LIBERA #98;

         aAlfa = "-" + (("0000" + nNumDef) % -104) + &13 + &10;
         |XGRABA nHandle, aAlfa;
     |FINSI;

     aAlfa = #91#2;  |QBF aAlfa;
     |XGRABA nHandle, aAlfa;
|FPRC;

|DEFBUCLE dsorw004B;
     #91#1;
     ;
     "        ", 000001;
     "zzzzzzzz", 999999;
     ;
     NULL, MiraAplicacionesPRM;
|FIN;

|PROCESO GrabaPRM;
     aAplicacion = "";
     aAbre       = "";
     |BUCLE dsorw004B;
     |SI aAbre ! "";
         |XCIERRA nHandle;
     |FINSI;
|FPRC;

|PROCESO Filtros;
     |ABRE #91;
     |HAZ OcultaCampos;
     |HAZ FiltrosCamposG;
     |HAZ FiltrosCamposP;

     |HAZ Tablas;
     |CIERRA #91;

     |HAZ GrabaPRM;

     |ABRE #90;  |CIERRA #90;  |DELINDEX #90;
     |ABRE #91;  |CIERRA #91;  |DELINDEX #91;
|FPRC;

|PROCESO dsorm011;
     aCaracter = "#";

     |SI #11#6 = " ";  |Y #11#9 = " ";  |Y #11#8 = " ";  |Y #11#7 = "X";
         aCaracter = "@";
     |FINSI;

     |SI #11#6 = "X";  |Y #11#9 = " ";  |Y #11#8 = " ";
         aCaracter = "A";
     |FINSI;

     |SI #11#6 = " ";  |Y #11#9 = "X";  |Y #11#8 = " ";
         aCaracter = "B";
     |FINSI;

     |SI #11#6 = " ";  |Y #11#9 = " ";  |Y #11#8 = "X";
         aCaracter = "C";
     |FINSI;

     |SI #11#6 = "X";  |Y #11#9 = "X";  |Y #11#8 = " ";
         aCaracter = "D";
     |FINSI;

     |SI #11#6 = "X";  |Y #11#9 = " ";  |Y #11#8 = "X";
         aCaracter = "E";
     |FINSI;

     |SI #11#6 = " ";  |Y #11#9 = "X";  |Y #11#8 = "X";
         aCaracter = "F";
     |FINSI;

     #91#0 = #11#1;
     #91#1 = 999999;
     |LEE 040#91];
     |SI FSalida = 0;
         |LEE 040#91a;
     |SINO;
         |LEE 040#91u;
     |FINSI;

     nLinea  = 1;
     nNumDef = 0;
     |SI FSalida = 0;  |Y #91#0 = #11#1;
         nLinea  = #91#1;
         nNumDef = #91#3;
     |FINSI;

     |DDEFECTO #91;
     |SI aAplicacion ! #11#1;
         aAplicacion  = #11#1;
         nNumDef      = 0;
         nLinea       = 1;
         aAlfa        = "";

         #91#0 = #11#1;
         #91#1 = nLinea;
         #91#2 = aAlfa;
         #91#3 = nNumDef;
         |GRABA 020#91n;
         |LIBERA #91;
     |FINSI;

     |DDEFECTO #91;
     #91#0  = #11#1;
     #91#1  = nLinea + 1;
     aAlfa  = aCaracter + #11#2;
     #91#2  = aAlfa;
     #91#3  = nNumDef + 1;
     |GRABA 020#91n;
     |LIBERA #91;

     #91#0     = #11#1;
     #91#1     = 1;
     |LEE 101#91=;
     |SI FSalida = 0;
         aAlfa = "-" + (("0000" + #91#3) % -104);
         #91#2 = aAlfa;
         |GRABA 020#91a;
     |FINSI;
     |LIBERA #91;
|FPRC;

|DEFBUCLE dsorm011;
     #11#1;
     ;
     nCodResto, "        ", "               ";
     nCodResto, "zzzzzzzz", "zzzzzzzzzzzzzzz";
     ;
     NULL, dsorm011;
|FIN;

|PROCESO dsorw004C;
     |SI aAplicacion ! #91#0;
         |SI aAbre ! "";
             |XCIERRA nHandle;
         |FINSI;

         |SALVA_DATOS #91;
         aAplicacion = #91#0;
         #91#0 = aAplicacion;
         #91#1 = 999999;
         |LEE 040#91];
         |SI FSalida = 0;
             |LEE 040#91a;
         |SINO;
             |LEE 040#91u;
         |FINSI;
         |SI FSalida = 0;  |Y #91#0 = aAplicacion;
             nNumDef = #91#3;
         |FINSI;

         |REPON_DATOS #91;
         |LEE 040#91=;

         aAbre       = aDirGrupo + aAplicacion + ".prm";   |QBT aAbre;
         |XABRE "AB", aAbre, nHandle;

         #98#0 = nCodResto;
         #98#1 = aAplicacion;
         |LEE 000#98=;
         |SI FSalida ! 0;
             aAlfa = "-0000" + &13 + &10;
             |XGRABA nHandle, aAlfa;

             aAlfa = "-0000" + &13 + &10;
             |XGRABA nHandle, aAlfa;
         |SINO;
             aAlfa = #98#2;  |QBF aAlfa;
             |SI aAlfa = "*";
                 aAlfa = "-0000" + &13 + &10;
                 |XGRABA nHandle, aAlfa;
             |FINSI;
         |FINSI;

         aAlfa = "-" + (("0000" + nNumDef) % -104) + &13 + &10;
         |XGRABA nHandle, aAlfa;
     |SINO;
         aAlfa = #91#2;  |QBF aAlfa;
         aAlfa = (aAlfa + (" " * 16)) % 116;
         aAlfa = aAlfa + &13 + &10;
         |XGRABA nHandle, aAlfa;
     |FINSI;
|FPRC;

|DEFBUCLE dsorw004C;
     #91#1;
     ;
     "        ", 000001;
     "zzzzzzzz", 999999;
     ;
     NULL, dsorw004C;
|FIN;

|PROCESO GrabaPRMEmpresas;
     aAplicacion = "";
     aAbre       = "";
     |BUCLE dsorw004C;
     |SI aAbre ! "";
         |XCIERRA nHandle;
     |FINSI;
|FPRC;

|PROCESO FiltrosEmpresas;
     |ABRE #91;
     aAplicacion = "";
     |BUCLE dsorm011;
     |CIERRA #91;

     |HAZ GrabaPRMEmpresas;

     |ABRE #91;  |CIERRA #91;  |DELINDEX #91;
|FPRC;

|| __________________________________________________________________________
||              PROCESO QUE REPARTE SE LLEVA LA MEJOR PARTE
|| __________________________________________________________________________

|PROCESO dsorm006B;
     |BORRA 020#6a;
     |LIBERA #6;
|FPRC;

|DEFBUCLE dsorm006B;
     #6#1;
     ;
     8888, "        ", "        ", 001;
     8888, "zzzzzzzz", "zzzzzzzz", 999;
     ;
     NULL, dsorm006B;
|FIN;

|| ********************************
|| P E R F I L E S
|| ********************************

|PROCESO dsorm006M;
     |SALVA_DATOS #6;

     #6#0 = 8888;

     aAlfa = #6#12;  |QBF aAlfa;
     |SI aAlfa = "%1"; #6#12 = aCodDepar;     |FINSI;
     |SI aAlfa = "%2"; #6#12 = aCodSubDepar;  |FINSI;
     |SI aAlfa = "%3"; #6#12 = aCodSerie;     |FINSI;
     |SI aAlfa = "%4"; #6#12 = aCodAlmacen;   |FINSI;
     |SI aAlfa = "%5"; #6#12 = aCodZona;      |FINSI;
     |SI aAlfa = "%6"; #6#12 = aCodFactura;   |FINSI;

     aAlfa = #6#13;  |QBF aAlfa;
     |SI aAlfa = "%1"; #6#13 = aCodDepar;     |FINSI;
     |SI aAlfa = "%2"; #6#13 = aCodSubDepar;  |FINSI;
     |SI aAlfa = "%3"; #6#13 = aCodSerie;     |FINSI;
     |SI aAlfa = "%4"; #6#13 = aCodAlmacen;   |FINSI;
     |SI aAlfa = "%5"; #6#13 = aCodZona;      |FINSI;
     |SI aAlfa = "%6"; #6#13 = aCodFactura;   |FINSI;

     |GRABA 020#6n;
     |LIBERA #6;

     |REPON_DATOS #6;
     |LEE 040#6=;
|FPRC;

|DEFBUCLE dsorm006M;
     #6#1;
     ;
     0000, "        ", "        ", 001;
     0000, "zzzzzzzz", "zzzzzzzz", 999;
     ;
     NULL, dsorm006M;
|FIN;

|PROCESO Convierte;
     |BUCLE dsorm006B;
     |BUCLE dsorm006M;
|FPRC;

|PROCESO Perfiles;
     |DBASE aBaseAqui;    |QBF aBaseAqui;
     aMas = aBaseAqui + "def/dsprem03.mas";  |QBT aMas;
     |CARGA_DEF aMas, preemes@;

     |ABRE #901;
     |SELECT #2#901;
     #901#11 = #0#0;
     |LEE 040#901=;
     |SI FSalida ! 0;
         |DESCARGA_DEF #preemes@;
         |ACABA;
     |FINSI;
     |CIERRA #901;

     nCodMenu     = (#901#3 * 100) + #901#4;
     nCodRegion   = #901#0;
     nCodDele     = #901#1;
     nCodAgencia  = #901#2;

     |DESCARGA_DEF #preemes@;

     |DBASE aBaseAqui;    |QBF aBaseAqui;
     aMas = aBaseAqui + "def/dsprem02.mas";  |QBT aMas;
     |CARGA_DEF aMas, preemes@;

     |ABRE #901;
     #901#0 = nCodRegion;
     #901#1 = nCodDele;
     #901#2 = nCodAgencia;
     |LEE 040#901=;
     |SI FSalida ! 0;
         |DESCARGA_DEF #preemes@;
         |ACABA;
     |FINSI;
     |CIERRA #901;

     nCodResto    = #901#4;
     aCodDepar    = #901#13;
     aCodSubDepar = #901#14;
     aCodSerie    = #901#17;
     aCodAlmacen  = #901#18;
     aCodZona     = #901#19;
     aCodFactura  = ("00000" + #901#16) % -105;

     |DESCARGA_DEF #preemes@;

     |HAZ Convierte;
|FPRC;

|| ********************************
|| A U D I T O R E S
|| ********************************

|PROCESO dsorm006F;
     |SI aAplicacion = #6#1;  |Y #6#2 = aTabla;
         |ACABA;
     |FINSI;

     aAlfa = #6#12;  |QBF aAlfa;
     |SI aAlfa = "%6";
         #6#12 = aCodFactura;
         |GRABA 020#6a;
         |LIBERA #6;

         aAplicacion = #6#1;
         aTabla      = #6#2;
     |FINSI;
|FPRC;

|DEFBUCLE dsorm006F;
     #6#1;
     ;
     8888, "        ", "        ", 001;
     8888, "zzzzzzzz", "zzzzzzzz", 999;
     be;
     NULL, dsorm006F;
|FIN;

|PROCESO gecom004;
     |SI nPasada   = 0;
         nEmpresas = nEmpresas + 1;
         |ACABA;
     |FINSI;

     #911#0 = #904#1;
     |LEE 000#911=;
     |SI FSalida ! 0;  |DDEFECTO #911;  |FINSI;

     aCodFactura  = nGrupo + (("00"  + #911#1) % -102) + "01";
     aAplicacion  = "";
     aTabla       = "";
     |BUCLE dsorm006F;
|FPRC;

|DEFBUCLE gecom004;
     #904#1002;
     ;
     nAuditor, 00001;
     nAuditor, 99999;
     ;
     NULL, gecom004;
|FIN;

|PROCESO dsorm006A;
     aAlfa = #6#12;  |QBF aAlfa;  |SI aAlfa ! "%6";  |ACABA;  |FINSI;
     aAlfa = #6#13;  |QBF aAlfa;  |SI aAlfa ! "";    |ACABA;  |FINSI;

     |SALVA_DATOS #6;

     #6#0 = 8888;

     |PARA nCampo = 1;  |SI nCampo [ nEmpresas;  |HACIENDO nCampo = nCampo + 1;
           #6#3  = nCampo;
           #6#14 = "S";
           |GRABA 020#6n;
           |LIBERA #6;
     |SIGUE;

     |REPON_DATOS #6;
     |LEE 040#6=;
|FPRC;

|DEFBUCLE dsorm006A;
     #6#1;
     ;
     0000, "        ", "        ", 001;
     0000, "zzzzzzzz", "zzzzzzzz", 999;
     ;
     NULL, dsorm006A;
|FIN;

|PROCESO Auditores;
     |DBASS aBaseChek;    |QBF aBaseChek;
     aBaseChek = aBaseChek + "gecochek/";
     aMas      = aBaseChek + "def/gecom001.mas";  |QBT aMas;
     |CARGA_DEF aMas, gecom001@;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     aMas = aBaseChek + "def/gecom004.mas";  |QBT aMas;
     |CARGA_DEF aMas, gecom004@;
     |SI FSalida ! 0;
         |DESCARGA_DEF #gecom001@;
         |ACABA;
     |FINSI;

     aMas = aBaseChek + "def/gecom011.mas";  |QBT aMas;
     |CARGA_DEF aMas, gecom011@;
     |SI FSalida ! 0;
         |DESCARGA_DEF #gecom004@;
         |DESCARGA_DEF #gecom001@;
         |ACABA;
     |FINSI;

     aFic = aBaseChek + "fich/";

     |PATH_DAT #902 aFic;
     |PATH_DAT #904 aFic;
     |PATH_DAT #911 aFic;

     |ABRE #902;
     |SELECT #4#902;
     #902#12 = #0#0;
     |LEE 000#902=;
     |SI FSalida = 0;
         nCodMenu  = (#902#7 * 100) + #902#9;
         nCodResto = 8888;
     |FINSI;

     nPasada   = 0;
     nEmpresas = 0;
     nAuditor  = #902#0;
     |BUCLE gecom004;

     |BUCLE dsorm006B;   || Preparacion de los filtros
     |BUCLE dsorm006A;

     |DBASS aBase;  |QBF aBase;
     aAlfa   = aBase % -201;
     nGrupo  = aAlfa;
     |SI nGrupo = 0;  nGrupo = 1;  |FINSI;

     nPasada   = 1;
     |ABRE #911;
     |BUCLE gecom004;
     |CIERRA #911;

     |DESCARGA_DEF #gecom011@;
     |DESCARGA_DEF #gecom004@;
     |DESCARGA_DEF #gecom001@;
|FPRC;

|PROCESO Tipo3;  |TIPO 3;
     aFichero = ("w3" + Usuario + "zzzzzzzz") % 108;
     |NOME_DAT #90 aFichero;
     |ABRE #90;  |CIERRA #90;  |DELINDEX #90;

     aFichero = ("w4" + Usuario + "zzzzzzzz") % 108;
     |NOME_DAT #91 aFichero;
     |ABRE #91;  |CIERRA #91;  |DELINDEX #91;

     aFichero = ("w8" + Usuario + "zzzzzzzz") % 108;
     |NOME_DAT #98 aFichero;
     |ABRE #98;  |CIERRA #98;  |DELINDEX #98;

     |SI aParametro = "AUTOMATICO";
         |SI #0#0 [ 8999;
             |HAZ Perfiles;
         |SINO;
             |HAZ Auditores;
         |FINSI;
     |SINO;
          nCodMenu  = #0#0;
          nCodResto = #0#0;
     |FINSI;

     aDClave = " ";
     aHClave = "z" * 100;

     |DFICE aFich;   |QBF aFich;
     aDirGrupo = aFich + "grupos";   |MKDIR aDirGrupo;
     aDirGrupo = aDirGrupo + "/";

||     |HAZ LimpiaDirectorio;

     |ABRE #98;
     |HAZ Menus;
     |HAZ Filtros;
     |HAZ FiltrosEmpresas;
     |CIERRA #98;

     |ABRE #90;  |CIERRA #90;  |DELINDEX #90;
     |ABRE #91;  |CIERRA #91;  |DELINDEX #91;
     |ABRE #98;  |CIERRA #98;  |DELINDEX #98;
|FPRC;

|PROCESO dsorm001;
     |SI #1#0 = 8888;  |ACABA;  |FINSI;

     #0#0 = #1#0;
     #0#1 = #1#1;
     |HAZ Tipo3;
|FPRC;

|DEFBUCLE dsorm001;
     #1#1;
     ;
     nDGrupo;
     nHGrupo;
     ;
     NULL, dsorm001;
|FIN;

|PROCESO CalculaMenus;
     |DDEFECTO #3;
     |ABRE #3;
     |SELECT #1#3;
     |LEE 000#3p;
     |PARA;  |SI FSalida = 0;  |HACIENDO;
         aPunto = #3#6;

         |SI enGrupo ! #3#0;  |O eaAplica ! #3#1;  |O eaMenu ! #3#2;
             |CIERRA #3;
             enGrupo  = #3#0;
             eaAplica = #3#1;
             eaMenu   = #3#2;
             |SUB_EJECUTA "dsorz001;CREAMENU";

             |ABRE #3;
             |SELECT #1#3;
             #3#0 = enGrupo;
             #3#1 = eaAplica;
             #3#2 = eaMenu;
             #3#6 = aPunto;
             |LEE 000#3];
         |FINSI;

         |LEE 000#3s;
     |SIGUE;
     |CIERRA #3;
|FPRC;

|PROCESO LimpiaDirectorio;
     |DBASS aBase;   |QBF aBase;
     |DFICE aFich;   |QBF aFich;
     aDirGrupo = aFich + "grupos";   |MKDIR aDirGrupo;
     aDirGrupo = aDirGrupo + "/";

     aDirFichero = aDirGrupo + "*";
     |_PDIR aDirFichero;
     |PARA;  |SI FSalida ] 0; |HACIENDO;
         aAlfa   = aBase + "bin/agirm -r " + aDirFichero;
         |SYSTEM aAlfa;

         |_SDIR aDirFichero;
     |SIGUE;
|FPRC;

|PROGRAMA;
|DEBUG;
     |HAZ LimpiaDirectorio;
     |HAZ CalculaMenus;

     aParametro = PARAMETRO;  |QBF aParametro;
     |SI aParametro = "";
         |MANTE #0;
     |SINO;
         nDGrupo = 1;
         nHGrupo = 9999;
         |SI aParametro ! "AUTOMATICO";
             nDGrupo = aParametro;
             nHGrupo = aParametro;
         |FINSI;
         |BUCLE dsorm001;
     |FINSI;
|FPRO;
