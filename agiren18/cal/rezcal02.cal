|FICHEROS;
      rew99999 #999;

      rem01010@ #10;
      rem01018@ #18;
      rem01022@ #22;
      rem01033@ #33;
      rem01026@ #26;

     &refere1@ #101;
     &refere2@ #102;
     &refere3@ #103;
|FIN;

|VARIABLES;
|FIN;

|INCLUYE i_varcal;

|PROCESO Ded_32_2_1;
     |SI #rem01018@#26 ! "S";  |ACABA;  |FINSI;

     |SI enConta < 6; enVal0133 = enVal0133 + #rem01018@#18;  |FINSI;

     nTotRdtos = nTotRenta - #rem01018@#7;    || 131
     nTotRdtos = nTotRdtos - #rem01018@#11;   || 158
     nTotRdtos = nTotRdtos - #rem01018@#13;   || 187

     enImporte = #rem01018@#18;
     nRdto     = 0;
     |SI enImporte > 0;
         nRdto = 2000;

         |SI enImporte [ 14450; |Y nTotRdtos < 6500.01;
             |SI enImporte < 11250;
                 nRdto = nRdto + 3700;
             |SINO;
                 |SI enImporte ] 11250;  |Y enImporte [ 14450;
                     nRdto = nRdto + (3700 - (1.15625 * (enImporte - 11250))) ! 2;

                 |FINSI;
             |FINSI;
         |FINSI;

         |SI enConta < 6;
             |SI #rew99999#3 ] 33;  |Y #rew99999#3 < 65;
                 |SI #rew99999#4 = "N";
                     enValMin = enValMin + 3500;
                     nRdto    = nRdto + 3500;
                 |SINO;
                     enValMin = enValMin + 7750;
                     nRdto    = nRdto + 7750;
                 |FINSI;
             |FINSI;

             |SI #rew99999#3 ] 65;
                 enValMin = enValMin + 7750;
                 nRdto    = nRdto + 7750;
             |FINSI;
         |SINO;
             nRdto = nRdto + enValMin;
         |FINSI;
     |FINSI;

     eaCasilla = "05-0147-LIQ18"; |HAZ SacaImporte;  nValor = enImporte;
     |SI nRdto > nValor;
         nRdto = nValor;
     |FINSI;

     |SI nRdto < 0;  nRdto = 0;  |FINSI;

     |SI enConta > 6;
         |SI nRdto > enVal0133;
             nRdto = enVal0133;
         |FINSI;
     |FINSI;

     enSwAcum  = 0;
     eaCasilla = "05-0148-LIQ18";
     enImporte = nRdto;
     |HAZ GrabaLineal;
|FPRC;

|PROCESO Ded_32_2_3;
     eaCasilla = "05-0148-LIQ18"; |HAZ SacaImporte;  nVal0135 = enImporte;
     |SI nVal0135 ! 0;  |ACABA;  |FINSI;

     nRdto = 0;
     |SI nTotRenta < 8000;
         nRdto = 1620;
     |SINO;
         |SI nTotRenta < 12000;
             nRdto = (N\ 1620 - (0.405 * (nTotRenta - 8000)));
         |FINSI;
     |FINSI;

     |SI #rem01010@#22 > 3700;
         nRdto = 0;
     |SINO;
         nSuma = #rem01010@#22 + nRdto;
         |SI nSuma > 3700;
             nRdto = 3700 - #rem01010@#22;
         |FINSI;
     |FINSI;

     nValor    = 0;
     eaCasilla = "05-0147-LIQ18"; |HAZ SacaImporte;  nValor = enImporte;
     eaCasilla = "06-0176-LIQ18"; |HAZ SacaImporte;  nValor = nValor + enImporte;
     eaCasilla = "07-0204-LIQ18"; |HAZ SacaImporte;  nValor = nValor + enImporte;

     |SI nValor < 0;  |ACABA;  |FINSI;

     eaCasilla = "05-0147-LIQ18"; |HAZ SacaImporte;  nValor = enImporte;
     |SI nValor > 0;
         enImporte = ((nRdto / #rem01018@#30) * nValor) ! 2;
         |SI enImporte > nValor;
             enImporte = nValor;
         |FINSI;

         |SI enImporte < 0;  enImporte = 0;  |FINSI;

         enSwAcum  = 0;
         eaCasilla = "05-0149-LIQ18";
         |HAZ GrabaLineal;
     |FINSI;

     eaCasilla = "06-0176-LIQ18"; |HAZ SacaImporte;  nValor = enImporte;
     |SI nValor > 0;
         enImporte = ((nRdto / #rem01018@#30) * nValor) ! 2;
         |SI enImporte > nValor;
             enImporte = nValor;
         |FINSI;

         |SI enImporte < 0;  enImporte = 0;  |FINSI;

         enSwAcum  = 0;
         eaCasilla = "06-0177-LIQ18";
         |HAZ GrabaLineal;
     |FINSI;

     eaCasilla = "07-0204-LIQ18"; |HAZ SacaImporte;  nValor = enImporte;
     |SI nValor > 0;
         enImporte = ((nRdto / #rem01018@#30) * nValor) ! 2;
         |SI enImporte > nValor;
             enImporte = nValor;
         |FINSI;

         |SI enImporte < 0;  enImporte = 0;  |FINSI;

         enSwAcum  = 0;
         eaCasilla = "07-0205-LIQ18";
         |HAZ GrabaLineal;
     |FINSI;
|FPRC;

|PROCESO Ded_32_3;
     |SI #rem01018@#29 ! "S";  |ACABA;  |FINSI;

     eaCasilla = "05-0147-LIQ18"; |HAZ SacaImporte;
     |SI enConta = 1;  enVal0134Tit = enImporte;  |FINSI;
     |SI enConta = 2;  enVal0134Con = enImporte;  |FINSI;

     |SI enConta > 6;
         nValor = enVal0134Tit + enVal0134Con;
     |SINO;
         nValor = enImporte;
     |FINSI;

     eaCasilla = "05-0148-LIQ18"; |HAZ SacaImporte;  nValor = nValor - enImporte;
     eaCasilla = "05-0149-LIQ18"; |HAZ SacaImporte;  nValor = nValor - enImporte;

     enImporte = 0;
     |SI nValor > 0;
         enImporte = nValor % 20;
         |SI enImporte > 20000;
             enImporte = 20000;
         |FINSI;
     |FINSI;

     enSwAcum  = 0;
     eaCasilla = "05-0150-LIQ18";
     |HAZ GrabaLineal;
|FPRC;

|PROCESO TotActividad;
     eaCasilla = "05-0147-LIQ18"; |HAZ SacaImporte; nValor = enImporte;
     eaCasilla = "05-0148-LIQ18"; |HAZ SacaImporte; nValor = nValor - enImporte;
     eaCasilla = "05-0149-LIQ18"; |HAZ SacaImporte; nValor = nValor - enImporte;
     eaCasilla = "05-0150-LIQ18"; |HAZ SacaImporte; nValor = nValor - enImporte;

     eaCasilla = "05-0147-LIQ18"; |HAZ SacaImporte;
     |SI enImporte ] 0;
         |SI nValor < 0;  nValor = 0;   |FINSI;
     |FINSI;

     enSwAcum  = 0;
     eaCasilla = "05-0155-LIQ18";
     enImporte = nValor;
     |HAZ GrabaLineal;

     eaCasilla = "06-0176-LIQ18"; |HAZ SacaImporte; nValor = enImporte;
     eaCasilla = "06-0177-LIQ18"; |HAZ SacaImporte; nValor = nValor - enImporte;

     eaCasilla = "06-0176-LIQ18"; |HAZ SacaImporte;
     |SI enImporte ] 0;
         |SI nValor < 0;  nValor = 0;   |FINSI;
     |FINSI;

     enSwAcum  = 0;
     eaCasilla = "06-0180-LIQ18";
     enImporte = nValor;
     |HAZ GrabaLineal;

     eaCasilla = "07-0204-LIQ18"; |HAZ SacaImporte; nValor = enImporte;
     eaCasilla = "07-0205-LIQ18"; |HAZ SacaImporte; nValor = nValor - enImporte;

     eaCasilla = "07-0204-LIQ18"; |HAZ SacaImporte;
     |SI enImporte ] 0;
         |SI nValor < 0;  nValor = 0;   |FINSI;
     |FINSI;

     enSwAcum  = 0;
     eaCasilla = "07-0206-LIQ18";
     enImporte = nValor;
     |HAZ GrabaLineal;
|FPRC;

|PROCESO rem01022;
     |SI nVal0135 = 0;
         nRdto = 0;
         |SI nTotRenta < 8000;
             nRdto = 1620;
         |SINO;
             |SI nTotRenta < 12000;
                 nRdto = (N\ 1620 - (0.405 * (nTotRenta - 8000)));
             |FINSI;
         |FINSI;

         |SI #rem01010@#22 > 3700;
             nRdto = 0;
         |SINO;
             nSuma = #rem01010@#22 + nRdto;
             |SI nSuma > 3700;
                 nRdto = 3700 - #rem01010@#22;
             |FINSI;
         |FINSI;

         nValor = #rem01022@#21;
         |SI nValor > 0;
             enImporte = ((nRdto / #rem01018@#30) * nValor) ! 2;
             |SI enImporte > nValor;
                 enImporte = nValor;
             |FINSI;

             |SI enImporte < 0;  enImporte = 0;  |FINSI;

             #rem01022@#39 = enImporte;
             #rem01022@#21 = (N\ #rem01022@#18 - #rem01022@#19 - #rem01022@#37);
             #rem01022@#21 = (N\ #rem01022@#21 - #rem01022@#38 - #rem01022@#39);
         |FINSI;
     |FINSI;

     |SI #rem01022@#36 = "S";
         #rem01022@#40 = (N\ (#rem01022@#21 % 20));
     |FINSI;

     #rem01022@#21 = (N\ #rem01022@#18 - #rem01022@#19 - #rem01022@#37);
     #rem01022@#21 = (N\ #rem01022@#21 - #rem01022@#38 - #rem01022@#39);
     #rem01022@#21 = (N\ #rem01022@#21 - #rem01022@#40);

     |GRABA 020#rem01022@.a;
     |LIBERA #rem01022@;

     #rem01026@#0 = #refere1@#0;
     #rem01026@#1 = #refere1@#1;
     #rem01026@#2 = #refere1@#2;
     |LEE 101#rem01026@.=;
     |SI FSalida ! 0;
         |DDEFECTO #rem01026@;
         #rem01026@#0 = #refere1@#0;
         #rem01026@#1 = #refere1@#1;
         #rem01026@#2 = #refere1@#2;
         |GRABA 020#rem01026@.b;
     |FINSI;



     #rem01026@#7   = (N\ #rem01026@#7 + #rem01022@#21);
     |GRABA 020#rem01026@.a;
     |LIBERA #rem01026@;

     enSwAcum  = 0;
     eaCasilla = "08-0249-LIQ18";  enImporte = #rem01026@#7;   |HAZ GrabaLineal;
|FPRC;

|DEFBUCLE rem01022;
     #rem01022@#1004;
     ;
     #refere1@#0, #refere1@#1, #refere1@#2, 001;
     #refere1@#0, #refere1@#1, #refere1@#2, 999;
     be;
     NULL, rem01022;
|FIN;

|PROCESO Ded_32_2_3_Comuneros;
     eaCasilla = "05-0148-LIQ18"; |HAZ SacaImporte;
     nVal0135  = enImporte;

     aDef    = eaPathDef + "rem01022.mas";
     |CARGA_DEF aDef, rem01022@;
     |SI FSalida ! 0;
         aAlfa = "     Error en cargar el Def " + aDef;
         |MENAV aAlfa;
         |ACABA;
     |FINSI;

     |SI enNomeDat = 1;  |NOME_DAT #rem01022@#"zdat1022"#;  |FINSI;

     aDef    = eaPathDef + "rem01026.mas";
     |CARGA_DEF aDef, rem01026@;
     |SI FSalida ! 0;
         |DESCARGA_DEF #rem01022@;

         aAlfa = "     Error en cargar el Def " + aDef;
         |MENAV aAlfa;
         |ACABA;
     |FINSI;

     |SI enNomeDat = 1;  |NOME_DAT #rem01026@#"zdat1026"#;  |FINSI;

     |ABRE #rem01026@;
     |BUCLE rem01022;
     |CIERRA #rem01026@;

     |DESCARGA_DEF #rem01026@;
     |DESCARGA_DEF #rem01022@;
|FPRC;

|PROCESO Calc2017;
     |SI #rem01010@#17 [ 14450; |Y nTotRenta < 6500.01;
         |SI #rem01010@#17 < 11250;
             nRdto2017 = 3700;
         |SINO;
             |SI #rem01010@#17 ] 11250;  |Y #rem01010@#17 [ 14450;
                 nRdto2017 = (3700 - (1.15625 * (#rem01010@#17 - 11250))) ! 2;
             |FINSI;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO Calc2018;
     |SI #rem01010@#17 [ 16825; |Y nTotRenta < 6500.01;
         |SI #rem01010@#17 < 13115;
             nRdto2018 = 5565;
         |SINO;
             |SI #rem01010@#17 ] 13115;  |Y #rem01010@#17 [ 16825;
                 nRdto2018 = (5565 - (1.5 * (#rem01010@#17 - 13115))) ! 2;
             |FINSI;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO CalculaA;
     aDef    = eaPathDef + "rem01010.mas";
     |CARGA_DEF aDef, rem01010@;
     |SI FSalida ! 0;
         aAlfa = "     Error en cargar el Def " + aDef;
         |MENAV aAlfa;
         |ACABA;
     |FINSI;

     |SI enNomeDat = 1;  |NOME_DAT #rem01010@#"zdat1010"#;  |FINSI;

     aDef    = eaPathDef + "rem01033.mas";
     |CARGA_DEF aDef, rem01033@;
     |SI FSalida ! 0;
         aAlfa = "     Error en cargar el Def " + aDef;
         |MENAV aAlfa;
         |DESCARGA_DEF #rem01010@;
         |ACABA;
     |FINSI;

     |SI enNomeDat = 1;  |NOME_DAT #rem01033@#"zdat1033"#;  |FINSI;

     aDef    = eaPathDef + "rem01018.mas";
     |CARGA_DEF aDef, rem01018@;
     |SI FSalida ! 0;
         aAlfa = "     Error en cargar el Def " + aDef;
         |MENAV aAlfa;
         |DESCARGA_DEF #rem01033@;
         |DESCARGA_DEF #rem01010@;
         |ACABA;
     |FINSI;

     |SI enNomeDat = 1;  |NOME_DAT #rem01018@#"zdat1018"#;  |FINSI;

     |ABRE #rew99999;
     |SELECT #2#rew99999;
     #rew99999#1 = #refere1@#2;
     |LEE 040#rew99999.=;
     |SI FSalida  ! 0;  |DDEFECTO #rew99999;  |FINSI;
     |CIERRA #rew99999;

     |ABRE #rem01010@;
     #rem01010@#0 = #refere1@#0;
     #rem01010@#1 = #refere1@#1;
     #rem01010@#2 = #refere1@#2;
     |LEE 040#rem01010@.=;
     |SI FSalida ! 0;
         |DDEFECTO #rem01010@;
         #rem01010@#0 = #refere1@#0;
         #rem01010@#1 = #refere1@#1;
         #rem01010@#2 = #refere1@#2;
         |GRABA 020#rem01010@.b;
     |FINSI;

     |ABRE #rem01033@;
     #rem01033@#0 = #refere1@#0;
     #rem01033@#1 = #refere1@#1;
     #rem01033@#2 = #refere1@#2;
     |LEE 000#rem01033@.=;
     |SI FSalida ! 0;  |DDEFECTO #rem01033@;  |FINSI;
     |CIERRA #rem01033@;

     |ABRE #rem01018@;
     |DDEFECTO #rem01018@;
     #rem01018@#0 = #refere1@#0;
     #rem01018@#1 = #refere1@#1;
     #rem01018@#2 = #refere1@#2;
     |LEE 101#rem01018@.=;
     |SI FSalida = 0;
         |SI #rem01018@#3 < 0;  #rem01018@#3 = 0;  |FINSI;
         |SI #rem01018@#4 < 0;  #rem01018@#4 = 0;  |FINSI;
         |SI #rem01018@#5 < 0;  #rem01018@#5 = 0;  |FINSI;
         |SI #rem01018@#6 < 0;  #rem01018@#6 = 0;  |FINSI;

         #rem01018@#30 = #rem01018@#3 + #rem01018@#4 + #rem01018@#5 + #rem01018@#6;

         |GRABA 020#rem01018@.a;
         |LIBERA #rem01018@;
     |FINSI;
     |CIERRA #rem01018@;

     nTotRenta = 0;
     eaCasilla = "02-0038-LIQ18"; |HAZ SacaImporte; nTotRenta = nTotRenta + enImporte;
     eaCasilla = "03-0054-LIQ18"; |HAZ SacaImporte; nTotRenta = nTotRenta + enImporte;
     eaCasilla = "04-0094-LIQ18"; |HAZ SacaImporte; nTotRenta = nTotRenta + enImporte;
     eaCasilla = "08-0246-LIQ18"; |HAZ SacaImporte; nTotRenta = nTotRenta + enImporte;
     eaCasilla = "08-0247-LIQ18"; |HAZ SacaImporte; nTotRenta = nTotRenta + enImporte;
     eaCasilla = "08-0250-LIQ18"; |HAZ SacaImporte; nTotRenta = nTotRenta + enImporte;
     eaCasilla = "08-0251-LIQ18"; |HAZ SacaImporte; nTotRenta = nTotRenta - enImporte;
     eaCasilla = "08-0253-LIQ18"; |HAZ SacaImporte; nTotRenta = nTotRenta - enImporte;
     eaCasilla = "09-0265-LIQ18"; |HAZ SacaImporte; nTotRenta = nTotRenta + enImporte;
     eaCasilla = "10-0270-LIQ18"; |HAZ SacaImporte; nTotRenta = nTotRenta + enImporte;
     eaCasilla = "11-0275-LIQ18"; |HAZ SacaImporte; nTotRenta = nTotRenta + enImporte;
     eaCasilla = "12-0280-LIQ18"; |HAZ SacaImporte; nTotRenta = nTotRenta + enImporte;
     eaCasilla = "24-0418-LIQ18"; |HAZ SacaImporte; nTotRenta = nTotRenta + enImporte;
     eaCasilla = "24-0419-LIQ18"; |HAZ SacaImporte; nTotRenta = nTotRenta - enImporte;
     eaCasilla = "25-0422-LIQ18"; |HAZ SacaImporte; nTotRenta = nTotRenta + enImporte;
     eaCasilla = "25-0423-LIQ18"; |HAZ SacaImporte; nTotRenta = nTotRenta - enImporte;

     nTotRenta    = nTotRenta + #rem01018@#14;   || Suma de mayor de 65 y 68
     nTotRenta    = nTotRenta + #rem01018@#7;    || 0131
     nTotRenta    = nTotRenta + #rem01018@#11;   || 0158
     nTotRenta    = nTotRenta + #rem01018@#13;   || 0187
     nTotRenta    = nTotRenta + #rem01018@#8;    || 0199 + 0205 + 0211
     nTotRenta    = nTotRenta + #rem01018@#9;    || 0220

     |SI enConta = 3;  enRentas3 = enRentas3 + #rem01010@#17; |FINSI;
     |SI enConta = 4;  enRentas4 = enRentas4 + #rem01010@#17; |FINSI;
     |SI enConta = 5;  enRentas5 = enRentas5 + #rem01010@#17; |FINSI;
     |SI enConta = 6;  enRentas6 = enRentas6 + #rem01010@#17; |FINSI;

     nRdto     = 0;
     nRdto2017 = 0;
     nRdto2018 = 0;
     |SI #rem01010@#17 > 0;
         |HAZ Calc2017;
         |HAZ Calc2018;

         nRdto = ((nRdto2018 - nRdto2017) / 2) + nRdto2017;
         |SI enConta = 1;  |O enConta = 2;
             |SI enConta = 1;  |Y eaMuerto = "T";
                 |SI efFechaMuerto [ "04.07.2018";
                     nRdto = nRdto2017;
                 |FINSI;
             |FINSI;

             |SI enConta = 2;  |Y eaMuerto = "C";
                 |SI efFechaMuerto [ "04.07.2018";
                     nRdto = nRdto2017;
                 |FINSI;
             |FINSI;
         |FINSI;
     |FINSI;

     nRdtoMov = 0;
     |SI #rem01010@#17 > 0;
         nRdtoMov = 2652;

         |SI #rem01010@#17 [ 13260; |Y nTotRenta < 6500.01;
             |SI #rem01010@#17 < 9180;
                 nRdtoMov = 4080;
             |SINO;
                 |SI #rem01010@#17 ] 9180;  |Y #rem01010@#17 [ 13260;
                     nRdtoMov = (4080 - (0.35 * (#rem01010@#17 - 9180))) ! 2;
                 |FINSI;
             |FINSI;
         |FINSI;
     |FINSI;

     enCam22      = nRdto;
     #rem01010@#18 = 2000;

     eaCasilla = "01-0018-LIQ18"; |HAZ SacaImporte;
     |SI enImporte < 0;
         #rem01010@#18 = 0;
         #rem01010@#19 = 0;
         #rem01010@#20 = 0;
     |SINO;
         nSuma = enImporte;
         |SI #rem01010@#18 ] nSuma;
             #rem01010@#18 = nSuma;
             nSuma = 0;
         |SINO;
             nSuma = nSuma - #rem01010@#18;
         |FINSI;

         |SI nSuma > 0;
             |SI #rem01010@#19 ] nSuma;
                 #rem01010@#19 = nSuma;
                 nSuma = 0;
             |SINO;
                 nSuma = nSuma - #rem01010@#19;
             |FINSI;
         |SINO;
             #rem01010@#19 = 0;
         |FINSI;

         |SI nSuma > 0;
             |SI #rem01010@#20 ] nSuma;
                 #rem01010@#20 = nSuma;
                 nSuma = 0;
             |SINO;
                 nSuma = nSuma - #rem01010@#20;
             |FINSI;
         |SINO;
             #rem01010@#20 = 0;
         |FINSI;
     |FINSI;

     #rem01010@#21 = (N\ #rem01010@#17 - #rem01010@#18 - #rem01010@#19 - #rem01010@#20);

     |SI enCam22 > #rem01010@#21;
         enCam22 = #rem01010@#21;
     |FINSI;

     #rem01010@#22 = enCam22;
     |SI #rem01010@#22 < 0;  #rem01010@#22 = 0;  |FINSI;

     |SI #rem01010@#21 < 0;
         #rem01010@#9  = 0;
         #rem01010@#22 = 0;
     |SINO;
         nSuma  = #rem01010@#21 + #rem01010@#9;

         |SI #rem01010@#22 ] nSuma;
             #rem01010@#22 = nSuma;
             nSuma = 0;
         |SINO;
             nSuma = nSuma - #rem01010@#22;
         |FINSI;
     |FINSI;

     #rem01010@#24 = (N\ #rem01010@#21 - #rem01010@#22);
     |SI #rem01010@#24 < 0;  |Y #rem01010@#21 ] 0;
         #rem01010@#24 = 0;
     |FINSI;

     |GRABA 020#rem01010@.a;
     |LIBERA #rem01010@;
     |CIERRA #rem01010@;

     enSwAcum  = 0;
     eaCasilla = "01-0019-LIQ18";  enImporte = #rem01010@#18;  |HAZ GrabaLineal;
     eaCasilla = "01-0020-LIQ18";  enImporte = #rem01010@#19;  |HAZ GrabaLineal;
     eaCasilla = "01-0021-LIQ18";  enImporte = #rem01010@#20;  |HAZ GrabaLineal;
     eaCasilla = "01-0022-LIQ18";  enImporte = #rem01010@#21;  |HAZ GrabaLineal;
     eaCasilla = "01-0023-LIQ18";  enImporte = #rem01010@#22;  |HAZ GrabaLineal;
     eaCasilla = "01-0025-LIQ18";  enImporte = #rem01010@#24;  |HAZ GrabaLineal;

     eaCasilla = "01-0022-LIQ18"; |HAZ SacaImporte; nTotRenta = nTotRenta + enImporte;

     |HAZ Ded_32_2_1;
     |HAZ Ded_32_2_3;
     |HAZ Ded_32_3;
     |HAZ TotActividad;
     |HAZ Ded_32_2_3_Comuneros;

     |DESCARGA_DEF #rem01018@;
     |DESCARGA_DEF #rem01033@;
     |DESCARGA_DEF #rem01010@;
|FPRC;
