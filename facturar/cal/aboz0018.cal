|FICHEROS;
     aboz0018;
     dsam0121;
     abom0514;
|FIN;

|VARIABLES;
     aRuta          = "";
     aBase          = "";
     aOri           = "";
     aDes           = "";
     aExtO          = "";
     aExtD          = "";
     aAlfa          = "";
     aAlfa1         = "";
     aAlfa2         = "";
     aAlfa3         = "";
     aLong          = "";
     aBaseDoc       = "";
     aPathPlantilla = "";
     aCaracter      = "";
     aAbre          = "";
     aCarac         = "";
     aPunto         = "";
     aBaseRemoto    = "";

     nLong          = 0;
     nCarac         = 0;
     nPos           = 0;
     nHandle        = 0;
     nAscii         = 0;

     {1}aContiene   = "";


     &eaParametro1   = "";
     &eaParametro2   = "";
     &eaOrigen       = "";
     &eaDestino      = "";
     &eaRuta         = "";
     &eaNomFic       = "";
|FIN;

|PROCESO DsSelect;
     || eaParametro1 = Path y fichero exe
     || eaParametro2 = Path y fichero a editar
     || eaParametro3 = Origen  del dsselect
     || eaParametro4 = Destino 1 del dsselect
     || eaParametro5 = Destino 2 del dsselect

     aAlfa = eaParametro2;  |QBF aAlfa;

     eaOrigen       = eaParametro1 + "dsselect.exe";
     eaDestino      = eaParametro2 + "dsselect.exe";
     |HAZ EnviaFicheroConMD5;

     aAlfa = eaParametro2 + "dsselect.txt";
     |RFBORRA aAlfa;

     aAlfa1 = eaParametro2 % 103;

     aAlfa  = "cmd " + &34 + "/c START /WAIT " + eaParametro2 + "dsselect.exe " + aAlfa1 + " " + eaParametro2 + "dsselect.txt" + &34;
     |RSYSTEM aAlfa;

     eaRuta   = "";
     eaNomFic = "";

     aAbre = eaParametro2 + "dsselect.txt";
     |XABRE "CU", aAbre, nHandle;
     |SI FSalida < 0;   |ACABA;  |FINSI;

     |XLEE nHandle, 250, eaRuta;
     |SI FSalida < 0
         eaRuta   = "";
         |XCIERRA nHandle;
         |ACABA;
     |FINSI;

     |XCIERRA nHandle;

     |QBF eaRuta;
     |SI eaRuta = "";  |ACABA;  |FINSI;

     aAlfa    = eaRuta;
     aLong    = aAlfa % 0;
     nLong    = aLong;
     |PARA nCarac = 1;  |SI nCarac [ nLong;  |HACIENDO nCarac = nCarac + 1;
           nPos   = (nCarac * 100) + 1;
           aCarac =  aAlfa % nPos;

           nAscii = &aCarac;

           |SI nAscii = 241;  aCarac = "¤";  |FINSI;
           |SI nAscii = 225;  aCarac = " ";  |FINSI;
           |SI nAscii = 233;  aCarac = "‚";  |FINSI;
           |SI nAscii = 237;  aCarac = "¡";  |FINSI;
           |SI nAscii = 243;  aCarac = "¢";  |FINSI;
           |SI nAscii = 250;  aCarac = "£";  |FINSI;

           |SI nAscii = 209;  aCarac = "¥";  |FINSI;
           |SI nAscii = 193;  aCarac = "A";  |FINSI;
           |SI nAscii = 201;  aCarac = "E";  |FINSI;
           |SI nAscii = 205;  aCarac = "I";  |FINSI;
           |SI nAscii = 211;  aCarac = "O";  |FINSI;
           |SI nAscii = 218;  aCarac = "U";  |FINSI;

           |SI aCarac = "/";  |O aCarac = "\";
               eaNomFic = "";
           |SINO;
               eaNomFic = eaNomFic + aCarac;
           |FINSI;
     |SIGUE;

     aAlfa = eaParametro2 + "dsselect.txt";
     |RFBORRA aAlfa;
|FPRC;

|PROCESO Tipo0C2;  |TIPO 0;
     |ABRE #dsam0121;
     #dsam0121#0 = #aboz0018#0;
     |LEE 000#dsam0121.=;
     |SI FSalida ! 0;  |DDEFECTO #dsam0121;  |FINSI;
     |CIERRA #dsam0121;

     aContiene = "";
     |ESPECIFICA "RBASS", aContiene;
     aBaseRemoto = aContiene + "bin/";

     |DBASE aBase;  |QBF aBase;
     eaParametro1 = aBase + "plantillas/";
     eaParametro2 = aBaseRemoto;

     |HAZ DsSelect;

     |SI eaRuta = "";  |ACABA;  |FINSI;

     aOri  = eaRuta;

     #aboz0018#2 = eaNomFic;  |PINTA #aboz0018#2;
|FPRC;

|PROCESO Tipo12;  |TIPO 12;
     |SI aOri = "";  |ACABA;  |FINSI;

     |CONFI "0000SImportamos el fichero";
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |DBASE aBaseDoc;  |QBF aBaseDoc;
     aPathPlantilla = aBaseDoc + "ipabogad";
     |MKDIR aPathPlantilla;

     aPathPlantilla = aPathPlantilla + "/documentos";
     |MKDIR aPathPlantilla;

     aPathPlantilla = aPathPlantilla + "/" + (("00" + #48#0) % -102);
     |MKDIR aPathPlantilla;

     aPathPlantilla = aPathPlantilla + "/" + (("0000" + #dsam0121#0) % -104);
     |MKDIR aPathPlantilla;

     aDes = aPathPlantilla + "/" + #aboz0018#2;    |QBF aDes;
     |RECIBE_FICHERO aOri, aDes;
     |SI FSalida ] 0;
         aAlfa = "0000Fichero copiado en la carpeta: " + (("0000" + #dsam0121#0) % -104);
         aAlfa = aAlfa + " de la empresa: " + #48#0;
     |SINO;
         aAlfa = "0000No se ha podido copir el documento.";
     |FINSI;

     |MENAV aAlfa;
|FPRC;

|PROCESO Tipo80;  |TIPO 80;
     |ABRE #abom0514;
     |LEE 000#abom0514.p;
     |SI FSalida ! 0;  |DDEFECTO #abom0514;  |FINSI;
     |CIERRA #abom0514;

     |SI #abom0514#21 ! "C";
         |MENAV "0000 Tiene la opcion de DISTRIBUIDA, el pase no procede.";
         |PTEC 807;
         |ACABA;
     |FINSI;
|FPRC;
