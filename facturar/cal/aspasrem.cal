|FICHEROS;
   aspasrem #0;
   ashisrec #1;
   ascabrem #2;
   aslinrem #3;
   asclient #5;
   asparcia #6;
   ashismic #90;
   asw00005 #99;

   &bancosp@ #706;

   asempcon;
   :/contagen/def/canempre;
   :/contagen/def/cacuenta;
|FIN

|VARIABLES;
    men1       = "    YA EXISTE NUMERO DE REMESA    ";
    men2       = "    NO EXISTE EL REGISTRO DEL BANCO  ";
    men3       = "    NO EXISTE EL REGISTRO DEL CLIENTE ";
    importe    = 0;
    nombanc    = "";
    mes        = "";
    mes_num    = 0;
    campo      = 0;
    linea      = 0;
    he_grabado = 0;
    mas_uno    = 0;
    fFech      = @;
    qFech      = "";
    dFech      = "";
    hFech      = "";
    aImprimir  = "";
    nCalc      = 0;
    nSinConta  = 0;
    nInkey     = 0;

    &enCodBanco     = 0;
    &enSwDeDonde    = 0;
    &eaPathContagen = "";
|FIN;

|INCLUYE i_ficher;
|INCLUYE i_varcof;

|CALCULO Tipo8Cuen; |TIPO 88;
   |HAZ Tipo88;

   |ABRE #asempcon;
   |DDEFECTO #asempcon;
   |LEE 001#asempcon.p;
   |CIERRA #asempcon;
   nSinConta = 0;

   |HAZ BusquedaApli;
   |SI eaPathContagen ! "";
       enEmpresa  = #asempcon#1;
       enPerConta = #asempcon#2;
       enSwSinMen = 1;
       |HAZ LeeEmpConta;
       |SI swerror = 1;
           |MENAV "     Empresa Contable Mal Informada ";
           nSinConta = 1;
       |SINO;
           |PATH_DAT #cacuenta#aPathConta#;
       |FINSI;
   |SINO;
       nSinConta = 1;
   |FINSI;
|FCAL;

|PROCESO funcion1; |TIPO 11;
   |SI FSalida ! 10; |ACABA; |FINSI;

   |LIBERA #99;
   |LEE 140#99=;

   |SI #99#11 = "S"; #99#11 = "N"; |SINO; #99#11 = "S"; |FINSI;
   |PINTA #99#11;
   |GRABA 140#99a;
   |PTEC 809;
|FPRC;

||---------------------------------------------------------------------------

|PROCESO lee_ultimo; |TIPO 7;
     |ABRE #2;
     |LEE 000#2u;
     |SI FSalida = 0;
          #0#0 = #2#0 + 1;
     |SINO;
          |DDEFECTO #0;
     |FINSI;
     |CIERRA #2;
|FPRC;

|PROCESO sacaano; |TIPO 7;
     fFech = ~;
     qFech = fFech % -104;
     dFech = "01.01." + qFech;
     hFech = "31.12." + qFech;
     #0#7  = dFech;
     #0#8  = hFech;
     #0#9  = dFech;
     #0#10 = hFech;
     |PINTA #0#7;
     |PINTA #0#8;
     |PINTA #0#9;
     |PINTA #0#10;
|FPRC;

|PROCESO clave1;|TIPO 0;
     |ABRE #2;
     #2#0 = #0#0;
     |LEE 040#2=;
     |SI FSalida = 0;
         |MENAV men1;
         |ERROR;
     |FINSI;
     |CIERRA #2;
|FPRC;

|RUTINA inferior;
     #99#0 = 1;
|FRUT;

|RUTINA superior;
    #99#0 = 999;
|FRUT;

|PROCESO BancoPropio; |TIPO 0;
     enCodBanco = #0#1;
     |HAZ LeeBancoPropio;
     |SI enSwDeDonde = 1;
         nombanc = #706#1;
         #0#14   = #706#11;
     |SINO;
         nombanc = #706#1;
         #0#14   = #706#23;
     |FINSI;
     |PINTA #0#14;
|FPRC;

|PROCESO DesmarcaRecs;
   #99#11 = "N";
   |GRABA 020#99a;
   |LIBERA #99;
|FPRC;

|DEFBUCLE DesmarcaRecs;
   #99#2;
   ;
   #1#3;
   #1#3;
   be;
   NULL, DesmarcaRecs;
|FIN;

|PROCESO ashisrec;
     #99#10 = #1#6 - #1#9;
     |SI #99#10 = 0; |ACABA; |FINSI;

     #0#15 = #0#15 + #99#10;

     |DDEFECTO #99;
     #5#0 = #1#3;
     |LEE 040#5=;
     |SI FSalida ! 0; |DDEFECTO #5; |FINSI;

     enCodBanco = #1#7;
     |HAZ LeeBancoPropio;

     linea  = linea + 1;
     #99#0  = linea;
     #99#1  = #1#0;
     #99#2  = #1#1;
     #99#3  = #1#3;
     #99#4  = #5#2;
     #99#5  = #1#7;
     #99#6  = #706#1;
     #99#7  = #1#2;
     #99#8  = #1#5;
     #99#9  = #1#6;
     #99#10 = #1#6 - #1#9;
     |SI #aspasrem#18 = "S"; |Y #99#10 < 0;
        #99#11 = "N";
     |SINO;
        #99#11 = #0#13;
     |FINSI;

     |SI #99#3 = 0;
         |ABRE #90;
         #90#0 = #1#0;
         #90#1 = #1#1;
         |LEE 000#90=;
         |SI FSalida ! 0;  |DDEFECTO #90;  |FINSI;
         |CIERRA #90;

         #99#4 = #ashismic#4;
     |FINSI;

     |GRABA 020#99n;
     |LIBERA #99;

     nCalc = #1#6 - #1#9;
     |SI #aspasrem#18 = "S"; |Y nCalc < 0;
        || Compruebo si hay otros recibos del cliente marcados para desmarcarlos
        |BUCLE DesmarcaRecs;
     |FINSI;
|FPRC;

|DEFBUCLE ashisrec;
     #1#1;
     2, 5, 7;
     #0#3, #0#5, #0#7, #0#9,  #0#11;
     #0#4, #0#6, #0#8, #0#10, #0#12;
     ;
     NULL, ashisrec;
|FIN;

|PROCESO Cobro;
     #6#0 = #1#0;
     #6#1 = #1#1;
     #6#2 = 999;
     |LEE 040#6];
     |SI FSalida = 0;
         |LEE 040#6a;
     |SINO;
         |LEE 040#6u;
     |FINSI;
     mas_uno = 1;
     |SI FSalida = 0;  |Y #6#0 = #1#0;  |Y #6#1 = #1#1;
         mas_uno = #6#2 + 1;
     |FINSI;

     |DDEFECTO #6;
     #6#0  = #1#0;
     #6#1  = #1#1;
     #6#2  = mas_uno;
     #6#3  = #0#2;
     #6#4  = #1#5;
     #6#5  = #1#6;
     #6#6  = #1#9;
     #6#7  = #1#6 - #1#9;
     #6#8  = #99#10;
     #6#9  = #1#3;
     #6#13 = #0#14;
     #6#14 = #3#0;
     #6#15 = #3#1;
     |GRABA 020#6n;
     |LIBERA #6;
|FPRC;

|PROCESO asw00005;
     #5#0 = #99#3;
     |LEE 040#5=;
     |SI FSalida ! 0; |DDEFECTO #5; |FINSI;

     #1#0 = #99#1;
     #1#1 = #99#2;
     |LIBERA #1;
     |LEE 140#1=;
     |SI FSalida ! 0; |ACABA; |FINSI;

     he_grabado = 1;
     linea      = linea + 1;

     |DDEFECTO #3;
     #3#0    = #0#0;
     #3#1    = linea;
     #3#2    = #1#0;
     #3#3    = #1#1;
     #3#4    = #1#3;
     #3#6    = #5#6;
     #1#15   = #99#10;
     #3#7    = #1#15;
     #3#8    = #1#2;
     #3#9    = #1#5;
     #3#10   = #5#77;
     #3#11   = #5#84;
     #3#12   = #5#76;
     #3#13   = #5#85;
     #3#14   = #5#83;
     #3#5    = #5#2;

     #0#16   = #0#16 + #1#15;
     #99#12  = #1#15;
     |GRABA 020#99a;
     |LIBERA #99;

     #1#8  = #0#2;
     #1#9  = #1#9 + #1#15;
     #1#10 = "S";
     #1#12 = "R";
     |GRABA 021#1a;
     |LIBERA #1;

     |ABRE #90;
     #90#0 = #1#0;
     #90#1 = #1#1;
     |LIBERA #90;
     |LEE 140#90=;
     |SI FSalida = 0;
         #90#32 = #1#10;
         #90#33 = #1#12;
         |GRABA 140#90a;
     |FINSI;
     |CIERRA #90;

     |SI #3#4 = 0;
         #3#5  = #ashismic#4;
||         #3#10 = "";                Tique 2815_74
||         #3#11 = "01.01.0000";
||         #3#12 = "";
||         #3#13 = "R";
||         #3#14 = "";
     |FINSI;

     |GRABA 020#3n;
     |LIBERA #3;

     mes     = #90#29 % 402;
     mes_num = mes;

     enCodBanco = #1#7;
     |HAZ LeeBancoPropio;
     |SI enSwDeDonde = 0;
         #706#0 = #1#7;
         |LEE 101#706=;
         |SI FSalida = 0;
             #706#20 = #706#20 - #1#15;
             #706#22 = #706#22 - #1#15;
             campo = 7 + mes_num;
             #706campo = #706campo - #1#15;
             |GRABA 020#706a;
             |LIBERA #706;
         |FINSI;

         #706#0 = #0#1;
         |LEE 140#706=;
         |SI FSalida = 0;
             #706#20   = #706#20 + #1#15;
             #706#22   = #706#22 + #1#15;
             campo = 7 + mes_num;
             #706campo = #706campo + #1#15;
             |GRABA 020#706a;
             |LIBERA #706;
         |FINSI;
     |FINSI;

     |HAZ Cobro;
|FPRC;

|DEFBUCLE asw00005;
     #99#1;
     11;
     0000, "S";
     9999, "S";
     be;
     NULL, asw00005;
|FIN;

|PROCESO asw00005I;
     |PRINT;
|FPRC;

|DEFBUCLE asw00005I;
     #99#1;
     ;
     0000;
     9999;
     be;
     NULL, asw00005I;
|FIN;

|PROCESO Impresion;
     |IMPRE 0;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |INFOR "aspasrem";
     |SI FSalida ! 0;  |FINIMP;  |ACABA;  |FINSI;

     |BUCLE asw00005I;

     |PIEINF;
     |FININF;
     |FINIMP;
|FPRC;

|RUTINA nInf2;
 |SI FSalida = "POSICION";
     #cacuenta#0 = #0#14;
 |SINO;
     #cacuenta#0 = "";
 |FINSI;
 #cacuenta#3 = "S";
|FRUT;

|RUTINA nSup2;
 #cacuenta#0 = "zzzzzzzzzzz";
 #cacuenta#3 = "S";
|FRUT;

|PROCESO LaConsCue;
  |ABRE #cacuenta;
  #cacuenta#0 = #0#14
  |CONSULTA_F_CLAVE #6#cacuenta, nInf2, nSup2;
  #0#14 = #cacuenta#0; |PINTA #0#14;
  |CIERRA #cacuenta;
|FPRC;

|CALCULO MiraCuenta; |TIPO 0;
   |SI nSinConta = 1; |ACABA; |FINSI;

   |SI FSalida = 2; |ACABA; |FINSI;
   nInkey = FSalida;

   eaCuenta = #0Campo; |QBF eaCuenta;
   |SI eaCuenta = ""; |Y nInkey ! 10;
       |MENAV "    Introduzca una Cuenta";
       |ERROR;
       |ACABA;
   |FINSI;

   |SI nInkey = 10;  |HAZ LaConsCue; |FINSI;

   emCta = 0;
   eaCuenta = #0Campo;
   |HAZ LeeCuenta;
   |SI eswCta = 1;
       |ERROR;
       |CONFI "    SError.! No existe Cuenta. Desea darla de alta ";
       |SI FSalida = 0;
           |SUB_EJECUTA_NP "aszc0001";
           #0Campo = eaCuenta;
           |PINTA #0Campo;
       |FINSI;
   |SINO;
       |SI eswCta = 2;
           |MENAV "0000Esta Cuenta NO tiene Pase Directo.";
           |ERROR;
       |FINSI;
   |FINSI;
|FCAL;

|CALCULO Cuenta; |TIPO 6;
     |SI nSinConta = 1; |ACABA; |FINSI;

     emCta = 1;
     eaCuenta = #aspasrem#Campo#;
     salidas  = FSalida;
     |HAZ FiltraPuntos;
     #aspasrem#Campo# = eaCuenta;
     |PINTA #aspasrem#Campo#;
     FSalida = salidas;

     |HAZ SacaNivel;
|FCAL;

|PROCESO proceso; |TIPO 3;
     comienza = "05";
     |HAZ fichero;
     |NOME_DAT #99 fichero;

     |ABRE #99; |CIERRA #99; |DELINDEX #99;

     he_grabado = 0;
     linea      = 0;

     |ABRE #5;
     |ABRE #99;
     |BUCLE ashisrec;
     |CIERRA #99;

     |SI linea = 0;
         |CIERRA #5;
         |ABRE #99; |CIERRA #99; |DELINDEX #99;
         |ACABA;
     |FINSI;

     |ENTLINEAL #1#99, -1, 4, 20, 1, inferior, superior;
     |CONFI "2400SDesea hacer la Remesa : ";
     |SI FSalida ! 0;
         |CIERRA #5;
         |ABRE #99; |CIERRA #99; |DELINDEX #99;
         |ACABA;
     |FINSI;

     |ABRE #1;
     |ABRE #2;
     |ABRE #3;
     |ABRE #6;

     linea      = 0;
     |BUCLE asw00005;
     |SI he_grabado = 1;
         |DDEFECTO #2;
         #2#0 = #0#0;
         #2#1 = #0#1;
         #2#2 = nombanc;
         #2#3 = #0#2;
         #2#4 = #0#16;
         |GRABA 020#2n;
         |LIBERA #2;
     |FINSI;

     |CIERRA #1;
     |CIERRA #2;
     |CIERRA #3;
     |CIERRA #5;
     |CIERRA #6;

     |BLANCO 0501 2380;
     |CUADRO 0812 2161;
     |ATRI R;
     |PINTA 0913, "                   INCIDENCIAS                  ";
     |ATRI r;

     |PINTA 1213, "   Total en Recibos ..: ";
     |PINTA 1313, "   Total en la Remesa : ";
     |PINTA 1413, "------------------------------------------------";
     |PINTA 1513, "   Diferencia ........: ";
     |PINTA 2013, "                                   Imprimir :   ";

     #0#17 = #0#15 - #0#16;

     |PINTA 1237, #0#15;
     |PINTA 1337, #0#16;
     |PINTA 1537, #0#17;

                     aImprimir = "N";
     |SI #0#17 ! 0;  aImprimir = "S";  |FINSI;

     E_inf = "SNsn";
     E_sup = "1";

     aImprimir = 2059 ? 1;
     aImprimir = aImprimir > " ";

     |SI aImprimir = "S";
         |HAZ Impresion;
     |FINSI;

     |ABRE #99; |CIERRA #99; |DELINDEX #99;
|FPRC;

|PROCESO AntesCampos; |TIPO 7;
   |HAZ EsVisados;
   |SI FSalida = 0;
      #aspasrem#18 = "S";
   |SINO;
      #aspasrem#18 = "N";
   |FINSI;
   |PINTA #aspasrem#18;
|FPRC;
