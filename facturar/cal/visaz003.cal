|FICHEROS;
   visaz003 #0;
   visaw002 #1;
   visaw003 #2;

   ashisrec #10;
   asclient #11;

   cacuenta@ #20;

   asempcon #100;
|FIN;

|VARIABLES;
   &enSwDet = 0;

   aAlfa  = "";
   aAlfa1 = "";
|FIN;

|PROCESO ashisrec;
   #asclient#0 = #ashisrec#3;
   |LEE 000#asclient.=;
   |SI FSalida ! 0; |DDEFECTO #asclient; |FINSI;

   #cacuenta@#0 = #asclient#52;
   |LEE 000#cacuenta@.=;
   |SI FSalida ! 0; |DDEFECTO #cacuenta@; #cacuenta@#53 = 0; |FINSI;

   #visaw002#0 = #ashisrec#3;
   |LEE 101#visaw002.=;
   |SI FSalida ! 0;
      |DDEFECTO #visaw002;
      #visaw002#0 = #ashisrec#3;
      #visaw002#1 = #asclient#14;
      #visaw002#2 = #asclient#1;
      |GRABA 020#visaw002.b;
   |FINSI;
   #visaw002#3 = #cacuenta@#53;
   #visaw002#4 = #visaw002#4 + (#ashisrec#6 - #ashisrec#9);
   |GRABA 020#visaw002.a; |LIBERA #visaw002;

   |DDEFECTO #visaw003;
   #visaw003#0 = #ashisrec#3;
   #visaw003#1 = #ashisrec#8;
   #visaw003#2 = #ashisrec#2;
   #visaw003#3 = #ashisrec#5;
   #visaw003#4 = #ashisrec#8;
   #visaw003#5 = #ashisrec#0;
   #visaw003#6 = #ashisrec#1;
   #visaw003#7 = 1;
   #visaw003#8 = #ashisrec#6 - #ashisrec#9;
   #visaw003#9 = #ashisrec#6;
   |GRABA 020#visaw003.n;
|FPRC;

|DEFBUCLE ashisrec;
#ashisrec#2;
3;
#visaz003#2, "  ", 00000, #visaz003#0;
#visaz003#3, "zz", 99999, #visaz003#1;
;
NULL, ashisrec;
|FIN;

|PROCESO ImpDetalle;
   enSwDet = 1;
   |PRINT;
|FPRC;

|DEFBUCLE ImpDetalle;
#visaw003#1;
;
#visaw002#0, "     ", 000000, 000;
#visaw002#0, "zzzzz", 999999, 999;
;
NULL, ImpDetalle;
|FIN;

|PROCESO Imprime;
   |SI #visaz003#5 = 1;
      |SI #visaw002#3 = 0; |Y #visaw002#4 = 0; |ACABA; |FINSI;
   |FINSI;
   |SI #visaz003#5 = 2;
      |SI #visaw002#3 = 0; |O #visaw002#4 = 0; |ACABA; |FINSI;
   |FINSI;
   |SI #visaz003#5 = 3;
      |SI #visaw002#3 = #visaw002#4; |ACABA; |FINSI;
   |FINSI;

   enSwDet = 0;
   |PRINT; || Imprime la cabecera

   |SI #visaz003#4 = "D";
      |BUCLE ImpDetalle;
   |FINSI;
|FPRC;

|DEFBUCLE Imprime;
#visaw002#1;
;
INICIO;
FINAL;
;
NULL, Imprime;
|FIN;

|PROGRAMA;
   |ABRE #visaz003;
   |LEE 000#visaz003.p;
   |SI FSalida ! 0;
      |DDEFECTO #visaz003;
      |GRABA 000#visaz003.c;
   |FINSI;

   |ABRE #asempcon;
   |LEE 000#asempcon.p;
   |SI FSalida ! 0; |DDEFECTO #asempcon; |FINSI;
   |CIERRA #asempcon;

   |CLS;
   |CABEZA "Listado saldos pendientes";

   |PINPA #0#0; |PINDA #0#0;
   |ENDATOS #1#0;
   |SI FSalida = 0;
      |GRABA 000#visaz003.a;

      aAlfa = "t1" + Usuario; |NOME_DAT #visaw002#aAlfa#;
      aAlfa = "t2" + Usuario; |NOME_DAT #visaw003#aAlfa#;
      |DELINDEX #visaw002; |ABRE #visaw002;
      |DELINDEX #visaw003; |ABRE #visaw003;
      |ABRE #asclient;

      |DBASS aAlfa; |QBF aAlfa;
      aAlfa = aAlfa + "contagen/def/cacuenta.mas";
      |CARGA_DEF aAlfa, cacuenta@;
      |SI FSalida = 0;
         |DBASS aAlfa; |QBF aAlfa;
         aAlfa1 = (("00000" + #asempcon#1) % -105) + #asempcon#2;
         aAlfa = aAlfa + "contagen/fich/" + aAlfa1 + "/"; |PATH_DAT #cacuenta@#aAlfa#;
         |ABRE #cacuenta@;
         |BUCLE ashisrec;

         Impresora = "CrystalReports";
         |IMPRE -1;
         |SI FSalida = 0;
            |INFOR "visaz003";
            |SI FSalida = 0;
               |BUCLE Imprime;
               |PIEINF; |FININF;
            |FINSI;
            |FINIMP;
         |FINSI;
         |CIERRA #cacuenta@; |DESCARGA_DEF #cacuenta@;
      |FINSI;

      |CIERRA #asclient;
      |CIERRA #visaw003; |DELINDEX #visaw003;
      |CIERRA #visaw002; |DELINDEX #visaw002;
   |FINSI;

   |CIERRA #visaz003;
|FPRO;
