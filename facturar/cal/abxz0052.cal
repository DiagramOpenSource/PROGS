|FICHEROS;
     abxz0052 #952;
     dsam0119 #119;
     dsam0120 #120;
     abom0514 #514;
     abxw0002 #902,MANTE;
|FIN;

|VARIABLES;
     aAbre          = "";
     aBase          = "";
     aAlfa          = "";
     aAlfa1         = "";
     aAlfa2         = "";
     aEsc1          = "";
     aEsc2          = "";
     aRetu          = "";
     aTecla         = "";
     aFichero       = "";
     aCarga         = "";
     aQueQuiero     = "";
     aTexto         = "";
     aLong          = "";
     aCarac         = "";
     aRefresca      = "";
     aBusca         = "";
     aIPRemoto      = "";
     aNombreIP      = "";
     aRuta          = "";

     nBotonLabel    = 0;
     nHandle        = 0;
     nTree          = -1;
     nPanta         = 0;
     nSubPanta      = 0;
     nLong          = 0;
     nCargar        = 0;
     nPos           = 0;
     nPosi          = 0;
     nBoton1        = 0;
     nBoton2        = 0;
     nBoton3        = 0;
     nSubVent       = 0;
     nCampo         = 0;
     nTCampo        = 0;
     nCarpeta1      = 0;
     nCarpeta2      = 0;
     FEstado        = 0;
     nSubBoton1     = 0;
     nSubBoton2     = 0;

     {-1}sTree      = 0;
     sT_nID         = 0;
     sT_nOper       = 0;
     sT_aData       = "";

     {-1}aVent      = "";
         aVent1     = 0;
         aVent2     = 0;
         aVent3     = 0;
         aVent4     = 0;
         aVent5     = "";

     &eaRutaOrigen  = "";
     &eaRutaDestino = "";
     &eaFicheroTxt  = "";
     &eaFicheroTgz  = "";
     &eaFicheroTar  = "";
     &eaAlfa        = "";
     &eaUnidadBasico = "";
     &eaDSMAC        = "";
     &enCodigo       = 0;
|FIN;

|PROCESO Tipo12Fxw2;  |TIPO 12;
|FPRC;

|PROCESO Tipo0C2Fxw2;  |TIPO 0;
     |C_M #902#3, 1, "S";

     |SI #902#2 = "1";   #902#3 = "DOC";   |C_M #902#3, 1, "N";   |FINSI;
     |SI #902#2 = "2";   #902#3 = "XLS";   |C_M #902#3, 1, "N";   |FINSI;
     |SI #902#2 = "3";   #902#3 = "TXT";   |C_M #902#3, 1, "N";   |FINSI;
     |SI #902#2 = "4";
         |SI #902#3 ! "TIF";  |Y #902#3 ! "PDF";
             #902#3 = "TIF";
         |FINSI;
     |FINSI;

     |ESTADO_CONTROL nSubBoton1, "ENABLE";
     |C_M #902#5, 1, "S";
     |C_M #902#7, 1, "S";
     |SI #902#2 = "1";
         |C_M #902#5, 1, "N";
         |C_M #902#7, 1, "N";
         #902#5 = "LLAMADA INTERNA DESDE PROGRAMA";
         #902#7 = "S";
         |ESTADO_CONTROL nSubBoton1, "DISABLE";
     |FINSI;

     |SI #902#7 = "S";
         |C_M #902#5, 1, "N";
         |ESTADO_CONTROL nSubBoton1, "DISABLE";
     |FINSI;

     |C_M #902#6, 1, "S";
     |ESTADO_CONTROL nSubBoton2, "ENABLE";
     |SI #514#21 = "C";
         |C_M #902#6, 1, "N";
         |ESTADO_CONTROL nSubBoton2, "DISABLE";
     |FINSI;

     |PINTA #902#3;
     |PINTA #902#5;
     |PINTA #902#6;
|FPRC;

|PROCESO Tipo0C7Fxw2;  |TIPO 0;
     |C_M #902#5, 1, "S";
     |ESTADO_CONTROL nSubBoton1, "ENABLE";
     |SI #514#21 = "C";  |O #902#7 = "S";
         |C_M #902#5, 1, "N";
         |ESTADO_CONTROL nSubBoton1, "DISABLE";
     |FINSI;

     |PINTA #902#5;
|FPRC;

|PROCESO Browse1;
     aRuta = "F" + #902#5;
     |BROWSE aRuta;
     aAlfa = aRuta % 280;  |QBF aRuta;
     |SI aAlfa ! "";
         #902#5 = aRuta % 280;
     |FINSI;
     |PINTA #902#5;
     |ERROR;
|FPRC;

|PROCESO Browse2;
     aRuta = "F" + #902#6;
     |BROWSE aRuta;
     aAlfa = aRuta % 280;  |QBF aRuta;
     |SI aAlfa ! "";
         #902#6 = aRuta % 280;
     |FINSI;
     |PINTA #902#6;
     |ERROR;
|FPRC;

|PROCESO NuevoPrograma;
     |DDEFECTO #902;

     |LEE 000#119u;
     |SI FSalida ! 0;  |DDEFECTO #119;  |FINSI;

     |SI #119#0 > 99;
         |PARA nCampo = 1;  |SI nCampo [ 100;  |HACIENDO nCampo = nCampo + 1;
               #119#0 = nCampo;
               |LEE 000#119=;
               |SI FSalida ! 0;
                   |SAL;
               |FINSI;
         |SIGUE;
     |FINSI;

     #119#0 = #119#0 + 1;

     |SI #119#0 > 99;
         |MENAV "0000 Ya tenemos 99 programas creados, borre alguno que no use.";
         |ACABA;
     |FINSI;

     #902#0 = #119#0;
     #902#4 = aNombreIP;

     |VENTANA 0701, 2599, -1, 17, "Nuevo Programa";
     nSubVent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nSubVent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |PINPA #0#902;
     nSubPanta = FSalida;

     |CONTROL_SIMPLE nSubPanta, "BOTON,{{136}}", 2092, 2095, Browse1;
     nSubBoton1 = FSalida;

     |CONTROL_SIMPLE nSubPanta, "BOTON,{{136}}", 2392, 2395, Browse2;
     nSubBoton2 = FSalida;

     |PINDA #0#902;

     |ENDATOS #1#902;
     FEstado = FSalida;
     |SI FSalida = 0;
         #119#0 = #902#0;
         #119#1 = #902#1;
         #119#2 = #902#2;
         #119#3 = #902#3;
         #119#4 = #902#7;

         |GRABA 020#119n;
         |LIBERA #119;

         enCodigo = #119#0;
         |HAZ CambiaAMac120;

         #120#0 = #119#0;
         #120#1 = eaDSMAC;
         |LEE 101#120=;
         |SI FSalida ! 0;
             #120#0 = #119#0;
             #120#1 = eaDSMAC;
             |GRABA 020#120b;
         |FINSI;
         #120#2 = #902#4;
         #120#3 = #902#5;
         #120#4 = #902#6;
         |GRABA 020#120a;
         |LIBERA #120;
     |FINSI;

     |FIN_CONTROL_WINDOWS nSubBoton1;
     |FIN_CONTROL_WINDOWS nSubBoton2;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nSubVent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA nSubVent;

     |SI FEstado = 0;
         aBusca = #902#0;
         |HAZ MontaArbol;
     |FINSI;
|FPRC;

|PROCESO ModificarPrograma;
     #119#0 = aTexto;
     |LEE 101#119=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |DDEFECTO #902;

     #902#0  = #119#0;
     #902#1  = #119#1;
     #902#2  = #119#2;
     #902#3  = #119#3;
     #902#7  = #119#4;

     enCodigo = #119#0;
     |HAZ CambiaAMac120;

     #120#0  = #119#0;
     #120#1  = eaDSMAC;
     |LEE 000#120=;
     |SI FSalida ! 0;  |DDEFECTO #120;  |FINSI;

     #902#4  = #120#2;
     #902#5  = #120#3;
     #902#6  = #120#4;

     |VENTANA 0701, 2599, -1, 17, "Modificar programa";
     nSubVent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nSubVent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |PINPA #0#902;
     nSubPanta = FSalida;

     |CONTROL_SIMPLE nSubPanta, "BOTON,{{136}}", 2092, 2095, Browse1;
     nSubBoton1 = FSalida;

     |CONTROL_SIMPLE nSubPanta, "BOTON,{{136}}", 2392, 2395, Browse2;
     nSubBoton2 = FSalida;

     |HAZ Tipo0C2Fxw2;
     |PINDA #0#902;

     |ENDATOS #1#902;
     FEstado = FSalida;
     |SI FSalida = 0;
         #119#1 = #902#1;
         #119#2 = #902#2;
         #119#3 = #902#3;
         #119#4 = #902#7;

         |GRABA 020#119a;
         |LIBERA #119;

         enCodigo = #119#0;
         |HAZ CambiaAMac120;

         #120#0 = #119#0;
         #120#1 = eaDSMAC;
         |LEE 101#120=;
         |SI FSalida ! 0;
             #120#0 = #119#0;
             #120#1 = eaDSMAC;
             |GRABA 020#120b;
         |FINSI;
         #120#2 = #902#4;
         #120#3 = #902#5;
         #120#4 = #902#6;
         |GRABA 020#120a;
         |LIBERA #120;
     |FINSI;

     |FIN_CONTROL_WINDOWS nSubBoton1;
     |FIN_CONTROL_WINDOWS nSubBoton2;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nSubVent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA nSubVent;

     |SI FEstado = 0;
         aBusca = #902#0;
         |HAZ MontaArbol;
     |FINSI;
|FPRC;

|PROCESO dsam0120B;
     |BORRA 020#120a;
     |LIBERA #120;
|FPRC;

|DEFBUCLE dsam0120B;
     #120#1;
     ;
     #119#0, "";
     #119#0, "zzzzzzzzzzzzzzzzzzzz";
     be;
     NULL, dsam0120B;
|FIN;

|PROCESO BorrarPrograma;
     #119#0 = aTexto;
     |LEE 101#119=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |CONFI "0000NSeguro que quiere borrar el programa seleccionado";
     |SI FSalida ! 0;
         |LIBERA #119;
         |ACABA;
     |FINSI;

     |BUCLE dsam0120B;

     |BORRA 020#119a;
     |LIBERA #119;

     |LEE 000#119p;
     aBusca = #119#0;
     |HAZ MontaArbol;
|FPRC;

|PROCESO EventoTree;
     |SI FEntrada ! 0;  |ACABA;  |FINSI;

     aAlfa       = Contenido;  |QBF aAlfa;
     aTexto      = "";
     aLong       = aAlfa % 0;
     nLong       = aLong;
     nCargar     = 0;
     |PARA nPosi = 1;  |SI nPosi [ nLong;  |HACIENDO nPosi = nPosi + 1;
           nPos = (nPosi * 100) + 1;
           aCarac = aAlfa % nPos;

           |SI nCargar = 1;
               aTexto = aTexto + aCarac;
           |FINSI;

           |SI aCarac = ":";  nCargar = 1; |FINSI;
     |SIGUE;

     |ESTADO_CONTROL nBoton2, "ENABLE";
     |ESTADO_CONTROL nBoton3, "ENABLE";

     aAlfa = aTexto % 102;
     |SI aTexto = "";
         |ESTADO_CONTROL nBoton2, "DISABLE";
         |ESTADO_CONTROL nBoton3, "DISABLE";
     |FINSI;
|FPRC;

|PROCESO CargaArbol;
     |QBF aAlfa;
     aAlfa = aAlfa + &13 + &10;
     |XGRABA nHandle, aAlfa;
|FPRC;

|PROCESO dsam0120;
     aAlfa     = #120#1 - ".";
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     nCarpeta2 = nCarpeta2  + 1;
     aAlfa1    = #120#1;  |QBF aAlfa1;
     aAlfa2    = #120#2;  |QBF aAlfa2;

     |SI aAlfa1 = eaDSMAC;
         aAlfa1 = "---> " + aAlfa1;
     |FINSI;

     aAlfa = nCarpeta1 + "," + nCarpeta2 + ":" + aAlfa1 + " - " + aAlfa2;
     |HAZ CargaArbol;
|FPRC;

|DEFBUCLE dsam0120;
     #120#1;
     ;
     #119#0, "";
     #119#0, "zzzzzzzzzzzzzzzzzzzz";
     ;
     NULL, dsam0120;
|FIN;

|PROCESO dsam0119;
     nCarpeta1 = nCarpeta1  + 1;

     aAlfa = nCarpeta1 + ":" + (("00" + #119#0) % -102) + " - " + #119#1;  |QBF aAlfa;
     aAlfa = aAlfa + " {{" + #119#0 + "}}";
     |HAZ CargaArbol;

     nCarpeta2 = 0;
     |BUCLE dsam0120;
|FPRC;

|DEFBUCLE dsam0119;
     #119#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, dsam0119;
|FIN;

|PROCESO MontaArbol;
     |SI nTree ! -1;
         |FIN_CONTROL_WINDOWS nTree;
         nTree = -1;
     |FINSI;

     |CONTROL_SIMPLE 0, "LABEL,{{5}}Cargando informaci¢n ...", 1315, 1680, NULL;
     nBotonLabel = FSalida;

     |PTEC 802;  |PAUSA;

     |DBASE aBase;  |QBF aBase;
     aAlfa = aBase + "basura";          |MKDIR aAlfa;

     eaRutaOrigen  = aBase + "basura/";
     eaRutaDestino = eaUnidadBasico + "\documentos";           |RMKDIR eaRutaDestino;
     eaRutaDestino = eaUnidadBasico + "\documentos\tmp";       |RMKDIR eaRutaDestino;
     eaRutaDestino = eaUnidadBasico + "\documentos\tmp\";
     eaFicheroTxt  = "abxz005201" + Usuario + ".txt";
     eaFicheroTgz  = "abxz005201" + Usuario + ".tgz";
     eaFicheroTar  = "abxz005201" + Usuario + ".tar";

     aAbre = eaRutaOrigen + eaFicheroTxt;

     sT_nID   = nTree;
     sT_nOper = 0;

     |XABRE "WB", aAbre, nHandle;

     nCarpeta1 = 0;

     |BUCLE dsam0119;

     |XCIERRA nHandle;

     |HAZ TraeTxtComprimido;
     aAlfa = eaRutaDestino + eaFicheroTxt;

     eaAlfa = "TREECTRL,Programas Externos {{" + aAlfa + "}}";
     |CONTROL_SIMPLE nPanta, eaAlfa, 0702, 2598, EventoTree;
     nTree = FSalida;

     eaFicheroTxt  = "abxz005201" + Usuario + ".txt";
     aAlfa         = eaRutaDestino + eaFicheroTxt;
     |RFBORRA aAlfa;

     |FIN_CONTROL_WINDOWS nBotonLabel;

     |SI aBusca ! "";
         sT_nID   = nTree;
         sT_nOper = 3;
         sT_aData = "[" + aBusca;
         |ESPECIFICA "COMMANDCONTROL", sTree;
         aRefresca = ":" + FSalida;
     |FINSI;

     sT_nID   = nTree;
     sT_nOper = 2;
     |SI aRefresca = "";
         sT_aData = "1:";
     |SINO;
         sT_aData = aRefresca;
     |FINSI;

     |ESPECIFICA "COMMANDCONTROL", sTree;
     |PULSATECLA;

     |FOCOTECLADO nTree;
|FPRC;

|PROCESO Tipo80;  |TIPO 80;
     |ABRE #514;
     |LEE 000#514p;
     |SI FSalida ! 0;  |DDEFECTO #514;  |FINSI;
     |CIERRA #514;

     FSalida = 2799;
|FPRC;

|PROCESO dsam0119Chequeo;
     enCodigo = #119#0;
     |HAZ CambiaAMac120;

     #120#0 = #119#0;
     #120#1 = eaDSMAC;
     |LEE 101#120=;
     |SI FSalida ! 0;
         |DDEFECTO #120;
         #120#0 = #119#0;
         #120#1 = eaDSMAC;
         #120#2 = aNombreIP;
         |SI #119#2 = 1;  #120#3 = "LLAMADA INTERNA DESDE PROGRAMA";  |FINSI;
         |GRABA 020#120n;
     |SINO;
         #120#2 = aNombreIP;
         |GRABA 020#120a;
     |FINSI;
     |LIBERA #120;
|FPRC;

|DEFBUCLE dsam0119Chequeo;
     #119#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, dsam0119Chequeo;
|FIN;

|PROGRAMA;
     |HAZ LeeUnidadBasico;
     |HAZ CogeMac;

     |ABRET #119;
     |ABRET #120;

     aIPRemoto = "";
     |IP_REMOTO aIPRemoto;   |QBT aIPRemoto;
     |SI aIPRemoto = "";
         aIPRemoto = "LOCAL";
         aNombreIP = "MAQUINA LOCAL";
     |SINO;
         |NOMBRE_IP aIPRemoto, aNombreIP;
     |FINSI;

     |BUCLE dsam0119Chequeo;

     |CLS;
     |CABEZA "Programa Externos";

     |PINPA #0#952;
     nPanta = FSalida;

     aAlfa = "LABEL,{{0}} Configuraci¢n de la MAC " + eaDSMAC;
     |CONTROL_SIMPLE nPanta, aAlfa, 0650, 0698, NULL;

     |CONTROL_SIMPLE nPanta, "BOTON,{{193}}Nuevo Programa", 2634, 2654, NuevoPrograma;
     nBoton1 = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,{{197}}Modificar Programa", 2656, 2676, ModificarPrograma;
     nBoton2 = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,{{199}}Borrar Programa", 2678, 2698, BorrarPrograma;
     nBoton3 = FSalida;

     |HAZ MontaArbol;

     aEsc1  = &255 + "806";
     aEsc2  = &255 + "807";
     aRetu  = &255 + "802";
     |PARA; |SI;  |HACIENDO;
         FSalida = "::{{001}}Salir," + nTree;
         |LEETECLA aTecla;
         |SI aTecla = aEsc1; |SAL;  |FINSI;
         |SI aTecla = aEsc2; |SAL;  |FINSI;
         |SI aTecla = aRetu; |SAL;  |FINSI;
     |SIGUE;

     |FIN_CONTROL_WINDOWS nTree;
     |FIN_CONTROL_WINDOWS nBoton1;
     |FIN_CONTROL_WINDOWS nBoton2;
     |FIN_CONTROL_WINDOWS nBoton3;

     |CIERRAT #119;
     |CIERRAT #120;
|FPRO;
