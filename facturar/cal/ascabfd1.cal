|FICHEROS;
     ascabfdi #0;
     ascontfd #1;
     ascontft #451;
     aslinfdi #2;
     asempres #3;
     asconcep #4;
     ashiscon #5;
     asclient #7;
     ashisfac #8;
     ashisrec #9;
     aslinfdl #107;

     ashismic #90;
     ashismil #91;
     ashismit #92;

     ascuolin #111;

     &bancosp@ #706;
     &fopagos@ #712;
|FIN;

|VARIABLES;
     &enCodBanco  = 0;
     &enCodPago   = 0;
     &enSwDeDonde = 0;

     &d_viene   = 1;
     &que_progra = "";

     aGuarda    = "";
     em         = 0;
     j          = 0;
     fechav     = @;
     sw_linea   = 0;
     cont       = 0;
     varalf     = "";
     x          = 0;
     salida     = 0;
     sub_ejec   = 0;
     FEstado    = 0;
     xxx        = 0;
     yyy        = 0;
     mes_a      = "";
     mes_num    = 0;
     campo      = 0;
     aste       = "";
     agi        = "";
     agi1       = "";
     agi2       = "";
     longi      = "";
     aAnyo      = "";
     aAlfa      = "";
     aAlfa1     = "";
     aAbre      = "";

     num        = 0;
     nVariable  = 0;
     nHandle    = 0;

     &tipo_d    = 0;
     &cliente_d = 0;
     &serie_d   = "";
     &factura_d = 0;

{-1} impresion  = "";
     impresion1 = "2400";
     impresion2 = "1";
     impresion3 = "";
     impresion4 = "FRB";
     impresion5 = "  Factura  ";
     impresion6 = "  Recibo  ";
     impresion7 = "  Borrar Registro  ";

{-1} actualizar  = "";
     actualizar1 = "2400";
     actualizar2 = "1";
     actualizar3 = "";
     actualizar4 = "FRA";
     actualizar5 = "  Factura  ";
     actualizar6 = "  Recibo  ";
     actualizar7 = "  Actualizar  ";

/*
{-1} actualizar  = "";
     actualizar1 = "2400";
     actualizar2 = "1";
     actualizar3 = "";
     actualizar4 = "FRAB";
     actualizar5 = "  Factura  ";
     actualizar6 = "  Recibo  ";
     actualizar7 = "  Actualizar  ";
     actualizar8 = "  Borrar Registro  ";
*/
|FIN;

|INCLUYE agicon_i;

|| *********************************************************************
|| ********************** CALCULOS DEL PROGRAMA ************************
|| *********************************************************************


|PROCESO entrada;
|ET campo_1;
    |ENCAMPO #0#0;
    |SI FSalida = 1; |O FSalida = 7; salida = 1; |ACABA; |FINSI;

    |ENCAMPO #1#0;
    |SI FSalida = 1; |O FSalida = 7; salida = 1; |ACABA; |FINSI;
    |SI FSalida = 2; |VETE campo_1;  |FINSI;

|ENDATOS #1#0;
salida = FSalida;
|FPRC;

|PROCESO cliente;|TIPO 0;
|SI #0#3 = "                                        ";
   #0#3 = #0#2;
   |PINTA #0#3;
|FINSI;
|FPRC;

|PROCESO AlContado;  |TIPO 6;
     |SI FSalida ! 10;  |ACABA;  |FINSI;

     aGuarda    = que_progra;
     que_progra = "asclient";
     #0#1    = 0;    |PINTA #0#1;

     |ABRE #7;
     #7#0 = #0#1;
     |LEE 101#7=;
     |SI FSalida ! 0;
         |DDEFECTO #7;
         #7#0 = #0#1;
         |GRABA 020#7b;
     |FINSI;

     |PUSHV 0501 2380;

     |DDEFECTO #7;
     #7#0 = #0#1;
     |PINPA #0#7;
     |PINDA #0#7;
     |ENDATOS #2#7;
     |SI FSalida ! 0;
         |ERROR;
     |SINO;
         |GRABA 020#7a;
     |FINSI;

     |LIBERA #7;
     |CIERRA #7;

     |POPV;

     que_progra = aGuarda;
|FPRC;

|PROCESO pintacli;|TIPO 0;
|ABRE #7;
#7#0 = #0#1;
|LEE 040#7=;
|SI FSalida = 0;
    #0#2 = #7#1;
    #0#3 = #7#2; |PINTA #0#3;
    #0#4 = #7#3;
    #0#5 = #7#4;
    #0#6 = #7#5;
    #0#7 = #7#6;
    #0#8 = #7#7;
    #0#9 = #7#8;
    #0#10 = #7#15; |PINTA #0#10;
    #0#11 = #7#16; |PINTA #0#11;
    #0#12 = #7#17; |PINTA #0#12;
    #0#13 = #7#11;
    #0#14 = #7#12;
    #0#15 = #7#13;
    #0#16 = #7#14;
    #0#24 = #7#23; |PINTA #0#24;
|SINO;
   |MENAV "    No existe cliente ";
   |ERROR;
|FINSI;
|CIERRA #7;
|FPRC;

|PROCESO control; |TIPO 0;
     |ABRE #1;
     |LEE 000#1p;
     |SI FSalida ! 0;  |DDEFECTO #1;  |FINSI;
     |CIERRA #1;

     |ABRE #451;
     #451#0 = #0#0;
     |LEE 101#451=;
     |SI FSalida ! 0;
         |DDEFECTO #451;
         #451#0 = #0#0;
         |GRABA 020#451b;
         |LIBERA #451;
     |FINSI;

     #0#27 = #451#1;
     #0#28 = #451#2 + 1;

     |PINTA #0#27;
     |PINTA #0#28;

     |SI #1#35 = "S";
         |SI #451#2 < #0#28;
             #451#2 = #0#28;
         |FINSI;
     |FINSI;

     |GRABA 020#451a;
     |LIBERA #451;
     |CIERRA #451;
|FPRC;

|PROCESO primera; |TIPO 7;
|SI sw_linea = 1; |ACABA; |FINSI;

|ABRE #1;
|LEE 040#1p;
|SI #1#32 = "N"; sw_linea = 1; |CIERRA #1; |ACABA; |FINSI;
|CIERRA #1;

|ABRE #4;
#4#0 = #1#33;
#4#1 = #1#34;
|LEE 040#4=;
|SI FSalida ! 0;   sw_linea = 1; |CIERRA #4; |ACABA; |FINSI;
|SI #4#3    ! "A"; sw_linea = 1; |CIERRA #4; |ACABA; |FINSI;
|CIERRA #1;

cont = (FEntrada / 100) ! 0;
|SI cont = 1; |Y sw_linea = 0; |Y #2#2 = 1;
    sw_linea = 0;
    |PTEC 801;
|FINSI;
|FPRC;

|PROCESO impreact;   || Emision y Actualizacion
|ET menuini;
    |MENU impresion;
    impresion2 = FSalida;
    |SI impresion2 < 1;|O impresion2 > 3; |VETE menuini; |FINSI;
    |SI impresion2 = 3;
         |BUCLELIN 1NULL#0;
         |BORRA 020#0c;
         |LIBERA #0;
         |PINDA #0#0;
    |FINSI;
    |SI impresion2 = 1;
        sub_ejec   = 1;
        |HAZ sub_ejecutas;
        |HAZ dosmenu;
    |FINSI;
    |SI impresion2 = 2;
        sub_ejec   = 2;
        |HAZ sub_ejecutas;
        |HAZ dosmenu;
    |FINSI;
|FPRC;

|PROCESO dosmenu;
|ET menuact;
   |MENU actualizar
   actualizar2 = FSalida;
   |SI actualizar2 < 1;|O actualizar2 > 4; |VETE menuact; |FINSI;
   |SI actualizar2 = 1;
       sub_ejec   = 1;
       |HAZ sub_ejecutas;
       |VETE menuact;
   |FINSI;
   |SI actualizar2 = 2;
       sub_ejec   = 2;
       |HAZ sub_ejecutas;
       |VETE menuact;
   |FINSI;
   |SI actualizar2 = 3;
       |HAZ actuali;
       |BUCLELIN 1NULL#0;
       |BORRA 020#0c;
       |LIBERA #0;
       |PINDA #0#0;
   |FINSI;
   |SI actualizar2 = 4;
       |BUCLELIN 1NULL#0;
       |BORRA 020#0c;
       |LIBERA #0;
       |PINDA #0#0;
   |FINSI;
|FPRC;

|PROCESO sub_ejecutas;
|CIERRAT #0;

|SI sub_ejec = 1; |SUB_EJECUTA "asfacges.run"; |FINSI;
|SI sub_ejec = 2; |SUB_EJECUTA "asrecges.run"; |FINSI;

|ABRET #0;

#0#0  = tipo_d;
#0#1  = cliente_d;
#0#27 = serie_d;
#0#28 = factura_d;
|LEE 140#0=;
|FPRC;

|| *********************************************************************
|| ********************** CALCULOS DE ACTUALIZACION*********************
|| *********************************************************************

|PROCESO lineas;
     |ABRE #111;
     |SELECT #2#111;
     aAnyo  = #2#17 % -104;
     #111#0 = #2#1;
     #111#9 = aAnyo;
     #111#2 = #2#4;
     #111#3 = #2#5;
     |LEE 101#111=;
     |SI FSalida = 0;
         #111#8 = #111#8 + 1;
         |GRABA 020#111a;
         |LIBERA #111;
     |FINSI;
     |CIERRA #111;



|| **********************  Graba Historico de conceptos **********************

#5#0 = #2#14;
#5#1 = #2#15;
#5#2 = #2#2;
|LIBERA #5;
|LEE 110#5=;
FEstado = FSalida;
|SI FSalida = 0;
    |MENAV "    Ya existia Factura en el Historico de Conceptos";
|FINSI;

#5#0  = #2#14;
#5#1  = #2#15;
#5#2  = #2#2;
#5#3  = #2#17;
#5#4  = #2#1;
#5#5  = #2#4;
#5#6  = #2#5;
#5#7  = #2#7;
#5#8  = #2#10;
#5#9  = #2#13;
#5#10 = #2#8;
#5#11 = #2#9;
|SI FEstado ! 0; |GRABA 020#5n; |SINO; |GRABA 020#5a; |FINSI;
|LIBERA #5;

|| ******************  Graba Historico Linea Minutas **********************

#91#0 = #2#14;
#91#1 = #2#15;
#91#2 = #2#2;
|LIBERA #91;
|LEE 110#91=;
FEstado = FSalida;

#91#0  = #2#14;
#91#1  = #2#15;
#91#2  = #2#2;
#91#3  = #2#17;
#91#4  = #2#4;
#91#5  = #2#5;
#91#6  = #2#6;
#91#7  = #2#7;
#91#8  = #2#8;
#91#9  = #2#9;
#91#10 = #2#10;
#91#11 = #2#11;
#91#12 = #2#12;
#91#13 = #2#13;
#91#14 = #2#16;
#91#30 = #2#18;
|SI FEstado ! 0; |GRABA 140#91n; |SINO; |GRABA 140#91a; |FINSI;

|ABRE #92;
|ABRE #107;
|PARA nVariable = 1;  |SI nVariable [ 100;  |HACIENDO nVariable = nVariable + 1;
      #107#0 = #2#0;
      #107#1 = #2#14;
      #107#2 = #2#15;
      #107#3 = #2#1;
      #107#4 = #2#2;
      #107#5 = nVariable;
      |LEE 101#107=;
      |SI FSalida = 0;
          #92#0 = #91#0;
          #92#1 = #91#1;
          #92#2 = #91#2;
          #92#3 = #107#5;
          #92#4 = #107#6;
          |GRABA 020#92n;
          |LIBERA #92;

          |BORRA 020#107a;
          |LIBERA #107;
      |SINO;
          |SAL;
      |FINSI;
|SIGUE;
|CIERRA #107;
|CIERRA #92;

|| **********************  Graba Conceptos **********************

mes_a   = #2#17 % 402;
mes_num = mes_a;

#4#0 = #2#4;
#4#1 = #2#5;
|LIBERA #4;
|LEE 140#4=;
|SI FSalida = 0;
    #4#31 = #4#31 + #2#13;
    #4#6  = #4#6  + #2#13;
    campo = 18 + mes_num; #4campo = #4campo +  #2#13;
    |GRABA 140#4a;
|FINSI;
|FPRC;

|PROCESO actuali;
|ABRE #4;
|ABRE #5;
|ABRE #91;
|BUCLELIN 0lineas#0;
|CIERRA #4;
|CIERRA #5;
|CIERRA #91;

|| **********************  Graba Bancos **********************

mes_a   = #0#29 % 402;
mes_num = mes_a;

enCodBanco = #0#11;
|HAZ LeeBancoPropio;
|SI enSwDeDonde = 0;
    |ABRE #706;
    #706#0 = #0#11;
    |LEE 101#706=;
    |SI FSalida = 0;
        #706#20 = #706#20 + #0#26;
        #706#22 = #706#22 + #0#26;
        campo = 7 + mes_num; #706campo = #706campo + #0#26;
        |GRABA 020#706a;
        |LIBERA #706;
    |FINSI;
    |CIERRA #706;
|FINSI;

|| **********************  Graba Clientes **********************

|ABRE #7;
#7#0 = #0#1;
|LIBERA #7;
|LEE 140#7=;
|SI FSalida = 0;
    #7#49 = #7#49 + #0#17;
    #7#50 = #7#50 + #0#18;
    #7#18 = #7#18 + #0#17;
    #7#19 = #7#19 + #0#18;

    campo = 24 + mes_num; #7campo = #7campo +  #0#17;
    campo = 36 + mes_num; #7campo = #7campo +  #0#18;

    #7#24 = #0#26;
    |GRABA 020#7a;
|FINSI;
|CIERRA #7;

|| **********************  Graba Historico de Facturas **********************
|ABRE #8;
#8#0 = #0#27;
#8#1 = #0#28;
|LIBERA #8;
|LEE 140#8=;
FEstado = FSalida
|SI FSalida = 0;
    |MENAV "     Ya existia la Factura en el Historico de Facturas";
|FINSI;

#8#0  = #0#27;
#8#1  = #0#28;
#8#2  = #0#29;
#8#3  = #0#1;
#8#4  = #0#0;
#8#5  = #0#17;
#8#6  = #0#18;
#8#7  = ((#0#23 * 100) / #0#22) ! 0;
#8#8  = #0#23;
#8#9  = ((#0#25 * 100) / #0#22) ! 0;
#8#10 = #0#25;
#8#11 = #0#24;
#8#12 = "N"
#8#13 = "N";
#8#14 = #0#19;
#8#15 = #0#20;
#8#16 = #0#21;
#8#17 = #0#22;
#8#18 = #0#26;
|SI FEstado ! 0; |GRABA 020#8n; |SINO; |GRABA 020#8a; |FINSI;
|CIERRA #8;

|| **********************  Graba Historico de Recibos **********************

|SI #0#10 = 99;
    fechav = #0#29;
|SINO;
    fechav    = #0#29;
    enCodPago = #0#10;
    |HAZ LeeFormaDePago;
    |SI #712#5 ! 0;
        |SI #712#2 = "D"; em = 1; |SINO; em = 1000; |FINSI;
        j = #712#5 / em;
        fechav = #0#29 + j;
    |FINSI;
|FINSI;

|ABRE #9;
#9#0 = #0#27;
#9#1 = #0#28;
|LIBERA #9;
|LEE 140#9=;
FEstado = FSalida;
|SI FSalida = 0;
   |MENAV "     Ya existia Factura en el Historico de Recibos";
|FINSI;
#9#0  = #0#27;
#9#1  = #0#28;
#9#2  = #0#29;
#9#3  = #0#1;
#9#4  = #0#0;
#9#5  = fechav;
#9#6  = #0#26;
#9#7  = #0#11;
#9#11 = #0#10;
aAlfa = #9#2 % -104;
#9#17 = aAlfa;
|SI FEstado ! 0; |GRABA 121#9n; |SINO; |GRABA 020#9a; |FINSI;
|CIERRA #9;

|| *************  Graba Historico de Minutas Cabecera **********************

|ABRE #90;
#90#0 = #0#27;
#90#1 = #0#28;
|LEE 101#90=;
|SI FSalida ! 0;
    |DDEFECTO #90;
    #90#0 = #0#27;
    #90#1 = #0#28;
    |GRABA 020#90b;
|FINSI;

|PARA xxx = 0; |SI xxx [ 26; |HACIENDO xxx = xxx + 1;
      yyy = xxx + 2;
      #90yyy = #0xxx;
|SIGUE;
#90#29 = #0#29;
#90#34 = #0#31;
#90#45 = ((#0#23 * 100) / #0#22) ! 0;
#90#46 = ((#0#25 * 100) / #0#22) ! 0;
|GRABA 020#90a;
|LIBERA #90;
|CIERRA #90;

|| **********************  Graba Control **********************

     |ABRE #1;
     |LEE 000#1p;
     |SI FSalida ! 0;  |DDEFECTO #451;  |FINSI;
     |CIERRA #1;

     |SI #1#35 ! "N";  |ACABA;  |FINSI;

     |ABRE #451;
     #451#0 = #0#0;
     |LEE 000#451=;
     |SI FSalida ! 0;
         |DDEFECTO #451;
         #451#0 = #0#0;
         |GRABA 020#451b;
     |FINSI;

     |SI #451#2 < #0#28;
         #451#2 = #0#28;
     |FINSI;
     |GRABA 020#451a;
     |LIBERA #451;
|FPRC;

|PROGRAMA;
     |PULSATECLA;
     |DFICE aAbre;  |QBF aAbre;
     aAbre = aAbre + "factdir.txt";
     |XABRE "E", aAbre, nHandle;
     |SI FSalida ] 0;
         |XABRE "UB", aAbre, nHandle;
         |XLEE nHandle, 250, aAlfa;   |QBF aAlfa;
         |XCIERRA nHandle;

         aAlfa = aAlfa % 108;

         aAlfa = "0000El usuario: " + aAlfa + " esta facturando en estos momentos.";
         |MENAV aAlfa;

         aAlfa = "0000No pueden dos o mas usuarios facturar en las misma empresa al mismo tiempo  ";
         |MENAV aAlfa;

         aAlfa = "0000Asegurese que no hay nadie mas.";

         |CONFI "0000NQuiere continuar. ";
         |SI FSalida ! 0;  |ACABA;  |FINSI;

         |FBORRA aAbre;
     |FINSI;

     |XABRE "WB", aAbre, nHandle;
     aAlfa = (((Usuario % 810) + "          ") % 110) + Usuario;
     |XGRABA nHandle, aAlfa;
     |XCIERRA nHandle;

     |MODULO que_progra;
     |CLS;
     |CABEZA "Facturas Directas";
     |ABRET #0;
     |PINPA #0#0;
     salida = 0;
     |PARA; |SI salida = 0; |HACIENDO;
            |DDEFECTO #0;
            |PINPA #0#0;
            |PINDA #0#0;
            |HAZ entrada;
            |SI salida = 0;
                |GRABA 140#0b;
                |LIBERA #0;
                tipo_d    = #0#0;
                cliente_d = #0#1;
                serie_d   = #0#27;
                factura_d = #0#28;
                |HAZ impreact;
            |FINSI;
     |SIGUE;
     |CIERRAT #0;

     |DFICE aAbre;  |QBF aAbre;
     aAbre = aAbre + "factdir.txt";
     |XABRE "UB", aAbre, nHandle;
     |XLEE nHandle, 250, aAlfa;   |QBF aAlfa;
     |XCIERRA nHandle;

     aAlfa1 = (((Usuario % 810) + "          ") % 110) + Usuario; |QBF aAlfa1;
     |SI aAlfa1 = aAlfa;
         |FBORRA aAbre;
     |FINSI;
|FPRO;
