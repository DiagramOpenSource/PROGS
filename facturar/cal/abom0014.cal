|FICHEROS;
     abom0014 #14;
     abom0514 #514;
|FIN;

|VARIABLES;
     nBoton2   = 0;
     nBoton3   = 0;
     nBoton4   = 0;
     nTecla    = 0;
     nHandle   = 0;
     nVariable = 0;
     nLong     = 0;
     nCaracter = 0;
     nPosi     = 0;
     nPos1     = 0;
     nPos2     = 0;
     nPos      = 0;

     aBr       = "";
     aPos      = "";
     aAlfa     = "";
     aAlfa1    = "";
     aAlfa2    = "";
     aRuta     = "";
     aLong     = "";
     aCaracter = "";
     aOri      = "";
     aDes      = "";

     &enTotalVaria    = 0;
     &eaNombreList    = "";
     &enCuadroIni     = 0;
     &enCuadroFin     = 0;
     &enCampoAncho    = 0;
     &enModoEdita     = 0;
     &enCodUbica      = 0;
     &efFechaDocu     = @;
     &eaRutaDocumento = "";
     &eaFicheroEdita  = "";
     &eaUnidadBasico  = "";

{101}&epPuntEdita     = "";
|FIN;

|PROCESO QuitaBr;
     aBr = aAlfa1 - "<br>";
     |SI FSalida = 0;
         epPuntEdita  = aAlfa1;
         IepPuntEdita = IepPuntEdita + 1;

         |ACABA;
     |FINSI;

     |PARA;  |SI;  |HACIENDO;
          aBr = aAlfa1 - "<br>";
          |SI FSalida = 0;  |SAL;  |FINSI;

          aAlfa2 = ("00" + FSalida) % -104;
          aPos   = aAlfa2 % 102;  nPos1 = aPos;
          aPos   = aAlfa2 % 302;  nPos2 = aPos;
          nPos   = ((nPos1 + nPos2) * 100) + 99;

          |SI nPos1 = 1;
              epPuntEdita  = "";
              IepPuntEdita = IepPuntEdita + 1;

              aAlfa1     = aBr;
          |SINO;
              nPos         = 100 + (nPos1 - 1);
              aAlfa2       = aAlfa1 % nPos;
              epPuntEdita  = aAlfa2;
              IepPuntEdita = IepPuntEdita + 1;

              |QBF aAlfa2;
              aAlfa1       = aAlfa1 - aAlfa2;

              |PARA;  |SI;  |HACIENDO;
                   aAlfa2 = aAlfa1 % 101;
                   |SI aAlfa2 ! " ";  |SAL;  |FINSI;

                   aAlfa1 = aAlfa1 - " ";
              |SIGUE;
          |FINSI;
     |SIGUE;

     |PARA;  |SI;  |HACIENDO;
             aAlfa2 = aAlfa1 % 101;
             |SI aAlfa2 ! " ";  |SAL;  |FINSI;

             aAlfa1 = aAlfa1 - " ";
     |SIGUE;

     |SI aAlfa1 ! "";
         epPuntEdita  = aAlfa1;
         IepPuntEdita = IepPuntEdita + 1;
     |FINSI;
|FPRC;

|PROCESO EditaCuerpo;
     |PARA nVariable = 1;  |SI nVariable [ 100;  |HACIENDO nVariable = nVariable + 1;
           IepPuntEdita  = epPuntEdita1 <-;
           IepPuntEdita = IepPuntEdita + (nVariable - 1);

           epPuntEdita  = "";
     |SIGUE;

     efFechaDocu  = #14#7;
     enCodUbica   = #514#9;  |HAZ LeeUbicaciones;
     aAlfa        = eaRutaDocumento + "/" + (("00000000" + #14#0) % -108) + ".txt";
     IepPuntEdita = epPuntEdita1 <-;

     |SI #514#21 = "C";
         |XABRE "U", aAlfa, nHandle;
     |SINO;
         |XABRE "CU", aAlfa, nHandle;
     |FINSI;

     |PARA;  |SI  FSalida ] 0;  |HACIENDO;
             |XLEE nHandle, 254, aAlfa1;
             |SI FSalida < 0;  |SAL;  |FINSI;

             aAlfa1 = aAlfa1 % 170;
             |HAZ QuitaBr;
     |SIGUE;
     |XCIERRA nHandle;

     enTotalVaria  = 99;
     eaNombreList  = "";
     enCuadroIni   = 0909;
     enCuadroFin   = 2280;
     enCampoAncho  = 70;
     enModoEdita   = 1;

     |PUSHV  0501 2380;
     |BLANCO 1009 2279;
     |HAZ Edita;
     |POPV;
|FPRC;

|PROCESO Tipo11;  |TIPO 11;
     nTecla = FSalida;

     |SI nTecla = 10;
         |HAZ EditaCuerpo;
     |FINSI;

     |SI nTecla = 11;
         |SI #14#6 = 0;
              |MENAV "     No hay Fichero Adjunto";
              |ACABA;
         |FINSI;

         |SI #14#9 = "N";
             eaFicheroEdita = #14#5;  |QBF eaFicheroEdita;
             |SI eaFicheroEdita ! "";
                 efFechaDocu    = #14#8;
                 enCodUbica     = #14#6;  |HAZ LeeUbicaciones;
                 eaFicheroEdita = #14#5;  |QBF eaFicheroEdita;
                 |HAZ AbreDocumento;
             |SINO;
                 |MENAV "     No hay Fichero Adjunto";
                 |ACABA;
             |FINSI;
         |SINO;
             aAlfa = #14#5;  |QBF aAlfa;
             |SI aAlfa ! "";
                 efFechaDocu = #14#8;
                 enCodUbica  = #14#6;  |HAZ LeeUbicaciones;
                 aAlfa       = eaRutaDocumento + "/" + #14#5;  |QBF aAlfa;

                 |SI #514#21 ! "C";
                     aLong    = aAlfa % 0;
                     nLong    = aLong;
                     aRuta    = "";

                     |PARA nCaracter = 1;  |SI nCaracter [ nLong;  |HACIENDO nCaracter = nCaracter + 1;
                           nPosi     = (100 * nCaracter) + 1;
                           aCaracter = aAlfa % nPosi;
                           |SI aCaracter = "/";
                               aCaracter = "\";
                           |FINSI;
                           aRuta = aRuta + aCaracter;
                     |SIGUE;
                     aAlfa = aRuta;
                 |SINO;
                     aOri = aAlfa;
                     aDes = eaUnidadBasico + "\DOCUMENTOS\IPABOGAD\" + #14#5;  |QBF aDes;
                     |ENVIA_FICHERO aOri, aDes;

                     aAlfa = aDes;
                 |FINSI;

                 aAlfa = "cmd " + &34 + "/c START /WAIT " + aAlfa + &34;
                 |RSYSTEM aAlfa;

                 |RFBORRA aDes;
             |FINSI;
         |FINSI;
     |FINSI;

     |SI nTecla = 12;
         |CONFI "0000NEsta Seguro de Borrar el Correo ";
         |SI FSalida ! 0;  |ACABA;  |FINSI;

         efFechaDocu = #14#7;
         enCodUbica  = #514#9;  |HAZ LeeUbicaciones;
         aAlfa       = eaRutaDocumento + "/" + (("00000000" + #14#0) % -108) + ".txt";
         |SI #514#21 ! "C";
             |RFBORRA aAlfa;
         |SINO;
             |FBORRA aAlfa;
         |FINSI;

         |LEE 101#14=;
         |SI FSalida = 0;
             |BORRA 020#14a;
         |FINSI;
         |LIBERA #14;

         |PONCONTROL 23, "SI";
     |FINSI;
|FPRC;

|RUTINA ClaveBaja14;
     #14#0  = 1;
|FRUT;

|RUTINA ClaveAlta14;
     #14#0  = 999999999;
|FRUT;

|PROCESO Boton2;
     |PTEC 824;
|FPRC;

|PROCESO Boton3;
     |PTEC 825;
|FPRC;

|PROCESO Boton4;
     |PTEC 826;
|FPRC;

|PROCESO PonBoton;
     |CONTROL_SIMPLE 0, "BOTON,F2 Visualizar Cuerpo", 2305, 2324, Boton2;
     nBoton2 = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,F3 Visualizar Adjunto", 2326, 2345, Boton3;
     nBoton3 = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,F4 Borrar Correo ", 2347, 2364, Boton4;
     nBoton4 = FSalida;
|FPRC;

|PROCESO FinBoton;
     |FIN_CONTROL_WINDOWS nBoton2;
     |FIN_CONTROL_WINDOWS nBoton3;
     |FIN_CONTROL_WINDOWS nBoton4;

     |PULSATECLA;
|FPRC;

|PROGRAMA;
     |HAZ LeeUnidadBasico;

     |ABRE #514;
     |LEE 040#514p;
     |SI FSalida ! 0;  |DDEFECTO #514;  |FINSI;
     |CIERRA #514;

     |SI #514#1 = "N";
         |MENAV "     No tiene Configurado el Correo en la Aplicacion";
         |ACABA;
     |FINSI;

     |CLS;

     |CABEZA "Salida de Correos";
     |HAZ PonBoton;
     |ENTLINEAL #1#14, -1, 4, 21, 1, ClaveBaja14, ClaveAlta14;
     |HAZ FinBoton;
|FPRO;
