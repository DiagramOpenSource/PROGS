|FICHEROS;
     abxz0055;
     abom0001;
     abom0002;
     abom0020;
     dsam0121;
     abom0514;
     abom0021;

     abom0505;
     abom0511;

     abxw0005;
|FIN;

|VARIABLES;
     &enNumExpeCorreo = 0;

     &eaDSMAC  = "";
     aPath     = "";
     aPortal   = "";
     aMensaje  = "";
     aOrigen   = "";
     aDestino  = "";
     aIPRemoto = "";
     aBorra    = "";
     aVerde    = "000,185,000;255,255,255";

     nRango          = 0;
     nHandle         = 0;
     nLong           = 0;
     nCaracter       = 0;
     nPos            = 0;
     nVentana        = 0;
     nCampo          = 0;
     nGuardaExpe     = -1;
     nBoton1         = 0;
     nBoton2         = 0;
     nBoton3         = 0;
     nBoton4         = 0;
     nBoton5         = 0;
     nBoton6         = 0;
     nLinea          = 0;
     nOpcion         = 0;
     nDoc            = 0;
     nEnvio          = 0;
     nTree           = -1;
     nSubVent        = 0;
     nSwTgzOk        = 0;
     nCar            = 0;
     nAsc            = 0;
     nRein           = 0;

     aEsc1           = "";
     aEsc2           = "";
     aRetu           = "";
     aTecla          = "";
     aAlfa           = "";
     aCarac10        = "";
     aCarac13        = "";
     aCadena         = "";
     aAbre           = "";
     aFicheroActu    = "";
     aBase           = "";
     aDBase          = "";
     aPathPaq        = "";
     aPathTar        = "";
     aDir            = "";
     aUser           = "";
     aHora           = "";
     aFicTar         = "";
     aLon            = "";
     aExt            = "";
     aCar            = "";

     fFecha          = @;

     {-1}aVent       = "";
         aVent1      = 0;
         aVent2      = 0;
         aVent3      = 0;
         aVent4      = 0;
         aVent5      = "";

     {1}aContiene    = "";

     {-1}sTree       = 0;
         sT_nID      = 0;
         sT_nOper    = 0;
         sT_aData    = "";

   {10} aPopup       = "";

     &enGrid2         = 0;
     &enPanta0        = 0;
     &enSubPanta      = 0;
     &enExped         = 0;
     &enLabel1        = -1;
     &enLabel2        = -1;
     &enLabel3        = -1;
     &enLabel4        = -1;
     &enExpediente    = 0;
     &enCodUbica      = 0;
     &eaRutaDocumento = "";
     &efFechaDocu     = @;
     &eaFicheroEdita  = "";
     &eaFichAdjunto   = "";
     &enCrear         = 0;
     &enLineas        = 0;
     &enElEnvio       = 0;
     &eaUnidadBasico  = "";
     &eaPaq           = "";
     &eaNomDes        = "";
     &enCambAdj       = 0;
|FIN;

|PROCESO DesactivaPantPrincipal;
     |HAZ QuitaBotonSalir1;

     |ESTADO_CONTROL nBoton1, "HIDE";
     |ESTADO_CONTROL nBoton2, "HIDE";
     |ESTADO_CONTROL nBoton3, "HIDE";
     |ESTADO_CONTROL nBoton4, "HIDE";
     |ESTADO_CONTROL nBoton5, "HIDE";
     |ESTADO_CONTROL nBoton6, "HIDE";

     |ESTADO_CONTROL enGrid2, "DISABLE";
|FPRC;

|PROCESO ActivaPantPrincipal;
     |HAZ PonBotonSalir1;

     |ESTADO_CONTROL nBoton1, "SHOW";
     |ESTADO_CONTROL nBoton2, "SHOW";
     |ESTADO_CONTROL nBoton3, "SHOW";
     |ESTADO_CONTROL nBoton4, "SHOW";
     |ESTADO_CONTROL nBoton5, "SHOW";
     |ESTADO_CONTROL nBoton6, "SHOW";

     |ESTADO_CONTROL enGrid2, "ENABLE";
|FPRC;

|PROCESO Boton1;
     |HAZ DesactivaPantPrincipal;

     |VENTANA 0601, 2499, -1, 17, "Nuevo documento";
     nSubVent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nSubVent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |DDEFECTO #abxw0005;
     |PINPA #0#abxw0005;
     enSubPanta = FSalida;

     |CONTROL_SIMPLE enSubPanta, "LABEL,{{2}}De un documento SIN expediente o formulario (Plantillas)", 1042, 1098, NULL;
     enLabel1 = FSalida;

     |CONTROL_SIMPLE enSubPanta, "LABEL,{{2}}De un documento de expediente", 1142, 1198, NULL;
     enLabel2 = FSalida;

     |CONTROL_SIMPLE enSubPanta, "LABEL,{{2}}Recoger un documento del PC", 1242, 1298, NULL;
     enLabel4 = FSalida;

     enLabel3 = -1;
     |SI enExped ! 0;
         |CONTROL_SIMPLE enSubPanta, "LABEL,{{2}}De la entrada general de documentos", 1342, 1398, NULL;
         enLabel3 = FSalida;
     |FINSI;

     #abom0002#0 = enExpediente;
     #abom0002#1 = 99999;
     |LEE 000#abom0002.];
     |SI FSalida = 0;
         |LEE 000#abom0002.a;
     |SINO;
         |LEE 000#abom0002.u;
     |FINSI;

     nLinea = 1;
     |SI FSalida = 0;  |Y #abom0002#0 = enExpediente;
         nLinea = #abom0002#1 + 1;
     |FINSI;

     #abxw0005#8 = nLinea;

     |ABRE #dsam0121;
     |LEE 000#dsam0121.p;
     |SI FSalida ! 0;  |DDEFECTO #dsam0121;  |FINSI;
     |CIERRA #dsam0121;

     #abxw0005#0 = #dsam0121#0;

     |PINDA #0#abxw0005;

     |HAZ Tipo0C0Fxw5;
     |HAZ Tipo0C2Fxw5;
     |HAZ Tipo0C4Fxw5;

     |ENDATOS #1#abxw0005;
     |SI FSalida = 0;
         |SI #abom0002#0 ! 0;
             |SI #abom0002#1 = 1;
                 |HAZ CogeTextoExpediente;
             |FINSI;
         |FINSI;
     |FINSI;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nSubVent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA nSubVent;

     |HAZ ActivaPantPrincipal;

     |REFRESCACONTROL enGrid2;  |PULSATECLA;
|FPRC;

|PROCESO Boton2;
     |LEE 101#abom0002.=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |CONFI "0000NSeguro que quiere borrar el documento";
     |SI FSalida ! 0;
         |LIBERA #abom0002;
         |ACABA;
     |FINSI;

     |BORRA 020#abom0002.a;
     |LIBERA #abom0002;

     efFechaDocu  = #abom0002#4;
     enCodUbica   = #abom0002#10;  |HAZ LeeUbicaciones;
     aAlfa        = eaRutaDocumento + "/" + #abom0002#3;  |QBF aAlfa;

     |SI #abom0514#21 ! "C";
         |RFBORRA aAlfa;
     |SINO;
         |FBORRA aAlfa;
     |FINSI;

     |ABRE #abom0020;
     |SELECT #2#abom0020;
     #abom0020#3 = #abom0002#0;
     #abom0020#5 = #abom0002#1;
     |LEE 101#abom0020.=;
     |SI FSalida = 0;
         |BORRA 020#abom0020.a;
         |LIBERA #abom0020;
     |FINSI;
     |CIERRA #abom0020;

     |REFRESCACONTROL enGrid2;
     |FOCOTECLADO enGrid2;
|FPRC;

|PROCESO CogeTextoExpediente;
     |SI #abom0514#27 ! "S";  |ACABA;  |FINSI;

     |DBASE aBase;  |QBF aBase;
     aBase = aBase + "documentos/tmp";   |MKDIR aBase;

     aAbre = aBase + "/" + Usuario + ".txt";
     |FBORRA aAbre;

     |ABRE #abom0001;
     #abom0001#0 = #abom0002#0;
     |LEE 101#abom0001.=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |QBF aFicheroActu;
     efFechaDocu    = #abom0002#4;
     enCodUbica     = #abom0002#10;  |HAZ LeeUbicaciones;
     eaFicheroEdita = #abom0002#3;   |QBF eaFicheroEdita;
     enLineas       = 10;

     |HAZ LeeTexto;

     #abom0001#60 = "";
     #abom0001#61 = "";
     #abom0001#62 = "";
     #abom0001#63 = "";
     #abom0001#64 = "";
     #abom0001#65 = "";
     #abom0001#66 = "";
     #abom0001#67 = "";
     #abom0001#68 = "";
     #abom0001#69 = "";

     nCampo   = 59;
     aCarac10 = &10;
     aCarac13 = &13;
     aAbre = aBase + "/" + Usuario + ".txt";
     |XABRE "E", aAbre, nHandle;
     |SI FSalida ] 0;
         |XABRE "AB", aAbre, nHandle;
         |PARA;  |SI FSalida ] 0;  |HACIENDO;
              |XLEE nHandle, 1, aAlfa;
              |SI FSalida [ 0;  |SAL;  |FINSI;

              |SI aAlfa = aCarac10;  |O aAlfa = aCarac13;
                  |SI aAlfa ! aCarac10;
                      nCampo   = nCampo + 1;
                      #abom0001#nCampo# = aCadena;
                  |FINSI;
                  aCadena  = "";
              |SINO;
                  aCadena = aCadena + aAlfa;
              |FINSI;
         |SIGUE;
         |XCIERRA nHandle;
     |FINSI;

     #abom0001#55 = #abom0002#7;
     #abom0001#56 = #abom0002#8;
     #abom0001#57 = #abom0002#9;

     |GRABA 020#abom0001.a;
     |LIBERA #abom0001;
     |CIERRA #abom0001;

     |FBORRA aAbre;
|FPRC;

|PROCESO GrabaLeido;
     #abom0505#0 = #abom0020#0;
     |LEE 000#abom0505.=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     aAlfa = Usuario % 810;
     aAlfa = (aAlfa + "          ") % 110;

     #abom0511#0 = #abom0505#0;
     #abom0511#1 = aAlfa;
     |LEE 000#abom0511.=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     #abom0020#7 = "S";
     |GRABA 020#abom0020.a;
|FPRC;;

|PROCESO Boton3;
     |LEE 101#abom0002.=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     enCrear = 1;
     |HAZ EditaDocumento;

     |GRABA 020#abom0002.a;
     |LIBERA #abom0002;

     |ABRE #abom0020;
     |SELECT #2#abom0020;
     #abom0020#3 = #abom0002#0;
     #abom0020#5 = #abom0002#1;
     |LEE 101#abom0020.=;
     |SI FSalida = 0;
         |ABRE #abom0505;
         |ABRE #abom0511;
         |HAZ GrabaLeido;
         |CIERRA #abom0505;
         |CIERRA #abom0511;
         |LIBERA #abom0020;
     |FINSI;
     |CIERRA #abom0020;

     |SI enElEnvio = 1;
         |SI #abom0002#0 ! 0;
             |SI #abom0002#1 = 1;
                 |HAZ CogeTextoExpediente;
             |FINSI;
         |FINSI;
     |FINSI;

     |REFRESCACONTROL enGrid2;   |PULSATECLA;
|FPRC;

|PROCESO Baja2;
     #abom0002#0  = enExpediente;
     #abom0002#1  = nDoc;
|FPRC;

|PROCESO Alta2;
     #abom0002#0  = enExpediente;
     #abom0002#1  = 99999;
|FPRC;

|PROCESO SacaExt;
     |QBF aAlfa;

     aLon  = aAlfa % 0;
     aExt  = "";
     |PARA nCar = 1; |SI nCar [ aLon; |HACIENDO nCar = nCar + 1;
          nPos = -(nCar * 100 + 1)
          aCar = aAlfa % nPos;

          |SI aCar = ".";
              |SAL;
          |FINSI;

          aExt = aCar + aExt;
     |SIGUE;
|FPRC;

|PROCESO SacaNom;
     |QBF aAlfa;

     aLon    = aAlfa % 0;
     eaNomDes = "";
     |PARA nCar = 1; |SI nCar [ aLon; |HACIENDO nCar = nCar + 1;
          nPos = (nCar * 100) + 1;
          aCar = (aAlfa % nPos) > "";
          nAsc = &aCar;

          |SI nAsc < 48;  |O nAsc > 57;
              |SI nAsc < 65;  |O nAsc > 90;
                  aCar = "_";
              |FINSI;
          |FINSI;

          eaNomDes = eaNomDes + aCar;
     |SIGUE;

     eaNomDes = eaNomDes + "." + aExt;
|FPRC;

|PROCESO UnicoAdjunto;
     |LEE 000#abom0002.=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |HAZ DesactivaPantPrincipal;

     |VENTANA 0501, 2799, -1, 17, "Env¡o de correo";
     nSubVent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nSubVent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     aAlfa           = #abom0002#3;  |HAZ SacaExt;
     aAlfa           = #abom0002#2;  |HAZ SacaNom;
     eaFicheroEdita  = #abom0002#3;   |QBF eaFicheroEdita;
     efFechaDocu     = #abom0002#4;
     enCodUbica      = #abom0002#10;  |HAZ LeeUbicaciones;
     enNumExpeCorreo = #abom0002#0;
     enCambAdj       = 1;

     nRein = 0;
     |PARA;  |SI;  |HACIENDO;
          aOrigen  = eaRutaDocumento + "/" + eaFicheroEdita;
          aDestino = aPathPaq + eaNomDes;
          |COPIA_FICHERO aOrigen, aDestino;
          |SI FSalida = 0;
              |SAL;
          |FINSI;

          |PULSATECLA;
          |SLEEP 1;

          nRein = nRein + 1;
          |SI nRein > 5;
              nRein = -1;
              |SAL;
          |FINSI;
     |SIGUE;

     |SI nRein = -1;
         |MENAV "0000Algo pasa, no puedo copiar el documento con el nuevo nombre";
     |SINO;
         eaFichAdjunto   = aDestino;

         |SUB_EJECUTA_NP "abxz0001";
         enNumExpeCorreo = 0;
     |FINSI;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nSubVent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA nSubVent;

     |HAZ ActivaPantPrincipal;

     |REFRESCACONTROL enGrid2;
     |FOCOTECLADO enGrid2;
|FPRC;

|PROCESO abom0002Adj;
     efFechaDocu    = #abom0002#4;
     enCodUbica     = #abom0002#10;  |HAZ LeeUbicaciones;
     eaFicheroEdita = #abom0002#3;   |QBF eaFicheroEdita;

     aAlfa          = #abom0002#3;  |HAZ SacaExt;
     aAlfa          = #abom0002#2;  |HAZ SacaNom;

     aOrigen  = eaRutaDocumento + "/" + eaFicheroEdita;
     aDestino = aPathPaq + eaNomDes;
     |COPIA_FICHERO aOrigen, aDestino;

     #abom0002#12 = "";

     |GRABA 020#abom0002.a;
     |LIBERA #abom0002;
|FPRC;

|DEFBUCLE abom0002Adj;
     #abom0002#1;
     12;
     enExpediente, 000002, aVerde;
     enExpediente, 999999, aVerde;
     be;
     NULL, abom0002Adj;
|FIN;

|PROCESO VariosAdjuntos;
     |MENAV "0000A continuaci¢n seleccione con doble click los documentos a adjuntar";

     nDoc   = 2;
     nEnvio = 0;
     |PARA;  |SI;  |HACIENDO;
          |CONSULTA_F_CLAVE #1#abom0002, Baja2, Alta2;
          |SI FSalida = 0;
              |LEE 000#abom0002.=;
              |SI FSalida = 0;
                  |SI #abom0002#12 = aVerde;
                      #abom0002#12 = "";

                      nEnvio = nEnvio - 1;
                  |SINO;
                      #abom0002#12 = aVerde;

                      nEnvio = nEnvio + 1;
                  |FINSI;
                  |GRABA 020#abom0002.a;
                  |LIBERA #abom0002;
              |FINSI;
          |SINO;
              |SI nEnvio = 0;
                  |CONFI "0000NNo hay documentos seleccionados. Quiere cancelar";
                  |SI FSalida = 0;
                      |SAL;
                  |FINSI;
              |SINO;
                  |CONFI "0000SConforme con la selecci¢n realizada.";
                  |SI FSalida = 0;
                      |SAL;
                  |FINSI;
              |FINSI;
          |FINSI;
     |SIGUE;

     |SI nEnvio = 0;
         |ACABA;
     |FINSI;

     aDir = aPathPaq + "*.*";
     |_PDIR aDir;
     |PARA; |SI FSalida = 0; |HACIENDO;
            |FBORRA aDir;

            |_SDIR aDir;
     |SIGUE;

     |PULSATECLA;

     |BUCLE abom0002Adj;

     fFecha = ~;
     |HORA aHora;
     eaPaq   = (fFecha % -104) + (fFecha % 402) + (fFecha % 102);
     eaPaq   = eaPaq + (aHora % 102) + (aHora % 402) + (aHora % 702);

     aPathTar = aDBase + "documentos/correos/paquetes/";

     nSwTgzOk = 0;
     aFicTar  = aPathTar + eaPaq + ".tar" + " " + (("00000000" + enExpediente) % -108) + "/*";

     |ATAR aFicTar, aPathTar;
     |SI FSalida = 0;
          |PULSATECLA;
          aFicTar = aPathTar + eaPaq + ".tar";
          |COMPRIME aFicTar;
          |SI FSalida = 0;
               aBorra = aPathTar + eaPaq + ".tar";
               |FBORRA aBorra;

               nSwTgzOk = 1;
          |FINSI;
     |FINSI;

     aDir = aPathPaq + "*.*";
     |_PDIR aDir;
     |PARA; |SI FSalida = 0; |HACIENDO;
            |FBORRA aDir;

            |_SDIR aDir;
     |SIGUE;

     |PULSATECLA;

     |SI nSwTgzOk ! 1;
         |MENAV "0000Ha habido un error a generar el paquete. Vuelva a intentarlo.";
         |ACABA;
     |FINSI;

     |HAZ DesactivaPantPrincipal;

     |VENTANA 0501, 2799, -1, 17, "Env¡o de correo";
     nSubVent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nSubVent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     efFechaDocu     = fFecha;
     eaFichAdjunto   = aPathTar + eaPaq + ".tgz";
     eaPaq           = eaPaq + ".tgz";
     enNumExpeCorreo = enExpediente;
     enCambAdj       = 1;

     |SUB_EJECUTA_NP "abxz0001";

     enNumExpeCorreo = 0;
     enCambAdj       = 0;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nSubVent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA nSubVent;

     |HAZ ActivaPantPrincipal;

     |REFRESCACONTROL enGrid2;
     |FOCOTECLADO enGrid2;
|FPRC;

|PROCESO Boton4;
     |SI enExped ! 0;
          aPopup1  = 0;
          aPopup2  = 3;
          aPopup3  = "Adjuntar el documento seleccionado";
          aPopup4  = "Adjuntar varios documentos";
          aPopup5  = "Nuevo correo";

          |PARA;  |SI;  |HACIENDO;
               IaPopup  = aPopup1  <-;
               |ESPECIFICA "TRACKPOPUP", aPopup;
               |SI FSalida ! 0;
                   nOpcion = FSalida;
                   |SI FSalida = 1;  |SAL;  |FINSI;
                   |SI FSalida = 2;  |SAL;  |FINSI;
                   |SI FSalida = 3;  |SAL;  |FINSI;
               |FINSI;
          |SIGUE;

          |SI nOpcion = 1;
              |SI #abom0514#27 = "S";  |Y #abom0002#1 = 1;  |Y #abom0002#0 ! 0;
                  |MENAV "0000No procede enviar el documento seleccionado";
              |SINO;
                  |HAZ UnicoAdjunto;
              |FINSI;
          |FINSI;

          |SI nOpcion = 2;  |HAZ VariosAdjuntos;  |FINSI;

          |SI nOpcion = 3;
              |VENTANA 0501, 2799, -1, 17, "Env¡o de correo nuevo";
              nSubVent = FSalida;

              aVent1 = -1;
              aVent2 = -1;
              aVent3 = 0;
              aVent4 = nSubVent;
              aVent5 = "MODAL";
              |ESPECIFICA "ESTADO_VENTANA", aVent;

              efFechaDocu    = ~;
              eaFichAdjunto  = "";
              enExped        = #abom0002#0;
              enNumExpeCorreo = enExped;

              |SUB_EJECUTA_NP "abxz0001";

              aVent1 = -1;
              aVent2 = -1;
              aVent3 = 0;
              aVent4 = nSubVent;
              aVent5 = "MODELESS";
              |ESPECIFICA "ESTADO_VENTANA", aVent;

              |FINVENTANA nSubVent;
              |FOCOTECLADO enGrid2;
          |FINSI;
     |SINO;
         |HAZ UnicoAdjunto;
     |FINSI;

     |PULSATECLA;
|FPRC;

|PROCESO Boton6;
     |LEE 000#abom0002.=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |HAZ CogeMac;

     ||Lee si tiene configurado el PATH por MAC.

     |ABRE #abom0021
     |DDEFECTO #abom0021;
     #abom0021#0 = eaDSMAC;
     |LEE 000#abom0021.=;
     |SI FSalida ! 0;
          aMensaje = "0000No ha definido el directorio donde ubicar el <portal>";
          |MENAV aMensaje;
          |ACABA;
     |FINSI;
     aPath = #abom0021#1;
     |QBF aPath;

     |SI aPath = "";
          aMensaje = "0000El directorio donde ubicar el <portal> esta vacio";
          |MENAV aMensaje;
          |ACABA;
     |FINSI;
     |CIERRA #abom0021;

     aPortal = aPath + "portal\";
     |RMKDIR aPortal;

     aBorra = aPortal + "*.*";
     |R_PDIR aBorra;
     |PARA; |SI FSalida = 0; |HACIENDO;
          |RFBORRA aBorra;
          |R_SDIR aBorra;
     |SIGUE;

     efFechaDocu    = #abom0002#4;
     enCodUbica     = #abom0002#10;  |HAZ LeeUbicaciones;
     eaFicheroEdita = #abom0002#3;   |QBF eaFicheroEdita;
     eaFichAdjunto  = eaRutaDocumento + "/" + eaFicheroEdita;

     aOrigen   = eaFichAdjunto;
     |SI #abom0514#21 ! "C";
          |XABRE "EC", aOrigen, nHandle;
     |SINO;
          |XABRE "E", aOrigen, nHandle;
     |FINSI;
     |SI FSalida < 0;
         |MENAV "0000 No encuentro el documento en el directorio origen";
         |ACABA;
     |FINSI;

     ||ARA38
     aOrigen   = eaFichAdjunto;
     aDestino  = aPortal + eaFicheroEdita;
     |SI #abom0514#21 = "C";
          aIPRemoto = "";
          |IP_REMOTO aIPRemoto;   |QBT aIPRemoto;
          |SI aIPRemoto ! "";
               |ENVIA_FICHERO aOrigen, aDestino;
          |SINO;
               |COPIA_FICHERO aOrigen, aDestino;
          |FINSI;
     |SINO;
          |COPIA_FICHERO aOrigen, aDestino;
     |FINSI;
|FPRC;

|PROCESO Boton5;
     |LEE 101#abom0002.=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |HAZ DesactivaPantPrincipal;

     |VENTANA 1010, 1490, -1, 17, "Cambiar descripci¢n";
     nSubVent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nSubVent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     E_inf     = "";
     E_sup     = "60";
     aAlfa     = #abom0002#2;
     aAlfa     = 1320 ? 1;

     #abom0002#2      = aAlfa;

     |GRABA 020#abom0002.a;
     |LIBERA #abom0002;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nSubVent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA nSubVent;

     |HAZ ActivaPantPrincipal;

     |REFRESCACONTROL enGrid2;  |PULSATECLA;
|FPRC;

|PROCESO EventoPanta;
     |SI FSalida = 1;  |O FSalida = 2;
         |LEE 000#abom0002.=;
         |SI FSalida = 0;
             |ESTADO_CONTROL nBoton2, "ENABLE";
             |ESTADO_CONTROL nBoton3, "ENABLE";
             |ESTADO_CONTROL nBoton4, "ENABLE";
             |ESTADO_CONTROL nBoton5, "ENABLE";
             |ESTADO_CONTROL nBoton6, "ENABLE";
         |SINO;
             |ESTADO_CONTROL nBoton2, "DISABLE";
             |ESTADO_CONTROL nBoton3, "DISABLE";
             |ESTADO_CONTROL nBoton4, "DISABLE";
             |ESTADO_CONTROL nBoton5, "DISABLE";
             |ESTADO_CONTROL nBoton6, "DISABLE";
         |FINSI;

         |SI #abom0514#27 = "S";  |Y #abom0002#1 = 1;  |Y #abom0002#0 ! 0;
||             |ESTADO_CONTROL nBoton4, "DISABLE";
         |FINSI;
     |FINSI;

     |SI FSalida = 4;
         |HAZ Boton3;
     |FINSI;
|FPRC;

|PROGRAMA;
     |HAZ LeeUnidadBasico;

     enExpediente = 0;
     |SI enExped ! 0;
         enExpediente = enExped;
     |FINSI;

     |DBASE aDBase;
     aPathPaq = aDBase + "documentos";                                  |MKDIR aPathPaq;
     aPathPaq = aPathPaq + "/correos";                                  |MKDIR aPathPaq;
     aPathPaq = aPathPaq + "/paquetes";                                 |MKDIR aPathPaq;
     aPathPaq = aPathPaq + "/" + (("00000000" + enExpediente) % -108);  |MKDIR aPathPaq;
     aPathPaq = aPathPaq + "/";

     |PINPA #0#abxz0055;  enPanta0 = FSalida;

     |SI enExped = 0;
         |CONTROL_SIMPLE 0, "BOTON,{{193}} Nuevo documento", 2602, 2715, Boton1;
         nBoton1  = FSalida;

         |CONTROL_SIMPLE 0, "BOTON,{{197}} Editar documento", 2618, 2731, Boton3;
         nBoton3 = FSalida;

         |CONTROL_SIMPLE 0, "BOTON,{{157}} Enviar correo", 2634, 2747, Boton4;
         nBoton4  = FSalida;

         |CONTROL_SIMPLE 0, "BOTON,{{197}} Modificar descripci¢n", 2650, 2763, Boton5;
         nBoton5  = FSalida;

         |CONTROL_SIMPLE 0, "BOTON,{{197}}Portal", 2666, 2679, Boton6;
         nBoton6  = FSalida;

         |CONTROL_SIMPLE enPanta0, "BOTON,{{199}} Borrar documento", 2780, 2798, Boton2;
         nBoton2  = FSalida;
     |SINO;
         |CONTROL_SIMPLE 0, "BOTON,{{193}} Nuevo documento", 3202, 3315, Boton1;
         nBoton1  = FSalida;

         |CONTROL_SIMPLE 0, "BOTON,{{197}} Editar documento", 3218, 3331, Boton3;
         nBoton3 = FSalida;

         |CONTROL_SIMPLE 0, "BOTON,{{157}} Enviar correo", 3234, 3347, Boton4;
         nBoton4  = FSalida;

         |CONTROL_SIMPLE 0, "BOTON,{{197}} Modificar descripci¢n", 3250, 3363, Boton5;
         nBoton5  = FSalida;

         |CONTROL_SIMPLE 0, "BOTON,{{197}} Portal", 3266, 3379, Boton6;
         nBoton6  = FSalida;

         |CONTROL_SIMPLE enPanta0, "BOTON,{{199}} Borrar documento", 2780, 2798, Boton2;
         nBoton2  = FSalida;
     |FINSI;

     |ESTADO_CONTROL nBoton2, "DISABLE";
     |ESTADO_CONTROL nBoton3, "DISABLE";
     |ESTADO_CONTROL nBoton4, "DISABLE";
     |ESTADO_CONTROL nBoton5, "DISABLE";
     |ESTADO_CONTROL nBoton6, "DISABLE";

     nRango = 4 + 8 + 16 + 32 + 128;
     nDoc   = 1;

     |LINEAL_SIMPLE #1#abom0002, nRango, 0602, 2698, Baja2, Alta2, EventoPanta;
     enGrid2  = FSalida;

     aEsc1  = &255 + "806";
     aEsc2  = &255 + "807";
     aRetu  = &255 + "802";
     |PARA; |SI;  |HACIENDO;
         |PULSATECLA;
         FSalida = "::{{001}}Salir," + enGrid2;
         |LEETECLA aTecla;
         |SI aTecla = aEsc1; |SAL;  |FINSI;
         |SI aTecla = aEsc2; |SAL;  |FINSI;
         |SI aTecla = aRetu; |SAL;  |FINSI;
     |SIGUE;

     |DESTRUYECONTROL enGrid2;

     |FIN_CONTROL_WINDOWS nBoton1;
     |FIN_CONTROL_WINDOWS nBoton2;
     |FIN_CONTROL_WINDOWS nBoton3;
     |FIN_CONTROL_WINDOWS nBoton4;
     |FIN_CONTROL_WINDOWS nBoton5;
     |FIN_CONTROL_WINDOWS nBoton6;

     |PULSATECLA;
     |PULSATECLA;
     |PULSATECLA;
     |PULSATECLA;
     |PULSATECLA;
|FPRO;

|PROCESO Tipo80Fxz55; |TIPO 80;
     |ABRE #abom0514;
     |LEE 000#abom0514.p;
     |SI FSalida ! 0;  |DDEFECTO #abom0514;  |FINSI;
     |CIERRA #abom0514;

     |ABRET #abom0002;

     |SI enExped = 0;
         FSalida = 2799;
     |SINO;
         FSalida = 3399;
     |FINSI;
|FPRC;

|PROCESO Tipo90Fxz55; |TIPO 90;
     |CIERRAT #abom0002;
|FPRC;
