|FICHEROS;
     abom0005 #0;
     abom0001 #1;
     abom0006 #4;
     abom0007 #7;
     abom0010 #10;
     abom0011 #111;
     abom0505 #5;
     abom0511 #11,MANTE;
     abom0512 #12;
     abow0005 #100;
     abow0008 #101;
     abow0019 #119,MANTE;
     asconcli #20;
     asconcep #21;
     asempres #22;
     asclient #23;
     abom0506 #506;
     asconlin #17;
|FIN;

|VARIABLES;
     nFichero  = 0;
     nCliente  = 0;
     aSeccion  = "";
     aConcepto = "";

     nError    = 0;
     nModo     = 0;
     nCampo    = 0;
     nCampo1   = 0;
     nEntrada  = 0;

     nContador = 0;
     nSwCab    = 0;
     nVariable = 0;
     nHora     = 0;
     nMinu     = 0;
     nNume     = 0;
     nSwPase   = 0;
     nCoste    = 0;
     nSwVaria  = 0;

     aAlfa     = "";
     aAlfa1    = "";
     aFichero  = "";
     aSecc     = "";
     aConc     = "";
     aAviso    = "";
     aTipoA    = "";
     aPlazo    = "";

     fFecha    = @;
     fDFecha   = @;
     fHFecha   = @;

     &enPrecio      = 0;
     &enCliente     = 0;
     &enExpediente  = 0;
     &efFecha       = @;
     &eaServicio    = "";
     &enPersona     = 0;
     &enLinea       = 0;
     &enVersion     = 0;

     &enDentro      = 0;
     &eaNombreList  = "";
     &enCuadroIni   = 0;
     &enCuadroFin   = 0;
     &enCampoAncho  = 0;
     &enModoEdita   = 0;
     &enTotalVaria  = 0;
{101}&epPuntEdita   = "";
|FIN;

|PROCESO MayusM0005;  |TIPO 6;
     #0Campo = #0Campo > #0Campo;  |PINTA  #0Campo;
|FPRC;

|PROCESO DentroM0005;  |TIPO 7;
     enDentro = 0;
|FPRC;

|PROCESO LeePersona5;  |TIPO 7;
     enDentro = 1;

     |SI #0#2 ! 0;  |ACABA;  |FINSI;

     |ABRE #506;
     #506#0 = #1#13;
     |LEE 040#506=;
     |SI FSalida ! 0;  |DDEFECTO #506;  |FINSI;
     |CIERRA #506;

     #0#5 = #506#2;   |PINTA #0#5;
     #0#6 = #506#3;   |PINTA #0#6;

     aAlfa = Usuario % 810;
     aAlfa = (aAlfa + "          ") % 110;

     |ABRE #5;
     |ABRE #11;
     |SELECT #2#11;

     #11#0 = 0;
     #11#1 = aAlfa;
     |LEE 040#11];
     |SI FSalida = 0;  |Y #11#1 = aAlfa;
         #5#0 = #11#0;
         |LEE 040#5=;
         |SI FSalida ! 0;  |DDEFECTO #5;  |FINSI;

         #0#2 = #11#0;
         #0#3 = #5#1;
     |FINSI;
     |CIERRA #5;
     |CIERRA #11;

     |PINTA #0#2;
|FPRC;

|PROCESO PonTReport;
     |ABRE #5;
     #5#0 = #0#2;
     |LEE 040#5=;
     |SI FSalida ! 0;  |DDEFECTO #5;  |FINSI;
     |CIERRA #5;

     #0#3  = #5#1;
     #0#17 = (#5#19 / (60 / #5#20));
|FPRC;

|RUTINA ClaveBaja020;
     #20#0 = #1#3;
     #20#1 = "    ";
     #20#2 = "  ";
|FRUT;

|RUTINA ClaveAlta020;
     #20#0 = #1#3;
     #20#1 = "zzzz";
     #20#2 = "zz";
|FRUT;

|PROCESO BuscaConcep;  |TIPO 0;    ||si pulsa F1 accede a conceptos / clientes
     |SI FSalida ! 9; |ACABA; |FINSI;

     |PUSHV 0501 2380;
     |PINPA #0#20;
     |PINTA 926, #1#3;

     aSecc = #0#5;
     aConc = #0#6;

     |ENTLINEAL #1#20, -3, 4, 19, 0, ClaveBaja020, ClaveAlta020;

     |POPV;

     #0#5  = aSecc;  |PINTA #0#5;
     #0#6  = aConc;  |PINTA #0#6;
|FPRC;

|PROCESO RecogeTexto;
     |ABRE #7;
     #7#0 = #0#0;
     #7#1 = #0#1;
     #7#2 = 1;
     |LEE 040#7];
     |SI FSalida = 0;  |Y #7#0 = #0#0; |Y #7#1 = #0#1;
         |CIERRA #7;
         |ACABA;
     |FINSI;

     |ABRE #17;
     |PARA nVariable = 1;  |SI nVariable [ 100;  |HACIENDO nVariable = nVariable + 1;
           #17#0 = nFichero;
           #17#1 = nCliente;
           #17#2 = aSeccion;
           #17#3 = aConcepto;
           #17#4 = nVariable;
           |LEE 040#17=;
           |SI FSalida = 0;
               #7#0 = #0#0;
               #7#1 = #0#1;
               #7#2 = nVariable;
               #7#3 = #17#5;
               |GRABA 020#7n;
               |LIBERA #7;
           |FINSI;
     |SIGUE;

     |CIERRA #7;
     |CIERRA #17;
|FPRC;

|PROCESO LeeConcep;  |TIPO 0;
     |ABRE #21;
     #21#0 = #0#5;
     #21#1 = #0#6;
     |LEE 040#21=;
     |SI FSalida ! 0;  |DDEFECTO #21;  |FINSI;
     |CIERRA #21;

     |C_M #0#16, 1, "S";
     |C_M #0#18, 1, "N";
     |SI #21#34 = "N";
         |C_M #0#16, 1, "N";
         |C_M #0#18, 1, "S";
     |FINSI;

     aAlfa = #0#7 + #0#8 + #0#9;   |QBT aAlfa;
     |SI aAlfa ! "";  |ACABA;  |FINSI;

     |ABRE #20;
     #20#0 = #1#3;
     #20#1 = #0#5;
     #20#2 = #0#6;
     |LEE 040#20=;
     |SI FSalida = 0;
         #0#7      = #20#7;
         #0#8      = #20#8;
         #0#9      = #20#9;

         nFichero  = 2;
         nCliente  = #20#0;
         aSeccion  = #20#1;
         aConcepto = #20#2;
         |HAZ RecogeTexto;
     |SINO;
         #0#7 = #21#16;
         #0#8 = #21#17;
         #0#9 = #21#18;

         nFichero  = 1;
         nCliente  = 0;
         aSeccion  = #21#0;
         aConcepto = #21#1;
         |HAZ RecogeTexto;
     |FINSI;
     |CIERRA #20;

     |PINTA #0#7;
     |PINTA #0#8;
     |PINTA #0#9;
|FPRC;

|PROCESO EditaAsuntoM005;  |TIPO 7;
     enTotalVaria  = 99;
     eaNombreList  = "";
     enCuadroIni   = 1030;
     enCuadroFin   = 1879;
     enCampoAncho  = 50;
     enModoEdita   = 0;

     |ABRE #7;

     #7#0 = #0#0;  #7#1 = #0#1;  #7#2 = 1;
     |LIBERA #7;
     |LEE 101#7=;
     |SI FSalida ! 0;
         #7#3 = #0#7;  |GRABA 020#7n;
     |FINSI;

     #7#0 = #0#0;  #7#1 = #0#1;  #7#2 = 2;
     |LIBERA #7;
     |LEE 101#7=;
     |SI FSalida ! 0;
         #7#3 = #0#8;  |GRABA 020#7n;
     |FINSI;

     #7#0 = #0#0;  #7#1 = #0#1;  #7#2 = 3;
     |LIBERA #7;
     |LEE 101#7=;
     |SI FSalida ! 0;
         #7#3 = #0#9;  |GRABA 020#7n;
     |FINSI;

     |PARA nVariable = 1;  |SI nVariable [ 100;  |HACIENDO nVariable = nVariable + 1;
           IepPuntEdita  = epPuntEdita1 <-;
           IepPuntEdita = IepPuntEdita + (nVariable - 1);

           #7#0 = #0#0;
           #7#1 = #0#1;
           #7#2 = nVariable;
           |LIBERA #7;
           |LEE 101#7=;
           |SI FSalida ! 0;
               epPuntEdita  = "";
           |SINO;
               epPuntEdita  = #7#3;
               |BORRA 020#7a;
           |FINSI;
     |SIGUE;

     |PUSHV  0501 2380;
     |CUADRO 0829 2080;
     |BLANCO 0930 1979;
     |ATRI S;  |PINTA 0936, "Introduzca el Texto del Seguimiento";  |ATRI s;

     |HAZ Edita;
     |POPV;

     |PARA nVariable = 1;  |SI nVariable [ 100;  |HACIENDO nVariable = nVariable + 1;
           IepPuntEdita = epPuntEdita1 <-;
           IepPuntEdita = IepPuntEdita + (nVariable - 1);

           |DDEFECTO #7;
           #7#0 = #0#0;
           #7#1 = #0#1;
           #7#2 = nVariable;
           |LIBERA #7;
           |LEE 1010#7=;
           |SI FSalida ! 0;  |GRABA 020#7b;  |FINSI;
           #7#3 = epPuntEdita;
           |GRABA 020#7a;

           |SI nVariable = 1;  #0#7 = #7#3;  |FINSI;
           |SI nVariable = 2;  #0#8 = #7#3;  |FINSI;
           |SI nVariable = 3;  #0#9 = #7#3;  |FINSI;
     |SIGUE;

     |PARA nVariable = 100;  |SI nVariable ] 1;  |HACIENDO nVariable = nVariable - 1;
           #7#0 = #0#0;
           #7#1 = #0#1;
           #7#2 = nVariable;
           |LIBERA #7;
           |LEE 101#7=;
           aAlfa = #7#3;  |QBF aAlfa;

           |SI aAlfa ! "";  |SAL;  |FINSI;

           |BORRA 020#7a;
     |SIGUE;

     |CIERRA #7;

     |PINTA #0#7;
     |PINTA #0#8;
     |PINTA #0#9;
|FPRC;

|PROCESO Agenda;  |TIPO 0;
     |SI #0#12 ! "S";  |ACABA;  |FINSI;

     |HORA aAlfa;
     aAlfa1 = aAlfa % 102;  #0#21 = aAlfa1;
     aAlfa1 = aAlfa % 402;  #0#22 = aAlfa1;
     aAlfa1 = aAlfa % 702;  #0#27 = aAlfa1;

     |PUSHV  0501 2380;
     |CUADRO 1020 2375;
     |BLANCO 1121 2274;
     |ATRI S;  |PINTA 1125, "Introduzca datos aviso de la Agenda";  |ATRI s;
     |PINTA 1321, "Aviso S/N :             Tipo Aviso : ";
     |PINTA 1421, "Fecha ....: ";
     |PINTA 1521, "Hora .....: ";
     |PINTA 1621, "Minuto ...: ";
     |PINTA 1721, "Plazo S/N : ";

     |PINTA 1333, aAviso;
     |PINTA 1358, aTipoA;
     |PINTA 1433, fFecha;
     |PINTA 1533, nHora;
     |PINTA 1633, nMinu;
     |PINTA 1733, aPlazo;

     |ET aAviso;
         E_sup  = "1";
         E_inf  = "SN";
         aAviso = 1333 ? 1;
         |SI FSalida = 1;  |VETE aAviso;  |FINSI;
         |SI FSalida = 7;  |VETE aAviso;  |FINSI;
         |SI FSalida = 2;  |VETE aAviso;  |FINSI;

     |ET aTipoA;
         |ATRI R;
         |PINTA 1723, "-0- Solo el dia de Aviso.  -X- Todos los Miercoles.";
         |PINTA 1823, "-T- Todos los Dias.        -J- Todos los Jueves.   ";
         |PINTA 1923, "-2- Dos Semanas Antes.     -V- Todos los Viernes.  ";
         |PINTA 2023, "-1- Una Semanas Antes.     -S- Todos los Sabados.  ";
         |PINTA 2123, "-L- Todos los Lunes.       -D- Todos los Domingos. ";
         |PINTA 2223, "-M- Todos los Martes.                              ";
         |ATRI 0;

         E_sup  = "1";
         E_inf  = "012TLMXJVSD";
         aTipoA = 1358 ? 1;

         |BLANCO 1723 2274;
         |PINTA 1721, "Plazo S/N : ";
         |PINTA 1733, aPlazo;

         |SI FSalida = 1;  |VETE aTipoA;  |FINSI;
         |SI FSalida = 7;  |VETE aTipoA;  |FINSI;
         |SI FSalida = 2;  |VETE aAviso;  |FINSI;

     |ET fFecha;
         |SI fFecha = "00.00.0000";  fFecha = ~;  |FINSI;
         fFecha = 1433 ? 1;
         |SI FSalida = 1;  |VETE fFecha;  |FINSI;
         |SI FSalida = 7;  |VETE fFecha;  |FINSI;
         |SI FSalida = 2;  |VETE aTipoA;  |FINSI;
         |SI fFecha = "00.00.0000";
             |MENAV  "       Fecha Incorrecta.";
             |VETE fFecha;
         |FINSI;

     |ET nHora;
          E_sup  = "23";
          E_inf  = "00";
          nHora = 1533 ? 1;
          |SI FSalida = 1;  |VETE nHora;   |FINSI;
          |SI FSalida = 7;  |VETE nHora;   |FINSI;
          |SI FSalida = 2;  |VETE fFecha;  |FINSI;

     |ET nMinu;
          E_sup  = "59";
          E_inf  = "00";
          nMinu = 1633 ? 1;
          |SI FSalida = 1;  |VETE nMinu;  |FINSI;
          |SI FSalida = 7;  |VETE nMinu;  |FINSI;
          |SI FSalida = 2;  |VETE nHora;   |FINSI;

     |ET Plazo;
         E_sup  = "1";
         E_inf  = "SN";
         aPlazo = 1733 ? 1;
         |SI FSalida = 1;  |VETE Plazo;   |FINSI;
         |SI FSalida = 7;  |VETE Plazo;   |FINSI;
         |SI FSalida = 2;  |VETE nMinu;   |FINSI;

      |CONFI "2400SConforme con lo introducido : ";
      |SI FSalida ! 0;
          |POPV;
          |ERROR;
          |ACABA;
      |FINSI;

      |POPV;

      |ABRE #4;
      |DDEFECTO #4;
      #4#0  = #0#0;
      #4#1  = #0#4;
      #4#2  = #0#21;
      #4#3  = #0#22;
      #4#18 = #0#27;
      |LEE 101#4=;
      |SI FSalida ! 0;
          |DDEFECTO #4;
          #4#0  = #0#0;
          #4#1  = #0#4;
          #4#2  = #0#21;
          #4#3  = #0#22;
          #4#18 = #0#27;
          |GRABA 020#4b;
      |FINSI;

      #4#4  = aAviso;
      #4#5  = fFecha;
      #4#6  = nHora;
      #4#7  = nMinu;
      #4#8  = #0#2;
      #4#9  = #0#3;
      #4#17 = aTipoA;
      #4#19 = aPlazo;

      |ABRE #7;
      |PARA nCampo = 1;  |SI nCampo [ 6;  |HACIENDO nCampo = nCampo + 1;
            nCampo1 = nCampo + 9;
            #7#0 = #0#0;  #7#1 = #0#1;  #7#2 = nCampo;  |LEE 040#7=;
            |SI FSalida = 0;  #4nCampo1 = #7#3;  |FINSI;
      |SIGUE;
      |CIERRA #7;

      |GRABA 020#4a;
      |LIBERA #4;
      |CIERRA #4;
|FPRC;

|PROCESO PonDefectos_5;
     |ABRE #12;
     #12#0 = #0Campo;
     |LEE 040#12=;
     |SI FSalida ! 0;  |DDEFECTO #12;  |FINSI;
     |CIERRA #12;

     #0#11 = #12#3;
     #0#12 = #12#2;
     #0#13 = #12#4;
     #0#14 = #12#5;
     #0#15 = #12#6;
     #0#19 = "N";
     #0#20 = "N";
|FPRC;

|PROCESO Tipo2M0005;  |TIPO 2;
     nEntrada = 0 +. 1;
     nModo    = (FEntrada / 100) ! 0;

     |SI #0#19 = "S";  |Y #0#13 = "S";
         |SI nEntrada ! 1;  |MENAV "     Linea Minutada.";  |FINSI;
         |ERROR;
         |ACABA;
     |FINSI;

     |SI #0#20 = "S";  |Y #0#13 = "S";
         |SI nEntrada ! 1;  |MENAV "     Linea Facturada.";  |FINSI;
         |ERROR;
         |ACABA;
     |FINSI;

     |SI #0#13 = "N";
         #0#19 = "S";   |PINTA #0#19;
         #0#20 = "S";   |PINTA #0#20;
     |SINO;
         #0#19 = "N";   |PINTA #0#19;
         #0#20 = "N";   |PINTA #0#20;
     |FINSI;

     |ABRE #21;
     #21#0 = #0#5;
     #21#1 = #0#6;
     |LEE 040#21=;
     |SI FSalida ! 0;  |DDEFECTO #21;  |FINSI;
     |CIERRA #21;

     |SI #21#34 = "S";
         #0#18 = #0#16 * #0#17;
     |FINSI;

     |SI #0#12 = "S";
         |SI nEntrada ! 1;
             |ABRE #4;
             #4#0  = #0#0;
             #4#1  = #0#4;
             #4#2  = #0#21;
             #4#3  = #0#22;
             #4#18 = #0#27;
             |LEE 101#4=;
             |SI FSalida = 0;
                 |SI nModo ! 3;
                     |CONFI "0000NUsted va a Modificar un Seguimiento con Agenda. Desea Continuar. ";
                 |SINO;
                     |CONFI "0000NUsted va a Borrar un Seguimiento con Agenda. Desea Borrarla. ";
                 |FINSI;

                 |SI FSalida ! 0;
                     |LIBERA #4;
                     |CIERRA #4;
                     |ERROR;
                     |ACABA;
                 |FINSI;

                 aAviso = #4#4;
                 aTipoA = #4#17;
                 fFecha = #4#5;
                 nHora  = #4#6;
                 nMinu  = #4#7;
                 aPlazo = #4#19;
                 |BORRA 020#4a;
             |FINSI;
             |LIBERA #4;
             |CIERRA #4;
         |FINSI;
     |FINSI;

     |SI nModo = 3;
         |ABRE #7;
         |PARA nCampo = 1;  |SI nCampo [ 99;  |HACIENDO nCampo = nCampo + 1;
               #7#0 = #0#0;
               #7#1 = #0#1;
               #7#2 = nCampo;
               |LIBERA #7;
               |LEE 101#7=;
               |SI FSalida = 0;  |BORRA 020#7a;  |FINSI;
         |SIGUE;
         |CIERRA #7;
     |FINSI;
|FPRC;

|| ************************************************************************
||                  PROCESO PARA PONER EN COLORES LAS LINEAS
|| ************************************************************************

|PROCESO Tipo5M0005;  |TIPO 5;
     #0#0 = #1#0;
     #0#1 = 1;
     |LEE 040#0];
     |SI FSalida = 0;  |Y #0#0 = #1#0;
         |SI #0#19 = "S";
             |LINEAS_BI_ATRI #0#19, N, I, 179;
         |SINO;
             |LINEAS_BI_ATRI #0#19, I, N, 179;
         |FINSI;
     |SINO;
         |LINEAS_BI_ATRI #0#19, I, N, 179;
     |FINSI;
|FPRC;

|PROCESO CargaW0005;
     nSwCab    = 1;

     |ABRE #7;
     |PARA nCampo = 1;  |SI nCampo [ 99;  |HACIENDO nCampo = nCampo + 1;
           #7#0 = #0#0;
           #7#1 = #0#1;
           #7#2 = nCampo;
           |LEE 040#7=;
           |SI FSalida = 0;
               nContador = nContador + 1;
               |SI nSwCab = 1;
                   #100#0    = nContador;
                   #100#1    = #0#2;
                   #100#2    = #0#4;
                   #100#3    = #0#5;
                   #100#4    = #0#6;
                   #100#5    = #7#3;
                   #100#6    = #0#10;
                   #100#7    = #0#0;
                   #100#8    = #0#1;
                   #100#9    = #0#19;
                   #100#10   = #0#20;
                   |GRABA 020#100n;
                   nSwCab = 0;
               |SINO;
                   |DDEFECTO #100;
                   #100#0 = nContador;
                   #100#5 = #7#3;
                   #100#7 = #0#0;
                   #100#8 = #0#1;
                   |GRABA 020#100n;
               |FINSI;
               |LIBERA #100;
           |SINO;
               |SI nSwCab = 1;
                   nContador = nContador + 1;
                   #100#0    = nContador;
                   #100#1    = #0#2;
                   #100#2    = #0#4;
                   #100#3    = #0#5;
                   #100#4    = #0#6;
                   #100#5    = "";
                   #100#6    = #0#10;
                   #100#7    = #0#0;
                   #100#8    = #0#1;
                   #100#9    = #0#19;
                   #100#10   = #0#20;
                   |GRABA 020#100n;
               |FINSI;
               |SAL;
           |FINSI;
     |SIGUE;
     |CIERRA #7;
|FPRC;

|DEFBUCLE abom0005_1;
     #0#1;
     ;
     #1#0, 001;
     #1#0, 999;
     ;
     NULL, CargaW0005;
|FIN;

|PROCESO CargaW0008;
     |ABRE #21;
     #21#0 = #0#5;
     #21#1 = #0#6;
     |LEE 040#21=;
     |SI FSalida ! 0;  |DDEFECTO #21;  |FINSI;
     |CIERRA #21;

     |SI #21#3 = "A";  |ACABA;  |FINSI;

     nSwCab    = 1;

     |ABRE #7;
     |PARA nCampo = 1;  |SI nCampo [ 99;  |HACIENDO nCampo = nCampo + 1;
           #7#0 = #0#0;
           #7#1 = #0#1;
           #7#2 = nCampo;
           |LEE 040#7=;
           |SI FSalida = 0;
               nContador = nContador + 1;
               |SI nSwCab = 1;
                   #101#0    = nContador;
                   #101#1    = #0#2;
                   #101#2    = #0#4;
                   #101#3    = #0#5;
                   #101#4    = #0#6;
                   #101#5    = #7#3;
                   #101#6    = #0#10;
                   #101#7    = #0#0;
                   #101#8    = #0#1;
                   #101#9    = #0#19;
                   #101#10   = #0#20;
                   #101#11   = #0#18;
                   #101#12   = #0#14;
                   |GRABA 020#101n;
                   nSwCab = 0;
               |SINO;
                   |DDEFECTO #101;
                   #101#0  = nContador;
                   #101#5  = #7#3;
                   #101#7  = #0#0;
                   #101#8  = #0#1;
                   #101#11 = #0#18;
                   #101#12 = #0#14;
                   |GRABA 020#101n;
               |FINSI;
               |LIBERA #101;
           |SINO;
               |SI nSwCab = 1;
                   nContador = nContador + 1;
                   #101#0    = nContador;
                   #101#1    = #0#2;
                   #101#2    = #0#4;
                   #101#3    = #0#5;
                   #101#4    = #0#6;
                   #101#5    = "";
                   #101#6    = #0#10;
                   #101#7    = #0#0;
                   #101#8    = #0#1;
                   #101#9    = #0#19;
                   #101#10   = #0#20;
                   #101#11   = #0#18;
                   #101#12   = #0#14;
                   |GRABA 020#101n;
               |FINSI;
               |SAL;
           |FINSI;
     |SIGUE;
     |CIERRA #7;
|FPRC;

|DEFBUCLE abom0005_2;
     #0#1;
     19, 4;
     #1#0, 001, "N", fDFecha;
     #1#0, 999, "N", fHFecha;
     ;
     NULL, CargaW0008;
|FIN;

|RUTINA ClaveBaja100;
     #100#0 = 1;
|FRUT;

|RUTINA ClaveAlta100;
     #100#0 = 99999;
|FRUT;

|PROCESO GrabaMinuta;
     |SI nContador = 0;
         |LEE 040#10u;
         nContador = #10#0 + 1;
     |FINSI;

     |SI nSwPase = 0;
         |SI #101#12 = "N";  |ACABA;  |FINSI;

         |SI nVariable = 0;
             aAlfa = #1#7 % 142;  |QBF aAlfa;
             |SI aAlfa ! "";
                 nVariable = nVariable + 1;
                 #111#0 = nContador;
                 #111#1 = nVariable;
                 #111#2 = "ASUNTO: " + aAlfa;
                 |GRABA 020#111n;
                 |LIBERA #111;
             |FINSI;

             aAlfa = #1#7 % 4336;  |QBF aAlfa;
             |SI aAlfa ! "";
                 nVariable = nVariable + 1;
                 #111#0 = nContador;
                 #111#1 = nVariable;
                 #111#2 = aAlfa;
                 |GRABA 020#111n;
                 |LIBERA #111;
             |FINSI;

             aAlfa = #1#8 % 150;  |QBF aAlfa;
             |SI aAlfa ! "";
                 nVariable = nVariable + 1;
                 #111#0 = nContador;
                 #111#1 = nVariable;
                 #111#2 = aAlfa;
                 |GRABA 020#111n;
                 |LIBERA #111;
             |FINSI;

             aAlfa = #1#8 % 5138;  |QBF aAlfa;
             |SI aAlfa ! "";
                 nVariable = nVariable + 1;
                 #111#0 = nContador;
                 #111#1 = nVariable;
                 #111#2 = aAlfa;
                 |GRABA 020#111n;
                 |LIBERA #111;
             |FINSI;

             aAlfa = #1#9 % 150;  |QBF aAlfa;
             |SI aAlfa ! "";
                 nVariable = nVariable + 1;
                 #111#0 = nContador;
                 #111#1 = nVariable;
                 #111#2 = aAlfa;
                 |GRABA 020#111n;
                 |LIBERA #111;
             |FINSI;

             aAlfa = #1#9 % 5138;  |QBF aAlfa;
             |SI aAlfa ! "";
                 nVariable = nVariable + 1;
                 #111#0 = nContador;
                 #111#1 = nVariable;
                 #111#2 = aAlfa;
                 |GRABA 020#111n;
                 |LIBERA #111;
             |FINSI;

             nSwVaria = 0;
             aAlfa = #1#17;   |QBF aAlfa;
             |SI aAlfa ! "";
                 |SI nSwVaria = 0;  nVariable = nVariable + 1;  nSwVaria = 1;  |FINSI;
                 nVariable = nVariable + 1;
                 #111#0 = nContador;
                 #111#1 = nVariable;
                 #111#2 = "TRIBUNAL : " + aAlfa;
                 |GRABA 020#111n;
                 |LIBERA #111;
             |FINSI;

             aAlfa = #1#15;   |QBF aAlfa;
             |SI aAlfa ! "";
                 |SI nSwVaria = 0;  nVariable = nVariable + 1;  nSwVaria = 1;  |FINSI;
                 nVariable = nVariable + 1;
                 #111#0 = nContador;
                 #111#1 = nVariable;
                 #111#2 = "AUTOS ...: " + aAlfa;
                 |GRABA 020#111n;
                 |LIBERA #111;
             |FINSI;

             aAlfa = #1#12;   |QBF aAlfa;
             |SI #1#12 ! 0;
                 |SI nSwVaria = 0;  nVariable = nVariable + 1;  nSwVaria = 1;  |FINSI;
                 nVariable = nVariable + 1;
                 #111#0 = nContador;
                 #111#1 = nVariable;
                 #111#2 = "CUANTIA .: " + aAlfa;
                 |GRABA 020#111n;
                 |LIBERA #111;
             |FINSI;
         |FINSI;

         |SI #101#9 ! " ";  nVariable = nVariable + 1;  |FINSI;

         nVariable = nVariable + 1;
         #111#0 = nContador;
         #111#1 = nVariable;
         #111#2 = #101#5;
         |GRABA 020#111n;
         |LIBERA #111;

         |ACABA;
     |FINSI;

     |SI #101#9 = " ";  |ACABA;  |FINSI;

     #0#0 = #101#7;
     #0#1 = #101#8;
     |LEE 101#0=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |ABRE #21;
     #21#0 = #0#5;
     #21#1 = #0#6;
     |LEE 040#21=;
     |SI FSalida ! 0;  |DDEFECTO #21;  |FINSI;
     |CIERRA #21;

     |ABRE #12;
     #12#0 = #0#10;
     |LEE 040#12=;
     |SI FSalida ! 0;  |DDEFECTO #12;  |FINSI;
     |CIERRA #12;

     #10#0 = nContador;
     |LEE 101#10=;
     |SI FSalida ! 0;
         #0#24 = #119#4;

         |DDEFECTO #10;
         #10#0  = nContador;
         #10#1  = #1#3;
         #10#2  = #101#2;
         #10#3  = #1#4;
         #10#4  = #0#0;
         #10#5  = 99999;
         #10#6  = #119#0;
         #10#7  = #119#1;
         #10#8  = "MINUTACION DE VARIAS LINEAS DE ANOTACIONES";
         #10#9  = "";
         #10#10 = "";
         #10#11 = #0#10;
         #10#12 = #12#1;
         #10#13 = #0#16;
         #10#14 = "S";
         #10#15 = #1#33;
         #10#16 = 1;
         #10#17 = #119#4;
         #10#18 = #119#4;
         #10#19 = "S";
         #10#20 = 0;
         #10#21 = 0;
         #10#23 = #119#3;
         #10#24 = #1#5;
         #10#25 = #1#6;
         #10#26 = #119#5;
         |GRABA 020#10n;
         |LIBERA #10;
     |SINO;
         #0#24  = 0;
         #10#13 = #10#13 + #0#16;
         #10#15 = #1#33;
         |GRABA 020#10a;
         |LIBERA #10;
     |FINSI;

     #0#19 = "S";
     #0#23 = #10#0;

     |GRABA 020#0a;
     |LIBERA #0;
|FPRC;

|DEFBUCLE abow0008_2;
     #101#1;
     ;
     00000;
     99999;
     ;
     NULL, GrabaMinuta;
|FIN;

|PROCESO EditaAsuntoW008;
     enTotalVaria  = 99;
     eaNombreList  = "";
     enCuadroIni   = 1030;
     enCuadroFin   = 1879;
     enCampoAncho  = 50;
     enModoEdita   = 0;

     |PARA nVariable = 1;  |SI nVariable [ 100;  |HACIENDO nVariable = nVariable + 1;
           IepPuntEdita  = epPuntEdita1 <-;
           IepPuntEdita = IepPuntEdita + (nVariable - 1);

           #111#0 = nContador;
           #111#1 = nVariable;
           |LIBERA #111;
           |LEE 101#111=;
           |SI FSalida ! 0;
               epPuntEdita  = "";
           |SINO;
               epPuntEdita  = #111#2;
               |BORRA 020#111a;
           |FINSI;
     |SIGUE;

     |PUSHV  0501 2380;
     |CUADRO 0829 2080;
     |BLANCO 0930 1979;
     |ATRI S;  |PINTA 0936, "Introduzca el Texto de la Minutacion";  |ATRI s;

     |HAZ Edita;

     |POPV;

     |PUSHV  0501 2380;
     |PINPA #0#119;
     |ABRE #22;
     #22#0 = #1#26;
     |LEE 040#22=;
     |SI FSalida ! 0;  |DDEFECTO #22;  |FINSI;
     |CIERRA #22;

     |ABRE #23;
     #23#0 = #1#3;
     |LEE 040#23=;
     |SI FSalida ! 0;  |DDEFECTO #23;  |FINSI;
     |CIERRA #23;

     |ABRE #506;
     #506#0 = #1#13;
     |LEE 040#506=;
     |SI FSalida ! 0;  |DDEFECTO #506;  |FINSI;
     |CIERRA #506;

     #119#0 = #506#2;
     #119#1 = #506#3;
     #119#3 = #22#14;
     #119#4 = 0;
     #119#5 = #23#23;

     |PINDA #0#119;
     |ENDATOS #1#119;
     |SI FSalida ! 0;
         |MENAV "     Orden de Minutacion Cancelada.";
         |POPV;
         nError = 1;
         |ACABA;
     |FINSI;

     |POPV;

     |PARA nVariable = 1;  |SI nVariable [ 100;  |HACIENDO nVariable = nVariable + 1;
           IepPuntEdita = epPuntEdita1 <-;
           IepPuntEdita = IepPuntEdita + (nVariable - 1);

           |DDEFECTO #111;
           #111#0 = nContador;
           #111#1 = nVariable;
           |LIBERA #111;
           |LEE 1010#111=;
           |SI FSalida ! 0;  |GRABA 020#111b;  |FINSI;
           #111#2 = epPuntEdita;
           |GRABA 020#111a;
     |SIGUE;

     |PARA nVariable = 100;  |SI nVariable ] 1;  |HACIENDO nVariable = nVariable - 1;
           #111#0 = nContador;
           #111#1 = nVariable;
           |LIBERA #111;
           |LEE 101#111=;
           aAlfa = #111#2;  |QBF aAlfa;

           |SI aAlfa ! "";  |SAL;  |FINSI;

           |BORRA 020#111a;
     |SIGUE;
|FPRC;

|RUTINA ClaveBaja101;
     #101#0 = 1;
|FRUT;

|RUTINA ClaveAlta101;
     #101#0 = 99999;
|FRUT;

|PROCESO Tipo11M0005; |TIPO 11;
     |SI FSalida ! 10;  |Y  FSalida ! 11;  |Y FSalida ! 12;
         |ACABA;
     |FINSI;

     |SI FSalida = 10;
         aFichero = ("a2" + Usuario + "zzzzzzzz") % 108; |NOME_DAT #100 aFichero;
         |ABRE #100;  |CIERRA #100;  |DELINDEX #100;

         nContador = 0;
         |ABRE #100;
         |BUCLE abom0005_1;
         |CIERRA #100;

         |SALVA_DATOS #0;
         |PUSHV 0501 2380;
         |PTEC 816;
         |ENTLINEAL #1#100, -1, 4, 22, 1, ClaveBaja100, ClaveAlta100;
         |POPV;
         |REPON_DATOS #0;
         |LEE 101#0=;

         |ABRE #100;  |CIERRA #100;  |DELINDEX #100;

         |PONCONTROL 23, "SI";
     |FINSI;

     |SI FSalida = 11;
         enCliente     = #1#3;
         enExpediente  = #0#0;
         efFecha       = #0#4;
         eaServicio    = #0#5;
         enPersona     = #0#2;
         enLinea       = #0#1;
         |LIBERA #1;
         |SUB_EJECUTA "aboz0006;1";
         enLinea       = 0;

         |PONCONTROL 23, "SI";

         |LIBERA #1;
         |LEE 101#1=;
     |FINSI;

     |SI FSalida = 12;
         aFichero = ("a2" + Usuario + "zzzzzzzz") % 108; |NOME_DAT #101 aFichero;
         |ABRE #101;  |CIERRA #101;  |DELINDEX #101;

         |PUSHV 0501 2380;

         |CUADRO 0810 1580;
         |BLANCO 0911 1479;

         |CUADRO 1019 1363;
         |BLANCO 1120 1262;

         fDFecha = "01.01.1999";
         fHFecha = "31.12.2099";
         |PINTA 1120, "Desde la Fecha de Seguimiento : ";
         |PINTA 1220, "Hasta la Fecha de Seguimiento : ";
         |PINTA 1152, fDFecha;
         |PINTA 1252, fHFecha;

         |ET fDFecha;
             fDFecha = 1152 ? 1;
             |SI FSalida = 1;  |VETE FinEtiF;  |FINSI;
             |SI FSalida = 2;  |VETE fDFecha;  |FINSI;

         |ET fHFecha;
             fHFecha = 1252 ? 1;
             |SI FSalida = 2;  |VETE fDFecha;  |FINSI;

         |ET FinEtiF;

         |POPV;

         nContador = 0;
         |ABRE #101;
         |BUCLE abom0005_2;
         |CIERRA #101;

         |PUSHV 0501 2380;
         |SALVA_DATOS #0;

         |PINPA #0#101;
         |ATRI I;
         nCoste = #1#32;
         |PINTA 2127, #1#33;
         |PINTA 2167, #1#31;
         |PINTA 2227, #1#32;
         |PINTA 2067, #1#32;
         |ATRI 0;

         |ENTLINEAL #1#101, -1, 4, 18, 0, ClaveBaja101, ClaveAlta101;

         |SI enPrecio ! 0;
             nContador = 0;
             |ABRE #10;
             |ABRE #111;

             nError    = 0;
             nSwPase   = 0;
             nVariable = 0;
             |BUCLE abow0008_2;
             |HAZ EditaAsuntoW008;
             |CIERRA #111;
             |SI nError = 0;
                 nSwPase = 1;
                 |BUCLE abow0008_2;
             |FINSI;
             |CIERRA #10;
         |SINO;
             #1#32 = nCoste;
         |FINSI;

         |POPV;
         |REPON_DATOS #0;
         |LEE 101#0=;

         |ABRE #101;  |CIERRA #101;  |DELINDEX #101;

         |PONCONTROL 23, "SI";

         |LIBERA #1;
         |LEE 101#1=;
     |FINSI;
|FPRC;
