|FICHEROS;
     aboz0006 #0;
     abom0001 #1;
     abom0010 #10,MANTE;
     abom0005 #5;
     abom0007 #7;
     abom0008 #8;
     abom0009 #9;
     abom0011 #11;
     asconcep #100;
     asconcli #101;
     asclient #102;
     ascuocab #103;
     ascuolin #104;
     abom0512 #512;
|FIN;

|VARIABLES;
     nContador = 0;
     nConta    = 0;
     nPrecio   = 0;
     nTipo     = 0;
     nVariable = 0;
     nSwVaria  = 0;

     aTipoLinea     = "";
     aParametro     = "";
     aAnyo          = "";
     aAlfa          = "";

     &enCliente     = 0;
     &enExpediente  = 0;
     &efFecha       = @;
     &eaServicio    = "";
     &enPersona     = 0;
     &enLinea       = 0;
|FIN;

|PROCESO Precio;
     #102#0  = #10#1;
     |LEE 040#102=;
     |SI FSalida ! 0;  |DDEFECTO #102;  |FINSI;

     |SI #102#22 = 1;  nPrecio = #100#4;   |FINSI;
     |SI #102#22 = 2;  nPrecio = #100#8;   |FINSI;
     |SI #102#22 = 3;  nPrecio = #100#10;  |FINSI;
     |SI #102#22 = 4;  nPrecio = #100#12;  |FINSI;
     |SI #102#22 = 5;  nPrecio = #100#14;  |FINSI;

     #101#0 = #10#1;
     #101#1 = #10#6;
     #101#2 = #10#7;
     |LEE 040#101=;
     |SI FSalida = 0;
         nPrecio = #101#10;
     |SINO;
         |DDEFECTO #101;
     |FINSI;

     nTipo = 0;
     aAnyo = #10#2 % -104;
     |ABRE #103;
     |ABRE #104;
     |SELECT #2#104;
     #104#0 = #10#1;
     #104#9 = aAnyo;
     #104#2 = #10#6;
     #104#3 = #10#7;
     |LEE 040#104=;
     |SI FSalida = 0;
         #103#0 = #104#0;
         #103#8 = #104#9;
         |LEE 040#103=;
         |SI FSalida = 0;
             nTipo   = #103#9;
             nPrecio = #103#5;
         |FINSI;
     |FINSI;
     |CIERRA #103;
     |CIERRA #104;

     |SI nTipo ! 0;  |ACABA;  |FINSI;

     nTipo = #101#11;
     |SI nTipo ! 0;  |ACABA;  |FINSI;

     nTipo = #100#32;
     |SI nTipo ! 0;  |ACABA;  |FINSI;

     nTipo = #102#68;
|FPRC;

|PROCESO GrabaTitulo;
     aAlfa = #1#7 % 142;  |QBF aAlfa;
     |SI aAlfa ! "";
         nContador = nContador + 1;
         #11#0 = #10#0;
         #11#1 = nContador;
         #11#2 = "ASUNTO: " + aAlfa;
         |GRABA 020#11n;
         |LIBERA #11;
     |FINSI;

     aAlfa = #1#7 % 4336;  |QBF aAlfa;
     |SI aAlfa ! "";
         nContador = nContador + 1;
         #11#0 = #10#0;
         #11#1 = nContador;
         #11#2 = aAlfa;
         |GRABA 020#11n;
         |LIBERA #11;
     |FINSI;

     aAlfa = #1#8 % 150;  |QBF aAlfa;
     |SI aAlfa ! "";
         nContador = nContador + 1;
         #11#0 = #10#0;
         #11#1 = nContador;
         #11#2 = aAlfa;
         |GRABA 020#11n;
         |LIBERA #11;
     |FINSI;

     aAlfa = #1#8 % 5138;  |QBF aAlfa;
     |SI aAlfa ! "";
         nContador = nContador + 1;
         #11#0 = #10#0;
         #11#1 = nContador;
         #11#2 = aAlfa;
         |GRABA 020#11n;
         |LIBERA #11;
     |FINSI;

     aAlfa = #1#9 % 150;  |QBF aAlfa;
     |SI aAlfa ! "";
         nContador = nContador + 1;
         #11#0 = #10#0;
         #11#1 = nContador;
         #11#2 = aAlfa;
         |GRABA 020#11n;
         |LIBERA #11;
     |FINSI;

     aAlfa = #1#9 % 5138;  |QBF aAlfa;
     |SI aAlfa ! "";
         nContador = nContador + 1;
         #11#0 = #10#0;
         #11#1 = nContador;
         #11#2 = aAlfa;
         |GRABA 020#11n;
         |LIBERA #11;
     |FINSI;

     nSwVaria = 0;
     aAlfa = #1#17;   |QBF aAlfa;
     |SI aAlfa ! "";
         |SI nSwVaria = 0;  nContador = nContador + 1;  nSwVaria = 1;  |FINSI;
         nContador = nContador + 1;
         #11#0 = #10#0;
         #11#1 = nContador;
         #11#2 = "TRIBUNAL : " + aAlfa;
         |GRABA 020#11n;
         |LIBERA #11;
     |FINSI;

     aAlfa = #1#15;   |QBF aAlfa;
     |SI aAlfa ! "";
         |SI nSwVaria = 0;  nContador = nContador + 1;  nSwVaria = 1;  |FINSI;
         nContador = nContador + 1;
         #11#0  = #10#0;
         #11#1  = nContador;
         #11#2 = "AUTOS ...: " + aAlfa;
         |GRABA 020#11n;
         |LIBERA #11;
     |FINSI;

     aAlfa = #1#12;   |QBF aAlfa;
     |SI aAlfa ! "";
         |SI nSwVaria = 0;  nContador = nContador + 1;  nSwVaria = 1;  |FINSI;
         nContador = nContador + 1;
         #11#0  = #10#0;
         #11#1  = nContador;
         #11#2 = "CUANTIA .: " + aAlfa;
         |GRABA 020#11n;
         |LIBERA #11;
     |FINSI;

     |SI nContador ! 0;
         nContador = nContador + 1;
     |FINSI;
|FPRC;

|PROCESO GrabaTempo1;
     #1#0 = #5#0;
     |LEE 040#1=;
     |SI FSalida ! 0;  |DDEFECTO #1;  |FINSI;

     |SI #0#0 ! 1;  |O #0#1 ! 99999;
         |SI #0#0 > #1#3;  |O #0#1 < #1#3;  |ACABA;  |FINSI;
     |FINSI;

     #512#0 = #5#10;
     |LEE 040#512=;
     |SI FSalida ! 0;  |DDEFECTO #512;  |FINSI;

     |LEE 040#10u;
     nConta = #10#0 + 1;

     |DDEFECTO #10;
     #10#0  = nConta;
     #10#1  = #1#3;
     #10#2  = #5#4;
     #10#3  = #1#4;
     #10#4  = #5#0;
     #10#5  = #5#1;
     #10#6  = #5#5;
     #10#7  = #5#6;
     #10#8  = #5#7;
     #10#9  = #5#8;
     #10#10 = #5#9;
     #10#11 = #5#10;
     #10#12 = #512#1;
     #10#13 = #5#16;
     #10#14 = #5#14;

     #100#0 = #10#6;
     #100#1 = #10#7;
     |LEE 040#100=;
     |SI FSalida ! 0;  |DDEFECTO #100;  |FINSI;

     |SI #100#34 = "S";
         #10#15  = #5#18;
         |HAZ Precio;
     |SINO;
         #10#15  = #5#18;
         nPrecio = #5#18;
     |FINSI;

     #10#16 = 1;
     #10#17 = nPrecio;
     #10#18 = nPrecio;
     #10#19 = "N";
     #10#20 = 0;
     #10#21 = 0;
     #10#23 = nTipo;
     #10#24 = #1#5;
     #10#25 = #1#6;
     |GRABA 020#10n;
     |LIBERA #10;

     |SI #5#14 = "N";  |ACABA;  |FINSI;

     |ABRE #7;
     |ABRE #11;

     #7#0      = #5#0;
     #7#1      = #5#1;
     nContador = 0;
     |PARA nVariable = 1;  |SI nVariable [ 100;  |HACIENDO nVariable = nVariable + 1;
           #7#2 = nVariable;
           |LEE 040#7=;
           |SI FSalida = 0;
               |SI nContador = 0;  |HAZ GrabaTitulo;  |FINSI;
               nContador = nContador + 1;
               #11#0  = #10#0;
               #11#1  = nContador;
               #11#2  = #7#3;
               |GRABA 020#11n;
               |LIBERA #11;
           |SINO;
               |SAL;
           |FINSI;
     |SIGUE;
     |CIERRA #7;
     |CIERRA #11;
|FPRC;

|DEFBUCLE abom0005_1;
     #5#2;
     1;
     "S", "N", #0#2, #0#4, #0#8, #0#6, #0#11;
     "S", "N", #0#3, #0#5, #0#9, #0#7, #0#12;
     ;
     NULL, GrabaTempo1;
|FIN;

|PROCESO GrabaTempo2;
     #512#0 = #8#10;
     |LEE 040#512=;
     |SI FSalida ! 0;  |DDEFECTO #512;  |FINSI;

     |LEE 040#10u;
     nConta = #10#0 + 1;

     |DDEFECTO #10;
     #10#0  = nConta;
     #10#1  = #8#0;
     #10#2  = #8#4;
     #10#4  = 0;
     #10#5  = #8#1;
     #10#6  = #8#5;
     #10#7  = #8#6;
     #10#8  = #8#7;
     #10#9  = #8#8;
     #10#10 = #8#9;
     #10#11 = #8#10;
     #10#12 = #512#1;
     #10#13 = #8#16;
     #10#14 = #8#14;
     #10#15 = #8#18;

     |HAZ Precio;

     #10#3  = #102#1;
     #10#16 = 1;
     #10#17 = nPrecio;
     #10#18 = nPrecio;
     #10#19 = "N";
     #10#20 = 0;
     #10#21 = 0;
     #10#23 = nTipo;
     #10#24 = #8#2;
     #10#25 = #8#3;
     |GRABA 020#10n;
     |LIBERA #10;

     |SI #8#14 = "N";  |ACABA;  |FINSI;

     |ABRE #9;
     |ABRE #11;

     #9#0      = #8#0;
     #9#1      = #8#1;
     nContador = 0;
     |PARA nVariable = 1;  |SI nVariable [ 100;  |HACIENDO nVariable = nVariable + 1;
           #9#2 = nVariable;
           |LEE 040#9=;
           |SI FSalida = 0;
               nContador = nContador + 1;
               #11#0  = #10#0;
               #11#1  = nContador;
               #11#2  = #9#3;
               |GRABA 020#11n;
               |LIBERA #11;
           |SINO;
               |SAL;
           |FINSI;
     |SIGUE;
     |CIERRA #9;
     |CIERRA #11;
|FPRC;

|DEFBUCLE abom0008;
     #8#2;
     1;
     "S", "N", #0#0, #0#4, #0#8, #0#6, #0#11;
     "S", "N", #0#1, #0#5, #0#9, #0#7, #0#12;
     ;
     NULL, GrabaTempo2;
|FIN;

|PROCESO GrabaEl5;
     #5#19 = #10#19;
     #5#23 = #10#0;

     |SI #5#19 = "N";
         #5#23 = 0;
         #5#24 = 0;
     |FINSI;

     |GRABA 020#5a;
     |LIBERA #5;
|FPRC;

|DEFBUCLE abom0005_2;
     #5#1;
     23, 20;
     #10#4, 000, #10#0, "N";
     #10#4, 999, #10#0, "N";
     be;
     NULL, GrabaEl5;
|FIN;

|RUTINA ClaveBaja;
     #10#19 = aTipoLinea;
     #10#1  = #0#0;
     #10#2  = #0#4;
     #10#0  = 1;
|FRUT;

|RUTINA ClaveAlta;
     #10#19 = aTipoLinea;
     #10#1  = #0#1;
     #10#2  = #0#5;
     #10#0  = 99999;
|FRUT;

|PROCESO Procesalos;
     |SI #10#4 ! 0;
         |SI #10#5 ! 99999;
             |ABRE #5;
             #5#0 = #10#4;
             #5#1 = #10#5;
             |LEE 101#5=;
             |SI FSalida = 0;
                 #5#19 = #10#19;
                 #5#23 = #10#0;
                 #5#24 = #10#18;
                 |GRABA 020#5a;
                 |LIBERA #5;
             |FINSI;
             |CIERRA #5;
         |SINO;
             |BUCLE abom0005_2;
         |FINSI;
     |SINO;
         #8#0 = #10#1;
         #8#1 = #10#5;
         |LEE 101#8=;
         |SI FSalida = 0;
             #8#19 = #10#19;
             |GRABA 020#8a;
             |LIBERA #8;
         |FINSI;
     |FINSI;

     |SI #10#19 = "N";
         |ABRE #11;
         |PARA nVariable = 1;  |SI nVariable [ 100;  |HACIENDO nVariable = nVariable + 1;
               #11#0 = #10#0;
               #11#1 = nVariable;
               |LIBERA #11;
               |LEE 101#11=;
               |SI FSalida = 0;
                   |BORRA 020#11a;
                   |LIBERA #11;
               |FINSI;
         |SIGUE;
         |CIERRA #11;

         |BORRA 020#10a;
         |LIBERA #10;
     |FINSI;
|FPRC;

|DEFBUCLE abom0010;
     #10#1;
     ;
     00000;
     99999;
     be;
     NULL, Procesalos;
|FIN;

|PROCESO Tipo3;  |TIPO 3;
     |ABRE #1;
     |ABRE #10;
     |ABRE #100;
     |ABRE #101;
     |ABRE #102;
     |ABRE #512;

     |BUCLE abom0005_1;
     |SI #0#2 = 0;  |Y #0#3 = 0;
         |BUCLE abom0008;
     |FINSI;

     |CIERRA #10;
     |CIERRA #100;
     |CIERRA #101;
     |CIERRA #102;
     |CIERRA #512;

     |SI nConta = 0;
         |MENAV "      Seleccion Vacia ...";
         |ACABA;
     |FINSI;

     |ENTLINEAL #2#10, -3, 4, 21, 1, ClaveBaja, ClaveAlta;

     |ABRE #8;
     |BUCLE abom0010;
     |CIERRA #8;
     |CIERRA #1;
|FPRC;

|PROGRAMA;
     aParametro = PARAMETRO;

     |SI aParametro = "2";
         #0#4       = "01.01.0000";
         #0#5       = "31.12.2999";
         |CLS;
         aTipoLinea = "S";
         |ABRE #1;
         |ENTLINEAL #2#10, -3, 4, 21, 1, ClaveBaja, ClaveAlta;
         |CIERRA #1;

         |ABRE #8;
         |BUCLE abom0010;
         |CIERRA #8;

         |ACABA;
     |FINSI;

     aTipoLinea = "N";
     |ABRET #0;
     |LEE 041#0p;
     |SI FSalida = 0;
         |MENAV "      Hay algun Usuario Minutando, Espere por Favor";
         |CIERRAT #0;
         |ACABA;
     |FINSI;

     |DDEFECTO #0;
     |GRABA 020#0n;
     |LIBERA #0;
     |CIERRAT #0;

     |SI aParametro = "";
         |PINPA #0#0;
         |PINDA #0#0;
         |ENDATOS #1#0;
     |FINSI;

     |SI aParametro = "1";
         #0#0  = enCliente;
         #0#1  = enCliente;
         #0#2  = enExpediente;
         #0#3  = enExpediente;
         #0#4  = efFecha;
         #0#5  = efFecha;
         #0#6  = eaServicio;
         #0#7  = eaServicio;
         #0#8  = enPersona;
         #0#9  = enPersona;
         #0#11 = enLinea;
         #0#12 = enLinea;
         |HAZ Tipo3;
     |FINSI;

     |DELINDEX #0;
|FPRO;
