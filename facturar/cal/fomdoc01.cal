|FICHEROS;
     fomdoc01 #0;
     :/integral/def/dsam0000;
     pdfimtmp #100;
|FIN;

|VARIABLES;
     &aArchivo = "";
     aAlfa = "";
     aAlfa1 = "";
     aCampo = "";
     aValor = "";
     aCadena = "";
     aCadenaSal = "";
     aCaracter = "";
     aCaracterSal = "";
     swExiste = 0;
     nTipo = 0;
     nAscii = 0;
     nLongitud = 0;
     aLongitud = "";
     nPos = 0;
     nBusca = 0;

     &swCopia = 1;
     &aListaFich = "";
|FIN;

||________PROCESO PARA LA IMPRESION DEL PDF

|PROCESO sustituye2;
     nAscii = & aCaracter;
     aCaracterSal = aCaracter;
     |SI nAscii = 160;             || el caracter  
          aCaracterSal = "\341";
     |FINSI;
     |SI nAscii = 130;             || el caracter ‚
          aCaracterSal = "\351";
     |FINSI;
     |SI nAscii = 161;             || el caracter ¡
          aCaracterSal = "\355";
     |FINSI;
     |SI nAscii = 162; |O nAscii = 243;            || el caracter ¢
          aCaracterSal = "\363";
     |FINSI;
     |SI nAscii = 163;             || el caracter £
          aCaracterSal = "\372";
     |FINSI;
     |SI nAscii = 164;             || el caracter ¤
          aCaracterSal = "\361";
     |FINSI;
     |SI nAscii = 241;             || el caracter ¤ (otro codigo ¨?)
          aCaracterSal = "\361";
     |FINSI;
     |SI nAscii = 165;             || el caracter ¥
          aCaracterSal = "\321";
     |FINSI;
     |SI nAscii = 129;
          aCaracterSal = "\374";   || el caracter u con dieresis
     |FINSI;
     |SI nAscii = 154;
          aCaracterSal = "\334";   || el caracter U (mayuscula) con dieresis
     |FINSI;
     |SI nAscii = 167;
          aCaracterSal = "\272";   || el caracter §
     |FINSI;
     |SI nAscii = 166;
          aCaracterSal = "\252";   || el caracter ¦
     |FINSI;
     |SI aCaracter = "(";
          aCaracterSal = "\(";
     |FINSI;
     |SI aCaracter = ")";
          aCaracterSal = "\)";
     |FINSI;
|FPRC;

|PROCESO sustituye;
     aCadenaSal = "";
     aLongitud = aCadena % 0;
     nLongitud = aLongitud;
     |PARA nPos = 1; |SI nPos [ nLongitud; |HACIENDO nPos = nPos + 1;
          nBusca = (nPos * 100) + 1;
          aCaracter = aCadena % nBusca;
          |HAZ sustituye2;
          aCadenaSal = aCadenaSal + aCaracterSal;
     |SIGUE;
|FPRC;

|PROCESO GrabaTemporal;
     aAlfa1 = aValor % 101;
     |SI aAlfa1 = ".";
          aValor = "";
     |FINSI;
     |QBF aValor;
     aCadena = aValor;
     |HAZ sustituye;
     aValor = aCadenaSal;
     #pdfimtmp#0 = aCampo;
     #pdfimtmp#1 = aValor;
     #pdfimtmp#2 = nTipo;
     |GRABA 020#pdfimtmp.n;
|FPRC;

|PROCESO CargaTemporal;
     aCampo = "NRO";
     aValor = #dsam0000#0;    |QBF aValor;
     |HAZ GrabaTemporal;

     aCampo = "NOMBRE";
     aValor = #dsam0000#1;    |QBF aValor;
     |HAZ GrabaTemporal;

     aCampo = "DOMICILIO";                   || DIRECCION DE ENVIO
     aAlfa = #dsam0000#39;    |QBF aAlfa;    || SIGLA
     aValor = aAlfa;
     aAlfa = #dsam0000#40;    |QBF aAlfa;    || CALLE
     aValor = aValor + " " + aAlfa;
     aAlfa = #dsam0000#41;    |QBF aAlfa;    || NUMERO
     aValor = aValor + "," + aAlfa;
     aAlfa = #dsam0000#42;    |QBF aAlfa;    || ESCALERA
     |SI aAlfa ! "";
          aValor = aValor + ", Esc. " + aAlfa;
     |FINSI;
     aAlfa = #dsam0000#43;    |QBF aAlfa;    || PISO
     |SI aAlfa ! "";
          aValor = aValor + " - " + aAlfa;
     |FINSI;
     aAlfa = #dsam0000#44;    |QBF aAlfa;    || PUERTA
     aValor = aValor + " " + aAlfa;
     |HAZ GrabaTemporal;

     aCampo = "CP";
     aAlfa = #dsam0000#45;    |QBF aAlfa;    || CODIGO PROVINCIA
     aAlfa = "00" + aAlfa;    aAlfa = aAlfa % -102;
     aValor = aAlfa;
     aAlfa = #dsam0000#46;    |QBF aAlfa;    || CODIGO POBLACION
     aAlfa = "000" + aAlfa;    aAlfa = aAlfa % -103;
     aValor = aValor + aAlfa;
     |HAZ GrabaTemporal;

     aCampo = "CIUDAD";
     aAlfa = #dsam0000#47;    |QBF aAlfa;    || POBLACION
     aValor = aAlfa;
     |HAZ GrabaTemporal;
|FPRC;

|PROCESO BuscaArchivo;
     aArchivo = "comunicacion.pdf";
|FPRC;

|PROCESO LeeClientes;
     swExiste = 0;
     |ABRE #dsam0000;
     #dsam0000#0 = #0#0;
     |LEE 000#dsam0000.=;
     |SI FSalida = 0;
          swExiste = 1;
     |FINSI;
     |CIERRA #dsam0000;
|FPRC;

|PROGRAMA;
     |CLS;
     |PINPA #0#0;
     |PINDA #0#0;
     |ENDATOS #1#0;
     |SI FSalida = 0;
          |HAZ LeeClientes;
          |SI swExiste = 1;
               |HAZ BuscaArchivo;     ||asigna a aArchivo el nombre del pdf
               ||HAZ CargaTemporal;    ||mira a ver si esta el pdf en el mantenimiento

               aAlfa1 = Usuario; |QBT aAlfa1;    aAlfa1 = ("c" + aAlfa1) % 108;
               |NOME_DAT #100 aAlfa1;
               |DELINDEX #pdfimtmp;
               |ABRE #pdfimtmp;

               ||BUCLE GrabaTemporal;
               |HAZ CargaTemporal;

               swCopia = 1;

               |SUB_EJECUTA "pdfgraba";
               |SUB_EJECUTA "pdfimpre";

               |CIERRA #pdfimtmp;
          |FINSI;
     |FINSI;
|FPRO;
