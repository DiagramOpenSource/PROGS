          **** PASE DEL HISTORICO DE RECIBOS AL SECUENCIAL ****
|FICHEROS;
  aspasban #0;
  ashisrec #1;
  asclient #2;
  indrecib #3;
  ashiscon #4;
  asbansec #5;
  ashismic #90;
|FIN;

|VARIABLES;
     cabe1   = "";
     indi1   = "";
     conc1_1 = "";
     conc1_2 = "";
     conc1_3 = "";
     conc1_4 = "";
     conc1_5 = "";
     tsuc1   = "";
     tgen1   = "";

     cabe2   = "";
     cabe3   = "      ";
     cabe5   = "";
     cabe6   = "          ";
     cabe7   = "03";
     cabe8   = "                                          ";

     indi2   = "";
     indi3   = "";
     indi4   = "";
     indi5   = "";
     indi6   = "";
     indi7   = "";
     indi8   = "";
     indi9   = "                       .";

     conc1   = "";
     conc2   = "";
     conc3   = "                                        ";
     conc4   = "                                        ";
     conc5   = "                                        ";

     tsuc2   = "                                                    ";
     tsuc3   = "";
     tsuc4   = ""
     tsuc5   = ""
     tsuc6   = "      ";
     tsuc7   = "";
     tsuc8   = "                                                     .";

     tgen2   = "                                                            ";
     tgen3   = "";
     tgen4   = "";
     tgen5   = "      ";
     tgen6   = "";
     tgen7   = "                                                     .";

     handle     = "";
     varalfa1   = "";
     varalfa2   = "";
     facalfa    = "";
     contenido  = "";
     longalf    = "";
     fiche      = "";
     espacio    = " ";
     dd         = "";
     mm         = "";
     aa         = "";
     fechalf    = "";
     impalf     = "";
     vacio      = "          ";
     banco      = "";
     ccc        = "";
     recibo     = 0;
     impsuc     = 0;
     regsuc     = 0;
     numsuc     = 0;
     trecibo    = 0;
     timpsuc    = 0;
     tnumsuc    = 0;
     serie      = "";
     factu      = 0;
     cont       = 0;
     contreg    = 0;
     empresa    = "";
     linea      = "Procesando Factura: Serie:    Factura:";
     final      = "                   ."
     l          = 0;
     valo       = 0;
     abc        = 0;
     cba        = "";

     &eaMoneda  = "";
     &enCanti   = 0;
     &enSwTrata = 0;
|FIN;

|PROCESO MiraVarMoneda;  |TIPO 7;
     |HAZ PintaMoneda;
     #0#12 = eaMoneda;

     |PINTA #0#12;
|FPRC;


|PROCESO secuencial;
  #2#0 = #1#3;              ||** Lee fichero de clientes;
  |LEE 040#2=;              ||** Ordena por Banco/Sucursal;
  |SI FSalida = 0;
      #3#0 = #1#0;  ||** Serie;
      #3#1 = #1#1;  ||** Factura;
      #3#2 = #2#2;  ||** Nom. Cliente;
      #3#3 = #2#8;  ||** Cif/Dni;
      #3#4 = #2#16; ||** Banco;
      #3#5 = #2#13; ||** C.C.C;
      #3#6 = #2#14; ||** Cta.Cte;
      #1#15 = #1#6 - #1#9;

      |SI #1#15 [ 0; |ACABA; |FINSI;

      #3#7 = #1#15;  ||** Importe Recibo;
      #3#8 = #1#5;  ||** Fecha Vencimiento;
      |SI #0#11 = "N";
          abc = 100000 + #2#0;
          cba = abc;
          #3#3 = #5#2 + cba;
      |FINSI;
      |GRABA 040#3n;||** Graba el fichero indexado;

      #1#10 = "S";
      #1#9 = #1#6;
      #1#12 = "R";
      #1#8 = ~;
      |GRABA 021#1a;
      |LIBERA #1;

      |ABRE #90;
      #90#0 = #1#0;
      #90#1 = #1#1;
      |LIBERA #90;
      |LEE 140#90=;
      |SI FSalida = 0;
          #90#32 = #1#10;
          #90#33 = #1#12;
          |GRABA 140#90a;
      |FINSI;
      |CIERRA #90;
  |SINO;
     |AVISO;
     |PINTA 2218,"ATENCION Cliente Inexistente! CODIGO.:"
     |PINTA 2257,#1#3;
     |PAUSA;
  |FINSI;
|FPRC;


|DEFBUCLE capasban0;
 #1#1;
 2,7;
 #0#0,#0#2,#0#4,#0#6;
 #0#1,#0#3,#0#5,#0#7;
 be;
 NULL,secuencial;
|FIN;


|PROCESO pase;|TIPO 3;
  eaMoneda = #0#12;

  |SI #0#12 = "PESETAS";
      cabe1   = "0380";
      indi1   = "0680";
      conc1_1 = "0681";
      conc1_2 = "0682";
      conc1_3 = "0683";
      conc1_4 = "0684";
      conc1_5 = "0685";
      tsuc1   = "0780";
      tgen1   = "0880";
  |SINO;
      cabe1   = "5380";
      indi1   = "5680";
      conc1_1 = "5681";
      conc1_2 = "5682";
      conc1_3 = "5683";
      conc1_4 = "5684";
      conc1_5 = "5685";
      tsuc1   = "5780";
      tgen1   = "5880";
  |FINSI;

  |ABRE #2;
  |ABRE #3;
  |CIERRA #3;
  |DELINDEX #3;
  |ABRE #3;
  |BUCLE capasban0;
  |CIERRA #2;
  |ABRE #5;
  #5#0 = #0#9;
  |LEE 040#5=;
  |DFICE empresa;
  fiche = "wt  " + empresa + "assecrec.seq";
  |QBF fiche;
  |FABRE fiche;
  handle = FSalida;
  |SI FSalida ] 0;
      |HAZ cabecera;
      |HAZ regindiv;
      |HAZ campo_sucursal;
      |HAZ campo_totales;
  |FINSI;
  FSalida = handle;
  |FCIERRA FSalida;
  |CIERRA #3;
  |DELINDEX #3;
  |CIERRA #5;
  |CLS;
  |CONFI "     DESEA GRABAR AHORA EL DISKETTE ";
  |SI FSalida = 0;
      |SUB_EJECUTA_NP "ascopdis";
  |FINSI;
|FPRC;


||***********************************************************;
||******************* REGISTRO DE CABECERA ******************;
||***********************************************************;
|PROCESO cabecera;
  fechalf = #0#8;
  dd = fechalf % A102;
  mm = fechalf % A402;
  aa = fechalf % A902;
  cabe2 = dd + mm + aa;             ||** Fecha por pantalla;
  cabe8 = cabe8 + final;            ||** 40 espacios en blanco+final;
  cabe5 = #5#4 + #5#5 + #5#6 + #5#7 + #5#8
  varalfa1 = cabe1 + #5#2 + cabe2 + cabe3 + #5#3 + cabe5 + cabe6 + cabe7 + cabe8;
  contenido = handle + espacio + varalfa1;
  |FGRABA contenido;
  l = l + 1;
|FPRC;

||***********************************************************;
||******************* REGISTRO INDIVIDUAL *******************;
||***********************************************************;
|PROCESO regindiv;
  |SELECT #2#3;
  |LEE 040#3p;
  |SI FSalida ! 0;   |ACABA;  |FINSI;
  |PINTA 2218,linea;
  |ATRI I;
  |PINTA 2245,#3#0;
  |PINTA 2257,#3#1;
  |ATRI i;
  banco = #3#4;
  ccc = #3#5;

|ET compara;
  |SI #3#4 ! banco;|O ccc ! #3#5;
      |HAZ campo_sucursal;
      banco  = #3#4;
      ccc    = #3#5;
      recibo = 0;
      impsuc = 0;
      regsuc = 0;
  |FINSI;
  |HAZ campo_indiv;
  recibo = recibo + 1;

  enSwTrata = 1;
  enCanti   = #3#7;
  |HAZ ConverMoneda;
  impsuc = impsuc + enCanti;

  regsuc = regsuc + 1;
  trecibo = trecibo + 1;
  timpsuc = timpsuc + enCanti;
  |SI #5#10 = "S"; |HAZ conceptos; |FINSI;
  |LEE 040#3s;

  |SI FSalida = 0;
      |ATRI I;
      |PINTA 2245,#3#0;
      |PINTA 2257,#3#1;
      |ATRI i;
      |VETE compara;
  |FINSI;
|FPRC;


||***********************************************************;
||******************* REGISTRO CONCEPTOS ********************;
||***********************************************************;
|PROCESO conceptos;
  |ABRE #4;
  cont = 0; ||** Contador de campos maximo 3;
  contreg = 0; ||** Contador de registros maximo 5;
  conc3 = "                                        ";
  conc4 = "                                        ";
  conc5 = "                                        ";
  #4#0 = #3#0;
  #4#1 = #3#1;
  #4#2 = 000;
  |LEE 040#4];
  |SI FSalida ! 0;  |CIERRA #4;  |ACABA;  |FINSI;
  |SI #4#0 ! #3#0;|O #4#1 ! #3#1;  |CIERRA #4;  |ACABA;  |FINSI;
  contreg = 1;
  cont = 1;
  serie = #4#0;
  factu = #4#1;
  conc3 = #4#7;
|ET otro;
  |LEE 040#4s;
  |SI FSalida ! 0;
      |VETE casifin;
  |FINSI;
  |SI serie ! #4#0;|O factu ! #4#1;
      |VETE casifin;
  |FINSI;
  cont = cont + 1;
  |SI cont = 1;conc3 = #4#7;|FINSI;
  |SI cont = 2;conc4 = #4#7;|FINSI;
  |SI cont = 3;conc5 = #4#7;|FINSI;
  |SI cont = 3;|Y contreg [ 5;
      cont = 0;
      |SI contreg = 1;conc1 = conc1_1 + #5#2;|FINSI;
      |SI contreg = 2;conc1 = conc1_2 + #5#2;|FINSI;
      |SI contreg = 3;conc1 = conc1_3 + #5#2;|FINSI;
      |SI contreg = 4;conc1 = conc1_4 + #5#2;|FINSI;
      |SI contreg = 5;conc1 = conc1_5 + #5#2;|FINSI;
      varalfa1 = conc1 + #3#3 + conc3 + conc4 + conc5 + final;
      contenido = handle + espacio + varalfa1;
      |FGRABA contenido;
      l = l + 1;
      contreg = contreg + 1;
      regsuc = regsuc + 1;
      conc3 = "                                        ";
      conc4 = "                                        ";
      conc5 = "                                        ";
  |FINSI;
  |VETE otro;
|ET casifin;
  |SI cont ! 0;|Y contreg [ 5;
      |SI contreg = 1;conc1 = conc1_1 + #5#2;|FINSI;
      |SI contreg = 2;conc1 = conc1_2 + #5#2;|FINSI;
      |SI contreg = 3;conc1 = conc1_3 + #5#2;|FINSI;
      |SI contreg = 4;conc1 = conc1_4 + #5#2;|FINSI;
      |SI contreg = 5;conc1 = conc1_5 + #5#2;|FINSI;
      varalfa1 = conc1 + #3#3 + conc3 + conc4 + conc5 + final;
      contenido = handle + espacio + varalfa1;
      |FGRABA contenido;
      l = l + 1;
      regsuc = regsuc + 1;
  |FINSI;
  |CIERRA #4;
|FPRC;

            ||Calculo registros individuales;
|PROCESO campo_indiv;
  indi4 = #3#5 % 108;               ||** 8 primeras cifras del ccc;
  indi4 = indi4 + vacio;
  indi4 = indi4 % 108;

  indi5 = #3#6 % 110;               ||** 10 primeros de la cta.cte;
  indi5 = indi5 + vacio;
  indi5 = indi5 % 110;

  enCanti   = #3#7;
  enSwTrata = 0;
  |HAZ ConverMoneda;

  |SI #0#12 = "EUROS  ";
      indi6 = ("000000000000" + enCanti) % -112;
  |SINO;
      indi6 = (("0000000000" + enCanti) % -110) + "00";
  |FINSI;

  fechalf = #3#8;                   ||** Fecha de emision;
  dd = fechalf % A102;
  mm = fechalf % A402;
  aa = fechalf % A902;
  indi7 = dd + mm + aa;
  facalfa = #3#1;
  indi8 = #5#9;
  |QBF indi8;
  indi8 = indi8 + " " + facalfa + "                                           ";
  indi8 = indi8 % A140;
  varalfa1 = indi1 + #5#2 + #3#3 + #3#2 + indi4 + indi5 + indi6 + indi7 + indi8 + indi9;
  contenido = handle + espacio + varalfa1;
  |FGRABA contenido;
  l = l + 1;
|FPRC;

||***********************************************************;
||******************* REGISTRO SUCURSAL *********************;
||***********************************************************;
|PROCESO campo_sucursal;
  tsuc3 = ccc % 108;                ||** c.c.c del banco;

  tsuc4 = recibo;                   ||** Num. recibos sucursal;
  tsuc4 = "0000000000" + tsuc4;
  tsuc4 = tsuc4 % -110;

  |SI #0#12 = "EUROS  ";
      impsuc = impsuc * 100;
      tsuc5  = ("000000000000" + impsuc) % -112;
  |SINO;
      tsuc5  = (("0000000000" + impsuc) % -110) + "00";
  |FINSI;

  regsuc = regsuc + 1;              ||** Numero de sucursales + 1;

  tsuc7 = regsuc;
  tsuc7 = "0000000000" + tsuc7;
  tsuc7 = tsuc7 % -110;

 varalfa1 = tsuc1 + #5#2 + tsuc2 + tsuc3 + tsuc4 + tsuc5 + tsuc6 + tsuc7 + tsuc8;
 contenido = handle + espacio + varalfa1;
 |FGRABA contenido;
 l = l + 1;
|FPRC;

||***********************************************************;
||******************* REGISTRO TOTAL GENERAL ****************;
||***********************************************************;
|PROCESO campo_totales;
  tgen3 = trecibo;                ||** Total num recibos;
  tgen3 = "0000000000" + tgen3;
  tgen3 = tgen3 % -110;

  |SI #0#12 = "EUROS  ";
      timpsuc = timpsuc * 100;
      tgen4   = ("000000000000" + timpsuc) % -112;
  |SINO;
      tgen4   = (("0000000000" + timpsuc) % -110) + "00";
  |FINSI;

  l = l + 1;
  tnumsuc = l;
  tgen6  = tnumsuc;               ||** Total reg. sucursales;
  tgen6  = "0000000000" + tgen6;
  tgen6  = tgen6 % -110;

  varalfa1 = tgen1 + #5#2 + tgen2 + tgen3 + tgen4 + tgen5 + tgen6 + tgen7;
  contenido = handle + espacio + varalfa1;
  |FGRABA contenido;
  |PINTA 2201,"No. de Recibos:                 ";
  valo = tgen3;
  |PINTA 2217,valo;
  |PINTA 2225,"No. de Registros:               ";
  valo = tgen6;
  |PINTA 2243,valo;
  |PINTA 2251,"Importe TOTAL:               ";
  valo = tgen4;
  |SI #0#12 = "PESETAS";
      valo = valo / 100;
  |FINSI;
  |PINTA 2266,valo;
  |PAUSA;
|FPRC;
