|FICHEROS;
     asz00001 #0;
     ascabrem #1;
     asw00025 #99,MANTE;

    &bancosp@ #706;
|FIN;

|VARIABLES;
     nSwHay      = 0;
     nSalida     = 0;
     nLinea      = 0;
     nTodo       = 0;
     nDBanco     = 0;
     nHBanco     = 0;
     nSaldoA     = 0;
     nSaldoT     = 0;
     nBanco      = 0;

     aFichero    = "";
     aTecla      = "";
     aEjer       = "";
     aEjerAnt    = "";

     &enCodBanco = 0;
|FIN;

|PROCESO VerRemesa; |TIPO 11;
     |SI FSalida ! 10;  |ACABA;  |FINSI;

     |PUSHV 0501 2380;

     |BLANCO 0501 2380;

     |ABRE #1;
     #1#0 = #99#1;
     |LEE 040#1=;
     |SI FSalida = 0;
         |PINPA #0#1;
         |PINDA #0#1;
         |C_M #1#1, 1, "N";
         |C_M #1#3, 1, "N";
         |ENDATOS #4#1;
     |FINSI;
     |CIERRA #1;

     |POPV;

     |ERROR;
|FPRC;

|PROCESO ImprimeRemesas;
     |PRINT;
|FPRC;

|DEFBUCLE asw00025;
     #99#1;
     ;
     #0#0,      1;
     #0#0, 999999;
     ;
     NULL, ImprimeRemesas;
|FIN;

|PROCESO LeeRemesas;
     |BUCLE asw00025;
     |PIEINF;
|FPRC;

|DEFBUCLE asz00001;
     #0#1;
     ;
     nDBanco;
     nHBanco;
     ;
     NULL, LeeRemesas;
|FIN;

|PROCESO Impresion;
     |IMPRE 0;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |INFOR "asz00001";
     |SI FSalida ! 0;  |FINIMP;  |ACABA;  |FINSI;

     |SI nTodo = 0;
         nDBanco = #0#0;
         nHBanco = #0#0;
     |SINO;
         nDBanco = 0;
         nHBanco = 99;
     |FINSI;

     |BUCLE asz00001;

     |FININF;
     |FINIMP;
|FPRC;

|PROCESO Tipo11;  |TIPO 11;
     |SI FSalida = 10;
         |ENTLINEAL #1#99, -2, 4, 22, 0, ClaveBaja99, ClaveAlta99;
         |ACABA;
     |FINSI;

     |SI FSalida = 11;
         nLinea = #0#0;
         nTodo  = 0;  |HAZ Impresion;

         #0#0   = nLinea;
         |LEE 040#0=;
         |PONCONTROL 23, "SI";
     |FINSI;

     |SI FSalida = 12;
         nLinea = #0#0;
         nTodo  = 1;  |HAZ Impresion;

         #0#0   = nLinea;
         |LEE 040#0=;
         |PONCONTROL 23, "SI";
     |FINSI;
|FPRC;

|PROCESO Tipo7;  |TIPO 7;
     |ENTLINEAL #1#99, -2, 7, 22, 0, ClaveBaja99, ClaveAlta99;
|FPRC;

|PROCESO CargaTempo;
     enCodBanco = #1#1;
     |HAZ LeeBancoPropio;

     |SI nBanco ! #1#1;
         aEjerAnt = "";
         nBanco   = #1#1;
         nSaldoT  = 0;
     |FINSI;

     #0#0  = #1#1;
     |LEE 101#0=;
     |SI FSalida ! 0;
         |DDEFECTO #0;
         #0#0 = #1#1;
         |GRABA 020#0b;
     |FINSI;
     #0#1 = #706#1;
     |GRABA 020#0a;
     |LIBERA #0;

     #99#0 = #1#1;
     #99#1 = #1#0;
     #99#2 = #1#3;
     #99#3 = #1#4;

     aEjer = #99#2 % -104;
     |SI aEjer ! aEjerAnt;
         nSaldoA  = #99#3;
         aEjerAnt = aEjer;
     |SINO;
         nSaldoA = nSaldoA + #99#3;
     |FINSI;

     nSaldoT = nSaldoT + #99#3;

     #99#4 = nSaldoA;
     #99#5 = nSaldoT;

     |GRABA 020#99n;
     |LIBERA #99;
     nSwHay = 1;
|FPRC;

|DEFBUCLE ascabrem;
     #1#2;
     ;
     00, 000001;
     99, 999999;
     ;
     NULL, CargaTempo;
|FIN;

|RUTINA ClaveBaja99;
     #99#0 = #0#0;
     #99#1 = 1;
|FRUT;

|RUTINA ClaveAlta99;
     #99#0 = #0#0;
     #99#1 = 999999;
|FRUT;

|RUTINA ClaveBaja0;
     #0#0 = 0;
|FRUT;

|RUTINA ClaveAlta0;
     #0#0 = 99;
|FRUT;

|PROGRAMA;
     aFichero = ("z01" + Usuario + "zzzzzzzz") % 108;
     |NOME_DAT #0 aFichero;

     aFichero = ("w25" + Usuario + "zzzzzzzz") % 108;
     |NOME_DAT #99 aFichero;

     |CLS;
     |CABEZA "Remesas por Bancos";

     nSwHay = 0;
     |ABRE #0;
     |ABRE #99;
     |BUCLE ascabrem;
     |CIERRA #99;
     |CIERRA #0;

     |SI nSwHay = 0;
         |MENAV "0000 No hay Remesas en esta Empresa";
         |ABRE #99;  |CIERRA #99;  |DELINDEX #99;
         |ABRE #0;   |CIERRA #0;   |DELINDEX #0;
         |ACABA;
     |FINSI;

     |ENTLINEAL #1#0, -1, 4, 22, 1, ClaveBaja0, ClaveAlta0;

     |ABRE #99;  |CIERRA #99;  |DELINDEX #99;
     |ABRE #0;   |CIERRA #0;   |DELINDEX #0;
|FPRO;
