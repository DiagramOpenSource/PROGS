|FICHEROS;
     aszp0010 #0;
     ascuocab #1;
     ascuolin #2;
     asconmen #3;
     asconcep #4;
     asconcli #5;
     asconlin #7;

     ascuocab@ #901;
     ascuolin@ #902;
|FIN;

|VARIABLES;
     aAlfa       = "";
     aBase       = "";
     aMas        = "";
     aDescri1    = "";
     aDescri2    = "";
     aDescri3    = "";
     aDescri4    = "";

     fFecha      = @;

     nVariable   = 0;
     nDeDonde    = 0;
|FIN;

|PROCESO PonAnyos;  |TIPO 7;
     fFecha = ~;
     aAlfa  = fFecha % -104;
     #0#3   = aAlfa;
     #0#2   = #0#3 - 1;

     |PINTA #0#2;
     |PINTA #0#3;
|FPRC;

|PROCESO GrabaConceptos;
     #4#0 = #901#3;
     #4#1 = #901#4;
     |LEE 040#4=;
     |SI FSalida ! 0; |DDEFECTO #4; |FINSI;

     #3#0 = #901#3;
     #3#1 = #901#4;
     |LEE 040#3=;
     |SI FSalida = 0;
         aDescri1 = #3#2;
         aDescri2 = #3#2;
         aDescri3 = #3#3;
         aDescri4 = #3#4;
         nDeDonde = 1;
     |SINO;
         aDescri1 = #4#2;
         aDescri2 = #4#16;
         aDescri3 = #4#17;
         aDescri4 = #4#18;
         nDeDonde = 2;
     |FINSI;

     #5#0 = #901#0; ||Cliente;
     #5#1 = #901#3; ||Seccion;
     #5#2 = #901#4; ||Concepto;
     |LEE 101#5=;
     |SI FSalida ! 0;
         #5#3  = "H";     ||Tipo;
         #5#4  = #4#33;
         #5#5  = "G";
         #5#6  = aDescri1;
         #5#7  = aDescri2;
         #5#8  = aDescri3;
         #5#9  = aDescri4;
         #5#10 = #901#7;
         #5#11 = #901#9;
         |GRABA 020#5n;
     |SINO;
         #5#3  = "H";
         #5#6  = aDescri1;
         #5#7  = aDescri2;
         #5#8  = aDescri3;
         #5#9  = aDescri4;
         #5#10 = #901#7;
         #5#11 = #901#9;
         |GRABA 020#5a;
    |FINSI;
    |LIBERA #5;

    |SI nDeDonde = 2;
         #7#0 = 2;
         #7#1 = #5#0;
         #7#2 = #5#1;
         #7#3 = #5#2;
         #7#4 = 1;
         |LEE 040#7=;
         |SI FSalida ! 0;
             |PARA nVariable = 1;  |SI nVariable [ 100;  |HACIENDO nVariable = nVariable + 1;
                   #7#0 = 1;
                   #7#1 = 0;
                   #7#2 = #5#1;
                   #7#3 = #5#2;
                   #7#4 = nVariable;
                   |LEE 040#7=;
                   |SI FSalida = 0;
                       #7#0 = 2;
                       #7#1 = #5#0;
                       #7#2 = #5#1;
                       #7#3 = #5#2;
                       #7#4 = nVariable;
                       |GRABA 020#7n;
                       |LIBERA #7;
                   |FINSI;
             |SIGUE;
         |FINSI;
     |SINO;
         #7#0 = 2;
         #7#1 = #5#0;
         #7#2 = #5#1;
         #7#3 = #5#2;
         #7#4 = 1;
         |LEE 040#7=;
         |SI FSalida ! 0;
             |PARA nVariable = 1;  |SI nVariable [ 100;  |HACIENDO nVariable = nVariable + 1;
                   #7#0 = 3;
                   #7#1 = 0;
                   #7#2 = #5#1;
                   #7#3 = #5#2;
                   #7#4 = nVariable;
                   |LEE 040#7=;
                   |SI FSalida = 0;
                       #7#0 = 2;
                       #7#1 = #5#0;
                       #7#2 = #5#1;
                       #7#3 = #5#2;
                       #7#4 = nVariable;
                       |GRABA 020#7n;
                       |LIBERA #7;
                   |FINSI;
             |SIGUE;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO ascuolin;
     #902#0 = #2#0;
     #902#1 = #2#1;
     #902#9 = #0#3;
     |LEE 101#902=;
     |SI FSalida = 0;
         |SI #0#4 = "N";
            |LIBERA #902;
            |ACABA;
         |SINO;
            |BORRA 020#902a;
            |LIBERA #902;
         |FINSI;
     |FINSI;

     #902#0 = #2#0;
     #902#1 = #2#1;
     #902#2 = #2#2;
     #902#3 = #2#3;
     #902#4 = #2#4;
     #902#6 = #2#6;

     |SI #0#0 = "+";  #902#5  = #2#5 > #0#1;  |FINSI;
     |SI #0#0 = "-";  #902#5  = #2#5 < #0#1;  |FINSI;

     #902#7 = #902#5 * #902#6;
     #902#8 = 0;
     #902#9 = #0#3;
     |GRABA 020#902n;
     |LIBERA #902;

     #901#6 = #901#6 + #902#7;
|FPRC;

|PROCESO ascuocab;
     #901#0 = #1#0;
     #901#8 = #0#3;
     |LEE 101#901=;
     |SI FSalida = 0;
         |SI #0#4 = "N";
            |LIBERA #901;
            |ACABA;
         |SINO;
            |BORRA 020#901a;
            |LIBERA #901;
         |FINSI;
     |FINSI;

     |DDEFECTO #901;
     #901#0 = #1#0;
     #901#8 = #0#3;
     #901#1 = #1#1;
     #901#2 = #1#2;
     #901#3 = #1#3;
     #901#4 = #1#4;
     #901#5 = #1#5;
     #901#9 = #1#9;

     |BUCLELIN 0ascuolin#1;

     |SI #901#2 = "M"; #901#7 = #901#6 / 12; |FINSI;
     |SI #901#2 = "T"; #901#7 = #901#6 / 4;  |FINSI;
     |SI #901#2 = "S"; #901#7 = #901#6 / 2;  |FINSI;
     |SI #901#2 = "A"; #901#7 = #901#6 / 1;  |FINSI;

     |GRABA 020#901n;
     |LIBERA #901;

     |HAZ GrabaConceptos;
|FPRC;

|DEFBUCLE ascuocab;
     #1#1
     ;
     00001, #0#2;
     99999, #0#3;
     ;
     NULL, ascuocab;
|FIN;

|PROCESO Tipo3;  |TIPO 3;
     |DBASE aBase;     |QBF aBase;

     aMas = aBase + "def/ascuocab.mas";
     |CARGA_DEF aMas, ascuocab@;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     aMas = aBase + "def/ascuolin.mas";
     |CARGA_DEF aMas, ascuolin@;
     |SI FSalida ! 0;
         |DESCARGA_DEF #ascuolin@;
         |ACABA;
     |FINSI;

     |ABRE #901;
     |ABRE #902;
     |ABRE #3;
     |ABRE #4;
     |ABRE #5;
     |ABRE #7;
     |BUCLE ascuocab;
     |CIERRA #3;
     |CIERRA #4;
     |CIERRA #5;
     |CIERRA #7;
     |CIERRA #901;
     |CIERRA #902;

     |DESCARGA_DEF #ascuolin@;
     |DESCARGA_DEF #ascuocab@;
|FPRC;
