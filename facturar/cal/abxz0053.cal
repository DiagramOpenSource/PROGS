|FICHEROS;
     abxz0053 #953;
     abom0514 #514;
     dsam0119 #119;
     dsam0120 #120;
     dsam0121 #121;
     dsam0122 #122;
     abxw0003 #903,MANTE;
|FIN;

|VARIABLES;
     aAbre          = "";
     aBase          = "";
     aAlfa          = "";
     aAlfa1         = "";
     aAlfa2         = "";
     aEsc1          = "";
     aEsc2          = "";
     aRetu          = "";
     aTecla         = "";
     aFichero       = "";
     aCarga         = "";
     aQueQuiero     = "";
     aTexto         = "";
     aLong          = "";
     aCarac         = "";
     aRefresca      = "";
     aBusca         = "";
     aIPRemoto      = "";
     aNombreIP      = "";
     aRuta          = "";

     nBotonLabel    = 0;
     nHandle        = 0;
     nTree          = -1;
     nPanta         = 0;
     nSubPanta      = 0;
     nLong          = 0;
     nCargar        = 0;
     nPos           = 0;
     nPosi          = 0;
     nBoton1        = 0;
     nBoton2        = 0;
     nBoton3        = 0;
     nSubVent       = 0;
     nCampo         = 0;
     nTCampo        = 0;
     nCarpeta1      = 0;
     nCarpeta2      = 0;
     FEstado        = 0;
     nSubBoton1     = 0;

     {-1}sTree      = 0;
     sT_nID         = 0;
     sT_nOper       = 0;
     sT_aData       = "";

     {-1}aVent      = "";
         aVent1     = 0;
         aVent2     = 0;
         aVent3     = 0;
         aVent4     = 0;
         aVent5     = "";

     &eaRutaOrigen  = "";
     &eaRutaDestino = "";
     &eaFicheroTxt  = "";
     &eaFicheroTgz  = "";
     &eaFicheroTar  = "";
     &eaAlfa        = "";
     &eaUnidadBasico = "";
     &eaDSMAC        = "";
     &enCodigo       = 0;
|FIN;

|PROCESO Tipo12Fxw3;  |TIPO 12;
|FPRC;

|PROCESO Browse;
     aRuta = "D" + #903#7;
     |BROWSE aRuta;
     aAlfa = aRuta % 280;  |QBF aRuta;
     |SI aAlfa ! "";
         #903#7 = aRuta % 280;
     |FINSI;
     |PINTA #903#7;
     |ERROR;
|FPRC;

|PROCESO Tipo0C7Fxw3;  |TIPO 0;
     |SI #514#21 = "C";  |ACABA;  |FINSI;

     aAlfa = #903#7;  |QBF aAlfa;
     |RMKDIR aAlfa;
|FPRC;

|PROCESO NuevoTipo;
     |DDEFECTO #903;

     |LEE 000#121u;
     |SI FSalida ! 0;  |DDEFECTO #119;  |FINSI;

     |SI #121#0 > 9999;
         |PARA nCampo = 1;  |SI nCampo [ 10000;  |HACIENDO nCampo = nCampo + 1;
               #121#0 = nCampo;
               |LEE 000#121=;
               |SI FSalida ! 0;
                   |SAL;
               |FINSI;
         |SIGUE;
     |FINSI;

     #121#0 = #121#0 + 1;

     |SI #121#0 > 9999;
         |MENAV "0000 Ya tenemos 9999 tipos creados, borre alguno que no use.";
         |ACABA;
     |FINSI;

     #903#0 = #121#0;

     |VENTANA 0701, 2599, -1, 17, "Nuevo Tipo";
     nSubVent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nSubVent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |PINPA #0#903;
     nSubPanta = FSalida;

     |C_M #903#7, 1, "N";
     |SI #514#21 = "D";
         |C_M #903#7, 1, "S";

         |CONTROL_SIMPLE nSubPanta, "BOTON,{{136}}", 1992, 1995, Browse;
         nSubBoton1 = FSalida;
     |FINSI;

     |PINDA #0#903;

     |ENDATOS #1#903;
     FEstado = FSalida;
     |SI FSalida = 0;
         #121#0 = #903#0;
         #121#1 = #903#1;
         #121#2 = #903#2;
         #121#3 = #903#3;
         #121#4 = #903#4;
         #121#5 = #903#5;

         |GRABA 020#121n;
         |LIBERA #121;

         |SI #514#21 = "D";
             enCodigo = #121#0;
             |HAZ CambiaAMac122;

             #122#0 = #121#0;
             #122#1 = eaDSMAC;
             |LEE 101#122=;
             |SI FSalida ! 0;
                 #122#0 = #121#0;
                 #122#1 = eaDSMAC;
                 |GRABA 020#122b;
             |FINSI;
             #122#2 = #903#6;
             #122#3 = #903#7;
             |GRABA 020#122a;
             |LIBERA #122;
         |FINSI;
     |FINSI;

     |SI #514#21 = "D";
         |FIN_CONTROL_WINDOWS nSubBoton1;
     |FINSI;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nSubVent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA nSubVent;

     |SI FEstado = 0;
         aBusca = #903#0;
         |HAZ MontaArbol;
     |FINSI;
|FPRC;

|PROCESO ModificarTipo;
     #121#0 = aTexto;
     |LEE 101#121=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |DDEFECTO #903;

     #903#0  = #121#0;
     #903#1  = #121#1;
     #903#2  = #121#2;
     #903#3  = #121#3;
     #903#4  = #121#4;
     #903#5  = #121#5;
     #903#6  = aNombreIP;

     |C_M #903#7, 1, "N";
     |SI #514#21 = "D";
         |C_M #903#7, 1, "S";

         enCodigo = #121#0;
         |HAZ CambiaAMac122;

         #122#0  = #121#0;
         #122#1  = eaDSMAC;
         |LEE 000#122=;
         |SI FSalida ! 0;  |DDEFECTO #122;  |FINSI;

         #903#7  = #122#3;
     |FINSI;

     |VENTANA 0701, 2599, -1, 17, "Modificar Tipo";
     nSubVent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nSubVent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |PINPA #0#903;
     nSubPanta = FSalida;

     |SI #514#21 = "D";
         |CONTROL_SIMPLE nSubPanta, "BOTON,{{136}}", 1992, 1995, Browse;
         nSubBoton1 = FSalida;
     |FINSI;

     |PINDA #0#903;

     |ENDATOS #1#903;
     FEstado = FSalida;
     |SI FSalida = 0;
         #121#1 = #903#1;
         #121#2 = #903#2;
         #121#3 = #903#3;
         #121#4 = #903#4;
         #121#5 = #903#5;

         |GRABA 020#121a;
         |LIBERA #121;

         |SI #514#21 = "D";
             enCodigo = #121#0;
             |HAZ CambiaAMac122;

             #122#0 = #121#0;
             #122#1  = eaDSMAC;
             |LEE 101#122=;
             |SI FSalida ! 0;
                 #122#0 = #121#0;
                 #122#1  = eaDSMAC;
                 |GRABA 020#122b;
             |FINSI;
             #122#2 = #903#6;
             #122#3 = #903#7;
             |GRABA 020#122a;
             |LIBERA #122;
         |FINSI;
     |FINSI;

     |SI #514#21 = "D";
         |FIN_CONTROL_WINDOWS nSubBoton1;
     |FINSI;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nSubVent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA nSubVent;

     |SI FEstado = 0;
         aBusca = #903#0;
         |HAZ MontaArbol;
     |FINSI;
|FPRC;

|PROCESO dsam0122B;
     |BORRA 020#122a;
     |LIBERA #122;
|FPRC;

|DEFBUCLE dsam0122B;
     #122#1;
     ;
     #121#0, "";
     #121#0, "zzzzzzzzzzzzzzzzzzzz";
     be;
     NULL, dsam0122B;
|FIN;

|PROCESO BorrarTipo;
     #121#0 = aTexto;
     |LEE 101#121=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |CONFI "0000NSeguro que quiere borrar el Tipo seleccionado";
     |SI FSalida ! 0;
         |LIBERA #121;
         |ACABA;
     |FINSI;

     |BUCLE dsam0122B;

     |BORRA 020#121a;
     |LIBERA #121;

     |LEE 000#121p;
     aBusca = #121#0;
     |HAZ MontaArbol;
|FPRC;

|PROCESO EventoTree;
     |SI FEntrada ! 0;  |ACABA;  |FINSI;

     aAlfa       = Contenido;  |QBF aAlfa;
     aTexto      = "";
     aLong       = aAlfa % 0;
     nLong       = aLong;
     nCargar     = 0;
     |PARA nPosi = 1;  |SI nPosi [ nLong;  |HACIENDO nPosi = nPosi + 1;
           nPos = (nPosi * 100) + 1;
           aCarac = aAlfa % nPos;

           |SI nCargar = 1;
               aTexto = aTexto + aCarac;
           |FINSI;

           |SI aCarac = ":";  nCargar = 1; |FINSI;
     |SIGUE;

     |ESTADO_CONTROL nBoton2, "ENABLE";
     |ESTADO_CONTROL nBoton3, "ENABLE";

     aAlfa = aTexto % 102;
     |SI aTexto = "";
         |ESTADO_CONTROL nBoton2, "DISABLE";
         |ESTADO_CONTROL nBoton3, "DISABLE";
     |FINSI;
|FPRC;

|PROCESO CargaArbol;
     |QBF aAlfa;
     aAlfa = aAlfa + &13 + &10;
     |XGRABA nHandle, aAlfa;
|FPRC;

|PROCESO dsam0122;
     aAlfa     = #122#1 - ".";
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     nCarpeta2 = nCarpeta2  + 1;
     aAlfa1    = #122#1;  |QBF aAlfa1;
     aAlfa2    = #122#2;  |QBF aAlfa2;

     |SI aAlfa1 = eaDSMAC;
         aAlfa1 = "---> " + aAlfa1;
     |FINSI;

     aAlfa = nCarpeta1 + "," + nCarpeta2 + ":" + aAlfa1 + " - " + aAlfa2;
     |HAZ CargaArbol;
|FPRC;

|DEFBUCLE dsam0122;
     #122#1;
     ;
     #121#0, "";
     #121#0, "zzzzzzzzzzzzzzzzzzzz";
     ;
     NULL, dsam0122;
|FIN;

|PROCESO dsam0121;
     nCarpeta1 = nCarpeta1  + 1;

     aAlfa = nCarpeta1 + ":" + (("0000" + #121#0) % -104) + " - " + #121#1;  |QBF aAlfa;
     aAlfa = aAlfa + " {{" + #121#0 + "}}";
     |HAZ CargaArbol;

     |SI #514#21 = "C";  |ACABA;  |FINSI;

     nCarpeta2 = 0;
     |BUCLE dsam0122;
|FPRC;

|DEFBUCLE dsam0121;
     #121#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, dsam0121;
|FIN;

|PROCESO MontaArbol;
     |SI nTree ! -1;
         |FIN_CONTROL_WINDOWS nTree;
         nTree = -1;
     |FINSI;

     |CONTROL_SIMPLE 0, "LABEL,{{5}}Cargando informaci¢n ...", 1315, 1680, NULL;
     nBotonLabel = FSalida;

     |PTEC 802;  |PAUSA;

     |DBASE aBase;  |QBF aBase;
     aAlfa = aBase + "basura";          |MKDIR aAlfa;

     eaRutaOrigen  = aBase + "basura/";
     eaRutaDestino = eaUnidadBasico + "\documentos";           |RMKDIR eaRutaDestino;
     eaRutaDestino = eaUnidadBasico + "\documentos\tmp";       |RMKDIR eaRutaDestino;
     eaRutaDestino = eaUnidadBasico + "\documentos\tmp\";
     eaFicheroTxt  = "abxz005301" + Usuario + ".txt";
     eaFicheroTgz  = "abxz005301" + Usuario + ".tgz";
     eaFicheroTar  = "abxz005301" + Usuario + ".tar";

     aAbre = eaRutaOrigen + eaFicheroTxt;

     sT_nID   = nTree;
     sT_nOper = 0;

     |XABRE "WB", aAbre, nHandle;

     nCarpeta1 = 0;

     |BUCLE dsam0121;

     |XCIERRA nHandle;

     |HAZ TraeTxtComprimido;
     aAlfa = eaRutaDestino + eaFicheroTxt;

     eaAlfa = "TREECTRL,Tipos de Documentos {{" + aAlfa + "}}";
     |CONTROL_SIMPLE nPanta, eaAlfa, 0702, 2598, EventoTree;
     nTree = FSalida;

     eaFicheroTxt  = "abxz005301" + Usuario + ".txt";
     aAlfa         = eaRutaDestino + eaFicheroTxt;
     |RFBORRA aAlfa;

     |FIN_CONTROL_WINDOWS nBotonLabel;

     |SI aBusca ! "";
         sT_nID   = nTree;
         sT_nOper = 3;
         sT_aData = "[" + aBusca;
         |ESPECIFICA "COMMANDCONTROL", sTree;
         aRefresca = ":" + FSalida;
     |FINSI;

     sT_nID   = nTree;
     sT_nOper = 2;
     |SI aRefresca = "";
         sT_aData = "1:";
     |SINO;
         sT_aData = aRefresca;
     |FINSI;

     |ESPECIFICA "COMMANDCONTROL", sTree;
     |PULSATECLA;

     |FOCOTECLADO nTree;
|FPRC;

|PROCESO Tipo80;  |TIPO 80;
     |ABRE #514;
     |LEE 000#514p;
     |SI FSalida ! 0;  |DDEFECTO #514;  |FINSI;
     |CIERRA #514;

     FSalida = 2799;
|FPRC;

|PROCESO dsam0121Chequeo;
     enCodigo = #121#0;
     |HAZ CambiaAMac122;

     #122#0 = #121#0;
     #122#1 = eaDSMAC;
     |LEE 101#122=;
     |SI FSalida ! 0;
         |DDEFECTO #122;
         #122#0 = #121#0;
         #122#1 = eaDSMAC;
         #122#2 = aNombreIP;
         |GRABA 020#122n;
     |SINO;
         #122#2 = aNombreIP;
         |GRABA 020#122a;
     |FINSI;
     |LIBERA #122;
|FPRC;

|DEFBUCLE dsam0121Chequeo;
     #121#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, dsam0121Chequeo;
|FIN;

|PROGRAMA;
     |HAZ LeeUnidadBasico;
     |HAZ CogeMac;

     |ABRET #119;
     |ABRET #120;
     |ABRET #121;
     |ABRET #122;

     aIPRemoto = "";
     |IP_REMOTO aIPRemoto;   |QBT aIPRemoto;
     |SI aIPRemoto = "";
         aIPRemoto = "LOCAL";
         aNombreIP = "MAQUINA LOCAL";
     |SINO;
         |NOMBRE_IP aIPRemoto, aNombreIP;
     |FINSI;

     |BUCLE dsam0121Chequeo;

     |CLS;
     |CABEZA "Tipos de Documentos";

     |PINPA #0#953;
     nPanta = FSalida;

     aAlfa = "LABEL,{{0}} Configuraci¢n de la MAC " + eaDSMAC;
     |CONTROL_SIMPLE nPanta, aAlfa, 0650, 0698, NULL;

     |CONTROL_SIMPLE nPanta, "BOTON,{{193}}Nuevo Tipo", 2634, 2654, NuevoTipo;
     nBoton1 = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,{{197}}Modificar Tipo", 2656, 2676, ModificarTipo;
     nBoton2 = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,{{199}}Borrar Tipo", 2678, 2698, BorrarTipo;
     nBoton3 = FSalida;

     |HAZ MontaArbol;

     aEsc1  = &255 + "806";
     aEsc2  = &255 + "807";
     aRetu  = &255 + "802";
     |PARA; |SI;  |HACIENDO;
         FSalida = "::{{001}}Salir," + nTree;
         |LEETECLA aTecla;
         |SI aTecla = aEsc1; |SAL;  |FINSI;
         |SI aTecla = aEsc2; |SAL;  |FINSI;
         |SI aTecla = aRetu; |SAL;  |FINSI;
     |SIGUE;

     |FIN_CONTROL_WINDOWS nTree;
     |FIN_CONTROL_WINDOWS nBoton1;
     |FIN_CONTROL_WINDOWS nBoton2;
     |FIN_CONTROL_WINDOWS nBoton3;

     |CIERRAT #119;
     |CIERRAT #120;
     |CIERRAT #121;
     |CIERRAT #122;
|FPRO;
