          **** PASE DEL HISTORICO DE RECIBOS AL SECUENCIAL ADAPTADO A LA ***
                        ***     NUEVA NORMA 58    ****

|FICHEROS;
  aspasc58 #0;
  ashisrec #1;
  asclient #2;
  indrecib #3;
  ashiscon #4;
  ascaisec #5;
  ashisfac #6;

  ascabmin #7;
  aslinmin #8;
  asempcon #9;
  asenlcon #10;
  ascontro #11;
  ascontrt #450;
  asconcep #12;
  ascabrem #13;
  aslinrem #14;
  asm00013 #15;
  asw00001 #99;
  ashismic #90;

  asparcia #100;
|FIN;

|VARIABLES;
    &eaMoneda  = "";
    &enCanti   = 0;
    &enSwTrata = 0;
    fFech      = @;
    qFech      = "";
    dFech      = "";
    hFech      = "";
    Moneda     = "";

    cabe1p    = "0170";  || Registro Presentador pesetas
    cabe1e    = "5170";  || Registro Presentador euros
    cabe1     = "";
    cabe2p    = "0370";  || Registro Ordenante pesetas
    cabe2e    = "5370";  || Registro Ordenante euros
    cabe2     = "";
    cabe3p    = "0670";  || Registro Individual pesetas
    cabe3e    = "5670";  || Registro Individual euros
    cabe3     = "";
    cabe4p    = "0676";  || Registro Individual NO domiciliado pesetas
    cabe4e    = "5676";  || Registro Individual NO domiciliado euros
    cabe4     = "";
    tord1p    = "0870";  || Total Ordenante pesetas
    tord1e    = "5870";  || Total Ordenante euros
    tord1     = "";
    tgen1p    = "0970";  || Total General pesetas
    tgen1e    = "5970";  || Total General euros
    tgen1     = "";

    NombreP = "                                        ";
    NifP    = "000000000000";
    sufiP   = "000";
    EntP    = "0000";
    SucP    = "0000";
    banAlfa = "           ";
    NombreO = "                                        ";
    NifO    = "000000000";
    sufiO   = "000";
    EntO    = "0000";
    SucO    = "0000";
    dcO     = "00";
    CtaO    = "0000000000";
    LocO    = "                                      ";
    ProP    = "00";
    ConCli  = "";
    fechapre = "      ";
    fechacar = "      ";
    dd       = "";
    mm       = "";
    aa       = "";
    tiProced = "06";
    sino     = "";
    ceros    = "000000000000000";

    libre6   = "      ";
    libre8   = "        ";
    libre10  = "          ";
    libre12  = "            ";
    libre16  = "                ";
    libre20  = "                    ";
    libre38  = "                                      ";
    libre40  = "                                        ";
    libre43  = "                                           ";
    libre52  = "                                                    ";
    libre72  = "                                                                        ";
    l         = 0;
    trecibo   = 0;
    timpsuc   = 0;
    tnumsuc   = 0;

    handle    = "";
    Varalfa   = "";
    varalfa1  = "";
    varalfa2  = "";
    contenido = "";
    elefe     = "";
    ceerre    = "";
    traba     = "";

    fiche     = "";
    espacio   = " ";
    vacio     = "          ";

    aFichero = "";
    ca1  = "";
    ca2  = "";
    ca3  = "";
    ca4  = "";
    ca5  = "";
    ca6  = "";
    ca7  = "";
    ca8  = "";
    ca9  = "";
    ca10 = "";

    alfa1     = "";
    alfa2     = "";
    Ultimo    = "";
    aAlfa     = "";
    swERROR   = 0;
    swdomi    = 0;
    nume      = 0;
    ccc       = "";
    empresa   = "";
    c_alfa1   = "";
    c_alfa2   = "";
    c_nume1   = 0;
    c_nume2   = 0;
    c_nume3   = 0;

  sw_existe = 0;
  varalf    = "";
  iva       = 0;
  c_clie    = "";
  fijo      = "";
  conver    = "";
  conver1   = "";
  conver2   = "";
  linea     = 0;

  &efFechaO = @;
  &efFechaL = @;
  &enImporte = 0;
  &enDesRemesa = 0;
|FIN;

|INCLUYE agicon_i;

|| ********************************************************************
|PROCESO MiraVarMoneda;  |TIPO 7;
     |HAZ PintaMoneda;
     #0#22 = eaMoneda;

     |PINTA #0#22;
|FPRC;

|PROCESO sacaano; |TIPO 7;
     fFech = ~;
     qFech = fFech % -104;
     dFech = "01.01." + qFech;
     hFech = "31.12." + qFech;
     #0#4  = dFech;
     #0#5  = hFech;
     |PINTA #0#4;
     |PINTA #0#5;
|FPRC;

|PROCESO que_selec;  |TIPO 0;
    |SI #0#15 = "N";
        |CAMPO_MODIFICA #0#0,  1, "S"; |PINTA #0#0;
        |CAMPO_MODIFICA #0#1,  1, "S"; |PINTA #0#1;
        |CAMPO_MODIFICA #0#2,  1, "S"; |PINTA #0#2;
        |CAMPO_MODIFICA #0#3,  1, "S"; |PINTA #0#3;
        |CAMPO_MODIFICA #0#4,  1, "S"; |PINTA #0#4;
        |CAMPO_MODIFICA #0#5,  1, "S"; |PINTA #0#5;
        |CAMPO_MODIFICA #0#6,  1, "S"; |PINTA #0#6;
        |CAMPO_MODIFICA #0#7,  1, "S"; |PINTA #0#7;
        |CAMPO_MODIFICA #0#16, 1, "N"; |PINTA #0#16;
    |SINO;
        |CAMPO_MODIFICA #0#0,  1, "N"; |PINTA #0#0;
        |CAMPO_MODIFICA #0#1,  1, "N"; |PINTA #0#1;
        |CAMPO_MODIFICA #0#2,  1, "N"; |PINTA #0#2;
        |CAMPO_MODIFICA #0#3,  1, "N"; |PINTA #0#3;
        |CAMPO_MODIFICA #0#4,  1, "N"; |PINTA #0#4;
        |CAMPO_MODIFICA #0#5,  1, "N"; |PINTA #0#5;
        |CAMPO_MODIFICA #0#6,  1, "N"; |PINTA #0#6;
        |CAMPO_MODIFICA #0#7,  1, "N"; |PINTA #0#7;
        |CAMPO_MODIFICA #0#16, 1, "S"; |PINTA #0#16;
    |FINSI;
|FPRC;

|PROCESO hay_remesa; |TIPO 0;
    |SI #0#16 = 0; |ACABA; |FINSI;

    |ABRE #13;
    #13#0 = #0#16;
    |LEE 040#13=;
    |SI FSalida ! 0;
        |MENAV "      No existe Remesa";
        |ERROR;
    |SINO;
        |SI #13#5 = "X";
            |CONFI "0000NEsta Remesa ya ha sido pasada por el Banco. Volver a realizar el Proceso";
            |SI FSalida ! 0;
                |ERROR;
            |FINSI;
        |FINSI;
    |FINSI;
    |CIERRA #13;
|FPRC;

||...................................................................................
||***********************************************************
||************ REGISTRO DE CABECERA DE PRESENTADOR **********
||***********************************************************
|PROCESO cabecera_p;
  cabe1 = cabe1e;
  cabe2 = cabe2e;
  cabe3 = cabe3e;
  cabe4 = cabe4e;
  tord1 = tord1e;
  tgen1 = tgen1e;
  |SI Moneda = "P";
      cabe1 = cabe1p;
      cabe2 = cabe2p;
      cabe3 = cabe3p;
      cabe4 = cabe4p;
      tord1 = tord1p;
      tgen1 = tgen1p;
  |FINSI;

  varalfa1 = "";
  varalfa1 =  cabe1 + NifP + fechapre + libre6 + NombreP;
  varalfa1 =  varalfa1 + libre20 + EntP + SucP;
  varalfa1 =  varalfa1 + libre12 + banAlfa + libre43;

  contenido = handle + " " + varalfa1 + elefe;
  |FGRABA contenido;

  ||   l = l + 1; .. este registro no suma hasta el final
|FPRC;

||***********************************************************
||************ REGISTRO DE CABECERA DE ORDENANTE ************
||***********************************************************

|PROCESO cab_ordenante;
  varalfa1 = "";
  varalfa1 =  cabe2 + NifO + fechapre + fechacar + NombreO;
  varalfa1 =  varalfa1 + EntO + SucO + dcO + CtaO;
  varalfa1 =  varalfa1 + libre8 + "06";
  varalfa1 =  varalfa1 + libre10 + banAlfa + libre43;

  contenido = handle + " " + varalfa1 + elefe;
  |FGRABA contenido;

  l = l + 1;
|FPRC;

||***********************************************************
||******************* REGISTRO INDIVIDUAL *******************
||***********************************************************
|PROCESO regindiv;
  |SELECT #3#3;          ||Cojo la tercera clave del fichero CCC y Cta.
  |LEE 040#3p;
  |SI FSalida ! 0;  |ACABA;  |FINSI;

|ET compara;
  |HAZ campo_indiv;
  trecibo = trecibo + 1;
  l = l + 1;

  enSwTrata = 1;
  enCanti   = #3#7;
  |HAZ ConverMoneda;
  timpsuc = timpsuc + enCanti;

  |SI #5#24 = "S"; |HAZ direccion; |FINSI;

  |LEE 040#3s;
  |SI FSalida = 0;  |VETE compara; |FINSI;
|FPRC;

|PROCESO campo_indiv;

  swERROR = 0;
  ca1 = "";                 || Referencia Cliente
  |SI #0#23 = "S";
      ca1 = #3#3;
  |SINO;
      ca1 = #3#9;
  |FINSI;
  |QBF ca1;
  |SI ca1 = ""; swERROR = 1; |FINSI; || ..... |ERROR10; |ACABA; |FINSI;
  ca1 = ceros + ca1; ca1 = ca1 % -112;

  ca2 = #3#2 + libre43;                                     || Nombre Cliente
  ca2 = ca2 % 140;

  ccc = #3#5;  |QBF ccc;  |HAZ cccBanco;         || Datos del banco del Cobro
  swdomi = 0;
  |SI ccc = ""; swdomi = 1; |FINSI; || ....... |ERROR10; |ACABA; |FINSI;
  traba = ccc % 0;
  nume = traba;
  |SI nume ! 10;  swdomi = 1;  |FINSI;
  ca3 = ceros + ccc; ca3 = ca3 % -110;            || C.C.C.

  ccc = #3#6;  |QBF ccc;  |HAZ cccBanco;
  |SI ccc = ""; swdomi = 1; |FINSI; || ....... |ERROR10; |ACABA; |FINSI;
  traba = ccc % 0;
  nume = traba;
  |SI nume ! 10;  swdomi = 1;  |FINSI;
  ca4 = ceros + ccc; ca4 = ca4 % -110;            || Cuenta corriente  Cliente

  enCanti   = #3#7;
  enSwTrata = 0;
  |HAZ ConverMoneda;
  ca5 = enCanti; |QBF ca5;
  ca5 = ceros + ca5; ca5 = ca5 % -110;

  ca6 = ConCli + " ";
  |SI Ultimo = "/";
      alfa1 = #3#0; |QBF alfa1;
      ca6 = ca6 + alfa1;
      alfa1 = #3#1; |QBF alfa1;
      ca6 = ca6 + alfa1 + " ";
  |FINSI;
  |SI Ultimo = "*";
      alfa1 = #3#1; |QBF alfa1;
      ca6 = ca6 + alfa1 + " ";
  |FINSI;

  ca6 = ca6 + libre43; ca6 = ca6 % 140;            || Concepto

  |SI #aspasc58#26 = "01.01.0000";
     alfa2 = #3#8;
  |SINO;
     alfa2 = #aspasc58#26;
  |FINSI;
  ca7   = alfa2 % 102;
  ca7   = ca7 + (alfa2 % 402);
  ca7   = ca7 + (alfa2 % 902);

  ca8   = #3#10; ca8 = ca8 + libre43; ca8 = ca8 % 140;  || Domicilio
  ca9   = #3#11; ca9 = ca9 + libre43; ca9 = ca9 % 135;  || Poblacion

  alfa1 = #3#12; |QBF alfa1; alfa1 = ("00"  + alfa1 ) % -102;
  alfa2 = #3#13; |QBF alfa2; alfa2 = ("000" + alfa2 ) % -103;
  ca10  = alfa1 + alfa2;                                || Codigo

  || ... Registro Individual Obligatorio (0670) ........................
  |SI swdomi = 1;
      ca3 = "          ";
      ca4 = "          ";
  |FINSI;
  varalfa1 = "";

  varalfa1 = cabe3 + NifO + ca1 + ca2 + ca3 + ca4 + ca5;
  varalfa1 = varalfa1 + #0#24 + #0#25 + ca6 + ca7 + "  ";

  contenido = handle + " " + varalfa1 + elefe;
  |FGRABA contenido;
|FPRC;

|PROCESO direccion;
  || ... Registro Individual Obligatorio (0676) ........................
||  |SI swdomi = 1;
      varalfa1 = "";
      varalfa1 = cabe4 + NifO + ca1 + ca8 + ca9 + ca10 + LocO + ProP;
      varalfa1 = varalfa1 + fechapre + libre8;
      contenido = handle + " " + varalfa1 + elefe;
      |FGRABA contenido;
      l = l + 1;
      swdomi = 0;
||  |FINSI;
|FPRC;

||************************************************************
||********** REGISTRO TOTAL ORDENANTE Y GENERAL **************
||************************************************************
|PROCESO campo_totales;

|| **** REGISTRO TOTAL ORDENANTE

  |SI #0#22 = "EUROS  ";
      timpsuc = timpsuc * 100;
  |FINSI;
  ca1 = timpsuc; |QBF ca1;
  ca1 = ceros + ca1; ca1 = ca1 % -110;

  ca2 = trecibo; |QBF ca2;
  ca2 = ceros + ca2; ca2 = ca2 % -110;

  l = l + 1;

  ca3 = l; |QBF ca3;
  ca3 = ceros + ca3; ca3 = ca3 % -110;

  varalfa1 = "";
  varalfa1 = tord1 + NifO + libre72 + ca1 + libre6;
  varalfa1 = varalfa1 + ca2 + ca3 + libre38;

  contenido = handle + " " + varalfa1 + elefe;
  |FGRABA contenido;

|| **** REGISTRO TOTAL GENERAL

  l = l + 2; || Sumamos este ultimo y el primero.

  ca3 = l; |QBF ca3;
  ca3 = ceros + ca3; ca3 = ca3 % -110;   || Numero total de Registros

  ca4 = "0001";                          || Numero de Ordenantes

  varalfa1 = tgen1 + NifP + libre52 + ca4 + libre16;
  varalfa1 = varalfa1 + ca1 + libre6 + ca2 + ca3 + libre38;

  contenido = handle + " " + varalfa1 + elefe;
  |FGRABA contenido;


  #0#17 = trecibo; |PINTA #0#17;
  #0#18 = l;       |PINTA #0#18;
  #0#19 = timpsuc;
  |SI #0#22 = "EUROS  ";
      #0#19 = timpsuc / 100;
  |FINSI;
  |PINTA #0#19;

|FPRC;

|| *******************************************************************
|| .... prepara el fichero auxiliar de recibos
|| *******************************************************************
|CALCULO secuen_01;
  enImporte =( #1#6 - #1#9 ) ! 2;
  |SI enImporte [ 0; |ACABA; |FINSI;   || no hay nada pendiente de cobrar

  #2#0 = #1#3;              ||** Lee fichero de clientes;
  |LEE 040#2=;              ||** Ordena por Banco/Sucursal;
  |SI FSalida = 0;

      #3#0 = #1#0;  ||** Serie;
      #3#1 = #1#1;  ||** Factura;
      |SI #0#21 = "S";
          Varalfa = #2#0;
          Varalfa = ("00000" + Varalfa) % -105;
          #3#2    = Varalfa + " " + #2#2;
      |SINO;
          #3#2 = #2#2;  ||** Nom. Cliente;
      |FINSI;
      #3#3 = #2#8;  ||** Cif/Dni;
      #3#4 = #2#16; ||** Banco;
      #3#5 = #2#13; ||** C.C.C;
      #3#6 = #2#14; ||** Cta del banco

      #3#7 = enImporte;  ||** Importe Recibo;
      |SI #5#13 = 1;  #3#8 = #1#2;  |FINSI;  ||** Fecha Emision
      |SI #5#13 = 2;  #3#8 = #1#5;  |FINSI;  ||** Fecha Vencimiento;

      #3#9   = #2#0;  || Numero
      #3#10  = #2#3;  || Direccion
      #3#11  = #2#6;  || Poblacion
      #3#12  = #2#4;  || C.P1
      #3#13  = #2#5;  || C.P2
      |GRABA 020#3n;||** Graba el fichero indexado;
      |LIBERA #3;

      #1#10 = "S";
      #1#12 = "R";
      #1#8  = ~;
      #1#9  = #1#9  + enImporte;
      #1#15 = enImporte;
      |GRABA 021#1a;
      |LIBERA #1;

      efFechaL = #0#8;
      |SI #aspasc58#15 = "N";
          |HAZ eGrCobroParcial;
      |FINSI;

      |ABRE #90;
      #90#0 = #1#0;
      #90#1 = #1#1;
      |LIBERA #90;
      |LEE 140#90=;
      |SI FSalida = 0;
          #90#32 = #1#10;
          #90#33 = #1#12;
          |GRABA 140#90a;
      |FINSI;
      |CIERRA #90;

      enDesRemesa = 0;
      |HAZ eGraba_Min;
  |SINO;
      |AVISO;
      |PINTA 2218,"ATENCION Cliente Inexistente! CODIGO.:"
      |PINTA 2257,#1#3;
      |PAUSA;
  |FINSI;
|FCAL;

|DEFBUCLE ashisrec;
  #1#1;
  2,7;
  #0#0, #0#2, #0#4, #0#6;
  #0#1, #0#3, #0#5, #0#7;
  be;
  NULL,secuen_01;
|FIN;

|CALCULO secuen_02;
  #1#0 = #14#2;    ||** Lee recibo
  #1#1 = #14#3;
  |LIBERA #1;
  |LEE 140#1=;
  |SI FSalida ! 0; |ACABA; |FINSI;

  #2#0 = #1#3;              ||** Lee fichero de clientes;
  |LEE 040#2=;              ||** Ordena por Banco/Sucursal;
  |SI FSalida ! 0; |ACABA; |FINSI;

  #3#0 = #1#0;  ||** Serie;
  #3#1 = #1#1;  ||** Factura;
  |SI #0#21 = "S";
      Varalfa = #2#0;
      Varalfa = ("00000" + Varalfa) % -105;
      #3#2    = Varalfa + " " + #2#2;
  |SINO;
      #3#2 = #2#2;  ||** Nom. Cliente;
  |FINSI;
  #3#3 = #2#8;   ||** Cif/Dni;
  #3#4 = #2#16;  ||** Banco;
  #3#5 = #2#13;  ||** C.C.C;
  #3#6 = #2#14;  ||** Cta.Cte;
  #3#7 = #14#7;  ||** Importe Recibo;
  |SI #5#13 = 1;  #3#8 = #1#2;  |FINSI;  ||** Fecha Emision
  |SI #5#13 = 2;  #3#8 = #1#5;  |FINSI;  ||** Fecha Vencimiento;

  #3#9 = #2#0;
  #3#10  = #2#3;  || Direccion
  #3#11  = #2#6;  || Poblacion
  #3#12  = #2#4;  || C.P1
  #3#13  = #2#5;  || C.P2
  |GRABA 020#3n; ||** Graba el fichero indexado;
  |LIBERA #3;

  efFechaL = #0#8;
  efFechaO = #13#6;
  enDesRemesa = 1;
  |HAZ eGraba_Min;
|FCAL;

|DEFBUCLE aslinrem;
  #14#1;
  ;
  #0#16, 000;
  #0#16, 999;
  ;
  NULL, secuen_02;
|FIN;

|| ///////////////////////////////////////////////////////////////////////
|CALCULO pase;|TIPO 3;

  eaMoneda = #0#22;

  Moneda = "E";
  |SI #0#22 = "PESETAS"; Moneda = "P"; |FINSI;

  elefe  = &010;
  ceerre = &013;
  |SI #0#13 = "S";
       traba = ceerre + elefe;
       elefe = traba;
  |FINSI;

  aFichero = ("58x" + Usuario) % 108;
  |NOME_DAT #3 aFichero;

  |ABRE #3;  |CIERRA #3;  |DELINDEX #3;

  |ABRE #2;
  |ABRE #3;

  |SI #0#15 = "N";
       |BUCLE ashisrec;
  |SINO;
       |ABRE #1;
       |BUCLE aslinrem;
       |CIERRA #1;
  |FINSI;

  |CIERRA #2;

  |HAZ varia_fich;

  |DFICE empresa;
  fiche = "wb  " + empresa + "assecrec.seq";  |QBF fiche;

  |FABRE fiche;
  handle = FSalida;
  |SI FSalida ] 0;
       |HAZ cabecera_p;
       |HAZ cab_ordenante;
       |HAZ regindiv;
       |HAZ campo_totales;
   |FINSI;

   FSalida = handle;
   |FCIERRA FSalida;

   |CIERRA #3; |DELINDEX #3;

  |SI #0#15 = "S";
      |ABRE #13;
      #13#0 = #0#16;
      |LEE 141#13=;
      |SI FSalida = 0;
          #13#5 = "X";
          #13#6 = efFechaL;
          |GRABA 020#13a;
      |FINSI;
      |LIBERA #13;
      |CIERRA #13;
  |FINSI;

   E_sup = 1;
   E_inf = "SNsn";
   sino  = "S";
   sino  = 1869 ? 1;
   sino  = sino > " ";
   |SI sino = "S"; |SUB_EJECUTA_NP "ascopdis"; |FINSI;
|FCAL;

|PROCESO varia_fich;

  |ABRE #5;
  #5#0 = #0#9;
  |LEE 040#5=;
  |CIERRA #5;

|| .......... Variables del Presentador, del Ordenante y comunes
  NombreP = #5#3 + libre43;     NombreP = NombreP % 140;

  NifP    = #5#2;
  sufiP   = #0#12; |QBT sufiP;
  sufiP   = ceros + sufiP;      sufiP = sufiP % -103;
  NifP    = NifP + sufiP;

  EntP    = #5#4; |QBT EntP;
  EntP    = ceros + EntP;       EntP    = EntP % -104;

  SucP    = #5#5; |QBT SucP;
  SucP    = ceros + SucP;       SucP    = SucP % -104;

  banAlfa = #5#19 + libre43;     banAlfa = banAlfa % 111;

  NombreO = #5#22 + libre43;    NombreO = NombreO % 140;

  NifO    = #5#23;
  sufiO   = #0#12; |QBT sufiO;
  sufiO   = ceros + sufiO;      sufiO = sufiO % -103;
  NifO    = NifO + sufiO;

  EntO    = #5#7; |QBT EntO;
  EntO    = ceros + EntO;       EntO    = EntO % -104;

  SucO    = #5#8; |QBT SucO;
  SucO    = ceros + SucO;       SucO    = SucO % -104;

  dcO     = #5#11; |QBT dcO;
  dcO     = ceros + dcO;        dcO     = dcO % -102;

  CtaO    = #5#6; |QBT CtaO;
  CtaO    = ceros + CtaO;       CtaO    = CtaO % -110;

  LocO    = #5#21 + libre43;    LocO    = LocO % 138;

  ProP    = #5#20; |QBT ProP;
  ProP    = ceros + ProP;       ProP    = ProP % -102;

  Ultimo  = #5#9 % -101;
  ConCli  = #5#9 % 139; |QBF ConCli;

  alfa1 = #0#8;
  dd = alfa1 % A102;
  mm = alfa1 % A402;
  aa = alfa1 % A902;
  fechapre = dd + mm + aa;   || Fecha Formato dia + mes + a¤o

  alfa1 = #0#11;
  dd = alfa1 % A102;
  mm = alfa1 % A402;
  aa = alfa1 % A902;
  fechacar = dd + mm + aa;   || Fecha Cargo Formato dia + mes + a¤o

|FPRC;

|| ************************************************************************
|PROCESO cccBanco;
 |QBT ccc;
 c_alfa1 = ccc % 0;
 c_nume1 = c_alfa1;
 c_alfa1 = "";
 c_alfa2 = "";
 |PARA c_nume2 = 1; |SI c_nume2 [ c_nume1; |HACIENDO c_nume2 = c_nume2 + 1;
       c_nume3 = c_nume2 * 100 + 1;
       c_alfa2 = ccc % c_nume3;
       |SI c_alfa2 ] "0"; |Y c_alfa2 [ "9";
           c_alfa1 = c_alfa1 + c_alfa2;
       |FINSI;
 |SIGUE;
 ccc = c_alfa1;
|FPRC;
