|FICHEROS;
     abxz0056 #956;
     abor0006 #6;
     abom0500;
     abom0514 #514;
     abxm0505 #505;
     abom0511 #511;
     abom0518 #518;
     abom9000 #900;

     abxw0009 #909,MANTE;

     abom0006@ #1006;
|FIN;

|VARIABLES;
     nCuentaPara = 0;

     nRango          = 0;
     nHandle         = 0;
     nLong           = 0;
     nCaracter       = 0;
     nPos            = 0;
     nCarga          = 0;
     nCarac          = 0;
     nVentana        = 0;
     nSubVent        = 0;
     nVent           = 0;
     nVentTrab       = 0;
     nCampo          = 0;
     nBoton1         = 0;
     nBoton2         = 0;
     nBoton3         = 0;
     nBoton4         = 0;
     nSubBoton1      = 0;
     nDExpediente    = 0;
     nHExpediente    = 0;
     nSubPanta       = 0;
     nModo           = 0;
     nCont           = 0;
     nPasa           = 0;
     nError          = 0;
     nPosi           = 0;
     nLen            = 0;
     nSwEnvio        = 0;
     nSwMensaje      = 0;
     nDirecto        = 0;
     nNume1          = 0;
     nNume2          = 0;
     nGuardaExpe     = 0;

     fFecha          = @;

     aEsc1           = "";
     aEsc2           = "";
     aRetu           = "";
     aTecla          = "";
     aAlfa           = "";
     aHora           = "";
     aNumero        = "";
     aPersona       = "";
     aCadena        = "";
     aComodin       = "";
     aAbre          = "";
     aBase          = "";
     aMas           = "";
     aRutaCorreo    = "";
     aCorreo        = "";
     aEjecuta       = "";
     aEnviado       = "";
     aRef           = "";
     aFic           = "";
     aIncidencia    = "";
     aLen           = "";
     aCaracter      = "";
     aReferencia    = "";
     aPathZimbra    = "";
     aOrigen        = "";
     aDestino       = "";
     aDocRemoto     = "";
     aDocServidor   = "";
     aJava          = "";
     aLong          = "";

     {-1}aVent       = "";
         aVent1      = 0;
         aVent2      = 0;
         aVent3      = 0;
         aVent4      = 0;
         aVent5      = "";


     {1}aContiene    = "";

     &enGrid1         = 0;
     &enPanta0        = 0;
     &enSubPanta      = 0;
     &enExped         = 0;
     &enLabel1        = -1;
     &enLabel2        = -1;
     &enLabel3        = -1;
     &enExpediente    = 0;
     &enCodUbica      = 0;
     &eaRutaDocumento = "";
     &efFechaDocu     = @;
     &eaFicheroEdita  = "";
     &eaFichAdjunto   = "";
     &enCodExpe       = 0;
     &enCodPers       = 0;
     &enCuenta        = 0;
     &enFSalida       = 0;
     &eaUnidadBasico  = "";
|FIN;

|PROCESO GrabaLogCorreo;
     |ABRE #900;
     #900#0  = aReferencia;
     |LEE 101#900=;
     |SI FSalida ! 0;
         |DDEFECTO #900;
         #900#0  = aReferencia;
         |GRABA 020#900b;
     |FINSI;

                          #900#1  = "SI";
     |SI aEnviado ! "S";  #900#1  = "NO";  |FINSI;

     #900#2 = aIncidencia;
     #900#3 = #511#2;
     #900#4 = #6#1;
     #900#5 = (("00" + #6#2) % -102) + ":" + (("00" + #6#3) % -102) + ":" + (("00" + #6#18) % -102);
     #900#6 = #6#5;
     #900#7 = (("00" + #6#6) % -102) + ":" + (("00" + #6#7) % -102) + ":" + "00";

     |GRABA 020#900a;
     |LIBERA #900;
|FPRC;

|PROCESO PorOutlook;
     aCorreo = #511#2;
     aFic    = aRutaCorreo + #511#1;    |QBF aFic;
     aAlfa   = aFic + ".err";   |RFBORRA aAlfa;
     aAlfa   = aFic + ".bue";   |RFBORRA aAlfa;

     |HAZ TraeDSENVIO;
     |SI enFSalida ! 0;
         nError      = 1;
         aEnviado    = "N";
         nSwEnvio    = 1;
         nSwMensaje  = 1;
         aIncidencia = "No puedo traerme el DSENVIO del servidor";
         |HAZ GrabaLogCorreo;
         |ACABA;
     |FINSI;

     aAbre   = aFic + ".txt";
     |XABRE "WBC", aAbre, nHandle;

     aAlfa = #518#4; |QBF aAlfa;
     aAlfa = aAlfa + &13 + &10;    |XGRABA nHandle, aAlfa;

     aAlfa = #518#6; |QBF aAlfa;
     aAlfa = aAlfa + &13 + &10;    |XGRABA nHandle, aAlfa;

     aAlfa = #518#7; |QBF aAlfa;
     aAlfa = aAlfa + &13 + &10;    |XGRABA nHandle, aAlfa;

     aAlfa = #518#5; |QBF aAlfa;
     aAlfa = aAlfa + &13 + &10;    |XGRABA nHandle, aAlfa;

     aAlfa = #511#2; |QBF aAlfa;
     aAlfa = aAlfa + &13 + &10;    |XGRABA nHandle, aAlfa;

     aAlfa = (#6#5 % 102) + "/" + (#6#5 % 402) + "/" + (#6#5 % -104);
     aAlfa = aAlfa + &13 + &10;    |XGRABA nHandle, aAlfa;

     aAlfa = (("00" + #6#6) % -102) + ":"+ (("00" + #6#7) % -102);
     aAlfa = aAlfa + &13 + &10;        |XGRABA nHandle, aAlfa;

     nNume1 = #6#6;
     nNume2 = #6#7 + 5;

     |SI nNume2 ] 60;
         nNume1 = #6#6 + 1;
         nNume2 = 0;
     |FINSI;

     |SI nNume1 > 23;
         nNume1 = #6#6;
         nNume2 = 59;
     |FINSI;

     aAlfa = (("00" + nNume1) % -102) + ":"+ (("00" + nNume2) % -102);
     aAlfa = aAlfa + &13 + &10;        |XGRABA nHandle, aAlfa;

     aAlfa = "";
     |SI #6#0 ! 0;
         aAlfa = aAlfa + #6#0 + "  ";
     |FINSI;

     |SI #6#19 = "S";;
         aAlfa = aAlfa + "PLAZO  ";
     |FINSI;

     aAlfa = aAlfa + #6#10;  |QBF aAlfa;
     aAlfa = aAlfa + " ";
     aAlfa = aAlfa + #6#11;  |QBF aAlfa;
     aAlfa = aAlfa + &13 + &10;  |XGRABA nHandle, aAlfa;

     aAlfa = #6#4 > "";
     aAlfa = aAlfa + &13 + &10;        |XGRABA nHandle, aAlfa;

     aAlfa = aRef + &13 + &10;         |XGRABA nHandle, aAlfa;

     aAlfa = #6#10;   |QBF aAlfa;
     aAlfa = aAlfa + &13 + &10;        |XGRABA nHandle, aAlfa;

     aAlfa = #6#11;   |QBF aAlfa;
     aAlfa = aAlfa + &13 + &10;        |XGRABA nHandle, aAlfa;

     aAlfa = #6#12;   |QBF aAlfa;
     aAlfa = aAlfa + &13 + &10;    |XGRABA nHandle, aAlfa;

     aAlfa = #6#13;   |QBF aAlfa;
     aAlfa = aAlfa + &13 + &10;    |XGRABA nHandle, aAlfa;

     aAlfa = #6#14;   |QBF aAlfa;
     aAlfa = aAlfa + &13 + &10;    |XGRABA nHandle, aAlfa;

     aAlfa = #6#15;   |QBF aAlfa;
     aAlfa = aAlfa + &13 + &10;    |XGRABA nHandle, aAlfa;

     |SI #6#21 ! "          ";
         aAlfa = "Remite: " + #6#21;  |QBF aAlfa;
         aAlfa = aAlfa + " " + #6#22;  |QBF aAlfa;
         aAlfa = aAlfa + &13 + &10;  |XGRABA nHandle, aAlfa;
     |FINSI;

     |XCIERRA nHandle;

     aEjecuta = "cmd " + &34 + "/c START /WAIT " + eaUnidadBasico + "\DOCUMENTOS\IPABOGAD\dsenvio " + aAbre + &34;

     |RSYSTEM aEjecuta;
     |PULSATECLA;

     |RFBORRA aAbre;

     aAbre = aFic + ".err";
     |XABRE "EC", aAbre, nHandle;
     |SI FSalida ] 0;
         |XABRE "ABC", aAbre, nHandle;
         |SI FSalida ] 0;
             |XLEE nHandle, 255, aIncidencia;
             |QBF aIncidencia;
             |XCIERRA nHandle;
             aEnviado    = "N";
             nSwEnvio    = 1;
             nSwMensaje  = 1;
         |FINSI;
     |FINSI;

     aAbre = aFic + ".bue";
     |XABRE "EC", aAbre, nHandle;
     |SI FSalida ] 0;
         |XABRE "ABC", aAbre, nHandle;
         |SI FSalida ] 0;
             |XLEE nHandle, 255, aIncidencia;
             |QBF aIncidencia;
             |XCIERRA nHandle;

             aEnviado = "S";
         |FINSI;
     |FINSI;

     |HAZ GrabaLogCorreo;

     aAlfa  = aFic + ".err";   |RFBORRA aAlfa;
     aAlfa  = aFic + ".bue";   |RFBORRA aAlfa;
|FPRC;

|PROCESO GrabaConRetorno;
     aAlfa = aAlfa + &13 + &10;
     |XGRABA nHandle, aAlfa;
|FPRC;

|PROCESO GrabaSinRetorno;
     |XGRABA nHandle, aAlfa;
|FPRC;

|PROCESO DondeEstasJava;
     aJava = "/usr/bin/java";
     |XABRE "E", aJava, nHandle;
     |SI FSalida ] 0;  |ACABA;  |FINSI;

     aJava = "/usr/local/bin/java";
     |XABRE "E", aJava, nHandle;
     |SI FSalida ] 0;  |ACABA;  |FINSI;

     aJava = "";
|FPRC;

|PROCESO PorZimbra;
     |DBASE aBase;
     aPathZimbra = aBase + "zimbra";             |MKDIR aPathZimbra;
     aPathZimbra = aBase + "zimbra/" + Usuario;  |MKDIR aPathZimbra;
     aPathZimbra = aBase + "zimbra/" + Usuario + "/";

     |HAZ DondeEstasJava;
     |SI aJava = "";
         nError      = 1;
         aEnviado    = "N";
         nSwEnvio    = 1;
         nSwMensaje  = 1;
         aIncidencia = "No encuentro el JAVA en el servidor";
         |HAZ GrabaLogCorreo;
         |ACABA;
     |FINSI;

     |VENTANA 0501, 0540, -1, 16, "Enviando calendario al ZIMBRA";
     nVentTrab = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVentTrab;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |PTEC 802;  |PAUSA;
     |PULSATECLA;

     aCorreo = #511#2;
     aFic    = aPathZimbra + #511#1;    |QBF aFic;

     aAbre   = aFic + ".xml";
     |XABRE "WB", aAbre, nHandle;

     aAlfa = "<?xml version=" + &34 + "1.0"  + &34 + " encoding=" + &34 + "UTF-8" + &34 + "?>";
     |HAZ GrabaConRetorno;

     aAlfa = "<meeting>";
     |HAZ GrabaConRetorno;

     aAlfa = "<asmtp_server>";              |HAZ GrabaSinRetorno;
     aAlfa = #518#4;     |QBF aAlfa;        |HAZ GrabaSinRetorno;
     aAlfa = "</asmtp_server>";             |HAZ GrabaConRetorno;

     aAlfa = "<usr_mail>";                  |HAZ GrabaSinRetorno;
     aAlfa = #518#6; |QBF aAlfa;            |HAZ GrabaSinRetorno;
     aAlfa = "</usr_mail>";                 |HAZ GrabaConRetorno;

     aAlfa = "<clave_mail>";                |HAZ GrabaSinRetorno;
     aAlfa = #518#7; |QBF aAlfa;            |HAZ GrabaSinRetorno;
     aAlfa = "</clave_mail>";               |HAZ GrabaConRetorno;

     aAlfa = "<organizador>";               |HAZ GrabaSinRetorno;
     aAlfa = #518#6; |QBF aAlfa;            |HAZ GrabaSinRetorno;
     aAlfa = "</organizador>";              |HAZ GrabaConRetorno;

     aAlfa = "<destino>";                   |HAZ GrabaSinRetorno;
     aAlfa = #511#2; |QBF aAlfa;            |HAZ GrabaSinRetorno;
     aAlfa = "</destino>";                  |HAZ GrabaConRetorno;

     aAlfa = "<fecha_inicio>";              |HAZ GrabaSinRetorno;
     aAlfa = (#6#5 % -104) + "-" + (#6#5 % 402) + "-" + (#6#5 % 102);     |HAZ GrabaSinRetorno;
     aAlfa = " " + (("00" + #6#6) % -102) + ":"+ (("00" + #6#7) % -102);  |HAZ GrabaSinRetorno;
     aAlfa = "</fecha_inicio>";                                           |HAZ GrabaConRetorno;

                       nNume1 = #6#6;      nNume2 = #6#7 + 5;
     |SI nNume2 ] 60;  nNume1 = #6#6 + 1;  nNume2 = 0;   |FINSI;
     |SI nNume1 > 23;  nNume1 = #6#6;      nNume2 = 59;  |FINSI;

     aAlfa = "<fecha_final>";              |HAZ GrabaSinRetorno;
     aAlfa = (#6#5 % -104) + "-" + (#6#5 % 402) + "-" + (#6#5 % 102);         |HAZ GrabaSinRetorno;
     aAlfa = " " + (("00" + nNume1) % -102) + ":"+ (("00" + nNume2) % -102);  |HAZ GrabaSinRetorno;
     aAlfa = "</fecha_final>";                                               |HAZ GrabaConRetorno;

     aAlfa = "<asunto>";                   |HAZ GrabaSinRetorno;

                       aAlfa = "";
     |SI #6#0 ! 0;     aAlfa = aAlfa + #6#0 + "  ";  |FINSI;
     |SI #6#19 = "S";  aAlfa = aAlfa + "PLAZO  ";                |FINSI;

     aAlfa = aAlfa + #6#10;  |QBF aAlfa;
     aAlfa = aAlfa + " ";
     aAlfa = aAlfa + #6#11;  |QBF aAlfa;   |HAZ GrabaSinRetorno;

     aAlfa = "</asunto>";                  |HAZ GrabaConRetorno;

     aAlfa = "<aviso>";                    |HAZ GrabaSinRetorno;
     aAlfa = (#6#4 > "");                  |HAZ GrabaSinRetorno;
     aAlfa = "</aviso>";                   |HAZ GrabaConRetorno;

     aAlfa = "<referencia>";               |HAZ GrabaSinRetorno;
     aAlfa = aRef;                         |HAZ GrabaSinRetorno;
     aAlfa = "</referencia>";              |HAZ GrabaConRetorno;

     aAlfa = "<descripcion>";               |HAZ GrabaSinRetorno;
     aAlfa = "<![CDATA[";                   |HAZ GrabaSinRetorno;
     aAlfa = #6#10;   |QBF aAlfa;           |HAZ GrabaSinRetorno;
     aAlfa = "<br>" + #6#11;   |QBF aAlfa;  |HAZ GrabaSinRetorno;
     aAlfa = "<br>" + #6#12;   |QBF aAlfa;  |HAZ GrabaSinRetorno;
     aAlfa = "<br>" + #6#13;   |QBF aAlfa;  |HAZ GrabaSinRetorno;
     aAlfa = "<br>" + #6#14;   |QBF aAlfa;  |HAZ GrabaSinRetorno;
     aAlfa = "<br>" + #6#15;   |QBF aAlfa;  |HAZ GrabaSinRetorno;

     |SI #6#21 ! "          ";
         aAlfa = "<br>Remite: " + #6#21;  |QBF aAlfa;  |HAZ GrabaSinRetorno;
         aAlfa = "<br>" + #6#22;  |QBF aAlfa;          |HAZ GrabaSinRetorno;
     |FINSI;

     aAlfa = "]]>";                         |HAZ GrabaSinRetorno;
     aAlfa = "</descripcion>";              |HAZ GrabaConRetorno;

     aAlfa = "</meeting>";                  |HAZ GrabaConRetorno;

     |XCIERRA nHandle;

     aOrigen  = aBase + "plantillas/dsmeeting.jar";
     aDestino = aPathZimbra + "dsmeeting.jar";

     aContiene1 = aDestino;
     |ESPECIFICA "MD5", aContiene;
     aDocRemoto = FSalida;  |QBF aDocRemoto;

     aContiene1 = aOrigen;
     |ESPECIFICA "MD5", aContiene;
     aDocServidor = FSalida;  |QBF aDocServidor;

     |SI aDocRemoto ! aDocServidor;
         |COPIA_FICHERO aOrigen, aDestino;
     |FINSI;

     aEjecuta = aJava + " -jar " + aPathZimbra + "dsmeeting.jar " + aAbre;
     |SYSTEM aEjecuta;

     aAbre = aFic + ".err";
     |XABRE "E", aAbre, nHandle;
     |SI FSalida ] 0;
         |XABRE "", aAbre, nHandle;
         |SI FSalida ] 0;
             |XLEE nHandle, 255, aIncidencia;
             |QBF aIncidencia;
             |XCIERRA nHandle;
             aEnviado    = "N";
             nSwEnvio    = 1;
             nSwMensaje  = 1;
         |FINSI;
     |FINSI;

     aAbre = aFic + ".bue";
     |XABRE "E", aAbre, nHandle;
     |SI FSalida ] 0;
         |XABRE "", aAbre, nHandle;
         |SI FSalida ] 0;
             |XLEE nHandle, 255, aIncidencia;
             |QBF aIncidencia;
             |XCIERRA nHandle;

             aEnviado = "S";
         |FINSI;
     |FINSI;

     |HAZ GrabaLogCorreo;

     aAlfa  = aFic + ".err";   |FBORRA aAlfa;
     aAlfa  = aFic + ".bue";   |FBORRA aAlfa;
     aAlfa  = aFic + ".xml";   |FBORRA aAlfa;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVentTrab;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA nVentTrab;
|FPRC;

|PROCESO abom0511C;
     aReferencia = aRef + #511#1;   |QBF aReferencia;

     |SI aCorreo = #511#2;  |ACABA;  |FINSI;

     aCorreo = #511#2;  |QBF aCorreo;
     |SI aCorreo = "";
         aIncidencia = "Sin cuenta de correo.";
         aEnviado    = "S";
         |ACABA;
     |FINSI;

     |ABRE #abom0500;
     #abom0500#0 = #abom0511#1;
     |LEE 000#abom0500.=;
     |SI FSalida ! 0;  |DDEFECTO #abom0500;  |FINSI;
     |CIERRA #abom0500;

     |SI #abom0500#2 = "O";  |HAZ PorOutlook;  |FINSI;
     |SI #abom0500#2 = "Z";  |HAZ PorZimbra;   |FINSI;

     |PULSATECLA;
|FPRC;

|DEFBUCLE abom0511C;
     #511#3;
     ;
     #6#8, "                                                            ";
     #6#8, "zzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzz";
     ;
     NULL, abom0511C;
|FIN;

|PROCESO abom0006;
     aRef = (#6#1 % -104) + (#6#1 % 402) + (#6#1 % 102);
     aRef = aRef + (("00" + #6#2)  % -102);
     aRef = aRef + (("00" + #6#3)  % -102);
     aRef = aRef + (("00" + #6#18) % -102);
     aRef = aRef + ("00000000" + #6#0) % -108;

     aCorreo  = "";
     aEnviado = "N";
     nSwEnvio = 0;
     |BUCLE abom0511C;

     |SI nDirecto = 0;
         #1006#0  = #6#0;
         #1006#1  = #6#1;
         #1006#2  = #6#2;
         #1006#3  = #6#3;
         #1006#18 = #6#18;
         |LEE 101#1006=;
         |SI FSalida = 0;
             |SI nSwEnvio = 0;
                 #1006#20 = "S";
             |SINO;
                 #1006#20 = "N";
             |FINSI;
             |GRABA 020#1006a;
             |LIBERA #1006;
         |FINSI;
     |SINO;
         |SI nSwEnvio = 0;
             #6#20 = "S";
         |SINO;
             #6#20 = "N";
         |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE abom0006;
     #6#4;
     ;
     "A", 00;
     "N", 99;
     be;
     NULL, abom0006;
|FIN;

|PROCESO OutlookLinea;
     |SI #6#4 = "N";
         #6#20 = "S";
         |ACABA;
     |FINSI;

     nDirecto = 1;

     |ABRE #514;
     |LEE 000#514p;
     |SI FSalida ! 0;
        |CIERRA #514;
        |ACABA;
     |FINSI;
     |CIERRA #514;

     |SI #514#2 ! "S";
         #6#20 = "S";
         |ACABA;
     |FINSI;

     FSalida = 0;
     |SI #514#12 = "S";
         |CONFI "0000NDesea hacer el volcado del mensaje a cada usuario ?";
     |FINSI;

     |SI FSalida ! 0;  |ACABA;  |FINSI;

     nCuentaPara = #abom0514#30;
     |SI nCuentaPara = 0;
          |VENTANA 0501, 2799, -1, 16, "Cuentas de correos - Pulse doble click sobre la cuenta por la que quiere enviar";
          nVentana = FSalida;

          aVent1 = -1;
          aVent2 = -1;
          aVent3 = 0;
          aVent4 = nVentana;
          aVent5 = "MODAL";
          |ESPECIFICA "ESTADO_VENTANA", aVent;

          |SUB_EJECUTA_NP "abom0518;PIDECUENTAX";

          aVent1 = -1;
          aVent2 = -1;
          aVent3 = 0;
          aVent4 = nVentana;
          aVent5 = "MODELESS";
          |ESPECIFICA "ESTADO_VENTANA", aVent;

          |FINVENTANA nVentana;
          |PULSATECLA;
     |SINO;
          enCuenta = nCuentaPara;
     |FINSI;

     |ABRE #518;
     #518#0 = enCuenta;
     |LEE 040#518=;
     |SI FSalida ! 0;
         |MENAV "       No existe la Cuenta de Correo.";
         |ACABA;
     |FINSI;
     |CIERRA #518;

     aRutaCorreo = eaUnidadBasico + "\DOCUMENTOS";                   |RMKDIR aRutaCorreo;
     aRutaCorreo = aRutaCorreo + "\IPABOGAD";         |RMKDIR aRutaCorreo;
     aRutaCorreo = aRutaCorreo + "\";                 |RMKDIR aRutaCorreo;

     |HAZ abom0006;

     |SI #6#20 = "N";
         |MENAV "0000 Han habido errores al enviar los correos, revise las cuentas.";
     |FINSI;

     |PULSATECLA;
|FPRC;

|PROCESO Outlook;
     nDirecto = 0;

     |DBASE aBase;  |QBF aBase;
     aMas = aBase + "def/abom0006.mas";
     |CARGA_DEF aMas, abom0006@;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |ABRE #1006;
     |SELECT #4#1006;

     #1006#20 = "A";
     #1006#8  = 0;
     |LEE 000#1006];
     |SI FSalida ! 0;  |O #1006#20 = "S";
         |DESCARGA_DEF #abom0006@;
         |ACABA;
     |FINSI;
     |CIERRA #1006;

     |ABRE #514;
     |LEE 000#514p;
     |SI FSalida ! 0;
        |MENAV "    Parametros para correo no establecidos";
        |CIERRA #514;
        |DESCARGA_DEF #abom0006@;
        |ACABA;
     |FINSI;
     |CIERRA #514;

     |SI #514#2 ! "S";
         |DESCARGA_DEF #abom0006@;
         |ACABA;
     |FINSI;

     FSalida = 0;
     |SI #514#12 = "S";
         |CONFI "0000NDesea hacer el volcado de los mensajes a cada usuario ?";
     |FINSI;

     |SI FSalida ! 0;
         |DESCARGA_DEF #abom0006@;
         |ACABA;
     |FINSI;

     nCuentaPara = #abom0514#30;
     |SI nCuentaPara = 0;
          |VENTANA 0501, 2799, -1, 16, "Cuentas de correos - Pulse doble click sobre la cuenta por la que quiere enviar";
          nVentana = FSalida;

          aVent1 = -1;
          aVent2 = -1;
          aVent3 = 0;
          aVent4 = nVentana;
          aVent5 = "MODAL";
          |ESPECIFICA "ESTADO_VENTANA", aVent;

          |SUB_EJECUTA_NP "abom0518;PIDECUENTAX";

          aVent1 = -1;
          aVent2 = -1;
          aVent3 = 0;
          aVent4 = nVentana;
          aVent5 = "MODELESS";
          |ESPECIFICA "ESTADO_VENTANA", aVent;

          |FINVENTANA nVentana;
     |SINO;
          enCuenta = nCuentaPara;
     |FINSI;

     |ABRE #518;
     #518#0 = enCuenta;
     |LEE 040#518=;
     |SI FSalida ! 0;
         |DESCARGA_DEF #abom0006@;
         |MENAV "       No existe la Cuenta de Correo.";
         |ACABA;
     |FINSI;
     |CIERRA #518;

     aRutaCorreo = eaUnidadBasico + "\DOCUMENTOS";    |RMKDIR aRutaCorreo;
     aRutaCorreo = aRutaCorreo + "\IPABOGAD";         |RMKDIR aRutaCorreo;
     aRutaCorreo = aRutaCorreo + "\";                 |RMKDIR aRutaCorreo;

     |ABRE #1006;
     |SELECT #1#1006;

     nSwMensaje = 0;
     |BUCLE abom0006;
     |CIERRA #1006;

     |DESCARGA_DEF #abom0006@;

     |SI nSwMensaje ! 0;
         |MENAV "0000 Han habido errores al enviar los correos, revise los datos.";
     |FINSI;
|FPRC;

|| **************************************************************************

|PROCESO Tipo12Fxw9;  |TIPO 12;
     |SI #909#4 = "S";
         |SI #909#21 = "S";  #909#17 = "0";  |FINSI;
         |SI #909#22 = "S";  #909#17 = "T";  |FINSI;
         |SI #909#23 = "S";  #909#17 = "2";  |FINSI;
         |SI #909#24 = "S";  #909#17 = "1";  |FINSI;
         |SI #909#25 = "S";  #909#17 = "L";  |FINSI;
         |SI #909#26 = "S";  #909#17 = "M";  |FINSI;
         |SI #909#27 = "S";  #909#17 = "X";  |FINSI;
         |SI #909#28 = "S";  #909#17 = "J";  |FINSI;
         |SI #909#29 = "S";  #909#17 = "V";  |FINSI;
         |SI #909#30 = "S";  #909#17 = "S";  |FINSI;
         |SI #909#31 = "S";  #909#17 = "D";  |FINSI;

         #909#20 = "N";
     |SINO;
         #909#17 = "";
         #909#20 = "S";
     |FINSI;
|FPRC;

|PROCESO Tipo0C4Fxw9;  |TIPO 0;
     |SI FSalida = 2;  |ERROR;  |ACABA;  |FINSI;

     |C_M #909#5,  1, "S";   |PINTA #909#5;
     |C_M #909#6,  1, "S";   |PINTA #909#6;
     |C_M #909#7,  1, "S";   |PINTA #909#7;
     |C_M #909#17, 1, "S";   |PINTA #909#17;
     |SI #909#4 = "N";
         |C_M #909#5,  1, "N";  #909#5  = "01.01.0000";  |PINTA #909#5;
         |C_M #909#6,  1, "N";  #909#6  = 0;             |PINTA #909#6;
         |C_M #909#7,  1, "N";  #909#7  = 0;             |PINTA #909#7;
         |C_M #909#17, 1, "N";  #909#17 = "";            |PINTA #909#17;
                                #909#16 = "S";           |PINTA #909#16;

         |PARA nCampo = 21;  |SI nCampo [ 31;  |HACIENDO nCampo = nCampo + 1;
               |C_M #909nCampo, 1, "N";   #909nCampo = "N";  |PINTA #909nCampo;
         |SIGUE;
     |SINO;
                              #909#16 = "N";           |PINTA #909#16;

         |PARA nCampo = 21;  |SI nCampo [ 31;  |HACIENDO nCampo = nCampo + 1;
               |C_M #909nCampo, 1, "S";   |PINTA #909nCampo;
         |SIGUE;
     |FINSI;
|FPRC;

|PROCESO Tipo0C8Fxw9;  |TIPO 0;
     |ABRE #505;
     #505#0 = #909#8;
     |LEE 000#505=;
     |SI FSalida ! 0;  |DDEFECTO #505;  |FINSI;
     |CIERRA #505;

     #909#9 = #505#1;  |PINTA #909#9;
|FPRC;

|PROCESO Tipo0Fxw9;  |TIPO 0;
     |SI #909Campo = "S";
         |PARA nCampo = 21;  |SI nCampo [ 31;  |HACIENDO nCampo = nCampo + 1;
               |SI nCampo ! Campo;
                   #909nCampo = "N";  |PINTA #909nCampo;
               |FINSI;
         |SIGUE;
     |FINSI;
|FPRC;

|PROCESO BuscaExpe;
     |VENTANA 1101, 2699, -1, 16, "Buscar Expedientes (Doble click en la linea para seleccionar)";
     nVent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |SUB_EJECUTA_NP "abxz0002";

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA nVent;


     #909#0 = enCodExpe;  |PINTA #909#0;
     #909#8 = enCodPers;  |PINTA #909#8;
|FPRC;

|PROCESO Boton1;
     nModo = 1;
     |DDEFECTO #909;

     |C_M #909#0, 1, "S";

     |HORA aHora;

     #909#0 = 0;
                           #909#1  = ~;
     aAlfa = aHora % 102;  #909#2  = aAlfa;
     aAlfa = aHora % 402;  #909#3  = aAlfa;
     aAlfa = aHora % 702;  #909#18 = aAlfa;
                           #909#5  = #909#1;

     #909#21 = "S";

     |VENTANA 0501, 2799, -1, 16, "Nueva anotaci¢n";
     nSubVent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nSubVent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |PINPA #0#909;
     nSubPanta = FSalida;

     |CONTROL_SIMPLE nSubPanta, "BOTON,{{204}}", 0835, 0838, BuscaExpe;
     nSubBoton1 = FSalida;

     |SI enExped ! 0;
         |C_M #909#0, 1, "N";
         #909#0 = enExped;
     |FINSI;

     |PINDA #0#909;

     |ENDATOS #1#909;
     |SI FSalida = 0;
         |DDEFECTO #6;
         |PARA nCampo = 0;  |SI nCampo [ 20;  |HACIENDO nCampo = nCampo + 1;
               #6nCampo = #909nCampo;
         |SIGUE;

         |SI #6#21 = "          ";
             #6#21 = Usuario % 810;

             |ABRE #511;
             |SELECT #2#511;
             #511#0 = 0;
             #511#1 = #6#21;
             |LEE 040#511];
             |SI FSalida = 0;  |Y #511#1 = #6#21;
                 #6#22 = #511#2;
             |FINSI;
             |CIERRA #511;
         |FINSI;

         |HAZ OutlookLinea;
         |GRABA 020#6n;
         |LIBERA #6;
     |FINSI;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nSubVent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA nSubVent;

     |PULSATECLA;
     |PULSATECLA;

     |LEE 000#6=;
     |REFRESCACONTROL enGrid1;
|FPRC;

|PROCESO Boton2;
     |LEE 000#6=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |DDEFECTO #909;

     |PARA nCampo = 0;  |SI nCampo [ 20;  |HACIENDO nCampo = nCampo + 1;
           #909nCampo = #6nCampo;
     |SIGUE;

     |SI #909#17 = "0";  #909#21 = "S";  |FINSI;
     |SI #909#17 = "T";  #909#22 = "S";  |FINSI;
     |SI #909#17 = "2";  #909#23 = "S";  |FINSI;
     |SI #909#17 = "1";  #909#24 = "S";  |FINSI;
     |SI #909#17 = "L";  #909#25 = "S";  |FINSI;
     |SI #909#17 = "M";  #909#26 = "S";  |FINSI;
     |SI #909#17 = "X";  #909#27 = "S";  |FINSI;
     |SI #909#17 = "J";  #909#28 = "S";  |FINSI;
     |SI #909#17 = "V";  #909#29 = "S";  |FINSI;
     |SI #909#17 = "S";  #909#30 = "S";  |FINSI;
     |SI #909#17 = "D";  #909#31 = "S";  |FINSI;

     |VENTANA 0501, 2799, -1, 16, "Consulta anotaci¢n";
     nSubVent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nSubVent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |PINPA #0#909;
     nSubPanta = FSalida;

     |PINDA #0#909;

     |LEETECLA aAlfa;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nSubVent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA nSubVent;


     |REFRESCACONTROL enGrid1;
|FPRC;

|PROCESO Boton3;
     |LEE 101#6=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |DDEFECTO #909;

     |C_M #909#0, 1, "N";

     |PARA nCampo = 0;  |SI nCampo [ 20;  |HACIENDO nCampo = nCampo + 1;
           #909nCampo = #6nCampo;
     |SIGUE;

     #909#20 = "N";

     |SI #909#17 = "0";  #909#21 = "S";  |FINSI;
     |SI #909#17 = "T";  #909#22 = "S";  |FINSI;
     |SI #909#17 = "2";  #909#23 = "S";  |FINSI;
     |SI #909#17 = "1";  #909#24 = "S";  |FINSI;
     |SI #909#17 = "L";  #909#25 = "S";  |FINSI;
     |SI #909#17 = "M";  #909#26 = "S";  |FINSI;
     |SI #909#17 = "X";  #909#27 = "S";  |FINSI;
     |SI #909#17 = "J";  #909#28 = "S";  |FINSI;
     |SI #909#17 = "V";  #909#29 = "S";  |FINSI;
     |SI #909#17 = "S";  #909#30 = "S";  |FINSI;
     |SI #909#17 = "D";  #909#31 = "S";  |FINSI;

     |VENTANA 0501, 2799, -1, 16, "Modificar anotaci¢n";
     nSubVent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nSubVent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |PINPA #0#909;
     nSubPanta = FSalida;

     |PINDA #0#909;
     |ENDATOS #1#909;
     |SI FSalida = 0;
         |PARA nCampo = 0;  |SI nCampo [ 20;  |HACIENDO nCampo = nCampo + 1;
               #6nCampo = #909nCampo;
         |SIGUE;

         |SI #6#21 = "          ";
             #6#21 = Usuario % 810;

             |ABRE #511;
             |SELECT #2#511;
             #511#0 = 0;
             #511#1 = #6#21;
             |LEE 040#511];
             |SI FSalida = 0;  |Y #511#1 = #6#21;
                 #6#22 = #511#2;
             |FINSI;
             |CIERRA #511;
         |FINSI;

         |HAZ OutlookLinea;
         |GRABA 020#6a;
     |FINSI;
     |LIBERA #6;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nSubVent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA nSubVent;


     |PULSATECLA;

     |REFRESCACONTROL enGrid1;
|FPRC;

|PROCESO Boton4;
     |LEE 101#6=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |CONFI "0000NSeguro que quiere borrar la anotaci¢n";
     |SI FSalida ! 0;
         |LIBERA #6;
         |ACABA;
     |FINSI;

     |BORRA 020#6a;
     |LIBERA #6;

     |REFRESCACONTROL enGrid1;
     |FOCOTECLADO enGrid1;
|FPRC;

|PROCESO EventoPanta;
     |SI FSalida = 1;  |O FSalida = 2;
         aAlfa = #6#1;  |QBT aAlfa;
         |SI aAlfa = "..";
             #6#1 = "01.01.0000";
         |FINSI;
         |LEE 000#6=;
         |SI FSalida = 0;
             |ESTADO_CONTROL nBoton2, "ENABLE";
             |ESTADO_CONTROL nBoton3, "ENABLE";
             |ESTADO_CONTROL nBoton4, "ENABLE";
         |SINO;
             |ESTADO_CONTROL nBoton2, "DISABLE";
             |ESTADO_CONTROL nBoton3, "DISABLE";
             |ESTADO_CONTROL nBoton4, "DISABLE";
         |FINSI;
     |FINSI;

     |SI FSalida = 4;
         |HAZ Boton2;
     |FINSI;
|FPRC;

|PROCESO Tipo80Fxz56; |TIPO 80;
     |ABRE #514;
     |LEE 000#514p;
     |SI FSalida ! 0;  |DDEFECTO #514;  |FINSI;
     |CIERRA #514;

     |ABRET #6;

     |SI enExped = 0;
         FSalida = 2799;
     |SINO;
         FSalida = 3399;
     |FINSI;
|FPRC;

|PROCESO Tipo90Fxz56; |TIPO 90;
     |CIERRAT #6;
|FPRC;

|PROCESO Baja6_5;
     #6#1  = "31.12.9999";
     #6#2  = 23;
     #6#3  = 59;
     #6#18 = 59;
|FPRC;

|PROCESO Alta6_5;
     #6#1  = "01.01.0000";
     #6#2  = 00;
     #6#3  = 00;
     #6#18 = 00;
|FPRC;

|PROCESO Baja6_6;
     #6#0  = nDExpediente;
     #6#1  = "31.12.9999";
     #6#2  = 23;
     #6#3  = 59;
     #6#18 = 59;
|FPRC;

|PROCESO Alta6_6;
     #6#0  = nHExpediente;
     #6#1  = "01.01.0000";
     #6#2  = 00;
     #6#3  = 00;
     #6#18 = 00;
|FPRC;

|PROGRAMA;
     |HAZ LeeUnidadBasico;

     |SI PARAMETRO ! "";
         |HAZ Outlook;
         |ACABA;
     |FINSI;

     nDExpediente = 0;
     nHExpediente = 99999999;
     enExpediente = 0;
     |SI enExped ! 0;
        enExpediente = enExped;
        nDExpediente = enExped;
        nHExpediente = enExped;
     |FINSI;

     |PINPA #0#956;  enPanta0 = FSalida;

     |SI enExped = 0;
         |CONTROL_SIMPLE 0, "BOTON,{{193}} Nueva anotaci¢n", 2602, 2717, Boton1;
         nBoton1  = FSalida;

         |CONTROL_SIMPLE 0, "BOTON,{{197}} Consulta anotaci¢n", 2619, 2732, Boton2;
         nBoton2  = FSalida;

         |CONTROL_SIMPLE 0, "BOTON,{{197}} Modificar anotaci¢n", 2634, 2747, Boton3;
         nBoton3  = FSalida;

         |CONTROL_SIMPLE 0, "BOTON,{{199}} Borrar anotaci¢n", 2649, 2764, Boton4;
         nBoton4  = FSalida;
     |SINO;
         |CONTROL_SIMPLE 0, "BOTON,{{193}} Nueva anotaci¢n", 3202, 3317, Boton1;
         nBoton1  = FSalida;

         |CONTROL_SIMPLE 0, "BOTON,{{197}} Consulta anotaci¢n", 3219, 3332, Boton2;
         nBoton2  = FSalida;

         |CONTROL_SIMPLE 0, "BOTON,{{197}} Modificar anotaci¢n", 3234, 3347, Boton3;
         nBoton3  = FSalida;

         |CONTROL_SIMPLE 0, "BOTON,{{199}} Borrar anotaci¢n", 3249, 3364, Boton4;
         nBoton4  = FSalida;
     |FINSI;

     |ESTADO_CONTROL nBoton2, "DISABLE";
     |ESTADO_CONTROL nBoton3, "DISABLE";
     |ESTADO_CONTROL nBoton4, "DISABLE";

     nRango = 4 + 8 + 16 + 32 + 128;

     |SI enExped = 0;
         |LINEAL_SIMPLE #5#6, nRango, 0602, 2698, Baja6_5, Alta6_5, EventoPanta;
     |SINO;
         |LINEAL_SIMPLE #6#6, nRango, 0602, 2698, Baja6_6, Alta6_6, EventoPanta;
     |FINSI;

     enGrid1  = FSalida;

     aEsc1  = &255 + "806";
     aEsc2  = &255 + "807";
     aRetu  = &255 + "802";
     |PARA; |SI;  |HACIENDO;
         FSalida = "::{{001}}Salir," + enGrid1;
         |LEETECLA aTecla;
         |SI aTecla = aEsc1; |SAL;  |FINSI;
         |SI aTecla = aEsc2; |SAL;  |FINSI;
         |SI aTecla = aRetu; |SAL;  |FINSI;
     |SIGUE;

     |PULSATECLA;

     |DESTRUYECONTROL enGrid1;

     |FIN_CONTROL_WINDOWS nBoton1;
     |FIN_CONTROL_WINDOWS nBoton2;
     |FIN_CONTROL_WINDOWS nBoton3;
     |FIN_CONTROL_WINDOWS nBoton4;

     |HAZ Outlook;
|FPRO;
