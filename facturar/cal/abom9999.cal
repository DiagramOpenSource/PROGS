|FICHEROS;
     abom0016 #16;            ||Bandeja pdtes.envio

     abom0505 #505;           ||Personal Despacho
     abom0511 #511;           ||Direcciones de Correo
     abom0514 #514;           ||Parametros Generales

     abom0018 #18;            ||Correos Alarmas Dest

     dsam0121 #121;           ||Tipos Documentos   C

     dsorz999@ #999;
|FIN;

|VARIABLES;
     aFicheroDestino = "";
     aBaseDoc = "";

     aBase  = "";
     aOrigen = "";
     aFicTxt = "";

     aIPRemoto = "";
     aEmail = "";
     aDirAnt = "";
     aAux = "";
     nDireccion = 0;
     &eaTipoEnvio = "";
     &enNumExp = 0;
     &efFechaMov = @;
     &enCodDestino = 0;
     &enCodDesDos = 0;

     aMensaje = "";
     nLinea = 0;
     aAlfa = "";
     nHandle = 0;

     aEnviadoEl = "";
     aDestino = "";
     aAlfa1 = "";
     aAlfa2 = "";
     aAlfa3 = "";
     aAlfa4 = "";
     aAlfa5 = "";
     fFecha = @;
     aFecha = "";
     aMas   = "";

     &efFechaDocu     = @;
     &eaRutaDocumento = "";
     &eaDsOporte      = "";
|FIN;

|PROCESO PonFecha;
     fFecha = ~;
     aFecha = fFecha;
     aAlfa1 = fFecha % 102;
     aAlfa2 = fFecha % 402;
     aAlfa3 = fFecha % -104;

     |SI aAlfa2 = "01";  aAlfa2 = "Enero";       |FINSI;
     |SI aAlfa2 = "02";  aAlfa2 = "Febrero";     |FINSI;
     |SI aAlfa2 = "03";  aAlfa2 = "Marzo";       |FINSI;
     |SI aAlfa2 = "04";  aAlfa2 = "Abril";       |FINSI;
     |SI aAlfa2 = "05";  aAlfa2 = "Mayo";        |FINSI;
     |SI aAlfa2 = "06";  aAlfa2 = "Junio";       |FINSI;
     |SI aAlfa2 = "07";  aAlfa2 = "Julio";       |FINSI;
     |SI aAlfa2 = "08";  aAlfa2 = "Agosto";      |FINSI;
     |SI aAlfa2 = "09";  aAlfa2 = "Septiembre";  |FINSI;
     |SI aAlfa2 = "10";  aAlfa2 = "Octubre";     |FINSI;
     |SI aAlfa2 = "11";  aAlfa2 = "Noviembre";   |FINSI;
     |SI aAlfa2 = "12";  aAlfa2 = "Diciembre";   |FINSI;

     fFecha = fFecha % 1;
     aAlfa4 = fFecha % 1109;  |QBF aAlfa4;
     |HORA aAlfa5;

     aEnviadoEl  = aAlfa4 + ", " + aAlfa1 + " de " + aAlfa2 + " de " + aAlfa3 + " a las " + aAlfa5;
|FPRC;

|PROCESO Cuerpo;
     aBase  = "";
     |DBASE aBase;  |QBF aBase;
     aBase  = aBase + "correos/";  |MKDIR aBase;

     efFechaDocu  = #16#7;
     aFicTxt = (("00000000" + #16#0) % -108) + (("0000" + #16#1) % -104) + ".txt";
     aAlfa   = aBase + aFicTxt;

     |XABRE "A", aAlfa, nHandle;

     aAlfa = #16#2;
     |XGRABA nHandle, aAlfa;

     |XCIERRA nHandle;
|FPRC;


|PROCESO abom0511_9999;
     aAux = #abom0511#2;
     |QBF aAux;
     |SI aAux ! "";
          |SI aDestino = "";
               aDestino = aAux;
          |SINO;
               aDestino = aDestino + ";" + aAux;
          |FINSI;
     |FINSI;
     |QBF aDestino;
|FPRC;

|DEFBUCLE abom0511_9999;
 #abom0511#1;
 ;
 nDireccion, "          ";
 nDireccion, "zzzzzzzzzz";
 ;
 NULL, abom0511_9999;
|FIN;

|PROCESO EnviaAlarma;
     |ABRE #514;
     |DDEFECTO #514;
     |LEE 000#514p;
     |CIERRA #514;

     aDestino = "";
     nDireccion = enCodDestino;
     |BUCLE abom0511_9999;

     |SI enCodDesDos ! enCodDestino;
          nDireccion = enCodDesDos;
          |BUCLE abom0511_9999;
     |FINSI;

     |SI aDestino ! "";
          |ABRE #16;
          |DDEFECTO #16;
          #16#0 = enNumExp;
          #16#1 = 9999;
          |LEE 000#16];
          |SI FSalida = 0;
               |LEE 000#16a;
          |SINO;
               |LEE 000#16u;
          |FINSI;
          |SI FSalida = 0; |Y #16#0 = enNumExp;
               nLinea = #16#1 + 1;
          |SINO;
               nLinea = 1;
          |FINSI;
          |DDEFECTO #16;
          #16#0 = enNumExp;
          #16#1 = nLinea;

          |SI eaTipoEnvio = "40DIAS";
               #16#2  = "Expediente " + enNumExp + ". Ultimo movimiento en " + efFechaMov + ". MAS DE 40 DIAS";
          |FINSI;

          |SI eaTipoEnvio = "50DIAS";
               #16#2  = "Expediente " + enNumExp + ". Ultimo movimiento en " + efFechaMov + ". MAS DE 50 DIAS";
          |FINSI;

          |SI eaTipoEnvio = "90DIAS";
               #16#2  = "Expediente " + enNumExp + ". Ultimo movimiento en " + efFechaMov + ". MAS DE 90 DIAS";
          |FINSI;

          |SI eaTipoEnvio = "CIERRE";
               #16#2  = "Expediente " + enNumExp + ". Cerrado con fecha " + efFechaMov;
          |FINSI;

          |HAZ PonFecha;

          #16#7  = ~;
          #16#3  = aEnviadoEl;
          #16#4  = aDestino;
          #16#5  = "";
          #16#6  = #514#9;
          #16#8  = ~;
          #16#9 = "N";
          #16#12 = #514#25;
          #16#13 = "S";
          |GRABA 020#16n;
          |CIERRA #16;

          |HAZ Cuerpo;
     |FINSI;
|FPRC;

|PROCESO abom0511_9999_dos;
     aAux = #abom0511#2;
     |QBF aAux;
     |SI aAux ! "";
          |DDEFECTO #abom0018;
          #abom0018#0 = aAux;
          #abom0018#1 = enNumExp;
          |LEE 101#abom0018.=;
          |SI FSalida ! 0; |GRABA 020#abom0018.b; |FINSI;

          |SI eaTipoEnvio = "40DIAS";
               #abom0018#2 = "Expediente " + enNumExp + ". Ultimo movimiento en " + efFechaMov + ". MAS DE 40 DIAS";
          |FINSI;

          |SI eaTipoEnvio = "50DIAS";
               #abom0018#2 = "Expediente " + enNumExp + ". Ultimo movimiento en " + efFechaMov + ". MAS DE 50 DIAS";
          |FINSI;

          |SI eaTipoEnvio = "90DIAS";
               #abom0018#2 = "Expediente " + enNumExp + ". Ultimo movimiento en " + efFechaMov + ". MAS DE 90 DIAS";
          |FINSI;
          |GRABA 020#abom0018.a;
          |LIBERA #abom0018;
     |FINSI;
|FPRC;

|DEFBUCLE abom0511_9999_dos;
 #abom0511#1;
 ;
 nDireccion, "          ";
 nDireccion, "zzzzzzzzzz";
 ;
 NULL, abom0511_9999_dos;
|FIN;

|PROCESO CtrlAlarmas;
     |ABRE #abom0018;

     |ABRE #514;
     |DDEFECTO #514;
     |LEE 000#514p;
     |CIERRA #514;

     aDestino = "";
     nDireccion = enCodDestino;
     |BUCLE abom0511_9999_dos;

     |SI enCodDesDos ! enCodDestino;
          nDireccion = enCodDesDos;
          |BUCLE abom0511_9999_dos;
     |FINSI;
     |CIERRA #abom0018;
|FPRC;

|PROCESO MeteCorreo;
     |HAZ PonFecha;

     aDestino = aDirAnt;
     #16#3  = aEnviadoEl;
     #16#4  = aDestino;
     #16#5  = "";
     #16#6  = #514#9;
     #16#8  = ~;
     #16#9 = "N";
     #16#12 = #514#25;
     #16#13 = "S";
     |GRABA 020#16n;

     |XCIERRA nHandle;

     |ABRE #dsam0121;
     |DDEFECTO #dsam0121;
     #dsam0121#0 = #514#9;
     |LEE 000#dsam0121.=;
     |CIERRA #dsam0121;

     |DBASE aBaseDoc;  |QBF aBaseDoc;
     eaRutaDocumento = aBaseDoc + "ipabogad";
     |MKDIR eaRutaDocumento;
     eaRutaDocumento = eaRutaDocumento + "/documentos";
     |MKDIR eaRutaDocumento;
     eaRutaDocumento = eaRutaDocumento + "/" + (("00" + #48#0) % -102);
     |MKDIR eaRutaDocumento;
     eaRutaDocumento = eaRutaDocumento + "/" + (("0000" + #121#0) % -104);
     |MKDIR eaRutaDocumento;

     efFechaDocu  = #16#7;

     |SI #121#4 = "S";
         |SI efFechaDocu ! "00.00.0000";
             aAlfa = (efFechaDocu % -104) + (efFechaDocu % 402);
             eaRutaDocumento = eaRutaDocumento + "/" + aAlfa;
             |MKDIR eaRutaDocumento;
         |FINSI;
     |FINSI;

     aOrigen = aFicheroDestino;
     aDestino = eaRutaDocumento + "/" + aFicTxt;

     |COPIA_FICHERO aOrigen, aDestino;
|FPRC;

|PROCESO DatosCorreo;
     fFecha = ~;
     aFecha = fFecha;
     |DDEFECTO #16;
     #16#0 = enNumExp;
     #16#1 = 9999;
     |LEE 000#16];
     |SI FSalida = 0;
          |LEE 000#16a;
     |SINO;
          |LEE 000#16u;
     |FINSI;
     |SI FSalida = 0; |Y #16#0 = enNumExp;
          nLinea = #16#1 + 1;
     |SINO;
          nLinea = 1;
     |FINSI;
     |DDEFECTO #16;
     #16#0 = enNumExp;
     #16#1 = nLinea;
     #16#2 = "Situacion expedientes a dia " + aFecha;
     #16#7 = ~;

     aBase  = "";
     |DBASE aBase;  |QBF aBase;
     aBase  = aBase + "correos/";  |MKDIR aBase;

     efFechaDocu  = #16#7;
     aFicTxt = (("00000000" + #16#0) % -108) + (("0000" + #16#1) % -104) + ".txt";
     aAlfa   = aBase + aFicTxt;

     aFicheroDestino = aAlfa;

     |XABRE "W", aAlfa, nHandle;
|FPRC;


|PROCESO MeteLinea;
     aAlfa = #abom0018#2;
     |XGRABA nHandle, aAlfa;
|FPRC;

|PROCESO abom0018;
     aEmail = #abom0018#0;
     |QBF aEmail;
     |SI aEmail ! aDirAnt;
          |SI aDirAnt ! "-1";
               |HAZ MeteCorreo;
          |FINSI;
          |HAZ DatosCorreo;
          aDirAnt = aEmail;
     |FINSI;

     |HAZ MeteLinea;

     ||TODO CCC
     |BORRA 020#abom0018.a;
|FPRC;

|DEFBUCLE abom0018;
 #abom0018#1;
 ;
 INICIO;
 FINAL;
 be;
 NULL, abom0018;
|FIN;

|PROCESO EnviaSituacion;
     fFecha = ~;
     aFecha = fFecha;

     |ABRE #16;
     aDirAnt = "-1";
     |BUCLE abom0018;
     |SI aDirAnt ! "-1";
          |HAZ MeteCorreo;
     |FINSI;
     |CIERRA #16;
|FPRC;

|PROCESO CompruebaDSOPORTE;
     eaDsOporte = "A";

     |DBASS aBase;
     aMas = aBase + "dsoporte/def/dsorz999.mas";
     |CARGA_DEF aMas, dsorz999@;
     |SI FSalida = 0;
         aMas = aBase + "dsoporte/fich/";
         |PATH_DAT #999 aMas;

         |ABRE #999;
         |LEE 000#999p;
         |SI FSalida = 0;
             |SI #999#1 = "A";  eaDsOporte = "A";  |FINSI;
             |SI #999#1 = "G";  eaDsOporte = "G";  |FINSI;
         |FINSI;
         |CIERRA #999;

         |DESCARGA_DEF #dsorz999@;
     |FINSI;

     |SI eaDsOporte = "A";
         |DBASS aBase;
         aMas = aBase + "gecochek/def/gecom001.mas";
         |XABRE "E", aMas, nHandle;
         |SI FSalida < 0;  eaDsOporte = "N";  |FINSI;
     |FINSI;

|FPRC;
