          **** PASE DEL HISTORICO DE RECIBOS AL SECUENCIAL ADAPTADO A LA ***
                        ***     NUEVA NORMA    ****

|FICHEROS;
  aspascar #0;
  ashisrec #1;
  asclient;
  indrecib #3;
  ashiscon #4;
  ascaisec #5;
  asprovin #6;

  asempcon #9;
  asenlcon #10;
  ascontro #11;
  ascontrt #450;
  asconcep #12;

  ascabrem #13;
  aslinrem;

  ashismic #90;
  asw00001 #99;

  asparcia #100;

  n19sepax #901;
  :/integral/def/dsam0000 #710;
|FIN;

|VARIABLES;
  cabe1   = "0180";
  orde1   = "0380";
  indi1   = "0680";
  conc1_1 = "0681";
  conc1_2 = "0682";
  conc1_3 = "0683";
  conc1_4 = "0684";
  conc1_5 = "0685";
  tgen1   = "0980";
  tord1   = "0880";

|| Cabecera del presentador;
  cabe2 = "";
  cabe3 = "      ";
  cabe5 = "";
  cabe6 = "          ";
  libre20 = "                    ";
  libre12 = "            ";
  libre40 = "                                        ";

|| Cabecera de Ordenante;
  orde2 = "";
  orde3 = "";
  orde5 = "";
  libre8= "        ";
  orde6 = "01";
  libre10 = "          ";
  fechacargo = @;

|| Registro individual Obligatorio
  indi2 = "";
  indi3 = "";
  indi4 = "";
  indi5 = "";
  indi6 = "";
  indi7 = "";
  indi8 = "";
  indi9 = "        ";
  indi10 = "          ";
  dc="";

  conc1 = "";
  conc2 = "";
  conc3 = "                                        ";
  conc4 = "                                        ";
  conc5 = "                                        ";

|| Registro Total General Y ordenante
  tgen2 = "                                                    ";
  tgen3 = "";
  tgen4 = "";
  tgen5 = "      ";
  tgen6 = "";
  tgen7 = "                                      ";
  tgen8 = "0001";
  tgen9 = "                ";

  tord9 = "                    ";

  handle = "";
  varalfa1 = "";
  varalfa2 = "";
  aAlfa    = "";
  aAlfa1   = "";
  nCanal   = 0;
  facalfa = "";
  contenido = "";
  longalf = "";
  fiche = "";
  espacio = " ";
  dd = "";
  mm = "";
  aa = "";
  fechalf = "";
  impalf = "";
  vacio = "          ";
  trecibo = 0;
  timpsuc = 0;
  tnumsuc = 0;
  serie = "";
  factu = 0;
  cont = 0;
  contreg = 0;
  empresa = "";
  linea_c = "Procesando Factura: Serie:    Factura:";
  linea   = 0;
  final = "                    ";
  final14 = "              ";
  l = 0;
  valo = 0;
  elefe = "";
  ceerre = "";
  traba = "";

  sw_existe = 0;
  c_clie    = "";
  fijo      = "";
  varalf    = "";
  posici    = "";

  ca1       = "";
  ca2       = "";
  ca3       = "";
  ca4       = "";
  ca5       = "";
  ca6       = "";
  ca7       = "";
  ca8       = "";
  ca9       = "";
  ca10      = "";
  ca11      = "";
  ca12      = "";
  ca        = "";
  conver    = "";
  conver1   = "";
  conver2   = "";
  sino      = "";
     fFech = @;
     qFech = "";
     dFech = "";
     hFech = "";

     &eaMoneda  = "";
     &enCanti   = 0;
     &enSwTrata = 0;
     &enCodBanco = 0;
     aFichero   = "";

     aCadena  = "";
     aChar1   = "";
     aChar2   = "";
     aParte1  = "";
     aParte2  = "";
     aPais    = "";
     aNif     = "";
     aDC      = "";
     aIdent   = "";
     aSufijo  = "";
     aRefer   = "";
     aFecRef  = "";
     aCodBIC  = "";
     aCodIBAN = "";
     aTipoPag = ""

     nCalc    = 0;
     nCalc1   = 0;
     nCalc2   = 0;
     nNume1   = 0;
     nNume2   = 0;
     nPara1   = 0;
     aFecha   = "";
     aHora    = "";
     nSwC39   = 0;
     aAlfa2   = "";
     aTexto   = "";
     aLon     = "";
     aCar     = "";

     nSw1     = 0;
     nSwOp    = 0;
     nCliente = 0;
     nError   = 0;
     swError  = 0;
     nLon     = 0;
     nCar     = 0;
     nPos     = 0;
     nOk      = 0;

     &nTotal  = 0;
     &nConta  = 0;
     &eaError = "";
     &SwXml       = 0;
     &efFechaO   = @;
     &efFechaL   = @;
     &enImporte  = 0;
     &enDesRemesa = 0;
|FIN;

|INCLUYE agicon_i;

|PROCESO MiraVarMoneda;  |TIPO 7;
     |HAZ PintaMoneda;
     #0#22 = eaMoneda;

     |PINTA #0#22;
|FPRC;

|PROCESO que_selec;
  |SI #0#15 = "N";
      |C_M #0#0,  1, "S"; |PINTA #0#0;
      |C_M #0#1,  1, "S"; |PINTA #0#1;
      |C_M #0#2,  1, "S"; |PINTA #0#2;
      |C_M #0#3,  1, "S"; |PINTA #0#3;
      |C_M #0#4,  1, "S"; |PINTA #0#4;
      |C_M #0#5,  1, "S"; |PINTA #0#5;
      |C_M #0#6,  1, "S"; |PINTA #0#6;
      |C_M #0#7,  1, "S"; |PINTA #0#7;
      |C_M #0#16, 1, "N"; |PINTA #0#16;
  |SINO;
      |C_M #0#0,  1, "N"; |PINTA #0#0;
      |C_M #0#1,  1, "N"; |PINTA #0#1;
      |C_M #0#2,  1, "N"; |PINTA #0#2;
      |C_M #0#3,  1, "N"; |PINTA #0#3;
      |C_M #0#4,  1, "N"; |PINTA #0#4;
      |C_M #0#5,  1, "N"; |PINTA #0#5;
      |C_M #0#6,  1, "N"; |PINTA #0#6;
      |C_M #0#7,  1, "N"; |PINTA #0#7;
      |C_M #0#16, 1, "S"; |PINTA #0#16;
  |FINSI;
|FPRC;

|CALCULO ForBS; |TIPO 0;
   aAlfa = "S";
   |SI #0#23 = "B"; aAlfa = "N"; |FINSI;
   |C_M #0#24,1,aAlfa;
|FCAL;

|CALCULO XmlTxt; |TIPO 0;
   aAlfa1 = "S";
   |SI #0#25 = "T";
       aAlfa1 = "N";
       #0#26  = "N"; |PINTA #0#26;
       #0#27  = "N"; |PINTA #0#27;
   |FINSI;
   |C_M #0#26, 1, aAlfa1;
   |C_M #0#27, 1, aAlfa1;
   |HAZ ElSufijo;
|FCAL;

|CALCULO Finan; |TIPO 0;
   aAlfa1 = "S";
   |SI #0#26 = "S"; aAlfa1 = "N"; |FINSI;
   |C_M #0#11, 1, aAlfa1;
   |HAZ ElSufijo;
|FCAL;

|CALCULO ElSufijo;
   |ABRE #5;
   |DDEFECTO #5;
   #5#0 = #0#9;
   |LEE 040#5=;
   |CIERRA #5;
   #0#21 = #5#30;
   |SI #0#26 = "S"; #0#21 = #5#40; |FINSI;
   |PINTA #0#21;
|FCAL;

|PROCESO hay_remesa; |TIPO 0;
   |SI #0#16 = 0; |ACABA; |FINSI;

   |ABRE #13;
   #13#0 = #0#16;
   |LEE 040#13=;
   |SI FSalida ! 0;
       |MENAV "      No existe Remesa";
       |ERROR;
   |SINO;
       |SI #13#5 = "X";
           |CONFI "0000NEsta Remesa ya ha sido pasada por el Banco. Volver a realizar el Proceso";
           |SI FSalida ! 0;
               |ERROR;
           |FINSI;
       |FINSI;

       efFechaO = #13#6;
       #0#9 = #13#1;
       |PINTA #0#9;
       |HAZ ElSufijo;
   |FINSI;
   |CIERRA #13;
|FPRC;

|PROCESO sacaano; |TIPO 7;
     fFech = ~;
     qFech = fFech % -104;
     dFech = "01.01." + qFech;
     hFech = "31.12." + qFech;
     #0#4  = dFech;
     #0#5  = hFech;
     |PINTA #0#4;
     |PINTA #0#5;
|FPRC;

|| ////////////////////////////////////////////////////////////////////
|PROCESO ReemplazaStr;
   aAlfa = aCadena - aChar1;
   |PARA; |SI FSalida ! 0; |HACIENDO;
      nCalc = FSalida + 98; aParte2 = aAlfa % nCalc;
      aParte1 = aAlfa - aParte2;
      aCadena = aParte1 + aChar2 + aParte2;

      aAlfa = aCadena - aChar1;
   |SIGUE;
|FPRC;

|PROCESO ObtenId;
   aPais = "ES";

   FSalida = 1; |PARA; |SI FSalida ! 0; |HACIENDO; aNif = aNif - "/"; |SIGUE;
   FSalida = 1; |PARA; |SI FSalida ! 0; |HACIENDO; aNif = aNif - "-"; |SIGUE;
   FSalida = 1; |PARA; |SI FSalida ! 0; |HACIENDO; aNif = aNif - "?"; |SIGUE;
   FSalida = 1; |PARA; |SI FSalida ! 0; |HACIENDO; aNif = aNif - ":"; |SIGUE;
   FSalida = 1; |PARA; |SI FSalida ! 0; |HACIENDO; aNif = aNif - "("; |SIGUE;
   FSalida = 1; |PARA; |SI FSalida ! 0; |HACIENDO; aNif = aNif - ")"; |SIGUE;
   FSalida = 1; |PARA; |SI FSalida ! 0; |HACIENDO; aNif = aNif - "."; |SIGUE;
   FSalida = 1; |PARA; |SI FSalida ! 0; |HACIENDO; aNif = aNif - ","; |SIGUE;
   FSalida = 1; |PARA; |SI FSalida ! 0; |HACIENDO; aNif = aNif - "'"; |SIGUE;
   FSalida = 1; |PARA; |SI FSalida ! 0; |HACIENDO; aNif = aNif - "+"; |SIGUE;
   FSalida = 1; |PARA; |SI FSalida ! 0; |HACIENDO; aNif = aNif - &34; |SIGUE;

   aCadena = aNif;
   aChar1 = "A"; aChar2 = "10"; |HAZ ReemplazaStr;
   aChar1 = "B"; aChar2 = "11"; |HAZ ReemplazaStr;
   aChar1 = "C"; aChar2 = "12"; |HAZ ReemplazaStr;
   aChar1 = "D"; aChar2 = "13"; |HAZ ReemplazaStr;
   aChar1 = "E"; aChar2 = "14"; |HAZ ReemplazaStr;
   aChar1 = "F"; aChar2 = "15"; |HAZ ReemplazaStr;
   aChar1 = "G"; aChar2 = "16"; |HAZ ReemplazaStr;
   aChar1 = "H"; aChar2 = "17"; |HAZ ReemplazaStr;
   aChar1 = "I"; aChar2 = "18"; |HAZ ReemplazaStr;
   aChar1 = "J"; aChar2 = "19"; |HAZ ReemplazaStr;
   aChar1 = "K"; aChar2 = "20"; |HAZ ReemplazaStr;
   aChar1 = "L"; aChar2 = "21"; |HAZ ReemplazaStr;
   aChar1 = "M"; aChar2 = "22"; |HAZ ReemplazaStr;
   aChar1 = "N"; aChar2 = "23"; |HAZ ReemplazaStr;
   aChar1 = "O"; aChar2 = "24"; |HAZ ReemplazaStr;
   aChar1 = "P"; aChar2 = "25"; |HAZ ReemplazaStr;
   aChar1 = "Q"; aChar2 = "26"; |HAZ ReemplazaStr;
   aChar1 = "R"; aChar2 = "27"; |HAZ ReemplazaStr;
   aChar1 = "S"; aChar2 = "28"; |HAZ ReemplazaStr;
   aChar1 = "T"; aChar2 = "29"; |HAZ ReemplazaStr;
   aChar1 = "U"; aChar2 = "30"; |HAZ ReemplazaStr;
   aChar1 = "V"; aChar2 = "31"; |HAZ ReemplazaStr;
   aChar1 = "W"; aChar2 = "32"; |HAZ ReemplazaStr;
   aChar1 = "X"; aChar2 = "33"; |HAZ ReemplazaStr;
   aChar1 = "Y"; aChar2 = "34"; |HAZ ReemplazaStr;
   aChar1 = "Z"; aChar2 = "35"; |HAZ ReemplazaStr;

|| ... Primera parte sin poner numero fijos
   nCalc  = aCadena;
   nCalc1 = nCalc / 97;
   aAlfa  = nCalc1; |QBF aAlfa;
   aAlfa  = aAlfa - ".";
   nCalc1 = ((FSalida / 100) ! 0) + 99;
   aAlfa  = aAlfa % nCalc1;
   nCalc1 = aAlfa;

   nCalc1 = (nCalc1 * 97);
   nCalc  = (nCalc - nCalc1) ! 0;
   aAlfa  = nCalc;

|| ... Segunda Parte con los numeros fijos
   aAlfa  = aAlfa + "142800";
   nCalc  = aAlfa;
   nCalc1 = nCalc / 97;
   aAlfa  = nCalc1; |QBF aAlfa;
   aAlfa  = aAlfa - ".";
   nCalc1 = ((FSalida / 100) ! 0) + 99;
   aAlfa  = aAlfa % nCalc1;
   nCalc1 = aAlfa;

   nCalc1 = (nCalc1 * 97);
   nCalc  = nCalc - nCalc1;
   nCalc1 = 98 - nCalc;

   aDC = ("00" + nCalc1) % -102;
   aIdent = aPais + aDC + aSufijo + aNif;

|FPRC;

|PROCESO ChequeoDatos;
  swError = 0;
  aRefer   = "";
  aFecRef  = "";
  aCodIBAN = "";

  swError = 0;
  |DDEFECTO #asclient;
  #asclient#0 = nCliente;
  |LEE 040#asclient.=;
  |SI FSalida ! 0;
      swError = 1;
      eaError =  " Cliente Inexistente. ";
  |FINSI;

  |SI swError = 0;
      aAlfa = #asclient#13;  |QBF aAlfa;
      |SI aAlfa ! "";
          aLon = aAlfa % 0;
          nLon = aLon;
          |PARA nCar = 1;  |SI nCar [ nLon;  |HACIENDO nCar = nCar + 1;
                nPos = (nCar * 100) + 1;
                aCar = aAlfa % nPos;
                nOk  = 0;
                |SI aCar ] "0";  |Y aCar [ "9";
                    nOk = 1;
                |FINSI;

                |SI nOk = 0;
                    swError = 1;
                    eaError =  " Datos del banco incoherente. ";
                    |SAL;
                |FINSI;
          |SIGUE;
      |FINSI;
  |FINSI;

  |SI swError = 0;
     |SI #0#15 = "S";
         aCodIBAN = #aslinrem#10; |QBF aCodIBAN;
         |SI aCodIBAN = "";
             #aslinrem#10 = #asclient#77;
             #aslinrem#11 = #asclient#84;
             #aslinrem#12 = #asclient#76;
             #aslinrem#13 = #asclient#85;
             #aslinrem#14 = #asclient#83;
         |FINSI;
         aRefer   = #aslinrem#14; |QBF aRefer;
         aFecRef  = #aslinrem#11;
         aCodIBAN = #aslinrem#10; |QBF aCodIBAN;
     |SINO;
         aRefer   = #asclient#83; |QBF aRefer;
         aFecRef  = #asclient#84;
         aCodIBAN = #asclient#77; |QBF aCodIBAN;
     |FINSI;

     |SI aRefer = ""; |O aCodIBAN = ""; |O aFecRef = "01.01.0000";
         eaError = " Faltan datos para la Remesa: ";
         |SI aRefer   = ""; eaError = eaError + "-Referencia de mandato "; |FINSI;
         |SI aCodIBAN = ""; eaError = eaError + "-Codigo IBAN "; |FINSI;
         |SI aFecRef  [ "01.01.1900"; eaError = eaError + "-Fecha Firma mandato "; |FINSI;
         swError = 1;
     |FINSI;
  |FINSI;

  |SI swError = 1; |Y nSwOp = 1;
      |SI nError = 0;
          |IMPRE 0;
          |SI FSalida ! 0; |ACABA; |FINSI;
          |INFOR "aspascar";
          |SI FSalida ! 0; |FINIMP; |ACABA; |FINSI;
      |FINSI;
      |PRINT;
      nError = 1;
  |FINSI;
|FPRC;

|PROCESO NuevoFinan;
      |DDEFECTO #901;
      #901#0 = #3#8;
      |LEE 101#901=;
      |SI FSalida ! 0;
          |GRABA 020#901b;
      |FINSI;
      #901#1 = #901#1 + 1;
      #901#2 = #901#2 + #3#7;
      |GRABA 020#901a;
      |LIBERA #901;
|FPRC;

|CALCULO secuen_01;
  enImporte =( #1#6 - #1#9 ) ! 2;
  |SI enImporte [ 0; |ACABA; |FINSI;   || no hay nada pendiente de cobrar

  nCliente = #1#3;
  |HAZ ChequeoDatos;
  |SI swError = 1;  |ACABA; |FINSI;

  nConta = nConta + 1;
  |SI nSwOp = 1;  |LIBERA #1; |ACABA; |FINSI;

  #3#0 = #ashisrec#0;  ||** Serie;
  #3#1 = #ashisrec#1;  ||** Factura;
  #3#2 = #asclient#2;  ||** Nom. Cliente;
  |SI #0#14 = "N";
       #3#3 = #asclient#8;  ||** Cif/Dni;
  |SINO;
       |HAZ quita_guion;
       #3#3 = ca;
  |FINSI;

  #3#4 = #asclient#16;     ||** Banco;
  #3#5 = #asclient#13;     ||** C.C.C;
  #3#6 = #asclient#14;     ||** Cta.Cte;
  #3#7 = enImporte;        ||** Importe Recibo;
  #3#8 = #1#5;             ||** Fecha Vencimiento;
  #3#9 = #asclient#0;      ||** Codigo cliente
  #3#10 = #asclient#3;     ||** Direccion
  #3#11 = #asclient#6;     ||** Poblacion
  #3#12 = #asclient#4;     ||** Cod.Provincia
  #3#13 = #asclient#5;     ||** Cod.Poblacion

  #3#15 = #asclient#77;    ||** IBAN
  #3#16 = #asclient#84;    ||** Fecha Mandato
  #3#17 = #asclient#76;    ||** BIC
  #3#18 = #asclient#85;    ||** Tipo pago Sepa
  #3#19 = #asclient#83;    ||** Referencia

  aAlfa = nConta;
  aAlfa = ("0000" + aAlfa) % -104;
  aAlfa = "L" + aAlfa;
  #3#14 = aAlfa;  ||** Contador de registros sin remesas

  |GRABA 020#3n;  ||** Graba el fichero indexado;
  |LIBERA #3;

  nTotal = nTotal + enImporte;

  |SI #0#26 = "S"; |HAZ NuevoFinan; |FINSI;

  #1#10 = "S";
  #1#12 = "R";
  #1#8  = ~;
  #1#9  = #1#9  + enImporte;
  #1#15 = enImporte;

  efFechaL = #0#8;
  |SI #aspascar#15 = "N";
      |HAZ eGrCobroParcial;
  |FINSI;

  #90#0 = #1#0;
  #90#1 = #1#1;
  |LIBERA #90;
  |LEE 140#90=;
  |SI FSalida = 0;
      #90#32 = #1#10;
      #90#33 = #1#12;
      |GRABA 140#90a;
  |FINSI;

  enDesRemesa = 0;
  |HAZ eGraba_Min;

  |GRABA 021#1a;
  |LIBERA #1;
|FCAL;

|CALCULO secuen_02;
  #1#0 = #aslinrem#2;
  #1#1 = #aslinrem#3;
  |LIBERA #1;
  |LEE 140#1=;
  |SI FSalida ! 0; |ACABA; |FINSI;

  nCliente = #1#3;              ||** Lee fichero de clientes;
  |HAZ ChequeoDatos;
  |SI swError = 1;  |ACABA; |FINSI;

  nConta = nConta + 1;
  |SI nSwOp = 1;  |LIBERA #1; |ACABA; |FINSI;

  #3#0 = #1#0;  ||** Serie;
  #3#1 = #1#1;  ||** Factura;
  #3#2 = #asclient#2;  ||** Nom. Cliente;
  |SI #0#14 = "N";
      #3#3 = #asclient#8;  ||** Cif/Dni;
  |SINO;
      |HAZ quita_guion;
      #3#3 = ca;
  |FINSI;
  #3#4  = #asclient#16;   ||** Banco;
  #3#5  = #asclient#13;   ||** C.C.C;
  #3#6  = #asclient#14;   ||** Cta.Cte;
  #3#7  = #aslinrem#7;   ||** Importe Recibo;
  #3#8  = #1#5;    ||** Fecha Vencimiento;
  #3#9  = #asclient#0;    ||** Codigo cliente
  #3#10 = #asclient#3;     ||** Direccion
  #3#11 = #asclient#6;     ||** Poblacion
  #3#12 = #asclient#4;     ||** Cod.Provincia
  #3#13 = #asclient#5;     ||** Cod.Provincia

  #3#15 = #aslinrem#10;    ||** IBAN
  #3#16 = #aslinrem#11;    ||** Fecha Mandato
  #3#17 = #aslinrem#12;    ||** BIC
  #3#18 = #aslinrem#13;    ||** Tipo pago Sepa
  #3#19 = #aslinrem#14;    ||** Referencia

  aAlfa = #aslinrem#1;
  aAlfa = ("0000" + aAlfa) % -104;
  aAlfa = "L" + aAlfa;
  #3#14 = aAlfa;
  |GRABA 020#3n;   ||** Graba el fichero indexado;
  |LIBERA #3;

  nTotal = nTotal + #aslinrem#7;
  |SI #0#26 = "S"; |HAZ NuevoFinan; |FINSI;

  efFechaO    = #13#6;
  efFechaL    = #0#8;
  enDesRemesa = 1;
  |HAZ eGraba_Min;
  |GRABA 020#1a;
  |LIBERA #1;
|FCAL;

|PROCESO quita_guion;
  ca1  = #asclient#8 % 101;
  ca2  = #asclient#8 % 201;
  ca3  = #asclient#8 % 301;
  ca4  = #asclient#8 % 401;
  ca5  = #asclient#8 % 501;
  ca6  = #asclient#8 % 601;
  ca7  = #asclient#8 % 701;
  ca8  = #asclient#8 % 801;
  ca9  = #asclient#8 % 901;
  ca10 = #asclient#8 % 1001;
  ca11 = #asclient#8 % 1101;
  ca12 = #asclient#8 % 1201;
  |SI ca1  = "."; |O ca1  = "-"; ca1  = ""; |FINSI;
  |SI ca2  = "."; |O ca2  = "-"; ca2  = ""; |FINSI;
  |SI ca3  = "."; |O ca3  = "-"; ca3  = ""; |FINSI;
  |SI ca4  = "."; |O ca4  = "-"; ca4  = ""; |FINSI;
  |SI ca5  = "."; |O ca5  = "-"; ca5  = ""; |FINSI;
  |SI ca6  = "."; |O ca6  = "-"; ca6  = ""; |FINSI;
  |SI ca7  = "."; |O ca7  = "-"; ca7  = ""; |FINSI;
  |SI ca8  = "."; |O ca8  = "-"; ca8  = ""; |FINSI;
  |SI ca9  = "."; |O ca9  = "-"; ca9  = ""; |FINSI;
  |SI ca10 = "."; |O ca10 = "-"; ca10 = ""; |FINSI;
  |SI ca11 = "."; |O ca11 = "-"; ca11 = ""; |FINSI;
  |SI ca12 = "."; |O ca12 = "-"; ca12 = ""; |FINSI;

  ca = ca1 + ca2 + ca3 + ca4 + ca5 + ca6 + ca7 + ca8 + ca9 + ca10 + ca11 + ca12;
  |QBF ca;
  ca = ("000000000000" + ca) % -112;
|FPRC;

|DEFBUCLE ashisrec;
  #1#1;
  2,7;
  #0#0, #0#2, #0#4, #0#6;
  #0#1, #0#3, #0#5, #0#7;
  be;
  NULL, secuen_01;
|FIN;

|DEFBUCLE aslinrem;
  #aslinrem#1;
  ;
  #0#16, 000;
  #0#16, 999;
  ;
  NULL, secuen_02;
|FIN;

|CALCULO pase; |TIPO 3;
  eaMoneda = #0#22;

  |SI #0#22 = "PESETAS";
      cabe1   = "0180";
      orde1   = "0380";
      indi1   = "0680";
      conc1_1 = "0681";
      conc1_2 = "0682";
      conc1_3 = "0683";
      conc1_4 = "0684";
      conc1_5 = "0685";
      tgen1   = "0980";
      tord1   = "0880";
  |SINO;
      cabe1   = "5180";
      orde1   = "5380";
      indi1   = "5680";
      conc1_1 = "5681";
      conc1_2 = "5682";
      conc1_3 = "5683";
      conc1_4 = "5684";
      conc1_5 = "5685";
      tgen1   = "5980";
      tord1   = "5880";
  |FINSI;

  elefe = &010;
  ceerre = &013;
  |SI #0#13 = "S";
      traba = ceerre + elefe;
      elefe = traba;
  |FINSI;

  aFichero = ("20x" + Usuario) % 108;
  |NOME_DAT #3 aFichero;
  |DELINDEX #3;

  |HORA aHora;   aHora  = (aHora  % 102) + (aHora  % 402) + (aHora  % 702);
  aFecha = #0#8; aFecha = (aFecha % 102) + (aFecha % 402) + (aFecha % 704);

  |SI #0#26 = "S";
      aAlfa = Usuario; aAlfa = "r1" + aAlfa;
      |NOME_DAT #901 aAlfa; |DELINDEX #901;
      |ABRE #901;
  |FINSI;

  nConta = 0;
  nTotal = 0;
  nError = 0;
  |ABRE #dsam0000;
  |ABRE #asclient;

  |ABRE #3;
  |SI #0#15 = "N";
      |ABRE #90;
      nSwOp = 1; |BUCLE ashisrec;
      |SI nError = 1;
          |PIEINF;
          |FININF;
          |FINIMP;
          |SI nConta ! 0;
              |CONFI "0000SHay errores en los Datos de los Recibos. Continuar con la Remesa";
              |SI FSalida = 0; nError = 0; |FINSI;
          |SINO;
              |MENAV "0000Hay errores en los Datos de los Recibos y no pasan a la Remesa";
          |FINSI;
      |FINSI;
      |SI nError = 0;
          nConta = 0;
          nTotal = 0;
          nSwOp  = 0;
          |BUCLE ashisrec;
      |FINSI;
      |CIERRA #90;
  |SINO;
      |ABRE #1;
      nSwOp = 1; |BUCLE aslinrem;
      |SI nError = 1;
          |PIEINF;
          |FININF;
          |FINIMP;
          |MENAV "0000Hay Errores en los Datos de los Recibos. Proceso interrumpdio";
      |SINO;
          nConta = 0;
          nTotal = 0;
          nSwOp  = 0;
          |BUCLE aslinrem;
      |FINSI;
      |CIERRA #1;
  |FINSI;

  |CIERRA #3;
  |CIERRA #asclient;

  |SI #0#26 = "S";
      |CIERRA #901;
  |FINSI;

  |SI nError = 0; |Y nConta = 0; |Y #0#15 = "N";
      |MENAV "    No existen Recibos para remesar";
      nError = 1;
  |FINSI;

  |SI nError = 0;

      |ABRE #5;
      #5#0 = #0#9;
      |LEE 040#5=;

      |SI #0#25 = "T";
          |ABRE #3;
          |ABRE #asclient;
          |ABRE #6;

          |DFICE empresa;
          fiche = "wb  " + empresa + "assecrec.seq";
          |QBF fiche;
          |FABRE fiche;
          handle = FSalida;
          |SI FSalida ] 0;
              |HAZ cabecera_p;
              |HAZ cab_ordenante;
              |HAZ regindiv;
              |HAZ campo_totales;
          |FINSI;
          FSalida = handle;
          |FCIERRA FSalida;
          |CIERRA #6;
          |CIERRA #asclient;
          |CIERRA #3;
      |SINO;
          |HAZ NuevoXML;
      |FINSI;
      |CIERRA #5;
  |FINSI;

  |DELINDEX #3;
  |CIERRA #dsam0000;

  |SI nError = 0;

      |SI #0#15 = "S";
          |ABRE #13;
          #13#0 = #0#16;
          |LEE 141#13=;
          |SI FSalida = 0;
              #13#5 = "X";
              #13#6 = efFechaL;
              #13#7 = #5#32;
              |GRABA 020#13a;
          |FINSI;
          |LIBERA #13;
          |CIERRA #13;
      |FINSI;

      E_sup = 1;
      E_i nf = "SNsn";
      sino  = "S";
      sino  = 1769 ? 1;
      sino  = sino > " ";
      |SI sino = "S"; |SUB_EJECUTA_NP "ascopdis"; |FINSI;
  |FINSI;
|FCAL;

||***********************************************************;
||************ REGISTRO DE CABECERA DE PRESENTADOR **********;
||***********************************************************;
|PROCESO cabecera_p;
  fechalf = #0#8;
  dd = fechalf % A102;
  mm = fechalf % A402;
  aa = fechalf % A902;
  cabe2 = dd + mm + aa;
  cabe5 =  #5#7 + #5#8;
  varalfa1 = cabe1 + #5#2 + #0#12 + cabe2 + cabe3 + #5#3 + libre20+ cabe5 + libre12 + libre40+ final14;
  ||contenido = handle + espacio + varalfa1 + elefe;
  ||FGRABA contenido;

  aSufijo = ("000" + #aspascar#12) % -103;
  aNif    = #5#2; |HAZ ObtenId;
  |SI #aspascar#23 = "S";
     varalfa1 = "0119143001";
     |SI #aspascar#24 = "S"; varalfa1 = "0119154001"; |FINSI;
  |SINO;
     varalfa1 = "0119445001";
  |FINSI;

  aAlfa = (aIdent + (" " * 35)) % 0135;
  varalfa1 = varalfa1 + aAlfa;
  aAlfa = (#5#3 + (" " * 70)) % 0170;
  varalfa1 = varalfa1 + aAlfa;
  aAlfa = #aspascar#8; |QBF aAlfa; aAlfa = (aAlfa % 0704) + (aAlfa % 0402) + (aAlfa % 0102);
  varalfa1 = varalfa1 + aAlfa;
  aAlfa = #aspascar#8; |QBF aAlfa;  aAlfa  = (aAlfa  % 0704) + (aAlfa  % 0402) + (aAlfa  % 0102);
  |HORA aAlfa1;        |QBF aAlfa1; aAlfa1 = (aAlfa1 % 0102) + (aAlfa1 % 0402) + (aAlfa1 % 0702) + "00001";

  aAlfa = "PRE" + aAlfa + aAlfa1; |QBF aAlfa;
  aAlfa = aAlfa + #5#2;
  aAlfa = aAlfa + aSufijo;             || falta Nif y sufijo

  aAlfa = (aAlfa + (" " * 35)) % 0135;

  varalfa1 = varalfa1 + aAlfa;
  contenido = handle + espacio + varalfa1;         |FGRABA contenido;

  varalfa1 = #5#7 + #5#8;
  contenido = handle + espacio + varalfa1;         |FGRABA contenido;

  varalfa1 = " " * 200;
  contenido = handle + espacio + varalfa1;         |FGRABA contenido;
  varalfa1 = " " * 234;
  contenido = handle + espacio + varalfa1 + elefe; |FGRABA contenido;

  l = l + 1;
|FPRC;

||***********************************************************;
||************ REGISTRO DE CABECERA DE ORDENANTE ************;
||***********************************************************;
|PROCESO cab_ordenante;
  fechalf = #0#8;
  dd = fechalf % A102;
  mm = fechalf % A402;
  aa = fechalf % A902;
  orde2 = dd + mm + aa;
  fechacargo = #0#11;
  dd = fechacargo % A102;
  mm = fechacargo % A402;
  aa = fechacargo % A902;
  orde3 = dd + mm + aa;
  orde5 = #5#4 + #5#5 + #5#11 + #5#6;
  varalfa1="";
  varalfa1 = orde1 + #5#2 + #0#21 + orde2 + orde3 + #5#3 + orde5 + libre8 + orde6 + libre10 + libre40 + final14;
  ||contenido = handle + espacio + varalfa1 + elefe;
  ||FGRABA contenido;

  aSufijo = ("000" + #aspascar#21) % -103;
  aNif    = #5#23; |HAZ ObtenId;
  |QBF aIdent; aIdent = (aIdent + (" " * 35)) % 0135;
  aAlfa = fechacargo; |QBF aAlfa; aAlfa = (aAlfa % 0704) + (aAlfa % 0402) + (aAlfa % 0102);
  |SI #aspascar#23 = "S";
     varalfa1 = "0219143002";
     |SI #aspascar#24 = "S"; varalfa1 = "0219154002"; |FINSI;
  |SINO;
     varalfa1 = "0219445002";
  |FINSI;
  varalfa1 = varalfa1 + aIdent + aAlfa;
  aAlfa = #5#22; |QBF aAlfa; aAlfa = (aAlfa + (" " * 70)) % 0170;
  varalfa1 = varalfa1 + aAlfa;
  contenido = handle + espacio + varalfa1;         |FGRABA contenido;

  varalfa1 = #5#25 + #5#26 + #5#27 + "ES" + #5#32;
  contenido = handle + espacio + varalfa1;         |FGRABA contenido;

  varalfa1 = " " * 200;
  contenido = handle + espacio + varalfa1;         |FGRABA contenido;
  varalfa1 = " " * 101;
  contenido = handle + espacio + varalfa1 + elefe; |FGRABA contenido;

  l = l + 1;
|FPRC;

||***********************************************************;
||******************* REGISTRO INDIVIDUAL *******************;
||***********************************************************;
|PROCESO regindiv;

  |SELECT #3#3;          ||Cojo la tercera clave del fichero CCC y Cta.
  |LEE 040#3p;
  |SI FSalida ! 0;  |ACABA;  |FINSI;

|ET compara;
  |HAZ campo_indiv;
  trecibo = trecibo + 1;

  enSwTrata = 1;
  enCanti   = #3#7;
  |HAZ ConverMoneda;
  timpsuc = timpsuc + enCanti;

  |SI #5#10 = "S"; |HAZ conceptos; |FINSI;
  |LEE 040#3s;
  |SI FSalida = 0;
      |VETE compara;
  |FINSI;
|FPRC;


||***********************************************************;
||******************* REGISTRO CONCEPTOS ********************;
||***********************************************************;
|CALCULO conceptos;
  |ACABA; || PEPE
  |ABRE #4;
  cont = 0;
  contreg = 0; ||** Contador de registros maximo 5;
  conc3 = "                                        ";
  conc4 = "                                        ";
  conc5 = "                                        ";
  #4#0 = #3#0;
  #4#1 = #3#1;
  #4#2 = 000;
  |LEE 040#4];
  |SI FSalida ! 0;  |CIERRA #4;  |ACABA;  |FINSI;
  |SI #4#0 ! #3#0; |O #4#1 ! #3#1;  |CIERRA #4;  |ACABA;  |FINSI;
  contreg = 1;
  cont = 1;
  serie = #4#0;
  factu = #4#1;
  conc3 = #4#7 % 140;
|ET otro;
  |LEE 040#4s;
  |SI FSalida ! 0;
      |VETE casifin;
  |FINSI;
  |SI serie ! #4#0;|O factu ! #4#1;
      |VETE casifin;
  |FINSI;
  cont = cont + 1;
  |SI cont = 1; conc3 = #4#7 % 140; |FINSI;
  |SI cont = 2; conc4 = #4#7 % 140; |FINSI;
  |SI cont = 3; conc5 = #4#7 % 140; |FINSI;
  |SI cont = 3; |Y contreg [ 5;
      cont = 0;
      |SI contreg = 1; conc1 = conc1_1 + #5#2; |FINSI;
      |SI contreg = 2; conc1 = conc1_2 + #5#2; |FINSI;
      |SI contreg = 3; conc1 = conc1_3 + #5#2; |FINSI;
      |SI contreg = 4; conc1 = conc1_4 + #5#2; |FINSI;
      |SI contreg = 5; conc1 = conc1_5 + #5#2; |FINSI;
      varalfa1 = conc1 + #0#21 + #3#3 + conc3 + conc4 + conc5 + final14;
      contenido = handle + espacio + varalfa1 + elefe;
      |FGRABA contenido;
      l = l + 1;
      contreg = contreg + 1;
      conc3 = "                                        ";
      conc4 = "                                        ";
      conc5 = "                                        ";
  |FINSI;
  |VETE otro;
|ET casifin;
  |SI cont ! 0;|Y contreg [ 5;
      |SI contreg = 1;conc1 = conc1_1 + #5#2;|FINSI;
      |SI contreg = 2;conc1 = conc1_2 + #5#2;|FINSI;
      |SI contreg = 3;conc1 = conc1_3 + #5#2;|FINSI;
      |SI contreg = 4;conc1 = conc1_4 + #5#2;|FINSI;
      |SI contreg = 5;conc1 = conc1_5 + #5#2;|FINSI;
      varalfa1 = conc1 + #0#21 + #3#3 + conc3 + conc4 + conc5 + final14;
      contenido = handle + espacio + varalfa1 + elefe;
      |FGRABA contenido;
      l = l + 1;
   |FINSI;
   |CIERRA #4;
|FCAL;

|PROCESO VarNova;
  |ABRE #90;
  |DDEFECTO #90;
  #90#0 = #3#0;
  #90#1 = #3#1;
  |LEE 041#90=;
  |CIERRA #90;

  aAlfa1 = aAlfa % 0;
  nNume1 = aAlfa1;
  aTexto = "";
  aAlfa1 = "";
  nSw1   = 0;
  |PARA nPara1 = 1; |SI nPara1 [ nNume1; |HACIENDO nPara1 = nPara1 + 1;
        nNume2 = (nPara1 * 100) + 1;
        aAlfa2 = aAlfa % nNume2;
        |SI aAlfa2 ! "#";
            |SI nSw1 = 0;
                aTexto = aTexto + aAlfa2;
            |SINO;
                aAlfa1 = aAlfa1 + aAlfa2;
            |FINSI;
        |SINO;
            |SI nSw1 = 0;
                nSw1 = 1;
                aAlfa1 = "";
            |SINO;
                aAlfa2 = "";
                |SI aAlfa1 = "S";
                    aAlfa2 = #90#0; |QBF aAlfa2;
                    |SI aAlfa2 ! ""; aAlfa2 = aAlfa2 + "/"; |FINSI;
                |FINSI;
                |SI aAlfa1 = "FRA";
                    aAlfa2 = #90#1; |QBF aAlfa2;
                    aAlfa2 = ("00000" + aAlfa2) % -105;
                |FINSI;
                |SI aAlfa1 = "FECHAFRA"; aAlfa2 = #90#29; |FINSI;
                |SI aAlfa1 = "BASEFRA";  aAlfa2 = #90#24; |FINSI;
                |SI aAlfa1 = "SUPLIDO";  aAlfa2 = #90#20; |FINSI;
                |SI aAlfa1 = "CIVAFRA";  aAlfa2 = #90#25; |FINSI;
                |SI aAlfa1 = "IRPFFRA";  aAlfa2 = #90#27; |FINSI;
                |SI aAlfa1 = "TOTALFRA"; aAlfa2 = #90#26; |FINSI;
                |SI aAlfa1 = "NOMESFRA"; |O aAlfa1 = "nomesfra"; |O aAlfa1 = "M";
                    aAlfa2 = #90#29;
                    aAlfa2 = aAlfa2 % 0402;
                    |SI aAlfa1 ! "M";
                        |SI aAlfa2 = "01"; aAlfa2 = "ENERO";      |FINSI;
                        |SI aAlfa2 = "02"; aAlfa2 = "FEBRERO";    |FINSI;
                        |SI aAlfa2 = "03"; aAlfa2 = "MARZO";      |FINSI;
                        |SI aAlfa2 = "04"; aAlfa2 = "ABRIL";      |FINSI;
                        |SI aAlfa2 = "05"; aAlfa2 = "MAYO";       |FINSI;
                        |SI aAlfa2 = "06"; aAlfa2 = "JUNIO";      |FINSI;
                        |SI aAlfa2 = "07"; aAlfa2 = "JULIO";      |FINSI;
                        |SI aAlfa2 = "08"; aAlfa2 = "AGOSTO";     |FINSI;
                        |SI aAlfa2 = "09"; aAlfa2 = "SEPTIEMBRE"; |FINSI;
                        |SI aAlfa2 = "10"; aAlfa2 = "OCTUBRE";    |FINSI;
                        |SI aAlfa2 = "11"; aAlfa2 = "NOVIEMBRE";  |FINSI;
                        |SI aAlfa2 = "12"; aAlfa2 = "DICIEMBRE";  |FINSI;
                        |SI aAlfa1 = "nomesfra"; aAlfa2 = aAlfa2 < ""; |FINSI;
                    |FINSI;
                |FINSI;
                |SI aAlfa1 = "AF";
                    aAlfa2 = #90#29;
                    aAlfa2 = aAlfa2 % -104;
                |FINSI;
                |QBF aAlfa2;

                aTexto = aTexto + aAlfa2;
                aAlfa1 = "";
                nSw1   = 0;
            |FINSI;
        |FINSI;
        aAlfa2 = "";
  |SIGUE;

  indi8 = aTexto; |QBF indi8;
  aAlfa = indi8 % 0;
  nNume1 = aAlfa;
  nNume1 = 140 - nNume1;
  |SI nNume1 ] 0;
      aAlfa = (" " * nNume1);
      indi8 = indi8 + aAlfa;
  |FINSI;
|FPRC;

            ||Calculo registros individuales;
|PROCESO campo_indiv;
  indi4 = #3#5 % 108;
  indi4 = indi4 + vacio;
  indi4 = indi4 % 108;

  #asclient#0 = #3#9;
  |LEE 000#asclient.=;
  |SI FSalida ! 0; |DDEFECTO #asclient; |FINSI;

  indi5 = #3#6 % 110;
  |QBT indi5;
  indi5="0000000000" + indi5;
  indi5= indi5 % -110;
  dc = #3#5 % 0902;
  |QBF dc;
  dc = dc + "**";
  dc = dc % 102;

  enCanti   = #3#7;
  enSwTrata = 0;
  |HAZ ConverMoneda;

  indi6 = ("0000000000" + enCanti) % -110;

  fechalf = #3#8;
  dd      = fechalf % A102;
  mm      = fechalf % A402;
  aa      = fechalf % A902;
  indi7   = dd + mm + aa;

  nSwC39 = 0;
  aAlfa  = #5#39; |QBF aAlfa;
  |SI aAlfa ! ""; nSwC39 = 1; |FINSI;

  |SI nSwC39 = 1;
      |HAZ VarNova;
  |SINO;
      posici  = #5#9 % -101;
      facalfa = #3#1;
      |SI posici = "*"; facalfa = #3#1;              |FINSI;
      |SI posici = "/"; facalfa = #3#0 + "-" + #3#1; |FINSI;
      |SI posici = "*"; |O posici = "/";
          indi8 = #5#9 % 0139; |QBF indi8;
      |SINO;
          indi8 = #5#9; |QBF indi8;
      |FINSI;
      indi8   = indi8 + " " + facalfa + "                                           ";
      indi8   = indi8 % A140;

  |FINSI;

  varalfa1 = indi1 + #5#2 + #0#21 + #3#3 + #3#2 + indi4 + dc + indi5 + indi6 + indi7 + indi10 + indi8 + indi9;

  || contenido = handle + espacio + varalfa1 + elefe;
  || |FGRABA contenido;

  aRefer   = #3#19;
  aFecRef  = #3#16;
  aCodIBAN = #3#15;
  aCodBIC  = #3#17;
  aTipoPag = "OOFF"
  |SI #3#18 ! "U";
      aTipoPag = "RCUR"
  |FINSI;

  indi6 = ("00000000000" + enCanti) % -111;
  |SI #aspascar#23 = "S";
     varalfa1 = "0319143003";
     |SI #aspascar#24 = "S"; varalfa1 = "0319154003"; |FINSI;
  |SINO;
     varalfa1 = "0319445003";
  |FINSI;

  aAlfa = #asclient#8; |QBF aAlfa;
  aAlfa = aAlfa + "CLI" + (("000000" + #asclient#0) % -106);
  aAlfa = (aAlfa + (" " * 35)) % 0135;

|| NIF + fecha + hora tique 5086_8:
  aAlfa = #asclient#8; |QBF aAlfa;
  aAlfa = aAlfa + aFecha + aHora;
  aAlfa = aAlfa + #3#14;
  aAlfa = (aAlfa + (" " * 35)) % 0135;

  varalfa1 = varalfa1 + aAlfa + aRefer + aTipoPag + "SUPP" + indi6;

  aAlfa = #3#2; |QBF aAlfa; aAlfa = (aAlfa + (" " * 70)) % 0170;
  varalfa1 = varalfa1 + (aFecRef % 0704) + (aFecRef % 0402) + (aFecRef % 0102);
  varalfa1 = varalfa1 + aCodBIC + aAlfa;
  contenido = handle + espacio + varalfa1;          |FGRABA contenido;

  aAlfa = #3#10; |QBF aAlfa; aAlfa = (aAlfa + (" " * 50)) % 0150;
  varalfa1 = aAlfa;
  aAlfa = (("00" + #3#12) % -102) + (("000" + #3#13) % -103) + " " + #3#11;
  |QBF aAlfa; aAlfa = (aAlfa + (" " * 50)) % 0150;
  varalfa1 = varalfa1 + aAlfa;

  #asprovin#0 = #3#12;
  |LEE 000#asprovin.=;
  |SI FSalida ! 0; |DDEFECTO #asprovin; |FINSI;

  aAlfa = #asprovin#1; |QBF aAlfa; aAlfa = (aAlfa + (" " * 40)) % 0140;
  varalfa1 = varalfa1 + aAlfa + "ES";

  aAlfa = #asclient#8; |QBF aAlfa; aAlfa = aAlfa % 0101;
  |SI aAlfa ] "A"; |Y aAlfa [ "W";
     varalfa1 = varalfa1 + "1";
     aAlfa = "1";
  |SINO;
     varalfa1 = varalfa1 + "2";
     aAlfa = "2";
  |FINSI;

  |SI aAlfa = "1";
     |SI aCodBIC = "           ";
         varalfa1 = varalfa1 + "I";
         aAlfa = #3#3; |QBF aAlfa; aAlfa = (aAlfa + (" " * 12)) % 0112;
         aAlfa = aAlfa + (" " * 23);
     |SINO;
         varalfa1 = varalfa1 + "A";
         aAlfa    = aCodBIC; aAlfa = aAlfa + (" " * 24);
     |FINSI;
  |SINO;
     varalfa1 = varalfa1 + "J";
     aAlfa    = #3#3; |QBF aAlfa; aAlfa = (aAlfa + (" " * 12)) % 0112;
     aAlfa    = aAlfa + (" " * 23);
  |FINSI;
  varalfa1 = varalfa1 + aAlfa + (" " * 35) + "A";
  contenido = handle + espacio + varalfa1;          |FGRABA contenido;


  varalfa1 = aCodIBAN + "SUPP" + indi8;
  |SI nSwC39 = 0;
      aAlfa = (" " * 119);
  |SINO;
      aAlfa = (" " * 19);
  |FINSI;
  varalfa1 = varalfa1 + aAlfa;

  contenido = handle + espacio + varalfa1 + elefe;
  |FGRABA contenido;

  l = l + 1;
|FPRC;

||***********************************************************;
||********** REGISTRO TOTAL ORDENANTE Y GENERAL *************;
||***********************************************************;
|PROCESO campo_totales;

|| **** REGISTRO TOTAL ORDENANTE

  tgen3 = trecibo;
  tgen3 = "0000000000" + tgen3;
  tgen3 = tgen3 % -110;

  |SI #0#22 = "EUROS  ";
      timpsuc = timpsuc * 100;
  |FINSI;

  l = l + 1;
  tnumsuc = l - 1;        ||Se quita un registro, porque no se cuenta
                          ||el primero
  tgen4  = ("00000000000000000" + timpsuc) % -117;
  nCalc = tnumsuc - 2; tgen5  = ("00000000" + nCalc) % -108;
  tgen6  = ("0000000000" + tnumsuc) % -110;
  aSufijo = ("000" + #aspascar#21) % -103;
  aNif    = #5#23; |HAZ ObtenId;
  |QBF aIdent; aIdent = (aIdent + (" " * 35)) % 0135;
  aAlfa = #aspascar#11; aAlfa = (aAlfa % 0704) + (aAlfa % 0402) + (aAlfa % 0102);
  varalfa1 = "04" + aIdent + aAlfa + tgen4 + tgen5 + tgen6;
  contenido = handle + espacio + varalfa1;         |FGRABA contenido;
  varalfa1 = " " * 120;
  contenido = handle + espacio + varalfa1;         |FGRABA contenido;
  varalfa1 = " " * 200;
  contenido = handle + espacio + varalfa1;         |FGRABA contenido;
  contenido = handle + espacio + varalfa1 + elefe; |FGRABA contenido;

  l = l + 1;
  tnumsuc = tnumsuc + 1;
  tgen6  = ("0000000000" + tnumsuc) % -110;
  varalfa1 = "05" + aIdent + tgen4 + tgen5 + tgen6;
  contenido = handle + espacio + varalfa1;         |FGRABA contenido;
  varalfa1 = " " * 128;
  contenido = handle + espacio + varalfa1;         |FGRABA contenido;
  varalfa1 = " " * 200;
  contenido = handle + espacio + varalfa1;         |FGRABA contenido;
  contenido = handle + espacio + varalfa1 + elefe; |FGRABA contenido;

|| **** REGISTRO TOTAL GENERAL

  l = l + 1; tnumsuc = l;

  nCalc  = tnumsuc;                 ||** Total reg. sucursales
  tgen6  = ("0000000000" + nCalc) % -110;

  varalfa1 = "99" + tgen4 + tgen5 + tgen6;
  contenido = handle + espacio + varalfa1;
  |FGRABA contenido;
  varalfa1 = " " * 163;
  contenido = handle + espacio + varalfa1;         |FGRABA contenido;
  varalfa1 = " " * 200;
  contenido = handle + espacio + varalfa1;         |FGRABA contenido;
  contenido = handle + espacio + varalfa1 + elefe; |FGRABA contenido;

  #0#17 = tgen3;    |PINTA #0#17;
  #0#18 = tgen6;    |PINTA #0#18;
  #0#19 = timpsuc;
  |SI #0#22 = "EUROS  ";
      #0#19 = timpsuc / 100;
  |FINSI;
  |PINTA #0#19;
|FPRC;
