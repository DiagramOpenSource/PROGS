|FICHEROS;
   asmancaj #0;
   asempcon #1;

   asmovtos #2;
   asmanmov #3;

   :/contagen/def/canempre #330;
   :/contagen/def/canempr1 #331;
   :/contagen/def/cacuenta #304;
   :/contagen/def/cadeflab #306;
|FIN;

|VARIABLES;
   nModoEntrada = 0;

   nEmpDef     = 0;
   nPerDef     = 0;

   nSwError    = 0;
   nDEmpresa   = 0;
   nHEmpresa   = 0;
   nDPeriodo   = 0;
   nHPeriodo   = 0;
   nInkey      = 0;

   aAlfa1      = "";
   aAlfa2      = "";
   aAlfa3      = "";
   nNume1      = 0;
   aPath       = "";
   aPant       = "";
|FIN;

|INCLUYE i_varcof;

|| :::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
|PROCESO LeeEmp;
 nSwError = 0;
 enSwSinMen = 1;
 enEmpresa  = #0#4;
 enPerConta = #0#5;
 |HAZ LeeEmpConta;
 |SI swerror = 1;
     nSwError = 1;
 |SINO;
      #0#8 = eaNombreEmp;
      #0#10 = dig4;  #0#11 = dig5;
      #0#12 = dig6;  #0#13 = dig7;
      #0#14 = dig8;  #0#15 = dig9;

      |C_M #0#16,1,eaDepar;
      |C_M #0#18,1,eaDepar;
      |SI eaDepar = "N";
          #0#16 = "";      |PINTA #0#16;
          #0#17 = "";      |PINTA #0#17;
          #0#18 = "N";     |PINTA #0#18;
      |SINO;
          nModoEntrada = (FEntrada / 100) ! 0;
          |SI nModoEntrada = 1;
              #0#16 = #306#22; |PINTA #0#16;
              #0#17 = #306#23; |PINTA #0#17;
          |FINSI;
      |FINSI;
 |FINSI;
|FPRC;

|RUTINA nInf1;
 |SI FSalida = "POSICION";
     #331#2 = #0#4;
     #331#3 = #0#5;
 |SINO;
     #331#2 = nDEmpresa;
     #331#3 = nDPeriodo;
 |FINSI;
|FRUT;

|RUTINA nSup1;
 #331#2 = nHEmpresa;
 #331#3 = nHPeriodo;
|FRUT;

|PROCESO LaConsEmp;
  |ABRE #331;
  nDEmpresa = 00001; nHEmpresa = 99999;
  nDPeriodo = 0;     nHPeriodo = 9;
  |SI Campo = 5;
      nDEmpresa = #0#4;
      nHEmpresa = #0#4;
  |FINSI;
  |CONSULTA_F_CLAVE #1#331, nInf1, nSup1;
  #0#4 = #331#2; |PINTA #0#4;
  #0#5 = #331#3; |PINTA #0#5;
  |CIERRA #331;
|FPRC;

|CALCULO ElDefecto; |TIPO 7;
 nModoEntrada = (FEntrada / 100) ! 0;
 |SI nModoEntrada = 1;
     #0#4 = nEmpDef; |PINTA #0#4;
     #0#5 = nPerDef; |PINTA #0#5;
 |FINSI;
|FCAL;

|CALCULO Importe; |TIPO 0;
    nInkey = FSalida;

    nModoEntrada = (FEntrada / 100) ! 0;
    |SI nModoEntrada = 1; nInkey = 11; |FINSI;

    |SI nInkey = 10; #0#7 = 0;    |FINSI;
    |SI nInkey = 11; #0#7 = #0#3; |FINSI;
    |SI nInkey = 12; |HAZ Recupero; |FINSI;

    |PINTA #0#7;
|FCAL;

|CALCULO LaEmpresa; |TIPO 0;
 nInkey = FSalida;

 |SI nInkey = 10;
     |HAZ LaConsEmp;
     |ACABA;
 |FINSI;

 |ABRE #330;
 #330#2 = #0#4;
 #330#3 = 0;
 |LEE 001#330];
 |SI FSalida ! 0; |O #330#2 ! #0#4;
     |MENAV "    Error. No existe Empresa Contable";
     |ERROR;
 |FINSI;
 |CIERRA #330;
|FCAL;

|CALCULO MiraEmpresa; |TIPO 0;
   |SI FSalida = 2; |ACABA; |FINSI;

   nInkey = FSalida;
   |SI nInkey = 10;
       |HAZ LaConsEmp;
   |FINSI;

   |HAZ LeeEmp;
   |SI nSwError = 0;
       |PINTA #0#8;
       |HAZ CalPath;
   |SINO;
       |MENAV "    Error. No existe Periodo Contable";
       |ERROR;
   |FINSI;
|FCAL;

|PROCESO CalPath;

   |DBASS aAlfa1; |QBF aAlfa1;
   aAlfa1 = aAlfa1 + "contagen/";
   aAlfa2 = ("00000" + #0#4) % -105;
   aAlfa3 = ("0" + #0#5) % -101;

   aPath  = aAlfa1 + "fich/" + aAlfa2 + aAlfa3 + "/";
   aPant  = aAlfa1 + "pan/";

   |PATH_DAT #304 aPath;
   |PATH_DAT #306 aPath;
   enEmpresa  = #0#4;
   enPerConta = #0#5;
   |ABRE #306;
   |DDEFECTO #306;
   |LEE 001#306p;
   |CIERRA #306;
|FPRC;

|RUTINA nInf2;
 |SI FSalida = "POSICION";
     #304#0 = #0#6;
 |SINO;
     #304#0 = "";
 |FINSI;
 #304#3 = "S";
|FRUT;

|RUTINA nSup2;
 #304#0 = "zzzzzzzzzzz";
 #304#3 = "S";
|FRUT;

|PROCESO LaConsCue;
  |ABRE #304;
  |CONSULTA_F_CLAVE #1#304, nInf2, nSup2;
  #0#6 = #304#0; |PINTA #0#6;
  |CIERRA #304;
|FPRC;

|CALCULO MiraCuenta; |TIPO 0;
   |SI FSalida = 2; |ACABA; |FINSI;
   nInkey = FSalida;

   eaCuenta = #0Campo; |QBF eaCuenta;
   |SI eaCuenta = ""; |Y nInkey ! 10;
       |MENAV "    Introduzca una Cuenta";
       |ERROR;
       |ACABA;
   |FINSI;

   |HAZ CalPath;

   |SI nInkey = 10;
       |HAZ LaConsCue;
   |FINSI;

   emCta = 0;
   eaCuenta = #0Campo;
   |HAZ LeeCuenta;
   |SI eswCta = 1;
        |ERROR;
        |CONFI "    SError.! No existe Cuenta. Desea darla de alta ";
        |SI FSalida = 0;
            |SUB_EJECUTA_NP "aszc0001";
            #0Campo = eaCuenta;
            |PINTA #0Campo;
        |FINSI;
   |SINO;
        |SI eswCta = 2;
            |MENAV "0000Esta Cuenta NO tiene Pase Directo.";
            |ERROR;
        |SINO;
            #0#9 = #304#2; |PINTA #0#9;
        |FINSI;
   |FINSI;
|FCAL;

|CALCULO Cuenta; |TIPO 6;
     emCta = 1;        || Dar mensajes de error
     eaCuenta = #0Campo;
     salidas  = FSalida;
     |HAZ FiltraPuntos;
     #0Campo = eaCuenta;
     |PINTA #0Campo;
     |HAZ SacaNivel;
|FCAL;

|CALCULO AntesDepar; |TIPO 7;
   |C_M #0Campo, 0, aAlfa1;
   |SI aAlfa1 = "S"
      |SI Haybasica = 1;
          |MENSA "    <F2> Consultar Actividades";
      |SINO;
          |MENSA "    <F2> Consultar Departamentos";
      |FINSI;
   |FINSI;
|FCAL;

|CALCULO TrasDepar; |TIPO 0;
   |SI eaDepar = "N"; |ACABA; |FINSI;

   eOpDep = 0; || 1 = Consulta pantalla
   |SI FSalida = 10; eOpDep = 1;  |FINSI;
   enActividad = #0#16;
   eaCodDep    = #0#16;
   |HAZ ActFactu;
   |SI eswLect = 1;
       |MENAV "    Error.! No Existe Actividad / Departamento";
       |ERROR;
   |SINO;
       aAlfa1 = enActividad;
       |SI Haybasica = 1;
           aAlfa1 = ("00" + aAlfa1) % A02;
       |FINSI;
       #0#16 = eaCodDep;    |PINTA #0#16;
       #0#17 = eaNombreAct; |PINTA #0#17;
       |SI efActFin > "01.01.1900";
           |CONFI "    NAntencion!!!. Actividad con Fecha de BAJA, ¨ Desea introducir Facturas ? ";
           |SI FSalida ! 0;
               |ERROR;
               |ACABA;
           |FINSI;
       |FINSI;
       |SI eaEsCom = "S";
           |CONFI "    NAntencion!!!. Actividad Comunera, ¨ Desea introducir Facturas ? ";
           |SI FSalida ! 0;
               |ERROR;
               |ACABA;
           |FINSI;
       |FINSI;
   |FINSI;
|FCAL;

|| **********************************************************************
|CALCULO MiraBien; |TIPO 3;
 |HAZ LeeEmp
 |SI nSwError = 1;
     |MENAV "    Error. Introduzca una Empresa Contable. ";
     |ERROR;
     |ACABA;
 |FINSI;
 eaCuenta = #0#6; |QBF eaCuenta;
 |SI eaCuenta ! "";
    emCta = 0;
    |HAZ LeeCuenta;
    |SI eswCta = 2; eswCta = 1; |FINSI;
 |FINSI;
 |SI eswCta = 1;
     |MENAV "    Error. Introduzca una Cuenta Contable";
     |ERROR;
 |FINSI;
|FCAL;

|| ....................... Carga defs de Contabilidad

|CALCULO Tipo8; |TIPO 80;
   |ABRE #1;
   |DDEFECTO #1;
   |LEE 001#1p;
   |CIERRA #1;
   nEmpDef = #1#1;
   nPerDef = #1#2;

   |HAZ DirAplSt;
|FCAL;
|| *******************************************************

|PROCESO LeeMov;
   #3#0 = #2#3;
   |LEE 000#3=;
   |SI FSalida ! 0;
       |ACABA;
   |FINSI;

   |SI #3#5 = "N";
       |SI #3#2 = "I";
           #0#7 = #0#7 +. #2#8;
       |SINO;
           #0#7 = #0#7 -. #2#8;
       |FINSI;
   |FINSI;
|FPRC;

|DEFBUCLE LeeMov;
 #2#1;
 ;
 #0#0, "01.01.0000", 0001;
 #0#0, "31.12.2999", 9999;
 ;
 NULL, LeeMov;
|FIN;

|PROCESO Recupero;
 #0#7 = #0#3;
 |ABRE #3;
 |BUCLE LeeMov;
 |CIERRA #3;
|FPRC;
