|FICHEROS;
     asbancos #0;                           || CALCULO PARA OPCION DE IM-   ;
|FIN;                                       || PRESION EN MANTENIMIENTOS    ;

|VARIABLES;
     informe = "asbancos";                  || CAMBIAR VARIABLE `informe',  ;
     men1 = "    NO EXISTE INFORME.";       || FICHERO Y NOMBRE DE CALCULO. ;
     men2 = "    NO EXISTE IMPRESORA.";

     aAlfa     = "";
     aAlfa1    = "";
     aAlfa2    = "";
     aEntidad  = "";
     aSucursal = "";
     aDC       = "";
     aCuenta   = "";
     aPais     = "";
     aChar     = "";
     aCadena   = "";

     nCalc     = 0;
     nSwCambia = 0;

     &eaEntidad  = "";
     &eaSucursal = "";
     &eaCuenta   = "";
     &eaDC       = "";

     &eaCta      = "";
     &eaPais     = "";
     &eaIBAN     = "";
|FIN;

|CALCULO ttbancos;|TIPO 4;
|IMPRE 0;                                   ||Abre impresora.                ;
|SI FSalida = 0;                            ||? existe impresora.            ;
    |INFOR informe;                         ||Abre informe.                  ;
    |SI FSalida = 0;                        ||? existe informe               ;
        |LEE 040#0p;                        ||Lee el primer registro.        ;
        |SI FSalida = 0;
            |PRINT;                         ||Comienza informe.              ;
        |FINSI;
        |ET leer;                           ||Bucle de lectura e impresion.  ;
        |LEE 040#0s;
        |SI FSalida = 0;
            |PRINT;
            |VETE leer;
        |FINSI;                             ||Fin de fichero.                ;
        |PIEINF;                            ||Impresion final.               ;
        |FININF;                            ||Cierra informe.                ;
        |FINIMP;                            ||Cierra impresora.              ;
    |SINO;
        |MENAV men1;                        ||No informe.                    ;
    |FINSI;
|SINO;
    |MENAV men2;                            ||No impresora.                  ;
|FINSI;
|FCAL;

|PROCESO MCtaBan; |TIPO 0;
     eaCta = #0Campo;
     |HAZ MFiltraPtos;
     #0Campo = eaCta; |PINTA #0Campo;
|FPRC;

|PROCESO Tipo7IBAN_Fbancos;  |TIPO 7;
     aAlfa = #asbancos#25;   |QBF aAlfa;

     |SI aAlfa ! "";  |ACABA;  |FINSI;

     eaCta  = #asbancos#7;
     eaPais = #asbancos#31;
     |HAZ CalcuIBAN;

     #asbancos#25 = eaIBAN;
|FPRC;

|PROCESO Tipo0IBAN_Fbancos;  |TIPO 0;
     eaCta  = #asbancos#7;
     eaPais = #asbancos#31;
     |HAZ CheqIBAN;

     aAlfa = #asbancos#25;    |QBF aAlfa;
     |SI aAlfa ! eaIBAN;
         |CONFI "0000SEl codigo IBAN puede ser erroneo, quiere calcularlo.";
         |SI FSalida = 0;
             #asbancos#25 = eaIBAN;
             |PINTA #asbancos#25;
             |ERROR;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO Tipo12Fasbancos;  |TIPO 12;
     eaCta  = #asbancos#7;
     eaPais = #asbancos#31;
     |HAZ CheqIBAN;

     aAlfa = #asbancos#25;    |QBF aAlfa;
     |SI aAlfa ! eaIBAN;
         |MENAV "0000El codigo IBAN puede ser erroneo.";
     |FINSI;
|FPRC;
