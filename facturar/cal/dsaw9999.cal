|FICHEROS;
     asempcon  #100;          ||Empresas Contables

     :/contagen/def/canempre #200;    || empresas Contables
     :/contagen/def/cacuenta #304;    || Plan contable

     asempcon  #200;          ||Empresas Contables
|FIN;

|VARIABLES;
     &enSwEstaPlan = 0;
     &PRMNT_enVer   = 0;
     aComodin1   = "";
     aComodin2   = "";
     nCont       = 0;
     nCalc       = 0;
     &eaEntidad  = "";
     &eaSucursal = "";
     &eaCuenta   = "";
     &eaDC       = "";
     &eaDato     = "";
     &eaCampo1   = "";
     &eaCampo2   = "";
     &eaCampo3   = "";
     &eaCampo4   = "";
     &eaCampo5   = "";
     &eaCampo6   = "";

     &eaVarDni   = "";
     &enCalcCif  = 0;

     aAlfa       = "";
     aLetrasNif  = "TRWAGMYFPDXBNJZSQVHLCKE";
     aLetrasCif  = "ABCDEFGHNPQSKLMJUVWR";
     aLetrasExt  = "XYZ";
     aLetraGuiri = "";
     aLetra1     = "";
     aLetra2     = "";
     aLetra3     = "";
     aLetra4     = "";
     aLetra5     = "";
     aLetra6     = "";
     aLetra7     = "";
     aLetra8     = "";
     aCadena     = "";
     aCaracter   = "";
     aCarac1     = 0;
     aCarac2     = 0;
     aCarac3     = 0;
     aCarac4     = 0;
     aCarac5     = 0;

     aDigito     = "";
     aDigito1    = "";
     aDigito2    = "";
     aDigito3    = "";
     aDigito4    = "";
     aDigito5    = "";
     aDigito6    = "";
     aDigito7    = "";
     aDigito8    = "";
     aDigito9    = "";
     aPath       = "";

     nDigito1    = 0;
     nDigito2    = 0;
     nDigito3    = 0;
     nDigito4    = 0;
     nDigito5    = 0;
     nDigito6    = 0;
     nDigito7    = 0;
     nDigito8    = 0;
     nDigito9    = 0;

     nValor      = 0;
     nValor1     = 0;
     nValor2     = 0;
     nValor3     = 0;
     nValor4     = 0;
     nValor5     = 0;
     nValor6     = 0;
     nValor7     = 0;
     nValor8     = 0;
     nSwCambia   = 0;

     nCaracter   = 0;
     nPosi       = 0;

     &endigC   = 0;
     &eaCta    = "";
     &eswCta   = 0;

     aLong  = "";
     nLong  = 0;

     nNume1 = 0;
     nPos   = 0;
     aAlfa1 = "";
     aAlfa2 = "";
     aAlfa3 = "";
     aAlfa4 = "";
     aGuarda = "";
     aPunto  = "";
     nCampo = 0;
|FIN;

|PROCESO CompruebaCif;
     nValor1   = nDigito3 + nDigito5 + nDigito7;

     nValor3   = ((nDigito2 * 2) $ 10) + ((((nDigito2 * 2) / 10) - .49) ! 0);
     nValor4   = ((nDigito4 * 2) $ 10) + ((((nDigito4 * 2) / 10) - .49) ! 0);
     nValor5   = ((nDigito6 * 2) $ 10) + ((((nDigito6 * 2) / 10) - .49) ! 0);
     nValor6   = ((nDigito8 * 2) $ 10) + ((((nDigito8 * 2) / 10) - .49) ! 0);

     nValor2   = nValor3 + nValor4 + nValor5 + nValor6;
     nValor    = 10 - ((nValor1 + nValor2) $ 10);

     |SI aDigito1 = "P";  |O aDigito1 = "S";  |O aDigito1 = "Q";
         nValor  = 64 + nValor;
         aDigito = &nValor;

         |SI aDigito9 ! aDigito;
             aAlfa = "0000 Puede estar mal introducido el CIF, el Digito de Control me da un resultado de " + aDigito;
             |MENAV aAlfa;
         |SINO;
             |MENAV "0000 El CIF es correcto.";
         |FINSI;
     |SINO;
         |SI nValor ] 10;  nValor = 0;  |FINSI;

         |SI nValor ! nDigito9;
             aAlfa = "0000 Puede estar mal introducido el CIF, el Digito de Control me da un resultado de " + nValor;
             |MENAV aAlfa;
         |SINO;
             |MENAV "0000 El CIF es correcto.";
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO CalculoDNI;
     |QBF eaVarDni;
     aLetra2 = eaVarDni;
     |QBT aLetra2;

     aLetraGuiri = "";
     aCaracter   = eaVarDni % 101;
     aAlfa       = aLetrasExt - aCaracter;
     |SI FSalida ! 0;
         aLetraGuiri  = aCaracter;
     |FINSI;

     aCaracter  = aLetra2 % 101;
     aAlfa      = aLetrasCif - aCaracter;

     |SI aAlfa = aLetrasCif;
         aLetra4 = "";
         aLetra3 = aLetra2 % 0;
         aCarac2 = aLetra3;

         |PARA aCarac5 = 1;  |SI aCarac5 [ aCarac2;  |HACIENDO aCarac5 = aCarac5 + 1;
               aCarac3 = (aCarac5 * 100) + 1;
               aLetra3 = aLetra2 % aCarac3;
               |SI aLetra3 ] "0"; |Y aLetra3 [ "9";
                   aLetra4 = aLetra4 + aLetra3;
              |FINSI;
         |SIGUE;

         |SI aLetraGuiri ! "";
             aLetra4 = ("0000000" + aLetra4) % -107;
             |SI aLetraGuiri = "X"; aLetra4 = "0" + aLetra4;  |FINSI;
             |SI aLetraGuiri = "Y"; aLetra4 = "1" + aLetra4;  |FINSI;
             |SI aLetraGuiri = "Z"; aLetra4 = "2" + aLetra4;  |FINSI;
         |FINSI;

         aCarac4 = aLetra4;
         |SI aCarac4 ! 0; |Y aLetra4 ! "";
             aCarac1 = aCarac4 $ 23;
             aCarac1 = aCarac1 + 1;
             aCarac3 = (aCarac1 * 100) + 1;
             aLetra1 = aLetrasNif % aCarac3;
             aLetra4 = "000000000000" + aLetra4;
             aLetra7 = aLetra4 % -103;
             aLetra6 = aLetra4 % -403;
             aLetra5 = aLetra4 % -703;

             aLetra8 = aLetra5  % 101;
             |SI aLetra8 = "0"; aLetra5 = aLetra5 % 202; |FINSI;

             aLetra8 = aLetra5  % 101;
             |SI aLetra8 = "0"; aLetra5 = aLetra5 % 201; |FINSI;

             aLetra8 = aLetra5  % 101;
             |SI aLetra8 = "0"; aLetra5 = ""; |FINSI;

             aLetra4 = aLetra6 + aLetra7 + aLetra1;
             |SI aLetra5 ! "";
                 aLetra4 = aLetra5 + aLetra6 + aLetra7 + aLetra1;
             |FINSI;

             eaVarDni = aLetra4;
             eaVarDni = ("000000000" + eaVarDni) % -109;
         |FINSI;
     |SINO;
         aCaracter = "";
         aCadena   = "";
         |PARA nCaracter = 1; |SI nCaracter [ 12; |HACIENDO nCaracter = nCaracter + 1;
               nPosi = (100 * nCaracter) + 1;
               aCaracter = eaVarDni % nPosi;
               |SI aCaracter ! "."; |Y aCaracter ! "-"; |Y aCaracter ! " ";
                   aCadena = aCadena + aCaracter;
               |FINSI;
         |SIGUE;
         eaVarDni = aCadena;

         aDigito1  = eaVarDni % 101;
         aDigito2  = eaVarDni % 201;   nDigito2  = aDigito2;
         aDigito3  = eaVarDni % 301;   nDigito3  = aDigito3;
         aDigito4  = eaVarDni % 401;   nDigito4  = aDigito4;
         aDigito5  = eaVarDni % 501;   nDigito5  = aDigito5;
         aDigito6  = eaVarDni % 601;   nDigito6  = aDigito6;
         aDigito7  = eaVarDni % 701;   nDigito7  = aDigito7;
         aDigito8  = eaVarDni % 801;   nDigito8  = aDigito8;
         aDigito9  = eaVarDni % 901;   nDigito9  = aDigito9;

         |SI enCalcCif ! 0;  |HAZ CompruebaCif;  |FINSI;
     |FINSI;

     |SI aLetraGuiri ! "";
         eaVarDni = aLetraGuiri + (eaVarDni % 211);
     |FINSI;

     eaVarDni = (eaVarDni + "            ") % 112;
|FPRC;

|PROCESO EsVisados;
     |DBASE aPath;
     aPath = aPath + "def/visam001.mas";
     |XABRE "E", aPath, 0;
|FPRC;

|PROCESO MFiltraPtos;
     ||ARA

    aGuarda = FSalida;
    |SI eaCta = "            "; |ACABA; |FINSI;

     |ABRE #asempcon;
     |DDEFECTO #asempcon;
     |LEE 000#asempcon.=;
     |SI FSalida ! 0;         |ACABA; |FINSI;
     |CIERRA #asempcon;

     ||Ahora leo en la contagen
     |ABRE #200;
     |DDEFECTO #200;
     #200#2 = #asempcon#1;
     #200#3 = #asempcon#2;
     |LEE 000#200=;
     |SI FSalida ! 0; |ACABA; |FINSI;
     nCampo = 13 + #200#13;
     endigC = #200nCampo;
     |CIERRA #200;

    aAlfa1 = "";
    |PARA nNume1 = 1; |SI nNume1 [ 12; |HACIENDO nNume1 = nNume1 + 1;
          aAlfa2 = nNume1; |QBF aAlfa2;
          aAlfa2 = aAlfa2 + "01";
          aAlfa3 = eaCta % aAlfa2;
          |SI aAlfa3 = "."; |O aAlfa3 = "0"; |O aAlfa3 = "1"; |O aAlfa3 = "2";
                            |O aAlfa3 = "3"; |O aAlfa3 = "4"; |O aAlfa3 = "5";
                            |O aAlfa3 = "6"; |O aAlfa3 = "7"; |O aAlfa3 = "8";
                            |O aAlfa3 = "9";
              aAlfa1 = aAlfa1 + aAlfa3;
          |FINSI;
    |SIGUE;
    eaCta = aAlfa1;

    aPunto = eaCta - ".";
    |SI FSalida ! 0;
        nNume1 = FSalida;
        nPos   = 100 + (((nNume1 / 100) ! 0) -1);
        aAlfa2 = eaCta % nPos;   |QBF aAlfa2;
        nPos   = (((nNume1 / 100) ! 0) * 100) + 199;
        aAlfa3 = eaCta % nPos;   |QBF aAlfa3;

        FSalida = nNume1;
        |PARA; |SI FSalida ! 0; |HACIENDO;
               aAlfa3 = aAlfa3 - "."; || limpiar mas puntos (los ignoramos)
        |SIGUE;

        nPos   = endigC - ( (A \ aAlfa2 % 0) + (A \ aAlfa3 % 0));
        aAlfa4 = (A\ "0" * nPos);
        eaCta  = (A\ aAlfa2 + aAlfa4 + aAlfa3);
    |FINSI;

    FSalida = aGuarda;
|FPRC;

|PROCESO CalculoDC;
     nDigito1  = 0;
     nDigito2  = 0;
     aComodin1 = "";
     aComodin2 = "";

     |PARA nCont = 1; |SI nCont [ 4; |HACIENDO nCont = nCont + 1;
           nCalc = 0 - ((100 * nCont) + 1);
           aCaracter = eaSucursal % nCalc;
           |SI aCaracter = ""; aCaracter = "0"; |FINSI;
           aComodin1 = aComodin1 + aCaracter;
     |SIGUE;

     |PARA nCont = 1; |SI nCont [ 4; |HACIENDO nCont = nCont + 1;
           nCalc = 0 - ((100 * nCont) + 1);
           aCaracter = eaEntidad % nCalc;
           |SI aCaracter = ""; aCaracter = "0"; |FINSI;
           aComodin1 = aComodin1 + aCaracter;
     |SIGUE;

     |PARA nCont = 1; |SI nCont [ 8; |HACIENDO nCont = nCont + 1;
           nCalc = (100 * nCont) + 1;
           aCaracter = aComodin1 % nCalc;
           nCalc     = aCaracter;

           |SI nCont = 1; nDigito1 = nDigito1 + (nCalc * 6);  |FINSI;
           |SI nCont = 2; nDigito1 = nDigito1 + (nCalc * 3);  |FINSI;
           |SI nCont = 3; nDigito1 = nDigito1 + (nCalc * 7);  |FINSI;
           |SI nCont = 4; nDigito1 = nDigito1 + (nCalc * 9);  |FINSI;
           |SI nCont = 5; nDigito1 = nDigito1 + (nCalc * 10); |FINSI;
           |SI nCont = 6; nDigito1 = nDigito1 + (nCalc * 5);  |FINSI;
           |SI nCont = 7; nDigito1 = nDigito1 + (nCalc * 8);  |FINSI;
           |SI nCont = 8; nDigito1 = nDigito1 + (nCalc * 4);  |FINSI;
     |SIGUE;

     nDigito1 = 11 - (nDigito1 $ 11);
     |SI nDigito1 = 10; nDigito1 = 1; |FINSI;
     |SI nDigito1 = 11; nDigito1 = 0; |FINSI;

     |PARA nCont = 1; |SI nCont [ 10; |HACIENDO nCont = nCont + 1;
           nCalc = 0 - ((100 * nCont) + 1);
           aCaracter = eaCuenta % nCalc;
           |SI aCaracter = ""; aCaracter = "0"; |FINSI;
           aComodin2 = aComodin2 + aCaracter;
     |SIGUE;

     |PARA nCont = 1; |SI nCont [ 10; |HACIENDO nCont = nCont + 1;
           nCalc = (100 * nCont) + 1;
           aCaracter = aComodin2 % nCalc;
           nCalc     = aCaracter;

           |SI nCont = 1;  nDigito2 = nDigito2 + (nCalc * 6);  |FINSI;
           |SI nCont = 2;  nDigito2 = nDigito2 + (nCalc * 3);  |FINSI;
           |SI nCont = 3;  nDigito2 = nDigito2 + (nCalc * 7);  |FINSI;
           |SI nCont = 4;  nDigito2 = nDigito2 + (nCalc * 9);  |FINSI;
           |SI nCont = 5;  nDigito2 = nDigito2 + (nCalc * 10); |FINSI;
           |SI nCont = 6;  nDigito2 = nDigito2 + (nCalc * 5);  |FINSI;
           |SI nCont = 7;  nDigito2 = nDigito2 + (nCalc * 8);  |FINSI;
           |SI nCont = 8;  nDigito2 = nDigito2 + (nCalc * 4);  |FINSI;
           |SI nCont = 9;  nDigito2 = nDigito2 + (nCalc * 2);  |FINSI;
           |SI nCont = 10; nDigito2 = nDigito2 + (nCalc * 1);  |FINSI;
      |SIGUE;

      nDigito2 = 11 - (nDigito2 $ 11);
      |SI nDigito2 = 10; nDigito2 = 1; |FINSI;
      |SI nDigito2 = 11; nDigito2 = 0; |FINSI;

      eaDC = nDigito1 + nDigito2;
|FPRC;


|PROCESO EstaPlanContable;
     enSwEstaPlan = 0;

     |SI eaCta = "            ";
          enSwEstaPlan = 1;
          |ACABA;
     |FINSI;

     |ABRE #asempcon;
     |LEE 000#asempcon.p;
     |SI FSalida ! 0; |ACABA; |FINSI;
     |CIERRA #asempcon;

     |DBASS aAlfa1; |QBF aAlfa1;
     aAlfa1 = aAlfa1 + "contagen/";
     aAlfa2 = ("00000" + #asempcon#1) % -105;
     aAlfa3 = ("0" + #asempcon#2) % -101;

     aPath  = aAlfa1 + "fich/" + aAlfa2 + aAlfa3 + "/";
     |PATH_DAT #304 aPath;

     |ABRE #304;
     #304#0 = eaCta;
     |LEE 000#304.=;
     |SI FSalida = 0;
          enSwEstaPlan = 1;
     |FINSI;
     |CIERRA #304;
|FPRC;

|PROCESO CogeVersion;
   PRMNT_enVer = 0;
|FPRC;

-----------------------------------------------------------------------------
CALCULO IBAN
-----------------------------------------------------------------------------

|VARIABLES;
     aCta     = "";
     aIban    = "";
     aLon     = "";
     aPais    = "";
     aNuevo   = "";
     aCar     = "";
     aDC      = "";

     i        = 0;
     iy       = 0;
     nNume    = 0;
     nResto   = 0;

     &eaPais    = "";
     &eaIBAN    = "";
     &eaIBANant = "";
|FIN;

|PROCESO Mod9710;
     aAlfa  = aIban;
     aAlfa2 = aAlfa % 0101;

     |PARA; |SI aAlfa2 = "0"; |HACIENDO;
        aAlfa = aAlfa % 0299; aAlfa2 = aAlfa % 0101;
     |SIGUE;

     aAlfa1 = "";
     |PARA; |SI aAlfa ! ""; |HACIENDO;
        aAlfa2 = aAlfa % 0; nCalc = aAlfa2;

        |SI nCalc > 5;
           aAlfa2 = aAlfa % 0105; aAlfa = aAlfa % 0699;
        |SINO;
           aAlfa2 = aAlfa; aAlfa = "";
        |FINSI;

        aAlfa1 = aAlfa1 + aAlfa2;
        nCalc  = aAlfa1;
        nCalc  = nCalc / 97;
        aAlfa2 = nCalc;
        aAlfa2 = aAlfa2 - ".";
        nCalc  = 0;

       |SI FSalida ! 0;
           nCalc  = FSalida + 98;
           aAlfa2 = aAlfa2 % nCalc;
           aAlfa2 = "0." + aAlfa2; nCalc = aAlfa2;
        |SINO;
        |FINSI;
        nCalc  = (nCalc * 97) ! 0;
        aAlfa1 = nCalc;
     |SIGUE;

     nCalc  = aAlfa1;
     nCalc  = 98 - nCalc;
     aDC    = ("00" + nCalc) % -102;
|FPRC;

|PROCESO CambiaLetra;
     aNuevo = "";
     iy     = aLon; iy = iy + 4;
     |PARA i = 1; |SI i [ iy; |HACIENDO i = i + 1;
          nPos = i * 100 + 1;
          aCar = aIban % nPos;
          |SI aCar ] "0"; |Y aCar [ "9";
               aNuevo = aNuevo + aCar;
          |SINO;
               nNume = &aCar; nNume = nNume - 55; aCar = nNume;
               aNuevo = aNuevo + aCar;
          |FINSI;
     |SIGUE;

     aIban = aNuevo;
|FPRC;

|PROCESO CalcuIBAN;

     |QBF eaIBANant;
     eaIBAN = "";
     aPais  = eaPais % 102; |QBF aPais;
     aCta   = eaCta; |QBF aCta;
     |SI eaIBANant ! "";
         aPais  = eaIBANant % 102; |QBF aPais;
         aCta   = eaIBANant % 530; |QBF aCta;
     |FINSI;

     nNume = aCta;
     aLon  = aCta % A0;
     |SI aLon ! 20; |Y aPais = "ES";
         |ACABA;
     |FINSI;  ||si es Espa¤a siempre 20

     |SI aLon ! 0; |Y aPais ! "";
          aPais = aPais > "";
          aIban = aCta + aPais + "00";
          |HAZ CambiaLetra;
          |HAZ Mod9710;
          eaIBAN = aPais + aDC + aCta;
     |FINSI;
|FPRC;

|PROCESO CheqIBAN;

     |QBF eaIBANant;
     eaIBAN = "";
     aPais  = eaPais % 102; |QBF aPais;
     aCta   = eaCta; |QBF aCta;
     |SI eaIBANant ! "";
         aPais  = eaIBANant % 102; |QBF aPais;
         aCta   = eaIBANant % 530; |QBF aCta;
     |FINSI;

     nNume  = aCta;
     aLon   = aCta % A0;
     |SI aLon ! 20; |Y aPais = "ES";
         |ACABA;
     |FINSI;  ||si es Espa¤a siempre 20

     |SI aLon ! 0; |Y aPais ! "";
          aPais = aPais > "";
          aIban = aCta + aPais + "00";

          |HAZ CambiaLetra;
          |HAZ Mod9710;

          eaIBAN = aPais + aDC + aCta;
     |FINSI;

     |QBF eaIBAN;
|FPRC;
