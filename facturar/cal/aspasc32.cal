          **** PASE DEL HISTORICO DE RECIBOS AL SECUENCIAL ADAPTADO A LA ***
                        ***     NUEVA NORMA 32    ****

|FICHEROS;
  aspasc32 #0;
  ashisrec #1;
  asclient #2;
  ascaisec #5;

  ascabmin #7;
  aslinmin #8;
  asempcon #9;
  asenlcon #10;
  ascontro #11;
  ascontrt #450;
  ascontft #451;
  asconcep #12;
  ascabrem #13;
  aslinrem #14;
  asm00013 #15;
  asw00001 #99;
|FIN;

|VARIABLES;
   NOrden   = 0;
   NumReg   = 0;
   NumRem   = 0;
   NumEfect = 0;
   NumRegis = 0;
   TotalGen = 0;
   Calc     = 0;
   Comodin  = "";
   Cadena   = "";
   CodCedente = "";
   Moneda = "";

   nCalc = 0;

   nPImporte = 0;
   nPEfectos = 0;

   &eaMoneda  = "";
    Handle    = "";
    Ultimo    = "";
    empresa   = "";
    fFech      = @;
    qFech      = "";
    dFech      = "";
    hFech      = "";
    fiche     = "";
    sino      = "";
    sw_existe = 0;
    varalf    = "";
    iva       = 0;
    c_clie    = "";
    fijo      = "";
    conver    = "";
    conver1   = "";
    conver2   = "";
    linea     = 0;
    &efFechaL = @;
    &efFechaO = @;
    &enDesRemesa = 0;
    &enImporte = 0;
|FIN;

|INCLUYE agicon_i;

|| ********************************************************************
|PROCESO MiraVarMoneda;  |TIPO 7;
     |HAZ PintaMoneda;
     #0#22 = eaMoneda;

     |PINTA #0#22;
|FPRC;

|PROCESO sacaano; |TIPO 7;
     fFech = ~;
     qFech = fFech % -104;
     dFech = "01.01." + qFech;
     hFech = "31.12." + qFech;
     #0#4  = dFech;
     #0#5  = hFech;
     |PINTA #0#4;
     |PINTA #0#5;
|FPRC;

|PROCESO El9; |TIPO 0;
  |ABRE #5;
  #5#0 = #0#9;
  |LEE 040#5=;
  |CIERRA #5;
  Ultimo  = #5#9 % -101;
  |SI Ultimo = "/"; |O Ultimo = "*";
      #0#26 = (#5#9 % 129);
      #0#27 = "S";
  |SINO;
      #0#26 = #5#9;
      #0#27 = "N";
  |FINSI;
  |PINTA #0#27;
|FPRC;

||...................................................................................
||***********************************************************
||************ REGISTRO DE CABECERA DE PRESENTADOR **********
||***********************************************************
|PROCESO RegistroInicial;
   Cadena  = "0265";
   Comodin = "  "; Cadena = Cadena + Comodin;
   Comodin = #0#11; Comodin = (Comodin % 0106) + (Comodin % -102);
   Comodin = Comodin - "."; Comodin = Comodin - "."; Cadena = Cadena + Comodin;
   Comodin = NOrden; NOrden = NOrden + 1; Comodin = ("0000" + Comodin) % -104;
   Cadena = Cadena + Comodin;
   Comodin = " " * 35; Cadena = Cadena + Comodin;
   Comodin = ("    " + #0#24) % -104; Cadena = Cadena + Comodin;
   Comodin = ("    " + #0#25) % -104; Cadena = Cadena + Comodin;
   Comodin = " " * 91; Cadena = Cadena + Comodin;
   Cadena = Cadena + &13 + &10;
   Cadena = Handle + " " + Cadena; |FGRABA Cadena;
   NumReg = NumReg + 1;
|FPRC;


|PROCESO RegistroCabRemesa;
   Cadena = "1165";
   Comodin = "  "; Cadena = Cadena + Comodin;
   Comodin = #0#11; Comodin = (Comodin % 0106) + (Comodin % -102);
   Comodin = Comodin - "."; Comodin = Comodin - "."; Cadena = Cadena + Comodin;
   Comodin = NOrden; Comodin = ("0000" + Comodin) % -104; Cadena = Cadena + Comodin;
   Comodin = " " * 12; Cadena = Cadena + Comodin;
   Comodin = ("000000000000000" + #0#23) % -115; Cadena = Cadena + Comodin;
   Cadena = Cadena + "1" + (" " * 21);
   Cadena = Cadena + #5#4 + #5#5 + #5#11 + #5#6;
   Cadena = Cadena + #5#4 + #5#5 + #5#11 + #5#6;
   Cadena = Cadena + #5#4 + #5#5 + #5#11 + #5#6;
   Cadena = Cadena + (" " * 25);
   Cadena = Cadena + &13 + &10;
   Cadena = Handle + " " + Cadena; |FGRABA Cadena;
   NumReg = NumReg + 1;
   nPEfectos = 0;
   nPImporte = 0;
|FPRC;

|PROCESO RegistroIndiv;
   CodCedente = #2#8; |QBF CodCedente;
   CodCedente = (CodCedente + (" " * 15)) % 0115;

   Cadena = "2565  ";
   Comodin = CodCedente;                             Cadena = Cadena + Comodin;
   Comodin = #0#11; Comodin = (Comodin % 0106) + (Comodin % -102);
   Comodin = Comodin - "."; Comodin = Comodin - "."; Cadena = Cadena + Comodin;
   Comodin = NOrden; Comodin = ("0000" + Comodin) % -104; Cadena = Cadena + Comodin;
   Comodin = ("00" + #2#4) % -102;                   Cadena = Cadena + Comodin;
   Comodin = "         ";                            Cadena = Cadena + Comodin;
   Comodin = #14#6; |QBF Comodin; Comodin = (Comodin + (" " * 20)) % 0120;
                                                     Cadena = Cadena + Comodin;
   Comodin = " " * 25;                               Cadena = Cadena + Comodin;
   Calc = #14#7;
   |SI Moneda = "E";
      Calc = Calc * 100;
   |SINO;
      Calc = ((Calc / 166.386) ! 2) * 100;
   |FINSI;
   Comodin = Calc; Comodin = (("0" * 9) + Comodin) % -109;
                                                     Cadena = Cadena + Comodin;
   Comodin = " " * 15;                               Cadena = Cadena + Comodin;
   Comodin = #14#9; Comodin = (Comodin % 0106) + (Comodin % -102);
   Comodin = Comodin - "."; Comodin = Comodin - "."; Cadena = Cadena + Comodin;
   Comodin = " " * 33;                               Cadena = Cadena + Comodin;
   Cadena = Cadena + &13 + &10;
   Cadena = Handle + " " + Cadena; |FGRABA Cadena;

   Cadena = "2665  ";
   Comodin = CodCedente;                             Cadena = Cadena + Comodin;
   Comodin = "  2";                                  Cadena = Cadena + Comodin;
   Comodin = #14#8; Comodin = (Comodin % 0106) + (Comodin % -102);
   Comodin = Comodin - "."; Comodin = Comodin - "."; Cadena = Cadena + Comodin;
   Comodin = "20";                                   Cadena = Cadena + Comodin;
   Comodin = #2#13; Comodin = Comodin % 0110; Cadena = Cadena + Comodin;
   Comodin = #2#14; Comodin = Comodin % 0110; Cadena = Cadena + Comodin;
   Comodin = #48#1; |QBF Comodin;
   Comodin = (Comodin + (" " * 34)) % 0134;          Cadena = Cadena + Comodin;
   Comodin = (#2#1 + (" " * 34)) % 0134;     Cadena = Cadena + Comodin;
   Comodin = #0#26; |QBF Comodin; Comodin = Comodin + " ";
   |SI #0#27 = "S"; Comodin = Comodin + ((("  " + #14#2) % -102) + "/" + (("000000" + #14#3) % -106)); |QBF Comodin; |FINSI;
   |SI #0#29 = "S"; Comodin = Comodin + "(" + #14#9 + ")"; |QBF Comodin; |FINSI;
   Comodin = Comodin % 0130;
   Cadena = Cadena + Comodin; Cadena = Cadena + &13 + &10;
   Cadena = Handle + " " + Cadena; |FGRABA Cadena;

   Cadena = "2765  ";
   Comodin = CodCedente;                             Cadena = Cadena + Comodin;
   Comodin = "  ";                                   Cadena = Cadena + Comodin;
   Comodin = #2#3;
   Comodin = ( Comodin + (" " * 34) ) % 0134;
                                                     Cadena = Cadena + Comodin;
   Comodin = (("00" + #2#4) % -102) + (("000" + #2#5) % -103);
                                                     Cadena = Cadena + Comodin;
   Comodin = #2#6;
   Comodin = ( Comodin + (" " * 20) ) % 0120;
                                                     Cadena = Cadena + Comodin;
   Comodin = (("00" + #2#4) % -102);         Cadena = Cadena + Comodin;
   Comodin = " " * 7;                                Cadena = Cadena + Comodin;
   Comodin = #2#11;
   Comodin = (Comodin + (" " * 15)) % 0115;
                                                     Cadena = Cadena + Comodin;
   Comodin = #2#12;
   Comodin = ( Comodin + (" " * 30)) % 0130;
                                                     Cadena = Cadena + Comodin;
   Comodin = " " * 14;                               Cadena = Cadena + Comodin;
   Cadena = Cadena + &13 + &10;
   Cadena = Handle + " " + Cadena; |FGRABA Cadena;

   NumReg = NumReg + 3;

   nPImporte = nPImporte + #14#7;
   nPEfectos = nPEfectos + 1;
|FPRC;

|PROCESO RegistroFinRemesa;

   Cadena = "7165  ";
   Comodin = #0#11; Comodin = (Comodin % 0106) + (Comodin % -102);
   Comodin = Comodin - "."; Comodin = Comodin - "."; Cadena = Cadena + Comodin;
   Comodin = NOrden; Comodin = ("0000" + Comodin) % -104;
                                                     Cadena = Cadena + Comodin;
   Comodin = " " * 59;                               Cadena = Cadena + Comodin;
   Calc = nPImporte;
   |SI Moneda = "E";
      Calc = Calc * 100;
   |SINO;
      Calc = ((Calc / 166.386) ! 2) * 100;
   |FINSI;
   Comodin = Calc;
   Comodin = (("0" * 10) + Comodin) % -110;
   TotalGen = TotalGen + nPImporte;
                                                     Cadena = Cadena + Comodin;
   Comodin = " " * 46;                               Cadena = Cadena + Comodin;
   nCalc = (nPEfectos * 3) + 2;
   Comodin = ("0000000" + nCalc) % -107;
                                                     Cadena = Cadena + Comodin;
   Comodin = ("000000" + nPEfectos) % -106;        Cadena = Cadena + Comodin;
   Comodin = " " * 6;                                Cadena = Cadena + Comodin;
   Cadena = Cadena + &13 + &10;
   Cadena = Handle + " " + Cadena; |FGRABA Cadena;
   NumEfect = NumEfect + nPEfectos;
   nCalc = (nPEfectos * 3) + 2;
   NumRegis = NumRegis + nCalc;
   NumRem   = NumRem + 1;
   NumReg = NumReg + 1;
   nPImporte = 0;
   nPEfectos = 0;
|FPRC;

|PROCESO RegistroFinal;
   NumReg =NumReg + 1;
   Cadena = "9865";
   Comodin = " " * 71;                               Cadena = Cadena + Comodin;
   Calc = TotalGen;
   |SI Moneda = "E";
       Calc = Calc * 100;
   |SINO;
       Calc = ((Calc / 166.386) ! 2) * 100;
   |FINSI;
   Comodin = ("0000000000" + Calc) % -110;           Cadena = Cadena + Comodin;
   Comodin = " " * 41;                               Cadena = Cadena + Comodin;
   Comodin = ("00000" + NumRem) % -105;              Cadena = Cadena + Comodin;
   NumRegis = NumRegis + 2;
   Comodin = ("0000000" + NumRegis) % -107;          Cadena = Cadena + Comodin;
   Comodin = ("000000" + NumEfect) % -106;           Cadena = Cadena + Comodin;
   Comodin = " " * 6;                                Cadena = Cadena + Comodin;
   Cadena = Cadena + &13 + &10;
   Cadena = Handle + " " + Cadena; |FGRABA Cadena;

   #0#17 = NumEfect; |PINTA #0#17;
   #0#18 = NumReg;   |PINTA #0#18;
   #0#19 = TotalGen; |PINTA #0#19;
|FPRC;

|| *******************************************************************
|| .... prepara el fichero auxiliar de recibos
|| *******************************************************************
|CALCULO LineaRem;
  #1#0 = #14#2;    ||** Lee recibo
  #1#1 = #14#3;
  |LIBERA #1;
  |LEE 140#1=;
  |SI FSalida ! 0; |ACABA; |FINSI;

  #2#0 = #1#3;              ||** Lee fichero de clientes;
  |LEE 040#2=;              ||** Ordena por Banco/Sucursal;
  |SI FSalida ! 0; |ACABA; |FINSI;

  efFechaO = #13#6;
  efFechaL = #0#8;
  enDesRemesa = 1;
  |HAZ eGraba_Min;
  |HAZ RegistroIndiv;
|FCAL;

|DEFBUCLE aslinrem;
  #14#1;
  ;
  #13#0, 000;
  #13#0, 999;
  ;
  NULL, LineaRem;
|FIN;

|PROCESO Remesas;
   |HAZ RegistroCabRemesa;
   |ABRE #1;
   |ABRE #2;
   |BUCLE aslinrem;
   |CIERRA #2;
   |CIERRA #1;
   |HAZ RegistroFinRemesa;

   #13#5 = "X";
   #13#6 = efFechaL;
   |GRABA 020#13a;
   |LIBERA #13;
|FPRC;

|DEFBUCLE ascabrem;
 #13#1;
 ;
 #0#15;
 #0#16;
 be;
 NULL, Remesas;
|FIN;


|| ///////////////////////////////////////////////////////////////////////
|CALCULO pase;|TIPO 3;

  eaMoneda = #0#22;

  Moneda = "E";
  |SI #0#22 = "PESETAS"; Moneda = "P"; |FINSI;

  |DFICE empresa;
  fiche = "wb  " + empresa + "assecrec.seq";  |QBF fiche;

  |FABRE fiche;
  Handle = FSalida;
  |SI FSalida ] 0;
       |HAZ RegistroInicial;
       |BUCLE ascabrem;
       |HAZ RegistroFinal;
   |FINSI;

   FSalida = Handle;
   |FCIERRA FSalida;

   E_sup = 1;
   E_inf = "SNsn";
   sino  = "S";
   sino  = 1769 ? 1;
   sino  = sino > " ";
   |SI sino = "S"; |SUB_EJECUTA_NP "ascopdis"; |FINSI;
|FCAL;
