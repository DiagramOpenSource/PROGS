|FICHEROS;
     gestom04 #0;
     asclient #1;
     asexcont #2;
     gestom01 #3;
     gestom02 #4,MANTE;
     gestow01 #5,MANTE;
|FIN;

|VARIABLES;
     Modo     = 0;
     &Matric  = "";
     &DeDonde = 1;
|FIN;

|PROCESO BuscaAnyo; |TIPO 7;
  Modo = (FEntrada / 100) ! 0;
  |SI Modo ! 1; |ACABA; |FINSI;

  |ABRE #2;
  |LEE 110#2u;
  |SI FSalida ! 0; |ACABA; |FINSI;
  #0#0 = #2#0;
  |CIERRA #2;
|FPRC;

|PROCESO LeeConta; |TIPO 7;
  |SI Modo ! 1; |ACABA; |FINSI;

  |ABRE #2;
  #2#0 = #0#0;
  |LEE 040#2=;
  |SI FSalida ! 0;
      #0#14 = 1; |PINTA #0#14;
      #2#0 = #0#0;
      #2#1 = 0;
      |GRABA 020#2b;
  |SINO;
      #0#14 = #2#1 + 1; |PINTA #0#14;
  |FINSI;
  |CIERRA #2;
|FPRC;

|PROCESO Contador; |TIPO 0;
  |SI Modo ! 1; |ACABA; |FINSI;

  |ABRE #2;
  #2#0 = #0#0;
  |LIBERA #2;
  |LEE 140#2=;
  |SI FSalida = 0;
      #2#1  = #0#14;
      |GRABA 140#2a;
  |FINSI;
  |CIERRA #2;
|FPRC;

|PROCESO Cliente;|TIPO 0;
  Modo = (FEntrada / 100) ! 0;
  |SI Modo ! 1; |ACABA; |FINSI;

  #1#0 = #0#1;
  |LEE 040#1=;
  |SI FSalida ! 0;  |ACABA;  |FINSI;

  #0#2 = #1#2;
  |SI #0#2 = "                                        ";
      #0#2 = #1#3;
  |FINSI;
  |PINTA #0#2;
|FPRC;

|PROCESO HayVehiculos;
  |C_M #0#16, 1, "S";
  |SI #0#15 ! "V";
      |C_M #0#16, 1, "N";  #0#16 = "";  |PINTA #0#16;
      |ACABA;
  |FINSI;

  |ABRE #3;
  #3#0 = #0#1;
  |LEE 040#3=;
  |SI FSalida ! 0;
      |MENAV "     No hay Vehiculos asignado al cliente.";
      |ERROR;
  |FINSI;
  |CIERRA #3;
|FPRC;

|PROCESO Cerrado; |TIPO 0;
  Modo = (FEntrada / 100) ! 0;
  |SI Modo ! 2; |ACABA; |FINSI;

  |SI #0#12 ! "S";  |ACABA;  |FINSI;

  |MENAV "    El expediente ya esta Cerrado. No se puede modificar.";
  |PTEC 807;
|FPRC;

|RUTINA ClaveBaja;
  #4#0 = #3#0;
  #4#1 = "           ";
|FRUT;

|RUTINA ClaveAlta;
  #4#0 = #3#0;
  #4#1 = "zzzzzzzzzzz";
|FRUT;

|PROCESO MiraVehiculo;
  |SI FSalida ! 9;  |ACABA;  |FINSI;

  Matric = "";
  |PUSHV 0501 2380;
  |PINPA #0#3;
  |PINDA #0#3;
  |ENTLINEAL #1#4, 2, 4, 22, 0, ClaveBaja, ClaveAlta;
  |POPV;

  |SI Matric ! "";
      #0#16 = Matric;  |PINTA #0#16;
  |FINSI;
|FPRC;

|PROCESO LeeVehiculo;
  |SI #0#15 ! "V";  |ACABA;  |FINSI;

  |ABRE #4;
  #4#0 = #0#1;
  #4#1 = #0#16;
  |LEE 040#4=;
  |SI FSalida ! 0;
      |MENAV  "     No existe el vehiculo en el Cliente.";
      |ERROR;
  |FINSI;
  |CIERRA #4;
|FPRC;

|PROCESO MensaBusca;  |TIPO 7;
  Modo = (FEntrada / 100) ! 0;
  |SI Modo ! 4;
      |SI Campo = 16;
          |MENSA "      Pulse  <F1> para consultar los vehiculos del Cliente";
      |FINSI;
      |ACABA;
  |FINSI;

  |MENSA "      Pulse <F2> para Buscar por el fichero de Vehiculos";
|FPRC;

|PROCESO Buscador;  |TIPO 0;
  |BLIN 04;

  |SI FSalida ! 10;  |ACABA;  |FINSI;

  |SI Modo ! 4;
      |MENAV "       Solo en Modo Consulta";
      |ACABA;
  |FINSI;

  |PUSHV 0501 2380;
  |ABRE #5;
  #5#0 = #0#1;
  #5#1 = #0#16;
  |LEE 040#5];
  |PINPA #0#5;
  |PINDA #0#5;
  |CONSULTA #1#5;
  |CIERRA #5;
  |POPV;

  |SELECT #3#0;
  #0#16 = #5#1;
  #0#1  = #5#0;
  |LEE 040#0];
  |SI FSalida ! 0; |Y #0#16 ! #5#1; |Y #0#1 ! #5#0;
      |MENAV "       [ No existe expedientes con esas se¤as ]";
      |DDEFECTO #0;
  |FINSI;
  |PINDA #0#0;
  |SELECT #1#0;
  |ERROR;
|FPRC;
