|FICHEROS;
     abxw0017 #917,MANTE;

     abxz0060 #960;
     abxm0505 #505,MANTE;
     abxm0506 #506;

     abom0511 #511;
     abom0001 #101;
     abom0005 #105;
     abom0006 #106;
     abom0007 #107;
     abom0514 #514;
|FIN;

|VARIABLES;
     nRango          = 0;
     nHandle         = 0;
     nLong           = 0;
     nCaracter       = 0;
     nPos            = 0;
     nVentana        = 0;
     nSubVent        = 0;
     nVent           = 0;
     nCampo          = 0;
     nCampo1         = 0;
     nBoton1         = 0;
     nBoton2         = 0;
     nBoton3         = 0;
     nBoton4         = 0;
     nBoton6         = 0;
     nSubBoton1      = 0;
     nDExpediente    = 0;
     nHExpediente    = 0;
     nSubPanta       = 0;
     nModo           = 0;
     nCont           = 0;
     nPasa           = 0;
     nError          = 0;
     nPosi           = 0;
     nLen            = 0;
     nSwEnvio        = 0;
     nSwMensaje      = 0;
     nDirecto        = 0;
     nNume1          = 0;
     nNume2          = 0;
     nTecla          = 0;
     nSwPon          = 0;
     nLinea          = 0;
     nHastaCampo     = 0;

     fFecha          = @;

     aEsc1           = "";
     aEsc2           = "";
     aRetu           = "";
     aTecla          = "";
     aAlfa           = "";
     aHora           = "";
     aNumero         = "";
     aPersona        = "";
     aCadena         = "";
     aComodin        = "";
     aAbre           = "";
     aBase           = "";
     aMas            = "";
     aRutaCorreo     = "";
     aCorreo         = "";
     aEjecuta        = "";
     aEnviado        = "";
     aRef            = "";
     aFic            = "";
     aIncidencia     = "";
     aLen            = "";
     aCaracter       = "";
     aReferencia     = "";
     aFicheroActu    = "";
     aDSCorreo       = "";
     aProxy          = "";
     aUsu            = "";
     aPwd            = "";
     aNombre         = "";
     aAlfa1          = "";
     aDirOri         = "";
     aDirDes         = "";
     aTitulo         = "";
     aTexto          = "";
     aMenLog         = "";
     aOrigen         = "";
     aDestino        = "";
     aLong           = "";
     aRuta           = "";

     {-1}aVent       = "";
         aVent1      = 0;
         aVent2      = 0;
         aVent3      = 0;
         aVent4      = 0;
         aVent5      = "";


     {1}aContiene    = "";

     &enGrid1         = 0;
     &enPanta0        = 0;
     &enSubPanta      = 0;
     &enExped         = 0;
     &enContador      = 0;
     &enLabel1        = -1;
     &enLabel2        = -1;
     &enLabel3        = -1;
     &enCodUbica      = 0;
     &eaRutaDocumento = "";
     &efFechaDocu     = @;
     &eaFicheroEdita  = "";
     &eaFichAdjunto   = "";
     &enCodExpe       = 0;
     &enCodPers       = 0;
     &enCuenta        = 0;
     &enFSalida       = 0;
     &enError         = 0;
     &enCliente       = 0;
     &enExpediente    = 0;
     &efFecha         = @;
     &eaServicio      = "";
     &enPersona       = 0;
     &enLinea         = 0;
|FIN;

|PROCESO AltaAgenda;
     |HORA aAlfa;
     aAlfa1 = aAlfa % 102;  #105#21 = aAlfa1;
     aAlfa1 = aAlfa % 402;  #105#22 = aAlfa1;
     aAlfa1 = aAlfa % 702;  #105#27 = aAlfa1;

     |ABRE #106;
     |DDEFECTO #106;
     #106#0  = #105#0;
     #106#1  = #105#4;
     #106#2  = #105#21;
     #106#3  = #105#22;
     #106#18 = #105#27;
     |LEE 101#106=;
     |SI FSalida ! 0;
         |DDEFECTO #106;
         #106#0  = #105#0;
         #106#1  = #105#4;
         #106#2  = #105#21;
         #106#3  = #105#22;
         #106#18 = #105#27;
         |GRABA 020#106b;
      |FINSI;

      #106#4  = #917#128;
      #106#5  = #917#130;
      #106#6  = #917#131;
      #106#7  = #917#132;
      #106#8  = #105#2;
      #106#9  = #105#3;
      #106#17 = #917#129;
      #106#19 = #917#133;
      #106#10 = #917#28;
      #106#11 = #917#29;
      #106#12 = #917#30;
      #106#13 = #917#31;
      #106#14 = #917#32;
      #106#15 = #917#33;

      |GRABA 020#106a;
      |LIBERA #106;
      |CIERRA #106;

      |SUB_EJECUTA "abxz0056;OUTLOOK";
|FPRC;

|PROCESO Nuevo;
     #105#0 = enExped;
     #105#1 = 999;
     |LEE 000#105];
     |SI FSalida = 0;
         |LEE 000#105a;
     |SINO;
         |LEE 000#105u;
     |FINSI;

     nLinea = 1;
     |SI FSalida = 0;  |Y #105#0 = enExped;
         nLinea = #105#1 + 1;
     |FINSI;

     |DDEFECTO #105;

     #105#0 = enExped;
     #105#1 = nLinea;
     |GRABA 020#105b;

     |DDEFECTO #917;

     |PARA nCampo = 0;  |SI nCampo [ 27;  |HACIENDO nCampo = nCampo + 1;
           #917nCampo = #105nCampo;
     |SIGUE;

     #917#128 = "N";
     #917#129 = " ";
     #917#130 = "01.01.0000";
     #917#131 = "  ";
     #917#132 = "  ";
     #917#133 = "N";

     |ABRE #506;
     #506#0 = #101#13;
     |LEE 040#506=;
     |SI FSalida ! 0;  |DDEFECTO #506;  |FINSI;
     |CIERRA #506;

     #917#5 = #506#2;
     #917#6 = #506#3;

     aAlfa = Usuario % 810;
     aAlfa = (aAlfa + "          ") % 110;

     |ABRE #505;
     |ABRE #511;
     |SELECT #2#511;

     #511#0 = 0;
     #511#1 = aAlfa;
     |LEE 040#511];
     |SI FSalida = 0;  |Y #511#1 = aAlfa;
         #505#0 = #511#0;
         |LEE 040#505=;
         |SI FSalida ! 0;  |DDEFECTO #505;  |FINSI;

         #917#2 = #511#0;
         #917#3 = #505#1;
     |FINSI;
     |CIERRA #505;
     |CIERRA #511;

     |C_M #917#128, 1, "N";
     |C_M #917#129, 1, "N";
     |C_M #917#130, 1, "N";
     |C_M #917#131, 1, "N";
     |C_M #917#132, 1, "N";
     |C_M #917#133, 1, "N";

     |VENTANA 0501, 2799, -1, 17, "Nuevo apunte";
     nSubVent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nSubVent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |PINPA #0#917;
     nSubPanta = FSalida;

     |PINDA #0#917;
     |ENDATOS #1#917;
     |SI FSalida = 0;
         |PARA nCampo = 0;  |SI nCampo [ 27;  |HACIENDO nCampo = nCampo + 1;
               #105nCampo = #917nCampo;
         |SIGUE;

         |SI #105#12 = "S";
             |HAZ AltaAgenda;
         |FINSI;

         |GRABA 020#105a;

         nHastaCampo = 28;
         |PARA nCampo = 127;  |SI nCampo ] 28;  |HACIENDO nCampo = nCampo - 1;
               aAlfa = #917nCampo;  |QBF aAlfa;
               |SI aAlfa ! "";
                   nHastaCampo = nCampo;
                   |SAL;
               |FINSI;
         |SIGUE;

         |ABRE #107;
         |PARA nCampo = 28;  |SI nCampo [ nHastaCampo;  |HACIENDO nCampo = nCampo + 1;
               nCampo1 = nCampo - 27;
               #107#0  = #105#0;
               #107#1  = #105#1;
               #107#2  = nCampo1;
               #107#3  = #917nCampo;
               |GRABA 020#107n;
               |LIBERA #107;
         |SIGUE;
         |CIERRA #107;

     |SINO;
         |BORRA 020#105a;
     |FINSI;
     |LIBERA #105;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nSubVent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA nSubVent;

     |REFRESCACONTROL enGrid1;
|FPRC;

|PROCESO Consulta;
     |LEE 000#105=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |DDEFECTO #917;

     |PARA nCampo = 0;  |SI nCampo [ 27;  |HACIENDO nCampo = nCampo + 1;
           #917nCampo = #105nCampo;
     |SIGUE;

     |PARA nCampo = 1;  |SI nCampo [ 100;  |HACIENDO nCampo = nCampo + 1;
           nCampo1     = nCampo + 27;
           #917nCampo1 = #960nCampo;
     |SIGUE;

     |SI #105#12 = "S";
         |ABRE #106;
         #106#0  = #105#0;
         #106#1  = #105#4;
         #106#2  = #105#21;
         #106#3  = #105#22;
         #106#18 = #105#27;
         |LEE 000#106=;
         |SI FSalida ! 0;  |DDEFECTO #106;  |FINSI;
         |CIERRA #106;

         #917#128 = #106#4;
         #917#129 = #106#17;
         #917#130 = #106#5;
         #917#131 = #106#6;
         #917#132 = #106#7;
     |SINO;
         #917#128 = "N";
         #917#129 = " ";
         #917#130 = "01.01.0000";
         #917#131 = "  ";
         #917#132 = "  ";
         #917#133 = "N";
     |FINSI;

     |VENTANA 0501, 2799, -1, 17, "Consulta apunte";
     nSubVent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nSubVent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |PINPA #0#917;
     nSubPanta = FSalida;

     |PINDA #0#917;

     |LEETECLA aAlfa;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nSubVent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA nSubVent;
|FPRC;

|PROCESO Modificar;
     |LEE 000#105=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |SI #105#19 = "S";  |Y #105#13 = "S";
         |MENAV "     Linea Minutada.";
         |ERROR;
         |ACABA;
     |FINSI;

     |SI #105#20 = "S";  |Y #105#13 = "S";
         |MENAV "     Linea Facturada.";
         |ERROR;
         |ACABA;
     |FINSI;

     |DDEFECTO #917;

     |PARA nCampo = 0;  |SI nCampo [ 27;  |HACIENDO nCampo = nCampo + 1;
           #917nCampo = #105nCampo;
     |SIGUE;

     |PARA nCampo = 1;  |SI nCampo [ 100;  |HACIENDO nCampo = nCampo + 1;
           nCampo1     = nCampo + 27;
           #917nCampo1 = #960nCampo;
     |SIGUE;

     |SI #105#12 = "S";
         |ABRE #106;
         #106#0  = #105#0;
         #106#1  = #105#4;
         #106#2  = #105#21;
         #106#3  = #105#22;
         #106#18 = #105#27;
         |LEE 000#106=;
         |SI FSalida = 0;
             |CONFI "0000NUsted va a Modificar un Seguimiento con Agenda. Desea Continuar. ";
             |SI FSalida ! 0;
                 |CIERRA #106;
                 |ACABA;
             |FINSI;
             |BORRA 020#106a;
             |LIBERA #106;
         |FINSI;
         |CIERRA #106;

         #917#128 = #106#4;
         #917#129 = #106#17;
         #917#130 = #106#5;
         #917#131 = #106#6;
         #917#132 = #106#7;
     |SINO;
         #917#128 = "N";
         #917#129 = " ";
         #917#130 = "01.01.0000";
         #917#131 = "  ";
         #917#132 = "  ";
         #917#133 = "N";
     |FINSI;

     |VENTANA 0501, 2799, -1, 17, "Modificar apunte";
     nSubVent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nSubVent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |LEE 101#105=;

     |PINPA #0#917;
     nSubPanta = FSalida;

     |PINDA #0#917;
     |ENDATOS #2#917;
     |SI FSalida = 0;
         |PARA nCampo = 0;  |SI nCampo [ 27;  |HACIENDO nCampo = nCampo + 1;
               #105nCampo = #917nCampo;
         |SIGUE;

         |SI #105#12 = "S";
             |HAZ AltaAgenda;
         |FINSI;

         |GRABA 020#105a;

         nHastaCampo = 28;
         |PARA nCampo = 127;  |SI nCampo ] 28;  |HACIENDO nCampo = nCampo - 1;
               aAlfa = #917nCampo;  |QBF aAlfa;
               |SI aAlfa ! "";
                   nHastaCampo = nCampo;
                   |SAL;
               |FINSI;
         |SIGUE;

         |ABRE #107;
         |PARA nCampo = 1;  |SI nCampo [ 100;  |HACIENDO nCampo = nCampo + 1;
               #107#0  = #105#0;
               #107#1  = #105#1;
               #107#2  = nCampo;
               |LEE 101#107=;
               |SI FSalida = 0;
                   |BORRA 020#107a;
                   |LIBERA #107;
               |FINSI;
         |SIGUE;

         |PARA nCampo = 28;  |SI nCampo [ nHastaCampo;  |HACIENDO nCampo = nCampo + 1;
               nCampo1 = nCampo - 27;
               #107#0  = #105#0;
               #107#1  = #105#1;
               #107#2  = nCampo1;
               #107#3  = #917nCampo;
               |GRABA 020#107n;
               |LIBERA #107;
         |SIGUE;
         |CIERRA #107;
     |SINO;
         |SI #105#12 = "S";
             |HAZ AltaAgenda;
             |GRABA 020#105a;
         |FINSI;
     |FINSI;

     |LIBERA #105;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nSubVent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA nSubVent;

     |REFRESCACONTROL enGrid1;
|FPRC;

|PROCESO Borrar;
     |LEE 000#105=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |SI #105#19 = "S";  |Y #105#13 = "S";
         |MENAV "     Linea Minutada.";
         |ERROR;
         |ACABA;
     |FINSI;

     |SI #105#20 = "S";  |Y #105#13 = "S";
         |MENAV "     Linea Facturada.";
         |ERROR;
         |ACABA;
     |FINSI;

     |SI #105#12 = "S";
         |ABRE #106;
         #106#0  = #105#0;
         #106#1  = #105#4;
         #106#2  = #105#21;
         #106#3  = #105#22;
         #106#18 = #105#27;
         |LEE 000#106=;
         |SI FSalida = 0;
             |CONFI "0000NUsted va a borrar un Seguimiento con Agenda. Desea Continuar. ";
             |SI FSalida ! 0;
                 |CIERRA #106;
                 |ACABA;
             |FINSI;
        |FINSI;
     |FINSI;

     |CONFI "0000NSeguro que quiere borrar el apunte";
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |SI #105#12 = "S";
         |ABRE #106;
         #106#0  = #105#0;
         #106#1  = #105#4;
         #106#2  = #105#21;
         #106#3  = #105#22;
         #106#18 = #105#27;
         |LEE 101#106=;
         |SI FSalida = 0;
             |BORRA 020#106a;
             |LIBERA #106;
         |FINSI;
         |CIERRA #106;
     |FINSI;

     |ABRE #107;
     |PARA nCampo = 1;  |SI nCampo [ 100;  |HACIENDO nCampo = nCampo + 1;
           #107#0  = #105#0;
           #107#1  = #105#1;
           #107#2  = nCampo;
           |LEE 101#107=;
           |SI FSalida = 0;
               |BORRA 020#107a;
               |LIBERA #107;
           |FINSI;
     |SIGUE;

     |LEE 101#105=;

     |BORRA 020#105a;
     |LIBERA #105;

     |REFRESCACONTROL enGrid1;
|FPRC;

|PROCESO EventoPanta105;
     |SI FSalida = 1;  |O FSalida = 2;
         |LEE 000#105=;
         |SI FSalida = 0;

             |PDEFECTO #960, 1, 105;

             |ESTADO_CONTROL nBoton2, "ENABLE";
             |ESTADO_CONTROL nBoton3, "ENABLE";
             |ESTADO_CONTROL nBoton4, "ENABLE";

             #107#0 = #105#0;
             #107#1 = #105#1;
             |PARA nCampo = 1;  |SI nCampo [ 100;  |HACIENDO nCampo = nCampo + 1;
                   #107#2 = nCampo;
                   |LEE 000#107=;
                   |SI FSalida ! 0;  |SAL;  |FINSI;

                   #960nCampo = #107#3;
             |SIGUE;

             #960#101 = #105#18;
             #960#102 = #105#24;
             #960#103 = #105#25;
             #960#104 = #105#26;

             |PINTA #960#1;
             |PINTA #960#101;
             |PINTA #960#102;
             |PINTA #960#103;
             |PINTA #960#104;
         |SINO;
             |ESTADO_CONTROL nBoton2, "DISABLE";
             |ESTADO_CONTROL nBoton3, "DISABLE";
             |ESTADO_CONTROL nBoton4, "DISABLE";
         |FINSI;
     |FINSI;

     |SI FSalida = 4;
         |HAZ Consulta;
     |FINSI;
|FPRC;

|PROCESO Baja105;
     #105#0  = enExped;
     #105#1  = 1;
|FPRC;

|PROCESO Alta105;
     #105#0  = enExped;
     #105#1  = 999;
|FPRC;

|PROCESO Tipo80Fxz60; |TIPO 80;
     |ABRE #514;
     |LEE 000#514p;
     |SI FSalida ! 0;  |DDEFECTO #514;  |FINSI;
     |CIERRA #514;

     |ABRE #101;
     #101#0 = enExped;
     |LEE 000#101=;
     |SI FSalida ! 0;  |DDEFECTO #101;  |FINSI;
     |CIERRA #101;

     |ABRET #105;
     |ABRET #107;

     FSalida = 3499;
|FPRC;

|PROCESO Tipo90Fxz60; |TIPO 90;
     |CIERRAT #105;
     |CIERRAT #107;
|FPRC;

|PROGRAMA;
     |PINPA #0#960;  enPanta0 = FSalida;
     |PINDA #0#960;

     |CONTROL_SIMPLE 0, "BOTON,{{193}} Nuevo apunte", 3502, 3517, Nuevo;

     |CONTROL_SIMPLE 0, "BOTON,{{193}} Nuevo apunte", 3202, 3317, Nuevo;
     nBoton1  = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,{{197}} Consulta apunte", 3219, 3334, Consulta;
     nBoton2  = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,{{197}} Modificar apunte", 3236, 3351, Modificar;
     nBoton3  = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,{{199}} Borrar apunte", 3253, 3368, Borrar;
     nBoton4  = FSalida;

     |ESTADO_CONTROL nBoton2, "DISABLE";
     |ESTADO_CONTROL nBoton3, "DISABLE";
     |ESTADO_CONTROL nBoton4, "DISABLE";

     |C_V #105#7, 1, "N";
     |C_V #105#8, 1, "N";
     |C_V #105#9, 1, "N";

     nRango = 4 + 8 + 16 + 32 + 128;

     |LINEAL_SIMPLE #1#105, nRango, 0602, 1498, Baja105, Alta105, EventoPanta105;
     enGrid1  = FSalida;

     aEsc1  = &255 + "806";
     aEsc2  = &255 + "807";
     aRetu  = &255 + "802";
     |PARA; |SI;  |HACIENDO;
         FSalida = "::{{001}}Salir," + enGrid1;
         |LEETECLA aTecla;
         |SI aTecla = aEsc1; |SAL;  |FINSI;
         |SI aTecla = aEsc2; |SAL;  |FINSI;
         |SI aTecla = aRetu; |SAL;  |FINSI;
     |SIGUE;

     |PULSATECLA;

     |DESTRUYECONTROL enGrid1;

     |FIN_CONTROL_WINDOWS nBoton1;
     |FIN_CONTROL_WINDOWS nBoton2;
     |FIN_CONTROL_WINDOWS nBoton3;
     |FIN_CONTROL_WINDOWS nBoton4;
|FPRO;
