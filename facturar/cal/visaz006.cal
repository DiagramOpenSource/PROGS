|FICHEROS;
   visaz006 #0;

   visaw006 #10,MANTE;
   visaw007 #11;
   aslinrem #12;
   asclient #13;

   visam009 #9;
   visam100 #100;
|FIN;

|VARIABLES;
   aAlfa   = "";
   aAlfa1  = "";
   aHora   = "";

   nCalc   = 0;
   nHandle = 0;

   aIPServ     = "";
   aServidor   = "";
   aDirOrigen  = "";
   aDirDestino = "";
   aAsunto     = "";
   aFichero    = "";
   aTexto      = "";
   aMensaje     = "";
|FIN;

|RUTINA infw007;
   #visaw007#0 = #visaw006#0;
   #visaw007#1 = "     ";
   #visaw007#2 = 000000;
|FRUT;

|RUTINA supw007;
   #visaw007#0 = #visaw006#0;
   #visaw007#1 = "zzzzz";
   #visaw007#2 = 999999;
|FRUT;

|RUTINA infw006;
   #visaw006#0 = 00000;
|FRUT;

|RUTINA supw006;
   #visaw006#0 = 99999;
|FRUT;

|PROCESO Marca; |TIPO 11;
   |SI FSalida = 10;
      |SI #visaw006#2 = "*";
         #visaw006#2 = " ";
      |SINO;
         #visaw006#2 = "*";
      |FINSI;
      |GRABA 020#visaw006.a;
      |PINTA #visaw006#2;
   |FINSI;

   |SI FSalida = 11;
      |PUSHV 0501 2380;
      |ENTLINEAL #1#visaw007, -2, 4, 22, 1, infw007, supw007;
      |POPV;
   |FINSI;
|FPRC;

|PROCESO Tipo12z0006; |TIPO 12;
|FPRC;

|PROCESO RellenaDatos;
   #asclient#0 = #aslinrem#4;
   |LEE 000#asclient.=;
   |SI FSalida ! 0; |DDEFECTO #asclient; |FINSI;

   #visaw006#0 = #aslinrem#4;
   |LEE 000#visaw006.=;
   |SI FSalida ! 0;
      |DDEFECTO #visaw006;
      #visaw006#0 = #aslinrem#4;
      #visaw006#1 = #aslinrem#5;
      #visaw006#2 = "*";
      #visaw006#3 = #asclient#57;
      |GRABA 020#visaw006.n;
   |FINSI;

   |DDEFECTO #visaw007;
   #visaw007#0 = #aslinrem#4;
   #visaw007#1 = #aslinrem#2;
   #visaw007#2 = #aslinrem#3;
   #visaw007#3 = #aslinrem#7;
   #visaw007#4 = #aslinrem#9;
   |GRABA 020#visaw007.n;
|FPRC;

|DEFBUCLE RellenaDatos;
#aslinrem#1;
;
#visaz006#0, 001;
#visaz006#0, 999;
;
NULL, RellenaDatos;
|FIN;

|PROCESO GrabaFras;
      aAlfa = "                      " + #visaw007#1 + "/";
      aAlfa = aAlfa + (("000000" + #visaw007#2) % -106) + "   ";
      aAlfa = aAlfa + #visaw007#4 + " ";
      nCalc = #visaw007#3; nCalc = nCalc * 100;
      aAlfa1 = nCalc; aAlfa1 = aAlfa1 % -101;
      |SI aAlfa1 = "0";
         aAlfa = aAlfa + (("         " + #visaw007#3) % -109);
         aAlfa = aAlfa + "0 ";
      |SINO;
         aAlfa = aAlfa + (("          " + #visaw007#3) % -110);
         aAlfa = aAlfa + " ";
      |FINSI;
      |XGRABA nHandle, aAlfa;
|FPRC;

|DEFBUCLE GrabaFras;
#visaw007#1;
;
#visaw006#0, "     ", 000000;
#visaw006#0, "zzzzz", 999999;
;
NULL, GrabaFras;
|FIN;

|PROCESO Envia;
   |DFICE aAlfa; |QBF aAlfa;
   aAlfa = aAlfa + "relacion.txt";
   |XABRE "W", aAlfa, nHandle;
   |SI FSalida ] 0;
      aAlfa = " **************************************************************************** ";
      |XGRABA nHandle, aAlfa;
      aAlfa = " *******                RELACION DE RECIBOS REMESADOS                 ******* ";
      |XGRABA nHandle, aAlfa;
      aAlfa = " **************************************************************************** ";
      |XGRABA nHandle, aAlfa;
      aAlfa = "";
      |XGRABA nHandle, aAlfa;
      aAlfa = "    Cliente (" + (("00000" + #visaw006#0) % -105) + ") " + #visaw006#1;
      |XGRABA nHandle, aAlfa;
      aAlfa = "";
      |XGRABA nHandle, aAlfa;
      aAlfa = "                      Serie Factura  Fecha vto. Importe";
      |XGRABA nHandle, aAlfa;
      aAlfa = "                      ------------------------------------";
      |XGRABA nHandle, aAlfa;

      |BUCLE GrabaFras;
      |XCIERRA nHandle;
   |FINSI;

   aIPServ     = #9#2;   |QBF aIPServ;
   aServidor   = #9#4;   |QBF aServidor;

   |SI #visam009#8 = "N";
        aDirOrigen = #9#5;   |QBF aDirOrigen;
   |SINO;
        aDirOrigen = #9#7;
        |QBF aDirOrigen;
        aDirOrigen = aDirOrigen + ":" + #9#5;
        |QBF aDirOrigen;
        aDirOrigen = aDirOrigen + ":" + #9#5;
        |QBF aDirOrigen;
   |FINSI;

   aDirDestino = #visaw006#3;  |QBF aDirDestino;
   aAsunto     = "Relacion de recibos remesados";  |QBF aAsunto;
   |DFICE aFichero; |QBF aFichero;
   aFichero = aFichero + "relacion.txt";
   aTexto      = "$$$$" + aFichero;  |QBF aTexto;

   |SI aServidor ! "";
        |DSCORREO_ENVIA aIPServ, aServidor, aDirOrigen, aDirDestino, aAsunto, aTexto, aFichero;
   |SINO;
        |ERROR10;
   |FINSI;
|FPRC;

|DEFBUCLE Envia;
#visaw006#1;
2;
00000, "*";
99999, "*";
;
NULL, Envia;
|FIN;

|PROGRAMA;
   |CLS;
   |CABEZA "Relacion recibos remesados por cliente";
   |PINPA #0#0; |PINDA #0#0;
   |ENTLINEAL #1#visaw006, -1, 7, 20, 1, infw006, supw006;

   |ENDATOS #1#0;
   |SI FSalida = 0;
      aAlfa = "t1" + Usuario; |NOME_DAT #visaw006#aAlfa#;
      aAlfa = "t2" + Usuario; |NOME_DAT #visaw006#aAlfa#;
      |DELINDEX #visaw006; |ABRE #visaw006;
      |DELINDEX #visaw007; |ABRE #visaw007;
      |ABRE #asclient;
      |BUCLE RellenaDatos;

      |ENTLINEAL #1#visaw006, -1, 4, 20, 1, infw006, supw006;

      aAlfa = "    N" + &191 + " Enviar por correo las relaciones a cada cliente marcado ? ";
      |CONFI aAlfa;
      |SI FSalida = 0;
         |ABRE #visam100;  |LEE 000#visam100.p;  |CIERRA #visam100;
         |ABRE #visam009;
         |DDEFECTO #visam009;
         #visam009#0 = #visam100#1;
         |LEE 000#visam009.=;
         |SI FSalida ! 0;
            aMensaje = "0000No existe Cuenta de Correo definida [" + #visam100#1 + "]";
            |MENAV aMensaje;
         |SINO;
            |BUCLE Envia;
         |FINSI;
         |CIERRA #visam009;
      |FINSI;
      |CIERRA #asclient;
      |CIERRA #visaw006; |DELINDEX #visaw006;
      |CIERRA #visaw007; |DELINDEX #visaw007;
   |FINSI;
|FPRO;
