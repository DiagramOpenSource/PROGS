|FICHEROS;
    visam002 #0;
    asconcli #1;
    visam001 #2;
    asconcep #3;
    asclient #4;

    asconlin #107;
    visam005 #7;
|FIN;

|VARIABLES;
     &enDentro      = 0;
     &eaNombreList  = "";
     &enCuadroIni   = 0;
     &enCuadroFin   = 0;
     &enCampoAncho  = 0;
     &enModoEdita   = 0;
     &enTotalVaria  = 0;
{101}&epPuntEdita   = "";

    nVariable = 0;
    aAlfa = "";
    nModoCli = 0;
    nTecla   = 0;

    aMensaje = "";
    conta = 0;
    nada = "";
    error1 = "    ATENCION no existe el concepto automatico";
    error2 = "    ATENCION fecha erronea";
    traba ="";
    valor = "";
    clie = 0;
    line = 0;
    cero = 0;
    secc = "";
    conc = "";

    secc_cli  = "";
    conc_cli  = "";
    descr_ant = "";
    conver    = "";
    conver1   = "";
    conver2   = "";
    modo      = 0;
|FIN;

|PROCESO nosepued; |TIPO 0;
#0#1 = #2#1;
|SI #0#15 = "S"; |Y #0#10 ! "N";
   |MENAV "     No se puede Modificar la Linea ya esta Facturada.";
   |ERROR;
|FINSI;
|FPRC;

|PROCESO conver_fec; |TIPO 0;
conver1 = #0#17 % 0102;
conver2 = #0#17 % 0402;
conver  = conver1 + conver2;
#0#3 = conver;
|FPRC;

|RUTINA minimo;
#1#0 = #0#1;
#1#1 = "    ";
#1#2 = "  ";
|FRUT;

|RUTINA maximo;
#1#0 = #0#1;
#1#1 = "ееее";
#1#2 = "ее";
|FRUT;

|PROCESO leeconcl;|TIPO 0;    ||si pulsa F1 accede a conceptos / clientes
nTecla = FSalida;

|SI #visam001#28 = "N"; |Y #visam002#4 = "MENS";
    |MENAV "     Mensajeria no autorizada. Pulsar una tecla para continuar";
|FINSI;

|SI nTecla ! 9; |ACABA; |FINSI;

secc_cli = "";
conc_cli = "";

|PUSHV 0501 2380;
|PINPA #0#1;
|PINTA 926, #0#1;

|ENTLINEAL #1#1, 3, 4, 19, 0, minimo, maximo;
|POPV;

|SI secc_cli ! ""; |O conc_cli ! "";
    #0#4 = secc_cli; |PINTA #0#4;
    #0#5 = conc_cli; |PINTA #0#5;
|FINSI;
|FPRC;

|PROCESO Recoge;
     |ABRE #7;
     |ABRE #107;

     |PARA nVariable = 1;  |SI nVariable [ 100;  |HACIENDO nVariable = nVariable + 1;
           |DDEFECTO #7;
           #7#0 = #0#0;
           #7#1 = #0#16;
           #7#2 = #0#2;
           #7#3 = nVariable;
           |LEE 101#7=;
           |SI FSalida = 0;
                |BORRA 020#7a;
           |FINSI;
     |SIGUE;

     |PARA nVariable = 1;  |SI nVariable [ 100;  |HACIENDO nVariable = nVariable + 1;

          |SI nModoCli = 1;
               |DDEFECTO #107;
               #107#0 = 2;
               #107#1 = #0#1;
               #107#2 = #0#4;
               #107#3 = #0#5;
               #107#4 = nVariable;
               |LEE 000#107=;
          |SINO;
               |DDEFECTO #107;
               #107#0 = 1;
               #107#1 = 0;
               #107#2 = #0#4;
               #107#3 = #0#5;
               #107#4 = nVariable;
               |LEE 000#107=;
          |FINSI;

          |DDEFECTO #7;
          #7#0 = #0#0;
          #7#1 = #0#16;
          #7#2 = #0#2;
          #7#3 = nVariable;
          |LEE 101#7=;
          |SI FSalida ! 0;  |GRABA 020#7b;  |FINSI;
          #7#4 = #107#5;
          |GRABA 020#7a;
          |LIBERA #7;
     |SIGUE;

     |PARA nVariable = 100;  |SI nVariable ] 1;  |HACIENDO nVariable = nVariable - 1;
           #7#0 = #0#0;
           #7#1 = #0#16;
           #7#2 = #0#2;
           #7#3 = nVariable;
           |LEE 101#7=;
           |SI FSalida = 0;
                aAlfa = #7#4;  |QBF aAlfa;
                |LIBERA #7;
                |SI aAlfa ! "";  |SAL;  |FINSI;
                |BORRA 020#7a;
           |FINSI;
     |SIGUE;

     |CIERRA #7;
     |CIERRA #107;
|FPRC;

|PROCESO leconcep;|TIPO 0;
modo = (FEntrada / 100) ! 0;

|SI secc = #0#4; |Y conc = #0#5; |Y clie = #0#1; |Y modo ! 1;
    |ACABA;
|FINSI;

|ABRET #1;
|ABRET #3;

#1#0 = #0#1;
#1#1 = #0#4;
#1#2 = #0#5;
|LEE 040#1=;
|SI FSalida = 0;
    #0#6  = #1#6;
    #0#7  = #1#7;
    #0#8  = #1#8;
    #0#9  = #1#9;
    #0#10 = #1#3;
    #0#11 = 1;
    |SI #visam001#5 = 4;
       #0#12 = (#1#10 < #asclient#23) ! 2;
    |SINO;
       |SI #1#3 = "H"; |Y #0#4 ! "MENS";
          #0#12 = (#1#10 < #asclient#23) ! 2;
       |SINO;
          #0#12 = #1#10;
       |FINSI;
    |FINSI;

    #0#14 = #1#5;
    nModoCli = 1;
    |HAZ Recoge;
|SINO;
    #3#0 = #0#4;
    #3#1 = #0#5;
    |LEE 040#3=;
    |SI FSalida = 0;
        |SI #3#3 = "N";
            #0#15   = "S";
        |SINO;
            #0#15   = "N";
        |FINSI;
        |PINTA #0#15;

        |ABRE #4;
        #4#0 = #0#1;
        |LEE 040#4=;
        |SI FSalida = 0;
            |SI #4#22 = 1; #0#12 = #3#4;  #0#14 = #3#5;  |FINSI;
            |SI #4#22 = 2; #0#12 = #3#8;  #0#14 = #3#9;  |FINSI;
            |SI #4#22 = 3; #0#12 = #3#10; #0#14 = #3#11; |FINSI;
            |SI #4#22 = 4; #0#12 = #3#12; #0#14 = #3#13; |FINSI;
            |SI #4#22 = 5; #0#12 = #3#14; #0#14 = #3#15; |FINSI;
        |FINSI;
        |CIERRA #4;

        |SI #visam001#5 = 4;
           #0#12 = (#0#12 < #asclient#23) ! 2;
        |SINO;
           |SI #3#3 = "H"; |Y #0#4 ! "MENS";
              #0#12 = (#0#12 < #asclient#23) ! 2;
           |FINSI;
        |FINSI;
        #0#6  = #3#2;
        #0#7  = #3#16;
        #0#8  = #3#17;
        #0#9  = #3#18;
        #0#10 = #3#3;
        #0#11 = 1;
        nModoCli = 0;
        |HAZ Recoge;
    |SINO;
        #0#5 = 0;
        |MENAV error1;
        |PTEC 808;
        |CIERRAT #1;
        |CIERRAT #3;
        |ACABA;
    |FINSI;
|FINSI;

|SI #0#14 = "U"; #0#13 = #0#12; |FINSI;

#0#13 = #0#12 * #0#11;

|CIERRAT #1;
|CIERRAT #3;

|PINTA #0#6;
|PINTA #0#7;
|PINTA #0#8;
|PINTA #0#9;
|PINTA #0#11;
|PINTA #0#12;
|PINTA #0#13;

secc = #0#4;
conc = #0#5;
clie = #0#1;
|FPRC;

|PROCESO descabre; |TIPO 0;
traba = #0#7;  |QBF traba;
valor = traba % A0;
|SI valor = 0;
    #0#7 = #0#6;
    |PINTA #0#7;
|FINSI;
|SI #0#7 = descr_ant;
    #0#7 = #0#6;
    |PINTA #0#7;
|FINSI;
|FPRC;

|PROCESO descr_ant; |TIPO 7;
descr_ant = #0#6;
|FPRC;

|PROCESO imporref; |TIPO 0;
  #0#13 = #0#12 * #0#11;
    |PINTA #0#13;
|FPRC;

|PROCESO refresca;|TIPO 2;
secc    = #0#4;
conc    = #0#5;
clie    = #0#1;

|SI #0#10 = "H";
    #2#6 = #2#6 +. #0#13;
    |SI #0#15 = "N"; #2#9 = #2#9 +. #0#13; |FINSI;
|FINSI;
|SI #0#10 = "S";
    #2#7 = #2#7 +. #0#13;
    |SI #0#15 = "N"; #2#10 = #2#10 +. #0#13; |FINSI;
|FINSI;
|SI #0#10 = "A";
    #2#8 = #2#8 +. #0#13;
    |SI #0#15 = "N"; #2#11 = #2#11 +. #0#13; |FINSI;
|FINSI;

|PINTA #0#13;
|PINTA #2#6; |PINTA #2#7;  |PINTA #2#8;
|PINTA #2#9; |PINTA #2#10; |PINTA #2#11;
|FPRC;

|PROCESO nomodi; |TIPO 7;
|ABRE #3;
#3#0  = #0#4;
#3#10 = #0#5;
|LEE 040#3=;
|SI FSalida ! 0; |DDEFECTO #3; |FINSI;
|CIERRA #3;

|CAMPO_MODIFICA #0#11, 1, "S";
|CAMPO_MODIFICA #0#12, 1, "S";
|CAMPO_MODIFICA #0#13, 1, "S";

|SI #0#14   = "G"; |CAMPO_MODIFICA #0#11, 1, "N"; |FINSI;

|SI #3#3  = "N";
    #0#11 = 0; |PINTA #0#11; |CAMPO_MODIFICA #0#11, 1, "N";
    #0#12 = 0; |PINTA #0#12; |CAMPO_MODIFICA #0#12, 1, "N";
    #0#13 = 0; |PINTA #0#13; |CAMPO_MODIFICA #0#13, 1, "N";
|FINSI;
|FPRC;

|| ************************************************************************
||                  PROCESOS LLAMADOS DESDE CONCEPTOS CLIENTES
|| ************************************************************************

|PROCESO este_bueno; |TIPO 11;
|SI FSalida ! 9; |ACABA; |FINSI;

|CAMPO_ATRIBUTO #1#1, R, 0, 0; |PINTA #1#1;
|CAMPO_ATRIBUTO #1#2, R, 0, 0; |PINTA #1#2;
|CAMPO_ATRIBUTO #1#6, R, 0, 0; |PINTA #1#6;
|CAMPO_ATRIBUTO #1#3, R, 0, 0; |PINTA #1#3;

|CONFI "2400SEl concepto es el correcto : ";
|SI FSalida = 0;
    secc_cli = #1#1;
    conc_cli = #1#2;
|FINSI;

|CAMPO_ATRIBUTO #1#1, D, 0, 0; |PINTA #1#1;
|CAMPO_ATRIBUTO #1#2, D, 0, 0; |PINTA #1#2;
|CAMPO_ATRIBUTO #1#6, D, 0, 0; |PINTA #1#6;
|CAMPO_ATRIBUTO #1#3, D, 0, 0; |PINTA #1#3;

|SI secc_cli ! ""; |O conc_cli ! ""; |PTEC 807; |FINSI;
|FPRC;

|PROCESO EditaTexto; |TIPO 7;

     enTotalVaria  = 99;
     eaNombreList  = "";
     enCuadroIni   = 1322;
     enCuadroFin   = 2164;
     enCampoAncho  = 40;
     enModoEdita   = 0;

     |ABRE #7;

     |PARA nVariable = 1;  |SI nVariable [ 100;  |HACIENDO nVariable = nVariable + 1;
           IepPuntEdita  = epPuntEdita1 <-;
           IepPuntEdita = IepPuntEdita + (nVariable - 1);

           #7#0 = #0#0;
           #7#1 = #0#16;
           #7#2 = #0#2;
           #7#3 = nVariable;
           |LEE 101#7=;
           |SI FSalida ! 0;
               epPuntEdita  = "";
           |SINO;
               epPuntEdita  = #7#4;
               |BORRA 020#7a;
           |FINSI;
           |LIBERA #7;
     |SIGUE;

     |PUSHV  0501 2380;
     |CUADRO 1221 2265;
     |BLANCO 1322 2164;
     |ATRI S;  |PINTA 1425, "Introduzca el Texto";  |ATRI s;

     |HAZ Edita;
     |POPV;

     |PARA nVariable = 1;  |SI nVariable [ 100;  |HACIENDO nVariable = nVariable + 1;
           IepPuntEdita = epPuntEdita1 <-;
           IepPuntEdita = IepPuntEdita + (nVariable - 1);

           |DDEFECTO #7;
           #7#0 = #0#0;
           #7#1 = #0#16;
           #7#2 = #0#2;
           #7#3 = nVariable;
           |LEE 1010#7=;
           |SI FSalida ! 0;  |GRABA 020#7b;  |FINSI;
           #7#4 = epPuntEdita;
           |GRABA 020#7a;
           |LIBERA #7;

           |SI nVariable = 1;  #0#7 = #7#4;  |FINSI;
           |SI nVariable = 2;  #0#8 = #7#4;  |FINSI;
           |SI nVariable = 3;  #0#9 = #7#4;  |FINSI;
     |SIGUE;

     |PARA nVariable = 100;  |SI nVariable ] 1;  |HACIENDO nVariable = nVariable - 1;
           #7#0 = #0#0;
           #7#1 = #0#16;
           #7#2 = #0#2;
           #7#3 = nVariable;
           |LEE 101#7=;
           |SI FSalida = 0;
                aAlfa = #7#4;  |QBF aAlfa;
                |LIBERA #7;
                |SI aAlfa ! "";  |SAL;  |FINSI;
                |BORRA 020#7a;
           |FINSI;
     |SIGUE;

     |CIERRA #7;

     |PINTA #0#7;
     |PINTA #0#8;
     |PINTA #0#9;
|FPRC;
