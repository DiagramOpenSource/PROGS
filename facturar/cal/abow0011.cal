|FICHEROS;
     abow0011 #901,MANTE;
     abow0012 #902,MANTE;
     abow0013 #603,MANTE;
     referen@ #1000;
|FIN;

|VARIABLES;
     aQueQuiero = "";
     aTitulo    = "";
     aNumCampos = "";
     nNumCampos = 0;
     nCampos    = 0;
     aNombCampo = "";
     aTipoCampo = "";
     aLongCampo = "";
     nSwNuest   = 0;

     aAlfa     = "";
     aCadena   = "";
     aAlfa1    = "";
     aAlfa2    = "";
     aDirBase  = "";
     aFichero  = "";
     aMas      = "";
     nContador = 0;
     nLong     = 0;
     nCaracter = 0;
     nPosi     = 0;
     nPtec     = 0;
     nSwSalir  = 0;
     nCodig    = 0;

     &enMailing     = 0;
     &eaTipoMailing = "";
|FIN;

|PROCESO Tipo7w0013;  |TIPO 7;
    |SI nSwSalir = 0;  |ACABA;  |FINSI;

    aAlfa   = aCadena % 0;
    nLong   = aAlfa;
    |PARA nCaracter = nLong;  |SI nCaracter ] 1;  |HACIENDO nCaracter = nCaracter - 1;
          nPosi  = (100 * nCaracter) + 1;
          aAlfa  = aCadena % nPosi;
          nPtec  = &aAlfa;
          |PTEC nPtec;
    |SIGUE;
    nSwSalir  = 0;
|FPRC;

|PROCESO Tipo11w0013;  |TIPO 11;
     |SI FSalida ! 10;  |ACABA;  |FINSI;

     nCodig = #603#1;
     |LEE 101#603=;
     |SI FSalida = 0;
         |BORRA 020#603a;
     |FINSI;
     |LIBERA #603;

     #603#0 = enMailing;
     #603#1 = nCodig;
     nCodig = 0;
     |LEE 040#603];
     |SI FSalida = 0;  |Y #603#0 = enMailing;
         nCodig = #603#1;
     |SINO;
         |LEE 040#603a;
         |SI FSalida = 0;  |Y #603#0 = enMailing;
             nCodig = #603#1;
         |FINSI;
     |FINSI;

     |SI nCodig = 0;  |PTEC 807;  |ACABA;  |FINSI;

     nSwSalir = 1;
     aCadena  = (("00000") + nCodig) % -105;
     |HAZ Tipo7w0013;
     nSwSalir = 0;
|FPRC;

|RUTINA ClaveBaja603;
     #603#0 = enMailing;
     #603#1 = 0;
|FRUT;

|RUTINA ClaveAlta603;
     #603#0 = enMailing;
     #603#1 = 99999;
|FRUT;

|RUTINA ClaveBaja902;
     #902#0 = #901#0;
     #902#1 = 0;
|FRUT;

|RUTINA ClaveAlta902;
     #902#0 = #901#0;
     #902#1 = 999;
|FRUT;

|PROCESO Tipo0w0012;  |TIPO 0;
     aAlfa = (("00" + #902#0) % -102) + (("000" + #902#1) % -103);

     |ABRE #603;
     #603#0 = enMailing;
     #603#1 = aAlfa;
     |LEE 101#603=;
     |SI FSalida ! 0;
         #603#0 = enMailing;
         #603#1 = aAlfa;
         #603#2 = #901#2;
         #603#3 = #902#2;
         #603#5 = #901#1;
         #603#6 = #902#4;
         #603#7 = #901#3;
         #603#8 = #902#3;
         |GRABA 020#603n;
     |FINSI;
     |LIBERA #603;
     |CIERRA #603;

     aCadena = (("00000") + #603#1) % -105;
     nSwSalir = 1;

     |PTEC 807;
     |PTEC 816;
     |ENTLINEAL #1#603, -2, 4, 22, 0, ClaveBaja603, ClaveAlta603;

     nSwSalir = 0;
|FPRC;

|PROCESO Tipo11w0011;  |TIPO 11;
     |SI FSalida ! 10;  |ACABA;  |FINSI;
     |ENTLINEAL #1#603, -2, 4, 22, 0, ClaveBaja603, ClaveAlta603;
     |ERROR;
|FPRC;

|PROCESO Tipo0w0011;  |TIPO 0;
     |SI FSalida ! 0;  |ACABA;  |FINSI;
     |ENTLINEAL #1#902, -2, 4, 22, 0, ClaveBaja902, ClaveAlta902;
     |ERROR;
|FPRC;

|PROCESO Tipo7w0011;  |TIPO 7;
     |ENTLINEAL #1#902, -2, 7, 22, 0, ClaveBaja902, ClaveAlta902;
     |ENTLINEAL #1#603, -2, 7, 22, 0, ClaveBaja603, ClaveAlta603;
|FPRC;

|RUTINA ClaveBaja901;
     #901#0 = 1;
|FRUT;

|RUTINA ClaveAlta901;
     #901#0 = 99;
|FRUT;

|PROCESO CargaTemporal;
     aMas  = aDirBase + "def/" + aFichero;
     |CARGA_DEF aMas, referen@;
     |SI FSalida ! 0;
         |MENAV  "         Error en cargar el Def ... Proceso Abortado.";
         |ACABA;
     |FINSI;

     nContador  = nContador + 1;
     aQueQuiero = "|TITULO";
     aTitulo    = #referen@#aQueQuiero#;  |QBF aTitulo;

     |SI aFichero ! "abom0505";
         |SI nSwNuest = 1;  aTitulo = aTitulo + " Nuestros";    |FINSI;
         |SI nSwNuest = 2;  aTitulo = aTitulo + " Contrarios";  |FINSI;
     |SINO;
         |SI nSwNuest = 1;  aTitulo = "Director Expediente";    |FINSI;
         |SI nSwNuest = 2;  aTitulo = "Prof.Apertura Expe.";    |FINSI;
     |FINSI;

     #901#0    = nContador;
     #901#1    = aFichero;
     #901#2    = aTitulo;
     #901#3    = nSwNuest;
     |GRABA 020#901n;
     |LIBERA #901;

     aQueQuiero = "|NC";
     aNumCampos = #referen@#aQueQuiero#;
     nNumCampos = aNumCampos;
     nNumCampos = nNumCampos - 1;
     |PARA nCampos = 0;  |SI nCampos [ nNumCampos;  |HACIENDO nCampos = nCampos + 1;
           aQueQuiero = "|C" + (("000" + nCampos) % -103) + "NOMBRE";
           aNombCampo = #referen@#aQueQuiero#;

           aQueQuiero = "|C" + (("000" + nCampos) % -103) + "TIPO";
           aTipoCampo = #referen@#aQueQuiero#;

           aQueQuiero = "|C" + (("000" + nCampos) % -103) + "LONGITUD";
           aLongCampo = #referen@#aQueQuiero#;

           #902#0 = #901#0;
           #902#1 = nCampos;
           #902#2 = aNombCampo;
           #902#3 = aTipoCampo;
           #902#4 = aLongCampo;
           |GRABA 020#902n;
           |LIBERA #902;
     |SIGUE;

     |DESCARGA_DEF #referen@;
|FPRC;

|PROGRAMA;
     aFichero = ("a7" + Usuario + "zzzzzzzz") % 108; |NOME_DAT #901 aFichero;
     |ABRE #901;  |CIERRA #901;  |DELINDEX #901;

     aFichero = ("a8" + Usuario + "zzzzzzzz") % 108; |NOME_DAT #902 aFichero;
     |ABRE #902;  |CIERRA #902;  |DELINDEX #902;

     |DBASE aDirBase;

     nContador = 0;
     |ABRE #901;
     |ABRE #902;


     |SI eaTipoMailing = "E";
         nSwNuest = 0;   aFichero = "abom0001";  |HAZ CargaTemporal;
                         aFichero = "asclient";  |HAZ CargaTemporal;
                         aFichero = "abom0501";  |HAZ CargaTemporal;
         nSwNuest = 1;   aFichero = "abom0502";  |HAZ CargaTemporal;
         nSwNuest = 2;   aFichero = "abom0502";  |HAZ CargaTemporal;
         nSwNuest = 1;   aFichero = "abom0503";  |HAZ CargaTemporal;
         nSwNuest = 2;   aFichero = "abom0503";  |HAZ CargaTemporal;
         nSwNuest = 1;   aFichero = "abom0505";  |HAZ CargaTemporal;
         nSwNuest = 2;   aFichero = "abom0505";  |HAZ CargaTemporal;
         nSwNuest = 0;   aFichero = "abom0504";  |HAZ CargaTemporal;
                         aFichero = "abom0506";  |HAZ CargaTemporal;
                         aFichero = "asempres";  |HAZ CargaTemporal;
                         aFichero = "aboy0004";  |HAZ CargaTemporal;
     |SINO;
                         nContador = 1;
                         aFichero  = "asclient";  |HAZ CargaTemporal;
     |FINSI;

     |CIERRA #901;
     |CIERRA #902;

     |ENTLINEAL #1#901, -1, 4, 12, 1, ClaveBaja901, ClaveAlta901;

     |ABRE #901;  |CIERRA #901;  |DELINDEX #901;
     |ABRE #902;  |CIERRA #902;  |DELINDEX #902;
|FPRO;
