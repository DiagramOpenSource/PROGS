|FICHEROS;
   asmanmov #0;
   asempcon #1;

   :/contagen/def/canempre #330;

   :/contagen/def/calibros #303;
   :/contagen/def/caivases #305;
   :/contagen/def/cacuenta #304;
   :/contagen/def/cacuiv2c #306;
   :/contagen/def/cacuiv2l #307;
   :/contagen/def/caivaasi #320;

   :/modelos/def/dsmom030 #130;

|FIN;

|VARIABLES;
   ModoEntrada = 0;

   nEmpDef     = 0;
   nPerDef     = 0;

   nSwError    = 0;
   nDEmpresa   = 0;
   nHEmpresa   = 0;
   nDPeriodo   = 0;
   nHPeriodo   = 0;
   nInkey      = 0;

   aAlfa1      = "";
   aAlfa2      = "";
   aAlfa3      = "";
   nNume1      = 0;
   aPath       = "";
   aPant       = "";
   elsw1       = 0;
   nPara1      = 0;

   eaCtaIVA   = "";
   aDesCon    = "";
   aIVASN     = "";
   aIRPFSN    = "";
   nConcepto  = 0;
|FIN;

|INCLUYE i_varcof;

|| :::::::::::::::::::::::::::::::::::::::::::::::::::::::::::

|RUTINA nInf2M;
 |SI FSalida = "POSICION";
     #304#2 = #0Campo;
 |SINO;
     #304#2 = "";
 |FINSI;
 #304#3 = "S";
|FRUT;

|RUTINA nSup2M;
 #304#0 = "zzzzzzzzzzz";
 #304#3 = "S";
|FRUT;

|CALCULO MiraCueM7; |TIPO 7;
   |C_M #0Campo, 0, aAlfa1;
   |SI aAlfa1 = "N"; |ACABA; |FINSI;
   |SI #0#23 = 2;
       |MENSA "    Contrapartida en el Registro de IVA. (=) La misma cuenta del Movimiento";
   |SINO;
       |MENSA "    Contrapartida en el Registro de IVA ";
   |FINSI;
|FCAL;

|PROCESO LaConsCueM;
  |ABRE #304;
  |CONSULTA_F_CLAVE #1#304, nInf2M, nSup2M;
  #0Campo = #304#0; |PINTA #0Campo;
  |CIERRA #304;
|FPRC;

|CALCULO MiraCueM; |TIPO 0;
   nInkey = FSalida;
   |SI nInkey  = 2; |ACABA; |FINSI;

   |C_M #0Campo, 0, aAlfa1;
   |SI aAlfa1 = "N"; |ACABA; |FINSI;

   eaCuenta = #0Campo; |QBF eaCuenta;
   |SI eaCuenta = ""; |Y nInkey ! 10;
       ||     |MENAV "    Introduzca una Cuenta";
       ||     |ERROR;
       |ACABA;
   |FINSI;

   |SI nInkey = 10;
       |HAZ LaConsCueM;
   |FINSI;

   eaCuenta = #0Campo; |QBF eaCuenta;
   elsw1 = 0;
   |SI Campo = 3;
       |HAZ asterisco;
   |FINSI;
   |SI Campo = 13; |Y #0#23 = 2;
       |SI eaCuenta = "="; |ACABA; |FINSI;
   |FINSI;
   |SI elsw1 = 0;
       emCta = 0;
       |HAZ LeeCuenta;
       |SI eswCta = 1;
           |ERROR;
           |CONFI "    SError.! No existe Cuenta. Desea darla de alta ";
           |SI FSalida = 0;
               |SUB_EJECUTA_NP "aszc0001";
               #0Campo = eaCuenta;
               |PINTA #0Campo;
           |FINSI;
       |SINO;
           |SI eswCta = 2;
               |MENAV "0000Esta Cuenta NO tiene Pase Directo.";
               |ERROR;
           |FINSI;
       |FINSI;
   |FINSI;
|FCAL;

|CALCULO CuentaM; |TIPO 6;
     |C_M #0Campo,0,aAlfa1;
     |SI aAlfa1 = "N"; |ACABA; |FINSI;

     eaCuenta = #0Campo;
     elsw1 = 0;
     |SI Campo = 3;
         |HAZ asterisco;
     |FINSI;
     |SI elsw1 ! 0;  |ACABA;  |FINSI;

     emCta = 1;        || Dar mensajes de error
     salidas  = FSalida;
     |HAZ FiltraPuntos;
     #0Campo = eaCuenta;
     |PINTA #0Campo;
     |HAZ SacaNivel;
|FCAL;

|CALCULO LosExp7; |TIPO 7;

 ModoEntrada = (FEntrada / 100) ! 0;

 |SI ModoEntrada = 1;
     aAlfa1 = "S";
     |SI #0#2 = "G"; aAlfa1 = "N"; |FINSI;
     #0#4 = aAlfa1; |PINTA #0#4;
 |FINSI;
|FCAL;

|CALCULO LosExp0; |TIPO 0;
 aAlfa1 = "S"; |SI #0#4 = "S"; aAlfa1 = "N"; |FINSI;
 || ... C_M #0#5,1,aAlfa1;
|FCAL;

|CALCULO CreaIVA; |TIPO 0;

 ModoEntrada = (FEntrada / 100) ! 0;

 aAlfa1 = #0#5;
 |SI #0#5 = "S";  aAlfa2 = "N"; |SINO; aAlfa2 = "S"; |FINSI;

 |C_M #0#6,1,  aAlfa1;
 |C_M #0#7,1,  aAlfa1;
 |C_M #0#8,1,  aAlfa1;

 |C_M #0#23,1, aAlfa1;

 |C_M #0#10,1, aAlfa1;

 |C_M #0#11,1, aAlfa1;
 |C_M #0#12,1, aAlfa1;
 |C_M #0#13,1, aAlfa1;

 |C_M #0#14,1, aAlfa1;
 |C_M #0#15,1, aAlfa1;

|| |C_M #0#17,1, aAlfa1;

 |C_M #0#18,1, aAlfa1;
 |C_M #0#19,1, aAlfa1;
 |C_M #0#20,1, aAlfa1;
 |C_M #0#21,1, aAlfa1;
 |C_M #0#22,1, aAlfa1;

 |SI aAlfa1 = "N";
     |PDEFECTO #0, 6,24;
     |PINDA #0#0;
 |SINO;
     |SI ModoEntrada = 1; |O Contenido ! #0Campo;
         #0#6 = "S";
         ||   |SI #0#2 = "I"; #0#6 = "R"; |FINSI;
         |HAZ LosDefectos;
     |FINSI;
 |FINSI;

 |PINTA #0#6;  |PINTA #0#7;
 |PINTA #0#8;
 |C_M #0#25, 1, aAlfa2;
 |SI aAlfa2 = "N";
     #0#25 = 1;   |PINTA #0#25;
 |FINSI;
|FCAL;

|CALCULO EsteImporta; |TIPO 0;

  ModoEntrada = (FEntrada / 100) ! 0;
  |C_M #0Campo,0,aAlfa1;
  |SI aAlfa1 = "N"; |ACABA; |FINSI;

  aAlfa1 = "S";
  |SI #0#23 = 2; aAlfa1 = "N"; |FINSI;

  |C_M #0#14, 1, aAlfa1;
||  |C_M #0#17, 1, aAlfa1;
  |C_M #0#18, 1, aAlfa1;
  |C_M #0#19, 1, aAlfa1;
  |C_M #0#20, 1, aAlfa1;
  |C_M #0#21, 1, aAlfa1;
  |C_M #0#22, 1, aAlfa1;

  |SI aAlfa1 = "N";
      |PDEFECTO #0, 14,23;
      |SI ModoEntrada = 1;
          #0#13 = "=";
      |FINSI;
  |FINSI;
  |PINDA #0#0;
|FCAL;

|CALCULO RepOSop; |TIPO 0;
  |C_M #0Campo,0,aAlfa1;
  |SI aAlfa1 = "N"; |ACABA; |FINSI;

/*
  |SI #0#2 = "I"; |Y  #0#6 = "S";
      |CONFI "    NConcepto de Ingreso, Tipo de IVA Soportado, Continuar ?";
      |SI FSalida ! 0;
          |ERROR;
      |FINSI;
  |FINSI;
  |SI #0#2 = "G"; |Y  #0#6 = "R";
      |CONFI "    NConcepto de Gasto, Tipo de IVA Repercutido, Continuar ?";
      |SI FSalida ! 0;
          |ERROR;
      |FINSI;
  |FINSI;
*/

  |SI #0#6 = " "; |ERROR;  |FINSI;
|FCAL;

|CALCULO cheqLibro; |TIPO 0;
   |C_M #0Campo,0,aAlfa1;
   |SI aAlfa1 = "N"; |ACABA; |FINSI;

   |SI #0Campo = 0; |ERROR; |ACABA; |FINSI;

   |ABRE #303;
   #303#0 = #0Campo;
   |LEE 010#303=;
   |SI FSalida ! 0;
       |MENAV "      No existe libro";
       |CIERRA #303;
       |ERROR;
       |ACABA;
   |FINSI;
   |CIERRA #303;

   |SI #303#2 ! #0#6;
       |MENAV "     Libro de IVA No Correcto";
       |ERROR;
   |FINSI;
|FCAL;

|CALCULO LaSerie; |TIPO 7;
   |SI #320#44 = "N"; |C_M #0#8,1,"N"; |FINSI;
|FCAL;

|CALCULO TrasConcep; |TIPO 0;
  |C_M #0Campo,0,aAlfa1;
  |SI aAlfa1 = "N"; |ACABA; |FINSI;

  ModoEntrada = (FEntrada / 100) ! 0;

  nConcepto = #0Campo;
  |HAZ LeeConcepto;
  |SI eswLect = 1;
      |MENAV "     No existe Concepto Automatico ";
      |ERROR;
      |ACABA;
  |FINSI;

  |SI Campo = 11;
      |SI #0#23 = 1; |Y aIVASN = "N";
          |MENAV "     Atencion. Este Concepto no es de IVA";
      |FINSI;

      #0#12 = aDesCon; |PINTA #0#12;

      |SI ModoEntrada = 1; |O Contenido ! #0Campo;
          #0#13 = eaCtaIVA; |PINTA #0#13;
      |FINSI;
  |FINSI;
|FCAL;

|CALCULO TrasConcepIRPF; |TIPO 0;
  |C_M #0Campo,0,aAlfa1;
  |SI aAlfa1 = "N"; |ACABA; |FINSI;
  ModoEntrada = (FEntrada / 100) ! 0;

  |SI #0Campo = 0;
      |C_M #0#19,1,"N"; #0#19 = ""; |PINTA #0#19;
      |C_M #0#20,1,"N"; #0#20 = ""; |PINTA #0#20;
      |C_M #0#21,1,"N"; #0#21 = ""; |PINTA #0#21;
      |C_M #0#22,1,"N"; #0#22 = 0;  |PINTA #0#22;
      |ACABA;
  |FINSI;
  |C_M #0#19,1,"S"; |C_M #0#20,1,"S";
  |C_M #0#21,1,"S"; |C_M #0#22,1,"S";

  nConcepto = #0#18;
  |HAZ LeeConcepto;
  |SI eswLect = 1;
      |MENAV "     No existe Concepto Automatico ";
      |ERROR;
      |ACABA;
  |FINSI;
  |SI aIRPFSN = "N";
      |MENAV "     Atencion. Este Concepto no es de IRPF";
  |FINSI;

  #0#19 = aDesCon; |PINTA #0#19;
  |SI ModoEntrada = 1; |O Contenido ! #0Campo;
      #0#20 = eaCtaIVA; |PINTA #0#20;
  |FINSI;
|FCAL;

|CALCULO PonAlFinal; |TIPO 7;
  |C_M #0Campo,0,aAlfa1;
  |SI aAlfa1 = "N"; |ACABA; |FINSI;

  |PTEC 816;
|FCAL;

|| -------------------------------------
|RUTINA inf2;
   #307#12 = enEjer;
   #307#0  = #0#6;
   #307#1  = 01;
|FRUT;

|RUTINA sup2;
   #307#12 = enEjer;
   #307#0  = #0#6;
   #307#1  = 99;
|FRUT;

|CALCULO Eltp100; |TIPO 0;
  nInkey = FSalida;
  |C_M #0Campo,0,aAlfa1;
  |SI aAlfa1 = "N"; |ACABA; |FINSI;

  |SI nInkey = 10;

      |ABRE #307;
      |CONSULTA_F_CLAVE #1#307,inf2,sup2;
      #0Campo = #307#1; |PINTA #0Campo;
      |ERROR;
      |CIERRA #307;
   |FINSI;

   |HAZ LeeTipoIVA;
   |PINTA #0#14;
   |PINTA #0#15;
   |PINTA #0#16;
 ||  |PINTA #0#17;
|FCAL;

|| *****************************************
|CALCULO MiraBienm; |TIPO 3;
 eaCuenta = #0#3;
 |HAZ asterisco;
 |QBF eaCuenta; eswCta = 0;
 |SI eaCuenta ! ""; |Y elsw1 = 0;
    |HAZ LeeCuenta;
    |SI eswCta = 2; eswCta = 1; |FINSI;
 |FINSI;
 |SI eswCta = 1;
     |MENAV "    Error. Introduzca una Cuenta Contable o Grupo de Cuentas";
     |ERROR;
 |FINSI;
|FCAL;

|| ....................... Carga defs de Contabilidad

|CALCULO Tipo8m; |TIPO 80;
   |ABRE #1;
   |DDEFECTO #1;
   |LEE 001#1p;
   |CIERRA #1;
   nEmpDef = #1#1;
   nPerDef = #1#2;

   enSwSinMen = 1;
   enEmpresa  = nEmpDef;
   enPerConta = nPerDef;
   |HAZ LeeEmpConta;
   |SI swerror = 1;
       |MENAV "    Error. La Empresa Contable Informada es Incorrecta. Modificar";
       |PTEC 807;
       |ACABA;
   |FINSI;
   |DBASS aAlfa1; |QBF aAlfa1;
   aAlfa1 = aAlfa1 + "contagen/";
   aAlfa2 = enEmpresa;  |QBF aAlfa2; aAlfa2 = ("00000" + aAlfa2) % -105;
   aAlfa3 = enPerConta; |QBF aAlfa3; aAlfa3 = ("0" + aAlfa3) % -101;

   aPath  = aAlfa1 + "fich/" + aAlfa2 + aAlfa3 + "/";
   aPant  = aAlfa1 + "pan/";

   |PATH_DAT #303 aPath;
   |PATH_DAT #304 aPath;
   |PATH_DAT #305 aPath;
   |PATH_DAT #320 aPath;

   |ABRE #320;
   |DDEFECTO #320;
   |LEE 001#320p;
   |CIERRA #320;
|FCAL;

|PROCESO asterisco;
  elsw1 = 0;
  |PARA nPara1 = 1; |SI nPara1 [ 12; |HACIENDO nPara1 = nPara1 + 1;
        nNume1 = (nPara1 * 100) + 1;
        aAlfa1 = eaCuenta % nNume1;
        |SI aAlfa1 = "*";
            elsw1 = nPara1;
            |SAL;
        |FINSI;
  |SIGUE;
|FPRC;

|| **********************************************************
|PROCESO LosDefectos;
 |ABRE #320;
 |DDEFECTO #320;
 |LEE 001#320p;
 |CIERRA #320;

 |SI #0#6 = "R";
     #0#7  = #320#3;
     #0#8  = #320#63;
     #0#9  = #320#23;
     #0#10 = #320#71;
     #0#11 = #320#5;
     #0#12 = #320#7;
     #0#14 = #320#9;
 |FINSI;
 |SI #0#6 = "S";
     #0#7  = #320#29;
     #0#8  = #320#65;
     #0#9  = #320#61;
     #0#10 = #320#21;
     #0#11 = #320#31;
     #0#12 = #320#33;
     #0#14 = #320#11;
 |FINSI;
 nConcepto = #0#11;
 |HAZ LeeConcepto;
 #0#12 = aDesCon;
 #0#13 = eaCtaIVA;
 |HAZ LeeTipoIVA;
|FPRC;

|PROCESO LeeConcepto;
   eaCtaIVA = "";
   eswLect = 0;
   |ABRE #dsmom030;
   #dsmom030#0 = nConcepto;
   |LEE 000#dsmom030.=;
   |SI FSalida ! 0;
       eswLect = 1;
       |CIERRA #dsmom030;
       |ACABA;
   |FINSI;
   |CIERRA #dsmom030;
   aDesCon = #dsmom030#1;
   aIVASN  = #dsmom030#4;
   aIRPFSN = #dsmom030#5;
   |SI #0#6 = "R"; eaCtaIVA = #dsmom030#20; |FINSI;
   |SI #0#6 = "S"; eaCtaIVA = #dsmom030#22; |FINSI;
|FPRC;

|PROCESO LeeTipoIVA;

  |ABRE #306;
  #306#20 = enEjer;
  #306#0  = #0#6;
  #306#21 = "01.01.2000";
  |LEE 010#306];
  |SI FSalida ! 0;
      |DDEFECTO #306;
      #306#20 = enEjer;
      #306#0  = #0#6;
      #306#21 = "01.01.2000";
      |GRABA 020#306b;
  |FINSI;
  |CIERRA #306;

  |ABRE #307;
  #307#12 = enEjer;
  #307#0  = #0#6;
  #307#1  = #0#14;
  #307#13 = "01.01.2000";
  |LEE 010#307=;
  |SI FSalida ! 0;
      |MENAV "    Control de cuentas no definido";
      |ERROR;
      |CIERRA #307;
      |ACABA;
  |FINSI;
  |CIERRA #307;

  |SI #306#2 = "N";            || Desglose IVA y Rec
      #0#16 = #306#6;
  |SINO;
      #0#16 = #307#3;
  |FINSI;
  #0#15 = #307#2;
|FPRC;
