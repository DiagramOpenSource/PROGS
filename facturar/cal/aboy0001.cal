|FICHEROS;
     aboy0001 #0;
     abom0001 #1;
     abom0002 #2;
     abom0003 #3;
     abom0004 #4;
     abom0005 #5;
     abom0006 #6;
     abom0007 #7;
|FIN;

|VARIABLES;
      nCampo       = 0;
      nSwTitulo    = 0;
      nSwCliente   = 0;

      aAlfa        = "";

     &enExped      = 0;
     &enVaralf     = "";
     &enSw         = 0;
|FIN;

|PROCESO Mayus;
     #0Campo = #0Campo > #0Campo;
     |PINTA #0Campo;
|FPRC;

|PROCESO ImpreSegui;
     enVaralf  = "                   " + #7#3;
     |PRINT;
|FPRC;

|DEFBUCLE abom0007;
     #7#1;
     ;
     #5#0, #5#1, 02;
     #5#0, #5#1, 99;
     ;
     NULL, ImpreSegui;
|FIN;

|PROCESO Seguimientos;
     |SI #0#21 = "N";
         |SI #5#11 = "S";  |ACABA;  |FINSI;
     |FINSI;

     |SI nSwTitulo = 0;
         enVaralf  = "";                        |PRINT;
         enVaralf  = "{{N}}SEGUIMIENTOS{{n}}";  |PRINT;
         enVaralf  = "";                        |PRINT;

         enVaralf  = "PD FECHA      SERV ACTUACION PROFESIONAL                              T.R M F";
         |PRINT;

         enVaralf  = "-----------------------------------------------------------------------------";
         |PRINT;

         nSwTitulo = 1;
     |FINSI;

     enVaralf  = (("00" + #5#2) % -102) + " " + #5#4 + " " + #5#5 + " " + #5#7 + (("   " + #5#16) % -103) + "  " + #5#19 + " " + #5#20;
     |PRINT;

     |BUCLE abom0007;
|FPRC;

|DEFBUCLE abom0005;
     #5#1;
     ;
     #1#0, 000;
     #1#0, 999;
     ;
     NULL, Seguimientos;
|FIN;

|PROCESO Clientes;
     |SI nSwTitulo = 0;
         enVaralf  = "{{N}}OTROS CLIENTES{{n}}";  |PRINT;
         enVaralf  = "";                      |PRINT;
         nSwTitulo = 1;
     |FINSI;

     enVaralf  = #3#2;
     |PRINT;
     nSwCliente = 1;
|FPRC;

|DEFBUCLE abom0003;
     #3#1;
     ;
     #1#0, 00000;
     #1#0, 99999;
     ;
     NULL, Clientes;
|FIN;

|PROCESO Contrarios;
     |SI nSwTitulo = 0;
         |SI nSwCliente = 1;
             enVaralf  = "";                      |PRINT;
         |FINSI;
         enVaralf  = "{{N}}CONTRARIOS{{n}}";  |PRINT;
         enVaralf  = "";                      |PRINT;
         nSwTitulo = 1;
     |FINSI;

     enVaralf  = #4#2;
     |PRINT;
|FPRC;

|DEFBUCLE abom0004;
     #4#1;
     ;
     #1#0, 000000;
     #1#0, 999999;
     ;
     NULL, Contrarios;
|FIN;

|PROCESO Agenda;
     |SI nSwTitulo = 0;
         enVaralf  = "";               |PRINT;
         enVaralf  = "{{N}}AGENDA{{n}}";  |PRINT;
         enVaralf  = "";               |PRINT;

         enVaralf  = "FECHA ANOTACION  A FECHA AVISO      PD TEXTO AVISO";
         |PRINT;

         enVaralf  = "-----------------------------------------------------------------------------";
         |PRINT;

         nSwTitulo = 1;
     |FINSI;

     enVaralf  = #6#1 + " " + (("00" + #6#2) % -102) + ":" + (("00" + #6#3) % -102) + " " + #6#4 + " " + #6#5 + " ";
     enVaralf  = enVaralf + (("00" + #6#6) % -102) + ":" + (("00" + #6#7) % -102) + " " + (("00" + #6#8) % -102) + " " + #6#10;
     |PRINT;

     aAlfa = #6#11;  |QBT aAlfa;
     |SI aAlfa ! "";
         enVaralf  = "                                       " + #6#11;
         |PRINT;
     |FINSI;

     aAlfa = #6#12;  |QBT aAlfa;
     |SI aAlfa ! "";
         enVaralf  = "                                       " + #6#12;
         |PRINT;
     |FINSI;

     aAlfa = #6#13;  |QBT aAlfa;
     |SI aAlfa ! "";
         enVaralf  = "                                       " + #6#13;
         |PRINT;
     |FINSI;

     aAlfa = #6#14;  |QBT aAlfa;
     |SI aAlfa ! "";
         enVaralf  = "                                       " + #6#14;
         |PRINT;
     |FINSI;

     aAlfa = #6#15;  |QBT aAlfa;
     |SI aAlfa ! "";
         enVaralf  = "                                       " + #6#15;
         |PRINT;
     |FINSI;
|FPRC;

|DEFBUCLE abom0006;
     #6#1;
     ;
     #1#0, "01.01.0000", 00, 00, 00;
     #1#0, "31.12.9999", 99, 99, 99;
     ;
     NULL, Agenda;
|FIN;

|PROCESO Documentos;
     |SI nSwTitulo = 0;
         enVaralf  = "";               |PRINT;
         enVaralf  = "{{N}}DOCUMENTOS{{n}}";  |PRINT;
         enVaralf  = "";               |PRINT;

         enVaralf  = "LIN DESCRIPCION                                                   DOCUMENTO";
         |PRINT;

         enVaralf  = "-----------------------------------------------------------------------------";
         |PRINT;

         nSwTitulo = 1;
     |FINSI;

     enVaralf  = (("000" + #2#1) % -103) + " " + #2#2 + " " + #2#3;
     |PRINT;
|FPRC;

|DEFBUCLE abom0002;
     #2#1;
     ;
     #1#0, 00000;
     #1#0, 99999;
     ;
     NULL, Documentos;
|FIN;

|PROCESO ImpreFicha;
     enSw = 0;   enVaralf  = ""; |PRINT;

     enSw       = 1;
     nSwCliente = 0;
                       nSwTitulo = 0;  |BUCLE abom0003;
                       nSwTitulo = 0;  |BUCLE abom0004;
     |SI #0#20 = "S";  nSwTitulo = 0;  |BUCLE abom0005;  |FINSI;
     |SI #0#22 = "S";  nSwTitulo = 0;  |BUCLE abom0006;  |FINSI;
     |SI #0#23 = "S";  nSwTitulo = 0;  |BUCLE abom0002;  |FINSI;

     |PIEINF;
|FPRC;

|PROCESO LeeExpe;
     #1#0 = #0nCampo;
     |LEE 040#1=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |SI #1#27 = "S";  |ACABA;  |FINSI;

     |HAZ ImpreFicha;
|FPRC;

|PROCESO Ficha;
     |IMPRE 0;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |INFOR "aboy0001";
     |SI FSalida ! 0;  |FINIMP;  |ACABA;  |FINSI;
     |ABRE #1;
     |PARA nCampo = 0;  |SI nCampo [ 19;  |HACIENDO nCampo = nCampo + 1;
           |SI #0nCampo ! 0;
               |HAZ LeeExpe;
           |FINSI;
     |SIGUE;
     |CIERRA #1;

     |FININF;
     |FINIMP;
|FPRC;

|PROCESO Tipo3;  |TIPO 3;
     |HAZ Ficha;
|FPRC;

|PROGRAMA;
     |SI enExped ! 0;
         |PARA nCampo = 0;  |SI nCampo [ 19;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "N";  #0nCampo = 0;
         |SIGUE;

         #0#0  = enExped;  |PINTA #0#0;
         #0#21 = "N";

         |SI PARAMETRO = "R";
             #0#20 = "N";
             #0#22 = "N";
             #0#23 = "N";
         |SINO;
             #0#20 = "S";
             #0#22 = "S";
             #0#23 = "S";
         |FINSI;

         |HAZ Tipo3;

         |ACABA;
     |FINSI;

    |CLS;
    |CABEZA "Impresion Expediente";
    |PINPA #0#0;
    |PINDA #0#0;
    |ENDATOS #1#0;
    |SI FSalida = 0;  |HAZ Tipo3;  |FINSI;
|FPRO;
