|FICHEROS;
     abom0602 #602;
     abom0603 #603;
     abom0514 #514;
     abow0015 #98,MANTE;
|FIN;

|VARIABLES;
     nCanal          = 0;
     nModo           = 0;
     nContador       = 0;
     nCampo          = 0;
     nImprimir       = 0;
     nSwAborto       = 0;
     nSwGraba        = 0;
     nDCodigo        = 0;
     nHCodigo        = 0;

     aAlfa           = "";
     aClave          = "";
     aClave1         = "";
     aClave2         = "";
     aClave3         = "";
     aRutaFichero    = "";
     aFicheroActu    = "";
     aRegilla        = "";
     aDirOrigen      = "";
     aDirDestino     = "";
     aOrigen         = "";
     aDestino        = "";
     aEjecuta        = "";
     aTemporal       = "";
     aPathPlan       = "";
     aCadena1        = "";
     aCadena2        = "";
     aExtension      = "";
     aNombreFich     = "";

     &enMailing      = 0;
     &eaCadenaImpre  = "";
     &eaRuta           = "";

     &eaFicheroRecog = "";
     &enFSalida        = 0;
     &efFechaDocu      = @;
     &enCodUbica       = 0;
     &enTipoDocumento  = 0;
     &eaRutaDocumento  = "";
     &eaNombrePrograma = "";
     &eaNombreRuta     = "";
     &eaRutaPlanilla   = "";
     &eaExtension      = "";
     &eaFicheroEdita   = "";
     &eaTipoMailing    = "";
     &eaPiePagina      = "";
     &eaCache          = "";
|FIN;

|PROCESO Imprime;
     aRegilla = "#";
     |SI nContador = 0;  aRegilla = " ";  |FINSI;
     aAlfa = ("00000" + #603#1) % -105;

     nContador     = nContador + 1;
     eaCadenaImpre = "#" + (aAlfa % 102) + "#" + "0" + (aAlfa % 303) + "   " + #603#2 + "   " + #603#3;
     |PRINT;
|FPRC;

|DEFBUCLE dsam0603C;
     #603#1;
     ;
     #602#0, nDCodigo;
     #602#0, nHCodigo;
     be;
     NULL, Imprime;
|FIN;

|RUTINA ClaveBaja603;
     #603#0 = #602#0;
     #603#1 = 00000;
|FRUT;

|RUTINA ClaveAlta603;
     #603#0 = #602#0;
     #603#1 = 99999;
|FRUT;

|RUTINA ClaveBaja98;
     #98#0 = " " * 30;
|FRUT;

|RUTINA ClaveAlta98;
     #98#0 = "z" * 30;
|FRUT;

|PROCESO RecogePlanillas;
     nSwGraba = 0;

     aTemporal = ("a4" + Usuario + "zzzzzzzz") % 108; |NOME_DAT #98 aTemporal;
     |ABRE #98;  |CIERRA #98;  |DELINDEX #98;

     |ABRE #98;

     |SI #514#21 ! "C";
         aPathPlan = eaRutaPlanilla;   |QBF aPathPlan;
         aPathPlan = aPathPlan - "\*" + "/";

         aOrigen   = aPathPlan;

         |R_PDIR aOrigen;
         |PARA; |SI FSalida = 0; |HACIENDO;
                aCadena1 = aOrigen     > " ";
                aCadena2 = eaExtension > " ";
                aCadena2 = "." + aCadena2;
                aAlfa    = aCadena1 - aCadena2;
                |SI aAlfa ! aCadena1;
                    aCadena1    = aOrigen    > " ";
                    aCadena2    = aPathPlan  > " ";  |QBF aCadena2
                    aNombreFich = aCadena1 - aCadena2;

                    #98#0 = aNombreFich;
                    #98#1 = aNombreFich;
                    |GRABA 020#98n;
                    |LIBERA #98;
                    nSwGraba = 1;
                |FINSI;

                |R_SDIR aOrigen;
         |SIGUE;
         |CIERRA #98;
     |SINO;
         aPathPlan = eaRutaPlanilla;   |QBF aPathPlan;
         aPathPlan = aPathPlan - "/*" + "/";

         aOrigen   = aPathPlan + "*." + (eaExtension < "");

         |_PDIR aOrigen;
         |PARA; |SI FSalida = 0; |HACIENDO;
                aNombreFich = aOrigen - aPathPlan;

                #98#0 = aNombreFich;
                #98#1 = aNombreFich;
                |GRABA 020#98n;
                |LIBERA #98;
                nSwGraba = 1;

                |_SDIR aOrigen;
         |SIGUE;
         |CIERRA #98;
     |FINSI;

     nSwAborto = 1;
     |SI nSwGraba = 1;
         |PUSHV 0501 2380;
         eaFicheroRecog = "";
         |ENTLINEAL #1#98, -1, 4, 22, 1, ClaveBaja98, ClaveAlta98;
         |POPV;

         |SI eaFicheroRecog ! "";
             aOrigen = aPathPlan + eaFicheroRecog;
         |FINSI;
     |SINO;
         |MENAV "      Seleccion Vacia.";
     |FINSI;

     |DELINDEX #98;
|FPRC;

|PROCESO Tipo3m0602;  |TIPO 3;
     enMailing     = #602#0;
     eaTipoMailing = #602#5;
     |SUB_EJECUTA_NP "abow0011";

     |HAZ Tipo7m0602;

     |IMPRE 0;
     |SI FSalida = 0;
         |INFOR "abom0603";
         |SI FSalida = 0;
             nContador = 0;
             nDCodigo  = 01000;  nHCodigo  = 01999;  |BUCLE dsam0603C;
             nDCodigo  = 02000;  nHCodigo  = 02999;  |BUCLE dsam0603C;
             nDCodigo  = 03000;  nHCodigo  = 03999;  |BUCLE dsam0603C;
             nDCodigo  = 04000;  nHCodigo  = 04999;  |BUCLE dsam0603C;
             nDCodigo  = 05000;  nHCodigo  = 05999;  |BUCLE dsam0603C;
             nDCodigo  = 06000;  nHCodigo  = 06999;  |BUCLE dsam0603C;
             nDCodigo  = 07000;  nHCodigo  = 07999;  |BUCLE dsam0603C;
             nDCodigo  = 08000;  nHCodigo  = 08999;  |BUCLE dsam0603C;
             nDCodigo  = 09000;  nHCodigo  = 09999;  |BUCLE dsam0603C;
             nDCodigo  = 10000;  nHCodigo  = 10999;  |BUCLE dsam0603C;
             nDCodigo  = 11000;  nHCodigo  = 11999;  |BUCLE dsam0603C;
             nDCodigo  = 12000;  nHCodigo  = 12999;  |BUCLE dsam0603C;
             nDCodigo  = 13000;  nHCodigo  = 13999;  |BUCLE dsam0603C;

             |SI nContador ! 0;  |PIEINF;  |FINSI;
             |FININF;
         |FINSI;
         |FINIMP;
     |FINSI;

     aOrigen  = eaRutaPlanilla;                   |QBF aOrigen;
     aDestino = eaRutaDocumento + "/" + #602#4;   |QBF aDestino;
     aDestino = aDestino + "." + eaExtension;     |QBF aDestino;

     |SI #514#21 ! "C";
         |XABRE "EC", aDestino, nCanal;
     |SINO;
         |XABRE "E", aDestino, nCanal;
     |FINSI;

     |SI FSalida ] 0;
         aOrigen  = "";
     |FINSI;
     |XCIERRA nCanal;

     |SI aOrigen ! "";
         aAlfa = aOrigen % -101;
         |SI aAlfa = "*";
             |HAZ RecogePlanillas;
             |QBF aOrigen;
         |FINSI;

         |SI aOrigen ! "";
             |SI #514#21 ! "C";
                 |RCOPIA_FICHERO aOrigen, aDestino;
             |SINO;
                 |COPIA_FICHERO aOrigen, aDestino;
             |FINSI;

             |SI FSalida ! 0;
                 |MENAV "         Ha habido un error al copiar la Plantilla.";
                 |ACABA;
             |FINSI;
         |FINSI;
     |FINSI;

     eaCache        = "N";
     eaFicheroEdita = #602#4;                              |QBF eaFicheroEdita;
     eaFicheroEdita = eaFicheroEdita + "." + eaExtension;  |QBF eaFicheroEdita;
     |HAZ AbreDocumento;
|FPRC;

|PROCESO Borra603;
     |BORRA 020#603a;
     |LIBERA #603;
|FPRC;

|DEFBUCLE dsam0603B;
     #603#1;
     ;
     #602#0, 00000;
     #602#0, 99999;
     ;
     NULL, Borra603;
|FIN;

|PROCESO Tipo2m0602;  |TIPO 2;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo ! 3;  |ACABA;  |FINSI;

     |BUCLE dsam0603B;

     efFechaDocu = "00.00.0000";
     enCodUbica  = #602#2;  |HAZ LeeUbicaciones;

     aFicheroActu = #602#4;                              |QBF aFicheroActu;
     aFicheroActu = aFicheroActu + "." + eaExtension;    |QBF aFicheroActu;
     aAlfa        = eaRutaDocumento + "/" + aFicheroActu;

     |SI #514#21 ! "C";
         |RFBORRA aAlfa;
     |SINO;
         |FBORRA aAlfa;
     |FINSI;
|FPRC;

|PROCESO Tipo7m0602;  |TIPO 7;
     |ENTLINEAL #1#603, -2, 7, 22, 1, ClaveBaja603, ClaveAlta603;
|FPRC;

|PROCESO ChequeaTipo;
     efFechaDocu = "00.00.0000";
     enCodUbica  = #602#2;  |HAZ LeeUbicaciones;
     eaRuta      = eaRutaDocumento;

     |SI enTipoDocumento ! 1;
         |MENAV "     El programa externo debe ser Tipo WORD.";
         |ERROR;
     |FINSI;
|FPRC;

|PROCESO ChequeaPrograma;
     #602#4 = #602#4 > " ";   |PINTA #602#4;

     aAlfa = #602#4;  |QBF aAlfa;
     |SI aAlfa = "";
         |MENAV "      Introduzca el Nombre del Documento.";
         |ERROR;
     |FINSI;
|FPRC;

|RUTINA ClaveBaja602;
     #602#0 = 1;
|FRUT;

|RUTINA ClaveAlta602;
     #602#0 = 999;
|FRUT;

|PROGRAMA;
     |ABRE #514;
     |LEE 040#514p;
     |SI FSalida ! 0;  |DDEFECTO #514;  |FINSI;
     |CIERRA #514;

     |CLS;
     |CABEZA "Creacion de Mailing";
     |ENTLINEAL #1#602, -1, 2, 22, 1, ClaveBaja602, ClaveAlta602;
|FPRO;
