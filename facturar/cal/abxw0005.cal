|FICHEROS;
     abxw0005,MANTE;

     abom0002;
     abom0514;

     abow0015;
     abxw0008,MANTE;

     reflejo@;
|FIN;

|VARIABLES;
     aExtension        = "";
     aFichero          = "";
     aFicheroP         = "";
     aFicheroT         = "";
     aPathPlan         = "";
     aOrigen           = "";
     aDestino          = "";
     aCadena           = "";
     aCadena1          = "";
     aCadena2          = "";
     aNombreFich       = "";
     aFicheroOrigen    = "";
     aFicheroDestino   = "";
     aFicheroActu      = "";
     aAlfa             = "";
     aAlfa1            = "";
     aHora             = "";
     aCodExp           = "";
     aCodLin           = "";
     aBase             = "";
     aMas              = "";
     aTipoScaneo       = "";
     aTipoScaneoT      = "";
     aFichEscan        = "";
     aMensaje          = "";
     aPathRemoto       = "";
     aRuta             = "";
     aLong             = "";
     aBaseLocal        = "";
     aVersion1         = "";
     aVersion2         = "";
     aIPRemoto         = "";

     nLong             = 0;
     nCarac            = 0;
     nPos              = 0;
     nSwGraba          = 0;
     nSwVal            = 0;
     nSwAborto         = 0;
     nSwEdita          = 0;
     nVent             = 0;
     nRango            = 0;
     nCanal            = 0;
     nAsignado         = 0;
     nErrorCampo       = 0;
     nGrabaFecha       = 0;
     nLinea            = 0;
     nTipo             = 0;
     nPanta            = 0;
     nError            = 0;
     nHoja             = 0;
     nSwFinal          = 0;
     nPregunta         = 0;
     nInstala          = 0;
     nHandle           = 0;
     nSubVent          = 0;
     nIntento          = 0;
     nSeg              = 0;
     nContaDocu        = 0;

     {-1}aVent         = "";
         aVent1        = 0;
         aVent2        = 0;
         aVent3        = 0;
         aVent4        = 0;
         aVent5        = "";

     {1}aContiene      = "";
     {1}aLocal         = "";

     &enFSalida        = 0;
     &efFechaDocu      = @;
     &enCodUbica       = 0;
     &enTipoDocumento  = 0;
     &eaRutaDocumento  = "";
     &eaNombrePrograma = "";
     &eaNombreRuta     = "";
     &eaRutaPlanilla   = "";
     &eaExtension      = "";
     &eaFicheroEdita   = "";
     &enLabel1         = -1;
     &enLabel2         = -1;
     &enLabel3         = -1;
     &enSubPanta       = 0;
     &enSwPlanilla     = 0;
     &eaRutaLocal      = "";

     &eaFicheroRecog = "";
     &enExpediente   = 0;
     &eaOrigen       = "";
     &eaDestino      = "";
     &aDocRemoto     = "";
     &aDocServidor   = "";
     &enCrear        = 0;
     &enExped        = 0;
     &eaUnidadBasico = "";
|FIN;

|PROCESO EscaneoPlana;
                        aTipoScaneo = "[SCANUI]";
     |SI #abxw0008#0 = "N";  aTipoScaneo = "[SCAN]";   |FINSI;

     nError = 0;
     |CARGADLL "dstwain.dll";
     |SI FSalida < 0;
         nError = 1;
         |MENAV "     No encuentro el driver : dstwain.dll";
         |ACABA;
     |FINSI;

     nHoja    = 1;
     nSwFinal = 0;
     |PARA; |SI nSwFinal = 0; |HACIENDO;
           nHoja = nHoja + 1;

           aTipoScaneoT = aTipoScaneo;

           |ALFACALLDLL "dstwain.dll","DsTwainEntry", aTipoScaneoT;
           |SI FSalida < 0;
               nError = 1;
               |MENAV "     No encuentro el SCANNER";
               |SAL;
           |FINSI;

           |SI aTipoScaneoT = "OK";
               aAlfa = "[SAVE:" + aFicheroP; |QBF aAlfa;
               aAlfa = aAlfa + "]";
               |ALFACALLDLL "dstwain.dll", "DsTwainEntry", aAlfa;
               nSwAborto = 0;
           |FINSI;

           |SI nHoja [ #abxw0008#2;
               aMensaje = "0000SIntroduzca la hoja numero: " + nHoja + " en el Escaner. Continuar?";
               |CONFI aMensaje;
               |SI FSalida ! 0;
                    nSwFinal = 1;
               |FINSI;
           |SINO;
               aMensaje = "0000NDesea seguir escaneando m s Hojas?";
               |CONFI aMensaje;
               |SI FSalida ! 0;
                    nSwFinal = 1;
               |FINSI;
           |FINSI;
     |SIGUE;

     |DESCARGADLL "dstwain.dll";
|FPRC;

|PROCESO EscaneoTrasera;
                        aTipoScaneo = "[SCANUI]";
     |SI #abxw0008#0 = "N";  aTipoScaneo = "[SCAN]";   |FINSI;

     nError = 0;
     |CARGADLL "dstwain.dll";
     |SI FSalida < 0;
         nError = 1;
         |MENAV "     No encuentro el driver : dstwain.dll";
         |ACABA;
     |FINSI;

     aTipoScaneoT = aTipoScaneo;

     aAlfa = "[AUTOSAVE:" + aFicheroT; |QBF aAlfa;
     aAlfa = aAlfa + "]";
     |ALFACALLDLL "dstwain.dll","DsTwainEntry", aAlfa;

     |ALFACALLDLL "dstwain.dll","DsTwainEntry", aTipoScaneoT;
     |SI FSalida < 0;
         nError = 1;
         |MENAV "     No encuentro el SCANNER";
         |ACABA;
     |FINSI;

     nSwAborto = 0;
     |DESCARGADLL "dstwain.dll";
|FPRC;

|PROCESO InstalaImpresora;
     nInstala = 0;

     aAlfa = eaUnidadBasico + "\ds\dspdf\bin\puerto.exe";
     |XABRE "EC", aAlfa, nHandle;
     |SI FSalida < 0;  nInstala = 1;  |FINSI;

     |DBASE aBase;
     eaOrigen      = aBase + "plantillas/dspdf.tgz";
     eaDestino     = eaUnidadBasico + "\DOCUMENTOS\IPABOGAD\dspdf.tgz";

     |HAZ EnviaFicheroConMD5;
     |SI aDocRemoto ! aDocServidor;
         nInstala = 1;
     |FINSI;

     |SI nInstala = 1;
         aAlfa = eaUnidadBasico + "\DOCUMENTOS\IPABOGAD\dspdf.tgz";
         Pos   = -1;
         |RDESCOMPRIME aAlfa;
         aAlfa1 = eaUnidadBasico + "\ds\";
         |RDETAR aAlfa, aAlfa1;

         aAlfa = eaUnidadBasico + "\DOCUMENTOS\IPABOGAD\dspdf.tar";  |RFBORRA aAlfa;

         aAlfa = eaUnidadBasico + "\ds\dspdf\bin\puerto.exe";
         |XABRE "EC", aAlfa, nHandle;
         |SI FSalida < 0;
             |MENAV "0000 No puedo instalar la impresora en PDF.";
             nSwAborto = 1;
             |ACABA;
         |FINSI;

         aAlfa  = "cmd " + &34 + "/c START /WAIT " + eaUnidadBasico + "\DS\DSPDF\BIN\PUERTO.EXE" + &34;
         |RSYSTEM aAlfa;

         |PULSATECLA;
     |FINSI;
|FPRC;

|PROCESO ActDllTwain;
     |ESPECIFICA "RBASS", aLocal;
     aBaseLocal = aLocal;

     eaDestino   = aBaseLocal + "bin/dstwain.dll";
     aContiene1 = eaDestino;
     |ESPECIFICA "REMOTOMD5", aContiene;
     aVersion1  = FSalida;  |QBF aVersion1;

     |DBASE aBase;  |QBF aBase;
     eaOrigen    = aBase + "plantillas/dstwain.dll";
     aContiene1 = eaOrigen;
     |ESPECIFICA "MD5", aContiene;
     aVersion2  = FSalida;  |QBF aVersion2;

     |SI aVersion1 ! aVersion2;
         aIPRemoto = "";
         |IP_REMOTO aIPRemoto;  |QBT aIPRemoto;

         |SI aIPRemoto ! "";
             |ENVIA_FICHERO eaOrigen, eaDestino;
         |SINO;
             |COPIA_FICHERO eaOrigen, eaDestino;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO Escanear;
     |HAZ ActDllTwain;

     nAsignado = 0;
     nPregunta = 0;
     nSwAborto = 1;

     aPathRemoto = eaUnidadBasico + "\DOCUMENTOS";              |RMKDIR aPathRemoto;
     aPathRemoto = eaUnidadBasico + "\DOCUMENTOS\IPABOGAD";     |RMKDIR aPathRemoto;

     aFicheroP   = aPathRemoto + "\imagen.tif"
     aFicheroT   = aPathRemoto + "\imagen.1tif"
     aFichero    = aPathRemoto + "\imagen.tif"

     |RFBORRA aFichero;

     |SI #abxw0008#1 = "P";  |HAZ EscaneoPlana;    |FINSI;
     |SI #abxw0008#1 = "T";  |HAZ EscaneoTrasera;  |FINSI;

     nSwAborto = 0;
     |XABRE "EC", aFichero, nHandle;
     |SI FSalida < 0;
         nSwAborto = 1;
     |FINSI;

     |PULSATECLA;

     |SI nSwAborto = 1;  |PTEC 807;  |ACABA;  |FINSI;

     eaExtension = eaExtension < "";

     |SI eaExtension = "pdf";
         |HAZ InstalaImpresora;

         aAlfa = eaUnidadBasico + "\ds\dspdf\novisu.txt";
         |XABRE "WBC", aAlfa, nHandle;
         aAlfa = "N" + &13 + &10;
         |XGRABA nHandle, aAlfa;
         |XCIERRA nHandle;

         |DBASE aBase;
         eaOrigen      = aBase + "plantillas/dsprint.exe";
         eaDestino     = eaUnidadBasico + "\DOCUMENTOS\IPABOGAD\dsprint.exe";

         |HAZ EnviaFicheroConMD5;

         |VENTANA 0501, 1099, -1, 16, "Creando fichero pdf";
         nSubVent = FSalida;

         aVent1 = -1;
         aVent2 = -1;
         aVent3 = 0;
         aVent4 = nSubVent;
         aVent5 = "MODAL";
         |ESPECIFICA "ESTADO_VENTANA", aVent;

         |CONTROL_SIMPLE 0, "LABEL,{{5}}Creando fichero PDF", 0520, 0798, NULL;
         |CONTROL_SIMPLE 0, "LABEL,{{2}}Espere por favor ...", 0940, 0998, NULL;

         |PTEC 802;
         |PAUSA;

         |PARA nIntento = 1;  |SI nIntento [ 5;  |HACIENDO nIntento = nIntento + 1;
              |PULSATECLA;
              |PULSATECLA;
              |PULSATECLA;

              aAlfa  = "cmd " + &34 + "/c START " + eaDestino + " " + aFichero + " DsPdf" + &34;
              |RSYSTEM aAlfa;

              aOrigen  = eaUnidadBasico + "\ds\dspdf\documentos\fin.txt";
              |PARA;  |SI;  |HACIENDO;
                     |XABRE "EC", aOrigen, nHandle;
                     |SI FSalida ] 0;
                         |SAL;
                     |FINSI;
                     |SLEEP 1;
                     |PULSATECLA;
              |SIGUE;

              |RFBORRA aOrigen;

              aOrigen  = eaUnidadBasico + "\ds\dspdf\documentos\informe.pdf";
              |XABRE "EC", aOrigen, nHandle;
              |SI FSalida ] 0;
                 |SAL;
              |FINSI;
         |SIGUE;

         |PULSATECLA;

         aOrigen  = eaUnidadBasico + "\ds\dspdf\documentos\informe.pdf";
         aDestino = eaUnidadBasico + "\DOCUMENTOS\IPABOGAD\imagen.pdf";
         |RCOPIA_FICHERO aOrigen, aDestino;

         |RFBORRA aOrigen;

         aVent1 = -1;
         aVent2 = -1;
         aVent3 = 0;
         aVent4 = nSubVent;
         aVent5 = "MODELESS";
         |ESPECIFICA "ESTADO_VENTANA", aVent;

         |FINVENTANA nSubVent;

         aOrigen  = eaUnidadBasico + "\DOCUMENTOS\IPABOGAD\IMAGEN.PDF";
     |SINO;
         aOrigen  = eaUnidadBasico + "\DOCUMENTOS\IPABOGAD\IMAGEN.TIF";
     |FINSI;

     |PULSATECLA;

     aAlfa = "w";
     |SI eaExtension ! "doc";
         |SI eaExtension = "xls";  aAlfa = "e";  |FINSI;
         |SI eaExtension = "tif";  aAlfa = "g";  |FINSI;
         |SI eaExtension = "pdf";  aAlfa = "g";  |FINSI;
     |FINSI;

     |SI #abom0002#1 [ 999;
         #abom0002#3 = aAlfa + (("00000000" + #abom0002#0) % -108) + (("000" + #abom0002#1) % -103) + "." + eaExtension;
     |SINO;
         #abom0002#3 = aAlfa + (("00000000" + #abom0002#0) % -108) + (("00000" + #abom0002#1) % -105) + "." + eaExtension;
     |FINSI;

     aDestino = eaUnidadBasico + "\DOCUMENTOS\IPABOGAD\" + #abom0002#3;  |QBF aDestino;
     |RFBORRA aDestino;
     |RRENOMBRA_FICHERO aOrigen, aDestino;

     nSwAborto = 1;
     |SI FSalida ! 0;
         |MENAV "         Ha habido un error al copiar el Documento.";
     |SINO;
         nSwAborto = 0;
     |FINSI;

     enCodUbica  = #abxw0005#0;  |HAZ LeeUbicaciones;
     aOrigen     = aDestino;
     aDestino    = eaRutaDocumento + "/" + #abom0002#3;   |QBF aDestino;

     |SI #abom0514#21 ! "C";
         |RCOPIA_FICHERO aOrigen, aDestino;
     |SINO;
         |RECIBE_FICHERO aOrigen, aDestino;
     |FINSI;

     aFichero    = aPathRemoto + "\imagen.tif"
     |RFBORRA aFichero;
     |RFBORRA aOrigen;

     |SI #abxw0008#4 = "N";  nSwEdita = 1;  |FINSI;

     |PTEC 807;
     |PULSATECLA;
|FPRC;

|PROCESO Imagenes;
     |VENTANA 1010, 1990, -1, 16, "Escanear";
     nVent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |PINPA #0#abxw0008;
     nPanta = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,{{205}} Escanear documento", 1137, 1257, Escanear;

     |PINDA #0#abxw0008;
     |ENDATOS #1#abxw0008;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA nVent;
|FPRC;

|PROCESO GrabaTemporal;
     #abow0015#0 = #abom0002#3;
     |LEE 000#abow0015.=;
     |SI FSalida = 0;  |ACABA;  |FINSI;

     #abow0015#0 = #abom0002#3;
     #abow0015#1 = #abom0002#2;
     #abow0015#2 = #abom0002#4;
     |GRABA 020#abow0015.n;
     |LIBERA #abow0015;
     nSwGraba = 1;
|FPRC;

|PROCESO LeeDocumento;
     nSwVal  = 0;
     aCodExp = aNombreFich % 208;
     aCodLin = aNombreFich % 1003;
     |SALVA_FICHA #abom0002;

     #abom0002#0 = aCodExp;
     #abom0002#1 = aCodLin;
     |SI #abom0002#0 = 0;  |Y #abom0002#1 = 0;
         |REPON_FICHA #abom0002;
         |ACABA;
     |FINSI;

     |LEE 000#abom0002.=;
     |SI FSalida = 0;
         |SI #abxw0005#2 = 1;
             nSwVal = 1;
         |SINO;
             aCadena = #abxw0005#3;  |QBF aCadena;
             |SI aCadena = "";
                 |HAZ GrabaTemporal;
             |SINO;
                 aCadena1 = #abom0002#2 > " ";  |QBF aCadena1;
                 aCadena2 = #abxw0005#3 > " ";  |QBF aCadena2;

                 aCadena = aCadena1 - aCadena2;
                 |SI aCadena ! aCadena1;
                     |HAZ GrabaTemporal;
                 |FINSI;
             |FINSI;
         |FINSI;
     |FINSI;

     |REPON_FICHA #abom0002;
|FPRC;

|PROCESO EventoPlanilla;
     |SI FSalida = 1;  |O FSalida = 2;
         |LEE 000#abow0015.=;
         |SI FSalida ! 0;  |DDEFECTO #abow0015;  |FINSI;
     |FINSI;

     |SI FSalida = 4;
         |SI enSwPlanilla = 0;  eaFicheroRecog = #abow0015#0;  |FINSI;
         |SI enSwPlanilla = 1;  eaFicheroRecog = #abow0015#1;  |FINSI;
         efFechaDocu    = #abow0015#2;

         |QBF eaFicheroRecog;

         |PTEC 806;
     |FINSI;
|FPRC;

|PROCESO RecogePlanillas;
     nSwGraba   = 0;
     nContaDocu = 0;

     aFichero = ("c1" + Usuario + "zzzzzzzz") % 108;
     |NOME_DAT #abow0015#aFichero#;
     |ABRE #abow0015;  |CIERRA #abow0015;  |DELINDEX #abow0015;

     |ABRE #abow0015;

     |SI #abom0514#21 ! "C";
         aPathPlan = #abxw0005#6;   |QBF aPathPlan;
         |SI #abxw0005#4 = 0;
             aPathPlan = aPathPlan - "\*" + "/";
         |SINO;
             aPathPlan = aPathPlan + "/";
         |FINSI;

         aOrigen    = aPathPlan;

         |R_PDIR aOrigen;
         |PARA; |SI FSalida = 0; |HACIENDO;
                aCadena1 = aOrigen     > " ";
                aCadena2 = eaExtension > " ";
                aCadena2 = "." + aCadena2;
                aDestino = aCadena1 - aCadena2;
                |SI aDestino ! aCadena1;
                    aCadena1    = aOrigen    > " ";
                    aCadena2    = aPathPlan  > " ";  |QBF aCadena2
                    aNombreFich = aCadena1 - aCadena2;
                    |HAZ LeeDocumento;
                    |SI nSwVal = 0;
                        #abow0015#0 = aNombreFich;
                        |LEE 000#abow0015.=;
                        |SI FSalida ! 0;
                            nContaDocu = nContaDocu + 1;
                            #abow0015#0 = aNombreFich;
                            #abow0015#1 = aNombreFich;
                            #abow0015#2 = #abom0002#4;
                            |GRABA 020#abow0015.n;
                            |LIBERA #abow0015;
                        |FINSI;
                        nSwGraba = 1;
                    |FINSI;
                |FINSI;

                |R_SDIR aOrigen;
         |SIGUE;
         |CIERRA #abow0015;
     |SINO;
         aPathPlan = #abxw0005#6;   |QBF aPathPlan;
         |SI #abxw0005#4 = 0;
             aPathPlan = aPathPlan - "/*" + "/";
         |SINO;
             aPathPlan = aPathPlan + "/";
         |FINSI;

         aOrigen    = aPathPlan + "*." + (eaExtension < "");

         |_PDIR aOrigen;
         |PARA; |SI FSalida = 0; |HACIENDO;
                aNombreFich = aOrigen - aPathPlan;
                |HAZ LeeDocumento;
                |SI nSwVal = 0;
                    #abow0015#0 = aNombreFich;
                    |LEE 000#abow0015.=;
                    |SI FSalida ! 0;
                        nContaDocu = nContaDocu + 1;

                        #abow0015#0 = aNombreFich;
                        #abow0015#1 = aNombreFich;
                        #abow0015#2 = #abom0002#4;
                        |GRABA 020#abow0015.n;
                        |LIBERA #abow0015;
                     |FINSI;
                     nSwGraba = 1;
                 |FINSI;
                 |_SDIR aOrigen;
         |SIGUE;
         |CIERRA #abow0015;
     |FINSI;

     nSwAborto = 1;
     |SI nSwGraba = 1;
         |SI nContaDocu > 1;
             |VENTANA 0802, 2398, -1, 16, "Planillas (Doble clic en el registro para seleccionar)";
             nVent = FSalida;

             aVent1 = -1;
             aVent2 = -1;
             aVent3 = 0;
             aVent4 = nVent;
             aVent5 = "MODAL";
             |ESPECIFICA "ESTADO_VENTANA", aVent;

             nRango         = 4 + 8 + 16 + 128;
             eaFicheroRecog = "";
             enSwPlanilla   = 1;

             |ABRE #abow0015;

             |LINEAL_SIMPLE #1#abow0015, nRango, 0902, 2298, NULL, NULL, EventoPlanilla;
             |CIERRA #abow0015;

             aVent1 = -1;
             aVent2 = -1;
             aVent3 = 0;
             aVent4 = nVent;
             aVent5 = "MODELESS";
             |ESPECIFICA "ESTADO_VENTANA", aVent;

             |FINVENTANA nVent;
         |SINO;
             |SI enSwPlanilla = 0;  eaFicheroRecog = #abow0015#0;  |FINSI;
             |SI enSwPlanilla = 1;  eaFicheroRecog = #abow0015#1;  |FINSI;

             efFechaDocu    = #abow0015#2;

             |QBF eaFicheroRecog;
         |FINSI;

         |SI eaFicheroRecog ! "";
             aFicheroOrigen  = aPathPlan + eaFicheroRecog;
             aFicheroDestino = aFicheroActu;
             |SI #abom0514#21 ! "C";
                 |RCOPIA_FICHERO aFicheroOrigen, aFicheroDestino;
             |SINO;
                 |COPIA_FICHERO aFicheroOrigen, aFicheroDestino;
             |FINSI;

             |SI FSalida ! 0;
                 |MENAV "         Ha habido un error al copiar el Documento.";
             |SINO;
                 nSwAborto = 0;
             |FINSI;
         |FINSI;
     |SINO;
         |MENAV "      Seleccion Vacia.";
     |FINSI;

     |DELINDEX #abow0015;
|FPRC;

|PROCESO GrabaTempo;
     |SI #reflejo@#0 = #abom0002#0;  |Y #reflejo@#1 = #abom0002#1;
         |ACABA;
     |FINSI;

     aCadena = #abxw0005#3;  |QBF aCadena;
     |SI aCadena ! "";
         aCadena1 = #reflejo@#2  > " ";  |QBF aCadena1;
         aCadena2 = #abxw0005#3    > " ";  |QBF aCadena2;

         aCadena = aCadena1 - aCadena2;
         |SI aCadena = aCadena1;  |ACABA;  |FINSI;
     |FINSI;

     #abow0015#0 = #reflejo@#3;
     |LEE 000#abow0015.=;
     |SI FSalida = 0;  |ACABA;  |FINSI;

     #abow0015#0 = #reflejo@#3;
     #abow0015#1 = #reflejo@#2;
     #abow0015#2 = #reflejo@#4;
     |GRABA 020#abow0015.n;
     |LIBERA #abow0015;
     nSwGraba = 1;
|FPRC;

|DEFBUCLE reflejo;
     #reflejo@#2003;
     ;
     #abxw0005#4, 00000000, 00001;
     #abxw0005#4, 99999999, 99999;
     ;
     NULL, GrabaTempo;
|FIN;

|PROCESO RecogeDeOtro;
     efFechaDocu = "00.00.0000";
     enCodUbica  = #abxw0005#4;  |HAZ LeeUbicaciones;
     nSwGraba    = 0;

     aFichero = ("c1" + Usuario + "zzzzzzzz") % 108;
     |NOME_DAT #abow0015#aFichero#;
     |ABRE #abow0015;  |CIERRA #abow0015;  |DELINDEX #abow0015;

     aOrigen = eaRutaDocumento; |QBF aOrigen;
     aOrigen = aOrigen + "/";   |QBF aOrigen;

     |DBASE aBase;  |QBF aBase;
     aMas = aBase + "def/abom0002.mas";
     |CARGA_DEF aMas, reflejo@;
     |SI FSalida ! 0;
         |MENAV "      Error en Cargar el Def";
         |ACABA;
     |FINSI;

     |ABRE #abow0015;
     |BUCLE reflejo;
     |CIERRA #abow0015;

     |DESCARGA_DEF #reflejo@;

     nSwAborto = 1;
     |SI nSwGraba = 1;
         |VENTANA 0802, 2398, -1, 16, "Documentos expedientes (Doble clic en el registro para seleccionar)";
         nVent = FSalida;

         aVent1 = -1;
         aVent2 = -1;
         aVent3 = 0;
         aVent4 = nVent;
         aVent5 = "MODAL";
         |ESPECIFICA "ESTADO_VENTANA", aVent;

         nRango         = 4 + 8 + 16 + 128;
         enSwPlanilla   = 0;
         eaFicheroRecog = "";

         |ABRE #abow0015;
         |LINEAL_SIMPLE #1#abow0015, nRango, 0902, 2298, NULL, NULL, EventoPlanilla;
         |CIERRA #abow0015;

         |SI eaFicheroRecog ! "";
             enCodUbica      = #abxw0005#4;  |HAZ LeeUbicaciones;
             aFicheroOrigen  = eaRutaDocumento + "/" + eaFicheroRecog;
             aFicheroDestino = aFicheroActu;

             |SI #abom0514#21 ! "C";
                 |RCOPIA_FICHERO aFicheroOrigen, aFicheroDestino;
             |SINO;
                 |COPIA_FICHERO aFicheroOrigen, aFicheroDestino;
             |FINSI;

             |SI FSalida ! 0;
                 |MENAV "         Ha habido un error al copiar el Documento.";
             |SINO;
                 nSwAborto = 0;
             |FINSI;
         |FINSI;

         aVent1 = -1;
         aVent2 = -1;
         aVent3 = 0;
         aVent4 = nVent;
         aVent5 = "MODELESS";
         |ESPECIFICA "ESTADO_VENTANA", aVent;

         |FINVENTANA nVent;
     |SINO;
         |MENAV "      Seleccion Vacia.";
     |FINSI;

     |DELINDEX #abow0015;
|FPRC;

|PROCESO MiraDocu0;
     aCadena = #abxw0005#3;  |QBF aCadena;
     |SI aCadena = "";
         |HAZ GrabaTemporal;
     |SINO;
         aCadena1 = #abom0002#2  > " ";  |QBF aCadena1;
         aCadena2 = #abxw0005#3 > " ";  |QBF aCadena2;

         aCadena = aCadena1 - aCadena2;
         |SI aCadena ! aCadena1;
             |HAZ GrabaTemporal;
         |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE abom0002;
     #abom0002#1;
     10;
     00000000, 00000, nTipo;
     00000000, 99999, nTipo;
     ;
     NULL, MiraDocu0;
|FIN;

|PROCESO RecogeDocuGeneral;
     aFichero = ("c1" + Usuario + "zzzzzzzz") % 108;
     |NOME_DAT #abow0015#aFichero#;
     |ABRE #abow0015;  |CIERRA #abow0015;  |DELINDEX #abow0015;

     |SALVA_FICHA #abom0002;

     |ABRE #abow0015;
     nTipo    = #abxw0005#0;
     nSwGraba = 0;
     |BUCLE abom0002;
     |REPON_FICHA #abom0002;

     nSwAborto = 1;
     |SI nSwGraba = 1;
         |VENTANA 0802, 2398, -1, 16, "Documentos entrada general (Doble clic en el registro para seleccionar)";
         nVent = FSalida;

         aVent1 = -1;
         aVent2 = -1;
         aVent3 = 0;
         aVent4 = nVent;
         aVent5 = "MODAL";
         |ESPECIFICA "ESTADO_VENTANA", aVent;

         nRango         = 4 + 8 + 16 + 128;
         enSwPlanilla   = 0;
         eaFicheroRecog = "";

         |ABRE #abow0015;
         |LINEAL_SIMPLE #1#abow0015, nRango, 0902, 2298, NULL, NULL, EventoPlanilla;
         |CIERRA #abow0015;

         |SI eaFicheroRecog ! "";
             enCodUbica      = #abxw0005#0;   |HAZ LeeUbicaciones;
             aFicheroOrigen  = eaRutaDocumento + "/" + eaFicheroRecog;
             aFicheroDestino = aFicheroActu;

             |SI #abom0514#21 ! "C";
                 |RCOPIA_FICHERO aFicheroOrigen, aFicheroDestino;
             |SINO;
                 |COPIA_FICHERO aFicheroOrigen, aFicheroDestino;
             |FINSI;

             |SI FSalida ! 0;
                 |MENAV "         Ha habido un error al copiar el Documento.";
             |SINO;
                 nSwAborto = 0;
             |FINSI;
         |FINSI;

         aVent1 = -1;
         aVent2 = -1;
         aVent3 = 0;
         aVent4 = nVent;
         aVent5 = "MODELESS";
         |ESPECIFICA "ESTADO_VENTANA", aVent;

         |FINVENTANA nVent;
     |SINO;
         |MENAV "      Seleccion Vacia.";
     |FINSI;

     |DELINDEX #abow0015;

     |SI nSwAborto = 0;
         |CONFI "2400SBorro el Documento de la Entrada General : ";
         |SI FSalida = 0;
             |SALVA_FICHA #abom0002;
             aCodExp = eaFicheroRecog % 208;
             aCodLin = eaFicheroRecog % 1003;
             #abom0002#0 = aCodExp;
             #abom0002#1 = aCodLin;
             |LEE 101#abom0002.=;
             |SI FSalida = 0;
                 |BORRA 020#abom0002.a;
                 |LIBERA #abom0002;
                 aFicheroOrigen  = eaRutaDocumento + "/" + eaFicheroRecog;
                 |SI #abom0514#21 ! "C";
                     |RFBORRA aFicheroOrigen;
                 |SINO;
                     |FBORRA aFicheroOrigen;
                 |FINSI;
             |FINSI;
             |REPON_FICHA #abom0002;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO RecogeDelPC;
     aRuta = "F*." + eaExtension;
     |BROWSE aRuta;

     aAlfa = aRuta;
     aRuta = "";
     aLong = aAlfa % 0;
     nLong = aLong;
     |PARA nCarac = 2;  |SI nCarac [ nLong;  |HACIENDO nCarac = nCarac + 1;
           nPos = (nCarac * 100) + 1;
           aRuta = aRuta + (aAlfa % nPos);
     |SIGUE;

     eaExtension = eaExtension < "";
     aExtension  = (aRuta % -103) < "";
     |SI aExtension ! eaExtension;
         |MENAV "0000 La extensi¢n del fichero seleccionado no corresponde con la extensi¢n de la ubicaci¢n que estamos trabajando";
         nSwAborto = 1;
         |ACABA;
     |FINSI;

     aAlfa = "w";
     |SI eaExtension ! "doc";
         |SI eaExtension = "xls";  aAlfa = "e";  |FINSI;
         |SI eaExtension = "tif";  aAlfa = "g";  |FINSI;
         |SI eaExtension = "pdf";  aAlfa = "g";  |FINSI;
     |FINSI;

     |SI #abom0002#1 [ 999;
         #abom0002#3 = aAlfa + (("00000000" + #abom0002#0) % -108) + (("000" + #abom0002#1) % -103) + "." + eaExtension;
     |SINO;
         #abom0002#3 = aAlfa + (("00000000" + #abom0002#0) % -108) + (("00000" + #abom0002#1) % -105) + "." + eaExtension;
     |FINSI;

     nSwAborto   = 0;
     enCodUbica  = #abxw0005#0;  |HAZ LeeUbicaciones;
     aOrigen     = aRuta;
     aDestino    = eaRutaDocumento + "/" + #abom0002#3;   |QBF aDestino;

     |SI #abom0514#21 ! "C";
         |RCOPIA_FICHERO aOrigen, aDestino;
     |SINO;
         |RECIBE_FICHERO aOrigen, aDestino;
     |FINSI;

     |SI FSalida < 0;
         |MENAV "0000 Problemas al copiar fichero en la ubicaci¢n destino.";
         nSwAborto = 1;
     |FINSI;
|FPRC;

|PROCESO TrataDocumentos;
     |SI #abxw0005#2 = "1";
         |SI #abxw0005#4 = 0;
             aFicheroOrigen  = #abxw0005#6;  |QBF aFicheroOrigen;
             aAlfa = aFicheroOrigen % -101;
             |SI aAlfa ! "*";
                 |SI aFicheroOrigen ! "";
                     aFicheroDestino = aFicheroActu;
                     |SI #abom0514#21 ! "C";
                         |RCOPIA_FICHERO aFicheroOrigen, aFicheroDestino;
                     |SINO;
                         |COPIA_FICHERO aFicheroOrigen, aFicheroDestino;
                     |FINSI;

                     |SI FSalida ! 0;
                         |MENAV "         Ha habido un error al copiar la Plantilla.";
                         nSwAborto = 1;
                         |ACABA;
                     |FINSI;
                 |FINSI;
             |SINO;
                 |HAZ RecogePlanillas;
             |FINSI;
         |SINO;
             aFicheroOrigen  = #abxw0005#6;  |QBF aFicheroOrigen;
             |HAZ RecogePlanillas;
         |FINSI;
     |FINSI;

     |SI #abxw0005#2 = "2";   |HAZ RecogeDeOtro;        |FINSI;
     |SI #abxw0005#2 = "3";   |HAZ RecogeDocuGeneral;   |FINSI;
     |SI #abxw0005#2 = "4";   |HAZ RecogeDelPC;         |FINSI;
|FPRC;

|PROCESO EditaDocumento;
     aAlfa       = #abom0002#3;  |QBF aAlfa;
     |SI aAlfa   = "";    |ACABA;   |FINSI;

     efFechaDocu    = #abom0002#4;
     enCodUbica     = #abom0002#10;  |HAZ LeeUbicaciones;
     eaFicheroEdita = #abom0002#3;   |QBF eaFicheroEdita;
     aFicheroActu   = eaRutaDocumento + "/" + eaFicheroEdita;

     |HAZ RutaLocal;

     nSwAborto = 0;
     nSwEdita  = 0;
     Pos       = -1;

     |SI #abom0514#21 ! "C";
         |XABRE "EC", aFicheroActu, nCanal;
     |SINO;
         |XABRE "E", aFicheroActu, nCanal;
     |FINSI;

     |SI FSalida < 0;
         Pos = -1;

         |SI enCrear = 0;
             |SI enTipoDocumento = 4;  |Y #abxw0005#2 = "1";
                 |HAZ Imagenes;
             |SINO;
                 |HAZ TrataDocumentos;
             |FINSI;
         |FINSI;

         |SI nSwAborto = 1;  |ACABA;  |FINSI;

         |SI nAsignado = 1;
            nAsignado = 0;
            |ACABA;
         |FINSI;
     |FINSI;

     |SI nSwEdita = 0;
         |VENTANA 0501, 1099, -1, 16, "Editando documento";
         nSubVent = FSalida;

         aVent1 = -1;
         aVent2 = -1;
         aVent3 = 0;
         aVent4 = nSubVent;
         aVent5 = "MODAL";
         |ESPECIFICA "ESTADO_VENTANA", aVent;

         |CONTROL_SIMPLE 0, "LABEL,{{5}}Editando documento", 0520, 0798, NULL;
         |CONTROL_SIMPLE 0, "LABEL,{{2}}Espere por favor ...", 0940, 0998, NULL;

         |PTEC 802;
         |PAUSA;

         |QBF aFicheroActu;
         efFechaDocu    = #abom0002#4;
         enCodUbica     = #abom0002#10;  |HAZ LeeUbicaciones;
         eaFicheroEdita = #abom0002#3;   |QBF eaFicheroEdita;
         |HAZ AbreDocumento;

         aVent1 = -1;
         aVent2 = -1;
         aVent3 = 0;
         aVent4 = nSubVent;
         aVent5 = "MODELESS";
         |ESPECIFICA "ESTADO_VENTANA", aVent;

         |FINVENTANA nSubVent;
     |FINSI;

     |HORA aHora;
     aAlfa = aHora % 102;  #abom0002#8 = aAlfa;   |PINTA #abom0002#8;
     aAlfa = aHora % 402;  #abom0002#9 = aAlfa;   |PINTA #abom0002#9;
                           #abom0002#7 = ~;       |PINTA #abom0002#7;

     |SI nErrorCampo = 0;  |ERROR;   |FINSI;
|FPRC;

|PROCESO PonFichero;
     efFechaDocu  = #abom0002#4;
     enCodUbica   = #abxw0005#0;  |HAZ LeeUbicaciones;

     eaExtension = eaExtension < "";
     aAlfa = "w";
     |SI eaExtension ! "doc";
         |SI eaExtension = "xls";  aAlfa = "e";  |FINSI;
         |SI eaExtension = "tif";  aAlfa = "g";  |FINSI;
         |SI eaExtension = "pdf";  aAlfa = "g";  |FINSI;
     |FINSI;

     |SI #abom0002#1 [ 999;
         #abom0002#3 = aAlfa + (("00000000" + #abom0002#0) % -108) + (("000" + #abom0002#1) % -103) + "." + eaExtension;
     |SINO;
         #abom0002#3 = aAlfa + (("00000000" + #abom0002#0) % -108) + (("00000" + #abom0002#1) % -105) + "." + eaExtension;
     |FINSI;

     #abom0002#2  = #abxw0005#7;
     #abom0002#10 = #abxw0005#0;

     FSalida     = 10;
     nErrorCampo = 1;
     nGrabaFecha = 1;
     enCrear     = 0;

     |GRABA 020#abom0002.a;
     |LIBERA #abom0002;

     |LEE 101#abom0002.=;

     |HAZ EditaDocumento;

     nGrabaFecha = 0;
     nErrorCampo = 0;
|FPRC;

|PROCESO Tipo12Fxw5;  |TIPO 12;
     |SI FSalida = 7;  |ACABA;  |FINSI;

     |DDEFECTO #abom0002;
     #abom0002#0 = enExpediente;
     #abom0002#1 = #abxw0005#8;

     |HORA aHora;
     aAlfa = aHora % 102;  #abom0002#5 = aAlfa;
     aAlfa = aHora % 402;  #abom0002#6 = aAlfa;

     |GRABA 020#abom0002.b;

     nSwAborto   = 0;
     |HAZ PonFichero;

     |SI nSwAborto = 1;
         |BORRA 020#abom0002.a;
         |LIBERA #abom0002;

         |MENAV "2300 Proceso cancelado.";
         |ACABA;
     |FINSI;

     #abom0002#2  = #abxw0005#7;

     |GRABA 020#abom0002.a;
     |LIBERA #abom0002;
|FPRC;

|PROCESO Tipo0C0Fxw5;  |TIPO 0;
     |SI enLabel1 ! -1;  |FIN_CONTROL_WINDOWS enLabel1;  |FINSI;

     enCodUbica = #abxw0005#0;   |HAZ LeeUbicaciones;
     |SI enTipoDocumento = 4;
         |CONTROL_SIMPLE enSubPanta, "LABEL,{{2}}Escanear una imagen nueva", 1042, 1098, NULL;
     |SINO;
         |CONTROL_SIMPLE enSubPanta, "LABEL,{{2}}De un documento SIN expediente o formulario (Plantillas)", 1042, 1098, NULL;
     |FINSI;
     enLabel1 = FSalida;
|FPRC;

|PROCESO Tipo0C2Fxw5;  |TIPO 0;
     efFechaDocu = "00.00.0000";
     enCodUbica  = #abxw0005#0;   |HAZ LeeUbicaciones;

     |C_M #abxw0005#3, 1, "S";
     |C_M #abxw0005#4, 1, "S";
     |SI #abxw0005#2 = "1";
         |C_M #abxw0005#3, 1, "N";
         #abxw0005#4 = 0;
         #abxw0005#5 = "PLANILLAS";
         #abxw0005#6 = eaRutaPlanilla;
     |FINSI;

     |SI #abxw0005#2 = "2";
         #abxw0005#4 = #abxw0005#0;
         #abxw0005#5 = #abxw0005#1;
     |FINSI;

     |SI enTipoDocumento = 4;  |Y #abxw0005#2 = "1";
         |C_M #abxw0005#3, 1, "N";
         |C_M #abxw0005#4, 1, "N";
         #abxw0005#4 = 0;
         #abxw0005#5 = "";
     |FINSI;

     |SI #abxw0005#2 = "4";
         |C_M #abxw0005#3, 1, "N";
         |C_M #abxw0005#4, 1, "N";
         #abxw0005#4 = 0;
         #abxw0005#5 = "";
     |FINSI;

     |PINTA #abxw0005#3;
     |PINTA #abxw0005#4;
     |PINTA #abxw0005#5;
     |PINTA #abxw0005#6;

     |HAZ Tipo0C4Fxw5;

     |SI enExpediente = 0;  |Y #abxw0005#2 = "3";
         |ERROR;
         |ACABA;
     |FINSI;

     |SI #abxw0005#2 = "3";
         |C_M #abxw0005#4, 1, "N";
     |FINSI;

     |PINTA #abxw0005#4;
|FPRC;

|PROCESO Tipo0C4Fxw5;  |TIPO 0;
     |SI #abxw0005#4 = 0;
          |SI Campo = 4;  |PTEC 806;  |FINSI;
         |ACABA;
     |FINSI;

     efFechaDocu = "00.00.0000";
     enCodUbica  = #abxw0005#0;   |HAZ LeeUbicaciones;
     aExtension  = eaExtension;

     enCodUbica = #abxw0005#4;   |HAZ LeeUbicaciones;

     |SI aExtension ! eaExtension;
         |MENAV "     El tipo del documento a buscar no coincide con el seleccionado.";
         |ERROR;
         |ACABA;
     |FINSI;

     |SI #abxw0005#4 ! 0;
         #abxw0005#5 = eaNombreRuta;     |PINTA #abxw0005#5;
         #abxw0005#6 = eaRutaDocumento;  |PINTA #abxw0005#6;
     |FINSI;

     |SI Campo = 4;  |PTEC 806;  |FINSI;
|FPRC;

|PROCESO Tipo0C8Fxw5;  |TIPO 0;
     #abom0002#0 = enExpediente;
     #abom0002#1 = #abxw0005#8;
     |LEE 000#abom0002.=;
     |SI FSalida = 0;
         |MENAV "0000 El documento ya existe.";
         |ERROR;
     |FINSI;
|FPRC;
