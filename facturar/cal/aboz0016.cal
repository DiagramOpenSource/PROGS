|FICHEROS;
     aboz0016 #0;
     dsam0119 #119;
     abom0514 #514;
|FIN;

|VARIABLES;
     aRuta          = "";
     aBase          = "";
     aOri           = "";
     aDes           = "";
     aExtO          = "";
     aExtD          = "";
     aAlfa          = "";
     aAlfa1         = "";
     aAlfa2         = "";
     aAlfa3         = "";
     aLong          = "";
     aBaseDoc       = "";
     aPathPlantilla = "";
     aCaracter      = "";

     nLong          = 0;
     nCarac         = 0;
     nPos           = 0;
|FIN;

|PROCESO Tipo0C2;  |TIPO 0;
     |ABRE #119;
     #119#0 = #0#0;
     |LEE 000#119=;
     |SI FSalida ! 0;  |DDEFECTO #119;  |FINSI;
     |CIERRA #119;

     |PARA;  |SI;  |HACIENDO;
          aRuta = "F\*." +  (#119#3 < "");  |QBF aRuta;
          |BROWSE aRuta;

          aOri  = aRuta % 299;  |QBF aOri;
          aExtD = (aOri % -103) < "";
          aExtO = #119#3 < "";
          |SI aExtD ! aExtO;
              |MENAV "0000 Extension del fichero distinta a la del Programa Externo.";
              |CONFI "0000NCancelamos el proceso";
              |SI FSalida = 0;
                  aOri = "";
                  |SAL;
              |FINSI;
          |SINO;
              |SAL;
          |FINSI;
      |SIGUE;

      |SI aOri = "";
          |ACABA;
      |FINSI;

      #0#2 = aOri;  |PINTA #0#2;
|FPRC;

|PROCESO Tipo7C3;  |TIPO 7;
     aLong   = aOri % 0;
     nLong   = aLong;
     aAlfa2  = "";
     |PARA nCarac = nLong;  |SI nCarac ] 1;  |HACIENDO nCarac = nCarac - 1;
           nPos      = (nCarac * 100) + 1;
           aCaracter = aOri % nPos;
           |SI aCaracter = "/";  |O aCaracter = "\";
               |SAL;
           |FINSI;
           aAlfa2 = aAlfa2 + aCaracter;
     |SIGUE;

     aLong     = aAlfa2 % 0;
     nLong     = aLong;
     aAlfa3    = "";
     |PARA nCarac = nLong;  |SI nCarac ] 1;  |HACIENDO nCarac = nCarac - 1;
           nPos      = (nCarac * 100) + 1;
           aCaracter = aAlfa2 % nPos;
           aAlfa3    = aAlfa3 + aCaracter;
     |SIGUE;

     aAlfa3 = aAlfa3 > "";
     #0#3   = aAlfa3 - ("." + #119#3);  |PINTA #0#3;
|FPRC;

|PROCESO Tipo12;  |TIPO 12;
     |SI aOri = "";  |ACABA;  |FINSI;

     |CONFI "0000SImportamos el fichero";
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |DBASE aBaseDoc;  |QBF aBaseDoc;
     aPathPlantilla = aBaseDoc + "ipabogad";
     |MKDIR aPathPlantilla;

     aPathPlantilla = aPathPlantilla + "/plantillas";
     |MKDIR aPathPlantilla;

     aPathPlantilla = aPathPlantilla + "/" + (("00" + #119#0) % -102);
     |MKDIR aPathPlantilla;

     aDes = aPathPlantilla + "/" + #0#3;    |QBF aDes;
     aDes = (aDes + "." + #119#3) < "";     |QBF aDes;
     |RECIBE_FICHERO aOri, aDes;
|FPRC;

|PROCESO Tipo80;  |TIPO 80;
     |ABRE #514;
     |LEE 000#514p;
     |SI FSalida ! 0;  |DDEFECTO #514;  |FINSI;
     |CIERRA #514;

     |SI #514#21 ! "C";
         |MENAV "0000 Tiene la opcion de DISTRIBUIDA, el pase no procede.";
         |PTEC 807;
         |ACABA;
     |FINSI;
|FPRC;
