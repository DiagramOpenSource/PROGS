|FICHEROS;
     visaz008 #0;        ||Entrada de cobros Parciales
     ashisrec #1;
     asparcia #2;        ||Consulta de cobros parciales
     asw00006 #3;
     ashiscon #5;
     ashisfac #6;
     visam017 #7;

     asempcon #50;

     ashismic #90;
     ashismil #91;
     asw00009 #99;       || Temporal importe pdte.
     visaw010 #100,MANTE;

     &bancosp@ #706;
     cacuent@ #1000;
     canempr@ #1001;
|FIN;

|VARIABLES;
     fDesde = @;
     fSistema = @;
     aSistema = "";
     aAnyo = "";
     aHasta = "";
     fHasta = @;

     aAlfa = "";

     aCad1 = "";
     aCad2 = "";
     aLong = "";

     nTotalImp = 0;
     nCalc     = 0;
     nCalc1    = 0;
     nCalc2    = 0;
     nMaxDig   = 0;
     nCont     = 0;
     nTecla    = 0;

     &enSwEstaPlan = 0;
     aMensaje = "";
     nSwVisados = 0;

     lin      = 0;
     nombre   = "";
     modo     = 0;
     tot      = 0;
     mensaje  = "";
     ultima   = "";
     alfa     = "";
     fichero  = "";
     nCampo   = 0;

     &enCodBanco  = 0;
     &enSwDeDonde = 0;


     &d_viene = 0;
     &d_clien = 0;
     &h_clien = 0;
     &d_fecha = @;
     &h_fecha = @;
     &eaPath  = "";
     &EURODB  = 0;

     &eaCta   = "";
|FIN;

|PROCESO tipo3; |TIPO 3;
     #0#9 = "S";
|FPRC;

|PROCESO impcob;
     #0#7 = #0#5 - #0#6;
     |PINTA #0#7;

     nCalc = #visaw010#7 - nTotalImp;
     |SI nCalc < #0#7;
        #0#8 = nCalc;
     |SINO;
        #0#8 = #0#7;
     |FINSI;
     |PINTA #0#8;
|FPRC;

|PROCESO entrar;
     nCalc = nTotalImp + #0#8;
     |SI nCalc > #visaw010#7;
          |MENAV "    ATENCION !! Importe ingreso superado. Si no es un cobro parcial puntee los cobros marcados";
          ||ERROR; |ACABA;
     |FINSI;
     |SI #0#8 > #0#7;
          |ABRE #visam017;
          #visam017#0 = #0#0;
          #visam017#1 = #0#1;
          |LEE 000#visam017.=;
          |SI FSalida = 0;
             aAlfa = "    Problemas. La factura " + #0#0 + "/" + (("00000" + #0#1) % -105) + " ya tiene exceso.";
             |MENAV aAlfa;
             |CIERRA #visam017;
             |ERROR; |ACABA;
          |FINSI;
          |CIERRA #visam017;
          |MENAV "0000   Se generara un registro con el exceso de importe";
     |FINSI;
     aAlfa = #visaw010#6; |QBF aAlfa;
     |SI aAlfa ! "";  #0#10 = #visaw010#6;  |FINSI;
|FPRC;

|PROCESO act_hist;
|SI #0#8 = 0;
    #1#0 = #0#0;
    #1#1 = #0#1;
    |LEE 101#1=;
    |SI FSalida = 0;
        #1#16 = #0#13;
        |GRABA 020#1a;
    |FINSI;
    |LIBERA #1;
    |ACABA;
|FINSI;

#1#0 = #0#0;
#1#1 = #0#1;
|LIBERA #1;
|LEE 140#1=;
|SI FSalida = 0;
    |SI #1#10 = "N"; |O #100#2 = 2;  ||Si no esta cobrado total
        tot = (#1#9 + #0#8) ! EURODB;
        |SI tot > #1#6;
            #1#9 = #1#6; #1#10 = "S";
            #visam017#0 = #0#0;
            #visam017#1 = #0#1;
            |LEE 000#visam017.=;
            |SI FSalida = 0;
               aAlfa = "    La factura " + #0#0 + "/" + (("00000" + #0#1) % -105) + " ya tenia exceso. El exceso se perdera.";
            |SINO;
               |DDEFECTO #visam017;
               #visam017#0 = #0#0;
               #visam017#1 = #0#1;
               #visam017#2 = #0#11;
               #visam017#3 = #0#12;
               #visam017#4 = #1#6;
               #visam017#5 = tot - #1#6;
               #visam017#6 = #0#10;
               |GRABA 020#visam017.n;
            |FINSI;
        |FINSI;
        |SI tot < #1#6; #1#9 = #1#9 + #0#8;       |FINSI;
        |SI tot = #1#6; #1#9 = #1#6; #1#10 = "S"; |FINSI;

        |SI #100#2 = 2;   #1#10 = "N";  |FINSI;
        #1#8  = #0#3;
        #1#16 = #0#13;
        |GRABA 140#1a;

        #90#0 = #1#0;
        #90#1 = #1#1;
        |LIBERA #90;
        |LEE 140#90=;
        |SI FSalida = 0;
            #90#32 = #1#10;
            |GRABA 140#90a;
        |FINSI;
    |FINSI;
|FINSI;

|DDEFECTO #2;
#2#0 = #0#0;
#2#1 = #0#1;
#2#2 = 999;
|LEE 001#2];
|SI FSalida = 0;
    |LEE 001#2a;
|SINO;
    |LEE 001#2u;
|FINSI;
|SI FSalida = 0; |Y #0#0 = #2#0; |Y #0#1 = #2#1;  ||Existe el registro
    lin = #2#2 + 1;
    |DDEFECTO #2;
     #2#0 = #0#0; #2#1 = #0#1; #2#2 = lin; |GRABA 021#2b;
|SINO;
    |DDEFECTO #2;
    #2#0 = #0#0; #2#1 = #0#1; #2#2 = 1; |GRABA 021#2b;
|FINSI;
#2#3  = #0#3;
#2#4  = #0#4;
#2#5  = #0#5;
#2#6  = #1#9;
#2#7  = #1#6 - #1#9;
|SI #0#8 > #0#7;
   #2#8 = #0#7;
|SINO;
   #2#8 = #0#8;
|FINSI;
#2#9  = #1#3;
#2#12 = #1#2;
#2#13 = #0#10;

|GRABA 140#2a; |LIBERA #2;

|SI #2#7 = 0;   || Cobrado por completo
   #ashismic#0 = #2#0;
   #ashismic#1 = #2#1;
   |LEE 101#ashismic.=;
   |SI FSalida = 0;
      #ashismic#32 = "S";
      |GRABA 020#ashismic.a; |LIBERA #ashismic;
   |FINSI;
|FINSI;

|FPRC;

|PROCESO graba_fich;
     |SI #1#6 = 0; |ACABA; |FINSI;

     enCodBanco = #1#7;
     |HAZ LeeBancoPropio;

     #90#0 = #1#0;
     #90#1 = #1#1;
     |LEE 040#90=;
     |SI FSalida ! 0; |DDEFECTO #90; |FINSI;

     |DDEFECTO #0;
     lin   = lin + 1;
     #0#2  = lin;
     #0#0  = #1#0;
     #0#1  = #1#1;
     #0#4  = #1#5;
     #0#5  = #1#6;
     #0#6  = #1#9;
     #0#7  = #0#5 - #0#6;
     #0#10 = #706#11;
     |SI enSwDeDonde = 0;   #0#10 = #706#23;  |FINSI;
     #0#11 = #1#3;
     #0#12 = #90#5;
     #0#13 = #1#16;
     |GRABA 021#0n;
     |LIBERA #0;

     #99#0 = #0#11;      || RELLENA EL TEMPORAL CON LOS IMPORTES PENDIENTES.
     |LEE 141#99=;
     |SI FSalida = 0;
          #99#1 = #99#1 + #0#7;
          |GRABA 021#99a;
          |LIBERA #99;
     |SINO;
          #99#1 = #0#7;
          |GRABA 021#99n;
          |LIBERA #99;
     |FINSI;

     |SI lin = 99999; |ERROR10; |FINSI;
|FPRC;

|DEFBUCLE mira_pdte;
    #1#5;
    ;
    #100#0, "N", "  " ,     0;
    #100#1, "N", "zz" , 99999;
    ;
    NULL , graba_fich;
|FIN;

|DEFBUCLE mira_impag;
    #1#5;
    5;
    #100#0, "S", "  " ,     0, #100#3;
    #100#1, "S", "zz" , 99999, #100#4;
    ;
    NULL, graba_fich;
|FIN;

|DEFBUCLE mira_pdte_2;
    #1#1;
    3,10;
    "  ",    0,#100#0,"N";
    "zz",99999,#100#1,"N";
    ;
    NULL , graba_fich;
|FIN;

|DEFBUCLE mira_impag_2;
    #1#1;
    3,10,5;
    "  ",    0,#100#0,"S",#100#3;
    "zz",99999,#100#1,"S",#100#4;
    ;
    NULL, graba_fich;
|FIN;


|DEFBUCLE actualiza;
 #0#2;
 ;
     0 , "  " ,     0 , "S";
 99999 , "zz" , 99999 , "S";
 e;
 NULL , act_hist;
|FIN;

|PROGRAMA;
     |HAZ temporal;
     |ABRE #99;
     lin = 0;
     |CLS;
     |CABEZA "Cobros Parciales";
     |PINPA #0#0;

     |SI d_viene ! 3;
         |PUSHV 0501 2380;
         |PINPA #0#100;
         |HAZ EsVisados;
         nSwVisados = FSalida;
         |SI nSwVisados = 0;
             #100#5 = 2;
         |FINSI;
         |PINDA #0#100;
         |ENDATOS #1#100;
         |SI FSalida ! 0;
             |POPV;
             |ACABA;
         |FINSI;
         |POPV;
     |SINO;
         |PATH_DAT #0 eaPath;
         |PATH_DAT #1 eaPath;
         |PATH_DAT #2 eaPath;
         |PATH_DAT #3 eaPath;
         |PATH_DAT #5 eaPath;
         |PATH_DAT #6 eaPath;
         |PATH_DAT #90 eaPath;
         |PATH_DAT #91 eaPath;
         |PATH_DAT #99 eaPath;

         |DDEFECTO #100;
         #100#0 = d_clien;
         #100#1 = h_clien;

         |HAZ EsVisados;
         nSwVisados = FSalida;
         |SI nSwVisados = 0;
             #100#5 = 2;
         |FINSI;
         #100#2 = 2;
         #100#3 = d_fecha;
         #100#4 = h_fecha;
     |FINSI;

     nombre = ("par" + (Usuario % 810)) % 108; |QBF nombre;
     |NOME_DAT #0 nombre;
     |ABRE #0;  |CIERRA #0;  |DELINDEX #0;

     |BLIN 4;
     |ABRE #0;
     |ABRE #1;
     |ABRE #90;
     |MENSA "0000Pulse  < ESPACIO >  Para detener el proceso";
     |SI #100#2 = 1;
          |SI #100#5 = 2;
               |BUCLE mira_pdte;
          |SINO;
               |BUCLE mira_pdte_2;
          |FINSI;
     |FINSI;
     |SI #100#2 = 2;
          |SI #100#5 = 2;
               |BUCLE mira_impag;
          |SINO;
               |BUCLE mira_impag_2;
          |FINSI;
     |FINSI;
     |CIERRA #1;
     |CIERRA #90;
     |CIERRA #0;

     |BLIN 4;
     |ENTLINEAL #1#0 , -1 , 2 , 22 , 1 , pro1 , pro2; || ord.  por cliente
     |CONFI "2400NActualizar S/N : ";
     |SI FSalida ! 0; |DELINDEX #0; |ACABA; |FINSI;

     |ABRE #1;
     |ABRE #2;
     |ABRE #visam017; |ABRE #ashismic;
     |BUCLE actualiza;
     |CIERRA #visam017; |CIERRA #ashismic;
     |CIERRA #1;
     |CIERRA #2;
     |CIERRA #90;

     |CIERRA #99;
     |DELINDEX #99;                || TEMPORAL.
     |DELINDEX #0;                 || TEMPORAL.
|FPRO;

|RUTINA infer;
#3#1 = "";
|FRUT;

|RUTINA super;
#3#1 = "zzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzz";
|FRUT;

|PROCESO consulta;
 |SI FSalida ! 9;  |ACABA;  |FINSI;
 nCampo = Campo;
 |PUSHV 0501 2380;
 |ENTLINEAL #2#3 , -1 , 4 , 21 , 1 , infer , super;
 |POPV;

 #100nCampo = #3#0;  |PINTA #100nCampo;
|FPRC;

|PROCESO pro1;
     #0#2 = 1;
|FPRC;

|PROCESO pro2;
     #0#2 = 99999;
|FPRC;

|PROCESO entrada;
     modo = (FEntrada / 100) ! 0;
     |SI modo = 1;
          |CAMPO_MODIFICA #0#0 , 1 , "S";
          |CAMPO_MODIFICA #0#1 , 1 , "S";
     |SINO;
          |CAMPO_MODIFICA #0#1 , 1 , "N";
          |CAMPO_MODIFICA #0#0 , 1 , "N";
     |FINSI;
|FPRC;

|PROCESO Tipo2; |TIPO 2;
     nTotalImp = nTotalImp +. #0#8; nTotalImp = nTotalImp ! 2;

     modo = (FEntrada / 100) ! 0;
     |SI modo = 3;
         |CONFI "2400NDamos de baja el Historico del Recibo : ";
         |SI FSalida = 0;
             |HAZ BajaRecibo;
         |SINO;
             |ERROR;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO BorraLMinutas;
|BORRA 140#91a;
|LIBERA #91;
|FPRC;

|DEFBUCLE ashismil;
#91#1;
;
#0#0, #0#1, 001;
#0#0, #0#1, 999;
;
NULL, BorraLMinutas;
|FIN;

|PROCESO BorraLConceptos;
|BORRA 140#5a;
|LIBERA #5;
|FPRC;

|DEFBUCLE ashiscon;
#5#1;
;
#0#0, #0#1, 000;
#0#0, #0#1, 999;
;
NULL, BorraLConceptos;
|FIN;

|PROCESO BajaRecibo;
  |ABRE #1;  #1#0  = #0#0; #1#1  = #0#1; |BORRA 140#1c;  |LIBERA #1;  |CIERRA #1;
  |ABRE #6;  #6#0  = #0#0; #6#1  = #0#1; |BORRA 140#6c;  |LIBERA #6;  |CIERRA #6;
  |ABRE #90; #90#0 = #0#0; #90#1 = #0#1; |BORRA 140#90c; |LIBERA #90; |CIERRA #90;
  |BUCLE ashismil;
  |BUCLE ashiscon;
|FPRC;

|PROCESO factura;
     |ABRE #1;
     #1#0 = #0#0;
     #1#1 = #0#1;
     |LEE 001#1=;
     |SI FSalida ! 0;
          |MENAV "0000Recibo Inexistente";
          |CIERRA #1;
          |ERROR;
          |ACABA;
     |FINSI;
     #0#4 = #1#5;
     #0#5 = #1#6;
     #0#6 = #1#9;
     #0#7 = #0#5 - #0#6;
     |PINTA #0#4;
     |PINTA #0#5;
     |PINTA #0#7;
     |CIERRA #1;
|FPRC;

|PROCESO ult;
     |SI FSalida = 9; #0#10 = ultima; |PINTA #0#10; |FINSI;
     eaCta = #0Campo;
     |HAZ MFiltraPtos;
     #0Campo = eaCta; |PINTA #0Campo;
     |QBF eaCta;
     |SI eaCta = "";
        |MENAV "    Cuenta en blanco"; |ERROR;
     |FINSI;

     |HAZ EsVisados;
     |SI FSalida = 0;
          |DBASS aAlfa; |QBF aAlfa;
          aAlfa = aAlfa + "contagen/def/cacuenta.mas";
          |CARGA_DEF aAlfa, cacuent@;
          |SI FSalida = 0;
             |ABRE #asempcon;
             |LEE 000#asempcon.p;
             |SI FSalida ! 0; |ACABA; |FINSI;
             |CIERRA #asempcon;

             |DBASS aAlfa; |QBF aAlfa;
             aAlfa = aAlfa + "contagen/fich/";
             aAlfa = aAlfa + ("00000" + #asempcon#1) % -105;
             aAlfa = aAlfa + (("0" + #asempcon#2) % -101) + "/";
             |PATH_DAT #cacuent@#aAlfa#;
             |ABRE #cacuent@;
             #cacuent@#0 = #0Campo;
             |LEE 000#cacuent@.=;
             |SI FSalida = 0;
                |SI #cacuent@#3 = "N";
                   aMensaje = "0000Cuenta con pase directo = 'N'";
                   |MENAV aMensaje; |ERROR;
                |FINSI;
             |FINSI;
             |CIERRA #cacuent@; |DESCARGA_DEF #cacuent@;
          |FINSI;

          |HAZ EstaPlanContable;
          |SI enSwEstaPlan = 0;
               aMensaje = "0000Cuenta no definida en Plan Contable";
               |MENAV aMensaje;
          |FINSI;
     |FINSI;

     ultima = #0#10;
|FPRC;

|PROCESO DudosoCobro;  |TIPO 7;
     |C_M #0#13, 1, "S";
     |SI #0#8 = #0#7;
         #0#13 = "N";  |PINTA #0#13;
         |C_M #0#13, 1, "N";
     |FINSI;
|FPRC;

||---------------------------------------------------------------------------
|| PINTA EL IMPORTE TOTAL PENDIENTE.
||---------------------------------------------------------------------------
|PROCESO pendiente; |TIPO 13;
     #99#0 = #0#11;
     |LEE 141#99=;
     |SI FSalida ! 0; #99#1 = 0; |FINSI;
     |ATRI I;
     |PINTA 0767,#99#1;
     |ATRI i;
     |LIBERA #99;
     #99#1 = nTotalImp;
     |PINTA 0867,#99#1;
|FPRC;

||---------------------------------------------------------------------------
|| CREA EL FICHERO TEMPORAL.
||---------------------------------------------------------------------------
|PROCESO temporal;
     fichero = Usuario % 810;
     |QBF fichero;
     fichero = (fichero + "zzzzzzzz") % 105;
     fichero = "TMP" + fichero;
     |NOME_DAT #99 fichero;
     |ABRE #99;
     |CIERRA #99;
     |DELINDEX #99;
|FPRC;

|PROCESO CobroImpag;
     |C_M #100#3, 1, "S";
     |C_M #100#4, 1, "S";
     |SI #100#2 = 1;
         |C_M #100#3, 1, "N";
         |C_M #100#4, 1, "N";
     |FINSI;
|FPRC;

|PROCESO Tipo0C3; |TIPO 0;
     |HAZ EsVisados;
     |SI FSalida = 0;
          fDesde = "01.01.2007";
          fSistema = ~;
          aSistema = fSistema;
          aAnyo = aSistema % -104;
          aHasta = "31.12." + aAnyo;
          fHasta = aHasta;

          |SI #visaz008#3 < fDesde; |O #visaz008#3 > fHasta;
               aMensaje = "0000Atencion: La fecha de cobro no puede ser inferior a 2007 ni superior a " + aAnyo;
               |MENAV aMensaje;
               |ERROR;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO CuentaTesor;
   nTecla = FSalida;

   |DBASS aAlfa; |QBF aAlfa;
   aAlfa = aAlfa + "contagen/def/cacuenta.mas";
   |CARGA_DEF aAlfa, cacuent@;
   |SI FSalida = 0;
      |DBASS aAlfa; |QBF aAlfa;
      aAlfa = aAlfa + "contagen/def/canempre.mas";
      |CARGA_DEF aAlfa, canempr@;
      |SI FSalida = 0;
         aAlfa = #0Campo; |QBF aAlfa;
         |SI aAlfa ! "";
            |ABRE #asempcon;
            |LEE 000#asempcon.p;
            |SI FSalida ! 0; |ACABA; |FINSI;
            |CIERRA #asempcon;

            |DBASS aAlfa; |QBF aAlfa;
            aAlfa = aAlfa + "contagen/fich/";
            |PATH_DAT #canempr@#aAlfa#;

            aAlfa = aAlfa + ("00000" + #asempcon#1) % -105;
            aAlfa = aAlfa + (("0" + #asempcon#2) % -101) + "/";
            |PATH_DAT #cacuent@#aAlfa#;

            |ABRE #canempr@; |ABRE #cacuent@;

            nMaxDig = 0;
            #canempr@#2 = #asempcon#1;
            #canempr@#3 = #asempcon#2;
            |LEE 000#canempr@.=;
            |SI FSalida = 0;
               |PARA nCont = 14; |SI nCont [ 19; |HACIENDO nCont = nCont + 1;
                  |SI nMaxDig [ #canempr@#nCont#;
                     nMaxDig = #canempr@#nCont#;
                  |FINSI;
               |SIGUE;
            |FINSI;

            aAlfa = #100Campo; |QBF aAlfa;
            aAlfa = aAlfa - ".";
            |PARA; |SI FSalida ! 0; |HACIENDO;
               nCalc1 = ((FSalida / 100) ! 0) + 99; aCad1 = aAlfa % nCalc1;
               nCalc2 = FSalida + 98;               aCad2 = aAlfa % nCalc2;
               aLong = aCad1 % 0; nCalc1 = aLong;
               aLong = aCad2 % 0; nCalc2 = aLong;
               nCalc1 = nMaxDig - (nCalc1 + nCalc2);
               aAlfa = aCad1 + ("0" * nCalc1) + aCad2;

               aAlfa = aAlfa - ".";
            |SIGUE;
            #100Campo = aAlfa; |PINTA #100Campo;

            |SI nTecla = 10;
               #cacuent@#0 = #100Campo;
               |LEE 000#cacuent@.];
               |CONSULTA_CLAVE #1#cacuent@;
               #100Campo = #cacuent@#0; |PINTA #100Campo;
            |FINSI;

            #cacuent@#0 = #100Campo;
            |LEE 000#cacuent@.=;
            |SI FSalida = 0;
               |SI #cacuent@#3 = "N";
                  aMensaje = "0000Cuenta con pase directo = 'N'";
                  |MENAV aMensaje; |ERROR;
               |FINSI;
            |SINO;
               aMensaje = "0000Cuenta inexistente";
               |MENAV aMensaje; |ERROR;
            |FINSI;
         |SINO;
            aMensaje = "0000La cuenta en blanco es incorrecta";
            |MENAV aMensaje; |ERROR;
         |FINSI;
         |CIERRA #canempr@; |DESCARGA_DEF #canempr@;
      |FINSI;
      |CIERRA #cacuent@; |DESCARGA_DEF #cacuent@;
   |FINSI;
|FPRC;
