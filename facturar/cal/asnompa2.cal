|FICHEROS;
    asnompa2 #0;
    :/laboral/def/labregcl #1;
    asconcep #2;
    asclient #3;
    asnomenl #4;
    asnomta2 #5;
    ascabmin #6;
    aslinmin #7;
    ascontro #8;
    ascontrt #450;
    aslinmil #10;
    asconlin #11;
|FIN;

|VARIABLES;
    nVariable     = 0;
    nFichero      = 0;
    nCliente      = 0;
    aSeccion      = "";
    aConcepto     = "";

    fecha  = @;
    varalf = "";
    honorario = 0;
    suplido   = 0;
    acuenta   = 0;
    seccion   = "";
    concepto  = "";
    canti     = 0;
    import    = 0;
    tlinea    = 0;
    iva       = 0;
    linea     = 0;
    FEstado   = 0;
    seleccio  = 0;
    conver    = "";
    conver1   = "";
    conver2   = "";

    &empre_p  = 0;
    &empre_f  = 0;
    &mes_p    = 0;
    &anyo_p   = 0;
    &fecha_p  = @;
    &tipo_p   = 0;
    &rete_p   = "";
    &tarifa   = 0;
    &d_tipo   = "";
    &h_tipo   = "";

    nTipoFactG=0;

    &h_comun    = 0;
    &anyo_n     = 0;
    &empre_n    = 0;
    &nSwSoloImp = 0;

    aDFacturado = "";
    aHFacturado = "";
|FIN;
____________________________________/ CALCULOS DE PANTALLA
|PROCESO pinta; |TIPO 7;
    fecha = ~;
    varalf = fecha % 402;
    #0#2 = varalf;
    varalf = fecha % 704;
    #0#3 = varalf;
    |PINTA #0#2;
    |PINTA #0#3;
|FPRC;

|PROCESO FactGlobal;|TIPO 0;
     |SI #0#10="N";
          |CAMPO_MODIFICA #0#5,1,"N";
     |SINO;
          |CAMPO_MODIFICA #0#5,1,"S";
     |FINSI;
|FPRC;
____________________________________/ GRABACION MINUTAS
|PROCESO leeconc;
|DDEFECTO #2;
    #2#0 = seccion;
    #2#1 = concepto;
|LEE 000#2=;
|FPRC;

|PROCESO GrabaTexto;
     |ABRE #10;

     #10#0 = #7#0;
     #10#1 = #7#1;
     #10#2 = #7#2;
     #10#3 = 1;
     |LEE 101#10=;
     |SI FSalida ! 0;
         |DDEFECTO #10;
         #10#0 = #7#0;
         #10#1 = #7#1;
         #10#2 = #7#2;
         #10#3 = 1;
         #10#4 = #7#7;
         |GRABA 020#10n;
     |FINSI;
     |LIBERA #10;

     #10#0 = #7#0;
     #10#1 = #7#1;
     #10#2 = #7#2;
     #10#3 = 2;
     |LEE 101#10=;
     |SI FSalida ! 0;
         |DDEFECTO #10;
         #10#0 = #7#0;
         #10#1 = #7#1;
         #10#2 = #7#2;
         #10#3 = 2;
         #10#4 = #7#8;
         |GRABA 020#10n;
     |FINSI;
     |LIBERA #10;

     #10#0 = #7#0;
     #10#1 = #7#1;
     #10#2 = #7#2;
     #10#3 = 3;
     |LEE 101#10=;
     |SI FSalida ! 0;
         |DDEFECTO #10;
         #10#0 = #7#0;
         #10#1 = #7#1;
         #10#2 = #7#2;
         #10#3 = 3;
         #10#4 = #7#9;
         |GRABA 020#10n;
     |FINSI;
     |LIBERA #10;

     |CIERRA #10;
|FPRC;

|PROCESO RecogeTexto;
     |ABRE #10;
     |ABRE #11;
     |PARA nVariable = 1;  |SI nVariable [ 100;  |HACIENDO nVariable = nVariable + 1;
           #11#0 = nFichero;
           #11#1 = nCliente;
           #11#2 = aSeccion;
           #11#3 = aConcepto;
           #11#4 = nVariable;
           |LEE 040#11=;
           |SI FSalida = 0;
               #10#0 = #7#0;
               #10#1 = #7#1;
               #10#2 = #7#2;
               #10#3 = nVariable;
               |LEE 101#10=;
               |SI FSalida ! 0;
                   |DDEFECTO #10;
                   #10#0 = #7#0;
                   #10#1 = #7#1;
                   #10#2 = #7#2;
                   #10#3 = nVariable;
                   |GRABA 020#10b;
               |FINSI;
               #10#4 = #11#5;
               |GRABA 020#10a;
               |LIBERA #10;
           |FINSI;
     |SIGUE;

     |CIERRA #10;
     |CIERRA #11;
|FPRC;

|PROCESO grabalin;
|ABRE #7;
    linea = linea + 5;
    #7#0  = #0#5;             || tipo facturacion
    #7#1  = empre_f;             || cliente
    #7#2  = linea;            || linea
    |HAZ conver_fec;
    #7#3  = conver;             || fecha
    #7#4  = seccion;          || seccion
    #7#5  = concepto;         || concepto
    #7#6  = #2#2;             || descrip. abreviada
    #7#7  = #2#16;            || descrip. 1
    #7#8  = #2#17;            || descrip. 2
    #7#9  = #2#18;            || descrip. 3
    #7#10 = #2#3;             || tipo
    #7#11 = canti;            || unidades
    #7#12 = import;           || ptas.
    #7#13 = tlinea;           || importe
    #7#14 = #2#5;             || unitario o global
    #7#15 = #0#4;             || Fecha DD/MM/AAAA
    |GRABA 020#7n;
    |CIERRA #7;

    nFichero  = 1;
    nCliente  = 0;
    aSeccion  = #7#4;
    aConcepto = #7#5;
    |HAZ RecogeTexto;
    |HAZ GrabaTexto;
|FPRC;

|PROCESO conver_fec; |TIPO 0;
conver1 = #0#4 % 0102;
conver2 = #0#4 % 0402;
conver  = conver1 + conver2;
|FPRC;

|PROCESO linealin;
|SI #7#10 = "H"; honorario = honorario + #7#13; |FINSI;
|SI #7#10 = "S"; suplido   = suplido   + #7#13; |FINSI;
|SI #7#10 = "A"; acuenta   = acuenta   + #7#13; |FINSI;
|FPRC;

|PROCESO cabecera;
|ABRE #6;
    #6#0 = #0#5;
    #6#1 = empre_f;
|LEE 101#6=;
    FEstado = FSalida;

    #6#0  = #0#5;             || tipo facturacion
    #6#1  = empre_f;             || cliente
    #6#2  = #3#1;             || razon social
    #6#3  = #3#2;             || nombre cliente
    #6#4  = #3#3;             || direccion
    #6#5  = #3#4;             || cod. postal 1
    #6#6  = #3#5;             || cod. postal 2
    #6#7  = #3#6;             || poblacion
    #6#8  = #3#7;             || provincia
    #6#9  = #3#8;             || cif
    #6#10 = #3#15;            || forma de pago
    #6#11 = #3#16;            || banco a remesar
|SI #0#6 = "S"; #6#12 = #3#17; |SINO; #6#12 = "N"; |FINSI;  || retencion
    #6#13 = #3#11;            || banco
    #6#14 = #3#12;            || direccion banco
    #6#15 = #3#13;            || ccc banco
    #6#16 = #3#14;            || cta. cte. banco

    honorario = 0;
    suplido   = 0;
    acuenta   = 0;

|BUCLELIN 0linealin#6;

    #6#17 = honorario;
    #6#18 = suplido;
    #6#19 = acuenta;
    #6#20 = #3#23;
    #6#21 = #6#17 % #6#20;
    #6#22 = #6#17 - #6#21;
|HAZ iva;
    #6#23 = #6#17 % iva;
    #6#24 = #6#22 + #6#23 + #6#18;
|SI #6#12 = "S"; #6#25 = #6#22 % #8#31; |SINO; #6#25 = 0; |FINSI;
    #6#26 = #6#24 - #6#19 - #6#25;
|SI FEstado = 0; |GRABA 020#6a; |SINO; |GRABA 020#6n; |FINSI;

|LIBERA #6;
|CIERRA #6;
|FPRC;

|PROCESO iva;
    |ABRE #8;
    |LEE 000#8p;
    |SI FSalida ! 0; |DDEFECTO #8; |FINSI;
    |CIERRA #8;

    |ABRE #450;
    #450#0 = #6#0;
    |LEE 000#450=;
    |SI FSalida ! 0; |DDEFECTO #450; |FINSI;
    |CIERRA #450;

    iva = #450#3;
|FPRC;
____________________________________/ PROCESO DE GRABACION
|PROCESO linea;
    linea = #7#2;
|FPRC;

|DEFBUCLE leelin;
    #7#1;
    ;
    #0#5, empre_f,   1;
    #0#5, empre_f, 999;
    e;
    NULL, linea;
|FIN;

|PROCESO impresio;
    honorario = 0;
    suplido   = 0;
    linea     = 0;

    |SI empre_p = 0; |Y nSwSoloImp = 0;
        #4#0 = #1#0;
        |LEE 000#4=;                            || lee enlace codigos
        |SI FSalida ! 0; |O #4#8 = "N"; |ACABA; |FINSI;
        empre_f = #4#2;

        |SI #0#10="S";         || tipo facturacion igual para todas las empresas
              #0#5=nTipoFactG;
        |SINO;
              #0#5=#4#6;      || facturacion individual, se guarda en #0#5 porque lo lee simpre de ahi
        |FINSI;

    |FINSI;

    |DDEFECTO #3;
    #3#0 = empre_f;                        || lee cliente facturacion
    |LEE 000#3=;

    |DDEFECTO #5;
    #5#1 = #1#4;                        || lee tarifa
    |LEE 000#5=;
    |SI FSalida = 0;

        |SI nSwSoloImp = 0;
            |BUCLE leelin;                      || ultima linea
        |FINSI;

        seccion = #5#2;
        concepto = #5#3;
        |HAZ leeconc;

        canti  = #1#5;
        import = #2#4;
        |SI #3#22 = 2; import = #2#8;  |FINSI;
        |SI #3#22 = 3; import = #2#10; |FINSI;
        |SI #3#22 = 4; import = #2#12; |FINSI;
        |SI #3#22 = 5; import = #2#14; |FINSI;

        tlinea = canti * import;

        |SI tlinea ! 0;
            h_comun = h_comun + tlinea;

            |SI nSwSoloImp = 0;
                |HAZ grabalin;
                |HAZ cabecera;
            |FINSI;

            |SI nSwSoloImp ! 2;  || solo cuendo no es el informe
                #1#6 = "S";      || lo regraba como facturado
                |GRABA 020#1a;
            |FINSI;
            seleccio = 1;
       |FINSI;
    |FINSI;
    |LIBERA #1;
|FPRC;

|DEFBUCLE asnomdat;
   #1#2;
   6;
   #0#0, #0#3, #0#2, #0#7, aDFacturado;
   #0#1, #0#3, #0#2, #0#8, aHFacturado;
   be;
   NULL, impresio;
|FIN;

|PROCESO tipo_3; |TIPO 3;

    aDFacturado = "N";
    aHFacturado = "N";
    |SI nSwSoloImp = 2;
        aDFacturado = " ";
        aHFacturado = "z";
    |FINSI;

    h_comun = 0;
    |SI #0#9 = "SI";
        |ABRE #2;
        |ABRE #3;
        |ABRE #4;
        |ABRE #5;
        |SELECT #2#5;

        |BUCLE asnomdat;
        |SI seleccio = 0;
            |SI empre_p = 0; |Y nSwSoloImp = 0;
                |MENAV "    Seleccion vacia ...";
            |FINSI;
        |FINSI;

        |CIERRA #2;
        |CIERRA #3;
        |CIERRA #4;
        |CIERRA #5;
    |FINSI;
|FPRC;

|PROGRAMA;
    |SI nSwSoloImp ! 0;
        #0#0 = empre_n;
        #0#1 = empre_n;
        #0#2 = mes_p;
        #0#3 = anyo_n;
        #0#5 = tipo_p;
        #0#7 = "    ";
        #0#8 = "zzzz";
        #0#9 = "SI";
        |HAZ tipo_3;
        |ACABA;
    |FINSI;

    |SI empre_f = 0;
        |CLS;
        |CABEZA "Pase Minut.Nominas";
        |DDEFECTO #0;
        |PINPA #0#0;
        |PINDA #0#0;
        |ENDATOS #1#0;
        |SI FSalida = 0;
            nTipoFactG=#0#5;
            |HAZ tipo_3;
        |FINSI;
    |SINO;
        #0#0 = empre_p;
        #0#1 = empre_p;
        #0#2 = mes_p;
        #0#3 = anyo_p;
        #0#4 = fecha_p;
        #0#5 = tipo_p;
        #0#6 = rete_p;
        #0#7 = d_tipo;
        #0#8 = h_tipo;
        #0#9 = "SI";
        |HAZ tipo_3;
    |FINSI;
|FPRO;
