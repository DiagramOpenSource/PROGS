|FICHEROS;
    exminuta #0;
    exintrod #1;    || expedientes
    extramit #2;    || tramites
    asclient #5;    || clientes
    asconcep #8;    || conceptos

    asenlcon #9;
    asempcon #10;
    asw00001 #99;

    ascabmin #20;   || minutas cabs.
    aslinmin #21;   || minutas lins.
    ascontro #22;   || control
    ascontrt #450;
|FIN;

|VARIABLES;
    alfa1 = "";
    alfa2 = "";
    alfa3 = "";
    alfa4 = "";
    para1 = 0;
    para2 = 0;
    para3 = 0;
    para4 = 0;
    fech1 = @;
    fech2 = @;
    fech3 = @;
    fech4 = @;
    elsw1 = 0;
    elsw2 = 0;
    elsw3 = 0;
    elsw4 = 0;
    FEstado = 0;
    seleccio = 0;
    modo = 0;

    c_clie    = "";
    fijo      = "";
    linea     = 0;
    iva       = 0;
    conver    = "";
    conver1   = "";
    conver2   = "";
|FIN;

|INCLUYE agicon_i;
____________________________________/
|PROCESO mayus; |TIPO 0;
    #0Campo = #0Campo > #0Campo;
|PINTA #0Campo;
|FPRC;
____________________________________/
|PROCESO graba_con;
|ABRE #9;
#9#0 = #21#4;
#9#1 = #21#5;
|LEE 000#9=;
|SI FSalida ! 0; |CIERRA #9; |ACABA; |FINSI;
|CIERRA #9;

|DDEFECTO #10;
|ABRE #10;
|LEE 000#10p;
|CIERRA #10;

    c_clie    = #21#1;
    c_clie    = ("00000" + c_clie) % -105;
    fijo    = #10#3; |QBF fijo;
    fijo    = fijo + c_clie;
    conver1 = #21#15 % 0102;
    conver2 = #21#15 % 0402;
    conver  = conver1 + conver2;
    c_fecha = conver;
    c_fecha = ("0000" + c_fecha) % -104;

|DDEFECTO #99;
|ABRE #99;
#99#0 = #21#0;
#99#1 = #21#1;
#99#2 = #21#2;
|LEE 101#99=;
    FEstado = FSalida;

    #99#3  = #10#1;
    #99#4  = #10#2;
    #99#5  = c_fecha;
    #99#6  = fijo;
    #99#7  = #9#2;
    #99#8  = #9#6;
    #99#9  = #9#3;
    #99#10 = #9#4;
    #99#11 = #21#13;
    #99#12 = #9#5;
    #99#13 = 0;
    #99#14 = "";

|SI FEstado = 0;    |GRABA 020#99a;     |FINSI;
|SI FEstado ! 0;    |GRABA 020#99n;     |FINSI;
|LIBERA #99;
|CIERRA #99;
|FPRC;

|PROCESO iva;
|ABRE #450;
#450#0 = #20#0;
|LEE 000#450=;
|SI FSalida ! 0; |DDEFECTO #450; |FINSI;
|CIERRA #450;

iva = #450#3;
|FPRC;

|PROCESO miralin;
    linea = #21#2;
|FPRC;

|PROCESO grabamin;
|DDEFECTO #20;
#20#0 = #1#27;      || tipo
#20#1 = #1#3;       || cliente
|LEE 101#20=;
|SI FSalida ! 0;
        linea = 1;
        #20#2 = #5#1;         || razon social
        #20#3 = #5#2;         || nombre
        #20#4 = #5#3;         || direccion
        #20#5 = #5#4;         || cod.postal 1
        #20#6 = #5#5;         || cod.postal 2
        #20#7 = #5#6;         || poblacion
        #20#8 = #5#7;         || provincia
        #20#9 = #5#8;         || cif
        #20#10 = #5#15;       || forma de pago
        #20#11 = #5#16;       || banco a remesar
        #20#12 = #5#17;       || retencion IRPF
        #20#13 = #5#11;       || banco
        #20#14 = #5#12;       || direccion banco
        #20#15 = #5#13;       || CCC
        #20#16 = #5#14;       || cta.cte.
        #20#20 = #5#23;       || descuento
        #20#30 = "Expediente Nro.: " + #1#0;      || observaciones
        linea = 5;
    |SI #22#32 = "S";         || saldo anterior S/N
        |SI #5#24 ! 0;
                #8#0 = #22#33;
                #8#1 = #22#34;
            |LEE 000#8=;
            |SI FSalida = 0; |Y #8#3 = "A";
                    #21#0  = #20#0;
                    #21#1  = #20#1;
                    #21#2  = linea;
                    #21#3  = 0;
                    #21#4  = #8#0;
                    #21#5  = #8#1;
                    #21#6  = #8#2;
                    #21#7  = #8#16;
                    #21#8  = #8#17;
                    #21#9  = #8#18;
                    #21#10 = #8#3;
                    #21#11 = 1;
                    #21#12 = -#5#24;
                    #21#13 = -#5#24;
                    #21#14 = "G";
                    #21#15 = "00.00.0000";
                |GRABA 020#21n;
                |HAZ graba_con;

                    #20#19 = #20#19 + #21#13;
                    #20#26 = #20#24 - #20#19 - #20#25;
                    linea = 5;
            |FINSI;
        |FINSI;
    |FINSI;
    |GRABA 020#20b;
        linea = linea + 5;
|SINO;
    |BUCLELIN 2miralin#20;
        linea = linea + 5;
|FINSI;
    #21#0 = #20#0;                                || tipo
    #21#1 = #20#1;                                || cliente
    #21#2 = linea;                                || linea
    alfa1 = #1#1 % 102;  nume1 = alfa1; || dia
    alfa2 = #1#1 % 402;  nume2 = alfa2; || mes
    #21#3 = (nume1 * 100) + nume2;                || fecha DDMM
    #21#4 = #1#10;                                || seccion
    #21#5 = #1#11;                                || concepto
    |DDEFECTO #8;   #8#0 = #21#4;  #8#1 = #21#5;  |LEE 000#8=;   || lee cto.
    #21#6  = #8#2;                                || descrip. ampliada
    #21#7  = #8#16;                               || descrip. 1
    #21#8  = #8#17;                               || descrip. 2
    #21#9  = #8#18;                               || descrip. 3
    #21#10 = #8#3;                                || tipo
    #21#11 = 1;                                   || unidades
|SI #1#28 = 1; #21#12 = #8#4; |FINSI;   || importe segun tarifa
|SI #1#28 = 2; #21#12 = #8#8; |FINSI;
|SI #1#28 = 3; #21#12 =#8#10; |FINSI;
|SI #1#28 = 4; #21#12 =#8#12; |FINSI;
|SI #1#28 = 5; #21#12 =#8#14; |FINSI;
    #21#13 = #21#11 * #21#12;                     || importe
    #21#14 = #8#3;                                || tipo
    #21#15 = #1#1;                                || fecha DD.MM.AAAA
|GRABA 020#21n;
|HAZ graba_con;

|SI #21#14 = "H";   #20#17 = #20#17 + #21#13;     |FINSI;
|SI #21#14 = "S";   #20#18 = #20#18 + #21#13;     |FINSI;
|SI #21#14 = "A";   #20#19 = #20#19 + #21#13;     |FINSI;

|HAZ iva;

    #20#21 = #20#17 % #20#20;
    #20#22 = #20#17 - #20#21;
    #20#23 = #20#22 % iva;
    #20#24 = #20#22 + #20#23 + #20#18;
|SI #20#12 = "S";
        #20#25 = #20#22 % #22#31;
|SINO;
        #20#25 = 0;
|FINSI;
    #20#26 = #20#24 - #20#19 - #20#25;

|GRABA 020#20a;
|LIBERA #20;
|FPRC;

|PROCESO expedientes1;
|DDEFECTO #5;  #5#0 = #1#3;                  |LEE 000#5=;   || cliente
|HAZ grabamin;

    #1#26 = "S";         || minutado SI
|GRABA 020#1a;
|SI FSalida = 0;
        seleccio = seleccio + 1;
|FINSI;
|LIBERA #1;
|FPRC;

|DEFBUCLE expedientes5;  || por clientes
 #1#1;
 1, 6, 2, 3, 4, 5, 10, 11, 12, 26;
 #0#1, #0#20, #0#3, #0#5, #0#7,  #0#9, #0#11, #0#13, #0#15, #0#17, "S", #0#19;
 #0#2, #0#21, #0#4, #0#6, #0#8, #0#10, #0#12, #0#14, #0#16, #0#18, "S", #0#19;
 be;
 NULL, NULL, NULL, NULL, NULL, expedientes1;
 #1#3, #1#0;
|FIN;

|PROCESO tipo3; |TIPO 3;
|SI #0#0 = "SI";
    |ABRE #22;
    |LEE 000#22p;
    |SI FSalida ! 0;  |DDEFECTO #22;  |FINSI;
    |CIERRA #22;


    |ABRE #5;
    |ABRE #8;
    |ABRET #20;
    |ABRET #21;
    |BUCLE expedientes5;      || por clientes
    |SI seleccio = 0;
            alfa1 = "    Seleccion vacia. Compruebe que los expedientes esten cerrados ...";
    |SINO;
        |SI #0#19 = "S";
            |SI seleccio > 1;
                    alfa1 = "    Minutados de nuevo " + seleccio + " expedientes ...";
            |SINO;
                    alfa1 = "    Minutado de nuevo " + seleccio + " expediente ...";
            |FINSI;
        |SINO;
            |SI seleccio > 1;
                    alfa1 = "    Minutados " + seleccio + " expedientes ...";
            |SINO;
                    alfa1 = "    Minutado " + seleccio + " expediente ...";
            |FINSI;
        |FINSI;
    |FINSI;
    |MENAV alfa1;
    |CIERRA #5;
    |CIERRA #8;
    |CIERRAT #20;
    |CIERRAT #21;
|FINSI;
|FPRC;
