|FICHEROS;
     abom0006 #0;
     abom0001 #6;
     abom0511 #511;
|FIN;

|VARIABLES;
     &nOpcion = 0;

     &enModo        = 0;
     nModo          = 0;

     nVariable      = 0;
     nForma         = 0;

     aHora          = "";
     aAlfa          = "";
     aTecla         = 0;

     &enDentro      = 0;

     &eaNombreList  = "";
     &enCuadroIni   = 0;
     &enCuadroFin   = 0;
     &enCampoAncho  = 0;
     &enModoEdita   = 0;
     &enTotalVaria  = 0;

{100}&epPuntEdita   = "";
|FIN;

|PROCESO PonHora;  |TIPO 7;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo ! 1;  |ACABA;  |FINSI;

     |HORA aHora;
     aAlfa = aHora % 102;  #0#2  = aAlfa;  |PINTA #0#2;
     aAlfa = aHora % 402;  #0#3  = aAlfa;  |PINTA #0#3;
     aAlfa = aHora % 702;  #0#18 = aAlfa;  |PINTA #0#3;
|FPRC;

|PROCESO CuadroM0006;  |TIPO 7;
     |PUSHV 0501 2380;

     |D_CUADRO 1425, 2177;
     |ATRI R;
     |PINTA 1526, "-0- Solo el dia de Aviso.  -X- Todos los Miercoles.";
     |PINTA 1626, "-T- Todos los Dias.        -J- Todos los Jueves.   ";
     |PINTA 1726, "-2- Dos Semanas Antes.     -V- Todos los Viernes.  ";
     |PINTA 1826, "-1- Una Semanas Antes.     -S- Todos los Sabados.  ";
     |PINTA 1926, "-L- Todos los Lunes.       -D- Todos los Domingos. ";
     |PINTA 2026, "-M- Todos los Martes.                              ";
     |ATRI 0;
|FPRC;

|PROCESO MayusM0006;
     #0Campo = #0Campo > #0Campo;
     |PINTA #0Campo;

     |SI Campo = 4;
         |C_M #0#5,  1, "S";
         |C_M #0#6,  1, "S";
         |C_M #0#7,  1, "S";
         |C_M #0#17, 1, "S";
         |SI #0#4 = "N";
             |C_M #0#5,  1, "N";  #0#5  = "01.01.0000";  |PINTA #0#5;
             |C_M #0#6,  1, "N";  #0#6  = 0;             |PINTA #0#6;
             |C_M #0#7,  1, "N";  #0#7  = 0;             |PINTA #0#7;
             |C_M #0#17, 1, "N";  #0#17 = " ";           |PINTA #0#17;
                                  #0#16 = "S";           |PINTA #0#16;
         |SINO;
                                  #0#16 = "N";           |PINTA #0#16;
         |FINSI;
     |FINSI;

     |SI Campo = 17;
         |POPV;
         |PINTA #0Campo;
     |FINSI;
|FPRC;

|PROCESO DentroM0006;  |TIPO 7;
     |SI Campo = 4;
         enDentro = 1;

         |SI #0#8 = 0;
             #0#8 = #6#5;     |PINTA #0#8;
             #0#9 = #6#6;     |PINTA #0#9;
         |FINSI;
     |FINSI;

     |SI Campo = 1;  enDentro = 0; |FINSI;
|FPRC;

|PROCESO EditaAsuntoM0006;  |TIPO 7;
     enTotalVaria  = 6;
     eaNombreList  = "";
     enCuadroIni   = 1743;
     enCuadroFin   = 2279;
     enCampoAncho  = 50;
     enModoEdita   = 0;

     IepPuntEdita  = epPuntEdita1 <-;
     |PARA nVariable = 10;  |SI nVariable [ 15;  |HACIENDO nVariable = nVariable + 1;
          epPuntEdita  = #0nVariable;
          IepPuntEdita = IepPuntEdita + 1;
     |SIGUE;

     |HAZ Edita;

     IepPuntEdita  = epPuntEdita1 <-;
     |PARA nVariable = 10;  |SI nVariable [ 15;  |HACIENDO nVariable = nVariable + 1;
          #0nVariable  = epPuntEdita;             |PINTA #0nVariable
          IepPuntEdita = IepPuntEdita + 1;
     |SIGUE;
|FPRC;

|| ************************************************************************
||                  PROCESO PARA PONER EN COLORES LAS LINEAS
|| ************************************************************************

|PROCESO Tipo5M0006;  |TIPO 5;
     #0#0  = #6#0;
     #0#1  = "01.01.0000";
     #0#2  = 0;
     #0#3  = 0;
     #0#18 = 0;
     |LEE 040#0];
     |SI FSalida = 0;  |Y #0#0 = #6#0;
         |SI #0#16 = "S";
             |LINEAS_BI_ATRI #0#16, N, I, 179;
         |SINO;
             |LINEAS_BI_ATRI #0#16, I, N, 179;
         |FINSI;
     |SINO;
         |LINEAS_BI_ATRI #0#16, I, N, 179;
     |FINSI;
|FPRC;

|PROCESO MiraTec06; |TIPO 11;
   |SI FSalida ] 10; |Y FSalida [ 15;
      |PTEC 807;
      |SI FSalida = 1;  nOpcion = 1; |FINSI;
      |SI FSalida = 10; nOpcion = 1; |FINSI;
      |SI FSalida = 11; nOpcion = 2; |FINSI;
      |SI FSalida = 12; nOpcion = 3; |FINSI;
      |SI FSalida = 13; nOpcion = 4; |FINSI;
      |SI FSalida = 14; nOpcion = 5; |FINSI;
      |SI FSalida = 15; nOpcion = 6; |FINSI;
   |FINSI;
|FPRC;

|PROCESO Tipo2m6;  |TIPO 2;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 3;
         |CONFI "2400NSeguro de dar de Baja la Agenda.";
         |SI FSalida ! 0;
             |ERROR;
         |FINSI;
     |FINSI;

     |SI #0#21 = "          ";
         #0#21 = Usuario % 810;

         |ABRE #511;
         |SELECT #2#511;
         #511#0 = 0;
         #511#1 = #0#21;
         |LEE 040#511];
         |SI FSalida = 0;  |Y #511#1 = #6#21;
             #0#22 = #511#2;
         |FINSI;
         |CIERRA #511;
     |FINSI;
|FPRC;
