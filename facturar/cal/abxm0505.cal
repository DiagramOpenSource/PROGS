|FICHEROS;
     abom0500;
     abxm0505 #505;
     abom0511 #511;
     abom0514 #514;
     abxm0515 #515,MANTE;
     abom0508 #508;

     abxm0020 #520;
     abxm0021 #521;
|FIN;

|VARIABLES;
     nVariable      = 0;
     nMail          = 0;
     nContenido     = 0;
     nGrid          = -1;
     nRango         = 0;
     nModo          = 0;
     nUsuario       = 0;
     nElemento      = 0;
     nBufferUsr     = 0;
     nSubVent       = 0;
     nError         = 0;
     nCampo         = 0;

     {-1}aVent      = "";
         aVent1     = 0;
         aVent2     = 0;
         aVent3     = 0;
         aVent4     = 0;
         aVent5     = "";

     aAlfa          = "";
     aUsuario       = "";
     aBase          = "";
     aMas           = "";
     aDBase         = "";
     aTexto         = "";

     &aNif          = "";

     &enCalcCif     = 0;
     &eaVarDni      = "";
     &aCadena       = "";
     &aPassword     = "";
|FIN;

|PROCESO Tipo3Fxm505;  |TIPO 3;
     |ABRE #514;
     |LEE 040#514p;
     |SI FSalida ! 0;  |DDEFECTO #514;  |FINSI;
     |CIERRA #514;

     |SI #514#1 ! "S";  |ACABA;  |FINSI;

     |ABRE #515;
     |SELECT #2#515;
     #515#1 = #505#1;
     |LEE 040#515=;
     |SI FSalida = 0;  |CIERRA #515;  |ACABA;  |FINSI;
     |CONFI "     Desea crearlo en la Libreta de Direcciones";
     |SI FSalida ! 0;  |CIERRA #515;  |ACABA;  |FINSI;

     |SELECT #1#515;
     |LEE 040#515u;

     nMail = #515#0 + 1;

     |DDEFECTO #515;
     #515#0 = nMail;
     |GRABA 020#515b;

     #515#1  = #505#1;
     #515#4  = #505#5;
     #515#6  = #505#6;
     #515#7  = #505#7;
     #515#8  = #505#8;
     #515#9  = #505#9;
     #515#10 = "TELEFONO PRINCIPAL";
     #515#11 = #505#10;
     #515#12 = "MOVIL";
     #515#13 = #505#12;
     #515#14 = "FAX";
     #515#15 = #505#11;
     #515#16 = #505#13;
     #515#18 = #505#14;
     #515#20 = #505#21;

     |VENTANA 0501, 2799, -1, 16, "Libreta de direcciones";
     nSubVent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nSubVent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |PINPA #0#515;
     |PINDA #0#515;
     |ENDATOS #1#515;
     |SI FSalida = 0;
         |GRABA 020#515a;
     |SINO;
         |BORRA 020#515a;
     |FINSI;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nSubVent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA nSubVent;
|FPRC;

|PROCESO PidePasswordFxm508;
     nError = 1;

     |ABRE #508;
     |LEE 000#508p;
     |SI FSalida ! 0;  |DDEFECTO #508;  |FINSI;
     |CIERRA #508;

     |SI #508nCampo = "          ";
         nError = 0;
         |ACABA;
     |FINSI;

     aTexto = "Password del administrador";

     |VENTANA 1330, 1770, -1, 16, aTexto;
     nSubVent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nSubVent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     E_inf     = "";
     E_sup     = "10";
     aPassword = "";
     aPassword = 1545 ? -1;
     |HAZ PonPassword;

     |SI aCadena ! #508nCampo;
         |MENAV "2300 Password incorrecto. No puedes modificar el campo.";
     |SINO;
         nError = 0;
     |FINSI;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nSubVent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA nSubVent;
|FPRC;

|PROCESO Tipo0C22Fxm505;  |TIPO 0;
     |SI #505#22 = Contenido;  |ACABA;  |FINSI;

     nCampo = 5;
     |HAZ PidePasswordFxm508;
     |SI nError = 1;
         #505#22 = Contenido;  |PINTA #505#22;
         |ERROR;
         |ACABA;
     |FINSI;
|FPRC;

|PROCESO LetraAut505;  |TIPO 6;
     |SI FSalida = 10;
         enCalcCif = 1;
         eaVarDni  =  #505Campo;
         |HAZ CalculoDNI;
         #505Campo = eaVarDni;
         |PINTA #505Campo;
         |ERROR;
         |ACABA;
     |FINSI;

     eaVarDni  = #505Campo;
     enCalcCif = 0;
     |HAZ CalculoDNI;
     eaVarDni = (eaVarDni + "            ") % 112;
     |SI #505Campo ! eaVarDni;
         |CONFI "0000SEl Nif puede ser Erroneo. Desea Calcularlo.";
         |SI FSalida = 0;
             #505Campo = eaVarDni;  |PINTA #505Campo;
             |ERROR;
             |ACABA;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO Tipo0C6Fxm505;
     nContenido = Contenido;
     |SI #505#6 = nContenido;  |ACABA;  |FINSI;

     |ABRE #520;
     #520#0 = #505#6;
     |LEE 000#520=;
     |SI FSalida ! 0;  |DDEFECTO #520;  |FINSI;
     |CIERRA #520;

     #505#9 = #520#1;  |PINTA #505#9;
|FPRC;

|PROCESO Tipo0C7Fxm505;
     nContenido = Contenido;
     |SI #505#7 = nContenido;  |ACABA;  |FINSI;

     |ABRE #521;
     #521#0 = #505#6;
     #521#1 = #505#7;
     |LEE 000#521=;
     |SI FSalida ! 0;  |DDEFECTO #521;  |FINSI;
     |CIERRA #521;

     #505#8 = #521#3;  |PINTA #505#8;
|FPRC;

|PROCESO Baja511;
     #511#0 = #505#0;
     #511#1 = "          ";
|FPRC;

|PROCESO Alta511;
     #511#0 = #505#0;
     #511#1 = "zzzzzzzzzz";
|FPRC;

|PROCESO Tipo13Fxm505;  |TIPO 13;
     |SI nGrid ! -1;
         |REFRESCACONTROL nGrid;
         |ACABA;
     |FINSI;

     nRango = 1 + 4 + 8 + 16 + 32;

     |LINEAL_SIMPLE #1#511, nRango, 2325, 2798, Baja511, Alta511, NULL;
     nGrid  = FSalida;
|FPRC;

|PROCESO Tipo0C0Fxm505;
     |REFRESCACONTROL nGrid;
|FPRC;

|PROCESO Usuario;
     |ABRE #abom0500;
     #abom0500#0 = #abom0511#1;
     |LEE 000#abom0500.=;
     |SI FSalida ! 0;
         |MENAV "      Usuario Inexistente";
         |ERROR;
         |ACABA;
     |FINSI;

     aAlfa = #abom0511#2;  |QBF aAlfa;
     |SI aAlfa = "";
         #abom0511#2 = #abom0500#1;
     |FINSI;
|FPRC;

|PROCESO Tipo80Fxm505;  |TIPO 80;
     |SUB_EJECUTA "abom0500;ALTA";

     |ABRET #511;

     FSalida = 2799;
     |HAZ DesactivaTipo80;
|FPRC;

|PROCESO Tipo90Fxm505;  |TIPO 90;
     |CIERRAT #511;
|FPRC;
