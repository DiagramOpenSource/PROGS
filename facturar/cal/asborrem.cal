|FICHEROS;
     ascabrem #0;
     aslinrem #1;
     asborrem #2;

     ashisrec #4;
     ashismic #5;
     asparcia #6;

     &bancosp@ #706;
|FIN;

|VARIABLES;
     fFech        = @;
     qFech        = "";
     dFech        = "";
     hFech        = "";
     mes          = "";
     mes_num      = 0;
     campo        = 0;
     cuenta_cob   = "";
     aDPase       = "";
     aHPase       = "";
     nError       = 0;
     nTotal       = 0;
     &enCodBanco  = 0;
     &enSwDeDonde = 0;
|FIN;

|PROCESO asparcia;
     |SI #6#14 = #1#0; |Y #6#15 = #1#1;
         |BORRA 020#6a;
     |FINSI;
     |LIBERA #6;
|FPRC;

|DEFBUCLE asparcia;
     #6#1;
     ;
     #1#2, #1#3, 000;
     #1#2, #1#3, 999;
     be;
     NULL, asparcia;
|FIN;

|PROCESO aslinrem;
     |ABRE #6;
     |DDEFECTO #6;
     #6#0 = #1#2;
     #6#1 = #1#3;
     |LEE 101#6];
     |PARA; |SI FSalida = 0; |Y #6#0  = #1#2; |Y #6#1  = #1#3; |HACIENDO;
        |SI #6#14 = #1#0; |Y #6#15 = #1#1; |SAL; |FINSI;
        |LEE 000#6s;
     |SIGUE;
     |SI #6#0 = #1#2; |Y #6#1 = #1#3;
        |SI #6#14 ! #1#0; |O #6#15 ! #1#1;
           |CIERRA #6;
           nTotal = nTotal + #1#7;
           nError = 1;
           |ACABA;
        |FINSI;
     |SINO;
        |CIERRA #6;
        nTotal = nTotal + #1#7;
        nError = 1;
        |ACABA;
     |FINSI;
     |CIERRA #6;

     #4#0 = #1#2;
     #4#1 = #1#3;
     |LEE 101#4=;
     |SI FSalida = 0;
         #4#9  = #4#9  - #1#7;
         #4#15 = #4#15 - #1#7;  ||importe remesado

         |SI #4#9 = 0;
             #4#12 = "N";
         |SINO;
             #4#12 = "M";
         |FINSI;

         #4#10 = "N";
         |GRABA 021#4a;
     |FINSI;
     |LIBERA #4;

     #5#0 = #1#2;
     #5#1 = #1#3;
     |LEE 101#5=;
     |SI FSalida = 0;
         #5#32 = #4#10;   ||Cobrado Total
         #5#33 = #4#12;   ||Contabilizado
         |GRABA 021#5a;
     |FINSI;
     |LIBERA #5;

     |BUCLE asparcia;

     mes     = #5#29 % 402;
     mes_num = mes;

     enCodBanco = #5#13;
     |HAZ LeeBancoPropio;
     |SI enSwDeDonde = 0;
         |ABRE #706;
         #706#0 = #5#13;
         |LEE 101#706=;
         |SI FSalida = 0;
             #706#20 = #706#20 + #1#7;
             #706#22 = #706#22 + #1#7;
             campo = 7 + mes_num;
             #706campo = #706campo + #1#7;
             |GRABA 020#706a;
             |LIBERA #706;
         |FINSI;

         #706#0 = #0#1;
         |LEE 140#706=;
         |SI FSalida = 0;
             #706#20 = #706#20 - #1#7;
             #706#22 = #706#22 - #1#7;
             campo = 7 + mes_num;
             #706campo = #706campo - #1#7;
             |GRABA 020#706a;
         |FINSI;
         |CIERRA #706;
     |FINSI;

     |BORRA 020#1a;
     |LIBERA #1;
|FPRC;

|CALCULO borrcab;
     nError = 0;
     nTotal = 0;
     |ABRE #4;
     |ABRE #5;
     |BUCLELIN 0aslinrem#0;
     |CIERRA #4;
     |CIERRA #5;

     |SI nError = 0;
        |BORRA 020#0a;
     |SINO;
        #0#4 = nTotal;
        |GRABA 020#0a;
     |FINSI;
     |LIBERA #0;
|FCAL;

|DEFBUCLE caborrem0;
     #0#1;
     1, 3, 5;
     #2#0, #2#2, #2#4, aDPase;
     #2#1, #2#3, #2#5, aHPase;
     ;
     NULL, borrcab;
|FIN;

|CALCULO borrado;|TIPO 3;
                     aDPase = " ";  aHPase = "z";
    |SI #2#6 = "S";  aDPase = "X";  aHPase = "X";  |FINSI;
    |SI #2#6 = "N";  aHPase = " ";  |FINSI;
    |ABRE #1;
    |BUCLE caborrem0;
    |CIERRA #1;
|FCAL;

|PROCESO sacaano; |TIPO 7;
     fFech = ~;
     qFech = fFech % -104;
     dFech = "01.01." + qFech;
     hFech = "31.12." + qFech;
     #2#4  = dFech;
     #2#5  = hFech;
     |PINTA #2#4;
     |PINTA #2#5;
|FPRC;
