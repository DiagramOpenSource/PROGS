|FICHEROS;
    asexpsi0 #0;
    asexpsi1 #1;    || temporal gestion
    asexpcab #2;    || expedientes simples cabs.
    asexplin #3;    || expedientes simples lins.
    asclient #4;    || clientes
    asconcep #5;    || conceptos
    asexpeca #6;    || expedientes cabs.
    asexpeli #7;    || expedientes lins.
|FIN;

|VARIABLES;
    alfa1 = "";
    alfa2 = "";
    alfa3 = "";
    alfa4 = "";
    nume1 = 0;
    nume2 = 0;
    nume3 = 0;
    nume4 = 0;
    para1 = 0;
    para2 = 0;
    para3 = 0;
    para4 = 0;
    fech1 = @;
    fech2 = @;
    fech3 = @;
    fech4 = @;
    FEstado = 0;
    guia = 0;
    otravez = 0;
    modo = 0;
    linea = 0;
    fecalf = "";
    campo0 = "";
    campo1 = "";
    campo2 = "";
    campo3 = "";
    campo4 = "";
    campo5 = "";
    d_exp = 0;
    h_exp = 0;
|FIN;
____________________________________/ PROGRAMA
|PROGRAMA;
|CLS;
|CABEZA "Gestion Exp.Simples";
|DDEFECTO #0;
|PINPA #0#0;
|PINDA #0#0;
|PARA; |SI otravez = 0; |HACIENDO;
    |ENDATOS #1#0;
    |SI FSalida = 0;
            d_exp = #0#3 * 100;
            h_exp = (#0#4 * 100) + 99;
        |HAZ gestion;
    |FINSI;
        otravez = FSalida;
|SIGUE;
|FPRO;
____________________________________/ CALCULOS PANTALLA
|CALCULO defecto; |TIPO 0;
    fech1 = ~;
    alfa1 = fech1;
    alfa1 = alfa1 % -104;
    alfa2 = "31.12." + alfa1;      #0#2 = alfa2;
    nume1 = alfa1;
    nume1 = nume1 - 3;
    alfa1 = nume1;
    alfa2 = "01.01." + alfa1;      #0#1 = alfa2;       || tres a¤os atras
|PINTA #0#1;
|PINTA #0#2;
|FCAL;
____________________________________/ CALCULOS GESTION
|CALCULO lee_lineas1;
    guia = guia + 10;
|DDEFECTO #1;
    #1#0 = guia;         || guia
    #1#1 = #3#1;         || expediente
    #1#2 = #3#2;         || contrario
    #1#3 = #3#3;         || fecha
    #1#4 = #3#4;         || clase
    #1#5 = #3#5;         || tipo
|GRABA 020#1n;
|FCAL;

|DEFBUCLE lee_lineas;
 #3#2;
 ;
 #2#0, #0#2, h_exp;
 #2#0, #0#1, d_exp;
 e;
 NULL, lee_lineas1;
|FIN;

|CALCULO gestion;
    guia = 0;
|ABRE #1;  |CIERRA #1;   |DELINDEX #1;
|ABRE #1;
|ABRE #2;
    #2#0 = #0#0;
|LEE 001#2=;
|SI FSalida = 0;
    |BUCLE lee_lineas;
|FINSI;
|PUSHV 05012380;
|PINPA #0#1;
|ATRI I;
|PINTA 0623, #4#0;
|PINTA 0631, #4#1;
|ATRI 0;
|ENTLINEAL #1#1, 1, 1, 22, 0, min, max;
|POPV;
|CIERRA #2;
|CIERRA #1;
|FCAL;

|CALCULO min;
    #1#0 = 1;
|FCAL;

|CALCULO max;
    #1#0 = 9999999;
|FCAL;
____________________________________/ CALCULOS ENLACE MANTENIMIENTO
|CALCULO existe; |TIPO 0;
    modo = (FEntrada / 100) ! 0;
|ABRE #3;
    #3#0 = #0#0;    || cliente
    #3#1 = #1#1;    || expediente
|LEE 001#3=;
|SI FSalida = 0; |Y modo = 1;
        nume1 = #1#1 $ 100;
    |SI nume1 ! 0;
        |MENAV "    ATENCION!!! Nro. de Expediente EXISTENTE ...";
        |ERROR;
    |SINO;
            nume1 = #1#1 / 100;
            nume2 = nume1;
        |PARA; |SI FSalida = 0;|Y nume1 = nume2; |HACIENDO;
                #1#1 = #3#1;
            |LEE 001#3s;
                nume2 = (#3#1 - (#3#1 $ 100)) / 100;
        |SIGUE;
            #1#1 = #1#1 + 1;
        |PINTA #1#1;
    |FINSI;
|FINSI;
|SI FSalida ! 0; |Y modo ! 1;
    |MENAV "    ATENCION!!! Nro. de Expediente INEXISTENTE ...";
    |ERROR;
|FINSI;
|CIERRA #3;
|FCAL;

|CALCULO enlaza; |TIPO 2;
|HAZ enl_cabs;
    modo = (FEntrada / 100) ! 0;
|SI modo = 1;  |HAZ enl_alta;  |FINSI;
|SI modo = 2;  |HAZ enl_modi;  |FINSI;
|SI modo = 3;  |HAZ enl_baja;  |FINSI;
|FCAL;

|CALCULO enl_cabs;
|ABRE #2;
    #2#0 = #0#0;    || cliente
|LEE 001#2=;
|SI FSalida ! 0;
    |GRABA 020#2n;
|FINSI;
|CIERRA #2;
|FCAL;

|CALCULO enl_alta;
|ABRE #3;
|DDEFECTO #3;
    #3#0 = #0#0;    || cliente
    #3#1 = #1#1;    || expediente
    #3#2 = #1#2;    || contrario
    #3#3 = #1#3;    || fecha
    #3#4 = #1#4;    || clase
    #3#5 = #1#5;    || tipo
|GRABA 020#3n;
|SI FSalida = 0;
    |HAZ graba_exp;
|FINSI;
|CIERRA #3;
|FCAL;

|CALCULO enl_modi;
|ABRE #3;
|DDEFECTO #3;
    #3#0 = #0#0;    || cliente
    #3#1 = #1#1;    || expediente
|LEE 121#3=;
    #3#2 = #1#2;    || contrario
    #3#3 = #1#3;    || fecha
    #3#4 = #1#4;    || clase
    #3#5 = #1#5;    || tipo
|GRABA 020#3a;
|LIBERA #3;
|CIERRA #3;
|FCAL;

|CALCULO enl_baja;
|ABRE #3;
|DDEFECTO #3;
    #3#0 = #0#0;    || cliente
    #3#1 = #1#1;    || expediente
|BORRA 020#3c;
|CIERRA #3;
|FCAL;

|CALCULO masdiez; |TIPO 7;
    modo = (FEntrada / 100) ! 0;
|SI modo = 1; |Y FSalida ! 1; |Y FSalida ! 7;
        #1#0 = #1#0 + 9;
    |PINTA #1#0;
|FINSI;
|FCAL;

|CALCULO noescape; |TIPO 0;
|SI FSalida = 1;
    |ERROR;
|FINSI;
|FCAL;
____________________________________/ CALCULOS ENLACE EXPEDIENTES
|CALCULO graba_exp; |TIPO 3;
    fecalf = "";
    campo0 = #3#0;  campo1 = #3#1;  campo2 = #3#2;
    campo3 = #3#3;  campo4 = #3#4;  campo5 = #3#5;
    nume1 = #3#1 $ 100;        || nro. de expediente
|SI nume1 = 0;                 || si estamos en el expediente inicial
        fecalf = #3#3;
|SINO;
        #3#1 = ((#3#1 / 100) ! 0) * 100;
    |LEE 001#3=;
    |SI FSalida ! 0;
        |MENAV "    ATENCION!!! No existe expediente inicial ...";
    |SINO;
            fecalf = #3#3;
    |FINSI;
|FINSI;
    #3#0 = campo0;  #3#1 = campo1;  #3#2 = campo2;
    #3#3 = campo3;  #3#4 = campo4;  #3#5 = campo5;
|SI fecalf ! "";
        alfa1 = fecalf % -102;      || saca a¤o
        nume1 = (#3#1 / 100) ! 0;   || saca expediente
    |HAZ grabalo;
|SINO;
    |ERROR;
|FINSI;
|FCAL;

|CALCULO grabalo;
|HAZ concepto;
|ABRET #6;
|ABRET #7;
    linea = 0;
    #6#0  = alfa1;       || a¤o
    #6#14 = nume1;       || expediente
|LEE 001#6=;
|SI FSalida = 0;
    |BUCLELIN 2cuenta#6;
|SINO;
    |DDEFECTO #6;
        #6#00 = alfa1;   || a¤o
        #6#01 = #3#0;    || cliente
        #6#02 = #4#2;    || nombre cliente
        #6#03 = #3#3;    || fecha apertura
        #6#13 = #3#2;    || contrario
        #6#14 = nume1;   || expediente
    |GRABA 020#6n;
|FINSI;
|DDEFECTO #7;
    #7#00 = #6#0;        || a¤o
    #7#01 = #6#1;        || cliente
    #7#02 = linea + 1;   || linea
    alfa1 = #3#3;  alfa1 = alfa1 % 102;  nume1 = alfa1;
    alfa2 = #3#3;  alfa2 = alfa2 % 402;  nume2 = alfa2;
    #7#03 = (nume1 * 100) + nume2;      || fecha
    #7#04 = #5#0;        || seccion
    #7#05 = #5#1;        || concepto
    #7#06 = #5#2;        || descr. concepto
    #7#07 = #5#16;       || descr. concepto 1
    #7#08 = #5#17;       || descr. concepto 2
    #7#09 = #5#18;       || descr. concepto 3
    #7#16 = #6#14;       || contador
|GRABA 020#7n;
|CIERRAT #6;
|CIERRAT #7;
|FCAL;

|CALCULO cuenta;
    linea = #7#2;
|FCAL;

|CALCULO concepto;
|ABRE #5;
|DDEFECTO #5;
    #5#0 = "NOTA";
    #5#1 = "01";
|LEE 001#5=;
|SI FSalida ! 0;
        #5#2 = "<CONCEPTO INEXISTENTE>";
    |GRABA 020#5n;
|FINSI;
|CIERRA #5;
|FCAL;
