|FICHEROS;
     abxz0061 #961;

     abom0020 #20;
     abom0505 #505;
     abom0511 #511;
|FIN;

|VARIABLES;
     aAbre          = "";
     aBase          = "";
     aAlfa          = "";
     aAlfa1         = "";
     aEsc1          = "";
     aEsc2          = "";
     aRetu          = "";
     aTecla         = "";
     aFichero       = "";
     aCarga         = "";
     aQueQuiero     = "";
     aTexto         = "";
     aLong          = "";
     aCarac         = "";
     aRefresca      = "";
     aBusca         = "";
     aAdministrador = "";

     nBotonLabel    = 0;
     nHandle        = 0;
     nTree          = -1;
     nPanta         = 0;
     nLong          = 0;
     nCargar        = 0;
     nPos           = 0;
     nPosi          = 0;
     nBoton1        = 0;
     nBoton2        = 0;
     nBoton3        = 0;
     nSubVent       = 0;
     nCampo         = 0;
     nTCampo        = 0;
     nCarpeta       = 0;
     FEstado        = 0;
     nPersona       = 0;
     nConta1        = 0;
     nConta2        = 0;
     nConta3        = 0;
     nArbol         = 0;

     fFecha         = @;

     {-1}sTree      = 0;
     sT_nID         = 0;
     sT_nOper       = 0;
     sT_aData       = "";

     {-1}aVent      = "";
         aVent1     = 0;
         aVent2     = 0;
         aVent3     = 0;
         aVent4     = 0;
         aVent5     = "";

     &eaRutaOrigen  = "";
     &eaRutaDestino = "";
     &eaFicheroTxt  = "";
     &eaFicheroTgz  = "";
     &eaFicheroTar  = "";
     &eaAlfa        = "";
     &eaUnidadBasico = "";
|FIN;

|PROCESO CargaArbol;
     |QBF aAlfa;
     aAlfa = aAlfa + &13 + &10;
     |XGRABA nHandle, aAlfa;
|FPRC;

|PROCESO abom0020;
     |SI #961#2 ! "T";
         |SI #20#7 ! #961#2;  |ACABA;  |FINSI;
     |FINSI;

     |SI aAdministrador = "N";
         #505#0 = #20#0;
         |LEE 000#505=;
         |SI FSalida ! 0;  |ACABA;  |FINSI;

         aAlfa = Usuario % 810;
         aAlfa = (aAlfa + "          ") % 110;

         #511#0 = #505#0;
         #511#1 = aAlfa;
         |LEE 000#511=;
         |SI FSalida ! 0;  |ACABA;  |FINSI;
     |FINSI;

     |SI nArbol = 1;
         |SI nPersona ! #20#0;
             nConta1 = nConta1 + 1;
             nConta2 = 0;
             nConta3 = 0;
             fFecha  = "01.01.0000";

             aAlfa = nConta1 + ":" + (("00" + #20#0) % -102) + " - " + #20#4;  |QBF aAlfa;
             |HAZ CargaArbol;
         |FINSI;

         |SI fFecha ! #20#1;
             nConta2 = nConta2 + 1;
             nConta3 = 0;
             aAlfa = nConta1 + "," + nConta2 + ":Fecha " + #20#1;  |QBF aAlfa;
             |HAZ CargaArbol;
         |FINSI;

         |SI #20#7 = "N";
             aAlfa1    = "(No leido) ";
         |SINO;
             aAlfa1    = "(Leido) ";
         |FINSI;

         nConta3 = nConta3 + 1;
         aAlfa = nConta1 + "," + nConta2 + "," + nConta3 + ":";
         aAlfa = aAlfa + aAlfa1 + "Expediente " + #20#3 + " Documento " + #20#5 + " - " + #20#6;
         |HAZ CargaArbol;
     |SINO;
         |PRINT;
     |FINSI;

     nPersona = #20#0;
     fFecha   = #20#1;
|FPRC;

|DEFBUCLE abom0020;
     #20#1;
     ;
     00, #961#0, "        ";
     99, #961#1, "zzzzzzzz";
     ;
     NULL, abom0020;
|FIN;

|PROCESO Imprimir;
     |IMPRE 0;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |INFOR "abxz0061";
     |SI FSalida ! 0;  |FINIMP;  |ACABA;  |FINSI;

     nArbol   = 0;
     nPersona = -1;

     |ABRE #505;
     |ABRE #511;
     |SELECT #1#511;
     |BUCLE abom0020;
     |CIERRA #505;
     |CIERRA #511;

     |PIEINF;
     |FININF;
     |FINIMP;
|FPRC;

|PROCESO MontaArbol;
     aAdministrador = "N";
     aAlfa          = Usuario % 810;
     aAlfa          = (aAlfa + "          ") % 110;

     |ABRE #505;
     |ABRE #511;
     |SELECT #2#511;
     |PARA nCampo = 1;  |SI nCampo [ 99;  |HACIENDO nCampo = nCampo + 1;
           #511#1 = aAlfa;
           #511#0 = nCampo;
           |LEE 000#511=;
           |SI FSalida = 0;
               #505#0 = #511#0;
               |LEE 040#505=;
               |SI FSalida ! 0;  |DDEFECTO #505;  |FINSI;

               |SI #505#22 = "S";
                   aAdministrador = "S";
                   |SAL;
               |FINSI;
           |FINSI;
     |SIGUE;
     |CIERRA #511;
     |CIERRA #505;

     |SI nTree ! -1;
         |FIN_CONTROL_WINDOWS nTree;
         nTree = -1;
     |FINSI;

     |CONTROL_SIMPLE 0, "LABEL,{{5}}Cargando informaci¢n ...", 1315, 1680, NULL;
     nBotonLabel = FSalida;

     |PTEC 802;  |PAUSA;

     |DBASE aBase;  |QBF aBase;
     aAlfa = aBase + "basura";          |MKDIR aAlfa;

     eaRutaOrigen  = aBase + "basura/";
     eaRutaDestino = eaUnidadBasico + "\documentos";           |RMKDIR eaRutaDestino;
     eaRutaDestino = eaUnidadBasico + "\documentos\tmp";       |RMKDIR eaRutaDestino;
     eaRutaDestino = eaUnidadBasico + "\documentos\tmp\";
     eaFicheroTxt  = "abxz006101" + Usuario + ".txt";
     eaFicheroTgz  = "abxz006101" + Usuario + ".tgz";
     eaFicheroTar  = "abxz006101" + Usuario + ".tar";

     aAbre = eaRutaOrigen + eaFicheroTxt;

     sT_nID   = nTree;
     sT_nOper = 0;

     |XABRE "WB", aAbre, nHandle;

     nConta1  = 0;
     nConta2  = 0;
     nConta3  = 0;
     nArbol   = 1;
     nPersona = -1;

     |ABRE #505;
     |ABRE #511;
     |SELECT #1#511;
     |BUCLE abom0020;
     |CIERRA #505;
     |CIERRA #511;

     |XCIERRA nHandle;

     |SI nConta1 ! 0;
         |HAZ TraeTxtComprimido;
         aAlfa = eaRutaDestino + eaFicheroTxt;

         eaAlfa = "TREECTRL,Entradas {{" + aAlfa + "}}";
         |CONTROL_SIMPLE nPanta, eaAlfa, 0502, 2598, NULL;
         nTree = FSalida;

         |CONTROL_SIMPLE 0, "BOTON,{{205}} Imprimir", 2602, 2715, Imprimir;
     |SINO;
         |MENAV "0000 Selecci¢n vac¡a.";
     |FINSI;

     eaFicheroTxt  = "abxz006101" + Usuario + ".txt";
     aAlfa         = eaRutaDestino + eaFicheroTxt;
     |RFBORRA aAlfa;

     |FIN_CONTROL_WINDOWS nBotonLabel;

     |SI nConta1 = 0;  |ACABA;  |FINSI;

     |SI aBusca ! "";
         sT_nID   = nTree;
         sT_nOper = 3;
         sT_aData = "[" + aBusca;
         |ESPECIFICA "COMMANDCONTROL", sTree;
         aRefresca = ":" + FSalida;
     |FINSI;

     sT_nID   = nTree;
     sT_nOper = 2;
     |SI aRefresca = "";
         sT_aData = "1:";
     |SINO;
         sT_aData = aRefresca;
     |FINSI;

     |ESPECIFICA "COMMANDCONTROL", sTree;
     |PULSATECLA;

     |FOCOTECLADO nTree;
|FPRC;

|PROCESO Tipo12Fxz61;  |TIPO 12;
     |HAZ LeeUnidadBasico;

     |VENTANA 0501, 2799, -1, 17, "Control de entradas de correos a expedientes";
     nSubVent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nSubVent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |HAZ MontaArbol;

     |SI nConta1 ! 0;
         aEsc1  = &255 + "806";
         aEsc2  = &255 + "807";
         aRetu  = &255 + "802";
         |PARA; |SI;  |HACIENDO;
             FSalida = "::{{001}}Salir," + nTree;
             |LEETECLA aTecla;
             |SI aTecla = aEsc1; |SAL;  |FINSI;
             |SI aTecla = aEsc2; |SAL;  |FINSI;
             |SI aTecla = aRetu; |SAL;  |FINSI;
         |SIGUE;

         |FIN_CONTROL_WINDOWS nTree;
     |FINSI;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nSubVent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA nSubVent;
|FPRC;

|PROCESO Tipo80Fxz61;  |TIPO 80;
     FSalida = 2799;
|FPRC;
