      -------------------------------------------------------------
      ___________________..ENLACE CON contagen..____________________
      _____________________________________________________________

|FICHEROS;
     :/contagen/def/camandia #1080;  || diarios (contagen)
     :/contagen/def/cacuenta #1085;  || cuentas (contagen)
     :/contagen/def/catradia #1088;  || trabajo act. cuentas (facges)
     :/contagen/def/caconasi #1090;  || contador documentos (contagen)
     :/contagen/def/camaasic #1091;  || apuntes cabs. (contagen)
     :/contagen/def/camaasil #1092;  || apuntes lins. (contagen)
     :/contagen/def/cacuebal #1093;  || Cuentas Balance
     :/contagen/def/canempre #1095;  || empresas contables (contagen)

     asempcon #1094;  || empresas contables (facges)

     asw00001 #99;  || trabajo de enlace (facges)
|FIN;

|VARIABLES;
     enlace_fich = "";
     enlace_empr = "";
     sw_enlace = 0;
     Fenlaza = 0;
     fecha1 = @;
     enfa1 = "";
     enfa2 = "";
     enfa3 = "";
     enfa4 = "";
     nume1 = 0;
     nume2 = 0;
     nume3 = 0;
     nume4 = 0;
     ult_apunt = 0;
     c_fecha = "";
     digcu = 0;
     conte = "";
     signe = "";
     digcu1 = 0;
     digcu2 = 0;
     &path = "";
     &path1 = "";
     &dig1 = 0;
     &dig3 = 0;
     &dig4 = 0;
     &dig5 = 0;
     &dig6 = 0;
     &dig7 = 0;
     &dig8 = 0;
     &dig9 = 0;
     &fecalf = "";
     &ini = 0;
     &mes = "";
     &mesfec = 0;
     &mesac = 0;
     &a = 0;
     &b = 0;
     &c = 0;
     &modcon = 0;
     &codcon = 0;
     &importe = 0;
     &ressum = 0;
     &opera = "";
     d_diari = 0;
     d_opera = "";
     d_impor = 0;
     sw_rup = 0;

     nElCliente = 0;
     dia_ant = 0;
     fec_ant = 0;
     con_ant = "";

     impor_a = 0;
     impor_cli = 0;
     nConAnt   = 0;
     aConAnt   = "";
     niv_ant   = 0;

     &eaPathContagen = "";

     uno = "";
     dos = "";
     nivel = 0;
     mascara = "";
     calfa1 = "";
     n = 0;
|FIN;
____________________________________/ rutinas de grabacion de asientos
|PROCESO apuntes;
    |SI impor_a ! 0;|Y conte ! "            ";|Y digcu ! 0;
        |DDEFECTO #1092;
        #1092#00 = dia_ant;      || diario
        #1092#01 = c_fecha;     || fecha
        #1092#02 = #1090#1;       || documento
        #1092#03 = ult_apunt;   || apunte
        #1092#04 = conte;       || cuenta
        #1092#05 = digcu;       || digito cuenta
        #1092#06 = nConAnt;     || concepto
        #1092#07 = aConAnt;     || descripcion
        #1092#08 = signe;       || signo
        #1092#09 = impor_a;     || importe
        |SI #1092#9 < 0;  #1092#9 = -#1092#9;     |FINSI;   || cambia signo al importe

        |SI #1092#08 = "D";
            #1092#16 = #1092#4;
            #1092#17 = #1092#5;
        |SINO;
            #1092#10 = #1092#4;
            #1092#11 = #1092#5;
        |FINSI;
        #1092#12 = "";          || soport./reperc.
        #1092#13 = 0;           || libro
        #1092#14 = 0;           || orden
        #1092#15 = "";          || serie
        #1092#19 = "";          || tipo acumulador
        #1092#20 = 0;           || numero acumulador
        #1092#21 = #1095#30;    || departamento por defecto
        #1092#28 = #1095#31;    || subdepartamento por defecto

        |GRABA 020#1092n;
        |SI FSalida = 0;
            |HAZ cabecera;
            ressum = #1092#9;
            |HAZ actualiz1;     || actualiza cuentas
            opera = "A";
            |HAZ altatra;       || actualiza niveles inferiores
            d_diari = #1092#0;
            d_opera = #1092#8;
            d_impor = #1092#9;
            |HAZ actu_diari;    || actualiza diario
            ult_apunt = ult_apunt + 10;
        |FINSI;
    |FINSI;
|FPRC;

|PROCESO cabecera;
    |DDEFECTO #1091;
    #1091#0 = #1092#0;
    #1091#1 = #1092#1;
    #1091#2 = #1092#2;
    |LEE 101#1091=;
    Fenlaza = FSalida;
    #1091#0 = #1092#0;         || diario
    #1091#1 = #1092#1;         || fecha
    #1091#2 = #1092#2;         || documento
    #1091#3 = #1091#2;         || asiento
    |SI #1092#8 = "D";
        #1091#4 = #1091#4 + #1092#9;
        |SI #1091#09 = 0; #1091#08 = #1092#4; #1091#09 = #1092#5; |FINSI; || contrp.HABER
    |FINSI;
    |SI #1092#8 = "H";
        #1091#5 = #1091#5 + #1092#9;
        |SI #1091#11 = 0; #1091#10 = #1092#4; #1091#11 = #1092#5; |FINSI; || contrp.DEBE
    |FINSI;
    |SI Fenlaza = 0;  |GRABA 020#1091a;  |FINSI;
    |SI Fenlaza ! 0;  |GRABA 020#1091n;  |FINSI;
    |LIBERA #1091;
|FPRC;
____________________________________/ rutinas de control de grabacion
|PROCESO la_fecha;
    nume1 = #99#5;
    |SI nume1 = 0;
        fecha1 = ~;
        enfa1 = fecha1;
        enfa1 = enfa1 % 0402;
        enfa1 = "01"+ enfa1;
        #99#5 = enfa1;
    |FINSI;
    enfa1 = #99#5 % 102;      || dia
    enfa2 = #99#5 % 302;      || mes

    enfa3 = #1095#26;
    enfa3 = "00" + enfa3;
    enfa3 = enfa3 % -102;     || año
    |SI enfa3 < "80";  enfa3="20"+enfa3;  |SINO;  enfa3="19"+enfa3;  |FINSI;

    c_fecha = enfa1 + "." + enfa2 + "." + enfa3;
|FPRC;

|PROCESO el_digito;
    digcu = 0;

    enfa1 = conte;  |QBF enfa1;
    enfa2 = enfa1 % 0;
    nume1 = enfa2;

    |SI #1095#14 = nume1; digcu = 1;     |FINSI;
    |SI #1095#15 = nume1; digcu = 2;     |FINSI;
    |SI #1095#16 = nume1; digcu = 3;     |FINSI;
    |SI #1095#17 = nume1; digcu = 4;     |FINSI;
    |SI #1095#18 = nume1; digcu = 5;     |FINSI;
    |SI #1095#19 = nume1; digcu = 6;     |FINSI;
|FPRC;

|PROCESO asiento; ||aqui
    |SI dia_ant ! #99#12; |O fec_ant ! #99#5;|O con_ant ! #99#7;|O sw_rup = 0;
         |O nConAnt ! #99#9;
        |SI sw_rup ! 0;
            |HAZ asiento2;   || apunte de la contrapartida
        |FINSI;

        impor_cli = 0;        || acumulador del importe
        |SI dia_ant ! #99#12; |O fec_ant ! #99#5;|O con_ant ! #99#7;|O sw_rup = 0;
           ult_apunt = 1;        || lineas de apuntes
           |HAZ contador;        || nro. de documento
        |FINSI;
        sw_rup = 1;           || primera ruptura y primer asiento
        dia_ant   = #99#12;
        fec_ant   = #99#5;
        con_ant   = #99#7;
        niv_ant   = #99#8;
    |FINSI;

    conte     = #99#6;  |HAZ el_digito;     digcu1 = digcu;
    conte     = #99#7;  |HAZ el_digito;     digcu2 = digcu;
    nConAnt   = #99#9;
    aConAnt   = #99#10;

    |SI digcu1 = 0;|O digcu2 = 0;
        |SI digcu1 = 0;
            |MENAV "    ERROR! NIVEL de CUENTA CLIENTE desconocido; ASIENTO ANULADO ...";
        |FINSI;
        |SI digcu2 = 0;
            |MENAV "    ERROR! NIVEL de CUENTA CONTRAPARTIDA desconocido; ASIENTO ANULADO ...";
        |FINSI;
    |SINO;
        |HAZ la_fecha;
        |HAZ asiento1;       || apunte del cliente

        #99#13 = #1090#1;       || marca numero de asiento
        #99#14 = "*";         || marca como contabilizado
        |GRABA 020#99a;
    |FINSI;

    |LIBERA #99;
|FPRC;

|PROCESO asiento1;            || apunte del cliente
   |SI #99#8 = "D";    signe = "H";   |FINSI;
   |SI #99#8 = "H";    signe = "D";   |FINSI;   || signo

    nElCliente = 1;
    conte     = #99#6;                        || cuenta cliente
    digcu     = digcu1;                       || digito
    impor_a   = #99#11;                       || importe linea
    nConAnt   = #99#9;
    aConAnt   = #99#10;
    |HAZ apuntes;
    nElCliente = 0;

    |SI signe = "D";
        impor_cli = impor_cli + #1092#9;      || acumula para la contrapartida
    |SINO;
        impor_cli = impor_cli - #1092#9;      || acumula para la contrapartida
    |FINSI;
|FPRC;

|PROCESO asiento2;                           || apunte de la linea
    signe = niv_ant;                         || signo
    conte = con_ant;                         || cuenta contrapartida
    digcu = digcu2;                          || digito

    |SI impor_cli < 0;
        impor_cli = -impor_cli;
        signe = "D";
    |SINO;
        signe = "H";
    |FINSI;

    impor_a = impor_cli;                     || importe total
    |HAZ apuntes;
|FPRC;

|PROCESO lee_conta1;
 |SI sw_enlace = 0;
     sw_enlace = 1;
     |ERROR10;            || para saber si hay alguno para contabilizar
 |SINO;
    n = n + 1; |PINTA 0530, n;
    |HAZ asiento;
 |FINSI;
 |LIBERA #99;
|FPRC;

|DEFBUCLE lee_conta;
 #99#1;
 14;
 00,     0,   0, " ";
 99, 99999, 999, " ";
 be;
 NULL, NULL, NULL, NULL, NULL, lee_conta1;
 #99#12, #99#5, #99#7, #99#0, #99#1, #99#2;
|FIN;

|PROCESO enlazando;
    dig1 = #1095#11;       || desde mes
    dig3 = #1095#13;       || nro. niveles
    dig4 = #1095#14;       || 1 nivel
    dig5 = #1095#15;       || 2 nivel
    dig6 = #1095#16;       || 3 nivel
    dig7 = #1095#17;       || 4 nivel
    dig8 = #1095#18;       || 5 nivel
    dig9 = #1095#19;       || 6 nivel

    |HAZ inicio_mayor;

    |ABRE #1091;
    |ABRE #1092;
    |BUCLE lee_conta;
    |SI sw_rup ! 0;
        |HAZ asiento2;
    |FINSI;
    |CIERRA #1091;
    |CIERRA #1092;

    |HAZ actua_mayor;
|FPRC;
____________________________________/ rutinas de inicio del enlace
|PROCESO enlaza_path;
    enlace_fich = eaPathContagen;  |QBF enlace_fich;
    enlace_fich = enlace_fich + "fich/";

    enfa1 = #1094#1; enfa1 = ("00000" + enfa1) % -105;  || empresa
    enfa2 = #1094#2;                                               || ejercicio
    enlace_empr = enlace_fich + enfa1 + enfa2 + "/";   || direc. ficheros

|PATH_DAT #1080 enlace_empr;         || diarios
|PATH_DAT #1085 enlace_empr;         || cuentas
|PATH_DAT #1090 enlace_empr;         || contador
|PATH_DAT #1091 enlace_empr;         || apuntes cabs.
|PATH_DAT #1092 enlace_empr;         || apuntes lins.
|PATH_DAT #1093 enlace_empr;         || Cuentas Balance

    path  = enlace_empr;
    path1 = (enlace_fich - "fich/") + "pan/";     || para el 'camaapua'
|FPRC;

|PROCESO enlaza_inicio;
    |HAZ BusquedaApli;

    |ABRE #1094;
    |LEE 000#1094p;             || lee empresas contables
    Fenlaza = FSalida;
    |CIERRA #1094;

    |SI Fenlaza = 0;
        |HAZ enlaza_path;     || construye los path's
        |ABRE #1095;
        #1095#2 = #1094#1;
        #1095#3 = #1094#2;
        |LEE 000#1095=;
        Fenlaza = FSalida;
        |CIERRA #1095;
        |SI Fenlaza = 0;
            |HAZ enlazando;        || lectura y contabilizacion
        |SINO;
            |MENAV "    ATENCION!!! No existe Empresa en CONTABILIDAD ...";
        |FINSI;
    |SINO;
        |MENAV "    ATENCION!!! Empresa Contable INDEFINIDA ...";
    |FINSI;
|FPRC;

|PROCESO enlaza_agicont; |TIPO 9;
    sw_enlace = 0;
    |BUCLE lee_conta;        || comprueba que haya algo

    |SI sw_enlace = 1;
        |CONFI "2400NAsientos Pendientes; actualizar en CONTABILIDAD (S/N): ";
        |SI FSalida = 0;
            |HAZ enlaza_inicio;
        |FINSI;
    |FINSI;
|FPRC;


____________________________________/ CONTADOR DE ASIENTOS
|CALCULO contador;
 |ABRE #1090;
 |LEE 101#1090p;
 |SI FSalida ! 0;
    |DDEFECTO #1090;
    |GRABA 020#1090b;
 |FINSI;
 #1090#1 = #1090#1 + 1;
 |GRABA 020#1090a;
 |LIBERA #1090;
 |CIERRA #1090;
|FCAL;

____________________________________/ ACTUALIZA DIARIO
|CALCULO actu_diari;
    |ABRE #1080;
    #1080#0 = d_diari;
    |LEE 101#1080=;
    |SI FSalida ! 0;
        |DDEFECTO #1080;
        #1080#0 = d_diari;
        #1080#1 = "<DIARIO SIN DEFINIR>";
        |GRABA 020#1080b;
    |FINSI;
    |SI d_opera = "D";
        #1080#2 = #1080#2 + d_impor;        || debe
    |SINO;
        #1080#3 = #1080#3 + d_impor;        || haber
    |FINSI;
    |GRABA 020#1080a;
    |LIBERA #1080;
    |CIERRA #1080;
|FCAL;

____________________________________/ ACTUALIZACION DEL MAYOR
|CALCULO actualiz1;
    |ABRE #1085;
    #1085#0 = #1092#4;
    #1085#1 = #1092#5;
    |LEE 101#1085=;
    |SI FSalida ! 0;
        |HAZ AltaLaCta;
    |FINSI;
    |SI #1092#0 = 0;
        |SI #1092#8 = "D";
            #1085#9 = #1085#9 + ressum;
            #1085#51 = #1085#51 + ressum;
        |SINO;
            #1085#10 = #1085#10 + ressum;
            #1085#52 = #1085#52 + ressum;
        |FINSI;
        #1085#11 = #1085#9 - #1085#10;
        #1085#53 = #1085#51 - #1085#52;
    |SINO;
        |SI #1092#0 = 99;
            |SI #1092#8 = "D";
                #1085#48 = #1085#48 + ressum;
                #1085#51 = #1085#51 + ressum;
            |SINO;
                #1085#49 = #1085#49 + ressum;
                #1085#52 = #1085#52 + ressum;
            |FINSI;
            #1085#50 = #1085#48 - #1085#49;
            #1085#53 = #1085#51 - #1085#52;
         |SINO;
            |HAZ actualiz2;
         |FINSI;
     |FINSI;
     |SI #1085#58 < #1092#1;
         #1085#58 = #1092#1;
     |FINSI;
     |GRABA 020#1085a;
     |LIBERA #1085;
     |CIERRA #1085;
|FCAL;

|CALCULO actualiz2;
    ini = dig1;
    fecalf = #1092#1;
    mes = fecalf % 402;
    mesfec = mes;
    |SI ini = 1;
        mesac = mesfec;
    |SINO;
        mesac = (mesfec - ini) + 1;
        |SI mesac [ 0;
            mesac = (13 - ini) + mesfec;
        |FINSI;
    |FINSI;
    a = (mesac * 3) + 9;
    b = a + 1;
    c = a + 2;
    |SI #1092#8 = "D";
        #1085a = #1085a + ressum;
        #1085#51 = #1085#51 + ressum;
    |SINO;
        #1085b = #1085b + ressum;
        #1085#52 = #1085#52 + ressum;
    |FINSI;
    #1085c = #1085a - #1085b;
    #1085#53 = #1085#51 - #1085#52;
|FCAL;

|CALCULO altatra;
    modcon = 1;
   |ABRE #1088;
    #1088#0 = codcon;
    #1088#1 = #1092#4;
    #1088#2 = #1092#5;
    #1088#3 = #1092#1;
    #1088#4 = #1092#8;
    #1088#6 = #1092#0;
    |LEE 101#1088=;
    |SI FSalida ! 0;
        |DDEFECTO #1088;
        #1088#0 = codcon;
        #1088#1 = #1092#4;
        #1088#2 = #1092#5;
        #1088#3 = #1092#1;
        #1088#4 = #1092#8;
        #1088#6 = #1092#0;
        |GRABA 101#1088b;
    |FINSI;
    importe = #1092#9;
    |SI opera = "B";
        importe = -importe;
    |FINSI;
    #1088#5 = #1088#5 + importe;
    |GRABA 101#1088a;
    |LIBERA #1088;
    |CIERRA #1088;
|FCAL;

|PROCESO inicio_mayor;
  |DDEFECTO #1088;
  |ABRE #1088;
  |LEE 101#1088u;
  |SI FSalida ! 0;
      #1088#0 = 1;
  |SINO;
      #1088#0 = #1088#0 + 1;
  |FINSI;
  |GRABA 000#1088n;
  codcon = #1088#0;
  |LIBERA #1088;
  |CIERRA #1088;
|FPRC;

|PROCESO actua_mayor;
|SI modcon = 0;
    |ABRE #1088;
    |DDEFECTO #1088;
        #1088#0 = codcon;
    |LEE 000#1088=;
    |SI FSalida = 0;     |BORRA 020#1088a;     |FINSI;
    |CIERRA #1088;
|SINO;
    |SUB_EJECUTA "camaapua"
|FINSI;
|FPRC;


|PROCESO AltaLaCta;
 #1085#0 = #1092#4;
 #1085#1 = #1092#5;
 |HAZ defecto_cuenta;
 |SI nElCliente = 1;
     |ABRE #asclient;
     calfa1 = #1085#0; |QBF calfa1;
     calfa1 = calfa1 % -105;
     #asclient#0 = calfa1;
     |LEE 001#asclient.=;
     |SI FSalida = 0;
         #1085#2 = #asclient#1;
     |FINSI;
     |CIERRA #asclient;
 |FINSI;
 |GRABA 020#1085b;
|FPRC;

|CALCULO defecto_cuenta;
   |ABRE #cacuebal;
   uno = #cacuenta#0;
   |QBF uno;               || quita los espacios blancos
   dos = uno % 0;
   |SI #cacuenta#1 = dig3; |O uno = "";
       #cacuenta#3 = "S";
   |FINSI;
   dos = uno + "000000000000";
   |SI #cacuenta#1 < 4;
      |VETE a_nivel_3;
   |FINSI;
   ||  dos = uno + "000000000000";
   uno = dos % 104;
   #cacuebal#0 = uno;
   |LEE 040#cacuebal.=;
   |SI FSalida = 0;
      |HAZ saca_defecto;
      |VETE findefecto_cuenta;
   |FINSI;
   |ET a_nivel_3;
   |SI #cacuenta#1 < 3;
      |VETE a_nivel_2;
   |FINSI;
   uno = dos % 103;
   #cacuebal#0 = uno;
   |LEE 040#cacuebal.=;
   |SI FSalida = 0;
      |HAZ saca_defecto;
      |VETE findefecto_cuenta;
   |FINSI;
   |ET a_nivel_2;
   |SI #cacuenta#1 < 2;
      |VETE a_nivel_1;
   |FINSI;
   uno = dos % 102;
   #cacuebal#0 = uno;
   |LEE 040#cacuebal.=;
   |SI FSalida = 0;
      |HAZ saca_defecto;
      |VETE findefecto_cuenta;
   |FINSI;
   |ET a_nivel_1;
   uno = dos % 101;
   #cacuebal#0 = uno;
   |LEE 040#cacuebal.=;
   |SI FSalida = 0;
      |HAZ saca_defecto;
      |VETE findefecto_cuenta;
   |SINO;
      |MENAV "0000Cuenta De Balance No Definida!!";
   |FINSI;

   |ET findefecto_cuenta;
   nivel = #cacuenta#1;

   mascara = #cacuenta#0;
   |QBF mascara;
   mascara = mascara +"zzzzzzzzzzzz";
   mascara = mascara % 112;
   #cacuenta#54    = mascara;
   #cacuenta#55    = nivel;
   |CIERRA #cacuebal;
|FCAL;

|RUTINA saca_defecto;
   #cacuenta#4 = #cacuebal#1;
   #cacuenta#5 = #cacuebal#2;
   #cacuenta#6 = #cacuebal#3;
   #cacuenta#7 = #cacuebal#4;
   #cacuenta#8 = #cacuebal#5;
|FRUT;
