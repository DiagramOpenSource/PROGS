          **** PASE DEL HISTORICO DE RECIBOS AL SECUENCIAL ADAPTADO A LA ***
                        ***     NUEVA NORMA 19  DETALLADO    ****

|FICHEROS;
  aspascon #0;
  ashisrec #1;
  asclient #2;
  indrecib #3;
  ashiscon #4;
  ascaisec #5;
  ashisfac #6;

  ascabmin #7;
  aslinmin #8;
  asempcon #9;
  asenlcon #10;
  ascontro #11;
  ascontrt #450;
  asconcep #12;
  ascabrem #13;
  aslinrem #14;
  asm00013 #15;
  asw00001 #99;
  ashismic #90;

  asparcia #100;
|FIN;

|VARIABLES;
  cabe1   = "0180";
  orde1   = "0380";
  indi1   = "0680";
  conc1_1 = "0681";
  conc1_2 = "0682";
  conc1_3 = "0683";
  conc1_4 = "0684";
  conc1_5 = "0685";
  tgen1   = "0980";
  tord1   = "0880";


|| Cabecera del presentador;
  cabe2 = "";
  cabe3 = "      ";
  cabe5 = "";
  cabe6 = "          ";
  libre20 = "                    ";
  libre12 = "            ";
  libre40 = "                                        ";

|| Cabecera de Ordenante;
  orde2 = "";
  orde3 = "";
  orde5 = "";
  libre8= "        ";
  orde6 = "01";
  libre10 = "          ";
  fechacargo = @;

|| Registro individual Obligatorio
  indi2 = "";
  indi3 = "";
  indi4 = "";
  indi5 = "";
  indi6 = "";
  indi7 = "";
  indi8 = "";
||  indi9 = "       .";
  indi9 = "        ";
  indi10 = "          ";
  dc="";

  conc1 = "";
  conc2 = "";
  conc3 = "                                        ";
  conc4 = "                                        ";
  conc5 = "                                        ";
  impor = "";

|| Registro Total General Y ordenante
  tgen2 = "                                                    ";
  tgen3 = "";
  tgen4 = "";
  tgen5 = "      ";
  tgen6 = "";
  tgen7 = "                                      ";
  tgen8 = "0001";
  tgen9 = "                ";

  tord9 = "                    ";

  handle = "";
  varalfa1 = "";
  varalfa2 = "";
  facalfa = "";
  contenido = "";
  longalf = "";
  fiche = "";
  espacio = " ";
  dd = "";
  mm = "";
  aa = "";
  fechalf = "";
  impalf = "";
  vacio = "          ";
  trecibo = 0;
  timpsuc = 0;
  tnumsuc = 0;
  serie = "";
  factu = 0;
  cont = 0;
  contreg = 0;
  empresa = "";
  linea_c = "Procesando Factura: Serie:    Factura:";
  linea   = 0;
||  final = "                   .";
  final = "                    ";
||  final14 = "             .";
  final14 = "              ";
  l = 0;
  valo = 0;
  elefe = "";
  ceerre = "";
  traba = "";
  desc1 = "";
  desc2 = "";
  desc3 = "";
  desc4 = "";
  desc5 = "";
  desc6 = "";
  desc7 = "";
  importe_p = 0;

  sw_existe = 0;
  varalf    = "";
  iva       = 0;
  c_clie    = "";
  fijo      = "";
  conver    = "";
  conver1   = "";
  conver2   = "";
  sino      = "";
  impor1    = "";
  impor2    = "";
  impor3    = "";
  impor4    = "";
  impor5    = "";
  impor6    = "";
  impor7    = "";
  Ultimo    = "";
  Varalfa   = "";
  aAlfa     = "";
     fFech = @;
     qFech = "";
     dFech = "";
     hFech = "";

     &eaMoneda  = "";
     &enCanti   = 0;
     &enSwTrata = 0;

  aFichero = "";

  &efFechaO  = @;
  &efFechaL  = @;
  &enImporte = 0;
  &enDesRemesa = 0;
|FIN;

|INCLUYE agicon_i;

|PROCESO MiraVarMoneda;  |TIPO 7;
     |HAZ PintaMoneda;
     #0#22 = eaMoneda;

     |PINTA #0#22;
|FPRC;

|PROCESO sacaano; |TIPO 7;
     fFech = ~;
     qFech = fFech % -104;
     dFech = "01.01." + qFech;
     hFech = "31.12." + qFech;
     #0#4  = dFech;
     #0#5  = hFech;
     |PINTA #0#4;
     |PINTA #0#5;
|FPRC;

|PROCESO que_selec;
|SI #0#15 = "N";
    |CAMPO_MODIFICA #0#0,  1, "S"; |PINTA #0#0;
    |CAMPO_MODIFICA #0#1,  1, "S"; |PINTA #0#1;
    |CAMPO_MODIFICA #0#2,  1, "S"; |PINTA #0#2;
    |CAMPO_MODIFICA #0#3,  1, "S"; |PINTA #0#3;
    |CAMPO_MODIFICA #0#4,  1, "S"; |PINTA #0#4;
    |CAMPO_MODIFICA #0#5,  1, "S"; |PINTA #0#5;
    |CAMPO_MODIFICA #0#6,  1, "S"; |PINTA #0#6;
    |CAMPO_MODIFICA #0#7,  1, "S"; |PINTA #0#7;
    |CAMPO_MODIFICA #0#16, 1, "N"; |PINTA #0#16;
|SINO;
    |CAMPO_MODIFICA #0#0,  1, "N"; |PINTA #0#0;
    |CAMPO_MODIFICA #0#1,  1, "N"; |PINTA #0#1;
    |CAMPO_MODIFICA #0#2,  1, "N"; |PINTA #0#2;
    |CAMPO_MODIFICA #0#3,  1, "N"; |PINTA #0#3;
    |CAMPO_MODIFICA #0#4,  1, "N"; |PINTA #0#4;
    |CAMPO_MODIFICA #0#5,  1, "N"; |PINTA #0#5;
    |CAMPO_MODIFICA #0#6,  1, "N"; |PINTA #0#6;
    |CAMPO_MODIFICA #0#7,  1, "N"; |PINTA #0#7;
    |CAMPO_MODIFICA #0#16, 1, "S"; |PINTA #0#16;
|FINSI;
|FPRC;

|PROCESO hay_remesa; |TIPO 0;
   |SI #0#16 = 0; |ACABA; |FINSI;

   |ABRE #13;
   #13#0 = #0#16;
   |LEE 040#13=;
   |SI FSalida ! 0;
       |MENAV "      No existe Remesa";
       |ERROR;
   |SINO;
       |SI #13#5 = "X";
           |CONFI "0000NEsta Remesa ya ha sido pasada por el Banco. Volver a realizar el Proceso";
           |SI FSalida ! 0;
               |ERROR;
           |FINSI;
       |FINSI;
   |FINSI;
   |CIERRA #13;
|FPRC;

|CALCULO secuen_01;
  enImporte =( #1#6 - #1#9 ) ! 2;
  |SI enImporte [ 0; |ACABA; |FINSI;   || no hay nada pendiente de cobrar

  #2#0 = #1#3;              ||** Lee fichero de clientes;
  |LEE 040#2=;              ||** Ordena por Banco/Sucursal;
  |SI FSalida = 0;
      #3#0 = #1#0;  ||** Serie;
      #3#1 = #1#1;  ||** Factura;
      |SI #0#21 = "S";
          Varalfa = #2#0;
          Varalfa = ("00000" + Varalfa) % -105;
          #3#2    = Varalfa + " " + #2#2;
      |SINO;
          #3#2 = #2#2;  ||** Nom. Cliente;
      |FINSI;
      #3#3 = #2#8;  ||** Cif/Dni;
      #3#4 = #2#16; ||** Banco;
      #3#5 = #2#13; ||** C.C.C;
      #3#6 = #2#14; ||** Cta del banco

      #3#7 = enImporte;  ||** Importe Recibo;
      |SI #5#13 = 1;  #3#8 = #1#2;  |FINSI;  ||** Fecha Emision
      |SI #5#13 = 2;  #3#8 = #1#5;  |FINSI;  ||** Fecha Vencimiento;
      |GRABA 020#3n;||** Graba el fichero indexado;
      |LIBERA #3;

      #1#10 = "S";
      #1#12 = "R";
      #1#8  = ~;
      #1#9  = #1#9  + enImporte;
      #1#15 = enImporte;
      |GRABA 021#1a;
      |LIBERA #1;

      efFechaL = #0#8;
      |SI #aspascon#15 = "N";
          |HAZ eGrCobroParcial;
      |FINSI;

      |ABRE #90;
      #90#0 = #1#0;
      #90#1 = #1#1;
      |LIBERA #90;
      |LEE 140#90=;
      |SI FSalida = 0;
          #90#32 = #1#10;
          #90#33 = #1#12;
          |GRABA 140#90a;
      |FINSI;
      |CIERRA #90;

      enDesRemesa = 0;
      |HAZ eGraba_Min;
  |SINO;
      |AVISO;
      |PINTA 2218,"ATENCION Cliente Inexistente! CODIGO.:"
      |PINTA 2257,#1#3;
      |PAUSA;
  |FINSI;
|FCAL;

|CALCULO secuen_02;
  #1#0 = #14#2;
  #1#1 = #14#3;
  |LIBERA #1;
  |LEE 140#1=;
  |SI FSalida ! 0; |ACABA; |FINSI;

  #2#0 = #1#3;              ||** Lee fichero de clientes;
  |LEE 040#2=;              ||** Ordena por Banco/Sucursal;
  |SI FSalida ! 0; |ACABA; |FINSI;

  #3#0 = #1#0;  ||** Serie;
  #3#1 = #1#1;  ||** Factura;
  |SI #0#21 = "S";
      Varalfa = #2#0;
      Varalfa = ("00000" + Varalfa) % -105;
      #3#2    = Varalfa + " " + #2#2;
  |SINO;
      #3#2 = #2#2;  ||** Nom. Cliente;
  |FINSI;
  #3#3 = #2#8;  ||** Cif/Dni;
  #3#4 = #2#16; ||** Banco;
  #3#5 = #2#13; ||** C.C.C;
  #3#6 = #2#14; ||** Cta.Cte;
  #3#7 = #14#7;  ||** Importe Recibo;
  |SI #5#13 = 1;  #3#8 = #1#2;  |FINSI;  ||** Fecha Emision
  |SI #5#13 = 2;  #3#8 = #1#5;  |FINSI;  ||** Fecha Vencimiento;
  |GRABA 020#3n; ||** Graba el fichero indexado;
  |LIBERA #3;

  efFechaO = #13#6;
  efFechaL = #0#8;
  enDesRemesa = 1;
  |HAZ eGraba_Min;
|FCAL;

|DEFBUCLE ashisrec;
  #1#1;
  2,7;
  #0#0, #0#2, #0#4, #0#6;
  #0#1, #0#3, #0#5, #0#7;
  be;
  NULL,secuen_01;
|FIN;

|DEFBUCLE aslinrem;
  #14#1;
  ;
  #0#16, 000;
  #0#16, 999;
  ;
  NULL, secuen_02;
|FIN;

|CALCULO pase;|TIPO 3;
   eaMoneda = #0#22;

  |SI #0#22 = "PESETAS";
      cabe1   = "0180";
      orde1   = "0380";
      indi1   = "0680";
      conc1_1 = "0681";
      conc1_2 = "0682";
      conc1_3 = "0683";
      conc1_4 = "0684";
      conc1_5 = "0685";
      tgen1   = "0980";
      tord1   = "0880";
  |SINO;
      cabe1   = "5180";
      orde1   = "5380";
      indi1   = "5680";
      conc1_1 = "5681";
      conc1_2 = "5682";
      conc1_3 = "5683";
      conc1_4 = "5684";
      conc1_5 = "5685";
      tgen1   = "5980";
      tord1   = "5880";
   |FINSI;

   elefe = &010;
   ceerre = &013;
   |SI #0#13 = "S";
       traba = ceerre + elefe;
       elefe = traba;
   |FINSI;

   aFichero = ("19x" + Usuario) % 108;
   |NOME_DAT #3 aFichero;

   |ABRE #3;  |CIERRA #3;  |DELINDEX #3;

   |ABRE #2;
   |ABRE #3;

   |SI #0#15 = "N";
       |BUCLE ashisrec;
   |SINO;
       |ABRE #1;
       |BUCLE aslinrem;
       |CIERRA #1;
   |FINSI;

   |CIERRA #2;
   |ABRE #5;
   #5#0 = #0#9;
   |LEE 040#5=;
   |DFICE empresa;
   fiche = "wb  " + empresa + "assecrec.seq";  |QBF fiche;

   |FABRE fiche;
   handle = FSalida;
   |SI FSalida ] 0;
       |HAZ cabecera_p;
       |HAZ cab_ordenante;
       |HAZ regindiv;
       |HAZ campo_totales;
   |FINSI;
   FSalida = handle;
   |FCIERRA FSalida;

   |CIERRA #3; |DELINDEX #3;
   |CIERRA #5;

   |SI #0#15 = "S";
       |ABRE #13;
       #13#0 = #0#16;
       |LEE 141#13=;
       |SI FSalida = 0;
           #13#5 = "X";
           #13#6 = efFechaL;
           |GRABA 020#13a;
       |FINSI;
       |LIBERA #13;
       |CIERRA #13;
   |FINSI;

   E_sup = 1;
   E_inf = "SNsn";
   sino  = "S";
   sino  = 1769 ? 1;
   sino  = sino > " ";
   |SI sino = "S"; |SUB_EJECUTA_NP "ascopdis"; |FINSI;
|FCAL;

||***********************************************************;
||************ REGISTRO DE CABECERA DE PRESENTADOR **********;
||***********************************************************;
|PROCESO cabecera_p;
  fechalf = #0#8;
  dd = fechalf % A102;
  mm = fechalf % A402;
  aa = fechalf % A902;
  cabe2 = dd + mm + aa;
  cabe5 =  #5#7 + #5#8;
  varalfa1 = cabe1 + #5#2 + #0#12 + cabe2 + cabe3 + #5#3 + libre20+ cabe5 + libre12 + libre40+ final14;
  contenido = handle + espacio + varalfa1 + elefe;
  |FGRABA contenido;
  l = l + 1;
|FPRC;

||***********************************************************;
||************ REGISTRO DE CABECERA DE ORDENANTE ************;
||***********************************************************;
|PROCESO cab_ordenante;
  fechalf = #0#8;
  dd = fechalf % A102;
  mm = fechalf % A402;
  aa = fechalf % A902;
  orde2 = dd + mm + aa;
  fechacargo = #0#11;
  dd = fechacargo % A102;
  mm = fechacargo % A402;
  aa = fechacargo % A902;
  orde3 = dd + mm + aa;
  orde5 = #5#4 + #5#5 + #5#11 + #5#6;
  varalfa1="";
  varalfa1 = orde1 + #5#2 + #0#12 + orde2 + orde3 + #5#3 + orde5 + libre8 + orde6 + libre10 + libre40 + final14;
  contenido = handle + espacio + varalfa1 + elefe;
  |FGRABA contenido;
  l = l + 1;
|FPRC;


||***********************************************************;
||******************* REGISTRO INDIVIDUAL *******************;
||***********************************************************;

|PROCESO regindiv;
  |SELECT #3#3;          ||Cojo la tercera clave del fichero CCC y Cta.
  |LEE 040#3p;
  |SI FSalida ! 0;  |ACABA;  |FINSI;

|ET compara;
  |HAZ campo_indiv;
  trecibo = trecibo + 1;

  enSwTrata = 1;
  enCanti   = #3#7;
  |HAZ ConverMoneda;
  timpsuc = timpsuc + enCanti;

  |SI #5#10 = "S"; |HAZ conceptos; |FINSI;  ||aqui


  |LEE 040#3s;
  |SI FSalida = 0;  |VETE compara; |FINSI;
|FPRC;

||***********************************************************;
||******************* REGISTRO CONCEPTOS ********************;
||***********************************************************;
|PROCESO LeeDetalle0;
         cont = cont + 1;
         |SI cont = 1;
             desc1   = (#15#9 + libre40) % 0140;
             enCanti = #15#8;    |HAZ ConverMoneda;
             impor1  = enCanti; |SI #15#8 = 0; impor1 = ""; |FINSI;
         |FINSI;

         |SI cont = 2;
             desc2   = (#15#9 + libre40) % 0140;
             enCanti = #15#8;      |HAZ ConverMoneda;
             impor2  = enCanti; |SI #15#8 = 0; impor2 = ""; |FINSI;
         |FINSI;

         |SI cont = 3;
             desc3   = (#15#9 + libre40) % 0140;
             enCanti = #15#8;      |HAZ ConverMoneda;
             impor3  = enCanti; |SI #15#8 = 0; impor3 = ""; |FINSI;
         |FINSI;

         |SI cont = 4;
             desc4   = (#15#9 + libre40) % 0140;
             enCanti = #15#8;      |HAZ ConverMoneda;
             impor4  = enCanti; |SI #15#8 = 0; impor4 = ""; |FINSI;
         |FINSI;

         |SI cont = 5;
             desc5   = (#15#9 + libre40) % 0140;
             enCanti = #15#8;      |HAZ ConverMoneda;
             impor5  = enCanti; |SI #15#8 = 0; impor5 = ""; |FINSI;
         |FINSI;

         |SI cont = 6;
             desc6   = (#15#9 + libre40) % 0140;
             enCanti = #15#8;      |HAZ ConverMoneda;
             impor6  = enCanti; |SI #15#8 = 0; impor6 = ""; |FINSI;
         |FINSI;

         |SI cont = 7;       ||Es el primer registro el 0681
             desc7 = (#15#9 + libre40) % 0140;
             enCanti = #15#8;      |HAZ ConverMoneda;
             impor7  = enCanti; |SI #15#8 = 0; impor7 = ""; |FINSI;
         |FINSI;
|FPRC;

|DEFBUCLE LeeDetalle;
 #15#1;
 ;
 #6#4, #6#3, #4#0, #4#1, "S", #4#2, 01;
 #6#4, #6#3, #4#0, #4#1, "S", #4#2, 99;
 e;
 NULL, LeeDetalle0;
|FIN;

|PROCESO BuscaPuntos;
      |SI impor = "";  |ACABA;  |FINSI;

      aAlfa = impor - ".";
      |SI FSalida = 0;
          impor = impor + ".00";
          |ACABA;
      |FINSI;

      aAlfa = impor % -301;
      |SI aAlfa = ".";  |ACABA;  |FINSI;

      aAlfa = impor % -201;
      |SI aAlfa = ".";
          impor = impor + "0";
      |FINSI;
|FPRC;

|PROCESO conceptos;
  |ABRE #4;
  |ABRE #6;
  impor1    ="";  impor2    ="";  impor3    ="";  impor4    ="";
  impor5    ="";  impor6    ="";  impor7    ="";
  desc1     = libre40; desc2 = libre40; desc3 = libre40; desc4 = libre40;
  desc5     = libre40; desc6 = libre40; desc7 = libre40;
  contreg   = 0; ||** Contador de registros maximo 5;
  conc3     = "                                        ";
  conc4     = "                                        ";
  conc5     = "                                        ";
  cont      = 0;

  importe_p = 0;
  #6#0 = #3#0; #6#1 = #3#1;
  |LEE 001#6=;
  |SI FSalida ! 0; |CIERRA #4; |CIERRA #6; |ACABA; |FINSI;

  enSwTrata = 1;

  #4#0 = #3#0;
  #4#1 = #3#1;
  #4#2 = 000;
  |LEE 040#4];
  |PARA; |SI FSalida = 0; |Y #4#0 = #3#0; |Y #4#1 = #3#1; |HACIENDO;
         cont = cont + 1;
         |SI cont = 1;
             desc1   = (#4#7 + libre40) % 0140;
             enCanti = #4#9;    |HAZ ConverMoneda;
             impor1  = enCanti; |SI #4#9 = 0; impor1 = ""; |FINSI;
         |FINSI;

         |SI cont = 2;
             desc2   = (#4#7 + libre40) % 0140;
             enCanti = #4#9;      |HAZ ConverMoneda;
             impor2  = enCanti; |SI #4#9 = 0; impor2 = ""; |FINSI;
         |FINSI;

         |SI cont = 3;
             desc3   = (#4#7 + libre40) % 0140;
             enCanti = #4#9;      |HAZ ConverMoneda;
             impor3  = enCanti; |SI #4#9 = 0; impor3 = ""; |FINSI;
         |FINSI;

         |SI cont = 4;
             desc4   = (#4#7 + libre40) % 0140;
             enCanti = #4#9;      |HAZ ConverMoneda;
             impor4  = enCanti; |SI #4#9 = 0; impor4 = ""; |FINSI;
         |FINSI;

         |SI cont = 5;
             desc5   = (#4#7 + libre40) % 0140;
             enCanti = #4#9;      |HAZ ConverMoneda;
             impor5  = enCanti; |SI #4#9 = 0; impor5 = ""; |FINSI;
         |FINSI;

         |SI cont = 6;
             desc6   = (#4#7 + libre40) % 0140;
             enCanti = #4#9;      |HAZ ConverMoneda;
             impor6  = enCanti; |SI #4#9 = 0; impor6 = ""; |FINSI;
         |FINSI;

         |SI cont = 7;       ||Es el primer registro el 0681
             desc7 = (#4#7 + libre40) % 0140;
             importe_p = importe_p + #4#9;
         |FINSI;

         |SI cont > 7;
             desc7 = ("VARIOS" + libre40) % 0140;
             importe_p = importe_p + #4#9;
         |FINSI;

         |SI #4#8 = "H"; |Y cont < 7; |BUCLE LeeDetalle; |FINSI;
         |LEE 040#4s;
     |SIGUE;

     |SI cont ] 7;
          enCanti = importe_p;      |HAZ ConverMoneda;
          impor   = enCanti;  |SI importe_p = 0; impor = ""; |FINSI;
          impor   = (libre40 + impor) % -0110;
          impor7  = impor;
          desc7   = (desc7 + libre40) % 140;
     |FINSI;

     |PARA contreg = 1; |SI contreg [ 5; |Y cont > 0; |HACIENDO contreg = contreg + 1;
          |SI contreg = 1;    ||Es el Primer Registro el 0681
               conc3   = ("           " +  #5#15 + " " + #3#8 + libre40) % 0140;
               conc4   = desc1;
               impor1  =  (libre40 + impor1) % -110;
               impor   = impor1;
               |HAZ BuscaPuntos;
               impor1  = impor;
               enCanti = #6#17;      |HAZ ConverMoneda;
               impor   = enCanti; |SI #6#17 = 0; impor = ""; |FINSI;
               |HAZ BuscaPuntos;
               impor   = (libre40 + impor) % -0110;
               conc5   = (impor1 + " " + #5#16 + " " + impor + libre40) % 140;
               |HAZ graba_con;
          |FINSI;

          |SI contreg = 2;    ||Es el segundo registro el 0682
               conc3   = desc2;
               impor2  = (libre40 + impor2) % -110;
               conc4   = impor2 + " " + (("I.V.A. " + #6#7 + libre40) % 0112);
               enCanti = #6#8;      |HAZ ConverMoneda;
               impor   = enCanti; impor = (libre40 + impor) % -0110;
               |HAZ BuscaPuntos;
               conc4   = (conc4 + "% : " + impor + libre40) % 0140;
               conc5   = desc3;
               |HAZ graba_con;
          |FINSI;

          |SI contreg = 3;    ||Es el tercer registro el 0683
              |SI #6#10 = 0;
                  impor3  = ( libre40 + impor3) % -110;
                  enCanti = #6#6;      |HAZ ConverMoneda;
                  impor   = enCanti; |SI #6#6 = 0; impor = ""; |FINSI;
                  |HAZ BuscaPuntos;
                  impor   = (libre40 + impor) % -0110;
                  conc3   = (impor3 + " " + #5#17 + " " + impor + libre40) % 0140;
                  conc4   = desc4;
                  impor4  = ( libre40 + impor4) % -110;
                  conc5   = (impor4 + " ---------------------------" + libre40) % 0140;
                  |HAZ graba_con;
              |SINO;
                  impor3  = ( libre40 + impor3) % -110;
                  enCanti = #6#6;      |HAZ ConverMoneda;
                  impor   = enCanti; |SI #6#6 = 0; impor = ""; |FINSI;
                  |HAZ BuscaPuntos;
                  impor   = (libre40 + impor) % -0110;
                  conc3   = (impor3 + " " + #5#17 + " " + impor + libre40) % 0140;
                  conc4   = desc4;
                  impor4  = ( libre40 + impor4) % -110;
                  impor   = #6#9; |QBT impor;
                  conc5   = (impor4 + " I.R.P.F.  " + impor + "% : ");
                  enCanti = #6#10;      |HAZ ConverMoneda;
                  impor   = enCanti; |SI #6#10 = 0; impor = ""; |FINSI;
                  |HAZ BuscaPuntos;
                  impor   = (libre40 + impor) % -0110;
                  conc5   = (conc5 + (impor + libre40)) % 0140;
                  |HAZ graba_con;
              |FINSI;
          |FINSI;

          |SI contreg = 4;    ||Es el cuarto registro el 0684;
               conc3   = desc5;
               impor5  = ( libre40 + impor5) % -110;
               enCanti = #6#18;      |HAZ ConverMoneda;
               impor   = enCanti; |SI #6#18 = 0; impor = ""; |FINSI;
               |HAZ BuscaPuntos;
               impor = (libre40 + impor) % -0110;
               conc4 = (impor5 + " " + #5#18 + " " + impor + libre40) % 0140;
               conc5 = desc6;
               |HAZ graba_con;
          |FINSI;

          |SI contreg = 5;    ||Es el quinto Registro el 0685
               impor6 = ( libre40 + impor6) % -110;
               conc3 = (impor6 + " ---------------------------" + libre40) % 0140;
               conc4 = desc7;
               conc5 = (impor7 + libre40) % 140;
               |HAZ graba_con;
          |FINSI;
     |SIGUE;

     |CIERRA #4;
     |CIERRA #6;
|FPRC;

|PROCESO graba_con;
      |SI contreg = 1; conc1 = conc1_1 + #5#2; |FINSI;
      |SI contreg = 2; conc1 = conc1_2 + #5#2; |FINSI;
      |SI contreg = 3; conc1 = conc1_3 + #5#2; |FINSI;
      |SI contreg = 4; conc1 = conc1_4 + #5#2; |FINSI;
      |SI contreg = 5; conc1 = conc1_5 + #5#2; |FINSI;
      varalfa1 = conc1 + #0#12 + #3#3 + conc3 + conc4 + conc5 + final14;
      contenido = handle + espacio + varalfa1 + elefe;
      |FGRABA contenido;
      l = l + 1;
      conc3 = "                                        ";
      conc4 = "                                        ";
      conc5 = "                                        ";
|FPRC;

            ||Calculo registros individuales;
|PROCESO campo_indiv;
  indi4 = #3#5 % 108;
  indi4 = indi4 + vacio;
  indi4 = indi4 % 108;


  indi5 = #3#6 % 110;
  |QBT indi5;
  indi5="0000000000" + indi5;
  indi5= indi5 % -110;
  dc = #3#5 % 0902;
  |QBF dc;
  dc = dc + "**";
  dc = dc % 102;

  enCanti   = #3#7;
  enSwTrata = 0;
  |HAZ ConverMoneda;

  indi6 = ("0000000000" + enCanti) % -110;

  fechalf = #3#8;
  dd      = fechalf % 102;
  mm      = fechalf % 402;
  aa      = fechalf % 902;
  indi7   = dd + mm + aa;
  facalfa = #3#1;
  Ultimo  = #5#9 % -101;
  indi8   = #5#9 % 130;  |QBF indi8;
  |SI Ultimo = "/";
      indi8   = (indi8 + " " + #3#0 + " " + facalfa + libre40) % 140;
  |SINO;
      indi8   = (indi8 + " " + facalfa + libre40) % 140;
  |FINSI;
varalfa1   = indi1 + #5#2 + #0#12 + #3#3 + #3#2 + indi4 + dc + indi5 + indi6 + indi7 + indi10 + indi8 + indi9;
  contenido = handle + espacio + varalfa1 + elefe;
  |FGRABA contenido;
  l = l + 1;
|FPRC;

||***********************************************************;
||********** REGISTRO TOTAL ORDENANTE Y GENERAL *************;
||***********************************************************;
|PROCESO campo_totales;

|| **** REGISTRO TOTAL ORDENANTE

  tgen3 = trecibo;
  tgen3 = "0000000000" + tgen3;
  tgen3 = tgen3 % -110;

  |SI #0#22 = "EUROS  ";
      timpsuc = timpsuc * 100;
  |FINSI;
  tgen4  = ("0000000000" + timpsuc) % -110;

  l = l + 1;

  tnumsuc = l - 1;        ||Se quita un registro, porque no se cuenta
                          ||el primero
  tgen6  = tnumsuc;
  tgen6  = "0000000000" + tgen6;
  tgen6  = tgen6 % -110;
  varalfa1 = tord1 + #5#2 + #0#12 + tgen2 + tord9 + tgen4 + tgen5 + tgen3 + tgen6 + tgen7;
  contenido = handle + espacio + varalfa1 + elefe;
  |FGRABA contenido;

|| **** REGISTRO TOTAL GENERAL

  tgen3 = trecibo;                ||** Total num recibos;
  tgen3 = "0000000000" + tgen3;
  tgen3 = tgen3 % -110;

  tgen4  = ("0000000000" + timpsuc) % -110;

  l = l + 1;
  tnumsuc = l;

  tgen6  = tnumsuc;                 ||** Total reg. sucursales
  tgen6  = "0000000000" + tgen6;
  tgen6  = tgen6 % -110;

  varalfa1 = tgen1 + #5#2 + #0#12 + tgen2 + tgen8 + tgen9 + tgen4 + tgen5 + tgen3 + tgen6 + tgen7;
  contenido = handle + espacio + varalfa1 + elefe;
  |FGRABA contenido;

  #0#17 = tgen3;  |PINTA #0#17;
  #0#18 = tgen6;  |PINTA #0#18;
  #0#19 = timpsuc;
  |SI #0#22 = "EUROS  ";
      #0#19 = timpsuc / 100;
  |FINSI;
  |PINTA #0#19;
|FPRC;
