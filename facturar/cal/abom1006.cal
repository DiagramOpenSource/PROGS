|FICHEROS;
     abom0006 #0,MANTE;
     abom0514 #104;
     abom0511 #511,MANTE;
     abom0518 #518;
     abom9000 #900;

     abom0500;

     abom0006@ #1006;
|FIN;

|VARIABLES;
     aNumero        = "";
     aHora          = "";
     aAlfa          = "";
     aPersona       = "";
     aCadena        = "";
     aComodin       = "";
     aAbre          = "";
     aBase          = "";
     aMas           = "";
     aRutaCorreo    = "";
     aCorreo        = "";
     aEjecuta       = "";
     aEnviado       = "";
     aRef           = "";
     aFic           = "";
     aIncidencia    = "";
     aLen           = "";
     aCaracter      = "";
     aReferencia    = "";
     aJava          = "";
     aPathZimbra    = "";
     aOrigen        = "";
     aDestino       = "";
     aDocRemoto     = "";
     aDocServidor   = "";


     nHandle        = 0;
     nCont          = 0;
     nPasa          = 0;
     nError         = 0;
     nCampo         = 0;
     nPosi          = 0;
     nLen           = 0;
     nSwEnvio       = 0;
     nSwMensaje     = 0;
     nDirecto       = 0;
     nNume1         = 0;
     nNume2         = 0;

     fFecha         = @;

    {1}aContiene    = "";

     &enCuenta      = 0;
     &enFSalida     = 0;
     &eaUnidadBasico = "";
|FIN;

|PROCESO QuitaRaros;
     |QBF aAlfa;
     aCadena = "";
     aLen    = aAlfa % 0;
     nLen    = aLen;
     |PARA nCampo = 1;  |SI nCampo [ nLen;  |HACIENDO nCampo = nCampo + 1;
           nPosi = (100 * nCampo) + 1;
           aCaracter = aAlfa % nPosi;
           |SI aCaracter = "¥";  aCaracter = &209; |FINSI;
           |SI aCaracter = "¤";  aCaracter = &241; |FINSI;
           |SI aCaracter = " ";  aCaracter = &225; |FINSI;
           |SI aCaracter = "‚";  aCaracter = &233; |FINSI;
           |SI aCaracter = "¡";  aCaracter = &237; |FINSI;
           |SI aCaracter = "¢";  aCaracter = &243; |FINSI;
           |SI aCaracter = "£";  aCaracter = &250; |FINSI;
           |SI aCaracter = "§";  aCaracter = &176; |FINSI;
           |SI aCaracter = "¦";  aCaracter = &170; |FINSI;
           aCadena = aCadena + aCaracter;
     |SIGUE;
     aAlfa = aCadena;
|FPRC;

|PROCESO GrabaLogCorreo;
     |ABRE #900;
     #900#0  = aReferencia;
     |LEE 101#900=;
     |SI FSalida ! 0;
         |DDEFECTO #900;
         #900#0  = aReferencia;
         |GRABA 020#900b;
     |FINSI;

                          #900#1  = "SI";
     |SI aEnviado ! "S";  #900#1  = "NO";  |FINSI;

     #900#2 = aIncidencia;
     #900#3 = #511#2;
     #900#4 = #0#1;
     #900#5 = (("00" + #0#2) % -102) + ":" + (("00" + #0#3) % -102) + ":" + (("00" + #0#18) % -102);
     #900#6 = #0#5;
     #900#7 = (("00" + #0#6) % -102) + ":" + (("00" + #0#7) % -102) + ":" + "00";

     |GRABA 020#900a;
     |LIBERA #900;
|FPRC;

|PROCESO PorOutlook;
     aCorreo = #511#2;
     aFic    = aRutaCorreo + #511#1;    |QBF aFic;
     aAlfa   = aFic + ".err";   |RFBORRA aAlfa;
     aAlfa   = aFic + ".bue";   |RFBORRA aAlfa;

     |HAZ TraeDSENVIO;
     |SI enFSalida ! 0;
         nError      = 1;
         aEnviado    = "N";
         nSwEnvio    = 1;
         nSwMensaje  = 1;
         aIncidencia = "No puedo traerme el DSENVIO del servidor";
         |HAZ GrabaLogCorreo;
         |ACABA;
     |FINSI;

     aAbre   = aFic + ".txt";
     |XABRE "WBC", aAbre, nHandle;

     aAlfa = #518#4; |QBF aAlfa;
     aAlfa = aAlfa + &13 + &10;    |XGRABA nHandle, aAlfa;

     aAlfa = #518#6; |QBF aAlfa;
     aAlfa = aAlfa + &13 + &10;    |XGRABA nHandle, aAlfa;

     aAlfa = #518#7; |QBF aAlfa;
     aAlfa = aAlfa + &13 + &10;    |XGRABA nHandle, aAlfa;

     aAlfa = #518#5; |QBF aAlfa;
     aAlfa = aAlfa + &13 + &10;    |XGRABA nHandle, aAlfa;

     aAlfa = #511#2; |QBF aAlfa;
     aAlfa = aAlfa + &13 + &10;    |XGRABA nHandle, aAlfa;

     aAlfa = (#0#5 % 102) + "/" + (#0#5 % 402) + "/" + (#0#5 % -104);
     aAlfa = aAlfa + &13 + &10;    |XGRABA nHandle, aAlfa;

     aAlfa = (("00" + #0#6) % -102) + ":"+ (("00" + #0#7) % -102);
     aAlfa = aAlfa + &13 + &10;        |XGRABA nHandle, aAlfa;

     nNume1 = #0#6;
     nNume2 = #0#7 + 5;

     |SI nNume2 ] 60;
         nNume1 = #0#6 + 1;
         nNume2 = 0;
     |FINSI;

     |SI nNume1 > 23;
         nNume1 = #0#6;
         nNume2 = 59;
     |FINSI;

     aAlfa = (("00" + nNume1) % -102) + ":"+ (("00" + nNume2) % -102);
     aAlfa = aAlfa + &13 + &10;        |XGRABA nHandle, aAlfa;


     aAlfa = "";
     |SI #0#0 ! 0;
         aAlfa = aAlfa + #0#0 + "  ";
     |FINSI;

     |SI #0#19 = "S";;
         aAlfa = aAlfa + "PLAZO  ";
     |FINSI;

     aAlfa = aAlfa + #0#10;  |QBF aAlfa;
     aAlfa = aAlfa + " ";
     aAlfa = aAlfa + #0#11;  |QBF aAlfa;
     aAlfa = aAlfa + &13 + &10;  |XGRABA nHandle, aAlfa;

     aAlfa = #0#4 > "";
     aAlfa = aAlfa + &13 + &10;        |XGRABA nHandle, aAlfa;

     aAlfa = aRef + &13 + &10;         |XGRABA nHandle, aAlfa;

     aAlfa = #0#10;   |QBF aAlfa;
     aAlfa = aAlfa + &13 + &10;        |XGRABA nHandle, aAlfa;

     aAlfa = #0#11;   |QBF aAlfa;
     aAlfa = aAlfa + &13 + &10;        |XGRABA nHandle, aAlfa;

     aAlfa = #0#12;   |QBF aAlfa;
     aAlfa = aAlfa + &13 + &10;    |XGRABA nHandle, aAlfa;

     aAlfa = #0#13;   |QBF aAlfa;
     aAlfa = aAlfa + &13 + &10;    |XGRABA nHandle, aAlfa;

     aAlfa = #0#14;   |QBF aAlfa;
     aAlfa = aAlfa + &13 + &10;    |XGRABA nHandle, aAlfa;

     aAlfa = #0#15;   |QBF aAlfa;
     aAlfa = aAlfa + &13 + &10;    |XGRABA nHandle, aAlfa;

     |SI #0#21 ! "          ";
         aAlfa = "Remite: " + #0#21;  |QBF aAlfa;
         aAlfa = aAlfa + " " + #0#22;  |QBF aAlfa;
         aAlfa = aAlfa + &13 + &10;  |XGRABA nHandle, aAlfa;
     |FINSI;

     |XCIERRA nHandle;

     aEjecuta = "cmd " + &34 + "/c START /WAIT " + eaUnidadBasico + "\DOCUMENTOS\IPABOGAD\dsenvio " + aAbre + &34;

     |RSYSTEM aEjecuta;
     |PULSATECLA;

     |RFBORRA aAbre;

     aAbre = aFic + ".err";
     |XABRE "EC", aAbre, nHandle;
     |SI FSalida ] 0;
         |XABRE "ABC", aAbre, nHandle;
         |SI FSalida ] 0;
             |XLEE nHandle, 255, aIncidencia;
             |QBF aIncidencia;
             |XCIERRA nHandle;
             aEnviado    = "N";
             nSwEnvio    = 1;
             nSwMensaje  = 1;
         |FINSI;
     |FINSI;

     aAbre = aFic + ".bue";
     |XABRE "EC", aAbre, nHandle;
     |SI FSalida ] 0;
         |XABRE "ABC", aAbre, nHandle;
         |SI FSalida ] 0;
             |XLEE nHandle, 255, aIncidencia;
             |QBF aIncidencia;
             |XCIERRA nHandle;

             aEnviado = "S";
         |FINSI;
     |FINSI;

     |HAZ GrabaLogCorreo;

     aAlfa  = aFic + ".err";   |RFBORRA aAlfa;
     aAlfa  = aFic + ".bue";   |RFBORRA aAlfa;
|FPRC;

|PROCESO GrabaConRetorno;
     aAlfa = aAlfa + &13 + &10;
     |XGRABA nHandle, aAlfa;
|FPRC;

|PROCESO GrabaSinRetorno;
     |XGRABA nHandle, aAlfa;
|FPRC;

|PROCESO DondeEstasJava;
     aJava = "/usr/bin/java";
     |XABRE "E", aJava, nHandle;
     |SI FSalida ] 0;  |ACABA;  |FINSI;

     aJava = "/usr/local/bin/java";
     |XABRE "E", aJava, nHandle;
     |SI FSalida ] 0;  |ACABA;  |FINSI;

     aJava = "";
|FPRC;

|PROCESO PorZimbra;
     |DBASE aBase;
     aPathZimbra = aBase + "zimbra";             |MKDIR aPathZimbra;
     aPathZimbra = aBase + "zimbra/" + Usuario;  |MKDIR aPathZimbra;
     aPathZimbra = aBase + "zimbra/" + Usuario + "/";

     |HAZ DondeEstasJava;
     |SI aJava = "";
         nError      = 1;
         aEnviado    = "N";
         nSwEnvio    = 1;
         nSwMensaje  = 1;
         aIncidencia = "No encuentro el JAVA en el servidor";
         |HAZ GrabaLogCorreo;
         |ACABA;
     |FINSI;

     aCorreo = #511#2;
     aFic    = aPathZimbra + #511#1;    |QBF aFic;

     aAbre   = aFic + ".xml";
     |XABRE "WB", aAbre, nHandle;

     aAlfa = "<?xml version=" + &34 + "1.0"  + &34 + " encoding=" + &34 + "UTF-8" + &34 + "?>";
     |HAZ GrabaConRetorno;

     aAlfa = "<meeting>";
     |HAZ GrabaConRetorno;

     aAlfa = "<asmtp_server>";              |HAZ GrabaSinRetorno;
     aAlfa = #518#4;     |QBF aAlfa;        |HAZ GrabaSinRetorno;
     aAlfa = "</asmtp_server>";             |HAZ GrabaConRetorno;

     aAlfa = "<usr_mail>";                  |HAZ GrabaSinRetorno;
     aAlfa = #518#6; |QBF aAlfa;            |HAZ GrabaSinRetorno;
     aAlfa = "</usr_mail>";                 |HAZ GrabaConRetorno;

     aAlfa = "<clave_mail>";                |HAZ GrabaSinRetorno;
     aAlfa = #518#7; |QBF aAlfa;            |HAZ GrabaSinRetorno;
     aAlfa = "</clave_mail>";               |HAZ GrabaConRetorno;

     aAlfa = "<organizador>";               |HAZ GrabaSinRetorno;
     aAlfa = #518#6; |QBF aAlfa;            |HAZ GrabaSinRetorno;
     aAlfa = "</organizador>";              |HAZ GrabaConRetorno;

     aAlfa = "<destino>";                   |HAZ GrabaSinRetorno;
     aAlfa = #511#2; |QBF aAlfa;            |HAZ GrabaSinRetorno;
     aAlfa = "</destino>";                  |HAZ GrabaConRetorno;

     aAlfa = "<fecha_inicio>";              |HAZ GrabaSinRetorno;
     aAlfa = (#0#5 % -104) + "-" + (#0#5 % 402) + "-" + (#0#5 % 102);     |HAZ GrabaSinRetorno;
     aAlfa = " " + (("00" + #0#6) % -102) + ":"+ (("00" + #0#7) % -102);  |HAZ GrabaSinRetorno;
     aAlfa = "</fecha_inicio>";                                           |HAZ GrabaConRetorno;

                       nNume1 = #0#6;      nNume2 = #0#7 + 5;
     |SI nNume2 ] 60;  nNume1 = #0#6 + 1;  nNume2 = 0;   |FINSI;
     |SI nNume1 > 23;  nNume1 = #0#6;      nNume2 = 59;  |FINSI;

     aAlfa = "<fecha_final>";              |HAZ GrabaSinRetorno;
     aAlfa = (#0#5 % -104) + "-" + (#0#5 % 402) + "-" + (#0#5 % 102);         |HAZ GrabaSinRetorno;
     aAlfa = " " + (("00" + nNume1) % -102) + ":"+ (("00" + nNume2) % -102);  |HAZ GrabaSinRetorno;
     aAlfa = "</fecha_final>";                                               |HAZ GrabaConRetorno;

     aAlfa = "<asunto>";                   |HAZ GrabaSinRetorno;

                       aAlfa = "";
     |SI #0#0 ! 0;     aAlfa = aAlfa +  #0#0 + "  ";  |FINSI;
     |SI #0#19 = "S";  aAlfa = aAlfa + "PLAZO  ";                |FINSI;

     aAlfa = aAlfa + #0#10;  |QBF aAlfa;
     aAlfa = aAlfa + " ";
     aAlfa = aAlfa + #0#11;  |QBF aAlfa;   |HAZ GrabaSinRetorno;

     aAlfa = "</asunto>";                  |HAZ GrabaConRetorno;

     aAlfa = "<aviso>";                    |HAZ GrabaSinRetorno;
     aAlfa = (#0#4 > "");                  |HAZ GrabaSinRetorno;
     aAlfa = "</aviso>";                   |HAZ GrabaConRetorno;

     aAlfa = "<referencia>";               |HAZ GrabaSinRetorno;
     aAlfa = aRef;                         |HAZ GrabaSinRetorno;
     aAlfa = "</referencia>";              |HAZ GrabaConRetorno;

     aAlfa = "<descripcion>";               |HAZ GrabaSinRetorno;
     aAlfa = "<![CDATA[";                   |HAZ GrabaSinRetorno;
     aAlfa = #0#10;   |QBF aAlfa;           |HAZ GrabaSinRetorno;
     aAlfa = "<br>" + #0#11;   |QBF aAlfa;  |HAZ GrabaSinRetorno;
     aAlfa = "<br>" + #0#12;   |QBF aAlfa;  |HAZ GrabaSinRetorno;
     aAlfa = "<br>" + #0#13;   |QBF aAlfa;  |HAZ GrabaSinRetorno;
     aAlfa = "<br>" + #0#14;   |QBF aAlfa;  |HAZ GrabaSinRetorno;
     aAlfa = "<br>" + #0#15;   |QBF aAlfa;  |HAZ GrabaSinRetorno;

     |SI #0#21 ! "          ";
         aAlfa = "<br>Remite: " + #0#21;  |QBF aAlfa;  |HAZ GrabaSinRetorno;
         aAlfa = "<br>" + #0#22;  |QBF aAlfa;          |HAZ GrabaSinRetorno;
     |FINSI;

     aAlfa = "]]>";                         |HAZ GrabaSinRetorno;
     aAlfa = "</descripcion>";              |HAZ GrabaConRetorno;

     aAlfa = "</meeting>";                  |HAZ GrabaConRetorno;

     |XCIERRA nHandle;

     aOrigen  = aBase + "plantillas/dsmeeting.jar";
     aDestino = aPathZimbra + "dsmeeting.jar";

     aContiene1 = aDestino;
     |ESPECIFICA "MD5", aContiene;
     aDocRemoto = FSalida;  |QBF aDocRemoto;

     aContiene1 = aOrigen;
     |ESPECIFICA "MD5", aContiene;
     aDocServidor = FSalida;  |QBF aDocServidor;

     |SI aDocRemoto ! aDocServidor;
         |COPIA_FICHERO aOrigen, aDestino;
     |FINSI;

     aEjecuta = aJava + " -jar " + aPathZimbra + "dsmeeting.jar " + aAbre;
     |SYSTEM aEjecuta;

     aAbre = aFic + ".err";
     |XABRE "E", aAbre, nHandle;
     |SI FSalida ] 0;
         |XABRE "", aAbre, nHandle;
         |SI FSalida ] 0;
             |XLEE nHandle, 255, aIncidencia;
             |QBF aIncidencia;
             |XCIERRA nHandle;
             aEnviado    = "N";
             nSwEnvio    = 1;
             nSwMensaje  = 1;
         |FINSI;
     |FINSI;

     aAbre = aFic + ".bue";
     |XABRE "E", aAbre, nHandle;
     |SI FSalida ] 0;
         |XABRE "", aAbre, nHandle;
         |SI FSalida ] 0;
             |XLEE nHandle, 255, aIncidencia;
             |QBF aIncidencia;
             |XCIERRA nHandle;

             aEnviado = "S";
         |FINSI;
     |FINSI;

     |HAZ GrabaLogCorreo;

     aAlfa  = aFic + ".err";   |FBORRA aAlfa;
     aAlfa  = aFic + ".bue";   |FBORRA aAlfa;
     aAlfa  = aFic + ".xml";   |FBORRA aAlfa;
|FPRC;


|PROCESO abom0511C;
     aReferencia = aRef + #511#1;   |QBF aReferencia;

     |SI aCorreo = #511#2;  |ACABA;  |FINSI;

     aCorreo = #511#2;  |QBF aCorreo;
     |SI aCorreo = "";
         aIncidencia = "Sin cuenta de correo.";
         aEnviado    = "S";
         |ACABA;
     |FINSI;

     |ABRE #abom0500;
     #abom0500#0 = #abom0511#1;
     |LEE 000#abom0500.=;
     |SI FSalida ! 0;  |DDEFECTO #abom0500;  |FINSI;
     |CIERRA #abom0500;

     |SI #abom0500#2 = "O";  |HAZ PorOutlook;  |FINSI;
     |SI #abom0500#2 = "Z";  |HAZ PorZimbra;   |FINSI;

     |PULSATECLA;

|FPRC;

|DEFBUCLE abom0511C;
     #511#3;
     ;
     #0#8, "                                                            ";
     #0#8, "zzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzz";
     ;
     NULL, abom0511C;
|FIN;

|PROCESO abom0006;
     |SI #0#4 = "N";
         #1006#0  = #0#0;
         #1006#1  = #0#1;
         #1006#2  = #0#2;
         #1006#3  = #0#3;
         #1006#18 = #0#18;
         |LEE 101#1006=;
         |SI FSalida = 0;
             #1006#20 = "S";
             |GRABA 020#1006a;
             |LIBERA #1006;
         |FINSI;
         |ACABA;
     |FINSI;

     aRef = (#0#1 % -104) + (#0#1 % 402) + (#0#1 % 102);
     aRef = aRef + (("00" + #0#2)  % -102);
     aRef = aRef + (("00" + #0#3)  % -102);
     aRef = aRef + (("00" + #0#18) % -102);
     aRef = aRef + ("00000000" + #0#0) % -108;

     aCorreo  = "";
     aEnviado = "N";
     nSwEnvio = 0;
     |BUCLE abom0511C;

     |SI nDirecto = 0;
         #1006#0  = #0#0;
         #1006#1  = #0#1;
         #1006#2  = #0#2;
         #1006#3  = #0#3;
         #1006#18 = #0#18;
         |LEE 101#1006=;
         |SI FSalida = 0;
             |SI nSwEnvio = 0;
                 #1006#20 = "S";
             |SINO;
                 #1006#20 = "N";
             |FINSI;
             |GRABA 020#1006a;
             |LIBERA #1006;
         |FINSI;
     |SINO;
         |SI nSwEnvio = 0;
             #0#20 = "S";
         |SINO;
             #0#20 = "N";
         |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE abom0006;
     #0#4;
     ;
     "A", 00;
     "N", 99;
     be;
     NULL, abom0006;
|FIN;

|PROCESO Outlook;
     nDirecto = 0;

     |DBASE aBase;  |QBF aBase;
     aMas = aBase + "def/abom0006.mas";
     |CARGA_DEF aMas, abom0006@;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |ABRE #1006;
     |SELECT #4#1006;

     #1006#20 = "A";
     #1006#8  = 0;
     |LEE 000#1006];
     |SI FSalida ! 0;  |O #1006#20 = "S";
         |DESCARGA_DEF #abom0006@;
         |ACABA;
     |FINSI;
     |CIERRA #1006;

     |ABRE #104;
     |LEE 000#104p;
     |SI FSalida ! 0;
        |MENAV "    Parametros para correo no establecidos";
        |CIERRA #104;
        |DESCARGA_DEF #abom0006@;
        |ACABA;
     |FINSI;
     |CIERRA #104;

     |SI #104#2 ! "S";
         |DESCARGA_DEF #abom0006@;
         |ACABA;
     |FINSI;

     FSalida = 0;
     |SI #104#12 = "S";
         |CONFI "0000NDesea hacer el volcado de los mensajes a cada usuario ?";
     |FINSI;

     |SI FSalida ! 0;
         |DESCARGA_DEF #abom0006@;
         |ACABA;
     |FINSI;

     |SUB_EJECUTA "abom0518;PIDECUENTA";

     |ABRE #518;
     #518#0 = enCuenta;
     |LEE 040#518=;
     |SI FSalida ! 0;
         |DESCARGA_DEF #abom0006@;
         |MENAV "       No existe la Cuenta de Correo.";
         |ACABA;
     |FINSI;
     |CIERRA #518;

     aRutaCorreo = eaUnidadBasico + "\DOCUMENTOS";                   |RMKDIR aRutaCorreo;
     aRutaCorreo = aRutaCorreo + "\IPABOGAD";         |RMKDIR aRutaCorreo;
     aRutaCorreo = aRutaCorreo + "\";                 |RMKDIR aRutaCorreo;


     |ABRE #1006;
     |SELECT #1#1006;

     nSwMensaje = 0;
     |BUCLE abom0006;
     |CIERRA #1006;

     |DESCARGA_DEF #abom0006@;

     |SI nSwMensaje ! 0;
         |MENAV "0000 Han habido errores al enviar los correos, revise los datos.";
     |FINSI;
|FPRC;

|PROCESO OutlookLinea;  |TIPO 3;
     |SI #0#4 = "N";
         #0#20 = "S";
         |ACABA;
     |FINSI;

     nDirecto = 1;

     |ABRE #104;
     |LEE 000#104p;
     |SI FSalida ! 0;
        |CIERRA #104;
        |ACABA;
     |FINSI;
     |CIERRA #104;

     |SI #104#2 ! "S";
         #0#20 = "S";
         |ACABA;
     |FINSI;

     FSalida = 0;
     |SI #104#12 = "S";
         |CONFI "0000NDesea hacer el volcado del mensaje a cada usuario ?";
     |FINSI;

     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |SUB_EJECUTA "abom0518;PIDECUENTA";

     |ABRE #518;
     #518#0 = enCuenta;
     |LEE 040#518=;
     |SI FSalida ! 0;
         |MENAV "       No existe la Cuenta de Correo.";
         |ACABA;
     |FINSI;
     |CIERRA #518;

     aRutaCorreo = eaUnidadBasico + "\DOCUMENTOS";    |RMKDIR aRutaCorreo;
     aRutaCorreo = aRutaCorreo + "\IPABOGAD";         |RMKDIR aRutaCorreo;
     aRutaCorreo = aRutaCorreo + "\";                 |RMKDIR aRutaCorreo;

     |HAZ abom0006;

     |SI #0#20 = "N";
         |MENAV "0000 Han habido errores al enviar los correos, revise las cuentas.";
     |FINSI;

     |PULSATECLA;
|FPRC;
