|FICHEROS;
     refer00@ #0;
|FIN;

|VARIABLES;
     aPrograma    = "";
     aDef         = "";
     aQueQuiero   = "";
     aNombreCampo = "";
     aFichero     = "";
     aAlfa        = "";
     aTrab        = "";
     aEmpresa     = "";
     aDire        = "";
     aDire2       = "";

     nCampo       = 0;
     nTCampo      = 0;
     nSwHay       = 0;
     nEmpresa     = 0;
     nNume        = 0;
     nTrab        = 0;
     nBarra       = 0;
     nPara        = 0;

     &eaMoneda    = "";
     &enCanti     = 0;
     &enSwTrata   = 0;
     &enEmpreFac  = -1;

     &EURODB      = 0;

     {-1}aMoneda   = "";
         aMoneda1  = "LEER";
         aMoneda2  = "";
|FIN;

|PROCESO PintaMoneda;  |TIPO 7;
     aPrograma = Control;  |QBF aPrograma;

     |DBASE aDef;  |QBF aDef;
     aDef   = aDef + "def/" + aPrograma + ".mas";
     |CARGA_DEF aDef, refer00@;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     aQueQuiero = "|NC";
     aFichero   = #0#aQueQuiero#;
     nTCampo    = aFichero;
     nTCampo    = nTCampo - 1;
     nSwHay     = 0;
     |PARA nCampo = 0;  |SI nCampo [ nTCampo;  |HACIENDO nCampo = nCampo + 1;
           aQueQuiero   = "|C" + (("000" + nCampo) % -103) + "NOMBRE";
           aNombreCampo = (#0#aQueQuiero# + (" " * 20)) % 120;
           |QBF aNombreCampo;
           |SI aNombreCampo = "***MONEDA***";
               nSwHay = 1;
               |SAL;
           |FINSI;
     |SIGUE;

     |SI nSwHay = 0;
         |DESCARGA_DEF #refer00@;
         |ACABA;
     |FINSI;

     IaMoneda = aMoneda1<-;
     |ESPECIFICA "MODO_VER_EURO", aMoneda;

     |SI EURODB ! 0;
         |SI aMoneda2 = "0";
             #0nCampo = "EUROS";
         |SINO;
             #0nCampo = "PESETAS";
         |FINSI;
     |SINO;
         |SI aMoneda2 = "0";
             #0nCampo = "PESETAS";
         |SINO;
             #0nCampo = "EUROS";
         |FINSI;
     |FINSI;

     |PINTA #0nCampo;
     eaMoneda = #0nCampo;

     |DESCARGA_DEF #refer00@;
|FPRC;

|PROCESO ConverMoneda;
     enCanti = enCanti ! EURODB;
     |SI EURODB ! 0;
         |SI eaMoneda = "PESETAS";
             enCanti = ((enCanti * 166.386) ! 0);
         |SINO;
             |SI enSwTrata = 0;  enCanti = enCanti * 100;  |FINSI;
         |FINSI;
     |SINO;
         |SI eaMoneda = "EUROS  ";
             |SI enSwTrata = 0;
                 enCanti = ((enCanti / 166.386) ! 2) * 100;
             |SINO;
                 enCanti = (enCanti / 166.386) ! 2;
             |FINSI;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO LeeMoneda;  |TIPO 80;
     |SI enEmpreFac = -1;
         nEmpresa = #48#0;
     |SINO;
         nEmpresa = enEmpreFac;
     |FINSI;

     aEmpresa = ("00" + nEmpresa) % -102;

     |DBASE aDire;
     aAlfa = aDire % 0;
     nNume = aAlfa;
     |PARA nPara = nNume; |SI nPara ] 1; |HACIENDO nPara = nPara - 1;
          nTrab = (nPara * 100) + 1;
          aTrab = aDire % nTrab;
          |SI aTrab = "/";
               |SI nBarra = 1;
                    |SAL;
               |FINSI;
               nBarra = 1;
          |SINO;
               aDire2 = aTrab + aDire2;
          |FINSI;
     |SIGUE;
     aDire = aDire2;

     aAlfa =  ":agitool/zmonedas;*" + aDire + "*" + aEmpresa + "*";
     |SUB_EJECUTA_NP aAlfa;
|FPRC;
