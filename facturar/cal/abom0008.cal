|FICHEROS;
     abom0008 #0;
     aboz0004 #6;
     abom0006 #4;
     abom0009 #7;
     abom0505 #5,MANTE;
     abom0511 #11,MANTE;
     abom0512 #12;
     abow0006 #100;
     asconlin #17;
     asconcli #20;
     asconcep #21;
|FIN;

|VARIABLES;
     nFichero  = 0;
     nCliente  = 0;
     aSeccion  = "";
     aConcepto = "";

     nModo     = 0;
     nCampo    = 0;
     nCampo1   = 0;
     nEntrada  = 0;
     nContador = 0;
     nSwCab    = 0;
     nVariable = 0;
     nHora     = 0;
     nMinu     = 0;

     aAlfa     = "";
     aAlfa1    = "";
     aFichero  = "";
     aSecc     = "";
     aConc     = "";
     aAviso    = "";
     aTipoA    = "";

     fFecha    = @;

     &enCliente     = 0;
     &enExpediente  = 0;
     &efFecha       = @;
     &eaServicio    = "";
     &enPersona     = 0;
     &enLinea       = 0;

     &enDentro  = 0;
     &eaNombreList  = "";
     &enCuadroIni   = 0;
     &enCuadroFin   = 0;
     &enCampoAncho  = 0;
     &enModoEdita   = 0;
     &enTotalVaria  = 0;
{101}&epPuntEdita   = "";
|FIN;

|PROCESO MayusM0008;  |TIPO 6;
     #0Campo = #0Campo > #0Campo;  |PINTA  #0Campo;
|FPRC;

|PROCESO LeePersona_8;  |TIPO 7;
     |SI #0#2 ! 0;  |ACABA;  |FINSI;

     aAlfa = Usuario % 810;

     |ABRE #5;
     |ABRE #11;
     |SELECT #2#11;

     #11#0 = 0;
     #11#1 = aAlfa;
     |LEE 040#11];
     |SI FSalida = 0;  |Y #11#1 = aAlfa;
         #5#0 = #11#0;
         |LEE 040#5=;
         |SI FSalida ! 0;  |DDEFECTO #5;  |FINSI;

         #0#2 = #11#0;
         #0#3 = #5#1;
     |FINSI;
     |CIERRA #5;
     |CIERRA #11;

     |PINTA #0#2;
|FPRC;

|PROCESO PonTReport_8;
     |ABRE #5;
     #5#0 = #0#2;
     |LEE 040#5=;
     |SI FSalida ! 0;  |DDEFECTO #5;  |FINSI;
     |CIERRA #5;

     #0#3  = #5#1;
     #0#17 = (#5#19 / (60 / #5#20));
|FPRC;

|RUTINA ClaveBaja020_8;
     #20#0 = #0#0;
     #20#1 = "    ";
     #20#2 = "  ";
|FRUT;

|RUTINA ClaveAlta020_8;
     #20#0 = #0#0;
     #20#1 = "zzzz";
     #20#2 = "zz";
|FRUT;

|PROCESO BuscaConcep_8;  |TIPO 0;    ||si pulsa F1 accede a conceptos / clientes
     |SI FSalida ! 9; |ACABA; |FINSI;

     |PUSHV 0501 2380;
     |PINPA #0#20;
     |PINTA 926, #0#0;

     aSecc = #0#5;
     aConc = #0#6;

     |ENTLINEAL #1#20, -3, 4, 19, 0, ClaveBaja020_8, ClaveAlta020_8;

     |POPV;

     #0#5  = aSecc;  |PINTA #0#5;
     #0#6  = aConc;  |PINTA #0#6;
|FPRC;

|PROCESO RecogeTexto8;
     |ABRE #7;
     #7#0 = #0#0;
     #7#1 = #0#1;
     #7#2 = 1;
     |LEE 040#7];
     |SI FSalida = 0;  |Y #7#0 = #0#0; |Y #7#1 = #0#1;
         |CIERRA #7;
         |ACABA;
     |FINSI;

     |ABRE #17;
     |PARA nVariable = 1;  |SI nVariable [ 100;  |HACIENDO nVariable = nVariable + 1;
           #17#0 = nFichero;
           #17#1 = nCliente;
           #17#2 = aSeccion;
           #17#3 = aConcepto;
           #17#4 = nVariable;
           |LEE 040#17=;
           |SI FSalida = 0;
               #7#0 = #0#0;
               #7#1 = #0#1;
               #7#2 = nVariable;
               #7#3 = #17#5;
               |GRABA 020#7n;
               |LIBERA #7;
           |FINSI;
     |SIGUE;

     |CIERRA #7;
     |CIERRA #17;
|FPRC;

|PROCESO LeeConcep_8;  |TIPO 0;
     |ABRE #20;
     |ABRE #21;

     #20#0 = #0#0;
     #20#1 = #0#5;
     #20#2 = #0#6;
     |LEE 040#20=;
     |SI FSalida = 0;
         #0#7  = #20#7;
         #0#8  = #20#8;
         #0#9  = #20#9;

         nFichero  = 2;
         nCliente  = #20#0;
         aSeccion  = #20#1;
         aConcepto = #20#2;
         |HAZ RecogeTexto8;
     |SINO;
         #21#0 = #0#5;
         #21#1 = #0#6;
         |LEE 040#21=;
         |SI FSalida ! 0;  |DDEFECTO #21;  |FINSI;

         #0#7 = #21#16;
         #0#8 = #21#17;
         #0#9 = #21#18;

         nFichero  = 1;
         nCliente  = 0;
         aSeccion  = #21#0;
         aConcepto = #21#1;
         |HAZ RecogeTexto8;
     |FINSI;

     |PINTA #0#7;
     |PINTA #0#8;
     |PINTA #0#9;
|FPRC;

|PROCESO EditaAsuntoM008;  |TIPO 7;
     enTotalVaria  = 99;
     eaNombreList  = "";
     enCuadroIni   = 1030;
     enCuadroFin   = 1879;
     enCampoAncho  = 50;
     enModoEdita   = 0;

     |ABRE #7;

     #7#0 = #0#0;  #7#1 = #0#1;  #7#2 = 1;
     |LIBERA #7;
     |LEE 101#7=;
     |SI FSalida ! 0;
         #7#3 = #0#7;  |GRABA 020#7n;
     |FINSI;

     #7#0 = #0#0;  #7#1 = #0#1;  #7#2 = 2;
     |LIBERA #7;
     |LEE 101#7=;
     |SI FSalida ! 0;
         #7#3 = #0#8;  |GRABA 020#7n;
     |FINSI;

     #7#0 = #0#0;  #7#1 = #0#1;  #7#2 = 3;
     |LIBERA #7;
     |LEE 101#7=;
     |SI FSalida ! 0;
         #7#3 = #0#9;  |GRABA 020#7n;
     |FINSI;

     |PARA nVariable = 1;  |SI nVariable [ 100;  |HACIENDO nVariable = nVariable + 1;
           IepPuntEdita  = epPuntEdita1 <-;
           IepPuntEdita = IepPuntEdita + (nVariable - 1);

           #7#0 = #0#0;
           #7#1 = #0#1;
           #7#2 = nVariable;
           |LIBERA #7;
           |LEE 101#7=;
           |SI FSalida ! 0;
               epPuntEdita  = "";
           |SINO;
               epPuntEdita  = #7#3;
               |BORRA 020#7a;
           |FINSI;
     |SIGUE;

     |PUSHV  0501 2380;
     |CUADRO 0829 2080;
     |BLANCO 0930 1979;
     |ATRI S;  |PINTA 0936, "Introduzca el Texto del Seguimiento";  |ATRI s;

     |HAZ Edita;
     |POPV;

     |PARA nVariable = 1;  |SI nVariable [ 100;  |HACIENDO nVariable = nVariable + 1;
           IepPuntEdita = epPuntEdita1 <-;
           IepPuntEdita = IepPuntEdita + (nVariable - 1);

           |DDEFECTO #7;
           #7#0 = #0#0;
           #7#1 = #0#1;
           #7#2 = nVariable;
           |LIBERA #7;
           |LEE 1010#7=;
           |SI FSalida ! 0;  |GRABA 020#7b;  |FINSI;
           #7#3 = epPuntEdita;
           |GRABA 020#7a;

           |SI nVariable = 1;  #0#7 = #7#3;  |FINSI;
           |SI nVariable = 2;  #0#8 = #7#3;  |FINSI;
           |SI nVariable = 3;  #0#9 = #7#3;  |FINSI;
     |SIGUE;

     |PARA nVariable = 100;  |SI nVariable ] 1;  |HACIENDO nVariable = nVariable - 1;
           #7#0 = #0#0;
           #7#1 = #0#1;
           #7#2 = nVariable;
           |LIBERA #7;
           |LEE 101#7=;
           aAlfa = #7#3;  |QBF aAlfa;

           |SI aAlfa ! "";  |SAL;  |FINSI;

           |BORRA 020#7a;
     |SIGUE;

     |CIERRA #7;

     |PINTA #0#7;
     |PINTA #0#8;
     |PINTA #0#9;
|FPRC;

|PROCESO Agenda_8;
     |HORA aAlfa;
     aAlfa1 = aAlfa % 102;  #0#21 = aAlfa1;
     aAlfa1 = aAlfa % 402;  #0#22 = aAlfa1;

     |ABRE #4;
     |DDEFECTO #4;
     #4#0 = 0;
     #4#1 = #0#4;
     #4#2 = #0#21;
     #4#3 = #0#22;
     |LEE 101#4=;
     |SI FSalida ! 0;
         |GRABA 020#4b;
     |FINSI;

     |PUSHV  0501 2380;
     |CUADRO 1020 2375;
     |BLANCO 1121 2274;
     |ATRI S;  |PINTA 1125, "Introduzca datos aviso de la Agenda";  |ATRI s;
     |PINTA 1321, "Aviso S/N :             Tipo Aviso : ";
     |PINTA 1421, "Fecha ....: ";
     |PINTA 1521, "Hora .....: ";
     |PINTA 1621, "Minuto ...: ";

     |PINTA 1333, aAviso;
     |PINTA 1358, aTipoA;
     |PINTA 1433, fFecha;
     |PINTA 1533, nHora;
     |PINTA 1633, nMinu;

     |ET aAviso;
         E_sup  = "1";
         E_inf  = "SN";
         aAviso = 1333 ? 1;
         |SI FSalida = 1;  |VETE FinEti;  |FINSI;
         |SI FSalida = 2;  |VETE aAviso;  |FINSI;

     |ET aTipoA;
         |ATRI R;
         |PINTA 1723, "-0- Solo el dia de Aviso.  -X- Todos los Miercoles.";
         |PINTA 1823, "-T- Todos los Dias.        -J- Todos los Jueves.   ";
         |PINTA 1923, "-2- Dos Semanas Antes.     -V- Todos los Viernes.  ";
         |PINTA 2023, "-1- Una Semanas Antes.     -S- Todos los Sabados.  ";
         |PINTA 2123, "-L- Todos los Lunes.       -D- Todos los Domingos. ";
         |PINTA 2223, "-M- Todos los Martes.                              ";
         |ATRI 0;

         E_sup  = "1";
         E_inf  = "012TLMXJVSD";
         aTipoA = 1358 ? 1;

         |BLANCO 1723 2274;

         |SI FSalida = 1;  |VETE FinEti;  |FINSI;
         |SI FSalida = 2;  |VETE aAviso;  |FINSI;

     |ET fFecha;
         fFecha = 1433 ? 1;
         |SI FSalida = 1;  |VETE FinEti;  |FINSI;
         |SI FSalida = 2;  |VETE aTipoA;  |FINSI;

     |ET nHora;
          E_sup  = "23";
          E_inf  = "00";
          nHora = 1533 ? 1;
          |SI FSalida = 1;  |VETE FinEti;  |FINSI;
          |SI FSalida = 2;  |VETE fFecha;  |FINSI;

     |ET nMinu;
          E_sup  = "59";
          E_inf  = "00";
          nMinu = 1633 ? 1;
          |SI FSalida = 1;  |VETE FinEti;  |FINSI;
          |SI FSalida = 2;  |VETE nHora;   |FINSI;

     |ET FinEti;
          |POPV;
          #4#4  = aAviso;
          #4#5  = fFecha;
          #4#6  = nHora;
          #4#7  = nMinu;
          #4#8  = #0#2;
          #4#9  = #0#3;
          #4#17 = aTipoA;

          |ABRE #7;
          |PARA nCampo = 1;  |SI nCampo [ 6;  |HACIENDO nCampo = nCampo + 1;
                nCampo1 = nCampo + 9;
                #7#0 = #0#0;  #7#1 = #0#1;  #7#2 = nCampo;  |LEE 040#7=;
                |SI FSalida = 0;  #4nCampo1 = #7#3;  |FINSI;
          |SIGUE;
          |CIERRA #7;

          |GRABA 020#4a;
          |LIBERA #4;
          |CIERRA #4;
|FPRC;

|PROCESO PonDefectos_8;
     |ABRE #12;
     #12#0 = #0Campo;
     |LEE 040#12=;
     |SI FSalida ! 0;  |DDEFECTO #12;  |FINSI;
     |CIERRA #12;

     #0#11 = #12#3;
     #0#12 = #12#2;
     #0#13 = #12#4;
     #0#14 = #12#5;
     #0#15 = #12#6;
     #0#19 = "N";
     #0#20 = "N";
|FPRC;

|PROCESO Tipo2M0008;  |TIPO 2;
     nEntrada = 0 +. 1;
     nModo    = (FEntrada / 100) ! 0;

     |SI #0#19 = "S";  |Y #0#13 = "S";
         |SI nEntrada ! 1;  |MENAV "     Linea Minutada.";  |FINSI;
         |ERROR;
         |ACABA;
     |FINSI;

     |SI #0#20 = "S";  |Y #0#13 = "S";
         |SI nEntrada ! 1;  |MENAV "     Linea Facturada.";  |FINSI;
         |ERROR;
         |ACABA;
     |FINSI;

     |SI #0#13 = "N";
         #0#19 = "S";   |PINTA #0#19;
         #0#20 = "S";   |PINTA #0#20;
     |SINO;
         #0#19 = "N";   |PINTA #0#19;
         #0#20 = "N";   |PINTA #0#20;
     |FINSI;

     #0#18 = #0#16 * #0#17;

     |SI #0#12 = "S";
         |SI nEntrada = 1;
             |HAZ Agenda_8;
         |SINO;
             |ABRE #4;
             #4#0 = 0;
             #4#1 = #0#4;
             #4#2 = #0#21;
             #4#3 = #0#22;
             |LEE 101#4=;
             |SI FSalida = 0;
                 aAviso = #4#4;
                 aTipoA = #4#17;
                 fFecha = #4#5;
                 nHora  = #4#6;
                 nMinu  = #4#7;
                 |BORRA 020#4a;
             |FINSI;
             |LIBERA #4;
             |CIERRA #4;
         |FINSI;
     |FINSI;

     |SI nModo = 3;
         |ABRE #7;
         |PARA nCampo = 1;  |SI nCampo [ 99;  |HACIENDO nCampo = nCampo + 1;
               #7#0 = #0#0;
               #7#1 = #0#1;
               #7#2 = nCampo;
               |LIBERA #7;
               |LEE 101#7=;
               |SI FSalida = 0;  |BORRA 020#7a;  |FINSI;
         |SIGUE;
         |CIERRA #7;
     |FINSI;
|FPRC;

|| ************************************************************************
||                  PROCESO PARA PONER EN COLORES LAS LINEAS
|| ************************************************************************

|PROCESO Tipo5M0008;  |TIPO 5;
     #0#0 = #6#1;
     #0#1 = 1;
     |LEE 040#0];
     |SI FSalida = 0;  |Y #0#0 = #6#1;
         |SI #0#19 = "S";
             |LINEAS_BI_ATRI #0#19, N, I, 179;
         |SINO;
             |LINEAS_BI_ATRI #0#19, I, N, 179;
         |FINSI;
     |SINO;
         |LINEAS_BI_ATRI #0#19, I, N, 179;
     |FINSI;
|FPRC;

|PROCESO CargaW0006;
     nSwCab    = 1;

     |ABRE #7;
     |PARA nCampo = 1;  |SI nCampo [ 99;  |HACIENDO nCampo = nCampo + 1;
           #7#0 = #0#0;
           #7#1 = #0#1;
           #7#2 = nCampo;
           |LEE 040#7=;
           |SI FSalida = 0;
               nContador = nContador + 1;
               |SI nSwCab = 1;
                   #100#0    = nContador;
                   #100#1    = #0#2;
                   #100#2    = #0#4;
                   #100#3    = #0#5;
                   #100#4    = #0#6;
                   #100#5    = #7#3;
                   #100#6    = #0#10;
                   #100#7    = #0#0;
                   #100#8    = #0#1;
                   #100#9    = #0#19;
                   #100#10   = #0#20;
                   |GRABA 020#100n;
                   nSwCab = 0;
               |SINO;
                   |DDEFECTO #100;
                   #100#0 = nContador;
                   #100#5 = #7#3;
                   #100#7 = #0#0;
                   #100#8 = #0#1;
                   |GRABA 020#100n;
               |FINSI;
               |LIBERA #100;
           |SINO;
               |SI nSwCab = 1;
                   nContador = nContador + 1;
                   #100#0    = nContador;
                   #100#1    = #0#2;
                   #100#2    = #0#4;
                   #100#3    = #0#5;
                   #100#4    = #0#6;
                   #100#5    = "";
                   #100#6    = #0#10;
                   #100#7    = #0#0;
                   #100#8    = #0#1;
                   #100#9    = #0#19;
                   #100#10   = #0#20;
                   |GRABA 020#100n;
               |FINSI;
               |SAL;
           |FINSI;
     |SIGUE;
     |CIERRA #7;
|FPRC;

|DEFBUCLE abom0008;
     #0#1;
     ;
     #6#1, 00001;
     #6#1, 99999;
     ;
     NULL, CargaW0006;
|FIN;

|RUTINA ClaveBaja100_8;
     #100#0 = 1;
|FRUT;

|RUTINA ClaveAlta100_8;
     #100#0 = 99999;
|FRUT;

|PROCESO Tipo11M0008; |TIPO 11;
     |SI FSalida ! 10;  |Y  FSalida ! 11;  |ACABA;  |FINSI;

     |SI FSalida = 10;
         aFichero = ("a3" + Usuario + "zzzzzzzz") % 108; |NOME_DAT #100 aFichero;
         |ABRE #100;  |CIERRA #100;  |DELINDEX #100;

         nContador = 0;
         |ABRE #100;
         |BUCLE abom0008;
         |CIERRA #100;

         |SALVA_DATOS #0;
         |PUSHV 0501 2380;
         |ENTLINEAL #1#100, -1, 4, 22, 1, ClaveBaja100_8, ClaveAlta100_8;
         |POPV;
         |REPON_DATOS #0;
         |LEE 101#0=;

         |ABRE #100;  |CIERRA #100;  |DELINDEX #100;

         |ERROR;
         |PTEC 812;
         |PTEC 812;
     |FINSI;

     |SI FSalida = 11;
         enCliente     = #0#0;
         enExpediente  = 0;
         efFecha       = #0#4;
         eaServicio    = #0#5;
         enPersona     = #0#2;
         enLinea       = #0#1;
         |SUB_EJECUTA "aboz0006;1";
         enLinea       = 0;
         |ERROR;
         |PTEC 808;
         |PTEC 809;
     |FINSI;
|FPRC;
