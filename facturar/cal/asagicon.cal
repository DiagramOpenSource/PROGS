
                    *** PASE FACTURAS VENTA AL AGICON ***
|FICHEROS;
     :/contagen/def/canempre #3;  ||empresas
     :/contagen/def/calibros #5;  ||libros
     :/modelos/def/dsmom030 #12; ||conceptos contabilidad

     encocuen #9;  ||trabajo
     encoemp  #30; ||Fichero de enlaces
     encorec  #31; ||Recibos enlaces
     entrabaj #8;  ||trabajo

     asconcue #6;  ||control pase
     asagicon #0;  ||pantalla ventas

     ashisfac #7;  ||facturas
     ashiscon #10; ||Conceptos
     ashisrec #19; ||Recibos
     ascuenco #11; ||Cuentas - Conceptos
     asclient #32; ||Clientes

     ashismic #90;
     ashismil #91;

     asw00007 #99;
|FIN;

|VARIABLES;
  Registro  = 100000;
  sw_docu   = 0;
  campo     = 0;
  dto       = 0;
  mes       = " ";
  ivapo     = 0;
  ivava     = 0;
  ivaporte  = 0;
  acuporte  = 0;
  ivavario  = 0;
  acuvario  = 0;
  iva1      = 0;
  iva2      = 0;
  iva3      = 0;
  re1       = 0;
  re2       = 0;
  re3       = 0;
  dh        = "";
  hh        = "";
  sw_pas    = 0;
  y         = 0;
  z         = 0;
  c         = 0;
  digito1   = 0;
  x         = 0;
  importe   = 0;
  lincont   = 0;
  asi       = 0;
  s         = 0;
  fac       = 0;
  fec       = 0;
  cli       = 0;
  sw_nueve  = 0;
  contador  = 0;
  conta     = 0;
  pepe      = 0;
  digito    = 0;
  pongo     = 0;
  cojo      = 0;
  d_cuen    = "            ";
  h_cuen    = "zzzzzzzzzzzz";
  d         = "D"
  h         = "H";
  men1      = "     NO EXISTE EMPRESA !";
  men2      = "     Longitud de Cuenta Fuera de Nivel !";
  path      = "";
  long      = "  ";
  dir_fich  = "";
  &empresa  = 0;
  anyo      = "";
  cod_emp1  = "";
  cod_emp2  = "";
  clave1    = "";
  clave2    = "";
  tra1      = 0;
  cod1      = 0;
  cod2      = 0;
  mes1      = "";
  traba1    = "";
  dia1      = "";
  fec1      = @;
  fec2      = @;
  cam       = 0;
  totfac    = 0;
  cue       = "";
  niv       = 0;
  deb       = "";
  imp       = 0;
  diario    = 0;
  fecha     = @;
  docume    = 0;
  asient    = 0;
  cabe      = 0;
  ordant    = 0;
  fecant    = @;
  cueant    = "";
  digant    = 0;
  coaant    = 0;
  desant    = "";
  conant    = "";
  dcoant    = 0;
  serant    = "";
  linea     = 0;
  cuenta    = "";
  nconcep   = "";
  alfa      = "";
  arte      = "";
  sub_prog  = "";
  sw_acaba  = 0;

  &dig1     = 0;
  &dig3     = 0;
  &dig4     = 0;
  &dig5     = 0;
  &dig6     = 0;
  &dig7     = 0;
  &dig8     = 0;
  &dig9     = 0;
  nombre    = "";
  nombre1   ="";
  dir_fich2 = "";
  emp       = "";
  i         = 0;
  cuen      = "";

  &eaPathContegen = "";
|FIN;

|INCLUYE i_ficher;

|PROCESO cuencon;
|ABRE #11;
#11#0 = #10#5;
#11#1 = #10#6;
|LEE 040#11=;
|SI FSalida = 0;
    |SI #6x = 1;
       importe = #10#9;
    |SINO;
       importe = #10#9 % #7#15;
       importe = #10#9 - importe;
    |FINSI;
    |SI importe ! 0;
       |HAZ montacu1;
       |HAZ concepto;
       #8#0 = lincont;
       #8#1 = asi;
       #8#2 = #6x;
       #8#3 = cuenta;
       #8#4 = digito1;
       #8#5 = #6s;
       |LIBERA #8; |LEE 101#8=;
       |SI FSalida ! 0;
           #8#0 = lincont;
           #8#1 = asi;
           #8#2 = #6x;
           #8#3 = cuenta;
           #8#4 = digito1;
           #8#5 = #6s;
           #8#6 = #6c;
           #8#7 = nconcep;
           #8#8 = 0;
           #8#9 = #7#0;
           #8#10 = #7#1;
           |GRABA 020#8b;
       |FINSI;
       #8#8 = #8#8 + importe;
       |GRABA 020#8a;
       |LIBERA #8;
       cue = cuenta;
       niv = digito1;
       deb = #6s;
       imp = importe;
       |HAZ gratra;
    |FINSI;
|FINSI;
|CIERRA #11;
|FPRC;

|DEFBUCLE ashiscon;
#10#1;
8;
#7#0, #7#1, 001, dh;
#7#0, #7#1, 999, hh;
e;
NULL, cuencon;
|FIN;

|PROCESO cuadre;
|ABRE #32;
#32#0 = #7#3;
|LEE 040#32=;
|SI FSalida ! 0; |DDEFECTO #32; |FINSI;
|CIERRA #32;

sw_pas = 1;
|SI cabe = 0; |HAZ cabecera; cabe = 1; linea = 0; |FINSI;

linea  = linea + 1;
#30#4  = linea;
#30#9  = #8#6;  ||concepto
#30#10 = #8#7;  ||descripcion concepto
#30#11 = #8#5;  ||signo
#30#12 = #8#8;  ||importe
#30#13 = "R";   ||Soportado/repercutido
#30#14 = #6#2;  ||libro
#30#15 = #6#3;  ||descripcion libro
#30#16 = #8#10; ||fra
#30#17 = #8#9;  ||Serie
#30#42 = "";
#30#43 = 0;

|SI #8#2 =  1; #30#42 = "B"; #30#43 = 1; |FINSI;
|SI #8#2 =  2; #30#42 = "B"; #30#43 = 1; |FINSI;
|SI #8#2 =  3; #30#42 = "B"; #30#43 = 1; |FINSI;
|SI #8#2 =  4; #30#42 = "B"; #30#43 = 1; |FINSI;
|SI #8#2 =  5; #30#42 = "B"; #30#43 = 1; |FINSI;
|SI #8#2 =  8; #30#42 = "I"; #30#43 = 6; |FINSI;
|SI #8#2 = 14; #30#42 = "B"; #30#43 = 1; |FINSI;
|SI #8#2 = 15; #30#42 = "B"; #30#43 = 1; |FINSI;

|SI #30#11 = "D";
    #30#5 = #8#3;  ||cUENTA DEBE
    #30#7 = #8#4;  ||NIVEL CUENTA DEBE
    #30#6 = "";
    #30#8 = 0;
|SINO;
    #30#5 =  "";   ||debe
    #30#7 = 0;     ||debe
    #30#6 = #8#3;  ||cuenta haber
    #30#8 = #8#4;  ||nivel debe
|FINSI;

||Se ponen los valores a 0 porque se grabara solamente con la clave
||del apunte

|PARA i = 22; |SI i [ 36; |HACIENDO i = i + 1;
     #30i = 0;
|SIGUE;

#30#45 = #0#13;
#30#46 = #0#14;
#30#47 = 0;
#30#48 = #32#8;
|SI sw_docu = 1; #30#47 = #30#2; |FINSI;
|GRABA 021#30n;
|LIBERA #30;
|FPRC;

|DEFBUCLE entrabaj1;
#8#1;
;
1, 1, 00, d_cuen, 1, d;
8, 1, 99, h_cuen, 6, h;
e;
NULL, cuadre;
|FIN;

|DEFBUCLE entrabaj2;
#8#1;
;
1, 2, 00, d_cuen, 1, d;
8, 2, 99, h_cuen, 6, h;
e;
NULL, cuadre;
|FIN;

|PROCESO hacer;
#7#0 = #99#1;
#7#1 = #99#2;
|LIBERA #7;
|LEE 140#7=;
|SI FSalida ! 0; |ACABA; |FINSI;

|SI cam = 0; fecant = #7#2; |HAZ acerar; cam = 1; |FINSI;

|SI #0#6 = "F";
   |SI fecant ! #7#2;
      Registro = 99999;
      |SI #6#4 = 1;
         sw_pas = 0;
         |BUCLE entrabaj1;
         |SI sw_pas = 1; |HAZ suma; |HAZ acerar; |FINSI;
      |SINO;
         sw_pas = 0;
         |BUCLE entrabaj1;
         |SI sw_pas = 1; |HAZ suma; |FINSI;

         sw_pas = 0;
         |BUCLE entrabaj2;
         |SI sw_pas = 1; |HAZ suma; |FINSI;

         |HAZ acerar;
      |FINSI;
      fecant = #7#2;
  |SINO;
      Registro = Registro - 1;
  |FINSI;
|SINO;
  Registro = 0;
  fecant   = #7#2;
|FINSI;

|SI #6#4 = 1;
   |HAZ factura1;
|SINO;
   |HAZ factura1;
   |HAZ factura2;
|FINSI;

|HAZ iva;

|SI #0#6 = "R";
   |SI #6#4 = 1;
      sw_pas = 0;
      |BUCLE entrabaj1;
      |SI sw_pas = 1; |HAZ suma; |FINSI;
      |HAZ acerar;
   |SINO;
      sw_pas = 0;
      |BUCLE entrabaj1;
      |SI sw_pas = 1; |HAZ suma; |FINSI;
      sw_pas = 0;
      |BUCLE entrabaj2;
      |SI sw_pas = 1; |HAZ suma; |FINSI;
      |HAZ acerar;
   |FINSI;
|FINSI;

#7#13 = "S";
|GRABA 020#7a;
|LIBERA #7;

#90#0 = #7#0;
#90#1 = #7#1;
|LIBERA #90;
|LEE 140#90=;
|SI FSalida = 0;
    #90#31 = "S";
    |GRABA 140#90a;
|FINSI;
|FPRC;

|DEFBUCLE asw00007;
#99#1;
8;
000, "S";
999, "S";
be;
NULL, hacer;
|FIN;

|PROCESO suma;
  cabe = 0;
  docume = docume + 1;
  asient = asient + 1;
|FPRC;

|PROCESO acerar;
  |CIERRA #8;
  |DELINDEX #8;
  |ABRE #8;
|FPRC;

|PROCESO iva;
y = 117; z = 118; c = 119;
|HAZ montacu;
|HAZ concepto;
|DDEFECTO #30;
linea = 0;

|SI #0#6 = "R"; |Y #0#18 = "S";
     #30#1  = fecha;
     #30#41 = fecha;
|SINO;
     #30#1  = fecant;
     #30#41 = fecant;
|FINSI;
#30#4 = Registro;
#30#0 = diario;
#30#2 = docume;  ||documento
|LEE 101#30=;
|SI FSalida ! 0;
     |GRABA 021#30b;
|FINSI;

#30#16 = #7#1; ||fra
#30#17 = #7#0; ||serie
#30#14 = #6#2; ||libro
#30#38 = "N";  ||inversiones
#30#13 = "R";  ||Repercutido
mes    = #30#1 % 402;
#30#44 = mes;  ||periodo

|SI #0#21 = "T";
    #30#44 = 1;
    |SI #30#44 > 3; #30#44 = 2; |FINSI;
    |SI #30#44 > 6; #30#44 = 3; |FINSI;
    |SI #30#44 > 9; #30#44 = 4; |FINSI;
|FINSI;

#5#0 = #6#2;
|LEE 040#5=;
|SI FSalida ! 0;
    #30#15 = "INEXISTENTE";
|SINO;
    #30#15 = #5#1;
|FINSI;

#30#20 = cuenta;         ||cuenta iva
#30#21 = digito1;        ||digito iva
#30#18 = #6c;            ||concepto iva
#30#19 = nconcep;        ||descripcion concepto iva

#30#22 = #30#22 + #7#17;                       || base iva
|SI #7#5 ! 0; #30#25 = #7#7; |FINSI;           || Iva%
#30#28 = #30#28 + #7#8;                        || Cuota

#30#26 = 0;
#30#27 = 0;
#30#29 = 0;
#30#30 = 0;
#30#32 = 0;
#30#33 = 0;
#30#35 = 0;
#30#36 = 0;
#30#39 = "S";
#30#37 = #30#37 + (#7#17 + #7#8);              ||Total Factura;

#30#45 = #0#13;
#30#46 = #0#14;
#30#47 = 0;
|SI sw_docu = 1; #30#47 = #30#2; |FINSI;

|GRABA 021#30a;
|LIBERA #30;
|SI #0#22 = "S"; |HAZ contacob; |FINSI;
|FPRC;

|PROCESO factura1;
|PARA x = 5; |SI x [ 12; |HACIENDO x = x + 1;
    |SI x = 5;  lincont = 1; |FINSI;
    |SI x = 6;  lincont = 2; |FINSI;
    |SI x = 7;  lincont = 3; |FINSI;
    |SI x = 8;  lincont = 4; |FINSI;
    |SI x = 9;  lincont = 5; |FINSI;
    |SI x = 10; lincont = 6; |FINSI;
    |SI x = 11; lincont = 7; |FINSI;
    |SI x = 12; lincont = 8; |FINSI;
    |SI #6x ! 0;
        y = x + 8;
        z = x + 16;
        s = x + 24;
        c = x + 32;
        fac = x + 40;
        fec = x + 48;
        cli = x + 151;
        |SI #6x ! 1; |Y #6x ! 2; |Y #6x ! 14; |Y #6x ! 15;
            |HAZ importe;
            |SI importe ! 0;   ||Si existe importe
                |HAZ montacu;
                |HAZ concepto;
                #8#0 = lincont;
                #8#1 = 1;
                #8#2 = #6x;
                #8#3 = cuenta;
                #8#4 = digito1;
                #8#5 = #6s;
                |LIBERA #8; |LEE 101#8=;
                |SI FSalida ! 0;
                    #8#0 = lincont;
                    #8#1 = 1;
                    #8#2 = #6x;
                    #8#3 = cuenta;
                    #8#4 = digito1;
                    #8#5 = #6s;
                    #8#6 = #6c;
                    #8#7 = nconcep;
                    #8#8 = 0;
                    #8#9 = #7#0;
                    #8#10 = #7#1;
                    |GRABA 020#8b;
                |FINSI;
                #8#8 = #8#8 + importe;
                |GRABA 020#8a;
                |LIBERA #8;
                cue = cuenta;
                niv = digito1;
                deb = #6s;
                imp = importe;
                |HAZ gratra;
            |FINSI;
        |SINO;
           asi = 1;
           |SI #6x = 1;  |O #6x = 2;  dh = "H"; hh = "H"; |FINSI;
           |SI #6x = 14; |O #6x = 15; dh = "H"; hh = "S"; |FINSI;
           |BUCLE ashiscon;
        |FINSI;
    |FINSI;
|SIGUE;
|FPRC;

|PROCESO factura2;
|PARA x = 61; |SI x [ 68; |HACIENDO x = x + 1;
    |SI x = 61; lincont = 1; |FINSI;
    |SI x = 62; lincont = 2; |FINSI;
    |SI x = 63; lincont = 3; |FINSI;
    |SI x = 64; lincont = 4; |FINSI;
    |SI x = 65; lincont = 5; |FINSI;
    |SI x = 66; lincont = 6; |FINSI;
    |SI x = 67; lincont = 7; |FINSI;
    |SI x = 68; lincont = 8; |FINSI;
    |SI #6x ! 0;
        y = x + 8;
        z = x + 16;
        s = x + 24;
        c = x + 32;
        fac = x + 40;
        fec = x + 48;
        cli = x + 103;
        |SI #6x ! 1; |Y #6x ! 2; |Y #6x ! 14; |Y #6x ! 15;
           |HAZ importe;
           |SI importe ! 0;
              |HAZ montacu;
              |HAZ concepto;
              #8#0 = lincont;
              #8#1 = 2;
              #8#2 = #6x;
              #8#3 = cuenta;
              #8#4 = digito1;
              #8#5 = #6s;
              |LIBERA #8; |LEE 101#8=;
              |SI FSalida ! 0;
                  #8#0 = lincont;
                  #8#1 = 2;
                  #8#2 = #6x;
                  #8#3 = cuenta;
                  #8#4 = digito1;
                  #8#5 = #6s;
                  #8#6 = #6c;
                  #8#7 = nconcep;
                  #8#8 = 0;
                  #8#9 = #7#0;
                  #8#10 = #7#1;
                  |GRABA 020#8b;
              |FINSI;
              #8#8 = #8#8 + importe;
              |GRABA 020#8a;
              |LIBERA #8;
              cue = cuenta;
              niv = digito1;
              deb = #6s;
              imp = importe;
              |HAZ gratra;
           |FINSI;
        |SINO;
           asi = 2;
           |SI #6x = 1;  |O #6x = 2;  dh = "H"; hh = "H"; |FINSI;
           |SI #6x = 14; |O #6x = 15; dh = "H"; hh = "S"; |FINSI;
           |BUCLE ashiscon;
        |FINSI;
    |FINSI;
|SIGUE;
|FPRC;

|PROCESO gratra;
sw_nueve = 1;
|ABRE #9;
#9#0 = 1;
#9#1 = cue;
#9#2 = niv;
|SI #0#6 = "R"; |Y #0#18 = "S";
   #9#3 = fecha;
|SINO;
   #9#3 = fecant;
|FINSI;

#9#4 = deb;
#9#6 = #0#7;
|LIBERA #9; |LEE 110#9=;
|SI FSalida ! 0;
    |DDEFECTO #9;
    #9#0 = 1;
    #9#1 = cue;
    #9#2 = niv;
    |SI #0#6 = "R"; |Y #0#18 = "S";
         #9#3 = #0#8;
    |SINO;
         #9#3 = fecant;
    |FINSI;
    #9#4 = deb;
    #9#6 = #0#7;
    |GRABA 020#9b;
|FINSI;
#9#5 = #9#5 + imp;
|GRABA 0201#9a;
|LIBERA #9;
|CIERRA #9;
|FPRC;

|PROCESO cabecera;
#30#0 = diario;
|SI #0#6 = "R"; |Y #0#18 = "S";
    #30#1 = fecha;
|SINO;
    #30#1 = fecant;
|FINSI;
#30#2 = docume;
#30#3 = asient;
#30#4 = 0;
#30#5 = 0;
#30#6 = 0;
|FPRC;

|PROCESO concepto;
|ABRE #12;
#12#0 = #6c;
|LEE 040#12=;
|SI FSalida ! 0; |ACABA; |FINSI;

nconcep = #12#1;  |QBF nconcep;

|SI #0#6 = "R";
    |SI #6fac = "S";
        nconcep = nconcep + " " + #7#0 + " " + #7#1;
    |FINSI;
    |SI #6fec = "S";
        nconcep = nconcep + " " + #7#2;
    |FINSI;
    |SI #6cli = "S";
        nconcep = nconcep + " " + #99#4;
    |FINSI;
|SINO;
    cuen = #6y;         ||si es clientes debe poner nuestra factura y no
    cuen = cuen % 102;  ||VARIAS
    |SI cuen = "43"; |O cuen = "CL";
        nconcep = nconcep + " " + #7#0 + " " + #7#1;
    |SINO;
        nconcep = nconcep + " " + "VARIAS";
    |FINSI;
|FINSI;
|FPRC;

|PROCESO montacu;
contador = 0;
conta    = 0;
cuenta   = #6y;

|SI #6y = "CLIE        ";
    |ABRE #32;
    #32#0 = #7#3;
    |LEE 040#32=;
    |SI FSalida = 0; |Y #32#52 ! "            ";
        cuenta = #32#52;
    |SINO;
        cuenta = "430*";
        |SI dig4 ! 0; #6z = 1; |FINSI;
        |SI dig5 ! 0; #6z = 2; |FINSI;
        |SI dig6 ! 0; #6z = 3; |FINSI;
        |SI dig7 ! 0; #6z = 4; |FINSI;
        |SI dig8 ! 0; #6z = 5; |FINSI;
        |SI dig9 ! 0; #6z = 6; |FINSI;
    |FINSI;
    |CIERRA #32;
|FINSI;

|PARA pepe = 1; |SI pepe [ 12; |HACIENDO pepe = pepe + 1;
      conta = (pepe * 100) + 1;
      arte = cuenta % conta;
      |SI arte = "*"; contador = pepe; |FINSI;
|SIGUE;

|SI contador ! 0;
    conta = 100 + (contador - 1);
    cuenta = cuenta % conta;
    long = cuenta % 0;
    |SI #6z = 1; digito = dig4; digito1 = 1; |FINSI;
    |SI #6z = 2; digito = dig5; digito1 = 2; |FINSI;
    |SI #6z = 3; digito = dig6; digito1 = 3; |FINSI;
    |SI #6z = 4; digito = dig7; digito1 = 4; |FINSI;
    |SI #6z = 5; digito = dig8; digito1 = 5; |FINSI;
    |SI #6z = 6; digito = dig9; digito1 = 6; |FINSI;
    pongo = digito - long;
    alfa = #7#3;
    alfa = ("00000" + alfa) % -105;
    alfa = "000000000000" + alfa;
    cojo = (100 + pongo) * -1;
    alfa = alfa % cojo;
    cuenta = cuenta + alfa;
|SINO;
    |QBF cuenta;
    long = cuenta % 0;
    digito1 = 0;
    |SI long = dig4; digito1 = 1; |FINSI;
    |SI long = dig5; digito1 = 2; |FINSI;
    |SI long = dig6; digito1 = 3; |FINSI;
    |SI long = dig7; digito1 = 4; |FINSI;
    |SI long = dig8; digito1 = 5; |FINSI;
    |SI long = dig9; digito1 = 6; |FINSI;
|FINSI;
|FPRC;

|PROCESO montacu1;
  cuenta = #11#3;          ||Cuenta contable venta de familias;
  |QBF cuenta;
  long = cuenta % 0;
  digito1 = 0;
  |SI long = dig4; digito1 = 1; |FINSI;
  |SI long = dig5; digito1 = 2; |FINSI;
  |SI long = dig6; digito1 = 3; |FINSI;
  |SI long = dig7; digito1 = 4; |FINSI;
  |SI long = dig8; digito1 = 5; |FINSI;
  |SI long = dig9; digito1 = 6; |FINSI;
  cuenta = #11#3;
|FPRC;

|PROCESO importe;
importe = 0;
|SI #6x = 3; importe = #7#16;                 |FINSI;  || Importe Dto;
|SI #6x = 4; importe = #7#5;                  |FINSI;  || Importe Bruto;
|SI #6x = 5; importe = #7#17;                 |FINSI;  || Bases Imponibles;
|SI #6x = 6; importe = #7#17 + #7#8;          |FINSI;  || B.imponible+iva
|SI #6x = 7; importe = #7#17 + #7#8 - #7#10;  |FINSI;  || B.imponible+iva-irpf
|SI #6x = 8; importe = #7#8;                  |FINSI;  || iva;
|SI #6x = 9; importe = #7#10;                 |FINSI;  || irpf
|SI #6x = 10;importe = #7#14;                 |FINSI;  || A cuenta
|SI #6x = 11;importe = #7#6;                  |FINSI;  || Suplidos
|SI #6x = 12;importe = #7#11;                 |FINSI;  || Total Factura
|SI #6x = 13;importe = #7#18;                 |FINSI;  || Total Recibo
|FPRC;

|PROCESO leereci;
|DDEFECTO #31;
#32#0 = #19#3;
|LEE 001#32=;
|SI FSalida = 0;
    #31#17 = #32#14;                    ||cta + DC
    #31#16 = #32#11;                    ||codig banc +  cod sucursal
|SINO;
    #31#17  = "";
    #31#16  = "";
|FINSI;
#31#11 = #0#23;
#31#12 = 0;
#31#8  = #19#6;
#31#13 = "N";
#31#5  = digito1;
#31#0  = #6#2;  ||libro;
#31#1  = #19#1; ||nro.factura;
#31#2  = #19#0; ||Serie;
#31#3  = 1;     ||nro.recibo;
#31#4  = cuenta;||cta;
#31#6  = #19#2; ||fech.emision;
#31#7  = #19#5; ||fech.vto;
#31#9  = 0;
#31#10 = 0;
#31#15 = #19#7;
#31#18 = "C";
|SI #19#9 > 0;
     #31#24 = "C";
     #31#25 = #19#9;
|FINSI;
|GRABA 020#31n;
|FPRC;

|DEFBUCLE artrecib;
#19#1;
;
#7#0, #7#1;
#7#0, #7#1;
e;
NULL,leereci;
|FIN;

|PROCESO contacob;
|ABRE #31;
|ABRE #32;
   |BUCLE artrecib;
|CIERRA #31;
|CIERRA #32;
|FPRC;

|| ***********************************************************************
||                       GRABACION DEL TEMPORAL
|| ***********************************************************************

|PROCESO graba_ind;
#32#0 = #7#3;
|LEE 040#32=;
|SI FSalida ! 0; |DDEFECTO #32; |FINSI;

linea = linea + 1;
#99#0 = linea;
#99#1 = #7#0;
#99#2 = #7#1;
#99#3 = #7#3;
#99#4 = #32#2;
#99#5 = #7#2;
#99#6 = #7#11;
#99#7 = #7#18;
#99#8 = #0#24;
|GRABA 140#99n;
|FPRC;

|DEFBUCLE ashisfac;
#7#3;
3, 13;
#0#4, #0#0, #0#2, #0#15, "N";
#0#5, #0#1, #0#3, #0#16, "N";
be;
NULL, graba_ind;
|FIN;

|RUTINA inferior;
#99#0 = 1;
|FRUT;

|RUTINA superior;
#99#0 = 999;
|FRUT;

|PROCESO actual; |TIPO 3;
Registro = 100000;
|HAZ borra_puentes;
|SI sw_acaba = 1; |ACABA; |FINSI;

comienza = "07"; |HAZ fichero;
|NOME_DAT #99 fichero;
|ABRE #99;     |CIERRA #99; |DELINDEX #99;

|ABRE #32;
|ABRE #99;
|BUCLE ashisfac;
|SI linea = 0; |ACABA; |FINSI;
|CIERRA #32;
|CIERRA #99;

|ENTLINEAL #1#99, 1, 4, 20, 1, inferior, superior;
|CONFI "2400SConforme con lo Seleccionado : ";
|SI FSalida ! 0;
    |ABRE #99; |CIERRA #99; |DELINDEX #99;
    |ACABA;
|FINSI;

|ABRE #9;      |CIERRA #9;  |DELINDEX #9;
|ABRE #30;     |CIERRA #30; |DELINDEX #30;
|ABRE #31;     |CIERRA #31; |DELINDEX #31;

sw_docu = 0;
|SI #0#9 ! 1; sw_docu = 1; |FINSI;
sw_nueve = 0;
diario   = #0#7;
fecha    = #0#8;
docume   = #0#9;
asient   = #0#10;
cabe     = 0;

|ABRE #5;
|ABRE #7;
|ABRE #8;
|ABRE #30;
|ABRE #90;
cam = 0;

|BUCLE asw00007;

|SI #0#6 ! "R"; |Y cam = 1;
    |SI #6#4 = 1;
        |BUCLE entrabaj1;
    |SINO;
        sw_pas = 0;
        |BUCLE entrabaj1;
        |SI sw_pas = 1;   |HAZ suma; |FINSI;
        |BUCLE entrabaj2;
    |FINSI;
|FINSI;

|CIERRA #30;
|CIERRA #90;
|CIERRA #5;
|CIERRA #7;
|CIERRA #8;
|DELINDEX #8;

|SI #0#11 ! "S"; |ACABA; |FINSI;

sub_prog = (("00000" + #0#13) % -105) + #0#14;
sub_prog = ":contagen/impgest.run canempr1 " + sub_prog;
|QBF sub_prog;
|SUB_EJECUTA sub_prog;
|FPRC;

|PROCESO borra_puentes;
sw_acaba = 0;
|DFICE emp;
|QBF emp;
emp = emp % -202;
|DBASS dir_fich2;
dir_fich2 = dir_fich2 + "facturar/fich/";      ||Directorio base

|PATH_DAT #30 dir_fich2;
|PATH_DAT #31 dir_fich2;
|PATH_DAT #9  dir_fich2;

nombre = "000000" + emp;
nombre = nombre % -104;
nombre1 = "enco" + nombre;          ||nombre del fichero es enco+empresa

|NOME_DAT #30  nombre1;
|ABRE #30;
|LEE 001#30p;
|SI FSalida = 0;
    |AVISO;
    |CONFI "2400NYa existe un enlace de esta empresa, Desea Borrarlo : ";
    |SI FSalida = 0;
        |CIERRA #30;
        |DELINDEX #30;
    |SINO;
        sw_acaba = 1;
        |ACABA;
    |FINSI;
|FINSI;

nombre1 = "enre" + nombre;          ||nombre del fichero es enco+empresa
|NOME_DAT #31  nombre1;
|ABRE #31;
|LEE 001#31p;
|SI FSalida = 0;  ||ha encontrado registros
    |AVISO;
    |CONFI "2400NYa existen recibos enlazados de esta empresa, Desea Borrarlo : ";
    |SI FSalida = 0;
        |CIERRA #31;
        |DELINDEX #31;
    |SINO;
        sw_acaba = 1;
        |ACABA;
    |FINSI;
|FINSI;

nombre1 = "encu" + nombre;
|NOME_DAT #9 nombre1;
|ABRE #9;
|LEE 001#9p;
|SI FSalida = 0; ||ha encontrado registros
    |AVISO;
    |CONFI "2400NYa existen cuentas enlazadas de esta empresa, Desea Borrarlo : ";
    |SI FSalida = 0;
        |CIERRA #9;
        |DELINDEX #9;
    |SINO;
        sw_acaba = 1;
        |ACABA;
    |FINSI;
|FINSI;
|FPRC;

|PROCESO funcion1; |TIPO 11;
|SI FSalida ! 9; |ACABA; |FINSI;

|LIBERA #99;
|LEE 140#99=;

|SI #99#8 = "S"; #99#8 = "N"; |SINO; #99#8 = "S"; |FINSI;
|PINTA #99#8;
|GRABA 140#99a;
|PTEC 809;
|FPRC;

|| ************************ CALCULOS DE PANTALLA *************************

|PROCESO inicio; |TIPO 0;
|HAZ BusquedaApli;

path = eaPathContegen;

empresa = #0#13;
empresa = "00000" + empresa;
empresa = empresa % -105;
anyo = #0#14;
anyo = "0" + anyo;
anyo = anyo % -101;
empresa = empresa + anyo;

|ABRE #3;
#3#2 = #0#13;
#3#3 = #0#14;
|LEE 040#3=;
|SI FSalida ! 0;
   |MENAV "      No existe Empresa Contable";
   |ERROR;
   |CIERRA #3;
   |ACABA;
|FINSI;
|CIERRA #3;

|ABRE #6;
#6#120 = #0#17;
|LEE 040#6=;
|SI FSalida ! 0;
       |MENAV "     No existe control con esta empresa.";
       |ERROR;
       |CIERRA #6;
       |ACABA;
|FINSI;
|CIERRA #6;

path = path + empresa +  "/";
|PATH_DAT #5path;

dig1 = #3#11;
dig3 = #3#13;
dig4 = #3#14;
dig5 = #3#15;
dig6 = #3#16;
dig7 = #3#17;
dig8 = #3#18;
dig9 = #3#19;
clave2 = ("00" + #3#26) % -102;
cod2 = #3#26;
tra1 = 100 + #3#11;
mes1 = tra1 % -102;

|SI cod2 < 80;
    traba1 = "01." + mes1 + ".20" + clave2;
|SINO;
    traba1 = "01." + mes1 + ".19" + clave2;
|FINSI;

fec1 = traba1;
tra1 = 100 + #3#12;
mes1 = tra1 % -102;

|SI #3#11 > #3#12;  cod2 = cod2 + 1;  |FINSI;

dia1 = "31";

|SI #3#12 = 4; |O #3#12 = 6; |O #3#12 = 9; |O #3#12 = 11;
    dia1 = "30";
|FINSI;

|SI #3#12 = 2;
    dia1 = "28";
    |SI cod2 = 88; |O cod2 = 92; |O cod2 = 96; |O cod2 = 0; |O cod2 = 4;
        dia1 = 29;
    |FINSI;
|FINSI;

tra1 = 100 + cod2;
clave2 = tra1 % -102;

|SI cod2 < 80;
    traba1 = dia1 + "." + mes1 + ".20" + clave2;
|SINO;
    traba1 = dia1 + "." + mes1 + ".19" + clave2;
|FINSI;

fec2 = traba1;
#0#4 = fec1;
#0#5 = fec2;
|PINTA #0#4;
|PINTA #0#5;
|FPRC;

|PROCESO fecha;
|SI #0Campo < fec1; |O #0Campo > fec2;
    |MENAV "     Fecha ERRONEA ";
    |ERROR;
|FINSI;
|FPRC;

|PROCESO fechafac; |TIPO 0;
     |SI #0#6 = "F";
          |CAMPO_MODIFICA #0#18 , 1 , AN;
          |CAMPO_MODIFICA #0#8 , 1 , AN;
     |SINO;
          |CAMPO_MODIFICA #0#18 , 1 , AS;
          |CAMPO_MODIFICA #0#8 , 1 , AS;

          |SI #0#18 = "S";
               |CAMPO_MODIFICA #0#8 , 1 , AS;
          |SINO;
               |CAMPO_MODIFICA #0#8 , 1 , AN;
          |FINSI;
     |FINSI;
|FPRC;
