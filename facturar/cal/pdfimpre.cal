|FICHEROS;
     pdfimtmp #100;
     :/laboral/def/pdfcontr #200;
     :/laboral/def/nomcontr;
|FIN;

|VARIABLES;
     aAlfa = "";
     aAlfa1 = "";
     aAlfa2 = "";
     aLinea = "";
     nLinea = 0;
     aValor = "";
     aCampo = "";
     &aArchivo = "";
     aArchivoPDF = "";
     aArchivoFDF = "";
     aPathLocPDF = "";
     aPathSerPDF = "";
     aPathLocFDF = "";
     aPathSerFDF = "";
     aPathReader = "";
     &aID = "";
     aOrigen = "";
     aDestino = "";
     nCanal = 0;
     aSystem = "";
     aDirBase = "";
     aLongitud = "";
     nLongitud = "";
     aCaracter = "";
     aCaracterSal = "";
     aCadena = "";
     aCadenaSal = "";
     nPos = 0;
     nBusca = 0;
     nAscii = 0;

     &enWeb = 0;
     &eaWebArchivo = "";
     &eaWebDirDes = "";
     &eaWebDirPDF = "";
     nTipo = 0;
     aIP = "";
     &swCopia = 0;
     &aProcedencia = "";
     &aListaFich = "";
     &swDondeEstoy = 0;
     aUsuario = "";
     &nTipoSalida = 0;
     &nTipoImpre = 0;
     &nNroCopias = 0;
     aParametros = "";
     i = 0;
     aFicheroBAT = "";
     &aVengoDe = "";
     aMiCaracter = "";
     nCaracter = 0;
     nPorDonde = 0;
     aEjecutable = "";
     aUnidad = "";
     aMatador = "";
     aElUltimo = "";
     aElUltimo2 = "";
     &swPDFMasivo = 0;
     &nContador = 0;
     j = 0;
     aOrigenS = "";
     aDestinoS = "";
     aRetardo = "";
     aRetardoExt = "";
     nNume = 0;

     aRuta = "";

     &eaUnidadBasico = "";
     &eaDSMAC        = "";
|FIN;

|PROCESO control;
     |ABRE #nomcontr;
     |LEE 000#nomcontr.p;
     |SI FSalida = 0;
          aRetardo = #nomcontr#36;
          nNume = #nomcontr#36 + 5;
          aRetardoExt = nNume;
     |FINSI;
     |CIERRA #nomcontr;
|FPRC;

|PROCESO CargaInicial;
     |DBASE aDirBase;
     aPathLocPDF = "/" + (eaUnidadBasico % 101) + "/ds/spoolpdf/";  || para la plantilla PDF (si, este formato)
     aPathLocFDF = eaUnidadBasico + "\ds\spoolpdf\";  || aqui va la plantilla y el FDF local
     aPathSerPDF = aDirBase + "pdf/";  || aqui van todas las plantillas PDF
     aPathSerFDF = aDirBase + "fich/"; || aqui va el FDF en el servidor
|FPRC;

|PROCESO BuscaMata;          || ME TRAIGO EL mata.exe DEL SERVIDOR A LOCAL
     aIP = "";
     |IP_REMOTO aIP;
     |DBASE aOrigen;
     aOrigen = aDirBase + "pdf/mata.exe";
     aDestino = eaUnidadBasico + "\ds\spoolpdf\mata.exe";
     |QBF aOrigen;
     |QBF aDestino;
     |SI aIP ! "";
         |ENVIA_FICHERO aOrigen, aDestino;
     |SINO;
         |COPIA_FICHERO aOrigen, aDestino;
     |FINSI;
     aMatador = aDestino;
|FPRC;

|PROCESO BuscaWait;          || ME TRAIGO EL mata.exe DEL SERVIDOR A LOCAL
     aIP = "";
     |IP_REMOTO aIP;
     |DBASE aOrigen;
     aOrigen = aDirBase + "pdf/wait.exe";
     ||aDestino = aPathReader + "wait.exe";
     aDestino = aPathLocFDF + "wait.exe";
     |QBF aOrigen;
     |QBF aDestino;
     |SI aIP ! "";
         |ENVIA_FICHERO aOrigen, aDestino;
     |SINO;
         |COPIA_FICHERO aOrigen, aDestino;
     |FINSI;
|FPRC;

|PROCESO BuscaRuta;
     |HAZ CogeMac;

     aIP = "";  |IP_REMOTO aIP;  |QBF aIP;

     |DBASS aRuta;
     |QBF aRuta;
     aRuta = aRuta + "laboral/fich/";
     |PATH_DAT #200 aRuta;

     |ABRE #pdfcontr;
     #pdfcontr#0 = eaDSMAC;
     |LEE 000#pdfcontr.=;
     |SI FSalida ! 0;
         #pdfcontr#0 = aIP;
         |LEE 000#pdfcontr.=;
         |SI FSalida = 0;
             #pdfcontr#0 = eaDSMAC;
             |GRABA 020#pdfcontr.n;
             |LIBERA #pdfcontr;
         |SINO;
             |DDEFECTO #pdfcontr;
         |FINSI;
     |FINSI;
     |CIERRA #pdfcontr;

     aPathReader = #pdfcontr#1;   |QBF aPathReader;
|FPRC;

|PROCESO BuscaCaracter;
     aLongitud = aCadena % 0;
     nLongitud = aLongitud;
     |PARA nPos = 1; |SI nPos [ nLongitud; |HACIENDO nPos = nPos + 1;
          nCaracter = ((nPos * 100) + 1) * nPorDonde;
          aCaracter = aCadena % nCaracter;
          |SI aCaracter = aMiCaracter;
               |SAL;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO imprime_nomina;
     aParametros = " /h /p";         || Impresora por defecto
     || aDestino = "start " + aEjecutable + aParametros + aListaFich;
     aDestino = aEjecutable + aParametros + aListaFich;
     aFicheroBAT = aPathLocFDF + Usuario + ".bat";
     |XABRE "CBW", aFicheroBAT, nCanal;
     |SI FSalida = 0;
          aAlfa = "@echo off" + &13 + &10;
          |XGRABA nCanal, aAlfa;
          aUnidad = aPathReader % 102;
          aAlfa = aUnidad + &13 + &10;
          |XGRABA nCanal, aAlfa;
          aAlfa = "cd " + &34 + aPathReader + &34 + &13 + &10;
          aAlfa = aAlfa - aUnidad;
          |XGRABA nCanal, aAlfa;
          |PARA i = 1; |SI i [ nContador; |HACIENDO i = i + 1;
               |PARA j = 1; |SI j [ nNroCopias; |HACIENDO j = j + 1;
                    aAlfa = i; aAlfa = "000" + aAlfa; aAlfa = aAlfa % -103;
                    aAlfa = "a" + aAlfa + ".fdf";
                    aUsuario = Usuario;
                    aAlfa = " " + &34 + eaUnidadBasico + "\ds\spoolpdf\" + Usuario + "\" + aAlfa + &34;
                    |SI i = 1; |Y j = 1;     || la primera lleva retardo extra
                         aAlfa = "wait -" + aRetardoExt + " " + aEjecutable + aParametros + aAlfa;
                    |SINO;
                         aAlfa = "wait -" + aRetardo + " " + aEjecutable + aParametros + aAlfa;
                    |FINSI;
                    aAlfa = aAlfa + &13 + &10;
                    |XGRABA nCanal, aAlfa;
               |SIGUE;
          |SIGUE;
     |FINSI;
     |XCIERRA nCanal;
|FPRC;

|PROGRAMA;
     |HAZ LeeUnidadBasico;

     |SI enWeb = 0;
          |HAZ BuscaRuta;
          |HAZ control;
          |HAZ CargaInicial;
          |HAZ BuscaMata;

          aPathReader = #pdfcontr#1;
          |QBF aPathReader;

          aCadena = aPathReader;
          aMiCaracter = "\";
          nPorDonde = -1;
          |HAZ BuscaCaracter;
          nPos = nPos -1;
          nPos = nPos + 100;
          nPos = (-1) * nPos;
          aEjecutable = aPathReader % nPos;
          aPathReader = aPathReader - aEjecutable;

          |HAZ BuscaWait;

          |SI nTipoSalida = 0; |Y aProcedencia ! "nomrecib"; || Solo Vista Previa
               aParametros = "";
               nNroCopias = 1;
          |FINSI;
          |SI nTipoSalida = 1;                    || Impresion
               |SI nTipoImpre = 1;
                    aParametros = " /p";            || Muesta cuadro impresion
                    nNroCopias = 1;
               |SINO;
                    aParametros = " /h /p /n";         || Impresora por defecto
                    nNroCopias = nNroCopias;
               |FINSI;
          |FINSI;

          |SI swPDFMasivo = 0;
               aDestino = "start " + aEjecutable + aParametros + aListaFich;
               aFicheroBAT = aPathLocFDF + Usuario + ".bat";
               |XABRE "CBW", aFicheroBAT, nCanal;
               |SI FSalida = 0;
                    aAlfa = "@echo off" + &13 + &10;
                    |XGRABA nCanal, aAlfa;
                    ||aUnidad = aPathReader % 102;
                    aUnidad = aPathLocFDF % 102;
                    aAlfa = aUnidad + &13 + &10;
                    |XGRABA nCanal, aAlfa;
                    ||aAlfa = "cd " + &34 + aPathReader + &34 + &13 + &10;
                    aAlfa = "cd " + &34 + aPathLocFDF + &34 + &13 + &10;
                    aAlfa = aAlfa - aUnidad;
                    |XGRABA nCanal, aAlfa;
                    aAlfa = "start " + aEjecutable + aParametros;
                    |XGRABA nCanal, aAlfa;
                    |PARA i = 1; |SI i [ nNroCopias; |HACIENDO i = i + 1;
                         aAlfa = aListaFich;
                         |XGRABA nCanal, aAlfa;
                    |SIGUE;
                    |SI nTipoSalida ! 0;
                         aAlfa = " " + &34 + "no.pdf" + &34;
                         |XGRABA nCanal, aAlfa;
                    |FINSI;
                    aAlfa = &13 + &10;
                    |XGRABA nCanal, aAlfa;
                    |SI nTipoSalida ! 0;
                         aAlfa = aMatador + &13 + &10;
                         |XGRABA nCanal, aAlfa;
                    |FINSI;
               |FINSI;
               |XCIERRA nCanal;
               |RSYSTEM aFicheroBAT;

               aAlfa = aPathSerFDF + aArchivoFDF;
               |FBORRA aAlfa;
          |SINO;
               |HAZ imprime_nomina;

               |RSYSTEM aFicheroBAT;

               aAlfa1 = nContador;
               aAlfa2 = nNroCopias;
               aAlfa = "    Nro. de nominas impresas: " + aAlfa1 + ".";
               aAlfa = aAlfa + " Nro. de copias: " + aAlfa2 + ".";
               |MENAV aAlfa;
               aAlfa = aPathSerFDF + aArchivoFDF;
               |FBORRA aAlfa;
          |FINSI;

          aListaFich = "";
          |CIERRA #pdfcontr;
     |FINSI;
|FPRO;
