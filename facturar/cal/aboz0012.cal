|FICHEROS;
     aboz0012 #0;
     abom0514 #514;
     abom0518 #518;
     abom0519 #519;
     abom0520 #520;
     abom0012 #12;
     abom0001 #1;
|FIN;

|VARIABLES;
      aDSCorreo   = "";   || Direccion IP de la maquina donde esta el DSCORREO
      aProxy      = "";   || Direccion IP del proxy, si no hay la misma
      aUsu        = "";
      aPwd        = "";
      aNombre     = "";
      aBase       = "";
      aAlfa       = "";
      aAlfax      = "";
      aOrigen     = "";
      aDestino    = "";
      aIPRemoto   = "";
      aFichero    = "";
      aAlfa1      = "";
      aAlfa11     = "";
      aAlfa2      = "";
      aAlfa3      = "";
      aAlfa4      = "";
      aAlfa5      = "";
      aCaracter   = "";
      aCaracterAnt= "";
      aCadena     = "";
      aExp        = "";
      aLong       = "";
      aParametro  = "";
      aMes        = "";
      aFecha      = "";
      aAsunto     = "";
      aSubject    = "";
      aPos        = "";
      aPos1       = "";
      aPos2       = "";

      nPos1       = 0;
      nPos2       = 0;
      nPos        = 0;
      nLinea      = 0;
      nHandle     = 0;
      nMail       = 0;
      nPosi       = 0;
      nNumero     = 0;
      nCaracter   = 0;
      nCarac      = 0;
      nVar        = 0;
      nLong       = 0;
      nLong1      = 0;
      nLong2      = 0;
      fFecha      = @;
      nVentana    = 0;
      nHay519     = 0;
      nHay520     = 0;

     {-1}aVent       = "";
         aVent1      = 0;
         aVent2      = 0;
         aVent3      = 0;
         aVent4      = 0;
         aVent5      = "";

      &efFechaDocu     = @;
      &enCodUbica      = 0;
      &eaRutaDocumento = "";
      &enFSalida       = 0;
      &enCuenta        = 0;
|FIN;

|PROCESO PonFecha;
     aFecha  = #12#3;
     aLong   = aFecha % 0;
     nLong   = aLong;
     nVar    = 1;
     aAlfa1  = "";
     aAlfa2  = "";
     aAlfa3  = "";
     aAlfa4  = "";
     aAlfa5  = "";
     |PARA nCarac = 1;  |SI nCarac [ nLong;  |HACIENDO nCarac = nCarac + 1;
           nPos      = (nCarac * 100) + 1;
           aCaracter = aFecha % nPos;
           |SI aCaracter = "+";  |SAL;  |FINSI;

           |SI aCaracter = ",";   |O aCaracter = " ";
               |SI aCaracterAnt ! ",";  |Y aCaracterAnt ! " ";
                   nVar = nVar + 1;
               |FINSI;
           |FINSI;

           |SI nVar > 5;  nVar = 5;  |FINSI;

||      Date: Sat, 5 Jan 2019 18:40:58 +0100

           |SI aCaracter ! ",";  |Y aCaracter ! " ";
               |SI nVar = 1;  aAlfa1 = aAlfa1 + aCaracter;  |FINSI;
               |SI nVar = 2;  aAlfa2 = aAlfa2 + aCaracter;  |FINSI;
               |SI nVar = 3;  aAlfa3 = aAlfa3 + aCaracter;  |FINSI;
               |SI nVar = 4;  aAlfa4 = aAlfa4 + aCaracter;  |FINSI;
               |SI nVar = 5;  aAlfa5 = aAlfa5 + aCaracter;  |FINSI;
           |FINSI;

           aCaracterAnt = aCaracter;
     |SIGUE;
                          aAlfa11 = "";
     |SI aAlfa1 = "Mon";  aAlfa11 = "Lunes";      |FINSI;
     |SI aAlfa1 = "Tue";  aAlfa11 = "Martes";     |FINSI;
     |SI aAlfa1 = "Wed";  aAlfa11 = "Miercoles";  |FINSI;
     |SI aAlfa1 = "Thu";  aAlfa11 = "Jueves";     |FINSI;
     |SI aAlfa1 = "Fri";  aAlfa11 = "Viernes";    |FINSI;
     |SI aAlfa1 = "Sad";  aAlfa11 = "Sabado";     |FINSI;
     |SI aAlfa1 = "Sat";  aAlfa11 = "Sabado";     |FINSI;
     |SI aAlfa1 = "Sun";  aAlfa11 = "Domingo";    |FINSI;

     |SI aAlfa11 = "";
         aAlfa5  = aAlfa4;
         aAlfa4  = aAlfa3;
         aAlfa3  = aAlfa2;
         aAlfa2  = aAlfa1;
         aAlfa1  = "";
     |FINSI;

     |SI aAlfa3 = "Jan";  aAlfa3 = "Enero";       aMes = "01";    |FINSI;
     |SI aAlfa3 = "Feb";  aAlfa3 = "Febrero";     aMes = "02";    |FINSI;
     |SI aAlfa3 = "Mar";  aAlfa3 = "Marzo";       aMes = "03";    |FINSI;
     |SI aAlfa3 = "Apr";  aAlfa3 = "Abril";       aMes = "04";    |FINSI;
     |SI aAlfa3 = "May";  aAlfa3 = "Mayo";        aMes = "05";    |FINSI;
     |SI aAlfa3 = "Jun";  aAlfa3 = "Junio";       aMes = "06";    |FINSI;
     |SI aAlfa3 = "Jul";  aAlfa3 = "Julio";       aMes = "07";    |FINSI;
     |SI aAlfa3 = "Aug";  aAlfa3 = "Agosto";      aMes = "08";    |FINSI;
     |SI aAlfa3 = "Sep";  aAlfa3 = "Septiembre";  aMes = "09";    |FINSI;
     |SI aAlfa3 = "Oct";  aAlfa3 = "Octubre";     aMes = "10";    |FINSI;
     |SI aAlfa3 = "Nov";  aAlfa3 = "Noviembre";   aMes = "11";    |FINSI;
     |SI aAlfa3 = "Dec";  aAlfa3 = "Diciembre";   aMes = "12";    |FINSI;

     aFecha = (("00" + aAlfa2) % -102) + "." + aMes + "." + aAlfa4;
     #12#6  = aFecha;

     |SI #12#6 < "01.01.0000";  |O #12#6 > "31.12.2999";
         #12#6 = "01.01.0000";
     |FINSI;

     |SI aAlfa11 = "";  |Y #12#6 > "01.01.0000";
         fFecha = #12#6 % 1;
         aAlfa1 = (fFecha % 1109) < " ";
     |FINSI;

     aAlfa5 = aAlfa5 % 108;
     #12#3  = aAlfa11 + ", " + (("00" + aAlfa2) % -102) + " de " + aAlfa3 + " de " + aAlfa4 + " a las " + aAlfa5;
|FPRC;

|PROCESO abom0519;
     nHay519 = 1;
     aAlfa2  = #519#2;  |QBF aAlfa2;
     aAlfa1  = #12#2 - aAlfa2;
     |SI aAlfa1 = #12#2;  |ACABA;  |FINSI;

     aAlfa2  = #519#2;   |QBF aAlfa2;
     aAlfa1  = #12#2;
     aAlfa3  = "";
     aCadena = "";
     |PARA nCaracter = 1;  |SI;  |HACIENDO nCaracter = nCaracter + 1;
           nPosi     = (100 * nCaracter) + 1;
           aCaracter = aAlfa1 % nPosi;
           aCadena   = aCadena + aCaracter;

           aLong     = aCadena % 0;
           nLong1    = aLong;
           aAlfa4    = aCadena - aAlfa2;
           aLong     = aAlfa4 % 0;
           nLong2    = aLong;
           |SI nLong1 ! nLong2;
               |SAL;
           |FINSI;
     |SIGUE;

     aAlfa1 = aAlfa1 - aCadena;
     nCarac = 0;
     |PARA nCaracter = 1;  |SI;  |HACIENDO nCaracter = nCaracter + 1;
           nPosi      = (100 * nCaracter) + 1;
           aCaracter  = aAlfa1 % nPosi;

           |SI aCaracter = " ";  |Y nCarac = 0;
           |SINO;
               aExp = aExp + aCaracter;
               nCarac = nCarac + 1;
           |FINSI;

           |SI nCarac = #519#3;  |SAL;  |FINSI;
           |SI nCaracter > 250;  |SAL;  |FINSI;
     |SIGUE;

     |SI aExp = "";  |ACABA;  |FINSI;

     |ABRE #1;
     |SELECT #7#1;
     #1#38 = aExp;
     |LEE 000#1=;
     |SI FSalida = 0;
         aExp = #1#0;
         |CIERRA #1;
         |ERROR10;
         |ACABA;
     |FINSI;
     |CIERRA #1;

     aExp = "";
|FPRC;

|DEFBUCLE abom0519;
     #519#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, abom0519;
|FIN;

|PROCESO abom0520;
     nHay520 = 1;
     aAlfa2  = #520#2;  |QBF aAlfa2;
     aAlfa1  = #12#2 - aAlfa2;

     |SI aAlfa1 = #12#2;  |ACABA;  |FINSI;

     aAlfa2  = #520#2;   |QBF aAlfa2;
     aAlfa1  = #12#2;
     aAlfa3  = "";
     aCadena = "";
     |PARA nCaracter = 1;  |SI;  |HACIENDO nCaracter = nCaracter + 1;
           nPosi     = (100 * nCaracter) + 1;
           aCaracter = aAlfa1 % nPosi;
           aCadena   = aCadena + aCaracter;

           aLong     = aCadena % 0;
           nLong1    = aLong;
           aAlfa4    = aCadena - aAlfa2;
           aLong     = aAlfa4 % 0;
           nLong2    = aLong;
           |SI nLong1 ! nLong2;
               |SAL;
           |FINSI;
     |SIGUE;

     aAlfa1  = aAlfa1 - aCadena;
     nNumero = 0;
     |PARA nCaracter = 1;  |SI;  |HACIENDO nCaracter = nCaracter + 1;
           nPosi      = (100 * nCaracter) + 1;
           aCaracter  = aAlfa1 % nPosi;
           |SI aCaracter ] "0"; |Y aCaracter [ "9";
               nNumero = 1;
               aExp = aExp + aCaracter;
           |FINSI;

           |SI aCaracter < "0";  |O aCaracter > "9";
               |SI nNumero = 1;  |SAL;  |FINSI;
           |FINSI;

           |SI nCaracter > 250;  |SAL;  |FINSI;
     |SIGUE;

     |SI aExp = "";  |ACABA;  |FINSI;

     |ABRE #1;
     |SELECT #1#1;
     #1#0 = aExp;
     |LEE 040#1=;
     |SI FSalida = 0;
         |CIERRA #1;
         |ERROR10;
         |ACABA;
     |FINSI;
     |CIERRA #1;

     aExp = "";
|FPRC;

|DEFBUCLE abom0520;
     #520#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, abom0520;
|FIN;

|PROCESO BuscaExpediente;
     nHay519 = 0;
     nHay520 = 0;
     aExp    = "";

     |SI #514#14 = "S";  |BUCLE abom0519;  |FINSI;

     |SI aExp ! "";  #12#1 = aExp;  |ACABA;  |FINSI;

     |SI #514#17 = "S";  |BUCLE abom0520;  |FINSI;

     |SI aExp ! "";  #12#1 = aExp;  |ACABA;  |FINSI;

     |SI nHay519 = 1;  |O nHay520 = 1;
         |ACABA;
     |FINSI;

     |SI #514#14 = "S";
         nPosi = (#514#15 * 100) + #514#16;
         aExp  = #12#2 % nPosi;  |QBF aExp;

         |SI aExp ! "";
             |ABRE #1;
             |SELECT #7#1;
             #1#38 = aExp;
             |LEE 040#1=;
             |SI FSalida = 0;
                 aExp = #1#0;
             |SINO;
                 aExp = "";
             |FINSI;
             |CIERRA #1;
         |FINSI;
     |FINSI;

     |SI #514#17 = "S";  |Y aExp = "";
         aAlfa2 = #514#11;  |QBF aAlfa2;
         aAlfa1 = #12#2 - aAlfa2;
         |SI aAlfa1 ! #12#2;
             aAlfa2 = #514#11;   |QBF aAlfa2;
             aAlfa1 = #12#2;
             aAlfa3 = "";
             |PARA nCaracter = 1;  |SI;  |HACIENDO nCaracter = nCaracter + 1;
                   nPosi     = (100 * nCaracter) + 1;
                   aCaracter = aAlfa1 % nPosi;
                   aCadena   = aCadena + aCaracter;

                   aLong     = aCadena % 0;
                   nLong1    = aLong;
                   aAlfa4    = aCadena - aAlfa2;
                   aLong     = aAlfa4 % 0;
                   nLong2    = aLong;
                   |SI nLong1 ! nLong2;
                       |SAL;
                   |FINSI;
             |SIGUE;

             aAlfa1 = aAlfa1 - aCadena;
             |PARA nCaracter = 1;  |SI;  |HACIENDO nCaracter = nCaracter + 1;
                   nPosi      = (100 * nCaracter) + 1;
                   aCaracter  = aAlfa1 % nPosi;
                   |SI aCaracter ] "0"; |Y aCaracter [ "9";
                       nNumero = 1;
                       aExp = aExp + aCaracter;
                   |FINSI;

                   |SI aCaracter < "0";  |O aCaracter > "9";
                       |SI nNumero = 1;  |SAL;  |FINSI;
                   |FINSI;
              |SIGUE;
         |FINSI;

         |ABRE #1;
         |SELECT #1#1;
         #1#0 = aExp;
         |LEE 040#1=;
         |SI FSalida ! 0;  aExp = "";  |FINSI;
         |CIERRA #1;
     |FINSI;

     #12#1 = aExp;
|FPRC;

|PROCESO LeeFichero;
     aLong   = aAsunto % 0;
     nLong   = aLong;
     aAlfa   = "";
     nVar    = 0;
     |PARA nCarac = 1;  |SI nCarac [ nLong;  |HACIENDO nCarac = nCarac + 1;
           nPos      = (nCarac * 100) + 1;
           aCaracter = aAsunto % nPos;

           |SI nVar = 1;
               aAlfa = aAlfa + aCaracter;
           |FINSI;

           |SI aCaracter = "]";  nVar = 1;  |FINSI;
     |SIGUE;

     #12#2 = aAlfa;

     nMail    = 0;
     aSubject = "";
     |PARA;  |SI  FSalida ] 0;  |HACIENDO;
             |XLEE nHandle, 254, aAlfa;
             |SI FSalida < 0;  |SAL;  |FINSI;

             aAlfa1 = aAlfa - "Subject: ";
             |SI aAlfa1 ! aAlfa; |Y aSubject = "";
                 aPos  = "0000" + FSalida;
                 aPos1 = aPos % -302;
                 aPos2 = aPos % -102;
                 nPos1 = aPos1;
                 nPos2 = aPos2;
                 nPos  = ((nPos1 + nPos2) * 100) + 99;

                 aSubject = aAlfa % nPos;
                 aSubject = aSubject - "=?iso-8859-1?Q?";
                 aSubject = aSubject - "?=";
                 |QBF aSubject;
                 |SI aSubject ! "";
                     #12#2 = aSubject;
                 |FINSI;

                 aAlfa1 = #12#2;
                 nMail  = nMail + 1;

                 |HAZ BuscaExpediente;
             |FINSI;

             aAlfa1 = aAlfa - "From: ";
             |SI aAlfa1 ! aAlfa;
                 #12#4 = aAlfa1;
                 nMail = nMail + 1;
             |FINSI;

             aAlfa1 = aAlfa - "Date: ";
             |SI aAlfa1 ! aAlfa;
                 #12#3 = aAlfa1;
                 |HAZ PonFecha;
                 nMail = nMail + 1;
             |FINSI;

             |SI nMail = 3;  |SAL;  |FINSI;
     |SIGUE;
|FPRC;

|PROCESO EnElSitio;
     nLinea = 1;
     |LEE 040#12u;
     |SI FSalida = 0;
         nLinea = #12#0 + 1;
     |FINSI;

     |DDEFECTO #12;
     #12#0    = nLinea;

     aOrigen   = aNombre;                           |QBF aOrigen;
     |XABRE "U", aOrigen, nHandle;
     |SI FSalida ] 0;
         |HAZ LeeFichero;
         |GRABA 020#12n;
         |LIBERA #12;
     |SINO;
         |SI aParametro = "";
             |MENAV "     Error al abrir fichero.";
         |FINSI;
         |ACABA;
     |FINSI;
     |XCIERRA nHandle;

     efFechaDocu = #12#6;
     enCodUbica  = #514#9;
     |HAZ LeeUbicaciones;
     |SI enFSalida ! 0;
         |SI aParametro = "";
             |MENAV "     No tengo acceso a la Ubicacion de las Carpetas de los Correos";
         |FINSI;
         |ACABA;
     |FINSI;

     aFichero  = (("00000000" + nLinea) % -108) + ".eml";
     aOrigen   = aNombre;                           |QBF aOrigen;
     aDestino  = eaRutaDocumento + "/" + aFichero;
     aIPRemoto = "";
     |IP_REMOTO aIPRemoto;   |QBT aIPRemoto;
     |SI aIPRemoto = "";
         |COPIA_FICHERO aOrigen, aDestino;
         |XABRE "EC", aDestino, nHandle;
     |SINO;
         |SI #514#21 ! "C";
             |ENVIA_FICHERO aOrigen, aDestino;
             |XABRE "EC", aDestino, nHandle;
         |SINO;
             |COPIA_FICHERO aOrigen, aDestino;
             |XABRE "E", aDestino, nHandle;
         |FINSI;
     |FINSI;

     |SI FSalida ] 0;
         |FBORRA aOrigen;
     |SINO;
         |SI aParametro = "";
             |MENAV "     Error al Copia el Correo.";
         |FINSI;
     |FINSI;
     |XCIERRA nHandle;
|FPRC;

|PROGRAMA;
     aParametro = PARAMETRO;  |QBF aParametro;

     |ABRE #0;
     |LEE 101#0p;
     |SI FSalida ! 0;
         |DDEFECTO #0;
         |GRABA 020#0b;
     |FINSI;

     |ABRE #514;
     |LEE 040#514p;
     |SI FSalida ! 0;  |DDEFECTO #514; |FINSI;
     |CIERRA #514;

     |SI #514#1 = "N";
         |SI aParametro = "";
             |MENAV "      La Aplicacion no tiene Configurada la Utilidad de Correo";
         |FINSI;
         |ACABA;
     |FINSI;

     efFechaDocu = ~;
     enCodUbica  = #514#9;
     |HAZ LeeUbicaciones;
     |SI enFSalida ! 0;
         |SI aParametro = "";
             |MENAV "     No tengo acceso a la Ubicacion de las Carpetas de los Correos";
         |FINSI;
         |ACABA;
     |FINSI;

     |SI aParametro = "X";
         |VENTANA 0501, 2799, -1, 16, "Recoger correo - Cuentas de correos - Pulse doble click sobre la cuenta por la que quiere enviar";
         nVentana = FSalida;

         aVent1 = -1;
         aVent2 = -1;
         aVent3 = 0;
         aVent4 = nVentana;
         aVent5 = "MODAL";
         |ESPECIFICA "ESTADO_VENTANA", aVent;

         enCuenta = 0;
         |SUB_EJECUTA_NP "abom0518;PIDECUENTAX";

         aVent1 = -1;
         aVent2 = -1;
         aVent3 = 0;
         aVent4 = nVentana;
         aVent5 = "MODELESS";
         |ESPECIFICA "ESTADO_VENTANA", aVent;

         |FINVENTANA nVentana;

         |PULSATECLA;
     |SINO;
         |SI aParametro = "";
             enCuenta = 0;
             |SUB_EJECUTA "abom0518;PIDECUENTA";
         |SINO;
             enCuenta = PARAMETRO;
         |FINSI;
     |FINSI;

     |SI enCuenta = 0;
         |MENAV "0000No se ha seleccionado ninguna cuenta de correo.";
         |ACABA;
     |FINSI;

     |ABRE #518;
     #518#0 = enCuenta;
     |LEE 040#518=;
     |SI FSalida ! 0;
         |SI aParametro = "";
             |MENAV "       No existe la Cuenta de Correo.";
         |FINSI;
         |ACABA;
     |FINSI;
     |CIERRA #518;

     |PINPA #0#0;
     |PINDA #0#0;

     aBase = "";
     |DBASE aBase;  |QBF aBase;
     aBase = aBase + "correos/";  |MKDIR aBase;

     #0#1      = 0;
     aDSCorreo = #518#2;           |QBF aDSCorreo;   || Direccion IP
     aUsu      = #518#6;           |QBF aUsu;        || usuario
     aPwd      = #518#7;           |QBF aPwd;        || password
     aProxy    = #518#3;           |QBF aProxy;      || servidor proxy
     aProxy    = ":" + aProxy;

     |ABRE #12;
     |PARA; |SI; |HACIENDO;
          aNombre   = "";
          |DSCORREO_RECIBE aDSCorreo, aProxy, aUsu, aPwd, aBase, aNombre;
          aAsunto = FSalida;

          aAlfa = aNombre;  |QBT aAlfa;
          |SI aAlfa ! "";
              #0#1 = #0#1 + 1;   |PINTA #0#1;
              |HAZ EnElSitio;
          |SINO;
              |SAL;
          |FINSI;

          |PULSATECLA;
     |SIGUE;
     |CIERRA #12;

     |SI #0#1 = 0;
         |SI aParametro = "";
             |MENAV "      No hay ningun mensaje en el Servidor";
         |FINSI;
     |SINO;
         aAlfa = "     Recibidos " + #0#1 + " Mensaje/s";
         |SI aParametro = "";
             |MENAV aAlfa;
         |FINSI;
     |FINSI;

     #0#1 = 0;
     |GRABA 020#0a;
     |LIBERA #0;
     |CIERRA #0;

     |PULSATECLA;
|FPRO;
