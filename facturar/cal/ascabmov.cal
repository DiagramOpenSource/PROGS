|FICHEROS;
   ascabmov #0;
   asmovtos #1;

   asmancaj #2;
   asmanmov #3;
   ascnsexp #4,MANTE;
   asexpeca #5;
   asexpeli #6;
   asconcep #7;

   asclient;

   :/contagen/def/canempre #330;
   :/contagen/def/cacuenta #304;
   :/contagen/def/cadeflab #306;
|FIN;

|VARIABLES;
   &entrada = 1;
   nModoEntrada = 0;

   aBlancs  = "";
   aAlfa    = "";
   aAlfa1   = "";
   aAlfa2   = "";
   aAlfa3   = "";
   nNume1   = 0;
   nNume2   = 0;

   aPath    = "";
   aPant    = "";

   nCliente  = 0;
   &enAnyo   = 0;
   &enNumExp = 0;
   nLinea    = 0;
   &enSw     = 0;
   Flag      = 0;
   NumLinea  = 0;
   nInkey    = 0;

   aAvisos    = "";

|FIN;

|INCLUYE i_varcof;

||************************************************************************
|CALCULO ProcesaConta; |TIPO 0;

   |SI FSalida = 1; |ERROR; |PTEC 807; |ACABA; |FINSI;

   |ABRE #2;
   #2#0 = #0#0;
   |LEE 000#2=;
   |SI FSalida ! 0;
       |MENAV "    Error. No existe Caja";
       |ERROR;
       |CIERRA #2;
       |ACABA;
   |FINSI;
   |CIERRA #2;

   enSwSinMen = 1;
   enEmpresa  = #2#4;
   enPerConta = #2#5;
   |HAZ LeeEmpConta;
   |SI swerror = 1;
       |MENAV "    Error. No existe Empresa Contable";
       |ERROR;
       |ACABA;
   |FINSI;

   |DBASS aAlfa1; |QBF aAlfa1;
   aAlfa1 = aAlfa1 + "contagen/";
   aAlfa2 = ("00000" + #2#4) % -105;
   aAlfa3 = ("0" + #2#5) % -101;

   aPath  = aAlfa1 + "fich/" + aAlfa2 + aAlfa3 + "/";
   aPant  = aAlfa1 + "pan/";

   |PATH_DAT #304 aPath;
   |PATH_DAT #306 aPath;

   #0#2 = #2#7; |PINTA #0#2;

   |ABRE #306;
   |DDEFECTO #306;
   |LEE 001#306p;
   |CIERRA #306;

   aAlfa1 = #2#18;
   |SI eaDepar = "N"; aAlfa1 = "N"; |FINSI;
   |C_M #1#16, 1, aAlfa1;
|FCAL;

|CALCULO feccab; |TIPO 0;
 |SI #0Campo < fec1; |O #0Campo > fec2;
     |MENAV "    Error. Esta fecha no corresponde al ejercicio ";
     |ERROR;
 |FINSI;
|FCAL;

|CALCULO Tipo3; |TIPO 3;
   |ABRE #2;
   #2#0 = #0#0;
   |LEE 000#2=;
   |SI FSalida = 0;
      #2#7 = #0#2;
      |GRABA 020#2a;
   |FINSI;
   |CIERRA #2;
|FCAL;

||************************************************************************

|CALCULO PintaClient; |TIPO 7;
   |PINTA 2209, " Cliente :                                    Exped.   /       ";
   |C_V #1#10,1,"S"; |PINTA #1#10;
   |C_V #1#11,1,"S"; |PINTA #1#11;
   |C_V #1#12,1,"S"; |PINTA #1#12;

   nModoEntrada = (FEntrada / 100) ! 0;
   |SI nModoEntrada = 1;
       #1#16 = #2#16; |PINTA #1#16;
   |FINSI;
|FCAL;

|CALCULO Actualiza; |TIPO 2;

   Flag = 0 +. 1;
   |SI #1#13 = "S";
       |SI Flag < 0;
          |MENAV "    Esta linea esta contabilizada. No puede ser modificada";
          |ERROR;
       |FINSI;
       |ACABA;
   |FINSI;

   |ABRE #7;
   #7#0 = #1#4;
   #7#1 = #1#5;
   |LEE 000#7=;
   |CIERRA #7;

   |SI #3#5 = "N";
       |SI #3#2 = "I";
           #0#2 = #0#2 +. #1#8;
       |SINO;
           #0#2 = #0#2 -. #1#8;
       |FINSI;
   |FINSI;
   |PINTA #0#2;

   #5#0  = #1#10;
   #5#14 = #1#11;
   |LEE 101#5=;
   |SI FSalida = 0;
       |SI Flag > 0;
           #6#0  = #5#0;
           #6#16 = #5#14;
           #6#2  = 1;
           |LEE 000#6];
           |SI FSalida = 0; |Y #6#0  = #5#0; |Y #6#16 = #5#14;
               |PARA; |SI FSalida = 0; |Y #6#0  = #5#0; |Y #6#16 = #5#14; |HACIENDO;
                      NumLinea = #6#2 + 1;
                      |LEE 000#6s;
               |SIGUE;
           |FINSI;
           |DDEFECTO #6;
           #6#0  = #5#0;
           #6#16 = #5#14;
           #6#2  = NumLinea;
           |GRABA 020#6b;
           #6#1  = #5#1;
           aAlfa = #1#1; |QBF aAlfa; aAlfa = aAlfa % 0104;
           #6#3  = aAlfa;
           #6#4  = #1#4;
           #6#5  = #1#5;
           #6#6  = #7#2;
           #6#7  = #7#16;
           #6#8  = #7#17;
           #6#9  = #7#18;
           #6#10 = #7#3;
           #6#11 = 1;
           #6#12 = #1#8;
           #6#13 = #1#8;
           #6#14 = "U";
           #6#15 = "N";
           #6#17 = #1#1;
           |GRABA 020#6a;
           |LIBERA #6;
           |SI #6#10 = "H";
               #5#6 = #5#6 +. #6#13;
               |SI #6#15 = "N"; #5#9 = #5#9 +. #6#13; |FINSI;
           |FINSI;
           |SI #6#10 = "S";
               #5#7 = #5#7 +. #6#13;
               |SI #6#15 = "N"; #5#10 = #5#10 +. #6#13; |FINSI;
           |FINSI;
           |SI #6#10 = "A";
               #5#8 = #5#8 +. #6#13;
               |SI #6#15 = "N"; #5#11 = #5#11 +. #6#13; |FINSI;
           |FINSI;
           |GRABA 020#5a;
           #1#14 = NumLinea;
      |SINO;
           #6#0  = #1#10;
           #6#16 = #1#11;
           #6#2  = #1#14;
           |LEE 101#6=;
           |SI FSalida = 0;
               |SI #6#10 = "H";
                   #5#6 = #5#6 +. #6#13;
                   |SI #6#15 = "N"; #5#9 = #5#9 +. #6#13; |FINSI;
               |FINSI;
               |SI #6#10 = "S";
                   #5#7 = #5#7 +. #6#13;
                   |SI #6#15 = "N"; #5#10 = #5#10 +. #6#13; |FINSI;
               |FINSI;
               |SI #6#10 = "A";
                   #5#8 = #5#8 +. #6#13;
                   |SI #6#15 = "N"; #5#11 = #5#11 +. #6#13; |FINSI;
               |FINSI;
               |GRABA 020#5a;
               |SI Flag < 0;
                   |BORRA 020#6a;
               |FINSI;
               |LIBERA #6;
           |FINSI;
      |FINSI;
      |LIBERA #5;
   |FINSI;

|FCAL;

|CALCULO BorraMensa; |TIPO 7;
   |C_V #1#10,1,"N";
   |C_V #1#11,1,"N";
   |C_V #1#12,1,"N";
   |PINTA 2209, "                                                                ";
|FCAL;


|CALCULO RecogeTipo; |TIPO 0;

   nModoEntrada = (FEntrada / 100) ! 0;
   |SI nModoEntrada = 1; |O #1#3 ! Contenido;
       #1#15 = #3#1; |PINTA #1#15;
   |FINSI;

   |ABRE #3;
   #3#0 = #1#3;
   |LEE 000#3=;
   |SI FSalida ! 0;
       |MENAV "    Error. No Existe Tipo Movimiento";
       |ERROR;
       |CIERRA #3;
       |ACABA;
   |FINSI;
   |CIERRA #3;

   aAlfa1 = #1#6; |QBF aAlfa1;
   aAlfa2 = #1#7; |QBF aAlfa2;
   |SI #3#2 = "I";
       |C_M #1#6, 1, "N";
       |C_M #1#7, 1, "S";
       |SI aAlfa1 ! ""; |Y aAlfa2 = "";
           #1#7 = #1#6; |PINTA #1#7;
       |FINSI;
       #1#6 = ""; |PINTA #1#6;
   |SINO;
       |C_M #1#6, 1, "S";
       |C_M #1#7, 1, "N";
       |SI aAlfa2 ! ""; |Y aAlfa1 = "";
           #1#6 = #1#7; |PINTA #1#6;
       |FINSI;
       #1#7 = ""; |PINTA #1#7;
   |FINSI;

   |SI nModoEntrada ! 1; |ACABA; |FINSI;

   aAlfa1 = #3#3; |QBF aAlfa1;
   aAlfa1 = aAlfa1 - "*";
   |SI #3#2 = "I";
       #1#7 = aAlfa1; |PINTA #1#7;
   |SINO;
       #1#6 = aAlfa1; |PINTA #1#6;
   |FINSI;
|FCAL;

|RUTINA nInfCta;
 |SI FSalida = "POSICION";
     #304#0 = eaCuenta;
 |SINO;
     #304#0 = "";
 |FINSI;
 #304#3 = "S";
|FRUT;

|RUTINA nSupCta;
 #304#0 = "zzzzzzzzzzz";
 #304#3 = "S";
|FRUT;

|PROCESO LaCons;
  |ABRE #304;
  |CONSULTA_F_CLAVE #1#304, nInfCta, nSupCta;
  #1Campo = #304#0; |PINTA #1Campo;
  |CIERRA #304;
|FPRC;

|CALCULO Ctafin; |TIPO 7;
   |C_M #1Campo,0,aAlfa1;
   |SI aAlfa1 = "N"; |ACABA; |FINSI;

   |PTEC 816;
|FCAL;

|CALCULO MiraCtaMov; |TIPO 0;
   nInkey = FSalida;
   |SI nInkey = 2; |ACABA; |FINSI;

   |C_M #1Campo,0,aAlfa1;
   |SI aAlfa1 = "N"; |ACABA; |FINSI;

   eaCuenta = #1Campo; |QBF eaCuenta;
   |SI eaCuenta = ""; |Y nInkey ! 10;
       |MENAV "    Introduzca una Cuenta";
       |ERROR;
       |ACABA;
   |FINSI;

   |SI nInkey = 10;
       |HAZ LaCons;
   |FINSI;

   emCta = 0;        || Dar mensajes de error
   eaCuenta = #1Campo;
   |HAZ LeeCuenta;
   |SI eswCta = 1;
        |ERROR;
        |CONFI "    SError.! No existe Cuenta. Desea darla de alta ";
        |SI FSalida = 0;
            |SUB_EJECUTA_NP "aszc0001";
            #1Campo = eaCuenta;
            |PINTA #1Campo;
        |FINSI;
   |SINO;
        |SI eswCta = 2;
            |MENAV "0000Esta Cuenta NO tiene Pase Directo.";
            |ERROR;
        |FINSI;
   |FINSI;
|FCAL;

|CALCULO CtaMov; |TIPO 6;
     |C_M #1Campo,0,aAlfa1;
     |SI aAlfa1 = "N"; |ACABA; |FINSI;

     nInkey = FSalida;
     |SI nInkey = 10; |ACABA; |FINSI;

     emCta = 1;        || Dar mensajes de error
     eaCuenta = #1Campo;
     salidas  = FSalida;
     |HAZ FiltraPuntos;
     #1Campo = eaCuenta;
     |PINTA #1Campo;
     |HAZ SacaNivel;
|FCAL;


|RUTINA inf;
   #4#1  = nCliente;
   #4#0  = 00;
   #4#14 = 000001;
|FRUT;

|RUTINA sup;
   #4#1  = nCliente;
   #4#0  = 99;
   #4#14 = 999999;
|FRUT;

|CALCULO SelecExped7; |TIPO 7;
 |SI #3#4 = "N";
     #1#9 = 0; |PINTA #1#9;
     |PTEC 802;
 |FINSI;
|FCAL;

|CALCULO SelecExped; |TIPO 6;
   |SI #3#4 = "S";

       |ABRE #asclient;
       #asclient#0 = #1#9;
       |LEE 041#asclient.=;
       |SI FSalida ! 0;
           |MENAV "    Error!. No existe Cliente";
           |ERROR;
           |CIERRA #asclient;
           |ACABA;
       |FINSI;
       |CIERRA #asclient;

       |PUSHV 0501 2480;
       nCliente = #1#9;
       |ABRE #4;
       enSw = -1;
       |ENTLINEAL #3#4, -3, 4, 19, 1, inf, sup;
       |SI enSw = 0;
           #1#10 = enAnyo;
           #1#11 = enNumExp;
           |CIERRA #4;
       |SINO;
           |CIERRA #4;
           |ERROR;
       |FINSI;
       |POPV;
   |FINSI;
|FCAL;


|CALCULO AntesImporte; |TIPO 7;
  |PUSHV 08101071;
  aAvisos = #3#24; |QBF aAvisos;
  |SI aAvisos ! "";
      |COLOR 4,0;
      |ATRI 0;

      |CUADRO 0810 10 71;

      |COLOR 14,0;
      |ATRI 0;

      aAlfa1 = aAvisos % 0;  nNume1 = aAlfa1;
      nNume2 = ((60 - nNume1) / 2) ! 0;     nNume1 = 60 - nNume1 - nNume2;
      aAvisos = (" " * nNume1) + aAvisos + (" " * nNume2);        || lo centra
      |PINTA 0911, aAvisos;
      |ATRI 0;
      |AVISO;
  |FINSI;
|FCAL;


|CALCULO ENoCero; |TIPO 0;
  |POPV;
  nInkey = FSalida;
  |SI nInkey = 2; |ACABA; |FINSI;

  |SI #1Campo = 0;
      |MENAV "    Atencion. Importe igual a Cero";
      |ERROR;
  |FINSI;
|FCAL;

|CALCULO ADepar; |TIPO 7;
   |C_M #1Campo, 0, aAlfa1;
   |SI aAlfa1 = "S"
      |SI Haybasica = 1;
          |MENSA "    <F2> Consultar Actividades";
      |SINO;
          |MENSA "    <F2> Consultar Departamentos";
      |FINSI;
   |FINSI;
|FCAL;

|CALCULO TDepar; |TIPO 0;
   |SI eaDepar = "N"; |ACABA; |FINSI;

   |C_M #1Campo,0,aAlfa1;
   |SI aAlfa1 = "N"; |ACABA; |FINSI;

   eOpDep = 0; || 1 = Consulta pantalla
   |SI FSalida = 10; eOpDep = 1;  |FINSI;
   enActividad = #1#16;
   eaCodDep    = #1#16;
   |HAZ ActFactu;
   |SI eswLect = 1;
       |MENAV "    Error.! No Existe Actividad / Departamento";
       |ERROR;
   |SINO;
       aAlfa1 = enActividad;
       |SI Haybasica = 1;
           aAlfa1 = ("00" + aAlfa1) % A02;
       |FINSI;
       #1#16 = eaCodDep;    |PINTA #1#16;
       |HAZ pDeparFa;
       |SI efActFin > "01.01.1900";
           |CONFI "    NAntencion!!!. Actividad con Fecha de BAJA, ¨ Desea introducir Facturas ? ";
           |SI FSalida ! 0;
               |ERROR;
               |ACABA;
           |FINSI;
       |FINSI;
       |SI eaEsCom = "S";
           |CONFI "    NAntencion!!!. Actividad Comunera, ¨ Desea introducir Facturas ? ";
           |SI FSalida ! 0;
               |ERROR;
               |ACABA;
           |FINSI;
       |FINSI;
   |FINSI;
|FCAL;

|CALCULO ElTipo13; |TIPO 13;
   aBlancs = " " * 78;
   |PINTA 2402, aBlancs;
   nModoEntrada = (FEntrada / 100) ! 0; |SI nModoEntrada < 2; |ACABA; |FINSI;
   |SI eaDepar = "S";
       eOpDep = 0;
       enActividad = #1#16;
       eaCodDep    = #1#16;
       |HAZ ActFactu;
       |HAZ pDeparFa;
   |FINSI;
|FCAL;

|CALCULO MBienMov; |TIPO 3;
   aAlfa1 = #1#6; |QBF aAlfa1;
   aAlfa2 = #1#7; |QBF aAlfa2;
   |SI aAlfa1 = "";  |Y aAlfa2 = "";    ||al menos una cuenta cargada;
       |MENAV "    ATENCION!!! Movimiento sin cuentas ...";
       |ERROR;
       |ACABA;
   |FINSI;
   |SI aAlfa1 ! ""; |Y aAlfa2 ! "";
       |MENAV "    ATENCION!!! Movimiento con DOS cuentas ";
       |ERROR;
       |ACABA;
   |FINSI;
   |SI aAlfa1 ! ""; eaCuenta = aAlfa1; |SINO eaCuenta = aAlfa2; |FINSI;
   |SI eaCuenta ! "";
       emCta = 0;
       |HAZ LeeCuenta;
       |SI eswCta = 2; eswCta = 1; |FINSI;
   |FINSI;
   |SI eswCta = 1;
       |MENAV "    Error. Introduzca una Cuenta Contable";
       |ERROR;
       |ACABA;
   |FINSI;

   |SI #1#8 = 0;                       || importe distinto de cero
       |MENAV "    ATENCION!!! Importe cero ...";
       |ERROR;
   |FINSI;
|FCAL;

|| *************************************************************************
|| *************************************************************************
|| *************************************************************************

|PROGRAMA;

   |CLS;
   |CABEZA "Entrada movimientos de caja";

   |HAZ DirAplSt;

   |PINPA #0#0;
   |PINDA #0#0;

   |ABRE #5;
   |ABRE #6;

   |PARA; |SI; |HACIENDO;
          |ENDATOS #1#0;
          |SI FSalida ! 0;
              |SAL;
          |FINSI;
   |SIGUE;

   |CIERRA #6;
   |CIERRA #5;
|FPRO;
