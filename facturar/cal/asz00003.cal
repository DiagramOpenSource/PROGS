|| ... enSwRel = 1  seleccionados
|| ... enSwRel = -1   seleccion vacia
|| ... enSwRel = 0   no vale seleccion No hay basica


|FICHEROS;
   asz00003 #0;
   asw00027 #27;

   :/basica/def/agitab04 #104;
   :/basica/def/agicen14 #114;
   :/basica/def/agifigen #118;
   :/basica/def/agim0019 #119;

   :/contagen/def/caselemz #911;
|FIN;

|VARIABLES;
   nCampo      = 0;
   nDEmpresa   = 0;
   nHEmpresa   = 0;
   &nGTempo     = 0;
   nSPin1      = 0;

   aAlfa1 = "";
   aAlfa2 = "";
   aAlfa3 = "";
   nPara1 = 0;
   nNume1 = 0;
   nNume2 = 0;
   nNume3 = 0;
   nNume4 = 0;

   aEstadoEmp = "";
   enEmpresa   = 0;
   nSwSelEmp = 1;

   &eaFicSel = "";
   &enSwRel  = 0;
   aPath    = "";
   aBase    = "";
   aPAcceso = "";
   aPGenral = "";
   aPContag = "";

   aLosDefs  = "";
   aElProgr  = "";
   nCanal    = 0;
   Haybasica = 0;

   nCampoCliente  = 0;
   nInkey         = 0;
   &eaCodPase     = "";
|FIN;

|| //////////////////// Calculos de Pantalla \\\\\\\\\\\\\\\\\\\\\\\\\\
|CALCULO NoPregunta; |TIPO 12;
|FCAL;

|CALCULO Parrilla; |TIPO 0;
     |SI #0#0 = 0;
         #0#0 = 0;  |PINTA #0#0;
         #0#1 = 0;  |PINTA #0#1;  |C_M #0#1, 1, "N";
         |PARA nCampo = 2;  |SI nCampo [ 21;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |SINO;
         |PARA nCampo = 2;  |SI nCampo [ 21;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "N";
               #0nCampo = 0;  |PINTA #0nCampo;
         |SIGUE;
     |FINSI;
|FCAL;

|CALCULO NoMasEmpresas; |TIPO 0;
     |SI #0Campo = 0;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 21;  |HACIENDO nCampo = nCampo + 1;
               #0nCampo = 0;  |C_M #0nCampo, 1, "N";
         |SIGUE;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 21;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |FINSI;
|FCAL;

|CALCULO SelTipo0; |TIPO 0;
 |SI Haybasica = 0; |ACABA; |FINSI;

 |SI #0#35 = "N";
     #0#23 = 0;   |PINTA #0#23;  |C_M #0#23, 1, "N";
     #0#24 = 99;  |PINTA #0#24;  |C_M #0#24, 1, "N";
     |PARA nCampo = 25;  |SI nCampo [ 34;  |HACIENDO nCampo = nCampo + 1;
           |C_M #0nCampo, 1, "S";
     |SIGUE;
 |SINO;
     |C_M #0#23, 1, "S";
     |C_M #0#24, 1, "S";
     |PARA nCampo = 25;  |SI nCampo [ 34;  |HACIENDO nCampo = nCampo + 1;
           |C_M #0nCampo, 1, "N";
           #0nCampo = 0;  |PINTA #0nCampo;
     |SIGUE;
 |FINSI;
|FCAL;

|CALCULO SelTipo1;
 |SI #0Campo = 0;
     |PARA nCampo = Campo + 1;  |SI nCampo [ 34;  |HACIENDO nCampo = nCampo + 1;
           #0Campo = 0;  |C_M #0nCampo, 1, "N";
     |SIGUE;
 |SINO;
     |PARA nCampo = Campo + 1;  |SI nCampo [ 34;  |HACIENDO nCampo = nCampo + 1;
           |C_M #0nCampo, 1, "S";
     |SIGUE;
 |FINSI;
|FCAL;

|| .... Planilla
|PROCESO nInf911;
 #911#0 = 1;
|FPRC;

|PROCESO nSup911;
 #911#0 = 99;
|FPRC;

|CALCULO LeePlanilla; |TIPO 6;
 nInkey = FSalida;

 |SI nInkey = 10;
     |ABRE #911;
     #911#0 = #0#36; |SI #911#0 = 0; #911#0 = 1; |FINSI;
     |CONSULTA_F_CLAVE #1#911,nInf911,nSup911;
     #0#36 = #911#0;
 |FINSI;

 |SI #0#36 = 0; |ACABA; |FINSI;
 |ABRE #911;
 #911#0 = #0#36;
 |LEE 001#911=;
 |SI FSalida ! 0;
     |MENAV "    No existe Planilla de Seleccion";
     |ERROR;
 |FINSI;
 |CIERRA #911;
|FCAL;

|CALCULO LaPlanilla; |TIPO 0;
  |SI #0#36 ! 0;
      |HAZ LPlan;
      |PINDA #0#0;
  |FINSI;
|FCAL;

|PROCESO LPlan;
      |SI #911#2 = "S";
          #0#0 = #911#3;
          #0#1 = #911#4;
          |PDEFECTO #0,2,22;
      |SINO;
          #0#0 = 0;
          #0#1 = 0;
          |PARA nPara1 = 2; |SI nPara1 [ 21; |HACIENDO nPara1 = nPara1 + 1;
                nNume1 = nPara1 + 3;
                #0nPara1 = #911nNume1;
          |SIGUE;
      |FINSI;
      |SI #911#46 = "I"; |O #911#46 = "T";
          #0#22 = "S";
      |SINO;
          #0#22 = "N";
      |FINSI;

      #0#35 = #911#47;
      #0#23 = #911#48;
      #0#24 = #911#49;
      |PARA nPara1 = 25; |SI nPara1 [ 34; |HACIENDO nPara1 = nPara1 + 1;
            nNume1 = nPara1 + 25;
            #0nPara1 = #911nNume1;
      |SIGUE;

|FPRC;


|| ::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
|PROCESO LEmpresa;
 enEmpresa  = #agifigen#0;

 aEstadoEmp = "S";
 |SI #agifigen#94 > "01.01.0000";
     aEstadoEmp = "N";
     |SI #0#22 = "N"; |ACABA; |FINSI;
 |FINSI;

|| ... Seleccion de Tipo de Empresa
  nSPin1 = #agifigen#87;
  nSwSelEmp = 1;
  |SI #0#35 = "S";
      |SI nSPin1 ] #0#23; |Y nSPin1 [ #0#24;
          nSwSelEmp = 0;
      |FINSI;
  |SINO;
      |PARA nCampo = 25;  |SI nCampo [ 34;  |HACIENDO nCampo = nCampo + 1;
            |SI nSPin1 = #0nCampo; |Y nSPin1 ! 0;
                nSwSelEmp = 0;
                |SAL;
            |FINSI;
      |SIGUE;
  |FINSI;
|| ------------------------------------------------------------------------

  |SI nSwSelEmp = 0;
      nGTempo = nGTempo + 1;
      |DDEFECTO #27;

      #27#0 = nGTempo;
      #27#1 = #agifigen#0;
      #27#2 = #agifigen#1;
      #27#3 = #agifigen#87;
      #27#4 = aEstadoEmp;
      |GRABA 020#27n;
 |FINSI;

|FPRC;

|DEFBUCLE agifigen;
 #agifigen#1;
 ;
 nDEmpresa;
 nHEmpresa;
 e;
 NULL, LEmpresa;
|FIN;

|| ***********************************************************************
|PROGRAMA;

   enSwRel = 1;
   |HAZ LaRelacion;
   |SI enSwRel = 0; |ACABA; |FINSI;


   |ABRE #0;
   |DDEFECTO #0;
   #0#37 = eaCodPase;
   |LEE 041#0=;
   |SI FSalida ! 0;
       |DDEFECTO #0
       #0#37 = eaCodPase;
       |GRABA 020#0c;
   |FINSI;

   |PINPA #0#0;
   |PINDA #0#0;

   eaFicSel = ("27w" + Usuario) % 108;
   |NOME_DAT #27 eaFicSel;
   |ABRE #27; |CIERRA #27; |DELINDEX #27;

   |ENDATOS #1#0;
   |SI FSalida ! 0;
       enSwRel = -1;
       |LIBERA #0;
       |CIERRA #0;
       |ACABA;
   |FINSI;
   |GRABA 020#0a;
   |LIBERA #0;
   |CIERRA #0;

   |ABRE #27;
   nGTempo = 0;   ||Busco Empresas Contables
   |SI #0#0 ! 0;
        nDEmpresa = #0#0;
        nHEmpresa = #0#1;
        |BUCLE agifigen;
   |SINO;
        |PARA nCampo = 2;  |SI nCampo [ 21;  |HACIENDO nCampo = nCampo + 1;
              nDEmpresa = #0nCampo;
              nHEmpresa = #0nCampo;
              |BUCLE agifigen;
        |SIGUE;
    |FINSI;
    |CIERRA #27;

    |SI nGTempo = 0;    || No existe ninguna empresa
        |MENAV "    Error. No existe ninguna Empresa dentro de la Seleccion ";
        |DELINDEX #27;
        enSwRel = -1;
    |FINSI;

|FPRO;
|| ************************************************************************

|PROCESO LaRelacion;
     |HAZ DirAplicSt;
     |SI Haybasica = 1;         || Existe Acceso;
         || ... Activado Basica
         |MODO_RELACION #0, 0, "agitab04", "X", "X";
         |MODO_RELACION #0, 1, "agitab04", "N", "N";
         enSwRel = 1;
     |SINO;
         || No existe Basica
         |MODO_RELACION #0, 1, "agitab04", "N", "N";
         |MODO_RELACION #0, 1, "agitab04", "X", "X";
         enSwRel = 0;
    |FINSI;

    aPath = aPContag + "fich/000000/";
    |PATH_DAT #911 aPath;
|FPRC;

|PROCESO DirAplicSt; ||Directorios Aplicaciones Standar

  |DBASS aBase; |QBT aBase;  || Directorio Base de las Aplaciones


  aPAcceso = aBase + "integral/"; || Directorio Acceso
  aPGenral = aBase + "basica/";   ||            Ficha Clientes General
  aPContag = aBase + "contagen/"; ||            Contabilidad General

  aLosDefs = aPGenral + "def/";
  aElProgr = aLosDefs + "agifigen.mas";
  |XABRE "E", aElProgr, nCanal;
  |SI FSalida ] 0;
      Haybasica = 1;
  |SINO;
      Haybasica = 0;
  |FINSI;
  |XCIERRA nCanal;
|FPRC;


|| ... Proceso Consulta Cliente
|PROCESO MensaCliente; |TIPO 7;
     |MENSA "      <F2> Consulta Clientes ";
|FPRC;

|PROCESO ConsuCliente; |TIPO 0;
     |ATRI R; |BLIN 04; |ATRI 0;
     |SI FSalida ! 10; |ACABA; |FINSI;

     |PUSHV 0501 2380;
     nCampoCliente  = Campo;

     |ABRE #agifigen;
     #agifigen#0 = #0nCampoCliente;
     |LEE 040#agifigen.=;

     |CONSULTA_CLAVE #2#agifigen;
     |CIERRA #agifigen;
     #0nCampoCliente = #agifigen#0;

     |POPV;
     |PINTA #0nCampoCliente;
|FPRC;
