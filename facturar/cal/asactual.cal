|FICHEROS;
    ascabmin #1;
    aslinmin #2;
    ascontro #4;
    ascontrt #450;
    asclient #5;
    asconcep #6;
    ashisfac #7;
    ashiscon #8;
    ashisrec #10;
    asactual #11;
    ashismic #90;
    ashismil #91;
    ashismit #92;

    abom0010 #100;
    ascuolin #111;
    aslinmil #107;
    abom0005 #105;
    abom0008 #108;
    abom0001 #101;
    abom0011 #211;
    abom0514 #514;

    asm00013 #500;

    &bancosp@ #706;
    &fopagos@ #712;

    asw00050;
|FIN;

|VARIABLES;

    nLinea = 0;
    aFecha = "";
    aAny = "";
    aMes = "";
    nMes = 0;
    nAny = 0;
    &eaFichero = "";
    &enDesdeActual = 0;

    &enModVisados = 0;

    aParametro = "";
    tdto    = 0;
    timpo   = 0;
    FEstado = 0;
    ivabo   = 0;
    acum    = 0;
    hono    = 0;
    supli   = 0;
    tacum   = 0;
    thono   = 0;
    tsupli  = 0;
    tfac    = 0;
    tpagar  = 0;
    tret    = 0;
    timp    = 0;
    ret     = 0;
    retenci = 0;
    fecli   = @;
    serie   = "  ";
    fechv   = @;
    em      = 0;
    j       = 0;
    xxx     = 0;
    yyy     = 0;
    mes     = "";
    mes_num = 0;
    campo   = 0;

    nVariable = 0;
    aAlfa     = "";
    nFactu    = 0;
    nDCliente = 0;
    nHCliente = 0;

    &EURODB   = 0;

    &enCodBanco  = 0;
    &enCodPago   = 0;
    &enSwDeDonde = 0;
    &enErrorFac  = 0;
    &enTipoFac   = 0;
    &enCliente   = 0;
    &eaParaConta = "";
|FIN;

|PROCESO concepto;
    mes = #2#15 % 0402;
    mes_num = mes;

    #6#0 = #2#4;
    #6#1 = #2#5;
    |LIBERA #6;
    |LEE 110#6=;
    |SI FSalida = 0;
        #6#31 = #6#31 + #2#13;
        #6#6  = #6#6  + #2#13;
        campo = 18 + mes_num; #6campo = #6campo +  #2#13;
        |GRABA 121#6a;
    |SINO;
        |PINTA 0410, "    CONCEPTO ";
        |PINTA 0423, #6#0;
        |PINTA 0427, #6#1;
        |PINTA 0429, " INEXISTENTE ";
        |AVISO;
        |PAUSA;
        |BLIN 4;
    |FINSI;
    |LIBERA #6;
|FPRC;

|PROCESO histocon;
    #8#0  = #1#27;
    #8#1  = #1#28;
    #8#2  = #2#2;
    |LIBERA #8;
    |LEE 110#8=;
    FEstado = FSalida;

    |SI FSalida = 0;
        |MENAV "     Ya existia la Factura en el Historico de Conceptos";
    |FINSI;

    #8#0  = #1#27;
    #8#1  = #1#28;
    #8#2  = #2#2;
    #8#3  = #2#15;
    #8#4  = #2#1;
    #8#5  = #2#4;
    #8#6  = #2#5;
    #8#7  = #2#7;
    #8#8  = #2#10;
    #8#9  = #2#13;
    #8#10 = #2#8;
    #8#11 = #2#9;
    |SI FEstado ! 0; |GRABA 020#8n; |SINO; |GRABA 020#8a; |FINSI;
    |LIBERA #8;
|FPRC;

|PROCESO graba_hislin;
    #91#0  = #1#27;
    #91#1  = #1#28;
    #91#2  = #2#2;
    #91#3  = #2#15;
    #91#4  = #2#4;
    #91#5  = #2#5;
    #91#6  = #2#6;
    #91#7  = #2#7;
    #91#8  = #2#8;
    #91#9  = #2#9;
    #91#10 = #2#10;
    #91#11 = #2#11;
    #91#12 = #2#12;
    #91#13 = #2#13;
    #91#14 = #2#14;
    #91#15 = #2#17;
    #91#30 = #2#18;
    |GRABA 040#91n;
|FPRC;

|PROCESO ActuaSegui;
     #105#20 = "S";
     |SI #105#24 ! 0;
         #105#26 = #100#18 % #100#26;
         #105#25 = #100#18 - #105#26;
     |FINSI;
     |GRABA 020#105a;
     |LIBERA #105;
|FPRC;

|DEFBUCLE abom0005;
     #105#1;
     23, 20;
     #100#4, 000, #100#0, "N";
     #100#4, 999, #100#0, "N";
     be;
     NULL, ActuaSegui;
|FIN;

|PROCESO Recalcula;
     #6#0 = #105#5;
     #6#1 = #105#6;
     |LEE 040#6=;
     |SI FSalida ! 0;  |DDEFECTO #6;  |FINSI;

     |SI #6#3 = "H";  |O #6#3 = "S";
         #101#33 = #101#33 + #105#18;
         |SI #105#19 = "S";
             #101#31 = #101#31 + #105#24;
         |FINSI;
         |SI #105#20 = "S";
             #101#10 = #101#10 + #105#25;
             #101#36 = #101#36 + #105#26;
         |FINSI;

         #101#11 = #101#31 - (#101#10 + #101#36);
         #101#32 = #101#10 - #101#33;
     |FINSI;

     |SI #6#3 = "A";
         |SI #105#20 ! "S";
             #101#37 = #101#37 + #105#18;
         |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE abom0005R;
     #105#1;
     ;
     #100#4, 000;
     #100#4, 999;
     ;
     NULL, Recalcula;
|FIN;

|PROCESO MiraProducto;
     |SI #100#4 ! 0;
         |SI #100#5 ! 99999;
             |ABRE #105;
             #105#0 = #100#4;
             #105#1 = #100#5;
             |LEE 101#105=;
             |SI FSalida = 0;
                 #105#20 = "S";
                 #105#26 = #100#18 % #100#26;
                 #105#25 = #100#18 - #105#26;
                 |GRABA 020#105a;
                 |LIBERA #105;
             |FINSI;
             |CIERRA #105;
         |SINO;
             |BUCLE abom0005;
         |FINSI;

         |ABRE #101;
         #101#0 = #100#4;
         |LEE 101#101=;
         |SI FSalida = 0;
             #101#10 = 0;
             #101#31 = 0;
             #101#32 = 0;
             #101#33 = 0;
             #101#36 = 0;
             #101#37 = 0;

             |BUCLE abom0005R;

             |ABRE #514;
             |LEE 000#514p;
             |SI FSalida ! 0;  |DDEFECTO #514;  |FINSI;
             |CIERRA #514;

             |SI #514#19 = "S";
                 #101#33 = #101#33 - #101#37;
                 #101#32 = #101#10 - #101#33;
             |FINSI;

             |GRABA 020#101a;
         |FINSI;
         |LIBERA #101;
         |CIERRA #101;
     |SINO;
         |ABRE #108;
         #108#0 = #100#1;
         #108#1 = #100#5;
         |LEE 101#108=;
         |SI FSalida = 0;
             #108#20 = "S";
             |GRABA 020#108a;
             |LIBERA #108;
         |FINSI;
         |CIERRA #108;
     |FINSI;

     |BORRA 020#100a;
     |LIBERA #100;

     |ABRE #211;
     |PARA nVariable = 1;  |SI nVariable [ 100;  |HACIENDO nVariable = nVariable + 1;
           #211#0 = #100#0;
           #211#1 = nVariable;
           |LIBERA #211;
           |LEE 101#211=;
           |SI FSalida = 0;
               |BORRA 020#211a;
               |LIBERA #211;
           |FINSI;
     |SIGUE;
     |CIERRA #211;
|FPRC;

|DEFBUCLE abom0010;
     #100#3;
     ;
     #2#1, #2#4, #2#5, "S";
     #2#1, #2#4, #2#5, "S";
     be;
     NULL, MiraProducto;
|FIN;

|PROCESO ActuaTrabajos;
     |ABRE #100;
     #100#0 = #2#16;
     |LEE 101#100=;
     |SI FSalida = 0;
         |HAZ MiraProducto;
         |CIERRA #100;
         |ACABA;
     |FINSI;
     |CIERRA #100;

     |BUCLE abom0010;
|FPRC;

|PROCESO BorraSubLineas;
     |ABRE #111;
     |SELECT #2#111;
     |SI #2#16 ! -1;
         |HAZ ActuaTrabajos;
     |FINSI;
     |CIERRA #111;

     |ABRE #92;
     |ABRE #107;
     |PARA nVariable = 1;  |SI nVariable [ 100;  |HACIENDO nVariable = nVariable + 1;
           #107#0 = #2#0;
           #107#1 = #2#1;
           #107#2 = #2#2;
           #107#3 = nVariable;
           |LEE 101#107=;
           |SI FSalida = 0;
               #92#0 = #91#0;
               #92#1 = #91#1;
               #92#2 = #91#2;
               #92#3 = #107#3;
               #92#4 = #107#4;
               |GRABA 020#92n;
               |LIBERA #92;

               |BORRA 020#107a;
               |LIBERA #107;
           |SINO;
               |SAL;
           |FINSI;
     |SIGUE;
     |CIERRA #107;
     |CIERRA #92;
|FPRC;

|PROCESO leelinea;
    hono  = 0;  supli = 0;  acum  = 0;

    |SI #2#10 = "H"; hono  = #2#13; supli = 0; acum  = 0; |FINSI;
    |SI #2#10 = "S"; supli = #2#13; hono  = 0; acum  = 0; |FINSI;
    |SI #2#10 = "A"; acum  = #2#13; hono  = 0; supli = 0; |FINSI;

    thono  = (thono  + hono)  ! EURODB;
    tsupli = (tsupli + supli) ! EURODB;
    tacum  = (tacum  + acum)  ! EURODB;

    |HAZ concepto;
    |HAZ histocon;
    |HAZ graba_hislin;
    |HAZ BorraSubLineas;
|FPRC;

|PROCESO bancos;
    mes = #1#29 % 0402;
    mes_num = mes;

    enCodBanco = #1#11;
    |HAZ LeeBancoPropio;

    |SI enSwDeDonde = 0;
        |ABRE #706;
        #706#0 = #1#11;
        |LEE 101#706=;
        |SI FSalida = 0;
            #706#20 = #706#20 + tpagar;
            #706#22 = #706#22 + tpagar;
            campo = 7 + mes_num;
            #706campo = #706campo + tpagar;
            |GRABA 020#706a;
        |SINO;
            |PINTA 0410, "    BANCO  ";
            |PINTA 0423, #706#0;
            |PINTA 0426, " INEXISTENTE ";
            |AVISO;
            |PAUSA;
            |BLIN 4;
        |FINSI;
        |LIBERA #706;
        |CIERRA #706;
    |FINSI;
|FPRC;

|PROCESO cliente;
    mes = #1#29 % 0402;
    mes_num = mes;

    |ABRE #5;
    #5#0 = #1#1;
    |LIBERA #5;
    |LEE 110#5=;
    |SI FSalida = 0;
        #5#49 = #5#49 + thono;
        #5#50 = #5#50 + tsupli;
        #5#18 = #5#18 + thono;
        #5#19 = #5#19 + tsupli;

        campo = 24 + mes_num; #5campo = #5campo +  thono;
        campo = 36 + mes_num; #5campo = #5campo +  tsupli;

        #5#24 = tpagar;
        |GRABA 020#5a;
    |SINO;
        |PINTA 0410, "    CLIENTE  ";
        |PINTA 0423, #5#0;
        |PINTA 0429, " INEXISTENTE ";
        |AVISO;
        |PAUSA;
        |BLIN 4;
    |FINSI;
    |LIBERA #5;
    |CIERRA #5;
|FPRC;

|PROCESO histofac;
    |ABRE #7;
    #7#0 = #1#27;
    #7#1 = #1#28;
    |LIBERA #7;
    |LEE 110#7=;
    FEstado = FSalida;

    |SI FSalida = 0;
        |MENAV "     Ya existia la Factura en el Historico de Facturas";
    |FINSI;

    #7#2  = #1#29;
    #7#3  = #1#1;
    #7#4  = #11#1;
    #7#5  = thono;
    #7#6  = tsupli;
    #7#7  = ivabo;
    #7#8  = timpo % ivabo;
    #7#9  = ret;
    #7#10 = tret;
    #7#11 = tfac;
    #7#12 = "N";
    #7#13 = "N";
    #7#14 = tacum;
    #7#15 = #1#20;
    #7#16 = tdto;
    #7#17 = timpo;
    #7#18 = tpagar;
    |SI FEstado ! 0; |GRABA 020#7n; |SINO; |GRABA 020#7a; |FINSI;
    |LIBERA #7;
    |CIERRA #7;
|FPRC;

|PROCESO mira_ven;
    enCodPago = #1#10;
    |HAZ LeeFormaDePago;
    |SI #712#5 ! 0;
        |SI #712#2 = "D"; em = 1; |SINO; em = 1000; |FINSI;
        j = #712#5 / em;

        |SI enModVisados = 0;
             fechv = #1#29 + j;
        |SINO;
             |SI #712#5 = 0;
                   fechv = #1#29;
             |SINO;
                   aFecha = #1#29;
                   aAny = aFecha % -104;
                   aMes = aFecha % 402;
                   nMes = aMes;
                   |SI nMes = 12;
                        nAny = aAny;
                        nAny = nAny + 1;
                        aAny = nAny;
                        aFecha = (("00" + #712#5) % -102) + "." + "01" + "." + aAny;
                   |SINO;
                        nMes = nMes + 1;
                        aMes = ("00" + nMes) % -102;
                        aFecha = (("00" + #712#5) % -102) + "." + aMes + "." + aAny;
                   |FINSI;
                   fechv = aFecha;
             |FINSI;
        |FINSI;
    |SINO;
        fechv = #1#29;
    |FINSI;
|FPRC;

|PROCESO historec;
    |ABRE #10;
    |DDEFECTO #10;
    #10#0 = #1#27;
    #10#1 = #1#28;
    |LIBERA #10;
    |LEE 110#10=;
    FEstado = FSalida;

    |SI FSalida = 0;
        |MENAV "     Ya existia la Factura en el Historico de Recibos";
    |FINSI;

    #10#2 = #1#29;
    #10#3 = #1#1;
    #10#4 = #11#1;
    |HAZ mira_ven;

    |SI fechv ! "01.01.0000"; |Y fechv ! "00.00.0000";
       #10#5  = fechv;
    |SINO;
       #10#5  = #10#2;
    |FINSI;
    #10#6  = tpagar;
    #10#7  = #1#11;
    #10#11 = #1#10;
    aAlfa  = #10#2 % -104;
    #10#17 = aAlfa;
    |SI FEstado ! 0; |GRABA 020#10n; |SINO; |GRABA 020#10a; |FINSI;
    |LIBERA #10;
    |CIERRA #10;
|FPRC;

|PROCESO HistCuotCli;
   |SALVA_DATOS #500;
   #500#10 = "S";
   |LEE 101#500=;
   |SI FSalida = 0;
       |BORRA 020#500a;
       |LIBERA #500;
   |FINSI;
   |REPON_DATOS #500;

   |LEE 101#500=;

   #500#10 = "S";
   |GRABA 020#500a;
   |LIBERA #500;
|FPRC;

|DEFBUCLE HistCuotCli;
     #asm00013#1;
     ;
     #1#0, #1#1, "  ", 00000, "N", 001, 01;
     #1#0, #1#1, "zz", 99999, "N", 999, 99;
     be;
     NULL, HistCuotCli;
|FIN;

|PROCESO graba_hiscab;
     |ABRE #90;
     #90#0 = #1#27;
     #90#1 = #1#28;
     |LEE 101#90=;
     |SI FSalida ! 0;
         |DDEFECTO #90;
         #90#0 = #1#27;
         #90#1 = #1#28;
         |GRABA 020#90b;
     |FINSI;

     |PARA xxx = 0; |SI xxx [ 26; |HACIENDO xxx = xxx + 1;
           yyy = xxx + 2;
           #90yyy = #1xxx;
     |SIGUE;

     #90#29 = #1#29;
     #90#34 = #1#30;
     #90#35 = #1#31;
     #90#36 = #1#32;
     #90#44 = #1#33;
     #90#45 = ivabo;
     #90#46 = ret;
     |GRABA 020#90a;
     |LIBERA #90;
     |CIERRA #90;

     |BUCLE HistCuotCli;
|FPRC;

|PROCESO imprimir;
     #5#0 = #1#1;
     |LEE 040#5=;

     |SI enModVisados = 0;
          |PRINT;
     |FINSI;

     thono  = 0;
     tsupli = 0;
     timp   = 0;
     tacum  = 0;
     tret   = 0;
     tpagar = 0;
|FPRC;

|PROCESO GrabaAuxiliar;
     nLinea = nLinea + 1;
     #asw00050#0 = nLinea;
     #asw00050#1 = #90#0;
     #asw00050#2 = #90#1;
     |GRABA 040#asw00050.n;
|FPRC;

|PROCESO leeminuta;
  |SI #1#28 ! 0;

    |ABRE #6;
    |ABRE #8;
    |ABRE #91;
    |BUCLELIN 0leelinea#1;
    |CIERRA #6;
    |CIERRA #8;
    |CIERRA #91;

    tdto   = (thono % #1#20) ! EURODB;
    timpo  = (thono - tdto)  ! EURODB;
    timp   = (timpo % ivabo) ! EURODB;
    tfac   = timpo + tsupli + timp;
    ret    = 0;
    tret   = 0;
    |SI #1#12 = "S"; ret = retenci; |FINSI;
    |SI #1#12 = "S";|Y thono ! 0; tret = (timpo % ret) ! EURODB; |FINSI;
    tpagar = tfac - tret - tacum;

    |HAZ bancos;
    |HAZ cliente;
    |HAZ histofac;
    |HAZ historec;
    |HAZ graba_hiscab;
    |HAZ imprimir;
    |HAZ GrabaAuxiliar;
    |BUCLELIN 1NULL#1;
    |BORRA 020#1a;

  |FINSI;

    |LIBERA #1;
|FPRC;

|DEFBUCLE ascabmin;
     #1#1;
     ;
     #11#1, nDCliente;
     #11#1, nHCliente;
     e;
     NULL, leeminuta;
|FIN;

|PROCESO calprin;
     eaFichero = ("1x" + Usuario) % -108;
     |NOME_DAT #asw00050#eaFichero#;
     |DELINDEX #asw00050;

     |SI enModVisados = 0;
          |IMPRE 0;
          |SI FSalida ! 0; |ACABA; |FINSI;

          |INFOR "asactual";
          |SI FSalida ! 0; |FINIMP; |ACABA; |FINSI;
     |FINSI;

     nLinea = 0;
     |ABRE #asw00050;
     |BUCLE ascabmin;
     |CIERRA #asw00050;

     |SI enModVisados = 0;
          |PIEINF;
          |FININF;
          |FINIMP;
     |FINSI;

     |SI  nLinea !  0; |Y #11#2 = "S";
          enDesdeActual = 1;
          |SUB_EJECUTA_NP "asm00010";
          enDesdeActual = 0;
     |FINSI;
|FPRC;

|PROCESO ivabo;
     |ABRE #4;
     |LEE 000#4p;
     |SI FSalida ! 0; |DDEFECTO #4; |FINSI;
     |CIERRA #4;

     |ABRE #450;
     #450#0 = #11#1;
     |LEE 000#450=;
     |SI FSalida ! 0; |DDEFECTO #450; |FINSI;
     |CIERRA #450;

     ivabo   = #450#3;
     retenci = #4#31;
|FPRC;

|PROGRAMA;
     nDCliente = 1;
     nHCliente = 99999;

     aParametro = PARAMETRO;
     |QBF aParametro;

     |ABRE #11;
     |DDEFECTO #11;

     |SI aParametro ! "";
         #11#0 = "SI";
         #11#1 = enTipoFac;
         #11#2 = eaParaConta;
         |SI aParametro = "TODAS"; aParametro = ""; |FINSI;
     |FINSI;

     |SI aParametro = "";

          |CLS;
          |PINPA #0#11;
          |PINDA #0#11;
          |ENDATOS #1#11;
          |SI FSalida = 0; |Y #11#0 = "SI";
               enErrorFac = 0;
               |HAZ ivabo;
               |HAZ calprin;
          |FINSI;
          |CIERRA #11;

     |SINO;

          |SI enCliente ! 0;
              nDCliente = enCliente;
              nHCliente = enCliente;
          |FINSI;

          |HAZ ivabo;
          |HAZ calprin;

          enErrorFac = 0;
          |CIERRA #11;

     |FINSI;

|FPRO;
