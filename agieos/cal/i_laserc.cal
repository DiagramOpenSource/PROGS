|FICHEROS;
     eosw0048 #600,MANTE;
|FIN;

|VARIABLES;
     aAlfan1     = "";
     Cadena      = "";
     DirectorioD = "";
     DirectorioB = "";
     Sistema     = "";
     Raiz        = "";
     aDirecBasi  = "";
     aDirecSesi  = "";
     Directorio  = "";
     Fichero     = "";
     FicheroC    = "";
     Valfa1      = "";
     Valfa2      = "";
     Valfa3      = "";
     Valfa4      = "";
     EsBasico    = 0;
     PathBass    = "";
     Handle      = "";
     Handle1     = "";
     Variable    = "";
     XPara       = 0;
     Sesion      = "";
     Empresa     = "";
     NACopiar    = 0;
     NFichero    = "";
     NFichero1   = "";
     Unidad      = "";
     Valfa       = "";
     nCampo      = 0;
     nCanti      = 0;
     aCanti      = "";
     aTipoSec    = "";
     aSigno      = "";
     aVersion    = "";
     aLongi      = "";
     nLongi      = "";
     aTrabajo    = "";
     aCaract     = "";
     aAlfan      = "";
     aNombre     = "";
     aNomAux     = "";
     nPara       = 0;
     nPosic      = 0;
     nVersion    = 0;
     nError      = 0;
     aFicher1    = "";
     aFicher2    = "";
     nVerifica   = 0;

     &enSwDirecta  = 0;
     &enPer        = 0;
     &enAnyo       = 0;
     &eaTipoSec    = "";
     &eaEjecutable = "";
     &eaTipoEmi    = "";
     &eaFichCol    = "";
     &eaFichAux    = "";
     &eaUnidad     = "";
     &eaMoneda     = "";
     &enCanti      = 0;

{700}&NEmpresas = "";
|FIN;

|PROCESO QueVersion;
     enSwDirecta = 0;

     |VERSIONRTME aVersion;
     |SI aVersion ] "9.09Q";  enSwDirecta = 1;  |ACABA;  |FINSI;

     aLongi = aVersion % 0;
     nLongi = aLongi;
     aAlfan = "";
     |PARA nPara = 1;  |SI nPara [ nLongi;  |HACIENDO nPara = nPara + 1;
           nPosic  = (100 * nPara) + 1;
           aCaract = aAlfan % nPosic;
           |SI aCaract = ".";  |SAL;  |FINSI;

           aVersion = aVersion + aCaract;
     |SIGUE;

     nVersion = aVersion;

     |SI nVersion ] 10;  enSwDirecta = 1; |FINSI;
|FPRC;

|| ************************************************************************
||                  GRABACION DEL DISKETTE PARA LASER
|| ************************************************************************

|PROCESO Mayus0048;
     #600Campo = #600Campo > #600Campo;  |PINTA #600Campo;
|FPRC;

|PROCESO CopiaFichero;
     nError   = 0;
     |SI #600#3 = "D";
         Unidad  = #600#1 + ":";
     |SINO;
         Unidad  = DirectorioD;
     |FINSI;

     |QUE_SISTEMA Sistema;
     FSalida = 0;
     |SI Sistema ! "ESDOS"; |Y #600#3 = "D";
         Valfa = Raiz + "bin/agcopiaa " + Directorio + Fichero + " " + Unidad;
         |PINTA 2018, "";
         |ATRI I; |SYSTEM Valfa; |ATRI i;
     |SINO;
         Valfa1 = Directorio + Fichero;
         Valfa2 = Unidad + Fichero;
         |COPIA_FICHERO Valfa1, Valfa2;
         |SI FSalida ! 0;
             |SI EsBasico = 1;
                 Valfa3 = "     No existe el ejecutable de Hacienda " + Fichero + ", pongase en contacto con su Distribuidor.";
                 |MENAV Valfa3;
                 nError = 1;
             |SINO;
                 Valfa3 = "Error [" + FSalida + "] en copia de ficheros ...";
                 |PINTA 2018, Valfa3;
                 |PINTA 2380, ".";
                 |PAUSA;
             |FINSI;
         |SINO;
             |SI Sistema ! "ESDOS";
                 Valfa = Raiz + "bin/agichmod 777 " + Valfa2;
                 |SYSTEM Valfa;
             |FINSI;
         |FINSI;
     |FINSI;

     |SI EsBasico = 0;
          Valfa1 = Directorio + Fichero;
          |FBORRA Valfa1;
     |FINSI;
|FPRC;

|PROCESO CopiaBasico;
     EsBasico = 1;
     Fichero  = eaEjecutable + ".exe";
     |HAZ CopiaFichero;
|FPRC;

|PROCESO Bat1901999;
     Pos = -1;
     |FABRE Path;
     Handle = FSalida;
     |SI FSalida ] 0;
         |SI #600#3 = "D";
             Variable = Handle + " A:";
             |FGRABA Variable;
         |FINSI;

         |SI enSwDirecta = 1;
             Variable = Handle + " cd \";
             |FGRABA Variable;

             Variable = Handle + " cd modelos";
             |FGRABA Variable;

             Variable = Handle + " cd " + aTipoSec;
             |FGRABA Variable;
         |FINSI;

         Variable = Handle + " " + eaEjecutable + " -y";
         |FGRABA Variable;

         |PARA XPara = 1; |SI XPara [ 700; |HACIENDO XPara = XPara + 1;
               |SI NEmpresas = ""; |SAL; |FINSI;

                EsBasico = 0;
                FicheroC = (NEmpresas % -108) + ".car";
                Fichero  = FicheroC;  |HAZ CopiaFichero;
                Fichero  = NEmpresas % -108;  |HAZ CopiaFichero;

                Variable = Handle + " del " + Fichero + ".txt";
                |FGRABA Variable;

                Variable = Handle + " 190pdf99 " + Fichero + " " + Fichero + ".txt " + FicheroC;
                |SI #600#3 = "D";
                    |SI #600#7 = "S"; Variable = Variable + "  -A";  |FINSI;
                |SINO;
                    |SI #600#6 = "S"; Variable = Variable + "  -A";  |FINSI;
                |FINSI;
                Variable = Variable + " -P0";
                |FGRABA Variable;

                INEmpresas = INEmpresas + 1;
         |SIGUE;

         |SI #600#3 = "D";
             Variable = Handle + " " + "C:";
             |FGRABA Variable;
         |FINSI;

         FSalida = Handle;
         |FCIERRA FSalida;

         EsBasico = 0;
         Fichero = "imprime.bat"; |HAZ CopiaFichero;
     |FINSI;
|FPRC;

|PROCESO BatWindows32;
     Pos = -1;
     |FABRE Path;
     Handle = FSalida;
     |SI FSalida ] 0;
         |SI #600#3 = "D";
             Variable = Handle + " A:";
             |FGRABA Variable;
         |FINSI;

         |SI enSwDirecta = 1;
             Variable = Handle + " cd \";
             |FGRABA Variable;

             Variable = Handle + " cd modelos";
             |FGRABA Variable;

             Variable = Handle + " cd " + aTipoSec;
             |FGRABA Variable;
         |FINSI;

         Variable = Handle + " " + eaEjecutable + " -y";
         |FGRABA Variable;

         |PARA XPara = 1; |SI XPara [ 700; |HACIENDO XPara = XPara + 1;
               |SI NEmpresas = ""; |SAL; |FINSI;

                EsBasico = 0;
                |SI aTipoSec = "347";
                    FicheroC = "opa" + (NEmpresas % -505) + ".txt";
                    Fichero  = FicheroC;  |HAZ CopiaFichero;

                    FicheroC = "opb" + (NEmpresas % -505) + ".txt";
                    Fichero  = FicheroC;  |HAZ CopiaFichero;

                    Variable = Handle + " del " + "opc" + (NEmpresas % -505) + ".txt";
                    |FGRABA Variable;
                |SINO;
                    |SI aTipoSec ! "390";
                        FicheroC = (NEmpresas % -108) + ".car";
                        Fichero  = FicheroC;  |HAZ CopiaFichero;
                    |FINSI;

                    Fichero  = NEmpresas % -108;  |HAZ CopiaFichero;
                    Variable = Handle + " del " + Fichero + ".txt";
                    |FGRABA Variable;
                |FINSI;

                |SI #600#3 = "D";
                    aAlfan1 = "/C:" + #600#7;
                |SINO;
                    aAlfan1 = "/C:" + #600#6;
                |FINSI;

                |SI aTipoSec = "390";
                    Variable = Handle + " wait " + aTipoSec + "pdf " + "/E:" + Fichero + " /R:" + Fichero + ".txt " + "/I: " + aAlfan1 + " /F:FLAG.TXT";
                    |FGRABA Variable;
                |FINSI;

                |SI aTipoSec = "347";
                    |SI enAnyo = 2000;
                        Variable = Handle + " wait " + "pdf347" + " /E:" + Fichero + " /R:" + Fichero + ".txt " + "/I: " + aAlfan1 + " /F:FLAG.TXT " + "/A:" + FicheroC + " /M:" + (eaMoneda % 101);
                    |FINSI;
                    |SI enAnyo ] 2001;
                        aFicher1 = "opa" + (NEmpresas % -505) + ".txt";
                        aFicher2 = "opc" + (NEmpresas % -505) + ".txt";
                        |SI nVerifica = 0;
                            Variable = Handle + " wait " + "347pdf" + " /E:" + aFicher1 + " /R:" + aFicher2 + "/I: " + aAlfan1 + " /F:FLAG.TXT " + " /A:" + FicheroC;
                        |SINO;
                            Variable = Handle + " wait " + "vi347" + " /E:" + aFicher1 + " /R:" + aFicher2 + " /F:FLAG.TXT ";
                        |FINSI;
                    |FINSI;
                    |FGRABA Variable;
                |FINSI;

                |SI aTipoSec ! "390";  |Y aTipoSec ! "347";
                    Variable = Handle + " wait " + aTipoSec + "pdf " + "/E:" + Fichero + " /R:" + Fichero + ".txt " + "/I: " + aAlfan1 + " /F:FLAG.TXT " + "/A:" + FicheroC;
                    |FGRABA Variable;
                |FINSI;

                |SI aTipoSec ! "347";
                    Variable = Handle + " DEL " + Fichero;
                    |FGRABA Variable;
                |FINSI;

                INEmpresas = INEmpresas + 1;
         |SIGUE;

         Variable = Handle + " DEL *.TAB";         |FGRABA Variable;
         Variable = Handle + " DEL *.CAR";         |FGRABA Variable;
         Variable = Handle + " DEL *.DLL";         |FGRABA Variable;
         Variable = Handle + " DEL ERROR*.*";      |FGRABA Variable;
         Variable = Handle + " DEL *.WMF";         |FGRABA Variable;
         Variable = Handle + " DEL *.SCC";         |FGRABA Variable;
         Variable = Handle + " DEL *.EXE";         |FGRABA Variable;
         Variable = Handle + " DEL DELEG.TXT";     |FGRABA Variable;
         Variable = Handle + " DEL PAISES.TXT";    |FGRABA Variable;
         Variable = Handle + " DEL PROVINCI.TXT";  |FGRABA Variable;
         Variable = Handle + " DEL SIGLAS.TXT";    |FGRABA Variable;
         Variable = Handle + " DEL SIGLA347.TXT";  |FGRABA Variable;
         Variable = Handle + " DEL ADMON.TXT";     |FGRABA Variable;
         Variable = Handle + " DEL *.BAT";         |FGRABA Variable;

         |SI #600#3 = "D";
             Variable = Handle + " " + "C:";
             |FGRABA Variable;
         |FINSI;

         FSalida = Handle;
         |FCIERRA FSalida;

         EsBasico = 0;
         Fichero = "imprime.bat"; |HAZ CopiaFichero;
     |FINSI;
|FPRC;

|PROCESO BatVeri190;
     Pos = -1;
     |FABRE Path;
     Handle = FSalida;
     |SI FSalida ] 0;
         |SI #600#3 = "D";
             Variable = Handle + " A:";
             |FGRABA Variable;
         |FINSI;

         |SI enSwDirecta = 1;
             Variable = Handle + " cd \";
             |FGRABA Variable;

             Variable = Handle + " cd modelos";
             |FGRABA Variable;

             Variable = Handle + " cd " + aTipoSec;
             |FGRABA Variable;
         |FINSI;

         Variable = Handle + " " + eaEjecutable + " -y";
         |FGRABA Variable;

         EsBasico = 0;
         |SI aTipoSec = "190";     FicheroC = "RT";    |FINSI;
         |SI aTipoSec = "180";     FicheroC = "RTPA";  |FINSI;
         ||SI enAnyo < 2000; FicheroC = "RT"; |SINO; FicheroC = "RC"; |FINSI;
         FicheroC = FicheroC + enAnyo + ".car";
         Fichero  = FicheroC;                     |HAZ CopiaFichero;

         EsBasico = 1;
         |SI aTipoSec = "190";     Fichero = "RT";     |FINSI;
         |SI aTipoSec = "180";     Fichero = "RTPA";   |FINSI;

         Fichero  = Fichero + enAnyo;                |HAZ CopiaFichero;
         eaFichCol = Fichero;
         EsBasico = 0;

         Variable = Handle + " del " + Fichero + ".txt";
         |FGRABA Variable;

         |SI enAnyo [ 2000;
            Variable = Handle + " wait vs19099 " + Fichero + " " + Fichero + ".txt " + FicheroC;
            |SI #600#3 = "D";
                |SI #600#7 = "S"; Variable = Variable + "  -A";  |FINSI;
            |SINO;
                |SI #600#6 = "S"; Variable = Variable + "  -A";  |FINSI;
            |FINSI;
            Variable = Variable + " -P0";
         |SINO;
            Variable = Handle + " wait vs190 /E:" + Fichero + " /R:" + Fichero + ".txt /A:" + FicheroC;
            |SI #600#3 = "D";
                |SI #600#7 = "S"; Variable = Variable + "  /C:S";  |FINSI;
            |SINO;
                |SI #600#6 = "S"; Variable = Variable + "  /C:S";  |FINSI;
            |FINSI;
         |FINSI;
         |FGRABA Variable;

         Variable = Handle + " DEL *.TAB";         |FGRABA Variable;
         Variable = Handle + " DEL *.DLL";         |FGRABA Variable;
         Variable = Handle + " DEL ERROR*.*";      |FGRABA Variable;
         Variable = Handle + " DEL *.WMF";         |FGRABA Variable;
         Variable = Handle + " DEL *.SCC";         |FGRABA Variable;
         Variable = Handle + " DEL *.EXE";         |FGRABA Variable;
         Variable = Handle + " DEL DELEG.TXT";     |FGRABA Variable;
         Variable = Handle + " DEL PAISES.TXT";    |FGRABA Variable;
         Variable = Handle + " DEL PROVINCI.TXT";  |FGRABA Variable;
         Variable = Handle + " DEL SIGLAS.TXT";    |FGRABA Variable;
         Variable = Handle + " DEL SIGLA347.TXT";  |FGRABA Variable;
         Variable = Handle + " DEL ADMON.TXT";     |FGRABA Variable;
         Variable = Handle + " DEL *.BAT";         |FGRABA Variable;

         |SI #600#3 = "D";
             Variable = Handle + " " + "C:";
             |FGRABA Variable;
         |FINSI;

         FSalida = Handle;
         |FCIERRA FSalida;

         EsBasico = 0;
         Fichero = "imprime.bat"; |HAZ CopiaFichero;
     |FINSI;
|FPRC;

|PROCESO BatVeri347;
     Pos = -1;
     |FABRE Path;
     Handle = FSalida;
     |SI FSalida ] 0;
         |SI #600#3 = "D";
             Variable = Handle + " A:";
             |FGRABA Variable;
         |FINSI;

         |SI enSwDirecta = 1;
             Variable = Handle + " cd \";
             |FGRABA Variable;

             Variable = Handle + " cd modelos";
             |FGRABA Variable;

             Variable = Handle + " cd " + aTipoSec;
             |FGRABA Variable;
         |FINSI;

         Variable = Handle + " " + eaEjecutable + " -y";
         |FGRABA Variable;

         EsBasico = 1;
         FicheroC = aNomAux; |QBF FicheroC
         |SI FicheroC = "";
            FicheroC = "auxiliar.txt";
         |FINSI;
         Fichero  = FicheroC;                     |HAZ CopiaFichero;
         eaFichAux = Fichero;

         Fichero = aNombre; |QBF Fichero;
         |SI Fichero = "";
            Fichero = "op" + enAnyo;
         |FINSI;
         |HAZ CopiaFichero;
         eaFichCol = Fichero;
         EsBasico = 0;

         Variable = Handle + " del " + Fichero + ".txt";
         |FGRABA Variable;

         Variable = Handle + " wait vs347 /E:" + Fichero + " /R:" + Fichero + ".txt /A:" + FicheroC;
         |SI #600#3 = "D";
             |SI #600#7 = "S"; Variable = Variable + "  /C:S";  |FINSI;
         |SINO;
             |SI #600#6 = "S"; Variable = Variable + "  /C:S";  |FINSI;
         |FINSI;
         |FGRABA Variable;

         |SI #600#3 = "D";
             Variable = Handle + " " + "C:";
             |FGRABA Variable;
         |FINSI;

         Variable = Handle + " DEL ADMON.TXT";     |FGRABA Variable;
         Variable = Handle + " DEL DELEG.TXT";     |FGRABA Variable;
         Variable = Handle + " DEL PAISES.TXT";    |FGRABA Variable;
         Variable = Handle + " DEL PROVINCI.TXT";  |FGRABA Variable;
         Variable = Handle + " DEL SIGLA347.TXT";  |FGRABA Variable;
         Variable = Handle + " DEL *.DLL";         |FGRABA Variable;
         Variable = Handle + " DEL *.EXE";         |FGRABA Variable;
         ||Variable = Handle + " DEL *.BAT";         |FGRABA Variable;

         FSalida = Handle;
         |FCIERRA FSalida;

         EsBasico = 0;
         Fichero = "imprime.bat"; |HAZ CopiaFichero;
     |FINSI;
|FPRC;

|PROCESO BatResto;
     Pos = -1;
     |FABRE Path;
     Handle = FSalida;
     |SI FSalida ] 0;
         |SI #600#3 = "D";
             Variable = Handle + " A:";
             |FGRABA Variable;
         |FINSI;

         |SI enSwDirecta = 1;
             Variable = Handle + " cd \";
             |FGRABA Variable;

             Variable = Handle + " cd modelos";
             |FGRABA Variable;

             Variable = Handle + " cd " + aTipoSec;
             |FGRABA Variable;
         |FINSI;

         Variable = Handle + " " + eaEjecutable + " -y";
         |FGRABA Variable;

         |PARA XPara = 1; |SI XPara [ 700; |HACIENDO XPara = XPara + 1;
               |SI NEmpresas = "";
                  |SAL;
               |FINSI;

                EsBasico = 0;
                Fichero  = NEmpresas % -108;             |HAZ CopiaFichero;

                Variable = Handle + " del " + Fichero + ".txt";
                |FGRABA Variable;

                |SI aTipoSec ! "347";  |Y aTipoSec ! "390";
                     |SI enAnyo ] 2001;
                        Variable = Handle + " wait mipf " + Fichero;
                     |SINO;
                        Variable = Handle + " pfpdf " + Fichero;
                     |FINSI;
                |SINO;
                     |SI aTipoSec = "347";
                         Variable = Handle + " 347pdf " + Fichero;
                     |FINSI;
                     |SI aTipoSec = "390";
                         Variable = Handle + " 390pdf " + Fichero;
                     |FINSI;
                |FINSI;

                |SI #600#3 = "D";
                    |SI #600#2 = "N"; Variable = Variable + "  s";  |FINSI;
                    |SI #600#7 = "S"; Variable = Variable + "  a";  |FINSI;
                    |SI #600#9 = "1"; Variable = Variable + "  P1"; |FINSI;
                    |SI #600#9 = "2"; Variable = Variable + "  P2"; |FINSI;
                    |SI #600#9 = "3"; Variable = Variable + "  P3"; |FINSI;
                |SINO;
                    |SI #600#5 = "N"; Variable = Variable + "  s"; |FINSI;
                    |SI #600#6 = "S"; Variable = Variable + "  a"; |FINSI;
                    |SI #600#8 = "1"; Variable = Variable + "  P1"; |FINSI;
                    |SI #600#8 = "2"; Variable = Variable + "  P2"; |FINSI;
                    |SI #600#8 = "3"; Variable = Variable + "  P3"; |FINSI;
                |FINSI;
                |FGRABA Variable;

                |SI #600#3 = "D";
                    Variable = Handle + " move errores.txt A:" + Fichero + ".txt";
                |SINO;
                    Variable = Handle + " move errores.txt " + Fichero + ".txt";
                |FINSI;
                |FGRABA Variable;

                INEmpresas = INEmpresas + 1;
         |SIGUE;


         |SI aTipoSec ! "347";  |Y aTipoSec ! "390";
               Variable = Handle + " del admon.*";        |FGRABA Variable;
               Variable = Handle + " del epi*.*";         |FGRABA Variable;
               Variable = Handle + " del die*.*";         |FGRABA Variable;
               |SI enAnyo ] 2001;
                  Variable = Handle + " del mipf*.*";         |FGRABA Variable;
               |SINO;
                  Variable = Handle + " del pfp*.*";         |FGRABA Variable;
               |FINSI;
         |SINO;
             |SI aTipoSec = "347";
                 Variable = Handle + " del die*.*";         |FGRABA Variable;
                 Variable = Handle + " del 347*.exe";       |FGRABA Variable;
             |FINSI;

             |SI aTipoSec = "390";
                 Variable = Handle + " del admon.*";       |FGRABA Variable;
                 Variable = Handle + " del die*.*";        |FGRABA Variable;
                 Variable = Handle + " del *.tab";         |FGRABA Variable;
                 Variable = Handle + " del 390pdf.*";      |FGRABA Variable;
                 Variable = Handle + " del *.imp";         |FGRABA Variable;
             |FINSI;
         |FINSI;

         |SI #600#3 = "D";
             Variable = Handle + " " + "C:";
             |FGRABA Variable;
         |FINSI;

         FSalida = Handle;
         |FCIERRA FSalida;

         EsBasico = 0;
         Fichero = "imprime.bat"; |HAZ CopiaFichero;
     |FINSI;
|FPRC;

|PROCESO CopiaBueno;
     Pos = -1;
     INEmpresas = NEmpresas1 <-;
     |ATRI P;
     |PINTA 2018, "";
     |ATRI p;

     PathBass = "";
     |DBASE PathBass;
     Path   = "wt  " + PathBass + "hacienda/" + aTipoSec + "/imprime.bat";

     |SI aTipoSec = "190";  |O aTipoSec = "180";
         |SI enAnyo [ 1999;
             |SI aTipoSec = "190";
                 |HAZ Bat1901999;
             |FINSI;
         |SINO;
             |HAZ BatWindows32;
         |FINSI;
     |SINO;
         |SI aTipoSec = "390";  |O aTipoSec = "347";
              |SI enAnyo [ 1999;
                  |HAZ BatResto;
              |SINO;
                   |HAZ BatWindows32;
              |FINSI;
         |SINO;
             |HAZ BatResto;
         |FINSI;
     |FINSI;

     |SI #600#3 = "D";
         |ATRI I;
         |PINTA 2018, " FICHEROS COPIADO !!!!. Introduzca el diskette  ";
         |PINTA 2118, " en una maquina en D.O.S. e introducir A:IMPRIME";
         |ATRI 0;
         |PAUSA;
     |SINO;
         |SI enSwDirecta = 0;
             |ATRI I;
             |PINTA 2018, " FICHEROS COPIADO !!!!. SESION COMPLETADA CON EXITO";
             |ATRI 0;
             |PAUSA;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO NoMoPuerto;
     |SI #600#3 = "D";
         |C_M #600#2, 1, "S";
         |C_V #600#2, 1, "S";
         |C_M #600#9, 1, "S";
         |C_V #600#9, 1, "S";

         |C_M #600#5, 1, "N";
         |C_V #600#5, 1, "N";
         |C_M #600#8, 1, "N";
         |C_V #600#8, 1, "N";
     |SINO;
         |C_M #600#2, 1, "N";
         |C_V #600#2, 1, "N";
         |C_M #600#9, 1, "N";
         |C_V #600#9, 1, "N";

         |C_M #600#5, 1, "S";
         |C_V #600#5, 1, "S";
         |C_M #600#8, 1, "S";
         |C_V #600#8, 1, "S";
     |FINSI;

     |SI enAnyo < 2000;  |ACABA;  |FINSI;

     |SI aTipoSec = "190";  |O aTipoSec = "180";
      |O aTipoSec = "390";  |O aTipoSec = "347";
         |C_M #600#5, 1, "N";
         |C_V #600#5, 1, "N";
         |C_M #600#8, 1, "N";
         |C_V #600#8, 1, "N";

         |C_M #600#2, 1, "N";
         |C_V #600#2, 1, "N";
         |C_M #600#9, 1, "N";
         |C_V #600#9, 1, "N";
     |FINSI;
|FPRC;

|PROCESO Diskette; |TIPO 0;
     #600#3 = #600#3 > #600#3;  |PINTA #600#3;

     |SI enSwDirecta = 0;  |C_M #600#4, 1, "S";  |FINSI;

     |HAZ NoMoPuerto;

     |C_M #600#0, 1, "S";
     |C_M #600#1, 1, "S";
     |C_M #600#6, 1, "S";  |C_V #600#6, 1, "S";
     |C_M #600#7, 1, "S";  |C_V #600#7, 1, "S";

     |SI #600#3 = "D";
         Atras = 1;
         |C_M #600#4, 1, "N";
         |C_M #600#6, 1, "N";
     |SINO;
         Atras = 0;
         |C_M #600#0, 1, "N";
         |C_M #600#1, 1, "N";
         |C_M #600#7, 1, "N";
     |FINSI;
|FPRC;

|PROCESO Atras;
     |SI Atras = 1; |ACABA; |FINSI;
     |PINPA #0#600;
     |PINDA #0#600;
|FPRC;

|PROCESO Sesion; |TIPO 0;
     |SI #600#3 = "D";                 |ACABA;  |FINSI;
     |SI #600#4 = "        "; |ERROR;  |ACABA;  |FINSI;

     Raiz        = "";  |DIRECTORIO Raiz;
     Directorio  = "";  |DBASE Directorio;
     Directorio  = Directorio + "hacienda/" + aTipoSec + "/";
     DirectorioB = Directorio;

     Directorio = "rt  " + Directorio;
     Sesion = #600#4; |QBF Sesion;
     Directorio = Directorio + Sesion + "/" + "imprime.bat";

     Pos = -1;
     |FABRE Directorio;
     Handle = FSalida;
     |SI FSalida ] 0;
         |SI enSwDirecta = 0;
             |CONFI "2400SLa Sesion ya existe, desea otra sesion : "
         |SINO;
             FSalida = -1;
         |FINSI;

         |SI FSalida = 0;
             |ERROR;
         |SINO;
             |SI aTipoSec = "190";  |O aTipoSec = "180";
                 Variable = DirectorioB + Sesion + "/die19001";      |FBORRA Variable;
                 Variable = DirectorioB + Sesion + "/die19002";      |FBORRA Variable;
                 Variable = DirectorioB + Sesion + "/die190a1";      |FBORRA Variable;
                 Variable = DirectorioB + Sesion + "/dvuz32.dll";    |FBORRA Variable;
                 Variable = DirectorioB + Sesion + "/calsel.dll";    |FBORRA Variable;
                 Variable = DirectorioB + Sesion + "/pdfenc32.dll";  |FBORRA Variable;
                 Variable = DirectorioB + Sesion + "/errorese.tab";  |FBORRA Variable;
                 Variable = DirectorioB + Sesion + "/siglas.tab";    |FBORRA Variable;
                 Variable = DirectorioB + Sesion + "/provinci.tab";  |FBORRA Variable;
                 Variable = DirectorioB + Sesion + "/admon.tab";     |FBORRA Variable;
                 Variable = DirectorioB + Sesion + "/190pdf99.exe";  |FBORRA Variable;
             |FINSI;
         |FINSI;
         |FCIERRA Handle;
     |SINO;
         |ACABA;
     |FINSI;
     Pos = -1;
|FPRC;

|PROCESO Ejecutable;
     |SI aTipoSec = "190";
         |SI enAnyo = 1999;  eaEjecutable = "i190ls99";  |FINSI;
         |SI enAnyo = 2000;  eaEjecutable = "i190ls00";  |FINSI;
         |SI enAnyo = 2001;  eaEjecutable = "i190ls01";  |FINSI;
         |SI enAnyo = 2002;  eaEjecutable = "zls02190";  |FINSI;
         |SI enAnyo = 2003;  eaEjecutable = "zls03190";  |FINSI;
         |ACABA;
     |FINSI;

     |SI aTipoSec = "180";
         |SI enAnyo = 2000;  eaEjecutable = "i180ls00";  |FINSI;
         |SI enAnyo = 2001;  eaEjecutable = "i180ls01";  |FINSI;
         |SI enAnyo = 2002;  eaEjecutable = "zls02180";  |FINSI;
         |SI enAnyo = 2003;  eaEjecutable = "zls03180";  |FINSI;
         |SI enAnyo = 2004;  eaEjecutable = "zls04180";  |FINSI;
         |SI enAnyo = 2005;  eaEjecutable = "zls05180";  |FINSI;
         |ACABA;
     |FINSI;

     |SI aTipoSec = "347";
         |SI enAnyo [ 1998;  eaEjecutable = "i347ls98";  |FINSI;
         |SI enAnyo = 1999;  eaEjecutable = "i347ls99";  |FINSI;
         |SI enAnyo = 2000;  eaEjecutable = "i347ls00";  |FINSI;
         |SI enAnyo = 2001;  eaEjecutable = "i347ls01";  |FINSI;
         |SI enAnyo = 2002;  eaEjecutable = "zls02347";  |FINSI;
         |SI enAnyo = 2003;  eaEjecutable = "zls03347";  |FINSI;
         |SI enAnyo = 2004;  eaEjecutable = "zls04347";  |FINSI;
         |SI enAnyo = 2005;  eaEjecutable = "zls05347";  |FINSI;
         |ACABA;
     |FINSI;

     |SI aTipoSec = "390";
         eaEjecutable = "zls03390";
         |SI enAnyo = 1997;  eaEjecutable = "i390ls97";  |FINSI;
         |SI enAnyo = 1998;  eaEjecutable = "i390ls98";  |FINSI;
         |SI enAnyo = 1999;  eaEjecutable = "i390ls99";  |FINSI;
         |SI enAnyo = 2000;  eaEjecutable = "i390ls00";  |FINSI;
         |SI enAnyo = 2001;  eaEjecutable = "i390ls01";  |FINSI;
         |SI enAnyo = 2002;  eaEjecutable = "zls02390";  |FINSI;
         |ACABA;
     |FINSI;

                         eaEjecutable = "imodls98";
     |SI enAnyo = 1999;  eaEjecutable = "imodls99";  |FINSI;
     |SI enAnyo = 2000;  eaEjecutable = "imodls20";  |FINSI;
     |SI enAnyo = 2001;  eaEjecutable = "imodls21";  |FINSI;
|FPRC;

|PROCESO GrabaDisketteL;
     |HAZ QueVersion;
     |HAZ Ejecutable;

     nVerifica = 0;

     |DDEFECTO #600;

     |SI enSwDirecta = 1;
         #600#3 = "S";
         #600#4 = "DIRECTA";
         |C_M #600#3, 1, "N";
         |C_M #600#4, 1, "N";
     |FINSI;

     |PUSHV 0501 2380;

     |HAZ NoMoPuerto;

     Pos = -1;
     |PINPA #0#600;
     |PINDA #0#600;
     |ENDATOS #2#600;
     |SI FSalida ! 0;  |POPV;  |ACABA;  |FINSI;

     Raiz       = "";  |DIRECTORIO Raiz;
     Directorio = "";  |DBASE Directorio;
     Directorio = Directorio + "hacienda/";      aDirecBasi = Directorio;
     Directorio = Directorio + aTipoSec;         |MKDIR Directorio;
     Directorio = Directorio + "/";              aDirecSesi = Directorio;

     |SI #600#3 = "S";
         Sesion = #600#4; |QBF Sesion;
         DirectorioD = Directorio + Sesion;
         |MKDIR DirectorioD;
         DirectorioD = DirectorioD + "/";
         Valfa = Raiz + "bin/agichmod 777 " + DirectorioD;
         |SYSTEM Valfa;
     |FINSI;

     Directorio = aDirecBasi;
     |SI #600#0 = "S"; |HAZ CopiaBasico; |FINSI;
     |SI nError = 1;  |ACABA;  |FINSI;

     Directorio = aDirecSesi;
     |HAZ CopiaBueno;

     |POPV;

     |SI enSwDirecta = 1;
         eaTipoSec = aTipoSec;
         eaTipoEmi = "LASER";
         |SUB_EJECUTA "eosz9999";
     |FINSI;
|FPRC;

|PROCESO GrabaDisketteF;
     nVerifica = 1;

     |SI enAnyo = 2005;  eaEjecutable = "zpr05347";  |FINSI;

     #600#3 = "S";
     #600#4 = "DIRECTA";

     Raiz       = "";  |DIRECTORIO Raiz;
     Directorio = "";  |DBASE Directorio;
     Directorio = Directorio + "hacienda/";      aDirecBasi = Directorio;
     Directorio = Directorio + aTipoSec;         |MKDIR Directorio;
     Directorio = Directorio + "/";              aDirecSesi = Directorio;

     Sesion = #600#4; |QBF Sesion;
     DirectorioD = Directorio + Sesion;
     |MKDIR DirectorioD;
     DirectorioD = DirectorioD + "/";
     Valfa = Raiz + "bin/agichmod 777 " + DirectorioD;
     |SYSTEM Valfa;

     Directorio = aDirecBasi;
     |SI #600#0 = "S"; |HAZ CopiaBasico; |FINSI;
     |SI nError = 1;  |ACABA;  |FINSI;

     Directorio = aDirecSesi;
     |HAZ CopiaBueno;

     |POPV;

     |SI enSwDirecta = 1;
         eaTipoSec = aTipoSec;
         eaTipoEmi = "VERIF347";
         |SUB_EJECUTA "eosz9999";
     |FINSI;
|FPRC;

|PROCESO GrabaDisketteV;
     |HAZ QueVersion;

     |SI aTipoSec = "190";
        eaEjecutable = "i190ve99";
        |SI enAnyo = 2001; eaEjecutable = "i190pr01"; |FINSI;
        |SI enAnyo = 2002; eaEjecutable = "zpr02190"; |FINSI;
        |SI enAnyo = 2003; eaEjecutable = "zpr03190"; |FINSI;
     |FINSI;

     |SI aTipoSec = "180";
         eaEjecutable = "i180ve99";
     |FINSI;

     |SI aTipoSec = "347";
         |SI enAnyo = 2000;  eaEjecutable = "i347pr00";  |FINSI;
         |SI enAnyo = 2001;  eaEjecutable = "i347pr01";  |FINSI;
         |SI enAnyo = 2002;  eaEjecutable = "zpr02347";  |FINSI;
         |SI enAnyo = 2003;  eaEjecutable = "zpr03347";  |FINSI;
         |SI enAnyo = 2004;  eaEjecutable = "zpr04347";  |FINSI;
     |FINSI;

     |SI enSwDirecta = 1;
         #600#3 = "S";
         #600#4 = "DIRECTA";
         |C_M #600#3, 1, "N";
         |C_M #600#4, 1, "N";
     |FINSI;

     |PUSHV 0501 2380;

     |SI aTipoSec = "190";  |O aTipoSec = 180;
         |DDEFECTO #600;

         |SI enSwDirecta = 1;
             #600#3 = "S";
             #600#4 = "DIRECTA";
             |C_M #600#3, 1, "N";
             |C_M #600#4, 1, "N";
         |FINSI;

         |HAZ NoMoPuerto;

         Pos = -1;
         |PINPA #0#600;
         |PINDA #0#600;
         |ENDATOS #2#600;
         |SI FSalida ! 0;  |POPV;  |ACABA;  |FINSI;
     |FINSI;

     |SI aTipoSec = "347";
         |DDEFECTO #600;

         |SI enSwDirecta = 1;
             #600#3 = "S";
             #600#4 = "DIRECTA";
             |C_M #600#3, 1, "N";
             |C_M #600#4, 1, "N";
         |FINSI;

         |HAZ NoMoPuerto;

         Pos = -1;
         |PINPA #0#600;
         |PINDA #0#600;
         |ENDATOS #2#600;
         |SI FSalida ! 0;  |POPV;  |ACABA;  |FINSI;
     |FINSI;

     Raiz       = "";  |DIRECTORIO Raiz;
     Directorio = "";  |DBASE Directorio;
     Directorio = Directorio + "hacienda/";      aDirecBasi = Directorio;
     Directorio = Directorio + aTipoSec;         |MKDIR Directorio;
     Directorio = Directorio + "/";              aDirecSesi = Directorio;

     |SI #600#3 = "S";
         Sesion = #600#4; |QBF Sesion;
         DirectorioD = Directorio + Sesion;
         |MKDIR DirectorioD;
         DirectorioD = DirectorioD + "/";
         Valfa = Raiz + "bin/agichmod 777 " + DirectorioD;
     |FINSI;

     Directorio = aDirecBasi;
     |SI #600#0 = "S"; |HAZ CopiaBasico; |FINSI;
     |SI nError = 1;  |ACABA;  |FINSI;

     Directorio = aDirecSesi;

     Pos = -1;
     PathBass = "";
     |DBASE PathBass;

     |SI aTipoSec = "190";  |O aTipoSec = "180";
         Path   = "wt  " + PathBass + "hacienda/" + aTipoSec + "/imprime.bat";
         |HAZ BatVeri190;
     |FINSI;

     |SI aTipoSec = "347";
         Path   = "wt  " + PathBass + "hacienda/" + aTipoSec + "/imprime.bat";
         |HAZ BatVeri347;
     |FINSI;

     |SI #600#3 = "D";
         |ATRI I;
         |PINTA 2018, " FICHEROS COPIADO !!!!. Introduzca el diskette  ";
         |SI aTipoSec = "347";
             |PINTA 2118, " en una maquina en D.O.S. e introducir A:verifica";
         |SINO;
             |PINTA 2118, " en una maquina en D.O.S. e introducir A:imprime";
         |FINSI;
         |ATRI 0;
         |PAUSA;
     |SINO;
         |SI enSwDirecta = 0;
             |ATRI I;
             |PINTA 2018, " FICHEROS COPIADO !!!!. SESION COMPLETADA CON EXITO";
             |ATRI 0;
             |PAUSA;
         |FINSI;
     |FINSI;
     |POPV;

     |SI aTipoSec = "190";  |O aTipoSec = "180";
         |SI enSwDirecta = 1;
             eaTipoSec = aTipoSec;
             eaTipoEmi = "VERIF190";
             |SUB_EJECUTA "eosz9999";
         |FINSI;
     |FINSI;

     |SI aTipoSec = "347";
         |SI enAnyo ] 2001;
             |SI enSwDirecta = 1;
                 eaTipoSec = aTipoSec;
                 eaTipoEmi = "VERIF347";
                 |SUB_EJECUTA "eosz9999";
             |FINSI;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO Errores180;
     eaTipoSec = aTipoSec;
     eaTipoEmi = "ERRORES180";
     |SUB_EJECUTA "eosz9999";
|FPRC;

|PROCESO Errores190;
     eaTipoSec = aTipoSec;
     eaTipoEmi = "ERRORES190";
     |SUB_EJECUTA "eosz9999";
|FPRC;

|PROCESO Errores347;
     eaTipoSec = aTipoSec;
     eaTipoEmi = "ERRORES";
     |SUB_EJECUTA "eosz9999";
|FPRC;

||*************************************************************************
