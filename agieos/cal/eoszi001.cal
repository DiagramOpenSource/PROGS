|FICHEROS;
    eoszi001 #0;
    eosclipr #1;    || cliente-proveedor general
    eosclpre #2;    || cliente-proveedor empresa
    eosca347 #3;    || mant. 347 cabs.
    eosli347 #4;    || mant. 347 lins.
    eosclien #5;    || empresas
    eosw0029 #30;   || fichero apoyo 1
|FIN;

|VARIABLES;
    alfa1 = "";
    alfa2 = "";
    alfa3 = "";
    alfa4 = "";
    nume1 = 0;
    nume2 = 0;
    nume3 = 0;
    nume4 = 0;
    para1 = 0;
    para2 = 0;
    para3 = 0;
    para4 = 0;
    fech1 = @;
    fech2 = @;
    fech3 = @;
    fech4 = @;
    elsw1 = 0;
    elsw2 = 0;
    elsw3 = 0;
    elsw4 = 0;
    FEstado = 0;
    seleccio = 0;
    modo = 0;
    sw_rup = 0;

    fabre = "";
    fcanl = "";
    field = "";
    dbase = "";
    codi = 0;
    codi1 = 0;
    codi2 = 0;
    linea = 0;
    emp_ant = -1;
|FIN;
____________________________________/ CALCULOS PANTALLA
|PROCESO mayus; |TIPO 0;
    #0Campo = #0Campo > #0Campo;
|PINTA #0Campo;
|FPRC;
____________________________________/ BORRADO
|PROCESO borra_empresa1;
|BORRA 020#2a;
|FPRC;

|DEFBUCLE borra_empresa;
 #2#1;
 ;
 emp_ant, 1,     0;
 emp_ant, 2, 99999;
 ;
 NULL, borra_empresa1;
|FIN;
____________________________________/ IMPORTACION INDEXADO
|PROCESO fich_04;
    linea = linea + 1;

|DDEFECTO #4;
    #4#0 = #30#1;        || empresa
    #4#1 = linea;        || linea
|SI #30#2 = "V";    #4#2 = 1; |FINSI;
|SI #30#2 = "C";    #4#2 = 2; |FINSI;        || tipo
|SI #30#2 = "P";    #4#2 = 3; |FINSI;
    alfa1 = #30#3;  |QBT alfa1;
    #4#3 = alfa1;        || cif
    #4#4 = #30#4;        || nombre
    ||#4#5 = ;             || ptas
    #4#6 = #30#5;        || sg
    #4#7 = #30#6;        || direccion
    #4#8 = #30#7;        || numero
    #4#9 = #30#8;        || cp. 1
    #4#10 = #30#9;       || cp. 2
    #4#11 = #30#11;      || provincia
    #4#12 = #30#10;      || poblacion
|GRABA 020#4n;

|PINTA 0430, #4#3;
|FPRC;

|PROCESO fich_03;
|DDEFECTO #3;
    #3#0 = #30#1;        || empresa
|LEE 000#3=;
|SI FSalida ! 0;
        #3#1 = #5#2;     || nombre
    |GRABA 020#3n;
|FINSI;
|FPRC;

|PROCESO fich_02;
|SI #30#2 ! "V";    codi1 = codi1 + 1;  codi = codi1;  |FINSI;
|SI #30#2 = "V";    codi2 = codi2 + 1;  codi = codi2;  |FINSI;

|DDEFECTO #2;
    #2#0 = #30#1;   || empresa
    #2#1 = codi;    || codigo
|SI #30#2 = "C";    #2#2 = 2; |FINSI;        || tipo
|SI #30#2 = "V";    #2#2 = 1; |FINSI;
|SI #30#2 = "P";    #2#2 = 2; |FINSI;
    alfa1 = #30#3;  |QBT alfa1;
    #2#3 = alfa1;   || cif
    #2#4 = #30#4;   || nombre
    #2#5 = #30#5;   || sg
    #2#6 = #30#6;   || direccion
    #2#7 = #30#7;   || numero
    ||#2#8 = ;        || piso
    ||#2#9 = ;        || puerta
    #2#10 = #30#8;  || cp. 1
    #2#11 = #30#9;  || cp. 2
    #2#12 = #30#11; || provincia
    #2#13 = #30#10; || poblacion
    ||#2#14 = ;       || iva
    ||#2#15 = ;       || rec.
    ||#2#16 = ;       || acum.ptas sin iva
    ||#2#17 = ;       || acum.ptas con iva
    ||#2#18 = ;       || epigrafe
    #2#19 = #5#2;   || nombre empresa
    #2#20 = "S";    || 347 S/N
    ||#2#21 = ;       || codigo trabajador

|GRABA 020#2n;

|PINTA 0430, #2#3;
|FPRC;

|PROCESO fich_01;
|DDEFECTO #1;
    alfa1 = #30#3;  |QBT alfa1;
    #1#0 = alfa1;        || cif
|LEE 101#1=;
    FEstado = FSalida;

    #1#1 = #30#4;   || nombre
    #1#2 = #30#5;   || sg
    #1#3 = #30#6;   || direccion
    #1#4 = #30#7;   || numero
    ||#1#5 = ;        || piso
    ||#1#6 = ;        || puerta
    #1#7 = #30#8;   || cp. 1
    #1#8 = #30#9;   || cp. 2
    #1#9 = #30#11;  || provincia
    #1#10 = #30#10; || poblacion
    ||#1#11 = ;       || iva
    ||#1#12 = ;       || rec.
    ||#1#13 = ;       || epigrafe

|SI FEstado = 0;    |GRABA 020#1a; |FINSI;
|SI FEstado ! 0;    |GRABA 020#1n; |FINSI;
|LIBERA #1;

|PINTA 0430, #1#0;
|FPRC;

|PROCESO lee_apoyo1;
|SI emp_ant ! #30#1;
        emp_ant = #30#1;
    |CIERRAT #2;  |BUCLE borra_empresa;  |ABRET #2;
        codi1 = 0;
        codi2 = 0;
        linea = 0;
        #3#0 = emp_ant;
    |LEE 000#3=;
    |SI FSalida = 0;
        |BORRA 020#3a;
        |BUCLELIN 1NULL#3;
    |FINSI;
|FINSI;

|DDEFECTO #5;
    #5#0 = #30#1;
|LEE 000#5=;

|PINTA 2301, #5#0;
|PINTA 2310, #5#2;

|HAZ fich_01;            || clientes/proveedores general
|HAZ fich_02;            || clientes/proveedores empresa
|HAZ fich_03;            || 347 cabs.
|HAZ fich_04;            || 347 lins.
|FPRC;

|DEFBUCLE lee_apoyo;
 #30#2;
 ;
 "     ", "        ";
 "zzzzz", "zzzzzzzz";
 ;
 NULL, lee_apoyo1;
|FIN;
____________________________________/ IMPORTACION SECUENCIAL
|PROCESO grabando;
|DDEFECTO #30;
    alfa1 = field % 108; |QBT alfa1;
    alfa1 = ("00000000" + alfa1) % -108;
    #30#0 = alfa1;             || clave
    #30#1 = field % 905;       || empresa
    #30#2 = field % 1401;      || tipo
    #30#3 = field % 1512;      || cif
    #30#4 = field % 2740;      || nombre
    #30#5 = field % 6702;      || sg
    #30#6 = field % 6925;      || direccion
    #30#7 = field % 9404;      || numero
    #30#8 = field % 9802;      || cp. 1
    #30#9 = field % 10003;     || cp. 2
    #30#10 = field % 10320;    || poblacion
    #30#11 = field % 12315;    || provincia
    #30#12 = field % 13810;    || importe
|GRABA 020#30n;
|SI FSalida = 0;
    |PINTA 0430, #30#0;
|FINSI;
|FPRC;

|PROCESO secuencial;
    dbase = "";
|DIRECTORIO dbase;
|DELINDEX #30;
|ABRE #30;
    fabre = "r   " + dbase + "reoimp01.dat";
|FABRE fabre;
|SI FSalida ] 0;
        fcanl = FSalida;
    |PARA; |SI; |HACIENDO;
            field = fcanl + " 254";
        |FLEE field;
        |SI FSalida > 0;
            |HAZ grabando;
        |SINO;
            |SAL;
        |FINSI;
    |SIGUE;
    |FCIERRA fcanl;
        Pos = -1;
|FINSI;
|FPRC;
____________________________________/ CALCULO PRINCIPAL
|PROCESO tipo3; |TIPO 3;
|SI #0#1 = "S";
    |HAZ secuencial;
|FINSI;
|ABRET #1;
|ABRET #2;
|ABRET #3;
|ABRET #4;
|ABRET #5;
    codi1 = 0;
    codi2 = 0;
|BUCLE lee_apoyo;
|CIERRAT #1;
|CIERRAT #2;
|CIERRAT #3;
|CIERRAT #4;
|CIERRAT #5;
|FPRC;
