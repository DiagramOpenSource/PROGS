|FICHEROS;
     eosz0016 #0;
     eosclien #1;
     eosw0086 #86;
     eosw0087 #87,MANTE;
     eosw0088 #88,MANTE;
|FIN;

|VARIABLES;
     &aNombre     = "";
     aAlfa        = "";
     aAlfa1       = "";
     aAlfa2       = "";
     aDirModelo   = "";
     aDirSesion   = "";
     aRaiz        = "";
     aDirectorio  = "";
     aSesion      = "";
     aError       = "";
     aFichero     = "";
     aAbre        = "";
     aHandle      = "";
     aVariable    = "";

     nRegistro    = 0;
     nHePasado    = 0;
     nNume        = 0;
     nCanal       = 0;

     &eaTipoSec   = "";
     &enAnyo      = 0;
     &enError     = 0;
     &eaPathModelo = "";
|FIN;

|RUTINA ClaveBaja86;
     #86#0 = 1;
|FRUT;

|RUTINA ClaveAlta86;
     #86#0 = 9999;
|FRUT;

|RUTINA ClaveBaja87;
     #87#0 = "        ";
|FRUT;

|RUTINA ClaveAlta87;
     #87#0 = "zzzzzzzz";
|FRUT;

|RUTINA ClaveBaja88;
     #88#0 = #87#0;
     #88#1 = 00001;
|FRUT;

|RUTINA ClaveAlta88;
     #88#0 = #87#0;
     #88#1 = 99999;
|FRUT;

|| 컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
|| PROCESOS LLAMADOS DESDE EL EOSW0088
|| 컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴

|PROCESO Printar;
     |PRINT;
|FPRC;

|DEFBUCLE eosm0086;
     #86#1;
     ;
     0001;
     9999;
     ;
     NULL, Printar;
|FIN;

|PROCESO Impresion;
     |IMPRE 0;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |INFOR "eosz0016";
     |SI FSalida ! 0;  |FINIMP;  |ACABA;  |FINSI;

     |BUCLE eosm0086;

     |PIEINF;
     |FININF;
     |FINIMP;
|FPRC;

|PROCESO CargaEntlineal;
     aSesion      = #88#0;  |QBT aSesion;
     aAlfa        = #88#2 % 107;
     |SI aAlfa = "SOPORTE";
         aError = #88#2 % 912; |QBF aError;
         |SI eaTipoSec = "180";
            aError = #88#2 % 912;
         |FINSI;
         |SI eaTipoSec = "347";
             aError       = "op" + enAnyo + ".txt";
         |FINSI;
         |SI eaTipoSec = "349";
             aError       = "cc" + enAnyo + ".txt";
         |FINSI;
     |SINO;
         |SI eaTipoSec = "130";
             aError       = "130" + (("00000" + #88#1) % -105) + ".txt";

             |XABRE "EC", aFichero, nCanal;
             aFichero     = "/MODELOS/" + eaTipoSec + "/" + aError;
             |SI FSalida < 0;
                 aError       = "139" + (("00000" + #88#1) % -105) + ".txt";
             |FINSI;
         |FINSI;

         |SI eaTipoSec = "347";
             aError       = "opc" + (("00000" + #88#1) % -105) + ".txt";
         |FINSI;

         |SI eaTipoSec ! "130"; |Y eaTipoSec ! "347";
             aError       = eaTipoSec + (("00000" + #88#1) % -105) + ".txt";
         |FINSI;

         |SI eaTipoSec = "110";  |O eaTipoSec = "115";  |O eaTipoSec = "130";
         |O  eaTipoSec = "131";  |O eaTipoSec = "300";  |O eaTipoSec = "310";
         |O  eaTipoSec = "311";  |O  eaTipoSec = "123"; |O  eaTipoSec = "303";
             |SI enAnyo ] 2002;
                 aError       = "err" + (("00000" + #88#1) % -105) + ".txt";
             |FINSI;
         |FINSI;
     |FINSI;

     |SI eaTipoSec = "349";
         aFichero     = "/MODELOS/" + eaTipoSec + "/" + enAnyo + "/" + aError;
     |SINO;
         |SI eaTipoSec = "303";
             aFichero     = eaPathModelo + aError;
         |SINO;
             aFichero     = "/MODELOS/" + eaTipoSec + "/" + aError;
         |FINSI;
     |FINSI;

     |XABRE "EC", aFichero, nCanal;
     |SI FSalida < 0;
         |MENAV "     No existe el Fichero de Errores";
         |ACABA;
     |FINSI;

     |XABRE "CU", aFichero, nCanal;
     nRegistro = 0;
     |ABRE #86;
     |PARA; |SI; |HACIENDO;
            nNume     = 250;
            |XLEE nCanal, nNume, aVariable;
            |SI FSalida < 0;  |SAL;  |FINSI;

            nRegistro = nRegistro + 1;
            #86#0 = nRegistro;
            #86#1 = aVariable % 0170;
            |GRABA 020#86n;
            |LIBERA #86;
     |SIGUE;
     |XCIERRA nCanal;
     |CIERRA #86;

     |SI nRegistro = 0;
         |MENAV "     El Fichero esta vacio.";
         |ACABA;
     |FINSI;

     |PUSHV 0501 2380;
     |PINPA #0#86;
     |ATRI R;
     |PINTA 0644, eaTipoSec;
     |PINTA 0669, #88#1;
     |ATRI 0;

     |ENTLINEAL #1#86, -1, 4, 22, 0, ClaveBaja86, ClaveAlta86;
     |CONFI "2400NDesea Imprimir el Documento : ";
     |SI FSalida = 0;
         |HAZ Impresion;
     |FINSI;

     |POPV;

     |DELINDEX #86;
|FPRC;

|PROCESO Tipo11w0088;  |TIPO 11;
     |SI FSalida ! 10;  |ACABA;  |FINSI;

     |HAZ CargaEntlineal;
|FPRC;

|| 컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
|| PROCESOS LLAMADOS DESDE EL EOSW0087
|| 컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
|PROCESO Tipo7w0087;  |TIPO 7;
     |PTEC 807;
     |ENTLINEAL #1#88, -1, 4, 21, 0, ClaveBaja88, ClaveAlta88;
|FPRC;

|PROCESO Tipo11w0087;  |TIPO 11;
     |SI FSalida ! 10;  |ACABA;  |FINSI;
     |ENTLINEAL #1#88, -1, 4, 21, 0, ClaveBaja88, ClaveAlta88;
|FPRC;

|| 컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
|| PROCESOS LLAMADOS DESDE EL EOSZ0016
|| 컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴

|PROCESO RestoModelos;
     nRegistro   = 0;
     aSesion     = #87#0;  |QBF aSesion;
     aDirSesion  = "/MODELOS/" + eaTipoSec + "/";
     aFichero    = "/MODELOS/" + eaTipoSec + "/*.txt";

     |SI eaTipoSec = "349";
         aDirSesion  = "/MODELOS/" + eaTipoSec + "/" + enAnyo + "/";
         aFichero    = "/MODELOS/" + eaTipoSec + "/" + enAnyo + "/*.txt";
     |FINSI;

     |R_PDIR aFichero;
     |PARA; |SI; |HACIENDO;
            |SI FSalida ! 0; |SAL; |FINSI;

            aError = aFichero - aDirSesion;
            aAlfa  = aError % 102;
            aAlfa  = aAlfa  > aAlfa;

            aAlfa1 = aError % 103;
            aAlfa1 = aAlfa1 > aAlfa1;
            |SI aAlfa1 = "ERR";
                aAlfa2 = aError % 401;
                |SI aAlfa2 < "0"; |O aAlfa2 > "9";
                    aAlfa = "";
                |FINSI;
            |FINSI;
            |SI aAlfa1 = "OPA";  aAlfa = "";     |FINSI;
            |SI aAlfa1 = "OPB";  aAlfa = "";     |FINSI;
            |SI aAlfa1 = "OPC";  aAlfa = "347";  |FINSI;
            |SI aAlfa1 = "CE2";  aAlfa = "";     |FINSI;
            |SI aAlfa1 = "CB2";  aAlfa = "";     |FINSI;

            |SI aAlfa = "RT"; |O aAlfa = "ER";  |O aAlfa = "OP";
             |O aAlfa = "CC";
                nHePasado = 1;
                nRegistro = 1;
                #88#0 = #87#0;
                #88#1 = 1;
                #88#2 = "SOPORTE " + aError;
                |GRABA 040#88n;
                |LIBERA #88;
            |SINO;
                |SI aAlfa ! "AU";  |Y aAlfa ! "";
                   aAlfa  = aError % 405;
                   #1#0   = aAlfa;
                   |LEE 040#1=;
                   |SI FSalida = 0;
                       |SI #1#0 = 0;  |SAL;  |FINSI;
                       nHePasado = 1;
                       nRegistro = 1;
                       #88#0 = #87#0;
                       #88#1 = #1#0;
                       #88#2 = #1#2;
                       |GRABA 040#88n;
                       |LIBERA #88;
                       |LIBERA #88;
                   |FINSI;
                |FINSI;
                |R_SDIR aFichero;
            |FINSI;
     |SIGUE;

     |SI nRegistro = 0;
         |BORRA 020#87a;
         |LIBERA #87;
     |FINSI;
|FPRC;

|PROCESO Modelo303;
     nRegistro   = 0;
     aSesion     = #87#0;  |QBF aSesion;
     aDirSesion  = eaPathModelo;
     aFichero    = eaPathModelo + "err*.txt";

     |R_PDIR aFichero;
     |PARA; |SI; |HACIENDO;
            |SI FSalida ! 0; |SAL; |FINSI;

            aError = aFichero % -112;
            aAlfa  = aError % 405;

            #1#0   = aAlfa;
            |LEE 040#1=;
            |SI FSalida = 0;
                |SI #1#0 = 0;  |SAL;  |FINSI;
                nHePasado = 1;
                nRegistro = 1;
                #88#0 = #87#0;
                #88#1 = #1#0;
                #88#2 = #1#2;
                |GRABA 020#88n;
                |LIBERA #88;
            |FINSI;

            |R_SDIR aFichero;
     |SIGUE;

     |SI nRegistro = 0;
         |BORRA 020#87a;
         |LIBERA #87;
     |FINSI;
|FPRC;

|PROCESO MiraEmpresas;
     |SI eaTipoSec = 303;  |HAZ Modelo303;     |FINSI;
     |SI eaTipoSec ! 303;  |HAZ RestoModelos;  |FINSI;
|FPRC;

|DEFBUCLE eosw0087;
     #87#1;
     ;
     "        ";
     "zzzzzzzz";
     be;
     NULL, MiraEmpresas;
|FIN;

|PROCESO Errores;
     aFichero = ("86" + Usuario + "zzzzzzzz") % 108;
     |NOME_DAT #86 aFichero;
     |ABRE #86;  |CIERRA #86;  |DELINDEX #86;

     aFichero = ("87" + Usuario + "zzzzzzzz") % 108;
     |NOME_DAT #87 aFichero;
     |ABRE #87;  |CIERRA #87;  |DELINDEX #87;

     aFichero = ("88" + Usuario + "zzzzzzzz") % 108;
     |NOME_DAT #88 aFichero;
     |ABRE #88;  |CIERRA #88;  |DELINDEX #88;

     |ABRE #87;
     #87#0 = "DIRECTA";
     |LEE 101#87=;
     |SI FSalida ! 0;
         nRegistro = 1;
         #87#0 = "DIRECTA";
         |GRABA 020#87n;
     |FINSI;
     |LIBERA #87;
     |CIERRA #87;

     nHePasado = 0;
     |ABRE #1;
     |ABRE #88;
     |BUCLE eosw0087;
     |CIERRA #1;
     |CIERRA #88;

     |SI nHePasado = 1;
         #0#0 = eaTipoSec;
         |PINPA #0#0;
         |PINDA #0#0;
         |ENTLINEAL #1#87, -1, 4, 21, 0, ClaveBaja87, ClaveAlta87;

         |SI eaTipoSec = "349";
             enError = 1;
         |FINSI;
     |FINSI;

     |DELINDEX #86;
     |DELINDEX #87;
     |DELINDEX #88;
|FPRC;
