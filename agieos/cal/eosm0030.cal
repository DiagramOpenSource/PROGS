|FICHEROS;
     eosm0030;
     eosm0029;
     eosm0031;
     eosm0032;
     eosm0033;
     eosm0034;
     eosm0035;
     eosm0036;
     eosm0133;

     eosaplic;
     eosclipr;

     :/agicli/def/agicontr;
|FIN;

|VARIABLES;
     nCampo       = 0;
     nModo        = 0;
     nSwBorra     = 0;
     nEjerAnt     = 0;
     nAcumulado   = 0;

     aAlfa        = "";
     aPathFich    = "";
     aPathPan     = "";
     aClave       = "";
     aSubClave    = "";

     &enEmpresa   = 0;
     &enDEmpresa  = 0;
     &enHEmpresa  = 0;
     &enEjercic   = 0;
     &enAnyo      = 0;
     &enTipo      = 0;
     &eaUltimo    = "";
     &enBoton1    = -1;

 {-1} aMenu     = "";
      aMenu1    = "1620";
      aMenu2    = "1";
      aMenu3    = "6";
      aMenu4    = "Recogida de Datos Externos                      ";
      aMenu5    = "Entrada por Trabajadores                        ";
      aMenu6    = "Calculo Modelo 110/111 y 190 desde Trabajadores ";
      aMenu7    = "Mantenimiento 110/111                           ";
      aMenu8    = "Mantenimiento 190                               ";
      aMenu9    = "Emisiones                                       ";

 {-1} aMenuPas  = "";
      aMenuPas1 = "1950";
      aMenuPas2 = "1";
      aMenuPas3 = "3";
      aMenuPas4 = "Revisar Retenciones Empresas Libros";
      aMenuPas5 = "Pase desde Libro a Trabajadores    ";
      aMenuPas6 = "Traerse datos Ejercicio Anterior   ";

 {-1} aMenuCal  = "";
      aMenuCal1 = "1830";
      aMenuCal2 = "1";
      aMenuCal3 = "2";
      aMenuCal4 = "Calculo del 110/111                             ";
      aMenuCal5 = "Calculo del 110/111 Ultimo Periodo y Modelo 190 ";

 {-1} aMenuEmi  = "";
      aMenuEmi1 = "1750";
      aMenuEmi2 = "1";
      aMenuEmi3 = "4";
      aMenuEmi4 = "Comprobacion de  Trabajadores   ";
      aMenuEmi5 = "Emision 110/111                 ";
      aMenuEmi6 = "Emision 190                     ";
      aMenuEmi7 = "Errores Laser                   ";

      aSubAnt   = "";
|FIN;


|PROCESO TipoDeclaracion;
     |PARA nCampo = 4;  |SI nCampo [ 13;  |HACIENDO nCampo = nCampo + 1;
           |C_M #eosm0030#nCampo#, 1, "S";
     |SIGUE;
     |C_M #eosm0030#20, 1, "S";
     |SI #eosm0030#20 = "   "; #eosm0030#20 = "111"; |FINSI;
     |SI #eosm0030#3 = " ";
         |PARA nCampo = 4;  |SI nCampo [ 13;  |HACIENDO nCampo = nCampo + 1;
               |C_M #eosm0030#nCampo#, 1, "N";
               #eosm0030#nCampo# = "";
               |PINTA #eosm0030#nCampo#;
         |SIGUE;
         |C_M #eosm0030#20, 1, "N";
     |FINSI;
     |PINTA #eosm0030#20;
|FPRC;

|PROCESO Como2015;
     aSubAnt = #eosm0032#4;

     #eosm0031#0 = #eosm0032#0;
     #eosm0031#1 = #eosm0032#1;
     #eosm0031#2 = #eosm0032#2;
     |LEE 040#eosm0031.=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     #eosm0032#1 = #eosm0030#1;
     |SI nAcumulado ! 0;
         |PDEFECTO #eosm0032, 7, 18;
     |FINSI;

     |SI #eosm0032#3 = "A";           || Tique 700000_9626
         |SI #eosm0032#4 = "  "; |Y #eosm0032#1 = 2015;
             #eosm0032#4 = "01";
         |FINSI;
     |FINSI;

     |GRABA 020#eosm0032.n;
     |LIBERA #eosm0032;

     #eosm0032#1 = nEjerAnt;
     #eosm0032#4 = aSubAnt;
     |LEE 101#eosm0032.=;
|FPRC;

|PROCESO Como2016;
     #eosm0031#0 = #eosm0032#0;
     #eosm0031#1 = #eosm0032#1;
     #eosm0031#2 = #eosm0032#2;
     |LEE 040#eosm0031.=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     aClave    = #eosm0032#3;
     aSubClave = #eosm0032#4;

     |SI #eosm0032#3 = "A";
         |SI #eosm0032#4 ! "  ";
             aSubClave = "";
         |FINSI;
     |FINSI;

     |SI #eosm0032#3 = "B";
         |SI #eosm0032#4 = "02";  aSubClave = "01";  |FINSI;
         |SI #eosm0032#4 = "03";  aSubClave = "02";  |FINSI;
         |SI #eosm0032#4 = "04";  aSubClave = "03";  |FINSI;
     |FINSI;

     #eosm0035#0 = #eosm0032#0;
     #eosm0035#1 = #eosm0030#1;
     #eosm0035#2 = #eosm0032#2;
     #eosm0035#3 = aClave;
     #eosm0035#4 = aSubClave;
     #eosm0035#5 = #eosm0032#5;
     #eosm0035#6 = #eosm0032#6;
     |LEE 101#eosm0035.=;
     |SI FSalida ! 0;
         |DDEFECTO #eosm0035;
         #eosm0035#0 = #eosm0032#0;
         #eosm0035#1 = #eosm0030#1;
         #eosm0035#2 = #eosm0032#2;
         #eosm0035#3 = aClave;
         #eosm0035#4 = aSubClave;
         #eosm0035#5 = #eosm0032#5;
         #eosm0035#6 = #eosm0032#6;
         |GRABA 020#eosm0035.b;
     |FINSI;

     |SI nAcumulado ! 0;
         |PDEFECTO #eosm0035, 7, 25;
     |SINO;
         |SI #eosm0032#3 = "A";  |Y #eosm0032#4 = "02";
             #eosm0035#18 = #eosm0035#18 + #eosm0032#7;
             #eosm0035#20 = #eosm0035#20 + #eosm0032#9;
             #eosm0035#19 = (#eosm0035#20 / #eosm0035#18) * 100
             #eosm0035#21 = #eosm0035#21 + #eosm0032#10;
             #eosm0035#22 = #eosm0035#22 + #eosm0032#11;
             #eosm0035#23 = #eosm0035#23 + #eosm0032#12;
             #eosm0035#24 = #eosm0032#13;
         |SINO;
             #eosm0035#7  = #eosm0035#7  + #eosm0032#7;
             #eosm0035#9  = #eosm0035#9  + #eosm0032#9;
             #eosm0035#8  = (#eosm0035#9 / #eosm0035#7) * 100
             #eosm0035#10 = #eosm0035#10 + #eosm0032#10;
             #eosm0035#11 = #eosm0035#11 + #eosm0032#11;
             #eosm0035#12 = #eosm0035#12 + #eosm0032#12;
             #eosm0035#13 = #eosm0032#13;
         |FINSI;
     |FINSI;

     |GRABA 020#eosm0035.a;
     |LIBERA #eosm0035;
|FPRC;

|PROCESO eosm0032;
     |SI #eosm0030#1 [ 2015;  |HAZ Como2015;  |FINSI;
     |SI #eosm0030#1 = 2016;  |HAZ Como2016;  |FINSI;
|FPRC;

|DEFBUCLE eosm0032;
     #eosm0032#1;
     ;
     #eosm0030#0, nEjerAnt, "            ", " ", "  ", 0,    1;
     #eosm0030#0, nEjerAnt, "zzzzzzzzzzzz", "z", "zz", 9, 9999;
     be;
     NULL, eosm0032;
|FIN;

|PROCESO eosm0035;
     aSubAnt = #eosm0035#4;

     #eosm0031#0 = #eosm0035#0;
     #eosm0031#1 = #eosm0035#1;
     #eosm0031#2 = #eosm0035#2;
     |LEE 000#eosm0031.=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     #eosm0035#1 = #eosm0030#1;
     |SI nAcumulado ! 0;
         |PDEFECTO #eosm0035, 7, 25;
     |FINSI;

     |GRABA 020#eosm0035.n;
     |LIBERA #eosm0035;

     #eosm0035#1 = nEjerAnt;
     #eosm0035#4 = aSubAnt;
     |LEE 101#eosm0035.=;
|FPRC;

|DEFBUCLE eosm0035;
     #eosm0035#1;
     ;
     #eosm0030#0, nEjerAnt, "            ", " ", "  ", 0,    1;
     #eosm0030#0, nEjerAnt, "zzzzzzzzzzzz", "z", "zz", 9, 9999;
     be;
     NULL, eosm0035;
|FIN;

|PROCESO EjerAnterior;
     |SI #eosm0030#1 [ 2015;
         |ABRE #eosm0032;
         #eosm0032#0 = #eosm0030#0;
         #eosm0032#1 = #eosm0030#1;
         #eosm0032#2 = "            ";
         #eosm0032#3 = " ";
         #eosm0032#4 = "  ";
         #eosm0032#5 = 0;
         #eosm0032#6 = 1;
         |LEE 040#eosm0032.];
         |SI FSalida = 0;  |Y #eosm0032#0 = #eosm0030#0;  |Y #eosm0032#1 = #eosm0030#1;
             |MENAV "       Ya existe datos en el Trabajador";
             |CIERRA #eosm0032;
             |ACABA;
         |FINSI;
         |CIERRA #eosm0032;
     |FINSI;

     |SI #eosm0030#1 ] 2016;
         |ABRE #eosm0035;
         #eosm0035#0 = #eosm0030#0;
         #eosm0035#1 = #eosm0030#1;
         #eosm0035#2 = "            ";
         #eosm0035#3 = " ";
         #eosm0035#4 = "  ";
         #eosm0035#5 = 0;
         #eosm0035#6 = 1;
         |LEE 040#eosm0035.];
         |SI FSalida = 0;  |Y #eosm0035#0 = #eosm0030#0;  |Y #eosm0035#1 = #eosm0030#1;
             |MENAV "       Ya existe datos en el Trabajador";
             |CIERRA #eosm0035;
             |ACABA;
         |FINSI;
         |CIERRA #eosm0035;
     |FINSI;

     |CONFI "2400SDesea respetar los acumulados : ";
     nAcumulado = FSalida;

     nEjerAnt = #eosm0030#1 - 1;

     |ABRE #eosm0031;
     |ABRET #eosm0032;
     |ABRET #eosm0035;

     |SI #eosm0030#1 [ 2015;
         |BUCLE eosm0032;
     |FINSI;

     |SI #eosm0030#1 = 2016;
         |BUCLE eosm0032;
     |FINSI;

     |SI #eosm0030#1 ] 2017;
         |BUCLE eosm0035;
     |FINSI;

     |CIERRA #eosm0031;
     |CIERRAT #eosm0032;
     |CIERRAT #eosm0035;
|FPRC;

|PROCESO MenuPas;
     |PUSHV 0501 2380;
     |PARA; |SI; |HACIENDO;
            |MENUG aMenuPas;
            aMenuPas2 = FSalida;

            |SI aMenuPas2 < "1";  |O aMenuPas2 > "3";  |SAL;  |FINSI;

            |LIBERA #eosm0030;
            |SI aMenuPas2 = "1";  |SUB_EJECUTA "eosz0013";  |FINSI;
            |SI aMenuPas2 = "2";  |SUB_EJECUTA "eoszp002";  |FINSI;
            |SI aMenuPas2 = "3";  |HAZ EjerAnterior;        |FINSI;

            |LEE 101#eosm0030.=;
     |SIGUE;
     |POPV;
|FPRC;

|PROCESO MenuCal;
     |PUSHV 0501 2380;
     |PARA; |SI; |HACIENDO;
            |MENUG aMenuCal;
            aMenuCal2 = FSalida;

            |SI aMenuCal2 < "1";  |O aMenuCal2 > "2";  |SAL;  |FINSI;

            |LIBERA #eosm0030;
            |SI aMenuCal2 = "1"; eaUltimo = "";  |SUB_EJECUTA "eosz0010";  |FINSI;
            |SI aMenuCal2 = "2"; eaUltimo = "U"; |SUB_EJECUTA "eosz0010";  |FINSI;
            |LEE 101#eosm0030.=;
     |SIGUE;
     |POPV;
|FPRC;

|PROCESO MenuEmi;
     |PUSHV 0501 2380;
     |PARA; |SI; |HACIENDO;
            |SI #eosm0030#1 [ 2003;
                aMenuEmi3 = "4";
            |SINO;
                aMenuEmi3 = "3";
            |FINSI;

            |MENUG aMenuEmi;
            aMenuEmi2 = FSalida;

            |SI aMenuEmi2 < "1";  |O aMenuEmi2 > "4";  |SAL;  |FINSI;

            |LIBERA #eosm0030;
            |SI aMenuEmi2 = "1";  |SUB_EJECUTA "eosy0013";    |FINSI;
            |SI aMenuEmi2 = "2";  |SUB_EJECUTA "eosy0008";    |FINSI;
            |SI aMenuEmi2 = "3";  |SUB_EJECUTA "eosy0009";    |FINSI;
            |SI aMenuEmi2 = "4";  |SUB_EJECUTA "eosz0016";    |FINSI;
            |LEE 101#eosm0030.=;
     |SIGUE;
     |POPV;
|FPRC;

|PROCESO Baja31;
     #eosm0031#0 = #eosm0030#0;
     #eosm0031#1 = #eosm0030#1;
     #eosm0031#2 = "            ";
|FPRC;

|PROCESO Alta31;
     #eosm0031#0 = #eosm0030#0;
     #eosm0031#1 = #eosm0030#1;
     #eosm0031#2 = "zzzzzzzzzzzz";
|FPRC;

|PROCESO GrabaTraba;
     #eosm0031#0 = #eosm0030#0;
     #eosm0031#1 = #eosm0030#1;
     #eosm0031#2 = #eosm0029#1;
     |LEE 101#eosm0031.=;
     |SI FSalida ! 0;
         #eosm0031#0 = #eosm0030#0;
         #eosm0031#1 = #eosm0030#1;
         #eosm0031#2 = #eosm0029#1;
         |GRABA 020#eosm0031.b;
     |FINSI;

     #eosm0031#3 = #eosm0029#2;
     |GRABA 020#eosm0031.a;
     |LIBERA #eosm0031;
|FPRC;

|DEFBUCLE eosm0029;
     #eosm0029#1;
     ;
     #eosm0030#0, "            ";
     #eosm0030#0, "zzzzzzzzzzzz";
     ;
     NULL, GrabaTraba;
|FIN;

|PROCESO Trabajadores;
     |ABRE #eosm0031;
     |BUCLE eosm0029;
     |CIERRA #eosm0031;

     |PUSHV 0501 2380;
     |SI #eosm0030#1 [ 2015;
         |C_A #eosm0031#2, 1, 12;
         |C_A #eosm0031#3, 1, 31;
         |C_P #eosm0031#3, 1, 1115;

         |ENTLINEAL #1#eosm0031, -3, 4, 22, 1, Baja31, Alta31;
     |FINSI;

     |SI #eosm0030#1 ] 2016;
         |C_A #eosm0031#2, 1, 9;
         |C_A #eosm0031#3, 1, 25;
         |C_P #eosm0031#3, 1, 1112;

         |BLANCO 0901 2380;
         |CUADRO 0901 2337;
         |CUADRO 0901 2311;
         |PINPA #0#eosm0035;

         |ATRI R;
         |PINTA 1002, "NIF Trab.";
         |PINTA 1012, "Nombre Trabajador        ";
         |ATRI r;

         |ENTLINEAL #1#eosm0031, -3, 4, 22, 0, Baja31, Alta31;
     |FINSI;
     |POPV;
|FPRC;

|PROCESO Baja33;
     #eosm0033#0 = #eosm0030#0;
     #eosm0033#1 = #eosm0030#1;
     #eosm0033#2 = "110";
     #eosm0033#3 = 01;
     #eosm0033#4 = 0;
|FPRC;

|PROCESO Alta33;
     #eosm0033#0 = #eosm0030#0;
     #eosm0033#1 = #eosm0030#1;
     #eosm0033#2 = "110";
     #eosm0033#3 = 12;
     #eosm0033#4 = 0;
|FPRC;

|PROCESO Baja133;
     #eosm0133#0 = #eosm0030#0;
     #eosm0133#1 = #eosm0030#1;
     #eosm0133#2 = "110";
     #eosm0133#3 = 01;
     #eosm0133#4 = 0;
|FPRC;

|PROCESO Alta133;
     #eosm0133#0 = #eosm0030#0;
     #eosm0133#1 = #eosm0030#1;
     #eosm0133#2 = "110";
     #eosm0133#3 = 12;
     #eosm0133#4 = 0;
|FPRC;

|PROCESO Modelo110;
     |PUSHV 0501 2380;
     |SI #eosm0030#1 < 2001;
        |ENTLINEAL #1#eosm0033, -4, 1, 22, 1, Baja33, Alta33;
     |SINO;
        |ENTLINEAL #1#eosm0133, -4, 1, 22, 1, Baja133, Alta133;
     |FINSI;
     |POPV;
|FPRC;

|PROCESO Baja34;
     #eosm0034#0 = #eosm0030#0;
     #eosm0034#1 = #eosm0030#1;
     #eosm0034#2 = "            ";
     #eosm0034#3 = 0000;
     #eosm0034#4 = " ";
     #eosm0034#5 = "  ";
     #eosm0034#6 = 0;
|FPRC;

|PROCESO Alta34;
     #eosm0034#0 = #eosm0030#0;
     #eosm0034#1 = #eosm0030#1;
     #eosm0034#2 = "zzzzzzzzzzzz";
     #eosm0034#3 = 9999;
     #eosm0034#4 = "z";
     #eosm0034#5 = "zz";
     #eosm0034#6 = 4;
|FPRC;

|PROCESO Modelo190_2015;
     |PUSHV 0501 2380;
     |PINPA #0#eosm0034;

     |CONTROL_SIMPLE 0, "BOTON,<F6> Datos Familiares ", 2260, 2279, Boton1;
     enBoton1 = FSalida;

     |SI #eosm0030#1 < 2015;
         |PINTA 2129, "Prolongacion ..:"; |C_V #eosm0034#36,1,"S";
         |PINTA 2229, "Movilidad .....:"; |C_P #eosm0034#37,1,2246;
         |C_V #eosm0034#62,1,"N";
     |SINO;
         |C_V #eosm0034#36,1,"N";
         |C_P #eosm0034#37,1,2146;
         |C_V #eosm0034#62,1,"S";
     |FINSI;

     |ENTLINEAL #1#eosm0034, -3, 1, 22, 0,Baja34, Alta34;

     |FIN_CONTROL_WINDOWS enBoton1;
     enBoton1 = -1;
     |PULSATECLA;

     |POPV;
|FPRC;

|PROCESO Baja36;
     #eosm0036#0 = #eosm0030#0;
     #eosm0036#1 = #eosm0030#1;
     #eosm0036#2 = "            ";
     #eosm0036#3 = 0000;
     #eosm0036#4 = " ";
     #eosm0036#5 = "  ";
     #eosm0036#6 = 0;
|FPRC;

|PROCESO Alta36;
     #eosm0036#0 = #eosm0030#0;
     #eosm0036#1 = #eosm0030#1;
     #eosm0036#2 = "zzzzzzzzzzzz";
     #eosm0036#3 = 9999;
     #eosm0036#4 = "z";
     #eosm0036#5 = "zz";
     #eosm0036#6 = 4;
|FPRC;

|PROCESO Modelo190_2016;
     |PUSHV 0501 2380;
     |PINPA #0#eosm0036;

     |CONTROL_SIMPLE 0, "BOTON,<F6> Datos Familiares ", 1070, 1079, Boton1;
     enBoton1 = FSalida;

     |ENTLINEAL #1#eosm0036, -3, 1, 22, 0, Baja36, Alta36;

     |FIN_CONTROL_WINDOWS enBoton1;
     enBoton1 = -1;
     |PULSATECLA;

     |POPV;
|FPRC;

|PROCESO Modelo190;
     |SI #eosm0030#1 [ 2015;
         |HAZ Modelo190_2015;
     |FINSI;

     |SI #eosm0030#1 ] 2016;
         |HAZ Modelo190_2016;
     |FINSI;
|FPRC;

|PROCESO T12F30;  |TIPO 12;
     |GRABA 020#eosm0030.a;

     aAlfa       = #eosm0030#1 % -102;
     enEmpresa   = #eosm0030#0;
     enEjercic   = #eosm0030#1;
     enAnyo      = #eosm0030#1;
     enTipo      = 0;

     |PUSHV 0501 2380;
     |PARA; |SI; |HACIENDO;
            |MENUG aMenu;
            aMenu2 = FSalida;

            |SI aMenu2 < "1";  |O aMenu2 > "6";  |SAL;  |FINSI;

            |SI aMenu2 = "1";    |HAZ MenuPas;             |FINSI;
            |SI aMenu2 = "2";    |HAZ Trabajadores;        |FINSI;
            |SI aMenu2 = "3";    |HAZ MenuCal;             |FINSI;
            |SI aMenu2 = "4";    |HAZ Modelo110;           |FINSI;
            |SI aMenu2 = "5";    |HAZ Modelo190;           |FINSI;
            |SI aMenu2 = "6";    |HAZ MenuEmi;             |FINSI;
     |SIGUE;
     |POPV;

     |LIBERA #eosm0030;
|FPRC;

|PROCESO BorraLos;
     |SI nSwBorra = 31;  |BORRA 020#eosm0031.a;  |LIBERA #eosm0031;  |FINSI;
     |SI nSwBorra = 32;  |BORRA 020#eosm0032.a;  |LIBERA #eosm0032;  |FINSI;
     |SI nSwBorra = 33;  |BORRA 020#eosm0033.a;  |LIBERA #eosm0033;  |FINSI;
     |SI nSwBorra = 34;  |BORRA 020#eosm0034.a;  |LIBERA #eosm0034;  |FINSI;
     |SI nSwBorra = 35;  |BORRA 020#eosm0035.a;  |LIBERA #eosm0035;  |FINSI;
     |SI nSwBorra = 36;  |BORRA 020#eosm0036.a;  |LIBERA #eosm0036;  |FINSI;
     |SI nSwBorra = 133; |BORRA 020#eosm0133.a;  |LIBERA #eosm0133;  |FINSI;
|FPRC;

|DEFBUCLE eosm0031Baja;
     #eosm0031#1;
     ;
     #eosm0030#0, #eosm0030#1, "            ";
     #eosm0030#0, #eosm0030#1, "zzzzzzzzzzzz";
     be;
     NULL, BorraLos;
|FIN;

|DEFBUCLE eosm0032Baja;
     #eosm0032#1;
     ;
     #eosm0030#0, #eosm0030#1, "            ", " ", "  ", 0,   01;
     #eosm0030#0, #eosm0030#1, "zzzzzzzzzzzz", "z", "zz", 9, 9999;
     be;
     NULL, BorraLos;
|FIN;

|DEFBUCLE eosm0035Baja;
     #eosm0035#1;
     ;
     #eosm0030#0, #eosm0030#1, "            ", " ", "  ", 0,   01;
     #eosm0030#0, #eosm0030#1, "zzzzzzzzzzzz", "z", "zz", 9, 9999;
     be;
     NULL, BorraLos;
|FIN;

|DEFBUCLE eosm0033Baja;
     #eosm0033#1;
     ;
     #eosm0030#0, #eosm0030#1, "110", 01, 00;
     #eosm0030#0, #eosm0030#1, "110", 12, 99;
     be;
     NULL, BorraLos;
|FIN;

|DEFBUCLE eosm0133Baja;
     #eosm0133#1;
     ;
     #eosm0030#0, #eosm0030#1, "110", 01, 00;
     #eosm0030#0, #eosm0030#1, "110", 12, 99;
     be;
     NULL, BorraLos;
|FIN;

|DEFBUCLE eosm0034Baja;
     #eosm0034#1;
     ;
     #eosm0030#0, #eosm0030#1, "            ", 0000, " ", "  ", 0;
     #eosm0030#0, #eosm0030#1, "zzzzzzzzzzzz", 9999, "z", "zz", 9;
     be;
     NULL, BorraLos;
|FIN;

|DEFBUCLE eosm0036Baja;
     #eosm0036#1;
     ;
     #eosm0030#0, #eosm0030#1, "            ", 0000, " ", "  ", 0;
     #eosm0030#0, #eosm0030#1, "zzzzzzzzzzzz", 9999, "z", "zz", 9;
     be;
     NULL, BorraLos;
|FIN;

|PROCESO T2F30;  |TIPO 2;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo ! 3;  |ACABA;  |FINSI;

     |MENAV "      Se borraran todos los datos incluidos modelos.";
     |CONFI "2400NEsta seguros de borrar la Ficha : ";
     |SI FSalida ! 0;  |ERROR;  |ACABA;  |FINSI;

     nSwBorra = 31;  |BUCLE eosm0031Baja;
     nSwBorra = 32;  |BUCLE eosm0032Baja;
     nSwBorra = 33;  |BUCLE eosm0033Baja;
     nSwBorra = 34;  |BUCLE eosm0034Baja;
     nSwBorra = 35;  |BUCLE eosm0035Baja;
     nSwBorra = 36;  |BUCLE eosm0036Baja;
     nSwBorra = 133; |BUCLE eosm0133Baja;
|FPRC;

|PROCESO T80F30;  |TIPO 80;
     |HAZ PatronM073;

     |ABRE #eosaplic;
     |LEE 040#eosaplic.p;
     |SI FSalida ! 0; |DDEFECTO #eosaplic; |FINSI;
     |CIERRA #eosaplic;

     |SI #eosaplic#11 = 1; |ACABA; |FINSI;

     |ABRE #agicontr;
     |LEE 040#agicontr.p;
     |SI FSalida ! 0; |CIERRA #agicontr; |ACABA; |FINSI;
     |CIERRA #agicontr;

     aPathFich = #agicontr#8; |QBF aPathFich;
     |SI aPathFich = "*";  |ACABA;  |FINSI;
     aPathPan = (aPathFich - "fich/") + "pan/";

     |PATH_DAT #eosclipr#aPathFich#;
     |PATH_PAN #eosclipr#aPathPan#;
|FPRC;
