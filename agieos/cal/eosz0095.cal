|FICHEROS;
   eosz0095 #0;

   eosm0093 #93;

   eosm3130 #130;
   eosm0130@ #1130;

   eosm0020 #131;
   eosm0020@ #1131;

   eosclien #1000;
|FIN;

|VARIABLES;
     aAlfa      = "";
     nCampo     = 0;
     &enEmpresa = 0;
     aParam     = "";
     aBase      = "";
     aMas       = "";
     nDEmpresa  = 0;
     nHEmpresa  = 0;
     nHPer      = 0;
     nHComple   = 0;

     nSumaD    = 0;
     nSwNeg    = 0;

     nEmpresa  = 0;
     nPeriodo  = 0;
     nEjer     = 0;
     nEjer4    = 0;
     nModelo   = 0;
     nComplem  = 0;
     nImpAnt   = 0;
     nImpNuevo = 0;
     nEl33     = 0;
     nEl23     = 0;
     nImporte  = 0;

     nTopeAgrario = 0;
     nTope131     = 0;
     nTope        = 0;
     nSuma        = 0;

     nNoModi   = 0;

     &enElEjer   = 0;
     &enDEmpresa = 0;
     &enHEmpresa = 0;
|FIN;

|INCLUYE i_consul;
|INCLUYE i_vareos;

|| /////////////////////// PROCESOS ENTRADA POR PANTALLA \\\\\\\\\\\\\\\\\\\\
|PROCESO AntesPantalla; |TIPO 7;
   |SI #0#14 < 15;
       aAlfa = "Recalculo Modelos 130 y 131, Minoracion por deduccion Art.80 Ley Impuesto ";
   |SINO;
       aAlfa = "Recalculo Modelos 130/131, Minoracion deduccion Art.110.3.c del Reglamento";
   |FINSI;
   |ATRI R; |PINTA 0703, aAlfa; |ATRI 0;
|FPRC;

|PROCESO ElAny;
   |HAZ AntesPantalla;
|FPRC;

|PROCESO Parrilla;
     |SI #0#0 = 0;
         #0#0 = 0;  |PINTA #0#0;
         #0#1 = 0;  |PINTA #0#1;  |C_M #0#1, 1, "N";
         |PARA nCampo = 2;  |SI nCampo [ 13;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |SINO;
         |SI enEmpresa = 0;
             |C_M #0#0, 1, "S";
             |C_M #0#1, 1, "S";
         |FINSI;

         |PARA nCampo = 2;  |SI nCampo [ 13;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "N";
               #0nCampo = 0;  |PINTA #0nCampo;
         |SIGUE;
     |FINSI;
|FPRC;

|PROCESO NoMasEmpresas;
     |SI #0Campo = 0;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 13;  |HACIENDO nCampo = nCampo + 1;
               #0nCampo = 0;  |C_M #0nCampo, 1, "N";
         |SIGUE;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 13;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |FINSI;
|FPRC;

|| ////////////////////////////////////////////////////////////////////////

|PROCESO eosm0020_neg;
     |SI #1131#47 < 0; nSwNeg = 1; |FINSI;

     |SI #1131#47 ] 0;  |Y nSwNeg = 0; |ACABA;  |FINSI;

     nSumaD = nSumaD + #1131#55;

     |SI #1131#47 < 0;
         #131#56 = #131#56 + -#1131#47;
     |SINO;
         #131#56 = #131#56 - #1131#55;
         |SI #131#56 [ 0; nSwNeg = 0; #131#56 = 0; nSumaD = 0; |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE eosm0020_neg;
     #1131#1003;
     ;
     #131#0, #131#1,     1;
     #131#0, #131#1, nHPer;
     ;
     NULL, eosm0020_neg;
|FIN;

|PROCESO CapturaNegativos131;
     #131#56 = 0;
     |SI #131#55 [ 0;  |ACABA;  |FINSI;

     |DBASE aBase;  |QBF aBase;
     aMas = aBase + "def/eosm0020.mas";
     |CARGA_DEF aMas, eosm0020@;

     nSwNeg   = 0;
     nSumaD   = 0;
     #131#56    = 0;
     nHPer    = #131#2 - 1;
     |BUCLE eosm0020_neg;

     |DESCARGA_DEF #eosm0020@;
|FPRC;

|PROCESO eosm0020Tope;
     nTope131 = nTope131 + #1131#61;
|FPRC;

|DEFBUCLE eosm0020Tope;
     #1131#1003;
     ;
     #131#0, #131#1,     1;
     #131#0, #131#1, nHPer;
     ;
     NULL, eosm0020Tope;
|FIN;

|PROCESO CapturaTope;
     |DBASE aBase;  |QBF aBase;
     aMas = aBase + "def/eosm0020.mas";
     |CARGA_DEF aMas, eosm0020@;

     nHPer    = #131#2 - 1;
     nTope131 = 0;
     |BUCLE eosm0020Tope;

     |DESCARGA_DEF #eosm0020@;
|FPRC;

|PROCESO CalculaDed131;
     #131#55 = #131#53 - #131#54 - #131#59;

     |SI #131#55 [ 0;
         #131#56 = 0;
         #131#61 = 0;
         |ACABA;
     |FINSI;

     |SI nEjer4 ] 2008;  |Y #131#2 > 1;
         |HAZ CapturaNegativos131;
     |FINSI;

     nSuma = #131#56 + #131#61;
     |SI nSuma > #131#55;
         |SI #131#56 > #131#55;
             #131#56 = #131#55;
             #131#61 = 0;
             |ACABA;
         |FINSI;

         #131#61 = #131#55 - #131#56;
     |FINSI;
|FPRC;

|PROCESO RecalModelo_131;

    |DDEFECTO #1000;
    #1000#0 = #131#0;
    |LEE 041#1000=;

     |SI #131#1 ] 80;
         nEjer4 = 1900 + #131#1;
     |SINO;
         nEjer4 = 2000 + #131#1;
     |FINSI;

     nEmpresa  = #131#0;
     nEjer     = #131#1;
     nPeriodo  = #131#2;

     |SI #131#4 = "S"; |ACABA; |FINSI; ||no modifica

     #131#40 = 0;
     #131#60 = 0;
     |SI #131#6  ! "S";  #131#40 = #131#40 + #131#11;  #131#60 = #131#60 + #131#9;  |FINSI;
     |SI #131#13 ! "S";  #131#40 = #131#40 + #131#18;  #131#60 = #131#60 + #131#16; |FINSI;
     |SI #131#20 ! "S";  #131#40 = #131#40 + #131#25;  #131#60 = #131#60 + #131#23; |FINSI;
     |SI #131#27 ! "S";  #131#40 = #131#40 + #131#32;  #131#60 = #131#60 + #131#30; |FINSI;
     |SI #131#34 ! "S";  #131#40 = #131#40 + #131#39;  #131#60 = #131#60 + #131#37; |FINSI;

     enMod130 = 131;
     enEmp130 = #131#0;
     enEje130 = #131#1;
     enPer130 = #131#2;
     enRend_NoAgr = #131#60;
     enRend_Agr   = #131#43;
     eaNombreAp   = #1000#2;
     |HAZ eLeeAplica;

     #131#59 = 0;
     |SI nEjer4 = 2008; |Y eaAplica1 ! "N";
         |SI #131#2 = 2;  #131#59 = 200;  |FINSI;
         |SI #131#2 = 3;  #131#59 = 100;  |FINSI;
         |SI #131#2 = 4;  #131#59 = 100;  |FINSI;
     |FINSI;

     |SI nEjer4 = 2009; |Y eaAplica1 ! "N";
         |SI #131#2 = 1;  #131#59 = 100;  |FINSI;
         |SI #131#2 = 2;  #131#59 = 100;  |FINSI;
         |SI #131#2 = 3;  #131#59 = 100;  |FINSI;
         |SI #131#2 = 4;  #131#59 = 100;  |FINSI;
     |FINSI;

     |SI nEjer4 ] 2010; |Y eaAplica1 ! "N";
         #131#59 = enApli400;
     |FINSI;

     #131#61 = 0;
     |SI nEjer4 ] 2009; |Y eaAplica2 = "S";
         |SI #131#40 ! 0;
             #131#61 = (#131#60 % .5) + (#131#51 % 2);
         |SINO;
             #131#61 = #131#43 % 2;
         |FINSI;

         |HAZ CapturaTope;
         |SI enAplica3 = 0;
             nTope = 660.14 - nTope131;
         |SINO;
             nTope = 440 - nTope131;
         |FINSI;
         |SI nTope < 0;  nTope = 0;  |FINSI;
         |SI #131#61 > nTope;
             #131#61 = nTope;
         |FINSI;
     |FINSI;

     |HAZ CalculaDed131;

     #131#57 = #131#55 - #131#56 - #131#61;
     #131#47 = #131#57 - #131#58;

     |GRABA 020#131a;
     |LIBERA #131;
|FPRC;

|DEFBUCLE eosm0020;
    #131#1;
    ;
    nDEmpresa, nEjer, 01;
    nHEmpresa, nEjer, 12;
    ;
    NULL, RecalModelo_131;
|FIN;


|| ---- 130 ----------------------------------------------------------------
|PROCESO eosm0130_neg;

     |SI #1130#3 = 8;  |Y #1130#2 = 1;
          nImporte = #1130#29;
     |SINO;
          nImporte = #1130#46;
     |FINSI;

     |SI nImporte < 0; nSwNeg = 1; |FINSI;

     |SI nImporte ] 0;  |Y nSwNeg = 0; |ACABA;  |FINSI;

     nSumaD = nSumaD + #1130#42;

     |SI nImporte < 0;
         #130#43 = #130#43 + -nImporte;
     |SINO;
         #130#43 = #130#43 - #1130#42;
         |SI #130#43 [ 0; nSwNeg = 0; #130#43 = 0; nSumaD = 0; |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE eosm0130_neg;
     #1130#1004;
     ;
     #130#0, #130#1,     1, #130#3;
     #130#0, #130#1, nHPer, #130#3;
     ;
     NULL, eosm0130_neg;
|FIN;

|PROCESO CapturaNegativos130;
     |SI #130#42 [ 0;  |ACABA;  |FINSI;

     |DBASE aBase;  |QBF aBase;
     aMas = aBase + "def/eosm3130.mas";
     |CARGA_DEF aMas, eosm0130@;

     nSwNeg   = 0;
     nSumaD   = 0;
     #130#43    = 0;
     nHPer    = #130#2 - 1;
     |BUCLE eosm0130_neg;

     |DESCARGA_DEF #eosm0130@;
|FPRC;

|PROCESO eosm0130Tope;
     nTopeAgrario = nTopeAgrario + #1130#47;
|FPRC;

|DEFBUCLE eosm0130Tope;
     #1130#1004;
     ;
     #130#0, #130#1,     1, #130#3;
     #130#0, #130#1, nHPer, #130#3;
     ;
     NULL, eosm0130Tope;
|FIN;

|PROCESO CapturaTopeAgrario;
     |DBASE aBase;  |QBF aBase;
     aMas = aBase + "def/eosm3130.mas";
     |CARGA_DEF aMas, eosm0130@;

     nTopeAgrario = 0;
     nHPer        = #130#2 - 1;
     |BUCLE eosm0130Tope;

     |DESCARGA_DEF #eosm0130@;
|FPRC;

|PROCESO CalculaDed130;
     |SI nEjer4 < 2008;
         #130#41 = 0;
         #130#42 = 0;
         #130#43 = 0;
         #130#44 = 0;
         #130#47 = 0;
         |ACABA;
     |FINSI;

     #130#42 = #130#29 - #130#41;

     |SI #130#42 [ 0;
         #130#43 = 0;
         #130#47 = 0;
         |ACABA;
     |FINSI;

     |SI nEjer4 = 2008;
         |SI #130#2 > 1;
             |HAZ CapturaNegativos130;
         |SINO;
             #130#41 = 0;
             #130#42 = 0;
             #130#43 = 0;
             #130#44 = 0;
             #130#47 = 0;
             |ACABA;
         |FINSI;
     |FINSI;

     |SI nEjer4 ] 2009;
         |SI #130#2 ! 1;
             |HAZ CapturaNegativos130;
         |FINSI;
     |FINSI;

     nSuma = #130#43 + #130#47;
     |SI nSuma > #130#42;
         |SI #130#43 > #130#42;
             #130#43 = #130#42;
             #130#47 = 0;
             |ACABA;
         |FINSI;

         #130#47 = #130#42 - #130#43;
     |FINSI;
|FPRC;

|PROCESO RecalModelo_130;

    |DDEFECTO #1000;
    #1000#0 = #130#0;
    |LEE 041#1000=;

     |SI #130#2 ] 80;
         nEjer4 = 1900 + #130#3;
     |SINO;
         nEjer4 = 2000 + #130#3;
     |FINSI;

     nEmpresa  = #130#0;
     nEjer     = #130#3;
     nPeriodo  = #130#2;

     |SI #130#5 = "S"; |ACABA; |FINSI; ||no modifica

     enMod130 = 130;
     enEmp130 = #130#0;
     enEje130 = #130#3;
     enPer130 = #130#2;
     enRend_NoAgr = #130#26;
     enRend_Agr   = #130#30;
     eaNombreAp   = #1000#2;
     |HAZ eLeeAplica;

     #130#41 = 0; #130#42 = 0;
     #130#43 = 0; #130#44 = 0;

     |SI nEjer4 = 2008; |Y eaAplica1 ! "N";
         |SI #130#2 = 2;  #130#41 = 200;  |FINSI;
         |SI #130#2 = 3;  #130#41 = 100;  |FINSI;
         |SI #130#2 = 4;  #130#41 = 100;  |FINSI;
     |FINSI;

     |SI nEjer4 = 2009; |Y eaAplica1 ! "N";
         |SI #130#2 = 1;  #130#41 = 100;  |FINSI;
         |SI #130#2 = 2;  #130#41 = 100;  |FINSI;
         |SI #130#2 = 3;  #130#41 = 100;  |FINSI;
         |SI #130#2 = 4;  #130#41 = 100;  |FINSI;
     |FINSI;

     |SI nEjer4 ] 2010; |Y eaAplica1 ! "N";
         #130#41 = enApli400;
     |FINSI;

     #130#47 = 0;
     |SI nEjer4 ] 2009; |Y eaAplica2 = "S";
         |SI #130#22 > 0;
             #130#47 = #130#22 % 2;
             |SI enAplica3 = 0;
                 |SI #130#47 > 660.14;
                     #130#47 = 660.14;
                 |FINSI;
             |SINO;
                 |SI #130#47 > 440;
                     #130#47 = 440;
                 |FINSI;
             |FINSI;
         |SINO;
             |SI #130#30 > 0;
                 #130#47 = #130#30 % 2;
                 |HAZ CapturaTopeAgrario;
                 |SI enAplica3 = 0;
                     nTope = 660.14 - nTopeAgrario;
                 |SINO;
                     nTope = 440 - nTopeAgrario;
                 |FINSI;
                 |SI nTope < 0;  nTope = 0;  |FINSI;
                 |SI #130#47 > nTope;
                     #130#47 = nTope;
                 |FINSI;
             |FINSI;
         |FINSI;
     |FINSI;

     |HAZ CalculaDed130;

     #130#44 = #130#42 - #130#43 - #130#47;
     #130#46 = #130#44 - #130#45;   ||nuevo calculo Ley 80

     |GRABA 020#130a;
     |LIBERA #130;
|FPRC;

|DEFBUCLE eosm3130;
    #130#1;
    ;
    nDEmpresa, 0, 01, nEjer;
    nHEmpresa, 9, 99, nEjer;
    ;
    NULL, RecalModelo_130;
|FIN;


|PROCESO Tipo3;  |TIPO 3;
     |ABRE #1000;
     nEjer = #0#14;
     |SI #0#0 ! 0;
         nDEmpresa = #0#0;
         nHEmpresa = #0#1;
         |SI #0#15 ! 1; nModelo = 130; |BUCLE eosm3130; |FINSI;
         |SI #0#15 ! 0; nModelo = 131; |BUCLE eosm0020; |FINSI;
     |SINO;
         |PARA nCampo = 2;  |SI nCampo [ 13;  |HACIENDO nCampo = nCampo + 1;
               nDEmpresa = #0nCampo;
               nHEmpresa = #0nCampo;
               |SI #0#15 ! 1; nModelo = 130; |BUCLE eosm3130; |FINSI;
               |SI #0#15 ! 0; nModelo = 131; |BUCLE eosm0020; |FINSI;
         |SIGUE;
     |FINSI;
     |CIERRA #1000;
|FPRC;

|PROGRAMA;
     aParam = PARAMETRO; |QBF aParam;

     |SI aParam ! "";
         |DDEFECTO #0;
         |SI aParam = "130"; #0#15 =0; |FINSI;
         |SI aParam = "131"; #0#15 =1; |FINSI;

         enSwApli = 4;
         |SI enElEjer ! 0;
             #0#14 = enElEjer;
             #0#0  = enDEmpresa;
             #0#1  = enHEmpresa;
         |FINSI;

         |HAZ Tipo3;
     |SINO;
          enSwApli = 0;
          |MANTE #0;
     |FINSI;
|FPRO;
