|FICHEROS;
     eosamopa #0;
     eosamorc #1;
     eosamorl #2;
     eosclien #3;
     eoscabco #4;
     eoslinco #5;
     eosclpre #6;
     eosconce #7;
     eosconc6 #8;
     eosactiv #9;
     eosw0045 #99,MANTE;
|FIN;

|VARIABLES;
     SwHay     = 0;
     SwTempo   = 0;
     XPara     = 0;
     Tecla     = "";
     Concepto2 = 0;
     Concepto6 = "";
     ConceptoN = "";
     Fichero   = "";
     VAnyo      = "";
     Linea     = 0;
     Trime     = 0;
     VAlfa1    = "";
     VAlfa2    = "";

     nNume1    = 0;
     aAlfa1    = "";
     fFecha    = @;
|FIN;

|INCLUYE i_consul;

|CALCULO eldefec; |TIPO 7;
 fFecha = ~;
 aAlfa1 = fFecha;
 aAlfa1 = aAlfa1 % -104;
 #0#33  = aAlfa1;
 |PINTA #0#33;
 aAlfa1 = aAlfa1 % -102;
 #0#34  = aAlfa1;
 |PINTA #0#34;
|FCAL;

|PROCESO Mayusculas;
  #0Campo = #0Campo > #0Campo;
  |PINTA #0Campo;
|FPRC;

|PROCESO Seleccion;
  |SI #0#0 = "S";
      |C_M #0#1, 1, "N";   #0#1 = 1;      |PINTA #0#1;
      |C_M #0#2, 1, "N";   #0#2 = 99999;  |PINTA #0#2;
      |PARA XPara = 3; |SI XPara [ 26;  |HACIENDO XPara = XPara + 1;
            |C_M #0XPara, 1, "S";
      |SIGUE;
  |SINO;
      |C_M #0#1, 1, "S";
      |C_M #0#2, 1, "S";
      |PARA XPara = 3; |SI XPara [ 26;  |HACIENDO XPara = XPara + 1;
            |C_M #0XPara, 1, "N";  #0XPara = 0;  |PINTA #0XPara;
      |SIGUE;
  |FINSI;
|FPRC;

|| ************************ Proceso para cargar caracteres en impresoras

|PROCESO Concepto;
  |SI Campo = 30;
      |SI #0Campo ! 0;
          |ABRE #7;
          #7#0 = #0Campo;
          |LEE 040#7=;
          |SI FSalida ! 0;
              |MENAV "      No Exite el Concepto.";
              |ERROR;
          |FINSI;
          |CIERRA #7;
      |FINSI;
  |FINSI;

  |SI Campo = 31;
      |SI #0Campo ! "      ";
          |ABRE #8;
          #8#0 = #0Campo;
          |LEE 040#8=;
          |SI FSalida ! 0;
              |MENAV "      No Exite el Concepto.";
              |ERROR;
          |FINSI;
          |CIERRA #8;
      |FINSI;
  |FINSI;
|FPRC;

|PROCESO LeeConce;
  |ABRE #9;
  #9#0 = #1#0;
  #9#1 = #1#16;
  #9#2 = #1#1;
  |LEE 040#9=;
  |SI FSalida ! 0; |DDEFECTO #9; |FINSI;
  |CIERRA #9;

  |SI #9#50 = 1;
      |ABRE #7;
      #7#0 = Concepto2;
      |LEE 040#7=;
      |SI FSalida ! 0; |DDEFECTO #7; |FINSI;
      |CIERRA #7;
      ConceptoN = #7#1;
  |SINO;
      |ABRE #8;
      #8#0 = Concepto6;
      |LEE 040#8=;
      |SI FSalida ! 0; |DDEFECTO #8; |FINSI;
      |CIERRA #8;
      ConceptoN = #8#1;
  |FINSI;
|FPRC;

|PROCESO Trimestre;
  |SI VAlfa2 = "01"; |O VAlfa2 = "02"; |O VAlfa2 = "03";  Trime = 1;  |FINSI;
  |SI VAlfa2 = "04"; |O VAlfa2 = "05"; |O VAlfa2 = "06";  Trime = 2;  |FINSI;
  |SI VAlfa2 = "07"; |O VAlfa2 = "08"; |O VAlfa2 = "09";  Trime = 3;  |FINSI;
  |SI VAlfa2 = "10"; |O VAlfa2 = "11"; |O VAlfa2 = "12";  Trime = 4;  |FINSI;
|FPRC;

|PROCESO LeeProveedor;
  |ABRE #6;
  #6#0 = #5#0;
  #6#2 = 2;
  #6#1 = #5#3;
  |LEE 040#6=;
  |SI FSalida ! 0; |DDEFECTO #6; |FINSI;
  |CIERRA #6;
|FPRC;

|PROCESO CreaFichero;
  Fichero = #4#0;
  Fichero = ("00000" +  Fichero) % -105;
  VAnyo   = #4#4;
  VAnyo   = ("00" + VAnyo) % -102;
  Fichero = "c" + Fichero + VAnyo;
  |NOME_DAT #5Fichero;

  Linea = 0;
  |ABRE #5;
  #5#0  = #4#0;
  #5#1  = 32000;
  #5#14 = #4#4;
  |LEE 000#5];
  |SI FSalida = 0;
      |LEE 000#5a;
  |SINO;
      |LEE 000#5u;
  |FINSI;

  |SI FSalida = 0;|Y #5#0 = #4#0; |Y #5#14 = #4#4;
      Linea = #5#1 + 1;
  |SINO;
      Linea = 1;
  |FINSI;
  |CIERRA #5;
|FPRC;

|PROCESO GrabaProveedor;
  |ABRE #6;
  #6#0 = #5#0;
  #6#2 = 2;
  #6#1 = #5#3;
  |LIBERA #6;
  |LEE 110#6=;
  |SI FSalida = 0;
      |SI #5#9 ! 0;
          #6#17 = #6#17 + (#5#7 + #5#9 + #5#11 - #5#42);
      |SINO;
          #6#16 = #6#16 + #5#7;
      |FINSI;
      |GRABA 020#6a;
  |FINSI;
  |LIBERA #6;
  |CIERRA #6;
|FPRC;

|PROCESO GrabaLinea;
  |HAZ CreaFichero;

  |DDEFECTO #5;
  |ABRE #5;
  #5#0 = #2#0;
  #5#1 = Linea;
  #5#2 = 2;
  |SI #2#6 = "A";
      Concepto2 = #0#30;
      Concepto6 = #0#31;
      |HAZ LeeConce;

      #5#3  = #99#4;
      #5#5  = #0#30;
      #5#72 = #0#31;
      #5#7  = #2#11;
      #5#8  = 0;
      #5#9  = 0;
  |SINO;
      Concepto2 = #1#9;
      Concepto6 = #1#17;
      |HAZ LeeConce;

      #5#3  = #1#8;
      #5#5  = #1#9;
      #5#72 = #1#17;
      #5#7  = #2#8;
      #5#8  = #2#9;
      #5#9  = #2#10;
  |FINSI;
  VAlfa1 = #2#5;
  VAlfa1 = VAlfa1 % 102;
  VAlfa2 = #2#5;
  VAlfa2 = VAlfa2 % 402;

  #5#35  = ConceptoN;
  #5#20  = "N";
  #5#21  = 1;
  #5#22  = #5#7 + #5#9;
  #5#4   = VAlfa1 + VAlfa2;
  #5#6   = #2#7;
  #5#10  = 0;
  #5#11  = 0;
  #5#13  = #2#5;
  #5#14  = #2#4;
  #5#15  = "";
  #5#16  = 0;

  |HAZ Trimestre;

  #5#17  = Trime;
  #5#18  = "N";
  #5#19  = #1#16;

  |HAZ LeeProveedor;

  #5#12  = #6#3;
  #5#38  = #6#4;

  |GRABA 041#5n;
  |CIERRA #5;
  |HAZ GrabaProveedor;
|FPRC;

|PROCESO GrabaCabecera;
  |SI #2#4 ! #0#34; |ACABA; |FINSI;
  |SI #2#12 ! "N";  |ACABA;  |FINSI;

  |ABRE #4;
  #4#0 = #2#0;
  #4#4 = #2#4;
  |LIBERA #4;
  |LEE 110#4=;
  |SI FSalida = 0;
      |SI #2#6 = "C";
          #4#5 = #4#5 + #2#8;
          #4#6 = #4#6 + #2#10;
      |SINO;
          #4#5 = #4#5 + #2#11;
      |FINSI;
       #4#8 = #4#5 + #4#6 + #4#7 - #4#9;
      |GRABA 020#4a;
      |LIBERA #4;
      |HAZ GrabaLinea;
  |SINO;

      #3#0 = #1#0;
      |LEE 040#3=;
      |SI FSalida ! 0;  |DDEFECTO #3;  |FINSI;

      |DDEFECTO #4;
      #4#0 = #2#0;
      #4#1 = #3#2;
      #4#2 = #3#1;
      #4#3 = 2;
      #4#4 = #2#4;
      |SI #2#6 = "C";
          #4#5 = #4#5 + #2#8;
          #4#6 = #4#6 + #2#10;
          #4#8 = #4#8 + #4#5 + #4#6;
      |SINO;
          #4#5 = #4#5 + #2#11;
          #4#8 = #4#8 + #4#5;
      |FINSI;
      |GRABA 020#4n;
      |LIBERA #4;
      |HAZ GrabaLinea;
  |FINSI;
  |CIERRA #4;

  #2#12 = "S";
  |GRABA 020#2a;
  |LIBERA #2;
|FPRC;

|PROCESO Lineas;
  |BUCLELIN 0GrabaCabecera#1;
|FPRC;

|DEFBUCLE eosamorcS;
  #1#1;
  ;
  #99#0, #99#1, #99#2;
  #99#0, #99#1, #99#2;
  ;
  NULL, Lineas;
|FIN;

|| ****************************** Procesos Preparacion de la Grabacion

|PROCESO PreparaPase;
  |BUCLE eosamorcS;
|FPRC;

|DEFBUCLE eosw0045;
  #99#1;
  7;
  00000, 00, 00000, "S";
  99999, 99, 99999, "S";
  ;
  NULL, PreparaPase;
|FIN;

|| ****************************** Procesos Grabacion del Temporal

|PROCESO MiraSiHay;
  |SI #2#4 ! #0#34; |ACABA; |FINSI;
  |SI #2#12 = "N";  SwHay = 1;  |FINSI;
|FPRC;

|PROCESO GrabaTempo;
  SwHay = 0;
  |BUCLELIN 0MiraSiHay#1;

  |SI SwHay = 0;  |ACABA;  |FINSI;

  #3#0 = #1#0;
  |LEE 040#3=;
  |SI FSalida ! 0;  |DDEFECTO #3;  |FINSI;

  #99#0 = #1#0;               || Empresa
  #99#1 = #1#1;               || A¤o
  #99#2 = #1#2;               || Elemento
  #99#3 = #1#3;               || Nombre Elemento
  #99#4 = 0;
  #99#5 = #3#2;
  #99#6 = "";
  #99#7 = "S";
  |GRABA 140#99n;
  |LIBERA #99;

  SwTempo = 1;
|FPRC;

|DEFBUCLE eosamorcP;
  #1#1;
  ;
  #0#1, #0#27, #0#28;
  #0#2, #0#27, #0#29;
  ;
  NULL, GrabaTempo;
|FIN;

|RUTINA ClaveBaja;
  #99#0 = 00001;
  #99#1 = 00;
  #99#2 = 0;
|FRUT;

|RUTINA ClaveAlta;
  #99#0 = 99999;
  #99#1 = 99;
  #99#2 = 99999;
|FRUT;

|PROCESO Tipo3; |TIPO 3;
  Fichero = Usuario % 808;  |QBT Fichero;
  Fichero = ("L8" + Fichero + "zzzzzzzz") % 108;
  |NOME_DAT #99 Fichero;
  |ABRE #99;  |CIERRA #99;  |DELINDEX #99;

  SwTempo = 0;

  |ABRE #99;
  |ABRE #3;
  |SI #0#0 = "S";
      |PARA XPara = 3; |SI XPara [ 26;  |HACIENDO XPara = XPara + 1;
            |SI #0XPara ! 0;
                #0#1 = #0XPara;
                #0#2 = #0XPara;
                |PARA nNume1 = #0#32; |SI nNume1 [ #0#33; |HACIENDO nNume1 = nNume1 + 1;
                      aAlfa1 = nNume1;
                      aAlfa1 = aAlfa1 % -102;
                      #0#27 = aAlfa1;
                      |BUCLE eosamorcP;
                |SIGUE;
             |FINSI;
      |SIGUE;
  |SINO;
      |PARA nNume1 = #0#32; |SI nNume1 [ #0#33; |HACIENDO nNume1 = nNume1 + 1;
            aAlfa1 = nNume1;
            aAlfa1 = aAlfa1 % -102;
            #0#27 = aAlfa1;
            |BUCLE eosamorcP;
      |SIGUE;
  |FINSI;
  |CIERRA #99;
  |CIERRA #3;

  |SI SwTempo = 0;
      |MENAV "    Seleccion Vacia ... ";
      |ABRE #99;  |CIERRA #99;  |DELINDEX #99;
      |ACABA;
  |FINSI;

  |ENTLINEAL #1#99, -3, 4, 20, 1, ClaveBaja, ClaveAlta;
  |CONFI "    SRealizar el Pase S/N";
  |SI FSalida = 0;
      |ABRE #3;
      |BUCLE eosw0045;
      |CIERRA #3;
  |FINSI;
  |DELINDEX #99;
|FPRC;

|| ****************************** Procesos del Temporal

|PROCESO Tipo11;  |TIPO 11;
  |SI FSalida ! 9;  |Y FSalida ! 10;  |ACABA;  |FINSI;

  Tecla = FSalida;

  |LIBERA #99;
  |LEE 140#99=;

  |SI Tecla = 9;
        |SI #99#7 = "N";
            #99#7 = "S";
        |SINO;
            #99#7 = "N";
            #99#4 = 0;
        |FINSI;
  |FINSI;

  |SI Tecla = 10;
      #99#7 = "S";
      |C_M #99#4, 1, "S";
      |ENCAMPO #4#99;
  |FINSI;

  |PINTA #99#4;
  |PINTA #99#7;

  |GRABA 140#99a;

  |C_M #99#4, 1, "N";
|FPRC;

|PROCESO MiraProve;  |TIPO 0;
  |SI FSalida ! 9;  |ACABA;  |FINSI;
  Campo10020 = #99#0;
  Campo10022 = 2;
  |HAZ ConsuCliProv;
  #99#4 = Campo10021; |PINTA #99#4;
|FPRC;

|PROCESO PonProve;  |TIPO 0;
  #99#6 = #6#4;   |PINTA #99#6;
  |SI #99#4 = 0;  |ACABA;  |FINSI;

  |ABRE #6;
  #6#0 = #99#0;
  #6#2 = 2;
  #6#1 = #99#4;
  |LEE 040#6=;
  |SI FSalida ! 0;
      #99#6 = #6#4;   |PINTA #99#6;
      |CIERRA #6;
      |MENAV "     No Existe Proveedor";
      |ERROR;
      |ACABA;
  |FINSI;
  |CIERRA #6;

  #99#6 = #6#4;   |PINTA #99#6;
|FPRC;
