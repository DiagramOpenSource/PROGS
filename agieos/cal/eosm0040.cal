|FICHEROS;
     eosm0040 #30;
     eosm0045 #29;
     eosm0041 #31;
     eosm0042 #32;
     eosm0043 #33;
     eosm0044 #34;

     eosaplic #100;
     eosclipr #103;
     :/agicli/def/agicontr #101;
|FIN;

|VARIABLES;
     nCampo       = 0;
     nSwBorra     = 0;
     nModo        = 0;
     nAcumulado   = 0;
     nEjerAnt     = 0;

     aAlfa        = "";
     aPathFich    = "";
     aPathPan     = "";

     &enEmpresa   = 0;
     &enEjercic   = 0;
     &enAnyo      = 0;
     &enTipo      = 0;
     &eaUltimo    = "";

 {-1} aMenu     = "";
      aMenu1    = "1620";
      aMenu2    = "1";
      aMenu3    = "6";
      aMenu4    = "Recogida de Datos Externos                  ";
      aMenu5    = "Entrada por Arrendadores                    ";
      aMenu6    = "Calculo Modelo 115 y 180 desde Arrendadores ";
      aMenu7    = "Mantenimiento 115                           ";
      aMenu8    = "Mantenimiento 180                           ";
      aMenu9    = "Emisiones                                   ";

 {-1} aMenuCal  = "";
      aMenuCal1 = "1830";
      aMenuCal2 = "1";
      aMenuCal3 = "2";
      aMenuCal4 = "Calculo del 115                             ";
      aMenuCal5 = "Calculo del 115 Ultimo Periodo y Modelo 180 ";

 {-1} aMenuPas  = "";
      aMenuPas1 = "1950";
      aMenuPas2 = "1";
      aMenuPas3 = "3";
      aMenuPas4 = "Pase desde Libros a Arrendadores   ";
      aMenuPas5 = "Traerse datos Ejercicio Anterior   ";
      aMenuPas6 = "Recoger Ficha de Arrendadores      ";

 {-1} aMenuEmi  = "";
      aMenuEmi1 = "1750";
      aMenuEmi2 = "1";
      aMenuEmi3 = "3";
      aMenuEmi4 = "Comprobacion Arrendadores   ";
      aMenuEmi5 = "Emision 115                 ";
      aMenuEmi6 = "Emision 180                 ";
      aMenuEmi7 = "Certificados                ";

      &PerDin = 0;
      &RetDin = 0;
      &PerEsp = 0;
      &RetEsp = 0;
      &NumPer = 0;
      Dni = "";
      Cont = 0;
      nReg = 0;
      nNoPaso = 0;
|FIN;

|PROCESO TipoDeclaracion;
     |PARA nCampo = 4;  |SI nCampo [ 13;  |HACIENDO nCampo = nCampo + 1;
           |C_M #30nCampo, 1, "S";
     |SIGUE;
     |SI #30#3 = " ";
         |PARA nCampo = 4;  |SI nCampo [ 13;  |HACIENDO nCampo = nCampo + 1;
               |C_M #30nCampo, 1, "N";
               #30nCampo = "";
               |PINTA #30nCampo;
         |SIGUE;
     |FINSI;
|FPRC;

|PROCESO PasaEjer;
     #31#0 = #32#0;
     #31#1 = #32#1;
     #31#2 = #32#2;
     #31#4 = #32#11;
     |LEE 040#31=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     #32#1 = #30#1;
     |SI nAcumulado ! 0;
         |PDEFECTO #32, 3, 9;
     |FINSI;

     |GRABA 020#32n;
     |LIBERA #32;

     #32#1 = nEjerAnt;
     |LEE 101#32=;
|FPRC;

|DEFBUCLE eosm0042;
     #32#1;
     ;
     #30#0, nEjerAnt, "            ", 01,    1;
     #30#0, nEjerAnt, "zzzzzzzzzzzz", 99, 9999;
     be;
     NULL, PasaEjer;
|FIN;

|PROCESO EjerAnterior;
     |ABRE #32;
     #32#0  = #30#0;
     #32#1  = #30#1;
     #32#2  = "            ";
     #32#9  = 1;
     #32#11 = 1;
     |LEE 040#32];
     |SI FSalida = 0;  |Y #32#0 = #30#0;  |Y #32#1 = #30#1;
         |MENAV "       Ya existe datos en los Arrendadores";
         |CIERRA #32;
         |ACABA;
     |FINSI;

     |CONFI "2400SDesea respetar los acumulados : ";
     nAcumulado = FSalida;

     nEjerAnt = #30#1 - 1;

     |ABRE #31;
     |BUCLE eosm0042;
     |CIERRA #31;
     |CIERRA #32;
|FPRC;

|PROCESO MenuPas;
     |PUSHV 0501 2380;
     |PARA; |SI; |HACIENDO;
            |MENUG aMenuPas;
            aMenuPas2 = FSalida;

            |SI aMenuPas2 < "1";  |O aMenuPas2 > "3";  |SAL;  |FINSI;

            |LIBERA #30;
            |SI aMenuPas2 = "1";  |SUB_EJECUTA "eoszp026";  |FINSI;
            |SI aMenuPas2 = "2";  |HAZ EjerAnterior;        |FINSI;
            |SI aMenuPas2 = "3";  |HAZ PaseArrendadores;    |FINSI;
            |LEE 101#30=;
     |SIGUE;
     |POPV;
|FPRC;

|PROCESO MenuCal;
     |PUSHV 0501 2380;
     |PARA; |SI; |HACIENDO;
            |MENUG aMenuCal;
            aMenuCal2 = FSalida;

            |SI aMenuCal2 < "1";  |O aMenuCal2 > "2";  |SAL;  |FINSI;

            |LIBERA #30;
            |SI aMenuCal2 = "1";  eaUltimo = "P"; |SUB_EJECUTA "eosz0012";  |FINSI;
            |SI aMenuCal2 = "2";  eaUltimo = "U"; |SUB_EJECUTA "eosz0012";  |FINSI;
            |LEE 101#30=;
     |SIGUE;
     |POPV;
|FPRC;

|PROCESO MenuEmi;
     |PUSHV 0501 2380;
     |PARA; |SI; |HACIENDO;
            |MENUG aMenuEmi;
            aMenuEmi2 = FSalida;

            |SI aMenuEmi2 < "1";  |O aMenuEmi2 > "4";  |SAL;  |FINSI;

            |LIBERA #30;
            |SI aMenuEmi2 = "1";  |SUB_EJECUTA "eosy0014";    |FINSI;
            |SI aMenuEmi2 = "2";  |SUB_EJECUTA "eosy0010";    |FINSI;
            |SI aMenuEmi2 = "3";  |SUB_EJECUTA "eosy0011";    |FINSI;
            |SI aMenuEmi2 = "4";  |SUB_EJECUTA "eosy0012";    |FINSI;
            |LEE 101#30=;
     |SIGUE;
     |POPV;
|FPRC;

|RUTINA ClaveBaja31;
     #31#0 = #30#0;
     #31#1 = #30#1;
     #31#2 = "            ";
     #31#4 = 01;
|FRUT;

|RUTINA ClaveAlta31;
     #31#0 = #30#0;
     #31#1 = #30#1;
     #31#2 = "zzzzzzzzzzzz";
     #31#4 = 99;
|FRUT;

|PROCESO GrabaArrend;
     #31#0 = #30#0;
     #31#1 = #30#1;
     #31#2 = #29#1;
     #31#4 = #29#4;
     |LEE 101#31=;
     |SI FSalida ! 0;
         #31#0 = #30#0;
         #31#1 = #30#1;
         #31#2 = #29#1;
         #31#4 = #29#4;
         |GRABA 020#31b;
     |FINSI;
     #31#3 = #29#2;
     #31#5 = #29#5;
     |GRABA 020#31a;
     |LIBERA #31;
|FPRC;

|DEFBUCLE eosm0045;
     #29#1;
     ;
     #30#0, "            ", 01;
     #30#0, "zzzzzzzzzzzz", 99;
     ;
     NULL, GrabaArrend;
|FIN;

|PROCESO PaseArrendadores;
     |ABRE #31;
     |BUCLE eosm0045;
     |CIERRA #31;
|FPRC;

|PROCESO Arrendadores;

     nNoPaso = 0;
     |ABRE #31;
     #31#0 = #30#0;
     #31#1 = #30#1;
     #31#2 = "            ";
     #31#4 = 1;
     |LEE 041#31];
     |SI FSalida = 0; |Y #31#0 = #30#0; |Y #31#1 = #30#1;
         nNoPaso = 1;
     |FINSI;

     |SI nNoPaso = 0;
        |BUCLE eosm0045;
     |FINSI;

     |CIERRA #31;

     |PUSHV 0501 2380;
     |ENTLINEAL #1#31, -3, 2, 22, 1, ClaveBaja31, ClaveAlta31;
     |POPV;
|FPRC;

|RUTINA ClaveBaja33;
     #33#0 = #30#0;
     #33#1 = #30#1;
     #33#2 = "115";
     #33#3 = 01;
     #33#4 = 0;
|FRUT;

|RUTINA ClaveAlta33;
     #33#0 = #30#0;
     #33#1 = #30#1;
     #33#2 = "115";
     #33#3 = 12;
     #33#4 = 99;
|FRUT;

|PROCESO Modelo115;
     |PUSHV 0501 2380;
     |ENTLINEAL #1#33, -4, 1, 18, 1, ClaveBaja33, ClaveAlta33;
     |POPV;
|FPRC;

|PROCESO PintaDatos;
   |PINTA 1322 , NumPer;
   |PINTA 1422 , PerDin;
   |PINTA 1522 , RetDin;
   |PINTA 1461 , PerEsp;
   |PINTA 1561 , RetEsp;
|FPRC;

|PROCESO BucleDatos;

   |SI #eosm0044#4 ! 0;
      |SI Dni ! #eosm0044#2; |O nReg ! #eosm0044#16;
          NumPer = NumPer + 1;
          Dni  = #eosm0044#2;
          nReg = #eosm0044#16;
      |FINSI;
      PerDin = PerDin + #eosm0044#4;
      RetDin = RetDin + #eosm0044#5;
   |FINSI;
   |SI #eosm0044#6 ! 0;
       |SI Dni ! #eosm0044#2; |O nReg ! #eosm0044#16;
           NumPer = NumPer + 1;
           Dni  = #eosm0044#2;
           nReg = #eosm0044#16;
       |FINSI;
       PerEsp = PerEsp + #eosm0044#6;
       RetEsp = RetEsp + #eosm0044#7;
   |FINSI;
|FPRC;

|DEFBUCLE BucleDatos;
   #eosm0044#1;
   ;
   #30#0,#30#1,"            ",0000, 01;
   #30#0,#30#1,"zzzzzzzzzzzz",9999, 99;
   ;
   NULL,BucleDatos;
|FIN;

|RUTINA ClaveBaja34;
     #34#0  = #30#0;
     #34#1  = #30#1;
     #34#2  = "            ";
     #34#3  = 0000;
     #34#16 = 01;
|FRUT;

|RUTINA ClaveAlta34;
     #34#0  = #30#0;
     #34#1  = #30#1;
     #34#2  = "zzzzzzzzzzzz";
     #34#3  = 9999;
     #34#16 = 99;
|FRUT;

|PROCESO Modelo180;
     |PUSHV 05012380;
     |PINPA #0#34;

     PerDin = 0;
     RetDin = 0;
     PerEsp = 0;
     RetEsp = 0;
     NumPer = 0;
     Dni = "";
     nReg = -1;
     |BUCLE BucleDatos;
     |HAZ PintaDatos;
     |ENTLINEAL #1#34, -3, 1, 21, 0, ClaveBaja34, ClaveAlta34;
     |POPV;
|FPRC;

|PROCESO Tipo12;  |TIPO 12;
     |GRABA 020#30a;

     aAlfa       = #30#1 % -102;
     enEmpresa   = #30#0;
     enEjercic   = #30#1;
     enAnyo      = aAlfa;
     enTipo      = 0;

     |PUSHV 0501 2380;
     |PARA; |SI; |HACIENDO;
            |MENUG aMenu;
            aMenu2 = FSalida;

            |SI aMenu2 < "1";  |O aMenu2 > "7";  |SAL;  |FINSI;

            |SI aMenu2 = "1";    |HAZ MenuPas;       |FINSI;
            |SI aMenu2 = "2";    |HAZ Arrendadores;  |FINSI;
            |SI aMenu2 = "3";    |HAZ MenuCal;       |FINSI;
            |SI aMenu2 = "4";    |HAZ Modelo115;     |FINSI;
            |SI aMenu2 = "5";    |HAZ Modelo180;     |FINSI;
            |SI aMenu2 = "6";    |HAZ MenuEmi;       |FINSI;
     |SIGUE;
     |POPV;
|FPRC;

|PROCESO Tipo8;  |TIPO 8;
     |ABRE #100;
     |LEE 040#100p;
     |SI FSalida ! 0; |DDEFECTO #100; |FINSI;
     |CIERRA #100;

     |SI #100#11 = 1; |ACABA; |FINSI;

     |ABRE #101;
     |LEE 040#101p;
     |SI FSalida ! 0; |CIERRA #101; |ACABA; |FINSI;
     |CIERRA #101;

     aPathFich = #101#8; |QBF aPathFich;
     |SI aPathFich = "*";  |ACABA;  |FINSI;
     aPathPan = (aPathFich - "fich/") + "pan/";

     |PATH_DAT #103 aPathFich;
     |PATH_PAN #103 aPathPan;
|FPRC;

|PROCESO BorraLos;
     |SI nSwBorra = 41;  |BORRA 020#31a;  |LIBERA #31;  |FINSI;
     |SI nSwBorra = 42;  |BORRA 020#32a;  |LIBERA #32;  |FINSI;
     |SI nSwBorra = 43;  |BORRA 020#33a;  |LIBERA #33;  |FINSI;
     |SI nSwBorra = 44;  |BORRA 020#34a;  |LIBERA #34;  |FINSI;
|FPRC;

|DEFBUCLE eosm0041Baja;
     #31#1;
     ;
     #30#0, #30#1, "            ", 01;
     #30#0, #30#1, "zzzzzzzzzzzz", 99;
     be;
     NULL, BorraLos;
|FIN;

|DEFBUCLE eosm0042Baja;
     #32#1;
     ;
     #30#0, #30#1, "            ", 01,    1;
     #30#0, #30#1, "zzzzzzzzzzzz", 99, 9999;
     be;
     NULL, BorraLos;
|FIN;

|DEFBUCLE eosm0043Baja;
     #33#1;
     ;
     #30#0, #30#1, "115", 01, 00;
     #30#0, #30#1, "115", 12, 99;
     be;
     NULL, BorraLos;
|FIN;

|DEFBUCLE eosm0044Baja;
     #34#1;
     ;
     #30#0, #30#1, "            ", 0000, 01;
     #30#0, #30#1, "zzzzzzzzzzzz", 9999, 99;
     be;
     NULL, BorraLos;
|FIN;

|PROCESO Tipo2m0040;  |TIPO 2;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo ! 3;  |ACABA;  |FINSI;

     |MENAV "      Se borraran todos los datos incluidos modelos.";
     |CONFI "2400NEsta seguros de borrar la Ficha : ";
     |SI FSalida ! 0;  |ERROR;  |ACABA;  |FINSI;

     nSwBorra = 41;  |BUCLE eosm0041Baja;
     nSwBorra = 42;  |BUCLE eosm0042Baja;
     nSwBorra = 43;  |BUCLE eosm0043Baja;
     nSwBorra = 44;  |BUCLE eosm0044Baja;
|FPRC;
