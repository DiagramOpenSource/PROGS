|FICHEROS;
     eosy0016 #0;
     eosclien #1;
     eosm0010 #10;
|FIN;

|VARIABLES;
     nCampoD   = 0;
     nCampo    = 0;
     nAnyo     = 0;
     nDEmpresa = 0;
     nHEmpresa = 0;

     aAlfa    = "";
     aAlfa1   = "";
     aAlfa2   = "";
     aAlfa3   = "";

     nNume    = 0;
     fFecha   = @;

     informe  = "";
     inform1  = "";
     inform2  = "";
|FIN;

|INCLUYE i_consul;

||컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
|| CALCULOS DE LA PANTALLA
||컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
|PROCESO eldefecto; |TIPO 7;
 fFecha = ~;
 aAlfa  = fFecha;
 aAlfa  = aAlfa % -104;
 nNume  = aAlfa;
 nNume  = nNume - 1;
 #0#38 = nNume; |PINTA #0#38;
|FPRC;

|PROCESO Parrilla;
     |SI #0#0 = 0;
         #0#0 = 0;  |PINTA #0#0;
         #0#1 = 0;  |PINTA #0#1;  |C_M #0#1, 1, "N";
         |PARA nCampoD = 2;  |SI nCampoD [ 37;  |HACIENDO nCampoD = nCampoD + 1;
               |C_M #0nCampoD, 1, "S";
         |SIGUE;
         |C_M #0#38, 1, "S";
     |SINO;
         |C_M #0#0,  1, "S";
         |C_M #0#1,  1, "S";
         |PARA nCampoD = 2;  |SI nCampoD [ 37;  |HACIENDO nCampoD = nCampoD + 1;
               |C_M #0nCampoD, 1, "N";
               #0nCampoD = 0;  |PINTA #0nCampoD;
         |SIGUE;
     |FINSI;
|FPRC;

|PROCESO NoMasEmpresas;
     |SI #0Campo = 0;
         |PARA nCampoD = Campo + 1;  |SI nCampoD [ 37;  |HACIENDO nCampoD = nCampoD + 1;
               #0Campo = 0;  |C_M #0nCampoD, 1, "N";
         |SIGUE;
     |SINO;
         |PARA nCampoD = Campo + 1;  |SI nCampoD [ 37;  |HACIENDO nCampoD = nCampoD + 1;
               |C_M #0nCampoD, 1, "S";
         |SIGUE;
     |FINSI;
|FPRC;

|PROCESO TipoCarta;
     |C_M #0#41, 1, "S";
     |C_M #0#42, 1, "S";
     |C_M #0#59, 1, "S";
     |C_M #0#61, 1, "S";
     |SI #0#40 ! 2;
         #0#41 = "";  |PINTA #0#41;  |C_M #0#41, 1, "N";
         #0#42 = "";  |PINTA #0#42;  |C_M #0#42, 1, "N";
         #0#59 = "";  |PINTA #0#59;  |C_M #0#59, 1, "N";
     |FINSI;
     |SI #0#40 ! 3;
         |C_M #0#61, 1, "N";
     |FINSI;
|FPRC;

|PROCESO EnCrystal; |TIPO 0;
  |C_M #0#61,0,aAlfa1;
  |SI aAlfa1 = "N"; |ACABA; |FINSI;

  |SI FSalida = 10;
      #0#61 = "eosy3016"; |PINTA #0#61;
  |FINSI;
|FPRC;

||컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
|| CALCULOS DE LA IMPRESION
||컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴

|PROCESO FechaBonita;
     aAlfa1 = #0#39 % 102;
     aAlfa2 = #0#39 % 402;
     aAlfa3 = #0#39 % 704;

     |SI aAlfa2 = "01";  aAlfa2 = "ENERO     ";  |FINSI;
     |SI aAlfa2 = "02";  aAlfa2 = "FEBRERO   ";  |FINSI;
     |SI aAlfa2 = "03";  aAlfa2 = "MARZO     ";  |FINSI;
     |SI aAlfa2 = "04";  aAlfa2 = "ABRIL     ";  |FINSI;
     |SI aAlfa2 = "05";  aAlfa2 = "MAYO      ";  |FINSI;
     |SI aAlfa2 = "06";  aAlfa2 = "JUNIO     ";  |FINSI;
     |SI aAlfa2 = "07";  aAlfa2 = "JULIO     ";  |FINSI;
     |SI aAlfa2 = "08";  aAlfa2 = "AGOSTO    ";  |FINSI;
     |SI aAlfa2 = "09";  aAlfa2 = "SEPTIEMBRE";  |FINSI;
     |SI aAlfa2 = "10";  aAlfa2 = "OCTUBRE   ";  |FINSI;
     |SI aAlfa2 = "11";  aAlfa2 = "NOVIEMBRE ";  |FINSI;
     |SI aAlfa2 = "12";  aAlfa2 = "DICIEMBRE ";  |FINSI;

     |SI #0#40 = 2;
         aAlfa = #0#59;
     |SINO;
         aAlfa = #1#13;
     |FINSI;
     |QBF aAlfa;
     |QBF aAlfa2;

     #0#55 = aAlfa + ", a " + aAlfa1 + " de " + aAlfa2 + " de " + aAlfa3;
|FPRC;

|PROCESO eosm0010;
     #0#49  = #10#6;
     #0#50  = #10#3;
     aAlfa  = #10#7;  |QBF aAlfa;
     |SI aAlfa ! "";  aAlfa = aAlfa + " ";  |FINSI;
     aAlfa = aAlfa + #10#8;  |QBF aAlfa;
     |SI #10#9  ! "    ";  aAlfa = aAlfa + ", " + #10#9;   |FINSI;
     |SI #10#11 ! "  ";    aAlfa = aAlfa + " "  + #10#11;  |FINSI;
     |SI #10#12 ! "  ";    aAlfa = aAlfa + " "  + #10#12;  |FINSI;
     #0#51 = aAlfa;
     #0#52 = (("00" + #10#13) % -102) + (("000" + #10#14) % -103);
     #0#53 = #10#15;
     #0#54 = #10#16;
     #0#57 = #10#19;
     #0#58 = #10#2;
     |SI #0#58 = "Z";  #0#58 = "X";  |FINSI;

                        #0#56 = 0;
     |SI #0#58 = "A";  #0#56 = 3005.06;  |FINSI;
     |SI #0#58 = "B";  #0#56 = 3005.06;  |FINSI;
     |SI #0#58 = "C";  #0#56 =  300.51;  |FINSI;
     |SI #0#58 = "D";  #0#56 = 3005.06;  |FINSI;
     |SI #0#58 = "E";  #0#56 = 3005.06;  |FINSI;

     #0#60 = #10#22 + #10#26;
     |SI #0#60 = 0;
         informe = inform1;
     |SINO;
         informe = inform2;
     |FINSI;

     |INFOR informe;
     |SI FSalida ! 0;  |ACABA;  |FINSI;


     |PRINT;
     |PIEINF;
     |FININF;
|FPRC;

|DEFBUCLE eosm0010;
     #10#1;
     ;
     #1#0, nAnyo, " ", "            ", " ", " ", "  ", " ", " ";
     #1#0, nAnyo, "z", "zzzzzzzzzzzz", "z", "z", "zz", "z", "z";
     ;
     NULL, eosm0010;
|FIN;

|PROCESO eosclien;
     #0#43 = #1#2;
     #0#44 = #1#1;

     aAlfa = #1#4;  |QBF aAlfa;
     |SI aAlfa ! "";  aAlfa = aAlfa + " ";  |FINSI;
     aAlfa = aAlfa + #1#5;  |QBF aAlfa;
     |SI #1#6 ! "    ";  aAlfa = aAlfa + ", " + #1#6;  |FINSI;
     |SI #1#8 ! "  ";    aAlfa = aAlfa + " "  + #1#8;  |FINSI;
     |SI #1#9 ! "  ";    aAlfa = aAlfa + " "  + #1#9;  |FINSI;
     #0#45 = aAlfa;
     #0#46 = (("00" + #1#10) % -102) + (("000" + #1#11) % -103);
     #0#47 = #1#13;
     #0#48 = #1#14;

     |BUCLE eosm0010;

|FPRC;

|DEFBUCLE eosclien;
     #1#1;
     ;
     nDEmpresa;
     nHEmpresa;
     ;
     NULL, eosclien;
|FIN;

|PROCESO Tipo3;  |TIPO 3;
     nAnyo = #0#38 - 2000;

     |SI #0#40 = 1;
         inform1 = "eosy1016";
         inform2 = "eosy1a16";
     |SINO;
         |SI #0#40 = 2;
             inform1 =  "eosy2016";
             inform2 =  "eosy2a16";
         |SINO;
             inform1 =  #0#61; |QBF inform1;
             inform2 =  inform1;
         |FINSI;
     |FINSI;
     |SI inform1 = "";
         |MENAV "    Error en el Formato de la Carta";
         |ACABA;
     |FINSI;

     |IMPRE 0;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |SI #0#1 ! 0;
         nDEmpresa = #0#0;
         nHEmpresa = #0#1;

         |BUCLE eosclien;
     |SINO;
         |PARA nCampo = 2;  |SI nCampo [ 37;  |HACIENDO nCampo = nCampo + 1;
               |SI #0nCampo ! 0;
                   nDEmpresa = #0nCampo;
                   nHEmpresa = #0nCampo;
                   |BUCLE eosclien;
               |FINSI;
         |SIGUE;
     |FINSI;

     |FININF;
     |FINIMP;
|FPRC;

|PROGRAMA;
    |ABRE #0;
    |LEE 001#0p;
    |SI FSalida ! 0;
        |DDEFECTO #0;
        |GRABA 020#0c;
    |FINSI;
    |CLS;
    |PINPA #0#0;
    |PINDA #0#0;

    |ENDATOS #1#0;
    |SI FSalida = 0;
        |GRABA 020#0a;
    |FINSI;
    |CIERRA #0;
|FPRO;
