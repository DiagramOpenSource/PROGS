|FICHEROS;
     eosem123;
     dsmoy000;
     eosma123;
     eosclien;
     eoshacie;
     eosdaban;

     t1512301;
|FIN;

|VARIABLES;
     nHEmpre  = 0;
     nDEmpre  = 0;
     nHandle1 = 0;
     nHandle2 = 0;
     nTCampo = 0;
     nSwHay  = 0;
     nHandlePortal = 0;
     nCampo        = 0;
     nCont         = 0;
     nLong         = 0;
     nComa         = 0;
     nCaracter     = 0;
     nPos          = 0;

     aAbre        = "";
     aFichero     = "";
     aAlfa        = "";
     aEnlace      = "";
     aHID         = "";
     aNDC         = "";
     aIDI         = "";
     aEJF         = "";
     aMOD         = "";
     aPRG         = "";
     aTIA         = "";
     aINI         = "";
     aFIN         = "";
     aAbrePortal  = "";
     aTipo        = "";
     aAlfa2       = "";
     aLong        = "";
     aCaracter    = "";

     aAlfa1       = "";
     aUnidad      = "";

  {1}aContiene    = "";

     &eaFOrigen          = "";
     &eaFDestino         = "";
     &eaEjer             = "";
     &eaPathEmision      = "";
     &eaPathLanza        = "";
     &eaPathExplorador   = "";
     &eaPathInternet     = "";
     &eaPathAEAT         = "";
     &eaFicheroPlano     = "";
     &eaFicheroSalid     = "";
     &eaFicheroImpre     = "";
     &eaFicheroError     = "";
     &eaOpcion           = "";
     &enEmpresa          = 0;
     &eaNomEmpresa       = "";
     &enPresentar        = 0;
     &enErrorPortal      = 0;
     &enTecla            = 0;
     &enPeriodo          = 0;
     &eaPeriodo          = "";
     &enNume             = 0;
     &eaModelos          = "";
     &eaAlfa             = "";
     &eaForzExplorador   = "";
     &eaUnidad           = "";
     &enPer              = 0;
     &enAnyo             = 0;
     &enHandlePortal     = 0;
     &eaNIF              = "";
     &enImprtMd          = 0;
     &eaVersMod          = "    ";
     &eaNIFDs            = "         ";
|FIN;

|PROCESO PresentaModelo_02;
     |SI eaOpcion = "1";
         eaNIF     = #t1512301#7;
         enImprtMd = #eosma123#26;
         |HAZ PortalPDFPeriodicos;
     |FINSI;

     |SI eaOpcion = "3";
         eaFOrigen  = eaPathEmision  + eaFicheroPlano;
         eaFDestino = eaPathInternet + eaFicheroPlano;
         |HAZ CopiaFicheroPtl;
     |FINSI;
|FPRC;

|PROCESO AbrePagina;
     eaPathAEAT = eaUnidad + "\AEAT";        |RMKDIR eaPathAEAT;
     eaPathAEAT = eaPathAEAT + "\" + #dsmoy000#0;  |RMKDIR eaPathAEAT;
     eaPathAEAT = eaPathAEAT + "\" + #dsmoy000#6;  |RMKDIR eaPathAEAT;
     eaPathAEAT = eaPathAEAT + "\";

     eaFOrigen  = eaPathEmision  + eaFicheroPlano;
     eaFDestino = eaPathAEAT + eaFicheroPlano;
     |HAZ CopiaFicheroPtl;

     |DBASE aAlfa;  |QBT aAlfa;
     eaFOrigen  = aAlfa + "hacienda/wait.exe";
     eaFDestino = eaPathAEAT + "wait.exe";
     |HAZ CopiaFicheroPtl;

     eaForzExplorador = "N";
     |HAZ Explorador;

     aAbre = eaPathAEAT + "imp" + Usuario + ".bat";
     |XABRE "WBC", aAbre, enHandlePortal;

     eaAlfa = eaPathAEAT + "wait ";
     |HAZ GrabaPortalSin;

     eaAlfa = eaPathExplorador + " " + &34;
     |HAZ GrabaPortalSin;

     eaAlfa = "https://www1.agenciatributaria.gob.es/wlpl/OV15-M123/index.zul";
     |HAZ GrabaPortalSin;

     |XCIERRA enHandlePortal;

     |RSYSTEM aAbre;

     aAlfa = eaPathExplorador > "";
     aAlfa = aAlfa - "CHROME.EXE";
     |SI FSalida ! 0;  |SLEEP 30;  |FINSI;

     aAlfa = eaPathAEAT + aFichero;
     |RFBORRA aAlfa;
     |RFBORRA aAbre;
|FPRC;

|PROCESO TDeclaracion_02;
     |ABRE #eosdaban;
     #eosdaban#0 = #eosclien#0;
     |LEE 000#eosdaban.=;
     |SI FSalida ! 0; |DDEFECTO #eosdaban; |FINSI;
     |CIERRA #eosdaban;

     |SI #eosma123#26 > 0;
         enNume = #eosma123#26;  |HAZ Conver17;  #t1512301#19 = eaAlfa;

                                 #t1512301#6 = "I";
         |SI #eosdaban#14 = "3"; #t1512301#6 = "U";  |FINSI;
         |SI #eosdaban#14 = "6"; #t1512301#6 = "G";  |FINSI;

         |SI #eosdaban#14 ! "1";
             #t1512301#22 = #eosdaban#7;
         |FINSI;
     |SINO;
         enNume = 0;  |HAZ Conver17;  #t1512301#19 = eaAlfa;
         #t1512301#6 = "N";
     |FINSI;
|FPRC;

|PROCESO BuscaNombre;
     |QBF eaAlfa;
     aAlfa1 = "";
     aAlfa2 = "";
     aLong  = eaAlfa % 0;
     nLong  = aLong;
     nComa  = 0;

     |PARA nCaracter = 1;  |SI nCaracter [ nLong;  |HACIENDO nCaracter = nCaracter + 1;
           nPos      = (nCaracter * 100) + 1;
           aCaracter = eaAlfa % nPos;

           |SI aCaracter = ",";
               nComa = 1;
           |SINO;
               |SI nComa = 0;
                   aAlfa1 = aAlfa1 + aCaracter;
               |SINO;
                   |SI aCaracter = " ";  |Y aAlfa2 = "";
                   |SINO;
                       aAlfa2 = aAlfa2 + aCaracter;
                   |FINSI;
               |FINSI;
           |FINSI;
      |SIGUE;
|FPRC;

|PROCESO Carga123_02;
     |DDEFECTO #t1512301;
     aAlfa         = #eosclien#1;  |QBF aAlfa;
     #t1512301#7   = ("000000000" + aAlfa) % -109;

     aAlfa = #eosclien#1 % 101;
     |SI aAlfa = "0"; |O aAlfa = "1"; |O aAlfa = "2"; |O aAlfa = "3";
      |O aAlfa = "4"; |O aAlfa = "5"; |O aAlfa = "6"; |O aAlfa = "7";
      |O aAlfa = "8"; |O aAlfa = "9"; |O aAlfa = "X"; |O aAlfa = "Y";
      |O aAlfa = "Z";
         eaAlfa = #eosclien#2;      |HAZ BuscaNombre;
         eaAlfa = aAlfa1;    |HAZ QuitaRaros;   #t1512301#8   = eaAlfa;
         eaAlfa = aAlfa2;    |HAZ QuitaRaros;   #t1512301#9   = eaAlfa;
     |SINO;
         eaAlfa   = #eosclien#2;  |HAZ QuitaRaros;  #t1512301#8 = eaAlfa;
                                                    #t1512301#9 = "";
     |FINSI;

     |HAZ TDeclaracion_02;

     #t1512301#10  = eaEjer;

     |SI #eosma123#29 = "M";
         #t1512301#11 = ("00" + #eosma123#2) % -102;
     |SINO;
         #t1512301#11 = #eosma123#2 + "T";
     |FINSI;

     eaAlfa  = ("00000000000000000" + #eosma123#15) % -115;    #t1512301#12 = eaAlfa;
     enNume = #eosma123#16;    |HAZ Conver17;   #t1512301#13 = eaAlfa;
     enNume = #eosma123#17;    |HAZ Conver17;   #t1512301#14 = eaAlfa;
     enNume = #eosma123#24;    |HAZ Conver17;   #t1512301#15 = eaAlfa;
     enNume = #eosma123#20 - #eosma123#25;   |HAZ Conver17;   #t1512301#16 = eaAlfa;
     enNume = #eosma123#26;   |HAZ Conver17;   #t1512301#17 = eaAlfa;
     enNume = 0;              |HAZ Conver17;   #t1512301#18 = eaAlfa;

     #t1512301#26 = &13 + &10;

     aAlfa = "<T1230" + #t1512301#10 + #t1512301#11 + "0000>";  |XGRABA nHandle2, aAlfa;
     aAlfa = "<AUX>";                                           |XGRABA nHandle2, aAlfa;
     aAlfa = " " * 70;                                          |XGRABA nHandle2, aAlfa;
     aAlfa = eaVersMod;                                         |XGRABA nHandle2, aAlfa;
     aAlfa = " " * 4;                                           |XGRABA nHandle2, aAlfa;
     aAlfa = eaNIFDs;                                           |XGRABA nHandle2, aAlfa;
     aAlfa = " " * 213;                                         |XGRABA nHandle2, aAlfa;
     aAlfa = "</AUX>";                                          |XGRABA nHandle2, aAlfa;

     aAlfa = "";
     |PARA nCampo = 1;  |SI nCampo [ 25;  |HACIENDO nCampo = nCampo + 1;
           aAlfa = #t1512301#nCampo#;
           |XGRABA nHandle2, aAlfa;
     |SIGUE;

     aAlfa = "</T1230" + #t1512301#10 + #t1512301#11 + "0000>";  |XGRABA nHandle2, aAlfa;
|FPRC;

|PROCESO eosma123_02;
     #eosclien#0 = #eosma123#0;
     |LEE 010#eosclien.=;

     #eosma123#27 = "S";
     #eosma123#28 = ~;
     |GRABA 020#eosma123.a;

     nSwHay = 1;

     |ABRE #eoshacie;
     #eoshacie#0 = #eosclien#18;
     #eoshacie#1 = #eosclien#19;
     |LEE 040#eoshacie.=;
     |SI FSalida ! 0; |DDEFECTO #eoshacie; |FINSI;
     |CIERRA #eoshacie;

     eaFicheroPlano = "130" + (("00000" + #eosclien#0) % -105) + ".txt";
     eaFicheroImpre = "130" + (("00000" + #eosclien#0) % -105) + ".pdf";
     eaFicheroError = "err" + (("00000" + #eosclien#0) % -105) + ".txt";

     aAlfa = eaPathLanza + eaFicheroPlano;  |RFBORRA aAlfa;
     aAlfa = eaPathLanza + eaFicheroImpre;  |RFBORRA aAlfa;
     aAlfa = eaPathLanza + eaFicheroError;  |RFBORRA aAlfa;

     aAbre = eaPathEmision + eaFicheroPlano;
     |XABRE "BW", aAbre, nHandle2;

     |HAZ Carga123_02;

     |XCIERRA nHandle2;

     enEmpresa    = #eosclien#0;
     eaNomEmpresa = #eosclien#1;
     |HAZ PresentaModelo_02;
|FPRC;

|DEFBUCLE eosma123_02;
     #eosma123#1;
     ;
     nDEmpre, #eosem123#40, #eosem123#39;
     nHEmpre, #eosem123#40, #eosem123#39;
     ;
     NULL, eosma123_02;
|FIN;

|PROCESO moy12302;
     |HAZ LeeUnidad;

     eaModelos  = "123";
     enPer      = #eosem123#39;
     enAnyo     = #eosem123#40;
     eaEjer     = #eosem123#40;
     enPeriodo  = #eosem123#39;

     |SI #eosem123#45 = "T";
         |SI #eosem123#39 = 1; eaPeriodo = "1T"; |FINSI;
         |SI #eosem123#39 = 2; eaPeriodo = "2T"; |FINSI;
         |SI #eosem123#39 = 3; eaPeriodo = "3T"; |FINSI;
         |SI #eosem123#39 = 4; eaPeriodo = "4T"; |FINSI;
     |SINO;
         eaPeriodo  = ("00" + enPeriodo) % -102;
     |FINSI;

     #dsmoy000#0 = eaModelos;
     #dsmoy000#6 = eaEjer;

     eaPathLanza = eaUnidad + "\MODELOS";            |RMKDIR eaPathLanza;
     eaPathLanza = eaPathLanza + "\" + eaModelos;    |RMKDIR eaPathLanza;
     eaPathLanza = eaPathLanza + "\" + eaEjer;       |RMKDIR eaPathLanza;
     eaPathLanza = eaPathLanza + "\" + eaPeriodo;    |RMKDIR eaPathLanza;
     eaPathLanza = eaPathLanza + "\";

     |DBASE eaPathEmision;
     eaPathEmision = eaPathEmision + "emisiones";       |MKDIR eaPathEmision;
     eaPathEmision = eaPathEmision + "/" + eaModelos;   |MKDIR eaPathEmision;
     eaPathEmision = eaPathEmision + "/" + eaEjer;      |MKDIR eaPathEmision;
     eaPathEmision = eaPathEmision + "/" + eaPeriodo;   |MKDIR eaPathEmision;
     eaPathEmision = eaPathEmision + "/";

     eaPathInternet = eaUnidad + "/AEAT";                |RMKDIR eaPathInternet;
     eaPathInternet = eaPathInternet + "/INTERNET";      |RMKDIR eaPathInternet;
     eaPathInternet = eaPathInternet + "/" + eaModelos;  |RMKDIR eaPathInternet;
     eaPathInternet = eaPathInternet + "/" + eaEjer;     |RMKDIR eaPathInternet;
     eaPathInternet = eaPathInternet + "/" + eaPeriodo;  |RMKDIR eaPathInternet;
     eaPathInternet = eaPathInternet + "/";

     |HAZ Botones2015;
     |SI enTecla = 0;  |ACABA;  |FINSI;

     |SI eaOpcion = "1";
         |MENAV "0000La opcion de validacion no esta activada por hacienda.";
         |ACABA;
     |FINSI;

     |ABRE #eosma123;
     |ABRE #eosclien;
     |SI #eosem123#0 = "S";
         nDEmpre = #eosem123#1;
         nHEmpre = #eosem123#2;
         |BUCLE eosma123_02;
     |SINO;
         |PARA nCont = 3; |SI nCont [ 38; |HACIENDO nCont = nCont + 1;
               nDEmpre = #eosem123#nCont#;
               nHEmpre = #eosem123#nCont#;
               |BUCLE eosma123_02;
         |SIGUE;
     |FINSI;
     |CIERRA #eosma123;
     |CIERRA #eosclien;

     |SI eaOpcion = "1";
         |HAZ SacaErrorPtl;
     |FINSI;

     |SI eaOpcion = "3";
         aAlfa =  "      Los ficheros generados estan: " + eaPathInternet + " de su PC";
         |MENAV aAlfa;

         |CONFI "0000SQuiere cargar la pagina de hacienda del modelo";
         |SI FSalida = 0;
             |HAZ AbrePagina;
         |FINSI;
     |FINSI;
|FPRC;
