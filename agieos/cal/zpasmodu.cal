|FICHEROS;
     zpasmodu #0;        || Este Def

     eosm0017 #300;      || Datos Cabecera
     eosm0018 #301;      || Datos Calculo
     eosm0019 #302;      || Mantenimiento 310
     eosm0020 #303;      || Mantenimiento 131
     eosm0021 #304;      || Desglose Trabajador
     eosm0022 #305;      || Desglose Elementos
     eoseleme #306;      || Elementos
     eoselepi #307;      || Epigrafes Modulos
     eosm0023 #308;      || Amortizaciones Cabeceras
     eosm0024 #309;      || Amortizaciones Lineas
     eosactiv #310;      || Actividades

     zbarw001 #401;      || Desglose Cabeceras
     zbarw002 #402;      || Desglose Lineas
     zbarw003 #403;      || Entrada Modulo
     zbarw004 #404;      || Liquidacion Modulo
     zbarw005 #405;      || Cabecera Amortizaciones
     zbarw006 #406;      || Lineas   Amortizaciones
|FIN;

|VARIABLES;
     nLEmpleo     = 0;
     nLResto      = 0;
     nElemen      = 0;
     nTipo        = 0;
     nAnyo19      = 0;
     nAnyoNaci    = 0;
     nActivi      = 0;
     nPara        = 0;
     nPara1       = 0;
     nPara2       = 0;
     nPara3       = 0;
     nSwPrint     = 0;
     nSwPie       = 0;
     nSwPasada    = 0;
     nResta       = 0;
     aAlfa        = "";
     nEmpresa     = 0;
     nAnyo        = 0;
     nTrimes      = 0;
     nSuma        = 0;
     nPeriodo     = 0;

     &Programa    = 2;
     &MTipo       = 0;
     &MEmpresa    = 0;
     &MActivi     = 0;
     &MAnyo       = 0;
     &MTempor     = 0;
     &aVaralfa    = "";

{210} &PunteroA   = "";
{210} &PunteroB   = "";
|FIN;

|PROCESO Empleo;
     nLEmpleo     = nLEmpleo + 1;

     aAlfa        = #402#31 % -104;
     nAnyoNaci    = aAlfa;
     nTipo        = 0;

     |SI #402#2 = 98;  nAnyo19 = 1998 - nAnyoNaci;  |FINSI;
     |SI #402#2 = 99;  nAnyo19 = 1999 - nAnyoNaci;  |FINSI;
     |SI #402#2 = 00;  nAnyo19 = 2000 - nAnyoNaci;  |FINSI;
     |SI #402#2 = 01;  nAnyo19 = 2001 - nAnyoNaci;  |FINSI;

     |SI #402#33  = "NORMAL    ";  nTipo = 4;  |FINSI;
     |SI #402#33  = "APRENDIZ  ";  nTipo = 2;  |FINSI;
     |SI nAnyo19  < 19;            nTipo = 1;  |FINSI;
     |SI #402#33  = "PRACTICAS ";  nTipo = 3;  |FINSI;

     |SI #402#4 = 50;
         |SI #402#33  = "NORMAL    ";  nTipo = 8;  |FINSI;
         |SI #402#33  = "APRENDIZ  ";  nTipo = 6;  |FINSI;
         |SI nAnyo19  < 19;            nTipo = 5;  |FINSI;
         |SI #402#33  = "PRACTICAS ";  nTipo = 7;  |FINSI;
     |FINSI;

     |SI #402#33  = "TITULAR   ";  nTipo = 9;  |FINSI;
     |SI #402#33  = "CONYUGE   ";  nTipo = 10; |FINSI;
     |SI #402#33  = "HIJO      ";  nTipo = 11; |FINSI;

     |DDEFECTO #304;
     #304#0   = #402#0;         || Empresa
     #304#1   = #402#2;         || A¤o
     #304#2   = #402#1;         || Actividad
     #304#3   = nLEmpleo;       || Linea
     #304#4   = nTipo;          || Tipo
     #304#5   = #402#35;        || IVA, IRPF o Ambos
     #304#6   = #402#6;         || Apellidos y Nombre

     |SI #402#17 = "S";
         |SI #402#12 [ 1;
             #304#7  = 8 * #402#10;         || Horas Dia
             #304#8  = 225;                 || Numero de Dias
             #304#9  = 1800;                || Horas Trabajadas
             #304#10 = 1800;                || Horas Convenio
             #304#11 = #402#12;             || Total
         |SINO;
             #304#7  = 0;                   || Horas Dia
             #304#8  = 0;                   || Numero de Dias
             #304#9  = 0;                   || Horas Trabajadas
             #304#10 = 0;                   || Horas Convenio
             #304#11 = #402#12;             || Total
         |FINSI;
     |SINO;
         #304#7  = #402#18;             || Horas Dia
         #304#8  = #402#20 / #402#18;   || Numero de Dias
         #304#9  = #402#20;             || Horas Trabajadas
         #304#10 = #402#21;             || Horas Convenio
         #304#11 = #402#12;             || Total
     |FINSI;

     |SI #402#26 = "S";
         |SI #402#12 [ 1;
             #304#12 = 8 * #402#11;         || Horas Dia
             #304#13 = 225;                 || Numero de Dias
             #304#14 = 1800;                || Horas Trabajadas
             #304#15 = 1800;                || Horas Convenio
             #304#16 = #402#13;             || Total
         |SINO;
             #304#12 = 0;                   || Horas Dia
             #304#13 = 0;                   || Numero de Dias
             #304#14 = 0;                   || Horas Trabajadas
             #304#15 = 0;                   || Horas Convenio
             #304#16 = #402#13;             || Total
         |FINSI;
     |SINO;
         #304#12 = #402#27;             || Horas Dia
         #304#13 = #402#29 / #402#27;   || Numero de Dias
         #304#14 = #402#29;             || Horas Trabajadas
         #304#15 = #402#30;             || Horas Convenio
         #304#16 = #402#13;             || Total
     |FINSI;

     #304#17 = #402#14;            || Fecha Alta
     #304#18 = #402#15;            || Fecha Baja

     |GRABA 020#304n;
     |LIBERA #304;
|FPRC;

|PROCESO Resto;
     nLResto  = nLResto+ 1;

     |DDEFECTO #305;
     #305#0   = #402#0;         || Empresa
     #305#1   = #402#2;         || A¤o
     #305#2   = #402#1;         || Actividad
     #305#3   = nLResto;        || Linea
     #305#4   = #402#4;         || Concepto
     #305#5   = #402#5;         || Nombre Concepto
     #305#6   = #402#35;        || IVA, IRPF o Ambos
     #305#7   = #402#10;        || Unidades al 1 de Enero
     #305#8   = #402#16;        || Dias
     #305#9   = #402#12;        || Total Unidades
     #305#10  = #402#11;        || Unidades al 31 de Diciembre
     #305#11  = #402#25;        || Dias
     #305#12  = #402#13;        || Total Unidades

     |ABRE #307;
     #307#0  = #401#3;
     #307#1  = #401#4;
     #307#41 = #401#5;
     |SI #401#2 ] 80;
         #307#2  = 1900 + #401#2;    || Anyo
     |SINO;
         #307#2  = 2000 + #401#2;    || Anyo
     |FINSI;
     |LEE 040#307=;
     |SI FSalida ! 0;  |DDEFECTO #307;  |FINSI;
     |CIERRA #307;

     #305#13 = 0;
     |PARA nPara = 4;  |SI nPara [ 36;  |HACIENDO nPara = nPara + 4;
           #305#13 = #305#13 + 1;
           |SI #305#4 = #307nPara;
               #306#0 = #307nPara;
               |LEE 040#306=;
               |SI FSalida = 0; |Y #306#3 = "N";
                   |SAL;
               |FINSI;
           |FINSI;
     |SIGUE;

     |GRABA 020#305n;
     |LIBERA #305;
|FPRC;

|PROCESO PasaDesglose;
     #306#0 = #402#4;
     |LEE 040#306=;
     |SI FSalida ! 0;  |DDEFECTO #306;  |FINSI;

     |SI #306#3 = "S";  |HAZ Empleo;  |FINSI;
     |SI #306#3 = "N";  |HAZ Resto;   |FINSI;
|FPRC;

|DEFBUCLE zbarw002;
     #402#1;
     ;
     #401#0, #401#1, #401#2, 00;
     #401#0, #401#1, #401#2, 99;
     ;
     NULL, PasaDesglose;
|FIN;

|PROCESO eoszp019;
     nSwPasada = 0;

     |ET zp019;
          |PARA nPara1 = 2;  |SI nPara1 [ 202;  |HACIENDO nPara1 = nPara1 + 1;
                IPunteroA = PunteroA1 <-;
                IPunteroA = IPunteroA + nPara1;
                PunteroA  = #300nPara1;
          |SIGUE;

          |PARA nPara1 = 3;  |SI nPara1 [ 114;  |HACIENDO nPara1 = nPara1 + 1;
                IPunteroB = PunteroB1 <-;
                IPunteroB = IPunteroB + nPara1;
                PunteroB  = #301nPara1;
          |SIGUE;

          MEmpresa = #300#0;
          MActivi  = #300#1;
          MAnyo    = #300#5;

          |PTEC 815;
          |PTEC 815;
          |PTEC 815;

          |SUB_EJECUTA "eoszp019";

          |PARA nPara1 = 2;  |SI nPara1 [ 202;  |HACIENDO nPara1 = nPara1 + 1;
                IPunteroA = PunteroA1 <-;
                IPunteroA = IPunteroA + nPara1;
                #300nPara1  = PunteroA;
          |SIGUE;

          |PARA nPara1 = 3;  |SI nPara1 [ 114;  |HACIENDO nPara1 = nPara1 + 1;
                IPunteroB = PunteroB1 <-;
                IPunteroB = IPunteroB + nPara1;
               #301nPara1  = PunteroB;
          |SIGUE;

          #404#0   = #301#0;              || Empresa
          #404#1   = #301#1;              || Actividad
          #404#2   = #301#2;              || A¤o
          #404#3   = 1;                   || Tipo
          |LEE 040#404=;
          |SI FSalida ! 0;  |DDEFECTO #404;  |FINSI;

          |SI #301#10 ! #404#19;  |Y #404#18 = 0; |Y #404#19 ! 0; |Y nSwPasada = 0;
              nSwPasada = 1;
              #301#4 = #301#5;
              |VETE zp019;
          |FINSI;


     #300#8  = "";
     #300#9  = "";
     #300#10 = "";

     MTipo     = 1;
     MEmpresa  = #300#0;
     MActivi   = #300#1;
     MAnyo     = #300#5;
     MTempor   = #301#63;

     |GRABA 120#300n;
     |GRABA 120#301n;

     |LIBERA #300;
     |LIBERA #301;
|FPRC;

|PROCESO PasaAmorl;
     |SI #406#6 = "V";  |ACABA;  |FINSI;
     |SI #406#6 = "C";  |ACABA;  |FINSI;

     |DDEFECTO #309;
     #309#0 = #308#0;
     #309#1 = #308#1;
     #309#2 = #308#2;
     #309#3 = #406#5;
     #309#4 = "A¥O : " + #309#3;
     #309#5 = #406#8;
     |GRABA 120#309n;
     |LIBERA #309;

     #308#21 = #308#21 + #309#5;
     #308#22 = #308#22 - #309#5;
     #308#23 = #309#3;
|FPRC;

|DEFBUCLE zbarw006;
     #406#1;
     ;
     #405#0, #405#1, #405#2, #405#3, 000;
     #405#0, #405#1, #405#2, #405#3, 999;
     ;
     NULL, PasaAmorl;
|FIN;

|PROCESO PasaAmorc;
     |DDEFECTO #308;
     #308#0  = #405#0;              || Empresa
     #308#1  = #405#1;              || Actividad
     #308#2  = #405#3;              || Elemento
     #308#3  = #405#6;              || Descripcion
     #308#4  = #405#4;              || Tipo Amortizacion
     #308#5  = #405#14;             || Nuevo o Usado
     #308#6  = #405#31;             || Sobre Base o Factura
     #308#7  = #405#9;              || Amortizacion Trimestral
     #308#8  = #405#9;              || Amortizacion Anual
     #308#9  = #405#10;             || Proveedor
     #308#12 = #405#12;             || Fecha Compra
     #308#13 = #405#13;             || Fecha Inicio
     #308#14 = #405#15;             || Importe Compra
     #308#15 = #405#16;             || % IVA Importe Compra
     #308#16 = #405#18;             || Total  Compra
     |SI #405#22 ! 0;
         #308#17 = #405#28;         || Fecha Venta
         #308#18 = #405#22;         || Importe Venta
         #308#19 = #405#23;         || % IVA Importe Venta
         #308#20 = #405#25;         || Total Venta
     |FINSI;
     #308#22 = #308#14;
     #308#25 = #405#9;              || Amortizacion Anual


     |BUCLE zbarw006;

     |GRABA 120#308n;
     |LIBERA #308;
|FPRC;

|DEFBUCLE zbarw005;
     #405#1;
     ;
     #401#0, #401#1, #401#2, 00000;
     #401#0, #401#1, #401#2, 99999;
     ;
     NULL, PasaAmorc;
|FIN;

|PROCESO CogeModelo;
  #300#191 = "N";  #300#192 = "N";  #300#193 = "N";  #300#194 = "N";
  #300#195 = "N";  #300#196 = "N";  #300#197 = "N";  #300#198 = "N";
  |ABRE #303;
  |PARA nPara3 = 1; |SI nPara3 [ 4; |HACIENDO nPara3 = nPara3 + 1;
        #303#0 = #300#0;
        #303#1 = #300#5;
        #303#2 = nPara3;
        |LEE 040#303=;
        |SI FSalida = 0; |Y #303#3 ! "01.01.0000";
            |PARA nPara = 5;  |SI nPara [ 33;  |HACIENDO nPara = nPara + 7;
                  |SI #300#1 = #303nPara;
                      nPara2 = nPara + 6;
                      |SI nPara3 = 1; #300#118 = #303nPara2; #300#191 = "S"; |FINSI;
                      |SI nPara3 = 2; #300#119 = #303nPara2; #300#192 = "S"; |FINSI;
                      |SI nPara3 = 3; #300#120 = #303nPara2; #300#193 = "S"; |FINSI;
                      |SI nPara3 = 4; #300#121 = #303nPara2; #300#194 = "S"; |FINSI;
                  |FINSI;
             |SIGUE;
       |FINSI;
  |SIGUE;
  |CIERRA #303;

  |ABRE #302;
  |PARA nPara3 = 1; |SI nPara3 [ 4; |HACIENDO nPara3 = nPara3 + 1;
        #302#0 = #300#0;
        #302#1 = #300#5;
        #302#2 = nPara3;
        |LEE 040#302=;
        |SI FSalida = 0; |Y #302#3 ! "01.01.0000";
            |PARA nPara = 5;  |SI nPara [ 33;  |HACIENDO nPara = nPara + 5;
                  |SI #300#1 = #302nPara;
                      nPara1 = nPara + 4;
                      |SI nPara3 = 1; #300#124 = #302nPara1; #300#195 = "S"; |FINSI;
                      |SI nPara3 = 2; #300#125 = #302nPara1; #300#196 = "S"; |FINSI;
                      |SI nPara3 = 3; #300#126 = #302nPara1; #300#197 = "S"; |FINSI;
                      |SI nPara3 = 4; #300#127 = #302nPara1; #300#198 = "S"; |FINSI;
                  |FINSI;
             |SIGUE;
       |FINSI;
  |SIGUE;
  |CIERRA #302;
|FPRC;

|PROCESO PasaModulo;
     #403#0   = #401#0;              || Empresa
     #403#1   = #401#1;              || Actividad
     #403#2   = #401#2;              || A¤o
     |LEE 040#403=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     nLEmpleo = 0;
     nLResto  = 0;
     nElemen  = 0;

     |ABRE #304;
     |ABRE #305;
     |ABRE #306;
     |BUCLE zbarw002;
     |CIERRA #304;
     |CIERRA #305;
     |CIERRA #306;

     |DDEFECTO #300;
     #300#0   = #401#0;              || Empresa
     #300#1   = #401#1;              || Actividad
     #300#2   = #401#3;              || Epigrafe 1
     #300#3   = #401#4;              || Epigrafe 2
     #300#4   = #401#5;              || Tipo
     #300#5   = #401#2;              || A¤o

     #310#0 = #300#0;
     #310#1 = #300#1;
     #310#2 = #300#5;
     |LEE 040#310=;
     |SI FSalida ! 0;  |DDEFECTO #310;  |FINSI;

     #300#6   = #310#6;              || Nombre Actividad
     #300#7   = #310#3;              || Nombre Cliente
     #300#199 = "S";                 || Desglose de Trabajadores
     #300#200 = "S";                 || Desglose de Elementos

     #300#129 = #403#8;
     #300#130 = #403#9;
     #300#131 = #403#10;
     #300#132 = #403#11;
     #300#128 = #300#129 + #300#130 + #300#131 + #300#132;

     #300#134 = #403#8;
     #300#135 = #403#9;
     #300#136 = #403#10;
     #300#137 = #403#11;
     #300#133 = #300#134 + #300#135 + #300#136 + #300#137;

     |DDEFECTO #301;
     #301#0   = #401#0;              || Empresa
     #301#1   = #401#1;              || Actividad
     #301#2   = #401#2;              || A¤o

     #404#0   = #401#0;              || Empresa
     #404#1   = #401#1;              || Actividad
     #404#2   = #401#2;              || A¤o
     #404#3   = 1;                   || Tipo
     |LEE 040#404=;
     |SI FSalida ! 0;  |DDEFECTO #404;  |FINSI;

     |SI #404#20 ! 0;
         #301#13 = (#404#20 * 100) / 5;
         #301#14 = 5;
         #301#15 = #404#20;                                  || Amortizacion
     |FINSI;

     |SI #404#23 ! 0;
         #301#42 = #404#23;                              || Quioscos
         #301#46 = #404#23;                              || Taxis
         #301#50 = #404#23;                              || Urbano
         #301#54 = #404#23;                              || Mercancias
     |FINSI;

     |SI #404#25 ! 0;                                    || Peque¤a Dimension
         #301#58 = #404#25;
     |FINSI;

     |SI #404#27 ! 0;
         #301#62 = #300#128;                             || Dias Temporada
         #301#63 = #404#27;                              || Temporada
     |FINSI;

     |SI #404#29 ! 0;                                    || Exceso
         #301#69 = #404#29;
     |FINSI;

     |SI #404#31 ! 0;                                    || Nueva Actividad
         #301#73 = #404#31;
     |FINSI;

     |SI #404#33 ! 0;                                   || Reduccion L.66/97
         #301#93 = #404#33;
     |FINSI;

     |SI #404#73 ! 0;
         #301#79 = #404#73;                              || Gastos
     |FINSI;

     #404#0   = #401#0;              || Empresa
     #404#1   = #401#1;              || Actividad
     #404#2   = #401#2;              || A¤o
     #404#3   = 31;                  || Tipo
     |LEE 040#404=;
     |SI FSalida ! 0;  |DDEFECTO #404;  |FINSI;

     |SI #404#20 ! 0;
         #301#27 = (#404#20 * 100) / 5;
         #301#28 = 5;
         #301#29 = #404#20;                                  || Amortizacion
     |FINSI;

     |SI #404#23 ! 0;
         #301#44 = #404#23;                              || Quioscos
         #301#48 = #404#23;                              || Taxis
         #301#52 = #404#23;                              || Urbano
         #301#56 = #404#23;                              || Mercancias
     |FINSI;

     |SI #404#25 ! 0;                                    || Peque¤a Dimension
         #301#60 = #404#25;
     |FINSI;

     |SI #404#27 ! 0;
         #301#65 = #300#128;                             || Dias Temporada
         #301#66 = #404#27;                              || Temporada
     |FINSI;

     |SI #404#29 ! 0;                                    || Exceso
         #301#71 = #404#29;
     |FINSI;

     |SI #404#31 ! 0;                                    || Nueva Actividad
         #301#75 = #404#31;
     |FINSI;

     |SI #404#33 ! 0;                                   || Reduccion L.66/97
         #301#95 = #404#33;
     |FINSI;

     |SI #404#73 ! 0;
         #301#80 = #404#73;                              || Gastos
     |FINSI;

     |ABRE #308;
     |ABRE #309;
     |BUCLE zbarw005;             || Amortizaciones
     |CIERRA #308;
     |CIERRA #309;

     |HAZ CogeModelo;
     |HAZ eoszp019;
|FPRC;

|DEFBUCLE zbarw001;
     #401#1;
     ;
     00000, 0, 00;
     99999, 9, 99;
     ;
     NULL, PasaModulo;
|FIN;

|PROCESO LeeActiv;  |TIPO 0;
     #310#0 = #302#0;
     #310#1 = nActivi;
     #310#2 = #302#1;
     |LEE 040#310=;
     |SI FSalida ! 0;  |DDEFECTO #310;  |FINSI;
|FPRC;

|PROCESO CalculaUltimo310;
     nEmpresa = #302#0;
     nAnyo    = #302#1;
     nTrimes  = #302#2;
     nSuma    = 0;

     |LIBERA #302;

     |PARA nPeriodo = 1;  |SI nPeriodo [ 3;  |HACIENDO nPeriodo = nPeriodo + 1;
           #302#0 = nEmpresa;
           #302#1 = nAnyo;
           #302#2 = nPeriodo;
           |LEE 000#302=;
           |SI FSalida = 0;  nSuma = nSuma + #302#44;  |FINSI;
     |SIGUE;

     #302#0 = nEmpresa;
     #302#1 = nAnyo;
     #302#2 = nTrimes;
     |LEE 101#302=;
     |SI FSalida = 0;
         #302#43 = nSuma;
         #302#44 = #302#30 - #302#43;
         #302#34 = #302#44 + #302#31 + #302#32 + #302#33;
         #302#46 = #302#35 + #302#45;
         #302#36 = #302#34 - #302#46;
         #302#38 = #302#36 - #302#37;

         |GRABA 020#302a;
         |LIBERA #302;
     |FINSI;
|FPRC;

|DEFBUCLE eosm0019;
     #302#1;
     ;
     00000, 00, 4;
     99999, 99, 4;
     be;
     NULL, CalculaUltimo310;
|FIN;

|PROCESO Imprime;
     |PRINT;
     nSwPrint = 1;
     nSwPie   = 1;
|FPRC;

|PROCESO Errores;
     nSwPrint = 0;

     #404#0 = #300#0;
     #404#1 = #300#1;
     #404#2 = #300#5;
     #404#3 = 1;
     |LEE 040#404=;
     |SI FSalida ! 0;  |DDEFECTO #404;  |FINSI;

     #301#0 = #300#0;
     #301#1 = #300#1;
     #301#2 = #300#5;
     |LEE 040#301=;
     |SI FSalida ! 0;  |DDEFECTO #301;  |FINSI;

     nResta = #300#110 - #404#4;
     |SI nResta < 0;  nResta = -nResta;  |FINSI;

     |SI nResta > 10;
         aVaralfa = "Rendimiento Previo IRPF Provisional descuadrado. Revise Desglose.";
         |HAZ Imprime;
     |FINSI;

     nResta = #300#112 - #404#5;
     |SI nResta < 0;  nResta = -nResta;  |FINSI;

     |SI nResta > 10;
         aVaralfa = "Rendimiento Previo IVA  Provisional descuadrado. Revise Desglose.";
         |HAZ Imprime;
     |FINSI;

     |SI nSwPrint = 1;  |ACABA;  |FINSI;

     nResta = #301#40 - #404#21;
     |SI nResta < 0;  nResta = -nResta;  |FINSI;

     |SI nResta > 10;
         aVaralfa = "Rdto.Neto Minorado Prov. descuadrado. Revise Min.Empleo o Amortizaciones";
         |HAZ Imprime;
     |FINSI;

     |SI nSwPrint = 1;  |ACABA;  |FINSI;

     nResta = #300#114 - #404#35;
     |SI nResta < 0;  nResta = -nResta;  |FINSI;

     |SI nResta > 10;
         aVaralfa = "Rdto.Neto Modulo Prov. descuadrado. Revise Indices de Reduccion";
         |HAZ Imprime;
     |FINSI;

     |SI nSwPrint = 1;  |ACABA;  |FINSI;

     |SI #300#128 = 364;
         aVaralfa = "M¢dulo Calculado a 364 D¡as. Deber¡a ser 365 Dias.";
         |HAZ Imprime;
     |FINSI;
|FPRC;

|DEFBUCLE eosm0017;
     #300#1;
     ;
     00000, 00, 1;
     99999, 99, 9;
     ;
     NULL, Errores;
|FIN;

|PROCESO Tipo3;  |TIPO 3;
     Programa    = 2;

     #0#0 = #0#0 > #0#0;  |PINTA #0#0;
     |SI #0#0 = "NO";  |ACABA;  |FINSI;

     |ABRE #300;  |CIERRA #300;  |DELINDEX #300;
     |ABRE #301;  |CIERRA #301;  |DELINDEX #301;
     |ABRE #302;  |CIERRA #302;  |DELINDEX #302;
     |ABRE #303;  |CIERRA #303;  |DELINDEX #303;
     |ABRE #304;  |CIERRA #304;  |DELINDEX #304;
     |ABRE #305;  |CIERRA #305;  |DELINDEX #305;
     |ABRE #308;  |CIERRA #308;  |DELINDEX #308;
     |ABRE #309;  |CIERRA #309;  |DELINDEX #309;

     |BUCLE eosm0019;

     |ABRE #300;
     |ABRE #301;
     |ABRE #310;
     |ABRE #403;
     |ABRE #404;
     |BUCLE zbarw001;
     |CIERRA #300;
     |CIERRA #301;
     |CIERRA #310;
     |CIERRA #403;
     |CIERRA #404;


     |ET Impresora;
          |IMPRE 0;
          |SI FSalida ! 0;
              |MENAV "        Por favor introduzca impresora. Es obligado el Informe";
              |VETE Impresora;
          |FINSI;

          |INFOR "zpasmodu";

          |ABRE #301;
          |ABRE #404;
          |BUCLE eosm0017;
          |CIERRA #301;
          |CIERRA #404;

          |SI nSwPie = 1;  |PIEINF;  |FINSI;
          |FININF;
          |FINIMP;
|FPRC;
