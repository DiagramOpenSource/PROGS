|FICHEROS;
     eosz0016 #0;
     eosclien #1;
     eosw0086 #86;
     eosw0087 #87,MANTE;
     eosw0088 #88,MANTE;
|FIN;

|VARIABLES;
     aAlfa        = "";
     aDirModelo   = "";
     aDirSesion   = "";
     aRaiz        = "";
     aDirectorio  = "";
     aSesion      = "";
     aError       = "";
     aFichero     = "";
     aAbre        = "";
     aHandle      = "";
     aVariable    = "";
     Path         = "";

     nRegistro    = 0;
     nHePasado    = 0;
|FIN;

|INCLUYE i_laserc;

|PROCESO Cuadro;  |TIPO 7;
     |PUSHV 0501 2380;
     |CUADRO 0820 2042;
     |BLANCO 0921 1941;
     |ATRI R;
     |PINTA 0921, "  MODELOS EN LASER   ";
     |PINTA 1021, " -110- Modelo 110    ";
     |PINTA 1121, " -115- Modelo 115    ";
     |PINTA 1221, " -130- Modelo 130    ";
     |PINTA 1321, " -131- Modelo 131    ";
     |PINTA 1421, " -190- Modelo 190    ";
     |PINTA 1521, " -300- Modelo 300    ";
     |PINTA 1621, " -310- Modelo 310    ";
     |PINTA 1721, " -311- Modelo 311    ";
     |PINTA 1821, " -347- Modelo 347    ";
     |PINTA 1921, " -390- Modelo 390    ";
     |ATRI 0;
|FPRC;

|PROCESO Reponer;  |TIPO 0;
     |SI #0#0 ! "110"; |Y #0#0 ! "115"; |Y #0#0 ! "130"; |Y #0#0 ! "131";
      |Y #0#0 ! "180"; |Y #0#0 ! "190"; |Y #0#0 ! "300"; |Y #0#0 ! "310";
      |Y #0#0 ! "311"; |Y #0#0 ! "347"; |Y #0#0 ! "390";
         |MENAV "     Modelo introducido incorrecto.";
         |ERROR;
         |ACABA;
     |FINSI;
     |POPV;

     |PINTA #0#0;
|FPRC;

|RUTINA ClaveBaja86;
     #86#0 = 1;
|FRUT;

|RUTINA ClaveAlta86;
     #86#0 = 9999;
|FRUT;

|RUTINA ClaveBaja87;
     #87#0 = "        ";
|FRUT;

|RUTINA ClaveAlta87;
     #87#0 = "zzzzzzzz";
|FRUT;

|RUTINA ClaveBaja88;
     #88#0 = #87#0;
     #88#1 = 00001;
|FRUT;

|RUTINA ClaveAlta88;
     #88#0 = #87#0;
     #88#1 = 99999;
|FRUT;

|| 컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
|| PROCESOS LLAMADOS DESDE EL EOSW0088
|| 컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴

|PROCESO Printar;
     |PRINT;
|FPRC;

|DEFBUCLE eosm0086;
     #86#1;
     ;
     0001;
     9999;
     ;
     NULL, Printar;
|FIN;

|PROCESO Impresion;
     |IMPRE 0;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |INFOR "eosz0016";
     |SI FSalida ! 0;  |FINIMP;  |ACABA;  |FINSI;

     |BUCLE eosm0086;

     |PIEINF;
     |FININF;
     |FINIMP;
|FPRC;

|PROCESO CargaEntlineal;
     aSesion      = #88#0;  |QBT aSesion;
     aError       = #0#0 + (("00000" + #88#1) % -105) + ".txt";
     aFichero     = aDirectorio + "hacienda/" + #0#0 + "/" + aSesion + "/" + aError;

     aAbre        = "rt  " + aFichero;

     Pos = -1;
     |FABRE aAbre;
     aHandle = FSalida;
     |SI FSalida < 0;
         |MENAV "     No puedo Abrir el Fichero.";
         |FCIERRA aHandle;
         |ACABA;
     |FINSI;

     nRegistro = 0;
     |ABRE #86;
     |PARA; |SI; |HACIENDO;
            aVariable = aHandle + " 250";
            |FLEE aVariable;
            |SI FSalida < 0; |SAL; |FINSI;
            nRegistro = nRegistro + 1;
            #86#0 = nRegistro;
            #86#1 = aVariable % 0170;
            |GRABA 020#86n;
            |LIBERA #86;
     |SIGUE;
     |FCIERRA aHandle;
     |CIERRA #86;

     |SI nRegistro = 0;
         |MENAV "     El Fichero esta vacio.";
         |ACABA;
     |FINSI;

     |PUSHV 0501 2380;
     |PINPA #0#86;
     |ATRI R;
     |PINTA 0644, #0#0;
     |PINTA 0669, #88#1;
     |ATRI 0;

     |ENTLINEAL #1#86, -1, 4, 22, 0, ClaveBaja86, ClaveAlta86;
     |CONFI "2400NDesea Imprimir el Documento : ";
     |SI FSalida = 0;
         |HAZ Impresion;
     |FINSI;

     |POPV;

     |DELINDEX #86;
|FPRC;


|PROCESO Tipo11w0088;  |TIPO 11;
     |SI FSalida ! 10;  |ACABA;  |FINSI;

     |HAZ CargaEntlineal;
|FPRC;

|| 컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
|| PROCESOS LLAMADOS DESDE EL EOSW0087
|| 컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
|PROCESO Tipo7w0087;  |TIPO 7;
     |PTEC 807;
     |ENTLINEAL #1#88, -1, 4, 21, 0, ClaveBaja88, ClaveAlta88;
|FPRC;

|PROCESO Tipo11w0087;  |TIPO 11;
     |SI FSalida ! 10;  |ACABA;  |FINSI;
     |ENTLINEAL #1#88, -1, 4, 21, 0, ClaveBaja88, ClaveAlta88;
|FPRC;

|| 컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
|| PROCESOS LLAMADOS DESDE EL EOSZ0016
|| 컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴

|PROCESO MiraEmpresas;
     nRegistro   = 0;
     aSesion     = #87#0;  |QBF aSesion;
     aDirSesion  = aDirModelo + aSesion + "/";
     aFichero    = aDirModelo + aSesion + "/*.txt";
     |_PDIR aFichero;
     |PARA; |SI; |HACIENDO;
            |SI FSalida ! 0; |SAL; |FINSI;

            aError = aFichero - aDirSesion;
            aAlfa  = aError % 405;
            #1#0   = aAlfa;
            |LEE 040#1=;
            |SI FSalida = 0;
                nHePasado = 1;
                nRegistro = 1;
                #88#0 = #87#0;
                #88#1 = #1#0;
                #88#2 = #1#2;
                |GRABA 020#88n;
                |LIBERA #88;
            |FINSI;

            |_SDIR aFichero;
     |SIGUE;

     |SI nRegistro = 0;
         |BORRA 020#87a;
         |LIBERA #87;
     |FINSI;
|FPRC;

|DEFBUCLE eosw0087;
     #87#1;
     ;
     "        ";
     "zzzzzzzz";
     be;
     NULL, MiraEmpresas;
|FIN;

|PROCESO Tipo12;  |TIPO 12;
     |HAZ QueVersion;
     |SI enSwDirecta = 1;
         eaTipoEmi = "ERRORES";
         |SI #0#0 = "190";  eaTipoEmi = "ERRORES190";  |FINSI;
         |SI #0#0 = "180";  eaTipoEmi = "ERRORES180";  |FINSI;
         eaTipoSec = #0#0;
         |SUB_EJECUTA "eosz9999";
         |ACABA;
     |FINSI;

     aFichero = ("86" + Usuario + "zzzzzzzz") % 108;
     |NOME_DAT #86 aFichero;
     |ABRE #86;  |CIERRA #86;  |DELINDEX #86;

     aFichero = ("87" + Usuario + "zzzzzzzz") % 108;
     |NOME_DAT #87 aFichero;
     |ABRE #87;  |CIERRA #87;  |DELINDEX #87;

     aFichero = ("88" + Usuario + "zzzzzzzz") % 108;
     |NOME_DAT #88 aFichero;
     |ABRE #88;  |CIERRA #88;  |DELINDEX #88;

     |ABRE #87;
     nRegistro    = 0;
     aRaiz        = "";  |DIRECTORIO aRaiz;
     aDirectorio  = "";  |DBASE aDirectorio;
     aDirModelo   = aDirectorio + "hacienda/" + #0#0 + "/";
     aFichero     = aDirectorio + "hacienda/" + #0#0 + "/*";

     |_PDIR aFichero;
     |PARA; |SI; |HACIENDO;
            |SI FSalida ! 0; |SAL; |FINSI;

            aSesion = aFichero - aDirModelo;
            aAlfa   = aSesion % -104;   |QBT aAlfa;
            |SI aAlfa ! ".exe";  |Y aAlfa ! ".EXE";
             |Y aAlfa ! ".";     |Y aAlfa ! "..";
                #87#0 = aSesion;
                |LEE 101#87=;
                |SI FSalida ! 0;
                    nRegistro = 1;
                    #87#0 = aSesion;
                    |GRABA 020#87n;
                |FINSI;
                |LIBERA #87;
            |FINSI;

            |_SDIR aFichero;
     |SIGUE;
     |CIERRA #87;

     |SI nRegistro = 0;
         |MENAV "     No hay sesiones creadas en el Modelo.";
         |DELINDEX #87;
         |ACABA;
     |FINSI;

     nHePasado = 0;
     |ABRE #1;
     |ABRE #88;
     |BUCLE eosw0087;
     |CIERRA #1;
     |CIERRA #88;

     |SI nHePasado = 1;
         |ENTLINEAL #1#87, -1, 4, 21, 0, ClaveBaja87, ClaveAlta87;
     |SINO;
         |MENAV  "     No hay errores en el Modelo.";
     |FINSI;

     |DELINDEX #86;
     |DELINDEX #87;
     |DELINDEX #88;
|FPRC;
