|FICHEROS;
     eosm1303 #0;
     eosclien #2;
     eosaplic #4;
     eosactiv #5;
     eosopes2 #7;
     eosw0052 #98,MANTE;
     eosma100 #100,MANTE;
     eosplan1 #44;

     regiva@  #1000;
|FIN;

|VARIABLES;
   &sw_sit     = 1;
   &d_empre    = 0;
   &h_empre    = 0;
   &el_anyo    = 0;
   &SwViene    = 1;
   &VEmpresa   = 0;
   &VPeriodo   = 0;
   &VAnyo      = 0;

   i_campo  = 0;
   cont     = 0;
   empresa  = 0;
   trime    = 0;
   anyo     = 0;
   activi   = 0;
   compen   = 0;
   base     = 0;
   tipo     = 0;
   total    = 0;
   rendi1   = 0;
   rendi2   = 0;
   penult   = 0;
   estado   = 0;
   ventas   = 0;
   x        = 0;
   camp     = 0;
   xxxxfiac = "  ";
   xxaponer = "  ";
   xrelleno = "                              ";
   modifica = 0;
   FEstado  = 0;
   modo     = 0;
   nError   = 0;
   aTipo    = "";
   nPer     = 0;

   nBoton340 = -1;
   nBoton341 = -1;
   nModo     = 0;
   nEjer     = 0;
   nTecla    = 0;
   nCampo    = 0;
   nCampo1   = 0;
   nCampo2   = 0;
   nCampo3   = 0;
   nIVA      = 0;

   aAlfa     = "";
   aBase     = "";
   aMas      = "";
   nSw       = 0;
   nSubeIVA  = 0;
   nMasBase  = 0;
   nPriIVA   = 0;
   nPregunto = 0;

   nSwGraba  = 0;
|FIN;

|INCLUYE i_seguim;
|INCLUYE i_devolv;
|INCLUYE i_period;

|PROCESO eosma100B;
     |BORRA 020#100a;
     |LIBERA #100;
|FPRC;

|DEFBUCLE eosma100B;
     #100#1;
     ;
     "303", #0#0, #0#45, #0#1, #0#2, 000;
     "303", #0#0, #0#45, #0#1, #0#2, 999;
     be;
     NULL, eosma100B;
|FIN;

|PROCESO borrado; |TIPO 2;
    modo = (FEntrada / 100) ! 0;
    |SI modo ! 3; |ACABA; |FINSI;

    |ABRE #7;
    #7#0 = #0#0;
    #7#1 = #0#45;
    #7#2 = #0#1;
    #7#3 = #0#2;
    |BORRA 140#7c;
    |LIBERA #7;
    |CIERRA #7;

    |BUCLE eosma100B;
|FPRC;

|PROCESO i_archiv; |TIPO 7;
    |SI #0#4 = "S";
        |MENAV "     Registro archivado, DESARCHIVELO";
        |PTEC 807;
    |FINSI;
|FPRC;

|PROCESO antes; |TIPO 7;
    i_campo = 1;
|FPRC;

|PROCESO empresa; |TIPO 0;
    |ABRE #4;
    |DDEFECTO #4;
    |LEE 040#4p;
    |CIERRA #4;

    i_campo = 0;
    |ABRE #2;
    #2#0 = #0#0;
    |LEE 040#2=;
    |SI FSalida ! 0;
        |MENAV "     No existe Empresa.";
        |ERROR;
    |SINO;
        #0#2  = #2#20;  |PINTA #0#2;
        cont = (FEntrada / 100) ! 0;
        |SI cont = 1;
            |ABRE #4;
            |LEE 040#4p;
            |SI FSalida = 0;
                #0#6  = #4#1; |PINTA #0#6;
                #0#9  = #4#2; |PINTA #0#9;
                #0#12 = #4#3; |PINTA #0#12;
                #0#15 = #4#4; |PINTA #0#15;
                #0#18 = #4#5; |PINTA #0#18;
                #0#47 = #4#6; |PINTA #0#47;
                #0#59 = #4#13;
                #0#62 = #4#14;
                #0#65 = #4#15;
           |FINSI;
           |CIERRA #4;
        |FINSI;
     |FINSI;
     |CIERRA #2;
|FPRC;

|PROCESO pinta_porr; |TIPO 13;
      |ABRE #5;
      #5#0 = #0#0;
      #5#1 = #0#45;
      #5#2 = #0#2;
      |LEE 040#5=;
      |SI FSalida ! 0; |DDEFECTO #5; |FINSI;
      |CIERRA #5;
      |PINTA 670, #5#28;
|FPRC;

|PROCESO activida; |TIPO 0;
    |SI #0#2 [ 8;
        |MENAV "0000Ejercicio erroneo. A partir del 2009";
        |ERROR;
        |ACABA;
    |FINSI;

    |SI #0#2 ] 80;
        |MENAV "0000Ejercicio erroneo. A partir del 2009";
        |ERROR;
        |ACABA;
    |FINSI;

    |SI i_campo = 1; |ACABA; |FINSI;
    |ABRE #5;
    #5#0 = #0#0;
    #5#1 = #0#45;
    #5#2 = #0#2;
    |LEE 040#5=;
    |SI FSalida ! 0;
        |MENAV "    No existe Actividad.";
        |ERROR;
    |SINO;
        |SI #5#14 = "S";
           |MENAV "    La Actividad es un Comunero";
           |ERROR;
        |FINSI;

        |SI #5#22 = "A";
           |MENAV "    La Actividad es de Modulos";
           |ERROR;
        |FINSI;
    |FINSI;
    |CIERRA #5;

    aTipo = #5#26;
    |SI aTipo = "F";  aTipo = "T";  |FINSI;
    |SI aTipo = "D";  aTipo = "M";  |FINSI;

    |SI aTipo = "T"; |Y #0#1 > 4;
        |MENAV "     El 303 esta preparado como Trimestral";
        |ERROR;
    |FINSI;
|FPRC;

|PROCESO Tipo7C83;  |TIPO 7;
     |C_M #0#83, 1, "N";
     |SI #0#3 ! "          ";
         |C_M #0#83, 1, "S";
     |FINSI;
|FPRC;


|PROCESO DfectoPeriodo; |TIPO 7;
     #0#1 = Periodo;
     |PINTA #0#1;
|FPRC;

|PROCESO anyopas; |TIPO 7;
     cont = (FEntrada / 100) ! 0;
     |SI cont = 1;
        |SI #0#1 = 1;
            |ABRE #5;
            #5#0 = #0#0;
            #5#1 = #0#45;
            #5#2 = #0#2 - 1;
            |SI #5#2 < 0;  #5#2 = 99;  |FINSI;
            |LEE 040#5=;
            |SI FSalida ! 0; |DDEFECTO #5; |FINSI;
            #0#29 = #5#34;

            #5#0 = #0#0;
            #5#1 = #0#45;
            #5#2 = #0#2;
            |LEE 040#5=;
            |SI FSalida ! 0; |DDEFECTO #5; |FINSI;

            |CIERRA #5;
        |SINO;
            empresa = #0#0;
            trime   = #0#1;
            anyo    = #0#2;
            activi  = #0#45;

           |DDEFECTO #0;
           #0#0    = empresa;
           #0#1    = trime - 1;
           #0#2    = anyo;
           #0#45   = activi;
           compen  = 0;
           |LEE 040#0=;
           |SI FSalida = 0;
               |SI #0#32 ! 0;
                   |SI #0#55 = 0;
                       |SI trime = 2;   || Solo 1 mes del 2009 fallo en la emision
                           compen = #0#32;
                       |FINSI;
                   |FINSI;

                   |SI #0#55 < 0;
                        compen = -#0#55;
                   |FINSI;
               |FINSI;
           |FINSI;

        |DDEFECTO #0;
        #0#0    = empresa;
        #0#1    = trime;
        #0#2    = anyo;
        #0#45   = activi;
        |LEE 040#0=;
        |SI FSalida ! 0;
            |DDEFECTO #0;
            #0#0 = empresa;
            #0#1 = trime;
            #0#2 = anyo;
            #0#45 = activi;
        |FINSI;
        #0#29   = compen;
        |PINTA #0#0;
        |PINTA #0#1;
        |PINTA #0#45;
        |PINTA #0#2;
   |FINSI;
|FINSI;
|FPRC;

|PROCESO modifica; |TIPO 0;
   modifica = 0;
   |SI #0Campo ! Contenido; modifica = 1; |FINSI;
|FPRC;

|PROCESO cuota1; |TIPO 7;
     |SI modifica = 0; |ACABA; |FINSI;
     |SI nSubeIVA = 0;
         base = Campo - 2;
         tipo = Campo - 1;
         #0Campo = #0base % #0tipo;
         |PINTA #0Campo;
     |FINSI;
     |SI nSubeIVA = 1; |Y Campo > 13;
         base = Campo - 2;
         tipo = Campo - 1;
         #0Campo = #0base % #0tipo;
         |PINTA #0Campo;
     |FINSI;
|FPRC;

|PROCESO sumacuot; |TIPO 0;
     #0#20 = #0#7 + #0#10 + #0#13 + #0#16 + #0#19 + #0#48 + #0#50;
     |PINTA #0#20;
     |HAZ especifi;
     |HAZ atribuir;
     |HAZ total;
|FPRC;

|PROCESO cuota2; |TIPO 0;
     #0#25 = #0#21 + #0#22 + #0#23 + #0#24 + #0#43 + #0#44 + #0#51+ #0#78 + #0#80;
     |PINTA #0#25;
     |HAZ atribuir;
     |HAZ total;
|FPRC;

|PROCESO atribuir; |TIPO 0;
    #0#26 = #0#20 - #0#25;  |PINTA #0#26;
    #0#28 = #0#26 % #0#27;  |PINTA #0#28;
|FPRC;

|PROCESO total; |TIPO 0;
     total = #0#28 - #0#29 + #0#30;

     aTipo = #5#26;
     |SI aTipo = "F";  aTipo = "T";  |FINSI;
     |SI aTipo = "D";  aTipo = "M";  |FINSI;

     |SI aTipo = "T";
         nPer = 4;
     |FINSI;

     |SI aTipo = "M";
         |SI #5#26 ! "D";
             nPer = 12;
         |SINO;
             nPer = #0#1;
         |FINSI;
     |FINSI;

     |SI total = 0;
         #0#33 = 0;
         #0#32 = 0;
         #0#31 = 0;
     |FINSI;

     |SI total > 0;
         #0#33 = total;
         #0#31 = 0;
         #0#32 = 0;
         |PINTA #0#31;
         |PINTA #0#32;
         |PINTA #0#33;
     |SINO;
         total = total * (-1);

         |SI #0#1 ! nPer;
             #0#32 = total;
             #0#31 = 0;
             #0#33 = 0;
             |PINTA #0#31;
             |PINTA #0#32;
             |PINTA #0#33;
         |SINO;
            |SI #5#27 = "C";
                #0#32 = total;
                #0#31 = 0;
                #0#33 = 0;
                |PINTA #0#31;
                |PINTA #0#32;
                |PINTA #0#33;
            |FINSI;

            |SI #5#27 = "D";
               #0#32 = 0;
               #0#33 = 0;
               #0#31 = total;
               |PINTA #0#31;
               |PINTA #0#32;
               |PINTA #0#33;
            |FINSI;
         |FINSI;
     |FINSI;
     |HAZ especifi;
|FPRC;

|PROCESO especifi; |TIPO 0;
     |ABRE #7;
     #7#0 = #0#0;
     #7#1 = #0#45;
     #7#2 = #0#1;
     #7#3 = #0#2;
     |LIBERA #7;
     |LEE 140#7=;
     FEstado = FSalida;
     |SI FSalida ! 0; |CDEFECTO #7, 1; |FINSI;

     #7#9   = #0#54;
     #7#10  = #0#81;
     #7#7   = #0#82;

     |SI FEstado = 0; |GRABA 140#7a; |SINO; |GRABA 140#7n; |FINSI;
     |CIERRA #7;
|FPRC;

|PROCESO totales; |TIPO 3;
     #0#20 = #0#7 + #0#10 + #0#13 + #0#16 + #0#19 + #0#48 + #0#50;
     #0#25 = #0#21 + #0#22 + #0#23 + #0#24 + #0#43 + #0#44 + #0#51 + #0#78 + #0#80;
     #0#26 = #0#20 - #0#25;

     aTipo = #5#26;
     |SI aTipo = "F";  aTipo = "T";  |FINSI;
     |SI aTipo = "D";  aTipo = "M";  |FINSI;

     |SI aTipo = "T";
         nPer = 4;
     |FINSI;

     |SI aTipo = "M";
         |SI #5#26 ! "D";
             nPer = 12;
         |SINO;
             nPer = #0#1;
         |FINSI;
     |FINSI;

     total = #0#28 - #0#29 + #0#30;
     |SI total = 0;
         #0#33 = 0;
         #0#31 = 0;
         #0#32 = 0;
     |FINSI;
     |SI total > 0;
         #0#33 = total;
         #0#31 = 0;
         #0#32 = 0;
     |SINO;
         total = total * (-1);
         |SI #0#1 ! nPer;
            #0#32 = total;
            #0#31 = 0;
            #0#33 = 0;
         |SINO;
            |SI #5#27 = "C"; #0#32 = total; #0#31 = 0; #0#33 = 0; |FINSI;
            |SI #5#27 = "D"; #0#31 = total; #0#32 = 0; #0#33 = 0; |FINSI;
         |FINSI;
     |FINSI;

     |ABRE #5;
     #5#0 = #0#0;
     #5#1 = #0#45;
     #5#2 = #0#2;
     |LIBERA #5;
     |LEE 140#5=;
     |SI FSalida = 0;
         #5#34 = 0;
         |SI #0#55 < 0;
             |SI aTipo = "T";  |Y #0#1 = 4;
                 |SI #5#27 = "C"; #5#34 = -#0#55; |FINSI;
             |FINSI;
             |SI aTipo = "M";  |Y #0#1 = 12;
                 |SI #5#27 = "C"; #5#34 = -#0#55; |FINSI;
             |FINSI;
         |FINSI;
         |GRABA 140#5a;
     |FINSI;
     |CIERRA #5;
|FPRC;

|PROCESO graba130; |TIPO 30;
    se_empre = #0#0;
    se_ejerc = #0#2;
    se_perio = #0#1;
    se_tipos = 303;
    se_activ = #0#45;
    se_impor = 0;
    |SI #0#33 > 0; se_impor =  #0#33; |FINSI;
    |SI #0#32 > 0; se_impor = -#0#32; |FINSI;
    |SI #0#31 > 0; se_impor = -#0#31; |FINSI;
    se_fecha = #0#3;
    se_factu = "";
    |HAZ seguimie;

    nPregunto = 0;
    |SI aTipo = "T";  |Y #0#1 = 4;  nPregunto = 1; |FINSI;
    |SI aTipo = "M";  |Y #0#1 = 12; nPregunto = 1; |FINSI;
    |SI nPregunto = 1;

        |CONFI "2400NDesea hacer el pase del Resumen Anual y Consultarlo : ";
        |SI FSalida ! 0; |ACABA; |FINSI;

        |LIBERA #0;
        |CIERRAT #0;
        |SALVA_DATOS #0;
        d_empre  = #0#0;
        h_empre  = #0#0;
        el_anyo  = #0#2;
        VEmpresa = #0#0;
        VPeriodo = #0#1;
        VAnyo    = #0#2;

        |SUB_EJECUTA "eosem303";
        |SUB_EJECUTA "eosp2390";
        |SUB_EJECUTA "eosm3390";

        |ABRET #0;
        |REPON_DATOS #0;
        |LIBERA #0;
        |LEE 140#0=;
    |FINSI;
|FPRC;

|PROCESO funcion_9; |TIPO 0;

|SI FSalida = 10;
    |PUSHV 0501 2380;
    |ABRE #7;
    #7#0 = #0#0;
    #7#1 = #0#45;
    #7#2 = #0#1;
    #7#3 = #0#2;
    |LIBERA #7;
    |LEE 140#7=;
    FEstado = FSalida;
    |SI FSalida ! 0; |CDEFECTO #7, 1; |FINSI;
    |PINPA #0#7;
    |PINDA #0#7;
    |ENDATOS #1#7;
    |SI FSalida = 0;
        |SI FEstado = 0; |GRABA 140#7a; |SINO; |GRABA 140#7n; |FINSI;
    |FINSI;
    |CIERRA #7;
    |POPV;
|FINSI;

|SI FSalida ! 11;  |ACABA; |FINSI;

aTipo = #5#26;
|SI aTipo = "F";  aTipo = "T";  |FINSI;
|SI aTipo = "D";  aTipo = "M";  |FINSI;

|SI aTipo = "T";
    |SI #0#1  ! 4; |ACABA; |FINSI;
|FINSI;

|SI aTipo = "M";
    |SI #5#26 ! "D";
        |SI #0#1  ! 12; |ACABA; |FINSI;
    |FINSI;
|FINSI;

|SI #0#31 = 0; |Y #0#32 = 0;
    |ACABA;
|FINSI;

empresa_d = #0#0;
activid_d = #0#45;
anyo_d    = #0#2;
|HAZ Preg_Devol;
|FPRC;


|PROCESO nomodi; |TIPO 7;
  |SI cont = 1; |HAZ nomodifi; |FINSI;
|FPRC;

|PROCESO nomodifi;             || RUTINA DE CONVERSION A NO MODIFICABLES
                               || VARIABLES: A_
    xxaponer = Anono;
    xxxxfiac = Control % A1001;
    Control = Control + xxxxfiac;
    Control = Control + xrelleno;
    Control = Control % A120;
    Control = Control + xxaponer;
|FPRC;

|PROCESO PideBase;  |TIPO 0;
     |SI FSalida = 10;
         |PUSHV 0501 2380;
         #98#0  = "ADQUISICIONES 1 ";
         #98#1  = "ADQUISICIONES 2 ";
         #98#2  = "ADQUISICIONES 3 ";
         #98#3  = "";
         #98#4  = #0#58;   #98#5  = #0#59;   #98#6  = #0#60;
         #98#7  = #0#61;   #98#8  = #0#62;   #98#9  = #0#63;
         #98#10 = #0#64;   #98#11 = #0#65;   #98#12 = #0#66;
         #98#13 = #0#67;   #98#14 = #0#68;   #98#15 = #0#69;

         |PINPA #0#98;
         |PINDA #0#98;
         |ENDATOS #1#98;
         |SI FSalida = 0;
             #0#58 = #98#4;   #0#59 = #98#5;   #0#60 = #98#6;
             #0#61 = #98#7;   #0#62 = #98#8;   #0#63 = #98#9;
             #0#64 = #98#10;  #0#65 = #98#11;  #0#66 = #98#12;
             #0#67 = #98#13;  #0#68 = #98#14;  #0#69 = #98#15;

             |CONFI "2400SLo Computamos al IVA Soportado : ";
             |SI FSalida = 0;
                 #0#72 = #0#58 + #0#61 + #0#64 + #0#67;
                 #0#51 = #0#60 + #0#63 + #0#66 + #0#69;
                 nCampo1 = 72;
                 |ABRE #100;
                 #100#0 = "303";
                 #100#1 = #0#0;
                 #100#2 = #0#45;
                 #100#3 = #0#1;
                 #100#4 = #0#2;
                 #100#5 = nCampo1;
                 |LEE 101#100=;
                 |SI FSalida ! 0;
                     |DDEFECTO #100;
                     #100#0 = "303";
                     #100#1 = #0#0;
                     #100#2 = #0#45;
                     #100#3 = #0#1;
                     #100#4 = #0#2;
                     #100#5 = nCampo1;
                     |GRABA 020#100b;
                 |FINSI;
                 #100#6  = #0#58; #100#7  = #0#59; #100#8  = #0#60;
                 #100#9  = #0#61; #100#10 = #0#62; #100#11 = #0#63;
                 #100#12 = #0#64; #100#13 = #0#65; #100#14 = #0#66;
                 #100#15 = #0#67; #100#16 = #0#68; #100#17 = #0#69;
                 |GRABA 020#100a;
                 |LIBERA #100;
                 |CIERRA #100;

             |FINSI;
         |FINSI;
         |POPV;
     |FINSI;

     #0#49 = #0#58 + #0#61 + #0#64 + #0#67;  |PINTA #0#49;
     #0#50 = #0#60 + #0#63 + #0#66 + #0#69;  |PINTA #0#50;
|FPRC;

|PROCESO PideBaseSopor;  |TIPO 0;
     nTecla = FSalida;
     |SI nTecla = 10; |O nTecla = 11; FSalida = nTecla; |HAZ funcion_9; |FINSI;
     |SI Campo = 70;
         nCampo1 = 70;
         nCampo2 = 21;
     |FINSI;

     |SI Campo = 71;
         nCampo1 = 71;
         nCampo2 = 22;
     |FINSI;

     |SI Campo = 72;
         nCampo1 = 72;
         nCampo2 = 51;
     |FINSI;

     |SI Campo = 75;
         nCampo1 = 75;
         nCampo2 = 43;
     |FINSI;

     |SI Campo = 76;
         nCampo1 = 76;
         nCampo2 = 44;
     |FINSI;

     |SI Campo = 77;
         nCampo1 = 77;
         nCampo2 = 78;
     |FINSI;

     |C_M #0nCampo2, 1, "N";

     |ABRE #100;
     #100#0 = "303";
     #100#1 = #0#0;
     #100#2 = #0#45;
     #100#3 = #0#1;
     #100#4 = #0#2;
     #100#5 = Campo;
     |LEE 101#100=;
     |SI FSalida ! 0;
         |DDEFECTO #100;
         #100#0 = "303";
         #100#1 = #0#0;
         #100#2 = #0#45;
         #100#3 = #0#1;
         #100#4 = #0#2;
         #100#5 = Campo;

         #100#6 = #0nCampo1;
         #100#7 = 0;
         #100#8 = #0nCampo2;

         |GRABA 020#100b;
     |FINSI;

     |SI nTecla = 13;
         |PUSHV 0501 2380;

         |PINPA #0#100;
         |PINDA #0#100;
         |ENDATOS #1#100;
         nError = FSalida;
         |SI FSalida = 0;
             #0nCampo1 = #100#6 + #100#9  + #100#12 + #100#15 + #100#18 + #100#21 + #100#24;
             #0nCampo2 = #100#8 + #100#11 + #100#14 + #100#17 + #100#20 + #100#23 + #100#26;
         |FINSI;

         |POPV;
     |FINSI;

     |SI nError = 0;
         |GRABA 020#100a;
     |FINSI;
     |LIBERA #100;
     |CIERRA #100;

     #0nCampo1 = #100#6 + #100#9  + #100#12 + #100#15 + #100#18 + #100#21 + #100#24;
     #0nCampo2 = #100#8 + #100#11 + #100#14 + #100#17 + #100#20 + #100#23 + #100#26;

     |PINTA #0nCampo1;
     |PINTA #0nCampo2;
|FPRC;

|PROCESO TotalRepNuevo;
    #0nCampo1 = #100#6 + #100#9  + #100#12 + #100#15 + #100#18 + #100#21 + #100#24;
    #0nCampo2 = #100#8 + #100#11 + #100#14 + #100#17 + #100#20 + #100#23 + #100#26;
    nMasBase = 0;
    nPriIVA  = 0;
    |SI #100#6  ! 0; nMasBase = nMasBase + 1; nPriIVA = #100#7;  |FINSI;
    |SI #100#9  ! 0; nMasBase = nMasBase + 1; nPriIVA = #100#10; |FINSI;
    |SI #100#12 ! 0; nMasBase = nMasBase + 1; nPriIVA = #100#13; |FINSI;
    |SI #100#15 ! 0; nMasBase = nMasBase + 1; nPriIVA = #100#16; |FINSI;
    |SI #100#18 ! 0; nMasBase = nMasBase + 1; nPriIVA = #100#19; |FINSI;
    |SI #100#21 ! 0; nMasBase = nMasBase + 1; nPriIVA = #100#22; |FINSI;
    |SI #100#24 ! 0; nMasBase = nMasBase + 1; nPriIVA = #100#25; |FINSI;
    |SI nMasBase ] 2;
        #0nCampo3 = (#0nCampo2 / #0nCampo1) * 100;
        |SI #0nCampo3 < 0;
            #0nCampo3 = 0;
        |FINSI;
    |SINO;
        #0nCampo3 = nPriIVA;
    |FINSI;
|FPRC;

|PROCESO AjustaTipo;
     |SI nSubeIVA  = 0;
         |SI nPriIVA = 8;  |O nPriIVA = 10; nPriIVA = 7;  |FINSI;
         |SI nPriIVA = 18; |O nPriIVA = 21; nPriIVA = 16; |FINSI;
     |FINSI;
     |SI nSubeIVA  = 1;
         |SI nPriIVA = 7;  |O nPriIVA = 10; nPriIVA = 8;  |FINSI;
         |SI nPriIVA = 16; |O nPriIVA = 21; nPriIVA = 18; |FINSI;
     |FINSI;
     |SI nSubeIVA  = 2;
         |SI nPriIVA = 7;  |O nPriIVA = 8;  nPriIVA = 10; |FINSI;
         |SI nPriIVA = 16; |O nPriIVA = 18; nPriIVA = 21; |FINSI;
     |FINSI;
|FPRC;

|PROCESO CalTipoyPer;
     |SI #0#2 ] 80;
         nEjer = 1900 + #0#2;
     |SINO;
         nEjer = 2000 + #0#2;
     |FINSI;

     aTipo = #5#26;
     |SI aTipo = "F";  aTipo = "T";  |FINSI;
     |SI aTipo = "D";  aTipo = "M";  |FINSI;

     nPer = 7;  |SI aTipo = "T"; nPer = 3; |FINSI;
     nSubeIVA  = 0;
     |SI nEjer = 2010; |Y #0#1 ] nPer;  nSubeIVA = 1;  |FINSI;
     |SI nEjer ] 2011;                  nSubeIVA = 1;  |FINSI;

     nPer = 9;  |SI aTipo = "T"; nPer = 3; |FINSI;
     |SI nEjer = 2012; |Y #0#1 ] nPer;  nSubeIVA = 2;  |FINSI;
     |SI nEjer ] 2013;                  nSubeIVA = 2;  |FINSI;
|FPRC;

|PROCESO Pide7; |TIPO 7;
     |HAZ CalTipoyPer;
     aAlfa = "    Base de IVA ";
     |SI nSubeIVA ! 0;
         aAlfa = "    Pulse <F5> para introducir el desglose de IVA  ";
     |FINSI;
     |MENSA aAlfa;
|FPRC;

|PROCESO Pide7R; |TIPO 7;
     aAlfa = "    Base de Recargo ";
     |SI nSubeIVA = 2;
         aAlfa = "    Pulse <F5> para introducir el desglose de Recargo  ";
     |FINSI;
     |MENSA aAlfa;
|FPRC;

|PROCESO PideBaseReper;  |TIPO 0;
     |BLIN 4;

     nTecla = FSalida;
     |SI Campo = 5;
         nCampo1 = 5;
         nCampo2 = 7;
         nCampo3 = 6;
     |FINSI;

     |SI Campo = 8;
         nCampo1 = 8;
         nCampo2 = 10;
         nCampo3 = 9;
     |FINSI;

     |SI Campo = 11;
         nCampo1 = 11;
         nCampo2 = 13;
         nCampo3 = 12;
     |FINSI;

     |SI Campo = 14;
         nCampo1 = 14;
         nCampo2 = 16;
         nCampo3 = 15;
     |FINSI;

     |SI Campo = 17;
         nCampo1 = 17;
         nCampo2 = 19;
         nCampo3 = 18;
     |FINSI;

     |SI Campo = 46;
         nCampo1 = 46;
         nCampo2 = 48;
         nCampo3 = 47;
     |FINSI;

     aAlfa = "S";
     |SI nSubeIVA ! 0;  aAlfa = "N"; |FINSI;
     |SI nSubeIVA = 1; |Y Campo > 11;
         aAlfa = "S";
     |FINSI;

     |C_M #0nCampo3, 1, aAlfa;
     |C_M #0nCampo2, 1, aAlfa;

     |ABRE #100;
     #100#0 = "303";
     #100#1 = #0#0;
     #100#2 = #0#45;
     #100#3 = #0#1;
     #100#4 = #0#2;
     #100#5 = Campo;
     |LEE 101#100=;
     |SI FSalida ! 0;
         |DDEFECTO #100;
         #100#0 = "303";
         #100#1 = #0#0;
         #100#2 = #0#45;
         #100#3 = #0#1;
         #100#4 = #0#2;
         #100#5 = Campo;

         #100#6 = #0nCampo1;
         #100#7 = #0nCampo3;
         #100#8 = #0nCampo2;

         |GRABA 020#100b;
     |FINSI;

     |SI nTecla = 13; |Y nSubeIVA = 1; |Y Campo > 11;
         nTecla = 0;
     |FINSI;

     |SI nTecla = 13; |Y  nSubeIVA ! 0;
         |PUSHV 0501 2380;

         |PINPA #0#100;
         |PINDA #0#100;
         |ENDATOS #1#100;
         nError = FSalida;
         |SI FSalida = 0;
             |HAZ TotalRepNuevo;
         |FINSI;

         |POPV;
     |FINSI;

     |SI nError = 0;
         |SI nSubeIVA = 0;
             |PDEFECTO #100,9,27;
             #100#6 = #0nCampo1;
             #100#7 = #0nCampo3;
             #100#8 = #0nCampo2;
         |FINSI;
         |SI nSubeIVA = 1; |Y Campo > 11;
             |PDEFECTO #100,9,27;
             #100#6 = #0nCampo1;
             #100#7 = #0nCampo3;
             #100#8 = #0nCampo2;
         |FINSI;
         |GRABA 020#100a;
     |FINSI;
     |LIBERA #100;
     |CIERRA #100;

     |SI nSubeIVA ! 0;
         |HAZ TotalRepNuevo;
     |FINSI;
     |SI #0nCampo3 = 0;
         |SI nCampo3 = 6;  nCampo = 1; |FINSI;
         |SI nCampo3 = 9;  nCampo = 2; |FINSI;
         |SI nCampo3 = 12; nCampo = 3; |FINSI;

         |SI nCampo3 = 15; nCampo = 4; |FINSI;
         |SI nCampo3 = 18; nCampo = 5; |FINSI;
         |SI nCampo3 = 47; nCampo = 6; |FINSI;
         #0nCampo3 = #4nCampo;
     |FINSI;

     |PINTA #0nCampo1;
     |PINTA #0nCampo2;
     |PINTA #0nCampo3;
|FPRC;

|PROCESO PideCuotaReper;  |TIPO 0;
     |SI Campo = 7;
         nCampo1 = 5;
         nCampo2 = 7;
         nCampo3 = 6;
     |FINSI;

     |SI Campo = 10;
         nCampo1 = 8;
         nCampo2 = 10;
         nCampo3 = 9;
     |FINSI;

     |SI Campo = 13;
         nCampo1 = 11;
         nCampo2 = 13;
         nCampo3 = 12;
     |FINSI;

     |SI Campo = 16;
         nCampo1 = 14;
         nCampo2 = 16;
         nCampo3 = 15;
     |FINSI;

     |SI Campo = 19;
         nCampo1 = 17;
         nCampo2 = 19;
         nCampo3 = 18;
     |FINSI;

     |SI Campo = 48;
         nCampo1 = 46;
         nCampo2 = 48;
         nCampo3 = 47;
     |FINSI;

     nSwGraba = 0;
     |SI nSubeIVA = 0; nSwGraba = 1; |FINSI;
     |SI nSubeIVA = 1; |Y nCampo1 > 11; nSwGraba = 1; |FINSI;
     |SI nSwGraba = 1;
         |ABRE #100;
         #100#0 = "303";
         #100#1 = #0#0;
         #100#2 = #0#45;
         #100#3 = #0#1;
         #100#4 = #0#2;
         #100#5 = nCampo1;
         |LEE 101#100=;
         |SI FSalida ! 0;
             |DDEFECTO #100;
             #100#0 = "303";
             #100#1 = #0#0;
             #100#2 = #0#45;
             #100#3 = #0#1;
             #100#4 = #0#2;
             #100#5 = nCampo1;
             |GRABA 020#100b;
         |FINSI;
         #100#6 = #0nCampo1;
         #100#7 = #0nCampo3;
         #100#8 = #0nCampo2;
         |GRABA 020#100a;
         |LIBERA #100;
         |CIERRA #100;
     |FINSI;
|FPRC;

|PROCESO Tipo1; |TIPO 1;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo ! 3;  |ACABA;  |FINSI;

     nEjer = 2000 + #0#2;

     aAlfa = "dsmo3401;";
     aAlfa = aAlfa + (("00000" + #0#0) % -105);    || Empresa
     aAlfa = aAlfa + (("0000" + nEjer) % -104);    || Ejercicio
     aAlfa = aAlfa + (("00" + #0#1) % -102);       || Periodo
     aAlfa = aAlfa + "340";                        || Modelo
     aAlfa = aAlfa + "00";                         || Complemento
     aAlfa = aAlfa + "01";                         || Actividad
     aAlfa = aAlfa + "03";                         || Tipo de entrada
     aAlfa = aAlfa + "0";                          || Mensaje

     |SUB_EJECUTA_NP aAlfa;
|FPRC;

|PROCESO Imprimir; |TIPO 4;
   SwViene  = 1;
   VEmpresa = #0#0;
   VAnyo    = #0#2;
   VPeriodo = #0#1;
   |SUB_EJECUTA "eosem303";
|FPRC;

|PROCESO Boton340;
     |LEE 000#0=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     nEjer = 2000 + #0#2;

     aAlfa = "|:agieos/dsmo3401;";
     aAlfa = aAlfa + (("00000" + #0#0) % -105);    || Empresa
     aAlfa = aAlfa + (("0000" + nEjer) % -104);    || Ejercicio
     aAlfa = aAlfa + (("00" + #0#1) % -102);       || Periodo
     aAlfa = aAlfa + "340";                        || Modelo
     aAlfa = aAlfa + "00";                         || Complemento
     aAlfa = aAlfa + "01";                         || Actividad
     aAlfa = aAlfa + "01";                         || Tipo de entrada
     aAlfa = aAlfa + "0";                          || Mensaje

     |SUB_EJECUTA_NP aAlfa;
|FPRC;

|PROCESO Boton341;
     |LEE 000#0=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     nEjer = 2000 + #0#2;

     aAlfa = "|:agieos/dsmo3401;";
     aAlfa = aAlfa + (("00000" + #0#0) % -105);    || Empresa
     aAlfa = aAlfa + (("0000" + nEjer) % -104);    || Ejercicio
     aAlfa = aAlfa + (("00" + #0#1) % -102);       || Periodo
     aAlfa = aAlfa + "340";                        || Modelo
     aAlfa = aAlfa + "00";                         || Complemento
     aAlfa = aAlfa + "00";                         || Actividad
     aAlfa = aAlfa + "04";                         || Tipo de entrada
     aAlfa = aAlfa + "0";                          || Mensaje

     |SUB_EJECUTA_NP aAlfa;
|FPRC;

|PROCESO Tipo14;  |TIPO 14;
     |SI nBoton340 ! -1;
         |SI #5#51 = "N";
             |ESTADO_CONTROL nBoton340, "DISABLE";
             |ESTADO_CONTROL nBoton341, "DISABLE";
             |ACABA;
         |FINSI;

         nModo = (FEntrada / 100) ! 0;
         |SI nModo ! 4;
             |ESTADO_CONTROL nBoton340, "DISABLE";
             |ESTADO_CONTROL nBoton341, "DISABLE";
             |ACABA;
         |FINSI;

         |LEE 000#0=;
         |SI FSalida ! 0;
               |ESTADO_CONTROL nBoton340, "DISABLE";
               |ESTADO_CONTROL nBoton341, "DISABLE";
         |SINO;
               |ESTADO_CONTROL nBoton340, "ENABLE";
               |ESTADO_CONTROL nBoton341, "ENABLE";
         |FINSI;

         |ACABA;
     |FINSI;

     |CONTROL_SIMPLE 0, "LBOTON,Modelo 340 Actividad", 0948, 0964, Boton340;
     nBoton340 = FSalida;

     |CONTROL_SIMPLE 0, "LBOTON,Modelo 340 Empresa", 0965, 0979, Boton341;
     nBoton341 = FSalida;

     |ESTADO_CONTROL nBoton340, "DISABLE";
     |ESTADO_CONTROL nBoton341, "DISABLE";
|FPRC;

|PROCESO Lee100_303;
     |SI nError = 1;  |ACABA;  |FINSI;

     |SI #0nCampo1 ! 0;  |O #0nCampo2 ! 0;
         #100#0 = "303";
         #100#1 = #0#0;
         #100#2 = #0#45;
         #100#3 = #0#1;
         #100#4 = #0#2;
         #100#5 = nCampo1;
         |LEE 000#100=;
         |SI FSalida ! 0;  |DDEFECTO #100;  |FINSI;

         #98#4 = #100#6 + #100#9  + #100#12 + #100#15 + #100#18 + #100#21 + #100#24;
         #98#6 = #100#8 + #100#11 + #100#14 + #100#17 + #100#20 + #100#23 + #100#26;

         |SI #1000nCampo1 ! #98#4;  |O #1000nCampo2 ! #98#6;
             |MENAV "0000No se puede cancelar la ficha.";
             aAlfa ="0000Se ha introducido desglose de compras y no coincide con los totales de compras.";
             |SI nCampo1 < 12;
                 aAlfa = "0000Se ha introducido desglose de ventas y no coincide con los totales de ventas.";
             |FINSI;
             |MENAV aAlfa;
             nError = 1;
             |ERROR;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO Tipo12;  |TIPO 12;
     |SI FSalida ! 7;  |ACABA;  |FINSI;

     |HAZ CalTipoyPer;

     |SI nEjer [ 2008;  |ACABA;  |FINSI;

     |DBASE aBase;  |QBF aBase;
     aMas = aBase + "def/eosm1303.mas";
     |CARGA_DEF aMas, regiva@;

     |ABRE #1000;
     #1000#0  = #0#0;
     #1000#45 = #0#45;
     #1000#1  = #0#1;
     #1000#2  = #0#2;
     |LEE 000#1000=;
     |SI FSalida ! 0;  |DDEFECTO #1000;  |FINSI;
     |CIERRA #1000;

     |ABRE #100;
     nError  = 0;

     nCampo1 = 70;  nCampo2 = 21;   |HAZ Lee100_303;
     nCampo1 = 71;  nCampo2 = 22;   |HAZ Lee100_303;
     nCampo1 = 72;  nCampo2 = 51;   |HAZ Lee100_303;
     nCampo1 = 75;  nCampo2 = 43;   |HAZ Lee100_303;
     nCampo1 = 76;  nCampo2 = 44;   |HAZ Lee100_303;
     nCampo1 = 77;  nCampo2 = 78;   |HAZ Lee100_303;
     |SI nSubeIVA ] 1;
         nCampo1 = 5;   nCampo2 = 7;    |HAZ Lee100_303;
         nCampo1 = 8;   nCampo2 = 10;   |HAZ Lee100_303;
         nCampo1 = 11;  nCampo2 = 13;   |HAZ Lee100_303;
     |FINSI;
     |SI nSubeIVA = 2;
         nCampo1 = 14;  nCampo2 = 16;   |HAZ Lee100_303;
         nCampo1 = 17;  nCampo2 = 19;   |HAZ Lee100_303;
         nCampo1 = 46;  nCampo2 = 48;   |HAZ Lee100_303;
     |FINSI;

     |DESCARGA_DEF #regiva@;
|FPRC;

|PROCESO Tipo90;  |TIPO 90;
     |FIN_CONTROL_WINDOWS nBoton340;
     |FIN_CONTROL_WINDOWS nBoton341;
|FPRC;

|PROCESO Graba100_Plan;
     |DDEFECTO #100;
     #100#0 = "PLA";
     #100#1 = #44#0;
     #100#2 = #44#83;
     #100#3 = #44#2;
     #100#4 = #44#1;
     #100#5 = nCampo1;
     #100#6 = #44nCampo1;
     #100#7 = nIVA;
     #100#8 = #44nCampo2;

     |GRABA 020#100n;
     |LIBERA #100;
|FPRC;

|PROCESO eosplan1;
  |PARA nCampo1 = 19;  |SI nCampo1 [ 21;  |HACIENDO nCampo1 = nCampo1 + 1;
        nCampo2 = nCampo1 + 21;
        |SI #44nCampo1 ! 0;  |O #44nCampo2 ! 0;
            nCampo3 = nCampo1 - 16;
            nIVA = #44nCampo3;
            |HAZ Graba100_Plan;
        |FINSI;
  |SIGUE;

  |SI nSw = 0;
     |PARA nCampo1 = 26;  |SI nCampo1 [ 34;  |HACIENDO nCampo1 = nCampo1 + 1;
           nCampo2 = nCampo1 + 104;
           |SI #44nCampo1 ! 0;  |O #44nCampo2 ! 0;
               nIVA = ((#44nCampo2 / #44nCampo1) * 100) ! 2;
              |SI nIVA ! 16;  |Y nIVA ! 4;  |Y nIVA ! 7;
                  |Y nIVA ! 0.5; |Y nIVA ! 1;
                  |Y nIVA ! 18; |Y nIVA ! 8;
                  |Y nIVA ! 21; |Y nIVA ! 10;
                  |Y nIVA ! 1.40; |Y nIVA ! 5.20;
                   nIVA = 0;
               |FINSI;

               |HAZ Graba100_Plan;
           |FINSI;
     |SIGUE;

     nCampo1 = 35;
     nCampo2 = 96;
     |SI #44nCampo1 ! 0;  |O #44nCampo2 ! 0;
         nIVA = ((#44nCampo2 / #44nCampo1) * 100) ! 2;
         |SI nIVA ! 16;  |Y nIVA ! 4;  |Y nIVA ! 7;
             |Y nIVA ! 0.5; |Y nIVA ! 1;
              |Y nIVA ! 18; |Y nIVA ! 8;
              |Y nIVA ! 21; |Y nIVA ! 10;
              |Y nIVA ! 1.40; |Y nIVA ! 5.20;
            nIVA = 0;
         |FINSI;

         |HAZ Graba100_Plan;
      |FINSI;

     |PARA nCampo1 = 108;  |SI nCampo1 [ 118;  |HACIENDO nCampo1 = nCampo1 + 1;
           nCampo2 = nCampo1 + 31;
           |SI #44nCampo1 ! 0;  |O #44nCampo2 ! 0;
               nIVA = ((#44nCampo2 / #44nCampo1) * 100) ! 2;
            |SI nIVA ! 16;  |Y nIVA ! 4;  |Y nIVA ! 7;
                |Y nIVA ! 0.5; |Y nIVA ! 1;
                 |Y nIVA ! 18; |Y nIVA ! 8;
                 |Y nIVA ! 21; |Y nIVA ! 10;
                 |Y nIVA ! 1.40; |Y nIVA ! 5.20;
                   nIVA = 0;
               |FINSI;

               |HAZ Graba100_Plan;
           |FINSI;
     |SIGUE;

  |FINSI;
|FPRC;

|DEFBUCLE eosplan1;
     #44#1;
     ;
     00001, 0, 09, 0;
     99999, 9, 10, 9;
     ;
     NULL, eosplan1;
|FIN;

|PROCESO Graba100_303;
     |SI #0nCampo1 = 0;  |Y #0nCampo2 = 0;
         |ACABA;
     |FINSI;

     nIVA = ((#0nCampo2 / #0nCampo1) * 100) ! 2;
     |SI nIVA ! 16;  |Y nIVA ! 4;  |Y nIVA ! 7;
         |Y nIVA ! 0.5; |Y nIVA ! 1;
           |Y nIVA ! 18; |Y nIVA ! 8;
           |Y nIVA ! 21; |Y nIVA ! 10;
           |Y nIVA ! 1.40; |Y nIVA ! 5.20;
         nIVA = 0;
     |FINSI;
     |SI nCampo1 [ 11; nIVA = #0nCampo3; |FINSI;

     |DDEFECTO #100;
     #100#0 = "303";
     #100#1 = #0#0;
     #100#2 = #0#45;
     #100#3 = #0#1;
     #100#4 = #0#2;
     #100#5 = nCampo1;
     #100#6 = #0nCampo1;
     #100#7 = nIVA;
     #100#8 = #0nCampo2;

     |GRABA 020#100n;
     |LIBERA #100;
|FPRC;

|PROCESO eosm1303V;
     nCampo1 = 5;   nCampo2 = 7;  nCampo3 = 6;  |HAZ Graba100_303;
     nCampo1 = 8;   nCampo2 = 10; nCampo3 = 9;  |HAZ Graba100_303;
     nCampo1 = 11;  nCampo2 = 13; nCampo3 = 12; |HAZ Graba100_303;

     nCampo1 = 14;  nCampo2 = 15; nCampo3 = 16; |HAZ Graba100_303;
     nCampo1 = 17;  nCampo2 = 18; nCampo3 = 19; |HAZ Graba100_303;
     nCampo1 = 46;  nCampo2 = 47; nCampo3 = 48; |HAZ Graba100_303;
     |SI nSw = 0;
         nCampo1 = 70;  nCampo2 = 21;   |HAZ Graba100_303;
         nCampo1 = 71;  nCampo2 = 22;   |HAZ Graba100_303;
         nCampo1 = 72;  nCampo2 = 51;   |HAZ Graba100_303;
         nCampo1 = 75;  nCampo2 = 43;   |HAZ Graba100_303;
         nCampo1 = 76;  nCampo2 = 44;   |HAZ Graba100_303;
         nCampo1 = 77;  nCampo2 = 78;   |HAZ Graba100_303;
    |FINSI;
|FPRC;

|DEFBUCLE eosm1303V;
     #0#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, eosm1303V;
|FIN;

|PROCESO eosma100C;
     nSw = 1;
     |ERROR10;
|FPRC;

|DEFBUCLE eosma100C;
     #100#1;
     ;
     "   ", 00001, 1, 01, 01, 005;
     "zzz", 99999, 6, 12, 99, 011;
     e;
     NULL, eosma100C;
|FIN;

|PROCESO Tipo88;  |TIPO 88;
     nSw = 0;
     |ABRE #100;
     |LEE 000#100p;
     |SI FSalida ! 0;
         |MENSA "0000 Grabando desglose IVA Compra, espere un momento.";
         |BUCLE eosplan1;
         |BUCLE eosm1303V;
         |BLIN 04;
     |SINO;
         |CIERRA #100;
         nSw = 0;
         |BUCLE eosma100C;
         |SI nSw = 0;
             |MENSA "0000 Grabando desglose IVA Ventas, espere un momento.";
             |ABRE #100;
             nSw = 1;
             |BUCLE eosplan1;
             |BUCLE eosm1303V;
             |BLIN 04;
         |FINSI;
     |FINSI;

     |CIERRA #100;
|FPRC;

|PROCESO Anula2014; |TIPO 0;
     |SI #0Campo ] 14;  |Y #0Campo [ 79;
         |MENAV "0000Mantenimiento en desuso, use el programa para 2014 y posteriores.";
         |ERROR;
     |FINSI;
|FPRC;
