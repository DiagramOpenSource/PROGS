|FICHEROS;
     eosm0093 #93;
     eosm0094 #94;

     espejoM@ #999;

     eosactiv #114;
     eoscomul #102;
|FIN;

|VARIABLES;
   nRendimiento = 0;
   nTopeHM = 0;
   nTopeM  = 0;
   nTopeV  = 0;
   nTopeV0 = 0;
   nTopeV1 = 0;
   nNume1  = 0;
   nNume2  = 0;
   nTotalM = 0;
   nTotalV = 0;
   nDiaT   = 0;
   nTDias  = 0;
   aAlfa1  = "";
   aAlfa2  = "";
   aAlfa3  = "";
   fFechaIni = @;
   fFechaFin = @;
   fFecha    = @;

   nEjer     = 0;
   nEjer4    = 0;
   aBase     = "";
   aMas      = "";
   nEstado   = 0;

   nEmp      = 0;
   nAct      = 0;
   nTp100    = 0;
|FIN;

|INCLUYE i_vareos;

|PROCESO dsmodAnt;
    #93#15 = #999#26;
|FPRC;

|DEFBUCLE dsmodAnt_130;
    #espejoM@#1004;
    ;
    enEmp130, 1, 01, nEjer;
    enEmp130, 9, 12, nEjer;
    ;
    NULL, dsmodAnt;
|FIN;

|PROCESO eosm0017Ant;
    |ABRE #999;
    |DDEFECTO #999;
    #999#0 = nEmp;
    #999#1 = nAct;
    #999#5 = nEjer;
    |LEE 041#999=;
    |SI FSalida = 0;
        #93#15 = #93#15 + (#999#115 % nTp100);
    |FINSI;
    |CIERRA #999;
|FPRC;

|PROCESO dsmom001;
     nEmp   = #102#0;
     nAct   = #102#1;
     nTp100 = #102#6;
     |HAZ eosm0017Ant;
|FPRC;

|DEFBUCLE dsmom001;
   #102#2;
   ;
   #114#0, #114#1, nEjer;
   #114#0, #114#1, nEjer;
   ;
   NULL, dsmom001;
|FIN;

|PROCESO PorActividad;

  |SI #114#46 = "S"; |ACABA; |FINSI;  || Comunidad de Bienes

  |SI #114#22 ! "A"; |ACABA; |FINSI;  || no tiene modelo 131

  |SI #114#48 [ "01.01.1900";  #114#48 = fFechaIni;  |FINSI;
  |SI #114#49 [ "01.01.1900";  #114#49 = fFechaFin;  |FINSI;

  |SI #114#48 > fFechaFin;  |ACABA;  |FINSI;
  |SI #114#49 < fFechaIni;  |ACABA;  |FINSI;

  nTDias = nDiaT;

  nEmp = #114#0;
  nAct = #114#1;
  |SI #114#14 = "N";
      nTp100 = 100;
      |HAZ eosm0017Ant;
  |SINO;
      |BUCLE dsmom001;
  |FINSI;
|FPRC;

|DEFBUCLE PorActividad;
   #114#1;
   ;
   enEmp130, 01, nEjer;
   enEmp130, 99, nEjer;
   ;
   NULL, PorActividad;
|FIN;

|PROCESO Calculo93_15;
     nEjer  = #93#1 - 1;
     #93#15 = 0;

     |DBASE aBase;  |QBF aBase;

     |SI enMod130 = 130;
         aMas = aBase + "def/eosm3130.mas";
         |CARGA_DEF aMas, espejoM@;
         |SI FSalida = 0;
             |BUCLE dsmodAnt_130;
             |DESCARGA_DEF #espejoM@;
          |FINSI;
     |FINSI;

     |SI enMod130 = 131;
         nNume1 = nEjer + 2000;
         aAlfa1 = nNume1; aAlfa1 = "01.01." + aAlfa1; fFechaIni = aAlfa1;
         aAlfa1 = nNume1; aAlfa1 = "31.12." + aAlfa1; fFechaFin = aAlfa1;

         nDiaT  = 365; |SI nEjer = 16; |O nEjer = 20; |O nEjer = 24;
                          |O nEjer = 28 ; nDiaT = 366; |FINSI;


         aMas = aBase + "def/eosm0017.mas";
         |CARGA_DEF aMas, espejoM@;
         |SI FSalida = 0;
             |BUCLE PorActividad;
             |DESCARGA_DEF #espejoM@;
        |FINSI;
     |FINSI;
|FPRC;

|PROCESO eGrabadsmom093;

     |SI enSwApli = 3;
         |HAZ Calculo93_15;
         |ACABA;
     |FINSI;

     |ABRE #93;
     #93#0 = enMod130;
     #93#1 = enEje130;
     #93#2 = enEmp130;
     |LEE 141#93=;
     nEstado = FSalida;
     |SI nEstado ! 0;
         |DDEFECTO #93;
         #93#0 = enMod130;
         #93#1 = enEje130;
         #93#2 = enEmp130;
         #93#3 = eaNombreAp;
         |GRABA 020#93b;
     |FINSI;
     |SI enSwApli = 1; |O enSwApli = 6; |O nEstado ! 0;
         |HAZ Calculo93_15;
     |FINSI;
     |SI enSwApli = 2; |O enSwApli = 6;
         enImporte = #93#15;
     |FINSI;
     |SI enSwApli = 5;
         #93#15 = enImporte;
     |FINSI;
     |SI enSwApli ! 6;
         |GRABA 020#93a;
     |FINSI;
     |LIBERA #93;
     |CIERRA #93;
|FPRC;

|PROCESO Leodsmom094;
   nTotalM = nTotalM + #94#8;
   nTotalV = nTotalV + #94#9;
|FPRC;

|DEFBUCLE Leodsmom094;
   #94#1;
   10;
   #93#1, #93#2, 1, 1;
   #93#1, #93#2, 9, enPer130;
   e;
   NULL, Leodsmom094;
|FIN;

|PROCESO eLeeAplica;
     nTopeHM = 8000;
     nTopeM  = 12000;
     nTopeV  = 33007.20;
     nTopeV0 = 33007.20;
     nTopeV1 = 22000.00;

     |SI enEje130 ] 80;
         nEjer4 = 1900 + enEje130;
     |SINO;
         nEjer4 = 2000 + enEje130;
     |FINSI;

     eaAplica1 = "N";
     eaAplica2 = "N";

     |SI nEjer4 ] 2008;

|| .................. Lee Fichero de Aplicar si o no
         enApli400 = 100;
         eaAplica1 = "S";
         eaAplica2 = "N";
         enAplica3 = 0;
         |ABRE #93;
         #93#0 = enMod130;
         #93#1 = enEje130;
         #93#2 = enEmp130;
         |LEE 041#93=;
         |SI FSalida = 0;
             eaAplica1 = #93#4;
             eaAplica2 = #93#5;
             enAplica3 = #93#14;
             |SI nEjer4 < 2011; enAplica3 = 0; |FINSI;
         |SINO;
              |SI nEjer4 ] 2015; |Y enSwApli = 0;
                  |DDEFECTO #93;
                  #93#0 = enMod130;
                  #93#1 = enEje130;
                  #93#2 = enEmp130;
                  #93#3 = eaNombreAp;
                  |HAZ Calculo93_15;
                  |GRABA 020#93b;
                  eaAplica1 = #93#4;
                  eaAplica2 = #93#5;
                  enAplica3 = #93#14;
             |FINSI;
         |FINSI;
         |LIBERA #93;
         |CIERRA #93;

|| ......... Hasta el a¤o 2009 sin calculo, solo con este fichero
         |SI nEjer4 < 2010;                   |ACABA; |FINSI;

|| ......... Si no desea aplicar ni uno ni el otro, acaba ya.
         |SI eaAplica1 = "N"; |Y eaAplica2 = "N"; |ACABA; |FINSI;

         |SI enPer130 =  1; nNume1 = 6; nNume2 = 10; |FINSI;
         |SI enPer130 =  2; nNume1 = 7; nNume2 = 11; |FINSI;
         |SI enPer130 =  3; nNume1 = 8; nNume2 = 12; |FINSI;
         |SI enPer130 =  4; nNume1 = 9; nNume2 = 13; |FINSI;

         nTotalM = 0;
         nTotalV = 0;
         |BUCLE Leodsmom094;  || Sumo actividades

         |SI eaAplica1 = "S";        || Calculo para Minoracion
             |SI nEjer4 < 2015;
                 enApli400 = 100;
                 nRendimiento = (enRend_NoAgr + (enRend_Agr * 0.25)) ! 2;
                 |SI #93nNume1 ! "X";   || Si no fuerzo a Deducir S
                     |SI nRendimiento > nTopeM; |O nTotalM > nTopeM;
                         eaAplica1 = "N";
                     |SINO;
                         |SI nTotalM > nTopeHM;
                             enApli400 = (( 400 - (nTotalM - nTopeHM) * 0.1) / 4) ! 2;
                         |FINSI;
                     |FINSI;
                 |FINSI;
             |SINO;
                 enApli400 = 0;
                 |SI #93#15 [ 9000;                     enApli400 = 100; |FINSI;
                 |SI #93#15 > 9000;  |Y #93#15 [ 10000; enApli400 =  75; |FINSI;
                 |SI #93#15 > 10000; |Y #93#15 [ 11000; enApli400 =  50; |FINSI;
                 |SI #93#15 > 11000; |Y #93#15 [ 12000; enApli400 =  25; |FINSI;
             |FINSI;
         |FINSI;

         nTopeV = nTopeV0;
         |SI enAplica3 = 1; nTopeV = nTopeV1; |FINSI;
         |SI eaAplica2 = "S";  || Calculo para Minoracion
             |SI #93nNume2 ! "X";
                 |SI nTotalV > nTopeV;
                     eaAplica2 = "N";
                 |FINSI;
             |FINSI;
        |FINSI;

     |FINSI;
|FPRC;

|PROCESO eGrabadsmom094;

  |SI enEje130 ] 80;
      nEjer4 = 1900 + enEje130;
  |SINO;
      nEjer4 = 2000 + enEje130;
   |FINSI;

   |SI nEjer4 < 2010; |ACABA; |FINSI;
   |SI nEjer4 > 2014; |ACABA; |FINSI;

   nTDias = 365;
   aAlfa1 = nEjer4;
   aAlfa2 = "01.01." + aAlfa1; fFecha = aAlfa2;  ||Inicio Ejercicio

   |SI enPer130 =  1;
       nDiaT = 90; aAlfa2 = "01.01."; aAlfa3 = "31.03.";
       |SI nEjer4 = 2012; nDiaT = 91; nTDias = 366; |FINSI;
   |FINSI;
   |SI enPer130 =  2; nDiaT = 91; aAlfa2 = "01.04."; aAlfa3 = "30.06."; |FINSI;
   |SI enPer130 =  3; nDiaT = 92; aAlfa2 = "01.07."; aAlfa3 = "30.09."; |FINSI;
   |SI enPer130 =  4; nDiaT = 92; aAlfa2 = "01.10."; aAlfa3 = "31.12."; |FINSI;

   aAlfa2 = aAlfa2 + aAlfa1; fFechaIni = aAlfa2;
   aAlfa3 = aAlfa3 + aAlfa1; fFechaFin = aAlfa3;

   |SI efFec130 [ fFecha;
       |SI enPer130 ! 1; |ACABA; |FINSI;  || No lo graba, solo el Primero
   |SINO;
       |SI efFec130 > fFechaFin; |ACABA; |FINSI;
       |SI efFec130 < fFechaIni; |ACABA; |FINSI;
   |FINSI;

   |ABRE #94;
   |DDEFECTO #94;
   #94#1 = enEje130;
   #94#2 = enEmp130;
   #94#3 = enAct130;
   |LEE 101#94=;
   |SI FSalida ! 0;
       #94#1 = enEje130;
       #94#2 = enEmp130;
       #94#3 = enAct130;
       |GRABA 020#94b;
   |FINSI;

|| ... Calculo de los dias del trimestre trabajados
   nNume1 = nDiaT;
   |SI efFec130 ] fFechaIni;
       nDiaT = fFechaFin - efFec130 + 1;
   |FINSI;

   #94#0 = enMod130;
   #94#4 = efFec130;
   #94#5 = nDiaT;
   #94#6 = enRend_NoAgr;
   #94#7 = enRend_Agr;
   #94#10 = enPer130;

   |SI #94#0 = 131; |Y enDias130 ! 0; nDiaT = nNume1; |FINSI;

   nRendimiento = (#94#6 + (#94#7 * 0.25)) ! 2;
   |SI #94#0 = 130;
       #94#8 = ((nRendimiento / #94#5) * nTDias) ! 2;
   |SINO;
       #94#8 = nRendimiento;  || 131 sin elevar al a¤o
       |SI enDias130 ! nDiaT; |Y enDias130 ! 0;
           #94#8 = ((nRendimiento / enDias130) * nDiaT) ! 2;
       |FINSI;
   |FINSI;

   nRendimiento = (#94#6 + #94#7) ! 2;
   |SI #94#0 = 130;
       #94#9 = ((nRendimiento / #94#5) * nTDias) ! 2;
   |SINO;
       #94#9 = nRendimiento;  || 131 sin elevar al a¤o
       |SI enDias130 ! nDiaT; |Y enDias130 ! 0;
           #94#9 = ((nRendimiento / enDias130) * nDiaT) ! 2;
       |FINSI;
   |FINSI;

   |GRABA 020#94a;
   |LIBERA #94;
   |CIERRA #94;
|FPRC;
