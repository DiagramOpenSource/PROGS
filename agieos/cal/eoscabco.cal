|FICHEROS;
   eoscabco #0;
   eosclien #1;
   eoslinco #2;
   eosactiv #4;
   eosutili #7;
   eosclpre #9;

   &eosesp@ #102;
|FIN;

|VARIABLES;
    DEmpre  = 0;
    HEmpre  = 0;
    swpasa  = 0;
    cont    = 0;
    alfa    = "";
    fichero = "";
    anyo    = "";
    &enEjer = 0;

    varalfa = "";
    vardia  = "";
    varmes  = "";
    fecha   = "";
|FIN;

|INCLUYE i_varcaj;

|PROCESO converli1; |TIPO 14;
    cont = (FEntrada / 100) ! 0;
    |SI cont = 4; |O FEntrada = 303; |Y swpasa = 0;
       fichero = #0#0;
       fichero = "00000" + fichero;
       fichero = fichero % -105;
       anyo    = #0#4;
       anyo    = "00" + anyo;
       anyo    = anyo % -102;
       eaNCaja = fichero + anyo;
       fichero = "c" + fichero + anyo;
       |NOME_DAT #2 fichero;
       |NOME_DAT #102 fichero;
       |SI FEntrada = 303; |HAZ pidefech; |FINSI;
    |FINSI;
    |SI #0#4 > 80;
        enEjer =1900 + #0#4;
    |SINO;
        enEjer =2000 + #0#4;
    |FINSI;
|FPRC;

|PROCESO converli2; |TIPO 0;
    swpasa = 0;
    cont = (FEntrada / 100) ! 0;
    |SI cont = 1; |O cont = 2; |O cont = 3;
       swpasa  = 1;
       fichero = #0#0;
       fichero = "00000" + fichero;
       fichero = fichero % -105;
       anyo    = #0#4;
       anyo    = "00" + anyo;
       anyo    = anyo % -102;
       eaNCaja = fichero + anyo;
       fichero = "c" + fichero + anyo;
       |NOME_DAT #2 fichero;
       |NOME_DAT #102 fichero;
       |SI cont = 1; |O cont = 2; |HAZ pidefech; |FINSI;
    |FINSI;
    |SI #0#4 > 80;
        enEjer =1900 + #0#4;
    |SINO;
        enEjer =2000 + #0#4;
    |FINSI;
|FPRC;

|PROCESO empresa;
     aTipoIVA = "S";
     swpasa = 0;
     |ABRE #1;
     #1#0 = #0#0;
     |LEE 040#1=;
     |SI FSalida = 0;
         #0#1 = #1#2;  |PINTA #0#1;
         #0#2 = #1#1;  |PINTA #0#2;
         #0#4 = #1#20; |PINTA #0#4;
     |SINO;
        |MENAV "     No existe Empresa.";
        |ERROR;
     |FINSI;
     |CIERRA #1;
|FPRC;

|PROCESO pidefech; |TIPO 0;
     cont = (FEntrada / 100) ! 0;
     |SI cont = 1; |O cont = 2; |O FEntrada = 303;

        |PUSHV 0501 2380;
        |ABRE #7;
        |PINPA #0#7;
        #7#4 = #0#4;
        |PINTA #7#4;

        |ET pedir;
           alfa = #7#0;
           |SI #0#4 ] 80;
               alfa = alfa % 108;
               alfa = alfa + #0#4;
           |SINO;
               alfa = (alfa % 106) + "20";
               alfa = alfa + (("00" + #0#4) % -102);
           |FINSI;
           #7#0 = alfa;
           |ENCAMPO #0#7;
           alfa = #7#0 % -102;
           |SI alfa ! #0#4;
              |MENAV "    El a¤o no corresponde al seleccionado.";
              |VETE pedir;
           |FINSI;

           alfa = #7#1;
           |SI #0#4 ] 80;
               alfa = alfa % 108;
               alfa = alfa + #0#4;
           |SINO;
               alfa = alfa % 106 + "20";
               alfa = alfa + (("00" + #0#4) % -102);
           |FINSI;

           #7#1 = alfa;

        |ET pedir1;
           |ENCAMPO #1#7;
           alfa = #7#1 % -102;
           |SI alfa ! #0#4;
              |MENAV "    El a¤o no corresponde al seleccionado.";
              |VETE pedir1;
           |FINSI;
           |SI #7#1 < #7#0;
               |MENAV "    La fecha hasta es menor a la fecha desde";
               |VETE pedir1;
           |FINSI;
        |POPV;
     |FINSI;
|FPRC;

|PROCESO callin;
     |SI #2#0 ! #0#0;
         |BORRA 140#2a;
         |LIBERA #2;
         |ACABA;
     |FINSI;

     #4#0 = #0#0;
     #4#1 = #2#19;
     #4#2 = #0#4;
     |LEE 040#4=;
     |SI FSalida ! 0; |DDEFECTO #4; |FINSI;

     #0#5  = #0#5 + (#2#07 + #2#23 + #2#24);
     #0#6  = #0#6 + (#2#9  + #2#27 + #2#28);
     #0#7  = #0#7 + (#2#11 + #2#31 + #2#32);
     #0#9  = #0#9 + (#2#42 + #2#43 + #2#44);
     #0#8  = #0#5 + #0#6 + #0#7 - #0#9;

     |PINTA #0#5;
     |PINTA #0#6;
     |PINTA #0#7;
     |PINTA #0#8;
     |PINTA #0#9;

    |SI #2#3 = 0; |Y #2#79 [ "01.01.1900"; |ACABA; |FINSI;

    |LIBERA #2;
    |LEE 140#2=;

    |SI #2#3 ! 0;
        #9#0 = #2#0;
        #9#1 = #2#3;
        #9#2 = 2;
        |LEE 040#9=;
        |SI FSalida = 0;
            #2#12 = #9#3;
            #2#38 = #9#4;
        |SINO;
            #2#12 = "";
            #2#38 = "";
         |FINSI;
     |FINSI;

     varalfa = #2#4;
     vardia = varalfa % 102;
     varmes = varalfa % -102;

     |SI #0#4 > 80;
         fecha = vardia + "." + varmes + "." + "19" + (("00" + #0#4) % -102);
     |SINO;
         fecha = vardia + "." + varmes + "." + "20" + (("00" + #0#4) % -102);
     |FINSI;
     #2#13 = fecha;

     |GRABA 140#2a;
     |LIBERA #2;
|FPRC;

|PROCESO recalcu; |TIPO 3;
     |CONFI "2400NDesea recalcular los importes y filtrar datos Proveedores : ";
     |SI FSalida ! 0; |ACABA; |FINSI;

     |PINTA 0420,"      !!!  Recalculando !!!";
     #0#5 = 0;
     #0#6 = 0;
     #0#7 = 0;
     #0#8 = 0;
     #0#9 = 0;

     |ABRE #9;
     |ABRE #4;
     |BUCLELIN 0callin#0;
     |CIERRA #9;
     |CIERRA #4;
     |PINTA 0420,"                               ";
|FPRC;
