|FICHEROS;
     eosem130;
     dsmoy000;
     eosclien;
     eosdaban;
     eoscomul;
     eosm2130;
     eosm3130;
     eossegu4;

     t1513001;
|FIN;

|VARIABLES;
     nPeriodo     = 0;
     nCampoD      = 0;
     aLLeno40     = "zzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzz";
     aVacio40     = "                                        ";
     aAlfa        = "";
     aAlfa2       = "";
     aLong        = "";
     aModelo      = "";
     aFichero     = "";
     aDirOri      = "";
     aDirDes      = "";
     aOrigen      = "";
     aDestino     = "";
     aCaracter    = "";

     nLong        = 0;
     nComa        = 0;
     nCaracter    = 0;
     nPos         = 0;
     nConver      = 0;
     nCampo       = 0;
     nHandlePortal= 0;
     nEl27        = 0;
     nAnyo2D      = 0;
     nDEmpre      = 0;
     nHEmpre      = 0;
     nPara1       = 0;
     &enEmpresa   = 0;
     &enEjercic   = 0;
     &dia         = "";
     &mes         = "";
     &anyo        = "";

     &EURODB    = 0;

     {-1}aMoneda   = "";
         aMoneda1  = "LEER";
         aMoneda2  = "";

     Comodin = "";

     aAbre        = "";
     aModelos     = "";
     aEnlace      = "";
     aHID         = "";
     aNDC         = "";
     aIDI         = "";
     aEJF         = "";
     aMOD         = "";
     aPRG         = "";
     aTIA         = "";
     aINI         = "";
     aFIN         = "";
     aAbrePortal  = "";
     aTipo        = "";

     nHandle1     = 0;
     nHandle2     = 0;
     nTCampo      = 0;

     aAlfa1       = "";
     aUnidad      = "";

  {1}aContiene    = "";

     &eaFOrigen   = "";
     &eaFDestino  = "";
     &eaInternet  = "";
     &eaImpresora = "";
     &enNume      = 0;
     &eaAlfa      = "";
     &eaDiaB      = "";
     &eaMesB      = "";
     &eaAnyoB     = "";
     &eaModelos   = "";

     &eaEjer             = 0;
     &eaPathEmision      = "";
     &eaPathLanza        = "";
     &eaPathExplorador   = "";
     &eaPathInternet     = "";
     &eaPathAEAT         = "";
     &eaFicheroPlano     = "";
     &eaFicheroSalid     = "";
     &eaFicheroImpre     = "";
     &eaFicheroError     = "";
     &eaOpcion           = "";
     &eaNomEmpresa       = "";
     &enPresentar        = 0;
     &enErrorPortal      = 0;
     &enTecla            = 0;
     &enPer              = 0;
     &enPeriodo          = 0;
     &enHandlePortal     = 0;
     &eaPeriodo          = "";
     &eaUnidad           = "";
     &eaForzExplorador   = "";
     &eaNIF              = "";
     &enImprtMd          = 0;
     &eaVersMod          = "    ";
     &eaNIFDs            = "         ";
|FIN;

|INCLUYE i_seguim;

|PROCESO PresentaModelo_02;
     |SI eaOpcion = "1";
         eaNIF     = #t1513001#7;
         enImprtMd = #eosm3130#46;
         |HAZ PortalPDFPeriodicos;
     |FINSI;

     |SI eaOpcion = "3";
         eaFOrigen  = eaPathEmision  + eaFicheroPlano;
         eaFDestino = eaPathInternet + eaFicheroPlano;
         |HAZ CopiaFicheroPtl;
     |FINSI;
|FPRC;

|PROCESO AbrePagina;
     eaPathAEAT = eaUnidad + "\AEAT";        |RMKDIR eaPathAEAT;
     eaPathAEAT = eaPathAEAT + "\" + eaModelos;  |RMKDIR eaPathAEAT;
     eaPathAEAT = eaPathAEAT + "\" + eaEjer;  |RMKDIR eaPathAEAT;
     eaPathAEAT = eaPathAEAT + "\" + eaPeriodo;  |RMKDIR eaPathAEAT;
     eaPathAEAT = eaPathAEAT + "\";

     eaFOrigen  = eaPathEmision  + eaFicheroPlano;
     eaFDestino = eaPathAEAT + eaFicheroPlano;
     |HAZ CopiaFicheroPtl;

     |DBASE aAlfa;  |QBT aAlfa;
     eaFOrigen  = aAlfa + "hacienda/wait.exe";
     eaFDestino = eaPathAEAT + "wait.exe";
     |HAZ CopiaFicheroPtl;

     eaForzExplorador = "N";
     |HAZ Explorador;

     aAbre = eaPathAEAT + "imp" + Usuario + ".bat";
     |XABRE "WBC", aAbre, enHandlePortal;

     eaAlfa = eaPathAEAT + "wait ";
     |HAZ GrabaPortalSin;

     eaAlfa = eaPathExplorador + " " + &34;
     |HAZ GrabaPortalSin;

     eaAlfa = "https://www1.agenciatributaria.gob.es/es13/h/ie51150a.html";
     |HAZ GrabaPortalSin;

     |XCIERRA enHandlePortal;

     |RSYSTEM aAbre;

     aAlfa = eaPathExplorador > "";
     aAlfa = aAlfa - "CHROME.EXE";
     |SI FSalida ! 0;  |SLEEP 30;  |FINSI;

     aAlfa = eaPathAEAT + aFichero;
     |RFBORRA aAlfa;
     |RFBORRA aAbre;
|FPRC;

|PROCESO BuscaNombre_02;
     |QBF eaAlfa;
     aAlfa1 = "";
     aAlfa2 = "";
     aLong  = eaAlfa % 0;
     nLong  = aLong;
     nComa  = 0;

     |PARA nCaracter = 1;  |SI nCaracter [ nLong;  |HACIENDO nCaracter = nCaracter + 1;
           nPos      = (nCaracter * 100) + 1;
           aCaracter = eaAlfa % nPos;

           |SI aCaracter = ",";
               nComa = 1;
           |SINO;
               |SI nComa = 0;
                   aAlfa1 = aAlfa1 + aCaracter;
               |SINO;
                   |SI aCaracter = " ";  |Y aAlfa2 = "";
                   |SINO;
                       aAlfa2 = aAlfa2 + aCaracter;
                   |FINSI;
               |FINSI;
           |FINSI;
      |SIGUE;
|FPRC;

|PROCESO TDeclaracion2015;
     |ABRE #eosdaban;
     #eosdaban#0 = #eosclien#0;
     |LEE 000#eosdaban.=;
     |SI FSalida ! 0; |DDEFECTO #eosdaban; |FINSI;
     |CIERRA #eosdaban;

     |SI #eosm3130#46 > 0;
                                 #t1513001#6 = "I";
         |SI #eosdaban#14 = "3"; #t1513001#6 = "U";  |FINSI;
         |SI #eosdaban#14 = "6"; #t1513001#6 = "G";  |FINSI;
         |SI #eosdaban#14 ! "1";
             #t1513001#33 = #eosdaban#7;
         |FINSI;
     |SINO;
         #t1513001#6 = "N";
         |SI #eosm3130#46 ! 0;
             |SI #eosm3130#2 ! 4;  #t1513001#6 = "B";  |FINSI;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO Carga130_02;
     |DDEFECTO #t1513001;

     aAlfa         = #eosclien#1;  |QBF aAlfa;
     #t1513001#7   = ("000000000" + aAlfa) % -109;

     aAlfa = #eosclien#1 % 101;
     |SI aAlfa = "0"; |O aAlfa = "1"; |O aAlfa = "2"; |O aAlfa = "3";
      |O aAlfa = "4"; |O aAlfa = "5"; |O aAlfa = "6"; |O aAlfa = "7";
      |O aAlfa = "8"; |O aAlfa = "9"; |O aAlfa = "X"; |O aAlfa = "Y";
      |O aAlfa = "Z";
         eaAlfa = #eosclien#2;      |HAZ BuscaNombre_02;
         eaAlfa = aAlfa1;    |HAZ QuitaRaros;   #t1513001#8   = eaAlfa;
         eaAlfa = aAlfa2;    |HAZ QuitaRaros;   #t1513001#9   = eaAlfa;
     |SINO;
         eaAlfa   = #eosclien#2;  |HAZ QuitaRaros;  #t1513001#8 = eaAlfa;
                                                    #t1513001#9 = "";
     |FINSI;

     #t1513001#10  = #eosem130#2;
     #t1513001#11  = eaPeriodo;

     enNume  = #eosm3130#8;                 |HAZ Conver17;  #t1513001#12 = eaAlfa;
     enNume  = #eosm3130#8 - #eosm3130#22;  |HAZ Conver17;  #t1513001#13 = eaAlfa;
     enNume  = #eosm3130#22;                |HAZ Conver17;  #t1513001#14 = eaAlfa;

     nEl27   = 0;
     |SI #eosm3130#27 > 0;
         enNume  = #eosm3130#27;      |HAZ Conver17;  #t1513001#15 = eaAlfa;
         nEl27   = #eosm3130#27;
     |FINSI;

     enNume  = #eosm3130#28;  |HAZ Conver17;  #t1513001#16 = eaAlfa;
     enNume  = #eosm3130#25;  |HAZ Conver17;  #t1513001#17 = eaAlfa;
     enNume  = nEl27 - #eosm3130#28 - #eosm3130#25;
                              |HAZ Conver17;  #t1513001#18 = eaAlfa;

     enNume  = #eosm3130#30;  |HAZ Conver17;  #t1513001#19 = eaAlfa;
     enNume  = #eosm3130#31;  |HAZ Conver17;  #t1513001#20 = eaAlfa;
     enNume  = #eosm3130#36;  |HAZ Conver17;  #t1513001#21 = eaAlfa;
     enNume  = #eosm3130#31 - #eosm3130#36;
                              |HAZ Conver17;  #t1513001#22 = eaAlfa;

     enNume  = #eosm3130#29;  |HAZ Conver17;  #t1513001#23 = eaAlfa; ||[12]
     enNume  = #eosm3130#41;  |HAZ Conver17;  #t1513001#24 = eaAlfa; ||[13]
     enNume  = #eosm3130#42;  |HAZ Conver17;  #t1513001#25 = eaAlfa; ||[14]
     enNume  = #eosm3130#43;  |HAZ Conver17;  #t1513001#26 = eaAlfa; ||[15]
     enNume  = #eosm3130#47;  |HAZ Conver17;  #t1513001#27 = eaAlfa; ||[16]
     enNume  = #eosm3130#44;  |HAZ Conver17;  #t1513001#28 = eaAlfa; ||[17]
     enNume  = #eosm3130#45;  |HAZ Conver17;  #t1513001#29 = eaAlfa; ||[18]
     enNume  = #eosm3130#46;  |HAZ Conver17;  #t1513001#30 = eaAlfa; ||[19]

     |HAZ TDeclaracion2015;

     |SI #eosm3130#38 = "S";
         #t1513001#31 = #eosm3130#40;
         #t1513001#32 = #eosm3130#39;
     |FINSI;

     #t1513001#37 = &13 + &10;

     aAlfa = "<T1300" + #t1513001#10 + #t1513001#11 + "0000>";  |XGRABA nHandle2, aAlfa;
     aAlfa = "<AUX>";                                           |XGRABA nHandle2, aAlfa;
     aAlfa = " " * 70;                                          |XGRABA nHandle2, aAlfa;
     aAlfa = eaVersMod;                                         |XGRABA nHandle2, aAlfa;
     aAlfa = " " * 4;                                           |XGRABA nHandle2, aAlfa;
     aAlfa = eaNIFDs;                                           |XGRABA nHandle2, aAlfa;
     aAlfa = " " * 213;                                         |XGRABA nHandle2, aAlfa;
     aAlfa = "</AUX>";                                          |XGRABA nHandle2, aAlfa;

     |PARA nCampo = 1;  |SI nCampo [ 36;  |HACIENDO nCampo = nCampo + 1;
           aAlfa = #t1513001#nCampo#;
           |XGRABA nHandle2, aAlfa;
     |SIGUE;

     aAlfa = "</T1300" + #t1513001#10 + #t1513001#11 + "0000>";  |XGRABA nHandle2, aAlfa;
|FPRC;

|PROCESO eosm2130_02;
     #eosm2130#4 = #eosem130#6;
     |GRABA 140#eosm2130.a;
     |LIBERA #eosm2130;
|FPRC;

|DEFBUCLE eosm2130_02;
     #eosm2130#1;
     ;
     #eosm3130#0, 1, #eosm3130#2, #eosm3130#3;
     #eosm3130#0, 6, #eosm3130#2, #eosm3130#3;
     be;
     NULL, eosm2130_02;
|FIN;

|PROCESO eoscomul_02;
     #eosm2130#0 = #eoscomul#0;
     #eosm2130#1 = #eoscomul#1;
     #eosm2130#2 = #eosm3130#2;
     #eosm2130#3 = #eosm3130#3;
     |LEE 101#eosm2130.=;
     |SI FSalida = 0;
         #eosm2130#4 = #eosem130#6;
         |GRABA 140#eosm2130.a;
         |LIBERA #eosm2130;
     |FINSI;
|FPRC;

|DEFBUCLE eoscomul_02;
     #eoscomul#2;
     ;
     #eosm3130#0, 1, #eosm3130#3;
     #eosm3130#0, 6, #eosm3130#3;
     ;
     NULL, eoscomul_02;
|FIN;

|PROCESO GrabaTrabajo;
     |SI #eosem130#32 = "N";
         |BUCLE eosm2130_02;
     |SINO;
         |ABRE #eosm2130;
         |BUCLE eoscomul_02;
         |CIERRA #eosm2130;
     |FINSI;
|FPRC;

|PROCESO eosm3130_02;
     |SI #eosem130#33 ! "N";
         #eosclien#0 = #eosm3130#0;
         |LEE 040#eosclien.=;
         |SI FSalida ! 0; |DDEFECTO #eosclien; |FINSI;
     |FINSI;

     eaFicheroPlano = "130" + (("00000" + #eosclien#0) % -105) + ".txt";
     eaFicheroImpre = "130" + (("00000" + #eosclien#0) % -105) + ".pdf";
     eaFicheroError = "err" + (("00000" + #eosclien#0) % -105) + ".txt";

     aAlfa = eaPathLanza + eaFicheroPlano;  |RFBORRA aAlfa;
     aAlfa = eaPathLanza + eaFicheroImpre;  |RFBORRA aAlfa;
     aAlfa = eaPathLanza + eaFicheroError;  |RFBORRA aAlfa;

     aAbre = eaPathEmision + eaFicheroPlano;
     |XABRE "BW", aAbre, nHandle2;

     |HAZ Carga130_02;

     |XCIERRA nHandle2;

     |HAZ PresentaModelo_02;

     enEmpresa    = #eosclien#0;
     eaNomEmpresa = #eosclien#1;

     #eosm3130#4 = #eosem130#6;
     |GRABA 140#eosm3130.a;
     |LIBERA #eosm3130;

     |HAZ GrabaTrabajo;

     |ABRE #eossegu4;
     #eossegu4#0 = #eosclien#0;
     |LEE 101#eossegu4.=;
     |SI FSalida = 0;
         #eossegu4#1 = #eossegu4#1 + 1;
         |GRABA 020#eossegu4.a;
     |SINO;
         #eossegu4#0 = #eosclien#0;
         #eossegu4#1 = 10;
         |GRABA 140#eossegu4.n;
     |FINSI;
     |CIERRA #eossegu4;

     aAlfa    = #eosem130#2 % -102;
     se_empre = #eosclien#0;
     se_ejerc = aAlfa;
     se_perio = #eosem130#4;
     se_tipos = 130;
     se_activ = #eossegu4#1;

     |SI #eossegu4#1 = 10; |BUCLE eossegui; |FINSI;

     aAlfa    = #eosem130#2 % -102;
     se_empre = #eosclien#0;
     se_ejerc = aAlfa;
     se_perio = #eosem130#4;
     se_tipos = 130;
     se_activ = #eossegu4#1;
     se_impor = #eosm3130#29;
     se_fecha = #eosem130#6;
     se_factu = "N";
     |HAZ seguimie;
     |HAZ AlAgicli;
|FPRC;

|DEFBUCLE eosm3130_02;
     #eosm3130#1;
     5;
     #eosem130#0, 0, #eosem130#4, nAnyo2D, "N";
     #eosem130#1, 9, #eosem130#4, nAnyo2D, "N";
     be;
     NULL, eosm3130_02;
|FIN;

|PROCESO eosclien_02;
     #eosem130#0 = #eosclien#0;
     #eosem130#1 = #eosclien#0;
     |BUCLE eosm3130_02;
|FPRC;

|DEFBUCLE eosclien_02;
     #eosclien#3;
     0;
     aVacio40, nDEmpre;
     aLLeno40, nHEmpre;
     e;
     NULL, eosclien_02;
|FIN;

|PROCESO moy13002;
     |HAZ LeeUnidad;

     eaModelos  = "130";
     eaEjer     = #eosem130#2;
     enPeriodo  = #eosem130#4;
     nAnyo2D    = #eosem130#2 - 2000;

     |SI #eosem130#4 = 1; eaPeriodo = "1T"; |FINSI;
     |SI #eosem130#4 = 2; eaPeriodo = "2T"; |FINSI;
     |SI #eosem130#4 = 3; eaPeriodo = "3T"; |FINSI;
     |SI #eosem130#4 = 4; eaPeriodo = "4T"; |FINSI;

     #dsmoy000#0 = eaModelos;
     #dsmoy000#6 = eaEjer;

     eaPathLanza = eaUnidad + "\MODELOS";            |RMKDIR eaPathLanza;
     eaPathLanza = eaPathLanza + "\" + eaModelos;    |RMKDIR eaPathLanza;
     eaPathLanza = eaPathLanza + "\" + eaEjer;       |RMKDIR eaPathLanza;
     eaPathLanza = eaPathLanza + "\" + eaPeriodo;    |RMKDIR eaPathLanza;
     eaPathLanza = eaPathLanza + "\";

     |DBASE eaPathEmision;
     eaPathEmision = eaPathEmision + "emisiones";       |MKDIR eaPathEmision;
     eaPathEmision = eaPathEmision + "/" + eaModelos;   |MKDIR eaPathEmision;
     eaPathEmision = eaPathEmision + "/" + eaEjer;      |MKDIR eaPathEmision;
     eaPathEmision = eaPathEmision + "/" + eaPeriodo;   |MKDIR eaPathEmision;
     eaPathEmision = eaPathEmision + "/";

     eaPathInternet = eaUnidad + "/AEAT";                |RMKDIR eaPathInternet;
     eaPathInternet = eaPathInternet + "/INTERNET";      |RMKDIR eaPathInternet;
     eaPathInternet = eaPathInternet + "/" + eaModelos;  |RMKDIR eaPathInternet;
     eaPathInternet = eaPathInternet + "/" + eaEjer;     |RMKDIR eaPathInternet;
     eaPathInternet = eaPathInternet + "/" + eaPeriodo;  |RMKDIR eaPathInternet;
     eaPathInternet = eaPathInternet + "/";

     |HAZ Botones2015;
     |SI enTecla = 0;  |ACABA;  |FINSI;

     |SI eaOpcion = "1";
         |MENAV "0000La opcion de validacion no esta activada por hacienda.";
         |ACABA;
     |FINSI;

     |SI #eosem130#33 = "N";
         nDEmpre = #eosem130#0;
         nHEmpre = #eosem130#0;
         |BUCLE eosclien_02;
     |SINO;
         |ABRE #eosclien;
         |SI #eosem130#7 = "S";
             |BUCLE eosm3130_02;
         |SINO;
             |PARA nPara1 = 8; |SI nPara1 [ 31; |HACIENDO nPara1 = nPara1 + 1;
                   |SI #eosem130#nPara1# ! 0;
                       #eosem130#0 = #eosem130#nPara1#;
                       #eosem130#1 = #eosem130#nPara1#;
                       |BUCLE eosm3130_02;
                   |FINSI;
             |SIGUE;
         |FINSI;
         |CIERRA #eosclien;
     |FINSI;

     |SI eaOpcion = "1";
         |HAZ SacaErrorPtl;
     |FINSI;

     |SI eaOpcion = "3";
         aAlfa =  "      Los ficheros generados estan: " + eaPathInternet + " de su PC";
         |MENAV aAlfa;

/*
         |CONFI "0000SQuiere cargar la pagina de hacienda del modelo";
         |SI FSalida = 0;
             |HAZ AbrePagina;
         |FINSI;
*/
     |FINSI;
|FPRC;
