|FICHEROS;
    eosma004 #0;
    eosclien #1;
    eosactiv #2;
|FIN;

|VARIABLES;
    campo  = 0;
    cont   = 0;

    dia1   = "";
    mes1   = "";
    any1   = "";
    fec    = "";
    sw     = 0;
    dia    = 0;
    mes    = 0;
    any    = 0;
    tope   = 0;
    mod    = 0;
|FINSI;

|PROCESO antes; |TIPO 7;
campo = Campo;
|FPRC;

|PROCESO activida; |TIPO 0;
|SI campo ! 3; |ACABA; |FINSI;

|ABRE #2;
#2#0 = #0#0;
#2#1 = #0#1;
#2#2 = #0#3;
|LEE 040#2=;
|SI FSalida = 0;
  |SI #2#14 = "S";
     |MENAV "     La Actividad es un Comunero";
     |ERROR;
  |FINSI;
|FINSI;
|CIERRA #2;
|FPRC;

|PROCESO periodos; |TIPO 0;
cont = (FEntrada / 100) ! 0;
|SI cont ! 1; |ACABA; |FINSI;

|SI #0#3 ] 80;
    fec  = "01.01.19" + #0#3;
|SINO;
    fec  = "01.01.20" + (("00" + #0#3) % -102);
|FINSI;
#0#6 = fec;

|SI #0#3 ] 80;
    fec  = "31.12.19" + #0#3;
|SINO;
    fec  = "31.12.20" + (("00" + #0#3) % -102);
|FINSI;
#0#7 = fec;
|PINTA #0#6;
|PINTA #0#7;
|FPRC;

|PROCESO verifech; |TIPO 0;
fec = #0Campo;
sw  = 0;

|SI fec = "          "; |ACABA; |FINSI;
|SI fec = ".........."; |ACABA; |FINSI;

dia1 = fec % 102;
mes1 = fec % 402;
any1 = fec % 704;

dia  = dia1;
mes  = mes1;
any  = any1;

|SI dia ! 0; |Y mes ! 0; |Y any ! 0;
    |SI mes [ 12; |Y mes ] 1;
        |SI mes=1;|O mes=3;|O mes=5;|O mes=7;|O mes=8;|O mes=10;|O mes=12;
            tope = 31;
        |SINO;
            |SI mes = 2;
                mod = any $ 4;
                |SI mod = 0; tope = 29; |SINO; tope = 28; |FINSI;
            |SINO;
                tope = 30;
            |FINSI;
        |FINSI;
        |SI dia [ tope; sw = 1; |FINSI;
    |FINSI;
|FINSI;

|SI sw = 0;
    |MENAV "0000ATENCION !!! Fecha erronea ...";
    |ERROR;
|FINSI;
|FPRC;
