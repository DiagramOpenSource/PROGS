|FICHEROS;
     eoszp016 #0;             || Este DEf
     eos349ca #1;             || Cabeceras 349
     eos349li #2;             || Lineas 349
     eosclien #3;             || Clientes
     eoscomul #5;             || Lineas Comuneros
     eoshacie #6;             || Datos Hacienda
     eosm0013 #100;           || Planillas Cabeceras
     eosm0014 #101;           || Planillas Lineas
     eosem349 #103;           || Emision 349
     eosw0047 #104;           || Ayuda Soporte Magnetico
|FIN;

|VARIABLES;
     Vacio20    = "                    ";
     Vacio40    = "                                        ";
     Salir      = 0;
     Opcion     = 0;
     nNume      = 0;

     Comodin    = "";
     Comodin1   = "";
     VAlfa      = "";
     VAlfa1     = "";
     VAlfa2     = "";
     VAlfa3     = "";
     VAlfa4     = "";
     VarNum     = 0;

     Unidad     = "";
     Raiz       = "";
     Sistema    = "";
     Letras     = "";
     Compl      = "";
     Refer      = "";

     Campos     = "";
     Campos1    = "";
     Campos2    = "";
     Campos3    = "";
     Campos4    = "";
     Campos5    = "";
     Campos6    = "";
     Campos7    = "";
     Campos8    = "";
     Campos9    = "";
     Campos10   = "";
     Campos11   = "";
     Campos12   = "";
     Campos13   = "";
     Campos14   = "";
     Campos15   = "";
     Campos16   = "";
     Campos17   = "";
     Campos18   = "";
     Campos19   = "";
     Campos20   = "";
     Campos21   = "";

     Clave      = "";
     Contador   = 0;
     ModoModi   = "";
     ModoModi1  = "";
     SwLee      = 0;
     Handle     = "";
     NFichero   = "";
     Fichero    = "";
     Fichero1   = "";
     FicheroD   = "";
     Directorio = "";
     Empresa    = "";

     CampoDni   = "";
     Letra1     = "";
     Letra2     = "";
     Letra3     = "";
     Letra4     = "";
     Letra5     = "";
     Letra6     = "";
     Letra7     = "";
     Letra8     = "";
     Caracter   = "";
     Carac1     = 0;
     Carac2     = 0;
     Carac3     = 0;
     Carac4     = 0;
     Carac5     = 0;
     XPara      = 0;

     NACopiar   = 0;
     sw_fisica  = 0;
     vacio_20   = "                    ";
     nCanti     = 0;
     nHandle1   = 0;
     nHandle2   = 0;
     nHandle3   = 0;
     nHandle4   = 0;
     aTecla     = "";
     aAbre      = "";
     aTercera   = "";
     aModelos   = "";
     aInternet  = "";
     aEjer      = "";

     &THojas    = 0;
     &NHojas    = 0;
     &dia       = "";
     &mes       = "";
     &anyo      = "";
     &nombrep   = "";
     &tipo_p    = "";
     &enEjercicio = 0;
     &eaMoneda    = "";
     &enCanti     = 0;
     &enAnyo      = 0;
     &eaVarDni    = "";
     &enCalcCif   = 0;


{-1} MGeneral   = "";
     MGeneral1  = "1860";
     MGeneral2  = "1";
     MGeneral3  = "4";
     MGeneral4  = "Creacion  Diskette ";
     MGeneral5  = "Emision   Portada  ";
     MGeneral6  = "Etiqueta  Diskette ";
     MGeneral7  = "Grabacion Diskette ";

     tip1 = "";
     tip2 = "";
     aTTel = "";
     aTipo = "";
     nLongitud = 0;
     aLongitud = "";
     Calc      = 0;
     Cambio    = 0;
     Redond    = 0;
     nEspa      = 0;
     nLong      = 0;
     nPos       = 0;
     aCadena    = "";
     Cadena1    = "";
     aLong      = "";
     aCaracter  = "";
     aAlfa1     = "";
     nNume1     = 0;
     aTipoSec   = "";
     aNombre    = "";
     aNomAux    = "";

     aAlfa   = "";
     aDirOri = "";
     aDirDes = "";
     SwError = 0;
     aDestino = "";
     aOrigen  = "";
     nCampo   = 0;
     &eaTipoSec = "";
     &eaTipoEmi = "";

     &eaTipoE        = "";
     &eaTipoR        = "";
     &eaTipoP        = "";
     &eaTipoC        = "";
     &eaTipoA        = "";
     &eaTipoV        = "";
     &eaModoImpre    = "";
     &eaModelos      = "";
     &eaEjer         = "";
     &eaFichDatos    = "";
     &eaFichAuxil    = "";
     &eaFichError    = "";
     &enBuenos       = 0;
     &enMalos        = 0;
     &enValidacion   = 0;
     &eaInternet     = "";
     &enError        = 0;
     &enInternet     = 1;
     &eaAlfa         = "";

{999}NEmpresas = "";
|FIN;

|INCLUYE i_anyode;
|INCLUYE i_consul;

|| ***********************************************************************
||                       PROCESO CARGA DE DATOS
|| ***********************************************************************

|PROGRAMA;
  |CLS;
  |CABEZA "Emision 349 Magn.";
  |DDEFECTO #0;
  |PINPA #0#0;
  |ABRE #0;
  |LEE 101#0p;
  |SI FSalida ! 0;
      |GRABA 020#0b;
  |FINSI;
  |PINDA #0#0;
  |ENDATOS #1#0;
  |SI FSalida = 0;
      |GRABA 020#0a;
  |FINSI;
  |LIBERA #0;
  |CIERRA #0;
|FPRO;

|PROCESO EsSust; |TIPO 0;
 |SI #0#29 = "S";
     |C_M #0#30,1,"S";
 |SINO;
     |C_M #0#30,1,"N";
     #0#30 = "0000000000000"; |PINTA #0#30;
 |FINSI;
|FPRC;

|PROCESO CogeMoneda;  |TIPO 7;
     |HAZ PintaMoneda;
     #0#28 = eaMoneda;
     |SI #0#2 ] 80;  |O #0#2 = 0;  #0#28 = "PESETAS"; |FINSI;
     |SI #0#2 = 1;   |Y #0#25 [ 3; #0#28 = "PESETAS"; |FINSI;
     |PINTA #0#28;
|FPRC;

|PROCESO Relleno;
  |SI Campo = 6;
      VAlfa = #0Campo; |QBT VAlfa;
      VAlfa = ("00000" + VAlfa) % -105;
  |FINSI;

  #0Campo = VAlfa;  |PINTA #0Campo;
|FPRC;

|PROCESO Mayus; |TIPO 0;
  #0Campo = #0Campo > #0Campo;
  |PINTA #0Campo;
|FPRC;

|PROCESO PintaAnyo; |TIPO 7;
  |HAZ i_anyodef;
  #0#2 = i_anyodef;
  |SI #0#2 < 0;  #0#2 = 99;  |FINSI;
  |PINTA #0#2;
|FPRC;

|PROCESO Individual; |TIPO 0;
  |SI #0#22 = "I";
      ModoModi  = "N";
      ModoModi1 = "S";
      #0#23    = 0;   |PINTA #0#23;
      |C_M #0#0, 1, "S";
  |FINSI;

  |SI #0#22 = "C"; ModoModi = "S"; ModoModi1 = "S"; |FINSI;
  |SI #0#22 = "L"; ModoModi = "N"; ModoModi1 = "N"; |FINSI;

  |C_M #0#01, 1, ModoModi;
  |C_M #0#03, 1, ModoModi;
  |C_M #0#04, 1, ModoModi;
  |C_M #0#05, 1, ModoModi;
  |C_M #0#06, 1, ModoModi;
  |C_M #0#07, 1, ModoModi;
  |C_M #0#08, 1, ModoModi;
  |C_M #0#09, 1, ModoModi;
  |C_M #0#11, 1, ModoModi1;
  |C_M #0#12, 1, ModoModi;
  |C_M #0#16, 1, ModoModi;
  |C_M #0#17, 1, ModoModi;
  |C_M #0#18, 1, ModoModi;
  |C_M #0#23, 1, ModoModi;

  |SI #0#22 = "L";  |C_M #0#23, 1, "S";  |FINSI;
|FPRC;

|PROCESO PonIgual;
  |SI #0#22 = "C";  |ACABA;  |FINSI;

  |SI #0#22 = "L";
      #0#9  = "";    |PINTA #0#9;              || NIF
      #0#3  = "";    |PINTA #0#3;              || Apellidos y Nombre
      #0#4  = "";    |PINTA #0#4;              || SG
      #0#5  = "";    |PINTA #0#5;              || Calle
      #0#6  = "";    |PINTA #0#6;              || Numero
      #0#11 = "";    |PINTA #0#11;             || Telefono
      #0#7  = "";    |PINTA #0#7;              || Municipio
      #0#8  = "";    |PINTA #0#8;              || Provincia
      #0#17 = 0;     |PINTA #0#17;             || Codigo Postal 1
      #0#18 = 0;     |PINTA #0#18;             || Codigo Postal 2
      #0#12 = 0;     |PINTA #0#12;             || Delegacion de Hacienda
      #0#16 = 0;     |PINTA #0#16;             || Administracion de Hacienda
  |FINSI;

  |SI #0#22 ! "I";  |ACABA;  |FINSI;

  #0#1 = #0#0;  |PINTA #0#1;
  |ABRE #1;
  #1#0 = #0#0;
  #1#1 = #0#25;
  #1#2 = #0#2;
  |LEE 040#1=;
  |SI FSalida ! 0;
      |CIERRA #1;
      |MENAV "     No Existe Movimientos en el Modelo 349 con esta Empresa";
      |ERROR;
      |ACABA;
  |FINSI;
  |CIERRA #1;

  |ABRE #3;
  #3#0 = #0#0;
  |LEE 040#3=;
  |SI FSalida ! 0;  |DDEFECTO #3;  |FINSI;
  |CIERRA #3;

  CampoDni = #3#1;  |HAZ LetraDni;
  #0#9  = CampoDni; |PINTA #0#9;              || NIF
  #0#3  = #3#2;     |PINTA #0#3;              || Apellidos y Nombre
  #0#4  = #3#4;     |PINTA #0#4;              || SG
  #0#5  = #3#5;     |PINTA #0#5;              || Calle
  #0#6  = #3#6;     |PINTA #0#6;              || Numero
  #0#31 = #3#7;     |PINTA #0#31;             || Escalera
  #0#32 = #3#8;     |PINTA #0#32;             || Piso
  #0#33 = #3#9;     |PINTA #0#33;             || Puerta
  #0#11 = #3#14;    |PINTA #0#11;             || Telefono
  #0#7  = #3#13;    |PINTA #0#7;              || Municipio
  #0#8  = #3#12;    |PINTA #0#8;              || Provincia
  #0#17 = #3#10;    |PINTA #0#17;             || Codigo Postal 1
  #0#18 = #3#11;    |PINTA #0#18;             || Codigo Postal 2
  #0#12 = #3#18;    |PINTA #0#12;             || Delegacion de Hacienda
  #0#16 = #3#19;    |PINTA #0#16;             || Administracion de Hacienda
|FPRC;

|PROCESO Planilla;
  |SI #0#22 = "I";  |ACABA;  |FINSI;

  |SI #0#23 = 0;
      |C_M #0#0, 1, "S";
      |C_M #0#1, 1, "S";
  |SINO;
      |C_M #0#0, 1, "N";  #0#0 =     1;  |PINTA #0#0;
      |C_M #0#1, 1, "N";  #0#1 = 99999;  |PINTA #0#1;

      |ABRE #100;
      #100#0 = #0#23;
      |LEE 040#100=;
      |SI FSalida ! 0;
          |MENAV "       No Existe la Planilla";
          |ERROR;
      |FINSI;
      |CIERRA #100;
  |FINSI;
|FPRC;

|PROCESO LetraDni;
     eaVarDni   = CampoDni;
     enCalcCif  = 3;
     |HAZ CalculoDNI;
     CampoDni = eaVarDni;
     CampoDni = ("000000000" + CampoDni) % -109;
|FPRC;

|| ***********************************************************************
||                       IMPRESION DE LA PORTADA
|| ***********************************************************************

|PROCESO FechaP;
  dia  = #0#15 % 102;
  mes  = #0#15 % 402;
  anyo = #0#15 % 704;

  |SI mes = "01";  mes = "ENERO     ";  |FINSI;
  |SI mes = "02";  mes = "FEBRERO   ";  |FINSI;
  |SI mes = "03";  mes = "MARZO     ";  |FINSI;
  |SI mes = "04";  mes = "ABRIL     ";  |FINSI;
  |SI mes = "05";  mes = "MAYO      ";  |FINSI;
  |SI mes = "06";  mes = "JUNIO     ";  |FINSI;
  |SI mes = "07";  mes = "JULIO     ";  |FINSI;
  |SI mes = "08";  mes = "AGOSTO    ";  |FINSI;
  |SI mes = "09";  mes = "SEPTIEMBRE";  |FINSI;
  |SI mes = "10";  mes = "OCTUBRE   ";  |FINSI;
  |SI mes = "11";  mes = "NOVIEMBRE ";  |FINSI;
  |SI mes = "12";  mes = "DICIEMBRE ";  |FINSI;
|FPRC;

|PROCESO Representante;
  #3#0 = #1#0;
  |LEE 040#3=;
  |SI FSalida ! 0; |DDEFECTO #3; |FINSI;

  nombrep   = "";
  sw_fisica = 0;

  #5#0 = #3#0;
  #5#1 = 1;
  #5#2 = 1;
  #5#7 = #0#2;
  |LEE 040#5];
  |SI FSalida = 0; |Y #5#0 = #3#0; |Y #5#7 = #0#2;
      #3#0 = #5#3;
      |LEE 040#3=;
      |SI FSalida = 0; sw_fisica = 1; nombrep = #3#2; |FINSI;
  |FINSI;

  |SI sw_fisica = 1; |ACABA; |FINSI;

  #3#0 = #1#0;
  |LEE 040#3=;
  |SI FSalida ! 0; |DDEFECTO #3; |FINSI;

  nombrep = #3#2;

  |SI #3#24 ! vacio_20; |O #3#25 ! vacio_20;
      nombrep = #3#24 + #3#25;
      |ACABA;
  |FINSI;

  |SI #3#29 ! vacio_20; |O #3#30 ! vacio_20;
      nombrep = #3#29 + #3#30;
      |ACABA;
  |FINSI;

  |SI #3#34 ! vacio_20; |O #3#35 ! vacio_20;
       nombrep = #3#34 + #3#35;
       |ACABA;
  |FINSI;
|FPRC;

|PROCESO LeePortadaP;
  #103#27 = #0#24;

  |SI #0#23 ! 0;
      #101#0 = #0#23;
      #101#1 = #1#0;
      |LEE 040#101=;
      |SI FSalida ! 0;  |ACABA;  |FINSI;
  |FINSI;

  |SI SwLee = 0;
      #103#35 = #103#35 + 1;
      #103#36 = #103#36 + (#1#6 + #1#8 + #1#10 + #1#11);
      |ACABA;
  |FINSI;

  |DDEFECTO #103;
  #103#25 = #0#2;                              || A¤o Emision
  #103#26 = #0#25;
  #103#37 = #1#0;
  #103#30 = #1#6;
  #103#31 = #1#8;
  #103#32 = #1#10 + #1#11;
  #103#33 = #1#7;
  #103#34 = #1#9;
  #103#35 = 0;
  #103#36 = 0;
  #103#38 = "";                                || Nombre Representante
  #103#39 = "";
  #103#40 = "";
  #103#41 = "";
  #103#42 = "";

  |SI #0#22 = "I";  #103#39 = "X";  |FINSI;    || Soporte Individual
  |SI #0#22 = "C";  #103#40 = "X";  |FINSI;    || Soporte Colectivo Declarante
  |SI #0#22 ! "L";  #103#42 = "X";  |FINSI;    || Soporte Colectivo Declarante

  |HAZ Representante;

  |SI enEjercicio [ 2002;
      |PRINT;
      |PIEINF;
  |SINO;
      aAlfa1 = "1";
      aAlfa1 = aAlfa1 + "349";
      aAlfa1 = aAlfa1 + enEjercicio;
      aAlfa1 = aAlfa1 + (#3#1 % 109);
      aAlfa1 = aAlfa1 + ("00"  + #3#18) % -102;
      aAlfa1 = aAlfa1 + ("000" + #3#19) % -103;
      aAlfa1 = aAlfa1 + #3#4;

      aAlfa  = #3#5; aTipo = "A"; nLongitud = 30; |HAZ AnalizaCadena;
      aAlfa1 = aAlfa1 + (aAlfa % 120);

      nNume = #3#6; aAlfa1 = aAlfa1 + (("00000" + nNume) % -105);

      aAlfa  = #3#13; aTipo = "A"; nLongitud = 25; |HAZ AnalizaCadena;
      aAlfa1 = aAlfa1 + (#3#13 % 112);

      aAlfa1 = aAlfa1 + ("00"  + #3#10) % -102;
      aAlfa1 = aAlfa1 + ((("00" + #3#10) % -102) + (("000" + #3#11) % -103));
      aAlfa1 = aAlfa1 + ((#0#15 % -104) + (#0#15 % 402) + (#0#15 % 102));

      aAlfa  = nombrep; aTipo = "A"; nLongitud = 40; |HAZ AnalizaCadena;
      aAlfa1 = aAlfa1 + ((aAlfa + (" " * 30)) % 130);

      aAlfa  = #103#27; aTipo = "A"; nLongitud = 20; |HAZ AnalizaCadena;
      aAlfa1 = aAlfa1 + (aAlfa + (" " * 5));

      aAlfa1 = aAlfa1 + ((("00000" + #3#0) % -105) + (" " * 15));
      aAlfa1 = aAlfa1 + &13 + &10;

      |XGRABA nHandle3, aAlfa1;
  |FINSI;

  #1#4 = #0#15;
  |GRABA 140#1a;
  |LIBERA #1;
|FPRC;

|DEFBUCLE eos349caP;
  #1#1;
  ;
  #0#0, #0#2, #0#25;
  #0#1, #0#2, #0#25;
  be;
  NULL, LeePortadaP;
|FIN;

|PROCESO Validacion_2007;
     eaTipoE =  "CE"   + enEjercicio + ".TXT";
     eaTipoR =  "CCC"  + (("00000" + #3#0) % -105) + ".TXT";
     eaTipoV =  "S";
     |HAZ LineaShellJava;

     eaFichError = "ccc";
     eaFichDatos = "ce" + enEjercicio + ".TXT";
     eaFichAuxil = "cb" + enEjercicio + ".TXT";
     enInternet  = 1;
     |HAZ BuscaErroresL;
     |SI enError ! 0;
         |MENAV "      Ha habido Errores, arreglelos y vuelva a emitir.";
         |ACABA;
     |FINSI;
|FPRC;

|PROCESO Validacion;
     |SI enEjercicio [ 2005;
         aTercera = "N";
         |CONFI "0000SDesea la impresion de la tercera hoja  ";
         |SI FSalida = 0;  aTercera = "S";  |FINSI;
     |FINSI;

     aAbre = "\MODELOS\349\" + enEjercicio + "\imprime.bat";
     |XABRE "CBW", aAbre, nHandle4;

     aAlfa = "CD \" + &13 + &10;                 |XGRABA nHandle4, aAlfa;
     aAlfa = "CD MODELOS" + &13 + &10;           |XGRABA nHandle4, aAlfa;
     aAlfa = "CD 349" + &13 + &10;               |XGRABA nHandle4, aAlfa;
     aAlfa = "CD " + enEjercicio + &13 + &10;    |XGRABA nHandle4, aAlfa;

     aAlfa = "ZPR" + aEjer + "349.EXE -Y" + &13 + &10;      |XGRABA nHandle4, aAlfa;

     |SI enEjercicio [ 2005;
         aAlfa = "wait vs349 /E:CE" + enEjercicio + ".TXT";
         aAlfa = aAlfa + " /R:CC" + enEjercicio + ".TXT ";
         aAlfa = aAlfa + "/I: ";
         aAlfa = aAlfa + "/C:" + aTercera + " ";
         aAlfa = aAlfa + "/F:FLAG.TXT ";
         aAlfa = aAlfa + "/A:CB" + enEjercicio + ".TXT";
         aAlfa = aAlfa + &13 + &10;
     |SINO;
         aAlfa = "wait vi349 /E:CE" + enEjercicio + ".TXT";
         aAlfa = aAlfa + " /R:CC" + enEjercicio + ".TXT ";
         aAlfa = aAlfa + "/F:FLAG.TXT ";
         aAlfa = aAlfa + &13 + &10;
     |FINSI;
     |XGRABA nHandle4, aAlfa;

     aAlfa = "DEL *.DLL" + &13 + &10;
     |XGRABA nHandle4, aAlfa;

     aAlfa = "DEL *.TAB" + &13 + &10;
     |XGRABA nHandle4, aAlfa;

     aAlfa = "DEL *.WMF" + &13 + &10;
     |XGRABA nHandle4, aAlfa;

     aAlfa = "DEL *.EXE" + &13 + &10;
     |XGRABA nHandle4, aAlfa;

     aAlfa = "DEL ERROR349.TXT" + &13 + &10;
     |XGRABA nHandle4, aAlfa;

     |XCIERRA nHandle4;

     |DBASE aModelos;  |QBT aModelos;

     aOrigen  = aModelos + "hacienda/zpr" + aEjer + "349.exe";
     aDestino = "/MODELOS/349/" + enEjercicio + "/zpr" + aEjer + "349.exe";

     aAlfa = "";
     |IP_REMOTO aAlfa;  |QBT aAlfa;
     |SI aAlfa ! "";
         |ENVIA_FICHERO aOrigen, aDestino;
     |SINO;
         |COPIA_FICHERO aOrigen, aDestino;
     |FINSI;

     |RSYSTEM aAbre;
     |RFBORRA aAbre;

     enAnyo    = enEjercicio;
     eaTipoSec = "349";
     eaTipoEmi = "ERRORES";
     |SUB_EJECUTA "eosz9999";
|FPRC;

|PROCESO Portada;
  |HAZ FechaP;

  |SI enEjercicio [ 2002;
      |IMPRE 0;
      |SI FSalida ! 0;  |ACABA;  |FINSI;

      |INFOR "eospo349";
      |SI FSalida ! 0;  |FINIMP;  |ACABA;  |FINSI;
  |SINO;
      aAbre = "\MODELOS\349\" + enEjercicio + "\CB" + enEjercicio + ".TXT";
      |XABRE "CBW", aAbre, nHandle3;
  |FINSI;

  tipo_p = "T";
  |SI #0#25 = 0; tipo_p = "A"; |FINSI;

  #103#35    = 0;
  #103#36    = 0;
  |SI #0#22 = "C";
      SwLee = 0;  |BUCLE eos349caP;

      |DDEFECTO #3;
      |DDEFECTO #1;
      #103#25 = #0#2;                   || A¤o Emision

      #103#31 = "";                     || Impreso
      #103#32 = "";                     || Soporte Individual
      #103#33 = "X";                    || Soporte Colectivo Presentador
      #103#34 = "";                     || Soporte Colectivo Declarante
      #103#37 = 0;                      || empresa
      #103#38 = "";                     || Nombre Representante

      #3#0  = 0;                        || Codigo Cliente
      CampoDni = #0#9;  |HAZ LetraDni;
      #3#1  = CampoDni;                 || NIF
      #3#2  = #0#3;                     || Apellidos y Nombre
      #3#4  = #0#4;                     || SG
      #3#5  = #0#5;                     || Calle
      #3#6  = #0#6;                     || Numero
      #3#14 = #0#11;                    || Telefono
      #3#13 = #0#7;                     || Municipio
      #3#12 = #0#8;                     || Provincia
      #3#10 = #0#17;                    || Codigo Postal 1
      #3#11 = #0#18;                    || Codigo Postal 2
      #3#18 = #0#12;                    || Delegacion de Hacienda
      #3#19 = #0#16;                    || Administracion de Hacienda

      |SI enEjercicio [ 2002;
          |PRINT;
          |PIEINF;
      |SINO;
          aAlfa1 = "0";
          aAlfa1 = aAlfa1 + "349";
          aAlfa1 = aAlfa1 + enEjercicio;
          aAlfa1 = aAlfa1 + (#3#1 % 109);
          aAlfa1 = aAlfa1 + ("00"  + #3#18) % -102;
          aAlfa1 = aAlfa1 + ("000" + #3#19) % -103;
          aAlfa1 = aAlfa1 + #3#4;

          aAlfa  = #3#5; aTipo = "A"; nLongitud = 30; |HAZ AnalizaCadena;
          aAlfa1 = aAlfa1 + (aAlfa % 120);

          nNume = #3#6; aAlfa1 = aAlfa1 + (("00000" + nNume) % -105);

          aAlfa  = #3#13; aTipo = "A"; nLongitud = 25; |HAZ AnalizaCadena;
          aAlfa1 = aAlfa1 + (aAlfa % 112);

          aAlfa1 = aAlfa1 + ("00"  + #3#10) % -102;
          aAlfa1 = aAlfa1 + (("00" + #3#10) % -102) + (("000" + #3#11) % -103);
          aAlfa1 = aAlfa1 + (#0#15 % -104) + (#0#15 % 402) + (#0#15 % 102);

          aAlfa  = #0#3; aTipo = "A"; nLongitud = 40; |HAZ AnalizaCadena;
          aAlfa1 = aAlfa1 + (aAlfa % 130);

          aAlfa  = #103#27; aTipo = "A"; nLongitud = 20; |HAZ AnalizaCadena;
          aAlfa1 = aAlfa1 + (aAlfa + (" " * 5));

          aAlfa1 = aAlfa1 + (" " * 20);
          aAlfa1 = aAlfa1 + &13 + &10;

          |XGRABA nHandle3, aAlfa1;
      |FINSI;
  |FINSI;

  #103#35    = 0;
  #103#36    = 0;

  SwLee = 1; |BUCLE eos349caP;

  |SI enEjercicio [ 2002;
      |FININF;
      |FINIMP;
  |SINO;
      |XCIERRA nHandle3;

      |SI enEjercicio ] 2003;  |Y enEjercicio [ 2006;
          |HAZ Validacion;
      |FINSI;

      |SI enEjercicio ] 2007;
          |HAZ Validacion_2007;
      |FINSI;
  |FINSI;
|FPRC;

|| ***********************************************************************
||                       GRABACION SECUENCIAL DISTINTOS TIPOS
|| ***********************************************************************

|PROCESO ConverCanti;
     eaMoneda = #0#28;
     enCanti  = nCanti;
     |HAZ ConverMoneda;
     nCanti   = enCanti;
|FPRC;

|PROCESO Tipo0;
  Campos1  = "0";
  Campos1 = Campos1 + "349";                        || Tipo Registro
  |SI  #0#2 > 80;
       VAlfa = #0#2;   VAlfa = "19" + (("00" + VAlfa) % -102);
  |SINO;
       VAlfa = #0#2;   VAlfa = "20" + (("00" + VAlfa) % -102);
  |FINSI;
  Campos2  = VAlfa;                                    || Ejercicio

  Campos3  = #0#9;                                     || N.I.F.
  Campos4  = #0#3;                                     || Apellidos y Nombre
  Campos5  = #0#4;                                     || SG
  Campos6  = #0#5 % 120;                               || Calle
  Campos7  = #0#6;                                     || Numero
  Campos8  = #0#31;                                    || Escalera
  Campos9  = #0#32;                                    || Piso
  Campos10 = #0#33;                                    || Puerta

  VAlfa    = ("00" + #0#17) % -102;
  Campos11 = VAlfa;                                    || Codigo Postal 1
  VAlfa    = ("000" + #0#18) % -103;
  Campos12 = VAlfa;                                    || Codigo Postal 2
  Campos13 = #0#7 % 112;                               || Municipio
  Campos14 = Campos11;                                 || Codigo Provincia

  VAlfa    = ("00000" + #103#35) % -105;               || Total Declarantes
  Campos15 = VAlfa;
  VAlfa    = ("000000000" + #103#36) % -109;           || Total Declarados
  Campos16 = VAlfa;
  Campos17 = aTTel;
  Campos18 = #0#11 % 0109;
  Campos19 = #0#26;                                    || Persona contacto

  |SI #1#1 = 0;  VAlfa = "0A";  |FINSI;
  |SI #1#1 = 1;  VAlfa = "1T";  |FINSI;
  |SI #1#1 = 2;  VAlfa = "2T";  |FINSI;
  |SI #1#1 = 3;  VAlfa = "3T";  |FINSI;
  |SI #1#1 = 4;  VAlfa = "4T";  |FINSI;
  Campos20 = VAlfa;                                    || Periodo

  Campos21 = " " * 75;

  Campos   = Campos1 + Campos2 + Campos3 + Campos4;
  Campos   = Campos   + Campos5  + Campos6  + Campos7  + Campos8  + Campos9;
  Campos   = Campos   + Campos10 + Campos11 + Campos12 + Campos13 + Campos14;
  Campos   = Campos   + Campos15 + Campos16 + Campos17 + Campos18 + Campos19;
  Campos   = Campos   + Campos20 + Campos21;
  Campos   = Campos   + &13 + &10;        || Caracter Final de Linea
  |XGRABA nHandle2, Campos;

  #104#14 = #104#14 + 1;
|FPRC;


|PROCESO Tipo1;
  Campos1  = "1";
  Campos1 = Campos1 + "349";                        || Tipo Registro
  |SI  #0#2 > 80;
       VAlfa = #0#2;   VAlfa = "19" + (("00" + VAlfa) % -102);
  |SINO;
       VAlfa = #0#2;   VAlfa = "20" + (("00" + VAlfa) % -102);
  |FINSI;
  Campos2  = VAlfa;                                    || Ejercicio

  CampoDni = #3#1;  |HAZ LetraDni;
  Campos3  = CampoDni % 109;                           || N.I.F.

  aAlfa    = #1#3; aTipo = "A"; nLongitud = 40; |HAZ AnalizaCadena;
  Campos4  = aAlfa;                                    || Apellidos y Nombre
  Campos5  = aTTel;
  Campos6  = #0#11 % 0109;
  Campos7  = #0#26;                                    || Persona contacto
  Campos8  = #0#27;                                    || Numero Justificante

  Campos9  = "  ";
  |SI #1#16 = "N"; Campos9 = "  ";  #1#17 = "0000000000000"; |FINSI;
  |SI #1#16 = "C"; Campos9 = "C ";  #1#17 = "0000000000000"; |FINSI;
  |SI #1#16 = "S"; Campos9 = " S";  |FINSI;
  Campos10 = #1#17;   ||justificante anterior

  |SI #1#1 = 0;  VAlfa = "0A";  |FINSI;
  |SI #1#1 = 1;  VAlfa = "1T";  |FINSI;
  |SI #1#1 = 2;  VAlfa = "2T";  |FINSI;
  |SI #1#1 = 3;  VAlfa = "3T";  |FINSI;
  |SI #1#1 = 4;  VAlfa = "4T";  |FINSI;
  Campos11  = VAlfa;                                    || Periodo

  nNume1   = #1#6 + #1#8;  ||Entregas + Adquisiciones
  aAlfa    = nNume1; aTipo = "N"; nLongitud = 9; |HAZ AnalizaCadena;
  Campos12 = aAlfa;                                     || Personas

  nNume1   = #1#7 + #1#9; ||Entregas + adquisiciones
  aAlfa    = nNume1; aTipo = "N"; nLongitud = 15; |HAZ AnalizaCadena;
  Campos13  = aAlfa;                                    || Cantidad

  nNume1   = #1#10 + #1#11;
  aAlfa    = nNume1; aTipo = "N"; nLongitud = 9; |HAZ AnalizaCadena;
  Campos14  = aAlfa;                                     || Personas

  nNume1   = #1#14 + #1#15;
  aAlfa    = nNume1; aTipo = "N"; nLongitud = 15; |HAZ AnalizaCadena;
  Campos15 = aAlfa;                                      || Cantidad

  Campos16 = " " * 65;

  Campos   = Campos1 + Campos2 + Campos3 + Campos4;
  Campos   = Campos   + Campos5  + Campos6  + Campos7  + Campos8  + Campos9;
  Campos   = Campos   + Campos10 + Campos11 + Campos12 + Campos13 + Campos14;
  Campos   = Campos   + Campos15 + Campos16;
  Campos   = Campos   + &13 + &10;        || Caracter Final de Linea
  |XGRABA nHandle2, Campos;
  #104#14 = #104#14 + 1;
|FPRC;

|PROCESO Tipo2_EA;
 || ... Tipo 2 de Registro para Operador Intracomunitario .................

  Campos1  = "2349";                                   || Tipo Registro

  |SI  #0#2 > 80;
       VAlfa = #0#2;   VAlfa = "19" + (("00" + VAlfa) % -102);
  |SINO;
       VAlfa = #0#2;   VAlfa = "20" + (("00" + VAlfa) % -102);
  |FINSI;
  Campos2  = VAlfa;                                    || Ejercicio

  CampoDni = #3#1;  |HAZ LetraDni;
  Campos3  = CampoDni % 109;                           || N.I.F.

  Campos4  = " " * 58;                                 || 58 posiciones blanco

  Campos5  = #2#5 % 102;
  |SI Campos5 = "GR";  Campos5 = "EL";  |FINSI;

  Campos6  = (#2#6 + "                 ") % 115;

  aAlfa    = #2#7; aTipo = "A"; nLongitud = 40; |HAZ AnalizaCadena;
  Campos7  = aAlfa;                                    || Apellidos y Nombre

  Campos8  = "A";
  |SI #2#4  = "E";  Campos8 = "E";  |FINSI;
  |SI #2#13 = "X";  Campos8 = "T";  |FINSI;

  aAlfa    = #2#8; aTipo = "N"; nLongitud = 13; |HAZ AnalizaCadena;
  Campos9  = aAlfa;

  Campos10 = " " * 4;

  Campos11 = " " * 2;

  Campos12 = " " * 13;

  Campos13 = " " * 13;

  Campos14 = " " * 72;

  Campos   = Campos1 + Campos2 + Campos3 + Campos4;
  Campos   = Campos   + Campos5  + Campos6  + Campos7  + Campos8 + Campos9;
  Campos   = Campos   + Campos10 + Campos11 + Campos12 + Campos13 + Campos14;
  Campos   = Campos   + &13 + &10;        || Caracter Final de Linea
  |XGRABA nHandle2, Campos;
  #104#14 = #104#14 + 1;
|FPRC;

|PROCESO Tipo2_RX;         ||AQUI
 || ... Tipo 2 de Registro de Rectificaciones

  Campos1  = "2349";                                   || Tipo Registro

  |SI  #0#2 > 80;
       VAlfa = #0#2;   VAlfa = "19" + (("00" + VAlfa) % -102);
  |SINO;
       VAlfa = #0#2;   VAlfa = "20" + (("00" + VAlfa) % -102);
  |FINSI;
  Campos2  = VAlfa;                                    || Ejercicio

  CampoDni = #3#1;  |HAZ LetraDni;
  Campos3  = CampoDni % 109;                           || N.I.F.

  Campos4  = " " * 58;                                  || 58 posiciones blanco

  Campos5  = #2#5 % 102;
  |SI Campos5 = "GR";  Campos5 = "EL";  |FINSI;

  Campos6  = (#2#6 + "                 ") % 115;

  aAlfa    = #2#7; aTipo = "A"; nLongitud = 40; |HAZ AnalizaCadena;
  Campos7  = aAlfa;                                    || Apellidos y Nombre

  Campos8   = "A";
  |SI #2#4  = "R";  Campos8 = "E";  |FINSI;
  |SI #2#13 = "X";  Campos8 = "T";  |FINSI;

  Campos9 = " " * 13;                 || 13 posiciones blanco

  |SI #2#11 < 80;
      nNume1 = 2000 + #2#11;
  |SINO;
      nNume1 = 1900 + #2#11;
  |FINSI;
  aAlfa = nNume1; |QBF aAlfa;
  Campos10 = aAlfa;

  |SI #2#12 = 0;  VAlfa = "0A";  |FINSI;
  |SI #2#12 = 1;  VAlfa = "1T";  |FINSI;
  |SI #2#12 = 2;  VAlfa = "2T";  |FINSI;
  |SI #2#12 = 3;  VAlfa = "3T";  |FINSI;
  |SI #2#12 = 4;  VAlfa = "4T";  |FINSI;
  Campos11 = VAlfa;                                    || Periodo

  aAlfa     = #2#8; aTipo = "N"; nLongitud = 13; |HAZ AnalizaCadena;
  Campos12  = aAlfa;

  aAlfa     = #2#28; aTipo = "N"; nLongitud = 13; |HAZ AnalizaCadena;
  Campos13  = aAlfa;

  Campos14 = (" " *  72);

  Campos   = Campos1 + Campos2 + Campos3 + Campos4;
  Campos   = Campos   + Campos5  + Campos6  + Campos7  + Campos8;
  Campos   = Campos   + Campos9  + Campos10  + Campos11 + Campos12;
  Campos   = Campos   + Campos13 + Campos14;
  Campos   = Campos   + &13 + &10;        || Caracter Final de Linea
  |XGRABA nHandle2, Campos;
  #104#14 = #104#14 + 1;
|FPRC;

|PROCESO Tipo2;
     |SI #2#4 = "E"; |O #2#4 = "A"; |HAZ Tipo2_EA; |FINSI;
     |SI #2#4 = "R"; |O #2#4 = "X"; |HAZ Tipo2_RX; |FINSI;
|FPRC;

|DEFBUCLE eos349li;
  #2#1;
  4;
  #1#0, #0#2, #0#25, 000, tip1;
  #1#0, #0#2, #0#25, 999, tip2;
  ;
  NULL, Tipo2;
|FIN;

|PROCESO eosm0010;
  tip1 = "E";
  tip2 = "E";
  |BUCLE eos349li; || las lineas - registro de operaciones
  tip1 = "A";
  tip2 = "A";
  |BUCLE eos349li; || las lineas - registro de operaciones
  tip1 = "R";
  tip2 = "X";
  |BUCLE eos349li; || las lineas - registro de operaciones
|FPRC;

|PROCESO LeePortadaC;
  |SI #0#23 ! 0;
      #101#0 = #0#23;
      #101#1 = #1#0;
      |LEE 040#101=;
      |SI FSalida ! 0;  |ACABA;  |FINSI;
  |FINSI;

  |SI SwLee = 0;
      #103#35 = #103#35 + 1;
      #103#36 = #103#36 + (#1#6 + #1#8 + #1#10 + #1#11);

      #104#0  = #104#0 + #1#6;
      #104#1  = #104#1 + #1#8;
      #104#2  = #104#2 + #1#10 + #1#11;
      #104#6  = #104#0 + #104#1 + #104#2;
      #104#7  = #104#7 + #1#7;
      #104#8  = #104#8 + #1#9;
      #104#9  = #104#9 + #1#14 + #1#15;
      #104#13 = #104#7 + #104#8 + #104#9;
      #104#15 = #104#15 + 1;

      |ACABA;
  |FINSI;

  #3#0  = #1#0;
  |LEE 040#3=;
  |SI FSalida ! 0; |DDEFECTO #3; |FINSI;

  SwLee = 1;  |HAZ Tipo1;

  SwLee = 2;  |HAZ eosm0010;

  #1#4 = #0#15;
  |GRABA 140#1a;
  |LIBERA #1;
|FPRC;

|DEFBUCLE eos349ca;
  #1#1;
  ;
  #0#0, #0#2, #0#25;
  #0#1, #0#2, #0#25;
  be;
  NULL, LeePortadaC;
|FIN;


|| ************************************************************************
||                  CREACION DISKETTE SOPORTE COLECTIVO E INDIVIDUAL
|| ************************************************************************

|PROCESO Creacion;
     |HAZ FechaP;

     |DDEFECTO #104;

     #103#35    = 0;
     #103#36    = 0;
     SwLee = 0;  |BUCLE eos349ca;

     aAbre = "\MODELOS\349\" + enEjercicio + "\CE" + enEjercicio + ".TXT";
     |XABRE "CBW", aAbre, nHandle2;
     |SI FSalida ] 0;
         |SI #0#22 = "C";  |HAZ Tipo0;  |FINSI;  || Registro del Presentador
         SwLee = 1;  |BUCLE eos349ca;
     |FINSI;
     |XCIERRA nHandle2;

     |PUSHV 0501 2380;
     |PINPA #0#104;
     |PINDA #0#104;
     |CONFI "2400SEs Correcto : ";
     |SI FSalida ! 0;
         |RFBORRA aAbre;
         Salir = 1;
     |FINSI;
     |POPV;
|FPRC;

|| ************************************************************************
||                  GRABACION DEL DISKETTE OP
|| ************************************************************************

|PROCESO GrabaDisketteC;
     aAbre = "\MODELOS\349\" + enEjercicio + "\CE" + enEjercicio + ".TXT";
     |XABRE "CE", aAbre, nHandle2;
     |SI FSalida < 0;
         |MENAV "     No se ha hecho la creacion del Diskette.";
         |ACABA;
     |FINSI;


     |PUSHV 0501 2380;
     |BLANCO 0501 2380;
     |CUADRO 0501 2380;
     |ATRI I;
     |PINTA 0715, "Introduzca un Diskette Formateado en D.O.S. en su PC,";
     |PINTA 0815, "y pulse una Tecla para iniciar la copia.             ";
     |ATRI i;
     |LEETECLA aTecla;
     |POPV;

     aAbre = "\MODELOS\349\" + enEjercicio + "\copiaa.bat";
     |XABRE "CBW", aAbre, nHandle4;

     aAlfa = "CD \" + &13 + &10;                           |XGRABA nHandle4, aAlfa;
     aAlfa = "CD MODELOS" + &13 + &10;                     |XGRABA nHandle4, aAlfa;
     aAlfa = "CD 349" + &13 + &10;                         |XGRABA nHandle4, aAlfa;
     aAlfa = "CD " + enEjercicio + &13 + &10;              |XGRABA nHandle4, aAlfa;
     aAlfa = "COPY CE" + enEjercicio + ".TXT A:" + &13 + &10;        |XGRABA nHandle4, aAlfa;
     |SI enEjercicio ] 2003;
         aAlfa = "COPY CB" + enEjercicio + ".TXT A:" + &13 + &10;    |XGRABA nHandle4, aAlfa;
     |FINSI;
     |XCIERRA nHandle4;

     |RSYSTEM aAbre;

     |RFBORRA aAbre;
|FPRC;

|| ***********************************************************************
||                       EMISION DE ETIQUETAS
|| ***********************************************************************

|PROCESO Etiquetas;
  |SI #104#14 = 0;
      |MENAV "     Por favor haga la creacion de Diskette.";
      |ACABA;
  |FINSI;

  |PUSHV 0501 2380;

  Campos1  = "19";
  |SI #0#2 < 80; Campos1 = "20"; |FINSI;
  VAlfa    = #0#2;   VAlfa = ("00" + VAlfa) % -102;
  Campos1  = Campos1 + VAlfa;                          || Ejercicio
  VAlfa    = #0#17;  VAlfa = ("00" + VAlfa) % -102;
  Campos2  = VAlfa;                                     || Codigo Postal 1
  VAlfa    = #0#18;  VAlfa = ("000" + VAlfa) % -103;
  Campos3  = VAlfa;                                     || Codigo Postal 2

  |ABRE #6;
  #6#0 = #0#12;
  #6#1 = #0#16;
  |LEE 040#6=;
  |SI FSalida ! 0; |DDEFECTO #6; |FINSI;
  |CIERRA #6;

  #104#16 = "Delegacion .........: " + #6#2;
  #104#17 = "Ejercicio ..........: " + Campos1;
  #104#18 = "Modelo .............: 349";
  #104#19 = "Numero Justificante : " + "349" + #0#27;
  #104#20 = "N.I.F. .............: " + #0#9;
  #104#21 = "Nombre : " + #0#3;
  #104#22 = #0#4 + " " + #0#5 + " " + #0#6 + " " + Campos2 + " " + Campos3 + " " + #0#7;
  #104#23 = "P.Cont.: " + #0#3;
  #104#24 = "Telefono ...........: " +  #0#11;
  #104#25 = "Numero total Registros ..: " + #104#14;
  #104#26 = "Densidad soporte : 1,44 MB en Diskettes de 3 1/2";
  #104#27 = "";
  #104#28 = "";
  |C_M #104#27, 1, "N";
  |C_M #104#28, 1, "N";
  |SI #0#22 = "C";
      #104#27 = "Numero total Declarantes : " + #104#15;
      #104#28 = "Numero total Declarados .: " + #104#6;
      |C_M #104#27, 1, "S";
      |C_M #104#28, 1, "S";
  |FINSI;

  |PINPA #2#104;
  |PINDA #2#104;
  |ENDATOS #1#104;
  |SI FSalida = 0;
      |PINPA #2#104;
      |PINDA #2#104;

      |CONFI "2400SRealizar Impresion de la Etiqueta : ";
      |SI FSalida ! 0; |POPV; |ACABA; |FINSI;

      |IMPRE 0;
      |SI FSalida ! 0;  |ACABA;  |FINSI;

      |INFOR "eoszp016";
      |SI FSalida ! 0;  |FINIMP;  |ACABA;  |FINSI;

      |PRINT; |PIEINF; |FININF; |FINIMP;
  |FINSI;
  |POPV;
|FPRC;

|| ***********************************************************************
||                       PROCESO DE DISTRIBUCION
|| ***********************************************************************

|PROCESO Distribuye; |TIPO 3;
   enEjercicio = 2000 + #0#2;
   |SI #0#2 ] 80;
       enEjercicio = 1900 + #0#2;
   |FINSI;

   eaModelos   = "349";
   enAnyo      = enEjercicio;
   eaEjer      = enEjercicio;

   aAlfa      = "/MODELOS";                             |RMKDIR aAlfa;
   aAlfa      = "/MODELOS/349";                         |RMKDIR aAlfa;
   aAlfa      = "/MODELOS/349/" + enEjercicio;          |RMKDIR aAlfa;

   aEjer = ("00" + #0#2) % -102;

  aTipoSec = "349";
  |SI #0#28 = "EUROS  ";
       eaMoneda = "E";
   |SINO;
       eaMoneda = "P";
   |FINSI;
   Cambio = 1;
   Redond = 2;
   |SI eaMoneda = "P";  Cambio = 166.386;  |FINSI;

  || Campos de Pantalla
  aAlfa = #0#3; aTipo = "A"; nLongitud = 40; |HAZ AnalizaCadena; #0#3  = aAlfa;

  eaAlfa = #0#11; |HAZ QuitaRarosTfno; #0#11 = eaAlfa;

  aAlfa = #0#26; aTipo = "A"; nLongitud = 40; |HAZ AnalizaCadena; #0#26 = aAlfa;

  aTTel = "T";
||  |SI #0#14 ! "F"; aTTel = "D"; |FINSI;

  |SI #0#14 = "F";
     |ABRE #1; |ABRE #3; |ABRE #101;
      Opcion = 1;
      Salir = 0;
      |HAZ Creacion;
      |SI Salir = 0;
          |HAZ ElTelemat;
      |FINSI;
      |CIERRA #1; |CIERRA #3; |CIERRA #101;
      |ACABA;
  |FINSI;

  |ABRE #1;
  |ABRE #3;
  |ABRE #101;
  |PARA; |SI Salir = 0; |HACIENDO;
         |SI enEjercicio ] 2003;
             MGeneral5  = "Validacion ";
         |FINSI;

         |MENUG MGeneral;
         MGeneral2 = FSalida;
         Opcion    = MGeneral2;
         Salir     = 1;
         |SI Opcion ] 1; |Y Opcion [ 4;
             Salir = 0;
             |SI Opcion = 1;  |HAZ Creacion;       |FINSI;
             |SI Opcion = 2;  |HAZ Portada;        |FINSI;
             |SI Opcion = 3;  |HAZ Etiquetas;      |FINSI;
             |SI Opcion = 4;  |HAZ GrabaDisketteC; |FINSI;
         |FINSI;
  |SIGUE;

  aAbre = "\MODELOS\349\" + enEjercicio + "\CE" + enEjercicio + ".TXT";  |RFBORRA aAbre;
  aAbre = "\MODELOS\349\" + enEjercicio + "\CB" + enEjercicio + ".TXT";  |RFBORRA aAbre;

  |CIERRA #1;
  |CIERRA #3;
  |CIERRA #101;
|FPRC;

|PROCESO ElTelemat;
     aAlfa      = "/AEAT";                                |RMKDIR aAlfa;
     aAlfa      = "/AEAT/INTERNET";                       |RMKDIR aAlfa;
     aAlfa      = "/AEAT/INTERNET/349";                   |RMKDIR aAlfa;
     aAlfa      = "/AEAT/INTERNET/349/" + enEjercicio;    |RMKDIR aAlfa;
     aInternet  = aAlfa + "/";

     |SI #0#22 = "I";
         |SI enEjercicio ] 2003;  |Y enEjercicio [ 2006;
             |HAZ Validacion;
         |FINSI;

         |SI enEjercicio ] 2007;
            |HAZ Validacion_2007;
         |FINSI;
     |FINSI;

     |SI enError ! 0;  |ACABA;  |FINSI;

     aOrigen    = "/MODELOS/349/" + enEjercicio + "/CE" + enEjercicio + ".TXT";

     |SI #0#22 = "I";
         aDestino   = aInternet + "CE" + (("00000" + #3#0) % -105) + ".TXT";
     |SINO;
         aDestino   = aInternet + "CE" + enEjercicio + ".TXT";
     |FINSI;

     |RCOPIA_FICHERO aOrigen, aDestino;

     aAlfa =  "      Los ficheros generados estan en " + aInternet + " de su PC";
     |MENAV aAlfa;
|FPRC;

|PROCESO AnalizaCadena;
     |QBF aAlfa;
     |SI aTipo = "N";
         |SI nLongitud = 15; |O nLongitud = 13;
             Calc = aAlfa; Calc = (Calc / Cambio) ! Redond;
             |SI #0#28 = "EUROS  ";  Calc = Calc * 100; |FINSI;
             aAlfa = Calc;
         |FINSI;
         Comodin = "0" * nLongitud;
     |SINO;
         Comodin = " " * nLongitud;
     |FINSI;

     nEspa   = 0;
     aCadena = "";
     aLong   = aAlfa % 0;
     nLong   = aLong;
     |PARA nCampo = 1;  |SI nCampo [ aLong;  |HACIENDO nCampo = nCampo + 1;
           nPos = (100 * nCampo) + 1;
           aCaracter = aAlfa % nPos;
           |SI aCaracter = ",";  aCaracter = "";   nEspa = nEspa + 1;  |FINSI;
           |SI aCaracter = "¥";  aCaracter = &209; |FINSI;
           |SI aCaracter = "¤";  aCaracter = &209; |FINSI;
           |SI aCaracter = "§";  aCaracter = &176; |FINSI;
           |SI aCaracter = "¦";  aCaracter = &170; |FINSI;
           |SI aCaracter = "'";  aCaracter = "";   |FINSI;
           aCadena = aCadena + aCaracter;
     |SIGUE;
     aAlfa = aCadena + (" " * nEspa);

     aAlfa = aAlfa > aAlfa;

     |SI aTipo = "A";
        Calc  = 100 + nLongitud;
        aAlfa = (aAlfa + Comodin) % Calc;
     |SINO;
        Calc  = 0 - (100 + nLongitud);
        aAlfa = (Comodin + aAlfa) % Calc;
     |FINSI;
|FPRC;
