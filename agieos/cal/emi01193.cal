|FICHEROS;
     eosem193;
     eosclien;
     eosm1193;
     eosg1193;
     eosp1193;
     dsmoy000;
|FIN;

|VARIABLES;
   aCuadre  = "";
   aTipo    = "";
   aTipoSec = "";
   PathBass = "";
   Cadena   = "";
   Handle   = "";
   aMensaje = "";
   aImpre   = "";
   aLong = "";

   aAlfa = "";
   Cont = 0;
   Sw = 0;
   Calc = 0;
   nCalc = 0;
   nCalc1 = 0;
   nLong  = 0;

   EmpreIni = 0;
   EmpreFin = 0;

   &EURO = 0;
   &EURODB = 0;
   &EURODV = 0;
   &FechaAlfa = "";
   DiaAlfa = "";
   MesAlfa = "";
   LugarAlfa = "";
   YearAlfa  = "";

   Mes = 0;

   Primero = 0;
   ClavAnt = "";
   ErrorSec = 0;

   Sistema = "";
   LongAlfa = "";
   Long = 0;

   Linea = 0;
   &TotalHojas = 0;
   &NumHoja = 0;
   Percep = 0;
   Gastos = 0;

   &LinPerc = 0;
   Opcion = 0;
   Path = "";
   Comodin1 = "";

   aAbre          = "";
   aIPRemoto      = "";
   aPathInter     = "";
   aFichero       = "";
   aBue           = "";
   aOrigen        = "";
   aDestino       = "";
   aModelos       = "";
   aAlfa1         = "";
   aAlfa2         = "";
   aAlfa3         = "";
   aAlfa4         = "";

   nHandle1       = 0;
   nHandle2       = 0;

   &enCanti       = 0;
   &enValidacion  = 0;
   &enBuenos      = 0;
   &eaAlfa        = "";
   &eaMoneda      = "";
   &enAnyo        = "";
   &eaInternet    = "";
   &eaFichError   = "";
   &eaFichDatos   = "";
   &eaFichAuxil   = "";
   &eaFOrigen     = "";
   &eaFDestino    = "";

     &eaTipoE        = "";
     &eaTipoR        = "";
     &eaTipoP        = "";
     &eaTipoC        = "";
     &eaTipoA        = "";
     &eaTipoV        = "";
     &eaModoImpre    = "";

{-1}Menu  = "";
    Menu1 = "2400";
    Menu2 = "1";
    Menu3 = "Tipo hojas interiores";
    Menu4 = "DM";
    Menu5 = "Dos perceptores";
    Menu6 = "Mas perceptores";

{-1}aMoneda   = "";
    aMoneda1  = "LEER";
    aMoneda2  = "";

     &eaModelos          = "";
     &eaEjer             = "";
     &eaPeriodo          = "";
     &eaPathEmision      = "";
     &eaPathLanza        = "";
     &eaPathExplorador   = "";
     &eaPathInternet     = "";
     &eaFicheroPlano     = "";
     &eaOpcion           = "";
     &eaUnidad           = "";
     &enErrorPortal      = 0;
     &eaFicheroImpre     = "";
     &eaFicheroSalid     = "";
     &eaFicheroError     = "";
     &enEmpresa          = 0;
     &eaNomEmpresa       = "";
     &enPresentar        = 0;
     &nCanti             = 0;
     &Comodin            = "";

    aMas = "";
    aBase = "";
|FIN;

|PROCESO Valor15;
     eaMoneda = #eosem193#49;
     enCanti  = nCanti;
     |HAZ ConverMoneda;
     nCanti   = enCanti;

     |SI nCanti ] 0;
         Comodin  = ("000000000000000" + nCanti) % -115;
     |SINO;
         nCanti    = -nCanti;
         Comodin   = "N" + (("000000000000000" + nCanti) % -114);
     |FINSI;
|FPRC;

|PROCESO Valor13;
     eaMoneda = #eosem193#49;
     enCanti  = nCanti;
     |HAZ ConverMoneda;
     nCanti   = enCanti;

     |SI nCanti ] 0;
         Comodin  = ("0000000000000" + nCanti) % -113;
     |SINO;
         nCanti    = -nCanti;
         Comodin   = "N" + (("0000000000000" + nCanti) % -112);
     |FINSI;
|FPRC;

|PROCESO Valor11;
     eaMoneda = #eosem193#49;
     enCanti  = nCanti;
     |HAZ ConverMoneda;
     nCanti   = enCanti;

     |SI nCanti ] 0;
         Comodin  = ("000000000000" + nCanti) % -111;
     |SINO;
         nCanti    = -nCanti;
         Comodin   = "N" + (("0000000000000" + nCanti) % -110);
     |FINSI;
|FPRC;

|PROCESO Valor12;
     eaMoneda = #eosem193#49;
     enCanti  = nCanti;
     |HAZ ConverMoneda;
     nCanti   = enCanti;

     |SI nCanti ] 0;
         Comodin  = ("000000000000" + nCanti) % -112;
     |SINO;
         nCanti    = -nCanti;
         Comodin   = "N" + (("0000000000000" + nCanti) % -111);
     |FINSI;
|FPRC;

|PROCESO Tipo1_2016;
      Cadena  = "1";
      Cadena  = Cadena + "193";
      Cadena  = Cadena + #eosem193#40;              || Ejercicio
      Comodin = #eosclien#1 % 0109;
      Cadena  =  Cadena + Comodin;                   || Nif Declarante
      eaAlfa  = #eosclien#2;  |HAZ QuitaRaros;
      aAlfa   = eaAlfa; |QBF aAlfa; aAlfa = (aAlfa + (" " * 40)) % 0140;
      Cadena  = Cadena + aAlfa;               || Nombre Declarante
      Cadena  = Cadena + "T";                       || Tipo de soporte
      Comodin = #eosm1193#23; |QBF Comodin;
      Comodin1 = #eosm1193#22; |QBF Comodin1;
      |SI Comodin1 = "";
         Comodin = #eosclien#14;
         Comodin1 = #eosclien#2;
      |FINSI;
      Comodin = Comodin % 109;
      |SI Comodin = "         ";  Comodin = "000000000";  |FINSI;
      Cadena  = Cadena + Comodin;                || Telefono Persona Contacto
      eaAlfa  = Comodin1;  |HAZ QuitaRaros;
      aAlfa   = eaAlfa; |QBF aAlfa; aAlfa = (aAlfa + (" " * 40)) % 0140;
      Cadena  = Cadena + aAlfa;                  || Persona de contacto
      Comodin = "1930000000000";
      Cadena = Cadena + Comodin;                     || Numero justificante

      |SI #eosm1193#2 = "C";                       || Tipo de declaracion
          Cadena = Cadena + "C";
          Cadena = Cadena + " ";
      |FINSI;
      |SI #eosm1193#2 = "S";
          Cadena = Cadena + " ";
          Cadena = Cadena + "S";
      |FINSI;
      |SI #eosm1193#2 = "C"; |O #eosm1193#2 = "S";
          Cadena = Cadena + "193";
          |PARA Cont = 10; |SI Cont [ 19; |HACIENDO Cont = Cont + 1;
                Cadena = Cadena + #eosm1193#Cont#;
          |SIGUE;
      |FINSI;

      |SI #eosm1193#2 = " ";
          Cadena = Cadena + " ";
          Cadena = Cadena + " ";
          Cadena = Cadena + "0000000000000";
      |FINSI;

      Comodin = ("000000000" + #eosm1193#3) % -109;
      Cadena = Cadena + Comodin;                 || Numero de Perceptores

      nCanti  = #eosm1193#4;  |HAZ Valor15;
      Cadena = Cadena + Comodin;                 || Base retenciones e Ing.a Cta.

      nCanti  = #eosm1193#5;  |HAZ Valor15;
      Cadena = Cadena + Comodin;                 || Retenciones e Ing.a Cta.

      nCanti  = #eosm1193#6;  |HAZ Valor15;
      Cadena = Cadena + Comodin;                 || Retenciones e Ing.a Cta. Ingresados

      Cadena = Cadena + (" " * 30);              || Vacio de 30

      nCanti  = #eosm1193#9;  |HAZ Valor15;
      Cadena = Cadena + Comodin;                 || Gastos

      |XGRABA nHandle2, Cadena;

      Cadena = " " * 66;
      |XGRABA nHandle2, Cadena;

      Cadena = (" " * 200);
      |XGRABA nHandle2, Cadena;

     |SI eaOpcion = "3";
         Cadena = &13 + &10;
         |XGRABA nHandle2, Cadena;
     |FINSI;
|FPRC;

|PROCESO eosp1193_2016;
     Cadena  = "2";
     Cadena  = Cadena + "193";
     Cadena  = Cadena + #eosem193#40;              || Ejercicio
     Comodin = #eosclien#1 % 0109;
     Cadena  = Cadena + Comodin;                   || Nif Declarante
     Comodin = #eosp1193#2 % 0109;
     Cadena  = Cadena + Comodin;                   || Nif Perceptor
     Comodin = #eosp1193#4 % 0109;
     Cadena  = Cadena + Comodin;                   || Nif del repte. legal
     eaAlfa  = #eosp1193#3;  |HAZ QuitaRaros;
     aAlfa   = eaAlfa; |QBF aAlfa; aAlfa = (aAlfa + (" " * 40)) % 0140;
     Cadena  = Cadena + aAlfa;                     || Nombre
     |SI #eosp1193#5 = "S";                       || Mediador
          Cadena = Cadena + "X";
     |SINO;
          Cadena = Cadena + " ";
     |FINSI;
     Comodin = #eosp1193#6;
     Comodin = "00" + Comodin;
     Comodin = Comodin % -102;
     Cadena  = Cadena + Comodin;                   || Codigo Provincia
     |SI #eosp1193#7 = 0;
         Cadena = Cadena + "0";                       || Clave Codigo
     |SINO;
         Cadena = Cadena + #eosp1193#7;               || Clave Codigo
     |FINSI;
     Comodin = #eosp1193#8 % 112;
     Cadena  = Cadena + Comodin;                   || Codigo Emisor
     Cadena  = Cadena + #eosp1193#9;               || Clave de percepcion
     Comodin = #eosp1193#10;
     Comodin = "00" + Comodin;
     Comodin = Comodin % -102;
     Cadena  = Cadena + Comodin;                   || Naturaleza
     Cadena  = Cadena + #eosp1193#11;              || Pago
     Cadena  = Cadena + #eosp1193#12;              || Tipo Codigo
     Cadena  = Cadena + #eosp1193#13;              || C.C.V.: Entidad
     Cadena  = Cadena + #eosp1193#14;              || C.C.V.: Sucursal
     Cadena  = Cadena + #eosp1193#15;              || C.C.V.: Digito Control
     Cadena  = Cadena + #eosp1193#16;              || C.C.V.: Numero Cuenta
     Cadena  = Cadena + #eosp1193#17;              || Pendiente
     Comodin = #eosp1193#18;
     Comodin = ("0000" + Comodin) % -104;
     Cadena  = Cadena + Comodin;                   || Ejercicio de devengo
     Cadena  = Cadena + #eosp1193#19;              || Tipo de percepcion

     nCanti  = #eosp1193#20;  |HAZ Valor13;
     Cadena  = Cadena + Comodin;                   || Importe Percepciones

     Cadena  = Cadena + "   ";                     || Vacio 3

     nCanti  = #eosp1193#22;  |HAZ Valor13;
     Cadena  = Cadena + Comodin;                   || Importe Reducciones

     nCanti  = #eosp1193#23;  |HAZ Valor13;
     Cadena  = Cadena + Comodin;                   || Base Retenc.e Ing.a Cta.

     Calc    = #eosp1193#24 * 100;
     Comodin = ("0000" + Calc) % -104;
     Cadena  = Cadena + Comodin;                   || % Retencion (Parte decimal)

     nCanti  = #eosp1193#25;  |HAZ Valor13;
     Cadena  = Cadena + Comodin;                   || Retenc. e Ing.a Cta.

     nCanti  = #eosp1193#32;  |HAZ Valor11;
     Cadena  = Cadena + Comodin;                   || Penalizaciones

     Cadena  = Cadena + (" " * 16);               || Vacio 31

     |SI #eosp1193#12 ! "P";
         Comodin = "0000000000000000000000000000000000000000";
         Cadena  = Cadena + Comodin;                             || Blancos
     |SINO;
        aAlfa1 = (#eosp1193#27 % -104) + (#eosp1193#27 % 0402) + (#eosp1193#27 % 102);
        aAlfa2 = (#eosp1193#28 % -104) + (#eosp1193#28 % 0402) + (#eosp1193#28 % 102);

        nCanti = #eosp1193#29;  |HAZ Valor12;
        aAlfa3 = Comodin;

        nCanti = #eosp1193#30;  |HAZ Valor12;
        aAlfa4 = Comodin;

        Comodin = aAlfa1 + aAlfa2 + aAlfa3 + aAlfa4;
        Cadena  = Cadena + Comodin;                   || Blancos
     |FINSI;

     |XGRABA nHandle2, Cadena;

     Cadena  = " " * 52;
     |XGRABA nHandle2, Cadena;

     Cadena  = (" " * 200);
     |XGRABA nHandle2, Cadena;

     |SI eaOpcion = "3";
         Cadena = &13 + &10;
         |XGRABA nHandle2, Cadena;
     |FINSI;
|FPRC;

|DEFBUCLE eosp1193_2016;
     #eosp1193#1;
     ;
     #eosm1193#0, #eosm1193#1, "            ", " ", 0000;
     #eosm1193#0, #eosm1193#1, "zzzzzzzzzzzz", "z", 9999;
     ;
     NULL, eosp1193_2016;
|FIN;

|PROCESO eosg1193_2016;
                    || GRABACION DEL REGISTRO DE TIPO "2" (GASTOS)

     Cadena  = "2";
     Cadena  = Cadena + "193";
     Cadena  = Cadena + #eosem193#40;              || Ejercicio
     Comodin = #eosclien#1 % 0109;
     Cadena  = Cadena + Comodin;                   || Nif Declarante
     Comodin = #eosg1193#2 % 0109;
     Cadena  = Cadena + Comodin;                   || Nif Contribuyente
     Comodin = #eosg1193#3 % 0109;
     Cadena  = Cadena + Comodin;                   || Nif Repr. legal

     eaAlfa  = #eosg1193#4;  |HAZ QuitaRaros;
     aAlfa   = eaAlfa; |QBF aAlfa; aAlfa = (aAlfa + (" " * 40)) % 0140;
     Cadena  = Cadena + aAlfa;                     || Nombre

     Comodin = " " * 119;
     Cadena  = Cadena + Comodin;

     nCanti  = #eosg1193#5;   |HAZ Valor13;
     Cadena  = Cadena + Comodin;                            || Importe de gastos
     |XGRABA nHandle2, Cadena;

     Cadena  = " " * 93;
     |XGRABA nHandle2, Cadena;

     Cadena = (" " * 200);
     |XGRABA nHandle2, Cadena;

     |SI eaOpcion = "3";
         Cadena = &13 + &10;
         |XGRABA nHandle2, Cadena;
     |FINSI;
|FPRC;

|DEFBUCLE eosg1193_2016;
     #eosg1193#1;
     ;
     #eosm1193#0, #eosm1193#1, "            ";
     #eosm1193#0, #eosm1193#1, "zzzzzzzzzzzz";
     ;
     NULL, eosg1193_2016;
|FIN;

|PROCESO eosm1193_2016;
     eaFicheroPlano = eaModelos + (("00000" + #eosm1193#0) % -105) + ".txt";
     eaFicheroImpre = eaModelos + (("00000" + #eosm1193#0) % -105) + ".imp";
     eaFicheroSalid = eaModelos + (("00000" + #eosm1193#0) % -105) + ".sal";
     eaFicheroError = "err" + (("00000" + #eosm1193#0) % -105) + ".txt";

     aAbre = eaPathEmision + eaFicheroPlano;
     |XABRE "BW", aAbre, nHandle2;

     #eosclien#0 = #eosm1193#0;
     |LEE 010#eosclien.=;

     |HAZ Tipo1_2016;
     |BUCLE eosp1193_2016;
     |BUCLE eosg1193_2016;

     |XCIERRA nHandle2;

     enEmpresa    = #eosm1193#0;
     eaNomEmpresa = #eosclien#2;
     |HAZ PresentaPortal;

     |SI enErrorPortal = 0;  |Y eaOpcion ! "1";
         #eosm1193#21 = #eosem193#41;
         |GRABA 020#eosm1193.a;
     |FINSI;

     |LIBERA #eosm1193;
|FPRC;

|DEFBUCLE eosm1193_2016;
     #eosm1193#1;
     ;
     EmpreIni, #eosem193#40, aTipo, 00;
     EmpreFin, #eosem193#40, aTipo, 99;
     be;
     NULL, eosm1193_2016;
|FIN;

|PROCESO Bloque2016;
     aTipo = #eosem193#50;
     |SI #eosem193#50 = "N"; aTipo = " "; |FINSI;

     enAnyo     = #eosem193#40;
     eaModelos  = "193";
     eaEjer     = #eosem193#40;

     #dsmoy000#0 = 193;
     #dsmoy000#6 = enAnyo;

     eaPathLanza = eaUnidad + "\MODELOS";            |RMKDIR eaPathLanza;
     eaPathLanza = eaPathLanza + "\" + eaModelos;    |RMKDIR eaPathLanza;
     eaPathLanza = eaPathLanza + "\" + eaEjer;       |RMKDIR eaPathLanza;
     eaPathLanza = eaPathLanza + "\";

     |DBASE eaPathEmision;
     eaPathEmision = eaPathEmision + "emisiones";       |MKDIR eaPathEmision;
     eaPathEmision = eaPathEmision + "/" + eaModelos;   |MKDIR eaPathEmision;
     eaPathEmision = eaPathEmision + "/" + eaEjer;      |MKDIR eaPathEmision;
     eaPathEmision = eaPathEmision + "/";

     eaPathInternet = eaUnidad + "/AEAT";                |RMKDIR eaPathInternet;
     eaPathInternet = eaPathInternet + "/INTERNET";      |RMKDIR eaPathInternet;
     eaPathInternet = eaPathInternet + "/" + eaModelos;  |RMKDIR eaPathInternet;
     eaPathInternet = eaPathInternet + "/" + eaEjer;     |RMKDIR eaPathInternet;
     eaPathInternet = eaPathInternet + "/";

     |HAZ Botones_2015;

     |SI eaOpcion = "0";  |ACABA;  |FINSI;

     |ABRE #eosclien;
     |SI #eosem193#0 = "S";
         EmpreIni = #eosem193#1;
         EmpreFin = #eosem193#2;
         |BUCLE eosm1193_2016;
     |SINO;
         |PARA Cont = 3; |SI Cont [ 38; |HACIENDO Cont = Cont + 1;
               EmpreIni = #eosem193#Cont#;
               EmpreFin = #eosem193#Cont#;
               |BUCLE eosm1193_2016;
         |SIGUE;
     |FINSI;
     |CIERRA #eosclien;

     |SI eaOpcion = "3";
         |SI enPresentar = 0;
             aAlfa =  "      Los ficheros generados estan: " + eaPathInternet + " de su PC";
             |MENAV aAlfa;
         |FINSI;
     |FINSI;
|FPRC;
