|FICHEROS;
   eosy0015 #0;
   eosm0013 #1;
   eosm0014 #2;

   eosp1193 #3;
   eosclien #4;
   eosm1193 #5;
|FIN;

|VARIABLES;
   nCont   = 0;
   nCampoD = 0;
   DEmpre  = 0;
   HEmpre  = 0;

   Comodin = "";

   &Dia     = "";
   &Mes     = "";
   &anyo    = "";
   &Descr1  = "";
   &Descr2  = "";
   &Direc   = "";
   &Local   = "";
   &NumJust = "";
|FIN;

|INCLUYE i_consul;

|PROCESO Parrilla;
   |SI #eosy0015#0 = 0;
      #eosy0015#1 = 0; |PINTA #eosy0015#0; |C_M #eosy0015#1,1,"N";
      |PARA nCont = 2; |SI nCont [ 37; |HACIENDO nCont = nCont + 1;
         |C_M #eosy0015#nCont#,1,"S";
      |SIGUE;
   |SINO;
      |C_M #eosy0015#1,1,"S";
      |PARA nCont = 2; |SI nCont [ 37; |HACIENDO nCont = nCont + 1;
         #eosy0015#nCont# = 0; |PINTA #eosy0015#nCont#;
         |C_M #eosy0015#nCont#,1,"N";
      |SIGUE;
   |FINSI;
|FPRC;

|PROCESO NoMasEmpresas;
   |SI #eosy0015#Campo# = 0;
      |PARA nCont = Campo + 1; |SI nCont [ 37; |HACIENDO nCont = nCont + 1;
         |C_M #eosy0015#nCont#,1,"N";
      |SIGUE;
   |SINO;
      |PARA nCont = Campo + 1; |SI nCont [ 37; |HACIENDO nCont = nCont + 1;
         |C_M #eosy0015#nCont#,1,"S";
      |SIGUE;
   |FINSI;
|FPRC;

|PROCESO Planilla;
   |SI #eosy0015#38 = 0;  |Y #eosy0015#0 = 0;
      |C_M #eosy0015#38, 1, "N";  #eosy0015#38 = 0;  |PINTA #eosy0015#38;
      |PARA nCampoD = 2;  |SI nCampoD [ 37;  |HACIENDO nCampoD = nCampoD + 1;
         |C_M #eosy0015#nCampoD#, 1, "S";
      |SIGUE;
   |SINO;
      |SI #eosy0015#0 = 0;
         |C_M #eosy0015#38, 1, "S";
         |PARA nCampoD = 2;  |SI nCampoD [ 37;  |HACIENDO nCampoD = nCampoD + 1;
            |C_M #eosy0015#nCampoD#, 1, "N";
            #eosy0015#nCampoD# = 0;  |PINTA #eosy0015#nCampoD#;
         |SIGUE;
      |FINSI;
   |FINSI;
|FPRC;

|PROCESO MesAlfa;
     |SI Mes = "01"; Mes = "Enero     "; |FINSI;
     |SI Mes = "02"; Mes = "Febrero   "; |FINSI;
     |SI Mes = "03"; Mes = "Marzo     "; |FINSI;
     |SI Mes = "04"; Mes = "Abril     "; |FINSI;
     |SI Mes = "05"; Mes = "Mayo      "; |FINSI;
     |SI Mes = "06"; Mes = "Junio     "; |FINSI;
     |SI Mes = "07"; Mes = "Julio     "; |FINSI;
     |SI Mes = "08"; Mes = "Agosto    "; |FINSI;
     |SI Mes = "09"; Mes = "Septiembre"; |FINSI;
     |SI Mes = "10"; Mes = "Octubre   "; |FINSI;
     |SI Mes = "11"; Mes = "Noviembre "; |FINSI;
     |SI Mes = "12"; Mes = "Diciembre "; |FINSI;
|FPRC;

|PROCESO Certificados;
   |ABRE #eosm1193; |DDEFECTO #eosm1193;
   #eosm1193#0 = #eosp1193#0;
   #eosm1193#1 = #eosp1193#1;
   |LEE 000#eosm1193.];
   |SI FSalida ! 0; |O #eosm1193#0 ! #eosp1193#0; |O #eosm1193#1 ! #eosp1193#1;
      |DDEFECTO #eosm1193;
   |FINSI;
   |CIERRA #eosm1193;

   #eosclien#0 = #eosp1193#0; |LEE 000#eosclien.=;
   Dia  = #eosy0015#40; |QBF Dia;  Dia  = ", a " + (Dia  % 0102);
   Mes  = #eosy0015#40; |QBF Mes;  Mes  = Mes  % 0402; |HAZ MesAlfa; |QBF Mes; Mes = " de " + Mes;
   anyo = #eosy0015#40; |QBF anyo; anyo = anyo % -104; anyo = " de " + anyo;

   |SI #eosp1193#9 = "A";
      Descr1 = "  Rendimientos del capital mobiliario por participacion en fondos   ";
      Descr2 = "       propios de cualquier entidad (CLAVE PERCEPCION A)            ";
   |FINSI;

   |SI #eosp1193#9 = "B";
      Descr1 = "Rendimientos del capital mobiliario con consideracion de explicitos ";
      Descr2 = "                      (CLAVE PERCEPCION B)                          ";
   |FINSI;

   |SI #eosp1193#9 = "C";
      Descr1 = "Rendimientos del capital mobiliario no comprendidos ni en la clave  ";
      Descr2 = "            A ni en la clave B (CLAVE PERCEPCION C)                 ";
   |FINSI;

   NumJust = " " + ((("0"*12) + #0#41) % -113) + " " + #0#42 + ".";

   Direc =         #eosclien#4; |QBF Direc; Direc = Direc + " ";
   Direc = Direc + #eosclien#5; |QBF Direc; Direc = Direc + " ";
   Comodin = #eosclien#6; |QBF Comodin;
   |SI Comodin ! ""; Direc = Direc + ", N§ "   + Comodin; |FINSI;
   Comodin = #eosclien#7; |QBF Comodin;
   |SI Comodin ! ""; Direc = Direc + ", Esc. " + Comodin; |FINSI;
   Comodin = #eosclien#8; |QBF Comodin;
   |SI Comodin ! ""; Direc = Direc + ", Piso " + Comodin; |FINSI;
   Comodin = #eosclien#9; |QBF Comodin;
   |SI Comodin ! ""; Direc = Direc + ", Pta. " + Comodin; |FINSI;

   Local = #eosy0015#46; |QBF Local;

   |PRINT;
   |PIEINF;
|FPRC;

|DEFBUCLE Certificados;
#eosp1193#1;
;
DEmpre,#eosy0015#39,"         ","A",0000;
HEmpre,#eosy0015#39,"zzzzzzzzz","C",2999;
;
NULL,Certificados;
|FIN;

|PROCESO Empresas;
   DEmpre = #eosm0014#1; HEmpre = #eosm0014#1;
   |BUCLE Certificados;
|FPRC;

|DEFBUCLE Empresas;
#eosm0014#1;
;
#eosy0015#38,00000;
#eosy0015#38,99999;
;
NULL,Empresas;
|FIN;

|PROGRAMA;
   |CLS;
   |CABEZA "Emision certificados modelo 193";
   |ABRE #eosy0015;
   |LEE 000#eosy0015.p;
   |SI FSalida ! 0;
      |DDEFECTO #eosy0015;
      |GRABA 020#eosy0015.n;
      |LEE 000#eosy0015.p;
   |FINSI;
   |PINPA #0#eosy0015; |PINDA #0#eosy0015;
   |ENDATOS #1#eosy0015;
   |SI FSalida = 0;
      |GRABA 020#eosy0015.a;
      |ABRE #eosclien;
      |IMPRE 0;
      |SI FSalida ! 0; |MENAV "    Proceso abortado"; |ACABA; |FINSI;
      |INFOR "eosy0015";
      |SI FSalida ! 0; |MENAV "    Proceso abortado"; |FINIMP; |ACABA; |FINSI;

      |SI #eosy0015#0 ! 0;
         DEmpre = #eosy0015#0; HEmpre = #eosy0015#1;
         |BUCLE Certificados;
      |SINO;
         |SI #eosy0015#38 ! 0;
            |BUCLE Empresas;
         |SINO;
            |PARA nCont = 2; |SI nCont [ 37; |HACIENDO nCont = nCont + 1;
               DEmpre = #eosy0015#nCont#; HEmpre = #eosy0015#nCont#;
               |BUCLE Certificados;
            |SIGUE;
         |FINSI;
      |FINSI;

      |FININF;
      |FINIMP;
      |CIERRA #eosclien;
   |FINSI;
   |CIERRA #eosy0015;
|FPRO;
