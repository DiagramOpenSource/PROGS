|FICHEROS;
     eosz0098 #98;

     eosm0093 #93;
     eosm0193@ #193;

     eosm3130 #130;
     eosm0020 #131;
     eosclien #1000;
     eosactiv #114;
|FIN;

|VARIABLES;
     nCampo    = 0;
     aParam    = "";
     aBase     = "";
     aMas      = "";
     nDEmpresa = 0;
     nHEmpresa = 0;

     &enElEjer   = 0;
     &enDEmpresa = 0;
     &enHEmpresa = 0;
     aAlfa1      = "";
     aCoBien     = "";
     nModelo     = 0;
     fFecIni     = @;
     fFecFin     = @;
     nSwConPro   = 0;
     nNoModi     = 0;
     nEmpresa    = 0;
     nNume1      = 0;
|FIN;

|INCLUYE i_vareos;
|| \\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\
|PROCESO NoModificar131;
   |SI #131#4 = "S"; nNoModi = 1; |FINSI;
|FPRC;

|DEFBUCLE NoModificar131;
   #131#1;
   ;
   nEmpresa, #98#2, 01;
   nEmpresa, #98#2, 99;
   ;
   NULL, NoModificar131;
|FIN;

|PROCESO NoModificar130;
  |SI #130#5 = "S"; nNoModi = 1; |FINSI;
|FPRC;

|DEFBUCLE NoModificar130;
  #130#1;
  ;
  nEmpresa, 0, 01, #98#2;
  nEmpresa, 9, 99, #98#2;
  ;
  NULL, NoModificar130;
|FIN;

|| .... Mirar si hace el modelo de IVA
|PROCESO MiraSiD;
  |SI #114#48 > "01.01.1900";
      |SI #114#48 > fFecFin;  |ACABA;  |FINSI;
  |FINSI;
  |SI #114#49 > "01.01.1900";
      |SI #114#49 < fFecIni;  |ACABA;  |FINSI;
  |FINSI;

  |SI #114#22 = "A"; |Y #98#0 = 130;
     |ACABA;
  |FINSI;
  |SI #114#22 ! "A"; |Y #98#0 = 131;
     |ACABA;
  |FINSI;

  nSwConPro = 0;
  aCoBien = #114#46;
|FPRC;

|DEFBUCLE MiraSiModelo;
  #114#1;
  ;
  nEmpresa, 1, #98#2;
  nEmpresa, 9, #98#2;
  ;
  NULL, MiraSiD;
|FIN;

|PROCESO LaSeleccion;
    aCoBien = "N";
    nSwConPro = -1;
    |BUCLE MiraSiModelo;
    |SI nSwConPro = -1;  |ACABA; |FINSI;
    |SI aCoBien = "S"; nSwConPro = -1; |ACABA; |FINSI;

     nNoModi = 0;
     |SI #98#0 = 130; |BUCLE NoModificar130; |FINSI;
     |SI #98#0 = 131; |BUCLE NoModificar131; |FINSI;
     |SI nNoModi = 1; nSwConPro = -1; |FINSI;
|FPRC;

|PROCESO agifigen;
    nEmpresa   = #1000#0;
    eaNombreAp = #1000#2;
    |HAZ LaSeleccion;
    |SI nSwConPro ! -1;
        enEmp130 = nEmpresa;
        enEje130 = #98#2;
        enMod130 = nModelo;
        enSwApli = 1;
        |HAZ eGrabadsmom093;
    |FINSI;
|FPRC;

|DEFBUCLE agifigen;
    #1000#1;
    ;
    nDEmpresa;
    nHEmpresa;
    e;
    NULL, agifigen;
|FIN;

|PROCESO CopConfigura;
    nEmpresa = #193#2;

    #93#0 = #193#0;
    #93#1 = #98#2;
    #93#2 = #193#2;
    |LEE 101#93=;
    |SI FSalida ! 0;

        |HAZ LaSeleccion;
        |SI nSwConPro ! -1;
            |DDEFECTO #93;

            #93#0  = #193#0;
            #93#1  = #98#2;
            #93#2  = #193#2;
            |PARA nCampo = 3; |SI nCampo [ 14; |HACIENDO nCampo = nCampo + 1;
                 #93nCampo = #193nCampo;
            |SIGUE;

             #93#15 = 0;
             enEmp130   = nEmpresa;
             enEje130   = #98#2;
             enMod130   = nModelo;
             eaNombreAp = #193#3;
             enSwApli   = 3;
             |HAZ eGrabadsmom093;
             |GRABA 020#93n;
       |FINSI;
    |SINO;
        |PARA nCampo = 3; |SI nCampo [ 14; |HACIENDO nCampo = nCampo + 1;
              #93nCampo = #193nCampo;
        |SIGUE;
        |SI #98#7 = "S";
             #93#15 = 0;
             enEmp130   = nEmpresa;
             enEje130   = #98#2;
             enMod130   = nModelo;
             eaNombreAp = #193#3;
             enSwApli   = 3;
             |HAZ eGrabadsmom093;
        |FINSI;
        |GRABA 020#93a;
    |FINSI;
    |LIBERA #93;
|FPRC;

|DEFBUCLE CopConfigura;
     #193#1003;
     ;
     nModelo, #98#1, nDEmpresa;
     nModelo, #98#1, nHEmpresa;
     e;
     NULL, CopConfigura;
|FIN;

|PROCESO BorrarPrimero;
     |BORRA 020#93a;
     |LIBERA #93;
|FPRC;

|DEFBUCLE BorrarPrimero;
     #93#1;
     ;
     nModelo, #98#2, nDEmpresa;
     nModelo, #98#2, nHEmpresa;
     be;
     NULL, BorrarPrimero;
|FIN;

|CALCULO Tipo12; |TIPO 12;
     nNume1 = 2000 + #98#2; aAlfa1 = nNume1;
     aAlfa1 = "01.01." + aAlfa1; fFecIni = aAlfa1;
     nNume1 = 2000 + #98#2; aAlfa1 = nNume1;
     aAlfa1 = "31.12." + aAlfa1; fFecFin = aAlfa1;

     enElEjer = 0;
     nModelo   = #98#0;
     nDEmpresa = #98#4;
     nHEmpresa = #98#5;

     |SI #98#6 = "SI";
         |DBASE aBase;  |QBF aBase;

         aMas = aBase + "def/eosm0093.mas";
         |CARGA_DEF aMas, eosm0193@;
         |SI FSalida = 0;

             |SI #98#3 = "S";
                 |BUCLE BorrarPrimero;
             |FINSI;

             |ABRE #114;
             |ABRE #93;
             |SI enAlta = 0;
                 |BUCLE CopConfigura;
             |SINO;
                 |BUCLE agifigen;
             |FINSI;
             |CIERRA #93;
             |CIERRA #114;

             |DESCARGA_DEF #eosm0193@;

             enSwApli   = 4;
             enElEjer   = #98#2;
             enDEmpresa = #98#4;
             enHEmpresa = #98#5;
             aAlfa1     = #98#0;
             aAlfa1     = "eosz0095;" + aAlfa1;
             |SUB_EJECUTA_NP aAlfa1;
         |FINSI;

     |FINSI;
|FCAL;

|PROGRAMA;

     aParam = PARAMETRO; |QBF aParam;
     |SI aParam = "";

         |CLS;
         |MANTE #98;

     |SINO;

         |DDEFECTO #98;
         |PINPA #0#98;
         |C_M #98#0, 1, "N";

         #98#0 = aParam;
         #98#1 = enElEjer - 1;
         #98#2 = enElEjer;

         |SI enAlta = 0;
             |ATRI R; |PINTA 0709, " Copiar Configuracion Deducciones Modelos 130 y 131 "; |ATRI 0;
             |SI enElEjer < 15;
                 |C_V #98#7, 1, "N"; |C_M #98#7, 1, "N";
                 |PINTA 1510, "                                                  ";
                 #98#7 = "N";
             |FINSI;
         |SINO;
             |ATRI R; |PINTA 0709, " Recalcular Rendimiento Ej.Anterior para Deducciones"; |ATRI 0;
             |C_V #98#1, 1, "N"; |C_M #98#1, 1, "N";
             |C_V #98#3, 1, "N"; |C_M #98#3, 1, "N";
             |C_V #98#7, 1, "N"; |C_M #98#7, 1, "N";
             #98#3 = "N"; #98#7 = "S";
             |PINTA 1010, "                                                  ";
             |PINTA 1410, "                                                  ";
             |PINTA 1510, "                                                  ";
         |FINSI;
         |PINDA #0#98;
         |ENDATOS #1#98;
     |FINSI;
|FPRO;
