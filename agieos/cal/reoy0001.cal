|FICHEROS;
     reoy0001 #0;
     eosm0023 #1;
     eosm0024 #2;
     eosclien #3;
     eosactiv #4;
     eoscolib #5;
     eosma004 #9;

     eosw0044 #99,MANTE;
|FIN;

|VARIABLES;
     fFecha    = @;

     aAlfa     = "";
     aFichero  = "";

     nTecla    = 0;
     nCampos   = 0;
     nAnyo     = 0;
     nSwTempo  = 0;
     nRegistro = 0;
     &anyo     = 0;
|FIN;

|INCLUYE i_consul;

|PROCESO Mayus;
     #0Campo = #0Campo > #0Campo;
     |PINTA #0Campo;
|FPRC;

|PROCESO PonFecha; |TIPO 7;
     fFecha  = ~;
     aAlfa   = fFecha % -104;
     #0#29   = aAlfa;  |PINTA #0#29;

     aAlfa  = "01.01." + #0#29;   #0#33 = aAlfa;  |PINTA #0#33;
     aAlfa  = "31.12." + #0#29;   #0#34 = aAlfa;  |PINTA #0#34;
|FPRC;

|PROCESO Seleccion;
     |SI #0#0 = "S";
         |C_M #0#1, 1, "N";   #0#1 = 1;      |PINTA #0#1;
         |C_M #0#2, 1, "N";   #0#2 = 99999;  |PINTA #0#2;
         |PARA nCampos = 3; |SI nCampos [ 26;  |HACIENDO nCampos = nCampos + 1;
               |C_M #0nCampos, 1, "S";
         |SIGUE;
     |SINO;
         |C_M #0#1, 1, "S";
         |C_M #0#2, 1, "S";
         |PARA nCampos = 3; |SI nCampos [ 26;  |HACIENDO nCampos = nCampos + 1;
               |C_M #0nCampos, 1, "N";  #0nCampos = 0;  |PINTA #0nCampos;
         |SIGUE;
     |FINSI;
|FPRC;

|PROCESO Graba04;
     |ABRE #5;
     |LEE 040#5p;
     |SI FSalida ! 0;  |DDEFECTO #5;  |FINSI;
     |CIERRA #5;

     |SI #5#89 = "N"; |ACABA; |FINSI;

     |ABRE #9;
     #9#0 = #99#0;
     #9#1 = #99#1;
     #9#2 = #0#32;
     #9#3 = #99#2;
     |LIBERA #9;
     |LEE 140#9=;
     |SI FSalida = 0;
         #9#6  = #0#33;
         #9#7  = #0#34;
         #9#16 = 1;
         #9#17 = #99#6 - 1;
         #9#18 = nRegistro;
         #9#19 = #0#30;
         |GRABA 140#9a;
     |SINO;
         #9#0  = #99#0;
         #9#1  = #99#1;
         #9#2  = #0#32;
         #9#3  = #99#2;
         #9#6  = #0#33;
         #9#7  = #0#34;
         #9#16 = 1;
         #9#17 = #99#6 - 1;
         #9#18 = nRegistro;
         #9#19 = #0#30;
         |GRABA 140#9n;
     |FINSI;
     |CIERRA #9;
|FPRC;

|PROCESO ImprimeL;
     anyo = #2#3;
     |SI #0#31 = "R";
         #0#38 = #2#5;
         #0#39 = #0#39 + #2#5;
         #0#40 = #0#37 - #0#39;
     |SINO;
         #0#35 = "01.01.0000";
         #0#42 = "01.01.0000";
         #0#36 = #2#4;
         #0#38 = #2#5;
         #0#39 = #0#39 + #2#5;
         #0#40 = #0#37 - #0#39;

         nRegistro = nRegistro + 1;
         |PRINT;
     |FINSI;
|FPRC;

|DEFBUCLE eosm0024;
     #2#1;
     ;
     #1#0, #1#1, #1#2,  0000;
     #1#0, #1#1, #1#2, #0#29;
     ;
     NULL, ImprimeL;
|FINSI;

|PROCESO ImprimeC;
     |SI #1#23 < #0#29;  |ACABA;  |FINSI;

     |PDEFECTO #0, 35, 41;

     #0#42 = #1#12;
     #0#35 = #1#13;
     #0#36 = #1#3;
     |SI #1#6 = "F";
         #0#37 = #1#16;
     |SINO;
         #0#37 = #1#14;
     |FINSI;

     #0#40 = #0#37 - #0#39;

     |SI #0#31 = "D";  nRegistro = nRegistro + 1;  |PRINT;  |FINSI;

     |BUCLE eosm0024;

     |SI #0#31 = "R";  nRegistro = nRegistro + 1;  |PRINT;  |FINSI;

     |HAZ Graba04;
|FPRC;

|DEFBUCLE eosm0023S;
     #1#1;
     ;
     #99#0, #99#1, 00000;
     #99#0, #99#1, 99999;
     ;
     NULL, ImprimeC;
|FIN;

|PROCESO PreparaLibro;
     nRegistro = 0;
     |BUCLE eosm0023S;
     |PIEINF;
|FPRC;

|DEFBUCLE eosw0044;
  #99#1;
  8;
  00000, 0, 00, "S";
  99999, 9, 99, "S";
  ;
  NULL, PreparaLibro;
|FIN;

|PROCESO GrabaTempo;
     aAlfa = ("0000" + #0#29) % -102;
     nAnyo = aAlfa;

     |SI #1#23 < #0#29;  |ACABA;  |FINSI;

     |ABRE #4;
     #4#0 = #1#0;
     #4#1 = #1#1;
     #4#2 = nAnyo;
     |LEE 040#4=;
     |SI FSalida ! 0; |DDEFECTO #4; |FINSI;
     |CIERRA #4;

     #99#0 = #1#0;
     #99#1 = #1#1;
     #99#2 = nAnyo;
     |LIBERA #99;
     |LEE 140#99=;
     |SI FSalida = 0;  |ACABA;  |FINSI;

     nSwTempo = 1;

     #99#0 = #1#0;
     #99#1 = #1#1;
     #99#2 = nAnyo;
     #99#3 = #4#3;
     #99#4 = #4#6;
     #99#5 = 1;
     #99#6 = 1;
     #99#7 = "N";
     #99#8 = "S";
     |GRABA 140#99n;
|FPRC;

|DEFBUCLE eosm0023P;
     #1#1;
     ;
     #0#1, #0#27, 00000;
     #0#2, #0#28, 99999;
     ;
     NULL, GrabaTempo;
|FIN;

|RUTINA ClaveBaja;
     #99#0 = 00001;
     #99#1 = 0;
     #99#2 = 00;
|FRUT;

|RUTINA ClaveAlta;
     #99#0 = 99999;
     #99#1 = 9;
     #99#2 = 99;
|FRUT;

|PROCESO Tipo3; |TIPO 3;
     aFichero = Usuario % 808;  |QBT aFichero;
     aFichero = ("L6" + aFichero + "zzzzzzzz") % 108;
     |NOME_DAT #99 aFichero;
     |ABRE #99;  |CIERRA #99;  |DELINDEX #99;

     |IMPRE 0;
     |SI FSalida ! 0; |ACABA; |FINSI;

     |ABRE #99;
     |SI #0#0 = "S";
         |PARA nCampos = 3; |SI nCampos [ 26;  |HACIENDO nCampos = nCampos + 1;
               |SI #0nCampos ! 0;
                   #0#1 = #0nCampos;
                   #0#2 = #0nCampos;
                   |BUCLE eosm0023P;
                |FINSI;
         |SIGUE;
     |SINO;
         |BUCLE eosm0023P;
     |FINSI;
     |CIERRA #99;

     |SI nSwTempo = 0;
         |ABRE #99;  |CIERRA #99;  |DELINDEX #99;
         |ACABA;
     |FINSI;

     |ENTLINEAL #1#99, 3, 4, 20, 1, ClaveBaja, ClaveAlta;

     |SI #0#31 = "D";  |INFOR "reoy1001";  |FINSI;
     |SI #0#31 = "R";  |INFOR "reoy2001";  |FINSI;
     |SI FSalida ! 0;  |FINIMP; |ACABA;    |FINSI;

     |ABRE #3;
     |BUCLE eosw0044;
     |CIERRA #3;
     |FININF;
     |FINIMP;
|FPRC;

|| ****************************** Procesos del Temporal

|PROCESO Tipo11;  |TIPO 11;
     |SI FSalida ! 9;  |Y FSalida ! 10;  |ACABA;  |FINSI;

     nTecla = FSalida;

     |LIBERA #99;
     |LEE 140#99=;

     |SI nTecla = 9;
           |SI #99#8 = "N";
               #99#8 = "S";
           |SINO;
               #99#8 = "N";
               #99#6 = 0;
           |FINSI;
     |FINSI;

     |SI nTecla = 10;
         #99#8 = "S";
         |C_M #99#6, 1, "S";
         |ENCAMPO #6#99;
     |FINSI;

     |PINTA #99#6;
     |PINTA #99#8;

     |GRABA 140#99a;

     |C_M #99#6, 1, "N";
|FPRC;
