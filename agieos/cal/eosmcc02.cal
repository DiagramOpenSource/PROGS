|FICHEROS;
  eosmccw1 #599;
  eosmcc01 #501;
  eosmcc02 #502,MANTE;
|FIN;

|VARIABLES;
  nLinea  = 0;
  nDLinea = 0;
  nHLinea = 0;
  nNumero = 0;
  aAlfa   = "";
  aAlfa1  = "";
  aAlfa2  = "";
  fFecha1 = @;
  nNume1  = 0;
  nNume2  = 0;
  nNume3  = 0;
  nTp     = 0;
  nModo   = 0;
  varmes  = 0;
  nSuma1    = 0;
  nSuma2    = 0;
  nSuma3    = 0;
  nNumBase  = 0;
  nLineaUlt = 0;
  nSwOp     = 0;
  &ePrograma = 0;
  nMultiplo  = 0;
|FIN;

|INCLUYE i_variar;
|INCLUYE i_varcaj;

|| ///////////////////////////
|CALCULO Tipo12CC02; |TIPO 12;
    || Para que no pregunte conforme
    |SI nModo = 1; eSwCambio = 1; |FINSI;
|FCAL;

|CALCULO Tipo7CC02; |TIPO 7;
    |BLIN 4;
    |SI ePrograma ! 1; |Y eModoCaja ! 4;
        |MENSA "    <F3> Modificar Registro, <F4> Baja Registro";
    |FINSI;
|FCAL;

|CALCULO Tipo0CC02; |TIPO 0;
    |BLIN 4;
    nModo = (FEntrada / 100) ! 0;
    |SI nModo = 1; |Y #599#16 = #599#10;
        aAlfa = "    NFactura cobrada. Desea dar de alta otra linea";
        |SI #599#13 = "S";
            aAlfa = "    NFactura pagada. Desea dar de alta otra linea";
        |FINSI;
        |CONFI aAlfa;
        |SI FSalida ! 0;
            |ERROR;
            |ACABA;
        |FINSI;
    |FINSI;
    |SI nModo = 1; |Y #502#4 = 1;
        #502#5 = #599#4; |PINTA #502#5;
        |HAZ CalPeriodo;
    |FINSI;
|FCAL;

|PROCESO LosTotales;
      #599#16 = #599#16 + (nMultiplo * #502#10);
      #599#18 = #599#18 + (nMultiplo * #502#13);
      #599#19 = #599#19 + (nMultiplo * #502#11);
      #599#20 = #599#20 + (nMultiplo * #502#12);

      #501#6  = #501#6  + (nMultiplo * #502#11);
      #501#7  = #501#7  + (nMultiplo * #502#12);
      #501#12 = #501#12 + (nMultiplo * #502#10);
      #501#16 = #501#16 + (nMultiplo * #502#13);

||      |SI ePrograma = 0;
          |PINTA 2227, #599#16;
          |PINTA 2243, #599#18;
||      |FINSI;
|FPRC;

|CALCULO Tipo2CC02; |TIPO 2;
     nModo = (FEntrada / 100) ! 0;

     |SI #502#36 ! -1; |Y nModo = 3;
         |MENAV "    Movimiento de otro Ejercicio. No puede borrarlo";
         |ERROR;
         |ACABA;
     |FINSI;

     nMultiplo = 0 +. 1;
     |HAZ LosTotales;
|FCAL;

|CALCULO Tipo1CC02; |TIPO 1;
     nModo = (FEntrada / 100) ! 0;
     |SI #502#36 ! -1;
         aAlfa =  "    Movimiento de otro Ejercicio. No puede ";
         |SI nModo = 2; aAlfa = aAlfa + "modificarlo"; |FINSI;
         |SI nModo = 3; aAlfa = aAlfa + "borrarlo"; |FINSI;
         |MENAV aAlfa;
         |ERROR;
         |ACABA;
     |FINSI;
|FCAL;

|CALCULO Tipo11CC02; |TIPO 11;
|FCAL;

|PROCESO CalPeriodo;
   aAlfa1 = #502#5;
   aAlfa1 = aAlfa1 % 0402; varmes = aAlfa1;
   |SI varmes =  1;|O varmes =  2;|O varmes =  3; #502#7 = 01; |FINSI;
   |SI varmes =  4;|O varmes =  5;|O varmes =  6; #502#7 = 02; |FINSI;
   |SI varmes =  7;|O varmes =  8;|O varmes =  9; #502#7 = 03; |FINSI;
   |SI varmes = 10;|O varmes = 11;|O varmes = 12; #502#7 = 04; |FINSI;
   |PINTA #502#7;
|FPRC;

|CALCULO Tipo0CC0205; |TIPO 0;   || Fecha

   |C_M #502Campo,0,aAlfa;
   |SI aAlfa = "N"; |ACABA; |FINSI;

   |SI #502#5 < #599#4;
       aAlfa = "    Error.! Fecha del cobro inferior a la Fecha de la Factura";
       |SI #599#13 = "S";
           aAlfa = "    Error.! Fecha del pago inferior a la Fecha de la Factura";
       |FINSI;
       |MENAV aAlfa;
       |ERROR;
       |ACABA;
   |FINSI;

   fFecha1 = Contenido;
   |SI fFecha1 ! #502#5;
       |HAZ CalPeriodo;
   |FINSI;
|FCAL;

|CALCULO Tipo7CC0210; |TIPO 7;
   aAlfa = "    Pulse <F2> para cuadrar Importe Total ";
   |SI #599#13 = "S";
       aAlfa = aAlfa + "pagado";
   |SINO;
       aAlfa = aAlfa + "cobrado";
   |FINSI;
   |MENSA aAlfa;
|FCAL;

|CALCULO Tipo0CC0210; |TIPO 0;   || Total Cobrado.
   |C_M #502Campo,0,aAlfa1;
   |SI aAlfa1 = "N"; |ACABA; |FINSI;

   |SI FSalida = 10;
       #502#10 = #599#10 - #599#16; |PINTA #502#10;
       #502#13 = 100 - #599#18;
   |FINSI;

   nNume1 = (#599#16 + #502#10) ! 2;
   |SI nNume1 > #599#10; |Y #599#10 > 0;
       |MENAV "1234Error, Importe superior al Total Factura";
       |ERROR;
       |ACABA;
   |FINSI;

   |SI nNume1 < #599#10; |Y #599#10 < 0;
       |MENAV "1234Error, Importe superior al Total Factura";
       |ERROR;
       |ACABA;
   |FINSI;

   nNume1 = Contenido;
   |SI nNume1 = #502Campo;
       |ACABA;
   |FINSI;

   nNume1 = (#599#16 + #502#10) ! 2;
   nNume3 = (#599#8 + #599#9) ! 2; ||sumo base + iva factura
   eSwCambio = 1;
   |SI #599#10 = #502#10;
       nTp = 100;
       #502#11 = #599#8;
       #502#12 = #599#9;
   |SINO;
       nTp   = ((#502#10 * 100) / #599#10) ! 4;
       #502#11 = (#599#8 % nTp) ! 2;
       #502#12 = (#599#9 % nTp) ! 2;
       |SI nNume1 = #599#10;
           nTp = (100 - #599#18) ! 4;
           #502#11 = #599#8 - #599#19;
           #502#12 = #599#9 - #599#20;
        |FINSI;
        |SI nNume3 = #599#10;   ||no hay IRPF
            nNume2 = #502#11 + #502#12;
            |SI nNume2 ! #502#10;     ||para cuadrar lo mas posible
                  #502#12 = (#502#12 + (#502#10 - nNume2)) ! 2;
            |FINSI;
       |FINSI;
   |FINSI;
   #502#13 = nTp;
|FCAL;
