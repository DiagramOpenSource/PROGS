|FICHEROS;
   eosz0096 #96;

   eosm0093 #93,MANTE;
   eosm0094 #94;

   eosm3130 #130;
   eosm0130@ #1130;

   eosm0020 #131;
   eosm0020@ #1131;

   eosclien #1000;
   eosactiv #114;
|FIN;

|VARIABLES;
     nCampo    = 0;

     aParam    = "";
     aBase     = "";
     aMas      = "";
     nDEmpresa = 0;
     nHEmpresa = 0;
     nHPer     = 0;
     nHComple  = 0;

     nSumaD    = 0;
     nSwNeg    = 0;

     nEmpresa  = 0;
     nPeriodo  = 0;
     nEjer     = 0;
     nEjer4    = 0;
     nModelo   = 0;
     nComplem  = 0;
     nImpAnt   = 0;
     nImpNuevo = 0;
     nImporte  = 0;

     nTope        = 0;
     nTopeAgrario = 0;
     nTope131     = 0;
     nSuma        = 0;

     aAlfa1    = "";
     aAlfa2    = "";
     errconta  = 0;
     nModo     = 0;
     nInkey    = 0;

     nSwConPro   = 0;
     fFecIni     = @;
     fFecFin     = @;
     aAplica1    = "";
     aAplica2    = "";
     nAplica3    = 0;
     aCoBien     = "";
     nNoModi     = 0;

     nApli400     = 0;
     nRend_NoAgr  = 0;
     nRend_Agr    = 0;
     nRendimiento = 0;
     nTopeHM      = 0;
     nTopeM       = 0;
     nTopeV       = 0;
     nTopeV0      = 0;
     nTopeV1      = 0;
     nTotalM      = 0;
     nTotalV      = 0;
     nPagAnt      = 0;

     nNume1       = 0;
     nNume2       = 0;
     &enElEjer    = 0;
     fFecha       = @;
     aAlfa        = "";
|FIN;

|INCLUYE i_vareos;
|| ////////////////////////////////////////////////////////////////////////
|PROCESO Leodsmom094_z96;
   nTotalM = nTotalM + #94#8;
   nTotalV = nTotalV + #94#9;
|FPRC;

|DEFBUCLE Leodsmom094_z96;
   #94#1;
   10;
   nEjer, nEmpresa, 0, 1;
   nEjer, nEmpresa, 9, nPeriodo;
   e;
   NULL, Leodsmom094_z96;
|FIN;

|PROCESO CambioAplicar;

     nApli400 = 100;
     aAplica1 = #93#4;
     aAplica2 = #93#5;
     nAplica3 = #93#14; |SI nEjer4 < 2011; nAplica3 = 0; |FINSI;

     nTopeHM  = 8000;
     nTopeM   = 12000;
     nTopeV   = 33007.20;
     nTopeV0  = 33007.20;
     nTopeV1  = 22000;

|| ......... Si no desea aplicar ni uno ni el otro, acaba ya.
     |SI aAplica1 = "N"; |Y aAplica2 = "N"; |ACABA; |FINSI;

     |SI nPeriodo =  1; nNume1 = 6; nNume2 = 10; |FINSI;
     |SI nPeriodo =  2; nNume1 = 7; nNume2 = 11; |FINSI;
     |SI nPeriodo =  3; nNume1 = 8; nNume2 = 12; |FINSI;
     |SI nPeriodo =  4; nNume1 = 9; nNume2 = 13; |FINSI;

     nTotalM = 0;
     nTotalV = 0;
     |BUCLE Leodsmom094_z96;  || Sumo actividades

     |SI aAplica1 = "S";        || Calculo para Minoracion

         |SI #93#1 < 15;
             nApli400 = 100;
             nRendimiento = (nRend_NoAgr + (nRend_Agr * 0.25)) ! 2;
             |SI #93nNume1 ! "X";   || Si no fuerzo a Deducir S
                 |SI nRendimiento > nTopeM; |O nTotalM > nTopeM;
                     aAplica1 = "N";
                 |SINO;
                     |SI nTotalM > nTopeHM;
                         nApli400 = (( 400 - (nTotalM - nTopeHM) * 0.1) / 4) ! 2;
                     |FINSI;
                 |FINSI;
             |FINSI;
         |SINO;
             nApli400 = 0;
             |SI #93#15 [ 9000;                     nApli400 = 100; |FINSI;
             |SI #93#15 > 9000;  |Y #93#15 [ 10000; nApli400 =  75; |FINSI;
             |SI #93#15 > 10000; |Y #93#15 [ 11000; nApli400 =  50; |FINSI;
             |SI #93#15 > 11000; |Y #93#15 [ 12000; nApli400 =  25; |FINSI;
         |FINSI;
     |FINSI;

     nTopeV = nTopeV0;
     |SI nAplica3 = 1; nTopeV = nTopeV1; |FINSI;
     |SI aAplica2 = "S";  || Calculo para Minoracion
         |SI #93nNume2 ! "X";
             |SI nTotalV > nTopeV;
                 aAplica2 = "N";
              |FINSI;
         |FINSI;
     |FINSI;
|FPRC;
|| ////////////////////////////////////////////////////////////////////////
|PROCESO eosm0020_neg;
     |SI #1131#47 < 0; nSwNeg = 1; |FINSI;

     |SI #1131#47 ] 0;  |Y nSwNeg = 0; |ACABA;  |FINSI;

     nSumaD = nSumaD + #1131#55;

     |SI #1131#47 < 0;
         #131#56 = #131#56 + -#1131#47;
     |SINO;
         #131#56 = #131#56 - #1131#55;
         |SI #131#56 [ 0; nSwNeg = 0; #131#56 = 0; nSumaD = 0; |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE eosm0020_neg;
     #1131#1003;
     ;
     #131#0, #131#1,     1;
     #131#0, #131#1, nHPer;
     ;
     NULL, eosm0020_neg;
|FIN;

|PROCESO CapturaNegativos131;
     #131#56 = 0;
     |SI #131#55 [ 0;  |ACABA;  |FINSI;

     |DBASE aBase;  |QBF aBase;
     aMas = aBase + "def/eosm0020.mas";
     |CARGA_DEF aMas, eosm0020@;

     nSwNeg   = 0;
     nSumaD   = 0;
     #131#56    = 0;
     nHPer    = #131#2 - 1;
     |BUCLE eosm0020_neg;
     |SI #131#56 > #131#55;  #131#56 = #131#55;  |FINSI;

     |DESCARGA_DEF #eosm0020@;
|FPRC;

|PROCESO eosm0020Tope;
     nTope131 = nTope131 + #1131#61;
|FPRC;

|DEFBUCLE eosm0020Tope;
     #1131#1003;
     ;
     #131#0, #131#1,     1;
     #131#0, #131#1, nHPer;
     ;
     NULL, eosm0020Tope;
|FIN;

|PROCESO CapturaTope;
     |DBASE aBase;  |QBF aBase;
     aMas = aBase + "def/eosm0020.mas";
     |CARGA_DEF aMas, eosm0020@;

     nHPer    = #131#2 - 1;
     nTope131 = 0;
     |BUCLE eosm0020Tope;

     |DESCARGA_DEF #eosm0020@;
|FPRC;

|PROCESO CalculaDed131;
     #131#55 = #131#53 - #131#54 - #131#59;

     |SI #131#55 [ 0;
         #131#56 = 0;
         #131#61 = 0;
         |ACABA;
     |FINSI;

     |SI nEjer4 ] 2008;  |Y #131#2 > 1;
         |HAZ CapturaNegativos131;
     |FINSI;

     nSuma = #131#56 + #131#61;
     |SI nSuma > #131#55;
         |SI #131#56 > #131#55;
             #131#56 = #131#55;
             #131#61 = 0;
             |ACABA;
         |FINSI;

         #131#61 = #131#55 - #131#56;
     |FINSI;
|FPRC;

|PROCESO RecalModelo_131;
     |SI #131#1 ] 80;
         nEjer4 = 1900 + #131#1;
     |SINO;
         nEjer4 = 2000 + #131#1;
     |FINSI;

     #131#40 = 0;
     #131#60 = 0;
     |SI #131#6  ! "S";  #131#40 = #131#40 + #131#11;  #131#60 = #131#60 + #131#9;  |FINSI;
     |SI #131#13 ! "S";  #131#40 = #131#40 + #131#18;  #131#60 = #131#60 + #131#16; |FINSI;
     |SI #131#20 ! "S";  #131#40 = #131#40 + #131#25;  #131#60 = #131#60 + #131#23; |FINSI;
     |SI #131#27 ! "S";  #131#40 = #131#40 + #131#32;  #131#60 = #131#60 + #131#30; |FINSI;
     |SI #131#34 ! "S";  #131#40 = #131#40 + #131#39;  #131#60 = #131#60 + #131#37; |FINSI;

     nEmpresa  = #131#0;
     nEjer     = #131#1;
     nPeriodo  = #131#2;

    |SI #131#4 = "S"; |ACABA; |FINSI; ||no modifica

     nRend_NoAgr = #131#60; nRend_Agr = #131#43;
     |SI nEjer4 ] 2010;  |HAZ CambioAplicar; |FINSI;

     #131#59 = 0;
     |SI nEjer4 = 2008; |Y aAplica1 ! "N";
         |SI #131#2 = 2;  #131#59 = 200;  |FINSI;
         |SI #131#2 = 3;  #131#59 = 100;  |FINSI;
         |SI #131#2 = 4;  #131#59 = 100;  |FINSI;
     |FINSI;

     |SI nEjer4 = 2009; |Y aAplica1 ! "N";
         |SI #131#2 = 1;  #131#59 = 100;  |FINSI;
         |SI #131#2 = 2;  #131#59 = 100;  |FINSI;
         |SI #131#2 = 3;  #131#59 = 100;  |FINSI;
         |SI #131#2 = 4;  #131#59 = 100;  |FINSI;
     |FINSI;

     |SI nEjer4 ] 2010; |Y aAplica1 ! "N";
         #131#59 = nApli400;
     |FINSI;

     #131#61 = 0;
     |SI nEjer4 ] 2009; |Y aAplica2 = "S";
         |SI #131#40 ! 0;
             #131#61 = (#131#60 % .5) + (#131#51 % 2);
         |SINO;
             #131#61 = #131#43 % 2;
         |FINSI;

         |HAZ CapturaTope;

         |SI nAplica3 = 0;
             nTope = 660.14 - nTope131;
         |SINO;
             nTope = 440 - nTope131;
         |FINSI;
         |SI nTope < 0;  nTope = 0;  |FINSI;
         |SI #131#61 > nTope;
             #131#61 = nTope;
         |FINSI;
     |FINSI;

     |HAZ CalculaDed131;

     #131#57 = #131#55 - #131#56 - #131#61;
     #131#47 = #131#57 - #131#58;

     |GRABA 020#131a;
     |LIBERA #131;
|FPRC;

|DEFBUCLE eosm0020;
    #131#1;
    ;
    nEmpresa, nEjer, 01;
    nEmpresa, nEjer, 12;
    ;
    NULL, RecalModelo_131;
|FIN;

|| ---- s130 ----------------------------------------------------------------
|PROCESO eosm0130_neg;

     |SI #1130#3 = 8;  |Y #1130#2 = 1;
          nImporte = #1130#29;
     |SINO;
          nImporte = #1130#46;
     |FINSI;

     |SI nImporte < 0; nSwNeg = 1; |FINSI;

     |SI nImporte ] 0;  |Y nSwNeg = 0; |ACABA;  |FINSI;

     nSumaD = nSumaD + #1130#42;

     |SI nImporte < 0;
         #130#43 = #130#43 + -nImporte;
     |SINO;
         #130#43 = #130#43 - #1130#42;
         |SI #130#43 [ 0; nSwNeg = 0; #130#43 = 0; nSumaD = 0; |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE eosm0130_neg;
     #1130#1004;
     ;
     #130#0, #130#1,     1, #130#3;
     #130#0, #130#1, nHPer, #130#3;
     ;
     NULL, eosm0130_neg;
|FIN;

|PROCESO CapturaNegativos130;
     |SI #130#42 [ 0;  |ACABA;  |FINSI;

     |DBASE aBase;  |QBF aBase;
     aMas = aBase + "def/eosm3130.mas";
     |CARGA_DEF aMas, eosm0130@;

     nSwNeg   = 0;
     nSumaD   = 0;
     #130#43    = 0;
     nHPer    = #130#2 - 1;
     |BUCLE eosm0130_neg;
     |SI #130#43 > #130#42;  #130#43 = #130#42;  |FINSI;

     |DESCARGA_DEF #eosm0130@;
|FPRC;

|PROCESO eosm0130Tope;
     nTopeAgrario = nTopeAgrario + #1130#47;
|FPRC;

|DEFBUCLE eosm0130Tope;
     #1130#1004;
     ;
     #130#0, #130#1,     1, #130#3;
     #130#0, #130#1, nHPer, #130#3;
     ;
     NULL, eosm0130Tope;
|FIN;

|PROCESO CapturaTopeAgrario;
     |DBASE aBase;  |QBF aBase;
     aMas = aBase + "def/eosm3130.mas";
     |CARGA_DEF aMas, eosm0130@;

     nTopeAgrario = 0;
     nHPer        = #130#2 - 1;
     |BUCLE eosm0130Tope;

     |DESCARGA_DEF #eosm0130@;
|FPRC;

|PROCESO CalculaDed130;
     |SI nEjer4 < 2008;
         #130#41 = 0;
         #130#42 = 0;
         #130#43 = 0;
         #130#44 = 0;
         #130#47 = 0;
         |ACABA;
     |FINSI;

     #130#42 = #130#29 - #130#41;

     |SI #130#42 [ 0;
         #130#43 = 0;
         #130#47 = 0;
         |ACABA;
     |FINSI;

     |SI nEjer4 = 2008;
         |SI #130#2 > 1;
             |HAZ CapturaNegativos130;
         |SINO;
             #130#41 = 0;
             #130#42 = 0;
             #130#43 = 0;
             #130#44 = 0;
             #130#47 = 0;
             |ACABA;
         |FINSI;
     |FINSI;

     |SI nEjer4 ] 2009;
         |SI #130#2 ! 1;
             |HAZ CapturaNegativos130;
         |FINSI;
     |FINSI;

     nSuma = #130#43 + #130#47;
     |SI nSuma > #130#42;
         |SI #130#43 > #130#42;
             #130#43 = #130#42;
             #130#47 = 0;
             |ACABA;
         |FINSI;

         #130#47 = #130#42 - #130#43;
     |FINSI;
|FPRC;

|PROCESO RecalModelo_130;
     |SI #130#3 ] 80;
         nEjer4 = 1900 + #130#3;
     |SINO;
         nEjer4 = 2000 + #130#3;
     |FINSI;

     nEmpresa  = #130#0;
     nEjer     = #130#3;
     nPeriodo  = #130#2;

     |SI #130#5 = "S"; |ACABA; |FINSI; ||no modifica

     nRend_NoAgr = #130#26; nRend_Agr = #130#30;
     |SI nEjer4 ] 2010;  |HAZ CambioAplicar; |FINSI;

     #130#41 = 0; #130#42 = 0;
     #130#43 = 0; #130#44 = 0;
     |SI nEjer4 = 2008; |Y aAplica1 ! "N";
         |SI #130#2 = 2;  #130#41 = 200;  |FINSI;
         |SI #130#2 = 3;  #130#41 = 100;  |FINSI;
         |SI #130#2 = 4;  #130#41 = 100;  |FINSI;
     |FINSI;

     |SI nEjer4 = 2009; |Y aAplica1 ! "N";
         |SI #130#2 = 1;  #130#41 = 100;  |FINSI;
         |SI #130#2 = 2;  #130#41 = 100;  |FINSI;
         |SI #130#2 = 3;  #130#41 = 100;  |FINSI;
         |SI #130#2 = 4;  #130#41 = 100;  |FINSI;
     |FINSI;

     |SI nEjer4 ] 2010; |Y aAplica1 ! "N";
         #130#41 = nApli400;
     |FINSI;

     #130#47 = 0;
     |SI nEjer4 ] 2009; |Y aAplica2 = "S";
         |SI #130#22 > 0;
             #130#47 = #130#22 % 2;
             |SI nAplica3 = 0;
                 |SI #130#47 > 660.14;
                     #130#47 = 660.14;
                 |FINSI;
             |SINO;
                 |SI #130#47 > 440;
                     #130#47 = 440;
                 |FINSI;
             |FINSI;
         |SINO;
             |SI #130#30 > 0;
                 #130#47 = #130#30 % 2;
                 |HAZ CapturaTopeAgrario;
                 |SI nAplica3 = 0;
                     nTope = 660.14 - nTopeAgrario;
                 |SINO;
                     nTope = 440 - nTopeAgrario;
                 |FINSI;
                 |SI nTope < 0;  nTope = 0;  |FINSI;
                 |SI #130#47 > nTope;
                     #130#47 = nTope;
                 |FINSI;
             |FINSI;
         |FINSI;
     |FINSI;

     |HAZ CalculaDed130;

     #130#44 = #130#42 - #130#43 - #130#47;
     #130#46 = #130#44 - #130#45;   ||nuevo calculo Ley 80

     |GRABA 020#130a;
     |LIBERA #130;
|FPRC;

|DEFBUCLE eosm3130;
    #130#1;
    ;
    nEmpresa, 0, 01, nEjer;
    nEmpresa, 9, 99, nEjer;
    ;
    NULL, RecalModelo_130;
|FIN;

|PROCESO Actualiza;
    nModelo   = #93#0;
    nEjer     = #93#1;
    nEmpresa  = #93#2;
    |SI nModelo = 130; |BUCLE eosm3130; |FINSI;
    |SI nModelo = 131; |BUCLE eosm0020; |FINSI;
|FPRC;

|CALCULO Tipo1_93; |TIPO 1;
   nModo = (FEntrada / 100) ! 0;
   |SI nModo = 3;
       |SI #96#1 < 15;
           |CONFI "    SConfirmacion Baja  (Aplicar Minoracion) ";
           |SI FSalida ! 0;
               |ERROR;
               |ACABA;
           |FINSI;

           #93#4 = "S"; #93#6 = ""; #93#7 = ""; #93#8 = ""; #93#9 = "";
           #93#5 = "N";

          aAplica1   = #93#4;
          aAplica2   = #93#5;
          nAplica3   = #93#14; |SI #93#1 < 11; nAplica3 = 0; |FINSI;
          |HAZ Actualiza;
       |SINO;
           |MENAV "0000No debe dar de Baja, para el nuevo calculo necesita el Rend.E.Anterior";
           |CONFI "0000NConfirma la Baja ";
           |SI FSalida ! 0;
               |ERROR;
               |ACABA;
           |FINSI;
           #93#4 = "S"; #93#6 = ""; #93#7 = ""; #93#8 = ""; #93#9 = "";
           #93#5 = "N"; #93#15 = 0;

           aAplica1   = #93#4;
           aAplica2   = #93#5;
           nAplica3   = #93#14; |SI #93#1 < 11; nAplica3 = 0; |FINSI;
           |HAZ Actualiza;
       |FINSI;
   |FINSI;
|FCAL;

|CALCULO Aplicar; |TIPO 0;
   aAplica1   = #93#4;
   aAplica2   = #93#5;
   nAplica3   = #93#14; |SI #93#1 < 11; nAplica3 = 0; |FINSI;
   |HAZ Actualiza;
|FCAL;

|CALCULO Tipo7C4F93; |TIPO 7;
   aAlfa1 = "0000Aplicar la Minoracion Articulo 110.3.c del Reglamento";
   |SI #93#1 < 2014;
       aAlfa1 = "0000Aplicar la Minoracion articulo 80 de la Ley del Impuesto";
   |FINSI;
   |MENSA aAlfa1;
|FCAL;

|CALCULO SiMino; |TIPO 0;
     aAlfa1 = #93#4;
     |C_M #93#6, 1, aAlfa1;
     |C_M #93#7, 1, aAlfa1;
     |C_M #93#8, 1, aAlfa1;
     |C_M #93#9, 1, aAlfa1;
     |SI #93#4 = "N";
         #93#6 = " "; |PINTA #93#6;
         #93#7 = " "; |PINTA #93#7;
         #93#8 = " "; |PINTA #93#8;
         #93#9 = " "; |PINTA #93#9;
     |FINSI;
|FCAL;

|CALCULO SiVivi; |TIPO 0;
     aAlfa1 = #93#5;
     |C_M #93#10, 1, aAlfa1;
     |C_M #93#11, 1, aAlfa1;
     |C_M #93#12, 1, aAlfa1;
     |C_M #93#13, 1, aAlfa1;
     |C_M #93#14, 1, aAlfa1;
     |SI #93#5 = "N";
         #93#10 = " "; |PINTA #93#10;
         #93#11 = " "; |PINTA #93#11;
         #93#12 = " "; |PINTA #93#12;
         #93#13 = " "; |PINTA #93#13;
     |FINSI;
     |SI nEjer4 < 2011; |C_M #93#14, 1, "N"; |FINSI;
|FCAL;

|PROCESO dsmod130Ant;
   nRend_NoAgr = #130#26;
   nRend_Agr   = #130#30;
   #93#15 = #130#26 + #130#30;
|FPRC;

|DEFBUCLE dsmod130Ant;
    #130#1;
    ;
    nEmpresa, 0, 01, nEjer;
    nEmpresa, 9, 99, nEjer;
    ;
    NULL, dsmod130Ant;
|FIN;

|PROCESO dsmod131Ant;
    nRend_NoAgr = #131#60;
    nRend_Agr   = #131#43;
    #93#15 = #131#43 + #131#60;
|FPRC;

|DEFBUCLE dsmod131Ant;
    #131#1;
    ;
    nEmpresa, nEjer, 01;
    nEmpresa, nEjer, 12;
    ;
    NULL, dsmod131Ant;
|FIN;

|CALCULO RecojoAuto; |TIPO 0;
     |C_M #93#15,0,aAlfa1;
     |SI aAlfa1 = "N"; |ACABA; |FINSI;

     nInkey = FSalida;
     |SI nInkey = 15;
         enSwApli = 3;
         enEmp130    = #93#2;
         enEje130    = #93#1;
         enMod130    = #93#0;
         eaNombreAp  = #1000#2;
         |HAZ eGrabadsmom093;
         |PINTA #93#15;
         |ERROR;
||         nModelo   = #93#0;
||         nEjer     = #93#1 - 1;
||         nEmpresa  = #93#2;
||         #93#15 = 0;
||         |SI nModelo = 130; |BUCLE dsmod130Ant; |FINSI;
||         |SI nModelo = 131; |BUCLE dsmod131Ant; |FINSI;
||         |PINTA #93#15;
     |FINSI;
|FCAL;

|| ***************************************************************************
|| \\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\
|| .... Mirar si hace el modelo de IVA
|PROCESO MiraSiD;
  |SI #114#48 > "01.01.1900";
      |SI #114#48 > fFecFin;  |ACABA;  |FINSI;
  |FINSI;
  |SI #114#49 > "01.01.1900";
      |SI #114#49 < fFecIni;  |ACABA;  |FINSI;
  |FINSI;

  |SI #114#22 = "A"; |Y #93#0 = 130;
     |ACABA;
  |FINSI;
  |SI #114#22 ! "A"; |Y #93#0 = 131;
     |ACABA;
  |FINSI;

  nSwConPro = 0;
  aCoBien = #114#46;
|FPRC;

|DEFBUCLE MiraSiModelo;
 #114#1;
 ;
 #93#2, 1, #93#1;
 #93#2, 9, #93#1;
 ;
 NULL, MiraSiD;
|FIN;

|PROCESO LeeEmpresa;
  || Lee Empresa
    errconta = 0;
    |ABRE #1000;
    #1000#0 = nEmpresa;
    |LEE 041#1000=;
    |SI FSalida ! 0;
        errconta = 1;
    |FINSI;
    |CIERRA #1000;
    #93#3 = #1000#2;
|FPRC;

|PROCESO BajaCli;
 #1000#0 = 1;
 #1000#2 = "";
|FPRC;

|PROCESO AltaCli;
 #1000#0 = 99999;
 #1000#2 = "z" * 40;
|FPRC;

|PROCESO Tipo11_93; |TIPO 11;
 nInkey = FSalida;

 |SI nInkey ! 10; |Y nInkey ! 11; |ACABA; |FINSI;
 |ERROR;
 |ABRE #1000;
 |SI nInkey = 10;
     #1000#0 = #93#2;
     |CONSULTA_F_CLAVE #1#1000, BajaCli, AltaCli;
 |SINO;
     #1000#2 = #93#3;
     |CONSULTA_F_CLAVE #3#1000, BajaCli, AltaCli;
 |FINSI;
 #93#2 = #1000#0; |PINTA #93#2;
 #93#3 = #1000#2; |PINTA #93#3;
 |CIERRA #1000;
|FPRC;

|PROCESO NoModificar131;
  |SI #131#4 = "S"; nNoModi = 1; |FINSI;
|FPRC;

|DEFBUCLE NoModificar131;
 #131#1;
 ;
 #93#2, #93#1, 01;
 #93#2, #93#1, 99;
 ;
 NULL, NoModificar131;
|FIN;

|PROCESO NoModificar130;
  |SI #130#5 = "S"; nNoModi = 1; |FINSI;
|FPRC;

|DEFBUCLE NoModificar130;
 #130#1;
 ;
 #93#2, 0, 01, #93#1;
 #93#2, 9, 99, #93#1;
 ;
 NULL, NoModificar130;
|FIN;

|PROCESO HazBorrar;
  |MENAV aAlfa1;
  nModo = (FEntrada / 100) ! 0;
  |SI nModo = 2;
      |LEE 041#93=;

      |BORRA 020#93a;
      |LIBERA #93;
      |PONCONTROL  23, "SI";
  |FINSI;
  |ERROR;
  |PINTA #93#2;
|FPRC;

|PROCESO Tipo0_93; |TIPO 0;
    nEmpresa = #93#2;
    |HAZ LeeEmpresa;
    |SI errconta = 1;
        aAlfa1 = "    No existe Empresa ";
        |HAZ HazBorrar;
        |ACABA;
    |FINSI;
    #93#3 = #1000#2; |PINTA #93#3;

    aCoBien = "N";
    nSwConPro = -1;
    |BUCLE MiraSiModelo;
    |SI nSwConPro = -1;
        aAlfa1 = "    Empresa sin Modelo " + #93#0;
        |HAZ HazBorrar;
        |ACABA;
    |FINSI;

    |SI aCoBien = "S";
        aAlfa1 = "    Empresa COMUNIDAD DE BIENES. (Informar los Comuneros) ";
        |HAZ HazBorrar;
        |ACABA;
    |FINSI;

     nNoModi = 0;
     |SI #93#0 = 130; |BUCLE NoModificar130; |FINSI;
     |SI #93#0 = 131; |BUCLE NoModificar131; |FINSI;
     |SI nNoModi = 1;
         |MENAV "    Empresa con Modelos Archivados, no puede modificarlos";
         |ERROR;
    |FINSI;
|FPRC;

|| **************************************************************************
|PROCESO AntesPantalla;
   |SI #96#1 < 15;
       aAlfa = "Art.80 de la Ley bis del impuesto ";
       |ATRI R; |PINTA 0673, "       "; |ATRI 0;
   |SINO;
       aAlfa = "Art.110.3.c. del Reglamento ";
       |PINTA 0673, "       ";
   |FINSI;
   |PINTA 0545, "                                  ";
   |ATRI R; |PINTA 0545, aAlfa; |ATRI 0;
|FPRC;

|CALCULO Tipo7C0F96; |TIPO 7;
  |SI #96#1 ] 15;
      aAlfa1 = "0000<F2> Copiar Configuracion Empresas a Nuevo Ejer., <F3> Recalcular Rend.Ant."
  |SINO;
      aAlfa1 = "0000<F2> Copiar Configuracion Empresas a Nuevo Ejercicio";
  |FINSI;
  |MENSA aAlfa1;
  |HAZ AntesPantalla;
|FCAL;

|CALCULO SiCopio; |TIPO 0;
  nInkey = FSalida;
  |BLIN 4;
  |HAZ AntesPantalla;
  |SI nInkey = 10;
      |ERROR;
      enAlta   = 0;
      enElEjer = #96#1;
      aAlfa1 = "eosz0098;" + aParam;
      |SUB_EJECUTA_NP aAlfa1;
      |SI enElEjer ! 0;
          #96#1 = enElEjer;
          |PTEC 802;
      |FINSI;
  |FINSI;
  |SI nInkey = 11; |Y #96#1 ] 15;
      |ERROR;
      enAlta   = 1;
      enElEjer = #96#1;
      aAlfa1 = "eosz0098;" + aParam;
      |SUB_EJECUTA_NP aAlfa1;
      |SI enElEjer ! 0;
          #96#1 = enElEjer;
          |PTEC 802;
      |FINSI;
  |FINSI;
|FCAL;

|PROCESO ClaveBaja;
 #93#0 = #96#0;
 #93#1 = #96#1;
 #93#2 = 1;
|FPRC;

|PROCESO ClaveAlta;
 #93#0 = #96#0;
 #93#1 = #96#1;
 #93#2 = 99999;
|FPRC;

|CALCULO Tipo12; |TIPO 12;

    |SI #96#1 > 80;
        nEjer4 = 1900 + #96#1;
    |SINO;
        nEjer4 = 2000 + #96#1;
    |FINSI;

     aAlfa2 = nEjer4;
     aAlfa1 = "01.01." + aAlfa2; fFecIni = aAlfa1;
     aAlfa1 = "31.12." + aAlfa2; fFecFin = aAlfa1;

     |C_V #93#5, 1, "S";
     |SI nEjer4 [ 2008; |C_V #93#5, 1, "N"; |FINSI;

     |SI nEjer4 [ 2009;
         |PINPA #0#96;
         |PINDA #0#96;
         |C_V #93#6,  1, "N"; |C_V #93#7,  1, "N";
         |C_V #93#8,  1, "N"; |C_V #93#9,  1, "N";
         |C_V #93#10, 1, "N"; |C_V #93#11, 1, "N";
         |C_V #93#12, 1, "N"; |C_V #93#13, 1, "N";
         |C_P #93#2,1,1205;
         |C_P #93#3,1,1212;
         |C_P #93#4,1,1258;
         |C_P #93#5,1,1268;
     |FINSI;

     |C_M #93#14,1,"S";
     |SI nEjer4 < 2011; |C_M #93#14, 1, "N"; |FINSI;

     |C_A #93#3,1,34;
     |C_V #93#15,1,"S"; |C_M #93#15,1,"S";
     |SI nEjer4 < 2015;
         |C_V #93#15,1,"N"; |C_M #93#15,1,"N";
         |C_A #93#3,1,80;
         |SI nEjer4 ] 2009;
             |ATRI R; |PINTA 1042, "            ";
             |PINTA 1142, "            "; |ATRI 0;
             |PINTA 1242, "            ";
             |PINTA 1342, "            ";
             |PINTA 1442, "            ";
             |PINTA 1542, "            ";
             |PINTA 1642, "            ";
             |PINTA 1742, "            ";
             |PINTA 1842, "            ";
             |PINTA 1942, "            ";
             |PINTA 2042, "            ";
             |PINTA 2142, "Ä";
         |FINSI;
     |FINSI;

     |ENTLINEAL #1#93, -3, 1, 20, 0, ClaveBaja, ClaveAlta;
|FCAL;

|PROGRAMA;
     aParam = PARAMETRO; |QBF aParam;
     |SI aParam = ""; aParam = "130"; |FINSI;

     |CLS;
     |CABEZA "Empresas sin Aplicar Minoracion y 2% prestamo";
     |PINPA #0#93;
     #96#0 = aParam;

     fFecha = ~;
     aAlfa = fFecha;
     aAlfa = aAlfa % -102;
     #96#1 = aAlfa;
     |PINDA #0#96;

     |ENDATOS #1#96;
|FPRO;
