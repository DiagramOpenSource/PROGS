|FICHEROS;
     eoszp028 #0;

     eosclpre #1;
     eosclien #2;
     eosclipr #98;

     eosaplic #96;
     :/agicli/def/agicontr #97;
|FIN;

|VARIABLES;
     PathFich98 = "";
     PathPan98  = "";

     CEmp       = 0;
     CNif       = 0;
     Registro   = 0;

     aNifQbf1   = "";
     aNifQbf2   = "";

     &eaVarDni  = "";
     &enCalcCif = 0;

     aAlfa1     = "";
     nCampo     = 0;
     aDNif      = "";
     aHNif      = "";
     nCambio    = 0;
     nCambioR   = 0;
     nCambioP   = 0;
     nSwCambio  = 0;
     nSwCambioR = 0;
     nSwCambioP = 0;
     nNume1     = 0;
     nNume2     = 0;
     nNume3     = 0;
     nDEmpresa  = 0;
     nHEmpresa  = 0;
     nDTipo     = 0;
     nHTipo     = 0;
|FIN;

|INCLUYE i_consul;

|CALCULO DeDonde; |TIPO 8;
     |ABRE #96;
     |LEE 040#96p;
     |SI FSalida ! 0; |DDEFECTO #96; |FINSI;
     |CIERRA #96;

     |DBASS PathFich98;
     |PATH_DAT #97 PathFich98;

     |SI #96#11 = 1; |ACABA; |FINSI;

     |ABRE #97;
     |LEE 040#97p;
     |SI FSalida ! 0; |CIERRA #97; |ACABA; |FINSI;
     |CIERRA #97;

     PathFich98 = #97#8; |QBF PathFich98;
     |SI PathFich98 = "*"; |ACABA; |FINSI;
     PathPan98 = (PathFich98 - "fich/") + "pan/";

     |PATH_DAT #98 PathFich98;
     |PATH_PAN #98 PathPan98;

|FCAL;

|CALCULO letrarel; |TIPO 0;
     |C_M #0Campo,0,aAlfa1;
     |SI aAlfa1 = "N"; |ACABA; |FINSI;

     |SI FSalida = 9;
         eaVarDni   = #0Campo;
         enCalcCif  = 1;
         |HAZ CalculoDNI;
         #0Campo = eaVarDni;
         |PINTA #0Campo;
         |ERROR;
         |ACABA;
     |FINSI;

     eaVarDni  = #0Campo;
     enCalcCif = 0;
     |HAZ CalculoDNI;

     aNifQbf1 = #0Campo;   |QBF aNifQbf1;
     aNifQbf2 = eaVarDni;  |QBF aNifQbf2;

     |SI aNifQbf1 ! aNifQbf2; |Y aNifQbf1 ! "";
         |CONFI "0000SEl Nif puede ser Erroneo. Desea Calcularlo.";
         |SI FSalida = 0;
             #0Campo = eaVarDni;  |PINTA #0Campo;
             |ERROR;
         |FINSI;
     |FINSI;
|FCAL;

|CALCULO Parrilla; |TIPO 0;
     |SI #0#0 = "N";
         #0#1 = "            ";  |PINTA #0#0;  |C_M #0#1, 1, "N";
         #0#2 = "zzzzzzzzzzzz";  |PINTA #0#2;  |C_M #0#2, 1, "N";
         |PARA nCampo = 3;  |SI nCampo [ 22;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |SINO;
         |C_M #0#1, 1, "S";
         |C_M #0#2, 1, "S";
         |PARA nCampo = 3;  |SI nCampo [ 22;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "N";
               #0nCampo = "";  |PINTA #0nCampo;
         |SIGUE;
     |FINSI;
|FCAL;

|CALCULO NoMasNifs;
     |C_M #0Campo,0,aAlfa1;
     |SI aAlfa1 = "N"; |ACABA; |FINSI;

     |SI #0Campo = "            ";
         |PARA nCampo = Campo + 1;  |SI nCampo [ 22;  |HACIENDO nCampo = nCampo + 1;
               #0nCampo = "            ";  |C_M #0nCampo, 1, "N";
         |SIGUE;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 22;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |FINSI;
|FCAL;

|CALCULO ParrillaE; |TIPO 0;
     |SI #0#43 = "N";
         #0#44 = 00000;  |PINTA #0#44;  |C_M #0#44, 1, "N";
         #0#45 = 99999;  |PINTA #0#45;  |C_M #0#45, 1, "N";
         |PARA nCampo = 23;  |SI nCampo [ 42;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |SINO;
         |C_M #0#44, 1, "S";
         |C_M #0#45, 1, "S";
         |PARA nCampo = 23;  |SI nCampo [ 42;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "N";
               #0nCampo = 0;  |PINTA #0nCampo;
         |SIGUE;
     |FINSI;
|FCAL;

|CALCULO NoMasEmpresas;
     |SI #0Campo = 0;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 42;  |HACIENDO nCampo = nCampo + 1;
               #0nCampo = 0;  |C_M #0nCampo, 1, "N";
         |SIGUE;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 42;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |FINSI;
|FCAL;

|CALCULO ExisteEmpresa; |TIPO 0;
     |C_M #0Campo,0,aAlfa1;
     |SI aAlfa1 = "N"; |ACABA; |FINSI;

     |SI #0Campo = 0; |ACABA; |FINSI;

     |ABRE #2;
     #2#0 = #0Campo;
     |LEE 040#2=;
     |SI FSalida ! 0;
         |MENAV "     No existe Empresa";
         |ERROR;
     |FINSI;
     |CIERRA #2;
|FCAL;

|CALCULO Cambiar; |TIPO 7;
  nCampo = Campo - 1;  aAlfa1 = "S";
  |SI #0nCampo = 0;
      #0Campo = 0; aAlfa1 = "N";
  |FINSI;
  |C_M #0Campo, 0, aAlfa1;

  |SI #0nCampo = 18; #0Campo = 21;    |FINSI;
  |SI #0nCampo = 16; #0Campo = 18;    |FINSI;
  |SI #0nCampo =  8; #0Campo = 10;    |FINSI;
  |SI #0nCampo =  7; #0Campo =  8;    |FINSI;
  |SI #0nCampo = 10; #0Campo = 12.50; |FINSI;
  |SI #0nCampo =  9; #0Campo = 10;    |FINSI;
  |PINTA #0Campo;
|FCAL;

|CALCULO elrecargo; |TIPO 0;
  aAlfa1 = #0#58;
  |C_M #0#59,1,aAlfa1;
  |C_M #0#60,1,aAlfa1;
  |C_M #0#61,1,aAlfa1;
  |C_M #0#62,1,aAlfa1;
  |SI aAlfa1 = "N";
      #0#59 = 0; |PINTA #0#59;
      #0#60 = 0; |PINTA #0#60;
      #0#61 = 0; |PINTA #0#61;
      #0#62 = 0; |PINTA #0#62;
  |FINSI;
|FCAL;

|CALCULO laretencion; |TIPO 0;
  aAlfa1 = #0#63;
  |C_M #0#64,1,aAlfa1;
  |C_M #0#65,1,aAlfa1;
  |SI aAlfa1 = "N";
      #0#64 = 0; |PINTA #0#64;
      #0#65 = 0; |PINTA #0#65;
  |FINSI;
|FCAL;

|CALCULO CambiarR; |TIPO 7;
  nCampo = Campo - 1;
  aAlfa1 = "S";
  |SI #0#58 = "N";  aAlfa1 = "N"; |FINSI;
  |SI #0nCampo = 0;
      #0Campo = 0; aAlfa1 = "N";
  |FINSI;
  |C_M #0Campo, 0, aAlfa1;

  |SI #0nCampo = 4; #0Campo = 5.20; |FINSI;
  |SI #0nCampo = 1; #0Campo = 1.40; |FINSI;
  |PINTA #0Campo;
|FCAL;

|CALCULO CambiarP; |TIPO 7;
  nCampo = Campo - 1;
  aAlfa1 = "S";
  |SI #0#63 = "N";  aAlfa1 = "N"; |FINSI;
  |SI #0nCampo = 0;
      #0Campo = 0; aAlfa1 = "N";
  |FINSI;
  |C_M #0Campo, 0, aAlfa1;

  |SI #0nCampo = 15; #0Campo = 21; |FINSI;
  |PINTA #0Campo;
|FCAL;

|| ***********************************************************************
|PROCESO CambioIVA;
  nCambio   = 0;  nSwCambio  = 0;
  nCambioR  = 0;  nSwCambioR = 0;
  nCambioP  = 0;  nSwCambioP = 0;

  |SI #0#50 ! 0; |Y nNume1 = #0#50;
       nCambio = #0#51; nSwCambio = 1;
  |FINSI;
  |SI #0#52 ! 0; |Y nNume1 = #0#52;
       nCambio = #0#53; nSwCambio = 1;
  |FINSI;
  |SI #0#54 ! 0; |Y nNume1 = #0#54;
       nCambio = #0#55; nSwCambio = 1;
  |FINSI;
  |SI #0#56 ! 0; |Y nNume1 = #0#56;
       nCambio = #0#57; nSwCambio = 1;
  |FINSI;

  |SI #0#58 = "S";
      |SI #0#59 ! 0; |Y nNume2 = #0#59;
          nCambioR = #0#60; nSwCambioR = 1;
      |FINSI;
      |SI #0#61 ! 0; |Y nNume2 = #0#61;
          nCambioR = #0#62; nSwCambioR = 1;
      |FINSI;
  |FINSI;

  |SI #0#63 = "S";
      |SI #0#64 ! 0; |Y nNume3 = #0#64;
          nCambioP = #0#65; nSwCambioP = 1;
      |FINSI;
  |FINSI;
|FPRC;

|PROCESO LeoEosClpre;
  |SI #0#0 = "N";
      nSwCambio = 0;
      |PARA CNif = 3; |SI CNif [ 22; |HACIENDO CNif = CNif + 1;
            |SI #0CNif ! "            "; |Y #0CNif = #1#3;
                nSwCambio = 1; |SAL;
            |FINSI;
      |SIGUE;
      |SI nSwCambio = 0; |ACABA; |FINSI;
  |FINSI;

  nNume1 = #1#14;
  nNume2 = #1#15;
  nNume3 = #1#22;
  |HAZ CambioIVA;
  |SI nSwCambio = 1; |O nSwCambioR = 1; |O nSwCambioP = 1;
      |SI nSwCambio  = 1; #1#14 = nCambio;  |FINSI;
      |SI nSwCambioR = 1; #1#15 = nCambioR; |FINSI;
      |SI nSwCambioP = 1; #1#22 = nCambioP; |FINSI;
      |GRABA 020#1a;
  |FINSI;
  |LIBERA #1;
|FPRC;

|DEFBUCLE LeoEosClpre;
   #1#1;
   3;
   nDEmpresa, nDTipo, #0#47, aDNif;
   nHEmpresa, nHTipo, #0#48, aHNif;
   be;
   NULL, LeoEosClpre;
|FIN;

|PROCESO LeoEosClipr;
  nNume1 = #98#11;
  nNume2 = #98#12;
  |HAZ CambioIVA;
  |SI nSwCambio = 1; |O nSwCambioR = 1;
      |SI nSwCambio  = 1; #98#11 = nCambio;  |FINSI;
      |SI nSwCambioR = 1; #98#12 = nCambioR; |FINSI;
      |GRABA 020#98a;
  |FINSI;
  |LIBERA #98;
|FPRC;

|DEFBUCLE LeoEosClipr;
   #98#1;
   ;
   aDNif;
   aHNif;
   be;
   NULL, LeoEosClipr;
|FIN;

|| ///////////////////////////////////////////////////////////////////////

|CALCULO Tipo3; |TIPO 3;

    |SI #0#49 = "S";      ||Hacer el Cambio en Ficha General
        |SI #0#0 = "S";
            aDNif = #0#1;
            aHNif = #0#2;
            |BUCLE LeoEosClipr;
        |SINO;
            |PARA CNif = 3; |SI CNif [ 22; |HACIENDO CNif = CNif + 1;
                  |SI #0CNif ! "            ";
                      aDNif = #0CNif;
                      aHNif = #0CNif;
                      |BUCLE LeoEosClipr;
                  |FINSI;
             |SIGUE;
            aDNif = "            ";
            aHNif = "zzzzzzzzzzzz";
        |FINSI;
    |FINSI;

    |SI #0#0 = "S";
        aDNif = #0#1;
        aHNif = #0#2;
    |SINO;
        aDNif = "            ";
        aHNif = "zzzzzzzzzzzz";
    |FINSI;
    nDTipo = 1; nHTipo = 2;
    |SI #0#46 = "C"; nDTipo = 1; nHTipo = 1; |FINSI;
    |SI #0#46 = "P"; nDTipo = 2; nHTipo = 2; |FINSI;

    |SI #0#43 = "S";
        nDEmpresa = #0#44;
        nHEmpresa = #0#45;
        |BUCLE LeoEosClpre;
    |SINO;
         |PARA CEmp = 23; |SI CEmp [ 42; |HACIENDO CEmp = CEmp + 1;
                |SI #0CEmp ! 0;
                     nDEmpresa = #0CEmp;
                     nHEmpresa = #0CEmp;
                     |BUCLE LeoEosClpre;
                |FINSI;
         |SIGUE;
    |FINSI;
|FCAL;
