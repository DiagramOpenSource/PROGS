|FICHEROS;
    eoszp022 #0;
    eosirpf1 #1,MANTE;        || irpf trabajadores
    eosclipr #2,MANTE;        || clientes-proveedores general
    eosclien #3;              || empresas

    eosw0059 #10,MANTE;       || temporal de introduccion
|FIN;

|VARIABLES;
    alfa1 = "";
    alfa2 = "";
    alfa3 = "";
    alfa4 = "";
    nume1 = 0;
    nume2 = 0;
    nume3 = 0;
    nume4 = 0;
    para1 = 0;
    para2 = 0;
    para3 = 0;
    para4 = 0;
    fech1 = @;
    fech2 = @;
    fech3 = @;
    fech4 = @;
    elsw1 = 0;
    elsw2 = 0;
    elsw3 = 0;
    elsw4 = 0;
    FEstado = 0;
    seleccio = 0;
    modo = 0;
    sw_rup = 0;

    x = "";
    final = "";
    letra = "";
    lugar = 0;
    resto = 0;
    nifnum = 0;

    opcion = 0;
{-1}menu = "";
    menu1 = "2400";
    menu2 = "1";
    menu3 = " Elija: ";
    menu4 = "RAC";
    menu5 = " Repasar ";
    menu6 = " Actualizar ";
    menu7 = " Cancelar ";

    sw_encuentra = 0;
    sw_error = 0;
    codigo = 0;
    ant_nif = "";
    ant_mes = 0;
    guarda_fe = "";
    puntero = 0;

     aNifQbf1   = "";
     aNifQbf2   = "";

     &eaVarDni  = "";
     &enCalcCif = 0;
|FIN;
____________________________________/
|PROCESO defecto; |TIPO 7;
    fech1 = ~;
    alfa1 = fech1;
    alfa1 = alfa1 % -102;
    #0Campo = alfa1;
    #0Campo = #0Campo - 1;
|PINTA #0Campo;
|FPRC;
____________________________________/ ACTUALIZACION DE LA INTRODUCCION
|PROCESO totales;
#1#35 = 0;
|PARA para1 = 23; |SI para1 [ 34; |HACIENDO para1 = para1 + 1;
      #1#35 = #1#35  + #1para1;
|SIGUE;

#1#48 = 0;
|PARA para1 = 36; |SI para1 [ 47; |HACIENDO para1 = para1 + 1;
      #1#48 = #1#48  + #1para1;
|SIGUE;

#1#61 = 0;
|PARA para1 = 49; |SI para1 [ 60; |HACIENDO para1 = para1 + 1;
      #1#61 = #1#61  + #1para1;
|SIGUE;

#1#74 = 0;
|PARA para1 = 62; |SI para1 [ 73; |HACIENDO para1 = para1 + 1;
      #1#74 = #1#74  + #1para1;
|SIGUE;

#1#87 = 0;
|PARA para1 = 75; |SI para1 [ 86; |HACIENDO para1 = para1 + 1;
      #1#87 = #1#87  + #1para1;
|SIGUE;

#1#100 = 0;
|PARA para1 = 88; |SI para1 [ 99; |HACIENDO para1 = para1 + 1;
      #1#100 = #1#100 + #1para1;
|SIGUE;

#1#113 = 0;
|PARA para1 = 101; |SI para1 [ 112; |HACIENDO para1 = para1 + 1;
      #1#113 = #1#113 + #1para1;
|SIGUE;

#1#126 = 0;
|PARA para1 = 114; |SI para1 [ 125; |HACIENDO para1 = para1 + 1;
      #1#126 = #1#126 + #1para1;
|SIGUE;
|FPRC;

|PROCESO del_temporal1;
    #1#0 = #10#0;        || empresa
    #1#1 = #10#4;        || trabajador
    #1#134 = #10#1;      || a¤o
|LEE 101#1=;
|SI FSalida = 0;
        #1#9 = #10#7;    || % irpf 1
        #1#10 = #10#11;  || % irpf 2
        #1#11 = #10#15;  || % irpf 3
        #1#12 = #10#5;   || clave 1
        #1#13 = #10#9;   || clave 2
        #1#14 = #10#13;  || clave 3
        puntero = 22 + #10#3; #1puntero = #1puntero + #10#6;    || base 1
        puntero = 48 + #10#3; #1puntero = #1puntero + #10#10;   || base 2
        puntero = 74 + #10#3; #1puntero = #1puntero + #10#14;   || base 3
        puntero = 35 + #10#3; #1puntero = #1puntero + #10#8;    || retencion 1
        puntero = 61 + #10#3; #1puntero = #1puntero + #10#12;   || retencion 2
        puntero = 87 + #10#3; #1puntero = #1puntero + #10#16;   || retencion 3
        puntero = 100+ #10#3; #1puntero = #1puntero + #10#17;   || base exenta
        puntero = 113+ #10#3; #1puntero = #1puntero + #10#18;   || seg.social
    |HAZ totales;
    |GRABA 020#1a;
|SINO;
        alfa1 = "    Trabajador [" + #1#1 + "] inaccesible o inexistente ...";
    |MENAV alfa1;
|FINSI;
|LIBERA #1;
|FPRC;

|DEFBUCLE del_temporal;
 #10#1;
 ;
     1,  0, "            ",  1;
 99999, 99, "zzzzzzzzzzzz", 12;
 ;
 NULL, del_temporal1;
|FIN;

|PROCESO actualiza;
|CONFI "    SConfirme la actualizacion (S/N): ";
|SI FSalida = 0;
    |BUCLE del_temporal;
|FINSI;
|FPRC;
____________________________________/ GESTION DE LA INTRODUCCION
|PROCESO min10;
    #10#0 = #0#0;
    #10#1 = #0#1;
    #10#2 = "            ";
    #10#3 = 1;
|FPRC;

|PROCESO max10;
    #10#0 = #0#0;
    #10#1 = #0#1;
    #10#2 = "zzzzzzzzzzzz";
    #10#3 = 12;
|FPRC;

|PROCESO las_lineas;
    alfa1 = Usuario;     |QBF alfa1;
|NOME_DAT #10 alfa1;
|DELINDEX #10;
|PARA; |SI; |HACIENDO;
    |ENTLINEAL #1#10, 3, 3, 22, 0, min10, max10;
    |SI #0#2 ! 0;|Y #0#3 ! 0;
        |AVISO;
        |MENU menu;
            opcion = FSalida;
        |SI opcion = 2;
            |HAZ actualiza;
        |FINSI;
        |SI opcion ! 1;
            |SAL;
        |FINSI;
    |SINO;
        |SAL;
    |FINSI;
|SIGUE;
|DELINDEX #10;
|FPRC;
____________________________________/ GRABACION DE CLIENTES-PROVEEDORES
|PROCESO graba_eosclipr1;
|DDEFECTO #2;
    #2#0 = #1#7;
|LEE 000#2=;
|SI FSalida ! 0;
        #2#1 = #1#3;          || nombre trabajador
        #2#10 = #1#4;         || localidad
        #2#7 = #1#5;          || cod. provincia
        #2#8 = #1#6;          || cod. poblacion
        #2#3 = #1#127;        || domicilio
        #2#4 = #1#128;        || numero
        #2#5 = #1#130;        || piso
        #2#6 = #1#131;        || puerta
        #2#9 = #1#132;        || provincia
    |GRABA 020#2n;
|FINSI;
|FPRC;

|DEFBUCLE graba_eosclipr;
 #1#2;
 ;
 "            ";
 "zzzzzzzzzzzz";
 ;
 NULL, graba_eosclipr1;
|FIN;

|PROCESO eosclipr;
|ATRI P;  |MENSA "    Preparando ficheros ...";   |ATRI 0;
|CIERRAT #1;  |BUCLE graba_eosclipr;  |ABRET #1;
|ATRI R;  |BLIN 4;  |ATRI 0;
|FPRC;
____________________________________/ PROCESO PADRE
|PROGRAMA;
|CLS;
|CABEZA "Entrada Rapida IRPF";
|ABRET #1;
|ABRET #2;
|PARA; |SI; |HACIENDO;
    |PDEFECTO #0, 2, 5;
        ant_nif = "";
        ant_mes = 0;
    |PINPA #0#0;
    |PINDA #0#0;
    |ENDATOS #1#0;
    |SI FSalida = 0;
        |HAZ eosclipr;
        |HAZ las_lineas;
    |SINO;
        |SAL;
    |FINSI;
|SIGUE;
|CIERRAT #1;
|CIERRAT #2;
|FPRO;
____________________________________/ DESDE EL ENTLINEAL
|PROCESO cod_ultimo;
|SELECT #4#1;
    #1#0 = #0#0;
    #1#134 = #0#1;
    #1#1 = 99999;
|LEE 000#1];
|SI FSalida = 0;
    |LEE 000#1a;
|SINO;
    |LEE 000#1u;
|FINSI;
|SI FSalida = 0;|Y #1#0 = #0#0;|Y #1#134 = #0#1;
        codigo = #1#1 + 1;
|SINO;
        codigo = 1;
|FINSI;
|SELECT #1#1;
|FPRC;

|PROCESO recoge;
    #10#7 = #1#9;  #10#11 = #1#10;  #10#15 = #1#11;    || porcentajes
    #10#5 = #1#12; #10#9  = #1#13;  #10#13 = #1#14;    || claves
    #10#4 = #1#1;                                      || codigo
    #10#19 = #1#135;                                   || tipo T/A
    #10#20 = #1#3;                                     || nombre trabajador
|FPRC;

|PROCESO buscalo1;
|SI sw_encuentra = 0;
        sw_encuentra = 1;
    |HAZ recoge;
|SINO;
        alfa1 = "    NIF [" + #10#2 + "] repetido en codigos [" + #10#4 + "] y [" + #1#1 + "] ...";
    |MENAV alfa1;
|FINSI;
|FPRC;

|DEFBUCLE buscalo;
 #1#2;
 0, 134;
 #10#2, #0#0, #0#1;
 #10#2, #0#0, #0#1;
 ;
 NULL, buscalo1;
|FIN;

|PROCESO coge_tra;
    sw_error = 0;
    sw_encuentra = 0;
|CIERRAT #1;  |BUCLE buscalo;  |ABRET #1;
|SI sw_encuentra = 0;
    |PUSHV 0501 2380;
    |PINPA #0#1;
    |HAZ cod_ultimo;
    |DDEFECTO #1;
        #1#0   = #0#0;             || empresa
        #1#1   = codigo;           || trabajador
        #1#134 = #0#1;             || a¤o
        #1#2   = #3#2;             || nombre empresa
        #1#3   = #2#1;             || nombre trabajador
        #1#4   = #2#10;            || localidad
        #1#5   = #2#7;             || cod. provincia
        #1#6   = #2#8;             || cod. poblacion
        #1#7   = #2#0;             || NIF
        #1#127 = #2#2 + #2#3;      || domicilio
        #1#128 = #2#4;             || numero
        #1#130 = #2#5;             || piso
        #1#131 = #2#6;             || puerta
        #1#132 = #2#9;             || provincia
    |GRABA 020#1b;
    |PINDA #0#1;
    |ENDATOS #1#1;
    |SI FSalida = 0;
        |GRABA 020#1a;
        |HAZ recoge;
    |SINO;
            sw_error = 1;
        |BORRA 020#1a;
    |FINSI;
    |LIBERA #1;
    |POPV;
|FINSI;
|PINTA #10#7;  |PINTA #10#11; |PINTA #10#15;
|PINTA #10#5;  |PINTA #10#9;  |PINTA #10#13;
|PINTA #10#4;  |PINTA #10#19; |PINTA #10#20;
|SI sw_error ! 0;
    |ERROR;
|FINSI;
|FPRC;

|PROCESO mira_tra; |TIPO 7;
    modo = (FEntrada / 100) ! 0;
|SI modo = 1;
        #2#0 = #10#2;
    |LEE 000#2=;
    |SI FSalida = 0;
        |HAZ coge_tra;
            ant_nif = #10#2;
    |SINO;
            alfa1 = "    NIF [" + #10#2 + "] inaccesible o inexistente ...";
        |MENAV alfa1;
        |ERROR;
    |FINSI;
|FINSI;
|FPRC;

|PROCESO tipo11; |TIPO 11;
|SI FSalida = 9;
        guarda_fe = FEntrada;      || en el 'CONSULTA' se pierde
    |PUSHV 0501 2380;
    |PINPA #0#1;
    |PINDA #0#1;
        #1#7 = #10#2;
    |CONSULTA #1#1;
    |SI FSalida = 0;
            #10#2 = #1#7;
    |FINSI;
    |POPV;
    |PINTA #10#2;
    |ERROR;
        FEntrada = guarda_fe;
|FINSI;
|SI FSalida = 10;
        guarda_fe = FEntrada;      || en el 'ENDATOS' se pierde
    |PUSHV 0501 2380;
    |PINPA #0#1;
        #1#0 = #0#0;
        #1#1 = #10#4;
        #1#134 = #0#1;
    |LEE 101#1=;
    |SI FSalida = 0;
        |PINDA #0#1;
        |ENDATOS #1#1;
        |SI FSalida = 0;
            |GRABA 020#1a;
        |FINSI;
    |SINO;
            alfa1 = "    Trabajador [" + #1#1 + "] inaccesible o inexistente ...";
        |MENAV alfa1;
    |FINSI;
    |LIBERA #1;
    |POPV;
    |ERROR;
        FEntrada = guarda_fe;
|FINSI;
|FPRC;

|PROCESO letnif; |TIPO 6;
     eaVarDni  = #0Campo;
     enCalcCif = 0;
     |HAZ CalculoDNI;

     aNifQbf1 = #0Campo;   |QBF aNifQbf1;
     aNifQbf2 = eaVarDni;  |QBF aNifQbf2;

     |SI aNifQbf1 ! aNifQbf2; |Y aNifQbf1 ! "";
         |CONFI "0000SEl Nif puede ser Erroneo. Desea Calcularlo.";
         |SI FSalida = 0;
             #0Campo = eaVarDni;  |PINTA #0Campo;
             |ERROR;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO cuotas; |TIPO 0;
    #10#8  = #10#6  % #10#7;
    #10#12 = #10#10 % #10#11;
    #10#16 = #10#14 % #10#15;
|PINTA #10#8;
|PINTA #10#12;
|PINTA #10#16;
|FPRC;

|PROCESO arrastra; |TIPO 7;
    modo = (FEntrada / 100) ! 0;
|SI modo = 1;
    |SI ant_nif ! "";
            #10#2 = ant_nif;
        |PINTA #10#2;
    |FINSI;
|FINSI;
|FPRC;

|PROCESO tipo2; |TIPO 2;
    nume1 = #10#6 + #10#10 + #10#14 + #10#17;     #0#2 = #0#2 +. nume1;
    nume1 = #10#8 + #10#12 + #10#16;              #0#3 = #0#3 +. nume1;
                                                  #0#4 = #0#4 +. #10#18;
|PINTA #0#2;
|PINTA #0#3;
|PINTA #0#4;
|FPRC;

|PROCESO mes7; |TIPO 7;
|SI modo = 1;|Y ant_mes ! 0;
    |SI ant_mes ! 12;
            #10#3 = ant_mes + 1;
    |SINO;
            #10#3 = 1;
    |FINSI;
    |PINTA #10#3;
|FINSI;
|FPRC;

|PROCESO mes0; |TIPO 0;
|SI modo = 1;
        ant_mes = #10#3;
|FINSI;
|FPRC;

|PROCESO noclave; |TIPO 0;
|SI #10Campo = " ";
        alfa1 = "N";
        nume1 = Campo + 1;    #10nume1 = 0;  |PINTA #10nume1;
        nume1 = Campo + 2;
        nume1 = Campo + 3;    #10nume1 = 0;  |PINTA #10nume1;
|SINO;
        alfa1 = "S";
|FINSI;
    nume1 = Campo + 1;   |C_M #10nume1, 1, alfa1;
    nume1 = Campo + 2;   |C_M #10nume1, 1, alfa1;
    nume1 = Campo + 3;   |C_M #10nume1, 1, alfa1;
|FPRC;
