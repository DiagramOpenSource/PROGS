|FICHEROS;
     reoz0003 #0;
     eosm0023 #1;
     eosm0024 #2;
     eosclien #3;
     eoscabco #4;
     eoslinco #5;
     eosclpre #6;
     eosconce #7;
     eosconc6 #8;
     eosactiv #9;
     eosw0045 #99,MANTE;
|FIN;

|VARIABLES;
     SwHay     = 0;
     SwTempo   = 0;
     nCampos   = 0;
     nAnyo     = 0;
     Tecla     = "";
     Concepto2 = 0;
     Concepto6 = "";
     ConceptoN = "";
     Fichero   = "";
     VAnyo     = "";
     Linea     = 0;
     Trime     = 0;

     fFecha    = @;
     aAlfa1    = "";
     aAlfa     = "";
     aDescri   = "";
     aEsCompra = "";
|FIN;

|INCLUYE i_consul;

 \
  \ *********************** PROCESOS CAMPOS DE LA PANTALLA

|PROCESO Mayusculas;
     #0Campo = #0Campo > #0Campo;
     |PINTA #0Campo;
|FPRC;

|PROCESO Seleccion;
     |SI #0#0 = "S";
         |C_M #0#1, 1, "N";   #0#1 = 1;      |PINTA #0#1;
         |C_M #0#2, 1, "N";   #0#2 = 99999;  |PINTA #0#2;
         |PARA nCampos = 3; |SI nCampos [ 26;  |HACIENDO nCampos = nCampos + 1;
               |C_M #0nCampos, 1, "S";
         |SIGUE;
     |SINO;
         |C_M #0#1, 1, "S";
         |C_M #0#2, 1, "S";
         |PARA nCampos = 3; |SI nCampos [ 26;  |HACIENDO nCampos = nCampos + 1;
               |C_M #0nCampos, 1, "N";  #0nCampos = 0;  |PINTA #0nCampos;
         |SIGUE;
     |FINSI;
|FPRC;

 \
  \ *********************** PROCESOS DE GRABACION DEL TEMPORAL

|PROCESO MiraSiHay;
     SwHay = 1;
|FPRC;

|DEFBUCLE eosm0024P;
     #2#1;
     6;
     #0#1, 1, #0#28, #0#27, "N";
     #0#2, 6, #0#29, #0#27, "N";
     ;
     NULL, MiraSiHay;
|FIN;

|PROCESO GrabaTempo;
     SwHay = 0;
     |BUCLE eosm0024P;

     |SI SwHay = 0;  |Y #1#24 ! "N";  |ACABA;  |FINSI;

     #3#0 = #1#0;
     |LEE 040#3=;
     |SI FSalida ! 0;  |DDEFECTO #3;  |FINSI;

     aAlfa = ("0000" + #0#27) % -102;

     #99#0 = #1#0;               || Empresa
     #99#1 = aAlfa;              || A¤o
     #99#2 = #1#2;               || Elemento
     #99#3 = #1#3;               || Nombre Elemento
     #99#4 = 0;
     #99#5 = #3#2;
     #99#6 = "";
     #99#7 = "S";
     |GRABA 140#99n;
     |LIBERA #99;

     SwTempo = 1;
|FPRC;

|DEFBUCLE eosm0023P;
     #1#1;
     ;
     #0#1, 1, #0#28;
     #0#2, 6, #0#29;
     ;
     NULL, GrabaTempo;
|FIN;

 \
  \ *********************** PROCESOS DE GRABACION DEL LIBRO

|PROCESO Concepto;
     |SI Campo = 30;
         |SI #0Campo ! 0;
             |ABRE #7;
             #7#0 = #0Campo;
             |LEE 040#7=;
             |SI FSalida ! 0;
                 |MENAV "      No Exite el Concepto.";
                 |ERROR;
             |FINSI;
             |CIERRA #7;
         |FINSI;
     |FINSI;

     |SI Campo = 31;
         |SI #0Campo ! "      ";
             |ABRE #8;
             #8#0 = #0Campo;
             |LEE 040#8=;
             |SI FSalida ! 0;
                 |MENAV "      No Exite el Concepto.";
                 |ERROR;
             |FINSI;
             |CIERRA #8;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO LeeConce;
     |ABRE #9;
     #9#0 = #99#0;
     #9#1 = #1#1;
     #9#2 = #99#1;
     |LEE 040#9=;
     |SI FSalida ! 0; |DDEFECTO #9; |FINSI;
     |CIERRA #9;

     |SI #9#50 = 1;
         |ABRE #7;
         #7#0 = Concepto2;
         |LEE 040#7=;
         |SI FSalida ! 0; |DDEFECTO #7; |FINSI;
         |CIERRA #7;
         ConceptoN = #7#1;
     |SINO;
         |ABRE #8;
         #8#0 = Concepto6;
         |LEE 040#8=;
         |SI FSalida ! 0; |DDEFECTO #8; |FINSI;
         |CIERRA #8;
         ConceptoN = #8#1;
     |FINSI;
|FPRC;

|PROCESO Trimestre;
     aAlfa1 = fFecha % 402;
     |SI aAlfa1 = "01"; |O aAlfa1 = "02"; |O aAlfa1 = "03";  Trime = 1;  |FINSI;
     |SI aAlfa1 = "04"; |O aAlfa1 = "05"; |O aAlfa1 = "06";  Trime = 2;  |FINSI;
     |SI aAlfa1 = "07"; |O aAlfa1 = "08"; |O aAlfa1 = "09";  Trime = 3;  |FINSI;
     |SI aAlfa1 = "10"; |O aAlfa1 = "11"; |O aAlfa1 = "12";  Trime = 4;  |FINSI;
|FPRC;

|PROCESO LeeProveedor;
     |ABRE #6;
     #6#0 = #5#0;
     #6#2 = 2;
     #6#1 = #5#3;
     |LEE 040#6=;
     |SI FSalida ! 0; |DDEFECTO #6; |FINSI;
     |CIERRA #6;
|FPRC;

|PROCESO CreaFichero;
     Fichero = #4#0;
     Fichero = ("00000" +  Fichero) % -105;
     VAnyo   = #4#4;
     VAnyo   = ("00" + VAnyo) % -102;
     Fichero = "c" + Fichero + VAnyo;
     |NOME_DAT #5Fichero;

     Linea = 0;
     |ABRE #5;
     #5#0  = #4#0;
     #5#1  = 32000;
     #5#14 = #4#4;
     |LEE 000#5];
     |SI FSalida = 0;
         |LEE 000#5a;
     |SINO;
         |LEE 000#5u;
     |FINSI;

     |SI FSalida = 0;|Y #5#0 = #4#0; |Y #5#14 = #4#4;
         Linea = #5#1 + 1;
     |SINO;
         Linea = 1;
     |FINSI;
     |CIERRA #5;
|FPRC;

|PROCESO GrabaProveedor;
     |ABRE #6;
     #6#0 = #5#0;
     #6#2 = 2;
     #6#1 = #5#3;
     |LIBERA #6;
     |LEE 110#6=;
     |SI FSalida = 0;
         |SI #5#9 ! 0;
             #6#17 = #6#17 + (#5#7 + #5#9 + #5#11 - #5#42);
         |SINO;
             #6#16 = #6#16 + #5#7;
         |FINSI;
         |GRABA 020#6a;
     |FINSI;
     |LIBERA #6;
     |CIERRA #6;
|FPRC;

|PROCESO GrabaLinea;
     |HAZ CreaFichero;

     |DDEFECTO #5;
     |ABRE #5;
     #5#0 = #99#0;
     #5#1 = Linea;
     #5#2 = 2;
     |SI aEsCompra = "N";
         Concepto2 = #0#30;
         Concepto6 = #0#31;
         |HAZ LeeConce;

         #5#3    = #99#4;
         #5#5    = #0#30;
         #5#72   = #0#31;
         #5#7    = #2#5;
         #5#8    = 0;
         #5#9    = 0;
         aAlfa   = "3112";
         aDescri = #2#4;
         aAlfa1  = "31.12." + #2#3;  fFecha  = aAlfa1;
     |SINO;
         Concepto2 = #1#10;
         Concepto6 = #1#11;
         |HAZ LeeConce;

         #5#3  = #1#9;
         #5#5  = #1#10;
         #5#72 = #1#11;
         #5#7  = #1#14;
         #5#8  = #1#15;
         #5#9  = #1#14 % #1#15;

         aAlfa   = (#1#12 % 102) + (#1#12 % 402);
         aDescri = "COMPRA " + #1#3;
         fFecha  = #1#12;
     |FINSI;

     #5#35  = ConceptoN;
     #5#20  = "N";
     #5#21  = 1;
     #5#22  = #5#7 + #5#9;
     #5#4   = aAlfa;
     #5#6   = aDescri;
     #5#10  = 0;
     #5#11  = 0;
     #5#13  = fFecha;
     #5#14  = #99#1;
     #5#15  = "";
     #5#16  = 0;

     |HAZ Trimestre;

     #5#17  = Trime;
     #5#18  = "N";
     #5#19  = #1#1;

     |HAZ LeeProveedor;

     #5#12  = #6#3;
     #5#38  = #6#4;

     |GRABA 041#5n;
     |CIERRA #5;
     |HAZ GrabaProveedor;
|FPRC;

|PROCESO GrabaCabecera;
     #4#0 = #99#0;
     #4#4 = #99#1;
     |LIBERA #4;
     |LEE 110#4=;
     |SI FSalida = 0;
         |SI aEsCompra = "S";
             #4#5 = #4#5 + #1#14;
             #4#6 = #4#6 + (#1#14 % #1#15);
         |SINO;
             #4#5 = #4#5 + #2#5;
         |FINSI;
         #4#8 = #4#5 + #4#6 + #4#7 - #4#9;
         |GRABA 020#4a;
         |LIBERA #4;
         |HAZ GrabaLinea;
     |SINO;
         |DDEFECTO #4;
         #4#0 = #99#0;
         #4#1 = #3#2;
         #4#2 = #3#1;
         #4#3 = 2;
         #4#4 = #99#1;
         |SI aEsCompra = "S";
             #4#5 = #4#5 + #1#14;
             #4#6 = #4#6 + (#1#14 % #1#15);
             #4#8 = #4#5 + #4#6 + #4#7 - #4#9;
         |SINO;
             #4#5 = #4#5 + #2#5;
             #4#8 = #4#5 + #4#6 + #4#7 - #4#9;
         |FINSI;
         |GRABA 020#4n;
         |LIBERA #4;
         |HAZ GrabaLinea;
     |FINSI;
|FPRC;

|PROCESO PasaAmort;
     |SI #2#5 = 0;  |ACABA;  |FINSI;

     |HAZ GrabaCabecera;

     #2#6 = "S";
     |GRABA 120#2a;
     |LIBERA #2;
|FPRC;

|DEFBUCLE eosm0024S;
     #2#1;
     6;
     #1#0, #1#1, #1#2, #0#27, "N";
     #1#0, #1#1, #1#2, #0#27, "N";
     ;
     NULL, PasaAmort;
|FIN;

|PROCESO PasaCompra;
     aEsCompra = "S";   |HAZ GrabaCabecera;

     #1#6 = "S";
     |GRABA 120#1a;
     |LIBERA #1;

     aEsCompra = "N";
     |BUCLE eosm0024S;
|FPRC;

|PROCESO El00023;
     aAlfa = #1#12 % -104;
     nAnyo = aAlfa;
     |SI #1#24 = "N";  |Y nAnyo = #0#27;
         |HAZ PasaCompra;

         #1#24 = "S";
         |GRABA 120#1a;
         |LIBERA #1;
         |ACABA;
     |FINSI;

     aEsCompra = "N";
     |BUCLE eosm0024S;
|FPRC;

|DEFBUCLE eosm0023S;
     #1#1;
     ;
     #99#0, 1, #99#2;
     #99#0, 6, #99#2;
     be;
     NULL, El00023;
|FIN;

|PROCESO PreparaPase;
     |ABRE #4;
     |BUCLE eosm0023S;
     |CIERRA #4;
|FPRC;

|DEFBUCLE eosw0045;
     #99#1;
     7;
     00000, 00, 00000, "S";
     99999, 99, 99999, "S";
     ;
     NULL, PreparaPase;
|FIN;


|RUTINA ClaveBaja;
     #99#0 = 00001;
     #99#1 = 00;
     #99#2 = 0;
|FRUT;

|RUTINA ClaveAlta;
     #99#0 = 99999;
     #99#1 = 99;
     #99#2 = 99999;
|FRUT;

|PROCESO Tipo3; |TIPO 3;
     Fichero = Usuario % 808;  |QBT Fichero;
     Fichero = ("L8" + Fichero + "zzzzzzzz") % 108;
     |NOME_DAT #99 Fichero;
     |ABRE #99;  |CIERRA #99;  |DELINDEX #99;

     SwTempo = 0;

     |ABRE #99;
     |ABRE #3;
     |SI #0#0 = "S";
         |PARA nCampos = 3; |SI nCampos [ 26;  |HACIENDO nCampos = nCampos + 1;
               |SI #0nCampos ! 0;
                   #0#1 = #0nCampos;
                   #0#2 = #0nCampos;
                   |BUCLE eosm0023P;
                |FINSI;
         |SIGUE;
     |SINO;
         |BUCLE eosm0023P;
     |FINSI;
     |CIERRA #99;
     |CIERRA #3;

     |SI SwTempo = 0;
         |ABRE #99;  |CIERRA #99;  |DELINDEX #99;
         |ACABA;
     |FINSI;

     |ENTLINEAL #1#99, 3, 4, 20, 1, ClaveBaja, ClaveAlta;

     |ABRE #3;
     |BUCLE eosw0045;
     |CIERRA #3;
     |DELINDEX #99;
|FPRC;

 \
  \ *********************** PROCESOS DEL MISMO TEMPORAL

|PROCESO Tipo11;  |TIPO 11;
     |SI FSalida ! 9;  |Y FSalida ! 10;  |ACABA;  |FINSI;

     Tecla = FSalida;

     |LIBERA #99;
     |LEE 140#99=;

     |SI Tecla = 9;
           |SI #99#7 = "N";
               #99#7 = "S";
           |SINO;
               #99#7 = "N";
               #99#4 = 0;
           |FINSI;
     |FINSI;

     |SI Tecla = 10;
         #99#7 = "S";
         |C_M #99#4, 1, "S";
         |ENCAMPO #4#99;
     |FINSI;

     |PINTA #99#4;
     |PINTA #99#7;

     |GRABA 140#99a;

     |C_M #99#4, 1, "N";
|FPRC;

|PROCESO MiraProve;  |TIPO 0;
     |SI FSalida ! 9;  |ACABA;  |FINSI;
     Campo10020 = #99#0;
     Campo10022 = 2;
     |HAZ ConsuCliProv;
     #99#4 = Campo10021; |PINTA #99#4;
|FPRC;

|PROCESO PonProve;  |TIPO 0;
     #99#6 = #6#4;   |PINTA #99#6;
     |SI #99#4 = 0;  |ACABA;  |FINSI;

     |ABRE #6;
     #6#0 = #99#0;
     #6#2 = 2;
     #6#1 = #99#4;
     |LEE 040#6=;
     |SI FSalida ! 0;
         #99#6 = #6#4;   |PINTA #99#6;
         |CIERRA #6;
         |MENAV "     No Existe Proveedor";
         |ERROR;
         |ACABA;
     |FINSI;
     |CIERRA #6;

     #99#6 = #6#4;   |PINTA #99#6;
|FPRC;
