|FICHEROS;
     eosm0017 #0;             || Fichero Modulos 1
     eosm0018 #1;             || Fichero Modulos 2
     eoszp019 #2;             || Fichero Carga de Datos
     eosw0058 #3;             || Fichero Carga de Datos
     eosw0060 #4;             || Fichero Carga de Datos
     eosw0070 #8;             || Fichero Carga de Datos
     eosactiv #5;             || Fichero Actividades
     eosm0019 #19;            || Mantenimiento 310
     eosm0020 #20;            || Mantenimiento 131
     eosm0021 #21;            || Desglose de Trabajadores
     eosm0022 #22;            || Desglose de Elementos

     eosclien;
|FIN;

|VARIABLES;
      aAlfa     = "";
      nNume     = 0;
      Modo      = 0;
      Salir     = 0;
      Opcion    = 0;
      XPara     = 0;
      XPara1    = 0;
      XPara2    = 0;
      Periodo   = 0;
      Campo1    = 0;
      Campo2    = 0;
      Campo3    = 0;
      Campo4    = 0;
      Campo5    = 0;
      Campo6    = 0;
      Campo7    = 0;
      SwBorrar  = 0;
      SwPaso    = 0;
      nValor    = 0;

      nEmple1   = 0;
      nEmple2   = 0;

     &eaComunidad = "";
     &Programa  = 1;
     &MTipo    = 0;
     &MEmpresa = 0;
     &MActivi  = 0;
     &MAnyo    = 0;
     &MTempor  = 0;
     &eaPrestige = "";
     &enCalcProv = 0;

     &enEmpresa   = 0;
     &enEjer      = 0;
     &enActividad = 0;
     &enPeriodo   = 0;
     &enComplem   = 0;
     &enModelo    = 0;

{-1} MGeneral   = "";
     MGeneral1  = "1431";
     MGeneral2  = "1";
     MGeneral3  = "4";
     MGeneral4  = "Introduccion           ";
     MGeneral5  = "Impresion              ";
     MGeneral6  = "Consulta Modelo 310/311";
     MGeneral7  = "Consulta Modelo 131    ";

     &enError15 = 0;
{220} &PunteroA  = "";
{210} &PunteroB  = "";
|FIN;


|PROCESO CuadroAP7; |TIPO 7;
 |PUSHV 1533 1980;
 |D_CUADRO 1533,1980;
 |ATRI R;
 |PINTA 1634, " 1-Importe Maximo Permitido                   ";
 |PINTA 1734, " 2-Imp. Maximo sin superar Cantidad Pendiente ";
 |PINTA 1834, " 3-Imp.a partir de la Amortizacion Definitiva ";
 |ATRI 0;
|FPRC;

|PROCESO CuadroAP0; |TIPO 0;
 |POPV;
|FPRC;

|PROCESO Mayus;
  #0Campo = #0Campo > #0Campo;  |PINTA #0Campo;

  |SI Campo = 201;  |Y #0Campo = "S";
      |PUSHV 0501 2380;
      |D_CUADRO 1138, 1571;
      |PINTA 1239, "Inicio de Actividad : ";
      |PINTA 1339, "Fecha Inicio .......: ";
      |PINTA 1439, "Amortizacion Provis.: ";
      |C_V #0#203, 1, "S";  |C_M #0#203, 1, "S";  |C_P #0#203, 1, 1261;  |PINTA #0#203;
      |C_V #0#204, 1, "S";  |C_M #0#204, 1, "S";  |C_P #0#204, 1, 1361;  |PINTA #0#204;
      |C_V #0#206, 1, "S";  |C_M #0#206, 1, "S";  |C_P #0#206, 1, 1461;  |PINTA #0#206;

      |ENCAMPO #203#0;
      |SI #0#203 = "S";
        |ET PideFecha;
          |ENCAMPO #204#0;
          aAlfa = #0#204 % -102;
          nNume = aAlfa;
          |SI #0#5 ! nNume;
              |MENAV "      Fecha Inicio Incorrecta.";
              |VETE PideFecha;
          |FINSI;
      |SINO;
          #0#204 = "01.01.0000";
      |FINSI;

      |ENCAMPO #206#0;

      |POPV;
  |FINSI;
|FPRC;

|PROCESO LeeAdjunto;
  Modo = (FEntrada / 100) ! 0;

  nEmple1 = 0;
  nEmple2 = 0;

  |ABRE #1;
  |SI Modo = 1;
      |SI #0#5 [ 80; |O #0#5 = 99;
          #1#0 = #0#0;
          #1#1 = #0#1;
          #1#2 = #0#5 - 1;
          |SI #1#2 < 0;  #1#2 = 99;  |FINSI;
          |LEE 040#1=;
          |SI FSalida = 0;
              nEmple1 = #1#6;
              nEmple2 = #1#85;
          |FINSI;

           |SI #0#5 = 4;  |O #0#5 = 8;  |O #0#5 = 12;  |O #0#5 = 16;
            |O #0#5 = 20; |O #0#5 = 24; |O #0#5 = 28;  |O #0#5 = 32;
               #0#128 = 366;
               #0#129 = 91;
               #0#133 = 366;
               #0#134 = 91;
           |FINSI;
      |FINSI;
  |SINO;
      |SI #0#5 = 16;
          |SI #0#128 = 365; |Y #0#129 = 90;
              #0#128 = 366; #0#129 = 91;
          |FINSI;
          |SI #0#133 = 365; |Y #0#134 = 90;
              #0#133 = 366; #0#134 = 91;
          |FINSI;
      |FINSI;
  |FINSI;

  #1#0 = #0#0;
  #1#1 = #0#1;
  #1#2 = #0#5;
  |LEE 101#1=;
  |SI FSalida ! 0;
      |DDEFECTO #1;
      #1#0 = #0#0;
      #1#1 = #0#1;
      #1#2 = #0#5;
      |GRABA 020#1b;
  |FINSI;

  |SI nEmple1 ! 0;  #1#4  = nEmple1;  |FINSI;
  |SI nEmple2 ! 0;  #1#83 = nEmple2;  |FINSI;
  |GRABA 020#1a;
  |LIBERA #1;
  |CIERRA #1;
|FPRC;

|PROCESO Tipo13; |TIPO 13;
  Modo = (FEntrada / 100) ! 0;
  |SI Modo ! 4; |ACABA; |FINSI;

  |HAZ CogeModelo;
  |HAZ MGeneral;
|FPRC;

|PROCESO Tipo3;  |TIPO 3;
  |HAZ CogeModelo;
  |HAZ MGeneral;
|FPRC;

|PROCESO Opcion1;
  |HAZ LeeAdjunto;

  |PARA XPara1 = 2;  |SI XPara1 [ 218;  |HACIENDO XPara1 = XPara1 + 1;
        IPunteroA = PunteroA1 <-;
        IPunteroA = IPunteroA + XPara1;
        PunteroA  = #0XPara1;
  |SIGUE;
                                  || 151
  |PARA XPara1 = 3;  |SI XPara1 [ 158;  |HACIENDO XPara1 = XPara1 + 1;
        IPunteroB = PunteroB1 <-;      nNume = IPunteroB;
        IPunteroB = IPunteroB + XPara1;
        PunteroB  = #1XPara1;
  |SIGUE;

  |SI Modo = 4;
      |PUSHV 0501 2380;
      |PARA XPara1 = 0;  |SI XPara1 [ 202;  |HACIENDO XPara1 = XPara1 + 1;
            XPara2 = XPara1 + 2;
            IPunteroA = PunteroA1 <-;
            IPunteroA = IPunteroA + XPara2;
            #2XPara1 = PunteroA;
      |SIGUE;

      |SI #2#3 ] 0;  |Y #2#3 < 80;
          #2#3 = 2000 + #2#3;
      |SINO;
          #2#3 = 1900 + #2#3;
      |FINSI;

      #2#203 = #0#205;

      |PINPA #0#2;
      |PINDA #0#2;
      |PAUSA;
      |POPV;
      |ACABA;
  |FINSI;

  MEmpresa   = #0#0;
  MActivi    = #0#1;
  MAnyo      = #0#5;
  eaPrestige = #0#205;
  enCalcProv = #0#206;
  eaComunidad = #5#46;

  |SUB_EJECUTA "eoszp019";

  |ABRE #1;
  #1#0 = #0#0;
  #1#1 = #0#1;
  #1#2 = #0#5;
  |LEE 101#1=;

  |PARA XPara1 = 2;  |SI XPara1 [ 218;  |HACIENDO XPara1 = XPara1 + 1;
        IPunteroA = PunteroA1 <-;
        IPunteroA = IPunteroA + XPara1;
        |SI XPara1 ! 5;
            #0XPara1  = PunteroA;
        |SINO;
            nValor = PunteroA;
            |SI nValor > 1900;
                 |SI nValor ] 2000;
                     #0XPara1 = nValor - 2000;
                 |SINO;
                     #0XPara1 = nValor - 1900;
                 |FINSI;
            |SINO;
                 #0XPara1 = nValor;
            |FINSI;
        |FINSI;
  |SIGUE;

  #0#205 = eaPrestige;

  |PARA XPara1 = 3;  |SI XPara1 [ 158;  |HACIENDO XPara1 = XPara1 + 1;
        IPunteroB = PunteroB1 <-;
        IPunteroB = IPunteroB + XPara1;
        #1XPara1  = PunteroB;
  |SIGUE;

  |GRABA 020#1a;
  |LIBERA #1;
  |CIERRA #1;

  #0#8  = "";
  #0#9  = "";
  #0#10 = "";

  MTipo     = 1;
  MEmpresa  = #0#0;
  MActivi   = #0#1;
  MAnyo     = #0#5;
  MTempor   = #1#63;
  |GRABA 140#0a;

  |CIERRAT #0;

  |SI #0#5 ] 14; |Y PARAMETRO = "";
      enEmpresa = #0#0;
      enEjer    = #0#5 + 2000;
      enPeriodo = 3;
      enModelo    = 303;
      enComplem   = 0;
      enActividad = #0#1;
  |FINSI;
  |SUB_EJECUTA "eoszp023";

  |ABRET #0;

  #0#0  = MEmpresa;
  #0#1  = MActivi;
  #0#5  = MAnyo;
  |LIBERA #0;
  |LEE 140#0=;
|FPRC;

|PROCESO Opcion2;
  |PARA XPara1 = 0;  |SI XPara1 [ 202;  |HACIENDO XPara1 = XPara1 + 1;
        XPara2   = XPara1 + 2;
        #2XPara1 = #0XPara2;
  |SIGUE;

  |SI #2#3 ] 0;  |Y #2#3 < 80;
      #2#3 = 2000 + #2#3;
  |SINO;
      #2#3 = 1900 + #2#3;
  |FINSI;

  eaPrestige = #0#205;

  |PARA XPara1   = 2;  |SI XPara1 [ 95;  |HACIENDO XPara1 = XPara1 + 1;
        XPara2   = XPara1 + 1;
        #3XPara1 = #1XPara2;
  |SIGUE;

   #3#108 = #1#125;
   #3#109 = #1#126;
   #3#110 = #1#127;
   #3#111 = #1#128;
   #3#113 = #1#129;
   #3#114 = #1#130;
   #3#116 = #1#131;
   #3#117 = #1#132;
   #3#118 = #1#133;
   #3#120 = #1#134;
   #3#121 = #1#135;
   #3#123 = #1#136;
   #3#124 = #1#137;
   #3#126 = #1#138;
   #3#127 = #1#139;
   #3#128 = #1#140;

  |PARA XPara1   = 1;  |SI XPara1 [ 16;  |HACIENDO XPara1 = XPara1 + 1;
        XPara2   = XPara1 + 98;
        #4XPara1 = #1XPara2;
  |SIGUE;

  #8#9  = #1#116;
  #8#11 = #1#118;
  #8#13 = #1#120;
  #8#15 = #1#122;
  #8#17 = #1#124;
  #8#19 = #1#141;
  #8#20 = #1#142;
  #8#21 = #1#143;

  #8#0   = #2#111;                      || Cuota Devengada operaciones
  #8#1   = #8#0 % 1;                    || 1 % Cuota Devengada
  #8#2   = #1#115;
  #8#3   = #8#1 + #8#2;                 || Total Cuota Soportada
  #8#4   = #3#65;                       || Coeficiente Temporada
  |SI #8#4 = 0;  #8#4 = 1;  |FINSI;
  #8#5   = (#8#0 - #8#3) * #8#4;        || Resultado
  #8#6   = #2#121;                      || Cuota minima
  #8#7   = #8#0 % #8#6;                 || Total Cuota Minima
  #8#7   = #8#7 * #8#4;                 || Total Cuota Minima
  #8#8   = #8#5;                       || Cuota derivada regiman general
  |SI #8#8 < #8#7;  #8#8 = #8#7;  |FINSI;

  |IMPRE 0;
  |SI FSalida ! 0;  |ACABA;  |FINSI;

  |SI #2#3 [ 2008;
      |INFOR "eoszp019";
  |SINO;
      |INFOR "eoszp119";
  |FINSI;

  |SI FSalida ! 0;  |FINIMP;  |ACABA;  |FINSI;

  |PRINT;
  |PIEINF;
  |FININF;
  |FINIMP;
|FPRC;

|PROCESO Opcion3;
  |PUSHV 0501 2380;

  |ABRET #19;
  |PINPA #0#19;
  |PINDA #0#19;
  |DDEFECTO #19;
  #19#0 = #0#0;
  #19#1 = #0#5;
  #19#2 = 1;
  |LEE 040#19];
  |SI FSalida ! 0;  |O #19#0 ! #0#0;  |O #19#1 ! #0#5;
      |MENAV "       No hay impresos del 310 creados.";
      |CIERRAT #19;
      |POPV;
      |ACABA;
  |SINO;
      |PINDA #0#19;
  |FINSI;

  |C_M #19#0, 1, "N";
  |C_M #19#1, 1, "N";
  |CONSULTA #1#19;

  |POPV;
  |CIERRAT #19;
|FPRC;

|PROCESO LaPantalla;
     |SI #20#1 < 15;
         aAlfa = "³ Minoracion (Art.80) ....:";
     |SINO;
         aAlfa = "³ Minoracion (Art.110.3.c):";
     |FINSI;
     |PINTA 1642, aAlfa;
|FPRC;

|PROCESO Opcion4;
  |PUSHV 0501 2380;

  |ABRET #20;
  |PINPA #0#20;
  |PINDA #0#20;
  |HAZ LaPantalla;
  |DDEFECTO #20;
  #20#0 = #0#0;
  #20#1 = #0#5;
  #20#2 = 1;
  |LEE 040#20];
  |SI FSalida ! 0;  |O #20#0 ! #0#0;  |O #20#1 ! #0#5;
      |MENAV "       No hay impresos del 131 creados.";
      |CIERRAT #20;
      |POPV;
      |ACABA;
  |SINO;
      |PINDA #0#20;
  |FINSI;

  |C_M #20#0, 1, "N";
  |C_M #20#1, 1, "N";
  |CONSULTA #1#20;

  |POPV;
  |CIERRAT #20;
|FPRC;

|PROCESO MGeneral;
  |HAZ LeeAdjunto;

  |SI PARAMETRO ! "";
      |HAZ Opcion1;
      |ACABA;
  |FINSI;

  Salir = 0;
  |PARA; |SI Salir = 0; |HACIENDO;
         |PINTA #0#0;    |PINTA #0#1;    |PINTA #0#2;    |PINTA #0#3;
         |PINTA #0#4;    |PINTA #0#5;    |PINTA #0#6;    |PINTA #0#7;

         |MENUG MGeneral;
         MGeneral2 = FSalida;
         Opcion    = FSalida;
         Salir     = 1;
         |SI Opcion ] 1; |Y Opcion [ 4;
             |SI Opcion = 1;  |HAZ Opcion1;  |FINSI;
             |SI Opcion = 2;  |HAZ Opcion2;  |FINSI;
             |SI Opcion = 3;  |HAZ Opcion3;  |FINSI;
             |SI Opcion = 4;  |HAZ Opcion4;  |FINSI;
             Salir = 0;
         |FINSI;
  |SIGUE;

  |PINPA #0#0;
|FPRC;

|PROCESO LeeActiv;  |TIPO 0;
  |ABRE #5;
  |SELECT #1#5;
  #5#0 = #0#0;
  #5#1 = #0#1;
  #5#2 = #0#5;
  |LEE 040#5=;
  |SI FSalida ! 0;  |DDEFECTO #5;  |FINSI;
  |CIERRA #5;

  |SI #5#14 = "S";
      |MENAV "     La Actividad es un Comunero";
      |ERROR;
      |ACABA;
  |FINSI;

  |SI #5#22 ! "A";
      |MENAV "     La Actividad no esta preparada para modulos";
      |ERROR;
      |ACABA;
  |FINSI;

  |SI #5#46 = "S";  #0#190 = "S";  |FINSI;
|FPRC;

||  ************************************************************************
||  ********************* CALCULOS PARA GRABAR EL 131 Y 310 ****************
||  ************************************************************************

|PROCESO Borra21;
  |BORRA 140#21a;
  |LIBERA #21;
|FPRC;

|DEFBUCLE eosm0021;
  #21#1;
  ;
  #0#0, #0#5, #0#1, 000;
  #0#0, #0#5, #0#1, 999;
  be;
  NULL, Borra21;
|FIN;

|PROCESO Borra22;
  |BORRA 140#22a;
  |LIBERA #22;
|FPRC;

|DEFBUCLE eosm0022;
  #22#1;
  ;
  #0#0, #0#5, #0#1, 000;
  #0#0, #0#5, #0#1, 999;
  be;
  NULL, Borra22;
|FIN;

|PROCESO Tipo2;  |TIPO 2;
  Modo = (FEntrada / 100) ! 0;
  |SI Modo ! 3;  |ACABA;  |FINSI;

  SwBorrar = 0;
  |SI #0#191 = "S";  SwBorrar = 1;  |FINSI;
  |SI #0#192 = "S";  SwBorrar = 1;  |FINSI;
  |SI #0#193 = "S";  SwBorrar = 1;  |FINSI;
  |SI #0#194 = "S";  SwBorrar = 1;  |FINSI;
  |SI #0#195 = "S";  SwBorrar = 1;  |FINSI;
  |SI #0#196 = "S";  SwBorrar = 1;  |FINSI;
  |SI #0#197 = "S";  SwBorrar = 1;  |FINSI;
  |SI #0#198 = "S";  SwBorrar = 1;  |FINSI;

  |SI SwBorrar = 1;
      |MENAV "      La ficha no se puede dar de baja hay modelos trimestrales impresos";
      |ERROR;
      |ACABA;
  |FINSI;

  |ABRE #1;
  #1#0 = #0#0;  #1#1 = #0#1;  #1#2 = #0#5;  |BORRA 140#1c;  |LIBERA #1;
  |CIERRA #1;

  |BUCLE eosm0021;
  |BUCLE eosm0022;

  MTipo    = 2;
  MEmpresa = #0#0;
  MActivi  = #0#1;
  MAnyo    = #0#5;

  |CIERRAT #0;
  |SUB_EJECUTA "eoszp023";
  |ABRET #0;

  #0#0  = MEmpresa;
  #0#1  = MActivi;
  #0#5  = MAnyo;
  |LIBERA #0;
  |LEE 140#0=;
|FPRC;

|PROCESO CogeModelo;
  #0#191 = "N";  #0#192 = "N";  #0#193 = "N";  #0#194 = "N";
  #0#195 = "N";  #0#196 = "N";  #0#197 = "N";  #0#198 = "N";
  |ABRE #20;
  |PARA Periodo = 1; |SI Periodo [ 4; |HACIENDO Periodo = Periodo + 1;
        #20#0 = #0#0;
        #20#1 = #0#5;
        #20#2 = Periodo;
        |LEE 040#20=;
        |SI FSalida = 0; |Y #20#3 ! "01.01.0000";
            |PARA Campo1 = 5;  |SI Campo1 [ 33;  |HACIENDO Campo1 = Campo1 + 7;
                  |SI #0#1 = #20Campo1;
                      Campo7 = Campo1 + 6;
                      |SI Periodo = 1; #0#118 = #20Campo7; #0#191 = "S"; |FINSI;
                      |SI Periodo = 2; #0#119 = #20Campo7; #0#192 = "S"; |FINSI;
                      |SI Periodo = 3; #0#120 = #20Campo7; #0#193 = "S"; |FINSI;
                      |SI Periodo = 4; #0#121 = #20Campo7; #0#194 = "S"; |FINSI;
                  |FINSI;
             |SIGUE;
       |FINSI;
  |SIGUE;
  |CIERRA #20;

  |ABRE #19;
  |PARA Periodo = 1; |SI Periodo [ 4; |HACIENDO Periodo = Periodo + 1;
        #19#0 = #0#0;
        #19#1 = #0#5;
        #19#2 = Periodo;
        |LEE 040#19=;
        |SI FSalida = 0; |Y #19#3 ! "01.01.0000";
            |PARA Campo1 = 5;  |SI Campo1 [ 33;  |HACIENDO Campo1 = Campo1 + 5;
                  |SI #0#1 = #19Campo1;
                      Campo5 = Campo1 + 4;
                      |SI Periodo = 1; #0#124 = #19Campo5; #0#195 = "S"; |FINSI;
                      |SI Periodo = 2; #0#125 = #19Campo5; #0#196 = "S"; |FINSI;
                      |SI Periodo = 3; #0#126 = #19Campo5; #0#197 = "S"; |FINSI;
                      |SI Periodo = 4; #0#127 = #19Campo5; #0#198 = "S"; |FINSI;
                  |FINSI;
             |SIGUE;
       |FINSI;
  |SIGUE;
  |CIERRA #19;
|FPRC;

|PROGRAMA;

     |SI PARAMETRO = "";
         enModelo = 131;
         aAlfa    = "eosy0092;MASIVO";
         |SUB_EJECUTA aAlfa;
         |SI enError15 = 1;
             |CONFI "0000NDesea Continuar";
             |SI FSalida ! 0;
                 |ACABA;
             |FINSI;
         |FINSI;

         |MANTE #0;
         |ACABA;
     |FINSI;

     |ABRE #eosclien;
     #eosclien#0 = enEmpresa;
     |LEE 040#eosclien.=;
     |SI FSalida ! 0;  |DDEFECTO #eosclien;  |FINSI;
     |CIERRA #eosclien;

     |ABRE #eosactiv;
     #eosactiv#0 = enEmpresa;
     #eosactiv#1 = enActividad;
     #eosactiv#2 = enEjer - 2000;
     |LEE 040#eosactiv.=;
     |SI FSalida ! 0;  |DDEFECTO #eosactiv;  |FINSI;

     |ABRET #eosm0017;
     #eosm0017#0 = enEmpresa;
     #eosm0017#5 = enEjer - 2000;
     #eosm0017#1 = enActividad;
     |LEE 101#eosm0017.=;
     |SI FSalida ! 0;
         |DDEFECTO #eosm0017;
         #eosm0017#0 = enEmpresa;
         #eosm0017#5 = enEjer - 2000;
         #eosm0017#1 = enActividad;

         |GRABA 020#eosm0017.b;
     |FINSI;

     #eosm0017#2 = #eosactiv#4;
     #eosm0017#3 = #eosactiv#5;
     #eosm0017#6 = #eosactiv#6;
     #eosm0017#7 = #eosclien#2;

     |PINPA #0#eosm0017;
     |PINDA #0#eosm0017;

     |ENDATOS #2#eosm0017;
     |SI FSalida = 0;
         |GRABA 020#eosm0017.a;
         |LIBERA #eosm0017;
     |FINSI;

     |CIERRA #eosactiv;
     |CIERRAT #eosm0017;
|FPRO;

|PROCESO eosm0018V;
     |SI #1#125 = "S";  |ERROR10;  |ACABA;  |FINSI;

     #1#125  = "S";
     #1#126  = "S";

     #1#128  = #1#94 + #1#77 + #1#79;
     #1#135  = #1#96 + #1#78 + #1#80;

     #1#94   = 0;
     #1#77   = 0;
     #1#79   = 0;
     #1#96   = 0;
     #1#78   = 0;
     #1#80   = 0;

     #1#138  = #1#82 % 5;
     #1#82   = #1#82 - #1#138;

     |GRABA 020#1a;
     |LIBERA #1;
|FPRC;

|DEFBUCLE eosm0018V;
     #1#1;
     ;
     00000, 09, 0;
     99999, 09, 9;
     be;
     NULL, eosm0018V;
|FIN;

|PROCESO Tipo80;  |TIPO 80;
     |BUCLE eosm0018V;
|FPRC;
