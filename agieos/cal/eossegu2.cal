|FICHEROS;
   eossegu2 #0;
   eossegui #1;
   eosactiv #2;
   eosclien #3;

   eosm1300 #4;
   eosm1303 #44;
   eosm1130 #5;
   eosut132 #8;
   eosm2130 #9;
   eosma330 #10;
   eosm0015 #11;
   eosm0019 #16;
   eosm0020 #17;

   dsmom004;
|FIN;

|VARIABLES;
    avance   = "";
    atras    = "";
    f1       = "";
    f2       = "";
    tecla    = "";
    linea    = 0;
    x        = 0;
    y        = 0;
    d_act    = 0;
    h_act    = 0;
    d_mod    = 0;
    h_mod    = 0;
    FEsta    = 0;
    pepe     = 0;
    jose     = 0;
    aFichero = "";
    nEjercicio = 0;

    &d_empre = 0;
    &h_empre = 0;
    &ejerc   = 0;
    &d_per   = 0;
    &h_per   = 0;
    &activ   = 0;
    &modelo  = 0;
|FIN;




|PROCESO pinta_men; |TIPO 7;
|CUADRO 0506 0861;
|ATRI R;
|SI #0#5 = 0;
    |PINTA 0607, "     <F2> Borrar Seguimiento                          ";
|SINO;
    |PINTA 0607, "     <F12> Para poder consultar el Modelo             ";
    |PINTA 0707, "     <F2>  Borrar Seguimiento                         ";
|FINSI;
|ATRI 0;
|FPRC;

|PROCESO pintali1; |TIPO 11;
|SI FSalida = 9; |Y #0#5 ! 0; |HAZ modelos; |FINSI;
|SI FSalida = 10;             |HAZ borrado; |FINSI;
|FPRC;

|PROCESO borra_segui;
|BORRA 140#1a;
|LIBERA #1;
|FPRC;

|DEFBUCLE eossegui1;
#1#1;
;
#0#1, #0#2, #0#3, #0#4, d_act;
#0#1, #0#2, #0#3, #0#4, h_act;
be;
NULL, borra_segui;
|FIN;

|PROCESO borrado;
|CONFI "2400NEstas Seguro de BORRAR el Seguimiento : ";
|SI FSalida ! 0; |ACABA; |FINSI;

|SI activ = 1; d_act = #0#5; h_act = #0#5; |FINSI;
|SI activ = 2; d_act = 10;   h_act = 99;   |FINSI;

|BUCLE eossegui1;
|LIBERA #0;
|LEE 140#0=;
#0#7 = "  BORRADO  ";
|GRABA 140#0a;
|LIBERA #0;
|FPRC;

|PROCESO modelos;
|PUSHV 0501 2380;
|SI #0#4 = 300; |HAZ mod300; |FINSI;
|SI #0#4 = 303; |HAZ mod303; |FINSI;
|SI #0#4 = 130; |HAZ mod130; |FINSI;
|SI #0#4 = 310;
    |SI #0#2 [ 80; |O #0#2 ] 98;
        |HAZ mod31098;
    |FINSI;
|FINSI;
|SI #0#4 = 330; |HAZ mod330; |FINSI;
|SI #0#4 = 131;
    |SI #0#2 ] 0; |O #0#2 [ 80;
        |HAZ mod13198;
    |SINO;
        |SI #0#2 ] 98;  |HAZ mod13198; |FINSI;
    |FINSI;
|FINSI;
|SI #0#4 = 132; |HAZ mod132; |FINSI;
|SI #0#4 = 309; |HAZ mod309; |FINSI;
|POPV;
|FPRC;

|PROCESO mod300;
avance = &255 + "815";
atras  = &255 + "814";

|ABRE #4;
#4#0  = #0#1;
#4#1  = #0#3;
#4#2  = #0#2;
#4#45 = #0#5;
|LEE 040#4=;
FEsta = FSalida;
|CIERRA #4;

|SI FEsta = 0;
   |ET panta1300;
       |PINPA #0#4;
       |PINDA #0#4;
       |LEETECLA tecla;
       |SI tecla = avance; |VETE panta2300; |FINSI;
       |ACABA;

   |ET panta2300;
       |PINPA #1#4;
       |PINDA #1#4;
       |LEETECLA tecla;
       |SI tecla = atras;  |VETE panta1300; |FINSI;
       |ACABA;
|FINSI;
|FPRC;

|PROCESO mod303;
avance = &255 + "815";
atras  = &255 + "814";

|ABRE #44;
#44#0  = #0#1;
#44#1  = #0#3;
#44#2  = #0#2;
#44#45 = #0#5;
|LEE 040#44=;
FEsta = FSalida;
|CIERRA #44;

|SI FEsta = 0;
   |ET panta1303;
       |PINPA #0#44;
       |PINDA #0#44;
       |LEETECLA tecla;
       |SI tecla = avance; |VETE panta2303; |FINSI;
       |ACABA;

   |ET panta2303;
       |PINPA #1#44;
       |PINDA #1#44;
       |LEETECLA tecla;
       |SI tecla = atras;  |VETE panta1303; |FINSI;
       |ACABA;
|FINSI;
|FPRC;

|PROCESO mod130;
|SI #0#2 ] 0;  |Y #0#2 [ 80;
    |ABRE #9;
    #9#0  = #0#1;
    #9#1  = #0#5;
    #9#2  = #0#3;
    #9#3  = #0#2;
    |LEE 040#9=;
    FEsta = FSalida;
    |CIERRA #9;

    |SI FEsta = 0;
        |PINPA #0#9;
        |PINDA #0#9;
        |PAUSA;
    |FINSI;
|SINO;
     |SI #0#2 [ 93;
         |ABRE #5;
         #5#0  = #0#1;
         #5#1  = #0#3;
         #5#2  = #0#2;
         #5#19 = #0#5;
         |LEE 040#5=;
         FEsta = FSalida;
         |CIERRA #5;

         |SI FEsta = 0;
             |PINPA #0#5;
             |PINDA #0#5;
             |PAUSA;
         |FINSI;
     |SINO;
         |ABRE #9;
         #9#0  = #0#1;
         #9#1  = #0#5;
         #9#2  = #0#3;
         #9#3  = #0#2;
         |LEE 040#9=;
         FEsta = FSalida;
         |CIERRA #9;

         |SI FEsta = 0;
             |PINPA #0#9;
             |PINDA #0#9;
             |PAUSA;
         |FINSI;
     |FINSI;
|FINSI;
|FPRC;

|PROCESO mod31098;
  |ABRE #16;
  #16#0  = #0#1;
  #16#1  = #0#2;
  #16#2  = #0#3;
  |LEE 040#16=;
  FEsta = FSalida;
  |CIERRA #16;

  |SI FEsta = 0;
      |PINPA #0#16;
      |PINDA #0#16;
      |PAUSA;
  |FINSI;
|FPRC;

|PROCESO mod330;
avance = &255 + "815";
atras  = &255 + "814";

FEsta = 0;
|ABRE #10;
#10#0  = #0#1;
#10#1  = #0#5;
#10#2  = #0#3;
#10#3  = "M";
#10#4  = #0#2;
|LEE 040#10];
|SI FSalida ! 0; |O #10#0 ! #0#1; |O #10#1 ! #0#5; |O #10#2 ! #0#3;
 |O #10#4 ! #0#2;
    FEsta = 1;
|FINSI;

|SI FEsta = 0;
   |ET panta1330;
       |PINPA #0#10;
       |PINDA #0#10;
       |LEETECLA tecla;
       |SI tecla = avance; |VETE panta2330; |FINSI;
       |ACABA;

   |ET panta2330;
       |PINPA #1#10;
       |PINDA #1#10;
       |LEETECLA tecla;
       |SI tecla = avance; |VETE panta3330; |FINSI;
       |SI tecla = atras;  |VETE panta1330; |FINSI;
       |ACABA;

   |ET panta3330;
       |PINPA #2#10;
       |PINDA #2#10;
       |LEETECLA tecla;
       |SI tecla = atras;  |VETE panta2330; |FINSI;
       |ACABA;
|FINSI;
|FPRC;

|PROCESO mod309;
avance = &255 + "815";
atras  = &255 + "814";

FEsta = 0;
|ABRE #11;
#11#0  = #0#1;
#11#1  = #0#5;
#11#2  = #0#3;
#11#3  = #0#2;
|LEE 040#11=;
|SI FSalida ! 0;  FEsta = 1;  |FINSI;
|CIERRA #11;

|SI FEsta = 0;
   |ET panta1309;
       |PINPA #0#11;
       |PINDA #0#11;
       |PAUSA;
       |ACABA;
|FINSI;
|FPRC;

|PROCESO mod13198;
  |ABRE #17;
  #17#0  = #0#1;
  #17#1  = #0#2;
  #17#2  = #0#3;
  |LEE 040#17=;
  FEsta = FSalida;
  |CIERRA #17;

  |SI FEsta = 0;
      |PINPA #0#17;
      |PINDA #0#17;
      |PAUSA;
  |FINSI;
|FPRC;

|PROCESO mod132;
avance = &255 + "815";
atras  = &255 + "814";

|ABRE #5;
#5#0  = #0#1;
#5#1  = #0#3;
#5#2  = #0#2;
#5#19 = #0#5;
|LEE 040#5=;
FEsta = FSalida;
|CIERRA #5;

|SI FEsta = 0; |Y #5#20 = "S";
   |DDEFECTO #8;
   |HAZ calcular;

   |ET panta1132;
       |PINPA #0#8;
       |PINDA #0#8;
       |LEETECLA tecla;
       |SI tecla = avance; |VETE panta2132; |FINSI;
       |ACABA;

   |ET panta2132;
       |PINPA #1#8;
       |PINDA #1#8;
       |LEETECLA tecla;
       |SI tecla = atras;  |VETE panta1132; |FINSI;
       |ACABA;
|FINSI;
|FPRC;

|PROCESO calcular;
|ABRE #2;
#2#0 = #5#0;
#2#1 = #5#19;
#2#2 = #5#2;
|LEE 110#2=;
FEsta = FSalida;
|CIERRA #2;
|SI FEsta ! 0; |ACABA; |FINSI;

|HAZ cargaut;
|SI #8#7 = 0; pepe = 1; |SINO; pepe = #8#7; |FINSI;
jose = (pepe * 100) / #8#8;
|SI jose [ 50; |Y jose > 0; #8#9 = jose; |FINSI;
#8#13 = #8#10 % 20;
|FPRC;

|PROCESO cargaut;
#8#0  = #2#0;
#8#1  = #2#3;
#8#2  = #2#1;
#8#3  = #2#6;
#8#4  = #5#1;
#8#5  = #5#2;
#8#12 = #5#18;

|SI #2#23 = "E";
    |SI #2#24 = 2; #8#7 = #5#7; |FINSI;
    |SI #2#24 = 1; #8#7 = #5#9; |FINSI;
    #8#6 = "EMPRESARIAL";
|FINSI;

|SI #2#23 = "P";
    #8#7 = #5#14;
    #8#6 = "PROFESIONAL";
|FINSI;

|ABRE #2;
#2#0 = #5#0;
#2#1 = #5#19;
#2#2 = #5#2 - 2;
|SI #2#2 = -2;  #2#2 = 98;  |FINSI;
|SI #2#2 = -1;  #2#2 = 99;  |FINSI;
|LEE 040#2=;
|SI FSalida ! 0; |DDEFECTO #2; |FINSI;

|SI #5#1 = 1; #8#8 = #2#30; |FINSI;
|SI #5#1 = 2; #8#8 = #2#31; |FINSI;
|SI #5#1 = 3; #8#8 = #2#32; |FINSI;
|SI #5#1 = 4; #8#8 = #2#33; |FINSI;

#2#0 = #5#0;
#2#1 = #5#19;
#2#2 = #5#2;
|LEE 040#2=;
|SI FSalida ! 0; |DDEFECTO #2; |FINSI;

|SI #5#1 = 1; #8#10 = #2#35; |FINSI;
|SI #5#1 = 2; #8#10 = #2#36; |FINSI;
|SI #5#1 = 3; #8#10 = #2#37; |FINSI;
|SI #5#1 = 4; #8#10 = #2#38; |FINSI;

|CIERRA #2;
|FPRC;

|| /////////////////////////////////////////////////////////////////////
|PROCESO cargaind;
  #3#0 = #1#0;
  |LEE 040#3=;
  |SI FSalida ! 0; |DDEFECTO #3; |FINSI;

  #2#0 = #1#0;
  #2#1 = #1#4;
  #2#2 = #1#1;
  |LEE 040#2=;
  |SI FSalida ! 0; |DDEFECTO #2; |FINSI;

  linea = linea + 1;
  |PARA x = 0; |SI x [ 7; |HACIENDO x = x + 1;
        y = x + 1;
        #0y = #1x;
  |SIGUE;
  #0#0  = linea;
  #0#9  = #3#2;
  #0#10 = #2#6;

  |SI activ = 2; #0#5 = 0; |FINSI;

  |SI activ = 1;
      |SI #1#3 = 300;
         |ABRE #4;
         #4#0  = #1#0;
         #4#1  = #1#2;
         #4#2  = #1#1;
         #4#45 = #1#4;
         |LEE 040#4=;
         |SI FSalida ! 0; |DDEFECTO #4; #4#3 = "  BORRADO"; |FINSI;
         #0#7 = #4#3;
         |CIERRA #4;
     |FINSI;

     |SI #1#3 = 303;
         |SI nEjercicio ] 2014;
             |ABRE #dsmom004;
             #dsmom004#0  = #1#0;
             #dsmom004#1  = nEjercicio;
             #dsmom004#2  = #1#2;
             #dsmom004#3  = #1#3;
             #dsmom004#4  = 99;
             |LEE 040#dsmom004.];
             |SI FSalida = 0;
                 |LEE 040#dsmom004.a;
             |SINO;
                 |LEE 040#dsmom004.u;
             |FINSI;
             |SI FSalida = 0; |Y #dsmom004#0 = #1#0; |Y #dsmom004#1  = nEjercicio;
                 |Y #dsmom004#2  = #1#2; |Y #dsmom004#3  = #1#3;
                 #0#7 = #dsmom004#10;
             |SINO;
                 #0#7 = "  BORRADO";
             |FINSI;
             |CIERRA #dsmom004;
         |SINO;
             |ABRE #44;
             #44#0  = #1#0;
             #44#1  = #1#2;
             #44#2  = #1#1;
             #44#45 = #1#4;
             |LEE 040#44=;
             |SI FSalida ! 0; |DDEFECTO #44; #44#3 = "  BORRADO"; |FINSI;
             #0#7 = #44#3;
             |CIERRA #44;
         |FINSI;
     |FINSI;

     |SI #1#3 = 130; |O #1#3 = 132;
         |SI #1#1 ] 0;  |Y #1#1 [ 80;
             |ABRE #9;
             #9#0  = #1#0;
             #9#1  = #1#4;
             #9#2  = #1#2;
             #9#3  = #1#1;
             |LEE 040#9=;
             |SI FSalida ! 0; |DDEFECTO #9; #9#4 = "  BORRADO"; |FINSI;
             #0#7 = #9#4;
             |CIERRA #9;
         |SINO;
             |SI #1#1 [ 93;
                 |ABRE #5;
                 #5#0  = #1#0;
                 #5#1  = #1#2;
                 #5#2  = #1#1;
                 #5#19 = #1#4;
                 |LEE 040#5=;
                 |SI FSalida ! 0; |DDEFECTO #5; #5#3 = "  BORRADO"; |FINSI;
                 #0#7 = #5#3;
                 |CIERRA #5;
              |SINO;
                 |ABRE #9;
                 #9#0  = #1#0;
                 #9#1  = #1#4;
                 #9#2  = #1#2;
                 #9#3  = #1#1;
                 |LEE 040#9=;
                 |SI FSalida ! 0; |DDEFECTO #9; #9#4 = "  BORRADO"; |FINSI;
                 #0#7 = #9#4;
                 |CIERRA #9;
              |FINSI;
       |FINSI;
    |FINSI;

    |SI #1#3 = 310;
        |SI #1#1 [ 80;  |O #1#1 ] 98;
            |ABRE #16;
            #16#0  = #1#0;
            #16#1  = #1#1;
            #16#2  = #1#2;
            |LEE 040#16=;
            |SI FSalida ! 0; |DDEFECTO #16; #16#3 = "  BORRADO"; |FINSI;
            #0#7 = #16#3;
            |CIERRA #16;
        |FINSI;
    |FINSI;

    |SI #1#3 = 330;
        |ABRE #10;
        #10#0  = #1#0;
        #10#1  = #1#4;
        #10#2  = #1#2;
        #10#3  = "M";
        #10#4  = #1#1;
        |LEE 040#10];
        |SI FSalida ! 0; |O #10#0 ! #1#0; |O #10#1 ! #1#4; |O #10#2 ! #1#2;
         |O #10#4 ! #1#1;
            |DDEFECTO #10; #10#5 = "  BORRADO";
        |FINSI;
        #0#7 = #10#5;
        |CIERRA #10;
    |FINSI;

    |SI #1#3 = 309;
        |ABRE #11;
        #11#0  = #1#0;
        #11#1  = #1#4;
        #11#2  = #1#2;
        #11#3  = #1#1;
        |LEE 040#11=;
        |SI FSalida ! 0;
            |DDEFECTO #11; #11#4 = "  BORRADO";
        |FINSI;
        #0#7 = #11#4;
        |CIERRA #10;
    |FINSI;

    |SI #1#3 = 131;
        |SI #1#1 [ 80;  |O #1#1 ] 98;
            |ABRE #17;
            #17#0  = #1#0;
            #17#1  = #1#1;
            #17#2  = #1#2;
            |LEE 040#17=;
            |SI FSalida ! 0; |DDEFECTO #17; #17#3 = "  BORRADO"; |FINSI;
            #0#7 = #17#3;
            |CIERRA #17;
        |FINSI;
    |FINSI;
|FINSI;
|GRABA 040#0n;
|FPRC;

|DEFBUCLE eossegui;
   #1#1;
   ;
   d_empre, ejerc, d_per, d_mod, d_act;
   h_empre, ejerc, h_per, h_mod, h_act;
   e;
   NULL, cargaind;
|FIN;
|| //////////////////////////////////////////////////////////////////////

|RUTINA inferior;
  #0#0 = 1;
|FRUT;

|RUTINA superior;
  #0#0 = linea;
|FRUT;

|PROGRAMA;

   aFichero = ("1y" + Usuario) % 108;
   |NOME_DAT #0 aFichero;
   |DELINDEX #0;

   linea     = 0;
   |SI activ = 1; d_act = 1;  h_act = 6;  |FINSI;
   |SI activ = 2; d_act = 10; h_act = 99; |FINSI;

   nEjercicio = 2000 + ejerc;
   |SI modelo [ 1; |Y nEjercicio ] 2014; |Y activ = 1;
       d_act = 1; h_act = 99;
   |FINSI;

   |SI modelo = 1;
       |SI ejerc [ 8;  |O ejerc ] 80;
           d_mod = 300; h_mod = 300;
       |SINO;
           d_mod = 303; h_mod = 303;
       |FINSI;
   |FINSI;
   |SI modelo = 2;  d_mod = 310; h_mod = 310; |FINSI;
   |SI modelo = 3;  d_mod = 370; h_mod = 370; |FINSI;
   |SI modelo = 4;  d_mod = 130; h_mod = 130; |FINSI;
   |SI modelo = 5;  d_mod = 131; h_mod = 131; |FINSI;
   |SI modelo = 6;  d_mod = 132; h_mod = 132; |FINSI;
   |SI modelo = 7;  d_mod = 330; h_mod = 330; |FINSI;
   |SI modelo = 8;  d_mod = 110; h_mod = 110; |FINSI;
   |SI modelo = 0;  d_mod = 000; h_mod = 999; |FINSI;
   |SI modelo = 9;  d_mod = 309; h_mod = 309; |FINSI;

   |ABRE #0;
   |ABRE #2;
   |ABRE #3;

   |BUCLE eossegui;

   |CIERRA #0;
   |CIERRA #2;
   |CIERRA #3;

   |SI linea = 0;
       |MENAV "0000Seleccion vacia";
       |ACABA;
   |FINSI;

   |PINPA #0#0;
   |ENTLINEAL #1#0, -1, 4, 21, 0, inferior, superior;
   |POPV;
   |DELINDEX #0;
|FPRO;
