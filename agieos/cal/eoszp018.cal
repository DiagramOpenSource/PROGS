|FICHEROS;
     eosplan1 #1;             || Planillas
     eosm1300 #2;             || Mantenimiento del 300
     eosma330 #3;             || Mantenimiento del 330
     eos349li #4;             || Lineal 349
     eosactiv #5;             || Actividad
     eos349ca #6;             || Cabecera 349
     eoslinve #7;             || Lineas Libros Ventas
     eosconce #8;             || Conceptos
     eosconpl #9;             || Control de Planillas
     eoslinco #10;            || Lineas Libros Compras
     eosw0053 #99,MANTE;      || Temporal
     eosw0054 #100,MANTE;     || Temporal
     eosw0055 #101,MANTE;     || Temporal
     eosw0056 #102;           || Temporal
|FIN;

|VARIABLES;
     Bas1    = 0;
     Bas2    = 0;
     Bas3    = 0;
     Bas4    = 0;
     Bas5    = 0;
     Bas6    = 0;
     Bas7    = 0;
     Cuo1    = 0;
     Cuo2    = 0;
     Cuo3    = 0;
     Cuo4    = 0;
     Cuo5    = 0;
     Cuo6    = 0;
     Cuo7    = 0;
     Campos  = 0;
     Campos1 = 0;
     Campos2 = 0;
     SwHay   = 0;
     Suma    = 0;
     FEstado = 0;
     XPara   = 0;
     Base1   = 0;
     Porc1   = 0;
     Cuot1   = 0;
     Base2   = 0;
     Porc2   = 0;
     Cuot2   = 0;
     Base3   = 0;
     Porc3   = 0;
     Cuot3   = 0;
     Conc    = 0;
     Base    = 0;
     CuoI    = 0;
     PorI    = 0;
     Fichero = "";
     BaseD   = 0;
     CuotD   = 0;
|FIN;

|| *************************************
|| Procesos del Pase
|| *************************************

|| -----------------
|| Procesoas Comunes
|| -----------------

|PROCESO Carga101;
  |ET Pide101;
      #101#0  = #99#0;
      #101#1  = #99#4;
      #101#2  = #99#1;
      #101#3  = #99#2;
      #101#4  = BaseD;
      #101#5  = CuotD;
      #101#6  = Base1;
      #101#7  = Porc1;
      #101#8  = Cuot1;
      #101#9  = Base2;
      #101#10 = Porc2;
      #101#11 = Cuot2;
      #101#12 = Base3;
      #101#13 = Porc3;
      #101#14 = Cuot3;

     |PINPA #0#101;
     |PINDA #0#101;
     |ENDATOS #1#101;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

  Suma = #101#6 + #101#9 + #101#12;
  |SI Suma ! BaseD;
      |MENAV "     La Base no coincide con la Base Declarada";
      |VETE Pide101;
  |FINSI;

  Suma = #101#8 + #101#11 + #101#14;
  |SI Suma ! CuotD;
      |MENAV "     La Cuota no coincide con la Cuota Declarada";
      |VETE Pide101;
  |FINSI;
|FPRC;

|PROCESO Carga102;
  |ET Pide102;
      #102#0  = #99#0;
      #102#1  = #99#4;
      #102#2  = #99#1;
      #102#3  = #99#2;
      #102#4  = Bas1;
      #102#5  = Bas2;
      #102#6  = Bas3;
      #102#7  = Bas4;
      #102#8  = Bas5;
      #102#9  = Bas6;
      #102#10 = Bas7;
      #102#11 = Cuo1;
      #102#12 = Cuo2;
      #102#13 = Cuo3;
      #102#14 = Cuo4;
      #102#15 = Cuo5;
      #102#16 = Cuo6;
      #102#17 = Cuo7;

     |PINPA #0#102;
     |PINDA #0#102;
     |ENDATOS #1#102;
     |SI FSalida ! 0;  |ACABA;  |FINSI;
|FPRC;

|| -----------------------------------------
|| Adquisiciones intracomunitarias Planillas
|| -----------------------------------------

|PROCESO GrabaTempo100;
  |PARA XPara = 0; |SI XPara [ 27;  |HACIENDO XPara = XPara + 1;
        #100XPara = #4XPara;
  |SIGUE;

  #100#28 = #99#4;
  |GRABA 140#100n;
|FPRC;

|DEFBUCLE eos349liT;
  #4#1;
  ;
  #1#0, #1#1, #1#2, 000;
  #1#0, #1#1, #1#2, 999;
  ;
  NULL, GrabaTempo100;
|FIN;

|PROCESO Graba349li;
  #4#0 = #100#0;
  #4#1 = #100#1;
  #4#2 = #100#2;
  #4#3 = #100#3;
  |LIBERA #4;
  |LEE 040#4=;
  |SI FSalida ! 0;
      |DDEFECTO #4;
      #4#0 = #100#0;
      #4#1 = #100#1;
      #4#2 = #100#2;
      #4#3 = #100#3;
      |GRABA 040#4b;
  |FINSI;

  |PARA XPara = 0; |SI XPara [ 27;  |HACIENDO XPara = XPara + 1;
        #4XPara = #100XPara;
  |SIGUE;
  |GRABA 140#4a;

  |ABRE #6;
  #6#0 = #100#0;
  #6#1 = #100#1;
  #6#2 = #100#2;
  |LIBERA #6;
  |LEE 040#6=;
  |SI FSalida ! 0;
      |DDEFECTO #6;
      #6#0 = #100#0;
      #6#1 = #100#1;
      #6#2 = #100#2;
      #6#3 = #100#28;
      #6#8 = 1;
      #6#9 = #100#8;
      |GRABA 040#6n;
  |FINSI;
  |CIERRA #6;
|FPRC;

|DEFBUCLE eosw0054;
  #100#1;
  ;
  #1#0, #1#1, #1#2, 000;
  #1#0, #1#1, #1#2, 999;
  ;
  NULL, Graba349li;
|FIN;

|RUTINA ClaveBaja100;
  #100#0  = #99#0;
  #100#1  = #99#2;
  #100#2  = 97;
  #100#3  = 1;
|FRUT;

|RUTINA ClaveAlta100;
  #100#0  = #99#0;
  #100#1  = #99#2;
  #100#2  = 97;
  #100#3  = 999;
|FRUT;

|PROCESO Punto11;
  |ABRE #100;  |CIERRA #100;  |DELINDEX #100;

  |ABRE #1;
  #1#0  = #99#0;
  #1#83 = #99#1;
  #1#1  = 97;
  #1#2  = #99#2;
  |LEE 040#1=;
  |SI FSalida ! 0;  |CIERRA #1;  |ACABA;  |FINSI;

  |ABRE #100;
  |BUCLE eos349liT;

  |LEE 040#100p;
  |SI FSalida ! 0;
      |ABRE #100;
      |DDEFECTO #100;
      #100#0  = #99#0;
      #100#1  = #99#2;
      #100#2  = 97;
      #100#3  = 1;
      #100#4  = "A";
      #100#8  = #1#88;
      #100#14 = #99#2;
      #100#15 = #99#1;
      |GRABA 140#100n;
      |CIERRA #100;
  |FINSI;
  |CIERRA #100;

  |ENTLINEAL #1#100, 4, 4, 16, 1, ClaveBaja100, ClaveAlta100;

  |ABRE #4;
  |BUCLE eosw0054;
  |CIERRA #4;
  |CIERRA #1;
  |ABRE #100;  |CIERRA #100;  |DELINDEX #100;
|FPRC;

|| -------------------------------------------------
|| Adquisiciones intracomunitarias Modelo 300 Manual
|| -------------------------------------------------

|PROCESO CogeIva;
  |SI #4#17 = 4;   Base1 = Base1 + #4#16;  Cuot1 = Cuot1 + #4#18;  |FINSI;
  |SI #4#17 = 7;   Base2 = Base2 + #4#16;  Cuot2 = Cuot2 + #4#18;  |FINSI;
  |SI #4#17 = 16;  Base3 = Base3 + #4#16;  Cuot3 = Cuot3 + #4#18;  |FINSI;
  |SI #4#20 = 4;   Base1 = Base1 + #4#19;  Cuot1 = Cuot1 + #4#21;  |FINSI;
  |SI #4#20 = 7;   Base2 = Base2 + #4#19;  Cuot2 = Cuot2 + #4#21;  |FINSI;
  |SI #4#20 = 16;  Base3 = Base3 + #4#19;  Cuot3 = Cuot3 + #4#21;  |FINSI;
  |SI #4#23 = 4;   Base1 = Base1 + #4#22;  Cuot1 = Cuot1 + #4#24;  |FINSI;
  |SI #4#23 = 7;   Base2 = Base2 + #4#22;  Cuot2 = Cuot2 + #4#24;  |FINSI;
  |SI #4#23 = 16;  Base3 = Base3 + #4#22;  Cuot3 = Cuot3 + #4#24;  |FINSI;

  Porc1 = 4;  Porc2 = 7;  Porc3 = 16;
|FPRC;

|DEFBUCLE eos349li12;
  #4#1;
  ;
  #1#0, #1#1, #1#2, 000;
  #1#0, #1#1, #1#2, 999;
  ;
  NULL, CogeIva;
|FIN;

|PROCESO Punto12;
  |ABRE #2;
  #2#0  = #99#0;
  #2#45 = #99#1;
  #2#1  = #99#2;
  #2#2  = 97;
  |LIBERA #2;
  |LEE 040#2=;
  |SI FSalida ! 0;  |CIERRA #2;  |ACABA;  |FINSI;

  |ABRE #1;
  #1#0  = #99#0;
  #1#83 = #99#1;
  #1#1  = 97;
  #1#2  = #99#2;
  |LEE 040#1=;
  |SI FSalida = 0;
     Base1 = 0;  Cuot1 = 0;
     Base2 = 0;  Cuot2 = 0;
     Base3 = 0;  Cuot3 = 0;
     |BUCLE eos349li12;
  |SINO;
     Base1 = #2#58;  Porc1 = #2#59;  Cuot1 = #2#60;
     Base2 = #2#61;  Porc2 = #2#62;  Cuot2 = #2#63;
     Base3 = #2#64;  Porc3 = #2#65;  Cuot3 = #2#66;
  |FINSI;
  |CIERRA #1;

  BaseD = #2#49;  CuotD = #2#50;
  |HAZ Carga101;
  |SI FSalida = 0;
        #2#58 = #101#6;
        #2#59 = #101#7;
        #2#60 = #101#8;
        #2#61 = #101#9;
        #2#62 = #101#10;
        #2#63 = #101#11;
        #2#64 = #101#12;
        #2#65 = #101#13;
        #2#66 = #101#14;
        |GRABA 140#2a;
  |FINSI;
  |CIERRA #2;
|FPRC;

|| ------------------------------------------------
|| Adquisiciones intracomunitarias Modelo 300 Libro
|| ------------------------------------------------

|PROCESO ConceptoV;
  #8#0 = Conc;
  |LEE 040#8=;
  |SI FSalida ! 0;  |ACABA;  |FINSI;
  |SI #8#6 ! "S";   |ACABA;  |FINSI;
  |SI #8#2 ! "R";|Y #8#2 ! "r";|Y #8#2 ! "I";|Y #8#2 ! "i";  |ACABA;  |FINSI;

  |SI PorI = 4;  Base1 = Base1 + Base;  Cuot1 = Cuot1 + CuoI;  |FINSI;
  |SI PorI = 7;  Base2 = Base2 + Base;  Cuot2 = Cuot2 + CuoI;  |FINSI;
  |SI PorI = 16; Base3 = Base3 + Base;  Cuot3 = Cuot3 + CuoI;  |FINSI;

  Porc1 = 4;
  Porc2 = 7;
  Porc3 = 16;
|FPRC;

|PROCESO MiraVentas;
  Conc = #7#5;
  Base = #7#7;
  PorI = #7#8;
  CuoI = #7#9;
  |HAZ ConceptoV;

  Conc = #7#33;
  Base = #7#23;
  PorI = #7#25;
  CuoI = #7#27;
  |HAZ ConceptoV;

  Conc = #7#34;
  Base = #7#24;
  PorI = #7#26;
  CuoI = #7#28;
  |HAZ ConceptoV;

  Conc = #7#60;
  Base = #7#45;
  PorI = #7#48;
  CuoI = #7#51;
  |HAZ ConceptoV;

  Conc = #7#61;
  Base = #7#46;
  PorI = #7#49;
  CuoI = #7#52;
  |HAZ ConceptoV;

  Conc = #7#62;
  Base = #7#47;
  PorI = #7#50;
  CuoI = #7#53;
  |HAZ ConceptoV;
|FPRC;

|DEFBUCLE eoslinve;
  #7#2;
  ;
  #99#0, 97, #99#2, #99#1, "01.01.1997", 00001;
  #99#0, 97, #99#2, #99#1, "31.12.1997", 32000;
  ;
  NULL, MiraVentas;
|FIN;

|PROCESO Punto22;
  |HAZ Actividad;
  |SI #5#26 = "N";  |ACABA;  |FINSI;

  |ABRE #2;
  #2#0  = #99#0;
  #2#45 = #99#1;
  #2#1  = #99#2;
  #2#2  = 97;
  |LIBERA #2;
  |LEE 040#2=;
  |SI FSalida ! 0;  |CIERRA #2;  |ACABA;  |FINSI;

  Fichero = "v" + (("00000" + #99#0) % -105) + "97";
  |NOME_DAT #7 Fichero;

   Base1 = 0;  Cuot1 = 0;
   Base2 = 0;  Cuot2 = 0;
   Base3 = 0;  Cuot3 = 0;

  |DDEFECTO #101;
  |ABRE #8;
  |BUCLE eoslinve;
  |CIERRA #8;

  BaseD = #2#49;  CuotD = #2#50;
  |HAZ Carga101;
  |SI FSalida = 0;
      #2#58 = #101#6;
      #2#59 = #101#7;
      #2#60 = #101#8;
      #2#61 = #101#9;
      #2#62 = #101#10;
      #2#63 = #101#11;
      #2#64 = #101#12;
      #2#65 = #101#13;
      #2#66 = #101#14;
      |GRABA 140#2a;
  |FINSI;
  |CIERRA #2;
|FPRC;

|| -------------------------------------------------
|| Adquisiciones intracomunitarias Modelo 330 Manual
|| -------------------------------------------------

|PROCESO Punto13;
  |ABRE #3;
  #3#0  = #99#0;
  #3#1  = #99#1;
  #3#2  = #99#2;
  #3#3  = "M";
  #3#4  = 97;
  |LIBERA #3;
  |LEE 040#3=;
  |SI FSalida ! 0;
      #3#0  = #99#0;
      #3#1  = #99#1;
      #3#2  = #99#2;
      #3#3  = "T";
      #3#4  = 97;
      |LIBERA #3;
      |LEE 040#3=;
      |SI FSalida ! 0;  |CIERRA #3;  |ACABA;  |FINSI;
  |FINSI;

  Base1 = #3#69;  Porc1 = #3#70;  Cuot1 = #3#71;
  Base2 = #3#72;  Porc2 = #3#73;  Cuot2 = #3#74;
  Base3 = #3#75;  Porc3 = #3#76;  Cuot3 = #3#77;

  BaseD = #3#25;  CuotD = #3#26;
  |HAZ Carga101;
  |SI FSalida = 0;
      #3#69 = #101#6;
      #3#70 = #101#7;
      #3#71 = #101#8;
      #3#72 = #101#9;
      #3#73 = #101#10;
      #3#74 = #101#11;
      #3#75 = #101#12;
      #3#76 = #101#13;
      #3#77 = #101#14;
      |GRABA 140#3a;
  |FINSI;
  |CIERRA #3;
|FPRC;

|| -------------------------------
|| IVA Soportado Modelo 300 Manual
|| -------------------------------

|PROCESO CogePlanillas;
  |HAZ Actividad;
  |ABRE #9;
  #9#0 = 97;
  |LEE 040#9=;
  |SI FSalida ! 0;  |DDEFECTO #9;  |FINSI;
  |CIERRA #9;

  |SI #5#22 = "B"; |O #5#22 = "N";
      |PARA Campos = 26; |SI Campos [ 34; |HACIENDO Campos = Campos + 1;
            Campos1 = Campos + 15;
            Campos2 = Campos + 104;
            |SI #1Campos2 ! 0;
                |SI #9Campos1 = 1; #102#4  = #102#4  + #1Campos; |FINSI;
                |SI #9Campos1 = 2; #102#6  = #102#6  + #1Campos; |FINSI;
                |SI #9Campos1 = 3; #102#9  = #102#9  + #1Campos; |FINSI;
                |SI #9Campos1 = 4; #102#10 = #102#10 + #1Campos; |FINSI;
                |SI #9Campos1 = 5; #102#7  = #102#7  + #1Campos; |FINSI;
            |FINSI;
      |SIGUE;

      |PARA Campos = 108; |SI Campos [ 118; |HACIENDO Campos = Campos + 1;
            Campos1 = Campos - 58;
            Campos2 = Campos + 31;
            |SI #1Campos2 ! 0;
                |SI #9Campos1 = 1; #102#4  = #102#4  + #1Campos; |FINSI;
                |SI #9Campos1 = 2; #102#6  = #102#6  + #1Campos; |FINSI;
                |SI #9Campos1 = 3; #102#9  = #102#9  + #1Campos; |FINSI;
                |SI #9Campos1 = 4; #102#10 = #102#10 + #1Campos; |FINSI;
                |SI #9Campos1 = 5; #102#7  = #102#7  + #1Campos; |FINSI;
            |FINSI;
      |SIGUE;
  |FINSI;

  |SI #5#22 = "C"; |O #5#22 = "A";
      |PARA Campos = 26; |SI Campos [ 34; |HACIENDO Campos = Campos + 1;
            Campos1 = Campos + 35;
            Campos2 = Campos + 104;
            |SI #1Campos2 ! 0;
                |SI #9Campos1 = 1; #102#4  = #102#4  + #1Campos; |FINSI;
                |SI #9Campos1 = 2; #102#6  = #102#6  + #1Campos; |FINSI;
                |SI #9Campos1 = 3; #102#9  = #102#9  + #1Campos; |FINSI;
                |SI #9Campos1 = 4; #102#10 = #102#10 + #1Campos; |FINSI;
                |SI #9Campos1 = 5; #102#7  = #102#7  + #1Campos; |FINSI;
            |FINSI;
      |SIGUE;

      |PARA Campos = 108; |SI Campos [ 118; |HACIENDO Campos = Campos + 1;
            Campos1 = Campos - 38;
            Campos2 = Campos + 31;
            |SI #1Campos2 ! 0;
                |SI #9Campos1 = 1; #102#4  = #102#4  + #1Campos; |FINSI;
                |SI #9Campos1 = 2; #102#6  = #102#6  + #1Campos; |FINSI;
                |SI #9Campos1 = 3; #102#9  = #102#9  + #1Campos; |FINSI;
                |SI #9Campos1 = 4; #102#10 = #102#10 + #1Campos; |FINSI;
                |SI #9Campos1 = 5; #102#7  = #102#7  + #1Campos; |FINSI;
            |FINSI;
      |SIGUE;
  |FINSI;
|FPRC;

|PROCESO Punto14;
  |ABRE #2;
  #2#0  = #99#0;
  #2#45 = #99#1;
  #2#1  = #99#2;
  #2#2  = 97;
  |LIBERA #2;
  |LEE 040#2=;
  |SI FSalida ! 0;  |CIERRA #2;  |ACABA;  |FINSI;

  |DDEFECTO #102;

  |ABRE #1;
  #1#0  = #99#0;
  #1#83 = #99#1;
  #1#1  = 97;
  #1#2  = #99#2;
  |LEE 040#1=;
  |SI FSalida = 0;
      |HAZ CogePlanillas;
  |FINSI;
  |CIERRA #1;

  #102#8 = #2#49;
  Bas1 = #102#4;  Bas2 = #102#5;  Bas3 = #102#6;
  Bas4 = #102#7;  Bas5 = #102#8;  Bas6 = #102#9;
  Bas7 = #102#10;
  Cuo1 = #2#21;  Cuo2 = #2#43;  Cuo3 = #2#22;
  Cuo4 = #2#44;  Cuo5 = #2#51;  Cuo6 = #2#23;
  Cuo7 = #2#24;

  |HAZ Carga102;
  |SI FSalida = 0;
      #2#70 = #102#4;
      #2#75 = #102#5;
      #2#71 = #102#6;
      #2#76 = #102#7;
      #2#72 = #102#8;
      #2#73 = #102#9;
      #2#74 = #102#10;
      |GRABA 140#2a;
  |FINSI;
  |CIERRA #2;
|FPRC;

|| ------------------------------------------------
|| Adquisiciones intracomunitarias Modelo 300 Libro
|| ------------------------------------------------

|PROCESO ConceptoC;
  |HAZ Actividad;
  |SI #5#26 = "N";  |ACABA;  |FINSI;

  #8#0 = Conc;
  |LEE 040#8=;
  |SI FSalida ! 0;  |ACABA;  |FINSI;
  |SI #8#2 ! "I";|Y #8#2 ! "i";|Y #8#2 ! "S";|Y #8#2 ! "s";  |ACABA;  |FINSI;

  |SI CuoI ! 0;
      |SI #8#8 = 1; #102#4 = #102#4 + Base; |FINSI;
      |SI #8#8 = 2; #102#6 = #102#6 + Base; |FINSI;
      |SI #8#8 = 3; #102#9 = #102#9 + Base; |FINSI;
      |SI #8#8 = 4; #102#5 = #102#5 + Base; |FINSI;
      |SI #8#8 = 5; #102#7 = #102#7 + Base; |FINSI;
      |SI #8#8 = 6; #102#8 = #102#8 + Base; |FINSI;
  |FINSI;
|FPRC;

|PROCESO MiraCompras;
  Conc = #10#5;
  Base = #10#7;
  PorI = #10#8;
  CuoI = #10#9;
  |HAZ ConceptoC;

  Conc = #10#33;
  Base = #10#23;
  PorI = #10#25;
  CuoI = #10#27;
  |HAZ ConceptoC;

  Conc = #10#34;
  Base = #10#24;
  PorI = #10#26;
  CuoI = #10#28;
  |HAZ ConceptoC;
|FPRC;

|DEFBUCLE eoslinco;
  #10#2;
  ;
  #99#0, 97, #99#2, #99#1, "01.01.1997", 00001;
  #99#0, 97, #99#2, #99#1, "31.12.1997", 32000;
  ;
  NULL, MiraCompras;
|FIN;

|PROCESO Punto24;
  |HAZ Actividad;
  |SI #5#26 = "N";  |ACABA;  |FINSI;

  |ABRE #2;
  #2#0  = #99#0;
  #2#45 = #99#1;
  #2#1  = #99#2;
  #2#2  = 97;
  |LIBERA #2;
  |LEE 040#2=;
  |SI FSalida ! 0;  |CIERRA #2;  |ACABA;  |FINSI;

  Fichero = "c" + (("00000" + #99#0) % -105) + "97";
  |NOME_DAT #10 Fichero;

  |DDEFECTO #102;
  |ABRE #8;
  |BUCLE eoslinco;
  |CIERRA #8;

  #102#8 = #2#49;
  Bas1 = #102#4;  Bas2 = #102#5;  Bas3 = #102#6;
  Bas4 = #102#7;  Bas5 = #102#8;  Bas6 = #102#9;
  Bas7 = #102#10;
  Cuo1 = #2#21;  Cuo2 = #2#43;  Cuo3 = #2#22;
  Cuo4 = #2#44;  Cuo5 = #2#51;  Cuo6 = #2#23;
  Cuo7 = #2#24;

  |HAZ Carga102;
  |SI FSalida = 0;
      #2#70 = #102#4;
      #2#75 = #102#5;
      #2#71 = #102#6;
      #2#76 = #102#7;
      #2#72 = #102#8;
      #2#73 = #102#9;
      #2#74 = #102#10;
      |GRABA 140#2a;
  |FINSI;
  |CIERRA #2;
|FPRC;

|| -------------------------------
|| IVA Soportado Modelo 330 Manual
|| -------------------------------

|PROCESO Punto15;
  |ABRE #3;
  #3#0  = #99#0;
  #3#1  = #99#1;
  #3#2  = #99#2;
  #3#3  = "M";
  #3#4  = 97;
  |LIBERA #3;
  |LEE 040#3=;
  |SI FSalida ! 0;
      #3#0  = #99#0;
      #3#1  = #99#1;
      #3#2  = #99#2;
      #3#3  = "T";
      #3#4  = 97;
      |LIBERA #3;
      |LEE 040#3=;
      |SI FSalida ! 0;  |CIERRA #3;  |ACABA;  |FINSI;
  |FINSI;

  |DDEFECTO #102;

  Bas1 = #3#28;  Bas2 = #3#36;  Bas3 = #3#30;
  Bas4 = #3#38;  Bas5 = #3#32;  Bas6 = #3#81;
  Bas7 = 0;

  Cuo1 = #3#29;  Cuo2 = #3#37;  Cuo3 = #3#31;
  Cuo4 = #3#39;  Cuo5 = #3#33;  Cuo6 = #3#34;
  Cuo7 = #3#35;

  |C_M #102#4, 1, "N";  |C_M #102#5, 1, "N";  |C_M #102#6,  1, "N";
  |C_M #102#7, 1, "N";  |C_M #102#8, 1, "N";  |C_M #102#10, 1, "N";

  |HAZ Carga102;
  |SI FSalida = 0;
      #3#28 = #102#4;
      #3#36 = #102#5;
      #3#30 = #102#6;
      #3#38 = #102#7;
      #3#32 = #102#8;
      #3#81 = #102#9;
      |GRABA 140#3a;
  |FINSI;
  |CIERRA #3;
|FPRC;

|| ************
|| Distribuidor
|| ************

|PROCESO AlRollo;
  |SI #99#3 = 1;
      |SI #99#7 = 1;  |HAZ Punto11;  |FINSI;
  |FINSI;

  |SI #99#3 = 2;
      |SI #99#7 = 1;  |HAZ Punto12;  |FINSI;
      |SI #99#7 = 2;  |HAZ Punto22;  |FINSI;
  |FINSI;

  |SI #99#3 = 3;
      |SI #99#7 = 1;  |HAZ Punto13;  |FINSI;
  |FINSI;

  |SI #99#3 = 4;
      |SI #99#7 = 1;  |HAZ Punto14;  |FINSI;
      |SI #99#7 = 2;  |HAZ Punto24;  |FINSI;
  |FINSI;

  |SI #99#3 = 5;
      |SI #99#7 = 1;  |HAZ Punto15;  |FINSI;
  |FINSI;
|FPRC;

|DEFBUCLE eosw0053;
  #99#1;
  ;
  1, 00000, 1, 00;
  9, 99999, 9, 99;
  be;
  NULL, AlRollo;
|FIN;

|| *************************************
|| Procesos Carga del Temporal
|| *************************************

|PROCESO Mira349;
  |SI #4#4 = "E";  |ACABA;  |FINSI;
  |SI #4#4 = "R";  |ACABA;  |FINSI;

  |SI #4#8 ! 0;
      |SI #4#16 ! 0; |O #4#19 ! 0; |O #4#22 ! 0; |O #4#25 ! 0;
          SwHay = 1;
          |ERROR10;
          |ACABA;
      |FINSI;
  |FINSI;
|FPRC;

|DEFBUCLE eos349li;
  #4#1;
  ;
  #1#0, #1#1, #1#2, 000;
  #1#0, #1#1, #1#2, 999;
  ;
  NULL, Mira349;
|FIN;

|PROCESO Actividad;
  |ABRE #5;
  #5#0 = #99#0;
  #5#1 = #99#1;
  #5#2 = 97;
  |LEE 040#5=;
  |SI FSalida ! 0;  |DDEFECTO #5;  |FINSI;
  |CIERRA #5;

  #99#4 = #5#3;                                     || Nombre
  #99#5 = #5#4 + " " + #5#5 + " " + #5#6;           || Actividad Nombre
|FPRC;


|PROCESO GrabaTempoP;
  |SI #1#88 ! 0; |O #1#90 ! 0;
      SwHay = 0;
      |BUCLE eos349li;
      |SI SwHay = 1;  |ACABA;  |FINSI;

      #99#0 = #1#0;                                   || Empresa
      #99#1 = #1#83;                                  || Actividad
      |HAZ Actividad;
      #99#2 = #1#2;                                   || Periodo
      #99#3 = 1;                                      || Concepto
      #99#6 = "Adq.Intracomunitarias en Planillas";   || Concepto
      #99#7 = 0;                                      || Arreglo
      |GRABA 140#99n;
      |LIBERA #99;
  |FINSI;
|FPRC;

|DEFBUCLE eosplan1;
  #1#1;
  ;
  00000, 0, 97, 1;
  99999, 9, 97, 4;
  ;
  NULL, GrabaTempoP;
|FIN;

|PROCESO GrabaTempo300;
  |SI #2#49 ! 0; |O #2#50 ! 0;
      Suma = #2#58 + #2#61 + #2#64 + #2#67;
      |SI Suma = 0;
          #99#0 = #2#0;                                    || Empresa
          #99#1 = #2#45;                                   || Actividad
          |HAZ Actividad;
          #99#2 = #2#1;                                    || Periodo
          #99#3 = 2;                                       || Tipo
          #99#6 = "Adq.Intracomunitarias en Modelo 300";   || Concepto
          #99#7 = 0;                                       || Arreglo
          |GRABA 140#99n;
          |LIBERA #99;
      |FINSI;
  |FINSI;

  Suma = #2#21 + #2#22 + #2#23 + #2#24 + #2#43 + #2#44 + #2#51;
  |SI Suma ! 0;
      Suma = #2#70 + #2#71 + #2#72 + #2#73 + #2#74 + #2#75 + #2#76;
      |SI Suma = 0;
          #99#0 = #2#0;                                    || Empresa
          #99#1 = #2#45;                                   || Actividad
          |HAZ Actividad;
          #99#2 = #2#1;                                    || Periodo
          #99#3 = 4;                                       || Tipo
          #99#6 = "IVA Soportado en el Modelo 300     ";   || Concepto
          #99#7 = 0;                                       || Arreglo
          |GRABA 140#99n;
          |LIBERA #99;
      |FINSI;
  |FINSI;
|FPRC;

|DEFBUCLE eosm1300;
  #2#1;
  ;
  00000, 0, 1, 97;
  99999, 9, 4, 97;
  ;
  NULL, GrabaTempo300;
|FIN;

|PROCESO GrabaTempo330;
  |SI #3#25 ! 0; |O #3#26 ! 0;
      Suma = #3#69 + #3#72 + #3#75 + #3#78;
      |SI Suma = 0;
          #99#0 = #3#0;                                    || Empresa
          #99#1 = #3#1;                                    || Actividad
          |HAZ Actividad;
          #99#2 = #3#2;                                    || Periodo
          #99#3 = 3;                                       || Concepto
          #99#6 = "Adq.Intracomunitarias en Modelo 330";   || Concepto
          #99#7 = 0;                                       || Arreglo
          |GRABA 140#99n;
          |LIBERA #99;
      |FINSI;
  |FINSI;

  |SI #3#34 ! 0;
      |SI #3#81 = 0;
          #99#0 = #3#0;                                    || Empresa
          #99#1 = #3#1;                                    || Actividad
          |HAZ Actividad;
          #99#2 = #3#2;                                    || Periodo
          #99#3 = 5;                                       || Concepto
          #99#6 = "IVA Soportado en el Modelo 330     ";   || Concepto
          #99#7 = 0;                                       || Arreglo
          |GRABA 140#99n;
          |LIBERA #99;
      |FINSI;
  |FINSI;
|FPRC;

|DEFBUCLE eosma330;
  #3#1;
  ;
  00000, 0, 01, " ", 97;
  99999, 9, 12, "z", 97;
  ;
  NULL, GrabaTempo330;
|FIN;

|RUTINA ClaveBaja;
  #99#0 = 1;
  #99#1 = 1;
  #99#2 = 1;
  #99#3 = 1;
|FRUT;

|RUTINA ClaveAlta;
  #99#0 = 99999;
  #99#1 = 6;
  #99#2 = 12;
  #99#3 = 9;
|FRUT;

|PROCESO Tipo3;  |TIPO 3;
  |ABRE #99; |CIERRA #99;  |DELINDEX #99;

  |ABRE #99;
  |BUCLE eosplan1;
  |BUCLE eosm1300;
  |BUCLE eosma330;

  |LEE 040#99p;
  |SI FSalida ! 0;
     |CIERRA #99;  |DELINDEX #99;
     |BLANCO 1015 1465;
     |ATRI R;
     |PINTA 1015, "                                                   ";
     |PINTA 1115, "          NO HAY NADA PARA CAMBIAR                 ";
     |PINTA 1215, "       PUEDE REALIZAR EL PASE DEL 390              ";
     |PINTA 1315, "                                                   ";
     |PINTA 1415, "                                                   ";
     |PAUSA;
     |ACABA;
  |FINSI;
  |CIERRA #99;

  |ENTLINEAL #1#99, 2, 4, 20, 1, ClaveBaja, ClaveAlta;
  |CONFI "2400SDesea Realizar el Pase en este momento :";
  |SI FSalida ! 0;  |ACABA;  |FINSI;

  |BUCLE eosw0053;

  |ABRE #99; |CIERRA #99;  |DELINDEX #99;
|FPRC;
