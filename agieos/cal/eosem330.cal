|FICHEROS;
    eosem330 #0;
    eosma330 #1;
    eosclien #2;
    eospant2 #3;
    eoshacie #4;
    eosdaban #5;
|FIN;

|VARIABLES;
    LLeno40   = "zzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzz";
    Vacio40   = "                                        ";
    DEmpre    = 0;
    HEmpre    = 0;
    x         = 0;

    aAlfa     = ""; &eaCamino = "";  &enCmpPagina = 0;

    &dia      = "";
    &mes      = "";
    &anyo     = "";
    &sin      = "";

    &SwViene   = 0;
    &VEmpresa  = 0;
    &VAnyo     = 0;
    &VPeriodo  = 0;
    &VActiv    = 0;

    Calc = 0;   Calc1 = 0;  Calc2 = 0;
    TipoDato = "";  Long  = 0;  LongDec = 0;
    Comodin = "";  Comodin1 = "";  Comodin2 = "";
    Vacio1 = "";  Vacio2 = "";
    nConver = 0; nNeg = 0;
|FIN;

|INCLUYE i_seguim;
|INCLUYE i_cuadre;
|INCLUYE i_anyode;
|INCLUYE i_consul;
|INCLUYE i_laserc;

|PROCESO Mayus; |TIPO 0;
#0Campo = #0Campo > #0Campo;
|PINTA #0Campo;
|FPRC;

|PROCESO pinta_anyo; |TIPO 7;
|HAZ i_anyodef;
#0#25 = i_anyodef;
|PINTA #0#25;
|FPRC;

|PROCESO seleccion;
|SI #0#3 = "S";
    |PARA x = 4; |SI x [ 24; |HACIENDO x = x + 1;
          |C_M #0x, 1, "N";
          #0x = 0;
          |PINTA #0x;
    |SIGUE;
    |C_M #0#00, 1, "S";
    |C_M #0#01, 1, "S";
    |C_M #0#30, 1, "S";
|SINO;
    #0#0  = 00001; |PINTA #0#0;  |C_M #0#00, 1, "N";
    #0#1  = 99999; |PINTA #0#1;  |C_M #0#01, 1, "N";
    #0#30 = "C";   |PINTA #0#30; |C_M #0#30, 1, "N";

    |PARA x = 4; |SI x [ 24; |HACIENDO x = x + 1;
          |C_M #0x, 1, "S";
    |SIGUE;
|FINSI;
|FPRC;

|PROCESO MiraVarMoneda;  |TIPO 7;
     |HAZ PintaMoneda;
     #0#31 = eaMoneda;

     |SI #0#25 ] 80; |O #0#25 = 00;
         |SI #0#29 = "F"; #0#31 = "PESETAS"; |FINSI;
     |FINSI;
     |SI #0#25 = 1; |Y #0#26 [ 3;
         |SI #0#29 = "F"; #0#31 = "PESETAS"; |FINSI;
     |FINSI;

     |PINTA #0#31;
|FPRC;

|PROCESO impresio;
    |SI #0#30 = "C";
        #2#0 = #1#0;
        |LEE 040#2=;
        |SI FSalida ! 0; |DDEFECTO #2; |FINSI;
    |FINSI;

    #5#0 = #1#0;
    |LEE 040#5=;
    |SI FSalida ! 0; |DDEFECTO #5; |FINSI;

    #4#0 = #2#18;
    #4#1 = #2#19;
    |LEE 040#4=;
    |SI FSalida ! 0; |DDEFECTO #4; |FINSI;

    |SI #1#27 = 0; |Y #1#40 = 0; |Y #1#45 = 0; |Y #1#46 = 0; |Y #1#47 = 0;
     |Y #1#48 = 0; |Y #1#49 = 0; |Y #1#50 = 0;
        sin = "X";
    |SINO;
        sin = " ";
    |FINSI;

|ET arriba1;
    |SI #0#29 = "F";
       |HAZ SecInternet330;
    |SINO;
       |SI #0#29 = "O";
           |PINPA #0#3;
           #3#0 = #1#0;
           #3#1 = #2#2;
           #3#2 = "Coloque Impreso 330.      Pulse Tecla para Impresion";

          |PINTA #3#0;
          |PINTA #3#1;
          |ATRI P; |PINTA #3#2; |ATRI 0;
          |PAUSA;

          |INFOR "eosem330";
          |SI FSalida ! 0; |ACABA; |FINSI;
          |HAZ i_cuadre;
       |SINO;
          |INFOR "eoseb330";
          |SI FSalida ! 0; |ACABA; |FINSI;
       |FINSI;
    |FINSI;

    |SI #0#29 ! "F";
       |PRINT;
       |PIEINF;
       |FININF;
    |FINSI;

    |SI #0#29 ! "O"; |ACABA; |FINSI;

    |CONFI "2400NDesea imprimirlo de nuevo : ";
    |SI FSalida = 0; |VETE arriba1; |FINSI;

    |CONFI "2400NDesea imprimir EL SOBRE : ";
    |SI FSalida = 0;
        |ET arriba2;
            |INFOR "eosso330";
            |SI FSalida ! 0; |ACABA; |FINSI;
            |PINPA #0#3;
            #3#0 = #1#0;
            #3#1 = #2#2;
            #3#2 = "Coloque SOBRE 330.      Pulse Tecla para Impresion";

            |PINTA #3#0;
            |PINTA #3#1;
            |ATRI P; |PINTA #3#2; |ATRI 0;
            |PAUSA;

            |HAZ i_cuadre;
            |PRINT;
            |PIEINF;
            |FININF;
            |CONFI "2400NDesea imprimirlo de nuevo : ";
            |SI FSalida = 0; |VETE arriba2; |FINSI;
    |FINSI;

se_empre = #1#0;
se_ejerc = #1#4;
se_perio = #1#2;
se_tipos = 330;
se_activ = #1#1 + 9;
se_impor = #1#50;
se_fecha = #0#2;
se_factu = "N";
|HAZ seguimie;
|HAZ AlAgicli;

#1#5 = #0#2;
|GRABA 140#1a;
|LIBERA #1;
|FPRC;

|DEFBUCLE eosma330;
#1#1;
6;
DEmpre, #0#27, #0#26, "M", #0#25, "N";
HEmpre, #0#28, #0#26, "T", #0#25, "N";
be;
NULL, impresio;
|FIN;

|PROCESO AlModelo;
DEmpre = #2#0;
HEmpre = #2#0;
|BUCLE eosma330;
|FPRC;

|DEFBUCLE eosclienN;
#2#3;
0;
Vacio40, #0#0;
LLeno40, #0#1;
e;
NULL, AlModelo;
|FIN;

|PROCESO impre;  |TIPO 3;
   aTipoSec = "330";
|SI #0#29 ! "F";
   |IMPRE 0;
   |SI FSalida ! 0; |ACABA; |FINSI;
|FINSI;

|HAZ fechapre;

|ABRE #4;
|ABRE #5;
|SI #0#30 = "N";
    |BUCLE eosclienN;
|SINO;
    |ABRE #2;
    |SI #0#3 = "S";
        DEmpre = #0#0;
        HEmpre = #0#1;
        |BUCLE eosma330;
    |SINO;
        |PARA x = 4; |SI x [ 24; |HACIENDO x = x + 1;
              |SI #0x ! 0;
                  DEmpre = #0x;
                  HEmpre = #0x;
                  |BUCLE eosma330;
              |FINSI;
        |SIGUE;
    |FINSI;
    |CIERRA #2;
|FINSI;

|CIERRA #4;
|CIERRA #5;
|SI #0#29 ! "F"; |FINIMP; |FINSI;
|FPRC;

|PROCESO fechapre;
dia  = #0#2 % 102;
anyo = #0#2 % -102;
mes  = #0#2 % 402;
|SI mes = "01"; mes = "Enero     "; |FINSI;
|SI mes = "02"; mes = "Febrero   "; |FINSI;
|SI mes = "03"; mes = "Marzo     "; |FINSI;
|SI mes = "04"; mes = "Abril     "; |FINSI;
|SI mes = "05"; mes = "Mayo      "; |FINSI;
|SI mes = "06"; mes = "Junio     "; |FINSI;
|SI mes = "07"; mes = "Julio     "; |FINSI;
|SI mes = "08"; mes = "Agosto    "; |FINSI;
|SI mes = "09"; mes = "Septiembre"; |FINSI;
|SI mes = "10"; mes = "Octubre   "; |FINSI;
|SI mes = "11"; mes = "Noviembre "; |FINSI;
|SI mes = "12"; mes = "Diciembre "; |FINSI;
|FPRC;

|PROCESO Formateo;
   nNeg = 0;
   |SI TipoDato = "N";
      |SI nConver = 1;
          eaMoneda = #0#31;
          enCanti  = Comodin;
          |HAZ ConverMoneda;
          Comodin  = enCanti;
      |FINSI;

      Vacio1 = "0" * Long;
      Vacio2 = "0" * LongDec;
      Comodin = Comodin - "-";
      |SI FSalida ! 0; nNeg = 1; |FINSI;
   |SINO;
      Vacio1 = " " * Long;
   |FINSI;

   |SI LongDec = 0;
      Calc = 0 - (100 + Long);
      Comodin = (Vacio1 + Comodin) % Calc;
   |SINO;
      Comodin = Comodin - ".";
      |SI FSalida = 0;
         Calc = 0 - (100 + Long);
         Comodin = (Vacio1 + Comodin) % Calc;
         Comodin1 = "0" * LongDec;
      |SINO;
         Calc1 = (((FSalida / 100) ! 0) - 1) + 100;
         Calc2 = (((FSalida / 100) ! 0) * 100) + 99;
         Comodin1 = Comodin % Calc2;
         Comodin  = Comodin % Calc1;
         Calc = 0 - (100 + Long);
         Comodin  = (Vacio1 + Comodin)  % Calc;
         Calc = 100 + LongDec;
         Comodin1 = (Comodin1 + Vacio2) % Calc;
      |FINSI;
   |FINSI;
   |SI TipoDato = "N"; |Y nNeg = 1;
      Calc = 0 - (100 + (Long - 1));
      Comodin = "N" + (Comodin % Calc)
   |FINSI;
|FPRC;

|PROCESO SecInternet330;
     Raiz       = "";  |DIRECTORIO Raiz;
     Directorio = "";
     |DBASE Directorio;
     Directorio = Directorio + "hacienda/internet/330";
     |MKDIR Directorio;
     Directorio = Directorio + "/";
     Valfa = Raiz + "bin/agichmod 777 " + Directorio;

     Empresa  = ("00000" + #2#0) % -105;

     Fichero    = aTipoSec + Empresa;
     INEmpresas = NEmpresas1 <-;
     INEmpresas = INEmpresas + NACopiar;
     NFichero   = Directorio + Fichero;          |QBF NFichero;
     NEmpresas  = NFichero;
     NACopiar   = NACopiar + 1;

     Fichero  = "wb  "+ NFichero;

     Pos = -1;
     |FABRE Fichero;
     Handle = FSalida;
     |SI FSalida ] 0;
         Cadena = "330";
         |SI #0#31 = "PESETAS";
            Cadena = Cadena + "P";
         |SINO;
            Cadena = Cadena + "E";
         |FINSI;
         Cadena = Cadena + (#eosclien#1 % 0109);    ||NIF
         Cadena = Cadena + "    ";                  || Letras de etiqueta
         Comodin = ("00" + #eosem330#25) % -102; |QBF Comodin;
         |SI Comodin ] "80";
            Comodin = "19" + Comodin;
         |SINO;
            Comodin = "20" + Comodin;
         |FINSI;
         Cadena = Cadena + Comodin;                 || Ejercicio
         Comodin = ("00" + #eosem330#26) % -102;
         Cadena = Cadena + Comodin;                 || Periodo

         |SI #1#7  = 0;  |Y #1#9  = 0;   #1#8  = 0;  |FINSI;
         |SI #1#10 = 0;  |Y #1#12 = 0;   #1#11 = 0;  |FINSI;
         |SI #1#13 = 0;  |Y #1#15 = 0;   #1#14 = 0;  |FINSI;
         |SI #1#16 = 0;  |Y #1#18 = 0;   #1#17 = 0;  |FINSI;
         |SI #1#19 = 0;  |Y #1#21 = 0;   #1#20 = 0;  |FINSI;
         |SI #1#22 = 0;  |Y #1#24 = 0;   #1#23 = 0;  |FINSI;

         Long = 17; LongDec = 0; TipoDato = "N";  nConver = 1;
         Comodin = #eosma330#7; |HAZ Formateo;
         Cadena = Cadena + Comodin; || Base Imponible 1
         Long = 2; LongDec = 2;  nConver = 0;
         Comodin = #eosma330#8; |HAZ Formateo;
         Cadena = Cadena + Comodin;  || Tipo 1
         Cadena = Cadena + Comodin1; || Tipo 1
         Long = 17; LongDec = 0;  nConver = 1;
         Comodin = #eosma330#9; |HAZ Formateo;
         Cadena = Cadena + Comodin; || Cuota 1

         Comodin = #eosma330#10; |HAZ Formateo;
         Cadena = Cadena + Comodin; || Base Imponible 2
         Long = 2; LongDec = 2;  nConver = 0;
         Comodin = #eosma330#11; |HAZ Formateo;
         Cadena = Cadena + Comodin;  || Tipo 2
         Cadena = Cadena + Comodin1; || Tipo 2
         Long = 17; LongDec = 0; nConver = 1;
         Comodin = #eosma330#12; |HAZ Formateo;
         Cadena = Cadena + Comodin; || Cuota 2

         Comodin = #eosma330#13; |HAZ Formateo;
         Cadena = Cadena + Comodin; || Base Imponible 3
         Long = 2; LongDec = 2;  nConver = 0;
         Comodin = #eosma330#14; |HAZ Formateo;
         Cadena = Cadena + Comodin;  || Tipo 3
         Cadena = Cadena + Comodin1; || Tipo 3
         Long = 17; LongDec = 0;  nConver = 1;
         Comodin = #eosma330#15; |HAZ Formateo;
         Cadena = Cadena + Comodin; || Cuota 3

         Comodin = #eosma330#16; |HAZ Formateo;
         Cadena = Cadena + Comodin; || Base Imponible 4
         Long = 2; LongDec = 2; nConver = 0;
         Comodin = #eosma330#17; |HAZ Formateo;
         Cadena = Cadena + Comodin;  || Tipo 4
         Cadena = Cadena + Comodin1; || Tipo 4
         Long = 17; LongDec = 0;  nConver = 1;
         Comodin = #eosma330#18; |HAZ Formateo;
         Cadena = Cadena + Comodin; || Cuota 4

         Comodin = #eosma330#19; |HAZ Formateo;
         Cadena = Cadena + Comodin; || Base Imponible 5
         Long = 2; LongDec = 2;  nConver = 0;
         Comodin = #eosma330#20; |HAZ Formateo;
         Cadena = Cadena + Comodin;  || Tipo 5
         Cadena = Cadena + Comodin1; || Tipo 5
         Long = 17; LongDec = 0;  nConver = 1;
         Comodin = #eosma330#21; |HAZ Formateo;
         Cadena = Cadena + Comodin; || Cuota 5
         Cadena = Handle + " " + Cadena;
         |FGRABA Cadena;

         Comodin = #eosma330#22; |HAZ Formateo;
         Cadena = Comodin;           || Base Imponible 6
         Long = 2; LongDec = 2;  nConver = 0;
         Comodin = #eosma330#23; |HAZ Formateo;
         Cadena = Cadena + Comodin;  || Tipo 6
         Cadena = Cadena + Comodin1; || Tipo 6
         Long = 17; LongDec = 0;  nConver = 1;
         Comodin = #eosma330#24; |HAZ Formateo;
         Cadena = Cadena + Comodin; || Cuota 6

         Comodin = #eosma330#25; |HAZ Formateo;
         Cadena = Cadena + Comodin; || Base Imponible 7
         Comodin = #eosma330#26; |HAZ Formateo;
         Cadena = Cadena + Comodin; || Cuota 7

         Comodin = #eosma330#27;  |HAZ Formateo; Cadena = Cadena + Comodin; || Total Cuota devengada
         Calc = #eosma330#28 + #eosma330#36;
         Comodin = Calc;          |HAZ Formateo; Cadena = Cadena + Comodin; || Base Oper.Interiores
         Calc = #eosma330#29 + #eosma330#37;
         Comodin = Calc;          |HAZ Formateo; Cadena = Cadena + Comodin; || Cuota Oper.Interiores
         Calc = #eosma330#30 + #eosma330#38;
         Comodin = Calc;          |HAZ Formateo; Cadena = Cadena + Comodin; || Base Importaciones
         Calc = #eosma330#31 + #eosma330#39;
         Comodin = Calc;          |HAZ Formateo; Cadena = Cadena + Comodin; || Cuota Importaciones
         Comodin = #eosma330#32;  |HAZ Formateo; Cadena = Cadena + Comodin; || Base Adqu.intracomun.
         Comodin = #eosma330#33;  |HAZ Formateo; Cadena = Cadena + Comodin; || Cuota Adqu.intracomun.
         Cadena = Handle + " " + Cadena;
         |FGRABA Cadena;

         Comodin = #eosma330#34;  |HAZ Formateo; Cadena = Comodin;          || Cuota Compens.A.G.y P.
         Comodin = #eosma330#35;  |HAZ Formateo; Cadena = Cadena + Comodin; || Cuota Regul.Inversiones
         Comodin = #eosma330#40; |HAZ Formateo; Cadena = Cadena + Comodin;  || Total a deducir
         Comodin = #eosma330#41; |HAZ Formateo; Cadena = Cadena + Comodin;  || Diferencia Total
         Long = 3; LongDec = 2;  nConver = 0;
         Comodin = #eosma330#42; |HAZ Formateo;
         Cadena = Cadena + Comodin;                                   || % atribuible al estado
         Cadena = Cadena + Comodin1;
         Long = 17; LongDec = 0;  nConver = 1;
         Comodin = #eosma330#43; |HAZ Formateo; Cadena = Cadena + Comodin; || Cuota atrib.admon.estado
         Comodin = #eosma330#44; |HAZ Formateo; Cadena = Cadena + Comodin; || Cuotas a compensar per.ant.
         Comodin = #eosma330#45; |HAZ Formateo; Cadena = Cadena + Comodin; || Entregas intracomunitarias
         Comodin = #eosma330#46; |HAZ Formateo; Cadena = Cadena + Comodin; || Exportaciones y oper.asimiladas
         Comodin = #eosma330#47; |HAZ Formateo; Cadena = Cadena + Comodin; || Op.suj.originan el derecho a dev.
         Comodin = #eosma330#68; |HAZ Formateo; Cadena = Cadena + Comodin; || Op.no suj.originan derecho a dev.
         Comodin = #eosma330#48; |HAZ Formateo; Cadena = Cadena + Comodin; || Maximo a devolver
         Comodin = #eosma330#49; |HAZ Formateo; Cadena = Cadena + Comodin; || Resultado de la regul.anual
         Comodin = #eosma330#50; |HAZ Formateo; Cadena = Cadena + Comodin; || Resultado
         Cadena = Handle + " " + Cadena;
         |FGRABA Cadena;

         Comodin = #eosma330#51; |HAZ Formateo; Cadena = Comodin;          || A devolver
         Comodin = #eosma330#52; |HAZ Formateo; Cadena = Cadena + Comodin; || A compensar periodos poster.
         Cadena = Handle + " " + Cadena;
         |FGRABA Cadena;

         Cadena = " " * 100;                                           || Persona de contacto
         Cadena = Handle + " " + Cadena;
         |FGRABA Cadena;

         Cadena = " " * 210;                                           || Telefono de contacto y parte de observaciones
         Cadena = Handle + " " + Cadena;
         |FGRABA Cadena;

         Cadena = " " * 150;                                           || La otra parte de observaciones
         Cadena = Handle + " " + Cadena;
         |FGRABA Cadena;
     |FINSI;
     FSalida = Handle;
     |FCIERRA FSalida;

     |RMKDIR "/AEAT";
     |RMKDIR "/AEAT/INTERNET/";
     |RMKDIR "/AEAT/INTERNET/330/";
     aAlfa = ""; |IP_REMOTO aAlfa; |QBF aAlfa;
     |DBASE Comodin; |QBF Comodin;
     Comodin = Comodin + "hacienda/internet/330/";
     Comodin2 = Comodin;
     Comodin = Comodin + "*"
     |_PDIR Comodin;
     |PARA; |SI FSalida ] 0; |HACIENDO;
        Comodin1 = "/AEAT/INTERNET/330/" + (Comodin - Comodin2);
        |SI aAlfa ! "";
           |ENVIA_FICHERO Comodin, Comodin1;
        |SINO;
           |COPIA_FICHERO Comodin, Comodin1;
        |FINSI;
        |_SDIR Comodin;
     |SIGUE;

     aAlfa =  "      Ficheros Generados. Situados en \AEAT\INTERNET\330 de su PC";
     |MENAV aAlfa;

     enCmpPagina = 32; |HAZ PideExplorer;
     |SI eaCamino ! "ERROR";
        Comodin1 = eaCamino + " "+ #eosem330#32; |QBF Comodin1;
        |RSYSTEM Comodin1;
     |FINSI;
|FPRC;

|PROGRAMA;
   |SI SwViene = 1;
      #0#0 = VEmpresa;
      #0#1 = VEmpresa;
      #0#3 = "S";
      #0#25 = VAnyo;
      #0#26 = VPeriodo;
      #0#27 = VActiv;
      #0#28 = VActiv;
      #0#29 = "O";
   |SINO;
      |CLS;
      |CABEZA "Emision modelo 330";
      |PINPA #0#0;
      |PINDA #0#0;
      |ENDATOS #1#0;
      |SI FSalida ! 0; |ACABA; |FINSI;
   |FINSI;
|FPRO;
