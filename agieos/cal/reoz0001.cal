|FICHEROS;
     reoz0001 #0;        || Este def
     eosm0023 #1,MANTE;  || Amortizaciones Modulos Cabeceras
     eosm0024 #2,MANTE;  || Amortizaciones Modulos Lineas

     eosactiv #3;        || Actividades
     eosclien #4;        || Empresas
     eosconce #5;        || Concepto 2 Digitos
     eosconc6 #6;        || Concepto 6 Digitos

     eosamorc #90;       || Cabeceras Amortizacion
     eosamorl #91;       || Lineas    Amortizacion

     eosclpre #120;
|FIN;

|VARIABLES;
     nInkey        = 0;
     nSwSal        = 1;
     nModo         = 0;
     nTecla        = 0;
     nFactor       = 0;
     nContenido    = 0;
     nPorceMin     = 0;
     nPorceMax     = 0;
     nCampos       = 0;
     nForma        = 0;
     aAlfa         = "";
     fFecha        = @;
     nDAnyo        = 0;
     nHAnyo        = 0;

     &MEmpresa     = 0;
     &MActivi      = 0;

     &nAmort       = 0;
     &nCAnyo       = 0;
     &nVAnyo       = 0;
     &nPendiente   = 0;
     &nTCompra     = 0;
     &nPorceA      = 0;
     &nPorceD      = 0;
     &nImport      = 0;
     &nTipoMod     = 0;

     &fFechaUso    = @;
     &fFechaCompra = @;
     &fFechaVenta  = @;
     &fPriCompra   = @;
     &fFinCompra   = @;
     &fPriVenta    = @;
     &fFinVenta    = @;

     nSwGraba = 0;
     aAlfa1   = "";
     nNume1   = 0;

     aT600    = "";
     &nT600    = 0;
     &EURODB  = 0;

     &enEosEmp = 0;
     &enEosCPr = 0;
     &eaEosNIF = "";
     &enEosTip = 0;

|FIN;

|INCLUYE i_consul;

************************************************************************
   PROCESO DEL EOSM0024
************************************************************************

|PROCESO SalgoYa;  |TIPO 7;
     |SI nSwSal = 1;  |PTEC 807;  |FINSI;
|FPRC;

|PROCESO Importe;
     |SI #2#5 > #1#22;
         |MENAV "0000 El Importe supera al Pendiente de Amortizacion";
         |ERROR;
     |FINSI;
|FPRC;

|PROCESO Tipo2_2;  |TIPO 2;
     |SI #1#17 > "01.01.0000";
         |MENAV "          El Bien ya est  vendido.";
         |ERROR;
         |ACABA;
     |FINSI;

     #1#21 = #1#21 +. #2#5;  |PINTA #1#21;
     #1#22 = #1#22 -. #2#5;  |PINTA #1#22;
|FPRC;

************************************************************************
   PROCESO DEL EOSM0023
************************************************************************

|PROCESO RecalculaLineas;
     #1#22 = #1#22 - #2#5;
     #1#21 = #1#21 + #2#5;
|FPRC;

|DEFBUCLE eosm0024R;
     #2#1;
     ;
     #1#0, #1#1, #1#2, 0000;
     #1#0, #1#1, #1#2, 9999;
     ;
     NULL, RecalculaLineas;
|FIN;

|PROCESO Tipo11_1;  |TIPO 11;
     |SI FSalida ! 10;  |ACABA;  |FINSI;

     |LEE 010#1=;

     |SI #1#6 = "B";  #1#22 = #1#14;  |FINSI;
     |SI #1#6 = "F";  #1#22 = #1#16;  |FINSI;

     #1#21 = 0;

     |BUCLE eosm0024R;

     |SI #1#17 > "01.01.0000";
         #1#22 = 0;
     |FINSI;

     |GRABA 020#1a;
     |LIBERA #1;

     |PINTA #1#21;
     |PINTA #1#22;

     |ERROR;
|FPRC;

|PROCESO TModulo7;  |TIPO 7;
     |PUSHV 0505 1673;
     |D_CUADRO 0506, 1673;

     |ATRI R;
     |PINTA 0607, " -0- Inmovilizado material nuevos  100% mismo a¤o y menor 601,02  ";
     |PINTA 0707, " -1- Edificios y otras construcciones  (Del  2,5%  al  5%) 40 a¤os";
     |PINTA 0807, " -2- Utiles, herramientas, equipos.    (Del 20  %  al 40%)  5 a¤os";
     |PINTA 0907, " -3- Elementos de transportes y resto  (Del 12,5%  al 25%)  8 a¤os";
     |PINTA 1007, " -4- Inmovilizado inmaterial           (Del 10  %  al 15%) 10 a¤os";
     |PINTA 1107, " -----------------------------------------------------------------";
     |PINTA 1207, " -5- Vacuno, porcino, ovino y caprino  (Del 12,5%  al 22%)  8 a¤os";
     |PINTA 1307, " -6- Equino y frutales no citricos     (Del  5,88% al 10%) 17 a¤os";
     |PINTA 1407, " -7- Frutales citricos y vi¤edos       (Del  2,22% al  5%) 45 a¤os";
     |PINTA 1507, " -8- Olivar                            (Del  1,25% al  3%) 80 a¤os";

     |ATRI r;
|FPRC;

|PROCESO TModulo0;  |TIPO 0;
     |POPV;

     nContenido = Contenido;
     |SI nContenido ! #1Campo;
         #1#25 = 0;
         #1#7  = 0;
         #1#8  = 0;
     |FINSI;

     |C_M #1#7,  1, "S";
     |C_M #1#8,  1, "S";
     |C_M #1#25, 1, "S";
     |SI #1Campo = 5;
         |C_M #1#7,  1, "N";
         |C_M #1#8,  1, "N";
         |C_M #1#25, 1, "N";
     |FINSI;
     |SI #1#4 = 0;  |HAZ ImporteCompra; |FINSI;
|FPRC;

|PROCESO NuevoUsado;
     nFactor = 1;
     |SI #1Campo = "U";  nFactor = 2;  |FINSI;

     |SI #1#7 = 0;
         |SI #1#4 = 0;  #1#7 = 100;           |FINSI;
         |SI #1#4 = 1;  #1#7 = 5  * nFactor;  |FINSI;
         |SI #1#4 = 2;  #1#7 = 40 * nFactor;  |FINSI;
         |SI #1#4 = 3;  #1#7 = 25 * nFactor;  |FINSI;
         |SI #1#4 = 4;  #1#7 = 15 * nFactor;  |FINSI;
         |SI #1#4 = 5;  #1#7 = 22 * nFactor;  |FINSI;
         |SI #1#4 = 6;  #1#7 = 10 * nFactor;  |FINSI;
         |SI #1#4 = 7;  #1#7 = 5  * nFactor;  |FINSI;
         |SI #1#4 = 8;  #1#7 = 3  * nFactor;  |FINSI;
     |FINSI;

     |SI #1#8 = 0;
         |SI #1#4 = 0;  #1#8 = 100;           |FINSI;
         |SI #1#4 = 1;  #1#8 =  2.5  * nFactor;  |FINSI;
         |SI #1#4 = 2;  #1#8 = 20    * nFactor;  |FINSI;
         |SI #1#4 = 3;  #1#8 = 12.5  * nFactor;  |FINSI;
         |SI #1#4 = 4;  #1#8 = 10    * nFactor;  |FINSI;
         |SI #1#4 = 5;  #1#8 = 12.5  * nFactor;  |FINSI;
         |SI #1#4 = 6;  #1#8 =  5.88 * nFactor;  |FINSI;
         |SI #1#4 = 7;  #1#8 =  2.22 * nFactor;  |FINSI;
         |SI #1#4 = 8;  #1#8 =  1.25 * nFactor;  |FINSI;
    |FINSI;

    |SI #1#25 = 0; #1#25 = #1#8;  |FINSI;

    |PINTA #1#7;
    |PINTA #1#8;
    |PINTA #1#25;
|FPRC;

|PROCESO FiltroPorce;  |TIPO 0;
     |SI #1#5 = "N"; nFactor = 1;  |FINSI;
     |SI #1#5 = "S"; nFactor = 2;  |FINSI;

     |SI #1#4 = 0;
           nPorceMin = 100;
           nPorceMax = 100;
     |FINSI;

     |SI #1#4 = 1;
           nPorceMin = 2.5 * nFactor;
           nPorceMax = 5   * nFactor;
     |FINSI;

     |SI #1#4 = 2;
           nPorceMin = 20  * nFactor;
           nPorceMax = 40  * nFactor;
     |FINSI;

     |SI #1#4 = 3;
           nPorceMin = 12.5 * nFactor;
           nPorceMax = 25   * nFactor;
     |FINSI;

     |SI #1#4 = 4;
           nPorceMin = 10   * nFactor;
           nPorceMax = 15   * nFactor;
     |FINSI;

     |SI #1#4 = 5;
           nPorceMin = 12.5 * nFactor;
           nPorceMax = 22   * nFactor;
     |FINSI;

     |SI #1#4 = 6;
           nPorceMin = 5.88  * nFactor;
           nPorceMax = 10    * nFactor;
     |FINSI;

     |SI #1#4 = 7;
           nPorceMin = 2.22 * nFactor;
           nPorceMax = 5    * nFactor;
     |FINSI;

     |SI #1#4 = 8;
           nPorceMin = 1.25 * nFactor;
           nPorceMax = 3    * nFactor;
     |FINSI;

     |SI #1Campo < nPorceMin;  |O #1Campo > nPorceMax;
         |MENAV "        Porcentaje fuera de limites.";
         |ERROR;
     |FINSI;
|FPRC;

|PROCESO ConsuPro;
     nInkey = FSalida;
     |SI nInkey = 11;
         |HAZ OtraAltaPrCl;
         |ACABA;
     |FINSI;

     Campo10020 = #0#0;
     Campo10022 = 2;
     Campo10021 = #1Campo;
     nCampos    = Campo;
     |HAZ ConsuCliProv;
     Campo = nCampos;
     #1nCampos = Campo10021;  |PINTA #1nCampos;

     |ABRE #120;
     #120#0 = #0#0;
     #120#1 = #1nCampos;
     #120#2 = 2;
     |LEE 001#120=;
     |SI FSalida ! 0;
         |CIERRA #120;
         |CONFI "    SNo existe Proveedor, Desea darlo de alta ";
         |SI FSalida ! 0;
             #1Campo = 0; |PINTA #1Campo;
         |SINO;
             |HAZ AltaPrCl;
         |FINSI;
     |SINO;
         |CIERRA #120;
     |FINSI;
|FPRC;

|PROCESO LeeConcepto2;
     |SI #3#50 ! 1;  |ACABA;  |FINSI;

     |ABRE #5;
     #5#0 = #1#10;
     |LEE 040#5=;
     |SI FSalida ! 0;
         |MENAV  "    No existe concepto.";
         |ERROR;
     |SINO;
          |SI #5#3 ! "G";
              |SI #5#2 ! "S"; |Y #5#2 ! "I";
                  |CONFI "    NEl concepto no es de Compra ni de Inversion. Continuar ";
                  |SI FSalida ! 0;
                      |ERROR;
                  |FINSI;
              |FINSI;
          |FINSI;
     |FINSI;
     |CIERRA #3;
|FPRC;

|PROCESO LeeConcepto6;
     |SI #3#50 ! 2;  |ACABA;  |FINSI;

     |ABRE #6;
     #6#0 = #1#11;
     |LEE 040#6=;
     |SI FSalida ! 0;
         |MENAV  "    No existe concepto.";
         |ERROR;
     |SINO;
         |SI #6#3 ! "G";
             |CONFI "    NEl concepto no esta definido como Gasto. Continuar ";
             |SI FSalida ! 0;
                 |ERROR;
             |FINSI;
         |FINSI;
     |FINSI;
     |CIERRA #6;
|FPRC;

|PROCESO FInicio7;  |TIPO 7;
     |SI #1Campo = "01.01.1900";
         #1Campo = #1#12;
         |PINTA #1#12;
     |FINSI;
|FPRC;

|PROCESO FInicio0;  |TIPO 0;
     |SI #1#13 < #1#12;
         |MENAV "    La Fecha Inicio debe ser igual o mayor a la de Compra";
         |ERROR;
     |FINSI;

     |C_M #1#14, 1, "S";
     |C_M #1#15, 1, "S";
     |C_M #1#16, 1, "S";
     |C_M #1#6,  1, "S";

     |SI #1#21 ! 0;
         |C_M #1#14, 1, "N";
         |C_M #1#15, 1, "N";
         |C_M #1#16, 1, "N";
         |C_M #1#6,  1, "N";
     |FINSI;

     aAlfa = #1#12 % 0402;
     |SI aAlfa = "01";  #1#26 = 1;  |FINSI;
     |SI aAlfa = "02";  #1#26 = 1;  |FINSI;
     |SI aAlfa = "03";  #1#26 = 1;  |FINSI;
     |SI aAlfa = "04";  #1#26 = 2;  |FINSI;
     |SI aAlfa = "05";  #1#26 = 2;  |FINSI;
     |SI aAlfa = "06";  #1#26 = 2;  |FINSI;
     |SI aAlfa = "07";  #1#26 = 3;  |FINSI;
     |SI aAlfa = "08";  #1#26 = 3;  |FINSI;
     |SI aAlfa = "09";  #1#26 = 3;  |FINSI;
     |SI aAlfa = "10";  #1#26 = 4;  |FINSI;
     |SI aAlfa = "11";  #1#26 = 4;  |FINSI;
     |SI aAlfa = "12";  #1#26 = 4;  |FINSI;
     |PINTA #1#26;
|FPRC;

|PROCESO ImporteCompra;  |TIPO 0;
     nModo  = (FEntrada / 100) ! 0;
     |SI #1#14 > nT600;  |Y #1#4 = 0; |Y nModo = 1;
         aAlfa1 = "    NEl Importe debe ser Menor o igual a ";
         aAlfa1 = aAlfa1 + aT600;
         aAlfa1 = aAlfa1 + " Desea Continuar S/N ?";
         |CONFI aAlfa1;
         |SI FSalida ! 0;
             |ERROR;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO TCompra;  |TIPO 0;
     #1#16 = #1#14 > #1#15;

     |SI #1#6 = "B";  #1#22 = #1#14 - #1#21;  |FINSI;
     |SI #1#6 = "F";  #1#22 = #1#16 - #1#21;  |FINSI;

     |SI #1#17 > "01.01.0000";
         #1#22 = 0;
     |FINSI;

     |PINTA #1#16;
     |PINTA #1#22;
|FPRC;

|PROCESO Venta;  |TIPO 0;
     |SI #1Campo ! "01.01.0000"
         |C_M #1#18, 1, "S";
         |C_M #1#19, 1, "S";
     |SINO;
         |C_M #1#18, 1, "N";   #1#18 = 0;  |PINTA #1#18;
         |C_M #1#19, 1, "N";   #1#19 = 0;  |PINTA #1#19;
     |FINSI;
|FPRC;

|PROCESO TVenta;  |TIPO 7;
     #1#20 = #1#18 > #1#19;
     |PINTA #1#20;
|FPRC;

|PROCESO Mayus;
     #1Campo = #1Campo > #1Campo;  |PINTA #1Campo;
|FPRC;

|RUTINA ClaveBaja2;
     #2#0 = #1#0;
     #2#1 = #1#1;
     #2#2 = #1#2;
     #2#3 = 1900;
|FRUT;

|RUTINA ClaveAlta2;
     #2#0 = #1#0;
     #2#1 = #1#1;
     #2#2 = #1#2;
     #2#3 = 2999;
|FRUT;

|PROCESO PresentaLinea;  |TIPO 7;
     nSwSal = 1;
     |ENTLINEAL #1#2, -4, 4, 21, 0, ClaveBaja2, ClaveAlta2;
|FPRC;

|PROCESO GrabaEl2;
     nSwGraba = 0;
     #2#0 = #1#0;
     #2#1 = #1#1;
     #2#2 = #1#2;
     #2#3 = nAmort;

     |LEE 101#2=;
     |SI FSalida ! 0;
         #2#0 = #1#0;
         #2#1 = #1#1;
         #2#2 = #1#2;
         #2#3 = nAmort;
         #2#4 = "A¥O : " + nAmort;
         #2#5 = nImport;
         |GRABA 120#2n;
         nSwGraba = 1;
     |FINSI;
     |LIBERA #2;
|FPRC;

|| **********************************
|PROCESO VuelvoACal;
     #1#22 = #1#22 - #2#5;
     #1#21 = #1#21 + #2#5;
     #1#23 = #2#3;
|FPRC;

|DEFBUCLE VuelvoACal;
     #2#1;
     ;
     #1#0, #1#1, #1#2, 0000;
     #1#0, #1#1, #1#2, 9999;
     ;
     NULL, VuelvoACal;
|FIN;
|| ******************************

|PROCESO CalculaAmort;
     |SI #1#21 ! 0;  |ACABA;  |FINSI;    || SI ya ha amortizado algo

     #1#21    = 0;
     |SI #1#6 = "B";  #1#22 = #1#14;  |FINSI;
     |SI #1#6 = "F";  #1#22 = #1#16;  |FINSI;
     #1#23    = 0;
     |BUCLE VuelvoACal;

     |ABRE #2;

     fFechaUso   = #1#13;
     fFechaVenta = #1#17;
     nTCompra    = #1#16;
     |SI #1#6 = "B";  nTCompra = #1#14;  |FINSI;

     nPorceA     = #1#25;
     nPorceD     = #1#8;
     nTipoMod    = #1#4;

     aAlfa  = "31.12." + (fFechaUso % -104);     fFinCompra = aAlfa;
     aAlfa  = "01.01." + (fFechaUso % -104);     fPriCompra = aAlfa;

     aAlfa  = fFechaUso   % -104;                nCAnyo    = aAlfa;
     aAlfa  = fFechaVenta % -104;                nVAnyo    = aAlfa;
     aAlfa  = "01.01." + (fFechaVenta % -104);   fPriVenta = aAlfa;
     aAlfa  = "31.12." + (fFechaVenta % -104);   fFinVenta = aAlfa;
     |SI fPriVenta < fFechaUso;                  fPriVenta = fFechaUso;  |FINSI;

     aAlfa       = fFechaUso % -104;
     nDAnyo      = aAlfa;

     fFecha      = ~;
     aAlfa       = fFecha % -104;
     nHAnyo      = aAlfa;
     nHAnyo      = nHAnyo - 1;

     |PARA nAmort = nDAnyo;  |SI nAmort [ nHAnyo;  |HACIENDO nAmort = nAmort + 1;
           |SI nAmort > nVAnyo;  |Y nVAnyo ! 0;  |SAL;  |FINSI;
           |SI #1#22 = 0;                        |SAL;  |FINSI;
           nPendiente = #1#22;

           |HAZ Amortizamelo;  || Por favor

           |HAZ GrabaEl2;
           |SI nSwGraba = 1;
               |SI nAmort = nVAnyo;
                   #1#22 = 0;                |PINTA #1#22;
               |SINO;
                   #1#22 = #1#22 - nImport;  |PINTA #1#22;
               |FINSI;

               #1#21 = #1#21 + nImport;  |PINTA #1#21;
               #1#23 = nAmort;           |PINTA #1#23;
           |FINSI;
     |SIGUE;
     |CIERRA #2;
|FPRC;

|PROCESO Tipo3_1;  |TIPO 3;
     nSwSal = 0;
     |HAZ CalculaAmort;
     |ENTLINEAL #1#2, -4, 1, 21, 0, ClaveBaja2, ClaveAlta2;
|FPRC;

|PROCESO BorraLineas;
     |BORRA 140#2a;
     |LIBERA #2;
|FPRC;

|DEFBUCLE eosm0024;
     #2#1;
     ;
     #1#0, #1#1, #1#2, 0000;
     #1#0, #1#1, #1#2, 9999;
     be;
     NULL, BorraLineas;
|FIN;

|PROCESO Tipo2_1;  |TIPO 2;
     nModo  = (FEntrada / 100) ! 0;

     |SI nModo ! 3;  |ACABA;  |FINSI;

     |BUCLE eosm0024;
|FPRC;

************************************************************************
   PROCESO DEL REOZ0001
************************************************************************

|RUTINA ClaveBaja1;
     #1#0 = #0#0;
     #1#1 = #0#2;
     #1#2 = 00001;
|FRUT;

|RUTINA ClaveAlta1;
     #1#0 = #0#0;
     #1#1 = #0#2;
     #1#2 = 99999;
|FRUT;

|PROCESO Tipo3;  |TIPO 3;
     |SI #3#50 = 1;
         |C_V #1#11, 1, "N";  |C_M #1#11, 1, "N";
         |C_V #1#10, 1, "S";  |C_M #1#10, 1, "S";
     |SINO;
         |C_V #1#11, 1, "S";  |C_M #1#11, 1, "S";
         |C_V #1#10, 1, "N";  |C_M #1#10, 1, "N";
     |FINSI;

     |ENTLINEAL #1#1, -3, 2, 13, 0, ClaveBaja1, ClaveAlta1;
|FPRC;

|PROCESO Actividad;
     nTecla = FSalida;

     |ABRE #4;
     #4#0 = #0#0;
     |LEE 040#4=;
     |SI FSalida ! 0;  |DDEFECTO #4; |FINSI;
     |CIERRA #4;

     |ABRE #3;
     |SI nTecla = 9;
         |PUSHV 0501 2380;
         #3#0 = #0#0;
         #3#1 = #0#2;
         #3#2 = #4#20;
         |LEE 040#3];
         |PINPA #0#3;
         |PINDA #0#3;
         |CONSULTA #1#3;
         |POPV;

         #0#2 = #3#1;   |PINTA #0#2;
         #0#3 = #3#6;   |PINTA #0#3;
     |FINSI;

     #3#0 = #0#0;
     #3#1 = #0#2;
     #3#2 = #4#20;
     |LEE 040#3];
     |SI FSalida ! 0;
         |ERROR;
     |SINO;
         |SI #0#0 ! #3#0;
             |MENAV "        No existe Actividad";
             |ERROR;
         |SINO;
             #0#2 = #3#1;   |PINTA #0#2;
             #0#3 = #3#6;   |PINTA #0#3;
         |FINSI;
     |FINSI;
     |CIERRA #3;
|FPRC;

|PROCESO eosamorl;
     |LIBERA #91;
     |LEE 120#91=;

     |SI #91#6 = "C";
         #1#15 = #91#9;
         |BORRA 120#91a;
         |ACABA;
     |FINSI;

     |SI #91#11 = 0;  |ACABA;  |FINSI;

     #2#0 = #1#0;
     #2#1 = #1#1;
     #2#2 = #1#2;
     |SI #91#4 [ 80;
          #2#3 = 2000 + #91#4;
     |SINO;
          #2#3 = 1900 + #91#4;
     |FINSI;
     #2#4 = "A¥O : " + #2#3;
     #2#5 = #91#11;
     |GRABA 120#2n;
     |LIBERA #2;


     #1#22 = #1#22 - #2#5;
     #1#21 = #1#21 + #2#5;
     #1#23 = #2#3;

     |BORRA 120#91a;
|FPRC;

|PROCESO PasaAmortiza;
     |DDEFECTO #1;
     #1#0  = #90#0;
     #1#1  = #90#16;
     #1#2  = #90#2;
     #1#3  = #90#3;
     #1#4  = #90#18;  |SI #1#4 = 5;  #1#4 = 0;  |FINSI;
     #1#7  = #90#5;
     #1#8  = #90#5;
     #1#9  = #90#8;
     #1#10 = #90#9;
     #1#11 = #90#17;
     #1#12 = #90#21; || fecha compra
     #1#13 = #90#10; || fecha inicio
     #1#14 = #90#11;
     #1#22 = #1#14;
     #1#25 = #90#5;

     |SI #1#4 = 0;  #1#7 = 100;  #1#8 = 100;  #1#25 = 100; |FINSI;

     |SI #1#7 = 0;
         |SI #1#4 = 1;  #1#7 =  5;  #1#8 =  5;  #1#25 = 5;  |FINSI;
         |SI #1#4 = 2;  #1#7 = 40;  #1#8 = 40;  #1#25 = 40; |FINSI;
         |SI #1#4 = 3;  #1#7 = 25;  #1#8 = 25;  #1#25 = 25; |FINSI;
         |SI #1#4 = 4;  #1#7 = 15;  #1#8 = 15;  #1#25 = 15; |FINSI;
         |SI #1#4 = 5;  #1#7 = 22;  #1#8 = 22;  #1#25 = 22; |FINSI;
         |SI #1#4 = 6;  #1#7 = 10;  #1#8 = 10;  #1#25 = 10; |FINSI;
         |SI #1#4 = 7;  #1#7 =  5;  #1#8 =  5;  #1#25 =  5; |FINSI;
         |SI #1#4 = 8;  #1#7 =  3;  #1#8 =  3;  #1#25 =  3; |FINSI;
     |FINSI;

     |BUCLELIN 0 eosamorl #90;

     #1#16 = #1#14 > #1#15;

     |GRABA 120#1n;
     |LIBERA #1;

     |BORRA 120#90a;
     |LIBERA #90;
|FPRC;

|DEFBUCLE eosamorc;
     #90#1;
     18;
     00000, 00, 00000, 1;
     99999, 99, 99999, 5;
     be;
     NULL, PasaAmortiza;
|FIN;

|PROCESO PonTrimestre;
     aAlfa = #1#12 % 0402;

     |SI aAlfa = "01";  #1#26 = 1;  |FINSI;
     |SI aAlfa = "02";  #1#26 = 1;  |FINSI;
     |SI aAlfa = "03";  #1#26 = 1;  |FINSI;
     |SI aAlfa = "04";  #1#26 = 2;  |FINSI;
     |SI aAlfa = "05";  #1#26 = 2;  |FINSI;
     |SI aAlfa = "06";  #1#26 = 2;  |FINSI;
     |SI aAlfa = "07";  #1#26 = 3;  |FINSI;
     |SI aAlfa = "08";  #1#26 = 3;  |FINSI;
     |SI aAlfa = "09";  #1#26 = 3;  |FINSI;
     |SI aAlfa = "10";  #1#26 = 4;  |FINSI;
     |SI aAlfa = "11";  #1#26 = 4;  |FINSI;
     |SI aAlfa = "12";  #1#26 = 4;  |FINSI;
     |GRABA 020#1a;
     |LIBERA #1;
|FPRC;

|DEFBUCLE eosm0023;
     #1#1;
     ;
     00000, 1, 00000;
     99999, 9, 99999;
     be;
     NULL, PonTrimestre;
|FIN;

|PROGRAMA;
     nT600 = 100000;
     |SI EURODB = 2;
         nT600 = (nT600 / 166.386) ! EURODB;
     |FINSI;
     aT600 = nT600; |QBF aT600;

     |ABRE #1;
     |ABRE #2;
     |LEE 040#1p;
     |SI FSalida ! 0;
         |BUCLE eosamorc;
     |SINO;
         |SI #1#26 = 0;
             |BUCLE eosm0023;
         |FINSI;
     |FINSI;
     |CIERRA #1;
     |CIERRA #2;

     |SI MEmpresa ! 0;
         #0#0 = MEmpresa;
         #0#2 = MActivi;
         |PINPA #0#0;
         |PINDA #0#0;
         |HAZ Actividad;
         |HAZ Tipo3;
         |ACABA;
     |FINSI;

     |CLS;
     |CABEZA "Amortizaciones Modulos";
     |PARA; |SI; |HACIENDO;
            |DDEFECTO #0;
            |PINPA #0#0;
            |PINDA #0#0;
            |ENDATOS #1#0;
            |SI FSalida ! 0;  |SAL;  |FINSI;
     |SIGUE;
|FPRO;

|PROCESO AltaPrCl;
 enEosEmp = #0#0;
 enEosCPr = #1Campo;
 eaEosNIF = "";
 enEosTip = 2;
 |SUB_EJECUTA_NP "eosaltcp";
|FPRC;

|PROCESO OtraAltaPrCl;
  |ABRE #120;
  |SELECT #1#120;
  #120#0 = #0#0;
  #120#2 = 2;
  #120#1 = 99999;
  |LEE 010#120];
  |SI FSalida = 0;
      |LEE 040#120a;
  |SINO;
      |LEE 040#120u;
  |FINSI;
  enEosCPr = 0;
  |SI FSalida = 0; |Y #120#0 = #0#0; |Y #120#2 = 2;
      enEosCPr = #120#1;
  |FINSI;
  |CIERRA #120;
  enEosCPr = enEosCPr + 1;
  enEosEmp = #0#0;
  eaEosNIF = "";
  enEosTip = 2;
  |SUB_EJECUTA_NP "eosaltcp";
  #1#9 = enEosCPr; |PINTA #1#9;
|FPRC;
