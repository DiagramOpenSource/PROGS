|FICHEROS;
     eoszp006 #0;
     eosclien #1;
     eosactiv #2;
     eosepial #3;

     dsmom004 #4;
     dsmom008 #8;

     dsmociva #11;
     dsmociv@ #101;
|FIN;

|VARIABLES;
     aTipo     = "";
     nPer      = 0;
     nAny      = 0;
     CampoModi = 0;
     Fecha     = @;
     VarAlfa   = "";
     Total     = 0;
     DEmpre    = 0;
     HEmpre    = 0;
     nEjerO    = 0;
     nEjerD    = 0;
     nCampo    = 0;

     aBase     = "";
     aMas      = "";
     nOpe      = 0;
     nSw       = 0;
     aAlfa     = "";

     nPara1     = 0;
     aQueQuiero = "";
     aNumCampos = "";
     nNumCampos = 0;

     aParam     = "";
     &eaPeriodo = "";
     nActividad = 0;
|FIN;

|INCLUYE i_consul;
|INCLUYE i_seguim;
|INCLUYE i_variar;

|PROCESO RecogeAnyo; |TIPO 7;
     Fecha   = ~;
     VarAlfa = Fecha % -104;
     nAny    = VarAlfa;
     |SI nAny [ 2013; nAny = 2014; |FINSI;

     VarAlfa = nAny;
     VarAlfa = VarAlfa % -102;
     #0#2    = VarAlfa;   |PINTA #0#2;
     |HAZ AntesPeriodo;
     |SI #0#27 = "T";
         #0#0 = 3; |PINTA #0#3;
     |FINSI;
|FPRC;

|PROCESO AntesPeriodo; |TIPO 7;
   |SI #0#27 = "T";
       aAlfa = "0000Periodo Trimestral: 03, 06, 09, 12";
   |SINO;
       aAlfa = "0000Periodo Mensual: Del 01 al 12";
   |FINSI;
   |MENSA aAlfa;
|FPRC;

|PROCESO Periodo;
     |SI #0#27 = "T";
         |SI #0Campo ! 3; |Y #0Campo ! 6;
            |Y #0Campo ! 9; |Y #0Campo ! 12;
               |MENAV "0000Error! Informar un periodo Trimestral";
               |ERROR;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO CalculaDestino;
     |SI FSalida = 2; |ACABA; |FINSI;

     nAny = #0#2 + 2000;
     |SI nAny < 2013; |ERROR; |ACABA; |FINSI;

     |SI #0#0 ! 12;
         |SI #0#27 = "T"; #0#1 = #0#0 + 3; |FINSI;
         |SI #0#27 = "M"; #0#1 = #0#0 + 1; |FINSI;
         |PINTA #0#1;
         #0#3 = #0#2;     |PINTA #0#3;
     |SINO;
         |SI #0#27 = "T"; #0#1 = 3; |FINSI;
         |SI #0#27 = "M"; #0#1 = 1; |FINSI;
         |PINTA #0#1;
         #0#3 = #0#2 + 1; |PINTA #0#3;
     |FINSI;
|FPRC;

|PROCESO FiltraPeriodo;
     |SI #0#1 [ #0#0; |Y #0#3 = #0#2;
         |MENAV "     El Periodo / A¤o Destino es inferior o igual al Origen";
         |ERROR;
         |ACABA;
     |FINSI;

     |SI #0#3 < #0#2;
         |MENAV "     El A¤o Destino es inferior al Origen";
         |ERROR;
     |FINSI;
|FPRC;

|PROCESO ModificaEmpresas;
     |SI #0#4 = "S";
         |C_M #0#25,1,"N"; #0#25 = 00001; |PINTA #0#25;
         |C_M #0#26,1,"N"; #0#26 = 99999; |PINTA #0#26;

         |PARA CampoModi = 5; |SI CampoModi [ 24; |HACIENDO CampoModi = CampoModi + 1;
               |C_M #0CampoModi, 1, "S";
         |SIGUE;
     |SINO;
         |C_M #0#25,1,"S";
         |C_M #0#26,1,"S";
         |PARA CampoModi = 5; |SI CampoModi [ 24; |HACIENDO CampoModi = CampoModi + 1;
               |C_M #0CampoModi, 1, "N";
               #0CampoModi = 0; |PINTA #0CampoModi;
         |SIGUE;
     |FINSI;
|FPRC;

|CALCULO NoMasEmpresas; |TIPO 0;
     |SI #0Campo = 0;
         |PARA CampoModi = Campo + 1;  |SI CampoModi [ 23;  |HACIENDO CampoModi = CampoModi + 1;
               #0CampoModi = 0;  |C_M #0CampoModi, 1, "N";
         |SIGUE;
     |SINO;
         |PARA CampoModi = Campo + 1;  |SI CampoModi [ 23;  |HACIENDO CampoModi = CampoModi + 1;
               |C_M #0CampoModi, 1, "S";
         |SIGUE;
     |FINSI;
|FCAL;

|| *************************************************************************
|PROCESO CopiaModelo;
    #dsmociv@#0 = #dsmociva#0;
    #dsmociv@#1 = enEjer;
    #dsmociv@#2 = enPeriodo;
    #dsmociv@#3 = #dsmociva#3;
    #dsmociv@#4 = #dsmociva#4;
    #dsmociv@#5 = #dsmociva#5;
    #dsmociv@#6 = #dsmociva#6;
    #dsmociv@#7 = #dsmociva#7;
    |LEE 141#dsmociv@.=;
    |SI FSalida ! 0;
        |DDEFECTO #dsmociv@;
        #dsmociv@#0 = #dsmociva#0;
        #dsmociv@#1 = enEjer;
        #dsmociv@#2 = enPeriodo;
        #dsmociv@#3 = #dsmociva#3;
        #dsmociv@#4 = #dsmociva#4;
        #dsmociv@#5 = #dsmociva#5;
        #dsmociv@#6 = #dsmociva#6;
        #dsmociv@#7 = #dsmociva#7;
       |GRABA 020#dsmociv@.b;
    |FINSI;
    |PARA nPara1 = 8; |SI nPara1 [ nNumCampos; |HACIENDO nPara1 = nPara1 + 1;
          #dsmociv@#nPara1# = #dsmociva#nPara1#;
    |SIGUE;
    |GRABA 020#dsmociv@.a;
    |LIBERA #dsmociv@;
|FPRC;

|DEFBUCLE dsmociva;
     #dsmociva#1;
     ;
     enEmpresa, nEjerO, #0#0, enModelo, enComplem, enActividad, 0,  0;
     enEmpresa, nEjerO, #0#0, enModelo, enComplem, enActividad, 9, 99;
     e;
     NULL, CopiaModelo;
|FIN;

|PROCESO Graba 303;

    |ABRE #dsmociva;
    #dsmociva#0 = enEmpresa;
    #dsmociva#1 = enEjer;
    #dsmociva#2 = enPeriodo;
    #dsmociva#3 = enModelo;
    #dsmociva#4 = enComplem;
    #dsmociva#5 = enActividad;
    #dsmociva#6 = 0;
    #dsmociva#7 = 0;
    |LEE 040#dsmociva.];
    |SI FSalida = 0;  |Y #dsmociva#0 = enEmpresa;  |Y #dsmociva#1 = enEjer;
        |Y #dsmociva#2 = enPeriodo;  |Y #dsmociva#3 = enModelo;
           |Y #dsmociva#4 = enComplem;  |Y #dsmociva#5 = enActividad;
        |CIERRA #dsmociva;
        |ACABA;
    |FINSI;
    |CIERRA #dsmociva;

    enTipoEntrada = 90;  |SUB_EJECUTA "dsmociva";
    enTipoEntrada = 1;

    |ABRE #dsmociv@;
    |BUCLE dsmociva;
    |CIERRA #dsmociv@;

    enTipoEntrada = 95;  |SUB_EJECUTA "dsmociva";
    enTipoEntrada = 91;  |SUB_EJECUTA "dsmociva";

    #dsmom004#0 = enEmpresa;
    #dsmom004#1 = enEjer;
    #dsmom004#2 = enPeriodo;
    #dsmom004#3 = 303;
    #dsmom004#4 = 0;
    |LEE 101#dsmom004.=;
    |SI FSalida = 0;
        |HAZ ChequeaSituacion;

        |GRABA 020#dsmom004.a;
        |LIBERA #dsmom004;
    |FINSI;
|FPRC;

|PROCESO GrabaIVA;
     |SI aParam ! "";
         |SI nActividad ! #2#1;
             |ACABA;
         |FINSI;
     |FINSI;

     #dsmom008#0 = #dsmom004#0;
     #dsmom008#1 = #dsmom004#1;
     #dsmom008#2 = #dsmom004#2;
     #dsmom008#3 = #dsmom004#3;
     #dsmom008#4 = #dsmom004#4;
     #dsmom008#5 = #2#1;
     |LEE 041#dsmom008.=;
     |SI FSalida ! 0;
         |ACABA;
     |FINSI;

     |SI #2#22 = "A"; |ACABA; |FINSI;

     enEmpresa   = #2#0;
     enEjer      = nEjerD;
     enPeriodo   = #0#1;
     enComplem   = 0;
     enActividad = #2#1;

     |SI nOpe = 0;
         nSw = 1;
         |ERROR10;
     |FINSI;

     |SI nOpe = 1;
         |SI #0#28 = "S";
             enTipoEntrada = 3;   |SUB_EJECUTA "dsmociva";
         |FINSI;

         |HAZ Graba303;

     |FINSI;

     |LIBERA #2;
|FPRC;

|DEFBUCLE eosactiv;
     #2#2;
     ;
     #1#0, #3#0, #3#1, #0#2;
     #1#0, #3#0, #3#1, #0#2;
     be;
     NULL, GrabaIVA;
|FIN;

|PROCESO MiraEpigrafe;
    |BUCLE eosactiv;
|FPRC;

|DEFBUCLE eosepial;
    #3#1;
    ;
    " ", "    ";
    "z", "zzzz";
    ;
    NULL, MiraEpigrafe;
|FIN;

|PROCESO eosclien;
   |DDEFECTO #dsmom004;
   #dsmom004#0 = #1#0;
   #dsmom004#1 = nEjerD;
   #dsmom004#2 = #0#1;
   #dsmom004#3 = 303;
   #dsmom004#4 = 0;
   |LEE 041#dsmom004.=;
   |SI FSalida ! 0; |O #dsmom004#22 ! #0#27;
       |ACABA;
   |FINSI;

   nSw  = 0;
   nOpe = 0; |BUCLE eosepial;

   |SI nSw = 1;
       nSw  = 0;
       nOpe = 1; |BUCLE eosepial;
   |FINSI;

|FPRC;

|DEFBUCLE eosclien;
   #1#1;
   ;
   DEmpre;
   HEmpre;
   e;
   NULL, eosclien;
|FIN;

|PROCESO Tipo3; |TIPO 3;

     enModelo = 303;
     nEjerO = 2000 + #0#2;
     nEjerD = 2000 + #0#3;
     enEjer = nEjerD;

     aQueQuiero = "|NC";
     aNumCampos = #dsmociva#aQueQuiero#;
     nNumCampos = aNumCampos;
     nNumCampos = nNumCampos - 1;

     |ABRE #dsmom004;
     |SELECT #3#dsmom004;
     #dsmom004#1  = nEjerD;
     #dsmom004#2  = #0#1;
     #dsmom004#3  = 303;
     |LEE 000#dsmom004.];
     |SI FSalida ! 0;  |O  #dsmom004#1 ! nEjerD;
         |O  #dsmom004#2 ! #0#1; |O  #dsmom004#3 ! 303;

          |SELECT #1#dsmom004;
          |CIERRA #dsmom004;

       |CONFI "0000SBandeja del Periodo Destino vacia. Calcular datos";
       |SI FSalida ! 0;
           |ACABA;
       |FINSI;
       enEjer    = nEjerD;
       enPeriodo = #0#1;
       |SUB_EJECUTA "dsmom004;303PLANING";

     |SINO;

       |SELECT #1#dsmom004;
       |CIERRA #dsmom004;

     |FINSI;

     |DBASE aBase;  |QBF aBase;
     aMas = aBase + "def/dsmociva.mas";
     |CARGA_DEF aMas, dsmociv@;
     |SI FSalida ! 0;
         |MENAV "    Error al Cargar el fichero dsmociva";
         |ACABA;
     |FINSI;

     |ABRET #dsmom004;
     |ABRET #8;
     |SI #0#4 = "N";
         DEmpre = #0#25;
         HEmpre = #0#26;
         |BUCLE eosclien;
     |SINO;
         |PARA CampoModi = 5; |SI CampoModi [ 24; |HACIENDO CampoModi = CampoModi + 1;
               |SI #0CampoModi ! 0;
                   DEmpre = #0CampoModi;
                   HEmpre = #0CampoModi;
                   |BUCLE eosclien;
               |FINSI;
         |SIGUE;
     |FINSI;
     |CIERRAT #8;
     |CIERRAT #dsmom004;

     |DESCARGA_DEF #dsmociv@;
|FPRC;


|PROGRAMA;
     aParam = PARAMETRO; |QBF aParam;

     |SI aParam = "AUTO";
         |DDEFECTO #0;
         nActividad = enActividad;
         #0#1  = enPeriodo;
         #0#3  = enEjer - 2000;
         #0#2  = #0#3;
         #0#25 = enEmpresa;
         #0#26 = enEmpresa;
         #0#27 = eaPeriodo;
         #0#28 = "S";
         |SI #0#27 = "M";  #0#0 = #0#1 - 1; |FINSI;
         |SI #0#27 = "T";  #0#0 = #0#1 - 3; |FINSI;
         |SI #0#0 [ 0;
             #0#0 = 12;
             #0#2 = #0#3 - 1;
         |FINSI;
         |HAZ Tipo3;
         enActividad = nActividad;
         |ACABA;
     |FINSI;

     |MANTE #0;
|FPRO;
