|FICHEROS;
   eoscabco #0;
   eoslinco #1;
   eoscabve #2;
   eoslinve #3;
   eosamorc #4;
   eosamorl #5;
   eosm0023 #6;
   eosm0024 #7;
|FIN;

|VARIABLES;
   &empresa = 0;
   &d_anyo  = 0;
   &h_anyo  = 0;
   &sw_anyo = 0;

   fichero  = "";
   anyo     = "";
   fich     = "";
   borra    = "";
   systema  = "",
   borrar   = "";
   dire     = "";

   aAlfa1   = "";
   nDesde   = 0;
   nHasta   = 0;
   nAny     = 0;
|FIN;

|PROGRAMA;
   |HAZ base;
   |HAZ fichero0;
   |HAZ fichero2;
   |HAZ fichero4;
   |HAZ fichero6;
|FPRO;

|PROCESO null;
|FPRC;

|| **************************************************************************
||                         Borrado Apuntes Compras
|| **************************************************************************
|PROCESO prepara0;
fichero = #0#0;
fichero = "00000" + fichero;
fichero = fichero % -105;
anyo    = #0#4;
anyo    = "00" + anyo;
anyo    = anyo % -102;
fichero = "c" + fichero + anyo;

|DFICE fich;
borrar = fich + fichero + ".dat"; |FBORRA borrar;
borrar = fich + fichero + ".ddx"; |FBORRA borrar;
borrar = fich + fichero + ".ixx"; |FBORRA borrar;

|BUCLELIN 1null#0;

|BORRA 020#0a;
|LIBERA #0;
|FPRC;

|DEFBUCLE cabcom;
#0#1;
;
empresa, d_anyo;
empresa, h_anyo;
be;
NULL, prepara0;
|FIN;

|PROCESO fichero0;
|BUCLE cabcom;
|FPRC;

|| **************************************************************************
||                         Borrado Apuntes Ventas
|| **************************************************************************
|PROCESO prepara1;
fichero = #2#0;
fichero = "00000" + fichero;
fichero = fichero % -105;
anyo    = #2#4;
anyo    = "00" + anyo;
anyo    = anyo % -102;
fichero = "v" + fichero + anyo;
|NOME_DAT #3fichero;

|DFICE fich;
borrar = fich + fichero + ".dat"; |FBORRA borrar;
borrar = fich + fichero + ".ddx"; |FBORRA borrar;
borrar = fich + fichero + ".ixx"; |FBORRA borrar;

|BORRA 020#2a;
|LIBERA #2;
|FPRC;

|DEFBUCLE cabven;
#2#1;
;
empresa, d_anyo;
empresa, h_anyo;
be;
NULL, prepara1;
|FIN;

|PROCESO fichero2;
|BUCLE cabven;
|FPRC;

|| **************************************************************************
||                         Borrado Amortizaciones
|| **************************************************************************

|PROCESO prepara2;
|BUCLELIN 1null#4;

|BORRA 020#4a;
|LIBERA #4;
|FPRC;

|DEFBUCLE amortiza;
#4#1;
;
empresa, d_anyo, 00001;
empresa, h_anyo, 99999;
be;
NULL, prepara2;
|FIN;

|PROCESO fichero4;
  |BUCLE amortiza;
|FPRC;


|PROCESO Borra07;
     |BORRA 120#7a;
     |LIBERA #7;
|FPRC;

|DEFBUCLE eosm0024;
     #7#1;
     ;
     #6#0, #6#1, #6#2, 0000;
     #6#0, #6#1, #6#2, 9999;
     be;
     NULL, Borra07;
|FIN;

|DEFBUCLE eosm0024_s;
     #7#1;
     ;
     #6#0, #6#1, #6#2, nDesde;
     #6#0, #6#1, #6#2, nHasta;
     be;
     NULL, Borra07;
|FIN;

|PROCESO Borra06;
     aAlfa1 = #6#12;
     aAlfa1 = aAlfa1 % -104;
     nAny = aAlfa1;
     |SI nAny ] nDesde; |Y nAny [ nHasta;
        |BUCLE eosm0024;
        |BORRA 120#6a;
     |SINO;
        |BUCLE eosm0024_s;
     |FINSI;
     |LIBERA #6;
|FPRC;

|DEFBUCLE eosm0023;
     #6#1;
     ;
     empresa, 1, 00001;
     empresa, 9, 99999;
     be;
     NULL, Borra06;
|FIN;

|PROCESO fichero6;
     |SI sw_anyo = 0;
         nDesde = 0000;
         nHasta = 9999;
     |SINO;
         nDesde = 2000 + d_anyo;
         |SI d_anyo  > 80;
             nDesde = 1900 + d_anyo;
         |FINSI;
         nHasta = 2000 + h_anyo;
         |SI h_anyo  > 80;
             nHasta = 1900 + h_anyo;
         |FINSI;
     |FINSI;
     |BUCLE eosm0023;
|FPRC;

|PROCESO base;
     |DFICE fich;
     |QUE_SISTEMA systema;
     |SI systema = "ESDOS";
         borra = "del ";
     |SINO;
         borra = "rm ";
     |FINSI;
|FPRC;
