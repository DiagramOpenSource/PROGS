|FICHEROS;
     eosy0007 #0;        || Este Def
     eosm0015 #1;        || Mantenimiento 309
     eosclien #2;        || Clientes
     eospant2 #3;
     eoshacie #4;        || Hacienda
     eosdaban #5;        || Bancos
|FIN;

|VARIABLES;
     XPara   = 0;
     LLeno40 = "zzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzz";
     Vacio40 = "                                        ";
     DEmpre  = 0;
     HEmpre  = 0;
     Mes     = "";
     informe = "";
|FIN;

|INCLUYE i_seguim;
|INCLUYE i_anyode;
|INCLUYE i_consul;

|PROCESO PintaAnyo; |TIPO 7;
  |HAZ i_anyodef;
  #0#25 = i_anyodef;   |PINTA #0#25;
|FPRC;

|PROCESO Mayus; |TIPO 0;
  #0Campo = #0Campo > #0Campo;
  |PINTA #0Campo;
|FPRC;

|PROCESO Seleccion;
  |SI #0#3 = "S";
      |PARA XPara = 4; |SI XPara [ 24; |HACIENDO XPara = XPara + 1;
            |C_M #0XPara, 1, "N";
            #0XPara = 0;
            |PINTA #0XPara;
      |SIGUE;
      |C_M #0#00, 1, "S";
      |C_M #0#01, 1, "S";
      |C_M #0#28, 1, "S";
  |SINO;
      #0#0  = 00001; |PINTA #0#0;  |C_M #0#00, 1, "N";
      #0#1  = 99999; |PINTA #0#1;  |C_M #0#01, 1, "N";
      #0#28 = "C";   |PINTA #0#28; |C_M #0#28, 1, "N";

      |PARA XPara = 4; |SI XPara [ 24; |HACIENDO XPara = XPara + 1;
            |C_M #0XPara, 1, "S";
      |SIGUE;
  |FINSI;
|FPRC;

|PROCESO Imprime;
  |SI #0#28 = "C";
      #2#0 = #1#0;
      |LEE 040#2=;
      |SI FSalida ! 0; |DDEFECTO #2; |FINSI;
  |FINSI;

  #5#0 = #1#0;
  |LEE 040#5=;
  |SI FSalida ! 0; |DDEFECTO #5; |FINSI;

  #4#0 = #2#18;
  #4#1 = #2#19;
  |LEE 040#4=;
  |SI FSalida ! 0; |DDEFECTO #4; |FINSI;

  |ET OtraVez;
      |SI #0#27 = "O";
          |PINPA #0#3;
          #3#0 = #1#0;
          #3#1 = #2#2;
          #3#2 = "Coloque Impreso 309.      Pulse Tecla para Impresion";

          |PINTA #3#0;
          |PINTA #3#1;
          |ATRI P; |PINTA #3#2; |ATRI 0;
          |PAUSA;
      |FINSI;
      |PRINT;
      |PIEINF;

      |SI #0#27 ! "O"; |ACABA; |FINSI;

      |CONFI "2400NDesea imprimirlo de nuevo : ";
      |SI FSalida = 0;  |VETE OtraVez; |FINSI;

  se_empre = #1#0;
  se_ejerc = #1#2;
  se_perio = #1#3;
  se_tipos = 309;
  se_activ = #1#1 + 9;
  se_impor = #1#50;
  se_fecha = #0#2;
  se_factu = "N";
  |HAZ seguimie;
  |HAZ AlAgicli;

  #1#4 = #0#2;
  |GRABA 140#1a;
  |LIBERA #1;
|FPRC;

|DEFBUCLE eosm0015;
  #1#1;
  5;
  DEmpre, 000, #0#25, #0#26, #0#33, "N";
  HEmpre, 999, #0#25, #0#26, #0#33, "N";
  be;
  NULL, Imprime;
|FIN;

|PROCESO AlModelo;
  DEmpre = #2#0;
  HEmpre = #2#0;
  |BUCLE eosm0015;
|FPRC;

|DEFBUCLE eosclien;
  #2#3;
  0;
  Vacio40, #0#0;
  LLeno40, #0#1;
  ;
  NULL, AlModelo;
|FIN;

|PROCESO Fecha;
  Mes = #0#2 % 402;

  |SI Mes = "01"; #0#30 = "ENERO     "; |FINSI;
  |SI Mes = "02"; #0#30 = "FEBRERO   "; |FINSI;
  |SI Mes = "03"; #0#30 = "MARZO     "; |FINSI;
  |SI Mes = "04"; #0#30 = "ABRIL     "; |FINSI;
  |SI Mes = "05"; #0#30 = "MAYO      "; |FINSI;
  |SI Mes = "06"; #0#30 = "JUNIO     "; |FINSI;
  |SI Mes = "07"; #0#30 = "JULIO     "; |FINSI;
  |SI Mes = "08"; #0#30 = "AGOSTO    "; |FINSI;
  |SI Mes = "09"; #0#30 = "SEPTIEMBRE"; |FINSI;
  |SI Mes = "10"; #0#30 = "OCTUBRE   "; |FINSI;
  |SI Mes = "11"; #0#30 = "NOVIEMBRE "; |FINSI;
  |SI Mes = "12"; #0#30 = "DICIEMBRE "; |FINSI;

  #0#29 = #0#2 %  102;
  #0#31 = #0#2 % -102;
|FPRC;

|PROCESO Tipo3;  |TIPO 3;
  |HAZ Fecha;
  |IMPRE 0;
  |SI FSalida ! 0;  |ACABA;  |FINSI;

  informe = "eos18309";
  |SI #0#25 ] 2; |Y #0#25 < 18;
     |SI #0#27 = "O";  informe = "eos02309";  |FINSI;
     |SI #0#27 = "B";  informe = "eos12309";  |FINSI;
  |FINSI;

  |INFOR informe;
  |SI FSalida ! 0;  |FINIMP;  |ACABA;   |FINSI;

  |ABRE #4;
  |ABRE #5;
  |SI #0#27 = "N";
      |BUCLE eosclien;
  |SINO;
      |ABRE #2;
      |SI #0#3 = "S";
          DEmpre  = #0#0;
          HEmpre  = #0#1;
          |BUCLE eosm0015;
      |SINO;
          |PARA XPara = 4; |SI XPara [ 24; |HACIENDO XPara = XPara + 1;
                |SI #0XPara ! 0;
                    DEmpre  = #0XPara;
                    HEmpre  = #0XPara;
                    |BUCLE eosm0015;
                |FINSI;
          |SIGUE;
      |FINSI;
      |CIERRA #2;
  |FINSI;

  |FININF;
  |FINIMP;
|FPRC;
