|FICHEROS;
    eosm3130 #0;
|FIN;

|VARIABLES;
    empresa   = 0;
    activid   = 0;
    periodo   = 0;
    ejercic   = 0;
    sw_esta   = 0;
    modo      = 0;
    tecla     = "";
    f1        = "823";
    xxx       = 0;
    aAlfa1    = "";
    aAlfa2    = "";
    aAlfa3    = "";
    nPanta    = 0;
    nEjer     = 0;
|FINSI;

|DDEFECTO #0;
|FIN;

|INCLUYE i_seguim;

|PROCESO alta; |TIPO 7;
   modo = (FEntrada / 100) ! 0;
   |SI modo = 1;
       |MENAV "     No se puede dar de ALTA, use los programas CORRECTOS.";
       |PTEC 807;
   |FINSI;
|FPRC;
|PROCESO AntesGastos; |TIPO 7;
    |SI #0#3 ] 15;
        |MENSA "0000Recuerde los Gastos no pueden superar los 2.000 euros en el Ejercicio";
    |FINSI;
|FPRC;

|PROCESO TotalGastos; |TIPO 0;
    |SI #0#21 ] 2000; |Y #0#3 ] 15;
        #0#21 = 2000;
    |FINSI;
|FPRC;

|PROCESO i_archiv; |TIPO 7;
   |SI #0#5 = "S";
       |MENAV "     Registro archivado, DESARCHIVELO";
       |PTEC 807;
   |FINSI;
|FPRC;

|PROCESO cuadro; |TIPO 7;
   |PUSHV 0501 2380;

   |CUADRO 908 1767;;
   |ATRI R;
   |PINTA 1009, "   Esta usted modificando el mantenimiento de emision,    ";
   |PINTA 1109, " pulse <F12> si desea entrar en todos los campos del      ";
   |PINTA 1209, " fichero, cualquier otra tecla solo Total Liquidacion.    ";
   |PINTA 1309, "   El programa no le efectuara ningun calculo, toda la    ";
   |PINTA 1409, " introduccion se hara manualmente.                        ";
   |PINTA 1509, "   Si modificaramos de nuevo la ficha en el mantenimiento ";
   |PINTA 1609, " del trabajo volveria a recalcular la ficha.              ";
   |ATRI r;

   f1 = &255 + "823";
   |LEETECLA tecla

    |SI #0#3 < 80;
        nEjer = 2000 + #0#3;
    |SINO;
        nEjer = 1900 + #0#3;
    |FINSI;
   |SI tecla = f1;
       |PARA xxx = 6; |SI xxx [ 37; |HACIENDO xxx = xxx + 1;
             |CAMPO_MODIFICA #0xxx , 1, "S";
       |SIGUE;
       aAlfa1 = "N";
       |SI nEjer ] 2010; |Y nEjer [ 2015; aAlfa1 = "S"; |FINSI;
       |CAMPO_MODIFICA #0#48, 1, aAlfa1;
       |CAMPO_MODIFICA #0#49, 1, aAlfa1;
   |SINO;
       |PARA xxx = 6; |SI xxx [ 37; |HACIENDO xxx = xxx + 1;
             |CAMPO_MODIFICA #0xxx , 1, "N";
       |SIGUE;
       |CAMPO_MODIFICA #0#48, 1, "N";
       |CAMPO_MODIFICA #0#49, 1, "N";
   |FINSI;
   |CAMPO_MODIFICA #0#29, 1, "S";
   |CAMPO_MODIFICA #0#32, 1, "N";
   |CAMPO_MODIFICA #0#33, 1, "N";
   |CAMPO_MODIFICA #0#34, 1, "N";
   |CAMPO_MODIFICA #0#35, 1, "N";

   |POPV;
|FPRC;

|PROCESO LaReduccion;
 |C_M #0#48,0, aAlfa1;
 |SI aAlfa1 = "N"; |ACABA; |FINSI;

 aAlfa1 = #0#48;  |C_M #0#49,1,aAlfa1;
 |SI #0#48 = "N"; #0#49 = 0; |PINTA #0#49;  |FINSI;
|FPRC;

|PROCESO Crea2015_3;
   |C_M #0#49, 0, aAlfa1;
   |SI aAlfa1 = "N"; |ACABA; |FINSI;

   |SI FSalida = 2; |ACABA; |FINSI;

   |SI #0#3 = 15;
       |SI #0#49 ! 0;
           aAlfa1 = "0000SImporte en Reduc.creacion empleo y ejercicio 2015 incompatibles. Modifiquela. ";
           |CONFI aAlfa1;
           |SI FSalida = 0;
               |ERROR;
           |FINSI;
       |SINO;
            #0#48 = "N"; |PINTA #0#48;
       |FINSI;
   |FINSI;
|FPRC;

|PROCESO Tipo3; |TIPO 3;
      |SI #0#3 = 15;
          |SI #0#49 ! 0;
              |MENAV "0000Hay Importe en la Reduccion por creacion de empleo, no es correcto para Ejer. 2015.";
          |SINO;
              #0#48 = "N";
          |FINSI;
      |FINSI;
|FPRC;

|PROCESO esta_super; |TIPO 2;
|SI #0#2 = 4; |ACABA; |FINSI;
|GRABA 140#0a;
|LIBERA #0;

sw_esta   = 0;
empresa   = #0#0;
activid   = #0#1;
periodo   = #0#2;
ejercic   = #0#3;

#0#2 = #0#2 + 1;
|LEE 040#0=;
|SI FSalida = 0; sw_esta = 1; |FINSI;

#0#0 = empresa;
#0#1 = activid;
#0#2 = periodo;
#0#3 = ejercic;
|LIBERA #0;
|LEE 140#0=;
|SI FSalida ! 0;
    |DDEFECTO #0;
    #0#0 = empresa;
    #0#1 = activid;
    #0#2 = periodo;
    #0#3 = ejercic;
|FINSI;

|SI sw_esta = 1;
    |MENAV "    Ya existe registro con un PERIODO superior";
    |ERROR;
|FINSI;
|FPRC;

|PROCESO Comple; |TIPO 0;
 aAlfa1 = "N"; |SI #0#38 = "S"; aAlfa1 = "S"; |FINSI;
 |C_M #0#39,1,aAlfa1;
 |C_M #0#40,1,aAlfa1;

 |SI #0#38 ! "S";
     #0#39 = ""; |PINTA #0#39;
     #0#40 = ""; |PINTA #0#40;
 |FINSI;
|FPRC;

|CALCULO Tipo13; |TIPO 13;
 modo  = (FEntrada / 100) ! 0;
 |SI modo < 1; |ACABA; |FINSI;

 |SI #0#3 < 80;
     nEjer = 2000 + #0#3;
 |SINO;
     nEjer = 1900 + #0#3;
 |FINSI;

 |SI nEjer [ 2007;  nPanta = 1; |FINSI;
 |SI nEjer ] 2008;
     nPanta = 2;
     |SI nEjer = 2008; |Y #0#2 = 1;
         nPanta = 1;
     |FINSI;
 |FINSI;
 |SI nPanta = 2;
     |C_V #0#41,1, "S"; |C_M #0#41,1,"S";
     |C_V #0#42,1, "S"; |C_M #0#42,1,"S";
     |C_V #0#43,1, "S"; |C_M #0#43,1,"S";
     |C_V #0#44,1, "S"; |C_M #0#44,1,"S";
     |C_V #0#47,1, "S"; |C_M #0#47,1,"S";
     aAlfa1 = "º Suma :           R.Neg:          º";
     aAlfa2 = "º Mino.:           Prest:          º";
     aAlfa3 = "º Dife.:           Total:          º";
 |SINO;
     |C_V #0#41,1, "N"; |C_M #0#41,1,"N";
     |C_V #0#42,1, "N"; |C_M #0#42,1,"N";
     |C_V #0#43,1, "N"; |C_M #0#43,1,"N";
     |C_V #0#44,1, "N"; |C_M #0#44,1,"N";
     |C_V #0#47,1, "N"; |C_M #0#47,1,"N";
     aAlfa1 = "º Suma :                           º";
     aAlfa2 = "º                                  º";
     aAlfa3 = "º                                  º";
 |FINSI;
 |PINTA 1945, aAlfa1;
 |PINTA 2045, aAlfa2;
 |PINTA 2145, aAlfa3;
|FCAL;

|CALCULO DesTrim; |TIPO 0;
 |HAZ Tipo13;
|FCAL;
