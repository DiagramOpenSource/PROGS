|FICHEROS;
     dsmom004;
     dsmom008;

     eoscabve;
     eoscabco;
     eosactiv;

     eosepial;
|FIN;

|VARIABLES;
     nEntrada      = 0;
     nTecla        = 0;
     nPasLib       = 0;
     nBtnMod8      = 0;
     nBtnBaj8      = 0;
     nBtnPas8      = 0;
     nBtnCop8      = 0;

     nSwCop8       = 0;
     &eaPeriodo    = "";
|FIN;

|INCLUYE i_variar;

|PROCESO PoderCopiar;

     nSwCop8 = 0;
     |SI #dsmom008#12 ! "303";
         nSwCop8 = 1;
     |SINO;
         |SI #dsmom008#1 = 2014;
             |SI #dsmom004#22 = "M"; |Y #dsmom008#2 = 1;
                 nSwCop8 = 1;
             |SINO;
                 |SI #dsmom004#22 = "T"; |Y #dsmom008#2 = 3;
                      nSwCop8 = 1;
                 |FINSI;
             |FINSI;
         |FINSI;
     |FINSI;

     |SI nSwCop8 = 0;
         |ABRE #eosactiv;
         |DDEFECTO #eosactiv;
         #eosactiv#0 = #dsmom008#0;
         #eosactiv#1 = #dsmom008#5;
         #eosactiv#2 = #dsmom008#1 - 2000;
         |LEE 041#eosactiv.=;

         |ABRE #eosepial;
         #eosepial#0 = #eosactiv#4;
         #eosepial#1 = #eosactiv#5;
         |LEE 041#eosepial.=;
         |SI FSalida ! 0;
             nSwCop8 = 1;
         |FINSI;
         |CIERRA #eosepial;
         |CIERRA #eosactiv;
     |FINSI;
|FPRC;

|PROCESO FinBotonm008;
     |FIN_CONTROL_WINDOWS nBtnMod8;
     |FIN_CONTROL_WINDOWS nBtnBaj8;
     |FIN_CONTROL_WINDOWS nBtnPas8;
     |FIN_CONTROL_WINDOWS nBtnCop8;

     |PULSATECLA;
|FPRC;

|PROCESO BajaModelo8;
     |SI #dsmom008#5   = 0;
         |SI #dsmom008#1 < 2017; |O #dsmom008#2 ! 12;
             |ACABA;
         |FINSI;
     |FINSI;

     |SI #dsmom004#11 = "X";
         |MENAV "     Modelo ya Remesado. No se puede dar de Baja";
         |ACABA;
     |FINSI;

     |SI #dsmom004#13 = "X";
         |MENAV "     Modelo ya Archivado. No se puede dar de Baja";
         |ACABA;
     |FINSI;

     |SI #dsmom004#9 = "X";
         |CONFI "0000NModelo ya impreso, Esta Seguro de la Baja : ";
         |SI FSalida ! 0;  |ACABA;  |FINSI;
     |SINO;
         |CONFI "0000NEsta seguro de dar de Baja el Modelo ";
         |SI FSalida ! 0;
             |ACABA;
         |FINSI;
     |FINSI;

     enModeloEntra = #dsmom008#12;
     eaModeloProg  = #dsmom008#7;   |QBF eaModeloProg;
     |HAZ EntraModelo;

     enTipoEntrada = 99;
     enExistencia = 0;
     |HAZ EntraModelo;
     |SI enExistencia = 1;
         |LEE 101#dsmom008.=;
         #dsmom008#11 = "SIN DATOS";
         |GRABA 020#dsmom008.a;
         |LIBERA #dsmom008;
     |FINSI;

     enTipoEntrada = 3;
|FPRC;

|PROCESO Tipo11m008;  |TIPO 11;
     nTecla = FSalida;

     |HAZ FinBotonm008;

     enEmpresa     = #dsmom008#0;
     enEjer        = #dsmom008#1;
     enPeriodo     = #dsmom008#2;
     enModelo      = #dsmom008#3;
     enComplem     = #dsmom008#4;
     enActividad   = #dsmom008#5;
     eaComunidad   = #dsmom008#9;
     eaComunero    = #dsmom008#10;
     eaCaptura     = #dsmom008#7;
     eaNombreAct   = #dsmom008#6;
     enModeloEntra = #dsmom008#12;
     eaModeloProg  = #dsmom008#7;   |QBF eaModeloProg;

|| \\\\\ Consulta Modelos ////////

     |SI nTecla = 10;
         enTipoEntrada = 1;
         |SI #dsmom008#5 = 0;
             enTipoEntrada = 4;
         |FINSI;

         |HAZ EntraModelo;
         |HAZ Refresco;
     |FINSI;

|| \\\\\ Pasar desde Libros ////////

     |SI nTecla = 15;
         |SI nPasLib = 0;  |ACABA;  |FINSI;

         |CONFI "0000NSeguro que quiere hacer el pase de libros, perdera los datos que tiene ahora";
         |SI FSalida ! 0;  |ACABA;  |FINSI;

         |SI #dsmom008#12 = "303";
             |SUB_EJECUTA "eospa303;AUTO";
         |FINSI;

         |SI #dsmom008#12 = "310";
             |SUB_EJECUTA "reoz0004;AUTO";
         |FINSI;
     |FINSI;

|| \\\\\ Borrar Modelos ////////

     |SI nTecla = 16;
         enTipoEntrada = 3;
         |HAZ BajaModelo8;
         |HAZ Refresco;
     |FINSI;


|| \\\\\ Copiar del Periodo Anterior ////////

     |SI nTecla = 11; |Y nSwCop8 = 0;

         |CONFI "0000NSeguro de recoger los datos del periodo anterior";
         |SI FSalida ! 0;  |ACABA;  |FINSI;

         eaPeriodo = #dsmom004#22;

         |SUB_EJECUTA "eoszp006;AUTO";

         |HAZ Refresco;
     |FINSI;
|FPRC;

|PROCESO BtnMod8;
     |PTEC 824;
|FPRC;

|PROCESO BtnPas8;
     |PTEC 829;
|FPRC;

|PROCESO BtnBaj8;
     |PTEC 830;
|FPRC;

|PROCESO BtnCop8;
     |PTEC 825;
|FPRC;

|PROCESO Tipo0m008;  |TIPO 0;
     |HAZ FinBotonm008;
|FPRC;

|PROCESO Tipo7m008;  |TIPO 7;
     |BLIN 04;

     |CONTROL_SIMPLE 0, "BOTON,F2 Entrada datos", 0867, 0880, BtnMod8;
     nBtnMod8 = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,F8 Baja registro", 1067, 1080, BtnBaj8;
     nBtnBaj8 = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,F7 Pase desde libros", 1267, 1280, BtnPas8;
     nBtnPas8 = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,F3 Copiar Periodo Anterior", 1467, 1580, BtnCop8;
     nBtnCop8 = FSalida;

     nSwCop8 = 1;
     |SI #dsmom008#5 = 0; |Y #dsmom008#1 ] 2017; |Y #dsmom008#2 = 12;
         |ESTADO_CONTROL nBtnPas8, "DISABLE";
         |ESTADO_CONTROL nBtnCop8, "DISABLE";
         |ACABA;
     |FINSI;

     |SI #dsmom008#5 = 0;
         |ESTADO_CONTROL nBtnBaj8, "DISABLE";
         |ESTADO_CONTROL nBtnPas8, "DISABLE";
         |ESTADO_CONTROL nBtnCop8, "DISABLE";
         |ACABA;
     |FINSI;

     |HAZ PoderCopiar;

     |SI nSwCop8 = 1;
         |ESTADO_CONTROL nBtnCop8, "DISABLE";
     |FINSI;

     nPasLib = 0;

     |SI #dsmom008#12 ! 303;  |Y #dsmom008#12 ! 310;
         |ACABA;
     |FINSI;

     nPasLib = 1;

     |ABRE #eoscabve;
     #eoscabve#0 = #dsmom008#0;
     #eoscabve#4 = #dsmom008#1 - 2000;
     |LEE 000#eoscabve.=;
     |SI FSalida = 0;
         |CIERRA #eoscabve;
         |ACABA;
     |FINSI;
     |CIERRA #eoscabve;

     |ABRE #eoscabco;
     #eoscabco#0 = #dsmom008#0;
     #eoscabco#4 = #dsmom008#1 - 2000;
     |LEE 000#eoscabco.=;
     |SI FSalida = 0;
         |CIERRA #eoscabco;
         |ACABA;
     |FINSI;
     |CIERRA #eoscabco;

     nPasLib = 0;
|FPRC;
