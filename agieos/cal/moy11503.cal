|FICHEROS;
     eosy0010;
     dsmoy000;
     eosclien #1;
     eosdaban #2;
     eoshacie #3;
     eosm0046 #28;
     eosm0040 #30;
     eosm0043 #33;
     t1511501;
|FIN;

|VARIABLES;
     nPeriodo     = 0;
     nCampoD      = 0;
     aLLeno40     = "zzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzz";
     aVacio40     = "                                        ";
     aAlfa        = "";
     aAlfa1       = "";
     aAlfa2       = "";
     aModelo      = "";
     aMonedaAnt   = "";
     aFichero     = "";

     aDirOri  = "";
     aDirDes  = "";
     aOrigen  = "";
     aDestino = "";

     nCalc = 0;
     nNeg  = 0;
     Calc         = 0;
     Calc1        = 0;
     Calc2        = 0;
     TipoDato     = "";
     Long         = 0;
     LongDec      = 0;
     Comodin1     = "";
     Comodin2     = "";
     Vacio1       = "";
     Vacio2       = "";
     nConver      = 0;
     nCampo       = 0;
     nHandlePortal= 0;

     &enEmpresa   = 0;
     &enEjercic   = 0;
     &dia         = "";
     &mes         = "";
     &anyo        = "";
     &eaCadena      = "";
     &eaCadena1     = "";
     &eaCadena2     = "";
     &eaCadena3     = "";

     &EURODB    = 0;

     {-1}aMoneda   = "";
         aMoneda1  = "LEER";
         aMoneda2  = "";

     Comodin = "";

     aAbre        = "";
     aModelos     = "";
     aEnlace      = "";
     aHID         = "";
     aNDC         = "";
     aIDI         = "";
     aEJF         = "";
     aMOD         = "";
     aPRG         = "";
     aTIA         = "";
     aINI         = "";
     aFIN         = "";
     aAbrePortal  = "";
     aTipo        = "";

     nHandle1     = 0;
     nHandle2     = 0;
     nTCampo      = 0;

     aUnidad      = "";

  {1}aContiene    = "";

     &eaFOrigen   = "";
     &eaFDestino  = "";
     &eaInternet  = "";
     &eaImpresora = "";
     &enNume      = 0;
     &eaAlfa      = "";
     &eaDiaB      = "";
     &eaMesB      = "";
     &eaAnyoB     = "";
     &eaModelos   = "";

     &eaEjer             = 0;
     &eaPathEmision      = "";
     &eaPathLanza        = "";
     &eaPathExplorador   = "";
     &eaPathInternet     = "";
     &eaPathPreDecla     = "";
     &eaPathAEAT         = "";
     &eaFicheroPlano     = "";
     &eaFicheroSalid     = "";
     &eaFicheroImpre     = "";
     &eaFicheroError     = "";
     &eaOpcion           = "";
     &eaNomEmpresa       = "";
     &enPresentar        = 0;
     &enErrorPortal      = 0;
     &enTecla            = 0;
     &enPer              = 0;
     &enPeriodo          = 0;
     &enHandlePortal     = 0;
     &eaPeriodo          = "";
     &eaUnidad           = "";
     &eaForzExplorador   = "";
     &eaNIF              = "";
     &eaAlfa1            = "";
     &eaAlfa2            = "";
     &eaVersMod          = "    ";
     &eaNIFDs            = "         ";

     &se_empre    = 0;
     &se_ejerc    = 0;
     &se_perio    = 0;
     &se_tipos    = 0;
     &se_activ    = 0;
     &se_impor    = 0;
     &se_fecha    = "";
     &se_factu    = "";
|FIN;

|PROCESO TDeclaracion2017;
     |ABRE #eosdaban;
     #eosdaban#0 = #eosclien#0;
     |LEE 000#eosdaban.=;
     |SI FSalida ! 0; |DDEFECTO #eosdaban; |FINSI;
     |CIERRA #eosdaban;

     |SI #eosm0043#12 > 0;
         enNume = #eosm0043#12;  |HAZ Conver17;  #t1511501#16 = eaAlfa;

                                 #t1511501#6 = "I";
         |SI #eosdaban#14 = "3"; #t1511501#6 = "U";  |FINSI;
         |SI #eosdaban#14 = "6"; #t1511501#6 = "G";  |FINSI;

         |SI #eosdaban#14 ! "1";
             #t1511501#19 = #eosdaban#7;
         |FINSI;
     |SINO;
         enNume = 0;  |HAZ Conver17;  #t1511501#16 = eaAlfa;
         #t1511501#6 = "N";
     |FINSI;
|FPRC;

|PROCESO MeteNombre;
     aAlfa = #eosclien#1 % 101;
     |SI aAlfa ! "X"; |Y aAlfa ! "Y";  |Y aAlfa ! "Z";
      |Y aAlfa ! "0"; |Y aAlfa ! "1";  |Y aAlfa ! "2";
      |Y aAlfa ! "3"; |Y aAlfa ! "4";  |Y aAlfa ! "5";
      |Y aAlfa ! "6"; |Y aAlfa ! "7";  |Y aAlfa ! "8";
      |Y aAlfa ! "9";
         eaAlfa = #eosclien#2;  |HAZ QuitaRaros;   #t1511501#8  = eaAlfa;
                                                   #t1511501#9  = "";
         |ACABA;
     |FINSI;

     eaCadena = #eosclien#2;  |HAZ SeparaNombre;

     eaAlfa = eaCadena1;  |HAZ QuitaRaros;  aAlfa1 = eaAlfa;
     eaAlfa = eaCadena2;  |HAZ QuitaRaros;  aAlfa2 = eaAlfa;

     #t1511501#8 = aAlfa1 + " " + aAlfa2;

     eaAlfa = eaCadena3;  |HAZ QuitaRaros;  #t1511501#9 = eaAlfa;
|FPRC;

|PROCESO Carga115_03;
     |DDEFECTO #t1511501;
     aAlfa         = #eosclien#1;  |QBF aAlfa;
     #t1511501#7   = ("000000000" + aAlfa) % -109;

     |HAZ MeteNombre;

     |HAZ TDeclaracion2017;

     #t1511501#10  = #eosm0043#1;
     #t1511501#11  = eaPeriodo;

     eaAlfa = #eosm0043#13; eaAlfa = ("00000000000000" + eaAlfa) % -115;
     #t1511501#12 = eaAlfa;

     enNume = #eosm0043#7 + #eosm0043#10;  |HAZ Conver17;   #t1511501#13 = eaAlfa;
     enNume = #eosm0043#8 + #eosm0043#11;  |HAZ Conver17;   #t1511501#14 = eaAlfa;
     enNume = #eosm0043#14;                |HAZ Conver17;   #t1511501#15 = eaAlfa;

     |SI #eosm0043#4 > 0;
         #t1511501#17 = "X";
         #t1511501#18 = #eosm0043#15;
     |FINSI;

     #t1511501#23 = &13 + &10;

     aAlfa = "<T1150" + #t1511501#10 + #t1511501#11 + "0000>";  |XGRABA nHandle2, aAlfa;
     aAlfa = "<AUX>";                                           |XGRABA nHandle2, aAlfa;
     aAlfa = " " * 70;                                          |XGRABA nHandle2, aAlfa;
     aAlfa = eaVersMod;                                         |XGRABA nHandle2, aAlfa;
     aAlfa = " " * 4;                                           |XGRABA nHandle2, aAlfa;
     aAlfa = eaNIFDs;                                           |XGRABA nHandle2, aAlfa;
     aAlfa = " " * 213;                                         |XGRABA nHandle2, aAlfa;
     aAlfa = "</AUX>";                                          |XGRABA nHandle2, aAlfa;

     |PARA nCampo = 1;  |SI nCampo [ 22;  |HACIENDO nCampo = nCampo + 1;
           aAlfa = #t1511501#nCampo#;
           |XGRABA nHandle2, aAlfa;
     |SIGUE;

     aAlfa = "</T1150" + #t1511501#10 + #t1511501#11 + "0000>";  |XGRABA nHandle2, aAlfa;
|FPRC;

|PROCESO Imprime115_03;
     #eosclien#0 = #30#0;
     |LEE 040#1.=;
     |SI FSalida ! 0;  |DDEFECTO #1;  |FINSI;

     #3#0 = #eosclien#18; #3#1 = #eosclien#19;
     |LEE 040#3.=;
     |SI FSalida ! 0; |DDEFECTO #3; |FINSI;

     #2#0 = #eosclien#0;
     |LEE 040#2.=;
     |SI FSalida ! 0; |DDEFECTO #2; |FINSI;

     #28#0 = #30#0;
     #28#1 = #30#1;
     |LEE 000#28.=;
     |SI FSalida ! 0;  |DDEFECTO #28;  |FINSI;

     |SI #28#2 = "T";
         |SI #eosy0010#38 = 0;  |ACABA;  |FINSI;
         nPeriodo = #eosy0010#38;
         enPer    = #eosy0010#38;
     |SINO;
         nPeriodo = #eosy0010#39;
         enPer    = #eosy0010#39;
         |SI #eosy0010#39 = 0;  |ACABA;  |FINSI;
     |FINSI;
     enPeriodo = enPer;

     #33#0 = #30#0;
     #33#1 = #30#1;
     #33#2 = 115;
     #33#3 = nPeriodo;
     #33#4 = #eosy0010#44;
     |LEE 000#33.=;
     |SI FSalida ! 0; |DDEFECTO #33; |ACABA; |FINSI;

     eaFicheroPlano = "115" + (("00000" + #eosclien#0) % -105) + ".txt";

     aAbre = eaPathEmision + eaFicheroPlano;
     |XABRE "BW", aAbre, nHandle2;

     |HAZ Carga115_03;

     |XCIERRA nHandle2;

     aAlfa    = #eosy0010#40 % -102;;
     se_empre = #30#0;
     se_ejerc = aAlfa;
     se_perio = nPeriodo;
     se_tipos = 115;
     se_activ = 10;
     se_impor = #33#12;
     se_fecha = #eosy0010#43;
     se_factu = "N";
     |HAZ seguimie;
     |HAZ AlAgicli;
     #33#5 = "S";
     |GRABA 020#33a;

     enEmpresa    = #eosclien#0;
     eaNomEmpresa = #eosclien#1;

                          eaFOrigen  = eaPathEmision  + eaFicheroPlano;
     |SI eaOpcion = "1";  eaFDestino = eaPathPreDecla + eaFicheroPlano;  |FINSI;
     |SI eaOpcion = "3";  eaFDestino = eaPathInternet + eaFicheroPlano;  |FINSI;

     |HAZ CopiaFicheroPtl;
|FPRC;

|DEFBUCLE eosm0030C_03;
     #30#1;
     ;
     #eosy0010#0, #eosy0010#40;
     #eosy0010#1, #eosy0010#40;
     ;
     NULL, Imprime115_03;
|FIN;

|DEFBUCLE eosm0030N_03;
     #30#2;
     ;
     aVacio40, #eosy0010#40;
     aLLeno40, #eosy0010#40;
     ;
     NULL, Imprime115_03;
|FIN;

|PROCESO moy11503;
     |DBASE eaPathEmision;
     eaPathEmision = eaPathEmision + "emisiones";       |MKDIR eaPathEmision;
     eaPathEmision = eaPathEmision + "/" + eaModelos;   |MKDIR eaPathEmision;
     eaPathEmision = eaPathEmision + "/" + eaEjer;      |MKDIR eaPathEmision;
     eaPathEmision = eaPathEmision + "/" + eaPeriodo;   |MKDIR eaPathEmision;
     eaPathEmision = eaPathEmision + "/";

     eaPathInternet = eaUnidad + "/AEAT";                |RMKDIR eaPathInternet;
     eaPathInternet = eaPathInternet + "/INTERNET";      |RMKDIR eaPathInternet;
     eaPathInternet = eaPathInternet + "/" + eaModelos;  |RMKDIR eaPathInternet;
     eaPathInternet = eaPathInternet + "/" + eaEjer;     |RMKDIR eaPathInternet;
     eaPathInternet = eaPathInternet + "/" + eaPeriodo;  |RMKDIR eaPathInternet;
     eaPathInternet = eaPathInternet + "/";

     eaPathPreDecla = eaUnidad + "/AEAT";                  |RMKDIR eaPathPreDecla;
     eaPathPreDecla = eaPathPreDecla + "/PREDECLARACION";  |RMKDIR eaPathPreDecla;
     eaPathPreDecla = eaPathPreDecla + "/" + #dsmoy000#0;  |RMKDIR eaPathPreDecla;
     eaPathPreDecla = eaPathPreDecla + "/" + #dsmoy000#6;  |RMKDIR eaPathPreDecla;
     eaPathPreDecla = eaPathPreDecla + "/" + eaPeriodo;    |RMKDIR eaPathPreDecla;
     eaPathPreDecla = eaPathPreDecla + "/";

     eaPathAEAT = eaUnidad + "\AEAT";        |RMKDIR eaPathAEAT;
     eaPathAEAT = eaPathAEAT + "\TMP";             |RMKDIR eaPathAEAT;
     eaPathAEAT = eaPathAEAT + "\" + #dsmoy000#0;  |RMKDIR eaPathAEAT;
     eaPathAEAT = eaPathAEAT + "\" + #dsmoy000#6;  |RMKDIR eaPathAEAT;
     |SI #dsmoy000#4 ! 99;
         eaPathAEAT = eaPathAEAT + "\" + (("00" + #dsmoy000#4) % -102);  |RMKDIR eaPathAEAT;
     |FINSI;
     eaPathAEAT = eaPathAEAT + "\";

     |HAZ Botones2015;
     |SI enTecla = 0;  |ACABA;  |FINSI;

     |SI eaOpcion = "1";
         |MENAV "0000Opcion pendiente de activacion por Hacienda";
         |ACABA;
     |FINSI;

     |ABRE #eosclien;
     |ABRE #2;
     |ABRE #3;
     |ABRE #28;
     |ABRE #33;
     |SI #eosy0010#0 = 0;
         |ABRE #30;
         |PARA nCampoD = Campo + 1;  |SI nCampoD [ 37;  |HACIENDO nCampoD = nCampoD + 1;
               |SI #eosy0010#nCampoD# ! 0;
                   #30#0 = #eosy0010#nCampoD#;
                   #30#1 = #eosy0010#40;
                   |LEE 040#30.=;
                   |SI FSalida = 0;
                       |HAZ Imprime115_03;
                   |FINSI;
               |FINSI;
         |SIGUE;
         |CIERRA #30;
     |SINO;
         |SI #eosy0010#42 = "C";
             |BUCLE eosm0030C_03;
         |SINO;
             |BUCLE eosm0030N_03;
         |FINSI;
     |FINSI;
     |CIERRA #eosclien;
     |CIERRA #2;
     |CIERRA #3;
     |CIERRA #28;
     |CIERRA #33;

     |SI eaOpcion = "1";
         eaAlfa1 = "Los ficheros generados se encuentran en su PC en el directorio:";
         eaAlfa2 = eaPathPreDecla;

         |HAZ PintaUbicacion;
     |FINSI;

     |SI eaOpcion = "3";
         eaAlfa1 = "Los ficheros generados se encuentran en su PC en el directorio:";
         eaAlfa2 = eaPathInternet;

         |HAZ PintaUbicacion;
     |FINSI;

     |SI eaOpcion = "3";
         |CONFI "0000SQuiere cargar la pagina de hacienda del modelo";
         |SI FSalida = 0;
             |HAZ FormularioOV17;
         |FINSI;
     |FINSI;
|FPRC;
