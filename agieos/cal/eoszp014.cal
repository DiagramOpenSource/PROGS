|FICHEROS;
     eoszp014 #0;             || Este Def
     eosca347 #1;             || Cabecera 347 antiguas
     eosli347 #2;             || Lineas   347 antiguas
     eosm0009 #3;             || Cabecera 347
     eosm0010 #4;             || Lineas   347
|FIN;

|VARIABLES;
     SwGraba = 0;
|FIN;

|PROGRAMA;
  |CLS;
  |PINPA #0#0;
  |CABEZA "Recupera 347";
  |ABRE #0;
  |DDEFECTO #0;
  |LEE 101#0p;
  |SI FSalida ! 0; |GRABA 020#0b; |FINSI;

  |SI #0#1 = "S";
      |MENAV "       El programa ha sido ejecutado.";
      |CIERRA #0;
      |ACABA;
  |FINSI;

  |PINDA #0#0;
  |ENDATOS #1#0;
  |SI FSalida = 0; #0#1 = "S"; |GRABA 020#0a; |FINSI;
  |LIBERA #0;
  |CIERRA #0;
|FPRO;

|PROCESO MiraCabe;
  SwGraba = 0;

  #3#0 = #1#0;
  #3#1 = #0#2;
  |LIBERA #3;
  |LEE 140#3=;
  |SI FSalida = 0; |ERROR; |FINSI;

  |DDEFECTO #3;
  #3#0 = #1#0;                || Empresa
  #3#1 = #0#2;                || A¤o
  #3#2 = #1#2;                || Fecha Presentacion
  #3#3 = #1#3;                || Archivado
|FPRC;

|PROCESO GrabaLinea;
  |SI #2#5 = 0; |ACABA; |FINSI;

  SwGraba = 1;
  |DDEFECTO #4;
  #4#0  = #3#0;                                   || Empresa
  #4#1  = #3#1;                                   || A¤o
  |SI #2#2 = 2; #4#2 = "A"; |FINSI;               || Clave
  |SI #2#2 = 1; #4#2 = "B"; |FINSI;               || Clave
  |SI #2#2 = 3; #4#2 = "C"; |FINSI;               || Clave
  #4#3  = #2#3;                                   || NIF
  #4#4  = "N";                                    || Operacion Seguro
  #4#5  = "N";                                    || Arrendamientos
  #4#6  = #2#4;                                   || Nombre
  #4#7  = #2#6;                                   || SG
  #4#8  = #2#7;                                   || Calle
  #4#9  = #2#8;                                   || Numero
  #4#13 = #2#9;                                   || Codigo Postal 1
  #4#14 = #2#10;                                  || Codigo Postal 2
  #4#15 = #2#12;                                  || Poblacion
  #4#16 = #2#11;                                  || Provincia
  #4#19 = #2#5;                                   || Importe
  |GRABA 140#4n;
  |LIBERA #4;

  |SI #4#2 = "A"; #3#6 = #3#6 + #4#19; #3#12 = #3#12 + 1; |FINSI;
  |SI #4#2 = "B"; #3#7 = #3#7 + #4#19; #3#13 = #3#13 + 1; |FINSI;
  |SI #4#2 = "C"; #3#8 = #3#8 + #4#19; #3#14 = #3#14 + 1; |FINSI;
|FPRC;

|PROCESO GrabaCabe;
  |SI SwGraba = 0; |ACABA; |FINSI;
  |GRABA 140#3n;
|FPRC;

|DEFBUCLE eosca347;
  #1#1;
  ;
  00001;
  99999;
  ;
  NULL, MiraCabe, GrabaLinea, GrabaCabe;
|FIN;

|PROCESO Tipo3; |TIPO 3;
  |ABRE #3;
  |ABRE #4;
  |BUCLE eosca347;
  |CIERRA #3;
  |CIERRA #4;
|FPRC;
