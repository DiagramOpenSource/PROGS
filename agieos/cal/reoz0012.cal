|FICHEROS;
     reoz0012  #0;
     reoz0010  #10;
     eosm0017  #17;
     eosm0018  #18;
     eosm0019  #19;
     eosm0019@ #119;
     eosw0070  #70;
     eosclien  #1;
     eosdaban  #4;
|FIN;

|VARIABLES;
     nCampo      = 0;
     nCampo1     = 0;
     nCampo5     = 0;
     nSwHay      = 0;
     nWord       = 0;
     nNume       = 0;
     nLong       = 0;
     nDecimal    = 0;
     nCaracter   = 0;
     nPos        = 0;

     aBase       = "";
     aMas        = "";
     aFichero    = "";
     aAlfa       = "";
     aCarta      = "";
     aOrigen     = "";
     aDestino    = "";
     aIPRemoto   = "";
     aAbre       = "";
     aIdenti     = "";
     aAsigna     = "";
     aDecima     = "";
     aEntero     = "";
     aLong       = "";
     aImpor1     = "";
     aImpor2     = "";
     aImpor3     = "";
     aImpor4     = "";
     aCaracter   = "";
     aDia        = "";
     aEje        = "";
     aMes        = "";

     &enResult   = 0;
     &enCodCarta = 0;
     &eaPrograma = "";
|FIN;

|PROCESO CartaOInforme;
     |C_M #0#3, 1, "S";
     |C_M #0#7, 1, "S";
     |SI #0#2 = "I";
         |C_M #0#3, 1, "N";
         |C_M #0#7, 1, "N";
         #0#3 = 2;   |PINTA #0#3;
         #0#4 = "";  |PINTA #0#4;
         #0#7 = "";  |PINTA #0#7;
     |FINSI;
|FPRC;

|PROCESO Carta;
     |SI #0#2 = "I";  |ACABA;  |FINSI;

     |SI FSalida = 10;
         eaPrograma = "reoz0010";
         |SUB_EJECUTA_NP "reoz0010";
         #0#3 = enCodCarta;  |PINTA #0#3;
         |ERROR;
         |ACABA;
     |FINSI;

     |ABRE #10;
     #10#0 = #0#3;
     |LEE 040#10=;
     |SI FSalida ! 0;
         |MENAV "     No existe la Carta.";
         |ERROR;
     |FINSI;
     |CIERRA #10;

     #0#4 = #10#1;  |PINTA #0#4;
|FPRC;

|PROCESO Calculo310;
     #119#30 = #119#9  + #119#14 + #119#19 + #119#24 + #119#29;
     #119#44 = #119#30 - #119#43;
     #119#34 = #119#44 + #119#31 + #119#32 + #119#33;
     #119#46 = #119#35 + #119#45;
     #119#36 = #119#34 - #119#46;
     #119#38 = #119#36 - #119#37;
|FPRC;

|PROCESO eosm0017;
     |SI #17#3 ! "7211"; |Y #17#3 ! "7213";  |Y #17#3 ! "7212";
      |Y #17#3 ! "722 "; |Y #17#3 ! "757 ";
          |ACABA;
     |FINSI;

     |SI #17#3 = "722 "; |Y #17#4 = 2;
          |ACABA;
     |FINSI;

     #18#0 = #17#0;
     #18#1 = #17#1;
     #18#2 = #17#5;
     |LEE 040#18=;
     |SI FSalida ! 0;  |DDEFECTO #18;  |FINSI;

     #70#0  = #17#113;
     #70#1  = #70#0 % 1;                 || 1 % Cuota Devengada
     #70#2  = #18#115;                   || Cuotas Soportadas
     #70#3  = #70#1 + #70#2;             || Total Cuota Soportada
     #70#4  = #18#66;                    || Coeficiente Temporada
     |SI #70#4 = 0;  #70#4 = 1;  |FINSI;
     #70#5  = (#70#0 - #70#3) * #70#4;   || Resultado

     |SI #17#3 = "7211";  #70#6 = 1;   |FINSI;
     |SI #17#3 = "7213";  #70#6 = 1;   |FINSI;
     |SI #17#3 = "7212";  #70#6 = 10;  |FINSI;
     |SI #17#3 = "722 ";  #70#6 = 10;  |FINSI;
     |SI #17#3 = "757 ";  #70#6 = 10;  |FINSI;

     #70#7  = #70#0 % #70#6;             || Total Cuota Minima
     #70#7  = #70#7 * #70#4;             || Total Cuota Minima
     #70#8  = #70#5;                     || Cuota derivada regiman general
     |SI #70#8 < #70#7;  #70#8 = #70#7;  |FINSI;

     enResult = (#70#8 + #18#118 + #18#120 + #18#122 + #18#124) ! 2;

     |SI enResult = #17#127;  |ACABA;  |FINSI;

     #19#0  = #17#0;
     #19#1  = #17#5;
     #19#2  = 4;
     |LEE 040#19=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     #119#0  = #17#0;
     #119#1  = #17#5;
     #119#2  = 4;
     |LEE 040#119=;
     |SI FSalida ! 0;
         |DDEFECTO #119;
         |PARA nCampo = 0;  |SI nCampo [ 61;  |HACIENDO nCampo = nCampo + 1;
               #119nCampo = #19nCampo;
         |SIGUE;
         |GRABA 020#119b;
     |FINSI;

     |PARA nCampo1 = 5; |SI nCampo1 [ 25; |HACIENDO nCampo1 = nCampo1 + 5;
           nCampo5 = nCampo1 + 4;

           |SI #119nCampo1 = #17#1;
               #119nCampo5 = enResult;
               |SAL;
           |FINSI;
     |SIGUE;

     |HAZ Calculo310;

     |GRABA 020#119a;
     |LIBERA #119;
|FPRC;

|DEFBUCLE eosm0017;
     #17#1;
     ;
     #0#0, 02, 1;
     #0#1, 02, 6;
     ;
     NULL, eosm0017;
|FIN;

|PROCESO AbreWord;
||     aAlfa = "C:/MODELOS";                   |RMKDIR aAlfa;
||     aAlfa = "C:/MODELOS/DOCUMENTOS";        |RMKDIR aAlfa;

     aAlfa = "/MODELOS";                   |RMKDIR aAlfa;
     aAlfa = "/MODELOS/DOCUMENTOS";        |RMKDIR aAlfa;

     |DBASE aBase;
     aCarta    = "cartas" + (("00" + #0#3) % -102) + ".doc";
     aOrigen   = aBase + "cartas/" + aCarta;
||     aDestino  = "C:/MODELOS/DOCUMENTOS/" + aCarta;
     aDestino  = "/MODELOS/DOCUMENTOS/" + aCarta;

     aIPRemoto = "";
     |IP_REMOTO aIPRemoto;  |QBT aIPRemoto;

     |SI aIPRemoto ! "";
         |ENVIA_FICHERO aOrigen, aDestino;
     |SINO;
         |COPIA_FICHERO aOrigen, aDestino;
     |FINSI;

     nWord  = 1;
     aAbre  = aDestino;

     |MENSA "      Abriendo el Documento, espere un Momento.";

     |WORD_ABRE nWord, aAbre;
     |WORD_PROPIEDADES nWord, "Visible", "NO";
|FPRC;

|PROCESO Asigna;
     FSalida = 0;
     |PARA; |SI FSalida = 0; |HACIENDO;
            |WORD_ASIGNA_TEXTO nWord, aIdenti, aAsigna;
     |SIGUE;
|FPRC;

|PROCESO NumeroDecimal;
     aAsigna   = nNume;
     aDecima   = "";
     aEntero   = "";
     aLong     = aAsigna % 0;
     nLong     = aLong;
     nDecimal = 0;
     |PARA nCaracter = 1;  |SI nCaracter [ nLong;  |HACIENDO nCaracter = nCaracter + 1;
           nPos      = (100 * nCaracter) + 1;
           aCaracter = aAsigna % nPos;
           |SI aCaracter = ".";
               nDecimal = 1;
           |SINO;
               |SI nDecimal = 0;
                   aEntero = aEntero + aCaracter;
               |SINO;
                   aDecima = aDecima + aCaracter;
               |FINSI;
           |FINSI;
     |SIGUE;
     aDecima   = (aDecima + "00") % 102;
     aAsigna   = aEntero;

     aLong     = aAsigna % 0;
     nLong     = aLong;
     aAsigna   = "             " + aAsigna;
     aAsigna   = aAsigna % -113;
     aImpor1   = aAsigna % -103;
     aImpor2   = aAsigna % -403;
     aImpor3   = aAsigna % -703;
     aImpor4   = aAsigna % -1004;
     |SI nLong ] 10;
         |SI aImpor4 ! "  -";
             aAsigna = aImpor4 + "." + aImpor3  + "." + aImpor2 + "." + aImpor1;
         |SINO;
             aAsigna = aImpor4 + aImpor3  + "." + aImpor2 + "." + aImpor1;
      |FINSI;
    |SINO;
        |SI nLong ] 7;
            |SI aImpor3 ! "  -";
                aAsigna = aImpor3  + "." + aImpor2 + "." + aImpor1;
            |SINO;
                aAsigna = aImpor3 + aImpor2 + "." + aImpor1;
            |FINSI;
        |SINO;
            |SI nLong ] 4;
                |SI aImpor2 ! "  -";
                    aAsigna = aImpor2 + "." + aImpor1;
                |SINO;
                    aAsigna = aImpor2 + aImpor1;
                |FINSI;
            |SINO;
                aAsigna = aImpor1;
            |FINSI;
        |FINSI;
     |FINSI;

     |QBT aAsigna;

     |SI aDecima ! "00";
         aAsigna = ("                " + aAsigna + "," + aDecima) % -116;
     |SINO;
         aAsigna = ("             " + aAsigna) % -113;
     |FINSI;

     |SI aAsigna = "            0";     aAsigna = "";  |FINSI;
     |SI aAsigna = "            0,00";  aAsigna = "";  |FINSI;

     FSalida = 0;
     |PARA; |SI FSalida = 0; |HACIENDO;
            |WORD_ASIGNA_TEXTO nWord, aIdenti, aAsigna;
     |SIGUE;
|FPRC;

|PROCESO Fecha;
     aDia = #0#5 % 102;
     aEje = #0#5 % -104;
     aMes = #0#5 % 402;

     |SI aMes = "01";  aMes = "Enero";       |FINSI;
     |SI aMes = "02";  aMes = "Febrero";     |FINSI;
     |SI aMes = "03";  aMes = "Marzo";       |FINSI;
     |SI aMes = "04";  aMes = "Abril";       |FINSI;
     |SI aMes = "05";  aMes = "Mayo";        |FINSI;
     |SI aMes = "06";  aMes = "Junio";       |FINSI;
     |SI aMes = "07";  aMes = "Julio";       |FINSI;
     |SI aMes = "08";  aMes = "Agosto";      |FINSI;
     |SI aMes = "09";  aMes = "Septiembre";  |FINSI;
     |SI aMes = "10";  aMes = "Octubre";     |FINSI;
     |SI aMes = "11";  aMes = "Noviembre";   |FINSI;
     |SI aMes = "12";  aMes = "Diciembre";   |FINSI;
|FPRC;

|PROCESO Asignacion;
     |MENSA "      Asignado Campos al Documento, espere un Momento.";

     aAsigna = "En " + #0#6;  |QBF aAsigna;
     |SI aAsigna ! "";  aAsigna = aAsigna + ", ";  |FINSI;
     |HAZ Fecha;
     aAsigna = aAsigna + aDia + " de " + aMes + " de " + aEje;
     aIdenti = "!#FECHA#";
     |HAZ Asigna;

     aAsigna = #1#2;       |QBF aAsigna;
     aIdenti = "!#NOM#";
     |HAZ Asigna;

     aAsigna  = #1#1;   |QBF aAsigna;
     aIdenti  = "!#NIF#";
     |HAZ Asigna;

     aAsigna  = #1#4;
     aIdenti  = "!#SG#";
     |HAZ Asigna;

     aAsigna  = #1#5;  |QBF aAsigna;
     aIdenti  = "!#DOMICILIO#";
     |HAZ Asigna;

     aAsigna = #1#6 % 104;  |QBF aAsigna;
     aIdenti  = "!#NUMERO#";
     |HAZ Asigna;

     aAsigna = #1#13;        |QBF aAsigna;
     aIdenti = "!#POBLACION#";
     |HAZ Asigna;

     aAsigna = #1#12;  |QBF aAsigna;
     aIdenti = "!#PROVINCIA#";
     |HAZ Asigna;

     |ABRE #4;
     #4#0 = #1#0;
     |LEE 040#4=;
     |SI FSalida ! 0;  |DDEFECTO #4;  |FINSI;
     |CIERRA #4;

     aAsigna = #4#2 + " " + #4#3 + " " +  #4#4 + " " + #4#5;
     aIdenti = "!#CUENTA#";
     |HAZ Asigna;

     nNume   = #19#38;    aIdenti = "!#1#";   |HAZ NumeroDecimal;
     nNume   = enResult;  aIdenti = "!#2#";   |HAZ NumeroDecimal;
     nNume = (enResult - #19#38) ! 2;
     |SI nNume < 0;  nNume = -nNume;  |FINSI;
                           aIdenti = "!#3#";   |HAZ NumeroDecimal;
|FPRC;

|PROCESO Cierre;
     |MENSA "      Imprimiendo el Documento, espere un Momento.";

     |SI #0#7 = "I";
         |WORD_IMPRIME nWord, "";
         |WORD_PROPIEDADES nWord, "Visible", "NO";

         |SLEEP 10;

         |MENSA "      Cerrando Documento";

         |WORD_SALVA nWord, "";
     |SINO;
         |WORD_PROPIEDADES nWord, "Visible", "SI";
         FSalida = "NO";
         |PARA;  |SI FSalida = "NO";  |HACIENDO;
                 |PULSATECLA;
                 |WORD_PROPIEDADES nWord, "Quit", "";
         |SIGUE;
     |FINSI;
|FPRC;

|PROCESO eosm0019Tempo;
     nSwHay = 1;

     #1#0  = #119#0;
     |LEE 040#1=;
     |SI FSalida ! 0;  |DDEFECTO #1;  |FINSI;

     #19#0 = #119#0;
     #19#1 = #119#1;
     #19#2 = #119#2;
     |LEE 040#19=;
     |SI FSalida ! 0;  |DDEFECTO #19;  |FINSI;

     enResult = #119#38;

     |SI #0#2 = "I";
         |PRINT;
     |FINSI;

     |SI #0#2 = "C";
         |HAZ AbreWord;
         |HAZ Asignacion;
         |HAZ Cierre;
         |RFBORRA aAbre;
     |FINSI;
|FPRC;

|DEFBUCLE eosm0019Tempo;
     #119#1003;
     ;
     00001, 02, 4;
     99999, 02, 4;
     ;
     NULL, eosm0019Tempo;
|FIN;

|PROCESO Informe;
     |IMPRE 0;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |INFOR "reoz0012";
     |SI FSalida ! 0;  |FINIMP;  |ACABA;  |FINSI;

     |BUCLE eosm0019Tempo;

     |SI nSwHay = 1;  |PIEINF;  |FINSI;
     |FININF;
     |FINIMP;
|FPRC;

|PROCESO Tipo3;  |TIPO 3;
     |DBASE aBase;  |QBT aBase;
     aMas = aBase + "def/eosm0019.mas";
     |CARGA_DEF aMas, eosm0019@;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     aFichero = ("19" + Usuario + "zzzzzzzz") % 108;
     |NOME_DAT #119 aFichero;
     |ABRE #119;  |CIERRA #119;  |DELINDEX #119;

     |ABRE #19;
     |ABRE #119;
     |ABRE #18;
     |BUCLE eosm0017;
     |CIERRA #18;
     |CIERRA #119;


     |ABRE #1;
     |SI #0#2 = "I";  |HAZ Informe;  |FINSI;

     |SI #0#2 = "C";
         |BUCLE eosm0019Tempo;
     |FINSI;

     |CIERRA #1;
     |CIERRA #19;

     |DELINDEX #119;
     |DESCARGA_DEF #eosm0019@;
|FPRC;
