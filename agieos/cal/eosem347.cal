|FICHEROS;
     eosem347 #0;
     eosm0009 #1;
     eosm0010 #2;
     eosclien #3;
     eoscomul #5;
|FIN;

|VARIABLES;
     nSwPaso    = 0;
     Vacio20    = "                    ";
     Vacio40    = "                                        ";
     LLeno40    = "zzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzz";
     Salir      = 0;
     Opcion     = 0;
     Campos     = 0;
     XPara      = 0;
     Empresa    = 0;
     VAlfa1     = "";
     VAlfa2     = "";
     VAlfa3     = "";
     VAlfa4     = "";
     DClave     = "";
     HClave     = "";
     Clave      = "";
     Contador   = 0;
     QueInforme = 0;
     nHojas     = 0;

     &THojas    = 0;
     &NHojas    = 0;
     &enAnyo    = 0;

{-1} MGeneral   = "";
     MGeneral1  = "1645";
     MGeneral2  = "1";
     MGeneral3  = "5";
     MGeneral4  = "Emision Portada              ";
     MGeneral5  = "Emision Hojas Comunes        ";
     MGeneral6  = "Emision Anexo Arrendamientos ";
     MGeneral7  = "-----------------------------";
     MGeneral8  = "Emision Papel en Blanco      ";
|FIN;

|INCLUYE i_cuadre;
|INCLUYE i_anyode;
|INCLUYE i_consul;

|PROCESO Mayus; |TIPO 0;
  #0Campo = #0Campo > #0Campo;
  |PINTA #0Campo;
|FPRC;

|PROCESO Parrilla; |TIPO 0;
  |SI #0#3 = "S";
      |PARA Campos = 4; |SI Campos [ 27; |HACIENDO Campos = Campos + 1;
            |C_M #0Campos, 1, "N";  #0Campos = 0;  |PINTA #0Campos;
      |SIGUE;
      |C_M #0#0, 1, "S";   |C_M #0#1, 1, "S";  |C_M #0#29, 1, "S";
  |SINO;
      |PARA Campos = 4; |SI Campos [ 27; |HACIENDO Campos = Campos + 1;
            |C_M #0Campos, 1, "S";
      |SIGUE;
      |C_M #0#0,  1, "N"; #0#0  = 1;     |PINTA #0#0;
      |C_M #0#1,  1, "N"; #0#1  = 99999; |PINTA #0#1;
      |C_M #0#29, 1, "N"; #0#29 = "C";  |PINTA #0#29;
  |FINSI;
|FPRC;

|PROCESO PintaAnyo; |TIPO 7;
  |HAZ i_anyodef;
  #0#28 = i_anyodef - 1;
  |SI #0#28 < 0;  #0#28 = 99;  |FINSI;
  |PINTA #0#28;
|FPRC;

|PROCESO FechaP;
  VAlfa2 = #0#2 % 102;
  VAlfa3 = #0#2 % 402;
  VAlfa4 = #0#2 % 704;

  |SI VAlfa3 = "01";  VAlfa3 = "Enero     ";  |FINSI;
  |SI VAlfa3 = "02";  VAlfa3 = "Febrero   ";  |FINSI;
  |SI VAlfa3 = "03";  VAlfa3 = "Marzo     ";  |FINSI;
  |SI VAlfa3 = "04";  VAlfa3 = "Abril     ";  |FINSI;
  |SI VAlfa3 = "05";  VAlfa3 = "Mayo      ";  |FINSI;
  |SI VAlfa3 = "06";  VAlfa3 = "Junio     ";  |FINSI;
  |SI VAlfa3 = "07";  VAlfa3 = "Julio     ";  |FINSI;
  |SI VAlfa3 = "08";  VAlfa3 = "Agosto    ";  |FINSI;
  |SI VAlfa3 = "09";  VAlfa3 = "Septiembre";  |FINSI;
  |SI VAlfa3 = "10";  VAlfa3 = "Octubre   ";  |FINSI;
  |SI VAlfa3 = "11";  VAlfa3 = "Noviembre ";  |FINSI;
  |SI VAlfa3 = "12";  VAlfa3 = "Diciembre ";  |FINSI;

  #0#30 = VAlfa2 + " de " + VAlfa3 + " de " + VAlfa4;
|FPRC;

|PROCESO LasLineas;
  |SI QueInforme = 1;
      |SI nSwPaso = -1;
          nSwPaso = 1;
          |ERROR10;
          |ACABA;
      |FINSI;
      |SI #2#2 = "A";  #1#12 = #1#12 + 1;  #1#6  = #1#6  + #2#19;  |FINSI;
      |SI #2#2 = "B";  #1#13 = #1#13 + 1;  #1#7  = #1#7  + #2#19;  |FINSI;
      |SI #2#2 = "C";  #1#14 = #1#14 + 1;  #1#8  = #1#8  + #2#19;  |FINSI;
      |SI #2#2 = "D";  #1#15 = #1#15 + 1;  #1#9  = #1#9  + #2#19;  |FINSI;
      |SI #2#2 = "E";  #1#16 = #1#16 + 1;  #1#10 = #1#10 + #2#19;  |FINSI;
      |SI #2#2 = "Z";  #1#17 = #1#17 + 1;  #1#11 = #1#11 + #2#19;  |FINSI;
      |ACABA;
  |FINSI;

  ||SI Clave ! #2#2; |Y Clave ! "";   Contador = 0;  |PIEINF;  |FINSI;

  Clave    = #2#2;
  Contador = Contador + 1;

  |SI QueInforme = 2;
      |SI Contador > 5;  Contador = 1; |PIEINF;  |FINSI;
  |FINSI;

  |SI QueInforme = 3;
      |SI Contador > 5;  Contador = 1; |PIEINF;  |FINSI;
  |FINSI;

  |PRINT;
|FPRC;

|DEFBUCLE eosm0010;
  #2#1;
  ;
  #3#0, #0#28, DClave, "            ", " ", " ", "  ", " ", " ";
  #3#0, #0#28, HClave, "zzzzzzzzzzzz", "z", "z", "zz", "z", "z";
  ;
  NULL, LasLineas;
|FIN;

|PROCESO Distribuye;
  |SI Opcion = 1; |O Opcion = 5;  ||  Portada
      #0#38   = Vacio40;
      Empresa = #3#0;

      |ABRE #5;
      #5#0 = #3#0;
      #5#1 = 1;
      #5#2 = 1;
      #5#7 = #0#28;
      |LEE 040#5];
      |SI FSalida = 0; |Y #5#0 = #3#0; |Y #5#7 = #0#28;
          #3#0 = #5#3;
          |LEE 040#3=;
          |SI FSalida = 0; #0#38 = #3#2; |FINSI;
      |FINSI;
      |CIERRA #5;

      #3#0 = Empresa;
      |LEE 040#3=;
      |SI FSalida ! 0;  |DDEFECTO #3;  |FINSI;

      |SI #0#38 = Vacio40;
          #0#38 = #3#2;

          |SI #3#24 ! Vacio20; |O #3#25 ! Vacio20;
              #0#38 = #3#24 + #3#25;
          |FINSI;

          |SI #3#29 ! Vacio20; |O #3#30 ! Vacio20;
              |SI #0#38 = Vacio40; #0#38 = #3#29 + #3#30; |FINSI;
          |FINSI;

          |SI #3#34 ! Vacio20; |O #3#35 ! Vacio20;
              |SI #0#38 = Vacio40; #0#38 = #3#34 + #3#35; |FINSI;
          |FINSI;
      |FINSI;

      #1#0 = #3#0;
      #1#1 = #0#28;
      |LIBERA #1;
      |LEE 140#1=;
      |SI FSalida ! 0; |ACABA; |FINSI;

      |SI Opcion = 5;
          QueInforme = 1;
          |INFOR "eospb347";
          |SI FSalida ! 0; |ACABA; |FINSI;
      |FINSI;

      nSwPaso = -1;  |BUCLE eosm0010;

      |SI nSwPaso = 1;
          #1#6  = 0;   #1#7  = 0;   #1#8  = 0;   #1#9  = 0;
          #1#10 = 0;   #1#11 = 0;   #1#12 = 0;   #1#13 = 0;
          #1#14 = 0;   #1#15 = 0;   #1#16 = 0;   #1#17 = 0;
          DClave = " ";
          HClave = "z";
          |BUCLE eosm0010;
      |FINSI;

      |SI Opcion ! 5;  #1#2 = #0#2;  |FINSI;
      |GRABA 140#1a;

      |PRINT;
      |PIEINF;

      |SI Opcion = 5;  |FININF;  |FINSI;

      |SI Opcion ! 5;  |ACABA;   |FINSI;
  |FINSI;

  #1#0 = #3#0;
  #1#1 = #0#28;
  |LEE 040#1=;

  nHojas   = #1#12 + #1#13 + #1#14 + #1#15 + #1#16;
  THojas   = (((nHojas / 5) + .49) ! 0);
  nHojas   = (((#1#17 / 5) + .49) ! 0);
  THojas   = THojas + nHojas;

  |SI Opcion = 2; |O Opcion = 5;
      |SI Opcion = 5;
          QueInforme = 2;
          |INFOR "eoshb347";
          |SI FSalida ! 0; |ACABA; |FINSI;
      |FINSI;

      Clave    = "";
      Contador = 0;
      NHojas   = 1;
      DClave   = "A";
      HClave   = "E";
      |BUCLE eosm0010;
      |SI Contador ! 0;  |PIEINF;  |FINSI;

      |SI Opcion = 5;  |FININF;  |FINSI;
  |FINSI;

  |SI Opcion = 3; |O Opcion = 5;
      |SI Opcion = 5;
          QueInforme = 3;
          |INFOR "eosab347";
          |SI FSalida ! 0; |ACABA; |FINSI;
      |FINSI;

      Clave    = "";
      Contador = 0;
      NHojas   = (THojas - (((#1#17 / 5) + .49) ! 0)) + 1;
      DClave   = "Z";
      HClave   = "Z";
      |BUCLE eosm0010;
      |SI Contador ! 0;  |PIEINF;  |FINSI;

      |SI Opcion = 5;  |FININF;  |FINSI;
  |FINSI;
|FPRC;

|DEFBUCLE eosclienC;
  #3#1;
  ;
  #0#0;
  #0#1;
  ;
  NULL, Distribuye;
|FIN;

|DEFBUCLE eosclienN;
  #3#3;
  0;
  Vacio40, #0#0;
  LLeno40, #0#1;
  ;
  NULL, Distribuye;
|FIN;

|PROCESO Distribucion;
  FSalida = 0;
  |SI Opcion = 1;  |INFOR "eospo347";  QueInforme = 1;  |FINSI;
  |SI Opcion = 2;  |INFOR "eosho347";  QueInforme = 2;  |FINSI;
  |SI Opcion = 3;  |INFOR "eosax347";  QueInforme = 3;  |FINSI;
  |SI FSalida ! 0; |ACABA;  |FINSI;

  |SI #0#3 = "S";
      |SI #0#29 = "C";  |BUCLE eosclienC;  |FINSI;
      |SI #0#29 = "N";  |BUCLE eosclienN;  |FINSI;
  |SINO;
      |PARA XPara = 4; |SI XPara [ 27; |HACIENDO XPara = XPara + 1;
            |SI #0XPara ! 0;
                #0#0 = #0XPara;
                #0#1 = #0XPara;
                |SI #0#29 = "C";  |BUCLE eosclienC;  |FINSI;
                |SI #0#29 = "N";  |BUCLE eosclienN;  |FINSI;
            |FINSI;
      |SIGUE;
  |FINSI;
  |FININF;
|FPRC;

|PROCESO Tipo3; |TIPO 3;
  |SI #0#28 [ 80;
      enAnyo = 2000 + #0#28;
  |SINO;
      enAnyo = 1900 + #0#28;
  |FINSI;

  #0#31 = "X";
  #0#32 = "";
  #0#33 = "";
  #0#34 = "";
  #0#35 = 0;
  #0#36 = 0;

  |HAZ FechaP;
  |ABRE #1;
  |PARA; |SI Salir = 0; |HACIENDO;
         |MENUG MGeneral;
         MGeneral2 = FSalida;
         Opcion    = MGeneral2;
         Salir     = 1;
         |SI Opcion ] 1; |Y Opcion [ 5;
             Salir = 0;
             |SI Opcion ! 4;
                 |IMPRE 0;
                 |SI FSalida = 0;
                     |HAZ Distribucion;
                     |FINIMP;
                 |FINSI;
             |FINSI;
         |FINSI;
  |SIGUE;
  |CIERRA #1;
|FPRC;
