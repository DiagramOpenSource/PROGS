|FICHEROS;
   trasz004 #0;
   trasw001 #10,MANTE;
   trasw002 #11;

   canempr@ #100;
   camasic@ #101;
   camasil@ #102;
   camaniv@ #103;
   caivali@ #104;
   camandi@ #105;
   cacuent@ #106;
   caclipr@ #107;
   caenlac@ #108;
   calibro@ #109;

   Referen@ #500;
|FIN;

|VARIABLES;

   &eaExterno = "";
   &enEmpreOri = 0;
   &enPerioOri = 0;
   &enNoModif  = 0;
   &eaCopiar   = "";
   &eaCeros    = "";
   &eaOtras    = "";
   &eaDepar    = "";

   nHayComillas = 0;

   nDia  = 0;
   nMes  = 0;
   nAnyo = 0;
   nIva  = 0;
   nConcepto = 0;
   nMarca    = 0;
   aTipo     = "";
   aPD       = "";
   aMask     = "";
   aDato1    = "";
   aDato2    = "";

   nLibro   = 0;
   nFactu   = 0;
   aSerie   = "";

   nNumCampo = 0;
   nModo    = 0;
   nAsiento = 0;
   nOrden   = 0;
   aSigno   = "";
   aCuenta  = "";
   aDescri  = "";
   nImporte = 0;
   nLinea   = 0;
   nNumSop  = 0;

   nDiario    = 0;
   fFecha     = @;
   nDocumento = 0;

   aAlfa   = "";
   aAlfa1  = "";
   aAlfa2  = "";
   aCR     = "";
   aLF     = "";
   aCadena = "";
   aDatos  = "";
   nNivel  = 0;

   nLong   = 0;
   nHandle = 0;
   nTecla  = 0;
   nCont   = 0;
   nCalc   = 0;
   nCalc1  = 0;
   nCalc2  = 0;
   nCampo  = 0;
   nSwError = 0;

{200}aCampos = "";
|FIN;

|PROCESO FiltraLetras;
   aAlfa = #trasw001#2; |QBF aAlfa;
   |SI aAlfa = ""; |ACABA; |FINSI;
   aAlfa = (("00" + aAlfa) % -102); #trasw001#2 = aAlfa; |PINTA #trasw001#2;

   aAlfa = #trasw001#2; |QBF aAlfa; aAlfa = aAlfa % 0101;
   |SI aAlfa < "0"; |O aAlfa > "9"; #trasw001#2 = "00"; |PINTA #trasw001#2; |ERROR; |FINSI;
   aAlfa = #trasw001#2; |QBF aAlfa; aAlfa = aAlfa % 0201;
   |SI aAlfa < "0"; |O aAlfa > "9"; #trasw001#2 = "00"; |PINTA #trasw001#2; |ERROR; |FINSI;
|FPRC;

|PROCESO MiraFichs;
   |SI FSalida = 2; |ACABA; |FINSI;
   aAlfa = #trasz004#0; |QBF aAlfa;

   aAlfa1 = aAlfa + #trasz004#Campo#; |QBF aAlfa1;
   |XABRE "E", aAlfa1, nHandle;
   |SI FSalida < 0;
      |MENAV "    Fichero no encontrado "; |ERROR;
   |FINSI;
|FPRC;

|PROCESO PathAutom;
   |SI FSalida = 10;
      |DBASS aAlfa; |QBF aAlfa; aAlfa = aAlfa + "contagen/";
      #trasz004#Campo# = aAlfa; |PINTA #trasz004#Campo#; |ERROR;
   |FINSI;
|FPRC;

|PROCESO MiraPath;
   aAlfa = #trasz004#10; |QBF aAlfa; aAlfa = aAlfa % -101;
   |SI aAlfa ! "/"; |Y aAlfa ! "\";
      aAlfa = #trasz004#10; |QBF aAlfa;
      aAlfa = aAlfa + "/"; #trasz004#10 = aAlfa; |PINTA #trasz004#10;
   |FINSI;

   aAlfa = #trasz004#10; |QBF aAlfa;
   aAlfa = aAlfa + "fich/canempre.dat";
   |XABRE "E", aAlfa, nHandle;
   |SI FSalida < 0;
      |MENAV "    No se ha encontrado el fichero de empresas "; |ERROR;
   |FINSI;
|FPRC;

|PROCESO MiraEmpresa;
   nTecla = FSalida;

   aAlfa = #trasz004#10; |QBF aAlfa;
   aAlfa = aAlfa + "def/canempre.mas";

   |CARGA_DEF aAlfa, canempr@;
   |SI FSalida = 0;
      aAlfa = #trasz004#10; |QBF aAlfa;
      aAlfa = aAlfa + "fich/"; |PATH_DAT #100 aAlfa;
      |ABRE #canempr@;
      |SI nTecla = 10;
         |CONSULTA_CLAVE #1#canempr@;
         |SI FSalida ! 0; |ERROR; |FINSI;
         #trasz004#7 = #canempr@#2; |PINTA #trasz004#7;
         #trasz004#8 = #canempr@#3; |PINTA #trasz004#8;
      |FINSI;
      #canempr@#2 = #trasz004#7;
      #canempr@#3 = #trasz004#8;
      |LEE 000#canempr@.=;
      |SI FSalida = 0;
         #trasz004#9 = #canempr@#1; |PINTA #trasz004#9;
      |FINSI;
      |CIERRA #canempr@; |DESCARGA_DEF #canempr@;
      |SI nTecla = 10; |ERROR; |FINSI;
   |FINSI;
|FPRC;

|PROCESO SacaNivel;
   aAlfa = aCuenta; |QBF aAlfa;
   aAlfa = aAlfa % 0; nLong = aAlfa;

   |PARA nCont = 14; |SI nCont [ 19; |HACIENDO nCont = nCont + 1;
      |SI nLong = #canempr@#nCont#; nNivel = nCont - 13; |FINSI;
   |SIGUE;
|FPRC;

|PROCESO Cambia1; |TIPO 1;
   |HAZ Cambia;
|FPRC;

|PROCESO Cambia;
   nModo = (FEntrada / 100) ! 0;
   |SI nModo = 1; |O nModo = 3; |ERROR; |FINSI;
|FPRC;

|RUTINA inf;
   #trasw001#0 = 00;
|FRUT;

|RUTINA sup;
   #trasw001#0 = 99;
|FRUT;

|PROCESO LeeRegistro;
   aCadena = ""; nHayComillas = 0; nNumCampo = 0; nSwError = 0;
   |XLEE nHandle, 1, aAlfa;
   |PARA; |SI FSalida > 0; |HACIENDO;
      |SI aAlfa ! aCR;
         aAlfa1 = &34;
         |SI aAlfa = aAlfa1;
            |SI nHayComillas = 1;
               nHayComillas = 0;
            |SINO;
               nHayComillas = 1;
            |FINSI;
         |FINSI;
         |SI aAlfa = ";"; |Y nHayComillas = 0;
            IaCampos = aCampos1<-; IaCampos = IaCampos + nNumCampo;
            aCadena = aCadena - &34; aCadena = aCadena - &34; |QBF aCadena;
            aAlfa = aCadena - "/";
            |SI FSalida ! 0;
               aAlfa = aAlfa - "/";
               |SI FSalida ! 0;
                  aAlfa = aAlfa - ":";
                  |SI FSalida ! 0;
                     aAlfa = aAlfa - ":";
                     |SI FSalida ! 0;    || Campo fecha de Access
                        aAlfa = aCadena; nDia = 0; nMes = 0; nAnyo = 0;
                        |PARA; |SI aAlfa ! ""; |HACIENDO;
                           aAlfa1 = aAlfa % 0101; aAlfa = aAlfa % 0299;
                           |SI aAlfa1 = "/";
                              |SI nDia = 0;
                                 nDia = aAlfa2; aAlfa2 = "";
                              |SINO;
                                 nMes = aAlfa2; aAlfa2 = "";
                              |FINSI;
                           |SINO;
                              |SI aAlfa1 = " ";
                                 nAnyo = aAlfa2; aAlfa2 = "";
                              |SINO;
                                 aAlfa2 = aAlfa2 + aAlfa1;
                              |FINSI;
                           |FINSI;
                        |SIGUE;
                        aCadena = (("00" + nDia) % -102) + "." + (("00" + nMes) % -102) + "." + nAnyo;
                        aAlfa2 = "";
                     |FINSI;
                  |FINSI;
               |FINSI;
            |FINSI;

            aCampos = aCadena;
            aCadena = ""; nHayComillas = 0; nNumCampo = nNumCampo + 1;
         |SINO;
            aCadena = aCadena + aAlfa;
         |FINSI;
      |SINO;
         |SAL;
      |FINSI;
      |XLEE nHandle, 1, aAlfa;
   |SIGUE;

   |SI FSalida [ 0;
      nSwError = 1;
   |SINO;
      nSwError = 0;
   |FINSI;
|FPRC;

|PROCESO IvaIntrac;
   aAlfa = #caenlac@#4; |QBF aAlfa; aAlfa = aAlfa % 0103;
   ||SI aAlfa = "477"; |O aAlfa = "472";
      ||BORRA 020#caenlac@.a;
   ||SINO;
      |SI #caenlac@#10 = "R";
         #caenlac@#6 = #trasz004#20;
      |SINO;
         #caenlac@#6 = #trasz004#18;
      |FINSI;
      |GRABA 020#caenlac@.a;
   ||FINSI;
|FPRC;

|DEFBUCLE IvaIntrac;
#caenlac@#1006;
;
nDiario, fFecha, nDocumento, 0000, #trasz004#7, #trasz004#8;
nDiario, fFecha, nDocumento, 9999, #trasz004#7, #trasz004#8;
;
NULL, IvaIntrac;
|FIN;

|PROCESO Iva;
   #caenlac@#10 = aTipo;
   #caenlac@#6  = nConcepto;
   |GRABA 020#caenlac@.a;
|FPRC;

|DEFBUCLE Iva;
#caenlac@#1006;
;
nDiario, fFecha, nDocumento, 0000, #trasz004#7, #trasz004#8;
nDiario, fFecha, nDocumento, 9999, #trasz004#7, #trasz004#8;
;
NULL, Iva;
|FIN;

|PROCESO Traspaso;
   nNumSop  = 1;
   aAlfa = #trasz004#10; |QBF aAlfa;

   aAlfa1 = aAlfa + "def/canempre.mas";
   |CARGA_DEF aAlfa1, canempr@;
   |SI FSalida ! 0; |ACABA; |FINSI;

   aAlfa1 = aAlfa + "def/camaasic.mas";
   |CARGA_DEF aAlfa1, camasic@;
   |SI FSalida ! 0;
      |DESCARGA_DEF #canempr@; |ACABA;
   |FINSI;

   aAlfa1 = aAlfa + "def/camaasil.mas";
   |CARGA_DEF aAlfa1, camasil@;
   |SI FSalida ! 0;
      |DESCARGA_DEF #camasic@;
      |DESCARGA_DEF #canempr@; |ACABA;
   |FINSI;

   aAlfa1 = aAlfa + "def/cacuenta.mas";
   |CARGA_DEF aAlfa1, cacuent@;
   |SI FSalida ! 0;
      |DESCARGA_DEF #camasil@;
      |DESCARGA_DEF #camasic@;
      |DESCARGA_DEF #canempr@; |ACABA;
   |FINSI;

   aAlfa1 = aAlfa + "def/camandia.mas";
   |CARGA_DEF aAlfa1, camandi@;
   |SI FSalida ! 0;
      |DESCARGA_DEF #cacuent@;
      |DESCARGA_DEF #camasil@;
      |DESCARGA_DEF #camasic@;
      |DESCARGA_DEF #canempr@; |ACABA;
   |FINSI;

   aAlfa1 = aAlfa + "def/caclipro.mas";
   |CARGA_DEF aAlfa1, caclipr@;
   |SI FSalida ! 0;
      |DESCARGA_DEF #camandi@;
      |DESCARGA_DEF #cacuent@;
      |DESCARGA_DEF #camasil@;
      |DESCARGA_DEF #camasic@;
      |DESCARGA_DEF #canempr@; |ACABA;
   |FINSI;

   aAlfa1 = aAlfa + "def/camaniva.mas";
   |CARGA_DEF aAlfa1, camaniv@;
   |SI FSalida ! 0;
      |DESCARGA_DEF #caclipr@;
      |DESCARGA_DEF #camandi@;
      |DESCARGA_DEF #cacuent@;
      |DESCARGA_DEF #camasil@;
      |DESCARGA_DEF #camasic@;
      |DESCARGA_DEF #canempr@; |ACABA;
   |FINSI;

   aAlfa1 = aAlfa + "def/caivalin.mas";
   |CARGA_DEF aAlfa1, caivali@;
   |SI FSalida ! 0;
      |DESCARGA_DEF #camaniv@;
      |DESCARGA_DEF #caclipr@;
      |DESCARGA_DEF #camandi@;
      |DESCARGA_DEF #cacuent@;
      |DESCARGA_DEF #camasil@;
      |DESCARGA_DEF #camasic@;
      |DESCARGA_DEF #canempr@; |ACABA;
   |FINSI;

   aAlfa1 = aAlfa + "def/caenlace.mas";
   |CARGA_DEF aAlfa1, caenlac@;
   |SI FSalida ! 0;
      |DESCARGA_DEF #caivali@;
      |DESCARGA_DEF #camaniv@;
      |DESCARGA_DEF #caclipr@;
      |DESCARGA_DEF #camandi@;
      |DESCARGA_DEF #cacuent@;
      |DESCARGA_DEF #camasil@;
      |DESCARGA_DEF #camasic@;
      |DESCARGA_DEF #canempr@; |ACABA;
   |FINSI;

   aAlfa = #trasz004#10; |QBF aAlfa;
   aAlfa = aAlfa + "fich/" + (("00000" + #trasz004#7) % -105) + #trasz004#8 + "/";
   |PATH_DAT #101 aAlfa; |PATH_DAT #102 aAlfa;
   |PATH_DAT #103 aAlfa; |PATH_DAT #104 aAlfa; |PATH_DAT #105 aAlfa;
   |PATH_DAT #106 aAlfa; |PATH_DAT #107 aAlfa; |PATH_DAT #108 aAlfa;
   aAlfa = "pu" + Usuario; |NOME_DAT #108 aAlfa;
   aAlfa = #trasz004#10; |QBF aAlfa;
   aAlfa = aAlfa + "fich/"; |PATH_DAT #100 aAlfa;

   |ABRE #canempr@;
   |DELINDEX #camasic@; |ABRE #camasic@;
   |DELINDEX #camasil@; |ABRE #camasil@;
   |DELINDEX #cacuent@; |ABRE #cacuent@;
   |DELINDEX #caclipr@; |ABRE #caclipr@;
   |DELINDEX #camaniv@; |ABRE #camaniv@;
   |DELINDEX #caivali@; |ABRE #caivali@;
   |DELINDEX #camandi@; |ABRE #camandi@;
   |DELINDEX #caenlac@; |ABRE #caenlac@;
   |DELINDEX #trasw001; |ABRE #trasw001;
   |DELINDEX #trasw002;

   aAlfa = #trasz004#3; |QBF aAlfa;
   |SI aAlfa ! "";

|| Creamos los ficheros basicos (partidas, epigrafes, plan contable general ....)

      eaExterno  = #trasz004#10; |QBF eaExterno;
      eaExterno  = eaExterno + "fich/" + (("00000" + #trasz004#7) % -105) + #trasz004#8 + "/";
      enEmpreOri = 0;
      enPerioOri = 0;
      eaCopiar   = "T";
      eaCeros    = "S";
      eaOtras    = "N";
      eaDepar    = "S";
      |SUB_EJECUTA_NP ":contagen/cacrebal";

      #canempr@#2 = #trasz004#7;
      #canempr@#3 = #trasz004#8;
      |LEE 000#canempr@.=;
      |SI FSalida ! 0; |DDEFECTO #canempr@; |FINSI;

|| Pasamos el plan contable

      |ATRI I; |PINTA 2210, " Procesando plan contable ....................."; |ATRI N;
      aCR = &10; aLF = &13;
      aAlfa = #trasz004#0; |QBF aAlfa; aAlfa1 = aAlfa + #trasz004#3; |QBF aAlfa1;
      |XABRE "B", aAlfa1, nHandle;
      |SI FSalida ] 0;
         |HAZ LeeRegistro;
         |PARA; |SI nSwError = 0; |HACIENDO;
            aCuenta = aCampos5; aDescri = aCampos6;
            #cacuent@#0 = aCuenta;
            #cacuent@#2 = aDescri;
            |LEE 000#cacuent@.=;
            |SI FSalida ! 0;
               aAlfa = " Procesando cuenta " + aCuenta + " [" + aDescri + "]";
               |BLIN 23;
               |ATRI I; |PINTA 2310, aAlfa; |ATRI N;
               nCalc = 14 + #canempr@#13;
               |PARA nCont = 14; |SI nCont [ nCalc; |HACIENDO nCont = nCont + 1;
                   nLong = 100 + #canempr@#nCont#;
                   aAlfa = aCuenta % nLong;
                   #cacuent@#0 = aAlfa;
                   |LEE 000#cacuent@.=;
                   |SI FSalida = 0; |Y nLong ! 100;
                      |REPON_FICHA #cacuent@;
                      #cacuent@#0 = aAlfa;
                      #cacuent@#1 = nCont - 13;
                      |LEE 000#cacuent@.=;
                      |SALVA_FICHA #cacuent@;
                   |FINSI;
                |SIGUE;
                |REPON_FICHA #cacuent@;

                aPD = "S"; nNivel = 0; |HAZ SacaNivel;
                nCalc = nNivel + 14;
                |PARA; |SI nCalc [ 19; |HACIENDO nCalc = nCalc + 1;
                   |SI #canempr@#nCalc# ! 0; aPD = "N"; |FINSI;
                |SIGUE;
                #cacuent@#0 = aCuenta;
                #cacuent@#1 = nNivel;
                #cacuent@#2 = aDescri;
                #cacuent@#3 = aPD;
                |GRABA 020#cacuent@.n;
             |FINSI;
             |HAZ LeeRegistro;
         |SIGUE;
      |FINSI;
      |XCIERRA nHandle;
   |FINSI;

|| Pasamos las fichas de Clientes/Proveedores

   |ATRI I; |PINTA 2210, " Procesando ficha de clientes / proveedores ..."; |ATRI N;

   aAlfa = #trasz004#5; |QBF aAlfa;
   |SI aAlfa ! "";
      aCR = &10; aLF = &13;
      aAlfa = #trasz004#0; |QBF aAlfa; aAlfa1 = aAlfa + #trasz004#5; |QBF aAlfa1;
      |XABRE "B", aAlfa1, nHandle;
      |SI FSalida ] 0;
         |HAZ LeeRegistro;
         |PARA; |SI nSwError = 0; |HACIENDO;
            aAlfa = " Procesando cli/pro " + aCampos5 + " [" + aCampos7 + "]";
            |BLIN 23;
            |ATRI I; |PINTA 2310, aAlfa; |ATRI N;

            |DDEFECTO #caclipr@;
            #caclipr@#23 = aCampos6;
            #caclipr@#10 = aCampos5;
            aCuenta = aCampos5; nNivel = 0; |HAZ SacaNivel;  #caclipr@#11 = nNivel;
            |LEE 000#caclipr@.=;
            |SI FSalida ! 0;
               |DDEFECTO #caclipr@;
               #caclipr@#23 = aCampos6;
               #caclipr@#10 = aCampos5;
               aCuenta = aCampos5; nNivel = 0; |HAZ SacaNivel;  #caclipr@#11 = nNivel;
               #caclipr@#0 = aCampos5;
               #caclipr@#1 = aCampos7;
               aAlfa = aCampos10;
               |SI aCampos11 ! ""; aAlfa = aAlfa + aCampos11; |FINSI;
               |SI aCampos12 ! ""; aAlfa = aAlfa + ", " + aCampos12; |FINSI;
               |SI aCampos13 ! ""; aAlfa = aAlfa + ", " + aCampos13; |FINSI;
               |SI aCampos14 ! ""; aAlfa = aAlfa + ", " + aCampos14; |FINSI;
               |SI aCampos15 ! ""; aAlfa = aAlfa + ", " + aCampos15; |FINSI;
               |SI aCampos16 ! ""; aAlfa = aAlfa + ", " + aCampos16; |FINSI;
               |SI aCampos17 ! ""; aAlfa = aAlfa + ", " + aCampos17; |FINSI;
               #caclipr@#2  = aAlfa;
               #caclipr@#3  = aCampos21;
               aAlfa = aCampos19; |QBF aAlfa;
               aAlfa = aAlfa % -103; #caclipr@#4 = aAlfa;
               #caclipr@#5  = aCampos48;
               #caclipr@#6  = aCampos48;
               aAlfa = aCampos23; |QBF aAlfa; aAlfa = aAlfa + aCampos24;
               #caclipr@#7  = aAlfa;
               aAlfa = aCampos25; |QBF aAlfa; aAlfa = aAlfa + aCampos26;
               #caclipr@#8  = aAlfa;
               #caclipr@#9  = aCampos9;
               #caclipr@#10 = aCampos5;
               aCuenta = #caclipr@#10; nNivel = 0; |HAZ SacaNivel; #caclipr@#11 = nNivel;
               aAlfa = aCampos; |QBF aAlfa;
               aAlfa = aAlfa % 0102; #caclipr@#24 = aAlfa;
               #caclipr@#25 = aCampos19;
               #caclipr@#34 = aCampos36;
               #caclipr@#35 = aCampos37;
               #caclipr@#36 = aCampos38;
               |GRABA 020#caclipr@.n;
            |FINSI;

            |HAZ LeeRegistro;
         |SIGUE;
      |FINSI;
      |XCIERRA nHandle;
   |FINSI;

|| Pasamos los diarios
   |ATRI I; |PINTA 2210, " Procesando ficha de diarios .................."; |ATRI N;
   aAlfa = #trasz004#4; |QBF aAlfa;
   |SI aAlfa ! "";
      aCR = &10; aLF = &13;
      aAlfa = #trasz004#0; |QBF aAlfa; aAlfa1 = aAlfa + #trasz004#4; |QBF aAlfa1;
      |XABRE "B", aAlfa1, nHandle;
      |SI FSalida ] 0;
         |HAZ LeeRegistro;
         |PARA; |SI nSwError = 0; |HACIENDO;
            aAlfa = " Procesando diario " + aCampos5 + " [" + aCampos6 + "]";
            |BLIN 23;
            |ATRI I; |PINTA 2310, aAlfa; |ATRI N;

            |DDEFECTO #trasw001;
            #trasw001#0 = aCampos5;
            #trasw001#1 = aCampos6;
            |GRABA 020#trasw001.n;

            |HAZ LeeRegistro;
         |SIGUE;
      |FINSI;
      |XCIERRA nHandle;

      |PUSHV 0501 2380;
      |ENTLINEAL #1#trasw001, -1, 2, 17, 1, inf, sup;
      |POPV;
   |FINSI;

   || Pasamos los asientos

   |ABRE #trasw002;

   |ATRI I; |PINTA 2210, " Procesando fichero de asientos ..............."; |ATRI N;
   aAlfa = #trasz004#6; |QBF aAlfa;
   |SI aAlfa ! "";
      aCR = &10; aLF = &13;
      aAlfa = #trasz004#0; |QBF aAlfa; aAlfa1 = aAlfa + #trasz004#6; |QBF aAlfa1;
      |XABRE "B", aAlfa1, nHandle;
      |SI FSalida ] 0;
         |HAZ LeeRegistro;
         |PARA; |SI nSwError = 0; |HACIENDO;
            |SI aCampos7 ] "8229"; |Y aCampos7 [ "8258"; |DEBUG; |FINSI;
            aAlfa = " Procesando asiento " + aCampos18 + "/" + aCampos8 + "/" + aCampos6;
            |BLIN 23;
            |ATRI I; |PINTA 2310, aAlfa; |ATRI N;

            #trasw001#0 = aCampos18;
            |LEE 000#trasw001.=;
            |SI FSalida = 0;
               |SI #trasw001#2 ! "  "; aCampos18 = #trasw001#2; |FINSI;
            |FINSI;

            #caenlac@#0 = aCampos18;
            #caenlac@#1 = aCampos8;
            #caenlac@#2 = aCampos6;
            #caenlac@#3 = aCampos7;
            #caenlac@#37 = #trasz004#7;
            #caenlac@#38 = #trasz004#8;
            |LEE 000#caenlac@.=;
            |SI FSalida = 0;
               #caenlac@#3 = #caenlac@#3 + 1;
               aCampos7    = #caenlac@#3;
            |FINSI;

            |DDEFECTO #caenlac@;
            #caenlac@#0 = aCampos18;
            #caenlac@#1 = aCampos8;
            #caenlac@#2 = aCampos6;
            #caenlac@#3 = aCampos7;
            #caenlac@#4 = aCampos12; aCuenta = aCampos12;
            nNivel = 0; |HAZ SacaNivel;
            #caenlac@#5 = nNivel;
            #caenlac@#6 = #trasz004#22;
            #caenlac@#7 = aCampos16;
            #caenlac@#8 = aCampos11;
            #caenlac@#9 = aCampos17;
            #caenlac@#37 = #trasz004#7;
            #caenlac@#38 = #trasz004#8;
            |GRABA 020#caenlac@.c;

            |DDEFECTO #trasw002;
            #trasw002#0 = #caenlac@#0;
            #trasw002#1 = #caenlac@#1;
            #trasw002#2 = #caenlac@#2;
            #trasw002#3 = #caenlac@#3;
            #trasw002#4 = "";
            |GRABA 020#trasw002.c;

            |HAZ LeeRegistro;
         |SIGUE;
      |FINSI;
      |XCIERRA nHandle;
   |FINSI;

   || Pasamos los movimientos de factura

   |SELECT #2#trasw002;

   |ATRI I; |PINTA 2210, " Procesando fichero de asientos ..............."; |ATRI N;
   aAlfa = #trasz004#1; |QBF aAlfa;
   |SI aAlfa ! "";
      aCR = &10; aLF = &13;
      aAlfa = #trasz004#0; |QBF aAlfa; aAlfa1 = aAlfa + #trasz004#1; |QBF aAlfa1;
      |XABRE "B", aAlfa1, nHandle;
      |SI FSalida ] 0;
         |HAZ LeeRegistro;
         |PARA; |SI nSwError = 0; |HACIENDO;

            aAlfa = " Procesando factura " + aCampos8 + "/" + aCampos9;
            |BLIN 23;
            |ATRI I; |PINTA 2310, aAlfa; |ATRI N;

            #trasw002#2 = aCampos6;
            #trasw002#3 = aCampos7;
            |LEE 000#trasw002.=;
            |SI FSalida = 0;
               #caenlac@#0  = #trasw002#0;
               #caenlac@#1  = #trasw002#1;
               #caenlac@#2  = #trasw002#2;
               #caenlac@#3  = #trasw002#3;
               #caenlac@#37 = #trasz004#7;
               #caenlac@#38 = #trasz004#8;
               |LEE 000#caenlac@.=;
               |SI FSalida = 0;
                  |SI aCampos13 = "R";
                     #caenlac@#11 = #trasz004#13;
                     |SI aCampos22 = "0";
                        nConcepto = #trasz004#14;
                        aTipo = "S";
                        #caenlac@#26 = "F";
                     |SINO;
                        nConcepto = #trasz004#20;
                        aTipo = "R";
                     |FINSI;
                     #caenlac@#12 = "";
                     #caenlac@#13 = nNumSop; nNumSop = nNumSop + 1;
                     |SI aCampos8 ! "";
                        aAlfa = aCampos8 + "/" + aCampos9;
                     |SINO;
                        aAlfa = aCampos9;
                     |FINSI;
                     #caenlac@#45 = aAlfa;
                  |FINSI;
                  |SI aCampos13 = "E";
                     #caenlac@#11 = #trasz004#12;
                     |SI aCampos22 = "0";
                        nConcepto = #trasz004#16;
                        aTipo = "R";
                        #caenlac@#26 = "F";
                     |SINO;
                        nConcepto = #trasz004#18;
                        aTipo = "S";
                     |FINSI;
                     #caenlac@#12 = aCampos8;
                     #caenlac@#13 = aCampos9;
                     #caenlac@#45 = "";
                  |FINSI;
                  #caenlac@#29 = aCampos10;
                  |SI aCampos22 = "0";
                     #caenlac@#30 = "N";
                     nDiario    = #caenlac@#0;
                     fFecha     = #caenlac@#1;
                     nDocumento = #caenlac@#2;
                     |SALVA_FICHA #caenlac@;
                     |BUCLE Iva;
                     |REPON_FICHA #caenlac@;
                     #caenlac@#10 = aTipo;
                     #caenlac@#6  = nConcepto;
                  |SINO;
                     nDiario    = #caenlac@#0;
                     fFecha     = #caenlac@#1;
                     nDocumento = #caenlac@#2;
                     |SALVA_FICHA #caenlac@;
                     |BUCLE IvaIntrac;
                     |REPON_FICHA #caenlac@;
                     #caenlac@#30 = "S";
                     |SI #caenlac@#10 = "R";
                        #caenlac@#6 = #trasz004#20;
                        #caenlac@#34 = #trasz004#13;
                     |SINO;
                        #caenlac@#6 = #trasz004#18;
                        #caenlac@#34 = #trasz004#12;
                     |FINSI;
                     #caenlac@#35 = aCampos8;
                     #caenlac@#36 = aCampos9;
                  |FINSI;
                  #caenlac@#43 = aCampos16;
                  aAlfa = aCampos15; |QBF aAlfa;
                  |SI aAlfa ! "";
                     #caenlac@#44 = aCampos15;
                  |SINO;
                     |SI aCampos13 = "R"; aAlfa = "P"; |FINSI;
                     |SI aCampos13 = "E"; aAlfa = "C"; |FINSI;
                     #caclipr@#23 = aAlfa;
                     #caclipr@#10 = aCampos14;
                     aCuenta = aCampos14; nNivel = 0; |HAZ SacaNivel;
                     #caclipr@#11 = nNivel;
                     |LEE 000#caclipr@.=;
                     |SI FSalida = 0;
                        #caenlac@#44 = #caclipr@#9;
                     |FINSI;
                  |FINSI;
                  |GRABA 020#caenlac@.a;
               |FINSI;
            |FINSI;

            |HAZ LeeRegistro;
         |SIGUE;
      |FINSI;
      |XCIERRA nHandle;
   |FINSI;

   || Pasamos los movimientos de iva

   |ATRI I; |PINTA 2210, " Procesando fichero de asientos ..............."; |ATRI N;
   aAlfa = #trasz004#2; |QBF aAlfa;
   |SI aAlfa ! "";
      aCR = &10; aLF = &13;
      aAlfa = #trasz004#0; |QBF aAlfa; aAlfa1 = aAlfa + #trasz004#2; |QBF aAlfa1;
      |XABRE "B", aAlfa1, nHandle;
      |SI FSalida ] 0;
         |HAZ LeeRegistro;
         |PARA; |SI nSwError = 0; |HACIENDO;
            |SI aCampos6 ! aDato1; |O aCampos7 ! aDato2;
               nIva = 1;
               aDato1 = aCampos6; aDato2 = aCampos7;
            |FINSI;
            aAlfa = " Procesando movimientos de iva [" + aCampos6 + "/" + aCampos7 + "]";

            |BLIN 23;
            |ATRI I; |PINTA 2310, aAlfa; |ATRI N;

            nLibro = 0; aSerie = ""; nFactu = 0;
            #trasw002#2 = aCampos6;
            #trasw002#3 = aCampos7;
            |LEE 000#trasw002.=;
            |SI FSalida = 0;
               nMarca = 0;

               #caenlac@#0 = #trasw002#0;
               #caenlac@#1 = #trasw002#1;
               #caenlac@#2 = #trasw002#2;
               #caenlac@#3 = #trasw002#3;
               #caenlac@#37 = #trasz004#7;
               #caenlac@#38 = #trasz004#8;
               |LEE 000#caenlac@.=;
               |PARA; |SI FSalida = 0; |Y #caenlac@#0 = #trasw002#0;
                       |Y #caenlac@#1 = #trasw002#1; |Y #caenlac@#2 = #trasw002#2;
                       |Y #caenlac@#37 = #trasz004#7; |Y #caenlac@#38 = #trasz004#8; |HACIENDO;
                  |SI #caenlac@#26 = "F";
                     nLibro = #caenlac@#11;
                     aSerie = #caenlac@#12;
                     nFactu = #caenlac@#13;
                  |FINSI;
                  |SI #caenlac@#26 = " ";
                     nCalc1 = #caenlac@#9; nCalc2 = aCampos10;
                     |SI nCalc1 = nCalc2;
                        #caenlac@#26 = "B"; #caenlac@#27 = 1; #caenlac@#28 = aCampos12;
                        nMarca = 1;
                     |FINSI;
                     nCalc2 = aCampos13;
                     |SI nCalc1 = nCalc2;
                        #caenlac@#26 = "I";
                     |FINSI;
                     nCalc2 = aCampos15;
                     |SI nCalc1 = nCalc2;
                        #caenlac@#26 = "R"; #caenlac@#27 = 1; #caenlac@#33 = aCampos14;
                        nMarca = 1;
                     |FINSI;
                     #caenlac@#11 = nLibro;
                     #caenlac@#12 = aSerie;
                     #caenlac@#13 = nFactu;
                  |FINSI;
                  |SI aCampos17 = "-1";  #caenlac@#31 = "S"; |FINSI;
                  |SI aCampos17 = "0";   #caenlac@#31 = "N"; |FINSI;
                  |GRABA 020#caenlac@.a;

                  |LEE 000#caenlac@.s;
               |SIGUE;

               |SI nMarca = 0;
                  #caenlac@#0 = #trasw002#0;
                  #caenlac@#1 = #trasw002#1;
                  #caenlac@#2 = #trasw002#2;
                  #caenlac@#3 = #trasw002#3;
                  #caenlac@#37 = #trasz004#7;
                  #caenlac@#38 = #trasz004#8;
                  |LEE 000#caenlac@.=;
                  |PARA; |SI FSalida = 0; |Y #caenlac@#0 = #trasw002#0;
                       |Y #caenlac@#1 = #trasw002#1; |Y #caenlac@#2 = #trasw002#2;
                       |Y #caenlac@#37 = #trasz004#7; |Y #caenlac@#38 = #trasz004#8; |HACIENDO;
                     |SI #caenlac@#26 = " ";
                        aAlfa = #caenlac@#4; |QBF aAlfa;
                        aAlfa = aAlfa % 0101;
                        |SI #caenlac@#10 = "S"; aMask = "6"; |SINO; aMask = "7"; |FINSI;
                        |SI aAlfa = aMask;
                           #caenlac@#26 = "B"; #caenlac@#28 = aCampos12;
                           #caenlac@#27 = nIva;
                           #caenlac@#9  = aCampos10;
                           |SALVA_FICHA #caenlac@;
                           nLinea = #caenlac@#3 + 1;
                           #caenlac@#3 = nLinea;
                           |LEE 000#caenlac@.=;
                           |PARA; |SI FSalida = 0; |HACIENDO;
                              nLinea = #caenlac@#3 + 1;
                              #caenlac@#3 = nLinea;
                              |LEE 000#caenlac@.=;
                           |SIGUE;
                           |REPON_FICHA #caenlac@;
                           nIva   = nIva        + 1;
                           |GRABA 020#caenlac@.a;
                           |SAL;
                        |FINSI;
                     |SINO;
                        |SI #caenlac@#26 = "B"; |Y #caenlac@#30 = "N";
                           #caenlac@#3 = nLinea; nLinea = #caenlac@#3 + 1;
                           #caenlac@#27 = nIva;
                           #caenlac@#28 = aCampos12;
                           #caenlac@#9  = aCampos10;
                           nIva   = nIva        + 1;
                           |GRABA 020#caenlac@.n;
                           |SAL;
                        |FINSI;
                     |FINSI;
                     |LEE 000#caenlac@.s;
                  |SIGUE;
               |FINSI;
            |FINSI;

            |HAZ LeeRegistro;
         |SIGUE;
      |FINSI;
      |XCIERRA nHandle;
   |FINSI;

   |SELECT #1#trasw002;
   |CIERRA #trasw002;

   |CIERRA #trasw001; |DELINDEX #trasw001;

   |CIERRA #caenlac@; |DESCARGA_DEF #caenlac@;
   |CIERRA #caivali@; |DESCARGA_DEF #caivali@;
   |CIERRA #camaniv@; |DESCARGA_DEF #camaniv@;
   |CIERRA #caclipr@; |DESCARGA_DEF #caclipr@;
   |CIERRA #camandi@; |DESCARGA_DEF #camandi@;
   |CIERRA #cacuent@; |DESCARGA_DEF #cacuent@;
   |CIERRA #camasil@; |DESCARGA_DEF #camasil@;
   |CIERRA #camasic@; |DESCARGA_DEF #camasic@;
   |CIERRA #canempr@; |DESCARGA_DEF #canempr@;

   |BLIN 22; |BLIN 23;

   enNoModif  = 100;
   aAlfa1 = #trasz004#10; |QBF aAlfa1;
   aAlfa1 = aAlfa1 + "fich/" + ((("00000" + #trasz004#7) % -105) + #trasz004#8) + "/";
   aAlfa = ":contagen/dsapunte.tab;" + aAlfa1 + ",pu" + Usuario;
   |SUB_EJECUTA_NP aAlfa;
|FPRC;

|PROCESO PintaLibro;
   nTecla = FSalida; |SI nTecla = 2; |ACABA; |FINSI;
   aAlfa = #trasz004#10; |QBF aAlfa;
   aAlfa1 = aAlfa + "def/calibros.mas";
   |CARGA_DEF aAlfa1, calibro@;
   |SI FSalida ! 0;
      |MENAV "    Problemas para cargar el def de libros"; |ERROR; |ACABA;
   |FINSI;
   aAlfa = #trasz004#10; |QBF aAlfa;
   aAlfa = aAlfa + "fich/" + (("00000" + #trasz004#7) % -105) + #trasz004#8 + "/";
   |PATH_DAT #109 aAlfa;
   |ABRE #calibro@;
   |SI nTecla = 10;
      |CONSULTA_CLAVE #1#calibro@;
      |SI FSalida = 0;
         #trasz004#Campo# = #calibro@#0;
      |FINSI;
   |FINSI;
   #calibro@#0 = #trasz004#Campo#;
   |LEE 000#calibro@.=;
   |SI FSalida = 0;
      |C_P #trasz004#Campo#, 0, nCalc; nCalc = nCalc + 3;
      |PINTA nCalc, #calibro@#1;
      |SI #calibro@#2 = "R";
         |SI Campo ! 12; |Y Campo ! 14;
            |MENAV "    Libro incorrecto"; |ERROR;
         |FINSI;
      |SINO;
         |SI Campo ! 13; |Y Campo ! 15;
            |MENAV "    Libro incorrecto"; |ERROR;
         |FINSI;
      |FINSI;
   |SINO;
      |MENAV "    Libro inexistente"; |ERROR;
   |FINSI;
   |CIERRA #calibro@; |DESCARGA_DEF #calibro@;
|FPRC;

|PROGRAMA;
   |ABRE #trasz004;
   |LEE 000#trasz004.p;
   |SI FSalida ! 0;
      |DDEFECTO #trasz004;
      |GRABA 020#trasz004.c;
   |FINSI;

   |CLS;
   |CABEZA " Traspaso de datos de Logic / Diagram (Contagen)";
   |PINPA #0#0; |PINDA #0#0;
   |ENDATOS #1#0;
   |SI FSalida = 0;
      |GRABA 020#trasz004.a;
      |CONFI "    NLos datos de la empresa indicada seran eliminados. Continuar ?";
      |SI FSalida = 0;
         |PUSHV 0501 2380;
         |BLANCO 2102 2378;
         |CUADRO 2102 2378;
         |HAZ Traspaso;
         |POPV;
      |FINSI;
   |FINSI;
   |CIERRA #trasz004;
|FPRO;
