|FICHEROS;
     :integral/dsam0000 #0;
     ensam001 #1;
     ensam002 #2;
     ensam003 #3;
     ensam004 #4;
     ensam502 #502;

     ensaz003 #99;
     ensaw004 #904;
|FIN;

|VARIABLES;
     aFichero      = "";
     aMarca        = "";
     aEsc1         = "";
     aEsc2         = "";
     aRetu         = "";
     aTecla        = "";
     aEntregar     = "";
     aD            = "";
     aH            = "zzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzz";

     nRango        = 0;
     nPos1         = 0;
     nPos2         = 0;
     nPanta        = 0;
     nGrid         = 0;
     nHay          = 0;
     nDCliente     = 0;
     nHCliente     = 0;
     nDCarpeta     = 0;
     nHCarpeta     = 0;
     nDLinea       = 0;
     nHLinea       = 0;
     nCliente      = 0;
     nVentana      = 0;
     nBoton1       = 0;
     nBoton2       = 0;
     nBoton3       = 0;
     nBoton4       = 0;
     nBoton5       = 0;

     &enDExp       = 0;
     &enHExp       = 0;
|FIN;

|PROCESO ensaw004A;
     #2#0 = #904#0;
     #2#1 = #904#1;
     #2#2 = #904#2;
     |LEE 101#2=;
     |SI FSalida = 0;
         #2#6 = "ENTREGADO";
         |GRABA 020#2a;
         |LIBERA #2;
     |FINSI;

     |SI nCliente ! #904#0;  |Y nCliente ! 0;
         #99#7     = #99#7 + 1;
         aEntregar = #904#12;
     |FINSI;

     |SI aEntregar ! #904#12;  |Y aEntregar ! "";
         #99#7     = #99#7 + 1;
     |FINSI;

     nCliente  = #904#0;
     aEntregar = #904#12;

     |DDEFECTO #3;
     #3#0 = #2#0;
     #3#1 = #2#1;
     #3#2 = #2#2;
     #3#3 = #99#7;
     #3#4 = #99#0;
     #3#5 = #99#6;
     |GRABA 020#3n;
     |LIBERA #3;

     #4#0 = #3#3;
     |LEE 101#4=;
     |SI FSalida ! 0;
         #4#0  = #3#3;
         #4#1  = #3#0;
         #4#2  = #904#7;
         #4#3  = #3#4;
         #4#4  = #3#5;
         #4#5  = #904#12;
         #4#6  = #904#4;
         #4#7  = #99#8;
         #4#8  = #99#9;
         #4#9  = #99#10;
         #4#10 = #99#11;
         #4#11 = #99#12;
         |GRABA 020#4n;
     |FINSI;
     |LIBERA #4;
|FPRC;

|DEFBUCLE ensaw004A;
     #904#3;
     ;
     "*", 00000, aD;
     "*", 99999, aH;
     ;
     NULL, ensaw004A;
|FIN;

|PROCESO ensam002;
     #1#0 = #2#0;
     #1#1 = #2#1;
     |LEE 000#1=;
     |SI FSalida ! 0;  |DDEFECTO #1;  |FINSI;

     #502#0 = #1#1;
     |LEE 000#502=;
     |SI FSalida ! 0;  |DDEFECTO #502;  |FINSI;

     |DDEFECTO #904;
     #904#0  = #2#0;
     #904#1  = #2#1;
     #904#2  = #2#2;
     #904#3  = "";
     #904#4  = #2#3;
     #904#5  = #2#4;
     #904#6  = #2#5;
     #904#7  = #0#1;
     #904#8  = #502#1;
     #904#9  = #0#109;
     #904#10 = #0#106;
     #904#11 = #0#107;
     #904#12 = #1#6;
     |GRABA 020#904n;
     |LIBERA #904;

     |SI #1#2 ! #502#1;
         #1#0 = #2#0;
         #1#1 = #2#1;
         |LEE 101#1=;
         |SI FSalida = 0;
             #1#2 = #502#1;
             |GRABA 020#1a;
             |LIBERA #1;
         |FINSI;
     |FINSI;

     nHay = 1;
|FPRC;

|DEFBUCLE ensam002;
     #2#3;
     ;
     #0#0, "PENDIENTE ";
     #0#0, "PENDIENTE ";
     ;
     NULL, ensam002;
|FIN;

|PROCESO dsam0000;
     |ABRE #1;
     |ABRE #502;
     |BUCLE ensam002;
     |CIERRA #1;
     |CIERRA #502;
|FPRC;

|DEFBUCLE dsam0000;
     #0#4;
     ;
     #99#2, #99#4, 00001;
     #99#3, #99#5, 99999;
     ;
     NULL, dsam0000;
|FIN;

|PROCESO ensaw004;
     #904#3 = aMarca;
     |GRABA 020#904a;
     |LIBERA #904;
|FPRC;

|DEFBUCLE ensaw004;
     #904#1;
     ;
     nDCliente, nDCarpeta, nDLinea;
     nHCliente, nHCarpeta, nHLinea;
     be;
     NULL, ensaw004;
|FIN;

|PROCESO Boton1;
     nDCliente = 1;
     nHCliente = 99999;
     nDCarpeta = 1;
     nHCarpeta = 999;
     nDLinea   = 1;
     nHLinea   = 9999;
     aMarca    = "*";

     |BUCLE ensaw004;

     |REFRESCACONTROL nGrid;
|FPRC;

|PROCESO Boton2;
     nDCliente = 1;
     nHCliente = 99999;
     nDCarpeta = 1;
     nHCarpeta = 999;
     nDLinea   = 1;
     nHLinea   = 9999;
     aMarca    = " ";

     |BUCLE ensaw004;

     |REFRESCACONTROL nGrid;
|FPRC;

|PROCESO Boton3;
     nDCliente = #904#0;
     nHCliente = #904#0;
     nDCarpeta = #904#1;
     nHCarpeta = #904#1;
     nDLinea   = #904#2;
     nHLinea   = #904#2;
     aMarca    = "*";

     |BUCLE ensaw004;

     #904#0 = nDCliente;
     #904#1 = nDCarpeta;
     #904#2 = nDLinea;
     |LEE 000#904=;

     |REFRESCACONTROL nGrid;

     |PTEC 809;
     |PTEC 809;
|FPRC;

|PROCESO Boton4;
     nDCliente = #904#0;
     nHCliente = #904#0;
     nDCarpeta = #904#1;
     nHCarpeta = #904#1;
     nDLinea   = #904#2;
     nHLinea   = #904#2;
     aMarca    = " ";

     |BUCLE ensaw004;

     #904#0 = nDCliente;
     #904#1 = nDCarpeta;
     #904#2 = nDLinea;
     |LEE 000#904=;

     |REFRESCACONTROL nGrid;
|FPRC;

|PROCESO Boton5;
     |CONFI "0000NSeguro de BORRAR DEFINITIVAMENTE el Documento.";
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |ABRE #2;
     #2#0 = #904#0;
     #2#1 = #904#1;
     #2#2 = #904#2;
     |LEE 101#2=;
     |SI FSalida = 0;
         |BORRA 020#2a;
         |LIBERA #2;
     |FINSI;
     |CIERRA #2;

     |LEE 101#904=;
     |SI FSalida = 0;
         |BORRA 020#904a;
         |LIBERA #904;
     |FINSI;

     |REFRESCACONTROL nGrid;
|FPRC;

|PROCESO Botones;
     |CONTROL_SIMPLE nPanta, "BOTON,ST", 1102, 1105, Boton1;
     nBoton1 = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,QT", 1107, 1110, Boton2;
     nBoton2 = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,SL", 1112, 1115, Boton3;
     nBoton3 = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,QL", 1117, 1120, Boton4;
     nBoton4 = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,{{207}} Borrar el Documento Definitivamente", 1140, 1170, Boton5;
     nBoton5 = FSalida;

     |ESTADO_CONTROL nBoton5, "DISABLE";
|FPRC;

|PROCESO Evento;
     |SI FSalida = 1;
         |LEE 000#904=;
         |SI FSalida ! 0;  |DDEFECTO #904;  |FINSI;

         |ABRE #3;
         #3#0 = #904#0;
         #3#1 = #904#1;
         #3#2 = #904#2;
         #3#3 = 1;
         |LEE 000#3];
         |SI FSalida = 0;  |Y  #3#0 = #904#0; |Y  #3#1 = #904#1; |Y #3#2 = #904#2;
             |ESTADO_CONTROL nBoton5, "DISABLE";
         |SINO;
             |ESTADO_CONTROL nBoton5, "ENABLE";
         |FINSI;
         |CIERRA #3;
     |FINSI;
|FPRC;

|PROCESO BajaPanta1;
     #904#0 = 00001;
     #904#1 = 001;
     #904#2 = 0001;
|FPRC;

|PROCESO AltaPanta1;
     #904#0 = 99999;
     #904#1 = 999;
     #904#2 = 9999;
|FPRC;

|PROCESO Tipo12;  |TIPO 12;
     |SI #99#7 = 0;
         |MENAV "0000 No se ha asignado Numero de Expediente";
         |ACABA;
     |FINSI;

     nHay = 0;
     |BUCLE dsam0000;
     |SI nHay = 0;
         |MENAV "0000 Seleccion Vacia.";
         |ACABA;
     |FINSI;

     |HAZ Botones;

     |LEE 000#904p;

     nRango = 4 + 8 + 16 + 32;
     nPos1  = 1201;
     nPos2  = 2698;

     |LINEAL_SIMPLE #1#904, nRango, nPos1, nPos2, BajaPanta1, AltaPanta1, Evento;
     nGrid = FSalida;

     aEsc1  = &255 + "806";
     aEsc2  = &255 + "807";
     aRetu  = &255 + "802";

     |PARA; |SI;  |HACIENDO;
         FSalida = "::Salir," + nGrid;
         |LEETECLA aTecla;
         |SI aTecla = aEsc1; |SAL;  |FINSI;
         |SI aTecla = aEsc2; |SAL;  |FINSI;
         |SI aTecla = aRetu; |SAL;  |FINSI;
     |SIGUE;

     |FIN_CONTROL_WINDOWS nBoton1;
     |FIN_CONTROL_WINDOWS nBoton2;
     |FIN_CONTROL_WINDOWS nBoton3;
     |FIN_CONTROL_WINDOWS nBoton4;
     |FIN_CONTROL_WINDOWS nBoton5;

     |SELECT #2#904;
     #904#3 = "*";
     |LEE 000#904=;
     |SI FSalida = 0;
         |CONFI "0000 Conforme con la Seleccion";
         |SI FSalida ! 0;  |ACABA;  |FINSI;

         nCliente   = 0;
         aEntregar  = "";
         enDExp     = #99#7;

         |ABRE #2;
         |ABRE #3;
         |ABRE #4;
         |BUCLE ensaw004A;
         |CIERRA #2;
         |CIERRA #3;
         |CIERRA #4;

         enHExp     = #99#7;

         |CONFI "0000SEmitir los Expedientes Generados ";
         |SI FSalida = 0;
             |SUB_EJECUTA "ensaz004;AUTOMA";
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO Expediente;
     |SI #99#7 = 0;
         |MENAV "0000 Introduzca el Numero Expediente que a partir de ahora vamos a incrementar";
         |ERROR;
     |FINSI;
|FPRC;

|PROCESO Tipo80;  |TIPO 80;
     FSalida = 2799;
|FPRC;

|PROGRAMA;
     enDExp   = 0;
     enHExp   = 0;

     aFichero = ("04" + Usuario + "zzzzzzzz") % 108;
     |NOME_DAT #904 aFichero;

     |ABRE #904;  |CIERRA #904;  |DELINDEX #904;

     |ABRET #904;

     |CABEZA "Salidad";

     |DDEFECTO #99;
     #99#0 = Usuario % 810;

     |C_M #99#7, 1, "S";
     |ABRE #3;
     |SELECT #2#3;
     |LEE 000#3u;
     |SI FSalida = 0;
         #99#7 = #3#3 + 1;
         |C_M #99#7, 1, "N";
     |FINSI;
     |CIERRA #3;

     |PINPA #0#99;  nPanta = FSalida;
     |PINDA #0#99;

     |ENDATOS #1#99;

     |CIERRAT #904;
     |DELINDEX #904;

     |PULSATECLA;
|FPRO;
