|FICHEROS;
     :integral/dsam0000 #0;
     :integral/dsam0108 #108;

     ensam001 #1;
     ensam002 #2;
     ensam003 #3;
     ensam501 #501;
     ensam502 #502;

     ensaz006 #99;
     ensaw008 #908;
|FIN;

|VARIABLES;
     aFichero    = "";
     aBase       = "";
     aTecnico    = "";
     aDTecnico   = "";
     aHTecnico   = "";
     aUsuario    = "";
     aDirUsuario = "";
     aIPServ     = "";
     aServCorreo = "";
     aFrom       = "";
     aTo         = "";
     aAsunto     = "";
     aMensaje    = "";
     aAlfa       = "";

     nDias     = 0;
     nHay      = 0;
     nTipo     = 0;

     &eaTitulo = "";
|FIN;

|PROCESO Tipo7C2Fz6;  |TIPO 7;
     |C_M #99#2, 1, "S";
     |C_M #99#3, 1, "S";

     |SI #99#1 = "I";
         |C_M #99#2, 1, "N";
         |C_M #99#3, 1, "N";
     |FINSI;
|FPRC;

|PROCESO ensam002;
     #0#0 = #2#0;
     |LEE 000#0=;
     |SI FSalida ! 0;  |DDEFECTO #0;  |FINSI;

     #1#0 = #2#0;
     #1#1 = #2#1;
     |LEE 000#1=;
     |SI FSalida ! 0;  |DDEFECTO #1;  |FINSI;

     #501#0 = #1#3;
     |LEE 000#501=;
     |SI FSalida ! 0;  |DDEFECTO #501;  |FINSI;

     #108#0 = #2#3;
     |LEE 000#108=;
     |SI FSalida ! 0;  |DDEFECTO #108;  |FINSI;

     |SI #99#0 = "V";
         |SI #2#5 > #99#5;  |ACABA;  |FINSI;
     |FINSI;

     |SI #99#0 = "A";
         nDias = #2#5 - #99#5;
         |SI nDias < 0;        |ACABA;   |FINSI;
         |SI nDias > #501#15;    |ACABA;   |FINSI;
     |FINSI;

     #502#0 = #1#1;
     |LEE 000#502=;
     |SI FSalida ! 0;  |DDEFECTO #502;  |FINSI;

     #908#0  = #2#3;
     #908#1  = #2#0;
     #908#2  = #2#1;
     #908#3  = #2#2;
     #908#4  = #2#4;
     #908#5  = #2#5;
     #908#6  = #108#1;
     #908#7  = #0#1;
     #908#8  = #502#1;
     #908#9  = #2#5 - #99#5;
     #908#10 = #0#109;

     |GRABA 020#908n;
     |LIBERA #908;

     |SI #1#2 ! #502#1;
         #1#0 = #2#0;
         #1#1 = #2#1;
         |LEE 101#1=;
         |SI FSalida = 0;
             #1#2 = #502#1;
             |GRABA 020#1a;
             |LIBERA #1;
         |FINSI;
     |FINSI;

     nHay = 1;
|FPRC;

|DEFBUCLE ensam002;
     #2#4;
     ;
     "PENDIENTE ", 00000, 000, 0000;
     "PENDIENTE ", 99999, 999, 9999;
     ;
     NULL, ensam002;
|FIN;

|PROCESO ensaw008;
     |SI nTipo = 1;
         |SI aTecnico ! #908#0;  |Y aTecnico ! "";
             |PIEINF;
         |FINSI;

         |PRINT;
         aTecnico = #908#0;
     |FINSI;

     |SI nTipo = 2;
         |PRINT;

         nHay = 1;
     |FINSI;
|FPRC;

|DEFBUCLE ensaw008;
     #908#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, ensaw008;
|FIN;

|PROCESO PorImpresora;
     nTipo = 1;

     |IMPRE 0;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |INFOR "ensaz006";
     |SI FSalida ! 0;  |FINIMP;  |ACABA;  |FINSI;

     aDTecnico = "";
     aHTecnico = "zzzzzzzz";
     |BUCLE ensaw008;

     |PIEINF;
     |FININF;
     |FINIMP;
|FPRC;

|PROCESO Correo;
     aFrom       = #108#3;
     aIPServ     = #99#2;
     aServCorreo = #99#3;
     aTo         = #108#3;
     |SI #99#0 = "A";
         aAsunto     = "Incidencias de Entregas a punto de Vencer";
         aMensaje    = aAsunto;
     |SINO;
         aAsunto     = "Incidencias de Entregas Vencidas";
         aMensaje    = aAsunto;
     |FINSI;

     |QBT aIPServ;
     |QBF aServCorreo;
     |QBF aFrom;
     |QBF aTo;
     |QBF aAsunto;
     |QBF aMensaje;

     |DSCORREO_ENVIA aIPServ, aServCorreo, aFrom, aTo, aAsunto, aMensaje, aFichero;
|FPRC;

|PROCESO dsam0108;
     aUsuario     = #108#0;  |QBF aUsuario;
     aDirUsuario  = #108#3;  |QBF aDirUsuario;
     |SI aDirUsuario  = "";  |ACABA;  |FINSI;

     |ABRE #908;
     |SELECT #2#908;
     #908#0 = #108#0;
     |LEE 000#908=;
     |SI FSalida ! 0;
         |CIERRA #908;
         |ACABA;
     |FINSI;
     |CIERRA #908;

     |DBASE aBase;  |QBF aBase;
     aFichero  = aBase + "correos";        |MKDIR aFichero;
     aFichero  = aFichero + "/" + aUsuario + ".txt";
     |FBORRA aFichero;

     Impresora = aFichero;
     |IMPRE -77;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |INFOR "ensaz006";
     |SI FSalida ! 0;  |FINIMP;  |ACABA;  |FINSI;

     nHay      = 0;
     aDTecnico = #108#0;
     aHTecnico = #108#0;
     |BUCLE ensaw008;

     |SI nHay = 1; |PIEINF;  |FINSI;
     |FININF;
     |FINIMP;

     |SI nHay = 1;
         |HAZ Correo;
     |FINSI;

     |FBORRA aFichero;
|FPRC;

|DEFBUCLE dsam0108;
     #108#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, dsam0108;
|FIN;

|PROCESO PorCorreo;
     aAlfa = #99#2;  |QBF aAlfa;
     |SI aAlfa = "";  |ACABA;  |FINSI;

     aAlfa = #99#3;  |QBF aAlfa;
     |SI aAlfa = "";  |ACABA;  |FINSI;

     nTipo = 2;
     |BUCLE dsam0108;
|FPRC;

|PROCESO Tipo12;  |TIPO 12;
     |SI FSalida = 7;  |ACABA;  |FINSI;

     |SI #99#0 = "A";
         eaTitulo = "LISTADO DE ENTREGAS A PUNTO DE VENCER";
     |SINO;
         eaTitulo = "LISTADO DE ENTREGAS VENCIDAS";
     |FINSI;

     aFichero = ("08" + Usuario + "zzzzzzzz") % 108;
     |NOME_DAT #908 aFichero;

     |ABRE #908;  |CIERRA #908;  |DELINDEX #908;

     nHay = 0;

     |ABRE #908;
     |ABRE #0;
     |ABRE #1;
     |ABRE #3;
     |ABRE #501;
     |ABRE #502;
     |ABRE #108;
     |BUCLE ensam002;
     |CIERRA #0;
     |CIERRA #1;
     |CIERRA #3;
     |CIERRA #501;
     |CIERRA #502;
     |CIERRA #108;
     |CIERRA #908;

     |SI nHay = 1;
         |SI #99#1 = "I";  |HAZ PorImpresora;    |FINSI;
         |SI #99#1 = "C";  |HAZ PorCorreo;       |FINSI;
     |FINSI;

     |DELINDEX #908;
|FPRC;

|PROCESO Tipo80;  |TIPO 80;
     FSalida = 2799;
|FPRC;

|PROGRAMA;
     |ABRET #99;
     |LEE 101#99p;
     |SI FSalida ! 0;
         |DDEFECTO #99;
         |GRABA 020#99b;
     |FINSI;

     |SI PARAMETRO = "VENCIDOS";
         #99#0 = "V";
         #99#1 = "C";
         #99#5 = ~;
         |HAZ Tipo12;
     |FINSI;

     |SI PARAMETRO = "PORVENCER";
         #99#0 = "A";
         #99#1 = "C";
         #99#5 = ~;
         |HAZ Tipo12;
     |FINSI;

     |SI PARAMETRO = "";
         |CABEZA "Emision Incidencias";

         #99#5 = ~;

         |PINPA #0#99;
         |PINDA #0#99;
         |ENDATOS #1#99;
     |FINSI;

     |GRABA 020#99a;
     |LIBERA #99;
     |CIERRAT #99;
|FPRO;
