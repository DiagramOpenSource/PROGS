|FICHEROS;
     :integral/dsam0000 #0;
     ensam001 #1;
     ensam002 #2;
     ensam003 #3;
     ensam004 #4;
     ensaz004 #99;
|FIN;

|VARIABLES;
     nContador        = 0;
     nConta           = 0;
     nExp             = 0;
     nHandle          = 0;

     aBase            = "";
     aCrystal         = "";

     &enDExp          = 0;
     &enHExp          = 0;
     &XVAR_DC_IMPRIME = 0;
|FIN;

|PROCESO ensam003;
     #0#0 = #3#0;
     |LEE 000#0=;
     |SI FSalida ! 0;  |DDEFECTO #0;  |FINSI;

     #1#0 = #3#0;
     #1#1 = #3#1;
     |LEE 000#1=;
     |SI FSalida ! 0;  |DDEFECTO #1;  |FINSI;

     #2#0 = #3#0;
     #2#1 = #3#1;
     #2#2 = #3#2;
     |LEE 000#2=;
     |SI FSalida ! 0;  |DDEFECTO #2;  |FINSI;

     nContador = nContador + 1;
     |PRINT;
|FPRC;

|DEFBUCLE ensam003;
     #3#2;
     ;
     nExp;
     nExp;
     ;
     NULL, ensam003;
|FIN;

|PROCESO Tipo12;  |TIPO 12;
     |SI FSalida = 7;  |ACABA;  |FINSI;

     |DBASE aBase;  |QBF aBase;
     aCrystal = aBase + "def/ensay001.rpt";
     |XABRE "E", aCrystal, nHandle;
     |SI FSalida ] 0;
         ||XVAR_DC_IMPRIME = 1;            || Directo por Impresora
         XVAR_DC_IMPRIME = 0;            || Previsualizacion
         Impresora = "CrystalReport";
         |IMPRE -1;
     |SINO;
         |IMPRE 0;
     |FINSI;

     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |INFOR "ensay001";
     |SI FSalida ! 0;  |FINIMP;  |ACABA;  |FINSI;

     |ABRE #0;
     |ABRE #1;
     |ABRE #2;
     |ABRE #4;

     |PARA nExp = #99#0;  |SI nExp [ #99#1;  |HACIENDO nExp = nExp + 1;
           nContador = 0;
           |BUCLE ensam003;

           #4#0 = nExp;
           |LEE 000#4=;
           |SI FSalida ! 0;  |DDEFECTO #4;  |FINSI;

           #1#2 = "";
           |PARA nConta = nContador;  |SI nConta [ 16;  |HACIENDO nConta = nConta + 1;
                 |PRINT;
           |SIGUE;
           |PIEINF;
     |SIGUE;

     |CIERRA #0;
     |CIERRA #1;
     |CIERRA #2;
     |CIERRA #4;

     |FININF;
     |FINIMP;
|FPRC;

|PROCESO Tipo80;  |TIPO 80;
     FSalida = 2899;
|FPRC;

|PROCESO Topes;
     |SI #99#0 < #99#2;  |O #99#0 > #99#3;
         |MENAV "0000 Rango de Expediente incorrecto.";
         |ERROR;
     |FINSI;

     |SI #99#1 < #99#2;  |O #99#1 > #99#3;
         |MENAV "0000 Rango de Expediente incorrecto.";
         |ERROR;
     |FINSI;
|FPRC;

|PROGRAMA;
     |ABRE #3;
     |SELECT #2#3;
     |LEE 000#3p;
     |SI FSalida ! 0;  |DDEFECTO #3;  |FINSI;
     #99#2 = #3#3;
     #99#0 = #99#2;

     |LEE 000#3u;
     |SI FSalida ! 0;  |DDEFECTO #3;  |FINSI;
     #99#3 = #3#3;
     |CIERRA #3;

     #99#0 = #99#2;
     #99#1 = #99#3;

     |PINPA #0#99;
     |PINDA #0#99;

     |C_M #99#0, 1, "S";
     |C_M #99#1, 1, "S";

     |SI PARAMETRO = "AUTOMA";
         #99#0 = enDExp;  |PINTA #99#0;  |C_M #99#0, 1, "N";
         #99#1 = enHExp;  |PINTA #99#1;  |C_M #99#1, 1, "N";
     |FINSI;

     |ENDATOS #1#99;
|FPRO;
