|FICHEROS;
  cainfcue #0;
  cacuenta #1;
  cainfcu1 #2;
|FIN;

|VARIABLES;
  &entrada = 1;
  informe1 = "cainfcta";
  informe2 = "cainfctd";
  linea    = 0;
  error    = 0;
  men1 =  "     NO EXISTE INFORME !";
  men2 =  "     NO EXISTE IMPRESORA !";
  alfa1 = "";
  {-1}menu = "";
      menu1 = "2400";
      menu2 = "1";
      menu3 = " Elija opcion: ";
      menu4 = "PI";
      menu5 = " Pantalla ";
      menu6 = " Impresora ";
|FIN;

|INCLUYE acceso_i;
|INCLUYE puntos_i;

|PROCESO cuenta; |TIPO 0;
  p_alfa1 = #0Campo;
  salidas = FSalida;
  |HAZ punto;
  |SI p_error ! 0;  |ERROR;  |ACABA;  |FINSI;
|FPRC;

|PROCESO cuentas;
  |SI #0#5 = "T";  |O #0#5 = #1#3;
      |PRINT;
  |FINSI;
|FPRC;

|PROCESO cuentap;
  |SI #0#5 ! "T";  |Y #0#5 ! #1#3;  |ACABA;  |FINSI;
  error = 0;
  #2#0 = #30#1;
  #2#1 = #30#26;
  |PINTA #2#0;
  |PINTA #2#1;
  |PINTA #2#2;
  Pos = linea;
  #2#3  = #1#0;
  #2#4  = #1#2;
  #2#5  = #1#4;
  #2#6  = #1#5;
  #2#7  = #1#6;
  #2#8  = #1#7;
  #2#9  = #1#8;
  #2#10 = #1#3;
  |PINDA #0#2;
  Pos = -1;
  linea = linea + 100;
  |SI linea > 1100; |HAZ cambiapa; |FINSI;
|FPRC;

|PROCESO cambiapa;
  linea = 100;
  error = 0;
  |PAUSA;
  |SI FSalida = 1; |O FSalida = 7;
      |ERROR10;
      error = 1;
  |SINO;
      |PINPA #0#2;
  |FINSI;
|FPRC;

     ||Ordenado por Codigo POR IMPRESORA;
|DEFBUCLE cainfcue0;
 #1#1;
 2;
 #0#0, 0, #0#2;
 #0#1, 9, #0#3;
 e;
 NULL, cuentas;
|FIN;

     ||Ordenado por Descripcion POR IMPRESORA;
|DEFBUCLE cainfcue1;
 #1#2;
 0;
 #0#2, #0#0;
 #0#3, #0#1;
 e;
 NULL, cuentas;
|FIN;

     ||Ordenado por Codigo POR PANTALLA;
|DEFBUCLE cainfcue2;
 #1#1;
 2;
 #0#0, 0, #0#2;
 #0#1, 9, #0#3;
 e;
 NULL, cuentap;
|FIN;

     ||Ordenado por Descripcion POR PANTALLA;
|DEFBUCLE cainfcue3;
 #1#2;
 0;
 #0#2, #0#0;
 #0#3, #0#1;
 e;
 NULL, cuentap;
|FIN;

|PROCESO impre;|TIPO 3;
  |HAZ inicio;
  |MENU menu;
  |SI FSalida ! 1; |Y FSalida ! 2; |ACABA; |FINSI;
  |SI FSalida = 1; |HAZ pantalla; |FINSI;
  |SI FSalida = 2; |HAZ impresora; |FINSI;
|FPRC;

|PROCESO impresora;
  |IMPRE 0;
  |SI FSalida ! 0;  |ACABA;  |FINSI;
  |SI #0#4 = 1;
      |INFOR informe1;
      |SI FSalida = 0;
          |BUCLE cainfcue0;
          |PIEINF;
          |FININF;
      |SINO;
          alfa1 = "    Informe [" + informe1 + "] inaccesible (" + FSalida + ") ...";
          |MENAV alfa1;
      |FINSI;
  |SINO;
      |INFOR informe2;
      |SI FSalida = 0;
          |BUCLE cainfcue1;
          |PIEINF;
          |FININF;
      |SINO;
          alfa1 = "    Informe [" + informe2 + "] inaccesible (" + FSalida + ") ...";
          |MENAV alfa1;
      |FINSI;
  |FINSI;
  |FINIMP;
|FPRC;

|PROCESO pantalla;
  error = 1;
  linea = 100;
  |PINPA #0#2;
  |SI #0#4 = 1;
      #2#2 = "Codigo";
      |BUCLE cainfcue2;
      |SI error = 0; |PAUSA; |FINSI;
  |SINO;
      #2#2 = "Descripcion";
      |BUCLE cainfcue3;
      |SI error = 0; |PAUSA; |FINSI;
  |FINSI;
|FPRC;

|PROCESO mayor;
 #0Campo = #0Campo > " ";
 |PINTA #0Campo;
|FPRC;
