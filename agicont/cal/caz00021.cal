|FICHEROS;
  caz00021 #0;

  camaasic #2;
  camaasil #3;
  catracie #4;
  camandia #10
|FIN;

|VARIABLES;
   &enDiario   = 0;
   &enContador = 0;
   &entrada = 1;
   &balance = 0;
   &pathbal = "";

   nDiario  = 0;
   fFecha   = @;
   nDocumento = 0;
   nHayDesc = 0;
   nLinea   = 0;
   nTDebe   = 0;
   nTHaber  = 0;
   nDebe    = 0;
   nHaber   = 0;
   nSaldo   = 0;
   aElDepar = "";
   aElSubde = "";
   aLaCtaAn = "";
   aAlfa1   = "";
   aAlfa2   = "";
   nNume1   = 0;
   nPaso    = 0;
   nInkey   = 0;

   nBusca   = 0;
   eaCuenta = "";
   eswCta   = 0;
   doce     = "            ";
   aLong    = "";
   nLong    = 0;
   enNivel  = 0;
   aFichero = "";
   aDFicOri = "";
   aDFicAct = "";
   &r_emp = 0;
   &r_per = 0;
|FIN;

|INCLUYE acceso_i;
|INCLUYE puntos_i;
|INCLUYE dsmone_i;
|INCLUYE condes_i;
|INCLUYE actcta_i;

|| **********************************************************************
|| ***** Calculos de Pantalla
|| **********************************************************************
|PROGRAMA;
  |CLS;
  |CABEZA "Cierre Cuentas Euro";

  |HAZ inicio;
  |HAZ VerCaconimp;
  |SI eaConDes ! "N";
      |C_P #0#18,1,1950;
      |C_A #0#18,1,26;
      |C_V #0#19,1,"S";
  |FINSI;

  |DDEFECTO #0;
  |PINPA #0#0;
  #0#0 = #30#2; #0#1 = #30#3;
  #0#2 = #30#1; #0#3 = #30#26;
  #0#4 = eaMoneda;

  |ABRE #5;
  |LEE 001#5u;
  #0#12 = #5#0;
  #0#13 = #5#1;
  |CIERRA #5;

  |PINDA #0#0;

  |ENDATOS #1#0;
  |SI FSalida = 0;
      |HAZ Cuadrar;
  |FINSI;
|FPRO;

|| **************************************************************************
|CALCULO consulta;
  |SI FSalida ! 9;  |ACABA;  |FINSI;
  |CIERRAT #30;
  r_emp = 0;
  |SUB_EJECUTA "caconemp";
  |ABRET #30;
  |ERROR;
  #0#5 = r_emp;
  #0#6 = r_per;
  |PTEC 802;
  |PTEC 802;
|FCAL;

|CALCULO esbuena;
  |SI FSalida = 9;  |ACABA;  |FINSI;
  |ABRE #30;
  #30#2 = #0#5;
  #30#3 = 0;
  |LEE 011#30];
  |SI FSalida ! 0;  |O #30#2 ! #0#5;
      |MENAV "     NO Existe la empresa Contable";
      |ERROR;
  |FINSI;
  |CIERRA #30;
|FCAL;

|CALCULO esbuena1;
  |SI FSalida = 2; |ACABA; |FINSI;
  |ABRE #30;
  #30#2 = #0#5;
  #30#3 = #0#6;
  |LEE 011#30=;
  |SI FSalida ! 0;
      |MENAV "     NO Existe Periodo en la empresa Contable";
      |CIERRA #30;
      |ERROR;
      |ACABA;
  |FINSI;
  |SI #30#13 ! dig3;  |O #30#14 ! dig4;  |O #30#15 ! dig5;
                      |O #30#16 ! dig6;  |O #30#17 ! dig7;
                      |O #30#18 ! dig8;  |O #30#19 ! dig9;
      |MENAV "     NO CORRESPONDEN LOS NIVELES";
      |CIERRA #30;
      |ERROR;
      |ACABA;
  |FINSI;
  #0#7 = #30#1;
  #0#8 = #30#26;
  |CIERRA #30;

  swOperacion = 0;
  enEmpresa = #0#5;
  enPeriodo = #0#6;
  |SUB_EJECUTA_NP "camoneda";
  #0#9 = eaMoneda;

  |PINTA #0#7;
  |PINTA #0#8;
  |PINTA #0#9;
|FCAL;

|CALCULO laBusca; |TIPO 0;
 |SI #0Campo = 1;
     |C_M #0#5,1,"S";
     |C_M #0#6,1,"S";
     aAlfa1 = " POR SALDO EN EMPRESA ANTERIOR ";
 |SINO;
     |C_M #0#5,1,"N";
     |C_M #0#6,1,"N";
     #0#5 = #0#0; |PINTA #0#5;
     #0#6 = #0#1; |PINTA #0#6;
     #0#7 = #0#2; |PINTA #0#7;
     #0#8 = #0#3; |PINTA #0#8;
     #0#9 = #0#4; |PINTA #0#4;
     aAlfa1 = " POR EXTRACTO CONTABLE ACTUAL  ";
 |FINSI;
 |ATRI R; |PINTA 1404, aAlfa1; |ATRI 0;
|FCAL;

||------------------------------------------------
|CALCULO TipoCuadre;
 |SI #0Campo = 1;
     |C_M #0#21,1,"S";
     |C_M #0#24,1,"N";
     |C_M #0#27,1,"N";
     #0#24 = ""; |PINTA #0#24;
     #0#26 = ""; |PINTA #0#26;
     #0#27 = ""; |PINTA #0#27;
     #0#29 = ""; |PINTA #0#29;
 |SINO;
     |C_M #0#21,1,"N";
     #0#21 = ""; |PINTA #0#21;
     #0#23 = ""; |PINTA #0#23;
     |C_M #0#24,1,"S";
     |C_M #0#27,1,"S";
 |FINSI;
|FCAL;

|CALCULO antesCta; |TIPO 7;
  |SI nPaso = 0;
      nPaso = 1;
      #0#27 = #0#24; |PINTA #0#27;
      #0#28 = #0#25;
      #0#29 = #0#26; |PINTA #0#29;
  |FINSI;
|FCAL;

|CALCULO cuenta; |TIPO 6;
  p_alfa1 = #0Campo;
  salidas = FSalida;
  |HAZ punto;
  |SI p_error ! 0;
      |MENAV "     ERROR EN CUENTA";
      |ERROR;
      |ACABA;
  |FINSI;
  eaCuenta = #0Campo; |HAZ SacaNivel;
  nNume1 = Campo + 1;
  #0nNume1 = enNivel;
|FCAL;

|CALCULO cuefin; |TIPO 0;
   |C_M #0Campo, 0, aAlfa1;
   |SI aAlfa1 = "N"; |ACABA; |FINSI;

   nInkey = FSalida;
   |SI nInkey = 15; |Y Campo = 27;
       #0#27 = #0#24;
       |PINTA #0#27;
   |FINSI;

   |SI #0Campo = "            ";
       |ERROR;
       |ACABA;
   |FINSI;
   eaCuenta = #0Campo;
   |HAZ SacaNivel;
   eaCuenta = #0Campo; |HAZ SacaNivel;
   nNume1 = Campo + 1;
   #0nNume1 = enNivel;
   |SI eswCta ! 0;  |ACABA; |FINSI;  ||Cuenta erronea

   |HAZ LeeCuenta;
   |SI eswCta ! 0;  |ACABA; |FINSI;  ||Cuenta erronea

   nNume1   = Campo + 2;
   #0nNume1 = #cacuenta#2; |PINTA #0nNume1;
|FCAL;

|| -----------------------------------------------------
|CALCULO c_descrip; |TIPO 7;
  |SI eaConDes ! "N";  #0Campo = ""; |FINSI;
  |PTEC 816;
|FCAL;

|CALCULO fecha; |TIPO 0;
   |SI #0Campo < fec1; |O #0Campo > fec2;
       |MENAV "    ATENCION!!! Fecha fuera de periodo ...";
       |ERROR;
   |FINSI;
|FCAL;

|CALCULO entrada; |TIPO 0;
  enDiario = 99;
  |HAZ contador;
  #0#16 = enContador;
  |PINTA #0#16;
|FCAL;

|| --------------------------------------------------------------------------
|| --------------------------------------------------------------------------
|| --------------------------------------------------------------------------
|| ... Desde Plan contable de la otra Empresa
|PROCESO CreaDatos0;
  #4#0 = #5#0;
  #4#1 = #5#1;
  #4#2 = #5#58;
  |SI #5#53 > 0;
      #4#3 = "D";
  |SINO;
      #4#3 = "H";
  |FINSI;
  #4#4 = #5#53;
  #4#5 = #5#2;
  #4#6 = #5#3;
  #4#7 = #5#4;
  #4#8 = #5#5;
  #4#9 = #5#6;
  #4#10 = #5#7;
  #4#11 = #5#8;
  #4#12 = #5#54;
  #4#13 = #5#55;
  |GRABA 020#4n;
|FPRC;

|DEFBUCLE CreaDatos;
 #5#1;
 3;
 #0#10, 0, "S";
 #0#12, 9, "S";
 e;
 NULL, CreaDatos0;
|FIN;

|| ... Desde el extracto contable de la Empresa Actual
|PROCESO CreaDatos2;
  #4#0 = #3#4;
  #4#1 = #3#5;
  |LEE 101#4=;
  |SI FSalida ! 0;
      |DDEFECTO #4;
      #4#0 = #3#4;
      #4#1 = #3#5;

      #5#0 = #4#0;
      #5#1 = #4#1;
      |LEE 001#5=;
      |SI FSalida ! 0; |DDEFECTO #5; |FINSI;

      #4#2 = #5#58;
      #4#5 = #5#2;
      #4#6 = #5#3;
      #4#7 = #5#4;
      #4#8 = #5#5;
      #4#9 = #5#6;
      #4#10 = #5#7;
      #4#11 = #5#8;
      #4#12 = #5#54;
      #4#13 = #5#55;
      |GRABA 020#4b;
  |FINSI;
  |SI #3#8 = "D";
      #4#4 = #4#4 + #3#9;
  |SINO;
      #4#4 = #4#4 - #3#9;
  |FINSI;
  |SI #4#4 > 0;
      #4#3 = "D";
  |SINO;
      #4#3 = "H";
  |FINSI;
  |GRABA 020#4a;
  |LIBERA #4;
|FPRC;

|DEFBUCLE CreaDatos1;
 #3#1;
 4;
 00, fec1, 000001, 0001, #0#10;
 99, fec2, 999999, 9999, #0#12;
 e;
 NULL, CreaDatos2;
|FIN;

|| -----------------------------------------
|PROCESO ApteCuadre;
 |SI #0#20 = 1;
     nSaldo = nDebe - nHaber;
     |SI nSaldo = 0;  |ACABA; |FINSI;
     #5#0 = #0#21;
     #5#1 = #0#22;
     |HAZ GrabaCta;  || por tanto creo linea de cuadre
 |SINO;
     nTDebe  = nDebe;
     nTHaber = nHaber;
     nSaldo = nTDebe;
     |SI nSaldo = 0;  |ACABA; |FINSI;
     #5#0 = #0#24;
     #5#1 = #0#25;
     |HAZ GrabaCta;  || por tanto creo linea de cuadre

     nSaldo = - nTHaber;
     |SI nSaldo = 0;  |ACABA; |FINSI;
     #5#0 = #0#27;
     #5#1 = #0#28;
     |HAZ GrabaCta;  || por tanto creo linea de cuadre
 |FINSI;
|FPRC;

|PROCESO ReponDatos;
 #5#0 = #4#0;
 #5#1 = #4#1;
 |LEE 041#5=;
 |SI FSalida ! 0;
     |DDEFECTO #5;
     #5#0 = #4#0;
     #5#1 = #4#1;
     #5#2 = #4#5;
     #5#3 = #4#6;
     #5#4 = #4#7;
     #5#5 = #4#8;
     #5#6 = #4#9;
     #5#7 = #4#10;
     #5#8 = #4#11;
     #5#54 = #4#12;
     #5#55 = #4#13;
     |GRABA 020#5n;
 |FINSI;
 |SI aMonedaC ! aMonedaA;
     impMoneda = #4#4;
     |HAZ ElCambioMoneda;
     #4#4 = impMoneda;
 |FINSI;
 |SI #4#4 ! #5#53;    || Hay Diferencia de saldos
     nHayDesc = nHayDesc + 1;

     |SI nBusca = 1;
                || ... Saldo Empresa Anterior / Saldo Cuenta
         nSaldo = #4#4 - #5#53;
     |SINO;
                || ... Saldo Cuenta / Saldo Extracto
         nSaldo = #5#53 - #4#4;
     |FINSI;

     |HAZ GrabaCta;  || por tanto creo linea de cuadre
 |FINSI;
 |LIBERA #4;
|FPRC;

|DEFBUCLE ReponDatos;
 #4#1;
 ;
 "            ", 0;
 "zzzzzzzzzzzz", 9;
 be;
 NULL, ReponDatos;
|FIN;

|| -----------------------------------------
|PROCESO Cuadrar;
  aFichero = ("y" + Usuario) % 108;
  |NOME_DAT #4 aFichero;
  |DELINDEX #4;

  |DBASE dir_fich;
  dir_fich = dir_fich + "fich/";

  aAlfa1 = #0#0; |QBF aAlfa1; aAlfa1 = ("00000" + aAlfa1) % -105;
  aAlfa2 = #0#1; |QBF aAlfa2; aAlfa2 = ("0" + aAlfa2) % -101;
  aDFicAct = dir_fich + aAlfa1 + aAlfa2 + "/";

  aAlfa1 = #0#5; |QBF aAlfa1; aAlfa1 = ("00000" + aAlfa1) % -105;
  aAlfa2 = #0#6; |QBF aAlfa2; aAlfa2 = ("0" + aAlfa2) % -101;
  aDFicOri = dir_fich + aAlfa1 + aAlfa2 + "/";

  nBusca     = #0#30;
  nDiario    = #0#14;
  fFecha     = #0#15;
  nDocumento = #0#16;
  nLinea     = -9;

  aMonedaC = #0#9;
  aMonedaA = #0#4;

 || . |CUADRO 2105 2371;
  |SI nBusca = 1;
      |PINTA 2406, " Leyendo cuentas en Empresa Anterior ... ";
  |SINO;
      |PINTA 2406, " Leyendo extracto de cuentas ...         ";
  |FINSI;
  eaMoneda = #0#9; |HAZ MoCierre;

  |PATH_DAT #5 aDFicOri;
  |ABRE #4;
  |SI nBusca = 1;
      |BUCLE CreaDatos;
  |SINO;
      |ABRE #5;
      |BUCLE CreaDatos1;
      |CIERRA #5;
  |FINSI;
  |CIERRA #4;

  || .................. Abre fichero Auxiliar de Cuentas
    |ABRE #8;
    |LEE 101#8u;
    |SI FSalida ! 0;
        |DDEFECTO #8;
        #8#0 = 1;
    |SINO;
        #8#0 = #8#0 + 1;
    |FINSI;
    |GRABA 101#8n;
    codcon = #8#0;
    |LIBERA #8;
    |CIERRA #8;
  || ----------------------------------------------------------
  |PINTA 2406, " Creando Asiento de Cuadre ...           ";

  eaMoneda = #0#4; |HAZ MoCierre;

  |PATH_DAT #5 aDFicAct;
  |ABRE #2;
  |ABRE #3;
  |ABRE #5;
  nDebe    = 0;
  nHaber   = 0;
  nHayDesc = 0;
  |BUCLE ReponDatos;
  |SI nHayDesc ! 0;
      |HAZ ApteCuadre;
  |FINSI;
  |CIERRA #5;
  |CIERRA #3;
  |CIERRA #2;

 || ............... Terminar
 |PINTA 2406, "                                         ";
 |SI modcon = 0;
      |ABRET #8
      |DDEFECTO #8;
      #8#0 = codcon;
      |LEE 040#8=;
      |BORRA 101#8a;
      |CIERRAT #8;
 |SINO;
      |SUB_EJECUTA_NP "camaapua";
      |DFICE pathbal;
       balance = 1;
      |SUB_EJECUTA "careccue";
 |FINSI;
 |DELINDEX #4;
|FPRC;

||***********************************************************************
|PROCESO GrabaCta;
   |DDEFECTO #camaasil;
   nLinea = nLinea + 10;

   #camaasil#0  = nDiario;
   #camaasil#1  = fFecha;
   #camaasil#2  = nDocumento;
   #camaasil#3  = nLinea;

   #camaasil#4  = #cacuenta#0;
   #camaasil#5  = #cacuenta#1;
   #camaasil#6  = #0#17;
   #camaasil#7  = #0#18;
   #camaasil#24 = #0#19;

   |SI nSaldo > 0;          || falta movimiento al DEBE
       #camaasil#8   = "D";
       nHaber = nHaber + nSaldo;
   |SINO;
       #camaasil#8  = "H";  ||falta movimiento al HABER
       nSaldo = - nSaldo;
       nDebe  = nDebe + nSaldo;
   |FINSI;
   #camaasil#9  = nSaldo;
   |HAZ adapta;

   #camaasil#21 = aElDepar;        || El Departamento

   |GRABA 020#camaasil.n;
   |LIBERA #camaasil;


   || ...    Datos Cabecera
   #camaasic#0 = nDiario;
   #camaasic#1 = fFecha;
   #camaasic#2 = nDocumento;
   |LEE 101#camaasic.=;
   |SI FSalida ! 0;
       |DDEFECTO #camaasic;
       #camaasic#0 = nDiario;
       #camaasic#1 = fFecha;
       #camaasic#2 = nDocumento;
       #camaasic#3 = nDocumento;
       |GRABA 020#camaasic.b;
   |FINSI;

   |SI #camaasil#8 = "D";
       #camaasic#4 = #camaasic#4 + #camaasil#9;
       |SI #camaasic#09 = 0; #camaasic#08 = #camaasil#4; #camaasic#09 = #camaasil#5; |FINSI;  || contrp.HABER
   |SINO;
       #camaasic#5 = #camaasic#5 + #camaasil#9;
       |SI #camaasic#11 = 0; #camaasic#10 = #camaasil#4; #camaasic#11 = #camaasil#5; |FINSI;  || contrp.DEBE
   |FINSI;
   #camaasic#6 = #camaasic#4 - #camaasic#5;        || saldo
   |GRABA 020#camaasic.a;
   |LIBERA #camaasic;

   ressum = #3#9;
   |HAZ actualiz1;          || cuenta
   |HAZ actualiz3;          || diario
   opera = "A";
   |HAZ altatra;            || subniveles
|FPRC;

|PROCESO actualiz3;
 |ABRE #camandia;
 #camandia#0 = #camaasil#0;
 |LEE 121#camandia.=;
 |SI FSalida ! 0;
     |DDEFECTO #camandia;
     #camandia#0 = #camaasil#0;
     #camandia#1 = "<DIARIO SIN DEFINIR>";
     |GRABA 020#camandia.b;
 |FINSI;
 |SI #camaasil#8 = "D";  #camandia#2 = #camandia#2 + #camaasil#9;  |FINSI;
 |SI #camaasil#8 = "H";  #camandia#3 = #camandia#3 + #camaasil#9;  |FINSI;
 |GRABA 020#camandia.a;
 |LIBERA #camandia;
 |CIERRA #camandia;
|FPRC;

|PROCESO adapta;
   |SI #camaasil#8 = "D";
       #camaasil#16 = #camaasil#4;
       #camaasil#17 = #camaasil#5;
       #camaasil#10 = "";
       #camaasil#11 = 0;
   |SINO;
       #camaasil#16 = "";
       #camaasil#17 = 0;
       #camaasil#10 = #camaasil#4;
       #camaasil#11 = #camaasil#5;
   |FINSI;
|FPRC;

|| ***********************************************************************
|| ***********************************************************************

|PROCESO SacaNivel;
   eswCta = 1;     ||Errores en Cuenta
   aLong = eaCuenta;
   |QBF aLong;
   aLong = aLong % 0;
   nLong = aLong;
   enNivel = 0;
   |SI nLong = dig4; |Y dig4 ! 0; eswCta = 0; enNivel = 1; |FINSI;
   |SI nLong = dig5; |Y dig5 ! 0; eswCta = 0; enNivel = 2; |FINSI;
   |SI nLong = dig6; |Y dig6 ! 0; eswCta = 0; enNivel = 3; |FINSI;
   |SI nLong = dig7; |Y dig7 ! 0; eswCta = 0; enNivel = 4; |FINSI;
   |SI nLong = dig8; |Y dig8 ! 0; eswCta = 0; enNivel = 5; |FINSI;
   |SI nLong = dig9; |Y dig9 ! 0; eswCta = 0; enNivel = 6; |FINSI;

   |SI eswCta = 1; |Y  nLong ! 0;
       |MENAV "      Longitud de Cuenta Fuera de Nivel ";
       |ERROR;
   |FINSI;
|FPRC;

|PROCESO LeeCuenta;
   eswCta = 0;
   |ABRE #cacuenta;
   #cacuenta#0 = eaCuenta;
   #cacuenta#1 = enNivel;
   |LEE 001#cacuenta.=;
   |SI FSalida ! 0;
       eswCta = 1;
       |MENAV "    NO EXISTE CUENTA ";
       |CIERRA #cacuenta;
       |ERROR;
       |ACABA;
   |FINSI;
   |CIERRA #cacuenta;

   |SI eaCuenta ! doce;
       |SI #cacuenta#3 = "N";
           eswCta = 2;
           |MENAV "     La Cuenta NO tiene PASE DIRECTO";
           |ERROR;
      |FINSI;
   |SINO;
      |ERROR;
   |FINSI;
|FPRC;
