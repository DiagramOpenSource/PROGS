|FICHEROS;
  ca8ut002 #0;

  canempre #30;
  ca8ctrle #400;  ||Control Empresa
  ca8eqmay #402;  ||Los cambios
  ca8m0003 #403;  ||Tabla de Esta Empresa
  ca8m0004 #404;  ||Tabla del Cierre

  caclipro #1;
  cacuenta #2;
  camaasil #4;
  catradia #9;

  canempre #30;
|FIN;

|VARIABLES;
   &entrada = 1;
   &tinforme = "";
   aAlfa1    = "";
   nNume1    = 0;
   nNume2    = 0;

   informe   = "ca8ut002";
   desde = "";
   aDCuenta = "";
   aHCuenta = "";
   aDNombre = "";
   aHNombre = "";
   &swInf = 0;
   &swImprimir = 0;
   &aError  = "";
   swSelec  = 0;
   sw = 0;
   aTitulo  = "";
   nImporte = 0;
   &nError   = 0;
   nCambia  = 0;
   aCuenta  = "";
   uno      = "";
   dos      = "";
   &modcon = 0;
   opera = "";
    ini = 0;
    fecalf = "";
    mes = "";
    mesfec = 0;
    mesac = 0;
    a = 0;
    b = 0;
    c = 0;
|FIN;

|INCLUYE varnue_i;
|INCLUYE acceso_i;
|INCLUYE puntos_i;

|PROCESO Tipo8; |TIPO 8;
   |HAZ inicio;
   |HAZ eVarEmpresa;
   |SI enEjercicio ! 2008;
       aAlfa1 = "    Proceso para el Ejercicio 2008. No ejecutable en el " + enEjercicio;
       |MENAV aAlfa1;
       |PTEC 807;
   |FINSI;
|FPRC;

|CALCULO LaSeleccion; |TIPO 0;
 |C_M #0#1, 1,"S"; |C_M #0#2, 1,"S";
 |C_M #0#3, 1,"S"; |C_M #0#4, 1,"S";
 |C_M #0#5, 1,"S"; |C_M #0#6, 1,"S";
 |C_M #0#7, 1,"S"; |C_M #0#8, 1,"S";
 |C_M #0#9, 1,"S"; |C_M #0#10,1,"S";
 |C_M #0#11,1,"S"; |C_M #0#12,1,"S";
 |C_M #0#13,1,"S"; |C_M #0#14,1,"S";
 |SI #0#0 = "C";
      |C_M #0#7, 1,"N"; |C_M #0#8, 1,"N";
      |C_M #0#9, 1,"N"; |C_M #0#10,1,"N";
      |C_M #0#11,1,"N"; |C_M #0#12,1,"N";
      |C_M #0#13,1,"N"; |C_M #0#14,1,"N";
 |FINSI;
 |SI #0#0 = "P";
      |C_M #0#1, 1,"N"; |C_M #0#2, 1,"N";
      |C_M #0#3, 1,"N"; |C_M #0#4, 1,"N";
      |C_M #0#5, 1,"N"; |C_M #0#6, 1,"N";
 |FINSI;
|FCAL;

|CALCULO ElPtec; |TIPO 7;
 |C_M #0Campo,0,aAlfa1;
 |SI aAlfa1 = "S";
     |PTEC 816;
 |FINSI;
|FCAL;

|CALCULO Hasta; |TIPO 0;
 |C_M #0Campo,0,aAlfa1;
 |SI aAlfa1 = "S";
     nNume1 = Campo - 1;
     |SI #0Campo < #0nNume1;
         |MENAV "    Error!. Cuenta Hasta Menor que la Cuenta Desde";
         |ERROR;
     |FINSI;
 |FINSI;
|FCAL;

|CALCULO cuenta;|TIPO 6;
     |C_M #0Campo, 0, aAlfa1;
     |SI aAlfa1 = "N"; |ACABA; |FINSI;
     p_alfa1 = #0Campo;
     salidas = FSalida;
     |HAZ punto;
     |SI p_error ! 0;  |ERROR;  |FINSI;
|FCAL;

|CALCULO AntesModi; |TIPO 7;
 aAlfa1 = "S";
 |SI #0#15 = "N";
     aAlfa1 = "N";
     #0#19 = "N"; |PINTA #0#19;
 |FINSI;
 |C_M #0#19, 1, aAlfa1;
|FCAL;

|| **********************************************************************

|CALCULO impre;|TIPO 3;

 |SI #0#16 = "S"
     |IMPRE 0;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |INFOR informe;
     |SI FSalida ! 0;  |FINIMP;  |ACABA;  |FINSI;
 |FINSI;

 |HAZ HazTodo;
 |SI #0#16 = "S"
     |FININF;
     |FINIMP;
 |FINSI;
|FCAL;

||************************************************************************
|PROCESO Actualiza;
    nImporte = #4#9;
    |SI opera = "B";
        nImporte = -nImporte;
    |FINSI;
    |HAZ a_cuenta;
    modcon = 1;               || graba el temporal para la actualiz. de ctas.
    #9#0 = codcon;
    #9#1 = #4#4;
    #9#2 = #4#5;
    #9#3 = #4#1;
    #9#4 = #4#8;
    #9#6 = #4#0;
    |LEE 101#9=;
    |SI FSalida ! 0;
        |DDEFECTO #9;
        #9#0 = codcon;
        #9#1 = #4#4;
        #9#2 = #4#5;
        #9#3 = #4#1;
        #9#4 = #4#8;
        #9#6 = #4#0;
        |GRABA 020#9b;
    |FINSI;
    #9#5 = #9#5 + nImporte;
    |GRABA 020#9a;
    |LIBERA #9;
|FPRC;

|PROCESO CambioApertura;
  |SI #404#14 ! " "; |ACABA; |FINSI; ||Se ha modificado manualmente
  #4#0 = #404#10;
  #4#1 = #404#11;
  #4#2 = #404#12;
  #4#3 = #404#0;
  |LEE 101#4=;
  |SI FSalida = 0;
      opera = "B";
      |HAZ Actualiza;

      #4#4 = #403#0;
      |SI #4#8 = "D";
           #4#16 = #4#4;
           #4#17 = #4#5;
      |SINO;
           #4#10 = #4#4;
           #4#11 = #4#5;
      |FINSI;
      opera = "A";
      |HAZ Actualiza;
      |GRABA 020#4a;
  |FINSI;
  |LIBERA #4;
  nError = 3;
  aError = "MODIFICADO EN EL ASIENTO DE APERTURA";
  |SI #0#16 = "S"; |PRINT; sw = 1; |FINSI;

  #404#8 = #403#0;
  |GRABA 020#404a;
  |LIBERA #404;
|FPRC;

|DEFBUCLE CambioApertura;
 #404#2;
 ;
 #403#0, 0001;
 #403#0, 9999;
 be;
 NULL, CambioApertura;
|FIN;

|PROCESO porimpre;

   swSelec = 0;
   |SI desde = "C";
       |SI #0#2 ! doce;
           |SI #403#0 ] #0#1; |Y #403#0 [ #0#2; swSelec = 1; |FINSI;
       |FINSI;
       |SI #0#4 ! doce;
           |SI #403#0 ] #0#3; |Y #403#0 [ #0#4; swSelec = 1; |FINSI;
       |FINSI;
       |SI #0#6 ! doce;
           |SI #403#0 ] #0#5; |Y #403#0 [ #0#6; swSelec = 1; |FINSI;
       |FINSI;
   |FINSI;
   |SI desde = "P";
       |SI #0#8 ! doce;
           |SI #403#0 ] #0#7; |Y #403#0 [ #0#8; swSelec = 1; |FINSI;
       |FINSI;
       |SI #0#10 ! doce;
           |SI #403#0 ] #0#9; |Y #403#0 [ #0#10; swSelec = 1; |FINSI;
       |FINSI;
       |SI #0#12 ! doce;
           |SI #403#0 ] #0#11; |Y #403#0 [ #0#12; swSelec = 1; |FINSI;
       |FINSI;
       |SI #0#14 ! doce;
           |SI #403#0 ] #0#13; |Y #403#0 [ #0#14; swSelec = 1; |FINSI;
       |FINSI;
   |FINSI;

   |SI swSelec = 0; |LIBERA #403; |ACABA; |FINSI;
   |SI #403#0 = #403#2; |LIBERA #403; |ACABA; |FINSI;  ||es la misma cuenta

   eaCuenta = #403#2;
   |HAZ eSacaNivel;
   #2#0 = #403#2;
   #2#1 = enNivel;
   |LEE 001#2=;
   |SI FSalida ! 0; |ACABA; |FINSI;

   |SI #0#19 = "S";
       aTitulo = "";
       #1#23   = "C";
       #1#10   = #2#0;
       #1#11   = #2#1;
       |LEE 001#1=;
       |SI FSalida ! 0;
           #1#23 = "P";
           #1#10 = #2#0;
           #1#11 = #2#1;
           |LEE 001#1=;
           |SI FSalida = 0;  aTitulo = #1#1; |FINSI;
       |SINO;
           aTitulo = #1#1;
       |FINSI;

       |QBF aTitulo; aAlfa1 = #2#2; |QBF aAlfa1;
       |SI aAlfa1 ! aTitulo; |Y aTitulo ! "";
           nError = 1;
           aError = "TITULO DE CUENTA DIFERENTE AL DE LA FICHA";
           |SI #0#16 = "S"; |PRINT; sw = 1; |FINSI;
           #2#2 = aTitulo;
           |GRABA 020#2a;
       |FINSI;
   |FINSI;

   nCambia  = 0;
   eaCuenta = #403#0;
   |HAZ MiraSiCambia;
   |SI nCambia = 1; |LIBERA #403; |ACABA; |FINSI; ||tiene cambio

   |ABRE #4;
   |BUCLE CambioApertura;
   |CIERRA #4;

   nError = 2;
   aError = "IGUALDAD ERRONEA EN LA TABLA DE EQUIVALENCIA";
   |SI #0#16 = "S"; |PRINT; sw = 1; |FINSI;
   #403#2 = #403#0;
   |GRABA 020#403a;
   |LIBERA #403;
|FPRC;

|DEFBUCLE Lee_codigo;
   #403#1;
   6;
   aDCuenta, " ";
   aHCuenta, " ";
   e;
   NULL, porimpre;
|FIN;

|PROCESO Leeimprime;
 sw = 0;

 tinforme = "PROVEEDORES";
 |SI desde = "C"; tinforme = "CLIENTES"; |FINSI;

 |ABRE #402;
 |ABRE #1;
 |ABRE #2;
 |BUCLE Lee_codigo;
 |CIERRA #2;
 |CIERRA #1;
 |CIERRA #402;

 |SI sw = 1;
     |SI #0#16 = "S"; |PIEINF; |FINSI;
 |FINSI;
|FPRC;

|PROCESO HazTodo;
  || .................. Abre fichero Auxiliar de Cuentas
    |ABRE #9;
    |LEE 101#9u;
    |SI FSalida ! 0;
        |DDEFECTO #9;
        #9#0 = 1;
    |SINO;
        #9#0 = #9#0 + 1;
    |FINSI;
    |GRABA 101#9n;
    codcon = #9#0;
    |LIBERA #9;
  || ----------------------------------------------------------

 |SI #0#0 ! "P";
     desde = "C";

     aDCuenta = #0#1;
     |SI aDCuenta > #0#3; |Y #0#4 ! doce; aDCuenta = #0#3; |FINSI;
     |SI aDCuenta > #0#5; |Y #0#6 ! doce; aDCuenta = #0#5; |FINSI;
     aHCuenta = #0#2;
     |SI aHCuenta < #0#4;                 aHCuenta = #0#4; |FINSI;
     |SI aHCuenta < #0#6;                 aHCuenta = #0#6; |FINSI;

     |HAZ Leeimprime;
 |FINSI;

 |SI #0#0 ! "C";
     desde = "P";

     aDCuenta = #0#7;
     |SI aDCuenta > #0#9;  |Y #0#10 ! doce; aDCuenta = #0#9;  |FINSI;
     |SI aDCuenta > #0#11; |Y #0#12 ! doce; aDCuenta = #0#11; |FINSI;
     |SI aDCuenta > #0#13; |Y #0#14 ! doce; aDCuenta = #0#13; |FINSI;
     aHCuenta = #0#8;
     |SI aHCuenta < #0#10;                 aHCuenta = #0#10; |FINSI;
     |SI aHCuenta < #0#12;                 aHCuenta = #0#12; |FINSI;
     |SI aHCuenta < #0#14;                 aHCuenta = #0#14; |FINSI;

     |HAZ Leeimprime;
 |FINSI;
 |CIERRA #9;
 |SI modcon = 0;
      |ABRET #9;
      |DDEFECTO #9;
      #4#0 = codcon;
      |LEE 101#9=;
      |SI FSalida = 0;
           |BORRA 020#9a;
      |FINSI;
      |LIBERA #9;
      |CIERRAT #9;
  |SINO;
      |SUB_EJECUTA_NP "camaapua"
  |FINSI;
|FPRC;

|PROCESO MiraSiCambia;
   nCambia = 0;
   |DDEFECTO #402;
   uno = eaCuenta; |QBF uno;
   dos = uno + "000000000000";

   uno = dos % A104;
   #402#0 = eNum2008;
   #402#1 = uno;
   |LEE 040#402=;
   |SI FSalida = 0;  |HAZ Datos;  |ACABA;  |FINSI;

   uno = dos % A103;
   #402#0 = eNum2008;
   #402#1 = uno;
   |LEE 040#402=;
   |SI FSalida = 0;  |HAZ Datos;  |ACABA;  |FINSI;

   uno = dos % A102;
   #402#0 = eNum2008;
   #402#1 = uno;
   |LEE 040#402=;
   |SI FSalida = 0;  |HAZ Datos;  |ACABA;  |FINSI;

   uno = dos % A101;
   #402#0 = eNum2008;
   #402#1 = uno;
   |LEE 040#402=;
   |SI FSalida = 0;
       |HAZ Datos;
   |FINSI;
|FPRC;

|PROCESO Datos;
   eaPlan8  = #402#3; |QBF eaPlan8;
   aAlfa1 = eaPlan8 % 0;

   nNume1 = aAlfa1;  || Longitud del Cambio
   nNume2 = nNume1 + 100;
   aAlfa1 = eaCuenta % nNume2;

   |SI aAlfa1 ! eaPlan8; nCambia = 1; |FINSI;
|FPRC;


|| **********
|PROCESO a_actualiza;                || rutina 'a_cuentas'
  ini = dig1;
  fecalf = #4#1;
  mes = fecalf % 402;
  mesfec = mes;
  |SI ini = 1;
      mesac = mesfec;
  |SINO;
      mesac = (mesfec - ini) + 1;
      |SI mesac [ 0;
          mesac = (13 - ini) + mesfec;
      |FINSI;
  |FINSI;
  a = (mesac * 3) + 9;
  b = a + 1;
  c = a + 2;
  |SI #4#8 = "D";
      #2a = #2a + nImporte;
      #2#51 = #2#51 + nImporte;
  |SINO;
      #2b = #2b + nImporte;
      #2#52 = #2#52 + nImporte;
  |FINSI;
  #2c = #2a - #2b;
  #2#53 = #2#51 - #2#52;
|FPRC;

|PROCESO a_cuenta;
 #2#0 = #4#4;
 #2#1 = #4#5;
 |LEE 101#2=;
 |SI FSalida = 0;
     |SI #4#0 = 0;
         |SI #4#8 = "D";
             #2#9  = #2#9  + nImporte;        || diario cero (saldos iniciales)
             #2#51 = #2#51 + nImporte;
         |SINO;
             #2#10 = #2#10 + nImporte;
             #2#52 = #2#52 + nImporte;
         |FINSI;
         #2#11 = #2#9 - #2#10;
         #2#53 = #2#51 - #2#52;
     |SINO;
         |SI #4#0 = 99;
             |SI #4#8 = "D";
                 #2#48 = #2#48 + nImporte;       || diario 99 (saldos finales)
                 #2#51 = #2#51 + nImporte;
             |SINO;
                 #2#49 = #2#49 + nImporte;
                 #2#52 = #2#52 + nImporte;
             |FINSI;
             #2#50 = #2#48 - #2#49;
             #2#53 = #2#51 - #2#52;
         |SINO;
             |HAZ a_actualiza;           || saldos del mes correspondiente
         |FINSI;
     |FINSI;
     |GRABA 020#2a;
     |LIBERA #2;
 |FINSI;
|FPRC;
