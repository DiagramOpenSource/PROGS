|FICHEROS;
     caut0013 #0;

     cacuivli #2,MANTE;
     cacoivli #4,MANTE;

     cacuivca #1;
     cacoivca #3;

     cacuenta #10;

     imcuivc@ #11;
     imcuivl@ #12;
     imcoivc@ #13;
     imcoivl@ #14;

     canempre #30;
     cabucem1 #998;
     caw00026 #999;
|FIN;

|VARIABLES;
     &entrada = 1;

     nCampo   = 0;
     aAlfa1   = "";
     aAlfa2   = "";
     aAlfa3   = "";
     nPara1   = 0;
     nPara2   = 0;
     nNume1   = 0;
     nNume2   = 0;

     &r_emp   = 0;
     &r_per   = 0;

     aFichero   = "";
     Linea      = 0;
     aBass      = "";
     aDef       = "";
     nSwNoDir   = 0;
     nSwTipo    = 0;
     nSwEjer    = 0;
     nEjer      = 0;

     aPath      = "";
     &eaOrigen  = "";
     &eaDestino = "";

     men2    = "     Longitud de Cuenta Fuera de Nivel !";
     men3    = "      Esta Cuenta NO tiene Pase directo";
     men4    = "      Cuenta INEXISTENTE o no tiene Pase directo";
     sw      = 0;
     long    = "  ";
     cta     = 0;
     nume1   = 0;
     alfa1   = "";
     alfa2   = "";
     nCambio = 0;

     aDef11      = "";
     aDef12      = "";
     aDef13      = "";
     aDef14      = "";
     nError      = 0;
     temporal1   = "";
     temporal2   = "";

     nNuevo      = 0;
     nOpera      = 0;
     nSwNuevo    = 0;
     aTipo       = "";
     aTipo0      = "";
     nBueno      = 0;
     nEstado     = 0;
     aQueQuiero  = "";
     aNumCampos  = "";
     nNumCampos  = 0;
     nSwCambio   = 0;
     nParaContar = 0;
     nSwAlgun    = 0;

     aFicheroV = "";
     aFicheroN = "";
     aFichero1 = "";
     aFichero2 = "";
     aFichero3 = "";
     aFichero4 = "";
     nSalir    = 0;

     aDTipo    = "";
     aHTipo    = "";
     nDTipo    = 0;
     nHTipo    = 0;
|FIN;

|INCLUYE acceso_i;
|INCLUYE varnue_i;
|INCLUYE puntos_i;

|| *************************************************************************
||                        CALCULOS DE PANTALLA
|| *************************************************************************

|PROCESO cuefin; |TIPO 0;
    nume1 = Campo + 1;
    |ABRE #10;
    #10#0 = #Fichero@#Campo#;
    #10#1 = #Fichero@#nume1#;
    |LEE 001#10=;
    |SI FSalida = 0;
        |SI #10#3 = "N";
            |MENAV men3;
            |ERROR;
        |FINSI;
    |SINO;
        |ERROR;
    |FINSI;
    |CIERRA #10;
|FPRC;

|PROCESO cuenta; |TIPO 6;
     salidas = FSalida;
     p_alfa1 = #Fichero@#Campo#;
     |HAZ punto;
     |SI p_error ! 0;  |ERROR;  |ACABA;  |FINSI;
     nume1 = Campo + 1;
     #Fichero@#nume1# = 0;
     alfa1 = #Fichero@#Campo#;
     |QBF alfa1;
     alfa2 = alfa1 % 0;
     long = alfa2;
     sw = 0;
     |SI long = dig4; sw = 1; #Fichero@#nume1# = 1; |FINSI;
     |SI long = dig5; sw = 1; #Fichero@#nume1# = 2; |FINSI;
     |SI long = dig6; sw = 1; #Fichero@#nume1# = 3; |FINSI;
     |SI long = dig7; sw = 1; #Fichero@#nume1# = 4; |FINSI;
     |SI long = dig8; sw = 1; #Fichero@#nume1# = 5; |FINSI;
     |SI long = dig9; sw = 1; #Fichero@#nume1# = 6; |FINSI;
     |SI long = 0;    sw = 1; #Fichero@#nume1# = 0; |FINSI;
     |SI sw = 0;  |MENAV men2;  |ERROR;  |FINSI;
|FPRC;

|PROCESO tipo13u; |TIPO 7;
  |SI #Fichero@#0 = "R";
      aAlfa1 = "Repercutido";
  |SINO;
      aAlfa1 = "  Soportado";
  |FINSI;
  |ATRI I; |PINTA 0908, aAlfa1; |ATRI 0;
|FPRC;

|PROCESO tipo13o; |TIPO 7;
  |SI #Fichero@#0 = "R";
      aAlfa1 = "Repercutido";
  |SINO;
      aAlfa1 = "  Soportado";
  |FINSI;
  |ATRI I; |PINTA 1308, aAlfa1; |ATRI 0;
|FPRC;

|| *************************************************************************
|PROCESO CargarFicheros;
  nError = 0;
  |DBASE aBass;  |QBT aBass;
  aDef11 = aBass + "def/cacuivca.mas";
  |CARGA_DEF aDef11, imcuivc@;
  |SI FSalida ! 0;
      |MENAV "    Error en def cacuivca. Proceso interrumpido";
      aDef11 = ""; nError = 1;
      |ACABA;
  |FINSI;

  aDef12 = aBass + "def/cacuivli.mas";
  |CARGA_DEF aDef12, imcuivl@;
  |SI FSalida ! 0;
      |MENAV "    Error en def cacuivli. Proceso interrumpido";
      aDef12 = ""; nError = 1;
      |ACABA;
  |FINSI;

  aDef13 = aBass + "def/cacoivca.mas";
  |CARGA_DEF aDef13, imcoivc@;
  |SI FSalida ! 0;
      |MENAV "    Error en def cacoivca. Proceso interrumpido";
      aDef13 = ""; nError = 1;
      |ACABA;
  |FINSI;

  aDef14 = aBass + "def/cacoivli.mas";
  |CARGA_DEF aDef14, imcoivl@;
  |SI FSalida ! 0;
      |MENAV "    Error en def cacoivli. Proceso interrumpido";
      aDef14 = ""; nError = 1;
  |FINSI;

  |NOME_DAT #11 aFichero1;
  |NOME_DAT #12 aFichero2;
  |NOME_DAT #13 aFichero3;
  |NOME_DAT #14 aFichero4;
|FPRC;

|PROCESO DesCargarFicheros;
  |SI aDef14 ! ""; |DESCARGA_DEF #14; aDef14 = ""; |FINSI;
  |SI aDef13 ! ""; |DESCARGA_DEF #13; aDef13 = ""; |FINSI;
  |SI aDef12 ! ""; |DESCARGA_DEF #12; aDef12 = ""; |FINSI;
  |SI aDef11 ! ""; |DESCARGA_DEF #11; aDef11 = ""; |FINSI;
  nError = 0;
|FPRC;

|| **********************************************************************
|PROCESO BuscaNuevo4;
   nNuevo = -1;
   |SI nParaContar > 99; |ACABA; |FINSI;

   #14#0 = aTipo;
   #14#1 = nParaContar;
   |LEE 001#14=;
   |SI FSalida ! 0;
       nNuevo = nParaContar;
   |SINO;
       |PARA nPara1 = 1; |SI nPara1 < 50; |HACIENDO nPara1 = nPara1 + 1;
             #14#0 = aTipo;
             #14#1 = nParaContar + nPara1;
             |SI #14#1 > 99;
                 nNuevo = -1;
                 |SAL;
             |SINO;
                 |LEE 001#14=;
                 |SI FSalida ! 0;
                     nNuevo = nParaContar + nPara1;
                     nParaContar = nNuevo;
                     |SAL;
                 |FINSI;
             |FINSI;
       |SIGUE;
   |FINSI;
   |SI nNuevo ! -1;
       nParaContar = nParaContar + 1;
   |FINSI;
|FPRC;

|PROCESO BuscaNuevo2;
   nNuevo = -1;
   |SI nParaContar > 99; |ACABA; |FINSI;

   #12#0 = aTipo;
   #12#1 = nParaContar;
   |LEE 001#12=;
   |SI FSalida ! 0;
       nNuevo = nParaContar;
   |SINO;
       |PARA nPara1 = 1; |SI nPara1 < 50; |HACIENDO nPara1 = nPara1 + 1;
             #12#0 = aTipo;
             #12#1 = nParaContar + nPara1;
             |SI #12#1 > 99;
                 nNuevo = -1;
                 |SAL;
             |SINO;
                 |LEE 001#12=;
                 |SI FSalida ! 0;
                     nNuevo = nParaContar + nPara1;
                     nParaContar = nNuevo;
                     |SAL;
                 |FINSI;
             |FINSI;
       |SIGUE;
   |FINSI;
   |SI nNuevo ! -1;
       nParaContar = nParaContar + 1;
   |FINSI;
|FPRC;
|| **********************************************************************
|PROCESO GrabaNuevo;
 #999#0 = aTipo0;
 #999#1 = nBueno;
 #999#2 = nNuevo;
 |GRABA 040#999n;
|FPRC;

|PROCESO  LecturaNuevos;
 nSwNuevo = 0;
 |SELECT #2#999;
 |DDEFECTO #999;
 #999#0 = aTipo0;
 #999#2 = nBueno;
 |LEE 041#999=;
 |SI FSalida = 0;
     nSwNuevo = 1;
 |FINSI;
 |SELECT #1#999;
|FPRC;

|| **********************************************************************
|PROCESO CambioTipo;
 nSwNuevo = 0;
 |DDEFECTO #999;
 #999#0 = "U";
 #999#1 = nNume1;
 |LEE 041#999=;
 |SI FSalida = 0;
     nSwNuevo = 1;
     nNume1   = #999#2;
     |SI nNume1 = 0; nNume1 = -1; |FINSI;
 |FINSI;
|FPRC;

|PROCESO CambioNumero;
|SI nEjer = 2010;
    |SI nNume1 = 16;   nNume1 = 18;   |FINSI;
    |SI nNume1 = 1.16; nNume1 = 1.18; |FINSI;
    |SI nNume1 = 0.16; nNume1 = 0.18; |FINSI;
    |SI nNume1 = 7;    nNume1 = 8;    |FINSI;
    |SI nNume1 = 1.07; nNume1 = 1.08; |FINSI;
    |SI nNume1 = 0.07; nNume1 = 0.08; |FINSI;
    |SI nNume1 = 9;    nNume1 = 10;   |FINSI;
    |SI nNume1 = 1.09; nNume1 = 1.10; |FINSI;
    |SI nNume1 = 0.09; nNume1 = 0.10; |FINSI;
    |SI nNume1 = 7.50; nNume1 = 8.50; |FINSI;
|FINSI;

|SI nEjer = 2012;
    |SI nNume1 = 10;   nNume1 = 12;   |FINSI;
    |SI nNume1 = 1.10; nNume1 = 1.12; |FINSI;
    |SI nNume1 = 0.10; nNume1 = 0.12; |FINSI;

    |SI nNume1 = 18;   nNume1 = 21;   |FINSI;
    |SI nNume1 = 1.21; nNume1 = 1.21; |FINSI;
    |SI nNume1 = 0.21; nNume1 = 0.21; |FINSI;
    |SI nNume1 = 8;    nNume1 = 10;   |FINSI;
    |SI nNume1 = 1.08; nNume1 = 1.10; |FINSI;
    |SI nNume1 = 0.08; nNume1 = 0.10; |FINSI;
    |SI nNume1 = 8.50; nNume1 = 10.50; |FINSI;

    |SI nNume1 ! #2#2;
        |SI nNume2 = 1;    nNume2 = 1.40;  |FINSI;
        |SI nNume2 = 4;    nNume2 = 5.20;  |FINSI;
    |FINSI;
|FINSI;
|FPRC;

|| **********************************************************************
|PROCESO cacoivli;
  aTipo0 = "O";
  nBueno = #4#1;
  |HAZ LecturaNuevos;
  |SI nSwNuevo = 1; |ACABA; |FINSI;

  |SALVA_DATOS #4;
  nSwCambio= 0;
  |PARA nPara1 = 4; |SI nPara1 [ 10; |HACIENDO nPara1 = nPara1 + 1;
        nNume1 = #4nPara1;
        |HAZ CambioTipo;
        |SI nNume1 ! #4nPara1;
            nSwCambio = 1;
            #4nPara1 = nNume1;
        |FINSI;
  |SIGUE;
  |SI nSwCambio = 1;
      |HAZ BuscaNuevo4;
      |SI nNuevo ! -1;
          aQueQuiero = "|NC";
          aNumCampos = #4#aQueQuiero#;
          nNumCampos = aNumCampos;
          nNumCampos = nNumCampos - 1;
          |PARA nPara1 = 0; |SI nPara1 [ nNumCampos; |HACIENDO nPara1 = nPara1 + 1;
                #14nPara1 = #4nPara1;
          |SIGUE;
          #14#0 = aTipo;
          #14#1 = nNuevo;
          |GRABA 020#14n;
          |HAZ GrabaNuevo;
      |FINSI;
   |FINSI;
   |LIBERA #4;
|FPRC;

|DEFBUCLE cacoivli;
 #4#1;
 ;
 aTipo, 01;
 aTipo, 99;
 be;
 NULL, cacoivli;
|FIN;

|PROCESO Cacuivli;
  aTipo0 = "U";
  nBueno = #2#1;
  |HAZ LecturaNuevos;
  |SI nSwNuevo = 1; |ACABA; |FINSI;

  nNume1 = #2#2; nNume2 = #2#5;
  |HAZ CambioNumero;
  |SI #2#2 ! nNume1; |O #2#5 ! nNume2;

      |HAZ BuscaNuevo2;
      |SI nNuevo ! -1;
          aQueQuiero = "|NC";
          aNumCampos = #2#aQueQuiero#;
          nNumCampos = aNumCampos;
          nNumCampos = nNumCampos - 1;
          |PARA nPara1 = 0; |SI nPara1 [ nNumCampos; |HACIENDO nPara1 = nPara1 + 1;
                #12nPara1 = #2nPara1;
          |SIGUE;
          #12#0 = aTipo;
          #12#1 = nNuevo;
          |GRABA 020#12n;
          |HAZ GrabaNuevo;
     |FINSI;
     #2#2 = nNume1;
     #2#5 = nNume2;
     |GRABA 020#2a;
     nSwAlgun = 1;
  |FINSI;
  |LIBERA #2;
|FPRC;

|DEFBUCLE cacuivli;
 #2#1;
 ;
 aTipo, 01;
 aTipo, 99;
 be;
 NULL, Cacuivli;
|FIN;

|PROCESO CambiosCuiva;
  |DELINDEX #999;

  nSwAlgun    = 0;
  nParaContar = 50;
  |SI nEjer = 2012; nParaContar = 70; |FINSI;
  |ABRE #999;
  |ABRE #12;
  |BUCLE cacuivli;
  |CIERRA #12;
  |CIERRA #999;
  |SI nSwAlgun = 1;
      nParaContar = 50;
      |SI nEjer = 2012; nParaContar = 70; |FINSI;
      |ABRE #999;
      |ABRE #14;
      |BUCLE cacoivli;
      |CIERRA #14;
      |CIERRA #999;
  |FINSI;
|FPRC;

|PROCESO IntentoCambio;

  |HAZ CargarFicheros;
  |SI nError = 1; |ACABA; |FINSI;

  aTipo = "R"; |HAZ CambiosCuiva;
  aTipo = "S"; |HAZ CambiosCuiva;

  |HAZ DesCargarFicheros;
|FPRC;

|| *****************************************************************

|PROCESO Inf2;
 #2#0 = "R";
 #2#1 = 01;
|FPRC;

|PROCESO Sup2;
 #2#0 = "S";
 #2#1 = 99;
|FPRC;

|PROCESO Inf4;
 #4#0 = "R";
 #4#1 = 01;
|FPRC;

|PROCESO Sup4;
 #4#0 = "S";
 #4#1 = 99;
|FPRC;

|PROCESO Cambio; TIPO 0;
  |SI #0#0 = "S"; |Y nCambio = 0;
      |HAZ IntentoCambio;
      |ENTLINEAL #1#2, -1, 7, 10, 0, Inf2, Sup2;
      |ENTLINEAL #1#4, -1, 7, 14, 0, Inf4, Sup4;
  |FINSI;

  |ENTLINEAL #1#2, -1, 2, 10, 0, Inf2, Sup2;
  |ENTLINEAL #1#4, -1, 2, 14, 0, Inf4, Sup4;
  nCambio = 1;
  |C_M #0#0, 1, "N";
|FPRC;

|CALCULO Consulta; |TIPO 0;
     |C_M #0Campo,0,aAlfa1;
     |SI aAlfa1 = "N"; |ACABA; |FINSI;

     |SI FSalida ! 9;  |ACABA;  |FINSI;

     |SUB_EJECUTA_NP "caconemp";
     |ERROR;
     |SI Campo = 3;
         #0#3 = r_emp;
         #0#5 = r_per;
         |PTEC 802;
         |PTEC 802;
     |FINSI;

     |SI Campo = 4;
         #0#4 = r_emp;
         #0#6 = r_per;
         |PTEC 802;
     |FINSI;

     |SI Campo ] 7;  |Y Campo [  16;
         #0Campo  = r_emp;
         nCampo   = Campo + 10;
         #0nCampo = r_per;
         |PTEC 802;
         |PTEC 802;
     |FINSI;

     |SI Campo = 37;
         #0#37 = r_emp;
         #0#38 = r_per;
         |PTEC 802;
         |PTEC 802;
     |FINSI;
|FCAL;

|CALCULO Parrilla; |TIPO 0;
     |C_M #0#2, 0, aAlfa1;
     |SI aAlfa1 = "N";  |ACABA;  |FINSI;

     |SI #0#2 = "N"; |O #0#2 = "X";
         #0#3 = 0;  |PINTA #0#3;  |C_M #0#3, 1, "N";
         #0#5 = 0;  |PINTA #0#5;  |C_M #0#5, 1, "N";
         #0#4 = 0;  |PINTA #0#4;  |C_M #0#4, 1, "N";
         #0#6 = 0;  |PINTA #0#6;  |C_M #0#6, 1, "N";
         |SI #0#2 = "N";
             |PARA nCampo = 7;  |SI nCampo [ 26;  |HACIENDO nCampo = nCampo + 1;
                   |C_M #0nCampo, 1, "S";
             |SIGUE;
         |FINSI;
     |FINSI;
     |SI #0#2 = "S"; |O #0#2 = "X";
         |SI #0#2 = "S";
             |C_M #0#3, 1, "S"; |C_M #0#5, 1, "S";
             |C_M #0#4, 1, "S"; |C_M #0#6, 1, "S";
         |FINSI;
         |PDEFECTO #0,7,37;
         |PARA nCampo = 7;  |SI nCampo [ 36;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "N";
               |PINTA #0nCampo;
         |SIGUE;
     |FINSI;
|FCAL;

|CALCULO NoMasEmpresas; |TIPO 0;
     |C_M #0Campo, 0, aAlfa1;
     |SI aAlfa1 = "N";  |ACABA;  |FINSI;
     |SI FSalida = 2;   |ACABA;  |FINSI;
     |SI FSalida = 9;   |HAZ Consulta;  |FINSI;

     |SI #0Campo = 0;
         nNume1 = Campo + 10;
         nNume2 = Campo + 20;
         #0nNume1 = 0; |C_M #0nNume1, 1, "N"; |PINTA #0nNume1;
         #0nNume2 = "";                       |PINTA #0nNume2;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 16;  |HACIENDO nCampo = nCampo + 1;
               nNume1 = nCampo + 10;
               nNume2 = nCampo + 20;
               #0nCampo = 0; |C_M #0nCampo, 1, "N"; |PINTA #0nCampo;
               #0nNume1 = 0; |C_M #0nNume1, 1, "N"; |PINTA #0nNume1;
               #0nNume2 = "";                       |PINTA #0nNume2;
         |SIGUE;
         |ACABA;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 26;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |FINSI;

     |ABRE #30;
     #30#2 = #0Campo;
     #30#3 = 0;
     |LEE 011#30];
     |SI FSalida ! 0;  |O #30#2 ! #0Campo;
         |MENAV "     NO Existe la Empresa Contable";
         |ERROR;
     |FINSI;
     |CIERRA #30;
|FCAL;

|CALCULO EsBuena1;
     |C_M #0Campo,0,aAlfa1;
     |SI aAlfa1 = "N"; |ACABA; |FINSI;

     |SI FSalida = 2; |ACABA; |FINSI;

     nCampo = Campo - 10;
     |SI #0nCampo = 0;  |ACABA;  |FINSI;

     |ABRE #30;
     #30#2 = #0nCampo;
     #30#3 = #0Campo;
     |LEE 011#30=;
     |SI FSalida ! 0;
         |MENAV "     NO Existe Periodo en la Empresa Contable";
         |CIERRA #30;
         |ERROR;
         |ACABA;
     |FINSI;
     |CIERRA #30;

     nCampo   = Campo + 10;
     #0nCampo = #30#1;
     |PINTA #0nCampo;
|FCAL;

|| **********************************************************************
|| ********************* Procesos de seleccion

|PROCESO CalDirectorio;
  nSwNoDir = 0;
  |DBASE dir_fich; |QBF dir_fich;
  dir_fich = dir_fich + "fich/";
  aAlfa1 = #998#0; |QBF aAlfa1;
  aAlfa2 = #998#1; |QBF aAlfa2;
  aAlfa1 = "00000" + aAlfa1;   aAlfa1 = aAlfa1 % -105;
  aAlfa2 = "0"     + aAlfa2;   aAlfa2 = aAlfa2 % -101;
  aPath = dir_fich + aAlfa1 + aAlfa2 + "/";

  |SI nOpera = 0;
      |MKDIR aPath;
      |SI FSalida = 0;
          nSwNoDir = 1;
          |RMDIR aPath;
      |FINSI;
 |FINSI;
|FPRC;

|| ******************************************************************
|PROCESO Rellena;
   |DDEFECTO #998;
   #998#0 = #30#2;
   #998#1 = #30#3;
   #998#2 = #30#11;
   |HAZ CalDirectorio;
   |SI nSwNoDir = 0;
       |GRABA 000#998n;
       Linea = Linea + 1;
   |FINSI;
|FPRC;

|DEFBUCLE Rellena;
 #30#1;
 ;
 #0#3, #0#5;
 #0#4, #0#6;
 ;
 NULL, Rellena;
|FIN;


|| *******************************************************************
|| ..... Copia del fichero en si
|| -------------------------------------------------------------------
|PROCESO Borro_Otro;
  temporal1 = eaOrigen + aFicheroV + ".dat"; |FBORRA temporal1;
  temporal1 = eaOrigen + aFicheroV + ".ixx"; |FBORRA temporal1;
  temporal1 = eaOrigen + aFicheroV + ".ddx"; |FBORRA temporal1;
|FPRC;

|PROCESO Copiar_Otro;

  temporal1 = eaOrigen + aFicheroV + ".ixx";
  temporal2 = eaOrigen + aFicheroN + ".ixx";
  |COPIA_FICHERO temporal1,temporal2;
  |SI FSalida ! 0;  |ACABA;  |FINSI;

  temporal1 = eaOrigen + aFicheroV + ".dat";
  temporal2 = eaOrigen + aFicheroN + ".dat";
  |COPIA_FICHERO temporal1,temporal2;
  |SI FSalida ! 0;
      temporal2 = eaOrigen + aFicheroN + ".ixx";
      |FBORRA temporal2;
      |ACABA;
  |FINSI;

  temporal1 = eaOrigen + aFicheroV + ".ddx";
  temporal2 = eaOrigen + aFicheroN + ".ddx";
  |COPIA_FICHERO temporal1,temporal2;
|FPRC;

|PROCESO Copiar;

  temporal1 = eaOrigen + aFichero + ".ixx";
  temporal2 = eaDestino+ aFichero + ".ixx";
  |COPIA_FICHERO temporal1,temporal2;
  |SI FSalida ! 0;  |ACABA;  |FINSI;

  temporal1 = eaOrigen + aFichero + ".dat";
  temporal2 = eaDestino+ aFichero + ".dat";
  |COPIA_FICHERO temporal1,temporal2;
  |SI FSalida ! 0;
      temporal2 = eaDestino+ aFichero + ".ixx";
      |FBORRA temporal2;
      |ACABA;
  |FINSI;

  temporal1 = eaOrigen + aFichero + ".ddx";
  temporal2 = eaDestino+ aFichero + ".ddx";
  |COPIA_FICHERO temporal1,temporal2;
|FPRC;

|PROCESO CopioaEmp;

    |SI #998#0 = #0#37; |Y #998#1 = #0#38; |ACABA; |FINSI;
    nOpera = 1;
    |HAZ CalDirectorio;
    eaDestino = aPath; |QBF eaDestino;

    aFichero = "cacuivca"; |HAZ Copiar;
    aFichero = "cacuivli"; |HAZ Copiar;
    aFichero = "cacoivca"; |HAZ Copiar;
    aFichero = "cacoivli"; |HAZ Copiar;
|FPRC;

|DEFBUCLE CopioaEmp;
 #998#1;
 ;
 00001, 0;
 99999, 9;
 ;
 NULL, CopioaEmp;
|FIN;

|PROCESO LaCopia;
  |BUCLE CopioaEmp;
|FPRC;

|| ************************************************************
|PROCESO Compruebo;
|SI nEjer = 2010;
   |SI #2#2 = 18;  nSalir = 1; |FINSI;
   |SI #2#2 = 10;  nSalir = 1; |FINSI;
   |SI #2#2 = 8;   nSalir = 1; |FINSI;
   |SI #2#2 = 8.5; nSalir = 1; |FINSI;
   |SI nSalir = 1; |ERROR10; |FINSI;
|FINSI;
|SI nEjer = 2012;
   |SI #2#2 = 21;   nSalir = 1; |FINSI;
   |SI #2#2 = 12;   nSalir = 1; |FINSI;
   |SI #2#2 = 10;   nSalir = 1; |FINSI;
   |SI #2#2 = 10.5; nSalir = 1; |FINSI;
   |SI nSalir = 1; |ERROR10; |FINSI;
|FINSI;
|FPRC;

|DEFBUCLE Compruebo;
 #2#1;
 ;
 " ", 01;
 "z", 99;
 e;
 NULL, Compruebo;
|FIN;

|PROCESO PrimeroAviso;
  nDTipo = 01; aDTipo = "R";
  nHTipo = 99; aHTipo = "S";
  nSalir = 0;
  |BUCLE Compruebo;
  |SI nSalir = 1;
      aAlfa1 = "    NExiste ya tipos del 18% y 8%. Continuar Proceso";
      |SI nEjer = 2012;
          aAlfa1 = "    NExiste ya tipos del 21% y 10%. Continuar Proceso";
      |FINSI;
      |CONFI aAlfa1;
      |SI FSalida = 0;
          nSalir = 0;
          #0#0 = "N";
      |FINSI;
  |FINSI;
|FPRC;

|PROCESO LaCopiaPrimera;  || del Bueno al de Trabajo
  |DFICE eaOrigen;
  aFicheroV = "cacuivca"; aFicheroN = aFichero1; |HAZ Copiar_Otro;
  aFicheroV = "cacuivli"; aFicheroN = aFichero2; |HAZ Copiar_Otro;
  aFicheroV = "cacoivca"; aFicheroN = aFichero3; |HAZ Copiar_Otro;
  aFicheroV = "cacoivli"; aFicheroN = aFichero4; |HAZ Copiar_Otro;

  |NOME_DAT #1 aFichero1;   ||Operamos con los de trabajo
  |NOME_DAT #2 aFichero2;
  |NOME_DAT #3 aFichero3;
  |NOME_DAT #4 aFichero4;
|FPRC;

|PROCESO LaCopiaSegunda;
  |DFICE eaOrigen;
  aFicheroN = "cacuivca"; aFicheroV = aFichero1; |HAZ Copiar_Otro; |HAZ Borro_Otro;
  aFicheroN = "cacuivli"; aFicheroV = aFichero2; |HAZ Copiar_Otro; |HAZ Borro_Otro;
  aFicheroN = "cacoivca"; aFicheroV = aFichero3; |HAZ Copiar_Otro; |HAZ Borro_Otro;
  aFicheroN = "cacoivli"; aFicheroV = aFichero4; |HAZ Copiar_Otro; |HAZ Borro_Otro;
|FPRC;

|PROCESO LosBorro;
  |DFICE eaOrigen;
  aFicheroV = aFichero1; |HAZ Borro_Otro;
  aFicheroV = aFichero2; |HAZ Borro_Otro;
  aFicheroV = aFichero3; |HAZ Borro_Otro;
  aFicheroV = aFichero4; |HAZ Borro_Otro;
|FPRC;

|| ************************************************************
|CALCULO Tipo3; |TIPO 12;

  |SI #0#39 = "NO";
      |HAZ LosBorro;
      |ACABA;
  |FINSI;

  |HAZ LaCopiaSegunda; || Paso los de Trabajo a los Buenos

  |SI #0#2 = "X"; |ACABA; |FINSI;

  |DBASE aBass;  |QBT aBass;

  aBass  = aBass + "fich/";
  aAlfa1 = #0#37; |QBF aAlfa1; aAlfa1 = ("00000" + aAlfa1) % -105;
  aAlfa2 = #0#38; |QBF aAlfa2; aAlfa2 = ("0"     + aAlfa2) % -101;
  eaOrigen = aBass + aAlfa1 + aAlfa2 + "/";

  aFichero = ("u6" + Usuario) % 108;
  |NOME_DAT #998 aFichero;
  |DELINDEX #998;

  nOpera = 0;
  Linea  = 1;
  |ABRE #998;
  |SI #0#2 = "S";
      |BUCLE Rellena;
  |SINO;
      |PARA nPara1 = 7; |SI nPara1 [ 16; |HACIENDO nPara1 = nPara1 + 1;
            nPara2 = nPara1 + 10;
            |SI #0nPara1 ! 0;
                |DDEFECTO #998;
                #998#0 = #0nPara1;
                #998#1 = #0nPara2;
                |HAZ CalDirectorio;
                |SI nSwNoDir = 0;
                    |GRABA 020#998n;
                    Linea  = Linea + 1;
                |FINSI;
             |FINSI;
      |SIGUE;
  |FINSI;
  |CIERRA #998;

  |SI Linea = 1;
      |MENAV "    Seleccion Vacia";
  |SINO;
      |HAZ LaCopia;
  |FINSI;
|FCAL;


|PROGRAMA;

 |HAZ inicio;
 |HAZ eVarEmpresa;
 |SI enEjercicio ! 2010; |Y enEjercicio ! 2012;
     |PINTA 1008, "Utilidad creada por los Cambios de IVA en los Ejercicios 2010 y 2012 ";
     |PINTA 1108, "Esta entrado en un periodo que no corresponde a estos ejercicios.    ";
     |PAUSA;
     |ACABA;
 |FINSI;

 nEjer = enEjercicio;
 aFichero = "1u" + Usuario;
 |NOME_DAT #999 aFichero;

 |DDEFECTO #0;
 #0#37 = enEmpresa;
 #0#38 = enPeriodo;
 #0#1  = enEjercicio;

 |C_V #2#0, 1, "S"; |C_M #2#0, 1,"S";
 |C_L #2#0 ,1, "S"; |C_P #2#0, 1, 0823;
 |C_P #2#1, 1, 0826; |C_P #2#2, 1, 0830;
 |C_P #2#3, 1, 0837; |C_P #2#5, 1, 0851;
 |C_P #2#6, 1, 0858; |C_P #2#8, 1, 0872;
 |C_P #2#9, 1, 0875; |C_P #2#10, 1, 0878;

 |C_V #4#0, 1, "S"; |C_M #4#0, 1,"S";
 |C_L #4#0, 1, "S"; |C_P #4#0, 1, 1223;
 |C_P #4#1, 1, 1226; |C_P #4#2, 1, 1230;
 |C_P #4#4, 1, 1244; |C_P #4#5, 1, 1247;
 |C_P #4#6, 1, 1250; |C_P #4#7, 1, 1253;
 |C_P #4#8, 1, 1256; |C_P #4#9, 1, 1259;
 |C_P #4#10, 1,1262; |C_P #4#11,1,1266;


 aFichero1 = "1a" + Usuario;
 aFichero2 = "2a" + Usuario;
 aFichero3 = "3a" + Usuario;
 aFichero4 = "4a" + Usuario;

 |CLS;
 |PINPA #0#0;
 |SI enEjercicio = 2012;
     |ATRI R; |PINTA 0570, "2012"; |ATRI 0;
 |FINSI;
 |PINDA #0#0;
 |ENTLINEAL #1#2, -1, 7, 10, 0, Inf2, Sup2;
 |ENTLINEAL #1#4, -1, 7, 14, 0, Inf4, Sup4;

 |HAZ PrimeroAviso;
 |SI nSalir = 0;
     |HAZ LaCopiaPrimera;
     |ENDATOS #1#0;
 |FINSI;
|FPRO;
