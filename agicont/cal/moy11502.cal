|FICHEROS;
     caemi115;

     caportad #01;
     catra115 #02;
     camar115 #04; || Resumen
     caman115 #20;
     camoneda #21;

     t1511501;
|FIN;

|VARIABLES;
    swtraba = 0;
    nume1 = 0;
    nume2 = 0;
    nume3 = 0;
    campo = 0;
    antes = 0;
    diferen = 0;
    desdemes = 0;
    hastames = 0;
    meses = 0;
    campotA1  = 0;
    campotB1  = 0;
    campotR1  = 0;
    campotA2  = 0;
    campotB2  = 0;
    campotR2  = 0;
    campobas1 = 0;
    camporet1 = 0;
    campoexen = 0;
    campobas2 = 0;
    camporet2 = 0;

    swsuma1   = 0;
    swsuma2   = 0;
    informe   = "";
    numf      = 0;
    se_factu  = "";
    se_perio  = 0;

    mas = 0;
    campofec = 0;
    p_mes = "";
    fecsys = @;
    alfa1 = "";
    alfa2 = "";
    alfa3 = "";
    alfa4 = "";
    peri  = 0;

    destra = 0;
    hastra = 0;
    no_vale = 0;

    sw_acumula = 0;
    des_emp = 0;
    has_emp = 0;
    grupo = 0;
    estado = 0;
    tiposel = "";
    CampoArchiv = 0;
    nIngreso    = 0;

     &perialf = "";
     &dia     = "";
     &mes     = "";
     &anyo    = "";

     nAnyo2D      = 0;
     aAlfa        = "";

     aAbre        = "";
     aModelos     = "";

     nHandle1     = 0;
     nHandle2     = 0;
     nSwHay       = 0;
     nTCampo      = 0;
     nCampo       = 0;

     aAlfa1       = "";
     aUnidad      = "";
     aEnlace      = "";
     aHID         = "";
     aNDC         = "";
     aIDI         = "";
     aEJF         = "";
     aMOD         = "";
     aPRG         = "";
     aTIA         = "";
     aINI         = "";
     aFIN         = "";
     aAbrePortal  = "";
     aTipo        = "";
     aFichero     = "";

  {1}aContiene    = "";

     &eaMonedEmpre = "";
     &eaFOrigen    = "";
     &eaFDestino   = "";
     &eaInternet   = "";
     &eaImpresora  = "";
     &enNume       = 0;
     &eaAlfa       = "";
     &eaDiaB       = "";
     &eaMesB       = "";
     &eaAnyoB      = "";
     &eaModelos    = "";
     &eaUnidadBasico = "";

     &eaEjer             = "";
     &eaPeriodo          = "";
     &eaPathEmision      = "";
     &eaPathLanza        = "";
     &eaPathExplorador   = "";
     &eaPathInternet     = "";
     &eaPathAEAT         = "";
     &eaFicheroPlano     = "";
     &eaFicheroSalid     = "";
     &eaFicheroImpre     = "";
     &eaFicheroError     = "";
     &eaForzExplorador   = "";
     &eaIBAN             = "";
     &eaUnidad           = "";
     &eaOpcion           = "";
     &eaNomEmpresa       = "";
     &enHandlePortal     = 0;
     &enPresentar        = 0;
     &enErrorPortal      = 0;
     &enTecla            = 0;
     &enPer              = 0;
     &enEmpresa          = 0;
     &eaCampo1           = "";
     &eaCampo2           = "";
     &eaCampo3           = "";
     &eaCampo4           = "";
     &eaCampo6           = "";
     &eaCampo8           = "";
     &eaCampo10          = "";
     &eaCampo19          = "";
     &eaNIF              = "";
     &enImprtMd          = 0;
|FIN;

|PROCESO PresentaModelo_02;
     |SI eaOpcion = "1";
         eaNIF     = #t1511501#7;
         enImprtMd = #caemi115#14 + #caemi115#17;
         |HAZ PortalPDFPeriodicos;
     |FINSI;

     |SI eaOpcion = "3";
         eaFOrigen  = eaPathEmision  + eaFicheroPlano;
         eaFDestino = eaPathInternet + eaFicheroPlano;
         |HAZ CopiaFicheroPtl;
     |FINSI;
|FPRC;

|PROCESO AbrePagina;
     eaPathAEAT = eaUnidad + "\AEAT";        |RMKDIR eaPathAEAT;
     eaPathAEAT = eaPathAEAT + "\" + eaModelos;  |RMKDIR eaPathAEAT;
     eaPathAEAT = eaPathAEAT + "\" + eaEjer;  |RMKDIR eaPathAEAT;
     eaPathAEAT = eaPathAEAT + "\" + eaPeriodo;  |RMKDIR eaPathAEAT;
     eaPathAEAT = eaPathAEAT + "\";

     eaFOrigen  = eaPathEmision  + eaFicheroPlano;
     eaFDestino = eaPathAEAT + eaFicheroPlano;
     |HAZ CopiaFicheroPtl;

     |DBASE aAlfa;  |QBT aAlfa;
     eaFOrigen  = aAlfa + "hacienda/wait.exe";
     eaFDestino = eaPathAEAT + "wait.exe";
     |HAZ CopiaFicheroPtl;

     eaForzExplorador = "N";
     |HAZ Explorador;

     aAbre = eaPathAEAT + "imp" + Usuario + ".bat";
     |XABRE "WBC", aAbre, enHandlePortal;

     eaAlfa = eaPathAEAT + "wait ";
     |HAZ GrabaPortalSin;

     eaAlfa = eaPathExplorador + " " + &34;
     |HAZ GrabaPortalSin;

     eaAlfa = "https://www1.agenciatributaria.gob.es/es13/h/ie51150a.html";
     |HAZ GrabaPortalSin;

     |XCIERRA enHandlePortal;

     |RSYSTEM aAbre;

     aAlfa = eaPathExplorador > "";
     aAlfa = aAlfa - "CHROME.EXE";
     |SI FSalida ! 0;  |SLEEP 30;  |FINSI;

     aAlfa = eaPathAEAT + aFichero;
     |RFBORRA aAlfa;
     |RFBORRA aAbre;
|FPRC;

|PROCESO por_dnis2;
     |DDEFECTO #2;
     #2#0  = #20#0;
     #2#7  = #20#7;       || dni
     |LEE 101#2=;
     estado = FSalida;
     #2#12 = 1;  #2#13 = 2;

     nume3 = 0; |SI #20#12 = 2; nume3 = 26; |FINSI;
     |PARA nume1 = 23; |SI nume1 [ 48; |HACIENDO nume1 = nume1 + 1;
           nume2 = nume1 + nume3;
           #2nume2 = #2nume2 + #20nume1;
     |SIGUE;

     nume3 = 0; |SI #20#13 = 1; nume3 = - 26; |FINSI;
     |PARA nume1 = 49; |SI nume1 [ 74; |HACIENDO nume1 = nume1 + 1;
           nume2 = nume1 + nume3;
           #2nume2 = #2nume2 + #20nume1;
     |SIGUE;

     #2#0  = #20#0;
     #2#7  = #20#7;       || dni
     |SI estado = 0;  |GRABA 000#2a;  |FINSI;
     |SI estado ! 0;  |GRABA 000#2n;  |FINSI;
     |LIBERA #2;
|FPRC;

|DEFBUCLE temporal;
     #20#1;
     135;
     #caemi115#0, 00000, nAnyo2D, "5";
     #caemi115#0, 99999, nAnyo2D, "5";
     be;
     NULL, por_dnis2;
|FIN;

|PROCESO por_dnis;
     |PINTA 2405, "Preparando Temporal por NIF's ... ";
     |ABRE #2;  |CIERRA #2;  |DELINDEX #2;

     |NOPARA;
     |ABRE #2;
     |BUCLE temporal;
     |CIERRA #2;
     |SIPARA;
     |PINTA 2405, "                                  ";
|FPRC;


|PROCESO TDeclaracion2015;
     nIngreso  = #caemi115#14 + #caemi115#17;
     |SI nIngreso > 0;
         enNume  = nIngreso;  |HAZ Conver17;  #t1511501#16 = eaAlfa;

         eaIBAN = #caportad#117;  |QBF eaIBAN;

                                                    #t1511501#6 = "I";
         |SI  eaIBAN ! "";  |Y #caemi115#19 = "D";  #t1511501#6 = "U";  |FINSI;

         |SI #caemi115#19 ! "N";
             #t1511501#19 = eaIBAN;
         |FINSI;
     |SINO;
         enNume  = 0;  |HAZ Conver17;  #t1511501#16 = eaAlfa;
         #t1511501#6 = "N";
     |FINSI;
|FPRC;

|PROCESO borrar;
    |PDEFECTO #caemi115,12,19;
    |PARA meses = desdemes; |SI meses [ hastames;  |HACIENDO meses = meses + 1;
          campotA1 = meses + 3;  campotA2 = meses + 39;
          campotB1 = meses + 15; campotB2 = meses + 51;
          campotR1 = meses + 27; campotR2 = meses + 63;

          #4campotA1 = 0; #4campotB1 = 0; #4campotR1 = 0;
          #4campotA2 = 0; #4campotB2 = 0; #4campotR2 = 0;
     |SIGUE;
|FPRC;

|PROCESO catra115;
    swsuma1 = 0;
    swsuma2 = 0;
    |PARA meses = desdemes; |SI meses [ hastames;  |HACIENDO meses = meses + 1;
           campobas1 = meses +  22;
           camporet1 = meses +  35;
           campobas2 = meses +  48;
           camporet2 = meses +  61;

          campotA1 = meses + 3;  campotA2 = meses + 39;
          campotB1 = meses + 15; campotB2 = meses + 51;
          campotR1 = meses + 27; campotR2 = meses + 63;

          #4campotB1 = #4campotB1 + #2campobas1;   || total bases  1
          #4campotR1 = #4campotR1 + #2camporet1;   || total reten. 1
          |SI #2campobas1 ! 0; |O #2camporet1 ! 0;
              #4campotA1 = #4campotA1 + 1;
          |FINSI;

          #4campotB2 = #4campotB2 + #2campobas2;   || total bases  2
          #4campotR2 = #4campotR2 + #2camporet2;   || total reten. 2
          |SI #2campobas2 ! 0; |O #2camporet2 ! 0;
              #4campotA2 = #4campotA2 + 1;
          |FINSI;

          #caemi115#13 = #caemi115#13 + #2campobas1;   || total bases  1
          #caemi115#14 = #caemi115#14 + #2camporet1;   || total reten. 1

     |SI #2campobas1 ! 0; |O #2camporet1 ! 0;
              swsuma1 = 1;
          |FINSI;
          #caemi115#16 = #caemi115#16 + #2campobas2;   || total bases  2
          #caemi115#17 = #caemi115#17 + #2camporet2;   || total reten. 2
          |SI #2campobas2 ! 0; |O #2camporet2 ! 0;
              swsuma2 = 1;
          |FINSI;
     |SIGUE;

     |SI swsuma1 = 1; #caemi115#12 = #caemi115#12 + 1; |FINSI;
     |SI swsuma2 = 1; #caemi115#15 = #caemi115#15 + 1; |FINSI;
     #caemi115#18 = #caemi115#14 + #caemi115#17;
     swtraba = 1;
|FPRC;

|DEFBUCLE catra115;
     #2#1;
     ;
     #caemi115#0, "            ";
     #caemi115#0, "zzzzzzzzzzzz";
     ;
     NULL, catra115;
|FIN;

|PROCESO Carga115_02;
     |ABRE #caportad;
     |DDEFECTO #caportad;
     #caportad#0 = enEmpresa;
     #caportad#1 = (#caemi115#3 - 2000);
     |LEE 001#caportad.=;
     |CIERRA #caportad;

     #4#0 = #caemi115#0;
     #4#2 = nAnyo2D;
     |LEE 101#4=;
     |SI FSalida ! 0;
         |DDEFECTO #4;
         #4#0 = #caemi115#0;
         #4#1 = #caemi115#1;
         #4#2 = nAnyo2D;
         |GRABA 020#4b;
     |FINSI;

     desdemes = 0;
     hastames = 0;
     |SI #caemi115#8 = "T";
         |SI #caemi115#2 =  1; desdemes =  1; numf = 78; perialf = "1T"; |FINSI;
         |SI #caemi115#2 =  2; desdemes =  4; numf = 81; perialf = "2T"; |FINSI;
         |SI #caemi115#2 =  3; desdemes =  7; numf = 84; perialf = "3T"; |FINSI;
         |SI #caemi115#2 =  4; desdemes = 10; numf = 87; perialf = "4T"; |FINSI;
         hastames = desdemes + 2;
         enPer = #caemi115#2;
     |SINO;
         desdemes = #caemi115#2;
         hastames = #caemi115#2;
         numf     = 76;
         perialf  = ("00" + #caemi115#2) % -102;
         enPer    = #caemi115#2;
     |FINSI;

     eaPeriodo = perialf;

     |HAZ borrar;
     no_vale = 0;
     swtraba = 0;
     |BUCLE catra115;

     |DDEFECTO #t1511501;

     aAlfa         = #caportad#13;  |QBF aAlfa;
     #t1511501#7   = ("000000000" + aAlfa) % -109;
     eaAlfa   = #caportad#2;  |HAZ QuitaRaros;    #t1511501#8  = eaAlfa;

     |HAZ TDeclaracion2015;

     #t1511501#10  = eaEjer;
     #t1511501#11  = eaPeriodo;

     enNume = #caemi115#12 + #caemi115#15;
     eaAlfa = enNume; eaAlfa = ("00000000000000" + eaAlfa) % -115;
     #t1511501#12 = eaAlfa;

     enNume = #caemi115#13 + #caemi115#16;  |HAZ Conver17;   #t1511501#13 = eaAlfa;
     enNume = #caemi115#14 + #caemi115#17;  |HAZ Conver17;   #t1511501#14 = eaAlfa;
     enNume = 0;                            |HAZ Conver17;   #t1511501#15 = eaAlfa;

     #t1511501#23 = &13 + &10;

     aAlfa = "<T1150" + #t1511501#10 + #t1511501#11 + "0000>";  |XGRABA nHandle2, aAlfa;
     aAlfa = "<AUX>";                                           |XGRABA nHandle2, aAlfa;
     aAlfa = " " * 70;                                          |XGRABA nHandle2, aAlfa;
     aAlfa = " " * 4;                                           |XGRABA nHandle2, aAlfa;
     aAlfa = " " * 4;                                           |XGRABA nHandle2, aAlfa;
     aAlfa = " " * 9;                                           |XGRABA nHandle2, aAlfa;
     aAlfa = " " * 213;                                         |XGRABA nHandle2, aAlfa;
     aAlfa = "</AUX>";                                          |XGRABA nHandle2, aAlfa;

     |PARA nCampo = 1;  |SI nCampo [ 22;  |HACIENDO nCampo = nCampo + 1;
           aAlfa = #t1511501#nCampo#;
           |XGRABA nHandle2, aAlfa;
     |SIGUE;

     aAlfa = "</T1150" + #t1511501#10 + #t1511501#11 + "0000>";  |XGRABA nHandle2, aAlfa;
|FPRC;

|PROCESO moy11502;
     |HAZ LeeUnidad;
     eaModelos  = "115";
     eaEjer     = #caemi115#3;
     eaPeriodo  = ("00" + #caemi115#2) % -102;

     nAnyo2D    = #caemi115#3 - 2000;

    perialf = eaPeriodo;
    |SI #caemi115#8 = "T";
        |SI #caemi115#2 = 1;  perialf = "1T"; |FINSI;
        |SI #caemi115#2 = 2;  perialf = "2T"; |FINSI;
        |SI #caemi115#2 = 3;  perialf = "3T"; |FINSI;
        |SI #caemi115#2 = 4;  perialf = "4T"; |FINSI;
    |FINSI;

     eaPathLanza = eaUnidad + "\MODELOS";            |RMKDIR eaPathLanza;
     eaPathLanza = eaPathLanza + "\" + eaModelos;    |RMKDIR eaPathLanza;
     eaPathLanza = eaPathLanza + "\" + eaEjer;       |RMKDIR eaPathLanza;
     eaPathLanza = eaPathLanza + "\" + perialf;     |RMKDIR eaPathLanza;
     eaPathLanza = eaPathLanza + "\";

     |DBASE eaPathEmision;
     eaPathEmision = eaPathEmision + "emisiones";       |MKDIR eaPathEmision;
     eaPathEmision = eaPathEmision + "/" + eaModelos;   |MKDIR eaPathEmision;
     eaPathEmision = eaPathEmision + "/" + eaEjer;      |MKDIR eaPathEmision;
     eaPathEmision = eaPathEmision + "/" + perialf;    |MKDIR eaPathEmision;
     eaPathEmision = eaPathEmision + "/";

     eaPathInternet = eaUnidad + "/AEAT";                |RMKDIR eaPathInternet;
     eaPathInternet = eaPathInternet + "/INTERNET";      |RMKDIR eaPathInternet;
     eaPathInternet = eaPathInternet + "/" + eaModelos;  |RMKDIR eaPathInternet;
     eaPathInternet = eaPathInternet + "/" + eaEjer;     |RMKDIR eaPathInternet;
     eaPathInternet = eaPathInternet + "/" + perialf;   |RMKDIR eaPathInternet;
     eaPathInternet = eaPathInternet + "/";

     |HAZ Botones2015;
     |SI enTecla = 0;  |ACABA;  |FINSI;

     |ABRE #1;
     #1#0 = #caemi115#0;
     #1#1 = nAnyo2D;
     |LEE 001#1=;
     |SI FSalida = 0;
         |HAZ AlAgicl1; || leer Modulo Integrador. ....
     |SINO;
         |MENAV "     ATENCION! NO existen datos en PORTADAS MOD. OFICIALES";
         |CIERRA #1;
         |ACABA;
     |FINSI;
     |CIERRA #1;

     nSwHay = 0;

     aFichero = "catra115" + Usuario;
     |NOME_DAT #2#aFichero#;

     des_emp = #caemi115#0;
     has_emp = #caemi115#0;
     |HAZ por_dnis;

     |ABRE #4;

     eaFicheroPlano = "115" + (("00000" + #1#0) % -105) + ".txt";
     eaFicheroImpre = "115" + (("00000" + #1#0) % -105) + ".pdf";
     eaFicheroError = "err" + (("00000" + #1#0) % -105) + ".txt";

     aAlfa = eaPathLanza + eaFicheroPlano;  |RFBORRA aAlfa;
     aAlfa = eaPathLanza + eaFicheroImpre;  |RFBORRA aAlfa;
     aAlfa = eaPathLanza + eaFicheroError;  |RFBORRA aAlfa;

     aAbre = eaPathEmision + eaFicheroPlano;
     |XABRE "BW", aAbre, nHandle2;

     |HAZ Carga115_02;

     |XCIERRA nHandle2;

     |CIERRA #4;
     |DELINDEX #2;

     enEmpresa    = #caportad#0;
     eaNomEmpresa = #caportad#2;

     |HAZ PresentaModelo_02;

     |SI eaOpcion = "1";
         |HAZ SacaErrorPtl;
     |FINSI;

     |SI eaOpcion = "3";
         aAlfa =  "      Los ficheros generados estan: " + eaPathInternet + " de su PC";
         |MENAV aAlfa;

         |CONFI "0000SQuiere cargar la pagina de hacienda del modelo";
         |SI FSalida = 0;
             |HAZ AbrePagina;
         |FINSI;
     |FINSI;
|FPRC;
