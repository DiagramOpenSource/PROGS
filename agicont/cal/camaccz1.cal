|FICHEROS;
   camaccz1 #0;

   camaccw4 #999,MANTE;

   caivasop #2;
   caivarep #12;

   caconimp #8;
   caivaasi #200;

   camacc01 #501;
   camacc02 #502;

   espejo@ #990;
|FIN;

|VARIABLES;
    &entrada = 1;
    nInkey   = 0;
    aAlfa1   = "";
    aAlfa2   = "";
    nNume1   = 0;
    nNume2   = 0;
    aFichero = "";
    inform1  = "camaccz1";
    inform2  = "camaccy1";
    informe  = "";

    nOp      = 0;
    nError   = 0;
    nCampo   = 0;
    aMas     = "";
    aDef     = "";
    fDFecha  = @;
    nSwAlgun = 0;
    aMulDep  = "";
    nDdepar  = 0;
    nHdepar  = 0;
    nSwSel   = 0;
    nSw      = 0;
    &aCif      = "";
    &enSwLinea = 0;
    &enTotal   = 0;
    &enTBases  = 0;
    &enTCuotas = 0;
    &eaTexto   = "";
    aParam     = "";
    &ePrograma = 0;
|FIN;

|INCLUYE acceso_i;
|INCLUYE puntos_i;
|INCLUYE varcaj_i;
|INCLUYE varnue_i;

|| ***************** Calculos de Pantalla
|CALCULO Primero;
  |HAZ inicio;
  |HAZ eVarEmpresa;

  |ABRE #200;
  |LEE 000#200p;
  |SI FSalida ! 0;
      |DDEFECTO #200;
      |GRABA 040#200n;
  |FINSI;
  |CIERRA #200;
  aAlfa1  = #200#44;
  |C_M #0#7,1,aAlfa1;
  |C_M #0#8,1,aAlfa1;
  #0#4 = fec1;
  #0#5 = fec2;
|FCAL;

|CALCULO QueIva; |TIPO 0;
   |SI #0#27 = "R";
       |PINTA 2018, "Cobradas : ";
       eaTexto = "Cobrados";
   |SINO;
       |PINTA 2018, "Pagadas .: ";
       eaTexto = "Pagados";
   |FINSI;
   aTipoIVA = #0#27;
|FCAL;

|CALCULO defectos; |TIPO 7;
    #0#4 = fec1; |PINTA #0#4;
    #0#5 = fec2; |PINTA #0#5;
|FCAL;

|CALCULO fecha; |TIPO 0;
   |SI #0Campo < fec1; |O #0Campo > fec2;
       |MENAV "    ERROR ESTA FECHA NO CORRESPONDE AL EJERCICIO";
       |ERROR;
       |ACABA;
   |FINSI;
   |SI Campo = 5; |Y #0#5 < #0#4;
       |MENAV "    FECHA ERRONEA ";
       |ERROR;
   |FINSI;
|FCAL;

|CALCULO DesHasC; |TIPO 0;
     |SI #0#11 = "N";
         #0#12 = 001;  |PINTA #0#12;  |C_M #0#12, 1, "N";
         #0#13 = 999;  |PINTA #0#13;  |C_M #0#13, 1, "N";
         |PARA nCampo = 14;  |SI nCampo [ 25;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |SINO;
         |C_M #0#12, 1, "S";
         |C_M #0#13, 1, "S";
         |PARA nCampo = 14;  |SI nCampo [ 25;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "N";
               #0nCampo = 0;  |PINTA #0nCampo;
         |SIGUE;
     |FINSI;
|FCAL;

|CALCULO NoMasCon; |TIPO 0;
     |SI #0Campo = 0;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 25;  |HACIENDO nCampo = nCampo + 1;
               #0nCampo = 0;  |C_M #0nCampo, 1, "N";
         |SIGUE;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 25;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |FINSI;
|FCAL;

|CALCULO Tipo7C26; |TIPO 7;
    |SI #0#27 = "R";
        aAlfa1 = "    (S) Fra Cobradas, (N) Fras pendientes de Cobrar, (T) Todas";
    |SINO;
        aAlfa1 = "    (S) Fra Pagadas, (N) Fras pendientes de Pagar, (T) Todas";
    |FINSI;
    |MENSA aAlfa1;
|FCAL;

|PROCESO cuenta; |TIPO 0;
    p_alfa1 = #0Campo;
    salidas = FSalida;
    |HAZ punto;
    |SI p_error ! 0;  |ERROR;  |FINSI;
|FPRC;

|CALCULO nodepar; |TIPO 7;
   |C_M #0#9, 1, eaDepar;
   |C_M #0#10, 1, eaDepar;
|FCAL;

|| *************************************************************************
|| *************************************************************************
|PROCESO ModCobros;
   enSwCaja = 1;
   |SI nInkey = 12; eModoCaja = 4; |FINSI;
   |SI nInkey = 13; eModoCaja = 2; |FINSI;

   eDevenCaja = #501#17;
   ePerioCaja = #999#37;
   |HAZ ePathCaja;

   ePrograma = 1;
   nError = 0;
   |SI aTipoIVA = "S";
       |CIERRA #2;  |PATH_DAT #2  ePathCaja;
       |ABRE #2;
       #2#0  = #999#0;
       #2#27 = #999#3;
       #2#2  = #999#2;
       |LEE 041#2=;
       |SI FSalida ! 0;
           nError = 1;
       |FINSI;
       |CIERRA #2;
   |SINO;
       |CIERRA #12;  |PATH_DAT #12  ePathCaja;

       |ABRE #12;
       #12#0  = #999#0;
       #12#27 = #999#3;
       #12#2  = #999#2;
       |LEE 041#12=;
       |SI FSalida ! 0;
           nError = 1;
       |FINSI;
       |CIERRA #12;
   |FINSI;

   |SI nError = 1;
       |MENAV "    Hay un error en los ficheros, no existe la factura";
       |ACABA;
   |FINSI;

   enLibroCaja = #999#0;
   eaSerieCaja = #999#3;
   enRegisCaja = #999#2;
   eDevenCaja  = #999#36;
   |HAZ eTrataClaveZ;

   |SI eModoCaja = 2;
|| ... Graba Auxiliar
       |ABRE #501;
       #501#17 = #999#36;
       #501#13 = #999#13;   ||Rep/Sop
       #501#0 =  #999#0;    ||Libro
       #501#14 = #999#3;    ||Serie
       #501#15 = #999#2;    ||Factura
       |LEE 141#501=;
       |SI FSalida ! 0;
           |BORRA 020#999a;
           |LIBERA #501;
           |CIERRA #501;
           |ACABA;
       |FINSI;

       #999#16 = #501#12;
       #999#17 = #501#8;
       #999#18 = #501#16;
       #999#19 = #501#6;
       #999#20 = #501#7;
       |LIBERA #501;
       |CIERRA #501;

       |SI #0#26 ! "T";
            |SI #999#16 ! #999#10; |Y #0#26 = "S";
                |BORRA 020#999a;
                |ACABA;
            |FINSI;
            |SI #999#16 = #999#10; |Y #0#26 = "N";
                |BORRA 020#999a;
                |ACABA;
            |FINSI;
        |FINSI;

        |GRABA 020#999a;
   |FINSI;
|FPRC;

|PROCESO LeeCc502;
  |PRINT;
  enSwLinea = 2;
|FPRC;

|DEFBUCLE LeeCc502;
   #502#1;
   ;
   aTipoIVA, eDevenCaja, enLibroCaja, eaSerieCaja,enRegisCaja, 01;
   aTipoIVA, eDevenCaja, enLibroCaja, eaSerieCaja,enRegisCaja, 99;
   ;
   NULL, LeeCc502;
|FIN;

|PROCESO ImprimeAux;

  |SI nSw = 0;
      aCif     = #990#12;
      eaCuenta = #990#5;
  |FINSI;

  |SI aCif ! #990#12;
     |SI #0#28 = "C";
         enSwLinea = 3; |PRINT;
     |FINSI;
     aCif     = #990#12;
     eaCuenta = #990#5;
     enSwLinea = 0;
  |FINSI;

  |SI eaCuenta ! #990#5;
      enSwLinea = 1;
      eaCuenta  = #990#5;
  |FINSI;

  |PDEFECTO #0,39,51;
  nError = 0;

   eDevenCaja = #990#36;
   ePerioCaja = #990#37;
   |HAZ ePathCaja;

   |SI aTipoIVA = "S";
       |CIERRA #2;  |PATH_DAT #2  ePathCaja;
       |ABRE #2;
       |DDEFECTO #2;
       #2#0  = #990#0;
       #2#27 = #990#3;
       #2#2  = #990#2;
       |LEE 041#2=;

       #0#39 = #2#51;
       #0#40 = #2#4;
       #0#41 = #2#50;
       #0#42 = #2#0;
       #0#43  = #2#27;
       aAlfa1 = ("000000" + #2#2) % -106;
       #0#44  = aAlfa1;
       #0#45  = #2#3;
       #0#46  = #2#6;
       #0#47  = #2#7;
       #0#48  = #2#26;
       #0#49  = #2#8  + #2#9  + #2#10 + #2#52 + #2#53;
       #0#50  = #2#14 + #2#15 + #2#16 + #2#56 + #2#57;
       |CIERRA #2;
   |SINO;
       |CIERRA #12;  |PATH_DAT #12  ePathCaja;

       |ABRE #12;
       |DDEFECTO #12;
       #12#0  = #990#0;
       #12#27 = #990#3;
       #12#2  = #990#2;
       |LEE 041#12=;

       #0#39 = #12#51;
       #0#40 = #12#4;
       #0#41 = #12#50;
       #0#42 = #12#0;
       #0#43 = #12#27;
       aAlfa1 = ("000000" + #2#2) % -106;
       #0#44  = aAlfa1;
       #0#45  = #12#3;
       #0#46  = #12#6;
       #0#47  = #12#7;
       #0#48  = #12#26;
       #0#49  = #12#8  + #12#9  + #12#10 + #12#52 + #12#53;
       #0#50  = #12#14 + #12#15 + #12#16 + #12#56 + #12#57;
       |CIERRA #12;
   |FINSI;

   enLibroCaja = #990#0;
   eaSerieCaja = #990#3;
   enRegisCaja = #990#2;
   eDevenCaja  = #999#36;

   enTotal    = #990#16;
   enTBases   = #990#19;
   enTCuotas  = #990#20;

  |SI #0#28 ! "C"; enSwLinea = 0; |FINSI;

  |PRINT;
  |SI #0#28 = "D";
      enSwLinea = 1;
      |BUCLE LeeCc502;
      |SI enSwLinea = 2; enSwLinea = 3; |PRINT; |FINSI;
  |FINSI;
  nSw       = 1;
  enSwLinea = 2;
|FPRC;

|DEFBUCLE ImprimeAux;
   #990#3005;
   ;
   aTipoIVA, "01.01.1900", #0#0, #0#7, #0#2;
   aTipoIVA, "31.12.2999", #0#1, #0#8, #0#3;
   ;
   NULL, ImprimeAux;
|FIN;

|DEFBUCLE ImprimeAux_Cif;
   #990#2006;
   ;
   aTipoIVA, "            ", "01.01.2000", #0#0, #0#7, #0#2;
   aTipoIVA, "zzzzzzzzzzzz", "31.12.2999", #0#1, #0#8, #0#3;
   ;
   NULL, ImprimeAux;
|FIN;

|PROCESO Imprimir;
  informe = inform2;
  |SI #0#28 ! "C";
      informe = inform1;
  |FINSI;
  |MENAV "0000 Recuerde seleccionar el papel de la impresora en Horizontal";

  |IMPRE 0;
  |SI FSalida ! 0;
      |MENAV "    Proceso interrumpido";
      |ACABA;
  |FINSI;
  |INFOR informe;
  |SI FSalida ! 0;
      |MENAV "    Error. No existe informe";
      |FINIMP;
      |ACABA;
  |FINSI;

  nSw = 0;
  enSwLinea = 0;
  |SI #0#28 ! "C";
      |BUCLE ImprimeAux;
  |SINO;
      |BUCLE ImprimeAux_Cif;
  |FINSI;

  |SI nSw = 1;
      |SI #0#28 = "C";
          enSwLinea = 3; |PRINT;
      |FINSI;
      |PIEINF;
  |FINSI;
  |FININF;
  |FINIMP;
|FPRC;

|PROCESO AntesImprimir;
  |C_M #0#28,1,"S"; |C_V #0#28,1,"S"; |C_P #0#28, 1, 1134;
  |C_M #0#29,1,"S"; |C_V #0#29,1,"S"; |C_P #0#29, 1, 1222;
  |C_M #0#30,1,"S"; |C_V #0#30,1,"S"; |C_P #0#30, 1, 1322; |C_A #0#30,1,50;

  |PUSHV  1003 1474;
  |BLANCO 1003 1474;
  |CUADRO 1003 1474;
  |CUADRO 1004 1473;
  |PINTA 1105, " Inf. Det/Res/Res. por CIF : "; |PINTA #0#28;
  |ATRI S; |PINTA 1111, "D";
           |PINTA 1115, "R";
           |PINTA 1128, "C"; |ATRI 0;
  |PINTA 1205, " Fecha Informe :  "; |PINTA #0#29
  |PINTA 1305, " Titulo Informe:  "; |PINTA #0#30

|ET Campo28;
  |ENCAMPO #28#0;
  |SI FSalida = 7; |POPV; |ACABA; |FINSI;
  |SI FSalida = 1; |VETE ElInforme; |FINSI;

|ET Campo29;
  |ENCAMPO #29#0;
  |SI FSalida = 1; |VETE ElInforme; |FINSI;
  |SI FSalida = 7; |POPV; |ACABA; |FINSI;
  |SI FSalida = 2; |VETE Campo28; |FINSI;

  |ENCAMPO #30#0;
  |SI FSalida = 1; |VETE ElInforme; |FINSI;
  |SI FSalida = 7; |POPV; |ACABA; |FINSI;
  |SI FSalida = 2; |VETE Campo29; |FINSI;

|ET ElInforme;
  |CONFI "0000SImprimir informe ";
  |SI FSalida = 0;
      |HAZ Imprimir;
  |FINSI;
  |POPV;
|FPRC;

|| ******************************************************
|CALCULO Tipo7Ccw4; |TIPO 7;
   |SI aTipoIVA = "R";
       aAlfa1 = "0000<F3> Imprime Relacion, <F4> Consulta Cobros, <F5> Modifica Cobros";
   |SINO;
       aAlfa1 = "0000<F3> Imprime Relacion, <F4> Consulta Pagos, <F5> Modifica Pagos";
   |FINSI;
   |MENSA aAlfa1;
|FCAL;

|CALCULO Tipo11Ccw4; |TIPO 11;

 nInkey = FSalida;

 |SI nInkey = 10;
||     |HAZ VerAsiento;
     |ERROR;
 |FINSI;

 |SI nInkey = 11;
     |HAZ AntesImprimir;
 |FINSI;

 |SI nInkey = 12; |O nInkey = 13;
     |LEE 141#999=;
     |HAZ ModCobros;
     |LIBERA #999;
     |PONCONTROL 23, "SI";
 |FINSI;

|FCAL;

|| *************************************************************************

|PROCESO IgualoCampos;
     #999#13 = aTipoIVA;
     #999#0  = enLibroCaja;
     #999#3  = eaSerieCaja;
     #999#2  = enRegisCaja;
     |SI aTipoIVA = "S";
         #999#4  = #caivasop#3;
         #999#5  = #caivasop#4;
         #999#6  = #caivasop#6;
         #999#7  = #caivasop#7;

         #999#8  = #caivasop#8  + #caivasop#9  + #caivasop#10 + #caivasop#52 + #caivasop#53;
         #999#9  = #caivasop#14 + #caivasop#15 + #caivasop#16 + #caivasop#56 + #caivasop#57;
         #999#10 = #caivasop#26;
         #999#11 = #caivasop#50;
         #999#12 = #caivasop#51;
         #999#14 = #caivasop#6;

         #999#21 = #caivasop#45;
         #999#22 = #caivasop#46;
         #999#23 = #caivasop#47;

         #999#26 = #caivasop#8;
         #999#27 = #caivasop#14;

         #999#28 = #caivasop#9;
         #999#29 = #caivasop#15;

         #999#30 = #caivasop#10;
         #999#31 = #caivasop#16;

         #999#32 = #caivasop#52;
         #999#33 = #caivasop#56;

         #999#34 = #caivasop#53;
         #999#35 = #caivasop#57;
     |FINSI;

     |SI aTipoIVA = "R";
         #999#4  = #caivarep#3;
         #999#5  = #caivarep#4;
         #999#6  = #caivarep#6;
         #999#7  = #caivarep#7;

         #999#8  = #caivarep#8  + #caivarep#9  + #caivarep#10 + #caivarep#52 + #caivarep#53;
         #999#9  = #caivarep#14 + #caivarep#15 + #caivarep#16 + #caivarep#56 + #caivarep#57;
         #999#10 = #caivarep#26;
         #999#11 = #caivarep#50;
         #999#12 = #caivarep#51;
         #999#14 = #caivarep#6;

         #999#21 = #caivarep#45;
         #999#22 = #caivarep#46;
         #999#23 = #caivarep#47;

         #999#26 = #caivarep#8;
         #999#27 = #caivarep#14;

         #999#28 = #caivarep#9;
         #999#29 = #caivarep#15;

         #999#30 = #caivarep#10;
         #999#31 = #caivarep#16;

         #999#32 = #caivarep#52;
         #999#33 = #caivarep#56;

         #999#34 = #caivarep#53;
         #999#35 = #caivarep#57;
     |FINSI;
|FPRC;

|| //////////////////////////////

|PROCESO Facturas;
  nSwSel   = 0;
  enSwCaja = 0;
  |SI aTipoIVA = "R";
      enConcepto = #12#6;
  |SINO;
      enConcepto = #2#6;
  |FINSI;
  |HAZ EsCajaIva;
  |SI enSwCaja = 0; |ACABA; |FINSI;

  |SI #0#11 = "N";
      nSwSel = 0;
      |PARA nCampo = 14; |SI nCampo [ 25; |HACIENDO nCampo = nCampo + 1;
            |SI #0nCampo ! 0;
                |SI #0nCampo = enConcepto;
                    nSwSel = 1;
                    |SAL;
                |FINSI;
            |FINSI;
      |SIGUE;
      |SI nSwSel = 0; |ACABA; |FINSI;
  |FINSI;

  |SI aTipoIVA= "S";
      enLibroCaja = #2#0;
      eaSerieCaja = #2#27;
      enRegisCaja = #2#2;
  |SINO;
      enLibroCaja = #12#0;
      eaSerieCaja = #12#27;
      enRegisCaja = #12#2;
  |FINSI;
  |HAZ IgualoCampos;

|SI nOp = 0;
|| ... Graba Auxiliar
   #501#17 = eDevenCaja;
   #501#13 = #999#13;   ||Rep/Sop
   #501#0 =  #999#0;    ||Libro
   #501#14 = #999#3;    ||Serie
   #501#15 = #999#2;    ||Factura
   |LEE 141#501=;
   |SI FSalida ! 0;
       |DDEFECTO #501;
       #501#13 = #999#13;
       #501#17 = eDevenCaja;
       #501#0  = #999#0;
       #501#14 = #999#3;
       #501#15 = #999#2;

       #501#2 = #999#21;
       #501#3 = #999#22;
       #501#4 = #999#23;

       |GRABA 020#501b;
    |FINSI;
    |LIBERA #501;
|FINSI;

    #999#16 = #501#12;
    #999#17 = #501#8;
    #999#18 = #501#16;
    #999#19 = #501#6;
    #999#20 = #501#7;

    |SI #0#26 ! "T";
        |SI #999#16 ! #999#10; |Y #0#26 = "S"; |ACABA; |FINSI;
        |SI #999#16 = #999#10; |Y #0#26 = "N"; |ACABA; |FINSI;
    |FINSI;

    #999#36 = #501#17;
    #999#37 = #501#18;
    |GRABA 020#999n;
    nSwAlgun = 1;
|FPRC;

|DEFBUCLE FrasVenta;
   #12#1;
   3, 6, 4, 49;
   #0#0, #0#7, #0#2, #0#4, #0#12, #0#31, #0#9;
   #0#1, #0#8, #0#3, #0#5, #0#13, #0#32, #0#10;
   e;
   NULL, Facturas;
|FIN;

|DEFBUCLE FrasCompra;
   #2#1;
   3, 6, 4, 49;
   #0#0, #0#7, #0#2, #0#4, #0#12, #0#31, #0#9;
   #0#1, #0#8, #0#3, #0#5, #0#13, #0#32, #0#10;
   e;
   NULL, Facturas;
|FIN;

|PROCESO Lee501;

   ePerioCaja = #501#18;
   |HAZ ePathCaja;

   eDevenCaja = #501#17;

   |SI aTipoIVA = "S";
       |CIERRA #2;  |PATH_DAT #2  ePathCaja;
       |ABRE #2;
       #2#0  = #501#0;
       #2#27 = #501#14;
       #2#2  = #501#15;
       |LEE 041#2=;
       |SI FSalida = 0;
           nSwSel = 1;
           |SI #2#4  < #0#31; |O #2#4  > #0#32; nSwSel = 0; |FINSI;
           |SI #2#6  < #0#12; |O #2#6  > #0#13; nSwSel = 0; |FINSI;
           |SI #2#27 < #0#7;  |O #2#27 > #0#8;  nSwSel = 0; |FINSI;
           |SI #2#2  < #0#2;  |O #2#3  > #0#3;  nSwSel = 0; |FINSI;
           |SI #2#49 < #0#9;  |O #2#49 > #0#10; nSwSel = 0; |FINSI;
           |SI nSwSel = 1;
               |HAZ Facturas;
           |FINSI;
      |FINSI;
      |CIERRA #2;
   |FINSI;

   |SI aTipoIVA = "R";
       |CIERRA #12; |PATH_DAT #12 ePathCaja;
       |ABRE #12;
       #12#0  = #501#0;
       #12#27 = #501#14;
       #12#2  = #501#15;
       |LEE 041#12=;
       |SI FSalida = 0;
           nSwSel = 1;
           |SI #12#4  < #0#31; |O #12#4  > #0#32; nSwSel = 0; |FINSI;
           |SI #12#6  < #0#12; |O #12#6  > #0#13; nSwSel = 0; |FINSI;
           |SI #12#27 < #0#7;  |O #12#27 > #0#8;  nSwSel = 0; |FINSI;
           |SI #12#2  < #0#2;  |O #12#3  > #0#3;  nSwSel = 0; |FINSI;
           |SI #12#49 < #0#9;  |O #12#49 > #0#10; nSwSel = 0; |FINSI;
           |SI nSwSel = 1;
               |HAZ Facturas;
           |FINSI;
      |FINSI;
      |CIERRA #12;
   |FINSI;
|FPRC;

|DEFBUCLE Lee501;
   #501#1;
   ;
   aTipoIVA, 0001, #0#0, "  ", 000001;
   aTipoIVA, 9999, #0#1, "zz", 999999;
   e;
   NULL, Lee501;
|FIN;

|| ******************************************************
|PROCESO AuxMin;
 #999#13 = aTipoIVA;
 #999#36 = 0;
 #999#0  = 01;
 #999#3  = "  ";
 #999#2  = 000001;
 #999#4  = "01.01.2000";
|FPRC;

|PROCESO AuxMax;
 #999#13 = aTipoIVA;
 #999#36 = 9999;
 #999#0  = 99;
 #999#3  = "zz";
 #999#2  = 999999;
 #999#4  = "31.12.2999";
|FPRC;

|CALCULO impre; |TIPO 3;

    |DBASE aMas;
    aDef = aMas + "def/camaccw4";
    |CARGA_DEF aDef, espejo@;
    |SI FSalida ! 0;
        |MENAV "    Error cargando def camaccw4";
        |ACABA;
    |FINSI;

   |SI #0#11 = "N";
       #0#12 = 001;
       #0#13 = 999;
   |FINSI;

   aFichero = "x" + Usuario;
   |NOME_DAT #999 aFichero;
   |NOME_DAT #990 aFichero;
   |DELINDEX #999;

   aTipoIVA = #0#27;
   nSwAlgun = 0;
   |ABRE #999;

   |ABRE #501;
   eDevenCaja = 0;
   nOp        = 0;
   |SI aTipoIVA = "R"; |BUCLE FrasVenta;  |FINSI;
   |SI aTipoIVA = "S"; |BUCLE FrasCompra; |FINSI;
   |CIERRA #501;

   |SI enEjercicio > 2014;
       nOp = 1; |BUCLE Lee501;
   |FINSI;

   |CIERRA #999;

   nForzCaja = 1;
   |SI nSwAlgun = 1;
       |SI aParam = "Informe";
           |HAZ Imprimir;
       |SINO;
           |PINPA #0#999;
           |SI #0#27 = "S";
               |ATRI R; |PINTA 0522, "Compra en Regimen Especial de Criterio de Caja"; |ATRI 0;
               |ATRI R; |PINTA 0831, "%Pagado"; |ATRI 0;
               |ATRI I; |PINTA 1868, "Pagado";
               |ATRI 0; |PINTA 1874, " -";
           |FINSI;
           |ENTLINEAL #3#999, -5, 4, 22, 0, AuxMin, AuxMax;
       |FINSI;
   |SINO;
       |MENAV "    No existe Facturas dentro de la seleccion informada";
   |FINSI;
   nForzCaja = 0;

   |DELINDEX #999;

   |DESCARGA_DEF #990;
|FCAL;

|PROGRAMA;
   aParam = PARAMETRO; |QBF aParam;
   aAlfa1 = aParam % 101;
   aParam = aParam - aAlfa1;

   #0#27 = aAlfa1;

   |HAZ Primero;
   |SI enEjercicio < 2014;
       |MENAV "    Fras en Regimen Especial de Cr. de Caja a partir del Ejercicio 2014";
       |ACABA;
   |FINSI;
   #0#35 = enEmpresa;
   #0#36 = enEjercicio;
   #0#37 = eaNombreEmp;
   #0#38 = eaNifEmp;

   |CLS;
   |PINPA #0#0;
   |SI #0#27 = "R";
       |ATRI R; |PINTA 0607, "Facturas de Venta en Regimen de Criterio de Caja"; |ATRI 0;
       |PINTA 2018, "Cobradas : ";
       eaTexto = "Cobrados";
       #0#51   = "Cliente";
   |SINO;
       |ATRI R; |PINTA 0607, "Facturas de Compra en Regimen de Criterio de Caja"; |ATRI 0;
       |PINTA 2018, "Pagadas .: ";
       eaTexto = "Pagados";
       #0#51   = "Proveedor";
   |FINSI;
   aTipoIVA = #0#27;

   |SI aParam = "Informe";
       |ATRI R;
       |SI #0#27 = "R";
           |PINTA 0607, "Informe Fras de Venta en Regimen Especial de Criterio de Caja";
           #0#30 = "INFORME DE FACTURAS DE VENTA EN REGIMEN DE CRITERIO DE CAJA";
       |SINO;
           |PINTA 0607, "Informe Fras de Compra en Regimen Especial de Criterio de Caja";
           #0#30 = "INFORME DE FACTURAS DE COMPRA EN REGIMEN DE CRITERIO DE CAJA";
       |FINSI;
       |ATRI 0;
       |PINTA 2035, "Inf.Det/Res/Res.por CIF:   ";
       |ATRI S; |PINTA 2039, "D";
                |PINTA 2043, "R";
                |PINTA 2055, "C"; |ATRI 0;
       |PINTA 2107, " Fecha :                                                        ";
       |PINTA 2207, " Titulo:                                                        ";
       |PINTA 2307, "읕컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴켸";
       |C_M #0#28, 1, "S"; |C_V #0#28, 1, "S";
       |C_M #0#29, 1, "S"; |C_V #0#29, 1, "S";
       |C_M #0#30, 1, "S"; |C_V #0#30, 1, "S";
   |FINSI;
   |PINDA #0#0;

   |ENDATOS #1#0;
|FPRO;
