|FICHEROS;
    pacontap #0;
    camaasic #2;    || apuntes cabs.
    camaasil #3;    || apuntes lins.
    cacuenta #4;    || plan contable
    cacuebal #15;   || cuentas de balance
    caclipro #16;   || clientes/proveedores
    ca8m0002 #422;
    ca8cumay #410;
|FIN;

|VARIABLES;
    &enDiario   = 0;
    &enContador = 0;
    fiche = "";
    handle = "";
    varalfa = "";
    trabajo = "";
    trabaj1 = "";
    traba = "";
    alfa1 = "";
    alfa2 = "";
    alfa3 = "";
    alfa4 = "";
    nume1 = 0;
    nume2 = 0;
    nume3 = 0;
    nume4 = 0;
    sw = 0;
    &param = 0;

    ult_apunt = 0;
    docum = 0;
    impor = 0;
    conte = "";
    digcu = 0;
    conce = 0;
    fecha = @;
    descr = "";
    signe = "";
    FEstado = 0;
    para1 = 0;
    para2 = 0;
    para3 = 0;
    para4 = 0;
    digito = 0;
    mascara = "";
    nada = "";
    debe = 0;
    haber = 0;
|FIN;

|INCLUYE acceso_i;
|INCLUYE actcta_i;
|INCLUYE actudi_i;
|INCLUYE varnue_i;
|INCLUYE defect_i;

|PROCESO mayus;
  #0Campo = #0Campo > " ";
  |PINTA #0Campo;
|FPRC;

____________________________________/ CALCULOS DE PANTALLA
|PROCESO mira; |TIPO 0;
    #4#0 = #0Campo;
|HAZ digito;
|SI digito = 0;
    |MENAV "    Longitud fuera de nivel ...";
    |ERROR;
|FINSI;
|FPRC;
____________________________________/ CALCULOS DEL PASE
|PROCESO la_129;
|SI debe ! haber;
        #4#0 = #0#8;
    |HAZ digito;
        conte = #0#8;
        digcu = digito;
        impor = debe - haber;
    |SI impor [ 0;
            signe = "D";
            impor = -impor;
    |SINO;
            signe = "H";
    |FINSI;
    |HAZ apuntes;
|FINSI;
|FPRC;

|PROCESO cli_prov;
  alfa1 = #4#0 % 103;
  |SI alfa1 ! "430";  |Y alfa1 ! "440";  |Y alfa1 ! "400";  |Y alfa1 ! "410";
      |ACABA;
  |FINSI;
  |DDEFECTO #16;
  |SI alfa1 = "430";|O alfa1 = "440";
      #16#23 = "C";
  |SINO;
      #16#23 = "P";
  |FINSI;
  #16#0 = #4#0;              || codigo (cuenta)
  #16#1 = #4#2;              || nombre
  #16#2 = varalfa % 5340;    || direccion
  #16#6 = varalfa % 9325;    || poblacion
  #16#9 = varalfa % 4310;    || cif
  #16#10 = #4#0;             || cuenta contable
  #16#11 = #4#1;             || digito
  |GRABA 020#16n;
|FPRC;


|PROCESO digito;
    alfa1 = #4#0;   |QBF alfa1;
    alfa2 = alfa1 % 0;   nume1 = alfa2;
    digito = 0;
|SI nume1 = dig4;   digito = 1;    |FINSI;
|SI nume1 = dig5;   digito = 2;    |FINSI;
|SI nume1 = dig6;   digito = 3;    |FINSI;
|SI nume1 = dig7;   digito = 4;    |FINSI;
|SI nume1 = dig8;   digito = 5;    |FINSI;
|SI nume1 = dig9;   digito = 6;    |FINSI;
|FPRC;

|PROCESO cuentas;
|DDEFECTO #4;

    #4#0 = varalfa % 112;     || cuenta
|HAZ digito;
|SI digito ! 0;
        #4#1 = digito;            || digito
        #4#2 = varalfa % 1330;    || descripcion
    |HAZ pinta;

        mascara = #4#0;
    |QBF mascara;
        mascara = mascara +"zzzzzzzzzzzz";
        mascara = mascara % 112;

        #4#54 = mascara;
        #4#55 = #4#1;

    |HAZ defect;

    |GRABA 000#4n;
    |SI FSalida = 0;
        |HAZ cli_prov;
            alfa1 = varalfa % 11811;   nume1 = alfa1;
            alfa2 = varalfa % 12911;   nume2 = alfa2;
        |SI nume1 ! nume2;
                conte = #4#0;
                digcu = #4#1;
                impor = nume1 - nume2;
            |SI impor [ 0;
                    signe = "H";
                    impor = -impor;
            |SINO;
                    signe = "D";
            |FINSI;
                alfa1 = #4#0 % 101;
            |SI alfa1 ! "6";|Y alfa1 ! "7";
                |HAZ apuntes;
            |FINSI;
        |FINSI;
    |FINSI;
|SINO;
    |MENAV "    No corresponden los Niveles; proceso interrumpido ...";
        sw = 1;
|FINSI;
|FPRC;

|PROCESO pinta;
|ATRI I;
|PINTA 2221, #4#0;
|PINTA 2234, #4#2;
|ATRI 0;
|FPRC;

|PROCESO nombre;
    Pos = -1;
    alfa1 = #0#1;
|QBF alfa1;
    alfa1 = alfa1 + "subcta";
|QBF alfa1;
    traba = trabaj1;
|QBF traba;
    alfa1 = alfa1 + traba + ".dbf";
|QBF alfa1;
    fiche = "rb  " + alfa1;
|QBF fiche;
|FPRC;

|PROCESO principio;
|PARA para1 = 1;  |SI para1 [ 547;  |HACIENDO para1 = para1 + 1;
        varalfa = handle + " 1";
    |FLEE varalfa;
|SIGUE;
|FPRC;

|PROCESO pasa;  |TIPO 3;
 |SI #30#26 < 80;
     enEjercicio = 2000 + #30#26;
 |SINO;
     enEjercicio = 1900 + #30#26;
 |FINSI;
|SI #0#0 ! "S";     |ACABA;   |FINSI;

    ult_apunt = 1;

    enDiario = 0;
    |HAZ contador;
    docum = enContador;
    alfa1 = "01.01.19" + #30#26;
    fecha = alfa1;

|ABRET #2;
|ABRET #3;
|ABRET #4;
|ABRET #16;
    trabaj1 = #0#2;
|HAZ nombre;
|FABRE fiche;
    handle = FSalida;
|SI FSalida ] 0;
    |HAZ principio;
        sw = 0;
    |PARA; |SI sw = 0; |HACIENDO;
            varalfa = handle + " 211";
        |FLEE varalfa;
        |SI FSalida = 211;
            |HAZ cuentas;
        |SINO;
                sw = 1;
        |FINSI;
    |SIGUE;
        FSalida = handle;
    |FCIERRA FSalida;
    |HAZ la_129;
|FINSI;
|CIERRAT #2;
|CIERRAT #3;
|CIERRAT #4;
|CIERRAT #16;
|FPRC;
____________________________________/ GRABACION DEL ASIENTO
|PROCESO apuntes;
|SI impor ! 0;|Y conte ! doce;|Y digcu ! 0;
    |DDEFECTO #3;
        #3#00 = 0;          || diario
        #3#01 = fecha;      || fecha
        #3#02 = docum;      || documento
        #3#03 = ult_apunt;  || apunte
        #3#04 = conte;      || cuenta
        #3#05 = digcu;      || digito cuenta
        #3#06 = #0#6;       || concepto
        #3#07 = #0#7;       || descripcion
        #3#08 = signe;      || signo
        #3#09 = impor;      || importe
    |SI #3#08 = "D";
            #3#16 = #3#4;
            #3#17 = #3#5;
    |SINO;
            #3#10 = #3#4;
            #3#11 = #3#5;
    |FINSI;
        #3#12 = "";           || cobros/pagos
        #3#13 = 0;            || libro
        #3#14 = 0;            || orden
        #3#15 = "";           || serie
        #3#22 = 0;            || nro.recibo
        #3#19 = "";           || tipo acumulador
        #3#20 = 0;            || numero acumulador
        #3#21 = "";           || departamento
    |GRABA 020#3n;
    |SI FSalida = 0;
        |SI #3#8 = "D";
                debe = debe + #3#9;
        |SINO;
                haber = haber + #3#9;
        |FINSI;
        |HAZ cabecera;
            ressum = #3#9;
        |HAZ actualiz1;     || actualiza cuentas
            opera = "A";
        |HAZ altatra;       || actualiza niveles inferiores
            d_diari = #3#0;
            d_opera = #3#8;
            d_impor = #3#9;
        |HAZ actu_diari;    || actualiza diario
            ult_apunt = ult_apunt + 10;
    |FINSI;
|FINSI;
|FPRC;

|PROCESO cabecera;
|DDEFECTO #2;
    #2#0 = #3#0;
    #2#1 = #3#1;
    #2#2 = #3#2;
|LEE 101#2=;
    FEstado = FSalida;
    #2#0 = #3#0;         || diario
    #2#1 = #3#1;         || fecha
    #2#2 = #3#2;         || documento
    #2#3 = #2#2;         || asiento
|SI #3#8 = "D";
        #2#4 = #2#4 + #3#9;
    |SI #2#09 = 0; #2#08 = #3#4; #2#09 = #3#5; |FINSI;      || contrp.HABER
|FINSI;
|SI #3#8 = "H";
        #2#5 = #2#5 + #3#9;
    |SI #2#11 = 0; #2#10 = #3#4; #2#11 = #3#5; |FINSI;      || contrp.DEBE
|FINSI;
|SI FEstado = 0;  |GRABA 020#2a;  |FINSI;
|SI FEstado ! 0;  |GRABA 020#2n;  |FINSI;
|LIBERA #2;
|FPRC;
