|FICHEROS;
    cajacomp #0;
    cajainli #1;
    cajainca #2;
    cacuenta #4;
    cajasald #90;
|FIN;

|VARIABLES;
    &entrada = 1;
    fecalf = "";
    mensa1 = "    ERROR ESTA FECHA NO CORRESPONDE AL EJERCICIO";
    varalf = "";
    sw = 0;
    diaant = 0;
    docant = 0;
    fecant = @;
    informe = "cajacomp";
    inform1 = "cajacomp";
    inform2 = "cajacom2";

    &a_haber = 0;
    &a_debe = 0;
    &saldo = 0;
    &s_haber = 0;
    &s_debe = 0;

    &t_haber = 0;
    &t_debe = 0;
    &t_saldo = 0;

    &anth = 0;
    &antd = 0;
    &hoyh = 0;
    &hoyd = 0;
    &swprim = 0;
    nume1 = 0;
    afecha = @;
    paso = 0;
    cpartida = "";
    dpartida = 0;
    {-1}form  = "";           || Variables menu formato <1> o <2>
        form1 = "2400";
        form2 = "1";
        form3 = "Elija formato: ";
        form4 = "12";
        form5 = " < 1 > ";
        form6 = " < 2 > ";
    op_form = 0;
|FIN;

|INCLUYE acceso_i;

|| ...... Calculo Campos
|PROCESO comienza; |TIPO 8;
 |HAZ inicio;
 |ABRE #90;
 |LEE 101#90p;
 |SI FSalida ! 0;
     |MENAV "    Error, no Existe fichero <Saldo Dia Anterior> ";
     |LIBERA #90;
     |CIERRA #90;
     |PTEC 807;
     |ACABA;
 |FINSI;
 cpartida = #90#2;
 dpartida = #90#3;
 afecha = #90#7; |SI afecha < fec1; afecha = fec1; |FINSI;
 antd = 0; anth = 0;
 |SI #90#1 > 0;
     anth = #90#1;
 |SINO;
     antd = -#90#1;
 |FINSI;
 saldo = #90#1;
 t_saldo = saldo;
 |LIBERA #90;
 |CIERRA #90;
|FPRC;

|PROCESO defectos; |TIPO 7;
 |SI paso = 0;
     #0#1 = afecha; |PINTA #0#1;
     #0#2 = fec2;   |PINTA #0#2;
     paso = 1;
 |FINSI;
|FPRC;

|PROCESO fechas; |TIPO 0;
  |SI #0Campo < fec1; |O #0Campo > fec2;
      |MENAV "    ERROR ESTA FECHA NO CORRESPONDE AL EJEFCICIO ";
      |ERROR;
      |ACABA;
  |FINSI;
|| ..................................................
  |SI Campo = 2;
      |SI #0#2 < #0#1;
          |MENAV "    ERROR EN FECHA ";
          |ERROR;
          |ACABA;
      |FINSI;
  |FINSI;
|FPRC;

|PROCESO contar;
 |SI sw = 0;
        diaant = #1#0;
        fecant = #1#1;
        docant = #1#2;
        sw = 1;
 |FINSI;
 |SI diaant ! #1#0; |O fecant ! #1#1; |O docant ! #1#2;
     diaant = #1#0;
     fecant = #1#1;
     docant = #1#2;
     s_debe = 0;
     s_haber = 0;
 |FINSI;

 |DDEFECTO #4;
 #4#0 = #1#4;
 #4#1 = #1#5;
 |LEE 000#4=;

 |SI #1#8 = "P";
     a_haber = 0;
     a_debe = #1#9;
 |SINO;
     a_haber = #1#9;
     a_debe = 0;
 |FINSI;

 saldo = saldo + (a_haber - a_debe);

 |PRINT;
 s_debe  = s_debe + a_debe;
 s_haber = s_haber + a_haber;
 t_haber = t_haber + a_haber;
 t_debe = t_debe + a_debe;
 t_saldo = saldo;
 swprim  = 1;
|FPRC;

|DEFBUCLE cacomdia0;
 #1#2;
 ;
 #0#3, #0#1,      0, 0000;
 #0#4, #0#2, 999999, 9999;
 ;
 NULL, contar;
|FIN;

|PROCESO impre;  |TIPO 3;
 sw = 0;

 |MENU form;
       op_form = FSalida;
 |SI op_form [ 0; |ACABA; |FINSI;

 |SI op_form = 1; informe = inform1; |FINSI;
 |SI op_form = 2; informe = inform2; |FINSI;

 |IMPRE 0;
 |SI FSalida ! 0; |ACABA; |FINSI;

 |INFOR informe;
 |SI FSalida ! 0; |FINIMP; |ACABA; |FINSI;

 |ABRE #4;

 |BUCLE cacomdia0;

|| |SI sw ! 0; |PIEINF; |FINSI;

 |SI sw ! 0;
     nume1 = t_haber - t_debe + #90#1;
     |SI nume1 > 0;
         hoyh = 0;
         hoyd = nume1;
     |SINO;
         hoyh = -nume1;
         hoyd = 0;
     |FINSI;
     |PIEINF;
 |FINSI;

 |FININF;
 |FINIMP;
 |CIERRA #4;
|FPRC;
