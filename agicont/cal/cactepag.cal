 ||  el #5 y el #8 estan reservados para los 'INCLUYE'
 || /para 'cacuenta' y 'catradia' respect.
|FICHEROS;
    cactepag #00;
    camaasic #02;   || apuntes cabs.
    camaasil #03;   || apuntes lins.
    capagcab #06;   || pagares cabs.
    capaglin #07;   || pagares lins.
    cacuadre #09;   || control cobros y pagos
    camandia #10;   || diarios
    cacuebal #11;   || cuentas de balance
    camanpag #12;   || pagos
    caivasop #13;   || Libro Iva Soportado.....
    caconcep #33;   || Conceptos.....

    caivaasi #210;
    ca8m0002 #422;
|FIN;

|VARIABLES;
    &enDiario   = 0;
    &enContador = 0;

    nConp    = "";
    nDcop    = "";
    nDcof    = "";
    acon     = 0;
    aDesc1   ="";
    aDesc2   ="";
    descri2  = "";
    &entrada = 1;
    alfa1 = "";
    alfa2 = "";
    alfa3 = "";
    alfa4 = "";
    aAlfa4 = "";
    aCampo1 = "";
    aCampo2 = "";
    aCampo3 = "";
    nume1 = 0;
    nume2 = 0;
    nume3 = 0;
    nume4 = 0;
    fech1 = @;
    fech2 = @;
    fech3 = @;
    fech4 = @;
    arteris = 0;
    swarteris = 0;
    mes_anyo = "";
    muestra = "";
    digito = 0;
    longi = 0;
    asie = 0;
    apun = 0;
    cuen = "";
    digi = 0;
    sign = "";
    ptas = 0;
    desc = "";
    descrip = "";
    impo = 0;
    sw = 0;
    FEstado = 0;

    seleccio = 0;
    sw_tipo = 0;

    nPara1 = 0;
    alfa5 = "";
    alfa6 = "";
    nume5 = 0;
    nume6 = 0;
    aMuestraO = "";
    nSwLaC = 0;

    swarterisBan = 0;
    muestraBan   = "";
    longiBan     = 0;
    nSwLaCBan    = 0;
    arterisBan   = 0;
    nSwQue       = 0;
    deparT       = "";
    deparL       = "";
    depar        = "";
    swdepar      = 0;
    nConcepto    = 0;
    &enEjerBal = 0;
|FIN;

|INCLUYE varnue_i;
|INCLUYE inicio_i;
|INCLUYE actcta_i;
|INCLUYE defec1_i;

**************************************************************************
          CALCULOS DESDE CAMPOS
**************************************************************************

|CALCULO limite; |TIPO 0;
   |C_M #0#7,0,alfa1;
   |SI alfa1 = "N"; |ACABA; |FINSI;

   |SI #0#7 < fec1;|O #0#7 > fec2;
       |MENAV "    ATENCION!!! La fecha no corresponde al ejercicio contable ...";
       |ERROR;
   |FINSI;
|FCAL;

|CALCULO final816; |TIPO 7;
 |PTEC 816;
|FCAL;

|CALCULO FechaAsto;
   |SI #cactepag#16 ! "C";
      |C_M #cactepag#7,1,"N";
      || #cactepag#13 = "N"; |PINTA #cactepag#13; |C_M #cactepag#13,1,"N";
   |SINO;
      |C_M #cactepag#7,1,"S"; |C_M #cactepag#13,1,"S";
   |FINSI;
|FCAL;

|| *************************************************************************
|PROCESO cabecera;       || actualiza cabeceras
  |SI #3#09 ! 0;
      |DDEFECTO #2;
      #2#00 = #3#0;    || diario
      #2#01 = #3#1;    || fecha
      #2#02 = #3#2;    || documento
      #2#03 = #3#2;    || asiento
      |LEE 101#2=;
      FEstado = FSalida;
      |SI #3#08 = "D";
          #2#04 = #2#04 + #3#09;
          |SI #2#09 = 0; #2#08 = #3#4; #2#09 = #3#5; |FINSI;  || contrp.HABER
      |FINSI;
      |SI #3#08 = "H";
          #2#05 = #2#05 + #3#09;
          |SI #2#11 = 0; #2#10 = #3#4; #2#11 = #3#5; |FINSI;  || contrp.DEBE
      |FINSI;
      #2#6 = #2#4 - #2#5;                            || saldo
      |SI FEstado = 0;  |GRABA 020#2a;  |FINSI;
      |SI FEstado ! 0;  |GRABA 020#2n;  |FINSI;
      |LIBERA #2;
  |FINSI;
|FPRC;

|PROCESO actualiz3;
    |ABRE #10;
    #10#0 = #3#0;
    |LEE 121#10=;
    |SI FSalida = 0;
        |SI #3#8 = "D";  #10#2 = #10#2 + #3#9;  |FINSI;
        |SI #3#8 = "H";  #10#3 = #10#3 + #3#9;  |FINSI;
        |GRABA 020#10a;
    |FINSI;
    |LIBERA #10;
    |CIERRA #10;
|FPRC;
|| *************************************************************************
|PROCESO contab2;    || graba apunte
    #0#08 = #6#0;   |PINTA #0#08;
    #0#09 = asie;   |PINTA #0#09;
    #0#10 = apun;   |PINTA #0#10;
    |DDEFECTO #3;
    #3#00 = #0#4;   || diario
    |SI #0#16 = "C"; #3#01 = #0#7; |FINSI;
    |SI #0#16 = "V"; #3#01 = #6#6; |FINSI;
    |SI #0#16 = "E"; #3#01 = #6#5; |FINSI;
    #3#02 = asie;   || asiento
    #3#03 = apun;   || apunte
    #3#04 = cuen;   || cuenta
    #3#05 = digi;   || digito
    #3#06 = acon;   || concepto
    #3#07 = desc;   || descripcion
    #3#08 = sign;   || signo
    #3#09 = ptas;   || importe

    |SI #3#8 = "D";
        #3#16 = #3#4;
        #3#17 = #3#5;
        #3#10 = "";
        #3#11 = 0;
    |SINO;
        #3#16 = "";
        #3#17 = 0;
        #3#10 = #3#4;
        #3#11 = #3#5;
    |FINSI;
    #3#21 = depar;
    |GRABA 020#3n;
    |HAZ cabecera;           || cabecera apunte
    ressum = #3#9;
    |HAZ actualiz1;          || cuenta
    |HAZ actualiz3;          || diario
    opera = "A";
    |HAZ altatra;            || subniveles
|FPRC;

|| ................... Contabiliza por pagos.....
|PROCESO por_cob;
       |HAZ descripcio2;
       apun = apun + 10;
       desc = descri2;
       cuen = #6#1;
       digi = #6#2;
       sign = "D";
       ptas = #7#7;
       depar = deparL;
       |HAZ contab2;
|FPRC;
|| ///////////////// Asientos especiales
|PROCESO AstoEspecial;
      |SI #6#13 = "S";  ||a efectos a pagar

          |HAZ filtro_prov;
          cuen = alfa2;
          digi = #6#2;
          sign = "H";
          ptas = #6#9;
          desc = descrip;
          acon = #0#5;
          |HAZ cuenta;
          depar = deparT;
          |HAZ contab2;

          apun = apun + 10;
          sign = "D";
          |HAZ cuenta;
          depar = deparT;
          |HAZ contab2;

          apun = apun + 10;
      |SINO;
          || ... #6#8 = "S";
      |FINSI;

      |HAZ filtro_banco;
      cuen = alfa2;
      digi = #6#4;
      sign = "H";
      ptas = #6#9;
      desc = descrip;
      acon = #0#5;
      |HAZ cuenta;
      depar = deparT;
      |HAZ contab2;
|FPRC;
|| \\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\
|| ................... Controla la grabacion de Apuntes
|PROCESO contab1;    || Con "*"
  deparT  = "";
  swdepar = 0;
  seleccio = 1;
  |HAZ descripcion;
  |SI apun = 0;
      enDiario = #0#4;
      |HAZ contador;
      asie = enContador;
      apun = - 9;
  |FINSI;

|| ..... Apuntes al proveedor
  |SI #0#13 = "S";
       cuen = #6#1;
       digi = #6#2;
       acon = #0#14;
       sign = "D";
       |BUCLELIN 2por_cob#6;
  |SINO;
       apun = apun + 10;
       cuen = #6#1;
       digi = #6#2;
       sign = "D";
       ptas = #6#9;
       desc = descrip;
       acon = #0#5;
       |HAZ contab2;
  |FINSI;
|| ............................................

  apun = apun + 10;
  |SI #9#80 = "N";   ||Como hasta ahora
      |SI #6#13 = "S";  ||a efectos a pagar
          |HAZ filtro_prov;
          cuen = alfa2;
          digi = #6#2;
      |SINO;
          cuen = #6#3; || al banco
          digi = #6#4;
          #6#8 = "S";
      |FINSI;
      sign = "H";
      ptas = #6#9;
      desc = descrip;
      acon = #0#5;
      |HAZ cuenta;
      depar = deparT;
      |HAZ contab2;
  |SINO;
       || ....Haz Asiento Especial
       |HAZ AstoEspecial;
  |FINSI;

  || ............. Fin Asiento
  apun = 0;
  #6#7 = "S";
  |GRABA 020#6a;
  |LIBERA #6;
|FPRC;

|DEFBUCLE cactepag0;
 #6#1;
 5,12,7,14;
 #0#0, #0#2, "S", "N", #0#11; || fecha emision, impresas Pagare SI, c. emision. NO
 #0#1, #0#3, "S", "N", #0#12;
 be;
 NULL, contab1;
 #6#1, #6#2, #6#0;
|FIN;
|| *************************************************************************
|| *************************************************************************
|| *************************************************************************
|| *************************************************************************

|PROCESO contab11;    || sin "*" 1§ los que van todos juntos
  deparT  = "";
  swdepar = 0;
  seleccio = 1;
  |HAZ descripcion;
  |SI apun = 0;
      enDiario = #0#4;
      |HAZ contador;
      asie = enContador;
      apun = - 9;
  |FINSI;
  apun = apun + 10;
  cuen = #6#1;
  digi = #6#2;
  sign = "D";
  ptas = #6#9;
  desc = descrip;
  acon = #0#5;
  |HAZ contab2;
  impo = impo + #6#9;

  #6#7 = "S";
  |GRABA 020#6a;
  |LIBERA #6;
|FPRC;

|DEFBUCLE cactepag01;
 #6#1;
 5,12,7,13,14;
 #0#0, #0#2, "S", "N", "S", #0#11; || fecha emision, impresas Pagare SI, c. emision. NO
 #0#1, #0#3, "S", "N", "S", #0#12; || Contabilizando a Efectos a Pagar.
 be;
 NULL, contab11;
 #6#1, #6#2, #6#0;
|FIN;

|PROCESO contab12;   || controla la grabacion de apuntes a Banco

  deparT  = "";
  swdepar = 0;
  seleccio = 1;
  |HAZ descripcion;
  |SI apun = 0;
      enDiario = #0#4;
      |HAZ contador;
      asie = enContador;
      apun = -9;
  |FINSI;
  apun = apun + 10;
  cuen = #6#1;
  digi = #6#2;
  sign = "D";
  ptas = #6#9;
  desc = descrip;
  acon = #0#5;
  |HAZ contab2;
  apun = apun + 10;
  cuen = #6#3;
  digi = #6#4;
  sign = "H";
  ptas = #6#9;
  desc = descrip;
  acon = #0#5;
  |HAZ cuenta;
  |HAZ contab2;
  apun = 0;
  #6#7 = "S";
  #6#8 = "S";
  |GRABA 020#6a;
  |LIBERA #6;
|FPRC;

|DEFBUCLE cactepag02;
 #6#1;
 5,12,7,13, 14;
 #0#0, #0#2, "S", "N", "N", #0#11;   || fecha emision, impresas Pagare SI, c. emision. NO
 #0#1, #0#3, "S", "N", "N", #0#12;   || no contabiliza a Efectos a Pagar.
 be;
 NULL, contab12;
 #6#1, #6#2, #6#0;
|FIN;


|PROCESO final;     || graba apunte cuando la contrapartida es unica
 |SI arteris = 0;
     apun = apun + 10;
     cuen = #9#21;
     digi = digito;
     sign = "H";
     ptas = impo;
     desc = #0#6;
     |QBF desc;
     |SI #0#16 = "C"; desc = desc + " " + #0#7; |FINSI;
     |SI #0#16 = "V"; desc = desc + " " + #6#6; |FINSI;
     |SI #0#16 = "E"; desc = desc + " " + #6#5; |FINSI;
     acon = #0#5;
     |HAZ cuenta;
     |HAZ contab2;
     apun = 0;
 |FINSI;
|FPRC;

|| ***********************************************************************
|PROCESO filtro_prov;        || construye la contrapartida individual

 |SI #6#2 = 1;  nume1 = dig4;  |FINSI;
 |SI #6#2 = 2;  nume1 = dig5;  |FINSI;
 |SI #6#2 = 3;  nume1 = dig6;  |FINSI;
 |SI #6#2 = 4;  nume1 = dig7;  |FINSI;
 |SI #6#2 = 5;  nume1 = dig8;  |FINSI;
 |SI #6#2 = 6;  nume1 = dig9;  |FINSI;

 aMuestraO = muestra;

 |SI nSwLaC = 1;
     alfa5 = muestra % 0;
     nume5 = alfa5;
     alfa5 = "";
     |PARA nPara1 = 1; |SI nPara1 [ nume5; |HACIENDO nPara1 = nPara1 + 1;
           nume6 = (nPara1 * 100) + 1;
           alfa6 = muestra % nume6;
           |SI alfa6 = "C";
               alfa6 = #6#1 % nume6;
           |FINSI;
           alfa5 = alfa5 + alfa6;
     |SIGUE;

     muestra = alfa5;
     alfa2   = muestra;
 |FINSI;

  nume2 = nume1 - (longi - 1);
  |SI arteris = 1;
      |SI swarteris = 0;
          nume2 = (100 * longi) + nume2;
          alfa1 = #6#1 % nume2;
          alfa2 = muestra + alfa1;             || si es "*"
      |SINO;
          nume2 = nume2 - 4;
          |SI nume2 < 0;
              |MENAV "    Cuenta 'Efec.Pagar' del Control Pagares con longitud fuera de nivel ...";
              nume2 = nume2 + 4;
              alfa1 = "0" * nume2;
              alfa2 = muestra + alfa1;
          |SINO;
             |SI nume2 = 0;  alfa1 = "";              |FINSI;   || cuenta ajustada
             |SI nume2 > 0;  alfa1 = "0" * nume2;     |FINSI;   || rellena de ceros
             ||tique 2236_109 para luego el vencimiento
            || |SI #0#16 = "C"; mes_anyo = (#0#7 % 402) + (#0#7 % -102); |FINSI;
            || |SI #0#16 = "V"; mes_anyo = (#6#6 % 402) + (#6#6 % -102); |FINSI;
            || |SI #0#16 = "E"; mes_anyo = (#6#5 % 402) + (#6#5 % -102); |FINSI;
             mes_anyo = (#6#6 % 402) + (#6#6 % -102); || ... Siempre Vencimiento
             alfa2 = muestra + alfa1 + mes_anyo;  || si es "%"
          |FINSI;
      |FINSI;
  |FINSI;
  muestra = aMuestraO;
|FPRC;

|PROCESO filtro_banco;        || construye la contrapartida individual

 |SI #6#4 = 1;  nume1 = dig4;  |FINSI;
 |SI #6#4 = 2;  nume1 = dig5;  |FINSI;
 |SI #6#4 = 3;  nume1 = dig6;  |FINSI;
 |SI #6#4 = 4;  nume1 = dig7;  |FINSI;
 |SI #6#4 = 5;  nume1 = dig8;  |FINSI;
 |SI #6#4 = 6;  nume1 = dig9;  |FINSI;

 aMuestraO = muestraBan;

 |SI nSwLaCBan = 1;
     alfa5 = muestraBan % 0;
     nume5 = alfa5;
     alfa5 = "";
     |PARA nPara1 = 1; |SI nPara1 [ nume5; |HACIENDO nPara1 = nPara1 + 1;
           nume6 = (nPara1 * 100) + 1;
           alfa6 = muestraBan % nume6;
           |SI alfa6 = "C";
               alfa6 = #6#3 % nume6;
           |FINSI;
           alfa5 = alfa5 + alfa6;
     |SIGUE;

     muestraBan = alfa5;
     alfa2   = muestraBan;
 |FINSI;

 |SI arterisBan = 1;
     nume2 = nume1 - (longiBan - 1);
     |SI swarterisBan = 0;
          nume2 = (100 * longiBan) + nume2;
          alfa1 = #6#3 % nume2;
          alfa2 = muestraBan + alfa1;             || si es "*"
     |SINO;
          nume2 = nume2 - 4;
          |SI nume2 < 0;
              |MENAV "    Cuenta 'Efec.Pagar' del Control Pagares con longitud fuera de nivel ...";
              nume2 = nume2 + 4;
              alfa1 = "0" * nume2;
              alfa2 = muestraBan + alfa1;
          |SINO;
              |SI nume2 = 0;  alfa1 = "";              |FINSI;   || cuenta ajustada
              |SI nume2 > 0;  alfa1 = "0" * nume2;     |FINSI;   || rellena de ceros
              |SI #0#16 = "C"; mes_anyo = (#0#7 % 402) + (#0#7 % -102); |FINSI;
              |SI #0#16 = "V"; mes_anyo = (#6#6 % 402) + (#6#6 % -102); |FINSI;
              |SI #0#16 = "E"; mes_anyo = (#6#5 % 402) + (#6#5 % -102); |FINSI;
              alfa2 = muestraBan + alfa1 + mes_anyo;  || si es "%"
          |FINSI;
     |FINSI;
  |FINSI;
  muestraBan = aMuestraO;
|FPRC;
|| -------------------------------------------------------------------------
|PROCESO cuenta;    || comprueba si la cuenta esta dada de alta
    |ABRE #5;
    #5#0 = cuen;
    #5#1 = digi;
    |LEE 001#5=;
    |SI FSalida ! 0;
        |DDEFECTO #5;
            #5#0 = cuen;
            #5#1 = digi;
        |HAZ defect1;
        |PUSHV 0501 2380;
        |PINPA #0#5;
        |PINDA #0#5;
        |ENDATOS #1#5;
        |SI FSalida = 0;
            |GRABA 020#5n;
        |FINSI;
        |POPV;
    |FINSI;
    |CIERRA #5;
|FPRC;

|| ......................................................................
|PROCESO descripcion;
    descrip = #0#6;
    |QBF descrip;
    alfa1 = "";
    |SI #0#18 = "B";
        |SI #6#11 ! 0;
            alfa1 = #6#11;
        |FINSI;
    |SINO;
        alfa1 = #6#0;
    |FINSI;

    alfa1 = alfa1 + " " + #6#6;
    descrip = descrip + " " + alfa1;
|FPRC;

|PROCESO descripcio2;

 |ABRE #13;
 |DDEFECTO #13;
 #13#0  = #7#2;  || Libro
 #13#27 = #7#9;  || Serie
 #13#2  = #7#3;  || Factura
 |LEE 001#13=;
 |CIERRA #13;
 deparL = #13#49;
 |SI swdepar = 0;
     swdepar = 1;
     deparT  = deparL;
 |FINSI;
 |SI deparT ! deparL; deparT = "00"; |FINSI;

|| ... Descripcion del Asiento
    alfa1 = "";
    alfa2 = "";
    alfa3 = "";
    alfa4 = "";

    || .. Descripcion
    descri2 = #0#15; |QBF descri2;

    || .. Numero factura
    alfa1  = #7#9 + aCampo2; |QBF alfa1;
    aAlfa4 = #7#3;
    |SI aCampo3 = "S"; aAlfa4 = ("000000" + aAlfa4) % -106; |FINSI;
    alfa1 = alfa1 + aAlfa4;
    alfa1 = " " + alfa1;

    || .. Fecha Vencimiento
    alfa3 = #6#6;
    alfa4 = " " + (alfa3 % 106) + (alfa3 % -104);

    |SI #0#17 = "D";
        |HAZ DesFactura;
    |FINSI;
    |SI #0#17 = "V";
        descri2 = descri2 + alfa4;
    |FINSI;
    |SI #0#17 = "A";
        descri2 = descri2 + alfa1 + " " + alfa4;
    |FINSI;
    |SI #0#17 = "F";
        descri2 = descri2 + alfa1; ||numero factura
    |FINSI;
    |SI #0#17 = "B";
        descri2 = descri2 + " " + #6#11; |QBF descri2;
        descri2 = descri2 + " " + #6#6; || Numero pagare + Fecha Vto.
    |FINSI;
|FPRC;

|PROCESO DesFactura;
 aDesc1    = "";
 aDesc2    = "";
 nConcepto = 0;

 |ABRE #13;
 #13#0  = #7#2;  || Libro
 #13#27 = #7#9;  || Serie
 #13#2  = #7#3;  || Factura
 |LEE 001#13=;
 |SI FSalida = 0;
     aDesc1    = #13#7; |QBF aDesc1;
     nConcepto = #13#6;
 |SINO;
     |ABRE #12;
     #12#0 = #7#2;
     #12#1 = #7#3;
     #12#2 = #7#9;
     #12#3 = #7#4;
     |LEE 001#12=;
     |SI FSalida = 0;
         nConcepto = #12#27;
         aDesc1    = #12#28; |QBF aDesc1;
     |SINO;
         aDesc1    = #7#5;   |QBF aDesc1;
     |FINSI;
     |CIERRA #12;
 |FINSI;
 |CIERRA #13;


 aDesc2 = "";
 |SI nConcepto ! 0;
     |ABRE #33;
     #33#0 = nConcepto;
     |LEE 001#33=;
     |SI FSalida = 0;
         aDesc2 = #33#1; |QBF aDesc2;
     |FINSI;
     |CIERRA #33;
     aDesc1 = aDesc1 - aDesc2;
 |FINSI;
 descri2 = descri2 + aDesc1;
|FPRC;
|| ...............................

|PROCESO digito;         || saca el digito de la contrapartida del control
    sw   = 0;
    digito = 0;  || inicializa el digito
    |SI longi = dig4; |Y dig4 ! 0; sw = 1; digito = 1; |FINSI;
    |SI longi = dig5; |Y dig5 ! 0; sw = 1; digito = 2; |FINSI;
    |SI longi = dig6; |Y dig6 ! 0; sw = 1; digito = 3; |FINSI;
    |SI longi = dig7; |Y dig7 ! 0; sw = 1; digito = 4; |FINSI;
    |SI longi = dig8; |Y dig8 ! 0; sw = 1; digito = 5; |FINSI;
    |SI longi = dig9; |Y dig9 ! 0; sw = 1; digito = 6; |FINSI;
    |SI sw = 0;
        |MENAV "    Contrapartida de Pagos Fuera de Nivel!!! Proceso Abortado ...";
    |FINSI;
|FPRC;

|PROCESO digitoBan;         || saca el digito de la contrapartida del control
    sw   = 0;
    digito = 0;  || inicializa el digito
    |SI longiBan = dig4; |Y dig4 ! 0; sw = 1; digito = 1; |FINSI;
    |SI longiBan = dig5; |Y dig5 ! 0; sw = 1; digito = 2; |FINSI;
    |SI longiBan = dig6; |Y dig6 ! 0; sw = 1; digito = 3; |FINSI;
    |SI longiBan = dig7; |Y dig7 ! 0; sw = 1; digito = 4; |FINSI;
    |SI longiBan = dig8; |Y dig8 ! 0; sw = 1; digito = 5; |FINSI;
    |SI longiBan = dig9; |Y dig9 ! 0; sw = 1; digito = 6; |FINSI;
    |SI sw = 0;
        |MENAV "    Contrapartida de Pagos Fuera de Nivel!!! Proceso Abortado ...";
    |FINSI;
|FPRC;

|PROCESO arteris;     || desglosa la contrapartida del control

 |SI #9#80 = "N";
     alfa1 = #9#21;
 |SINO;
     alfa1 = #9#81;
 |FINSI;

 nSwQue = 0;

 |QBF alfa1;
 alfa2 = alfa1 % 0;
 longi = alfa2;            || 'longi' sirve despues (no tocar)
 nume3 = (longi * 100) + 1;
 alfa2 = alfa1 % nume3;
 |SI alfa2 = "*";|O alfa2 = "%";
     nume3     = 99 + longi;
     muestra   = alfa1 % nume3;
     arteris   = 1;
     swarteris = 0;
     |SI alfa2 = "%";
         swarteris = 1;
     |FINSI;
     nSwQue = 1;
 |SINO;
     |HAZ digito;
     |SI sw = 0;   arteris = 2;  |FINSI; || contrapartida fuera de nivel!!!
     |SI sw ! 0;   arteris = 0;  |FINSI;
     muestra = alfa1;
 |FINSI;

  nSwLaC = 0;
  |PARA nume4 = 1; |SI nume4 [ longi; |HACIENDO nume4 = nume4 + 1;
        nume1 = (nume4 * 100) + 1;
        alfa3 = alfa1 % nume1;
        |SI alfa3 = "C";  nSwLaC = 1; |FINSI;
  |SIGUE;

  |SI nSwLaC = 1; nSwQue = 1; |FINSI;


|| ... campos de Asiento Especial
    |SI #9#80 = "N"; |ACABA; |FINSI;

    alfa1 = #9#83;
    |QBF alfa1;
    alfa2    = alfa1 % 0;
    longiBan = alfa2;            || 'longiBan' sirve despues (no tocar)
    nume3 = (longiBan * 100) + 1;
    alfa2 = alfa1 % nume3;
    |SI alfa2 = "*";|O alfa2 = "%";
        nume3 = 99 + longiBan;
        muestraBan    = alfa1 % nume3;
        arterisBan    = 1;
        swarterisBan  = 0;
        |SI alfa2 = "%";
            swarterisBan = 1;
        |FINSI;
        nSwQue = 1;
    |SINO;
         |HAZ digitoBan;
         |SI sw = 0;   arterisBan = 2;  |FINSI; || contrapartida fuera de nivel!!!
         |SI sw ! 0;   arterisBan = 0;  |FINSI;
         muestraBan = alfa1;
    |FINSI;

    nSwLaCBan = 0;
    |PARA nume4 = 1; |SI nume4 [ longiBan; |HACIENDO nume4 = nume4 + 1;
          nume1 = (nume4 * 100) + 1;
          alfa3 = alfa1 % nume1;
          |SI alfa3 = "C";  nSwLaCBan = 1; |FINSI;
    |SIGUE;

    |SI nSwLaCBan = 1; nSwQue = 1; |FINSI;

|FPRC;

|PROCESO subniveles;     || actualiza subniveles de cuentas
 |SI modcon = 0;
    |ABRE #8;
    |DDEFECTO #8;
    #8#0 = codcon;
    |LEE 001#8=;
    |SI FSalida = 0;
        |BORRA 020#8a;
    |FINSI;
    |CIERRA #8;
 |SINO;
    |SUB_EJECUTA "camaapua.run";
 |FINSI;
|FPRC;

|PROCESO LeeCaivaasi;
 |ABRE #210;
 |DDEFECTO #210;
 |LEE 001#210p;
 |CIERRA #210;
 aCampo1 = #210#45;
 aCampo2 = #210#48;
 aCampo3 = #210#49;
|FPRC;

||*******************************************************************
|CALCULO proceso; |TIPO 3;
     |SI #30#26 < 80;
         enEjercicio = 2000 + #30#26;
     |SINO;
         enEjercicio = 1900 + #30#26;
     |FINSI;


 |ABRE #9;
 |DDEFECTO #9;
 |LEE 000#9p;
 |SI FSalida ! 0;
     |MENAV "    ATENCION!!! Control de Pagares y Talones sin definir ...";
     |CIERRA #9;
     |ACABA;
 |FINSI;
 |CIERRA #9;

 |HAZ LeeCaivaasi;

 |ABRE #2;
 |ABRE #3;
 |ABRE #12;

 apun = 0;
 |HAZ arteris;
 |SI nSwQue = 1;   || Con variacion de Cuentas
     |BUCLE cactepag0;
 |SINO;
     |SI arteris = 0;  || Cuenta Completa
         |BUCLE cactepag01;
         |SI impo ! 0;
             |HAZ final;  || contrapartida englobada
         |FINSI;
         |BUCLE cactepag02;
     |FINSI;
 |FINSI;
 |SI seleccio = 0;
     |MENAV "    Seleccion vacia ...";
 |FINSI;

 |CIERRA #2;
 |CIERRA #3;
 |CIERRA #12;
 |HAZ subniveles;
|FCAL;
