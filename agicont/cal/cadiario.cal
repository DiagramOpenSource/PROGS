|FICHEROS;
  cadiario #0;
  camaasic #1;
  camaasil #2;
  cacuenta #4;
  caconimp #8;
  ca8m0004 #404;

  cabucem1 #100;
|FIN;

|VARIABLES;
  nSwPri = 0;
  FEstado = 0;
  siai = 0;
  &entrada = 1;
  sw = 0;
  fecha = 0;
  fecalf = "";
  mensa1 = "    ERROR ESTA FECHA NO CORRESPONDE AL EJERCICIO";

  aAlfa = "";
  &swlin = 0;
  &descuadre = 0;
  &a_debe = 0;
  &a_haber = 0;
  &tde = 0;
  &tha = 0;
  &hoja = 0;
  &nPagDup = 0;
  &nSwPagDup = 0;
  &eaDesPac  = "";
  d = "D";
  h = "H";
  informe = "";
  alfa1 = "";
  alfa2 = "";
  alfa3 = "";
  nume1 = 0;
  nume2 = 0;
  nume3 = 0;
  nume4 = 0;

  Comodin  = "";
  Comodin1 = "";
  Handle  = 0;
  Numeracion = 1;

  primero    = "";
  temporal1  = "";
  aRegistros = "";
  nRegistros = 0;
  nSwDos     = 0;
  fLaFecha1  = @;
  fLaFecha2  = @;
  fLaFecha3  = @;
  fLaFecha4  = @;

  nPara1     = 0;
  &eaUltNiv  = "";
  nNume1     = 0;
  aNoUlt     = "";
  &enBasImp  = 0;
  &eaNomImp  = "";
  &eaUnidadBTmp = "";

  nDiaAper     = 0;
  nIgual       = 0;
  nConcepto    = 0;
  aDescripcion = "";
  aSubdep      = "";
  nOpe         = 0;
  nLinea       = 0;
  nDia8 = 0;
  fFec8 = @;
  nDoc8 = 0;

  nCa21 = 0;
  nCa23 = 0;
  nCampo = 0;
  aCampo21 = "";
  aCampo23 = "";

  nSwAlgun = 0;

   &eaLegEmp = "";
   &eaLegPer = "";
   &eaFormato   = "";
   &efFecLeg    = @;
   &eaDatLeg    = "";
|FIN;

|INCLUYE acceso_i;
|INCLUYE varnue_i;
|INCLUYE condes_i;

|PROCESO desasien;
  |SI #0#13 < #0#12;
      |MENAV "     Error en Introduccion";
      |ERROR;
  |FINSI;
|FPRC;

|PROCESO desdiari;
  |SI #0#7 < #0#6;
      |MENAV "     Error en Introduccion";
      |ERROR;
  |FINSI;
|FPRC;

|PROCESO no_dep; |TIPO 7;
    alfa1 = #30#24;
|CAMPO_MODIFICA #0#14, 1, alfa1;
|CAMPO_MODIFICA #0#15, 1, alfa1;
|FPRC;


|PROCESO mayor;
  alfa1 = #0Campo > " ";
  #0Campo = alfa1;
  |PINTA #0Campo;
|FPRC;

|PROCESO mayor1;
  |HAZ mayor;
  |SI #0Campo = "S";
      #0#9 = 0;
      #0#10 = 0;
      |PINTA #0#9;
      |PINTA #0#10;
      |CAMPO_MODIFICA #0#9,1,"N";
      |CAMPO_MODIFICA #0#10,1,"N";
  |SINO;
      |CAMPO_MODIFICA #0#9,1,"S";
      |CAMPO_MODIFICA #0#10,1,"S";
  |FINSI;
|FPRC;

|PROCESO estacua;
  |SI #0#9 ! #0#10;
      |MENAV "      ATENCION descuadre en DEBE y HABER";
      |ERROR;
  |FINSI;
|FPRC;

|PROCESO AsAper_d; |TIPO 0;
 |C_M #0Campo, 0, alfa1;
 |SI alfa1 = "N"; |ACABA; |FINSI;
 alfa1 = #0Campo; nCampo = Campo + 2;
 |C_M #0nCampo, 1, alfa1;
|FPRC;

|PROCESO fechad;
  |SI #0#0 < fec1; |O #0#0 > fec2;
      |MENAV mensa1;
      |ERROR;
  |FINSI;
|FPRC;

|PROCESO fechah;
  |SI #0#1 < fec1; |O #0#1 > fec2; |O #0#1 < #0#0;
      |MENAV mensa1;
      |ERROR;
  |FINSI;
|FPRC;

|| *******************************************************

|PROCESO Empresas;

   #cabucem1#0 = eaLegEmp;
   #cabucem1#1 = eaLegPer;
   cod1 = eaLegEmp;
   cod2 = eaLegPer;
   emEmp = 1;
   |HAZ leelo;
   |SI swerror ! 0;  |ACABA;   |FINSI;

   |DBASE fich_alta; |QBF fich_alta;
   fich_alta = fich_alta + "fich/" + (("00000" + #canempre#2) % -105) + #canempre#3 + "/";

   |PATH_DAT #0 fich_alta; |PATH_DAT #1 fich_alta;
   |PATH_DAT #2 fich_alta; |PATH_DAT #4 fich_alta;
   |PATH_DAT #8 fich_alta; |PATH_DAT #404 fich_alta;
   |PATH_DAT #908 fich_alta;

   |HAZ VerCaconimp;
   |HAZ eVarEmpresa;

   |SI eaFormato = "S";
       nSwDos = 0; || no podemos dividir por trimestres
   |SINO;
       |HAZ VQuants;
   |FINSI;

   aNoUlt = "112";
   |SI eaUltNiv = "N";
       |SI dig3 = 1; nNume1 = dig4; |FINSI;
       |SI dig3 = 2; nNume1 = dig4; |FINSI;
       |SI dig3 = 3; nNume1 = dig5; |FINSI;
       |SI dig3 = 4; nNume1 = dig6; |FINSI;
       |SI dig3 = 5; nNume1 = dig7; |FINSI;
       |SI dig3 = 6; nNume1 = dig8; |FINSI;
       nNume1 = 100 + nNume1;
       aNoUlt = nNume1;
   |FINSI;
   |PARA nPara1 = 0; |SI nPara1 [ nSwDos; |HACIENDO nPara1 = nPara1 + 1;

        |ABRE #0;
        #0#8 = 1;
        |LEE 141#0=;
        |SI FSalida ! 0;
            |DDEFECTO #0;
            #0#0 = fec1;
            #0#1 = fec2;
            #0#8 = 1;
            |HAZ LeoConimp8;
            |GRABA 001#0c;
        |FINSI;
        aCampo21 = #0#21;
        aCampo23 = #0#23;

        |SI nSwDos = 0;
            |DDEFECTO #0;
            #0#0 = fec1;
            #0#1 = fec2;
        |SINO;
             |SI nPara1 = 0;
                 |DDEFECTO #0;
                 #0#0 = fec1;
                 #0#1 = fLaFecha1;
                 #0#21 = aCampo21;
                 #0#23 = aCampo23;
             |SINO;
                 |SI nPara1 = 1; #0#1 = fLaFecha2; |FINSI;
                 |SI nPara1 = 2; #0#1 = fLaFecha3; |FINSI;
                 |SI nPara1 = 3; #0#1 = fLaFecha4; |FINSI;
             |FINSI;
        |FINSI;

        #0#5 = efFecLeg;
        #0#8      = 1;
        sw        = 0;
        nPagDup   = #0#4;
        nSwPagDup = 0;


        |SI eaFormato ! "S";
            Impresora = eaNomImp + "?"+ eaUnidadBTmp + "\tmp1,,,?Diario";
            |SI nSwDos ! 0;
                |SI nPara1 = 0;  Impresora = Impresora + "1T"; |FINSI;
                |SI nPara1 = 1;  Impresora = Impresora + "2T"; |FINSI;
                |SI nPara1 = 2;  Impresora = Impresora + "3T"; |FINSI;
                |SI nPara1 = 3;  Impresora = Impresora + "4T"; |FINSI;
            |FINSI;
        |SINO;
             Impresora = eaNomImp + ";" + eaUnidadBTmp +"\Diario";
             Impresora = Impresora + ".pdf";
        |FINSI;

        |HAZ impre;                 || Calculo impresion

        #0#9      = tde;
        #0#10     = tha;
        #0#4      = hoja;
        |SI #0#1 ! fec2;
             #0#0 = #0#1 + 1;
        |SINO;
             #0#0 = fec2;
        |FINSI;
        #0#1  = fec2;
        #0#11 = "S";
        |SI nSwDos ! 0; |Y nPara1 ! 3; #0#11 = "N"; |FINSI;
        |GRABA 001#0a;
        |CIERRA #0;

        |HAZ borrar;                || Borra acumulado

  |SI nSwDos = 0; |O nPara1 = 3;
        |DBASE Comodin1; |QBF Comodin1;
        Comodin1 = Comodin1 + "hacienda/PresLibr/" + eaLegEmp + eaLegPer + "/";
        |SI eaFormato ! "S";
            Comodin1 = Comodin1 + "DIARIO*.xls";
        |SINO;
            Comodin1 = Comodin1 + "DIARIO*.pdf";
        |FINSI;
        |_PDIR Comodin1;
        |SI FSalida = 0;
            |PARA; |SI FSalida = 0; |HACIENDO;
                   Comodin = Comodin1 % -107; Comodin = Comodin % 0103;
                   Numeracion = Comodin; Numeracion = Numeracion + 1;
                   |_SDIR Comodin1;
            |SIGUE;
        |SINO;
            Numeracion = 1;
        |FINSI;

        |SI eaFormato ! "S";
            Comodin = eaUnidadBTmp + "/tmp1.xls";
        |SINO;
            Comodin = eaUnidadBTmp + "/Diario.pdf";
        |FINSI;
        |XABRE "C", Comodin, Handle;
        |SI FSalida ] 0;
            |XCIERRA Handle;
            |DBASE Comodin1; |QBF Comodin1;
            Comodin1 = Comodin1 + "hacienda/PresLibr/";
            |MKDIR Comodin1;
            Comodin1 = Comodin1 + eaLegEmp + eaLegPer + "/";
            |MKDIR Comodin1;
            |SI eaFormato ! "S";
                Comodin1 = Comodin1 + "DIARIO_" + (("000" + Numeracion) % -103) + ".xls";
            |SINO;
                Comodin1 = Comodin1 + "DIARIO_" + (("000" + Numeracion) % -103) + ".pdf";
            |FINSI;
            |IP_REMOTO aAlfa; |QBF aAlfa;
            |SI aAlfa ! "";
               |RECIBE_FICHERO Comodin, Comodin1;
            |SINO;
               |COPIA_FICHERO Comodin, Comodin1;
            |FINSI;
            FSalida = 1;

            |PARA; |SI FSalida ! 0; |HACIENDO;
                   |RFBORRA Comodin;
                   |SI FSalida ! 0; |Y eaFormato ! "S";
                       |MENAV "    Termine la tarea de Excel que tiene activa";
                   |FINSI;
            |SIGUE;
        |FINSI;
     |FINSI;

   |SIGUE;

|FPRC;

|| *********************************************************************
||                             COMIENZO
|| *********************************************************************
|PROCESO LeoConimp8;
   |ABRE #8;
   |DDEFECTO #8;
   #8#0 = 1;
   |LEE 001#8=;
   |CIERRA #8;
   #0#21 = #8#11;
   #0#23 = #8#13;
|FPRC;

|PROGRAMA;

   nCa21 = 21;
   nCa23 = 23;
   |SI PARAMETRO = "";

      eaUltNiv = "S";
      |CLS;
      |CABEZA "Listado Diario Oficial";

      |ABRE #0;

      |HAZ inicio;
      |HAZ eVarEmpresa;
      |HAZ VerCaconimp;

      #0#8 = 1;
      |LEE 141#0=;
      |SI FSalida ! 0;
          |DDEFECTO #0;
          #0#0 = fec1;
          #0#1 = fec2;
          #0#8 = 1;
          |HAZ LeoConimp8;
          |GRABA 001#0c;
      |FINSI;

      FSalida = 1;
      |PARA;|SI FSalida = 1; |HACIENDO;
            sw = 0;
            |PINPA #0#0;
            #0#8 = 1;
            |LEE 141#0=;
            #0#5 = ~;
         || ...   |SI enEjercicio ! 2008; Ocultamos
                #0#21 = "N";
                |C_V #0#21,1,"N"; |C_M #0#21,1,"N";
                |C_V #0#23,1,"N"; |C_M #0#23,1,"N";
                alfa1 = "ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ";
                alfa2 = "                                                             ";
                |PINTA 1810, alfa1;
                |PINTA 1910, alfa2;
                |PINTA 2010, alfa2;
                |PINTA 2110, alfa2;
         || ...  |FINSI;
            |PINDA #0#0;                    || Pinta los datos por defecto
            |ENDATOS #1#0;                  || Seleccion informe
            |SI FSalida = 0;                || Si el conforme es igua a S
                nPagDup = #0#4;
                nSwPagDup = 0;
                |HAZ impre;                 || Calculo impresion
                #0#9 = tde;
                #0#10 = tha;
                #0#4 = hoja;
                |SI #0#1 ! fec2;
                    #0#0 = #0#1 + 1;
                |SINO;
                    #0#0 = fec2;
                |FINSI;
                #0#1 = fec2;
                #0#11 = "S";
                |GRABA 001#0a;
                |HAZ borrar;                || Borra acumulado
                FSalida = 1;                || Vuelve al bucle
            |SINO;
                FSalida = 0;
            |FINSI;
       |SIGUE;
       |CIERRA #0;

   |SINO;
       |HAZ Empresas;
   |FINSI;
|FPRO;

|| *************************************************************************
||                          MEOLLO
|| *************************************************************************
|PROCESO VerAper08_d;
  nIgual = 0;
  |ABRE #404;
  |LEE 001#404p;
  |SI FSalida = 0;
      |SI #404#10 = #1#0; |Y #404#11 = #1#1; |Y #404#12 = #1#2;
          nIgual = 1;
          nDia8 = #404#10;
          fFec8 = #404#11;
          nDoc8 = #404#12;
      |FINSI;
  |FINSI;
  |CIERRA #404;
|FPRC;

|PROCESO AImprimir;
   |SI nOpe = 3; |Y #camaasil#8 ! "D";
       nLinea = nLinea - 1;
       |ACABA;    || No lo imprime
   |FINSI;
   |SI nOpe = 4; |Y #camaasil#8 ! "H";
       nLinea = nLinea - 1;
       |ACABA;    || No lo imprime
   |FINSI;

   swlin = 0;
   |SI #camaasil#8 = "D";
       a_debe = #camaasil#9;
       a_haber = 0;
   |SINO;
       a_debe = 0;
       a_haber = #camaasil#9;
   |FINSI;


   eaDesPac = #2#24;   ||Descripcion
   |QBF eaDesPac;
   eaDesPac = eaDesPac + " " + #2#7;

   |SI eaUltNiv = "N";
       #camaasil#4 = #camaasil#4 % aNoUlt;
   |FINSI;
   |PRINT; nSwAlgun = 1;
|FPRC;

|PROCESO Leo8m004;
   |SI nOpe = 2;      || Sumar solo ls diferencias
       |SI #404#1 ! #404#8;
           tde = tde + (#404#4 + #404#5) ! EURODB;
           tha = tha + (#404#4 + #404#5) ! EURODB;
       |FINSI;
       |ACABA;
   |FINSI;

   |SI #404#13 < #0#14;  |O #404#13 > #0#15;  |ACABA;  |FINSI;

   #2#0 = #404#10;
   #2#1 = #404#11;
   #2#2 = #404#12;
   #2#21 = #404#13;
   #2#24 = "";
   |SI nOpe = 0;      ||primer asiento
       #2#3 = #404#0;  || La Linea
       #2#4 = #404#1;  || Cuenta Anterior
       #2#5 = #404#7;
       #2#6 = nConcepto;
       #2#7 = aDescripcion;
       #2#8 = #404#3;
       #2#9 = #404#4 + #404#5;
       #4#2 = #404#2;
       |HAZ AImprimir;
       |SI #404#1 ! #404#8;
           #1#4 = #1#4 + #2#9;
           #1#5 = #1#5 + #2#9;
       |FINSI;
   |FINSI;

   |SI nOpe = 1; |O nOpe = 3; |O nOpe = 4;
                                            ||primer asiento
       |SI #404#1 = #404#8; |ACABA; |FINSI; ||no ya cambio de cuenta

       nLinea = nLinea + 1;
       #2#3 = nLinea;  || La Linea
       #2#4 = #404#1;  || Cuenta Anterior
       #2#5 = #404#7;
       #2#6 = #0#22;
       #2#7 = #0#23;
       |SI #404#3 = "D";
           #2#8 = "H";
       |SINO;
           #2#8 = "D";
       |FINSI;
       #2#9 = #404#4 + #404#5;
       #4#2 = #404#2;
       |HAZ AImprimir;

       nLinea = nLinea + 1;
       #2#3 = nLinea;  || La Linea
       #2#4 = #404#8;  || Cuenta Anterior
       #2#5 = #404#9;
       #2#6 = #0#22;
       #2#7 = #0#23;
       #2#8 = #404#3;
       #2#9 = #404#4 + #404#5;
       #4#0 = #2#4;
       #4#1 = #2#5;
       |LEE 040#4=;
       |SI FSalida ! 0; |DDEFECTO #4; |FINSI;
       |HAZ AImprimir;
   |FINSI;
|FPRC;

|DEFBUCLE Leo8m004;
 #404#1;
 ;
 nDia8, fFec8, nDoc8, 0001;
 nDia8, fFec8, nDoc8, 9999;
 ;
 NULL, Leo8m004;
|FIN;

|DEFBUCLE Leo8m004_2;
 #404#4;
 ;
 nDia8, fFec8, nDoc8, " ", 0001;
 nDia8, fFec8, nDoc8, "z", 9999;
 ;
 NULL, Leo8m004;
|FIN;

|PROCESO Apertura2008;

   |SALVA_DATOS #1;
   #1#3 = #1#3 - 1; |SI #1#3 [ 0; #1#3 = 1; |FINSI;
   descuadre = (#1#4 - #1#5) ! EURODB;
   swlin = 1;
   |SI descuadre ! 0;
      swlin = 2;
      |SI descuadre < 0; descuadre = descuadre * (-1); |FINSI;
   |FINSI;
   |PRINT; nSwAlgun = 1;

   #1#4 = 0; #1#5 = 0; #1#6 = 0;
   nOpe = 0;
   |SI #0#3 = "S";
       |BUCLE Leo8m004_2;
   |SINO;
       |BUCLE Leo8m004;
   |FINSI;

   #1#3 = #1#3 + 1;
   descuadre = (#1#4 - #1#5) ! EURODB;
   swlin = 1;
   |SI descuadre ! 0;
      swlin = 2;
      |SI descuadre < 0; descuadre = descuadre * (-1); |FINSI;
   |FINSI;
   |PRINT; nSwAlgun = 1;

   nLinea = 0;
   nOpe   = 1;
   |SI #0#3 = "S";
       nOpe = 3; |BUCLE Leo8m004_2;  ||Debe
       nOpe = 4; |BUCLE Leo8m004_2;  ||Haber
   |SINO;
       |BUCLE Leo8m004;
   |FINSI;

   |REPON_DATOS #1;
|FPRC;

|| *************************************************************************
|PROCESO mirasiai;
  |SI #2#21 < #0#14; |O #2#21 > #0#15; |ACABA; |FINSI;
  siai = 1;
   nConcepto    = #2#6;
   aDescripcion = #2#7;
|FPRC;

|PROCESO porimpre;
  |SI #2#21 < #0#14; |O #2#21 > #0#15; |ACABA; |FINSI;
  #4#0 = #2#4;
  #4#1 = #2#5;
  |LEE 040#4=;
  |SI FSalida ! 0;
      |DDEFECTO #4;
  |FINSI;

  eaDesPac = #2#24;   ||Descripcion
  |QBF eaDesPac;
  eaDesPac = eaDesPac + " " + #2#7;

  |SI eaUltNiv = "N";
      #2#4 = #2#4 % aNoUlt;
  |FINSI;

  swlin = 0;
  |SI #2#8 = "D";
      a_debe = #2#9;
      a_haber = 0;
  |SINO;
      a_debe = 0;
      a_haber = #2#9;
  |FINSI;

  |PRINT; nSwAlgun = 1;
|FPRC;

|DEFBUCLE cadiario2;
 #2#2;
 3;
 #1#0, #1#1, #1#2, d, 0001;
 #1#0, #1#1, #1#2, h, 9999;
 e;
 NULL,porimpre;
|FIN;

|PROCESO bucleimp;

  |SI #0#2 = "S";
      |SI sw = 0; fecha = #1#1; sw = 1; |FINSI;
      |SI fecha ! #1#1; |PIEINF; fecha = #1#1; |FINSI;
  |FINSI;

  nConcepto    = 0;
  aDescripcion = "";
  siai = 0;
  |BUCLELIN 0mirasiai#1;
  |SI siai ! 0;
      |SI #0nCa21 = "S";
          |SI #1#0 = 0; |Y nDiaAper = 0;
               |HAZ VerAper08_d;
               |SI nIgual = 1;
                   |HAZ Apertura2008;
                   nDiaAper = 1;
                   |ACABA;           ||este asiento se trata de forma diferente
               |FINSI;
           |FINSI;
      |FINSI;

      descuadre = #1#4 - #1#5;
      |SI descuadre = 0;
          swlin = 1;
          |PRINT; nSwAlgun = 1;
      |SINO;
          swlin = 2;
          |SI descuadre < 0; descuadre = descuadre * (-1); |FINSI;
          |PRINT; nSwAlgun = 1;
      |FINSI;

      |SI #0#3 = "S";
          |BUCLE cadiario2;
      |SINO;
          |BUCLELIN 0porimpre#1;
      |FINSI;
   |FINSI;
|FPRC;

|DEFBUCLE cadiario1;
 #1#2;
 ;
 #0#12, #0#0, #0#6, 000001;
 #0#13, #0#1, #0#7, 999999;
 e;
 NULL,bucleimp;
|FIN;

||  #1#1,#1#3,#1#0,#1#2;

|PROCESO contar;
   |SI #1#1 < #0#0;
       tde = (tde + #1#4) ! EURODB;
       tha = (tha + #1#5) ! EURODB;
       |SI #0nCa21= "S";
           |SI #1#0 = 0; |Y nDiaAper = 0;
               |HAZ VerAper08_d;
               |SI nIgual = 1;
                   nOpe = 2;|BUCLE Leo8m004;   || Solo Suma el ficticio
                   nDiaAper = 1;
               |FINSI;
           |FINSI;
       |FINSI;
   |FINSI;
|FPRC;


|DEFBUCLE cadiario0;
 #1#2;
 ;
      1, fec1, #0#6, 000001;
 999999, #0#0, #0#7, 999999;
 e;
 NULL,contar;
|FIN;

|PROCESO impre;
  hoja = #0#4;

  |SI PARAMETRO ! "";
      |SI enBasImp ! 0;
          |IMPRE -1;
          |SI FSalida ! 0;
              Impresora = Impresora - eaNomImp;
              |IMPRE 0;
          |FINSI;
      |SINO;
          |IMPRE 0;
      |FINSI;
  |SINO;
      Impresora = "Crystal Reports";
      |IMPRE 0;
  |FINSI;

  |SI FSalida ! 0;  |MENAV "     NO existe impresora";  |ACABA;  |FINSI;
  |HAZ los_informes;
  informe = que_i1;
  |SI eaConDes ! "N"; informe = (que_i1 % 107) + "0"; |FINSI;

  |SI Impresora = "CrystalReport";
      informe = "cc" + (informe %0306);
  |SINO;
      |SI PARAMETRO ! ""; |Y eaFormato = "S";
          informe = "cc" + (informe %0306);
      |FINSI;
  |FINSI;

  |INFOR informe;
  |SI FSalida ! 0; |MENAV "     NO existe informe"; |FINIMP;  |ACABA;  |FINSI;

  nDiaAper = 0;
  nSwAlgun = 0;
  |ABRE #4;
  |SI #0#11 = "S";
      |BUCLE cadiario0;
  |SINO;
      tde = #0#9;
      tha = #0#10;
  |FINSI;
  |BUCLE cadiario1;
  |CIERRA #4;
  |SI nSwAlgun = 1; |PIEINF; |FINSI;
  |FININF;
  |FINIMP;
|FPRC;

|PROCESO borrar;
   swlin = 0;
   descuadre = 0;
   a_debe = 0;
   a_haber = 0;
   tde = 0;
   tha = 0;
   hoja = 0;
   nDiaAper = 0;
|FPRC;

|PROCESO VQuants;
  nSwDos     = 0;
  nRegistros = 0;
  primero   = "camaasil";
  temporal1 = fich_alta;
  |LEEDEDEF primero, temporal1;
  |SI FSalida ] 0;
      temporal1 = FSalida % 375;
      aRegistros = (A\temporal1 % 1615);
      nRegistros = aRegistros;
  |FINSI;

  fLaFecha1 = fec2;
  fLaFecha2 = fec2;
  fLaFecha3 = fec2;
  fLaFecha4 = fec2;

  |SI nRegistros > 35000;

      nSwDos = 3;

      alfa1 = fec1;
      alfa2 = alfa1 % 402;
      alfa3 = alfa1 % 704;
      nume1 = alfa2;
      nume1 = nume1 + 2;
      |HAZ CalFecha;
      fLaFecha1 = alfa1;  || .. 31.03

      alfa1 = fec1;
      alfa2 = alfa1 % 402;
      alfa3 = alfa1 % 704;
      nume1 = alfa2;
      nume1 = nume1 + 5;
      |HAZ CalFecha;
      fLaFecha2 = alfa1;  || .. 30.06

      alfa1 = fec1;
      alfa2 = alfa1 % 402;
      alfa3 = alfa1 % 704;
      nume1 = alfa2;
      nume1 = nume1 + 8;
      |HAZ CalFecha;
      fLaFecha3 = alfa1;  || .. 31.09

      alfa1 = fec1;
      alfa2 = alfa1 % 402;
      alfa3 = alfa1 % 704;
      nume1 = alfa2;
      nume1 = nume1 + 11;
      |HAZ CalFecha;
      fLaFecha4 = alfa1;  || .. 31.12

   |FINSI;
|FPRC;

|PROCESO CalFecha;

      |SI nume1 > 12;
          nume1 = nume1 - 12;
          nume2 = alfa3; nume2 = nume2 + 1; alfa3 = nume2;
      |FINSI;
      alfa2 = nume1; alfa2 = ("00" + alfa2) % -102;

      alfa1 = "31.";
      |SI nume1 = 4; |O nume1 = 6; |O nume1 = 9; |O nume1 = 11;
          alfa1 = "30.";
      |SINO;
          |SI nume1 = 2;
              alfa1 = "28.";
              nume2 = alfa3;
              nume3 = nume2 / 4; nume4 = (nume2 / 4) ! 0;
              |SI nume3 = nume4;
                  alfa1 = "29.";
              |FINSI;
          |FINSI;
      |FINSI;

      alfa1 = alfa1 + alfa2 + "." + alfa3;
|FPRC;

|PROCESO CopiaNuevo;
   |DBASE Comodin; |QBF Comodin;
   Comodin = Comodin + "hacienda/tmp1.xls";
   Comodin1 = "";
   |IP_REMOTO Comodin1; |QBF Comodin1;
   |SI Comodin1 ! "";
      Comodin1 = eaUnidadBTmp + "\tmp1.xls";
      |ENVIA_FICHERO Comodin, Comodin1;
   |SINO;
      Comodin1 = eaUnidadBTmp + "\tmp1.xls";
      |COPIA_FICHERO Comodin, Comodin1;
   |FINSI;
|FPRC;
