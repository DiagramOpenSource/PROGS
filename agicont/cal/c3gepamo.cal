|FICHEROS;
    c3gepamo #00;
    cacuenta #05;
    canempre #30;
    camaasil #31;
    camaasic #32;
    camocabe #40;
    camoline #41;
    camogrup #42;
    catmpamo #43;
    caconasi #20;
|FIN;

|VARIABLES;
  &enDiario   = 0;
  &enContador = 0;
  &entrada = 1;
  fecalf   = "";
  fecsys   = @;
  ini      = 0;
  mes      = "";
  mesfec   = 0;
  mesac    = 0;
  a        = 0;
  b        = 0;
  c        = 0;
  modcon   = 0;
  importe  = 0;
  opera    = "";

  alfa1 = "";
  alfa2 = "";
  alfa3 = "";
  alfa4 = "";
  x     = 0;

  dos    = "";
  tres   = "";
  cuatro = "";
  cinco  = "";

  cuenta = "";
  cuenta1= "";
  conter = 0;
  signo  = "";

  dig_dos = 0;
  dig_tre = 0;
  dig_cua = 0;
  dig_cin = 0;
  digito  = 0;
  digito1 = 0;
  digit   = 0;
  impor   = 0;

  cuenta_ini  = "";
  grupo_ini   = 0;
  nivel_ini   = 0;
  sw_tmp      = 0;
  num_lin     = 0;
  lin_grabada = "N";
  nombre      = "";
  sw_paso     = 0;
  tot         = 0;

  empcon = 0;
  ejecon = 0;
  leidos = 0;
  dirfich0 = "";
  &dir_H = "";
  xx = 0;
  yy = 0;
  y = 0;
  &r_emp = 0;
  &r_per = 0;
  sw_error = 0;
  men1 = "     NO Existe Periodo en la empresa Contable";
  men2 = "     El a¤o contable NO esta dentro de la seleccion";
  men9 = "     Empresa Contable INACTIVA ";
  sw = 0;
  pasar = 0;
  concepto = "";
  any = 0;

  aDepar = "";
  nGrupo = 0;
  depar_ini = "";

|FIN;

|INCLUYE inicio_i;  || lectura de la empresa
|INCLUYE actudi_i;  || actualizacion diario
|INCLUYE numamo_i;  || amortizacion
|INCLUYE condes_i;

|PROCESO numamo; |TIPO 0;
    x_alfa1 = #0Campo;
|HAZ x_punto;
    #0Campo = x_alfa1;
    |PINTA #0Campo;
|FPRC;

|PROCESO any; |TIPO 0;
  alfa1 = #0#2;
  alfa1 = alfa1 % -102;
  any = alfa1;
|FPRC;

|PROCESO final816; |TIPO 7;
  |SI eaConDes ! "N";
      |PINTA 0946, #0Campo;
      #0Campo = "";
  |FINSI;
  |PTEC 816;
|FPRC;

|PROCESO fin_anyo; |TIPO 7;
    #0Campo = fec2;
    |PINTA #0Campo;
|FPRC;

|PROCESO limite; |TIPO 0;
|SI #0Campo > fec2;|O #0Campo < fec1;
    |MENAV "   ATENCION!!! ESTA FECHA NO CORRESPONDE AL EJERCICIO ";
    |ERROR;
|FINSI;
|FPRC;

|PROCESO defecto; |TIPO 7;
  fecsys = fec2
  alfa1 = fecsys % -104;
  #0Campo = alfa1; |PINTA #0Campo;
  |HAZ VerCaconimp;
|FPRC;

|PROCESO mayor1; |TIPO 0;
  |SI #0Campo = "N";
      |PARA x = 15;  |SI x [ 54;  |HACIENDO x = x + 1;
            |CAMPO_MODIFICA #0x,1,"S";
      |SIGUE;
      #0#11 = 0;
      #0#12 = 0;
      #0#13 = 0;
      #0#14 = 0;
      |CAMPO_MODIFICA #0#11,1,"N";
      |CAMPO_MODIFICA #0#13,1,"N";
      |CAMPO_MODIFICA #0#12,1,"N";
      |CAMPO_MODIFICA #0#14,1,"N";
      |PINTA #0#11;
      |PINTA #0#13;
      |PINTA #0#12;
      |PINTA #0#14;
  |SINO;
      |PARA x = 15;  |SI x [ 54;  |HACIENDO x = x + 1;
            #0x = 0;
            |CAMPO_MODIFICA #0x,1,"N";
      |SIGUE;
      |CAMPO_MODIFICA #0#11,1,"S";
      |CAMPO_MODIFICA #0#13,1,"S";
      |CAMPO_MODIFICA #0#12,1,"S";
      |CAMPO_MODIFICA #0#14,1,"S";
  |FINSI;
|FPRC;

|PROCESO consulta; |TIPO 0;
  |SI FSalida ! 9;  |ACABA;  |FINSI;
  |SUB_EJECUTA "caconemp.run";
  |ERROR;
  |SI Campo = 11;
      #0#11 = r_emp;
      #0#13 = r_per;
      |PTEC 802;
      |PTEC 802;
  |FINSI;
  |SI Campo = 12;
      #0#12 = r_emp;
      #0#14 = r_per;
      |PTEC 802;
  |FINSI;
  |SI Campo ] 15;
      #0Campo = r_emp;
      y = Campo + 20;
      #0y = r_per;
      |PTEC 802;
      |PTEC 802;
  |FINSI;
|FPRC;

|PROCESO deshas; |TIPO 0;
  |SI #0#12 < #0#11;
      |MENAV "      Error en Introduccion";
      |ERROR;
  |FINSI;
|FPRC;

|PROCESO no_dep; |TIPO 7;
 alfa1 = #30#24;
 |C_M #0#75,1,alfa1;
|FPRC;

|| --------------- comprueba desde-hasta empresas ---------------------------
|PROCESO buscaem1;
  sw_error = 0;
  |SI #30#21 = "I"; |ACABA; |FINSI;
  |SI #30#26 ! any; |ACABA; |FINSI;
  leidos = 1;
  |ERROR10;
|FPRC;

|DEFBUCLE buscaempre;
#30#1;
;
#0#11,#0#13;
#0#12,#0#14;
e;
NULL,buscaem1;
|FIN;

|PROCESO buclempre;
  |SI #0#13 > #0#14;
      |MENAV "     Error en Introduccion";
      |ERROR;
      |ACABA;
  |FINSI;
  |SI #0#11 = 0; |Y #0#12 = 0; |ACABA; |FINSI;
  leidos = 0;
  |BUCLE buscaempre;
  |SI leidos = 0;
      |MENAV "    ATENCION SELECCION VACIA";
      |ERROR;
      |ACABA;
  |FINSI;
|FPRC;

|| ------------------------------------------------------------------------

|PROCESO nomodi;
  |PARA x = Campo + 1;  |SI x [ 34;  |HACIENDO x = x + 1;
        #0x = 0;
        |CAMPO_MODIFICA #0x,1,"N";
        y = x + 20;
        #0y = 0;
        |CAMPO_MODIFICA #0y,1,"N";
  |SIGUE;
  y = Campo + 20;
  #0y = 0;
  |CAMPO_MODIFICA #0y,1,"N";
  |PINTA #0y;
|FPRC;

|PROCESO esbuena; || desde campo de empresas seleccion
  |SI FSalida = 2; |ACABA; |FINSI;
  |SI #0Campo = 0; |HAZ nomodi;  |ACABA;  |FINSI;
  |ABRE #30;
  #30#2 = #0Campo;
  #30#3 = 0;
  |LEE 011#30];
  |SI FSalida ! 0;  |O #30#2 ! #0Campo;
      |MENAV "     NO Existe la empresa Contable";
      |ERROR;
  |FINSI;
  |CIERRA #30;
|FPRC;

|PROCESO esbuena1;
  |SI FSalida = 2;|ACABA;|FINSI;
  x = Campo - 20;
  |SI #0x = 0;  |ACABA;  |FINSI;
  sw_error = 0;
  |ABRE #30;
  x = Campo - 20;
  #30#2 = #0x;
  #30#3 = #0Campo;
  |LEE 011#30=;
  |SI FSalida ! 0;
      sw_error = 1;
      |CIERRA #30;
      |MENAV men1;
      |ERROR;
      |ACABA;
  |FINSI;
  |CIERRA #30;
  |SI #30#21 = "I"; |MENAV men9; |ERROR; |ACABA; |FINSI;
  |SI #30#26 ! any; sw_error = 2; |MENAV men2; |ERROR; |ACABA; |FINSI;
|FPRC;

|| **********************************************************************
||                       CALCULOS DEL PROCESO
|| **********************************************************************

|| --- Seleccion DESDE / HASTA Empresa.

|PROCESO esdesd1;
  sw_error = 0;
  |SI #30#26 ! any; |ACABA; |FINSI;
  |SI #30#21 = "I"; |ACABA; |FINSI;

  |DBASE dir_fich;
  dirfich0 = dir_fich + "fich/";
  alfa1 = #30#2;
  alfa2 = #30#3;
  alfa1 = "00000" + alfa1;   alfa1 = alfa1 % -105;
  alfa2 = "0"   + alfa2;   alfa2 = alfa2 % -101;
  dirfich0 = dirfich0 + alfa1 + alfa2 + "/";
  |HAZ haztodo;
|FPRC;

|DEFBUCLE esdesde;
 #30#1;
 ;
 #0#11, #0#13;
 #0#12, #0#14;
 e;
 NULL,esdesd1;
|FIN;

|| --- Seleccion en parrilla.

|PROCESO esunaauna;
  |PARA xx = 15;  |SI xx [ 34;  |HACIENDO xx = xx + 1;
        |SI #0xx = 0;  |SAL;  |FINSI;

        |DBASE dir_fich;
        dirfich0 = dir_fich + "fich/";
        yy = xx + 20;
        alfa1 = #0xx;
        alfa2 = #0yy;
        alfa1 = "00000" + alfa1;   alfa1 = alfa1 % -105;
        alfa2 = "0"   + alfa2;   alfa2 = alfa2 % -101;
        dirfich0 = dirfich0 + alfa1 + alfa2 + "/";

        |ABRE #30;
        #30#2 = #0xx;
        #30#3 = #0yy;
        |LEE 011#30=;
        |SI FSalida = 0;
            |SI #30#26 = any; |Y #30#21 ! "I";
                |HAZ haztodo;
            |FINSI;
        |FINSI;
        |CIERRA #30;
  |SIGUE;
|FPRC;

|| *************************************************************************

|PROCESO act_asiento; || crea asiento contable por linea
    |ABRE #31;
    |ABRE #32;

    enDiario = #0#3;
    |HAZ contador;

    |PARA x = 1; |SI x [ 11; |HACIENDO x = x + 10;
        |SI x = 1;
            conter = cuenta;
             digit = digito;
             signo = "D";
        |SINO;
            conter = cuenta1;
            digit = digito1;
            signo = "H";
        |FINSI;
        |DDEFECTO #31;
          #31#0 = #0#3;      || diario
          #31#1 = #0#4;      || fecha asiento
          #31#2 = enContador;|| nro. de asiento
          #31#3 = x;         || lineas de apuntes
          #31#4 = conter;    || cuenta
          #31#5 = digit;     || digito
          #31#6 = #0#5;      || concepto contable
          #31#7 = #0#6;      || descripcion
          enNumCon = #31#6; |HAZ VerConcepto; #31#24 = eaDesCon;
          #31#8 = signo;     || signo
          #31#9 = impor;     || importe
          |SI #31#8 = "D";
              #31#16 = #31#4;         || carga en el debe
              #31#17 = #31#5;
              #31#10 = "";
              #31#11 = 0;
          |SINO;
              #31#16 = "";            || carga en el haber
              #31#17 = 0;
              #31#10 = #31#4;
              #31#11 = #31#5;
          |FINSI;
          #31#21 = aDepar;
          |GRABA 020#31n;
          |SI FSalida = 0;
             |DDEFECTO #32;
                #32#0 = #31#0;  || diario
                #32#1 = #31#1;  || fecha
                #32#2 = #31#2;  || documento
            |LEE 101#32=;
            |SI FSalida ! 0;
                #32#3 = #31#2;  || asiento
                |GRABA 020#32n;
                |LIBERA #32;
                |LEE 121#32=;
            |FINSI;
            |HAZ actualiz1;
                opera = "A";
            |HAZ altatra;
                d_diari = #31#0;   || diario
                d_opera = #31#8;   || signo
                d_impor = #31#9;   || importe
            |HAZ actu_diari;       || actualiza el diario
            |LIBERA #32;
        |FINSI;
    |SIGUE;

    |CIERRA #32;
    |CIERRA #31;
|FPRC;

|PROCESO graba_cuen;
     nGrupo = #40#15; |SI #0#7  = "N"; nGrupo = 0;  |FINSI;
     aDepar = #40#28; |SI #0#75 = "N"; aDepar = ""; |FINSI;

     |DDEFECTO #43;
     #43#6 = aDepar;
     #43#0 = nGrupo;
     #43#1 = cuenta;
     #43#2 = digito;
     |LEE 101#43=;
     |SI FSalida ! 0;
         |DDEFECTO #43;
         #43#6 = aDepar;
         #43#0 = nGrupo;
         #43#1 = cuenta;
         #43#2 = digito;
         |GRABA 021#43b;
     |FINSI;
     #43#3 = #43#3 + #41#9; || ............... cantidad al DEBE
     |GRABA 021#43a;
     |LIBERA #43;

     |DDEFECTO #43;
     #43#6 = aDepar;
     #43#0 = nGrupo;
     #43#1 = cuenta1;
     #43#2 = digito1;
     |LEE 101#43=;
     |SI FSalida ! 0;
         |DDEFECTO #43;
         #43#6 = aDepar;
         #43#0 = nGrupo;
         #43#1 = cuenta1;
         #43#2 = digito1;
         |GRABA 021#43b;
     |FINSI;

     #43#4 = #43#4 + #41#9; || ................ cantidad al HABER
     |GRABA 021#43a;
     |LIBERA #43;
|FPRC;

|| ***** lectura lineas amortiz.

|PROCESO lineas2;              || acum. imp para asiento y act. contabilizado

    |SI #41#4 ! "A";|ACABA; |FINSI;
    |SI #41#9 =  0; |ACABA; |FINSI;

    cuenta  = dos;
    digito  = dig_dos;
    cuenta1 = tres;
    digito1 = dig_tre;

    sw_paso = 1;
    impor = #41#9;
    tot = tot + impor;
    |SI #0#7 = "S"; |O #0#75 = "S"; |HAZ graba_cuen; |FINSI;

    #41#6 = "S";
    |GRABA 020#41a;
    |LIBERA #41;
|FPRC;

|DEFBUCLE capasamo1; || lee lineas de amortizaciones clave 2
 #41#2;
 ;
 #40#0,#40#1,   1, #0#2, "N";
 #40#0,#40#1, 999, #0#2, "N";
 be;
 NULL, lineas2;
|FIN;


|| ***** lectura cabeceras amortiz.
|PROCESO lineas; || lee grupo de amortizaciones

    aDepar = #40#28;

    |ABRE #42;
    #42#0 = #40#15;
    |LEE 001#42=;
    |SI FSalida = 0;
        dos     = #42#1;  || cta. amortiz
        dig_dos = #42#2;  || digito
        tres    = #42#3;  || cta. amortiz acum
        dig_tre = #42#4;  || digito
        cuatro  = #42#5;  || cta. amortiz. regulariz.
        dig_cua = #42#6;  || digito
        cinco   = #42#7;  || cta. amortiz. regulariz. acum
        dig_cin = #42#8;  || digito

        |ATRI R;
        |PINTA 2267, #40#0; |PINTA 2276, #40#1;
        |ATRI 0;

        tot = 0;
        sw_paso = 0;
        |BUCLE capasamo1;
        |SI #0#7 = "N"; |Y #0#75 = "N";
            |SI sw_paso = 1;
                impor = tot;
                |HAZ act_asiento;
            |FINSI;
         |FINSI;
     |FINSI;
     |CIERRA #42;
|FPRC;


|DEFBUCLE capasamo0; || lee cabeceras de amortizaciones
  #40#1;
  ;
  #0#0,#0#8;
  #0#1,#0#9;
  e;
  NULL, lineas;
|FIN;

|| ////////////////////////////////////////////////////////////////////////
|PROCESO BuscaCap;
          |LIBERA #32;
          FSalida = 0;
          |PARA ; |SI FSalida = 0; |HACIENDO;
               enDiario = #0#3;
               |HAZ contador;
               #32#0 = #0#3;
               #32#1 = #0#4;
               #32#2 = enContador;
               #32#3 = #32#2;
               |LEE 001#32=;
          |SIGUE;
          |GRABA 021#32b;
           num_lin = -9;
|FPRC;


|PROCESO grabalin;

     lin_grabada = "S";
     |SI sw_tmp = 0;
          |HAZ BuscaCap;
          depar_ini  = #43#6;
          grupo_ini  = #43#0;
          cuenta_ini = #43#1;
          nivel_ini  = #43#2;
          sw_tmp = 1;
     |FINSI;

     |SI #43#0 ! grupo_ini; |O #43#6 ! depar_ini;
          |HAZ BuscaCap;
     |FINSI;

     |DDEFECTO #31;
     #31#0 = #0#3;       ||Diario
     #31#1 = #0#4;       ||Fecha
     #31#2 = enContador;      ||Cuenta
     #31#6 = #0#5;       ||Concepto
     #31#7 = #0#6;       ||Descripcion del concepto
     enNumCon = #31#6; |HAZ VerConcepto; #31#24 = eaDesCon;
     conter = #43#1;
     digit  = #43#2;
     |SI #43#3 ! 0;      ||Cuenta de debe
          num_lin = num_lin + 10;
          #31#3 = num_lin;    ||linea
          #31#8 = "D";
          #31#4 = #43#1;      ||Cuenta
          #31#5 = #43#2;      ||nivel
          #31#9 = #43#3;
          #31#16 = #43#1;
          #31#17 = #43#2;
          #31#10 = "";
          #31#11 = 0;
          #31#21 = #43#6; |SI #31#21 = "  "; #31#21 = "01"; |FINSI;
          signo = "D";
          |HAZ actualiz1;
          opera = "A";
          |HAZ altatra;
          d_diari = #31#0;   || diario
          d_opera = #31#8;   || signo
          d_impor = #31#9;   || importe
          |HAZ actu_diari;       || actualiza el diario
          |GRABA 021#31c;
     |FINSI;

     |SI #43#4 ! 0;
          num_lin = num_lin + 10;
          #31#3 = num_lin;    ||linea
          #31#8 = "H";
          #31#9 = #43#4;
          #31#4 = #43#1;      ||Cuenta
          #31#5 = #43#2;      ||nivel
          #31#10 = #43#1;
          #31#11 = #43#2;
          #31#16 = "";
          #31#17 = 0;
          #31#21 = #43#6; |SI #31#21 = "  "; #31#21 = "01"; |FINSI;
          signo = "H";
          |HAZ actualiz1;
          opera = "A";
          |HAZ altatra;
          d_diari = #31#0;   || diario
          d_opera = #31#8;   || signo
          d_impor = #31#9;   || importe
          |HAZ actu_diari;       || actualiza el diario
          |GRABA 021#31c;
     |FINSI;
     |LIBERA #31;

     depar_ini  = #43#6;
     grupo_ini  = #43#0;
     cuenta_ini = #43#1;
     nivel_ini  = #43#2;
|FPRC;

|DEFBUCLE recup_tmp; || para grabar asiento por grupos
  #43#1;
  ;
  "  ",    0, "            " , 0 , " ";
  "zz", 9999, "zzzzzzzzzzzz" , 9 , "z";
  e;
  NULL , grabalin;
|FIN;
|| *********************************************************************
||                              PROGRAMA
|| *********************************************************************

|PROCESO tipo3; |TIPO 3;
  sw = 0;
  #0#0 = "        ";
  #0#1 = "zzzzzzzz";
  #0#8 = 00;
  #0#9 = 99;
  alfa1 = #0#2;
  alfa1 = alfa1 % -102;
  any = alfa1;

  nombre = ("con" + Usuario % 108);
  |NOME_DAT #43 nombre;
  |ABRE #43; |CIERRA #43; |DELINDEX #43;

  |PUSHV 2102 2378;
  |ATRI I;
  |CUADRO 2102 2378
  |ATRI N;
  |ATRI R;
  |PINTA 2203," EMPRESA:                                                                  ";
  |ATRI 0;

  |SI #0#10 = "S";
      |BUCLE esdesde;
  |SINO;
      |HAZ esunaauna;
  |FINSI;
|FPRC;

|| ************************************************************************
|| ******************* ASIENTOS CONTABLES AMORTIZACIONES ******************
|| ************************************************************************

|PROCESO haztodo;
  enEmpresa = #canempre#2;
  enPeriodo = #canempre#3;
  swOperacion = 0
  |SUB_EJECUTA "camoneda";

  dir_H = dirfich0;
      dig1 = #30#11;
      dig3 = #30#13;
      dig4 = #30#14;
      dig5 = #30#15;
      dig6 = #30#16;
      dig7 = #30#17;
      dig8 = #30#18;
      dig9 = #30#19;

  |PATH_DAT #5   dirfich0;
  |PATH_DAT #8   dirfich0;
  |PATH_DAT #10  dirfich0;
  |PATH_DAT #20  dirfich0;
  |PATH_DAT #909 dirfich0;
  |PATH_DAT #31  dirfich0;
  |PATH_DAT #32  dirfich0;
  |PATH_DAT #40  dirfich0;
  |PATH_DAT #41  dirfich0;
  |PATH_DAT #42  dirfich0;

  empcon = #30#1;
  ejecon = #30#26;

  |ATRI R;
  |PINTA 2213, #30#2;
  |PINTA 2219, #30#3;
  |PINTA 2221, #30#1;
  |ATRI 0;

  |ABRE #43;
  |BUCLE capasamo0;
  |CIERRA #43;

  |SI #0#7 = "S"; |O #0#75 = "S";
      |ABRE #31;
      |ABRE #32;
      |BUCLE recup_tmp;
      |CIERRA #32;
      |CIERRA #31;
  |FINSI;
  |DELINDEX #43;

  |SI modcon = 0;
      |ABRE #8;
      |DDEFECTO #8;
      #8#0 = codcon;
      |LEE 001#8=;
      |SI FSalida = 0;
          |BORRA 020#8a;
      |FINSI;
      |CIERRA #8;
  |SINO;
      |SUB_EJECUTA "camaapua";
  |FINSI;
|FPRC;

**********************************************************************
    ACTUALIZACION DE CUENTAS
**********************************************************************

|PROCESO actualiz1;
|ABRE #5;
    #5#0 = conter;
    #5#1 = digit;
|LEE 101#5=;
|SI #31#0 = 0;
    |SI #31#8 = "D";
            #5#9 = #5#9 + #31#9;
            #5#51 = #5#51 + #31#9;
    |SINO;
            #5#10 = #5#10 + #31#9;
            #5#52 = #5#52 + #31#9;
    |FINSI;
        #5#11 = #5#9 - #5#10;
        #5#53 = #5#51 - #5#52;
|SINO;
    |SI #31#0 = 99;
        |SI #31#8 = "D";
                #5#48 = #5#48 + #31#9;
                #5#51 = #5#51 + #31#9;
        |SINO;
                #5#49 = #5#49 + #31#9;
                #5#52 = #5#52 + #31#9;
        |FINSI;
            #5#50 = #5#48 - #5#49;
            #5#53 = #5#51 - #5#52;
    |SINO;
        |HAZ actualiz2;
    |FINSI;
|FINSI;
|SI #5#58 < #31#1;
        #5#58 = #31#1;
|FINSI;
|GRABA 020#5a;
|LIBERA #5;
|CIERRA #5;
|HAZ actualiz3;
|FPRC;

|PROCESO actualiz2;
    ini = dig1;      || numero de niveles
    fecalf = #31#1;
    mes = fecalf % 402;
    mesfec = mes;
|SI ini = 1;
        mesac = mesfec;
|SINO;
        mesac = (mesfec - ini) + 1;
    |SI mesac [ 0;
            mesac = (13 - ini) + mesfec;
    |FINSI;
|FINSI;
    a = (mesac * 3) + 9;
    b = a + 1;
    c = a + 2;
|SI #31#8 = "D";
        #5a = #5a + #31#9;
        #5#51 = #5#51 + #31#9;
|SINO;
        #5b = #5b + #31#9;
        #5#52 = #5#52 + #31#9;
|FINSI;
    #5c = #5a - #5b;
    #5#53 = #5#51 - #5#52;
|FPRC;

|PROCESO actualiz3;
|SI #31#8 = "D";
        #32#4 = #32#4 + #31#9;
    |SI #32#09 = 0; #32#08 = #31#4; #32#09 = #31#5; |FINSI; || contrp.HABER
|FINSI;
|SI #31#8 = "H";
        #32#5 = #32#5 + #31#9;
    |SI #32#11 = 0; #32#10 = #31#4; #32#11 = #31#5; |FINSI; || contrp.DEBE
|FINSI;
|GRABA 020#32a;
|FPRC;

**********************************************************************
    ACTUALIZACION DE 'camaasic' (PARA LA ACTUALIZACION DE NIVELES)
**********************************************************************

|PROCESO altatra;
    modcon = 1;
|ABRE #8;
    #8#0 = codcon;
    #8#1 = #31#4;
    #8#2 = #31#5;
    #8#3 = #31#1;
    #8#4 = #31#8;
    #8#6 = #31#0;
|LEE 101#8=;
|SI FSalida ! 0;
    |DDEFECTO #8;
        #8#0 = codcon;
        #8#1 = #31#4;
        #8#2 = #31#5;
        #8#3 = #31#1;
        #8#4 = #31#8;
        #8#6 = #31#0;
    |GRABA 101#8b;
|FINSI;
    importe = #31#9;  || aqui si que se usa el importe real
|SI opera = "B";
        importe = -importe;
|FINSI;
    #8#5 = #8#5 + importe;
|GRABA 101#8a;
|LIBERA #8;
|CIERRA #8;
|FPRC;
