|FICHEROS;
    caratios #0;
    caformul #1;
    cacuenta #4;
|FIN;

|CAMPOS;
   #0#0  ratio;
   #0#2  formula;
   #0#15 periodo;
   #0#16 fecha;
|FIN;

|VARIABLES;

titulo = "Ratios";

{-1}posi = 0;
posi1  = 1211;
posi2  = 1411;
posi3  = 1611;
posi4  = 1811;
posi5  = 2011;
posi6  = 2211;
posi7  = 1247;
posi8  = 1447;
posi9  = 1647;
posi10 = 1847;
posi11 = 2047;
posi12 = 2247;

pregun1 = "2400NDesea calcular ahora el Ratio ? ";
pregun2 = "2400NDesea imprimir el Ratio ? ";

informe = "caratios";

ctcuen = "ctcuen";

mensaje1="PROGRAMADOR:No alteres el orden de estos mensajes ni intercales nada";
mensaje01="0000*ATENCION !!!! La formula es demasiado grande ! ";
mensaje02="0000*ATENCION !!!! La formula contiene algun parentesis ) de mas !";
mensaje03="0000*ATENCION !!!! La formula contiene algun parentesis ( de mas !";
mensaje04="0000*ATENCION !!!! Formula Incoherente !!";

mensaje2 = "0401**** Acumulando Cuenta :";
mensaje3 = "2400**** CALCULANDO ****";
cperi = "period";
mensaje4    = "0000**  ATENCION !!!! Fichero de Ejercicios Inexistente **";
   error = 0;
{300}simple = "";
{-1}sim = 0;
   nada = "";
   trabajo = "";
   alfa1 = "";
   alfa2 = "";
   alfa3 = "";
   alfa4 = "";
   formu = "";
   M = "*";
   D = "/";
   S = "+";
   R = "-";
   dolar = "$";
   punte = "&";

   i = 0;
   n = 0;
   j = 0;
   modo = 0;
   poper = 0;
   nivel = 0;
   buceo = 0;
   paren = 0;
   sfinal = 0;

   alt = 0;
   aln = 0;
   al1 = "";
   al2 = "";
   all = 0;
{-1}alp = 0;
   total1 = 0;
   total2 = 0;
   total3 = 0;
   total4 = 0;
   total5 = 0;
   total6 = 0;
   total7 = 0;
   total8 = 0;
   total9 = 0;
   total10= 0;
   total11= 0;
   total12= 0;
   espacios = "                ";
   anadido = "+$0";
   pare1 = "(";
   pare2 = ")";
|FIN;

|INCLUYE acceso_i;

|PROCESO tipo3; |TIPO 30;
  |CONFI pregun1;
  |SI FSalida ! 0;  |ACABA;  |FINSI;
  |HAZ calcula;
  |SI error = 0;
      |CONFI pregun2;
      |SI FSalida ! 0;  |ACABA;  |FINSI;
      |IMPRE 0;
      |SI FSalida ! 0;  |ACABA;  |FINSI;
      |INFOR informe;
      |SI FSalida ! 0;  |FINIMP; |ACABA;  |FINSI;
      |PRINT;
      |PIEINF;
      |FININF;
      |FINIMP;
  |SINO;
      Isim = mensaje1 <-;
      Isim = Isim + error;
      mensaje1 = sim;
      |MENAV mensaje1;
  |FINSI;
|FPRC;


|PROCESO calcula;|TIPO 0;

|BLIN 24;
|ATRI P;
|MENSA mensaje3;
|ATRI 0;

error  = 0;
|| Borrar Importes de la ficha;
|PARA i = 3;|SI i < 15;|HACIENDO i = i + 1;
   #0i = 0;
|SIGUE;

|| Primer paso: componer las formula definitiva leyendo fichero de formulas
|ABRE #1;
formu = formula;
#1#0 = formu;
|LEE 020#1=;
|SI FSalida = 0;
    trabajo = #1#2;
    |PARA buceo = 0;|SI buceo = 0;|Y error = 0;|HACIENDO;
         |QBT trabajo;
         buceo = 1;
         alfa1 = trabajo % 0;n = alfa1 * 100;n = n + 1;
         |PARA i = 101;|SI i [ n;|HACIENDO i = i + 100;
             alfa1 = trabajo % i;
             |SI alfa1 = AR;
                 i = i + 103;
                 formu = trabajo % i;
                 #1#0 = formu;
                 |LEE 020#1=;
                 formu = nada;
                 |SI FSalida = 0;
                    formu = pare1 + #1#2;
                    formu = formu + anadido + pare2;
                    |QBT formu;
                 |FINSI;
                 i = i / 100;i = i ! 0;i = i - 2;
                 alfa1 = nada;
                 aln = 100;
                 alt = i;
                 |PARA;|SI alt > 99;|HACIENDO alt = alt - 80;
                     all = aln + 80;
                     al1 = trabajo % all;
                     alfa1 = alfa1 + al1;
                     aln = aln + 8000;
                 |SIGUE;
                 all = aln + alt;
                 al1 = trabajo % all;
                 alfa1 = alfa1 + al1;
                 alfa1 = alfa1 + formu;
                 i = i + 6;
                 al1 = trabajo % 0;all = al1 - i;all = all + 1;
                 al1 = nada;
                 i = i * 100;
                 |PARA;|SI all > 99;|HACIENDO all = all - 80;
                    aln = i + 80;
                    al2 = trabajo % aln;
                    al1 = al1 + al2;
                    i = i + 8000;
                 |SIGUE;
                 i = i + all;
                 al2 = trabajo % i;
                 trabajo = al1 + al2;
                 trabajo = alfa1 + trabajo;
                 alfa1 = trabajo % 0;
                 |SI alfa1 > 240;
                        ||***** Error 1 exceso
                        error = 1;
                 |FINSI;
                 buceo = 0; || Continuar buscando formulas relacionadas
             |SINO;
         |SIGUE;
             |FINSI;
    |SIGUE;
|FINSI;
|CIERRA #1;

al1 = trabajo % 0;
|SI al1 > 0; || Si hay formula se calcula

  trabajo = trabajo + anadido; || Esto es por si acaso no hay operaciones

  || Segundo paso: desglosar en orden de ejecucion en subformulas los parentesis

  Isimple = simple1 <-; || A partir del 1 desglose de parentesis
  |PARA paren = 0;|SI paren = 0;|Y error = 0;|HACIENDO;
      |HAZ parentes;
  |SIGUE;
  simple = trabajo; || Formula resultante
  Isimple = Isimple + 1;
  sfinal = Isimple

  || Tercer paso : resolver las formulas sencillas
  |PARA Isimple=simple1<-;|SI Isimple < sfinal;|Y error = 0;
                                          |HACIENDO Isimple=Isimple + 1;
    Isim = simple200<-; || Pila de resultados intermedios
    trabajo = simple;
    |HAZ formula; || Resuelve una formula sencilla
    simple = trabajo;
  |SIGUE;

  |SI error = 0;
     alfa1 = nada;
     Isimple = sfinal - 1;
     trabajo = simple;
     || Poner resultado en la ficha
     j = 115;
     |PARA i = 3;|SI i < 15;|HACIENDO i = i + 1;
        alfa1 = trabajo % j;
        #0i = alfa1;
        j = j + 1500;
     |SIGUE;
     |PINDA #0#0;
  |FINSI;

|FINSI;

|BLIN 24;
|FPRC;


|PROCESO parentes;

alfa1 = trabajo % 0;n = alfa1;n = n * 100;n = n + 1;
nivel = 0;
paren = 1;
|PARA i = 101;|SI i [ n;|Y error = 0;|HACIENDO i = i + 100;
   alfa1 = trabajo % i;
   |SI alfa1 = pare1;
      nivel = i + 100;
   |FINSI;
   |SI alfa1 = pare2;
      |SI nivel = 0;
                     ||***** Error 2 parentesis )
          error = 2;
      |SINO;
        || Sustituir
        i = i - nivel;i = i / 100;i = i ! 0;i = i - 1;
        nivel = nivel + i;
        simple = trabajo % nivel;
        i = nivel / 100;i = i ! 0;i = i - 2;
        alfa1 = nada;
        aln = 100;
        |PARA;|SI i > 99;|HACIENDO i = i - 80;
              all = aln + 80;
              al1 = trabajo % all;
              alfa1 = alfa1 + al1;
              aln = aln + 8000;
        |SIGUE;
        all = aln + i;
        al1 = trabajo % all;
        alfa1 = alfa1 + al1;
        i = nivel / 100;i = i ! 0;nivel = nivel $ 100;
        i = i + nivel;i = i + 1;
        al1 = trabajo % 0;all = al1 - i;all = all + 1;
        i = i * 100;
        al1 = nada;
        |PARA;|SI all > 99;|HACIENDO all = all - 80;
               aln = i + 80;
               al2 = trabajo % aln;
               al1 = al1 + al2;
               i = i + 8000;
        |SIGUE;
        i = i + all;
        al2 = trabajo % i;
        trabajo = al1 + al2;
        alfa1 = alfa1 + punte;
        alfa2 = A0000 + Isimple;
        alfa2 = alfa2 % -104;
        alfa1 = alfa1 + alfa2;
        trabajo = alfa1 + trabajo;
        Isimple = Isimple + 1;
        paren = 0;
      |FINSI;
   |SINO;
|SIGUE;
      |SI nivel ! 0;
          || Error 3 parantesis (
          error = 3;
      |FINSI;
   |FINSI;

|FPRC;


|PROCESO formula;

   || Primero sustituir las multiplicaciones y divisiones buceo = 0
   || luego sumas y restas buceo = 1

|PARA buceo = 0;|SI buceo < 2;|Y error = 0;|HACIENDO buceo = buceo + 1;

   alfa1 = trabajo % 0;
   n = alfa1 * 100;n = n + 1;
   nivel = 0;
   poper = 0;
   modo = 0;
   |PARA i = 101;|SI i [ n;|HACIENDO i = i + 100;
        alfa1 = trabajo % i;
        |SI alfa1 = M;|O alfa1 = D;|O alfa1 = S;|O alfa1 = R;|O i = n;
            |SI modo = 0;
                |SI alfa1 = M;|O alfa1 = D;|O buceo = 1;
                   alfa2 = alfa1;
                   poper = i / 100;poper = poper ! 0;
                   modo = 1;
                |SINO;
                   nivel = i / 100;nivel = nivel ! 0;
                |FINSI;
            |SINO;
                |SI i = n;
                   i = i + 100;
                |FINSI;
                nivel = nivel + 1;
                j = poper - nivel;
                |SI j [ 0;
                   ||**** Error 4 operando erroneo
                   error = 4;
                |FINSI;
                nivel = nivel * 100;
                nivel = nivel + j;
                formu = trabajo % nivel;
                |HAZ valor; || Obtenido valor 1
                alfa3 = formu;
                poper = poper + 1;
                j = i / 100;j = j ! 0;
                j = j - poper;
                |SI j [ 0;
                   ||**** Error 4 operando erroneo
                   error = 4;
                |FINSI;
                poper = poper * 100;
                poper = poper + j;
                formu = trabajo % poper;
                |HAZ valor; || Obtenido valor 2
                alfa4 = formu;
                |SI alfa2 = S; || En formu vuelve el resultado
                   |HAZ suma;
                |FINSI;
                |SI alfa2 = R;
                   |HAZ resta;
                |FINSI;
                |SI alfa2 = M;
                   |HAZ multi;
                |FINSI;
                |SI alfa2 = D;
                   |HAZ divi;
                |FINSI;
                || Sustituir operacion por su resultado en la pila
                j = nivel $ 100; || longitud op 1
                poper = poper $ 100; || longitud op 2
                poper = j + poper;poper = poper + 1; || + 1 de la operacion
                poper = 5 - poper;
                poper = poper * 100; || poper es ahora la diferecia

                j = nivel / 100;j = j ! 0; || total hasta el cambio
                j = j - 1;
                alfa2 = nada;
                aln = 100;
                |PARA;|SI j > 99;|HACIENDO j = j - 80;
                     all = aln + 80;
                     al1 = trabajo % all;
                     alfa2 = alfa2 + al1;
                     aln = aln + 8000;
                |SIGUE;
                all = aln + j;
                al1 = trabajo % all;
                alfa2 = alfa2 + al1;
                j = i / 100;j = j ! 0;
                al1 = trabajo % 0;all = al1 - j;all = all + 1;
                j = j * 100;
                al1 = nada;
                |PARA;|SI all > 99;|HACIENDO all = all - 80;
                     aln = j + 80;
                     al2 = trabajo % aln;
                     al1 = al1 + al2;
                     j = j + 8000;
                |SIGUE;
                j = j + all;
                al2 = trabajo % j;
                trabajo = al1 + al2;

                alfa2 = alfa2 + punte;
                alfa4 = A0000 + Isim;
                alfa4 = alfa4 % -104;
                alfa2 = alfa2 + alfa4;

                trabajo = alfa2 + trabajo;
                n = n + poper; || nuevo total string
                i = i + poper; || Reposiciono puntero al string

                sim = formu; || guardar en pila
                Isim = Isim + 1; || Incrementar pila
                nivel = i / 100;nivel = nivel ! 0;
                modo = 0;
                |SI alfa1 = M;|O alfa1 = D;|O buceo = 1;
                   poper = nivel;
                   nivel = nivel - 6;
                   alfa2 = alfa1;
                   modo = 1;
                |FINSI;
            |FINSI;
        |FINSI;
   |SIGUE;

|SIGUE;

|| Tiene que haber quedado en trabajo un puntero a la pila
alfa1 = trabajo % 204;
Isim = alfa1;
trabajo = sim; || sustituirlo por el resultado

|FPRC;


|PROCESO valor;
|| entra formu , y sale formu con el valor en formato 15 * 12

|| Tres tipos :
|| 1 = $numero : se pone el numero en formato 15 * 12
|| 2 = numero : siendo numero una cuenta se hace su acumulado
|| 3 = &puntero : se sustituye el valor
|| Si todo va bien el string debe venir `limpio`
al1 = nada;
al1 = formu % 101;
|SI al1 = punte; || caso puntero
   al1 = formu % 0;all = al1;all = all - 1;all = all + 200;
   al1 = formu % all;
   Ialp = al1;
   formu = alp; || Todos los `apuntados` estan en 15 * 12
|SINO;
   |SI al1 = dolar; || caso numero directo
      al1 = formu % 0;all = al1;all = all - 1;all = all + 200;
      al1 = formu % all;
      al1 = al1 + espacios;
      al1 = al1 % 115;
      formu = nada;
      |PARA aln = 0;|SI aln < 12;|HACIENDO aln = aln + 1;
          formu = formu + al1;
      |SIGUE;
   |SINO;  || caso cuenta
      al1 = formu % 0;aln = al1;aln = aln + 100;
      total1 = 0;
      total2 = 0;
      total3 = 0;
      total4 = 0;
      total5 = 0;
      total6 = 0;
      total7 = 0;
      total8 = 0;
      total9 = 0;
      total10= 0;
      total11= 0;
      total12= 0;
      |BLIN 4;
      |MENSA mensaje2;
      |ABRE #4;
      #4#0 = formu;
      #4#1 = 1;
      |LEE 000#4];
      al1 = #4#0 % aln;
      |PARA;|SI FSalida = 0;|Y al1 = formu;|HACIENDO;
         |PINTA 426,#4#0;
         |SI #4#3 = "S";
              |PARA alt = 11;|SI alt < 50;|HACIENDO alt = alt + 3;
                  |SI alt < 17;total1 = total1 + #4alt;|FINSI;
                  |SI alt < 20;total2 = total2 + #4alt;|FINSI;
                  |SI alt < 23;total3 = total3 + #4alt;|FINSI;
                  |SI alt < 26;total4 = total4 + #4alt;|FINSI;
                  |SI alt < 29;total5 = total5 + #4alt;|FINSI;
                  |SI alt < 32;total6 = total6 + #4alt;|FINSI;
                  |SI alt < 35;total7 = total7 + #4alt;|FINSI;
                  |SI alt < 38;total8 = total8 + #4alt;|FINSI;
                  |SI alt < 41;total9 = total9 + #4alt;|FINSI;
                  |SI alt < 44;total10 = total10 + #4alt;|FINSI;
                  |SI alt < 47;total11 = total11 + #4alt;|FINSI;
                  total12 = total12 + #4alt;
              |SIGUE;
         |FINSI;
         |LEE 000#4s;
         al1 = #4#0 % aln;
      |SIGUE;
      |CIERRA #4;
      |BLIN 4;
      Ialp = total1 <-;
      formu = nada;
      |PARA alt = 0;|SI alt < 12;|HACIENDO alt = alt + 1;
         al1 = alp + espacios;
         al1 = al1 % 115;
         formu = formu + al1;
         Ialp = Ialp + 1;
      |SIGUE;
   |FINSI;
|FINSI;

|FPRC;

|| OPeraciones sencillas : entran alfa3 y alfa4 como operandos, sale formu
||  como resultado (todo va en 15 * 12)

|PROCESO suma;
formu = nada;
|PARA aln = 115;|SI aln < 18115;|HACIENDO aln = aln + 1500;
   al1 = alfa3 % aln;
   al2 = alfa4 % aln;

   alt = al1 + al2;

   al1 = alt + espacios;
   al1 = al1 % 115;
   formu = formu + al1;
|SIGUE;
|FPRC;

|PROCESO resta;
formu = nada;
|PARA aln = 115;|SI aln < 18115;|HACIENDO aln = aln + 1500;
   al1 = alfa3 % aln;
   al2 = alfa4 % aln;

   alt = al1 - al2;

   al1 = alt + espacios;
   al1 = al1 % 115;
   formu = formu + al1;
|SIGUE;
|FPRC;

|PROCESO multi;
formu = nada;
|PARA aln = 115;|SI aln < 18115;|HACIENDO aln = aln + 1500;
   al1 = alfa3 % aln;
   al2 = alfa4 % aln;

   alt = al1 * al2;

   al1 = alt + espacios;
   al1 = al1 % 115;
   formu = formu + al1;
|SIGUE;
|FPRC;


|PROCESO divi;
formu = nada;
|PARA aln = 115;|SI aln < 18115;|HACIENDO aln = aln + 1500;
   al1 = alfa3 % aln;
   al2 = alfa4 % aln;

   alt = al1 / al2;

   al1 = alt + espacios;
   al1 = al1 % 115;
   formu = formu + al1;
|SIGUE;
|FPRC;
