|FICHEROS;
    careo300 #0;    || def entrada
    eosactiv #1;    || actividades empresas 'REO'
    eosm1300 #2;    || modelo 300 'REO'
    eosclien #3;    || empresas 'REO'
    caivarep #4;    || iva repercutido
    caivasop #5;    || iva soportado
    eosaplic #6;    || control iva 'REO'
    cacon300 #7;    || conceptos IVA a 300
    caportad #8;    || Datos Portada Modelos
    eosopesp #9;    || Operaciones especificas 'REO'
    agicontr #23;   || control de aplicaciones
|FIN;

|VARIABLES;
    alfa1 = "";
    alfa2 = "";
    alfa3 = "";
    alfa4 = "";
    nume1 = 0;
    nume2 = 0;
    nume3 = 0;
    nume4 = 0;
    fech1 = @;
    fech2 = @;
    fech3 = @;
    fech4 = @;
    FEstado = 0;
    FEstad1 = 0;
    dir_reo = "";
    per_ant = -1;
    x1 = 0;
    x2 = 0;
    x3 = 0;
    x4 = 0;
    x5 = 0;
    t_ivasop = 0;
    total = 0;
    tot_iva = 0;
    tot_bas = 0;
      sw_exi = 0;
      &a_emp = 0;
      &a_per = 0;
      &ac_pe = 0;
      dirfich0 = "";
      &compens = 0;
      perio = 0;
      activi = "";
      any = 0;
      sw_per = 0;
      pto91 = 0;
  agi  = "";
  agi1 = "";
  agi2 = "";
  num = 0;
  aste = "";
  longi = "";
|FIN;

|INCLUYE acceso_i;

***************************************************************************
               CALCULOS DESDE CAMPOS
***************************************************************************

|PROCESO path_reo; |TIPO 8;
  |HAZ inicio;
  |HAZ tipo_8;
  |SI aste = "*";
      |MENAV "    ATENCION!!! R.E.O no instalado ...";
      |PTEC 807;
      |ACABA;
  |FINSI;
  dir_reo = agi1;
  |PATH_DAT #3 dir_reo;
  |PATH_DAT #1 dir_reo;
  |PATH_DAT #2 dir_reo;
  |PATH_DAT #6 dir_reo;
  |PATH_DAT #9 dir_reo;
|FPRC;

|PROCESO def_empresa; |TIPO 7;
  #0#3 = #30#2;
  |PINTA #0#3;
|FPRC;

|PROCESO empresas; |TIPO 0;
 |SI aste = "*"; |ACABA; |FINSI;
 |ABRE #3;
 #3#0 = #0#3;
 |LEE 001#3=;
 FEstado = FSalida;
 |SI FEstado ! 0;  |DDEFECTO #3;  |FINSI;
 |ATRI I;
 |PINTA 0934, #3#2;
 |ATRI 0;
 |CIERRA #3;
 |SI FEstado ! 0;
     |MENAV "    ATENCION!!! Empresa Inexistente ...";
     |ERROR;
 |FINSI;
|FPRC;

|PROCESO actividad; |TIPO 0;
 |SI aste = "*"; |ACABA; |FINSI;
 |ABRE #1;
    #1#0 = #0#3;
    #1#1 = #0#0;
    #1#2 = #30#26;
 |LEE 001#1=;
    FEstado = FSalida;
 |SI FEstado ! 0;  |DDEFECTO #1;  |FINSI;
 |ATRI I;
 |PINTA 1034, #1#6;
 |ATRI 0;
 |CIERRA #1;
 |SI FEstado ! 0;
    |MENAV "    ATENCION!!! Actividad Inexistente ...";
    |ERROR;
 |FINSI;
|FPRC;

***************************************************************************
               GRABACION DEL 300
***************************************************************************

||.........TOTALES 300

|PROCESO totaliza_300;
  #2#29 = compens;
  compens = 0;
  #2#20 = #2#07+#2#10+#2#13+#2#16+#2#19+#2#48+#2#50;      || t. devengado
  #2#25 = #2#21+#2#22+#2#51+#2#23+#2#24+#2#43+#2#44;      || t. deducible
  #2#26 = #2#20-#2#25;                                    || diferencia
  #2#28 = #2#26%#2#27;                                    || atribuible
  #2#31 = 0;
  #2#32 = 0;
  #2#33 = 0;
  |SI #2#28 > 0;  #2#33 = #2#28;  |FINSI;                 || a ingresar
  |SI #2#28 < 0;  #2#32 = #2#28;  |FINSI;                 || a compensar
  |SI #2#28 < 0;  #2#31 = #2#28;  |FINSI;                 || a devolver
||totales
  #2#20 = #2#7 + #2#10 + #2#13 + #2#16 + #2#19 + #2#48 + #2#50;
  #2#25 = #2#21 + #2#22 + #2#23 + #2#24 + #2#43 + #2#44 + #2#51;
  #2#26 = #2#20 - #2#25;
  #2#35 = #2#54;
  #2#52 = #2#49;
  #2#42 = #2#5  + #2#8  + #2#11;
  #2#42 = #2#42 + #2#34 + #2#35 + #2#36 + #2#37 + #2#38 + #2#39
  #2#42 = #2#42 - #2#40 - #2#41;

  total = #2#28 - #2#29 - #2#30;
  #2#55 = total;
  |SI total = 0;
      #2#33 = 0;
      #2#31 = 0;
      #2#32 = 0;
  |FINSI;
  |SI total > 0;
      #2#33 = total;
      #2#31 = 0;
      #2#32 = 0;
  |SINO;         ||total negativo
      total = total * (-1);
      #2#33 = 0;
      #2#32 = total;
      #2#31 = 0;
      |SI #8#71 = "D";
          |SI #2#45 = "T"; |Y #2#1 = 4;
              #2#32 = 0;
              #2#31 = total;
          |FINSI;
          |SI #2#45 = "M"; |Y #2#1 = 12;
              #2#32 = 0;
              #2#31 = total;
          |FINSI;
          |SI #2#45 = "A";
              #2#32 = 0;
              #2#31 = total;
          |FINSI;
      |FINSI;
  |FINSI;

  #9#4  = #2#34;
  #9#5  = #2#38;
  #9#6  = #2#39;
  #9#7  = #2#35;
  #9#8  = #2#36;
  #9#9  = #2#37;
  #9#10 = #2#40;
  #9#11 = #2#41;
  #9#12 = #2#52;
  #9#13 = #2#53;
  #9#14 = pto91;
  #9#15 = #2#56;
  #9#16 = #2#57;
|FPRC;

||.........IVA SOPORTADO

|PROCESO iva_sop;
  t_ivasop = #5#14+#5#15+#5#16+#5#20+#5#21+#5#22+#5#56+#5#57+#5#60+#5#61; || suma recargo
  |SI #5#23 = "S";
      |SI #5#48 = "S";
          #2#43 = #2#43 + t_ivasop;   || operaciones interiores inversiones
      |SINO;
          #2#21 = #2#21 + t_ivasop;   || operaciones interiores corrientes
      |FINSI;
  |FINSI;
|FPRC;

|PROCESO acum_sop;
     tot_iva = #5#14 + #5#15 + #5#16 + #5#56 + #5#57; || no suma recargo
     tot_bas = #5#8 + #5#10 + #5#9 + #5#52 + #5#53;
     |SI #5#23 = "S";
          |SI #5#48 = "N";         ||Si no es de inversion
               |HAZ noinver;
          |SINO;         ||SI es de inversiones
               |HAZ inver;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO inver;
     |SI #7#1 = 5;
          #2#24 = #2#24 + tot_iva;
     |SINO;
          |SI #7#1 = 6;       ||Op. Interiores Inversiones
               #2#43 = #2#43 + tot_iva;
          |SINO;
               |SI #7#1 = 7;  ||Importaciones Inversiones
                    #2#44 = #2#44 + tot_iva;
               |SINO;
                    |SI #7#1 = 8;
                         #2#54 = #2#54 - tot_bas;
                         #2#35 = #2#35 - tot_bas;
                    |SINO;
                         |SI #7#1 = 3;
                             #2#51 = #2#51 + tot_iva; || al 51 porque en EOS no esta 58
                             |SI #7#2 = 2;
                                 #2#49 = #2#49 + tot_bas;          ||base intracomunitario
                                 #2#50 = #2#50 + tot_iva;
                                 #2#52 = #2#52 + tot_bas;
                             |FINSI;
                         |SINO;
                             #2#24 = #2#24 + tot_iva;
                         |FINSI;
                    |FINSI;
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO noinver;
     |SI #7#1 = 1;       ||Operaciones Corrientes
          #2#21 = #2#21 + tot_iva;
     |SINO;
          |SI #7#1 = 2;  ||Importaciones
               #2#22 = #2#22 + tot_iva;
          |SINO;
               |SI #7#1 = 3;  ||Adj. Intracomunitaria
                    #2#51 = #2#51 + tot_iva;
                    |SI #7#2 = 2;
                        #2#49 = #2#49 + tot_bas;          ||base intracomunitario
                        #2#50 = #2#50 + tot_iva;
                        #2#52 = #2#52 + tot_bas;
                    |FINSI;
               |SINO;
                    |SI #7#1 = 4;  ||Regimen AGp
                         #2#23 = #2#23 + tot_iva;
                    |SINO;
                         |SI #7#1 = 8;  ||Entraga intracom
                              #2#54 = #2#54 - tot_bas;
                              #2#35 = #2#35 - tot_bas;
                         |SINO;
                              #2#21 = #2#21 + tot_iva;
                         |FINSI;
                    |FINSI;
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO abre_300_sop;
  perio = #5#44;
  activi = #0#0;
  any = #30#26;
  |SI perio ! 1;
      |HAZ lee_ant;        || carga compensar
  |SINO;
       |HAZ lee_any_a;     || el del a¤o pasado
  |FINSI;
  |DDEFECTO #2;
  #2#00 = #0#3;        || empresa
  #2#45 = #0#0;        || actividad
  #2#01 = #5#44;       || periodo
  #2#02 = #30#26;      || a¤o
  |LEE 101#2=;
  FEstado = FSalida;
  #2#00 = #0#3;        || empresa
  #2#45 = #0#0;        || actividad
  #2#01 = #5#44;       || periodo
  #2#02 = #30#26;      || a¤o
  |SI FSalida ! 0;
    #2#06 = #6#1;
    #2#09 = #6#2;
    #2#12 = #6#3;
    #2#15 = #6#4;
    #2#18 = #6#5;
    #2#47 = #6#6;
  |FINSI;
  |DDEFECTO #9;
  #9#00 = #0#3;        || empresa
  #9#01 = #0#0;        || actividad
  #9#02 = #5#44;       || periodo
  #9#03 = #30#26;      || a¤o
  |LEE 101#9=;
  FEstad1 = FSalida;
  #9#00 = #0#3;        || empresa
  #9#01 = #0#0;        || actividad
  #9#02 = #5#44;       || periodo
  #9#03 = #30#26;      || a¤o
  pto91 = #9#14;
|FPRC;

|PROCESO cierra_300_sop;
|SI per_ant ! -1;
    |HAZ totaliza_300;
    |SI FEstado = 0;  |GRABA 020#2a;  |FINSI;
    |SI FEstado ! 0;  |GRABA 020#2n;  |FINSI;
    |LIBERA #2;
    |SI FEstad1 = 0;  |GRABA 020#9a;  |FINSI;
    |SI FEstad1 ! 0;  |GRABA 020#9n;  |FINSI;
    |LIBERA #9;
|FINSI;
|FPRC;

|PROCESO sopor1;
  |PINTA 2145, #5#44;
  |PINTA 2148, #5#0;
  |PINTA 2151, #5#2;
  |SI per_ant ! #5#44;
      |HAZ cierra_300_sop;
      |HAZ abre_300_sop;
      per_ant = #5#44;
  |FINSI;
  #7#0 = #5#6;
  |LEE 001#7=;
  |SI FSalida = 0;
      |SI #7#1 ! 9;
          |HAZ acum_sop;
      |FINSI;
  |SINO;
       |HAZ iva_sop;
  |FINSI;
#5#28="S";
|GRABA 011#5a;
|FPRC;

|DEFBUCLE sopor;
 #5#1;
 44;
 #0#4, "  ",      1, #0#1;
 #0#5, "zz", 999999, #0#2;
 e;
 NULL, NULL, NULL, NULL, NULL, sopor1;
 #5#44, #5#0, #5#2, #5#27;    || ordena por periodo
|FIN;

||.........IVA REPERCUTIDO

|PROCESO acum_rep;
     tot_bas = #4#8  + #4#9  + #4#10 + #4#52 + #4#53; ||Total bases
     tot_iva = #4#14 + #4#15 + #4#16 + #4#56 + #4#57; || Total IVA
     tot_iva = tot_iva + #4#20 + #4#21 + #4#22 + #4#60 + #4#61; || + recargo

     |SI #7#2 = 2;  ||Adq Intracomunitarias
          #2#49 = #2#49 + tot_bas;          ||base intracomunitario
          #2#50 = #2#50 + tot_iva;
          |SI #7#1 = 3;
              |SI #4#48 = "N"; #2#51 = #2#51 + tot_iva; |FINSI;
              |SI #4#48 = "S"; #2#58 = #2#58 + tot_iva; |FINSI;
          |FINSI;
          #2#52 = #2#52 + tot_bas;
     |FINSI;

     |SI #7#2 = 3;       ||Entrada Intracomunitaria
          #2#54 = #2#54 + tot_bas;
          #2#35 = #2#35 + tot_bas;
     |FINSI;
     |SI #7#2 = 4;       ||Operaciones Regimen Simplificado
          #2#34 = #2#34 + tot_bas;
     |FINSI;
     |SI #7#2 = 5;       ||Op. AGyP
          #2#38 = #2#38 + tot_bas;
     |FINSI;
     |SI #7#2 = 6;       ||Recargo de equivalencia
          #2#39  = #2#39 + tot_bas;
     |FINSI;
     |SI #7#2 = 7;       || Operaciones sujetas
         pto91 = pto91 + tot_bas;
     |FINSI;
     |SI #7#2 = 8;       ||Exportaciones y otras con derecho a deduccion
          #2#36 = #2#36 + tot_bas;
     |FINSI;
     |SI #7#2 = 9;       ||Op Exentas sin derecho a deduccion
          #2#37 = #2#37 + tot_bas;
     |FINSI;
     |SI #7#2 = 10;      ||Entrega bienes inmuebles
          #2#40 = #2#40 + tot_bas;
     |FINSI;
     |SI #7#2 = 11;      ||Ventas a distancia no sujetas
          #2#53 = #2#53 + tot_bas;
     |FINSI;
     |SI #7#2 = 12;      ||Entrega bienes objetos de otros estados miembros
          #2#56 = #2#56 + tot_bas;
     |FINSI;
     |SI #7#2 = 13;      ||Entregas Interiores de Bienes
          #2#57 = #2#57 + tot_bas;
     |FINSI;
     |SI #7#2 = 14;      ||Entrega bienes de inversion
          #2#41 = #2#41 + tot_bas;
     |FINSI;
|FPRC;

|PROCESO recargo;
    x1 = 0;  x2 = 0;  x3 = 0;  x4 = 0;  x5 = 0;
|SI #4#17 = #6#4;   x1 = 14;  |FINSI;
|SI #4#17 = #6#5;   x1 = 17;  |FINSI;
|SI #4#17 = #6#6;   x1 = 46;  |FINSI;

|SI #4#18 = #6#4;   x2 = 14;  |FINSI;
|SI #4#18 = #6#5;   x2 = 17;  |FINSI;
|SI #4#18 = #6#6;   x2 = 46;  |FINSI;

|SI #4#19 = #6#4;   x3 = 14;  |FINSI;
|SI #4#19 = #6#5;   x3 = 17;  |FINSI;
|SI #4#19 = #6#6;   x3 = 46;  |FINSI;

|SI #4#58 = #6#4;   x4 = 14;  |FINSI;
|SI #4#58 = #6#5;   x4 = 17;  |FINSI;
|SI #4#58 = #6#6;   x4 = 46;  |FINSI;

|SI #4#59 = #6#4;   x5 = 14;  |FINSI;
|SI #4#59 = #6#5;   x5 = 17;  |FINSI;
|SI #4#59 = #6#6;   x5 = 46;  |FINSI;

|SI x1 ! 0;
    #2x1 = #2x1 + #4#08;                     || base 1
    x1 = x1 + 1;    #2x1 = #4#17;            || (%) rec. 1
    x1 = x1 + 1;    #2x1 = #2x1 + #4#20;     || cuota rec. 1
|FINSI;
|SI x2 ! 0;
    #2x2 = #2x2 + #4#09;                     || base 2
    x2 = x2 + 1;    #2x2 = #4#18;            || (%) rec. 2
    x2 = x2 + 1;    #2x2 = #2x2 + #4#21;     || cuota rec. 2
|FINSI;
|SI x3 ! 0;
    #2x3 = #2x3 + #4#10;                     || base 3
    x3 = x3 + 1;    #2x3 = #4#19;            || (%) rec. 3
    x3 = x3 + 1;    #2x3 = #2x3 + #4#22;     || cuota rec. 3
|FINSI;
|SI x4 ! 0;
    #2x4 = #2x4 + #4#52;                     || base 4
    x4 = x4 + 1;    #2x4 = #4#58;            || (%) rec. 4
    x4 = x4 + 1;    #2x4 = #2x4 + #4#60;     || cuota rec. 4
|FINSI;
|SI x5 ! 0;
    #2x5 = #2x5 + #4#53;                     || base 5
    x5 = x5 + 1;    #2x5 = #4#59;            || (%) rec. 5
    x5 = x5 + 1;    #2x5 = #2x5 + #4#61;     || cuota rec. 5
|FINSI;
|FPRC;

|PROCESO iva_rep;
    x1 = 0;  x2 = 0;  x3 = 0;  x4 = 0;  x5 = 0;
|SI #4#11 = #6#1;   x1 =  5;  |FINSI;
|SI #4#11 = #6#2;   x1 =  8;  |FINSI;
|SI #4#11 = #6#3;   x1 = 11;  |FINSI;

|SI #4#12 = #6#1;   x2 =  5;  |FINSI;
|SI #4#12 = #6#2;   x2 =  8;  |FINSI;
|SI #4#12 = #6#3;   x2 = 11;  |FINSI;

|SI #4#13 = #6#1;   x3 =  5;  |FINSI;
|SI #4#13 = #6#2;   x3 =  8;  |FINSI;
|SI #4#13 = #6#3;   x3 = 11;  |FINSI;

|SI #4#54 = #6#1;   x4 =  5;  |FINSI;
|SI #4#54 = #6#2;   x4 =  8;  |FINSI;
|SI #4#54 = #6#3;   x4 = 11;  |FINSI;

|SI #4#55 = #6#1;   x5 =  5;  |FINSI;
|SI #4#55 = #6#2;   x5 =  8;  |FINSI;
|SI #4#55 = #6#3;   x5 = 11;  |FINSI;

|SI x1 ! 0;
    #2x1 = #2x1 + #4#08;                     || base 1
    x1 = x1 + 1;    #2x1 = #4#11;            || (%) iva 1
    x1 = x1 + 1;    #2x1 = #2x1 + #4#14;     || cuota iva 1
|FINSI;
|SI x2 ! 0;
    #2x2 = #2x2 + #4#09;                     || base 2
    x2 = x2 + 1;    #2x2 = #4#12;            || (%) iva 2
    x2 = x2 + 1;    #2x2 = #2x2 + #4#15;     || cuota iva 2
|FINSI;
|SI x3 ! 0;
    #2x3 = #2x3 + #4#10;                     || base 3
    x3 = x3 + 1;    #2x3 = #4#13;            || (%) iva 3
    x3 = x3 + 1;    #2x3 = #2x3 + #4#16;     || cuota iva 3
|FINSI;
|SI x4 ! 0;
    #2x4 = #2x4 + #4#52;                     || base 4
    x4 = x4 + 1;    #2x4 = #4#54;            || (%) iva 4
    x4 = x4 + 1;    #2x4 = #2x4 + #4#56;     || cuota iva 4
|FINSI;
|SI x5 ! 0;
    #2x5 = #2x5 + #4#53;                     || base 5
    x5 = x5 + 1;    #2x5 = #4#55;            || (%) iva 5
    x5 = x5 + 1;    #2x5 = #2x5 + #4#57;     || cuota iva 5
|FINSI;
|FPRC;

|PROCESO abre_300_rep;
  perio = #4#44; || periodo (1-12)
  activi = #0#0; || tipo (T/M/A)
  any = #30#26;  || any
  |SI perio ! 1;
      |HAZ lee_ant;        || carga compensar
  |SINO;
      |HAZ lee_any_a;     || el del a¤o pasado
  |FINSI;
|| ............................ .... .... lee Mantenimiento 300
    #2#00 = #0#3;        || empresa
    #2#45 = #0#0;        || actividad
    #2#01 = #4#44;       || periodo
    #2#02 = #30#26;      || a¤o
|LEE 001#2=;
|SI FSalida = 0;
    |BORRA 020#2a;
|FINSI;
|DDEFECTO #2;
    #2#00 = #0#3;        || empresa
    #2#45 = #0#0;        || actividad
    #2#01 = #4#44;       || periodo
    #2#02 = #30#26;      || a¤o
    #2#06 = #6#1;
    #2#09 = #6#2;
    #2#12 = #6#3;
    #2#15 = #6#4;
    #2#18 = #6#5;
    #2#47 = #6#6;
|| ......................... lee registro operaciones especiales
    #9#00 = #0#3;        || empresa
    #9#01 = #0#0;        || actividad
    #9#02 = #4#44;       || periodo
    #9#03 = #30#26;      || a¤o
|LEE 001#9=;
|SI FSalida = 0;
    |BORRA 020#9a;
|FINSI;
|DDEFECTO #9;
    #9#00 = #0#3;        || empresa
    #9#01 = #0#0;        || actividad
    #9#02 = #4#44;       || periodo
    #9#03 = #30#26;      || a¤o
    pto91 = 0;
|FPRC;

|PROCESO cierra_300_rep;
|SI per_ant ! -1;
    |HAZ totaliza_300;
    |GRABA 020#2n;
    |GRABA 020#9n;
|FINSI;
|FPRC;

|PROCESO reper1;
  |PINTA 2045, #4#44;
  |PINTA 2048, #4#0;
  |PINTA 2051, #4#2;
  |SI per_ant ! #4#44;
      |HAZ cierra_300_rep;
      |HAZ abre_300_rep;
      per_ant = #4#44;
  |FINSI;
#7#0 = #4#6;
|LEE 001#7=;
|SI FSalida = 0;
     |SI #7#2 = 1; |O #7#2 = 0;
          |HAZ iva_rep;
          |HAZ recargo;
     |SINO;
          |SI #7#2 = 14; |O #7#2 = 10;
              |HAZ iva_rep;
              |HAZ recargo;
          |FINSI;
          |HAZ acum_rep;
     |FINSI;
|SINO;
     |HAZ iva_rep;
     |HAZ recargo;
|FINSI;
  #4#28 = "S";
  |GRABA 011#4a;
|FPRC;

|DEFBUCLE reper;
 #4#1;
 44;
 #0#4, "  ",      1, #0#1;
 #0#5, "zz", 999999, #0#2;
 e;
 NULL, NULL, NULL, NULL, NULL, reper1;
 #4#44, #4#0, #4#2, #4#27;    || ordena por periodo
|FIN;

|PROCESO borrando;
|BORRA 020#2a;
|FPRC;

|DEFBUCLE borra;
 #2#1;
 ;
 #0#3, #0#0, #0#1, #30#26;
 #0#3, #0#0, #0#2, #30#26;
 e;
 NULL, borrando;
|FIN;

|PROCESO lee_ant;
     compens = 0;
     perio = perio - 1;
     #2#45 = activi;
     #2#1 = perio;
     #2#2 = any;
     |LEE 001#2=;
     |SI FSalida = 0;
          compens = #2#32;
     |FINSI;
     perio = perio + 1;
     sw_per = 1;
|FPRC;

|PROCESO lee_any_a;
     compens = 0;
     sw_exi = 0;
     || lee el a¤o anterior, para ver si existe
     |ABRE #30;
     a_emp = #30#2;
     ac_pe = #30#3;
     a_per = ac_pe - 1;
     |SI a_per < 0; a_per = 9; |FINSI;
     #30#3 = a_per;
     |LEE 011#30=;
     |SI FSalida ! 0;
         sw_exi = 1;
     |FINSI;
     #30#2 = a_emp;
     #30#3 = ac_pe;
     |LEE 011#30=;
     |CIERRA #30;
     |SI sw_exi = 1; |ACABA; |FINSI;

   |SUB_EJECUTA "caany300";
|FPRC;

|PROCESO reo300; |TIPO 3;
  |SI aste = "*"; |ACABA; |FINSI;
  |DDEFECTO #8;
  |ABRE #8;
  #8#0 = #30#2;
  #8#1 = #30#26;
  |LEE 000#8=;
  |SI FSalida ! 0;
      |MENAV "    No encontrado Fichero Portada MODELOS";
      |ERROR;
  |FINSI;
  |CIERRA #8;
  |ABRE #6;
  |LEE 001#6p;
  FEstado = FSalida;
  |CIERRA #6;
  |SI FEstado = 0;
      |BUCLE borra;
      |ABRE #2;
      |ABRE #9;
      |ABRE #7;

        per_ant = -1;
      |BUCLE reper;
      |HAZ cierra_300_rep;

        per_ant = -1;
      |BUCLE sopor;
      |HAZ cierra_300_sop;

      |CIERRA #2;
      |CIERRA #9;
      |CIERRA #7;
  |SINO;
      |MENAV "    ATENCION!!! No Existe Control de IVA en R.E.O. ...";
  |FINSI;
|FPRC;

|PROCESO tipo_8; || control de la ficha de clientes general
  |DIRECTORIO agi;
  |QBF agi;
  |PATH_DAT #23 agi;

|ABRE #23;
#23#0 = 1;
|LEE 040#23=;
    || |SI FSalida ! 0; aste = "*"; |CIERRA #23; |ACABA; |FINSI;
    |SI FSalida ! 0;
        agi1 = agi + "agieos/fich/";
        |CIERRA #23;
        |ACABA;
    |FINSI;
|CIERRA #23;
aste = #23#3;
|QBF aste;
   |SI aste  = "*"; |ACABA; |FINSI;

agi1 = #23#3; |QBF agi1;

longi = agi1 % 0;
num   = longi;
num   = 100 + (num - 5);
longi = num;

agi2  = (agi1 % longi) + "pan/";

|FPRC;
