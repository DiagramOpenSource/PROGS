|FICHEROS;
    cabasipr #0;
    cacuentm #1;    || temporal cuentas presupuestos

    caparbal #2;
    cabaexp0 #3;
    cabaexp2 #5;
    caexistc #6;
    caexistl #7;

    cacuenta #10;   || plan contable
    cacuepre #11;   || fichas presupuestarias
|FIN;

|VARIABLES;
  &eaFicbal = "";
  &eaNomSes = "";
  &eaDepSes = "";
    &entrada = 1;
    informe  = "cabasipr";

    mes      = 0;
    letrames = "";
    nivel    = 0;
    linea    = 0;

    activo = 0;
    pasivo = 0;
    debe1  = 0;
    debe2  = 0;
    debe3  = 0;
    debe4  = 0;
    haber1 = 0;
    haber2 = 0;
    haber3 = 0;
    haber4 = 0;

    activo_p = 0;
    pasivo_p = 0;
    debe1_p  = 0;
    debe2_p  = 0;
    debe3_p  = 0;
    debe4_p  = 0;
    haber1_p = 0;
    haber2_p = 0;
    haber3_p = 0;
    haber4_p = 0;

    sw_error = 0;

    saldo = 0;
    debe  = 0;
    haber = 0;
    saldo_p = 0;
    debe_p  = 0;
    haber_p = 0;

    x = 0;
    y = 0;
    eldesde = 0;
    elhasta = 0;
    eldes_p = 0;
    elhas_p = 0;
    des_exi  = 0;
    has_exi  = 0;

    sw = 0;
    agrupaci = 0;
    epigrafe = 0;
    sw_epi = 0;

    d = "A";
    h = "P";
    tipo = "";

    cue_perdi = "";
    dig_perdi = 0;
    act_cuent = "";
    act_nivel = 0;
    acor_c = "";
    acor_d = 0;
    alar_c = "";
    alar_d = 0;

    t_agru = "";
    t_epig = "";
    t_part = "";
    sw_exp = 0;

    &sw_inf = 0;
    &cantidad = 0;
    &cantidad_p = 0;
    &variable = "";
    &canti = 0;
    &canti_p = 0;
    &doh = "";

    descar = 0;

     runnom = "cabasipr";

     traraf = 0;
     &r_emp = 0;
     &r_per = 0;
     &r_eje = 0;
     &r_nom = "";
     &d_mes = 0;
     &h_mes = 0;
     &estru = 0;
     aparam = "";
     d_cuen = "";
     h_cuen = "";
     d_digi = 0;
     h_digi = 0;
|FIN;

|INCLUYE presup_i;
|INCLUYE acceso_i;
|INCLUYE digito_i;

|PROCESO mayor;
  #0Campo = #0Campo > " ";
  |PINTA #0Campo;
|FPRC;

|PROCESO antesque;
  aparam = PARAMETRO; |QBF aparam;
  |SI aparam = "fech";
      |HAZ AntesSesion;
      |NOME_DAT #10 eaFicbal;
      |NOME_DAT #11 "cabalpre";
  |FINSI;
|FPRC;

|PROCESO PintoSes; |TIPO 7;
  aparam = PARAMETRO; |QBF aparam;
  |SI aparam = "fech";
      |HAZ AntesSesion;
      |SI eaNomSes > "";
          |ATRI I; |PINTA 0505, "SESION: "; |PINTA 0513,eaNomSes;
          |SI eaDepSes > "";
              |PINTA 0558, "Departamento: "; |PINTA 0572, eaDepSes;
          |FINSI;
          |ATRI 0;
      |FINSI;
  |FINSI;
|FPRC;

|| ========================================================================
||                        CALCULOS PANTALLA
|| ========================================================================

|PROCESO pintames; |TIPO 0;
    x = Campo + 2;                || Coge el campo del nombre del mes
|SI #0Campo ! 0;|Y #0Campo ! 13;
        mes = #30#11 + (#0Campo - 1);   || Operacion para seber que mes es
      |SI mes > 12; mes = mes - 12; |FINSI;
|SINO;
        mes = #0Campo;
|FINSI;
|HAZ mesletra;                          || Recoge el mes en letra
    #0x = letrames;
|PINTA #0x;                       || Pinta el nombre del mes

|SI Campo = 1;
    |SI #0#0 > #0#1;
        |MENAV "    Error de Entrada ...";
        |ERROR;
        |ACABA;
     |FINSI;                            || Si el mes hasta es menor al desde
|FINSI;
|FPRC;

|PROCESO pintames2; |TIPO 0;
|SI #0Campo ! 0;|Y #0Campo ! 13;
        mes = #30#11 + (#0Campo - 1);   || Operacion para saber que mes es
      |SI mes > 12; mes = mes - 12; |FINSI;
|SINO;
        mes = #0Campo;
|FINSI;
|HAZ mesletra;
|ATRI I;
|SI Campo = 10;  |PINTA 1827, letrames;  |FINSI;
|SI Campo = 11;  |PINTA 1927, letrames;  |FINSI;
|ATRI 0;
|FPRC;

|PROCESO mesletra;
|SI mes = 0;  letrames = "Apertura  "; |FINSI;
|SI mes = 1;  letrames = "Enero     "; |FINSI;
|SI mes = 2;  letrames = "Febrero   "; |FINSI;
|SI mes = 3;  letrames = "Marzo     "; |FINSI;
|SI mes = 4;  letrames = "Abril     "; |FINSI;
|SI mes = 5;  letrames = "Mayo      "; |FINSI;
|SI mes = 6;  letrames = "Junio     "; |FINSI;
|SI mes = 7;  letrames = "Julio     "; |FINSI;
|SI mes = 8;  letrames = "Agosto    "; |FINSI;
|SI mes = 9;  letrames = "Septiembre"; |FINSI;
|SI mes = 10; letrames = "Octubre   "; |FINSI;
|SI mes = 11; letrames = "Noviembre "; |FINSI;
|SI mes = 12; letrames = "Diciembre "; |FINSI;
|SI mes = 13; letrames = "Cierre    "; |FINSI;
|FPRC;

|PROCESO nivel; |TIPO 7;
  |SI #0#4 = "N";
      |CAMPO_MODIFICA #0#5,1,"N";
  |SINO;
      |CAMPO_MODIFICA #0#5,1,"S";
  |FINSI;
|FPRC;


|PROCESO miraniv; |TIPO 0;
     nivel = #0#5;
|SI nivel = 1;|Y dig4 = 0;  |AVISO;  |ERROR;  |FINSI;
|SI nivel = 2;|Y dig5 = 0;  |AVISO;  |ERROR;  |FINSI;
|SI nivel = 3;|Y dig6 = 0;  |AVISO;  |ERROR;  |FINSI;
|SI nivel = 4;|Y dig7 = 0;  |AVISO;  |ERROR;  |FINSI;
|SI nivel = 5;|Y dig8 = 0;  |AVISO;  |ERROR;  |FINSI;
|SI nivel = 6;|Y dig9 = 0;  |AVISO;  |ERROR;  |FINSI;
|FPRC;

|PROCESO estruct; |TIPO 0;
  |SI #0#6 = 0;  |ACABA;  |FINSI;
  |ABRE #6;
  #6#0 = #0#6;
  |LEE 040#6=;
  |SI FSalida ! 0;
      |MENAV "    No existe la estructura de descarga.";
      |CIERRA #6;
      |ERROR;
      |ACABA;
  |FINSI;
  |HAZ cue_per;
  |CIERRA #6;
|FPRC;

|PROCESO cue_per; || calcula la cuenta de perdidas y ganancias
  cue_perdi = #6#2;
  dig_perdi = #6#3;
  |SI #0#4 = "N";  nivel = #6#3;  |FINSI;
  |SI nivel < #6#3;
      |SI nivel = 1;  traraf = 100 + dig4;  |FINSI;
      |SI nivel = 2;  traraf = 100 + dig5;  |FINSI;
      |SI nivel = 3;  traraf = 100 + dig6;  |FINSI;
      |SI nivel = 4;  traraf = 100 + dig7;  |FINSI;
      |SI nivel = 5;  traraf = 100 + dig8;  |FINSI;
      cue_perdi = #6#2 % traraf;
      dig_perdi = nivel;
  |FINSI;
  cue_perdi = cue_perdi + "            ";
  cue_perdi = cue_perdi % 112;
|FPRC;

|PROCESO conexi;
  d_mes = #0#10;
  h_mes = #0#11;
  estru = #0#6;
  r_emp = empre;
  r_per = ejerc;
  r_eje = #30#26;
  r_nom = #30#1;
  |SUB_EJECUTA "caexicon.run";
|FPRC;

|| ========================================================================
||                     CALCULOS de FICHEROS AUXILIARES
|| ========================================================================
|PROCESO grabatra;  || ... Graba temporal de Agrupaciones Exp0;

|SI saldo = 0; |Y saldo_p = 0; |ACABA; |FINSI;
  #3#0 = #1#5;
  #3#1 = #1#4;
  #3#2 = #1#6;
  #3#3 = #1#7;
  #3#4 = #1#8;
  |LEE 101#3=;
  |SI FSalida ! 0;
      |DDEFECTO #3;
      #3#0 = #1#5;
      #3#1 = #1#4;
      #3#2 = #1#6;
      #3#3 = #1#7;
      #3#4 = #1#8;
      |HAZ leexplot;
      #3#5 = t_agru;
      #3#6 = t_epig;
      #3#7 = t_part;
      |GRABA 020#3c;
  |FINSI;
  #3#8  = #3#8  + saldo;
  #3#10 = #3#10 + saldo_p;
  |GRABA 020#3a;
  |LIBERA #3;

|| ... Graba Auxiliar de Cuentas.

|SI #0#4 = "N"; |ACABA; |FINSI;

  #5#0 = #1#5;
  #5#1 = #1#4;
  #5#2 = #1#6;
  #5#3 = #1#7;
  #5#4 = #1#8;
  #5#5 = #1#0;
  #5#6 = #1#1;
  #5#7 = #1#2;
  |LEE 000#5=;
  |SI FSalida ! 0;
      |DDEFECTO #5;
      #5#0 = #1#5;
      #5#1 = #1#4;
      #5#2 = #1#6;
      #5#3 = #1#7;
      #5#4 = #1#8;
      #5#5 = #1#0;
      #5#6 = #1#1;
      #5#7 = #1#2;
      |HAZ leexplot;
      #5#8 = t_agru;
      #5#9 = t_epig;
      #5#10 = t_part;
      #5#11 = 0;
      #5#12 = 0;
      |GRABA 020#5c;
  |FINSI;

  #5#11 = #5#11 + saldo;
  #5#12 = #5#12 + saldo_p;

  |GRABA 020#5a;
  |LIBERA #5;
|FPRC;

|PROCESO exis_expl;
  descar = #7des_exi - #7has_exi; || iniciales - finales
  |SI descar = 0; |ACABA; |FINSI;
  |SI descar ] 0;
      act_cuent = #7#5;     || cuenta descarga D
      act_nivel = #7#6;     || digito descarga D
  |SINO;
      act_cuent = #7#8;     || cuenta descarga H
      act_nivel = #7#9;     || digito descarga H
  |FINSI;

  acor_c = act_cuent; acor_d = act_nivel;
  |HAZ acortacuen;
  act_cuent = acor_c; act_nivel = acor_d;

  |SI act_cuent = #1#0; |Y act_nivel = #1#1;
      sw_exp = 1;
      |SI descar ] 0;
          debe = debe + descar;
      |SINO;
          haber = haber - descar;
      |FINSI;
       saldo = debe - haber;
  |FINSI;
|FPRC;

|DEFBUCLE exis2;
 #7#1;
 ;
 #0#6, 01;
 #0#6, 99;
 be;
 NULL, exis_expl;
|FIN;

|PROCESO exis_3000;
  sw_exp  = 1;
  saldo   = saldo + #7has_exi;
|FPRC;

|DEFBUCLE exis1;
 #7#2;
 ;
 #0#6, #1#0, #1#1;
 #0#6, alar_c, 9;
 e;
 NULL,exis_3000;
|FIN;

|PROCESO calcular;
 saldo   = 0;  || Saldo Real
 saldo_p = 0;  || Saldo Presupuestado
 debe    = 0;
 haber   = 0;
 sw_exp  = 0;

 |PARA x = eldes_p; |SI x [ elhas_p; |HACIENDO x = x + 1;
       saldo_p = saldo_p + #1x;
 |SIGUE;

 alfa1   = #1#0 % 101;

 |SI alfa1 = "3";
     |BUCLE exis1;
     |SI sw_exp = 1; saldo_p = saldo; |FINSI;
 |FINSI;
 |SI alfa1 = "6"; |O alfa1 = "7";
     |BUCLE exis2;
     |SI sw_exp = 1; saldo_p = saldo; |FINSI;
 |FINSI;

 |SI sw_exp = 1; |ACABA; |FINSI;

 |PARA x = eldesde; |SI x [ elhasta; |HACIENDO x = x + 3;
       y = x + 1;
       debe  = debe  + #1x;
       haber = haber + #1y;
       saldo = debe - haber;
 |SIGUE;
|FPRC;

|PROCESO grabar;
  |HAZ digitos2;
  |SI d_error ! 0;  |ACABA;  |FINSI;
  |SI #0#4 = "N";  |Y #1#3 = "N";                     |ACABA;  |FINSI;
  |SI #0#4 = "S";  |Y #1#55 > nivel;                  |ACABA;  |FINSI;
  |SI #0#4 = "S";  |Y #1#3 = "N";  |Y #1#55 ! nivel;  |ACABA;  |FINSI;

  alar_c = #1#0;
  |HAZ alargacuen;

  |SI #1#0 ! cue_perdi; || no es perdidas y ganancias
      |HAZ calcular;
      |SI saldo ! 0;
          |SI #1#5  = "H"; |O #1#5 = "P"; saldo = - saldo; |FINSI;
          |SI #1#5 = "A"; |O #1#5 = "P"; |HAZ grabatra; |FINSI;
          |HAZ sumasaldo;
      |FINSI;
  |FINSI;
|FPRC;

|DEFBUCLE cabasipr0;
 #1#1;
 ;
 d_cuen, d_digi;
 h_cuen, h_digi;
 be;
 NULL, grabar;
|FIN;
|| ************************************************************************

|PROCESO sumasaldo;    || Calculamos cada cuenta
 |SI #1#5 = "A"; activo = activo + saldo; activo_p = activo_p + saldo_p; |FINSI;
 |SI #1#5 = "P"; pasivo = pasivo + saldo; pasivo_p = pasivo_p + saldo_p; |FINSI;

 |SI #1#5 = "D";
     |SI #1#4 = 1;
         debe1 = debe1 + saldo;
     |FINSI;
     |SI #1#4 = 2;
         debe2 = debe2 + saldo;
     |FINSI;
     |SI #1#4 = 3;
         debe3 = debe3 + saldo;
     |FINSI;
     |SI #1#4 = 4;
         debe4 = debe4 + saldo;
     |FINSI;
     |SI #1#71 = "D";
         debe1_p = debe1_p + saldo_p;
     |SINO;
         debe1_p = debe1_p - saldo_p;
     |FINSI;
 |FINSI;
 |SI #1#5 = "H";
     |SI #1#4 = 1;
         haber1 = haber1 + saldo;
     |FINSI;
     |SI #1#4 = 2;
         haber2 = haber2 + saldo;
     |FINSI;
     |SI #1#4 = 3;
         haber3 = haber3 + saldo;
     |FINSI;
     |SI #1#4 = 4;
         haber4 = haber4 + saldo;
     |FINSI;
     |SI #1#71 = "H";
         haber1_p = haber1_p + saldo_p;
     |SINO;
         haber1_p = haber1_p - saldo_p;
     |FINSI;
 |FINSI;
|FPRC;

|| ***********************************************************************
|PROCESO gr_final; || Grabar totales
 saldo_p = 0;
 debe    = 0;
 haber   = 0;
 #1#0 = cue_perdi;
 #1#1 = dig_perdi;
 |LEE 001#1=;
 |SI FSalida ! 0;
     sw_error = 1;
     |MENAV "    ATENCION !!!. NO existe la cuenta de Perdidas y Ganancias ";
     |ACABA;
 |FINSI;
 |PARA x = eldesde; |SI x [ elhasta; |HACIENDO x = x + 3;
       y = x + 1;
       debe  = debe  + #1x;
       haber = haber + #1y;
       saldo = debe - haber;
 |SIGUE;
 |PARA x = eldes_p; |SI x [ elhas_p; |HACIENDO x = x + 1;
       saldo_p = saldo_p + #1x;
 |SIGUE;

 debe  = debe1  + debe2  + debe3  + debe4;
 haber = haber1 + haber2 + haber3 + haber4;
 saldo = saldo + ( debe - haber);

 debe_p  = debe1_p;
 haber_p = haber1_p;
 |SI #1#5 = "P"; saldo = - saldo; |FINSI;

 |SI #1#71 = "D"; saldo_p = saldo_p + ( debe_p - haber_p); |FINSI;
 |SI #1#71 = "H"; saldo_p = saldo_p + ( haber_p - debe_p); |FINSI;

 |HAZ grabatra; || ....................... Graba la cuenta 129. como otra mas.
 |SI #1#5 = "A"; activo = activo + saldo; activo_p = activo_p + saldo_p; |FINSI;
 |SI #1#5 = "P"; pasivo = pasivo + saldo; pasivo_p = pasivo_p + saldo_p; |FINSI;
|FPRC;

|| ***************************************************************************
|| *********************  CALCULO DE IMPRESION *******************************
|| ***************************************************************************

|PROCESO impresio;

 |SI sw = 0;
     tipo = #3#0;
     agrupaci = #3#2;
     epigrafe = #3#3;
     |SI #3#0 = "A"; doh = "ACTIVO"; |FINSI;
     |SI #3#0 = "P"; doh = "PASIVO"; |FINSI;
     |HAZ agrupacion;
     sw = 1;
 |FINSI;

 |SI tipo ! #3#0;
     sw_inf = 5;  |PRINT;  |PRINT;
     sw_inf = 1;
     |SI tipo = "A";
         variable = "  TOTAL ACTIVO ....................";
         canti = activo;
         canti_p = activo_p;
         |PRINT;
         doh = "PASIVO";
     |SINO;
         variable = "  TOTAL PASIVO ....................";
         canti = pasivo;
         canti_p = pasivo_p;
         |PRINT;
     |FINSI;
     |PIEINF;
     tipo = #3#0;
     agrupaci = #3#2;
     epigrafe = #3#3;
     |HAZ agrupacion;
 |FINSI;
 |HAZ epigraf;
|FPRC;

|PROCESO epigraf;
 |SI agrupaci ! #3#2;
     sw_inf = 5;  |PRINT;       || linea en blanco
     |HAZ agrupacion;
     agrupaci = #3#2;
 |FINSI;
 |HAZ rotura;
 sw_epi = 0;
|FPRC;

|PROCESO rotura;
 |SI epigrafe ! #3#3; |O sw_epi = 1;
     |SI #3#3 ! 0;
         sw_inf = 2;  |PRINT;        || epigrafe
     |FINSI;
 |FINSI;
 |HAZ partida;                                       || partida
 |HAZ detalle;                                       || cuentas
 epigrafe = #3#3;
|FPRC;

|PROCESO agrupacion;
  sw_epi = 1;                || fuerza la impresion del epigrafe
  |SI #3#3 = 0;                  || epigrafe=0
      sw_inf = 7;  |PRINT;   || agrupacion (con importe)
  |SINO;
      sw_inf = 0;  |PRINT;   || agrupacion (sin importe)
  |FINSI;
|FPRC;

|PROCESO partida;        || imprime titulo de partidas
  |SI #3#4 ! 0;
      sw_inf = 3;
      |PRINT;
  |FINSI;
|FPRC;

|PROCESO cuentas;
 cantidad   = #5#11;
 cantidad_p = #5#12;
 sw_inf = 6;
 |SI cantidad ! 0; |O cantidad_p ! 0;
     |PRINT;
 |FINSI;
|FPRC;

|DEFBUCLE cabasipr4;
 #5#1;
 ;
 #3#0, #3#1, #3#2, #3#3, #3#4, "            ", 0;
 #3#0, #3#1, #3#2, #3#3, #3#4, "zzzzzzzzzzzz", 9;
 e;
 NULL, NULL, NULL, NULL, NULL, cuentas;
 #5#5, #5#6;
|FIN;


|PROCESO detalle;        || imprime detalle de cuentas
  |SI #0#4 = "S";
      |BUCLE cabasipr4;
      sw_inf=5;
      |PRINT;
  |FINSI;
|FPRC;

|| ------------------
|DEFBUCLE cabasipr1;
 #3#1;
 ;
 d, 0, 01, 00, 00;
 h, 0, 99, 99, 99;
 be;
 NULL, impresio;
|FIN;

|| **********************************************************************

|PROCESO impre; |TIPO 3;
  |HAZ antesque;
  |HAZ conexi;
  |DELINDEX #3;
  |DELINDEX #5;

   |IMPRE 0; |SI FSalida ! 0; |ACABA; |FINSI;

   |INFOR informe; |SI FSalida ! 0; |ACABA; |FINSI;

   t_agrupa = #0#12;
   des_cuen = "            ";
   has_cuen = "zzzzzzzzzzzz";
   |HAZ presup0;

   sw = 0;
   |HAZ sabercam; || Calculo para saber los limites de saldos

   |ABRET #1;
   |ABRE #2;
   |ABRE #3;
   |ABRE #5;

   |BUCLE cabasipr0;      || Bucle de Calculos de cuentas
   |HAZ gr_final;

   |CIERRA #1;
   |CIERRA #2;
   |CIERRA #3;
   |CIERRA #5;

   |SI sw_error = 1; |ACABA; |FINSI;

|| ========================================================================

   |BUCLE cabasipr1;
   |HAZ final;

   |PIEINF;
   |FININF;

   |DELINDEX #1;
   |DELINDEX #3;
   |DELINDEX #5;
|FPRC;

|PROCESO final;
sw_inf = 5; |PRINT; |PRINT;
sw_inf = 1;
|SI tipo = "A";
  variable = "  TOTAL ACTIVO ....................";
  canti = activo;
  canti_p = activo_p;
  doh = "PASIVO";
  |PRINT;
|SINO;
  variable = "  TOTAL PASIVO ....................";
  canti = pasivo;
  canti_p = pasivo_p;
  |PRINT;
|FINSI;
|FPRC;

|| ***************** Rutinas ayuda

|PROCESO sabercam;
  eldesde = (#0#0 * 3) + 9;
  elhasta = (#0#1 * 3) + 9;
  des_exi =  #0#10 + 11;
  has_exi =  #0#11 + 11;
  d_cuen = "            "; d_digi = 0;
  h_cuen = "999999999999"; h_digi = 9;
  nivel = #0#5;

  eldes_p = 58 + #0#0; |SI #0#0 = 0;  eldes_p = 59; |FINSI;
                       |SI #0#0 = 13; eldes_p = 70; |FINSI;

  elhas_p = 58 + #0#1; |SI #0#1 = 0;  elhas_p = 59; |FINSI;
                       |SI #0#1 = 13; elhas_p = 70; |FINSI;
|FPRC;

|PROCESO leexplot;
 #2#0 = #3#0;
 #2#1 = #3#2;
 #2#3 = #3#3;
 #2#5 = #3#4;
 |LEE 040#2=;
 |SI FSalida ! 0; |DDEFECTO #2; |FINSI;
 t_agru = #2#2;
 t_epig = #2#4;
 t_part = #2#6;
|FPRC;

|PROCESO acortacuen;
  |SI #0#4 = "N"; |ACABA; |FINSI;
  |SI nivel < acor_d;
      |SI nivel = 1;  traraf = 100 + dig4;  |FINSI;
      |SI nivel = 2;  traraf = 100 + dig5;  |FINSI;
      |SI nivel = 3;  traraf = 100 + dig6;  |FINSI;
      |SI nivel = 4;  traraf = 100 + dig7;  |FINSI;
      |SI nivel = 5;  traraf = 100 + dig8;  |FINSI;
      acor_c = acor_c % traraf;
      acor_d = nivel;
      acor_c = acor_c + "              ";
      acor_c = acor_c % 112;
  |FINSI;
|FPRC;

|PROCESO alargacuen;
  |QBF alar_c;
  alar_c = alar_c + "99999999999999";
  alar_c = alar_c % 112;
|FPRC;
