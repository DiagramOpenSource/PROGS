|FICHEROS;
    n43genap #0;       || Este Def.
    cacuenta #2;       || Cuentas Contables.
    n43datos #3;       || Datos Importados.
    n43tempw #4,MANTE; || Temporal Datos Importados.
    n43tempa #5,MANTE; || Temporal Asiento.
    n43tempv #6,MANTE; || Temporal Pagos/Cobros.
    n43tempc #7,MANTE; || Pide Cuenta..
    camanpag #20;      || Pagos........
    camancob #33;      || Cobros.......
    n43contc #40;      || Apuntes Para Pase a Diario (C).
    n43contl #50;      || Apuntes Para Pase a Diario (L).
    caivarep #51;      || Iva Repercutido.
    caivasop #52;      || Iva Soportado.
    caconcep #333;     || Conceptos Contab.
|FIN;

|VARIABLES;
    aGuarda  = "";
    aFech    = @;
    aDeha    = "";
    nImpo    = 0;
    aDocu    = "";
    nConp    = "";
    nDcop    = "";
    nDcof    = "";
    avisos   = "";
    contaasi = 0;
    swcam    = 0;
    linea    = 0;
    saldod   = 0;
    saldoh   = 0;
    saldot   = 0;
    opcion   = 0;
    swban    = 0;
    conta    = 0;
    guarda   = "";
    fichero1 = "";
    fichero2 = "";
    contador = 0;
    contadorc = 0;
    fd = @;
    fh = @;
    fichero = "";
    &entrada = 1;
    dfe = "01.01.1900";
    hfe = "31.12.2300";
    cont = 0;
    long = "  ";
    cta  = 0;
    men1  = "     NO EXISTE EMPRESA !";
    men2  = "     Longitud de Cuenta Fuera de Nivel !";
    num   = 0;
    sw = 0;
    juan = "            ";
    ctanum = 0;
    ctaalf = "";
    longnum = "";
    vacio = "            ";
    xxxxfiac = " ";
    xxaponer = " ";
    xrelleno = "                              ";
     letra = "";
     alfa1 = "";
     alfa2 = "";
     final = "";
     x = "";
     nume1 = 0;
     nume2 = 0;
     lugar = 0;
     nifnum = 0;
     resto = 0;
     cuentat = "";
     inkey = 0;
{-1}menu = "";
    menu1 = "2400";
    menu2 = "1";
    menu3 = "";
    menu4 = "RCS";
    menu5 = " Revisar ";
    menu6 = " Contabilizar ";
    menu7 = " Salir ";

    Comodin = "";

|FIN;

|INCLUYE acceso_i;
|INCLUYE puntos_i;


|PROGRAMA;
     |HAZ inicio;
     |CLS;
     |CABEZA "Consolidacion Manual";
     |PINPA #0#0;
     |ET Vuelve;
     inkey = 0;
     |ENCAMPO #0#0;
     |SI FSalida = 0;
          |HAZ cargad;
          |VETE Vuelve;
     |SINO;
          |SUB_EJECUTA "n43gendi.tab";
     |FINSI;
|FPRO;

|PROCESO cuenta;|TIPO 6;
    nDevol = 1;
    entrada = 1;
    p_alfa1 = #0Campo;
    salidas = FSalida;
|HAZ punto;
    #0Campo = p_alfa5;
    |PINTA #0Campo;
|SI p_error ! 0;  |ERROR;  |ACABA;  |FINSI;
    cta  = #0Campo;
    long = cta % A0;
    cont = 0;
    sw = 0;
|PARA num = 14;|SI num [ 19;|HACIENDO num = num + 1;
        cont = cont + 1
    |SI long = #30num;
            sw = 1;
            num = 19;
    |FINSI;
|SIGUE;
|SI sw = 0;
    |MENAV men2;
    |ERROR;
    |ACABA;
|FINSI;
    #0#1 = cont;
|FPRC;


|PROCESO tecla; |TIPO 11;
     |SI FSalida = 9;
          |PTEC 806;
          inkey = 9;
     |FINSI;
     |SI FSalida = 10;
          |SI #4#14 = "*";
               #4#14 = " ";
          |SINO;
               #4#14 = "*";
          |FINSI;
          |GRABA 000#4a;
     |FINSI;
     |SI FSalida = 11;
          |PINTA 1063, #4#7;
          |PINTA 1163, #4#13;
          |HAZ asiento;
          inkey = 10;
          |PTEC 806;
     |FINSI;
|FPRC;

|PROCESO camancob;
        |DDEFECTO #51;
        #51#0  = #33#0; ||Libro...
        #51#2  = #33#1; ||Factura.
        #51#27 = #33#2; ||Serie...
        |LEE 041#51=;
        contador = contador + 1;
        #6#0   = contador;
        #6#1   = #33#0;
        #6#2   = #33#1;
        #6#3   = #33#2;
        #6#4   = #33#3;
        #6#5   = #33#6;
        #6#6   = #33#7;
        #6#7   = #33#8 - #33#24;
        #6#8   = #51#6;
        #6#9   = #51#7;
        #6#10  = "";
        |GRABA 000#6n;
|FPRC;

|DEFBUCLE camancob;
  #33#2;
  23;
  #7#0,#7#1,dfe,"N";
  #7#0,#7#1,hfe,"N";
  ;
  NULL,camancob;
|FIN;

|PROCESO camanpag;
        |DDEFECTO #52;
        #52#0  = #20#0; ||Libro...
        #52#2  = #20#1; ||Factura.
        #52#27 = #20#2; ||Serie...
        |LEE 041#52=;
        |SI FSalida = 0;
           #6#8   = #52#6;
           #6#9   = #52#7;
        |SINO;
           #6#8 = #20#27;
           #6#9 = #20#28;
        |FINSI;
        contador = contador + 1;
        #6#0   = contador;
        #6#1   = #20#0;
        #6#2   = #20#1;
        #6#3   = #20#2;
        #6#4   = #20#3;
        #6#5   = #20#6;
        #6#6   = #20#7;
        #6#7   = #20#8 - #20#23;
        #6#10  = "";
        |GRABA 000#6n;
|FPRC;

|DEFBUCLE camanpag;
  #20#2;
  22;
  #7#0,#7#1,dfe,"N";
  #7#0,#7#1,hfe,"N";
  ;
  NULL,camanpag;
|FIN;

|PROCESO n43datos;
     #4#0   = #3#0;
     #4#1   = #3#1;
     #4#2   = #3#2;
     #4#3   = #3#3;
     #4#4   = #3#4;
     #4#5   = #3#5;
     #4#6   = #3#6;
     #4#7   = #3#7;
     #4#8   = #3#8;
     #4#9   = #3#9;
     #4#10  = #3#10;
     #4#11  = #3#11;
     #4#12  = #3#12;
     #4#13  = 0;
     #4#14  = "";
     |GRABA 000#4n;
|FPRC;


|DEFBUCLE n43datos;
 #3#2;
 9,11;
 #0#0,000000,"N","N";
 #0#0,999999,"N","N";
 ;
 NULL,n43datos;
|FIN;


|PROCESO cargad;
     fichero = Usuario; fichero = "T0" + fichero;
     |QBF fichero;
     |NOME_DAT #4 fichero;
   |ET enlisi;
     |DELINDEX #4;
     |ABRE #4;
     |BUCLE n43datos;
   |ET enli;
     |PINPA #0#4;
     inkey = 0;
     |ENTLINEAL #1#4, -2 , 4 , 12 , 0 , inff , supf;
     |SI inkey ! 9; |Y inkey ! 11; |VETE enli; |FINSI;
     |SI inkey = 11;
          |CIERRA #4;
          |VETE enlisi;
     |FINSI;
     |CIERRA #4;
     |DELINDEX #4;
|FPRC;

|RUTINA inff;
     #4#1 = #0#0;
     #4#0 = 000001;
|FRUT;

|RUTINA supf;
     #4#1 = #0#0;
     #4#0 = 999999;
|FRUT;


|PROCESO asiento;
  |PUSHV 0501 2380;
  |PINPA #0#7;
  |ENDATOS #1#7;
  |POPV;
  |SI FSalida = 0;

     fichero1 = Usuario; fichero1 = "T1" + fichero1;
     |QBF fichero1;
     |NOME_DAT #6 fichero1;
     |DELINDEX #6;
     |ABRE #6;
     contador = 0;
   |SI #7#2= "P";
     |ABRE #52;
     |BUCLE camanpag;
     |CIERRA #52;
   |FINSI;
   |SI #7#2= "C";
     |ABRE #51;
     |BUCLE camancob;
     |CIERRA #51;
   |FINSI;
   |SI #7#2 = "C"; |O #7#2 = "P";
   |ET asi;
     inkey = 0;
     |ENTLINEAL #1#6, -1 , 4 , 21 , 1 , infa , supa;
     |SI inkey ! 9; |VETE asi;  |FINSI;
     |CIERRA #6;
     |SI #4#13 ! 0;
          |HAZ actua;
     |FINSI;
   |SINO;
          |HAZ actua;
   |FINSI;
     |DELINDEX #6;
   |FINSI;
|FPRC;


|PROCESO tecla2; |TIPO 11;
     |SI FSalida = 9;
          |PTEC 806;
          inkey = 9;
     |FINSI;
     |SI FSalida = 10;
          |HAZ graba;
          inkey = 0;
     |FINSI;
|FPRC;

|RUTINA infa;
     #6#0 = 1;
|FRUT;

|RUTINA supa;
     #6#0 = 999999;
|FRUT;

|PROCESO graba;
     |SI #6#10 = "*";
          #6#10 = " ";
          #4#13 = #4#13 - #6#7;
          |PINTA 1163, #4#13;
     |SINO;
          #6#10 = "*";
          #4#13 = #4#13 + #6#7;
          |PINTA 1163, #4#13;
     |FINSI;
     |GRABA 000#6a;
|FPRC;

|PROCESO cpen;
          |ABRE #2;
          #2#0 = #7#0;
          #2#1 = #7#1;
          |LEE 040#2=;
          |SI FSalida = 0;
               #5#3  = #2#2; || Descripcion Cta...
          |FINSI;
          |CIERRA #2;
          conta = conta + 1;
          #5#0 = conta; || Contador......
          #5#1 = #7#0;  || Cuenta Banco..
          #5#2 = #7#1;  || Digito .......
          #5#4 = aFech; || Fecha Operacion....
          |SI saldot [ 0;
               #5#5 = "D";
               #5#6 = (saldot * -1);
          |SINO;
               #5#5 = "H";
               #5#6 = saldot;
          |FINSI;
          #5#7 = aDocu; || Documento..........
          #5#8 = #7#4;  || Cod.Concepto.......
          #5#9 = #7#5;  || Descripcion........
          #5#10 = 0;    || Libro..............
          #5#11 = 0;    || Orden..............
          #5#12 = "";   || ??.................
          #5#13 = 0;    || N§ Pago............
          #5#14 = "";   || Cobro/Pago/Nada....
          #5#15 = "N";  || Conciliar NO.......
               saldod = 0;
               saldoh = 0;
               saldot = 0;
          |GRABA 020#5n;
|FPRC;



|PROCESO leeselec;
          aFech = #4#2;
          aDeha = #4#6;
          nImpo = #4#7;
          aDocu = #4#8;
          conta = conta + 1;
          |ABRE #2;
          #2#0 = #0#0;
          #2#1 = #0#1;
          |LEE 040#2=;
          |SI FSalida = 0;
               #5#3  = #2#2; || Descripcion Cta...
          |FINSI;
          |CIERRA #2;
          #5#0 = conta; || Contador......
          #5#1 = #0#0;  || Cuenta Banco..
          #5#2 = #0#1;  || Digito .......
          #5#4 = #4#2;  || Fecha Operacion....
          #5#5 = #4#6;
          #5#6 = #4#7;
          #5#7 = #4#8;  || Documento..........
          |SI #7#2 = "G"; |O #7#2 = "I";
               #5#8 = #7#4;  || Cod.Concepto.......
               #5#9 = #7#5;  || Descripcion........
          |SINO;
               #5#8 = #4#4;
               #5#9 = #4#5;
          |FINSI;
          #5#10 = 0;    || Libro..............
          #5#11 = 0;    || Orden..............
          #5#12 = "";   || ??.................
          #5#13 = 0;    || N§ Pago............
          #5#14 = "";   || Cobro/Pago/Nada....
          #5#15 = "S";  || Conciliar SI.......
          |GRABA 020#5n;
          |SI #5#5 = "D";
               saldod = saldod + #5#6;
          |SINO;
               saldoh = saldoh + #5#6;
          |FINSI;
               saldot = saldod - saldoh;
|FPRC;

|DEFBUCLE leeselec;
   #4#2;
   14;
   000000,"*";
   999999,"*";
   ;
   NULL,leeselec;
|FIN;

|PROCESO sacacocp
     nConp = #7#5;
     nDcop = "";
     nDcof = "";
     |ABRE #333;
     #333#0 = #6#8;
     |LEE 000#333=;
     |SI FSalida = 0;
         nDcof = #333#1;
         |QBF nDcof;
         nDcop = #6#9 - nDcof;
         |QBF nConp;
         nConp = nConp + " " + nDcop;
     |FINSI;
     |CIERRA #333;
|FPRC;

|PROCESO cpentrad;
          |ABRE #2;
          #2#0 = #7#0;
          #2#1 = #7#1;
          |LEE 040#2=;
          |SI FSalida = 0;
               #5#3  = #2#2; || Descripcion Cta...
          |FINSI;
          |CIERRA #2;
          conta = conta + 1;
          #5#0 = conta; || Contador......
          #5#1 = #7#0;  || Cuenta Banco..
          #5#2 = #7#1;  || Digito .......
          #5#4 = aFech; || Fecha Operacion....
          |SI aDeha = "D";
               #5#5 = "H";
          |SINO;
               #5#5 = "D";
          |FINSI;
          #5#6 = #6#7;
          #5#7 = "";    || Documento......
          |HAZ sacacocp
          #5#8 = #7#4;  || Cod.Concepto... ante   #6#8 que es el codigo de pago
          #5#9 = nConp; || Descripcion....
          #5#10 = #6#1; || Libro..............
          #5#11 = #6#2; || Orden..............
          #5#12 = #6#3; || ??.................
          #5#13 = #6#4; || N§ Pago............
          #5#14 = #7#2; || Cobro/Pago/Nada....
          #5#15 = "N";  || Conciliar NO.......
          |SI #5#5 = "D";
               saldod = saldod + #5#6;
          |SINO;
               saldoh = saldoh + #5#6;
          |FINSI;
               saldot = saldod - saldoh;
          |GRABA 020#5n;
|FPRC;

|DEFBUCLE cpentrad;
   #6#1;
   10;
   000000,"*";
   999999,"*";
   ;
   NULL,cpentrad;
|FIN;

|PROCESO actua;
     fichero2 = Usuario; fichero2 = "T2" + fichero2;
     |QBF fichero2;
     |NOME_DAT #5 fichero2;
     |DELINDEX #5;
     |ABRE #5;
     conta = 0;
     swban = 0;
     |SI #7#2 = "C"; |O #7#2 = "P";
            |BUCLE leeselec;           || Carga Seleccionados.
            |BUCLE cpentrad;           || Bucle Cobros/Pagos;
     |FINSI;
     |SI #7#2 = "G"; |O #7#2 = "I";
            |BUCLE leeselec;
            |HAZ cpen;                 || Gastos / Ingresos;
     |FINSI;
     |ET kkj;
     |PUSHV 0501 2380;
     |PINPA #0#5;
     |PINTA 1957 , saldod;
     |PINTA 2057 , saldoh;
     |PINTA 2069 , saldot;
     |ENTLINEAL #1#5, -1 , 2 , 17 , 0 , infa5 , supa5;
     |POPV;
        |SI saldot ! 0;
            avisos = "AVISO!!! Asiento Descuadrado";
            |HAZ aviso5;
            |PAUSA;
            |HAZ aviso9;
        |FINSI;
        |MENU menu;
            opcion = FSalida;
        |SI opcion = 1;
            |VETE kkj;
        |FINSI;
        |SI opcion = 2;
            |HAZ contabiliza;
        |FINSI;
     |CIERRA #5;
|FPRC;

|RUTINA infa5;
     #5#0 = 1;
|FRUT;

|RUTINA supa5;
     #5#0 = 9999;
|FRUT;


|PROCESO cuentac;|TIPO 6;
    aGuarda = #0#0;
    entrada = 1;
    p_alfa5 = "";
    p_alfa1 = #7#0;
    salidas = FSalida;
|HAZ punto;
    |SI salidas ! 18;
          #7#0 = p_alfa5;
    |SINO;
          #7#0 = #0#0;
    |FINSI;
    #0#0 = aGuarda;
    |PINTA #7#0;
    |PINTA #0#0;
|SI p_error ! 0;  |ERROR;  |ACABA;  |FINSI;
    cta  = #7#0;
    long = cta % A0;
    cont = 0;
    sw = 0;
|PARA num = 14;|SI num [ 19;|HACIENDO num = num + 1;
        cont = cont + 1
    |SI long = #30num;
            sw = 1;
            num = 19;
    |FINSI;
|SIGUE;
|SI sw = 0;
    |MENAV men2;
    |ERROR;
    |ACABA;
|FINSI;
    #7#1 = cont;
|FPRC;

|PROCESO cuentad;|TIPO 6;
    nDevol = 1;
    entrada = 1;
    p_alfa1 = #5#1;
    salidas = FSalida;
|HAZ punto;
    #5#1 = p_alfa5;
    |PINTA #5#1;
|SI p_error ! 0;  |ERROR;  |ACABA;  |FINSI;
    cta  = #5#1;
    long = cta % A0;
    cont = 0;
    sw = 0;
|PARA num = 14;|SI num [ 19;|HACIENDO num = num + 1;
        cont = cont + 1
    |SI long = #30num;
            sw = 1;
            num = 19;
    |FINSI;
|SIGUE;
|SI sw = 0;
    |MENAV men2;
    |ERROR;
    |ACABA;
|FINSI;
    #5#2 = cont;
|FPRC;


|PROCESO suma; |TIPO 2;
  |SI #5#5 = "D";
   saldod = saldod +. #5#6;
  |FINSI;
  |SI #5#5 = "H";
   saldoh = saldoh +. #5#6;
  |FINSI;
  saldot = saldod - saldoh;
  |PINTA 1957 , "          ";
  |PINTA 2057 , "          ";
  |PINTA 2069 , "          ";
  |PINTA 1957 , saldod;
  |PINTA 2057 , saldoh;
  |PINTA 2069 , saldot;
|FPRC;

|PROCESO n43tempa;
     |SI swcam = 0;
          swcam = 1;
          |LEE 000#40u;
          |SI FSalida ! 0;
               contaasi = 1;
          |SINO;
               contaasi = #40#0 + 1;
          |FINSI;
          #40#0 = contaasi; || Contador Asientos;
          #40#1 = #7#3;     || Diario Contable..
          #40#2 = #5#4;     || Fecha Movimiento;
          |GRABA 000#40n;
     |FINSI;
     linea = linea + 1;
     #50#0 = contaasi;
     #50#1 = linea;
     #50#2 = #5#1;      || Cuenta;
     #50#3 = #5#2;      || Digito;
     #50#4 = #5#3;      || Descripcion Cta.
     #50#5 = #5#8;      || Codigo concepto.
     #50#6 = #5#9;      || Descripcion concepto.
     #50#7 = #5#5;      || D/H.
     #50#8 = #5#6;      || Importe..............
     #50#9 = #5#14;     || Cobro/Pago...........
     #50#10 = #5#10;    || Libro.
     #50#11 = #5#11;    || Orde/Factura.
     #50#12 = #5#12;    || Serie........
     #50#13 = #5#13;    || N§ Pago......
     #50#14 = #5#15;    || Conciliar S/N........
     |GRABA 020#50n;
   |SI #5#14 = "P"; |O #5#14 = "p";
     #20#0 = #5#10;
     #20#1 = #5#11;
     #20#2 = #5#12;
     #20#3 = #5#13;
     |LEE 101#20=;
     |SI FSalida = 0;
          #20#23 = #20#23 + #5#6;
          #20#22 = "S";
          |GRABA 020#20a;
     |FINSI;
   |FINSI;
   |SI #5#14 = "C"; |O #5#14 = "c";
     #33#0 = #5#10;
     #33#1 = #5#11;
     #33#2 = #5#12;
     #33#3 = #5#13;
     |LEE 101#33=;
     |SI FSalida = 0;
          #33#24 = #33#24 + #5#6;
          #33#23 = "S";
          |GRABA 020#33a;
     |FINSI;
   |FINSI;
|FPRC;

|DEFBUCLE n43tempa;
    #5#1;
    ;
    1;
    9999;
    ;
    NULL,n43tempa;
|FIN;

|PROCESO desmarca;
          |ABRE #3;
          #3#0 = #4#0;
          |LEE 101#3=;
          #3#9 = "S";
          #3#11 = "S";
          |GRABA 020#3a;
          |CIERRA #3;
          |BORRA 041#4a;
|FPRC;

|DEFBUCLE desmarca;
  #4#2;
  14;
  000000,"*";
  999999,"*";
  ;
  NULL,desmarca;
|FIN;


|PROCESO contabiliza;
     linea = 0;
     swcam = 0;
     |ABRE #40;
     |ABRE #50;
     |ABRE #20;
     |ABRE #33;
     |BUCLE n43tempa;
     |BUCLE desmarca;
     |CIERRA #40;
     |CIERRA #50;
     |CIERRA #20;
     |CIERRA #33;
|FPRC;

|CALCULO mayusc; |TIPO 0;
     #7Campo = #7Campo > "";
     |PINTA #7Campo;
|FCAL;

|CALCULO mayuse; |TIPO 0;
     #5Campo = #5Campo > "";
     |PINTA #5Campo;
|FCAL;

|PROCESO aviso5;
|PUSHV 09101171;
|HAZ color_f;
|CUADRO 09 10 11 71;
|HAZ color_c;
|QBF avisos;
    alfa1 = avisos % 0;  nume1 = alfa1;
    nume2 = ((60 - nume1) / 2) ! 0;     nume1 = 60 - nume1 - nume2;
    avisos = (" " * nume1) + avisos + (" " * nume2);        || lo centra
|PINTA 1011, avisos;
|ATRI 0;
|AVISO;
|FPRC;

|PROCESO aviso9;         || desde calculo 'descuadre', 'modbaj'
|ATRI 0;
|POPV;
|FPRC;
|PROCESO color_c;        || activa el color caracter en avisos
|COLOR 14, 0;
|ATRI 0;
|FPRC;

|PROCESO color_f;        || activa el color graficos en avisos
|COLOR 4, 0;
|ATRI 0;
|FPRC;
