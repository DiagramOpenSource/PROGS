|FICHEROS;
  c3gereli #0;

  caportad #2;
  caivarep #10;
  caivasop #11;
  caivaasi #200;

  canempre #30;
|FIN;

|VARIABLES;
  leidos = 0;
  dirfich0 = "";
  emp1 = 0;
  per1 = 0;
  xx = 0;
  yy = 0;
  x = 0;
  y = 0;
  varcamp = 0;
  &r_emp = 0;
  &r_per = 0;
  alfa1 = "";
  alfa2 = "";
  men9 = "     Empresa Contable INACTIVA ";
  &enEjercicio = 0;
|FIN;

|INCLUYE acceso_i;       || lectura de la empresa

|| *************************************************************************
||                        CALCULOS DE PANTALLA
|| *************************************************************************

|PROCESO alentrar; |TIPO 7;
  #0#1 = #30#26;
  |PINTA #0#1;
|FPRC;

|PROCESO consulta;
  |SI FSalida ! 9;  |ACABA;  |FINSI;
  |SUB_EJECUTA "caconemp.run";
  |ERROR;
  |SI Campo = 3;
      #0#3 = r_emp;
      #0#5 = r_per;
      |PTEC 802;
      |PTEC 802;
  |FINSI;
  |SI Campo = 4;
      #0#4 = r_emp;
      #0#6 = r_per;
      |PTEC 802;
  |FINSI;
  |SI Campo ] 7;
      #0Campo = r_emp;
      varcamp = Campo + 20;
      #0varcamp = r_per;
      |PTEC 802;
      |PTEC 802;
  |FINSI;
|FPRC;

|PROCESO mayor;
  alfa1 = #0Campo > " ";
  #0Campo = alfa1;
  |PINTA #0Campo;
|FPRC;

|PROCESO mayor1;
  |HAZ mayor;
  |SI #0Campo = "N";
      |PARA x = 7;  |SI x [ 46;  |HACIENDO x = x + 1;
            |CAMPO_MODIFICA #0x,1,"S";
      |SIGUE;
      #0#3 = 0;
      #0#5 = 0;
      #0#4 = 0;
      #0#6 = 0;
      |CAMPO_MODIFICA #0#3,1,"N";
      |CAMPO_MODIFICA #0#5,1,"N";
      |CAMPO_MODIFICA #0#4,1,"N";
      |CAMPO_MODIFICA #0#6,1,"N";
      |PINTA #0#3;
      |PINTA #0#5;
      |PINTA #0#4;
      |PINTA #0#6;
  |SINO;
      |PARA x = 7;  |SI x [ 46;  |HACIENDO x = x + 1;
            #0x = 0;
            |CAMPO_MODIFICA #0x,1,"N";
      |SIGUE;
      |CAMPO_MODIFICA #0#3,1,"S";
      |CAMPO_MODIFICA #0#5,1,"S";
      |CAMPO_MODIFICA #0#4,1,"S";
      |CAMPO_MODIFICA #0#6,1,"S";
  |FINSI;
|FPRC;

|| --------------- comprueba desde-hasta empresas ---------------------------

|PROCESO buscaem1;
  |SI #30#26 ! #0#1; |ACABA; |FINSI;
  |SI #30#21 = "I";  |ACABA; |FINSI;
  leidos = 1;
  |ERROR10;
|FPRC;

|DEFBUCLE buscaempre;
#30#1;
;
#0#3,#0#5;
#0#4,#0#6;
e;
NULL,buscaem1;
|FIN;

|PROCESO buclempre;
  |SI #0#5 > #0#6;
      |MENAV "     ERROR EN INTRODUCCION";
      |ERROR;
      |ACABA;
  |FINSI;
  |SI #0#3 = 0;  |Y #0#4 = 0;  |ACABA;  |FINSI;
  leidos = 0;
  |BUCLE buscaempre;
  |SI leidos = 0;
      |MENAV "    ATENCION SELECCION VACIA";
      |ERROR;
      |ACABA;
  |FINSI;
|FPRC;

|PROCESO deshas;
  |SI #0#4 < #0#3;
      |MENAV "      Error en Introduccion";
      |ERROR;
  |FINSI;
|FPRC;

|| ------------------------------------------------------------------------
|PROCESO simodi; |TIPO 7;
  |PARA x = Campo + 1;  |SI x [ 26;  |HACIENDO x = x + 1;
        |CAMPO_MODIFICA #0x,1,"S";
        y = x + 20;
        |CAMPO_MODIFICA #0y,1,"S";
  |SIGUE;
  y = Campo + 20;
  |CAMPO_MODIFICA #0y,1,"S";
|FPRC;

|PROCESO nomodi;
  |PARA x = Campo + 1;  |SI x [ 26;  |HACIENDO x = x + 1;
        #0x = 0;
        |CAMPO_MODIFICA #0x,1,"N";
        y = x + 20;
        #0y = 0;
        |CAMPO_MODIFICA #0y,1,"N";
  |SIGUE;
  y = Campo + 20;
  #0y = 0;
  |CAMPO_MODIFICA #0y,1,"N";
  |PINTA #0y;
|FPRC;

|PROCESO esbuena; || desde campo de empresas seleccion
  |SI FSalida = 2; |ACABA; |FINSI;
  |SI #0Campo = 0; |HAZ nomodi;  |ACABA;  |FINSI;
  |ABRE #30;
  #30#2 = #0Campo;
  #30#3 = 0;
  |LEE 011#30];
  |SI FSalida ! 0;  |O #30#2 ! #0Campo;
      |MENAV "     NO Existe la Empresa Contable";
      |ERROR;
  |FINSI;
  |CIERRA #30;
|FPRC;

|PROCESO esbuena1;
  |SI FSalida = 2;|ACABA;|FINSI;
  x = Campo - 20;
  |SI #0x = 0;  |ACABA;  |FINSI;
  |ABRE #30;
  x = Campo - 20;
  #30#2 = #0x;
  #30#3 = #0Campo;
  |LEE 011#30=;
  |SI FSalida ! 0;
      |MENAV "     NO Existe Periodo en la Empresa Contable";
      |CIERRA #30;
      |ERROR;
      |ACABA;
  |FINSI;
  |CIERRA #30;

  |SI #30#21 = "I";
      |MENAV men9;
      |ERROR;
      |ACABA;
  |FINSI;

  |SI #30#26 ! #0#1;
      |MENAV "     El a¤o contable NO esta dentro de la seleccion ";
      |ERROR;
      |ACABA;
  |FINSI;

  x = Campo + 20;
  #0x = #30#1;
  |PINTA #0x;
  x = Campo - 20;
  alfa1 = #0x;
  alfa2 = #0Campo;
|FPRC;

|| **********************************************************************
||                       CALCULOS DE IMPRESION
|| **********************************************************************

|| ---------- Seleccion DESDE / HASTA Empresa.

|PROCESO esdesd1;
  |DBASE dir_fich;
  dirfich0 = dir_fich + "fich/";
  alfa1 = #30#2;
  alfa2 = #30#3;
  emp1 = #30#2;
  per1 = #30#3;
  alfa1 = "00000" + alfa1;   alfa1 = alfa1 % -105;
  alfa2 = "0"   + alfa2;   alfa2 = alfa2 % -101;
  dirfich0 = dirfich0 + alfa1 + alfa2 + "/";

  |SI #30#26 ! #0#1; |O #30#21 = "I";
      |ACABA;
  |FINSI;

  |HAZ haztodo;
|FPRC;

|DEFBUCLE esdesde;
#30#1;
;
#0#3,#0#5;
#0#4,#0#6;
e;
NULL,esdesd1;
|FIN;

|| ---------- Seleccion en parrilla.

|PROCESO esunaauna;

  |PARA xx = 7;  |SI xx [ 26;  |HACIENDO xx = xx + 1;
        |SI #0xx = 0;  |SAL;  |FINSI;

        |DBASE dir_fich;
        dirfich0 = dir_fich + "fich/";
        yy = xx + 20;
        emp1 = #0xx;
        per1 = #0yy;
        alfa1 = #0xx;
        alfa2 = #0yy;
        alfa1 = "00000" + alfa1;   alfa1 = alfa1 % -105;
        alfa2 = "0"   + alfa2;   alfa2 = alfa2 % -101;
        dirfich0 = dirfich0 + alfa1 + alfa2 + "/";
|| lee empresa
        |ABRE #30;
        #30#2 = emp1;
        #30#3 = per1;
        |LEE 011#30=;
        |CIERRA #30;
        |SI #30#26 ! #0#1; |O #30#21 = "I";
            |VETE otramas;
        |FINSI;
        |HAZ haztodo;
|ET otramas;
  |SIGUE;
|FPRC;

|| *************** propio del rec lculo
|PROCESO CalPeriodo_R;
   |PINTA 2210, "Registro: ";
   |PINTA 2220, #10#0;
   |PINTA 2223, #10#27;
   |PINTA 2226, #10#2;

   alfa1 = #10#3; |QBF alfa1;
   alfa1 = alfa1 % 0402;
   #10#44 = alfa1;
   |SI #200#46 = "T";
       |SI alfa1 ] "01"; |Y alfa1 [ "03"; #10#44 = 1; |FINSI;
       |SI alfa1 ] "04"; |Y alfa1 [ "06"; #10#44 = 2; |FINSI;
       |SI alfa1 ] "07"; |Y alfa1 [ "09"; #10#44 = 3; |FINSI;
       |SI alfa1 ] "10"; |Y alfa1 [ "12"; #10#44 = 4; |FINSI;
   |SINO;
       |SI #200#46 = "A"; #10#44 = 0; |FINSI;
   |FINSI;
   |GRABA 020#10a;
   |LIBERA #10;
|FPRC;

|DEFBUCLE caivarep;
     #10#1;
     ;
     #0#67, "  ", 000001;
     #0#68, "zz", 999999;
     be;
     NULL, CalPeriodo_R;
|FIN;

|PROCESO CalPeriodo_S;
   |PINTA 2210, "Registro: ";
   |PINTA 2220, #11#0;
   |PINTA 2223, #11#27;
   |PINTA 2226, #11#2;

   alfa1 = #11#3; |QBF alfa1;
   alfa1 = alfa1 % 0402;
   #11#44 = alfa1;
   |SI #200#46 = "T";
       |SI alfa1 ] "01"; |Y alfa1 [ "03"; #11#44 = 1; |FINSI;
       |SI alfa1 ] "04"; |Y alfa1 [ "06"; #11#44 = 2; |FINSI;
       |SI alfa1 ] "07"; |Y alfa1 [ "09"; #11#44 = 3; |FINSI;
       |SI alfa1 ] "10"; |Y alfa1 [ "12"; #11#44 = 4; |FINSI;
   |SINO;
       |SI #200#46 = "A"; #11#44 = 0; |FINSI;
   |FINSI;
   |GRABA 020#11a;
   |LIBERA #11;
|FPRC;

|DEFBUCLE caivasop;
     #11#1;
     ;
     #0#67, "  ", 000001;
     #0#68, "zz", 999999;
     be;
     NULL, CalPeriodo_S;
|FIN;

|PROCESO haztodo;
  |PATH_DAT #2 dirfich0;
  |PATH_DAT #10 dirfich0;
  |PATH_DAT #11 dirfich0;
  |PATH_DAT #200 dirfich0;

  |ATRI R;
  |PINTA 2213, #30#2;
  |PINTA 2219, #30#3;
  |PINTA 2221, #30#1;

  |ATRI 0;
  |ABRE #200;
  |DDEFECTO #200;
  |LEE 101#200p;
  |SI FSalida ! 0;
      |DDEFECTO #200;
  |FINSI;
  |CIERRA #200;

  |ABRE #2;
  #2#0 = #30#2;
  #2#1 = #30#26;
  |LEE 101#2=;
  |SI FSalida = 0;
      #2#93 = #200#46;
      |GRABA 020#2a;
  |FINSI;
  |LIBERA #2;
  |CIERRA #2;

  |SI #0#69 ! "S";
      |BUCLE caivarep;
  |FINSI;

  |SI #0#69 ! "R";
      |BUCLE caivasop;
  |FINSI;
|FPRC;

|| *********************************************************************
||                    COMIENZO con TIPO 3
|| *********************************************************************

|PROCESO impre; |TIPO 3;
  |SI #0#0 = "N"; |ACABA; |FINSI;

  |SI #0#1 < 80;
      enEjercicio = 2000 + #0#1;
  |SINO;
      enEjercicio = 1900 + #0#1;
  |FINSI;
  |PUSHV 2102 2378;
  |ATRI I;
  |CUADRO 2102 2378
  |ATRI N;
  |ATRI R;
  |PINTA 2203," EMPRESA:                                                                  ";
  |ATRI 0;

  |SI #0#2 = "S";
      |BUCLE esdesde;
  |SINO;
      |HAZ esunaauna;
  |FINSI;
  |POPV;
|FPRC;
