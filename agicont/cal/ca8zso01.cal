|FICHEROS;
  ca8mtlin #0,MANTE;
  ca8m0000 #1;
  ca8zso01 #10;
  ca8w0001 #50,MANTE;
|FIN;

|VARIABLES;
 aParam    = "";
 nInkey    = 0;
 aAlfa1    = "";
 aAlfa2    = "";
 aAlfa3    = "";
 nNumero   = 0;
 &eaExtension = "";
 &eaPrograma  = "";
   &nBalNum     = 0;
   &enDPlan     = 0;
   &wPatOfi     = 0;
   &wTipoPat    = 0;
|FIN;

|INCLUYE acceso_i;
|INCLUYE varnue_i;

|| ******************************************** Calculos Pantalla
|CALCULO wTipo12; |TIPO 12;
|FCAL;

|CALCULO ParaCopiar; |TIPO 0;
  nInkey = FSalida;
  |SI nInkey = 1; nInkey =  -1; |ACABA; |FINSI;

  |SI nInkey = 10;
      |HAZ CopiarNum;
      |ERROR;
      |ACABA;
  |FINSI;

  enDPlan  = #10#2;
  nBalNum  = #10#0;
  wTipoPat = 1;
  |HAZ eVerEsPatron;
  |SI wPatOfi = 1;
      |CONFI "    NCodigo de Patron Oficial. Continuar S/N";
      |SI FSalida ! 0;
          |ERROR;
          |ACABA;
      |FINSI;
  |FINSI;

  |SI nInkey = 11;
      |CONFI "    NConfirmacion baja(S/N) ...";
      |SI FSalida = 0;
          |HAZ Borrar;
          |ERROR;
      |FINSI;
  |FINSI;
|FCAL;

|| /////////////////////// PROCESOS DE TRABAJO \\\\\\\\\\\\\\\\\\\\
|RUTINA min;
  #0#8 = #10#2;
  #0#0 = #10#0;
  #0#1  = 001;
|FRUT;

|RUTINA max;
  #0#8 = #10#2;
  #0#0 = #10#0;
  #0#1 = 999;
|FRUT;

|PROCESO Tipo3;  |TIPO 3;

  |ENTLINEAL #1#0, -3, 2, 22, 0, min, max;
|FPRC;
|| **************************************************************************

|PROCESO BorroCtas;
 |BORRA 020#0a;
 |LIBERA #0;
|FPRC;

|DEFBUCLE BorroCtas;
 #0#1;
 ;
 #10#2, nNumero, 001;
 #10#2, nNumero, 999;
 be;
 NULL, BorroCtas;
|FIN;

|PROCESO Borrar;
 nNumero = #10#0;
 |BUCLE BorroCtas;
|FPRC;

|| ****************** PROCESO para copiar a¤os
|PROCESO CopiarLin;
     |SALVA_FICHA #0;
     #0#0 = #50#1;
     |GRABA 020#0n;
     |REPON_FICHA #0;
     |LIBERA #0;
|FPRC;

|DEFBUCLE CopiaLineas;
     #0#1;
     ;
     #10#2, #50#0, 001;
     #10#2, #50#0, 999;
     be;
     NULL, CopiarLin;
|FIN;


|PROCESO LeeDefinicion;
 |SI Campo = 1; |Y #50#1 = #50#0;
     |MENAV "    Error, origen y destino igual";
     |ERROR;
     |ACABA;
 |FINSI;
 |SI Campo = 1;
     enDPlan  = #10#2;
     nBalNum  = #50#1;
     wTipoPat = 1;
     |HAZ eVerEsPatron;
     |SI wPatOfi = 1;
         |CONFI "    NCodigo de Patron Oficial. Continuar S/N";
         |SI FSalida ! 0;
              |ERROR;
              |ACABA;
          |FINSI;
     |FINSI;
 |FINSI;

 |ABRE #1;
 #1#0 = #50Campo;
 |LEE 101#1=;
 |SI FSalida = 0;
      |SI Campo = 0; #50#2 = #1#1; |PINTA #50#2; |FINSI;
      |SI Campo = 1;
          #50#3 = #1#1; |PINTA #50#3;
      |FINSI;
  |SINO;
      |SI Campo = 1; #50#3 = #50#2; |PINTA #50#3; |FINSI;
      |SI Campo = 0;
          |MENAV "    No existe definicion Origen ";
          |ERROR;
      |FINSI;
  |FINSI;
  |LIBERA #1;
  |CIERRA #1;
|FPRC;


|CALCULO CopiarNum;

 |PUSHV 0809 1457;
 |PINPA #0#50;
 #50#0 = #10#0;
 #50#1 = #10#0 + 1; |SI #50#1 > 0; #50#1 = 1; |FINSI;
 #50#4 = "Descripcion Lineas";
 |PINDA #0#50;
 |ENDATOS #1#50;
 |SI FSalida ! 0;
     |POPV;
     |ACABA;
 |FINSI;

 |ABRE #0;
 #0#8 = #10#2;
 #0#0 = #50#0;
 #0#1 = 1;
 |LEE 001#0];
 |SI FSalida ! 0; |O #0#0 ! #50#0; |O #0#8 ! #10#2;
     |MENAV "    Copia cancelada. No existe Tabla Origen ...";
     |POPV;
     |CIERRA #0;
     |ACABA;
 |FINSI;

 #0#8 = #10#2;
 #0#0 = #50#1;
 #0#1 = 1;
 |LEE 101#0];
 |SI FSalida = 0; |Y #0#0 = #50#1; |Y #0#8 = #10#2;
     |AVISO;
     |CONFI "    NRegrabar Destino YA EXISTENTE (S/N): ";
     |SI FSalida ! 0;
         |MENAV "    Copia cancelada ...";
         |POPV;
         |CIERRA #0;
         |ACABA;
     |SINO;
         |CIERRA #0;
         nNumero = #50#1;
         |BUCLE BorroCtas;
     |FINSI;
 |SINO;
     |CIERRA #0;
 |FINSI;

 |ABRE #1;
 #1#0 = #50#1;
 |LEE 101#1=;
 |SI FSalida ! 0;
     #1#1 = #50#3;
     |GRABA 020#1n;
 |SINO;
     #1#1 = #50#3;
     |GRABA 020#1a;
 |FINSI;
 |LIBERA #1;
 |CIERRA #1;

 |BUCLE CopiaLineas;
 |POPV;

 #10#0 = #50#1;
 #10#1 = #50#3;
 |PTEC 809;
 |PTEC 808;
|FCAL;

|| **************************************************************************
|PROGRAMA;
    aParam = PARAMETRO; |QBF aParam;
    |SI aParam = "";
        |HAZ inicio;
        |HAZ eVarEmpresa;
    |SINO;
         enEjerBal = aParam; |SI enEjerBal < 2008; enEjerBal = 2008; |FINSI;
    |FINSI;

    |HAZ DirAplicSt;

    |SI HaySociedad = 1;

       aAlfa1 = enEjerBal;
       aAlfa1 = ":sociedad/soda8z01;" + aAlfa1;
       |SUB_EJECUTA_NP aAlfa1;

   |SINO;

       |CLS;
       |CABEZA "Descripcion de Lineas Estado cambios en el Pat. Neto";
       |PINPA #0#10;
       #10#2 = enEjerBal;
       |PINDA #0#10;

       FSalida = 1;
       |PARA; |SI 0 < FSalida; |HACIENDO;
              |ENDATOS #1#10;
              |SI 0 [ FSalida;
                  |SI nInkey = -1; |SAL; |FINSI;
                  |HAZ Tipo3;
                  FSalida = 1;
              |FINSI;
       |SIGUE;
  |FINSI;
|FPRO;
