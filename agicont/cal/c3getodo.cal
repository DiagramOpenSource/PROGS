|FICHEROS;
  c3getodo #0;
  cacuenta #1;
  caparexp #2;
  caparbal #9;
  cabaexp0 #3; || fichero trabajo totales
  cabaexp2 #5; || fichero trabajo cuentas
  caexistc #6;
  caexistl #7;
  canempre #30;
  caconimp #8;
|FIN;

|VARIABLES;
  &pathbal = "";
  &balance = 1;
  &entrada = 0;
  &aparam = "";
  informe = "";
  alfa1 = "";
  alfa2 = "";
  alfa3 = "";
  nume1 = 0;
    x = 0;
    y = 0;
  &r_emp = 0;
  &r_per = 0;
  &d_mes = 0;
  &h_mes = 0;
  &estru = 0;
  &r_eje = 0;
  &r_nom = "";
  &anu = 0;
  &anu1 = 0;
    varcamp = 0;
    mes = 0;
    letrames = "";
    nivel = 0;
    sw_error = 0;
    cue_perdi = "";
    dig_perdi = 0;
    traraf = 0;
    acor_c = "";
    alar_c = "";
    acor_d = 0;
    t_agru = "";
    t_epig = "";
    t_part = "";

  &eldesde = 0;
  &elhasta = 0;
  des_exi = 0;
  has_exi = 0;
  d_cuen = "";
  h_cuen = "";
  d_digi = 0;
  h_digi = 9;
    dos = 0;
    acumula = 0;
    acumul2 = 0;
 act_cuent = "";
 act_nivel = 0;
 saldo   = 0;
 debe    = 0;
 haber   = 0;
 sw_exp  = 0;
 descar  = 0;
 bene    = 0;
  activo = 0;
  pasivo = 0;
   explo = 0;
  debe1  = 0;
  debe2  = 0;
  debe3  = 0;
  debe4  = 0;
  haber1 = 0;
  haber2 = 0;
  haber3 = 0;
  haber4 = 0;
  tipo = "";
  bloque = 0;
  agrupaci = 0;
  epigrafe = 0;
  partida = 0;

  canti = 0;
  punto_h1 = 0;
  punto_h2 = 0;
  punto_h3 = 0;
  punto_h4 = 0;
  punto_h5 = 0;
  punto_h6 = 0;
  punto_d1 = 0;
  punto_d2 = 0;
  punto_d3 = 0;
  punto_d4 = 0;
  punto_d5 = 0;
  punto_d6 = 0;

  sw        = 0;
  sw_agr    = 0;
  sw_epi    = 0;
  sw_alguno = 0;
  swalgun   = 0;

  &titulo = "";
  &sw_inf = 0;
  &cantidad = 0;
  &cantidad2 = 0;
  &variable = "";
  &doh = "";
  &cta = "";
  &nom = "";
  &mesi = "";
  &mesf = "";
  &fec  = @;
 d_tipo = "";
 h_tipo = "";
 d_bloque = 0;
 h_bloque = 0;
 lin = 0;
 swfinal = 0;
 &empcon = "";
 &ejecon = 0;
  dirfich0 = "";
  dirfich1 = "";
  desde = 0;
  hasta = 0;
  traba = "";
  leidos = 0;
  num1 = 0;
  num2 = 0;
  num3 = 0;
  primvez = 0;
  xx = 0;
  yy = 0;
  emp1 = 0;
  emp2 = 0;
  per1 = 0;
  per2 = 0;
  men9 = "     Empresa Contable INACTIVA ";
|FIN;

|INCLUYE acceso_i;
|INCLUYE digito_i;

|| ========================================================================
||                        CALCULOS PANTALLA
|| ========================================================================

|PROCESO coloca;  |TIPO 8;
  |HAZ inicio;
  |DBASE dir_fich;
  dir_fich = dir_fich + "fich/";
|FPRC;

|PROCESO consulta;
  |SI FSalida ! 9;  |ACABA;  |FINSI;
  |SUB_EJECUTA "caconemp.run";
  |ERROR;
  |SI Campo = 24;
      #0#24 = r_emp;
      #0#25 = r_per;
      |PTEC 802;
      |PTEC 802;
  |FINSI;
  |SI Campo = 29;
      #0#29 = r_emp;
      #0#31 = r_per;
      |PTEC 802;
  |FINSI;
  |SI Campo = 30;
      #0#30 = r_emp;
      |PTEC 802;
  |FINSI;
  |SI Campo ] 32;
      #0Campo = r_emp;
      varcamp = Campo + 20;
      #0varcamp = r_per;
      |PTEC 802;
      |PTEC 802;
  |FINSI;
|FPRC;

|PROCESO pintames; |TIPO 0;
  varcamp = Campo + 2;                || Coge el campo del nombre del mes
  |SI #0Campo ! 0;|Y #0Campo ! 13;
      mes = #30#11 + (#0Campo - 1);   || Operacion para saber que mes es
      |SI mes > 12; mes = mes - 12; |FINSI;
  |SINO;
      mes = #0Campo;
  |FINSI;
  |HAZ mesletra;                      || Recoge el mes en letra
  #0varcamp = letrames;
  |PINTA #0varcamp;                   || Pinta el nombre del mes
  |SI Campo = 1;  |Y #0#0 > #0#1;
      |MENAV "    Error de Entrada ...";
      |ERROR;
  |FINSI;
|FPRC;

|PROCESO pintames2; |TIPO 0;
  |SI #0Campo ! 0;|Y #0Campo ! 13;
      mes = #30#11 + (#0Campo - 1);   || Operacion para saber que mes es
      |SI mes > 12; mes = mes - 12; |FINSI;
  |SINO;
      mes = #0Campo;
  |FINSI;
  |HAZ mesletra;
  |ATRI I;
  |SI Campo = 7;  |PINTA 1426, letrames;  |FINSI;
  |SI Campo = 8;  |PINTA 1460, letrames;  |FINSI;
  |ATRI 0;
  |SI Campo = 8;  |Y #0#7 > #0#8;
      |MENAV "    Error de Entrada ...";
      |ERROR;
   |FINSI;
|FPRC;

|PROCESO mesletra;
  |SI mes = 0;  letrames = "Apertura  "; |FINSI;
  |SI mes = 1;  letrames = "Enero     "; |FINSI;
  |SI mes = 2;  letrames = "Febrero   "; |FINSI;
  |SI mes = 3;  letrames = "Marzo     "; |FINSI;
  |SI mes = 4;  letrames = "Abril     "; |FINSI;
  |SI mes = 5;  letrames = "Mayo      "; |FINSI;
  |SI mes = 6;  letrames = "Junio     "; |FINSI;
  |SI mes = 7;  letrames = "Julio     "; |FINSI;
  |SI mes = 8;  letrames = "Agosto    "; |FINSI;
  |SI mes = 9;  letrames = "Septiembre"; |FINSI;
  |SI mes = 10; letrames = "Octubre   "; |FINSI;
  |SI mes = 11; letrames = "Noviembre "; |FINSI;
  |SI mes = 12; letrames = "Diciembre "; |FINSI;
  |SI mes = 13; letrames = "Cierre    "; |FINSI;
|FPRC;

|PROCESO nivel; |TIPO 7;
  |SI #0#4 = "N";
      |CAMPO_MODIFICA #0#5,1,"N";
  |SINO;
      |CAMPO_MODIFICA #0#5,1,"S";
  |FINSI;
|FPRC;

|PROCESO miraniv; |TIPO 0;
  nivel = #0#5;
  |SI nivel = 1;|Y dig4 = 0;  |AVISO;  |ERROR;  |FINSI;
  |SI nivel = 2;|Y dig5 = 0;  |AVISO;  |ERROR;  |FINSI;
  |SI nivel = 3;|Y dig6 = 0;  |AVISO;  |ERROR;  |FINSI;
  |SI nivel = 4;|Y dig7 = 0;  |AVISO;  |ERROR;  |FINSI;
  |SI nivel = 5;|Y dig8 = 0;  |AVISO;  |ERROR;  |FINSI;
  |SI nivel = 6;|Y dig9 = 0;  |AVISO;  |ERROR;  |FINSI;
|FPRC;

|PROCESO mayor;
  alfa1 = #0Campo > " ";
  #0Campo = alfa1;
  |PINTA #0Campo;
|FPRC;

|PROCESO mayor1;
  |HAZ mayor;
  |SI #0Campo = "N";
      |PARA x = 32;  |SI x [ 71;  |HACIENDO x = x + 1;
            |CAMPO_MODIFICA #0x,1,"S";
      |SIGUE;
      #0#29 = 0;
      #0#30 = 0;
      #0#31 = 0;
      #0#92 = 0;
      |CAMPO_MODIFICA #0#29,1,"N";
      |CAMPO_MODIFICA #0#30,1,"N";
      |CAMPO_MODIFICA #0#31,1,"N";
      |CAMPO_MODIFICA #0#92,1,"N";
      |PINTA #0#29;
      |PINTA #0#30;
      |PINTA #0#31;
      |PINTA #0#92;
  |SINO;
      |PARA x = 32;  |SI x [ 71;  |HACIENDO x = x + 1;
            #0x = 0;
            |CAMPO_MODIFICA #0x,1,"N";
      |SIGUE;
      |CAMPO_MODIFICA #0#29,1,"S";
      |CAMPO_MODIFICA #0#30,1,"S";
      |CAMPO_MODIFICA #0#31,1,"S";
      |SI #0#27 = "N";
          |CAMPO_MODIFICA #0#92,1,"S";
      |SINO;
          |CAMPO_MODIFICA #0#92,1,"N";
      |FINSI;
  |FINSI;
|FPRC;

|PROCESO buscaem1;
  |SI #30#21 = "I"; |ACABA; |FINSI;
  leidos = 1;
  |SI primvez = 0; |Y #0#27 = "S";
      num1 = #30#2;
      num2 = #30#3;
      num3 = num2 - 1;
      |SI num3 < 0;  num3 = 9;  |FINSI;
      #30#2 = num1;
      #30#3 = num3;
      |LEE 141#30=;
      |SI FSalida ! 0;
          alfa1 = num1;
          alfa2 = num3;
          traba = "     Empresa: " + alfa1 + "  " + alfa2;
          sw_error = 3;
      |FINSI;
      #30#2 = num1;
      #30#3 = num2;
      |LEE 141#30=;
      |SI sw_error = 3;
          |ERROR10;
          |ACABA;
      |FINSI;
  |FINSI;
  |HAZ caldigitos;
  |SI sw_error = 1;
      alfa1 = #30#2;
      alfa2 = #30#3;
      traba = "      Empresa: " + alfa1 + " " + alfa2;
      |ERROR10;
      |ACABA;
  |FINSI;
  alfa1 = #30#2;
  alfa2 = #30#3;
  |HAZ estruc;
  |SI sw_error = 1;
      sw_error = 2;
      alfa1 = #30#2;
      alfa2 = #30#3;
      traba = "      Empresa: " + alfa1 + " " + alfa2;
      |ERROR10;
  |FINSI;
|FPRC;

|DEFBUCLE buscaempre;
#30#1;
;
#0#29,desde;
#0#30,hasta;
e;
NULL,buscaem1;
|FIN;

|PROCESO periodes;
  |SI #0#27 = "N";
      #0#92 = #0Campo;
      |SI #0#28 = "N";
          |CAMPO_MODIFICA #0#92,1,"N";
      |SINO;
          |CAMPO_MODIFICA #0#92,1,"S";
      |FINSI;
  |SINO;
      #0#92 = #0Campo;
      |CAMPO_MODIFICA #0#92,1,"N";
      |PINTA #0#92;
  |FINSI;
|FPRC;

|PROCESO buclempre;
  |SI #0#31 > #0#92;
      |MENAV "     ERROR EN INTRODUCCION";
      |ERROR;
      |ACABA;
  |FINSI;
  |SI #0#29 = 0;  |Y #0#30 = 0;  |ACABA;  |FINSI;
  desde = #0#31;
  hasta = #0#92;
  primvez = 0;
|ET segundo;
  sw_error = 0;
  leidos = 0;
  |BUCLE buscaempre;
  |SI leidos = 0;
      |MENAV "    ATENCION SELECCION VACIA";
      |ERROR;
      |ACABA;
  |FINSI;
  |SI sw_error = 1;
      sw_error = 0;
      traba = traba + " NO SE CORRESPONDEN LOS NIVELES:";
      |MENAV traba;
      |ERROR;
      |ACABA;
  |FINSI;
  |SI sw_error = 2;
      sw_error = 0;
      traba = traba + " SIN ESTRUCTURAS DE DESCARGA";
      |MENAV traba;
      |ERROR;
  |FINSI;
  |SI sw_error = 3;
      sw_error = 0;
      traba = traba + " NO EXISTE EMPRESA";
      |MENAV traba;
      |ERROR;
  |FINSI;
  |SI #0#27 ! "S";  |O primvez ! 0;
      |ACABA;
  |FINSI;
  primvez = 1;
  desde = #0#31 - 1;
  |SI desde < 0;  desde = 9;  |FINSI;
  hasta = desde;
  |VETE segundo;
|FPRC;

|PROCESO deshas;
  |SI #0#30 < #0#29;
      |MENAV "      Error en Introduccion";
      |ERROR;
  |FINSI;
|FPRC;

|PROCESO pintanter;
  |SI #0#27 = "N";  |ACABA;  |FINSI;
  x = Campo - 19;
  y = Campo - 20;
  |SI #0y = 0;  |ACABA;  |FINSI;
  #0x = #0y;
  |PINTA #0x;
  x = Campo + 1;
  #0x = #0Campo - 1;
  |SI #0x < 0;  #0x = 9;  |FINSI;
  |PINTA #0x;
|FPRC;

|PROCESO nomodi;
      |PARA x = Campo + 1;  |SI x [ 51;  |HACIENDO x = x + 1;
            #0x = 0;
            |CAMPO_MODIFICA #0x,1,"N";
            y = x + 20;
            #0y = 0;
            |CAMPO_MODIFICA #0y,1,"N";
      |SIGUE;
      y = Campo + 20;
      #0y = 0;
      |CAMPO_MODIFICA #0y,1,"N";
      |PINTA #0y;
|FPRC;

|PROCESO esbuena;
  |SI FSalida = 2; |ACABA; |FINSI;
  |SI #0#27 = "S"; |Y #0Campo = 0;
      |SI Campo = 33; |O Campo = 35;  |O Campo = 37;  |O Campo = 39;
                      |O Campo = 41;  |O Campo = 43;  |O Campo = 45;
                      |O Campo = 47;  |O Campo = 49;  |O Campo = 51;
          x = Campo - 1;
          |SI #0x ! 0;
              |MENAV "     DEBE INTRODUCIR UNA EMPRESA QUE EXISTA";
              |ERROR;
              |ACABA;
          |FINSI;
      |FINSI;
  |FINSI;
  |SI #0Campo = 0; |HAZ nomodi;  |ACABA;  |FINSI;
  |ABRE #30;
  #30#2 = #0Campo;
  #30#3 = 0;
  |LEE 011#30];
  |SI FSalida ! 0;  |O #30#2 ! #0Campo;
      |MENAV "     NO Existe la empresa Contable";
      |ERROR;
  |FINSI;
  |CIERRA #30;
|FPRC;

|PROCESO estruc;
  alfa1 = "00000" + alfa1;   alfa1 = alfa1 % -105;
  alfa2 = "0"   + alfa2;   alfa2 = alfa2 % -101;
  dirfich0 = dir_fich + alfa1 + alfa2 + "/";
  |PATH_DAT #6 dirfich0;
  |ABRE #6;
  #6#0 = #0#6;
  |LEE 000#6=;
  |SI FSalida ! 0;  sw_error = 1;  |FINSI;
  |CIERRA #6;
|FPRC;

|PROCESO caldigitos;
  |SI #30#13 < #0#5;  sw_error = 1;  |ACABA;  |FINSI;
|FPRC;

|PROCESO esbuena1;
  |SI FSalida = 2;|ACABA;|FINSI;
  x = Campo - 20;
  |SI #0x = 0;  |ACABA;  |FINSI;
  |ABRE #30;
  x = Campo - 20;
  #30#2 = #0x;
  #30#3 = #0Campo;
  |LEE 011#30=;
  |SI FSalida ! 0;
      |MENAV "     NO Existe Periodo en la empresa Contable";
      |CIERRA #30;
      |ERROR;
      |ACABA;
  |FINSI;
  |CIERRA #30;
  |SI #30#21 = "I";
      |MENAV men9;
      |ERROR;
      |ACABA;
  |FINSI;
  sw_error = 0;
  |HAZ caldigitos;
  |SI sw_error = 1;
      sw_error = 0;
      |MENAV "      NO SE CORRESPONDEN LOS NIVELES:";
      |ERROR;
      |ACABA;
  |FINSI;
  x = Campo + 20;
  #0x = #30#1;
  |PINTA #0x;
  sw_error = 0;
  x = Campo - 20;
  alfa1 = #0x;
  alfa2 = #0Campo;
  |HAZ estruc;
  |SI sw_error = 1;
      sw_error = 0;
      |MENAV "     ATENCION LA EMPRESA NO TIENE ESTRUCTURA DE DESCARGA";
      |ERROR;
      |ACABA;
  |FINSI;
  |HAZ conexi;
|FPRC;

|PROCESO conexi;
  d_mes = #0#7;
  h_mes = #0#8;
  estru = #0#6;
  xx = Campo - 20;
  yy = Campo + 20;
  r_emp = #0xx;
  r_per = #0Campo;
  r_eje = #30#26;
  r_nom = #30#1;
  |SUB_EJECUTA "caexicon.run";
|FPRC;


|PROCESO esdesd1;
  |SI #30#21 = "I"; |ACABA; |FINSI;
  alfa1 = #30#2;
  alfa2 = #30#3;
  emp1 = #30#2;
  per1 = #30#3;
  alfa1 = "00000" + alfa1;   alfa1 = alfa1 % -105;
  alfa2 = "0"   + alfa2;   alfa2 = alfa2 % -101;
  dirfich0 = dir_fich + alfa1 + alfa2 + "/";
  |PATH_DAT #8 dirfich0;
  |ABRE #8;
  |LEE 111#8p;
  |SI #8#2 = "S"  |Y #8#3 = "N";
      pathbal = dirfich0;
      balance = 1;
      |CIERRA #8;
      |SUB_EJECUTA "careccue.tab";
  |FINSI;
  |CIERRA #8;
  |SI #0#27 = "S";
      emp2 = #30#2;
      per2 = #30#3 - 1;
      |SI per2 < 0;  per2 = 9;  |FINSI;
      alfa1 = #30#2;
      alfa2 = per2;
      alfa1 = "00000" + alfa1;   alfa1 = alfa1 % -105;
      alfa2 = "0"   + alfa2;   alfa2 = alfa2 % -101;
      dirfich1 = dir_fich + alfa1 + alfa2 + "/";
      |PATH_DAT #8 dirfich1;
      |ABRE #8;
      |LEE 111#8p;
      |SI #8#2 = "S"  |Y #8#3 = "N";
          pathbal = dirfich1;
          balance = 1;
          |CIERRA #8;
          |SUB_EJECUTA "careccue.tab";
      |FINSI;
      |CIERRA #8;
  |FINSI;
  |HAZ haztodo;
  #30#2 = emp1;
  #30#3 = per1;
  |LEE 141#30=;
|FPRC;

|DEFBUCLE esdesde;
#30#1;
;
#0#29,#0#31;
#0#30,#0#92;
e;
NULL,esdesd1;
|FIN;

|| *************************************************************************
|PROCESO impre;  |TIPO 3;
  |IMPRE 0;
  |SI FSalida ! 0;  |ACABA;  |FINSI;
  |HAZ los_informes;
  |SI #0#27 = "N";
      informe = que_i1;
  |SINO;
      informe = que_i2;
  |FINSI;
  |INFOR informe;
  |SI FSalida ! 0;  |FINIMP;  |ACABA;  |FINSI;

  mesi = #0#2; mesf = #0#3;
  fec = #0#12;

  eldesde = (#0#0 * 3) + 9;
  elhasta = (#0#1 * 3) + 9;
  des_exi =  #0#7 + 11;
  has_exi =  #0#8 + 11;
  d_cuen = "            "; d_digi = 0;
  h_cuen = "999999999999"; h_digi = 9;
  nivel = #0#5;

  |SI #0#28 = "S";
      |BUCLE esdesde;
  |SINO;
      |HAZ esunaauna;
  |FINSI;
  |FININF;
  |FINIMP;
|FPRC;

|PROCESO esunaauna;
  |SI #0#27 = "S";
      |PARA xx = 32;  |SI xx [ 50;  |HACIENDO xx = xx + 2;
            |SI #0xx = 0;  |SAL;  |FINSI;
            yy = xx + 20;
            alfa1 = #0xx;
            alfa2 = #0yy;
            emp1 = #0xx;
            per1 = #0yy;
            alfa1 = "00000" + alfa1;   alfa1 = alfa1 % -105;
            alfa2 = "0"   + alfa2;   alfa2 = alfa2 % -101;
            dirfich0 = dir_fich + alfa1 + alfa2 + "/";
            |PATH_DAT #8 dirfich0;
            |ABRE #8;
            |LEE 111#8p;
            |SI #8#2 = "S"  |Y #8#3 = "N";
                pathbal = dirfich0;
                balance = 1;
                |CIERRA #8;
                |SUB_EJECUTA "careccue.tab";
            |FINSI;
            |CIERRA #8;
            yy = xx + 1;
            emp2 = #0yy;
            alfa1 = #0yy;
            yy = yy + 20;
            per2 = #0yy;
            alfa2 = #0yy;
            alfa1 = "00000" + alfa1;   alfa1 = alfa1 % -105;
            alfa2 = "0"   + alfa2;   alfa2 = alfa2 % -101;
            dirfich1 = dir_fich + alfa1 + alfa2 + "/";
            |PATH_DAT #8 dirfich1;
            |ABRE #8;
            |LEE 111#8p;
            |SI #8#2 = "S"  |Y #8#3 = "N";
                pathbal = dirfich1;
                balance = 1;
                |CIERRA #8;
                |SUB_EJECUTA "careccue.tab";
            |FINSI;
            |CIERRA #8;
            |HAZ haztodo;
      |SIGUE;
  |SINO;
      |PARA xx = 32;  |SI xx [ 51;  |HACIENDO xx = xx + 1;
            |SI #0xx = 0;  |SAL;  |FINSI;
            yy = xx + 20;
            emp1 = #0xx;
            per1 = #0yy;
            alfa1 = #0xx;
            alfa2 = #0yy;
            alfa1 = "00000" + alfa1;   alfa1 = alfa1 % -105;
            alfa2 = "0"   + alfa2;   alfa2 = alfa2 % -101;
            dirfich0 = dir_fich + alfa1 + alfa2 + "/";
            |PATH_DAT #8 dirfich0;
            |ABRE #8;
            |LEE 111#8p;
            |SI #8#2 = "S"  |Y #8#3 = "N";
                pathbal = dirfich0;
                balance = 1;
                |CIERRA #8;
                |SUB_EJECUTA "careccue.tab";
            |FINSI;
            |CIERRA #8;
            |HAZ haztodo;
      |SIGUE;
  |FINSI;
|FPRC;

|| --------- Propio del informe



|PROCESO cue_per; || calcula la cuenta de perdidas y ganancias
  sw_error = 0;
  |ABRE #6;
  |DDEFECTO #6;
  #6#0 = #0#6;
  |LEE 040#6=;
  |SI FSalida ! 0;
      sw_error = 1;
      |CIERRA #6;
      |ACABA;
  |FINSI;
  cue_perdi = #6#2;
  dig_perdi = #6#3;
  |SI #0#4 = "S";
      acor_c = cue_perdi;
      acor_d = dig_perdi;
      |HAZ acortacuen;
      cue_perdi = acor_c;
      dig_perdi = acor_d;
  |FINSI;
  |CIERRA #6;
|FPRC;

|| ========================================================================
||                     CALCULOS de FICHEROS AUXILIARES
|| ========================================================================
|PROCESO grabatra;  || ... Graba temporal de Agrupaciones Exp0;

|SI saldo = 0; |ACABA; |FINSI;
  #3#0 = #1#5;
  #3#1 = #1#4;
  #3#2 = #1#6;
  #3#3 = #1#7;
  #3#4 = #1#8;
  |LEE 101#3=;
  |SI FSalida ! 0;
      |DDEFECTO #3;
      #3#0 = #1#5;
      #3#1 = #1#4;
      #3#2 = #1#6;
      #3#3 = #1#7;
      #3#4 = #1#8;
      |HAZ leexplot;
      #3#5 = t_agru;
      #3#6 = t_epig;
      #3#7 = t_part;
      |GRABA 020#3c;
  |FINSI;
  #3acumula = #3acumula + saldo;
  |GRABA 020#3a;
  |LIBERA #3;

|| ... Graba Auxiliar de Cuentas.

|SI #0#4 = "N"; |ACABA; |FINSI;

  #5#0 = #1#5;
  #5#1 = #1#4;
  #5#2 = #1#6;
  #5#3 = #1#7;
  #5#4 = #1#8;
  #5#5 = #1#0;
  #5#6 = #1#1;
  #5#7 = #1#2;
  |LEE 000#5=;
  |SI FSalida ! 0;
      |DDEFECTO #5;
      #5#0 = #1#5;
      #5#1 = #1#4;
      #5#2 = #1#6;
      #5#3 = #1#7;
      #5#4 = #1#8;
      #5#5 = #1#0;
      #5#6 = #1#1;
      #5#7 = #1#2;
      |HAZ leexplot;
      #5#8 = t_agru;
      #5#9 = t_epig;
      #5#10 = t_part;
      #5#11 = 0;
      #5#12 = 0;
      |GRABA 020#5c;
  |FINSI;

  #5acumul2 = #5acumul2 + saldo;
  |GRABA 020#5a;
  |LIBERA #5;
|FPRC;

|PROCESO exis_expl;
  descar = #7des_exi - #7has_exi; || iniciales - finales
  |SI descar = 0; |ACABA; |FINSI;
  |SI descar ] 0;
      act_cuent = #7#5;     || cuenta descarga D
      act_nivel = #7#6;     || digito descarga D
  |SINO;
      act_cuent = #7#8;     || cuenta descarga H
      act_nivel = #7#9;     || digito descarga H
  |FINSI;

  acor_c = act_cuent; acor_d = act_nivel;
  |HAZ acortacuen;
  act_cuent = acor_c; act_nivel = acor_d;

  |SI act_cuent = #1#0; |Y act_nivel = #1#1;
      sw_exp = 1;
      |SI descar ] 0;
          debe = debe + descar;
      |SINO;
          haber = haber - descar;
      |FINSI;
       saldo = debe - haber;
  |FINSI;
|FPRC;

|DEFBUCLE exis2;
 #7#1;
 ;
 #0#6, 01;
 #0#6, 99;
 be;
 NULL, exis_expl;
|FIN;

|PROCESO exis_3000;
  sw_exp  = 1;
  saldo   = saldo + #7has_exi;
|FPRC;

|DEFBUCLE exis1;
 #7#2;
 ;
 #0#6, #1#0, #1#1;
 #0#6, alar_c, 9;
 e;
 NULL,exis_3000;
|FIN;

|PROCESO calcular;
 saldo   = 0;
 debe    = 0;
 haber   = 0;
 sw_exp  = 0;
 alfa1   = #1#0 % 101;
 |SI alfa1 = "3";                 |BUCLE exis1; |FINSI;
 |SI alfa1 = "6"; |O alfa1 = "7"; |BUCLE exis2; |FINSI;

 |SI sw_exp = 1; |ACABA; |FINSI;

 |PARA x = eldesde; |SI x [ elhasta; |HACIENDO x = x + 3;
       y = x + 1;
       debe  = debe  + #1x;
       haber = haber + #1y;
       saldo = debe - haber;
 |SIGUE;
|FPRC;

|PROCESO grabar;
  |HAZ digitos2;
  |SI d_error ! 0;  |ACABA;  |FINSI;
  |SI #0#4 = "N";  |Y #1#3 = "N";                     |ACABA;  |FINSI;
  |SI #0#4 = "S";  |Y #1#55 > nivel;                  |ACABA;  |FINSI;
  |SI #0#4 = "S";  |Y #1#3 = "N";  |Y #1#55 ! nivel;  |ACABA;  |FINSI;

  alar_c = #1#0;
  |HAZ alargacuen;

  |SI #1#0 ! cue_perdi; || no es perdidas y ganancias
      |HAZ calcular;
      |SI saldo ! 0;
          |SI #1#5 = "H"; |O #1#5 = "P";
              saldo = - saldo;
          |FINSI;
          |HAZ grabatra;
          |HAZ sumasaldo;
      |FINSI;
  |FINSI;
|FPRC;

|DEFBUCLE cagetodo0;
 #1#1;
 ;
 d_cuen, d_digi;
 h_cuen, h_digi;
 be;
 NULL, grabar;
|FIN;
|| ************************************************************************

|PROCESO sumasaldo;    || Calculamos cada cuenta
 |SI #1#5 = "A"; activo = activo + saldo; |FINSI;
 |SI #1#5 = "P"; pasivo = pasivo + saldo; |FINSI;
 |SI #1#5 = "D";
     |SI #1#4 = 1;
         debe1 = debe1 + saldo;
     |FINSI;
     |SI #1#4 = 2;
         debe2 = debe2 + saldo;
     |FINSI;
     |SI #1#4 = 3;
         debe3 = debe3 + saldo;
     |FINSI;
     |SI #1#4 = 4;
         debe4 = debe4 + saldo;
     |FINSI;
 |FINSI;
 |SI #1#5 = "H";
     |SI #1#4 = 1;
         haber1 = haber1 + saldo;
     |FINSI;
     |SI #1#4 = 2;
         haber2 = haber2 + saldo;
     |FINSI;
     |SI #1#4 = 3;
         haber3 = haber3 + saldo;
     |FINSI;
     |SI #1#4 = 4;
         haber4 = haber4 + saldo;
     |FINSI;
 |FINSI;
|FPRC;

|PROCESO calcula_tot;    || Calculamos totales

  punto_d1 = 0;   punto_h1 = 0;
  punto_d2 = 0;   punto_h2 = 0;
  punto_d3 = 0;   punto_h3 = 0;
  punto_d4 = 0;   punto_h4 = 0;
  punto_d5 = 0;   punto_h5 = 0;
  punto_d6 = 0;   punto_h6 = 0;

  canti = haber1 - debe1;
  |SI canti < 0;
      punto_h1 = - canti;
  |SINO;
      punto_d1 = canti;
  |FINSI;

  canti = haber2 - debe2;
  |SI canti < 0;
      punto_h2 = - canti;
  |SINO;
      punto_d2 = canti;
  |FINSI;

  canti = punto_d1 + punto_d2 - punto_h1 - punto_h2;
  |SI canti < 0;
      punto_h3 = - canti;
  |SINO;
      punto_d3 = canti;
  |FINSI;

  canti = haber3 - debe3;
  |SI canti < 0;
      punto_h4 = - canti;
  |SINO;
      punto_d4 = canti;
  |FINSI;

  canti = punto_d3 + punto_d4 - punto_h3 - punto_h4;
  |SI canti < 0;
      punto_h5 = - canti;
  |SINO;
      punto_d5 = canti;
  |FINSI;

  punto_h6 = punto_h5;
  |SI punto_h5 > 0;
      punto_h6 = punto_h6 + debe4;
  |FINSI;
  |SI punto_h6 < 0; punto_h6 = 0; |FINSI;

  punto_d6 = punto_d5;
  ||SI debe4 > 0;                       || R.PRIEGO (24.03.99) impuesto neg.
      punto_d6 = punto_d6 - debe4;
  ||FINSI;
  |SI punto_d6 < 0; punto_d6 = 0; |FINSI;
|FPRC;
|| ***********************************************************************

|PROCESO gr_final; || Grabar totales
 debe = 0;
 haber = 0;
 #1#0 = cue_perdi;
 #1#1 = dig_perdi;
 |LEE 001#1=;
 |SI FSalida ! 0;
     sw_error = 1;
     |MENAV "    ATENCION !!!. NO existe la cuenta de Perdidas y Ganancias ";
     |ACABA;
 |FINSI;
 |PARA x = eldesde; |SI x [ elhasta; |HACIENDO x = x + 3;
       y = x + 1;
       debe  = debe  + #1x;
       haber = haber + #1y;
       saldo = debe - haber;
 |SIGUE;
 debe  = debe1  + debe2  + debe3  + debe4;
 haber = haber1 + haber2 + haber3 + haber4;
 saldo = saldo + ( debe - haber);
 |SI #1#5 = "P";
     saldo = - saldo;
 |FINSI;
 |HAZ grabatra; || ....................... Graba la cuenta 129. como otra mas.
 |SI #1#5 = "A"; activo = activo + saldo; |FINSI;
 |SI #1#5 = "P"; pasivo = pasivo + saldo; |FINSI;

|| Grabar totales
 |HAZ calcula_tot;

|PARA nume1 = 1; |SI nume1 [ 14; |HACIENDO nume1 = nume1 + 1;
   saldo = 0;
   agrupaci = 99;
   epigrafe = 00;
   partida  = 00;
   |SI nume1 = 1;               bloque = 0;         tipo = "A"; |FINSI;
   |SI nume1 = 2;               bloque = 0;         tipo = "P"; |FINSI;
   |SI nume1 ] 3; |Y nume1 [ 8; bloque = nume1 - 2; tipo = "D"; |FINSI;
   |SI nume1 ] 9;               bloque = nume1 - 8; tipo = "H"; |FINSI;
   |SI bloque = 3; bloque = 2; agrupaci = 100; |FINSI;
   |SI bloque = 4; bloque = 3; |FINSI;
   |SI bloque = 5; bloque = 3; agrupaci = 100; |FINSI;
   #3#0 = tipo;
   #3#1 = bloque;
   #3#2 = agrupaci;
   #3#3 = epigrafe;
   #3#4 = partida;
   |LEE 101#3=;
   |SI FSalida ! 0;
       |DDEFECTO #3;
       #3#0 = tipo;
       #3#1 = bloque;
       #3#2 = agrupaci;
       #3#3 = epigrafe;
       #3#4 = partida;
       |HAZ titulos;
       t_agru = variable %  140;
       t_epig = variable % 4120;
       #3#5 = t_agru;
       #3#6 = t_epig;
       #3#7 = "";
       |GRABA 020#3c;
  |FINSI;
  |SI tipo = "A"; saldo = activo; |FINSI;
  |SI tipo = "P"; saldo = pasivo; |FINSI;
  |SI tipo = "D";
      |SI nume1 = 3; saldo = punto_d1; |FINSI;
      |SI nume1 = 4; saldo = punto_d2; |FINSI;
      |SI nume1 = 5; saldo = punto_d3; |FINSI;
      |SI nume1 = 6; saldo = punto_d4; |FINSI;
      |SI nume1 = 7; saldo = punto_d5; |FINSI;
      |SI nume1 = 8; saldo = punto_d6; |FINSI;
  |FINSI;
  |SI tipo = "H";
      |SI nume1 =  9; saldo = punto_h1; |FINSI;
      |SI nume1 = 10; saldo = punto_h2; |FINSI;
      |SI nume1 = 11; saldo = punto_h3; |FINSI;
      |SI nume1 = 12; saldo = punto_h4; |FINSI;
      |SI nume1 = 13; saldo = punto_h5; |FINSI;
      |SI nume1 = 14; saldo = punto_h6; |FINSI;
  |FINSI;
  #3acumula = #3acumula + saldo;
  |GRABA 020#3a;
  |LIBERA #3;
|SIGUE;
|FPRC;
|| ***********************************************************************
|PROCESO cantidad;
 |SI sw_inf = 0;
     |PRINT; swalgun = 1;
 |SINO;
     |SI cantidad ! 0; |O cantidad2 ! 0;
         |PRINT; swalgun = 1;
     |FINSI;
 |FINSI;
|FPRC;

|PROCESO cuenta1;
  sw_alguno = 1;
  cta = #5#5;
  nom = #5#7;
  cantidad = #5#11;
  cantidad2 = #5#12;
  sw_inf = 2;
  |HAZ cantidad;
|FPRC;

|DEFBUCLE cagetodo5;
 #5#1;
 ;
 #3#0, #3#1, #3#2, #3#3, #3#4, "            ", 0;
 #3#0, #3#1, #3#2, #3#3, #3#4, "zzzzzzzzzzzz", 9;
 e;
 NULL, NULL, NULL, NULL, NULL, cuenta1;
 #5#5, #5#6;
|FIN;

|PROCESO impresio;
  |SI d_bloque = 0;
      |SI #3#0 = "D"; |O #3#0 = "H"; |ACABA; |FINSI; || hay algun error el la estructura
  |FINSI;
  |SI sw = 0;
      |HAZ canv_tipo;
      sw = 1;
  |FINSI;

  |SI tipo ! #3#0;
      |SI swalgun = 1; |PIEINF; |FINSI;
      swalgun = 0;
      |HAZ canv_tipo;
  |FINSI;

  |SI bloque ! #3#1; |HAZ canv_bloq; |FINSI;

  cantidad  = #3#8;
  cantidad2 = #3#10;

  |SI agrupaci ! #3#2; |HAZ canv_agru; |FINSI;

  |SI sw_agr = 0;
      |HAZ imp_agr;
  |FINSI;

  |SI epigrafe ! #3#3; |HAZ canv_epi; |FINSI;

  |SI sw_epi = 0;
      |HAZ imp_epi;
  |FINSI;

  |SI #3#4 ! 0; |Y #3#4 < 99;
      sw_inf = 1;
      variable = "          " + #3#7;
      |HAZ cantidad;
  |FINSI;

  partida = #3#4;
  |SI #0#4 = "S";
      sw_alguno = 0;
      |BUCLE cagetodo5;
      |SI sw_alguno = 1;
          sw_inf = 4;
          |PRINT; swalgun = 1;
      |FINSI;
  |FINSI;
|FPRC;

|PROCESO imp_agr;
  sw_inf = 0;
  variable = #3#5;
  |SI #3#2 ] 99;
      sw_epi = 1;
      sw_inf = 3;
      variable = #3#5 + #3#6;
  |SINO;
      |SI #3#3 = 0; |Y #3#4 = 0; || ... Agrupacion con importe.
          sw_epi = 1;
          sw_inf = 1;
      |FINSI;
  |FINSI;
  sw_agr = 1;
  |HAZ cantidad;
|FPRC;

|PROCESO imp_epi;
  sw_inf = 0;
  variable = "     " + #3#6;
  |SI #3#4 = 0;
      sw_inf = 1;
  |FINSI;
  sw_epi = 1;
  |SI #3#3 = 0; |ACABA; |FINSI;
  |HAZ cantidad;
|FPRC;

|PROCESO canv_tipo;
  sw_agr = 0;
  sw_epi = 0;
  tipo     = #3#0;
  bloque   = #3#1;
  agrupaci = #3#2;
  epigrafe = #3#3;
  partida  = 0;
  |SI #3#0 = "A"; doh = "ACTIVO"; |FINSI;
  |SI #3#0 = "P"; doh = "PASIVO"; |FINSI;
  |SI #3#0 = "D"; doh = "DEBE";   |FINSI;
  |SI #3#0 = "H"; doh = "HABER";  |FINSI;
|FPRC;

|PROCESO canv_bloq;
  |SI #3#8 ! 0; |O #3#10 ! 0;
      sw_inf = 4;
      |PRINT; swalgun = 1;
  |FINSI;
  sw_epi = 0;
  bloque   = #3#1;
  agrupaci = #3#2; |SI agrupaci ] 99; sw_agr = 0; |FINSI;
  epigrafe = #3#3;
|FPRC;

|PROCESO canv_agru;
  |SI #3#8 ! 0; |O #3#10 ! 0;
      sw_inf = 4;
      |PRINT; swalgun = 1;
  |FINSI;
  sw_agr = 0;
  sw_epi = 0;
  agrupaci = #3#2;
  epigrafe = #3#3;
|FPRC;

|PROCESO canv_epi;
  sw_epi = 0;
  epigrafe = #3#3;
|FPRC;

|DEFBUCLE cagetodo2;
 #3#1;
 ;
 d_tipo, d_bloque,  00, 00, 00;
 h_tipo, h_bloque, 100, 99, 99;
 be;
 NULL, impresio;
|FIN;

|PROCESO cagetodo1;
 titulo = "     < BALANCE DE SITUACION >     ";
 d_tipo = "A";
 h_tipo = "P";
 d_bloque = 0;
 h_bloque = 0;
 sw = 0;
 swalgun = 0;
 |BUCLE cagetodo2;
 |SI swalgun = 1;
     |PIEINF;
     swalgun = 0;
 |FINSI;
 titulo = "< CUENTA DE PERDIDAS Y GANANCIAS >";
 d_tipo = "D";
 h_tipo = "H";
 d_bloque = 1;
 h_bloque = 9;
 sw = 0;
 swalgun = 0;
 |BUCLE cagetodo2;
 |SI swalgun = 1;
     |PIEINF;
     swalgun = 0;
 |FINSI;
|FPRC;

|PROCESO leexplot;  || Lee el fichero de explotacion
  t_agru = "";
  t_epig = "";
  t_part = "";
  |SI #1#4 = 0;
      #9#0 = #1#5;
      #9#1 = #1#6;
      #9#3 = #1#7;
      #9#5 = #1#8;
      |LEE 040#9=; |SI FSalida ! 0; |DDEFECTO #9; |FINSI;
      t_agru = #9#2;
      t_epig = #9#4;
      t_part = #9#6;
 |SINO;
      #2#0 = #1#4;
      #2#1 = #1#5;
      #2#2 = #1#6;
      #2#4 = #1#7;
      #2#6 = #1#8;
      |LEE 040#2=; |SI FSalida ! 0; |DDEFECTO #2; |FINSI;
      t_agru = #2#3;
      t_epig = #2#5;
      t_part = #2#7;
  |FINSI;
|FPRC;

|PROCESO acortacuen;
  |SI #0#4 = "N"; |ACABA; |FINSI;
  |SI nivel < acor_d;
      |SI nivel = 1;  traraf = 100 + dig4;  |FINSI;
      |SI nivel = 2;  traraf = 100 + dig5;  |FINSI;
      |SI nivel = 3;  traraf = 100 + dig6;  |FINSI;
      |SI nivel = 4;  traraf = 100 + dig7;  |FINSI;
      |SI nivel = 5;  traraf = 100 + dig8;  |FINSI;
      acor_c = acor_c % traraf;
      acor_d = nivel;
      acor_c = acor_c + "              ";
      acor_c = acor_c % 112;
  |FINSI;
|FPRC;

|PROCESO alargacuen;
  |QBF alar_c;
  alar_c = alar_c + "99999999999999";
  alar_c = alar_c % 112;
|FPRC;

|PROCESO titulos;

|SI nume1 =  1; variable = "123456789*123456789*123456789*123456789*123456789*123456789*"; |FINSI;
|SI nume1 =  1; variable = "                         T O T A L   A C T I V O :          "; |FINSI;
|SI nume1 =  2; variable = "                         T O T A L   P A S I V O :          "; |FINSI;
|SI nume1 =  3; variable = "  I. BENEFICIOS DE EXPLOTACION ............................."; |FINSI;
|SI nume1 =  4; variable = " II. RESULTADOS FINANCIEROS POSITIVOS ......................"; |FINSI;
|SI nume1 =  5; variable = "III. BENEFICIOS DE LAS ACTIVIDADES ORDINARIAS .............."; |FINSI;
|SI nume1 =  6; variable = " IV. RESULTADOS EXTRAORDINARIOS POSITIVOS .................."; |FINSI;
|SI nume1 =  7; variable = "  V. BENEFICIO ANTES DE IMPUESTOS .........................."; |FINSI;
|SI nume1 =  8; variable = " VI. RESULTADO DEL EJERCICIO (BENEFICIOS) .................."; |FINSI;
|SI nume1 =  9; variable = "  I. PERDIDAS DE EXPLOTACION ..............................."; |FINSI;
|SI nume1 = 10; variable = " II. RESULTADOS FINANCIEROS NEGATIVOS ......................"; |FINSI;
|SI nume1 = 11; variable = "III. PERDIDAS DE LAS ACTIVIDADES ORDINARIAS ................"; |FINSI;
|SI nume1 = 12; variable = " IV. RESULTADOS EXTRAORDINARIOS NEGATIVOS .................."; |FINSI;
|SI nume1 = 13; variable = "  V. PERDIDAS ANTES DE IMPUESTOS ..........................."; |FINSI;
|SI nume1 = 14; variable = " VI. RESULTADO DEL EJERCICIO (PERDIDAS) ...................."; |FINSI;
|FPRC;

|| ***********************************************************************
|PROCESO haztodo;
  enEmpresa = #canempre#2;
  enPeriodo = #canempre#3;
  swOperacion = 0
  |SUB_EJECUTA "camoneda.tab";

  |ABRE #3;
  |CIERRA #3;
  |DELINDEX #3;
  |ABRE #5;
  |CIERRA #5;
  |DELINDEX #5;

  |PATH_DAT #1 dirfich0;
  |PATH_DAT #2 dirfich0;
  |PATH_DAT #9 dirfich0;
  |PATH_DAT #6 dirfich0;
  |PATH_DAT #7 dirfich0;
  |ABRE #30;
  #30#2 = emp1;
  #30#3 = per1;
  |LEE 011#30=;
  anu = 1900 + #30#26;
  |SI anu [ 1970;  anu = anu + 100;  |FINSI;
  empcon = #30#1;
  ejecon = #30#26;
  |CIERRA #30;
  sw = 0;
  |PARA dos = 1;  |SI dos [ 2;  |HACIENDO dos = dos + 1;
        acumula = 8;
        acumul2 = 11;
        activo = 0;
        pasivo = 0;
        explo = 0;
        debe1  = 0;
        debe2  = 0;
        debe3  = 0;
        debe4  = 0;
        haber1 = 0;
        haber2 = 0;
        haber3 = 0;
        haber4 = 0;
        |SI dos = 2;  |Y #0#27 = "N";
            |SAL;
        |FINSI;
        |SI dos = 2;
            acumula = 10;
            acumul2 = 12;
            |PATH_DAT #1 dirfich1;
            |PATH_DAT #2 dirfich1;
            |PATH_DAT #9 dirfich1;
            |PATH_DAT #6 dirfich1;
            |PATH_DAT #7 dirfich1;
            |ABRE #30;
            #30#2 = emp2;
            #30#3 = per2;
            |LEE 011#30=;
            anu1 = 1900 + #30#26;
            |SI anu1 [ 1970;  anu1 = anu1 + 100;  |FINSI;
            |CIERRA #30;
        |FINSI;
||... Modificar Cuentas a balance segun saldo
  |SI dos = 1;
      r_emp = emp1;
      r_per = per1;
  |SINO;
      r_emp = emp2;
      r_per = per2;
  |FINSI;
  eldesde = (#0#0 * 3) + 11;
  elhasta = (#0#1 * 3) + 11;
  |SUB_EJECUTA "carecsig.run";
  eldesde = (#0#0 * 3) + 9;
  elhasta = (#0#1 * 3) + 9;
|| ---------------------------------------------------------------------
  dig1 = #30#11;       || desde mes
  dig3 = #30#13;       || nro. niveles
  dig4 = #30#14;       || 1 nivel
  dig5 = #30#15;       || 2 nivel
  dig6 = #30#16;       || 3 nivel
  dig7 = #30#17;       || 4 nivel
  dig8 = #30#18;       || 5 nivel
  dig9 = #30#19;       || 6 nivel
  |HAZ cue_per;  || calculamos la cuenta de perdidas y ganancias.

  |ABRET #1;
  |ABRE #2;
  |ABRE #9;
  |ABRE #3;
  |ABRE #5;
  |BUCLE cagetodo0;      || Bucle de Calculos cuentas
  |HAZ gr_final;         || Graba Totales

  |CIERRAT #1;
  |CIERRA #2;
  |CIERRA #9;
  |CIERRA #3;
  |CIERRA #5;
  |SI sw_error = 1; |ACABA; |FINSI;
|SIGUE; || ==================================================================
          |ABRE #30;
          #30#2 = empre;
          #30#3 = ejerc;
          |LEE 011#30=;
          |CIERRA #30;

  |ABRE #3;
  |ABRE #5;

  |HAZ cagetodo1;      || Bucle de impresion

  |CIERRA #3;
  |CIERRA #5;
  |DELINDEX #3;
  |DELINDEX #5;
|FPRC;
