|FICHEROS;
    hilcosy0 #0;       || Informe comprobacion suma y saldos
    cacuenta #1;       || Plan contable
    hilcosy1 #2;       || Fichero fantasma de impresion por pantalla
    caconimp #8;
    camaasil #9;
|FIN;

|VARIABLES;
  &balance = 1;
  &pathbal = "";
    &entrada = 1;
    potra = 0;
    lin = 0;
    sw_error = 0;
    x = 0;
    informe = "";
    men1     = "     NO EXISTE EMPRESA !";
    mes = 0;                  || Variable para saber el mes seleccionado
    letrames = "";            || Variable para poner el mes en letra
    varcamp = 0;              || Variable para posicionarse en un campo
    xxxxfiac = "  ";          || Variables para no modificable
    xxaponer = "  ";
    xrelleno = "                              ";
    error1 = "0000Nivel repetido";
    error2 = "0000No existe informe";
    error3 = "0000El numero de nivel no coincide con el numero de nivel de la empresa";
    swprint = 0;       || Variable para saber si imprimimos o no
    swsuma = 0;        || Variable para saber si sumamos o no
    nivel = 0;         || Variable que posee el nivel mas alto
    saldo = 0;         || Resta entre el debe y el haber;
    sw = 0;            || sw para nivel anterior
    nivelact = 0;      || Nivel actual
    rellenos = "";     || Variable para rellenar el hasta de la cuenta en zetas
    &i_debe = 0;       || Total del debe por registro
    &i_haber = 0;      || Total del haber por registro
    &i_deudor = 0;     || Total saldo deudor por registro
    &i_acreedor = 0;   || Total saldo acreedor por registro
    &t_debe = 0;       || Total del debe del informe
    &t_haber = 0;      || Total del haber del informe;
    &t_deudor = 0;     || Total saldo deudor por informe
    &t_acreedor = 0;   || Total saldo acreedor por informe
    &swlinea = 0;      || Pone o no linea en blanco en informe
    &swcomen = 0;
    {-1}menu  = "";           || Variables menu impresora o pantalla
        menu1 = "2400";
        menu2 = "1";
        menu3 = "Elija opcion: ";
        menu4 = "PI";
        menu5 = " Pantalla ";
        menu6 = " Impresora ";
    {-1}form  = "";           || Variables menu impresora o pantalla
        form1 = "2400";
        form2 = "1";
        form3 = "Elija formato: ";
        form4 = "12";
        form5 = " < 1 > ";
        form6 = " < 2 > ";
    op_form = 0;
    men2  = "     Longitud de Cuenta Fuera de Nivel !";
    long = "  ";
    cta  = 0;
    num   = 0;
    swdig = 0;
    digito = 0;
    menor = 0;
    para1 = 0;
    nume1 = 0;
    nume2 = 0;
    nume3 = 0;
    nume4 = 0;
    alfa1 = "";
    alfa2 = "";
    alfa3 = "";
    alfa4 = "";
    dos_ant = "xx";
    &pagina = 0;
    sw_pagina = 0;
    &aparam = "";
    &enSwOper = 0;
    &r_emp = 0;
    &r_per = 0;
    vermov = 0;

    Calc1 = 0;
    Calc2 = 0;
    Mes1  = 0;
    Mes2  = 0;
    Comodin = "";
    FecIni = @;
    FecFin = @;
    anyo = "";
    apertura = 0;
    cierre = 0;
    Cont = 0;

    &nPagDup = 0;
    &nSwPagDup = 0;
|FIN;

|INCLUYE acceso_i;
|INCLUYE digito_i;
|INCLUYE puntos_i;

|PROCESO mayus;
  #0Campo = #0Campo > " ";
  |PINTA #0Campo;
|FPRC;

|PROCESO nosaldo; |TIPO 7;
  alfa1 = "S";
  |SI #0#6 ! "N";  alfa1 = "N";  #0#22 = "S"; |FINSI;
  |CAMPO_MODIFICA #0#22, 1, alfa1;
  |PINTA #0#22;
|FPRC;

|| ***********************************************************************
||                       CALCULOS DE PANTALLA
|| ***********************************************************************

|PROCESO &c_pagina;
  |SI sw_pagina = 0;
      pagina = #0#21;
      sw_pagina = 1;

      nPagDup = #0#21;
      nSwPagDup = 1;
  |SINO;
      pagina = pagina + 1;

      |HAZ PDuplex;
  |FINSI;
|FPRC;

|PROCESO mesconta; |TIPO 0;

  varcamp = Campo + 1;                || Coge el campo del nombre del mes
  mes = #0Campo;
  |SI #0Campo ! 0;|Y #0Campo ! 13;
      mes = #30#11 + (#0Campo - 1);     || Operacion para seber que mes es
      |SI mes > 12; mes = mes - 12; |FINSI;
  |FINSI;
  |HAZ mesletra;                      || Recoge el mes en letra
  #0varcamp = letrames;
  potra = Campo;
  |PINTA #0varcamp;                   || Pinta el nombre del mes
  Campo = potra;

  |SI Campo = 4;
      |SI #0#2 > #0#4;
          |MENAV "    ATENCION!!! Error de Entrada ...";
          |ERROR;
          |ACABA; || Si el mes hasta es menor al desde
      |FINSI;
  |FINSI;

|FPRC;

|PROCESO mesletra;
  |SI mes = 0;  letrames = "Apertura  "; |FINSI;
  |SI mes = 1;  letrames = "Enero     "; |FINSI;
  |SI mes = 2;  letrames = "Febrero   "; |FINSI;
  |SI mes = 3;  letrames = "Marzo     "; |FINSI;
  |SI mes = 4;  letrames = "Abril     "; |FINSI;
  |SI mes = 5;  letrames = "Mayo      "; |FINSI;
  |SI mes = 6;  letrames = "Junio     "; |FINSI;
  |SI mes = 7;  letrames = "Julio     "; |FINSI;
  |SI mes = 8;  letrames = "Agosto    "; |FINSI;
  |SI mes = 9;  letrames = "Septiembre"; |FINSI;
  |SI mes = 10; letrames = "Octubre   "; |FINSI;
  |SI mes = 11; letrames = "Noviembre "; |FINSI;
  |SI mes = 12; letrames = "Diciembre "; |FINSI;
  |SI mes = 13; letrames = "Cierre    "; |FINSI;
|FPRC;

|PROCESO numnivel;|TIPO 0;
  |SI #0#8 > #30#13;
      |MENAV error3;
      |ERROR;
      |ACABA;
  |FINSI;
|FPRC;

|PROCESO nivel;|TIPO 7;  || Calculo para hacer no modificable
|SI Campo = 10; |Y #0#8 < 2; || Si solo hemos seleccionado
   #0Campo = 0; |PINTA #0Campo;
   |HAZ nomodifi;              || un solo nivel;
|FINSI;
|SI Campo = 11; |Y #0#8 < 3; || Si solo hemos seleccionado
   #0Campo = 0; |PINTA #0Campo;
   |HAZ nomodifi;              || dos niveles;
|FINSI;
|SI Campo = 12; |Y #0#8 < 4; || Si solo hemos seleccionado
   #0Campo = 0; |PINTA #0Campo;
   |HAZ nomodifi;              || tres niveles;
|FINSI;
|SI Campo = 13; |Y #0#8 < 5; || Si solo hemos seleccionado
   #0Campo = 0; |PINTA #0Campo;
   |HAZ nomodifi;              || cuatro niveles;
|FINSI;
|SI Campo = 14; |Y #0#8 < 6; || Si solo hemos seleccionado
   #0Campo = 0; |PINTA #0Campo;
   |HAZ nomodifi;              || cinco niveles;
|FINSI;
|FPRC;

|PROCESO nomodifi;             || RUTINA DE CONVERSION A NO MODIFICABLES
    xxaponer = Anono;
    xxxxfiac = Control % A1001;
    Control = Control + xxxxfiac;
    Control = Control + xrelleno;
    Control = Control % A120;
    Control = Control + xxaponer;
|FPRC;

|PROCESO nivelrep;|TIPO 0;  || Para comprobar que no repetimos los niveles

|SI nivel = 0;        || Buscamo el nivel mas alto;
    nivel = #0#9;
|FINSI;

|SI Campo = 10;
   |SI #0#10 = #0#9;|Y #0#10 ! 0;    || Si hay dos niveles iguales error
      |MENAV error1;
      |ERROR;
      |ACABA;
   |SINO;
       |SI #0#10 > #0#9;  || Busca el nivel mas alto
           nivel = #0#10;
       |SINO;
           nivel = #0#9;
       |FINSI;
   |FINSI;
|FINSI;

|SI Campo = 11; |Y #0#11 ! 0;
    |SI #0#11 = #0#9; |O #0#11 = #0#10; || Si hay dos niveles iguales error
       |MENAV error1;
       |ERROR;
       |ACABA;
    |FINSI;

    |SI #0#11 > nivel;  || Busca el nivel mas alto
        nivel = #0#11;
    |FINSI;
|FINSI;

|SI Campo = 12; |Y #0#12 ! 0;      || Si hay dos niveles iguales error
    |SI #0#12 = #0#9; |O #0#12 = #0#10; |O #0#12 = #0#11;
       |MENAV error1;
       |ERROR;
       |ACABA;
    |FINSI;

    |SI #0#12 > nivel;  || Busca el nivel mas alto
        nivel = #0#12;
    |FINSI;
|FINSI;

|SI Campo = 13; |Y #0#13 ! 0;     || Si hay dos niveles iguales error
    |SI #0#13 = #0#9; |O #0#13 = #0#10; |O #0#13 = #0#11; |O #0#13 = #0#12;
       |MENAV error1;
       |ERROR;
       |ACABA;
    |FINSI;

    |SI #0#13 > nivel;  || Busca el nivel mas alto
        nivel = #0#13;
    |FINSI;
|FINSI;

|SI Campo = 14; |Y #0#14 ! 0;    || Si hay dos niveles iguales error
    |SI #0#14 = #0#9; |O #0#14 = #0#10; |O #0#14 = #0#11; |O #0#14 = #0#12; |O #0#14 = #0#13;
       |MENAV error1;
       |ERROR;
       |ACABA;
    |FINSI;

    |SI #0#14 > nivel;  || Busca el nivel mas alto
        nivel = #0#14;
    |FINSI;
|FINSI;

|FPRC;

|| **********************************************************************
||                       CALCULOS DE IMPRESION
|| **********************************************************************

|PROCESO condicion;      || Calculo condiciones de impresion

  |HAZ digitos2;
  |SI d_error ! 0;  |ACABA;  |FINSI;
  swprint = 0;         || Variable para saber si imprimimos o no
  swsuma = 0;          || Variable para saber si sumamos o no
  i_debe = 0;          || Pone a cero el total debe del registro
  i_haber = 0;         || Pone a cero el total haber del registro

  |SI sw = 0;
      nivelact = #1#55;     || Nivel actual
      sw = 1;
  |FINSI;

  |SI #1#55 ! 0;     || Condicion para saber si el nivel esta seleccionado
      |SI #1#55 = #0#9;|O #1#55 = #0#10;|O #1#55 = #0#11;      || Compara 3 niveles
          swprint = 1;
      |SINO;
          |SI #1#55 = #0#12;|O #1#55 = #0#13;|O #1#55 = #0#14; || Compara 3 niveles
              swprint = 1;
          |SINO;
              swprint = 0;
          |FINSI;
      |FINSI;
  |FINSI;

|SI swprint = 1; |Y #1#55 = nivel;      || Si es el nivel mas alto entoces
        swsuma = 1;                     || se suma.
|FINSI;

|SI #1#3 = "S";|Y #1#55 [ nivel;   || Si el nivel no esta seleccionado
        swsuma = 1;                || pero tiene en el campo pase una S
        swprint = 1;               || entonces se imprime y se suma.
|FINSI;

|SI swprint = 1;    || Si se puede imprimir
    varcamp = ((#0#2 * 3) + 9);
    |SI menu2 = 1; |HAZ operapanta; |FINSI; || Si es por pantalla
    |SI menu2 = 2; |HAZ operaimpre; |FINSI; || Si es por impresora
|FINSI;
|FPRC;

|PROCESO operapanta;   || Operaciones por pantalla;
    #2#2 = 0;
    #2#3 = 0;
|SI lin > 1200;
    |HAZ cambiapag;  || Cambia de pagina
    |SI sw_error = 1; |ACABA; |FINSI;
|FINSI;

    #2#0 = #1#0;     || Cuenta
    #2#1 = #1#2;     || Descripcion

    anyo = ("00" + #48#26) % -102;
    |SI anyo ] "80";
       anyo = "19" + anyo;
    |SINO;
       anyo = "20" + anyo;
    |FINSI;
    Comodin = ("00" + #0#2) % -102;
    |SI Comodin = "00"; apertura = 1; |SINO; apertura = 0; |FINSI;
    Comodin = ("00" + #0#4) % -102;

    |SI Comodin = "13"; cierre = 1; |SINO; cierre = 0; |FINSI;
    Comodin = "01." + (("00" + #0#2) % -102) + "." + anyo;
    FecIni = Comodin;
    Comodin = "31." + (("00" + #0#4) % -102) + "." + anyo;
    FecFin = Comodin;

|PARA x = #0#2; |SI x [ #0#4; |HACIENDO x = x + 1;  || Bucle de campos
        #2#2 = #2#2 + #1varcamp;    || Suma el debe
        varcamp = varcamp + 1;      || Se situa en  el campo del haber
        #2#3 = #2#3 + #1varcamp;    || Suma el haber
        varcamp = varcamp + 2;      || Se situa en el campo debe
|SIGUE;

    |HAZ BuscaMovs;

|SI #0#7 = "S";
        #2#2 = #2#2 + #1#9;         || Suma saldo debe inicial
        #2#3 = #2#3 + #1#10;        || Suma saldo haber inicial
|FINSI;

    #2#4 = #2#2 - #2#3;             || Coge la diferencia


    FSalida = 0;

|SI vermov = 0; |Y #0#6 ! "N"; || ctas sin movimientos
    |ACABA;
|FINSI;
|SI #2#2 = #2#3; |Y #0#22 = "N"; || cuentas sin saldo
    |ACABA;
|FINSI;
|SI swsuma = 1;    || Si se puede sumar los totales
        #2#5 = #2#5 + #2#2;         || Suma el total del debe
        #2#6 = #2#6 + #2#3;         || Suma el total del haber
        #2#7 = #2#7 + #2#4;         || Suma total saldo
|FINSI;

|SI 0 [ FSalida;    || Pinta linea por pantalla
    |PINTA #2#5; |PINTA #2#6; |PINTA #2#7;  || Pinta los totales
        Pos = lin;
    |PINDA #0#2;         || Pinta las lineas
        Pos = -1;
        lin = lin + 100;     || Posicionar una linea mas abajo
|FINSI;
|FPRC;

|PROCESO cambiapag;
sw_error = 0;
lin = 100;
|PAUSA;
|SI 1 = FSalida;|O 7 = FSalida;
    |ERROR10;    || Rompe el bucle y acaba
    sw_error = 1;
|SINO;
   |PINPA #0#2;
   #2#8 = #2#5; #2#9 = #2#6; #2#10 = #2#7;   || Coge los valores
   |PINTA #2#8; |PINTA #2#9; |PINTA #2#10;   || Pinta la suma anterior
   FSalida = 0;
|FINSI;
|FPRC;

|PROCESO operaimpre;     || impresora
|PARA x = #0#2; |SI x [ #0#4; |HACIENDO x = x + 1;
        i_debe = i_debe + #1varcamp;                   || acumula debe
        varcamp = varcamp + 1;
        i_haber = i_haber + #1varcamp;                 || acumula haber
        varcamp = varcamp + 2;
|SIGUE;

|SI #0#7 = "S";                    || acumula saldo inicial
        i_debe = i_debe + #1#9;         || debe
        i_haber = i_haber + #1#10;      || haber
|FINSI;

    saldo = i_debe - i_haber;      || saldo
|SI saldo > 0; i_deudor = saldo; i_acreedor = 0;            |FINSI;
|SI saldo < 0; i_deudor = 0;     i_acreedor = saldo * (-1); |FINSI;
|SI saldo = 0; i_deudor = 0;     i_acreedor = 0;            |FINSI;

    anyo = ("00" + #48#26) % -102;
    |SI anyo ] "80";
       anyo = "19" + anyo;
    |SINO;
       anyo = "20" + anyo;
    |FINSI;
    Comodin = ("00" + #0#2) % -102;
    |SI Comodin = "00"; apertura = 1; |SINO; apertura = 0; |FINSI;
    Comodin = ("00" + #0#4) % -102;

    |SI Comodin = "13"; cierre = 1; |SINO; cierre = 0; |FINSI;
    Comodin = "01." + (("00" + #0#2) % -102) + "." + anyo;
    FecIni = Comodin;
    Comodin = "31." + (("00" + #0#4) % -102) + "." + anyo;
    FecFin = Comodin;
|HAZ BuscaMovs;

|SI vermov = 0; |Y #0#6 ! "N"; || ctas sin movimientos
    |ACABA;
|FINSI;
|SI i_debe = i_haber; |Y #0#22 = "N";    || cuentas sin saldo
    |ACABA;
|FINSI;

|SI nivelact ! #1#55;         || cambio de nivel, linea en blanco
        swlinea = 1;
        nivelact = #1#55;
|SINO;
        swlinea = 0;
|FINSI;

|SI #0#8 = 1;  |HAZ artenvas; |FINSI;   || separacion lineas formato ARTENVAS

|PRINT;
    swcomen = 1;              || linea suma anterior
|SI swsuma = 1;     || acumula totales
        t_debe = t_debe + i_debe;                 || debe
        t_haber = t_haber + i_haber;              || haber
        t_deudor = t_deudor + i_deudor;           || saldo deudor
        t_acreedor = t_acreedor + i_acreedor;     || saldo acreedor
|FINSI;
|FPRC;

|PROCESO artenvas;
    alfa1 = #1#0;
    alfa1 = alfa1 % 102;

|SI dos_ant = "xx"; dos_ant = alfa1;    |FINSI;

|SI alfa1 ! dos_ant;
        dos_ant = alfa1;
        swlinea = 1;          || cuando solo se ha seleccionado un nivel y
|SINO;                        ||/cambia en sus dos primeros digitos se
        swlinea = 0;          ||/imprime una linea en blanco!!!
|FINSI;
|FPRC;

|DEFBUCLE hilcosy00;      || plan contable (3ra. clave)
 #1#3;
 ;
     #0#0, 1;
 rellenos, 6;
 e;
 NULL, condicion;
|FIN;

|PROCESO impre;
 |MENU form;
 op_form = FSalida;
 |SI op_form > 0;
     |SI op_form = 1;
         informe = "caycosy0";
     |FINSI;
 |SINO;
     |ACABA;
 |FINSI;

 |IMPRE 0;
 |SI FSalida = 0;
     |INFOR informe;
     |SI FSalida = 0;
         vermov = 0;
         |BUCLE hilcosy00;
         |PIEINF;
         |FININF;
     |SINO;
         |MENAV error2;
     |FINSI;
 |SINO;
    |MENAV "    Proceso abortado ...";
 |FINSI;
 |FINIMP;
|FPRC;

|PROCESO panta;
    lin = 100;
|PINPA #0#2;       || Pinta la pantalla del informe
|BUCLE hilcosy00;
|PAUSA;
|FPRC;

|PROCESO borrar;
|DDEFECTO #2;
    nivel = 0;
    sw = 0;
    swcomen = 0;
    t_debe = 0;
    t_haber = 0;
    t_deudor = 0;
    t_acreedor = 0;
|FPRC;

|| ***********************************************************************
||                       CALCULO ENTRADA DE DATOS
|| ***********************************************************************

|PROCESO rellena;
    rellenos = "zzzzzzzzzzzz";
    menor = -1;

|PARA para1 = 9; |SI para1 [ 14; |HACIENDO para1 = para1 + 1;
    |SI #0para1 ! 0;
        |SI menor = -1;  menor = #0para1;  |FINSI;
        |SI menor > #0para1;  menor = #0para1;  |FINSI;
    |FINSI;                   || saca el nivel elegido mas peque¤o
|SIGUE;

|SI menor ! 0;
    |SI menor = 1;  nume1 = dig4;  |FINSI;
    |SI menor = 2;  nume1 = dig5;  |FINSI;   || construye el hasta segun
    |SI menor = 3;  nume1 = dig6;  |FINSI;   ||/el nivel mas bajo elegido
    |SI menor = 4;  nume1 = dig7;  |FINSI;
    |SI menor = 5;  nume1 = dig8;  |FINSI;
    |SI menor = 6;  nume1 = dig9;  |FINSI;
        nume1 = 100 + nume1;
        rellenos = #0#1 % nume1;
        rellenos = rellenos + "zzzzzzzzzzzz";
        rellenos = rellenos % 112;
|FINSI;
|FPRC;

|PROGRAMA;
  |ABRE #8;
  |LEE 111#8p;
  |SI #8#2 = "S";  |Y #8#3 = "N";  |Y aparam = "";
      |DFICE pathbal;
      balance = 1;
      |CIERRA #8;
      |SUB_EJECUTA "careccue.tab";
  |FINSI;
  |CIERRA #8;
  |HAZ inicio;

      enSwOper = 1;
      r_emp = empre;
      r_per = ejerc;
      |SUB_EJECUTA "carecsig";
      enSwOper = 0;

  |CLS;
  |CABEZA "Comp.Sumas y Saldos";
  |DDEFECTO #0;
  || Bucle menu
  FSalida = 1;
  |PARA; |SI 0 < FSalida; |HACIENDO;
             sw_pagina = 0;
         |PINPA #0#0;    || Pinta la pantalla del informe
         |PINDA #0#0;    || Pinta los datos por defecto del informe
         |MENU menu;
         |SI 0 < FSalida;          || Se ha elegido opcion
             menu2 = FSalida;      || Defecto de la ultima opcion elegida
             |ENDATOS #1#0;        || Seleccion informe
             |SI 0 [ FSalida;
                 |HAZ nivelrep;
                 |HAZ rellena;
                 |SI menu2 = 1; |HAZ panta; |FINSI;  || Por pantalla
                 |SI menu2 = 2; |HAZ impre; |FINSI;  || Por impresora
                 |HAZ borrar;
              |FINSI;
              FSalida = 1;
        |FINSI;
  |SIGUE;
|FPRO;

|PROCESO cuenta; |TIPO 6;
    p_alfa1 = #0Campo;
    salidas = FSalida;
|HAZ punto;
|SI p_error ! 0;  |ERROR;  |ACABA;  |FINSI;
|SI #0Campo = "            ";  |ERROR;  |ACABA;  |FINSI;
    digito = Campo + 19;
    cta  = #0Campo;
    long = cta % 0;
    swdig = 0;
|SI long = #30#14; swdig = 1; #0digito = 1; |FINSI;
|SI long = #30#15; swdig = 1; #0digito = 2; |FINSI;
|SI long = #30#16; swdig = 1; #0digito = 3; |FINSI;
|SI long = #30#17; swdig = 1; #0digito = 4; |FINSI;
|SI long = #30#18; swdig = 1; #0digito = 5; |FINSI;
|SI long = #30#19; swdig = 1; #0digito = 6; |FINSI;
|SI swdig = 0;
    |MENAV men2;
    |ERROR;
|FINSI;
|FPRC;

|PROCESO BuscaMovs;
   |SI #0#6 = "S"; |O #0#6 = "A";
      Comodin = FecIni % 0402; Mes1 = Comodin;
      Comodin = FecFin % 0402; Mes2 = Comodin;

      Calc1 = 9 + (Mes1 * 3);
      Calc2 = 9 + (Mes2 * 3);
      vermov = 0;
      |PARA Cont = Calc1; |SI Cont [ Calc2; |HACIENDO Cont = Cont + 1;
         |SI #cacuenta#Cont# ! 0; vermov = 1; |SAL; |FINSI;
         Cont = Cont + 1;
         |SI #cacuenta#Cont# ! 0; vermov = 1; |SAL; |FINSI;
         Cont = Cont - 1;
      |SIGUE;
   |FINSI;

   |SI #0#6 = "M"; |O #0#6 = "A";
      |SI menu2 = 1;
         |SI #cacuenta#3 = "N";
            |SI #2#2 = 0; |Y #2#3 = 0; vermov = 0; |SINO; vermov = 1; |FINSI;
            |ACABA;
         |FINSI;
      |SINO;
         |SI #cacuenta#3 = "N";
            |SI #cacuenta#51 = 0; |Y #cacuenta#52 = 0; vermov = 0; |SINO; vermov = 1; |FINSI;
            |ACABA;
         |FINSI;
      |FINSI;

      vermov = 0;
      |ABRE #camaasil;
      |SELECT #3#camaasil;
      #camaasil#4 = #cacuenta#0;
      #camaasil#5 = #cacuenta#1;
      #camaasil#1 = "01.01.1900";
      #camaasil#2 = 1;
      |LEE 010#camaasil.];
      |PARA; |SI FSalida = 0; |HACIENDO;
         |SI FSalida = 0; |Y #camaasil#4 = #cacuenta#0; |Y #camaasil#5 = #cacuenta#1;
            |SI #camaasil#0 ] 1; |Y #camaasil#0 [ 98;
               |SI FecIni [ #camaasil#1; |Y FecFin ] #camaasil#1; vermov = 1; |FINSI;
            |SINO;
               |SI #camaasil#0 = 0;  |Y apertura = 1; vermov = 1; |FINSI;
               |SI #camaasil#0 = 99; |Y cierre   = 1; vermov = 1; |FINSI;
            |FINSI;
            |SI vermov = 1; |SAL; |FINSI;
            |LEE 010#camaasil.s;
            |SI FSalida ! 0; |SAL; |FINSI;
         |SINO;
            |SAL;
         |FINSI;
      |SIGUE;
      |SELECT #1#camaasil;
      |CIERRA #camaasil;
   |FINSI;
|FPRC;
