|FICHEROS;
    caemipa1 #0;
    capagcab #1;
    capaglin #2;
    cacuenta #3;
    camanban #4;
    caclipro #5;
    cacuadre #9;

    camanpag #15;
|FIN;

|VARIABLES;
    &entrada = 1;
    &sw = 0;
    &importe = 0;
    &prov = "";
    &prov1 = "";
    &cc1 = "";
    &cc2 = "";
    &cc3 = "";
    &des = " ";
    &dni = " ";
    &dir = " ";
    &pob = " ";
    &pro = " ";
    &dia = "";
    &mes = "";
    &any = "";
    &dia_e = "";
    &dianum = 0;
    &mes_e = "";
    &any_e = "";
    &totbar = "";
    sw_act = 0;
    seleccio = 0;
    alfa1 = "";
    alfa2 = "";
    x = 0;
    informe = "";
    swpasa = 0;
    nume1 = 0;
    nume2 = 0;
    nume3 = 0;
    para1 = 0;

    impletra = "";
    Long = "";
    LongNum = 0;
    lleno = 0;
    traba = "";
    traba1 = "";
    nume = 0;
    cincue = "--------------------------------------------------------------";
    nArriba = 46;
    nAbajo = 53;
    aArriba = "146";
    aAbajo = "153";
    &letra1 = "";
    &letra2 = "";
    &enCanti = 0;
    &eaCanti = "";
|FIN;

|INCLUYE acceso_i;

|PROCESO mayor;
   alfa1 = #0Campo > " ";
   #0Campo = alfa1;
   |PINTA #0Campo;

   |SI Campo = 19;
       |SI #0Campo = "S";
           |C_M #0#12,1,"S";
       |SINO;
           |C_M #0#12,1,"N";
       |FINSI;
   |FINSI;
|FPRC;

|PROCESO cuadre; |TIPO 8;
  |HAZ inicio;
  sw_act = 0;    ||  0 = N control (Carta/Pagare) junto / 1 = S por separado
  |ABRE #9;
  |DDEFECTO #9;
  #9#0 = 1;
  |LEE 001#9=;
  |SI FSalida = 0;
      |SI #9#17 = "S";
          sw_act = 1;
      |FINSI;
  |FINSI;
  |CIERRA #9;
|FPRC;

|PROCESO tipo7; |TIPO 7;
  |SI swpasa = 1; |ACABA; |FINSI;
  |SI #9#35 [ 10; x = #9#35 + 6;  |FINSI;
  |SI #9#35 > 10; x = #9#35 + 14; |FINSI;
  #0#14 = #9#35;
  #0#15 = #9x;
  |PINTA #0#14;
  |PINTA #0#15;
  swpasa = 1;
|FPRC;

|PROCESO formato; |TIPO 0;
 |SI FSalida = 2; |ACABA; |FINSI;
 |SI #0#14 [ 10; x = #0#14 + 6;    |FINSI;
 |SI #0#14 > 10; x = #0#14 + 14;   |FINSI;
 #0#15 = #9x;
 |ATRI I;
 |PINTA #0#15;
 |ATRI 0;

 x = #0#14 + 37;
 |SI #9x ! 0;
      nArriba = #9x;   aArriba = "1" + nArriba;
 |FINSI;
 x = #0#14 + 57;
 |SI #9x ! 0;
      nAbajo = #9x;    aAbajo = "1" + nAbajo;
 |FINSI;
|FPRC;

|PROCESO informa; |TIPO 0;
  |SI FSalida ! 9;  |ACABA;  |FINSI;
  |PUSHV 1049 2178;
  |ATRI I;
  |CUADRO 10 49 21 78;
  |ATRI N;
  |PINTA 1150 ,  "01-[        ]  11-[        ]";
  |PINTA 1250 ,  "02-[        ]  12-[        ]";
  |PINTA 1350 ,  "03-[        ]  13-[        ]";
  |PINTA 1450 ,  "04-[        ]  14-[        ]";
  |PINTA 1550 ,  "05-[        ]  15-[        ]";
  |PINTA 1650 ,  "06-[        ]  16-[        ]";
  |PINTA 1750 ,  "07-[        ]  17-[        ]";
  |PINTA 1850 ,  "08-[        ]  18-[        ]";
  |PINTA 1950 ,  "09-[        ]  19-[        ]";
  |PINTA 2050 ,  "10-[        ]  20-[        ]";
  |ATRI I;
  |PINTA 1154,  #9#7;  |PINTA 1169, #9#25;
  |PINTA 1254,  #9#8;  |PINTA 1269, #9#26;
  |PINTA 1354,  #9#9;  |PINTA 1369, #9#27;
  |PINTA 1454,  #9#10; |PINTA 1469, #9#28;
  |PINTA 1554,  #9#11; |PINTA 1569, #9#29;
  |PINTA 1654,  #9#12; |PINTA 1669, #9#30;
  |PINTA 1754,  #9#13; |PINTA 1769, #9#31;
  |PINTA 1854,  #9#14; |PINTA 1869, #9#32;
  |PINTA 1954,  #9#15; |PINTA 1969, #9#33;
  |PINTA 2054,  #9#16; |PINTA 2069, #9#34;
  |ATRI 0;
  |PAUSA;
  |POPV;
  |ERROR;
|FPRC;

|| -------------------------------------------------------------------------
|PROCESO fec_emisi;
  alfa1 = #1#5;
  dia_e = alfa1 % 102;
  dianum = dia_e;
  mes_e = alfa1 % 402;
  any_e = alfa1 % -104;
  |SI mes_e = "01";  mes_e = "Enero     ";  |FINSI;
  |SI mes_e = "02";  mes_e = "Febrero   ";  |FINSI;
  |SI mes_e = "03";  mes_e = "Marzo     ";  |FINSI;
  |SI mes_e = "04";  mes_e = "Abril     ";  |FINSI;
  |SI mes_e = "05";  mes_e = "Mayo      ";  |FINSI;
  |SI mes_e = "06";  mes_e = "Junio     ";  |FINSI;
  |SI mes_e = "07";  mes_e = "Julio     ";  |FINSI;
  |SI mes_e = "08";  mes_e = "Agosto    ";  |FINSI;
  |SI mes_e = "09";  mes_e = "Septiembre";  |FINSI;
  |SI mes_e = "10";  mes_e = "Octubre   ";  |FINSI;
  |SI mes_e = "11";  mes_e = "Noviembre ";  |FINSI;
  |SI mes_e = "12";  mes_e = "Diciembre ";  |FINSI;
|FPRC;

|PROCESO fec_venci;
  alfa1 = #1#6;
  dia = alfa1 % 102;
  mes = alfa1 % 402;
  any = alfa1 % -104;
  |SI mes = "01";  mes = "Enero     ";  |FINSI;
  |SI mes = "02";  mes = "Febrero   ";  |FINSI;
  |SI mes = "03";  mes = "Marzo     ";  |FINSI;
  |SI mes = "04";  mes = "Abril     ";  |FINSI;
  |SI mes = "05";  mes = "Mayo      ";  |FINSI;
  |SI mes = "06";  mes = "Junio     ";  |FINSI;
  |SI mes = "07";  mes = "Julio     ";  |FINSI;
  |SI mes = "08";  mes = "Agosto    ";  |FINSI;
  |SI mes = "09";  mes = "Septiembre";  |FINSI;
  |SI mes = "10";  mes = "Octubre   ";  |FINSI;
  |SI mes = "11";  mes = "Noviembre ";  |FINSI;
  |SI mes = "12";  mes = "Diciembre ";  |FINSI;
|FPRC;

|PROCESO cuentas;
  des = "<CUENTA SIN DEFINIR>";
  dni = " ";
  dir = " ";
  pob = " ";
  pro = " ";
  #5#23 = "P";
  #5#10 = #1#1;
  #5#11 = #1#2;
  |LEE 001#5=;        || lee el proveedor
  |SI FSalida = 0;
      des = #5#1;
      dni = #5#9;
      dir = #5#2;
      pob = #5#6;
      |QBF pob;
      alfa1 = #5#3;  alfa1 = "00"  + alfa1;  alfa1 = alfa1 % -102;
      alfa2 = #5#4;  alfa2 = "000" + alfa2;  alfa2 = alfa2 % -103;
      pob = "(" + alfa1 + alfa2 + ") " + pob;
      pro = #5#5;
  |SINO;
      #3#0 = #1#1;   || si no esta lee la cuenta del proveedor
      #3#1 = #1#2;
      |LEE 001#3=;
      |SI FSalida = 0;  des = #3#2;  |FINSI;
  |FINSI;

  #4#10 = #1#3;   || lee el banco
  #4#11 = #1#4;
  |LEE 001#4=;
  |SI FSalida ! 0;  |DDEFECTO #4;  |FINSI;
  cc1 = #4#9 % 104;
  cc2 = #4#9 % 604;
  cc3 = #4#9;
  |QBF cc3;
  cc3 = cc3 % -102;
|FPRC;

|PROCESO emitidos;
     #15#0 = #2#2;  || libro
     #15#1 = #2#3;  || orden
     #15#2 = #2#9;  || serie.pago
     #15#3 = #2#4;  || nro.pago
     |LEE 141#15=;
     |SI FSalida = 0;
         #15#13 = "S";
         |GRABA 020#15a;
     |FINSI;
     |LIBERA #15;
|FPRC;

|PROCESO impresion;
  seleccio = 1;
  sw = 1;
  |HAZ cuentas;
  |HAZ fec_emisi;
  |HAZ fec_venci;
  importe = #1#9;
  enCanti = importe;     |HAZ ConBarras;     totbar = eaCanti;
  |HAZ pasaaletra;
  |PRINT;
|FPRC;

|PROCESO imprelin;

  |SI #0#13 ! #1#12;      |ACABA;  |FINSI;  || impresa

  |SI #0#10 ! "A";
      |SI #0#10 ! #1#07;  |ACABA;  |FINSI;  || emision
  |FINSI;
  |SI #0#11 ! "A";
      |SI #0#11 ! #1#08;  |ACABA;  |FINSI;  || vencimiento
  |FINSI;

  |INFOR informe;
  |SI FSalida ! 0;
      |PUSHV 2301 2380;
      |MENSA "2300­ATENCION! NO EXISTE FORMATO PAGARE ELEGIDO ";
      |CONFI "2400S¨Desea continuar el proceso utilizando el Formato Standard? ";
      |POPV;
      |SI FSalida = 0;
          informe = "caemipa1";
          |INFOR informe;
      |SINO;
          |ERROR10;
          seleccio = 2;
          |ACABA;
      |FINSI;
  |FINSI;

  |SI #0#19 = "S";  #1#11 = #0#12; |FINSI;
  sw = 0;
  |HAZ impresion;

  |SI sw = 1;
      |PIEINF;
      |ABRE #15;
      |BUCLELIN 2emitidos#1;
      |CIERRA #15;
      #1#12 = "S";          || impresa si
      |SI sw_act = 0; #1#10 = "S"; |FINSI;
      |GRABA 020#1a;
      #0#12 = #0#12 + 1;    || va sumando uno
  |FINSI;
  |FININF;
  |LIBERA #1;
|FPRC;

|DEFBUCLE caemipa10;
 #1#1;
 5,6,1,3,14;
 #0#0,#0#2,#0#4,#0#6,#0#8,#0#16; || Pagare,F.emi,F.vto,Prove,Banco,T.Pagare
 #0#1,#0#3,#0#5,#0#7,#0#9,#0#17;
 be;
 NULL, imprelin;
|FIN;

|PROCESO impre; |TIPO 3;

   informe = #0#15; |QBF informe;
   |SI informe [ "        ";
       informe = "caemipa1";
   |FINSI;

  |IMPRE 0;
  |SI FSalida ! 0;  |ACABA;  |FINSI;
  |ABRE #3;
  |ABRE #4;
  |SELECT #2#4;
  |ABRE #5;
  |BUCLE caemipa10;
  |SI seleccio = 0;
      |MENAV "    Seleccion vacia ...";
  |FINSI;
  |CIERRA #3;
  |CIERRA #4;
  |CIERRA #5;
  |FINIMP;
|FPRC;

|PROCESO pasaaletra;
  impletra = #1#9;
  |LETRA impletra;
  Long = impletra % 0;
  LongNum = Long;
  |SI LongNum [ nArriba;
      letra1 = impletra + cincue;
      letra1 = letra1 % aArriba;
      letra2 = cincue;
      letra2 = letra2 % aAbajo;
      |ACABA;
  |FINSI;
  |QBF impletra;
  impletra = impletra + " ";
  LongNum = LongNum + 1;
  lleno = 0;
  letra1 = "";
  letra2 = "";
  traba = "";
  |PARA nume = 1;  |SI nume [ LongNum;  |HACIENDO nume = nume + 1;
        nume1 = (nume * 100) + 1;
        Long = nume1;
        traba1 = impletra % Long;
        |SI lleno = 0;
            |SI traba1 ! " ";
                traba = traba + traba1;
            |SINO;
                |SI nume [ nArriba;
                    letra1 = letra1 + traba + " ";
                    traba = "";
                |SINO;
                    letra2 = traba + " ";
                    lleno = 1;
                    traba = "";
                |FINSI;
            |FINSI;
        |SINO;
            letra2 = letra2 + traba1;
        |FINSI;
  |SIGUE;
  letra1 = letra1 + cincue;
  letra1 = letra1 % aArriba;
  letra2 = letra2 + cincue;
  letra2 = letra2 % aAbajo;
|FPRC;
