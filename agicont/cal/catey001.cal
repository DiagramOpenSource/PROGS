|FICHEROS;
    catey001;
    catem001;
    catem002;
    catew001;

    cacuenta;
    camanrec;
    camanpag;
    camancob;

    agif114@;
    agif041@;
|FIN;

|VARIABLES;
    aAlfa      = "";
    aAlfa1     = "";
    aAlfa2     = "";
    aAlfa3     = "";
    nInkey     = 0;
    nCampo     = 0;
    nNume1     = 0;
    nNume2     = 0;
    nPara1     = 0;
    fFechaHoy  = @;
    fFecha     = @;
    informe    = "catey001";
    aFichero   = "";
    aCuenta    = "";
    aDCuenta   = "";
    aHCuenta   = "";
    aTipo      = "";

    nComer     = 0;
    nSaldo     = 0;
    nIngreso   = 0;
    nGasto     = 0;
    nSw        = 0;
    nAlguno    = 0;
    nExiste    = 0;
    nDebe      = 0;
    nHaber     = 0;

    aDef       = "";
    aDef1      = "";
    aDef2      = "";
    aPath      = "";
    aEmpresa   = "";

    aDAlfa1 = "";
    aHAlfa1 = "";
    aDAlfa2 = "";
    aHAlfa2 = "";
    aDHAlfa = "";
    nDNume1 = 0;
    nHNume1 = 0;
    nDNume2 = 0;
    nHNume2 = 0;
    nDNume3 = 0;
    nHNume3 = 0;

    aQueQuiero = "";
    aTitulo    = "";
    aNumCampos = "";
    nNumCampos = 0;
    nCampos    = 0;
    aNombCampo = "";
    aTipoCampo = "";
    aLongCampo = "";
    nLongCampo = 0;

    &SwLinea   = 0;
    &enPorCsv  = 0;
    &eaTituloL = "";
    &eaNomFic  = "";
    &eSwEspecial = 0;
|FIN;

|INCLUYE varnue_i;
|INCLUYE acceso_i;

|| *********************************************************************
|PROCESO Tipo8; |TIPO 8;
   |HAZ inicio;
   |HAZ eVarEmpresa;
|FPRC;

|CALCULO Tipo7C0; |TIPO 7;
   |HAZ LeeParametros;
   fFechaHoy = ~;
   #catey001#15 = fFechaHoy; |PINTA #catey001#15;
   #catey001#16 = fec2;      |PINTA #catey001#16;
|FCAL;

|PROCESO LeeParametros;
   nExiste = 0;
   |ABRE #catem001;
   #catem001#0 = #catey001#0;
   |LEE 041#catem001.=;
   |SI FSalida = 0;
       |PARA nCampo = 1; |SI nCampo [ 14; |HACIENDO nCampo = nCampo + 1;
            #catey001#nCampo# = #catem001#nCampo#;
            |PINTA #catey001#nCampo#;
       |SIGUE;
       #catey001#21 = #catem001#15;
       #catey001#20 = "Contabilidad";
       #catey001#19 = "";
       #catey001#22 = "";
       |SI #catey001#21 = "G";
           #catey001#20 = "Gestion";
           #catey001#19 = #catem001#16;
           nNume1 = #catem001#18; |SI nNume1 = 0; nNume1 = 2; |FINSI;
           nNume1 = - (100 + nNume1);
           aAlfa1 = ("00000" + #catem001#17) % nNume1;
           #catey001#22 = aAlfa1;
       |FINSI;
       |PINTA #catey001#20;
       |PINTA #catey001#19;
       |PINTA #catey001#22;
   |SINO;
       nExiste = 1;
   |FINSI;
   |CIERRA #catem001;
|FPRC;

|CALCULO Tipo0C0; |TIPO 6;
   |HAZ LeeParametros;
   |SI nExiste = 1;
       |MENAV "0000No existe Codigo de Parametros";
       |ERROR;
   |FINSI;
|FCAL;

|CALCULO ElPath; |TIPO 0;
    nInkey = FSalida;
    |C_M #catey001#Campo#,0,aAlfa1;
    |SI aAlfa1 = "N"; |ACABA; |FINSI;

    |SI nInkey  = 10; |O nInkey  = 11;
        |SI nInkey = 10;
            aAlfa1 = "D c:\";
            |BROWSE aAlfa1;
            aAlfa1 = aAlfa1 % 270;
        |FINSI;
        |SI nInkey = 11;
            |DBASS aAlfa1;
            aAlfa1 = aAlfa1 + "dscomer8";
        |FINSI;
        #catey001#Campo# = aAlfa1;
        |PINTA #catey001#Campo#
    |FINSI;

    aAlfa1 = #catey001#Campo#;
    |QBF aAlfa1;
    |SI aAlfa1 = "";
        |MENAV "0000Error, Introduzca un directorio";
        |ERROR;
        |ACABA;
    |FINSI;

    aAlfa2 = aAlfa1 % -101;
    |SI aAlfa2 ! "/";|Y aAlfa2 ! "\";
        aAlfa1 = aAlfa1 + "/";
        #catey001#Campo# = aAlfa1;
        |PINTA #catey001#Campo#;
    |FINSI;

    aDef =  aAlfa1 + "def/agifa041.mas";
    |CARGA_DEF aDef, agif041@;
    |SI FSalida ! 0;
        |MENAV "    Imposible encontrar aplicacion en el path indicado";
        |ERROR;
    |SINO;
        |DESCARGA_DEF #agif041@;
    |FINSI;
|FCAL;

|CALCULO Tipo0C2; |TIPO 0;

   |SI #catey001#2 = "X";
       |PARA nCampo = 3; |SI nCampo [ 14; |HACIENDO nCampo = nCampo + 1;
             |C_M #catey001#nCampo#, 1, "N";
             #catey001#nCampo# = 0;
             |PINTA #catey001#nCampo#;
       |SIGUE;
   |FINSI;

   |SI #catey001#2 = "S";
       |C_M #catey001#3, 1, "S";
       |C_M #catey001#4, 1, "S";
       |PARA nCampo = 5; |SI nCampo [ 14; |HACIENDO nCampo = nCampo + 1;
             |C_M #catey001#nCampo#, 1, "N";
             #catey001#nCampo# = 0;
             |PINTA #catey001#nCampo#;
       |SIGUE;
   |FINSI;

   |SI #catey001#2 = "N";
       |C_M #catey001#3, 1, "N"; #catey001#3 = 01; |PINTA #catey001#3;
       |C_M #catey001#4, 1, "N"; #catey001#4 = 99; |PINTA #catey001#4;

       |PARA nCampo = 5; |SI nCampo [ 14; |HACIENDO nCampo = nCampo + 1;
             |C_M #catey001#nCampo#, 1, "S";
       |SIGUE;
  |FINSI;

|FCAL;

|CALCULO NoMasRecy; |TIPO 0;
     |SI #catey001#Campo# = 0;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 14;  |HACIENDO nCampo = nCampo + 1;
               #catey001#nCampo# = 0;  |C_M #catey001#nCampo#, 1, "N";
         |SIGUE;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 14;  |HACIENDO nCampo = nCampo + 1;
               |C_M #catey001#nCampo#, 1, "S";
         |SIGUE;
     |FINSI;
|FCAL;

|CALCULO fecha; |TIPO 0;
      |SI #catey001#Campo# < fFechaHoy;
          |MENAV "0000Error. Fecha Inferior a dia de hoy";
  ||        |ERROR;
          |ACABA;
      |FINSI;
      |SI #catey001#15 > #catey001#16; |Y Campo = 16;
          |MENAV "0000Error en las Fechas";
          |ERROR;
      |FINSI;
|FCAL;
|| ***********************************************************************

|PROCESO catew001;
    eSwEspecial = 0;
    |SI #catew001#0 < #catey001#15; eSwEspecial = 1; |FINSI;

    |SI enPorCsv = 1;
         |HAZ GrabaCsv;
    |SINO;
         |PRINT;
         SwLinea = SwLinea + 1;
    |FINSI;
|FPRC;

|DEFBUCLE catew001;
  #catew001#1;
  ;
  "01.01.0000";
  #catey001#16;
  ;
  NULL, catew001;
|FIN;

|PROCESO Imprimir;

   |HAZ LeeUnidadBasico;

   enPorCsv = 0;
   |SI #catey001#17 = "E";
       enPorCsv = 1;
       |HAZ PorCsv_1;
   |SINO;
       |IMPRE 0;
       |SI FSalida ! 0;
           |MENAV "0000Proceso Interrumpido";
           |ACABA;
       |FINSI;
       |INFOR informe;
       |SI FSalida ! 0;
           |MENAV "0000No existe informe. Proceso interrumpido";
           |FINIMP;
           |ACABA;
       |FINSI;
   |FINSI;

   |SI enPorCsv = 1;
       aAlfa    = ("00000" + enEmpresa) % -105;
       eaNomFic = (#catey001#15 % 102) + (#catey001#15 % 402) + (#catey001#15 % 704);
       eaNomFic = "IT" + aAlfa + eaNomFic;
       |HAZ PorCsv_2;
   |FINSI;

   SwLinea = 0;
   |BUCLE catew001;
   |SI enPorCsv = 1;
       |HAZ PresentaDocCsv;
   |SINO;
       |SI SwLinea ! 0;
           |PIEINF;
       |FINSI;
       |FININF;
       |FININF;
   |FINSI;
|FPRC;

|| ////////////// Auxiliar
|PROCESO FechaBonita;
    aAlfa  = fFecha % 102;
    nNume1 = aAlfa;
    aAlfa  = nNume1;
    aAlfa1 = fFecha % 402;
    aAlfa2 = fFecha % -104;

    |SI aAlfa1 = "01"; aAlfa1 = " de enero ";      |FINSI;
    |SI aAlfa1 = "02"; aAlfa1 = " de febrero ";    |FINSI;
    |SI aAlfa1 = "03"; aAlfa1 = " de marzo ";      |FINSI;
    |SI aAlfa1 = "04"; aAlfa1 = " de abril ";      |FINSI;
    |SI aAlfa1 = "05"; aAlfa1 = " de mayo ";       |FINSI;
    |SI aAlfa1 = "06"; aAlfa1 = " de junio ";      |FINSI;
    |SI aAlfa1 = "07"; aAlfa1 = " de julio ";      |FINSI;
    |SI aAlfa1 = "08"; aAlfa1 = " de agosto ";     |FINSI;
    |SI aAlfa1 = "09"; aAlfa1 = " de septiembre "; |FINSI;
    |SI aAlfa1 = "10"; aAlfa1 = " de octubre ";    |FINSI;
    |SI aAlfa1 = "11"; aAlfa1 = " de noviembre ";  |FINSI;
    |SI aAlfa1 = "12"; aAlfa1 = " de diciembre ";  |FINSI;

    aAlfa = aAlfa + aAlfa1 + aAlfa2;
|FPRC;

|PROCESO GrabaAuxiliar;
   |SI fFecha < #catey001#15;
       fFecha = "01.01.0000";
   |FINSI;

   #catew001#0 = fFecha;
   |LEE 041#catew001.=;
   |SI FSalida ! 0;
       |DDEFECTO #catew001;
       #catew001#0 = fFecha;
       |HAZ FechaBonita;
       #catew001#1 = aAlfa;
       |GRABA 020#catew001.b;
   |FINSI;

   |SI fFecha = #catey001#15;
       #catew001#2 = #catey001#18;
   |FINSI;
   #catew001#3 = #catew001#3 + nIngreso;
   #catew001#4 = #catew001#4 + nGasto;
   |GRABA 020#catew001.a;
   |LIBERA #catew001;
|FPRC;

|PROCESO ElAuxiliar;
   |SI #catew001#0 = #catey001#15;
        nSaldo = #catew001#2;
   |FINSI;
   nSaldo = nSaldo + #catew001#3 - #catew001#4;
   #catew001#5 = nSaldo;
   |GRABA 020#catew001.a;
   |LIBERA #catew001;
|FPRC;

|DEFBUCLE ElAuxiliar;
   #catew001#1;
   ;
   #catey001#15;
   #catey001#16;
   ;
   NULL, ElAuxiliar;
|FIN;

|| ////////////// Pagos
|PROCESO LeePagos;
   nSw    = 0;
   nGasto = 0;
   |SI #catey001#2 ! "X";
       |SI #catey001#2 = "S";
           |SI #camanpag#11 ] #catey001#3; |Y #camanpag#11 [ #catey001#4;
               nSw = 1;
           |FINSI;
       |SINO;
           |PARA nPara1 = 5; |SI nPara1 [ 14; |HACIENDO nPara1 = nPara1 + 1;
                 |SI #catey001#nPara1# ! 0; |Y #camanpag#11 = #catey001#nPara1#;
                     nSw = 1;
                 |FINSI;
           |SIGUE;
       |FINSI;
   |FINSI;
   |SI nSw = 0; |Y #camanpag#22 ! "S";
       nGasto = (#camanpag#8 - #camanpag#23) ! 2;
   |FINSI;
   |SI nGasto ! 0;
       fFecha   = #camanpag#7;
       nIngreso = 0;
       |HAZ GrabaAuxiliar;
       nAlguno = 1;
   |FINSI;
|FPRC;

|DEFBUCLE LeePagos;
   #camanpag#3;
   ;
   "01.01.0000", "01.01.0000";
   #catey001#16, "31.12.2099";
   e;
   NULL, LeePagos;
|FIN;

|| ////////////// Cobros
|PROCESO LeeCobros;
   nSw      = 0;
   nIngreso = 0;
   |SI #catey001#2 ! "X";
       |SI #catey001#2 = "S";
           |SI #camancob#11 ] #catey001#3; |Y #camancob#11 [ #catey001#4;
               nSw = 1;
           |FINSI;
       |SINO;
           |PARA nPara1 = 5; |SI nPara1 [ 14; |HACIENDO nPara1 = nPara1 + 1;
                 |SI #catey001#nPara1# ! 0; |Y #camancob#11 = #catey001#nPara1#;
                     nSw = 1;
                 |FINSI;
           |SIGUE;
       |FINSI;
   |FINSI;
   |SI nSw = 0; |Y #camancob#23 ! "S";
       nIngreso = (#camancob#8 - #camancob#24) ! 2;
   |FINSI;
   |SI nIngreso ! 0;
       fFecha = #camancob#7;
       nGasto = 0;
       |HAZ GrabaAuxiliar;
       nAlguno = 1;
   |FINSI;
|FPRC;

|DEFBUCLE LeeCobros;
   #camancob#3;
   ;
   "01.01.0000", "01.01.0000";
   #catey001#16, "31.12.2099";
   e;
   NULL, LeeCobros;
|FIN;

|| ////////////// Cobros Comercial
|PROCESO LeeCobComer;
   nSw      = 0;
   nIngreso = 0;
   |SI #catey001#2 ! "X";
       |SI #catey001#2 = "S";
           |SI #agif114@#21 ] #catey001#3; |Y #agif114@#21 [ #catey001#4;
               nSw = 1;
           |FINSI;
       |SINO;
           |PARA nPara1 = 5; |SI nPara1 [ 14; |HACIENDO nPara1 = nPara1 + 1;
                 |SI #catey001#nPara1# ! 0; |Y #agif114@#21 = #catey001#nPara1#;
                     nSw = 1;
                 |FINSI;
           |SIGUE;
       |FINSI;
   |FINSI;
   |SI nSw = 0; |Y #agif114@#13 ! "S";
       nIngreso = (#agif114@#9 - #agif114@#19) ! 2;
   |FINSI;
   |SI nIngreso ! 0;
       fFecha = #agif114@#8;
       nGasto = 0;
       |HAZ GrabaAuxiliar;
       nAlguno = 1;
   |FINSI;
|FPRC;

|DEFBUCLE LeeCobComer_AA;
   #agif114@#1003;
   8;
   aDAlfa1, aDAlfa2, 000, "01.01.0000";
   aHAlfa1, aHAlfa2, 999, #catey001#16;
   e;
   NULL, LeeCobComer;
|FIN;

|DEFBUCLE LeeCobComer_AN;
   #agif114@#1003;
   8;
   aDAlfa1, nDNume2, 000, "01.01.0000";
   aHAlfa1, nHNume2, 999, #catey001#16;
   e;
   NULL, LeeCobComer;
|FIN;

|DEFBUCLE LeeCobComer_NN;
   #agif114@#1003;
   8;
   nDNume1, nDNume2, 000, "01.01.0000";
   nHNume1, nHNume2, 999, #catey001#16;
   e;
   NULL, LeeCobComer;
|FIN;

|DEFBUCLE LeeCobComer_NA;
   #agif114@#1003;
   8;
   nDNume1, aDAlfa2, 000, "01.01.0000";
   nHNume1, aHAlfa2, 999, #catey001#16;
   e;
   NULL, LeeCobComer;
|FIN;

|PROCESO ProDsComer;
     nSw    = 0;
     aAlfa1 = "A";
     aAlfa2 = "A";
     aQueQuiero = "|NC";
     aNumCampos = #agif114@#aQueQuiero#;
     nNumCampos = aNumCampos;
     nNumCampos = nNumCampos - 1;
     |PARA nCampos = 0;  |SI nCampos [ nNumCampos;  |HACIENDO nCampos = nCampos + 1;
           aQueQuiero = "|C" + (("000" + nCampos) % -103) + "NOMBRE";
           aNombCampo = #agif114@#aQueQuiero#;

           aQueQuiero = "|C" + (("000" + nCampos) % -103) + "TIPO";
           aTipoCampo = #agif114@#aQueQuiero#;

           aQueQuiero = "|C" + (("000" + nCampos) % -103) + "LONGITUD";
           aLongCampo = #agif114@#aQueQuiero#;
           nLongCampo = aLongCampo;

           aAlfa = aNombCampo - "SERIE";
           |SI FSalida ! 0;
               nSw    = nSw + 1;
               aAlfa1 = aTipoCampo;
               |SI aTipoCampo = "A";
                   aDAlfa1 = " " * nLongCampo;
                   aHAlfa1 = "z" * nLongCampo;
               |SINO;
                   aDAlfa1 = "0" * nLongCampo; nDNume1 = aDAlfa1;
                   aHAlfa1 = "9" * nLongCampo; nHNume1 = aHAlfa1;
               |FINSI;
           |FINSI;
           aAlfa = aNombCampo - "Factura";
           |SI FSalida ! 0;
               nSw    = nSw + 1;
               aAlfa2 = aTipoCampo;
               |SI aTipoCampo = "A";
                   aDAlfa2 = " " * nLongCampo;
                   aHAlfa2 = "z" * nLongCampo;
               |SINO;
                   aDAlfa2 = "0" * nLongCampo; nDNume2 = aDAlfa2;
                   aHAlfa2 = "9" * nLongCampo; nHNume2 = aHAlfa2;
               |FINSI;
          |FINSI;
          |SI nSw = 2; |SAL; |FINSI;
     |SIGUE;
     |SI aAlfa1 = "A"; |Y aAlfa2 = "A"; |BUCLE LeeCobComer_AA;  |FINSI;
     |SI aAlfa1 = "A"; |Y aAlfa2 = "N"; |BUCLE LeeCobComer_AN;  |FINSI;
     |SI aAlfa1 = "N"; |Y aAlfa2 = "N"; |BUCLE LeeCobComer_NN;  |FINSI;
     |SI aAlfa1 = "N"; |Y aAlfa2 = "A"; |BUCLE LeeCobComer_NA;  |FINSI;
|FPRC;

|| ////////////// Saldo Cuentas
|PROCESO LasCuentas;
   nDebe  = nDebe  + (#cacuenta#51 - #cacuenta#48);
   nHaber = nHaber + (#cacuenta#52 - #cacuenta#49);
   nSaldo = nDebe - nHaber;
   |SI #catem002#3 = "D";
       #catey001#18 = #catey001#18 + (#cacuenta#53 - #cacuenta#50);
   |SINO;
       #catey001#18 = #catey001#18 - (#cacuenta#53 - #cacuenta#50);
   |FINSI;
|FPRC;

|DEFBUCLE LasCuentas;
    #cacuenta#1;
    3;
    aDCuenta, 1, "S";
    aHCuenta, 9, "S";
    ;
    NULL, LasCuentas;
|FIN;

|PROCESO LeeCatem002;
    aAlfa = #catem002#2 - "*";
    |SI FSalida = 0;
        aCuenta = #catem002#2;
    |SINO;
        aCuenta = aAlfa;
    |FINSI;
    |QBF aCuenta;
    aDCuenta = (aCuenta + "            ") % 112;
    aHCuenta = (aCuenta + "zzzzzzzzzzzz") % 112;

    |BUCLE LasCuentas;
|FPRC;

|DEFBUCLE LeeCatem002;
    #catem002#1;
    ;
    #catey001#0, 001;
    #catey001#0, 999;
    e;
    NULL, LeeCatem002;
|FIN;

|PROCESO Descarga;
    |SI aDef2 ! ""; |DESCARGA_DEF #agif114@; |FINSI;
    |SI aDef1 ! ""; |DESCARGA_DEF #agif041@; |FINSI;
|FPRC;

|PROCESO Tipo3; |TIPO 3;

     nComer = 0;
     |SI #catey001#21 = "G";
         nComer = 1;
         aAlfa1 = #catey001#19; |QBF aAlfa1;
         |SI aAlfa1 = "";
             |MENAV "0000Directorio erroneo";
             |ACABA;
         |FINSI;

         aDef1 =  aAlfa1 + "def/agifa041.mas";
         |CARGA_DEF aDef1, agif041@;
         |SI FSalida ! 0;
             aDef1 = "";
             |MENAV "0000NO se ha podido leer el agifa041";
             |HAZ Descarga;
             |ACABA;
         |FINSI;
         aDef2 =  aAlfa1 + "def/agifa114.mas";
         |CARGA_DEF aDef2, agif114@;
         |SI FSalida ! 0;
             aDef2 = "";
             |MENAV "0000NO se ha podido leer el agifa114";
             |HAZ Descarga;
             |ACABA;
         |FINSI;
         aEmpresa = #catey001#22; |QBF aEmpresa;
         aPath = aAlfa1 + "fich/" + aEmpresa + "/";
         |PATH_DAT #agif114@#aPath#;
     |FINSI;

     aFichero = "w" + Usuario;
     |NOME_DAT #catew001#aFichero#;
     |DELINDEX #catew001;

     nDebe  = 0;
     nHaber = 0;
     nSaldo = 0;
     #catey001#18 = 0;

     |BUCLE LeeCatem002;  ||Calcula el saldo de las cuentas elegidas

     nAlguno = 0;
     |ABRE #catew001;

     eaTituloL = #catey001#1;
     fFecha   = #catey001#15;
     nIngreso = 0;
     nGasto   = 0;
     |HAZ GrabaAuxiliar;

     |SI nComer = 0;
         |BUCLE LeeCobros;
     |SINO;
         |HAZ ProDsComer;
     |FINSI;

     |BUCLE LeePagos;

     |CIERRA #catew001;

     |SI nAlguno = 0;
         |MENAV "0000No existen datos segun la seleccion";
         |ACABA;
     |FINSI;

     nSaldo = 0;
     |BUCLE ElAuxiliar;  || recalculo de saldos final dia
/*
     |ABRE #catew001;
     |CONSULTA_CLAVE #1#catew001;
     |CIERRA #catew001;
*/
     |HAZ Imprimir;

     |DELINDEX #catew001;
|FPRC;
