|FICHEROS;
     hogarz03;
     caivarep;
     caivasop;
|FIN;

|VARIABLES;
     aEmp                = "";
     aLib                = "";
     aSer                = "";
     aFac                = "";
     aFic                = "";
     aAbre               = "";
     aBase               = "";
     aBass               = "";
     aPathPDF            = "";
     aPathPaq            = "";
     aPathTar            = "";
     aPaq                = "";
     aHora               = "";
     aFicTar             = "";
     aBorra              = "";
     aOri                = "";
     aDes                = "";
     aIPRemoto           = "";
     aAlfa               = "";

     nPas                = 0;
     nOk                 = 0;
     nSwTgzOk            = 0;

     fFecha              = @;

     &eaUnidadBasico     = "";
|FIN;

|PROCESO BorraCarpeta;
     aFic = aPathPDF + "*.pdf";
     |_PDIR aFic;
     |PARA;  |SI FSalida = 0;  |HACIENDO;
          |FBORRA aFic;

          |_SDIR aFic;
     |SIGUE;

     aAlfa = aBass + "bin/agirm -r " + aPathPDF;

     |SYSTEM aAlfa;
|FPRC;

|PROCESO Paquete;
     nSwTgzOk = 0;
     aFicTar  = aPathTar + aPaq + ".tar" + " " + aPaq + "/*";

     |ATAR aFicTar, aPathTar;
     |SI FSalida = 0;
          |PULSATECLA;
          aFicTar = aPathTar + aPaq + ".tar";
          |COMPRIME aFicTar;
          |SI FSalida = 0;
               aBorra = aPathTar + aPaq + ".tar";
               |FBORRA aBorra;

               nSwTgzOk = 1;
          |FINSI;
     |FINSI;

     |SI nSwTgzOk = 1;
         |PULSATECLA;

         aOri = aPathTar + aPaq + ".tgz";
         aDes = aPathPaq + aPaq + ".tgz";  |QBT aDes;

         aIPRemoto   = "";
         |IP_REMOTO aIPRemoto;

         |SI aIPRemoto ! "";
             |ENVIA_FICHERO aOri, aDes;
         |SINO;
             |COPIA_FICHERO aOri, aDes;
         |FINSI;

         |FBORRA aOri;
     |FINSI;

     |PULSATECLA;
     |HAZ BorraCarpeta;
     |PULSATECLA;
|FPRC;

|PROCESO caivarep;
     aEmp = (("00000" + #48#2) % -105) + #48#3;
     aSer = #caivarep#27;  |QBF aSer;
     aSer = ("__" + aSer) % -102;
     aFac = #caivarep#2;  |QBF aFac;
     aFac = ("000000" + aFac) % -106;
     aFic = aEmp + aSer + aFac + ".pdf";

     aAbre = aBass + "hogareur/hogardoc/" + PARAMETRO + aFic;
     |XABRE "E", aAbre, 0;
     |SI FSalida < 0;
         |SI nPas = 0;
             nOk = 1;
             |ERROR10;
         |FINSI;
     |FINSI;

     |SI nPas = 0;
         |ACABA;
     |FINSI;

     aOri = aAbre;
     aDes = aPathPDF + PARAMETRO + aFic;
     |COPIA_FICHERO aOri, aDes;

     nOk = 1;
|FPRC;

|DEFBUCLE caivarep;
     #caivarep#1;
     3;
     #hogarz03#0, #hogarz03#2, #hogarz03#4, #hogarz03#6;
     #hogarz03#1, #hogarz03#3, #hogarz03#5, #hogarz03#7;
     ;
     NULL, caivarep;
|FIN;

|PROCESO caivasop;
     aEmp = (("00000" + #48#2) % -105) + #48#3;
     aLib = ("00" + #caivasop#0) % -102;
     aSer = #caivasop#27;  |QBF aSer;
     aSer = ("__" + aSer) % -102;
     aFac = #caivasop#2;  |QBF aFac;
     aFac = ("000000" + aFac) % -106;
     aFic = aEmp + aLib + aSer + aFac + ".pdf";

     aAbre = aBass + "hogareur/hogardoc/" + PARAMETRO + aFic;
     |XABRE "E", aAbre, 0;
     |SI FSalida < 0;
         |SI nPas = 0;
             nOk = 1;
             |ERROR10;
         |FINSI;
     |FINSI;

     |SI nPas = 0;
         |ACABA;
     |FINSI;

     aOri = aAbre;
     aDes = aPathPDF + PARAMETRO + aFic;
     |COPIA_FICHERO aOri, aDes;

     nOk = 1;
|FPRC;

|DEFBUCLE caivasop;
     #caivasop#1;
     3;
     #hogarz03#0, #hogarz03#2, #hogarz03#4, #hogarz03#6;
     #hogarz03#1, #hogarz03#3, #hogarz03#5, #hogarz03#7;
     ;
     NULL, caivasop;
|FIN;

|PROCESO T12;  |TIPO 12;
     |HAZ LeeUnidadBasico;

     |DBASS aBass;
     |DBASE aBase;

     fFecha = ~;
     |HORA aHora;

     |SI PARAMETRO = "VG";  aPaq = "Ventas";   |FINSI;
     |SI PARAMETRO = "CC";  aPaq = "Compras";  |FINSI;

     aPaq   = aPaq + (fFecha % -104) + (fFecha % 402) + (fFecha % 102);
     aPaq   = aPaq + (aHora % 102) + (aHora % 402) + (aHora % 702);

     aPathPDF = aBase + "/tmp";                         |MKDIR aPathPDF;
     aPathPDF = aBase + "/tmp/paquetes";                |MKDIR aPathPDF;
     aPathPDF = aBase + "/tmp/paquetes/";               |MKDIR aPathPDF;
     aPathPDF = aBase + "/tmp/paquetes/" + aPaq;        |MKDIR aPathPDF;
     aPathPDF = aBase + "/tmp/paquetes/" + aPaq + "/";

     aPathTar = aBase + "/tmp/paquetes/";

     aPathPaq = eaUnidadBasico + "\documentos";                    |RMKDIR aPathPaq;
     aPathPaq = eaUnidadBasico + "\documentos\hogartex";           |RMKDIR aPathPaq;
     aPathPaq = eaUnidadBasico + "\documentos\hogartex\paquetes";  |RMKDIR aPathPaq;
     aPathPaq = eaUnidadBasico + "\documentos\hogartex\paquetes\";

     nPas = 0;
     nOk  = 0;
     |SI PARAMETRO = "VG";  |BUCLE caivarep;  |FINSI;
     |SI PARAMETRO = "CC";  |BUCLE caivasop;  |FINSI;

     |SI nOk = 1;
         |MENAV "0000Hay registros de la seleccion que no tienen PDF asociados";
         |CONFI "0000NQuiere continuar de todas formas";
         |SI FSalida ! 0;  |ACABA;  |FINSI;
     |FINSI;

     |PUSHV 0501 2380;

     |BLANCO 0501 2380;
     |CUADRO 0509 0870;

     |PINTA 0610, "                    Procesando empaquetado                 ";
     |PINTA 0710, " Esto puede tardar varios minutos, segun el volumen de PDFs";

     nPas = 1;
     nOk  = 0;
     |SI PARAMETRO = "VG";  |BUCLE caivarep;  |FINSI;
     |SI PARAMETRO = "CC";  |BUCLE caivasop;  |FINSI;

     |SI nOk = 0;
         |MENAV "0000Seleccion vacia";
         |ACABA;
     |FINSI;

     |HAZ Paquete;

     |POPV;

     |XABRE "EC", aDes, 0;
     |SI FSalida ] 0;
         |PUSHV 0501 2380;
         |BLANCO 0501 2380;
         |CUADRO 1202 1578;

         |PINTA 1303, "Generado. La ubicaci¢n y nombre del paquete es la siguiente:";
         |PINTA 1406, aDes;

         |PAUSA;
         |POPV;
     |SINO;
         |MENAV "0000Ha habido un error a generar el paquete. Vuelva a intentarlo.";
     |FINSI;
|FPRC;
