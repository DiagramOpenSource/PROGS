|FICHEROS;
  camac349 #0;
  camal349 #1;
  eosclipr,MANTE;

  caclipro;
  agipobla;
  agiprovi;
  agim0016;

  canempre;
  agicontr;
|FIN;

|VARIABLES;
  pepe = 0;
  mod       = 0;
  men1      = "0000SEs correcto S/N : ";
  men6      = "    NConfirma el Alta en Fichero General (S/N): ";
  verrfec   = "0000ATENCION !! Fecha erronea o formato incorrecto ";
  vdia1     = "";
  vme1      = "";
  vany1     = "";
  vdia      = 0;
  vme       = 0;
  vany      = 0;
  vtope     = 0;
  vsw       = 0;
  vfec      = "";
  vblanco   = "          ";
  vpuntos   = "..........";
  v         = 0;
  vacio     = "                                        ";
  cliente   = "";
  cuarenta  = "                                        ";

  letra     = "";
  aAlfa1    = "";
  final     = "";
  x         = "";
  nOk       = 0;
  nNume1    = 0;
  nNume2    = 0;
  nifnum    = 0;
  resto     = 0;
  nEjer     = 0;

  salida    = 0;
  agi       = "";
  num       = 0;
  longi     = "";

  &swentra   = 0;
  &swmod     = 0;
  &aste      = "";
  &agi1      = "";
  &agi2      = "";
  &eaVarDni  = "";
  &enCalcCif = 0;
|FIN;

|INCLUYE acceso_i;

|| **************************************************************************
|PROCESO alentrar;  |TIPO 8;
  |HAZ inicio;
  |HAZ tipo_8; || comprueba la existencia de la ficha general
  |SI aste ! "*";
      |PATH_DAT #eosclipr#agi1#;
      |PATH_DAT #agipobla#agi1#;
      |PATH_DAT #agiprovi#agi1#;
      |PATH_DAT #agim0016#agi1#;

      |PATH_PAN #eosclipr#agi2#;
      |PATH_PAN #agipobla#agi2#;
      |PATH_PAN #agiprovi#agi2#;
      |PATH_PAN #agim0016#agi2#;
  |FINSI;
|FPRC;

|PROCESO antesque;  |TIPO 7;
  |ABRE #canempre;
  #canempre#2 = enEmpresa;
  #canempre#3 = enPeriodo;
  |LEE 040#canempre.=;
  #camac349#0 = #canempre#2;
  #camac349#3 = #canempre#1;
  #camac349#2 = #canempre#26;
  |PINTA #camac349#0;
  |PINTA #camac349#2;
  |PINTA #camac349#3;
  |CIERRA #canempre;

  |SI #camac349#2 ] 20;
      |ATRI I; |PINTA 0902, "Claves E/S/M/H/R/D/C";
      |PINTA 1048, "U/Z/W/V/T/Q/P  "; |ATRI 0;
  |SINO;
       |PINTA 0902, "ÄÄÄClaves E/S/M/HÄÄÄÄ";
       |ATRI I; |PINTA 1005, "Claves E/S/M/H";
       |PINTA 1048, "Claves R/Z/W/V"; |ATRI 0;
  |FINSI;
|FPRC;

|PROCESO despuesde;
  |SI #camac349#0 ! cod1;
      |ERROR;
      #camac349#0 = cod1;
      |PINTA #camac349#0;
  |FINSI;
|FPRC;

|PROCESO mayor11;
  |SI Campo = 12;
      |SI #camac349#Campo# = "N";
          #camac349#13 = " ";
          |PINTA #camac349#13;
          |C_M #camac349#13,1,"N";
      |SINO;
          |C_M #camac349#13,1,"S";
          |SI #camac349#13 = " ";
              #camac349#13 = "I";
              |PINTA #camac349#13;
          |FINSI;
      |FINSI;
  |FINSI;
  |SI Campo = 13;
      |SI #camac349#12 = "S"; |Y #camac349#13 = " ";
          #camac349#13 = "I";
          |PINTA #camac349#13;
      |FINSI;
  |FINSI;
|FPRC;

|PROCESO Tipo0C1;  |TIPO 0;
     nEjer = 2000 + #camac349#2;
     |SI #camac349#1 ] 80;
         nEjer = 1900 + #camac349#2;
     |FINSI;

     |SI #camac349#1 = 0;
         |SI nEjer ] 2020; |O nEjer [ 2009;
             |MENAV "0000Periodo no válido a partir del ejercicio 2020";
             |ERROR;
             |ACABA;
         |FINSI;

         |C_M #camac349#16, 1, "N";
         #camac349#16 = "A";
         |PINTA #camac349#16;
         |ACABA;
     |FINSI;

     |SI nEjer [ 2009;
         |C_M #camac349#16, 1, "N";
         #camac349#16 = "T";
         |PINTA #camac349#16;
         |ACABA;
     |FINSI;

     |SI #camac349#1 [ 4;
         |C_M #camac349#16, 1, "S";
         |ACABA;
     |FINSI;

     |C_M #camac349#16, 1, "N";
     #camac349#16 = "M";
     |PINTA #camac349#16;
|FPRC;

|PROCESO Tipo7C16;  |TIPO 7;
    aAlfa1 = "0000<A> Anual; <T> Trimestral;  <M> Mensual";
    |SI nEjer ] 2020;
        aAlfa1 = "0000<T> Trimestral;  <M> Mensual";
    |FINSI;
    |MENSA aAlfa1;
|FPRC;

|PROCESO Tipo0C16;  |TIPO 0;
     |BLIN 4;
     |C_M #camac349#17, 1, "S";

     |SI nEjer ] 2020; |Y #camac349#16 = "A";
         |ERROR;
         |ACABA;
     |FINSI;

     |SI #camac349#16 ! "M";
         |C_M #camac349#17, 1, "N";
         #camac349#17 = "N";  |PINTA #camac349#17;
         |ACABA;
     |FINSI;

     |SI #camac349#1 ! 2;  |Y #camac349#1 ! 5;  |Y #camac349#1 ! 8;  |Y #camac349#1 ! 11;
         |C_M #camac349#17, 1, "N";
         #camac349#17 = "N";  |PINTA #camac349#17;
     |FINSI;
|FPRC;
|| --------------------------------------------------------------------------

|PROCESO actualiz; |TIPO 2;
   nOk = 0;
   |SI #1#2 ] 20;
       |SI #1#4 = "E";  |O #1#4 = "S"; |O #1#4 = "M"; |O #1#4 = "H";
         |O #1#4 = "R";  |O #1#4 = "D"; |O #1#4 = "C";
           nOk = 1;
       |FINSI;
   |SINO;
       |SI #1#4 = "E";  |O #1#4 = "S"; |O #1#4 = "M"; |O #1#4 = "H";
           nOk = 1;
       |FINSI;
   |FINSI;
   |SI nOk = 1;
       #camac349#6 = #camac349#6 +. 1;
       #camac349#7 = #camac349#7 +. #1#8;
   |FINSI;

   |SI #1#4 = "A";  |O #1#4 = "I";
       #camac349#8 = #camac349#8 +. 1;
       #camac349#9 = #camac349#9 +. #1#8;
   |FINSI;

   nOk = 0;
   |SI #1#2 ] 20;
       |SI #1#4 = "U";  |O #1#4 = "Z"; |O #1#4 = "W";  |O #1#4 = "V";
          |O #1#4 = "T";  |O #1#4 = "Q"; |O #1#4 = "P";
           nOk = 1;
       |FINSI;
   |SINO;
       |SI #1#4 = "R";  |O #1#4 = "Z"; |O #1#4 = "W";  |O #1#4 = "V";
           nOk = 1;
       |FINSI;
   |FINSI;
   |SI nOk = 1;
       #camac349#10 = #camac349#10 +. 1;
       #camac349#14 = #camac349#14 +. #1#8;
   |FINSI;

   |SI #1#4 = "X";  |O #1#4 = "Y";
       #camac349#11 = #camac349#11 +. 1;
       #camac349#15 = #camac349#15 +. #1#8;
   |FINSI;

   |PINTA #camac349#6;  |PINTA #camac349#7;
   |PINTA #camac349#8;  |PINTA #camac349#9;
   |PINTA #camac349#10; |PINTA #camac349#11;
   |PINTA #camac349#14; |PINTA #camac349#15;
|FPRC;

|CALCULO NoBarra; |TIPO 5;
   FSalida = "SINBARRA";
|FCAL;

|PROCESO cuapais;
 |PUSHV 1110 2268;
 |D_CUADRO 1110, 2268;

 |ATRI R;
 |PINTA 1211, " ----------------- PAISES COMUNITARIOS ------------------";

 |ATRI N;
 |PINTA 1311, " DE   Alemania     FR   Francia         LT   Lituania    ";
 |PINTA 1411, " AT   Austria      EL   Grecia          LU   Luxemburgo  ";
 |PINTA 1511, " BE   Belgica      GB   Gran Breta¤a    MT   Malta       ";
 |PINTA 1611, " CY   Chipre       NL   Holanda         PL   Polonia     ";
 |PINTA 1711, " DK   Dinamarca    HU   Hungria         PT   Portugal    ";
 |PINTA 1811, " SI   Eslovenia    IT   Italia          CZ   Rep.Checa   ";
 |PINTA 1911, " EE   Estonia      IE   Irlanda         SK   Rep.Eslovaca";
 |PINTA 2011, " FI   Finlandia    LV   Letonia         SE   Suecia      ";
 |PINTA 2111, " BG   Bulgaria     RO   Rumania         HR   Croacia     ";

 |ATRI I;
 |PINTA 1312 , "DE"; |PINTA 1330 , "FR"; |PINTA 1351 , "LT";
 |PINTA 1412 , "AT"; |PINTA 1430 , "EL"; |PINTA 1451 , "LU";
 |PINTA 1512 , "BE"; |PINTA 1530 , "GB"; |PINTA 1551 , "MT";
 |PINTA 1612 , "CY"; |PINTA 1630 , "NL"; |PINTA 1651 , "PL";
 |PINTA 1712 , "DK"; |PINTA 1730 , "HU"; |PINTA 1751 , "PT";
 |PINTA 1812 , "SI"; |PINTA 1830 , "IT"; |PINTA 1851 , "CZ";
 |PINTA 1912 , "EE"; |PINTA 1930 , "IE"; |PINTA 1951 , "SK";
 |PINTA 2012 , "FI"; |PINTA 2030 , "LV"; |PINTA 2051 , "SE";
 |PINTA 2112 , "BG"; |PINTA 2130 , "RO"; |PINTA 2051 , "HR";
 |ATRI 0;

 |PAUSA;
 |POPV;
|FPRC;

|PROCESO filtro; |TIPO 0;
  salida = FSalida;
  |C_M #camal349#Campo#,0,aAlfa1;
  |SI aAlfa1 = "N"; |ACABA; |FINSI;

  |SI salida = 9;
      |HAZ cuapais;
      |ERROR;
      |ACABA;
  |FINSI;

  |SI #camal349#Campo# = "DE ";  |ACABA;  |FINSI;
  |SI #camal349#Campo# = "AT ";  |ACABA;  |FINSI;
  |SI #camal349#Campo# = "BE ";  |ACABA;  |FINSI;
  |SI #camal349#Campo# = "BG ";  |ACABA;  |FINSI;
  |SI #camal349#Campo# = "CY ";  |ACABA;  |FINSI;
  |SI #camal349#Campo# = "DK ";  |ACABA;  |FINSI;
  |SI #camal349#Campo# = "SI ";  |ACABA;  |FINSI;
  |SI #camal349#Campo# = "EE ";  |ACABA;  |FINSI;
  |SI #camal349#Campo# = "FI ";  |ACABA;  |FINSI;
  |SI #camal349#Campo# = "FR ";  |ACABA;  |FINSI;
  |SI #camal349#Campo# = "EL ";  |ACABA;  |FINSI;
  |SI #camal349#Campo# = "GB ";  |ACABA;  |FINSI;
  |SI #camal349#Campo# = "NL ";  |ACABA;  |FINSI;
  |SI #camal349#Campo# = "HU ";  |ACABA;  |FINSI;
  |SI #camal349#Campo# = "IT ";  |ACABA;  |FINSI;
  |SI #camal349#Campo# = "IE ";  |ACABA;  |FINSI;
  |SI #camal349#Campo# = "LV ";  |ACABA;  |FINSI;
  |SI #camal349#Campo# = "LT ";  |ACABA;  |FINSI;
  |SI #camal349#Campo# = "LU ";  |ACABA;  |FINSI;
  |SI #camal349#Campo# = "MT ";  |ACABA;  |FINSI;
  |SI #camal349#Campo# = "PL ";  |ACABA;  |FINSI;
  |SI #camal349#Campo# = "PT ";  |ACABA;  |FINSI;
  |SI #camal349#Campo# = "CZ ";  |ACABA;  |FINSI;
  |SI #camal349#Campo# = "SK ";  |ACABA;  |FINSI;
  |SI #camal349#Campo# = "RO ";  |ACABA;  |FINSI;
  |SI #camal349#Campo# = "SE ";  |ACABA;  |FINSI;
  |SI #camal349#Campo# = "HR ";  |ACABA;  |FINSI;

  |MENAV "     Codigo de Pais incorrecto. ";
  |ERROR;
|FPRC;

|PROCESO conficha;
  |PUSHV 0501 2380;
  |SI aste ! "*";
      |ABRE #eosclipr;
      || |PINPA #0#eosclipr;
      #eosclipr#0 = #camal349#Campo#;
      |||LEE 000#eosclipr.];
      |CONSULTA_CLAVE #1#eosclipr;
      #camal349#Campo# = #eosclipr#0;
      |CIERRA #eosclipr;
  |SINO;
      |ABRE #caclipro;
      |SELECT #4#caclipro;

      |PINPA #0#caclipro;
      #caclipro#9 = #camal349#Campo#;
      |LEE 000#caclipro.=;
      |CONSULTA #3#caclipro;
      #camal349#Campo# = #caclipro#9;

      |SELECT #1#caclipro;
      |CIERRA #caclipro;
  |FINSI;
  |POPV;
  |PINTA #camal349#Campo#;
|FPRC;

|PROCESO letra_0;
  |SI #camal349#Campo# ! "ES "; |ACABA; |FINSI;

   eaVarDni   = #camal349#Campo#;
   enCalcCif  = 1;
   |HAZ CalculoDNI;
   #camal349#Campo# = eaVarDni;
   |PINTA #camal349#Campo#;
|FPRC;

|PROCESO cliente;|TIPO 0;

   salida = FSalida;
   |SI salida = 9;  |HAZ letra_0; |FINSI;
   |SI salida = 10; |HAZ conficha; |FINSI;

   |C_M #camal349#Campo#,0,aAlfa1;
   |SI aAlfa1 = "N"; |ACABA; |FINSI;

   swmod = 0;
   nNume1 = Campo - 1;
   nNume2 = Campo + 1;
   |SI aste ! "*"; |Y #camal349#Campo# ! "               ";
       |ABRE #eosclipr;
       #eosclipr#0 = #camal349#Campo#;
       |LEE 040#eosclipr.=;
       |SI FSalida = 0;
           #camal349#nNume1# = #eosclipr#14;
           #camal349#nNume2# = #eosclipr#1;
           swmod = 1;
       |SINO;
           |HAZ altaclipr;
       |FINSI;
       |CIERRA #eosclipr;
  |FINSI;
  |SI swmod = 0;
      |ABRE #caclipro;
      |SELECT #4#caclipro;
      |DDEFECTO #caclipro;
      #caclipro#9 = #camal349#Campo#;
      |LEE 040#caclipro.=;
      |SI FSalida = 0;
          #camal349#nNume1# = #caclipro#24;
          #camal349#nNume2# = #caclipro#1;
      |FINSI;
      |SELECT #1#caclipro;
      |CIERRA #caclipro;
   |FINSI;
   |PINTA #camal349#nNume1#;
   |PINTA #camal349#nNume2#;
|FPRC;

|PROCESO Tipo0C12; |TIPO 0;
     |SI FSalida ! 10;
         |SI #1#12 ] 5;  |Y #1#12 [ 10;
             |MENAV "0000Periodo incorrecto.";
             |ERROR;
         |FINSI;

         |SI #1#12 ] 23;
             |MENAV "0000Periodo incorrecto.";
             |ERROR;
         |FINSI;

         |SI #1#12 = 0; |Y #1#11 ] 20;
             |MENAV "0000Periodo incorrecto.";
             |ERROR;
         |FINSI;
         |ACABA;
     |FINSI;

     |PUSHV 0501 2380;
     |BLANCO 0715 2147;
     |CUADRO 0715 2147;

     |PINTA 0816, "00.- Anual";
     |PINTA 0916, "                               ";
     |PINTA 1016, "01.- Primer  Trimestre         ";
     |PINTA 1116, "02.- Segundo Trimestre         ";
     |PINTA 1216, "03.- Tercero Trimestre         ";
     |PINTA 1316, "04.- Cuarto  Trimestre         ";
     |PINTA 1416, "                               ";
     |PINTA 1516, "11.- Enero      17.- Julio     ";
     |PINTA 1616, "12.- Febrero    18.- Agosto    ";
     |PINTA 1716, "13.- Marzo      19.- Septiembre";
     |PINTA 1816, "14.- Abril      20.- Octubre   ";
     |PINTA 1916, "15.- Mayo       21.- Noviembre ";
     |PINTA 2016, "16.- Junio      22.- Diciembre ";

     |PAUSA;
     |POPV;

     |ERROR;
|FPRC;

|PROCESO Tipo0C4; |TIPO 0;
     |SI FSalida = 10;
         |PUSHV 0501 2380;
         |SI #1#2 ] 20;
             |BLANCO 0808 1879;
             |CUADRO 0808 1879;

             |PINTA 0909, " A- Adquisiciones                              X- Rectificaciones (A) ";
             |PINTA 1009, " E- Entregas                                   U- Rectificaciones (E) ";
             |PINTA 1109, " S- Prestaciones                               Z- Rectificaciones (S) ";
             |PINTA 1209, " I- Adquisiciones en territorio impuesto       Y- Rectificaciones (I) ";
             |PINTA 1309, " M- Entregas post. importacion exenta          W- Rectificaciones (M) ";
             |PINTA 1409, " H- Entregas post.im.exenta con representante  V- Rectificaciones (H) ";
             |PINTA 1509, " R- Transferencias de bienes de consigna       T- Rectificaciones (R) ";
             |PINTA 1609, " D- Devoluciones de bienes de consigna         Q- Rectificaciones (D) ";
             |PINTA 1709, " C- Sustitucion destinatario bienes consigna   P- Rectificaciones (C) ";
             |ATRI I;
             |PINTA 0910, "A"; |PINTA 0956, "X";
             |PINTA 1010, "E"; |PINTA 1056, "U";
             |PINTA 1110, "S"; |PINTA 1156, "Z";
             |PINTA 1210, "I"; |PINTA 1256, "Y";
             |PINTA 1310, "M"; |PINTA 1356, "W";
             |PINTA 1410, "H"; |PINTA 1456, "V";
             |PINTA 1510, "R"; |PINTA 1556, "T";
             |PINTA 1610, "D"; |PINTA 1656, "Q";
             |PINTA 1710, "C"; |PINTA 1756, "P";
             |ATRI 0;
         |SINO;
             |BLANCO 0710 2075;
             |CUADRO 0710 2075;

             |PINTA 0811, "A.- Adquisiciones";
             |PINTA 0911, "X.- Rectificaciones adquisiciones";
             |PINTA 1011, "E.- Entregas";
             |PINTA 1111, "R.- Rectificaciones entregas";
             |PINTA 1211, "S.- Prestaciones";
             |PINTA 1311, "Z.- Rectificaciones Prestaciones";
             |PINTA 1411, "I.- Adquisiciones en el territorio del impuesto";
             |PINTA 1511, "Y.- Rectificaciones adquisiciones en el territorio del impuesto";
             |PINTA 1611, "M.- Entregas posteriores a importacion exenta";
             |PINTA 1711, "W.- Rectificaciones entregas posteriores a importacion exenta  ";
             |PINTA 1811, "H.- Entregas posteriores importacion exenta con representante  ";
             |PINTA 1911, "V.- Rectificaciones ent.posteriores importacion exenta con rep.";
         |FINSI;
         |PAUSA;
         |POPV;

         |ERROR;
         |ACABA;
     |FINSI;

     nOk = 0;
     |SI #1#2 ] 20;
         |SI #1#4 ! "A";  |Y #1#4 ! "X";
          |Y #1#4 ! "E";  |Y #1#4 ! "U";
          |Y #1#4 ! "S";  |Y #1#4 ! "Z";
          |Y #1#4 ! "I";  |Y #1#4 ! "Y";
          |Y #1#4 ! "M";  |Y #1#4 ! "W";
          |Y #1#4 ! "H";  |Y #1#4 ! "V";
          |Y #1#4 ! "R";  |Y #1#4 ! "T";
          |Y #1#4 ! "D";  |Y #1#4 ! "Q";
          |Y #1#4 ! "C";  |Y #1#4 ! "P";
             nOk = 1;
         |FINSI;
     |SINO;
         |SI #1#4 ! "A";  |Y #1#4 ! "X";
          |Y #1#4 ! "E";  |Y #1#4 ! "R";
          |Y #1#4 ! "S";  |Y #1#4 ! "Z";
          |Y #1#4 ! "I";  |Y #1#4 ! "Y";
          |Y #1#4 ! "M";  |Y #1#4 ! "W";
          |Y #1#4 ! "H";  |Y #1#4 ! "V";
             nOk = 1;
         |FINSI;
     |FINSI;

     |SI nOk = 1;
         |MENAV "0000Tipo erroneo. Pulse F2 para ver la ayuda.";
         |ERROR;
         |ACABA;
     |FINSI;

     |C_M #1#9,  1, "S";
     |C_M #1#10, 1, "S";
     |C_M #1#11, 1, "S";
     |C_M #1#12, 1, "S";
     |C_M #1#13, 1, "S";
     |C_M #1#16, 1, "S";

     nOk = 0;
     |SI #1#2 ] 20;
        |SI #1#4 = "E"; |O #1#4 = "M"; |O #1#4 = "H";
          |O #1#4 = "R"; |O #1#4 = "D"; |O #1#4 = "C";
             nOk = 1;
        |FINSI;
     |SINO;
        |SI #1#4 = "E"; |O #1#4 = "M"; |O #1#4 = "H";
            nOk = 1;
        |FINSI;
     |FINSI;
     |SI nOk = 1;
         |C_M #1#10, 1, "N";
         |C_M #1#11, 1, "N";
         |C_M #1#12, 1, "N";
         |C_M #1#16, 1, "N";

         #1#10 = "";
         #1#11 = 0;
         #1#12 = 0;
         #1#16 = 0;
     |FINSI;

     |SI #1#4 = "S";
         |C_M #1#10, 1, "N";
         |C_M #1#11, 1, "N";
         |C_M #1#12, 1, "N";
         |C_M #1#13, 1, "N";
         |C_M #1#16, 1, "N";

         #1#10 = "";
         #1#11 = 0;
         #1#12 = 0;
         #1#13 = "";
         #1#16 = 0;
     |FINSI;

     |SI #1#4 = "A";  |O #1#4 = "I";
         |C_M #1#10, 1, "N";
         |C_M #1#11, 1, "N";
         |C_M #1#12, 1, "N";
         |C_M #1#13, 1, "N";
         |C_M #1#16, 1, "N";

         #1#10 = "";
         #1#11 = 0;
         #1#12 = 0;
         #1#13 = "";
         #1#16 = 0;
     |FINSI;

     nOk = 0;
     |SI #1#2 ] 20;
         |SI #1#4 = "U";  |O #1#4 = "Y";  |O #1#4 = "Z";
             nOk = 1;
         |FINSI;
     |SINO;
         |SI #1#4 = "X";  |O #1#4 = "Y";  |O #1#4 = "Z";
             nOk = 1;
         |FINSI;
     |FINSI;
     |SI nOk = 1;
         |C_M #1#13, 1, "N";
         #1#13 = "";
     |FINSI;

     |PINTA #1#9;
     |PINTA #1#10;
     |PINTA #1#11;
     |PINTA #1#12;
     |PINTA #1#13;
     |PINTA #1#16;
|FPRC;

|PROCESO OtrosDatos;

     |SI #1#4 ! "C"; |Y #1#4 ! "P";
         |ACABA;
     |FINSI;

     |PUSHV 0915 1278;
     |BLANCO 0915 1278;
     |CUADRO 0915 1278;

     |ATRI R;
     |PINTA 1016, "NIF         Pais Nombre Destinatario sustituto                ";
     |ATRI 0;

     |C_V #1#17, 1, "S"; |C_M #1#17, 1, "S";
     |C_V #1#18, 1, "S"; |C_M #1#18, 1, "S";
     |C_V #1#19, 1, "S"; |C_M #1#19, 1, "S";
     |PINTA #1#17;
     |PINTA #1#18;
     |PINTA #1#19;

|ET Campo18;
     |ENCAMPO #18#1;
     |SI FSalida = 7; |VETE FinTC; |FINSI;

|ET Campo17;
     |ENCAMPO #17#1;
     |SI FSalida = 2; |VETE Campo18; |FINSI;
     |SI FSalida = 7; |VETE FinTC;   |FINSI;

     |ENCAMPO #19#1;
     |SI FSalida = 2; |VETE Campo17; |FINSI;

|ET FinTC;
     |C_V #1#17, 1, "N"; |C_M #1#17, 1, "N";
     |C_V #1#18, 1, "N"; |C_M #1#18, 1, "N";
     |C_V #1#19, 1, "N"; |C_M #1#19, 1, "N";
     |POPV;
|FPRC;

|| *************************************************************************

|PROCESO tipo_8; || control de la ficha de clientes general
  |DIRECTORIO agi;
  |QBF agi;
  |PATH_DAT #agicontr#agi#;

  |ABRE #agicontr;
  #agicontr#0 = 1;
  |LEE 040#agicontr.=;
  |SI FSalida ! 0;
      aste = "*";
      |CIERRA #agicontr;
      |ACABA;
  |FINSI;
  |CIERRA #agicontr;
  aste = #agicontr#7;
  |QBF aste;
  |SI aste  = "*"; |ACABA; |FINSI;

  agi1 = #agicontr#8; |QBF agi1;
  longi = agi1 % 0;
  num   = longi;
  num   = 100 + (num - 5);
  longi = num;

  agi2  = (agi1 % longi) + "pan/";
|FPRC;

|PROCESO altaclipr;
    |SI aste = "*"; |ACABA; |FINSI;
    |CONFI men6;
    |SI FSalida = 0;
        |PUSHV 0501 2380;
        |PINPA #0#eosclipr;
        |DDEFECTO #eosclipr;
        |PINDA #0#eosclipr;
        #eosclipr#0 = #camal349#Campo#;
        |PINTA #eosclipr#0;
        |ENDATOS #1#eosclipr;
        |SI FSalida = 0;
            |POPV;
            #camal349#nNume2# = #eosclipr#1;
            #camal349#nNume1# = #eosclipr#14;
            |PINTA #camal349#nNume2#;
            |PINTA #camal349#nNume1#;
            swmod=1;
            |GRABA 020#eosclipr.n;
        |SINO;
            |POPV;
        |FINSI;
  |FINSI;
  |LIBERA #eosclipr;
  |CIERRA #eosclipr;
|FPRC;
