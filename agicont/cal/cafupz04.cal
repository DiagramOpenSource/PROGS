|FICHEROS;
  cafupz04 #0;

  cafupm01 #1;
  cafupm02 #2;
  cafupm03 #3;
  cafupm04 #4;
  cafupm05 #5;
  cafupw03 #903;

  cacuenta #15;
  canempre #30;
  &cuentap@ #905;
  cafupw05  #999,MANTE;
|FIN;

|VARIABLES;
 seleccio      = 0;
 &nPlaEmp = 0
 &enEjercicio  = 0;
 &enAny        = 0;
 &eaNombreEmp  = "";
 &eaDirDestino = "";
 &eaDirOrigen  = "";
 &eaCuenta     = "";
 &enNivel      = 0;
 &enDef        = 0;
 &enSwLinea    = 0;
 &eaAper       = "";
 &eaExc        = "";

 nEstado  = 0;
 nInkey   = 0;

  nDefin   = 0;

  &nMaxOri = 0;
  &nMaxDes = 0;
 {-1}punt1 = 0;

  aBase    = "";
  aDef     = "";
  aAlfa1   = "";
  aAlfa2   = "";
  aFichero = "";
  nSwAlguno = 0;

  nSwError = 0;
  ddig3 = 0;
  ddig4 = 0;
  ddig5 = 0;
  ddig6 = 0;
  ddig7 = 0;
  ddig8 = 0;
  ddig9 = 0;
  nSwExis = 0;
  nPerCon = 0;
  nSwNO   = 0;
  nSw     = 0;
  informe = "cafupz04";
  nSwImp  = 0;
  nPara1  = 0;
|FIN;

|INCLUYE acceso_i;
|INCLUYE condes_i;
|INCLUYE puntos_i;

|| /////////////////////// PROCESOS DE TRABAJO \\\\\\\\\\\\\\\\\\\\
|PROCESO TrasDefin; |TIPO 0;
 |C_M #0#5, 0, aAlfa1;
 |SI aAlfa1 = "N"; |ACABA; |FINSI;

 |HAZ LeeDefinicion;
 |SI nSwError = 1;
     |ERROR;
 |FINSI;
 |SI nSwError = 2;
     |MENAV "    El Plan Contable es General, no corresponde Chequeo";
     |ERROR;
 |FINSI;
 |PINTA #0#6;
|FPRC;

|PROCESO LeeDefinicion;
 nSwError = 0;
 |ABRE #1;
 #1#0 = #0#5;
 |LEE 001#1=;
 |SI FSalida ! 0;
     nSwError = 1;
     #1#1 = "NO existe definicion";
 |SINO;
     |SI #1#12 = "G";
         nSwError = 2;
         #1#1 = "Plan Contable General";
     |FINSI;
 |FINSI;
 #0#6 = #1#1;
 |CIERRA #1;
|FPRC;

|CALCULO Todas;
|C_M #0#7,1,"S";
|C_M #0#8,1,"S";
 |SI #1#12 = "G";
     |C_M #0#7,1,"N"; #0#7 = 1;     |PINTA #0#7;
     |C_M #0#8,1,"N"; #0#8 = 99999; |PINTA #0#8;
 |FINSI;
|FCAL;

|| /////////////////////// PROCESOS DE TRABAJO \\\\\\\\\\\\\\\\\\\\
|| ------------------------------------------------------------------------

|PROCESO ElCanempre;
  nSwExis = 1;
  nPerCon = #30#3;

  |DFICB aAlfa1;
  aAlfa2 = #30#0; |QBF aAlfa2;
  aAlfa2 = ("000000" + #30#0) % -106;
  eaDirOrigen = aAlfa1 + aAlfa2 + "/";

  ddig3 = #30#13;
  ddig4 = #30#14;       || 1 nivel
  ddig5 = #30#15;       || 2 nivel
  ddig6 = #30#16;       || 3 nivel
  ddig7 = #30#17;       || 4 nivel
  ddig8 = #30#18;       || 5 nivel
  ddig9 = #30#19;       || 6 nivel
  || Recoger informacion sobre los niveles ...
  Ipunt1 = ddig3<-;
  Ipunt1 = Ipunt1 + ddig3;
  nMaxOri = punt1;

  |ERROR10;
|FPRC;

|DEFBUCLE ElCanempre;
 #30#1;
 26;
 #2#1, 0, #0#4;
 #2#1, 9, #0#4;
 ;
 NULL, ElCanempre;
|FIN;
|| ////////////////////////////////////////////////////////////////////////
|PROCESO GrabaAux;

 |SI #999#7 = "N"; |O #999#5 = "            ";
     nSwAlguno = 1;
 |SINO;
     #5#0 = #999#0;
     #5#1 = #999#1;
     #5#2 = #999#2;
     #5#3 = #999#3;
     |LEE 101#5=;
     |SI FSalida ! 0;
         #5#0 = #999#0;
         #5#1 = #999#1;
         #5#2 = #999#2;
         #5#3 = #999#3;
         |GRABA 020#5b;
     |FINSI;
     #5#4 = #999#4;
     #5#5 = #999#5;
     #5#6 = #999#6;
     |GRABA 020#5a;
     |LIBERA #5;

     |BORRA 020#999a;
 |FINSI;
|FPRC;

|DEFBUCLE GrabaAux;
 #999#1;
 ;
 #2#0, #2#1, "            ", 0;
 #2#0, #2#1, "zzzzzzzzzzzz", 9;
 ;
 NULL, GrabaAux;
|FIN;

|PROCESO Actualizar;
 |ABRE #5;
 |BUCLE GrabaAux;
 |CIERRA #5;
|FPRC;

|| ////////////////////////////////////////////////////////////////////////
|PROCESO LeeEstaCta;
     |HAZ Sacanivel;
     |ABRE #15;
     |DDEFECTO #15;
     #15#0 = eaCuenta;
     #15#1 = enNivel;
     |LEE 001#15=;
     |SI FSalida ! 0;
         #999#5 = "";
     |FINSI;
     |CIERRA #15;
|FPRC;

|| ////////////////////////////////////////////////////////////////////////
|PROCESO ImpAuxiliar;
 |PRINT;
 nSw = 1;
|FPRC;

|DEFBUCLE ImpAuxiliar;
 #999#1;
 ;
 #2#0, #2#1, "            ", 0;
 #2#0, #2#1, "zzzzzzzzzzzz", 9;
 ;
 NULL, ImpAuxiliar;
|FIN;

|PROCESO MiroExcluir;
 |SI eaCuenta ] #4#3; |Y eaCuenta [ #4#4;
     nSwNO = 1;
     |ERROR10;
 |FINSI;
|FPRC;

|DEFBUCLE MiroExcluir;
 #4#1;
 ;
 #2#0, #2#1, 001;
 #2#0, #2#1, 999;
 ;
 NULL, MiroExcluir;
|FIN;

|PROCESO LeePlan;
 aAlfa1   = #905#0; |QBF aAlfa1; |SI aAlfa1 = ""; |ACABA; |FINSI;
 eaCuenta = #905#0;
 enNivel  = #905#1;
 |SI #2#4 = "S";
     nSwNO = 0;
     |BUCLE MiroExcluir;
     |SI nSwNO = 1; |ACABA; |FINSI;
 |FINSI;

 #5#0 = #2#0;
 #5#1 = #2#1;
 #5#2 = eaCuenta;
 #5#3 = enNivel;
 |LEE 001#5=;
 |SI FSalida = 0;
     |ACABA;
 |FINSI;

 #999#0 = #2#0;
 #999#1 = #2#1;
 #999#2 = eaCuenta;
 #999#3 = enNivel;
 #999#4 = #905#2;
 #999#5 = eaCuenta;
 #999#6 = enNivel;
 #999#7 = "N";
 |HAZ LeeEstaCta;
 |GRABA 020#999n;
 nSwAlguno = 1;
|FPRC;

|DEFBUCLE LeePlan;
 #905#1002;
 3;
 "            ", 0, "S";
 "zzzzzzzzzzzz", 9, "S";
 ;
 NULL, LeePlan;
|FIN;

|PROCESO ClaveBaja999;
 #999#0 = #0#5;
 #999#1 = #2#1;
 #999#2 = "            ";
 #999#3 = 0;
|FPRC;

|PROCESO ClaveAlta999;
 #999#0 = #0#5;
 #999#1 = #2#1;
 #999#2 = "zzzzzzzzzzzz";
 #999#3 = 9;
|FPRC;

|PROCESO PorEmpresas;
  |DELINDEX #999;

  nSwExis = 0;
  |BUCLE ElCanempre;
  |SI nSwExis = 0; |ACABA; |FINSI;

  |SI nMaxDes ! nMaxOri;
      || |MENAV "    No corresponde Nivel Cuentas ";
      |ACABA;
  |FINSI;

  |PATH_DAT #905 eaDirOrigen;

  nSwAlguno = 0;
  |ABRE #5;
  |ABRE #999;
  |BUCLE LeePlan;
  |CIERRA #999;
  |CIERRA #5;

  |SI enDef ! 0; |Y nSwAlguno = 1;
      |CONFI "    SExisten cuentas que no estan informadas, modificar ";
      |SI FSalida ! 0;
          #0#9 = "N";
      |FINSI;
  |FINSI;
  |SI nSwAlguno = 1;
      |SI #0#9 = "S";
          |PUSHV 0501 2380;
          |PINPA #0#999;
          |PINTA 0726, #0#6;
          |PINTA 0926, #2#2;
          |ENTLINEAL #1#999, -3, 4, 21, 0, ClaveBaja999, ClaveAlta999;
          |CONFI "    SActualizar la informacion Introducida";
          |SI FSalida = 0;
              nSwAlguno = 0;
              |HAZ Actualizar;
          |FINSI;
      |FINSI;

      |SI enDef ! 0; |Y nSwAlguno = 1;
          |CONFI "    SObtener Listado  ";
          |SI FSalida ! 0;
              nSwAlguno = 0;
          |FINSI;
      |FINSI;
      |SI nSwAlguno = 1;
          |SI nSwImp = 0;
              nSwImp = 1;
              |IMPRE 0;
              |SI FSalida ! 0; nSwImp = 2; |ACABA; |FINSI;
              |INFOR informe;
              |SI FSalida ! 0; nSwImp = 2; |FINIMP; |ACABA; |FINSI;
          |FINSI;
          |SI nSwImp = 1;
              nSw = 0;
              |BUCLE ImpAuxiliar;
              |SI nSw = 1;
                  |PIEINF;
              |FINSI;
          |FINSI;
      |FINSI;
  |FINSI;
|FPRC;

|DEFBUCLE PorEmpresas;
  #2#1;
  ;
  #0#5, #0#7;
  #0#5, #0#8;
  e;
  NULL, PorEmpresas;
|FIN;

|PROCESO ChequeoEmp;
  |BUCLE PorEmpresas;
|FPRC;

|PROCESO ChequeoGral;
|FPRC;

|CALCULO Tipo3; |TIPO 3;

  |SI #1#12 = "G";
      |HAZ ChequeoGral;
  |SINO;
      |HAZ ChequeoEmp;
  |FINSI;
  |SI nSwImp = 1;
      |FININF;
      |FINIMP;
  |FINSI;
|FCAL;

|PROGRAMA;

 |DDEFECTO #0;
 |SI enDef = 0;
     |HAZ inicio;
     Ipunt1      = dig3<-;
     Ipunt1      = Ipunt1 + dig3;
     nMaxDes     = punt1;
     enAny       = #30#26;
     eaNombreEmp = #30#1;
     |SI enAny < 80;
         enEjercicio = 2000 + enAny;
    |SINO;
         enEjercicio = 1900 + enAny;
    |FINSI;
 |FINSI;

 |DFICE eaDirDestino;
 #0#0  = enEmpresa;
 #0#1  = enPeriodo;
 #0#2  = eaNombreEmp;
 #0#3  = enEjercicio;
 #0#4  = enAny;

 |DBASE aBase;
 aDef = aBase + "def/cacuenta.mas";
 |CARGA_DEF aDef, cuentap@;
 |SI FSalida ! 0;
     |MENAV "    Error en Cacuenta";
     |ACABA;
 |FINSI;

 aFichero = "fz4" + Usuario;
 |NOME_DAT #999 aFichero;
 |DELINDEX #999;

 |SI enDef ! 0;
     #0#5 = enDef;
     |HAZ LeeDefinicion;
     |SI nSwError = 1;
         |MENAV "    Error. No existe el codigo de la Definicion";
         |ACABA;
     |FINSI;
     #0#7 = nPlaEmp;
     #0#8 = nPlaEmp;
     |HAZ Tipo3;
 |SINO;
     |CLS;
     |CABEZA "Chequeo Cuentas Consolidacion";
     |PINPA #0#0;
     |PINDA #0#0;
     |ENDATOS #1#0;
 |FINSI;

 |DESCARGA_DEF #905;
|FPRO;
