|FICHEROS;
  ca8eqmay #0;
  ca8comay #1;
  ca8cumay #2;
  cacuenta #3;
  ca8z0002 #10;
  ca8w0001 #50,MANTE;

  canempre #30;
|FIN;

|VARIABLES;
 nInkey    = 0;
 aAlfa1    = "";
 aAlfa2    = "";
 nNumero   = 0;
 nNume1    = 0;
 nNume2    = 0;
 aDirBase  = "";
 &aPathPlan = "";
 nEstado   = 0;
 nDEmpresa = 0;
 nHEmpresa = 0;
 aParam    = "";
|FIN;

|INCLUYE varnue_i;
|INCLUYE acceso_i;

|| ******************************************** Calculos Pantalla
|CALCULO wTipo12; |TIPO 12;
|FCAL;

|CALCULO ParaCopiar; |TIPO 0;
  nInkey = FSalida;
  |SI nInkey = 1; nInkey =  -1; |ACABA; |FINSI;

  |SI nInkey = 10;
      |HAZ CopiarNum;
      |ERROR;
      |ACABA;
  |FINSI;

  |SI #10#0 = 0; |O #10#0 = 1;
      |O #10#0 = 20; |O #10#0 = 21;
         |O #10#0 = 30; |O #10#0 = 31;
         |CONFI "    NCodigo de Patron Oficial. Continuar S/N";
         |SI FSalida ! 0;
             |ERROR;
             |ACABA;
         |FINSI;
  |FINSI;

  |SI nInkey = 11;
      |CONFI "    NConfirmacion baja(S/N) ...";
      |SI FSalida = 0;
          |HAZ Borrar;
          |ERROR;
      |FINSI;
  |FINSI;
|FCAL;

|| /////////////////////// PROCESOS DE TRABAJO \\\\\\\\\\\\\\\\\\\\
|RUTINA min;
  #0#0 = #10#0;
  #0#1  = "     ";
|FRUT;

|RUTINA max;
  #0#0 = #10#0;
  #0#1 = "zzzzz";
|FRUT;

|PROCESO Tipo3;  |TIPO 3;
  |ENTLINEAL #1#0, -2, 2, 21, 0, min, max;
|FPRC;
|| **************************************************************************

|PROCESO BorroCtas;
 |BORRA 020#0a;
 |LIBERA #0;
|FPRC;

|DEFBUCLE BorroCtas;
 #0#1;
 ;
 nNumero, "     ";
 nNumero, "zzzzz";
 be;
 NULL, BorroCtas;
|FIN;

|PROCESO Borrar;
 nNumero = #10#0;
 |BUCLE BorroCtas;
|FPRC;

|| ****************** PROCESO para copiar a¤os
|PROCESO CopiarLin;
     |SALVA_FICHA #0;
     #0#0 = #50#1;
     |GRABA 020#0n;
     |REPON_FICHA #0;
     |LIBERA #0;
|FPRC;

|DEFBUCLE CopiaLineas;
     #0#1;
     ;
     #50#0, "     ";
     #50#0, "zzzzz";
     be;
     NULL, CopiarLin;
|FIN;


|PROCESO LeeDefinicion;
 |SI Campo = 1; |Y #50#1 = #50#0;
     |MENAV "    Error, origen y destino igual";
     |ERROR;
     |ACABA;
 |FINSI;

 |SI Campo = 1;
     |SI #50#1 = 0; |O #50#1 = 1;
       |O #50#1 = 20; |O #50#1 = 21;
          |O #50#1 = 30; |O #50#1 = 31;
          |CONFI "    NCodigo de Patron Oficial. Continuar S/N";
          |SI FSalida ! 0;
              |ERROR;
              |ACABA;
          |FINSI;
     |FINSI;
 |FINSI;

 |ABRE #1;
 #1#0 = #50Campo;
 |LEE 101#1=;
 |SI FSalida = 0;
     |SI Campo = 0; #50#2 = #1#1; |PINTA #50#2; |FINSI;
     |SI Campo = 1;
         #50#3 = #1#1; |PINTA #50#3;
     |FINSI;
 |SINO;
     |SI Campo = 1; #50#3 = #50#2; |PINTA #50#3; |FINSI;
     |SI Campo = 0;
         |MENAV "    No existe definicion Origen ";
         |ERROR;
     |FINSI;
 |FINSI;
 |LIBERA #1;
 |CIERRA #1;
|FPRC;

|CALCULO CopiarNum;

 |PUSHV 0809 1457;
 |PINPA #0#50;
 #50#0 = #10#0;
 #50#1 = #10#0 + 1; |SI #50#1 > 0; #50#1 = 1; |FINSI;
 #50#4 = "Tabla Equivalencias";
 |PINDA #0#50;
 |ENDATOS #1#50;
 |SI FSalida ! 0;
     |POPV;
     |ACABA;
 |FINSI;

 |ABRE #0;
 #0#0 = #50#0;
 #0#1  = "     ";
 |LEE 001#0];
 |SI FSalida ! 0; |O #0#0 ! #50#0;
     |MENAV "    Copia cancelada. No existe Tabla Origen ...";
     |POPV;
     |CIERRA #0;
     |ACABA;
 |FINSI;

 #0#0 = #50#1;
 #0#1 = "     ";
 |LEE 101#0];
 |SI FSalida = 0; |Y #0#0 = #50#1;
     |AVISO;
     |CONFI "    NRegrabar Tabla Destino YA EXISTENTE (S/N): ";
     |SI FSalida ! 0;
         |MENAV "    Copia cancelada ...";
         |POPV;
         |CIERRA #0;
         |ACABA;
     |SINO;
         |CIERRA #0;
         nNumero = #50#1;
         |BUCLE BorroCtas;
     |FINSI;
 |SINO;
     |CIERRA #0;
 |FINSI;

 |ABRE #1;
 #1#0 = #50#1;
 |LEE 101#1=;
 |SI FSalida ! 0;
     #1#1 = #50#3;
     |GRABA 020#1n;
 |SINO;
     #1#1 = #50#3;
     |GRABA 020#1a;
 |FINSI;
 |LIBERA #1;
 |CIERRA #1;

 |BUCLE CopiaLineas;
 |POPV;

 #10#0 = #50#1;
 #10#1 = #50#3;
 |PTEC 809;
 |PTEC 808;
|FCAL;

|| **************************************************************************
|PROCESO LeoEmpresa;
 |SI  #30#26 = 07;
      nNume1 = #30#2;
      nNume2 = #30#3;
      |ERROR10;
 |FINSI;
|FPRC;

|DEFBUCLE LeoEmpresa;
 #30#1;
 ;
 nDEmpresa, 0;
 nHEmpresa, 9;
 ;
 NULL, LeoEmpresa;
|FIN;

|PROGRAMA;
   enEjerBal = 2008;
   aParam = PARAMETRO; |QBF aParam;
   |SI aParam = "";
       |HAZ inicio;
       |HAZ eVarEmpresa;
   |SINO;
       enEmpresa   = 1;
       enEjercicio = 2008;
   |FINSI;

   |SI enEjercicio < 2008;
       |DFICE aPathPlan;  ||estoy en Empresa del 2007 o inferior
   |SINO;
       nNume1 = -1;
       nDEmpresa = enEmpresa;
       nHEmpresa = enEmpresa;
       |BUCLE LeoEmpresa;
       |SI nNume1 ! -1;
           nDEmpresa = 00001;
           nHEmpresa = 99999;
           |BUCLE LeoEmpresa;
       |FINSI;
       |SI nNume1 ! -1;
           aAlfa1 = nNume1; aAlfa1 = ("00000" + aAlfa1) % -105;
           aAlfa2 = nNume2; aAlfa2 = ("0"     + aAlfa2) % -101;
           |DFICB aPathPlan;
           aPathPlan = aPathPlan + aAlfa1 + aAlfa2 + "/";
       |SINO;
           |DFICE aPathPlan;

       |FINSI;
   |FINSI;

   |CLS;
   |CABEZA "Tabla Equivalencia Plan 1990 y Plan 2008";
   |PINPA #0#10;
   |PINDA #0#10;

   FSalida = 1;
   |PARA; |SI 0 < FSalida; |HACIENDO;
          |ENDATOS #1#10;
          |SI 0 [ FSalida;
              |SI nInkey = -1; |SAL; |FINSI;
              |HAZ Tipo3;
              FSalida = 1;
          |FINSI;
   |SIGUE;
|FPRO;
