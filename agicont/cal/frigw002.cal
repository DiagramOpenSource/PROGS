|FICHEROS;
     frigw002 #0;
     impfich  #1;
     cacuenta #2;
     cacuebal #5;
     caclipro #6;
     canempre #10;
     ca8m0002 #422;
|FIN;

|VARIABLES;
     aEmp     = "";
     aPeri    = "";
{100}C        = "";
     aReg1    = "";
     aFic     = "";
     aAlfa    = "";
     nHandle  = 0;
     clave    = " ";
     long     = "  ";
     errant   = 0;
     path0    = "";
     path     = "";
     patpan   = "";
     &empresa = "";
     &dig1    = 0;
     &dig3    = 0;
     &dig4    = 0;
     &dig5    = 0;
     &dig6    = 0;
     &dig7    = 0;
     &dig8    = 0;
     &dig9    = 0;
     cuini    = "            ";
     cufin    = "999999999998";
     diini    = 0;
     difin    = 9;
     fecini   = "01.01.0000";
     fecfin   = "31.12.8999";
     fini     = @;
     ffin     = @;
     cuenta   = " ";
     tifin    = "H";
     diain    = 0;
     salida   = 0;
     diafi    = 99;
     prim     = 0;
     cuant    = "";
     lon      = 0;
     aa       = 0;
     ini      = 0;
     fecalf   = "";
     mesfec   = 0;
     nomcue   = " ";
     a        = 0;
     b        = 0;
     c        = 0;
     mensa    = "";
     digito1  = 0;
     traba    = "";
     errcuent = "aaaaaaaaaaaa";
     mensaje1 = "0000Cuenta fuera de nivel !!";
     dir_fich = " ";
     anyo     = " ";
     litmes   = " ";
     mes      = 0;
     dmes     = " ";
     hmes     = " ";
     swerror  = 0;
     estado   = 0;
     estadc   = 0;
     alfa1    = "";
     alfa2    = "";
     alfa3    = "";

     tipoalta = "";
     nEstado  = 0;
     &enEjerBal = 0;
|FIN;

|INCLUYE varnue_i;
|INCLUYE defec1_i;

|PROCESO AltaFicha;
 |ABRE #6;
 |DDEFECTO #6;
 #6#23 = tipoalta;
 #6#10 = #2#0;
 #6#11 = #2#1;
 |LEE 101#6=;
 nEstado = FSalida;
 |SI nEstado = 0; |Y #0#2 = "N";
     |LIBERA #6;
     |CIERRA #6;
     |ACABA;
 |FINSI;
 |SI nEstado ! 0;
     alfa1 = #6#10; alfa1 = alfa1 % 412;
     #6#0 = alfa1;
 |FINSI;
 #6#1 = C2;
 #6#2 = C4;
 alfa1 = C7 % 102; #6#3 = alfa1;
 alfa1 = C7 % 303; #6#4 = alfa1;
 #6#5 = C6;
 #6#6 = C5;
 #6#9 = C3;
 #6#10 = #2#0;
 #6#11 = #2#1;
 #6#23 = tipoalta;
 #6#25 = C7;
 |SI nEstado = 0; |GRABA 020#6a; |FINSI;
 |SI nEstado ! 0; |GRABA 020#6n; |FINSI;
 |LIBERA #6;
 |CIERRA #6;
|FPRC;

|PROCESO PasaCuenta;
     |ATRI I;
     |PINTA 1721, C1;
     |PINTA 1821, C2;
     |ATRI 0;

     cuenta = C1;
     nomcue = C2;
     |QBF cuenta
     |SI cuenta = ""; |ACABA; |FINSI;  || No hay Cuenta

     long = cuenta % 0;
     digito1 = 0;
     |SI long = dig4; digito1 = 1; |FINSI;
     |SI long = dig5; digito1 = 2; |FINSI;
     |SI long = dig6; digito1 = 3; |FINSI;
     |SI long = dig7; digito1 = 4; |FINSI;
     |SI long = dig8; digito1 = 5; |FINSI;
     |SI long = dig9; digito1 = 6; |FINSI;

     |SI digito1 = 0;
         aAlfa = "0000Cuenta [" + cuenta + "] fuera de nivel.";
         |MENAV aAlfa;
         |ACABA;
     |FINSI;

     |DDEFECTO #2;

     #2#0  = cuenta; ||Cod.Cuenta;
     #2#1  = digito1;||Nivel de la cuenta;
     |LEE 000#2=;
     estado = FSalida;
     |SI estado = 0; |Y #0#2 = "N";
         |ACABA;
     |FINSI;

     #2#2  = nomcue; ||Descripcion cuenta;
     #2#3  = "S";    ||Pase directo;

     |SI estado ! 0;
         |HAZ defect1;
         |GRABA 020#2n;
     |SINO;
         |GRABA 020#2a;
     |FINSI;
     |LIBERA #2;
     alfa1 = #2#0; |QBF alfa1;
     alfa2 = alfa1 % 102;
     tipoalta = "";
     |SI alfa2 = "43"; |O alfa2 = "44";
         tipoalta = "C";
     |FINSI;
     |SI alfa2 = "40"; |O alfa2 = "41";  |O alfa2 = "52";
         tipoalta = "P";
     |FINSI;
     |SI tipoalta ! "";
         |HAZ AltaFicha;
     |FINSI;
|FPRC;

|PROCESO mes;
 |SI mes = 01;litmes = "Enero";     |FINSI;
 |SI mes = 02;litmes = "Febrero";   |FINSI;
 |SI mes = 03;litmes = "Marzo";     |FINSI;
 |SI mes = 04;litmes = "Abril";     |FINSI;
 |SI mes = 05;litmes = "Mayo";      |FINSI;
 |SI mes = 06;litmes = "Junio";     |FINSI;
 |SI mes = 07;litmes = "Julio";     |FINSI;
 |SI mes = 08;litmes = "Agosto";    |FINSI;
 |SI mes = 09;litmes = "Septiembre";|FINSI;
 |SI mes = 10;litmes = "Octubre";   |FINSI;
 |SI mes = 11;litmes = "Noviembre"; |FINSI;
 |SI mes = 12;litmes = "Diciembre"; |FINSI;
|FPRC;

|PROCESO RegistrosCta;
     C1 = aReg1 % 0112; ||Subcuenta
     C2 = aReg1 % 1340; ||Nombre
     C3 = aReg1 % 5315; ||NIF
     C4 = aReg1 % 6835; ||Domicilio
     C5 = aReg1 % 10325;  ||Poblacion
     C6 = aReg1 % 12820;  ||Provincia
     C7 = aReg1 % 14805;  ||Codigo Postal
     C8 = aReg1 % 15301;  ||Interno
     C9 = aReg1 % 15405;  ||Divisa
     C10 = aReg1 % 15901; ||Uso
|FPRC;

|PROCESO acceder;
     |DFICE aAlfa;
     aEmp = aAlfa % -305;
     aPeri = aAlfa % -201;
     |DBASE aAlfa;
     aAlfa = aAlfa + "fich/";
     |PATH_DAT #10 aAlfa;

     |ABRE #10;
     #10#2 = aEmp;
     #10#3 = aPeri;
     |LEE 020#10=;
     |CIERRA #10;

     dig1 = #10#11;
     dig3 = #10#13;
     dig4 = #10#14;
     dig5 = #10#15;
     dig6 = #10#16;
     dig7 = #10#17;
     dig8 = #10#18;
     dig9 = #10#19;
     mes = #10#11;
     |HAZ mes;
     dmes = litmes;
     mes = #10#12;
     |HAZ mes;
     hmes = litmes;

     |SI #10#26 < 80;
         enEjercicio = 2000 + #10#26;
     |SINO;
         enEjercicio = 1900 + #10#26;
     |FINSI;
|FPRC;

|PROCESO Tipo3; |TIPO 3;
     |HAZ acceder;

     |ABRE #1;
     #1#0 = #0#1;
     |LEE 020#1=;
     |CIERRA #1;

     aFic = #1#2; |QBF aFic;
     |XABRE "U", aFic, nHandle;
     |SI FSalida < 0;
          |XABRE "CU", aFic, nHandle;
     |FINSI;
     |SI FSalida < 0;
          aAlfa = "0000Error al abrir:" + aFic;
          |MENAV aAlfa;
     |SINO;
          |ABRE #2;
          |PARA; |SI; |HACIENDO;
               |XLEE nHandle, 250, aReg1;
               |SI FSalida < 12; |SAL; |FINSI;
               |HAZ RegistrosCta;
               |HAZ PasaCuenta;
          |SIGUE;
          |CIERRA #2;
          |XCIERRA nHandle;
     |FINSI;
|FPRC;

|PROGRAMA;
     |ABRE #0;
     |LEE 101#0p;
     |SI FSalida ! 0; |GRABA 020#0b; |FINSI;
     |CLS;
     |PINPA #0#0;
     |PINDA #0#0;
     |ENDATOS #1#0;
     |SI FSalida = 0;
          |GRABA 020#0a;
     |FINSI;
     |CIERRA #0;
|FPRO;
