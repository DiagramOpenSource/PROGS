|FICHEROS;
   caemrcc2 #0;

   caemrctm #1,MANTE;
   cacuenta #2;
   camancob #3;
   caclipro #4;

   carctxcb #5;
   carctxpi #6;
   caemrctx #7;
|FIN;

|VARIABLES;
   &entrada = 1;

   &Lugar  = "";
   &aFecha = "";
   nDia    = 0;
   aDia    = "";
   nMes    = 0;
   aMes    = "";
   aAny    = "";
   aAlfa1  = "";

   &Separacion = 7;

   nSwOper   = 0;
   eaCuenta  = "";
   enNivel   = 0;
   aNombre   = "";
   Calc      = 0;
   Linea     = 1;
   NumLineas = 0;
   HayDatos  = 0;
   ContLins  = 0;
   nSw       = 0;

   &Cab = 0;
   &CabCue = 0;
   &Cue = 0;
   &PieCue = 0;
   &Pie = 0;
   &Separa = 0;

   DFactura = 0;
   HFactura = 0;
   Cont     = 0;
|FIN;

|INCLUYE acceso_i;

|| **************************** Calculo Pantalla
|CALCULO Mayor; |TIPO 0;
  #0Campo = #0Campo > " ";
  |PINTA #0Campo;
|FCAL;

|CALCULO MiraSelec; |TIPO 0;
   |HAZ Mayor;
   |SI #0#17 = "S";
      |PARA Cont = 18; |SI Cont [ 41; |HACIENDO Cont = Cont + 1;
            |C_M #0Cont,1,"N";
      |SIGUE;
      |C_M #0#1,1,"S";
      |C_M #0#5,1,"S";
   |SINO;
      |PARA Cont = 18; |SI Cont [ 41; |HACIENDO Cont = Cont + 1;
            |C_M #0Cont,1,"S";
      |SIGUE;
      |C_M #0#1,1,"N";
      |C_M #0#5,1,"N";
   |FINSI;
|FCAL;

|CALCULO Leecarta; |TIPO 6;
 |ABRE #7;
 |SI #0Campo = 0;
     #7#0 = 0;
     |LEE 001#7=;
     |SI FSalida ! 0;
         #7#0 = 0;
         |GRABA 001#7n;
     |FINSI;
 |SINO;
     #7#0 = #0Campo;
     |LEE 001#7=;
 |FINSI;
 |CIERRA #7;
|FCAL;
|| *********************************************************************
|| *********************************************************************
|PROCESO Pie;
  |SI nSwOper = 0;
      ContLins = ContLins + 1;
  |SINO;
      |PRINT;
      |SI PieCue = 1; PieCue = 0; |FINSI;
  |FINSI;
|FPRC;

|DEFBUCLE Pie;
 #6#1;
 ;
 #0#12, 001;
 #0#12, 999;
 ;
 NULL, Pie;
|FIN;

|PROCESO Cabecera;
   ContLins = ContLins + 1;
   |PRINT;
|FPRC;

|DEFBUCLE Cabecera;
 #5#1;
 ;
 #0#12,001;
 #0#12,999;
 e;
 NULL,Cabecera;
|FIN;

|| ///////////////////////////////
|PROCESO LaCabCliente;

   |HAZ LeeCliente;
   Cab    = 1;
   CabCue = 0;
   Cue    = 0;
   PieCue = 0;
   Pie    = 0;
   ContLins = 19;
   |BUCLE Cabecera;
   Cab    = 0;
   CabCue = 1;
   Cue    = 1;
|FPRC;

|PROCESO ElPieCliente;
   Cab    = 0;
   CabCue = 0;
   Cue    = 0;
   PieCue = 1;
   Pie    = 1;
   nSwOper = 1;
   |BUCLE Pie;
   |SI PieCue = 1;  || no existen lineas
       Pie = 0;
       |PRINT;
   |FINSI;
   Cab    = 0;
   CabCue = 0;
   Cue    = 0;
   PieCue = 0;
   Pie    = 0;
   |PIEINF;
|FPRC;

|PROCESO Cuerpo;

 |SI nSw = 0;
     nSw = 1;
     eaCuenta = #1#4;
     enNivel  = #1#15;
     |HAZ LaCabCliente;
 |FINSI;

 |SI eaCuenta ! #1#4;
     |HAZ ElPieCliente;
     eaCuenta = #1#4;
     enNivel  = #1#15;
     |HAZ LaCabCliente;
 |FINSI;

 |PRINT;
 |SI CabCue = 1; CabCue = 0; |FINSI;
 ContLins = ContLins + 1;
|FPRC;

|DEFBUCLE LeeAuxiliar;
 #1#2;
 9;
 "            ", 001, "*";
 "zzzzzzzzzzzz", 999, "*";
 ;
 NULL,Cuerpo;
|FIN;

|PROCESO Imprimir;

   |HAZ DatosBonitos;

   |IMPRE 0;
   |SI FSalida < 0; |ACABA; |FINSI;

   |INFOR "caemrcc2";
   |SI FSalida ! 0;
       |MENAV "    No existe Informe";
       |FINIMP;
       |ACABA;
   |FINSI;

   nSwOper  = 0;
   ContLins = 19;
   |BUCLE Pie;

   nSw      = 0;
   eaCuenta = "";
   |BUCLE LeeAuxiliar;
   |SI nSw = 1;
       |HAZ ElPieCliente;
   |FINSI;

   |FININF;
   |FINIMP;
|FPRC;

|| **********************************************************
|PROCESO LeeCliente;
 |ABRE #2;
 #2#0 = eaCuenta;
 #2#1 = enNivel;
 |LEE 001#2=;
 |SI FSalida ! 0;
     #2#2 = "No existe cliente";
 |FINSI;
 aNombre = #2#2;
 |CIERRA #2;

 |ABRE #4;
 #4#23 = "C";
 #4#10 = eaCuenta;
 #4#11 = enNivel;
 |LEE 001#4=;
 |SI FSalida ! 0;
     #4#23 = "P";
     #4#10 = eaCuenta;
     #4#11 = enNivel;
     |LEE 001#4=;
     |SI FSalida = 0;
         aNombre = #4#1;
     |FINSI;
 |SINO;
     aNombre = #4#1;
 |FINSI;
 |CIERRA #4;
|FPRC;

|PROCESO Rellena;

   |SI #0#42 = "P"; |Y  #3#27 ! 0; |ACABA; |FINSI;
   |SI #0#42 = "I"; |Y  #3#27 = 0; |ACABA; |FINSI;

   |DDEFECTO #1;
   #1#0 = Linea;
   Linea = Linea + 1;
   #1#1 = #3#2;  ||Serie
   #1#2 = #3#1;  ||Factura
   #1#3 = #3#3;  ||Num.cobro
   #1#4 = #3#4;  ||Cliente
   #1#15 = #3#5;

   eaCuenta = #3#4;
   enNivel  = #3#5;
   |HAZ LeeCliente;
   #1#5 = aNombre;      ||Nombre Cliente
   #1#6 = #3#7;         ||Fecha Vto.
   #1#7 = #3#8;         ||Importe con impagado
   #1#8 = #3#9;         ||Gastos Negociacion
   #1#9 = " ";
   |SI #0#43 = "S"; HayDatos = HayDatos + 1; #1#9 = "*"; |FINSI;
   #1#10 = #3#6;        ||Fecha Emision
   #1#11 = #3#26;       ||Importe Original
   #1#12 = #3#24;       ||Importes pagados
   #1#13 = #3#10;       ||Gastos impagados
   #1#14 = #3#26 + #3#9 + #3#10 - #3#24;
   |GRABA 020#1n;
   nSw = 1;
|FPRC;

|DEFBUCLE RellenaRecibos;
 #3#1;
 4,7,23,11;
 01, #0#0, DFactura, 001, #0#2, #0#3, "N", #0#15;
 99, #0#4, HFactura, 999, #0#6, #0#7, "N", #0#16;
 e;
 NULL,Rellena;
|FIN;

|RUTINA inf;
   #1#0 = 001;
|FRUT;

|RUTINA sup;
   #1#0 = 999;
|FRUT;

|PROCESO Marcar; |TIPO 11;
  |SI FSalida = 9;
      |SI #1#9 = " ";
          #1#9 = "*";
          HayDatos = HayDatos + 1;
      |SINO;
          #1#9 = " ";
          HayDatos = HayDatos - 1;
      |FINSI;
      |GRABA 020#1a;
  |FINSI;
|FPRC;

|| ************************************************************************
|CALCULO ElRecibo; |TIPO 3;

   |ABRE #7;
   #7#0 = #0#12;
   |LEE 000#7=;
   |SI FSalida = 0;
       NumLineas = #7#2;
   |FINSI;
   |CIERRA #7;

   |DELINDEX #1;

   nSw      = 0;
   HayDatos = 0;
   Linea    = 1;
   |ABRE #1;
   |SI #0#17 = "S";
       DFactura = #0#1;
       HFactura = #0#5;
       |BUCLE RellenaRecibos;
   |SINO;
       |PARA Cont = 18; |SI Cont [ 41; |HACIENDO Cont = Cont + 1;
             |SI #0Cont ! 0;
                 DFactura = #0Cont;
                 HFactura = #0Cont;
                 |BUCLE RellenaRecibos;
             |FINSI;
       |SIGUE;
   |FINSI;
   |CIERRA #1;

   |SI nSw ! 0;
       |PINPA #0#1;
       |ATRI R;
       |MENSA "0400<F12> Marcar/Desmarcar";
       |ATRI N;
       |ENTLINEAL #1#1, -1, 4, 21, 0, inf, sup;
   |FINSI;

   |SI HayDatos = 0;
       |MENAV "    Seleccion vacia";
   |SINO;
       |HAZ Imprimir;
   |FINSI;

   |DELINDEX #1;
|FCAL;

|PROCESO DatosBonitos;
   Lugar = #0#8; |QBF Lugar;
   Lugar = (" " * 25) + Lugar;
   Lugar = Lugar % -125;

   aAlfa1 = #0#9
   aDia   = aAlfa1 % 102; nDia = aDia; aDia = nDia;
   aMes   = aAlfa1 % 402; nMes = aMes;
   aAny   = aAlfa1 % 704;

   |SI nMes = 1;  aMes = "Enero";      |FINSI;
   |SI nMes = 2;  aMes = "Febrero";    |FINSI;
   |SI nMes = 3;  aMes = "Marzo";      |FINSI;
   |SI nMes = 4;  aMes = "Abril";      |FINSI;
   |SI nMes = 5;  aMes = "Mayo";       |FINSI;
   |SI nMes = 6;  aMes = "Junio";      |FINSI;
   |SI nMes = 7;  aMes = "Julio";      |FINSI;
   |SI nMes = 8;  aMes = "Agosto";     |FINSI;
   |SI nMes = 9;  aMes = "Septiembre"; |FINSI;
   |SI nMes = 10; aMes = "Octubre";    |FINSI;
   |SI nMes = 11; aMes = "Noviembre";  |FINSI;
   |SI nMes = 12; aMes = "Diciembre";  |FINSI;

   aFecha = aDia + " de " + aMes + " de "+ aAny
|FPRC;
