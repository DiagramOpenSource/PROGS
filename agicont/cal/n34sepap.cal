|FICHEROS;
 n34sepap #0;
 n34sepam #1;
 camingca #2;  || Confirming Cabeceras
 camingli #3;  || Confirming Lineas
 camanpag #4;  || Mantenimiento pagos
 camanban #8;  || Bancos..............
 caprovin #9;  || Provincias

 caclipro #100;
 n34sepat #101;
 cacopdis #102;

 agim0016@ #1000;
|FIN;

|VARIABLES;
 &eaEntidad    = "";
 &eaSucursal   = "";
 &eaCuenta     = "";
 &eaDC         = "";

 aPath = "";
 nSwIntegral = 0;
 nSwBasica = 0;
 nAvisa = 0;
 &fecha = @;
 &fp = "";
 &eaDirProv = "";
 &eaNifProv = "";
 &HayConta = 0;
 &eaCadena = "";

 &eaCodigo  = "";
 &eaTipoCod = "";
 &aCuenta   = "";

 &eaNif       = "";
 &eaNombre    = "";
 &eaApCorreos = "";
 &eaDireccion = "";
 &eaPoblacion = "";
 &eaProvincia = "";
 &eaCP        = "";
 &eaPais      = "";
 &eaCodIBAN   = "";
 &eaCodBIC    = "";

 aHora   = "";
 aCodigo = "";
 aNombre = "";
 aExtr   = "";
 aSEPA   = "";
 nSwPaises = 0;

 nCalc      = 0;
 nImporte   = 0;
 Prefijo    = "";
 aRef       = "";
 aDirec1    = "";
 aDirec2    = "";
 fFechaEmis = @;

 &aInforme  = "";
 &eaNombre1 = "";
 &eaOrigen  = "";
 aDestino   = "";
 Comodin    = "";
 Comodin1   = "";
 nSwImpre   = 0;
 &SwXml     = 0;

 &Niv1 = 0;
 &Niv2 = 0;
 &Niv3 = 0;
 &Niv4 = 0;
 &Niv5 = 0;
 &Niv6 = 0;
 &Divisa = "";

 aCuentaCt = ""; nNivel = 0;

 alfa1 = "";
 alfa2 = "";
 alfa3 = "";
 alfa4 = "";
 nume1 = 0;
 nume2 = 0;
 nume3 = 0;
 nume4 = 0;

 mens     = "";
 swErrorR = 0;
 swErrorP = 0;
 menR1    = "    No Existe Confirme !!!";
 menR2    = "    Confirme YA contabilizado ";
 menR3    = "2400SConfirme YA Emitido en Papel. Aceptar S/N ";
 menR4    = "2400NConfirme YA lanzado en Diskette. Aceptar S/N ";

 Nombre  = "";
 CodCli  = "";
 sino     = "";
 ceros    = "000000000000000";
 fechapre = "      ";
 dd       = "";
 mm       = "";
 aa       = "";
 cabe1    = "0406";  || Registro Cabecera
 cabe2    = "2";  || Registro Detalle Pagos
 cabe3    = "3";  || Registro Total
 aPaisSEPA = "";

 libre    = " ";
 libre40  = "";
 ca1  = "";
 ca2  = "";
 ca3  = "";
 ca4  = "";
 ca5  = "";
 ca6  = "";
 ca7  = "";
 ca8  = "";
 ca9  = "";
 ca10 = "";
 ca11 = "";
 ca12 = "";
 ca13 = "";
 ca14 = "";
 ca15 = "";
 ca115 = "";
 ca16 = "";
 ca17 = "";
 ca18 = "";
 ca19 = "";
 ca20 = "";
 ca21 = "";
 l          = 0;
 trecibos   = 0;
 timporte   = 0;

 NifProv    = "";
 NombreProv = "";
 DirecProv  = "";
 PoblaProv  = "";
 ProviProv  = "";
 Cp1Prov    = "";
 Cp2Prov    = "";
 TelefProv  = "";
 FaxProv    = "";
 PaisProv   = "";
 Entidad    = "";
 Sucursal   = "";
 DC         = "";
 Cuenta     = "";
 varios     = 0;

 handle     = "";
 fiche      = "";
 dirempr    = "";
 varalfa1   = "";
 varalfa2   = "";
 contenido  = "";
 swERROR    = 0;
 CampoDni   = "";
 Letra1     = "";
 Letra2     = "";
 Letra3     = "";
 Letra4     = "";
 Letra5     = "";
 Letra6     = "";
 Letra7     = "";
 Letra8     = "";
 Caracter   = "";
 Carac1     = 0;
 Carac2     = 0;
 Carac3     = 0;
 Carac4     = 0;
 Carac5     = 0;
 XPara      = 0;

 ccc        = "";
 c_alfa1    = "";
 c_alfa2    = "";
 c_nume1    = 0;
 c_nume2    = 0;
 c_nume3    = 0;

 direcc = "";
 numero = 0;
 longi  = 0;
 concepto = "";

 &programa = "";
 &ficcopia = "";
 aAlfa  = "";
 aAlfa1 = "";
 aAlfa2 = "";

     &eaVarDni  = "";
     &enCalcCif = 0;

  nTF = 0;
  nSwError = 0;
  aErrTit = "";

  aCountry = "";

  aChar1  = "";
  aCadena = "";

  nTotGeneral    = 0;
  nTotalGeneral  = 0;
  nRegsIndiv     = 0;
  nRegsGeneral   = 0;
  nRegTrSEPA     = 0;
  nTotTrSEPA     = 0;
  nSituacionSEPA = 0;
  nCont  = 0;
  nSwCab = 0;

  enMayus = 0;

  aMax   = "";
  aMin   = "";
  aRaya  = "";
  aLen   = "";
  aCaracter = "";

  nLen   = 0;
  nPos   = 0;
  nCampo = 0;

  sw     = 0;

  nVuelta   = 0;
  nSwCuenta = 1;

  &eaPain    = "";
  &eaFichero = "";
  &eaTag     = "";
  &eaVal     = "";
  &eaGrupo   = "";
     nRegistros1 = 0;
     nRegistros2 = 0;
     nRegistros3 = 0;
     nTotal1 = 0;
     nTotal2 = 0;
     nTotal3 = 0;
     fHoy = @;
     nNume = 0;
     aPais = "";
     aNif = "";
     aSufijo = "";
     aId = "";
     nPersona = 0;
     aCar = "";
     aLon = "";
     nLon = 0;
     aDeci = "";

    aConce  = "";
    aConce1 = "";
    aConce2 = "";
|FIN;

|| *************************************************************************
|PROCESO Tiene;
     |SI nAvisa = 0;
          |QBF aAlfa;
          |SI aAlfa = "";
               nAvisa = 1;
               aAlfa = #caclipro#10; |QBF aAlfa;
               |SI #caclipro#23 = "C";
                    aAlfa = "0000Cliente: [" + aAlfa + "] con datos incompletos en contabilidad...";
               |SINO;
                    aAlfa = "0000Proveedor: [" + aAlfa + "] con datos incompletos en contabilidad...";
               |FINSI;
               |MENAV aAlfa;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO CheqDatosPrv;
     #n34sepat#0 = #caclipro#23;
     #n34sepat#1 = #caclipro#10;
     |LEE 000#n34sepat.=;
     |SI FSalida = 0;
          |ACABA;
     |SINO;
          |GRABA 020#n34sepat.n;
     |FINSI;

     nAvisa = 0;
     aAlfa = #caclipro#17; |HAZ Tiene;
     aAlfa = #caclipro#18; |HAZ Tiene;
     aAlfa = #caclipro#13; |HAZ Tiene;
     aAlfa = #caclipro#14; |HAZ Tiene;

     aAlfa = "";
     |SI #caclipro#15 = 0; |HAZ Tiene; |FINSI;
     |SI #caclipro#16 = 0; |HAZ Tiene; |FINSI;

     aAlfa = #caclipro#29; |HAZ Tiene;
     aAlfa = #caclipro#30; |HAZ Tiene;
     aAlfa = #caclipro#31; |HAZ Tiene;
     aAlfa = #caclipro#32; |HAZ Tiene;
     aAlfa = #caclipro#33; |HAZ Tiene;
     aAlfa = #caclipro#34; |HAZ Tiene;
     aAlfa = #caclipro#35; |HAZ Tiene;
     aAlfa = "";
|FPRC;

|PROCESO SacaNivel1;
   |QBF aCuentaCt;
   aAlfa = aCuentaCt % 0; nNivel = aAlfa;
   |SI nNivel = #48#14; nNivel = 1; |FINSI;
   |SI nNivel = #48#15; nNivel = 2; |FINSI;
   |SI nNivel = #48#16; nNivel = 3; |FINSI;
   |SI nNivel = #48#17; nNivel = 4; |FINSI;
   |SI nNivel = #48#18; nNivel = 5; |FINSI;
   |SI nNivel = #48#19; nNivel = 6; |FINSI;
|FPRC;

|PROCESO ver_confirming; |TIPO 6;
 |SI FSalida = 2; |ACABA; |FINSI;
 |ABRE #camingca;
 |HAZ leerem;
 |CIERRA #camingca;
 |SI swErrorR ! 0;
     |SI swErrorR < 3;
         |MENAV mens;
         |ERROR;
         |ACABA;
     |SINO;
         |CONFI mens;
         |SI FSalida ! 0;
             |ERROR;
             |ACABA;
         |FINSI;
     |FINSI;
 |FINSI;
 #n34sepap#2 = #camingca#3; || Banco
 |HAZ TomaDatos;
 ||#n34sepap#5 = #camingca#6; |PINTA #0#5; || Codigo cliente entidad
 ||#n34sepap#3 = #camingca#1; |PINTA #0#3; || Fecha presentacion

 |PINTA 0935, #camingca#2;   || Vto.
 |PINTA 0958, #camingca#8;   || N§ Efectos
 |PINTA 1035, #camingca#3;   || Banco
 |PINTA 1049, #camingca#9;  || Importe Total
|FPRC;

|PROCESO TomaDatos;
   |ABRE #n34sepam;
   #n34sepam#0 = #n34sepap#2;
   |LEE 000#n34sepam.=;
   |SI FSalida ! 0;
     |DDEFECTO #n34sepam;
   |FINSI;
   |CIERRA #n34sepam;

   #n34sepap#5  = #n34sepam#23; |PINTA #n34sepap#5;
   #n34sepap#13 = #n34sepam#24; |PINTA #n34sepap#13;
|FPRC;

|PROCESO leerem;
 swErrorR = 0;
 |DDEFECTO #camingca;
 #camingca#0 = #n34sepap#1;
 |LEE 001#camingca.=;
 |SI FSalida ! 0;
     swErrorR = 1; mens = menR1;
 |SINO;
     |SI #camingca#11 = "S";
         swErrorR = 2; mens = menR2;
     |SINO;
         |SI #camingca#10 = "S"; swErrorR = 3; mens = menR3; |FINSI;
         |SI #camingca#10 = "D"; swErrorR = 4; mens = menR4; |FINSI;
     |FINSI;
     |ABRE #camanban;
     #camanban#0 = #camingca#3;
     |LEE 000#camanban.=;
     |CIERRA #camanban;
 |FINSI;
|FPRC;

||...................................................................................
|PROCESO QuitaRaros;
   aMax = &175;
   aMin = &223;
   aRaya = &196;

   aCadena = "";
   aLen    = eaCadena % 0;
   nLen    = aLen;
   |PARA nCampo = 1;  |SI nCampo [ nLen;  |HACIENDO nCampo = nCampo + 1;
         nPos = (100 * nCampo) + 1;
         aCaracter = eaCadena % nPos;
         sw = 0;
         |SI enMayus = 0;
            |SI aCaracter = "¥";  aCaracter = &209; sw = 1; |FINSI;
            |SI aCaracter = "¤";  aCaracter = &241; sw = 1; |FINSI;
            |SI aCaracter = " ";  aCaracter = &225; sw = 1; |FINSI;
            |SI aCaracter = "‚";  aCaracter = &233; sw = 1; |FINSI;
            |SI aCaracter = "¡";  aCaracter = &237; sw = 1; |FINSI;
            |SI aCaracter = "¢";  aCaracter = &243; sw = 1; |FINSI;
            |SI aCaracter = "£";  aCaracter = &250; sw = 1; |FINSI;
         |SINO;
            |SI aCaracter = "¥";  aCaracter = &209; sw = 1; |FINSI;
            |SI aCaracter = "¤";  aCaracter = &209; sw = 1; |FINSI;
            |SI aCaracter = " ";  aCaracter = &65;  sw = 1; |FINSI;
            |SI aCaracter = "‚";  aCaracter = &69;  sw = 1; |FINSI;
            |SI aCaracter = "¡";  aCaracter = &73;  sw = 1; |FINSI;
            |SI aCaracter = "¢";  aCaracter = &79;  sw = 1; |FINSI;
            |SI aCaracter = "£";  aCaracter = &85;  sw = 1; |FINSI;
         |FINSI;
         |SI aCaracter = "§";  aCaracter = &176; sw = 1; |FINSI;
         |SI aCaracter = "¦";  aCaracter = &170; sw = 1; |FINSI;
         |SI aCaracter > aMax; |Y aCaracter < aMin; |Y sw = 0;
            |SI aCaracter = aRaya;
                aCaracter = "_";
            |SINO;
                 aCaracter = " ";
            |FINSI;
        |FINSI;
        aCadena = aCadena + aCaracter;
  |SIGUE;
  eaCadena = aCadena;
|FPRC;
||***********************************************************
||****************** REGISTROS DE CABECERA ******************
||***********************************************************

|PROCESO CabeceraGeneral;
  ca1  = "01ORD34145001";
  aAlfa  = #n34sepam#2; |QBF aAlfa;
  ca2  = (aAlfa + (" " * 9)) % 0109;
  ca3  = #n34sepam#26;
  ca4  = #n34sepap#3;                 || Fecha creacion fichero
  ca4  = (ca4 % 0704) + (ca4 % 0402) + (ca4 % 0102);
  ca5  = #n34sepap#15;                || Fecha proceso soporte
  ca5  = (ca5 % 0704) + (ca5 % 0402) + (ca5 % 0102);
  ca6  = "A";
  aAlfa = #n34sepam#30;
  |QBF aAlfa;
  aAlfa = (aAlfa + (" " * 34)) % 0134;
  ca7 = aAlfa;
  ca8 = "0"
  aAlfa = #n34sepam#3; |QBF aAlfa;
  aAlfa = (aAlfa + (" " * 70)) % 0170;
  ca9 = aAlfa;
  aAlfa = #n34sepam#7;
  aAlfa = (aAlfa + (" " * 50)) % 0150;
  ca10 = aAlfa;
  aAlfa = "C.P. " + (("00" + #n34sepam#8) % -102) + (("000" + #n34sepam#9) % -103);
  aAlfa = aAlfa + " " + #n34sepam#10; |QBF aAlfa;
  aAlfa = (aAlfa + (" " * 50)) % 0150;
  ca11 = aAlfa;

  |ABRE #caprovin;
  #caprovin#0 = #n34sepam#8;
  |LEE 000#caprovin.=;
  |SI FSalida ! 0; |DDEFECTO #caprovin; |FINSI;
  |CIERRA #caprovin;

  aAlfa = #caprovin#1; aAlfa = (aAlfa + (" " * 40)) % 0140; ca12 = aAlfa;
  ca13 = #n34sepam#27;
  ca14 = " " * 200;
  ca15 = " " * 110;  || tique 500005 _ 89

  varalfa1 = Prefijo + ca1 + ca2 + ca3 + ca4 + ca5 + ca6 + ca7 + ca8 + ca9;
  eaCadena = varalfa1; |HAZ QuitaRaros; varalfa1 = eaCadena;
  contenido = handle + " " + varalfa1;
  |FGRABA contenido;

  varalfa1 = Prefijo + ca10 + ca11 + ca12 + ca13;
  eaCadena = varalfa1; |HAZ QuitaRaros; varalfa1 = eaCadena;
  contenido = handle + " " + varalfa1;
  |FGRABA contenido;

  contenido = handle + " " + ca14;
  |FGRABA contenido;

  contenido = handle + " " + ca15 + &#0#7 + &#0#8;
  |FGRABA contenido;
  l = l + 1;

  nTotGeneral = 0;
  nRegsGeneral = 1; ||Este
  nRegsIndiv  = 0;
|FPRC;

|PROCESO CabeceraSEPA;
   nSwCab = 1;
   |SI nCont = 1; ca1 = "02SCT34145"; |FINSI;
   |SI nCont = 2; ca1 = "02OTR34145"; |FINSI;
   |SI nCont = 3; ca1 = "02CHQ34145"; |FINSI;
   ca2  = #n34sepam#2;
   ca3  = #n34sepam#26;
   ca4 = " " * 200;
   ca5 = " " * 200;
   ca6 = " " * 178;

   contenido = handle + " " + ca1 + ca2 + ca3;
   |FGRABA contenido;

   contenido = handle + " " + ca4;
   |FGRABA contenido;

   contenido = handle + " " + ca5;
   |FGRABA contenido;

   contenido = handle + " " + ca6 + &#0#7 + &#0#8;
   |FGRABA contenido;
   nTotTrSEPA   = 0;
   nRegTrSEPA   = 0;

   l = l + 1;
   nRegsGeneral = nRegsGeneral + 1;
|FPRC;

||...................................................................................
||***********************************************************
||****************** REGISTROS INDIVIDUALES *****************
||***********************************************************

|PROCESO leecl_y_cobro;
 || .............. Lee la ficha del Cliente para obtener los datos necesarios

 NombreProv = "";
 DirecProv  = "";
 PoblaProv  = "";
 ProviProv  = "";
 Cp1Prov    = "";
 Cp2Prov    = "";
 TelefProv  = "";
 FaxProv    = "";
 PaisProv   = "";
 Entidad    = "";
 Sucursal   = "";
 DC         = "";
 Cuenta     = "";

 aRef = " " * 12;

 |DDEFECTO #camanpag;
 #camanpag#0 = #camingli#2;
 #camanpag#1 = #camingli#3;
 #camanpag#2 = #camingli#4;
 #camanpag#3 = #camingli#5;
 |LEE 000#camanpag.=;
 |SI FSalida ! 0; |DDEFECTO #camanpag; |FINSI;

 fFechaEmis = #camanpag#4;

 #caclipro#23 = "P";
 #caclipro#10 = #camanpag#4;
 aCuentaCt = #camanpag#4; |HAZ SacaNivel1; #caclipro#11 = nNivel;
 |LEE 000#caclipro.=;
 |SI FSalida = 0;
    |SI #0#16 = "S"; |HAZ CheqDatosPrv; |FINSI;
    NifProv    = #caclipro#9;
    NombreProv = #caclipro#1;
    DirecProv  = #caclipro#2;
    PoblaProv  = #caclipro#6;
    ProviProv  = #caclipro#5;
    Cp1Prov    = ("00" + #caclipro#3) % -102;
    Cp2Prov    = ("000" + #caclipro#4) % -103;
    TelefProv  = #caclipro#7;
    FaxProv    = #caclipro#8;
    PaisProv   = #caclipro#24;
    |SI #n34sepap#4 = "1"; aRef = #caclipro#10; |FINSI;
    |SI #n34sepap#4 = "2"; aRef = #caclipro#0;  |FINSI;
    |QBF aRef; aRef = ((" " * 12) + aRef) % -112;

    aAlfa    = #caclipro#18; |QBF aAlfa;
    Entidad  = aAlfa % 0104;
    Sucursal = aAlfa % 0504;
    DC       = aAlfa % 0902;
    Cuenta = #caclipro#17; |QBF Cuenta;
    Cuenta = ("0000000000" + Cuenta) % -110;
    eaCodIBAN = #caclipro#30;
    eaCodBIC  = #caclipro#29;

 |SINO;
    aAlfa = "    No se encuentran los datos del proveedor " + #camanpag#6; |QBF aAlfa;
    aAlfa = aAlfa + " en la ficha de contabilidad";
 |FINSI;

 aAlfa = PaisProv; |QBF aAlfa; aAlfa = aAlfa % 0102;
 nSituacionSEPA = 0;

 |SI nSwPaises = 1;
    #agim0016@#0 = #caclipro#24;
    |LEE 000#agim0016@.=;
    |SI FSalida = 0;
       aAlfa = #agim0016@#0; |QBF aAlfa;
       |SI aAlfa = "ES";
          nSituacionSEPA = 1;
       |SINO;
          |SI #agim0016@#6 = "S";
             nSituacionSEPA = 2;
          |SINO;
             nSituacionSEPA = 3;
          |FINSI;
       |FINSI;
    |FINSI;
 |SINO;
    aAlfa = #caclipro#24; |QBF aAlfa;
    aAlfa = aAlfa % 0102;
    |SI aAlfa = "ES";
       nSituacionSEPA = 1;
    |SINO;
       |SI aAlfa = "DE"; |O aAlfa = "AT"; |O aAlfa = "BE"; |O aAlfa = "CY";
        |O aAlfa = "DK"; |O aAlfa = "SI"; |O aAlfa = "SK"; |O aAlfa = "EE";
        |O aAlfa = "FI"; |O aAlfa = "BG"; |O aAlfa = "FR"; |O aAlfa = "GR";
        |O aAlfa = "GB"; |O aAlfa = "NL"; |O aAlfa = "HU"; |O aAlfa = "IT";
        |O aAlfa = "IE"; |O aAlfa = "IS"; |O aAlfa = "LV"; |O aAlfa = "LT";
        |O aAlfa = "LU"; |O aAlfa = "MT"; |O aAlfa = "PL"; |O aAlfa = "PT";
        |O aAlfa = "CH"; |O aAlfa = "SE"; |O aAlfa = "CZ"; |O aAlfa = "HR";
        |O aAlfa = "RO"; |O aAlfa = "LI"; |O aAlfa = "MC"; |O aAlfa = "NO";
           nSituacionSEPA = 2;
       |SINO;
           nSituacionSEPA = 3;
       |FINSI;
    |FINSI;
 |FINSI;

 eaNif       = NifProv;
 eaDireccion = DirecProv;
 eaNombre    = NombreProv;
 eaPoblacion = PoblaProv;
 eaProvincia = ProviProv;
 eaCP        = ("00" + Cp1Prov) % -102 + ("000" + Cp2Prov) % -103;
 eaPais      = PaisProv;
|FPRC;

|PROCESO IndividualSEPA;
   |SI nCont = 1;
      trecibos = trecibos + 1;
      ca1 = "03SCT34145002";
      ca2 = #n34sepap#13; |QBF ca2;
      aAlfa = ca2; |QBF aAlfa; ca2 = (aAlfa + (" " * 35)) % 0135;
      ca3 = "A";
      aAlfa = #caclipro#30 + #caclipro#31 + #caclipro#32 + #caclipro#33 + #caclipro#34 + #caclipro#35;
      |QBF aAlfa; |QBF aAlfa; ca4 = (aAlfa + (" " * 34)) % 0134;
   |FINSI;
   |SI nCont = 2;
      trecibos = trecibos + 1;
      ca1 = "03OTR34145006";
      aAlfa = #n34sepam#3; |QBF aAlfa; ca2 = (aAlfa + (" " * 35)) % 0135;
      ca3 = "A";
      aAlfa = #caclipro#30 + #caclipro#31 + #caclipro#32 + #caclipro#33 + #caclipro#34 + #caclipro#35;
      |QBF aAlfa; |QBF aAlfa; ca4 = (aAlfa + (" " * 34)) % 0134;
   |FINSI;
   |SI nCont = 3;
      trecibos = trecibos + 1;
      ca1 = "03CHQ34145008";
      aAlfa = #n34sepam#2; |QBF aAlfa; ca2 = (aAlfa + (" " * 35)) % 0135;
      aAlfa = #n34sepam#3; |QBF aAlfa; aAlfa = (aAlfa + (" " * 70)) % 0170;
      ca2 = ca2 + aAlfa;
      ca3 = "";
      ca4 = "";
   |FINSI;
   nCalc = #camingli#8;
   nCalc = (nCalc * 100) ! 0;
   ca5 = (("0" * 11) + nCalc) % -111;
  nTotalGeneral  = nTotalGeneral + #camingli#8;

   nTotTrSEPA = nTotTrSEPA + #camingli#8;
   nTotGeneral = nTotGeneral + #camingli#8;
   timporte = timporte + #camingli#8;

   |SI nCont = 1; |O nCont = 2;
      ca6 = "3";
      ca7 = #caclipro#29; ca7 = (ca7 + (" " * 11)) % 0111;
   |FINSI;
   |SI nCont = 3;
      ca6 = "";
      ca7 = "";
   |FINSI;
   contenido = handle + " " + ca1 + ca2 + ca3 + ca4 + ca5 + ca6 + ca7;
   |FGRABA contenido;

   |SI nCont = 1; |O nCont = 3;
      aAlfa = #caclipro#1; |QBF aAlfa; aAlfa = (aAlfa + (" " * 70)) % 0170;
      ca8 = aAlfa;
      aAlfa = #caclipro#2; |QBF aAlfa; aAlfa = (aAlfa + (" " * 50)) % 0150;
      ca9 = aAlfa;
      aAlfa = ((("00" + #caclipro#3) % -102) + (("000" + #caclipro#4) % -103)) + " - " + #caclipro#6; |QBF aAlfa; aAlfa = (aAlfa + (" " * 50)) % 0150;
      ca10 = aAlfa;
      aAlfa = #caclipro#5; |QBF aAlfa; aAlfa = (aAlfa + (" " * 40)) % 0140;
      ca11 = aAlfa;
      ca12 = #caclipro#24; |QBF ca12; ca12 = (ca12 + "  ") % 0102;
      contenido = handle + " " + ca8 + ca9 + ca10 + ca11 + ca12;
      |FGRABA contenido;

      |SI nCont = 1;
         aAlfa = "EN CONCEPTO PAGO " + #camanpag#28; |QBF aAlfa;
         ca13 = (aAlfa + (" " * 99)) % 0199;
         ca13 = ca13 + (" " * 41);
         ca14 = " " * 35;
         ca15 = "    ";  || tique 500005_89  ca15 = "SUPP"
         ca16 = "    ";  ||                  ca16 = "IVPT";
         contenido = handle + " " + ca13 + ca14 + ca15 + ca16;
         |FGRABA contenido;

         ca17 = " " * 99;
         contenido = handle + " " + ca17 + &#0#7 + &#0#8;
         |FGRABA contenido;
      |FINSI;
      |SI nCont = 3;
         ca13 = "3";
         ca14 = " " * 200;
         contenido = handle + " " + ca13 + ca14;
         |FGRABA contenido;

         ca14 = " " * 58;
         contenido = handle + " " + ca14 + &#0#7 + &#0#8;
         |FGRABA contenido;
      |FINSI;
   |FINSI;
   |SI nCont = 2;
      aAlfa = #caclipro#1; |QBF aAlfa; aAlfa = (aAlfa + (" " * 35)) % 0135;
      ca8 = aAlfa;
      aAlfa = #caclipro#2; |QBF aAlfa;
      aAlfa = aAlfa + ", " + ((("00" + #caclipro#3) % -102) + (("000" + #caclipro#4) % -103)) + " - " + #caclipro#6; |QBF aAlfa;
      aAlfa = aAlfa + " (" + #caclipro#5; |QBF aAlfa;
      aAlfa = aAlfa + ", " + #caclipro#24; |QBF aAlfa;
      aAlfa = aAlfa + ") "
      aAlfa = (aAlfa + (" " * 99)) % 0199;
      aAlfa = aAlfa + "      ";
      ca9 = aAlfa;
      contenido = handle + " " + ca8 + ca9;
      |FGRABA contenido;

      aAlfa = "EN CONCEPTO PAGO S/FRA. " + #camanpag#28; |QBF aAlfa;
      ca10 = (aAlfa + (" " * 72)) % 0172;
      ca11 = #camanpag#28; |QBF ca11; ca11 = ((" " * 13) + ca11) % 0113;
      ca12 = "3";
      contenido = handle + " " + ca10 + ca11 + ca12;
      |FGRABA contenido;

      ca13 = " " * 200;
      contenido = handle + " " + ca13;
      |FGRABA contenido;

      ca14 = " " * 68;
      contenido = handle + " " + ca14 + &#0#7 + &#0#8;
      |FGRABA contenido;
   |FINSI;

   nRegTrSEPA   = nRegTrSEPA   + 1;
   nRegsIndiv   = nRegsIndiv   + 1;
   nRegsGeneral = nRegsGeneral + 1;
   l = l + 1;
|FPRC;

||...................................................................................
||***********************************************************
||****************** REGISTROS DE TOTALES *******************
||***********************************************************
|PROCESO TotalSEPA;
   |SI nCont = 1; ca1 = "04SCT"; |FINSI;
   |SI nCont = 2; ca1 = "04OTR"; |FINSI;
   |SI nCont = 3; ca1 = "04CHQ"; |FINSI;

   nTotTrSEPA = nTotTrSEPA * 100;
   ca2 = nTotTrSEPA;
   ca2 = (("0" * 17) + ca2) % -117;
   ca3 = nRegTrSEPA;
   ca3 = (("0" * 8) + ca3) % -108;
   nRegTrSEPA = nRegTrSEPA + 2;
   ca4 = nRegTrSEPA;
   ca4 = (("0" * 10) + ca4) % -110;

   ca5 = " " * 200;
   ca6 = " " * 200;
   ca7 = " " * 160;
   contenido = handle + " " + ca1 + ca2 + ca3 + ca4;
   |FGRABA contenido;

   contenido = handle + " " + ca5;
   |FGRABA contenido;

   contenido = handle + " " + ca6;
   |FGRABA contenido;

   contenido = handle + " " + ca7 + &#0#7 + &#0#8;
   |FGRABA contenido;

   nRegsGeneral = nRegsGeneral + 1;
   l = l + 1;
|FPRC;

|PROCESO TotalGeneral;
   nRegsGeneral = nRegsGeneral + 1;
   l = l + 1;

   ca1 = "99ORD";
   nCalc = nTotalGeneral;
   nCalc = (nCalc * 100) ! 0;
   ca2 = nCalc;
   ca2 = (("0" * 17) + ca2) % -117;

   aAlfa = nRegsIndiv;
   ca3 = (("0" * 8) + aAlfa) % -108;

   aAlfa = nRegsGeneral;
   ca4 = (("0" * 10) + aAlfa) % -110;

   ca5 = " " * 200;
   ca6 = " " * 200;
   ca7 = " " * 160;
   contenido = handle + " " + ca1 + ca2 + ca3 + ca4;
   |FGRABA contenido;

   contenido = handle + " " + ca5;
   |FGRABA contenido;

   contenido = handle + " " + ca6;
   |FGRABA contenido;

   contenido = handle + " " + ca7 + &#0#7 + &#0#8;
   |FGRABA contenido;
|FPRC;

|PROCESO ProcesaRec;
   aAlfa = #48#17; |QBF aAlfa;
   || aAlfa1 = aAlfa + "fich/" + (("00000" + #camanpag#46) % -105) + "/";
   aAlfa1 = aAlfa + "fich/02779/";
   |PATH_DAT #n34sepat#aAlfa1#; |ABRE #n34sepat;

   swERROR = 0;
   |HAZ leecl_y_cobro;
   eaNifProv = NifProv; eaDirProv = DirecProv;
   |SI nSwImpre = 1; |PRINT; |FINSI;
   |SI swERROR = 1; |ACABA; |FINSI;   || Salta este Cobro. NO el resto

   || Primero compruebo si pertenece al grupo que estoy pasando ...
   |SI nCont = 1; || Transferencias SEPA
      |SI nSituacionSEPA = 1; |O nSituacionSEPA = 2;
         |SI #camingli#12 = "T";
            |SI nSwCab = 0; |HAZ CabeceraSEPA; |FINSI;
            |HAZ IndividualSEPA;
         |FINSI;
      |FINSI;
   |FINSI;
   |SI nCont = 2; || Transferencias No SEPA
      |SI #camingli#12 = "T"; |Y nSituacionSEPA = 3;
         |SI nSwCab = 0; |HAZ CabeceraSEPA; |FINSI;
         |HAZ IndividualSEPA;
      |FINSI;
   |FINSI;
   |SI nCont = 3; || Cheques
      |SI #camingli#12 = "C";
         |SI nSwCab = 0; |HAZ CabeceraSEPA; |FINSI;
         |HAZ IndividualSEPA;
      |FINSI;
   |FINSI;

   |CIERRA #n34sepat;
|FPRC;

|PROCESO ProcesaRecibos;
   |HAZ CabeceraGeneral;

   |PARA nCont = 1; |SI nCont [ 3; |HACIENDO nCont = nCont + 1;
      nSwCab = 0;
      |BUCLELIN 0ProcesaRec#2;
      |SI nSwCab ! 0;
         |HAZ TotalSEPA;
      |FINSI;
   |SIGUE;

   |HAZ TotalGeneral;
|FPRC;

|PROCESO Mod9710;
   aAlfa2 = aAlfa % 0101;
   |PARA; |SI aAlfa2 = "0"; |HACIENDO;
      aAlfa = aAlfa % 0299; aAlfa2 = aAlfa % 0101;
   |SIGUE;

   aAlfa1 = "";
   |PARA; |SI aAlfa ! ""; |HACIENDO;
      aAlfa2 = aAlfa % 0; nCalc = aAlfa2;
      |SI nCalc > 5;
         aAlfa2 = aAlfa % 0105; aAlfa = aAlfa % 0699;
      |SINO;
         aAlfa2 = aAlfa; aAlfa = "";
      |FINSI;
      aAlfa1 = aAlfa1 + aAlfa2;
      nCalc = aAlfa1;
      nCalc = nCalc / 97;
      aAlfa2 = nCalc;
      aAlfa2 = aAlfa2 - ".";
      |SI FSalida ! 0;
         nCalc = FSalida + 98;
         aAlfa2 = aAlfa2 % nCalc;
         aAlfa2 = "0." + aAlfa2; nCalc = aAlfa2;
      |SINO;
         nCalc = 0;
      |FINSI;
      nCalc = (nCalc * 97) ! 0;
      aAlfa1 = nCalc;
   |SIGUE;
   nCalc  = aAlfa1;
   nCalc = 98 - nCalc;
   aAlfa = ("00" + nCalc) % -102;
|| nCalc  = 98 - (nCalc - (((nCalc / 97) ! 0) * 97)); || MOD 97-10  ISO 7604
|FPRC;

|PROCESO varia_fich;
|| .......... Variables del Presentador, del Ordenante y comunes

  CodCli = #n34sepap#5; |QBF CodCli;
  CodCli = CodCli + "  "; CodCli = CodCli % 102;

  libre40 = libre * 40;
  Nombre = #n34sepam#3 + libre40;
  Nombre = Nombre % 140;

  alfa1 = #n34sepap#3;
  dd = alfa1 % A102;
  mm = alfa1 % A402;
  aa = alfa1 % A902;
  fechapre = aa + mm + dd;   || Fecha Formato a¤o + mes + dia
|FPRC;

|| **************************************************************************
|PROCESO MiraPaisAgicli;
   #agim0016@#0 = aCodigo;
   |LEE 101#agim0016@.=;
   |SI FSalida ! 0;
      |DDEFECTO #agim0016@;
      #agim0016@#0 = aCodigo;
      #agim0016@#1 = aNombre;
      #agim0016@#5 = aExtr;
      #agim0016@#6 = aSEPA;
      |GRABA 020#agim0016@.n;
   |SINO;
      |SI #agim0016@#6 ! aSEPA;
         #agim0016@#6 = aSEPA;
         |GRABA 020#agim0016@.a; |LIBERA #agim0016@;
      |FINSI;
   |FINSI;
|FPRC;

|PROCESO CompruebaPaisesAgicli;
   aCodigo = "ES"; aNombre = "ESPA¥A";       aExtr = "N"; aSEPA = "S"; |HAZ MiraPaisAgicli;
   aCodigo = "DE"; aNombre = "ALEMANIA";     aExtr = "S"; aSEPA = "S"; |HAZ MiraPaisAgicli;
   aCodigo = "AT"; aNombre = "AUSTRIA";      aExtr = "S"; aSEPA = "S"; |HAZ MiraPaisAgicli;
   aCodigo = "BE"; aNombre = "BELGICA";      aExtr = "S"; aSEPA = "S"; |HAZ MiraPaisAgicli;
   aCodigo = "CY"; aNombre = "CHIPRE";       aExtr = "S"; aSEPA = "S"; |HAZ MiraPaisAgicli;
   aCodigo = "DK"; aNombre = "DINAMARCA";    aExtr = "S"; aSEPA = "S"; |HAZ MiraPaisAgicli;
   aCodigo = "SI"; aNombre = "ESLOVENIA";    aExtr = "S"; aSEPA = "S"; |HAZ MiraPaisAgicli;
   aCodigo = "SK"; aNombre = "ESLOVAQUIA";   aExtr = "S"; aSEPA = "S"; |HAZ MiraPaisAgicli;
   aCodigo = "EE"; aNombre = "ESTONIA";      aExtr = "S"; aSEPA = "S"; |HAZ MiraPaisAgicli;
   aCodigo = "FI"; aNombre = "FINLANDIA";    aExtr = "S"; aSEPA = "S"; |HAZ MiraPaisAgicli;
   aCodigo = "BG"; aNombre = "BULGARIA";     aExtr = "S"; aSEPA = "S"; |HAZ MiraPaisAgicli;
   aCodigo = "FR"; aNombre = "FRANCIA";      aExtr = "S"; aSEPA = "S"; |HAZ MiraPaisAgicli;
   aCodigo = "GR"; aNombre = "GRECIA";       aExtr = "S"; aSEPA = "S"; |HAZ MiraPaisAgicli;
   aCodigo = "GB"; aNombre = "REINO UNIDO";  aExtr = "S"; aSEPA = "S"; |HAZ MiraPaisAgicli;
   aCodigo = "NL"; aNombre = "HOLANDA";      aExtr = "S"; aSEPA = "S"; |HAZ MiraPaisAgicli;
   aCodigo = "HU"; aNombre = "HUNGRIA";      aExtr = "S"; aSEPA = "S"; |HAZ MiraPaisAgicli;
   aCodigo = "IT"; aNombre = "ITALIA";       aExtr = "S"; aSEPA = "S"; |HAZ MiraPaisAgicli;
   aCodigo = "IE"; aNombre = "IRLANDA";      aExtr = "S"; aSEPA = "S"; |HAZ MiraPaisAgicli;
   aCodigo = "IS"; aNombre = "ISLANDIA";     aExtr = "S"; aSEPA = "S"; |HAZ MiraPaisAgicli;
   aCodigo = "LV"; aNombre = "LETONIA";      aExtr = "S"; aSEPA = "S"; |HAZ MiraPaisAgicli;
   aCodigo = "LT"; aNombre = "LITUANIA";     aExtr = "S"; aSEPA = "S"; |HAZ MiraPaisAgicli;
   aCodigo = "LU"; aNombre = "LUXEMBURGO";   aExtr = "S"; aSEPA = "S"; |HAZ MiraPaisAgicli;
   aCodigo = "MT"; aNombre = "MALTA";        aExtr = "S"; aSEPA = "S"; |HAZ MiraPaisAgicli;
   aCodigo = "PL"; aNombre = "POLONIA";      aExtr = "S"; aSEPA = "S"; |HAZ MiraPaisAgicli;
   aCodigo = "PT"; aNombre = "PORTUGAL";     aExtr = "S"; aSEPA = "S"; |HAZ MiraPaisAgicli;
   aCodigo = "CH"; aNombre = "SUIZA";        aExtr = "S"; aSEPA = "S"; |HAZ MiraPaisAgicli;
   aCodigo = "SE"; aNombre = "SUECIA";       aExtr = "S"; aSEPA = "S"; |HAZ MiraPaisAgicli;
   aCodigo = "CZ"; aNombre = "REPUB.CHECA";  aExtr = "S"; aSEPA = "S"; |HAZ MiraPaisAgicli;
   aCodigo = "HR"; aNombre = "CROACIA";      aExtr = "S"; aSEPA = "S"; |HAZ MiraPaisAgicli;
   aCodigo = "RO"; aNombre = "RUMANIA";      aExtr = "S"; aSEPA = "S"; |HAZ MiraPaisAgicli;
   aCodigo = "LI"; aNombre = "LIECHTENSTEIN";aExtr = "S"; aSEPA = "S"; |HAZ MiraPaisAgicli;
   aCodigo = "MC"; aNombre = "MONACO";       aExtr = "S"; aSEPA = "S"; |HAZ MiraPaisAgicli;
   aCodigo = "NO"; aNombre = "NORUEGA";      aExtr = "S"; aSEPA = "S"; |HAZ MiraPaisAgicli;
|FPRC;

|| **************************************************************************
|PROCESO Individual1;    ||Transferecnia Sepa
     |QBF eaCodBIC;
     |QBF eaCodIBAN;
     |QBF eaDireccion;
     |QBF eaPoblacion;
     |QBF eaProvincia;

     eaGrupo = "S";
     eaTag = "CdtTrfTxInf";|HAZ XmlTag;

     eaGrupo = "S";
     eaTag = "PmtId";|HAZ XmlTag;

||2.30
     |QBF eaNif;
     eaVal = eaNif + (fHoy % 102) + (fHoy % 402) + (fHoy % 704);
     eaVal = eaVal + (aHora % 102) + (aHora % 402) + (aHora % 702);
     eaVal = eaVal + (("00000" + #3#0) % -105) + (("000" + #3#1) % -103);
     eaTag = "EndToEndId"; |HAZ XmlTag;

     eaGrupo = "N"; |HAZ XmlTag;

     eaGrupo = "S";
     eaTag = "Amt";|HAZ XmlTag;

     nNume = #3#8; |HAZ NumDeci; eaVal = aAlfa;
     eaTag = "InstdAmt Ccy=" + &34 + "EUR" + &34; |HAZ XmlTag;

     eaGrupo = "N"; |HAZ XmlTag;
||2.77
     |SI eaCodBIC ! "";
          eaGrupo = "S";
          eaTag = "CdtrAgt";|HAZ XmlTag;

          eaGrupo = "S";
          eaTag = "FinInstnId";|HAZ XmlTag;

          eaVal = eaCodBIC;
          eaTag = "BIC"; |HAZ XmlTag;

          eaGrupo = "N"; |HAZ XmlTag;
          eaGrupo = "N"; |HAZ XmlTag;
     |FINSI;
||2.79
     eaGrupo = "S";
     eaTag = "Cdtr";|HAZ XmlTag;

     eaVal = eaNombre; |QBF eaVal;
     |SI eaVal ! "";
          eaTag = "Nm"; |HAZ XmlTag;
     |FINSI;

     eaGrupo = "S";
     eaTag = "PstlAdr";|HAZ XmlTag;

     eaVal = eaPais; |QBF eaPais;
     |SI eaPais = ""; eaVal = "ES"; |FINSI;
     eaTag = "Ctry"; |HAZ XmlTag;

     eaVal = eaDireccion; |QBF eaVal;
     |SI eaVal ! "";
          eaTag = "AdrLine"; |HAZ XmlTag;
     |FINSI;

     |SI eaPoblacion = "";
          |SI eaProvincia ! "";
               eaVal = eaProvincia;
               eaTag = "AdrLine"; |HAZ XmlTag;
          |FINSI;
     |SINO;
          |SI eaProvincia ! "";
               eaVal = eaPoblacion + ", " + eaProvincia;
               eaTag = "AdrLine"; |HAZ XmlTag;
          |SINO;
               eaVal = eaPoblacion;
               eaTag = "AdrLine"; |HAZ XmlTag;
          |FINSI;
     |FINSI;

     eaGrupo = "N"; |HAZ XmlTag;
     eaGrupo = "N"; |HAZ XmlTag;

     eaGrupo = "S";
     eaTag = "CdtrAcct";|HAZ XmlTag;

     eaGrupo = "S";
     eaTag = "Id";|HAZ XmlTag;

     eaVal = eaCodIBAN;
     eaTag = "IBAN"; |HAZ XmlTag;

     eaGrupo = "N"; |HAZ XmlTag;
     eaGrupo = "N"; |HAZ XmlTag;

     eaGrupo = "S";
     eaTag = "RmtInf";|HAZ XmlTag;

/*
     aAlfa = "FACTURA:" + #4#2; |QBF aAlfa;
     aAlfa = aAlfa + "/" + (("00000"+#4#1)%-105) + "-" + (("000"+#4#3)%-103);
     eaVal = #4#6; |HAZ FormaFecha;
     eaVal = aAlfa + " " + eaVal;
*/
     eaVal = aConce;
     eaTag = "Ustrd"; |HAZ XmlTag;

     eaGrupo = "N"; |HAZ XmlTag;
     eaGrupo = "N"; |HAZ XmlTag;
|FPRC;

|PROCESO Individual2;    ||Transferencias Otras
     |HAZ Individual1;
|FPRC;

|PROCESO Individual3;    ||Cheques
     |HAZ Individual1;
|FPRC;

|PROCESO InfoPago;
     nSwCab = 1;

     eaGrupo = "S";
     eaTag = "PmtInf";|HAZ XmlTag;

     fHoy = ~;
     |HORA aHora;
     aAlfa = #1#2; |QBF aAlfa;
     eaVal = (fHoy % 102) + (fHoy % 402) + (fHoy % 704);
     eaVal = eaVal + (aHora % 102) + (aHora % 402) + (aHora % 702);
     eaVal = eaVal + aAlfa + "001";
     eaTag = "PmtInfId"; |HAZ XmlTag;

     |SI nVuelta = 3; eaVal = "CHK"; |SINO; eaVal = "TRF"; |FINSI;
     eaTag = "PmtMtd"; |HAZ XmlTag;

     eaVal = #0#15; |HAZ FormaFecha;
     eaTag = "ReqdExctnDt"; |HAZ XmlTag;

     eaGrupo = "S";
     eaTag = "Dbtr"; |HAZ XmlTag;

     eaVal = #1#3; |QBF eaVal;
     |SI eaVal ! "";
          eaTag = "Nm"; |HAZ XmlTag;
     |FINSI;

     eaGrupo = "S";
     eaTag = "PstlAdr"; |HAZ XmlTag;

     eaVal = #1#27;
     eaTag = "Ctry"; |HAZ XmlTag;

     eaVal = #1#10; |QBF eaVal;
     |SI eaVal ! "";
          eaTag = "AdrLine"; |HAZ XmlTag;
     |FINSI;

     eaVal =  #1#7; |QBF eaVal;
     |SI eaVal ! "";
          eaTag = "AdrLine"; |HAZ XmlTag;
     |FINSI;

     eaGrupo = "N"; |HAZ XmlTag;
     eaGrupo = "N"; |HAZ XmlTag;
||2.20  ????
     eaGrupo = "S";
     eaTag = "DbtrAcct"; |HAZ XmlTag;

     eaGrupo = "S";
     eaTag = "Id"; |HAZ XmlTag;

     aAlfa = #1#30; |QBF aAlfa;
     |SI aAlfa ! "";
          eaVal = aAlfa;
          eaTag = "IBAN"; |HAZ XmlTag;
     |SINO;
          eaGrupo = "S";
          eaTag = "Othr"; |HAZ XmlTag;

          aPais = "ES";
          aNif = #1#2;
          aSufijo = #1#26; |QBF aSufijo; aSufijo = ("000" + aSufijo) % -103;
          |HAZ ObtenId;
          eaVal = aId;
          eaTag = "Id"; |HAZ XmlTag;

          eaGrupo = "N"; |HAZ XmlTag;
     |FINSI;
     eaGrupo = "N"; |HAZ XmlTag;
     eaGrupo = "N"; |HAZ XmlTag;

     eaGrupo = "S";
     eaTag = "DbtrAgt"; |HAZ XmlTag;

     eaGrupo = "S";
     eaTag = "FinInstnId"; |HAZ XmlTag;

/*
     aAlfa = #1#29; |QBF aAlfa;
     |SI aAlfa ! "";
          eaVal = aAlfa;
          eaTag = "BIC"; |HAZ XmlTag;
     |FINSI;

     eaGrupo = "S";
     eaTag = "Othr"; |HAZ XmlTag;

     eaVal = #1#2;
     eaTag = "Id"; |HAZ XmlTag;
*/
     eaVal = #1#29; |QBF eaVal;
     |SI eaVal ! "";
          eaTag = "BIC"; |HAZ XmlTag;
     |SINO;
          eaGrupo = "S";
          eaTag = "Othr"; |HAZ XmlTag;

          eaVal = "NOTPROVIDED";
          eaTag = "Id"; |HAZ XmlTag;

          eaGrupo = "N"; |HAZ XmlTag;
     |FINSI;

||     eaGrupo = "N"; |HAZ XmlTag;
     eaGrupo = "N"; |HAZ XmlTag;
     eaGrupo = "N"; |HAZ XmlTag;
|FPRC;

|PROCESO Concepto;
     aConce1 = "";
     aAlfa = #1#11; |QBF aAlfa;
     |SI aAlfa ! ""; |Y #1#21 = "S";
          aConce1 = aAlfa;
          |SI #1#12 = "S";
              |SI #camanpag#2 ! "  ";
                  aConce1 = aConce1 + #camanpag#2 + "/"; |QBF aConce1;
              |FINSI;
              aConce1 = aConce1 + #camanpag#1; |QBF aConce1;
              aConce1 = aConce1 + " ";
          |FINSI;
          |SI #1#13 = "S";
               aConce1 = aConce1 + (("000"+#camanpag#3)%-103);
               aConce1 = aConce1 + " ";
          |FINSI;
          |SI #1#14 = "S";
               aConce1 = aConce1 + (#camanpag#6%102)+(#camanpag#6%402)+(#camanpag#6%704);
          |FINSI;
     |FINSI;

     aConce2 = "";
     aAlfa = #1#16; |QBF aAlfa;
     |SI aAlfa ! ""; |Y #1#21 = "S";
          aConce2 = aAlfa;
          |SI #1#17 = "S";
              |SI #camanpag#2 ! "  ";
                  aConce2 = aConce2 + #camanpag#2 + "/"; |QBF aConce2;
              |FINSI;
              aConce2 = aConce2 + #camanpag#1; |QBF aConce2;
              aConce2 = aConce2 + " ";
          |FINSI;
          |SI #1#18 = "S";
               aConce2 = aConce2 + (("000"+#camanpag#3)%-103);
               aConce2 = aConce2 + " ";
          |FINSI;
          |SI #1#19 = "S";
               aConce2 = aConce2 + (#camanpag#6%102)+(#camanpag#6%402)+(#camanpag#6%704);
          |FINSI;
     |FINSI;

     aAlfa = aConce1 + " " + aConce2;

     aAlfa = "EN CONCEPTO PAGO " + #camanpag#28; |QBF aAlfa;  || tique 500005_109

     aLon = aAlfa % A0; nLon = aLon; nLon = 140 - nLon;
     aConce = aAlfa + (" " * nLon);       || aqui tenemos 140 como maximo

     |SI nVuelta = 1; || long=140
         ||los que trae
     |FINSI;
     |SI nVuelta = 2; || long=72
          aConce = aConce % 172;
     |FINSI;
|FPRC;

|PROCESO Cabecera;
     eaGrupo = "S";
     eaTag = "CstmrCdtTrfInitn";|HAZ XmlTag;

     eaGrupo = "S";
     eaTag = "GrpHdr"; |HAZ XmlTag;

     eaVal = ("00000" + #2#0) % -105;
     eaTag = "MsgId"; |HAZ XmlTag;

     |HAZ HoraActual; eaVal = aAlfa;
     eaTag = "CreDtTm"; |HAZ XmlTag;

     nNume = nRegistros1 + nRegistros2 + nRegistros3;
     eaVal = nNume;
     eaTag = "NbOfTxs"; |HAZ XmlTag;

     nNume = nTotal1 + nTotal2 + nTotal3; |HAZ NumDeci;
     eaVal = aAlfa;
     eaTag = "CtrlSum"; |HAZ XmlTag;

     eaGrupo = "S";
     eaTag = "InitgPty"; |HAZ XmlTag;

     eaVal = #1#3; |QBF eaVal;
     |SI eaVal ! "";
          eaTag = "Nm"; |HAZ XmlTag;
     |FINSI;

     aAlfa = #1#2; |QBF aAlfa; aAlfa = aAlfa % 0101;
     |SI aAlfa ] "A"; |Y aAlfa [ "W";
          nPersona = 1; || Persona juridica
     |SINO;
          nPersona = 2; || Persona fisica
     |FINSI;

     eaGrupo = "S";
     eaTag = "Id"; |HAZ XmlTag;

     |SI nPersona = 1;
          eaGrupo = "S";
          eaTag = "OrgId"; |HAZ XmlTag;

          eaGrupo = "S";
          eaTag = "Othr"; |HAZ XmlTag;

          aPais = "ES";
          aNif = #1#2;
          aSufijo = #1#26; |QBF aSufijo; aSufijo = ("000" + aSufijo) % -103;
||           |HAZ ObtenId;
||           eaVal = aId;
          eaVal = aNif + aSufijo;
          eaTag = "Id"; |HAZ XmlTag;

          eaGrupo = "N"; |HAZ XmlTag;
          eaGrupo = "N"; |HAZ XmlTag;
     |SINO;
          eaGrupo = "S";
          eaTag = "PrvtId"; |HAZ XmlTag;

          eaGrupo = "S";
          eaTag = "Othr"; |HAZ XmlTag;

          aPais = "ES";
          aNif = #1#2;
          aSufijo = #1#26; |QBF aSufijo; aSufijo = ("000" + aSufijo) % -103;
||           |HAZ ObtenId;
||           eaVal = aId;
          eaVal = aNif + aSufijo;
          eaTag = "Id"; |HAZ XmlTag;

          eaGrupo = "N"; |HAZ XmlTag;
          eaGrupo = "N"; |HAZ XmlTag;
     |FINSI;

     eaGrupo = "N"; |HAZ XmlTag;
     eaGrupo = "N"; |HAZ XmlTag;
     eaGrupo = "N"; |HAZ XmlTag;
|FPRC;

|PROCESO NumDeci;
     nNume = (nNume * 100) ! 0;
     aAlfa = nNume;
     aLon = aAlfa % A0; nLon = aLon;
     aDeci = aAlfa % -102;
     nLon = nLon + 100 - 2;
     aAlfa = (aAlfa % nLon) + "." + aDeci;
|FPRC;

|PROCESO FormaFecha;
     eaVal = (eaVal % 704) + "-" + (eaVal % 402) + "-" + (eaVal % 102);
|FPRC;

|PROCESO HoraActual;
     fHoy = ~;
     |HORA aAlfa;
     aAlfa = (fHoy % 704) + "-" + (fHoy % 402) + "-" + (fHoy % 102) + "T" + aAlfa;
|FPRC;

|PROCESO ObtenId;
     |PARA FSalida = 1; |SI FSalida ! 0; |HACIENDO; aNif = aNif - "/"; |SIGUE;
     |PARA FSalida = 1; |SI FSalida ! 0; |HACIENDO; aNif = aNif - "-"; |SIGUE;
     |PARA FSalida = 1; |SI FSalida ! 0; |HACIENDO; aNif = aNif - "?"; |SIGUE;
     |PARA FSalida = 1; |SI FSalida ! 0; |HACIENDO; aNif = aNif - ":"; |SIGUE;
     |PARA FSalida = 1; |SI FSalida ! 0; |HACIENDO; aNif = aNif - "("; |SIGUE;
     |PARA FSalida = 1; |SI FSalida ! 0; |HACIENDO; aNif = aNif - ")"; |SIGUE;
     |PARA FSalida = 1; |SI FSalida ! 0; |HACIENDO; aNif = aNif - "."; |SIGUE;
     |PARA FSalida = 1; |SI FSalida ! 0; |HACIENDO; aNif = aNif - ","; |SIGUE;
     |PARA FSalida = 1; |SI FSalida ! 0; |HACIENDO; aNif = aNif - "'"; |SIGUE;
     |PARA FSalida = 1; |SI FSalida ! 0; |HACIENDO; aNif = aNif - "+"; |SIGUE;
     |PARA FSalida = 1; |SI FSalida ! 0; |HACIENDO; aNif = aNif - &34; |SIGUE;

     aAlfa = aNif + aPais + "00";

     aAlfa = aAlfa > aAlfa;
     aCar = ""; aCadena = "";
     |PARA; |SI aAlfa ! ""; |HACIENDO;
          aCar = aAlfa % 0101; aAlfa = aAlfa % 0299;
          |SI aCar ] "0"; |Y aCar [ "9";
               aCadena = aCadena + aCar;
          |SINO;
               |SI aCar ] "A"; |Y aCar [ "Z"; |Y aCar ! "Ñ";
                    nNume = &aCar;
                    nNume = nNume - 55;
                    aCar = nNume;
                    aCadena = aCadena + aCar;
               |FINSI;
          |FINSI;
     |SIGUE;
     aAlfa = aCadena;
     |HAZ Mod_9710;
     aId = aPais + aAlfa + aSufijo + aNif;
|FPRC;

|PROCESO Mod_9710;
     aAlfa2 = aAlfa % 0101;
     |PARA; |SI aAlfa2 = "0"; |HACIENDO;
          aAlfa = aAlfa % 0299; aAlfa2 = aAlfa % 0101;
     |SIGUE;

     aAlfa1 = "";
     |PARA; |SI aAlfa ! ""; |HACIENDO;
          aAlfa2 = aAlfa % 0; nNume = aAlfa2;
          |SI nNume > 5;
               aAlfa2 = aAlfa % 0105;
               aAlfa = aAlfa % 0699;
          |SINO;
               aAlfa2 = aAlfa;
               aAlfa = "";
          |FINSI;
          aAlfa1 = aAlfa1 + aAlfa2;
          nNume = aAlfa1;
          nNume = nNume / 97;
          aAlfa2 = nNume;
          aAlfa2 = aAlfa2 - ".";
          |SI FSalida ! 0;
               nNume = FSalida + 98;
               aAlfa2 = aAlfa2 % nNume;
               aAlfa2 = "0." + aAlfa2;
               nNume = aAlfa2;
          |SINO;
               nNume = 0;
          |FINSI;
          nNume = (nNume * 97) ! 0;
          aAlfa1 = nNume;
     |SIGUE;
     nNume  = aAlfa1;
     nNume = 98 - nNume;
     aAlfa = ("00" + nNume) % -102;
||nNume  = 98 - (nNume - (((nNume / 97) ! 0) * 97)); || MOD 97-10  ISO 7604
|FPRC;

|PROCESO ProcRecXML;
     swERROR = 0;
     |HAZ leecl_y_cobro;
     |SI swERROR = 0;
          eaNifProv = NifProv;
          eaDirProv = DirecProv;
          |HAZ Concepto;

          |SI nVuelta = 1; || Transferencias SEPA
               |SI nSituacionSEPA = 1; |O nSituacionSEPA = 2;
                    |SI #camingli#12 = "T";
                         |SI nSwCuenta = 1;
                              nRegistros1 = nRegistros1 + 1;
                              nTotal1 = nTotal1 + #camingli#8;
                         |SINO;
                              |SI nSwCab = 0; |HAZ InfoPago; |FINSI;
                              |HAZ Individual1;
                         |FINSI;
                    |FINSI;
               |FINSI;
          |FINSI;
          |SI nVuelta = 2; || Transferencias No SEPA
               |SI #camingli#12 = "T"; |Y nSituacionSEPA = 3;
                    |SI nSwCuenta = 1;
                         nRegistros2 = nRegistros2 + 1;
                         nTotal2 = nTotal2 + #camingli#8;
                    |SINO;
                         |SI nSwCab = 0; |HAZ InfoPago; |FINSI;
                         |HAZ Individual2;
                    |FINSI;
               |FINSI;
          |FINSI;
          |SI nVuelta = 3; || Cheques
               |SI #camingli#12 = "C";
                    |SI nSwCuenta = 1;
                         nRegistros3 = nRegistros3 + 1;
                         nTotal3 = nTotal3 + #camingli#8;
                    |SINO;
                         |SI nSwCab = 0; |HAZ InfoPago; |FINSI;
                         |HAZ Individual3;
                    |FINSI;
               |FINSI;
          |FINSI;
     |FINSI;

|FPRC;


|PROCESO ProcesaRecXML;
     nSwCuenta = 1;
     |PARA nVuelta = 1; |SI nVuelta [ 3; |HACIENDO nVuelta = nVuelta + 1;
          |BUCLELIN 0ProcRecXML#2;
     |SIGUE;

     |HAZ Cabecera;

     nSwCuenta = 2;
     |PARA nVuelta = 1; |SI nVuelta [ 3; |HACIENDO nVuelta = nVuelta + 1;
          nSwCab = 0;
          |BUCLELIN 0ProcRecXML#2;
     |SIGUE;
|FPRC;
|| **************************************************************************
|PROCESO pasecuencial;

  |ABRE #n34sepam;
  #n34sepam#0 = #n34sepap#2;
  |LEE 000#n34sepam.=;
  |SI FSalida ! 0;
     |DDEFECTO #n34sepam;
  |FINSI;
  |CIERRA #n34sepam;

  |ABRE #camingca;
  |HAZ varia_fich;

  |C_V #n34sepap#10,1,"S"; #n34sepap#10 = 0;
  |C_V #n34sepap#11,1,"S"; #n34sepap#11 = 0;
  |C_V #n34sepap#12,1,"S"; #n34sepap#12 = 0;

|| ...................................................
  |ABRE #camanpag;         || Abre Mantenimiento de efectos
  |ABRE #caclipro;

  nSwPaises = 0;
  |DBASS aAlfa; |QBF aAlfa;
  aPath = aAlfa;
  aAlfa = aPath + "agicli/";
  aAlfa1 = aAlfa + "def/agim0016.mas";
  |CARGA_DEF aAlfa1, agim0016@;
  |SI FSalida = 0;
     aAlfa1 = aAlfa + "fich/";
     |PATH_DAT #agim0016@#aAlfa1#;
     |ABRE #agim0016@;

     |HAZ CompruebaPaisesAgicli;
     nSwPaises = 1;
  |FINSI;

  |SI #0#17 = "T";
    || Abrir Fichero Secuencial

      |DFICE dirempr;
      fiche = "wb  " + dirempr + "casecmin.seq";
      |QBF fiche;
      |FABRE fiche;
      handle = FSalida;
      |SI FSalida ] 0;
         |HAZ ProcesaRecibos;
      |FINSI;
      contenido = handle + " " + &#0#9;
      |FGRABA contenido;

      FSalida = handle;
      |FCIERRA FSalida;

      Pos = -1;
  |SINO;
      |DFICE Comodin;
      Comodin1 = ("00000" + #0#1) % -105;
      Comodin1 = "n34" + Comodin1 + ".xml";
      eaOrigen = Comodin;
      eaNombre1 = Comodin1;
      eaFichero = Comodin + Comodin1;
      eaPain = "001.001.03";
      |HAZ XmlIni;
      |SI FSalida ] 0;
          |HAZ ProcesaRecXML;
          |HAZ XmlFin;
          trecibos = nRegistros1 + nRegistros2 + nRegistros3;
          timporte = nTotal1 + nTotal2 + nTotal3;
          l        = trecibos;
      |FINSI;
  |FINSI;

  |SI nSwPaises = 1;
      |CIERRA #agim0016@;
      |DESCARGA_DEF #agim0016@;
  |FINSI;
  |CIERRA #caclipro;
  |CIERRA #camanpag;

|| ...   |SI swERROR = 1; |ACABA; |FINSI;

  |SI trecibos ! 0;
      #camingca#0 = #n34sepap#1;
      |LEE 101#camingca.=;
      |SI FSalida = 0;
         #camingca#7 = "D";
         |GRABA 020#camingca.a;
         |LIBERA #camingca;
      |FINSI;
  |FINSI;
  |CIERRA #camingca;

  |BLANCO 1258 2280;
  |D_CUADRO 1258, 2280;
  |ATRI R;
  |PINTA 1359, " Numero Recibos     ";
  |PINTA 1559, " Numero Registros   ";
  |PINTA 1759, " Importe Total      ";
  |PINTA 1959, " Copiar a ubicacion ";
  |ATRI 0;
  |PINTA 1459, "                    ";
  |PINTA 1659, "                    ";
  |PINTA 1859, "                    ";
  |ATRI 0;

  #n34sepap#10 = trecibos;  |PINTA #n34sepap#10;
  #n34sepap#11 = l;         |PINTA #n34sepap#11;
  #n34sepap#12 = timporte;  |PINTA #n34sepap#12;

  E_sup = 1;
  E_inf = "SNsn";
  sino  = "S";
  sino  = 2068 ? 1;
  sino  = sino > " ";
  |SI sino = "S";
     |DFICE eaOrigen; |QBF eaOrigen;
     |SI #0#17 = "T";
         eaNombre1 = "casecmin.seq";
     |SINO;
         SwXml = 1;
     |FINSI;
     |SUB_EJECUTA "cacopdis";
  |FINSI;
|FPRC;

|PROGRAMA;
   aAlfa = Usuario;
   |NOME_DAT #n34sepat#aAlfa#;
   |DELINDEX #n34sepat;
   |ABRE #n34sepat;

   |CLS;
   |CABEZA " Emision normas SEPA: Norma 34.14";
   |PINPA #0#0; |PINDA #0#0;
   |ENDATOS #1#0;
   |SI FSalida = 0;

      nSwImpre = 0;
      |HAZ pasecuencial;
      |SI nSwImpre = 1;
          |PIEINF;
          |FININF;
          |FINIMP;
          nSwImpre = 0;
      |FINSI;
   |FINSI;

   |CIERRA #n34sepat;
   |DELINDEX #n34sepat;
|FPRO;
