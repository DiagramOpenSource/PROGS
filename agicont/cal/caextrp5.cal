|FICHEROS;
   caextrp5 #4;         || display extractos
   cacuenta #5;         || cuentas
   caextrp4 #6;         || display asientos completo
   camaasil #7;         || apuntes lins.
   caextras #8;         || fichero anot.extracontables
   caextral #9;         || anot.extracontables Lineas
   caexttip #10;        || Tipos anot.extracontables

   imagen@  #999;
   canempre #30;
|FIN;

|VARIABLES;
   aFichero = "";
   alfa1 = "";
   alfa2 = "";
   alfa3 = "";
   alfa4 = "";
   nume1 = 0;
   nume2 = 0;
   nume3 = 0;
   nume4 = 0;
   fech1 = @;
   fech2 = @;
   fech3 = @;
   fech4 = @;
   FEstado = 0;
   inkey = 0;
   seleccio = 0;

   &d_abajo = 0;
   &h_abajo = 0;
   &s_abajo = 0;
   totallin = 0;
   lalinea = 0;
   nDebe   = 0;
   nHaber  = 0;
   nApunte = 0;
   aCuenta = "";
   informe = "caextrp5";

   &aFitxer = "";
   aMas = "";
   &enDi = 0;
   &efFe = @;
   &enDo = 0;
   &enAp = 0;
   &eaDes = "";
   &eaD = "";
   &eaS = "";
   &enDeb = 0;
   &enHar = 0;
   &enSal = 0;
   &enTD  = 0;
   &enTH  = 0;
   &enTS  = 0;
   nPrim  = 0;

   &swVarios     = 0;
   &eaMonOrg     = "";
   aBlancs       = "";

   &enEjercicio = 0;
   &dirfich0    = "";
   &impMoneda   = 0;
   &mulMoneda   = 0;
   &eaMoneda    = "";
   &eaConDes    = "";
   &enEmpresa   = 0;
   &enPeriodo   = 0;
   &eaFEmp      = "";
   &ext2        = "";
   &eaParam     = "";
   &enEmpOrg    = 0;
   &enPerOrg    = 0;
|FIN;

|PROCESO Eltipo71; |TIPO 7;
||  |SI eaParam ! "001496";
||      |MENSA "    F2-Consulta Asto;F6-Anotaciones Extracontable";
||  |SINO;
||      |MENSA "    F2-Consulta Asto;F3-Informe;F6-Anotaciones Extracontable";
||  |FINSI;
|FPRC;

|PROCESO pinta113; |TIPO 13;
   |C_V #4#27,0,alfa4;
   |SI alfa4 = "S";
       |ATRI P; |PINTA 1008, "Ej:"; |ATRI 0;
       || ... |ATRI I; |PINTA 1070, "Emp.:"; |ATRI 0;
   |FINSI;
   |HAZ totales1;
|FPRC;

|PROCESO pintando1; |TIPO 11;
   inkey = FSalida;
||    |HAZ totales1;
   |SI swVarios = 1; |HAZ Situafich1; |FINSI;
   |SI inkey = 10;  |HAZ asient1;       |FINSI;
   |SI inkey = 11;
||       |Y eaParam = "001496";
       |HAZ ElInform1;
   |FINSI;
   |SI inkey = 14;  |HAZ extraco1;      |FINSI;
|FPRC;

_____________________________________________________________________________
|PROCESO totales1;                  || PINTA LOS TOTALES DEL EXTRACTO
   #4#14 = d_abajo;
   #4#15 = h_abajo;
   #4#16 = s_abajo;
   |PINTA #4#14;
   |PINTA #4#15;
   |PINTA #4#16;
|FPRC;
_____________________________________________________________________________
|PROCESO lee_asient11;             || CARGA LA PANTALLA DEL ASIENTO COMPLETO
   seleccio = 1;
   |HAZ cuenta1;
   |SI eaMonOrg ! eaMoneda;  |HAZ LoCambio;  |FINSI;

   |DDEFECTO #6;
   #caextrp4#00 = #7#3;        || apunte
   #caextrp4#01 = #7#16;       || cuenta DEBE
   #caextrp4#02 = #7#17;       || digito DEBE
   #caextrp4#03 = #7#10;       || cuenta HABER
   #caextrp4#04 = #7#11;       || digito HABER
   #caextrp4#05 = #7#6;        || concepto
   #caextrp4#06 = #7#7;        || descripcion
   |SI eaConDes ! "N";
       alfa1 = #7#24; |QBF alfa1;
       #caextrp4#6 = alfa1 + " " + #7#7;
   |FINSI;
   #caextrp4#07 = #7#21;       || departamento
   #caextrp4#08 = #7#9;        || importe
   #caextrp4#09 = #cacuenta#2;        || descripcion
   #caextrp4#10 = #cacuenta#51;       || debe
   #caextrp4#11 = #cacuenta#52;       || haber
   #caextrp4#12 = #cacuenta#53;       || saldo
   #caextrp4#13 = #7#0;        || diario
   #caextrp4#14 = #7#1;        || fecha
   #caextrp4#15 = #7#2;        || documento
   |SI #caextrp4#0 = #4#19;
      lalinea = #caextrp4#0;
   |FINSI;
   |GRABA 020#6n;
   totallin = totallin + 1;
|FPRC;

|DEFBUCLE lee_asient1;
   #7#1;
   ;
   #4#18, #4#8, #4#9,    1;
   #4#18, #4#8, #4#9, 9999;
   e;
   NULL, lee_asient11;
|FIN;

|PROCESO asient1;
   aFichero = ("r" + Usuario);
   aFichero = (aFichero + "zzzzzzzz") % 108;
   |NOME_DAT #6 aFichero;
   |DELINDEX #6;

   seleccio = 0;

   |ABRE #6;
   lalinea = 1;
   totallin = 0;
   |BUCLE lee_asient1;
   |CIERRA #6;
   |SI seleccio ! 0;
      |PUSHV 07012380;
      |ENTLINEAL #1#6, -1, 4, 20, 1, mini1, maxi1;
      |POPV;
   |SINO;
      |MENAV "    Seleccion vacia ...";
   |FINSI;

   |DELINDEX #6;

|FPRC;

|PROCESO mini1;
   |SI FSalida = "POSICION";  |Y totallin > 8;
      #caextrp4#0 = lalinea;
   |SINO;
      #caextrp4#0 = 1;
   |FINSI;
|FPRC;

|PROCESO maxi1;
   #caextrp4#0 = 9999;
|FPRC;

|PROCESO cuenta1;
   |ABRE #5;
   |SELECT #1#5;
   #cacuenta#0 = #7#4;         || cuenta
   #cacuenta#1 = #7#5;         || digito
   |LEE 001#5=;
   |SI FSalida ! 0;
      |DDEFECTO #5;
      #cacuenta#2 = "<CUENTA INEXISTENTE>";
   |FINSI;
   |CIERRA #5;
|FPRC;
_____________________________________________________________________________
|PROCESO extraco1;            || CARGA LA PANTALLA DE LA ANOT.EXTRACONTABLE
   |ABRE #8;
   #caextras#0 = #4#18;        || diario
   #caextras#1 = #4#8;         || fecha
   #caextras#2 = #4#9;         || documento
   |LEE 001#8=;
   |SI FSalida = 0;
      |PUSHV 08092067;
      |PINPA #1#4;
      |ATRI I; |PINTA 0927, #caextras#0;
      |PINTA 0937, #caextras#1;
      |PINTA 0959, #caextras#2;
      |PINTA 1128, #caextras#3;
      |ATRI 0;
      |HAZ mod_campos1;
      |ENTLINEAL #1#9, -4, 4, 18, 0, minA1, maxA1;
      |POPV;
   |SINO;
      |MENAV "    Asiento sin Anotaciones Extracontables ...";
   |FINSI;
   |CIERRA #8;
|FPRC;

|RUTINA minA1;
   #caextral#0 = #caextras#0;
   #caextral#1 = #caextras#1;
   #caextral#2 = #caextras#2;
   #caextral#3 = 1;
|FRUT;

|RUTINA maxA1;
   #caextral#0 = #caextras#0;
   #caextral#1 = #caextras#1;
   #caextral#2 = #caextras#2;
   #caextral#3 = 9999;
|FRUT;

|PROCESO mod_campos1;
   |C_M #caextral#4,1,"N";
   |C_P #caextral#3, 1, 1311;
   |C_P #caextral#4, 1, 1316;
   |ABRE #10;
   #caexttip#0 = #caextras#3;
   |LEE 001#10=;
   |SI FSalida = 0;
      |PINTA 1131, #caexttip#1;
   |FINSI;
   |CIERRA #10;
|FPRC;
|| --------------------------------------------------------------------
|PROCESO LeeAuxilia1;
 enDi  = #999#18;
 efFe  = #999#8;
 enDo  = #999#9;
 enAp  = #999#19;
 eaDes = #999#10;
 eaD   = #999#20;
 eaS   = #999#21;
 |SI #999#11 = "D";
     enDeb = #999#12;
     enHar = 0;
 |SINO;
     enHar = #999#12;
     enDeb = 0;
 |FINSI;
 enSal = #999#13;
 |SI nPrim = 0;
     nPrim = 1;
     enTD = #999#5 - enDeb;
     enTH = #999#6 - enHar;
 |FINSI;

 |PRINT;
 enTD = enTD + enDeb;
 enTH = enTH + enHar;
 enTS = enTD - enTH;
|FPRC;

|DEFBUCLE LeeAuxilia1;
 #999#2006;
 ;
 "01.01.0000", 00, 000001, 0001, 00001, 000001;
 "31.12.2999", 99, 999999, 9999, 99999, 999999;
 ;
 NULL, LeeAuxilia1;
|FIN;

|PROCESO ElInform1;
 |ABRE #30;
 |DDEFECTO #30;
 #30#2 = enEmpOrg;
 #30#3 = enPerOrg;
 |LEE 001#30=;
 |CIERRA #30;
 |SI #30#26 > 80;
     enEjercicio = 1900 + #30#26;
 |SINO;
     enEjercicio = 2000 + #30#26;
 |FINSI;

 |PATH_DAT #5 eaFEmp;
 |ABRE #5;
 |DDEFECTO #5;
 #5#0 = #4#1;
 #5#1 = ext2;
 |LEE 001#5=;
 |CIERRA #5;

 |DBASE alfa1;
 aMas  = alfa1 + "def/caextrp5.mas";

 |CARGA_DEF aMas, imagen@;
 |SI FSalida = 0;

     |IMPRE 0;
     |SI FSalida ! 0; |ACABA; |FINSI;

     |INFOR informe;
     |SI FSalida ! 0; |FINIMP; |ACABA; |FINSI;

     |NOME_DAT #999 aFitxer;
     enTD = 0;
     enTH = 0;
     enTS = 0;
     nPrim = 0;

     |BUCLE LeeAuxilia1;
     |PIEINF;

     |FININF;
     |FINIMP;
     |DESCARGA_DEF #imagen@;
 |FINSI;
 |PATH_DAT #5 dirfich0;
|FPRC;

|PROCESO Situafich1;
   enEmpresa = #4#23;
   enPeriodo = #4#24;
   dirfich0 = #4#22; |QBF dirfich0;
   eaMoneda = #4#25;
   |PATH_DAT #5 dirfich0;
   |PATH_DAT #7 dirfich0;
   |PATH_DAT #8 dirfich0;
   |PATH_DAT #9 dirfich0;
   |PATH_DAT #10 dirfich0;
|FPRC;
