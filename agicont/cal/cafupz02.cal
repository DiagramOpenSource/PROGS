|FICHEROS;
  cafupz02 #0;

  cafupm01 #1;
  cafupm02 #2;
  cafupm03 #3;
  cafupm04 #4;
  cafupm05 #5;

  catradia #9;
  camandia #10;
  cacuenta #15;
  camaasil #31;
  camaasic #32;

  catraci@ #998;
  catracie #999;

  canempre #30;
|FIN;


|VARIABLES;

    &entrada = 1;
    &enEjercicio = 0;
    &enAny       = 0;
    &eaNombreEmp = "";
    &eaDirDestino = "";
    &eaDirOrigen  = "";
    &eaCuenta = "";
    &enNivel = 0;
    &enDef = 0;

    nEstado  = 0;
    nInkey   = 0;
    nModo    = 0;
    aLong    = "";
    nLong    = 0;
    aFichero = "";

    &swVarios   = 0;
    &enDDiario  = 0;
    &enHDiario  = 0;
    &efDFechas  = @;
    &efHFechas  = @;
    &enDDocume  = 0;
    &enHDocume  = 0;
    &enBorraAu  = 0;

    &nMaxOri = 0;
    &nMaxDes = 0;
    {-1}punt1 = 0;
    ddig3 = 0;
    ddig4 = 0;
    ddig5 = 0;
    ddig6 = 0;
    ddig7 = 0;
    ddig8 = 0;
    ddig9 = 0;

    aBase    = "";
    aDef     = "";
    nSwError = 0;
    nSwNO    = 0;
    nSwExis  = 0;

    aAlfa1   = "";
    aAlfa2   = "";
    nPara1   = 0;
    nNume1   = 0;
    nPerCon  = 0;
    nImporte = 0;

    &enContador = 0;
    &enDiario   = 0;
    nSwAlgun    = 0;
    sw          = 0;
    nLinea      = 0;

    &modcon  = 0;
    ini      = 0;
    fecalf   = "";
    mes      = "";
    mesfec   = 0;
    mesac    = 0;
    a = 0;
    b = 0;
    c = 0;
|FIN;

|INCLUYE acceso_i;
|INCLUYE condes_i;
|INCLUYE puntos_i;


|| /////////////////////// PROCESOS DE TRABAJO \\\\\\\\\\\\\\\\\\\\
|PROCESO TrasDefin; |TIPO 0;
 |C_M #0#5, 0, aAlfa1;
 |SI aAlfa1 = "N"; |ACABA; |FINSI;

 |HAZ LeeDefinicion;
 |SI nSwError = 1;
     |ERROR;
     |ACABA;
 |FINSI;
 |SI nSwError = 2;
     |CONFI "    SDesea realizar otra vez la consolidacion, borrando el anterior";
     |SI FSalida ! 0;
         |ERROR;
         |ACABA;
     |FINSI;
 |FINSI;
 |PINTA #0#6;
 |PINTA #0#7;
 |PINTA #0#8;
 |PINTA #0#9;
 |PINTA #0#12;
 |PINTA #0#13;
 |PINTA #0#14;
 |PINTA #0#16;
|FPRC;

|PROCESO fecha;
  |SI #0Campo < fec1; |O #0Campo > fec2;
      |MENAV "     Fecha ERRONEA ";
      |ERROR;
  |FINSI;
|FPRC;

|PROCESO entrada; |TIPO 0;
  |SI FSalida = 2; |ACABA; |FINSI;

  enDiario = #0#7;
  |HAZ contador;

  #0#9 = enContador;
  |PINTA #0#9;
|FPRC;

|PROCESO LeeDefinicion;
 nSwError = 0;
 |ABRE #1;
 #1#0 = #0#5;
 |LEE 001#1=;
 |SI FSalida ! 0;
     nSwError = 1;
 |SINO;
     |SI #1#3 > "01.01.0000";
         #0#7 = #1#2;
         #0#8 = #1#3;
         #0#9 = #1#4;
         #0#12 = #1#7;
         #0#13 = #1#8;
         #0#14 = #1#9;
         #0#15 = #1#10;
         #0#16 = #1#11;
         nSwError = 2;
    |FINSI;
    #0#6 = #1#1;
 |FINSI;
 |CIERRA #1;
|FPRC;

|PROCESO final816; |TIPO 7;
  |HAZ VerCaconimp;
  |SI eaConDes ! "N";
      |PINTA 1739, #0Campo;
      #0Campo = "";
  |FINSI;
  |PTEC 816;
|FPRC;

|PROCESO cuenta;|TIPO 6;
    aAlfa1 = #0Campo;
    |QBF aAlfa1;
    |SI aAlfa1 = "";
        |CONFI "    SCta en Blanco, el asiento puede quedar Descuadrado. Continuar";
        |SI FSalida ! 0;
            |ERROR;
       |FINSI;
       |ACABA;
    |FINSI;

    p_alfa1 = #0Campo;
    salidas = FSalida;
    |HAZ punto;
    |SI p_error ! 0;  |ERROR;  |ACABA;  |FINSI;
    eaCuenta = #0#14;
    |HAZ Sacanivel;
    |SI enNivel = 0;
       |MENAV "     Longitud de Cuenta Fuera de Nivel !";
       |ERROR;
    |FINSI;
    #0#15 = enNivel;
|FPRC;

|PROCESO cuefin; |TIPO 0;
    aAlfa1 = #0Campo;
    |QBF aAlfa1;
    |SI aAlfa1 = "";
        |ACABA;
    |FINSI;
    |ABRE #15;
    #15#0 = #0#14;
    #15#1 = #0#15;
    |LEE 001#15=;
    |SI FSalida ! 0;
        |MENAV "    Error. No Existe Cuenta Contable";
        |ERROR;
    |SINO;
        #0#16 = #15#2; |PINTA #0#16;
        |SI #15#3 ! "S";
            |MENAV "      Esta Cuenta NO tiene Pase directo";
            |ERROR;
        |FINSI;
    |FINSI;
    |CIERRA #15;
|FPRC;

|| *********************************************************************
|PROCESO ElCanempre;
  nSwExis = 1;
  nPerCon = #30#3;

  |DFICB aAlfa1;
  aAlfa2 = #30#0; |QBF aAlfa2;
  aAlfa2 = ("000000" + #30#0) % -106;
  eaDirOrigen = aAlfa1 + aAlfa2 + "/";

  ddig3 = #30#13;
  ddig4 = #30#14;       || 1 nivel
  ddig5 = #30#15;       || 2 nivel
  ddig6 = #30#16;       || 3 nivel
  ddig7 = #30#17;       || 4 nivel
  ddig8 = #30#18;       || 5 nivel
  ddig9 = #30#19;       || 6 nivel
  || Recoger informacion sobre los niveles ...
  Ipunt1 = ddig3<-;
  Ipunt1 = Ipunt1 + ddig3;
  nMaxOri = punt1;

  |ERROR10;
|FPRC;

|DEFBUCLE ElCanempre;
 #30#1;
 26;
 #2#1, 0, #0#4;
 #2#1, 9, #0#4;
 ;
 NULL, ElCanempre;
|FIN;
|| ////////////////////////////////////////////////////////////////////////

|PROCESO MiroExcluir;
 |SI eaCuenta ] #4#3; |Y eaCuenta [ #4#4;
     nSwNO = 1;
     |ERROR10;
 |FINSI;
|FPRC;

|DEFBUCLE MiroExcluir;
 #4#1;
 ;
 #2#0, #2#1, 001;
 #2#0, #2#1, 999;
 ;
 NULL, MiroExcluir;
|FIN;

|PROCESO LeeM005;

 eaCuenta = #5#2;
 enNivel  = #5#3;
 |SI #2#4 = "S";
     nSwNO = 0;
     |BUCLE MiroExcluir;
     |SI nSwNO = 1; |ACABA; |FINSI;
 |FINSI;

 |ABRE #15;
 #15#0 = #5#2;
 #15#1 = #5#3;
 |LEE 001#15=;
 |SI FSalida = 0;
     |HAZ SumaSaldo;

     #998#0 = #5#5;
     #998#1 = 0;
     |LEE 001#998=;
     |SI FSalida ! 0;
         |DDEFECTO #998;
         #998#0 = #5#5;
         #998#1 = 0;
         |GRABA 020#998c;
     |FINSI;
     #998#4 = (#998#4 + nImporte) ! EURODB;
     |GRABA 020#998a;
     nSwAlgun = 1;
 |FINSI;
 |CIERRA #15;
|FPRC;

|DEFBUCLE LeeM005;
 #5#1;
 ;
 #2#0, #2#1, "            ", 0;
 #2#0, #2#1, "zzzzzzzzzzzz", 9;
 ;
 NULL, LeeM005;
|FIN;

|PROCESO SumaSaldo;
 nImporte = 0;
 nNume1 = 14; |SI #2#3 = "S"; nNume1 = 11; |FINSI;

 |PARA nPara1 = nNume1; |SI nPara1 [ 47; |HACIENDO nPara1 = nPara1 + 3;
       nImporte = nImporte + #15nPara1;
 |SIGUE;
|FPRC;

|PROCESO LeePlan;

 eaCuenta = #15#0;
 enNivel  = #15#1;
 |SI #2#4 = "S";
     nSwNO = 0;
     |BUCLE MiroExcluir;
     |SI nSwNO = 1; |ACABA; |FINSI;
 |FINSI;

 |HAZ SumaSaldo;

 #999#0 = #15#0;
 #999#1 = 0;
 |LEE 001#999=;
 |SI FSalida ! 0;
     |DDEFECTO #999;
     #999#0 = #15#0;
     #999#1 = 0;
     |GRABA 020#999c;
 |FINSI;
 #999#4 = (#999#4 + nImporte) ! EURODB;
 |GRABA 020#999a;
|FPRC;

|DEFBUCLE LeePlan;
 #15#1;
 3;
 "            ", 0, "S";
 "zzzzzzzzzzzz", 9, "S";
 ;
 NULL, LeePlan;
|FIN;

|PROCESO LeeEmpresas;

  nSwExis = 0;
  |BUCLE ElCanempre;
  |SI nSwExis = 0; |ACABA; |FINSI;

  |SI nMaxDes ! nMaxOri;
      || |MENAV "    No corresponde Nivel Cuentas ";
      |ACABA;
  |FINSI;

  |PATH_DAT #15 eaDirOrigen;

  |SI #1#12 = "G";
      |ABRE #999;
      |BUCLE LeePlan;
      |CIERRA #999;
  |SINO;
      |ABRE #998;
      |BUCLE LeeM005;
      |CIERRA #998;
  |FINSI;
|FPRC;

|DEFBUCLE LeeEmpresas;
 #2#1;
 ;
 #0#5, 00001;
 #0#5, 99999;
 ;
 NULL, LeeEmpresas;
|FIN;

||////////////////////////////////////////////////////////
|PROCESO LeeX999;
 nImporte = nImporte + #999#4;
|FPRC;

|DEFBUCLE LeeX999;
 #999#1;
 ;
 #3#3, 0;
 #3#4, 0;
 ;
 NULL, LeeX999;
|FIN;

|PROCESO LeeM003;
 #998#0 = #3#1;
 #998#1 = 0;
 |LEE 001#998=;
 |SI FSalida ! 0;
     |DDEFECTO #998;
     #998#0 = #3#1;
     #998#1 = 0;
     |GRABA 020#998c;
 |FINSI;

 nImporte = 0;
 |BUCLE LeeX999;
 |SI nImporte ! 0;
     #998#4 = #998#4 + nImporte;
     |GRABA 020#998a;
     nSwAlgun = 1;
 |FINSI;
|FPRC;

|DEFBUCLE LeeM003;
 #3#1;
 ;
 #0#5, "            ", 001;
 #0#5, "zzzzzzzzzzzz", 999;
 ;
 NULL, LeeM003;
|FIN;

||////////////////////////////////////////////////////////
|PROCESO AltaApunte;
     |DDEFECTO #31;
     #31#0 = #32#0;
     #31#1 = #32#1;
     #31#2 = #32#2;
     #31#3 = nLinea;
     |HAZ Sacanivel;
     #31#4 = eaCuenta;
     #31#5 = enNivel;

     #31#6 = #0#12;
     enNumCon = #31#6; |HAZ VerConcepto; #31#24 = eaDesCon;
     #31#7 = #0#13;

     #31#8 = "D";
     |SI nImporte < 0;
         nImporte = - nImporte;
         #31#8 = "H";
     |FINSI;
     #31#9 = nImporte;
     |HAZ adapta;
     |GRABA 020#31n;
     |SI FSalida = 0;
         |HAZ Actualizar;
         nLinea = nLinea + 3;
     |FINSI;
|FPRC;

|PROCESO LeeX998;

 nImporte = #998#4;

 |SI nImporte ! 0;
     |SI sw = 0;
         sw = 1;
         |HAZ Cabecera;
     |FINSI;

     eaCuenta = #998#0;
     |HAZ AltaApunte;
 |FINSI;
|FPRC;

|DEFBUCLE LeeX998;
 #998#1002;
 ;
 "            ", 0;
 "zzzzzzzzzzzz", 0;
 ;
 NULL, LeeX998;
|FIN;

|PROCESO CuadreAsto;
 |SI #0#14 > "            ";
     nImporte = - #32#6;

     |SI nImporte ! 0;
         eaCuenta = #0#14;
         |HAZ AltaApunte;
     |FINSI;
 |FINSI;
|FPRC;


|PROCESO CrearAsiento;
  || .................. Abre fichero Auxiliar de Cuentas
    |ABRE #9;
    |LEE 101#9u;
    |SI FSalida ! 0;
        |DDEFECTO #9;
        #9#0 = 1;
    |SINO;
        #9#0 = #9#0 + 1;
    |FINSI;
    |GRABA 101#9n;
    codcon = #9#0;
    |LIBERA #9;

    sw   = 0;
    |ABRE #31;
    |ABRE #32;
    |BUCLE LeeX998;
    |SI sw = 1;
        |HAZ CuadreAsto;
    |FINSI;
    |CIERRA #32;
    |CIERRA #31;

    |CIERRA #9;

    |SI sw = 1;
        |ABRE #1;
        #1#0 = #0#5;
        |LEE 001#1=;
        nEstado = FSalida;
        #1#0 = #0#5;
        #1#1 = #0#6;
        aAlfa1 = #0#7; aAlfa1 = ("00" + aAlfa1) % -102;
        #1#2 = aAlfa1;
        #1#3 = #0#8;
        aAlfa1 = #0#9; aAlfa1 = ("000000" + aAlfa1) % -106;
        #1#4 = aAlfa1;
        #1#5 = #0#10;
        #1#6 = #0#11;
        #1#7 = #0#12;
        #1#8 = #0#13;
        #1#9 = #0#14;
        #1#10 = #0#15;
        #1#11 = #0#16;
        |SI nEstado = 0; |GRABA 020#1a; |FINSI;
        |SI nEstado ! 0; |GRABA 020#1n; |FINSI;
        |LIBERA #1;
        |CIERRA #1;
    |FINSI;
|FPRC;
||////////////////////////////////////////////////////////

|PROCESO LaConsol; |TIPO 3;

  |SI #1#3 > "01.01.0000";
      swVarios = 0;
      enDDiario  = #1#2;         || diario
      enHDiario  = #1#2;
      efDFechas  = #1#3;
      efHFechas  = #1#3;
      enDDocume  = #1#4;
      enHDocume  = #1#4;
      enBorraAu  = 2;
      |SUB_EJECUTA_NP "caborasi;NOMEQUITESQUETECORTOLOSGUEVOS";
      #1#2 = "";
      #1#3 = "01.01.0000";
      #1#4 = "";
  |FINSI;

 |DBASE aBase;
 aDef = aBase + "def/catracie.mas";
 |CARGA_DEF aDef, catraci@;
 |SI FSalida ! 0;
     |MENAV "    Error en Ficheros";
     |ACABA;
 |FINSI;

 aFichero = "1z" + Usuario;
 |NOME_DAT #999 aFichero;
 |DELINDEX #999;

 aFichero = "2z" + Usuario;
 |NOME_DAT #998 aFichero;
 |DELINDEX #998;

 |BUCLE LeeEmpresas;  || Crea el Fichero Auxiliar

 |PATH_DAT #15 eaDirDestino;
 |SI #1#12 = "G";
     nSwAlgun = 0;
     |ABRE #998;
     |BUCLE LeeM003;
     |CIERRA #998;
 |FINSI;

 |SI nSwAlgun ! 0;
     |HAZ CrearAsiento;
 |FINSI;

 |DESCARGA_DEF #catraci@;
 |SI modcon = 0;
     |ABRE #9;
     |DDEFECTO #9;
     #9#0 = codcon;
     |LEE 041#9=;
     |SI FSalida = 0;
         |BORRA 020#9a;
     |FINSI;
     |CIERRA #9;
 |SINO;
     |SUB_EJECUTA "camaapua";
 |FINSI;
|FPRC;

|PROGRAMA;
 |HAZ inicio;

 |DDEFECTO #0;
 |SI enDef = 0;
     Ipunt1      = dig3<-;
     Ipunt1      = Ipunt1 + dig3;
     nMaxDes     = punt1;
     enAny       = #30#26;
     eaNombreEmp = #30#1;
     |SI enAny < 80;
         enEjercicio = 2000 + enAny;
    |SINO;
         enEjercicio = 1900 + enAny;
    |FINSI;
 |FINSI;

 |DFICE eaDirDestino;
 #0#0  = enEmpresa;
 #0#1  = enPeriodo;
 #0#2  = eaNombreEmp;
 #0#3  = enEjercicio;
 #0#4  = enAny;
 #0#11 = Usuario % 820;

 |CLS;
 |CABEZA "Consolidar Empresas";
 |SI enDef ! 0;
     #0#5 = enDef;
     |HAZ LeeDefinicion;
     |SI nSwError = 1;
         |MENAV "    Error. No existe el codigo de la Definicion";
         |ACABA;
     |FINSI;
     |C_M #0#5,1,"N";
 |FINSI;
 |PINPA #0#0;
 |PINDA #0#0;
 |ENDATOS #1#0;
|FPRO;


|| ********************************************************************
|PROCESO Cabecera;
  |DDEFECTO #32;
  #32#0 = #0#7;
  #32#1 = #0#8;
  #32#2 = #0#9;
  nLinea = -2;
  |GRABA 020#32n;
  |SI FSalida ! 0;
      #31#0 = #32#0;
      #31#1 = #32#1;
      #31#2 = #32#2;
      #31#3 = 9999;
      |LEE 040#31];
      |SI FSalida = 0;
          |LEE 040#31a;
          |SI #31#0 = #32#0; |Y #31#1 = #32#1; |Y #31#2 = #32#2;
              nLinea = #31#3;
          |FINSI;
      |SINO;
          |LEE 040#31u;
          |SI FSalida = 0; |Y #31#0 = #32#0; |Y #31#1 = #32#1; |Y #31#2 = #32#2;
               nLinea = #31#3;
          |FINSI;
      |FINSI;
  |FINSI;
  nLinea = nLinea + 3;

|FPRC;

|PROCESO adapta;
|SI #31#8 = "D";
        #31#16 = #31#4;
        #31#17 = #31#5;
        #31#10 = "";
        #31#11 = 0;
|SINO;
        #31#16 = "";
        #31#17 = 0;
        #31#10 = #31#4;
        #31#11 = #31#5;
|FINSI;
|FPRC;

|PROCESO Actualizar;

  #32#0 = #0#7;
  #32#1 = #0#8;
  #32#2 = #0#9;
  |LEE 101#32=;
  |SI #31#8 = "D";
      #32#4 = #32#4 + #31#9;
      |SI #32#09 = 0; #32#08 = #31#4; #32#09 = #31#5; |FINSI;
  |SINO;
       #32#5 = #32#5 + #31#9;
       |SI #32#11 = 0; #32#10 = #31#4; #32#11 = #31#5; |FINSI;
  |FINSI;
  #32#6 = #32#4 - #32#5;
  |GRABA 020#32a;
  |LIBERA #32;

  |HAZ a_diario;
  |HAZ a_cuenta;
  |HAZ gratra;
|FPRC;

|PROCESO a_diario;
  |ABRE #10;
  #10#0 = #31#0;
  |LEE 101#10=;
  |SI FSalida ! 0;              || actualiza el diario
     |DDEFECTO #10;
     #10#0 = #31#0;
     #10#1 = "<DIARIO SIN DEFINIR>";
     |GRABA 020#10b;
  |FINSI;
  |SI #31#8 = "D";  #10#2 = #10#2 + #31#9;  |FINSI;
  |SI #31#8 = "H";  #10#3 = #10#3 + #31#9;  |FINSI;
  |GRABA 020#10a;
  |LIBERA #10;
  |CIERRA #10;
|FPRC;

|PROCESO a_actualiza;                || rutina 'a_cuentas'
  ini    = dig1;
  fecalf = #31#1;
  mes    = fecalf % 402;
  mesfec = mes;
  |SI ini = 1;
       mesac = mesfec;
  |SINO;
       mesac = (mesfec - ini) + 1;
       |SI mesac [ 0;
           mesac = (13 - ini) + mesfec;
       |FINSI;
  |FINSI;
  a = (mesac * 3) + 9;
  b = a + 1;
  c = a + 2;
  |SI #31#8 = "D";
      #15a   = #15a + #31#9;
      #15#51 = #15#51 + #31#9;
  |SINO;
      #15b   = #15b + #31#9;
      #15#52 = #15#52 + #31#9;
  |FINSI;
  #15c   = #15a - #15b;
  #15#53 = #15#51 - #15#52;
|FPRC;

|PROCESO a_cuenta;
  #15#0 = #31#4;
  #15#1 = #31#5;
  |LEE 101#15=;
  |SI FSalida = 0;
     |SI #31#0 = 0;
         |SI #31#8 = "D";
             #15#9  = #15#9  + #31#9;        || diario cero (saldos iniciales)
             #15#51 = #15#51 + #31#9;
         |SINO;
             #15#10 = #15#10 + #31#9;
             #15#52 = #15#52 + #31#9;
         |FINSI;
         #15#11 = #15#9 - #15#10;
         #15#53 = #15#51 - #15#52;
     |SINO;
         |SI #31#0 = 99;
             |SI #31#8 = "D";
                 #15#48 = #15#48 + #31#9;       || diario 99 (saldos finales)
                 #15#51 = #15#51 + #31#9;
             |SINO;
                 #15#49 = #15#49 + #31#9;
                 #15#52 = #15#52 + #31#9;
             |FINSI;
             #15#50 = #15#48 - #15#49;
             #15#53 = #15#51 - #15#52;
         |SINO;
             |HAZ a_actualiza;           || saldos del mes correspondiente
         |FINSI;
     |FINSI;
     |GRABA 020#15a;
     |LIBERA #15;
 |FINSI;
|FPRC;

|PROCESO gratra;
    modcon = 1;               || graba el temporal para la actualiz. de ctas.
    #9#0 = codcon;
    #9#1 = #31#4;
    #9#2 = #31#5;
    #9#3 = #0#8;
    #9#4 = #31#8;
    #9#6 = #0#7;
    |LIBERA #9;
    |LEE 101#9=;
    |SI FSalida ! 0;
        |DDEFECTO #9;
        #9#0 = codcon;
        #9#1 = #31#4;
        #9#2 = #31#5;
        #9#3 = #0#8;
        #9#4 = #31#8;
        #9#6 = #0#7;
        |GRABA 101#9b;
    |FINSI;
    #9#5 = #9#5 + #31#9;
    |GRABA 101#9a;
|FPRC;

|PROCESO Sacanivel;
   aLong = eaCuenta; |QBF aLong;
   aLong = aLong % 0;
   nLong = aLong;
   enNivel = 0;
   |SI nLong = dig4; |Y dig4 ! 0; enNivel = 1; |FINSI;
   |SI nLong = dig5; |Y dig5 ! 0; enNivel = 2; |FINSI;
   |SI nLong = dig6; |Y dig6 ! 0; enNivel = 3; |FINSI;
   |SI nLong = dig7; |Y dig7 ! 0; enNivel = 4; |FINSI;
   |SI nLong = dig8; |Y dig8 ! 0; enNivel = 5; |FINSI;
   |SI nLong = dig9; |Y dig9 ! 0; enNivel = 6; |FINSI;
|FPRC;
