|FICHEROS;
    cactetal #00;   ||  el #5 y el #8 estan reservados para los 'INCLUYE'
    camaasic #02;   || /para 'cacuenta' y 'catradia' respect.
    camaasil #03;
    camantal #06;
    cacuadre #09;   || control cobros y pagos
    camandia #10;
    cacuebal #11;
    ca8m0002 #422;
|FIN;

|VARIABLES;
    &enDiario   = 0;
    &enContador = 0;
    &entrada = 1;
    alfa1 = "";
    alfa2 = "";
    alfa3 = "";
    alfa4 = "";
    nume1 = 0;
    nume2 = 0;
    nume3 = 0;
    nume4 = 0;
    fech1 = @;
    fech2 = @;
    fech3 = @;
    fech4 = @;
    arteris = 0;
    muestra = "";
    digito = 0;
    longi = 0;
    asie = 0;
    apun = 0;
    cuen = "";
    digi = 0;
    sign = "";
    ptas = 0;
    desc = "";
    descrip = "";
    impo = 0;
    sw = 0;
    FEstado = 0;
    &enEjerBal = 0;
|FIN;


|INCLUYE varnue_i;
|INCLUYE inicio_i;
|INCLUYE actcta_i;
|INCLUYE defec1_i;

|| *************************************************************************
|PROCESO contab2;    || graba apunte
    #0#08 = #6#0;   |PINTA #0#08;
    #0#09 = asie;   |PINTA #0#09;
    #0#10 = apun;   |PINTA #0#10;
  |DDEFECTO #3;
    #3#00 = #0#4;   || diario
    #3#01 = #0#7;   || fecha
    #3#02 = asie;   || asiento
    #3#03 = apun;   || apunte
    #3#04 = cuen;   || cuenta
    #3#05 = digi;   || digito
    #3#06 = #0#5;   || concepto
    #3#07 = desc;   || descripcion
    #3#08 = sign;   || signo
    #3#09 = ptas;   || importe
|SI #3#8 = "D";
        #3#16 = #3#4;
        #3#17 = #3#5;
        #3#10 = "";
        #3#11 = 0;
|SINO;
        #3#16 = "";
        #3#17 = 0;
        #3#10 = #3#4;
        #3#11 = #3#5;
|FINSI;
|GRABA 020#3n;
|HAZ cabecera;           || cabecera apunte
    ressum = #3#9;
|HAZ actualiz1;          || cuenta
|HAZ actualiz3;          || diario
    opera = "A";
|HAZ altatra;            || subniveles
|FPRC;

|| ................... Controla la grabacion de Apuntes
|PROCESO contab1;    || Con "*"
  |HAZ descripcion;
  |SI apun = 0;
      enDiario = #0#4;
      |HAZ contador;
      asie = enContador;
      apun = - 9;
  |FINSI;
  apun = apun + 10;
  cuen = #6#1;
  digi = #6#2;
  sign = "D";
  ptas = #6#9;
  desc = descrip;
  |HAZ contab2;
  apun = apun + 10;
  |SI #6#13 = "S";
      |HAZ filtro;
      cuen = alfa2;
      digi = #6#2;
  |SINO;
      cuen = #6#3;
      digi = #6#4;
      #6#8 = "S";
  |FINSI;
  sign = "H";
  ptas = #6#9;
  desc = descrip;
  |HAZ cuenta;
  |HAZ contab2;
  || ............. Fin Asiento
  apun = 0;
  #6#7 = "S";
  |GRABA 020#6a;
  |LIBERA #6;
|FPRC;

|DEFBUCLE cactetal0;
 #6#1;
 5,12,7;
 #0#0, #0#2, "S", "N"; || fecha emision, impresas Pagare SI, c. emision. NO
 #0#1, #0#3, "S", "N";
 be;
 NULL, contab1;
 #6#1, #6#2, #6#0;
|FIN;
|| *************************************************************************

|PROCESO contab11;    || sin "*" 1§ los que van todos juntos

  |HAZ descripcion;
  |SI apun = 0;
      enDiario = #0#4;
      |HAZ contador;
      asie = enContador;
      apun = - 9;
  |FINSI;
  apun = apun + 10;
  cuen = #6#1;
  digi = #6#2;
  sign = "D";
  ptas = #6#9;
  desc = descrip;
  |HAZ contab2;
  impo = impo + #6#9;

  #6#7 = "S";
  |GRABA 020#6a;
  |LIBERA #6;
|FPRC;

|DEFBUCLE cactetal01;
 #6#1;
 5,12,7,13;
 #0#0, #0#2, "S", "N", "S"; || fecha emision, impresas Pagare SI, c. emision. NO
 #0#1, #0#3, "S", "N", "S"; || Contabilizando a Efectos a Pagar.
 be;
 NULL, contab11;
 #6#1, #6#2, #6#0;
|FIN;
|| ........................................................................

|PROCESO contab12;   || controla la grabacion de apuntes a Banco
  |HAZ descripcion;
  |SI apun = 0;
      enDiario = #0#4;
      |HAZ contador;
      asie = enContador;
      apun = -9;
  |FINSI;
  apun = apun + 10;
  cuen = #6#1;
  digi = #6#2;
  sign = "D";
  ptas = #6#9;
  desc = descrip;
  |HAZ contab2;
  apun = apun + 10;
  cuen = #6#3;
  digi = #6#4;
  sign = "H";
  ptas = #6#9;
  desc = descrip;
  |HAZ cuenta;
  |HAZ contab2;
  apun = 0;
  #6#7 = "S";
  #6#8 = "S";
  |GRABA 020#6a;
  |LIBERA #6;
|FPRC;

|DEFBUCLE cactetal02;
 #6#1;
 5,12,7,13;
 #0#0, #0#2, "S", "N", "N";   || fecha emision, impresas Pagare SI, c. emision. NO
 #0#1, #0#3, "S", "N", "N";   || no contabiliza a Efectos a Pagar.
 be;
 NULL, contab12;
 #6#1, #6#2, #6#0;
|FIN;

|PROCESO proceso; |TIPO 3;  || calculo padre
     |SI #30#26 < 80;
         enEjercicio = 2000 + #30#26;
     |SINO;
         enEjercicio = 1900 + #30#26;
     |FINSI;


|ABRE #2;
|ABRE #3;
|ABRE #9;
|LEE 001#9p;
|SI FSalida = 0;
    apun = 0;
    |HAZ arteris;
    |SI arteris = 1;   || Con "*";
        |BUCLE cactetal0;
    |SINO;
        |SI arteris = 0;  || Cuenta Completa
            |BUCLE cactetal01;
            |SI impo ! 0;
                |HAZ final;  || contrapartida englobada
            |FINSI;
            |BUCLE cactetal02;
        |FINSI;
    |FINSI;
|SINO;
    |MENAV "    ATENCION!!! Control de Pagares y Talones sin definir ...";
|FINSI;
|HAZ subniveles;
|CIERRA #2;
|CIERRA #3;
|FPRC;

|PROCESO arteris;     || desglosa la contrapartida del control
    alfa1 = #9#23;
|QBF alfa1;
    alfa2 = alfa1 % 0;
    longi = alfa2;            || 'longi' sirve despues (no tocar)
    nume3 = (longi * 100) + 1;
    alfa2 = alfa1 % nume3;
|SI alfa2 = "*";
        nume3 = 99 + longi;
        muestra = alfa1 % nume3;
        arteris = 1;
|SINO;
    |HAZ digito;
    |SI sw = 0;   arteris = 2;  |FINSI; || contrapartida fuera de nivel!!!
    |SI sw ! 0;   arteris = 0;  |FINSI;
|FINSI;
|FPRC;

|PROCESO final;     || graba apunte cuando la contrapartida es unica
|SI arteris = 0;
        apun = apun + 10;
        cuen = #9#23;
        digi = digito;
        sign = "H";
        ptas = impo;
        desc = #0#6;
    |QBF desc;
        desc = desc + " " + #0#7;
    |HAZ cuenta;
    |HAZ contab2;
    apun = 0;
|FINSI;
|FPRC;

|PROCESO descripcion;
    descrip = #0#6;
|QBF descrip;
    alfa1 = #6#0;  alfa1 = "000000" + alfa1;  alfa1 = alfa1 % -106;
    descrip = descrip + " " + alfa1;
|FPRC;

|PROCESO subniveles;     || actualiza subniveles de cuentas
|SI modcon = 0;
    |ABRE #8;
    |DDEFECTO #8;
        #8#0 = codcon;
    |LEE 001#8=;
    |SI FSalida = 0;
        |BORRA 020#8a;
    |FINSI;
    |CIERRA #8;
|SINO;
    |SUB_EJECUTA "camaapua.run";
|FINSI;
|FPRC;

|PROCESO cabecera;       || actualiza cabeceras
|SI #3#09 ! 0;
    |DDEFECTO #2;
        #2#00 = #3#0;    || diario
        #2#01 = #3#1;    || fecha
        #2#02 = #3#2;    || documento
        #2#03 = #3#2;    || asiento
    |LEE 101#2=;
        FEstado = FSalida;
    |SI #3#08 = "D";
            #2#04 = #2#04 + #3#09;
        |SI #2#09 = 0; #2#08 = #3#4; #2#09 = #3#5; |FINSI;  || contrp.HABER
    |FINSI;
    |SI #3#08 = "H";
            #2#05 = #2#05 + #3#09;
        |SI #2#11 = 0; #2#10 = #3#4; #2#11 = #3#5; |FINSI;  || contrp.DEBE
    |FINSI;
        #2#6 = #2#4 - #2#5;                            || saldo
    |SI FEstado = 0;  |GRABA 020#2a;  |FINSI;
    |SI FEstado ! 0;  |GRABA 020#2n;  |FINSI;
    |LIBERA #2;
|FINSI;
|FPRC;

|PROCESO actualiz3;
|ABRE #10;
    #10#0 = #3#0;
|LEE 121#10=;
|SI FSalida = 0;
    |SI #3#8 = "D";  #10#2 = #10#2 + #3#9;  |FINSI;
    |SI #3#8 = "H";  #10#3 = #10#3 + #3#9;  |FINSI;
    |GRABA 020#10a;
|FINSI;
|LIBERA #10;
|CIERRA #10;
|FPRC;

|PROCESO digito;         || saca el digito de la contrapartida del control
    sw   = 0;
    digito = 0;  || inicializa el digito
|SI longi = dig4; |Y dig4 ! 0; sw = 1; digito = 1; |FINSI;
|SI longi = dig5; |Y dig5 ! 0; sw = 1; digito = 2; |FINSI;
|SI longi = dig6; |Y dig6 ! 0; sw = 1; digito = 3; |FINSI;
|SI longi = dig7; |Y dig7 ! 0; sw = 1; digito = 4; |FINSI;
|SI longi = dig8; |Y dig8 ! 0; sw = 1; digito = 5; |FINSI;
|SI longi = dig9; |Y dig9 ! 0; sw = 1; digito = 6; |FINSI;
|SI sw = 0;
    |MENAV "    Contrapartida de Talon Fuera de Nivel!!! Proceso Abortado ...";
|FINSI;
|FPRC;

|PROCESO filtro;         || construye la contrapartida individual
|SI #6#2 = 1;  nume1 = dig4;  |FINSI;
|SI #6#2 = 2;  nume1 = dig5;  |FINSI;
|SI #6#2 = 3;  nume1 = dig6;  |FINSI;
|SI #6#2 = 4;  nume1 = dig7;  |FINSI;
|SI #6#2 = 5;  nume1 = dig8;  |FINSI;
|SI #6#2 = 6;  nume1 = dig9;  |FINSI;
    nume2 = nume1 - (longi - 1);
    nume2 = (100 * longi) + nume2;
    alfa1 = #6#1 % nume2;
    alfa2 = muestra + alfa1;
|FPRC;

|PROCESO cuenta;    || comprueba si la cuenta esta dada de alta
|ABRE #5;
    #5#0 = cuen;
    #5#1 = digi;
|LEE 001#5=;
|SI FSalida ! 0;
    |DDEFECTO #5;
        #5#0 = cuen;
        #5#1 = digi;
    |HAZ defect1;
    |PUSHV 0501 2380;
    |PINPA #0#5;
    |PINDA #0#5;
    |ENDATOS #1#5;
    |SI FSalida = 0;
        |GRABA 020#5n;
    |FINSI;
    |POPV;
|FINSI;
|CIERRA #5;
|FPRC;

**************************************************************************
          CALCULOS DESDE CAMPOS
**************************************************************************

|PROCESO limite; |TIPO 0;
|SI #0#7 < fec1;|O #0#7 > fec2;
    |MENAV "    ATENCION!!! La fecha no corresponde al ejercicio contable ...";
    |ERROR;
|FINSI;
|FPRC;

|PROCESO final816; |TIPO 7;
 |PTEC 816;
|FPRC;
